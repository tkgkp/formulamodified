:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_tec_ktkw.fml
:: Utworzony: 11.05.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_TEC_KTKW - Kalkulacja karty technologicznej
::            Obsługa tabel KKTL, KPOZK, KALMAT, KALTLS, KALTOU, KALTTO, KALWRK
::            Parametryzacja kalkulacji
::            Porównywanie kalkulacji, what-if, drill-down
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności kalkulowania karty technologicznej (TTE_TEC_KTKW)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=TKTL, type=_TKTL, name=Wskazanie na kartę technologiczną, required=T, keyref=T
{? var_pres('TKTL',_in)<>type_of(~~) & var_pres('TKTL',_in)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_in)=type_of(~~) || return() ?};

::# kind=WE, symbol=AKT, type=STRING, name=Czy aktywować kalkulację?, required=N, keyref=N,,fml_val="exec('edit_boolean','#edit',,'Czy aktywować kalkulację?')"
{? var_pres('AKT',_in)<>type_of(~~) & var_pres('AKT',_in)<>type_of('') || return() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=RESULT, type=STRING, name="Wynik czynności (OK, gdy wszystkie kalkulacje zostały poprawnie przeliczone)", required=N
{? var_pres('RESULT',_out)<>type_of(~~) & var_pres('RESULT',_out)<>type_of('') || return() ?};

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE

_can_continue:=1;
_clean_result:=params_exec('clean','!tte_tec_ktkw',_mp,_in);
_can_continue:=_clean_result.RESULT;
_tktl:=_clean_result.TKTL;

{? _can_continue>0 & _tktl<>null()
||
   {? exec('FindAndGet','#table',TKTL,_tktl,,"TORW",'')<>'T'
   || FUN.emsg('Niezgodność wywołania czynności.\nNie można wykonać kalkulacji dla wzorca technologii.'@);
      _mp.error('Niezgodność wywołania czynności. Nie można wykonać kalkulacji dla wzorca technologii.')

   || exec('tktl_cntx_psh','tech_common');
      exec('tktl_use','tech_common',ref_name(_tktl)+3);
      TKTL.clear();

      {? TKTL.seek(_tktl)
      ||
::       Czynność uruchomiona jako usługa - kalkulowanie wszystkich produktów karty.
::       Jeżeli w którejkolwiek kalkulacji wystąpi błąd, to RESULT:='BŁĄD' a czynność się kończy
         {? _mp.isService()
         || _args:=__PARSES.args('OkresRok');
            _args.OBSZAR:='LMG';
            _args.AR:=date()~1;
            _args.AM:=date()~2;
            __PARSES.setVal('OkresRok',_args);
            KOMM.init(250,,'Wyniki kalkulacji dla karty technologicznej'@,'',,,0);
            _ok:=exec('kalk_auto','!tte_tec_ktkw',TKTL.ref(),1,_in.AKT);
            _out.RESULT:={? _ok || 'OK' || 'BŁĄD' ?};
            _mp.save(,_out);
            _mp.done()

::       Czynność uruchomiona automatycznie lub dla grupy rekordów - kalkulowanie wszystkich produktów karty.
::       Jeżeli w którejkolwiek kalkulacji wystąpi błąd, to czynność pozostaje na ToDo, wpp. zakończenie
         |? _mp.isAutoRun() | _mp.akcja()='GRUPA'
         || {? _mp.isAutoRun() || KOMM.init(250,,'Wyniki kalkulacji dla karty technologicznej'@,'',,,0) ?};
            _ok:=exec('kalk_auto','!tte_tec_ktkw',TKTL.ref(),_mp.isAutoRun(),_in.AKT);
            {? _ok=0 & _mp.isAutoRun() || KOMM.select() ?};
            _out.RESULT:={? _ok || 'OK' || 'BŁĄD' ?};
            _mp.save(,_out);
            {? _mp.isMicro() || _mp.cancel() |? _ok || _mp.done() ?}

::       Zakończenie czynności - oświadczenie operatora (nie musi być zarejestrowana żadna kalkulacja)
         |? _mp.akcja()='ZAKOŃCZ'
         || {? FUN.ask('Czy zakończyć kalkulowanie karty?'@)
            || _out.RESULT:='OK';
               KKTL.actions_grayed('WER','Z:Z');
               _mp.save(,_out);
               _mp.done();
               sel_exit()
            ?}

::       Wywołanie z menu obszaru roboczego TTE_TEC (lub inne miejsca selekcji dla TKTL)
         |? _mp.akcja()='MENU'
         || _mp.keep();
            exec('menu_start','tech_head');
            exec('kalk_poj','!tte_tec_ktkw',TKTL.ref(),null(),1,{? _mp.isMicro() || 0 || 1 ?});
            exec('menu_stop','tech_head');
            {? _mp.isMicro() || _mp.cancel() ?}

::       Wywołanie z menu obszaru roboczego TTE_TEC (lub inne miejsca selekcji dla TKTLW)
         |? _mp.akcja()='MENU_PROD'
         || _mp.keep();
            exec('menu_start','tech_head');
            exec('kalk_poj','!tte_tec_ktkw',TKTL.ref(),_context.TKTLW,1,{? _mp.isMicro() || 0 || 1 ?});
            exec('menu_stop','tech_head');
            {? _mp.isMicro() || _mp.cancel() ?}

::       Wywołanie z listy ToDo
         |? _mp.pathTodo()
         || {? _mp.isMicro()
            || _mp.cancel()
            || _mp.keep();
               exec('menu_start','tech_head');
               exec('kalk_poj','!tte_tec_ktkw',TKTL.ref(),null(),TKTL.STAT_N<>'N',1);
               exec('menu_stop','tech_head')
            ?}

         || FUN.info('Nie obsłużony kontekst wywołania: '+_mp.path()+' '+_mp.akcja());
            _mp.cancel()
         ?}
      ?};

      exec('tktl_cntx_pop','tech_common')
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności kalkulowania karty technologicznej (TTE_TEC_KTKW)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest rekord kluczowy to ustawiam odpowiedniego TKTL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Wykonaj kalkulację dla karty technologicznej %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"'Wykonaj kalkulację dla karty technologicznej %1 wer. %2'[NRK,WER]",'')

:: jest parametr wejściowy to ustawiam odpowiedniego TKTL
|? var_pres('TKTL',_in)
|| _tmp:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Wykonaj kalkulację dla karty technologicznej %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"'Wykonaj kalkulację dla karty technologicznej %1 wer. %2'[NRK,WER]",'')

?};
_desc


\action_menu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Kalkulacja karty technologicznej - właściwa akcja z menu okna wertowania TKTL
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.sel_size()=0
|| {? TKTL.STAT_N='N'
   || exec('menu_start','tech_head');
      exec('kalk_poj','!tte_tec_ktkw',TKTL.ref(),null(),-1,0);
      exec('menu_stop','tech_head')

   |? TKTL.ARCH='T' | ~exec('tktl_act','tech_head',0)
   || exec('menu_start','tech_head');
      exec('kalk_poj','!tte_tec_ktkw',TKTL.ref(),null(),0,0);
      exec('menu_stop','tech_head')

   |? exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_KTKW')
   || _args:=exec('mp_run_a','#b__box');
      _args.ACT_UID:='TTE_TEC_KTKW';
      _args.UIDREF:=TKTL.uidref();
      _args.AKCJA:='MENU';
      _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
      exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

      exec('mp_run','#b__box',_args)

   || exec('menu_start','tech_head');
      exec('kalk_poj','!tte_tec_ktkw',TKTL.ref(),null(),0,0);
      exec('menu_stop','tech_head')

   ?}
|| _karta:=exec('record','#to_string',TKTL.ref());
   {? TKTL.STAT_N='N'
   || KOMM.info('Karta %1 nie została przekalkulowana — nie zakończono redakcji nagłówka'@[_karta])

   |? TKTL.ARCH='T'
   || KOMM.info('Karta %1 nie została przekalkulowana — karta archiwalna'@[_karta])

   |? TKTL.TYP().DEF_OPCK=null()
   || KOMM.info('Karta %1 nie została przekalkulowana — brak domyślnego wariantu kalkulacji'@[_karta])

   || _args:=exec('mp_run_a','#b__box');
      _args.ACT_UID:='TTE_TEC_KTKW';
      _args.UIDREF:=TKTL.uidref();
      _args.AKCJA:='GRUPA';
      _args.GRUPA:='T';
      _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
      exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

      exec('mp_run','#b__box',_args)

   ?}
?};
~~


\action_menu_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Kalkulacja karty technologicznej - akcja 'po' z menu okna wertowania TKTL
::----------------------------------------------------------------------------------------------------------------------
~~


\action_menu_group_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Kalkulacja karty technologicznej - akcja 'przed grupą' z menu okna wertowania TKTL
::  OLD: \ask/pkalk.fml
::       \kalk_zb/pkalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_KTKW')
|| _res:=FUN.ask('Ilość kart do przekalkulowania: %1.\n'
                 'Operacja może być czasochłonna.\n\n'
                 'Czy kontynuować?'@[$TKTL.sel_size()]);
   {? _res
   || KOMM.init(250,,'Wyniki kalkulacji dla zaznaczonych kart'@,'',,,0)
   ?}
|| FUN.info('Brak uprawnień do wykonania kalkulacji dla zaznaczonych kart technologicznych.'@);
   _res:=0
?};
_res


\action_menu_group_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Kalkulacja karty technologicznej - akcja 'po grupie' z menu okna wertowania TKTL
::----------------------------------------------------------------------------------------------------------------------
KOMM.select();
~~


\action_menu_prod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Kalkulacja karty technologicznej - właściwa akcja z menu okna wertowania TKTLW
::----------------------------------------------------------------------------------------------------------------------
{? TKTLW.sel_size()=0
|| {? TKTLW.TKTL().STAT_N='N'
   || exec('menu_start','tech_head');
      exec('kalk_poj','!tte_tec_ktkw',TKTLW.TKTL,TKTLW.ref(),0,0);
      exec('menu_stop','tech_head')

   |? TKTLW.TKTL().ARCH='T'
   || exec('menu_start','tech_head');
      exec('kalk_poj','!tte_tec_ktkw',TKTLW.TKTL,TKTLW.ref(),0,0);
      exec('menu_stop','tech_head')

   |? exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_KTKW')
   || _args:=exec('mp_run_a','#b__box');
      _args.ACT_UID:='TTE_TEC_KTKW';
      _args.UIDREF:=exec('FindAndGet','#table',TKTL,TKTLW.TKTL,,"uidref()",'');
      _args.AKCJA:='MENU_PROD';
      _args.CONTEXT:=obj_new('TKTLW'); _args.CONTEXT.TKTLW:=TKTLW.ref();
      _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
      exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTLW.TKTL);

      exec('mp_run','#b__box',_args)

   || exec('menu_start','tech_head');
      exec('kalk_poj','!tte_tec_ktkw',TKTLW.TKTL,TKTLW.ref(),0,0);
      exec('menu_stop','tech_head')

   ?}

|| FUN.info('Akcja grupowa nie jest obsługiwana.'@)
?};
~~


\action_menu_prod_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Kalkulacja karty technologicznej - akcja 'po' z menu okna wertowania TKTLW
::----------------------------------------------------------------------------------------------------------------------
~~


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Kalkulacja karty technologicznej - akcja Zakończ
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='TTE_TEC_KTKW';
_args.UIDREF:=TKTL.uidref();
_args.AKCJA:='ZAKOŃCZ';
_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

exec('mp_run','#b__box',_args);
~~


::======================================================================================================================
:: Obsługa tabeli KKTL - kalkulacje kart technologicznych
::======================================================================================================================


\kalk_poj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60]
:: OPIS: Kalkulowanie wybranej karty / produktu karty
::   WE: [_a] - TKTL.ref()
::       [_b] - TKTLW.ref()
::       [_c] - tylko podgląd (domyślnie - 0), pełne redagowanie (1), tylko usuwanie (-1)
::       [_d] - uruchomione w procesie (1), poza procesem (domyślnie - 0)
::   WY: 1
::  OLD: \kalk_poj/pkalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null()) || _tktl:=_a || _tktl:=TKTL.ref() ?};
{? var_pres('_b')=type_of(null()) || _tktlw:=_b || _tktlw:=TKTLW.ref() ?};
{? var_pres('_c')=type_of(0) || _edit:=_c || _edit:=0 ?};
{? var_pres('_d')=type_of(0) || _proc:=_d || _proc:=0 ?};

TKTL.cntx_psh(); TKTLW.cntx_psh();
TKTL.clear();
{? TKTL.seek(_tktl)
|| _locked:=exec('tktl_lock','tech_common',#_tktl,'N');
   _edit:={? _locked>0 || _edit || 0 ?};

   VAR.A_KTL:=TKTL.ref();
   VAR.A_KKTL:=null();
   {? _tktlw<>null()
   || VAR.A_TKTLW:=_tktlw
   || TKTLW.index('REF');
      TKTLW.prefix(_tktl);
      {? TKTLW.size()>1
      || {? TKTLW.first()
         || VAR.A_TKTLW:=null();
            TKTLW.blank(1);
            {!
            |? {? VAR.A_KTL().KTM=TKTLW.KTM
               || VAR.A_TKTLW:=TKTLW.ref()
               ?};
               TKTLW.next()
            !}
         ?}
      |? TKTLW.first()
      || VAR.A_TKTLW:=TKTLW.ref()
      || VAR.A_TKTLW:=null();
         TKTLW.blank(1)
      ?}
   ?};
   KKTL.clear();
   KKTL.index('KNR');
   {? _tktlw<>null()
   || KKTL.prefix(VAR.A_KTL,VAR.A_TKTLW)
   || KKTL.prefix(VAR.A_KTL)
   ?};
   VAR.A_WAR:=exec('find_opc','!tte_tec_ktkw',VAR.A_KTL);
   KWAR.clear();
   KWAR.f_set('NAZ',,'"KWAR"."REFERENCE" in
                      (select TPKTLF.OPC from TPKTLF where TPKTLF.TYP=\':_a\')',
                      $VAR.A_KTL().TYP);
   {? KWAR.f_first() | TKTL.DEF_OPCK<>null()
   || KWAR.win_dict('SLO');
::    Zmienna _xkktl do przechowania ref-ów porównywanych kalkulacji
      _xkktl:=obj_new('kktl','pkktl','licznik');
      params_set('xkktl',_xkktl,'proc',_proc,'TKTLW',_tktlw);

      _grp:=KKTL.grp_make(
         {? _tktlw=null()
         || 'Kalkulacje karty technologicznej %1'@[exec('record','#to_string',_tktl)]
         || _ktm:=exec('FindAndGet','#table',TKTLW,_tktlw,,"KTM",null());
            'Kalkulacje produktu %1 wg karty %2'@[exec('record','#to_string',_ktm),exec('record','#to_string',_tktl)]
         ?},,'#kktl_grp'+$(_tktlw=null()),
         10,10,,,'normal'
      );
      _f_ar:="
         _proc:={? params_get().proc || '' || 'Z:Z' ?};
         {? KKTL.STAN='T'
         || KKTL.actions_grayed(cur_win(1,1),'UAR'+_proc)
         |? KKTL.STAN='B'
         || KKTL.actions_grayed(cur_win(1,1),'UA'+_proc)
         || KKTL.actions_grayed(cur_win(1,1),'C(P)'+_proc)
         ?};
         ~~
      ";
      KKTL.grp_sel(_grp,,'WER',,_f_ar,,,12,,,,,'maximized');
      KKTL.win_sel(_grp);
      KKTL.win_edit('RED');
      _kalk:=exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_KKAL') |
             exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_PKAL');
      _anal:=exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_KANA') |
             exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_PANA');
      {? _kalk=0 | _anal=0 || _hid:='W(E)' || _hid:='' ?};
      KKTL.actions('WER',{? _edit>0 || _hid+'' || _hid+'dAKW(A)RZ:dZ'+{? _edit=0 || 'U' || '' ?} ?});
      KKTL.actions_grayed('WER',{? _proc || '' || 'Z:Z' ?});
      KKTL.select();
      KWAR.f_clear()
   || FUN.emsg('Nie można wykonać kalkulacji — brak dostępnych wariantów.'@)
   ?};
   {? _locked || exec('tktl_unlock','tech_common',#_tktl,'N') ?}
?};
TKTL.cntx_pop(); TKTLW.cntx_pop();
1


\kalk_auto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Automatyczne przekalkulowanie produktów karty
::   WE: _a - TKTL.ref()
::       _b - czy inicjować KOMM
::       [_c] - STRING - czy aktywować kalkulację (T/~T)
::   WY: 1 - wszystkie kalkulacje się powiodły, 0 - co najmniej jednej kalkulacji nie udało się przeliczyć
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;
_komm:=_b;

_akt:='';
{? var_pres('_c')=type_of('')
|| _akt:=_c
?};

{? _komm || KOMM.init(250,,'Wyniki kalkulacji dla karty'@,'',,,0) ?};

_ok:=1;
TKTLW.cntx_psh();
TKTLW.index('REF');
TKTLW.prefix(_tktl);
{? TKTLW.first()
|| {!
   |? _ok*=exec('kalk4ktl','!tte_tec_ktkw',_tktl,0,TKTLW.ref(),0,_akt);
      TKTLW.next()
   !}
|| _ok*=exec('kalk4ktl','!tte_tec_ktkw',_tktl,0,null(),0,_akt)
?};
TKTLW.cntx_pop();

::{? _komm || KOMM.select() ?};
_ok


\bkktlknr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Blank pola KKTL.KNR (numerowanie w ramach TKTL)
::  OLD: \bkktlknr/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KKTL.cntx_psh();
KKTL.clear();
KKTL.index('NR');
KKTL.prefix(VAR.A_KTL);
_res:={? KKTL.last() || KKTL.KNR+1 || 1 ?};
KKTL.cntx_pop();
_res


\ae_kktl_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: Po redakcji pola KKTL.KNR
::  OLD: \ae_kktl_nr/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _res:=exec('itsPositive','#field',1); _res
|| _knr:=KKTL.KNR;
   KKTL.cntx_psh();
   KKTL.index('NR');
   KKTL.prefix(VAR.A_KTL);
   _ok:=1;
   {? KKTL.first()
   || {!
      |? {? KKTL.KNR=_knr || _ok:=0 ?};
         KKTL.next()
      !}
   ?};
   KKTL.cntx_pop();
   {? ~_ok || FUN.info('Numer kalkulacji nie może się powtórzyć.'@) ?};
   _ok
|| 0
?}


\bekktlopc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Przed redakcją pola KKTL.OPC
::   WY: 1 / 0
::  OLD: \bekktlopc/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? KWAR.f_size()>1 || 1 || 0 ?}


\kktl_dd_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Przed wyświetleniem pola KKTL.DT_D
::   WY: '' lub kod kolorowania
::  OLD: \kktl_dd_bd/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
''


\kktl_dd_fd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Format wyświetlania pola KKTL.DT_D
::----------------------------------------------------------------------------------------------------------------------
{? KKTL.DT_D=date(0,0,0) || 'empty=1' || 'empty=0' ?}


\kktl_dd_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Przed redakcją pola KKTL.DT_D
::   WY: 0 / 1
::  OLD: \kktl_dd_be/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
1


\kktl_dtd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.60]
:: OPIS: po redakcji pola KKTL.DT_D
::  OLD: \kktl_dtd/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()=date(0,0,0)
|| 1
|| exec('le_today','#field')
?}


\kktl_dw_blank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Wartość początkowa pola KKTL.DW
::  OLD: \kktl_dw_blank/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KKTL.DT+KKTL.OPC().DW


\KKTL_DW_BE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Przed redakcją pola KKTL.DW
::  OLD: \KKTL_DW_BE/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()=date(0,0,0) || fld(KKTL.DT+KKTL.OPC().DW); win_disp() ?};
1


\KKTL_DW_F3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: F3 przy redakcji pola KKTL.DW
::  OLD: \KKTL_DW_F3/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
undefine();
define('KKTLDWF3',KKTL.OPC().DW,form(KKTL.DT)+' + ilość dni','Ilość dni ważności kalkulacji'@,,10,0);
def_btn('text=%1'['Zapisz'@],'key:F2');
def_btn('text=%1'['Anuluj'@],'key:Esc');
_valid:="
   {? @.DEFINE.KKTLDWF3<0
   || FUN.error('Ilość dni ujemna.\nTermin ważności kalkulacji nie może być wcześniejszy niż %1.'@[form(KKTL.DT)]);
      0
   || 1
   ?}
";
{? def_edit(_valid,FUN.TYT)
|| fld(KKTL.DT+@.DEFINE.KKTLDWF3)
?};
undefine();
~~


\run_kalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Po dodaniu nowej kalkulacji (od razu proponuje wykonanie kalkulacji)
::   WY: 0
::  OLD: \run_kalk/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
win_disp();
exec('kalkuluj','!tte_tec_ktkw');
0


\kalkuluj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: uruchamia proces kalkulacji karty
::   WE: [_a] - czy wyświetlić wynik kalkulacji (1), domyślnie nie (0)
::   WY: 1 - przeliczenie było wykonywane, 0 - przeliczenie nie bło wykonywane
::  OLD: \kalkuluj/pkalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _display:=_a || _display:=1 ?};

_res:=0;

{? KKTL.r_lock(1,1,1)
|| {? KKTL.STAN='N'
   || {? KKTL.DT_D>=date() | KKTL.DT_D=date(0,0,0)
      || {? FUN.ask('Czy wykonać kalkulację?'@)
         ||
::          Deklaruje obiekt KOMW do obsługi ostrzeżeń powstałych podczas kalkulacji
::            exec('init','communiq');
            {? var_pres('KOMW')<100 || KOMW:=obj_new(@.CLASS.JCQ) ?};
            KOMW.init(250,,'Ostrzeżenia powstałe podczas kalkulacji'@,'',,,0);

::          Inicjuje KOMMa który zawiera błędy i ostrzeżenia
            KOMM.init(250,,'Wyniki działania kalkulacji'@,'',,,2);

            {? exec('kalk_one','!tte_tec_ktkw',KKTL.ref())
            || KOMW.select();
               {? _display || exec('kktlwndp','!tte_tec_ktkw') ?};
               _res:=1
            || KOMM.info('Kalkulacja przerwana.'@);
               KOMM.select()
            ?}
         ?}
      || FUN.emsg('Data cen prognozowanych wcześniejsza od bieżącej.\nWykonanie opcji nie jest możliwe.'@)
      ?}
   || FUN.emsg('Kalkulacja jest zatwierdzona.\nWykonanie opcji nie jest możliwe.'@)
   ?};
:: Usuwam obiekt do obsługi ostrzeżeń
   VAR_DEL.delete('KOMW');
   KKTL.r_unlock()
|| FUN.emsg('Kalkulacja redagowana przez innego użytkownika.\nWykonanie opcji nie jest możliwe.'@)
?};
_res


\kalk_one
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: kalkuluje jedną kalkulację
::   WE: _a - KKTL.ref()
::       [_b] - czy zachować wyliczenie (domyślnie 1)
::       [_c] - czy pokazywać parametry (domyślnie 1)
::       [_d] - czy kalkulacja tymczasowa (domyślnie 0)
::       [_e] - STRING - czy aktywować kalkulację (T/~T)
::   WY: 0 / 1 - czy prawidłowo przeliczono
::  OLD: \kalk_one/pkalk.fml
::----------------------------------------------------------------------------------------------------------------------
_kktl_ref:=_a;
_save:={? var_pres('_b')=type_of(0) || _b || 1 ?};
_show_par:={? var_pres('_c')=type_of(0) || _c || 1 ?};
_kalkt:={? var_pres('_d')=type_of(0) || _d || 0 ?};
_akt:='';
{? var_pres('_e')=type_of('')
|| _akt:=_e
?};

KKTL.cntx_psh();
KKTL.clear();
_ok:=KKTL.seek(_kktl_ref);
_delpar:=0;
{? var_pres('__KPOZK')>0 & _kalkt
|| __KPOZK.erase();
   exec('KL_done','!tte_tec_ktkw');
   exec('ProDpric_stop','kalk_mater')
?};
VAR_DEL.delete('EXIT');
EXIT:=0;
exec('ProDpric_start','kalk_mater');
{? _ok
|| KOMM.sect_beg(
      'Technologia: '+KKTL.NRK().NRK+' wer. '+KKTL.NRK().WER+
      ';  kalkulacja nr: '+$KKTL.KNR+{? KKTL.TKTLW=null() || '' || '; produkt: '+KKTL.TKTLW().KTM().KTM ?},,'64:0:128',1
   );
   VAR.A_WAR:=KKTL.OPC;
   exec('KL_init','!tte_tec_ktkw',_kalkt);
:: Czy nie zmieniano nic w parametrach?
   {? exec('kpar_changed','!tte_tec_ktkw')
   || KOMM.error('Kalkulacja zawiera nieaktualną listę parametrów. Ponowne przeliczenie nie jest możliwe.'@);
      _ok:=0
   || {? _save & ~_kalkt
      ||
::         exec('poz_del','tech_kalk');
         _delpar:=1;
         exec('del_kalmat','!tte_tec_ktkw',KKTL.ref());
         exec('del_kaltto','!tte_tec_ktkw',KKTL.ref());
         exec('del_kalwrk','!tte_tec_ktkw',KKTL.ref());
         exec('del_kaltls','!tte_tec_ktkw',KKTL.ref());
         exec('del_kaltou','!tte_tec_ktkw',KKTL.ref())
      ?};
      {? KL.calculate(_show_par)
      || _ok:=1;
         {? _save & ~_kalkt
         || exec('del_kpar','!tte_tec_ktkw',KKTL.ref());
            _delpar:=0;
            KL.save()
         |? _kalkt=1
         || exec('load4tab','!tte_tec_ktkw',KL);
            exec('kalk_compare','!tte_tec_ktkw');
            save_kalk:=save_kons:=0;
            __KPOZK.prefix();
            __KPOZK.first()
         ?}
      || _ok:=0
      ?};
      {? _delpar || _delpar:=0; exec('del_kpar','!tte_tec_ktkw',KKTL.ref()) ?};
      {? ~_kalkt
      || exec('KL_done','!tte_tec_ktkw');
         exec('ProDpric_stop','kalk_mater')
      ?}
   ?};
   KOMM.sect_end()
?};
KKTL.cntx_pop();
{? (_ok & _save)
|| exec('KKTL_update','tech_kalk',KKTL.ref());
   {? _akt='T'
   || exec('zatwkalk','!tte_tec_ktkw',0)
   ?}
?};
_ok


\kalk4ktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Wykonuje kalkulację dla danej karty
::   WE: _a - TKTL.ref()
::       _b - tworzyć nową kalkulację?
::       _c - TKTLW.ref() / null()
::       [_d] - pokazać dialog z parametrami?
::       [_e] - STRING - czy aktywować kalkulację (T/~T)
::   WY: 1 / 0 - gdy błędy w przeliczaniu
::  OLD: \kalk4ktl/pkalk.fml
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;
_newk:=_b;
_tktlw:=_c;
_show_par:={? var_pres('_d')=type_of(0) || _d || 1 ?};
_akt:='';
{? var_pres('_e')=type_of('')
|| _akt:=_e
?};

VAR.A_KTL:=_tktl;
VAR.A_TKTLW:=_tktlw;

TKTL.cntx_psh();
TKTL.clear();
_ok:=1;
TKTL.seek(VAR.A_KTL);
VAR.A_WAR:=exec('find_opc','!tte_tec_ktkw',VAR.A_KTL);
{? VAR.A_WAR=null()
|| KOMM.add('%1 / %2 — nie została przeliczona — brak domyślnego wariantu kalkulacyjnego'@[TKTL.NRK,TKTL.WER],4);
   TKTL.cntx_pop();
   return(0)
?};
:: Szuka ostatniej niezatwierdzonej kalkulacji
KKTL.clear();
KKTL.index('KSWN');
KKTL.prefix(VAR.A_KTL,'N',VAR.A_TKTLW,VAR.A_WAR);
{? ~KKTL.last() || _newk:=1 ?};
{? _newk || _ok:=exec('KKTL_new','tech_kalk',VAR.A_KTL,VAR.A_WAR,VAR.A_TKTLW) ?};
{? _ok   || _ok:=exec('kalk_one','!tte_tec_ktkw',KKTL.ref(),1,_show_par,,_akt) ?};
TKTL.cntx_pop();
_ok


\find_opc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Podaje ref wariantu dla danej karty technologicznej
::   WE: _a - TKTL.ref()
::   WY: KWAR.ref(), wpp null()
::  OLD: \find_opc/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
_tktl_ref:=_a;

_wynik:=null();
TKTL.cntx_psh();
TKTL.clear();
_ok:=TKTL.seek(_tktl_ref);
{? _ok
|| {? TKTL.DEF_OPCK<>null()
   || _wynik:=TKTL.DEF_OPCK
   || {? TKTL.TYP().DEF_OPCK<>null()
      || _wynik:=TKTL.TYP().DEF_OPCK
      ?}
   ?}
?};
TKTL.cntx_pop();
_wynik


\load4tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Wpisuje rubryki kalkulacji do tabeli tymczasowej pozycji kalkulacji.
::   WE: _a - obiekt kalkulacyjny
::  OLD: \load4tab/prodobi.fml
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;

KRUB.cntx_psh();
KRUB.clear();
{! _nr:=1..(_obj.MAX)
|! {? _obj.G[_nr]=2
   || __KPOZK.blank();
      KRUB.seek(_obj.R[_nr]);
      __KPOZK.NR:=KRUB.NR;
      __KPOZK.OPIS:=KRUB.OPIS;
      __KPOZK.WART:=_obj.L[1][_nr];
      __KPOZK.add()
   ?}
!};
KRUB.cntx_pop();
~~


\kalk_compare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: Przepisuje wartości do porównania
::  OLD: \kalk_compare/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
__KPOZK.cntx_psh();
__KPOZK.prefix();
{? __KPOZK.first()
|| {!
   |? KPOZK.prefix(VAR.A_KKTL,__KPOZK.NR);
      {? KPOZK.first()
      || __KPOZK.WART1:=KPOZK.WART;
         __KPOZK.put()
      ?};
      __KPOZK.next()
   !}
?};
__KPOZK.cntx_pop()


\KL_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: przygotowanie obiektu dla kalkulacji
::  OLD: \KL_init/pkalk.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('KL');
KL:=obj_new(@.CLASS.ANKA);
{? _a=0
|| exec('sv_load4tech','tech_kalk',KL)
|| exec('sv_kload4tech','tech_kalk',KL)
?};
~~


\KL_done
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: sprzątanie po obiekcie dla kalkulacji
::  OLD: \KL_done/pkalk.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('KL');
~~


\kktlwndp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: przed POZYCJE kalkulacji
::  OLD: \kktlwndp/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KKTL.cntx_psh();
KKTL.win_sel('WER');
KPOZK.clear();
KPOZK.index('KR');
KPOZK.prefix(KKTL.ref());
exec('kj_licz','tech_kalk');
KPOZK.win_fml('WER',,'RUBR','NR','ICON_BEFORE',"
   KFORM.index('WR');
   KFORM.prefix(KKTL.OPC,KPOZK.RUBR().NR);
   {? KFORM.first()
   || {? KFORM.FRS=''
      || ''
      || 'xwin16.png:42'
      ?}
   || ''
   ?}
");
_f_close:="
   {? params_get().args.change>0
   || FUN.ask(
         'Wprowadzono modyfikacje do pozycji kalkulacji, a nie została ona przeliczona.'@+
         '\n\n'+'Czy na pewno zamknąć okno?'@
      )
   || 1
   ?}
";
_grp:=KPOZK.grp_make('Kalkulacja TKW karty: %1 wersja: %2'@[VAR.A_KTL().NRK,TKTL.WER],,'#kktlwndp',,,_f_close,,'normal');
KPOZK.grp_sel(_grp,,'WER',,,,,,,,,,'maximized');
KPOZK.win_sel(_grp);
_args:=obj_new('change');
_args.change:=0;
params_set('args',_args);
KPOZK.actions('WER',{? KKTL.STAN='N' || '' || 'pWC' ?});
KPOZK.select();
KPOZK.win_fml(KPOZK.win_sel('WER'),,'RUBR','NR','ICON_BEFORE',"''");
KKTL.cntx_pop();
~~


\kalkusun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: usunięcie kalkulacji karty technologicznej razem z jej pozycjami
::  OLD: \kalkusun/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
_grupa:=KKTL.sel_size()<>0;

{? KKTL.r_lock(1,1,1)
|| {? KKTL.STAN='N'
   || {? _grupa
      || KKTL.r_unlock(); exec('kalk_del','!tte_tec_ktkw',KKTL.ref())
      || {? FUN.ask('Czy usunąć bieżący wiersz?'@)
         || KKTL.r_unlock(); exec('kalk_del','!tte_tec_ktkw',KKTL.ref())
         || KKTL.r_unlock()
         ?}
      ?}
   || _msg:='Kalkulacja %1 jest zatwierdzona.'@[$KKTL.KNR];
      {? _grupa
      || KOMM.add(_msg)
      || FUN.emsg(_msg)
      ?};
      KKTL.r_unlock()
   ?}
|| _msg:='Kalkulacja %1 jest redagowana przez innego użytkownika.'@[$KKTL.KNR];
   {? _grupa
   || KOMM.add(_msg)
   || FUN.emsg(_msg)
   ?}
?}


\kalkusun_gpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Usunięcie kalkulacji karty technologicznej - przed grupą rekordów
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?'@)
|| KOMM.init(,,'Usuwanie kalkulacji karty technologicznej'@);
   1
|| 0
?}


\kalkusun_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Usunięcie kalkulacji karty technologicznej - po grupie rekordów
::----------------------------------------------------------------------------------------------------------------------
KOMM.select();
~~


\kalk_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: usunięcie kalkulacji (bez dyskusji)
::   WE: KKTL.ref()
::  OLD: \kalk_del/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? KKTL.seek(_a)
||
:: pozycje
   exec('poz_del','tech_kalk',KKTL.ref());
:: dane pomocnicze
   KKALKH.clear();
   KKALKH.index('KR');
   KKALKH.prefix(KKTL.ref());
   {? KKALKH.first() || {! |? KKALKH.del() !} ?};
:: ceny materiałów
   exec('del_kalmat','!tte_tec_ktkw',KKTL.ref());
:: stawki operacji
   exec('del_kaltto','!tte_tec_ktkw',KKTL.ref());
:: stawki stanowisk
   exec('del_kalwrk','!tte_tec_ktkw',KKTL.ref());
:: koszyty N-P-U
   exec('del_kaltls','!tte_tec_ktkw',KKTL.ref());
:: stawki usług
   exec('del_kaltou','!tte_tec_ktkw',KKTL.ref());
:: parametry
   exec('del_kpar','!tte_tec_ktkw',KKTL.ref());
   KKTL.del()
?}


\kalk_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60]
:: OPIS: Selekcja wszystkich danych dodatkowych kalkulacji
::  OLD: \kalk_dod/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KALMAT.index('KSN');
KALMAT.prefix(KKTL.ref());
KALMAT.win_edit('RED');
KALMAT.win_patt('WZOR');
KALTTO.index('KON');
KALTTO.prefix(KKTL.ref());
KALTTO.first();
KALTTO.win_edit('');
KALWRK.index('KPN');
KALWRK.prefix(KKTL.ref());
KALWRK.first();
KALWRK.win_edit('');
KALTOU.index('KTN');
KALTOU.prefix(KKTL.ref());
KALTOU.first();
KALTOU.win_edit('');
KALTLS.index('KTN');
KALTLS.prefix(KKTL.ref());
KALTLS.first();
KALTLS.win_edit('');
KPAR.index('KN');
KPAR.prefix(KKTL.ref());
KPAR.first();
KPAR.win_edit('');
KKALKH.index('KRW');
KKALKH.prefix(KKTL.ref());
KKALKH.first();

_high:=13;
_grp:=KALMAT.grp_make('Dane dodatkowe kalkulacji technologii'@,,'kalmat_grp',8,8,,,'normal');
KALMAT.grp_sel(_grp,,'WER','Ceny surowców'@,,,,_high,,,,,'maximized');
KALMAT.grp_sel(_grp,KALTTO,'WER','Stawki operacji'@,,,,_high,,,,,'maximized');
KALMAT.grp_sel(_grp,KALWRK,'WER','Koszty stanowisk'@,,,,_high,,,,,'maximized');
KALMAT.grp_sel(_grp,KALTOU,'WER','Stawki usług'@,,,,_high,,,,,'maximized');
KALMAT.grp_sel(_grp,KALTLS,'WER','Ceny N-P-U'@,,,,_high,,,,,'maximized');
KALMAT.grp_sel(_grp,KPAR,'SLO','Parametry'@,,,,_high,,,,,'maximized');
KALMAT.grp_sel(_grp,KKALKH,'WER','Pomocnicze'@,,,,_high,,,,,'maximized');
KALMAT.win_sel(_grp);

KALMAT.select();
~~


\zatwkalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Zatwierdzenie kalkulacji
::       Kontekst wywołania - bieżący rekord KKTL
::  OLD: \zatwkalk/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? KKTL.r_lock(1,1,1)
|| {? KKTL.NRK().KTM<>null()
   || {? KKTL.TKTLW<>null()
      || {? KKTL.STAN<>'T'
         || KKTL.cntx_psh();
            KKTL.prefix();
            KPOZK.clear();
            KPOZK.index('KR');
            KPOZK.prefix(KKTL.ref());
            {? KPOZK.first()
            || KPOZK.clear();
               exec('zatwkalk1','!tte_tec_ktkw')
            || FUN.emsg('Niemożliwe zatwierdzenie kalkulacji bez pozycji.'@)
            ?};
            KKTL.cntx_pop()
         || FUN.emsg('Ta kalkulacja jest już zatwierdzona.'@)
         ?}
      || FUN.emsg(
            'Niemożliwe zatwierdzenie kalkulacji prototypowej.\n\n'
            'Zatwierdzić można wyłącznie kalkulacje\n'
            'z wypełnionym polem ''Kod produktu''.'@
         )
      ?}
   || FUN.emsg(
         'Niemożliwe zatwierdzenie kalkulacji do karty prototypowej.\n'
         'Wypełnij pole ''Kod produktu'' w karcie technologicznej.'@
      )
   ?};
   KKTL.r_unlock()
|| FUN.emsg('Kalkulacja jest redagowana przez inego użytkownika.\nZatwierdzenie niemożliwe.'@)
?};
0


\zatwkalk1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Zatwierdzenie karty kalkulacyjnej
::   WE: [_a] - czy z dialogami (domyślnie - 1) / bez dialogów (0)
::   WY: 0/1 - czy udała się akceptacja kalkulacji
::  OLD: \zatwkalk1/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _dialog:=_a || _dialog:=1 ?};

{? _dialog=0 | FUN.ask('Czy aktywować kalkulację?'@)
|| _ktm:=KKTL.TKTLW().KTM;
   KKTL.cntx_psh();
   KKTL.clear();
   KKTL.index('KZ');
   KKTL.prefix(VAR.A_KTL,'T',_ktm);
   {? KKTL.first()
   || KKTL.cntx_psh();
      KKTL.clear();
      KKTL.STAN:='B';
      KKTL.put();
      KKTL.cntx_pop()
   ?};
   KKTL.cntx_pop();
   KKTL.ACCEPT:=OPERATOR.USER;
   KKTL.STAN:='T';
   KKTL.put();
:: Można zautomatyzować dodawanie propozycji ceny produktu
   {? 0 || exec('kktl2cena','material_prod',0) ?}
?};
~~


\prn_kkt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: wydruk bieżącej kalkulacji technologii - KKTL /KPOZK
::  OLD: \prn_kkt/drukujp.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','TTE_TEC_KTKW','tte_kkt*',,1);
1


\rkprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: kolorowanie rekordów w oknie z kalkulacjami
::  OLD: \rkprz/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? KKTL.STAN='T'
|| 'KKTL#01#01'
|? KKTL.STAN='B'
|| 'KKTL#01#02'
|| ''
?}


\kktl_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Po rekordzie dla KKTL
::   WY: zgodny z chk_rec()
::  OLD: \kktl_po/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
chk_rec('OPC')


\leg_kktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Legenda w oknie kalkulacji (tabela KKTL)
::  OLD: \leg_kktl/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','KKTL#01#')


\pw_tktm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: przed wyświetleniem pola VAR.TKTM
::  OLD: \pw_tktm/polap.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.TKTM:=KKTL.TKTLW().KTM().KTM;
1


\pr_tktm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: przed redakcją pola VAR.TKTM
::  OLD: \pr_tktm/polap.fml
::----------------------------------------------------------------------------------------------------------------------
{? params_get().TKTLW=null() || 1 || 0 ?}


\f3_tktm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: Formuła na F3 w polu VAR.TKTM - 'Produkt'
::  OLD: \f3_tktm/polap.fml
::----------------------------------------------------------------------------------------------------------------------
_ktm:='';

TKTLW.index('TP');
TKTLW.prefix(KKTL.NRK);
TKTLW.win_sel('WER_T');
TKTLW.actions('WER_T','dpuAMO:d','W');
{? TKTLW.select()
|| VAR.TKTM:=TKTLW.KTM().KTM;
   KKTL.TKTLW:=TKTLW.ref();
   _ktm:=VAR.TKTM
|| VAR.TKTM:='';
   KKTL.TKTLW:=null()
?};

_ktm


\po_tktm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Po redakcji pola VAR.TKTM - 'Produkt'
::  OLD: \po_tktm/polap.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=0;

{? fld()='' || KKTL.TKTLW:=null(); TKTLW.blank(1); return(1) ?};

TKTLW.index('TP');
TKTLW.prefix(KKTL.NRK,fld());
{? TKTLW.first()
|| VAR.TKTM:=TKTLW.KTM().KTM;
   KKTL.TKTLW:=TKTLW.ref();
   _res:=1
|| _choice:=FUN.choice('Brak pozycji w słowniku.'@,1,'Powtórz'@,'Wyświetl &słownik'@);
   {? _choice=1
   || return(0)
   |? _choice=2
   || _ktm:=exec('f3_tktm','!tte_tec_ktkw');
      {? _ktm<>''
      || VAR.TKTM:=_ktm;
         _res:=exec('po_tktm','!tte_tec_ktkw')
      ?}
   || VAR.TKTM:='';
      _res:=1
   ?}
?};
_res


\kktl_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Zwraca złączenie na kalkulację
::   WY: KKTL.ref()
::  OLD: \kktl_ref/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KKTL.ref()


\kktlcomp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK
:: OPIS: Wywołanie porównania kalkulacji TKW - właściwa akcja
::  OLD: \kktlcomp/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? KKTL.sel_size()=0
|| KKTL.cntx_psh();
   VAR.A_KKTL:=KKTL.ref();
   KKTL.win_sel('SLO');
   {? KKTL.select()
   || exec('compare','!tte_tec_ktkw',VAR.A_KKTL,KKTL.ref())
   ?};
   KKTL.cntx_pop()
|| params_get().xkktl.licznik+=1;
   params_get().xkktl[params_get().xkktl.licznik]:=KKTL.ref()
?};
~~


\kktlcomp_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Porównanie dwóch kalkulacji - akcja przed grupą rekordów
::----------------------------------------------------------------------------------------------------------------------
{? KKTL.sel_size()<>2
|| FUN.info('Należy zaznaczyć dwie kalkulacje do porównania.'@);
   0
|| params_get().xkktl.licznik:=0;
   1
?}


\kktlcomp_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Porównanie dwóch kalkulacji - akcja po grupie rekordów
::----------------------------------------------------------------------------------------------------------------------
exec('compare','!tte_tec_ktkw',params_get().xkktl.kktl,params_get().xkktl.pkktl);
~~


\compare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: Wyświetlanie wyników porównania dwóch kalkulacji
::   WE: _a - KKTL.ref()
::       _b - KKTL.ref()
::  OLD: \compare/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.A_KKTL:=_a;
VAR.A_PKKTL:=_b;

_kpozk:=tab_tmp(1,
   'NR','INTEGER','Numer',
   'WARTOSC','REAL','Wartość'
);
KPOZK.cntx_psh();
KPOZK.index('KR');
KPOZK.prefix();
{? KPOZK.find_key(VAR.A_PKKTL,VAR.A_PKKTL().NRK().TYP().CRUBIL) || _il:=KPOZK.WART || _il:=0 ?};
{? KPOZK.find_key(VAR.A_PKKTL,VAR.A_PKKTL().NRK().TYP().CRUBMAT) || _mat:=KPOZK.WART || _mat:=0 ?};
{? KPOZK.find_key(VAR.A_PKKTL,VAR.A_PKKTL().NRK().TYP().CRUBROB) || _rob:=KPOZK.WART || _rob:=0 ?};
{? _il<>0
|| VAR.JKPMAT:=_mat/_il;
   VAR.JKPROB:=_rob/_il
|| VAR.JKPMAT:=VAR.JKPROB:=0
?};
KPOZK.prefix();
{? KPOZK.find_key(VAR.A_KKTL,VAR.A_KKTL().NRK().TYP().CRUBIL) || _il:=KPOZK.WART || _il:=0 ?};
{? KPOZK.find_key(VAR.A_KKTL,VAR.A_KKTL().NRK().TYP().CRUBMAT) || _mat:=KPOZK.WART || _mat:=0 ?};
{? KPOZK.find_key(VAR.A_KKTL,VAR.A_KKTL().NRK().TYP().CRUBROB) || _rob:=KPOZK.WART || _rob:=0 ?};
{? _il<>0
|| VAR.JK_MAT:=_mat/_il;
   VAR.JK_ROB:=_rob/_il
|| VAR.JK_MAT:=VAR.JK_ROB:=0
?};
KPOZK.index('KR');
KFORM.cntx_psh();
KFORM.index('WR');
KFORM.prefix(VAR.A_PKKTL().OPC);
{? KFORM.first()
|| {!
   |? KPOZK.prefix(VAR.A_KKTL,KFORM.NRUB().NR);
      {? ~KPOZK.first()
      || KPOZK.KALK:=VAR.A_KKTL;
         KPOZK.RUBR:=KFORM.NRUB;
         KPOZK.WART:=0;
         KPOZK.add()
      ?};
      KPOZK.prefix(VAR.A_PKKTL,KFORM.NRUB().NR);
      {? KPOZK.first()
      || _kpozk.NR:=KFORM.NRUB().NR;
         _kpozk.WARTOSC:=KPOZK.WART;
         _kpozk.add()
      ?};
      KFORM.next()
   !}
?};
KPOZK.cntx_pop();
KFORM.prefix(VAR.A_KKTL().OPC);
{? KFORM.first()
|| {!
   |? _kpozk.prefix(KFORM.NRUB().NR);
      {? ~_kpozk.first()
      || _kpozk.NR:=KFORM.NRUB().NR;
         _kpozk.WARTOSC:=0;
         _kpozk.add()
      ?};
      KFORM.next()
   !}
?};
KFORM.cntx_pop();
KPOZK.clear();
KFORM.clear();
KFORM.index('WR');
KPOZK.prefix(VAR.A_KKTL);
_wer:='WERP';
KPOZK.win_sel(_wer);
KPOZK.win_fml(_wer,,'WART',,'ICON_BEFORE',"
   KFORM.index('WR');
   KFORM.prefix(VAR.A_KKTL().OPC,KPOZK.RUBR().NR);
   {? KFORM.first()
   || {? KFORM.FRS=''
      || ''
      || 'xwin16.png:42'
      ?}
   || ''
   ?}
");
KPOZK.win_fml(_wer,VAR,'WARPOR',,'ICON_BEFORE',"
   KFORM.index('WR');
   KFORM.prefix(VAR.A_PKKTL().OPC,KPOZK.RUBR().NR);
   {? KFORM.first()
   || {? KFORM.FRS=''
      || ''
      || 'xwin16.png:42'
      ?}
   || ''
   ?}
");
KPOZK.hdr_sel(' '+'TKW'@);
params_set('kpozk',_kpozk);
KPOZK.select();
KPOZK.win_fml(_wer,,'WART',,'ICON_BEFORE',"");
KPOZK.win_fml(_wer,VAR,'WARPOR',,'ICON_BEFORE',"");
KPOZK.first();
:: sprzątanie chwilowo dodanych rubryk
{!
|?
   KFORM.prefix(KPOZK.KALK().OPC,KPOZK.RUBR().NR);
   {? ~KFORM.first()
   || KPOZK.del()
   || KPOZK.next()
   ?}
!};
{? KKTL.seek(VAR.A_PKKTL) || KKTL.r_unlock() ?};
VAR.A_PKKTL:=null();
~~


\algchange
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: dodanie tabel tymczasowych i okien do obsługi kalkulacji ze zmienionym algorytmem.
::  OLD: \algchange/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~KKTL.r_lock(1,1,1) || FUN.emsg('Kalkulacja redagowana przez innego użytkownika.'@); return() ?};
VAR.A_KKTL:=KKTL.ref();
__LEVEL:=1;

VAR_DEL.delete('__KFORM','__KKS','__KPOZK');

__KFORM:=tab_tmp(1,
   'NR','INTEGER','Nr.rubryki',
   'OPIS','STRING[40]','Opis rubryki',
   'REF','INTEGER','#KFORM.ref()',
   'FORM','STRING[255]','Formuła',
   'FRS','STRING[255]','Formuła - wyświetlanie szczegółów'
);
__KFORM.fld_attr(,2);

__KKS:=tab_tmp(1,
   'NR','INTEGER','Nr.rubryki',
   'REF','INTEGER','#KKS.ref()',
   'OPIS','STRING[40]','Opis',
   'WAR','REAL','Wartość'
);
__KKS.fld_attr(,2);

__KPOZK:=tab_tmp(1,
   'NR','INTEGER','Numer rubryki',
   'WART','REAL','Wartość',
   'WART1','REAL','Wartość1',
   'ROZN','REAL','Różnica',
   'OPIS','STRING[40]','Opis'
);

redp:=__KFORM.mk_edit('Formuły kalkulacyjne'@,0,'kform_redp');
__KFORM.win_efld(redp,,'NR',,,6,,1,'Numer'@,,,);
__KFORM.win_efld(redp,,'OPIS',,,35,,1,'Opis'@);
__KFORM.win_efld(redp,,'FORM',,,60,,,'Formuła'@);
__KFORM.efld_opt(redp, 'mark=1',,'FORM');
__KFORM.win_ebtn(redp,'text=%1'['&Zapisz'@],"'key:F2'");
__KFORM.win_ebtn(redp,'text=%1'['&Anuluj'@],"'key:Esc'");

red:=__KFORM.mk_edit('Formuły kalkulacyjne'@,0,'kform_red');
__KFORM.win_efld(red,KFORM,'NRUB','NR','NR',6,,,'Numer'@,,,);
__KFORM.efld_opt(red,'mark=1',KFORM,'NRUB','NR');
__KFORM.win_efld(red,KFORM,'NRUB','OPIS',,35,,1,'Opis'@);
__KFORM.win_efld(red,,'FORM',,,60,,,'Formuła'@);
__KFORM.efld_opt(red, 'mark=1',,'FORM');
__KFORM.win_ebtn(red,'text=%1'['&Zapisz'@],"'key:F2'");
__KFORM.win_ebtn(red,'text=%1'['&Anuluj'@],"'key:Esc'");

_wer:=__KFORM.mk_sel('Formuły kalkulcyjne'@,'N',0,'kform_wer',,5,29);
__KFORM.win_fld(_wer,,'NR',,,6,,1,'Numer'@);
__KFORM.win_fld(_wer,,'OPIS',,,40,,1,'Opis'@);
__KFORM.win_fld(_wer,,'FORM',,,60,,,'Formuła'@);
__KFORM.win_act(_wer,0,'Formuła','Dołącz'@@,,,"exec('Add_kform','!tte_tec_ktkw')",,1,,,,'D');
__KFORM.win_act(_wer,1,'Formuła','Dołącz'@@,,,"exec('Add_kform','!tte_tec_ktkw')",,1,,,,'D');
__KFORM.win_act(_wer,0,'Formuła','Popraw'@@,,,"__KFTEM:=__KFORM.FORM;
                                             __KFORM.win_edit(redp);
                                             __KFORM.edit(\"
                                                            _ok:='';
                                                            {? __KFORM.FORM=''
                                                            || _ok:='FORM';
                                                               FUN.emsg('Formuła kalkulacyjna musi być wypełniona.'@)
                                                            ?};
                                                            {? _ok=''
                                                            ||
                                                               {? __KFTEM<>__KFORM.FORM
                                                               || _ok:=exec('fparse','kalkulator',__KFORM.FORM,'fka')
                                                               || _ok:=1
                                                               ?}
                                                            ?};
                                                            _ok
                                                          \")
                                            ","{? __KFTEM<>__KFORM.FORM
                                               ||
                                                  __KFORM.put()
                                               ?};
                                               &__KFTEM;
                                               __KFORM.win_edit(red)",,,,,'P');
__KFORM.win_act(_wer,0,'Usuń');
__KFORM.win_act(_wer,0,'Formuła','P&rzeładuj'@@,,,"exec('ref_kform','!tte_tec_ktkw')",,,,,,'R');
__KFORM.win_act(_wer,1,'Formuła','P&rzeładuj'@@,,,"exec('ref_kform','!tte_tec_ktkw')",,,,,,'R');

_wer1:=__KKS.mk_sel('Stałe użyte do kalkulacji'@,'N',0,'kks_wer',,5,29);
__KKS.win_fld(_wer1,,'NR',,,6,,1,'Numer'@);
__KKS.win_fld(_wer1,,'OPIS',,,35,,1,'Opis'@);
__KKS.win_fld(_wer1,,'WAR',,,15,4,,'Wartość'@);
__KKS.win_act(_wer1,0,'Formuła','Dołącz'@@,,,"exec('Add_kks','!tte_tec_ktkw')",,1,,,,'D');
__KKS.win_act(_wer1,1,'Formuła','Dołącz'@@,,,"exec('Add_kks','!tte_tec_ktkw')",,1,,,,'D');
__KKS.win_act(_wer1,0,'Popraw',,,,"__KKS.win_edit()");
__KKS.win_act(_wer1,0,'Usuń');
__KKS.win_act(_wer1,0,'Formuła','P&rzeładuj'@@,,,"exec('ref_kks','!tte_tec_ktkw')",,,,,,'R');
__KKS.win_act(_wer1,1,'Formuła','P&rzeładuj'@@,,,"exec('ref_kks','!tte_tec_ktkw')",,,,,,'R');
__KKS.win_act(_wer1,,'Wyświetl',,,,"
   KRUB.index('NR');
   KRUB.prefix(__KKS.NR);
   {? KRUB.first() || _krub:=KRUB.ref() || _krub:=null() ?};
   KKS.blank();
   KKS.RUBR:=_krub;
   __KKS.display();
   KRUB.prefix();
   ~~
");

red1:=__KKS.mk_edit('Stałe użyte do kalkulacji'@,0,'kks_red');
__KKS.win_efld(red1,KKS,'RUBR','NR','NR',6,,,'Numer'@,,,);
__KKS.efld_opt(red1,'mark=1',KKS,'RUBR','NR');
__KKS.win_efld(red1,KKS,'RUBR','OPIS',,35,,1,'Opis'@);
__KKS.win_efld(red1,,'WAR',,,15,4,,'Wartość'@);
__KKS.win_ebtn(red1,'text=%1'['&Zapisz'@],"'key:F2'");
__KKS.win_ebtn(red1,'text=%1'['&Anuluj'@],"'key:Esc'");

_wer2:=__KPOZK.mk_sel('Pozycje kalkulacji'@,'N',0,'kpozk_wer',,5,29);
__KPOZK.win_fld(_wer2,,'NR',,,6,,,'Numer'@);
__KPOZK.win_fld(_wer2,,'OPIS',,,35,,,'Opis'@);
__KPOZK.win_fld(_wer2,,'WART1',,,12,4,,'Wartość stara'@);
__KPOZK.win_fld(_wer2,,'WART',,,12,4,,'Wartość nowa'@);
__KPOZK.win_fld(_wer2,,'ROZN',,,12,4,,'Różnica'@);

_formula:="exec('formchange','!tte_tec_ktkw')";
__KPOZK.win_act(_wer2,0,'Formuła','Formuły'@@,,,_formula,,,,,,'F');
__KPOZK.win_act(_wer2,1,'Formuła','Formuły'@@,,,_formula,,,,,,'F');
_formula:="exec('constchange','!tte_tec_ktkw')";
__KPOZK.win_act(_wer2,0,'Formuła','Stałe'@@,,,_formula,,,,,,'S');
__KPOZK.win_act(_wer2,1,'Formuła','Stałe'@@,,,_formula,,,,,,'S');
_formula:="exec('sur_kalk','kalk_mater')";
__KPOZK.win_act(_wer2,0,'Formuła','Su&rowce'@@,,,_formula,,,,,,'R');
__KPOZK.win_act(_wer2,1,'Formuła','Su&rowce'@@,,,_formula,,,,,,'R');
_formula:="exec('oper_kalk','kalk_oper')";
__KPOZK.win_act(_wer2,0,'Formuła','S&tanowiska'@@,,,_formula,,,,,,'T');
__KPOZK.win_act(_wer2,1,'Formuła','S&tanowiska'@@,,,_formula,,,,,,'T');
_formula:="
   KOMM.init(250,,'Wyniki działania kalkulacji'@,'',,,2);
   {? ~exec('kalk_one','!tte_tec_ktkw',VAR.A_KKTL,1,1,1)
   || KOMM.info('Kalkulacja przerwana.'@);
      KOMM.select()
   ?};
   cur_tab(1,1).actions_grayed(cur_win(1,1))
";
__KPOZK.win_act(_wer2,0,'Formuła','K&alkuluj'@@,,,_formula,,,,,,'A');
__KPOZK.win_act(_wer2,1,'Formuła','K&alkuluj'@@,,,_formula,,,,,,'A');
{? VAR.A_KKTL().STAN='N'
|| _formula:="exec('save_kalk','!tte_tec_ktkw')";
   __KPOZK.win_act(_wer2,0,'Formuła','Zapisz &wyniki'@@,,,_formula,,,,,,'W');
   __KPOZK.win_act(_wer2,1,'Formuła','Zapisz &wyniki'@@,,,_formula,,,,,,'W')
?};
{? exec('chk_role','#b__box',OPERATOR.USER,'ZWS_PAR_TTKT')
|| _formula:="exec('save_cons','!tte_tec_ktkw')";
   __KPOZK.win_act(_wer2,0,'Formuła','Zapisz formuły i stałe'@@,,,_formula,,,,,,'Z');
   __KPOZK.win_act(_wer2,1,'Formuła','Zapisz formuły i stałe'@@,,,_formula,,,,,,'Z')
?};
__KPOZK.win_act(_wer2,0,'Formuła','Druku&j'@@,,,"exec('drkkpozz','!tte_tec_ktkw')",,1,,,,'J');
__KPOZK.win_act(_wer2,,'Formuła','Legenda'@@,,,"exec('legenda','color','KPOZKT#01')",,0,,,,'L');
_formula:="
   cur_tab(1,1).ROZN:=cur_tab(1,1).WART1-cur_tab(1,1).WART;
   {? cur_tab(1,1).ROZN<>0
   || Color.rekprzed('KPOZKT#01#01')
   || ''
   ?}
";
__KPOZK.win_act(_wer2,,'Rekord',,,,_formula,,0);

__KPOZK.win_btn(_wer2,'text=%1,panel=bottom,align=begin'['Formuły'@],'menu:F');
__KPOZK.win_btn(_wer2,'text=%1,panel=bottom,align=begin'['Stałe'@],'menu:S');
__KPOZK.win_btn(_wer2,'text=%1,panel=bottom,align=begin'['Su&rowce'@],'menu:R');
__KPOZK.win_btn(_wer2,'text=%1,panel=bottom,align=begin'['S&tanowiska'@],'menu:T');
__KPOZK.win_btn(_wer2,'text=%1,panel=bottom,align=begin'['K&alkuluj'@],'menu:A');

__KFORM.win_sel(_wer);
__KFORM.win_edit(red);
__KKS.win_sel(_wer1);
__KKS.win_edit(red1);
__KPOZK.win_sel(_wer2);

KFORM.cntx_psh();
KFORM.index('WR');
KFORM.prefix(VAR.A_KKTL().OPC);
{? KFORM.first()
|| {!
   |? __KFORM.REF:=#KFORM.ref();
      __KFORM.NR:=KFORM.NRUB().NR;
      __KFORM.OPIS:=KRUB.OPIS;
      __KFORM.FORM:=KFORM.FRM;
      __KFORM.FRS:=KFORM.FRS;
      __KFORM.add();
      KFORM.next()
   !}
?};
KFORM.cntx_pop();

KKS.cntx_psh();
KKS.index('WR');
KKS.prefix(VAR.A_KKTL().OPC);
{? KKS.first()
|| {!
   |? __KKS.REF:=#KKS.ref();
      __KKS.NR:=KKS.RUBR().NR;
      __KKS.OPIS:=KKS.RUBR().OPIS;
      __KKS.WAR:=KKS.WAR;
      __KKS.add();
      KKS.next()
   !}
?};
KKS.cntx_pop();

KPOZK.cntx_psh();
KPOZK.index('KR');
KPOZK.prefix(VAR.A_KKTL);
{? KPOZK.first()
|| {!
   |? __KPOZK.NR:=KPOZK.RUBR().NR;
      __KPOZK.WART:=0;
      __KPOZK.WART1:=KPOZK.WART;
      __KPOZK.ROZN:=KPOZK.WART;
      __KPOZK.OPIS:=KPOZK.RUBR().OPIS;
      __KPOZK.add();
      KPOZK.next()
   !}
?};
KPOZK.cntx_pop();

save_kalk:=save_kons:=1;

__KPOZK.clear();
__KPOZK.first();
__KPOZK.actions_grayed(_wer2,'WJ');
__KPOZK.select();

{? VAR.A_KKTL().STAN='N'
|| {? save_kalk=0
   || {? FUN.ask('Czy zapisać wyniki kalkulacji?'@)
      || exec('save_kalk','!tte_tec_ktkw')
      ?}
   ?};
::   {? save_kons=0 & exec('chk_role','#b__box',OPERATOR.USER,'ZWS_PAR_TTKT')
::   || {? FUN.ask('Czy zapisać nowe formuły i stałe kalkulacji?'@)
::      || exec('save_cons','!tte_tec_ktkw')
::      ?}
::   ?};
   ~~
?};
&save_kons;
&save_kalk;
obj_del(__KFORM);
obj_del(__KKS);
obj_del(__KPOZK);
&__LEVEL;
&redp;
&red;
&red1;
VAR_DEL.delete('__TMAT','__TOPER');
{? var_pres('KL')>0
|| exec('KL_done','!tte_tec_ktkw');
   exec('ProDpric_stop','kalk_mater')
?};
KKTL.r_unlock();
~~


\Add_kform
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: Dodanie nowej formuły
::  OLD: \Add_kform/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
__KFORM.win_edit(red);
__KFORM.blank();
KFORM.blank();
{? __KFORM.edit("
      _ok:='';
      {? KFORM.NRUB=null()
      || _ok:='NRUB';
         FUN.emsg('Numer rubryki musi być wypełniony.'@)
      ?};
      {? _ok='' & __KFORM.FORM=''
      || _ok:='FORM';
         FUN.emsg('Formuła kalkulacyjna musi być wypełniona.'@)
      ?};
      {? _ok=''
      ||
         {? exec('fparse','kalkulator',__KFORM.FORM,'fka')
         ||
            __KFORM.cntx_psh();
            __KFORM.prefix(KRUB.NR);
            {? __KFORM.first()
            || FUN.emsg('Formuła dla rubryki o numerze: %1 już istnieje.'@[form(__KFORM.NR)]);
               _ok:='NRUB'
            ?};
            __KFORM.cntx_pop()
         ?}
      ?};
      _ok
   ")
|| __KFORM.REF:=0;
   __KFORM.NR:=KRUB.NR;
   __KFORM.OPIS:=KRUB.OPIS;
   __KFORM.add()
?};
__KFORM.win_edit(redp);
~~


\ref_kform
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: przeładowanie formuł
::  OLD: \ref_kform/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
__KFORM.clear();
__KFORM.erase();
KFORM.cntx_psh();
KFORM.index('WR');
KFORM.prefix(VAR.A_KKTL().OPC);
{? KFORM.first()
|| {!
   |? __KFORM.REF:=#KFORM.ref();
      __KFORM.NR:=KFORM.NRUB().NR;
      __KFORM.OPIS:=KRUB.OPIS;
      __KFORM.FORM:=KFORM.FRM;
      __KFORM.FRS:=KFORM.FRS;
      __KFORM.add();
      KFORM.next()
   !}
?};
KFORM.cntx_pop();
__KFORM.first()


\Add_kks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: Dodanie nowej stałej
::  OLD: \Add_kks/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
__KKS.win_edit(red1);
__KKS.blank();
KKS.blank();
{? __KKS.edit("
      _ok:='';
      {? KKS.RUBR=null()
      || _ok:='RUBR';
         FUN.emsg('Numer rubryki musi być wypełniony.'@)
      ?};
      {? _ok=''
      ||
         __KKS.cntx_psh();
         __KKS.prefix(KRUB.NR);
         {? __KKS.first()
         || FUN.emsg('Stała dla rubryki o numerze: %1 już istnieje'@[form(__KKS.NR)]);
            _ok:='RUBR'
         ?};
         __KKS.cntx_pop()
      ?};
      _ok
   ")
|| __KKS.REF:=0;
   __KKS.NR:=KRUB.NR;
   __KKS.OPIS:=KRUB.OPIS;
   __KKS.add()
?}


\ref_kks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: przeładowanie stałych
::  OLD: \ref_kks/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
__KKS.clear();
__KKS.erase();
KKS.cntx_psh();
KKS.index('WR');
KKS.prefix(VAR.A_KKTL().OPC);
{? KKS.first()
|| {!
   |? __KKS.REF:=#KKS.ref();
      __KKS.NR:=KKS.RUBR().NR;
      __KKS.OPIS:=KKS.RUBR().OPIS;
      __KKS.WAR:=KKS.WAR;
      __KKS.add();
      KKS.next()
   !}
?};
KKS.cntx_pop();
__KKS.first()


\drkkpozz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Porównanie kalkulacji TKW
::  OLD: \drkkpozz/drukujp.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
rep_exec('tte_drukpozz',,1);
1


\drkkpozk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Porównanie kalkulacji TKW
::  OLD: \drkkpozk/drukujp.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
rep_exec('tte_drukpozk',,1);
1


\save_kalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: usunięcie starych wyników kalkulacji i zapisanie nowych wartości.
::  OLD: \save_kalk/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~FUN.ask('Czy zapisać zmodyfikowane wyniki kalkulacji?'@) || return() ?};
__KPOZK.clear();
{? __KPOZK.first()
|| exec('poz_del','tech_kalk',KKTL.ref());
   exec('del_kalmat','!tte_tec_ktkw',KKTL.ref());
   exec('del_kaltto','!tte_tec_ktkw',KKTL.ref());
   exec('del_kalwrk','!tte_tec_ktkw',KKTL.ref());
   exec('del_kaltou','!tte_tec_ktkw',KKTL.ref());
   exec('del_kaltls','!tte_tec_ktkw',KKTL.ref());
   exec('del_kpar','!tte_tec_ktkw',KKTL.ref());
   KL.save();
   exec('KKTL_update','tech_kalk',KKTL.ref());
   save_kalk:=1
|| FUN.info('Nie została wykonana kalkulacja, zapisanie wyników nie jest możliwe.'@)
?}


\save_cons
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: Zapisanie zmienionych wartosci formul i stalych do tabel systemu
::  OLD: \save_cons/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Czy zapisać zmodyfikowane formuły i stałe?'@,,'Bieżący wariant'@,'Nowy wariant'@);
{? _choice=0
|| return()
||
   __KPOZK.clear();
   {? __KPOZK.first()
   || {! |? exec('savetab','!tte_tec_ktkw',_choice-1)=0 !};
      save_kons:=1
   || FUN.info('Nie została wykonana kalkulacja, zapisanie wyników nie jest możliwe.'@)
   ?}
?};
~~


\savetab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: Zapisanie zmienionych wartości formuł i stałych do tabel systemu
::   WE: [_a]: 0 - zapis do bieżącego wariantu, 1 - nowy wariant
::   WY: -1 / 0 / 1 - esc, albo czy udany zapis
::  OLD: \savetab/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _<1 || _new:=0 || _new:=_a ?};

_kwar:=VAR.A_WAR;

{? _new
||
   undefine();
   define('WAR',KWAR.NAZ,'Nowy wariant'@,'Nazwa nowego wariantu'@,40,40);
   {? def_edit("chk_rec()",ST.MSG)
   || KWAR.NAZ:=DEFINE.WAR;
      {? KWAR.add(1)
      || _kwar:=KWAR.ref();
         TPKTLF.clear();
         TPKTLF.TYP:=TKTL.TYP;
         TPKTLF.OPC:=_kwar;
         TPKTLF.add();
         KWAR.f_set('NAZ',,
            '"KWAR"."REFERENCE" in
            (select TPKTLF.OPC from TPKTLF where TPKTLF.TYP=\':_a\')',
            $VAR.A_KTL().TYP
         )
      || FUN.emsg('"'+KWAR.NAZ+'"\n\n'+'Wariant o tej nazwie już istnieje.'@);
         return(0)
      ?}
   || return(-1)
   ?}
?};

KFORM.index('WR');
__KFORM.prefix();
{? __KFORM.first()
|| {!
   |? KFORM.prefix(_kwar,__KFORM.NR);
      {? KFORM.first()
      ||
         KFORM.FRM:=__KFORM.FORM;
         KFORM.put()
      ||
         KFORM.OPC:=_kwar;
         KRUB.cntx_psh();
         KRUB.index('NR');
         KRUB.prefix(__KFORM.NR);
         {? KRUB.first()
         || KFORM.NRUB:=KRUB.ref()
         || KFORM.NRUB:=null()
         ?};
         KRUB.cntx_pop();
         KFORM.FRM:=__KFORM.FORM;
         KFORM.FRS:=__KFORM.FRS;
         KFORM.add();
         __KFORM.REF:=#KFORM.ref();
         __KFORM.put()
      ?};
      __KFORM.next()
   !}
?};
KKS.index('WR');
KKS.prefix(_kwar);
{? KKS.first()
|| {! |? KKS.del() !}
?};
__KKS.prefix();
{? __KKS.first()
|| {!
   |? KKS.OPC:=_kwar;
      KRUB.cntx_psh();
      KRUB.index('NR');
      KRUB.prefix(__KKS.NR);
      {? KRUB.first()
      || KKS.RUBR:=KRUB.ref()
      || KKS.RUBR:=null()
      ?};
      KRUB.cntx_pop();
      KKS.WAR:=__KKS.WAR;
      KKS.add();
      __KKS.REF:=#KKS.ref();
      __KKS.put();
      __KKS.next()
   !}
?};
1


\formchange
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: Wyświetla formuły - do modyfikacji w kalkulacji what-if
::  OLD: \formchange/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
__KFORM.prefix();
__KFORM.select();
~~


\constchange
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: Wyświetla stałe - do modyfikacji w kalkulacji what-if
::  OLD: \constchange/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
__KKS.prefix();
__KKS.select();
~~


\kalkcomp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Wywołanie porównania kalkulacji TKW
::  OLD: \kalkcomp/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAR.A_KKTL<>KKTL.ref()
|| sel_exit()
|| FUN.emsg('Należy wybrać inną kalkulację niż aktualna.'@);
   ''
?}


\stan_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis stanu kalkulacji, np. do wydruków
::   WE: _a - litera stanu (T,N,B)
::   WY: opis
::----------------------------------------------------------------------------------------------------------------------
{? _a='T' || 'Zatwierdzona'@
|? _a='N' || 'Niezatwierdzona'@
|? _a='B' || 'Była zatwierdzona'@
          || ''
?}


::======================================================================================================================
:: Obsługa tabeli KPOZK - pozycje kalkulacji kart technologicznych
::======================================================================================================================


\kpoz1rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MK [8.10]
:: OPIS: kolorowanie rekordów w oknie z wynikami kalkulacji (porównanie dwóch kalkulacji)
::  OLD: \kpoz1rec/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
_kpozk:=params_get().kpozk;

{? VAR.A_PKKTL=null()
|| KFORM.prefix(KKTL.OPC,KPOZK.RUBR().NR)
|| KFORM.prefix(VAR.A_KKTL().OPC,KPOZK.RUBR().NR)
?};

{? KFORM.first() & KFORM.FRS<>'' || KPOZK.actions_grayed('WERP','') || KPOZK.actions_grayed('WERP','E') ?};
_kpozk.prefix(KPOZK.RUBR().NR);
{? _kpozk.first()
|| VAR.WARPOR:=_kpozk.WARTOSC;
   VAR1.ROZNICA:=KPOZK.WART-_kpozk.WARTOSC
|| VAR.WARPOR:=0;
   VAR1.ROZNICA:=0
?};
_res:=KPOZK.RUBR().NR;
{? _res=KKTL.NRK().TYP().CRUB |
   _res=KKTL.NRK().TYP().CRUBMAT |
   _res=KKTL.NRK().TYP().CRUBROB |
   _res=KKTL.NRK().TYP().CRUBIL
|| 'KPOZK#02#01'
|| ''
?}


\szcz_kpozk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: Szczegóły dla pozycji kalkulacji technologii
::       Formuła działa w kontekście bieżącej kalkulacji technologii
::       Dla porównania kalkulacji bierze VAR.A_KKTL
::  OLD: \szcz_kpozk/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KFORM.index('WR');
{? VAR.A_PKKTL=null()
|| KFORM.prefix(KKTL.OPC,KPOZK.RUBR().NR)
|| KFORM.prefix(VAR.A_KKTL().OPC,KPOZK.RUBR().NR)
?};
{? KFORM.first()
|| {? KFORM.FRS=''
   || FUN.info('Brak szczegółów do pozycji kalkulacji.'@)
   |? KPOZK.MANUAL='T'
   || FUN.info('Nie można wyświetlić szczegółów do modyfikowanej pozycji kalkulacji.'@)
   || ($KFORM.FRS)()
   ?}
?};
~~


\kpoz_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.53]
:: OPIS: Kolorowanie rekordów w oknie z wynikami kalkulacji
::  OLD: \kpoz_rec/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KFORM.index('WR');
KFORM.prefix(KKTL.OPC,KPOZK.RUBR().NR);
{? KFORM.first() & KFORM.FRS<>''
|| KPOZK.actions_grayed(cur_win(1,1),'')
|| KPOZK.actions_grayed(cur_win(1,1),'E')
?};
_res:=KPOZK.RUBR().NR;
{? _res=KKTL.NRK().TYP().CRUB |
   _res=KKTL.NRK().TYP().CRUBMAT |
   _res=KKTL.NRK().TYP().CRUBROB |
   _res=KKTL.NRK().TYP().CRUBIL
|| 'KPOZK#01#01'
|| ''
?}


\leg_kpozk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Legenda w oknie pozycji kalkulacji (tabela KPOZK)
::  OLD: \leg_kpozk/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','KPOZK#01#','#KPOZK#01')


\kpozk_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.46]
:: OPIS: Przywraca stan znacznika KPOZK.MANUAL:='N'
::  OLD: \kpozk_default/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get().args;
KPOZK.MANUAL:='N';
KPOZK.put();
_args.change:=1;
~~


\kpozk_przelicz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.46]
:: OPIS: Przelicza ponownie kalkulację
::  OLD: \kpozk_przelicz/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get().args;

KPOZK.cntx_psh();
{? exec('kalkuluj','!tte_tec_ktkw',0) || _args.change:=0 ?};
exec('kj_licz','tech_kalk');
KPOZK.cntx_pop();
~~


\kpozk_war_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.46]
:: OPIS: Po redakcji pola KPOZ.WAR
::  OLD: \kpozk_war_ae/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get().args;
KPOZK.MANUAL:='T';
_args.change:=1;
1


::======================================================================================================================
:: Obsługa tabeli KALMAT - ceny surowców pobrane do kalkulacji
::======================================================================================================================


\del_kalmat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.60]
:: OPIS: Kasuje surowce przy kalkulacji
::   WE: _a - KKTL.ref()
::  OLD: \del_kalmat/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KALMAT.index('KS');
KALMAT.prefix(_a);
{? KALMAT.first()
|| {! |? KALMAT.del() !}
?};
~~


\kalmat_display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [12.41]
:: OPIS: Akcja na wyświetl tabeli KALMAT okno WER
::  OLD: \kalmat_display/tex_kalk.fml
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? KALMAT.RTKTL<>''
|| exec('display_vars','tech_common',KALMAT.RTKTL)
?};
KALMAT.win_edit('RED');
KALMAT.display();
~~


\rkprz_kalmat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Akcja przed rekord w oknie KALMAT.WER
::  OLD: \rkprz_kalmat/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.REAL:=KALMAT.IL*KALMAT.PRICE;
{? KALMAT.POCH='T' || VAR.STRING:='Ceny własne'@
|? KALMAT.POCH='H' || VAR.STRING:='Wprowadzona ręcznie'@
|? KALMAT.POCH='K' || VAR.STRING:='Kalkulacja półfabrykatu'@
|? KALMAT.POCH='D' || VAR.STRING:='Dokumenty magazynowe'@
|? KALMAT.POCH='L' || VAR.STRING:='Ostatnia dostawa'@
|? KALMAT.POCH='R' || VAR.STRING:='Brak ceny'@
                   || VAR.STRING:=''
?};
~~


\KALMAT_trigger
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [12.41]
:: OPIS: Triggery dla tabeli KALMAT
::  OLD: \KALMAT/trigger.fml
::  TAG: <PRIVATE>
::----------------------------------------------------------------------------------------------------------------------
{? KALMAT.TKTL<>null()
|| KALMAT.RTKTL:=$KALMAT.TKTL
?};
1


\kalmat_trig_add_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Trigger przed add dla tabeli KALMAT
::----------------------------------------------------------------------------------------------------------------------
exec('KALMAT_trigger','!tte_tec_ktkw')


\kalmat_trig_put_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Trigger przed put dla tabeli KALMAT
::----------------------------------------------------------------------------------------------------------------------
exec('KALMAT_trigger','!tte_tec_ktkw')


::======================================================================================================================
:: Obsługa tabeli KALTTO - stawki operacji pobrane do kalkulacji
::======================================================================================================================


\del_kaltto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.60]
:: OPIS: Kasuje stawki operacji przy kalkulacji
::   WE: _a - KKTL.ref()
::  OLD: \del_kaltto/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KALTTO.index('KO');
KALTTO.prefix(_a);
{? KALTTO.first()
|| {! |? KALTTO.del() !}
?};
~~


\rkprz_kaltto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rekord przed w oknie KALTTO.WER
::----------------------------------------------------------------------------------------------------------------------
VAR.REAL:=KALTTO.IL*KALTTO.STAWKA;
~~


::======================================================================================================================
:: Obsługa tabeli KALWRK - stawki stanowisk pobrane do kalkulacji
::======================================================================================================================


\del_kalwrk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.60]
:: OPIS: Kasuje stawki stanowisk przy kalkulacji
::   WE: _a - KKTL.ref()
::  OLD: \del_kalwrk/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KALWRK.index('KP');
KALWRK.prefix(_a);
{? KALWRK.first()
|| {! |? KALWRK.del() !}
?};
~~


\rkprz_kalwrk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rekord przed w oknie KALWRK.WER
::----------------------------------------------------------------------------------------------------------------------
VAR.REAL:=KALWRK.IL*KALWRK.KH;
~~


::======================================================================================================================
:: Obsługa tabeli KALTLS - stawki stanowisk pobrane do kalkulacji
::======================================================================================================================


\del_kaltls
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.70] PR/WRT/PRODUKCJ/8.60/0012
:: OPIS: Kasuje ceny NPU przy kalkulacji
::   WE: _a - KKTL.ref()
::  OLD: \del_kaltls/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KALTLS.index('KT');
KALTLS.prefix(_a);
{? KALTLS.first()
|| {! |? KALTLS.del() !}
?};
~~


\rkprz_kaltls
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rekord przed w oknie KALTLS.WER
::----------------------------------------------------------------------------------------------------------------------
VAR.REAL:=KALTLS.IL*KALTLS.KOSZT;
~~


::======================================================================================================================
:: Obsługa tabeli KALTOU - stawki usług pobrane do kalkulacji
::======================================================================================================================


\del_kaltou
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.70]
:: OPIS: Kasuje stawki usług przy kalkulacji
::   WE: _a - KKTL.ref()
::  OLD: \del_kaltou/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
KALTOU.index('KT');
KALTOU.prefix(_a);
{? KALTOU.first()
|| {! |? KALTOU.del() !}
?};
~~


\rkprz_kaltou
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rekord przed w oknie KALTOU.WER
::----------------------------------------------------------------------------------------------------------------------
VAR.REAL:=KALTOU.IL*KALTOU.CENA;
~~


::======================================================================================================================
:: Obsługa tabeli KPAR - parametry do kalkulacji
::======================================================================================================================


\del_kpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: kasuje parametry do kalkulacji
::   WE: _a - KKTL.ref()
::  OLD: \del_kpar/tex_tpar.fml
::----------------------------------------------------------------------------------------------------------------------
KKTL.seek(_a);
KPAR.clear();
KPAR.index('KN');
KPAR.prefix(KKTL.ref());
{? KPAR.first()
|| {! |? KPAR.del() !}
?}


\kpar_changed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: sprawdza, czy kalkulacja zawiera aktualną listę parametrów
::       KONTEKST: KKTL.ref()
::  OLD: \kpar_changed/pkalk.fml
::----------------------------------------------------------------------------------------------------------------------
KPOZK.index('KR');
KPOZK.prefix(KKTL.ref());
:: nie była liczona, więc nie ma co sprawdzać
{? ~KPOZK.first() || return(0) ?};
TPAR.index('NN');
TPAR.prefix(KKTL.NRK);
_tpar_size:=TPAR.size();
KPAR.index('KN');
KPAR.prefix(KKTL.ref());
_kpar_size:=KPAR.size();
:: zmieniła się ilość parametrów
{? _kpar_size<>_tpar_size || return(1) ?};
:: czy zmieniła się numeracja parametrów
{? TPAR.first()
|| {!
   |? {? ~KPAR.find_key(TPAR.NRP) || return(1) ?};
      TPAR.next()
   !}
?};
{? KPAR.first()
|| {!
   |? {? ~TPAR.find_key(KPAR.NRP) || return(1) ?};
      KPAR.next()
   !}
?};
0


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Funkcja czyszcząca czynności - w razie potrzeby jak nie ma rekordu kluczowego zrobi done albo cancel
::       Dodatkowo może być wywoływana przez czynność czyszczącą zadania na TODO
::   WE: [_a] - _mp - obiekt Menadżera procesów
::       [_b] - tablica z parametrami wejściowymi
::   WY: obj_new() - obiekt wynikowy
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>100
|| _mp:=_a
|| _mp:=params_get().mp
?};
{? var_pres('_b')>100
|| _in:=_b
|| _in:=params_get().in
?};
exec('tech_clean','tech_common',_mp,_in,'')


\kalmat_fd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Format wyświetlenia dla pól tabeli KALMAT
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
_res:='';
{? _fld='IL'
|| _res:='out_prec='+$ST.DOKL
?};
_res

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 cc4adf177191a5b8a14d04831f374f6f3e8e1cd41fd46a209e4a05ee3714ac7aec30fee08d9b75ef46afe65ac9b421aafe6b7b63032a42aac6e654264cbb3d8e7fc2d79bb9d6f995a2fbec3a817519904038c4ef9e8a92f341cebd11ef6968beaf71eec9643b5b2e3843f427964a6eb5b826003e270bde97eed3e86fccfe139e

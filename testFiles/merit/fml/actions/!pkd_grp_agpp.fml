:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkd_grp_agpp.fml [22.26]
:: Utworzony: 26.11.2021
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły czynności PKD_GRP_AGPP - Grupy uprawnień współprac.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Grupy uprawnień współprac. - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('no_limit','gp_api','PKD')
|| return()
?};

_wnd:=P.mk_sel(P_FILTER.F_ZATR().OPIS,'P',0,'#p_grp_gp_def',,,,,'U');
:: widok
P.win_fld(_wnd,,'T',,,-9,,,,,MS.comment(P,'T'));
P.win_fld(_wnd,,'OSOBA','NAZWISKO',,-20,,,,,MS.comment(OSOBA,'NAZWISKO'));
P.win_fld(_wnd,,'OSOBA','PIERWSZE',,-12,,,,,MS.comment(OSOBA,'PIERWSZE'));
P.win_fld(_wnd,,'OSOBA','PESEL',,-11,,,,,MS.comment(OSOBA,'PESEL'));
P.win_fld(_wnd,,'WYDZIAL','SYMBOL',,-15,,,MS.name(P,'WYDZIAL'),,MS.comment(P,'WYDZIAL'));
P.win_fld(_wnd,,'ST','ST',,-20,,,MS.name(P,'ST'),,MS.comment(P,'ST'));
P.win_fld(_wnd,,'CP','CP',,-7,,,MS.name(P,'CP'),,MS.comment(P,'CP'));
P.win_fld(_wnd,,'ZA',,,-3,,,,,MS.comment(P,'ZA'),2,,"'T'","'N'");
P.win_fld(_wnd,,'GP_DEF','SYMBOL',,-8,,,'Grupa (symbol)'@,,MS.comment(GP_DEF,'SYMBOL'));
P.win_fld(_wnd,,'GP_DEF','OPIS',,-30,,,'Grupa (opis)'@,,MS.comment(GP_DEF,'OPIS'));
:: obsługa
P.win_act(_wnd,,'Formuła','Przypisz'@@,,'Przypisanie grupy uprawnień współpracownikowi'@,
   "exec('przypisz_b','!pkd_grp_agpp')",
   "exec('przypisz_a','!pkd_grp_agpp')",
   1,1,
   "exec('przypisz_gb','!pkd_grp_agpp')"
);
_txt:='Szukaj'@;
_fml:="exec('p_ustaw_ezk','szukaj')";
P.win_act(_wnd,,'Menu',_txt);
P.win_act(_wnd,,'Formuła','Szukaj dokładnie'@,_txt,,_fml,"exec('p_d','szukaj')");
P.win_act(_wnd,,'Formuła','Szukaj &kontekstowo'@,_txt,,_fml,"exec('p_k','szukaj')");
P.win_act(_wnd,,'#F3',,,,_fml,"exec('p_f3_wnd','szukaj')");
P.win_act(_wnd,,'Kolejność');
P.win_act(_wnd,,'Wyświetl',,,,"exec('p_bw','pracownik')");
P.win_act(_wnd,,'Rekord',,,,"exec('rekord_b','!pkd_grp_agpp',_a)");

GP_DEF.cntx_psh();
GP_DEF.win_sel('WYB');
P.cntx_psh();
P.clear();
P.win_sel(_wnd);

:: udostępnij listę współpracowników
exec('filtruj_p','schemat',
   'PKD',
   P_FILTER.UD_DEF,
   P_FILTER.F_ZATR().KOD,
   'T',
   P_FILTER.STATUS,,,
   1
);
P.trig_off('put');
P.select();
P.trig_on('put');

:: porządki
P.f_clear();
P.cntx_pop();
P.win_del(_wnd);
GP_DEF.f_clear();
GP_DEF.cntx_pop();
~~


\wybierz_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Wybór grupy ochron do przypisania.
::   WE:
::   WY: 0/1 - wybrano grupę (0-nie/1-tak)
::----------------------------------------------------------------------------------------------------------------------
exec('filtruj_def','gp_api','PKD');
GP_DEF.select()


\przypisz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed przypisaniem grupy ochron współpracownikowi.
::   WE:
::   WY: 0/1 - wykonanie przypisania grupy ochron (0-nie/1-tak)
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('p_gp_can_mod','pracownik',P.ref())
|| return(0)
?};

{? P.sel_size()=0
|| exec('wybierz_g','!pkd_grp_agpp')
|| 1
?}


\przypisz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przypisanie grupy współpracownikowi.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
P.GP_DEF:=GP_DEF.ref();
{? P.put()
|| exec('aktualizuj_p','gp_api',P.ref())
?};
~~


\przypisz_gb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Przed przypisaniem grupy ochron wybranym współpracownikom.
::   WE:
::   WY: 0/1 - wykonanie przypisania grupy ochron (0-nie/1-tak)
::----------------------------------------------------------------------------------------------------------------------
exec('wybierz_g','!pkd_grp_agpp')


\rekord_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Akcja "rekord przed" - blokowanie akcji.
::   WE: _a [INTEGER] - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _a=0 || return() ?};

_aid:='';
{? P.sel_size()=0 & ~exec('p_gp_can_mod','pracownik',P.ref())
|| _aid+='P'
?};

P.actions_grayed(cur_win(1,1),_aid);
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:52 1ed1d840feb5f5dabc64b13f5df8bfa6efd0c59a6307730e00c343482004892dc8e812feca602bb0a9ad7dfb8abf6908701fd1758d6c77ab1c2d791f3e58edad75efa3a81f58f1710342888cccbe182068e7978fe9f59ab092697c7cf3ef16b2187a4c6a88010a6d9ad90a15727d60d08557f4c758c77e55d1ab4a186c131627

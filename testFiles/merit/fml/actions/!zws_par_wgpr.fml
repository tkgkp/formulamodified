:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_wgpr.fml
:: Utworzony: 04.08.2017
:: Autor: ML
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_WGPR - Grupy pracowników.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ML [17.42]
:: OPIS: Grupy pracowników - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# permissions=F_ZATR,UD_SKL

PGRUPY.cntx_psh();
P.cntx_psh();
POZPG.cntx_psh();
P.clear();
ST.WYD:=__PARSES.getVal('WydzialProd').REF_UD_SKL;
_grp:=PGRUPY.grp_make('Grupy'@,,'pgrupy_sel',,,,,);
PGRUPY.grp_sel(_grp,,'WER',,"exec('gp_gf','!zws_par_wgpr')",,,15,,,,,'maximized_with_title');
PGRUPY.grp_splt(_grp,,'vertical','zlbs');
PGRUPY.grp_sel(_grp,POZPG,'WER',,,,,,"
      {? grp_empty(PGRUPY,'WER')
      || '#disable'
      || _grayed:={? exec('blk_lock','#table','PGRUPY',PGRUPY.ref()) || '' || 'Dpu:D' ?};
         POZPG.actions_grayed('WER',_grayed);
         ~~
      ?}
   ","exec('blk_unlock','#table','PGRUPY',PGRUPY.ref())",,,'maximized_with_title');

PGRUPY.win_sel(_grp);
PGRUPY.actions('WER');
POZPG.fld_attr(,2);

PGRUPY.select();
POZPG.fld_attr(,1);

POZPG.cntx_pop();
PGRUPY.cntx_pop();
P.cntx_pop();
~~


\gp_gf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ML [17.42]
:: OPIS: Po odswiezeniu PGRUPY wypelnienie okna definicji skladu grupy pracownikow
::  OLD: \gp_gf/wypdefin.fml
::----------------------------------------------------------------------------------------------------------------------
POZPG.index('DANE'); POZPG.prefix(PGRUPY.ref());
POZPG.hdr_sel(); POZPG.hdr_sel(' grupy '@+PGRUPY.SYM); POZPG.first();
{? PGRUPY.size=0
|| POZPG.actions('WER','D:D',,1)
|| POZPG.actions('WER','',,1)
?};
grp_disp(POZPG,'WER')


\pg_da
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ML [17.42]
:: OPIS: Dolaczenie pracownikow do grupy
::       Wyswietla liste pracownikow, ktorzy jeszcze nie naleza do zadnej grupy
::       jesli uzytkownik zaznaczy grupe rekordow, to dopisuje wszystkich zaznaczonych,
::       jesli nie zaznaczy to dopisuje tylko biezacego
::  OLD: \pg_da/wypdefin.fml
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.clear();
_p6520:=exec('get','#params',6520,2,OPERATOR.USER);
_sql:=sql('select distinct P.REFERENCE as P from POZPG join P using (POZPG.P,P.REFERENCE)');
_lista:='';
{? _sql.first()
|| {!|?
      _lista+='('''+_sql.P+'''),';
     _sql.next()
   !}
?};
_lista:=_lista-1;

{? _lista<>''
|| _where:='P.REFERENCE NOT in VALUES'+_lista
|| _where:=''
?};
_view:=1+_p6520;
_jorg:=_p6520+1;
{? _ret:=exec('wybierz_pracownika','wyp',0,_view,_jorg,,_where);
   {? _ret.STATUS=''
   || {? _ret.P.first() || _res:=exec('FindAndGet','#table',P,_ret.P.UID,,"ref()",null()) || _res:=null() ?}
   || FUN.emsg(_ret.STATUS);
      _res:=null()
   ?};
   _res<>null()

|| POZPG.blank();
   POZPG.PGRUPY:=PGRUPY.ref();
   POZPG.P:=_res;
  {? ~POZPG.add(1)
  ||
     FUN.error('Pracownik '@
         +POZPG.P().OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE+'\n'
         +' jest już dopisany do jakiejś grupy.'@)
  ?};
  POZPG.first()

?};
P.cntx_pop();
~~


\def_n_gp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ML [17.42]
:: OPIS: Wyswietla liste pracownikow, ktorzy jeszcze nie naleza do zadnej grupy pracownikow
::  OLD: \def_n_gp/wypdefin.fml
::----------------------------------------------------------------------------------------------------------------------
P.cntx_psh();
P.clear();
_p6520:=exec('get','#params',6520,2,OPERATOR.USER);
_sql:=sql('select distinct P.REFERENCE as P from POZPG join P using (POZPG.P,P.REFERENCE)');
_lista:='';
{? _sql.first()
|| {!|?
      _lista+='('''+_sql.P+'''),';
     _sql.next()
   !}
?};
_lista:=_lista-1;
{? _lista=''
|| _where:=''
|| _where:='P.REFERENCE NOT in VALUES'+_lista
?};
_view:=1+_p6520;
_jorg:=_p6520+1;
exec('filtruj_p','wyp',1+_p6520,_p6520+1,_where);

P.win_sel('WYP');
P.win_edit();
P.actions('WYP','NRZ:Z',,1);
P.fld_attr(,2);

P.select();
P.fld_attr(,1);

P.cntx_pop();
~~


\def_c_gp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ML [17.42]
:: OPIS: Oczyszczenie grup z pracownikow nieaktywnych
::  OLD: \def_c_gp/wypdefin.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć pracowników niezatrudnionych z wszystkich grup pracowników?'@)
||
   POZPG.cntx_psh();
   POZPG.clear();
   POZPG.for_each("{? POZPG.P().ZA='N' || POZPG.del() ?}");
   POZPG.cntx_pop()
?};
~~


\prn_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Wydruki dla grup pracownikow
::----------------------------------------------------------------------------------------------------------------------
:rep_exec('wyp_pgp*')
exec('rep_exec','#b_report','ZWS_PAR_WGPR','wyp_pgp*',,1)


\pgr_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ML [17.42]
:: OPIS: Usunięcie Grupy pracowników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pgrupa_usun','normatyw')

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 6fa31864eba3315cceedc07946998ce0944ba7c96e07ef879d37439bf5911e424ba93b02f8c1ec1f3e2c66764159b6b35f6fdd0b2ad60c31d4ab7d684dba2778818d480c8f6a358292d9e29fbfee760af5a99a251558c11a7e8589b4259097faf84a26f8c1f2f3a218a3ebd87b7e9d674b1cad3a3aee9d4a227f69b27bc9f918

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_krap.fml
:: Utworzony: 28.08.2017
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły czynności definiowania raportu kontroli dla wyłączeń
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Definiowanie raportu kontroli dla wyłączeń - formuła główna
::----------------------------------------------------------------------------------------------------------------------
::# properties=GRP_FIRM
_grp:=KON_NRAP.grp_make('Definicje raportu kontroli dla wyłączeń'@);
KON_NRAP.grp_sel(_grp,,'WER',,"
   exec('prfx','!zws_par_krap');
   grp_disp(KON_PRAP,'WER',1)
");
KON_NRAP.grp_splt(_grp,,'horizontal','bottom',10);
KON_NRAP.grp_sel(_grp,KON_PRAP,'WER','Pionowa'@,,,,,"exec('prfx','!zws_par_krap','N');~~");
KON_NRAP.grp_sel(_grp,KON_PRAP,'WER','Pozioma'@,,,,,"exec('prfx','!zws_par_krap','Z');~~");
KON_NRAP.win_sel(_grp);
KON_NRAP.actions('WER','Z');
KON_NRAP.cntx_psh();
KON_NRAP.index('SYM'); KON_NRAP.prefix();
KON_NRAP.select();
KON_NRAP.cntx_pop();
~~


\bl_kon_nrap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Wartość początkowa pola KON_PRAP.KON_NRAP
::----------------------------------------------------------------------------------------------------------------------
KON_NRAP.ref()


\bl_kon_prap_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Wartość początkowa pola KON_PRAP.TYP
::----------------------------------------------------------------------------------------------------------------------
ROZNE.TYPKPOZ


\prfx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Ustawia dziedzinę pozycji
::   WE: [_a] - typ definicji
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')>0 || ROZNE.TYPKPOZ:=_a ?};
KON_PRAP.index('KON_PRAP');
{? KON_NRAP.get()
|| KON_PRAP.prefix(KON_NRAP.ref(),ROZNE.TYPKPOZ);
   KON_PRAP.actions('WER',,,1)
|| KON_PRAP.prefix(null);
   KON_PRAP.actions('WER','D:D',,1)
?};
~~


\be_kon_prap_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Przed redakcją KON_PRAP.GR_SIK
::----------------------------------------------------------------------------------------------------------------------
__F_ref:='S';
REF.GRUPA();
GR_SIK.f_clear();
1


\ae_kon_prap_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Po redakcji pola KON_PRAP.GR_SIK
::----------------------------------------------------------------------------------------------------------------------
WYRAZ.GRUPA:=KON_PRAP.GR_SIK;
{? WYRAZ.GRUPA<>KON_PRAP.GR_KOL().GRUPA || KON_PRAP.GR_KOL:=null ?};
{? WYRAZ.GRUPA<>KON_PRAP.DEFW().GRUPA || KON_PRAP.DEFW:=null ?};
_enabled:=WYRAZ.GRUPA<>null;
KON_PRAP.efld_opt('RED','enable='+$_enabled,KON_PRAP,'GR_KOL');
KON_PRAP.efld_opt('RED','enable='+$_enabled,KON_PRAP,'DEFW');
_mark:=1;
KON_PRAP.efld_opt('RED','mark='+$_mark,KON_PRAP,'GR_SIK');
KON_PRAP.efld_opt('RED','mark='+$_mark,KON_PRAP,'GR_KOL');
KON_PRAP.efld_opt('RED','mark='+$_mark,KON_PRAP,'DEFW');
1


\ar_kon_nrap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Rekord po tabeli KON_NRAP
::----------------------------------------------------------------------------------------------------------------------
_ret:=__CHK.record(KON_NRAP,,'SYM');
{? _ret=''
|| _ref:=KON_NRAP.ref();
   KON_NRAP.cntx_psh();
   KON_NRAP.index('SYM'); KON_NRAP.prefix(KON_NRAP.SYM,);
   {? KON_NRAP.first() & (-menu_txt()<>'popraw' | KON_NRAP.ref()<>_ref)
   || FUN.info('Istnieje definicja o symbolu: %1.'@[KON_NRAP.SYM]);
      _ret:='SYM'
   ?};
   KON_NRAP.cntx_pop()
?};
_ret


\ar_kon_prap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Rekord po tabeli KON_PRAP
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(KON_PRAP,,'GR_SIK','GR_KOL','DEFW')


\bd_kon_nrap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Przed akcją usuń okna WER tabeli KON_NRAP
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć definicję i jej pozycje?'@)
|| {? exec('kon_nrap_lock','!zws_par_krap',1)
   || KON_PRAP.index('KON_PRAP');
      KON_PRAP.prefix(KON_NRAP.ref());
      {? KON_PRAP.first() || {! |? KON_PRAP.del !} ?};
      {? ~KON_NRAP.del(,1)
      || exec('kon_nrap_lock','!zws_par_krap',0)
      ?}
   ?}
?}


\kon_prap_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Akcja Pozycje okna WER tabeli KON_NRAP
::----------------------------------------------------------------------------------------------------------------------
_grp:=KON_PRAP.grp_make('Pozycje definicji raportu kontroli dla wyłączeń'@);
KON_PRAP.grp_sel(_grp,,'WER','Pionowa'@,,,,,"exec('prfx','!zws_par_krap','N');~~");
KON_PRAP.grp_sel(_grp,,'WER','Pozioma'@,,,,,"exec('prfx','!zws_par_krap','Z');~~");
KON_PRAP.win_sel(_grp);
KON_PRAP.select()


\bm_kon_prap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Przed popraw tabeli KON_PRAP
::----------------------------------------------------------------------------------------------------------------------
{? ~KON_PRAP.r_lock(1,1,1)
|| FUN.info('Pozycja raportu jest obsługiwana przez innego użytkownika.'@)
|| {? KON_PRAP.edit("exec('ar_kon_prap','!zws_par_krap')")
   || KON_PRAP.put()
   ?};
   KON_PRAP.r_unlock()
?}


\bd_kon_prap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Przed usuń tabeli KON_PRAP
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć pozycję raportu?'@)
|| {? ~KON_PRAP.r_lock(1,1,1)
   || FUN.info('Pozycja raportu jest obsługiwana przez innego użytkownika.'@)
   || {? ~KON_PRAP.del(,1)
      || KON_PRAP.r_unlock()
      ?}
   ?}
?}


\bm_kon_nrap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Przed popraw okna KON_NRAP
::----------------------------------------------------------------------------------------------------------------------
{? ~KON_NRAP.r_lock(1,1,1)
|| FUN.info('Raport jest obsługiwana przez innego użytkownika.'@)
|| {? KON_NRAP.edit("exec('ar_kon_nrap','!zws_par_krap')")
   || KON_NRAP.put()
   ?};
   KON_NRAP.r_unlock()
?}


\kon_nrap_lock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Blokowanie/odblokowanie raportu
::   WE: _a - 1-blokowanie, 0-odblokowywanie
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? ~KON_NRAP.r_lock(1,1,1)
   || FUN.info('Raport jest obsługiwany przez innego użytkownika.'@);
      0
   || _ok:=1;
      KON_PRAP.cntx_psh();
      KON_PRAP.index('KON_PRAP');
      KON_PRAP.prefix(KON_NRAP.ref());
      {? KON_PRAP.first()
      || {!
         |? _ok:=KON_PRAP.r_lock(1,1,1);
            _ok & KON_PRAP.next()
         !}
      ?};
      {? ~_ok
      || {? KON_PRAP.first()
         || {!
            |? KON_PRAP.r_unlock();
               KON_PRAP.next()
            !}
         ?};
         KON_NRAP.r_unlock()
      ?};
      KON_PRAP.cntx_pop();
      {? ~_ok
      || FUN.info('Pozycje raportu są obsługiwane przez innego użytkownika.'@)
      ?};
      _ok
   ?}
|| KON_PRAP.cntx_psh();
   KON_PRAP.index('KON_PRAP');
   KON_PRAP.prefix(KON_NRAP.ref());
   {? KON_PRAP.first()
   || {!
      |? KON_PRAP.r_unlock();
         KON_PRAP.next()
      !}
   ?};
   KON_PRAP.cntx_pop();
   KON_NRAP.r_unlock()
?}

:Sign Version 2.0 jowisz:1028 2019/06/07 15:56:04 cf815f6d5976f3bd1a9635ede3f98b26b2867c6975895df238f5d5aebc5b1d41679dd3ed8730056188338434f8e29e635844d2c25151acf342974681d44ace1ab1224b1bd1da48887e42abf93040b7589f1374a1c91f596939d4b7f6686b052d7ea48d4a9b098f7d0f46ea0950c81fdbdb4de7856820a9961725fb9820bb04c0

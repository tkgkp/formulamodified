:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_trbr.fml
:: Utworzony: 26.04.2022
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Formuły czynności ZWS_PAR_TRBR - Rodzaje braków
::            Obsługa tabeli: BRAKI_R
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Formuła główna czynności parametryzacyjnej (nieprocesowej) - rodzaje braków
::----------------------------------------------------------------------------------------------------------------------
BRAKI_R.cntx_psh();
BRAKI_R.win_sel('WER');
BRAKI_R.index('KOD');
BRAKI_R.prefix();
BRAKI_R.select();
BRAKI_R.cntx_pop();
~~


\braki_r_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: BRAKI_R - rekord po w oknie WER
::----------------------------------------------------------------------------------------------------------------------
_put:=-menu_txt()='popraw';
exec('braki_r_chk','braki',_put)


\braki_r_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: BRAKI_R - akcja Usuń w oknie WER
::----------------------------------------------------------------------------------------------------------------------
_count:=BRAKI_R.count();
_ilro:=0;
BRAKI_RO.cntx_psh();
BRAKI_RO.index('BRAKI_R');
BRAKI_RO.prefix(BRAKI_R.ref());
{? BRAKI_RO.first()
|| _ilro:=BRAKI_RO.size()
?};
{? _ilro=_count
|| {? FUN.ask('Czy usunąć wybrany rekord?'@)
   ||
      do();
      {? _ilro>0
      || {!
         |?
            BRAKI_RO.del()
         !}
      ?};
      BRAKI_R.del();
      end()
   ?}
|| FUN.info('Rodzaj braku wykorzystany w systemie.\nUsunięcie niemożliwe.'@)
?};
BRAKI_RO.cntx_pop();
~~


\braki_r_oper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: BRAKI_R - akcja Operacje w oknie WER (wyświetla listę powiązanych operacji)
::----------------------------------------------------------------------------------------------------------------------
TTOPER.cntx_psh();
TTOUT.cntx_psh();
BRAKI_RO.cntx_psh();
::BRAKI_RO.index('BRAKI_R');
::BRAKI_RO.prefix(BRAKI_R.ref());
exec('oper_prep_win','!zws_par_trbr');
VAR.GRUPA:='N';
BRAKI_RO.select();
BRAKI_RO.cntx_pop();
TTOPER.cntx_pop();
TTOUT.cntx_pop();
~~


\oper_prep_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Przygotowuje okna selekcji dla tabel BRAKI_RO, TTOUT i TTOPER
::----------------------------------------------------------------------------------------------------------------------
_grp:=BRAKI_RO.grp_make('Operacje do rodzaju braku'@,,,,,,,'normal');
_before:="
   VAR.BRAKI_TO:='T';
   BRAKI_RO.f_set('TTOPER(KOD)',,'BRAKI_R=:_a and TTOPER is not NULL',BRAKI_R.ref());
   ~~
";
_ar:="
   ~~
";
BRAKI_RO.grp_sel(_grp,,'WER_OPER','Operacje'@,_ar,,,,_before,,,,'maximized','grp_brakiro_o');
_before:="
   VAR.BRAKI_TO:='N';
   BRAKI_RO.f_set('TTOUT(KOD)',,'BRAKI_R=:_a and TTOUT is not NULL',BRAKI_R.ref());
   ~~
";
_ar:="
   ~~
";
BRAKI_RO.grp_sel(_grp,,'WER_KOOP','Usługi (kooperacje)'@,_ar,,,,_before,,,,'maximized','grp_brakiro_k');
BRAKI_RO.win_sel(_grp);

:: Okna selekcji TTOUT, TTOPER
_win:=TTOUT.mk_sel('Taryfikator usług (w kooperacji)'@,'P',,'ttout_brakiro',1,1,,,'U','T');
TTOUT.win_fld(_win,TTOUT,'KOD',,,10,0,0);
TTOUT.win_fld(_win,TTOUT,'NA',,,50,0,0);
TTOUT.win_fld(_win,TTOUT,'CENA',,,16,2,0,);
TTOUT.win_act(_win,1,'Dołącz');
task_attach('ZWS_PAR_TTAR');
TTOUT.win_act(_win,1,'Rekord',,,,,"exec('sprttout','!zws_par_ttar')");
TTOUT.win_act(_win,0,'Formuła','&Wybierz'@@,,,"params_exec('add_selected_oper','!zws_par_trbr')"
   ,"exec('sel_exit_','#window')",1,1,"",,'W',0,'target=window');
TTOUT.win_act(_win,0,'Dołącz');
task_attach('ZWS_PAR_TTAR');
TTOUT.win_act(_win,0,'Popraw');
task_attach('ZWS_PAR_TTAR');
TTOUT.win_act(_win,0,'Formuła','Usuń'@@,,,"exec('usun_ttout','!zws_par_ttar')",,,,,,'U');
task_attach('ZWS_PAR_TTAR');
TTOUT.win_act(_win,0,'Szukaj',,,,"exec('ttout_szukaj','!zws_par_ttar')");
TTOUT.win_act(_win,0,'Kolejność');
TTOUT.win_sel(_win);

_win:=TTOPER.mk_sel('Taryfikator operacji'@,'P',0,'ttoper_brakiro',1,1,,0,'U','T');
TTOPER.win_fld(_win,TTOPER,'KOD',,,10,0,1,'',0,'Kod operacji'@);
TTOPER.win_fld(_win,TTOPER,'NA',,,50,0,0,'',0,'Nazwa operacji procesu technologicznego'@);
TTOPER.win_fld(_win,TTOPER,'STAWKA',,,15,4,0,'Stawka godzinowa'@@,0,'Stawka godzinowa operacji'@);
TTOPER.win_act(_win,1,'Dołącz',,,,"exec('ttoper_add','!zws_par_ttar')");
task_attach('ZWS_PAR_TTAR');
TTOPER.win_act(_win,1,'Rekord',,,,,"exec('chk_ttoper','!zws_par_ttar')");
TTOPER.win_act(_win,0,'Formuła','&Wybierz'@@,'','',"params_exec('add_selected_oper','!zws_par_trbr')",
   "exec('sel_exit_','#window')",1,1,"","",'W',0,'mobile=1,target=window');
TTOPER.win_act(_win,0,'Dołącz',,,,"exec('ttoper_add','!zws_par_ttar')");
task_attach('ZWS_PAR_TTAR');
TTOPER.win_act(_win,0,'Popraw',,,,"exec('ttoper_modify','!zws_par_ttar')");
task_attach('ZWS_PAR_TTAR');
TTOPER.win_act(_win,0,'Formuła','Usuń'@@,,,"exec('usun_ttoper','!zws_par_ttar')",,,,,,'U');
task_attach('ZWS_PAR_TTAR');
TTOPER.win_act(_win,0,'Szukaj','','','',"exec('ttoper_szukaj','!zws_par_ttar')");
TTOPER.win_act(_win,0,'Kolejność');
TTOPER.win_act(_win,0,'Rekord','','','',"","exec('chk_ttoper','!zws_par_ttar')");
TTOPER.win_act(_win,0,'Wyświetl','','','',"exec('ttoper_display','!zws_par_ttar')");
TTOPER.win_sel(_win);
~~


\braki_r_oper_grb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Grupa przed dla akcji Operacje w oknie WER
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Jakiego typu operacje przypisać do zaznaczonych rodzajów braków?'@,,'Wewnętrzne'@,'Zewnętrzne'@);
{? _choice>0
|| exec('oper_prep_win','!zws_par_trbr');
   {? _choice=1
   || VAR.BRAKI_TO:='T'
   || VAR.BRAKI_TO:='N'
   ?};
   VAR.GRUPA:='T';
   exec('braki_ro_add','!zws_par_trbr');
   BRAKI_R.sel_adel();
   VAR.GRUPA:='N';
   ''
?};
0


\braki_r_oper_gra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Grupa po dla akcji Operacje w oknie WER
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~


\braki_ro_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Walidacja dla tabeli BRAKI_RO
::   WE: _a - add/put: [0] - dołącz , 1 - popraw
::       _b - czy wyświetlać komunikaty: 0 - nie, [1] - tak
::   WY: akronim błędnie wypełnionego pola lub '' w przypadku pomyślnej walidacji
::----------------------------------------------------------------------------------------------------------------------
_put:=-menu_txt()='popraw';
exec('braki_ro_chk','braki',_put)


\braki_ro_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: BRAKI_RO - akcja Dołącz w oknach WER_OPER, WER_KOOP
::----------------------------------------------------------------------------------------------------------------------
_ref:=BRAKI_R.ref();
_tab:={? VAR.BRAKI_TO='T' || TTOPER || TTOUT ?};
_tab.cntx_psh();
_tab.clear();
{? VAR.GRUPA='T'
|| _where:=''
|| _where:='KOD not in (select :_a.KOD from BRAKI_RO join BRAKI_R join :_a where BRAKI_R.REFERENCE=\':_b\')'
?};
_tab.f_set(,,_where,_tab,$_ref);
_ref_table:=exec('ref_table','#table');
params_set('ref_table',_ref_table);
{? _tab.select()
|| _ref_tab:=_ref_table.tab;
   {? _ref_tab.first()
   || BRAKI_R.cntx_psh();
      BRAKI_R.clear();
      _tab_sel:=BRAKI_R.sel_aget();
      _first:={? VAR.GRUPA='T' || _tab_sel.first() || 1 ?};

      {? _first>0
      ||
::       Pętla po rodzajach braków (dla akcji grupowej)
         {!
         |?
            {? {? VAR.GRUPA='T' || BRAKI_R.seek(_tab_sel.REF) || 1 ?}
            ||
               _ref_tab.first();
::             Pętla po wybranych operacjach
               {!
               |?
                  {? _tab.f_seek(_ref_tab.SQL)
                  || BRAKI_RO.blank();
                     BRAKI_RO.BRAKI_R:=BRAKI_R.ref();
                     {? _tab=TTOPER
                     || BRAKI_RO.TTOPER:=TTOPER.ref()
                     || BRAKI_RO.TTOUT:=TTOUT.ref()
                     ?};
                     {? exec('braki_ro_chk','braki',0,0)=''
                     || BRAKI_RO.add()
                     ?}
                  ?};
                  _ref_tab.next()
               !}
            ?};
            _loop:={? VAR.GRUPA='T' || _tab_sel.next() || 0 ?};
            _loop
         !}
      ?};
      BRAKI_R.cntx_pop();
::    Odświeżenie okna w grupie
      {? VAR.GRUPA<>'T'
      || {? VAR.BRAKI_TO='T'
         || grp_disp(BRAKI_RO,'WER_OPER')
         || grp_disp(BRAKI_RO,'WER_KOOP')
         ?}
      ?}
   ?}
?};
{? _tab.f_active() || _tab.f_clear(1) ?};
_tab.cntx_pop();
~~


\braki_ro_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: BRAKI_RO - akcja Usuń w oknach WER_OPER, WER_KOOP
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRUPA='T' | FUN.ask('Czy usunąć wybrany rekord?'@)
|| BRAKI_RO.del()
?};
~~


\braki_ro_del_grb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: BRAKI_RO - Grupa przed dla akcji Usuń w oknach WER_OPER, WER_KOOP
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy na pewno usunąć wybrane rekordy?'@)
|| VAR.GRUPA:='T'
?}


\braki_ro_del_gra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: BRAKI_RO - Grupa po dla akcji Usuń w oknach WER_OPER, WER_KOOP
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~


\add_selected_oper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Dodanie operacji do tabeli refów (wybór operacji dla rodzaju braku - w szczególności dla akcji grupowej)
::----------------------------------------------------------------------------------------------------------------------
_ref_table:=params_get().ref_table;
{? VAR.BRAKI_TO='T'
|| _ref_table.add(TTOPER.ref())
|| _ref_table.add(TTOUT.ref())
?};
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:56 c8e8790b7e5693d18677110de9b85a376130b5c677015f265dcbee3ec8f19030d2a83f54e973bc2ed7a2daa119aebdf59b5f668e24e1c8e480048b522307178e736a42ced9f705c9c9457a10f931bbfe2b7db59148a1aff8005a412dfb5609938407b415cd5fc09f6348174249344bd98e777590c115f003a699b78c208a79c5

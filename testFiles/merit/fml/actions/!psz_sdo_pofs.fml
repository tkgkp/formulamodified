:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !psz_sdo_pofs.fml
:: Utworzony: 27.09.2017
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Obsługa czynności PSZ_SDO_POFS - Oferty szkoleniowe.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Oferty szkoleniowe - główna formuła czynności.
::----------------------------------------------------------------------------------------------------------------------
_wnd:=exec('create_wnd','!psz_sdo_pofs');

_ctx:=obj_new('OFE_1','OFE_2');
_ctx.OFE_1:=obj_new('KEY','REF');
_ctx.OFE_2:=obj_new('KEY','REF');
params_set('ctx',_ctx);

_org:=exec('save_fml_all','#field',SZK_OFE);

SZK_ORG.cntx_psh();
SZK_ORG.win_sel(_wnd);
SZK_ORG.select();
SZK_ORG.cntx_pop();
SZK_ORG.win_del(_wnd);

exec('restore_fml_all','#field',SZK_OFE,_org);
~~


\create_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Tworzy okienko czynności.
::   WE:
::   WY: akronim okienka tymczasowego
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';

_wnd:=SZK_ORG.grp_make('Oferty szkoleniowe'@,
:  po wyświetleniu
   "  _par:=params_get();
      params_set(_par);
      _ctx:=_par.ctx;
      _ctx.OFE_1.KEY:='SZK_WZO';
      _ctx.OFE_1.REF:=null;
      _ctx.OFE_2.KEY:='SZK_ORG';
      _ctx.OFE_2.REF:=null;

      SZK_TREN.cntx_psh();
      SZK_TREN.f_clear();
      SZK_TREN.prefix();
      SZK_TREN.win_dict('WER');
      SZK_ORG.cntx_psh();
      SZK_ORG.f_clear();
      SZK_ORG.index('SZK_ORG');
      SZK_ORG.prefix();
      SZK_ORG.win_dict('SLO');
      SZK_WZO.cntx_psh();
      SZK_WZO.f_clear();
      SZK_WZO.index('OPIS');
      SZK_WZO.prefix();
      SZK_WZO.win_dict('WER');
      SZK_OFE.cntx_psh();
      grp_disp(SZK_ORG,'PSZ_OC',1);
      1
   ",
:  identyfikator
   'psz_sdo_posf',
:  położenie
   0,0,
:  zamknięcie
   "  SZK_OFE.efld_opt('RED','mark=1',,'SZK_ORG','NAZWA');
      SZK_OFE.efld_opt('RED','mark=1',,'SZK_WZO','OPIS');

      SZK_TREN.cntx_pop();
      SZK_OFE.cntx_pop();
      SZK_WZO.cntx_pop();
      SZK_ORG.cntx_pop();
      1
   "
);

: lista organizatorow szkoleń
SZK_ORG.grp_sel(_wnd,SZK_ORG,'PSZ_OC','Wg organizatorów'@,
:  po odświeżeniu
   "  params_set(params_get());
      grp_disp(SZK_OFE,'WER_WZO',1)
   ",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);
SZK_ORG.tab_splt(_wnd,,'horizontal','bottom1');

: oferta wskazywanego organizatora
SZK_ORG.grp_sel(_wnd,SZK_OFE,'WER_WZO',,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "  {? grp_empty(SZK_ORG,'PSZ_OC')=1
      || return('#disable')
      ?};
      {? _a
      || SZK_OFE.fld_fml('SZK_ORG','BLANK',$'SZK_ORG.ref()');
         SZK_OFE.fld_fml('SZK_ORG','BEFORE_EDIT',$'0');
         SZK_OFE.efld_opt('RED','mark=0',,'SZK_ORG','NAZWA');
         SZK_OFE.fld_fml('SZK_WZO','BLANK',$'null');
         SZK_OFE.fld_fml('SZK_WZO','BEFORE_EDIT',$'1');
         SZK_OFE.efld_opt('RED','mark=1',,'SZK_WZO','OPIS')
      ?};
      _ctx:=params_get().ctx.OFE_1;
      SZK_OFE.index(_ctx.KEY);
      SZK_OFE.prefix(SZK_ORG.ref());
      SZK_OFE.seek(_ctx.REF)
   ",
:  po obsłudze
   "  _ctx:=params_get().ctx.OFE_1;
      _ctx.REF:=SZK_OFE.ref();
      {? _a
      || _ctx.KEY:=SZK_OFE.index('?')
      ?}
   ",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

:  lista wzorców szkoleń
SZK_ORG.grp_sel(_wnd,SZK_WZO,'WER','Wg wzorców'@,
:  po odświeżeniu
   "  params_set(params_get());
      grp_disp(SZK_OFE,'WER_ORG',1)
   ",
:  położenie i wysokość
   ,,15,
:  przed obsługą
   "",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);
SZK_ORG.tab_splt(_wnd,,'horizontal','bottom2');

:  oferty dla wskazywanego wzorca
SZK_ORG.grp_sel(_wnd,SZK_OFE,'WER_ORG',,
:  po odświeżeniu
   "",
:  położenie i wysokość
   ,,,
:  przed obsługą
   "  {? grp_empty(SZK_WZO,'WER')=1
      || return('#disable')
      ?};
      {? _a
      || SZK_OFE.fld_fml('SZK_ORG','BLANK',$'null');
         SZK_OFE.fld_fml('SZK_ORG','BEFORE_EDIT',$'1');
         SZK_OFE.efld_opt('RED','mark=1',,'SZK_ORG','NAZWA');
         SZK_OFE.fld_fml('SZK_WZO','BLANK',$'SZK_WZO.ref()');
         SZK_OFE.fld_fml('SZK_WZO','BEFORE_EDIT',$'0');
         SZK_OFE.efld_opt('RED','mark=0',,'SZK_WZO','OPIS')
      ?};
      _ctx:=params_get().ctx.OFE_2;
      SZK_OFE.index(_ctx.KEY);
      SZK_OFE.prefix(SZK_WZO.ref());
      SZK_OFE.seek(_ctx.REF)
   ",
:  po obsłudze
   "  _ctx:=params_get().ctx.OFE_2;
      _ctx.REF:=SZK_OFE.ref();
      {? _a
      || _ctx.KEY:=SZK_OFE.index('?')
      ?}
   ",
:  utrwalenie, aktywacja, wypełnienie
   0,0,_mode
);

_wnd


\szk_ofe_xxx_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła akcji "Okienko przed" okienek tabeli SZK_OFE.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_psh','phr_widok','PSZ_SDO_POFS');

_wnd:=cur_win(1,1);
{? _wnd='WER_ORG' || exec('setup_icon','phr_widok','SZK_ORG','NAZWA')
|? _wnd='WER_WZO' || exec('setup_icon','phr_widok','SZK_WZO','OPIS')
?};

SZK_OFE.win_edit('RED');
SZK_OFE.win_patt('WZO');

1


\szk_ofe_xxx_oa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Formuła akcji "Okienko po" okienek tabeli SZK_OFE.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_pop','phr_widok');
1


\szk_ofe_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Po edycji wiersza tabeli SZK_OFE.
::   WE:
::   WY: akronim niewypełnionego pola, 0 w przypadku powielenia klucza, tekst pusty jeśli rekord jest poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('szk_ofe_chk','phr_sz_tab',-menu_txt()='popraw')

:Sign Version 2.0 jowisz:1028 2019/06/07 15:55:59 67f457c7d716d8e9dfd5335bd48e4504d45c42cc8ff7bcdc40d3f88174a516445c609465a5c0d39161e66fb24f70fc4bcd27e6987aa6f4f73142807965f0519b4d0e6d94b4671dfdbdb8547e8120a73e12e3cf54630692dd199abeaf93a053c06cce36b363b8193424eef7b5be541a1d71961d99df1b6b37cf76d59e9bf4213d

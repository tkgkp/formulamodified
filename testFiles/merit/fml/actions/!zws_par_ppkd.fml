:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zws_par_ppkd.fml
:: Utworzony: 26.11.2015
:: Autor: PT
::======================================================================================================================
:: Zawartość:
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Edycja stałych obszaru Kadry - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_data:=exec('od_daty','#stalesys');
{? _data.OK<>1
|| return()
?};

_klasa:=VAR.KLASA;
_wnd:='PKD';
KST.cntx_psh();
KST.win_edit(_wnd);
KST.hdr_edit('Stałe dziedziny kadry na dzień %1'@[$_data.OD]);
:exec('kst_efld_opt','!zws_par_ppkd','*',KST,_wnd);

:: ustawienie dynamicznych formuł przed i po redagowaniu
POLA_GRP.TXT_1:=POLA_GRP.TXT_2:=POLA_GRP.TXT_3:=POLA_GRP.TXT_4:='';
KST_PAR.fld_fml('EZLAMAIL','AFTER_EDIT',"exec('ezlamail_ae','stalesys')");
POLA_GRP.fld_fml('TXT_1','BEFORE_EDIT',"exec('ezlapass_be','stalesys')");
POLA_GRP.fld_fml('TXT_1','AFTER_EDIT',"exec('ezlapass_ae','stalesys')");
POLA_GRP.fld_fml('TXT_2','BEFORE_EDIT',"exec('ezlapass_be','stalesys')");
POLA_GRP.fld_fml('TXT_2','AFTER_EDIT',"exec('ezlapass_ae','stalesys')");

exec('czytaj','#stalesys',_data.OD,KST,KST_PAR);
exec('kst_efld_opt','!zws_par_ppkd','*',KST,_wnd);
{? KST.edit("exec('kst_ae','!zws_par_ppkd')")
|| exec('zapisz','#stalesys',_data.OD,KST,KST_PAR)
?};

KST.cntx_pop();
VAR.KLASA:=_klasa;
~~


\kst_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PT [17.00]
:: OPIS: Formuła odpowiedzialna za dynamiczne ustawianie właściwości pól zmiennej KST. Formuła wywoływana jest w dwóch
::       kontekstach pracy:
::          - Po redagowaniu konkretnego pola, które determinuje właściwości wyświetlania innych pól.
::          - Przed wyświetleniem okna redagowania (przed właściwymi akcjami Dołącz, Popraw, Wyświetl), ustawia
::            właściwości wszystkich pól (wymagających tego).
::       Kontekst pracy jest określany na podstawie argumentu wywołania.
::   WE: [_a] [STRING] - Kontekst pracy:
::             '1' - Obsługa jednego pola [domyślnie].
::             '*' - Obsługa wszystkich pól.
::       [_b] [TABLE]  - Uchwyt tabeli, w oknie redagowania której znajdują sie pola. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęta bieżąca tabela.
::       [_c] [STRING] - Akronim okna, w którym mają być ustawione właściwości pól. Jeżeli _a='1', parametr jest
::             opcjonalny - zostanie przyjęte bieżące okno.
::       [_d] [STRING] - Akronim pola, którego wartość determinuje właściwości wyświetlania innych pól. Parametr ma
::             znaczenie wyłącznie dla _a='1'. [Domyślnie: bieżące pole].
::   WY: 0 - Błąd argumentów wywołania.
::       1 - Argumenty poprawne (właściwości ustawione).
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (_a='1' | _a='*') || _a || '1' ?};
{? var_pres('_b')=type_of(H)
|| _tab:=_b
|? _tryb='1'
|| _tab:=cur_tab(1,1)
|| return(0)
?};
{? var_pres('_c')=type_of('')
|| _we:=_c
|? _tryb='1'
|| _we:=cur_win(1,1)
|| return(0)
?};
{? var_pres('_d')=type_of('')
|| _fld:=_d
|? _tryb='1'
|| _fld:=cur_afld()
|| _fld:=''
?};

{? _fld='' | _fld='PFZOWCZY'
|| _sval:=$(KST.PFZOWCZY);
   _tab.efld_opt(_we,'enable='+_sval,,'PFZOWPP');
   _tab.efld_opt(_we,'enable='+_sval,,'PFZOWDAT');
   _tab.efld_opt(_we,'mark='+_sval,,'PFZOWDAT');
   _tab.efld_opt(_we,'enable='+_sval,,'PFZOWKW');
   _tab.efld_opt(_we,'mark='+_sval,,'PFZOWKW');
   _tab.efld_opt(_we,'enable='+_sval,,'PFZOWZNA');
   _tab.efld_opt(_we,'mark='+_sval,,'PFZOWZNA');
   {? _fld<>''
   || exec('kst_efld_opt','!zws_par_ppkd','1',_tab,_we,'PFZOWPP')
   ?}
?};

{? _fld='' | _fld='PFZOWPP'
|| _sval:=$(KST.PFZOWCZY & KST.PFZOWPP='3');
   _tab.efld_opt(_we,'enable='+_sval,,'PFZOWOPI');
   _tab.efld_opt(_we,'mark='+_sval,,'PFZOWOPI')
?};

{? _fld='' | _fld='PFZPCCZY'
|| _sval:=$(KST.PFZPCCZY);
   _tab.efld_opt(_we,'enable='+_sval,,'PFZPCPP');
   _tab.efld_opt(_we,'enable='+_sval,,'PFZPCDEC');
   _tab.efld_opt(_we,'enable='+_sval,,'PFZPCDAT');
   _tab.efld_opt(_we,'mark='+_sval,,'PFZPCDAT');
   _tab.efld_opt(_we,'enable='+_sval,,'PFZPCZNA');
   _tab.efld_opt(_we,'mark='+_sval,,'PFZPCZNA');
   _tab.efld_opt(_we,'enable='+_sval,,'PFZPCDST');
   _tab.efld_opt(_we,'mark='+_sval,,'PFZPCDST');

   _tab.efld_opt(_we,'enable='+_sval,,'PFFORPRP');
   _tab.efld_opt(_we,'mark='+_sval,,'PFFORPRP');
   _tab.efld_opt(_we,'enable='+_sval,,'PFFORWLA');
   _tab.efld_opt(_we,'mark='+_sval,,'PFFORWLA');
   _tab.efld_opt(_we,'enable='+_sval,,'PFFORWLA');
   _tab.efld_opt(_we,'enable='+_sval,,'PFWIEFIR');
   _tab.efld_opt(_we,'mark='+_sval,,'PFWIEFIR');
   _tab.efld_opt(_we,'enable='+_sval,,'PFFORPR');
   _tab.efld_opt(_we,'mark='+_sval,,'PFFORPR');
   _tab.efld_opt(_we,'enable='+_sval,,'PFTYPFIR');
   _tab.efld_opt(_we,'mark='+_sval,,'PFTYPFIR');
   _tab.efld_opt(_we,'enable='+_sval,,'PFR_LIST');
   _tab.efld_opt(_we,'mark='+_sval,,'PFR_LIST');

   _tab.efld_opt(_we,'enable='+_sval,,'PFR_R_L');
   _tab.efld_opt(_we,'mark='+_sval,,'PFR_R_L');
   _tab.efld_opt(_we,'enable='+_sval,,'PFR_R_U');
   _tab.efld_opt(_we,'mark='+_sval,,'PFR_R_U');
   _tab.efld_opt(_we,'enable='+_sval,,'PFR_R_Z');
   _tab.efld_opt(_we,'mark='+_sval,,'PFR_R_Z')

?};

{? _fld='' | _fld='EZLAMAIL'
|| _sval:=$(KST_PAR.EZLAMAIL<>'');
   _tab.efld_opt(_we,'enable='+_sval,POLA_GRP,'TXT_1');
   _tab.efld_opt(_we,'mark='+~_sval,POLA_GRP,'TXT_1');
   _tab.efld_opt(_we,'enable='+_sval,POLA_GRP,'TXT_2');
   _tab.efld_opt(_we,'mark='+~_sval,POLA_GRP,'TXT_2')
?};

1


\kst_pfronx_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Po redagowaniu wybranych pól zmiennej KST związanych z PFRON-em.
::   WE:
::   WY:
::  OLD: \pfzow_be/war_tech.fml
::  OLD: \pfzowop_be/war_tech.fml
::  OLD: \pfzpc_be/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
exec('kst_efld_opt','!zws_par_ppkd')


\kst_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Rekord - po" dla tabeli KST.
::       Kontekst: stałe systemu - kadry.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Dane podstawowe (pierwsza zakładka).
{? (_chk:=exec('kst_ae_com01','stalesys'))<>''
|| return(_chk)

|? (_chk:=exec('kst_ae_prn','stalesys'))<>''
|| return(_chk)
?};

:: PFRON (druga zakładka).
{? ~exec('mail_ok','#email',KST.PFEMAIL)
|| FUN.info('Nieprawidłowy adres e-mail.'@);
   return('PFEMAIL')
?};

{? KST.PFZOWCZY
|| {? KST.PFZOWPP='3' & (_chk:=__CHK.record(KST,,'PFZOWOPI'))<>''
   || return(_chk)
   |? (_chk:=__CHK.record(KST,,'PFZOWDAT','PFZOWZNA','PFZOWKW'))<>''
   || return(_chk)
   |? KST.PFZOWKW<0
   || FUN.emsg('Kwota złagodzenia nie może być ujemny.'@);
      return('PFZOWKW')
   ?}
?};

{? KST.PFZPCCZY
|| _chk:=__CHK.record(KST,,
      'PFZPCDAT','PFZPCZNA','PFZPCDST',
      'PFFORPRP','PFFORWLA','PFWIEFIR','PFFORPR','PFTYPFIR','PFR_LIST',
      'PFR_R_L','PFR_R_U','PFR_R_Z');
   {? _chk<>''
   || return(_chk)
   ?};
   {? KST.PFR_R_L<0
   || FUN.emsg('Współczynnik niepełnosprawności lekkiej nie może być ujemny.'@);
      return('PFR_R_L')
   ?};
   {? KST.PFR_R_U<0
   || FUN.emsg('Współczynnik niepełnosprawności umiarkowanej nie może być ujemny.'@);
      return('PFR_R_U')
   ?};
   {? KST.PFR_R_Z<0
   || FUN.emsg('Współczynnik niepełnosprawności znacznej nie może być ujemny.'@);
      return('PFR_R_Z')
   ?}
?};

:: PUE (trzecia zakładka).
{? KST_PAR.EZLAMAIL<>''
|| {? ~exec('mail_ok','#email',KST_PAR.EZLAMAIL)
   || FUN.emsg('Nieprawidłowy adres e-mail.'@);
      return('EZLAMAIL')
   |? KST_PAR.EZLAPASS='' & (_chk:=__CHK.record2(POLA_GRP,'TXT_1','Hasło do usługi'))<>''
   || return(_chk)
   |? KST_PAR.EZLAPZIP='' & (_chk:=__CHK.record2(POLA_GRP,'TXT_2','Hasło do pliku ZIP'))<>''
   || return(_chk)
   ?}
?};

:: Sprzątanie (przed zapisem).
_d0:=date(0,0,0);
{? ~KST.PFZOWCZY
|| KST.PFZOWPP:='';
   KST.PFZOWDAT:=_d0;
   KST.PFZOWKW:=0;
   KST.PFZOWZNA:=''
?};
{? KST.PFZOWPP<>'3'
|| KST.PFZOWOPI:=''
?};

{? ~KST.PFZPCCZY
|| KST.PFZPCPP:=KST.PFZPCDEC:='';
   KST.PFZPCDAT:=_d0;
   KST.PFZPCZNA:='';
   KST.PFZPCDST:=_d0;

   KST.PFFORPRP:=KST.PFFORWLA:=KST.PFFORWLA:=KST.PFWIEFIR:=KST.PFFORPR:=KST.PFTYPFIR:=KST.PFR_LIST:='';

   KST.PFR_R_L:=KST.PFR_R_U:=KST.PFR_R_Z:=0
?};

:: Wykonanie na końcu, mamy pewność że okienko się zatwierdzi
{? KST_PAR.EZLAMAIL=''
|| KST_PAR.EZLAPASS:=KST_PAR.EZLAPZIP:='';
   POLA_GRP.TXT_1:=POLA_GRP.TXT_2:=''
?};
{? POLA_GRP.TXT_1<>''
|| KST_PAR.EZLAPASS:=base64('encode',exec('pwd_encode','#string',POLA_GRP.TXT_1,exec('encode_key','#mailbox')));
   POLA_GRP.TXT_1:=''
?};
{? POLA_GRP.TXT_2<>''
|| KST_PAR.EZLAPZIP:=pass_crypt(POLA_GRP.TXT_2);
   POLA_GRP.TXT_2:=''
?};

''


\kst_chor_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Udostępnia do edycji tabelę KST_CHOR
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
R.win_dict('SLO');
R.win_edit('RED');
R.win_patt('WZO');

KST_CHOR.cntx_psh();
KST_CHOR.index('KST_CHOR');
KST_CHOR.clear();
KST_CHOR.win_sel('WER');
KST_CHOR.win_edit('RED');
KST_CHOR.select();

KST_CHOR.cntx_pop();
R.cntx_pop();
''


\kst_chor_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Sprawdza poprawność wiersza tabeli KST_CHOR
::   WE:
::   WY: zgodny ze specyfikacją dla akcji "rekord po"
::----------------------------------------------------------------------------------------------------------------------
__CHK.table(KST_CHOR,-menu_txt()='popraw')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:40 1f09c8256a06c1773083ee6dcd6511df64728fbd6658844d498e5c8af0121bc58b0c13224fbe23044fc0e77873af196e66c7b8ce4277c9b923daf49e7a93a4a49c0aaa57aac4f131109f7d71d099efe601d82f8b97d32374d6fdfd6a86b21aa2d79499bb4b61fa36558234300ecbd68f0c2199a0e1b019dff2b441948418e7a0

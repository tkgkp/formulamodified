:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !zpr_ins_pmax.fml
:: Utworzony: 27.08.2021
:: Autor: WH
::======================================================================================================================
:: Zawartość: Formuły czynności ZPR_INS_PMAX
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Czynność informacji o przekroczeniu ilości instancji (ZPR_INS_PMAX) - formuła główna
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

:: WŁAŚCIWOŚCI CZYNNOŚCI
::# properties=SERVICE,GRP_FIRM

:: PARAMETRY WY:
::# kind=WY, symbol=BODYT, type=MEMO, name=Treść w formacie tekstowym, required=N
::# kind=WY, symbol=BODYH, type=MEMO, name=Treść w formacie HTML, required=N
::# kind=WY, symbol=FILE_A, type=BLOB, name=Wynik w postaci załącznika, required=N
::# kind=WY, symbol=SUB, type=STRING, name=Temat wiadomości, required=N
::# kind=WY, symbol=ILOSC, type=NUMBER, name=Całkowita ilość instancji, required=N

_can_continue:=1;

{? _can_continue>0
||
   _tab:=tab_tmp(2
      ,'SYMBOL','STRING[20]','Symbol procesu'
      ,'VER','STRING[20]','Wersja procesu'
      ,'NAME','STRING[100]','Nazwa procesu'
      ,'MAX','REAL','Ilość instancji max'
      ,'COUNTER','REAL','Ilość instancji'
      ,'DIFF','REAL','Przekroczenie'
   );
   _counter:=0;

   BI_PROC.cntx_psh();
   BI_PROC.index('PROBORN');
   B_PROC.cntx_psh();
   B_PROC.index('SYMVER');
   B_PROC.prefix(REF.FIRMA);
   {? B_PROC.first()
   || {!
      |? {? B_PROC.MAX_INS>0
         || BI_PROC.prefix(B_PROC.ref());
            _size:=BI_PROC.size();
            {? _size>B_PROC.MAX_INS
            || _tab.blank();
               _tab.SYMBOL:=B_PROC.SYMBOL;
               _tab.NAME:=B_PROC.NAME;
               _tab.VER:=B_PROC.VER;
               _tab.MAX:=B_PROC.MAX_INS;
               _tab.COUNTER:=_size;
               _diff:=_size-B_PROC.MAX_INS;
               _tab.DIFF:=_diff;
               _counter+=_diff;
               _tab.add()
            ?}
         ?};
         B_PROC.next()
      !}
   ?};
   B_PROC.cntx_pop();
   BI_PROC.cntx_pop();

   _out.SUB:='Powiadomienie o przekroczeniu ilości instancji procesów w firmie: %1'[REF.FIRMA().OPIS];

   _done:=1;
   {? _mp.isService()=0
   || exec('win','!zpr_ins_pmax',_tab,_out.SUB);
      _tab.prefix();
      _can_continue:=_tab.select()
   ?};

   _tab.prefix();
   {? _tab.size()=0
   || _can_continue:=0
   ?};

   {? _can_continue=0
   || _mp.cancel()
   ?};

   {? _can_continue>0
   ||
      _out.ILOSC:=_counter;

::    Tworzę zawartość tekstową
      _out.BODYT:='Analiza w firmie %1'
                  'Suma przekroczonych instancji: %2. '@[REF.FIRMA().OPIS,form(_counter,,0)];
      _tab.cntx_psh();
      _tab.clear();
      {? _tab.first()
      || {!
         |? _out.BODYT+=_tab.SYMBOL+';';
            _out.BODYT+=_tab.NAME+';';
            _out.BODYT+=_tab.VER+';';
            _out.BODYT+=$_tab.MAX+';';
            _out.BODYT+=$_tab.COUNTER+';';
            _out.BODYT+=$_tab.DIFF+'\n';
            _tab.next()
         !}
      ?};

::    Tworzę zawartość HTML
      _out.BODYH:='Analiza w firmie <b>%1</b>. '
                  'Suma przekroczonych instancji <b>%2</b>.'@[REF.FIRMA().OPIS,form(_counter,,0)];

      _out.BODYH+='
        <table [[STYLE_TABLE]]>
        <tr [[STYLE_TABLE_TR]]>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%1</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%2</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%3</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%4</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%5</span></th>
          <th [[STYLE_TABLE_TH]]><span style="font-weight:bold">%6</span></th>
        </tr>
      '['Symbol procesu'@,'Nazwa'@,'Wersja'@,'Max instancji'@,'Ilość instancji'@,'Przekroczenie'@];

      {? _tab.first()
      || {!
         |?
            _out.BODYH+='
                <tr [[STYLE_TABLE_TR]]>
                <td [[STYLE_TABLE_TD]]>%1</td>
                <td [[STYLE_TABLE_TD]]>%2</td>
                <td [[STYLE_TABLE_TD]]>%3</td>
                <td [[STYLE_TABLE_TD]]>%4</td>
                <td [[STYLE_TABLE_TD]]>%5</td>
                <td [[STYLE_TABLE_TD]]>%6</td>
                </tr>
            '[_tab.SYMBOL,_tab.NAME,_tab.VER,$_tab.MAX,$_tab.COUNTER,$_tab.DIFF];
            _tab.next()
         !}
      ?};
      _out.BODYH+='</table>';


::    Eksport do pliku CSV
      _name:='inst_pmax_'+$SYSLOG.tm_stamp()+'.csv';
      _can_continue:=_tab.export(_name,,,'UTF-8',,
                                    'SYMBOL',,1,,
                                    'NAME',,2,,
                                    'VER',,3,,
                                    'MAX',,4,,
                                    'COUNTER',,5,,
                                    'DIFF',,6,);
      {? _can_continue>0
      || {? _mp.bl_add('FILE_A',exec('kind_out','#b_port'),_name,1)>0
         || ferase(_name,1)
         ?}
      ?};
      _tab.cntx_pop();

::    Obsługa done
      _mp.save(,_out);
      {? _done>0
      || _mp.done()
      ?}
   ?}
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Formuła na opis TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_desc:='Wyślij powiadomienie o przekroczeniu ilości instancji procesów'@@;
_desc


\win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Tworzy okno wertowania
::   WE: _a - tab_tmp - tabelka
::       _b - STRING - tytuł okienka
::   WY: STRING - uchwyt do okna
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

_tab:=_a;
_title:=_b;

_wid:='#binstmax';

_wer:=_tab.mk_sel(_title,'P',0,_wid,1,1,,,'U');

_tab.win_fld(_wer,,'SYMBOL'  ,,,20,,,'Symbol'@);
_tab.win_fld(_wer,,'NAME'  ,,,30,,,'Nazwa'@);
_tab.win_fld(_wer,,'VER'  ,,,10,,,'Wersja'@);
_tab.win_fld(_wer,,'MAX'  ,,,15,,,'Maksymalna ilość instancji'@);
_tab.win_fld(_wer,,'COUNTER'  ,,,10,,,'Aktualna ilość instancji'@);
_tab.win_act(_wer,,'Kolejność');

_fb:="
   sel_exit()
";
_tab.win_act(_wer,,'Formuła','&Zamknij'@@,,,_fb,,,,,,'Z');
_btn:=_tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Zamknij'@],'menu:Z',,,,,,'');
_tab.btn_opt(_btn,'tooltip=%1'[exec('help_red_esc','#window','Z')]);
_tab.win_sel(_wer);
_wer


\action
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Akcja z obszaru roboczego
::   WE:
::   WY:
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZPR_INS_PMAX';
_params.PROC_START:='T';
exec('mp_run','#b__box',_params);
~~



:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:38 ce843b1777731f487b7953031c4f27d7427a67afa225719a06b4422dbb43b18300aebf4dfafa6b65a7b6e86d922b38913a08115805e4b4c0431ba5bdfbec21866577113666bda048f5c89d63891138e0fa72e3c4cdea3042d2b531b62e0284b9064157e6e4e428368174b62c6e473a80b30971d7dd4de52757182de4db44bf77

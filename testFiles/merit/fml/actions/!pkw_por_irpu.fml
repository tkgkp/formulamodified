:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !pkw_por_irpu.fml
:: Utworzony: 24.06.2016
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Obsługa czynności webTerm-owej PKW_POR_IRPU - Rejestracja planu urlopowego.
::
:: Statusy planów urlopowych:
::  pole 'ST' - status planu
::    'A' - aktywny
::    'K' - korekta
::    'U' - uzupełnienie
::    'N' - archiwalny
::
::  pole 'AZ' - stan akceptacji
::    'E' - plan w edycji
::    '?' - plan czeka na weryfikację przełożonego
::    'T' - plan zaakceptowany przez przełożonego
::    'N' - plan odrzucony przez przełożonego
::
:: Przepływy:
::    'E' -> {'?'}      Po edycji plan przesyłany jest do weryfikacji
::    '?' -> {'T'/'N'}  Złożony wniosek [?] może zostać zaakceptowany [T] lub odrzucony [N].
::    'T' -> {'K'/'U'}  Tylko dla zaakceptowanego planu [T] można utworzć korektę [K] lub uzupełnienie [U].
::    'N' -> {'?'}      Plan odrzucony [N] może zostać ponownie przesłany do akceptacji (np. po poprawieniu).
::
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Rejestracja planu urlopowego (webTerm) - główna formuła czynności.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# access=exec('access','!pkw_por_irpu')
::
::# kind=WE, symbol=URL_PLAN, type=_URL_PLAN, name=Wskazanie na plan urlopowy, required=N, keyref=T
::# kind=WE, symbol=P, type=_P, name=Wskazanie na pracownika, required=N
::# kind=WE, symbol=ROK, type=NUMBER, name=Rok planu urlopowego, required=N
::
::# kind=WEW, symbol=URL_PLAN, type=_URL_PLAN, name=Wskazanie na plan urlopowy, required=N
::
::# kind=WY, symbol=URL_PLAN, type=_URL_PLAN, name=Wskazanie na plan urlopowy, required=N
::
params_set(_par:=params_get());

_mp:=_par.mp;
_in:=_par.in;
_int:=_par.int;
_out:=_par.out;
_context:=_par.context;
_akcja:=_mp.akcja();

{? _akcja='webTermProcCancel'
|| return(~~)
?};

exec('czytaj','#stalesys',date(),KST);

_refresh:=0;
_is_context:=obj_ntab(_context) & var_pres('URL_PLAN',_context)=type_of(null());

{? _akcja='DOŁĄCZ'
::    dołączenie nowego rekordu
|| _result:=exec('url_plan_add','!pkw_por_irpu',_in.P,_in.ROK,'A','E');

   {? _result.ERROR=''
   || _uid:=exec('ref2uid','#table',_result.URL_PLAN);
      _mp.keyRef(_uid);
      _int.URL_PLAN:=_out.URL_PLAN:=_result.URL_PLAN;
      _mp.save(_int,_out);
      _mp.descTodo();
      _mp.keep();
      _refresh:=1
   || FUN.emsg(_result.ERROR)
   ?}

|? _akcja='USUŃ'
||
   {? _is_context
   || {? exec('url_plan_delete','plany_urlopowe',_context.URL_PLAN)>0
      || _int.URL_PLAN:=_out.URL_PLAN:=null();
         _mp.save(_int,_out);
         _mp.cancel();
         _refresh:=1
      || FUN.emsg('Nie można usunąć planu urlopowego'@)
      ?}
   || FUN.emsg('Brak wskazania planu urlopowego.'@)
   ?}

|? _akcja='WYCOFAJ'
|| _mp.cancel();
   _refresh:=1

|? _akcja='KOREKTA' | _akcja='UZUPEŁNIJ'
||
   {? _is_context
   || _refresh:=1;
      {? _akcja='KOREKTA'
      || _result:=exec('url_plan_kor','!pkw_por_irpu',_context.URL_PLAN)
      || _result:=exec('url_plan_uzu','!pkw_por_irpu',_context.URL_PLAN)
      ?};
      {? _result.ERROR=''
      || _uid:=exec('ref2uid','#table',_result.URL_PLAN);
         _mp.keyRef(_uid);
         _int.URL_PLAN:=_out.URL_PLAN:=_result.URL_PLAN;
         _mp.save(_int,_out);
         _mp.descTodo();
         _mp.keep()
      || FUN.emsg(_result.ERROR);
         _refresh:=0
      ?}
   || FUN.emsg('Brak wskazania planu urlopowego.'@)
   ?}

|? _akcja='ZAKOŃCZ'
|| {? _is_context
   || _int.URL_PLAN:=_out.URL_PLAN:=_context.URL_PLAN;
      _mp.save(_int,_out);
      _mp.done();
      _refresh:=1
   || _mp.error('Brak wskazania planu urlopowego.'@)
   ?}
?};

:: odświeżenie okna grupowego
{? _refresh & (_akcja='DOŁĄCZ' | _akcja='WYCOFAJ' | (_is_context & _context.REFRESH))
|| _multi:=exec('isStartMultiProc','#b__box','PKW_POR_IRPU') & _mp.pathArea();
   _bottom:=((_multi) & ((_akcja='DOŁĄCZ') | (_akcja='KOREKTA') | (_akcja='UZUPEŁNIJ')) | (_akcja='ZAKOŃCZ'));
   {? (_akcja='KOREKTA' | _akcja='UZUPEŁNIJ') & (_result.URL_PLAN<>null())
   || URL_PLAN.seek(_result.URL_PLAN)
   ?};
   web_top_refresh(_bottom)
?};
~~


\web_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Uruchomienie czynności z WWW
::   WE: _a  [STRING]    - 'Proc' lub 'Todo' lub 'Area'
::      [_b] [REFERENCE] - Wskazanie na instancję elementu w procesie
::                         (bi_prel dla Todo) lub element procesu (b_prel dla Proc).
::----------------------------------------------------------------------------------------------------------------------
  _path:={? var_pres('_a')=type_of('')     || _a || return(~~) ?};
  _prel:={? var_pres('_b')=type_of(null()) || _b || null()     ?};

_par:=params_get();

{? _path='Area'
:: Uruchomienie z obszaru roboczego. - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
|| _web_params:=web_params_get(1);

   exec('web_run','#b__box',
         _web_params.B_PREL,_web_params.AKCJA,_web_params.PORTS_IN,'web_Area',,_web_params.CONTEXT)

|? _path='Todo'
:: Uruchomienie z listy zadań (czynność w procesie, wywołana z parametrem - wskazaniem wniosku urlopowego).  - - - - - -
|| {? _prel=null() | ~(exec('env_wt','b_proces') & exec('init','pkw')) || return() ?};

   _bi_prel:=_prel;
   _ref:=null();

:: ustalenie parametru wewnętrznego
   _b_ele:=exec('FindAndGet','#table',BI_PREL,_bi_prel,,"B_PREL().B_ELE",null());

   _kind_in:=exec('kind_in','#b_port');
   _in:=exec('getPorts','#b_port',_b_ele,_kind_in,_bi_prel);
   exec('fillPorts','#bi_port',_bi_prel,_kind_in,_in);

   {? type_of(_in.URL_PLAN)=7
   || _ref:=_in.URL_PLAN
   ?};

   {? _ref=null()
   || _kind_int:=exec('kind_internal','#b_port');
      _int:=exec('getPorts','#b_port',_b_ele,_kind_int,_bi_prel);
      exec('fillPorts','#bi_port',_bi_prel,_kind_int,_int);
      {? type_of(_int.URL_PLAN)=7
      || _ref:=_int.URL_PLAN
      ?}
   ?};

   URL_PLAN.prefix();
   {? URL_PLAN.seek(_ref)
   || exec('web_global_params_set','pkw','PKW_POR_IRPU','P_REF',URL_PLAN.P);
      exec('web_global_params_set','pkw','PKW_POR_IRPU','ROK',URL_PLAN.ROK);

::    wyświetlenie okna grupowego
      exec('select_irpu','plany_urlopowe',_path)

   || FUN.emsg('Nie udało się znaleźć planu urlopowego.\nCzynność zostanie wycofana.'@);
::    zwijamy proces bo coś poszło nie tak
      _web_params:=exec('mp_run_a','#b__box');
      _web_params.B_PREL:=_bi_prel;
      _web_params.ACT_UID:='PKW_POR_IRPU';
      _web_params.AKCJA:='WYCOFAJ';
      _web_params.CONTEXT:=$(
         "  _context:=obj_new('REFRESH');
            _context.REFRESH:=1;
            _context
         "
      );
      exec('web_run','#b__box',
         _web_params.B_PREL,_web_params.AKCJA,_web_params.PORTS_IN,'web_Todo',,_web_params.CONTEXT)
   ?}

|? _path='Proc'
:: Uruchomienie z pulpitu (czynność startowa).   - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
|| {? _prel=null() || return() ?};

   _b_prel:=_prel;

      _fml_b:="exec('p_web_cx_set_prefix','p_web',,'RODZAJ','W');
::       jeżeli ścieżka to start procesu i mamy tylko jeden rekord to nie pokazujemy
::       okna wyboru z przebiegami pracownika
         {? P_WEB_CX.first() & (P_WEB_CX.size()=1)
         || exec('p_web_wer_wybierz','p_web',1);
            0
         || 1
         ?}";

    _fml_a:=$("exec('web_global_params_set','pkw','PKW_POR_IRPU','ROK',date~1);
             exec('select_irpu','plany_urlopowe','Proc','"+$_b_prel+"')
          ");
   exec('p_web_btn_cntx','p_web',_fml_b,_fml_a)
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Opis na TODO
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_tab:=exec('init_desc_tab','pracownik');

_url_plan:={? var_pres('URL_PLAN',_in) || _in.URL_PLAN || null() ?};
   _p_ref:={? var_pres('P',_in)        || _in.P        || null() ?};
     _rok:={? var_pres('ROK',_in)      || _in.ROK      || 0      ?};

_akcja:='';
{? +app_info('web_sesid')
|| _web_params:=web_params_get();
   _akcja:={? obj_ntab(_web_params)  & var_pres('AKCJA',_web_params)>0
           || _web_params.AKCJA
           || ''
           ?}
?};

{? _url_plan
|| URL_PLAN.cntx_psh();
   URL_PLAN.prefix();
   {? URL_PLAN.seek(_url_plan)
   || _rok:=URL_PLAN.ROK;
      _p_ref:=URL_PLAN.P
   ?};
   URL_PLAN.cntx_pop()
?};

{? _p_ref
|| P.cntx_psh();
   P.prefix();
   {? P.seek(_p_ref)
   || OSOBA.cntx_psh();
      OSOBA.prefix();
      UD_SKL.cntx_psh();
      UD_SKL.prefix();
      STN.cntx_psh();
      STN.prefix();
      _tab.ZAW_DANE:='T';
      _tab.NAZWISKO:=P.OSOBA().NAZWISKO;
      _tab.PIERWSZE:=OSOBA.PIERWSZE;
      _tab.T:=P.T;
      _tab.UD_SKL:=P.WYDZIAL().SYMBOL;
      _tab.STN:=P.ST().ST;
      STN.cntx_pop();
      UD_SKL.cntx_pop();
      OSOBA.cntx_pop()
   ?};
   P.cntx_pop()
?};

{? _akcja='KOREKTA'
|| {? _rok
   || {? _tab.ZAW_DANE='T'
      || 'Zakończ rejestrację planu urlopowego (korekta) na %1r. dla %2 %3, Numer teczki - %4, Jednostka - %5, Stanowisko %6'@@
            [$_rok,_tab.NAZWISKO,_tab.PIERWSZE,_tab.T,_tab.UD_SKL,_tab.STN]
      || 'Zakończ rejestrację planu urlopowego (korekta) na %1r.'@@[$_rok]
      ?}
   || {? _tab.ZAW_DANE='T'
      || 'Zakończ rejestrację planu urlopowego (korekta)  dla %1 %2, Numer teczki - %3, Jednostka - %4, Stanowisko %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.T,_tab.UD_SKL,_tab.STN]
      || 'Zakończ rejestrację planu urlopowego (korekta)'@@
      ?}
   ?}
|? _akcja='UZUPEŁNIJ'
|| {? _rok
   || {? _tab.ZAW_DANE='T'
      || 'Zakończ rejestrację planu urlopowego (uzupełnienie) na %1r. dla %2 %3, Numer teczki - %4, Jednostka - %5, Stanowisko %6'@@
            [$_rok,_tab.NAZWISKO,_tab.PIERWSZE,_tab.T,_tab.UD_SKL,_tab.STN]
      || 'Zakończ rejestrację planu urlopowego (uzupełnienie) na %1r.'@@[$_rok]
      ?}
   || {? _tab.ZAW_DANE='T'
      || 'Zakończ rejestrację planu urlopowego (uzupełnienie)  dla %1 %2, Numer teczki - %3, Jednostka - %4, Stanowisko %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.T,_tab.UD_SKL,_tab.STN]
      || 'Zakończ rejestrację planu urlopowego (uzupełnienie)'@@
      ?}
   ?}
|| {? _rok
   || {? _tab.ZAW_DANE='T'
      || 'Zakończ rejestrację planu urlopowego na %1r. dla %2 %3, Numer teczki - %4, Jednostka - %5, Stanowisko %6'@@
            [$_rok,_tab.NAZWISKO,_tab.PIERWSZE,_tab.T,_tab.UD_SKL,_tab.STN]
      || 'Zakończ rejestrację planu urlopowego na %1r.'@@[$_rok]
      ?}
   || {? _tab.ZAW_DANE='T'
      || 'Zakończ rejestrację planu urlopowego dla %1 %2, Numer teczki - %3, Jednostka - %4, Stanowisko %5'@@
            [_tab.NAZWISKO,_tab.PIERWSZE,_tab.T,_tab.UD_SKL,_tab.STN]
      || 'Zakończ rejestrację planu urlopowego'@@
      ?}
   ?}
?}


\url_plan_web_wer_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Obsługa akcji "Po odświeżeniu" okna WEB_WER tabeli URL_PLAN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=web_params_get();
  _path:={? var_pres('path',_params)>0   || _params.path   || '' ?};
_b_prel:={? var_pres('b_prel',_params)>0 || _params.b_prel || '' ?};

:: Dostępność przycisków.
_ag:='';

_uid_ref:=URL_PLAN.uidref();

:: sprawdzamy czy dla podanego rekordu kluczowego istnieje czynność, która jest aktywna w jakimś procesie;
:: sprawdzamy czy wogóle jest aktywny jakiś proces gdzie ta czynność występuje;
:: sprawdzamy czy nie ma zgłoszonych problemów w tym procesie
_res:=exec('isActiveProcOrProblems','#b__box',_uid_ref);
_isProblem:=_res.isProblem;
_isActiveProc:=_res.isActive;
_isActiveInCurProc:=exec('record_keyrefed','#b__box',_uid_ref,'PKW_POR_IRPU');

:: jeżeli proces się skończył, to sprawdzamy czy mamy zaakceptowany proces, w którym ta czynnośc występuje
:: teoretycznie powinniśmy, ale jeśli nie mamy to traktujemy to jako sytuację wyjątkową i dopuszczamy wtedy
:: do uaktywnienia przycisku "Usuń"
_isAcceptedProc:=_isActiveProc;
{? ~_isActiveProc
|| _isAcceptedProc:=exec('is_act','#b__box','PKW_POR_IRPU',0)
?};

:: akcja Rok [R]
{? _path='Todo'
|| _ag+='R'
?};

{? URL_PLAN.size()
||
:: sprawdzamy dla ścieżki "Proc" czy wybraliśmy dobry proces, inaczej nie będą dostępne przyciski
   _isProcOK:=1;
   {? (_path='Proc') & (+_b_prel) & _res.b_proc
   || B_PREL.cntx_psh();
      B_PREL.prefix();
      {? B_PREL.seek(_b_prel,)
      || _isProcOK*=(_res.b_proc=B_PREL.B_PROC);
         {? ~_isProcOK
         || B_PROC.cntx_psh();
            B_PROC.prefix();
            {? B_PROC.seek(_res.b_proc)
            || FUN.emsg('Rekord planu urlopowego powiązany jest z niezakończonym procesem:'@+'\n\n'+
                        '"'+exec('B_PROC','#to_string')+'"'+'\n\n'+
                        'Nie można redagować rekordu w ramach aktualnie uruchomionego procesu.'@)
            ?};
            B_PROC.cntx_pop()
         ?}
      ?};
      B_PREL.cntx_pop()
   ?};
   exec('web_global_params_set','pkw','PKW_POR_IRPU','isProcOK',_isProcOK);

   {? _isProcOK
   ||
:: akcja Dołącz [D]
      _ag+='D';
:: akcje Korekta [K] i Uzupełnij [E]
      {? ((URL_PLAN.AZ<>'T') | (URL_PLAN.ST='N')) &
::  dla planów aktywnych i odrzuconych, których proces się zakończył udostępniamu opcję korekty lub uzupełnienia
        ~((URL_PLAN.AZ='N') & (URL_PLAN.ST='A') & ~_isActiveInCurProc)
::   dla planów archiwalnych lub nie zatwierdzonych
      || _ag+='KE'
      |? (URL_PLAN.AZ='T') & (URL_PLAN.ST='A')
::    dla planów zaakceptowanych i aktywnych sprawdzamy czy nie ma już korekty bądź uzupełnienia
      || _kor_uzu:=0;
         URL_PLAN.cntx_psh();
         URL_PLAN.index('PRACROK');
         URL_PLAN.prefix(URL_PLAN.P,URL_PLAN.ROK);
         {? URL_PLAN.first()
         || {!
            |? {? (URL_PLAN.ST='K' | URL_PLAN.ST='U')
               || _kor_uzu:=1
               ?};
               ~_kor_uzu & URL_PLAN.next()
            !}
         ?};
         URL_PLAN.cntx_pop();
         {? _kor_uzu || _ag+='KE' ?}
      ?};

:: akcja Usuń [U] i Zakończ [Z]
      {? (URL_PLAN.AZ<>'E') & (_isActiveProc & ~_isProblem | _isAcceptedProc)
      || _ag+={? URL_PLAN.AZ='N' & (URL_PLAN.ST='K' | URL_PLAN.ST='U' | URL_PLAN.ST='A')
              || 'U'
              || 'UZ'
              ?}
      ?};

:: uzupełnienie informacji o roku planu
      exec('url_plan_hdr_rok','plany_urlopowe',URL_PLAN.ROK)
   ||
      _ag:='DKEUZ'
   ?}
?};

{? ~_isActiveInCurProc | _isProblem || _ag+='Z' ?};

:_ag:='';
::!!! Chyba błąd technologii.
URL_PLAN.web_win_opt(web_top_win(),web_top_ident(),'grayed=');
URL_PLAN.web_win_opt(web_top_win(),web_top_ident(),'grayed='+_ag)


\url_poz_web_wer_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Obsługa akcji "Po odświeżeniu" okna WEB_WER tabeli URL_PLAN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_empty:={? var_pres('_a')=type_of(0) || _a || return() ?};

:: Dostępność przycisków.
_ag:='';

URL_PLAN.cntx_psh();
{? ~_empty
|| {? URL_POZ.size()
   ||
      _isProcOK:=exec('web_global_params_get','pkw','PKW_POR_IRPU','isProcOK',1);

      {? _isProcOK
      ||
         {? URL_POZ.URL_PLAN().AZ='E' | (URL_PLAN.AZ='N' & (URL_PLAN.ST='A' | URL_PLAN.ST='K' | URL_PLAN.ST='K'))
:: jeżeli dostępna edycja planu
         ||
:: akcja Popraw i Usuń
            {? URL_POZ.CAN_DEL<>'T'
            || _ag+='PU'
            ?}
         ||
:: jeżeli edycja planu już jest zakończona
            _ag+='DPU'
         ?}
      ||
         _ag+='DPU'
      ?}
   ?}
?};

:: sprawdzenie aktywnych czynności
{? exec('record_keyrefed','#b__box',URL_PLAN.uidref(),'PKW_POR_IRPU')=0 || _ag+='DPU' ?};

URL_PLAN.cntx_pop();

::_ag:='';
::!!! Chyba błąd technologii.
URL_PLAN.web_win_opt(web_top_win(),web_top_ident(),'grayed=');
URL_PLAN.web_win_opt(web_top_win(),web_top_ident(),'grayed='+_ag)


\url_plan_web_wer_gr_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Przed obsługa dla okna WEB_WER tabeli URL_PLAN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','pkw_por');
exec('url_plan_web_prefix','!pkw_por_irpu');
~~


\url_plan_ctrl_gr_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Przed obsługa dla okna CTRL tabeli URL_PLAN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','pkw_por');
web_ctrl_call('URL_PLAN_CTRL',,'setTitle',
      exec('nazwa','#system')+': '+'Rejestracja planów urlopowych'@
);
web_ctrl_call('URL_PLAN_CTRL',,'setBody',exec('html','plany_urlopowe',web_win_ref('URL_PLAN_WEB_WER')));
~~


\url_plan_ctrl_lim_gr_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Przed obsługa dla okna CTRL_LIM tabeli URL_PLAN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','pkw_por');
_ref:=web_win_ref('URL_PLAN_WEB_WER');
P.cntx_psh();
OSOBA.cntx_psh();
URL_PLAN.cntx_psh();
URL_PLAN.prefix();
{? URL_PLAN.seek(_ref)
|| URL_PLAN.P().OSOBA();
   web_ctrl_call('URL_PLAN_CTRL_LIM',,'setTitle',
      exec('nazwa','#system')+': '+'Rejestracja planów urlopowych'@
   );
   web_ctrl_call('URL_PLAN_CTRL_LIM',,'setHeadEnd',
      '<div class="info_box title text_center">%1</div>'[exec('P','#to_string')]
   );
   web_ctrl_call('URL_PLAN_CTRL_LIM',,'setBody',exec('html_lim','plany_urlopowe','URL_PLAN_WEB_WER'))
?};
URL_PLAN.cntx_pop();
OSOBA.cntx_pop();
P.cntx_pop();
~~


\url_poz_web_wer_gr_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Przed obsługą okna WEB_WER tabeli URL_POZ w oknie grupowym WER_GR tabeli URL_PLAN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:=web_win_ref('URL_PLAN_WEB_WER');
{? type_of(_ref)
|| URL_POZ.index('URL_POZ');
   URL_POZ.prefix(_ref)
?};
~~


\url_plan_web_add_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Dodaje plan urlopowy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_web_params:=web_params_get();
web_params_set(_web_params);
_web_n_tab:=obj_ntab(_web_params);
  _path:={? _web_n_tab  & var_pres('path',_web_params)>0
         || _web_params.path
         || ''
         ?};
_b_prel:={? _web_n_tab & var_pres('b_prel',_web_params)>0
         || exec('FindAndGet','#table',B_PREL,_web_params.b_prel,,,"null()")
         || null()
         ?};

{? ~exec('is_act','#b__box','PKW_POR_IRPU',1)
|| return()
:: Ustawiam środowisko.
|? exec('env_wt','b_proces') & exec('load_cur_prac','p_web')
|| _akcja:='DOŁĄCZ';
   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='PKW_POR_IRPU';
   _params.AKCJA:=_akcja;
   _params.PROC_START:='T';

   _rok:=exec('web_global_params_get','pkw','PKW_POR_IRPU','ROK',date~1);

:: wypełnienie portów wejściowych
   _params.PORTS_IN:=$(
      "  _uid:='"+_params.ACT_UID+"';
         _refP:=exec('FindAndGet','#table',P,'"+P.uidref()+"',,,null());
         _ports_in:=exec('portsIn','#b__box',_uid);
         exec('portsInSet','#b__box',_ports_in,_uid,'P',_refP);
         exec('portsInSet','#b__box',_ports_in,_uid,'ROK',"+$_rok+");
         _ports_in
      "
   );

:: uzupełnienie kontekstu pracy
   _params.CONTEXT:=$(
            "  _context:=obj_new('REFRESH');
               _context.REFRESH:=1;
               _context
            "
         );

:: uruchomienie procesowości
   {? _path='Proc' & _b_prel<>null()
   || exec('web_run','#b__box',_b_prel,_akcja,_params.PORTS_IN,'web_Proc',,_params.CONTEXT)
   || exec('mp_run','#b__box',_params)
   ?}
?};
~~


\url_plan_web_kor_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Akcja "Korekta" okna wertowania WEB_WER tabeli ULR_PLAN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

_web_params:=web_params_get();
web_params_set(_web_params);
_web_n_tab:=obj_ntab(_web_params);
  _path:={? _web_n_tab  & var_pres('path',_web_params)>0
         || _web_params.path
         || ''
         ?};
_b_prel:={? _web_n_tab & var_pres('b_prel',_web_params)>0
         || exec('FindAndGet','#table',B_PREL,_web_params.b_prel,,,"null()")
         || null()
         ?};

:: Sprawdzenie czy czynność przekazana argumentem występuje w aktywnym i zaakceptowanym procesie.
{? ~exec('is_act','#b__box','PKW_POR_IRPU',1)
|| return()
:: Ustawiam środowisko.
|? exec('env_wt','b_proces')
|| _akcja:='KOREKTA';
   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='PKW_POR_IRPU';
   _params.AKCJA:=_akcja;
   _params.PROC_START:='T';
   _params.UIDREF:=URL_PLAN.uidref();

:: wypełnienie portów wejściowych
   _params.PORTS_IN:=$(
      "  _uid:='"+_params.ACT_UID+"';
         _ref:=exec('FindAndGet','#table',URL_PLAN,'"+URL_PLAN.uidref()+"',,,null());
         _ports_in:=exec('portsIn','#b__box',_uid);
         exec('portsInSet','#b__box',_ports_in,_uid,'URL_PLAN',_ref);
         _ports_in
      "
   );

:: uzupełnienie kontekstu pracy
   _params.CONTEXT:=$(
            "  _context:=obj_new('URL_PLAN','REFRESH');
               _context.URL_PLAN:=exec('FindAndGet','#table',URL_PLAN,'"+URL_PLAN.uidref()+"',,,null());
               _context.REFRESH:=1;
               _context
            "
         );

:: uruchomienie procesowości
   {? _path='Proc' & _b_prel<>null()
   || exec('web_run','#b__box',_b_prel,_akcja,_params.PORTS_IN,'web_Proc',,_params.CONTEXT)
   || exec('mp_run','#b__box',_params)
   ?}
?}


\url_plan_web_uzup_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Akcja "Uzupełnij plan" okna wertowania WEB_WER tabeli ULR_PLAN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

_web_params:=web_params_get();
web_params_set(_web_params);
_web_n_tab:=obj_ntab(_web_params);
  _path:={? _web_n_tab  & var_pres('path',_web_params)>0
         || _web_params.path
         || ''
         ?};
_b_prel:={? _web_n_tab & var_pres('b_prel',_web_params)>0
         || exec('FindAndGet','#table',B_PREL,_web_params.b_prel,,,"null()")
         || null()
         ?};

:: Sprawdzenie czy czynność przekazana argumentem występuje w aktywnym i zaakceptowanym procesie.
{? ~exec('is_act','#b__box','PKW_POR_IRPU',1)
|| return()
:: Ustawiam środowisko.
|? exec('env_wt','b_proces')
|| _akcja:='UZUPEŁNIJ';
   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='PKW_POR_IRPU';
   _params.AKCJA:=_akcja;
   _params.PROC_START:='T';
   _params.UIDREF:=URL_PLAN.uidref();

:: wypełnienie portów wejściowych
   _params.PORTS_IN:=$(
      "  _uid:='"+_params.ACT_UID+"';
         _ref:=exec('FindAndGet','#table',URL_PLAN,'"+URL_PLAN.uidref()+"',,,null());
         _ports_in:=exec('portsIn','#b__box',_uid);
         exec('portsInSet','#b__box',_ports_in,_uid,'URL_PLAN',_ref);
         _ports_in
      "
   );

:: uzupełnienie kontekstu pracy
   _params.CONTEXT:=$(
            "  _context:=obj_new('URL_PLAN','REFRESH');
               _context.URL_PLAN:=exec('FindAndGet','#table',URL_PLAN,'"+URL_PLAN.uidref()+"',,,null());
               _context.REFRESH:=1;
               _context
            "
         );

:: uruchomienie procesowości
   {? _path='Proc' & _b_prel<>null()
   || exec('web_run','#b__box',_b_prel,_akcja,_params.PORTS_IN,'web_Proc',,_params.CONTEXT)
   || exec('mp_run','#b__box',_params)
   ?}
?}


\url_plan_web_del_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Akcja "Usuń" okna wertowania WEB_WER tabeli ULR_PLAN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

:: Zawsze zaczynamy, od przekazania parametrów "dalej".
web_params_set(web_params_get());

_fml:="
   web_params_set(web_params_get());

:: Ustawiam środowisko.
   {? _a=1 & exec('env_wt','b_proces')
   || {? URL_PLAN.web_cntx_load(1)
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:='PKW_POR_IRPU';
         _params.AKCJA:='USUŃ';
         _params.UIDREF:=URL_PLAN.uidref();
         _params.CONTEXT:=$(
            \"  _context:=obj_new('URL_PLAN','REFRESH');
               _context.URL_PLAN:=exec('FindAndGet','#table',URL_PLAN,'\"+URL_PLAN.uidref()+\"',,,null());
               _context.REFRESH:=1;
               _context
            \"
         );
         exec('mp_run','#b__box',_params)
      ?};

::    wyczyszczenie okna z kontrolką
      web_ctrl_call('URL_PLAN_CTRL',,'setBody','')
   ?}";

URL_PLAN.web_cntx_save(1);
web_params_set(web_params_get());
web_ask(_fml,'Czy usunąć wybrany plan urlopowy?'@,'Plany urlopowe'@);
~~


\url_plan_web_zakoncz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Akcja "Zakończ" przycisku
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

_uid_ref:=URL_PLAN.uidref();
_url_plan:=exec('FindAndGet','#table',URL_PLAN,_uid_ref,,,null());
_act_uid:='PKW_POR_IRPU';

{? ~exec('is_act','#b__box',_act_uid,1)
|| return()
||
:: sprawdzamy czy wogóle jest aktywny jakiś proces gdzie ten rekord kluczony występuje;
:: sprawdzamy czy nie ma zgłoszonych problemów w tym procesie
   _res:=exec('isActiveProcOrProblems','#b__box',_uid_ref);

   {? ~_res.isActive
   || FUN.emsg('Bieżący rekord nie jest powiązany z żadnym uruchomionym procesem.'@+'\n'+
                'Czynność nie może być kontynuowana.'@);
      return()
   |? _res.isProblem
   || FUN.emsg('W procesie, z którym jest powiązany bieżący rekord został zgłoszony błąd.'@+'\n'+
                'Czynność nie może być kontynuowana.'@);
      return()
   ?};

   URL_POZ.cntx_psh();
   URL_POZ.index('URL_POZ');
   URL_POZ.prefix(_url_plan);
   {? URL_POZ.size()
   || exec('url_wnio_edit','plany_urlopowe',_url_plan,'?',,_act_uid)
   || FUN.info('Plan nie zawiera pozycji.'@)
   ?};
   URL_POZ.cntx_pop()
?}


\url_plan_web_rok_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Akcja "Rok" okna wertowania WEB_WER tabeli ULR_PLAN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('url_plan_rok','plany_urlopowe','PKW_POR_IRPU')


\url_poz_web_add_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Akcja "Dołącz" dla pozycji planu urlopowego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
web_params_set(_web_params:=web_params_get());

_ref:=web_win_ref('URL_PLAN_WEB_WER');
URL_PLAN.cntx_psh();
URL_PLAN.prefix();
{? type_of(_ref) & URL_PLAN.seek(_ref,)
|| URL_POZ.blank(1);
   URL_POZ.URL_PLAN:=_ref;
   URL_POZ.CAN_DEL:='T';
   URL_PLAN.web_cntx_save(1);
   URL_POZ.web_edit('WEB_RED','url_poz12345',,,"

      web_params_set(_web_params:=web_params_get());

::    Ustawiam środowisko.
      exec('env_wt','b_proces');

      _result:='';
      {? _a='OK'
      || _result:=exec('add_urpl','plany_urlopowe',URL_POZ.URL_PLAN,,URL_POZ.OD,URL_POZ.DO,URL_POZ.RODZAJ);
         {? +_result
         || FUN.emsg(_result)
         ?}
      ?};
      {? _result=''
      || URL_POZ.web_eclose('WEB_RED');
         URL_PLAN.web_cntx_load(1);
         URL_PLAN.web_refresh('WEB_GR','URL_PLAN_WEB_WER')
      ?}
   ")
?};
URL_PLAN.cntx_pop();
~~


\url_poz_web_put_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Akcja "Popraw" dla pozycji planu urlopowego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

web_params_set(_web_params:=web_params_get());

_ref:=web_win_ref('URL_PLAN_WEB_WER');
URL_PLAN.cntx_psh();
URL_PLAN.prefix();
{? type_of(_ref) & URL_PLAN.seek(_ref,)
|| URL_PLAN.web_cntx_save(1);
   URL_POZ.web_cntx_save(1);
   URL_POZ.web_edit('WEB_RED','url_poz12345',,,"

      web_params_set(_web_params:=web_params_get());

::    Ustawiam środowisko.
      exec('env_wt','b_proces');

      _result:='';
      {? _a='OK'
      || _result:=exec('add_urpl','plany_urlopowe',URL_POZ.URL_PLAN,URL_POZ.ref(),URL_POZ.OD,URL_POZ.DO,URL_POZ.RODZAJ);
         {? +_result
         || FUN.emsg(_result)
         ?}
      ?};
      URL_PLAN.web_cntx_load(1);
      URL_POZ.web_cntx_load(1);
      {? _result=''
      || URL_POZ.web_eclose('WEB_RED');
         URL_PLAN.web_refresh('WEB_GR','URL_PLAN_WEB_WER')
      ?}
   ")
?};
URL_PLAN.cntx_pop();
~~


\url_poz_web_del_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Akcja "Usuń" dla pozycji planu urlopowego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

_fml:=$("
   {? _a=1 & URL_POZ.web_cntx_load(1)
   || URL_POZ.URL_PLAN();

::    Ustawiam środowisko.
      exec('env_wt','b_proces');

      {? exec('url_poz_delete','plany_urlopowe',URL_POZ.ref())>0
      || _szef:='';
         {? exec('load_cur_prac','p_web')
         || _szef:={? exec('p_web_cx_get_rodzaj','pkw')='P' || 'S' || '' ?}
         ?};
::       dodanie informacji, że plan został poprawiony
         {? _szef='S'
         || URL_PLAN.AP:='T';
            URL_PLAN.put()
         ?}

      || FUN.info('"+'Nie można usunąć wybranej pozycji planu urlopowego.'@+"')
      ?};
::    odrysowanie główego okna w grupie
      URL_PLAN.web_refresh('WEB_GR','URL_PLAN_WEB_WER')
   ?}");

URL_POZ.web_cntx_save(1);
web_ask(_fml,'Czy usunąć wybraną pozycję planu urlopowego?'@,'Plany urlopowe'@);
~~


\url_plan_web_red_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Przed obsługą dla okienka redagowania WEB_RED
::   WE: _a -
::       _b -
::   WY:
::----------------------------------------------------------------------------------------------------------------------
~~


\url_plan_web_prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Ustawia prefix na tabeli URL_PLAN
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_p_ref:=exec('web_global_params_get','pkw','PKW_POR_IRPU','P_REF',null());
_rok:=exec('web_global_params_get','pkw','PKW_POR_IRPU','ROK',date~1);

{? _p_ref=null()
:: Odczytuję zapamiętany kontekst tabeli P.
|| {? exec('load_cur_prac','p_web')
   || _p_ref:=P.ref()
   ?}
?};
URL_PLAN.index('PRACROK');
URL_PLAN.prefix(_p_ref,_rok);
~~


\url_plan_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Tworzy korektę planu
::   WE: _a [REFERENCE] - wskazanie na aktywny plan urlopowy
::   WY:    [OBJECT]    - obiekt zawierający pola:
::                        ERROR - komunikatem błędu lub napis pusty gdy zakonczono poprawnie
::                        URL_PLAN - wskazanie na dodany plan urlopowy
::----------------------------------------------------------------------------------------------------------------------
_result:=obj_new('ERROR','URL_PLAN');
_result.ERROR:='Korekta planu - błędny parametr wejściowy'@;
_result.URL_PLAN:=null();

_ref:={? var_pres('_a')=type_of(null) || _a || return(_result) ?};

_result.ERROR:='';

URL_PLAN.cntx_psh();
URL_PLAN.prefix();
{? URL_PLAN.seek(_ref)
|| obj_del(_result);
   _result:=exec('url_plan_add','!pkw_por_irpu',URL_PLAN.P,URL_PLAN.ROK,'K','E')
?};
URL_PLAN.cntx_pop();
_result


\url_plan_uzu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Tworzy uzupełnienie planu
::   WE: _a [REFERENCE] - wskazanie na aktywny plan urlopowy
::   WY:    [OBJECT]    - obiekt zawierający pola:
::                        ERROR - komunikatem błędu lub napis pusty gdy zakonczono poprawnie
::                        URL_PLAN - wskazanie na dodany plan urlopowy
::----------------------------------------------------------------------------------------------------------------------
_result:=obj_new('ERROR','URL_PLAN');
_result.ERROR:='Uzupełnienie planu - błędny parametr wejściowy'@;
_result.URL_PLAN:=null();

_ref:={? var_pres('_a')=type_of(null) || _a || return(_result) ?};

_result.ERROR:='';

URL_PLAN.cntx_psh();
URL_PLAN.prefix();
{? URL_PLAN.seek(_ref)
|| obj_del(_result);
   _result:=exec('url_plan_add','!pkw_por_irpu',URL_PLAN.P,URL_PLAN.ROK,'U','E')
?};
URL_PLAN.cntx_pop();
_result


\url_plan_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KFI [2009]
::  MOD: TMR [17.00]
:: OPIS: Dodaje plan urlopowy
::   WE: _a [REFERENCE] - wskazanie napracownika
::       _b [INTEGER]   - rok dla którego plan ma byc dodany
::       _c [STRING]    - status planu urlopowego (A-ktywny/K-orekta/U-zuelnienie)
::       _d [STRING]    - stan akceptacji (T-zaakceptowany/E-w edycji/?-w weryfikacji)
::   WY:    [OBJECT]    - obiekt zawierający pola:
::                        ERROR - komunikatem błędu lub napis pusty gdy zakonczono poprawnie
::                        URL_PLAN - wskazanie na dodany plan urlopowy
::
::  OLD: \add_plan/portal.prc
::----------------------------------------------------------------------------------------------------------------------
_result:=obj_new('ERROR','URL_PLAN');
_result.ERROR:='Dodawanie planu urlopowego - błędny parametr wejściowy'@;
_result.URL_PLAN:=null();

 _p_ref:={? var_pres('_a')=type_of(null()) || _a || return(_result) ?};
   _rok:={? var_pres('_b')=type_of(0)      || _b || return(_result) ?};
_status:={? var_pres('_c')=type_of('')     || _c || return(_result) ?};
_akcept:={? var_pres('_d')=type_of('')     || _d || return(_result) ?};

_result.ERROR:='';

:: powrot w przypadku zerwanej transakcji
{? do_state()=2 || return(_result) ?};

URL_PLAN.clear();
P.cntx_psh();
P.prefix();
{? P.seek(_p_ref)
|| URL_PLAN.blank();
   URL_PLAN.P:=_p_ref;
   URL_PLAN.ROK:=_rok;
   URL_PLAN.DATAZAL:=date();
   URL_PLAN.ST:=_status;
   URL_PLAN.AZ:=_akcept;
   URL_PLAN.AP:='N';

:: zalozenie transakcji
   _mydo:=do_state()=0;
   {? _mydo || do() ?};

   {? _status='A' & exec('url_plan_exist','plany_urlopowe',_p_ref,_rok)>0
   || _result.ERROR:='Dodanie planu nie powiodło się.\nIstnieje już plan urlopowy w wybranym roku.'@
   |? URL_PLAN.add(1)
   || {? _status='K' | _status='U'
      || _result.ERROR:=exec('url_plan_add_next','!pkw_por_irpu',_status, URL_PLAN.ref())
      |? _status<>'A'
      || _result.ERROR:='Niedopuszczalna wartość parametru STATUS.'@
      ?};
      {? _result.ERROR=''
      || _result.URL_PLAN:=URL_PLAN.ref()
      || undo()
      ?}
   || _result.ERROR:='Dodanie planu nie powiodło się.'@
   ?};

   {? _mydo || end() ?}

|| _result.ERROR:='Nie znaleziono pracownika.'@
?};
P.cntx_pop();
_result


\url_plan_add_next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KFI [2009]
::  MOD: TMR [17.00]
:: OPIS: Jezeli zostal dodany nowy plan urlopowy to ta formulka sprawdza czy nie ma juz aktywnego
::       planu urlopowego tego pracownika w tym samym roku, jezeli jest, to przepisuje pozycje tego
::       aktywnego planu urlopowego do nowo dodanego planu urlopowego(najprawdopodobniej korekty,
::       badz uzupelnienia planu)
::   WE: _a [STRING]    - znacznik (K-korekta/U-uzupełnienie)
::       _b [REFERENCE] - wskazanie na plan urlopowy, który ma być korektą
::   WY:    [STRING] - komunikat bledu, pusty - zakonczono poprawnie
::  OLD: \add_plan/porta_ka.fml
::----------------------------------------------------------------------------------------------------------------------
_result:='Dodawanie następnego planu urlopowego - błędny parametr wejściowy'@;

_status:={? var_pres('_a')=type_of('')     || _a || return(_result) ?};
   _ref:={? var_pres('_b')=type_of(null()) || _b || return(_result) ?};

:: powrot w przypadku zerwanej transakcji
_result:='Zerwana transakcja'@;
{? do_state()=2 || return(_result) ?};

_result:='';

:: zalozenie transakcji
_mydo:=do_state()=0;
{? _mydo || do() ?};

_dalej:=1;
URL_PLAN.index('ROK');
URL_PLAN.prefix();
{? URL_PLAN.find_key(URL_PLAN.P,'A',URL_PLAN.ROK)
|| URL_POZ.index('URL_POZ');
   URL_POZ.prefix(URL_PLAN.ref());
   {? URL_POZ.first()
   || _tmp:=tab_tmp(1,'REF','INTEGER',);
      {!
      |? _tmp.REF:=#URL_POZ.ref();
         _tmp.add();
         URL_POZ.next()
      !};
      URL_POZ.clear();
      URL_PLAN.prefix();
      {? URL_PLAN.seek(_ref,) & _tmp.first()
      || {!
         |? {? URL_POZ.seek(_tmp.REF,)
            || URL_POZ.URL_PLAN:=URL_PLAN.ref();
               URL_POZ.CAN_DEL:={? _a='U' || 'N' || 'T' ?};
               _dalej*=URL_POZ.add(1)
            ?};
            _dalej & _tmp.next()
         !}
      ?}
   || _result:='Plan nie zawiera pozycji.'@;
      _dalej:=0
   ?}
|| _result:='Nie znaleziono nagłówka planu.'@;
   _dalej:=0
?};
{? _dalej=0 || undo() ?};
{? _mydo || end() ?};

_result


\access
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR & TMR [17.00]
:: OPIS: Formuła uprawnień, decyduje o tym, czy dany użytkownik może uruchomić bieżącą instancję czynności.
::   WE:
::   WY: 0 - Użytkownik nie ma uprawnień do wykonania bieżącej instancji czynności.
::       1 - Użytkownik ma uprawnienia do wykonania bieżącej instancji czynności.
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','pkw_por');
params_set(_par:=params_get());
_mp:=_par.mp;
_in:=_par.in;
_int:=_mp.load(exec('kind_internal','#b_port'));

:: Użytkownik weryfikowany (być może zastępujący).
_user:=_par.user;
:: Użytkownik zastępowany lub ~~.
_user_r:=_par.user_r;

_ret:=0;

_usr:={? _user_r=~~ || _user || _user_r ?};
_osoba:=exec('FindAndGet','#table',USERS,_usr,,"OSOBA",null());
{? _osoba=null()
|| return(_ret)
?};

:: sprawdzamy czy mamy jak określić pracownika z parametrów, które przyszły do czynności
_p_ref:=null();
_url_plan:=null();
{? var_pres('P',_in)=type_of(null()) & _in.P<>null()
|| _p_ref:=_in.P
|? var_pres('URL_PLAN',_in)=type_of(null()) & _in.URL_PLAN<>null()
|| _url_plan:=_in.URL_PLAN
|? var_pres('URL_PLAN',_int)=type_of(null()) & _int.URL_PLAN<>null()
|| _url_plan:=_int.URL_PLAN
?};

{? _p_ref=null() & _url_plan
|| _p_ref:=exec('FindAndGet','#table',URL_PLAN,_url_plan,,"P",null())
?};

P.cntx_psh();
P.index('OSOBA');
P.prefix();
{? _p_ref & P.seek(_p_ref)
||
:: Należy sprawdzić, czy użytkownik rejestrujący jest tą samą osobą co na planie urlopowym.
   _ret:=P.OSOBA=_osoba

:: Jeżeli nie mamy nic w parametrach wejściowych ani w wewnętrznych, to na uruchomienie czynności
:: pozwólmy każdemu. Jest to przypadek uruchomienia czynności startowej z pulpitu
|| _ret:=1
?};
P.cntx_pop();
_ret

:Sign Version 2.0 jowisz:1048 2020/10/16 15:19:17 c11654686e01af83f4b980ac127c417fd86607cf96f70f97e5c03e1c56ff866d98747f7aee31b7489bf5630edc2a43b84e119e49241afa3d3eee2a66a945a1f0f7603a983812a052e72dc0d5c21b6da3804182a6e97f5ba33424c5e1c3ecb7f4eaef141669fb6c33d2c872c0e7abcb336f6d412801391dbb3aa4c997144912ef

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_rrk_dkps.fml
:: Utworzony: 31.08.2022
:: Autor: PBS
::======================================================================================================================
:: Zawartość: Formuły czynności FKS_RRK_DKPS - Utworzenie PDF do potwierdzenia salda
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Utworzenie PDF do potwierdzenia salda - główna formuła czynności FKS_RRK_DKPS
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS,HRB
::# parses=exec('parses_ser_nags','rozrach_kor')
::# kind=WE, symbol=SER_NAG, type=_SER_NAG, name=Wskazanie na potwierdzenie salda, required=T, keyref=T
::# kind=WE, symbol=TYP_KON, type=STRING, name=Nazwa typu kontaktu z kontrahentem, required=N, fml_val="exec('zdarzt_sel','dokum_zal')"
::# kind=WY, symbol=DOKUM, type=_DOKUM, name=Wskazanie na załącznik z plikiem pdf, required=T
_par:=params_get();
_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;
_akcja:=_mp.akcja();
SER_KOR.TYP:='P';
_dokum:=null;
RACHBANK.KB_1R:='';
ToPDF:=_auto:=_mp.isAutoRun();
_typ:={? var_press('TYP_KON',_in)>0 || _in.TYP_KON || '' ?};
{? _mp.pathTodo() | _auto
|| {? var_press('SER_NAG',_in)>0
   || _ref:=BB.sqlint($_in.SER_NAG); _nam:=form(($_in.SER_NAG)-8);
      {? _ref<>0 & _nam<>''
      || SER_NAG.cntx_psh(); SER_NAG.prefix();
         {? SER_NAG.seek(_ref,_nam)
         || {? SER_NAG.T<>SER_KOR.TYP
            || {? _auto
               || _mp.error('Wybrana korespondencja nie jest potwierdzeniem salda.'@)
               || FUN.info('Wybrana korespondencja nie jest potwierdzeniem salda.'@); _mp.error()
               ?}
            |? SER_NAG.ST_AKC<>'T'
            || {? ~_auto || FUN.info('Potwierdzenie salda nie jest zaakceptowane.'@) ?}
            |? SER_NAG.KH().JEZYK & ~exec('sprwzwyd','rozrach','fks_km_p',SER_NAG.KH().JEZYK().KOD,0)
            || {? ~_auto || FUN.info('Brak wzorca dla pliku PDF w języku %1 do kontaktu z kontrahentem %2.'@[SLO.KOD,KH.SKR]) ?}
            |? RACHBANK.KB_1R=''
            || SKID_RBK.index('TAB'); SKID_RBK.prefix(null,REF.INFO,REF.INFO);
               {? SKID_RBK.first
               || _il:=1;
                  {! |? {? SKID_RBK.WAL=SER_NAG.WAL & SKID_RBK.RD='T'|| _il:=0; RACHBANK.KB_1R:=SKID_RBK.N ?};
                        _il & SKID_RBK.next()
                  !}
               ?};
               {? ~_auto & RACHBANK.KB_1R=''
               || FUN.info('Brak ustawionego domyślnego rachunku bankowego licencjobiorcy.'@)
               ?};
               {?  RACHBANK.KB_1R<>'' & (_auto | FUN.ask('Czy utworzyć plik PDF dla potwierdzenia salda\ndla kontrahenta: %1 z dnia: %2?'@[SER_NAG.KH().SKR,$SER_NAG.DATA]))
               || _dokum:=exec('add_pdf','!fks_rrk_dkps',{? _auto || 2 || 1 ?},_typ)
               ?}
            |? _auto | FUN.ask('Czy utworzyć plik PDF dla potwierdzenia salda\ndla kontrahenta: %1 z dnia: %2?'@[SER_NAG.KH().SKR,$SER_NAG.DATA])
            || _dokum:=exec('add_pdf','!fks_rrk_dkps',{? _auto || 2 || 1 ?},_typ)
            ?}
         || {? ~_auto
            || FUN.info('Nie znaleziono korespondencji z rozrachunków.'@)
            ?};
            _out.DOKUM:=null;
            _mp.save(,_out);
            _mp.done()
         ?};
         SER_NAG.cntx_pop()
      || {? ~_auto
         || FUN.info('Nie znaleziono korespondencji z rozrachunków.'@)
         ?};
         _out.DOKUM:=null;
         _mp.save(,_out);
         _mp.done()
      ?}
   || {? ~_auto
      || FUN.info('Nie znaleziono korespondencji z rozrachunków.'@)
      ?};
      _out.DOKUM:=null;
      _mp.save(,_out);
      _mp.done()
   ?}
|? _akcja='AddPDF'
|| {? SER_NAG.ST_AKC='T'
   || _il:=1;
      RACHBANK.KB_1R:='';
      SKID_RBK.index('TAB'); SKID_RBK.prefix(null,REF.INFO,REF.INFO);
      {? SKID_RBK.WAL<>null & SKID_RBK.first()
      || {!
         |? {? SKID_RBK.WAL=SER_NAG.WAL & SKID_RBK.RD='T' || _il:=0; RACHBANK.KB_1R:=SKID_RBK.N ?};
            _il & SKID_RBK.next()
         !}
      ?};
      {? _il & SER_NAG.WAL=null() & SKID_RBK.first()
      || {!
         |? {? SKID_RBK.INNWAL='T' & & SKID_RBK.RD='T' || _il:=0; RACHBANK.KB_1R:=SKID_RBK.N ?};
            _il & SKID_RBK.next()
         !}
      ?}
   ?};
   {? SER_NAG.ST_AKC<>'T'
   || FUN.info('Potwierdzenie salda nie jest zaakceptowane.'@)
   |? SER_NAG.KH().JEZYK & ~exec('sprwzwyd','rozrach','fks_km_p',SER_NAG.KH().JEZYK().KOD,0)
   || FUN.info('Brak wzorca dla pliku PDF w języku %1 do kontaktu z kontrahentem %2.'@[SLO.KOD,KH.SKR])
   |? RACHBANK.KB_1R=''
   || {? FUN.ask('Brak ustawionego domyślnego rachunku bankowego licencjobiorcy. Wyświetlić listę dostępnych rachunków?'@)
      || {? exec('rachunki_banki','rachunki')
         || RACHBANK.KB_1R:=SKID_RBK.N;
            {? FUN.ask('Czy utworzyć plik PDF dla potwierdzenia salda\ndla kontrahenta: %1 z dnia: %2?'@[SER_NAG.KH().SKR,$SER_NAG.DATA])
            || _dokum:=exec('add_pdf','!fks_rrk_dkps',1,_typ)
            ?}
         ?}
      ?}
   |? FUN.ask('Czy utworzyć plik PDF dla potwierdzenia salda\ndla kontrahenta: %1 z dnia: %2?'@[SER_NAG.KH().SKR,$SER_NAG.DATA])
   || _dokum:=exec('add_pdf','!fks_rrk_dkps',1,_typ)
   ?};
   VAR_DEL.delete('__POTWPAR','repesc')
?};
VAR_DEL.delete('ToPDF','ToPDF1','ToPDF2');
{? _dokum
|| _out.DOKUM:=_dokum;
   _mp.save(,_out);
   _mp.done()
?};
~~


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_desc:='Utwórz PDF do potwierdzenia salda'@@;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('SER_NAG',_we)>0
|| _ref:=BB.sqlint($_we.SER_NAG); _nam:=form(($_we.SER_NAG)-8);
   {? _ref<>0 & _nam<>''
   || SER_NAG.cntx_psh(); SER_NAG.use(_nam); SER_NAG.prefix();
      {? SER_NAG.seek(_ref,_nam)
      || _desc:='Utwórz PDF do potwierdzenia salda dla kontrahenta: %1 z dnia: %2'@@[SER_NAG.KH().SKR,$SER_NAG.DATA]
      ?};
      SER_NAG.cntx_pop()
   ?}
?};
_desc


\add_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodanie załacznika w postacji wydruku do PDF
::   WE: _a - nadpisać? 0-nie [1]-pytać 2-tak
::       _b - nazwa typu kontaktu
::   WY: Wskazanie na załacznik do wysłania (DOKUM.ref)
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
_ref:=null;
_dalej:=1;
_nag:=$SER_NAG.ref();
DOKUM.cntx_psh();
DOKUM.index('TYP'); DOKUM.prefix(_nag,'P');
_jest:=DOKUM.first();
{? _jest
|| _blob:=DOKUM.DOKUM;
   {? _a=1
   || _dalej:=FUN.ask('Istnieje już plik PDF.\nCzy nadpisać istniejący?'@)
   |? _a=2
   || _dalej:=1
   || _dalej:=0
   ?}
?};
{? _dalej
|| _plik:='potwierdzenie_salda'+$DOKUM.tm_stamp()+'.pdf';
   _plik_baza:='potwierdzenie_salda.pdf';
   _tab:=exec('email_os_kon_t','kontrahent',SER_NAG.KH,'Windykacja');
   ZDARZT.cntx_psh();
   ZDARZT.index('ZDARZT'); ZDARZT.prefix('N',_b,);
   _typ_kon:={? ZDARZT.first() || ZDARZT.ref() || null ?};
   ZDARZT.cntx_pop();
   SER_NAGS.cntx_psh(); SER_NAGS.index('SERNAGS1'); SER_NAGS.prefix(SER_NAG.FIRMA,SER_NAG.ref(),SER_NAG.ODD);
   {? SER_NAGS.first() & var_press('repesc')<=0
   || _ok:=exec('rep_exec','#b_report','FKS_RRK_DKPS','fks_potwkor',,0,_plik,0,,,,'W')
   || _ok:=0
   ?};
   SER_NAGS.cntx_pop();
   {? var_pres('ObjForMerge')>0
   || _plik:=ObjForMerge.doc2pdf()
   ?};
   _dw:='';
   {? _ok | var_pres('ObjForMerge')>0
   || {? _tab.first() & (_tab.size()>1)
      || _email:='';
         _ok:=exec('add','dokum_zal',_nag,_plik,~_jest,_email,_plik_baza,
                   SER_NAG.KH,{? _email<>'' || KH_OSOB.NAZWISKO+' '+KH_OSOB.IMIE || '' ?},
                   SER_NAG.ODD,_typ_kon,'Potwierdzenie salda nr '+SER_NAG.ID);
         {? _ok
         || {? _jest
            || _jest:=exec('find_dokumz','bloby',DOKUM.ref(),_blob)
            ?};
            DOKUMZ.prefix();
            exec('add_dokumz2','bloby',_plik_baza,~_jest);
            _ref:=DOKUM.ref()
         ?}
      |? (_tab.first() & _tab.size()=1) | ~_tab.size
      || _email:=_tab.EMAIL;
         _ok:=exec('add','dokum_zal',_nag,_plik,~_jest,_email,_plik_baza,
                   SER_NAG.KH,{? _email<>'' || KH_OSOB.NAZWISKO+' '+KH_OSOB.IMIE || '' ?},
                   SER_NAG.ODD,_typ_kon,'Potwierdzenie salda nr '+SER_NAG.ID);
         {? _ok
         || {? _jest
            || _jest:=exec('find_dokumz','bloby',DOKUM.ref(),_blob)
            ?};
            DOKUMZ.prefix();
            exec('add_dokumz2','bloby',_plik_baza,~_jest);
            _ref:=DOKUM.ref()
         ?}
      ?}
   || VAR_DEL.delete('repesc');
      repesc:=1
   ?}
?};
DOKUM.cntx_pop();

{? var_pres('ObjForMerge')>0
|| ObjForMerge.addToTab(ObjForMerge.TAB.FILENAME,'');
   ObjForMerge.addToTab(ObjForMerge.RESFILE,'');
   ObjForMerge.delFiles(0);
   VAR_DEL.delete('ObjForMerge')
?};

_ref

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 172a412638f4af0dccd8f71d6e2a62c1c3d5fd9e0e62856d105fd447809bf29dd292fdb8663d2c2f076e63212c06de6d773b87195dc2d5ba9faba01415248e109eb322d81138768200295925fe2f9e92707aa11bdd125f0f484d947b9e4e7a63fdff1b86a6be8eba2ffc82a8b1f1b431e0d80f5b71a56ffbd2b6b2f4a3637228

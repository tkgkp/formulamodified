:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lmo_mob_dkom.fml
:: Utworzony: 19.04.2016
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Czynność LMO_MOB_DKOM - Realizacja kompletacji na podstawie operacji mobilnej
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# properties=LOOP,SERVICE
::# permissions=ODDZ
::# kind=WE,  symbol=EANN,    type=_EANN,    name=Operacja mobilna,              required=N,   keyref=T
::# kind=WE,  symbol=GRP_KEY, type=STRING,   name=Klucz grupujący operacji,      required=N
::# kind=WE,  symbol=GEN_OPE, type=STRING,   name=Generacja operacji,            required=N
::# kind=WE,  symbol=TYPYDOK, type=_TYPYDOK, name=Typ dokumentu magazynowego,    required=N, fml_val="exec('typydok','!lmo_mob_dkom')", fml_exp="exec('typydok_export','magdok_nag',_a)"
::# kind=WE,  symbol=MG,      type=_MG,      name=Magazyn dla operacji,          required=N, fml_val="exec('mg','!lmo_mob_dkom')"
::# kind=WE,  symbol=MD,      type=_MG,      name=Magazyn dla przesunięcia,      required=N, fml_val="exec('mg','!lmo_mob_dkom')"
::# kind=WE,  symbol=CZYTW,   type=STRING,   name=Czy uzupełniać terminy ważn.,  required=N, fml_val="exec('answer2str','params',,,'Czy uzupełniać terminy ważności?')"
::# kind=WE,  symbol=OKR,     type=NUMBER,   name=O ile przesunąć datę,          required=N
::# kind=WEW, symbol=ND,      type=_ND,      name=Dokument wydania,              required=N
::# kind=WY,  symbol=ND,      type=_ND,      name=Dokument wydania,              required=N
::# kind=WY,  symbol=GEN_OPE, type=STRING,   name=Generacja operacji,            required=N
::# kind=WY,  symbol=IL_SRV,  type=NUMBER,   name=Ilość zatwierdzonych operacji, required=N
::# condition=Rejestracja dokumentu, act_uid=LMG_MAG_DWYD,   auto=T,  formula=_a.ND<>~~ & _a.ND<>null()
_mp:=params_get().mp;
_in:=params_get().in;
_out:=params_get().out;

exec('init','lmo');

_akcja:=_mp.akcja();
_auto:=_mp.isAutoRun();
_service:=_mp.isService();
_proces:=_mp.pathProc();
_area:=_mp.pathArea();

_select:=1;

_mp.trigRef('ND',,1,,'INT','ND');

_grpkey:={? var_pres('GRP_KEY',_in)=type_of('') & _in.GRP_KEY<>'' || _in.GRP_KEY || '' ?};
_oper:=var_pres('GEN_OPE',_in)=type_of('') & _in.GEN_OPE<>'' & _mp.loop()<>0;
_brak:=0;

:: ewentualny wybór operacji
{? ~_service & ~_oper & ~(var_pres('EANN',_in)=type_of(null()) & _in.EANN) & ~_area
|| exec('openean','open_tab',ST.ODDZ+'__');
   _brak:=1;
   EANN.index('DATA');
   EANN.prefix('T','Z');
   {? EANN.first()
   || {!
      |? _brak:=~(EANN.STAN='Z' & exec('ctrlEANN','magazyn_mob','Z',1));
         _brak & EANN.next()
      !}
   ?};
   {? ~_brak & exec('operacje','magazyn_mob','Z',,,0)
   || _in.EANN:=EANN.ref(); _mp.save('IN','EANN',EANN.ref())
   || _select:=0
   ?}
?};

{? _brak
|| FUN.info('Brak dostępnych do realizacji operacji typu kompletacja wysyłki.'@);
   _mp.cancel()
|? _service & ~(var_pres('TYPYDOK',_in)=type_of(null()) & _in.TYPYDOK)
|| _mp.error('Dla czynności typu serwis brak wymaganego parametru TYPYDOK.')
|? _service & ~(var_pres('MG',_in)=type_of(null()) & _in.MG)
|| _mp.error('Dla czynności typu serwis brak wymaganego parametru MG.')
|? ~_service & ~_oper & ~(var_pres('EANN',_in)=type_of(null()) & _in.EANN)
|| {? ~_select
   || _mp.cancel()
   || _mp.error('Brak wymaganego parametru EANN.')
   ?}
|? ~_service & ~_oper & exec('FindAndGet','#table',EANN,_in.EANN,,"TYP")<>'Z'
|| _mp.error('Parametr EANN wskazuję na inną operację mobilną niż Kompletacja wysyłki.')
|? _service
|| {? var_pres('OKR',_in)<>type_of(0) | _in.OKR<0 || _in.OKR:=0 ?};
   {? var_pres('MD',_in)<>type_of(null()) || _in.MD:=null() ?};
   {? var_pres('CZYTW',_in)<>type_of('') | ~((';TN'*_in.CZYTW)>1) || _in.CZYTW:='N' ?};
   _mp.save('IN',_in);
   EANN.cntx_psh();
   EANN.prefix();
   params_set('mp',_mp,'in',_in);
   exec('autoOper','!lmo_mob_dkom');
   EANN.cntx_pop()
|| {? var_pres('TYPYDOK',_in)<>type_of(null()) || _in.TYPYDOK:=null() ?};
   {? var_pres('MD',_in)<>type_of(0) || _in.MD:=null() ?};
   {? var_pres('OKR',_in)<>type_of(0) | _in.OKR<0 || _in.OKR:=0 ?};
   {? var_pres('CZYTW',_in)<>type_of('') | ~((';TN'*_in.CZYTW)>1) || _in.CZYTW:='N' ?};
   _mp.save('IN',_in);
   _continue:=0;
   _gen_ope:='';
   _ctrl:=1;

   {? _mp.loop()=0
:: pierwsze wywołanie
   || EANN.cntx_psh();
      _jest:=EANN.seek(_in.EANN);
      {? ~_jest
      || _mp.error('Nie znaleziono operacji mobilnej.')
      |? (_wyp:=exec('ctrlEANN','magazyn_mob','Z',0))<=0
      || _ctrl:=0;
         {? EANN.AKT='N' || _mp.done() || _mp.cancel() ?}
      || _answer:=
         {? _akcja='Generuj_wg_ope'
            | _proces
            | _mp.pathTodo()
         || params_set('mp',_mp,'in',_in);
            _grp_key:=exec('realeann_kpl','magazyn_mob',_akcja='Generuj_wg_ope',0,0,_wyp,_grpkey);
            _internal:=_mp.load('INT');
            _continue:={? var_pres('ND',_internal)=type_of(null()) & _internal.ND || 1 || -1 ?};
            {? _continue=1
            || _gen_ope:=exec('FindAndGet','#table',ND,_internal.ND,,"GRP_KEY",'')
            ?};
            obj_del(_internal)
         || _mp.error('Nieobsługiwana ścieżka.')
         ?}
      ?};
      EANN.cntx_pop()
:: wywołanie w pętli
   || _continue:=var_pres('GEN_OPE',_in)=type_of('') & _in.GEN_OPE<>'';
      {? _continue || _gen_ope:=_in.GEN_OPE ?}
   ?};

   {? _continue=-1
:: Wycofano się z wystawiania operacji
   || 1
   |? _continue=1 & _gen_ope<>''
:: Zapisanie parametru wyjściowego EANN, wykluczenie kolejnej realizacji z pętli, zakończenie czynności
   || ND.cntx_psh();
      _internal:=_mp.load('INT');
      {? var_pres('ND',_internal)=type_of(null()) & _internal.ND || ND.use(ref_name(_internal.ND)) ?};
      obj_del(_internal);
      ND.prefix();
      _grp_key:=_gen_ope-1;
      _grp_key_on:=_grp_key+'1';
      _grp_key_off:=_grp_key+'0';
      {? _grp_key<>''
      || ND.index('GRP_KEY');
         ND.prefix(_grp_key_on);
         {? ND.first()
         || ND.cntx_psh();
            ND.prefix();
            ND.GRP_KEY:=_grp_key_off;
            do();
            ND.put();
            _mp.save(exec('kind_out','#b_port'),'ND',ND.ref());
            _mp.save(exec('kind_out','#b_port'),'GEN_OPE',_gen_ope);
            end();
            ND.cntx_pop();
            {? ND.first()
::          kontynuacja pętli
            || _mp.loop_continue()
            ?}
         ?}
      ?};
      ND.cntx_pop();
      _mp.done()
   |? _ctrl
   || _mp.error('Brak oczekiwanego parametru GEN_OPE.')
   ?}
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));
_out:=_mp.load(exec('kind_in','#b_port'));

_wgr:={? var_pres('EANN',_in)<>type_of(~~) & _in.EANN
      || _in.EANN
      || null()
      ?};

{? _wgr<>null()
|| 'Zrealizuj kompletację wysyłki na podstawie operacji kompletacji %1'@@[exec('record','#to_string',_wgr)]
|| 'Zrealizuj kompletację wysyłki na podstawie operacji kompletacji'@@
?}


\typydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na wartość parametru TYPYDOK
::   WY: TYPYDOK.ref()
::----------------------------------------------------------------------------------------------------------------------
_prod:=exec('tte_lic','tte');
exec('typ_dok','lmg','"TYPYDOK".P=\'N\''
    +{? _prod='N' || ' and "TYPYDOK".WYR=\'N\'' || '' ?},,,0,0,,,,,-1)


\mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na wartość parametru TYPYDOK
::   WY: TYPYDOK.ref()
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null();
MG.cntx_psh();
MG.index('SL_ALL');
MG.prefix('T');
{? MG.first()
|| MG.win_sel('SLO');
   {? MG.select() || _wyn:=MG.ref() ?}
?};
MG.cntx_pop();
_wyn


\autoOper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Automatycznie realizuje operacje
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_il_srv:=0;

_eann:=sql('select '+
           '  EANN.REFERENCE REF, '+
           '  EANN.DATA DATA '+
           'from @EANN '+
           ' where EANN.REFERENCE like '+'''eann_\\_\\_%'' escape ''\\'''+
           '   and EANN.AKT=''T'''+
           '   and EANN.TYP=''Z'''+
           '   and EANN.STAN=''Z''');

_eann.clear();
{? _eann.first()
|| {!
   |? _oddz:=1+(4-_eann.REF);
      _rok:=form((_eann.DATA~1)-2000,-2,0,'99');
      exec('open','open_tab',_oddz,_rok);
      {? (EANN.clear(); EANN.seek(_eann.REF)) & (_wyp:=exec('ctrlEANN','magazyn_mob','Z',1))>0
      || _il_srv+=params_exec('autooper','magazyn_mob',EANN.ref(),_wyp)
      ?};
      _eann.next()
   !}
?};
obj_del(_eann);

_mp.save('OUT','IL_SRV',_il_srv);
_mp.done();
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:22:50 3a699bfb2938e0bbee2943a3d7b74461195101c566c7eaf0bc49b85823a54e23c2e71500b474e4372625ed97af14612879b7a14b409a090134a01ce3db6602708d651c03e668b136d2b8a1f500ff89dbc4384ad57ef5c95ca29e88ba0fd08d5893577f0ef93974f9b54dfe76d768612abfdca70ac5bee31de71ff51c0e361673

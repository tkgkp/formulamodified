:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !fks_ewi_wkkr.fml
:: Utworzony: 26.11.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły czynności FST_EWI_WKKR - Druk kodu kreskowego dla środka
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Druk kodu kreskowego dla środka - główna formuła czynności FST_EWI_WKKR.
::----------------------------------------------------------------------------------------------------------------------
::# permissions=FJKS
::# access=exec('uprawnienia','!fst_ewi_wkkr')
:: PARAMETRY WE:
::# kind=WE, symbol=SRSR, type=_SRSR, name=Wskazanie na środek, required=T, keyref=T
:: PARAMETRY WY:
::# kind=WY, symbol=SRSR, type=_SRSR, name=Wskazanie na środek, required=T
::
_args:=params_get();
_we:=_args.in;
_wew:=_args.int;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=-_mp.akcja();

{? _mp.pathTodo()
|| exec('init','fst');
   exec('czytaj','#stalesys',,XINFO);
   exec('czytaj','#stalesys',,FINFO);
   SRD.maski('r')
?};

__BARDRU:=0;
_srsr_rf:=null;
_usu:=0;
BtnDek:=0;
{? _mp.pathTodo()
|| {? var_pres('SRSR',_we) & type_of(_we.SRSR)=type_of(null) & _we.SRSR<>null
   || SRSR.prefix();
      {? SRSR.seek(_we.SRSR)
      || _mp.trigRef('SRSR');
         SRST.index('SROD');
         SRST.prefix(SRSR.ref(),SSTALE.AR,SSTALE.AO);
         {? SRST.first()
         || EDIT_ES.Z_NRI:=SRST.NRI;
            EDIT_ES.Z_NST:=SRST.NST;
            EDIT_ES.win_edit('DRUK');
            _title:='Druk kodu kreskowego'@;
            _win:=EDIT_ES.mk_edit(_title,,'_edit_es_druk');
            EDIT_ES.win_ewin(_win,,'DRUK');
            _par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Drukuj'@];
            EDIT_ES.win_ebtn(_win,_par,"BtnDek:=1; 'key:Esc'");
            _par:='text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@];
            _anuluj:=EDIT_ES.win_ebtn(_win,_par,"BtnDek:=0; 'key:Esc'");
            EDIT_ES.btn_eopt(_win,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
            EDIT_ES.win_edit(_win);
            EDIT_ES.display()
         || FUN.emsg('Brak danych środka w bieżącym okresie, druk kodu kreskowego niemożliwy.'@)
         ?}
      ?}
   || FUN.emsg('Nie znaleziono środka.'@);
      _usu:=1
   ?}
|? _mp.pathArea()
|| _mp.trigRef('SRSR');
   BtnDek:=1
?};

{? BtnDek
|| {? exec('bar_druk','!fst_ewi_wkkr')
   || _srsr_rf:=SRSR.ref();
      __BARDRU:=1
   || FUN.emsg('Druk kodu kreskowego nieudany.'@)
   ?}
?};

:: jeżeli wydrukowano
{? _srsr_rf
|| _wy.SRSR:=_srsr_rf;
   _mp.keyRef(SRSR.uidref());
   _mp.save(,_wy);
   _mp.done()
:: jeżeli nie znaleziono środka
|? _srsr_rf=null & _usu=1
|| _wy.SRSR:=null;
   _mp.save(,_wy);
   _mp.done()
:: jeżeli zrezygnowano z druku
|| _mp.cancel()
?}


\m_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Uruchomienie druku kodu kreskowego w obszarze roboczym
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FST_EWI_WKKR';
_params.AKCJA:=menu_txt();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SRSR',SRSR.ref());
_params.PROC_START:='N';
exec('mp_run','#b__box',_params);
1


\bar_druk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Druk kodu kreskowego dla bieżącego środka (tylko na drukarce kodów!)
::   WE: _a = numer inwentarzowy do wygenerowania kodu [STRING], _b = rodzaj
::  OLD: \bar_druk/wydr_es.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? PARWYD.TYPDR=null
|| FUN.emsg('Nie wybrano typu drukarki w parametrach wydruków użytkownika\n'
           '(Ustawienia i parametryzacja -> Ustawienia -> Środki trwałe -> Parametry urządzeń użytkownika).\n'
           'Nie można uruchomić wydruku.'@);
   _dalej:=0
?};
{? _dalej
|| {? PAR_WYDR.URZ_LAB=null
   || FUN.emsg('W parametrach wydruków nie ustawiono obowiązującej definicji etykiety.'@);
      _dalej:=0
   ?}
?};
{? _dalej
|| VAR.S_OR_E:=0;
   _dalej:=exec('druk','kody_kresk')
?};
_dalej


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh(); USERS.prefix();
{? USERS.seek(_user)
||
:: czy user ma uprawnienia do jednostki księgowej środka
   SRSR.cntx_psh();
   SRSR.prefix();
   {? var_pres('SRSR',_in)=type_of(null()) & _in.SRSR<>null() & SRSR.seek(_in.SRSR,ref_name(_in.SRSR),1)
   || {? exec('usr_fjks','b_perm',SRSR.ODD,USERS.ref())
      || _result:=1
      ?}
   || _result:=1
   ?};
   SRSR.cntx_pop()
?};
USERS.cntx_pop();
_result


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na opis czynności na liście ToDo
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('SRSR',_in)=type_of(null()) & _in.SRSR & SRSR.seek(_in.SRSR,ref_name(_in.SRSR),1)
|| 'Drukuj kod kreskowy dla środka: %1'@@[exec('record','#to_string',_in.SRSR)]
|| 'Drukuj kod kreskowy'@@
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wydruk kodów kreskowych
::  OLD: \wydruk/skid_uz.fml
::----------------------------------------------------------------------------------------------------------------------7
SRST.cntx_psh(); SRST.prefix();
ODD.cntx_psh();
exec('odd_filtr','fst');
licznik:=0;
VAR_DEL.delete('BAR_SROD', 'BAR_ELEM', 'BAR_IND');
BAR_SROD:=tab_tmp(1, 'REF', 'INTEGER'    , 'Nr ref-a rekordu tabeli SRST',
                     'NRI', 'STRING[15]' , 'Nr inwentarzowy',
                     'NST', 'STRING[100]', 'Nazwa',
                     'ODD', 'STRING[8]', 'Jedn. ksieg.');
_exit:=0;
{? PAR_WYDR.TYPDR=null
|| FUN.emsg('Nie wybrano typu drukarki w parametrach wydruków użytkownika\n'
            '(Ustawienia i parametryzacja -> Ustawienia -> Środki trwałe -> Parametry urządzeń użytkownika).\n'
            'Nie można uruchomić wydruku.'@);
   _exit:=1
?};
{? ~_exit
|| {? OPERATOR.DEPT<>null
   || PAR_WYDR.ODD:=OPERATOR.DEPT
   || PAR_WYDR.ODD:=null
   ?};
   PAR_WYDR.OD_NRI:='';
   PAR_WYDR.DO_NRI:='';
   PAR_WYDR.ETY_WYB:=2;
   PAR_WYDR.win_edit('ETYKIETY');
   PAR_WYDR.hdr_edit(': na drukarce kodów kreskowych'@);
   {? PAR_WYDR.edit("exec('check_etykiety','!fst_ewi_wkkr')") || _exit:=0 || _exit:=1 ?}
?};
{? ~_exit || exec('barcode', '!fst_ewi_wkkr', 0) ?};
ODD.cntx_pop();
SRST.cntx_pop();
&licznik;
VAR_DEL.delete('BAR_SROD')


\check_etykiety
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2006]
:: OPIS: Formula sprawdza wypelnienie pol w oknie parametrow druku etykiet
::----------------------------------------------------------------------------------------------------------------------
exec('check_etykiety','fst')


\barcode
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2006]
:: OPIS: Formula do druku kodow kreskowych na drukarkach Zebra, Datamax, Clever
::   WE: Jesli _a jest i jest rowne 1 to wydruk arkusza etykiet na standardowej drukarce,
::       jesli _a nie ma lub jest = 0, wydruk na drukarce kodow kreskowych
::----------------------------------------------------------------------------------------------------------------------
exec('barcode','fst')


\ae_ety_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2006]
:: OPIS: Formula po rdakcji pola PAR_WYDR.ETY_WYB
::  OLD: \ae_ety_wyb/wydr_es.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_WYDR.ETY_WYB <> 3
|| PAR_WYDR.OD_NRI:='';
   PAR_WYDR.DO_NRI:=''
?};
1


\be_odnri
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2006]
:: OPIS: Formula przed redakcja pol PAR_WYDR.OD_NRI i PAR_WYDR.DO_NRI
::  OLD: \be_odnri/wydr_es.fml
::  OLD: \be_odnri/!fst_ewi_wkks.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_WYDR.ETY_WYB = 3 || 1 || 0 ?}


\f3_odnri
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2006]
:: OPIS: Formula na F3 w polach PAR_WYDR.OD_NRI i PAR_WYDR.DO_NRI
::  OLD: \f3_odnri/wydr_es.fml
::----------------------------------------------------------------------------------------------------------------------
_nri:=fld();
_ind:=SRST.index('?');
_win:=exec('create_win_srst','fst');
SRST.win_sel(_win);
{? PAR_WYDR.ZAKRESDR=0
|| {? PAR_WYDR.ODD<>null
   || SRST.index('OKR_O_W'); SRST.prefix(SSTALE.AO,PAR_WYDR.ODD)
   || SRST.index('OKR_O_W'); SRST.prefix(SSTALE.AO)
   ?}
|? PAR_WYDR.ZAKRESDR=1
|| {? PAR_WYDR.ODD<>null
   || SRST.index('OKRES_W'); SRST.prefix('T',SSTALE.AO,PAR_WYDR.ODD().OD,)
   || SRST.index('OKRES_W'); SRST.prefix('T',SSTALE.AO)
   ?}
|? PAR_WYDR.ZAKRESDR=2
|| {? PAR_WYDR.ODD<>null
   || SRST.index('OKRES_W'); SRST.prefix('N',SSTALE.AO,PAR_WYDR.ODD().OD,)
   || SRST.index('OKRES_W'); SRST.prefix('N',SSTALE.AO)
   ?}
?};
{? SRST.select() || _nri:=SRST.NRI ?};
SRST.index(_ind);
_nri

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:39 b3ddf9e7cd7e774e08d32e23f1e7c9ad113a970106d2eba1fd9791357c2887baca4fbc338e90c68c51dfb7d37fbc47b9eaf752e2b3b5b581c45f06159d1d2a5d3f0540d31f975b61edf941f0730914c62c1cf5b968f14701fb1d18d9a910bce65dc254c917f05d297879e3cdd808e0878125e9a17fdbee8d0c5347f514830962

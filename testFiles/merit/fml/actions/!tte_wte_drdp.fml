:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !tte_wte_drdp.fml
:: Utworzony: 02.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły czynności TTE_WTE_DRDP - Rejestrowanie pozostałych danych wzorca technologii
::            Uwaga: większość kodu jest wspólna z czynnością TTE_TEC_DRDP - w przypadku zmian modyfikować oba pliki
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Główna formuła czynności rejestrowania pozostałych danych dla wzorca technologii (TTE_WTE_DRDP)
::       Zadaniem formuły jest zredagowanie parametrów, produktów, atrybutów, dokumentów i N-P-U wzorca technologii
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_int:=params_get().int;
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;

:: UPRAWNIENIA
::# permissions=ODDZ

:: PARAMETRY WE:
::# kind=WE, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=T, keyref=T
{? var_pres('TKTL',_in)<>type_of(~~) & var_pres('TKTL',_in)<>type_of(null()) || return() ?};
{? var_pres('TKTL',_in)=type_of(~~) || return() ?};

:: PARAMETRY WY:
::# kind=WY, symbol=TKTL, type=_TKTL, name=Wskazanie na wzorzec technologii, required=N
{? var_pres('TKTL',_out)<>type_of(~~) & var_pres('TKTL',_out)<>type_of(null()) || return() ?};

{? _mp.isMicro()
||
:: Obsługa poza procesem
   _akcja:=_mp.akcja();

:: Zakończenie rejestrowania
   {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
   || {? exec('valid','tech_prod',_in.TKTL)
            &
         FUN.ask('Czy zakończyć rejestrację pozostałych danych dla całego wzorca technologii?'@)

      || exec('FindAndGet','#table',TKTL,_in.TKTL,,"STAT_P:='T'; STAN:='P'; put()")
      ?}

:: Redagowanie N-P-U (do wzorca albo do operacji)
   |? _akcja='NPU'
   || exec('tools_main','tech_tool',_in.TKTL,_context.oper,_context.npu,_context.init,_context.used)

:: Parametry (do wzorca)
   |? _akcja='PAR'
   || exec('show_tpart','tech_param',_in.TKTL)

:: Atrybuty (do wzorca)
   |? _akcja='ATR'
   || exec('TKTL','tech_atr',_in.TKTL)

:: Dokumentacja (do wzorca)
   |? _akcja='DOK'
   || exec('TKTL','tech_doc',_in.TKTL)

   || FUN.info('Nie obsłużony kontekst wywołania.'@)
   ?}
||
:: Obsługa w procesie
   exec('tpoz_in_process','!tte_wte_drdp',_in,_int,_out,_mp,_context)
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opis dla czynności rejestrowania pozostałych danych dla wzorca technologii (TTE_WTE_DRDP)
::       UWAGA: do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: zwraca opis Zadania
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;

_desc:='';
_keyRefs:=_mp.getRefs();
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest rekord kluczowy to ustawiam odpowiedniego TKTL
{? var_pres('[1]',_keyRefs)
|| _tmp:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj pozostałe dane dla wzorca technologii %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_keyRefs[1],,"'Zredaguj pozostałe dane dla wzorca technologii %1 wer. %2'[NRK,WER]",'')

:: jest parametr wejściowy to ustawiam odpowiedniego TKTL
|? var_pres('TKTL',_in)
|| _tmp:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"NRK+' ||| '+WER",'');
   _tab_tmp:=spli_str(_tmp,' ||| ');
   _desc:={? _tmp<>'' || 'Zredaguj pozostałe dane dla wzorca technologii %1 wer. %2'@@[_tab_tmp[1],_tab_tmp[2]] || '' ?}
::|| _desc:=exec('FindAndGet','#table',TKTL,_in.TKTL,,"'Zredaguj pozostałe dane dla wzorca technologii %1 wer. %2'[NRK,WER]",'')

?};
_desc


\tpoz_in_process
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła rejestrowania pozostałych danych dla wzorca technologii
::       Używana przez exec('main','!tte_wte_drdp')
::   WE: _a - [obj_new] - parametry wejściowe czynności
::       _b - [obj_new] - parametry wewnętrzne czynności
::       _c - [obj_new] - parametry wyjściowe czynności
::       _d - obiekt odpowiedzialny za obsługę procesu
::       _e - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_int:=_b;
_out:=_c;
_mp:=_d;
_context:=_e;

_torw:=VAR.A_TORW:='W';
_arch:=VAR.ARCH:='N';

_akcja:=_mp.akcja();

_ok:=0;
_res:=~~;

{? _in.TKTL
||
   {? exec('FindAndGet','#table',TKTL,_in.TKTL,,"TORW",'')<>_torw
   || _msg:='Niezgodność wywołania czynności.\nNie można redagować pozostałych danych dla karty technologicznej.';
      FUN.emsg(_msg);
      _mp.error(_msg)

   |? exec('FindAndGet','#table',TKTL,_in.TKTL,,"ARCH",'')<>_arch
   || _msg:='Niezgodność wywołania czynności.\nNie można redagować pozostałych danych dla wzorca archiwalnego.';
      FUN.emsg(_msg);
      _mp.error(_msg)

   ||
      exec('tktl_cntx_psh','tech_common');
      exec('tktl_use','tech_common',ref_name(_in.TKTL)+3);
      TKTL.clear();

      {? TKTL.seek(_in.TKTL)
      ||
         {? TKTL.STAT_P='T'
         || FUN.info('Zakończono redagowanie pozostałych danych dla wzorca technologii.'@)

         || _mp.keyRef(TKTL.uidref());
            _mp.descTodo();

::          Zmienne
            VAR.A_KTL:=TKTL.ref();
            VAR.GRUPA:='N';

::          Właściwa obsługa
::          Zakończenie rejestrowania
            {? _akcja='ZAKOŃCZ_Z_PYTANIEM'
            || {? exec('valid','tech_prod',TKTL.ref())
                     &
                  FUN.ask('Czy zakończyć rejestrację pozostałych danych dla całego wzorca technologii?'@)

               || TKTL.STAT_P:='T';
                  TKTL.STAN:='P';
                  TKTL.put()
               ?}

::          Redagowanie N-P-U (do wzorca albo do operacji, teraz zawsze wyświetlane wszystkie)
            |? _akcja='NPU'
            || {? 1
::                  TKTL.TYP().UTIL='K' | _context.oper<>null()
               || _res:=exec('tools_main','tech_tool',_in.TKTL,_context.oper,_context.npu,_context.init,_context.used)
               || exec('menu_start','tech_head');
                  exec('tree','tech_oper',5);
                  exec('menu_stop','tech_head');
                  _res:=''
               ?}

::          Parametry (do wzorca)
            |? _akcja='PAR'
            || _res:=exec('show_tpart','tech_param',TKTL.ref())

::          Atrybuty (do wzorca)
            |? _akcja='ATR'
            || _res:=exec('TKTL','tech_atr',TKTL.ref())

::          Dokumentacja (do wzorca)
            |? _akcja='DOK'
            || _res:=exec('TKTL','tech_doc',TKTL.ref())

::          Lista TODO
            |? _akcja=''
            || _view:=0;
               {? TKTL.STAT_N='N'
               || _choice:=FUN.choice('Nie zakończono rejestracji nagłówka wzorca.'@,,'Zakończ nagłówek i redaguj'@,'Podgląd'@);
                  {? _choice=1
                  || _args:=exec('mp_run_a','#b__box');
                     _args.ACT_UID:='TTE_WTE_DRTE';
                     _args.UIDREF:=TKTL.uidref();
                     _args.AKCJA:='ZAKOŃCZ_BEZ_PYTANIA';
                     exec('mp_run','#b__box',_args);
                     TKTL.get()
                  || _view:=1
                  ?}
               || _choice:=2
               ?};
               {? _choice>0
               || _red:=TKTL.mk_edit('Dane pozostałe wzorca technologii'@+{? _view || ' — '+'PODGLĄD'@ || '' ?},,'#tktl_w_dred');
                  TKTL.win_ewin(_red,,'DISP_');
                  TKTL.win_edit(_red);
                  _btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['N-P-U'@],"exec('action_tools','tech_tool')");
                  TKTL.btn_eopt(_red,_btn,{? TKTL.TYP().UTIL='N' || 'state=grayed' || 'state=normal' ?});
                  _btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['Parametry'@],"exec('action_params','!tte_wte_drdp')");
                  TKTL.btn_eopt(_red,_btn,{? TKTL.TYP().PAR='N' || 'state=grayed' || 'state=normal' ?});
                  _btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['Atrybu&ty'@],"exec('action_attrib','!tte_wte_drdp')");
                  _btn:=TKTL.win_ebtn(_red,'text=%1,align=begin'['Doku&mentacja'@],"exec('action_docum','!tte_wte_drdp')");
                  {? exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRDP')
                  || _btn:=TKTL.win_ebtn(_red,'text=%1,align=end'['&Zakończ'@],"exec('tpoz_dalej','!tte_wte_drdp')");
                     TKTL.btn_eopt(_red,_btn,{? TKTL.STAT_N='N' || 'state=grayed' || 'state=normal' ?})
                  ?};
                  _btn:=TKTL.win_ebtn(_red,'text=%1,align=end'['&Anuluj'@],"'key:Esc'");
                  {? TKTL.edit()
                  || {? TKTL.STAT_P='T'
                     || TKTL.put();
::                   Uruchomienie ponownie instancji tej samej czynności może zmienić status, więc przywracam
::                        _mp.coreKeep();
                        ~~
                     ?}
                  ?}
               ?}
            ?}
         ?};

         TKTL.clear();
         {? TKTL.seek(_in.TKTL)
         ||
::          Ustawiony status - czynność zostaje zakończona
            {? TKTL.STAT_P='T'
            || _out.TKTL:=TKTL.ref();
               _mp.save(,_out);
               _mp.done();
               _res:='key:Esc'

            ?}
         ?}
      ?};

      exec('tktl_cntx_pop','tech_common')

   ?}

|| _msg:='Nie przekazono albo usunięto wzorzec technologii.';
   FUN.emsg(_msg);
   _mp.error(_msg)
?};

_res


\action_params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie pozostałych danych dla wzorca technologii - akcja 'Parametry' z menu okna wertowania
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N='N'
|| set_help(exec('set_help','#help','TTE_WTE_DRDP'));
   exec('show_tpart','tech_param',TKTL.ref(),1)

|? TKTL.ARCH='N' & TKTL.STAT_P='N' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRDP')
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRDP';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='PAR';

   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| set_help(exec('set_help','#help','TTE_WTE_DRDP'));
   exec('show_tpart','tech_param',TKTL.ref(),1)
?}


\action_attrib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie pozostałych danych dla wzorca technologii - akcja 'Atrybuty' z menu okna wertowania
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N='N'
|| set_help(exec('set_help','#help','TTE_WTE_DRDP'));
   exec('TKTL','tech_atr',TKTL.ref())

|? TKTL.ARCH='N' & TKTL.STAT_P='N' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRDP')
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRDP';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='ATR';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| set_help(exec('set_help','#help','TTE_WTE_DRDP'));
   exec('TKTL','tech_atr',TKTL.ref())
?}


\action_docum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie pozostałych danych dla wzorca technologii - akcja 'Dokumentacja' z menu okna wertowania
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_N='N'
|| set_help(exec('set_help','#help','TTE_WTE_DRDP'));
   exec('TKTL','tech_doc',TKTL.ref())

|? TKTL.ARCH='N' & TKTL.STAT_P='N' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRDP')
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:='TTE_WTE_DRDP';
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='DOK';
   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| set_help(exec('set_help','#help','TTE_WTE_DRDP'));
   exec('TKTL','tech_doc',TKTL.ref())
?}


\action_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zakończenie rejestrowania pozostałych danych dla wzorca technologii
::       - obsługa przycisku w oknie kart technologicznych (obsługa z pytaniem)
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_P='N'
|| {? exec('tktl_lock','tech_common',,'P')
   || _args:=exec('mp_run_a','#b__box');
      _args.ACT_UID:='TTE_WTE_DRDP';
      _args.UIDREF:=TKTL.uidref();
      _args.AKCJA:='ZAKOŃCZ_Z_PYTANIEM';
      _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
      exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

      exec('mp_run','#b__box',_args);

      exec('tktl_unlock','tech_common',,'P')
   ?}

|| FUN.info('Zakończono rejestrację pozostałych danych dla wzorca technologii.\nModyfikacje niemożliwe.'@)
?};
'key:Esc'


\tpoz_dalej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obsługa przycisku 'Zakończ' w oknie redagowania TKTL
::       Zatwierdza dodatkowe dane i popycha proces dalej
::       Kontekst wywołania - rekord TKTL
::----------------------------------------------------------------------------------------------------------------------
_result:='';

:: Rekord mógł być zmodyfikowany przez inne czynności, więc go ponownie wczytuję
{? TKTL.get()
|| {? TKTL.STAT_P='T'
   || FUN.info('Rejestracja pozostałych danych wzorca technologii została już zakończona.'@);
      _result:='key:Esc'
   |? TKTL.STAT_N='N'
   || FUN.info('Nie zakończono redagowania nagłówka wzorca technologii.'@)
   || {? exec('tktl_lock','tech_common',TKTL.ref(),'P')
      || {? exec('valid','tech_prod',TKTL.ref())
               &
            FUN.ask('Czy zakończyć rejestrację pozostałych danych dla całego wzorca technologii?'@)
         || TKTL.STAT_P:='T';
            TKTL.STAN:='P';
            _result:='key:F2'
         ?};
         exec('tktl_unlock','tech_common',TKTL.ref(),'P')
      ?}
   ?}
|| _result:='key:Esc'
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:09:37 e203ee6d88bcc47f6bf760671b602a97b6a6ac58796e4e578c87115c7d114942afcfe4fe2417e15416167d99147fa68a416b9611e06415fe5f9a38342bd6ff2a7cec9642bdd37491ece2284e8eec3a0472a56393b5c6e8e2e04d42ee5764ca7660db9f44db5cc709a46a5238e38deefb1d9ae20e1deb511842460d968b3a511e

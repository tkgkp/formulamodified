:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: !lsp_fak_zwfs.fml
:: Utworzony: 04.05.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Utworzenie dokumentu w PDF
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła główna czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
::# permissions=ODDZ,LSP
::# parses=exec('parses','!lsp_fak_zwfs')
::# kind=WE, symbol=FAKS,    type=_FAKS,  name=Dokument sprzedaży, required=T, keyref=T
::# kind=WE, symbol=CZY_PDF, type=STRING, name=Utwórz PDF,         required=N, keyref=N, fml_val="exec('answer2str','params','TAK','NIE','Czy tworzyć plik w formacie PDF?')"
::# kind=WE, symbol=PRN_MSK, type=STRING, name=Maska wydruku,      required=N, keyref=N
::# kind=WY, symbol=DOKUM,   type=_DOKUM, name=Załącznik,          required=N

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

exec('init_fak','lsp');

_akcja:=_mp.akcja();
_auto:=_mp.isAutoRun();
_group:=_mp.isGroup();

_in_pdf:={? var_pres('CZY_PDF',_in)=type_of('') || _in.CZY_PDF || 'NIE' ?};
_czy_pdf:={? _akcja<>'' || ~(_akcja*'Drukuj') || _in_pdf='TAK' | _in_pdf='E-DOKUMENT' ?};
_czy_edokument:={? _akcja<>'' || (_akcja*'E-dokument')>0 || _in_pdf='E-DOKUMENT' ?};
_rodzaje_edokumentu:='';
_keep:=_akcja<>'' & {? _in_pdf='TAK' || _akcja*'Drukuj' || ~(_akcja*'Drukuj') ?};

_msk:={? var_pres('PRN_MSK',_in)=type_of('') & _in.PRN_MSK<>'' & fexists(_in.PRN_MSK+'.rpt',1)
      || _in.PRN_MSK
      || ''
      ?};


{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
||
:: Ustawienie kontekstu wg instancji elementu w procesie
   FAKS.cntx_psh();
   FAKS.prefix();
   {? FAKS.seek(_in.FAKS)
   ||
      _continue:=1;
      _rodzaje_edokumentu:=exec('rodzaje_edokumentu','zbl_dok',exec('kh_dokum','zbl_dok',FAKS.ref()),FAKS.T,FAKS.DW,
         FAKS.WHERE,FAKS.MANSKSEF);
      _faks_to_string:=exec('record','#to_string',FAKS.ref());
      _bl_dokum_uzu_pdf:=_rodzaje_edokumentu*'ksef'
            & exec('bl_dokum_seek','zbl',FAKS)
            & exec('dokumz','edi_fo_ufd',DOKUM.ref(),'pdf')=null();
      {? _bl_dokum_uzu_pdf & FAKS.NRKSEF=''
      ||
         exec('add_kom','#message','Utworzenie wydruku PDF do E-dokumentu jest dostępne po jego potwierdzeniu..'@,
            7,_faks_to_string);
         _continue:=0
      ?};
      {? _czy_edokument & _rodzaje_edokumentu=''
      ||
         exec('add_kom','#message','Należy użyć opcji Drukuj lub Utwórz PDF.'@,7,_faks_to_string);
         _continue:=0
      ?};
      {? _rodzaje_edokumentu<>'' & _rodzaje_edokumentu<>'|e-mail|'
            &
         _continue>0 & _czy_edokument & exec('chk_role','#b__box',OPERATOR.USER,'ZBL_DOK_RBUF')=0
      ||
         exec('add_kom','#message','Brak uprawnień do czynności: ZBL_DOK_RBUF Obsługa dokumentów Businesslink.'@,
            7,_faks_to_string);
         _mp.done();
         _continue:=0
      ?};
      {? _continue>0 & _czy_edokument & FAKS.STAT_REJ<>'T'
      ||
         exec('add_kom','#message','Opcja E-dokument jest niedostępna dla niezaakceptowanego dokumentu.'@,
            7,_faks_to_string);
         _mp.done();
         _continue:=0
      ?};
      {? _continue>0 & _czy_edokument & FAKS.STATKSEF<>'' & FAKS.STATKSEF<>exec('ksef_donot_applay','zbl_stat')
         & FAKS.STATKSEF<>exec('before_send','zbl_stat') & _bl_dokum_uzu_pdf=0
      ||
         exec('add_kom','#message',
               'Opcja E-dokument jest dostępna tylko dla dokumentu o statusie "%1". Obecny status to "%2"'@
               [exec('before_send','zbl_stat'),FAKS.STATKSEF],
               7,_faks_to_string);
         _mp.done();
         _continue:=0
      ?};
      {? _continue>0 & (_msk='' | _msk*'?' | _msk*'*' ) & _czy_edokument
      ||
         _msk:=exec('msk','dp_wydrukow',FAKS.ref(),'E-dokument');
         {? _msk=''
         ||
            _txt:='Nie znaleziono domyślnych parametrów wydruku.'@;
            {? _group
            ||
               exec('add_kom','#message',_txt,7,_faks_to_string)
            ||
               FUN.info(_txt)
            ?};
            _mp.done();
            _continue:=0
         ?}

      ?};
      {? _continue>0
      ||
::       grupowy wydruk
         _grp_dr:=exec('czy_grp_dr','faktury_wydruk');
::       grupowy wydruk w procesie (czynność uruchomiona automatycznie)
         _grp_dr_proc:=_grp_dr=0 & _group;
         {? _grp_dr_proc
         ||
            {? FAKS.EDOKUMEN='T'
            ||
               exec('grp_dr_done','param_wydr');
::             __GRP_DR - srodowisko grupowego drukowania
               __GRP_DR:=exec('grp_dr_init','param_wydr');
::             __GRP_DR.GR - [0/1] - wydruk grupowy
               __GRP_DR.GR:=1;
::             __GRP_DR.FIRST - drukowanie pierwszego dokumentu sposrod zaznaczonych 0-nie 1-tak
               __GRP_DR.FIRST:=0;
::             __GRP_DR.PDF - utworz pdf 0-nie 1-tak
               __GRP_DR.PDF:=1;
::             __GRP_DR.SHOW_PDF - wyswietlac utworzone pdf 0-nie 1-tak
               __GRP_DR.SHOW_PDF:=0;
               {? FAKS.SZ='S' & FAKS.WHERE='G' & ~exec('dok_maga','powdok',2)
               ||
                  _p300218:=exec('get', '#params',300218);
                  __GRP_DR.DR_N_GWZ:=
                     {? _p300218='T' || 1
                     |? _p300218='N' || 0
                     || FUN.ask('Brak wydania z magazynu do faktury.\nCzy drukować dokument?'@
                           ,,,'GRP_LSP_FAK_ZWFS',__kom)
                     ?}
               ?}
            ||
               _grp_dr_show_pdf:=FUN.ask('Wyświetlić wygenerowane dokumenty?'@,,,'GRP_LSP_FAK_ZWFS',__kom);
               _grp_dr_first:=FUN.grp_auto()=0;
               exec('grp_dr_done','param_wydr');
::             __GRP_DR - srodowisko grupowego drukowania
               __GRP_DR:=exec('grp_dr_init','param_wydr');
::             __GRP_DR.GR - [0/1] - wydruk grupowy
               __GRP_DR.GR:=1;
::             __GRP_DR.FIRST - drukowanie pierwszego dokumentu sposrod zaznaczonych 0-nie 1-tak
               __GRP_DR.FIRST:=_grp_dr_first;
::             __GRP_DR.PDF - utworz pdf 0-nie 1-tak
               __GRP_DR.PDF:=_czy_pdf;
::             __GRP_DR.SHOW_PDF - wyswietlac utworzone pdf 0-nie 1-tak
               __GRP_DR.SHOW_PDF:=_grp_dr_show_pdf
            ?}
         ?};
         {? FAKS.EDOKUMEN='T' & _akcja='' & exec('bl_dokum_seek','zbl',FAKS) & (_bl_dokum_uzu_pdf=0 | FAKS.NRKSEF='')
         ||
            _result:=obj_new('REF','WYN');
            _result.WYN:=1;
            _result.REF:=DOKUM.ref()

         |? FAKS.EDOKUMEN='T' & _akcja='' & _auto
         ||
            _result:=obj_new('REF','WYN');
            _result.WYN:=1;
            _result.REF:={? exec('dokum_seek','zbl',FAKS) || DOKUM.ref() || null() ?}

         |? _czy_edokument & exec('bl_dokum_seek','zbl',FAKS) & _bl_dokum_uzu_pdf=0
         ||
            exec('bl_act','zbl_dok','PDW',DOKUM.ref());
            _result:=obj_new('REF','WYN');
            _result.WYN:=1;
            _result.REF:=DOKUM.ref()

         |? _grp_dr_proc & ~_czy_pdf
         ||
            _result:=obj_new('REF','WYN');
            _result.WYN:=1
         ||
            _result:=exec('fak_druk','faktury_wspolne',_czy_pdf,_msk,_auto,_czy_edokument,_rodzaje_edokumentu)
         ?};
         {? _czy_pdf & _result.WYN
         ||
            _out.DOKUM:=_result.REF;
            _mp.save(,_out);
            {? ~_keep || _mp.done() ?}

         |? ~_czy_pdf & _result.WYN
         ||
            {? ~_keep || _mp.done() ?}
         ?};
         {? _grp_dr_proc || exec('grp_dr_done','param_wydr') ?}
      ?}
   ||
      _mp.error('Nie znaleziono dokumentu.')
   ?};
   FAKS.cntx_pop()
||
   _mp.error('Brak parametru wejściowego FAKS.')
?}


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła TO-DO
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       mp  - obiekt odpowiedzialny za obsługę procesu
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_in:=_mp.load(exec('kind_in','#b_port'));

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
||
   {? _in.CZY_PDF='NIE'
   || 'Wydrukuj dokument sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
   || 'Utwórz PDF dokumentu sprzedaży: %1'@@[exec('record','#to_string',_in.FAKS)]
   ?}
|| ''
?}


\faks_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument sprzedaży - Utwórz PDF
::----------------------------------------------------------------------------------------------------------------------
exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Area Utwórz PDF')


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła ustala PARSES
::   WE: UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;

{? var_pres('FAKS',_in)=type_of(null()) & _in.FAKS
|| FAKS.cntx_psh();
   FAKS.use(ref_name(_in.FAKS));
   FAKS.prefix();
   {? FAKS.seek(_in.FAKS)
   || __PARSES.setVal('OddzialLogProd',FAKS.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LSP';
      _args.AR:=FAKS.AR;
      _args.AM:=FAKS.AM;
      __PARSES.setVal('OkresRok',_args)
   ?};
   FAKS.cntx_pop()
?};

1


\faks_prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument sprzedaży - Drukuj
::----------------------------------------------------------------------------------------------------------------------
exec('fak_druk_gr','faktury_wspolne',0,'LSP_FAK_ZWFS','Area Drukuj')


\faks_pdf_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument zaliczkowy - Utwórz PDF
::  OLD: \fzal_prn/zalicz.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fzal_prn','faktury_zalicz',1)


\faks_prn_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dokument zaliczkowy - Drukuj
::  OLD: \fzal_prn/zalicz.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fzal_prn','faktury_zalicz',0)


\czy_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: fml_val parametru wejściowego CZY_PDF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Czy tworzyć plik w formacie PDF?',,'E-DOKUMENT','TAK','NIE');
{? _choice=1 || 'E-DOKUMENT'
|? _choice=2 || 'TAK'
|? _choice=3 || 'NIE'
|| ~~
?}


\faks_edokument
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Dokument sprzedaży - Edokument
::----------------------------------------------------------------------------------------------------------------------
exec('fak_druk_gr','faktury_wspolne',1,'LSP_FAK_ZWFS','Area E-dokument')

:Sign Version 2.0 jowisz:1045 2024/02/05 13:31:36 47d6de2b6f1e25248a7f52a5f4e8daed6c1a66752d8413c0b8aaa96fdc064c8edf5e5c639fcac90bfff1cefceab8a410bdb0a0e2516278099d214369f8d8ebcc491c057bfd5755d21c2871262a60f4816c019c68e499ed494e5cada64b99b6bcd62cbfada0afc93ba4d04cb719461d8ddd3439f2bf685e8d5eafd08f28483f9f

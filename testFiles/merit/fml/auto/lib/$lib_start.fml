:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku:
:: Utworzony: 26.10.2018
:: Autor: GZ
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================


\start_testy_nocne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS: Uruchamia nocne testy
::   WE:
::   WY:
:: ~OST: INENVGET,INFERASE,INFEXISTS
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
:: Inicjalizacja obiektu testowego
{? envget('@MacroTestyZero')='1'
|| _test_driver:=exec('init','$lib_base','$autotest_zero.log')
|? envget('@NEW_FIRM')='1'
|| _test_driver:=exec('init','$lib_base','$test_new_firm.log')
|| _test_driver:=exec('init','$lib_base','$autotest.log')
?};
{? var_pres('PLACE_LP',@.CLASS)<0
|| exec('place_listy_plac','$listy_plac')
?};
:: inicjalizacja obiektu STR
exec(,'_string');
{? var_pres('STR')<>type_of(@.CLASS.STRING)
|| STR:=obj_new(@.CLASS.STRING)
?};
:: @WHEN
:: Zalogowanie się na pulpit
_timeout:=_test_driver.timeout;
_test_driver.timeout:=900000;
_pulpit:=_test_driver.app_open('admin', 'admin123', 'firma', 'merit001', '', '');
_test_driver.timeout:=_timeout;

:: Testy. Jeżeli dane pełne, uruchamiamy testy dla danych pełnych. Jeżeli puste, dla pustych.

{? envget('@MacroTestyZero')='1'
|| exec('testy_zero','$lib_start',_test_driver,_pulpit)
|? envget('@NEW_FIRM')='1'
|| exec('new_firm','$lib_start',_test_driver,_pulpit)
|| exec('testy','$lib_start',_test_driver,_pulpit)
?};

:: @THEN
:: Zamknięcie testowanej aplikacji, skopiowanie wyników
_test_driver.app_close(_pulpit);
:: informacja dla jenkins, że wszystko poszło ok
{? fexists('@c:\\auto\\jenkins\\jenkins_ok.txt',0) || ferase('@c:\\auto\\jenkins\\jenkins_ok.txt',0) ?};
~~


\start_testy_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS:
::   WE:
::   WY:
:: ~OST: INENVGET,INFERASE,INFEXISTS
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
:: Inicjalizacja obiektu testowego
::_test_driver:=exec('init','$lib_base');
:: inicjalizacja obiektu STR
::exec(,'_string');
::{? var_pres('STR')<>type_of(@.CLASS.STRING)
::|| STR:=obj_new(@.CLASS.STRING)
::?};

:  aktualizacja plików do wykonania testów portalu
::system('sh "/home/macroadm/docker/portaltesty/scripts/testy_act"');
:: @WHEN
{? envget('@MacroTestyZero')='1'
|| ~~
||
   exec('businesslink_ksef','$test_businesslink_ksef');
   exec('portal_adres','$test_portal_adres',,0);
   exec('portal_urlop','$test_portal_urlop',,0);
   exec('portal_zatrudnienie','$test_portal_zatrudnienie',,0,'Zmiana zatrudnienia');
   exec('portal_zatrudnienie','$test_portal_zatrudnienie',,0,'Wniosek o zatrudnienie');
   exec('portal_zatrudnienie','$test_portal_zatrudnienie',,0,'POM zmiana zatrudnienia')
?};
:: informacja dla jenkins, że wszystko poszło ok
::{? fexists('@c:\\auto\\jenkins\\jenkins_ok.txt',0) || ferase('@c:\\auto\\jenkins\\jenkins_ok.txt',0) ?};
~~


\start_testy_z_aplikacji
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS: Uruchamia testy z dowolnej zakładki (gdy już jest otwarty pulpit)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
:: Inicjalizacja obiektu testowego
exec('init','__test');
{? var_pres('PLACE_LP',@.CLASS)<0
|| exec('place_listy_plac','$listy_plac')
?};
:: inicjalizacja obiektu STR
exec(,'_string');
{? var_pres('STR')<>type_of(@.CLASS.STRING)
|| STR:=obj_new(@.CLASS.STRING)
?};
_test_driver:=obj_new(@.CLASS.AUTOTEST);
_test_driver.logger.log_actions:=1;
_test_driver.exec_after_error:=0;
_test_driver.set_lib_mock_enabled('', '', 0);
_test_driver.set_lib_mock_enabled('mbutil.jar', '', 1);
_test_driver.set_lib_mock_enabled('filedlg.jar', '', 1);
_test_driver.set_ctrl_mock_enabled('', '', 0);
_test_driver.set_ctrl_mock_enabled('!application>ctr:filechooser', '', 1);

:: @WHEN
_pulpit:=_test_driver.app_activate('1');

:: Testy
{? _test_driver.execute("OKRO.size>0")
|| exec('testy','$lib_start',_test_driver,_pulpit)
|| exec('testy_zero','$lib_start',_test_driver,_pulpit)
?};


:: @THEN
:: Wyświetlenie wyników
_test_driver.execute("exec('log_viewer','$lib_base')");
~~


\testy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS: Testy uruchamiane na kontenerze z danymi
::   WE: _a - obiekt _test_driver
::       _b - wskazanie na aplikację: Pulpit
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;
exec('test_form_chk','$test_form_chk',_test_driver);
exec('czy_zadania_bez_bledu','$test_zadania_potransferowe',_test_driver);
exec('exec_main','$test_%release',_test_driver);
exec('exec_main','$test_mbuilder',_test_driver);
exec('akceptacja_wykonanych_zad','atm');
exec(,'$test_dodaj_kalendarz_standard',_test_driver,_b);
exec('zaloz_rok','$test_uzup_param',_test_driver);
exec('zaloz_rok_000','$test_uzup_param',_test_driver);
exec('test_kontrahenci','$test_kontrahenci',_test_driver,_b);
exec('test_materialy','$test_materialy',_test_driver,_b);
exec(,'$test_import_wzorc',_test_driver,_b);
exec(,'$test_zatrudnij_nowa_osobe',_test_driver,_b);
exec(,'$test_nalicz_liste_plac',_test_driver,_b);
exec('main','$test_akc_PK1_zatr_prac',_test_driver,_b);
exec('srodki_trwale_add','$test_srodki_trwale',_test_driver,_b);
exec('hbn_memo','$test_hbn_check',_test_driver,_b);
exec('amor_nalicz','$test_naliczanie_amortyzacji',_test_driver,_b);
exec('amor_dekret','$test_dekret_amortyzacji',_test_driver,_b);
exec('test_dokumenty_ksiegowe','$test_dokumenty_ksiegowe',_test_driver,_b);
~~


\testy_zero
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.22]
:: OPIS: Testy uruchamiane na kontenerze bez danych
::   WE: _a - obiekt _test_driver
::       _b - wskazanie na aplikację: Pulpit
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;
exec(,'$test_pliki_procesow',_test_driver,_b);
exec(,'$test_dane_wzorc',_test_driver,_b);
exec('main','$test_dodanie_uzytkownikow',,_b);
exec('akceprtacja_schematow_VAT','$test_uzup_param',_test_driver);
_test_driver.app_close(_b);
_timeout:=_test_driver.timeout;
_test_driver.timeout:=900000;
_pulpit_ksiegowa:=_test_driver.app_open('ksiegowa', 'ksiegowa', 'firma', 'merit001', '', '');
_test_driver.timeout:=_timeout;
exec('parametry_pracy_zero','$test_dokumenty_ksiegowe',_test_driver);
exec('test_kontrahenci','$test_kontrahenci',_test_driver,_b);
exec('test_dokumenty_ksiegowe','$test_dokumenty_ksiegowe',_test_driver,_pulpit_ksiegowa)


\make_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [23.25]
:: OPIS: Importuje proces i dodaje nowe firmy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('importuj_proces','$test_nowa_firma');
exec('dodaj_z_katalogu','$test_nowa_firma');
exec('dodaj_z_wzor','$test_nowa_firma');
delay(300);
exec('todo_rozpocznij','$test_nowa_firma')


\start_test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Uruchamia test z administracji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_controls_table:=exec('przypadki_testowe','$lib_start');
_controls_table.first();
_win:=_controls_table.mk_sel('Zestaw testów automatycznych'@,'T',0,'wer',,,,,'U','T',,0,,'maximized');
_controls_table.win_fld(_win,_controls_table,'NR',,,);
_controls_table.win_fld(_win,_controls_table,'ZAKRES',,,);
_controls_table.win_fld(_win,_controls_table,'NAME',,,);
_controls_table.win_sel(_win);
_controls_table.win_act(_win,,'Formuła','Rozpocznij test'@,,,"sel_exit()",,1,,,,'W');
{? _controls_table.select()
|| _ref:=_controls_table.ref()
|| _ref:=null()
?};
_pulpit:='1';
{? _ref<>null
|| _controls_table.seek(_ref);
   {? _controls_table.ZAKRES<>'Testy Portal'
   ||
::  wyczyszczenie pliku log
      {? _controls_table.LOG<>''
      || _file:=fopen(_controls_table.LOG,'uw',1);
         fclose(_file)
      ?};
      {? var_pres('PLACE_LP',@.CLASS)<0
      || exec('place_listy_plac','$listy_plac')
      ?};
:: testy
      exec('init','__test');
:: inicjalizacja obiektu STR
      exec(,'_string');
      {? var_pres('STR')<>type_of(@.CLASS.STRING)
      || STR:=obj_new(@.CLASS.STRING)
      ?};
      _test_driver:=obj_new(@.CLASS.AUTOTEST);
      _test_driver.logger.log_actions:=1;
      _test_driver.exec_after_error:=0;
      _test_driver.set_lib_mock_enabled('', '', 0);
      _test_driver.set_lib_mock_enabled('mbutil.jar', '', 1);
      _test_driver.set_lib_mock_enabled('filedlg.jar', '', 1);
      _test_driver.set_ctrl_mock_enabled('', '', 0);
      _test_driver.set_ctrl_mock_enabled('!application>ctr:filechooser', '', 1);
      _pulpit:=exec('get_desctop_id','$lib_base',_test_driver);
      {? _pulpit<>'' & _controls_table.ZAKRES<>'Testy Portal'
      || _test_driver.app_activate(_pulpit)
      ?};
      params_set('test_driver',_test_driver,'pulpit',_pulpit);
      ($_controls_table.FORMULA)();
      {? _controls_table.LOG<>'' & _controls_table.LOG<>'dynamiczny'
      ||
         _error:=0;
         {? _file:=fopen(_controls_table.LOG,'ur',1)
         || {!|? (_line:=fread(_file))<>'\n' |!
               {? _line*'ERROR'>0 || _error:=1 ?};
               _file
            !};
            fclose(_file);
            {? _error
            || FUN.error('Podczas testu wystąpiły błędy.\nZostanie wyświetlona przeglądarka logów',' - Wynik testu');
               exec('log_viewer','$lib_base')
            || {? ~FUN.choice('Brak błędów w pliku log. '@,1,'OK',,,,'Przeglądarka logów',' - Wynik testu')
               || exec('log_viewer','$lib_base')
               ?}
            ?}
         ||
            FUN.error('Błąd otwarcia pliku log',' - Wynik testu')
         ?}
      ?}
   || ($_controls_table.FORMULA)()
   ?}
?};
''


\start_released
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: uruchamia testy na wersji sprzedawanej
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------

_rcv:='grzegorz.zabkiewicz@assecobs.pl,artur.sloma@assecobs.pl,wojciech.handel@assecobs.pl';
_body:=exec('test_to_string','$test_form_chk');
_body+=exec('main','$test_%release',2);
{? form(_body)<>''
|| _lacida_tab:=proc_exec('lacida_email@start_tran');
   _lacida_tab.first();
   _mail_from_lacida:=_lacida_tab.INFO;
   {? _mail_from_lacida<>''
   || _rcv+=','+_mail_from_lacida
   ?};
   exec('report_mail','$lib_base',_rcv,_body)
?}


\przypadki_testowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.14]
:: OPIS: Uzupełnia i zwraca tabelę z przypadkami testowymi
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_controls_table:=tab_tmp(3,
   'NR'  ,'INTEGER','Lp.',
   'ZAKRES','STRING[30]','Zakres kontroli'@,
   'NAME','STRING[190]','Nazwa kontroli'@,
   'FORMULA','STRING[200]','Formuła'@,
   'LOG','STRING[30]','Plik log'@,
   'OWN','STRING[1]','Własny'@
);
_ndx:=_controls_table.ndx_tmp(,1,'NR',,,'ZAKRES',,,'NAME',,);
::_ndx:=_controls_table.ndx_tmp(,1,'OWN',,1,'ZAKRES',,,'NAME',,);
_controls_table.index(_ndx);
_controls_table.import('$start_test.csv',,0,,'UTF-8,header,pth',,
   'NR'  ,,1,,
   'ZAKRES'  ,,2,,
   'NAME'  ,,3,,
   'FORMULA'  ,,4,,
   'LOG',,5,,
   'OWN',,6,);
exec('add_own_test','$lib_start',_controls_table);
::    ponowne zanumerowanie rekordów
{? _controls_table.last()
||
   {! _i:=1.._controls_table.size()
   |! _controls_table.NR:=_controls_table.size()+1-_i;
      _controls_table.put();
      _controls_table.prev()
   !}
?};
_controls_table


\add_own_test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.14]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_controls_table:=_a;

_excluded_files:=tab_tmp(1,
   'NAME','STRING[170]','Nazwa kontroli'@
);
{? _controls_table.first()
|| {! |?
      _file:=(_controls_table.FORMULA*'$test_'-1)-_controls_table.FORMULA;
      _file:=(_file*'\''-1)+_file+'.fml';
      _excluded_files.NAME:=_file;
      {? ~_excluded_files.find_rec() || _excluded_files.add() ?};
      _controls_table.next()
   !}
?};
_excluded_files.NAME:='$test_akceptacja_procesow.fml';_excluded_files.add();
_excluded_files.NAME:='$test_dane_wzorc.fml';_excluded_files.add();
_excluded_files.NAME:='$test_dodaj_kalendarz_standard.fml';_excluded_files.add();
_excluded_files.NAME:='$test_dodanie_uzytkownikow.fml';_excluded_files.add();
_excluded_files.NAME:='$test_form_chk.fml';_excluded_files.add();
_excluded_files.NAME:='$test_import_wzorc.fml';_excluded_files.add();
_excluded_files.NAME:='$test_uzup_param.fml';_excluded_files.add();
_excluded_files.NAME:='$test_zadania_potransferowe.fml';_excluded_files.add();
_excluded_files.NAME:='$test_portal_adres.fml';_excluded_files.add();
_excluded_files.NAME:='$test_mail.fml';_excluded_files.add();
_excluded_files.NAME:='$test_mail.fml';_excluded_files.add();
_excluded_files.NAME:='$test_portal_urlop.fml';_excluded_files.add();
_excluded_files.NAME:='$test_portal_zatrudnienie.fml';_excluded_files.add();
_excluded_files.NAME:='$test_businesslink_ksef.fml';_excluded_files.add();
:: ----------------------------wykluczenie do emisji 20.14--------------------------------------------------------------
_excluded_files.NAME:='$test_nalicz_liste_plac.fml';_excluded_files.add();
_excluded_files.NAME:='$test_naliczanie_amortyzacji.fml';_excluded_files.add();
:: ---------------------------------------------------------------------------------------------------------------------
_fdir:=fdir(pth_dir('$test_.fml'));
_qfdir:=fdir(pth_dir('q$.fml'));
{? _qfdir.first()
|| {! |?
      {? _qfdir.NAME*'q$'=1
      || _fdir.NAME:=_qfdir.NAME;
         _fdir.TYPE:=_qfdir.TYPE;
         _fdir.DIR:=_qfdir.DIR;
         _fdir.MOD_DATE:=_qfdir.MOD_DATE;
         _fdir.MOD_TIME:=_qfdir.MOD_TIME;
         _fdir.TREE:=_qfdir.TREE;
         _fdir.add()
      ?};
      _qfdir.next()
   !}
?};
&_qfdir;
{? fexists('$autotest.fml',1)
|| _fdir.NAME:='$autotest.fml';
   _fdir.TYPE:='f';
   _fdir.DIR:=pth_dir('autotest.fml');
   _fdir.add()
?};
{? _excluded_files.first()
|| {! |?
      {? _fdir.find_tab(,'NAME',,'=',_excluded_files.NAME) || _fdir.del() ?};
      _excluded_files.next()
   !}
?};
{? _fdir.first()
|| {! |?
      {? _fdir.NAME*'.fml'
      ||
         {? _f:=fopen(_fdir.NAME,'ur',1)
         || {! |? (_line:=fread(_f))<>'\n'
            |!
               {? 1+_line='\\'
               ||
                  _controls_table.NR:=0;
                  _controls_table.ZAKRES:='Własne testy';
                  _controls_table.NAME:='Test "'+(1-_line)+'" z pliku "'+_fdir.NAME+'"';
                  _controls_table.FORMULA:='exec(\''+(1-_line)+'\',\''+_fdir.NAME-4+'\')';
                  _controls_table.LOG:='autotest.log';
                  _controls_table.OWN:='T';
                  _controls_table.add()
               ?}
            !};
            fclose(_f)
         ?}
      ?};
      _fdir.next()
   !}
?}


\new_firm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [23.25]
:: OPIS: Importuje proces i dodaje nowe firmy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('importuj_proces','$test_nowa_firma');
exec('dodaj_z_katalogu','$test_nowa_firma');
exec('dodaj_z_wzor','$test_nowa_firma');
delay(300);
exec('todo_rozpocznij','$test_nowa_firma')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:11:20 9c50bf7bcebcfc6bbfddf5b1d8674244ec894cf69afc1fcad6bf39130e69d114eacbd4883edf79b1d1d7aebeaa6658a1058d944f367b329f46185624957a57df0ffbdbc50d71711565e7422ee15618cb3fbfd03df8ca5c6f798ef3fa79a1f4ef16770a7c0163387a3742bf89f40a7e3abb986ac79962cc16a8f61e3452fe03ea

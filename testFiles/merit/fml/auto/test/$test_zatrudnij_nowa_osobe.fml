:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test01_zatrudnij_nowa_osobe.fml
:: Utworzony: 08.11.2018
:: Autor: ARTSLO
:: Systemy:
::======================================================================================================================
:: Zawartość:
::   AREA: [ZWS_DOS], [PKD_ZAT]
:: PROCES: [PER_ZA1], [PER_ZA2]
::  TABLE: [OSOBA], [P]
::======================================================================================================================
::GIVEN
_test_driver:=exec('init','$lib_base','$test_zatrudnij_nowa_osobe.log');
:: dane przypadków i ich liczba
_osoby:=exec('importBuff','$lib_base','$zatrudnij_nowa_osobe_osoby.csv',exec('bf_os_add','$dane_osobowe'));
_size_os:=_osoby.size();
_umowy:=exec('importBuff','$lib_base','$zatrudnij_nowa_osobe_umowy.csv',exec('bf_h_um_add','$zawierane_umowy_personel'));
_size_um:=_umowy.size();
:: ustalenie uzywanych obszarów
_app:=obj_new(4);
_app[1]:=_test_driver.app_activate(_b);
_test_driver.control('!leftmenu', 'open', 'Wspólne\\Obszary\\Dane osobowe', 1);
_test_driver.app_wait_for_change();
_app[2]:=_test_driver.app_activate();
_test_driver.control('!leftmenu', 'open', 'Kadry\\Obszary\\Zawierane umowy (personel)', 1);
_test_driver.app_wait_for_change();
_app[3]:=_test_driver.app_activate();
:: tablice wyników
_wynik_os:=obj_new(_size_os);
{! _n:=1.._size_os |! _wynik_os[_n]:=0 !};
_wynik_um:=obj_new(_size_os);
{! _n:=1.._size_um |! _wynik_um[_n]:=0 !};

::WHEN
_loop:=1;
{? ~_osoby.first() || return(0) ?};
{? ~_umowy.first() || return(0) ?};
{!
|? _test_driver.app_activate(_app[2]);
   _wynik_os[_loop]:=exec('dolacz_osobe','$dane_osobowe',_test_driver,_osoby,0,'PER_ZA1');
   {? _wynik_os[_loop]
   || _test_driver.app_activate(_app[3]);
      _wynik_um[_loop]:=exec('dolacz_umowe','$zawierane_umowy_personel',_test_driver,_osoby,_umowy,0,'PER_ZA2',1)
   ?};
   _loop+=1;
   _osoby.next() & _umowy.next() & _wynik_os[_loop-1] & _wynik_um[_loop-1]
!};

::czyścimy
_test_driver.control('!leftmenu', 'open', 'Kadry\\Obszary\\Zdarzenia kadrowe', 1);
_test_driver.app_wait_for_change();

_app[4]:=_test_driver.app_activate();
exec('wait_time','$lib_base',_test_driver,time(0,0,30));

_osoby.first();
_lp:=1;
{!
|? {? _wynik_um[_lp]=1
   || {? _test_driver.exist('wnd:PKD_EZKI')
      || _test_driver.click('wnd:PKD_EZKI')
      || _test_driver.click('wnd:PKD_EZK')
      ?};
      _test_driver.filter('@P.OSOBA.NAZWISKO', _osoby.NAZWISKO, '@P.OSOBA.PIERWSZE', _osoby.PIERWSZE);
      _test_driver.click('fld:@P.OSOBA.PIERWSZE(row=1)');
      _test_driver.click('btn:aa9184814000000');
      _test_driver.click('dbtn:Tak');
      _test_driver.click('dbtn:Tak');
      _test_driver.assert('', 'wnd:@tmp.e_tmp', 'title', 'Weryfikacja usunięcia danych', 'type', 'e');
      _test_driver.click('fld:@tmp.PATTERN');
      _test_driver.send(_osoby.NAZWISKO+' '+_osoby.PIERWSZE+' 01', '', 'fld:@tmp.CONFIRM');
      _test_driver.click('btn:@OK')
   ?};
   _lp+=1;
   _osoby.next()
!};

_test_driver.app_activate(_app[2]);
_osoby.first();
_test_driver.assert('', 'wnd:zws_dos_osoba', 'title', 'Dane osobowe', 'type', 's');
{!
|? _test_driver.filter('@OSOBA.NAZWISKO', _osoby.NAZWISKO, '@OSOBA.PIERWSZE', _osoby.PIERWSZE);
   _test_driver.click('fld:@OSOBA.DOWOD(row=1)');
   _test_driver.click('btn:USUŃ');
   _test_driver.click('dbtn:Tak');
   _test_driver.click('dbtn:Tak');
   _test_driver.assert('', 'wnd:@tmp.e_tmp', 'title', 'Weryfikacja usunięcia danych osobowych', 'type', 'e');
   _test_driver.click('fld:@tmp.KEY1');
   _test_driver.send(_osoby.NAZWISKO+' '+_osoby.PIERWSZE+' '+{? _osoby.PESEL<>'' || _osoby.PESEL || _osoby.PASZPORT ?}, '', 'fld:@tmp.KEY2');
   _test_driver.send('', '{F2}', '');

   _osoby.next()
!};

::THEN
_test_driver.app_close(_app[2]);
_test_driver.app_close(_app[3]);
_test_driver.app_close(_app[4]);
_wyn1:=_wyn2:=0;
{! _n:=1.._size_os |! _wyn1+=_wynik_os[_n] !};
{! _n:=1.._size_um |! _wyn2+=_wynik_um[_n] !};
{? _wyn1=_size_os & _wyn2=_size_um
|| return(1)
|| return(0)
?}

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:03 d867198c1211cde4c0bd68618756e12070f388acf338c4969ed5197f62faccfabe81c1c4bce86034613f7dbbd024197427f3cd9c8358c6f0952affd05027b759900884c83eaf048ca93dcdd2cf303effaf63c82caae03a25e3afe7a94e056ece343d6d20e2fabdde46504df3430faf170945d926d1f91ed8c113afc116ea0a67

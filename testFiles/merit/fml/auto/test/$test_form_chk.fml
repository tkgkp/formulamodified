:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_form_chk.fml
:: Utworzony: 27.02.2019
:: Autor: GZ
::======================================================================================================================
:: Zawartość: Testy automatyczne: analiza form_chk
:: AREA: [ZWS_PAR]
::======================================================================================================================


\test_form_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.22]
:: OPIS: Testy specyfikacji
:: ~OST: INENVGET
::----------------------------------------------------------------------------------------------------------------------
::@GIVEN
_test_driver:=_a;
_svnexe:='c:\\VisualSVN\\bin\\svn.exe';
_cmd:='@'+envget('@COMSPEC')+' /c '+'c:\\auto\\tools\\svn_update.bat ';
system(_cmd,1);

_log_file:=fopen('$test_form_chk.log','uw',1);
{? _log_file
|| fwrite(_log_file,'INFO: Rozpoczęcie testu: test_form_chk ' + form(date()) + ' ' +form(time())+'\n')
|| return(0)
?};
_timeout:=_test_driver.timeout;
_test_driver.timeout:=1800000;
_app:=_test_driver.app_activate();
USERS_Z.index('USERS_Z');
USERS_Z.prefix('Lacida');
_log_txt:='';
{? USERS_Z.first()
||
   fclose(_log_file);
::   _log_txt:=_test_driver.execute($('exec(\'execute_form_chk\',\'$test_form_chk\',\''+USERS_Z.LOGIN+'\',\''+USERS_Z.HASLO+'\')'));
   _test_driver.execute($('exec(\'exec_form_chk\',\'$test_form_chk\',\''+USERS_Z.LOGIN+'\',\''+USERS_Z.HASLO+'\')'));
   _log_file:=fopen('$test_form_chk.log','ua',1);
   {? _log_txt<>''
   || fwrite(_log_file,_log_txt)
   ?}
?};
fwrite(_log_file,'INFO: Zakończenie testu: test_form_chk ' + form(date()) + ' ' +form(time())+'\n');
fclose(_log_file);
_test_driver.timeout:=_timeout;
return(1)


\exec_form_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [20.14]
:: OPIS:
::   WE: _a - user do Lacidy
::       _b - hasło do Lacidy
::   WY:
:: ~OST: INENVGET,INFERASE,INFEXISTS,INFOPEN
::----------------------------------------------------------------------------------------------------------------------
::@GIVEN
_login:=_a;
_pass:=_b;
_log_txt:='';
_log_file:=fopen('/auto/merit/robot/system/admin/$test_form_chk.log','ua',0);
::@WHEN
exec('dekl','classlist');

_result:=form_chk();

::@THEN
{? _result.first()
|| {! |?
:: 1. zapis błędu do loga
      _txt:='ERROR: '+_result.ERR_DESC+' ';
      {? _result.FORM_N<>''
      || _txt+='\\'+_result.FORM_N+'/'+_result.FILE_N
      |? _result.CLASS_N<>''
      || _txt+='klasa: '+_result.CLASS_N+', metoda: '+_result.METH_N
      || _txt+='plik: '+_result.FILE_N
      ?};
      _txt+=' | Lina: '+$_result.ERR_LINE;
      {? _log_file<>0
      || fwrite(_log_file,_txt)
      ?};
::      _log_txt+=(_txt+'\n');
:: 2. zgłoszenie błedu do lacidy
      {? var_pres('_args')>0 || &_args ?};
      _args:=exec('zglos_defekt_args','$lib_base');
      _args.LOGIN:=_login;
      _args.PASS:=_pass;
      _args.RODZAJ:='Błąd Składniowy';
      _args.APPL:='ZWS';
:: wersji nie wypeniam - chcę domyślna
      _args.ZGLOSIL:='autotesty';

:: jezeli to formuła, to winowajcą jest ostatni ją modyfikował,
:: w przeciwnym razie szukamy ostatniego modyfikującego wskazaną linię w pliku
      {? _result.FORM_N<>''
      || _blame:=exec('ostatni_modyfikujacy_fml','$lib_base',_result.FORM_N,_result.FILE_N)
      || _pthdir:=pth_dir(_result.FILE_N);
         _new_path:='c:\\auto\\wersja_rrxx\\17xx\\'+gsub(12-_pthdir,'/','\\')+'\\'+_result.FILE_N;
         _cmd:='@'+envget('@COMSPEC')+' /c '+'c:\\auto\\tools\\svn_blame.bat '
               +_new_path;
         system(_cmd,1);
         _blame:='';
         {? fexists('@c:\\auto\\wyniki\\form_chk.txt',0)
         ||
            _form_chk_results:=fopen('@c:\\auto\\wyniki\\form_chk.txt','r',0);
            {! .. _result.ERR_LINE
            |! _line:=fread(_form_chk_results)
            !};
            fclose(_form_chk_results);
            ferase('@c:\\auto\\wyniki\\form_chk.txt',0);
            {? var_pres('_form_chk_results')>100 || obj_del(_form_chk_results) ?};
            _char:=(|_line)*' ';
            _blame:=|(_char-|_line);
            _char:=_blame*' ';
            _blame:=(_char-1)+_blame
         ?}
      ?};
      {? _blame='' | _blame='awi' | _blame='izafij' | _blame='pjaskul'
      || _args.WYKON:='grzabkie'
      || _args.WYKON:=_blame
      ?};
      _args.WERYFIK:='grzabkie';
      _args.NAZ:=_txt;
      _txt:='[ŚCIEŻKA]\n';
      {? _result.FORM_N <> ''
      || _txt+='\\'+_result.FORM_N+'/'+_result.FILE_N
      |? _result.CLASS_N<>''
      || _txt+='klasa: '+_result.CLASS_N+', metoda: '+_result.METH_N
      || _txt+='plik: '+_result.FILE_N
      ?};
      _txt+=' | Lina: '+$_result.ERR_LINE
           +'\n[PARAMETRY]\n\n[KROKI]\n\n[REZULTAT AKTUALNY]\n'
           +'Podczas testów automatycznych wykryty został'
           +'błąd składniowy: '+_result.ERR_DESC;
      {? _result.FORM_N <> ''
      || _txt+='\\'+_result.FORM_N+'/'+_result.FILE_N
      |? _result.CLASS_N<>''
      || _txt+='klasa: '+_result.CLASS_N+', metoda: '+_result.METH_N
      || _txt+='plik: '+_result.FILE_N
      ?};
      _txt+=' | Linia: '+$_result.ERR_LINE + '\n\n'
           +'[REZULTAT OCZEKIWANY]\nUsunięcie błędu składniowego przed kolejną sesją testów.';
      _args.MEMO:=_txt;
      exec('zglos_defekt','$lib_base',_args);
      _result.next()
   !}
?};
obj_del(_result);
fclose(_log_file);
:return(_log_txt)
~~


\test_to_string
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=form_chk();
_string:='';
::@THEN
{? _result.first()
|| {! |?
      _string+='ERROR: '+_result.ERR_DESC+' ';
      {? _result.FORM_N<>''
      || _string+='\\'+_result.FORM_N+'/'+_result.FILE_N
      |? _result.CLASS_N<>''
      || _string+='klasa: '+_result.CLASS_N+', metoda: '+_result.METH_N
      || _string+='plik: '+_result.FILE_N
      ?};
      _string+=' | Lina: '+$_result.ERR_LINE+'\n';
     _result.next()
   !}
?};
_string

:Sign Version 2.0 jowisz:1048 2023/06/23 14:12:56 3bf1576b77da885bd7193b83d4afd5a6978fa17c1bef08620b861ba3edcb834c8c4431b579b055da3d7d237767b4a3f3b2b2bd23d49195d2e5e9decb19a230e638ea4f8013e4c1a492b084f5c7fd4cb48565735939a0d4cc6afaec50f22b3076f24ea63b57e0be3a21f7e770b6ab0fad57d0e80d0df0b5e687151bdbdefb1b8e

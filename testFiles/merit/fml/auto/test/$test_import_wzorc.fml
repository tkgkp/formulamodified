:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_import_wzorc.fml
:: Utworzony: 18.05.2020
:: Autor: ARTSLO
:: Systemy:
::======================================================================================================================
:: Zawartość: Test automatyczny dla obszaru ZWS - import poszczególnych gałęzi dany  wzorcowych z Ustawienia i
::            parametryzacja
:: AREA: [ZWS_PAR]
::   WE: _a - [aktualnie nic, względy historyczne kiedyś był przekazywany obiekt testowy]
::       _b - wskazanie na zakladke pulpitu
::   WY: 1/0 - pełne powodzenie testu lub porażka
::======================================================================================================================
_test_driver:=exec('init','$lib_base','$test_import_wzorc.log');
_test_driver.logger.info('START TESTU: $test_import_wzorc.fml (time: '+$date()+' '+(time()$3)+')');
_set:=exec('importBuff','$lib_base','$import_wzorc_ustawienia.csv',exec('bf_imp_xls','$wspolne'));
_zestawy:=exec('importBuff','$lib_base','$import_wzorc_zestawy.csv',exec('bf_imp_xls_zes','$ustawienia_i_parametryzacja'));
_set.first();
_wynik:=obj_new(_zestawy.size());
_komunikaty:=obj_new(_zestawy.size());
_blok:=0;
_war:=0;
_obszar:=obj_new('PULPIT','USTAWIENIA_PARAM');
{? _b<>~~
|| _obszar.PULPIT:=_test_driver.app_activate(_b)
|| _obszar.PULPIT:=_test_driver.app_activate('0')
?};
_loop:=1;
_timeout:=_test_driver.timeout;
_test_driver.timeout:=1800000;

::WHEN
_obszar.USTAWIENIA_PARAM:=exec('otworz_obszar','$lib_base',_test_driver,'Ustawienia i parametryzacja');

{? ~_zestawy.first()
|| _test_driver.logger.info('WYNIK TESTU: Test zakończony niepowodzeniem. Brak wskazanych zestawów do importu.');
   return(0)
?};
{!
|? _wynik[_loop]:=exec('xls_import_z_galezi','$ustawienia_i_parametryzacja',_test_driver,_set,_zestawy.KOD,_zestawy.PATH,1);
   _komunikaty[_loop]:=exec('clean_xls_kom','$wspolne',_wynik[_loop].KOM);
   {? _komunikaty[_loop].size()=0 || _wynik[_loop].warunkowe:=0 ?};
   {? _wynik[_loop].blokujace=1 || _blok:=1 ?};
   {? _wynik[_loop].warunkowe=1 || _war:=1 ?};
   _loop+=1;
   _test_driver.menu('Zwiń/rozwiń');
   _test_driver.menu('Zwiń/rozwiń');
   _zestawy.next()
!};

::THEN
_test_driver.app_close(_obszar.USTAWIENIA_PARAM);
{? _blok=0
|| _test_driver.logger.info('WYNIK TESTU: Test zakończony powodzeniem.')
|| _test_driver.logger.info('WYNIK TESTU: Test zakończony niepowodzeniem. Po imporcie były komunikaty blokujące walidacje.')
?};
{? _war=1
|| _test_driver.logger.info('WYNIK POSREDNI: Po imporcie były komunikaty nie blokujące walidacji.')
?};
{! _n:=1..obj_len(_komunikaty)
|! {? _komunikaty[_n].first()
   || {! |?
         _test_driver.logger.info('WYNIK POSREDNI: '+_komunikaty[_n].FILE+';'+_komunikaty[_n].SHEET+';'+_komunikaty[_n].ROW+';'+_komunikaty[_n].MESSAGE);
         _komunikaty[_n].next()
      !}
   ?}
!};
_test_driver.logger.info('KONIEC TESTU: $test_dane_wzorc.fml (time: '+$date()+' '+(time()$3)+')');
_test_driver.timeout:=_timeout;
return(_blok)

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:06 2979247f48ddeed45dc26319400b2136b75e90da3418e1796360fd2961b5caef331cb2cb7d6bca406889f9e9e7e2870249ed2124972cbdfc9fd79f11c43add12a4c7ff9c08c137dc673978ed7e9dda74e990421d6d0f4aca68446540e3f4fe8d9bdd342d8c0f9b323427f4e4a0d7875c7149e04e83a48e37ee50932bdfea1e23

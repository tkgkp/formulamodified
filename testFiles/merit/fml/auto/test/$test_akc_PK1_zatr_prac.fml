:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_akc_PK1_zatr_prac.fml
:: Utworzony: 25.02.2019
:: Autor: ARTSLO
::======================================================================================================================
:: Zawartość: Test automatyczny dla obszaru ZWS - dodanie nowej osoby i uzupełnienie informacji dodatkowych
::   AREA: [PKD_ZAT], [ZWS_DOS]
:: PROCES: [PER_REJ], [PER_ZA1]
::======================================================================================================================
\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO 25.02.2019
:: OPIS:
::   WE:
::   WY:
:: ~OST: INFERASE
::----------------------------------------------------------------------------------------------------------------------
::GIVEN
_test_driver:=exec('init','$lib_base','$test_akc_PK1_zatr_prac.log');
_test_driver.logger.info('START TESTU: $test_akc_PK1_zatr_prac.fml (time: '+$date()+' '+(time()$3)+')');
_osoby:=exec('importBuff','$lib_base','$PK1_osoby.csv',exec('bf_os_add','$dane_osobowe'));
_adresy:=exec('importBuff','$lib_base','$PK1_adresy.csv',exec('bf_adres','$dane_osobowe'));
_dane_ubez:=exec('importBuff','$lib_base','$PK1_dane_ubez.csv',exec('bf_dane_ubz','$dane_osobowe'));
_zalaczniki:=exec('importBuff','$lib_base','$PK1_zalaczniki.csv',exec('bf_zalacznik','$dane_osobowe'),'UTF-8');
_dane_okr_zatr:=exec('importBuff','$lib_base','$PK1_dane_okr.csv',exec('bf_okr_zatr','$dane_osobowe'));
_dane_czl_rodz:=exec('importBuff','$lib_base','$PK1_czlonek_rodziny.csv',exec('bf_czl_rodz','$dane_osobowe'));
_dane_rachunek:=exec('importBuff','$lib_base','$PK1_rachunki.csv',exec('bf_rachunek','$dane_osobowe'));
_dane_szkola:=exec('importBuff','$lib_base','$PK1_szkoly.csv',exec('bf_szkola','$dane_osobowe'));
_urz_skarb:=exec('importBuff','$lib_base','$PK1_urz_skarb.csv',exec('bf_urz_skarb','$dane_osobowe'));
_umowy:=exec('importBuff','$lib_base','$PK1_umowy.csv',exec('bf_h_um_add','$zawierane_umowy_personel'));
_badania:=exec('importBuff','$lib_base','$PK1_badania.csv',exec('bf_badanie','$zawierane_umowy_personel'));

_obszar:=obj_new('PULPIT','DANE_OSOBOWE','ZAWIERANE_UMOWY');
_obszar.PULPIT:=_test_driver.app_activate(_b);

_plik:=exec('przygotuj_plik_zal','$lib_base',_test_driver,'$file_examplePDF.pdf');

_size_os:=_osoby.size();
_size_adres:=_adresy.size();
_size_dane_ubez:=_dane_ubez.size();
_size_umowa:=_umowy.size();
::dodać sprawdzenie czy size się zgadzają

_wynik:=obj_new('osoba','adres','dane_ubez','dane_okr_zatr','dane_czl_rodz','rachunek','szkoly','urz_skarb','zalacznik');
{! _n:=1..obj_len(_wynik) |! _wynik[_n]:=0 !};
_wyniki:=obj_new(_size_os);
{! _n:=1.._size_os |! _wyniki[_n]:=_wynik !};

_wynik2:=obj_new('umowa','badania');
{! _n:=1..obj_len(_wynik2) |! _wynik2[_n]:=0 !};
_wyn_umowa:=obj_new(_size_umowa);
{! _n:=1.._size_os |! _wyn_umowa[_n]:=_wynik2 !};

_loop:=1;

::WHEN
_obszar.DANE_OSOBOWE:=exec('otworz_obszar','$lib_base',_test_driver,'Dane osobowe');
_obszar.ZAWIERANE_UMOWY:=exec('otworz_obszar','$lib_base',_test_driver,'Zawierane umowy (personel)',,'wnd:selektor');
{? _osoby.first() & _adresy.first() & _dane_ubez.first() & _zalaczniki.first() & _dane_okr_zatr.first() &
   _dane_czl_rodz.first() & _dane_rachunek.first() & _dane_szkola.first() & _urz_skarb.first() & _umowy.first() &
   _badania.first()
|| {!
   |? _test_driver.app_activate(_obszar.DANE_OSOBOWE);
      _wyniki[_loop].osoba:=exec('dolacz_osobe','$dane_osobowe',_test_driver,_osoby,0,'PER_REJ');
      exec('wybierz_osobe_pesel','$dane_osobowe',_test_driver,_osoby.NAZWISKO,_osoby.PIERWSZE,_osoby.PESEL);
      _wyniki[_loop].adres:=exec('dodaj_adres','$dane_osobowe',_test_driver,_adresy);
      _wyniki[_loop].dane_ubez:=exec('dodaj_dane_ubez','$dane_osobowe',_test_driver,_dane_ubez);
      {? _plik.DIR<>'' & _plik.NAME<>''
      || _zalaczniki.ZAL:=_plik.DIR+'\\'+_plik.NAME;
         _wyniki[_loop].zalacznik:=exec('dodaj_zalacznik','$dane_osobowe',_test_driver,_zalaczniki)
      ?};
      _wyniki[_loop].dane_okr_zatr:=exec('dodaj_okr_zatr','$dane_osobowe',_test_driver,_dane_okr_zatr);
      _wyniki[_loop].dane_czl_rodz:=exec('dodaj_czl_rodz','$dane_osobowe',_test_driver,_dane_czl_rodz);
      _wyniki[_loop].rachunek:=exec('dodaj_rach_bnk','$dane_osobowe',_test_driver,_dane_rachunek);
      _wyniki[_loop].szkoly:=exec('dodaj_wyksztalcenie','$dane_osobowe',_test_driver,_dane_szkola);
      _wyniki[_loop].urz_skarb:=exec('dodaj_urzad_skarb','$dane_osobowe',_test_driver,_urz_skarb);

      _test_driver.app_activate(_obszar.ZAWIERANE_UMOWY);
      exec('ustaw_par_obszaru','$zawierane_umowy_personel',_test_driver,'ud_sch  00000001','f_zatr  00000002');
      _wyn_umowa[_loop].umowa:=exec('dolacz_umowe','$zawierane_umowy_personel',_test_driver,_osoby,_umowy,0,'PER_ZA1',0);
      exec('wybierz_umowe','$zawierane_umowy_personel',_test_driver,_umowy.T,_osoby.NAZWISKO,_osoby.PIERWSZE,_osoby.PESEL);
      exec('dolacz_badanie','$zawierane_umowy_personel',_test_driver,_badania);

      _loop+=1;
      _osoby.next() & _adresy.next() & _dane_ubez.next() & _zalaczniki.next() & _dane_okr_zatr.next() &
      _dane_czl_rodz.next() & _dane_rachunek.next() & _dane_szkola.next() & _urz_skarb.next() & _umowy.next() &
      _badania.next()
   !}
?};

:: czyścimy
_test_driver.app_activate(_obszar.DANE_OSOBOWE);

_osoby.first();
_test_driver.click('wnd:zws_dos_osoba');

_test_driver.filter('@OSOBA.NAZWISKO', '', '@OSOBA.PIERWSZE', '', '@OSOBA.PESEL', '');
{!
|? _test_driver.filter('@OSOBA.NAZWISKO', _osoby.NAZWISKO, '@OSOBA.PIERWSZE', _osoby.PIERWSZE);
   _test_driver.click('fld:@OSOBA.DOWOD(row=1)');
   _test_driver.click('btn:USUŃ');
   _test_driver.click('dbtn:Tak');
   _test_driver.click('dbtn:Tak');
   _test_driver.assert('', 'wnd:@tmp.e_tmp', 'title', 'Weryfikacja usunięcia danych osobowych', 'type', 'e');
   _test_driver.click('fld:@tmp.KEY1');
   _test_driver.send(_osoby.NAZWISKO+' '+_osoby.PIERWSZE+' '+{? _osoby.PESEL<>'' || _osoby.PESEL || _osoby.PASZPORT ?}, '', 'fld:@tmp.KEY2');
   _test_driver.send('', '{F2}', '');

   _osoby.next()
!};

::THEN
::usuwam skopiowany plik zalacznika; nie usuwam utworzonego katalogu bo nie mam funkcji do tego
{? _plik.DIR<>'' & _plik.NAME<>''
|| ferase('@'+_plik.DIR+'\\'+_plik.NAME)
?};
_test_driver.app_close(_obszar.ZAWIERANE_UMOWY);
_test_driver.app_close(_obszar.DANE_OSOBOWE);
_test_driver.logger.info('KONIEC TESTU: $test_akc_PK1_zatr_prac.fml (time: '+$date()+' '+(time()$3)+')');
::Zmienić sposób zwrotu wyniki
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:06 aafde423e8d8651262426b13444f9b2c05a291181b2ac446c0da115fa232e2efc69a161b7d2464addbe63b06da7c07d69e97554057bc12d8a6676547e2043192b8b457c0fd4cba7c871e6825925d791c651acc1a57d7cd4f939c98db0f546a7db468ebfb3ae52dfa95d9ed9dc5e60695943131c8092dabf3e2c70f831ae956fe

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_portal_urlop.fml
:: Utworzony: 10.02.2022
:: Autor: WHAN
:: Systemy:
::======================================================================================================================
:: Zawartość: Skrypty testujące obsługę wniosków
::======================================================================================================================
::    UWAGA test wprowadza dane na podstawie pliku $vacation.csv
::    pole AKCJE oznacza jakie akcje i w jakiej kolejności wykonuje test:
::    SP - Wprowadzenie wniosku (Portal)
::    SPR - Wywołanie komunikacji z portalem (Merit) wysyłanie przetwarzanie odbieranie
::    AM1 - Akceptacja wniosku 1 poziom (Merit)
::    AP1 - Akceptacja wniosku 1 poziom (Portal)
::    AM2 - Akceptacja wniosku 2 poziom (Merit)
::    AP2 - Akceptacja wniosku 2 poziom (Portal)
::    RM1 - Odrzucenie wniosku 1 poziom (Merit)
::    RP1 - Odrzucenie wniosku 1 poziom (Portal)
::    RM2 - Odrzucenie wniosku 2 poziom (Merit)
::    RP2 - Odrzucenie wniosku 2 poziom (Portal)
::    CP1 - Sprawdzenie statusu po akceptacji/odrzuceniu 1 poziom (Portal)
::    CP2 - Sprawdzenie statusu po akceptacji/odrzuceniu 2 poziom (Portal)
::    IM  - Wprowadzenie nieobecności z wniosku urlopowego
::    MP  - Sprawdzenie maila
::    DM  - Usuwanie zmian danych po teście (Merit)
::
\portal_urlop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: główna formuła testu wykonywana dla działania na końcówce
::   WE: [_a] - [STRING] ID testu/testów z pliku csv
::       [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika
::   WY:
:: ~OST: INFCOPY,INFEXISTS,INFMKDIR,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 | type_of(_a)<>type_of('') | _a=''
|| _cases:=''
|| _cases:=_a
?};
{? var_press('_b')<=0 | type_of(_b)<>type_of(1) | _b=0
|| _mode:=0
|| _mode:=1
?};
_sep_cli:=exec('sep','#file',0);
: definiowanie ścieżki lokalnej
{? var_press('__test_path')=type_of('')
|| _tmp_dir:=__test_path
|| _tmp_dir:=3+tmp_dir()+'auto'
?};
{? _mode=1
|| undefine();
   define('PATH','','','Ścieżka',100,100);
   DEFINE.PATH:=_tmp_dir;
   def_btn('text=%1,icon=xwin16.png:13'['OK'@],'key:F2');
   def_btn('text=%1,icon=xwin16.png:14'['Anuluj'@],'key:Esc');
   {? def_edit(,'Podaj ścieżkę lokalnego folderu środowiska testowego:'@)=0
   || undefine();
      return(~~)
   ?};
   __test_path:=_tmp_dir:=DEFINE.PATH;
   undefine()
?};
{? _tmp_dir+1='/' | _tmp_dir+1='\\'
|| _tmp_dir:=_tmp_dir-1
?};
{? _tmp_dir=3+tmp_dir()+'auto' & fexists('@'+(3+tmp_dir())+'auto',0)=0
|| fmkdir('@'+(3+tmp_dir()),'auto')
?};
{? fexists('@'+_tmp_dir)=0
|| return(0)
?};
:: sprawdzanie potrzebnych plików i ewentualne kopiowanie z serwera na końcówkę
{? ~fexists(_tmp_dir+'/test_portal')
|| fmkdir('@'+_tmp_dir,'test_portal')
?};
_tmp_dir:=_tmp_dir+_sep_cli+'test_portal';
_pth_jar:=pth_dir('$.fml')-3+'jar'+'/';
_pth_driver:=pth_dir('$.fml')-3+'driver'+'/';
_jar:='PortalTest.jar';
{? ~fexists('@'+_tmp_dir+'/'+_jar)
|| {? ~fexists(_pth_jar-1)
   || fmkdir(_pth_jar-4,'jar')
   ?};
::   exec('svn_export','%transfer',_svn+'merit/fml/auto/jar/PortalTest.jar',_pth_jar+_jar,_pocztam);
   fcopy(_pth_jar+_jar,'@'+_tmp_dir+'/'+_jar,0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/'+'csv')
|| fmkdir('@'+_tmp_dir,'csv')
?};
{? ~fexists('@'+_tmp_dir+'/csv/$vacation.csv')
||
::   exec('svn_export','%transfer',_svn+'merit/fml/auto/csv/$vacation.csv',pth_dir('$.csv')+'/$vacation.csv',_pocztam);
   fcopy(pth_dir('$.csv')+'/$vacation.csv','@'+_tmp_dir+'/csv/$vacation.csv',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/'+'driver')
|| fmkdir('@'+_tmp_dir,'driver')
?};
{? ~fexists('@'+_tmp_dir+'/driver/'+'chromedriver.exe')
||
::   exec('svn_export','%transfer',_svn+'merit/fml/auto/driver/chromedriver.exe','@'+_tmp_dir+'/driver/'+'chromedriver.exe',_pocztam)
   fcopy(_pth_driver+'chromedriver.exe','@'+_tmp_dir+'/driver/'+'chromedriver.exe',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/log')
|| fmkdir('@'+_tmp_dir,'log')
?};
_dane_wniosek:=exec('importuj_dane_wniosek','$test_portal_urlop','@'+_tmp_dir+'/csv/$vacation.csv');
{? ~_dane_wniosek.first()
|| 'Brak zaimportowanych danych';
   return(0)
?};
::_robot:=(app_info('app_ident')='robot' | app_info('app_ident')='robot_manager');
_robot:=app_info('app_par1')='START_TESTY_PORTAL';
_test_driver:=exec('init','$lib_base','$manager.log');
{? ~_robot
||
   _pulpit:=exec('get_desctop_id','$lib_base',_test_driver);
   {? _pulpit<>''
   || _test_driver.app_close(_pulpit)
   ?}
?};
{? _mode=0 & _robot
|| exec('clear_urlop','$test_portal_urlop',_dane_wniosek,2)
?};
exec('MBJAR','#object');

{? _cases<>''
|| _cases_spli:=spli_str(_cases,',');
   _case_no:=1
?};
{! |?
   {? _cases<>''
   || {? ~_dane_wniosek.find_tab(,'ID',,'=',_cases_spli[_case_no])
      || _file:=fopen('$test_portal_urlop.log','ua',1);
         fwrite(_file,'\nERROR: Nie odnaleziono wniosku w pliku *.csv. ID: '+_dane_wniosek.ID);
         fclose(_file);
         return(0)
      ?}
   ?};
   {? ~fexists('@'+_tmp_dir+'/log/'+_dane_wniosek.ID)
   || fmkdir('@'+_tmp_dir+'/log',_dane_wniosek.ID)
   ?};
   {? _dane_wniosek.AKCJE<>''
   || _akcje:=spli_str(_dane_wniosek.AKCJE,',');
      _spr:=0;
      {! _i:=1..obj_len(_akcje)
      |!
::    pole AKCJE oznacza jakie akcje i w jakiej kolejności wykonuje test:
::    SP - Wprowadzenie wniosku (Portal)
::    SPR - Wywołanie komunikacji z portalem (Merit) wysyłanie przetwarzanie odbieranie
::    AM1 - Akceptacja wniosku 1 poziom (Merit)
::    AP1 - Akceptacja wniosku 1 poziom (Portal)
::    AM2 - Akceptacja wniosku 2 poziom (Merit)
::    AP2 - Akceptacja wniosku 2 poziom (Portal)
::    RM1 - Odrzucenie wniosku 1 poziom (Merit)
::    RP1 - Odrzucenie wniosku 1 poziom (Portal)
::    RM2 - Odrzucenie wniosku 2 poziom (Merit)
::    RP2 - Odrzucenie wniosku 2 poziom (Portal)
::    CP1 - Sprawdzenie statusu po akceptacji/odrzuceniu 1 poziom (Portal)
::    CP2 - Sprawdzenie statusu po akceptacji/odrzuceniu 2 poziom (Portal)
::    IM  - Wprowadzenie nieobecności z wniosku urlopowego
::    MP  - Sprawdzenie maila
::    DM - Usuwanie zmian danych po teście (Merit)
         {? _akcje[_i]='SP'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--vacation-start',_dane_wniosek.ID)
         |? _akcje[_i]='SPR'
         || _spr+=1;
            exec('send_process_receive','$test_portal_urlop',_test_driver)
         |? _akcje[_i]='AM1'
         || exec('accept_reject_urlop','$test_portal_urlop',_dane_wniosek,_mode,1,1);
            fcopy('$accept_urlop_L1.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.accept_vacation_L1.log',1,0,1)
         |? _akcje[_i]='AP1'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--vacation-accept',_dane_wniosek.ID,'1')
         |? _akcje[_i]='AM2'
         || exec('accept_reject_urlop','$test_portal_urlop',_dane_wniosek,_mode,2,1);
            fcopy('$accept_urlop_L2.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.accept_vacation_L2.log',1,0,1)
         |? _akcje[_i]='AP2'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--vacation-accept',_dane_wniosek.ID,'2')
         |? _akcje[_i]='RM1'
         || exec('accept_reject_urlop','$test_portal_urlop',_dane_wniosek,_mode,1,0);
            fcopy('$reject_urlop_L1.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.reject_vacation_L1.log',1,0,1)
         |? _akcje[_i]='RP1'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--vacation-reject',_dane_wniosek.ID,'1')
         |? _akcje[_i]='RM2'
         || exec('accept_reject_urlop','$test_portal_urlop',_dane_wniosek,_mode,2,0);
            fcopy('$reject_urlop_L2.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.reject_vacation_L2.log',1,0,1)
         |? _akcje[_i]='RP2'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--vacation-reject',_dane_wniosek.ID,'2')
         |? _akcje[_i]='CP1'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--vacation-check',_dane_wniosek.ID,'1')
         |? _akcje[_i]='CP2'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--vacation-check',_dane_wniosek.ID,'2')
         |? _akcje[_i]='IM'
         || exec('insert_urlop','$test_portal_urlop',_dane_wniosek);
            fcopy('$insert_absence.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$(_i-_spr)+'.insert_absence.log',1,0,1)
         |? _akcje[_i]='MP'
         || exec('portal_mail','$test_mail',_dane_wniosek.ID,'./log/'+_dane_wniosek.ID+'/'+$(_i-_spr)+'.mail.log')
         |? _akcje[_i]='DM'
         || exec('clear_urlop','$test_portal_urlop',_dane_wniosek,_mode);
            fcopy('$clear_urlop.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$(_i-_spr)+'.clear_vacation.log',1,0,1)
         ?}
      !};
      &_akcje
   ?};
   {? _cases<>''
   || _case_no+=1;
      {? obj_len(_cases_spli)>=_case_no
      || _loop:=1
      || _loop:=0
      ?}
   || _loop:=_dane_wniosek.next()
   ?};
   _loop
!};
{? ~_robot
|| _test_driver.app_open('admin', 'admin123', app_info('cluster_group'), app_info('app_ident'), '', '');
::   exec('set_active_win','$lib_base',_test_driver,_cur_win);
   _exec:='FUN.info(\'Wyniki testu znajdują się w katalogu '+_tmp_dir+_sep_cli+'log\')';
   _test_driver.execute($(gsub(_exec,'\\','\\\\')))
?};
~~


\accept_reject_urlop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Główna metoda akceptująca/odrzucająca wnioskek urlopowy
::   WE: _a - dane z pliku csv
::       [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika
::       [_c] - [1/2] 1 - domyślnie, poziom akceptacji
::       [_d] - [0/1] 1 - domyślnie, 0 - odrzucenie, 1 - akceptacja
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_dane_wniosek:=_a;
{? var_press('_b')<0 | type_of(_b)<>type_of(1) | type_of(_b)=type_of(1) & _b=0
|| _mode:=0
|| _mode:=1
?};
{? var_press('_c')<0 | type_of(_c)<>type_of(1) | type_of(_c)=type_of(1) & _c=2
|| _level:=2
|| _level:=1
?};
{? var_press('_d')<0 | type_of(_d)<>type_of(1) | type_of(_d)=type_of(1) & _d=1
|| _accept:=1
|| _accept:=0
?};
{? _accept
|| _log_name:='$accept_urlop_L'+$_level+'.log'
|| _log_name:='$reject_urlop_L'+$_level+'.log'
?};
ferase(pth_dir()+'/system/admin/'+_log_name);
ferase(pth_dir('$accept_urlop_L1.log')+'/'+_log_name);
_test_driver:=exec('init','$lib_base',_log_name);
:: @WHEN
{? _level=1
|| _login:=_dane_wniosek.LOGIN_M1;
   _haslo:=_dane_wniosek.HASLO_M1
|| _login:=_dane_wniosek.LOGIN_M2;
   _haslo:=_dane_wniosek.HASLO_M2
?};
_pulpit:=_test_driver.app_open(_login, _haslo, app_info('cluster_group'), 'merit001', '', '');
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'todo');
_app_todo:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@BI_TODO.WER', 'title', 'Zadania', 'type', 's');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_vwu')");
_test_driver.execute("exec('receive','$test_portal_urlop')");
_test_driver.execute("exec('process','$test_portal_urlop')");
_test_driver.execute("exec('send','$test_portal_urlop')");
_file:=fopen(_log_name,'ua',1);
fwrite(_file,'\n\nINFO: Poziom: ' +$_level+ '. ' + {? _accept || 'Akceptacja' || 'Odrzucenie' ?} + ' wniosku ID: '+_dane_wniosek.ID);
fclose(_file);
_test_driver.send('', '^{F5}', '');
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=1)');
_test_driver.filter('@BI_TODO.DT', $date(),
                    '@BI_TODO.BI_PREL.DESC', 'Zweryfikuj wniosek o urlop: '+_dane_wniosek.NAZWISKO+' '+_dane_wniosek.IMIE,
                    '@BI_TODO.B_DOMAIN.NAME', 'Obieg wniosków');
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=2)');
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=1)');
_test_driver.send('', '{PGUP}', '');
_test_driver.menu('Rozpocznij');
_test_driver.assert('', 'wnd:@tmp.obe_faw_red', 'title', 'Wniosek', 'type', 'e');
{? _accept
|| _test_driver.click('btn:@Akceptuj')
|| _test_driver.click('btn:@Odrzuć')
?};
_test_driver.assert('', 'wnd:@EDOKOS.obeakckom', 'title', {? _accept || 'Akceptacja' || 'Odrzucenie' ?}, 'type', 'e');
_test_driver.send( {? _accept
                   || {? _level=1 || _dane_wniosek.AKCEPT_1 || _dane_wniosek.AKCEPT_2 ?}
                   || {? _level=1 || _dane_wniosek.ODRZUC_1 || _dane_wniosek.ODRZUC_2 ?}
                   ?} , '', 'fld:@EDOKOS.UW_DL');
{? _accept
|| _test_driver.click('btn:@Zakończ')
|| _test_driver.click('btn:@Odrzuć')
?};

_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_vwu')");
_test_driver.execute("exec('receive','$test_portal_urlop')");
_test_driver.execute("exec('process','$test_portal_urlop')");
_test_driver.execute("exec('send','$test_portal_urlop')");
delay(30);
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_vwu')");
_test_driver.execute("exec('receive','$test_portal_urlop')");
_test_driver.execute("exec('process','$test_portal_urlop')");
_test_driver.execute("exec('send','$test_portal_urlop')");
delay(60);
_test_driver.app_close(_app_todo);
_test_driver.app_close(_pulpit);
~~


\insert_urlop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Główna metoda wprowadzająca nieobecność z wniosku urlopowego
::   WE: _a - dane z pliku csv
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_dane_wniosek:=_a;
_log_name:='$insert_absence.log';
ferase(pth_dir()+'/system/admin/'+_log_name);
ferase(pth_dir('$insert_absence.log')+'/'+_log_name);
_test_driver:=exec('init','$lib_base',_log_name);
:: @WHEN
_login:=_dane_wniosek.LOGIN_M2;
_haslo:=_dane_wniosek.HASLO_M2;
_pulpit:=_test_driver.app_open(_login, _haslo, app_info('cluster_group'), 'merit001', '', '');
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'todo');
_app_todo:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@BI_TODO.WER', 'title', 'Zadania', 'type', 's');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_vwu')");
_test_driver.execute("exec('receive','$test_portal_urlop')");
_test_driver.execute("exec('process','$test_portal_urlop')");
_test_driver.execute("exec('send','$test_portal_urlop')");
_file:=fopen(_log_name,'ua',1);
fwrite(_file,'\n\nINFO: Wprowadzenie nieobecności z wniosku urlopowego ID: '+_dane_wniosek.ID);
fclose(_file);
_test_driver.send('', '^{F5}', '');
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=1)');
_test_driver.filter('@BI_TODO.DT', $date(),
                    '@BI_TODO.BI_PREL.DESC', 'Zarejestruj nieobecność na podstawie wniosku o urlop: '
                                                      +_dane_wniosek.NAZWISKO+' '+_dane_wniosek.IMIE,
                    '@BI_TODO.B_DOMAIN.NAME', 'Kadry');
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=2)');
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=1)');
_test_driver.menu('Rozpocznij');
_test_driver.click('dbtn:Zakończ');
_test_driver.assert('', 'wnd:@tmp.kyoaskzuwalisct', 'title', '', 'type', 's');
_test_driver.click('btn:@OK');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_vwu')");
_test_driver.execute("exec('receive','$test_portal_urlop')");
_test_driver.execute("exec('process','$test_portal_urlop')");
_test_driver.execute("exec('send','$test_portal_urlop')");
delay(30);
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_vwu')");
_test_driver.execute("exec('receive','$test_portal_urlop')");
_test_driver.execute("exec('process','$test_portal_urlop')");
_test_driver.execute("exec('send','$test_portal_urlop')");
delay(60);
_test_driver.app_close(_app_todo);
_test_driver.app_close(_pulpit);
~~


\clear_urlop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Czyści dane z testu
::   WE: _a - dane z pliku csv
::      [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika; 2 - czyszczenie danych przed testami
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_log_name:='$clear_urlop.log';
_test_driver:=exec('init','$lib_base',_log_name);
{? var_press('_b')<0 | _b=0
|| _mode:=0
|| _mode:=_b
?};
_dane_wniosek:=_a;
:: @WHEN
ferase(pth_dir()+'/system/admin/'+_log_name);
ferase(pth_dir(_log_name)+'/'+_log_name);
_pulpit:=_test_driver.app_open('admin', 'admin123', app_info('cluster_group'), 'merit001', '', '');
:: usunięcie wprowadzonego urlopu w danych osobowych
_test_driver.control('!desktop', 'open', 'Kadry\\Obszary\\Zdarzenia kadrowe', 7);
_app_zdarz_kadr:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:PKD_EZK', 'title', '', 'type', 's');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("exec('receive','$test_portal_urlop')");
_test_driver.execute("exec('process','$test_portal_urlop')");
_test_driver.execute("exec('send','$test_portal_urlop')");
_file:=fopen(_log_name,'ua',1);
fwrite(_file,'\n\nINFO: Usunięcie danych wniosku ID: '+_dane_wniosek.ID);
fclose(_file);
:: usunięcie wniosku z kadr
{? _mode<>2
|| _test_driver.click('fld:@P.OSOBA.PIERWSZE(row=1)');
   _test_driver.filter('@P.OSOBA.NAZWISKO', _dane_wniosek.NAZWISKO,
                       '@P.OSOBA.PIERWSZE', _dane_wniosek.IMIE);
   _test_driver.click('fld:@P.OSOBA.NAZWISKO(row=1)');

   _test_driver.click('gtab:@Nieobecności');
   _test_driver.filter('@N.NB.RT', _dane_wniosek.TYP,
                       '@N.OD', gsub(_dane_wniosek.DATAOD,'-','/'),
                       '@N.DO', gsub(_dane_wniosek.DATADO,'-','/'))
|| _test_driver.click('fld:@P.OSOBA.PIERWSZE(row=1)');
   _test_driver.filter('@P.OSOBA.NAZWISKO', 'Abram',
                       '@P.OSOBA.PIERWSZE', 'Adam');
   _test_driver.click('fld:@P.OSOBA.NAZWISKO(row=1)');

   _test_driver.click('gtab:@Nieobecności');
   _test_driver.filter('@N.NB.RT', _dane_wniosek.TYP,
                       '@N.OD', $(date~1),
                       '@N.DO', $(date~1))
?};
_test_driver.click('fld:@N.NB.RT(row=2)');
_test_driver.click('fld:@N.NB.RT(row=1)');
_size:=_test_driver.execute("{? N.f_active()
                                || N.f_size()
                                || N.size()
                                ?}");
{? type_of(_size)=type_of(1)
|| {! _i:=1.._size |!
      _test_driver.click('fld:@N.NB.RT(row=2)');
      _test_driver.click('fld:@N.NB.RT(row=1)');
      _test_driver.click('btn:USUŃ');
      _test_driver.click('dbtn:Tak')
   !}
?};
_test_driver.click('gtab:@Wnioski o urlop');
{? _mode<>2
|| _test_driver.filter('@NWU.D', $date(),
                       '@NWU.R.RT', _dane_wniosek.TYP,
                       '@NWU.OD', gsub(_dane_wniosek.DATAOD,'-','/'),
                       '@NWU.DO', gsub(_dane_wniosek.DATADO,'-','/'),
                       '@NWU.KOMENT', _dane_wniosek.UWAGI)
|| _test_driver.filter('@NWU.R.RT', _dane_wniosek.TYP,
                       '@NWU.OD', gsub(4+_dane_wniosek.DATAOD,'-','/'),
                       '@NWU.DO', gsub(4+_dane_wniosek.DATADO,'-','/'),
                       '@NWU.KOMENT', _dane_wniosek.UWAGI)
?};
_size:=_test_driver.execute("{? NWU.f_active()
                             || NWU.f_size()
                             || NWU.size()
                             ?}");
{? type_of(_size)=type_of(1)
|| {! _i:=1.._size |!
      _test_driver.click('fld:@NWU.R.RT(row=2)');
      _test_driver.click('fld:@NWU.R.RT(row=1)');
      _test_driver.click('btn:USUŃ');
      _test_driver.click('dbtn:Tak')
   !}
?};
_test_driver.app_close(_app_zdarz_kadr);
_test_driver.app_activate(_pulpit);
:: usunięcie wniosku z obiegów
_test_driver.control('!desktop', 'open', 'Obieg wniosków\\Obszary\\Wnioski w obiegu - techniczne', 7);
_app_wnioski_tech:=_test_driver.app_wait_for_change();

_test_driver.execute("EDOKUM.use('skid_vwu')");
_test_driver.execute("EDOKUM.prefix()");
_test_driver.click('wnd:@#edokatrt');
_test_driver.click('wnd:@edokumtmp01');
_test_driver.click('fld:@EDOKUM.DATAW(row=1)');
_size:=_test_driver.execute("{? EDOKUM.f_active()
                             || EDOKUM.f_size()
                             || EDOKUM.size()
                             ?}");
{? _size>0
||
   {? _mode<>2
   ||
      _test_driver.filter('@EDOKUM.DATAW', $date(),
                          '@EDOKUM.USERS.KOD', _dane_wniosek.LOGIN_M,
                          '@EDOKUM.TYP.NAZWA', 'Wniosek urlopowy (HR Portal)');
                          _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))")
   || _test_driver.filter('@EDOKUM.TYP.NAZWA', 'Wniosek urlopowy (HR Portal)');
      _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))")
   ?};
   _test_driver.click('wnd:@#edokatrt');
   _test_driver.click('wnd:@edokumtmp01');

   _test_driver.click('fld:@EDOKUM.DATAW(row=1)');
   _size:=_test_driver.execute("{? EDOKUM.f_active()
                                || EDOKUM.f_size()
                                || EDOKUM.size()
                                ?}");
   {? type_of(_size)=type_of(1)
   || {! _i:=1.._size |!
         _test_driver.click('wnd:@edokumtmp01');
         _test_driver.click('fld:@EDOKUM.TYP.NAZWA(row=1)');
         _test_driver.menu('Usuń');
         _test_driver.click('dbtn:Tak')
      !}
   ?}
?};
:: wysłanie na portal
delay(5);
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("exec('receive','$test_portal_urlop')");
_test_driver.execute("exec('process','$test_portal_urlop')");
_test_driver.execute("exec('send','$test_portal_urlop')");
_test_driver.app_close(_app_wnioski_tech);
_test_driver.app_close(_pulpit);
~~


\send_process_receive
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Wywołanie komunikacji z portalem (Merit) wysyłanie przetwarzanie odbieranie
::   [_a] - _test_driver
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')>0
|| _test_driver:=_a
|| _test_driver:=exec('init','$lib_base','$manager.log')
?};
_pulpit:=_test_driver.app_open('admin', 'admin123', app_info('cluster_group'), 'merit001', '', '');
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'todo');
_app_todo:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@BI_TODO.WER', 'title', 'Zadania', 'type', 's');
{! _i:=1..2
|! _test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
   _test_driver.execute("EDOKUM.use('skid_vwu')");
   _test_driver.execute("exec('receive','$test_portal_urlop')");
   _test_driver.execute("exec('process','$test_portal_urlop')");
   _test_driver.execute("exec('send','$test_portal_urlop')");
   delay(15)
!};
_test_driver.app_close(_app_todo);
_test_driver.app_close(_pulpit);
~~


\receive
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: testuj odbieranie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
exec('process_receive','sync_mwa','PORTAL_WNIOSKI',,,,,0);
EDOKUM.cntx_pop()


\process
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: testuj przetwarzanie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
exec('process','portal_wnioski',1,0);
EDOKUM.cntx_pop()


\send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: testuj wysyłanie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
exec('process_send','sync_mwa','PORTAL_WNIOSKI',,,,,,,0);
EDOKUM.cntx_pop()


\importuj_dane_wniosek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Importuje z csv dane wniosku
::   WE: _a - nazwa pliku
::   WY: _tab_tmp
::----------------------------------------------------------------------------------------------------------------------
_dane_w:=tab_tmp(1,
'ID','STRING[3]','ID',
'AKCJE','STRING[50]','Kody akcji do wykonania',
'ADRES','STRING[255]','Adres www portalu',
'LOGIN_P','STRING[100]','Użytkownik portal',
'HASLO_P','STRING[100]','Hasło portal',
'IMIE','STRING[40]','Imię',
'NAZWISKO','STRING[40]','Nazwisko',
'LOGIN_P1','STRING[50]','Użytkownik Portal akc. 1 poziom',
'HASLO_P1','STRING[50]','Hasło Portal akc. 1 poziom',
'LOGIN_P2','STRING[50]','Użytkownik Portal akc. 2 poziom',
'HASLO_P2','STRING[50]','Hasło Portal akc. 2 poziom',
'LOGIN_M','STRING[20]','Użytkownik Merit',
'LOGIN_M1','STRING[20]','Użytkownik Merit akc. 1 poziom',
'HASLO_M1','STRING[100]','Hasło Merit akc. 1 poziom',
'LOGIN_M2','STRING[20]','Użytkownik Merit akc. 2 poziom',
'HASLO_M2','STRING[100]','Hasło Merit akc. 2 poziom',
'TYP','STRING[100]','Typ urlopu',
'PODTYP','STRING[100]','Typ urlopu',
'DATAOD','STRING[10]','Data od',
'DATADO','STRING[10]','Data do',
'DNI','STRING[2]','Liczba dni',
'GODZIN','STRING[2]','Liczba godzin roboczych',
'UWAGI','STRING[100]','Uwagi',
'AKCEPT_1','STRING[100]','Uwagi akceptacji poziom 1',
'AKCEPT_2','STRING[100]','Uwagi akceptacji poziom 2',
'ODRZUC_1','STRING[100]','Uwagi odrzucenia poziom 1',
'ODRZUC_2','STRING[100]','Uwagi odrzucenia poziom 2',
'STATUS_1','STRING[100]','Status po akceptacji/odrzuceniu poziom 1',
'STATUS_2','STRING[100]','Status po akceptacji/odrzuceniu poziom 2');

_plik:=_a;
{? _plik*'/' | _plik*'\\'
|| _pth:=',nopth'
|| _pth:=''
?};
_dane_w.import(_plik,,1,,'UTF-8,header'+_pth,,
'ID',,1,,
'AKCJE',,2,,
'ADRES',,3,,
'LOGIN_P',,4,,
'HASLO_P',,5,,
'IMIE',,6,,
'NAZWISKO',,7,,
'LOGIN_P1',,8,,
'HASLO_P1',,9,,
'LOGIN_P2',,10,,
'HASLO_P2',,11,,
'LOGIN_M',,12,,
'LOGIN_M1',,13,,
'HASLO_M1',,14,,
'LOGIN_M2',,15,,
'HASLO_M2',,16,,
'TYP',,17,,
'PODTYP',,18,,
'DATAOD',,19,,
'DATADO',,20,,
'DNI',,21,,
'GODZIN',,22,,
'UWAGI',,23,,
'AKCEPT_1',,24,,
'AKCEPT_2',,25,,
'ODRZUC_1',,26,,
'ODRZUC_2',,27,,
'STATUS_1',,28,,
'STATUS_2',,29,);

{? _dane_w.first()
|| {! |?
:: jeśli data od/do pusta
      {? _dane_w.DATAOD=''
      || _dane_w.DATAOD:=gsub($date(),'/','-');
         _dane_w.put()
      ?};
      {? _dane_w.DATAOD*'.'
      || _dane_w.DATAOD:=gsub(_dane_w.DATAOD,'.','-');
         _dane_w.put()
      ?};
      {? _dane_w.DATADO=''
      || _dane_w.DATADO:=gsub($(date()+1),'/','-');
         _dane_w.put()
      ?};
      {? _dane_w.DATADO*'.'
      || _dane_w.DATADO:=gsub(_dane_w.DATADO,'.','-');
         _dane_w.put()
      ?};
      _dane_w.next()
   !}
?};
_dane_w


:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:06 3a82847e0f49bf0bccd5c297ddac6e13f8c8f8ca3f9e5040ed55fe72260bed4a5b6d4bdd35acf3a8a1cbd613f07b5277dc678b6b8355ce0b9dd3692520ab77915de4637bbf3fbb08eba9b2aa51e8ed94174adf83e7297d84629897d2251f6e366ed93ee48c22b59ed230d3d655b477750f80f0d093e3064179d267246908ff84

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_businesslink.fml
:: Utworzony: 12.05.2022
:: Autor: WHAN
:: Systemy:
::======================================================================================================================
:: Zawartość: Skrypty testujące obsługę businesslink, ksef
::======================================================================================================================

\businesslink_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: główna formuła testu wykonywana dla działania na końcówce
::   WE: [_a] - [STRING] ID testu/testów z pliku csv
::       [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - tryb użytkownika
::   WY:
:: ~OST: INFCOPY,INFEXISTS,INFMKDIR,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 | type_of(_a)<>type_of('') | _a=''
|| _cases:=''
|| _cases:=_a
?};
{? var_press('_b')<=0 | type_of(_b)<>type_of(1) | _b=0
|| _mode:=0
|| _mode:=1
?};
_robot:=app_info('app_par1')='START_TESTY_PORTAL';
_sep_cli:=exec('sep','#file',0);
: definiowanie ścieżki lokalnej
{? var_press('__test_path')=type_of('')
|| _tmp_dir:=__test_path
|| _tmp_dir:=3+tmp_dir()+'auto'
?};
{? _mode=1
|| undefine();
   define('PATH','','','Ścieżka',100,100);
   DEFINE.PATH:=_tmp_dir;
   def_btn('text=%1,icon=xwin16.png:13'['OK'@],'key:F2');
   def_btn('text=%1,icon=xwin16.png:14'['Anuluj'@],'key:Esc');
   {? def_edit(,'Podaj ścieżkę lokalnego folderu środowiska testowego:'@)=0
   || undefine();
      return(~~)
   ?};
   __test_path:=_tmp_dir:=DEFINE.PATH;
   undefine()
?};
{? _tmp_dir+1='/' | _tmp_dir+1='\\'
|| _tmp_dir:=_tmp_dir-1
?};
{? _tmp_dir=3+tmp_dir()+'auto' & fexists('@'+(3+tmp_dir())+'auto',0)=0
|| fmkdir('@'+(3+tmp_dir()),'auto')
?};
{? fexists('@'+_tmp_dir)=0
|| return(0)
?};
:: sprawdzanie potrzebnych plików i ewentualne kopiowanie z serwera na końcówkę
{? ~fexists(_tmp_dir+'/test_portal')
|| fmkdir('@'+_tmp_dir,'test_portal')
?};
_tmp_dir:=_tmp_dir+_sep_cli+'test_portal';
_pth_jar:=pth_dir('$.fml')-3+'jar'+'/';
_pth_driver:=pth_dir('$.fml')-3+'driver'+'/';
_jar:='PortalTest.jar';
{? ~fexists('@'+_tmp_dir+'/'+_jar)
|| {? ~fexists(_pth_jar-1)
   || fmkdir(_pth_jar-4,'jar')
   ?};
   fcopy(_pth_jar+_jar,'@'+_tmp_dir+'/'+_jar,0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/'+'csv')
|| fmkdir('@'+_tmp_dir,'csv')
?};
{? ~fexists('@'+_tmp_dir+'/csv/$businesslink_ksef.csv')
|| fcopy(pth_dir('$.csv')+'/$businesslink_ksef.csv','@'+_tmp_dir+'/csv/$businesslink_ksef.csv',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/'+'driver')
|| fmkdir('@'+_tmp_dir,'driver')
?};
{? ~fexists('@'+_tmp_dir+'/driver/'+'chromedriver.exe')
||
::   exec('svn_export','%transfer',_svn+'merit/fml/auto/driver/chromedriver.exe','@'+_tmp_dir+'/driver/'+'chromedriver.exe',_pocztam)
   fcopy(_pth_driver+'chromedriver.exe','@'+_tmp_dir+'/driver/'+'chromedriver.exe',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/log')
|| fmkdir('@'+_tmp_dir,'log')
?};
_dane:=exec('importuj_dane','$test_businesslink_ksef','@'+_tmp_dir+'/csv/$businesslink_ksef.csv');
{? ~_dane.first()
|| 'Brak zaimportowanych danych';
   return(0)
?};
ferase(pth_dir()+'/system/admin/'+'$test_businesslink_ksef.log');
ferase(pth_dir('$test_businesslink_ksef.log')+'/'+'$test_businesslink_ksef.log');
_test_driver:=exec('init','$lib_base','$test_businesslink_ksef.log');
exec('MBJAR','#object');
{? _cases<>''
|| _cases_spli:=spli_str(_cases,',');
   _case_no:=1
?};
{! |?
   {? _cases<>''
   || {? ~_dane.find_tab(,'ID',,'=',_cases_spli[_case_no])
      || _file:=fopen('$test_businesslink_ksef.log','ua',1);
         fwrite(_file,'\nERROR: Nie odnaleziono wniosku w pliku *.csv. ID: '+_dane.ID);
         fclose(_file);
         return(0)
      ?}
   ?};
   {? ~fexists('@'+_tmp_dir+'/log/'+_dane.ID)
   || fmkdir('@'+_tmp_dir+'/log',_dane.ID)
   ?};
::   parametry pracy
   _param:=obj_new('ROK','OKRES','JEDN');
   _param.ROK:=date~1;
   _param.OKRES:=spli_str(form(date(),,8),' ')[1];
   _param.JEDN:='CENTRALA';
   exec('parametry_pracy','$test_dokumenty_ksiegowe',_test_driver,_param);
::   czy dokument jest wysyłany do KSEF odczytuje przy dodawaniu dokumentu na podstawie pola FAKS.T().KSEF
   _add_res:=exec('dolacz_i_wyslij','$test_businesslink_ksef',_test_driver,_dane);
   fcopy('$test_businesslink_ksef.log','@'+_tmp_dir+'/log/'+_dane.ID+'/1.add&send_'+
               {? _add_res.ksef || 'ksef' || 'businesslink' ?}+'.log',1,0,1);
   {? type_of(_add_res.symbol)=type_of('') & _add_res.symbol<>''
   ||
::      sprawdzenie danych na BL
      MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--BL-check-FV',_dane.ID,_add_res.symbol);
::      sprawdzenie maila
      _mail:=obj_new('SUBJECT','BODY');
      _mail.BODY:='Faktura: '+_add_res.symbol;
      exec('portal_mail','$test_mail',_dane.ID,'./log/'+_dane.ID+'/3.mail.log','Businesslink',_mail);
::       Sprawdzenie faktury w drugiej firmie
      exec('spr_FV_f002','$test_businesslink_ksef',_test_driver,_add_res.symbol,_add_res.ksef);
      fcopy('$test_businesslink_ksef.log','@'+_tmp_dir+'/log/'+_dane.ID+'/1.add&send_'+
               {? _add_res.ksef || 'ksef' || 'businesslink' ?}+'.log',1,0,1)
   ?};
   {? _cases<>''
   || _case_no+=1;
      {? obj_len(_cases_spli)>=_case_no
      || _loop:=1
      || _loop:=0
      ?}
   || _loop:=_dane.next()
   ?};
   _loop
!};
{? ~_robot
|| _exec:='FUN.info(\'Wyniki testu znajdują się w katalogu '+_tmp_dir+_sep_cli+'log\')';
   _test_driver.execute($(gsub(_exec,'\\','\\\\')))
?};
~~


\dolacz_i_wyslij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: Dołączenie dokumentu sprzedaży i wysłanie do Businesslink
::   WE: _a - _test_driver
::       _b - dane z pliku *.csv
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;
_dane:=_b;
_robot:=app_info('app_par1')='START_TESTY_PORTAL';
_res:=obj_new('symbol','ksef');
{? ~_robot
|| _pulpit:=exec('get_desctop_id','$lib_base',_test_driver);
   _app:=_test_driver.app_activate(_pulpit)
|| _app:=_test_driver.app_open('admin', 'admin123', app_info('cluster_group'), app_info('app_ident'), '', '')
?};
_test_driver.control('!leftmenu', 'open', 'Sprzedaż\\Obszary\\Dokumenty sprzedaży', 1);
_app_dok_spr:=_test_driver.app_wait_for_change();
delay(2);
_test_driver.menu('Dołącz:Dokument pozostały');
_test_driver.assert('', 'wnd:@tmp.aasdfjeoifjjeaf', 'title', 'Procesy', 'type', 's');
_test_driver.filter('@tmp.UID', 'SPR_FAK');
_test_driver.click('btn:@Uruchom');
_test_driver.assert('', 'wnd:@TYPYSP.WYST', 'title', 'Wybierz typ dokumentu', 'type', 's');
_test_driver.filter('@TYPYSP.T', _dane.TYPSP);
_test_driver.menu('Wybierz');
_test_driver.assert('', 'wnd:@FAKS.#fakseditb__a', 'title', 'Dane dokumentu sprzedaży', 'type', 'e');
_res.ksef:=_test_driver.execute("FAKS.T().KSEF='T'");
{? _res.ksef
|| _test_driver.click('fld:@FAKS.KH.KOD')
?};
_test_driver.send('', '{F3}', 'fld:@FAKS.KH.KOD');
_test_driver.assert('', 'wnd:@KH.WER', 'title', 'Kontrahenci', 'type', 's');
_test_driver.filter('@KH.SKR', _dane.KH);
_test_driver.menu('wybierz');
_test_driver.click('fld:@FZ.FORMAPLA');
_test_driver.send('', '{F3}', '');
_test_driver.assert('', 'wnd:@SLO.SPR_SEL', 'title', '', 'type', 's');
_test_driver.filter('@SLO.KOD', _dane.FORM_PLA);
_test_driver.click('btn:WYB_PLAT');
_test_driver.click('fld:@FAKS.RAB');
_test_driver.send(_dane.RABAT, '', 'fld:@FAKS.RAB');
_test_driver.click('fld:@FAKS.NR');
_res.symbol:=_test_driver.state('fld:@FAKS.SYM').value;
::   Pozycje
_test_driver.click('btn:@Pozycje');
_test_driver.assert('', 'wnd:@FAP.WER', 'title', 'Pozycje dokumentu', 'type', 's');
_test_driver.menu('Dołącz');
_test_driver.assert('', 'wnd:@FAP.fapzwred', 'title', 'Pozycja dokumentu', 'type', 'e');
{? _dane.RODZAJ='Materiał'
|| _test_driver.click('fld:@POMOC.RODZ(ndx=1)')
|? _dane.RODZAJ='Usługa'
|| _test_driver.click('fld:@POMOC.RODZ(ndx=2)')
?};
_test_driver.click('fld:@D_HELP.M_FAP');
_test_driver.send('', '{F3}', '');
_test_driver.assert('', 'wnd:@M.NL_WER', 'title', 'Materiały', 'type', 's');
_test_driver.filter('@M.N', _dane.NAZWA);
_test_driver.click('fld:@M.N(row=1)');
_test_driver.menu('Wybierz');
_test_driver.click('fld:@DISP.ILF');
_test_driver.send(_dane.ILOSC, '', 'fld:@DISP.ILF');
_test_driver.click('fld:@FAP.CPR');
_test_driver.send(_dane.CENA, '', 'fld:@FAP.CPR');
_test_driver.send('', '{F2}', '');
_test_driver.click('btn:END');
_test_driver.click('dbtn:Tak');
_test_driver.click('dbtn:Nie');
::   Wyślij do KSEF
_test_driver.menu('Funkcje:KSeF:Wyślij');
_test_driver.click('dbtn:Tak');
::  Sprawdzenie statusu przed synchronizacją
_test_driver.menu('Funkcje:Załączniki');
delay(2);
_test_driver.click('fld:@DOKUM.DOKUM(row=1)');
_test_driver.menu('Wysyłanie:Informacja z Businesslink');
_test_driver.click('dbtn:OK');
_test_driver.click('fld:@DOKUM.SW(row=1)');
_stat:=_test_driver.state('fld:@DOKUM.SW(row=1)').value;
_test_driver.assert_true('Dokument nie został wysłany do Businesslink. Status dokumentu: '+_stat,
             (_stat*'Przekazano do Businesslink')<>0);
::  Synchronizacja dokumentów
{? var_pres('_pulpit')<>type_of('')
|| _pulpit:=exec('get_desctop_id','$lib_base',_test_driver)
?};
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'open', 'Wspólne\\Obszary\\Administracja', 7);
_administracja:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@XPERTIS.ADMIN', 'title', 'Administracja', 'type', 'e');
_test_driver.click('gtab:@Businesslink');
{! _i:=1..5
|! delay(60);
   _test_driver.click('btn:@Synchronizacja dokumentów');
   _test_driver.assert('', 'wnd:@tmp.kyoaskzuwalisct', 'title', '', 'type', 's');
   _test_driver.click('btn:@OK')
!};
_test_driver.app_close(_administracja);
_test_driver.app_activate(_app_dok_spr);
::  Sprawdzenie statusu po synchronizacji
{? _res.ksef
|| _test_driver.win_action('close');
   _test_driver.click('fld:@FAKS.SYM(row=1)');
   _test_driver.filter('@FAKS.SYM', _res.symbol, '@FAKS.STATKSEF', 'Wysłany, potwierdzony');
   _test_driver.click('fld:@FAKS.SYM(row=1)');
   _size:=_test_driver.execute("FAKS.f_size()");
   _test_driver.assert_true('Dokument niepotwierdzony',_size=1)
|| _test_driver.menu('Wysyłanie:Informacja z Businesslink');
   _test_driver.click('dbtn:OK');
   _test_driver.click('fld:@DOKUM.SW(row=1)');
   _stat:=_test_driver.state('fld:@DOKUM.SW(row=1)').value;
   _test_driver.assert_true('Dokument nie został odebrany w Businesslink. Status dokumentu: '+_stat,
                (_stat*'Odebrano w Businesslink')<>0);
   _test_driver.win_action('close')
?};
_test_driver.app_close(_app_dok_spr);
_res


\spr_FV_f002
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: Sprawdzenie faktury w drugiej firmie
::   WE: _a - _test_driver
::       _b - symbol FV
::       _c - czy ksef (0 - businesslink, 1 - ksef)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;
_symbol:=_b;
_pulpit:=exec('get_desctop_id','$lib_base',_test_driver);
_test_driver.app_activate(_pulpit);
_test_driver.app_close(_pulpit);
_app_f002:=_test_driver.app_open('admin', 'admin123', app_info('cluster_group'), app_info('app_ident')-1+'2', '', '');
:: Synchronizacja
_test_driver.app_activate(_app_f002);
_test_driver.control('!desktop', 'open', 'Wspólne\\Obszary\\Administracja', 7);
_administracja:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@XPERTIS.ADMIN', 'title', 'Administracja', 'type', 'e');
_test_driver.click('gtab:@Businesslink');
delay(5);
_test_driver.click('btn:@Synchronizacja dokumentów');
_test_driver.assert('', 'wnd:@tmp.kyoaskzuwalisct', 'title', '', 'type', 's');
_test_driver.click('btn:@OK');
_test_driver.app_close(_administracja);
_test_driver.app_activate(_app_f002);
:: Sprawdzenie faktury w drugiej firmie
_test_driver.control('!desktop', 'open', 'Businesslink\\Obszary\\Dokumenty Businesslink', 7);
_dok_bl:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:bl_wer_out', 'title', 'Wychodzące', 'type', 's');
_test_driver.click('gtab:@Wszystkie przychodzące');
_test_driver.click('fld:@DOKUM.KR_OP(row=1)');
_test_driver.filter('@DOKUM.SYM_ZEW',_symbol);
_test_driver.click('fld:@DOKUM.SYM_ZEW(row=1)');
_size:=_test_driver.execute("DOKUM.f_size()");
_test_driver.assert_true('Dokument nie został odebrany w drugiej firmie',_size=1);
_test_driver.app_close(_dok_bl);
_test_driver.app_close(_app_f002);
_app_f002:=_test_driver.app_open('admin', 'admin123', app_info('cluster_group'), app_info('app_ident')-1+'1', '', '')


\importuj_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Importuje z csv dane wniosku
::   WE: _a - nazwa pliku
::   WY: _tab_tmp
::----------------------------------------------------------------------------------------------------------------------
_dane:=tab_tmp(1,
'ID','STRING[3]','ID',
'ADRES','STRING[255]','Adres www portalu',
'LOGIN_P','STRING[100]','Użytkownik portal',
'HASLO_P','STRING[100]','Hasło portal',
'TYPSP','STRING[20]','Typ dokumentu',
'KH','STRING[100]','Kontrahent',
'FORM_PLA','STRING[100]','Form płatności',
'RABAT','STRING[100]','Rabat',
'RODZAJ','STRING[100]','Rodzaj pozycji',
'NAZWA','STRING[100]','Nazwa materiału/usługi',
'ILOSC','STRING[100]','Ilość',
'CENA','STRING[100]','Cena');

_plik:=_a;
{? _plik*'/' | _plik*'\\'
|| _pth:=',nopth'
|| _pth:=''
?};
_dane.import(_plik,,1,,'UTF-8,header'+_pth,,
'ID',,1,,
'ADRES',,2,,
'LOGIN_P',,3,,
'HASLO_P',,4,,
'TYPSP',,5,,
'KH',,6,,
'FORM_PLA',,7,,
'RABAT',,8,,
'RODZAJ',,9,,
'NAZWA',,10,,
'ILOSC',,11,,
'CENA',,12,);
_dane

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:06 9f251c5639fcd3bd09ef892830f9aaf6c04d930f8bfe7b3ec165f7df6145560e81ca9a87b17631aca39e60d9c5627054fead9414fcd3adc34176f008a3e5d28b7375d84a345183324b6c85cda305f968ed789700ab821339d52b281137157910bc6a4e0e94a871087e943399db67a67175530dd41603baf5217577eeeea86e5a

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_hbn_check.fml
:: Utworzony: 19.02.2019
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Testy automatyczne dla obszaru ZWS i HBN - kontrola poprawności kodu w polach memo tabeli HBP
:: AREA: [ZWS_PAR]
::======================================================================================================================

\hbn_memo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Testy specyfikacji
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;

:: jeżeli poprzedni test na tej samej kopii obiektu testującego zakończył się błędem to nie uruchamia się
:: test bo wynik może być nieoczekiwany - poprzedni test mógł zostwić niewyczyszczone środowisko, otwarte zakładki itp.
{? _test_driver.error_no<>0 || return(0) ?};

_test_driver.exec_after_error:=0;
_test_driver.execute("exec('test_hbp_memo','$test_hbn_check')");
return(1)


\test_hbp_memo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Kontrola poprawności kodu w polach memo tabeli HBP
::----------------------------------------------------------------------------------------------------------------------
HBPKI.cntx_psh();

_f:=fopen('$test_hbn_check.log','uw',1);
{? _f
|| fwrite(_f,'INFO: Rozpoczęcie testu: test_hbn_hbp_check (formuły w specyfikacjach bankowych)')
|| HBPKI.cntx_pop();
   return(0)
?};

_licznik:=0;

HBPKI.prefix();
{? HBPKI.first()
|| {! |?
      HBP.index('HBNAZWA');
      HBP.prefix(HBPKI.ref());
      {? HBP.first()
      || {! |?
            _txt:=HBP.memo_txt(,1,'FML');
            {? _txt<>''
            || _result:=form_chk($_txt);
               {? _result.first()
               || {! |?
                     _licznik+=1;
                     _txt:='ERROR: '+HBPKI.TPK+' | '+HBP.NP+' | '+_result.ERR_DESC+' | '+$_result.ERR_LINE+' | '+$_result.ERR_COL;
                     fwrite(_f,_txt);
                     _result.next()
                  !}
               ?};
               obj_del(_result)
            ?};
            HBP.next()
         !}
      ?};
      HBPKI.next()
   !}
?};
{? _f
|| fwrite(_f,'INFO: Zakończenie testu: test_hbn_hbp_check (formuły w specyfikacjach bankowych)')
|| HBPKI.cntx_pop();
   return(0)
?};
fclose(_f);

HBPKI.cntx_pop()

:Sign Version 2.0 jowisz:1048 2023/06/23 14:12:56 134d0cb17b5ca9eaa6621cbd22216a40f5a05c72c1fbb254285b490611b666e6b6e522ba2a7edd37c344fc07a3a3b77eb9f9aec551e999367cac41e77d7163c3149e517ef7a4f2628221755c77dcb943dc6ddce9876814c3fa5fd25594ba96ff406d10e5895af772cc216a175a06972bf1c43c7be1849457a1cb86a567fbae6a

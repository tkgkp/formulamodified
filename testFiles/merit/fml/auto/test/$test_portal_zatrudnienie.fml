:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_portal_zatrudnienie.fml
:: Utworzony: 10.02.2022
:: Autor: WHAN
:: Systemy:
::======================================================================================================================
:: Zawartość: Skrypty testujące obsługę wniosków
::======================================================================================================================
::    UWAGA test wprowadza dane na podstawie pliku $employment_change.csv
::    pole AKCJE oznacza jakie akcje i w jakiej kolejności wykonuje test:
::    SP - Wprowadzenie wniosku (Portal)
::    SPR - Wywołanie komunikacji z portalem (Merit) wysyłanie przetwarzanie odbieranie
::    AM1 - Akceptacja wniosku 1 poziom (Merit)
::    AP1 - Akceptacja wniosku 1 poziom (Portal)
::    AM2 - Akceptacja wniosku 2 poziom (Merit)
::    AP2 - Akceptacja wniosku 2 poziom (Portal)
::    RM1 - Odrzucenie wniosku 1 poziom (Merit)
::    RP1 - Odrzucenie wniosku 1 poziom (Portal)
::    RM2 - Odrzucenie wniosku 2 poziom (Merit)
::    RP2 - Odrzucenie wniosku 2 poziom (Portal)
::    CLP1 - Zamknięcie 1 poziom (Portal)
::    CLP2 - Zamknięcie 2 poziom (Portal)
::    WP1 - Wycofanie 1 poziom (Portal)
::    WP2 - Wycofanie 2 poziom (Portal)
::    CP1 - Sprawdzenie statusu po akceptacji/odrzuceniu 1 poziom (Portal)
::    CP2 - Sprawdzenie statusu po akceptacji/odrzuceniu 2 poziom (Portal)
::    RP  - Sprawdzenie statusu po odrzuceniu, poprawienie wprowadzonych danych, ponowne wysłanie wniosku (Portal)
::    MP  - Sprawdzenie maila
::    DM  - Usuwanie zmian danych po teście (Merit)
::
\portal_zatrudnienie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: główna formuła testu wykonywana dla działania na końcówce
::   WE: [_a] - [STRING] ID testu/testów z pliku csv
::       [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - tryb użytkownika
::       [_c] - [STRING] 'Wniosek o zatrudnienie'/'Zmiana zatrudnienia' - typ wniosku, domyślnie 'Zmiana zatrudnienia'
::   WY:
:: ~OST: INFCOPY,INFEXISTS,INFMKDIR,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 | type_of(_a)<>type_of('') | _a=''
|| _cases:=''
|| _cases:=_a
?};
{? var_press('_b')<=0 | type_of(_b)<>type_of(1) | _b=0
|| _mode:=0
|| _mode:=1
?};
_POM:=0;
{? var_press('_c')<=0 | type_of(_c)<>type_of('') | _c=''
|| _application:='Zmiana zatrudnienia';
   _type:='--change'
|| _application:=_c;
   {? _application='Zmiana zatrudnienia' | _application='POM zmiana zatrudnienia'
   || _type:='--change'
   || _type:='--start'
   ?}
?};
_sep_cli:=exec('sep','#file',0);
: definiowanie ścieżki lokalnej
{? var_press('__test_path')=type_of('')
|| _tmp_dir:=__test_path
|| _tmp_dir:=3+tmp_dir()+'auto'
?};
{? _mode=1
|| undefine();
   define('PATH','','','Ścieżka',100,100);
   DEFINE.PATH:=_tmp_dir;
   def_btn('text=%1,icon=xwin16.png:13'['OK'@],'key:F2');
   def_btn('text=%1,icon=xwin16.png:14'['Anuluj'@],'key:Esc');
   {? def_edit(,'Podaj ścieżkę lokalnego folderu środowiska testowego:'@)=0
   || undefine();
      return(~~)
   ?};
   __test_path:=_tmp_dir:=DEFINE.PATH;
   undefine()
?};
{? _tmp_dir+1='/' | _tmp_dir+1='\\'
|| _tmp_dir:=_tmp_dir-1
?};
{? _tmp_dir=3+tmp_dir()+'auto' & fexists('@'+(3+tmp_dir())+'auto',0)=0
|| fmkdir('@'+(3+tmp_dir()),'auto')
?};
{? fexists('@'+_tmp_dir)=0
|| return(0)
?};
:: sprawdzanie potrzebnych plików i ewentualne kopiowanie z serwera na końcówkę
{? ~fexists(_tmp_dir+'/test_portal')
|| fmkdir('@'+_tmp_dir,'test_portal')
?};
_tmp_dir:=_tmp_dir+_sep_cli+'test_portal';
_pth_jar:=pth_dir('$.fml')-3+'jar'+'/';
_pth_driver:=pth_dir('$.fml')-3+'driver'+'/';
_jar:='PortalTest.jar';
{? ~fexists('@'+_tmp_dir+'/'+_jar)
|| {? ~fexists(_pth_jar-1)
   || fmkdir(_pth_jar-4,'jar')
   ?};
::   exec('svn_export','%transfer',_svn+'merit/fml/auto/jar/PortalTest.jar',_pth_jar+_jar,_pocztam);
   fcopy(_pth_jar+_jar,'@'+_tmp_dir+'/'+_jar,0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/'+'csv')
|| fmkdir('@'+_tmp_dir,'csv')
?};
{? ~fexists('@'+_tmp_dir+'/csv/$employment_change.csv')
|| fcopy(pth_dir('$.csv')+'/$employment_change.csv','@'+_tmp_dir+'/csv/$employment_change.csv',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/csv/$employment_start.csv')
|| fcopy(pth_dir('$.csv')+'/$employment_start.csv','@'+_tmp_dir+'/csv/$employment_start.csv',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/csv/$pom_employment_change.csv')
|| fcopy(pth_dir('$.csv')+'/$pom_employment_change.csv','@'+_tmp_dir+'/csv/$pom_employment_change.csv',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/'+'driver')
|| fmkdir('@'+_tmp_dir,'driver')
?};
{? ~fexists('@'+_tmp_dir+'/driver/'+'chromedriver.exe')
||
::   exec('svn_export','%transfer',_svn+'merit/fml/auto/driver/chromedriver.exe','@'+_tmp_dir+'/driver/'+'chromedriver.exe',_pocztam)
   fcopy(_pth_driver+'chromedriver.exe','@'+_tmp_dir+'/driver/'+'chromedriver.exe',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/log')
|| fmkdir('@'+_tmp_dir,'log')
?};
{? _application='Wniosek o zatrudnienie'
|| _dane_wniosek:=exec('importuj_dane_wniosek2','$test_portal_zatrudnienie','@'+_tmp_dir+'/csv/$employment_start.csv')
|? _application='POM zmiana zatrudnienia'
|| _dane_wniosek:=exec('importuj_dane_wniosek','$test_portal_zatrudnienie','@'+_tmp_dir+'/csv/$pom_employment_change.csv')
|| _dane_wniosek:=exec('importuj_dane_wniosek','$test_portal_zatrudnienie','@'+_tmp_dir+'/csv/$employment_change.csv')
?};
{? ~_dane_wniosek.first()
|| 'Brak zaimportowanych danych';
   return(0)
?};
::_robot:=(app_info('app_ident')='robot' | app_info('app_ident')='robot_manager');
_robot:=app_info('app_par1')='START_TESTY_PORTAL';
_test_driver:=exec('init','$lib_base','$manager.log');
{? ~_robot
||
   _pulpit:=exec('get_desctop_id','$lib_base',_test_driver);
   {? _pulpit<>''
   || _test_driver.app_close(_pulpit)
   ?}
?};
{? _mode=0 & _robot
|| exec('clear_zatrudnienie','$test_portal_zatrudnienie',_dane_wniosek,2)
?};
exec('MBJAR','#object');

{? _cases<>''
|| _cases_spli:=spli_str(_cases,',');
   _case_no:=1
?};
{! |?
   {? _cases<>''
   || {? ~_dane_wniosek.find_tab(,'ID',,'=',_cases_spli[_case_no])
      || _file:=fopen('$test_portal_zatrudnienie.log','ua',1);
         fwrite(_file,'\nERROR: Nie odnaleziono wniosku w pliku *.csv. ID: '+_dane_wniosek.ID);
         fclose(_file);
         return(0)
      ?}
   ?};
   {? ~fexists('@'+_tmp_dir+'/log/'+_dane_wniosek.ID)
   || fmkdir('@'+_tmp_dir+'/log',_dane_wniosek.ID)
   ?};
   {? _dane_wniosek.AKCJE<>''
   || _akcje:=spli_str(_dane_wniosek.AKCJE,',');
      _spr:=0;
      {! _i:=1..obj_len(_akcje)
      |!
::    pole AKCJE oznacza jakie akcje i w jakiej kolejności wykonuje test:
::    SP - Wprowadzenie wniosku (Portal)
::    SPR - Wywołanie komunikacji z portalem (Merit) wysyłanie przetwarzanie odbieranie
::    AM1 - Akceptacja wniosku 1 poziom (Merit)
::    AP1 - Akceptacja wniosku 1 poziom (Portal)
::    AM2 - Akceptacja wniosku 2 poziom (Merit)
::    AP2 - Akceptacja wniosku 2 poziom (Portal)
::    RM1 - Odrzucenie wniosku 1 poziom (Merit)
::    RP1 - Odrzucenie wniosku 1 poziom (Portal)
::    RM2 - Odrzucenie wniosku 2 poziom (Merit)
::    RP2 - Odrzucenie wniosku 2 poziom (Portal)
::    CLP1 - Zamknięcie 1 poziom (Portal)
::    CLP2 - Zamknięcie 2 poziom (Portal)
::    WP1 - Wycofanie 1 poziom (Portal)
::    WP2 - Wycofanie 2 poziom (Portal)
::    CP1 - Sprawdzenie statusu po akceptacji/odrzuceniu 1 poziom (Portal)
::    CP2 - Sprawdzenie statusu po akceptacji/odrzuceniu 2 poziom (Portal)
::    RP  - Sprawdzenie statusu po odrzuceniu, poprawienie wprowadzonych danych, ponowne wysłanie wniosku (Portal)
::    CW0 - Sprawdzenie dostępności akcji Wycofaj po akceptacji 0 poziom (Portal)
::    CS-x-y - Sprawdzenie statusu wniosku jako użytkownik poziomu x i porownanie z konkretnym statusem
::    MP  - Sprawdzenie maila
::    DM  - Usuwanie zmian danych po teście (Merit)
         {? _akcje[_i]='SP'
         || {? _application='Wniosek o zatrudnienie'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-start',_dane_wniosek.ID)
            |? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment_change_pom','--new','0',_dane_wniosek.ID,$(_i-_spr),'0')
            |? _application='Zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-change',_dane_wniosek.ID)
            ?}
         |? _akcje[_i]='SPR'
         || _spr+=1;
            exec('send_process_receive','$test_portal_zatrudnienie',_test_driver)
         |? _akcje[_i]='AM1'
         || exec('accept_reject_zatrudnienie','$test_portal_zatrudnienie',_dane_wniosek,_mode,1,1);
            fcopy('$accept_zatrudnienie_L1.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.accept_employment_L1.log',1,0,1)
         |? _akcje[_i]='AP1'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment_change_pom','--accept','1',_dane_wniosek.ID,$(_i-_spr),'0')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-accept',_type,_dane_wniosek.ID,'1')
            ?}
         |? _akcje[_i]='AM2'
         || exec('accept_reject_zatrudnienie','$test_portal_zatrudnienie',_dane_wniosek,_mode,2,1);
            fcopy('$accept_zatrudnienie_L2.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.accept_employment_L2.log',1,0,1)
         |? _akcje[_i]='AP2'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment_change_pom','--accept','2',_dane_wniosek.ID,$(_i-_spr),'0')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-accept',_type,_dane_wniosek.ID,'2')
            ?}
         |? _akcje[_i]='RM1'
         || exec('accept_reject_zatrudnienie','$test_portal_zatrudnienie',_dane_wniosek,_mode,1,0);
            fcopy('$reject_zatrudnienie_L1.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.reject_employment_L1.log',1,0,1)
         |? _akcje[_i]='RP1'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-reject',_dane_wniosek.ID,'1')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-reject',_type,_dane_wniosek.ID,'1')
            ?}
         |? _akcje[_i]='RM2'
         || exec('accept_reject_zatrudnienie','$test_portal_zatrudnienie',_dane_wniosek,_mode,2,0);
            fcopy('$reject_zatrudnienie_L2.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.reject_employment_L2.log',1,0,1)
         |? _akcje[_i]='RP2'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-reject',_dane_wniosek.ID,'2')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-reject',_type,_dane_wniosek.ID,'2')
            ?}
         |? _akcje[_i]='CLP1'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment_change_pom','--close','1',_dane_wniosek.ID,$(_i-_spr),'0')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-close',_type,_dane_wniosek.ID,'1')
            ?}
         |? _akcje[_i]='CLP2'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment_change_pom','--close','2',_dane_wniosek.ID,$(_i-_spr),'0')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-close',_type,_dane_wniosek.ID,'2')
            ?}
         |? _akcje[_i]='WP1'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-withdraw',_dane_wniosek.ID,'1')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-withdraw',_type,_dane_wniosek.ID,'1')
            ?}
         |? _akcje[_i]='WP2'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-withdraw',_dane_wniosek.ID,'2')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-withdraw',_type,_dane_wniosek.ID,'2')
            ?}
         |? _akcje[_i]='CP1'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment_change_pom','--check','1',_dane_wniosek.ID,$(_i-_spr),'0')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-check',_dane_wniosek.ID,'1')
            ?}
         |? _akcje[_i]='CP2'
         || {? _application='POM zmiana zatrudnienia'
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment_change_pom','--check','2',_dane_wniosek.ID,$(_i-_spr),'0')
            || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-check',_dane_wniosek.ID,'2')
            ?}
         |? _akcje[_i]='CW0'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment_change_pom','--check-revoke','0',_dane_wniosek.ID,$(_i-_spr),'0')
         |? _akcje[_i]='IM'
         || exec('insert_zatrudnienie','$test_portal_zatrudnienie',_dane_wniosek);
            fcopy('$insert_absence.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$(_i-_spr)+'.insert_absence.log',1,0,1)
         |? _akcje[_i]='RP'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment-correct',_dane_wniosek.ID)
         |? _akcje[_i]='MP'
         || exec('portal_mail','$test_mail',_dane_wniosek.ID,'./log/'+_dane_wniosek.ID+'/'+$(_i-_spr)+'.mail.log')
         |? _akcje[_i]='DM'
         || exec('clear_zatrudnienie','$test_portal_zatrudnienie',_dane_wniosek,_mode);
            fcopy('$clear_zatrudnienie.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$(_i-_spr)+'.clear_employment.log',1,0,1)
         |? 2+_akcje[_i]='CS'
         || _cs_par:=spli_str(_akcje[_i],'-');
             MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--employment_change_pom','--check',_cs_par[2],_dane_wniosek.ID,$(_i-_spr),_cs_par[3])
         ?}
      !};
      &_akcje;
      {? var_press('_cs_par')>=0 || &_cs_par ?}
   ?};
   {? _cases<>''
   || _case_no+=1;
      {? obj_len(_cases_spli)>=_case_no
      || _loop:=1
      || _loop:=0
      ?}
   || _loop:=_dane_wniosek.next()
   ?};
   _loop
!};
{? ~_robot
|| _test_driver.app_open('admin', 'admin123', app_info('cluster_group'), app_info('app_ident'), '', '');
::   exec('set_active_win','$lib_base',_test_driver,_cur_win);
   _exec:='FUN.info(\'Wyniki testu znajdują się w katalogu '+_tmp_dir+_sep_cli+'log\')';
   _test_driver.execute($(gsub(_exec,'\\','\\\\')))
?};
~~


\accept_reject_zatrudnienie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Główna metoda akceptująca/odrzucająca wnioskek zatrudnienieowy
::   WE: _a - dane z pliku csv
::       [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika
::       [_c] - [1/2] 1 - domyślnie, poziom akceptacji
::       [_d] - [0/1] 1 - domyślnie, 0 - odrzucenie, 1 - akceptacja
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_dane_wniosek:=_a;
{? var_press('_b')<0 | type_of(_b)<>type_of(1) | type_of(_b)=type_of(1) & _b=0
|| _mode:=0
|| _mode:=1
?};
{? var_press('_c')<0 | type_of(_c)<>type_of(1) | type_of(_c)=type_of(1) & _c=2
|| _level:=2
|| _level:=1
?};
{? var_press('_d')<0 | type_of(_d)<>type_of(1) | type_of(_d)=type_of(1) & _d=1
|| _accept:=1
|| _accept:=0
?};
{? _accept
|| _log_name:='$accept_zatrudnienie_L'+$_level+'.log'
|| _log_name:='$reject_zatrudnienie_L'+$_level+'.log'
?};
ferase(pth_dir()+'/system/admin/'+_log_name);
ferase(pth_dir('$accept_zatrudnienie_L1.log')+'/'+_log_name);
_test_driver:=exec('init','$lib_base',_log_name);
:: @WHEN
{? _level=1
|| _login:=_dane_wniosek.LOGIN_M1;
   _haslo:=_dane_wniosek.HASLO_M1
|| _login:=_dane_wniosek.LOGIN_M2;
   _haslo:=_dane_wniosek.HASLO_M2
?};
_pulpit:=_test_driver.app_open(_login, _haslo, app_info('cluster_group'), 'merit001', '', '');
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'todo');
_app_todo:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@BI_TODO.WER', 'title', 'Zadania', 'type', 's');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
_test_driver.execute("exec('receive','$test_portal_zatrudnienie')");
_test_driver.execute("exec('process','$test_portal_zatrudnienie')");
_test_driver.execute("exec('send','$test_portal_zatrudnienie')");
_file:=fopen(_log_name,'ua',1);
fwrite(_file,'\n\nINFO: Poziom: ' +$_level+ '. ' + {? _accept || 'Akceptacja' || 'Odrzucenie' ?} + ' wniosku ID: '+_dane_wniosek.ID);
fclose(_file);
_test_driver.send('', '^{F5}', '');
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=1)');
_test_driver.filter('@BI_TODO.DT', $date(),
                    '@BI_TODO.BI_PREL.DESC', 'Zaakceptuj zmiana zatrudnienia: '+_dane_wniosek.NAZWISKO+' '+_dane_wniosek.IMIE,
                    '@BI_TODO.B_DOMAIN.NAME', 'Obieg wniosków');
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=2)');
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=1)');
_test_driver.send('', '{PGUP}', '');
_test_driver.menu('Rozpocznij');
_test_driver.assert('', 'wnd:@tmp.obe_faw_red', 'title', 'Wniosek', 'type', 'e');
{? _accept
|| _test_driver.click('btn:@Akceptuj')
|| _test_driver.click('btn:@Odrzuć')
?};
_test_driver.assert('', 'wnd:@EDOKOS.obeakckom', 'title', {? _accept || 'Akceptacja' || 'Odrzucenie' ?}, 'type', 'e');
_test_driver.send( {? _accept
                   || {? _level=1 || _dane_wniosek.AKCEPT_1 || _dane_wniosek.AKCEPT_2 ?}
                   || {? _level=1 || _dane_wniosek.ODRZUC_1 || _dane_wniosek.ODRZUC_2 ?}
                   ?} , '', 'fld:@EDOKOS.UW_DL');
{? _accept
|| _test_driver.click('btn:@Zakończ')
|| _test_driver.click('btn:@Odrzuć')
?};

_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
_test_driver.execute("exec('receive','$test_portal_zatrudnienie')");
_test_driver.execute("exec('process','$test_portal_zatrudnienie')");
_test_driver.execute("exec('send','$test_portal_zatrudnienie')");
delay(30);
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
_test_driver.execute("exec('receive','$test_portal_zatrudnienie')");
_test_driver.execute("exec('process','$test_portal_zatrudnienie')");
_test_driver.execute("exec('send','$test_portal_zatrudnienie')");
delay(60);
_test_driver.app_close(_app_todo);
_test_driver.app_close(_pulpit);
~~


\clear_zatrudnienie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Czyści dane z testu
::   WE: _a - dane z pliku csv
::      [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika; 2 - czyszczenie danych przed testami
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_log_name:='$clear_zatrudnienie.log';
_test_driver:=exec('init','$lib_base',_log_name);
{? var_press('_b')<0 | _b=0
|| _mode:=0
|| _mode:=_b
?};
_dane_wniosek:=_a;
:: @WHEN
ferase(pth_dir()+'/system/admin/'+_log_name);
ferase(pth_dir(_log_name)+'/'+_log_name);
_pulpit:=_test_driver.app_open('admin', 'admin123', app_info('cluster_group'), 'merit001', '', '');
:: usunięcie wprowadzonego zatrudnienia w Zdarzeniach kadrowych
_test_driver.control('!desktop', 'open', 'Kadry\\Obszary\\Zdarzenia kadrowe', 7);
_app_zdarz_kadr:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:PKD_EZK', 'title', '', 'type', 's');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("exec('receive','$test_portal_zatrudnienie')");
_test_driver.execute("exec('process','$test_portal_zatrudnienie')");
_test_driver.execute("exec('send','$test_portal_zatrudnienie')");
_file:=fopen(_log_name,'ua',1);
fwrite(_file,'\n\nINFO: Usunięcie danych wniosku ID: '+_dane_wniosek.ID);
fclose(_file);
:: usunięcie wniosku z kadr
{? var_press('ZMIANAOD',_dane_wniosek)>=0
|| {? _mode<>2
   || _test_driver.click('fld:@P.OSOBA.PIERWSZE(row=1)');
      _test_driver.filter('@P.OSOBA.NAZWISKO', _dane_wniosek.NAZWISKO,
                          '@P.OSOBA.PIERWSZE', _dane_wniosek.IMIE);
      _test_driver.click('fld:@P.OSOBA.NAZWISKO(row=1)');
      _test_driver.click('gtab:@Umowy o pracę');

      _test_driver.click('wnd:@H.WER_PR');
      _test_driver.click('fld:@H.DO(row=1)');
      _test_driver.filter('@H.OD', gsub(_dane_wniosek.ZMIANAOD,'-','/'),
                          '@H.ST.ST', _dane_wniosek.STANOWI,
                          '@H.S1', _dane_wniosek.WYNAGR1)
   || _test_driver.click('fld:@P.OSOBA.PIERWSZE(row=1)');
      _test_driver.filter('@P.OSOBA.NAZWISKO', 'Abram',
                          '@P.OSOBA.PIERWSZE', 'Adam');
      _test_driver.click('fld:@P.OSOBA.NAZWISKO(row=1)');
      _test_driver.click('gtab:@Umowy o pracę');

      _test_driver.click('wnd:@H.WER_PR');
      _test_driver.click('fld:@H.DO(row=1)');

      _test_driver.filter('@H.OD', $(date~1))
   ?};

   _test_driver.click('fld:@H.DO(row=2)');
   _test_driver.click('fld:@H.DO(row=1)');
   _size:=_test_driver.execute("{? H.f_active()
                                   || H.f_size()
                                   || H.size()
                                   ?}");
   {? type_of(_size)=type_of(1)
   || {! _i:=1.._size |!
         _test_driver.click('fld:@H.OD(row=2)');
         _test_driver.click('fld:@H.OD(row=1)');
         _test_driver.menu('Usuń');
         _test_driver.click('dbtn:Tak');
         _test_driver.click('dbtn:Tak')
      !}
   ?}
?};
_test_driver.app_close(_app_zdarz_kadr);
_test_driver.app_activate(_pulpit);
:: usunięcie wniosku z obiegów
_test_driver.control('!desktop', 'open', 'Obieg wniosków\\Obszary\\Wnioski w obiegu - techniczne', 7);
_app_wnioski_tech:=_test_driver.app_wait_for_change();

_test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
_test_driver.execute("EDOKUM.prefix()");
_test_driver.click('wnd:@#edokatrt');
_test_driver.click('wnd:@edokumtmp01');
_test_driver.click('fld:@EDOKUM.DATAW(row=1)');
_size:=_test_driver.execute("{? EDOKUM.f_active()
                             || EDOKUM.f_size()
                             || EDOKUM.size()
                             ?}");
{? _size>0
||
   {? _mode<>2
   ||
      _test_driver.filter('@EDOKUM.DATAW', $date(),
                          '@EDOKUM.USERS.KOD', _dane_wniosek.LOGIN_M,
                          '@EDOKUM.TYP.NAZWA', 'Zmiana zatrudnienia');
                          _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))")
   || _test_driver.filter('@EDOKUM.TYP.NAZWA', 'Zmiana zatrudnienia');
      _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))")
   ?};
   _test_driver.click('wnd:@#edokatrt');
   _test_driver.click('wnd:@edokumtmp01');

   _test_driver.click('fld:@EDOKUM.DATAW(row=1)');
   _size:=_test_driver.execute("{? EDOKUM.f_active()
                                || EDOKUM.f_size()
                                || EDOKUM.size()
                                ?}");
   {? type_of(_size)=type_of(1)
   || {! _i:=1.._size |!
         _test_driver.click('wnd:@edokumtmp01');
         _test_driver.click('fld:@EDOKUM.TYP.NAZWA(row=1)');
         _test_driver.menu('Usuń');
         _test_driver.click('dbtn:Tak')
      !}
   ?}
?};

_test_driver.execute("EDOKUM.use('skid_v_1')");
_test_driver.execute("EDOKUM.prefix()");
_test_driver.click('wnd:@#edokatrt');
_test_driver.click('wnd:@edokumtmp01');
_test_driver.click('fld:@EDOKUM.DATAW(row=1)');
_size:=_test_driver.execute("{? EDOKUM.f_active()
                             || EDOKUM.f_size()
                             || EDOKUM.size()
                             ?}");
{? _size>0
||
   {? _mode<>2
   ||
      _test_driver.filter('@EDOKUM.DATAW', $date(),
                          '@EDOKUM.USERS.KOD', _dane_wniosek.LOGIN_M,
                          '@EDOKUM.TYP.NAZWA', 'Zmiana zatrudnienia');
                          _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))")
   || _test_driver.filter('@EDOKUM.TYP.NAZWA', 'Zmiana zatrudnienia');
      _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))")
   ?};
   _test_driver.click('wnd:@#edokatrt');
   _test_driver.click('wnd:@edokumtmp01');

   _test_driver.click('fld:@EDOKUM.DATAW(row=1)');
   _size:=_test_driver.execute("{? EDOKUM.f_active()
                                || EDOKUM.f_size()
                                || EDOKUM.size()
                                ?}");
   {? type_of(_size)=type_of(1)
   || {! _i:=1.._size |!
         _test_driver.click('wnd:@edokumtmp01');
         _test_driver.click('fld:@EDOKUM.TYP.NAZWA(row=1)');
         _test_driver.menu('Usuń');
         _test_driver.click('dbtn:Tak')
      !}
   ?}
?};
:: wysłanie na portal
delay(5);
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("exec('receive','$test_portal_zatrudnienie')");
_test_driver.execute("exec('process','$test_portal_zatrudnienie')");
_test_driver.execute("exec('send','$test_portal_zatrudnienie')");
_test_driver.app_close(_app_wnioski_tech);
_test_driver.app_close(_pulpit);
~~


\send_process_receive
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Wywołanie komunikacji z portalem (Merit) wysyłanie przetwarzanie odbieranie
::   [_a] - _test_driver
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')>0
|| _test_driver:=_a
|| _test_driver:=exec('init','$lib_base','$manager.log')
?};
_pulpit:=_test_driver.app_open('admin', 'admin123', app_info('cluster_group'), 'merit001', '', '');
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'todo');
_app_todo:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@BI_TODO.WER', 'title', 'Zadania', 'type', 's');
{! _i:=1..2
|! _test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
   _test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
   _test_driver.execute("exec('receive','$test_portal_zatrudnienie')");
   _test_driver.execute("exec('process','$test_portal_zatrudnienie')");
   _test_driver.execute("exec('send','$test_portal_zatrudnienie')");
   delay(15)
!};
_test_driver.app_close(_app_todo);
_test_driver.app_close(_pulpit);
~~


\receive
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: testuj odbieranie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
exec('process_receive','sync_mwa','PORTAL_WNIOSKI',,,,,0);
EDOKUM.cntx_pop()


\process
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: testuj przetwarzanie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
exec('process','portal_wnioski',1,0);
EDOKUM.cntx_pop()


\send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: testuj wysyłanie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
exec('process_send','sync_mwa','PORTAL_WNIOSKI',,,,,,,0);
EDOKUM.cntx_pop()


\importuj_dane_wniosek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Importuje z csv dane wniosku
::   WE: _a - nazwa pliku
::   WY: _tab_tmp
::----------------------------------------------------------------------------------------------------------------------
_dane_w:=tab_tmp(1,
'ID','STRING[3]','ID',
'AKCJE','STRING[50]','Kody akcji do wykonania',
'ADRES','STRING[255]','Adres www portalu',
'LOGIN_P','STRING[100]','Użytkownik portal',
'HASLO_P','STRING[100]','Hasło portal',
'IMIE','STRING[40]','Imię',
'NAZWISKO','STRING[40]','Nazwisko',
'LOGIN_P1','STRING[50]','Użytkownik Portal akc. 1 poziom',
'HASLO_P1','STRING[50]','Hasło Portal akc. 1 poziom',
'LOGIN_P2','STRING[50]','Użytkownik Portal akc. 2 poziom',
'HASLO_P2','STRING[50]','Hasło Portal akc. 2 poziom',
'LOGIN_M','STRING[20]','Użytkownik Merit',
'LOGIN_M1','STRING[20]','Użytkownik Merit akc. 1 poziom',
'HASLO_M1','STRING[100]','Hasło Merit akc. 1 poziom',
'LOGIN_M2','STRING[20]','Użytkownik Merit akc. 2 poziom',
'HASLO_M2','STRING[100]','Hasło Merit akc. 2 poziom',
'PRACOWNI','STRING[100]','Typ zatrudnienieu',
'ZMIANAOD','STRING[10]','Data zmiany',
'DZIAL','STRING[100]','Dział',
'STANOWI','STRING[100]','Stanowisko',
'POZYCJA','STRING[100]','Pozycja w organizacji',
'MIEJSCE','STRING[100]','Miejsce pracy',
'ETAT_L','STRING[10]','Licznik wymiaru zatrudnienia',
'ETAT_M','STRING[10]','Mianownik wymiaru zatrudnienia',
'CHARAKTE','STRING[100]','Charakter pracy',
'WYNAGR1','STRING[10]','Wynagrodzenie',
'WYNAGR2','STRING[10]','Stały dodatek funkcyjny',
'WYNAGR3','STRING[10]','Stała premia',
'BENEFITY','STRING[100]','Benefity',
'INFO_DOD','STRING[255]','Informacje dodatkowe',
'AKCEPT_1','STRING[100]','Uwagi akceptacji poziom 1',
'AKCEPT_2','STRING[100]','Uwagi akceptacji poziom 2',
'ODRZUC_1','STRING[100]','Uwagi odrzucenia poziom 1',
'ODRZUC_2','STRING[100]','Uwagi odrzucenia poziom 2',
'STATUS_1','STRING[100]','Status po akceptacji/odrzuceniu poziom 1',
'STATUS_2','STRING[100]','Status po akceptacji/odrzuceniu poziom 2');

_plik:=_a;
{? _plik*'/' | _plik*'\\'
|| _pth:=',nopth'
|| _pth:=''
?};
_dane_w.import(_plik,,1,,'UTF-8,header'+_pth,,
'ID',,1,,
'AKCJE',,2,,
'ADRES',,3,,
'LOGIN_P',,4,,
'HASLO_P',,5,,
'IMIE',,6,,
'NAZWISKO',,7,,
'LOGIN_P1',,8,,
'HASLO_P1',,9,,
'LOGIN_P2',,10,,
'HASLO_P2',,11,,
'LOGIN_M',,12,,
'LOGIN_M1',,13,,
'HASLO_M1',,14,,
'LOGIN_M2',,15,,
'HASLO_M2',,16,,
'PRACOWNI',,17,,
'ZMIANAOD',,18,,
'DZIAL',,19,,
'STANOWI',,20,,
'POZYCJA',,21,,
'MIEJSCE',,22,,
'ETAT_L',,23,,
'ETAT_M',,24,,
'CHARAKTE',,25,,
'WYNAGR1',,26,,
'WYNAGR2',,27,,
'WYNAGR3',,28,,
'BENEFITY',,29,,
'INFO_DOD',,30,,
'AKCEPT_1',,31,,
'AKCEPT_2',,32,,
'ODRZUC_1',,33,,
'ODRZUC_2',,34,,
'STATUS_1',,35,,
'STATUS_2',,36,);

{? _dane_w.first()
|| {! |?
:: jeśli data od/do pusta
      {? _dane_w.ZMIANAOD=''
      || _dane_w.ZMIANAOD:=gsub($date(),'/','-');
         _dane_w.put()
      ?};
      _dane_w.next()
   !}
?};
_dane_w


\importuj_dane_wniosek2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Importuje z csv dane wniosku
::   WE: _a - nazwa pliku
::   WY: _tab_tmp
::----------------------------------------------------------------------------------------------------------------------
_dane_w:=tab_tmp(1,
'ID','STRING[3]','ID',
'AKCJE','STRING[50]','Kody akcji do wykonania',
'ADRES','STRING[255]','Adres www portalu',
'LOGIN_P','STRING[100]','Użytkownik portal',
'HASLO_P','STRING[100]','Hasło portal',
'LOGIN_P1','STRING[50]','Użytkownik Portal akc. 1 poziom',
'HASLO_P1','STRING[50]','Hasło Portal akc. 1 poziom',
'LOGIN_P2','STRING[50]','Użytkownik Portal akc. 2 poziom',
'HASLO_P2','STRING[50]','Hasło Portal akc. 2 poziom',
'LOGIN_M','STRING[20]','Użytkownik Merit',
'LOGIN_M1','STRING[20]','Użytkownik Merit akc. 1 poziom',
'HASLO_M1','STRING[100]','Hasło Merit akc. 1 poziom',
'LOGIN_M2','STRING[20]','Użytkownik Merit akc. 2 poziom',
'HASLO_M2','STRING[100]','Hasło Merit akc. 2 poziom',
'IMIE','STRING[40]','Imię',
'NAZWISKO','STRING[40]','Nazwisko',
'DZIAL','STRING[100]','Dział',
'STANOWI','STRING[100]','Stanowisko',
'POZYCJA','STRING[100]','Pozycja w organizacji',
'PRZEL','STRING[100]','Przełożony',
'MIEJSCE','STRING[100]','Miejsce pracy',
'CHARAKTE','STRING[100]','Charakter pracy',
'ETAT_L','STRING[10]','Licznik wymiaru zatrudnienia',
'ETAT_M','STRING[10]','Mianownik wymiaru zatrudnienia',
'WYNAGR','STRING[10]','Wynagrodzenie',
'BENEFITY','STRING[100]','Benefity',
'DODATEKF','STRING[10]','Stały dodatek funkcyjny',
'DODATEK','STRING[10]','Dodatek',
'UMOWA_T','STRING[40]','Rodzaj umowy',
'DATA_ZAT','STRING[10]','Data zatrudnienia',
'UMOWA_OD','STRING[10]','Umowa od',
'UMOWA_DO','STRING[10]','Umowa do',
'INFO_DOD','STRING[255]','Informacje dodatkowe',
'AKCEPT_1','STRING[100]','Uwagi akceptacji poziom 1',
'AKCEPT_2','STRING[100]','Uwagi akceptacji poziom 2',
'ODRZUC_1','STRING[100]','Uwagi odrzucenia poziom 1',
'ODRZUC_2','STRING[100]','Uwagi odrzucenia poziom 2',
'STATUS_1','STRING[100]','Status po akceptacji/odrzuceniu poziom 1',
'STATUS_2','STRING[100]','Status po akceptacji/odrzuceniu poziom 2');

_plik:=_a;
{? _plik*'/' | _plik*'\\'
|| _pth:=',nopth'
|| _pth:=''
?};
_dane_w.import(_plik,,1,,'UTF-8,header'+_pth,,
'ID',,1,,
'AKCJE',,2,,
'ADRES',,3,,
'LOGIN_P',,4,,
'HASLO_P',,5,,
'LOGIN_P1',,6,,
'HASLO_P1',,7,,
'LOGIN_P2',,8,,
'HASLO_P2',,9,,
'LOGIN_M',,10,,
'LOGIN_M1',,11,,
'HASLO_M1',,12,,
'LOGIN_M2',,13,,
'HASLO_M2',,14,,
'IMIE',,15,,
'NAZWISKO',,16,,
'DZIAL',,17,,
'STANOWI',,18,,
'POZYCJA',,19,,
'PRZEL',,20,,
'MIEJSCE',,21,,
'CHARAKTE',,22,,
'ETAT_L',,23,,
'ETAT_M',,24,,
'WYNAGR',,25,,
'BENEFITY',,26,,
'DODATEKF',,27,,
'DODATEK',,28,,
'UMOWA_T',,29,,
'DATA_ZAT',,30,,
'UMOWA_OD',,31,,
'UMOWA_DO',,32,,
'INFO_DOD',,33,,
'AKCEPT_1',,34,,
'AKCEPT_2',,35,,
'ODRZUC_1',,36,,
'ODRZUC_2',,37,,
'STATUS_1',,38,,
'STATUS_2',,39,);

{? _dane_w.first()
|| {! |?
:: jeśli data pusta
      {? _dane_w.DATA_ZAT=''
      || _dane_w.DATA_ZAT:=gsub($date(),'/','-');
         _dane_w.put()
      ?};
      {? _dane_w.UMOWA_OD=''
      || _dane_w.UMOWA_OD:=gsub($date(),'/','-');
         _dane_w.put()
      ?};
      _dane_w.next()
   !}
?};
_dane_w

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:06 7729a02b63c01b073e52465f94539c2fc540029a6e6dd075d339f588d09b1b5768bfcbdedc2dcae574e7415aea8562072c1c5273f0b29ebb1bc1d5728605a3b8aae7dad53019e1fb1ac413d163c3f0e9595605780ff69abd17703b4b6468c3dc131af7efadf03f50e719545a94b59f87ee04c725f6bba34e5a25921c2b74c765

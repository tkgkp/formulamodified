:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_mail.fml
:: Utworzony: 19.04.2022
:: Autor: WHAN
:: Systemy:
::======================================================================================================================
:: Zawartość: Skrypty sprawdzające dostarczenie maili
::======================================================================================================================
::    UWAGA test wprowadza dane na podstawie pliku $mail.csv
::
\portal_mail
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: główna formuła testu wykonywana dla działania na końcówce
::   WE: _a - [STRING] ID testu z pliku csv
::       _b - [STRING] ścieżka pliku log
::       _c - [STRING] 'Portal HR'/'Businesslink', jakiej aplikacji dotyczy test, domyślnie 'PortalHR'
::      [_d]- [STRING] tablica z polami SUBJECT,BODY jeśli dane nie pobierać z pliku *.csv
::   WY:
:: ~OST: INFCOPY,INFEXISTS,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 | type_of(_a)<>type_of('') | _a=''
|| _file:=fopen('$test_mail.log','ua',1);
      fwrite(_file,'\nERROR: Nie odnaleziono danych w pliku $mail.csv. ID: '+_dane_mail.ID);
      fclose(_file);
      return(0)
|| _case:=_a
?};
_sep_cli:=exec('sep','#file',0);
{? var_press('__test_path')=type_of('')
|| _tmp_dir:=__test_path
|| _tmp_dir:=3+tmp_dir()+'auto'
?};
{? _tmp_dir+1='/' | _tmp_dir+1='\\'
|| _tmp_dir:=_tmp_dir-1
?};
_tmp_dir:=_tmp_dir+_sep_cli+'test_portal';
{? var_press('_b')<=0 | type_of(_b)<>type_of('')
|| _logpath:='./log/mail_portal.log'
|| _logpath:=_b
?};
{? var_press('_c')<=0 | type_of(_c)<>type_of('')
|| _type:='--mail'
|| _type:={? _c='Businesslink' || '--BL-check-mail' || '--mail' ?}
?};
_jar:='PortalTest.jar';
exec('create_catalog','%transfer','@'+gsub(_tmp_dir,'\\','/')+'/credentials');
exec('create_catalog','%transfer','@'+_tmp_dir+'/credentials/tokens_bl');
exec('create_catalog','%transfer','@'+_tmp_dir+'/credentials/tokens_hr');
_copy_file:="{? ~fexists('@'+_a+'/'+_b)
             || fcopy(pth_dir('$.fml')-3+_b,'@'+_a+'/'+_b,0,0,1)
             ?}";
_copy_file(_tmp_dir,'csv/$mail.csv');
_copy_file(_tmp_dir,'credentials/tokens_bl/StoredCredential');
_copy_file(_tmp_dir,'credentials/tokens_hr/StoredCredential');
_copy_file(_tmp_dir,'credentials/credentials_bl.json');
_copy_file(_tmp_dir,'credentials/credentials_hr.json');

_dane_mail:=exec('importuj_dane_mail','$test_mail','@'+_tmp_dir+'/csv/$mail.csv');
{? ~_dane_mail.first()
|| 'Brak zaimportowanych danych';
   return(0)
?};
{? ~_dane_mail.find_tab(,'ID',,'=',_case)
|| _file:=fopen('$test_mail.log','ua',1);
   fwrite(_file,'\nERROR: Nie odnaleziono danych w pliku $mail.csv. ID: '+_dane_mail.ID);
   fclose(_file);
   return(0)
?};
{? _dane_mail.DURATION='' || _dane_mail.DURATION:=$(30-time()~2%*30+10) ?};
{? _dane_mail.SINCE='' || _dane_mail.SINCE:=$(30-time()~2%*30+15) ?};
{? var_press('_d')=exec('type_of_array','#var') & var_press('SUBJECT',_d)=type_of('') || _dane_mail.SUBJECT:=_d.SUBJECT ?};
{? var_press('_d')=exec('type_of_array','#var') & var_press('BODY',_d)=type_of('') || _dane_mail.BODY:=_d.BODY ?};

MBJAR.JavaExec(_tmp_dir,0,'-jar','-Dfile.encoding=UTF-8',_jar,_type,
                                 _dane_mail.DURATION,
                                 _dane_mail.SINCE,
                                 _logpath,
                                 _dane_mail.FROM,
                                 _dane_mail.TO,
                                 _dane_mail.SUBJECT,
                                 _dane_mail.BODY);
~~


\importuj_dane_mail
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Importuje z csv dane maila
::   WE: _a - nazwa pliku
::   WY: _tab_tmp
::----------------------------------------------------------------------------------------------------------------------
_dane_m:=tab_tmp(1,
'ID','STRING[3]','ID',
'DURATION','STRING[50]','Czas sprawdzania maila [min.]',
'SINCE','STRING[50]','Od jak dawna sprawdzać maile [min.]',
'FROM','STRING[50]','Nadawca mail',
'TO','STRING[50]','Odbiorca mail',
'SUBJECT','STRING[255]','Tytuł malia',
'BODY','STRING[255]','Treść maila (fragment lub cała)');

_plik:=_a;
{? _plik*'/' | _plik*'\\'
|| _pth:=',nopth'
|| _pth:=''
?};
_dane_m.import(_plik,,1,,'UTF-8,header'+_pth,,
'ID',,1,,
'DURATION',,2,,
'SINCE',,3,,
'FROM',,4,,
'TO',,5,,
'SUBJECT',,6,,
'BODY',,7,);
_dane_m

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:06 105bb7dc0924dc06580a750b05561a631fbe1e2a55761aeea6fe64cdc9a1fe95414debc4b5eed34df3ad3f4958c8f7d3cf791162dd3c8ad76073597cc57b58c13351312d6b8051ef5c261fd62eb18cb3c745f8c075601f7f8566232d398c803db045f3f477bd7b98f4ce8960821e3e43c2e43367b854ad9e7d79864088598a0d

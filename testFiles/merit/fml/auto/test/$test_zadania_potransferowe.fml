:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: Nowy1.fml
:: Utworzony: 05.02.2020
:: Autor: GZ
:: Systemy:
::======================================================================================================================
:: Zawartość: Weryfikacja zadań potransferowych
::======================================================================================================================


\czy_zadania_bez_bledu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [20.14]
:: OPIS: Sprawdza, czy któreś zadanie potransferowe nie zakończyło się błędem
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::@GIVEN
_test_driver:=_a;
_test_driver.logger.filename:='$test_zadania_potransferowe.log';
_log_file:=fopen('$test_zadania_potransferowe.log','uw',1);
{? _log_file
|| fwrite(_log_file,'INFO: Rozpoczęcie testu: test_form_chk ' + form(date()) + ' ' +form(time())+'\n')
|| return(0)
?};

::@WHEN
USERS_Z.cntx_psh();
USERS_Z.index('USERS_Z');
USERS_Z.prefix('Lacida');
{? USERS_Z.first()
|| _test_driver.execute($('exec(\'exec_czy_zadania_bez_bledu\',\'$test_zadania_potransferowe\',\''
                         +USERS_Z.LOGIN+'\',\''+USERS_Z.HASLO+'\')'))
?};
USERS_Z.cntx_pop();
fwrite(_log_file,'INFO: Zakończenie testu: test_form_chk ' + form(date()) + ' ' +form(time())+'\n');
fclose(_log_file);
return(1)


\exec_czy_zadania_bez_bledu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [20.14]
:: OPIS: Sprawdza, czy któreś zadanie potransferowe nie zakończyło się błędem
::   WE: _a - login do Lacidy
::       _b - hasło do Lacidy
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UPG_REAL.cntx_psh();
UPG_REAL.index('TASKERR');
UPG_REAL.prefix(1);
{? UPG_REAL.first()
|| _args:=exec('zglos_defekt_args','$lib_base');
   _args.LOGIN:=_a;
   _args.PASS:=_b;
   _args.RODZAJ:='Błąd Działania';
   _args.APPL:='ZWS';
:: wersji nie wypeniam - chcę domyślna
   _args.ZGLOSIL:='autotesty';

:: z treści wywołania wydłubuje plik i formułę i przekazuje do szukanai ostatniego modyfikującego
   _wyw_tab:=spli_str(UPG_REAL.UPG_TASK().FORM,',');
   _plik:=1-_wyw_tab[2]-2+'.fml';
   _formula:=6-_wyw_tab[1]-1;

   _blame:=exec('ostatni_modyfikujacy_fml','$lib_base',_formula,_plik);
   {? _blame<>''
   || _args.WYKON:=_blame
   || _args.WYKON:='grzabkie'
   ?};
   _args.WERYFIK:='grzabkie';
   _args.NAZ:='ERROR UPGRADE: Zadanie '+ UPG_REAL.UPG_TASK().SYMBOL + ' z pliku '+ _plik +' zatrzymuje kolejne zadania potransferowe.';
   _txt:='[ŚCIEŻKA]\n'
        +'Administracja->Zadania potransferowe'
        +'\n[PARAMETRY]\n\n[KROKI]\n\n[REZULTAT AKTUALNY]\n'
        +'Podczas testów automatycznych nie powiodły się zadania potransferowe.\n'
        +'Zadanie: '+ UPG_REAL.UPG_TASK().SYMBOL + ' z pliku '+ _plik + ' zatrzymało kolejne zadania potransferowe.\n'
        +'[REZULTAT OCZEKIWANY]\nUsunięcie błędu składniowego przed kolejną sesją testów.';
   _args.MEMO:=_txt;
   exec('zglos_defekt','$lib_base',_args)
?};
UPG_REAL.cntx_pop()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:06 93b2bb7317c3ada58ebaaa7bee0a13cf20bb574a2263d6f2e920dbca66b64feea07771bdcb699de2fd2cd3be6bae6adb016f767f03b2020de7e5b540da07bdcc2e13fcc3e075e52dd14ea61ec30b6978cd12f42af2b7e4b062220696cf11f56392523ceff4020233946ac9aeb2106f3b116a7efdd079ce33972987ad57794c54

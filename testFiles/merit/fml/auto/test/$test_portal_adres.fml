:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_portal_adres.fml
:: Utworzony: 10.02.2022
:: Autor: WHAN
:: Systemy:
::======================================================================================================================
:: Zawartość: Skrypty testujące obsługę wniosków
::======================================================================================================================
::    UWAGA test wprowadza dane na podstawie pliku $change_address.csv
::    pole AKCJE oznacza jakie akcje i w jakiej kolejności wykonuje test:
::    SP - Wprowadzenie wniosku (Portal)
::    AM - Akceptacja wniosku (Merit)
::    RM - Odrzucenie wniosku (Merit)
::    CP - Sprawdzenie statusu po akceptacji/odrzuceniu (Portal)
::    DM - Usuwanie zmian danych po teście (Merit)
::
\portal_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [22.26]
:: OPIS: główna formuła testu wykonywana dla działania na końcówce
::   WE: [_a] - [STRING] ID testu/testów z pliku csv
::       [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika
::   WY:
:: ~OST: INFCOPY,INFEXISTS,INFMKDIR,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 | type_of(_a)<>type_of('') | _a=''
|| _cases:=''
|| _cases:=_a
?};
{? var_press('_b')<=0 | type_of(_b)<>type_of(1) | _b=0
|| _mode:=0
|| _mode:=1
?};
_sep_cli:=exec('sep','#file',0);
: definiowanie ścieżki lokalnej
{? var_press('__test_path')=type_of('')
|| _tmp_dir:=__test_path
|| _tmp_dir:=3+tmp_dir()+'auto'
?};
{? _mode=1
|| undefine();
   define('PATH','','','Ścieżka',100,100);
   DEFINE.PATH:=_tmp_dir;
   def_btn('text=%1,icon=xwin16.png:13'['OK'@],'key:F2');
   def_btn('text=%1,icon=xwin16.png:14'['Anuluj'@],'key:Esc');
   {? def_edit(,'Podaj ścieżkę lokalnego folderu środowiska testowego:'@)=0
   || undefine();
      return(~~)
   ?};
   __test_path:=_tmp_dir:=DEFINE.PATH;
   undefine()
?};
{? _tmp_dir+1='/' | _tmp_dir+1='\\'
|| _tmp_dir:=_tmp_dir-1
?};
{? _tmp_dir=3+tmp_dir()+'auto' & fexists('@'+(3+tmp_dir())+'auto',0)=0
|| fmkdir('@'+(3+tmp_dir()),'auto')
?};
{? fexists('@'+_tmp_dir)=0
|| return(0)
?};
:: sprawdzanie potrzebnych plików i ewentualne kopiowanie z serwera na końcówkę
{? ~fexists('@'+_tmp_dir+'/test_portal')
|| fmkdir('@'+_tmp_dir,'test_portal')
?};
_tmp_dir:=_tmp_dir+_sep_cli+'test_portal';
_pth_jar:=pth_dir('$.fml')-3+'jar'+'/';
_pth_driver:=pth_dir('$.fml')-3+'driver'+'/';
_jar:='PortalTest.jar';
{? ~fexists('@'+_tmp_dir+'/'+_jar)
|| {? ~fexists(_pth_jar-1)
   || fmkdir(_pth_jar-4,'jar')
   ?};
::   exec('svn_export','%transfer',_svn+'merit/fml/auto/jar/PortalTest.jar',_pth_jar+_jar,_pocztam);
   fcopy(_pth_jar+_jar,'@'+_tmp_dir+'/'+_jar,0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/'+'csv')
|| fmkdir('@'+_tmp_dir,'csv')
?};
{? ~fexists('@'+_tmp_dir+'/csv/$change_address.csv')
||
::   exec('svn_export','%transfer',_svn+'merit/fml/auto/csv/$change_address.csv',pth_dir('$.csv')+'/$change_address.csv',_pocztam);
   fcopy(pth_dir('$.csv')+'/$change_address.csv','@'+_tmp_dir+'/csv/$change_address.csv',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/'+'driver')
|| fmkdir('@'+_tmp_dir,'driver')
?};
{? ~fexists('@'+_tmp_dir+'/driver/'+'chromedriver.exe')
||
::   exec('svn_export','%transfer',_svn+'merit/fml/auto/driver/chromedriver.exe','@'+_tmp_dir+'/driver/'+'chromedriver.exe',_pocztam)
   fcopy(_pth_driver+'chromedriver.exe','@'+_tmp_dir+'/driver/'+'chromedriver.exe',0,0,1)
?};
{? ~fexists('@'+_tmp_dir+'/log')
|| fmkdir('@'+_tmp_dir,'log')
?};
_dane_wniosek:=exec('importuj_dane_wniosek','$test_portal_adres','@'+_tmp_dir+'/csv/$change_address.csv');
{? ~_dane_wniosek.first()
|| 'Brak zaimportowanych danych';
   return(0)
?};
::_robot:=(app_info('app_ident')='robot' | app_info('app_ident')='robot_manager');
_robot:=app_info('app_par1')='START_TESTY_PORTAL';
_test_driver:=exec('init','$lib_base','$manager.log');
{? ~_robot
||
   _pulpit:=exec('get_desctop_id','$lib_base',_test_driver);
   {? _pulpit<>''
   || _test_driver.app_close(_pulpit)
   ?}
?};
{? _mode=0 & _robot
|| exec('test_adres_clear','$test_portal_adres',_dane_wniosek,2)
?};
exec('MBJAR','#object');

{? _cases<>''
|| _cases_spli:=spli_str(_cases,',');
   _case_no:=1
?};
{! |?
   {? _cases<>''
   || {? ~_dane_wniosek.find_tab(,'ID',,'=',_cases_spli[_case_no])
      || _file:=fopen('$test_portal_adres.log','ua',1);
         fwrite(_file,'\nERROR: Nie odnaleziono wniosku w pliku *.csv. ID: '+_dane_wniosek.ID);
         fclose(_file);
         return(0)
      ?}
   ?};
   {? ~fexists('@'+_tmp_dir+'/log/'+_dane_wniosek.ID)
   || fmkdir('@'+_tmp_dir+'/log',_dane_wniosek.ID)
   ?};
   {? _dane_wniosek.AKCJE<>''
   || _akcje:=spli_str(_dane_wniosek.AKCJE,',');
      {! _i:=1..obj_len(_akcje)
      |!
::    pole AKCJE oznacza jakie akcje i w jakiej kolejności wykonuje test:
::    SP - Wprowadzenie wniosku (Portal)
::    AM - Akceptacja wniosku (Merit)
::    RM - Odrzucenie wniosku (Merit)
::    CP - Sprawdzenie statusu oraz wprowadzonych danych po akceptacji (Portal)
::    RP - Sprawdzenie statusu po odrzuceniu, poprawienie wprowadzonych danych, ponowne wysłanie wniosku (Portal)
::    DM - Usuwanie zmian danych po teście (Merit)
         {? _akcje[_i]='SP'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--address-start',_dane_wniosek.ID)
         |? _akcje[_i]='AM'
         || ferase(pth_dir()+'/system/admin/$accept_portal_adres.log');
            ferase(pth_dir('$accept_portal_adres.log')+'/$accept_portal_adres.log');
            exec('accept_portal_adres2','$test_portal_adres',_dane_wniosek,_mode,'1');
            fcopy('$accept_portal_adres.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.accept_portal_adres.log',1,0,1)
         |? _akcje[_i]='RM'
         || ferase(pth_dir()+'/system/admin/$reject_portal_adres.log');
            ferase(pth_dir('$reject_portal_adres.log')+'/$reject_portal_adres.log');
            exec('reject_portal_adres','$test_portal_adres',_dane_wniosek,_mode,'1');
            fcopy('$reject_portal_adres.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.reject_portal_adres.log',1,0,1)
         |? _akcje[_i]='CP'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--address-check',_dane_wniosek.ID)
         |? _akcje[_i]='RP'
         || MBJAR.JavaExec(_tmp_dir,1,'-jar',_jar,'--address-correct',_dane_wniosek.ID)
         |? _akcje[_i]='DM'
         || ferase(pth_dir()+'/system/admin/$clear_portal_adres.log');
            ferase(pth_dir('$clear_portal_adres.log')+'/$clear_portal_adres.log');
            exec('test_adres_clear','$test_portal_adres',_dane_wniosek,_mode);
            fcopy('$clear_portal_adres.log','@'+_tmp_dir+'/log/'+_dane_wniosek.ID+'/'+$_i+'.clear_portal_adres.log',1,0,1)
         ?}
      !};
      &_akcje
   ?};
   {? _cases<>''
   || _case_no+=1;
      {? obj_len(_cases_spli)>=_case_no
      || _loop:=1
      || _loop:=0
      ?}
   || _loop:=_dane_wniosek.next()
   ?};
   _loop
!};
{? ~_robot
|| _test_driver.app_open('admin', 'admin123', app_info('cluster_group'), app_info('app_ident'), '', '');
::   exec('set_active_win','$lib_base',_test_driver,_cur_win);
   _exec:='FUN.info(\'Wyniki testu znajdują się w katalogu '+_tmp_dir+_sep_cli+'log\')';
   _test_driver.execute($(gsub(_exec,'\\','\\\\')))
?};
~~


\accept_portal_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Główna metoda akceptująca wnioskek zmiany adresu
::   WE: _a - dane z pliku csv
::       [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika
::       [_c] - wskazanie na aplikację: Pulpit
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=exec('init','$lib_base','$accept_portal_adres.log');
{? var_press('_b')<0 | _b=0
|| _mode:=0
|| _mode:=1
?};
{? var_press('_c')<0 | _c=''
|| _c:='1'
?};
_dane_wniosek:=_a;
:: @WHEN
_pulpit:=_test_driver.app_open('ubajer', 'ubajer', 'portaltesty', 'merit001', '', '');
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'reload');
_test_driver.app_wait_for_change();
_test_driver.control('!desktop', 'todo');
_app_todo:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@BI_TODO.WER', 'title', 'Zadania', 'type', 's');

_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
::{! _i:=1..(_dane_wniosek.size())
::|?
::      akceptacja
   _file:=fopen('$accept_portal_adres.log','ua',1);
   fwrite(_file,'\n\nINFO: Akceptacja wniosku ID: '+_dane_wniosek.ID);
   fclose(_file);
   _test_driver.send('', '^{F5}', '');

   _test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=1)');
   _test_driver.filter('@BI_TODO.DT', $date(),'@BI_TODO.BI_STAT.NAME', 'Wolne',
                       '@BI_TODO.BI_PREL.DESC', 'Zaakceptuj zmiana danych adresowych');
   _test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=1)');
   _test_driver.send('', '{PGUP}', '');
   _test_driver.send('', '{PGUP}', '');
::   _test_driver.click('btn:TODO_RUN');
   _test_driver.menu('Rozpocznij');
   _test_driver.assert('', 'wnd:@tmp.obe_faw_red', 'title', 'Wniosek', 'type', 'e');
::   _test_driver.click('btn:@Akceptuj');
   _test_driver.menu('Akceptuj');
   _test_driver.assert('', 'wnd:@EDOKOS.obeakckom', 'title', 'Akceptacja', 'type', 'e');
   _test_driver.send(_dane_wniosek.AKCEPTUJ, '', 'fld:@EDOKOS.UW_DL');
   _test_driver.click('btn:@Zakończ');
::   _dane_wniosek.ID<>_caseID & _dane_wniosek.next()
::!};
_test_driver.app_close(_app_todo);

_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
delay(30);
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
delay(60);
_test_driver.app_close(_pulpit);
~~


\accept_portal_adres2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Główna metoda akceptująca wnioskek zmiany adresu
::   WE: _a - dane z pliku csv
::       [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika
::       [_c] - wskazanie na aplikację: Pulpit
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=exec('init','$lib_base','$accept_portal_adres.log');
{? var_press('_b')<0 | _b=0
|| _mode:=0
|| _mode:=1
?};
{? var_press('_c')<0 | _c=''
|| _c:='1'
?};
_dane_wniosek:=_a;
:: @WHEN
_pulpit:=_test_driver.app_open('ubajer', 'ubajer', app_info('cluster_group'), 'merit001', '', '');
:: otwarcie obszaru wniosków w obiegu
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'open', 'Obieg wniosków\\Obszary\\Wnioski w obiegu', 7);
_app_wnioski:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@wnioselr', 'title', '', 'type', 's');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
_test_driver.click('gtab:@Akceptacja');
::      akceptacja
   _file:=fopen('$accept_portal_adres.log','ua',1);
   fwrite(_file,'\n\nINFO: Akcepracja wniosku ID: '+_dane_wniosek.ID);
   fclose(_file);
   _test_driver.filter('@EDOKUM.DATAW', $date(),
                       '@EDOKUM.USERS.DANE', _dane_wniosek.IMIE +' '+ _dane_wniosek.NAZWISKO,
                       '@EDOKUM.DOSTAWCA.NAZWISKO', _dane_wniosek.NAZWISKO,
                       '@EDOKUM.TR', 'Zmiana danych adresowych');
   _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))");
   _test_driver.click('fld:@EDOKUM.DOSTAWCA.NAZWISKO(row=2)');
   _test_driver.click('fld:@EDOKUM.DOSTAWCA.NAZWISKO(row=1)');
   delay(1);
   _test_driver.menu('Akceptuj');
   _test_driver.assert('', 'wnd:@EDOKOS.obeakckom', 'title', 'Akceptacja', 'type', 'e');
   _test_driver.send(_dane_wniosek.AKCEPTUJ, '', 'fld:@EDOKOS.UW_DL');
   _test_driver.click('btn:@Zakończ');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
::_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
delay(30);
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
delay(60);
_test_driver.app_close(_app_wnioski);
_test_driver.app_close(_pulpit);
~~


\reject_portal_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Główna metoda odrzucająca wnioskek zmiany adresu
::   WE: _a - dane z pliku csv
::       [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika
::       _c  - wskazanie na aplikację: Pulpit
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=exec('init','$lib_base','$reject_portal_adres.log');
{? var_press('_b')<0 | _b=0
|| _mode:=0
|| _mode:=1
?};
{? var_press('_c')<0 | _c=''
|| _c:='1'
?};
_dane_wniosek:=_a;
:: @WHEN
_pulpit:=_test_driver.app_open('ubajer', 'ubajer', app_info('cluster_group'), 'merit001', '', '');
:: otwarcie obszaru wniosków w obiegu
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'open', 'Obieg wniosków\\Obszary\\Wnioski w obiegu', 7);
_app_wnioski:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@wnioselr', 'title', '', 'type', 's');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))");
_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
_test_driver.click('gtab:@Akceptacja');
::      odrzucenie
   _file:=fopen('$reject_portal_adres.log','ua',1);
   fwrite(_file,'\n\nINFO: Odrzucenie wniosku ID: '+_dane_wniosek.ID);
   fclose(_file);
   _test_driver.filter('@EDOKUM.DATAW', $date(),
                       '@EDOKUM.USERS.DANE', _dane_wniosek.IMIE +' '+ _dane_wniosek.NAZWISKO,
                       '@EDOKUM.DOSTAWCA.NAZWISKO', _dane_wniosek.NAZWISKO,
                       '@EDOKUM.TR', 'Zmiana danych adresowych');
   _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))");
   _test_driver.click('fld:@EDOKUM.DOSTAWCA.NAZWISKO(row=2)');
   _test_driver.click('fld:@EDOKUM.DOSTAWCA.NAZWISKO(row=1)');
   _test_driver.menu('Odrzuć');
::   _test_driver.click('btn:@Odrzuć');
   _test_driver.assert('', 'wnd:@EDOKOS.obeakckom', 'title', 'Odrzucenie', 'type', 'e');
   _test_driver.send(_dane_wniosek.ODRZUC, '', 'fld:@EDOKOS.UW_DL');
   _test_driver.click('btn:@Odrzuć');
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
::_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
delay(30);
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
delay(60);
_test_driver.app_close(_app_wnioski);
_test_driver.app_close(_pulpit);
~~


\test_adres_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Czyści dane z testu
::   WE: _a - dane z pliku csv
::      [_b] - [1/0] 0 - domyślnie, tryb auto; 1 - trub użytkownika; 2 - czyszczenie danych przed testami
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=exec('init','$lib_base','$clear_portal_adres.log');
{? var_press('_b')<0 | _b=0
|| _mode:=0
|| _mode:=_b
?};
_dane_wniosek:=_a;
:: @WHEN
_pulpit:=_test_driver.app_open('admin', 'admin123', app_info('cluster_group'), 'merit001', '', '');
:: usunięcie wprowadzonego adresu w danych osobowych
_test_driver.control('!desktop', 'open', 'Wspólne\\Obszary\\Dane osobowe', 7);
_app_dane_oso:=_test_driver.app_wait_for_change();
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
_test_driver.assert('', 'wnd:zws_dos_osoba', 'title', 'Dane osobowe', 'type', 's');
_file:=fopen('$clear_portal_adres.log','ua',1);
fwrite(_file,'\n\nINFO: Usunięcie danych wniosku ID: '+_dane_wniosek.ID);
fclose(_file);
_test_driver.click('fld:@OSOBA.NAZWISKO(row=1)');

_test_driver.filter('@OSOBA.NAZWISKO', _dane_wniosek.NAZWISKO, '@OSOBA.PIERWSZE', _dane_wniosek.IMIE);
_test_driver.execute("params_exec('osoba_bd','!zws_dos_prdo',0)");
_test_driver.click('wnd:zws_dos_osoba');
_test_driver.click('wnd:adr');
_test_driver.click('fld:@EDIT_VAR.RODZAJ(row=1)');

{? _mode<>2
|| _test_driver.filter('@OS_ADRES.OD', gsub(_dane_wniosek.ZMIANAOD,'-','/'),
                       '@OS_ADRES.DO', gsub(_dane_wniosek.ZMIANADO,'-','/'),
                       '@OS_ADRES.MIASTO', _dane_wniosek.MIEJSC,
                       '@OS_ADRES.ULICA', _dane_wniosek.ULICA,
                       '@OS_ADRES.DOM', _dane_wniosek.NRDOMU,
                       '@OS_ADRES.LOKAL', _dane_wniosek.NRLOK);
   _test_driver.execute("params_exec('os_adres_bd','osoba',0)");
   _test_driver.click('wnd:zws_dos_osoba');
   _test_driver.click('wnd:adr');

   _test_driver.click('fld:@OS_ADRES.ULICA(row=2)');
   _test_driver.click('fld:@OS_ADRES.ULICA(row=1)');
   _test_driver.click('btn:USUŃ');
   _test_driver.click('dbtn:Tak')
|| _test_driver.filter('@OS_ADRES.OD', '20');

   _test_driver.execute("params_exec('os_adres_bd','osoba',0)");
   _size:=_test_driver.execute("{? OS_ADRES.f_active()
                                || OS_ADRES.f_size()
                                || OS_ADRES.size()
                                ?}");
   {? type_of(_size)=type_of(1)
   || {! _i:=1.._size |!
         _test_driver.click('wnd:zws_dos_osoba');
         _test_driver.click('wnd:adr');

         _test_driver.click('fld:@OS_ADRES.ULICA(row=2)');
         _test_driver.click('fld:@OS_ADRES.ULICA(row=1)');
         _test_driver.click('btn:USUŃ');
         _test_driver.click('dbtn:Tak')
      !}
   ?}
?};
_test_driver.app_close(_app_dane_oso);
_test_driver.app_activate(_pulpit);
_test_driver.control('!desktop', 'open', 'Obieg wniosków\\Obszary\\Wnioski w obiegu - techniczne', 7);
_app_wnioski_tech:=_test_driver.app_wait_for_change();
_test_driver.execute("EDOKUM.prefix()");
{! _j:=1..2
|! _test_driver.click('wnd:@#edokatrt');
   _test_driver.click('wnd:@edokumtmp01');

   _test_driver.click('fld:@EDOKUM.DATAW(row=1)');
   _size:=_test_driver.execute("{? EDOKUM.f_active()
                         || EDOKUM.f_size()
                         || EDOKUM.size()
                         ?}");
   {? _size>0
   ||
      {? _mode<>2
      ||
         _test_driver.filter('@EDOKUM.DATAW', $date(),
                             '@EDOKUM.USERS.KOD', _dane_wniosek.LOGIN_M,
                             '@EDOKUM.TYP.NAZWA', 'Zmiana danych adresowych');
                             _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))")
      || _test_driver.filter('@EDOKUM.TYP.NAZWA', 'Zmiana danych adresowych');
         _test_driver.execute("grp_disp(cur_tab(1,1),cur_win(1,1))")
      ?};
      _test_driver.click('wnd:@#edokatrt');
      _test_driver.click('wnd:@edokumtmp01');

      _test_driver.click('fld:@EDOKUM.DATAW(row=1)');
      _size:=_test_driver.execute("{? EDOKUM.f_active()
                            || EDOKUM.f_size()
                            || EDOKUM.size()
                            ?}");
      {? type_of(_size)=type_of(1)
      || {! _i:=1.._size |!
            _test_driver.click('wnd:@edokumtmp01');

            _test_driver.click('fld:@EDOKUM.TYP.NAZWA(row=1)');

            _test_driver.menu('Usuń');
            _test_driver.click('dbtn:Tak')
         !}
      ?}
   ?};
   _test_driver.execute("EDOKUM.use('skid_v'+($(date()~1)+2))")
!};
delay(5);
_test_driver.execute("KOMM.init(255,,'Odbieranie danych'@)");
_test_driver.execute("exec('receive','$test_portal_adres')");
_test_driver.execute("exec('process','$test_portal_adres')");
_test_driver.execute("exec('send','$test_portal_adres')");
_test_driver.app_close(_app_wnioski_tech);
_test_driver.app_close(_pulpit);
~~


\receive
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: testuj odbieranie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
exec('process_receive','sync_mwa','PORTAL_WNIOSKI',,,,,0);
EDOKUM.cntx_pop()


\process
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: testuj przetwarzanie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
exec('process','portal_wnioski',1,0);
EDOKUM.cntx_pop()


\send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: testuj wysyłanie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
exec('process_send','sync_mwa','PORTAL_WNIOSKI',,,,,,,0);
EDOKUM.cntx_pop()


\importuj_dane_wniosek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Importuje z csv dane wniosku
::   WE: _a - nazwa pliku
::   WY: _tab_tmp
::----------------------------------------------------------------------------------------------------------------------
_dane_w:=tab_tmp(1,
'ID','STRING[3]','Adres www portalu',
'AKCJE','STRING[30]','Kody akcji do wykonania',
'ADRES','STRING[255]','Adres www portalu',
'LOGIN_P','STRING[100]','Użytkownik portal',
'HASLO_P','STRING[100]','Hasło portal',
'IMIE','STRING[40]','Imię',
'NAZWISKO','STRING[40]','Nazwisko',
'LOGIN_M','STRING[20]','Użytkownik merit',
'ZMIANAOD','STRING[10]','Zmiana od',
'ZMIANADO','STRING[10]','Zmiana do',
'TYPADRES','STRING[100]','Typ adresu',
'KRAJ','STRING[100]','Kraj',
'KODP','STRING[6]','Kod pocztowy',
'WOJEW','STRING[100]','Województwo',
'POWIAT','STRING[100]','Powiat',
'GMINADZ','STRING[100]','Gmina/Dizelnica',
'MIEJSC','STRING[100]','Miejscowość',
'ULICA','STRING[100]','Ulica',
'NRDOMU','STRING[100]','Nr domu',
'NRLOK','STRING[100]','Nr lokalu',
'EMAIL','STRING[100]','E-mail',
'NRTEL','STRING[100]','Nr telefonu',
'AKCEPTUJ','STRING[100]','Nr telefonu',
'ODRZUC','STRING[100]','Nr telefonu');

_plik:=_a;
{? _plik*'/' | _plik*'\\'
|| _pth:=',nopth'
|| _pth:=''
?};
_dane_w.import(_plik,,1,,'UTF-8,header'+_pth,,
'ID',,1,,
'AKCJE',,2,,
'ADRES',,3,,
'LOGIN_P',,4,,
'HASLO_P',,5,,
'IMIE',,6,,
'NAZWISKO',,7,,
'LOGIN_M',,8,,
'ZMIANAOD',,9,,
'ZMIANADO',,10,,
'TYPADRES',,11,,
'KRAJ',,12,,
'KODP',,13,,
'WOJEW',,14,,
'POWIAT',,15,,
'GMINADZ',,16,,
'MIEJSC',,17,,
'ULICA',,18,,
'NRDOMU',,19,,
'NRLOK',,20,,
'EMAIL',,21,,
'NRTEL',,22,,
'AKCEPTUJ',,23,,
'ODRZUC',,24,);

{? _dane_w.first()
|| {! |?
:: jeśli data od/do pusta
      {? _dane_w.ZMIANAOD=''
      || _dane_w.ZMIANAOD:=gsub($date(),'/','-');
         _dane_w.put()
      ?};
      {? _dane_w.ZMIANAOD*'.'
      || _dane_w.ZMIANAOD:=gsub(_dane_w.ZMIANAOD,'.','-');
         _dane_w.put()
      ?};
      {? _dane_w.ZMIANADO=''
      || _dane_w.ZMIANADO:=gsub($(date()+1),'/','-');
         _dane_w.put()
      ?};
      {? _dane_w.ZMIANADO*'.'
      || _dane_w.ZMIANADO:=gsub(_dane_w.ZMIANADO,'.','-');
         _dane_w.put()
      ?};
      _dane_w.next()
   !}
?};
_dane_w


:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:06 39e3bf2fecb62a77f195797fe91bbe6e0ff006ba269802741f03929e083eb7650c40728a2bd37d9806b464a0750502b300da1b10181a951857dd9f5fc89c3cc12e814ad5ca33d009472d4aa8bb2b8ce217135bd1351253e4c6b952ac2389d274d23863670753354a6c408d8686f31e11bde10996ddbf517314bdd281f4c5a547

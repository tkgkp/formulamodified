:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_dekret_amortyzacji.fml
:: Utworzony: 22.02.2019
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Testy automatyczne dla obszaru FKS i FST - dekretowanie amortyzacji
:: AREA: [FKS_DKS]
::======================================================================================================================


\amor_dekret
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Test dekretowania amortyzacji środków trwałych
::   WE: _a - obiekt testujący
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;

:: jeżeli poprzedni test na tej samej kopii obiektu testującego zakończył się błędem to nie uruchamia się
:: test bo wynik może być nieoczekiwany - poprzedni test mógł zostwić niewyczyszczone środowisko, otwarte zakładki itp.
{? _test_driver.error_no<>0 || return(0) ?};

:: w razie potrzeby spowolnienia wykonywania testu można włączyć poniższą funkcję
::_test_driver.set_delay(1,0);
_test_driver.logger.log_actions:=1;
_file:=_test_driver.logger.filename;
_test_driver.logger.filename:='$test_dekret_amortyzacji.log';
_test_driver.log_init_info();

_test_driver.exec_after_error:=0;
_test_driver.set_lib_mock_enabled('', '', 0);
_test_driver.set_lib_mock_enabled('mbutil.jar', '', 1);
_test_driver.set_lib_mock_enabled('filedlg.jar', '', 1);

_test_driver.set_ctrl_mock_enabled('', '', 0);
_test_driver.set_ctrl_mock_enabled('!application>ctr:filechooser', '', 1);

::_app:=_test_driver.app_open('admin', 'admin123', 'firma', 'merit001', '', '');
::_app:=_test_driver.app_activate();
::exec('schemat_dziedz_amor','$srodki_trwale',_test_driver);

_app:=_test_driver.app_activate(_b);
_test_driver.control('!desktop', 'open', 'Finanse\\Obszary\\Dokumenty księgowe', 7);
_test_driver.app_wait_for_change();
_app_ksg:=_test_driver.app_activate();

_test_driver.assert('', 'wnd:fks_dok_odd', 'title', 'Dokumenty księgowe', 'type', 's');
_test_driver.click('wnd:@tree_rej');

_test_driver.menu('Szukaj dokładnie');
_test_driver.send('PK - Polecenie księgowania', '{ENTER}', 'fld:@tmp.REJ');
_test_driver.click('gtab:@Dokumenty w rejestrze');


:: ustawienie parametrów pracy - dla danych demo: okres styczeń 2018
exec('fst_par_prac','$srodki_trwale',_test_driver,2017,'sierpień');

_size:=_test_driver.execute("DOK.size");
{? _size >0
||
   _test_driver.click('fld:@DOK.NK(row=1)');
   _test_driver.filter('@DOK.NK', 'Amor. bilan. 08/2017');
   _test_driver.click('fld:@DOK.NK(row=1)');
   {?_test_driver.state('fld:@DOK.NK(row=1)').value='Amor. bilan. 08/2017'
   || _test_driver.click('fld:@DOK.NK(row=1)');
      _test_driver.click('btn:btnDEL');
      _test_driver.click('dbtn:Tak')
   ?};
   _test_driver.filter()
?};
_result:=exec('wyb_rodzaje_ewidencji','$srodki_trwale',_test_driver,'Amortyzacja bilansowa');

{? _result<>0
|| _dbtn:=_test_driver.exist('dbtn:OK');
   {? _dbtn
   || _test_driver.click('dbtn:OK')
   || _btn:=_test_driver.exist('btn:@OK');
      {? _btn
      || _test_driver.logger.info('Dokument już istnieje lub pojawiły się inne błędy.');
         _test_driver.click('btn:@OK')

      || _test_driver.logger.error(0,'Dekretowanie amortyzacji - nieoczekiwane okno lub brak okna.')
      ?}
   ?};
   _test_driver.send('', '{ESC}', '')
?};

{? _result & _test_driver.error_no=0
|| _test_driver.logger.info('Zadekretowano amortyzację bilansową.');
:: zamykanie okna kartoteki środków
   _test_driver.app_close(_app_ksg);
   _test_driver.logger.filename:=_file;
   return(1)
|| _test_driver.logger.error(0,'Nie udało się zadekretować amortyzacji bilansowej.');
   _test_driver.app_close(_app_ksg);
   _test_driver.logger.filename:=_file;
   return(0)
?}

:Sign Version 2.0 jowisz:1048 2021/04/09 15:23:47 13513c537c890d8983a9b8bc0a66708abc18afb602e0cf574fa133d3f11df7324f42a87401b79ccba80ac4cbf621660ba99605e4f9d1aa1f9f5e91ef8f47e8073dd33721e6a290b898d5a3da20fdc5bad3de1c6f5c7ed031cd24bb35ae1e8647cd472407a8577b38be412682021e0fab70398dd83bb638099276a7191a11a6ac

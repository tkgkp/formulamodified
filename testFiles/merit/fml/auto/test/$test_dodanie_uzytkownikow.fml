:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_dodanie_uzytkownikow.fml
:: Utworzony: 23.04.2019
:: Autor: ARTSLO
:: Systemy:
::======================================================================================================================
:: Zawartość: Test automatyczny dla obszaru ZWS - import uzytkowników i ról oraz nadanie uprawnień
:: AREA: [ZWS_ADM]
::   WE: _a - [aktualnie nic, względy historyczne kiedyś był przekazywany obiekt testowy]
::   WY: 1/0 - pełne powodzenie testu lub porażka
::======================================================================================================================
\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO 23.04.2019
:: OPIS:
::   WE:
::   WY:
:: ~OST: INFERASE
::----------------------------------------------------------------------------------------------------------------------
::GIVEN

_test_driver:=exec('init','$lib_base','$test_dodanie_uzytkownikow.log');
_test_driver.logger.info('START TESTU: $test_dodanie_uzytkownikow.fml (time: '+$date()+' '+(time()$3)+')');

:: import danych do wprowadzenia z plików .csv
_set:=exec('importBuff','$lib_base','$add_users_ustawienia.csv',exec('bf_imp_xls','$wspolne'));
_upr:=exec('importBuff','$lib_base','$add_users_upr.csv',exec('bf_usr_upr','$uzytkownicy_role_uprawnienia'));
_sch:=exec('importBuff','$lib_base','$add_users_sch.csv',exec('bf_usr_sch','$uzytkownicy_role_uprawnienia'));
_set.first();

:: powołanie potrzebnych zmiennych i ustawienie ich początkowych wartości
_obszar:=obj_new('PULPIT','ADMINISTRACJA','UZYTKOWNICY');
_obszar.PULPIT:=_test_driver.app_activate(_b);
_loop:=1;
_timeout:=_test_driver.timeout;
_test_driver.timeout:=900000;
_wyn_upr:=tab_tmp(1,'KOM','STRING[100]','Tekst niepowodzenia');
_users:=sql('select distinct :_a.USER from :_a UNION select distinct :_b.USER from :_b',_upr,_sch);
_dupr:=sql('select distinct UPR from :_a',_upr);
_upr.index(_upr.ndx_tmp(,1,'USER',,,'UPR',,));
_sch.index(_sch.ndx_tmp(,1,'USER',,));


::WHEN

_plik:=exec('przygotuj_plik_zal','$lib_base',_test_driver,'$add_users_example.xlsx','users.xlsx',_set.DIR);
_plik2:=exec('przygotuj_plik_zal','$lib_base',_test_driver,'stale_systemu.xlsx','stale_systemu.xlsx','@c:\\\\auto\\\\wersja_rrxx\\\\17xx\\\\danewzor\\\\');
_set.DIR:=_plik.DIR;
_obszar.ADMINISTRACJA:=exec('otworz_obszar','$lib_base',_test_driver,'Administracja');
_wynik:=exec('excel_import','$administracja',_test_driver,_set);
_komunikaty:=exec('clean_xls_kom','$wspolne',_wynik.KOM);
{? _komunikaty.size()=0 || _wynik.warunkowe:=0 ?};
_obszar.UZYTKOWNICY:=exec('otworz_obszar','$lib_base',_test_driver,'Użytkownicy, role, uprawnienia');

{? _users.first()
|| {! |?
      _test_driver.click('wnd:@USERS.CALL');
      exec('wybierz_uzytkownika','$uzytkownicy_role_uprawnienia',_test_driver,_users.USER);
      {? _dupr.first()
      || {! |?
            _upr.prefix(_users.USER,_dupr.UPR);
            {? _upr.first()
            || {! |?
                  _wyn:=exec('dodaj_uprawnienie','$uzytkownicy_role_uprawnienia',_test_driver,_upr);
                  {? _wyn<>'' || _wyn_upr.KOM:=_wyn; _wyn_upr.add() ?};
                  _upr.next()
               !}
            ?};
            _dupr.next()
         !}
      ?};
      _sch.prefix(_users.USER);
      {? _sch.first()
      || {! |?
            _wyn:=exec('nadaj_upr_sch','$uzytkownicy_role_uprawnienia',_test_driver,_sch);
            {? _wyn<>'' || _wyn_upr.KOM:=_wyn; _wyn_upr.add() ?};
            _sch.next()
         !}
      ?};
      _users.next()
   !}
?};


::THEN

::usuwam skopiowany pliki zalacznika; nie usuwam utworzonego katalogu bo nie mam funkcji do tego
{? _plik.DIR<>'' & _plik.NAME<>''
|| ferase('@'+_plik.DIR+'\\'+_plik.NAME)
?};
{? _plik2.DIR<>'' & _plik2.NAME<>''
|| ferase('@'+_plik.DIR+'\\'+_plik2.NAME)
?};
:: zamykamy zakładki
_test_driver.app_close(_obszar.UZYTKOWNICY);
_test_driver.app_close(_obszar.ADMINISTRACJA);
:: odpisanie odpowiednich komunikatów o przebiegu testu
{? (_wynik.blokujace=0 | (_wynik.blokujace=1 & _komunikaty.size()=0)) & _wyn_upr.size()=0
|| _test_driver.logger.info('WYNIK TESTU: Test zakończony powodzeniem.')
|| _test_driver.logger.info('WYNIK TESTU: Test zakończony niepowodzeniem.')
?};
{? _wynik.warunkowe=1 & _komunikaty.size()<>0
|| _test_driver.logger.info('WYNIK POSREDNI: Po imporcie były komunikaty nie blokujące walidacji.')
?};
{? _wynik.blokujace=1 & _komunikaty.size()<>0
|| _test_driver.logger.info('WYNIK NEGATYWNY: Po imporcie były komunikaty blokujące walidacje.')
?};
{? _wyn_upr.size()>0 & _wyn_upr.first()
|| {! |? _test_driver.logger.info('WYNIK NEGATYWNY: Nie udało się ustawić uprawnienia - '+_wyn_upr.KOM); _wyn_upr.next() !}
?};
{? _komunikaty.first()
|| {! |?
      _test_driver.logger.info('WYNIK POSREDNI: '+_komunikaty.FILE+';'+_komunikaty.SHEET+';'+_komunikaty.ROW+';'+_komunikaty.MESSAGE);
      _komunikaty.next()
   !}
?};
_test_driver.logger.info('KONIEC TESTU: $test_dodanie_uzytkownikow.fml (time: '+$date()+' '+(time()$3)+')');
_test_driver.timeout:=_timeout;
return(~_wynik.blokujace & _wyn_upr.size()=0)

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:06 ae73eb610518984f83eef5601de1c5f13010dfbc04b57c00cee6d0c4241950f9cbddf1d40f1b9eefcba0aaecfb9b5ba12b290d153135e39a1c1a64fd31be5d306bcdff9d63360ecfd7001708e152d01f89892ed2f4a78ccb2b945bdf4f1d71eaf1cc65b7d7dcb6fff79d591b6edc103318b4c6ad4215586f0135ae67fe427b46

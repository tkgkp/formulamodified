:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $test_dane_wzorc.fml
:: Utworzony: 22.03.2019
:: Autor: ARTSLO
:: Systemy:
::======================================================================================================================
:: Zawartość: Test automatyczny dla obszaru ZWS - import danych wzorcowych
:: AREA: [ZWS_ADM]
::   WE: _a - [aktualnie nic, względy historyczne kiedyś był przekazywany obiekt testowy]
::       _b - wskazanie na zakladke pulpitu
::   WY: 1/0 - pełne powodzenie testu lub porażka
::======================================================================================================================
::GIVEN
_test_driver:=exec('init','$lib_base','$test_dane_wzorc.log');
_test_driver.logger.info('START TESTU: $test_dane_wzorc.fml (time: '+$date()+' '+(time()$3)+')');
_set:=exec('importBuff','$lib_base','$dane_wzorc_ustawienia.csv',exec('bf_imp_xls','$wspolne'));
_set.first();
_obszar:=obj_new('PULPIT','ADMINISTRACJA');
{? _b<>~~
|| _obszar.PULPIT:=_test_driver.app_activate(_b)
|| _obszar.PULPIT:=_test_driver.app_activate('1')
?};
_loop:=1;
_timeout:=_test_driver.timeout;
_test_driver.timeout:=3600000;

::WHEN
_obszar.ADMINISTRACJA:=exec('otworz_obszar','$lib_base',_test_driver,'Administracja');
_wynik:=exec('excel_import','$administracja',_test_driver,_set);
_komunikaty:=exec('clean_xls_kom','$wspolne',_wynik.KOM);
{? _komunikaty.size()=0 || _wynik.warunkowe:=0 ?};

::THEN
_test_driver.app_close(_obszar.ADMINISTRACJA);
_notOK:=_komunikaty.size()>0 | _wynik.proc.size()>0 | ( type_of(_wynik.dial)<>type_of(~~) & _wynik.dial.size()>0);
{? ~_notOK
|| _test_driver.logger.info('WYNIK TESTU: Test zakończony powodzeniem.')
|| _test_driver.logger.info('WYNIK TESTU: Test zakończony niepowodzeniem.')
?};
{? _komunikaty.size()>0 & _komunikaty.first()
|| {! |?
      _test_driver.logger.info('WYNIK POSREDNI: '+_komunikaty.FILE+';'+_komunikaty.SHEET+';'+_komunikaty.ROW+';'+_komunikaty.MESSAGE);
      _komunikaty.next()
   !}
?};
{? _wynik.proc.size()>0 & _wynik.proc.first()
|| {! |?
      _test_driver.logger.info('WYNIK POSREDNI: '+_wynik.proc.MESSAGE);
      _wynik.proc.next()
   !}
?};
{? type_of(_wynik.dial)<>type_of(~~) & _wynik.dial.size()>0 & _wynik.dial.first()
|| {! |?
      _test_driver.logger.info('WYNIK POSREDNI: '+_wynik.dial.MSG);
      _wynik.dial.next()
   !}
?};
_test_driver.logger.info('KONIEC TESTU: $test_dane_wzorc.fml (time: '+$date()+' '+(time()$3)+')');
_test_driver.timeout:=_timeout;
return(_wynik.blokujace)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:12:56 94e8e207bdbc1d7abd5c710b709a18be31bbca7ff361a39fb845c69ff41b8163c9bd5066a8eed05f7a31193e0b11a4ca59e749de5600180e196e726c052c1c798d1e87959e648a919c95b89605aea248b27dfb18e3c3fbfa08142a63b6979c11118f9f2e9386880d198d62d4249499c135b2a0184a2d8cf5dc459c8e7b89cb28

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $srodki_trwale.fml
:: Utworzony: 20.02.2019
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły pomocnicze do testów środków trwałych
::======================================================================================================================


\srodki_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Formuła czyści środki dodane w czasie auto-testów, wywołanie w kontekście aktywnego okna wertowania środków
::   WE: _a - obiekt testujący
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;

_test_driver.filter('@SRST.NST', 'AUTO:');
_result:=_test_driver.execute("SRST.f_first()");
{? _result
||
   _test_driver.click('fld:@SRST.NST(row=1)');
   {! |?
      _test:=_test_driver.execute("SRD.isEditable()");
      {? _test
      ||
         _test_driver.click('wnd:zsrdo_wer');
         _test_driver.click('fld:@SRDO.DW(row=1)');
         _size:=_test_driver.execute("SRDO.size");
         {? _size>0
         ||
            {! _ii:=1.._size
            |!
               _test_driver.click('wnd:zsrdo_wer');
               _test_driver.click('fld:@SRDO.DW(row=1)');
               _test_driver.menu('Usuń');
               _test_driver.click('dbtn:Tak')
            !}
         ?};
         _test_driver.click('wnd:zsrst_wer');
         _test_driver.click('fld:@SRST.NST(row=1)');
         _test_driver.menu('Usuń');
         _test_driver.click('dbtn:Tak');
         _result:=_test_driver.execute("SRST.f_size()")
      || _test_driver.send('', '{DOWN}', '');
         _result:=_test_driver.execute("SRST.f_next()")
      ?};
      _result
   !}
?};
_test_driver.filter()


\fst_par_prac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Ustawienie parametrów pracy w obszarze FST - kartoteka środków
::   WE: _a - obiekt testujący, _b - rok [INTEGER], _c - okres [STRING]
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;

_test_driver.menu('Parametry pracy');
_test_driver.assert('', 'wnd:@PARSES.#parsesdom', 'title', 'Parametry pracy', 'type', 'e');
_test_driver.click('fld:@PARSES.OKRO_R(ndx=1)');
_test_driver.send('1', '', 'fld:@PARSES.OKRO_R');

_formula:='OKRO.cntx_psh(); OKRO.index(\'NAZ\'); OKRO.prefix(REF.FIRMA); _size:=OKRO.size(); OKRO.cntx_pop(); _size-1';
_size:=_test_driver.execute($_formula);

_formula:='{? OKRO.NAZWA=\''+_c+'\' & OKRO.ROK='+$_b+' || 1 || 0 ?}';

_test_driver.click('fld:@OKRESY.PAR_OKR');
_test_driver.send('', '{F3}', 'fld:@OKRESY.PAR_OKR');
_test_driver.assert('', 'wnd:@OKRO.WYB_OKRO', 'title', 'Okresy', 'type', 's');
_poz:=1;
_test_driver.send('', '{HOME}', '');
_result:=_test_driver.execute($_formula);
{? _size>1
|| {! |?
      _test_driver.send('', '{DOWN}', '');
      _poz+=1;
      _result:=_test_driver.execute($_formula);
      _result=0 & _poz<_size
   !}
?};
{? _result || _test_driver.send('', '{ENTER}', '') || _test_driver.send('', '{ESC}', '') ?};

_test_driver.click('fld:@PARSES.ODD_ZAK(ndx=1)');
_test_driver.send('1', '', 'fld:@PARSES.ODD_ZAK');
_test_driver.send('', '{F2}', '')


\check_amort
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Weryfikacja czy naliczono amortyzację dla wszystkich środków
::   WY: 1 - ok, 0 - nie udało się naliczyć dla wszystkich środków
::----------------------------------------------------------------------------------------------------------------------
_check:=1;
SRST.cntx_psh();
{? SRST.first()
|| {! |?
      {? SRST.NAL<>'T' || _check:=0 ?};
      _check & SRST.next()
   !}
?};
SRST.cntx_pop();
_check


\add_ot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Dołączanie dokumentu OT
::   WE: _a - obiekt sterujący
::       _b - Nazwa środka dla którego dołączany jest dokument OT
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;

_test_driver.click('wnd:zsrdo_wer');

_test_driver.click('fld:@SRDO.SYMBOL(row=2)');
_test_driver.menu('Dołącz:Dokument przyjęcia');
_test_driver.assert('', 'wnd:@SRDO.RED_P', 'title', 'Dokument przyjęcia', 'type', 'e');
_test_driver.send('', '{ENTER}', 'fld:@SRDO.NR');
_test_driver.send('', '{ENTER}', 'fld:@SRDO.SYMBOL');
_test_driver.send('', '{ENTER}', 'fld:@SRDO.DW');
_test_driver.send('', '{ENTER}', 'fld:@SRDO.DO');
_test_driver.send('Przyjęcie środka: '+_b, '{ENTER}', 'fld:@SRDO.UWAGI');
_test_driver.send('', '{F2}', 'fld:@SRDO.KOMISJA')


\wyb_rodzaje_ewidencji
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Wybór rodzaju dekretacji
::   WE: _a = wskazanie na obiekt testujący
::       _b = nazwa oczekiwanego schematu dekretacji
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;

_test_driver.menu('Dekretuj:Z innych dziedzin');
_test_driver.assert('', 'wnd:@tmp.tr_dek_run', 'title', 'Rodzaje ewidencji', 'type', 's');
_test_driver.click('fld:@tmp.OPIS(row=1)');

_formula:='T_DK_RUN.cntx_psh(); T_DK_RUN.prefix(); _size:=T_DK_RUN.size(); T_DK_RUN.cntx_pop(); _size-1';
_size:=_test_driver.execute($_formula);

_formula:='{? T_DK_RUN.OPIS=\''+_b+'\' || 1 || 0 ?}';

_poz:=1;
_test_driver.send('', '{HOME}', '');
_result:=_test_driver.execute($_formula);
{? _size>1
|| {! |?
      _test_driver.send('', '{DOWN}', '');
      _poz+=1;
      _result:=_test_driver.execute($_formula);
      _result=0 & _poz<_size
   !}
?};
{? _result
|| _test_driver.click('btn:@Dekretuj dla jednostki')
|| _test_driver.logger.error(0,'Nie znaleziono podanego rodzaju ewidencji: '+_b);
   _test_driver.send('', '{ESC}', '')
?};
_result


\schemat_dziedz_amor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Dodanie schematu dziedzinowego dla amortyzacji bilansowej
::   WE: _a - wskazanie na obiekt testujący
::       _b [STRING] - rok
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;
_rok:=_b;
_test_driver.control('!desktop', 'open', 'Wspólne\\Obszary\\Ustawienia i parametryzacja', 7);
_app1:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@param', 'title', 'Parametryzacja', 'type', 's');
_test_driver.click('gtab:@Wykonanie');
_test_driver.filter('@FP_WYK.FP_DEF.NAZWA', 'struktury księgowe', '@FP_WYK.FP_INS.NAZWA',_rok);
_test_driver.click('fld:@FP_WYK.FP_DEF.NAZWA(row=1)');
_test_driver.menu('Wybierz');
_test_driver.assert('', 'wnd:@KS.WER', 'title', 'Konta syntetyczne', 'type', 's');
_test_driver.click('gtab:@Schematy dziedzinowe');
_test_driver.click('fld:@tmp.OPIS(row=18)');
_test_driver.click('wnd:@DEK_NAG.DEK_ALL');
_test_driver.click('fld:@DEK_NAG.WARLOG(row=1)');
_test_driver.click('btn:btnPUT');
_test_driver.assert('', 'wnd:@DEK_NAG.DEK_NAG1', 'title', 'Schemat dziedzinowy', 'type', 'e');
_test_driver.click('fld:@ZMIENNE.DEK_SREJ');
_test_driver.send('', '{F3}', 'fld:@ZMIENNE.DEK_SREJ');
_test_driver.assert('', 'wnd:@DEK_STAL.DEK_STAL', 'title', 'Stałe dla schematów dziedzinowych', 'type', 's');
_test_driver.click('btn:btnADD');
_test_driver.assert('', 'wnd:@DEK_STAL.RED', 'title', 'Stałe dla schematów dziedzinowych', 'type', 'e');
_test_driver.send('', '{F3}', 'fld:@DEK_STAL.ODD.OD');
_test_driver.assert('', 'wnd:@ODD.SLO', 'title', 'Jednostki księgowe', 'type', 's');
_test_driver.filter('@ODD.OD', 'CENTRALA');
_test_driver.click('fld:@ODD.OD(row=1)');
_test_driver.menu('wybierz');
_test_driver.send('', '{F3}', 'fld:@DEK_STAL.REJ.KOD');
_test_driver.assert('', 'wnd:@REJ.SLO1', 'title', 'Rejestry', 'type', 's');
_test_driver.filter('@REJ.KOD', 'PK');
_test_driver.click('fld:@REJ.KOD(row=1)');
_test_driver.send('', '{ENTER}', '');
_test_driver.send('', '{F3}', 'fld:@DEK_STAL.DOK_REJ.NAZ');
_test_driver.assert('', 'wnd:@DOK_REJ.SLO', 'title', 'Rodzaje dokumentów', 'type', 's');
_test_driver.click('fld:@DOK_REJ.NAZ(row=1)');
_test_driver.click('fld:@DOK_REJ.NAZ(row=1)');
_test_driver.menu('wybierz');
_test_driver.send('', '{F2}', '');
_test_driver.send('', '{ESC}', '');
_test_driver.send('1', '', 'fld:@DEK_NAG.SDREJ');
_test_driver.send('', '{F2}', '');
_test_driver.send('', '{ESC}', '');
_test_driver.send('', '{ESC}', '');
_app1:=_test_driver.app_wait_for_change()

:Sign Version 2.0 jowisz:1048 2021/04/09 15:22:55 11304e9d81a0f549a16a6a5440c9f8509d3f275fe5a8f9c4e254826ec8f3bb06c0974def1e555b0096aeb0524bf62c3e99cbf8f7237feb9509da167aebaa35e464811778e2c20c27b895c2d300a3dbe03683647a40d55f7d64db2d6946397331df87bdf5c89f0cdc4bb3c3db1d459b663dcf82a787f6a1983946cb9c88b88684

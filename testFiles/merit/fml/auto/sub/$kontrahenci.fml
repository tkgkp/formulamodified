:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $kontrahenci.fml
:: Utworzony: 26.10.2018
:: Autor: GZ
:: Systemy:
::======================================================================================================================
:: Zawartość: Skrypty automatyzujące obsługę kontrahentów
::======================================================================================================================


\obszar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS:
:: AREA: ZWS_KHR
:: TABLE: KH
::   WE: _a - obiekt testujący
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
:: @WHEN
_test_driver.control('!desktop', 'open', 'Wspólne\\Obszary\\Kontrahenci', 7);

_test_driver.app_wait_for_change()


\dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS: Dodaje kontrahenta. Korzysta z danych w tabelach lub wypełnia okno predefiniowanymi danymi
:: AREA: ZWS_KHR
:: TABLE: KH
::   WE: _a - obiekt testujący
::       [_b] - tabela z danymi ogólnymi
::       [_c] - tabela z innymi danymi
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_ile_kh_przed:=exec('policz_kontrahentow','$kontrahenci',_test_driver);
_kod_kontrahenta:='';
{? _>1 & type_of(_b) = type_of(SYSLOG)
|| _dane_ogolne:=_b
|| _dane_ogolne:=~~
?};
{? _>2 & type_of(_c) = type_of(SYSLOG)
|| _inne_dane:=_c;
   _ndx_inne_dane:=_inne_dane.ndx_tmp('',1,'KOD',,0);
   _inne_dane.index(_ndx_inne_dane)
|| _inne_dane:=~~
?};
:: @WHEN
_test_driver.menu('Dołącz');
{? type_of(_dane_ogolne) = type_of(SYSLOG)
|| exec('wypelnij_dane_ogolne','$kontrahenci',_test_driver,_dane_ogolne);
   {? type_of(_inne_dane) = type_of(SYSLOG)
   || _inne_dane.prefix(_dane_ogolne.KOD);
      {? _inne_dane.first()
      || _test_driver.click('etab:@Inne dane');

         exec('wypelnij_inne_dane','$kontrahenci',_test_driver,_inne_dane);
         _test_driver.click('etab:@Dane ogólne')
      ?}
   ?}
|| _test_driver.send('AUTOTEST', '', 'fld:@KH.KOD');
   _test_driver.click('fld:@KH.SKR');
   _test_driver.send('AUTOTEST', '', 'fld:@KH.SKR');
   _test_driver.click('fld:@KH.NAZ_P');
   _test_driver.send('AUTOTEST_01', '', 'fld:@KH.NAZ_P');
   _test_driver.click('fld:@KH.NAZ_P');
   _test_driver.send('AUTOTEST_01', '', 'fld:@KH.NAZ_P');
   _test_driver.click('fld:@KH.NAZ');
   _test_driver.click('fld:@KH.NIP');
   _test_driver.send('4283823141', '', 'fld:@KH.NIP')
?};
_test_driver.send('', '{F2}', '');
_nip_zajety:=_test_driver.exist('dbtn:Kontynuacja');
{? _nip_zajety
|| _test_driver.click('dbtn:Kontynuacja');
   _test_driver.click('dbtn:Nie');
   _kod_kontrahenta:=_test_driver.state('fld:@KH.KOD').value;
   _test_driver.send('', '{ESC}', 'fld:@KH.NIP');
   _test_driver.win_action('file_close')
|| _kod_kontrahenta:=_test_driver.state('fld:@KH.KOD').value;
   _kod_z_bazy:=_test_driver.execute("KH.KOD");
   _test_driver.assert_true('Kod kontrahenta inny niż bazie',_kod_kontrahenta=_kod_z_bazy)
?};

_ile_kh_po:=exec('policz_kontrahentow','$kontrahenci',_test_driver);
:: @THEN
_test_driver.assert_false('Nip zajęty',_nip_zajety);
_test_driver.assert_true('Pojawiło się okno dialogowe',~_test_driver.exist('dbtn:OK'));
_test_driver.assert_equals('Liczba kontrahentów się nie zwiększyła po dodaniu',_ile_kh_po,_ile_kh_przed+1);
_test_driver.assert_true('Pusty kod kontrahenta',_kod_kontrahenta<>'');
{? type_of(_b)>0
|| _test_driver.assert_true('Kod kontrahenta inny niż dodany',_kod_kontrahenta=_dane_ogolne.KOD)
|| _test_driver.assert_true('Kod kontrahenta inny niż dodany',_kod_kontrahenta='AUTOTEST')
?};
{? type_of(_c)>0
|| _inne_dane.ndx_drop(_ndx_inne_dane)
?};
_kod_kontrahenta


\szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS:
::   WE: _a - obiekt testujący
::       _b - kod kontrahenta do wyszukania
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_kod_kontrahenta:=_b;
:: @WHEN
_test_driver.menu('Szukaj dokładnie');
_test_driver.send(_kod_kontrahenta, '{F2}', 'fld:@KH.KOD');
:: @THEN
_znalazl:=~_test_driver.exist('dbtn:OK');
_test_driver.assert_true('Nie znaleziono kontrahenta',_znalazl);
_znalazl


\wyswietl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS:
:: AREA: ZWS_KHR
:: TABLE: KH
::   WE: _a - obiekt testujący
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
:: @WHEN
_test_driver.send('', '{SPACE}', '');
_test_driver.send('', '{ESC}', '')


\popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS:
::   WE: _a - obiekt testujący
::       _b - kod kontrahenta
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_kod_kontrahenta:={? _>1 & type_of(_b)=type_of('') || _b || '' ?};
:: @WHEN
{? _kod_kontrahenta<>''
|| exec('szukaj','$kontrahenci',_test_driver,_kod_kontrahenta)
?};
_test_driver.menu('Popraw');
_test_driver.click('fld:@KH.NAZ_P');
_stara_nazwa:=_test_driver.execute("KH.NAZ_P");
_test_driver.send(_stara_nazwa+'02', '', 'fld:@KH.NAZ_P');
_test_driver.click('fld:@KH.NAZ');
_test_driver.send(_stara_nazwa+'02', '', 'fld:@KH.NAZ');
_test_driver.send('', '{F2}', '')


\usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS:
:: AREA: ZWS_KHR
:: TABLE: KH
::   WE: _a - obiekt testujący
::       _b - kod kontrahenta
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_kod_kontrahenta:={? _>1 & type_of(_b)=type_of('') || _b || '' ?};
:: @WHEN
{? _kod_kontrahenta<>''
|| exec('szukaj','$kontrahenci',_test_driver,_kod_kontrahenta)
?};
_test_driver.menu('Usuń');
_test_driver.click('dbtn:Tak')


\policz_kontrahentow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS:
::   WE: _a - obiekt testujący
::   WY: KH.size
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
:: @WHEN
_fml:="KH.cntx_psh();
       KH.prefix();
       _size:=KH.size();
       KH.cntx_pop();
       _size";
_size:=_test_driver.execute(_fml);
:: @THEN
_size


\wypelnij_dane_ogolne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS: Wypełnia zakładkę 'dane ogólne'
::   WE: _a - obiekt testujący
::       _b - tabela z danymi
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
{? _>1 & type_of(_b) = type_of(SYSLOG)
|| _dane_ogolne:=_b
|| return()
?};
:: @WHEN
_test_driver.send(_dane_ogolne.KOD, '', 'fld:@KH.KOD');
_test_driver.click('fld:@KH.GLN');
_test_driver.send(_dane_ogolne.GLN, '', 'fld:@KH.GLN');
{? _dane_ogolne.TYP<>''
|| _test_driver.click('fld:@KODSLU.TYP');
   _test_driver.send('', '{F3}', 'fld:@KODSLU.TYP');
   _test_driver.popup(_dane_ogolne.TYP)
?};
_test_driver.click('fld:@KH.FIZYCZNY');
{? _dane_ogolne.FIZYCZNY='1' | _dane_ogolne.FIZYCZNY='T'
|| _test_driver.send('checked', '', 'fld:@KH.FIZYCZNY')
|| _test_driver.send('unchecked', '', 'fld:@KH.FIZYCZNY')
?};
_test_driver.click('fld:@KH_DOD.DTKL');
_test_driver.send(_dane_ogolne.DTKL,'','fld:@KH_DOD.DTKL');
_test_driver.click('fld:@KH.NIP');
_test_driver.send(_dane_ogolne.NIP,'','fld:@KH.NIP');
{? _dane_ogolne.PESEL<> ''
|| _test_driver.click('fld:@KH.PESEL');
   _test_driver.send(_dane_ogolne.PESEL,'','fld:@KH.PESEL')
?};
{? _dane_ogolne.NIP_UE<>''
||
   _test_driver.click('fld:@KODSLU.NIP_UE');
   _test_driver.send('','{F3}','fld:@KODSLU.NIP_UE');
   {? ~exec('policz','$nipy_ue',_test_driver)
      | exec('szukaj','$nipy_ue',_test_driver,_dane_ogolne.NIP_UE,0)
   ||
      exec('dolacz','$nipy_ue',_test_driver,_dane_ogolne.NIP_UE)
   ?};
   _test_driver.send('', '{ESC}', '')
?};
_test_driver.click('fld:@KH.REG');
_test_driver.send(_dane_ogolne.REG,'','fld:@KH.REG');
_test_driver.click('fld:@KH.REG_DATA');
_test_driver.send(_dane_ogolne.REG_DATA,'','fld:@KH.REG_DATA');
_test_driver.click('fld:@KH.SKR');
_test_driver.send(_dane_ogolne.SKR,'','fld:@KH.SKR');
_test_driver.click('fld:@KH.NAZ_P');
_test_driver.send(_dane_ogolne.NAZ_P,'','fld:@KH.NAZ_P');
_test_driver.click('fld:@KH.NAZ');
_test_driver.send(_dane_ogolne.NAZ,'','fld:@KH.NAZ');
{? _dane_ogolne.GRKH<>''
|| _test_driver.click('fld:@KH.GRKH.KOD');
   _test_driver.send('', '{F3}', 'fld:@KH.GRKH.KOD');
   exec('szukaj','$grupy_kontrahentow',_test_driver,_dane_ogolne.GRKH);
   _test_driver.menu('wybierz')
?};
{? _dane_ogolne.OBS<>''
|| _test_driver.click('fld:@KH.OBS.KOD');
   _test_driver.send('', '{F3}', 'fld:@KH.GRKH.KOD');
   exec('szukaj','$obszary_sprzedazy',_test_driver,_dane_ogolne.OBS);
   _test_driver.menu('wybierz')
?};
:: Zakładam, ze wyszukiwanie płatności się zmieni, więc zostawiam bez obsługi F3
_test_driver.click('fld:@KH.PLATNOSC.KOD');
_test_driver.send(_dane_ogolne.PLATNOSC,'','fld:@KH.PLATNOSC.KOD');
_test_driver.click('fld:@KH.U');
_test_driver.send(_dane_ogolne.U,'','fld:@KH.U');
_test_driver.click('fld:@KH.WIELKOSC');
_test_driver.send('', '{F3}', '');
{? _dane_ogolne.WIELKOSC='1'
|| _test_driver.popup('mały')
|? _dane_ogolne.WIELKOSC='2'
|| _test_driver.popup('średni')
|? _dane_ogolne.WIELKOSC='3'
|| _test_driver.popup('duży')
|| _test_driver.popup('mały')
?}
::{? _dane_ogolne.STATUS<>''
::||
::   _test_driver.click('fld:@KH_DOD.STATUS');
::   {? _dane_ogolne.STATUS='P'
::   || _test_driver.send('checked', '', 'fld:@KH_DOD.STATUS')
::   || _test_driver.send('unchecked', '', 'fld:@KH_DOD.STATUS')
::   ?}
::?}


\wypelnij_inne_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [19.02]
:: OPIS: Wypełnia zakładkę 'dane ogólne'
::   WE: _a - obiekt testujący
::       _b - tabela z danymi
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN

_test_driver:=_a;
{? _>1 & type_of(_b) = type_of(SYSLOG)
|| _inne_dane:=_b
|| return()
?};
:: @WHEN
{? _inne_dane.KODISO<>''
|| _test_driver.send('', '{F3}', '');
   _test_driver.send(_inne_dane.KODISO, '{ENTER}', '');
   _test_driver.menu('wybierz')
?};
{? _inne_dane.STR_PZ='1'
|| _test_driver.click('fld:@KH.STR_PZ(ndx=1)')
|? _inne_dane.STR_PZ='2'
|| _test_driver.click('fld:@KH.STR_PZ(ndx=2)')
|? _inne_dane.STR_PZ='3'
|| _test_driver.click('fld:@KH.STR_PZ(ndx=3)')
|? _inne_dane.STR_PZ='4'
|| _test_driver.click('fld:@KH.STR_PZ(ndx=4)')
?};
_test_driver.click('fld:@KH_DOD.SP');
{? _inne_dane.SP='T'
|| _test_driver.send('checked','','fld:@KH_DOD.SP')
|| _test_driver.send('unchecked','','fld:@KH_DOD.SP')
?};
_test_driver.click('fld:@KH.KRS');
_test_driver.send(_inne_dane.KRS,'','fld:@KH.KRS');
_test_driver.click('fld:@KH.KRS_DATA');
_test_driver.send(_inne_dane.KRS_DATA,'','fld:@KH.KRS_DATA');
_test_driver.click('fld:@KH.KAPITAL');
_test_driver.send(_inne_dane.KAPITAL,'','fld:@KH.KAPITAL');
_test_driver.click('fld:@KH.KAP_ZROD');
_test_driver.send(_inne_dane.KAP_ZROD,'','fld:@KH.KAP_ZROD');
_test_driver.click('fld:@KH.EORI');
_test_driver.send(_inne_dane.EORI,'','fld:@KH.EORI');

_test_driver.click('fld:@KH.NDT.TR');
_test_driver.send('', '{F3}', '');
_test_driver.send(_inne_dane.TR, '{ENTER}', '');
_test_driver.menu('wybierz');


_test_driver.click('fld:@KH.SN_DT');
_test_driver.send(_inne_dane.SN_DT,'','fld:@KH.SN_DT');

_test_driver.click('fld:@KH.DATA_WDT');
_test_driver.send(_inne_dane.DATA_WDT,'','fld:@KH.DATA_WDT');

{? _inne_dane.REJ_INFO<>''
|| _test_driver.click('fld:@KH.REJ_INFO');
   _test_driver.send(_inne_dane.REJ_INFO,'','fld:@KH.REJ_INFO')
?};
{? _inne_dane.KOLOR<>''
|| _test_driver.click('fld:@KH.KOLOR');
   _test_driver.send(_inne_dane.KOLOR,'','fld:@KH.KOLOR')
?};

{? _inne_dane.KOD_1<>''
||   _test_driver.click('fld:@KH.JEZYK.KOD');
   _test_driver.send('', '{F3}', 'fld:@KH.JEZYK.KOD');
   _test_driver.send(_inne_dane.KOD_1, '{ENTER}', '');
   _test_driver.send('', '{ENTER}', '')
?};

_test_driver.click('fld:@KH.WP.TR');
_test_driver.send('', '{F3}', '');
_test_driver.menu('Dołącz');
_kod:=form(_test_driver.execute("SLO.size")+1,-3);
_test_driver.send(_kod, '{TAB}', 'fld:@SLO.KOD');
_test_driver.send(_inne_dane.TR_1, '{ENTER}', 'fld:@SLO.TR');
_test_driver.send('','{F2}','');

_test_driver.menu('wybierz')


:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:05 286f07b34da10e84cd5d0b61e2ebbfb9c03d5aef0e62db7f8f5d5d6f732acdb7515662b34a90edc391427462b4fbfcfdb078b482e463f40ffc1ccc4a532000535e2f8e5a2407881dd78b170ffd38c9b039bb873fa78e61a6eba3ec4763938c220525e01a56b460a0df3e11d0d9968b85876bc5d88313ff643e608991f281d1da

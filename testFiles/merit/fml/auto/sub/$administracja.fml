:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $administracja.fml
:: Utworzony: 25.03.2019
:: Autor: ARTSLO
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================
\excel_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: AREA: [ZWS_ADM]
:: OPIS: Formuła rozpoczyna import danych z plików excel
::   WE: _a - wskazanie na obiekt testowy
::       _b - obiekt z ustawieniami importu
::   WY: obiekt z informacją czy były komunikaty
::----------------------------------------------------------------------------------------------------------------------
::GIVEN
_test_driver:=_a;
_test_driver.logger.info('START WYWOŁANIE: \\excel_import/$administracja.fml (time: '+$date()+' '+(time()$3)+')');
_set:=_b;
_wynik:=obj_new('blokujace','warunkowe','KOM','proc','dial');
_wynik.blokujace:=_wynik.warunkowe:=0;
_wynik.proc:=tab_tmp(,'LP','INTEGER','lp','MESSAGE','STRING[255]','msg','NAG','INTEGER','Nagłówek');
_proc_files:=exec('proc_files_list','$wspolne');
_ndx:=_proc_files.ndx_tmp(,1,'NAME',,,'NAME',,);
_proc_files.index(_ndx);

::WHEN
_test_driver.assert('', 'wnd:@XPERTIS.ADMIN', 'title', 'Administracja', 'type', 'e');
_test_driver.click('gtab:@Wspólne');
exec('wait_time','$lib_base',_test_driver,time(0,0,15));
_test_driver.click('btn:aa1840427000000');
exec('wait_time','$lib_base',_test_driver,time(0,0,15));
_war1:="{? _a.exist('fld:@tmp.MODE') &  _a.exist('fld:@tmp.PROC') & _a.exist('fld:@tmp.DETAILS') || 0 || 1 ?}";
exec('wait','$lib_base',_test_driver,_war1,'(oczekiwanie na inicjalizację importu)');

:: fragment dodany na potrzeby ominięcia błędu okienka z kontrolką :::::::::::::::::::::::::::::::::::::::::::::::::::::
_test_driver.win_action('maximize');
_test_driver.win_action('restore');
::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

:: Wybór ustawień importu
{? _test_driver.state('fld:@tmp.PROC').value<>'3'
|| _test_driver.click('fld:@tmp.PROC(ndx=3)');
   _test_driver.send('3', '', 'fld:@tmp.PROC')

?};
{? _set.NADPISUJ='T'
|| _test_driver.click('fld:@tmp.MODE(ndx=1)');
   _test_driver.send('1', '', 'fld:@tmp.MODE')
|| _test_driver.click('fld:@tmp.MODE(ndx=2)');
   _test_driver.send('2', '', 'fld:@tmp.MODE')
?};
_test_driver.send('','{F2}','');
{? _set.NADPISUJ='T' & _test_driver.exist('dbtn:Tak')
|| _test_driver.click('dbtn:Tak')
?};

_test_driver.ctrl_call_ret('!application>ctr:filechooser', 'showOpenDialog', 'int', '1');
_test_driver.ctrl_call_ret('!application>ctr:filechooser', 'getSelectedFile', 'string', _set.DIR);
_warunek:="{? _a.exist('dial:')
           || {? ~(var_pres('__dial_kom')>0)
              || __dial_kom:=tab_tmp(,'MSG','STRING[255]','Komunikat')
              ?};
              __dial_kom.MSG:=_a.state('dial:').message;
              __dial_kom.add();
::              {? _a.exist('dbtn:Zamknij')
::              || _a.click('dbtn:Zamknij')
::              |? _a.exist('dbtn:OK')
::              || _a.click('dbtn:OK')
::              |? _a.exist('dbtn:Tak')
::              || _a.click('dbtn:Tak')
::              ?}
              {? _a.exist('dbtn:Tak')
              || _a.click('dbtn:Tak')
              ?}
           ?};
           {? _a.exist('wnd:@tmp.kyoaskzuwalisct')
           || {? _a.exist('btn:@OK')
              || _a.click('btn:@OK')
              ?}
           ?};
           ~(_a.exist('wnd:@tmp.#xls_import_nag'))";
exec('wait','$lib_base',_test_driver,_warunek,'(oczekiwanie na zakończenie importu)');

:: pobieramy szczegóły dotyczące importu procesow
{? var_pres('__proc_kom')<0
|| _dalej:={? _test_driver.execute("{? var_press('XLS_PROC_KOMM')>0 || '1' || '0' ?}")='1' || 1 || 0 ?};
   {? _dalej
   || _xls_size:=_test_driver.execute("XLS_PROC_KOMM.TAB.size()");
      _pak_size:=9;
      _vpakietow:={? ((_xls_size/_pak_size)-((_xls_size/_pak_size)$0))>0 || (_xls_size/_pak_size)+1 || (_xls_size/_pak_size) ?};
      _txt:=obj_new(_vpakietow);
      _start:=0;
      _end:=10;
      {! _i:=1.._vpakietow
      |! _fmrl:='_tab:=sql(\'select LP, MESSAGE from :_a where LP>:_b and LP<:_c \',XLS_PROC_KOMM.TAB,'+$_start+','+$_end+');';
         _fmrl+='_tab.json_records(,\'LP\',,\'MESSAGE\',)';
         _txt[_i]:=_test_driver.execute($_fmrl);
         _start+=_pak_size;
         _end+=_pak_size
      !};
      __proc_kom:=_txt
   ?}
?};

::THEN
:: sprawdzenie istanienia komunikatów do importu
_test_driver.assert('', 'wnd:@tmp.#xls_import_nag', 'title', 'Podsumowanie importu', 'type', 's');
_test_driver.click('btn:@Wszystkie komunikaty');
_test_driver.assert('', 'wnd:@tmp.#xls_import_his', 'title', 'Historia importu', 'type', 's');

:: komunikaty blokujące walidacje
_test_driver.filter('@tmp.RESVALID', '~1');
_test_driver.click('fld:@tmp.RESVALID(row=1)');
{? _test_driver.state('fld:@tmp.RESVALID(row=1)').value<>''
|| _wynik.blokujace:=1
?};

::komunikaty nie blokujące walidacji
_test_driver.filter('@tmp.RESVALID', '1');
_test_driver.click('fld:@tmp.RESVALID(row=1)');
{? _test_driver.state('fld:@tmp.RESVALID(row=1)').value<>''
|| _wynik.warunkowe:=1
?};

::opuszczenie okna wszystkich komunikatów
_test_driver.send('', '{ESC}', '');
_test_driver.execute("params_exec('import_kom', '$wspolne',,1,'plik_roboczy')");
_wynik.KOM:=exec('importBuff','$lib_base','$import_kom.csv',exec('bf_xls_kom','$wspolne'),'UTF-8');
{? var_pres('__proc_kom')>0
|| {! _i:=1..obj_len(__proc_kom)
   |! _json_sparsowany:=json_parse(__proc_kom[_i]);
      {? ~(type_of(_json_sparsowany)=type_of(~~))
      || {! _n:=1..obj_len(_json_sparsowany)
         |! _wynik.proc.MESSAGE:=_json_sparsowany[_n].MESSAGE;
            _wynik.proc.LP:=#_json_sparsowany[_n].LP;
            _nagt:=((_json_sparsowany[_n].MESSAGE*' ')-1)+_json_sparsowany[_n].MESSAGE;
            _wynik.proc.NAG:={? _proc_files.find_key(_nagt,_nagt)
                             || 1
                             || 0
                             ?};
            _wynik.proc.add()
         !}
      ?};
      &_json_sparsowany
   !};
   &__proc_kom
?};
{? var_pres('__dial_kom')>0
|| _wynik.dial:=__dial_kom
?};

::opuszczenie mechanizmu
_test_driver.send('', '{ESC}', '');

_test_driver.logger.info('KONIEC WYWOŁANIE: \\excel_import/$administracja.fml (time: '+$date()+' '+(time()$3)+')');
return(_wynik)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:12:17 68dfb7e29196ffa89a41640dd2d9ebcf0871e7bbc721e2be842ba2811dc24735d88005b8a4b710bc71ebe37aad3aa9a4019c7a3c9fa5d16a2f5975d75c0967ff0e6285da2ffe5dd183e961fe2f08fa170177758ec81ad1e03b24b56160476c4dce83ce7674236072ba6406a8843da879205d576bfb642e6873150ca43584d108

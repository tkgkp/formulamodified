:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $obsluga_procesow.fml
:: Utworzony: 08.04.2019
:: Autor: ARTSLO
:: Systemy:
::======================================================================================================================
:: Zawartość: Plik z funkcjiami bazowymi dla obszaru Obsługa procesów. Podstawowe formuły obsługujące funkcjonalności
::            oraz tworzące bufory na niezbędne dane.
::======================================================================================================================
\bf_proc_akc
::----------------------------------------------------------------------------------------------------------------------
::   UTW: ARTSLO [19.22]
::  OPIS: bufor na liste symboli procesów które mają być akceptowane
::----------------------------------------------------------------------------------------------------------------------
return(tab_tmp(1,
'SYMBOL','STRING[100]','Symbol procesu',
'WERSJA','STRING[5]','Wersja procesu'))


\akceptuj_proces
::----------------------------------------------------------------------------------------------------------------------
::   UTW: ARTSLO [19.22]
::  OPIS: Formuła realizująca akceptowanie procesu
::  AREA: [ZPR_PRO]
:: TABLE: [B_PROC]
::    WE: _a - wskazanie na obiekt testujący
::        _b - parametr określający tryb pracy 0 - akceptacja wskazanego procesu / 1 - akceptacja wszystkich procesów
::        _c - wskazanie na obiekt okreslający proces dla trybu pracy akceptacji wskazanego procesu
::    WY: 1/0 - powodzenie operacji
::----------------------------------------------------------------------------------------------------------------------
::GIVEN
_test_driver:=_a;
_test_driver.logger.info('START WYWOŁANIE: \\akceptuj_proces/$obsluga_procesow.fml (time: '+$date()+' '+(time()$3)+')');
_tryb:=_b;
{? ~_tryb || _proces:=_c ?};
_fail:=0;
_msg:='';
_war_czekania:="_a.exist('dial:') & (11+(_a.state('dial:').title))+((_a.state('dial:').title)+2)='Merit ERP (%)'";

::WHEN
_test_driver.assert('', 'wnd:@B_PROC.WER', 'title', 'Procesy', 'type', 's');
{? _tryb
|| _test_driver.click('fld:@B_PROC.NAME(row=2)');
   _test_driver.send('', '{NUMPADADD}', '');
   {? ~_test_driver.exist('btn:B_PROC_AKCEPTUJ')
   || _test_driver.fail('Przycisk Akceptuj jest niedostępny!')
   || _test_driver.click('btn:B_PROC_AKCEPTUJ');
      _test_driver.click('dbtn:Tak');
      exec('wait','$lib_base',_test_driver,_war_czekania,'(przetwarzanie grupowe akceptacji procesów)');
      _test_driver.assert('', 'wnd:@tmp.kyoaskzuwalisct', 'title', '', 'type', 's');
      _test_driver.click('btn:@OK')
   ?}
|| _test_driver.filter('@B_PROC.SYMBOL', _proces.SYMBOL, '@B_PROC.VER', _proces.WERSJA);
   _test_driver.click('fld:@B_PROC.SYMBOL(row=1)');
   _test_driver.assert('', 'fld:@B_PROC.SYMBOL(row=1)', 'value', _proces.SYMBOL);
   {? ~_test_driver.exist('btn:B_PROC_AKCEPTUJ')
   || {? _test_driver.state('fld:@B_PROC.ACCEPTED(row=1)').value='checked'
      || _test_driver.logger.info('Proces '+_proces.SYMBOL+' był jest już zaakceptowany')
      || _test_driver.fail('Przycisk Akceptuj jest niedostępny!')
      ?}
   || _test_driver.click('btn:B_PROC_AKCEPTUJ');
      {? _test_driver.exist('wnd:@tmp.kyoaskzuwalisct')
      || _test_driver.click('btn:@OK');
         {? _test_driver.exist('dial:') &
            _test_driver.state('dial:').message='Podczas weryfikacji procesu zostały zgłoszone uwagi.\n\nCzy zaakceptować definicję procesu?'
         || _test_driver.click('dbtn:Tak')
         ?}
      || _test_driver.click('dbtn:Tak')
      ?}
   ?}
?};

::THEN
{? _tryb
|| _test_driver.filter('@B_PROC.ACCEPTED', 'N');
   _test_driver.click('fld:@B_PROC.SYMBOL(row=1)');
   _fail:=_test_driver.state('fld:@B_PROC.SYMBOL(row=1)').value<>'';
   _msg:='Nie wszystkie procesy udało się zaakceptować'
|| _test_driver.filter('@B_PROC.SYMBOL', _proces.SYMBOL, '@B_PROC.VER', _proces.WERSJA);
   _fail:=_test_driver.state('fld:@B_PROC.ACCEPTED(row=1)').value='unchecked';
   _msg:='Nie udało się zaakceptować procesu!'
?};
{? _fail
|| _test_driver.fail(_msg)
?};
_test_driver.logger.info('KONIEC WYWOŁANIE: \\akceptuj_proces/$obsluga_procesow.fml (time: '+$date()+' '+(time()$3)+')');
return(_fail)

:Sign Version 2.0 jowisz:1048 2021/04/09 15:22:55 5428eb6b938ce36d5b27f2da358533aa2b5ef0f59d336d02fb6e3f1b6addb698cca2025afb80c1afe9e0175dfb26c7d1afe09bccc37a74ae541754832b454541826f4efd542b4ad72a4672cd7a8d7a0846a88c932019f0b0cc0ee92ed09aca7eab82302610dab1f996dd10ddcbf71c030b7beb48f9c89c88f8c7113c2653e3ea

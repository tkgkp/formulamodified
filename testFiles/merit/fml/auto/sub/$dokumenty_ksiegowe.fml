:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $dokumenty_ksiegowe.fml
:: Utworzony: 07.04.2020
:: Autor: WHAN
:: Systemy:
::======================================================================================================================
:: Zawartość: Skrypty automatyzujące obsługę dokumentów księgowych
::======================================================================================================================


\obszar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: uruchamia obszar dokumenty księgowe
:: TABLE: DOK
::   WE: _a - obiekt testujący
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
:: @WHEN
_test_driver.control('!desktop', 'open', 'Finanse\\Obszary\\Dokumenty księgowe', 7);

_app:=_test_driver.app_wait_for_change();
:: @THEN
_test_driver.assert('', 'wnd:fks_dok_odd', 'edit_field', '', 'state', 'maximized', 'sum_row_allowed', 'true', 'sum_row_enabled', 'false', 'title', 'Dokumenty księgowe', 'type', 's');
_app


\dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: dołącza dokument księgowy
::   WE: _a - obiekt testujący
::       _b - tabela z danymi ogólnymi
::       _c - tabela z pozycjami VAT
::       _d - tabela z pozycjami dekret
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
{? _>1 & type_of(_b) = type_of(SYSLOG)
|| _dane_ogolne:=_b
|| _dane_ogolne:=~~
?};
{? var_pres('_c')>0 & type_of(_c) = type_of(SYSLOG)
|| _dane_dekret:=_c;
   _ndx_dane_dekret:=_dane_dekret.ndx_tmp('',1,'ID_DOK',,0);
   _dane_dekret.index(_ndx_dane_dekret)
|| _dane_dekret:=~~
?};
{? var_pres('_d')>0  & type_of(_d) = type_of(SYSLOG)
|| _dane_poz:=_d;
   _ndx_dane_poz:=_dane_poz.ndx_tmp('',1,'ID_DOK',,0);
   _dane_poz.index(_ndx_dane_poz)
|| _dane_poz:=~~
?};
:: @WHEN
_test_driver.click('btn:addDOK');
exec('wypelnij_dane_ogolne','$dokumenty_ksiegowe',_test_driver,_dane_ogolne);
:: jeśli symbol lub nr nadany automatycznie to wpisz do tabeli z danymi
{? _dane_ogolne.NK=''
|| _dane_ogolne.NK:=_test_driver.state('fld:@DOK.NK').value;
   _dane_ogolne.put()
?};
{? _dane_ogolne.NR=0
|| _dane_ogolne.NR:=#_test_driver.state('fld:@DOK.NR').value;
   _dane_ogolne.put()
?};
_test_driver.click('btn:@Zapisz');
:: Obsługa komunikatu - "Aktualny okres obrachunkowy kwiecień 2021.\nRozliczenie podatku VAT nastąpi w okresie sierpień 2021.\nAkceptujesz?"
{? _test_driver.exist('dial:') & _test_driver.state('dial:').message*'Aktualny okres obrachunkowy'
   & _test_driver.state('dial:').message*'Akceptujesz?'
|| _test_driver.click('dbtn:Tak');
   _test_driver.assert('', 'wnd:poz_vsel', 'title', 'Pozycje VAT', 'type', 's')
?};
::   wybór pozycji z danego dokumentu
_dane_dekret.prefix(_dane_ogolne.ID,);
{? _dane_ogolne.KOD='BANK_EUR'
   | _dane_ogolne.KOD='BANK_PLN'
   | _dane_ogolne.KOD='BANK_USD'
   | _dane_ogolne.KOD='BARTER'
   | _dane_ogolne.KOD='KASA_EUR'
   | _dane_ogolne.KOD='KASA_PLN'
   | _dane_ogolne.KOD='MAGAZYN'
   | _dane_ogolne.KOD='NOTY'
   | _dane_ogolne.KOD='PK'
   | _dane_ogolne.KOD='PŁACE'
|| exec('dolacz_pozycje_dekret','$dokumenty_ksiegowe',_test_driver,_dane_dekret);
   _test_driver.click('wnd:@DOK.dokeditmain[2]>btn:@Zakończ dokument')
|? _dane_ogolne.KOD='SPR_EXP'
   | _dane_ogolne.KOD='SPR_KRAJ'
   | _dane_ogolne.KOD='SPR_UE'
   | _dane_ogolne.KOD='ZAK_IMP'
   | _dane_ogolne.KOD='ZAK_INW'
   | _dane_ogolne.KOD='ZAK_KRAJ'
   | _dane_ogolne.KOD='ZAK_UE'
|| _dane_poz.prefix(_dane_ogolne.ID,);
   exec('dolacz_pozycje_zak_spr','$dokumenty_ksiegowe',_test_driver,_dane_poz,_dane_dekret,_dane_ogolne);
   _test_driver.click('btn:@Zakończ dokument');
   {? _test_driver.exist('dial:')
   ||
      {? _test_driver.state('dial:').message*'Aktualny okres obrachunkowy'
         & _test_driver.state('dial:').message*'Akceptujesz?'
      || _test_driver.click('dbtn:Tak');
         _test_driver.click('dbtn:Tak')
      ?};
      {? _test_driver.exist('dial:') & _test_driver.state('dial:').message='Oznaczono dokument opcją \"Wymagany split payment\".\n\nPosiada'
      +' on pozycje VAT z wybraną opcją \"Split payment - Wymagany\".\nKwota brutto nie przekracza jednak 15 000,00 zł.'
      +'\nCzy kontynuować?'
      || _test_driver.click('dbtn:Tak')
      ?};
      {? _test_driver.exist('dial:') & _test_driver.state('dial:').message='Niezgodność kręgu kosztów dla dokumentu.'
      || _test_driver.assert('', 'dial:@MessageBox', 'icon', 'ASK', 'message', 'Niezgodność kręgu kosztów dla dokumentu.', 'title', 'Merit ERP');
         _test_driver.click('dbtn:Kontynuuj')
      ?};
      {? _test_driver.exist('dial:') & _test_driver.state('dial:').message*
                        'Nie można zakończyć rejestracji dokumentu:\nDokument'
      || {? _test_driver.state('dial:').message * 'pozycja 2'
         || _test_driver.click('dbtn:OK');
            _test_driver.click('btn:@Pozycje');

            _test_driver.click('gtab:@Dekret');
            _test_driver.click('fld:@POZ.KON(row=2)');
            _test_driver.click('btn:aa4961765000000');
            _test_driver.assert('', 'wnd:@POZ.POZ_PRR2', 'title', 'Pozycja dokumentu', 'type', 'e');
            _wartosc:=_test_driver.state('fld:@POZ.KON').value;
::      wstawienie nr. zlecenia w odpowiednie miejsce
            {? _wartosc*'PL-'>0
            ||
               _wartosc:=(_wartosc*'PL-'-1)+_wartosc+'PL-'+_dane_ogolne.ZLEC+'-'+( (_wartosc*'PL-'+2)-_wartosc)
            |? _wartosc*'EX-'>0
            || _wartosc:=(_wartosc*'EX-'-1)+_wartosc+'EX-'+_dane_ogolne.ZLEC+'-'+( (_wartosc*'EX-'+2)-_wartosc)
            |? _wartosc*'UE-'>0
            || _wartosc:=(_wartosc*'UE-'-1)+_wartosc+'UE-'+_dane_ogolne.ZLEC+'-'+( (_wartosc*'UE-'+2)-_wartosc)
            ?};
            _test_driver.send(_wartosc, '', 'fld:@POZ.KON', 21);
            _test_driver.send('', '{F2}', '');
            _test_driver.send('', '{ESC}', '');
            _test_driver.click('btn:@Zakończ dokument')
         |? _test_driver.state('dial:').message * 'pozycja 1'
         ||
::         dodanie kontrahenta do słownika kont analitycznych
            _test_driver.click('dbtn:OK');

            _test_driver.click('btn:@Pozycje');
            _test_driver.assert('', 'wnd:poz_vsel', 'title', 'Pozycje VAT', 'type', 's');
            _test_driver.click('gtab:@Dekret');
            _test_driver.click('fld:@POZ.KON(row=1)');
            _test_driver.click('btn:aa4961765000000');
            _test_driver.assert('', 'wnd:@POZ.POZ_PRR2', 'title', 'Pozycja dokumentu', 'type', 'e');
            _test_driver.send(_test_driver.state('fld:@POZ.KON').value-5, '{F3}', 'fld:@POZ.KON', 7);

            _test_driver.assert('', 'wnd:@SLO.EDKON', 'title', '', 'type', 's');
            _test_driver.filter('@SLO.KOD', _dane_ogolne.KOD_KH);
            _test_driver.click('fld:@SLO.KOD(row=1)');
            _test_driver.click('fld:@SLO.TR(row=1)');
            {? _test_driver.state('fld:@SLO.TR(row=1)').value=''
            || _test_driver.click('btn:aa2465262000000');
               _test_driver.assert('', 'wnd:@KH.WER_SLO', 'title', 'Kontrahenci', 'type', 's');
               _test_driver.filter('@KH.KOD',_dane_ogolne.KOD_KH);
               _test_driver.click('fld:@KH.KOD(row=1)');
               _test_driver.click('fld:@KH.SKR(row=1)');
               _test_driver.send('', '{ENTER}', '');
               _test_driver.click('fld:@SLO.TR(row=1)')
            ?};
            _test_driver.send('', '{ENTER}', '');
            _test_driver.send('', '{F2}', '');
            _test_driver.click('wnd:@DOK.dokeditmain[2]>btn:@Zakończ dokument')
         ?}
      ?}
   ?}
?}


\dolacz_pozycje_dekret
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: metoda dołączająca pozycje dokumentu MAGAZYN, NOTY, PK, PŁACE
:: TABLE: DOK
::   WE: _a - obiekt testujący
::       _b - tabela z pozycjami do dodania z założonym prefix'em
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_dane_poz:=_b;
:: @WHEN
_test_driver.menu('Dołącz');

{? _dane_poz.first()
||
   {! |?
      _test_driver.assert('', 'wnd:@POZ.POZ_PRR2', 'title', 'Pozycja dokumentu', 'type', 'e');
      {? _dane_poz.KON<>''
      ||
         _test_driver.send(_dane_poz.KON, '', 'fld:@POZ.KON', 6)
      ?};
      {? _dane_poz.STR<>''
      || _test_driver.click('fld:@POZ.STR');
         _test_driver.send(_dane_poz.STR, '', 'fld:@POZ.STR')
      ?};
      {? _dane_poz.WAL_KOD<>''
      || _test_driver.click('fld:@POZ.WAL.KOD');
         _test_driver.send(_dane_poz.WAL_KOD, '', 'fld:@POZ.WAL.KOD')
      ?};
      {? _dane_poz.OD<>'' & _test_driver.state('fld:@POZ.ODD.OD').enable='1'

      || _test_driver.click('fld:@POZ.ODD.OD');
         _test_driver.send(_dane_poz.OD, '','fld:@POZ.ODD.OD')
      ?};
      {? _dane_poz.ID<>'' & _test_driver.state('fld:@POZ.ID').enable='1'
      || _test_driver.click('fld:@POZ.ID');
         _test_driver.send(_dane_poz.ID, '','fld:@POZ.ID')
      ?};
      {? _dane_poz.WSK_PRAT<>'' & _test_driver.state('fld:@POZ.WSK_PRAT').enable='1'
      || _test_driver.click('fld:@ROZRACH.WSK_PRAT');
         _test_driver.send(_dane_poz.WSK_PRAT, '','fld:@ROZRACH.WSK_PRAT')
      ?};
      {? _dane_poz.SYM_ZEW<>'' & _test_driver.state('fld:@POZ.SYM_ZEW').enable='1'
      || _test_driver.click('fld:@POZ.SYM_ZEW');
         _test_driver.send(_dane_poz.SYM_ZEW, '','fld:@POZ.SYM_ZEW')
      ?};
      {? _dane_poz.TID<>'' & _test_driver.state('fld:@POZ.TID').enable='1'
      || _test_driver.click('fld:@POZ.TID');
         _test_driver.send(_dane_poz.TID, '','fld:@POZ.TID')
      ?};
      {? _dane_poz.SLO_PROJ<>'' & _test_driver.state('fld:@POZ.SLO_PROJ.KOD').enable='1'
      || _test_driver.click('fld:@POZ.SLO_PROJ.KOD');
         _test_driver.send(_dane_poz.SLO_PROJ, '','fld:@POZ.SLO_PROJ.KOD')
      ?};
      {? _dane_poz.DO<>'' & _test_driver.state('fld:@POZ.DO').enable='1'
      || _test_driver.click('fld:@POZ.DO');
         _test_driver.send(_dane_poz.DO, '','fld:@POZ.DO')
      ?};
      {? _dane_poz.TP<>'' & _test_driver.state('fld:@POZ.TP').enable='1'
      || _test_driver.click('fld:@POZ.TP');
         _test_driver.send(_dane_poz.TP, '','fld:@POZ.TP')
      ?};
      {? _dane_poz.SP_WYM<>''
      ||
         {? _dane_poz.SP_WYM='W' | _dane_poz.SP_WYM='T'
         || _test_driver.click('fld:@POZ.SP_WYM(ndx=1)')
         |? _dane_poz.SP_WYM='Z'
         || _test_driver.click('fld:@POZ.SP_WYM(ndx=2)')
         |? _dane_poz.SP_WYM='D'
         || _test_driver.click('fld:@POZ.SP_WYM(ndx=3)')
         |? _dane_poz.SP_WYM='B' | _dane_poz.SP_WYM='N'
         || _test_driver.click('fld:@POZ.SP_WYM(ndx=4)')
         ?}
      ?};
      {? _dane_poz.SUMW<>0
      || _test_driver.click('fld:@POZ.SUMW');
         _test_driver.send($_dane_poz.SUMW, '','fld:@POZ.SUMW')
      ?};
      {? _dane_poz.KURS<>0
      || _test_driver.click('fld:@POZ.KURS');
         _test_driver.send($_dane_poz.KURS, '','fld:@POZ.KURS')
      ?};
      {? _dane_poz.SUM<>0
      || _test_driver.click('fld:@POZ.SUM');
         _test_driver.send($_dane_poz.SUM, '','fld:@POZ.SUM')
      ?};
      {? _dane_poz.SP_V<>0
      || _test_driver.click('fld:@POZ.SP_V');
         _test_driver.send($_dane_poz.SP_V, '','fld:@POZ.SP_V')
      ?};
      {? _dane_poz.OP<>''
      || _test_driver.click('fld:@POZ.OP');
         _test_driver.send(_dane_poz.OP, '','fld:@POZ.OP')
      ?};
      _test_driver.send('', '{F2}', '');
      _dane_poz.next()
   !}
||
   _test_driver.fail('Brak zaimportowanych pozycji dokumentu');
   return(0)
?};
_test_driver.send('', '{ESC}', 'fld:@POZ.KON')


\dolacz_pozycje_zak_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: metoda dołączająca pozycje dokumentu  SPR_EXP, SPR_KRAJ, SPR_UE, ZAK_IMP, ZAK_INW, ZAK_KRAJ, ZAK_UE
:: TABLE: DOK
::   WE: _a - obiekt testujący
::       _b - tabela z pozycjami VAT     do dodania z założonym prefix'em
::       _c - tabela z pozycjami dekretu do dodania z założonym prefix'em
::       _d - tabela z danymi ogólnymi
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_dane_poz:=_b;
_dane_dekret:=_c;
_dane_ogolne:=_d;
:: @WHEN
_test_driver.click('btn:aa8053507000000');
:: Dodanie pozycji VAT
{? _dane_poz.first()
||
   {! |?
      exec('wypelnij_pozycje_zak_spr','$dokumenty_ksiegowe',_test_driver,_dane_poz,_dane_ogolne);
      _test_driver.send('', '{F2}', '');
      _dane_poz.next()
   !};
::   Gdy nie ma pozycji dekretu to dekretujemy automatycznie
   {? ~_dane_dekret.first()
   || _test_driver.click('btn:btnAUTD')
   ?};
   {?  _test_driver.exist('dial:')
   || {? _test_driver.state('dial:').message='Pozycje VAT zostały automatycznie zadekretowane.'
      || _test_driver.click('dbtn:OK')
      |? _test_driver.state('dial:').message='Automatyczne dekretowanie dokumentu VAT dla pozycji'
      || _test_driver.click('dbtn:Wszystkich')
      || _test_driver.click('dbtn:Tak')
      ?}
   ?}
?};
:: Dodanie pozycji dekretu
{? _dane_dekret.first()
   || _test_driver.click('gtab:@Dekret');

      exec('dolacz_pozycje_dekret','$dokumenty_ksiegowe',_test_driver,_dane_dekret)
   ?};
_test_driver.send('', '{ESC}', '')


\wyswietl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: metoda wyświetlająca dokument
:: AREA: ZWS_KHR
:: TABLE: KH
::   WE: _a - obiekt testujący
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
:: @WHEN
_test_driver.send('', '{SPACE}', '');
_test_driver.assert('', 'wnd:@DOK.dokeditmain', 'title', 'Dokument księgowy', 'type', 'e');
_test_driver.send('', '{ESC}', '')


\popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: metoda modyfikująca symbol dokumentu
::   WE: _a - obiekt testujący
::       _b - tabela danymi dokumentu
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
{? _>1 & type_of(_b) = type_of(SYSLOG)
|| _dane_ogolne:=_b
|| return()
?};
:: @WHEN
_grayed:=_test_driver.execute( "DOK.actions_grayed(cur_win(1,1))" );
_test_driver.execute($('DOK.actions_grayed(\'C_REN4\',\''+_grayed+'\')'));
:: wycięcie podmenu
{! |? _poz1:=_grayed*'('
|!
_str1:=_poz1-1+_grayed;
_poz2:=_grayed*')';
_str2:=_poz2-_grayed;
_grayed:=_str1+_str2
!};
{? _grayed * 'W'>0
|| _test_driver.menu('Wycofaj księgowanie')
|| _test_driver.menu('Wycofaj')
?};
_test_driver.click('btn:btnPUT');
_test_driver.assert('', 'wnd:@DOK.dokeditmain', 'title', 'Dokument księgowy', 'type', 'e');
_test_driver.click('fld:@DOK.NK');
_symbol:='';
{? _dane_ogolne.KOD='MAGAZYN'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'MAGAZYN','M')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='NOTY'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'NOTY','N')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='PK'
|| _symbol:=_test_driver.execute( "exec('sym_dok','fks_auto')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='PŁACE'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'PŁACE','PŁ')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='SPR_EXP'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'SPR_EXP','SE')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='SPR_KRAJ'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'SPR_KRAJ','SK')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='SPR_UE'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'SPR_UE','SU')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='ZAK_IMP'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'ZAK_IMP','ZIM')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='ZAK_INW'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'ZAK_INW','ZIN')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='ZAK_KRAJ'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'ZAK_KRAJ','ZK')" )+'/'+_dane_ogolne.ID
|? _dane_ogolne.KOD='ZAK_UE'
|| _symbol:=_test_driver.execute( "gsub(exec('sym_dok','fks_auto'),'ZAK_UE','ZU')" )+'/'+_dane_ogolne.ID
?};
_test_driver.send(_symbol, '', 'fld:@DOK.NK');
_dane_ogolne.NK:=_symbol;
_dane_ogolne.put();
_test_driver.click('btn:@Zakończ dokument');
:: Obsługa komunikatu - "Aktualny okres obrachunkowy kwiecień 2021.\nRozliczenie podatku VAT nastąpi w okresie sierpień 2021.\nAkceptujesz?"
{? _test_driver.exist('dial:') & _test_driver.state('dial:').message*'Aktualny okres obrachunkowy'
   & _test_driver.state('dial:').message*'Akceptujesz?'
|| _test_driver.click('dbtn:Tak')
?}


\przelew
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: wykonuje zadanie przelewu z listy todo
::   WE: _a - obiekt testujący
::       _b - Symbol dokumentu
::       _c - Kwota
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_sym:=_b;
_kwota:=_c;
:: @WHEN
_test_driver.filter('@BI_TODO.BI_PREL.DESC', 'Utwórz przelew z dokumentu: '+_sym);
_test_driver.click('fld:@BI_TODO.BI_PREL.DESC(row=1)');
_test_driver.click('btn:TODO_RUN');
{? _test_driver.execute( "exec('f_pbgnt2','homebank',OP.TZ)")=0
|| _test_driver.click('dbtn:Utwórz przelew')
?};
_test_driver.assert('', 'wnd:@WYDRUKIN.PRZELEW2', 'title', 'Parametry przelewów', 'type', 'e');
_test_driver.send('', '{F2}', '');
_test_driver.assert('', 'wnd:@PB.pbedit_edit', 'title', 'Przelew', 'type', 'e');
_test_driver.click('fld:@PB.SP');
_test_driver.send('unchecked', '', 'fld:@PB.SP');
_test_driver.click('fld:@RACHBANK.RB_KH_PB');
_test_driver.send('55 1030 0019 8957 3738 3902 7349', '', 'fld:@RACHBANK.RB_KH_PB');
{? #_test_driver.state('fld:@PB.KW').value<=0
||  _test_driver.click('fld:@PB.KW');
   _test_driver.send($_kwota, '', 'fld:@PB.KW')
?};
_test_driver.click('btn:@OK')


\usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: usuwa dokument
::   WE: _a - obiekt testujący
::       _b - dane dokumentu
::       _c - nr w rejestrze
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_dane_dokument:=_b;
_rejestr:=_dane_dokument.KOD;
_nr_rej:=$_dane_dokument.NR;
_sym:=_dane_dokument.NK;
:: @WHEN
{? _rejestr<>'' & _nr_rej<>''
||
   {? _nr_rej<>'0'
   ||
      _test_driver.filter('@DOK.REJ.KOD', _rejestr, '@DOK.NR', _nr_rej, '@DOK.NK', _sym)
   ||
      _test_driver.filter('@DOK.REJ.KOD', _rejestr, '@DOK.NK', _sym)
   ?};
   _test_driver.click('fld:@DOK.NR(row=1)')
?};
_grayed:=_test_driver.execute( "DOK.actions_grayed(cur_win(1,1))" );
_test_driver.execute($('DOK.actions_grayed(\'C_REN4\',\''+_grayed+'\')'));
:: wycięcie podmenu
{! |? _poz1:=_grayed*'('
|!
_str1:=_poz1-1+_grayed;
_poz2:=_grayed*')';
_str2:=_poz2-_grayed;
_grayed:=_str1+_str2
!};
{? _grayed * 'W'>0
|| _test_driver.menu('Wycofaj księgowanie')
|| _test_driver.menu('Wycofaj')
?};
_test_driver.click('btn:btnDEL');
_test_driver.click('dbtn:Tak');
_test_driver.filter()


\wypelnij_dane_ogolne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Wypełnia zakładkę 'dane ogólne'
::   WE: _a - obiekt testujący
::       _b - tabela z danymi
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
{? _>1 & type_of(_b) = type_of(SYSLOG)
|| _dane_ogolne:=_b
|| return()
?};
:: @WHEN
:: Okno DOK_ADD1 tabeli POMOC
_test_driver.send(_dane_ogolne.KOD, '', 'fld:@POMOC.REJD.KOD');
{? _dane_ogolne.SYM<>''
|| _test_driver.click('fld:@POMOC.DZ.SYM');

   _test_driver.send(_dane_ogolne.SYM, '', 'fld:@POMOC.DZ.SYM')
?};
{? _dane_ogolne.NAZ<>''
|| _test_driver.click('fld:@POMOC.D.NAZ');
   _test_driver.send(_dane_ogolne.NAZ, '', 'fld:@POMOC.D.NAZ')
?};
{? _dane_ogolne.SYM_1<>''
|| _test_driver.click('fld:@POMOC.V.SYM');
   _test_driver.send(_dane_ogolne.SYM_1, '', 'fld:@POMOC.V.SYM')
?};
_test_driver.send('', '{F2}', '');
::Okno dokumentu
{? _dane_ogolne.NR<>0
|| _test_driver.click('fld:@DOK.NR');
   _test_driver.send($_dane_ogolne.NR, '', 'fld:@DOK.NR')
?};
_test_driver.click('fld:@DOK.NK');
{? _dane_ogolne.NK<>''
|| _test_driver.send(_dane_ogolne.NK, '', 'fld:@DOK.NK')
?};

{? _dane_ogolne.KOR_PRZY<>''
|| _test_driver.click('fld:@DOK.KOR_PRZY');
   _test_driver.send(_dane_ogolne.KOR_PRZY, '', 'fld:@DOK.KOR_PRZY')
?};
{? _dane_ogolne.KOR_OKR<>''
|| _test_driver.click('fld:@DOK.KOR_OKR');
   _test_driver.send(_dane_ogolne.KOR_OKR, '', 'fld:@DOK.KOR_OKR')
?};
{? _dane_ogolne.KOR_ZEW<>''
|| _test_driver.click('fld:@DOK.KOR_ZEW');
   _test_driver.send(_dane_ogolne.KOR_ZEW, '', 'fld:@DOK.KOR_ZEW')
?};
{? _dane_ogolne.KOR<>''
|| _test_driver.click('fld:@DOK.KOR');
   _test_driver.send(_dane_ogolne.KOR, '', 'fld:@DOK.KOR')
?};
{? _dane_ogolne.DOP<>''
|| _test_driver.click('fld:@DOK.DOP');
   _test_driver.send(_dane_ogolne.DOP, '', 'fld:@DOK.DOP')
?};
{? _dane_ogolne.DTW<>''
|| _test_driver.click('fld:@DOK.DTW');
    _test_driver.send(_dane_ogolne.DTW, '', 'fld:@DOK.DTW')
?};
{? _dane_ogolne.DTO<>''
|| _test_driver.click('fld:@DOK.DTO');
    _test_driver.send(_dane_ogolne.DTO, '', 'fld:@DOK.DTO')
?};
{? _dane_ogolne.SLSLU<>''
|| _test_driver.click('fld:@SLOSLU.SLSLU');
   _test_driver.send(_dane_ogolne.SLSLU, '', 'fld:@SLOSLU.SLSLU')
?};
{? _dane_ogolne.KOD_KH<>''
|| _test_driver.click('fld:@DOK.WYS.KOD');
   _test_driver.send(_dane_ogolne.KOD_KH, '', 'fld:@DOK.WYS.KOD');
   _test_driver.click('fld:@DOK.KH');

   {?  _test_driver.exist('dial:')
    & _test_driver.state('dial:').message='Brak pozycji w słowniku.'
   || _test_driver.click('dbtn:Wyświetl słownik');

      _test_driver.assert('', 'wnd:@SLO.ONE', 'title', '', 'type', 's');
      _test_driver.click('btn:aa2465262000000');
      _test_driver.assert('', 'wnd:@KH.WER_SLO', 'title', 'Kontrahenci', 'type', 's');
      _test_driver.filter('@KH.KOD', _dane_ogolne.KOD_KH);
      _test_driver.click('fld:@KH.KOD(row=1)');
      _test_driver.send('', '{ENTER}', '');
      _test_driver.filter('@SLO.KOD', _dane_ogolne.KOD_KH);
      _test_driver.click('fld:@SLO.KOD(row=1)');
      _test_driver.send('', '{ENTER}', '')
   ?}
?};
{? _test_driver.exist('fld:@DOK.SP_PL.KOD') & _test_driver.state('fld:@DOK.SP_PL.KOD').value=''
|| _test_driver.click('fld:@DOK.SP_PL.KOD');
   _test_driver.send('00014', '{ENTER}', 'fld:@DOK.SP_PL.KOD')
?};
{? _dane_ogolne.D3<>''
|| _test_driver.click('fld:@DOK.D3');
   _test_driver.send(_dane_ogolne.D3, '', 'fld:@DOK.D3')
?};
{? _dane_ogolne.SP_WYM<>''
||
   _test_driver.click('fld:@DOK.SP_WYM');
   {? _dane_ogolne.SP_WYM='T' | _dane_ogolne.SP_WYM='1'
   || _test_driver.send('checked', '', 'fld:@DOK.SP_WYM')
   || _test_driver.send('unchecked', '', 'fld:@DOK.SP_WYM')
   ?}
?};
{? _dane_ogolne.KB_3R<>''
|| _test_driver.click('fld:@RACHBANK.KB_3R');
   _test_driver.send(_dane_ogolne.KB_3R, '', 'fld:@RACHBANK.KB_3R')
?};
{? _dane_ogolne.KB_2R<>''
|| _test_driver.click('fld:@RACHBANK.KB_2R');
   _test_driver.send(_dane_ogolne.KB_2R, '', 'fld:@RACHBANK.KB_2R')
?};
{? _dane_ogolne.KOD_3<>''
|| _test_driver.click('fld:@DOK.WAL.KOD');
   _test_driver.send(_dane_ogolne.KOD_3, '', 'fld:@DOK.WAL.KOD')
?};
{? _dane_ogolne.KOD_4<>''
|| _test_driver.click('fld:@DOK.BANK.KOD');
   _test_driver.send(_dane_ogolne.KOD_4, '', 'fld:@DOK.BANK.KOD')
?};
{? _dane_ogolne.SYM_3<>''
|| _test_driver.click('fld:@DOK.TKRS.SYM');
   _test_driver.send(_dane_ogolne.SYM_3, '', 'fld:@DOK.TKRS.SYM')
?};
{? _dane_ogolne.TYPKRS<>''
|| _test_driver.click('fld:@DOK.TYPKRS');
   _test_driver.send(_dane_ogolne.TYPKRS, '', 'fld:DOK.TYPKRS')
?};
{? _dane_ogolne.SUMWAL<>0
|| _test_driver.click('fld:@DOK.SUMWAL');
   _test_driver.send($_dane_ogolne.SUMWAL, '', 'fld:@DOK.SUMWAL')
?};
{? _dane_ogolne.TR<>''
|| _test_driver.click('fld:@DOK.TR');
   _test_driver.send(_dane_ogolne.TR, '', 'fld:@DOK.TR')
?};
{? _dane_ogolne.NAZ_1<>''
|| _test_driver.click('fld:@DOK.DODSLU.NAZ');
   _test_driver.send(_dane_ogolne.NAZ_1, '', 'fld:@DOK.DODSLU.NAZ')
?};
{? _dane_ogolne.KOD_5<>''
|| _test_driver.click('fld:@DOK.DODSLO.KOD');
   _test_driver.send(_dane_ogolne.KOD_5, '', 'fld:@DOK.DODSLO.KOD')
?};
{? _test_driver.state('fld:@DOK.NK').value=''
|| _test_driver.click('fld:@DOK.NK');
::   _test_driver.send(exec('sym_dok','fks_auto'), '', 'fld:@DOK.NK')
   _test_driver.send(_test_driver.state('fld:@DOK.NR').value, '', 'fld:@DOK.NK')
?};
{? _test_driver.exist('fld:@DOK.D4') & _test_driver.state('fld:@DOK.D4').value='0000/00/00'
|| _test_driver.click('fld:@DOK.D4');
   {? _dane_ogolne.D4<>''
   || _test_driver.send(_dane_ogolne.D4,'','fld:@DOK.D4')
   || _test_driver.click('fld:@DOK.NK');
      {? _test_driver.exist('dial:')
         & _test_driver.state('dial:').message='Data nie może być wcześniejsza od daty wystawienia.'
      ||
         _test_driver.click('dbtn:OK');
         _test_driver.send($date(),'','fld:@DOK.D4')
      ?}
   ?}
?}


\wypelnij_pozycje_zak_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Wypełnia pozycje dokumentu
::   WE: _a - obiekt testujący
::       _b - tabela z danymi
::       _c - tabela z danymi ogólnymi
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
{? _>1 & type_of(_b) = type_of(SYSLOG)
|| _dane_poz:=_b
|| return()
?};
_dane_ogolne:=_c;
:: @WHEN
{? _test_driver.exist('fld:@VPOZ.WCLO')
|| {? _dane_poz.WCLO<>0
   || _test_driver.click('fld:@VPOZ.WCLO');
      _test_driver.send($_dane_poz.WCLO,'','fld:@VPOZ.WCLO')
   ?}
?};
{? _test_driver.exist('fld:@VPOZ.CLO')
|| {? _dane_poz.CLO<>0
   || _test_driver.click('fld:@VPOZ.CLO');
      _test_driver.send($_dane_poz.CLO,'','fld:@VPOZ.CLO')
   ?}
?};
{? _test_driver.exist('fld:@VPOZ.AKCYZA')
|| {? _dane_poz.AKCYZA<>0
   || _test_driver.click('fld:@VPOZ.AKCYZA');
      _test_driver.send($_dane_poz.AKCYZA,'','fld:@VPOZ.AKCYZA')
   ?}
?};
_test_driver.click('fld:@VPOZ.NETTO');
_test_driver.send($_dane_poz.NETTO,'','fld:@VPOZ.NETTO');
_test_driver.click('fld:@SKID.PR');
{? _dane_poz.PR<>''
   || _test_driver.send(_dane_poz.PR,'','fld:@SKID.PR')
?};
{? _dane_poz.VAT<>0
|| _test_driver.click('fld:@VPOZ.VAT');
   _test_driver.send($_dane_poz.VAT,'','fld:@VPOZ.VAT')
?};
{? _dane_poz.BRUTTO<>0
|| _test_driver.click('fld:@VPOZ.BRUTTO');
   _test_driver.send($_dane_poz.BRUTTO,'','fld:@VPOZ.BRUTTO')
?};
{? _dane_poz.KOD<>''
|| _test_driver.click('fld:@VPOZ.GRVAT.KOD');
   {? _dane_poz.KOD<>'ZakuPodZ'
   || _test_driver.send(_dane_poz.KOD,'','fld:@VPOZ.GRVAT.KOD')
   ||
      _test_driver.send('ZakuPodZ', '{ENTER}', 'fld:@VPOZ.GRVAT.KOD');
      {? _test_driver.exist('dial:') & _test_driver.state('dial:').message='Brak pozycji w słowniku.'
      || exec('add_zakupodz','$dokumenty_ksiegowe',_test_driver,_dane_ogolne);
         _test_driver.click('dbtn:Powtórz')
      ?}
   ?}
?};
{? _dane_poz.VAT_ODL<>0
|| _test_driver.click('fld:@VPOZ.VAT_ODL');
   _test_driver.send($_dane_poz.VAT_ODL,'','fld:@VPOZ.VAT_ODL')
?};
{? _dane_poz.SP_WYM<>''
||
   {? _dane_poz.SP_WYM='T' | _dane_poz.SP_WYM='W'
   || _test_driver.click('fld:@VPOZ.SP_WYM(ndx=1)')
   |? _dane_poz.SP_WYM='Z'
   || _test_driver.click('fld:@VPOZ.SP_WYM(ndx=2)')
   |? _dane_poz.SP_WYM='D' | _dane_poz.SP_WYM='N'
   || _test_driver.click('fld:@VPOZ.SP_WYM(ndx=3)')
   ?}
?};
{? _dane_poz.R<>''
|| _test_driver.click('fld:@VPOZ.R');
   _test_driver.send(_dane_poz.R,'','fld:@VPOZ.R')
?};
{? _dane_poz.KK<>''
|| _test_driver.click('fld:@VAT7.KK');
   {? _dane_poz.KK='T' | _dane_poz.KK='1'
   || _test_driver.send('checked', '', 'fld:@VAT7.KK')
   || _test_driver.send('unchecked', '', 'fld:@VAT7.KK')
   ?}
?};
{? _dane_poz.KK_1<>''
|| _test_driver.click('fld:@VPOZ.KK');
   _test_driver.send(_dane_poz.KK_1,'','fld:@VPOZ.KK')
?};
{? _dane_poz.K2<>''
|| _test_driver.click('fld:@VAT7.K2');
   {? _dane_poz.K2='T' | _dane_poz.K2='1'
   || _test_driver.send('checked', '', 'fld:@VAT7.K2')
   || _test_driver.send('unchecked', '', 'fld:@VAT7.K2')
   ?}
?};
{? _dane_poz.K2_1<>''
||_test_driver.click('fld:@VPOZ.K2');
   _test_driver.send(_dane_poz.K2_1,'','fld:@VPOZ.K2')
?};
{? _dane_poz.KOS<>0
|| _test_driver.click('fld:@VPOZ.KOS');
   _test_driver.send($_dane_poz.KOS,'','fld:@VPOZ.KOS')
?};
{? _dane_poz.OP<>''
|| _test_driver.click('fld:@VPOZ.OP');
   _test_driver.send(_dane_poz.OP,'','fld:@VPOZ.OP')
?}


\przypadki_testowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.14]
:: OPIS: metoda wykonująca dodatkowe przypadki testowe
::   WE: _a - obiekt testujący
::       _b - tabela danymi dokumentu
::       _c - [STRING] kod przypadku testowego
::       _d - zakładka pulpitu
::       _e - zakładka dokumentów księgowych

::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
{? var_pres('_b') & type_of(_b) = type_of(SYSLOG)
|| _dane_ogolne:=_b
|| return()
?};
{? var_pres('_c') & type_of(_c) = type_of('')
|| _przypadek:=_c
?};
_pulpit:=_d;
_app_dokumenty_ks:=_e;
:: @WHEN
{? _przypadek='sp1'
||
   _test_driver.menu('Pozycje');
   _test_driver.assert('', 'wnd:poz_vsel', 'title', 'Pozycje VAT', 'type', 's');
   _test_driver.click('gtab:@Dekret');


   _spr:="_suma:=0;
   {? POZ.size()>0 & POZ.first
   ||
      {! |?
         _suma+=POZ.SP_V;
         POZ.next()
      !}
   ?};
   _suma=VPOZ.VAT";
   _wynik:=_test_driver.execute(_spr);
   {? _wynik
   || _test_driver.logger.info('POPRAWNY WYNIK TESTU SPLIT PAYMENT. ID DOKUMENTU:' + $_dane_ogolne.ID)
   || _test_driver.fail('BŁĘDNY WYNIK TESTU SPLIT PAYMENT. ID DOKUMENTU' + $_dane_ogolne.ID)
   ?};
   _test_driver.send('', '{ESC}', '')
|? _przypadek='bsp2'
||
   _test_driver.app_activate(_pulpit);
   _test_driver.control('!desktop', 'open', 'Wspólne\\Obszary\\Użytkownicy, role, uprawnienia', 7);
   _app1:=_test_driver.app_wait_for_change();
   _test_driver.assert('', 'wnd:@USERS.CALL', 'title', 'Użytkownicy systemu', 'type', 's');
   _test_driver.click('btn:USERS_UPR');
   _test_driver.assert('', 'wnd:@#upr_role_all', 'title', 'Role systemu', 'type', 's');
   _test_driver.filter('@B_ROLE.NAME', 'Księgowy');
   _test_driver.click('fld:@B_ROLE.NAME(row=1)');
   _test_driver.click('wnd:@#upr_usr_all');
   _test_driver.click('fld:@USERS.KOD(row=1)');

   _size:=_test_driver.execute("USERS.f_size");
   {? _size>0
   || _test_driver.filter('@USERS.KOD', 'admin');
      _test_driver.click('fld:@USERS.KOD(row=1)')
   ?};
::   zapisujemy uprawnienia admina
   _ksiegowy:=~(_test_driver.state('fld:@USERS.KOD(row=1)').value='admin');
   _dane_ogolne.ROBOCZE:='KSIĘGOWY->ADMIN='+$_ksiegowy;
   _dane_ogolne.put();
   {? ~_ksiegowy
   || _test_driver.click('wnd:@#upr_usr_role>btn:@<<<')
   ?};
   _test_driver.send('', '{ESC}', '');
   _test_driver.send('', '{ESC}', '');
   _test_driver.app_activate(_app_dokumenty_ks)
|? _przypadek='sp2'
||
   _test_driver.app_activate(_pulpit);
   _test_driver.control('!desktop', 'open', 'Finanse\\Obszary\\Rozrachunki', 7);
   _app1:=_test_driver.app_wait_for_change();
   _test_driver.assert('', 'wnd:@OP.WER2', 'title', 'Rozrachunki', 'type', 's');
   _test_driver.menu('Parametry pracy');
   _test_driver.assert('', 'wnd:@PARSES.#parsesdom', 'title', 'Parametry pracy', 'type', 'e');
   _test_driver.click('fld:@OKRESY.PAR_OKR');
   _test_driver.send('', '{F3}', 'fld:@OKRESY.PAR_OKR');
   _test_driver.assert('', 'wnd:@OKRO.WYB_OKRO', 'title', 'Okresy', 'type', 's');
   _test_driver.send($_dane_ogolne.ROK, '{ENTER}', '');
   _test_driver.send('', '{ENTER}', '');
   _test_driver.send('1', '', 'fld:@PARSES.ODD_ZAK');
   _test_driver.click('fld:@OKRESY.PAR_OKR');
   _test_driver.send(_dane_ogolne.OKRES, '{ENTER}', 'fld:@OKRESY.PAR_OKR');
   _test_driver.send('1', '{F2}', 'fld:@PARSES.ODD_ZAK');
   _test_driver.filter('@OP.AN', '200-02-00001', '@OP.SYM', '12.51/1807/0048 AAA');
   _test_driver.click('fld:@OP.SYM(row=1)');
   _test_driver.menu('Rozlicz');
   _test_driver.assert('', 'wnd:@OPTMP.PAR', 'title', 'Rozliczenie rozrachunku', 'type', 's');
   _test_driver.filter('@OPTMP.SYM', '12.51/1807/0048 BBB');
   _test_driver.click('fld:@OPTMP.SYM_ZEW(row=1)');
   _test_driver.menu('Rozlicz całkowicie');
   _test_driver.click('btn:btnZAKROZL');
   _test_driver.click('dbtn:Akceptuj');
   _test_driver.click('wnd:zap_op_rozl');
   _test_driver.click('fld:@ZAP_OP.WN(row=1)');
   exec('on_error','$lib_base',_test_driver);
   _test_driver.logger.info(_test_driver.state('fld:@ZAP_OP.SP_V_MA(row=1)').value);
   _test_driver.assert('', 'fld:@ZAP_OP.SP_V_MA(row=1)', 'value', '230,00');
   _test_driver.logger.info(_test_driver.state('fld:@ZAP_OP.SP_V_WN(row=1)').value);
   _test_driver.assert('', 'fld:@ZAP_OP.SP_V_WN(row=1)', 'value', '0,00');
   _test_driver.logger.info(_test_driver.state('fld:@ZAP_OP.WN(row=1)').value);
   _test_driver.assert('', 'fld:@ZAP_OP.WN(row=1)', 'value', '0,00');
   _test_driver.logger.info(_test_driver.state('fld:@ZAP_OP.MA(row=1)').value);
   _test_driver.assert('', 'fld:@ZAP_OP.MA(row=1)', 'value', '1.230,00');
   _test_driver.win_action('restore');
   exec('on_error','$lib_base',_test_driver);
   _test_driver.send('', '{DOWN}', '');
   _test_driver.click('fld:@ZAP_OP.WN(row=1)');
   _test_driver.logger.info(_test_driver.state('fld:@ZAP_OP.SP_V_MA(row=1)').value);
   _test_driver.assert('', 'fld:@ZAP_OP.SP_V_MA(row=1)', 'value', '0,00');
   _test_driver.logger.info(_test_driver.state('fld:@ZAP_OP.SP_V_WN(row=1)').value);
   _test_driver.assert('', 'fld:@ZAP_OP.SP_V_WN(row=1)', 'value', '0,00');
   _test_driver.logger.info(_test_driver.state('fld:@ZAP_OP.WN(row=1)').value);
   _test_driver.assert('', 'fld:@ZAP_OP.WN(row=1)', 'value', '0,00');
   _test_driver.logger.info(_test_driver.state('fld:@ZAP_OP.MA(row=1)').value);
   _test_driver.assert('', 'fld:@ZAP_OP.MA(row=1)', 'value', '-123,00');
   _test_driver.win_action('maximize');
   exec('on_error','$lib_base',_test_driver);
   _test_driver.click('wnd:@OP.WER2');
   _test_driver.click('fld:@OP.SYM(row=1)');
   _test_driver.menu('Historia rozliczeń:Przeglądanie rozliczeń');
   _test_driver.assert('', 'wnd:@PAR_NAG.PAR_WER', 'title', 'Rozliczenia rozrachunków', 'type', 's');
   _test_driver.filter('@PAR_NAG.OP', '12.51/1807/0048 AAA');
   _test_driver.click('fld:@PAR_NAG.OP(row=1)');
   _test_driver.menu('Anuluj akceptację');
   _test_driver.click('dbtn:Tak');
   _test_driver.menu('Usuń');
   _test_driver.click('dbtn:Tak');
   _test_driver.send('', '{ESC}', '');
   _test_driver.app_close(_app1);
   _dane_ogolne.cntx_psh();
   _dane_ogolne.prefix('21',);
   _ksiegowy:=_dane_ogolne.ROBOCZE;
   _dane_ogolne.cntx_pop();
::   przywracamy uprawnienia admina jeśli je zmienialiśmy
   {? _ksiegowy='KSIĘGOWY->ADMIN=0'
   ||
      _test_driver.app_activate(_pulpit);
      _test_driver.control('!desktop', 'open', 'Wspólne\\Obszary\\Użytkownicy, role, uprawnienia', 7);
      _app1:=_test_driver.app_wait_for_change();
      _test_driver.assert('', 'wnd:@USERS.CALL', 'title', 'Użytkownicy systemu', 'type', 's');
      _test_driver.click('btn:USERS_UPR');
      _test_driver.assert('', 'wnd:@#upr_role_all', 'title', 'Role systemu', 'type', 's');
      _test_driver.filter('@B_ROLE.NAME', 'Księgowy');
      _test_driver.click('wnd:@#upr_usr_role');
      _test_driver.filter('@B_USRROL.USERS.KOD', 'admin');
      _test_driver.click('fld:@B_USRROL.USERS.KOD(row=1)');
      {? _test_driver.state('fld:@B_USRROL.USERS.KOD(row=1)').value='admin'
      ||
         _test_driver.click('btn:@^>^>^>');
         _test_driver.click('dbtn:Tak')
      ?};
      _test_driver.send('', '{ESC}', '');
      _test_driver.send('', '{ESC}', '');
      _app1:=_test_driver.app_wait_for_change()
   ?};
   _test_driver.app_activate(_app_dokumenty_ks)
|? _przypadek='bsp3' | _przypadek='asp3'
||
   _procent:={? _przypadek='bsp3' || '43' || '100' ?};
   _test_driver.app_activate(_pulpit);
   _test_driver.control('!desktop', 'open', 'Wspólne\\Obszary\\Ustawienia i parametryzacja', 7);
   _app2:=_test_driver.app_wait_for_change();
   _test_driver.assert('', 'wnd:@param', 'title', 'Parametryzacja', 'type', 's');
   _test_driver.click('fld:@tmp.NAZWA(row=1)');

   _test_driver.click('fld:@tmp.NAZWA(row=3;tree)');
   _test_driver.click('fld:@tmp.NAZWA(row=5)');
   {? _test_driver.state('fld:@tmp.NAZWA(row=5)').value<>'Struktury księgowe'
   ||
      _drzewo:=obj_new('title','branch_el');
      _drzewo.title:='Parametryzacja';
      _drzewo.branch_el:=obj_new(1);
      _drzewo.branch_el[1]:=obj_new('kod','search_value');
      _drzewo.branch_el[1].kod:='NAZWA';
      _drzewo.branch_el[1].search_value:='Struktury księgowe';
      _dodatkowy_war:='';
      exec('znajdz_w_drzewie','$lib_base',_test_driver,_drzewo,_dodatkowy_war)
   ?};
   _test_driver.menu('Wybierz');
   _test_driver.assert('', 'wnd:@ROK_F.rok_fwerk', 'title', 'Lata bilansowe', 'type', 's');
   _test_driver.send($_dane_ogolne.ROK, '{ENTER}', '');
   _test_driver.menu('Popraw');
   _test_driver.assert('', 'wnd:@ROK_F.POP1', 'title', 'Rok bilansowy', 'type', 'e');
   _test_driver.click('fld:@ROK_F.PROC_VAT');
   _test_driver.send(_procent, '{TAB}', 'fld:@ROK_F.PROC_VAT');
   _test_driver.send('', '{F2}', '');
   _test_driver.send('', '{ESC}', '');
   _test_driver.app_close(_app2);
   _test_driver.app_activate(_app_dokumenty_ks)
|? _przypadek='sp3'
||
   _test_driver.filter('@DOK.REJ.KOD', 'ZAK_KRAJ', '@DOK.NK', '12.51/1809/0049');
   _test_driver.click('fld:@DOK.NK(row=1)');
   _test_driver.menu('Pozycje');
   _test_driver.assert('', 'wnd:poz_vsel', 'title', 'Pozycje VAT', 'type', 's');
   _test_driver.click('gtab:@Dekret');
   _test_driver.filter('@POZ.KON', '206-02-00002');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='211,3'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 211,3') ?};
   _test_driver.filter('@POZ.KON', '224');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='0'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 0') ?};
   _test_driver.filter('@POZ.KON', '200-02-00001');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='230'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 230') ?};
   _test_driver.filter('@POZ.KON', '310-CHAN');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='0'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 0') ?};
   _test_driver.filter('@POZ.KON', '301');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='0'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 0') ?};
   _test_driver.send('', '{ESC}', '');
   _test_driver.filter();
   _test_driver.click('fld:@DOK.REJ.KOD(row=1)')
|? _przypadek='sp4'
||
   _test_driver.assert('', 'wnd:fks_dok_rej', 'title', 'Dokumenty księgowe', 'type', 's');
   _test_driver.filter('@DOK.NK', '12.51/1807/0034 KOR');
   _test_driver.filter('@DOK.REJ.KOD', 'ZAK_KRAJ', '@DOK.NK', '12.51/1807/0034 KOR');
   _test_driver.click('fld:@DOK.NK(row=1)');
   _test_driver.menu('Pozycje');
   _test_driver.assert('', 'wnd:poz_vsel', 'title', 'Pozycje VAT', 'type', 's');
   _test_driver.click('gtab:@Dekret');
   _test_driver.filter('@POZ.KON', '200-02-00001', '@POZ.WN', '7461,99');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='552,74'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 552,74') ?};
   _test_driver.filter('@POZ.KON', '200-02-00001', '@POZ.MA', '7461,99');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='552,74'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 552,74') ?};
   _test_driver.filter('@POZ.KON', '200-02-00001', '@POZ.MA', '-7461,99');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='-552,74'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: -552,74') ?};
   _test_driver.filter('@POZ.KON', '224');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='0'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 0') ?};
   _test_driver.filter('@POZ.KON', '301');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='0'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 0') ?};
   _test_driver.send('', '{ESC}', '');
   _test_driver.filter();
   _test_driver.click('fld:@DOK.NK(row=1)')
|? _przypadek='sp5'
||
   _test_driver.filter('@DOK.REJ.KOD', 'ZAK_KRAJ', '@DOK.NK', '12.51/1807/0034 KOR2');
   _test_driver.click('fld:@DOK.NK(row=1)');
   _test_driver.menu('Pozycje');
   _test_driver.assert('', 'wnd:poz_vsel', 'title', 'Pozycje VAT', 'type', 's');
   _test_driver.click('gtab:@Dekret');
   _test_driver.filter('@POZ.KON', '200-02-00001', '@POZ.WN', '1149,45');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='85,14'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 85,14') ?};
   _test_driver.filter('@POZ.KON', '200-02-00001', '@POZ.MA', '1149,45');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='85,14'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 85,14') ?};
   _test_driver.filter('@POZ.KON', '200-02-00001', '@POZ.MA', '-1149,45');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='-85,14'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: -85,14') ?};
   _test_driver.filter('@POZ.KON', '224');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='0'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 0') ?};
   _test_driver.filter('@POZ.KON', '301');
   _test_driver.click('fld:@POZ.KON(row=1)');
   {? _test_driver.execute("$POZ.SP_V")='0'
   || _test_driver.logger.info('POPRAWNA WARTOŚĆ POZ.SP_V')
   || _test_driver.fail('BŁĘDNY WYNIK POZ.SP_V. POZ.SP_V='+_test_driver.execute("$POZ.SP_V")+'. OCZEKIWANA: 0') ?};
   _test_driver.send('', '{ESC}', '');
   _test_driver.filter();
   _test_driver.click('fld:@DOK.REJ.KOD(row=1)')
?}


\add_zakupodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.14]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_test_driver:=_a;
_dane_ogolne:=_b;
_test_driver.control('!leftmenu', 'open', 'Wspólne\\Obszary\\Ustawienia i parametryzacja', 1);
_app1:=_test_driver.app_wait_for_change();
_test_driver.assert('', 'wnd:@param', 'title', 'Parametryzacja', 'type', 's');
_test_driver.click('gtab:@Wykonanie');
exec('dokumenty_split_payment','$test_uzup_param',_test_driver,$_dane_ogolne.ROK);
_test_driver.app_close(_app1)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:12:17 8ea865b5d7c80b96ef8395a7749cf468673b83f6e9d8e59a0be858b06378eb1e87c6f8db14b9c7ceeed78cd46ac1c1186227dbca2459539b77f827cc5dc7f4d0719654bf617ad98b7a37d6a244dfde52dc1f440643ff5354517eab8e435336eeefc46a29b802a58868325e0fd42dc899a56a5a4aff9b3848ae17025aa7c11365

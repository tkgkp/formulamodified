:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: $uzytkownicy_role_uprawnienia.fml
:: Utworzony: 23.04.2019
:: Autor: ARTSLO
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================
\bf_usr_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: OPIS: bufor na ustawienia uprawnień dla użytkowników
::----------------------------------------------------------------------------------------------------------------------
return(tab_tmp(1,
'USER','STRING[10]','nazwa użytkownika jTerm',
'UPR','STRING[100]','Dział uprawnień',
'ODD','STRING[10]','Oddział',
'KOD','STRING[20]','Kod/Symbol',
'NAZWA','STRING[100]','Nazwa',
'RACH','STRING[51]','Numer rachunku',
'BANK','STRING[80]','Nazwa banku'))


\bf_usr_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: OPIS: bufor na ustawienia uprawnień użytkowników do schematów danych
::----------------------------------------------------------------------------------------------------------------------
return(tab_tmp(1,
'USER','STRING[10]','nazwa użytkownika jTerm',
'OSYMBOL','STRING[20]','Symbol obszaru',
'OOPIS','STRING[40]','Opis obszaru',
'TSYMBOL','STRING[20]','Symbol typu',
'TOPIS','STRING[40]','Opis typu',
'KOD','STRING[20]','Kod jednostki'))


\cfg_upr_dan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ARTSLO [19.22]
:: OPIS: Konfiguracja zawierająca informacje o symbolu i tytule okna dla konkretnego typu uprawnień
::   WY: tabela zawierająca konfigurację
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,
'NAZWA','STRING[40]','Nazwa uprawnienia do danych',
'OKNO','STRING[40]','Symbol elementu okna',
'TITLE','STRING[40]','Tytuł okienka');
_tab.NAZWA:=_tab.TITLE:='Jednostki księgowe'; _tab.OKNO:='wnd:@tmp.#odduprusr'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Rachunki bankowe'; _tab.OKNO:='wnd:@tmp.#rbuprusr'; _tab.add();
_tab.NAZWA:='Rodzaje przelewów'; _tab.OKNO:='wnd:@tmp.#ztluprusr'; _tab.TITLE:='Typy przelewów'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do oddziałów'; _tab.OKNO:='wnd:@tmp.#userupoddz'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do magazynów'; _tab.OKNO:='wnd:@tmp.#userup__mg'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do stanowisk sprzedaży'; _tab.OKNO:='wnd:@tmp.#userup_sts'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do stanowisk zakupu'; _tab.OKNO:='wnd:@tmp.#userup_stz'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do magazynów realizujących zamówienia sprzedaży'; _tab.OKNO:='wnd:@tmp.#userup__zam'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do magazynów realizujących zamówienia wewnętrzne'; _tab.OKNO:='wnd:@tmp.#userup__zaw';  _tab.add();
_tab.NAZWA:='Uprawnienia do form współpracy'; _tab.OKNO:='wnd:@USERS_FZ.F_ZATR'; _tab.TITLE:='Formy współpracy'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do stanowisk kasowych'; _tab.OKNO:='wnd:@tmp.#userup__kas'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do multikas'; _tab.OKNO:='wnd:@tmp.#userup__kas'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do cenników sprzedaży'; _tab.OKNO:='wnd:@tmp.#userup__tars'; _tab.add();
_tab.NAZWA:=_tab.TITLE:='Uprawnienia do cenników dostawców'; _tab.OKNO:='wnd:@tmp.#userup__tard'; _tab.add();
return(_tab)


\wybierz_uzytkownika
::----------------------------------------------------------------------------------------------------------------------
::   UTW: ARTSLO [19.22]
::  OPIS: Formuła do ustawienia się na wybranym uzytkowniku w głównym oknie obszaru użytkownicy, role, uprawnienia
::        uwaga ustawia filtr podręczny
::  AREA: [ZWS_USR]
:: TABLE: [USERS]
::    WE: _a - wskazanie na obiekt testowy
::        _b [STRING] - nazwa uzytkownika w jTerm
::    WY: obiekt zawierający informacje czy udało się ustawić na użytkowniku i czy jest on aktywny
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_test_driver.logger.info('START WYWOŁANIE: \\wybierz_uzytkownika/$uzytkownicy_role_uprawnienia.fml (time: '+$date()+' '+(time()$3)+')');
_symbol:=_b;
_wyn:=obj_new('PASSED','ACTIVE');

:: @WHEN
_test_driver.assert('', 'wnd:@USERS.CALL', 'title', 'Użytkownicy systemu', 'type', 's');
_test_driver.filter('@USERS.KOD', _symbol);
_test_driver.click('fld:@USERS.KOD(row=1)');

:: @THEN
_wyn.PASSED:=_test_driver.assert_true('Niezgadza się symbol',_test_driver.state('fld:@USERS.KOD(row=1)').value=_symbol);
{? _wyn.PASSED
|| _wyn.ACTIVE:=_test_driver.state('fld:@USERS.AKT(row=1)').value='checked'
|| _wyn.ACTIVE:=0
?};
_test_driver.logger.info('KONIEC WYWOŁANIE: \\wybierz_uzytkownika/$uzytkownicy_role_uprawnienia.fml (time: '+$date()+' '+(time()$3)+')');
return(_wyn)


\dodaj_uprawnienie
::----------------------------------------------------------------------------------------------------------------------
::   UTW: ARTSLO [19.22]
::  OPIS: Formuła dodająca uprawnienie(dowolne poza schematami danych) dla obecnie wybranego w oknie użytkownika systemu
::  AREA: [ZWS_USR]
:: TABLE: [B_PERM], [USERDEP], [UPR_RB], [UPR], [USERS_UP], [USERS_FZ], [KUSERUPR], [MKASAUPR], [TARUP]
::    WE: _a - wskazanie na obiekt testowy
::        _b - wskazania na obiekt zawierający dane wprowadzaniego uprawnienia
::    WY: [STRING] - pusty gdy zakończono zadanie powodzeniem / treść co poszło nie tak
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_test_driver.logger.info('START WYWOŁANIE: \\dodaj_uprawnienie/$uzytkownicy_role_uprawnienia.fml (time: '+$date()+' '+(time()$3)+')');
_test_driver.sa_reset_error();
_msg:='';
_win_cfg:=exec('cfg_upr_dan','$uzytkownicy_role_uprawnienia');

:: @WHEN

::ustalenie konfiguracji (symbol i tytuł okna) dla wybranego typu uprawnień
_ind:=_win_cfg.ndx_tmp(,1,'NAZWA',,);
_win_cfg.index(_ind);
_win_cfg.prefix(_b.UPR);
{? ~_win_cfg.first()
|| _test_driver.fail('Nie znaleziono konfiguracji okna dla wybranego uprawnienia do danych');
   return('Nie znaleziono konfiguracji okna dla - '+_b.USER+';'+_b.UPR)
?};

::ustawienie się w oknie typów uprawnień i wywołanie okna do ich nadania
_test_driver.click('wnd:@#userperm3');
_test_driver.assert('', 'wnd:@#userperm3', 'title', 'Uprawnienia do danych');
_test_driver.filter('@tmp.DESC', _b.UPR);
_test_driver.click('fld:@tmp.DESC(row=1)');
_test_driver.assert_true('',_test_driver.state('fld:@tmp.DESC(row=1)').value<>'');
{? _test_driver.state('fld:@tmp.DESC(row=1)').value<>''
|| _test_driver.click('btn:@Ustaw');
   {? _test_driver.exist('dial:') & _test_driver.exist('dbtn:Wybranych')
   || _test_driver.click('dbtn:Wybranych')
   ?};
:: sprawdzenie czy przycisk Akceptuje jest dostepny, definiuje czy mozna wogóle jakieś uprawnienie nadać
:: dla wszystkich typów po za formami współpracy
   {? {? _b.UPR<>'Uprawnienia do form współpracy' || _test_driver.state('btn:@Akceptuj').enable='true' || 1 ?}
   || _test_driver.assert('', _win_cfg.OKNO, 'title', _win_cfg.TITLE, 'type', 's');

:: w zależności od typu uprawnienia różny filtr i asercja na znalezienie rekordu
      {? _b.UPR='Rachunki bankowe'
      || _test_driver.filter('@tmp.RB', _b.RACH, '@tmp.NB', _b.BANK);
         _test_driver.click('fld:@tmp.NB(row=1)');
         _test_driver.assert('','fld:@tmp.RB(row=1)','value',_b.RACH);
         _test_driver.assert('','fld:@tmp.NB(row=1)','value',_b.BANK)
      |? _b.UPR='Rodzaje przelewów' |
         _b.UPR='Uprawnienia do oddziałów' |
         _b.UPR='Uprawnienia do stanowisk kasowych' |
         _b.UPR='Uprawnienia do multikas' |
         _b.UPR='Uprawnienia do cenników sprzedaży' |
         _b.UPR='Uprawnienia do cenników dostawców' |
         _b.UPR='Jednostki księgowe'
      || _test_driver.filter('@tmp.KOD', _b.KOD, '@tmp.OPIS', _b.NAZWA);
         _test_driver.click('fld:@tmp.OPIS(row=1)');
         _test_driver.assert('','fld:@tmp.KOD(row=1)','value',_b.KOD);
         _test_driver.assert('','fld:@tmp.OPIS(row=1)','value',_b.NAZWA)
      |? _b.UPR='Uprawnienia do magazynów' |
         _b.UPR='Uprawnienia do stanowisk sprzedaży' |
         _b.UPR='Uprawnienia do stanowisk zakupu' |
         _b.UPR='Uprawnienia do magazynów realizujących zamówienia sprzedaży' |
         _b.UPR='Uprawnienia do magazynów realizujących zamówienia wewnętrzne'
      || _test_driver.filter('@tmp.ODD', _b.ODD,'@tmp.KOD', _b.KOD, '@tmp.OPIS', _b.NAZWA);
         _test_driver.click('fld:@tmp.OPIS(row=1)');
         _test_driver.assert('','fld:@tmp.ODD(row=1)','value',_b.ODD);
         _test_driver.assert('','fld:@tmp.KOD(row=1)','value',_b.KOD);
         _test_driver.assert('','fld:@tmp.OPIS(row=1)','value',_b.NAZWA)
      |? _b.UPR='Uprawnienia do form współpracy'
      || _test_driver.filter('@USERS_FZ.F_ZATR.KOD', _b.KOD, '@USERS_FZ.F_ZATR.OPIS', _b.NAZWA);
         _test_driver.click('fld:@USERS_FZ.F_ZATR.OPIS(row=1)');
         _test_driver.assert('','fld:@USERS_FZ.F_ZATR.KOD(row=1)','value',_b.KOD);
         _test_driver.assert('','fld:@USERS_FZ.F_ZATR.OPIS(row=1)','value',_b.NAZWA)
      ?};
:: sprawdzamy czy Przycisk Nadaj jest aktywny i czy dla tego typu występuje przycisk Akceptuj
      {? _test_driver.state('menu:Nadaj').enable='true'
      || _test_driver.menu('Nadaj');
         {? _test_driver.exist('btn:@Akceptuj')
         || _test_driver.click('btn:@Akceptuj');
            {? _b.UPR='Uprawnienia do stanowisk kasowych' & _test_driver.exist('btn:@Akceptuj')
            || _test_driver.send('', '{ESC}', '')
            ?}
         || _test_driver.send('', '{ESC}', '')
         ?}
      || _test_driver.send('', '{ESC}', '');
         {? _test_driver.exist('dial:') & _test_driver.state('dial:').message='Czy zakończyć nadawania uprawnień?\n(Wprowadzone zmiany będą anulowane).'
         || _test_driver.click('dbtn:Tak')
         ?}
      ?}
   || _test_driver.fail('Brak rekordów do których można by nadać uprawnienia. Przycisk Akceptuj jest nieaktywny.')
   ?}
?};

:: @THEN

:: jeśli po drodze wystąpił błąd poprzez asercję tworzymy komunikat dla jakiego uprawnienia się to wydarzyło
{? ~_test_driver.sa_is_error()=0
|| _msg+=_b.USER+';'+'UPR';
   {? _b.ODD <>'' || _msg+=';'+_b.ODD ?};
   {? _b.KOD <>'' || _msg+=';'+_b.KOD ?};
   {? _b.NAZWA <>'' || _msg+=';'+_b.NAZWA ?};
   {? _b.RACH <>'' || _msg+=';'+_b.RACH ?};
   {? _b.BANK <>'' || _msg+=';'+_b.BANK ?}
?};
_test_driver.logger.info('KONIEC WYWOŁANIE: \\dodaj_uprawnienie/$uzytkownicy_role_uprawnienia.fml (time: '+$date()+' '+(time()$3)+')');
return(_msg)


\nadaj_upr_sch
::----------------------------------------------------------------------------------------------------------------------
::   UTW: ARTSLO [19.22]
::  OPIS: Formuła dodająca uprawnienie w schematach danych dla obecnie wybranego w oknie użytkownika systemu
::  AREA: [ZWS_USR]
:: TABLE: [B_PREM], [UDB_GRP], [UDB_SYS], [UD_DEF], [UD_POM], [UDB_UPR]
::    WE: _a - wskazanie na obiekt testowy
::        _b - wskazania na obiekt zawierający dane wprowadzaniego uprawnienia
::    WY: [STRING] - pusty gdy zakończono zadanie powodzeniem / treść co poszło nie tak
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_test_driver.logger.info('START WYWOŁANIE: \\nadaj_upr_sch/$uzytkownicy_role_uprawnienia.fml (time: '+$date()+' '+(time()$3)+')');
_test_driver.sa_reset_error();
_msg:='';

:: @WHEN

::ustawienie się odpowiednim oknie i wybranie odpowiedniego typu uprawnienia
_test_driver.click('wnd:@#userperm3');
_test_driver.assert('', 'wnd:@#userperm3', 'title', 'Uprawnienia do danych');
_test_driver.filter('@tmp.DESC', 'Uprawnienia do schematów danych');
_test_driver.click('fld:@tmp.DESC(row=1)');
{? _test_driver.assert_true('',_test_driver.state('fld:@tmp.DESC(row=1)').value<>'')
|| _test_driver.click('btn:@Ustaw');
   {? _test_driver.exist('dial:') & _test_driver.exist('dbtn:Wybranych')
   || _test_driver.click('dbtn:Wybranych')
   ?};
:: wybranie odpowiedniego obszaru
    _test_driver.assert('', 'wnd:@UDB_GRP.NAW', 'title', 'Obszary', 'type', 's');
    _test_driver.filter('@UDB_GRP.SYMBOL', _b.OSYMBOL, '@UDB_GRP.OPIS', _b.OOPIS);
    _test_driver.click('fld:@UDB_GRP.SYMBOL(row=1)');
    _test_driver.assert('', 'fld:@UDB_GRP.SYMBOL(row=1)', 'value', _b.OSYMBOL);
    _test_driver.assert('', 'fld:@UDB_GRP.OPIS(row=1)', 'value', _b.OOPIS);
:: wybranie odpowiedniego typu schematu
    {? _test_driver.sa_is_error()=0
    || _test_driver.click('wnd:@UDB_SYS.NAW');
       _test_driver.filter('@UDB_SYS.UD_TYP.SYMBOL', _b.TSYMBOL, '@UDB_SYS.UD_TYP.OPIS', _b.TOPIS);
       _test_driver.click('fld:@UDB_SYS.UD_TYP.SYMBOL(row=1)');
       _test_driver.assert('', 'fld:@UDB_SYS.UD_TYP.SYMBOL(row=1)', 'value', _b.TSYMBOL);
       _test_driver.assert('', 'fld:@UDB_SYS.UD_TYP.OPIS(row=1)', 'value', _b.TOPIS);
:: znalezienie jednostki i nadanie uprawnienia do niej
       {? _test_driver.sa_is_error()=0
       || _test_driver.click('wnd:@UD_DEF.UDB_UPR');
          {? exec('szukaj_jd_org','$uzytkownicy_role_uprawnienia',_test_driver,'Dokładnie', _b.KOD)
          || {? _test_driver.state('btn:aa0592839000000').enable='true'
             || _test_driver.click('btn:aa0592839000000')
             ?};
             _test_driver.click('fld:@UD_POM.DOSTEP(row=1)');
             _test_driver.assert('Nie udało się nadać uprawnienia', 'fld:@UD_POM.DOSTEP(row=1)','value','checked');
             _test_driver.send('', '{ESC}', '')
          || _test_driver.send('', '{ESC}', '')
          ?}
       || _test_driver.send('', '{ESC}', '')
       ?}
    || _test_driver.send('', '{ESC}', '')
    ?}
?};

:: @THEN

:: jeśli po drodze wystąpił błąd poprzez asercję tworzymy komunikat dla jakiego uprawnienia się to wydarzyło
{? ~_test_driver.sa_is_error()=0
|| _msg+=_b.USER;
   {? _b.OSYMBOL <>'' || _msg+=';'+_b.OSYMBOL ?};
   {? _b.OOPIS <>'' || _msg+=';'+_b.OOPIS ?};
   {? _b.TSYMBOL <>'' || _msg+=';'+_b.TSYMBOL ?};
   {? _b.TOPIS <>'' || _msg+=';'+_b.TOPIS ?};
   {? _b.KOD <>'' || _msg+=';'+_b.KOD ?}
?};
_test_driver.logger.info('KONIEC WYWOŁANIE: \\nadaj_upr_sch/$uzytkownicy_role_uprawnienia.fml (time: '+$date()+' '+(time()$3)+')');
return(_msg)


\szukaj_jd_org
::----------------------------------------------------------------------------------------------------------------------
::   UTW: ARTSLO [19.22]
::  OPIS: Funkcja realizująca szukanie jednostki organizacyjnej poprzez akcje z menu górnego okna Struktury w
::        uprawnieniach do schematów danych
::  AREA: [ZWS_USR]
:: TABLE: [UD_DEF], [UD_SKL]
::    WE: _a - wskazanie na obiekt testowy
::        _b [STRING] - tryb wyszukiwania: Dokładnie/Kontekstowo/Dokładnie w rozwiniętych/Kontekstowo w rozwinietych
::        _c [STRING] - kod jednostki
::    WY: 1/0 - powodzenie operacji
::----------------------------------------------------------------------------------------------------------------------
:: @GIVEN
_test_driver:=_a;
_tryb:=_b;
_kod:=_c;
_wyn:=1;

:: @WHEN

:: sprawdzamy czy znajdujemy się we właściwym okienku
_test_driver.assert('', 'wnd:@UD_DEF.UDB_UPR', 'title', 'Struktura', 'type', 's');
{?  _test_driver.sa_is_error()=0
|| _test_driver.click('fld:@UD_DEF.UD_SKL.SYMBOL(row=1)');
:: rozwijam całą listę
   _test_driver.menu('Zwiń/rozwiń');
   {? _tryb='Dokładnie'
   || _test_driver.menu('Szukaj dokładnie');
      _test_driver.send('', '{F3}', 'fld:@UD_DEF.UD_SKL.SYMBOL');
      _test_driver.filter('@UD_SKL.SYMBOL', _kod);
      _test_driver.click('fld:@UD_SKL.SYMBOL(row=1)');
      _test_driver.assert('', 'fld:@UD_SKL.SYMBOL(row=1)','value', _kod);
      {? _test_driver.sa_is_error()=0
      || _test_driver.menu('wybierz');
         _test_driver.send('', '{F2}', '');
:: sprawdzamy czy wyszukiwanie się powiowdło, jesli nie pojawi się okno dialogowe
         {? _test_driver.exist('dial:') & _test_driver.state('dial:').title='Uwaga 412' &
            _test_driver.state('dial:').message='tabela: UD_DEF\n\nNie znaleziono rekordu zgodnego ze wzorcem.'
         || _test_driver.assert_true('Nie znaleziono wybranej jednostki w strukturze.',0);
            _test_driver.click('dbtn:OK');
            _wyn:=0
         || _wyn:=1
         ?}
      || _wyn:=0
      ?}
   |? _tryb='Kontekstowo'
   || _test_driver.assert_true('Tryb nie jest obsłuzony!',0)
   |? _tryb='Dokładnie w rozwiniętych'
   || _test_driver.assert_true('Tryb nie jest obsłuzony!',0)
   |? _tryb='Kontekstowo w rozwiniętych'
   || _test_driver.assert_true('Tryb nie jest obsłuzony!',0)
   ?}
|| _wyn:=0
?};

:: @THEN
::zwracamy wynik w postaci powodzenia szukania i braku błędów po drodze, zakładamy, że przy powodzeniu jesteśmy
:: ustawieni na odpowiednim liściu drzewa
return( _wyn & _test_driver.sa_is_error()=0)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:12:17 0fa0f56336ea6867b75ef4e92a8aac9505949c0bb0be52bbc1ff62e86f40a83d269b183141a17dd09235f02312f994d0641c29a4cc9deedcb3aa199dbace9695ed91aa952a2b3632bc25aa0cee53b50b1b70a78eaf922fcc12eb40a32d037425b6d6a3945062ff20048c3615ecbfee3e29b4237373dd292e8bfa933ee5b35052

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #system.fml
:: Utworzony: 30.11.2016
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Formuły do obsługi informacji o systemie
::======================================================================================================================

\wersja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.00]
:: OPIS: Wersja systemu
::----------------------------------------------------------------------------------------------------------------------
'23.25'


\nazwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Nazwa systemu (nie tłumaczymy)
::----------------------------------------------------------------------------------------------------------------------
'Merit ERP'


\producent
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Nazwa producenta systemu
::----------------------------------------------------------------------------------------------------------------------
'Asseco Business Solutions S.A.'


\shortname
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Krótka nazwa systemu (nie tłumaczymy)
::----------------------------------------------------------------------------------------------------------------------
'Merit'


\www
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Adres strony internetowej producenta systemu
::----------------------------------------------------------------------------------------------------------------------
'http://www.macrologic.pl/'


\server
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Adres i port AS
::   WY: obj_new('address','port')
:: ~OST: INODBCDSN
::----------------------------------------------------------------------------------------------------------------------
_asport:='5580';
_asaddress:='localhost';
{? exec('interm','#system')=0
||
   _dsn:=jdbc_dsn();
   _dsn_tab:=spli_str(_dsn,';');
   {! _it:=1.. obj_len(_dsn_tab)
   |! {? 7+_dsn_tab[_it]='ASPORT=' || _asport:=(7-_dsn_tab[_it])
      |? 10+_dsn_tab[_it]='ASADDRESS=' || _asaddress:=(10-_dsn_tab[_it])
      ?}
   !}
?};
exec('obj_ntab_set','#array',,'address',_asaddress,'port',_asport)


\link_uri
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.42]
:: OPIS: Opakowanie wywołania funkcji link_uri(params, [address], [group], [appl])
::   WE: _a DICT params - nazwana tablica z parametrami typu STRING
::                        można np. wygenerować za pomocą funkcji \obj_ntab_set/#array.fml
::       [_b] STRING address - adres zawierający protokół, nazwę serwera i port,
::                        np: mbase://localhost:5580 albo http://localhost:8888
::       [_c] STRING group - grupa z cluster.cfg
::       [_d] STRING appl - nazwa aplikacji z cluster.cfg
::   WY: STRING lub '' jeżeli nie udało się utworzyć link
::----------------------------------------------------------------------------------------------------------------------
_params:=_a;
_result:='';

:: Domyślnie link do protokołu mbase
{? var_pres('_b')=type_of('')
|| _address:=_b
|| exec('czytaj','#stalesys',,XINFO,'LINK_SRV','LINK_PRT');
   {? XINFO.LINK_SRV='' | XINFO.LINK_PRT=0
   || _server:=exec('server','#system');
      _address:='mbase://%1:%2'[_server.address,_server.port]
   || _address:='mbase://%1:%2'[XINFO.LINK_SRV,$XINFO.LINK_PRT]
   ?}
?};

{? var_pres('_c')=type_of('')
|| _group:=_c
|| _group:=app_info('cluster_group')
?};

{? var_pres('_d')=type_of('')
|| _appl:=_d
|| _appl:=REF.FIRMA().APP_IDEN
::   _appl:=app_info('app_ident')
?};

::_result:='mbase://'+_address+'/'+_group+'/'+_appl+'?';
::_names:=obj_ntab_names(_params);
::_inet:=inet_get('https://');
::{! _it:=1..obj_len(_names)
::|! _key:=_names[_it];
::   _value:=_inet.url_encode(($("_a."+_key))(_params));
::   _result+=_key+'='+_value+'&'
::!};
::_result:=_result-1;
{? _address<>''
|| _result:=link_uri(_params,_address,_group,_appl)
?};
_result


\runtime
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Formuła sprawdza, czy aplikacja została uruchomiona wskazaną lub nowszą wersją runtime'u.
::   WE:  _a  [NUMBER] - Kod wersji produkcyjnej (np. 18.02).
::        _b  [STRING] - Kod wersji rozwojowej (np. 'P.20').
::       [_c] [NUMBER] - Tryb wsadowy (bez komunikatów): 0*/1.
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0)
|| _vp:=_a
|| return(0)
?};
{? var_pres('_b')=type_of('')
|| _vr:=_b
|| return(0)
?};
_batch:=var_pres('_c')=type_of(0) & _c;

_run:=user(11);
{? #_run>=_vp | _run>=_vr
|| 1
|| {? ~_batch
   || FUN.info('Funkcjonalność wymaga MacroBASE co najmniej w wersji '+$_vp+'.')
   ?};
   0
?}


\runtime_p60
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.14]
:: OPIS: Sprawdza czy aplikacja jest uruchomiona runtime P.60
::   WY: 0 - nie
::       1 - tak
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
exec('runtime','#system',20,'P.60',1)


\runtime_dro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Sprawdza, czy uruchomiono wersją narzędzi dla DRO.
::   WY: 0/1 - wersja klienta/DRO
::----------------------------------------------------------------------------------------------------------------------
user(7)='PRAC000004'


\patch_level
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.14]
:: OPIS: Formuła zwraca numer zastosowanego patch-a
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('run_info')<0
|| 0
|| #run_info('patch_level')
?}


\base_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.14_05]
:: OPIS: Formuła zwraca identyfikator bazy danych
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('base_id')<0
|| ''
|? var_pres('SL_api')=type_of(@.CLASS.SLAPICLASS)
|| SL_api.base_id()
|| base_id()
?}


\cli_ver
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Sprawdza czy aplikacja jest uruchomiona w środowisku jterm lub interm
::   WY: STRING: 'jterm','interm','cgi','mbasic' itp
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=cli_ver();
{? _result='htmlclient'
|| _result:='interm'
?};
_result


\cli_functions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Sprawdza czy jest uruchomiony indevice i jest dostęp np do urządzeń i plików lokalnych
::   WY: 0/1
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? exec('interm','#system')>0
|| {? indevice('supported')
   || _result:=indevice('run')
   || _result:=0
   ?}
?};
_result


\interm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Sprawdza czy aplikacja jest uruchomiona w interm
::   WY: 0/1
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? cli_ver()='interm'
|| _result:=1
?};
_result


\interm_nacc_msg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Zwraca tekst komunikatu o niedostępności funkcji w środowisku inTerm.
::   WY: STRING - treść komunikatu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
'Funkcja nie jest dostępna w środowisku inTerm.'@


\indevice_nacc_msg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Zwraca tekst komunikatu o niedostępności indevice w środowisku inTerm.
::   WY: STRING - treść komunikatu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? indevice('supported')
|| 'Funkcja nie jest dostępna w środowisku inTerm bez inDevice.'@
|| 'Funkcja nie jest dostępna na tej platformie (brak inDevice).'@
?}


\runtime_r10
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Sprawdza czy aplikacja jest uruchomiona runtime R.10
::   WY: 0 - nie
::       1 - tak
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
exec('runtime','#system',21,'R.10',1)


\mobile
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Sprawdza czy aplikacja jest uruchomiona w trybie mobilnym
::   WY: 0 - nie mobilny
::       1 - mobilny
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? REF.MOBILE=''
|| REF.MOBILE:={? app_info('html_mobile') || 'T' || 'N' ?}
?};
_result:={? REF.MOBILE='T' || 1 || 0 ?};
_result


\base_id_manage
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Zarządzanie identyfikatorem bazy danych
::   WE: [_a] - kontekst wywołania: [0]-zarządzanie identyfikatorem, 1-zadanie transferowe
::   WY: 1/-1 - w przypadku uruchomienia jako zadania transferowego, ~~ - wpp
::----------------------------------------------------------------------------------------------------------------------
_context:={? var_pres('_a')=type_of(0) || _a || 0 ?};

:: W trybie "expert" dostępne resetowanie identyfikatora i wyświetlanie ścieżki do pliku technicznego
_expert:=0;

{? _context=1
|| _txt:=
      exec('base_id_info_transfer','#system');
   _choice:=FUN.choice(_txt,,'Dalej'@);
   {? _choice=0 || return(-1) ?}
?};

_tab:=tab_tmp(
   ,'BASE_ID','STRING[40]','Identyfikator bazy danych'@
   ,'STATUS','STRING[40]','Status identyfikatora'@
   ,'PTH','STRING[255]','Ścieżka do pliku z identyfikatorem'@
);
_tab.blank();
_tab.BASE_ID:=SL_api.base_id(1);
_tab.STATUS:=exec('base_id_status','#system');
_tab.PTH:=pth_dir('base_id');

_red:=_tab.mk_edit('Identyfikator bazy danych — zarządzanie'@,,'baseidred',,,'normal');
_tab.win_esep(_red,'Identyfikator bazy danych'@);
_tab.win_efld(_red,,'BASE_ID',,,,,1,'Bieżący'@);
_tab.win_efld(_red,XINFO,'BASE_ID',,,,,1,'Zapamiętany'@);
_tab.win_efld(_red,,'STATUS',,,,,1);
{? _expert || _tab.win_efld(_red,,'PTH',,,100,,1) ?};
_tab.win_edit(_red);

{? _expert
|| _formula:="
      _verify:=SL_api.verify_base_id();
      _remember:=0;
      _txt:=
         'Uwaga:\n\n'
         'Komunikacja z usługami zewnętrznymi, takimi jak Businesslink i Portal HR\n'
         'we wszystkich firmach korzystających ze wspólnej bazy danych jest obecnie wstrzymana.\n\n'
         'Aby odblokować komunikację należy zapamiętać bieżący identyfikator a następnie\n'
         'wykonać dla każdego ze zintegrowanych dzierżawców procedurę:\n'
         '- Ustaw identyfikator aplikacji klienta.'@;
      {? _verify>0
      || FUN.info('Zapamiętany identyfikator bazy jest zgodny z obecnie używanym.'@)
      |? _verify<0
      || _choice:=FUN.choice(
            'Błąd konfiguracji systemu, identyfikator bazy danych nie został jeszcze zapamiętany.'@+'\n\n'+_txt,,
            'Zapamiętaj bieżący'@
         );
         {? _choice=1 || _remember:=1 ?}
      || _choice:=FUN.choice(
            'Niezgodność zapamiętanego identyfikatora bazy danych: %1\nz bieżąco używanym: %2.'@
            [XINFO.BASE_ID,exec('base_id','#system')]+'\n\n'+_txt,,
            'Zapamiętaj bieżący'@
         );
         {? _choice=1 || _remember:=1 ?}
      ?};
      {? _remember
      || SL_api.save_base_id();
         _txt:='Trwa odświeżenie — wyłączenie kolejek na serwerze … 1/2'@;
         progress(30,_txt,'Odświeżenie środowiska'@,0);
         exec('cgi_enable_disable','#b_worker',0);
         _txt:='Trwa odświeżenie — włączenie kolejek na serwerze … 2/2'@;
         progress(60,_txt,'Odświeżenie środowiska'@,0);
         exec('cgi_enable_disable','#b_worker',1);
         prgs_clr();
         _tab.STATUS:=exec('base_id_status','#system');
         win_disp()
      ?};
      ''
   ";
   _tab.win_ebtn(_red,'text=%1,display=1,align=begin'['Weryfikuj identyfikator'@],_formula)
?};

{? _expert
|| _formula:="
      {? FUN.ask(
            'Czy zresetować identyfikator bazy danych?\n\n\n'
            'Uwaga:\n\n'
            'Reset identyfikatora spowoduje wstrzymanie komunikacji z usługami zewnętrznymi, '
            'takimi jak Businesslink i Portal HR\n'
            'we wszystkich firmach korzystających ze wspólnej bazy danych.\n\n'
            'Aby odblokować komunikację należy wykonać dla każdego ze zintegrowanych dzierżawców jedną z procedur:\n'
            '- Ustaw identyfikator aplikacji klienta.'@
         )
      || ferase('base_id',1);
         cur_tab(1,1).BASE_ID:=SL_api.base_id(1);
         _txt:='Trwa odświeżenie — wyłączenie kolejek na serwerze … 1/2'@;
         progress(30,_txt,'Odświeżenie środowiska'@,0);
         exec('cgi_enable_disable','#b_worker',0);
         _txt:='Trwa odświeżenie — włączenie kolejek na serwerze … 2/2'@;
         progress(60,_txt,'Odświeżenie środowiska'@,0);
         exec('cgi_enable_disable','#b_worker',1);
         prgs_clr();
         _tab.STATUS:=exec('base_id_status','#system');
         win_disp()
      ?};
      ''
   ";
   _tab.win_ebtn(_red,'text=%1,display=1,align=begin'['Resetuj identyfikator'@],_formula)
?};

_formula:="
   _tab:=cur_tab(1,1);
   {? XINFO.BASE_ID=_tab.BASE_ID
   || FUN.info('Prawidłowy identyfikator jest już zapamiętany.'@)
   || {? FUN.ask(
            'Czy zapamiętać bieżący identyfikator?\n\n'
            'Zmiany w komunikacji zostaną uwzględnione po restarcie systemu.\n'
            'Procesy kolejek zostaną automatycznie wyłączone i włączone.\n'@
         )
      || SL_api.save_base_id();
         _txt:='Trwa odświeżenie — wyłączenie kolejek na serwerze … 1/2'@;
         progress(30,_txt,'Odświeżenie środowiska'@,0);
         exec('cgi_enable_disable','#b_worker',0);
         _txt:='Trwa odświeżenie — włączenie kolejek na serwerze … 2/2'@;
         progress(60,_txt,'Odświeżenie środowiska'@,0);
         exec('cgi_enable_disable','#b_worker',1);
         prgs_clr();
         _tab.STATUS:=exec('base_id_status','#system');
         win_disp()
      ?}
   ?};
   ''
";
_tab.win_ebtn(_red,'text=%1,align=begin'['Zapamiętaj bieżący'@],_formula);

{? _context=1
|| exec('ok_esc','#window',_tab,_red,,,,,,,'Zadanie wykonane'@)
|| _formula:="
      _txt:=exec('base_id_info','#system');
      FUN.info(_txt);
      ''
   ";
   _tab.win_ebtn(_red,'text=%1,align=begin'['Informacja'@],_formula)
?};

{? _tab.edit() || _res:=1 || _res:=-1 ?};
{? _context=1 || _res || ~~ ?}


\base_id_status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Zwraca bieżący status identyfikatora bazy danych - opisowo
::----------------------------------------------------------------------------------------------------------------------
_verify:=SL_api.verify_base_id();
{? _verify>0
|| 'Zgodny'@
|? _verify<0
|| 'Nie zapamiętany'@
|| 'Niezgodny'@
?}


\base_id_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Treść informacji o identyfikatorze bazy i jego statusach
::----------------------------------------------------------------------------------------------------------------------
'Jeżeli identyfikator jest niezgodny to komunikacja z usługami zewnętrznymi,\n'
'takimi jak Businesslink i Portal HR we wszystkich firmach korzystających\n'
'ze wspólnej bazy danych będzie wstrzymana.\n\n'
'Aby odblokować komunikację należy zapamiętać bieżący identyfikator a następnie\n'
'wykonać dla każdego ze zintegrowanych dzierżawców następującą procedurę:\n'
'- Ustaw identyfikator aplikacji klienta (należy wpisać wartość skopiowaną z pola \'Bieżący\').'@


\base_id_info_transfer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Treść informacji o identyfikatorze bazy i jego statusach - wariant dla zadania transferowego
::----------------------------------------------------------------------------------------------------------------------
'Po wykonaniu transferu do nowej wersji systemu, o ile dane znajdują się obecnie\n'
'w innej lokalizacji, mógł się zmienić identyfikator bazy danych.'@+
'\n\n'+
exec('base_id_info','#system')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:13:35 ae1b8893790cc9c9b44cbd906bc763a9b23e0189158b4e2dcd682f5029cd4e1848e8909f6d1758bb15c95a070c2a279f5d77dc41f11c8105fb6b4427f2511b863a55f4890f8c1ad43cfd59179d76e44442a7f2fdd6fb17d7496e20d7fa6a8a967f4a7922a294a949d265a8c54d5196efd1c83535c1cb5400cb89b4bfe540abf4

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #help.fml [17.00]
:: Utworzony: 06.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Tymczasowe formuły do obsługi helpów
::======================================================================================================================


\help
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wywołuje help dla bieżącego ToDo
::   WE: Symbol czynności, obszaru, czynności raportującej
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || FUN.info('Brak parametru.'@); return(0) ?};
_a:=-_a;
_path:=exec('set_help','#help',_a);
exec('browser_launch','#help',_path)


\set_help
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zwraca kontekst pomocy wg podanego parametru
::   WE: _a - parametr z symbolem obszaru lub czynności, jeżeli pusty to zwraca adres głównej strony dokumentacji
::   WY: kontekst pomocy do wstawienia do instrukcji set_help()
::----------------------------------------------------------------------------------------------------------------------
{? +app_info('web_sesid') | XINFO.DOKUWIKI='' || exec('czytaj','#stalesys',,XINFO,'DOKUWIKI','DOKU_INT') ?};
{? exec('interm','#system') & XINFO.DOKU_INT<>''
|| _adres:=-(form(XINFO.DOKU_INT))
|| _adres:=-(form(XINFO.DOKUWIKI))
?};
_offline:=0;
{? _adres ='#disable' || return ('#disable') ?};
{? _adres='' ||  _adres:=exec('default_address','#help') ?};
{? 4+_adres='file'
|| 'adres offline';
   _offline:=1;
   {? _>0 & _a=''
   || _adres
   || _adres:=_adres-10;
      _adres+='dziedziny/';
      _a:=-_a;
      {? _>0 & _a*':'
      || _adres+gsub(_a,':','/')+'.html'
      || _adres+(3+(_a))+'/'+(_a)+'.html'
      ?}
   ?}
||
    _adres
?};
{? _offline | _adres+10='start.html'
||
   _offline:=1;
   {? _>0 & _a=''
   || _adres
   |? _>0 & _a*':'
   || _a:=-_a;
      _adres-=10;
      _adres+=gsub(_a,':','/')+'.html'
   || _a:=-_a;
      _adres-=10;
      _adres:=_adres+'dziedziny/'+(3+(_a))+'/'+(_a)+'.html'
   ?}
||
   {? ~(7+_adres='http://' | 8+_adres='https://') || _adres:='http://'+_adres ?};
   {? _adres+8<>'doku.php'
   || {? _adres+1<>'/' || _adres+='/' ?};
      {? _>0 & _a=''
      || _adres:=_adres+'doku.php'
      |? _>0 & _a*':'
      || _a:=-_a;
         _adres:=_adres+'doku.php?id='+_a
      || _a:=-_a;
        _adres:=_adres+'doku.php?id=dziedziny:'+(3+(_a))+':'+(_a)
      ?}
   || {? _>0 & _a=''
      || _adres
      |? _>0 & _a*':'
      || _a:=-_a;
         _adres:=_adres+'?id='+_a
      || _a:=-_a;
         _adres:=_adres+'?id=dziedziny:'+(3+(_a))+':'+(_a)
      ?}
   ?}
?};
{? ~_offline ||
   {? _adres*'?'>0 || _adres+='&' || _adres+='?' ?};
   _adres+='st='+exec('gen_token','#help',_adres)
?};
_adres


\str2hex
::----------------------------------------------------------------------------------------------------------------------
::  UTW: trocek [21.xx]
:: OPIS:
::   WE: _a - ciag tekstowy
::   WY: hex
::----------------------------------------------------------------------------------------------------------------------
_tab:='0123456789abcdef';
_hex:='';
{!  _idx:=1 .. +_a |!
    _znak:=%(1+(_idx-_a ));
    _h1:=_znak % 16;
    _h2:=_znak %* 16;
    _hex+=1+(_h1-_tab);
    _hex+=1+(_h2-_tab)
!};
_hex


\gen_token
::----------------------------------------------------------------------------------------------------------------------
::  UTW: bryq [22.26]
:: OPIS: generowanie tokena dostępowego do dokuwiki
::   WE: _a - adres dokuwiki
::   WY: string token
::----------------------------------------------------------------------------------------------------------------------

{? var_press('__dwtok')>0 ||  return (__dwtok) ?};
{? +app_info('web_sesid') | XINFO.DOKUWIKI='' || exec('czytaj','#stalesys',,XINFO,'DOKUWIKI') ?};
_str:=$(date~1)+form(date~2,-2)+form(date~3,-2)+'MeritRulezWiki'+$(int(1000*rand));
_h:=hash(_str,'sha1');
__dwtok:=base64('encode','token:'+_h);
__dwtok


\get_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Zwraca adres dokuwiki (dla kontrolek pulpitu)
::   WE: [_a] - jeśli podano to ścieżka do doklejenia
::       [_b] - jezeli podano to adres serwera dokumentacynego
::       [_c] - INTEGER - 0/1 - czy podczytywać #stalesys
::----------------------------------------------------------------------------------------------------------------------
_readstale:=1;
{? var_pres('_c')=type_of(0)
|| _readstale:=_c
?};
{? _<2 | _b=''
|| {? _readstale>0
   || exec('czytaj','#stalesys',,XINFO,'DOKUWIKI','DOKU_INT')
   ?};
   {? exec('interm','#system') & XINFO.DOKU_INT<>''
   || _adres:=-(form(XINFO.DOKU_INT))
   || _adres:=-(form(XINFO.DOKUWIKI))
   ?}
|| _adres:=_b
?};
{? _adres='#disable' || return ('#disable') ?};
_offline:=0;
{? _adres='' || _adres:=exec('default_address','#help') ?};

{? 4+_adres='file'
||
   _offline:=1;
   {? _>0 & _a<>''
   || _adres:=_adres-10;
      _adres:=_adres+'dziedziny/'+(3+(_a))+'/'+(_a)+'.html'
   || _adres
   ?}
|| _adres
?};
{? _offline | _adres+10='start.html'
||
   _offline:=1;
   {? _>0 & _a=''
   || _adres
   |? _>0 & _a*':'
   || _a:=-_a;
      _adres-=10;
      _adres+=gsub(_a,':','/')+'.html'
   || _a:=-_a;
      _adres-=10;
      _adres:=_adres+'dziedziny/'+(3+(_a))+'/'+(_a)+'.html'
   ?}
||
   {? ~(7+_adres='http://' | 8+_adres='https://') || _adres:='http://'+_adres ?};
   {? _adres+8<>'doku.php'
   || {? _adres+1<>'/' || _adres+='/' ?};
      {? _>0 & _a<>'' || _adres:=_adres+'doku.php?id='+_a || _adres:=_adres+'doku.php?id=' ?}
   || {? _>0 & _a<>'' || _adres:=_adres+'?id='+_a || _adres:=_adres+'?id=' ?}
   ?}
?};
{? ~_offline ||
   {? _>0 & _a<>'' || _adres+='&' ?};
   _adres+='st='+exec('gen_token','#help',_adres)
?};
_adres


\browser_launch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Uruchamia domyślną przeglądarkę w systemie na przekazanym adresie
::   WE: _a - STRING - adres strony WWW
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
{? +app_info('web_sesid')
||
   web_open_link(_path)
||
   cli_open_link(_path)
?};
~~


\default_address
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Zwraca domyślną stronę dla dokuwiki jeżeli system nie sparametryzowany
::   WY: STRING
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_wersja:=exec('wersja','#system');
{? _wersja*'RR.xx'>0
|| _wer:='17xx'
|| _wer:=gsub(_wersja,'.','')
?};
'https://dokuwiki.macrologic.pl/dokuwiki/!'+_wer+'/doku.php'


\co_nowego
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Zwraca adres dla przycisku Co nowego
::   WY: STRING
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_wersja:=exec('wersja','#system');

{? _wersja*'RR.xx'
||
:: Dopóki po stronie dokuwiki adres się nie zmieni to co emisja tu ręcznie poprawić
:: dla wersji rozwojowej
   _wersja:='23xx'
|| _wersja:=gsub(_wersja,'.','')
?};

_param:='dziedziny:zzmiany:'+_wersja+'_zmiany';
exec('set_help','#help',_param)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:13:35 2e45fd9069d5088c06ab3eb1823ae08d55a04b05a2e4dfbf66997d45718cd2456e5668635d02e968c6257928c15e778a9b5dcc53ab0b62238571047586fa01817a08a0a1ff29712db55334318979d9219f6fcc6d72884d785e0c30f9f186d70dac992bc8de3c4d295044a73637597673251be1556047a8eee791cf29f2e11431

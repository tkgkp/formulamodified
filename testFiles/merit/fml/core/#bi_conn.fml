:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #bi_conn.fml [17.00]
:: Utworzony: 22.11.2013
:: Autor: WH
::======================================================================================================================
:: Zawartosc: Formuły do obsługi tabeli BI_CONN - powiązania między instancjami elementów
::======================================================================================================================


\buffer
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [17.00]
:: OPIS: Tworzy bufor tabeli B_POCONN
::   WY: obj_new() - tablica nazwana - reprezentacja rekordu B_POCONN
::----------------------------------------------------------------------------------------------------------------------
exec('BI_CONN','#buffer')


\add
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [17.00]
:: OPIS: Dodaje do tabeli BI_CONN jeden rekord
::   WE: _a - obj_new - tablica nazwana będąca buforem tabeli exec('buffer','#bi_conn')
::   WY: BI_CONN.ref() lub null
::----------------------------------------------------------------------------------------------------------------------
_buffer:={? var_pres('_a')>100
         || _a
         || exec('buffer','#bi_conn')
         ?};
_result:=null();

BI_CONN.cntx_psh(); {? BI_CONN.name()='' || BI_CONN.use('bi_c____') ?};
BI_CONN.index('TO'); BI_CONN.clear();
BI_CONN.blank();
_buffer.set();
{? BI_CONN.add()>0
|| _result:=BI_CONN.ref()
?};
BI_CONN.cntx_pop();
_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Kasuje podany rekord tabeli BI_CONN (wykonywane w transakcji!!!)
::   WE: _a - BI_CONN.ref()
::   WY: >0 -wyczyszczone,
::      <=0 -niewyczyszczone
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
BI_CONN.cntx_psh(); {? BI_CONN.name()='' || BI_CONN.use('bi_c____') ?};
BI_CONN.index('TO'); BI_CONN.clear();
{? BI_CONN.seek(_ref)
|| {? BI_CONN.del(,1)>0
   || _result:=1
   || undo();
      _result:=-3
   ?}
|| _result:=0
?};
BI_CONN.cntx_pop();
{? _result<0
|| undo()
?};

{? _mydo || end() ?};
_result


\can_add
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [17.00]
:: OPIS: Sprawdza czy można dodać takiego BI_CONNa - czy nie ma już takiego w systemie
::   WE: _a - obj_new - tablica nazwana będąca buforem tabeli exec('buffer','#bi_conn')
::   WY: 0 - nie można dodawać
::       1 - można dodawać
::----------------------------------------------------------------------------------------------------------------------
_buffer:=_a;

_result:=1;

BI_CONN.cntx_psh(); {? BI_CONN.name()='' || BI_CONN.use('bi_c____') ?};
BI_CONN.index('BI_PROC');
BI_CONN.prefix(_buffer.BI_PROC,_buffer.FROM,_buffer.TO);
{? BI_CONN.size()>0
|| _result:=0
?};
BI_CONN.cntx_pop();
_result


\has_conn2prel
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [17.00]
:: OPIS: Sprawdza czy podana instancja elementu _a ma już powiązania do elementu procesu _b
::   WE: _a - BI_PREL.ref() - instancja elementu procesu który badam
::       _b - B_PREL.ref() - element procesu do którego powinny byc powiązania
::   WY: null() - brak instancji elementu _a który miałby juz powiązania do _b
::       BI_PREL.ref - instacja _a która ma powiązania do jakiejś instancji _b
::----------------------------------------------------------------------------------------------------------------------
_bi_prel:=_a;
_b_prel:=_b;

_result:=null();

BI_PREL.cntx_psh(); {? BI_PREL.name()='' || BI_PREL.use('bi_e____') ?};
BI_PREL.index('TM_BORN'); BI_PREL.clear();

BI_CONN.cntx_psh(); {? BI_CONN.name()='' || BI_CONN.use('bi_c____') ?}; BI_CONN.index('TO');

{? BI_PREL.seek(_bi_prel)
|| BI_CONN.prefix(BI_PREL.BI_PROC,BI_PREL.ref());

:: Iteruje po polączeniach wpadających do BI_PRELa
   {? BI_CONN.first()
   || {!
      |?
::       Sprawdzam czy polączenie łączy do B_PRELa którego chcę sprawdzić
         BI_PREL.cntx_psh();
         _check:=0;
         {? $BI_CONN.FROM().B_PREL=$_b_prel
         || _check:=1
         ?};
         BI_PREL.cntx_pop();

         {? _check>0
         || _result:=BI_PREL.ref()
         ?};
         BI_CONN.next() & _result=null()
      !}
   ?}
?};
BI_CONN.cntx_pop();
BI_PREL.cntx_pop();
_result


\bi_conn_berec
::----------------------------------------------------------------------------------------------------------------------
:: DOST: MBUILDER
::  UTW: WH [17.00]
:: OPIS: Akcja przed rekord w oknach wertowania - ustawia wartości zmiennych itp
::----------------------------------------------------------------------------------------------------------------------
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 15:58:44 ce86646064a865f7feb383e7607651af3a534aff03caee7d8b60ed878bfc566e356b57b1331811d89a3af583927422cb2604e16049950ad633dfd6fdeeaf02bccf9de67d6c7a67ef5353383a996a6f7095eb76146a9db351cad466275085a265117812ae6dc5030a4286f46cf79b6ea22764d986b317b24c773723eba7275203

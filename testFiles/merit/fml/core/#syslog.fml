:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #syslog.fml
:: Utworzony: 02.06.2015
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły do obsługi dziennika systemu.
::======================================================================================================================


\zmiany
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Formuła dla bieżącego rekordu aktywnego okna prezentuje historię jego zmian.
::   WE: _a [TABLE] - alias tabeli, z której pochodzi rekord [domyślnie: tabela bieżącego okna].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_TAB:={? var_pres('_a')<>type_of(SYSLOG) || cur_tab(1,1) || _a ?};
_id_acr:=_TAB.idadd_acr();

_query:=
   'select '
   '  L.IDADD as IDSYS, '
   '  cast(L.IDADD as DATE_TYPE) as DATA, '
   '  cast(L.IDADD as TIME_TYPE) as CZAS, '
   '  case '
   '     when L.COMMAND=\'A\' then \'3\' '
   '     when L.COMMAND=\'P\' then \'2\' '
   '     when L.COMMAND=\'D\' then \'1\' '
   '     else \'0\' '
   '  end as NUMER, '
   '  L.MUTATCOD as KTO, '
   '  L.COMMAND as KOD, '
   '  case '
   '     when L.COMMAND=\'A\' then \'%1\' '
   '     when L.COMMAND=\'P\' then \'%2\' '
   '     when L.COMMAND=\'D\' then \'%3\' '
   '     else \'?\' '
   '  end as OPIS, '
   '  L.DBF as PLIK, '
   '  L.REFERENCE as SQLREF '
   'from @SYSSLG as L '
   ['Utworzenie'@,'Modyfikacja'@,'Usunięcie'@];
_order:=
   'order by IDSYS desc';

{? _id_acr<>''
|| _LOG:=sql(
      _query+'where L.IDSRC=\':_a\' '+_order,
      ($'_a.%1'[_id_acr])(_TAB)
   )
|| _LOG:=sql(
      _query+' where L.RECNO=:_a and L.DBF=\':_b\' '+_order,
      ($'#_a.ref()')(_TAB),($'ref_name(_a.ref())')(_TAB)
   )
?};

_wnd:=_LOG.mk_sel('Zmiany'@,'T',0,'sysslg_wer',,,,,'U');
_LOG.win_fld(_wnd,,'DATA',,,-10,,,'Data'@,,'Data zdarzenia'@);
_LOG.win_fld(_wnd,,'CZAS',,,-10,,,'Godzina'@,,'Godzina zdarzenia'@);
_LOG.win_fld(_wnd,,'KTO',,,-10,,,'Użytkownik'@,,'Identyfikator użytkownika'@);
_LOG.win_fld(_wnd,,'OPIS',,,-20,,,'Operacja'@,,'Opis operacji'@);
_LOG.win_act(_wnd,,'Formuła','Szczegół&y'@@,,,
   $'exec(\'szczegoly\',\'#syslog\',cur_tab(1,1).SQLREF,\'%1\')'[!_TAB],,1,,,,'Y'
);
_LOG.win_act(_wnd,,'Szukaj');
_LOG.win_act(_wnd,,'Kolejność');
_LOG.win_sel(_wnd);

_wnd:=_LOG.mk_edit('Zmiana'@,0,'sysslg_red');
_LOG.win_esep(_wnd,'Dane podstawowe'@);
_LOG.win_efld(_wnd,,'DATA',,,10,,,'Data'@,,'Data zdarzenia'@);
_LOG.win_efld(_wnd,,'CZAS',,,25,,,'Godzina'@,,'Godzina zdarzenia'@);
_LOG.win_efld(_wnd,,'KTO',,,25,,,'Użytkownik'@,,'Identyfikator użytkownika'@);
_LOG.win_efld(_wnd,,'KOD',,,25,,,'Kod'@,,'Kod operacji'@);
_LOG.win_efld(_wnd,,'OPIS',,,25,,,'Opis'@,,'Opis operacji'@);
exec('ok_esc','#window',_LOG,_wnd);
_LOG.win_edit(_wnd);

_LOG.select();
obj_del(_LOG);
~~


\szczegoly
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Wyświetla szczegółowe informacje o modyfikacji.
::   WE: _a [SQLREF] - tekstowa reprezentacja wskazania wiersza w tabeli SYSSLG.
::       _b [STRING] - akronim tabeli, której modyfikacja jest prezentowana.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_LOG:=sysslg_pos(exec('ref_sysslg','#syslog',_a));

_loop:=_LOG.first();
{!
|? _loop
|! {? _LOG.TYPE='REFERENCE'
   || _LOG.BSTRING:=exec('record','#to_string',_LOG.BSTRING);
      _LOG.ASTRING:=exec('record','#to_string',_LOG.ASTRING);
      _LOG.put()
   ?};
   _loop:=_LOG.next()
!};
_LOG.first();

_wnd:=_LOG.mk_sel('Szczegóły'@,'T',0,'sysslg_pos',,,,,'U');
_LOG.win_fld(_wnd,,'FACR',,,-10,,,'Pole'@,,'Akronim pola'@);
_LOG.win_fld(_wnd,POLA_GRP,'TXT_1',,,-20,,,'Opis'@,,'Nazwa pola'@);
_LOG.win_fld(_wnd,,'BSTRING',,,-40,,,'Wartość przed zmianą'@,,'Wartość pola przed zmianą'@);
_LOG.win_fld(_wnd,,'ASTRING',,,-40,,,'Wartość po zmianie'@,,'Wartość pola po zmianie'@);
_LOG.win_act(_wnd,,'Wyświetl',,,,$'exec(\'wyswietl\',\'#syslog\',cur_tab(1,1),\'%1\')'[_b]);
_LOG.win_act(_wnd,,'Rekord',,,,$'POLA_GRP.TXT_1:=exec(\'fldLabel\',\'#field\',\'%1\',cur_tab(1,1).FACR);~~'[_b]);
_LOG.win_sel(_wnd);

_LOG.select();
obj_del(_LOG);
~~


\wyswietl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Prezentacja szczegółów zmian w rekordzie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wnd:=_a.mk_edit('Szczegóły'@,0);
_fld:=exec('fldLabel','#field',_b,_a.FACR);

_prec:=0;
_bfld:='BSTRING';
_afld:='ASTRING';
{? _a.TYPE='SYS_MEMO'
|| _bfld:='BSYSMEM';
   _afld:='ASYSMEM';
   _prec:=-5
?};

_a.win_esep(_wnd,{? _fld='' || 'Pole %1'@[_fld] || _fld ?});
_a.win_efld(_wnd,,_bfld,,,40,_prec,,'Wartość przed zmianą'@,,'Wartość pola przed zmianą'@);
_a.win_efld(_wnd,,_afld,,,40,_prec,,'Wartość po zmianie'@,,'Wartość pola po zmianie'@);

_a.cntx_psh();
_a.win_edit(_wnd);
_a.display();
_a.cntx_pop();
_a.win_edel(_wnd);
~~


\ref_sysslg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.28]
:: OPIS: Zwraca wskazanie wiersza tabeli SYSSLG.
::   WE: _a [SQLREF] - tekstowa reprezentacja wskazania wiersza w tabeli SYSSLG.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? (var_pres('_a')<>type_of('') & +_a<>16) | ref_tab(_a)<>SYSSLG
|| return(null)
?};

_ref:=null();
SYSSLG.cntx_psh();
SYSSLG.use(ref_name(_a));
SYSSLG.prefix();
{? SYSSLG.seek(_a)
|| _ref:=SYSSLG.ref()
?};
SYSSLG.cntx_pop();
_ref

:Sign Version 2.0 jowisz:1045 2023/09/06 09:13:11 5a8a61e057d5d21f044f9fa29ac2d3ac18ee6e1e0c2625b14faf7de8b660be80520213981e0ebff7cc4ed69d00c4d90c89322575b69074250589a951e23e10024100047ff472f353c887198615d764618dd8f0dea5ce28cebba8dfa6ab93ce9d94b87d0325d2e48ffcf5f79787b7a3e5e533053d62a403aabafcaa107a58f383

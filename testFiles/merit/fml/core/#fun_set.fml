:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #fun_set.fml [21.37]
:: Utworzony: 15.07.2021
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Definicja obiektu obsługującego zestaw formuł dostępnych do uruchomienia.
::======================================================================================================================


\obj_decl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Deklaruje klasę ułatwiającą zarządzanie dostępnymi formułami.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('FUN_SET',@.CLASS)>0
|| return()
?};

obj_decl('FUN_SET',
:: tabela formuł
   obj_fld('TAB',~~),
:: oryginalne parametry
   obj_fld('par',~~),

:  inicjalizacja obiektu
   obj_meth('__init',"
      .par:=
         {? var_pres('_b')<100
         || params_get()
         || _b
         ?};
      .TAB:=exec('create_tab','#fun_set')
   "),

:: wyszukuje element o podanym symbolu:
::    _a STRING - symbol elementu
::    _b INTEGER - nie wyświetlaj komunikatu (<>0)
   obj_meth('find_sym',"
      _symbol:=_a;
      _silent:=(var_pres('_b')=type_of(0) & _b<>0);
      _ref:=null;
      {? _symbol<>''
      || .TAB.cntx_psh();
         .TAB.prefix();
         {? .TAB.find_tab(,'SYMBOL',,'=',_symbol)
         || _ref:=.TAB.ref()
         || {? ~_silent
            || FUN.emsg('Element o symbolu \"%1\" nie istnieje.'@[_symbol])
            ?}
         ?};
         .TAB.cntx_pop()
      ?};
      _ref
   ",type_of('')),

:: dodaje grupę formuł:
::    _a STRING - nazwa grupy
::    _b STRING - symbol grupy formuł
::    _c [STRING] - komentarz, zawartość grupy
::    _d [STRING] - symbol zapisu nadrzędnego
   obj_meth('add_grp',"
      exec('add_grp','#fun_set',
         .TAB,
         {? var_pres('_d')=type_of('') || _d || '' ?},
         _a,
         {? var_pres('_c')=type_of('') || _c || '' ?},
         _b
      );
      (.)
   ",type_of(''),type_of('')),

:: dodaje formułę:
::    _a STRING - nazwa formuły
::    _b RULE - formuła do wywołania
::    _c [STRING] - komentarz, opis działania
::    _d [RULE/STRING] - dostępność do uruchomienia
::    _e [STRING] - identyfikator grupy formuł
::    _f [STRING] - identyfikator elementu
   obj_meth('add_fml',"
      exec('add_fml','#fun_set',
         .TAB,
         {? var_pres('_e')=type_of('') || _e || '' ?},
         _a,
         {? var_pres('_c')=type_of('') || _c || '' ?},
         {? var_pres('_f')=type_of('') || _f || '' ?},
         _b,
         {? var_pres('_d')=type_of('') | var_pres('_d')=type_of($'') || _d || 'T' ?}
      );
      (.)
   ",type_of(''),type_of("")),

:: ustala formułę dynamicznego określania dostępności:
::    _a STRING - identyfikator elementu
::    _b RULE - formuła ustalająca dostępność
   obj_meth('acc_fml',"
      _symbol:=_a;
      _rule:=_b;
      {? _symbol<>''
      || _ref:=.find_sym(_symbol);
         {? .TAB.seek(_ref)
         || .TAB.ACC_MOD:='T';
            .TAB.memo_set($_rule,'ACC_FML');
            .TAB.put() & .TAB.memo_put(,'ACC_FML')
         ?}
      ?};
      (.)
   ",type_of(''),type_of("")),

:: uruchomienie wywoływacza formuł
::    _a [STRING] - tytuł okienka wyboru
   obj_meth('run',"
      _hdr:=
         {? var_pres('_a')<>type_of('')
         || 'Funkcje'@
         || _a
         ?};
      exec('run','#fun_set',.,_hdr)
   ")
)


\acc_group
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Formuła ustalająca dostępność grupy.
::   WE: _a TABLE - alias tabeli zestawu funkcji
::       _b STRING - aktualna wartość znacznika ACC_VAL
::   WY: wartość znacznika ACC_VAL [T/N]
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;

_TAB.cntx_psh();
_TAB.prefix();
_test:=_TAB.find_tab(,'PARENT',,'=',#_TAB.ref(),'ACC_VAL',,'=','T');
_TAB.cntx_pop();

{? _test<>0 || 'T' || 'N' ?}


\add_fml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Tworzy zapis w tabeli formuł - formuła.
::   WE: _a TABLE - alias tabeli formuł
::       _b INTEGER/STRING - wiersz nadrzędny (numer/symbol)
::       _c STRING - nazwa formuły
::       _d STRING - komentarz, opis działania
::       _e STRING - symbol identyfikujący
::       _f RULE - formuła do wykonania
::       _g RULE/STRING - dostępność do uruchomienia
::   WY: wynik operacji zapisu
::----------------------------------------------------------------------------------------------------------------------
exec('add_row','#fun_set',_a,_b,'F',_c,_d,_e,_f,_g)


\add_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Tworzy zapis w tabeli formuł - grupa.
::   WE: _a TABLE - alias tabeli formuł
::       _b INTEGER/STRING - wiersz nadrzędny (numer/symbol)
::       _c STRING - nazwa grupy
::       _d STRING - komentarz, opis zawartości
::       _e STRING - symbol identyfikujący
::   WY: wynik operacji zapisu
::----------------------------------------------------------------------------------------------------------------------
exec('add_row','#fun_set',_a,_b,'G',_c,_d,_e,"","exec('acc_group','#fun_set',_a,_b)")


\add_row
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Wstawia wiersz z podanymi wartościami do tabeli formuł.
::   WE: _a TABLE - alias tabeli
::       _b INTEGER/STRING - wiersz nadrzędny (numer/symbol)
::       _c STRING - rodzaj zapisu: G - grupa, F - formuła
::       _d STRING - nazwa elementu
::       _e STRING - komentarz, opis elementu
::       _f STRING - symbol identyfikujący
::       _g RULE - formuła do wykonania
::       _h RULE/STRING - dostępność elementu
::   WY: wynik operacji dodania wiersza i zapisu pól notatnikowych
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;
_parent:=_b;
_level:=0;
_type:=_c;
_name:=_d;
_desc:=_e;
_rule:=_g;
_symbol:=_f;
_access:=_h;

_TAB.prefix();
_TAB.cntx_psh();
{? type_of(_parent)=type_of('')
|| {? _parent=''
   || _parent:=0
   |? _TAB.find_tab(,'SYMBOL',,'=',_parent)
   || _parent:=#_TAB.ref()
   ?}
?};
{? _TAB.seek(_parent,)
|| _level:=_TAB.LEVEL+1
?};
_TAB.cntx_pop();

_TAB.blank(1);
_TAB.PARENT:=_parent;
_TAB.NUMBER:=_a.size()+1;
_TAB.LEVEL:=_level;
_TAB.TYPE:=_type;
_TAB.NAME:=_name;
_TAB.memo_set(_desc,'DESC');
_TAB.SYMBOL:=_symbol;
_TAB.memo_set($_rule,'EXE_FML');
{? type_of(_access)=type_of("")
|| _TAB.ACC_MOD:='T';
   _TAB.ACC_VAL:='';
   _TAB.memo_set($_access,'ACC_FML')
|? type_of(_access)=type_of('')
|| _TAB.ACC_MOD:='N';
   _TAB.ACC_VAL:=_access;
   _TAB.memo_set('','ACC_FML')
?};

_TAB.add() &
_TAB.memo_put(,'DESC') &
_TAB.memo_put(,'EXE_FML')&
_TAB.memo_put(,'ACC_FML')


\create_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Tworzy tabelę tymczasową do przechowywania informacji o zestawie formuł.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_TAB:=tab_tmp(1,
   'PARENT', 'TREE_REF',   'Zapis nadrzędny'@,
   'NUMBER', 'INTEGER',    'Liczba porządkująca'@,
   'LEVEL',  'INTEGER',    'Poziom'@,
   'TYPE',   'STRING[1]',  'Rodzaj'@,
   'NAME',   'STRING[255]','Nazwa'@,
   'DESC',   'SYS_MEMO',   'Opis'@,
   'SYMBOL', 'STRING[32]', 'Symbol'@,
   'EXE_FML','SYS_MEMO',   'Formuła'@,
   'ACC_MOD','STRING[1]',  'Zmiana'@,
   'ACC_VAL','STRING[1]',  'Dostęp'@,
   'ACC_FML','SYS_MEMO',   'Formuła'@
);

:: zablokuj wstawianie i podsumowanie kolumn
_TAB.fld_attr(,2,2);

_TAB


\run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Udostępnia zestaw formuł.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_obj:=_a;
_hdr:=_b;

_TAB:=_a.TAB;
_TAB.cntx_psh();
_TAB.prefix();

:: ustal poziomy elementów
_TAB.f_set('NUMBER',,'PARENT<>0');
_loop:=_TAB.f_first();
{!
|? _loop
|! _level:=0;
   _TAB.cntx_psh();
   {? _TAB.seek(_TAB.PARENT,)
   || _level:=_TAB.LEVEL+1
   ?};
   _TAB.cntx_pop();
   {? _level<>0
   || _TAB.LEVEL:=_level;
      _TAB.put()
   ?};
   _loop:=_TAB.f_next()
!};

:: wstępnie ustal dostępność elementów
_TAB.f_set('LEVEL^',,'ACC_MOD=\'T\'');
_loop:=_TAB.f_last();
{!
|? _loop
|! exec('update_acc','#fun_set',_TAB);
   _loop:=_TAB.f_prev()
!};

_TAB.f_clear();
_TAB.cntx_pop();

:: ustaw oryginalne parametry
params_set(_a.par);

:: pobierz ustawienia widoku
_upf_sec:='#fun_set';
_upf_val:='tree';
_tree:=#exec('get','#profile',,_upf_sec,_upf_val,'1');

_loop:=1;
{!
|? _loop
|! _TAB.cntx_psh();
   {? _tree=0
   || {? exec('select_list','#fun_set',_TAB,_hdr)
      || _tree:=1
      || _loop:=0
      ?}
   || {? exec('select_tree','#fun_set',_TAB,_hdr)
      || _tree:=0
      || _loop:=0
      ?}
   ?};
   _key:=_TAB.index('?');
   _wnd:=_TAB.win_sel('?');
:: posprzątaj
   _TAB.cntx_pop();
   _TAB.ndx_drop(_key);
   _TAB.win_del(_wnd);
:: zapamiętaj rodzaj stosowanego widoku
   exec('set','#profile',,_upf_sec,_upf_val,$_tree)
!};
~~


\display_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Tworzy okienko redakcji na potrzeby wyświetlania szczegółowych informacji o funkcji.
::   WE: _a TABLE - alias tabeli zestawu funkcji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;

_wnd:=_TAB.mk_edit('Szczegóły'@,0,'#fun_set_disp');
_TAB.win_esep(_wnd,'Dane podstawowe'@);
_TAB.win_efld(_wnd,,'NAME',,,80,,,,,'Nazwa funkcji'@);
_TAB.win_efld(_wnd,,'DESC',,,80,-5,,,,'Opis działania'@);
_TAB.win_efld(_wnd,,'ACC_VAL',,,,,,,,'Funkcja dostępna [T/N]'@,
   'check-box','left_label=1,check_label=%1'['Funkcja dostępna'@],
   "'T'","'N'"
);

_wnd


\select_cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Dodaje do okienka elementy wspólne dla obu widoków.
::   WE: _a TABLE - alias tabeli zestawu funkcji
::       _b STRING - akronim okienka wertowania
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;
_wnd:=_b;

:: kolumny widoku
_TAB.win_fld(_wnd,,'NAME',,,80,,,,,'Nazwa funkcji'@);
_TAB.win_fld(_wnd,,'ACC_VAL',,,-3,,,,,'Funkcja dostępna [T/N]'@,2,,"'T'","'N'");

:: wspólne funkcje
_TAB.win_act(_wnd,,'Formuła','Uruchom'@@,,,,"params_exec('wnd_uruchom_a','#fun_set',cur_tab(1,1))",1);
_TAB.win_act(_wnd,,'Rekord',,,,"params_exec('wnd_rekord_b','#fun_set',cur_tab(1,1),cur_win(1,1),_a)");

:: wspólne przyciski
_opt:='panel=bottom,align=end';
_TAB.win_btn(_wnd,'text=%1,%2'['&OK'@,_opt],'menu:U');
_TAB.win_btn(_wnd,'text=%1,%2'['&Anuluj'@,_opt],'key:Esc');
~~


\select_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Tworzy i wyświetla listę funkcji.
::   WE: _a TABLE - alias tabeli zestawu funkcji
::       _b STRING - tytuł okienka wyboru funkcji
::   WY: wynik metody select tabeli dostępnych funkcji
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;
_hdr:=_b;

:: przenieś parametry
params_set(params_get());

_key:=_TAB.ndx_tmp(,1,'NUMBER',,);
_wnd:=_TAB.mk_sel(_hdr,'N',,'#fun_sel_list',,,,0,'U');
_TAB.win_sopt(_wnd,'select_record_checkbox=0');
:: dodaj wspólne dla obu widoków kolumny i akcje
exec('select_cfg','#fun_set',_TAB,_wnd);
:: zmiana widoku
_name:='Widok drzewa'@@;
_help:='Prezentuje dane w formie drzewka'@;
_rule:="sel_exit()";
_opts:='target=window';
_TAB.win_act(_wnd,1,'Formuła',_name,,,,_rule,,,,,,,_opts);
_TAB.win_act(_wnd,0,'Formuła',_name,,,,_rule,,,,,,,_opts);

:: utwórz i ustaw okienko podglądu szczegułów
_TAB.win_edit(exec('display_wnd','#fun_set',_TAB));

:: wyświetl
_TAB.index(_key);
_TAB.win_sel(_wnd);
_TAB.select()


\select_tree
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Tworzy i wyświetla drzewko funkcji.
::   WE: _a TABLE - alias tabeli zestawu funkcji
::       _b STRING - tytuł okienka wyboru funkcji
::   WY: wynik metody select tabeli dostępnych funkcji
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;
_hdr:=_b;

:: przenieś parametry
params_set(params_get());

_key:=_TAB.ndx_tmp(,1,'PARENT',,,'NUMBER',,);
_wnd:=_TAB.mk_sel(_hdr,'N',,'#fun_sel_tree',,,,1);
_TAB.win_sopt(_wnd,'select_record_checkbox=0');
:: dodaj wspólne dla obu widoków kolumny i akcje
exec('select_cfg','#fun_set',_TAB,_wnd);
:: zmiana widoku
_name:='Widok listy'@@;
_help:='Prezentuje dane w formie listy'@;
_rule:="sel_exit()";
_opts:='target=window';
_TAB.win_act(_wnd,1,'Formuła',_name,,,,_rule,,,,,,,_opts);
_TAB.win_act(_wnd,0,'Formuła',_name,,,,_rule,,,,,,,_opts);

:: utwórz i ustaw okienko podglądu szczegułów
_TAB.win_edit(exec('display_wnd','#fun_set',_TAB));

:: wyświetl
_TAB.index(_key);
_TAB.win_sel(_wnd);
_TAB.select()


\update_acc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Aktualizuje znacznik dostępności.
::   WE: _a TABLE - alias tabeli zestawu funkcji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;

{? _TAB.ACC_MOD<>'T'
|| return()
?};

:: ustal dostęp
_new:='N';
_old:=_TAB.ACC_VAL;
_acc:=($_TAB.memo_txt(,1,'ACC_FML'))(_TAB,_old);

{? type_of(_acc)<>type_of('')
|| FUN.emsg(
      'Błędny typ wartości zwróconej przez formułę określającą dostępność elementu "%1".\n'
      'Oczekiwany jest napis \'T\' lub \'N\'.'@[_TAB.NAME]
   )
|? _acc<>'T' & _acc<>'N'
|| FUN.emsg(
      'Błędny wartość zwrócona przez formułę określającą dostępność elementu "%1".\n'
      'Oczekiwany jest napis \'T\' lub \'N\'.'@[_TAB.NAME]
   )
|| _new:=_acc
?};

{? _new<>_old
:: zmiana dostępu
|| _TAB.ACC_VAL:=_new;
   _TAB.put()
?};
~~


\wnd_rekord_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Obsługa akcji "rekord przed" okienek wywoływacza.
::   WE: _a TABLE - alias tabeli zestawu funkcji
::       _b STRING - akronim okienka wertowania
::       _c INTEGER - tryb odrysowywania wiersza
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;
_wnd:=_b;
_mod:=_c;

:: aktualizuj znacznik dostępności
exec('update_acc','#fun_set',_TAB);

{? _mod<>0
|| _aid:='';
   {? _TAB.TYPE='G' | _TAB.ACC_VAL='N'
   || _aid:='U'
   ?};
   _TAB.actions_grayed(_wnd,_aid)
?};

_TAB.TYPE='G'


\wnd_uruchom_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Obsługa akcji "Uruchom" okienek wywoływacza.
::   WE: _a TABLE - alias tabeli zestawu funkcji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;

params_set(params_get());
($_TAB.memo_txt(,1,'EXE_FML'))();
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:13:35 05b6c29ff093d15a1d02659e08266ebb82958c006640715a933d11df5b1c30fe125e8c2593f62a9e2c28d95a6ecfd84c840128c7acebe25f0e79cb05eaf78621d0bb260f3c8ab3aa2f7813187906297b419c74720d81f9682b81a1dc72a9c9dd034c2ccd021f211e915bd5f316ecaa6527bb0f2d9ee9948513b81d526d8c9370

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #mwapi.fml
:: Utworzony: 08.05.2019
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi usługi sieciowej MacroWebAPI
::======================================================================================================================


\wsenv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PS] [17.14]
:: OPIS: Tworzy i zwraca środowisko pracy web serwisu, lub tylko zwraca, jeżeli środowisko już zostało zainicjalizowane.
::   WY: DICT - tablica nazwana - środowisko web serwisu
::  OLD: \wsenv/#webservice.fml
::----------------------------------------------------------------------------------------------------------------------
::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablicę i komentować poszczególne jej elementy
::       powiedzmy, że to będzie pole
         _fld:="31+form(_a)";
::       powiedzmy, że to będzie metoda
         _mth:="31+form(_a)";

{? ~exec('on','#mwapi')
||
   __wsenv:=obj_new(
::                  POLA
                     _fld('TAB_INFO'  ,'Tabela tymczasowa zawierająca komunikaty dla webserwisu')
                    ,_fld('IDADD'     ,'IDADD rekordu')
::                  METODY
                    ,_mth('load'      ,'Tworzy struktury')
                    ,_mth('add_info'  ,'Dodaje informację do tabeli z wiadomościami')
                    ,_mth('add_error' ,'Dodaje błąd do tabeli z wiadomościami')
                    ,_mth('has_errors','Zwraca informacje (1/0), czy w tabeli są jakieś błędy')
                    ,_mth('has_info'  ,'Zwraca informacje (1/0), czy w tabeli są jakieś informacje')
                    ,_mth('to_json'   ,'Tworzy obiekt JSON ze wszystkich komunikatów')
                    ,_mth('erase'     ,'Usuwa wszystkie komunikaty')
                    ,_mth('info'      ,'Usuwa wszystkie komunikaty')
                    );

   __wsenv.load:="
      .TAB_INFO:=tab_tmp_out_tr(1
         ,'TYPE','STRING[10]','Typ wiadomości (ERROR, INFO)'
         ,'MESSAGE','SYS_MEMO','Treść wiadomości'
      );
      .IDADD:=''
   ";

   __wsenv.add_info:="
      .TAB_INFO.blank();
      .TAB_INFO.TYPE:='INFO';
      .TAB_INFO.memo_set(_a,'MESSAGE');
      {? .TAB_INFO.add() || .TAB_INFO.memo_put() ?}
   ";

   __wsenv.add_error:="
      .TAB_INFO.blank();
      .TAB_INFO.TYPE:='ERROR';
      .TAB_INFO.memo_set(_a,'MESSAGE');
      {? .TAB_INFO.add() || .TAB_INFO.memo_put() ?}
   ";

   __wsenv.has_errors:="
      .TAB_INFO.prefix('ERROR',);
      _has:=.TAB_INFO.first();
      .TAB_INFO.prefix();
      _has
   ";

   __wsenv.has_info:="
      .TAB_INFO.prefix('INFO',);
      _has:=.TAB_INFO.first();
      .TAB_INFO.prefix();
      _has
   ";

   __wsenv.to_json:="
      .TAB_INFO.json_records(,
         'TYPE',,'MESSAGE',
      )
   ";

   __wsenv.erase:="
      .TAB_INFO.erase();
      .IDADD:=''
   ";

   __wsenv.info:="
      '{\"TYPE\":\"INFO\",\"MESSAGE\": \"%1\"}'[_a]
   ";

   __wsenv.load()
?};
__wsenv


\on
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Czy dostępne komunikaty dla webservice
::  OLD: \on/#webservice.fml
::----------------------------------------------------------------------------------------------------------------------
var_pres('__wsenv')>0

:Sign Version 2.0 jowisz:1045 2020/04/03 17:05:31 00dd84cd14de3295a72e0252bae7c35e223798112c5fcde1b226f2b362b52571500ce7cfe1ee0af053d65864c3ac11344558708a747d065ace5b10002b6ff16de5dd604a468c2b3e3c63b7327b3f10f0dae1c5f7a8e558ed0091d9fd0f334a6f7a62749c9d3354d6f009a3c461e1a67416d80842acccf11c5a1fdbf4bb700b8c

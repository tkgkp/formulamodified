:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #serwery.fml
:: Utworzony: 09.07.2020
:: Autor: GZ
::======================================================================================================================
:: Zawartość: Obsługa serwerów zewnętrznych
::======================================================================================================================


\srv_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [20.42]
:: OPIS: Selekcja listy serwerów
::----------------------------------------------------------------------------------------------------------------------
POCZTAM.fld_attr('PASS',2);
POCZTAM.win_sel('WERS');
POCZTAM.win_edit('REDS');
POCZTAM.win_patt('SZUKS');
POCZTAM.index('SERVER');
POCZTAM.prefix(REF.FIRMA,'S');
POCZTAM.select();
''


\srv_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [20.42]
:: OPIS: Obsługa przed dołączeniem POCZTAM
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
POCZTAV.PASS:='';
POCZTAM.blank();
POCZTAM.RODZ:='S';
POCZTAM.PORT:=0;
{? POCZTAM.edit("exec('srv_rkpo','#serwery')")
|| POCZTAM.add()
?};
1


\srv_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [20.42]
:: OPIS: Obsługa przed poprawianiem POCZTAM
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
POCZTAV.PASS:='';
1


\srv_rkpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [20.42]
:: OPIS: Rekord po dla okna POCZTAM.WERS
::----------------------------------------------------------------------------------------------------------------------
:: proste sprawdzenie nazwy/IP serwera
{? POCZTAM.SERVER=''
|| FUN.info('Podaj nazwę albo adres IP serwera.'@);
   'SERVER'
|? ~exec('srv_unique','#serwery')
|| FUN.info('Istnieje już serwer z tym adresem.'@);
   'SERVER'
||
   POCZTAM.RODZ:='S';
   _adres:=-| POCZTAM.SERVER;
   _ok:=1;
   {! _it:=1..+_adres
   |! _znak:=(1+(_it-_adres));
      {? '-.0123456789abcdefghijklmnopqrstuwvxyz_'*_znak=0
      || _ok:=0
      ?}
   !};
   {? _ok
   ||
::    jeżeli hasło zostało wpisane, to jest kodowane
      {? POCZTAV.PASS<>''
      || POCZTAM.PASS:=exec('pwd_encode','#string',POCZTAV.PASS,exec('encode_key','#mailbox'))
      || POCZTAM.PASS:=''
      ?};
      1
   || FUN.emsg('Niepoprawna nazwa serwera SMTP.'@);
      'SERVER'
   ?}
?}


\srv_unique
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Sprawdzenie unikalności rekordu POCZTAM
::   WY: 1 - jest unikalny, 0 - nie jest
::----------------------------------------------------------------------------------------------------------------------
_unik:=1;
POCZTAM.cntx_psh();
{? 1+menu_pth()='D'
|| _ref:=-1
|| _ref:=POCZTAM.ref()
?};
_srv:=POCZTAM.SERVER;
POCZTAM.index('SERVER');
POCZTAM.prefix(REF.FIRMA,'S',_srv,_srv);
{? POCZTAM.first()
|| {!
   |? {? POCZTAM.ref()<>_ref
      || _unik:=0
      ?};
      _unik & POCZTAM.next()
   !}
?};
POCZTAM.cntx_pop();
_unik

:Sign Version 2.0 jowisz:1048 2021/04/09 15:24:22 2a68e0ee916194be0cc4b6c012a127e4c8ff93ffc03216d69f3b171c341b32683276913c6f6ab91dad75508cfc224d887524ddb362fdcd9a31e85ad6946629c379559a0cbb339fb199df3f69ae7336a32b1b6d32572e5184205bc29950a4f24bcbd1c1cbcab0f43dbf78991c8952e8370645f8189b10716f337a65906f0e26bf

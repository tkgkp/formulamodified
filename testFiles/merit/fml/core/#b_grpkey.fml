:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #b_grpkey.fml
:: Utworzony: 10.07.2017
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Klucze grupujące
::======================================================================================================================


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Dodanie pozycji klucza grupującego
::   WE: _a - klucz grupujący
::       _b - pozycja klucza
::   WY: 0-nie dodano, 1-dodano
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('')
   | var_pres('_b')<>type_of('')
|| FUN.info('Błędna wartość parametru.'@);
   return(0)
?};
B_GRPKEY.index('ITEM');
B_GRPKEY.prefix();
{? ~B_GRPKEY.find_key(_a,_b)
|| B_GRPKEY.blank();
   B_GRPKEY.GRPKEY:=_a;
   B_GRPKEY.ITEM:=_b;
   B_GRPKEY.add()
?}


\del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Usunięcie pozycji klucza grupującego
::   WE: _a - klucz grupujący
::       _b - pozycja klucza
::   WY: 0-nie usunięto, >0-usunięto
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('')
   | var_pres('_b')<>type_of('')
|| FUN.info('Błędna wartość parametru.'@);
   return(0)
?};
B_GRPKEY.cntx_psh();
B_GRPKEY.index('ITEM');
B_GRPKEY.prefix();
_return:=
   {? B_GRPKEY.find_key(_a,_b,)
   || B_GRPKEY.del(1,1)
   || 1
   ?};
B_GRPKEY.cntx_pop();
_return


\delFirst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Usuwa pierwszą pozycję klucza grupującego
::   WE: _a - klucz grupujący
::   WY: 0-nie usunięto, 1-usunięto
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('')
|| FUN.info('Błędna wartość parametru.'@);
   return(0)
?};
_del:=1;
B_GRPKEY.index('ITEM1');
B_GRPKEY.prefix(_a,);
{? B_GRPKEY.first()
|| _del:=B_GRPKEY.del(1,1)
?};
_del>0


\delAll
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Usuwa wszystkie pozycje klucza grupującego
::   WE: _a - klucz grupujący
::   WY: 0-nie usunięto, 1-usunięto
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('')
|| FUN.info('Błędna wartość parametru.'@);
   return(0)
?};
_del:=1;
B_GRPKEY.index('ITEM1');
B_GRPKEY.prefix(_a,);
_loop:=B_GRPKEY.first();
{!
|? _loop
|!
   _del:=B_GRPKEY.del(1,1);
   _loop:=_del=2 | _del=3
!};
_del=1


\delItem
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Usuwa pozycję _a ze wszystkich kluczy grupujących
::   WE: _a - pozycja
::   WY:
::----------------------------------------------------------------------------------------------------------------------
B_GRPKEY.cntx_psh();
B_GRPKEY.index('ITEM2');
B_GRPKEY.prefix(_a,);
_loop:=B_GRPKEY.first();
{!
|? _loop
|!
   _loop:=B_GRPKEY.del(1,1);
   _loop:=_loop=2 | _loop=3
!};
B_GRPKEY.cntx_pop()


\get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Zwraca pierwszą pozycję klucza grupującego
::   WE: _a - Klucz grupujący
::   WY: Pierwsza pozycja klucza grupującego
::----------------------------------------------------------------------------------------------------------------------
_return:=~~;
{? var_pres('_a')<>type_of('')
|| FUN.info('Błędny typ parametru.'@)
|| B_GRPKEY.cntx_psh();
   B_GRPKEY.index('ITEM1');
   B_GRPKEY.prefix(_a,);
   {? B_GRPKEY.first() || _return:=B_GRPKEY.ITEM ?};
   B_GRPKEY.cntx_pop()
?};
_return


\getAll
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Zwraca pozycje klucza grupującego
::   WE: _a - Klucz grupujący
::   WY: Tabela z pozycjami klucza grupującego
::----------------------------------------------------------------------------------------------------------------------
_return:=~~;
{? var_pres('_a')<>type_of('')
|| FUN.info('Błędny typ parametru.'@)
|| _Tab:=tab_tmp(1
      ,'IDADD' ,'STRING[31]'  ,'IDADD'
      ,'ITEM'  ,'STRING[100]' ,'ITEM');
   _continue:=1;
   B_GRPKEY.cntx_psh();
   B_GRPKEY.index('ITEM1');
   B_GRPKEY.prefix(_a,);
   _loop:=B_GRPKEY.first();
   {!
   |? _loop
   |!
      _Tab.IDADD:=B_GRPKEY.IDADD;
      _Tab.ITEM:=B_GRPKEY.ITEM;
      _continue:=_Tab.add();
      _loop:=_continue & B_GRPKEY.next()
   !};
   {? _continue || _return:=_Tab ?};
   B_GRPKEY.cntx_pop()
?};
_return


\clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Usuwa pozycje nieużywanych kluczy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Tabele analizowanych kluczy grupujących
_Tab:=tab_tmp(
   ,'GRPKEY','STRING[100]' ,'GRPKEY');

:: Ustalenie zakresu analizy
B_GRPKEY.cntx_psh();
B_GRPKEY.index('IDADD');
B_GRPKEY.prefix();
_idadd:={? B_GRPKEY.last() || B_GRPKEY.IDADD || ~~ ?};
B_GRPKEY.cntx_pop();
{? _idadd=~~ || return() ?};

:: Zebranie wszystkich kluczy grupującyh użytych w procesie
B_GRPKEY.cntx_psh();
B_GRPKEY.index('ITEM');
B_GRPKEY.prefix();
_loop:=B_GRPKEY.first();
{!
|? _loop
|!
   {? B_GRPKEY.IDADD<=_idadd & ~_Tab.find_key(B_GRPKEY.GRPKEY,)
   ||
      _Tab.GRPKEY:=B_GRPKEY.GRPKEY;
      _Tab.add()
   ?};
   _loop:=B_GRPKEY.next()
!};
B_GRPKEY.cntx_pop();

:: Analiza kluczy grupujących
BI_PORT.cntx_psh();
exec('bistat_status','#bi_stat');
_type_of:=type_of('');
_ii:=0; _bi_stat:=obj_new(3);
_ii+=1; _bi_stat[_ii]:=__Status.URUCHOMIONY;
_ii+=1; _bi_stat[_ii]:=__Status.ZAWIESZONY;
_loop:=_Tab.first();
{!
|? _loop
|!
   _del:=0;
   _to:=obj_len(_bi_stat);
   {! _ii:=1.._to
   |? _del=0 & _ii<_to
   |!
      BI_PORT.index('GRPKEY');
      BI_PORT.prefix('T',_bi_stat[_ii],_Tab.GRPKEY,);
      {? BI_PORT.first()
      || _del:=1
      ?}
   !};

   _loop:={? _del || _Tab.del() || _Tab.next() ?}
!};
BI_PORT.cntx_pop();

:: Usunięcie pozycji nieużywanych kluczy
_loop:=_Tab.first();
{!
|? _loop
|!
   exec('delAll','#b_grpkey',_Tab.GRPKEY);
   _loop:=_Tab.next()
!}


\test
:: Ustalenie kluczy grupujących
::_mp.ggrpkey(_out.GGRPKEY,_in.GGRPKEY);
::_mp.grpkey(_out.GRPKEY,_in.GRPKEY);
::_mp.grpkey(_out.GGRPKEY,_in.GGRPKEY);

:: add
::_val:='aaa';
::_mp.grpkeyAdd(_val);
::_mp.ggrpkeyAdd(_val);
::_val:='abba';
::_mp.grpkeyAdd(_val);
::_mp.ggrpkeyAdd(_val);

:: get
::FUN.info(_mp.grpkeyGet());
:: getAll
::_Tab1:=_mp.grpkeyGetAll();
::_tab_wer:=_Tab1.mk_sel(_mp.GRPKEY);
::_Tab1.win_fld(_tab_wer,,'ITEM',,,50);
::_Tab1.win_fld(_tab_wer,,'IDADD',,,31);
::_Tab1.win_sel(_tab_wer);
::_Tab1.select();

:: del
::_mp.grpkeyDel();
::_mp.grpkeyDel(_val);
:: delAll
::_mp.grpkeyDelAll();

:: b_grpkey - zawartość
_wer:=B_GRPKEY.mk_sel();
B_GRPKEY.win_fld(_wer,,'GRPKEY',,,50);
B_GRPKEY.win_fld(_wer,,'ITEM',,,50);
B_GRPKEY.win_fld(_wer,,'IDADD',,,31);
B_GRPKEY.win_sel(_wer);
B_GRPKEY.prefix();
B_GRPKEY.select();

_mp.keep();
return()


\findItem
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.14]
:: OPIS: Sprawdza, czy w podanym kluczu grupującym istnieje pozycja.
::   WE: _a [STRING] - Klucz grupujący.
::       _b [STRING] - Pozycja.
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
B_GRPKEY.cntx_psh();
B_GRPKEY.index('ITEM');
B_GRPKEY.prefix(_a,_b,);
_ret:=B_GRPKEY.first();
B_GRPKEY.cntx_pop();
_ret

:Sign Version 2.0 jowisz:1048 2023/06/23 14:13:35 c3a8eb2a8b78052ef697ce60dccd4e23371090ed9a53a44dcd897a72e32957593a6bab2b538b7238e8d7867ba158c2975d855c1ba0f0358624c6bf8ef9fdd4c4bbbe81767b31cb5664cc43de009a467e24a15b94fd4ce67a87682044618059c9b7973ab2f5178d75c515316274cd3110df42aac7093a7a7bfc47a7afb04b159a

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #b_trig.fml [17.00]
:: Utworzony: 05.11.2014
:: Autor: WH
::======================================================================================================================
:: Zawartosc: Triggery do obslugi procesowości
::======================================================================================================================


\ustaw_trigger
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [17.00]
:: OPIS: Wywolanie przypisań triggerow (funkcja wywoływana podczas startu systemu)
::----------------------------------------------------------------------------------------------------------------------
exec('B_PROC'  ,'#b_trig');
exec('B_PREL'  ,'#b_trig');
exec('B_ROLE'  ,'#b_trig');
exec('BI_PROC' ,'#b_trig');
exec('B_TIMER' ,'#b_trig');
exec('BI_QUEUE','#b_trig');
exec('DOK'     ,'#b_trig');
exec('FAKS'    ,'#b_trig');
~~


\BI_PROC
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: TMR [17.00]
:: OPIS: Triggery dla tabeli BI_PROC
::----------------------------------------------------------------------------------------------------------------------
BI_PROC.trig_a('put',"
   {? BI_PROC.BI_STAT=__Status.ZAKONCZONY
      | BI_PROC.BI_STAT=__Status.AWARYJNIE
   || B_KEYREF.cntx_psh();
      B_KEYREF.index('BI_PROC');
      B_KEYREF.prefix(ref_name(BI_PROC.ref()),BI_PROC.ref());
      {? B_KEYREF.first()
      || {!|? B_KEYREF.del(1) !}
      ?};
      B_KEYREF.cntx_pop()
   ?};
   ~~
",'bi_procaput');
~~


\B_ROLE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Triggery dla tabeli B_ROLE
::  TAG: <PRYWATNA><MODELER><PROCES>
::----------------------------------------------------------------------------------------------------------------------
_formula:="
   B_LANE.cntx_psh();
   B_LANE.index('B_ROLE');
   B_LANE.prefix(B_ROLE.FIRMA,B_ROLE.ref(),exec('type_role','#b_lane'));
   {? B_LANE.first()
   ||
::    Jest już taki tor - aktualizuje symbol B_ELE na podstawie nazwy roli
      B_ELE.cntx_psh();
      B_LANE.B_ELE().SYMBOL:=B_ROLE.NAME;
      B_ELE.put();
      B_ELE.cntx_pop()
   ||
::    Nie ma jeszcze takiego toru - zakładam nowy
      exec('add','#b_lane',B_ROLE.ref())
   ?};
   B_LANE.cntx_pop();
   ~~
";
B_ROLE.trig_a('add',_formula,'b_roleadd');
B_ROLE.trig_a('put',_formula,'b_roleput');
~~


\B_PROC
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Triggery dla tabeli B_PROC
::   WE: [_a] - INTEGER - 0/[1] - triggery włączone lub wyłączone
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_on:=1;
{? var_pres('_a')=type_of(0)
|| _on:=_a
?};
{? _on>0
|| _formula:="
      IVAL_DEF.cntx_psh();
      {? B_PROC.IVAL_DEF<>null()
      || B_PROC.TM_PLAN:=B_PROC.IVAL_DEF().TM_STAMP
      || B_PROC.TM_PLAN:=0
      ?};
      IVAL_DEF.cntx_pop();
      1
   "
|| _formula:=""
?};

B_PROC.trig_b('add',_formula,'b_procbadd');
B_PROC.trig_b('put',_formula,'b_procbput');
~~


\B_PREL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Triggery dla tabeli B_PREL
::   WE: [_a] - INTEGER - 0/[1] - triggery włączone lub wyłączone
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_on:=1;
{? var_pres('_a')=type_of(0)
|| _on:=_a
?};
{? _on>0
|| _formula:="
      IVAL_DEF.cntx_psh();
      {? B_PREL.IVAL_DEF<>null()
      || B_PREL.TM_PLAN:=B_PREL.IVAL_DEF().TM_STAMP
      || B_PREL.TM_PLAN:=0
      ?};
      IVAL_DEF.cntx_pop();
      1
   "
|| _formula:=""
?};
B_PREL.trig_b('add',_formula,'b_prelbadd');
B_PREL.trig_b('put',_formula,'b_prelbput');
~~


\B_TIMER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Triggery dla tabeli B_TIMER
::   WE: [_a] - INTEGER - 0/[1] - triggery włączone lub wyłączone
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_on:=1;
{? var_pres('_a')=type_of(0)
|| _on:=_a
?};
{? _on>0
|| _formula:="
      IVAL_DEF.cntx_psh();
      {? B_TIMER.IVAL_DEF<>null()
      || B_TIMER.TM_PLAN:=B_TIMER.IVAL_DEF().TM_STAMP
      || B_TIMER.TM_PLAN:=0
      ?};
      IVAL_DEF.cntx_pop();
      1
   "
|| _formula:=""
?};
B_TIMER.trig_b('add',_formula,'b_timerbadd');
B_TIMER.trig_b('put',_formula,'b_timerbput');
~~


\BI_QUEUE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Triggery dla tabeli BI_QUEUE
::   WE: [_a] - INTEGER - 0/[1] - triggery włączone (o ile __develop) lub wyłączone
::----------------------------------------------------------------------------------------------------------------------
_on:=1;
{? var_pres('_a')=type_of(0)
|| _on:=_a
?};
_formula_add:="";
_formula_del:="";
{? _on>0 & __develop
|| _formula_add:="exec('trig_a_add','#bi_queue')";
   _formula_del:="exec('trig_a_del','#bi_queue')"
?};
BI_QUEUE.trig_a('add',_formula_add,'biqueueadd');
BI_QUEUE.trig_a('put',_formula_del,'biqueueput');
~~


\usun_trigger
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: [rr] [17.00]
:: OPIS: Usunięcie przypisań triggerow
::----------------------------------------------------------------------------------------------------------------------
B_ACTION.trig_a('add',"",'b_actionaadd');
BI_PREL.trig_a('put',"",'bi_prelaput');
BI_PROC.trig_a('put',"",'bi_procaput');
exec('B_PROC'  ,'#b_trig',0);
exec('B_PREL'  ,'#b_trig',0);
exec('B_TIMER' ,'#b_trig',0);
~~


\DOK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.14_17]
:: OPIS: Triggery dla tabeli DOK
::----------------------------------------------------------------------------------------------------------------------
_fml:="
{? (DOK.DOK_REJ().SLO().KOD='VAT' | DOK.DOK_REJ().SLO().KOD='SAD') || exec('add_dok_jpk','obiegi2') ?};
~~
";
DOK.trig_a('add',_fml,'dok_trig_a_add')


\FAKS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Triggery dla tabeli FAKS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_trig:="
   {? var_pres('__MANSKSEF')=type_of('') & (__MANSKSEF='BC' | __MANSKSEF='BC_ATTA_ADD')
   ||
      ~~

   |? FAKS.MANSKSEF='T' | FAKS.STAT_REJ='A'
   ||
      FAKS.STATKSEF:=exec('ksef_donot_applay','zbl_stat')
   |?
      FAKS.STATKSEF=''
         |
      FAKS.STATKSEF=exec('ksef_donot_applay','zbl_stat')
         |
      FAKS.STATKSEF=exec('before_send','zbl_stat')
   ||
      FAKS.STATKSEF:=exec('faks_statksef','faktury_nag1')
   ?};
   FAKS.EDOKUMEN:=exec('faks_edokumen','zbl');
   FAKS.DRUKUJ:=exec('faks_drukuj','zbl');
   {? exec('upgrade2325_blbc1','zbl')
   || {?
         FAKS.STATBC=''
            |
         FAKS.STATBC=exec('check_none','zbl_stat')
            |
         FAKS.STATBC=exec('check_check','zbl_stat')
      ||
         FAKS.STATBC:=
            {? exec('is_businesscheck','businesslink3')>0
                  &
               {? FAKS.T <> null() || FAKS.T().KSEF_BC > 0 || 0 ?}
                  &
               FAKS.STATKSEF<>exec('ksef_donot_applay','zbl_stat')
            ||
               exec('check_check','zbl_stat')
            ||
               exec('check_none','zbl_stat')
            ?}
      ?}
   ?};
   1
";
FAKS.trig_b('put',_trig,'zbl')

:Sign Version 2.0 jowisz:1045 2023/09/27 10:38:07 34e3381ad99692abf8c6f265be8fd2732c84334ef531a24be4b95406d031d71a30d9ecb15f437a3d175556c37bf09ec2decdd7da5c64e30daf5545ed26543e23f6edfe4374f1355f1d6eb570ec356b0343918f0359389080239e2824466349f55ff9c2c0e8432a7553eadda5cd07c1abdfc58d56b303b234ca08d027c52f7efb

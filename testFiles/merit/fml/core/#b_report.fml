:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #b_report.fml [17.00]
:: Utworzony: 2014-12-03
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły do obsługi czynności raportujących.
::======================================================================================================================


\tab_report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Budowa tabeli tymczasowej i okienka "Raportów".
::   WE: _a [OBJECT] - obj_new('TAB','WER')
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_sql:='select';
_sql+=' B_DOMAIN.SYMBOL, B_DOMAIN.COLOR,';
_sql+=' B_ACTION.UID, B_ACTION.NAME, B_ACTION.FMMAIN, B_ACTION.FMTODO, B_ACTION.ICON, ';
_sql+=' space(48) as UID_EXE, ';
_sql+=' space(255) as DOKUWIKI, ';
_sql+=' space(100) as AH, ';
_sql+=' space(1) as B_CAN, ';
_sql+=' space(255) as URL, ';
_sql+=' cast(space(1) as SYS_MEMO_TYPE) as DSK_URL ';
_sql+='from B_ACTION join B_DOMAIN ';
_sql+='where B_ACTION.REP=1 and B_ACTION.AKT=\'T\' ';
_sql+='order by B_ACTION.UID';

_task:=app_info('task');
_task.index(_task.ndx_tmp(,,'SYMBOL',,));
_obj.TAB:=sql(_sql);
_interm:=exec('interm','#system')>0;
_mobile:=exec('mobile','#system')>0;
_websesid:=+app_info('web_sesid');
exec('czytaj','#stalesys',,XINFO,'DOKUWIKI','DOKU_INT');
{? exec('interm','#system') & XINFO.DOKU_INT<>''
|| _adr_wi:=-(form(XINFO.DOKU_INT))
|| _adr_wi:=-(form(XINFO.DOKUWIKI))
?};
{? _obj.TAB.first()
|| _user:=exec('operatorUser','#users');
   B_ACTION.cntx_psh();
   B_ACTION.index('UNIK');
   B_ACTION.prefix();
   {!
   |?
      _task.prefix(_obj.TAB.UID);
      _can_add:=0;
      {? _task.first() & B_ACTION.find_key(_obj.TAB.UID,) & exec('lic','#b_action',B_ACTION.ref()) &
         exec('chk_role','#b__box',_user,_obj.TAB.UID)
         & ((_websesid & (_task.B_CAN='W' | _task.B_CAN='Q'))
            | (_websesid=0 & (_task.B_CAN='M' | _task.B_CAN='Q')) )
      || _can_add:=1
      ?};
      {? _can_add>0
      ||
         {? _interm
         || {? _task.INTERM<>'T'
            || _can_add:=0
            ?}
         ?};
         {? _mobile
         || {? _task.MOBILE<>'T'
            || _can_add:=0
            ?}
         ?}
      ?};

      {? _can_add>0
      || _formula:=
            'exec(\'wybierz\',\'#b_report\',\'%1\',\'%2\',\'%3\')' [_obj.TAB.SYMBOL,_obj.TAB.UID,_obj.TAB.FMMAIN];
         _obj.TAB.UID_EXE:=exec('bdexec_check','#b_desktop',_formula,_obj.TAB.SYMBOL);
::       adres serwera dokumentacyjnego + ścieżka
         _path:=exec('get_adres','#help','dziedziny:%1:%2'[3+(-_obj.TAB.UID),-_obj.TAB.UID],_adr_wi,0);

         _obj.TAB.B_CAN:=_task.B_CAN;
         _obj.TAB.AH:='RAPORT:'@+' '+ {? _task.first() || _task.AH ?};
         _obj.TAB.NAME:=_task.NAME;
         _obj.TAB.DOKUWIKI:=_path;
         {? _obj.TAB.B_CAN='Q'
         ||
            {? var_pres('_result')>0 || obj_del(_result) ?};
            _result:=mb_exec(B_ACTION.FMMAIN);
            _obj.TAB.URL:=_result.URL;
            _obj.TAB.memo_set(_result.DSK_URL)
         ?};
         _obj.TAB.put();
         _obj.TAB.memo_put();
         _obj.TAB.next()
      || _obj.TAB.del()
      ?}
   !};
   B_ACTION.cntx_pop()
?};
_obj.TAB.first();
~~


\tab_report2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Budowa tabeli tymczasowej i okienka "Raportów". Wersja bardziej optymalna
::   WE: _a [OBJECT] - obj_new('TAB','WER')
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;

_tab:=tab_tmp(1,
   'UID','STRING[12]','Identyfikator'@,
   'SYMBOL','STRING[12]','Symbol'@,
   'COLOR','STRING[20]','Kolor'@,
   'NAME','STRING[30]','Nazwa'@,
   'FMMAIN','STRING[35]','Formuła główna'@,
   'FMTODO','STRING[35]','Formuła TODO'@,
   'ICON','STRING[60]','Ikona'@,
   'B_CAN','STRING[1]','Kanał'@,
   'UID_EXE','STRING[48]','UID_EXE'@,
   'DOKUWIKI','STRING[255]','DOKUWIKI'@,
   'URL','STRING[255]','Adres URL dla QLIK',
   'DSK_URL','SYS_MEMO','Adres URL dla QLIK',
   'AH','STRING[60]','Podpowiedz'@
);
_task:=app_info('task');
_task.index(_task.ndx_tmp(,,'SYMBOL',,));
_interm:=exec('interm','#system')>0;
_mobile:=exec('mobile','#system')>0;
_websesid:=+app_info('web_sesid');
exec('czytaj','#stalesys',,XINFO,'DOKUWIKI','DOKU_INT');
{? exec('interm','#system') & XINFO.DOKU_INT<>''
|| _adr_wi:=-(form(XINFO.DOKU_INT))
|| _adr_wi:=-(form(XINFO.DOKUWIKI))
?};

B_ACTION.cntx_psh();
B_ACTION.index('REPORT');
B_ACTION.prefix('T',1);
{? B_ACTION.first()
|| _user:=exec('operatorUser','#users');
   {!
   |?
      _task.prefix(B_ACTION.UID,);
      _can_add:=0;
      {? _task.first() & exec('lic','#b_action',B_ACTION.ref()) &
         exec('chk_role','#b__box',_user,B_ACTION.UID,2)
         & ((_websesid & (_task.B_CAN='W' | _task.B_CAN='Q'))
            | (_websesid=0 & (_task.B_CAN='M' | _task.B_CAN='Q')) )
      || _can_add:=1
      ?};
      {? _can_add>0
      ||
         {? _interm
         || {? _task.INTERM<>'T'
            || _can_add:=0
            ?}
         ?};
         {? _mobile
         || {? _task.MOBILE<>'T'
            || _can_add:=0
            ?}
         ?}
      ?};

      {? _can_add>0
      ||
         _tab.blank();
         _tab.UID:=B_ACTION.UID;
         _tab.SYMBOL:=B_ACTION.B_DOMAIN().SYMBOL;
         _tab.COLOR:=B_DOMAIN.COLOR;
         _tab.NAME:=B_ACTION.NAME;
         _tab.FMMAIN:=B_ACTION.FMMAIN;
         _tab.FMTODO:=B_ACTION.FMTODO;
         _tab.ICON:=B_ACTION.ICON;
         _tab.B_CAN:=_task.B_CAN;
         _formula:='exec(\'wybierz\',\'#b_report\',\'%1\',\'%2\',\'%3\')'[B_DOMAIN.SYMBOL,_tab.UID,_tab.FMMAIN];
         _tab.UID_EXE:=exec('bdexec_check','#b_desktop',_formula,B_DOMAIN.SYMBOL);

         _path:=exec('get_adres','#help','dziedziny:%1:%2'[3+(-_tab.UID),-_tab.UID],_adr_wi,0);
         _tab.DOKUWIKI:=_path;

         {? var_pres('_res_qlik')>100 || obj_del(_res_qlik) ?};

         {? _tab.B_CAN='Q'
         ||
            _res_qlik:=mb_exec(B_ACTION.FMMAIN);
            _tab.URL:=_res_qlik.URL
         ?};
         _tab.AH:='RAPORT:'@+' '+_task.AH;
         {? _tab.add()
         || {? _tab.B_CAN='Q' & var_pres('_res_qlik')>100
            || _tab.memo_set(_res_qlik.DSK_URL);
               _tab.memo_put(,'DSK_URL')
            ?}
         ?};
         ~~
      ?};
      B_ACTION.next()
   !}
?};
B_ACTION.cntx_pop();
_tab.first();
_obj.TAB:=_tab;
~~


\rep_exec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa raportów - funkcja wywoływana z czynności raportujących. Poza pierwszym parametrem - pozostałe są
::       zgodne z tymi dotyczącymi funkcji systemowej rep_exec().
::   WE:  _a  [STRING]  - Identyfikator czynności wywołującej (poprawność nie jest sprawdzana).
::       [_b] [STRING]  - Maska raportów [domyślnie: '*'].
::       [_c] [STRING]  - Tytuł menu, [domyślnie: nazwa czynności].
::       [_d] [INTEGER] - Sposób działania po wykonaniu raportu:
::             0 - funkcja kończy działanie [domyślnie];
::             1 - można wybrać kolejny raport;
::            -1 - zwraca maskę wybranego wydruku
::             2 - funkcja kończy działanie i zwraca, czy raport został wykonany
::       [_e] [STRING]  - Nazwa pliku do eksportu sprawozdania [domyślnie: '' - brak pliku].
::       [_f] [INTEGER] - Wydruk "bez drukarki" [domyślnie: 1 - z drukarką].
::       [_g] [STRING]  - Dodatkowe parametry (tylko przy tworzeniu PDF) [domyślnie: ''].
::       [_h] [OBJECT]  - obiekt klasy REPTAB do obsługi tabeli z raportami
::       [_i] [STRING]  - obiekt dodający akcję dla okienka, pola:
::                         N - nazwa Paperless &HR,
::                         W - wyróżnik H,
::                         F - formuła akcji
::                         I - ikona dla rekordu
::                         R - rodzaj
::       [_j] [STRING/RULE]  - Jeśli wywołanie dotyczy jednego raportu (maska sprawozdań _b to nazwa 1 wydruku),
::                        to podanie wyróżnika akcji W/E/K wywoła akcję zgodną z wyróżnikiem lub uruchomi formułę
::   WY: dotyczy _d=0 i _d=1: 0 - Dla wywołania z konkretnym raportem: pliku nie znaleziono.
::                      1 - W przeciwnym wypadku.
::       dotyczy _d<0:  maska wybranego wydruku lub _b.
::       dotyczy _d=2:  czy raport został wykonany
::----------------------------------------------------------------------------------------------------------------------
_uid:=_a;
_maska:={? var_pres('_b')=type_of('') || _b || '*' ?};
_tyt:={? var_pres('_c')=type_of('') || _c || '' ?};
_again:={? var_pres('_d')=type_of(0) || _d || 0 ?};
_exp_file:={? var_pres('_e')=type_of('') || _e || '' ?};
_noprinter:={? var_pres('_f')=type_of(0) || _f || 0 ?};
_reppar:={? var_pres('_g')=type_of('') || _g || '' ?};
_czyDodatek:=var_pres('_i')>100;

{? _again<-1 | _again>2
|| _again:=0
?};

_msk:='';

params_set(params_get());

_params:=params_get();
{? var_pres('_params')>100 & var_pres('RAP_ID',_params)=type_of('') & _params.RAP_ID<>''
|| {? fexists(_params.RAP_ID+'.rpt',1)
   || return(rep_exec(_params.RAP_ID,,,_exp_file,_noprinter,_reppar))
   || return(0)
   ?}
?};

exec('__FileLayer','#object');

{? _tyt=''
|| _TASK:=app_info('task');
   _tyt:={? _TASK.find_key(3+_uid,_uid,) || _TASK.NAME || 'WZORCE SPRAWOZDAŃ'@ ?};
   obj_del(_TASK)
?};

_tab:={? var_pres('REPTAB',@.CLASS)>0 & var_pres('_h')=type_of(@.CLASS.REPTAB) || _h.tab || rep_info(_maska) ?};
_tab.first();
_runAuto:=
   {? var_pres('_j')=type_of('') & 'WEK'*_j & _tab.size()=1
   || _j
   |? var_pres('_j')=type_of("") & +_j & _tab.size()=1
   || _j
   || ''
   ?};

_isfml:=var_pres('FORMULA',_tab)>0;
_dl:=_tab.size()+1;
{? _dl<9 || _dl:=9 |? _dl>30 || _dl:=30 ?};
_ws:=_tab.mk_sel(_tyt,'N',,'#wer',,,_dl,,'U');
_tab.win_fld(_ws,,'TITLE',,,,,,,1);

_tab.win_act(_ws,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
_cfg:=',btn_label_align=center,panel=right,align=begin';
_btnOK:=_tab.win_btn(_ws,'text='+'Wybierz'@+_cfg,'menu:W');
{? sec_superuser()
|| _tab.win_act(_ws,,'Formuła','Edytuj'@@,,,
      "  _TAB:=cur_tab(1,1);
         _rpms:=_TAB.FILENAME;
         _rpmu:=_rpms+'.m';
         {? fexists(_rpmu+'.rpm',1)=1
         || popup(0,_TAB.TITLE,
               'Przeglądaj wersję standardową',,$('txt_view(\\\''+_rpms+'.rpm\\\')'),
               'Redaguj wersję zmodyfikowaną',,$('exec(\\\'edytuj\\\',\\\'#b_report\\\',\\\''+_rpmu+'\\\')')
            )
         || popup(0,_TAB.TITLE,
               'Redaguj wersję standardową',,$('exec(\\\'edytuj\\\',\\\'#b_report\\\',\\\''+_rpms+'\\\')'),
               'Redaguj wersję zmodyfikowaną',,$('exec(\\\'edytuj\\\',\\\'#b_report\\\',\\\''+_rpmu+'\\\')')
            )
         ?}
      ",
      ,,,,,
      'E',,'icon=edit'
   );
   _tab.win_act(_ws,,'Formuła','Kompiluj'@@,,,
      "  _TAB:=cur_tab(1,1);
         _mask:=__FileLayer.real_name(_TAB.FILENAME,'rpm');
         {? _TAB.sel_size()>0
         || _mask:='['+(1+_mask)+']'+(1-_mask)
         ?};
         rep_tran(_mask)
      ",,,
      1,,
      "FUN.info('Zakończono kompilację wybranych wzorców sprawozdań.'@)",
      'K'
   );

   _edit_btn:=_tab.win_btn(_ws,'text='+'Edytuj'@+_cfg,'menu:E');
   _tab.btn_opt(_edit_btn,'tooltip="%1"'['Edytuj kod wydruku'@]);
   _compile_btn:=_tab.win_btn(_ws,'text='+'Kompiluj'@+_cfg,'menu:K');
   _tab.btn_opt(_compile_btn,'tooltip="%1"'['Kompiluj wydruk'@])
?};
{? _czyDodatek
|| _tab.win_act(_ws,,'Formuła',_i.N,,,_i.F,,,,,,_i.W);
::   _cfg:=',btn_label_align=center,panel=bottom,align=begin';
   _tab.win_btn(_ws,'text='+_i.N+_cfg,'menu:'+_i.W);
:: wyszarzamy akcję dla pustej i niepustej dziedziny
   _tab.win_act(_ws,,'Rekord',,,,
               $("_ag:={? cur_tab(1,1).VERSION*'PEP' || '' || '%1:%1'['"+_i.W+"'] ?};
                   cur_tab(1,1).actions_grayed(cur_win(1,1),_ag)
               "));
   _tab.win_fml(_ws,,'TITLE',,'ICON_BEFORE',
               $("{? cur_tab(1,1).VERSION*'PEP' || '"+_i.I+"' || 'xwin16.png:110' ?}"))
?};

_btnan:=_tab.win_btn(_ws,'text='+'Anuluj'@+_cfg,'key:Esc');
_tab.btn_opt(_btnOK,'default=1,tooltip="%1"'['Wybierz raport do wydrukowania'@]);
_tab.btn_opt(_btnan,'tooltip='+exec('help_red_esc','#window','A'));
_tab.win_sopt(_ws,'select_record_checkbox=0');

_tab.win_sel(_ws);

_we:=_tab.mk_edit('Sprawozdanie'@,,'#red');
_tab.win_esep(_we,'Dane podstawowe'@);
_width:=74;
_tab.win_efld(_we,,'TITLE',,,_width,,,'Tytuł sprawozdania'@,,
   'Tytuł sprawozdania (argument konstrukcji %1'@['{TITLE: }']
);
_tab.win_efld(_we,,'FILENAME',,,_width,,,'Nazwa zbioru dyskowego'@,,
   'Nazwa zbioru dyskowego ze sprawozdaniem (bez rozszerzenia)'@
);
_tab.win_efld(_we,,'VERSION',,,_width,,,'Wersja sprawozdania'@,,
   'Wersja sprawozdania (argument konstrukcji %1)'@['{VERSION: }']
);
{? _isfml
|| _tab.win_efld(_we,,'FORMULA',,,_width,,,'Formuła wstępna'@)
?};
_tab.win_edit(_we);

_wykonano:=0;
_poz:=1;
{!
|? {? _czyDodatek & _i.R='PEP'
   || {? var_pres('_pep')=117 || obj_del(_pep) ?};
      _pep:=obj_new('active','fml_after','P','CALL','CALL2');
      _pep.active:=0;
      _pep.fml_after:="";
      _pep.P:=null();
      {? var_pres('_params')>100
      || _new:=exec('obj_ntab_set','#array',_params,'PEP',_pep);
         {? var_pres('_params')>100 || obj_del(_params) ?};
         &_params;
         _params:=_new;
         obj_del(_new); &_new
      || _params:=obj_new('PEP');
         _params.PEP:=_pep
      ?};
      params_set(_params)
   ?};
   _poz:=_tab.select(,1,_poz,,,_runAuto);
   {? _again<0 & _poz
   || _msk:=_tab.FILENAME;
      0
   |? _again>=0
   || {? _poz
      || {? _isfml
         || ($_tab.FORMULA)()
         ?};
         _wykonano:=rep_exec(_tab.FILENAME,,,_exp_file,_noprinter,_reppar);
         {? var_pres('_pep')=117 & _pep.active & +_pep.fml_after
         || {? var_pres('CALL2',_pep)=type_of(SYSLOG) | var_pres('CALL2',_pep)=type_of(~~)
            || _pep.fml_after(_pep.P,_pep.CALL,_pep.CALL2)
            || _pep.fml_after(_pep.P,_pep.CALL)
            ?}
         ?};
         _again=1
      ?}
   || 0
   ?}
!};
{? _again<0
|| _msk
|? _again=2
|| _wykonano
|| _tab.size()
?}


\wybierz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła odpowiada za uruchomienie zestawu raportów.
::   WE: _a [STRING] - Symbol dziedziny produktowej.
::       _b [STRING] - Identyfikator czynności.
::       _c [STRING] - Nazwa formuły głównej.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;

_id:=__PARSES.clone('S','U');
_pid:=__PARSES.getPID();
{? _id='' | _pid=''
|| FUN.info('Przygotowanie środowiska nie powiodło się.'@);
   return(_ret)
?};

B_DOMAIN.cntx_psh();
B_DOMAIN.index('SYMBOL');
B_DOMAIN.prefix(_a,);
{? B_DOMAIN.first() & B_DOMAIN.FM<>''
|| mb_exec(B_DOMAIN.FM)
?};
__PARSES.pushEnv();
{? __PARSES.setMode('S',_id,,_pid) & __PARSES.setEnv(_b)
||
:: przed uruchomieniem ustawienie kontekstu dokumentacji
   _path:=exec('set_help','#help',_b);
   _jTerm:=app_info('web_sesid')='';
   {? _jTerm || set_help(_path) || web_set_help(_path) ?};
   _ret:=mb_exec(_c)
?};
__PARSES.popEnv();
__PARSES.del(_id);
B_DOMAIN.cntx_pop();
_ret


\edytuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Użytkownikowi specjalnemu udostępnia do edycji szablon sprawozdania
::   WE: _a [STRING] - Nazwa pliku (bez rozszerzenia ale z ewntualnym .m) zawierającego szablon sprawozdania.
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_rpm:=_a;

{? _rpm+2='.m'
|| _std:=_rpm-2;
   {? ~fexists(_rpm+'.rpm',1)
   || fcopy(_std+'.rpm',_rpm+'.rpm',1,1,)
   ?};
   _ret:=txt_edit(_rpm+'.rpm',,,'UTF-8');
:: Czy wersja zmodyfikowana ma sens, czy też powinna zostać usunięta?
   _del:=0;
   _size:=-1;
   _hash:='';
   _fh:=fopen(_rpm+'.rpm','br',1,1,1,1);
   {? _fh.is_open()
   || _size:=_fh.fgetsize();
      _hash:=hash(_fh);
      _fh.fclose()
   ?};
   obj_del(_fh);
   {? _size>0
   || _fh:=fopen(_std+'.rpm','br',1,1,1,1);
      {? _fh.is_open()
      || _del:=_size=_fh.fgetsize() & hash(_fh)=_hash;
         _fh.fclose()
      ?};
      obj_del(_fh)
   ?};
   {? _size=3 | _del
::    Jeżeli plik zawiera jedynie BOM lub jest identyczny z oryginałem, to usuwamy.
   || ferase(_rpm+'.rpm',1)
   ?}

|| _std:=_rpm;
   _ret:=txt_edit(_rpm+'.rpm',,,'UTF-8')
?};

{? _ret & FUN.ask('Czy skompilować szablon sprawozdania?'@)
|| rep_tran(_std)
?};

~~


\decl_reptab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Deklaracja klasy REPTAB do obsługi tabeli z raportami
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('REPTAB',@.CLASS)>0
|| return(~~)
?};

obj_decl('REPTAB',

   obj_fld('SELF',~~),
   obj_fld('tab',~~),

   obj_meth('__init',"
      .SELF:=_a;
      .tab:=tab_tmp(2,
         'TITLE','STRING[80]','Tytuł',
         'FILENAME','STRING[60]','Nazwa pliku',
         'VERSION','STRING[40]','Wersja',
         'FORMULA','STRING[255]','Formuła'
      );
      ~~
   "),

   obj_meth('add',"
      .tab.blank(1);
      .tab.TITLE:=_a;
      .tab.FILENAME:=_b;
      .tab.FORMULA:=$_c;
      .tab.add();
      return(.SELF)
   ",type_of(''),type_of(''),type_of($''))

)


\ctrlMask
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: sprawdza czy podana maska wydruku zawiera się we wzorcowej
::   WE: _a - maska wydruku
::       _b - wzorzec
::   WY: 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
_len:=+_b;
_res:=~((_len+_a)*'*') & (_len+_a)=_b;
_res


\gen_rep_titles
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.22]
:: OPIS: Generuje plik formuł: rep_titles.fml albo rep_titles.m.fml
::       z tytułami wydruków (do wciągnięcia do modułu tłumaczeń)
::   WE: [_a] - czy kompilować wydruki (domyślnie 0 - nie)
::       [_b] - czy generować plik w wersji dev, nawet gdy brak upgrade_??xx.fml (domyślnie - wg upgrade_??xx.fml)
::   WY: liczba tutułów
::----------------------------------------------------------------------------------------------------------------------
:: czy kompilować wydruki
{? var_pres('_a')=type_of(0) || _compile:=_a || _compile:=0 ?};
:: wersja dev, jeżeli jest plik upgrade_??xx.fml
{? var_pres('_b')=type_of(0) || _dev:=_b || _dev:=files('upgrade_??xx.fml').first() ?};

{? _compile || rep_tran() ?};

_tab:=rep_info(,1);
{? ~_tab.first() || return(0) ?};

:: Zadbajmy o prawidłowe końce linii ...
_eol:=%13+%10;
_out:='';
_last:='';
{!
|? {? 1+_tab.FILENAME<>'_' & _tab.FILENAME+1<>'q' & _tab.TITLE<>'' & _last<>_tab.TITLE
   || _last:=_tab.TITLE;
      _out+='''%1''@;%2'[_tab.TITLE,_eol]
   ?};
   _tab.next()
!};
{? _out='' || return(0) ?};
{? _dev
|| _filename:='rep_titles.fml'
|| _filename:='rep_titles.m.fml'
?};
_file:=fopen(_filename,'uw!',1,1,1);
{? _file.is_open()
|| _file.fwrite(
      ':!UTF-8'+_eol+
      ':: (c) Macrologic S.A. Wszelkie prawa zastrzeżone'+_eol+
      '::'+(118*'=')+_eol+
      ':: Nazwa pliku: %1' [_filename]+_eol+
      ':: Autor: <wygenerowany automatycznie>'+_eol+
      '::'+(118*'=')+_eol+
      ':: Zawartość: Tytuły wydruków - PLIK GENEROWANY AUTOMATYCZNIE '
                     '- przy ponownej generacji zmiany zostaną nadpisane!'+_eol+
      '::            W celu wygenerowania pliku, należy uruchomić formułę exec(''gen_rep_titles'',''#b_report'')'+_eol+
      '::'+(118*'=')+_eol+
      (_eol*2)+
      _out+_eol+'~~'
   )
?};
_tab.size()

:Sign Version 2.0 jowisz:1045 2023/08/17 16:30:23 0fa19ef2be4a26d9bbc24843cb104c655eeb67094bad03cb96133c944207025bc62a48fea56766df777ad0d95c25320cab8ad33562eb0a19ecd1fc9693ba5d9fa9c5c337a0ccf17bf029d4493c9d08cccf17df8df5e1a38a262a30c3bddab9dc643267e52f022e873a6e5b3063852d0a1f5858017e7082700cacec1979fe55c4

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #mailbox.fml [17.00]
:: Utworzony: 26.01.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Funkcje obsługi skrzynek wiadomości
::======================================================================================================================


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: Selekcja wiadomości
::  OLD: \em_sel/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
exec('MAIL','#object');

VAR_DEL.delete('__Box','__werb');

__Box:=tab_tmp(1,'LP','INTEGER','Lp','NAZWA','STRING[30]','Nazwa','KOD','STRING[1]','Kod');
__Box.LP:=1; __Box.KOD:='N'; __Box.NAZWA:=exec('box_name','#mailbox',__Box.KOD); __Box.add();
__Box.LP:=2; __Box.KOD:='S'; __Box.NAZWA:=exec('box_name','#mailbox',__Box.KOD); __Box.add();
__Box.LP:=3; __Box.KOD:='D'; __Box.NAZWA:=exec('box_name','#mailbox',__Box.KOD); __Box.add();

__werb:=__Box.mk_sel('Foldery'@,'N',0,'box_wer');
__Box.win_fld(__werb,,'NAZWA',,,,,,'Foldery'@);
__Box.win_fml(__werb,,'NAZWA',,'ICON_BEFORE',"exec('box_icon','#mailbox',__Box.KOD)");

_mode:='normal';
{? exec('interm','#system')
|| _mode:='maximized'
?};

_grp:=__Box.grp_make('Wiadomości'@,"grp_disp(__Box,__werb,0);~~",'box_grp',,,,,_mode);
__Box.grp_sel(_grp,,__werb,,"
   POCZTA.index('DT1');
   POCZTA.prefix(REF.FIRMA,'P',__Box.KOD);
   POCZTA.hdr_sel();
   POCZTA.hdr_sel(__Box.NAZWA);
   POCZTA.actions_grayed('WER',{? __Box.KOD='N' || ':' || 'DPTHABWE:D' ?});
   grp_disp(POCZTA,'WER',0);
   ~~
",,,,,,,,'maximized');
__Box.grp_splt(_grp,,'vertical','skrzynka',',20%');
__Box.grp_sel(_grp,POCZTA,'WER',,,,,25,,,,,'maximized_with_title','WER',1);
__Box.grp_splt(_grp,,'horizontal','panel',',90%');
__Box.grp_edit(_grp,POCZTA,'PANEL',,,,,,'maximized');

exec('icons','#mailbox','WER');

__Box.win_sopt(__werb,'select_record_checkbox=0');

POCZTA.win_edit('REDP');
POCZTA.win_patt('WZOR');

__Box.dnd_sel(__werb,,'records.POCZTA',"
   POCZTA.cntx_psh(); __Box.cntx_psh();
   POCZTA.clear();
   __Box.seek(dnd_info('dest_record'));
   _wybor:=FUN.choice('Zaznaczone rekordy:'@,,'Skopiować'@,'Przenieść'@);
   {? _wybor<>0
   || _tab:=dnd_info('dropped_records');
      {? _tab.first()
      || {!
         |?
            {? POCZTA.seek(_tab.REF,'poczta')
            || POCZTA.BOX:=__Box.KOD;
               {? _wybor=1
               ||
                  _old:=POCZTA.ref();
                  POCZTA.TM_STAMP:=exec('poczta_t','#mailbox');
                  POCZTA.SOURCE:='';
                  POCZTA.PROC:='';
                  POCZTA.INS_CODE:='';
                  {? POCZTA.add()
                  || exec('em_copy','#mailbox',_old,POCZTA.ref())
                  ?}
               ||
                  POCZTA.put()
               ?}
            ?};
            _tab.next()
         !}
      ?}
   ?};
   POCZTA.cntx_pop(); __Box.cntx_pop();
   ~~
");

__Box.win_sel(_grp);
__Box.first();
POCZTA.win_sel('WER');
POCZTA.hdr_sel();
POCZTA.hdr_sel(__Box.NAZWA);
POCZTA.index('DT1');
POCZTAV.PE:='P';
POCZTA.prefix(REF.FIRMA,'P',__Box.KOD);
__Box.select();

VAR_DEL.delete('__Box','__werb');
''


\be_ffrom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Przed redakcją pola POCZTA.FFROM
::  OLD: \be_ffrom/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
1


\be_fsender
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO
:: OPIS: przed redakcją pola POCZTA.FSENDER
::  OLD: \be_fsender/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
1


\be_freplyto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Przed redakcją pola POCZTA.FREPLYTO
::  OLD: \be_freplyto/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
1


\box_icon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Ikona skrzynki
::   WE: _a - kod skrzynki
::  OLD: \box_icon/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='N' || 'xwin16.png:79'
|? _a='S' || 'xwin16.png:77'
|? _a='D' || 'xwin16.png:78'
|| ''
?}


\em_emx_pre
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Przed rekord w oknach tabeli POCZTA
::   WE: _a - parametr systemowy
::  OLD: \em_emx_pre/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a & 3+cur_win(1,1)='SEL'
|| POCZTA.actions_grayed(cur_win(1,1),{? POCZTA.BOX='N' || ':' || 'DPTHABWE:D' ?})
?};
exec('rekprzed','color','POCZTA#')


\em_rkprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Przed rekord w oknach tabeli POCZTA - faktyczna akcja
::  OLD: \em_rkprz/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? POCZTA.PE='P'
|| exec('em_tm_stamp','#mailbox');
   POCZTAV.BOX:=exec('box_name','#mailbox',POCZTA.BOX);
   {? POCZTA.COUNTER<exec('get','#params',700004,1)
   || 'POCZTA#01#01'
   || 'POCZTA#01#02'
   ?}
|? POCZTA.PE='E'
|| 'POCZTA#02#01'
|? POCZTA.PE='Z'
|| {? POCZTA.RESULT='X'
   || 'POCZTA#03#02'
   |? POCZTA.RESULT='T'
   || 'POCZTA#03#03'
   |? POCZTA.RESULT='N'
   || 'POCZTA#03#04'
   || 'POCZTA#03#01'
   ?}
?}


\em_eml_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: Obsługa na 'Dołącz' w oknie wertowania wiadomości
::  OLD: \em_eml_d/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTA.blank();
exec('em_tm_stamp','#mailbox');
POCZTA.win_edit('REDP');
POCZTA.efld_opt('REDP',{? exec('get','#params',700002,2)='' || 'mark=1' || 'mark=0' ?},,'FFROM');
{? POCZTA.memo_set('','BODYT') & POCZTA.memo_set('','BODYH') & POCZTA.edit("exec('chk_eml','#mailbox')")
|| {? POCZTA.add()
   || POCZTA.memo_put(,'BODYT');
      POCZTA.memo_put(,'BODYH');
      exec('em_srcv','#mailbox')
   ?}
?}


\em_eml_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: obsługa na 'Popraw' w oknie wertowania wiadomości
::  OLD: \em_eml_p/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~POCZTA.r_lock(1,1,1)
|| FUN.emsg('Poprawienie nie jest możliwe.\nRekord jest redagowany lub przetwarzany.'@)
|| POCZTA.win_edit('REDP');
   POCZTA.efld_opt('REDP',{? exec('get','#params',700002,2)='' || 'mark=1' || 'mark=0' ?},,'FFROM');
   {? POCZTA.memo_get(,'BODYT') & POCZTA.memo_get(,'BODYH') & POCZTA.edit("exec('chk_eml','#mailbox')")
   || {? POCZTA.put()
      || POCZTA.memo_put(,'BODYT');
         POCZTA.memo_put(,'BODYH')
      ?}
   ?};
   POCZTA.r_unlock()
?}


\em_eml_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: obsługa na 'Usuń' w oknie wertowania wiadomości
::  OLD: \em_eml_u/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__GRUPA')<0 || __GRUPA:=0 ?};
{? ~POCZTA.r_lock(1,1,1)
|| {? ~__GRUPA
   || FUN.emsg('Usunięcie nie jest możliwe.\nZapis jest redagowany lub przetwarzany.'@)
   ?}
|| {? __GRUPA & POCZTA.SOURCE='' |
      ~__GRUPA & {? POCZTA.SOURCE=''
                 || FUN.ask('Czy usunąć bieżący zapis?'@)
                 || FUN.ask(
                       'Wiadomość powiązana z zapisem źródłowym "%1".\n'
                       'Czy na pewno usunąć?'@[exec('record','#to_string',POCZTA.SOURCE)]
                    )
                 ?}
   || {? __Box.KOD='D'
      ||
::       Skrzynka 'elementy usunięte' więc faktyczne usuwanie
         POCZTAR.index('P');
         POCZTAR.prefix(POCZTA.ref());
         {? POCZTAR.first()
         || {! |? POCZTAR.del() !}
         ?};
         POCZTAA.index('P');
         POCZTAA.prefix(POCZTA.ref());
         {? POCZTAA.first()
         || {! |? POCZTAA.del() !}
         ?};
         ferase(exec('ilog_name','#mailbox',POCZTA.ref()),1);
         POCZTA.r_unlock();
         {? __GRUPA || __DELETED+=1 ?};
         POCZTA.del()
      ||
::       Przesunięcie do skrzynki 'elementy usunięte'
         POCZTA.cntx_psh();
         {? POCZTA.next() || _goto:=POCZTA.ref() |? POCZTA.prev() || _goto:=POCZTA.ref() || _goto:=null() ?};
         POCZTA.cntx_pop();
         POCZTA.cntx_psh();
         POCZTA.prefix();
         POCZTA.BOX:='D';
         {? __GRUPA || __DELETED+=1 ?};
         POCZTA.put();
         POCZTA.cntx_pop();
         POCZTA.seek(_goto)
      ?}
   || POCZTA.r_unlock()
   ?}
?}


\em_usu_gpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.70]
:: OPIS: Przed grupą dla usuwania wiadomości
::  OLD: \em_usu_gpr/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone zapisy?'@)
|| __GRUPA:=1;
   __ZNACZONE:=POCZTA.sel_size();
   __DELETED:=0;
   1
|| 0
?}


\em_usu_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.70]
:: OPIS: Po grupie dla usuwania wiadomości
::  OLD: \em_usu_gpo/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? __ZNACZONE=__DELETED
|| FUN.info('Usunięto zaznaczone zapisy.'@)
|| FUN.emsg('Ilość zaznaczonych zapisów: %1.\nIlość usuniętych zapisów: %2.'@[$__ZNACZONE,$__DELETED])
?};
&__GRUPA;
&__ZNACZONE;
&__DELETED;
~~


\em_przesun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Przesunięcie wiadomości do innej skrzynki
::  OLD: \em_przesun/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_wer:=__Box.mk_sel('Przesuń wiadomość do folderu'@,'N',0,'box_przesun1',,,10);
__Box.win_fld(_wer,,'NAZWA',,,40,,,' '@);
__Box.win_act(_wer,,'Formuła','Dalej'@@,,,"sel_exit()",,1,,,,'D');
__Box.win_fml(_wer,,'NAZWA',,'ICON_BEFORE',"exec('box_icon','#mailbox',__Box.KOD)");
__Box.win_sel(_wer);
{? POCZTA.sel_size()=0 || __Box.cntx_psh() ?};
{? POCZTA.sel_size()>0 | __Box.select()
|| POCZTA.cntx_psh();
   POCZTA.clear();
   POCZTA.BOX:=__Box.KOD;
   POCZTA.put();
   POCZTA.cntx_pop()
?};
{? POCZTA.sel_size()=0 || __Box.cntx_pop() ?};
~~


\em_przes_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Przesunięcie wiadomości do innej skrzynki - przed grupa
::  OLD: \em_przes_bg/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_wer:=__Box.mk_sel('Przesuń wiadomości do folderu'@,'N',0,'box_przesunx',,,10);
__Box.win_fld(_wer,,'NAZWA',,,40,,,' '@);
__Box.win_act(_wer,,'Formuła','Dalej'@@,,,"sel_exit()",,1,,,,'D');
__Box.win_fml(_wer,,'NAZWA',,'ICON_BEFORE',"exec('box_icon','#mailbox',__Box.KOD)");
__Box.win_sel(_wer);
__Box.cntx_psh();
{? __Box.select() || 1 || 0 ?}


\em_przes_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Przesunięcie wiadomości do innej skrzynki - po grupie
::  OLD: \em_przes_ag/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
__Box.cntx_pop();
~~


\em_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Kopiowanie wiadomości do innej skrzynki
::  OLD: \em_kopiuj/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_wer:=__Box.mk_sel('Kopiuj wiadomość do folderu'@,'N',0,'box_kopiuj1',,,10);
__Box.win_fld(_wer,,'NAZWA',,,40,,,' '@);
__Box.win_act(_wer,,'Formuła','Dalej'@@,,,"sel_exit()",,1,,,,'D');
__Box.win_fml(_wer,,'NAZWA',,'ICON_BEFORE',"exec('box_icon','#mailbox',__Box.KOD)");
__Box.win_sel(_wer);
{? POCZTA.sel_size()=0 || __Box.cntx_psh() ?};
{? POCZTA.sel_size()>0 | __Box.select()
|| _old:=POCZTA.ref();
   POCZTA.cntx_psh();
   POCZTA.clear();
   POCZTA.BOX:=__Box.KOD;
   POCZTA.TM_STAMP:=exec('poczta_t','#mailbox');
   POCZTA.SOURCE:='';
   POCZTA.COUNTER:=0;
   POCZTA.SENDINFO:='N';
   POCZTA.TRYB:='N';
   POCZTA.PROC:='';
   POCZTA.INS_CODE:='';
   {? POCZTA.add()
   || exec('em_copy','#mailbox',_old,POCZTA.ref())
   ?};
   POCZTA.cntx_pop()
?};
{? POCZTA.sel_size()=0 || __Box.cntx_pop() ?};
~~


\em_kop_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Kopiowanie wiadomości do innej skrzynki - przed grupą
::  OLD: \em_kop_bg/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_wer:=__Box.mk_sel('Kopiuj wiadomości do folderu'@,'N',0,'box_kopiujx',,,10);
__Box.win_fld(_wer,,'NAZWA',,,40,,,' '@);
__Box.win_act(_wer,,'Formuła','Dalej'@@,,,"sel_exit()",,1,,,,'D');
__Box.win_fml(_wer,,'NAZWA',,'ICON_BEFORE',"exec('box_icon','#mailbox',__Box.KOD)");
__Box.win_sel(_wer);
__Box.cntx_psh();
{? __Box.select() || 1 || 0 ?}


\em_kop_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Kopiowanie wiadomości do innej skrzynki - po grupie
::  OLD: \em_kop_ag/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
__Box.cntx_pop();
~~


\em_eml_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: obsługa 'Treść' w oknie wertowania wiadomości
::  OLD: \em_eml_t/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~POCZTA.r_lock(1,1,1)
|| FUN.emsg('Redagowanie treści nie jest możliwe.\nRekord jest redagowany lub przetwarzany.'@)
||
   _title:='Wiadomość — %1'@[POCZTA.SUB];
   _txt:=POCZTA.memo_txt(,1,'BODYT');
   _txt:=exec('edit_memo','#edit',_txt,'Treść tekstowa'@,_title,120,20,,0);
   {? type_of(_txt)=type_of('')
   || POCZTA.memo_set(_txt,'BODYT');
      POCZTA.memo_put(,'BODYT')
   ?};

   POCZTA.r_unlock()
?};
~~


\em_eml_h
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: obsługa 'Html' w oknie wertowania wiadomości
::  OLD: \em_eml_h/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~POCZTA.r_lock(1,1,1)
|| FUN.emsg('Redagowanie treści nie jest możliwe.\nRekord jest redagowany lub przetwarzany.'@)
||
   _title:='Wiadomość — %1'@[POCZTA.SUB];
   _txt:=POCZTA.memo_txt(,1,'BODYH');
::   _txt:=exec('edit_memo','#edit',_txt,'Treść HTML'@,_title,120,20,,0);
::   {? type_of(_txt)=type_of('')
::   || POCZTA.memo_set(_txt,'BODYH');
::      POCZTA.memo_put(,'BODYH')
::   ?};
   exec('edit_html_sysmemo','#edit',POCZTA,'BODYH','mail_edit',,'Treść HTML'@);
   POCZTA.r_unlock()
?};
~~


\em_eml_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: obsługa na 'Wyświetl' w oknie wertowania wiadomości
::  OLD: \em_eml_w/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTA.win_edit('REDP');
{? POCZTA.memo_get(,'BODYT') & POCZTA.memo_get(,'BODYH')
|| POCZTA.display()
?};
~~


\em_eml_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Podgląd wiadomości HTML w przeglądarce
::       Uwaga: tylko jeden 'cid:' w wierszu
::   WE: [_a] - INTEGER - [1]/2 - czym ma być otwierany podgląd:
::                                    1 - przeglądarka webbrowser2 (domyślnie)
::                                    2 - edytor HTML
::       [_b] - kontekst wywołania - [1]/2/3
::                                    1 - rekord POCZTA (podgląd wiadomości)
::                                    2 - rekord POCZTATL (podgląd wzorca wypełnionego przykładową treścią)
::                                    3 - plik wzorca (podgląd wzorca wypełnionego przykładową treścią) wymagane _c
::       [_c] - nazwa pliku wzorca wg pth
::
::  OLD: \em_eml_g/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_viewmode:=1;
{? var_pres('_a')=type_of(0)
|| _viewmode:=_a
?};

_context:=1;
{? var_pres('_b')=type_of(0)
|| _context:=_b
?};

_filename:='';
{? var_pres('_c')=type_of('')
|| _filename:=_c
?};

{? _context=1
|| _sub:=POCZTA.SUB;
:: Sprawdzenie, czy pusta wiadomość i brak załączników
   {? POCZTA.memo_txt(,1,'BODYT')='' & POCZTA.memo_txt(,1,'BODYH')=''
   || FUN.info('Brak treści — podgląd wiadomości nie jest dostępny.'@);
      return()
   ?};
:: Ciało wiadomości - o ile html, to podmiana tagów, o ile txt, to osadzenie w <pre>
   _body:=POCZTA.memo_txt(,1,'BODYH');
   {? _body=''
   || _body:='<pre>%1</pre>'[POCZTA.memo_txt(,1,'BODYT')]
   || _body:=exec('body_tags','#email',_body)
   ?}
|| _sub:='Lorem ipsum dolor sit amet, consectetur adipiscing elit';
   _body:=
      '<p>Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et '
      'dolore magna aliqua. Ut enim ad minim veniam, quis nostrud exercitation ullamco laboris nisi ut aliquip ex ea '
      'commodo consequat. Duis aute irure dolor in reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla '
      'pariatur. Excepteur sint occaecat cupidatat non proident, sunt in culpa qui officia deserunt mollit anim id est '
      'laborum.</p>'
      '<p>Zażółć gęślą jaźń.</p>
      [[BODY_BUTTON(http://macrologic.pl,Lorem ipsum,Lorem ipsum)]]';
   _body:=exec('body_tags','#email',_body)
?};

POCZTAA.cntx_psh();
POCZTAA.index('P');
POCZTAA.prefix({? _context=1 || POCZTA.ref() || null() ?});

_files:=tab_tmp(1,'FILENAME','STRING[255]','Nazwa pliku'@);
_fname:='eml.html';
_html:=fopen(_fname,'Uw',1,,1);
{? _html.is_open()
||
:: Przepisanie pliku template.htm do pliku wynikowego
   {? _context=1
   || _template_name:=
         {? POCZTA.TEMPLATE=''
         || _default_template:=exec('get','#params',700014);
            {? _default_template=''
            || 'template_info.htm'
            || _default_template
            ?}
         || POCZTA.TEMPLATE
         ?}
   |? _context=2
   || _template_name:=POCZTATL.NAME
   |? _context=3
   || _template_name:=_filename
   ?};

::   _template:=fopen(_template_name,'ur',1,,1);
   _template:=exec('template_file','#mailbox',_template_name);

:: Załączniki ==========================================================================================================
:: Załączniki dołączone - tekst do podmiany w tagu [[MAIL_ATTACHMENTS]]
   _pliki:=0;
   _mail_att:='';
   {? POCZTAA.first()
   || {!
      |? {? POCZTAA.ATT_EMB='A' || _pliki+=1 ?};
         POCZTAA.next()
      !};
      {? _pliki>0 || _mail_att+='<p>' ?}
   ?};

:: Wszystkie załączniki (osadzone i dołączone):
:: - przekopiowanie na terminal
:: - utworzenie linków do dołączonych plików
   {? POCZTAA.first()
   || {!
      |? _name:=POCZTAA.bl_info('FILE','NAME');
         {? POCZTAA.bl_get('FILE',exec('tmp_filepath','#file',_name,1),0)
         || {? POCZTAA.ATT_EMB='A' || _mail_att+='<a href="%1" target="_blank">%1</a><br>'[_name] ?}
         ?};
         POCZTAA.next()
      !}
   ?};

:: Załączniki dołączone - znacznik zamykający
   {? _pliki>0 || _mail_att+='</p>' ?};
::======================================================================================================================

   {? _template.is_open()
   || {!
      |? _line:=_template.fread();
         _next:=_line<>'\n';
         {? _next
         || _tresc:=_line;
::          Wyciągnięcie nazw plików ze stałych cid-ów wzorca
            _pos:=_tresc*'"cid:';
            {? _pos>0
            || _tresc:=(_pos+4)-_tresc;
               _files.FILENAME:=((_tresc*'"')-1)+_tresc;
               _files.add()
            ?};
            _line:=gsub(_line,'cid:','');

::          Podmiana tagów =============================================================================================
::          [[MAIL_STYLE]] z pliku style.css albo z parametru 700015
            {? _line*'[[MAIL_STYLE]]'>0
            || _style:=exec('style','#email');
               _line:=gsub(_line,'[[MAIL_STYLE]]',_style)
            ?};
::          [[MAIL_LOGO_H]] logo "górne"
            {? _line*'[[MAIL_LOGO_H]]'>0
            || {? exec('get','#params',700016,2)='T' || _blob:=exec('get_logo_h','#blob') || _blob:=0 ?};
               {? _blob
               || exec('bl_get','#blob',_blob)
               || _name:=exec('head_logo','#email');
                  _f_name:=exec('tmp_filepath','#file',_name,1);
                  fcopy(_name,_f_name,1,0)
               ?};
               _firma_logo_head:={? _blob || exec('bl_info','#blob',_blob,'NAME') || '' ?};
               _mail_logo_h:='<a href="%1"><img src="%2" alt="%3" style="border-width: 0"></a>'
                  [  exec('firma_www','#email'),
                     {? _firma_logo_head='' || exec('head_logo','#email') || _firma_logo_head ?},
                     exec('firma_name','#email')
                  ];
               _line:=gsub(_line,'[[MAIL_LOGO_H]]',_mail_logo_h)
            ?};
::          [[MAIL_HEADLINE]] bezpośrednio temat maila
            _line:=gsub(_line,'[[MAIL_HEADLINE]]',_sub);
::          [[MAIL_ICON_*]] ikona funkcyjna konkretna - zakładamy istnienie plików mail_icon_*.png
            _t1:='[[MAIL_ICON_'; _pos1:=_line*_t1;
            {? _pos1>0
            || _t2:=(_pos1-1)-_line; _pos2:=_t2*']]';
               _tag:=(_pos2+1)+_t2;
               _name:='%1.png'[2-_tag-2];
               {? fexists(_name,1)=0 || _name:='mail_icon_info.png' ?};
               _f_name:=exec('tmp_filepath','#file',_name,1);
               fcopy(_name,_f_name,1,0);
               _mail_icon:='<img src="%1" alt="Info" style="display: block;" />'[_name];
               _line:=gsub(_line,_tag,_mail_icon)
            ?};
::          [[MAIL_BODY]] ciało wiadomości
            {? _line*'[[MAIL_BODY]]'>0
            || _line:=gsub(_line,'[[MAIL_BODY]]',_body)
            ?};
::          [[MAIL_FOOTER]] z parametru 700013
            _line:=gsub(_line,'[[MAIL_FOOTER]]',exec('footer','#email'));
::          [[MAIL_LOGO]] logo
            {? _line*'[[MAIL_LOGO]]'>0
            || _name:='mail_logo_mini.png';
               _f_name:=exec('tmp_filepath','#file',_name,1);
               fcopy(_name,_f_name,1,0);
               _mail_logo:='<img src="%1" alt="Logo" style="display: block;" />'[_name];
               _line:=gsub(_line,'[[MAIL_LOGO]]',_mail_logo)
            ?};
::          [[MAIL_LOGO_F]] logo "dolne"
            {? _line*'[[MAIL_LOGO_F]]'>0
            || {? exec('get','#params',700017,2)='T' || _blob:=exec('get_logo_f','#blob') || _blob:=0 ?};
               {? _blob
               || exec('bl_get','#blob',_blob)
               || _name:=exec('erp_logo','#email');
                  _f_name:=exec('tmp_filepath','#file',_name,1);
                  fcopy(_name,_f_name,1,0)
               ?};
               _firma_logo_foot:={? _blob || exec('bl_info','#blob',_blob,'NAME') || '' ?};
               _mail_logo_f:='<a href="%1"><img src="%2" alt="%3" style="border-width: 0"></a>'
                  [  {? _firma_logo_foot='' || exec('erp_www','#email') || exec('firma_www','#email') ?},
                     {? _firma_logo_foot='' || exec('erp_logo','#email') || _firma_logo_foot ?},
                     {? _firma_logo_foot='' || exec('erp_name','#email') || exec('firma_name','#email') ?}
                  ];
               _line:=gsub(_line,'[[MAIL_LOGO_F]]',_mail_logo_f)
            ?};
::          [[MAIL_ATTACHMENTS]] osobna sekcja na listę załączników
            _line:=gsub(_line,'[[MAIL_ATTACHMENTS]]',_mail_att);
::          ============================================================================================================

::          wynikowy wiersz do wynikowego pliku
            _html.fwrite(_line)
         ?};
         _next
      !};
      _template.fclose()
   ?};

:: Przekopiowanie utworzonej strony na terminal
   __fname:=exec('tmp_filepath','#file',_fname);
   _can_continue:=fcopy(_fname,__fname,1,0,1);

:: Uruchomienie w kontrolce webbrowser
   {? _viewmode=1
   ||
      _win_acr:=POCZTA.mk_ctr('Wiadomość — %1'@[_sub],'#ctrl_browser');
      POCZTA.win_ctr(_win_acr);
      POCZTA.win_cctr(_win_acr,'ctr_id','@webframe');
      POCZTA.control(,,"
         ctr_set(,'ctr_id','navigate','%1'[__fname])
      ")
   |? _viewmode=2
   ||
::    Uruchomienie w edytorze HTML
      exec('edit_html_file','#edit',__fname,'mail_view','Wiadomość — %1'@[_sub],0,0)
   ?};
   &__fname
|| FUN.error('Brak dostępu do pliku %1.'@[_fname])
?};

POCZTAA.cntx_pop();
~~


\em_attach_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.28]
:: OPIS: 'Załączniki -> Pliki dołączone'
::----------------------------------------------------------------------------------------------------------------------
exec('em_attach','#mailbox','A')


\em_attach_e
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.28]
:: OPIS: 'Załączniki -> Pliki osadzone'
::----------------------------------------------------------------------------------------------------------------------
exec('em_attach','#mailbox','E')


\em_attach
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60]
:: OPIS: 'Załączniki' w oknach wertowania tabeli POCZTA
::   WE: _a - 'A' pliki dołączone, 'E' - pliki osadzone
::  OLD: \em_attach/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? POCZTA.r_lock(1,1,1)
|| POCZTAA.index('P');
   POCZTAA.prefix(POCZTA.ref(),_a);
   _fml_edit:="
      params_set(params_get());
      _env:=params_get().env_blob;
      _args:=_a;
      _buffer:=_b;
      _action:=_c;
      _uid:=_d;
      {? _action='Popraw'
      || _env.act_popraw(_uid)
      || _buffer.POCZTA:=_args.POCZTA;
         _buffer.ATT_EMB:=_args.ATT_EMB;
         1
      ?}
   ";
   _fml_file:="";
   _fml_valid:="
      _filename:=_c;
      _dialog:=_e;
      _result:=1;
      _env:=params_get().env_blob;
      {? _env.has_file(_filename)>0
      || _result:=0;
         _msg:='Plik o nazwie: %1 jest już dodany jako załącznik i nie może zostać dodany ponownie.'@[_filename];
         {? _dialog=0
         || FUN.emsg(_msg)
         || KOMM.add(_msg,2,,1)
         ?}
      ?};
      _result
   ";
   _args:=obj_new('POCZTA','ATT_EMB'); _args.POCZTA:=POCZTA.ref(); _args.ATT_EMB:=_a;
   exec('edit_blob','#edit',POCZTAA,'FILE',exec('POCZTAA','#buffer'),_fml_edit,_fml_file,_args,POCZTA.BOX='N',
      {? _a='E' || 'Pliki osadzone'@ || 'Pliki dołączone'@ ?},'table',,,_fml_valid
   );
   POCZTA.r_unlock()
|| FUN.emsg('Redagowanie listy załączników nie jest możliwe.\nRekord jest redagowany lub przetwarzany.'@)
?};
~~


\em_send1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: Wysyłanie wiadomości (akcja w oknie)
::  OLD: \em_send1/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('sendmail_on','#mailbox',1)
||
   _sub:=POCZTA.SUB;

   POCZTAR.index('LP');
   POCZTAR.prefix(POCZTA.ref());
   {? ~POCZTAR.first()
   || {? POCZTA.sel_size()>0
      || KOMM.add('Brak odbiorców wiadomości: %1'@[_sub],2)
      || FUN.info('Brak odbiorców wiadomości.'@)
      ?}

   |? POCZTA.COUNTER>=exec('get','#params',700004,1)
   || {? POCZTA.sel_size()>0
      || KOMM.add('Liczba prób wysłania wiadomości została przekroczona: %1'@[_sub],2)
      || FUN.info('Liczba prób wysłania wiadomości została przekroczona.\n\n'
                      'Użyj funkcji \'Resetuj\', aby umożliwić wysłanie.'@)
      ?}

   |? POCZTA.TRYB='T' | POCZTA.TRYB='X'
   || {? exec('em_send_one','#mailbox',POCZTA.ref(),POCZTA.sel_size()>0)>0
      ||
         {? POCZTA.sel_size()>0
         || KOMM.add('Wiadomość wysłana: %1'@[_sub],1)
         ?}
      ||
         {? POCZTA.sel_size()>0
         || KOMM.add('Wiadomość nie została wysłana: %1'@[_sub],2)
         || FUN.emsg('Wiadomość nie została wysłana.'@)
         ?}
      ?}
   || {? POCZTA.sel_size()>0
      || KOMM.add('Wiadomość zablokowana: %1'@[_sub],52)
      || FUN.info('Wiadomość zablokowana.\n\nUżyj funkcji \'Aktywuj\', aby umożliwić wysłanie.'@)
      ?}
   ?}
?};
~~


\em_send_gpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60]
:: OPIS: Przed grupą 'Wyślij' w oknie POCZTA.WERP
::   WY: 0 / 1
::  OLD: \em_send_gpr/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('sendmail_on','#mailbox',1)
|| __SENT:=0;
   _size:=POCZTA.sel_size();
   _res:={? _size>1
         || FUN.ask('Czy chcesz wysłać %1 wiadomości?'@[$_size])
         || FUN.ask('Czy chcesz wysłać zaznaczoną wiadomość?'@)
         ?};
   {? _res
   || __SENT:=0;
      KOMM.init(150,,'Wysyłanie wiadomości'@,'');
      KOMM.sect_beg('Wysyłanie wiadomości.'@)
   ?}
|| _res:=0
?};
_res


\em_send_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60]
:: OPIS: Po grupie 'Wyślij' w oknie POCZTA.WERP
::  OLD: \em_send_gpo/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? __SENT=1
|| KOMM.update(1,'Wysłano jedną wiadomość.'@)
|? __SENT>1
|| KOMM.update(1,'Wysłano %1 wiadomości.'@[$__SENT])
|| KOMM.update(1,'Nie wysłano wiadomości.'@)
?};
&__SENT;
KOMM.select();
~~


\em_send_one
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: Wysyłanie pojedynczej wiadomości
::   WE: _a - POCZTA.ref() lub $POCZTA.ref()
::       [_b] - bez dialogów (1), z dialogami (0 - domyślnie)
::       [_c] - obj_new('RAPORT') - w przypadku nieudanej wysyłki dopisywany jest wiersz do raportu
::       [_d] - obj_new() - obiekt z danymi serwera SMTP
::   WY: 0 / 1
::  OLD: \em_send_one/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? type_of(_a)=type_of('') | type_of(_a)=type_of(null()) || _ref:=_a ?};
{? var_pres('_b')=type_of(0) || _nomsg:=_b || _nomsg:=0 ?};
{? cli_ver()='cgi' || _nomsg:=0 ?};

{? var_pres('_c')=exec('type_of_array','#var') || _raport:=_c || _raport:=obj_new('RAPORT'); _raport.RAPORT:='' ?};

{? var_pres('_d')=exec('type_of_array','#var') || _server:=_d || _server:=~~ ?};

POCZTA.cntx_psh();
POCZTA.clear();

:: rekord mógł zostać usunięty
{? ~POCZTA.seek(_ref) || POCZTA.cntx_pop(); return(0) ?};
:: rekord jest przetwarzany
{? ~POCZTA.r_lock(1,1,1) || POCZTA.cntx_pop(); return(0) ?};
:: blokada Rekordu powiązanego aby można było ustawić stan wysyłki
{? exec('source_lock','#mailbox')=0 ||  POCZTA.r_unlock(); POCZTA.cntx_pop(); return(0) ?};

_details:=exec('proc_details','#mailbox');
_proc_details:={? +_details>0 || '\n'+_details || '' ?};

:: licznik prób wysyłania został przekroczony
{? POCZTA.COUNTER>=exec('get','#params',700004,1)
|| exec('source_set_info','#mailbox','Wysyłka nie udana '+$date()+' '+time()$3,'N');
   exec('source_unlock','#mailbox');
   {? POCZTA.SENDINFO<>'T'
   || _raport.RAPORT+=(19+(tm_form(POCZTA.TM_STAMP)))+' — '+POCZTA.SUB+
         ': ilość prób wysłania wiadomości została przekroczona.'+_proc_details+'\n'
   ?};
   POCZTA.r_unlock();
   POCZTA.cntx_pop();
   return(0)
?};

_eml:=obj_new(3); _eml[1]:='POCZTA'; _eml[2]:=POCZTA.ref(); _eml[3]:='BODYT';
_html:=obj_new(3); _html[1]:='POCZTA'; _html[2]:=POCZTA.ref(); _html[3]:='BODYH';

POCZTAA.index('P');
POCZTAA.prefix(POCZTA.ref());
{? POCZTAA.first()
|| _file:=tab_tmp(1,
      'FILE','STRING[255]','Plik',
      'ATT_EMB','STRING[1]','Attached/embed',
      'ID','STRING[255]','Identyfikator'
   );
   {!
   |?
      {? POCZTAA.FILE<>null()
      || _pth:=pth_dir('test.emx');
         {? sys_name(1)='WINDOWS'
         || _pth+='\\'+POCZTAA.FILE().NAME
         || _pth+='/'+gsub(STR.maz2nop(POCZTAA.FILE().NAME),' ','_')
         ?};
         {? POCZTAA.bl_get('FILE',_pth,0)
         || _file.FILE:=_pth;
            _file.ATT_EMB:=POCZTAA.ATT_EMB;
            {? sys_name(1)='WINDOWS'
            || _file.ID:=POCZTAA.FILE().NAME
            || _file.ID:=gsub(STR.maz2nop(POCZTAA.FILE().NAME),' ','_')
            ?};
            _file.add()
         || _raport.RAPORT+=(19+(tm_form(POCZTA.TM_STAMP)))+' — '+POCZTA.SUB+
               ': problem z pobraniem załączników.'+_proc_details+'\n\n';
            exec('source_unlock','#mailbox');
            POCZTA.r_unlock();
            POCZTA.cntx_pop();
            return(0)
         ?}
      ?};
      POCZTAA.next()
   !}
|| _file:=''
?};
_rcv:=tab_tmp(1,'LP','INTEGER','Lp','ADRES','STRING[100]','Adres');
_cc:=tab_tmp(1,'LP','INTEGER','Lp','ADRES','STRING[100]','Adres');
_bcc:=tab_tmp(1,'LP','INTEGER','Lp','ADRES','STRING[100]','Adres');
_rcv1:=_cc1:=_bcc1:='';
_lp:=_lpcc:=_lpbcc:=0;
_ok:=0;

POCZTAR.index('LP');
POCZTAR.prefix(POCZTA.ref());
{? ~POCZTAR.first()
|| {? _nomsg
   || KOMM.add('Brak odbiorców.'@)
   || FUN.emsg('Brak odbiorców.'@)
   ?};
   _raport.RAPORT+=(19+(tm_form(POCZTA.TM_STAMP)))+' — '+POCZTA.SUB+': brak odbiorców.'+_proc_details+'\n\n';
   exec('source_unlock','#mailbox');
   POCZTA.r_unlock();
   POCZTA.cntx_pop();
   return(0)
|| {!
   |?
      {? POCZTAR.SRCV=''
      ||
         {? _nomsg
         || KOMM.add('Pusty adres e-mail odbiorcy.'@)
         || FUN.emsg('Pusty adres e-mail odbiorcy.'@)
         ?};
         _raport.RAPORT+=(19+(tm_form(POCZTA.TM_STAMP)))+' — '+POCZTA.SUB+': pusty adres e-mail odbiorcy.'
            +_proc_details+'\n\n';
         exec('source_unlock','#mailbox');
         POCZTA.r_unlock();
         POCZTA.cntx_pop();
         return(0)
      |? ~exec('mail_ok','#email',POCZTAR.SRCV)
      ||
         {? _nomsg
         || KOMM.add('Niepoprawny adres e-mail odbiorcy: %1.'@[POCZTAR.SRCV])
         || FUN.emsg('Niepoprawny adres e-mail odbiorcy: %1.'@[POCZTAR.SRCV])
         ?};
         _raport.RAPORT+=(19+(tm_form(POCZTA.TM_STAMP)))+' — '+POCZTA.SUB+': niepoprawny adres e-mail odbiorcy: '
            +POCZTAR.SRCV+'.'+_proc_details+'\n\n';
         exec('source_unlock','#mailbox');
         POCZTA.r_unlock();
         POCZTA.cntx_pop();
         return(0)
      ?};

      {? POCZTAR.R='T'
      || _lp+=1;
         {? _lp=1 || _rcv1:=POCZTAR.SRCV+'... ' ?};
         _rcv.LP:=_lp;
         _rcv.ADRES:=POCZTAR.SRCV;
         _rcv.add()
      |? POCZTAR.R='C'
      || _lpcc+=1;
         {? _lpcc=1 || _cc1:=POCZTAR.SRCV+'... ' ?};
         _cc.LP:=_lpcc;
         _cc.ADRES:=POCZTAR.SRCV;
         _cc.add()
      |? POCZTAR.R='B'
      || _lpbcc+=1;
         {? _lpbcc=1 || _bcc1:=POCZTAR.SRCV+'... ' ?};
         _bcc.LP:=_lpbcc;
         _bcc.ADRES:=POCZTAR.SRCV;
         _bcc.add()
      ?};
      POCZTAR.next()
   !}
?};

_txt:='Wysyłam: '+POCZTA.SUB+'; do: '+_rcv1+_cc1+_bcc1;

{? var_pres('ECHO') >=0 || ECHO.display(_txt) || echo(_txt) ?};

_from:=POCZTA.FFROM;
{? _from=''
||
   _email:=exec('get','#params',700002,2);
   {? ~exec('mail_ok','#email',_email)
   ||
      {? _nomsg
      || KOMM.add('Niepoprawny adres e-mail autora: %1 — przyjęty zostanie autor systemowy.'@[_email])
      || FUN.emsg('Niepoprawny adres e-mail autora: %1\nPrzyjęty zostanie autor systemowy.'@[_email])
      ?};
      _from:=''
   || _from:=_email
   ?}
?};

_args:=MAIL.Send_a();
_args.To:=_rcv;
_args.CC:=_cc;
_args.BCC:=_bcc;
_args.Subject:=POCZTA.SUB;
_args.Sender:=POCZTA.FSENDER;
_args.Text:=_eml;
_args.Html:=_html;
_args.Attach:=_file;
_args.Return:=(POCZTA.RETURN='T');
_args.Reply_to:=POCZTA.FREPLYTO;
_args.From:=_from;
_args.ID:=exec('eml_id','#mailbox',POCZTA.ref());
_args.HEADTAGS:=POCZTA.memo_txt(,1,'HEADTAGS');
_args.Template:=
   {? POCZTA.TEMPLATE=''
   || _default_template:=exec('get','#params',700014);
      {? _default_template=''
      || 'template_info.htm'
      || _default_template
      ?}
   || POCZTA.TEMPLATE
   ?};
_args.Server:=_server;

_err:=MAIL.Send(_args);
{? _err<>0
|| {? _nomsg
   || KOMM.add('Błąd:'@+' '+gsub(MAIL.Result(),'\n\n',' — '),14)
   || {? _err>=10 | _err<=-4
      || FUN.info(MAIL.Result())
      |? exec('get','#params',700007,2)='I'
      || FUN.info(MAIL.Result()+'\n\n'+
            'Szczegółowe raporty:\n'
            '• raport z wysyłania wiadomości e-mail (przycisk [Raport]).\n'
            '• raport z przetwarzania wiadomości e-mail (przycisk [Raporty]).'@)
      || FUN.info(MAIL.Result()+'\n\n'+'Szczegółowe raporty — przycisk [Raporty].'@)
      ?}
   ?};
   _raport.RAPORT+=(19+(tm_form(POCZTA.TM_STAMP)))+' — '+POCZTA.SUB+': błąd '+gsub(MAIL.Result(),'\n\n',' — ')
      +_proc_details+'\n\n'
?};

:: Przy diagnozie wysyłania można odblokować poniższy wiersz - nie będą kasowane powiadomienia mimo ich wysłania
::_err:=1;
::
{? _err=0
||
   {? type_of(_file)=type_of(SYSLOG) & _file.first()
   || {!
      |?
         ferase(_file.FILE,0);
         _file.next()
      !}
   ?};
   exec('source_set_info','#mailbox','Wysłano '+$date()+' '+time()$3,'T');
   exec('source_unlock','#mailbox');
   POCZTA.BOX:='S';
   _ok:=POCZTA.put();
   POCZTA.r_unlock();
   {? var_pres('__SENT')=type_of(0) || __SENT+=1 ?}
||
   exec('source_set_info','#mailbox',$POCZTA.COUNTER+'. próba wysyłki '+$date()+' '+time()$3,'N');
   exec('source_unlock','#mailbox');
   POCZTA.COUNTER+=1;
   POCZTA.put();
   POCZTA.r_unlock()
?};
{? var_pres('ECHO')>=0 || ECHO.display('') || echo('') ?};

POCZTA.cntx_pop();

_ok


\em_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Aktywowanie/blokowanie powiadomienia/zadania
::   WE: _a - 1: aktywowanie, 0: blokowanie
::  OLD: \em_akt/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__GRUPA')<0 || __GRUPA:=0 ?};
_txt:=
   {? _a
   || 'Aktywowanie nie jest możliwe.\nRekord jest redagowany lub przetwarzany.'@
   || 'Blokowanie nie jest możliwe.\nRekord jest redagowany lub przetwarzany.'@
   ?};
{? POCZTA.r_lock(1,1,1)
|| {? POCZTA.TRYB='X'
   || {? __GRUPA=0
      || {? _a
         || FUN.info('Aktywowanie nie jest możliwe.\nWiadomość techniczna.'@)
         || FUN.info('Blokowanie nie jest możliwe.\nWiadomość techniczna.'@)
         ?}
      ?}
   || {? _a
      || POCZTA.TRYB:='T'
      || POCZTA.TRYB:='N'
      ?};
      POCZTA.put()
   ?};
   POCZTA.r_unlock()
|| {? __GRUPA=0
   || FUN.emsg(_txt)
   ?}
?};
~~


\em_akt_gpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Przed grupą - aktywowanie/blokowanie powiadomienia/zadania
::   WY: 1
::  OLD: \em_akt_gpr/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
__GRUPA:=1;
1


\em_akt_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Po grupie - aktywowanie/blokowanie powiadomienia/zadania
::  OLD: \em_akt_gpo/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
&__GRUPA;
~~


\em_eml_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Obsługa funkcji 'Raport' w oknie wiadomości
::  OLD: \em_eml_r/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_file:=exec('ilog_name','#mailbox',POCZTA.ref());
{? fexists(_file,1)
|| exec('view_log','#mailbox',_file)
|| FUN.info('Brak raportu dla bieżącego zapisu.'@)
?};
~~


\view_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Podgląd wybranego raportu z wysyłania wiadomości
::   WE: _a - nazwa pliku
::  OLD: \view_log/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,'LP','INTEGER','Lp','TXT','STRING[255]','Tekst');
_wer:=_tab.mk_sel('Zawartość pliku %1'@[_a],'N',0,'mailog',,,,,'U');
_tab.win_sel(_wer);
_tab.win_fld(_wer,,'TXT',,,100,,,' '@);
_tab.win_act(_wer,0,'Rekord',,,,"exec('rekprzed','color','MAILLOG#01')");
_tab.win_act(_wer,0,'Szukaj');
_tab.win_act(_wer,,'Formuła','Legenda'@@,,,"exec('legenda','color','MAILLOG#01')",,,,,,'L');
_file:=fopen(_a,'ur',1);
{? _file>0
||
   _lp:=0;
   {!
   |?
      _line:=fread(_file);
      _lp+=1;
      _line<>'\n'
   !};

   {? _lp>10000
   ||
      undefine();
      define('LINES',_lp,'Ilość końcowych wierszy do pobrania'@,'Ilość końcowych wierszy loga do pobrania'@,,10);
      _btn:=def_btn('text=%1'['Zapisz'@],'key:F2');
      def_bopt(_btn,'default=1');
      def_btn('text=%1'['Anuluj'@],'key:Esc');
      {? def_edit(,FUN.TYT)
      || _start:=_lp-DEFINE.LINES
      || fclose(_file);
         return()
      ?}
   ||
      _start:=0
   ?};
   fclose(_file);

   fopen(_a,'ur',1);

   _lp:=0;

   {!
   |?
      _line:=fread(_file);
      _lp+=1;
      {? _line<>'\n' & _lp>=_start
      || _tab.LP:=_lp;
         _tab.TXT:=gsub(_line,'\t','    ');
         _tab.add()
      ?};
      _line<>'\n'
   !};
   fclose(_file)
?};
_tab.select();
~~


\em_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Podgląd raportów z wysyłania wiadomości
::  OLD: \em_log/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_logi1:=files('mail*.log');
{? type_of(_logi1)=type_of(~~) || FUN.error('Błąd pobrania nazw plików.'@); return('') ?};

_logi2:=files('ml*.log');
{? type_of(_logi2)=type_of(~~) || FUN.error('Błąd pobrania nazw plików.'@); return('') ?};

_logj1:=files('majl*.log');
{? type_of(_logj1)=type_of(~~) || FUN.error('Błąd pobrania nazw plików.'@); return('') ?};

_logj2:=files('mj*.log');
{? type_of(_logj2)=type_of(~~) || FUN.error('Błąd pobrania nazw plików.'@); return('') ?};

__logi:=sql('
   select
      :_a.FILENAME,
      0 as SIZE
   from :_a
   union all
   select
      :_b.FILENAME,
      0 as SIZE
   from :_b
   order by 1 desc
',_logi1,_logi2);

{? type_of(__logi)=type_of(~~) || &__logi; exec('err_sql','#sql'); return('') ?};

_logj:=sql('
   select
      :_a.FILENAME,
      0 as SIZE
   from :_a
   union all
   select
      :_b.FILENAME,
      0 as SIZE
   from :_b
   order by 1 desc
',_logj1,_logj2);

{? type_of(_logj)=type_of(~~) || exec('err_sql','#sql'); return('') ?};

{? __logi.first()
|| {!
   |?
      _file:=fopen(__logi.FILENAME,'b',1,0);
      {? _file>0 || __logi.SIZE:=fgetsize(_file); fclose(_file); __logi.put() ?};
      __logi.next()
   !}
?};
__logi.first();

{? _logj.first()
|| {!
   |?
      _fjle:=fopen(_logj.FILENAME,'b',1,0);
      {? _fjle>0 || _logj.SIZE:=fgetsize(_fjle); fclose(_fjle); _logj.put() ?};
      _logj.next()
   !}
?};
_logj.first();

__weri:=__logi.mk_sel('Raporty z wysyłania wiadomości e-mail'@,'N',0,'mailogi');
__logi.win_fld(__weri,,'FILENAME',,,40,,,'Nazwa pliku'@);
__logi.win_fld(__weri,,'SIZE',,,15,,,'Wielkość'@);
__logi.win_act(__weri,,'Formuła','Pod&gląd'@@,,
   'Podgląd zawartości pliku',"exec('view_log','#mailbox',cur_tab(1,1).FILENAME)",,1,,,,'G');
__logi.win_act(__weri,,'Formuła','Zapisz'@@,,
   'Zapisanie pliku na lokalnym komputerze'@,"exec('save_log','#mailbox',cur_tab(1,1).FILENAME)",,,,,,'Z');

_werj:=_logj.mk_sel('Raporty z przetwarzania wiadomości e-mail'@,'N',0,'majlogi');
_logj.win_fld(_werj,,'FILENAME',,,40,,,'Nazwa pliku'@);
_logj.win_fld(_werj,,'SIZE',,,15,,,'Wielkość'@);
_logj.win_act(_werj,,'Formuła','Pod&gląd'@@,,
   'Podgląd zawartości pliku'@,"exec('view_log','#mailbox',cur_tab(1,1).FILENAME)",,1,,,,'G');
_logj.win_act(_werj,,'Formuła','Zapisz'@@,,
   'Zapisanie pliku na lokalnym komputerze'@,"exec('save_log','#mailbox',cur_tab(1,1).FILENAME)",,,,,,'Z');

_grp:=_logj.grp_make('Raporty'@,"grp_disp(__logi,__weri)",'majlgrp',30,10,,,'normal');
_logj.grp_sel(_grp,,_werj,,,,,15,,,,,'maximized_with_title');
_logj.grp_splt(_grp,,'vertical','right');
_logj.grp_sel(_grp,__logi,__weri,,,,,15,,,,,'maximized_with_title');

_logj.win_sel(_grp);
_logj.select();
VAR_DEL.delete('__logi','__weri');
win_activate('<');
''


\save_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Zapisanie wybranego raportu z wysyłania wiadomości
::   WE: _a - nazwa pliku
::  OLD: \save_log/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_filename:=dlg_save(_a,1);

{? type_of(_filename)=type_of('')
|| {? _filename<>''
   || {? exec('interm','#system')=0
      || FUN.info('Plik %1 został zapisany.'@[_filename])
      ?}
   ?}
|| FUN.emsg('Nie udało się zapisać pliku w docelowym katalogu.'@)
?};
~~


\eml_reset
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Resetuje licznik dla niewysłanej wiadomości
::  OLD: \eml_reset/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? POCZTA.r_lock(1,1,1)
|| _put:=0;
   {? exec('source_lock','#mailbox')
   || POCZTA.COUNTER:=0;
      POCZTA.SENDINFO:='N';
      _put:=1;
      exec('source_set_info','#mailbox','Wznowiono próbę wysyłki '+$date()+' '+time()$3,'N');
      exec('source_unlock','#mailbox')
   ?};
   POCZTA.r_unlock();
   {? _put || POCZTA.put() ?}
|| FUN.emsg('Resetowanie licznika niemożliwe.\nRekord jest redagowany lub przetwarzany.'@)
?};
~~


\em_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Kopiowanie danych powiązanych z wiadomością (załączniki, treść, odbiorcy)
::   WE: _a - źródło (POCZTA.ref())
::       _b - cel (POCZTA.ref())
::       UWAGA: typy parametrów nie są badane
::  OLD: \em_copy/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTA.cntx_psh();
POCZTA.clear();
{? POCZTA.seek(_a) & POCZTA.memo_get(,'BODYT') & POCZTA.memo_get(,'BODYH')
|| {? POCZTA.seek(_b) || POCZTA.memo_put(,'BODYT'); POCZTA.memo_put(,'BODYH') ?}
?};
POCZTA.cntx_pop();

POCZTAA.index('P');
POCZTAA.prefix(_a);
{? POCZTAA.first()
|| {!
   |? POCZTAA.cntx_psh();
      POCZTAA.clear();
      POCZTAA.POCZTA:=_b;
      POCZTAA.add();
      POCZTAA.cntx_pop();
      POCZTAA.next()
   !}
?};

POCZTAR.index('LP');
POCZTAR.prefix(_a);
{? POCZTAR.first()
|| {!
   |? POCZTAR.cntx_psh();
      POCZTAR.clear();
      POCZTAR.POCZTA:=_b;
      POCZTAR.add();
      POCZTAR.cntx_pop();
      POCZTAR.next()
   !}
?};
~~


\chk_eml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Kontrola rekordu poczty po redakcji
::  OLD: \chk_eml/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('get','#params',700002,2)='' & POCZTA.FFROM=''
|| FUN.emsg(
      'Nie określono domyślnego autora (parametr 700002).\n'
      'Wymagane wypełnienie adresu e-mail autora wiadomości.'@
   );
   return('FFROM')

|? ~exec('mail_ok','#email',POCZTA.FFROM)
|| FUN.emsg('Niepoprawny adres e-mail autora.'@);
   return('FFROM')

|? ~exec('mail_ok','#email',POCZTA.FSENDER)
|| FUN.emsg('Niepoprawny adres e-mail nadawcy.'@);
   return('FSENDER')

|? ~exec('mail_ok','#email',POCZTA.FREPLYTO)
|| FUN.emsg('Niepoprawny adres e-mail do odpowiedzi.'@);
   return('FREPLYTO')

?};
__CHK.record(POCZTA,,'SUB')


\em_srcv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: Lista odbiorców wiadomości
::  OLD: \em_srcv/skid_eml.fml
::  OLD: \em_xrcv/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? (POCZTA.BOX='S' | POCZTA.BOX='D') || _display:=1 || _display:=0 ?};

_RCV:=tab_tmp(3,
   'PARENT','TREE_REF','',
   'LP','INTEGER','Lp'@,
   'R','STRING[1]','Rodzaj odbiorcy'@,
   'SRCV','STRING[100]','Odbiorca'@,
   'JORG','STRING[16]','Jednostka organizacyjna'@,
   'REF','INTEGER','#POCZTA.ref()'
);

POCZTAV.POCZTA:=POCZTA.ref();

_parent:=obj_new('T','C','B');
{! _iter:=1..3 |! _parent[_iter]:=obj_new('R','RR','REF') !};

_parent.T.R:='T'; _parent.T.RR:='Do:'@;
_parent.C.R:='C'; _parent.C.RR:='DW:'@;
_parent.B.R:='B'; _parent.B.RR:='UDW:'@;

{! _iter:=1..3
|!
   _RCV.PARENT:=0;
   _RCV.LP:=_iter;
   _RCV.R:=_parent[_iter].R;
   _RCV.SRCV:=_parent[_iter].RR;
   _RCV.JORG:='';
   _RCV.REF:=0;
   _RCV.add();

   _parent[_iter].REF:=_RCV.ref();
   POCZTAR.index('LP');
   POCZTAR.prefix(POCZTAV.POCZTA,_parent[_iter].R);
   {? POCZTAR.first()
   || {!
      |? _RCV.PARENT:=_parent[_iter].REF;
         _RCV.LP:=POCZTAR.LP;
         _RCV.R:=POCZTAR.R;
         _RCV.SRCV:=POCZTAR.SRCV;
         _RCV.JORG:=POCZTAR.JORG().SYMBOL;
         _RCV.REF:=#POCZTAR.ref();
         _RCV.add();
         POCZTAR.next()
      !}
   ?}
!};
POCZTAR.prefix(POCZTAV.POCZTA);
_RCV.first();

_werc:=POCZTARC.mk_sel('Książka adresowa'@,'P',0,'pocztarc_wer',,15,15,0,'U');
POCZTARC.win_fld(_werc,,'ADRES',,,45);
_addrcv:="
   params_set(params_get());
   _parent:=params_get().parent;
   _RCV:=params_get().RCV;
   _wer:=params_get().wer;
   POCZTAR.blank();
   POCZTAR.R:=_parent[_a].R;
   POCZTAR.RCV:=POCZTARC.ref();
   POCZTAR.SRCV:=POCZTARC.ADRES;
   POCZTAR.JORG:=null();
   {? POCZTAR.add(1)
   || _RCV.PARENT:=#_parent[_a].REF;
      _RCV.R:=_parent[_a].R;
      _RCV.SRCV:=POCZTAR.SRCV;
      _RCV.JORG:='';
      _RCV.REF:=POCZTAR.ref();
      _RCV.add()
   ||
      {? var_pres('FUN')>0 || FUN.info('Taki adres już jest dodany do listy odbiorców.'@) ?}
   ?};
   grp_disp(_RCV,_wer);
   ''
";
_fb:="params_set(params_get()); params_get().addrcv(1)";
POCZTARC.win_act(_werc,,'Formuła','&Do'@@,,'Dodanie adresu do listy odbiorców'@,_fb,,,1,,,'D');
_btn:=POCZTARC.win_btn(_werc,'btn_label_align=left, text=%1'['&Do'@],'menu:D');
POCZTARC.btn_opt(_btn,'tooltip=%1'['Dodanie adresu do listy odbiorców'@]);
_fb:="params_set(params_get()); params_get().addrcv(2)";
POCZTARC.win_act(_werc,,'Formuła','D&W'@@,,'Dodanie adresu do listy "do wiadomości"'@,_fb,,,1,,,'W');
_btn:=POCZTARC.win_btn(_werc,'btn_label_align=left, text=%1'['D&W'@],'menu:W');
POCZTARC.btn_opt(_btn,'tooltip=%1'['Dodanie adresu do listy "do wiadomości"'@]);
_fb:="params_set(params_get()); params_get().addrcv(3)";
POCZTARC.win_act(_werc,,'Formuła','&UDW'@@,,'Dodanie adresu do listy "ukryte do wiadomości"'@,_fb,,,1,,,'U');
_btn:=POCZTARC.win_btn(_werc,'btn_label_align=left, text=%1'['&UDW'@],'menu:U');
POCZTARC.btn_opt(_btn,'tooltip=%1'['Dodanie adresu do listy "ukryte do wiadomości"'@]);

_wer:=_RCV.mk_sel('Odbiorcy'@,'N',0,'pocztar_werp',,15,15,1);
_RCV.win_fld(_wer,,'SRCV',,,45);

_btn_del:='';
{? ~_display
|| _fb:="
      params_set(params_get());
      _RCV:=params_get().RCV;
      POCZTAR.blank();
      POCZTAR.R:=_RCV.R;
      POCZTAR.win_edit('REDS');
      {? POCZTAR.edit(\"
                        {? POCZTAR.SRCV<>'' & exec('mail_ok','#email',POCZTAR.SRCV)
                        || 1
                        || FUN.emsg('Niepoprawny adres e-mail odbiorcy.'@);
                           'SRCV'
                        ?}
                      \")
      || {? POCZTAR.add()
         || _RCV.PARENT:=($('params_get().parent.'+POCZTAR.R+'.REF'))();
            _RCV.R:=POCZTAR.R;
            _RCV.LP:=POCZTAR.LP;
            _RCV.SRCV:={? POCZTAR.SRCV<>'' || POCZTAR.SRCV || POCZTAR.RCV().ADRES ?};
            _RCV.JORG:=POCZTAR.JORG().SYMBOL;
            _RCV.REF:=#POCZTAR.ref();
            _RCV.add()
         ?}
      ?}
   ";
   _RCV.win_act(_wer,0,'Formuła','Dołącz'@@,,,_fb,,,,,,'D');
   _RCV.win_act(_wer,1,'Formuła','Dołącz'@@,,,_fb,,,,,,'D');

   _fb:="
      params_set(params_get());
      _RCV:=params_get().RCV;
      {? _RCV.REF<>0 & POCZTAR.seek(_RCV.REF,)
      || POCZTAR.win_edit('REDS');
         {? POCZTAR.edit(\"
                           {? POCZTAR.SRCV<>'' & exec('mail_ok','#email',POCZTAR.SRCV)
                           || 1
                           || FUN.emsg('Niepoprawny adres e-mail odbiorcy.'@);
                              'SRCV'
                           ?}
                         \")
         || {? POCZTAR.put()
            || _RCV.PARENT:=($('params_get().parent.'+POCZTAR.R+'.REF'))();
               _RCV.R:=POCZTAR.R;
               _RCV.LP:=POCZTAR.LP;
               _RCV.SRCV:={? POCZTAR.SRCV<>'' || POCZTAR.SRCV || POCZTAR.RCV().ADRES ?};
               _RCV.JORG:=POCZTAR.JORG().SYMBOL;
               _RCV.REF:=#POCZTAR.ref();
               _RCV.put()
            ?}
         ?}
      ?}
   ";
   _RCV.win_act(_wer,,'Formuła','Popraw'@@,,,_fb,,,,,,'P');

   _fb:="
      params_set(params_get());
      _RCV:=params_get().RCV;
      {? _RCV.REF<>0 & POCZTAR.seek(_RCV.REF,)
      || {? FUN.ask('Czy usunąć bieżący wiersz?'@)
         || {? POCZTAR.del(,1)>0
            || _parent:=_RCV.PARENT;
               {? _RCV.del()
               || _RCV.seek(_parent,)
               ?}
            ?}
         ?}
      ?}
   ";
   _RCV.win_act(_wer,,'Formuła','Usuń'@@,,,_fb,,,,,,'U');
   _btn_del:=_RCV.win_btn(_wer,'text=%1'['&Usuń'@],'menu:U');

   _fb:="
      {? _a
      || _RCV:=params_get().RCV;
         _RCV.actions_grayed(cur_win(1,1),{? _RCV.REF=0 || 'PU' || '' ?})
      ?};
      ~~
   ";
   _RCV.win_act(_wer,,'Rekord',,,,_fb)

?};

_fb:="sel_exit()";
_RCV.win_act(_wer,0,'Formuła','&OK'@@,,,_fb,,,,,,'O');
_RCV.win_act(_wer,1,'Formuła','&OK'@@,,,_fb,,,,,,'O');
_btn:=_RCV.win_btn(_wer,'text=%1'['&OK'@],'menu:O');

_fb:="exec('zwrw_all','#tree',params_get().RCV,'PARENT',params_get().wer)";
_RCV.win_act(_wer,,'Formuła','Zwiń/rozwiń'@@,,,_fb,,,,,,'Z');

params_set('RCV',_RCV,'wer',_wer,'addrcv',_addrcv,'parent',_parent,'btn_del',_btn_del);

{? _display
|| _fb:="
      params_set(params_get());
      params_get().RCV.tr_set(1,params_get().wer,1,0);
      grp_disp(params_get().RCV,params_get().wer)
   ";
   _grp:=_RCV.grp_make('Odbiorcy'@,_fb,'pocztarc_grp0',50,20,,,'normal');
   _RCV.grp_sel(_grp,,_wer,,,,,,,,,,'maximized');
   _RCV.win_sel(_grp);
   _RCV.select()

|| _fb:="
      params_set(params_get());
      params_get().RCV.tr_set(1,params_get().wer,1,0);
      grp_disp(params_get().RCV,params_get().wer)
   ";
   _grp:=POCZTARC.grp_make('Odbiorcy'@,_fb,'pocztarc_grp',,20,,,'normal');
   POCZTARC.grp_sel(_grp,,_werc,,,,,,,,,,'maximized');
   POCZTARC.grp_splt(_grp,,'vertical','pocztar');
   POCZTARC.grp_sel(_grp,_RCV,_wer,,,,,,,,,,'maximized');
   POCZTARC.win_sel(_grp);
   POCZTARC.win_dict('WER');
   POCZTARC.actions('WER','TZO');
   POCZTARC.index('ADRES');
   POCZTARC.prefix(REF.FIRMA);
   {? POCZTA.r_lock(1,1,1)
   || POCZTARC.select();
      POCZTA.r_unlock()
   || FUN.emsg('Redagowanie listy odbiorców nie jest możliwe.\nRekord jest redagowany lub przetwarzany.'@)
   ?}

?};

~~


\pta_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Wartość początkowa pola złączeniowego do tabeli POCZTA w tabelach POCZTA*
::   WY:
::  OLD: \pta_ref/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAV.POCZTA


\ptr_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Wartość początkowa pola POCZTAR.RCV
::   WY:
::  OLD: \ptr_ref/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAV.POCZTARC


\em_rc_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: F3 w polu POCZTAR.SRCV
::  OLD: \em_rc_f3/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTARC.index('ADRES');
POCZTARC.prefix(REF.FIRMA);
POCZTARC.win_sel('WER');
POCZTARC.actions('WER','ZO','T');
POCZTARC.find_key(fld());
{? POCZTARC.select(,1,10)
|| POCZTAR.RCV:=POCZTARC.ref();
   POCZTARC.ADRES
|| ~~
?}


\em_r_bre
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60]
:: OPIS: Przed rekord w oknach wertowania tabeli POCZTAR
::  OLD: \em_r_bre/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? POCZTAR.R='T'
|| POCZTAV.R:='Do'@
|? POCZTAR.R='C'
|| POCZTAV.R:='DW'@
|? POCZTAR.R='B'
|| POCZTAV.R:='UDW'@
|| POCZTAV.R:=''
?};
::POCZTAV.SYSTEM:=exec('nsystem','skid_for',POCZTAR.POCZTA().SYSTEM);
~~


\em_r_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Kontrola rekordu odbiorcy poczty po redakcji
::  OLD: \em_r_chk/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('mail_ok','#email',POCZTAR.SRCV)
|| FUN.emsg('Niepoprawny adres e-mail nadawcy.'@);
   return('SRCV')
?};
__CHK.record(POCZTAR,,'RCV','POCZTA')


\em_a_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Kontrola rekordu w książce adresowej po redakcji
::  OLD: \em_a_chk/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('mail_ok','#email',POCZTARC.ADRES)
||
   FUN.emsg('Niepoprawny adres e-mail.'@);
   return('ADRES')
?};
__CHK.record(POCZTARC,,'ADRES')


\legenda_emp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Legenda w oknie wertowania POCZTA.WER, POCZTA.SEL_KH, POCZTA.SEL_OK
::  OLD: \legenda_emp/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_win(1,1)='WER'
|| exec('legenda','color','POCZTA#01#','#POCZTA#02','#POCZTA#01','#POCZTA#05')
|| exec('legenda','color','POCZTA#01#','#POCZTA#02','#POCZTA#05','#POCZTA#06')
?};
~~


\ilog_name
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Nazwa pliku indywidualnego raportu
::   WE: _a - POCZTA.ref()
::  OLD: \ilog_name/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_poczta:=_a;
'mx'+exec('eml_id','#mailbox',_poczta)+'.log'


\eml_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Identyfikator maila
::   WE: POCZTA.ref()
::  OLD: \eml_id/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
($_a)+8


\em_tm_stamp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO
:: OPIS: Tłumaczy POCZTA.TM_STAMP do POCZTAV.STRING
::  OLD: \em_tm_stamp/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAV.STRING:=19+(tm_form(POCZTA.TM_STAMP));
0


\var_pe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Zwraca wartość pola POCZTAV.PE
::   WY: POCZTAV.PE
::  OLD: \var_pe/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAV.PE


\poczta_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Wartość początkowa pola POCZTA.TM_STAMP
::   WY: tm_stamp()
::  OLD: \poczta_t/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTA.tm_stamp()


\em_adr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: Lista odbiorców - książka adresowa
::  OLD: \em_adr/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTARC.index('ADRES');
POCZTARC.prefix(REF.FIRMA);
POCZTARC.win_sel('WER');
POCZTARC.win_edit();
POCZTARC.actions('WER','TZ','O:d');
POCZTARC.select();
win_activate('<');
''


\srv_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Selekcja listy serwerów
::  OLD: \srv_select/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAM.fld_attr('PASS',2);

POCZTAM.win_sel('WER');
POCZTAM.win_edit('RED');
POCZTAM.win_patt('SZUK');
POCZTAM.index('SERVER');
POCZTAM.prefix(REF.FIRMA,'P');
_not_def:=1;
{!
|? POCZTAM.select();
   {? POCZTAM.first()
   || {!
      |? {? POCZTAM.DEFAULT='T' || _not_def:=0 ?};
         POCZTAM.next()
      !}
   || return('')
   ?};
   {? _not_def
   || FUN.emsg('Wskaż domyślny serwer SMTP.'@); 1
   || 0
   ?}
!};
win_activate('<');
''


\srv_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Obsługa przed dołączeniem POCZTAM
::   WY: 1
::  OLD: \srv_dolacz_b/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAV.PASS:='';
1


\srv_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Obsługa przed poprawianiem POCZTAM
::   WY: 1
::  OLD: \srv_popraw_b/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAV.PASS:='';
1


\srv_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Ustawia serwer jako domyślny
::  OLD: \srv_default/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy ustawić serwer %1 jako domyślny?'@[POCZTAM.SERVER])
|| POCZTAM.cntx_psh();
   POCZTAM.index('SERVER');
   POCZTAM.prefix(REF.FIRMA,'P');
   {? POCZTAM.first()
   || {!
      |? POCZTAM.DEFAULT:='N';
         POCZTAM.put();
         POCZTAM.next()
      !}
   ?};
   POCZTAM.cntx_pop();
   POCZTAM.DEFAULT:='T';
   POCZTAM.put()
?};
~~


\srv_rkpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Rekord po dla okna POCZTAM.WER
::  OLD: \srv_rkpo/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
:: proste sprawdzenie nazwy/IP serwera
{? POCZTAM.SERVER=''
|| FUN.info('Podaj nazwę albo adres IP serwera SMTP.'@);
   'SERVER'
|? ~exec('srv_unique','#mailbox')
|| FUN.info('Istnieje już serwer SMTP z takimi danymi.'@);
   'SERVER'
||
   _adres:=-| POCZTAM.SERVER;
   _ok:=1;
   {! _it:=1..+_adres
   |! _znak:=(1+(_it-_adres));
      {? '-.0123456789abcdefghijklmnopqrstuwvxyz_'*_znak=0
      || _ok:=0
      ?}
   !};
   {? _ok
   ||
::    jeżeli hasło zostało wpisane, to jest kodowane
      {? POCZTAV.PASS<>''
      || POCZTAM.PASS:=exec('pwd_encode','#string',POCZTAV.PASS,exec('encode_key','#mailbox'))
      || POCZTAM.PASS:=''
      ?};
      1
   || FUN.emsg('Niepoprawna nazwa serwera SMTP.'@);
      'SERVER'
   ?}
?}


\srv_unique
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Sprawdzenie unikalności rekordu POCZTAM
::   WY: 1 - jest unikalny, 0 - nie jest
::----------------------------------------------------------------------------------------------------------------------
_unik:=1;
POCZTAM.cntx_psh();
{? 1+menu_pth()='D'
|| _ref:=-1
|| _ref:=POCZTAM.ref()
?};
_srv:=POCZTAM.SERVER;
_prt:=POCZTAM.PORT;
_aut:=POCZTAM.AUTH;
_usr:=POCZTAM.USER;
_ssl:=POCZTAM.SSL;
_tls:=POCZTAM.TLS;
POCZTAM.index('SERVER');
POCZTAM.prefix(REF.FIRMA,'P',_srv,_srv);
{? POCZTAM.first()
|| {!
   |? {? POCZTAM.ref()<>_ref & POCZTAM.PORT=_prt & POCZTAM.AUTH=_aut & POCZTAM.USER=_usr
         & POCZTAM.SSL=_ssl & POCZTAM.TLS=_tls
      || _unik:=0
      ?};
      _unik & POCZTAM.next()
   !}
?};
POCZTAM.cntx_pop();
_unik


\pocztam_ssl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Po redakcji pola POCZTAM.SSL
::   WY: 1
::  OLD: \pocztam_ssl_ae/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? POCZTAM.SSL='1' || POCZTAM.TLS:='0' ?};
1


\pocztam_tls_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Po redakcji pola POCZTAM.TLS
::   WY: 1
::  OLD: \pocztam_tls_ae/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? POCZTAM.TLS='1' || POCZTAM.SSL:='0' ?};
1


\encode_key
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Klucz kodujący hasła do serwera SMTP
::  OLD: \encode_key/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
'j88brflmobp5v5ji6z40t1u3'


\set_virt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Ustawia metody wirtualne obiektu MAIL
::  OLD: \set_virt/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
set_virt(MAIL,'smtp_srv',"
   POCZTAME.index('EMAIL');
   POCZTAME.prefix(REF.FIRMA,_a,);
   {? POCZTAME.first()
   ||
      .SMTPSERV:=POCZTAME.POCZTAM().SERVER;
      .SMTPPORT:=POCZTAM.PORT;
      .SMTPTIMO:=POCZTAM.TIMO;
      {? POCZTAM.AUTH_KND*'OAuth'>0
      || .SMTPAUTH:='2';
         .SMTPUSER:=POCZTAM.USER;
         {? POCZTAM.AUTH_EXP<=(utc_get()-5)
         || exec('token_refresh','#oauth2',,0)
         ?};
         .SMTPPASS:=POCZTAM.memo_txt(,1,'AUTH_TKN')
      || .SMTPAUTH:=POCZTAM.AUTH;
         .SMTPUSER:=POCZTAM.USER;
         .SMTPPASS:=exec('pwd_decode','#string',POCZTAM.PASS,exec('encode_key','#mailbox'),1)
      ?};
      .SMTPSSL:=POCZTAM.SSL;
      .SMTPTLS:=POCZTAM.TLS
   ||
      POCZTAM.index('DEFAULT');
      POCZTAM.prefix(REF.FIRMA,'P','T');
      {? POCZTAM.first()
      ||
         .SMTPSERV:=POCZTAM.SERVER;
         .SMTPPORT:=POCZTAM.PORT;
         .SMTPTIMO:=POCZTAM.TIMO;
         {? POCZTAM.AUTH_KND*'OAuth'>0
         || .SMTPAUTH:='2';
            .SMTPUSER:=POCZTAM.USER;
            {? POCZTAM.AUTH_EXP<=(utc_get()-5)
            || exec('token_refresh','#oauth2',,0)
            ?};
            .SMTPPASS:=POCZTAM.memo_txt(,1,'AUTH_TKN')
         || .SMTPAUTH:=POCZTAM.AUTH;
            .SMTPUSER:=POCZTAM.USER;
            .SMTPPASS:=exec('pwd_decode','#string',POCZTAM.PASS,exec('encode_key','#mailbox'),1)
         ?};
         .SMTPSSL:=POCZTAM.SSL;
         .SMTPTLS:=POCZTAM.TLS
      ||
         .SMTPSERV:='';
         .SMTPPORT:=-1;
         .SMTPTIMO:=-1;
         .SMTPAUTH:='0';
         .SMTPUSER:='';
         .SMTPPASS:='';
         .SMTPSSL:='0';
         .SMTPTLS:='0'
      ?}
   ?};
   ~~
",type_of(''));
~~


\pocztam_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Wartość domyślna pola POCZTAME.POCZTAM
::  OLD: \pocztam_ref/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAM.ref()


\srve_rkpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Rekord po dla okna POCZTAME.WER
::  OLD: \srve_rkpo/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=__CHK.record(POCZTAME,,'EMAIL');
{? _result=''
|| {? ~exec('mail_ok','#email',POCZTAME.EMAIL)
   || FUN.info('Niepoprawny adres e-mail.'@);
      _result:='EMAIL'
   ?};
   {? _result=''
   || _ref:={? -menu_txt()='popraw' || POCZTAME.ref() || null() ?};
      POCZTAME.cntx_psh();
      POCZTAME.index('SERVER');
      POCZTAME.prefix(POCZTAME.POCZTAM,POCZTAME.EMAIL,);
      {? POCZTAME.first() & POCZTAME.ref()<>_ref
      || FUN.info('Podany adres jest już przypisany do serwera.\nNależy wpisać inny adres e-mail.'@);
         _result:='EMAIL'
      ?};
      POCZTAME.cntx_pop()
   ?}
?};
_result


\srv_email
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Adresy e-mail dla serwera
::  OLD: \srv_email/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAME.win_sel('WER');
POCZTAME.index('SERVER');
POCZTAME.prefix(POCZTAM.ref());
POCZTAME.select();
~~


\add_email_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Argumet funkcji \add_email/#mailbox.fml
::   WY: obj_new()
::----------------------------------------------------------------------------------------------------------------------
_args:=obj_new(
:: Temat - STRING
   'SUB'
:: Autor - STRING
   ,'FROM'
:: Nadawca - STRING
   ,'SENDER'
:: Odpowiedz-do - STRING
   ,'REPLYTO'
:: Odbiorcy - STRING (jeden odbiorca), złączenie do tabeli POCZTARC (jeden odbiorca)
:: albo tab_tmp() z pierwszą kolumną STRING (e-mail), drugą (opcjonalnie) określającą rodzaj odbiorcy
:: - T: do, C: do wiadomości, B: ukryte do wiadomości
   ,'RCV'
:: Treść wiadomości (wariant tekstowy) - STRING (ograniczenie długości jak dla zmiennej tekstowej)
:: albo tab_tmp() z pierwszą kolumną numerującą wiersze, z drugą typu STRING (kolejne wiersze wiadomości)
:: - rozmiar wiadomości nie może przekroczyć maksymalnego rozmiaru pola notatnikowego
   ,'BODYT'
:: Treść wiadomości (wariant HTML) - STRING (ograniczenie długości jak dla zmiennej tekstowej)
:: albo tab_tmp() z pierwszą kolumną numerującą wiersze, z drugą typu STRING (kolejne wiersze wiadomości)
:: - rozmiar wiadomości nie może przekroczyć maksymalnego rozmiaru pola notatnikowego
   ,'BODYH'
:: Znacznik potwierdzenia odbioru STRING[1]
   ,'RETURN'
:: Załącznik - STRING albo tab_tmp() z pierwszą kolumną typu STRING - pełna ścieżka do pliku
:: z drugą (opcjonalnie) typu BLOBRAW, z trzecią (opcjonalnie) oznaczającą czy dołączony, czy osadzony (A/E)
   ,'ATTACH'
:: Zapis źródłowy w postaci refSQL (STRING[16]), np. $DOKUM.ref()
   ,'SOURCE'
:: Tagi dodawane do nagłówka
   ,'HEADTAGS'
:: Wzorzec wiadomości
   ,'TEMPLATE'
:: Tryb wysyłania STRING[1]:
:: T (aktywny - wysyłaj), N (nieaktywny - nie wysyłaj), X (mail techniczny - wysyłaj procesem technicznym)
   ,'TRYB'
:: Symbol i wersja procesu
   ,'PROC'
:: Kod instancji procesu
   ,'INS_CODE'
:: $POCZTA.ref()
   ,'REFERENCE'
:: Czy dodawać prefiks [XINFO.SKTROT] do tematu wiadomości - NUMBER:
:: 1 (dodawać wg wartości parametru 700012 - domyślnie), 0 (nie dodawać)
   ,'SUB_PREFIX'
);
_args.SUB:='';
_args.FROM:='';
_args.SENDER:='';
_args.REPLYTO:='';
_args.RCV:='';
_args.BODYT:='';
_args.BODYH:='';
_args.RETURN:='N';
_args.ATTACH:=~~;
_args.SOURCE:='';
_args.HEADTAGS:='';
_args.TEMPLATE:='';
_args.TRYB:='T';
_args.PROC:='';
_args.INS_CODE:='';
_args.REFERENCE:='';
_args.SUB_PREFIX:=1;
_args


\add_email
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Dodaje pocztę do skrzynki nadawczej
::   WE: _a - argument - wynik formuły \add_email_a/#mailbox.fml
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_args:=_a;

_result:=0;

POCZTA.cntx_psh();
POCZTA.clear();
POCZTAV.PE:='P';
POCZTA.blank();
POCZTA.SUB:={? _args.SUB_PREFIX=1 || exec('pre_sub','#email') || '' ?}+_args.SUB;
POCZTA.FSENDER:={? type_of(_args.SENDER)=type_of('') || _args.SENDER || '' ?};
POCZTA.FFROM:={? type_of(_args.FROM)=type_of('') || _args.FROM || '' ?};
POCZTA.FREPLYTO:={? type_of(_args.REPLYTO)=type_of('') || _args.REPLYTO || '' ?};
POCZTA.RETURN:=_args.RETURN;
POCZTA.SOURCE:=_args.SOURCE;
POCZTA.TEMPLATE:={? _args.TEMPLATE='' || exec('get','#params',700014) || _args.TEMPLATE ?};
POCZTA.TRYB:=_args.TRYB;
POCZTA.PROC:=_args.PROC;
POCZTA.INS_CODE:=_args.INS_CODE;
{? POCZTA.add()
|| _result:=1;
   _args.REFERENCE:=POCZTA.ref();
   {? POCZTA.r_lock(1,1,1)
   || POCZTAV.POCZTA:=POCZTA.ref();

::    Treść
      {? type_of(_args.BODYT)=type_of('')
      || POCZTA.memo_set(_args.BODYT,'BODYT');
         POCZTA.memo_put(,'BODYT')
      |? type_of(_args.BODYT)=type_of(SYSLOG)
      || {? _args.BODYT.first()
         || _memo:=POCZTA.memo_get('w','BODYT');
            {!
            |? _memo.fwrite(_args.BODYT[2]);
               _args.BODYT.next()
            !};
            POCZTA.memo_put(_memo,'BODYT');
            &_memo
         ?}
      ?};
      {? type_of(_args.BODYH)=type_of('')
      || POCZTA.memo_set(_args.BODYH,'BODYH');
         POCZTA.memo_put(,'BODYH')
      |? type_of(_args.BODYH)=type_of(SYSLOG)
      || {? _args.BODYH.first()
         || _memo:=POCZTA.memo_get('w','BODYH');
            {!
            |? _memo.fwrite(_args.BODYH[2]);
               _args.BODYH.next()
            !};
            POCZTA.memo_put(_memo,'BODYH');
            &_memo
         ?}
      ?};

::    Odbiorcy
      {? type_of(_args.RCV)=type_of('')
      || POCZTAR.cntx_psh();
         POCZTAR.prefix();
         POCZTAR.blank(1);
         POCZTAR.POCZTA:=POCZTA.ref();
         POCZTAR.SRCV:=_args.RCV;
         POCZTAR.R:='T';
         POCZTAR.add();
         POCZTAR.cntx_pop()
      |? type_of(_args.RCV)=type_of(null())
      || {? _args.RCV<>null()
         || POCZTAR.cntx_psh();
            POCZTAR.prefix();
            POCZTAR.blank(1);
            POCZTAR.POCZTA:=POCZTA.ref();
            POCZTAR.RCV:=_args.RCV;
            POCZTAR.SRCV:=_args.RCV.ADRES;
            POCZTAR.R:='T';
            POCZTAR.add(1);
            POCZTAR.cntx_pop()
         ?}
      |? type_of(_args.RCV)=type_of(SYSLOG)
      || {? _args.RCV.first()
         || {!
            |? POCZTAR.cntx_psh();
               POCZTAR.prefix();
               POCZTAR.blank(1);
               POCZTAR.POCZTA:=POCZTA.ref();
               POCZTAR.SRCV:=_args.RCV[1];
               POCZTAR.R:={? obj_len(_args.RCV)>1 || _args.RCV[2] || 'T' ?};
               POCZTAR.add(1);
               POCZTAR.cntx_pop();
               _args.RCV.next()
            !}
         ?}
      ?};

::    Załączniki
      {? type_of(_args.ATTACH)=type_of('')
      || POCZTAA.clear();
         POCZTAA.blank();
         POCZTAA.add();
         POCZTAA.bl_put('FILE',_args.ATTACH,0)
      |? type_of(_args.ATTACH)=7
      || POCZTAA.clear();
         POCZTAA.blank();
         POCZTAA.FILE:=_args.ATTACH;
         POCZTAA.add()
      |? type_of(_args.ATTACH)=type_of(SYSLOG)
      || {? _args.ATTACH.first()
         || POCZTAA.clear();
            {!
            |? {? _args.ATTACH[1]<>''
               || POCZTAA.blank();
                  {? obj_len(_args.ATTACH)>2 || POCZTAA.ATT_EMB:=_args.ATTACH[3] ?};
                  POCZTAA.add();
                  POCZTAA.bl_put('FILE',_args.ATTACH[1],0)
               |? obj_len(_args.ATTACH)>1 & _args.ATTACH[2]<>null()
               || POCZTAA.blank();
                  POCZTAA.FILE:=_args.ATTACH[2];
                  {? obj_len(_args.ATTACH)>2 || POCZTAA.ATT_EMB:=_args.ATTACH[3] ?};
                  POCZTAA.add()
               ?};
               _args.ATTACH.next()
            !}
         ?}
      ?};

::    Tagi nagłówkowe
      {? type_of(_args.HEADTAGS)=type_of('')
      || {? _args.HEADTAGS<>'' & _args.HEADTAGS*'='>0
         || POCZTA.memo_set(_args.HEADTAGS,'HEADTAGS');
            POCZTA.memo_put(,'HEADTAGS')
         ?}
      ?};
      exec('source_set_info','#mailbox','Przekazano do wysłania '+$date()+' '+time()$3,'N');
      POCZTA.r_unlock()
   ?}
?};
POCZTA.cntx_pop();
_result


\params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Redagowanie uniwersalnych parametrów - tylko zakładka 'Poczta'
::----------------------------------------------------------------------------------------------------------------------
exec('select_def','#params','Poczta','dziedziny:zws:zws_par_d_parametry_aplikacyjne_wsp#zakladka_poczta');
win_activate('<');
''


\em_a_em
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: Wiadomości w kolejce dla adresata
::  OLD: \em_a_em/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAR.cntx_psh();
POCZTAR.index('O');
POCZTAR.prefix(POCZTARC.ref(),'P');
POCZTAR.win_sel('WERP');
POCZTAR.hdr_sel(' — '+POCZTARC.ADRES);
POCZTAR.win_edit('REDP');
_formikon:="{? POCZTAR.POCZTA().TRYB='T' || 'xwin16.png:156' || 'xwin16.png:157' ?}";
POCZTAR.win_fml('WERP',,'POCZTA','TRYB','ICON_BEFORE',_formikon);
POCZTAR.select();
POCZTAR.cntx_pop();
~~


\legenda_erp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2010]
:: OPIS: Legenda w oknie wertowania POCZTAR.WERP
::  OLD: \legenda_erp/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','#POCZTA#02');
~~


\log_kolor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Kolorowanie loga poczty
::  OLD: \log_kolor/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_txt:=~-cur_tab(1,1).TXT;
{? _txt*'SEVERE' | 2+_txt='55' | (_txt*': 5' & ~(_txt*'PORT: 5')) | _txt*'KODEM: 1' | _txt*'EXCEPTION'
|| 'MAILLOG#01#02'
|? 1+_txt='4' | (_txt*': 4' & ~(_txt*'PORT: 4')) | 2+_txt='33' | 2+_txt='35'
|| 'MAILLOG#01#03'
|? _txt*'FINE' | ((2+_txt='22' | 2+_txt='25' | 2+_txt='23') & ~(_txt*'INFO')) |
   _txt*': 22' | _txt*': 25' | _txt*'KODEM: 0'
|| 'MAILLOG#01#04'
|? _txt=''
|| 'MAILLOG#01#01'
|| ''
?}


\bd_poczta_jorg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Przed wyświetl pola POCZTAV.JORG
::  OLD: \bd_poczta_jorg/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
POCZTAV.JORG:=POCZTAR.JORG().SYMBOL


\send_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.40]
:: OPIS: Wysyłanie wiadomości - element czynności
::   WE: _a - obiekt menadżera procesów
::   WY: obj_new('RESULT','BODYT')
::       - RESULT: 'OK' (wszystkie wiadomości zostały wysłane), 'BŁĄD' (co najmniej jedna wiadomość nie została wysłana)
::       - BODYT: raport
::  OLD: \em_send/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_mp:=_a;

_result:=obj_new('RESULT','BODYT');
_raport:=obj_new('RAPORT'); _raport.RAPORT:='Niewysłane wiadomości:\n\n';

{? exec('sendmail_on','#mailbox')
||
   POCZTA.cntx_psh();
   POCZTA.clear();

   _to_send:=sql('
      select POCZTA.REFERENCE as REF
      from POCZTA
      where POCZTA.FIRMA=:_a and POCZTA.PE=\'P\' and POCZTA.BOX=\'N\' and POCZTA.TRYB=\'T\'
   ',REF.FIRMA);

   KOMM.init(150,,'Wysyłanie wiadomości'@,'');
   _ok:=1;
   {? _to_send.first()
   || {!
      |? _ok*=exec('em_send_one','#mailbox',_to_send.REF,1,_raport);
         _to_send.next()
      !}
   ?};
:{? ~_mp.isService() || KOMM.select() ?};
   KOMM.select();

   POCZTA.cntx_pop()
||
   _ok:=1
?};

{? _ok || _result.RESULT:='OK' || _result.RESULT:='BŁĄD' ?};
_result.BODYT:=_raport.RAPORT;
_result


\chk_sent
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Sprawdzenie, czy wiadomość została wysłana
::   WE: _a - TAB.ref() powiązanego rekordu źródłowego
::   WY: oznaczenie skrzynki, w której jest wysłana wiadomość
::----------------------------------------------------------------------------------------------------------------------
_source_ref:=exec('FindAndGet','#table',ref_tab(_a),_a,,"uidref()",'');
_box:='';
POCZTA.cntx_psh();
POCZTA.index('SOURCE');
POCZTA.prefix(REF.FIRMA,_source_ref,);
{? POCZTA.first() || _box:=POCZTA.BOX ?};
POCZTA.cntx_pop();
_box


\log4source
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Wyświetla logi poczty dla źródłowego zapisu
::   WE: _a - TAB.ref() powiązanego rekordu źródłowego
::----------------------------------------------------------------------------------------------------------------------
{? exec('get','#params',700007,2)='I'
|| _source_ref:=exec('FindAndGet','#table',ref_tab(_a),_a,,"uidref()",'');
   POCZTA.index('SOURCE');
   POCZTA.prefix(REF.FIRMA,_source_ref,);
   {? POCZTA.first()
   || exec('em_eml_r','#mailbox')
   || FUN.info('Wiadomość e-mail nie odnaleziona.'@)
   ?}
|| FUN.info(
      'Raporty nie są ustawione jako indywidualne dla wiadomości.\n'
      'Należy zmienić parametr 700007 w systemie, aby można było śledzić raporty.'@
   )
?};
''


\edit_email
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Redagowanie parametru typu e-mail
::   WE: [_a] - STRING - wartość początkowa
::       [_b] - STRING - akronim parametru
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _value:=_a || _value:='' ?};
{? var_pres('_b')=type_of('') || _port:=_b || _port:='' ?};

_result:=exec('edit_email','#edit',_value,'Wartość parametru '+_port);
{? type_of(_result)=type_of('')
|| _result
|| _value
?}


\edit_email_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Redagowanie parametru typu lista e-maili
::   WE: [_a] - TEXT - wartość początkowa
::       [_b] - STRING - akronim parametru
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _value:=_a || _value:='' ?};
{? var_pres('_b')=type_of('') || _port:=_b || _port:='' ?};

_result:=exec('edit_email_list','#edit',_value,'Wartość parametru '+_port);
{? type_of(_result)=type_of('')
|| _result
|| _value
?}


\source_lock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MaX [12.30]
:: OPIS: Ustawia locka na dokument źródłowy aby można było wpisać mu stan
::   WY: 1-Udało się zablokować rekord, 0-Nie udało się zablokować rekordu
::  OLD: \dokumlocki/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? POCZTA.SOURCE<>''
|| _tab:=ref_tab(POCZTA.SOURCE);
   {? var_pres('_tab')>100
   || _maska:=ref_name(POCZTA.SOURCE);
      _tab.cntx_psh();
      {? _tab.name()<>_maska
      || _tab.use(_maska)
      ?};
      _tab.prefix();
      {? _tab.seek(POCZTA.SOURCE)
      || _result:=_tab.r_lock(1,1,1);
         {? _result & var_pres('REFSQL',_tab)=27 & _tab.REFSQL<>''
         || _tab1:=ref_tab(_tab.REFSQL);
            {? var_pres('_tab1')>100
            || _maska1:=ref_name(_tab.REFSQL);
               _tab1.cntx_psh();
               {? _tab1.name()<>_maska1
               || _tab1.use(_maska1)
               ?};
               _tab1.prefix();
               {? _tab1.seek(_tab.REFSQL)
               || _result:=_tab1.r_lock(1,1,1)
               || _result:=0
               ?};
               _tab1.cntx_pop()
            ?}
         ?}
      || _result:=0
      ?};
      _tab.cntx_pop()
   ?}
?};
_result


\source_unlock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MaX [12.30]
:: OPIS: Zdejmuje locka z dokumentu źródłowego
::  OLD: \dokumlocki/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? POCZTA.SOURCE<>''
|| _tab:=ref_tab(POCZTA.SOURCE);
   {? var_pres('_tab')>100
   || _maska:=ref_name(POCZTA.SOURCE);
      _tab.cntx_psh();
      {? _tab.name()<>_maska
      || _tab.use(_maska)
      ?};
      _tab.prefix();
      {? _tab.seek(POCZTA.SOURCE)
      || _tab.r_unlock();
         {? var_pres('REFSQL',_tab)=27 & _tab.REFSQL<>''
         || _tab1:=ref_tab(_tab.REFSQL);
            {? var_pres('_tab1')>100
            || _maska1:=ref_name(_tab.REFSQL);
               _tab1.cntx_psh();
               {? _tab1.name()<>_maska1
               || _tab1.use(_maska1)
               ?};
               _tab1.prefix();
               {? _tab1.seek(_tab.REFSQL)
               || _tab1.r_unlock()
               ?};
               _tab1.cntx_pop()
            ?}
         ?}
      ?};
      _tab.cntx_pop()
   ?}
?};
~~


\source_set_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MaX [12.30]
:: OPIS: Ustawia informacje dla rekordu źródłowego przy wysyłce maila
::       obsługiwane pola: _tab.SW - [STRING] - stan wysyłki (opisowo)
::                         _tab.WO - [STRING] - wiadomość wysłana (N/T)
::   WE: [_a] - opis do przekazania
::       [_b] - [T/N] znacznik czy mail został wysłany
::  OLD: \dokum_set_info/skid_eml.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _sw:=_a || _sw:='' ?};
{? var_pres('_b')=type_of('') || _wo:=_b || _wo:='N' ?};

{? POCZTA.SOURCE<>''
|| _tab:=ref_tab(POCZTA.SOURCE);
   _maska:=ref_name(POCZTA.SOURCE);
   _tab.cntx_psh();
   {? _tab.name()<>_maska
   || _tab.use(_maska)
   ?};
   _tab.prefix();
   {? _tab.seek(POCZTA.SOURCE)
   || _put:=0;
      {? var_pres('SW',_tab)=27
      || _pos:=_tab.SW*^160;
         _tab.SW:={? _pos=0 || _sw || _sw+^160+(_pos-_tab.SW) ?};
         _put:=1
      ?};
      {? var_pres('WO',_tab)=27
      || _tab.WO:=_wo;
         _put:=1
      ?};
      {? _put || _tab.put() ?}
   ?};
   _tab.cntx_pop()
?};
~~


\email_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Sprawdza, czy do wiadomości przypisane są poprawne adresy e-mail
::----------------------------------------------------------------------------------------------------------------------
KOMM.init(,,'Niepoprawne adresy e-mail','');
POCZTAR.cntx_psh();
POCZTAR.index('LP');
POCZTAR.prefix(POCZTA.ref());
_empty:=1;
{? POCZTAR.first()
|| {!
   |? _empty:=0;
      {? ~exec('mail_ok','#email',POCZTAR.SRCV) || KOMM.add(POCZTAR.SRCV) ?};
      POCZTAR.next()
   !}
?};
POCZTAR.cntx_pop();
{? KOMM.empty()
|| {? _empty
   || FUN.info('Brak adresów e-mail do sprawdzenia.'@)
   || FUN.info('Adresy e-mail w wiadomości są poprawne.'@)
   ?}
|| KOMM.select()
?};
~~


\box_name
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Zwraca nazwę skrzynki
::   WE: _a - Kod skrzynki: N, S, D
::   WY: Nazwa skrzynki
::----------------------------------------------------------------------------------------------------------------------
{? _a='N' || _box_name:='Skrzynka nadawcza'@
|? _a='S' || _box_name:='Elementy wysłane'@
|? _a='D' || _box_name:='Elementy usunięte'@
|? _a='I' || _box_name:='Skrzynka odbiorcza'@
          || _box_name:='---'
?};
_box_name


\icons
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Ustala ikony w oknie wertowania poczty
::   WE: _a - akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_wer:=_a;

:: ikona skrzynki
{? 3+_wer='SEL'
|| _formikon:="exec('box_icon','#mailbox',POCZTA.BOX)";
   POCZTA.win_fml(_wer,POCZTAV,'BOX',,'ICON_BEFORE',_formikon)
?};
:: ikona załączników
_formikon:="
   POCZTAA.index('P');
   POCZTAA.prefix(POCZTA.ref());
   {? POCZTAA.first()
   || 'xwin16.png:119'
   || 'xwin16.png:110'
   ?}
";
POCZTA.win_fml(_wer,,'SUB',,'ICON_BEFORE',_formikon);
:: ikona kłódki
_formikon:="{? POCZTA.TRYB='T' || 'xwin16.png:156' |? POCZTA.TRYB='X' || 'xwin16.png:158' || 'xwin16.png:157' ?}";
POCZTA.win_fml(_wer,,'TRYB',,'ICON_BEFORE',_formikon);
:: ikona listy odbiorców
{? _wer='WER'
|| _formikon:="
      POCZTAR.index('P');
      POCZTAR.prefix(POCZTA.ref());
      {? POCZTAR.first() & POCZTA.FRCV<>''
      || 'xwin16.png:32'
      |? POCZTAR.first()
      || 'xwin16.png:101'
      |? POCZTA.FRCV<>''
      || 'xwin16.png:33'
      || 'xwin16.png:110'
      ?}";
::   POCZTA.win_fml(_wer,POCZTAV,'STRING',,'ICON_BEFORE',_formikon)
   POCZTA.win_fml(_wer,,'DT',,'ICON_BEFORE',_formikon)
?};
~~


\template_files
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Zwraca listę plików wzorców wiadomości e-mail
::   WE: [_a] - czy ładować opisy z komentarzy [0]/(1)
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _load_desc:=_a || _load_desc:=0 ?};

_files:=files('*template*.htm');
{? _load_desc
|| _tab:=tab_tmp(1,'FILENAME','STRING[64]','Plik'@,'DESC','STRING[255]','Opis'@);
   {? _files.first()
   || {!
      |? _tab.FILENAME:=_files.FILENAME;
         _file:=fopen(_files.FILENAME,'ur',1,0,1);
         {? _file.is_open()
         || _desc:='';
            {!
            |? _line:=_file.fread();
               _pos1:=_line*'[[DESCRIPTION:';
               {? _pos1>0
               || _pos2:=_line*']]';
                  _desc:=(_pos1+13)-((_pos2-1)+_line)
               ?};
               _line<>'\n' & _desc=''
            !};
            _tab.DESC:=_desc;
            _file.fclose()
         ?};
         obj_del(_file);
         _tab.add();
         _files.next()
      !}
   ?};
   _tab
|| _files
?}


\template_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Obsługa na F3 w polu B_MSG.TEMPLATE, POCZTA.TEMPLATE, parametru 700014
::       Kontekst wywołania - pole
::   WY: nazwa wybranego pliku wzorca / ~~
::----------------------------------------------------------------------------------------------------------------------
_version:=exec('template_version','#mailbox');

_result:=~~;
_tab:=exec('template_names','#mailbox',{? _version=1 || 'P' || 'PD' ?});
{? type_of(_tab)>100
|| _tab.find_key(fld());
   _wer:=_tab.mk_sel('Wzorce'@,,,'templates',,,10,,'U');
   {? _version>1
   || _tab.win_fld(_wer,,'FILE',,,,,,,,,2,,"'T'","'N'")
   ?};
   _tab.win_fld(_wer,,'FILENAME',,,30);
   _tab.win_fld(_wer,,'DESC',,,80);
   _tab.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
   _tab.win_act(_wer,,'Formuła','Legenda'@@,,,"exec('legenda','color','$%1'['Wersja zmodyfikowana'@])",,,,,,'L');
   _formula:="
      _tab:=cur_tab(1,1);
      _m_filename:='%1.m.htm'[spli_str(_tab.FILENAME,'.htm')[1]];
      {? fexists(_m_filename,1)
      || exec('findtmp','#color')
      || ~~
      ?}
   ";
   _tab.win_act(_wer,,'Rekord',,,,_formula);
   _tab.win_sel(_wer);
   {? _tab.select(,1,5)
   || _result:=_tab.FILENAME
   ?}
?};
_result


\template_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Obsługa po redagowaniu pola B_MSG.TEMPLATE, POCZTA.TEMPLATE, walidacja dla parametru 700014
::   WE: [_a] - wartość sprawdzanego pola
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _val:=_a || _val:=fld() ?};

_version:=exec('template_version','#mailbox');

_result:=1;
{? _val<>''
|| _tab:=exec('template_names','#mailbox',{? _version=1 || 'P' || 'PD' ?});
   _tab.prefix(_val,);
   {? ~_tab.first()
   || FUN.info('Wzorzec %1 nie jest dostępny w pliku ani w bazie wzorców.'@[_val]);
      _result:=0
   ?}
?};
_result


\template_param
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Redagowanie wzorca od strony parametrów
::   WE: [_a] - TEXT - wartość początkowa
::       [_b] - STRING - akronim parametru
::   WY: STRING - wartość parametru
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _value:=_a || _value:='' ?};
{? var_pres('_b')=type_of('') || _port:=_b || _port:='' ?};

_valid:="
   _txt:=cur_tab(1,1).memo_txt(,,'MEMO');
   exec('head_tags_valid','#b_msg',_txt,'MEMO')
";
_result:=exec('edit_string','#edit',
   _value,
   'Wartość parametru %1'@[_port],
   "exec('template_ae','#mailbox',cur_tab(1,1).VAL)",
   "exec('template_f3','#mailbox')"
);
{? type_of(_result)=type_of('')
|| _result
|| _value
?}


\del_source
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Przeniesienie do usuniętych poczty powiązanej z rekordem źródłowym
::   WE: _a - TAB.uidref() rekordu źródłowego
::----------------------------------------------------------------------------------------------------------------------
_source_uref:=_a;
{? _source_uref<>''
|| POCZTA.cntx_psh();
   POCZTA.index('SOURCE');
   POCZTA.prefix(REF.FIRMA,_source_uref,);
   {? POCZTA.first()
   || {!
      |? {? POCZTA.BOX='N' | POCZTA.BOX='S'
         || POCZTA.BOX:='D';
            POCZTA.put(1)
         ?};
         POCZTA.next()
      !}
   ?};
   POCZTA.cntx_pop()
?};
~~


\tresc_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.42]
:: OPIS: Uruchamia podgląd wiadomości za pomocą edytora HTML
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
exec('em_eml_g','#mailbox',2);
~~


\sendmail_on
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.14]
:: OPIS: Warunek na włączenie wysyłki maili
::   WE: _a - czy wyświetlać komunikat [0]/1
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _dialog:=_a || _dialog:=0 ?};

_app_ident:=app_info('app_ident');
:: odczytanie parametrów przekazanych w cluster.cfg
_ap:=AppList.my.tab;
_ap.index(AppList.my.ndx_app);
_ap.prefix(_app_ident,);
{? _ap.first()
|| _app_par5:=_ap.APP_PAR5
|| _app_par5:=''
?};
_sendmail_off:='SENDMAIL_OFF';
_on:=((_app_par5*_sendmail_off)=0);
{? _dialog & _on=0
|| FUN.info(
      'Wysyłanie wiadomości jest wyłączone.\n\n'
      'W pliku %1 dla bieżącej aplikacji (%2) jest ustawiona wartość %3=%4.'@
      ['cluster.cfg',_app_ident,'app_par5',_sendmail_off]
   )
?};
_on


\proc_details
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Zwraca informacje o symbolu procesu oraz kodzie instancji z jakiego zostało utworzone powiadomienie.
::       Dotyczy bieżącego rekordu tabeli POCZTA
::----------------------------------------------------------------------------------------------------------------------
_res:='';
{? POCZTA.PROC<>'' & POCZTA.INS_CODE<>''
|| _res+='Proces: %1, kod instancji: %2.'@[POCZTA.PROC,POCZTA.INS_CODE]
?};

_res


\auth_kind_blank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Wartość początkowa pola POCZTAM.AUTH_KND
::----------------------------------------------------------------------------------------------------------------------
exec('auth_basic','#mailbox')


\auth_kind_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Przed redakcją pola POCZTAM.AUTH_KND
::----------------------------------------------------------------------------------------------------------------------
POCZTAM.AUTH='1'


\auth_kind_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: F3 dla pola POCZTAM.AUTH_KND
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('auth_kind_list','#mailbox');
_tab.prefix();
_tab.find_key(fld());
_wer:=_tab.mk_sel('Metody autoryzacji'@,,,'authkindsel',,,10,,'U');
_tab.win_fld(_wer,,'AUTH');
_tab.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
_tab.win_sel(_wer);
{? _tab.select(,1,-1)
|| _tab.AUTH
|| ~~
?}


\auth_kind_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Po redakcji dla pola POCZTAM.AUTH_KND
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('auth_kind_list','#mailbox');
_tab.prefix(fld(),);
{? _tab.first()
|| 1
|| FUN.info('Błędna metoda autoryzacji.'@);
   0
?}


\auth_kind_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Lista metod autoryzacji
::   WY: tab_tmp()
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,'AUTH','STRING[20]','Metody autoryzacji'@);
_tab.AUTH:=exec('auth_basic','#mailbox'); _tab.add();
{? exec('client_id','#oauth2_gm')<>''
|| _tab.AUTH:=exec('auth_oauth2_g','#mailbox'); _tab.add()
?};
{? exec('client_id','#oauth2_ms')<>''
|| _tab.AUTH:=exec('auth_oauth2_m','#mailbox'); _tab.add()
?};
_tab


\auth_basic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Autoryzacja Basic
::----------------------------------------------------------------------------------------------------------------------
'Basic'


\auth_oauth2_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Autoryzacja OAuth2 Google
::----------------------------------------------------------------------------------------------------------------------
'OAuth2 Google'


\auth_oauth2_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Autoryzacja OAuth2 Microsoft
::----------------------------------------------------------------------------------------------------------------------
'OAuth2 Microsoft'


\srv_auth
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Wywołuje proces autoryzacji - pobranie tokentów
::----------------------------------------------------------------------------------------------------------------------
exec('oauth_challange','#oauth2',POCZTAM.AUTH_KND);
~~


\edit_phone_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Redagowanie parametru typu numer telefonu
::   WE: [_a] - STRING - wartość początkowa
::       [_b] - STRING - akronim parametru
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _value:=_a || _value:='' ?};
{? var_pres('_b')=type_of('') || _port:=_b || _port:='' ?};

_result:=exec('edit_phone_nr','#edit',_value,'Wartość parametru '+_port);
{? type_of(_result)=type_of('')
|| _result
|| _value
?}


\pocztate_fld_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wartość początkowa pól tabeli POCZTATE
::----------------------------------------------------------------------------------------------------------------------
_afld:=cur_afld();
{? _afld='SYSTEM'
|| ''
|? _afld='BODY'
|| 'N'
|| ~~
?}


\pocztate_fld_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Przed redakcją pól tabeli POCZTATE
::----------------------------------------------------------------------------------------------------------------------
_afld:=cur_afld();
{? _afld='BODY'
|| {? POCZTATE.SYSTEM='T'
   || 0
   || 1
   ?}
|| ~~
?}


\pocztate_fld_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Obsługa na F3 dla pól tabeli POCZTATE
::----------------------------------------------------------------------------------------------------------------------
_afld:=cur_afld();
{? _afld='ICON'
|| _icon:=exec('ico_selttf','#icon',POCZTATE.ICON);
   {? _icon<>''
   || _icon
   || ~~
   ?}
|| ~~
?}


\pocztate_fld_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Po redakcji pól tabeli POCZTATE
::----------------------------------------------------------------------------------------------------------------------
::_afld:=cur_afld();
1


\pocztate_mk_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Tworzy okno wertowania tabeli POCZTATE
::----------------------------------------------------------------------------------------------------------------------
POCZTATE.win_edit(exec('pocztate_mk_edit','#mailbox'));

_version:=exec('template_version','#mailbox');

_wer:=POCZTATE.mk_sel('Dostępne elementy, z których można zbudować wzorzec'@,,,'pocztatesel',,,,,'U');
POCZTATE.win_fld(_wer,,'SYSTEM',,,,,,,,,2,,"'T'","'N'");
{? _version>1
|| POCZTATE.win_fld(_wer,,'BODY',,,,,,,,,2,,"'T'","'N'")
?};
POCZTATE.win_fld(_wer,,'SYMBOL');
POCZTATE.win_fld(_wer,,'NAME');
_formula:="exec('pocztate_add','#mailbox')";
POCZTATE.win_act(_wer,0,'Formuła','Dołącz'@@,,,_formula,,,,,,'D');
POCZTATE.win_act(_wer,1,'Formuła','Dołącz'@@,,,_formula,,,,,,'D');
_formula:="exec('pocztate_modify','#mailbox')";
POCZTATE.win_act(_wer,,'Formuła','Popraw'@@,,,_formula,,,,,,'P');
_formula:="exec('pocztate_del','#mailbox')";
POCZTATE.win_act(_wer,,'Formuła','Usuń'@@,,,_formula,,,,,,'U');
_formula:="exec('pocztate_copy','#mailbox')";
POCZTATE.win_act(_wer,,'Formuła','K&opiuj'@@,,,_formula,,,,,,'O');
_formula:="exec('pocztate_import','#mailbox')";
POCZTATE.win_act(_wer,0,'Formuła','Importuj'@@,,'Importuje elementy systemowe'@,_formula,,,,,,'I');
POCZTATE.win_act(_wer,1,'Formuła','Importuj'@@,,'Importuje elementy systemowe'@,_formula,,,,,,'I');
POCZTATE.win_act(_wer,,'Kolejność');
_formula:="
   {? _a
   || _grayed:={? POCZTATE.SYSTEM='T' || 'PU' || '' ?};
      POCZTATE.actions_grayed(cur_win(1,1),_grayed)
   ?};
   ~~
";
POCZTATE.win_act(_wer,,'Rekord',,,,_formula);
POCZTATE.win_fml(_wer,,'SYMBOL',,'ICON_BEFORE',"exec('pocztate_icon','#mailbox')");
_wer


\pocztate_icon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Ikona przy symbolu elementu
::----------------------------------------------------------------------------------------------------------------------
{? POCZTATE.ICON=''
|| exec('pusta','#icon')
|| POCZTATE.ICON+{? POCZTATE.SYSTEM='T' || '' || ',106:168:79' ?}
?}


\pocztate_mk_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Tworzy okno redagowania tabeli POCZTATE
::----------------------------------------------------------------------------------------------------------------------
_width:=100;

_varsion:=exec('template_version','#mailbox');

_red:=POCZTATE.mk_edit('Element wzorca'@,,'templateele');
POCZTATE.win_efld(_red,,'SYSTEM',,,,,1,,,,
   'check-box','check_label="%1"'['Element systemowy, nie podlega modyfikacji'@],"'T'","'N'"
);
{? _varsion>1
|| POCZTATE.win_efld(_red,,'BODY',,,,,,,,,
      'check-box','check_label="%1"'['Dotyczy treści wiadomości'@],"'T'","'N'"
   )
?};
POCZTATE.win_efld(_red,,'SYMBOL',,,_width,,,'Element'@);
POCZTATE.efld_opt(_red,'mark=1',,'SYMBOL');
POCZTATE.win_efld(_red,,'NAME',,,_width,,,'Nazwa'@);
POCZTATE.efld_opt(_red,'mark=1',,'NAME');
POCZTATE.win_efld(_red,,'ICON',,,_width-3,,,,,,,'F3_button=1');
POCZTATE.win_efld(_red,,'CONTENT',,,_width,-20);
POCZTATE.efld_opt(_red,'mark=1',,'CONTENT');
exec('ok_esc','#window',POCZTATE,_red);
_red


\pocztate_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS:
::----------------------------------------------------------------------------------------------------------------------
_varsion:=exec('template_version','#mailbox');

POCZTATE.cntx_psh();
{? _varsion=1
|| POCZTATE.index('SYSSYM');
   POCZTATE.prefix('N')
|| POCZTATE.index('SYMBOL');
   POCZTATE.prefix()
?};
POCZTATE.win_sel(exec('pocztate_mk_sel','#mailbox'));
POCZTATE.select();
POCZTATE.cntx_pop();
~~


\pocztate_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Akcja importu standardowych elementów wzorca
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy zaimportować lub zaktualizować elementy systemowe?'@)
|| _tab:=exec('template_elements','#mailbox');
   {? _tab.first()
   || POCZTATE.cntx_psh();
      POCZTATE.index('SYMBOL');
      {!
      |? POCZTATE.prefix(_tab.SYMBOL,);
         {? POCZTATE.first()
         || POCZTATE.NAME:=_tab.NAME;
            POCZTATE.ICON:=_tab.ICON;
            POCZTATE.BODY:=_tab.BODY;
            POCZTATE.memo_set(_tab.memo_txt(,1,'CONTENT'));
            {? POCZTATE.put() || POCZTATE.memo_put(,'CONTENT') ?}
         || POCZTATE.SYSTEM:='T';
            POCZTATE.SYMBOL:=_tab.SYMBOL;
            POCZTATE.NAME:=_tab.NAME;
            POCZTATE.ICON:=_tab.ICON;
            POCZTATE.BODY:=_tab.BODY;
            POCZTATE.memo_set(_tab.memo_txt(,1,'CONTENT'));
            {? POCZTATE.add() || POCZTATE.memo_put(,'CONTENT') ?}
         ?};
         _tab.next()
      !};
      POCZTATE.cntx_pop()
   ?}
?};
~~


\pocztate_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS:
::----------------------------------------------------------------------------------------------------------------------
_valid:="__CHK.table(POCZTATE,0,,'SYMBOL','NAME','CONTENT')";
POCZTATE.blank();
POCZTATE.SYSTEM:='N';
POCZTATE.memo_set('','CONTENT');
{? POCZTATE.edit(_valid)
|| {? POCZTATE.add() || POCZTATE.memo_put(,'CONTENT') ?}
?};
~~


\pocztate_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Modyfikacja dostępnego elementu wzorca
::----------------------------------------------------------------------------------------------------------------------
{? POCZTATE.SYSTEM='T'
|| FUN.info('Element systemowy — nie można modyfikować.'@)
|| _valid:="__CHK.table(POCZTATE,1,,'SYMBOL','NAME','CONTENT')";
   POCZTATE.memo_get(,'CONTENT');
   {? POCZTATE.edit(_valid)
   || {? POCZTATE.put() || POCZTATE.memo_put(,'CONTENT') ?}
   ?}
?};
~~


\pocztate_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Usunięcie dostępnego elementu wzorca
::----------------------------------------------------------------------------------------------------------------------
{? POCZTATE.SYSTEM='T'
|| FUN.info('Element systemowy — nie można usunąć.'@)
|? ~exec('testlink','#record',POCZTATE,1)
|| {? FUN.ask('Czy usunąć bieżący wiersz?'@)
   || POCZTATE.del()
   ?}
?};
~~


\pocztate_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS:
::----------------------------------------------------------------------------------------------------------------------
_valid:="__CHK.table(POCZTATE,0,,'SYMBOL','NAME','CONTENT')";
POCZTATE.memo_get(,'CONTENT');
POCZTATE.SYSTEM:='N';
{? POCZTATE.edit(_valid)
|| {? POCZTATE.add() || POCZTATE.memo_put(,'CONTENT') ?}
?};
~~


\templates
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Tabela z dostępnymi wzorcami standardowymi
::----------------------------------------------------------------------------------------------------------------------
_templates:=
   'error:Wzorzec template_error (błąd) standardowy:'
      'system_top,headline,icon_error,body,footer,logo,system_bottom;'
   'info:Wzorzec template_info (informacja) standardowy:'
      'system_top,headline,icon_info,body,footer,logo,system_bottom;'
   'invoice:Wzorzec template_invoice (faktura) standardowy:'
      'system_top,headline,icon_invoice,body,footer,logo,system_bottom;'
   'notify:Wzorzec template_notify (powiadomienie) standardowy:'
      'system_top,headline,icon_notify,body,footer,logo,system_bottom;'
   'polling:Wzorzec template_polling (głosowanie) standardowy:'
      'system_top,headline,icon_polling,body,footer,logo,system_bottom';
_templates_tab:=spli_str(_templates,';');

_tab:=tab_tmp(2
   ,'NAME','STRING[50]','Nazwa'@
   ,'DESC','STRING[255]','Opis'@
   ,'ELEMENTS','SYS_MEMO','Elementy'@
);

_size:=obj_len(_templates_tab);
{? _size>0
|| {! _it:=1.._size
   |! _tab.NAME:=spli_str(_templates_tab[_it],':')[1];
      _tab.DESC:=spli_str(_templates_tab[_it],':')[2];
      _elements:=spli_str(_templates_tab[_it],':')[3];
      _tab.memo_set(_elements,'ELEMENTS');
      {? _tab.add() || _tab.memo_put(,'ELEMENTS') ?}
   !}
?};
_tab


\templates_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Selekcja wzorców wiadomości - panel do zarządzania
::----------------------------------------------------------------------------------------------------------------------
POCZTATE.win_sel(exec('pocztate_mk_sel','#mailbox'));

_tab:=exec('template_names','#mailbox','P');
_wer_p:=_tab.mk_sel('Pliki wzorców'@,,,'templatefiles',,,,,'U');
_tab.win_fld(_wer_p,,'FILENAME',,,50);
_tab.win_fld(_wer_p,,'DESC',,,100);
_formula:="exec('template_view_file','#mailbox',cur_tab(1,1).FILENAME)";
_tab.win_act(_wer_p,,'Formuła','Pod&gląd (oryginał)'@@,,,_formula,,1,,,,'G');
_formula:="exec('template_view_file','#mailbox',cur_tab(1,1).FILENAME,1)";
_tab.win_act(_wer_p,,'Formuła','P&odgląd (wersja zmodyfikowana)'@@,,,_formula,,,,,,'O');
_formula:="exec('template_delete_file','#mailbox',cur_tab(1,1).FILENAME)";
_tab.win_act(_wer_p,,'Formuła','Usuń modyfikacje'@@,,,_formula,,,,,,'U');
_tab.win_act(_wer_p,,'Formuła','Legenda'@@,,,"exec('legenda','color','$%1'['Wersja zmodyfikowana'@])",,,,,,'L');
_formula:="
   _tab:=cur_tab(1,1);
   _m_filename:='%1.m.htm'[spli_str(_tab.FILENAME,'.htm')[1]];
   {? fexists(_m_filename,1)
   || exec('findtmp','#color')
   || ~~
   ?}
";
_tab.win_act(_wer_p,,'Rekord',,,,_formula);

_formula:="params_get().tab.first(); grp_disp(params_get().tab,params_get().wer_p)";
_grp:=POCZTATL.grp_make('Zarządzanie wzorcami'@,_formula,'templatemain',,,,,'html_maximized');

_wer_d:=exec('pocztatl_mk_sel','#mailbox');
POCZTATL.grp_sel(_grp,,_wer_d,,,,,,,,,,'maximized_with_title');
POCZTATL.grp_splt(_grp,,'horizontal','files','18,50%');
POCZTATL.grp_sel(_grp,_tab,_wer_p,,,,,,,,,,'maximized_with_title');
POCZTATL.win_sel(_grp);

params_set('tab',_tab,'wer_p',_wer_p);
POCZTATL.index('NAME');
POCZTATL.prefix();
POCZTATL.select();
''


\template_delete_file
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Usuwanie pliku .m.htm skojarzonego z podanym plikiem .htm
::   WE: _a - nazwa pliku .htm
::----------------------------------------------------------------------------------------------------------------------
_filename:=_a;

_m_filename:='%1.m.htm'[spli_str(_filename,'.htm')[1]];
{? fexists(_m_filename,1)
|| {? FUN.ask('Jest zapisana zmodyfikowana wersja wzorca %1.\nCzy ją usunąć?'[_filename])
   || ferase(_m_filename,1)
   ?}
|| FUN.info('Nie ma zapisanej zmodyfikowanej wersji wzorca %1.'@[_filename])
?};
~~


\pocztatl_mk_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Okno wertowania definicji wzorców wiadomości
::----------------------------------------------------------------------------------------------------------------------
_wer:=POCZTATL.mk_sel('Definicje'@,,,'templates',,,,,'U');
POCZTATL.win_fld(_wer,,'NAME',,,50);
POCZTATL.win_fld(_wer,,'DESC',,,100);
POCZTATL.win_act(_wer,0,'Formuła','Dołącz'@@,,,"exec('pocztatl_add','#mailbox')",,,,,,'D');
POCZTATL.win_act(_wer,1,'Formuła','Dołącz'@@,,,"exec('pocztatl_add','#mailbox')",,1,,,,'D');
POCZTATL.win_act(_wer,,'Formuła','Popraw'@@,,,"exec('pocztatl_modify','#mailbox')",,,,,,'P');
POCZTATL.win_act(_wer,,'Formuła','Usuń'@@,,,"exec('pocztatl_del','#mailbox')",,,,,,'U');
_formula:="
   exec('pocztatp_select','#mailbox');
   exec('template_write','#mailbox',0);
   ~~
";
POCZTATL.win_act(_wer,,'Formuła','Poz&ycje'@@,,,_formula,,1,,,,'Y');
_formula:="exec('pocztatl_import','#mailbox')";
POCZTATL.win_act(_wer,0,'Formuła','Importuj'@@,,,_formula,,,,,,'I');
POCZTATL.win_act(_wer,1,'Formuła','Importuj'@@,,,_formula,,,,,,'I');
_formula:="
   exec('pocztatl_copy','#mailbox');
   exec('template_write','#mailbox',0);
   ~~
";
POCZTATL.win_act(_wer,,'Formuła','K&opiuj'@@,,,,_formula,,,,,'O');
POCZTATL.win_act(_wer,,'Formuła','Zapisz'@@,,,"exec('template_write_file','#mailbox')",,,,,,'Z');
POCZTATL.win_act(_wer,,'Formuła','Pod&gląd'@@,,,"exec('template_view','#mailbox')",,,,,,'G');
POCZTATL.win_act(_wer,,'Formuła','Dostępne e&lementy'@@,,,"exec('pocztate_select','#mailbox')",,,,,,'L');
POCZTATL.win_act(_wer,,'Kolejność');
_wer


\pocztatl_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Import wzorców standardowych
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy zaimportować brakujące wzorce zgodne z definicjami standardowymi?'@)
|| _tab:=exec('templates','#mailbox');
   {? _tab.first()
   || POCZTATL.cntx_psh(); POCZTATE.cntx_psh(); POCZTATP.cntx_psh();
      POCZTATL.index('NAME'); POCZTATE.index('SYSSYM'); POCZTATP.prefix();
      {!
      |? POCZTATL.prefix(_tab.NAME,);
         {? ~POCZTATL.first()
         || POCZTATL.blank(1);
            POCZTATL.NAME:=_tab.NAME;
            POCZTATL.DESC:=_tab.DESC;
            {? POCZTATL.add()
            || _elements:=spli_str(_tab.memo_txt(,1,'ELEMENTS'),',');
               _size:=obj_len(_elements);
               {? _size>0
               || {! _it:=1.. _size
                  |! POCZTATE.prefix('N','T',_elements[_it],);
                     {? POCZTATE.first()
                     || POCZTATP.blank(1);
                        POCZTATP.LP:=_it;
                        POCZTATP.POCZTATL:=POCZTATL.ref();
                        POCZTATP.POCZTATE:=POCZTATE.ref();
                        POCZTATP.add()
                     ?}
                  !};
                  exec('template_write','#mailbox',0)
               ?};
               obj_del(_elements)
            ?}
         ?};
         _tab.next()
      !};
      POCZTATL.cntx_pop(); POCZTATE.cntx_pop(); POCZTATP.cntx_pop()
   ?}
?};
~~


\pocztatl_mk_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Okno redagowania wzorców wiadomości
::----------------------------------------------------------------------------------------------------------------------
_red:=POCZTATL.mk_edit('Wzorzec'@,,'templateedit');
POCZTATL.win_esep(_red,'Dane podstawowe'@);
POCZTATL.win_efld(_red,,'NAME',,,100);
POCZTATL.efld_opt(_red,'mark=1',,'NAME');
POCZTATL.win_efld(_red,,'DESC',,,100);
POCZTATL.efld_opt(_red,'mark=1',,'DESC');
exec('ok_esc','#window',POCZTATL,_red);
_red


\pocztatl_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Dołączenie wzorca
::----------------------------------------------------------------------------------------------------------------------
POCZTATL.win_edit(exec('pocztatl_mk_edit','#mailbox'));
POCZTATL.blank();
{? POCZTATL.edit("__CHK.table(POCZTATL,0,,'SYMBOL','NAME')")
|| POCZTATL.add()
?};
~~


\pocztatl_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Poprawienie wzorca
::----------------------------------------------------------------------------------------------------------------------
POCZTATL.win_edit(exec('pocztatl_mk_edit','#mailbox'));
{? POCZTATL.edit("__CHK.table(POCZTATL,1,,'SYMBOL','NAME')")
|| POCZTATL.put()
?};
~~


\pocztatl_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Usuwanie wzorca
::----------------------------------------------------------------------------------------------------------------------
:: Sprawdzenie wykorzystania - parametr 700014
_default_template:=exec('get','#params',700014);
{? POCZTATL.NAME=_default_template
|| FUN.info('Nie można usunąć, wzorzec ustawiony jest jako domyślny (parametr %1).'[$700014])
|? FUN.ask('Czy usunąć worzec?'@)
|| do();
   POCZTATP.cntx_psh();
   POCZTATP.index('LP');
   POCZTATP.prefix(POCZTATL.ref());
   {? POCZTATP.first()
   || {!
      |? POCZTATP.del()
      !}
   ?};
   POCZTATP.cntx_pop();
   POCZTATL.del();
   end()
?};
~~


\pocztatl_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Kopiuje wzorzec z pozycjami
::----------------------------------------------------------------------------------------------------------------------
_from:=POCZTATL.ref();
POCZTATL.win_edit(exec('pocztatl_mk_edit','#mailbox'));
{? POCZTATL.edit("__CHK.table(POCZTATL,0,,'SYMBOL','NAME')")
|| {? POCZTATL.add()
   || _to:=POCZTATL.ref();
      POCZTATP.index('LP');
      POCZTATP.prefix(_from);
      {? POCZTATP.first()
      || {!
         |? POCZTATP.cntx_psh();
            POCZTATP.prefix();
            POCZTATP.POCZTATL:=_to;
            POCZTATP.add();
            POCZTATP.cntx_pop();
            POCZTATP.next()
         !}
      ?}
   ?}
?};
~~


\pocztatp_fld_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Po redakcji pól w tabeli POCZTATP
::----------------------------------------------------------------------------------------------------------------------
1


\pocztatp_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Selekcja pozycji wzorca wiadomości
::----------------------------------------------------------------------------------------------------------------------
POCZTATP.cntx_psh();

POCZTATP.index('LP');
POCZTATP.prefix(POCZTATL.ref());

_wer:=exec('pocztatp_mk_sel','#mailbox');
POCZTATP.win_sel(_wer);
POCZTATP.select();

POCZTATP.cntx_pop();
~~


\pocztatp_mk_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Okno wertowania pozycji wzorca wiadomości
::----------------------------------------------------------------------------------------------------------------------
POCZTATP.win_edit(exec('pocztatp_mk_edit','#mailbox'));

_wer:=POCZTATP.mk_sel('Pozycje wzorca'@,,,'templatespos',,,,,'U');
POCZTATP.win_fld(_wer,,'LP',,,5);
POCZTATP.win_fld(_wer,,'POCZTATE','SYMBOL',,,,,'Element'@);
POCZTATP.win_fld(_wer,,'POCZTATE','NAME',,,,,'Nazwa'@);
_formula:="
   _lp:=exec('NewOrder','#table','POCZTATP','LP','LP',POCZTATL.ref());
   POCZTATP.blank();
   POCZTATP.LP:=_lp;
   POCZTATE.memo_set('','CONTENT');
   POCZTATP.POCZTATL:=POCZTATL.ref();
   _valid:=\"__CHK.table(POCZTATP,0,,'POCZTATE')\";
   {? POCZTATP.edit(_valid)
   || POCZTATP.add()
   ?};
   ~~
";
POCZTATP.win_act(_wer,0,'Formuła','Dołącz'@@,,,_formula,,,,,,'D');
POCZTATP.win_act(_wer,1,'Formuła','Dołącz'@@,,,_formula,,1,,,,'D');
_formula:="
   _valid:=\"__CHK.table(POCZTATP,1,,'POCZTATE')\";
   {? POCZTATP.edit(_valid)
   || POCZTATP.put()
   ?};
   ~~
";
POCZTATP.win_act(_wer,,'Formuła','Popraw'@@,,,_formula,,,,,,'P');
_formula:="
   {? FUN.ask('Czy usunąć bieżący wiersz?'@)
   || POCZTATP.del();
      exec('ReNumAfterDel','#table','POCZTATP','LP','POCZTATL.ref()')
   ?};
   ~~";
POCZTATP.win_act(_wer,,'Formuła','Usuń'@@,,,_formula,,,,,,'U');
POCZTATP.win_act(_wer,,'Formuła','Zapisz'@@,,,"exec('template_write_file','#mailbox')",,,,,,'Z');
_formula:="
   exec('template_write','#mailbox',0);
   exec('template_view','#mailbox');
   ~~
";
POCZTATP.win_act(_wer,,'Formuła','Pod&gląd'@@,,,_formula,,1,,,,'G');
_formula:="
   POCZTATP.POCZTATE();
   POCZTATE.memo_get(,'CONTENT',0);
   ~~
";
POCZTATP.win_act(_wer,0,'Menu','Przesuń&'@@,,'Przenumerowanie pozycji'@,,,,,,,'Ń');
POCZTATP.win_act(_wer,0,'Formuła','W &górę'@@,'#Ń','Przesuwa rekord w górę o n pozycji'@,,
   "params_exec('pocztatp_move','#mailbox','U')",,1,"params_exec('pocztatp_move','#mailbox','U')",,'G',0);
POCZTATP.act_icn(_wer,0,'ŃG','|2arrow-up');
POCZTATP.win_act(_wer,0,'Formuła','W &dół'@@,'#Ń','Przesuwa rekord w dół o n pozycji'@,
   "params_exec('pocztatp_move','#mailbox','D')",,,1,"params_exec('pocztatp_move','#mailbox','D')",,'D');
POCZTATP.act_icn(_wer,0,'ŃD','|2arrow-down');
POCZTATP.win_act(_wer,0,'Formuła','Do &numeru'@@,'#Ń','Przesuwa rekord do wskazanego numeru'@,
   "params_exec('pocztatp_move','#mailbox','N')",,,1,"params_exec('pocztatp_move','#mailbox','N')",,'N');
POCZTATP.act_icn(_wer,0,'ŃN','|arrow-goto-next');
POCZTATP.win_act(_wer,,'Rekord',,,,_formula);
POCZTATP.win_fml(_wer,,'POCZTATE','SYMBOL','ICON_BEFORE',"exec('pocztate_icon','#mailbox')");
_wer


\pocztatp_move
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Akcja Przesuń w oknie z pozycjami wzorca
::   WE: _a - STRING - kierunek przesuwania: 'U','D','N'
::----------------------------------------------------------------------------------------------------------------------
_dir:='N';
{? var_pres('_a')=type_of('')
|| _dir:=_a
?};
exec('zmien_lpa','#dragdrop','LP','LP',,,_dir);
~~


\pocztatp_mk_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Okno redagowania pozycji wzorca wiadomości
::----------------------------------------------------------------------------------------------------------------------
_width:=100;

_red:=POCZTATP.mk_edit('Pozycja wzorca'@,,'templatepos');
POCZTATP.win_efld(_red,,'LP',,,,,1);
POCZTATP.win_efld(_red,,'POCZTATE','SYMBOL','SYSSYM',_width-3,,,'Element'@);
POCZTATP.win_efld(_red,,'POCZTATE','NAME',,_width-3,,,'Nazwa'@);
POCZTATP.win_efld(_red,POCZTATE,'CONTENT',,,_width,-20,1);
exec('ok_esc','#window',POCZTATP,_red);
_red


\template_write
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Zapisuje zawartość wzorca do pola POCZTATL.CONTENT
::   WE: [_a] - dialog 0/[1]
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _dialog:=_a || _dialog:=1 ?};
{? ~_dialog | FUN.ask('Czy zapamiętać aktualną zawartość wzorca?'@)
||
   POCZTATP.cntx_psh(); POCZTATE.cntx_psh();
   POCZTATE.prefix();
   POCZTATP.index('LP');
   POCZTATP.prefix(POCZTATL.ref());
   {? POCZTATP.first()
   || _file:=fopen(null(),'Uw!',,1,1);
      {? _file.is_open()
      || {!
         |? POCZTATP.POCZTATE().memo_get(,'CONTENT',0);
            _file.fwrite(POCZTATE.memo_txt(,0,'CONTENT'));
            POCZTATP.next()
         !};
         POCZTATL.bl_put('CONTENT',_file,,,'template.htm');
         _file.fclose()
      ?}
   ?};
   POCZTATP.cntx_pop(); POCZTATE.cntx_pop()
?};
~~


\template_write_file
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Zapisuje zawartość wzorca z pola POCZTATL.CONTENT do pliku m.htm zgodnie z pth
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('template_names','#mailbox','P');
_wer:=_tab.mk_sel('Zapisanie zmodyfikowanej wersji wzorca'@);
_tab.win_fld(_wer,,'FILENAME',,,30);
_tab.win_fld(_wer,,'DESC',,,80);
_tab.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
_tab.win_act(_wer,,'Formuła','Legenda'@@,,,"exec('legenda','color','$%1'['Wersja zmodyfikowana'@])",,,,,,'L');
_formula:="
   _tab:=cur_tab(1,1);
   _m_filename:='%1.m.htm'[spli_str(_tab.FILENAME,'.htm')[1]];
   {? fexists(_m_filename,1)
   || exec('findtmp','#color')
   || ~~
   ?}
";
_tab.win_act(_wer,,'Rekord',,,,_formula);
_tab.win_sel(_wer);
{? _tab.select()
|| _m_filename:='%1.m.htm'[spli_str(_tab.FILENAME,'.htm')[1]];
   {? {? fexists(_m_filename,1)
      || FUN.ask('Jest już zapisana zmodyfikowana wersja wzorca %1.\nCzy zapisać?'[_tab.FILENAME])
      || FUN.ask('Czy zapisać zmodyfikowaną wersję wzorca %1?'@[_tab.FILENAME])
      ?}
   || POCZTATL.bl_get('CONTENT',_m_filename,1)
   ?}
?};
~~


\template_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Podgląd wzorca - w kontekście rekordu tabeli POCZTATL
::----------------------------------------------------------------------------------------------------------------------
{? POCZTATL.CONTENT=null()
|| FUN.info('Brak treści wzorca do podglądu.'@)
|| _choice:=FUN.choice('Czy załadować podgląd definicji wzorca?'@,,'Tylko wzorzec'@,'Przykład wypełnienia'@);

   {? _choice=2
   || exec('em_eml_g','#mailbox',1,2)
   |? _choice=1
   ||
::    Przekopiowanie utworzonego wzorca na terminal
      __fname:=exec('tmp_filepath','#file','template.htm');
      _can_continue:=POCZTATL.bl_get('CONTENT',__fname,0);

::    Uruchomienie w kontrolce webbrowser
      {? _can_continue>0
      || _win_acr:=POCZTATL.mk_ctr('Wzorzec — %1'@[POCZTATL.NAME],'#ctrl_browser');
         POCZTATL.win_ctr(_win_acr);
         POCZTATL.win_cctr(_win_acr,'ctr_id','@webframe');
         POCZTATL.control(,,"ctr_set(,'ctr_id','navigate','%1'[__fname])")
      ?};
      &__fname
   ?}
?};
~~


\template_view_file
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Podgląd wzorca - w kontekście pliku podanego parametrem
::   WE: _a - nazwa pliku wzorca (.htm) szukanego zgodnie z pth
::       [_b] - [0]/1 - czy wersja zmodyfikowana
::----------------------------------------------------------------------------------------------------------------------
_modified:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_fname:={? _modified>0 || '%1.m.htm'[spli_str(_a,'.htm')[1]] || _a ?};

{? ~fexists(_fname,1)
|| FUN.info('Nie ma zapisanej zmodyfikowanej wersji wzorca %1.'@[_a]);
   return()
?};

{? _modified
|| _choice:=FUN.choice('Czy załadować podgląd zmodyfkowanego wzorca %1?'@[_a],,'Tylko wzorzec'@,'Przykład wypełnienia'@)
|| _choice:=FUN.choice('Czy załadować podgląd wzorca %1?'@[_a],,'Tylko wzorzec'@,'Przykład wypełnienia'@)
?};

{? _choice=2
|| exec('em_eml_g','#mailbox',1,3,_fname)
|? _choice=1
||
:: Przekopiowanie pliku na terminal
   __fname:=exec('tmp_filepath','#file',_fname);
   _can_continue:=fcopy(_fname,__fname,1,0,1);

:: Uruchomienie w kontrolce webbrowser
   {? _can_continue>0
   || _win_acr:=POCZTATL.mk_ctr('Wzorzec — %1'@[_fname],'#ctrl_browser');
      POCZTATL.win_ctr(_win_acr);
      POCZTATL.win_cctr(_win_acr,'ctr_id','@webframe');
      POCZTATL.control(,,"ctr_set(,'ctr_id','navigate','%1'[__fname])")
   ?};
   &__fname
?};
~~


\template_elements
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Tabela z dostępnymi elementami standardowymi
::----------------------------------------------------------------------------------------------------------------------
_elements:=
   'N:system_top:Element systemowy — część górna:@system|windowpanel-tophide;'
   'N:system_top_wide:Element systemowy — część górna (wersja szeroka):@system|windowpanel-tophide;'
   'N:system_bottom:Element systemowy — część dolna:@system|windowpanel-bottomhide;'
   'N:headline:Nagłówek — temat wiadomości:@system|label;'
   'N:icon_error:Ikona "error" (błąd):|warning;'
   'N:icon_info:Ikona "info" (informacja):@system|file-info;'
   'N:icon_invoice:Ikona "invoice" (faktura):@system|document-text;'
   'N:icon_notify:Ikona "notify" (powiadomienie):@system|notifications-active;'
   'N:icon_polling:Ikona "polling" (głosowanie):|documents-check;'
   'N:body:Zawartość — zasadnicza treść wiadomości:@system|document-text;'
   'N:footer:Stopka (z parametru 700013):@system|is-empty;'
   'N:logo:Logo firmowe:|picture;'
   'T:button:Parametryzowany przycisk:|checkbox;'
   'T:button2:Parametryzowany przycisk podwójny:@system|indeterminate-check-box;'
   'T:style_table:Styl tabeli:@system|tool-brush;'
   'T:style_table_tr:Styl wiersza tabeli:@system|tool-brush;'
   'T:style_table_th:Styl celi nagłówka tabeli:@system|tool-brush;'
   'T:style_table_td:Styl celi tabeli:@system|tool-brush';
_elements_tab:=spli_str(_elements,';');

_tab:=tab_tmp(2
   ,'SYMBOL','STRING[30]','Symbol'@
   ,'NAME','STRING[100]','Nazwa'@
   ,'ICON','STRING[60]','Ikona'@
   ,'CONTENT','SYS_MEMO','Zawartość'@
   ,'BODY','STRING[1]','Element treści'@
);
_size:=obj_len(_elements_tab);
{? _size>0
|| {! _it:=1.._size
   |! _tab.SYMBOL:=spli_str(_elements_tab[_it],':')[2];
      _tab.NAME:=spli_str(_elements_tab[_it],':')[3];
      _tab.ICON:=spli_str(_elements_tab[_it],':')[4];
      _tab.BODY:=spli_str(_elements_tab[_it],':')[1];
      _content:=exec('%1_%2'[{? _tab.BODY='T' || 'body' || 'element' ?},_tab.SYMBOL],'#template');
      _tab.memo_set(_content,'CONTENT');
      {? _tab.add() || _tab.memo_put(,'CONTENT') ?}
   !}
?};
_tab


\template_elements_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Selekcja dostępnych elementów wzorca
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('template_elements','#mailbox');

_wer:=_tab.mk_sel('Dostępne elementy wzorca'@,,,'elementssel',,,,,'U');
_tab.win_fld(_wer,,'SYMBOL');
_tab.win_fld(_wer,,'BODY',,,,,,,,,2,,"'T'","'N'");
_tab.win_fld(_wer,,'NAME');
_tab.win_sel(_wer);

_width:=100;
_red:=_tab.mk_edit('Element'@,,'elementred');
_tab.win_efld(_red,,'SYMBOL',,,_width);
_tab.win_efld(_red,,'BODY',,,,,,,,,
   'check-box','check_label="%1"'['Dotyczy treści wiadomości'@],"'T'","'N'"
);
_tab.win_efld(_red,,'NAME',,,_width);
_tab.win_efld(_red,,'CONTENT',,,_width,-20);
_tab.win_edit(_red);

_tab.select();
~~


\template_names
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Zwraca tabelę tymczasową z nazwami wzorców - (P)likowe i (D)efiniowane w bazie
::   WE: [_a] - zakres wzorców: ['PD'], 'P', 'D'
::----------------------------------------------------------------------------------------------------------------------
_scope:={? var_pres('_a')=type_of('') || _a || 'PD' ?};

_tab:=tab_tmp(1
   ,'FILENAME','STRING[64]','Nazwa'@
   ,'DESC','STRING[255]','Opis'@
   ,'FILE','STRING[1]','Wzorzec w pliku'@
);

{? _scope*'P'>0
|| _tab_files:=exec('template_files','#mailbox',1);
   {? _tab_files.first()
   || {!
      |? {? _tab_files.FILENAME*'.m.'=0
         || _tab.FILE:='T';
            _tab.FILENAME:=_tab_files.FILENAME;
            _tab.DESC:=_tab_files.DESC;
            _tab.add()
         ?};
         _tab_files.next()
      !}
   ?}
?};

{? _scope*'D'>0
|| POCZTATL.cntx_psh();
   POCZTATL.index('NAME');
   POCZTATL.prefix();
   {? POCZTATL.first()
   || {!
      |? _tab.FILE:='N';
         _tab.FILENAME:=POCZTATL.NAME;
         _tab.DESC:=POCZTATL.DESC;
         _tab.add();
         POCZTATL.next()
      !}
   ?};
   POCZTATL.cntx_pop()
?};

_tab


\template_file
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Zwraca obiekt plikowy z wzorcem do odczytu
::   WE: _a - nazwa wzorca
::   WY: FILE
::----------------------------------------------------------------------------------------------------------------------
POCZTATL.cntx_psh();
POCZTATL.index('NAME');
POCZTATL.prefix(_a,);
{? POCZTATL.first()
|| _template:=fopen(POCZTATL.CONTENT,'ur',,,1)
|| _m_filename:='%1.m.htm'[spli_str(_a,'.htm')[1]];
   {? fexists(_m_filename,1)
   || _template:=fopen(_m_filename,'ur',1,,1)
   || _template:=fopen(_a,'ur',1,,1)
   ?}
?};
POCZTATL.cntx_pop();
_template


\proc_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Podgląd procesu, w którym powstała wiadomość
::----------------------------------------------------------------------------------------------------------------------
{? POCZTA.INS_CODE<>''
|| BI_PROC.index('CODE');
   BI_PROC.prefix(POCZTA.INS_CODE,);
   {? BI_PROC.first()
   || exec('select4biproc_alt','#b_design',BI_PROC.ref(),,'bi_proc_todo')
   || FUN.info('Podglad instancji procesu nie jest dostępny.'@)
   ?}
|| FUN.info('Wiadomość nie powstała w procesie.'@)
?};
~~


\template_version
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Zwraca numer wersji wzorców
::   WY: 1 ...
::----------------------------------------------------------------------------------------------------------------------
:: 1 - wersja początkowa - możliwość nadpisania wzorców plikowych opracowanymi wzorcami własnymi do plików .m.htm
:: 2, 3 ... - obsługuje dodatkowo np. wzorce treści, czyli elementy takich jak style tabel i definicje przycisków
1

:Sign Version 2.0 jowisz:1045 2023/12/04 14:55:35 3cae740fd4e8ada32ef53a94d8ca37482f1238ab26815e89c486a286b1f23e355dfb159d6a248020949b76aca17134965d3b8b35cd508a3d57d80d409b9eef18672beeb6cb46a7af4141aff922c8180eb84cb58780a99d466765870459c2c7e2282e192b414bd1c4fe65fe3d0acc55cc34d8355f5718b9aab76c76c78c561904

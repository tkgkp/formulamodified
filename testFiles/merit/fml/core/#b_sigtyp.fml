:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #b_sigtyp.fml [17.00]
:: Utworzony: 01.10.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa słownika typów parametrów dla sygnałów (B_SIGTYP)
::======================================================================================================================


\buffer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zwraca obiekt nazwany - bufor tabeli B_SIGTYP
::   WY: obj_new()
::  TAG: <PROCES>
::----------------------------------------------------------------------------------------------------------------------
exec('B_SIGTYP','#buffer')


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Dodaje do tabeli B_SIGTYP jeden rekord
::   WE: _a - obj_new - tablica nazwana będąca buforem tabeli exec('buffer','#b_sigtyp')
::   WY: B_SIGPRT.ref() lub null()
::  TAG: <PROCES><ADD>
::----------------------------------------------------------------------------------------------------------------------
_buffer:={? var_pres('_a')>100
         || _a
         || exec('buffer','#b_sigtyp')
         ?};
_result:=null();

B_SIGTYP.cntx_psh(); B_SIGTYP.clear();
B_SIGTYP.blank();
_buffer.set();
{? B_SIGTYP.add()>0
|| _result:=B_SIGTYP.ref()
?};
B_SIGTYP.cntx_pop();
_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Kasuje podany rekord tabeli B_SIGTYP (wykonywane w transakcji!!!)
::   WE: _a - B_SIGTYP.ref()
::   WY: >0 -wyczyszczone,
::      <=0 -niewyczyszczone
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
B_SIGTYP.cntx_psh(); B_SIGTYP.clear();
{? B_SIGTYP.seek(_ref)
|| {? exec('can_delete','#b_sigtyp',_ref)>0
   || {? exec('clean','#b_sigtyp',_ref)>0
      || {? B_SIGTYP.del(,1)>0
         || _result:=1
         || undo();
            _result:=-4
         ?}
      || _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};
B_SIGTYP.cntx_pop();
{? _result<0
|| undo()
?};

{? _mydo || end() ?};
_result


\can_delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Sprawdza, czy można usunąć B_SIGTYP, czyli czy ma powiązania z B_SIGPRT
::   WE: _a - B_SIGTYP.ref()
::----------------------------------------------------------------------------------------------------------------------
_b_sigtyp:=_a;

_result:=1;
_can_continue:=1;

B_SIGPRT.cntx_psh();

:: Sprawdza przypisanie do portów zdarzenia - tabela B_SIGPRT
{? _can_continue
|| B_SIGPRT.index('TYP');
   B_SIGPRT.prefix(_b_sigtyp);
   {? B_SIGPRT.first()
   || _result:=0;
      _can_continue:=0
   ?}
?};

B_SIGPRT.cntx_pop();

_result


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Czyści powiązania do rekordu tabeli B_SIGTYP
::   WE: _a - B_SIGTYP.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---

:: --- wszystkie powiązania usunięte? ---
{? _can_continue>0
|| _result:=1
|| undo()
?};
{? _mydo || end() ?};

_result


\usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Usuń w oknie B_SIGTYP.WER
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć wskazaną pozycję?'@)
|| _result:=exec('delete','#b_sigtyp',B_SIGTYP.ref());
   {? _result<=0
   || FUN.info('Nie można usunąć pozycji, która jest powiązana w systemie.'@)
   ?}
?};
~~


\after_record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obsługa 'po rekord' w oknie B_SIGTYP.WER (walidacja)
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.record(B_SIGTYP);
{? _chk=''
|| {? ~exec('valid_acronim','#string',B_SIGTYP.PARAMETR)
   || FUN.info('Niedozwolony akronim parametru.\n\n%1'@[exec('valid_acronim_desc','#string')]);
      _chk:='PARAMETR'
   ?}
?};
_chk

:Sign Version 2.0 jowisz:1028 2019/06/07 15:58:46 879875d1318484d55fbb28a47ddbaeb3866fdbd14f41ebca942c83f8022b0f0970fe9059dce3d870fb2fcd29fd265197bfc06aa336b1ca61db032c347c56ee2ddc5c9fa71ae821b985c6d71ce75b4acfebe250021c63d1a5771b153f8efc963841d4e83497f21466dab667d3ade1db30828082c85582de58eef8581399d14498

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #b_area.fml [17.00]
:: Utworzony: 02.2014
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi mechanizmu obszarów roboczych
::======================================================================================================================


\tab_area
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Tabela tymczasowa obszarów roboczych
::   WE: _a - obj_new('TAB','WER')
::       _b - INTEGER - [1] - zwracaj wszystkie obszary do których mam uprawnienia
::                       2 -  zwracaj tylko obszary do których mam licencję i uprawnienia
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_lic_mode:=1;
{? var_pres('_b')=type_of(0)
|| _lic_mode:=_b
?};

_tab:=tab_tmp(1,
   'SYMBOL','STRING[12]','Symbol'@,
   'NAME','STRING[30]','Nazwa'@,
   'FM','STRING[35]','Formuła'@,
   'ICON','STRING[60]','Ikona'@,
   'TCALL','STRING[1]','Wywołanie'@,
   'DOMAIN','STRING[3]','Dziedzina'@,
   'UID_EXE','STRING[48]','UID_EXE'@,
   'DOKUWIKI','STRING[255]','DOKUWIKI'@,
   'AH','STRING[60]','Podpowiedz'@,
   'LIC','INTEGER','Czy jest licencja?'@
);
_tab2:=tab_tmp(1,
   'REF','INTEGER',,
   'ACT','STRING[12]',,
   'CALL','STRING[12]',
);

_obj.TAB:=_tab;

_can_continue:=1;

_tab.cntx_psh();
_app_info:=app_info('area');
_area_task:=app_info('area_task');
exec('proenv','#b_proman');
_domain:=__proenv.DOM_LIC;
_domain.cntx_psh();
_websesid:=app_info('web_sesid');
_interm:=exec('interm','#system')>0;
_mobile:=exec('mobile','#system')>0;

:: Usuwam całą zawartość dotychczasową
_tab.erase(); _tab2.erase();
_tab.clear(); _tab2.clear();

exec('czytaj','#stalesys',,XINFO,'DOKUWIKI','DOKU_INT');
{? exec('interm','#system') & XINFO.DOKU_INT<>''
|| _adr_wi:=-(form(XINFO.DOKU_INT))
|| _adr_wi:=-(form(XINFO.DOKUWIKI))
?};

_dom_tab:=~~;
{? _lic_mode=2
|| _dom_tab:=_domain
?};
{? _app_info.first()
|| {!
   |?
      _can_add:=0;
::    Sprawdzam uprawnienia do obszaru roboczego
      {? exec('authorized','#b_area',_app_info.SYMBOL,_area_task,_dom_tab,_websesid)>0
      || _can_add:=1
      ?};

      {? _can_add>0
      ||
         _tab.blank();
         _tab.SYMBOL:=_app_info.SYMBOL;
         _name:=_app_info.NAME;
         _info:=_app_info.AH;
::       Jak obszar wdrożeniowy to pobieram nazwę z wtyczki
         {? _tab.SYMBOL*'ZWS_W' & Plugin.runnable('WDR_AREA_NAME_001')
         || _namep:=Plugin.run('WDR_AREA_NAME_001',_tab.SYMBOL);
            {? _namep<>''
            || _name:=_namep;
               _info:=_namep
            ?}
         ?};
         _tab.NAME:=_name;
         _tab.FM:=_app_info.FM;
         _tab.ICON:=_app_info.ICON;
         _tab.TCALL:=_app_info.TCALL;
         _tab.AH:='OBSZAR: '@+_info;
         _tab.DOMAIN:=3+_tab.SYMBOL;
         _domain.prefix(_tab.DOMAIN,);
         {? _domain.first()
         || _tab.LIC:=1
         || _tab.LIC:=0
         ?};

::       Zmiana treści formuły spowoduje, że skróty na pulpicie odwołujące się do tworzonego elementu przestaną być
::       aktywne. Dlatego ewentualne extrasy proponuję umieszczać już w samej \wybierz/#b_area.fml.
         _formula:='exec(\'wybierz\',\'#b_area\',\''+_tab.FM+'\',\''+_tab.SYMBOL+'\')';
         _tab.UID_EXE:=exec('bdexec_check','#b_desktop',_formula,_tab.DOMAIN);

::        adres serwera dokumentacyjnego + ścieżka
         _path:=exec('get_adres','#help','dziedziny:'+(3+(-_tab.SYMBOL))+':'+(-_tab.SYMBOL),_adr_wi,0);
         _tab.DOKUWIKI:=_path
      ?};

      _add:=0;
      {? _can_add>0 & +_websesid & _app_info.TCALL='W'
      ||
         _add:=_tab.add()
      |? _can_add>0 & +_websesid=0 & _app_info.TCALL='M'
      ||
         {? _interm
         || {? _app_info.INTERM<>'T'
            || _can_add:=0
            ?}
         ?};
         {? _mobile
         || {? _app_info.MOBILE<>'T'
            || _can_add:=0
            ?}
         ?};

         {? _can_add>0
         || _add:=_tab.add()
         ?}
      ?};

      {? _can_add>0 & _add
      ||
         _domain.prefix(_tab.DOMAIN);
         _area_task.prefix(_tab.SYMBOL,);
         {? ~_domain.first & _area_task.first()
         || {!
            |? _tab2.blank();
               _tab2.REF:=#_tab.ref();
               _tab2.ACT:=_area_task.B_TASK;
               _tab2.CALL:=_area_task.B_CALL;
               _tab2.add();
::             ustalanie obszaru z 1 czynności
               {? _tab.DOMAIN=''
               ||
                  B_ACTION.index('UNIK');
                  B_ACTION.prefix(_tab2.ACT);
                  {? B_ACTION.first()
                  ||
                     _tab.DOMAIN:=B_ACTION.B_DOMAIN().SYMBOL;
                     _tab.put()
                  ?}
               ?};
               _area_task.next()
            !}
         ?}
      ?};
      _app_info.next()
   !}
?};
_domain.cntx_pop();
_tab.cntx_pop();
~~


\authorized
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Sprawdza, czy obszar roboczy może być użyty przez operatora
::   WE: _a - STRING - symbol obszaru roboczego
::       _b - uchwyt do tabeli z czynnościami obszaru (app_info('area_task'))
::       [_c] - tab_tmp - tabelka tymczasowa z dziedzinami do których mam licencję (dla optymalizacji)
::       [_d] - STRING - identyfikator sesji webterm (dla optymalizacji)
::----------------------------------------------------------------------------------------------------------------------
_b_area:=_a;
_result:=0;

_web_sesid:='';
{? var_pres('_d')=type_of('')
|| _web_sesid:=_d
|| _web_sesid:=app_info('web_sesid')
?};

{? +_web_sesid
|| OPERATOR.USER:=exec('user_ident_web','#users')
?};
_areatask:=_b;

_domain_lic:=~~;
{? var_pres('_c')>100
|| _domain_lic:=_c
?};

_areatask.prefix(_b_area,);

_can_chk:=1;
{? (sec_superuser()>0 & (_b_area='ZPR_PRO' | _b_area='ZWS_ADM' | _b_area='ZWS_USR' | _b_area='ZWS_PAR'))
||
:: Jeśli jestem superuserem to zawsze dodaje uprawnienia do obsługi procesów, administracyjnego i użytkowników
   _result:=1;
   _can_chk:=0;
   ~~
?};

{? _can_chk>0 & var_pres('_domain_lic')>100
|| _dom:=3+_b_area;
   {? _dom<>''
   || _domain_lic.prefix(_dom,);
      {? _domain_lic.first()=0
      || _can_chk:=0
      ?}
   ?}
?};

{? _can_chk>0 & _areatask.first()
|| {!
   |? {? exec('chk_role','#b__box',OPERATOR.USER,_areatask.B_TASK,,2)
      || _result:=1
      ?};
      _areatask.next() & _result=0
   !}
?};
_result


\b_area_exec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Uruchomienie obszaru roboczego
::       UWAGA: nie obsługuje przekazywania parametrów do procesu potomnego i kolorowania wg dziedziny
::   WE: _a - formuła obszaru roboczego: app_info('area').FM
::       _b - symbol obszaru roboczego
::       [_c] - link
::       [_d] - obiekt ext z dodatkowymi parametrami
::       [_e] - string z referencją projektu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('')
|| _formula:=_a
|| return()
?};
{? var_pres('_c')=type_of('') & var_pres('_e')=type_of('')
|| params_set('LINK',_c,'PROJ_REF',_e)
|? var_pres('_c')=type_of('')
|| params_set('LINK',_c)
?};
{? var_pres('_d')=117
|| params_set(exec('obj_ntab_set','#array',params_get(1),'ext',_d))
?};

_webTerm:=app_info('web_sesid')<>'';
{? _webTerm
|| {? exec('env_wt_core','#web_srv') & __PARSES.setMode('W')
   || _path:=exec('set_help','#help',_b);
      web_set_help(_path);
      mb_exec(_a);
      web_ses_close()
   ?}

|| _id:=__PARSES.clone('S','U');
   _pid:=__PARSES.getPID();
   {? _id='' | _pid=''
   || FUN.info('Przygotowanie parametrów sesji nie powiodło się.\nObszar nie może być uruchomiony.'@);
      return()
   ?};
   __PARSES.pushEnv();
   {? __PARSES.setMode('S',_id,,_pid) & (',ZWS_ADM,ZWS_PAR,ZWS_USR,FKS_HUB,'*',%1,' [_b] | __PARSES.setEnv(_b))
   || _path:=exec('set_help','#help',_b);
      set_help(_path);
      mb_exec(_a)
   || FUN.info('Uruchomienie sesji nie powiodło się.'@)
   ?};
   __PARSES.popEnv();
   __PARSES.del(_id)
?}


\wybierz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Obsługa akcji "Wybierz".
::   WE: _a - Nazwa formuły "głównej" obszaru.
::       _b - Symbol uruchamianego obszaru.
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_setargs:=params_get(1);
exec('firma','#firma',exec('firma_symbol','#firma'));
{? exec('authorized','#b_area',_b,app_info('area_task'))>0
|| {? +app_info('web_sesid')
   || exec('AreaTitle','#object')
   ?};
   AreaTitle.setArea(_b);
   {? type_of(_setargs)>0 & var_pres('LINK',_setargs)
   || {? var_pres('PROJ_REF',_setargs)=type_of('') || _proj:=_setargs.PROJ_REF || _proj:='' ?};
      exec('b_area_exec','#b_area',_a,_b,_setargs.LINK,,_proj)
   |? type_of(_args)>0 & var_pres('ext',_args)>0
   || exec('b_area_exec','#b_area',_a,_b,,_args.ext)
   || exec('b_area_exec','#b_area',_a,_b)
   ?}
|| FUN.info('Brak uprawnień do obszaru roboczego.'@)
?}


\set_title
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.02]
:: OPIS: Formuła odpowiedzialna za ustawienie tytułu obszaru roboczego. Głównie do wykorzystania @webTerm.
::   WE:  _a  [STRING] - Symbol rzeczywistego obszaru roboczego.
::       [_b] [STRING] - Symbol obszaru wirtualnego.
::   WY: Status operacji [0/1].
::----------------------------------------------------------------------------------------------------------------------
_tSTRING:=type_of('');
{? var_pres('_a')=_tSTRING
|| _ar:=_a
|| return(0)
?};
_av:={? var_pres('_b')=_tSTRING || _b || _ar ?};

exec('AreaTitle','#object');
AreaTitle.setArea(_ar,_av);
AreaTitle.setTitle();
1


\decl_areatit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Deklaracja klasy AREATIT do obsługi tytułu obszaru roboczego
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('AREATIT',@.CLASS)<0
|| obj_decl('AREATIT',
      obj_fld('area',''),
      obj_fld('areatit',''),
      obj_fld('tab',~~),
      obj_fld('win',''),
      obj_fld('tabarea',~~),
      obj_fld('sys_name',''),
      obj_fld('webterm',0),
      obj_fld('proces',''),
      obj_fld('title_override',''),
      obj_fld('tab_override',''),

      obj_meth('__init',"
         .tabarea:=app_info('area');
         .tabarea.blank();
         .tabarea.NAME:='Pulpit'@;
         .tabarea.SYMBOL:='#PULPIT';
         .tabarea.add();

         .tabarea.blank();
         .tabarea.NAME:='Uruchomienie procesu'@;
         .tabarea.SYMBOL:='#PROC';
         .tabarea.add();

         .webterm:=app_info('web_sesid')<>'';
         .sys_name:=exec('nazwa','#system');
::       Aktualizacja danych obszarów wdrożeniowych
         exec('tabarea_update_wdr','#b_area',.tabarea);
         ~~
      "),

      obj_meth('clear',"
         .title_override:='';
         AREATIT.cntx_psh();
         AREATIT.index('PAR');
         AREATIT.prefix();

         AREATPAR.cntx_psh();
         AREATPAR.index('UNIK');
         AREATPAR.prefix();

         AREADOM.cntx_psh();
         AREADOM.index('PAR');
         AREADOM.prefix();

         _can_continue:=1;

         {? _can_continue>0 & AREADOM.first()
         || {!
            |? _can_continue:=AREADOM.del(,1);
               AREADOM.first() & _can_continue>0
            !}
         ?};

         {? _can_continue>0 & AREATIT.first()
         || {!
            |? _can_continue:=AREATIT.del(,1);
               AREATIT.first() & _can_continue>0
            !}
         ?};

         {? _can_continue>0 & AREATPAR.first()
         || {? AREATPAR.first()
            || {!
               |? _next:=0;
                  _ref_nxt:=null();
                  AREATPAR.cntx_psh();
                  {? AREATPAR.next()
                  || _ref_nxt:=AREATPAR.ref()
                  ?};
                  AREATPAR.cntx_pop();

                  {? AREATPAR.count()=0
                  || _can_continue:=AREATPAR.del(,1)
                  ?};

                  {? _ref_nxt<>null()
                  || _next:=AREATPAR.seek(_ref_nxt)
                  ?};
                  _next>0 & _can_continue>0
               !}
            ?}
         ?};

         AREADOM.cntx_pop();
         AREATPAR.cntx_pop();
         AREATIT.cntx_pop();
         return(.)
      "),

      obj_meth('addParam',"
         AREATPAR.index('UNIK');
         AREATPAR.prefix();
         {? AREATPAR.find_key(_a,)
         || AREATPAR.DESC:=_b;
            AREATPAR.FML:=_c;
            AREATPAR.put()
         || AREATPAR.NAME:=_a;
            AREATPAR.DESC:=_b;
            AREATPAR.FML:=_c;
            AREATPAR.add()
         ?};
         return(.)
      ",type_of(''),type_of(''),type_of(''),-1),

      obj_meth('addTitleParams',"
         {? _>1
         || _call:=_a;
            AREATIT.index('AREA');
            AREATIT.prefix(_call,);
            {? AREATIT.first()
            || {! |? AREATIT.del() !}
            ?};
            AREATPAR.index('UNIK');
            AREATPAR.prefix();
            {! _ii:=2.._
            |! _name:=_[_ii];
               {? AREATPAR.find_key(_name,)
               || AREATIT.AREA:=_call;
                  AREATIT.AREATPAR:=AREATPAR.ref();
                  {? AREATIT.add()
                  || .addDomain4Param(_name,3+_call)
                  ?}
               ?}
            !}
         ?};
         return(.)
      ",type_of(''),type_of('')),

      obj_meth('addDomain4Param',"
         _param_name:=_a;
         _domain:=_b;
         AREADOM.cntx_psh();
         AREADOM.index('DOMAIN');
         AREATPAR.cntx_psh();
         AREATPAR.index('UNIK');
         AREATPAR.prefix(_param_name);
         {? AREATPAR.first()
         || AREADOM.prefix(_domain,AREATPAR.ref());
            {? AREADOM.first()=0
            || AREADOM.blank();
               AREADOM.AREATPAR:=AREATPAR.ref();
               AREADOM.DOMAIN:=_domain;
               AREADOM.add()
            ?}
         ?};
         AREADOM.cntx_pop();
         AREATPAR.cntx_pop();
         return(.)
      ",type_of(''),type_of('')),



      obj_meth('addDesktopParams',"
         {? _>0
         || _call:='#PULPIT';
            AREATIT.cntx_psh();
            AREATIT.index('AREA');
            AREATIT.prefix(_call,);
            {? AREATIT.first()
            || {! |? AREATIT.del() !}
            ?};
            AREATPAR.cntx_psh();
            AREATPAR.index('UNIK');

            {! _ii:=1.._
            |! _name:=_[_ii];

               {? AREATPAR.find_key(_name,)
               ||
                  AREATIT.prefix();
                  AREATIT.blank();
                  AREATIT.AREA:=_call;
                  AREATIT.AREATPAR:=AREATPAR.ref();
                  AREATIT.add()
               ?}
            !};
            AREATIT.cntx_pop();
            AREATPAR.cntx_pop()
         ?};
         return(.)
      ",type_of('')),

      obj_meth('getTitleParams',"
         params_set(params_get());
         _title:='';
         exec('proenv','#b_proman');
         _domain_lic:=__proenv.DOM_LIC;
         _domain_lic.cntx_psh();
         AREATIT.index('AREA');
         AREATIT.prefix(_a,);
         AREATPAR.prefix();
         {? AREATIT.first()
         || {!
            |? _tr:=translate(AREATIT.AREATPAR().DESC);
               _title+={? +_tr || _tr+': ' || '' ?}+($AREATPAR.FML)()+', ';
               AREATIT.next()
            !}
         ?};
         _domain_lic.cntx_pop();
         _title-2
      ",type_of(''),-1),

      obj_meth('getTitle',"
         params_set(params_get());
         _result:='';
         {? .title_override<>''
         || _result:=.title_override
         |? .proces<>''
         || _result:=.areatit+' — '+'Proces: %1'@[.proces]
         || _par:=.getTitleParams(.area);
            _result:=.areatit+{? _par<>'' || ' — '+_par || '' ?}
         ?};
         _result
      "),

      obj_meth('setTitle',"
         params_set(params_get());
         _title:=.getTitle();
         {? .webterm
         || web_title('%1, %2: %3'[.sys_name, 'Firma '@+REF.FIRMA().SYMBOL, _title],'all');
            return(~~)
         ||
::          Nie dotykać, ustalane z UX, DSYS i wszystkimi świętymi
            _title2:=spli_str(_title,' — ');
            _title_left:='';
            {? obj_len(_title2)>0
            || _title_left:='%1'[_title2[1]]
            || _title_left:=_title
            ?};
            title('','title_bar',1);
            {? _title_left<>''
            || title(_title_left,'title_left')
            ?};
            {? obj_len(_title2)>1
            || _first:=~-(1+_title2[2]);
               _title2[2]:=_first+(1-_title2[2]);
               title('%1'[_title2[2]],'title_right',1)
            ?};

            _tab:='';
            {? .tab_override<>''
            || _tab:=.tab_override
            |? .areatit<>''
            || _tab:=.areatit
            |? .title_override<>''
            || _tab:=.title_override
            ?};
            {? _tab<>''
            || title('%1'[_tab],'tab',1)
            ?};
            ~~
         ?};
         {? .title_override=''
         ||
            {? var_pres('tab',.)>0
            || _chg:=0;
               {? var_pres('win',.)>0 & .win<>.tab.win_sel('?')
               || _chg:=1;
                  _old:=.tab.win_sel('?');
                  .tab.win_sel(.win)
               ?};
               .tab.hdr_sel();
               .tab.hdr_sel(_title_left);
               {? _chg
               || .tab.win_sel(_old)
               ?};
               ~~
            |? var_pres('tab',.)=type_of(~~)
            || ~~
            |? var_pres('_a')<=0 | _a
            || FUN.emsg('Nie ustawiono tabeli w AreaTitle.'@)
            ?}
         ?};

::       Po ustawieniu tytułu kasujemy zmienne do nadpisywania title
         {? .title_override<>''
         || .title_override:=''
         ?};
         {? .tab_override<>''
         || .tab_override:=''
         ?};
         ~~
      "),

      obj_meth('setArea',"
         .setArea(_a,_a)
      ",type_of('')),

      obj_meth('setArea',"
         .area:=_a;
         _area:=_b;
         .title_override:='';
         .tab_override:='';
         .areatit:={? .tabarea.find_key(_area,) || .tabarea.NAME || 'BRAK OBSZARU '+_area ?}
      ",type_of(''),type_of('')),

      obj_meth('setTabWin',"
         {? type_of(.tab)>100 || obj_del(.tab) ?};
         .tab:=_a;
         .win:={? var_pres('_b')>0 || _b || ~~?}
      ",type_of(SYSLOG)),

      obj_meth('setTabWin',"
         {? type_of(.tab)>100 || obj_del(.tab) ?};
         .tab:=_a;
         .win:=~~
      ",type_of(~~)),

      obj_meth('setProc',"
         .proces:=_a;
         .title_override:='';
         .tab_override:='';
         .setTitle();
         ~~
      ",type_of(''))
   )
?}


\tabarea_update_wdr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Formuła aktualizuje dane (nazwy) wdrożeniowych obszarów roboczych dla podanej tabeli tymczasowej.
::   WE:  _a  [STRING] - tabela, która jest aktualizowana - wynik wywołania app_info('area')
::----------------------------------------------------------------------------------------------------------------------
_tabarea:=_a;
{? var_press('_tabarea')>100 & Plugin.runnable('WDR_AREA_NAME_001')
|| _tabarea.cntx_psh();
   _tabarea.prefix('ZWS_W');
   {? _tabarea.first()
   ||
      {!
      |?
         _namep:=Plugin.run('WDR_AREA_NAME_001',_tabarea.SYMBOL);
         {? _namep<>''
         || _tabarea.NAME:=_namep;
            _tabarea.put()
         ?};
         _tabarea.next()
      !}
   ?};
   _tabarea.cntx_pop()
?};
~~

:Sign Version 2.0 jowisz:1045 2024/02/06 08:03:57 61ca0abfff79f72361599201f677ccd7740db4ec2d24fda5bed1f96837204be658b416c5eaba96ea4631f6d60274652a1f609adfb74b5d08e87c489c9717e270d1d1115cb22d4cbaab8bf253021b305ec01de18267144dcf0b36139db03b7909901eea613d1862edf31e6ac1fb923eb6eda585bf9f6f9be61dc20f80ea408ccc

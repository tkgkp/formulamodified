:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #json.fml
:: Utworzony: 14/01/2022
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Funkcje związane z obsługą JSON-a
::======================================================================================================================


\file2tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Konwertuje napis JSON-a zawarty w pliku na tabelę w formacie przekazanym w parametrze
::   WE:  _a  [DICT]   - tabela, do której będziemy dodawać rekordy
::        _b  [STRING] - nazwa pliku
::       [_c] [NUMBER] - czy szukać pliku zgodnie z pth? (0 / 1-domyślnie)
::   WY: -1/0/n
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
   _TAB:={? var_pres('_a')=type_of(SYSLOG) || _a || return(_result) ?};
 _fName:={? var_pres('_b')=type_of('')     || _b || return(_result) ?};
   _pth:={? var_pres('_c')=type_of(0)      || _c || 1               ?};
_result:=0;

_file:=fopen(_fName,'r',_pth,,1);
{? _file.is_open()
|| _obj:=json_parse(_file);
   _result:=exec('obj2tab','#json',_TAB,_obj);
   fclose(&_file)
?};
_result


\string2tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Konwertuje stringową reprezentacje JSON-a na tabelę w formacie przekazanym w parametrze
::   WE:  _a  [DICT]   - tabela, do której będziemy dodawać rekordy
::        _b  [STRING] - napis będący stringową reprezentacją JSON-a
::   WY: -1/0/n
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
   _TAB:={? var_pres('_a')=type_of(SYSLOG) || _a || return(_result) ?};
_string:={? var_pres('_b')=type_of('')     || _b || return(_result) ?};

_obj:=json_parse(_string);
exec('obj2tab','#json',_TAB,_obj)


\obj2tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Konwertuje obiekt, który reprezentuje tablicę JSON-a na tabelę w formacie przekazanym w parametrze
::   WE:  _a  [DICT]   - tabela, do której będziemy dodawać rekordy
::        _b  [STRING] - napis będący stringową reprezentacją JSON-a
::   WY: -1/0/n
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_TAB:={? var_pres('_a')=type_of(SYSLOG) || _a || return(_result) ?};
_obj:={? var_pres('_b')=117             || _b || return(_result) ?};
_result:=0;

{! _ind:=1..obj_len(_obj)
|! {? var_pres('_elem')>0 || obj_del(_elem) ?};
   _elem:=_obj[_ind];
   _found:=0;
   {? obj_ntab(_elem)
   || {! _fld:=1.._TAB.fld_num()
      |! _fld_acr:=_TAB.fld_acr(_fld);
         {? var_pres(_fld_acr,_elem)>0
         || ($('_a.%1:=_b.%1'[_fld_acr]))(_TAB,_elem);
            _found+=1
         ?}
      !}
   ?};
   {? _found || _result+=_TAB.add() ?}
!};
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:09 6e2c22c0a9b23b8a76d4ed4576854b595648bc3ddb871ac5498d22f2b0143b1feac9342218ffa6d502c182740bea449cc08876203f36616743b726ce7611eccb7cf4e6ec68659588bdabcc169b70ba804eb912ba37be64fd2ec8bf7c4453e74e0c353a94f05bfc58ac71cff75983c0b2330081b269eacaaedb9d930c6373a246

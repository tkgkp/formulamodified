:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #window.fml
:: Utworzony: 17.12.2014
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Uniwersalne mechanizmy obsługi okien (budowa okien tymczasowych, uniwersalne akcje dla okien )
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Deklaracja klasy wspomagającej zapamiętywanie akronimów okienek tymczasowych tabel.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('WINDOW',@.CLASS)>0 || return ?};
exec('decl_buf','#window');

obj_decl('WINDOW',
   obj_fld('SEL',~~),
   obj_fld('EDIT',~~),
   obj_fld('DICT',~~),
   obj_fld('PATT',~~),

:: konstruktor
   obj_meth('__init',"
      _buf:=tab_tmp(,
         'CLS','STRING[8]','Klasa',
         'TAB','STRING[8]','Tabela',
         'WND','STRING[8]','Akronim',
         'TMP','STRING[8]','Okienko'
      );
      _buf.index(_buf.ndx_tmp(,,'CLS',,,'TAB',,,'WND',,,'WND',,));

      _a.SEL:=obj_new(@.CLASS.WND_BUFF,'win_sel',_buf);
      _a.EDIT:=obj_new(@.CLASS.WND_BUFF,'win_edit',_buf);
      _a.DICT:=obj_new(@.CLASS.WND_BUFF,'win_dict',_buf);
      _a.PATT:=obj_new(@.CLASS.WND_BUFF,'win_patt',_buf)
   "),

   obj_meth('id',"
      -(2-!_a+'_'+_b)
   ",type_of(SYSLOG),type_of('')),

   obj_meth('select',"
      .SEL.set(_a,_b);
      _a.select(var_pres('_c')=type_of(0) & _c,var_pres('_d')=type_of(0) & _d)
   ",type_of(SYSLOG),type_of('')),

   obj_meth('edit',"
      .EDIT.set(_a,_b);
      _a.edit(
         {? var_pres('_c')=type_of($('')) || _c
         |? var_pres('_c')=type_of('') || $(_c)
         || $'1'
         ?}
      )
   ",type_of(SYSLOG),type_of(''))
)


\decl_buf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Deklaracja klasy wspomagającej zapamiętywanie akronimow okienek tymczasowych tabel.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('WND_BUFF',@.CLASS)>0 || return ?};

obj_decl('WND_BUFF',
   obj_fld('CLS',~~),
:: bufor pomocniczy
   obj_fld('BUF',~~),

:: konstruktor
   obj_meth('__init',"
      _a.CLS:=_b;
      _a.BUF:=_c;
      set_virt(_a,'set',$('_a.'+_b+'(.get(_a,_b))'),type_of(SYSLOG),type_of(''))
   ",type_of(''),type_of(SYSLOG)),

:: Zwraca akronim tabeli.
:: _a - alias do tabeli
   obj_meth('tab_acr',"_id:=!_a; {? +_id || 2-!_a || _a.name ?}",type_of(SYSLOG)),

:: Zwraca akronim okienka tymczasowego tabeli.
:: _a - alias do tabeli
:: _b - identyfikator okna
   obj_meth('get',"
      .BUF.clear;
      {? .BUF.find_key(.CLS,.tab_acr(_a),_b,_b)
      || .BUF.TMP
      || ''
     ?}
   ",type_of(SYSLOG),type_of('')),

:: Zwraca identyfikator okienka tymczasowego tabeli.
:: _a - alias do tabeli
:: _b - akronim okna
   obj_meth('get_id',"
      .BUF.blank;
      .BUF.CLS:=.CLS;
      .BUF.TAB:=.tab_acr(_a);
      .BUF.TMP:=_b;
      .BUF.clear;
      {? .BUF.find_rec
      || .BUF.WND
      || ''
      ?}
   ",type_of(SYSLOG),type_of('')),

:: Zapamiętuje lub usuwa informacje o oknie tymczasowym, zwracając poprzednio zapamiętaną wartość.
:: _a - alias do tabeli
:: _b - identyfikator okna
:: _c - akronim okna tymczasowego (opcjonalny)
:: Jeśli argument _c nie będzie niepustym napisem, to jeęli istnieje zapis dla tabeli _a i okna _b,
:: to zostanie on usunięty.
   obj_meth('put',"
      _wnd:=.get(_a,_b);
      {? _wnd<>''
      || {? var_pres('_c')=type_of('') & _c<>''
         || .BUF.TMP:=_c;
            .BUF.put
         || .BUF.del
         ?}
      |? var_pres('_c')=type_of('') & _c<>''
      || .BUF.TAB:=.tab_acr(_a);
         .BUF.CLS:=.CLS;
         .BUF.WND:=_b;
         .BUF.TMP:=_c;
         .BUF.add
      ?};
      _wnd
   ",type_of(SYSLOG),type_of('')),

:: Ustawia okienko wertowania tabeli.
:: _a - alias do tabeli
:: _b - identyfikator okna
   obj_virt('set',"",type_of(SYSLOG),type_of(''))
)


\sel_exit_
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: sel_exit
::  OLD: \sel_exit_/war_tech.fml
::  OLD: \wyb_par_p/skl_okr.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| sel_exit()
|| web_top_tab().web_close();
:: jeżeli jest zdefiniowana formuła na zamknięcie okna w przekazanych parametrach to uruchamiamy ją
   _par:=web_params_get();
   {? obj_ntab(_par) & var_pres('ESC_FORM',_par)=type_of("")
   || _par.ESC_FORM()
   ?};
:: koniecznie trzeba zwrócić 0 dla wywołania sel_exita jako akcji grupowej w webTermie
:: (dla zwykłej akcji działa to również dobrze)
   0
?}


\win_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: win_disp
::  OLD: \win_disp/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
win_disp()


\help_act_zakoncz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Tekst będący podpowiedzią czynnościowego przycisku Zakończ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'"%1"'['Zakończenie wykonywania bieżącej czynności'@]


\help_act_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Tekst będący podpowiedzią czynnościowego przycisku OK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'"%1"'['Zapisanie aktualnego stanu danych i odłożenie czynności na listę zadań do późniejszego wykonania'@]


\help_act_esc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Tekt będący podpowiedzią czynnościowego przycisku Anuluj.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'"%1"'['Rezygnacja z wykonywania czynności w tej chwili'@]


\help_red_zakoncz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Tekst będący podpowiedzią czynnościowego przycisku Zakończ.
::   WE: _a[STRING] - Z-zamówienie/D-dokument/P-potwierdzenie/O-oferta/A-paczka/R-raport/DBA-dokument bez akceptacji/
::                  - L-polecenie/E-reklamacje/Y-dyspozycja/I-realizacja/U-umowa/G-zgłoszenie/DK-dokument księgowy
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_pres('_a')=type_of('') || _a || '' ?};
_res:=
   {? _a='Z'   ||  'Zakończ zamówienie, aby było gotowe do akceptacji'@
   |? _a='D'   ||  'Zakończ dokument, aby był gotowy do akceptacji'@
   |? _a='DK'  ||  'Zakończ dokument, aby był gotowy do księgowania'@
   |? _a='DBA' ||  'Zakończ dokument'@
   |? _a='P'   ||  'Zakończ potwierdzenie, aby było gotowe do akceptacji'@
   |? _a='O'   ||  'Zakończ ofertę, aby była gotowa do akceptacji'@
   |? _a='A'   ||  'Zakończ paczkę, aby była gotowa do akceptacji'@
   |? _a='R'   ||  'Zakończ raport'@
   |? _a='L'   ||  'Zakończ polecenie, aby było gotowe do akceptacji'@
   |? _a='E'   ||  'Zakończ reklamację, aby była gotowa do akceptacji'@
   |? _a='Y'   ||  'Zakończ dyspozycję, aby była gotowa do akceptacji'@
   |? _a='I'   ||  'Zakończ realizację'@
   |? _a='U'   ||  'Zakończ umowę, aby była gotowa do akceptacji'@
   |? _a='G'   ||  'Zakończ zgłoszenie, aby było gotowe do akceptacji'@
   |? _a='C'   ||  'Zakończ rejestrację nagłówka zlecenia, aby było gotowe do dalszej obsługi'@
   |? _a='V'   ||  'Zakończ deklarację, aby była gotowa do akceptacji'@
   |? _a='H'   ||  'Zakończ przelew, aby był gotowy do akceptacji'@
   |? _a='S'   ||  'Zakończ sprawozdanie, aby było gotowy do akceptacji'@
   |? _a='N'   ||  'Zakończ rejestrację noty'@
   |? _a='INW' ||  'Zakończ inwentaryzację'@
   |? _a='PRC' ||  'Zakończ przecenę'@
   |? _a='PKD_A' || 'Zaakceptuj, aby przejść dalej'@
   |? _a='PKD_B' || 'Zatrudnij'@
   |? _a='PKD_C' || 'Odblokuj miesiąc rozliczeniowy'@
   |? _a='PKD_D' || 'Zablokuj miesiąc rozliczeniowy'@
   |? _a='PKD_E' || 'Otwórz okres rozliczeniowy'@
   |? _a='PKD_F' || 'Zamknij okres rozliczeniowy'@
   |? _a='PKD_G' || 'Otwórz miesiąc rozliczeniowy'@
   |? _a='PKD_H' || 'Zamknij miesiąc rozliczeniowy'@
   |? _a='PKD_I' || 'Odblokuj okres rozliczeniowy'@
   |? _a='PKD_J' || 'Zablokuj okres rozliczeniowy'@
   |? _a='PKD_K' || 'Przypisz współpracownika do okresu rozliczeniowego'@
   |? _a='PKD_L' || 'Zakończ import przeszeregowań'@
   |? _a='HBN_P' || 'Zakończ eksport przelewów z listy płac'@
   |? _a='J_SIG' || 'Zakończ podpisywanie i zamknij okno'@
   |? _a='LOG_OBG' || 'Utwórz dokument w obiegu na podstawie dokumentu logistycznego'@
   || ''
   ?};
'"%1"'[_res]


\help_red_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Tekst będący podpowiedzią redakcyjnego przycisku OK.
::   WE: _a[STRING] - Z- zapisz, P-potwierdź
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_pres('_a')=type_of('') || _a || '' ?};
_res:=
   {? _a='Z' ||  'Zapisz dane z możliwością powrotu do edycji'@
   |? _a='P' ||  'Potwierdź wybór i zamknij okno'@
   || 'Zatwierdzenie i zapisanie wprowadzonych danych'@
   ?};
'"%1"'[_res]


\help_red_esc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Tekt będący podpowiedzią redakcyjnego przycisku Anuluj.
::   WE: _a[STRING] - A- Anuluj, P-zamknij okno pozycji dokumentu, K-zamknij okno poz. korespondencji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_pres('_a')=type_of('') || _a || '' ?};
_res:=
   {? _a='A' ||  'Anuluj zmiany i zamknij okno'@
   |? _a='P' ||  'Wyjdź z pozycji dokumentu i zamknij okno'@
   |? _a='K' ||  'Wyjdź z pozycji korespondencji i zamknij okno'@
   |? _a='H' ||  'Wyjdź z pozycji harmonogramu i zamknij okno'@
   |? _a='Z' ||  'Zamknij okno powiadomień'@
   |? _a='S' ||  'Zamknij okno'@
   || 'Rezygnacja z zapisania wprowadzonych danych'@
   ?};
'"%1"'[_res]


\zakoncz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła dodaje przycisk Zakończ do okna redagowania. Dla _c=0 przycisk jest wyszarzany.
::   WE:  _a  [TABLE]  - Uchwyt tabeli.
::        _b  [STRING] - Akronim okienka.
::       [_c] [NUMBER] - Typ podpowiedzi:
::                      0 - "Zwykłe" [domyślnie];
::                      1 - "Czynnościowe".
::       [_d] [STRING/RULE] - Formuła wykonywana po wybraniu przycisku Zakończ [domyślnie: 'key:F2'].
::       [_e] [NUMBER] - Przycisk dostępny podczas wyświetlania okna:
::                         0 - Nie [domyslnie];
::                         1 - Tak,
::       [_f] [NUMBER] - Podpowiedź
::       [_g] [STRING] - Tekst przycisku
::   WY: Uchwyt dodanego przycisku.
::----------------------------------------------------------------------------------------------------------------------
_TAB:=_a;
_we:=_b;
_act:=var_pres('_c')=type_of(0) & _c;
_display:=var_pres('_e')=type_of(0) & _e;
_text:={? var_pres('_g')=type_of('') & _g<>'' || _g || '&Zakończ'@ ?};

_cfg:=',btn_label_align=center,panel=bottom,align=end,display=%1' [$_display];

_vpd:=var_pres('_d');
_rule:={? _vpd=type_of('') | _vpd=type_of("") || _d || 'key:F2' ?};
_bok:=_TAB.win_ebtn(_we,'text=%1' [_text]+_cfg,_rule);
_help:=
   {? var_pres('_f')=type_of('')
   || _f
   ||
      {? _act
      || exec('help_act_zakoncz','#window')
      || exec('help_red_zakoncz','#window')
      ?}
   ?};
_ret:=
   {? _act
   || _TAB.btn_opt(_bok,'state=normal,tooltip=%1'[_help])
   || _TAB.btn_opt(_bok,'state=grayed,tooltip=%1'[_help])
   ?};

_ret


\ok_esc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Dodaje klawisze OK/Anuluj do okienka redagowania tabeli.
::   WE:  _a  [TABLE]  - Uchwyt tabeli.
::        _b  [STRING] - Akronim okienka.
::       [_c] [NUMBER] - Typ podpowiedzi:
::                      0 - "Zwykłe" [domyślnie];
::                      1 - "Czynnościowe".
::       [_d] [STRING/RULE] - Formuła wykonywana po wybraniu przycisku OK [domyślnie: 'key:F2'].
::       [_e] [STRING/RULE] - Formuła wykonywana po wybraniu przycisku Anuluj [domyślnie: 'key:Esc'].
::       [_f] [NUMBER] - 0 - bez OK, 1 - z ok
::       [_g] [NUMBER/STRING] - Przyciski dostępne podczas wyświetlania okna:
::                         0 - Nie [domyslnie];
::                         1 - Tak,
::                         'OK,ANULUJ' - widoczne przyciski
::       [_h] [STRING] - podpowiedź dla OK
::       [_i] [STRING] - tekst przycisku dla ok
::       [_j] [STRING] - podpowiedź dla Anuluj
::       [_k] [STRING] - tekst przycisku dla Anuluj
::   WY: Tablica z elementami nazwanymi:
::       OK  - Uchwyt dodanego przycisku OK;
::       ESC - Uchwyt dodanego przycisku Anuluj;
::----------------------------------------------------------------------------------------------------------------------
_TAB:=_a;
_we:=_b;
_act:=var_pres('_c')=type_of(0) & _c;
_czy_ok:={? var_pres('_f')=type_of(0) || _f || 1 ?};
_disp_ok:=
   {? var_pres('_g')=type_of(0) || _g
   |? var_pres('_g')=type_of('') || (','+-_g+',')*',ok,'
   ?};
_disp_cancel:=
   {? var_pres('_g')=type_of(0) || _g
   |? var_pres('_g')=type_of('') || (','+-_g+',')*',anuluj,'
   ?};
_text_ok:={? var_pres('_i')=type_of('') & _i<>''
          || _i
          || '&OK'@
          ?};
_text_an:={? var_pres('_k')=type_of('') & _k<>''
          || _k
          || '&Anuluj'@
          ?};
_help_ok:={? var_pres('_h')=type_of('')
          || _h
          || {? _act
             || exec('help_act_ok','#window')
             || exec('help_red_ok','#window')
             ?}
          ?};
_help_an:={? var_pres('_j')=type_of('')
          || _j
          || {? _act
             || exec('help_act_esc','#window')
             || exec('help_red_esc','#window')
             ?}
          ?};

_ret:=obj_new('OK','ESC');

_tstring:=type_of('');
_trule:=type_of("");

_cfg:=',btn_label_align=center,panel=bottom,align=end';
_cfg_ok:='%1,display=%2'[_cfg,$_disp_ok];
_cfg_cancel:='%1,display=%2'[_cfg,$_disp_cancel];

{? _czy_ok
|| _vpd:=var_pres('_d');
   _rule:={? _vpd=_tstring | _vpd=_trule || _d || 'key:F2' ?};
   _bok:=_TAB.win_ebtn(_we,'text=%1' [_text_ok]+_cfg_ok,_rule);
   _TAB.btn_eopt(_we,_bok,'tooltip='+_help_ok);
   _ret.OK:=_bok
?};

_vpe:=var_pres('_e');
_rule:={? _vpe=_tstring | _vpe=_trule || _e || 'key:Esc' ?};
_besc:=_TAB.win_ebtn(_we,'text=%1' [_text_an]+_cfg_cancel,_rule);
_TAB.btn_eopt(_we,_besc,'tooltip='+_help_an);

_ret.ESC:=_besc;

_ret


\actions_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Tablica nazwana do sterowania akcjami
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
obj_new('WIN_ACR','HID_ACTIONS','DEFAULT','DISP_WIN')


\setup_buttons
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ustala właściwości przycisków "Zakończ", "OK", "Anuluj" zależnie od kontekstu wywołania.
::       Zakłada się, że przyciski są identyfikowane odpowiednio przez "DONE", "OK", "ANULUJ"
::   WE: _a - alias zmiennej / tabeli
::       _b - akronim okienka redakcji
::       _c - tryb działania: 1 - mikroczynność, 0 - proces
::       _d - identyfikator przycisku "Zakończ"
::       _e - identyfikator przycisku "Ok"
::       _f - identyfikator przycisku "Anuluj"
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_a.btn_eopt(_b,_d,'tooltip=%1,state=%2'[exec('help_act_zakoncz','#window'),{? _c || 'grayed' || 'normal' ?}]);
_a.btn_eopt(_b,_e,'tooltip=%1'[exec({? _c || 'help_red_ok' || 'help_act_ok' ?},'#window')]);
_a.btn_eopt(_b,_f,'tooltip=%1'[exec({? _c || 'help_act_esc' || 'help_act_esc' ?},'#window')]);
~~


\efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Zwraca string z formułą do ustawiania opcji dla pól
::   WE: [_a] [INTEGER] - Czy webTerm
::        _b  [STRING]  - Akronim okna, w którym mają być ustawione właściwości pól.
::       [_c] [STRING]  - Identyfikator okna w grupie (parametr istotny dla webTerma).
::   WY: '' lub napis z formułą
::----------------------------------------------------------------------------------------------------------------------
  _webTerm:={? var_pres('_a')=type_of(0)  || _a || app_info('web_sesid')<>'' ?};
       _we:={? var_pres('_b')=type_of('') || _b || return('')                ?};
_grp_ident:={? var_pres('_c')=type_of('') || _c || ''                        ?};

{? _webTerm
|| $( '_a.'+
      {? {? _grp_ident='' || web_top_win()=_we || _grp_ident=web_top_ident() ?}
      || 'web_efld_opt'
      || 'web_efld_init'
      ?}+
      '('
         '_b,'
         '_c,'
         '_d,'
         '{? var_pres(\'_e\')>0 || _e || _a ?},'
         '{? var_pres(\'_f\')>0 || _f || \'\' ?},'
         '{? var_pres(\'_g\')>0 || _g || \'\' ?}'
      ')'
   )
|| $( '_a.efld_opt'
      '('
         '_b,_d,_e,'
         '{? var_pres(\'_f\')>0 || _f || _a ?},'
         '{? var_pres(\'_g\')>0 || _g || \'\' ?}'
      ')'
   )
?}


\exists
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy istnieje okno redagowania/wertowania
::   WE:  _a  - tabela
::        _b  - akronim
::       [_c] - typ okna: ['R']-redagowania 'W'-wertowania
::  OLD: \is_red/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_win:=_b;
_typ:={? var_pres('_c')<=0 || 'R' || _c ?};
_tab.cntx_psh();
no_msg(1);
_jest:={? _typ='R' || _tab.win_edit(_win)=_win || _tab.win_sel(_win)=_win ?};
no_msg(0);
_tab.cntx_pop();
_jest


\text_red_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Tekst przycisku OK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zapi&sz'@


\text_red_szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Tekst przycisku OK w trybie wyszukiwania. Do wykorzystania w specyficznych przypadkach, gdy do wyszukiwania nie
::       jest wykorzystywane okno tabeli a np. okno zmiennej strukturalnej.
::   WE:
::   WY: tekst etykiety przycisku
::----------------------------------------------------------------------------------------------------------------------
'Szukaj'@


\text_red_zakoncz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Tekst przycisku Zakończ.
::   WE: _a[STRING] - Z-zamówienie/D-dokument/P-potwierdzenie/O-oferta/A-paczka/R-raport/L-polecenie/E-reklamacje/
::                    Y-dyspozycje/I-realizacja/U-umowa/G-zgłoszenie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_pres('_a')=type_of('') || _a || '' ?};
_res:=
   {? _a='Z' ||  '&Zakończ zamówienie'@
   |? _a='D' ||  '&Zakończ dokument'@
   |? _a='P' ||  '&Zakończ potwierdzenie'@
   |? _a='O' ||  '&Zakończ ofertę'@
   |? _a='A' ||  '&Zakończ paczkę'@
   |? _a='R' ||  '&Zakończ raport'@
   |? _a='L' ||  '&Zakończ polecenie'@
   |? _a='E' ||  '&Zakończ reklamację'@
   |? _a='Y' ||  '&Zakończ dyspozycję'@
   |? _a='I' ||  '&Zakończ realizację'@
   |? _a='U' ||  '&Zakończ umowę'@
   |? _a='G' ||  '&Zakończ zgłoszenie'@
   |? _a='C' ||  '&Zakończ rejestrację'@
   |? _a='V' ||  '&Zakończ deklarację'@
   |? _a='S' ||  '&Zakończ sprawozdanie'@
   |? _a='INN'   || '&Zakończ'@
   |? _a='PKD_A' || 'A&kceptuj'@
   |? _a='PKD_B' || '&Zatrudnij'@
   |? _a='PKD_C' || '&Odblokuj miesiąc'@
   |? _a='PKD_D' || '&Zablokuj miesiąc'@
   |? _a='PKD_E' || '&Otwórz okres'@
   |? _a='PKD_F' || '&Zamknij okres'@
   |? _a='PKD_G' || '&Otwórz miesiąc'@
   |? _a='PKD_H' || '&Zamknij miesiąc'@
   |? _a='PKD_I' || '&Odblokuj okres'@
   |? _a='PKD_J' || '&Zablokuj okres'@
   |? _a='PKD_K' || '&Przypisz do okresu'@
   |? _a='PKD_L' || '&Zakończ'@
   |? _a='PKD_M' || '&Generuj raport'@
   || '&Zakończ'@
   ?};
'"%1"'[_res]

:Sign Version 2.0 jowisz:1045 2023/08/17 16:30:23 b18e7f61cf65a7cf5da592c01e93da6584e507c1c0d0e175febae8a863d6296273eea382376aef9a3e6314a6998081fb221eb350ec60185480e0e5835c31b7f023cc436b878f7540d7caeb5a013162666bf3149f852740c7c0962ef615f78d0a5d385719c3ed9f82ff754a3029f847643a4be88ec3e8563d5c22d649baaedfee

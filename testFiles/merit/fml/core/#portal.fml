:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #portal.fml
:: Utworzony: 14.04.2020
:: Autor: TS
::======================================================================================================================
:: Zawartość: Część 'core' obsługi HR portalu
::======================================================================================================================


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Wczytuje identyfikatory funkcjonalności w portalu. Identyfikatory są określane
::       w komentarzu formuły głównej czynności w linii ::# portal=
::       Kontekst pracy - bufor B_ACTION któremu nawinąć pola B_ACTION.PORTAL i B_ACTION.PORT_OPR
::   WE: _a - nazwa formuły głównej
::       _b - plik w którym znajduje się formuła główna
::       [_c] - INTEGER - 0/[1] - czy dodawać komunikaty do KOMM
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_fun_g:=_a;
_plik:=_b;

_komm:=1;
{? var_pres('_c')=type_of(0)
|| _komm:=_c
?};

_portal_opr:='';
{? exec('is_fun','#file',_plik,_fun_g)>0
||
   _fun_str:=exec('string_fun','#file',_plik,_fun_g,'::# portal=',,'^','::#');
   _split:=spli_str(_fun_str,'\n');

   {? obj_len(_split)>0
   ||
      {! _it:=1..obj_len(_split)
      |!
         _wiersz:=_split[_it];

         {? _wiersz<>'\n' & _wiersz<>''
         || {? _portal_opr=''
            || _portal_opr:=gsub(gsub(gsub(_wiersz,',',',\n'),';',',\n'),' ','')
            ||
               {? _komm>0
               || _msg:='W formule czynności: %1 znajdują się dwa wpisy dotyczące portalu. '
                        'Został użyty pierwszy z nich.'@[_fun_g+'\\'+(-_plik)+'.fml'];
                  _sect:='Czynność: %1 — %2'@[B_ACTION.UID,B_ACTION.NAME];
                  {? KOMM.find_msg(_sect) || KOMM.set_root(_sect) || KOMM.sect_beg(_sect) ?};
                  KOMM.add(_msg,'xwin16.png:15');
                  KOMM.sect_end()
               ?}
            ?}
         ?}
      !}
   ?}
?};
B_ACTION.memo_set(_portal_opr,'PORT_OPR');
B_ACTION.PORTAL:={? _portal_opr='' || 'N' || 'T' ?};
~~


\portal_fun_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Wyświetla listę funkcjonalności portalu dla czynności 'portalowej'
::----------------------------------------------------------------------------------------------------------------------
_tab:=MOD_ENV.TAB_ACT;
B_ACTION.cntx_psh();
B_ACTION.clear();
{? B_ACTION.seek(_tab.B_ACT)
|| exec('edit_memo','#edit',B_ACTION.memo_txt(,1,'PORT_OPR'),'Funkcjonalności portalu'@,,50,25,,,0)
?};
B_ACTION.cntx_pop();
''


\portalu_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Przetwarza zapisy w tabeli PORTALU (uprawnienia w portalu) na podstawie uprawnień użytkowników do ról
::       w bieżącej firmie albo we wszystkich firmach - w zależności od ustawienia XINFO.POR_CONF:
::       - dla wartości W (wielofirmowa) pobierane są uprawnienia ze wszystkich firm
::   WE: [_a] - [0]/1 czy wyświetlać dialogi i progress
::       [_b] - USERS.ref() - jeśli wystąpi, to wyznacza uprawnienia dla tego użytkownika,
::              nie aktualizuje wcześniej uprawnień z serwera
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0)
|| _dialog:=_a
|| _dialog:=0
?};

{? var_pres('_b')=type_of(null()) & ref_tab(_b)=USERS
|| _user:=_b
|| _user:=null()
?};

_can_continue:=1;

{? _dialog
|| {? _user=null()
   || _can_continue:=FUN.ask('Czy aktualizować uprawnienia użytkowników do funkcjonalności portalu?'@)
   || _can_continue:=FUN.ask('Czy aktualizować uprawnienia użytkownika do funkcjonalności portalu?'@)
   ?}
?};

{? _can_continue=0 || return() ?};

:: Pobierane wszytkie rekordy, nawet gdy formuła NIE dotyczy jednego użytkownika
{? _user=null()
|| KOMM.init(,,'Aktualizacja uprawnień'@);
   exec('process_receive','sync_mwa','PORTAL_CFG',-1,,'PORTALU',1,0,'sl_TenantUserRightsGet')
?};

_isWebTermInHrPortal:=exec('isWebTermInHRPortalEnabled','portal_engine');
_lic_prc:=exec('lic','#b_domain','PRC');
_lic_seo:=exec('lic','#b_domain','SEO');
_lic_por:=exec('lic','#b_domain','POR');
exec('czytaj','#stalesys',,XINFO);
{? XINFO.POR_CONF='J'
|| _firma:=REF.FIRMA
|| _firma:=null()
?};

USERS.cntx_psh();
USERSF.cntx_psh();
PORTALU.cntx_psh();
B_ACTION.cntx_psh();

:: Wyznaczenie schematu uprawnień dla pojedynczego użytkownika (na podstawie dostępnych czynności "portalowych")
:: Zapisanie powiązania uprawnienia z czynnością
_tab:=tab_tmp(1,'OPR_NAME','STRING[100]','','ENABLED','STRING[1]','');
_act:=tab_tmp(2,'UPR','STRING[100]','','AKC','INTEGER','');
PORTALU.index('OPR');
PORTALU.prefix('W');
{? PORTALU.first()
|| {!
   |? _opr_name:=PORTALU.OPR_NAME;
      _tab.prefix(_opr_name,);
      {? ~_tab.first()
      || _tab.OPR_NAME:=_opr_name;
         _tab.ENABLED:='N';
         _tab.add()
      ?};
      PORTALU.next()
   !}
?};
B_ACTION.index('PORTAL');
B_ACTION.prefix('T');
{? B_ACTION.first()
|| {!
   |? _port_opr:=B_ACTION.memo_txt(,1,'PORT_OPR');
      _port_opr:=spli_str(_port_opr,',');
      {! _it:=1..obj_len(_port_opr)
      |! _opr_name:=form(_port_opr[_it]);
         {? _opr_name<>''
         || _tab.prefix(_opr_name,);
            {? ~_tab.first()
            || _tab.OPR_NAME:=form(_opr_name);
               _tab.ENABLED:='N';
               _tab.add()
            ?};
            {? ~_act.find_key(_opr_name,#B_ACTION.ref())
            || _act.blank(1);
               _act.UPR:=_opr_name;
               _act.AKC:=#B_ACTION.ref();
               _act.add()
            ?}
         ?}
      !};
      obj_del(_port_opr);
      B_ACTION.next()
   !}
?};
_tab.prefix();
::exec('select','#table',_tab);

:: Deaktywacja zapisów, które nie dotyczą aktywnych użytkowników portalowych
:: Nie uruchamiane gdy formuła dotyczy pojedynczego użytkownika
{? _user=null()
|| PORTALU.index('LOGIN');
   PORTALU.prefix('U');
   {? PORTALU.first()
   || {!
      |? USERS.index('WEBLOGIN');
         USERS.prefix(PORTALU.LOGIN,);
::       Nie ma użytkownika o takim loginie
         {? ~USERS.first()
         || PORTALU.ENABLED:='N';
            PORTALU.put()
         || USERSF.index('UNIK');
            USERSF.prefix(_firma,USERS.ref());
::          Jest użytkownik, ale nie jest aktywny
            {? USERSF.first() & USERSF.PORTAL2<>'T'
            ||
::            USERS.PMPRAC:=0;
::            USERS.PMKIER:=0;
::            USERS.PMSEOD:=0;
::            USERS.put();
               PORTALU.ENABLED:='N';
               PORTALU.put()
            ?}
         ?};
         PORTALU.next()
      !}
   ?}
?};

:: Pulpit można mieć tylko jeden
_pulpit_prac:='STD_PC_E_HomePage_DSH';
_pulpit_kier:='STD_PC_M_HomePage_DSH';
:: Dostęp do akceptacji wniosków z widocznym wynagrodzeniem ma wyższy priorytet od dostępu bez wynagrodzenia
:: w przypadku "trzymania" przez użytkownika zarówno roli akceptujący wnioski z wynagordzeniami jak i bez
_akcept_b:='STD_PC_M_CurrentCoopTermsBasic_NewPersonRequest_PRC';
_akcept_w:='STD_PC_M_CurrentCoopTerms_NewPersonRequest_PRC';

:: Pętla po aktywnych użytkownikach portalowych w bieżącej firmie, albo po jednym użytkowniku gdy podany
USERS.prefix();
USERSF.index('PORTAL');
{? _user=null()
|| USERSF.prefix(_firma,'T')
|| USERSF.prefix(_firma,'T',exec('FindAndGet','#table',USERS,_user,,"USERS.KOD",''))
?};
_size:=USERSF.size();
_tabSize:=_tab.size();
{? USERSF.first()
||
   PORTALU.index('LOGIN');

   {? _dialog || FUN.prg_start(_size*_tabSize,'Aktualizacja'@,,,1) ?};
   {!
   |? USERSF.USERS();
      {? _dialog || FUN.prg_next(1,'Aktualizacja: %1'@[USERS.WEBLOGIN]) ?};
      _pmprac:=0; _ppprac:=0;
      _pmkier:=0; _ppkier:=0;
      _pmseod:=0; _ppseod:=0;
      _akcb:=0; _akcw:=0;
::    Na wszelki wypadek sprawdzenie, czy jest login webowy i czy jest poprawnym adresem e-mail,
::    oraz koniecznie, czy użytkownik jest aktywny
      {? USERS.AKT='T' & USERS.WEBLOGIN<>'' & exec('mail_ok','#email',USERS.WEBLOGIN)
      ||
::       Czyszczenie roboczej tabeli uprawnień
         _tab.prefix();
         {? _tab.first()
         || {!
            |? _tab.ENABLED:='N';
               _tab.put();
               _tab.next()
            !}
         ?};

::       Obróbka uprawnień dla użytkownika
         _tab.prefix();
         {? _tab.first()
         || {!
            |? {? _dialog || FUN.prg_next(1,'Aktualizacja (1/2): %1'@[USERS.WEBLOGIN]) ?};
::             Jeżeli jest to uprawnienie pracownicze (także RCP), a jest blokada, to gaszę
               {? (8+_tab.OPR_NAME='STD_PC_E' | _tab.OPR_NAME='STD_PC_RCP_PRC') & (USERS.PXPRAC | ~_lic_por)
               || _enabled:='N'

::             Jeżeli jest to uprawnienie kierownicze, a jest blokada, to gaszę
               |? 8+_tab.OPR_NAME='STD_PC_M' & (USERS.PXKIER | ~_lic_por)
               || _enabled:='N'

::             Jeżeli jest to uprawnienie obiegowe, a jest blokada lub brak licencji na SEO, to gaszę
               |? 8+_tab.OPR_NAME='STD_PC_S' & (USERS.PXSEOD | ~_lic_seo)
               || _enabled:='N'

               ||
::                Wyszukanie powiązanej czynności
                  B_ACTION.index('PORTAL');
                  B_ACTION.prefix('T');
                  _act.prefix(_tab.OPR_NAME,);
::                Jeżeli użytkownik ma dostęp do czynności, to dostaje uprawnienie na portalu
::                Jeżeli nie ma zdefiniowanej powiązanej czynności to uprawnienie jest gaszone
                  _enabled:='N';
                  {? _act.first()
                  || {!
                     |? {? B_ACTION.seek(_act.AKC,)
                        ||
::                         Konfiguracja uprawnień zawsze wielofirmowa - uprawnienie dostajemy,
::                         jeśli jest w dowolnej firmie!!
                           {? XINFO.POR_CONF='W';1
                           || FIRMA.index('SYMBOL');
                              FIRMA.prefix();
                              {? FIRMA.first()
                              || {!
                                 |? {? FIRMA.A='T' & exec('chk_role','#b__box',USERS.ref(),B_ACTION.ref(),FIRMA.ref())
                                    || _enabled:='T'
                                    ?};
                                    FIRMA.next()
                                 !}
                              ?}
                           || {? exec('chk_role','#b__box',USERS.ref(),B_ACTION.ref())
                              || _enabled:='T'
                              ?}
                           ?}
                        ?};
                        _enabled='N' & _act.next()
                     !}
                  ?}
               ?};
               {? _enabled='T'
               || {? 8+_tab.OPR_NAME='STD_PC_E' | _tab.OPR_NAME='STD_PC_RCP_PRC'
                  || _pmprac:=1
                  |? 8+_tab.OPR_NAME='STD_PC_M'
                  || _pmkier:=1
                  |? 8+_tab.OPR_NAME='STD_PC_S'
                  || _pmseod:=1
                  ?};
                  {? _tab.OPR_NAME=_pulpit_prac
                  || _ppprac:=1
                  ?};
                  {? _tab.OPR_NAME=_pulpit_kier
                  || _ppkier:=1
                  ?};
                  {? _tab.OPR_NAME=_akcept_b
                  || _akcb:=1
                  ?};
                  {? _tab.OPR_NAME=_akcept_w
                  || _akcw:=1
                  ?}
               ?};
               _tab.ENABLED:=_enabled;
               _tab.put();
               _tab.next()
            !}
         ?};

::       Jeżeli użytkownik ma jednocześnie pulpit kierownikia i pracownika, to należy mu zgasić pulpit pracownika
         {? _ppprac & _ppkier
         || _tab.prefix(_pulpit_prac,);
            {? _tab.first()
            || _tab.ENABLED:='N';
               _tab.put()
            ?}
         ?};

::       obsługa przycisków Aplikacje->Czas pracy oraz Aplikacje->Dodatkowe - wyłączamy je jeżeli nie jest zaznaczona
::       współpraca z portalem webTerma
         {? ~_isWebTermInHrPortal
         ||  _tab.prefix('STD_PC_EXTRA_PRC',);
            {? _tab.first()
            || _tab.ENABLED:='N';
               _tab.put()
            ?}
         ?};
::       jeżeli nie mamy licencji na PRC to wyłączamy uprawnienie dla przycisku linkToRCP
         {? ~_isWebTermInHrPortal | ~_lic_prc
         || _tab.prefix('STD_PC_RCP_PRC',);
            {? _tab.first()
            || _tab.ENABLED:='N';
               _tab.put()
            ?}
         ?};

::       jeżeli użytkownik ma jednocześnie prawo do akceptacji wniosków z kasą jak i bez to "gasimy" uprawnienie do
::       przycisku 'Nowy wniosek' w zakładce 'Mój zespół (bez kwot)' na Portalu HR
         {? _akcb & _akcw
         || _tab.prefix(_akcept_b,);
            {? _tab.first()
            || _tab.ENABLED:='N';
               _tab.put()
            ?}
         ?};

::       Naniesienie uprawnień na tabelę PORTALU
         _tab.prefix();
         {? _tab.first()
         || {!
            |? PORTALU.prefix('U',USERS.WEBLOGIN,_tab.OPR_NAME,);
               {? PORTALU.first()
               || PORTALU.ENABLED:=_tab.ENABLED;
                  PORTALU.put()
               || {? 8+_tab.OPR_NAME='STD_PC_S' & _pmseod
                     | _tab.OPR_NAME='STD_PortalCloud_SEOD' & _pmseod
                     | 8+_tab.OPR_NAME<>'STD_PC_S' & _tab.OPR_NAME<>'STD_PortalCloud_SEOD'
                  || PORTALU.TYPE:='U';
                     PORTALU.LOGIN:=USERS.WEBLOGIN;
                     PORTALU.OPR_NAME:=_tab.OPR_NAME;
                     PORTALU.ENABLED:=_tab.ENABLED;
                     PORTALU.ID_ERP:='';
                     PORTALU.add()
                  ?}
               ?};
               _tab.next()
            !}
         ?}
      || {? _dialog || FUN.prg_next(_tabSize,'Aktualizacja: %1'@[USERS.WEBLOGIN]) ?}
      ?};

::    To, co myśli Merit o uprawnieniach do portalu i zużytych potencjalnie licencjach
      USERS.PMPRAC:=_pmprac;
      USERS.PMKIER:=_pmkier;
      USERS.PMSEOD:=_pmseod;
      USERS.put();

      USERSF.next()
   !};
   {? _dialog || FUN.prg_stop() ?};
   ~~
?};

USERS.cntx_pop();
USERSF.cntx_pop();
PORTALU.cntx_pop();
B_ACTION.cntx_pop();
~~


\get_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [21.37]
:: OPIS: Pobiera wartość parametru portalu o identyfikatorze przekazanym w argumencie _a.
::   WE: _a [STRING] - identyfikator parametru
::   WY: Wartość parametru
::----------------------------------------------------------------------------------------------------------------------
_id:={? var_pres('_a')=type_of('') & _a<>'' || _a || return('') ?};
exec('czytaj','#stalesys',,XINFO,'POR_CONF');
_multiCompany:=(XINFO.POR_CONF='W');

_get:="@.PORPAR.WARTOSC";
_firma:={? _multiCompany || null || exec('ref_firma','ustawienia') ?};
exec('FindInSet','#table','PORPAR','SYMBOL',_id,_firma,_get,1,,'')

:Sign Version 2.0 jowisz:1045 2023/12/21 12:17:10 931de2e6645be6b325585d6850a16d7677034787b8207852463125cc31dc8165a8a9fcb0dca854bbafe2d5a92fe1a6a8115e78ec253bd4927c2c3b930f1ffee736e308e4c728086527cfb9dce737e83fae48ed5d7cc569545d73ecfdfda774d60321abe6a1cd95fc5a932f3b795873c4008e94abdd96eecd9649caa52d022146

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #b_perm.fml
:: Utworzony: 09.02.2015 [17.00]
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi uprawnień do danych
::======================================================================================================================


\decl_perm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Deklaracja klasy PERM do obsługi uprawnień
:: ~OST: INWINBTN
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('PERM',@.CLASS)<0
|| obj_decl('PERM',
      obj_fld('Act',0),
      obj_fld('Tmp',~~),
      obj_fld('UserWin',~~),
      obj_fld('UserWin2',~~),
      obj_fld('UserWin3',~~),
      obj_fld('UserWin4',~~),
      obj_fld('RejNew',0),
      obj_fld('RejNewTab',~~),
      obj_fld('ActRolForDataTab',~~),
      obj_fld('RejNewWin',~~),
      obj_fld('GrpTyp',0),
      obj_fld('needPermissions',~~),
      obj_fld('needTab',~~),
      obj_fld('Init',1),
      obj_fld('FNoInit',""),
      obj_fld('UserPermTab',~~),
      obj_fld('UserPermI',~~),
      obj_fld('UserPermGrp',~~),
      obj_fld('UserPermFirst',~~),
      obj_meth('__init',"
         .Tmp:=tab_tmp(1,
            'PERM','INTEGER',
         );
         .needTab:=tab_tmp(1,
            'DESC','STRING[100]',
         );
         .RejNewTab:=.tab_tmp();
         .ActRolForDataTab:=.tab_tmp2();
         .UserPermTab:=.tab_tmp3();
         .UserPermI:=obj_new('IDesc','IRef');
         .UserPermI.IDesc:=.UserPermTab.index('?');
         .UserPermI.IRef:=.UserPermTab.ndx_tmp('',1,'REF',,0);
         ~~
      "),
      obj_meth('tab_tmp',"
         tab_tmp(2,
            'USER','INTEGER',,
            'NAME','STRING[20]','Nazwa',
            'DESC','STRING[100]','Opis',
            'ACCESS','STRING[1]',,
            'FULL','STRING[1]',,
            'REF','INTEGER',,
            'USERKOD','STRING[10]','Użytkownik'
         )
      "),
      obj_meth('tab_tmp2',"
         tab_tmp(1,
            'NAMEROL','STRING[60]','Rola',
            'UID','STRING[12]','Identyfikator czynności',
            'NAME','STRING[60]','Nazwa czynności'

         )
      "),
      obj_meth('tab_tmp3',"
         tab_tmp(1,
            'DESC','STRING[255]','Opis',
            'REF','INTEGER','Ref',
            'ACCESS','STRING[1]','Ustawione'
         )
      "),
      obj_meth('getUserPermTab',"
         .UserPermTab
      "),
      obj_meth('setUser',"
         _grp:=.UserPermGrp:=USERS.sel_size();
         _tab:=.UserPermTab;
         _tab.erase();
         {? _grp
         || VAR_DEL.delete('UserPermSel');
            _sel:=UserPermSel:=USERS.sel_aget();
            .UserPermFirst:=null;
            {? _sel.first()
            || _first:=1;
               USERS.cntx_psh();
               USERS.prefix();
               B_PERM_U.cntx_psh();
               B_PERM_U.index('USER');
               {!
               |? {? USERS.seek(_sel.REF)
                  || {? .UserPermFirst=null
                     || .UserPermFirst:=USERS.ref()
                     ?};
                     B_PERM_U.prefix(REF.FIRMA,USERS.ref());
                     {? _first
                     || _first:=0;
                        {? B_PERM_U.first()
                        || {!
                           |? _tab.blank(1);
                              _tab.REF:=B_PERM_U.B_PERM;
                              _tab.ACCESS:='';
                              _tab.DESC:=B_PERM_U.B_PERM().DESC;
                              _tab.add();
                              B_PERM_U.next()
                           !}
                        ?}
                     || {? _tab.first()
                        || {!
                           |? {? B_PERM_U.find_key(_tab.REF)
                              || _tab.next()
                              || _tab.del()
                              ?}
                           !}
                        ?}
                     ?}
                  ?};
                  _tab.first() & _sel.next()
               !};
               B_PERM_U.cntx_pop();
               USERS.cntx_pop()
            ?}
         || B_PERM_U.index('USER');
            B_PERM_U.prefix(REF.FIRMA,_a);
            {? B_PERM_U.first()
            || _tab.prefix();
               {!
               |? _tab.blank(1);
                  _tab.REF:=B_PERM_U.ref();
                  _tab.ACCESS:=B_PERM_U.ACCESS;
                  _tab.DESC:=B_PERM_U.B_PERM().DESC;
                  _tab.add();
                  B_PERM_U.next()
               !}
            ?}
         ?};
         _tab.first()
      "),
      obj_meth('hasAny',"
         B_PERM_U.index('USER');
         B_PERM_U.prefix(REF.FIRMA,_a);
         B_PERM_U.first()
      "),
      obj_meth('win',"
         {? _a=3
         || _tab:=.UserPermTab;
            _wer:=_tab.mk_sel('Uprawnienia do danych'@,'P',,'#userperm'+$_a,,,,,'U','T');
            _tab.win_fld(_wer,,'ACCESS',,,,,1,'Ustawione'@,,'Czy uprawnienie jest ustawione?'@,2,,\"'T'\",\"'N'\",\"''\");
            _tab.win_fld(_wer,,'DESC',,,42,,,,,'Uprawnienie'@);
            _formula:=\"Perm.fun_set(1)\";
            _grupa:=\"Perm.grp_set(1)\";
            _tab.win_act(_wer,,'Formuła','Ustaw'@@,,'Ustawianie uprawnień do danych'@,_formula,,1,,,,'U');
            _btn:=_tab.win_btn(_wer,'text=%1,panel=right,align=begin'['Ustaw'@],'menu:U',,,,,,'noempty');
            _tab.win_act(_wer,,'Kolejność');
            _tab.win_act(_wer,,'Rekord',,,,\"
               _grp:=Perm.UserPermGrp;
               _tab:=Perm.UserPermTab;
               {? _grp
               || B_PERM_U.index('USER');
                  B_PERM_U.prefix(REF.FIRMA,Perm.UserPermFirst,_tab.REF);
                  B_PERM_U.first();
                  ''
               || B_PERM_U.prefix();
                  {? B_PERM_U.seek(_tab.REF)
                  || _tab.ACCESS:=B_PERM_U.ACCESS; _tab.put();
                     exec('br_b_perm_u','#b_perm',B_PERM_U.ACCESS,B_PERM_U.FULL)
                  || ''
                  ?}
               ?}
            \");
            _tab.win_act(_wer,,'Formuła','Legenda'@@,,,\"exec('legenda','color','B_PERM#01')\",,,,,,'L');
            _wer
         || _o:=B_PERM_U.mk_sel('Uprawnienia do danych'@,'P',,'#userperm'+$_a,,,,,'U','T');
            B_PERM_U.win_fld(_o,,'ACCESS',,,,,1,'Ustawione'@,,'Czy uprawnienie jest ustawione?'@,2,,\"'T'\",\"'N'\",);
            {? _a || B_PERM_U.win_fld(_o,,'USER','KOD',,,,,'Użytkownik'@) ?};
            B_PERM_U.win_fld(_o,,'B_PERM','DESC',,42,,,,,'Uprawnienie'@);
            _formula:=\"Perm.fun_set(1)\";
            _grupa:=\"Perm.grp_set(1)\";
            B_PERM_U.win_act(_o,,'Formuła','Ustaw'@@,,'Ustawianie uprawnień do danych'@,_formula,,1,,,,'U');
::         _formula2:=\"exec('get_act_for_perm','zws',B_PERM_U)\";
::         B_PERM_U.win_btn(_o,'text=%1,panel=right,align=begin'['Czynności'@],'Formuła',_formula2,,,,,'noempty');
            _btn:=B_PERM_U.win_btn(_o,'text=%1,panel=right,align=begin'['Ustaw'@],'menu:U',,,,,,'noempty');
            B_PERM_U.win_act(_o,,'Kolejność');
            B_PERM_U.win_act(_o,,'Rekord',,,,\"exec('br_b_perm_u','#b_perm',B_PERM_U.ACCESS,B_PERM_U.FULL)\");
            B_PERM_U.win_act(_o,,'Formuła','Legenda'@@,,,\"exec('legenda','color','B_PERM#01')\",,,,,,'L');
            _o
         ?}
      "),
      obj_meth('win2',"
         _o:=.RejNewTab.mk_sel('Uprawnienia do danych'@,'P',,'#userperm2',,,,,'U','T');
         .RejNewTab.win_fld(_o,,'ACCESS',,,,,1,'Ustawione'@,,,2,,\"'T'\",\"'N'\");
         .RejNewTab.win_fld(_o,,'USERKOD',,,,,,'Użytkownik'@);
         .RejNewTab.win_fld(_o,,'DESC',,,80,,,,,'Uprawnienie'@);
         _formula:=\"Perm.fun_set(2)\";
         _grupa:=\"Perm.grp_set(2)\";
         .RejNewTab.win_act(_o,,'Formuła','Ustaw'@@,,'Ustawianie uprawnień do danych'@,_formula,,1,,,,'U');
         _btn:=.RejNewTab.win_btn(_o,'text=%1,panel=right,align=begin'['Ustaw'@],'menu:U',,,,,,'noempty');
         .RejNewTab.win_act(_o,,'Kolejność',,,);
         .RejNewTab.win_act(_o,,'Rekord',,,,\"exec('br_b_perm_u','#b_perm',Perm.RejNewTab.ACCESS,Perm.RejNewTab.FULL)\");
         .RejNewTab.win_act(_o,,'Formuła','Legenda'@@,,,\"exec('legenda','color','B_PERM#01')\",,,,,,'L');
         _o
      "),
      obj_meth('win3',"
         _title:='Czynności i role z uprawnienia do danej: ';
         _o:=.ActRolForDataTab.mk_sel(_title,'P',,'#userperm3',,,,,'U','T');
         .ActRolForDataTab.win_fld(_o,,'NAMEROL',,,25,,,,,'Rola'@);
         .ActRolForDataTab.win_fld(_o,,'UID',,,,,,,,'Identyfikator czynności'@);
         .ActRolForDataTab.win_fld(_o,,'NAME',,,20,,,,,'Nazwa czynności'@);
         .ActRolForDataTab.win_sel(_o);
         _o
      "),
      obj_meth('grp_set',"
         .GrpTyp:=FUN.choice(
            'Do którego?'@,_cur,
            '&Wszystkich'@,'Wy&branych'@,'Ża&dnego'@)
      "),
      obj_meth('fun_set',"
         {? _a=2
         || B_PERM_U.prefix();
            _ok:=B_PERM_U.seek(.RejNewTab.REF,)
         || _ok:=1
         ?};
         {? _ok
         || B_PERM_U.USER();
            on_error(3);
            _err:=0;
            _typ:=($B_PERM_U.B_PERM().F_SET)({? .UserPermGrp || UserPermSel || B_PERM_U.USER ?});
            {? in_error()
            || _err:=1;
               _typ:=-1
            ?};
            on_error();
            {? _typ<>-1
            || {? .UserPermGrp
               || _sel:=UserPermSel;
                  {? _sel.first()
                  || USERS.cntx_psh();
                     USERS.prefix();
                     _b_perm:=B_PERM_U.B_PERM;
                     B_PERM_U.cntx_psh();
                     B_PERM_U.index('USER'); B_PERM_U.prefix(REF.FIRMA);
                     {!
                     |? {? USERS.seek(_sel.REF,) & B_PERM_U.find_key(USERS.ref(),_b_perm)
                        || .setType(_typ);
                           B_PERM_U.put()
                        ?};
                        _sel.next()
                     !};
                     B_PERM_U.cntx_pop();
                     USERS.cntx_pop()
                  ?}
               || .setType(_typ);
                  {? B_PERM_U.put() & _a=2
                  || .RejNewTab.ACCESS:=B_PERM_U.ACCESS;
                     .RejNewTab.FULL:=B_PERM_U.FULL;
                     .RejNewTab.put()
                  ?}
               ?}
            ?};
            {? _err
            || FUN.info('Powstał błąd podczas nadawania uprawnień.'@)
            ?}
         ?}
      "),
       obj_meth('add4user',"
         B_PERM_U.cntx_psh();
         B_PERM_U.index('USER'); B_PERM_U.prefix(_a);
         {? ~B_PERM_U.find_key(_b,_c)
         || B_PERM.prefix();
            {? B_PERM.seek(_c)
            || B_PERM_U.blank(1);
               B_PERM_U.FIRMA:=_a;
               B_PERM_U.USER:=_b;
               B_PERM_U.B_PERM:=_c;
               B_PERM_U.ACCESS:='N';
               B_PERM_U.FULL:='N';
               B_PERM_U.COUNT:=1;
               {? B_PERM_U.add() & .RejNew
               || .RejNewTab.blank(1);
                  .RejNewTab.USER:=#B_PERM_U.USER;
                  .RejNewTab.NAME:=B_PERM.NAME;
                  .RejNewTab.DESC:=B_PERM.DESC;
                  .RejNewTab.ACCESS:='N';
                  .RejNewTab.FULL:='N';
                  .RejNewTab.REF:=#B_PERM_U.ref();
                  .RejNewTab.USERKOD:=B_PERM_U.USER().KOD;
                  .RejNewTab.add()
               ?}
            ?}
         || B_PERM_U.COUNT+=1;
            B_PERM_U.put()
         ?};
         B_PERM_U.cntx_pop()
      "),
      obj_meth('del4user',"
         B_PERM_U.cntx_psh();
         B_PERM_U.index('USER'); B_PERM_U.prefix(_a);
         {? B_PERM_U.find_key(_b,_c)
         || B_PERM_U.COUNT-=1;
            {? B_PERM_U.COUNT<=0
            || B_PERM_U.del()
            || B_PERM_U.put()
            ?}
         ?};
         B_PERM_U.cntx_pop()
      "),
      obj_meth('allPermSet',"
         _set:=1;
         B_PERM_U.cntx_psh();
         B_PERM_U.index('USER');
         B_PERM_U.prefix(REF.FIRMA,_a);
         {? B_PERM_U.first()
         || {!
            |? _set:=B_PERM_U.ACCESS='T';
               _set=1 & B_PERM_U.next()
            !}
         ?};
         B_PERM_U.cntx_pop();
         _set
      "),
      obj_meth('perm2tmp',"
         .Tmp.erase();
         .Act:=_a;
         B_PERM_A.cntx_psh();
         B_PERM_A.index('B_ACTION');
         B_PERM_A.prefix(_a);
         {? B_PERM_A.first()
         || {!
            |? .Tmp.PERM:=B_PERM_A.B_PERM;
               .Tmp.add();
               B_PERM_A.next()
            !}
         ?};
         B_PERM_A.cntx_pop()
      "),
      obj_meth('del_tmp',"
         {? .Tmp.find_key(_a)
         || .Tmp.del()
         ?}
      "),
      obj_meth('del_rest',"
         B_PERM_A.cntx_psh();
         B_PERM_A.index('B_ACTION');
         B_PERM_A.prefix(.Act);
         {? .Tmp.first()
         || {!
            |? {? B_PERM_A.find_key(.Tmp.PERM)
               || B_PERM_A.del()
               ?};
               .Tmp.next()
            !}
         ?};
         B_PERM_A.cntx_pop();
         .Tmp.erase()
      "),
      obj_meth('add',"
         {? .Init
         || B_PERM.index('NAME'); B_PERM.prefix();
            _jest:=B_PERM.find_key(_a,);
            {? ~_jest
            || B_PERM.blank(1);
               B_PERM.NAME:=_a
            ?};
            B_PERM.DESC:=_b;
            B_PERM.F_SET:={? type_of(_c)=type_of(\"\") || $_c || _c ?};
            {? var_pres('_d')>0
            || B_PERM.F_COPY:={? type_of(_d)=type_of(\"\") || $_d || _d ?}
            ?};
            {? _jest
            || B_PERM.put()
            || B_PERM.add()
            ?}
         |? $.FNoInit<>''
         || .FNoInit(_a)
         ?}
      "),
      obj_meth('perm2act',"
         B_PERM_A.index('B_ACTION'); B_PERM_A.prefix(_a);
         B_PERM.index('NAME'); B_PERM.prefix();
         _perms:=spli_str(_b,',');
         _ret:='';
         {! _i:=1..obj_len(_perms)
         |! {? B_PERM.find_key(_perms[_i],)
            || {? B_PERM_A.find_key(B_PERM.ref())
               || .del_tmp(B_PERM.ref())
               || B_PERM_A.blank(1);
                  B_PERM_A.B_ACTION:=_a;
                  B_PERM_A.B_PERM:=B_PERM.ref();
                  B_PERM_A.add();
                  .del_tmp(B_PERM.ref())
               ?}
            || _ret+={? _ret<>'' || ', ' || '' ?}+_perms[_i]
            ?}
         !};
         _ret
      "),
      obj_meth('start_rej',"
         .RejNew:=1;
         .RejNewTab.erase()
      "),
      obj_meth('stop_rej',"
         {? .RejNewTab.first()
         || {? FUN.choice( 'Wprowadzone zmiany wymagają nadania\n'
                  'użytkownikom uprawnień do danych.'@,,
                  'Nadaj uprawnienia'@)
            || .RejNewTab.win_sel(.rejNewWin());
               .RejNewTab.select();
               .RejNewTab.erase()
            ?}
         ?};
         .RejNew:=0
      "),
      obj_meth('needAdd',"
         {? ~.needTab.find_key(_a,)
         || .needTab.DESC:=_a;
            .needTab.add()
         ?}
      ",type_of('')),
      obj_meth('needClear',"
         {? .needTab.first() || {! |? .needTab.del() !} ?}
      "),
      obj_meth('needString',"
         _str:='';
         {? .needTab.first()
         || {!
            |? _str+=.needTab.DESC+'\n';
               .needTab.next()
            !}
         ?};
         _str
      "),
      obj_meth('hasPermissions',"
         {? var_pres('_b')<=0 || _b:=1 ?};
         {? var_pres('_c')<=0 || _c:=1 ?};
         {? var_pres('_d')<=0 || _d:=OPERATOR.USER ?};
         _tak:=1;
         {? _c
         || .needClear()
         ?};
         B_PERM_A.index('B_ACTION');
         B_PERM_A.prefix(_a);
         B_PERM_U.cntx_psh();
         B_PERM_U.index('USER');
         B_PERM_U.prefix(REF.FIRMA,_d);
         {? B_PERM_A.first()
         || {!
            |? {? ~B_PERM_U.find_key(B_PERM_A.B_PERM) | B_PERM_U.ACCESS<>'T'
               || _tak:=0;
                  .needAdd(B_PERM_A.B_PERM().DESC)
               ?};
               B_PERM_A.next()
            !}
         ?};
         B_PERM_U.cntx_pop();
         .needPermissions:=.needString();
         {? ~_tak & _b
         || FUN.emsg('Brak uprawnień do następujących danych:\n%1'@[Perm.needPermissions])
         ?};
         _tak
      ",type_of(B_ACTION.ref)),
      obj_meth('hasPermArea',"
         {? var_pres('_b')<=0 || _b:=OPERATOR.USER ?};
         _tak:=1;
         .needClear();
         B_ACTION.cntx_psh();
         B_ACTION.index('UNIK'); B_ACTION.prefix();
         _task:=app_info('area_task');
         _task.prefix(_a);
         {? _task.first()
         || {!
            |? {? B_ACTION.find_key(_task.B_TASK,) & B_ACTION.PROC='N' & exec('chk_role','#b__box',_b,B_ACTION.UID)
               || _has:=.hasPermissions(B_ACTION.ref(),0,0,_b);
                  _tak:=_tak & _has
               ?};
               _task.next()
            !}
         ?};
         B_ACTION.cntx_pop();
         _tak
      ",type_of('AREA')),
      obj_meth('get',"
         B_PERM.index('NAME');
         B_PERM.prefix();
         {? B_PERM.find_key(_a,)
         || B_PERM.ref()
         || null
         ?}
      "),
      obj_meth('hasPermissions',"
         {? var_pres('_b')<=0 || _b:=OPERATOR.USER ?};
         _tak:=0;
         _perm:=.get(_a);
         {? _perm<>null
         || B_PERM_U.cntx_psh();
            B_PERM_U.index('USER');
            B_PERM_U.prefix(REF.FIRMA,_b);
            _tak:=B_PERM_U.find_key(_perm) & B_PERM_U.ACCESS='T';
            B_PERM_U.cntx_pop()
         ?};
         _tak
      ",type_of('KOD')),
       obj_meth('hasFull',"
         {? var_pres('_b')<=0 || _b:=OPERATOR.USER ?};
         _tak:=0;
         _perm:=.get(_a);
         {? _perm<>null
         || B_PERM_U.cntx_psh();
            B_PERM_U.index('USER');
            B_PERM_U.prefix(REF.FIRMA,_b);
            _tak:=B_PERM_U.find_key(_perm) & B_PERM_U.FULL='T';
            B_PERM_U.cntx_pop()
         ?};
         _tak
      ",type_of('KOD')),
      obj_meth('getType',"
         {? B_PERM_U.FULL='T' || 2 |? B_PERM_U.ACCESS='T' || 1 ?}
      "),
      obj_meth('setType',"
         {? _a=2
         || B_PERM_U.FULL:='T';
            B_PERM_U.ACCESS:='T'
         |? _a=1
         || B_PERM_U.FULL:='N';
            B_PERM_U.ACCESS:='T'
         || B_PERM_U.FULL:='N';
            B_PERM_U.ACCESS:='N'
         ?}
      "),
       obj_meth('hasPermAct',"
         _has:=0;
         B_ACTION.cntx_psh();
         B_ACTION.prefix();
         {? B_ACTION.seek(_a)
         || B_PERM_A.cntx_psh();
            B_PERM_A.index('B_ACTION');
            B_PERM_A.prefix(B_ACTION.ref());
            _has:=B_PERM_A.first();
            B_PERM_A.cntx_pop()
         ?};
         B_ACTION.cntx_pop();
         _has
      ",type_of('ref_sql')),

:     Dla bieżącego użytkownika w wybranej firmie ustawia uprawnienia _a (nazwa) zgodnie z _b (0/1/2)
      obj_meth('setPermission',"
         .setPermission(_a,REF.FIRMA,OPERATOR.USER,_b)
      ",type_of(''),type_of(0)),

:     Dla wskazanego użytkownika we wskazanej firmie ustawia uprawnienia _a (nazwa) zgodnie z _b (0/1/2)
      obj_meth('setPermission',"
         B_PERM_U.cntx_psh();
         B_PERM_U.index('USER');
         B_PERM_U.prefix(_b,_c,.get(_a));
         {? B_PERM_U.first()
         || .setType(_d);
            B_PERM_U.put()
         ?};
         B_PERM_U.cntx_pop();
         B_PERM_U.get();
         ~~
      ",type_of(''),type_of(null),type_of(null),type_of(0)),

      obj_meth('userWin',"
         {? .UserWin=~~
         || .UserWin:=.win(0)
         ?};
         .UserWin
      "),
      obj_meth('userWin2',"
         {? .UserWin2=~~
         || .UserWin2:=.win(1)
         ?};
         .UserWin2
      "),
      obj_meth('userWin3',"
         {? .UserWin3=~~
         || .UserWin3:=.win3()
         ?};
         .UserWin3
      "),
      obj_meth('userWin4',"
         {? .UserWin4=~~
         || .UserWin4:=.win(3)
         ?};
         .UserWin4
      "),
      obj_meth('rejNewWin',"
         {? .RejNewWin=~~
         || .RejNewWin:=.win2()
         ?};
         .RejNewWin
      "),
      obj_meth('actRolForDataTab',"
         .addActRol();
         .ActRolForDataTab
      "),
:     Kopia uprawnień z użytkownika _a do użytkownika _b z czyszczeniem obecnych gdy ustawione _c
      obj_meth('copy',"
         B_PERM_U.cntx_psh();
         B_PERM_U.index('USER');
         B_PERM_U.prefix(REF.FIRMA,_a);
         {? B_PERM_U.first()
         || {!
            |? {? B_PERM_U.B_PERM().F_COPY<>''
               || _access:=B_PERM_U.ACCESS;
                  _full:=B_PERM_U.FULL;
                  B_PERM_U.cntx_psh();
                  B_PERM_U.prefix(REF.FIRMA,_b,B_PERM_U.B_PERM);
                  {? B_PERM_U.first()
                  || B_PERM_U.ACCESS:=_access;
                     B_PERM_U.FULL:=_full;
                     B_PERM_U.put();
                     params_set('FROM',_a,'TO',_b,'CLEAR',_c);
                     ($('params_set(params_get());'+B_PERM_U.B_PERM().F_COPY))()
                  ?};
                  B_PERM_U.cntx_pop()
               ?};
               B_PERM_U.next()
            !}
         ?};
         B_PERM_U.cntx_pop()
      ",type_of(null),type_of(null),type_of(0)),
:     Czynności i role z tytułu których dany użytkownik potrzebuje uprawnień do danych
      obj_meth('addActRol',"
         .ActRolForDataTab.erase();

          B_USRROL.cntx_psh();
          B_USRROL.index('USER'); B_USRROL.prefix(REF.FIRMA,USERS.ref());
          {? B_USRROL.first()
          || {!
             |? B_ACTROL.cntx_psh();
                B_ACTROL.index('S1');
                B_ACTROL.prefix(B_USRROL.B_ROLE);
                {? B_ACTROL.first()
                || {!
                   |? B_PERM_A.cntx_psh();
                      B_PERM_A.index('B_ACTION'); B_PERM_A.prefix(); B_PERM_A.prefix(B_ACTROL.B_ACTION,B_PERM_U.B_PERM);
                      {? B_PERM_A.first()
                      || {!
                         |? .ActRolForDataTab.blank(1);
                            .ActRolForDataTab.NAMEROL:=B_ACTROL.B_ROLE().NAME;
                            .ActRolForDataTab.UID:=B_PERM_A.B_ACTION().UID;
                            .ActRolForDataTab.NAME:=B_PERM_A.B_ACTION().NAME;
                            .ActRolForDataTab.add();
                            B_PERM_A.next()
                         !}
                      ?};
                      B_PERM_A.cntx_pop();
                      B_ACTROL.next()
                   !}
                ?};
                B_ACTROL.cntx_pop();
                B_USRROL.next()
             !}
          ?};
          B_USRROL.cntx_pop();
         .ActRolForDataTab.hdr_sel();.ActRolForDataTab.hdr_sel(B_PERM_U.B_PERM().DESC)
      ")
   )
?}


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wczytuje uprawnienia przypisane czynnościom
::   WE: [_a] - [1]-z komunikatami 0-bez
::       [_b] - B_DOMAIN.ref - obszar którego czynności aktualizować, jeśli nie podane to wszystkie obszary
::----------------------------------------------------------------------------------------------------------------------
_komm:={? var_pres('_a')>0 || _a || 1 ?};
{? _komm>0
|| KOMM.init(,,'Import uprawnień do danych czynności'@)
?};

_b_domain:=null();
{? var_pres('_b')=type_of(B_DOMAIN.ref())
|| _b_domain:=_b
?};

B_DOMAIN.index('SYMBOL');
B_DOMAIN.prefix();
B_ACTION.index('B_DOMAIN');
{? _b_domain=null()
||
:: Wszystkie obszary
   {? B_DOMAIN.first()
   || {!
      |? B_ACTION.prefix('T',B_DOMAIN.ref());
         {? B_ACTION.first()
         || {!
            |? {? B_ACTION.MANUAL<>'T'
               || exec('perm4act','#b_perm',1)
               ?};
               B_ACTION.next()
            !}
         ?};
         B_DOMAIN.next()
      !}
   ?}
||
:: Tylko jeden wybrany obszar
   B_ACTION.prefix('T',_b_domain);
   {? B_ACTION.first()
   || {!
      |? {? B_ACTION.MANUAL<>'T'
         || exec('perm4act','#b_perm',1)
         ?};
         B_ACTION.next()
      !}
   ?}
?};
{? _komm & ~KOMM.empty()
|| KOMM.select()
?}


\perm4act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wczytuje uprawnienia przypisane czynności
::   WE: [_a] - [1]-z komunikatami 0-bez
::----------------------------------------------------------------------------------------------------------------------
_fun_g:='main';
_plik:=exec('filename','#b_action');
_ok:=1;
_sect:='Czynność: %1 — %2'@[B_ACTION.UID,B_ACTION.NAME];
{? exec('is_fun','#file',_plik,_fun_g)
|| Perm.perm2tmp(B_ACTION.ref());

   _fun_str:=exec('string_fun','#file',_plik,_fun_g,'::# permissions=');
   _split:=spli_str(_fun_str,'\n');
   {? obj_len(_split)>0
   ||
      {! _it:=1..obj_len(_split)
      |!
         _wiersz:=_split[_it];
         {? _wiersz<>'\n' & _wiersz<>''
         || _ret:=Perm.perm2act(B_ACTION.ref(),_wiersz);
            {? _a & _ret<>''
            || _ok:=0;

               {? KOMM.find_msg(_sect) || KOMM.set_root(_sect) || KOMM.sect_beg(_sect) ?};
               KOMM.add('Brak uprawnień: %1.'@[_ret],'xwin16.png:14');
               KOMM.sect_end()
            ?}
         ?}
      !}
   ?};
   Perm.del_rest()
?};
_ok


\trig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Inicjalizacja trigerów do uprawnień
::----------------------------------------------------------------------------------------------------------------------
_name:='uprawnienia';
B_PERM_A.trig_a('add',"exec('t_b_perm_a','#b_perm',2,_a);~~",_name);
B_PERM_A.trig_b('del',"exec('t_b_perm_a','#b_perm',3,_a)",_name);
B_USRROL.trig_a('add',"exec('t_b_usrrol','#b_perm',2,_a);~~",_name);
B_USRROL.trig_b('del',"exec('t_b_usrrol','#b_perm',3,_a)",_name);
B_ACTROL.trig_a('add',"exec('t_b_actrol','#b_perm',2,_a);~~",_name);
B_ACTROL.trig_b('del',"exec('t_b_actrol','#b_perm',3,_a)",_name);
~~


\t_b_usrrol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Triger na tabeli B_USRROLE
::   WE: _a - typ operacji: 2-add 3-del
::       _b - czy operacja się powiodła
::----------------------------------------------------------------------------------------------------------------------
{? _a=2 & _b | _a=3
|| B_ACTROL.cntx_psh();
   B_ACTROL.index('B_ROLE'); B_ACTROL.prefix(B_USRROL.B_ROLE);
   B_PERM_A.cntx_psh();
   B_PERM_A.index('B_ACTION');
   {? B_ACTROL.first()
   || {!
      |? B_PERM_A.prefix(B_ACTROL.B_ACTION,);
         {? B_PERM_A.first()
         || {!
            |? exec('b_perm_u','#b_perm',_a);
               B_PERM_A.next()
            !}
         ?};
         B_ACTROL.next()
      !}
   ?};
   B_PERM_A.cntx_pop();
   B_ACTROL.cntx_pop()
?};
1


\t_b_actrol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Triger na tabeli B_ACTROL
::   WE: _a - typ operacji: 2-add 3-del
::       _b - czy operacja się powiodła
::----------------------------------------------------------------------------------------------------------------------
{? _a=2 & _b | _a=3
|| B_USRROL.cntx_psh();
   B_USRROL.index('UNIK'); B_USRROL.prefix(B_ACTROL.FIRMA,B_ACTROL.B_ROLE,);
   B_PERM_A.cntx_psh();
   B_PERM_A.index('B_ACTION'); B_PERM_A.prefix(B_ACTROL.B_ACTION);
   {? B_USRROL.first()
   || {!
      |? {? B_PERM_A.first()
         || {!
            |? exec('b_perm_u','#b_perm',_a);
               B_PERM_A.next()
            !}
         ?};
         B_USRROL.next()
      !}
   ?};
   B_PERM_A.cntx_pop();
   B_USRROL.cntx_pop()
?};
1


\t_b_perm_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Triger na tabeli B_PERM_A
::   WE: _a - typ operacji: 2-add 3-del
::       _b - czy operacja się powiodła
::----------------------------------------------------------------------------------------------------------------------
{? _a=2 & _b | _a=3
|| B_USRROL.cntx_psh();
   B_USRROL.index('UNIK');
   B_ACTROL.cntx_psh();
   B_ACTROL.index('ACTION'); B_ACTROL.prefix(B_PERM_A.B_ACTION);
   {? B_ACTROL.first()
   || {!
      |? B_USRROL.prefix(B_ACTROL.FIRMA,B_ACTROL.B_ROLE);
         {? B_USRROL.first()
         || {!
            |? exec('b_perm_u','#b_perm',_a);
               B_USRROL.next()
            !}
         ?};
         B_ACTROL.next()
      !}
   ?};
   B_ACTROL.cntx_pop();
   B_USRROL.cntx_pop()
?};
1


\b_perm_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Pomocnicza dla trigerów
::   WE: _a - typ operacji: 2-add 3-del
::----------------------------------------------------------------------------------------------------------------------
{? _a=2
|| Perm.add4user(B_ACTROL.FIRMA,B_USRROL.USERS,B_PERM_A.B_PERM)
|? _a=3
|| Perm.del4user(B_ACTROL.FIRMA,B_USRROL.USERS,B_PERM_A.B_PERM)
?}


\perm_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Uprawnienia - tabele
::   WE: _a - obj_new('TAB1','TAB2','WER1','WER2','REF')
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_obj.TAB1:=B_PERM_U;
_obj.TAB2:=tab_tmp(1,
   'DESC','STRING[100]','Opis',
   'REF','STRING[16]','$B_PERM_U.ref()'
);
_obj.T2I1:=_obj.TAB2.index('?')


\perm_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Uprawnienia - okna wertowania
::   WE: _a - obj_new('TAB1','TAB2','WER1','WER2','REF')
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_Tab1:=_obj.TAB1;
_Tab2:=_obj.TAB2;
_wer1:=_obj.WER1;
_wer2:=_obj.WER2;

_wer1:=_obj.WER1:=_Tab1.mk_sel('Uprawnienia'@,'P',,'#werbpermu1',,,,,'U');
_Tab1.win_fld(_wer1,,'B_PERM','DESC',,100,,,'Opis'@);
::exec('upr_wer1_act','#b_role',_Tab1,_wer1);

_wer2:=_obj.WER2:=_Tab2.mk_sel('Uprawnienia'@,,,'#werbpermu2',,,,,'U');
_Tab2.win_fld(_wer2,,'DESC',,,79,,,'Opis'@);
_Tab2.win_act(_wer2,,'Kolejność');
~~


\perm_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Uzupełnienie tabeli z uprawnieniami do danych
::   WE: _a - tabela do uzupełnienia
::       _b - typ rekordu bazowego: Role, User, Action
::       _c - rekord bazowy
::----------------------------------------------------------------------------------------------------------------------
_Perm:=_a;
{? _b='R'
|| B_ACTROL.cntx_psh();
   B_ACTROL.index('UNIK'); B_ACTROL.prefix(REF.FIRMA,_c);
   B_PERM_A.index('B_ACTION');
   {? B_ACTROL.first()
   || {!
      |? exec('perm_set','#b_perm',_a,'A',B_ACTROL.B_ACTION);
         B_ACTROL.next()
      !}
   ?};
   B_ACTROL.cntx_pop()
|? _b='A'
|| B_PERM_A.index('B_ACTION');
   B_PERM_A.prefix(_c);
   {? B_PERM_A.first()
   || {!
      |? B_PERM_A.B_PERM();
         exec('add2tmp','#b_perm',_a);
         B_PERM_A.next()
      !}
   ?}
|? _b='U'
|| B_PERM_U.index('USER'); B_PERM_U.prefix(REF.FIRMA,_c);
   {? B_PERM_U.first()
   || {!
      |? B_PERM_U.B_PERM();
         exec('add2tmp','#b_perm',_a);
         B_PERM_U.next()
      !}
   ?}
?}


\add2tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodanie uprawnienia do tabeli tymczasowej
::   WE: _a - tabela
::----------------------------------------------------------------------------------------------------------------------
{? ~_a.find_key(B_PERM.DESC,)
|| _a.blank(1);
   _a.DESC:=B_PERM.DESC;
   _a.add()
?}


\b_perm_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przeglądanie uprawnień do danych
::----------------------------------------------------------------------------------------------------------------------
_grp:=B_PERM_U.grp_make('Uprawnienia do danych',,'usersperm',,,,,'normal');
USERS.cntx_psh();
B_PERM_U.cntx_psh();
B_PERM_U.grp_sel(_grp,,Perm.userWin2(),'Uprawnienia do danych',"Perm.addActRol();grp_disp(Perm.ActRolForDataTab,Perm.userWin3(),0)",,,15,,,,,'maximized');
B_PERM_U.tab_splt(_grp,,'horizontal','P');
B_PERM_U.grp_sel(_grp,Perm.ActRolForDataTab,Perm.userWin3(),,"",,,,"","",,,'maximized_with_title');
B_PERM_U.win_sel(_grp);
B_PERM_U.index('USER');
B_PERM_U.prefix(REF.FIRMA);
B_PERM_U.select();
B_PERM_U.cntx_pop();
USERS.cntx_pop();
''


\b_perm_a_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przegladanie uprawnień czynności do danych
::----------------------------------------------------------------------------------------------------------------------
_tab:=MOD_ENV.TAB_ACT;
B_ACTION.cntx_psh();
B_ACTION.prefix();
{? B_ACTION.seek(_tab.B_ACT)
|| B_PERM_A.index('B_ACTION');
   B_PERM_A.prefix(B_ACTION.ref());
   _o:=B_PERM_A.mk_sel('Uprawnienia czynności: %1 — %2'@[B_ACTION.UID,B_ACTION.NAME],'P',,'bpermasel',,,,,'U');
   B_PERM_A.win_fld(_o,,'B_PERM','NAME');
   B_PERM_A.win_fld(_o,,'B_PERM','DESC',,60);
   B_PERM_A.win_sel(_o);
   B_PERM_A.select()
?};
B_ACTION.cntx_pop();
''


\br_b_perm_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed okien z uprawnieniami
::   WE: _a - częściowe
::       _b - pełne
::----------------------------------------------------------------------------------------------------------------------
_typ:={? _b='T' || 2 |? _a='T' || 1 ?};
Color.fnd_kol('B_PERM#01#'+($_typ))

:Sign Version 2.0 jowisz:1048 2023/06/23 14:13:35 435b351dbb502f4d69f6c79b1cf6a4ddaf3a3e9fd292039b83b62f56735f5a4bce297cb9abecb20afed524180ea4d091844c8c31ad689185ff4e750a1cecb2630a6659487859f7158d6e50a36abef11150538b82dc87e4ee8a17d1e6bf16664da18fd7fd63f624191aafe41838868f1bb9e477f9eeaa0eda4a3ef47253c742d8

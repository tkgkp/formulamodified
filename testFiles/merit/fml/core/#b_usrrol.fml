:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: #b_usrrol.fml [17.00]
:: Utworzony: 08.10.2013
:: Autor:  AWI
::======================================================================================================================
:: Zawartosc: Procesowosc role uzytkownikow
::======================================================================================================================


\buffer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zwraca obiekt nazwany - bufor tabeli B_USRROL
::   WY: obj_new()
::----------------------------------------------------------------------------------------------------------------------
exec('B_USRROL','#buffer')


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Funkcja dodająca wpis do tabeli B_USRROL
::   WE: _a - obj_new - tablica nazwana bedaca buforem tabeli exec('buffer','#b_usrrol')
::       [_b] - ZAST_NAG.ref() - wskazanie na zastępstwo
::       [_c] - INTEGER - 0/[1]/2 - czy wyświetlać komunikaty: 0 - nie, 1 - na ekran, 2 - do KOMMa
::   WY:      [REFERENCE]  - wskazanie na dodany rekord (lub już istniejący)
::----------------------------------------------------------------------------------------------------------------------
_buffer:={? var_pres('_a')>100 || _a || return(null) ?};
_zast:={? var_pres('_b')>0 || _b || null ?};

_dialog:=1;
{? var_pres('_c')=type_of(0)
|| _dialog:=_c
?};

_res:=0;
B_USRROL.cntx_psh();
B_USRROL.index('UNIK');
B_USRROL.prefix(_buffer.FIRMA,_buffer.B_ROLE,_buffer.USERS,_zast);
{? B_USRROL.first()
|| _buffer.get();
   _res:=1
||
   {? exec('users_prot_role_add','#b_role',_buffer.USERS,_buffer.B_ROLE,_dialog)>0
   ||
      B_USRROL.blank(1);
      _buffer.set();
      B_USRROL.TM_START:=B_USRROL.tm_stamp();
      B_USRROL.STARTD:=date();
      B_USRROL.STARTT:=time();
      B_USRROL.ZAST_NAG:=_zast;
      B_USRROL.U1_KOD:=exec('username','#users');
      B_USRROL.U1_DANE:=userdata();
      {? B_USRROL.add()
      || _buffer.get();
         _res:=exec('after_add_usrrole','#b_usrdom',_buffer)
      ?}
   ?}
?};
B_USRROL.cntx_pop();
_res


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Kasuje podany rekord tabeli B_USRROL
::   WE: _a - [REFERENCE]  - B_USRROL.ref()
::       [_b] - INTEGER - [0]/1/2 - czy wyświetlać komunikaty: 0 - nie, 1 - na ekran, 2 - do KOMMa
::   WY: >0 -wyczyszczone,
::      <=0 -niewyczyszczone
::----------------------------------------------------------------------------------------------------------------------
_ref:={? var_pres('_a')=type_of(null) || _a || return(0) ?};

_dialog:=0;
{? var_pres('_b')=type_of(0)
|| _dialog:=_b
?};

:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
B_USRROL.cntx_psh(); B_USRROL.clear();
_buffer:=exec('buffer','#b_usrrol');
{? B_USRROL.seek(_ref)
||
   {? exec('users_prot_role_del','#b_role',B_USRROL.USERS,B_USRROL.B_ROLE,_dialog)>0
   ||
      _buffer.get();
      {? B_USRROL.del(,1)>0
      ||
::       Jeżeli usunięto uprawnienie to aktualizuję obszary dla użytkowników
         _result:=exec('after_del_usrrole','#b_usrdom',_buffer);

::       Jeżeli udało się usunąć uprawnienie to dodaję do historii zapis o tym od kiedy do kiedy
::       uprawnienie obowiazywało
         {? _result>0
         || _result:=exec('add_urhis','#b_history',_buffer)
         ?}
      || undo();
         _result:=-3
      ?}
   ?}
|| _result:=0
?};
B_USRROL.cntx_pop();

{? _result<0 || undo() ?};
{? _mydo || end() ?};

{? _result>0
|| exec('delUsrActTodo','#bi_todo',_buffer.FIRMA,_buffer.B_ROLE,_buffer.USERS,null())
?};
_result

:Sign Version 2.0 jowisz:1048 2020/10/16 15:20:23 a18b5da3a425ba0934355274678123d682df7d60f48b8b7ac8393d35f5c5474f4f9b7a5e232c4ae303f8adecb30b0ead7cece685ededec85601ad88d658239bf29575c197b5462f8a86f531a7262d57b60cb513de2f03c4b4d9a6d206a0341a426352cbed1559b23ffa069ac9d2832e08c659de3b4b52083855ea483edaa8c78

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #profile.fml [2000]
:: Utworzony: 2000/11/10
:: Autor: GS
::======================================================================================================================
:: Zawartość: Definicje funkcji umożliwiających zachowywanie "ustawień".
::======================================================================================================================


\default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [8.60]
:: OPIS: Realizacja propozycji PR/WRT/KALI/8.40/0017
::       Funkcja kopiuje plik zawierający domyślne ustawienia jako plik ustawień użytkownika. Sprawdzane jest istnienie
::       (zgodnie z definicją ścieżek) pliku np. profile.upf. Jeśli plik nie został odnaleziony, to pod tą nazwą
::       tworzona jest kopia pliku np. profile.dfg. Obie nazwy plików muszą być podane.
::   WE: _a [STRING] - Nazwa pliku źródłowego
::       _b [STRING] - Nazwa pliku docelowego
::   WY:
::  OLD: \default/profile.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || return() ?};
{? var_pres('_b')<>type_of('') || return() ?};
{? _a='' | _b='' || return() ?};
_source:=_a;
_target:=_b;

{? fexists(_source,1) & ~fexists(_target,1)
:: Jeżeli istnieje plik źródłowy (z domyślnymi ustawieniami) i nie istnieje plik docelowy (z ustawieniami użytkownika)
:: to utwórzmy go.
|| fcopy(_source,_target,1,1,0)
?}


\get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Odczytuje wartość parametru z pliku konfiguracyjnego.
::   WE: [_a] [STRING] - Nazwa pliku [domyślnie: 'profile.upf'].
::        _b  [STRING] - Nazwa sekcji.
::        _c  [STRING] - Nazwa parametru.
::        _d  [STRING] - Wartość domyślna w przypadku braku.
::   WY: Ciąg znaków - wartość parametru.
::  OLD: \get/profile.fml
::----------------------------------------------------------------------------------------------------------------------
_fn:={? var_pres('_a')=type_of('') || _a || 'profile.upf' ?};

_ret:='';
{? var_pres('_b')=type_of('') & +form(_b) & var_pres('_c')=type_of('') & +form(_c)
|| _BUFF:=exec('load','#profile',_fn);
   {? _BUFF.find_key(_b,_c,)
   || _ret:=_BUFF.memo_txt(,1,'VALUE')
   || {? var_pres('_d')=type_of('')
      || _ret:=_d
      ?}
   ?}
?};
_ret


\set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Ustawienie wartość parametru w pliku konfiguracyjnym.
::   WE: [_a] [STRING] - Nazwa pliku [domyślnie: 'profile.upf'].
::        _b  [STRING] - Nazwa sekcji.
::        _c  [STRING] - Nazwa parametru.
::       [_d] [STRING] - Wartość parametru [domyślnie: ''].
::  OLD: \set/profile.fml
::----------------------------------------------------------------------------------------------------------------------
_fn:={? var_pres('_a')=type_of('') || _a || 'profile.upf' ?};
_value:={? var_pres('_d')=type_of('') || _d || '' ?};

{? var_pres('_b')=type_of('') & +form(_b) & var_pres('_c')=type_of('') & +form(_c)
|| _BUFF:=exec('load','#profile',_fn);
   {? ~_BUFF.find_key(_b,_c,)
   || _BUFF.GROUP:=_b;
      _BUFF.NAME:=_c;
      _BUFF.add()
   ?};
   _BUFF.memo_set(_value,'VALUE');
   _BUFF.memo_put(,'VALUE');
   exec('save','#profile',_BUFF,_fn)
?}


\buff
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Tworzy bufor przechowujący nazwy i wartości parametrów. Zapisy posortowane są według sekcji i nazwy parametru.
::   WY: alias do tabeli tymczasowej zawierającej wartości parametrów
::  OLD: \buff/profile.fml
::----------------------------------------------------------------------------------------------------------------------
tab_tmp(2,
   'GROUP','STRING[64]', 'Sekcja',
   'NAME' ,'STRING[64]', 'Parametr',
   'VALUE','SYS_MEMO','Wartość'
)


\load
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Wypełnia bufor parametrów zawartością pliku konfiguracyjnego.
::   WE: [_a] [STRING] - Nazwa pliku [domyślnie: 'profile.upf'].
::   WY: Alias do tabeli tymczasowej zawierającej wartości parametrów.
::  OLD: \load/profile.fml
::----------------------------------------------------------------------------------------------------------------------
_fn:={? var_pres('_a')=type_of('') || _a || 'profile.upf' ?};
_BUFF:=exec('buff','#profile');
_fh:=fopen(_fn,'r',1,,1);
{? _fh.is_open()
|| {!
   |? (_line:=_fh.fread())<>'\n'
   |! _flda:=spli_str(_line,%9);
      _BUFF.GROUP:=_flda[1];
      _BUFF.NAME:=_flda[2];
      _BUFF.add();
      _BUFF.memo_set(_flda[3],'VALUE');
      _BUFF.memo_put(,'VALUE');
      obj_del(_flda)
   !};
   _fh.fclose()
?};
_BUFF


\save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Zapisuje do pliku zawartość bufora parametrów.
::   WE: _a [TABLE]  - Alias bufora.
::       _b [STRING] - Nazwa pliku.
::  OLD: \save/profile.fml
::----------------------------------------------------------------------------------------------------------------------
_fh:=fopen(_b,'w',1,,1);
{? _fh.is_open()
|| _sep:=%9;
   _loop:=_a.first();
   {!
   |? _loop
   |! _fh.fwrite('%2%1%3%1%4%1' [_sep,_a.GROUP,_a.NAME,_a.memo_txt(,1,'VALUE')]);
      _loop:=_a.next()
   !};
   _fh.fclose()
?}

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:04 d75418885d03233946456bb396f9c34f30708ffe8016ff2a50431c93eb631b2f9988a510fa9462d9dca3b13cecc360f6446d72c098f45402e4784332bf9e3f9829f341ec20027d256ce2e0036681baa5ad1a2c834ab29c4be244b1e00f8c20782b73ae3e8a56be655834fae9d1a2c83a5df92d9533f83168c51d7f17e2794788

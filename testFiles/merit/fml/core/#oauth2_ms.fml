:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #oauth2_ms.fml [22.26]
:: Utworzony: 10.01.2022
:: Autor: PD
::======================================================================================================================
:: Zawartość: - OAuth2 w obsłudze poczty elektronicznej - Microsoft
::======================================================================================================================


\tenant_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Identyfikator dzierżawcy (tenanta)
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',700030)


\auth_uri
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Uri autoryzacyjny
::----------------------------------------------------------------------------------------------------------------------
'https://login.microsoftonline.com/%1/oauth2/v2.0/authorize'[exec('tenant_id','#oauth2_ms')]


\token_uri
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Uri obsługujący token
::----------------------------------------------------------------------------------------------------------------------
'https://login.microsoftonline.com/%1/oauth2/v2.0/token'[exec('tenant_id','#oauth2_ms')]


\client_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Client_id
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',700031)


\client_secret
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Client_secret
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',700032)


\response_type
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Typ odpowiedzi
::----------------------------------------------------------------------------------------------------------------------
'code'


\response_mode
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Rodzaj odpowiedzi
::----------------------------------------------------------------------------------------------------------------------
'query'


\scope
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Zakres autoryzacji
::----------------------------------------------------------------------------------------------------------------------
'offline_access https://outlook.office.com/SMTP.Send'


\grant_type
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Typ uprawnienia
::----------------------------------------------------------------------------------------------------------------------
'authorization_code'


\is_verbose
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Czy włączone jest logowanie systemowe podczas komunikacji z serwisem OAuth
::----------------------------------------------------------------------------------------------------------------------
1


\oauth_code
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Zwraca kod autoryzacyjny dla aktywnej konfiguracji (albo pusty tekst w przypadku błędu)
::       Wyświetlane jest okno dialogowe w przeglądarce, w przypadku pozytywnej autoryzacji kod autoryzacyjny
::       odsyłany jest na adres zwrotny
::   WE: _a - obj_new - obiekt sniffera
::       _b - INTEGER - port
::       _b - STRING - kontekst dla sniffera
::       _c - STRING - adres przekierowania
::   WY: kod autoryzacyjny / ''
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_port:=_b;
_context:=_c;
_redirect_uri:=_d;

_auth_uri:=exec('auth_uri','#oauth2_ms');
_client_id:=exec('client_id','#oauth2_ms');
_response_type:=exec('response_type','#oauth2_ms');
_response_mode:=exec('response_mode','#oauth2_ms');
_scope:=exec('scope','#oauth2_ms');

_result:='';

{? _client_id=''
|| FUN.info('Brak danych konfiguracyjnych Microsoft w parametrach programu.'@);
   return(_result)
?};

_request:='';
_request+='?client_id=%1'[_client_id];
_request+='&response_type=%1'[_response_type];
_request+='&redirect_uri=%1'[_redirect_uri];
_request+='&response_mode=%1'[_response_mode];
_request+='&scope=%1'[_scope];

:: Obecnie zawsze w trybie przeglądarki wbudowanej
{? 1
||
:: Uruchomienie nasłuchu
   _obj.start(_port,_context);

:: Uruchomienie przeglądarki
   _win_acr:=SYSLOG.mk_ctr('','#ctrl_browser');
   SYSLOG.win_ctr(_win_acr);
   SYSLOG.win_cctr(_win_acr,'ctr_id','@webframe');
   _address:=_auth_uri+_request;
   params_set('request',_address);
:: Włączenie programu przechwytującego wywołanie zwrotne
   SYSLOG.control(,,"ctr_set(,'ctr_id','navigate',params_get().request)");

:: Odczyt wywołania zwrotnego
   _res:=_obj.get_result();

   {? _res<>''
   ||
::    Wydłubanie kodu
      _res:=spli_str(_res,'?')[2];
      _tab:=spli_str(_res,'&');
      {! _it:=1.. obj_len(_tab)
      |!
         {? 5+_tab[_it]='code='
         || _result:=gsub(_tab[_it],'code=','')
         ?}
      !}
   ?};
   _obj.stop();
   ~~
||
:: Włączenie programu przechwytującego wywołanie zwrotne
   exec('browser_launch','#help',_auth_uri+_request);
:: Oczekiwanie na wywołanie zwrotne {! |? !}
   ~~
?};

_result


\oauth_token
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Zwraca JSON z tokenem (i ew. tokenem odświeżającym) dla aktywnej konfiguracji
::       (albo JSON ze szczegółami błędu)
::   WE: [_a] - kod autoryzacyjny
::       [_b] - token odświeżający
::       [_c] - STRING - adres przekierowania
::   WY: obj_new('JSON','OK')
::----------------------------------------------------------------------------------------------------------------------
_token_uri:=exec('token_uri','#oauth2_ms');
_client_id:=exec('client_id','#oauth2_ms');
_response_type:=exec('response_type','#oauth2_ms');
_response_mode:=exec('response_mode','#oauth2_ms');
_scope:=exec('scope','#oauth2_ms');
_grant_type:=exec('grant_type','#oauth2_ms');
_client_secret:=exec('client_secret','#oauth2_ms');

_result:=obj_new('JSON','OK');
_result.JSON:=''; _result.OK:=0;

{? _client_id=''
|| _result.JSON:='{"error":"Brak danych konfiguracyjnych Microsoft w parametrach programu."}';
   return(_result)
?};

:: może być tylko jeden parametr
_code:={? var_pres('_a')=type_of('') || _a || '' ?};
_refresh_token:={? var_pres('_b')=type_of('') || _b || '' ?};

_redirect_uri:={? var_pres('_c')=type_of('') || _c || '' ?};

_inet:=inet_get(_token_uri);
{? type_of(_inet)>100
||
   _inet.append_header('Content-Type: application/x-www-form-urlencoded');

   {? exec('is_verbose','#oauth2_ms') || _inet.set_verbose('#oauth_ms.log') ?};

   _request:='';
   _request+='client_id=%1'[_inet.url_encode(_client_id)];
   _request+='&scope=%1'[_scope];
   _request+='&redirect_uri=%1'[_inet.url_encode(_redirect_uri)];
::   _request+='&grant_type=%1'[_inet.url_encode(_grant_type)];
   _request+='&grant_type=%1'[_inet.url_encode({? _code<>'' || 'authorization_code' || 'refresh_token' ?})];
   _request+='&client_secret=%1'[_inet.url_encode(_client_secret)];

   {? _code<>'' || _request+='&code=%1'[_inet.url_encode(_code)] ?};
   {? _refresh_token<>'' || _request+='&refresh_token=%1'[_inet.url_encode(_refresh_token)] ?};

   _utc_start:=utc_get();
   _status:=_inet.http_post(,_request);
   _utc_stop:=utc_get();

:: Zapis do rejestru zdarzeń - wykonywany obligatoryjnie
   _mwac_log_ref:=exec('mwac_log','#mwac',
      'OAUTH',
      _inet.get_url(),
      'POST',
      _inet.get_status(),
      _utc_start,
      _utc_stop,
      _request,
      _inet.get_data(),
      _inet.get_header(1),
      _inet.get_header(0)
   );
   exec('mwac_log_app','#mwac',_mwac_log_ref,'|OAUTH|MS|','');

   {? _status=200
   || _result.JSON:=_inet.get_data();
      _result.OK:=1
   || _result.JSON:=_inet.get_data();
      _result.OK:=0
   ?}
?};

_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:09 0072b4307da27d0bfb591489721036d99cec8530b2732b6fdce350217ba3489f4d2089aeab89edd12009d2c51b20a0350ad8a4745cecec8411aca08347e1d0a5f4571f726a5c27ed3dcc60035d2482e1e8720950c98b736517251a3e31fbda42666b5ef762dd34fb854d200aea569ca2bca7ec9e692ff253f62ac90163ae4dca

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #ntc.fml [23.25]
:: Utworzony: 21.12.2022
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa powiadomień ntc
::======================================================================================================================


\ntc_send_bi_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wysyła powiadomienie w kontekście pozycji na liście zadań
::       Kontekst wywołania: rekord BI_TODO
::----------------------------------------------------------------------------------------------------------------------
_user:=BI_TODO.USERS().KOD;

_app_ident:=exec('app_ident','#ntc');

_cluster_group:=app_info('cluster_group');

_title:={? BI_TODO.DESC='' || BI_TODO.BI_PREL().DESC || BI_TODO.DESC ?};

_prior_r:=BI_TODO.BI_PREL().PRIOR_R;
_obj:=obj_new('variant','icon','info');
_obj.variant:={? _prior_r=3 || 'danger' |? _prior_r=2 || 'warning' |? _prior_r=1 || 'success' || 'info' ?};
_obj.icon:={? BI_TODO.PRIOR_W>0>0 || 'warning2' || 'info-point' ?};
_obj.info:=
   {? BI_TODO.PRIOR_W>0
   || '<html>'
      '<span style="color: #DC1E28;">Zadanie o podwyższonym priorytecie własnym użytkownika</span>'
      '</html>'
   || ''
   ?};
_priority:=json_obj(_obj);

_btn1:='Uruchom';
_action1:="exec('start','#ntc',1)";

_btn2:='Lista zadań';
_action2:="exec('start','#ntc',2)";

_id:=ntc_send(
   _user,
   _app_ident,
   _cluster_group,
   _title,,
   BI_TODO.BI_PREL().uidref(),
   _priority,,,
   _btn1,_action1,
   _btn2,_action2,,,,,,,
   BI_TODO.uidref()
);
_id


\start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uruchamia system z poziomu powiadomienia ntc
::       W parametrze notice_param1 przekazane jest wskazanie na pozycję listy zadań
::   WE: _a - wariant uruchamiania:
::            [1] - uruchomione zostanie wskazane zadanie
::            2 - wyświetlona zostanie lista zadań w kontekście wskazanego zadania
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _variant:=_a || _variant:=1 ?};
_param1:=app_info('notice_param1');

{? type_of(_param1)=type_of('') & ref_tab(_param1)=BI_TODO
||
   exec('start_proces','b_proces',0);

   BI_TODO.prefix();
   {? BI_TODO.seek(_param1)
   || {? _variant=1
      || params_exec('todoRun','#bi_todo')
      |? _variant=2
      || exec('title','#bi_todo','Zadania'@);
         exec('select_user','#bi_todo',,0,BI_TODO.ref())
      || FUN.info('Błędny wariant %1 uruchamiania formuły %2.'@[$_variant,'\\start/#ntc.fml'])
      ?}
   || FUN.info('Nie znaleziono zadania.'@)
   ?}
?};
~~


\app_ident
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Identyfikator aplikacji używany do zaadresowania powiadomnienia
::----------------------------------------------------------------------------------------------------------------------
:: Jeżeli cluster.cfg jest prawidłowo zbudowany, to zostanie znaleziona nieukryta aplikacja w tej samej grupie
:: przypisana do tej samej firmy co kolejka
_tab:=AppList.my.tab;
_tab.cntx_psh();
_ndx:=_tab.ndx_tmp(,,'HIDDEN',,,'APP_PAR1',,);
_tab.index(_ndx);
_tab.prefix('N',REF.FIRMA().SYMBOL,);
{? _tab.first()
|| _app_ident:=_tab.APP_IDEN
|| _app_ident:=app_info('app_ident')
?};
_tab.ndx_drop(_ndx);
_tab.cntx_pop();
_app_ident


\variant_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Tabela z dostępnymi wariantami
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,'VARIANT','STRING[20]','Wariant'@);
_tab.VARIANT:='info'; _tab.add();
_tab.VARIANT:='success'; _tab.add();
_tab.VARIANT:='danger'; _tab.add();
_tab.VARIANT:='warning'; _tab.add();
_tab.VARIANT:='neutral'; _tab.add();
_tab


\variant_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wybór wariantu
::   WE: _a - wybrany wariant
::   WY: wybrany wariant / ~~ rezygnacja
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _variant:=_a || _variant:='info' ?};
_tab:=exec('variant_tab','#ntc');
_tab.find_key(_variant);
_wer:=_tab.mk_sel('Warianty'@,,,'variantntc',,,10,,'U');
_tab.win_fld(_wer,,'VARIANT',,,,,,'');
_tab.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
_tab.win_sel(_wer);
{? _tab.select(,1,5)
|| _tab.VARIANT
|| ~~
?}


\icon_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wybór ikony
::   WE: _a - wybrana ikona
::   WY: wybrana ikona / ~~ rezygnacja
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _icon:=_a || _icon:='' ?};
_prefix:='@system|';
_icon:=gsub(exec('ico_selttf','#icon','%1%2'[_prefix,_icon],'S'),_prefix,'');
{? _icon<>''
|| _icon
|| ~~
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:13:35 8d00341bd9b19ec867a1d10b8096c29feb5a6cd7f429a601115e74d4b7ceb957ded420f540792e5cfbd9258b6a610a42752d5c6c58f8c74dd86349e7743fde1605fc8b40cb20968014959fd510a42db3d26c9951b3c14639d0103978de34cc0e052450f3a4744f8f0793af623fa97ba2d208361d6a7493b26f744b368fc1f3b5

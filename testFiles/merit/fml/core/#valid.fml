:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: #valid.fml [17.00]
:: Utworzony: 18.12.2014
:: Autor: AK
::======================================================================================================================
:: Zawartość: Uniwersalne formuły sprawdzające poprawność wypełnienia pól
::======================================================================================================================


\chk_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Sprawdzanie po okienku, chk_rec()
::  OLD: \chk_rec/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
chk_rec()


\chk_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO
:: OPIS: sprawdzenie wypełnienie pola tabeli
::  OLD: \chk_fld/pola.fml
::----------------------------------------------------------------------------------------------------------------------
chk_fld()


\allDigit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: sprawdza czy podany ciąg znaków składa sie wyłacznie z cyfr, pomijając znak "-"
::       oraz pomijając zapis wykładniczy
::       innymi słowy czy podany ciąg znaków jest liczbą rzeczywistą w sensie: (-) cześć całkowita.cześć ułamkowa
::   WE: _a - ciąg znakow do sprawdzenia
::       [_b] - 1-czy liczba całkowita (0-domyślnie rzeczywista)
::   WY: 1-tylko cyfry 0-nie tylko
::  OLD: \alldigit/podstawy.fml
::----------------------------------------------------------------------------------------------------------------------
_a:=gsub(_a,',','.');
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};

_wyn:=0;
{? (-~_a)*'e'     || _wyn:=0
|? '.,-+'*form(_a) || _wyn:=0
|| {? fabs(#_a)>0 || _wyn:=1 || _wyn:=fabs(#(_a+'1'))>0 ?}
?};
{? _b & _wyn || _wyn:=~(_a*'.') ?};
_wyn


\ctrltypp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: kontrola typu pola NDG
::   WE: _a - typ pola
::       _b - wartość
::   WY: 1-jest OK 0-nie jest dobrze
::  OLD: \ctrltypp/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
no_msg(1);
errno;
{? _b=''
|| _wyn:=1
|? (1+_a)='N'
|| {? (1-_a)='+' || _b:=STR.gsub(_b,' ','x') || _b:=STR.gsub(_b,' ','') ?};
   _b:=STR.gsub(_b,',','.');
   _wyn:=exec('allDigit','#valid',_b)
|? _a='D'
|| {? _b='' || _b:='0000-00-00' ?};
   _yy:=4+_b;
   _mm:=2+(5-_b);
   _dd:=_b+2;
   _wyn:=((+_b)=10)
    & exec('allDigit','#valid',_yy,1)
    & exec('allDigit','#valid',_mm,1)
    & exec('allDigit','#valid',_dd,1)
    & {? ~(#_yy) || ~(#_mm) & ~(#_dd) || (#_mm)>0 & (#_dd)>0  ?};
   {? _wyn || date(#_yy,#_mm,#_dd) ?}
|? _a='G'
|| {? _b='' || _b:='00:00:00' ?};
   _nn:=_b;
   _wsk:=_nn*':';
   {? _wsk
   || _hh:=(_wsk-1)+_nn;
      _wyn:={? +_hh>2 || 0 || exec('allDigit','#valid',_hh,1) ?};
      _nn:=_wsk-_nn;
      _wsk:=_nn*':';
      {? _wyn
      || {? _wsk
         || _mm:=(_wsk-1)+_nn;
            _wyn:={? +_mm>2 || 0 || exec('allDigit','#valid',_mm,1) ?};
            {? _wyn
            || _nn:=_wsk-_nn;
               _ss:=_nn;
               _wyn:=_nn='' | {? +_nn>2 || 0 || exec('allDigit','#valid',_nn,1) ?}
            ?}
         || _mm:=_nn;
            _ss:='';
            _wyn:=_nn='' | {? +_nn>2 || 0 || exec('allDigit','#valid',_nn,1) ?}
         ?}
      || _mm:='';
         _ss:=''
      ?}
   || _wyn:=0
   ?};
   {? _wyn || time(#_hh,#_mm,#_ss) ?}
?};
{? _nr:=errno || _wyn:=($_nr)='0' ?};
no_msg(0);
_wyn


\allAlfaNum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: sprawdza czy podany ciąg znaków składa sie wyłącznie z cyfr i dużych liter
::   WE: _a - ciąg znakow do sprawdzenia
::   WY: 1-znaki alfanumeryczne 0-nie tylko
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_val:={? var_pres('_a')=type_of('') || _a || '' ?};
{? (+_val)>0
|| {!
   |? _ch:=1+_val;
      _nr:=%_ch;
      {? (_nr>=48 & _nr<=57) | (_nr>=65 & _nr<=90)
      || _val:=gsub(_val,_ch)
      || _res:=0
      ?};
      _res & (+_val)>0
   !}
?};

_res

:Sign Version 2.0 jowisz:1048 2023/06/23 14:13:35 c445270413de97cefc488913c3e2fae273a1e7d3b7a5400fdfc4350c27b51f39b75e33787eb7f79bf01f723ea255d740da5b06ecd5b5b633f895691a5f7a9416bdc64bd3ddd4ed084f2f09f6b668f2e4e58ce0be099b84cda1e6494850f77c56287b2c9b807ccda4b5c3d717522129022f62287cf2e7ffb9f4e29563ff5758f1

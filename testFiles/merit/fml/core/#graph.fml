:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #graph.fml [17.00]
:: Utworzony: 08.01.2015
:: Autor: AK
::======================================================================================================================
:: Zawartość: Uniwersalne formuły do obsługi diagramów, grafów
::======================================================================================================================


\dot_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.30]
:: OPIS: Tworzy obiekt do obsługi diagramów
::   WE: _a - nazwa pliku
::       [_b] - typ wyniku
::   WY: _diagram
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::  OLD: \dot_obj/diagram.fml
:: ~OST: INSYSEXEC,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
:: zmienne/formuły pomocnicze w deklaracji tablicy
_obj:=exec('obj_new','#var');
:: definicja _diagram
_obj.fld('file_name',_a   ,'nazwa pliku z diagramem');
_obj.fld('file_dir' ,     ,'nazwa folderu z diagramem');
_obj.fld('file_ext' ,'dot','rozszerzenie pliku z diagramem');
_obj.fld('file'     ,     ,'plik z diagramem');

_obj.fld('silent'   ,0    ,'wartości predefiniowane właściwości');
_obj.fld('display'  ,1    ,'wyświetlić wynik dot`a?');
_obj.fld('dot_cmd'  ,exec('get','#params',100900,2),'Scieżka uruchomieniowa do dot`a');
_obj.fld('out_kind' ,{? var_pres('_b')=2 || _b || 'jpg' ?},'typ generowanego wyjścia dla diagramu');
_obj.fld('out_dir'  ,     ,'folder dla wyjścia diagramu');
_obj.fld('debug'    ,0    ,'włączone mechanizmy kontrolne?');

_obj.fld('enum'     ,     ,'wartości predefiniowane właściwości');

_obj.meth('clean'   ,'Zeruje tablicę wartoscią ~~',"{! _pp:=1..obj_len(_a) |! _a[_pp]:=~~ !}");

_obj.meth('open'    ,'otwiera plik z diagramem do zapisu',"
:: sprawdzam czy podany dot_cmd rzeczywiście istnieje
   {? .dot_cmd<>'' & fexists(.dot_cmd,0)=1
   || {? .file_dir=~~ || .file_dir:=gsub(pth_dir(.file_name+'.'+.file_ext),'\\\\','/') ?};
      {? .out_dir=~~  || .out_dir:=gsub(pth_dir(.file_name+'.'+.out_kind),'\\\\','/') ?};
      .file:=fopen(.file_dir+'/'+.file_name+'.'+.file_ext,'Uw',0,0,1);
      .file.is_open
   |? .dot_cmd<>''
   || {? .silent=0
      || FUN.error('Proszę podać w parametrach działania systemu prawidłową ścieżkę do generatora diagramów.'@)
      ?};
      0
   || 0
   ?}
");

_obj.meth('close'     ,'zamyka plik z diagramem',"{? .file.is_open() || .file.fwrite('}'); .file.fclose() ?}");

_obj.meth('cr_graph'  ,'tworzy obiekt dla grafu',"
   _obj:=exec('obj_new','#var');
   _obj.fldv('bgcolor');
   _obj.fldv('fillcolor');
   _obj.fldv('fontcolor');
   _obj.fldv('fontname');
   _obj.fldv('fontsize');
   _obj.fldv('ratio');
   _obj.fldv('rank');
   _obj.fldv('rankdir');
   _obj.fldv('orientation');
   _obj.fldv('label');
   _obj.fldv('center');
   _obj.fldv('style');
   _obj.fldv('splines');
   _obj.fldv('comment');
   _obj.fldv('margin');
   _obj.new(~(var_pres('_a')=1 & _a=1))
");

_obj.meth('cr_sub'    ,'tworzy obiekt dla subgraph-u',"
   _obj:=exec('obj_new','#var');
   _obj.fldv('bgcolor');
   _obj.fldv('fillcolor');
   _obj.fldv('fontcolor');
   _obj.fldv('fontname');
   _obj.fldv('fontsize');
   _obj.fldv('ratio');
   _obj.fldv('rank');
   _obj.fldv('rankdir');
   _obj.fldv('orientation');
   _obj.fldv('label');
   _obj.fldv('center');
   _obj.fldv('style');
   _obj.fldv('splines');
   _obj.fldv('comment');
   _obj.fldv('margin');
   _obj.new(~(var_pres('_a')=1 & _a=1))
");

_obj.meth('cr_node'   ,'tworzy obiekt dla węzła',"
   _obj:=exec('obj_new','#var');
   _obj.fldv('color');
   _obj.fldv('fillcolor');
   _obj.fldv('fixedsize');
   _obj.fldv('fontcolor');
   _obj.fldv('fontname');
   _obj.fldv('fontsize');
   _obj.fldv('group');
   _obj.fldv('height');
   _obj.fldv('label');
   _obj.fldv('labelloc');
   _obj.fldv('orientation');
   _obj.fldv('shape');
   _obj.fldv('style');
   _obj.fldv('comment');
   _obj.fldv('width');
   _obj.fldv('image');
   _obj.fldv('margin');
   _obj.new(~(var_pres('_a')=1 & _a=1))
");

_obj.meth('cr_edge'   ,'tworzy obiekt dla krawędzi',"
   _obj:=exec('obj_new','#var');
   _obj.fldv('arrowhead');
   _obj.fldv('arrowsize');
   _obj.fldv('arrowtail');
   _obj.fldv('color');
   _obj.fldv('decorate');
   _obj.fldv('dir');
   _obj.fldv('fontcolor');
   _obj.fldv('fontname');
   _obj.fldv('fontsize');
   _obj.fldv('headlabel');
   _obj.fldv('headport');
   _obj.fldv('taillabel');
   _obj.fldv('tailport');
   _obj.fldv('label');
   _obj.fldv('style');
   _obj.fldv('weight');
   _obj.fldv('comment');
   _obj.new(~(var_pres('_a')=1 & _a=1))
");

_obj.meth('param2line','konwertuje dane w tablicy do lini',"
   _param:=_a;
   _props:=_b;
   _separator:={? var_pres('_c')=2 || _c || ',' ?};
   _line:='';
   {! _pp:=1..obj_len(_param)
   |! {? _param[_pp]<>~~
      || _value:={? type_of(_param[_pp])=1 || '='+form(_param[_pp],,,'9.')
                 |? type_of(_param[_pp])=2 || '='+_param[_pp]+''
                 |? type_of(_param[_pp])=3 || '=\"'+_param[_pp]+'\"'
                                           || ''
                 ?};
         _line+=_props[_pp]+_value+_separator
      ?}
   !};
   {? _line+(+_separator)=_separator || _line:=_line-(+_separator) ?};
   _line
");

_obj.meth('add_graph' ,'dodaje parametry grafu',"
   _id:={? type_of(_a)=3  || '\"'+_a+'\"' || _a ?};
   _result:=1;
   _param:={? var_pres('_b')>100 || _b || obj_new('none') ?};
   .file.fwrite('digraph '+_id+' {');
   _line:=.param2line(_param,.enum.graph,';');
   {? _line<>'' || .file.fwrite(_line+';') ?};
   ~~
");

_obj.meth('add_sub'   ,'dodaje subgraph',"
   _id:={? type_of(_a)=3  || '\"'+_a+'\"' || _a ?};
   _param:={? var_pres('_b')>100 || _b || obj_new('none') ?};
   .file.fwrite('subgraph '+_id+' {');
   _line:=.param2line(_param,.enum.graph,';');
   {? _line<>'' || .file.fwrite(_line+';') ?};
   ~~
");

_obj.meth('end_sub'   ,'koniec subgraph-u',"
   .file.fwrite('}');
   ~~
");

_obj.meth('add_node'  ,'dodaję węzeł',"
   _id:={? type_of(_a)=3  || '\"'+_a+'\"' || _a ?};
   _label:=~~;
   _result:=1;
   _param:=obj_new('label');
   {? var_pres('_b')=2   || _param.label:=_b
   |? var_pres('_b')>100 || obj_del(_param); _param:=_b
   ?};
   _line:=.param2line(_param,.enum.node);
   .file.fwrite(_id+{? _line='' || '' || ' ['+_line+']' ?})
");

_obj.meth('add_edge'  ,'dodaję krawedź',"
   _left:={? type_of(_a)=3  || '\"'+_a+'\"' || _a ?};
   _right:={? type_of(_b)=3  || '\"'+_b+'\"' || _b ?};
   _result:=1;
   _param:={? var_pres('_c')>100 || _c || obj_new('none') ?};

   _line:=.param2line(_param,.enum.edge);
   .file.fwrite(_left+' -> '+_right+{? _line='' || '' || ' ['+_line+']' ?})
");

_obj.meth('run'       ,'uruchamia dot`a i (opcjonalnie) wyświetla obrazek',"
   _file_out:=.file_name+'.'+.out_kind;
   _file_dot:=.file_name+'.'+.file_ext;
   _file_download:=_file_out;
   {? var_pres('_a')=type_of('')
   || _file_download:=_a
   ?};
   {? .file.is_open() || .close() ?};

   {? sys_name(1)='U_LINUX'
   || _cmd:='\"'+.dot_cmd+'\" -T'+.out_kind+' -o'+.out_dir+'/'+_file_out
                              +{? .debug || ' -v ' || '' ?}
                              +' -Kdot '+.file_ext+' '+.file_dir+'/'+_file_dot
::                              +{? .debug || ' > '+.file_dir+'/'+.file_name+'.log' || '' ?}
                              ;
      _system:=system(_cmd,1)

   || _cmd:='@QUOTE@'+.dot_cmd+'@QUOTE@ -T'+.out_kind+' -o'+.out_dir+'/'+_file_out
                              +{? .debug || ' -v ' || '' ?}
                              +' -Kdot '+.file_ext+' '+.file_dir+'/'+_file_dot
::                              +{? .debug || ' > '+.file_dir+'/'+.file_name+'.log' || '' ?}
                              ;
      _system:=system(pth_dir('startx.exe')+'\\\\startx.exe /NOUI /B /RETURNERROR \"'+_cmd+'\"',1)

   ?};

   {? _system>1
   || FUN.error('Aplikacja generatora diagramów zwróciła kod błędu #%1'
               '.\nNie można kontynuować generowania diagramu.'@[$_system]);
      0
   || {? .display
      || {? exec('cli_functions','#system')=0
         || dlg_save(_file_out,1,_file_download)
         || sys_exec(_file_out,,0)
         ?}
      ?};
      1
   ?}
");

_obj.meth('get'       ,'ściąga obrazek na terminal, zwraca lokalizację pliku',"
   _result:='';
   {? exec('cli_functions','#system')
   || _sour:=.out_dir+exec('sep','#file',1)+.file_name+'.'+.out_kind;
      _dest:='@'+tmp_dir()+'/'+.file_name+'.'+.out_kind;
      {? fcopy(_sour,_dest,0,0,1)
      || _result:=1-_dest
      ?}
   || FUN.emsg(exec('indevice_nacc_msg','#system'))
   ?};
   _result
");

_obj.meth('get_name_source'       ,'ściąga obrazek na terminal, zwraca lokalizację pliku',"
   _result:='';
   _result:=.out_dir+'/'+.file_name+'.'+.out_kind;
   _result
");

_diagram:=_obj.new();
_diagram.enum:=exec('enum','#graph',_diagram);
_diagram


\enum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.30]
:: OPIS: Obiekt typów wyliczeniowych
::   WE: _a - _diagram
::   WY: _enum
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystapić błąd.
::  OLD: \enum/diagram.fml
::----------------------------------------------------------------------------------------------------------------------
_diagram:=_a;
_obj:=exec('obj_new','#var');
:: === enum
_obj.clean();
_obj.fld('graph'  ,,'nazwy właściwości grafu'   );
_obj.fld('node'   ,,'nazwy właściwości węzła'   );
_obj.fld('edge'   ,,'nazwy właściwości krawędzi');
_obj.fld('color'  ,,'kolory');
_obj.fld('shape'  ,,'ksztalty');
_obj.fld('style'  ,,'style');
_obj.meth('setlabel','tworzy etykietę na podstawie parametrów - każdy parametr to osobny segment',"
   _result:='';
   {! __:=1.._ |! _result+='<f'+($__)+'>'+_[__]+'|' !};
   '\"'+{? _result+1='|' || _result-1 || _result ?}+'\"'
");
_enum:=_obj.new();

:: === enum - graph
_enum.graph:=_diagram.cr_graph(1);
:: === enum - node
_enum.node:=_diagram.cr_node(1);
:: === enum - edge
_enum.edge:=_diagram.cr_edge(1);

:: === enum - shape
_obj.clean();
_obj.fldv('ellipse');
_obj.fldv('box');
_obj.fldv('circle');
_obj.fldv('doublecircle');
_obj.fldv('record');
_obj.fldv('Mrecord');
_obj.fldv('plaintext');
_obj.fldv('polygon');
_obj.fldv('diamond');
_enum.shape:=_obj.new();

:: === enum - style
_obj.clean();
_obj.fldv('solid');
_obj.fldv('dashed');
_obj.fldv('dotted');
_obj.fldv('bold');
_obj.fldv('rounded');
_obj.fldv('diagonals');
_obj.fldv('filled');
_obj.fldv('striped');
_obj.fldv('wedged');
_obj.fldv('invis');
_enum.style:=_obj.new();

:: === enum - color
_obj.clean();
_obj.fld('black'      ,"#000000");
_obj.fld('white'      ,"#FFFFFF");
_obj.fld('red'        ,"#FF0000");
_obj.fld('tomato'     ,"#FF6347");
_obj.fld('lightpink'  ,"#FFB6C1");
_obj.fld('yellow'     ,"#FFFF00");
_obj.fld('wheat'      ,"#F5DEB3");
_obj.fld('ivory'      ,"#FFFFF0");
_obj.fld('lime'       ,"#00FF00");
_obj.fld('aqua'       ,"#00FFFF");
_obj.fld('cyan'       ,"#00FFFF");
_obj.fld('lightcyan'  ,"#E0FFFF");
_obj.fld('blue'       ,"#0000FF");
_obj.fld('lightblue'  ,"#ADD8E6");
_obj.fld('fuchsia'    ,"#FF00FF");
_obj.fld('orange'     ,"#FFA500");
_obj.fld('gray'       ,"#808080");
_obj.fld('silver'     ,"#C0C0C0");
_obj.fld('maroon'     ,"#800000");
_obj.fld('olive'      ,"#808000");
_obj.fld('green'      ,"#008000");
_obj.fld('teal'       ,"#008080");
_obj.fld('navy'       ,"#000080");
_obj.fld('purple'     ,"#800080");
_obj.fld('transparent',"transparent");

_obj.meth('other_fg',,"_color:=Color.rekprzed(_a); Color.chng_kol(((_color*',')-1)+_color))");
_obj.meth('other_bg',,"_color:=Color.rekprzed(_a); Color.chng_kol(((_color*',')-_color))");

_enum.color:=_obj.new();
:: zwracamy obiekt wyliczeniowy
_enum


\image
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Pełna nazwa pliku (obrazka) ze scieżką zapisana w ""
::   WE: _a - nazwa pliku
::  OLD: \image/diagram.fml
::----------------------------------------------------------------------------------------------------------------------
'"'+pth_dir(_a)+'\\'+_a+'"'


\test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [17.00]
:: OPIS: Formuła testowa dla diagramów, grafów
::  OLD: \test/diagram.fml
::----------------------------------------------------------------------------------------------------------------------
_dia:=exec('dot_obj','#graph','test');
_dia.out_kind:='jpg';
_dia.debug:=1;
{? _dia.open()
|| _graph:=_dia.cr_graph();
   _node:=_dia.cr_node();
   _edge:=_dia.cr_edge();

   _graph.rankdir:='LR';
   _graph.fontsize:='20.0';
   _graph.fontname:='Arial';
   _graph.bgcolor:=_dia.enum.color.aqua;
   _dia.add_graph('G',_graph);

   _node.shape:=_dia.enum.shape.record;
   _node.color:=_dia.enum.color.red;
   _node.fontcolor:=_dia.enum.color.gray;
   _dia.add_node('node',_node);

   exec('array_blank','#array',_node,~~);
   _node.label:='"Testowy\\n Węzeł1"';
   _dia.add_node('"test1"',_node);

   exec('array_blank','#array',_node,~~);
   _node.label:=_dia.enum.setlabel('testowa etykieta','druga etykieta','trzecia etykieta');
   _dia.add_node('"uid"',_node);

   _edge.label:="Etykieta";
   _dia.add_edge('"uid"','"test1"',_edge);

   _dia.run()
?};
~~


\gen_tabline
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2010]
:: OPIS: Generuje linie tabeli HTML na podstawie ciągu parametrów
::       Funkcja pomocnicza do generowania etykiet HTML
::   WE: trójki parametrów: treść,pozycjonowanie['l','r','c'],kolor
::   WY: string <TR><TD>...</TD></TR>, jeśli coś pójdzie nie tak zwraca ''
::  OLD: \gen_tabline/wsp_mag.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
{? _>0
||
   {? _%*3>0
   ||
      FUN.emsg('Błąd generowania kolumny tabeli.'@);
      return('')
   ?};
   {! _loo:=1.._/3
   |!
      _poz:=_loo*3-2;
      _align:={? type_of(_[_poz+1])=type_of(' ')
              ||
                 {? -_[_poz+1]='l'
                 || 'LEFT'
                 |? -_[_poz+1]='r'
                 || 'RIGHT'
                 || 'CENTER'
                 ?}
              || 'CENTER'
              ?};
      _bgcolor:={? type_of(_[_poz+2])=type_of(' ')
                ||
                   {? +_[_poz+2]<3
                   || 'white'
                   |? 1+_[_poz+2]='#' & +_[_poz+2]<>7
                   || 'white'
                   || _[_poz+2]
                   ?}
                || 'white'
                ?};
      _ret+='<TD ALIGN="'+_align+'" BGCOLOR="'+_bgcolor+'">'
          +form(_[_poz])
          +'</TD>\n'
   !}
?};
{? +_ret || _ret:='<TR>'+_ret+'</TR>' || '' ?};
return(_ret)


\gen_tabline4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2010]
:: OPIS: Generuje linie tabeli HTML na podstawie ciągu parametrów
::       Funkcja pomocnicza do generowania etykiet HTML
::   WE: czwórki parametrów: treść,pozycjonowanie['l','r','c'],kolor,colspan
::   WY: string <TR><TD>...</TD></TR>, jeśli coś pójdzie nie tak zwraca ''
::  OLD: \gen_tabline4/wsp_mag.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
{? _>0
||
   {? _%*4>0
   ||
      FUN.emsg('Błąd generowania kolumny tabeli.'@);
      return('')
   ?};
   {! _loo:=1.._/4
   |!
      _poz:=_loo*4-3;
      _align:={? type_of(_[_poz+1])=type_of(' ')
              ||
                 {? -_[_poz+1]='l'
                 || 'LEFT'
                 |? -_[_poz+1]='r'
                 || 'RIGHT'
                 || 'CENTER'
                 ?}
              || 'CENTER'
              ?};
      _bgcolor:={? type_of(_[_poz+2])=type_of(' ')
                ||
                   {? +_[_poz+2]<3
                   || 'white'
                   |? 1+_[_poz+2]='#' & +_[_poz+2]<>7
                   || 'white'
                   || _[_poz+2]
                   ?}
                || 'white'
                ?};
      _colspan:={? type_of(_[_poz+3])=type_of(1)
                ||
                   {? _[_poz+3]>0
                   || form(_[_poz+3],1,0)
                   || ''
                   ?}
                || ''
                ?};
      _ret+='<TD ALIGN="'+_align
          +'" BGCOLOR="'+_bgcolor
          +{? +_colspan>0 || '" COLSPAN="'+_colspan || '' ?}
          +'">'
          +form(_[_poz])
          +'</TD>\n'
   !}
?};
{? +_ret || _ret:='<TR>'+_ret+'</TR>' || '' ?};
return(_ret)


\txt_just
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2010]
:: OPIS: Justyfikacja tekstu. Wstawia łamianie linii w miarę możliwosci
::       jak najzgodniej z wytyczną co do ilości znaków podanej w _b
::       Dodatkowo poprawia odstępy po kropkach i przecinkach
::       Funkcja pomocnicza do generowania etykiet.
::   WE: _a - tekst
::       _b - co ile znaków zawijać
::       _c=0 zawijać znakiem \n _c=1 wstawiać znaki zawinięcia \n (\\n)
::   WY: zawinięty tekst - w miejscach zawinięcia wpisane \n - przydatne np. do choice
::       Nie pozwala na pozostawienie jednoliterowego wyrazu na końcu linii.
::  OLD: \txt_just/wsp_mag.fml
::----------------------------------------------------------------------------------------------------------------------

_gchr:="(_b+_a)+1";
{? _<1 || return(_a) ?};
{? _<2 || return(_a) ?};
{? +_a=0 || return(_a) ?};
{? _<3 || _c:=0 ?};
_br:={? _c>0 || '\\n' || '\n'?};
:: poprawienie kropek i przecinków
:: ustawienie znaku po pojedynczych wyrazach
_a:=exec('txt_fixsingle','#graph',exec('txt_redot','#graph',_a),1);
_alen:=+_a;
:: limit znaków nie może byc bezsensowny i ciąg nie może być mniejszy(równy) w stosunku do limitu
{? _b<4 | _alen<=_b || return(_a) ?};
STR.split(_a,' ');
_len:=0;
_res:='';
_ress:='';
{! |?
   _len:=+_res;
   _word:=STR.get_word();
   _wlen:=+_word;

   {? _wlen>=_b
:: jeśli przekroczy
   ||
      _ress+={? +_res>0 || _res+_br || '' ?}+_word+_br;
      _res:=''
:: jeśli docelowo przekroczy
   |? (_len+_wlen)-_b>(_b-_len)
:: jeśli suma za długa
   ||
      _ress+=_res+_word+_br;
      _res:=''
   ||
:: w przeciwnym wypadku
      _res+=_word+' '
   ?};
   _wlen>0
!};
{? +_res>0
||
   _ress+=_res
?};
:: zdjęcie znaków po pojedynczych wyrazach
_ress:=exec('txt_fixsingle','#graph',_ress,2);
return(_ress)


\txt_fixsingle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2010]
:: OPIS: Funkcja znakująca (zdejmująca oznaczenie) spacje po wyrazie jednoliterowym
::       Funkcja pomocnicza do generowania etykiet.
::   WE: _a - tekst
::       _b=1 - zamiana spacji na znak specjalny,
::       _b=2 - zamiana znaku specjalnego na spację
::   WY: tekst
::  OLD: \txt_fixsingle/wsp_mag.fml
::----------------------------------------------------------------------------------------------------------------------

_spec:=%7;
_gchr:="(_b+_a)+1";
{? _<2 || return('') ?};
_alen:=+_a;
{? _alen<6 || return(_a) ?};

_re:='';
{! _lo:=1.._alen
|!
   _chp1:=_gchr(_a,_lo-1);
   _chp2:=_gchr(_a,_lo-2);
   _chn1:=_gchr(_a,_lo+1);
   _chn2:=_gchr(_a,_lo+2);
   _ch:=_gchr(_a,_lo);
   {? (_lo>=3) | (_lo<=(_alen-2))
   ||
      {? (_b=2) & (_ch=_spec)
      ||
         _re+=' '
      |? (_b=1) & (_chp1<>' ') & (_chn1<>' ') & (_ch=' ') & (_chn2<>' ') & (_chp2=' ')
      ||
         _re+=_spec
      ||
         _re+=_ch
      ?}
   ?}
!};
return(_re)


\txt_redot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RS [2010]
:: OPIS: Wstawia spacje po kropkach i przecinkach w zdaniach jeśli ich nie ma.
::       Funkcja pomocnicza do generowania etykiet.
::   WE: _a - tekst
::   WY: tekst z poprawionymi kropkami i przecinkami
::  OLD: \txt_redot/wsp_mag.fml
::----------------------------------------------------------------------------------------------------------------------
_gchr:="(_b+_a)+1";
{? _<1 || return('') ?};
_alen:=+_a;
{? _alen<2 || return(_a) ?};

_re:='';
{! _lo:=1.._alen
|!
   _ch:=_gchr(_a,_lo);
   _re+=_ch;
   {? _lo<_alen
   ||
      _ch1:=_gchr(_a,_lo+1);
      {? (_ch='.' & _ch1<>' ' & _ch1<>'.') | (_ch=',' & _ch1<>' ')
      ||
         _re+=' '
      ?}
   ?}
!};
return(_re)


\run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ATA [2009]
:: OPIS: Uruchamia zewnętrzny program generujący plik jpg, który przedstawia diagram powiązań pomiędzy zadaniami
::       Jako argumenty należy podać nazwy plików. W przypadku, gdy argumenty nie zostaną podane, przyjęte zostaną
::       nazwy dot1.jpg i dot1.dot
::   WE: [_a] - nazwa pliku do ściągnięcia na terminal
::       [_b] - nazwa pliku dot. Nazwa ta musi być taka sama jak podana dla funkcji open
::       [_c] - 0 / 1 - czy wyświetlać obrazek (domyślnie tak = 1)
::       [_d] - typ pliku wynikowego [jpg]
::   WY: 0 / 1
::  OLD: \run/skid_har.fml
:: ~OST: INSYSEXEC
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(' ') || _a:='dot1' ?};
_name_download:=_a;

{? var_pres('_b')<>type_of(' ') || _b:='dot1' ?};
_name:=_b;
_name_dot:=_name+'.dot';
_pth_dot:=pth_dir(_name_dot);

{? var_pres('_c')<>type_of(0) || _display:=1 || _display:=_c ?};

{? var_pres('_d')=type_of('') || _ext:=_d || _ext:='jpg' ?};

_name_jpg:=_name+'.'+_ext;
_pth_jpg:=pth_dir(_name_jpg);

_path_run:=exec('get','#params',100900,2);

{? _path_run<>''
|| {? fexists(_path_run,0)
   ||
      no_msg(1);
      {? sys_name(1)='WINDOWS'
      || _system:=system(_path_run+' -T'+_ext+' -o'+_pth_jpg+'\\'+_name_jpg+' -Kdot '+_pth_dot+'\\'+_name_dot,1)
      || _system:=system(_path_run+' -T'+_ext+' -o'+_pth_jpg+'/'+_name_jpg+' -Kdot '+_pth_dot+'/'+_name_dot,1)
      ?};
      no_msg(0);
      {? _system>1
      || FUN.emsg('Aplikacja generatora diagramów zwróciła kod błędu #%1'
                  '.\nNie można kontynuować generowania diagramu.'@[$_system]);
         0
      || {? _display
         || {? exec('interm','#system')
            || dlg_save(_name_jpg,1,_name_download)
            || sys_exec(_name_jpg,,0)
            ?}
         ?};
         1
      ?}
   ||
      {? _display
      || FUN.emsg('Proszę podać w parametrach działania systemu prawidłową ścieżkę do generatora diagramów.'@)
      ?};
      0
   ?}
|| {? _display
   || FUN.emsg('Proszę podać w parametrach działania systemu ścieżkę do generatora diagramów.'@)
   ?};
   0
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:09 66e2b4bf8e76764a19776f4171e6e27766f3fd9ab5a3560458cf4c00eb8773f3dd83d0b0d24ee33557bae418779df063c47094fb1b285a202ded636817cf896bdefd814e86938a740dc366d1b162613543f46c691425a9c7d97c74b26978b764675e8075337cf003a01332a8876eb421754419f8389f407aadae82b721b2245a

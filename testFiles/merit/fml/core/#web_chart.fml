:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #web_chart.fml
:: Utworzony: 29.09.2016 [17.00]
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Obsługa wykresów dla webTerma
::======================================================================================================================

\declare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Funkcja deklarująca obiekt do obsługi wykresów
::   WE:
::   WY: instancja obiektu
::----------------------------------------------------------------------------------------------------------------------
::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablice i komentować poszczególne jej elementy
_fld:="31+form(_a)";
_mth:="31+form(_a)";
::tworze tablice nazwana do przechowywania zmiennych
_chart:=obj_new(
                _fld('TabData'      ,'tabela z danymi wykresu')
               ,_fld('TabText'      ,'tabela z danymi wykresu')
               ,_fld('win_id'       ,'identyfikator okna')
               ,_fld('search_down'  ,'czy przeszukiwać w dół')
               ,_fld('type'         ,'typ wykresu')
               ,_fld('jm'           ,'jednostka miary')
               ,_fld('width'        ,'szerokość wykresu')
               ,_fld('height'       ,'wysokość wykresu')
               ,_fld('donut'        ,'rozmiar dziurki')
               ,_fld('title'        ,'tytuł na wydruku')

::             metody <PRYWATNE>
               ,_mth('init'         ,'inicjuje obiekt')
               ,_mth('initTabData'  ,'inicjuje tabelę z danymi')
               ,_mth('initTabText'  ,'inicjuje tabelę z tekstami')
               ,_mth('clear'        ,'czyści obiekt z informacji o wykresie')
               ,_mth('sendData'     ,'wysyła dane z tabeli do kontrolki')
               ,_mth('sendText'     ,'wysyła teksty do kontrolki')
               ,_mth('sendTitle'    ,'wysyła tytuł wydruku do kontrolki')

::             metody <PUBLICZNE>
               ,_mth('addText'      ,'dodaje text do umieszczenia na wykresie')
               ,_mth('addVal'       ,'dodaje pojedynczą parę klucz-wartość do danych')
               ,_mth('delVal'       ,'usuwa pojedynczą parę klucz-wartość z danych')
               ,_mth('setJM'        ,'ustawia jednostkę miary pokazywaną na wykresie')
               ,_mth('setWidth'     ,'ustawia szerokość wykresu')
               ,_mth('setHeight'    ,'ustawia wysokość wykresu')
               ,_mth('setDonut'     ,'ustawia dziurkę na wykresie dla typu "pie"')
               ,_mth('setTitle'     ,'ustawia tytuł wydruku')
               ,_mth('eraseData'    ,'usuwa wszystkie dane')
               ,_mth('show'         ,'pokazuje/tworzy wykres w oknie kontrolki')
               ,_mth('reload'       ,'odświeża/przeładowuje dane wykresu')
             );
::usuwamy zbedne zmienne
&_mth; &_fld;

_chart.win_id:='';
_chart.search_down:='';
_chart.type:='';
_chart.jm:='';
_chart.width:=0;
_chart.height:=0;
_chart.donut:=0;
_chart.title:='';

:: metody <PRYWATNE> ---------------------------------------------------------------------------------------------------
_chart.init:="
        _win_id:={? var_pres('_a')=type_of('') || _a || return(0) ?};
   _search_down:={? var_pres('_b')=type_of(0)  || _b || 0         ?};
          _type:={? var_pres('_c')=type_of('') || _c || 'pie'     ?};

   .win_id:=_win_id;
   .search_down:=_search_down;
   .type:=_type;

   .initTabData();
   .initTabText()
";

_chart.initTabData:="
   {? var_pres('TabData',.)>100 || obj_del(.TabData) ?};
   .TabData:=tab_tmp(1,
      'LP','INTEGER','lp',
      'KEY','STRING[255]','key',
      'VALUE','STRING[255]','value',
      'RVAL','REAL','real value'
   )
";

_chart.initTabText:="
   {? var_pres('TabText',.)>100 || obj_del(.TabText) ?};
   .TabText:=tab_tmp(2,
      'TYPE','STRING[8]','type',
      'TEXT','STRING[255]','text',
      'X','INTEGER','x',
      'Y','INTEGER','y',
      'FONT','STRING[255]','font'
   )
";

_chart.sendData:="
   web_ctrl_set_obj(.win_id,,'des','noData','brak danych'@,'printBtn','Drukuj'@);
   web_ctrl_set_obj(.win_id,,'cfgPie','jm',.jm,'width',$.width,'height',$.height,'donut',$.donut);
   web_ctrl_set_table(.win_id,,'storeData',.TabData)
";

_chart.sendText:="
   web_ctrl_set_table(.win_id,,'storeText',.TabText)
";

_chart.sendTitle:="
   web_ctrl_call(.win_id,,'setTitle', .title)
";

_chart.clear:="
   web_ctrl_call(.win_id,,'clear')
";

:: metody <PUBLICZNE> --------------------------------------------------------------------------------------------------
_chart.addText:="
    _text:={? var_pres('_a')=type_of('') || _a || '' ?};
   _pos_x:={? var_pres('_b')=type_of(0)  || _b || 0  ?};
   _pos_y:={? var_pres('_c')=type_of(0)  || _c || 0  ?};
    _font:={? var_pres('_d')=type_of('') || _d || '' ?};

   .TabText.blank(1);
   .TabText.TYPE:='text';
   .TabText.TEXT:=_text;
   .TabText.X:=_pos_x;
   .TabText.Y:=_pos_y;
   .TabText.FONT:=_font;

   {? .TabText.add()
   || .TabText.ref()
   || 0
   ?}

";

_chart.addVal:="
   _key:={? var_pres('_a')=type_of('') || _a || return(0) ?};
   _val:={? var_pres('_b')=type_of(0)  || _b || #_b ?};

   _lp:={? .TabData.last() || .TabData.LP+1 || 1 ?};
   .TabData.blank(1);
   .TabData.LP:=_lp;
   .TabData.KEY:=_key;
   .TabData.RVAL:=_val;
   .TabData.VALUE:=form(_val$2,,,'9.');
   {? .TabData.add()
   || .TabData.ref()
   || 0
   ?}
";

_chart.delVal:="
   _ref:={? var_pres('_a')=type_of(null()) || _a || return(0) ?};
   {? .TabData.seek(_ref,) || .TabData.del(,1) || 0 ?}
";

_chart.setJM:="
   .jm:={? var_pres('_a')=type_of('') || _a || '' ?}
";

_chart.setWidth:="
   .width:={? var_pres('_a')=type_of(0) || _a || 0 ?}
";

_chart.setHeight:="
   .height:={? var_pres('_a')=type_of(0) || _a || 0 ?}
";

_chart.setDonut:="
   .donut:={? var_pres('_a')=type_of(0) || _a || 0 ?}
";

_chart.setTitle:="
   .title:={? var_pres('_a')=type_of('') || _a || '' ?}
";

_chart.eraseData:="
   .TabData.erase()
";

_chart.show:="
   _height:=0;
   .clear();
   .sendData();
   .sendText();
   .sendTitle();
   {? .type='pie'
   || web_ctrl_call(.win_id,.search_down,'createPie')
   ?}
";

_chart.reload:="
   .sendData();
   web_ctrl_call(.win_id,,'loadData')
";

_chart.init();
_chart

:Sign Version 2.0 jowisz:1045 2020/04/03 17:05:32 dfa3d8482fbf52e523fcea1d4c7bc9c26aad34164cdf750ec4b4f32fe7f931fd2bfdc3ce42d1543f041160603a7e17c36511133f7f3af43a5a81e52a3ba9e5366539eb15e0197db3d424618a41744b1f24435e81a7f400fe0837886cc935c36e6c07e65ed882717bb8a6de0d2d7296d08cbd6baf3167cf54c786c9b06dc0547c

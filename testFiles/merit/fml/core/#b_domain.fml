:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #b_domain.fml [17.00]
:: Utworzony: 18.12.2014
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Formuły do obsługi tabeli B_DOMAIN
::======================================================================================================================


\buffer
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [17.00]
:: OPIS: Tworzy bufor tabeli B_DOMAIN
::   WY: obj_new() - tablica nazwana - reprezentacją rekordu B_DOMAIN
::----------------------------------------------------------------------------------------------------------------------
exec('B_DOMAIN','#buffer')


\lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [17.00]
:: OPIS: Sprawdza, czy dziedzina jest licencjonowana.
::   WE: _a [STRING/REFERENCE] - Kod lub wskazanie dziedziny.
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
:: Jeżeli transakcja zerwana
{? do_state()=2 || return(0) ?};
_dom:='';

_ret:=0;

{? var_pres('_a')=type_of('')
|| _dom:=_a
|? var_pres('_a')=type_of(null()) & _a<>null() & ref_tab(_a)=B_DOMAIN
|| B_DOMAIN.cntx_psh();
   B_DOMAIN.prefix();
   {? B_DOMAIN.seek(_a)
   || _dom:=B_DOMAIN.SYMBOL
   ?};
   B_DOMAIN.cntx_pop()
?};

{? _dom<>''
|| exec('proenv','#b_proman');
   _domain:=__proenv.DOM_LIC;
   {? var_pres('_domain')>100
   || _domain.prefix(_dom,);
      _ret:=_domain.first()
   ?}
?};

_ret


\lic_or
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.42]
:: OPIS: Sprawdza, czy któraś z dziedzin jest licencjonowana.
::   WE: _a [STRING] - kod dziedziny
::       [..] [STRING] - kod dziedziny
::   WY: 1 - którakolwiek z podanych licencji jest dostępna
::       0 - żadna z podanych licencji nie jest dostępna
::UWAGA: poprawność argumentów wywołania nie jest weryfikowana
::----------------------------------------------------------------------------------------------------------------------
exec('proenv','#b_proman');
_domain:=__proenv.DOM_LIC;
{? type_of(_domain)<>type_of(SYSLOG)
|| return(0)
?};

_domain.prefix();
{! _arg:=1.._
|! {? _domain.find_key(_[_arg],)
   || return(1)
   ?}
!};
0


\b_dom_kolor_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [17.00]
:: OPIS: Przed wyświetleniem pola B_DOMAIN.COLOR
::----------------------------------------------------------------------------------------------------------------------
exec('hex2dec','#color',B_DOMAIN.COLOR)+','+exec('hex2dec','#color',B_DOMAIN.COLOR)


\domain_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [17.00]
:: OPIS: Zwraca DOMAIN.ref() na podstawie podanego kodu
::   WE: _a - Symbol obszaru
::   WY:  B_DOMAIN.ref()
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
B_DOMAIN.cntx_psh();
B_DOMAIN.index('SYMBOL');
B_DOMAIN.prefix(_a,);
{? B_DOMAIN.first() || _result:=B_DOMAIN.ref() ?};
B_DOMAIN.cntx_pop();
_result


\domain_megad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.00]
:: OPIS: Zwraca nazwę megadziedziny na podstawie podanego symbolu obszary
::   WE: _a - B_DOMAIN.ref
::       [_b] - INTEGER - 0/[1] - komunikaty
::   WY: nazwa megadzieniny
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_b_domain:=_a;
_komm:=1;
{? var_pres('_b')=type_of(0)
|| _komm:=_b
?};

_colors:=exec('mega_colors','#b_domain');
_names:=exec('mega_names','#b_domain');
B_DOMAIN.cntx_psh(); B_DOMAIN.prefix();
{? B_DOMAIN.seek(_b_domain)
|| {? B_DOMAIN.COLOR=_colors.FINANSE
   || _result:=_names.FINANSE
   |? B_DOMAIN.COLOR=_colors.PERSONEL
   || _result:=_names.PERSONEL
   |? B_DOMAIN.COLOR=_colors.LOG_PROD
   || _result:=_names.LOG_PROD
   |? B_DOMAIN.COLOR=_colors.ZARZ
   || _result:=_names.ZARZ
   |? B_DOMAIN.COLOR=_colors.ZWS | B_DOMAIN.COLOR=''
   ||
::    ZWS nie należy do żadnej megadziedziny
      _result:=''
   || {? _komm>0
      || FUN.wdrerror('Błąd w funkcji domain_megad - kolor dziedziny się rozsynchronizował z Builderem')
      ?}
   ?}
?};
B_DOMAIN.cntx_pop();
::_wyn:='';
:::: Finanse
::{? _a='FKS' | _a='FST' | _a='HBN' | _a='KAS'
::||
::   _wyn:='Finanse'
:::: Personel
::|? _a='PKD' | _a='PPL' | _a='PKW' | _a='PRC' | _a='POC' | _a='PBA' | _a='PSZ'
::||
::   _wyn:='Personel'
::::  Logistyka i Produkcja
::|? _a='LMG' | _a='LMO' | _a='LSP' | _a='LSS' | _a='LZK' | _a='TPP' | _a='TTE' | _a='WYP'
::||
::   _wyn:='Logistyka i produkcja'
::::   to-do Zarządzanie
::||
::   _wyn:='Zarządzanie'
::?};
::_wyn
_result


\mega_colors
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Zwraca kolory dla megadziedzin
::   WY: obj_new - tablica nazwana
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=obj_new('FINANSE','PERSONEL','LOG_PROD','ZARZ','ZWS');
_result.FINANSE:='21aaaa';
_result.PERSONEL:='a954a5';
_result.LOG_PROD:='389848';
_result.ZARZ:='f8982a';
_result.ZWS:='1470B2';
_result


\mega_names
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Zwraca nazwy dla megadziedzin
::   WY: obj_new - tablica nazwana
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=obj_new('FINANSE','PERSONEL','LOG_PROD','ZARZ','ZWS');
_result.FINANSE:='Finanse';
_result.PERSONEL:='Personel';
_result.LOG_PROD:='Logistyka i produkcja';
_result.ZARZ:='Zarządzanie';
_result.ZWS:='Inne';
_result


\mega_names_tra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Zwraca nazwy dla megadziedzin przetłumaczone
::   WY: obj_new - tablica nazwana
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=obj_new('FINANSE','PERSONEL','LOG_PROD','ZARZ','ZWS');
_result.FINANSE:='Finanse'@;
_result.PERSONEL:='Personel'@;
_result.LOG_PROD:='Logistyka i produkcja'@;
_result.ZARZ:='Zarządzanie'@;
_result.ZWS:='Inne'@;
_result


\wybierz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła obsługująca wybór dziedziny.
::       Formuła może być wykorzystywana jako argument atrybutu 'fml_val' parametru czynności.
::   WE: [_a] [STRING] - Wartość początkowa w oknie, wartość na której będzie ustawiony kursor.
::   WY: Tablica nazwana - reprezentacja rekordu B_DOMAIN.
::----------------------------------------------------------------------------------------------------------------------
_buf:=exec('buffer','#b_domain');
B_DOMAIN.cntx_psh();
B_DOMAIN.index('SYMBOL');
B_DOMAIN.prefix();
B_DOMAIN.win_edit();
B_DOMAIN.win_patt();
B_DOMAIN.win_sel('WYB');
_jest:=var_pres('_a')=type_of('') & B_DOMAIN.find_key(_a,);
{? _jest
|| _buf.get()
?};
{? B_DOMAIN.select(,1,-1)
|| _buf.get()
|? ~_jest
|| _buf.blank()
?};
B_DOMAIN.cntx_pop();
_buf


\wybierz_wiele
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Formuła obsługująca wybór wielu dziedziny.
::   WE:
::   WY: alias tabeli tymczasowej zawierającej wskazania wybranych dziedzin (kolumna REF).
::----------------------------------------------------------------------------------------------------------------------
_BUF:=tab_tmp(1,
   'NUM','INTEGER',,
   'REF','STRING[16]',
);

_wnd:=B_DOMAIN.mk_sel('Obszary'@,'P',0,'#b_domain_wyb_w',,,,,'U');
B_DOMAIN.win_fld(_wnd,,'SYMBOL',,,-3,,,,,MS.comment(B_DOMAIN,'SYMBOL'));
B_DOMAIN.win_fld(_wnd,,'NAME',,,-30,,,,,MS.comment(B_DOMAIN,'NAME'));
B_DOMAIN.win_act(_wnd,,'Formuła','Wybierz'@,,,,
   "  _BUF:=params_get().BUF;
      _BUF.REF:=$B_DOMAIN.ref();
      _BUF.NUM+=1;
      _BUF.add();
      {? B_DOMAIN.sel_size()=0
      || sel_exit()
      ?}
   ",
   1,1,,
   "sel_exit()"
);
B_DOMAIN.win_act(_wnd,,'Szukaj');
B_DOMAIN.win_act(_wnd,,'Kolejność');

B_DOMAIN.cntx_psh();
B_DOMAIN.win_sel(_wnd);
params_set('BUF',_BUF);
B_DOMAIN.select();
B_DOMAIN.cntx_pop();
B_DOMAIN.win_del(_wnd);

_BUF


\name
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zwraca nazwę domeny
::   WE: _a - symbol
::----------------------------------------------------------------------------------------------------------------------
_result:='';
B_DOMAIN.cntx_psh();
B_DOMAIN.index('SYMBOL');
B_DOMAIN.prefix(_a,);
{? B_DOMAIN.first() || _result:=B_DOMAIN.NAME ?};
B_DOMAIN.cntx_pop();
_result


\domain_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Zwraca dziedziny produktowe do których jest licencja
::   WE: [_a] - INTEGER - 0/[1] - czy wykluczać dziedziny niedostępne na interm
::  TAG: <PUBLICZNA>
::   WY: Zwracana jest tabela _result lub ~~ jeżeli nie udało się jej utworzyć przez błąd wywołania
::----------------------------------------------------------------------------------------------------------------------
_interm_del:=1;
{? var_pres('_a')=type_of(0)
|| _interm_del:=_a
?};
_result:=app_info('domain_lic');
_websesid:=+app_info('web_sesid');
:: W przypadku, gdy app_info() nie zwraca tabeli to zwracany jest ~~, ponieważ tabela nie jest utworzona
{? type_of(_result)<>type_of(SYSLOG)
|| return(~~)
?};
_result.cntx_psh();

:: Jakbyśmy chcieli testowo wyłączyć licencję na jakieś dziedziny, to tu:
:: tu jeżeli chcemy wyłączyć dziedziny zarówno na jTermie i webTermie
_domains_all:='';
:: tu jeżeli chcemy wyłączyć dziedziny tylko na jTermie
_domains_jTerm:='';
:: tu jeżeli chcemy wyłączyć dziedziny tylko na webTermie
_domains_webTerm:='';

{? _websesid
|| exec('firma','#firma',exec('firma_symbol','#firma'));
:: tu dodatkowo wyłączamy na webTermie dziedziny w zależoności od ustawionego
:: parametru 389 - współpraca HR Portal z WebTermem
  _parHR:=exec('get_par','#parametr',389,2)='T';
  {? _parHR || _domains_webTerm+='PKW,PPK' || '' ?}
?};
_domains_exclude:=_domains_all+{? _websesid || _domains_webTerm || _domains_jTerm ?};
_domains_exclude:=spli_str(_domains_exclude,',');
{! _it:=1.. obj_len(_domains_exclude)
|! _result.prefix(_domains_exclude[_it],);
   {? _result.first()
   || _result.del(,1)
   ?}
!};

:: Obsługa dziedzin niedostępnych na interm
{? _interm_del>0 & exec('interm','#system')>0
|| _mobile:=exec('mobile','#system');
   _result.prefix();
   {? _result.first()
   || {!
      |? _can_continue:=1;
         _next:=0;
         _ref_nxt:=null();
         _result.cntx_psh();
         {? _result.next()
         || _ref_nxt:=_result.ref()
         ?};
         _result.cntx_pop();

         _can_del:=0;
         {? _result.INTERM<>'T' & _result.SYMBOL<>'SEO'
         || _can_del:=1
         ?};
         {? _mobile>0
         || {? _result.MOBILE<>'T'
            || _can_del:=1
            ?}
         ?};
         {? _can_del>0
         || _can_continue:=_result.del(,1)
         ?};
         {? _ref_nxt<>null()
         || _next:=_result.seek(_ref_nxt)
         ?};
         _next>0 & _can_continue>0
      !}
   ?}
?};
_result.cntx_pop();

_result


\translate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Tłumaczy nazwy dziedzin
::   WE: _a - język
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_lang:=_a;

_set:='domain';

TRANSTMP.index(TRANSTMP_NDX);
TRANSTMP.prefix(_lang,_set);
{? ~TRANSTMP.first()
||
   B_DOMAIN.cntx_psh();
   B_DOMAIN.index('SYMBOL');
   B_DOMAIN.prefix();
   _Domain:=app_info('domain');
   _loop:=_Domain.first();
   {!
   |? _loop
   |!
      {? B_DOMAIN.find_key(_Domain.SYMBOL,)
      ||
         TRANSTMP.LANG:=_lang;
         TRANSTMP.SET:=_set;
         TRANSTMP.ITEM:=$B_DOMAIN.ref();
         TRANSTMP.TRAN:=_Domain.NAME;
         TRANSTMP.add(1)
      ?};
      _loop:=_Domain.next()
   !};
   B_DOMAIN.cntx_pop()
?}

:Sign Version 2.0 jowisz:1045 2023/11/10 08:30:04 e126cd78fd3d4d3662fee7bb908415cf199b3634b6bad738359d21ee895bc3fe4296d8894477aaac3f90cfdf7cbb5d68b2a4ebf5269dce7771895f47f09ff61a73f8285baf63eb31db86c0afa013c3a8a76bdc23ceccf7061d141d46ef7ad0aef0c9f0ad4fc3e0eab0b298297b1725f3c290f542e5645b1af13da68a72be098e

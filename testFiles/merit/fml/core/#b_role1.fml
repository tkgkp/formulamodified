:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #b_role1.fml
:: Utworzony: 09.09.2022 [23.25]
:: Autor: MW
::======================================================================================================================
:: Zawartość: Formuły do obsługi tabeli B_ROLE - okna dla Interm
::======================================================================================================================

\after_rfr_role_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Formuła na odśwież po w głównym oknie B_ROLE (akcja "Uprawnienia" -> zakładka "Role i użytkownicy")
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;

{? var_pres('TAB_ROLE',_env)>100
|| obj_del(_env.TAB_ROLE)
?};
_env.SEL_USER0:=null();

:: Sprawdzam czy jest sens robić coś, czyli czy dziedzina nie jest pusta
_can_continue:=1;
{? grp_empty(cur_tab(1,1),cur_win(1,1))=1
|| _can_continue:=0
?};

FIRMA.cntx_psh();
_czapa:='N';
{? REF.FIRMA().TYP='C'
|| _czapa:='T'
?};

{? _can_continue>0
||
   {? B_ROLE.sel_size()>0
   ||
::    Zaznaczenie grupowe

      B_USRROL.hdr_sel(''@);
      B_USRROL.hdr_sel(' (zaznaczenie grupowe)'@);

      _env.TAB_ROLE:=B_ROLE.sel_aget();

::    Sprawdzam sumę kontrolną z zaznaczenia - jeżeli się nie zmieniła to nic nie robię

      _crc:=-1;
      _sum:=sql('select SUM(CRC) as SUMA from :_a',_env.TAB_ROLE);
      {? _sum.first()
      || _crc:=_sum.SUMA
      ?};

      {? _crc<>_env.CRC_ROLE
      || _env.CRC_ROLE:=_crc;
         _can_continue:=1
      || _can_continue:=0
      ?};

      {? _can_continue>0
      ||
::       Obsługa użytkowników ról
         _tab_usr:=tab_tmp(1,
            'USERS','STRING[16]','Ref SQL USERS',
            'B_USRROL','STRING[16]','Ref SQL B_USRROL'
         );
         B_USRROL.cntx_psh();
         B_USRROL.index('WER1');
         _empty:=0;
         {? _env.TAB_ROLE.first()
         || {!
            |?
               _b_role:=exec('FindAndGet','#table',B_ROLE,_env.TAB_ROLE.REF,B_ROLE.name(),,null());
               {? _b_role<>null()
               || B_USRROL.prefix(REF.FIRMA,_b_role);
                  {? B_USRROL.first()
                  || {!
                     |?
::                      Sprawdzam czy użytkownik występuje we wszystkich zaznaczonych rolach
                        _can_add:=exec('chk_b_usrrol','#b_role',_env.TAB_ROLE,B_USRROL.USERS);

                        {? _can_add>0
                        || _tab_usr.prefix($B_USRROL.USERS);
                           {? _tab_usr.size()=0
                           || _tab_usr.blank();
                              _tab_usr.USERS:=$B_USRROL.USERS;
                              _tab_usr.B_USRROL:=$B_USRROL.ref();
                              _tab_usr.add()
                           ?}
                        ?};
                        B_USRROL.next()
                     !}
                  ||
::                   Zaznaczona rola nie ma żadnych użytkowników, więc przerywam i pokazuje pustą
::                   tabelkę użytkowników
                     _empty:=1
                  ?}
               ?};
               _env.TAB_ROLE.next() & _empty=0
            !}
         ?};
         B_USRROL.cntx_pop();
         {? _empty>0
         ||
::          Pusta dziedzina użytkowników ról
            {? B_USRROL.f_active()>0
            || B_USRROL.f_clear(1)
            ?};
            B_USRROL.index('UNIK');
            B_USRROL.prefix(null(),null(),null());

            {? USERS.f_active()>0
            || USERS.f_clear(1)
            ?};
            USERS.index('USR_KKOD');
            USERS.prefix()
         ||
::          Zakładam filtry
            USERS.prefix();
            _where:='USERS.AKT=\'T\' AND USERS.REFERENCE not in (select :_a.USERS from :_a)';
            USERS.f_set('KOD','',_where,_tab_usr);

            B_USRROL.prefix();
            _where:='B_USRROL.REFERENCE in (select :_a.B_USRROL from :_a)';
            B_USRROL.f_set('USERS(KOD)','',_where,_tab_usr);
            B_USRROL.f_first()
         ?}

      ?}
   ||
::    Brak zaznaczenia grupowego

      B_USRROL.hdr_sel('');
      B_USRROL.hdr_sel(': %1'[B_ROLE.NAME]);

      _env.CRC_ROLE:=0;
      _env.TAB_ROLE:=tab_tmp(1,
         'REF','INTEGER','#B_ROLE'
      );
      _env.TAB_ROLE.blank();
      _env.TAB_ROLE.REF:=#B_ROLE.ref();
      _env.TAB_ROLE.add();

::    Użytkownicy ról
      B_USRROL.f_clear(1);
      B_USRROL.index('WER1');
      B_USRROL.prefix(REF.FIRMA,B_ROLE.ref());
      {? _env.SEL_USRROL<>null()
      || {? B_USRROL.seek(_env.SEL_USRROL)=0
         || B_USRROL.first()
         ?}
      || B_USRROL.first()
      ?};

::    Użytkownicy wszyscy
      USERS.prefix();
      _where:='USERS.AKT=\'T\' AND USERS.REFERENCE not in (select B_USRROL.USERS from B_USRROL where B_USRROL.B_ROLE=:_a and B_USRROL.FIRMA=:_b)';
      USERS.f_set('KOD','',_where,B_ROLE.ref(),REF.FIRMA);
      ~~
   ?}
||
:: Pusta dziedzina ról
   B_USRROL.hdr_sel(''@);

   _env.SEL_ROLE:=null();
   {? B_USRROL.f_active()>0
   || B_USRROL.f_clear(1)
   ?};
   B_USRROL.index('UNIK');
   B_USRROL.prefix(null(),null(),null());

   {? USERS.f_active()>0
   || USERS.f_clear(1)
   ?};
   USERS.index('USR_KKOD');
   USERS.prefix()

?};
FIRMA.cntx_pop();
grp_disp(USERS,_env.WIN_USR_ALL,,1);
grp_disp(B_USRROL,_env.WIN_USR_ROLE,,1);
~~


\grp_make_role_usr_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Tworzy okno grupowe uprawnień (akcja "Uprawnienia")
::   WE: _a - env - środowisko działania - wynik działania exec('env','#b_role1')
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_tab:=B_ROLE;
{? _env.WIN_MAIN=''
||
   _grpbefor:="
         params_set(params_get());
         _env:=params_get().env;

         grp_disp(USERS,_env.WIN_USR_ALL);
         ~~
      ";
:: Okno główne
   _grp:=_tab.grp_make(_env.tit_main,_grpbefor,_env.wid_main,1,1);

:: Okno role, zakładka "Role i użytkownicy"
   _before:="{? _a || B_ROLE.f_clear(1);B_ROLE.index('UNIK');B_ROLE.prefix(REF.FIRMA) ?}; ~~";
   _rfr:="params_set(params_get());exec('after_rfr_role_main','#b_role1')";
   _tab.grp_sel(_grp,B_ROLE,_env.WIN_ROLE_ALL,_env.tit_role_usr,_rfr,,,20,_before,,,1,'maximized_with_title',,1);

:: Okno użytkownicy ról
   _tab.tab_splt(_grp,,'vertical','prawygorny',',20%');
   _before:="
      params_set(params_get());
      _env:=params_get().env;
      {? B_USRROL.f_active()>0 & B_USRROL.sel_size()=0
      || B_USRROL.f_rfresh()
      ?};
      ~~
   ";
   _rfr:="params_exec('after_rfr_usrrol','#b_role')";
   _tab.grp_sel(_grp,B_USRROL,_env.WIN_USR_ROLE,,_rfr,,,,_before,,,,'maximized_with_title');

:: Okno użytkownicy wszyscy
   _tab.tab_splt(_grp,'prawygorny','vertical','prawy2');
   _rfr:="params_exec('after_rfr_users','#b_role')";
   _before:="
      _user_action:=_a;
      params_set(params_get());
      _env:=params_get().env;
      {? _env.SEL_USER<>null()
      || {? USERS.f_active()>0 & USERS.sel_size()=0
         || USERS.f_rfresh();
            USERS.f_seek(_env.SEL_USER)
         || USERS.seek(_env.SEL_USER)
         ?}
      || {? USERS.f_active()>0 & USERS.sel_size()=0
         || USERS.f_rfresh()
         ?}
      ?};
      ~~
   ";
   _after:="
      params_set(params_get());
      _env:=params_get().env;
      exec('tab_users','#b_role1');
      ~~
   ";
   _tab.grp_sel(_grp,USERS,_env.WIN_USR_ALL,,_rfr,,,,_before,_after,,,'maximized_with_title');

:: Okno role, zakładka "Role i czynności"
   _rfr:="params_set(params_get());exec('after_rfr_role_main2','#b_role1')";
   _before:="{? _a || B_ROLE.f_clear(1);B_ROLE.index('UNIK');B_ROLE.prefix(REF.FIRMA) ?}; ~~";
   _tab.grp_sel(_grp,B_ROLE,_env.WIN_ROLE_ALL2,_env.tit_role_act,_rfr,,,20,_before,,,1,'maximized_with_title');

:: Okno czynności ról
   _tab.tab_splt(_grp,,'vertical','prawydolny',',20%');
   _rfr:="params_exec('after_rfr_actrol','#b_role')";
   _before:="
      params_set(params_get());
      _env:=params_get().env;
      {? B_ACTROL.f_active()>0 & B_ACTROL.sel_size()=0
      || B_ACTROL.f_rfresh()
      ?};

      ~~
   ";
   _tab.grp_sel(_grp,B_ACTROL,_env.WIN_ACT_ROLE,,_rfr,,,,_before,,,,'maximized_with_title');

:: Okno czynności wszystkie
   _tab.tab_splt(_grp,'prawydolny','vertical','prawydolny2');
   _rfr:="params_exec('after_rfr_actions','#b_role')";
   _before:="
      _user_action:=_a;
      params_set(params_get());
      _env:=params_get().env;

      {? _env.SEL_ACTION<>null()
      || {? B_ACTION.f_active()>0 & B_ACTION.sel_size()=0
         || B_ACTION.f_rfresh();
            B_ACTION.f_seek(_env.SEL_ACTION)
         || B_ACTION.seek(_env.SEL_ACTION)
         ?}
      || {? B_ACTION.f_active()>0 & B_ACTION.sel_size()=0
         || B_ACTION.f_rfresh()
         ?}
      ?};
      ~~
   ";
   _after:="
      params_set(params_get());
      _env:=params_get().env;
      exec('tab_actions','#b_role1');
      ~~
   ";
   _tab.grp_sel(_grp,B_ACTION,_env.WIN_ACT_ALL,,_rfr,,,,_before,_after,,,'maximized_with_title');

:: okno użytkownicy, zakładka "Użytkownicy i role"
   _before:="USERS.f_clear(1);~~";
   _rfr:="params_set(params_get());exec('after_rfr_users_main','#b_role')";
   _tab.grp_sel(_grp,USERS,_env.WIN_USR_ALL2,_env.tit_usr_role,_rfr,,,20,_before,,,,'maximized_with_title');

:: Okno role użytkowników
   _tab.tab_splt(_grp,,'vertical','prawygorny',',33%');
   _rfr:="params_exec('after_rfr_role','#b_role')";
   _before:="
      params_set(params_get());
      _env:=params_get().env;
      {? B_USRROL.f_active()>0 & B_USRROL.sel_size()=0
      || B_USRROL.f_rfresh()
      ?};
      ~~
   ";
   _tab.grp_sel(_grp,B_USRROL,_env.WIN_ROLE_USR,,_rfr,,,,_before,,,,'maximized_with_title');

:: Okno wszystkie role
   _tab.tab_splt(_grp,'prawygorny','vertical','prawy2');
   _rfr:="params_exec('after_rfr_role','#b_role')";
   _before:="
      _user_action:=_a;
      params_set(params_get());
      _env:=params_get().env;
      {? _env.SEL_ROLE<>null()
      || {? B_ROLE.f_active()>0 &  B_ROLE.sel_size()=0
         || B_ROLE.f_rfresh();
            B_ROLE.f_seek(_env.SEL_ROLE)
         || B_ROLE.seek(_env.SEL_ROLE)
         ?}
      || {? B_ROLE.f_active()>0 & B_ROLE.sel_size()=0
         || B_ROLE.f_rfresh()
         ?}
      ?};
      ~~
   ";
   _after:="
      params_set(params_get());
      _env:=params_get().env;
      exec('tab_role_all','#b_role1');
      ~~
   ";
   _tab.grp_sel(_grp,B_ROLE,_env.WIN_ROLE_ALL3,,_rfr,,,,_before,_after,,,'maximized_with_title');

   _tab.win_sel(_grp);
   _env.WIN_MAIN:=_grp;
   ~~
?};
~~


\after_rfr_role_main2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Formuła na odśwież po w głównym oknie B_ROLE, zakładka "Role i czynności"
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;

{? var_pres('TAB_ROLE',_env)>100
|| obj_del(_env.TAB_ROLE)
?};
_env.SEL_USER0:=null();

:: Sprawdzam czy jest sens robić coś, czyli czy dziedzina nie jest pusta
_can_continue:=1;
{? grp_empty(cur_tab(1,1),cur_win(1,1))=1
|| _can_continue:=0
?};

FIRMA.cntx_psh();
_czapa:='N';
{? REF.FIRMA().TYP='C'
|| _czapa:='T'
?};

{? _can_continue>0
||
   {? B_ROLE.sel_size()>0
   ||
::    Zaznaczenie grupowe

      B_ACTROL.hdr_sel(''@);
      B_ACTROL.hdr_sel(' (zaznaczenie grupowe)'@);

      _env.TAB_ROLE:=B_ROLE.sel_aget();

::    Sprawdzam sumę kontrolną z zaznaczenia - jeżeli się nie zmieniła to nic nie robię

      _crc:=-1;
      _sum:=sql('select SUM(CRC) as SUMA from :_a',_env.TAB_ROLE);
      {? _sum.first()
      || _crc:=_sum.SUMA
      ?};

      {? _crc<>_env.CRC_ROLE
      || _env.CRC_ROLE:=_crc;
         _can_continue:=1
      || _can_continue:=0
      ?};

      {? _can_continue>0
      ||
::       Obsługa czynności ról
         _tab_act:=tab_tmp(1,
            'B_ACTION','STRING[16]','Ref SQL B_ACTION',
            'B_ACTROL','STRING[16]','Ref SQL B_ACTROL'
         );
         B_ACTROL.cntx_psh();
         B_ACTROL.index('B_ROLE');
         _empty:=0;
         {? _env.TAB_ROLE.first()
         || {!
            |?
               _b_role:=exec('FindAndGet','#table',B_ROLE,_env.TAB_ROLE.REF,B_ROLE.name(),,null());
               {? _b_role<>null()
               || B_ACTROL.prefix(_b_role);
                  {? B_ACTROL.first()
                  || {!
                     |?
::                   Sprawdzam czy czynność występuje we wszystkich zaznaczonych rolach
                        _can_add:=exec('chk_b_actrol','#b_role',_env.TAB_ROLE,B_ACTROL.B_ACTION);

                        {? _can_add>0
                        || _tab_act.prefix($B_ACTROL.B_ACTION);
                           {? _tab_act.size()=0
                           || _tab_act.blank();
                              _tab_act.B_ACTION:=$B_ACTROL.B_ACTION;
                              _tab_act.B_ACTROL:=$B_ACTROL.ref();
                              _tab_act.add()
                           ?}
                        ?};
                        B_ACTROL.next()
                     !}
                  ||
::                Zaznaczona rola nie ma żadnych czynności, więc przerywam i pokazuje pustą
::                tabelkę czynności
                     _empty:=1
                  ?}
               ?};
               _env.TAB_ROLE.next() & _empty=0
            !}
         ?};
         B_ACTROL.cntx_pop();
         {? _empty>0
         ||
::          Pusta dziedzina czynności ról
            {? B_ACTROL.f_active()>0
            || B_ACTROL.f_clear(1)
            ?};
            B_ACTROL.index('UNIK');
            B_ACTROL.prefix(null(),null(),null());


            {? B_ACTION.f_active()>0
            || B_ACTION.f_clear(1)
            ?};
            B_ACTION.index('UNIK');
            B_ACTION.prefix()
         ||
::          Zakładam filtry
            B_ACTION.prefix();
            {? _czapa='T'
            || _where:='B_ACTION.GRP_FIRM=\'T\' and B_ACTION.AKT=\'T\' and B_ACTION.REFERENCE not in (select :_a.B_ACTION from :_a)'
            || _where:='B_ACTION.AKT=\'T\' and B_ACTION.REFERENCE not in (select :_a.B_ACTION from :_a)'
            ?};
            B_ACTION.f_set('UID','',_where,_tab_act);

            B_ACTROL.prefix();
            _where:='B_ACTROL.REFERENCE in (select :_a.B_ACTROL from :_a)';
            B_ACTROL.f_set('B_ACTION(UID)','',_where,_tab_act);
            B_ACTROL.f_first()
         ?}
      ?}
   ||
::    Brak zaznaczenia grupowego

      B_ACTROL.hdr_sel('');
      B_ACTROL.hdr_sel(': %1'[B_ROLE.NAME]);

      _env.CRC_ROLE:=0;
      _env.TAB_ROLE:=tab_tmp(1,
         'REF','INTEGER','#B_ROLE'
      );
      _env.TAB_ROLE.blank();
      _env.TAB_ROLE.REF:=#B_ROLE.ref();
      _env.TAB_ROLE.add();

::    Czynności
      B_ACTROL.f_clear(1);
      B_ACTROL.index('B_ROLE');
      B_ACTROL.prefix(B_ROLE.ref());
      {? _env.SEL_ACTROL<>null()
      || {? B_ACTROL.seek(_env.SEL_ACTROL)=0
         || B_ACTROL.first()
         ?}
      || B_ACTROL.first()
      ?};

::    Czynności wszystkie
      B_ACTION.prefix();
      {? _czapa='T'
      || _where:='B_ACTION.GRP_FIRM=\'T\' and B_ACTION.AKT=\'T\' and B_ACTION.REFERENCE not in (select B_ACTROL.B_ACTION from B_ACTROL where B_ACTROL.B_ROLE=:_a and B_ACTROL.FIRMA=:_b)'
      || _where:='B_ACTION.AKT=\'T\' and B_ACTION.REFERENCE not in (select B_ACTROL.B_ACTION from B_ACTROL where B_ACTROL.B_ROLE=:_a and B_ACTROL.FIRMA=:_b)'
      ?};
      B_ACTION.f_set('UID','',_where,B_ROLE.ref(),REF.FIRMA);
      ~~
   ?}
||
:: Pusta dziedzina ról
   B_ACTROL.hdr_sel(''@);

   _env.SEL_ROLE:=null();
   {? B_ACTROL.f_active()>0
   || B_ACTROL.f_clear(1)
   ?};
   B_ACTROL.index('UNIK');
   B_ACTROL.prefix(null(),null(),null());

   {? B_ACTION.f_active()>0
   || B_ACTION.f_clear(1)
   ?};
   B_ACTION.index('UNIK');
   B_ACTION.prefix()

?};
FIRMA.cntx_pop();

grp_disp(B_ACTION,_env.WIN_ACT_ALL,,1);
grp_disp(B_ACTROL,_env.WIN_ACT_ROLE,,1);
~~


\show_role_actions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Pokazuje czynności roli
::   WE: _a - B_ROLE.ref
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_role:=_a;
params_set(params_get());
_env:=params_get().env;

B_ACTROL.cntx_psh();
_wer:=exec('win_actrol','#b_role',_env,'#upr_role_acts','ACTION',0,0);
B_ACTROL.index('DISP1');
B_ACTROL.prefix(REF.FIRMA,_role);
B_ACTROL.win_sel(_wer);
B_ACTROL.select();
B_ACTROL.cntx_pop();
~~


\tab_role_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: zapisuje wybrane role w _env.TAB_ROLE_ALL
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;

::    Zapamiętuję zaznaczenie
{? var_pres('TAB_ROLE_ALL',_env)>100
|| obj_del(_env.TAB_ROLE_ALL)
?};

_tab:=cur_tab(1,1);
{? _tab.sel_size()>0
|| _env.TAB_ROLE_ALL:=_tab.sel_aget()
|? grp_empty(cur_tab(1,1),cur_win(1,1))<>1
|| _env.TAB_ROLE_ALL:=tab_tmp(1,
      'REF','INTEGER','#USERS');
   _env.TAB_ROLE_ALL.blank();
   _env.TAB_ROLE_ALL.REF:=#B_ROLE.ref();
   _env.TAB_ROLE_ALL.add()
?};
~~


\tab_users
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: zapisuje wybrane role w _env.TAB_USERS
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;

::    Zapamiętuję zaznaczenie
{? var_pres('TAB_USERS',_env)>100
|| obj_del(_env.TAB_USERS)
?};

_tab:=cur_tab(1,1);
{? _tab.sel_size()>0
|| _env.TAB_USERS:=_tab.sel_aget()
|? grp_empty(cur_tab(1,1),cur_win(1,1))<>1
|| _env.TAB_USERS:=tab_tmp(1,
      'REF','INTEGER','#USERS');
   _env.TAB_USERS.blank();
   _env.TAB_USERS.REF:=#_tab.ref();
   _env.TAB_USERS.add()
?};
~~


\tab_actions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: zapisuje wybrane role w _env.TAB_ACTIONS
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;

::    Zapamiętuję zaznaczenie
{? var_pres('TAB_ACTIONS',_env)>100
|| obj_del(_env.TAB_ACTIONS)
?};

_tab:=cur_tab(1,1);
{? _tab.sel_size()>0
|| _env.TAB_ACTIONS:=_tab.sel_aget()
|? grp_empty(cur_tab(1,1),cur_win(1,1))<>1
|| _env.TAB_ACTIONS:=tab_tmp(1,
      'REF','INTEGER','#B_ACTION');
   _env.TAB_ACTIONS.blank();
   _env.TAB_ACTIONS.REF:=#_tab.ref();
   _env.TAB_ACTIONS.add()
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:13:35 387e915e504313ab2e186a8752451ab210fc36217ccca47a87ab021dfa871435588bca891b444df5615b82b18feb4a8075632aef3e3b3455e81b0f2a964bf5c951b5e0b3ba25585a24f2a720aa8cf36f7b2754c5a3e4dbeb3e7acf5c8334227b0cbd9dc8cae13d4dd9e7ebdefc9990d0da8e11bd8b2f96633318d38947b1b905

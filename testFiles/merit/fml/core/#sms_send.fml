:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #sms_send.fml
:: Utworzony: 28.03.2023
:: Autor: PD
::======================================================================================================================
:: Zawartość: Mechanizm wysyłki SMS
::======================================================================================================================
\send_sms
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS:  utworzenie zamówienia
::   WE:_a- _courier_code
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_token:='Authorization: Bearer %1'[exec('get','#params',700020,2)];
_method:='sms.do';
_b:={? exec('get','#params',700025,2)='N' || STR.maz2nop(_b) || _b ?};
_json:=exec('json_sms','#sms_send',_a,_b,_c,_d,_e,_f,_g);
_wyn:=exec('http_send','#sms_send',_json,_method,1,1,_json,_token)


\http_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: wysyła plik lub stringa postem i zapisuje w logu (jeśli podane parametry _b / _c)
::   WE: _a - plik lub string do wysłania (lub '' bez zapisu pliku w blobie)
::       _b - opis do logu
::       _c - czy zapisywać plik w logu - domyślnie 1
::       _d - 0-get status, 1-get bytes
::       _e - JSON do zapisu
::       _f - token
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_url:='https://api.smsapi.pl/' + _b;
{? _<3 || _c:=1 ?};
{? _<4 || _d:=0 ?};

_http:=inet_get(_url);

{? type_of(_http)<100
||
   exec('log_write_new','#sms_send','','Błąd wywołania funkcji %1.'['inet_get()'],0);
   return(-1)
?};

_http.append_header('Content-Type: application/json; charset=UTF-8');
_http.append_header(_f);
_utc_start:=utc_get();
_http.http_post(,_a);
_utc_stop:=utc_get();
_res:=_http.get_status();
_ans:=_http.get_data();
exec('log_write_new','#sms_send', {? _c=1 || _e || '' ?} , _b, $_res, _http, _utc_start, _utc_stop,
   {? _c=1 || _ans || '' ?}, _url);
{? _d || _ans || _res ?}


\log_write_new
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS:  zapis pliku / zawartości zmiennej w logu
::   WE: _a - plik (obiekt) / string ( może być '' )
::       _b - opis do logu
::       _c - status
::       _d - obiekt INET
::       _e - _utc_start
::       _f - _utc_stop
::       _g - odpowiedź
::       _h - url
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mwac_log_ref:=exec('mwac_log','#mwac',
   _b,
   _h,
   'REST',
   _d.get_status(),
   _e,
   _f,
   _a,
   _g,
   _d.get_header(1),
   _d.get_header(0)
);

_error:='';
_result:='';

{? type_of(json_parse(_g))=117
& var_pres('list', json_parse(_g))>0
& obj_len(json_parse(_g).list)>0
& var_pres('error', json_parse(_g).list[1])=0
|| _result:='1'
||
   {? type_of(json_parse(_g))=117
   & var_pres('error', json_parse(_g))>0
   & var_pres('message', json_parse(_g))>0
   || _error:=json_parse(_g).message
   ?};
   _result:='0'
?};
{? _mwac_log_ref<>''
|| exec('mwac_log_app','zui',_mwac_log_ref,'|SMSApi|R:%1|'[_result],_error)
?};
~~


\json_sms
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS:
::   WE: _a - adresta
::       _b - wiadomość
::       _c - nadawca
::       _d - flash (0/1)
::       _e - fast (0/1)
::       _f - data
::       _g - czas
::----------------------------------------------------------------------------------------------------------------------
_json:='
{
  "to":"%1",
  "message": %2,
  "from": "%3",
  "normalize": 0,
  "encoding": "utf-8",
  "flash": %4,
  "test": 0,
  "details": 1,
  "date": "%5",
  "date_validate": 1,
  "time_restriction": "ignore",
  "allow_duplicates": 0,
  "fast": %6,
  "format": "json"
}'[
::to
_a,
::message
json_value(_b),
::from
_c,
::flash
_d,
::date
10+$exec('to_utc','#tm_stamp',tm_stamp(_f~1,_f~2,_f~3,_g~1,_g~2,_g~3)),
::fast
_e
];
_json

:Sign Version 2.0 jowisz:1048 2023/06/23 14:13:35 29848d9a1775866242e92b0f8a508468c4d97c2dcfa84b545de49067e4fd31c498dca5ea9975882e85ae164c63ff3116968f56be14576d971b6bbc843af4d36480abcd0c5523ab76c093cd6e6c41d1050d05a6ec0948c4954450ab69ee006b3f7a55cecde09d352adee01e96ef317194cbef06e6b66471f3ae04f35cbd29556c

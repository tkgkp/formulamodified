:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #b_usrdom.fml
:: Utworzony: 10.08.2016
:: Autor: WH
::======================================================================================================================
:: Zawartość: Formuły do obsługi tabeli B_USRDOM - dziedziny dla użytkowniów
::======================================================================================================================


\buffer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Zwraca bufor tabeli B_USRDOM
::   WY: obj_new()
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
exec('B_USRDOM','#buffer')


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Funkcja dodająca rekord do tabeli B_USRROL
::   WE: _a - obj_new - tablica nazwana bedaca buforem tabeli exec('buffer','#b_usrdom')
::   WY:      [REFERENCE]  - wskazanie na dodany rekord (lub już istniejący) lub null jeśli porażka
::----------------------------------------------------------------------------------------------------------------------
_buffer:={? var_pres('_a')>100 || _a || return(null) ?};

_result:=null();
B_USRDOM.cntx_psh();
B_USRDOM.index('USERS');
B_USRDOM.prefix(_buffer.FIRMA,_buffer.USERS,_buffer.B_DOMAIN);
{? B_USRDOM.first()
|| _buffer.get();
   _result:=B_USRDOM.ref()
|| B_USRDOM.blank(1);
   _buffer.set();

   {? B_USRDOM.add()
   || _buffer.get();
      _result:=B_USRDOM.ref();
      exec('b_usrdom_add','#params',B_USRDOM.B_DOMAIN().SYMBOL,B_USRDOM.USERS,'T');
      exec('b_usrdom_add','#params',B_USRDOM.B_DOMAIN().SYMBOL,B_USRDOM.USERS,'N');
      ~~
   ?}
?};
B_USRDOM.cntx_pop();
_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Kasuje podany rekord tabeli B_USRDOM (wykonywane w transakcji!!!)
::   WE: _a - B_USRDOM.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
B_USRDOM.cntx_psh(); B_USRDOM.clear();
{? B_USRDOM.seek(_ref)
|| _user:=B_USRDOM.USERS;
   _dom_sym:=B_USRDOM.B_DOMAIN().SYMBOL;

   {? B_USRDOM.del(,1)>0
   || _result:=1;
      exec('b_usrdom_del','#params',_dom_sym,_user,'T');
      exec('b_usrdom_del','#params',_dom_sym,_user,'N');
      ~~
   ?}
|| _result:=0
?};

{? _result<=0
|| undo()
?};

B_USRDOM.cntx_pop();
{? _mydo || end() ?};
_result


\after_add_usrrole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Po dodaniu rekordu B_USRROL aktualizuje dziedziny dla użytkownika
::   WE: _a - obj_new - bufor tabeli B_USRROL
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_buf_usrrol:=_a;

B_ACTION.cntx_psh();
B_ACTROL.cntx_psh();
B_ACTROL.index('B_ROLE');
B_ACTROL.prefix(_buf_usrrol.B_ROLE);

_mydo:=do_state()=0;
{? _mydo || do() ?};

_result:=0;
_can_continue:=1;

{? B_ACTROL.first()
|| _buffer:=exec('buffer','#b_usrdom');
   _buffer.USERS:=_buf_usrrol.USERS;
   _firma:=exec('FindAndGet','#table',B_ROLE,_buf_usrrol.B_ROLE,,"FIRMA",null());
   _buffer.FIRMA:=_firma;
   {!
   |?
      _buffer.B_DOMAIN:=B_ACTROL.B_ACTION().B_DOMAIN;
      _buffer.B_CAN_J:='N';
      _buffer.B_CAN_W:='N';
      _buffer.B_CAN_Q:='N';

      {? B_ACTION.B_CAN='B'
      || _buffer.B_CAN_J:='T';
         _buffer.B_CAN_W:='T'
      ?};
      {? B_ACTION.B_CAN='M'
      || _buffer.B_CAN_J:='T'
      ?};
      {? B_ACTION.B_CAN='W'
      || _buffer.B_CAN_W:='T'
      ?};
      {? B_ACTION.B_CAN='Q'
      || _buffer.B_CAN_Q:='T'
      ?};

      {? exec('add','#b_usrdom',_buffer)=null()
      || _can_continue:=0
      ?};
      B_ACTROL.next() & _can_continue>0
   !}
?};

{? _can_continue=0
|| undo()
?};

{? _mydo || end() ?};

B_ACTROL.cntx_pop();
B_ACTION.cntx_pop();
{? _can_continue>0
|| _result:=1
?};
_result


\after_del_usrrole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Po usunięciu rekordu B_USRROL aktualizuje dziedziny dla użytkownika
::   WE: _a - obj_new - bufor tabeli B_USRROL
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_buf_usrrol:=_a;

_mydo:=do_state()=0;
{? _mydo || do() ?};

_result:=0;
_can_continue:=1;

B_USRDOM.cntx_psh();
B_USRDOM.index('USERS');
_firma:=exec('FindAndGet','#table',B_ROLE,_buf_usrrol.B_ROLE,,"FIRMA",null());
B_USRDOM.prefix(_firma,_buf_usrrol.USERS);
B_ACTROL.cntx_psh();
B_ACTROL.index('B_DOMAIN');
B_USRROL.cntx_psh();
B_USRROL.index('USER');
{? B_USRDOM.first()
|| {!
   |? _next:=0;
      _ref_nxt:=null();
      B_USRDOM.cntx_psh();
      {? B_USRDOM.next()
      || _ref_nxt:=B_USRDOM.ref()
      ?};
      B_USRDOM.cntx_pop();

::    Sprawdzam czy użytkownik ma jakąkolwiek rolę uprawniającą go czynności z tej dziedziny
      _can_delete:=1;
      B_USRROL.prefix(REF.FIRMA,B_USRDOM.USERS);
      {? B_USRROL.first()
      || {!
         |?
            B_ACTROL.prefix(B_USRROL.B_ROLE,B_USRDOM.B_DOMAIN);
            {? B_ACTROL.first()>0
            || _can_delete:=0
            ?};

::          Aktualizacja kanałów dostępu
            B_ACTROL.prefix(B_USRROL.B_ROLE,B_USRDOM.B_DOMAIN,'B');
            {? B_ACTROL.first()
            || B_USRDOM.B_CAN_J:='T';
               B_USRDOM.B_CAN_W:='T'
            ?};
            B_ACTROL.prefix(B_USRROL.B_ROLE,B_USRDOM.B_DOMAIN,'M');
            {? B_ACTROL.first()
            || B_USRDOM.B_CAN_J:='T'
            ?};
            B_ACTROL.prefix(B_USRROL.B_ROLE,B_USRDOM.B_DOMAIN,'W');
            {? B_ACTROL.first()
            || B_USRDOM.B_CAN_W:='T'
            ?};
            B_ACTROL.prefix(B_USRROL.B_ROLE,B_USRDOM.B_DOMAIN,'Q');
            {? B_ACTROL.first()
            || B_USRDOM.B_CAN_Q:='T'
            ?};
            B_USRDOM.put();
            B_USRROL.next()
         !}
      ?};
      {? _can_delete>0
      || _can_continue:=exec('delete','#b_usrdom',B_USRDOM.ref())
      ?};

      {? _ref_nxt<>null()
      || _next:=B_USRDOM.seek(_ref_nxt)
      ?};
      _next>0 & _can_continue>0
   !}
?};

{? _can_continue=0
|| undo()
?};

{? _mydo || end() ?};

B_USRDOM.cntx_pop();
B_ACTROL.cntx_pop();
B_USRROL.cntx_pop();
{? _can_continue>0
|| _result:=1
?};
_result


\after_add_actrole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Po dodaniu rekordu B_ACTROL aktualizuje dziedziny dla użytkowników
::       Kontekst działania dodany rekord B_ACTROL
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_mydo:=do_state()=0;
{? _mydo || do() ?};

_result:=0;
_can_continue:=1;

B_ROLE.cntx_psh();
B_ACTION.cntx_psh();
B_USRROL.cntx_psh();
B_USRROL.index('B_ROLE');
B_USRROL.prefix(B_ACTROL.B_ROLE);
{? B_USRROL.first()
|| _buffer:=exec('buffer','#b_usrdom');
   {!
   |? _buffer.USERS:=B_USRROL.USERS;
      _buffer.B_DOMAIN:=B_ACTROL.B_ACTION().B_DOMAIN;
      {? B_ACTION.B_CAN='B'
      || _buffer.B_CAN_J:='T';
         _buffer.B_CAN_W:='T'
      ?};
      {? B_ACTION.B_CAN='M'
      || _buffer.B_CAN_J:='T'
      ?};
      {? B_ACTION.B_CAN='W'
      || _buffer.B_CAN_W:='T'
      ?};
      {? B_ACTION.B_CAN='Q'
      || _buffer.B_CAN_Q:='T'
      ?};
      _buffer.FIRMA:=B_ACTROL.B_ROLE().FIRMA;
      {? exec('add','#b_usrdom',_buffer)=null()
      || _can_continue:=0
      ?};
      B_USRROL.next() & _can_continue>0
   !}
?};

{? _can_continue=0
|| undo()
?};

{? _mydo || end() ?};

B_USRROL.cntx_pop();
B_ACTION.cntx_pop();
B_ROLE.cntx_pop();

{? _can_continue>0
|| _result:=1
?};
_result


\after_del_actrole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Po usunięciu rekordu B_ACTROL aktualizuje dziedziny dla użytkowników
::   WE: _a - B_ACTION.ref() - ref czynności
::       _b - B_ROLE.ref()   - ref roli
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_b_action:=_a;
_b_role:=_b;

_mydo:=do_state()=0;
{? _mydo || do() ?};

_result:=0;
_can_continue:=1;

B_ACTROL.cntx_psh();
B_ACTROL.index('B_DOMAIN');
_b_domain:=exec('FindAndGet','#table',B_ACTION,_b_action,,"B_DOMAIN",null());
{? _b_domain<>null()
||
:: Sprawdzam czy rola uprawnia do obszaru
   B_ACTROL.prefix(_b_role,_b_domain);
   {? B_ACTROL.size()=0
   ||
      _firma:=exec('FindAndGet','#table',B_ROLE,_b_role,,"FIRMA",null());
::    Rola nie uprawnia już do obszaru więc wszystkim użytkownikom tnę rekordy B_USRDOM
      B_USRDOM.cntx_psh();
      B_USRDOM.index('USERS');
      B_USRROL.cntx_psh();
      B_USRROL.index('B_ROLE');
      B_USRROL.prefix(_b_role);
      {? B_USRROL.first()
      || {!
         |?
            B_USRDOM.prefix(_firma,B_USRROL.USERS,_b_domain);
            {? B_USRDOM.first()
            || {!
               |? _next:=0;
                  _ref_nxt:=null();
                  B_USRDOM.cntx_psh();
                  {? B_USRDOM.next()
                  || _ref_nxt:=B_USRDOM.ref()
                  ?};
                  B_USRDOM.cntx_pop();

::                Sprawdzam czy użytkownik ma jakąkolwiek inną rolę uprawniającą go czynności z tej dziedziny
                  _can_delete:=1;
                  B_USRROL.cntx_psh();
                  B_USRROL.index('USER');
                  B_ACTROL.index('B_DOMAIN');
                  B_USRROL.prefix(REF.FIRMA,B_USRDOM.USERS);
                  {? B_USRROL.first()
                  || {!
                     |?
                        B_ACTROL.prefix(B_USRROL.B_ROLE,_b_domain);
                        {? B_ACTROL.size()>0
                        || _can_delete:=0
                        ?};

::                      Aktualizacja kanałów dostępu
                        B_ACTROL.prefix(B_USRROL.B_ROLE,_b_domain,'B');
                        {? B_ACTROL.first()
                        || B_USRDOM.B_CAN_J:='T';
                           B_USRDOM.B_CAN_W:='T'
                        ?};
                        B_ACTROL.prefix(B_USRROL.B_ROLE,_b_domain,'M');
                        {? B_ACTROL.first()
                        || B_USRDOM.B_CAN_J:='T'
                        ?};
                        B_ACTROL.prefix(B_USRROL.B_ROLE,_b_domain,'W');
                        {? B_ACTROL.first()
                        || B_USRDOM.B_CAN_W:='T'
                        ?};
                        B_ACTROL.prefix(B_USRROL.B_ROLE,_b_domain,'Q');
                        {? B_ACTROL.first()
                        || B_USRDOM.B_CAN_Q:='T'
                        ?};
                        B_USRDOM.put();

                        B_USRROL.next() & _can_delete>0
                     !}
                  ?};
                  B_USRROL.cntx_pop();

                  {? _can_delete>0
                  || _can_continue:=exec('delete','#b_usrdom',B_USRDOM.ref())
                  ?};

                  {? _ref_nxt<>null()
                  || _next:=B_USRDOM.seek(_ref_nxt)
                  ?};
                  _next>0 & _can_continue>0
               !}
            ?};
            B_USRROL.next() & _can_continue>0
         !}
      ?};
      B_USRROL.cntx_pop();
      B_USRDOM.cntx_pop()
   ?}
?};
B_ACTROL.cntx_pop();
{? _can_continue=0
|| undo()
?};

{? _mydo || end() ?};
{? _can_continue>0
|| _result:=1
?};
_result


\napraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Nawija brakujące rekordy B_USRDOM
::   WE: [_a] - 0(domyślnie) z komunikatami 1-bez komunikatów
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_nocomm:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_can_continue:=1;
_result:=0;

B_USRROL.cntx_psh();
B_USRROL.index('B_ROLE');
B_ACTROL.cntx_psh();
B_ACTROL.index('B_ROLE');
B_ROLE.cntx_psh();
B_ROLE.index('UNIK');
{? B_ROLE.first()
|| FUN.prg_start(B_ROLE.size(),'Odtwarzanie dziedzin dla użytkowników.');
   {!
   |?
      _next:=0;

      B_ACTROL.prefix(B_ROLE.ref());
      {? B_ACTROL.first()
      || {!
         |? _can_continue:=exec('after_add_actrole','#b_usrdom');
            B_ACTROL.next() & _can_continue>0
         !}
      ?};

      {? _can_continue>0
      || _next:=B_ROLE.next()
      ?};
      {? _can_continue>0
      || FUN.prg_next(1,'Przetwarzanie roli: %1 w firmie: %2'@[B_ROLE.NAME,B_ROLE.FIRMA().OPIS])
      ?};
      _next>0
   !};
   FUN.prg_stop()
?};

B_ROLE.cntx_pop();
B_USRROL.cntx_pop();
B_ACTROL.cntx_pop();
{? _can_continue>0
|| _result:=1
|| {? ~_nocomm || FUN.emsg('Odtwarzanie dziedzin dla użytkowników zakończone niepowodzeniem.'@) ?}
?};
_result


\napraw_canal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Nawija brakujące rekordy B_USRDOM dotyczące kanałów dostępu
::   WE: [_a] - 0(domyślnie) z komunikatami 1-bez komunikatów
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_nocomm:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_can_continue:=1;
_result:=0;
B_USRDOM.cntx_psh();
B_USRDOM.index('USERS');
B_USRROL.cntx_psh();
B_USRROL.index('USER');
B_ACTROL.cntx_psh();
B_ACTROL.index('B_DOMAIN');
B_ROLE.cntx_psh();
B_USRDOM.prefix(REF.FIRMA);
{? B_USRDOM.first()
|| {!
   |?
      B_USRROL.prefix(REF.FIRMA,B_USRDOM.USERS);
      B_USRDOM.B_CAN_J:='N';
      B_USRDOM.B_CAN_W:='N';
      B_USRDOM.B_CAN_Q:='N';
      {? B_USRROL.first()
      || {!
         |?
::          Aktualizacja kanałów dostępu
            B_ACTROL.prefix(B_USRROL.B_ROLE,B_USRDOM.B_DOMAIN,'B');
            {? B_ACTROL.first()
            || B_USRDOM.B_CAN_J:='T';
               B_USRDOM.B_CAN_W:='T'
            ?};
            B_ACTROL.prefix(B_USRROL.B_ROLE,B_USRDOM.B_DOMAIN,'M');
            {? B_ACTROL.first()
            || B_USRDOM.B_CAN_J:='T'
            ?};
            B_ACTROL.prefix(B_USRROL.B_ROLE,B_USRDOM.B_DOMAIN,'W');
            {? B_ACTROL.first()
            || B_USRDOM.B_CAN_W:='T'
            ?};
            B_ACTROL.prefix(B_USRROL.B_ROLE,B_USRDOM.B_DOMAIN,'Q');
            {? B_ACTROL.first()
            || B_USRDOM.B_CAN_Q:='T'
            ?};
            B_USRROL.next() & _can_continue>0
         !}
      ?};
      _can_continue:=B_USRDOM.put();
      B_USRDOM.next() & _can_continue>0
   !}
?};

B_ROLE.cntx_pop();
B_USRROL.cntx_pop();
B_ACTROL.cntx_pop();
B_USRDOM.cntx_pop();
{? _can_continue>0
|| _result:=1
|| {? ~_nocomm || FUN.emsg('Odtwarzanie dziedzin dla użytkowników zakończone niepowodzeniem.'@) ?}
?};
_result


:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:04 696156e64d3274fe35aa2ddb76e72fd11e3b518d34a84e3e6efe6a0fb7110e316815509b80385e4abb6e95e5c92863c4d7cabdbd6146443e422c0e2da5bbd8f5e8d41a5a56138f32ed709864cdeaf9a8f6b53c21252b221060f668213c3a7dee06f5591114232e796e0732fee073c0c090b41ed77607daa854c2fee21c45193b

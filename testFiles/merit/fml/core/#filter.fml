:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: #filter.fml
:: Utworzony: 11.05.2018
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Plik zawiera formuły wspomagające obsługę filtrów.
::======================================================================================================================


\get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła zwraca informacje o ustawionym filtrze.
::   WE: _a [TABLE] - Uchwyt tabeli.
::   WY: Jeżeli parametr nie jest uchwytem tabeli lub dla tabeli nie jest zdefiniowany filtr, to wynikiem będzie ~~.
::       Jeżeli dla tabeli filtr jest zdefiniowany, to wynikiem będzie tablica z nazwanymi elementami będącymi
::       wartościami argumentów z ostatniego wywołania metody f_set.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(SYSLOG)
|| _a.tab_info('f_set')
|| ~~
?}


\set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła ustawia (przywraca) filtr zwracany przez \get.
::   WE: _a [TABLE] - Uchwyt tabeli.
::       _b [ARRAY] - Definicja filtra (wynik \get).
::   WY: -1 - Błędne parametry wywołania.
::        0 - Filtr nie został nałożony (wynik metody f_set).
::        1 - Filtr został nałożony (wynik metody f_set).
::        2 - Definicja filtra była pusta (filtr nie był nałożony w chwili wywołania \get).
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(SYSLOG)
|| _TAB:=_a
|| return(-1)
?};

_tSTRING:=type_of('');
_vpb:=var_pres('_b');
{? _vpb<0
|| return(-1)
|? _vpb=type_of(~~)
|| _TAB.f_clear();
   return(2)
|? ~obj_ntab(_b) |
   var_pres('sort',_b)<>_tSTRING | var_pres('from',_b)<>_tSTRING | var_pres('where',_b)<>_tSTRING |
   ~(var_pres('param',_b)=type_of(~~) | var_pres('param',_b)>100)
|| return(-1)
?};

_def:=_b;

_fml:='_a.f_set(_b,_c,_d';
{? var_pres('param',_def)>100
|| {! _lp:=1 .. obj_len(_def.param)
   |! _fml+=',_e[%1]' [$_lp]
   !}
?};
_fml+=')';
($_fml)(_TAB,_def.sort,_def.from,_def.where,_def.param)

:Sign Version 2.0 jowisz:1048 2020/10/16 15:20:23 9b8a10ae5507cece25aa30a6dc3ba2a96b732b04f6216932a41a3eb3d43d32e950af74ce3999b879b89e7525f0a59531c2e58ff8e84037102001742b18aad9f88d8ef55b67db23bf801eea9b2e3f3b1175177d58be38d8aa8d76b38e8ba71fb732f253da4af64851b1c3f33b1631b373206c38e964eda04388e4059ad4ff8b63

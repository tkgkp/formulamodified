:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ro_wycz.fml
:: Utworzony: 05.04.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę wycofywania zgód.
::======================================================================================================================
:: Do formuł wywoływanych przez akcję "Wycofaj" przekazywany jest jeden argument - tablica nazwana z następującymi
:: elementami:
::    RODO     - Obiekt klasy RODO.
::    SRC_TAB  - Alias tabeli, z której pochodzi zapis i dla którego będzie wycofywana zgoda.
::    SRC_REF  - Wskazanie wiersza tabeli SRC_TAB.
::    RO_PERM  - Wskazanie wiersza w tabeli RO_PERM
::    RO_REQN  - Wskazanie wiersza w tabeli RO_REQN
::    CODE     - Kod zgody
::    RET      - Tablica nazwana (patrz \ret_val/ro_cfg.fml) do umieszczenia statusu wyszukiwania.
::======================================================================================================================


\fml_map
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Tworzy i wypełnia mapę formuł dla tabel, dla których będą wycofywane zgody.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_map:=tab_tmp(2,
   'TABLE',   'STRING[8]', 'Tabela',
   'CODE',    'STRING[40]','Kod zgody',
   'FORMULA', 'STRING[255]','Formuła wycofania zgody',
   'PRE_COND','STRING[255]','Formuła warunku wstępnego'
);

_add:="
   _a.TABLE:=_b;
   _a.CODE:=_c;
   _a.FORMULA:=form(_d);
   _a.PRE_COND:=form(_e);
   _a.add()
";

_add(_map,'RP_OSOBA','RECRUITMENT_PROCESS',
   "exec('rp_osoba_prz','ro_wycz',_a)",
   "exec('rp_osoba_ask','ro_wycz',_a)"
);
_add(_map,'RP_OSOBA','RECRUITMENT_RETAIN',
   "exec('rp_osoba_poz','ro_wycz',_a)",
   ""
);

_map


\rp_osoba_ask
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Wyświetla dialog z prośbą o potwierdzenie o wycofanie zgody na przetwarzanie danych kandydata. Potwierdzenie
::       jest równoznaczne z
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej parametry działania formuły
::   WY: wynik FUN.ask
::----------------------------------------------------------------------------------------------------------------------
FUN.ask(
   'Wycofanie zgody na przetwarzanie danych jest jednoznaczne z ich zamazaniem.\n'+
   'Czy na pewno wycofać zgodę kandydata i nadpisać jego dane osobowe?'
)


\rp_osoba_prz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Wycofanie zgody na przetwarzanie danych osobowych na potrzeby procesu rekrutacji.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej parametry działania formuły
::   WY: 0 - porażka, 1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;

_epilog:="
   RO_PERM.cntx_pop();
   RO_REQN.cntx_pop();
   ~~
";

RO_REQN.cntx_psh();
RO_PERM.cntx_psh();
RO_PERM.prefix();
{? ~RO_PERM.seek(_in.RO_PERM)
|| _epilog();
   return(0)
?};

RP_OSOBA.ZGODA:='N';
{? RP_OSOBA.put()
|| _arg:=exec('zap_arg','rodo',_in.RODO);
   _arg.SRC:=exec('ref_arg','ro_cfg',_in.SRC_TAB,_in.SRC_REF);
   _arg.NEW:=exec('ref_arg','ro_cfg');
:  zanonimizuj dane kandydata do pracy
   exec('rp_osoba','ro_zap',_arg);
   {? _arg.RET.STATUS=0
   || _aip:=exec('is_aip','rodo');
      _cel:=RO_PERM.RO_REQC().CODE;
      _typ:='RECRUITMENT_RETAIN';
      RO_PERM.cntx_psh();
      RO_PERM.index('RO_PERM');
:     sprawdź, czy istnieje opcjonalna zgoda na przetwarzanie danych
      RO_PERM.prefix('RP_OSOBA',RP_OSOBA.name(),#RP_OSOBA.ref(),_cel,_typ);
:     w przypadku braku zgody "głównej", opcjonalne usuwamy
      {!
      |? RO_PERM.first()
      |! {? _aip & RO_PERM.RO_REQN().STATUS<>'P'
         || RO_REQN.STATUS:='P';
            RO_REQN.put()
         ?};
         exec('ro_reqn_del','ro_req',RO_PERM.RO_REQN,~_aip)
      !};
      RO_PERM.cntx_pop()
   ?};
   {? _arg.RET.STATUS=0
   ||
:     zależnie od współpracy z RU nagłówek usuń lub przygotuj do wysyłki
      exec('ro_reqn_del','ro_req',RO_PERM.RO_REQN,~exec('is_aip','rodo'))
   ?}
?};

_epilog();

_arg.RET.STATUS=0


\rp_osoba_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Wycofanie zgody na pozostawienie danych osobowych po zakończeniu procesu rekrutacji.
::   WE: _a [OBJECT] - wskazanie tablicy nazwanej zawierającej parametry działania formuły
::   WY: 0 - porażka, 1 - sukces
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_ret:=0;

_epilog:="
   RO_PERM.cntx_pop();
   ~~
";

RO_PERM.cntx_psh();
RO_PERM.prefix();
{? ~RO_PERM.seek(_in.RO_PERM)
|| _epilog();
   return(0)
?};

RP_OSOBA.ZOSTAW:='N';
{? RP_OSOBA.put()
||
   {? RP_OSOBA.ZGODA='N'
:     wycofano "główną" zgodę
   || exec('ro_reqn_del','ro_req',RO_PERM.RO_REQN,~exec('is_aip','rodo'))

   |? ~exec('is_aip','rodo')
:     usuń przy braku współpracy z RU
   || exec('ro_reqn_del','ro_req',RO_PERM.RO_REQN,1)
   ?};
   _in.RET.STATUS:=0;
   _ret:=1
?};

_epilog();

_ret

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:01 1b4bd084cc3a223371bb4dd2146e3baa92c74626e8720b6cd3bf3157ca1c0c88fb7f4d46de707432aa39b6f77243b9c113dcb0af2b727b4e957718402eda70bf8980ddabfd2eaf944666437e60eed020b1d166f7c93f2be8bc3d01c5a7624ed72fc2b46b2663d8b14dd88923b557b366bf76b38dcf4a68f9c711319e1c72ae35

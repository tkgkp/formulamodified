:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: control_marza.fml
:: Utworzony: 05.06.2020
:: Autor: AMK
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi analiz marży IV
::======================================================================================================================


\m4_be_udsch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przed redakcją pól parametrycznych typu UD_SCH
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
_typ:={? _fld='MSCHWIEL'
      || 'POZ_BUD'
      |? _fld='MSCHKLI' | _fld='MSCHPROD'
      || 'OB_KOSZ'
      |? _fld='MSCHMPK'
      || 'PODZORG'
      |? _fld='MSCHKLPR' | _fld='MSCHMKLP'
      || 'KLI_PROD'
      || ''
      ?};
{? _typ<>'' || exec('szukaj_ud_typ','schemat',_typ) ?};
1


\m4_ae_udsch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Po redakcji pól parametrycznych typu UD_SCH
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.cntx_psh(); UD_DEF.index('PODTEC');
{? ~XINFO.MSCHPROD
|| XINFO.MDOMPR:=null; XINFO.MFORMPRO:=null
|? XINFO.MDOMPR
|| UD_DEF.prefix(XINFO.MSCHPROD,XINFO.MDOMPR);
   {? ~UD_DEF.first() || XINFO.MDOMPR:=null ?}
?};
{? ~XINFO.MSCHKLI
|| XINFO.MDOMKL:=null; XINFO.MFORMKLI:=null
|? XINFO.MDOMKL
|| UD_DEF.prefix(XINFO.MSCHKLI,XINFO.MDOMKL);
   {? ~UD_DEF.first() || XINFO.XINFO.MDOMKL:=null ?}
?};
{? ~XINFO.MSCHKLPR
|| XINFO.MSOKPF:=XINFO.MMZOKPF:=XINFO.MMWOKPF:=XINFO.MFORMKP:=null;
   UD_POM.WYM8:=''; XINFO.MDOMKLPR:=null
?};
{? ~XINFO.MSCHWIEL
|| XINFO.MSPR:=1; XINFO.MSPF:=null; UD_POM.PBUD:=''; XINFO.MSPE:=null;
   XINFO.MMZPR:=1; XINFO.MMZPF:=null; UD_POM.WYM4:=''; XINFO.MMZPE:=null;
   XINFO.MMWPR:=1; XINFO.MMWPF:=null; UD_POM.WYM5:=''; XINFO.MMWPE:=null;
   XINFO.MDOMWK:=XINFO.MDOMWP:=null
|| exec('add_skl','control_marza',XINFO.MSCHWIEL,'MR_4LKOSZTY','Koszty pozostałe',null);
   exec('add_skl','control_marza',XINFO.MSCHWIEL,'MR_4LPRZYCHODY','Przychody pozostałe',null)
?};
{? ~XINFO.MSCHKLPR | ~XINFO.MSCHMPK
|| XINFO.MFORMKPR:=null
?};
UD_DEF.cntx_pop();
win_disp(); 1


\param_m4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Parametry obsługi marży IV
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO);
SKID_MBN.cntx_psh(); SKID_MBN.win_dict('SLO');
FORMULA.cntx_psh(); FORMULA.win_edit('FORMULY'); FORMULA.win_dict('FORMULY'); FORMULA.actions('WER','T:T');
exec('set_sch','control_marza');
exec('edytuj','#stalesys',XINFO,'M4_CONTR'," ''")<>date(0,0,0);
SKID_MBN.cntx_pop(); FORMULA.cntx_pop();
1


\m4_be_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przed redakcją pola XINFO.FORMPROD i XINFO.FORMKLI
::----------------------------------------------------------------------------------------------------------------------
ZMIENNE.RODZAJ:='C'; 1


\m4_mdspmod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Na wyświetl dla pola XINFO.MODEW
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.MSCHPROD
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MDOMPR');
   XINFO.efld_opt('M4_CONTR','enable=1',UD_POM,'OKOSZ');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MFORMPRO')
|| XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MDOMPR');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'OKOSZ');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MFORMPRO')
?};
{? XINFO.MSCHKLI
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MDOMKL');
   XINFO.efld_opt('M4_CONTR','enable=1',UD_POM,'JORG');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MFORMKLI')
|| XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MDOMKL');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'JORG');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MFORMKLI')
?};
{? XINFO.MSCHKLPR
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MSOKPF');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMZOKPF');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMWOKPF');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MFORMKP');
   XINFO.efld_opt('M4_CONTR','enable=1',UD_POM,'WYM8');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MDOMKLPR')
|| XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MSOKPF');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMZOKPF');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMWOKPF');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MFORMKP');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'WYM8');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MDOMKLPR')
?};
{? ~XINFO.MSCHWIEL
|| XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MSPR');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MSPF');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'PBUD');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MSPE')
|? XINFO.MSPR=1
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MSPR');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MSPF');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'PBUD');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MSPE')
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MSPR');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MSPF');
   XINFO.efld_opt('M4_CONTR','enable=1',UD_POM,'PBUD');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MSPE')
?};
XINFO.efld_opt('M4_CONTR','enable=1',UD_POM,'WYM6');
XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MDOMWP');
XINFO.efld_opt('M4_CONTR','enable=1',UD_POM,'WYM7');
XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MDOMWK');
{? ~XINFO.MSCHWIEL
|| XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMZPR');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMZPF');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'WYM4');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMZPE');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'WYM6');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MDOMWP');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'WYM7');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MDOMWK')
|? XINFO.MMZPR=1
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMZPR');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMZPF');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'WYM4');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMZPE')
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMZPR');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMZPF');
   XINFO.efld_opt('M4_CONTR','enable=1',UD_POM,'WYM4');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMZPE')
?};
{? ~XINFO.MSCHWIEL
|| XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMWPR');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMWPF');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'WYM5');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMWPE')
|? XINFO.MMWPR=1
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMWPR');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMWPF');
   XINFO.efld_opt('M4_CONTR','enable=0',UD_POM,'WYM5');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMWPE')
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMWPR');
   XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MMWPF');
   XINFO.efld_opt('M4_CONTR','enable=1',UD_POM,'WYM5');
   XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MMWPE')
?};
{? XINFO.MSCHKLPR & XINFO.MSCHMPK
|| XINFO.efld_opt('M4_CONTR','enable=1',XINFO,'MFORMKPR')
|| XINFO.efld_opt('M4_CONTR','enable=0',XINFO,'MFORMKPR')
?};
1


\set_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Ustawia formuły dla UD_POM
::----------------------------------------------------------------------------------------------------------------------
exec('set_fml','#field',UD_POM,'JORG',
      "UD_POM.JORG:={? XINFO.MDOMKL || XINFO.MDOMKL().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHKLI);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHKLI);
       {? _ref
       || XINFO.MDOMKL:=_ref; fld(XINFO.MDOMKL().SYMBOL)
       || XINFO.MDOMKL:=null; fld('')
       ?};
       1
      "
   );
exec('set_fml','#field',UD_POM,'OKOSZ',
      "UD_POM.OKOSZ:={? XINFO.MDOMPR || XINFO.MDOMPR().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHPROD);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHPROD);
       {? _ref
       || XINFO.MDOMPR:=_ref; fld(XINFO.MDOMPR().SYMBOL)
       || XINFO.MDOMPR:=null; fld('')
       ?};
       1
      "
   );
exec('set_fml','#field',UD_POM,'PBUD',
      "UD_POM.PBUD:={? XINFO.MSPE || XINFO.MSPE().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHWIEL);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHWIEL);
       {? _ref
       || XINFO.MSPE:=_ref; fld(XINFO.MSPE().SYMBOL)
       || XINFO.MSPE:=null; fld('')
       ?};
       1
      "
   );
exec('set_fml','#field',UD_POM,'WYM4',
      "UD_POM.WYM4:={? XINFO.MMZPE || XINFO.MMZPE().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHWIEL);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHWIEL);
       {? _ref
       || XINFO.MMZPE:=_ref; fld(XINFO.MMZPE().SYMBOL)
       || XINFO.MMZPE:=null; fld('')
       ?};
       1
      "
   );
exec('set_fml','#field',UD_POM,'WYM5',
      "UD_POM.WYM5:={? XINFO.MMWPE || XINFO.MMWPE().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHWIEL);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHWIEL);
       {? _ref
       || XINFO.MMWPE:=_ref; fld(XINFO.MMWPE().SYMBOL)
       || XINFO.MMWPE:=null; fld('')
       ?};
       1
      "
   );
exec('set_fml','#field',UD_POM,'WYM6',
      "UD_POM.WYM6:={? XINFO.MDOMWP || XINFO.MDOMWP().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHWIEL);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHWIEL);
       {? _ref
       || XINFO.MDOMWP:=_ref; fld(XINFO.MDOMWP().SYMBOL)
       || XINFO.MDOMWP:=null; fld('')
       ?};
       1
      "
   );
exec('set_fml','#field',UD_POM,'WYM7',
      "UD_POM.WYM7:={? XINFO.MDOMWP || XINFO.MDOMWK().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHWIEL);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHWIEL);
       {? _ref
       || XINFO.MDOMWK:=_ref; fld(XINFO.MDOMWK().SYMBOL)
       || XINFO.MDOMWK:=null; fld('')
       ?};
       1
      "
   );
exec('set_fml','#field',UD_POM,'WYM8',
      "UD_POM.WYM8:={? XINFO.MDOMKLPR || XINFO.MDOMKLPR().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHKLPR);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHKLPR);
       {? _ref
       || XINFO.MDOMKLPR:=_ref; fld(XINFO.MDOMKLPR().SYMBOL)
       || XINFO.MDOMKLPR:=null; fld('')
       ?};
       1
      "
   );
1


\f3_dim
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Klawisz F3 dla wymiaru
::   WE: _a - ref schematu
::----------------------------------------------------------------------------------------------------------------------
_sym:='';
{? type_of(_a)=type_of(null) & _a
|| UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
   {? UD_SCH.seek(_a)
   || UD_DEF.cntx_psh();
      UD_DEF.index('SCHSYM'); UD_DEF.prefix(UD_SCH.ref()); _sym:=fld(); {? _sym<>'' || UD_DEF.find_key(_sym) ?};
      UD_DEF.index('SYMBOL'); UD_DEF.prefix(UD_SCH.ref());
      UD_DEF.win_sel('WYB'); UD_DEF.actions('WYB','DPU:D');
      UD_DEF.hdr_sel(); UD_DEF.hdr_sel(UD_SCH.OPIS);
      {? _sym='' || UD_DEF.first() ?};
      {? UD_DEF.select(,1,1)
      || _sym:=UD_DEF.UD_SKL().SYMBOL
      ?};
      UD_DEF.cntx_pop()
   ?};
   UD_SCH.cntx_pop()
?};
_sym


\m4_przyp_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie kartoteki do schematu danych
::----------------------------------------------------------------------------------------------------------------------
_size:=cur_tab(1,0).sel_size();
{? (cur_tab(1,0)=TYPYSP | cur_tab(1,0)=TYPYDOK)
|| {? ~_size
   || {? XINFO.MMODEW
      || ZMIENNE.SYSTEM:='EMAG';
         CON_DEF.win_edit('M4REDLOG');
         menu_txt(,'Dołącz');
         exec('zm_con_def','control_marza');
         {? CON_DEF.edit("exec('m4_przyp_log_after','control_marza')")
         || M4_ZM.PDATA_OD:=CON_DEF.M4DATAOD;
            M4_ZM.PDATA_DO:=CON_DEF.M4DATADO;
            {? exec('spr_con_def','control_marza')
            || CON_DEF.prefix(); CON_DEF.add()
            ?}
         ?};
         ZMIENNE.SYSTEM:=''
      || FUN.info('Nie ustawiono w stałych systemu modelu ewidencyjnego.'@)
      ?}
   || BUF.restore();
      {? cur_tab(1,0)=TYPYSP
      || CON_DEF.TYPYSP:=TYPYSP.ref()
      |? cur_tab(1,0)=TYPYDOK
      || CON_DEF.TYPYDOK:=TYPYDOK.ref()
      ?};
      M4_ZM.PDATA_OD:=CON_DEF.M4DATAOD;
      M4_ZM.PDATA_DO:=CON_DEF.M4DATADO;
      {? exec('spr_con_def','control_marza') || CON_DEF.prefix(); CON_DEF.add() ?}
   ?}
|| KH.cntx_psh(); KH.index('KOD'); KH.prefix();
   M.cntx_psh(); M.index('MATKTM'); M.prefix();
   _dalej:={? cur_tab(1,0)=TMPKHM4
           || KH.seek(TMPKHM4.REFKH,KH.name())
           |? cur_tab(1,0)=TMPMM4
           || M.seek(TMPMM4.REFM,M.name())
           |? cur_tab(1,0)=TMPUM4
           || M.seek(TMPUM4.REFM,M.name())
           || 1
           ?};
   {? _dalej
   || {? (((cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4 | cur_tab(1,0)=MGR) & XINFO.MSCHPROD) |
          ((cur_tab(1,0)=TMPKHM4 | cur_tab(1,0)=GRKH) & XINFO.MSCHKLI))
      || {? ~_size
         || exec('m4_przyp_sch_zm','control_marza')
         ?};
         {? _size | M4_ZM.edit("exec('m4_przyp_sch_after','control_marza')")
         || {? exec('spr_con_def','control_marza')
            || CON_DEF.cntx_psh(); CON_DEF.prefix();
               CON_DEF.blank(1);
               CON_DEF.JORG_S:={? M4_ZM.PRZYPISZ=1
                               || M4_ZM.UD_SKL
                               || {? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4 | cur_tab(1,0)=MGR
                                  || ($XINFO.MFORMPRO().FORMULA)()
                                  || ($XINFO.MFORMKLI().FORMULA)()
                                  ?}
                               ?};
               {? CON_DEF.JORG_S
               || {? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
                  || CON_DEF.M:=M.ref()
                  |? cur_tab(1,0)=MGR
                  || CON_DEF.M4_MGR:=MGR.ref()
                  |? cur_tab(1,0)=TMPKHM4
                  || CON_DEF.M4_KH:=KH.ref()
                  |? cur_tab(1,0)=GRKH
                  || CON_DEF.M4_GRKH:=GRKH.ref()
                  ?};
                  CON_DEF.M4_DEF:=1;
                  CON_DEF.M4DATAOD:={? var_pres('m4data_od')>0 || m4data_od || M4_ZM.PDATA_OD ?};
                  CON_DEF.M4DATADO:={? var_pres('m4data_do')>0 || m4data_do || M4_ZM.PDATA_DO ?};
                  CON_DEF.add();
                  {? cur_tab(1,0)=TMPKHM4 | cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
                  || exec('m4_zakres','control_marza',1)
                  ?}
               || _txt:={? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
                        || {? M.RODZ='T'
                           || 'Nie udało się ustalić przypisywanego elementu schematu dla materiału %1.'@[M.KTM]
                           || 'Nie udało się ustalić przypisywanego elementu schematu dla usługi %1.'@[M.KTM]
                           ?}
                        |? cur_tab(1,0)=MGR
                        || {? MGR.RODZ='T'
                           || 'Nie udało się ustalić przypisywanego elementu schematu dla grupy materiałów %1.'@[MGR.KOD]
                           || 'Nie udało się ustalić przypisywanego elementu schematu dla grupy usług %1.'@[MGR.KOD]
                           ?}
                        |? cur_tab(1,0)=TMPKHM4
                        || 'Nie udało się ustalić przypisywanego elementu schematu dla kontrahenta %1.'@[KH.KOD+' '+KH.SKR]
                        |? cur_tab(1,0)=GRKH
                        || 'Nie udało się ustalić przypisywanego elementu schematu dla grupy kontrahentów %1.'@[GRKH.KOD]
                        || ''
                        ?};
                  {? _size
                  || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
                  || FUN.info(_txt)
                  ?}
               ?};
               CON_DEF.cntx_pop()
            ?}
         ?}
      |? ~_size
      || {? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4 | cur_tab(1,0)=MGR
         || FUN.info('Nie ustawiono w parametrach systemu schematu Produkt.'@)
         |? cur_tab(1,0)=TMPKHM4 | cur_tab(1,0)=GRKH
         || FUN.info('Nie ustawiono w parametrach systemu schematu Klient.'@)
         ?}
      ?}
   ?};
   KH.cntx_pop(); M.cntx_pop()
?};
1


\m4_bg_przyp_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie kartoteki do schematu danych - grupa przed
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? (cur_tab(1,0)=TYPYSP | cur_tab(1,0)=TYPYDOK)
|| {? XINFO.MMODEW
   || ZMIENNE.SYSTEM:='EMAG';
      CON_DEF.win_edit('M4REDLOG');
      menu_txt(,'Dołącz');
      exec('zm_con_def','control_marza');
      _zwrot:=CON_DEF.edit("exec('m4_przyp_log_after','control_marza')");
      {? _zwrot
      || VAR_DEL.delete('BUF');
         BUF:=obj_new(@.CLASS.BUFFER,'CON_DEF');
         BUF.save();
         exec('ini_kom','#message','Przypisania definicji dla marży IV.'@)
      ?}
   || FUN.info('Nie ustawiono w stałych systemu modelu ewidencyjnego.'@)
   ?}
|| {? ((cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4 | cur_tab(1,0)=MGR) & XINFO.MSCHPROD) |
      ((cur_tab(1,0)=TMPKHM4 | cur_tab(1,0)=GRKH) & XINFO.MSCHKLI)
   || exec('m4_przyp_sch_zm','control_marza');
      _zwrot:=M4_ZM.edit("exec('m4_przyp_sch_after','control_marza')")
   || {? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4 | cur_tab(1,0)=MGR
      || FUN.info('Nie ustawiono w parametrach systemu schematu Produkt.'@)
      |? cur_tab(1,0)=TMPKHM4 | cur_tab(1,0)=GRKH
      || FUN.info('Nie ustawiono w parametrach systemu schematu Klient.'@)
      ?}
   ?};
   {? _zwrot
   || exec('ini_kom','#message','Informacje o przypisaniu do schematu.'@);
      m4data_od:=M4_ZM.PDATA_OD; m4data_do:=M4_ZM.PDATA_DO
   ?}
?};
_zwrot


\m4_ag_przyp_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie kartoteki do schematu danych - grupa po
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('m4data_od','m4data_do');
exec('end_kom','#message');
ZMIENNE.SYSTEM:='';
1


\zm_con_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Ustawia zmienne przed edycją CON_DEF
::----------------------------------------------------------------------------------------------------------------------
UD_POM.JORG:=UD_POM.PBUD:=UD_POM.OKOSZ:=UD_POM.WYM4:=UD_POM.WYM5:='';
CON_DEF.blank();
CON_DEF.M4DATAOD:=date();
CON_DEF.M4DATADO:=date(0,0,0);
CON_DEF.SKID_MB:=XINFO.MMODEW;
CON_DEF.LP:=0;
CON_DEF.M4_DEF:=1


\spr_con_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdzenie czy nie ma CON_DEF na podany zakres dat
::   WY: 1/0 - nie ma/jest
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1; _txt:='';
CON_DEF.cntx_psh();
{? cur_tab(1,0)=TYPYSP
|| CON_DEF.index('CONDEF13')
|? cur_tab(1,0)=TYPYDOK
|| CON_DEF.index('CONDEF14')
|? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
|| CON_DEF.index('CON_DEF8')
|? cur_tab(1,0)=MGR
|| CON_DEF.index('CON_DEF9')
|? cur_tab(1,0)=TMPKHM4
|| CON_DEF.index('CONDEF10')
|? cur_tab(1,0)=GRKH
|| CON_DEF.index('CONDEF11')
?};
KH.cntx_psh(); KH.index('KOD'); KH.prefix();
M.cntx_psh(); M.index('MATKTM'); M.prefix();
{? cur_tab(1,0)=TMPKHM4
|| {? KH.seek(TMPKHM4.REFKH,KH.name())
   || CON_DEF.prefix(1,KH.ref())
   || _zwrot:=0;
      _txt:='Nie znaleziono kontrahenta %1.'@[TMPKHM4.KOD]
   ?}
|? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
|| {? M.seek(cur_tab(1,0).REFM,M.name())
   || CON_DEF.prefix(1,M.ref())
   || _zwrot:=0;
      _txt:={? cur_tab(1,0)=TMPMM4
            || 'Nie znaleziono materiału %1.'@[TMPMM4.KTM]
            || 'Nie znaleziono usługi %1.'@[TMPUM4.KTM]
            ?}
   ?}
|| CON_DEF.prefix(1,cur_tab(1,0).ref())
?};
{? _zwrot
|| {? CON_DEF.first()
   || {! |?
         {? (M4_ZM.PDATA_DO=date(0,0,0) & CON_DEF.M4DATAOD>=M4_ZM.PDATA_OD) |
            (CON_DEF.M4DATADO<>date(0,0,0) & M4_ZM.PDATA_DO<>date(0,0,0) &
             M4_ZM.PDATA_OD>=CON_DEF.M4DATAOD & M4_ZM.PDATA_OD<=CON_DEF.M4DATADO) |
            (CON_DEF.M4DATADO<>date(0,0,0) & M4_ZM.PDATA_DO<>date(0,0,0) &
             M4_ZM.PDATA_DO>=CON_DEF.M4DATAOD & M4_ZM.PDATA_DO<=CON_DEF.M4DATADO)
         || _zwrot:=0
         ?};
         _zwrot & CON_DEF.next()
      !}
   ?};
   {? _zwrot
   || {? CON_DEF.last() & CON_DEF.M4DATADO=date(0,0,0) || CON_DEF.M4DATADO:=M4_ZM.PDATA_OD-1; CON_DEF.put() ?}
   || _size:=cur_tab(1,0).sel_size();
      _txt:={? cur_tab(1,0)=TYPYSP
            || 'Istnieje już przypisanie definicji dla marży IV na podany zakres dat dla typu dokumentu %1.'@[TYPYSP.T]
            |? cur_tab(1,0)=TYPYDOK
            || 'Istnieje już przypisanie definicji dla marży IV na podany zakres dat dla typu dokumentu %1.'@[TYPYDOK.T]
            |? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
            || {? M.RODZ='T'
               || 'Istnieje już przypisanie elementu schematu na podany zakres dat dla materiału %1.'@[M.KTM]
               || 'Istnieje już przypisanie elementu schematu na podany zakres dat dla usługi %1.'@[M.KTM]
               ?}
            |? cur_tab(1,0)=MGR
            || {? MGR.RODZ='T'
               || 'Istnieje już przypisanie elementu schematu na podany zakres dat dla grupy materiałów %1.'@[MGR.KOD]
               || 'Istnieje już przypisanie elementu schematu na podany zakres dat dla grupy usług %1.'@[MGR.KOD]
               ?}
            |? cur_tab(1,0)=TMPKHM4
            || 'Istnieje już przypisanie elementu schematu na podany zakres dat dla kontrahenta %1.'@[KH.KOD+' '+KH.SKR]
            |? cur_tab(1,0)=GRKH
            || 'Istnieje już przypisanie elementu schematu na podany zakres dat dla grupy kontrahentów %1.'@[GRKH.KOD]
            || ''
            ?}
   ?}
?};
CON_DEF.cntx_pop(); KH.cntx_pop(); M.cntx_pop();
{? ~_zwrot
|| {? _size
   || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
   || FUN.info(_txt)
   ?}
?};
_zwrot


\m4_przyp_sch_zm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie kartoteki do schematu danych
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4 | cur_tab(1,0)=MGR
|| exec('set_fml','#field',UD_POM,'JORG',
        "UD_POM.JORG:={? M4_ZM.UD_SKL || M4_ZM.UD_SKL().SYMBOL || '' ?};~~",
        "1",
        "_sym:=exec('f3_dim','control_marza',XINFO.MSCHPROD);
         {? _sym<>'' || fld(_sym) ?}
        ",
        "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHPROD);
         {? _ref
         || M4_ZM.UD_SKL:=_ref; fld(M4_ZM.UD_SKL().SYMBOL)
         || M4_ZM.UD_SKL:=null; fld('')
         ?};
         1
        "
        )
|| exec('set_fml','#field',UD_POM,'JORG',
        "UD_POM.JORG:={? M4_ZM.UD_SKL || M4_ZM.UD_SKL().SYMBOL || '' ?};~~",
        "1",
        "_sym:=exec('f3_dim','control_marza',XINFO.MSCHKLI);
         {? _sym<>'' || fld(_sym) ?}
        ",
        "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHKLI);
         {? _ref
         || M4_ZM.UD_SKL:=_ref; fld(M4_ZM.UD_SKL().SYMBOL)
         || M4_ZM.UD_SKL:=null; fld('')
         ?};
         1
        "
        )
?};
M4_ZM.win_edit('SCH'); M4_ZM.PRZYPISZ:=1; M4_ZM.UD_SKL:=null;
M4_ZM.PDATA_OD:=date(); M4_ZM.PDATA_DO:=date(0,0,0);
1


\m4_przyp_sch_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie kartoteki do schematu danych - po redakcji okienka
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? M4_ZM.PRZYPISZ=1 & ~M4_ZM.UD_SKL
|| FUN.info('Nie wskazano elementu schematu danych do którego ma być przypisanie.'@); _zwrot:='JORG'
?};
{? _zwrot='' & M4_ZM.PRZYPISZ=2 & (cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4 | cur_tab(1,0)=MGR) & ~XINFO.MFORMPRO
|| FUN.info('Nie wskazano w stałych systemu formuły na tworzenie elementów schematu Produkt.'@); _zwrot:='PRZYPISZ'
?};
{? _zwrot='' & M4_ZM.PRZYPISZ=2 & (cur_tab(1,0)=TMPKHM4 | cur_tab(1,0)=GRKH) & ~XINFO.MFORMKLI
|| FUN.info('Nie wskazano w stałych systemu formuły na tworzenie elementów schematu Klient.'@); _zwrot:='PRZYPISZ'
?};
{? _zwrot='' & M4_ZM.PDATA_OD=date(0,0,0)
|| FUN.info('Nie wprowadzono daty od.'@); _zwrot:='PDATA_OD'
?};
{? _zwrot='' & M4_ZM.PDATA_DO<>date(0,0,0) & M4_ZM.PDATA_OD>M4_ZM.PDATA_DO
|| FUN.info('Nieprawidłowy zakres dat.'@); _zwrot:='PDATA_OD'
?};
_zwrot


\m4_przyp_log_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie typu dokumentu do modelu - po redakcji okienka
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? CON_DEF.M4DATAOD=date(0,0,0)
|| FUN.info('Nie wprowadzono daty od.'@); _zwrot:='M4DATAOD'
?};
{? _zwrot='' & CON_DEF.M4DATADO<>date(0,0,0) & CON_DEF.M4DATAOD>CON_DEF.M4DATADO
|| FUN.info('Nieprawidłowy zakres dat.'@); _zwrot:='M4DATAOD'
?};
_zwrot


\m4_ae_przypisz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Po redakcji pola M4_ZM.PRZYPISZ
::----------------------------------------------------------------------------------------------------------------------
{? M4_ZM.PRZYPISZ=1
|| M4_ZM.efld_opt('SCH','enable=1',M4_ZM,'UD_SKL');
   M4_ZM.efld_opt('SCH','enable=1',UD_POM,'JORG')
|| M4_ZM.efld_opt('SCH','enable=0',M4_ZM,'UD_SKL');
   M4_ZM.efld_opt('SCH','enable=0',UD_POM,'JORG');
   UD_POM.JORG:=''; M4_ZM.UD_SKL:=null
?};
1


\m4_usun_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Usunięcie przypisania kartoteki do schematu danych
::----------------------------------------------------------------------------------------------------------------------
_size:=cur_tab(1,0).sel_size();
_txt:={? cur_tab(1,0)=TYPYSP | cur_tab(1,0)=TYPYDOK
      || 'Usunąć definicję dla marży IV?'@
      || 'Usunąć przypisanie do schematu?'@
      ?};
{? _size | FUN.ask(_txt)
|| CON_DEF.cntx_psh();
   {? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
   || CON_DEF.index('CON_DEF8')
   |? cur_tab(1,0)=MGR
   || CON_DEF.index('CON_DEF9')
   |? cur_tab(1,0)=TMPKHM4
   || CON_DEF.index('CONDEF10')
   |? cur_tab(1,0)=GRKH
   || CON_DEF.index('CONDEF11')
   |? cur_tab(1,0)=TYPYSP
   || CON_DEF.index('CONDEF13')
   |? cur_tab(1,0)=TYPYDOK
   || CON_DEF.index('CONDEF14')
   ?};
   _zwrot:=1;
   {? cur_tab(1,0)=TMPKHM4
   || KH.cntx_psh(); KH.index('KOD'); KH.prefix();
      {? KH.seek(TMPKHM4.REFKH,KH.name())
      || CON_DEF.prefix(1,KH.ref())
      || _zwrot:=0
      ?};
      KH.cntx_pop()
   |? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
   || M.cntx_psh(); M.index('MATKTM'); M.prefix();
      {? M.seek(cur_tab(1,0).REFM,M.name())
      || CON_DEF.prefix(1,M.ref())
      || _zwrot:=0
      ?};
      M.cntx_pop()
   || CON_DEF.prefix(1,cur_tab(1,0).ref())
   ?};
   {? _zwrot & exec('warunek_bds','control_marza')
   || CON_DEF.del();
      {? cur_tab(1,0)=TMPKHM4 | cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4 || exec('m4_zakres','control_marza',1) ?}
   ?};
   CON_DEF.cntx_pop()
?}


\m4_bg_usun_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Usunięcie przypisania kartoteki do schematu danych - grupa przed
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,0)=TYPYSP | cur_tab(1,0)=TYPYDOK
|| FUN.ask('Usunąć zaznaczone definicje dla marży IV?'@)
|| FUN.ask('Usunąć zaznaczone przypisania do schematu?'@)
?}


\m4_ag_usun_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Usunięcie przypisania kartoteki do schematu danych - grupa po
::----------------------------------------------------------------------------------------------------------------------
1


\m4_bds_def_zm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Na wyświetl dla zmiennej M4_ZM.SYMBOL i M4_ZM.DATA_OD
::   WE: _a - zadeklarowane - bez szukania rekordu bazowego
::----------------------------------------------------------------------------------------------------------------------
CON_DEF.cntx_psh();
{? cur_tab(1,0)=TYPYSP
|| M4_ZM.DATA_OD:=M4_ZM.DATA_DO:=date(0,0,0); M4_ZM.PRZYPDEF:=0;
   CON_DEF.index('CONDEF13'); CON_DEF.prefix(1,TYPYSP.ref());
   {? exec('warunek_bds','control_marza')
   || M4_ZM.DATA_OD:=CON_DEF.M4DATAOD;
      M4_ZM.DATA_DO:=CON_DEF.M4DATADO;
      M4_ZM.PRZYPDEF:=1
   ?}
|? cur_tab(1,0)=TYPYDOK
|| M4_ZM.DATA_OD:=M4_ZM.DATA_DO:=date(0,0,0); M4_ZM.PRZYPDEF:=0;
   CON_DEF.index('CONDEF14'); CON_DEF.prefix(1,TYPYDOK.ref());
   {? exec('warunek_bds','control_marza')
   || M4_ZM.DATA_OD:=CON_DEF.M4DATAOD;
      M4_ZM.DATA_DO:=CON_DEF.M4DATADO;
      M4_ZM.PRZYPDEF:=1
   ?}
|| M4_ZM.DATA_OD:=M4_ZM.DATA_DO:=date(0,0,0); M4_ZM.SYMBOL:=M4_ZM.OPIS:=''; M4_ZM.PRZYP_GR:=0;
   {? cur_tab(1,0)=MGR
   || CON_DEF.index('CON_DEF9'); CON_DEF.prefix(1,MGR.ref());
      {? exec('warunek_bds','control_marza')
      || M4_ZM.SYMBOL:=CON_DEF.JORG_S().SYMBOL;
         M4_ZM.DATA_OD:=CON_DEF.M4DATAOD;
         M4_ZM.DATA_DO:=CON_DEF.M4DATADO;
         M4_ZM.OPIS:=UD_SKL.OPIS;
         M4_ZM.PRZYP_GR:=1
      ?}
   |? cur_tab(1,0)=GRKH
   || CON_DEF.index('CONDEF11'); CON_DEF.prefix(1,GRKH.ref());
      {? exec('warunek_bds','control_marza')
      || M4_ZM.SYMBOL:=CON_DEF.JORG_S().SYMBOL;
         M4_ZM.DATA_OD:=CON_DEF.M4DATAOD;
         M4_ZM.DATA_DO:=CON_DEF.M4DATADO;
         M4_ZM.OPIS:=UD_SKL.OPIS;
         M4_ZM.PRZYP_GR:=1
      ?}
   ?}
?};
CON_DEF.cntx_pop();
1


\warunek_bds
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Warunek na wyświetlanie zmiennych
::   WE: _a - zadeklarowane - na datę - wpw data bieżąca
::----------------------------------------------------------------------------------------------------------------------
_data:={? _ || _a || date() ?};
CON_DEF.find_le(_data) & CON_DEF.M4DATAOD<=_data &
(CON_DEF.M4DATADO>=_data | CON_DEF.M4DATADO=date(0,0,0))


\m4_delcondef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Usuwanie powiązania z elementem schematu
::----------------------------------------------------------------------------------------------------------------------
_size:=CON_DEF.sel_size();
_pyt:={? var_pres('przypsch')>0 & przypsch=0
      || 'Usunąć definicję dla marży IV?'@
      || 'Usunąć przypisanie do schematu?'@
      ?};
{? _size | FUN.ask(_pyt)
|| CON_DEF.del()
?}


\m4_bgdelcondef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Usuwanie powiązania z elementem schematu - grupa przed
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('przypsch')>0 & przypsch=0
|| FUN.ask('Usunąć zaznaczone definicje dla marży IV?'@)
|| FUN.ask('Usunąć zaznaczone przypisania do schematu?'@)
?}


\m4_przyp_sch_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Schemat produkt - select
::----------------------------------------------------------------------------------------------------------------------
przypsch:=1; _zwrot:=1;
CON_DEF.cntx_psh();
{? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
|| CON_DEF.index('CON_DEF8')
|? cur_tab(1,0)=MGR
|| CON_DEF.index('CON_DEF9')
|? cur_tab(1,0)=TMPKHM4
|| CON_DEF.index('CONDEF10')
|? cur_tab(1,0)=GRKH
|| CON_DEF.index('CONDEF11')
|? cur_tab(1,0)=TYPYSP
|| CON_DEF.index('CONDEF13'); przypsch:=0
|? cur_tab(1,0)=TYPYDOK
|| CON_DEF.index('CONDEF14'); przypsch:=0
?};
{? cur_tab(1,0)=TMPKHM4
|| KH.cntx_psh(); KH.index('KOD'); KH.prefix();
   {? KH.seek(TMPKHM4.REFKH,KH.name())
   || CON_DEF.prefix(1,KH.ref())
   || FUN.info('Nie znaleziono kontrahenta %1.'@[TMPKHM4.KOD]);
      _zwrot:=0
   ?};
   KH.cntx_pop()
|? cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
|| M.cntx_psh(); M.index('MATKTM'); M.prefix();
   {? M.seek(cur_tab(1,0).REFM,M.name())
   || CON_DEF.prefix(1,M.ref())
   || _txt:={? cur_tab(1,0)=TMPMM4
            || FUN.info('Nie znaleziono materiału %1.'@[TMPMM4.KTM])
            || FUN.info('Nie znaleziono usługi %1.'@[TMPUM4.KTM])
            ?};
      _zwrot:=0
   ?};
   M.cntx_pop()
|| CON_DEF.prefix(1,cur_tab(1,0).ref())
?};
{? _zwrot
|| {? cur_tab(1,0)=TYPYSP | cur_tab(1,0)=TYPYDOK
   || CON_DEF.win_sel('M4_WER2')
   || CON_DEF.win_sel('M4_WER1')
   ?};
   {? cur_tab(1,0)=TYPYSP | cur_tab(1,0)=TYPYDOK
   || CON_DEF.win_edit('M4REDLOG');
      exec('set_fml','!ctr_pco_zldk')
   || CON_DEF.win_edit('')
   ?};
   CON_DEF.select();
   {? cur_tab(1,0)=TMPKHM4 | cur_tab(1,0)=TMPMM4 | cur_tab(1,0)=TMPUM4
   || exec('m4_zakres','control_marza',1)
   ?}
?};
VAR_DEL.delete('przypsch');
CON_DEF.cntx_pop();
1


\bobs_gr_mat_usl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przed obsługą grup materiałów \ usług
::   WE: _a - 'T' - materiały
::            'U' - usługi
::----------------------------------------------------------------------------------------------------------------------
MGR.index('KOD'); MGR.prefix(_a);
MGR.first();
1


\bobs_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przed obsługą kontrahentów
::----------------------------------------------------------------------------------------------------------------------
exec('m4_zakres','control_marza');
TMPKHM4.first();
1


\m4_zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Zakres danych
::   WE: _a - opcja - odświeżanie dla bieżącego rekordu
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab(1,0);
{? _tab=TMPKHM4
|| {? _
   || TMPKHM4.SYMBOL:=TMPKHM4.OPIS:='';
      TMPKHM4.DATA_OD:=TMPKHM4.DATA_DO:=date(0,0,0);
      TMPKHM4.PRZYP_GR:='N';
      KH.cntx_psh(); KH.index('KOD'); KH.prefix(2,TMPKHM4.KOD,);
      {? KH.first()
      || exec('m4_zakres_kh','control_marza')
      ?};
      KH.cntx_pop();
      TMPKHM4.put()
   || TMPKHM4.erase();
      KH.cntx_psh(); KH.index('KOD'); KH.prefix(2);
      {? KH.first()
      || {! |?
            TMPKHM4.blank();
            TMPKHM4.PRZYP_GR:='N';
            TMPKHM4.KOD:=KH.KOD;
            TMPKHM4.SKR:=KH.SKR;
            TMPKHM4.NIP:=KH.NIP;
            TMPKHM4.NAZ_P:=KH.NAZ_P;
            {? KH.GRKH
            || TMPKHM4.GRUPAKOD:=KH.GRKH().KOD;
               TMPKHM4.GRUPANAZ:=GRKH.NAZ
            ?};
            TMPKHM4.REFKH:=KH.ref();
            exec('m4_zakres_kh','control_marza');
            TMPKHM4.add();
            KH.next()
         !}
      ?};
      KH.cntx_pop()
   ?}
|? _tab=TMPMM4 | _tab=TMPUM4
|| {? _
   || _tab.SYMBOL:=_tab.OPIS:='';
      _tab.DATA_OD:=_tab.DATA_DO:=date(0,0,0);
      _tab.PRZYP_GR:='N';
      M.cntx_psh(); M.index('MATKTM'); M.prefix(_tab.KTM,_tab.KTM);
      {? M.first()
      || exec('m4_zakres_m','control_marza')
      ?};
      M.cntx_pop();
      _tab.put()
   || _tab.erase();
      M.cntx_psh();
      exec('zakres_set_towar','material');
      {? M.first()
      || {! |?
            _tab.blank();
            _tab.PRZYP_GR:='N';
            _tab.KTM:=M.KTM;
            _tab.N:=M.N;
            _tab.JM:={? M.J || M.J().KOD || '' ?};
            _tab.REFM:=M.ref();
            {? M.MGR
            || _tab.GRUPAKOD:=M.MGR().KOD;
               _tab.GRUPANAZ:=MGR.NAZ
            ?};
            exec('m4_zakres_m','control_marza');
            _tab.add();
            M.next()
         !}
      ?};
      M.cntx_pop()
   ?}
?}


\m4_zakres_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Zakres danych dla kontrahentów - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
TMPKHM4.PRZYP_GR:='N';
CON_DEF.cntx_psh(); CON_DEF.index('CONDEF10'); CON_DEF.prefix(1,KH.ref());
{? exec('warunek_bds','control_marza')
|| TMPKHM4.SYMBOL:=CON_DEF.JORG_S().SYMBOL;
   TMPKHM4.DATA_OD:=CON_DEF.M4DATAOD;
   TMPKHM4.DATA_DO:=CON_DEF.M4DATADO;
   TMPKHM4.OPIS:=UD_SKL.OPIS
?};
{? TMPKHM4.SYMBOL='' & KH.GRKH
|| CON_DEF.index('CONDEF11'); CON_DEF.prefix(1,KH.GRKH);
   {? exec('warunek_bds','control_marza')
   || TMPKHM4.SYMBOL:=CON_DEF.JORG_S().SYMBOL;
      TMPKHM4.DATA_OD:=CON_DEF.M4DATAOD;
      TMPKHM4.DATA_DO:=CON_DEF.M4DATADO;
      TMPKHM4.OPIS:=UD_SKL.OPIS;
      TMPKHM4.PRZYP_GR:='T'
   ?}
?};
CON_DEF.cntx_pop()


\m4_zakres_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Zakres danych dla materiałów i usług - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab(1,0);
_tab.PRZYP_GR:='N';
CON_DEF.index('CON_DEF8'); CON_DEF.prefix(1,M.ref());
{? exec('warunek_bds','control_marza')
|| _tab.SYMBOL:=CON_DEF.JORG_S().SYMBOL;
   _tab.DATA_OD:=CON_DEF.M4DATAOD;
   _tab.DATA_DO:=CON_DEF.M4DATADO;
   _tab.OPIS:=UD_SKL.OPIS
?};
{? _tab.SYMBOL='' & M.MGR
|| CON_DEF.index('CON_DEF9'); CON_DEF.prefix(1,M.MGR);
   {? exec('warunek_bds','control_marza')
   || _tab.SYMBOL:=CON_DEF.JORG_S().SYMBOL;
      _tab.DATA_OD:=CON_DEF.M4DATAOD;
      _tab.DATA_DO:=CON_DEF.M4DATADO;
      _tab.OPIS:=UD_SKL.OPIS;
      _tab.PRZYP_GR:='T'
   ?}
?}


\m4_tmpkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Tworzenie tabelki tymczasowej z kontrahentami
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TMPKHM4');
TMPKHM4:=tab_tmp(2,'KOD','STRING[8]','Kod'@,
                   'SKR','STRING[10]','Skrót'@,
                   'NIP','STRING[20]','NIP'@,
                   'NAZ_P','STRING[150]','Nazwa'@,
                   'SYMBOL','STRING[16]','Klient'@,
                   'OPIS','STRING[60]',''@,
                   'PRZYP_GR','STRING[1]','Dla grupy'@,
                   'GRUPAKOD','STRING[20]','Kod grupy'@,
                   'GRUPANAZ','STRING[40]','Nazwa grupy'@,
                   'DATA_OD','DATE','Od dnia'@,
                   'DATA_DO','DATE','Do dnia'@,
                   'REFKH','INTEGER','Ref kh'
                );
_wer:=TMPKHM4.mk_sel('Kontrahenci'@,'P',0,'tmpkhm4_wer',,,,,'U');
TMPKHM4.win_fld(_wer,,'KOD');
TMPKHM4.win_fld(_wer,,'SKR');
TMPKHM4.win_fld(_wer,,'NAZ_P',,,22);
TMPKHM4.win_fld(_wer,,'NIP',,,20);
TMPKHM4.win_fld(_wer,,'SYMBOL',,,16,,,'Klient'@);
TMPKHM4.win_fld(_wer,,'OPIS',,,20);
TMPKHM4.win_fld(_wer,,'PRZYP_GR',,,9,,,'Dla grupy'@,,'Przypisanie dla grupy'@,2,,"'T'","'N'");
TMPKHM4.win_fld(_wer,,'GRUPAKOD',,,15);
TMPKHM4.win_fld(_wer,,'DATA_OD',,,10);
TMPKHM4.win_fld(_wer,,'DATA_DO',,,10);
TMPKHM4.win_act(_wer,,'Formuła','Przypisz do schematu Klient'@@,,'Przypisanie do schematu Klient'@,,
                "exec('m4_przyp_sch','control_marza')",1,
                1,"exec('m4_bg_przyp_sch','control_marza')","exec('m4_ag_przyp_sch','control_marza')");
TMPKHM4.win_act(_wer,,'Formuła','Usuń ze schematu Klient'@@,,'Usunięcie ze schematu Klient'@,,
                "exec('m4_usun_sch','control_marza')",,
                1,"exec('m4_bg_usun_sch','control_marza')","exec('m4_ag_usun_sch','control_marza')");
TMPKHM4.win_act(_wer,,'Formuła','Przypisania do s&chematu'@@,,'Przypisania do schematu Klient'@,,
                "exec('m4_przyp_sch_sel','control_marza')");
TMPKHM4.win_act(_wer,,'Formuła','Sc&hemat Klient'@@,,'Schemat Klient'@,,
                "exec('m4_sch','control_marza','K')");
TMPKHM4.win_act(_wer,1,'Formuła','Sc&hemat Klient'@@,,'Schemat Klient'@,,
                "exec('m4_sch','control_marza','K')",1);
TMPKHM4.win_act(_wer,,'Wyświetl',,,,"exec('dspkhm4','control_marza')");
TMPKHM4.win_act(_wer,,'Kolejność');
TMPKHM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Przypisz do schematu Klient'@],'menu:P');
TMPKHM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Usuń ze schematu Klient'@],'menu:U');
TMPKHM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Przypisania do s&chematu'@],'menu:C');
TMPKHM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=begin'['Sc&hemat Klient'@],'menu:H');
TMPKHM4.win_sel(_wer)


\dspkhm4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Na wyswietl dla kontrahentow
::  OLD: \dspkhnip/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
KH.cntx_psh(); KH.prefix();
{? KH.seek(TMPKHM4.REFKH,KH.name())
|| exec('kh_dod_ini','kontrahent');
   KH.win_edit('RED'); KH.display()
?};
KH.cntx_pop()


\m4_tmpm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Tworzenie tabelki tymczasowej z materiałami
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TMPMM4');
TMPMM4:=tab_tmp(2,'KTM','STRING[50]','Indeks'@,
                  'N','STRING[100]','Nazwa'@,
                  'JM','STRING[8]','jm'@,
                  'SYMBOL','STRING[16]','Produkt'@,
                  'OPIS','STRING[60]',''@,
                  'PRZYP_GR','STRING[1]','Dla grupy'@,
                  'GRUPAKOD','STRING[8]','Kod grupy'@,
                  'GRUPANAZ','STRING[40]','Nazwa grupy'@,
                  'DATA_OD','DATE','Od dnia'@,
                  'DATA_DO','DATE','Do dnia'@,
                  'REFM','INTEGER','Ref M'
                );
_wer:=TMPMM4.mk_sel('Materiały'@,'P',0,'tmpmm4_wer',,,,,'U');
TMPMM4.win_fld(_wer,,'KTM',,,15);
TMPMM4.win_fld(_wer,,'N',,,29);
TMPMM4.win_fld(_wer,,'JM',,,6);
TMPMM4.win_fld(_wer,,'SYMBOL',,,16,,,'Produkt'@);
TMPMM4.win_fld(_wer,,'OPIS',,,27);
TMPMM4.win_fld(_wer,,'PRZYP_GR',,,9,,,'Dla grupy'@,,'Przypisanie dla grupy'@,2,,"'T'","'N'");
TMPMM4.win_fld(_wer,,'GRUPAKOD',,,8);
TMPMM4.win_fld(_wer,,'DATA_OD',,,10);
TMPMM4.win_fld(_wer,,'DATA_DO',,,10);
TMPMM4.win_act(_wer,,'Formuła','De&finicje dla controllingu'@@,,'De&finicje dla controllingu'@,,
                "exec('con_def','!ctr_pco_zldk')");
TMPMM4.win_act(_wer,,'Formuła','Przypisz do schematu Produkt'@@,,'Przypisanie do schematu Produkt'@,,
                "exec('m4_przyp_sch','control_marza')",1,
                1,"exec('m4_bg_przyp_sch','control_marza')","exec('m4_ag_przyp_sch','control_marza')");
TMPMM4.win_act(_wer,,'Formuła','Usuń ze schematu Produkt'@@,,'Usunięcie ze schematu Produkt'@,,
                "exec('m4_usun_sch','control_marza')",,
                1,"exec('m4_bg_usun_sch','control_marza')","exec('m4_ag_usun_sch','control_marza')");
TMPMM4.win_act(_wer,,'Formuła','Przypisania do s&chematu'@@,,'Przypisania do schematu Produkt'@,,
                "exec('m4_przyp_sch_sel','control_marza')");
TMPMM4.win_act(_wer,,'Formuła','Sc&hemat Produkt'@@,,'Schemat Produkt'@,,
                "exec('m4_sch','control_marza','P')");
TMPMM4.win_act(_wer,1,'Formuła','Sc&hemat Produkt'@@,,'Schemat Produkt'@,,
                "exec('m4_sch','control_marza','P')",1);
TMPMM4.win_act(_wer,,'Wyświetl',,,,"exec('dspmm4','control_marza')");
TMPMM4.win_act(_wer,,'Kolejność');
TMPMM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Przypisz do schematu Produkt'@],'menu:P');
TMPMM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Usuń ze schematu Produkt'@],'menu:U');
TMPMM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Przypisania do s&chematu'@],'menu:C');
TMPMM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=begin'['Sc&hemat Produkt'@],'menu:H');
TMPMM4.win_sel(_wer)


\dspmm4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Na wyświetl dla materiałów i usług
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
M.cntx_psh(); M.index('MATKTM'); M.prefix();
{? M.seek(cur_tab(1,0).REFM,M.name())
|| exec('m_rek_przed','!zws_par_kmtr')
?};
M.display();
M.cntx_pop();
_zwrot


\m4_tmpu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Tworzenie tabelki tymczasowej z usługami
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TMPUM4');
TMPUM4:=tab_tmp(2,'KTM','STRING[50]','Indeks'@,
                  'N','STRING[100]','Nazwa'@,
                  'JM','STRING[8]','jm'@,
                  'SYMBOL','STRING[16]','Produkt'@,
                  'OPIS','STRING[60]',''@,
                  'PRZYP_GR','STRING[1]','Dla grupy'@,
                  'GRUPAKOD','STRING[20]','Kod grupy'@,
                  'GRUPANAZ','STRING[40]','Nazwa grupy'@,
                  'DATA_OD','DATE','Od dnia'@,
                  'DATA_DO','DATE','Do dnia'@,
                  'REFM','INTEGER','Ref M'
                );
_wer:=TMPUM4.mk_sel('Usługi'@,'P',0,'tmpum4_wer',,,,,'U');
TMPUM4.win_fld(_wer,,'KTM',,,15);
TMPUM4.win_fld(_wer,,'N',,,29);
TMPUM4.win_fld(_wer,,'JM',,,6);
TMPUM4.win_fld(_wer,,'SYMBOL',,,16,,,'Produkt'@);
TMPUM4.win_fld(_wer,,'OPIS',,,27);
TMPUM4.win_fld(_wer,,'PRZYP_GR',,,9,,,'Dla grupy'@,,'Przypisanie dla grupy'@,2,,"'T'","'N'");
TMPUM4.win_fld(_wer,,'GRUPAKOD',,,8);
TMPUM4.win_fld(_wer,,'DATA_OD',,,10);
TMPUM4.win_fld(_wer,,'DATA_DO',,,10);
TMPUM4.win_act(_wer,,'Formuła','De&finicje dla controllingu'@@,,'De&finicje dla controllingu'@,,
                "exec('con_def','!ctr_pco_zldk')");
TMPUM4.win_act(_wer,,'Formuła','Przypisz do schematu Produkt'@@,,'Przypisanie do schematu Produkt'@,,
                "exec('m4_przyp_sch','control_marza')",1,
                1,"exec('m4_bg_przyp_sch','control_marza')","exec('m4_ag_przyp_sch','control_marza')");
TMPUM4.win_act(_wer,,'Formuła','Usuń ze schematu Produkt'@@,,'Usunięcie ze schematu Produkt'@,,
                "exec('m4_usun_sch','control_marza')",,
                1,"exec('m4_bg_usun_sch','control_marza')","exec('m4_ag_usun_sch','control_marza')");
TMPUM4.win_act(_wer,,'Formuła','Przypisania do s&chematu'@@,,'Przypisania do schematu Produkt'@,,
                "exec('m4_przyp_sch_sel','control_marza')");
TMPUM4.win_act(_wer,,'Formuła','Sc&hemat Produkt'@@,,'Schemat Produkt'@,,
                "exec('m4_sch','control_marza','P')");
TMPUM4.win_act(_wer,1,'Formuła','Sc&hemat Produkt'@@,,'Schemat Produkt'@,,
                "exec('m4_sch','control_marza','P')",1);
TMPUM4.win_act(_wer,,'Wyświetl',,,,"exec('dspmm4','control_marza')");
TMPUM4.win_act(_wer,,'Kolejność');
TMPUM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Przypisz do schematu Produkt'@],'menu:P');
TMPUM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Usuń ze schematu Produkt'@],'menu:U');
TMPUM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Przypisania do s&chematu'@],'menu:C');
TMPUM4.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=begin'['Sc&hemat Produkt'@],'menu:H');
TMPUM4.win_sel(_wer)


\bobs_grkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przed obsługą grup kontrahentów
::----------------------------------------------------------------------------------------------------------------------
GRKH.index('KOD'); GRKH.prefix(); GRKH.first(); 1


\m4_ae_rodzaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Po redakcji pól XINFO.MSPR, XINFO.MMZPR, XINFO.MMWPR
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.MSPR=1
|| UD_POM.PBUD:=''; XINFO.MSPE:=null
|| XINFO.MSPF:=null
?};
{? XINFO.MMZPR=1
|| UD_POM.WYM4:=''; XINFO.MMZPE:=null
|| XINFO.MMZPF:=null
?};
{? XINFO.MMWPR=1
|| UD_POM.WYM5:=''; XINFO.MMWPE:=null
|| XINFO.MMWPF:=null
?};
win_disp();
1


\m4_gen_kp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Generowanie elementów wymiaru Klient-Produkt lub MPK-Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
{? UD_SCH.ref()=XINFO.MSCHKLPR
|| exec('m4_gen_klpr','control_marza')
|| exec('m4_gen_mpkklpr','control_marza')
?}


\m4_kharm_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Edycja harmonogramu budżetowego - generowanie schematu MPK-Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
{? K_HARM.SKID_MBN=XINFO.MMODBUD & K_HARM.OD=K_HARM.DO
|| exec('m4_gen_mpkklpr','control_marza')
?};
1


\m4_gen_mpkklpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Generowanie elementów wymiaru MPK-Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Wygenerować elementy schematu MPK-Klient-Produkt?'@)
|| _dalej:=1;
   {? ~XINFO.MSCHKLI
   || FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat Klient.'@); _dalej:=0
   ?};
   {? _dalej & ~XINFO.MSCHPROD
   || FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat Produkt.'@); _dalej:=0
   ?};
   {? _dalej & ~XINFO.MSCHKLPR
   || FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat Klient-Produkt.'@); _dalej:=0
   ?};
   {? _dalej & ~XINFO.MSCHMKLP
   || FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat MPK-Klient-Produkt.'@); _dalej:=0
   ?};
   {? _dalej & ~exec('spr_mpk_klpr','control_marza')
   || FUN.info('Nie uzupełniono wszystkich cech MPK dla schematu Klient-Produkt na bieżącą datę.'@);
      _dalej:=0
   ?};
   {? _dalej
   || exec('m4_gen_mpkklpr1','control_marza')
   ?}
?}


\m4_gen_mpkklpr1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Generowanie elementów wymiaru MPK-Klient-Produkt - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
_symbol:={? UD_DEF.get() || UD_DEF.SYMBOL || '' ?};
UD_DEF.cntx_psh();
UD_DEF.index('SYMBOL'); UD_DEF.prefix(XINFO.MSCHMKLP,null);
{? UD_DEF.first() || exec('del_sch','!zws_par_cmod',null) ?};
UD_DEF.cntx_pop();
UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix();
UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_SCH.prefix();
UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
{? XINFO.MSCHMKLP
|| XINFO.MSCHMKLP();
   exec('m4_el_grup_skl','control_marza','__DO_ROZLICZENIA','Elementy do rozliczenia');
   exec('m4_el_grup_skl','control_marza','__KLIENT-PRODUKT','Klient-produkt')
?};
UD_SCH.cntx_pop(); UD_TYP.cntx_pop(); UD_DEF.cntx_pop(); UD_SKL.cntx_pop();
UD_CECH.cntx_psh(); UD_CECH.index('UNIK'); UD_CECH.prefix(XINFO.MSCHKLPR,XINFO.MSCHMPK);
{? UD_CECH.first()
|| _nadrz1:=0;
   UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_DEF.prefix(XINFO.MSCHMKLP);
   UD_SKL.cntx_psh(); UD_DEF.cntx_psh(); UD_SCH.cntx_psh();
   {? UD_SKL.find_key(XINFO.MSCHMKLP().UD_TYP,'__DO_ROZLICZENIA','__DO_ROZLICZENIA') &
      UD_DEF.find_key(UD_SKL.ref())
   || _nadrz1:=#UD_DEF.ref()
   ?};
   _nadrz2:=0;
   {? UD_SKL.find_key(XINFO.MSCHMKLP().UD_TYP,'__KLIENT-PRODUKT','__KLIENT-PRODUKT') &
      UD_DEF.find_key(UD_SKL.ref())
   || _nadrz2:=#UD_DEF.ref()
   ?};
   UD_SKL.cntx_pop(); UD_DEF.cntx_pop(); UD_SCH.cntx_pop(); UD_DEF.cntx_pop();
   UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
   UD_WCECH.cntx_psh(); UD_WCECH.index('DISP');
   ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix();
   OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
   UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_DEF.prefix(XINFO.MSCHKLPR);
   {? UD_DEF.first()
   || {! |?
         {? (UD_DEF.OPIS*'Rozdzielnik')<>0 | (UD_DEF.SYMBOL*'_BRAK-_BRAK')<>0
         || exec('add_skl','control_marza',XINFO.MSCHMKLP,UD_DEF.SYMBOL,UD_DEF.OPIS,_nadrz1)
         || _zwrot:=0;
            UD_WCECH.prefix(UD_CECH.ref(),UD_DEF.UD_SKL);
            {? UD_WCECH.last()
            || {! |?
                  {? UD_WCECH.OKRO_F().POCZ<>date(0,0,0) & OKRO_F.KON<>date(0,0,0) & date()>=OKRO_F.POCZ
                  || UD_SKL.cntx_psh(); UD_SKL.prefix();
                     _skl:=exec('add_skl','control_marza',XINFO.MSCHMKLP,UD_WCECH.UD_SKLWY().SYMBOL,UD_SKL.OPIS,
                                _nadrz2);
                     {? #_skl[2]
                     || exec('add_skl','control_marza',XINFO.MSCHMKLP,UD_WCECH.UD_SKLWE().SYMBOL,UD_SKL.OPIS,#_skl[2])
                     ?};
                     obj_del(_skl);
                     UD_SKL.cntx_pop();
                     _zwrot:=1
                  ?};
                  ~_zwrot & UD_WCECH.prev()
               !}
            ?}
         ?};
         UD_DEF.next()
      !}
   ?};
   UD_DEF.cntx_pop(); UD_WCECH.cntx_pop(); UD_SKL.cntx_pop(); OKRO_F.cntx_pop(); ROK_F.cntx_pop()
?};
UD_CECH.cntx_pop();
{? _symbol<>''
|| UD_DEF.blank(1); UD_DEF.SYMBOL:=_symbol; UD_DEF.find_rec()
?}


\add_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Dodawanie elementów do schematu
::   WE: _a - schemat (UD_SCH.ref())
::       _b - symbol
::       _c - opis
::       _d - ref nadrzędnego UD_DEF
::  WY:  obiekt dwuelementowy _1 - UD_SKL.ref(), _2 - UD_DEF.ref()
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=obj_new(2); _zwrot[1]:=_zwrot[2]:=null;
UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
{? UD_SCH.seek(_a)
|| UD_DEF.cntx_psh(); UD_DEF.index('SCHSYM'); UD_DEF.prefix(_a,_b,);
   {? UD_DEF.first()
   || _zwrot[1]:=UD_DEF.UD_SKL; _zwrot[2]:=UD_DEF.ref()
   || _skl:=null;
      UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix(UD_SCH.UD_TYP,_b,);
      {? UD_SKL.first()
      || _skl:=UD_SKL.ref()
      || UD_SKL.blank(1);
         UD_SKL.UD_TYP:=UD_SCH.UD_TYP;
         UD_SKL.SYMBOL:=_b;
         UD_SKL.OPIS:=_c;
         UD_SKL.LISTA:='T';
         UD_SKL.AKTYWNY:='T';
         {? UD_SKL.add(1) || _skl:=UD_SKL.ref() ?}
      ?};
      UD_SKL.cntx_pop();
      {? _skl
      || _zwrot[1]:=_skl;
         UD_DEF.blank(1);
         UD_DEF.UD_SCH:=UD_SCH.ref();
         UD_DEF.UD_SKL:=_skl;
         UD_DEF.UD_POZ:=null;
         UD_DEF.SYMBOL:=_b;
         UD_DEF.OPIS:=_c;
         UD_DEF.UD_DEF:=_d;
         {? UD_DEF.add() || _zwrot[2]:=UD_DEF.ref() ?}
      ?}
   ?};
   UD_DEF.cntx_pop()
?};
UD_SCH.cntx_pop();
_zwrot


\spr_mpk_klpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdza czy są przypiete na bieżącą datę wszystkie cechy MPK dla schematu Klient-Produkt (poza rozdzielnikami)
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
UD_CECH.cntx_psh(); UD_CECH.index('UNIK'); UD_CECH.prefix(XINFO.MSCHKLPR,XINFO.MSCHMPK);
{? UD_CECH.first()
|| UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
   UD_WCECH.cntx_psh(); UD_WCECH.index('DISP');
   ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix();
   OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
   UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_DEF.prefix(XINFO.MSCHKLPR);
   {? UD_DEF.first()
   || {! |?
         {? (UD_DEF.OPIS*'Rozdzielnik')=0 & (UD_DEF.SYMBOL*'_BRAK-_BRAK')=0 & (2+UD_DEF.SYMBOL)<>'__'
         || _zwrot:=0;
            UD_WCECH.prefix(UD_CECH.ref(),UD_DEF.UD_SKL);
            {? UD_WCECH.last()
            || {! |?
                  {? UD_WCECH.OKRO_F().POCZ<>date(0,0,0) & OKRO_F.KON<>date(0,0,0) & date()>=OKRO_F.POCZ
                  || _zwrot:=1
                  ?};
                  ~_zwrot & UD_WCECH.prev()
               !}
            ?}
         ?};
         _zwrot & UD_DEF.next()
      !}
   ?};
   UD_DEF.cntx_pop(); UD_WCECH.cntx_pop(); UD_SKL.cntx_pop(); OKRO_F.cntx_pop(); ROK_F.cntx_pop()
|| _zwrot:=0
?};
UD_CECH.cntx_pop();
_zwrot


\m4_gen_klpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Generowanie elementów wymiaru Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? ~XINFO.MSCHKLI
|| FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat Klient.'@); _dalej:=0
?};
{? _dalej & ~XINFO.MSCHPROD
|| FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat Produkt.'@); _dalej:=0
?};
{? _dalej & ~XINFO.MSCHKLPR
|| FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat Klient-Produkt.'@); _dalej:=0
?};
{? _dalej & ~XINFO.MFORMKP
|| FUN.info('Nie wybrano w stałych systemu formuły na tworzenie symboli i opisów schematu Klient-Produkt.'@); _dalej:=0
?};
{? _dalej
|| UD_TYP.cntx_psh(); UD_SCH.cntx_psh();
   M4_ZM.UD_SKL1:=M4_ZM.UD_SKL2:=M4_ZM.UD_SKL3:=null;
   exec('szuk_okr','okresy',date(,1,1));
   M4_ZM.ROK_F:=M4_ZM.OKRO_F:=null;
   M4_ZM.OKRO_F:=ROZNE.UT_OKROD;
   {? ROZNE.UT_OKROD || M4_ZM.ROK_F:=ROZNE.UT_OKROD().ROK ?};
   exec('set_sch_kp','control_marza');
   M4_ZM.win_edit('K_P');
   {? M4_ZM.edit("exec('spr_k_p','control_marza')")
   || TMP_K_P:=tab_tmp(1,'K_P','STRING[1]','Klient / Produkt'@,
                         'REFUDSKL','INTEGER','Ref UD_SKL'@
                       );
      _jest_k:=_jest_p:=0; uddefadd:=0;
      {? M4_ZM.UD_SKL1
      || TMP_K_P.blank(1);
         TMP_K_P.K_P:='K';
         TMP_K_P.REFUDSKL:=M4_ZM.UD_SKL1;
         TMP_K_P.add();
         _jest_k:=1
      || UD_DEF.cntx_psh(); UD_DEF.index('SCHSYM'); UD_DEF.prefix(XINFO.MSCHKLI);
         UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
         {? UD_DEF.first()
         || {! |?
               UD_DEF.UD_SKL();
               TMP_K_P.blank(1);
               TMP_K_P.K_P:='K';
               TMP_K_P.REFUDSKL:=UD_SKL.ref();
               TMP_K_P.add(); _jest_k:=1;
               UD_DEF.next()
            !}
         ?};
         UD_DEF.cntx_pop(); UD_SKL.cntx_pop()
      ?};
      {? M4_ZM.UD_SKL2
      || TMP_K_P.blank(1);
         TMP_K_P.K_P:='P';
         TMP_K_P.REFUDSKL:=M4_ZM.UD_SKL2;
         TMP_K_P.add();
         _jest_p:=1
      || UD_DEF.cntx_psh(); UD_DEF.index('SCHSYM'); UD_DEF.prefix(XINFO.MSCHPROD);
         UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
         {? UD_DEF.first()
         || {! |?
               UD_DEF.UD_SKL();
               TMP_K_P.blank(1);
               TMP_K_P.K_P:='P';
               TMP_K_P.REFUDSKL:=UD_SKL.ref();
               TMP_K_P.add(); _jest_p:=1;
               UD_DEF.next()
            !}
         ?};
         UD_DEF.cntx_pop(); UD_SKL.cntx_pop()
      ?};
      {? _jest_k & _jest_p
      || UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
         TMP_K_P.prefix('K');
         {? TMP_K_P.first()
         || {! |?
               {? UD_SKL.seek(TMP_K_P.REFUDSKL,UD_SKL.name())
               || _ref_k:=UD_SKL.ref();
                  TMP_K_P.cntx_psh(); TMP_K_P.prefix('P');
                  {? TMP_K_P.first()
                  || {! |?
                        {? UD_SKL.seek(TMP_K_P.REFUDSKL,UD_SKL.name())
                        || _ref_p:=UD_SKL.ref();
                           exec('m4_gen_kp1','control_marza',_ref_k,_ref_p,M4_ZM.UD_SKL3)
                        ?};
                        TMP_K_P.next()
                     !}
                  ?};
                  TMP_K_P.cntx_pop()
               ?};
               TMP_K_P.next()
            !}
         ?};
         UD_SKL.cntx_pop()
      ?};
      FUN.info('Liczba dodanych elementów: %1.'@[$uddefadd]);
      VAR_DEL.delete('TMP_K_P','uddefadd')
   ?};
   UD_TYP.cntx_pop(); UD_SCH.cntx_pop()
?}


\m4_gen_kp1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Generowanie elementów wymiaru Klient-Produkt - wewnętrzna
::   WE: _a - ref klient
::       _b - ref produkt
::       _c - ref MPK
::       _d - ref UD_SKL Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
_skl:=null;
{? _=3 & type_of(_a)=type_of(null) & type_of(_b)=type_of(null) & type_of(_c)=type_of(null)
|| _zwrot:=($XINFO.MFORMKP().FORMULA)(_a,_b);
   {? type_of(_zwrot)>100 & obj_len(_zwrot)=3 &
      type_of(_zwrot[1])=2 & type_of(_zwrot[2])=2 & type_of(_zwrot[3])=2 & _zwrot[1]<>''
   || UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
      {? UD_SCH.seek(XINFO.MSCHKLPR)
      || UD_DEF.cntx_psh(); UD_DEF.index('SCHSYM');
         _nadrz:=null;
         {? _zwrot[3]<>''
         || UD_DEF.prefix(UD_SCH.ref(),_zwrot[3],);
            {? UD_DEF.first() || _nadrz:=UD_DEF.ref() ?}
         ?};
         UD_DEF.prefix(UD_SCH.ref(),_zwrot[1],);
         {? UD_DEF.first()
         || _skl:=UD_DEF.UD_SKL; exec('m4_gen_kp_cechy','control_marza',_a,_b,_c)
         || UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix(UD_SCH.UD_TYP,_zwrot[1],);
            {? UD_SKL.first()
            || _skl:=UD_SKL.ref()
            || UD_SKL.blank(1);
               UD_SKL.UD_TYP:=UD_SCH.UD_TYP;
               UD_SKL.SYMBOL:=_zwrot[1];
               UD_SKL.OPIS:=_zwrot[2];
               UD_SKL.LISTA:=UD_SKL.AKTYWNY:='T';
               {? UD_SKL.add(1) || _skl:=UD_SKL.ref() ?}
            ?};
            UD_SKL.cntx_pop();
            {? _skl
            || UD_DEF.blank(1);
               UD_DEF.UD_SCH:=UD_SCH.ref();
               UD_DEF.UD_DEF:=_nadrz;
               UD_DEF.UD_SKL:=_skl;
               UD_DEF.UD_POZ:=null;
               UD_DEF.SYMBOL:=_zwrot[1];
               UD_DEF.OPIS:=_zwrot[2];
               {? UD_DEF.add()
               || uddefadd+=1;
                  exec('m4_gen_kp_cechy','control_marza',_a,_b,_c)
               ?}
            ?}
         ?};
         UD_DEF.cntx_pop()
      ?};
      UD_SCH.cntx_pop()
   ?};
   obj_del(_zwrot)
?};
_skl


\m4_gen_kp_cechy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Generowanie elementów wymiaru Klient-Produkt - dodawanie cech
::   WE: _a - ref Klient (UD_SKL)
::       _b - ref Produkt (UD_SKL)
::       _c - ref MPK (UD_SKL)
::----------------------------------------------------------------------------------------------------------------------
UD_WCECH.cntx_psh(); UD_WCECH.index('UNIK');
UD_CECH.cntx_psh(); UD_CECH.index('UNIK'); UD_CECH.prefix(XINFO.MSCHKLPR,XINFO.MSCHKLI);
UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
_dalej:=1;
{? ~UD_CECH.first()
|| UD_CECH.blank(1);
   UD_CECH.UD_SCHWE:=XINFO.MSCHKLPR;
   UD_CECH.UD_SCHWY:=XINFO.MSCHKLI;
   UD_CECH.NAZWA:=XINFO.MSCHKLI().OPIS;
   _dalej:=UD_CECH.add()
?};
{? _dalej
|| UD_CECH.prefix(XINFO.MSCHKLPR,XINFO.MSCHPROD);
   {? ~UD_CECH.first()
   || UD_CECH.blank(1);
      UD_CECH.UD_SCHWE:=XINFO.MSCHKLPR;
      UD_CECH.UD_SCHWY:=XINFO.MSCHPROD;
      UD_CECH.NAZWA:=XINFO.MSCHPROD().OPIS;
      _dalej:=UD_CECH.add()
   ?}
?};
{? _dalej & XINFO.MSCHMPK
|| UD_CECH.prefix(XINFO.MSCHKLPR,XINFO.MSCHMPK);
   {? ~UD_CECH.first()
   || UD_CECH.blank(1);
      UD_CECH.UD_SCHWE:=XINFO.MSCHKLPR;
      UD_CECH.UD_SCHWY:=XINFO.MSCHMPK;
      UD_CECH.NAZWA:=XINFO.MSCHMPK().OPIS;
      _dalej:=UD_CECH.add()
   ?}
?};
{? _dalej
|| UD_CECH.prefix(XINFO.MSCHKLPR,XINFO.MSCHKLI);
   {? UD_CECH.first()
   || UD_WCECH.prefix(UD_CECH.ref(),UD_DEF.UD_SKL,M4_ZM.ROK_F,M4_ZM.OKRO_F);
      {? UD_WCECH.first()
      || UD_WCECH.UD_SKLWY:=_a;
         UD_WCECH.put()
      || UD_WCECH.blank(1);
         UD_WCECH.UD_CECH:=UD_CECH.ref();
         UD_WCECH.UD_SKLWE:=UD_DEF.UD_SKL;
         UD_WCECH.UD_SKLWY:=_a;
         UD_WCECH.ROK_F:=M4_ZM.ROK_F;
         UD_WCECH.OKRO_F:=M4_ZM.OKRO_F;
         UD_WCECH.add()
      ?}
   ?};
   UD_CECH.prefix(XINFO.MSCHKLPR,XINFO.MSCHPROD);
   {? UD_CECH.first()
   || UD_WCECH.prefix(UD_CECH.ref(),UD_DEF.UD_SKL,M4_ZM.ROK_F,M4_ZM.OKRO_F);
      {? UD_WCECH.first()
      || UD_WCECH.UD_SKLWY:=_b;
         UD_WCECH.put()
      || UD_WCECH.blank(1);
         UD_WCECH.UD_CECH:=UD_CECH.ref();
         UD_WCECH.UD_SKLWE:=UD_DEF.UD_SKL;
         UD_WCECH.UD_SKLWY:=_b;
         UD_WCECH.ROK_F:=M4_ZM.ROK_F;
         UD_WCECH.OKRO_F:=M4_ZM.OKRO_F;
         UD_WCECH.add()
      ?}
   ?};
   {? XINFO.MSCHMPK & _c
   || UD_CECH.prefix(XINFO.MSCHKLPR,XINFO.MSCHMPK);
      {? UD_CECH.first()
      || UD_WCECH.prefix(UD_CECH.ref(),UD_DEF.UD_SKL,M4_ZM.ROK_F,M4_ZM.OKRO_F);
         {? UD_WCECH.first()
         || UD_WCECH.UD_SKLWY:=_c;
            UD_WCECH.put()
         || UD_WCECH.blank(1);
            UD_WCECH.UD_CECH:=UD_CECH.ref();
            UD_WCECH.UD_SKLWE:=UD_DEF.UD_SKL;
            UD_WCECH.UD_SKLWY:=_c;
            UD_WCECH.ROK_F:=M4_ZM.ROK_F;
            UD_WCECH.OKRO_F:=M4_ZM.OKRO_F;
            UD_WCECH.add()
         ?}
      ?}
   ?}
?};
UD_CECH.cntx_pop(); UD_WCECH.cntx_pop(); UD_SCH.cntx_pop()


\spr_k_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdzenie parametrów przy generowaniu elementów schematu Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
{? ~M4_ZM.ROK_F
|| FUN.info('Nie wybrano roku początkowego.'@); 'ROK_F'
|? ~M4_ZM.OKRO_F
|| FUN.info('Nie wybrano okresu początkowego.'@); 'OKRO_F'
|| ''
?}


\set_sch_kp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Ustawia formuły dla UD_POM - wybór Klienta i Produktu
::----------------------------------------------------------------------------------------------------------------------
exec('set_fml','#field',UD_POM,'PBUD',
      "{? XINFO.MSCHMPK
       || M4_ZM.efld_opt('K_P','enable=1',M4_ZM,'UD_SKL3');
          M4_ZM.efld_opt('K_P','enable=1',UD_POM,'OKOSZ')
       || M4_ZM.efld_opt('K_P','enable=0',M4_ZM,'UD_SKL3');
          M4_ZM.efld_opt('K_P','enable=0',UD_POM,'OKOSZ')
       ?};
       UD_POM.PBUD:={? M4_ZM.UD_SKL1 || M4_ZM.UD_SKL1().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHKLI);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHKLI);
       {? _ref
       || M4_ZM.UD_SKL1:=_ref; fld(M4_ZM.UD_SKL1().SYMBOL)
       || M4_ZM.UD_SKL1:=null; fld('')
       ?};
       1
      "
   );
exec('set_fml','#field',UD_POM,'JORG',
      "UD_POM.JORG:={? M4_ZM.UD_SKL2 ||M4_ZM.UD_SKL2().SYMBOL || '' ?};~~",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHPROD);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHPROD);
       {? _ref
       || M4_ZM.UD_SKL2:=_ref; fld(M4_ZM.UD_SKL2().SYMBOL)
       || M4_ZM.UD_SKL2:=null; fld('')
       ?};
       1
      "
   );
exec('set_fml','#field',UD_POM,'OKOSZ',
      "{? XINFO.MSCHMPK
       || UD_POM.OKOSZ:={? M4_ZM.UD_SKL3 || M4_ZM.UD_SKL3().SYMBOL || '' ?}; 1
       || 0
       ?}",
      "{? XINFO.MSCHMPK || 1 || 0 ?}",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHMPK);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHMPK);
       {? _ref
       || M4_ZM.UD_SKL3:=_ref; fld(M4_ZM.UD_SKL3().SYMBOL)
       || M4_ZM.UD_SKL3:=null; fld('')
       ?};
       1
      "
   );
1


\m4_ae_rokf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Po redakcji pola M4_ZM.ROK_F
::----------------------------------------------------------------------------------------------------------------------
{? M4_ZM.ROK_F=null
|| M4_ZM.OKRO_F:=null
|? M4_ZM.OKRO_F<>null
|| OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(M4_ZM.ROK_F);
   {? ~OKRO_F.seek(M4_ZM.OKRO_F) ||  M4_ZM.OKRO_F:=null ?};
   OKRO_F.cntx_pop()
?};
1


\m4_be_okrof
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przed redakcją pola M4_ZM.OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? M4_ZM.ROK_F<>null
|| M4_ZM.ROK_F(); 1
|| 0
?}


\m4_kp_mpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie MPK do elementów schematu
::----------------------------------------------------------------------------------------------------------------------
_ud_def:=#UD_DEF.ref();
SCH_MPK:=tab_tmp(1,'SYM_KP','STRING[16]','Symbol'@,
                  'OPIS_KP','STRING[60]','Opis'@,
                  'SYM_MPK','STRING[16]','Symbol MPK'@,
                  'OPIS_MPK','STRING[60]','Opis MPK'@,
                  'ROK_F','STRING[20]','Od roku'@,
                  'OKRO_F','STRING[20]','Od okresu'@,
                  'REF_SKL','INTEGER','Ref UD_SKL',
                  'REF_DEF','INTEGER','Ref UD_DEF'
                );
_index1:=SCH_MPK.index('?');
_index2:=SCH_MPK.ndx_tmp('',1,'REF_DEF',,1);
{? UD_DEF.UD_SCH=XINFO.MSCHKLPR
|| _wer:=SCH_MPK.mk_sel('Elementy schematu Klient-Produkt'@,'P',0,'kp_mpk__wer',,,,,'U');
   SCH_MPK.win_fld(_wer,,'SYM_KP',,,,,,'Symbol Klient-Produkt'@);
   SCH_MPK.win_fld(_wer,,'OPIS_KP',,,40,,,'Opis Klient-Produkt'@)
|| _wer:=SCH_MPK.mk_sel('Elementy schematu pozycji budżetowych'@,'P',0,'pb_mpk__wer',,,,,'U');
   SCH_MPK.win_fld(_wer,,'SYM_KP',,,,,,'Symbol pozycji budżetowej'@);
   SCH_MPK.win_fld(_wer,,'OPIS_KP',,,40,,,'Opis pozycji budżetowej'@)
?};
SCH_MPK.win_fld(_wer,,'SYM_MPK');
SCH_MPK.win_fld(_wer,,'OPIS_MPK',,,40);
SCH_MPK.win_fld(_wer,,'ROK_F',,,7);
SCH_MPK.win_fld(_wer,,'OKRO_F',,,15);
SCH_MPK.win_act(_wer,,'Formuła','Przypisz MPK'@@,,'Przypisanie do MPK'@,,
                "exec('m4_kp_przyp_mpk','control_marza')",1,1,"exec('m4_bg_kp_przyp_mpk','control_marza')");
SCH_MPK.win_act(_wer,,'Formuła','Cechy'@@,,'Cechy elementu'@,,"exec('m4_kp_cechy','control_marza')");
SCH_MPK.win_act(_wer,,'Kolejność');
SCH_MPK.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Przypisz do MPK'@],'menu:P');
SCH_MPK.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Cechy'@],'menu:C');
SCH_MPK.win_sel(_wer);
UD_CECH.cntx_psh(); UD_CECH.index('UNIK'); UD_CECH.prefix(UD_DEF.UD_SCH,XINFO.MSCHMPK);
{? ~UD_CECH.first()
|| UD_CECH.blank(1);
   UD_CECH.UD_SCHWE:=UD_DEF.UD_SCH;
   UD_CECH.UD_SCHWY:=XINFO.MSCHMPK;
   UD_CECH.NAZWA:=XINFO.MSCHMPK().OPIS;
   UD_CECH.add()
?};
{? UD_CECH.first()
|| UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
   UD_WCECH.cntx_psh(); UD_WCECH.index('DISP');
   ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix();
   OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
   UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_DEF.prefix(UD_DEF.UD_SCH);
   {? UD_DEF.first()
   || {! |?
         SCH_MPK.blank(1);
         SCH_MPK.SYM_KP:=UD_DEF.SYMBOL;
         SCH_MPK.OPIS_KP:=UD_DEF.OPIS;
         SCH_MPK.REF_SKL:=UD_DEF.UD_SKL;
         SCH_MPK.REF_DEF:=UD_DEF.ref();
         _znal:=0;
         UD_WCECH.prefix(UD_CECH.ref(),UD_DEF.UD_SKL);
         {? UD_WCECH.last()
         || {! |?
               {? UD_WCECH.OKRO_F().POCZ<>date(0,0,0) & OKRO_F.KON<>date(0,0,0) & date()>=OKRO_F.POCZ
               || _znal:=1;
                  SCH_MPK.SYM_MPK:=UD_WCECH.UD_SKLWY().SYMBOL;
                  SCH_MPK.OPIS_MPK:=UD_SKL.OPIS;
                  SCH_MPK.ROK_F:=UD_WCECH.ROK_F().NAZ;
                  SCH_MPK.OKRO_F:=UD_WCECH.OKRO_F().NAZ
               ?};
               ~_znal & UD_WCECH.prev()
            !}
         ?};
         SCH_MPK.add();
         UD_DEF.next()
      !}
   ?};
   UD_DEF.cntx_pop(); UD_WCECH.cntx_pop(); UD_SKL.cntx_pop(); OKRO_F.cntx_pop(); ROK_F.cntx_pop()
?};
UD_CECH.cntx_pop();
_znal:=0;
{? _ud_def
|| SCH_MPK.index(_index2); SCH_MPK.prefix();
   {? SCH_MPK.find_key(_ud_def) || _znal:=1 ?}
?};
SCH_MPK.index(_index1); SCH_MPK.prefix();
{? ~_znal || SCH_MPK.first() ?};
SCH_MPK.select(,1);
VAR_DEL.delete('SCH_MPK')


\m4_kp_cechy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Cechy dla elementu wymiaru Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_DEF.prefix();
{? UD_DEF.seek(SCH_MPK.REF_DEF,UD_DEF.name())
|| exec('wyk_cech','!zws_par_cmod');
   exec('mpk_refresh','control_marza')
|| FUN.info('Nie znaleziono zaznaczonego elementu wymiaru Klient-Produkt.'@)
?};
UD_DEF.cntx_pop()


\m4_kp_przyp_mpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie MPK do elementów schematu Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1; _size:=SCH_MPK.sel_size();
{? ~XINFO.MSCHMPK
|| FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat MPK.'@); _dalej:=0
?};
{? _dalej
|| UD_TYP.cntx_psh(); UD_SCH.cntx_psh();
   {? ~_size
   || M4_ZM.UD_SKL3:=null;
      exec('szuk_okr','okresy',date(,1,1));
      M4_ZM.ROK_F:=M4_ZM.OKRO_F:=null;
      M4_ZM.OKRO_F:=ROZNE.UT_OKROD;
      {? ROZNE.UT_OKROD || M4_ZM.ROK_F:=ROZNE.UT_OKROD().ROK ?};
      exec('set_sch_kp_mpk','control_marza');
      M4_ZM.win_edit('K_P_MPK')
   ?};
   {? _size | M4_ZM.edit("exec('spr_k_p_mpk','control_marza')")
   || UD_CECH.cntx_psh(); UD_CECH.index('UNIK'); UD_CECH.prefix(UD_DEF.UD_SCH,XINFO.MSCHMPK);
      UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
      {? UD_CECH.first() & UD_SKL.seek(SCH_MPK.REF_SKL,UD_SKL.name())
      || UD_WCECH.cntx_psh();
         UD_WCECH.index('UNIK'); UD_WCECH.prefix(UD_CECH.ref(),UD_SKL.ref(),M4_ZM.ROK_F,M4_ZM.OKRO_F);
         {? UD_WCECH.first()
         || UD_WCECH.UD_SKLWY:=M4_ZM.UD_SKL3;
            UD_WCECH.put()
         || UD_WCECH.blank(1);
            UD_WCECH.UD_CECH:=UD_CECH.ref();
            UD_WCECH.UD_SKLWE:=UD_SKL.ref();
            UD_WCECH.UD_SKLWY:=M4_ZM.UD_SKL3;
            UD_WCECH.ROK_F:=M4_ZM.ROK_F;
            UD_WCECH.OKRO_F:=M4_ZM.OKRO_F;
            UD_WCECH.add()
         ?};
         exec('mpk_refresh','control_marza');
         UD_WCECH.cntx_pop()
      ?};
      UD_CECH.cntx_pop(); UD_SKL.cntx_pop()
   ?};
   UD_TYP.cntx_pop(); UD_SCH.cntx_pop()
?}


\m4_bg_kp_przyp_mpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie MPK do elementów schematu Klient-Produkt - grupa przed
::----------------------------------------------------------------------------------------------------------------------
M4_ZM.UD_SKL3:=null;
exec('szuk_okr','okresy',date(,1,1));
M4_ZM.ROK_F:=M4_ZM.OKRO_F:=null;
M4_ZM.OKRO_F:=ROZNE.UT_OKROD;
{? ROZNE.UT_OKROD || M4_ZM.ROK_F:=ROZNE.UT_OKROD().ROK ?};
exec('set_sch_kp_mpk','control_marza');
M4_ZM.win_edit('K_P_MPK');
M4_ZM.edit("exec('spr_k_p_mpk','control_marza')")


\mpk_refresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Odświeża MPK dla elementów Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix();
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
UD_CECH.cntx_psh(); UD_CECH.index('UNIK'); UD_CECH.prefix(UD_DEF.UD_SCH,XINFO.MSCHMPK);
UD_WCECH.cntx_psh(); UD_WCECH.index('DISP');
{? UD_SKL.seek(SCH_MPK.REF_SKL,UD_SKL.name()) & UD_CECH.first()
|| SCH_MPK.SYM_MPK:=SCH_MPK.OPIS_MPK:=SCH_MPK.ROK_F:=SCH_MPK.OKRO_F:='';
   UD_WCECH.prefix(UD_CECH.ref(),UD_SKL.ref()); _znal:=0;
   {? UD_WCECH.last()
   || {! |?
         {? UD_WCECH.OKRO_F().POCZ<>date(0,0,0) & OKRO_F.KON<>date(0,0,0) & date()>=OKRO_F.POCZ
         || _znal:=1;
            SCH_MPK.SYM_MPK:=UD_WCECH.UD_SKLWY().SYMBOL;
            SCH_MPK.OPIS_MPK:=UD_SKL.OPIS;
            SCH_MPK.ROK_F:=OKRO_F.ROK().NAZ;
            SCH_MPK.OKRO_F:=OKRO_F.NAZ
         ?};
         ~_znal & UD_WCECH.prev()
      !}
   ?};
   SCH_MPK.put()
?};
UD_CECH.cntx_pop(); UD_WCECH.cntx_pop(); UD_SKL.cntx_pop(); ROK_F.cntx_pop(); OKRO_F.cntx_pop()


\spr_k_p_mpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdzenie parametrów przy przypisywaniu MPK do elementów schematu Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
{? ~M4_ZM.UD_SKL3
|| FUN.info('Nie wybrano MPK do przypisania.'@); 'OKOSZ'
|? ~M4_ZM.ROK_F
|| FUN.info('Nie wybrano roku początkowego.'@); 'ROK_F'
|? ~M4_ZM.OKRO_F
|| FUN.info('Nie wybrano okresu początkowego.'@); 'OKRO_F'
|| ''
?}


\set_sch_kp_mpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Klient produkt - ustawia formuły dla MPK
::----------------------------------------------------------------------------------------------------------------------
exec('set_fml','#field',UD_POM,'OKOSZ',
      "UD_POM.OKOSZ:={? M4_ZM.UD_SKL3 || M4_ZM.UD_SKL3().SYMBOL || '' ?}; 1",
      "1",
      "_sym:=exec('f3_dim','control_marza',XINFO.MSCHMPK);
       {? _sym<>'' || fld(_sym) ?}
      ",
      "_ref:=exec('ud_def_symbol_ae','schemat',XINFO.MSCHMPK);
       {? _ref
       || M4_ZM.UD_SKL3:=_ref; fld(M4_ZM.UD_SKL3().SYMBOL)
       || M4_ZM.UD_SKL3:=null; fld('')
       ?};
       1
      "
   );
1


\m4_kp_gen_rozdz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Tworzenie rozdzielnika dla MPK
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? ~XINFO.MSCHKLPR
|| FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat Klient-Produkt.'@); _dalej:=0
?};
{? _dalej & ~XINFO.MSCHMPK
|| FUN.info('Nie wybrano w stałych systemu schematu oznaczonego jako schemat MPK.'@); _dalej:=0
?};
{? _dalej & ~XINFO.MFORMKPR
|| FUN.info('Nie wybrano w stałych systemu formuły na tworzenie symboli i opisów rozdzielników'+
            ' MPK schematu Klient-Produkt.'@);
   _dalej:=0
?};
{? _dalej & ~XINFO.MDOMKL
|| FUN.info('Nie wybrano w stałych systemu domyślnego elementu schematu Klient.'@); _dalej:=0
?};
{? _dalej & ~XINFO.MDOMPR
|| FUN.info('Nie wybrano w stałych systemu domyślnego elementu schematu Produkt.'@); _dalej:=0
?};
{? _dalej
|| M4_ZM.UD_SKL3:=null;
   exec('szuk_okr','okresy',date(,1,1));
   M4_ZM.ROK_F:=M4_ZM.OKRO_F:=null;
   M4_ZM.OKRO_F:=ROZNE.UT_OKROD;
   {? ROZNE.UT_OKROD || M4_ZM.ROK_F:=ROZNE.UT_OKROD().ROK ?};
   exec('set_sch_kp_mpk','control_marza');
   M4_ZM.win_edit('KPROZMPK');
   {? M4_ZM.edit("exec('spr_mpk_rozdz','control_marza')")
   || uddefadd:=0;
      UD_DEF.cntx_psh(); UD_DEF.index('SCHSYM'); UD_DEF.prefix(XINFO.MSCHMPK);
      UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
      {? M4_ZM.UD_SKL3
      || M4_ZM.UD_SKL3();
         exec('m4_kp_gen_rozdz1','control_marza')
      || {? UD_DEF.first()
         || {! |?
               UD_DEF.UD_SKL();
               exec('m4_kp_gen_rozdz1','control_marza');
               UD_DEF.next()
            !}
         ?}
      ?};
      UD_DEF.cntx_pop(); UD_SKL.cntx_pop();
      FUN.info('Liczba dodanych elementów: %1.'@[$uddefadd]);
      VAR_DEL.delete('uddefadd')
   ?}
?}


\m4_kp_gen_rozdz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Tworzenie rozdzielnika dla MPK - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
_mpk:=UD_SKL.ref();
_zwrot:=($XINFO.MFORMKPR().FORMULA)(UD_SKL.SYMBOL,UD_SKL.OPIS);
{? type_of(_zwrot)>100 & obj_len(_zwrot)=3 &
   type_of(_zwrot[1])=2 & type_of(_zwrot[2])=2 & type_of(_zwrot[3])=2 & _zwrot[1]<>''
|| UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
   {? UD_SCH.seek(XINFO.MSCHKLPR)
   || UD_DEF.cntx_psh(); UD_DEF.index('SCHSYM');
      _nadrz:=null;
      {? _zwrot[3]<>''
      || UD_DEF.prefix(UD_SCH.ref(),_zwrot[3],);
         {? UD_DEF.first() || _nadrz:=UD_DEF.ref() ?}
      ?};
      UD_DEF.prefix(UD_SCH.ref(),_zwrot[1],);
      {? UD_DEF.first()
      || exec('m4_gen_kp_cechy','control_marza',XINFO.MDOMKL,XINFO.MDOMPR,_mpk)
      || _skl:=null;
         UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix(UD_SCH.UD_TYP,_zwrot[1],);
         {? UD_SKL.first()
         || _skl:=UD_SKL.ref()
         || UD_SKL.blank(1);
            UD_SKL.UD_TYP:=UD_SCH.UD_TYP;
            UD_SKL.SYMBOL:=_zwrot[1];
            UD_SKL.OPIS:=_zwrot[2];
            UD_SKL.LISTA:=UD_SKL.AKTYWNY:='T';
            {? UD_SKL.add(1) || _skl:=UD_SKL.ref() ?}
         ?};
         UD_SKL.cntx_pop();
         {? _skl
         || UD_DEF.blank(1);
            UD_DEF.UD_SCH:=UD_SCH.ref();
            UD_DEF.UD_DEF:=_nadrz;
            UD_DEF.UD_SKL:=_skl;
            UD_DEF.UD_POZ:=null;
            UD_DEF.SYMBOL:=_zwrot[1];
            UD_DEF.OPIS:=_zwrot[2];
            {? UD_DEF.add()
            || exec('m4_gen_kp_cechy','control_marza',XINFO.MDOMKL,XINFO.MDOMPR,_mpk);
               {? var_pres('uddefadd')>0 || uddefadd+=1 ?}
            ?}
         ?}
      ?};
      UD_DEF.cntx_pop()
   ?};
   UD_SCH.cntx_pop()
?}


\spr_mpk_rozdz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdzenie parametrów przy tworzeniu rozdzielnika RMK dla wymiaru Klient-Produkt
::----------------------------------------------------------------------------------------------------------------------
{? ~M4_ZM.ROK_F
|| FUN.info('Nie wybrano roku początkowego.'@); 'ROK_F'
|? ~M4_ZM.OKRO_F
|| FUN.info('Nie wybrano okresu początkowego.'@); 'OKRO_F'
|| ''
?}


\m4_przyp_konta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie kont do pozycji budżetowej
::----------------------------------------------------------------------------------------------------------------------
exec('szuk_okr','okresy',date());
M4_ZM.ROK_F:=M4_ZM.OKRO_F:=null;
M4_ZM.OKRO_F:=ROZNE.UT_OKROD;
{? ROZNE.UT_OKROD || M4_ZM.ROK_F:=ROZNE.UT_OKROD().ROK ?};
M4_ZM.win_edit('ROK');
{? M4_ZM.edit("exec('spr_rok','control_marza')")
|| _ar:=SSTALE.AR; SSTALE.AR:=M4_ZM.ROK_F;
   ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix();
   BILANSP.ASYNT:=M4_ZM.ROK_F().SYNT;
   BILANSP.ASEP:=ROK_F.SEP;
   ROK_F.cntx_pop();
   VAR_DEL.delete('TMP_AN');
   TMP_AN:=tab_tmp(1,'SYM','STRING[20]','Symbol'@,
                     'OPIS','STRING[80]','Opis'@,
                     'PRZYP','STRING[1]','Przypisane'@,
                     'PRZYPSYM','STRING[16]','Przypisane do'@,
                     'PRZYPOPI','STRING[60]',''@
                   );
   UD_DEF.cntx_psh(); UD_DEF.index('SCHOPIS'); UD_DEF.prefix(UD_DEF.UD_SCH);
   AN.cntx_psh(); AN.use('koan__'+ROK_F.KOD); AN.index('WALSYM'); AN.prefix(FINFO.NAROD);
   {? AN.first()
   || {! |?
         exec('pw_opan','dok_fks_zest');
         TMP_AN.blank(1);
         TMP_AN.SYM:=AN.SYM;
         TMP_AN.OPIS:=KOMPEN.OPIS;
         TMP_AN.PRZYP:={? UD_DEF.find_key(AN.SYM) || 'T' || 'N' ?};
         {? TMP_AN.PRZYP='T' & UD_DEF.UD_DEF & UD_DEF.seek(UD_DEF.UD_DEF,UD_DEF.name())
         || TMP_AN.PRZYPSYM:=UD_DEF.SYMBOL;
            TMP_AN.PRZYPOPI:=UD_DEF.OPIS
         ?};
         TMP_AN.add();
         AN.next()
      !}
   ?};
   UD_DEF.cntx_pop(); AN.cntx_pop();
   _wer:=TMP_AN.mk_sel('Wybierz konta z roku %1'@[M4_ZM.ROK_F().NAZ],'P',0,'m4_tmp_an_wer',,,,,'U');
   TMP_AN.win_fld(_wer,,'SYM');
   TMP_AN.win_fld(_wer,,'OPIS',,,40);
   TMP_AN.win_fld(_wer,,'PRZYP',,,9,,,'Przypisane'@,,'Przypisanie do schematu'@,2,,"'T'","'N'");
   TMP_AN.win_fld(_wer,,'PRZYPSYM');
   TMP_AN.win_fld(_wer,,'PRZYPOPI',,,40);
   TMP_AN.win_act(_wer,,'Formuła','Przypisz konta'@@,,'Przypisanie kont'@,,"exec('m4_pb_konta','control_marza')",1,1);
   TMP_AN.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Przypisz konta'@],'menu:P');
   TMP_AN.win_act(_wer,,'Szukaj'@);
   TMP_AN.win_act(_wer,,'Kolejność'@);
   TMP_AN.win_sel(_wer);
   TMP_AN.select();
   SSTALE.AR:=_ar
?};
VAR_DEL.delete('TMP_AN')


\spr_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie kont do pozycji budżetowej - walidacja roku
::----------------------------------------------------------------------------------------------------------------------
{? ~M4_ZM.ROK_F
|| FUN.info('Nie wybrano roku bilansowego.'@); 'ROK_F'
|| ''
?}


\m4_skidxdud_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Dodaje rekord w SKIDSDUD z odpowiednią stroną
::   WE: _a - ref UD_SKL
::       _b - strona
::----------------------------------------------------------------------------------------------------------------------
SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ'); SKIDXDUD.prefix(_a);
{? ~SKIDXDUD.first()
|| SKIDXDUD.blank();
   SKIDXDUD.POZ_BUD:=_a;
   SKIDXDUD.WART_IL:='W';
   SKIDXDUD.JM:=null;
   SKIDXDUD.PREC:=0;
   SKIDXDUD.STR:=_b;
   SKIDXDUD.WSK_WAL:='N';
   SKIDXDUD.WAL:=null;
   SKIDXDUD.CIW:='W';
   SKIDXDUD.add()
?};
SKIDXDUD.cntx_pop()


\m4_zeskon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Zestawy kont
::----------------------------------------------------------------------------------------------------------------------
_ar:=SSTALE.AR;
KON_WYDR.cntx_psh(); KON_WYDR.index('DISP'); KON_WYDR.prefix(); KON_WYDR.actions('WER','F');
KON_SCH.cntx_psh(); KON_SCH.actions('M4_WER',':D');
rok_ref:=null;
M4_ZESK.cntx_psh(); M4_ZESK.index('DISP'); M4_ZESK.prefix(REF.FIRMA);
M4_ZESK.win_edit('RED');
_okn_grp:=M4_ZESK.grp_make('Zestawy kont dla analizy marży'@,,'m4_zesk_sel',,,,,'normal');
M4_ZESK.grp_sel(_okn_grp,M4_ZESK,'WER',,
                "KON_SCH.index('IND_LP');
                 {? KON_WYDR.get()
                 || M4_ZESK.KON_WYDR();
                    KON_SCH.prefix(KON_WYDR.ref());
                    KON_SCH.actions('M4_WER',,,1);
                    ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix();
                    BILANSP.ASYNT:=M4_ZESK.ROK_F().SYNT;
                    BILANSP.ASEP:=ROK_F.SEP;
                    SSTALE.AR:=M4_ZESK.ROK_F;
                    ROK_F.cntx_pop()
                 || KON_SCH.prefix(null);
                    KON_SCH.actions('M4_WER',':D',,1)
                 ?};
                 grp_disp(KON_SCH,'M4_WER',,1)
                ",,,10,"M4_ZESK.index('DISP'); M4_ZESK.prefix(REF.FIRMA)",,,,'maximized');
M4_ZESK.grp_splt(_okn_grp,,'horizontal','bottom');
M4_ZESK.grp_sel(_okn_grp,KON_SCH,'M4_WER',,,,,,,,,,'maximized');
M4_ZESK.win_sel(_okn_grp);
M4_ZESK.select();
M4_ZESK.cntx_pop(); KON_WYDR.cntx_pop(); KON_SCH.cntx_pop();
VAR_DEL.delete('rok_ref');
SSTALE.AR:=_ar


\m4_add_zesk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Zestawy kont - dołączanie
::----------------------------------------------------------------------------------------------------------------------
M4_ZESK.blank(1);
{? M4_ZESK.edit("exec('spr_zesk','control_marza')") || M4_ZESK.add() ?}


\m4_edi_zesk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Zestawy kont - poprawianie
::----------------------------------------------------------------------------------------------------------------------
{? M4_ZESK.edit("exec('spr_zesk','control_marza')") || M4_ZESK.put() ?}


\m4_del_zesk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Zestawy kont - usuwanie
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunać przypisanie zestawu kont dla controllingu?'@)
|| M4_ZESK.del()
?}


\ae_rok_zk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Po redakcji pola M4_ZESK.ROK_F
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? M4_ZESK.ROK_F=null
|| FUN.info('Nie wybrano zestawu roku bilansowego.'@); _zwrot:='ROK_F'
|? M4_ZESK.KON_WYDR
|| KON_WYDR.cntx_psh(); KON_WYDR.index('UNIK'); KON_WYDR.prefix(M4_ZESK.ROK_F);
   {? ~KON_WYDR.seek(M4_ZESK.KON_WYDR) || M4_ZESK.KON_WYDR:=null ?};
   KON_WYDR.cntx_pop()
?};
rok_ref:=M4_ZESK.ROK_F;
_zwrot


\spr_zesk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdzenie zestawu kont
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? M4_ZESK.KON_WYDR=null
|| FUN.info('Nie wybrano zestawu kont.'@); _zwrot:='KON_WYDR'
?};
{? _zwrot=''
|| {? -menu_txt='popraw' || _ref_gr:=M4_ZESK.ref() ?};
   _ref:=M4_ZESK.ref();
   M4_ZESK.cntx_psh(); M4_ZESK.index('UNIK'); M4_ZESK.prefix(M4_ZESK.ROK_F,M4_ZESK.RODZAJ);
   {? M4_ZESK.first() & ((-menu_txt()='popraw' & M4_ZESK.ref()<>_ref) | -menu_txt()<>'popraw')
   || FUN.info('Istnieje już zestaw kont danego rodzaju dla roku %1.'@[M4_ZESK.ROK_F().NAZ]);
      _zwrot:='ROK_F'
   ?};
   M4_ZESK.cntx_pop()
?};
_zwrot


\m4_bds_model_con_fun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Na wyświetl dla pola CON_FUN.SKID_MBN
::----------------------------------------------------------------------------------------------------------------------
{? CON_FUN.win_edit('?')='RED2'
|| {? CON_FUN.MODEL=null | XINFO.MMODEW=null | CON_FUN.MODEL<>XINFO.MMODEW
   || CON_FUN.efld_opt('RED2','enable=0',CON_FUN,'PAR10')
   || CON_FUN.efld_opt('RED2','enable=1',CON_FUN,'PAR10')
   ?}
?};
1


\m4_ae_model_con_fun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Na wyświetl dla pola CON_FUN.SKID_MBN
::----------------------------------------------------------------------------------------------------------------------
{? CON_FUN.win_edit('?')='RED2' & (CON_FUN.MODEL=null | XINFO.MMODEW=null | CON_FUN.MODEL<>XINFO.MMODEW)
|| CON_FUN.PAR10:=0; win_disp()
?};
1


\m4_dok_podz_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Tworzenie podziałów controllingowych dla dokumentów księgowych - sprawdzenie
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? CON_FUN.PAR10
|| OKRO_F.cntx_psh(); ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix(); OKRO_F.ROK();
   {? XINFO.MSCHWIEL=null
   || _ok:=0;
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,'W parametrach systemu nie ustawiono schematu Wielkość.'@,
           0,null,null,null,'')
   ?};
   {? ~XINFO.MSCHKLI
   || _ok:=0;
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,'W parametrach systemu nie ustawiono schematu Klient.'@,
           0,null,null,null,'')
   ?};
   {? ~XINFO.MSCHPROD
   || _ok:=0;
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,'W parametrach systemu nie ustawiono schematu Produkt.'@,
           0,null,null,null,'')
   ?};
   {? ~XINFO.MSCHKLPR
   || _ok:=0;
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,'W parametrach systemu nie ustawiono schematu Klient-Produkt.'@,
           0,null,null,null,'')
   ?};
   {? XINFO.MDOMWP=null
   || _ok:=0;
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,
           'W parametrach systemu nie ustalono elementu domyślnego dla przychodów.'@,0,null,null,null,'')
   ?};
   {? XINFO.MDOMWK=null
   || _ok:=0;
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,
           'W parametrach systemu nie ustalono elementu domyślnego dla kosztów.'@,
           0,null,null,null,'')
   ?};
   {? XINFO.MDOMKLPR=null
   || _ok:=0;
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,
           'W parametrach systemu nie ustalono elementu domyślnego dla schmatu Klient-Produkt.'@,
           0,ROK_F.ref(),null,null,'')
   ?};
   {? ~XINFO.MFORMKP
   || _ok:=0;
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,
           'W parametrach systemu nie wybrano formuły na tworzenie symboli i opisów schematu Klient-Produkt.'@,
           0,null,null,null,'')
   ?};
   M4_ZESK.cntx_psh(); M4_ZESK.prefix(ROK_F.ref());
   {? ~M4_ZESK.first()
   || _ok:=0;
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,
           'Nie ustalono w parametrach systemu zestawu kont dla przychodów i/lub kosztów.'@,
           0,ROK_F.ref(),null,null,'')
   ?};
   M4_ZESK.cntx_pop();
   ROK_F.cntx_pop(); OKRO_F.cntx_pop()
?};
_ok


\m4_dok_podz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Tworzenie podziałów controllingowych dla dokumentów księgowych
::----------------------------------------------------------------------------------------------------------------------
SKIDXD.cntx_psh(); SKIDXD.use('skxd'+(DOK.name()+4)); SKIDXD.index('DOK'); SKIDXD.prefix(DOK.ref());
POZ.cntx_psh(); POZ.use('pozy'+(DOK.name()+4));
POZ.index('DOK'); POZ.prefix(DOK.ref());
{? POZ.first()
|| {! |?
      _znal:=exec('m4_dok_podz_sprk','control_marza','P');
      {? _znal[1] & _znal[3]
      || exec('m4_gen_skidxd','control_marza',_znal[3],_znal[2],POZ.STR,XINFO.MMODEW,POZ.SUM,XINFO.MDOMKLPR)
      ?};
      obj_del(_znal);
      _znal:=exec('m4_dok_podz_sprk','control_marza','K');
      {? _znal[1] & _znal[3]
      || exec('m4_gen_skidxd','control_marza',_znal[3],_znal[2],POZ.STR,XINFO.MMODEW,POZ.SUM,XINFO.MDOMKLPR)
      ?};
      obj_del(_znal);
      POZ.next()
   !}
?};
POZ.cntx_pop();SKIDXD.cntx_pop();
1


\m4_gen_skidxd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Tworzenie podziałów dla dokumentów
::   WE: _a - pozycja budżetowa (UD_SKL)
::       _b - strona pozycji budżetowej
::       _c - strona elementu
::       _d - ref modelu (SKID_MBN)
::       _e - kwota
::       _f - element Klient-Produkt (UD_SKL)
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
SKID_MBN.cntx_psh(); SKID_MBN.index('KOD'); SKID_MBN.prefix();
{? _d=null | _a=null | _f=null | ~SKID_MBN.seek(_d,SKID_MBN.name()) || _ok:=0 ?};
{? _ok
|| SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(_d);
   _ile_wym:=SKID_MBP.size();
   SKID_MBP.cntx_pop();
   {? ~_ile_wym
   || _ok:=0;
      SKID_MBN.cntx_psh(); SKID_MBN.index('KOD'); SKID_MBN.prefix();
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,'Błędna definicja modelu '+SKID_MBN.KOD+'.',0,SSTALE.AR,
           SSTALE.AO,SKID_MBN.ref(),$DOK.ref());
      SKID_MBN.cntx_pop()
   ?}
?};
{?_ok
|| SKIDXD.blank(1);
   SKIDXD.DOK:=DOK.ref();
   SKIDXD.SKID_MB:=_d;
   SKIDXD.PBUD:=_a;
   SKIDXD.JORG:=_f;
   {? ~exec('spr_wym','control',_d,1,_ile_wym) || _ok:=0 ?}
?};
{? _ok
|| SKIDXD.WAL:=exec('wal_nar','dok_fks');
   SKIDXD.GEN_AUT:='T';
   SKIDXD.M4_PODZ:='T';
   SKIDXD.cntx_psh();
   SKIDXD.index('UNIK');
   SKIDXD.prefix(SKIDXD.GEN_AUT,SKIDXD.DOK,SKIDXD.SKID_MB,
                 SKIDXD.PBUD,SKIDXD.JORG,SKIDXD.OKOSZ,SKIDXD.WYM4,SKIDXD.WYM5,SKIDXD.WAL,SKIDXD.JM);
   {? SKIDXD.first()
   || SKIDXD.WART+={? -_c=-_b || _e || (-1)*_e ?};
      _ok:=SKIDXD.put();
      SKIDXD.cntx_pop()
   || SKIDXD.prefix();
      SKIDXD.WART:={? -_c=-_b || _e || (-1)*_e ?};
      _ok:=SKIDXD.add();
      SKIDXD.cntx_pop()
   ?};
   {? ~_ok
   || OKRO_F.cntx_psh(); ROK_F.cntx_psh(); REJ.cntx_psh();
      exec('add_kom','dok_fks',3,'',CON_FUN.SYM,'Nieudane dodanie podziału dla dokumentu '+DOK.NK+' ('+$SSTALE.AO().NR+
           '/'+SSTALE.AR().NAZ+', rejestr '+DOK.REJ().KOD+' numer '+$DOK.NR+').',
           0,SSTALE.AR,SSTALE.AO,SKIDXD.SKID_MB,$SKIDXD.DOK);
      OKRO_F.cntx_pop(); ROK_F.cntx_pop(); REJ.cntx_pop()
   ?}
?};
SKID_MBN.cntx_pop()


\m4_podz_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Dodaje podział controllingowy dla marży IV dla pozycji dokumentów magazynowych
::----------------------------------------------------------------------------------------------------------------------
TYPYDOK.cntx_psh(); TYPYDOK.index('TYP'); TYPYDOK.prefix();
FORMULA.cntx_psh(); FORMULA.index('FORMULA3'); FORMULA.prefix();
_zew:=ND.TYP().Z;
{? DK.first()
|| {! |?
      _pb:=_kp:=null;
      {? _zew='T'
      || _skl:={? XINFO.MMZPR=1
               || {? XINFO.MMZPF
                  || ($(XINFO.MMZPF().FORMULA))()
                  || null
                  ?}
               || XINFO.MMZPE
               ?};
         {? XINFO.MMZOKPF
         || _zwrot:=($(XINFO.MMZOKPF().FORMULA))()
         || _zwrot:=obj_new(2); _zwrot[1]:=_zwrot[2]:=null
         ?}
      || _skl:={? XINFO.MMWPR=1
               || {? XINFO.MMWPF
                  || ($(XINFO.MMWPF().FORMULA))()
                  || null
                  ?}
               || XINFO.MMWPE
               ?};
         {? XINFO.MMWOKPF
         || _zwrot:=($(XINFO.MMWOKPF().FORMULA))()
         || _zwrot:=obj_new(2); _zwrot[1]:=_zwrot[2]:=null
         ?}
      ?};
      {? exec('spr_ud_skl','control_marza',XINFO.MSCHWIEL,_skl) || _pb:=_skl ?};
      {? _zwrot[1]<>null & _zwrot[2]<>null
      || _kp:=exec('m4_gen_kp1','control_marza',_zwrot[1],_zwrot[2],null)
      ?};
      {? _pb<>null & _kp<>null
      || _strona:=exec('m4_skidxdud_sp_str','control_marza',_pb);
         exec('m4_gen_skidxd','control_marza',_pb,_strona,_strona,XINFO.MMODEW,DK.WAR,_kp)
      ?};
      obj_del(_zwrot);
      DK.next()
   !}
?};
TYPYDOK.cntx_pop(); FORMULA.cntx_pop()


\m4_podz_sprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Dodaje podział controllingowy dla marży IV dla pozycji dokumentów sprzedaży
::----------------------------------------------------------------------------------------------------------------------
TYPYDOK.cntx_psh(); TYPYDOK.index('TYP'); TYPYDOK.prefix();
FORMULA.cntx_psh(); FORMULA.index('FORMULA3'); FORMULA.prefix();
FAP.index('FAP'); FAP.prefix(FAKS.ref());
{? FAP.first()
|| {! |?
      _pb:=_kp:=null;
      _skl:={? XINFO.MSPR=1
            || {? XINFO.MSPF
               || ($(XINFO.MSPF().FORMULA))()
               || null
               ?}
            || XINFO.MSPE
            ?};
      {? XINFO.MSOKPF
      || _zwrot:=($(XINFO.MSOKPF().FORMULA))()
      || _zwrot:=obj_new(2); _zwrot[1]:=_zwrot[2]:=null
      ?};
      {? exec('spr_ud_skl','control_marza',XINFO.MSCHWIEL,_skl) || _pb:=_skl ?};
      {? _zwrot[1]<>null & _zwrot[2]<>null
      || _kp:=exec('m4_gen_kp1','control_marza',_zwrot[1],_zwrot[2],null)
      ?};
      {? _pb<>null & _kp<>null
      || _strona:=exec('m4_skidxdud_sp_str','control_marza',_pb);
         exec('m4_gen_skidxd','control_marza',_pb,_strona,_strona,XINFO.MMODEW,FAP.WWAL_P,_kp)
      ?};
      obj_del(_zwrot);
      FAP.next()
   !}
?};
TYPYDOK.cntx_pop(); FORMULA.cntx_pop()


\m4_szuk_def_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Szuka zapisu w CON_DEF dla rekordu w KH i daty
::   WE: _a - ref kontrahenta
::       _b - data
::   WY: CON_DEF.JORG_S (UD_SKL)
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=null;
KH.cntx_psh(); KH.index('KOD'); KH.prefix();
{? _=2 & type_of(_a)=type_of(null) & _a & type_of(_b)=type_of(date()) & _b<>date(0,0,0) & KH.seek(_a,KH.name())
|| CON_DEF.cntx_psh(); CON_DEF.index('CONDEF10'); CON_DEF.prefix(1,_a);
   {? exec('warunek_bds','control_marza',_b) || _zwrot:=CON_DEF.JORG_S ?};
   {? _zwrot=null & KH.GRKH
   || CON_DEF.index('CONDEF11'); CON_DEF.prefix(1,KH.GRKH);
      {? exec('warunek_bds','control_marza',_b) || _zwrot:=CON_DEF.JORG_S?}
   ?};
   CON_DEF.cntx_pop()
?};
KH.cntx_pop();
_zwrot


\m4_szuk_def_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Szuka zapisu w CON_DEF dla rekordu w M i daty
::   WE: _a - ref M
::       _b - data
::   WY: CON_DEF.JORG_S (UD_SKL)
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=null;
M.cntx_psh(); M.index('MATKTM'); M.prefix();
{? _=2 & type_of(_a)=type_of(null) & _a & type_of(_b)=type_of(date()) & _b<>date(0,0,0) & M.seek(_a,M.name())
|| CON_DEF.cntx_psh(); CON_DEF.index('CON_DEF8'); CON_DEF.prefix(1,M.ref());
   {? exec('warunek_bds','control_marza',_b) || _zwrot:=CON_DEF.JORG_S ?};
   {? _zwrot=null & M.MGR
   || CON_DEF.index('CON_DEF9'); CON_DEF.prefix(1,M.MGR);
      {? exec('warunek_bds','control_marza',_b) || _zwrot:=CON_DEF.JORG_S ?}
   ?};
   CON_DEF.cntx_pop()
?};
M.cntx_pop();
_zwrot


\spr_ud_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdzenie, czy element jest w schemacie
::   WE: _a - schemat (UD_SCH)
::       _b - element UD_SKL lub STRING
::   WY: 1/0 - tak/nie
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? type_of(_b)=type_of(SYSLOG.ref()) & _b
|| UD_DEF.index('PODTEC'); UD_DEF.prefix(_a,_b);
   _zwrot:=UD_DEF.first()
|? type_of(_b)=type_of('') & _b<>''
|| UD_DEF.index('SCHSYM'); UD_DEF.prefix(_a,_b,);
   _zwrot:=UD_DEF.first()
?};
_zwrot


\m4_dok_podz_sprk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Tworzenie podziałów controllingowych dla dokumentów księgowych
::   WE: _a - P - Przychody, K - Koszty
::   WY: Znalezione konto (1/0)
::       Strona pozycji budżetowej
::       Ref UD_SKL pozycji budżetowej
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=obj_new(3); _zwrot[1]:=0; _zwrot[2]:=''; _zwrot[3]:=null;
M4_ZESK.cntx_psh(); M4_ZESK.prefix(ROK_F.ref(),_a);
{? M4_ZESK.first()
|| KON_SCH.cntx_psh(); KON_SCH.index('IND_LP'); KON_SCH.prefix(M4_ZESK.KON_WYDR);
   {? KON_SCH.first()
   || {! |?
         {? KON_SCH.MZ='M'
         || WYDRUKIN.DLKON:=35; WYDRUKI.MASKA:=KON_SCH.MASKA;
            {? +WYDRUKI.MASKA<WYDRUKIN.DLKON
            || WYDRUKI.MASKA+=(WYDRUKIN.DLKON-+WYDRUKI.MASKA)*'?'
            ?};
            _zwrot[1]:=exec('mask','konto',POZ.KON)
         || _ile_od:=(+KON_SCH.ZOD); _kon_od:=(_ile_od+POZ.KON);
            _ile_do:=(+KON_SCH.ZDO); _kon_do:=(_ile_do+POZ.KON);
            _zwrot[1]:=(_kon_od>=KON_SCH.ZOD & _kon_do<=KON_SCH.ZDO)
         ?};
         ~_zwrot[1] & KON_SCH.next()
      !}
   ?};
   KON_SCH.cntx_pop()
?};
M4_ZESK.cntx_pop();
{? _zwrot[1]
|| UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
   UD_DEF.cntx_psh(); UD_DEF.index('SCHOPIS'); UD_DEF.prefix(XINFO.MSCHWIEL,POZ.KON);
   {? UD_DEF.first()
   || _zwrot[2]:=exec('m4_skidxdud_sp_str','control_marza',UD_DEF.UD_SKL);
      _zwrot[3]:=UD_DEF.UD_SKL
   || _ar:=SSTALE.AR; SSTALE.AR:=ROK_F.ref();
      BILANSP.ASYNT:=ROK_F.SYNT;
      BILANSP.ASEP:=ROK_F.SEP;
      KOMPEN.OPIS:='';
      AN.cntx_psh(); AN.use('koan__'+ROK_F.KOD); AN.index('WALSYM'); AN.prefix(FINFO.NAROD,POZ.KON,);
      {? AN.first()
      || exec('pw_opan','dok_fks_zest')
      ?};
      AN.cntx_pop();
      SSTALE.AR:=_ar;
      {? KOMPEN.OPIS<>''
      || _nadrz_skl:={? _a='P' || XINFO.MDOMWP || XINFO.MDOMWK ?};
         UD_DEF.index('SKLREF'); UD_DEF.prefix(_nadrz_skl);
         _nadrz:={? UD_DEF.first() || UD_DEF.ref() || null ?};
         _skl:=exec('m4_pb_konta1','control_marza',POZ.KON,KOMPEN.OPIS,_nadrz,XINFO.MSCHWIEL);
         {? _skl
         || _zwrot[2]:=exec('m4_skidxdud_sp_str','control_marza',_skl);
            _zwrot[3]:=_skl
         ?}
      ?}
   ?};
   UD_DEF.cntx_pop(); UD_SKL.cntx_pop()
?};
_zwrot


\m4_pb_konta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie kont do pozycji budżetowych
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix();
M4_ZM.ROK_F();
exec('m4_pb_konta1','control_marza',TMP_AN.SYM,TMP_AN.OPIS,UD_DEF.ref(),UD_DEF.UD_SCH);
ROK_F.cntx_pop();
{? AN.sel_size()<=1 || sel_exit() ?}


\m4_pb_konta1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Przypisanie kont do pozycji budżetowych - wewnętrzna
::   WE: _a - symbol konta
::       _b - opis konta
::       _c - ref nadrzędnego UD_DEF
::       _d - ref schematu (UD_SCH)
::   WY: ref składnika (UD_SKL)
::----------------------------------------------------------------------------------------------------------------------
_str:='Wn';
_opis:=_a+'-'+_b;
_dl:=ROK_F.SYNT;
_skl:=null;
SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ'); SKIDXDUD.prefix(UD_DEF.UD_SKL);
{? SKIDXDUD.first() || _str:=SKIDXDUD.STR ?};
SKIDXDUD.cntx_pop();
UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
{? _d & UD_SCH.seek(_d,UD_SCH.name())
|| UD_DEF.cntx_psh(); UD_DEF.index('SCHOPIS'); UD_DEF.prefix(UD_SCH.ref(),_opis,);
   {? UD_DEF.first()
   || UD_DEF.cntx_psh(); UD_DEF.index('SYMBOL'); UD_DEF.prefix(UD_DEF.UD_SCH,#UD_DEF.ref());
      {? ~UD_DEF.first()
      || UD_DEF.prefix(); UD_DEF.del()
      ?};
      UD_DEF.cntx_pop()
   ?};
   {? ~UD_DEF.first()
   || UD_SKL.cntx_psh(); UD_SKL.index('OPIS'); UD_SKL.prefix(UD_SCH.UD_TYP,_opis,);
      {? UD_SKL.first()
      || _skl:=UD_SKL.ref()
      || _nsym1:=(_dl+(_a+_dl*'0'));
         UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix(UD_SCH.UD_TYP,_nsym1);
         _nsym:={? UD_SKL.last()
                || _nsym1+form((#(_dl-UD_SKL.SYMBOL)+1),-(16-_dl),,'9.')
                || 15+(_nsym1+16*'0')+'1'
                ?};
         UD_SKL.cntx_pop();
         UD_SKL.blank(1);
         UD_SKL.UD_TYP:=UD_SCH.UD_TYP;
         UD_SKL.SYMBOL:=_nsym;
         UD_SKL.OPIS:=_opis;
         UD_SKL.LISTA:=UD_SKL.AKTYWNY:='T';
         {? UD_SKL.add(1) || _skl:=UD_SKL.ref() ?}
      ?};
      {? _skl
      || exec('m4_skidxdud_str','control_marza',_skl,_str);
         UD_DEF.blank(1);
         UD_DEF.UD_SCH:=UD_SCH.ref();
         UD_DEF.UD_DEF:=_c;
         UD_DEF.UD_SKL:=_skl;
         UD_DEF.UD_POZ:=null;
         UD_DEF.SYMBOL:=UD_SKL.SYMBOL;
         UD_DEF.OPIS:=UD_SKL.OPIS;
         UD_DEF.prefix(); UD_DEF.add()
      ?};
      UD_SKL.cntx_pop()
   ?};
   UD_DEF.cntx_pop()
?};
UD_SCH.cntx_pop();
_skl


\m4_skidxdud_sp_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdzenie strony dla pozycji budżetowej
::----------------------------------------------------------------------------------------------------------------------
_str:='Wn';
SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ'); SKIDXDUD.prefix(_a);
{? SKIDXDUD.first() || _str:=SKIDXDUD.STR ?};
SKIDXDUD.cntx_pop();
_str


\m4_czy_def_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdzenie, czy jest definicja dla marży IV na datę dokumentu magazynowego
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
CON_DEF.cntx_psh(); CON_DEF.index('CONDEF14'); CON_DEF.prefix(1,ND.TYP);
_zwrot:=exec('warunek_bds','control_marza',ND.D);
CON_DEF.cntx_pop()


\m4_czy_def_sprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Sprawdzenie, czy jest definicja dla marży IV na datę dokumentu sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
CON_DEF.cntx_psh(); CON_DEF.index('CONDEF13'); CON_DEF.prefix(1,FAKS.T);
_zwrot:=exec('warunek_bds','control_marza',FAKS.D);
CON_DEF.cntx_pop()


\m4_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Schematy danych dla marży IV
::   WE: _a - 'W' - schemat Wielkość
::            'MKP' - schemat MPK-Klient-Produkt
::            'KP' - schemat Klient-Produkt
::            'K' - schemat Klient
::            'P' - schemat Produkt
::            'M' - schemat MPK
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('m4_def')<=0 || m4_def:=2 ?};
exec('fl_decl','dekret_aut');
_schemat:=null;
{? _a='W'
|| {? XINFO.MSCHWIEL=null
   || FUN.info('W stałych systemu nie wybrano schematu Wielkość.'@)
   || _schemat:=XINFO.MSCHWIEL
   ?}
|? _a='MKP'
|| {? XINFO.MSCHMKLP=null
   || FUN.info('W stałych systemu nie wybrano schematu MPK-Klient-Produkt.'@)
   || _schemat:=XINFO.MSCHMKLP
   ?}
|? _a='KP'
|| {? XINFO.MSCHKLPR=null
   || FUN.info('W stałych systemu nie wybrano schematu Klient-Produkt.'@)
   || _schemat:=XINFO.MSCHKLPR
   ?}
|? _a='K'
||  {? XINFO.MSCHKLI=null
   || FUN.info('W stałych systemu nie wybrano schematu Klient.'@)
   || _schemat:=XINFO.MSCHKLI
   ?}
|? _a='P'
||  {? XINFO.MSCHPROD=null
   || FUN.info('W stałych systemu nie wybrano schematu Produkt.'@)
   || _schemat:=XINFO.MSCHPROD
   ?}
|? _a='M'
||  {? XINFO.MSCHMPK=null
   || FUN.info('W stałych systemu nie wybrano schematu MPK.'@)
   || _schemat:=XINFO.MSCHMPK
   ?}
?};
{? _schemat
|| UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
   {? UD_SCH.seek(_schemat)
   || UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix();
      UD_SCH.UD_TYP();
      UD_DEF.cntx_psh(); UD_DEF.index('SYMBOL'); UD_DEF.prefix(_schemat);
      {? _a='W'
      || exec('definicja','!zws_par_cmod',2)
      || exec('m4_dsp_con_def','control_marza',_schemat);
         {? _a='W'
         || UD_DEF.dnd_sel('M4_WER',,'records.UD_DEF',"exec('ud_def_dnd','schemat')")
         ?};
         _cntx:=obj_new('M4');
         _cntx.M4:=1;
         params_set('cntx',_cntx);
         _act:=exec('adm_def_bs','!zws_par_rsch',0,0);
         params_set();
         UD_DEF.actions('M4_WER',_act,,1);
         _okn_grp:=UD_DEF.grp_make('Schemat %1 - %2'@[UD_SCH.SYMBOL,UD_SCH.OPIS],,'m4_ud_def_sel',,,,,'normal');
         UD_DEF.grp_sel(_okn_grp,UD_DEF,'M4_WER',,
                         "{? UD_DEF.get()
                          || POWUDDEF.prefix(#UD_DEF.ref())
                          || POWUDDEF.prefix(null)
                          ?};
                          grp_disp(POWUDDEF,POWUDDEF.win_sel('?'),,1)
                         ",,,20,,,,,'maximized');
         UD_DEF.grp_splt(_okn_grp,,'horizontal','bottom');
         UD_DEF.grp_sel(_okn_grp,POWUDDEF,POWUDDEF.win_sel('?'),,,,,,,,,,'maximized_with_title');
         UD_DEF.win_sel(_okn_grp);
         UD_DEF.select();
         UD_DEF.dnd_sel('M4_WER',,'records.UD_DEF',"")
      ?};
      UD_DEF.cntx_pop(); UD_TYP.cntx_pop()
   ?};
   UD_SCH.cntx_pop()
?};
{? m4_def=2 || VAR_DEL.delete('m4_def') ?};
VAR_DEL.delete('POWUDDEF')


\m4_dsp_con_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Wyświetlenie powiązanych definicji dla marzy IV
::   WE: _a - schemat (UD_SCH)
::----------------------------------------------------------------------------------------------------------------------
POWUDDEF:=tab_tmp(3,'UD_DEF','INTEGER','Ref UD_DEF'@,
                  'DATA_OD','DATE','Od dnia'@,
                  'DATA_DO','DATE','Do dnia'@,
                  'KOD','STRING[20]','Kod / index'@,
                  'OPIS','STRING[120]','Opis'@,
                  'MIEJSCE','STRING[20]','Miejsce powiązania'@
               );
_wer:=POWUDDEF.mk_sel('Powiązania elementu - Marża IV'@,'P',0,'powuddef_wer',,,,,'U');
POWUDDEF.win_fld(_wer,,'DATA_OD',,,10);
POWUDDEF.win_fld(_wer,,'DATA_DO',,,10);
POWUDDEF.win_fld(_wer,,'KOD',,,20);
POWUDDEF.win_fld(_wer,,'OPIS',,,43);
POWUDDEF.win_fld(_wer,,'MIEJSCE',,,18);
POWUDDEF.win_act(_wer,,'Szukaj');
POWUDDEF.win_act(_wer,,'Kolejność');
POWUDDEF.win_sel(_wer);
CON_DEF.cntx_psh(); CON_DEF.index('CONDEF12');
M.cntx_psh(); MGR.cntx_psh(); KH.cntx_psh(); GRKH.cntx_psh(); UD_DEF.cntx_psh();
{? UD_DEF.first()
|| {! |?
      CON_DEF.prefix(1,UD_DEF.UD_SKL);
      {? CON_DEF.first()
      || {! |?
            {? CON_DEF.M
            || POWUDDEF.KOD:=CON_DEF.M().KTM; POWUDDEF.OPIS:=M.N;
               POWUDDEF.MIEJSCE:={? M.RODZ='T' || 'Materiał' || 'Usługa' ?}
            |? CON_DEF.M4_MGR
            || POWUDDEF.KOD:=CON_DEF.M4_MGR().KOD; POWUDDEF.OPIS:=MGR.NAZ;
               POWUDDEF.MIEJSCE:={? MGR.RODZ='T' || 'Grupa materiałów' || 'Grupa usług' ?}
            |? CON_DEF.M4_KH
            || POWUDDEF.KOD:=CON_DEF.M4_KH().KOD; POWUDDEF.OPIS:=KH.SKR+' - '+KH.NAZ;
               POWUDDEF.MIEJSCE:='Kontrahent'
            |? CON_DEF.M4_GRKH
            || POWUDDEF.KOD:=CON_DEF.M4_GRKH().KOD; POWUDDEF.OPIS:=GRKH.NAZ;
               POWUDDEF.MIEJSCE:='Grupa kontrahentów'
            ?};
            POWUDDEF.UD_DEF:=#UD_DEF.ref();
            POWUDDEF.DATA_OD:=CON_DEF.M4DATAOD;
            POWUDDEF.DATA_DO:=CON_DEF.M4DATADO;
            POWUDDEF.add();
            CON_DEF.next()
         !}
      ?};
      UD_DEF.next()
   !}
?};
M.cntx_pop(); MGR.cntx_pop(); KH.cntx_pop(); GRKH.cntx_pop(); UD_DEF.cntx_pop();
CON_DEF.cntx_pop()


\m4_col_ud_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Rekord przed dla UD_DEF - elementy powiązane
::----------------------------------------------------------------------------------------------------------------------
POWUDDEF.prefix(#UD_DEF.ref);
{? POWUDDEF.find_le(date()) & POWUDDEF.DATA_OD<=date() &
   (POWUDDEF.DATA_DO>=date() | POWUDDEF.DATA_DO=date(0,0,0))
|| 'UD_DEF#07#01'
|| ''
?}


\m4_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Wyświetlanie modelu
::   WE: _a - 'A' - do arkuszy
::            'E' - ewidencyjny
::            'R' - do raportowania
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
SKID_MBN.cntx_psh(); SKID_MBN.index('KOD'); SKID_MBN.prefix();
{? _a='A'
|| {? XINFO.MMODBUD=null
   || FUN.info('W parametrach systemu nie zdefiniowano modelu do arkuszy.'@); _zwrot:=0
   || XINFO.MMODBUD()
   ?}
|? _a='E'
|| {? XINFO.MMODEW=null
   || FUN.info('W parametrach systemu nie zdefiniowano modelu ewidencyjnego.'@); _zwrot:=0
   || XINFO.MMODEW()
   ?}
|? _a='R'
|| {? XINFO.MMODRAP=null
   || FUN.info('W parametrach systemu nie zdefiniowano modelu do arkuszy.'@); _zwrot:=0
   || XINFO.MMODRAP()
   ?}
?};
{? _zwrot
|| SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
   exec('fl_decl','dekret_aut');
   exec('ini_okn','!zws_par_cmod');
   _okn_grp:=SKID_MBP.grp_make('Model'@,,'tab_skid_mbp1',,,,,'maximized');
   SKID_MBP.grp_sel(_okn_grp,SKID_MBP,'KONTROL',,"exec('arw_skid_mbp','!zws_par_cmod')",,,,,,,,'maximized_with_title');
   exec('sch_def_cech','!zws_par_cmod',SKID_MBP,_okn_grp,'');
   SKID_MBP.win_sel(_okn_grp); SKID_MBP.select();
   VAR_DEL.delete('__SMBN_D','__SMBN_L');
   SKID_MBP.cntx_pop()
?};
SKID_MBN.cntx_pop()


\m4_export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Eksport ustawień marży IV
:: ~OST: INFILECHOOSER
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Wyeksportować ustawienia dla marży IV?'@)
|| m4_exp:=1;
   exec('czytaj','#stalesys');
   _Interm:=exec('interm','#system');
   {? _Interm
   || __dir_controling:=fmk_tmp_dir(0);
      path_lok:=fmkdir(__dir_controling.get_path(),'dir')
   || path_lok:=exec('filechooser','#file','Wybierz folder dla eksportowanych plików'@,0,,,,'DIRECTORIES_ONLY',1)
   ?};
   {? path_lok<>'' & (path_lok+1)<>'\\' || path_lok+='\\' ?};
   {? path_lok<>''
   ||
:: formuły
      {? _Interm
      || _dir_path:=path_lok
      || _dir_path:='@'+path_lok
      ?};
      FORMULA.cntx_psh(); FORMULA.index('FORMULA4'); FORMULA.prefix();
      _f1:=fopen(_dir_path+'m4_formula.txt','w',0);
      {? _f1
      || exec('m4_export_formula','control_marza',_f1,XINFO.MFORMKLI);
         exec('m4_export_formula','control_marza',_f1,XINFO.MFORMPRO);
         exec('m4_export_formula','control_marza',_f1,XINFO.MFORMKP);
         exec('m4_export_formula','control_marza',_f1,XINFO.MFORMKPR);
         exec('m4_export_formula','control_marza',_f1,XINFO.MSPF);
         exec('m4_export_formula','control_marza',_f1,XINFO.MSOKPF);
         exec('m4_export_formula','control_marza',_f1,XINFO.MMZPF);
         exec('m4_export_formula','control_marza',_f1,XINFO.MMZOKPF);
         exec('m4_export_formula','control_marza',_f1,XINFO.MMWPF);
         exec('m4_export_formula','control_marza',_f1,XINFO.MMWOKPF);
         fclose(_f1)
      ?};
      FORMULA.cntx_pop();
:: schematy danych
      UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
      UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix();
      m4_dud:=fopen(_dir_path+'m4_skl_skidxdud.txt','w',0);
      {? m4_dud
      || {? XINFO.MSCHWIEL
         || m4_pref:='MR_';
            XINFO.MSCHWIEL(); exec('eksp_sch','control',3);
            VAR_DEL.delete('m4_pref')
         ?};
         {? XINFO.MSCHMPK
         || XINFO.MSCHMPK(); exec('eksp_sch','control',3)
         ?};
         fclose(m4_dud)
      ?};
      _f3:=fopen(_dir_path+'m4_schematy.txt','w',0);
      {? _f3
      || _exp:='';
         _exp+={? XINFO.MSCHWIEL
               || XINFO.MSCHWIEL().UD_TYP().SYMBOL+'@'+UD_SCH.SYMBOL+'@'
               || '@@'
               ?};
         _exp+={? XINFO.MSCHMPK
               || XINFO.MSCHMPK().UD_TYP().SYMBOL+'@'+UD_SCH.SYMBOL+'@'
               || '@@'
               ?};
         fwrite(_f3,_exp);
         fclose(_f3)
      ?};
      UD_SCH.cntx_pop(); UD_TYP.cntx_pop();
:: modele
      SKID_MBN.cntx_psh(); SKID_MBN.index('KOD'); SKID_MBN.prefix();
      SKID_MBP.cntx_psh(); SKID_MBP.index('LP');
      K_INSMBN.cntx_psh(); K_INSMBN.index('INST2');
      UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
      UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix();
      _f2:=fopen(_dir_path+'m4_modele.txt','w',0);
      {? _f2
      || exec('m4_export_model','control_marza',_f2,XINFO.MMODEW);
         exec('m4_export_model','control_marza',_f2,XINFO.MMODBUD);
         exec('m4_export_model','control_marza',_f2,XINFO.MMODRAP);
         fclose(_f2)
      ?};
      SKID_MBN.cntx_pop(); SKID_MBP.cntx_pop(); K_INSMBN.cntx_pop(); UD_SCH.cntx_pop(); UD_TYP.cntx_pop();
:: parametry systemu (XINFO)
      exec('m4_export_ustawienia','control_marza')
   ?};
   {? _Interm
   || _zipname:=__dir_controling.get_path()+'\\M4_ustawienia.zip';
      fpack_add(_zipname,path_lok);
      dlg_save(_zipname,0)
   || FUN.info('Ustawienia dla marży IV wyeksportowano do katalogu \n%1.'@[path_lok])
   ?};
   VAR_DEL.delete('path_lok','m4_exp','m4_dud','__dir_controling')
?}


\m4_export_formula
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Eksport ustawień marży IV - formuły
::   WE: _a - uchwyt do pliku
::       _b - ref formuły
::----------------------------------------------------------------------------------------------------------------------
{? _b & FORMULA.seek(_b)
|| fwrite(_a,FORMULA.RODZAJ+'@'+FORMULA.SKROT+'@'+FORMULA.NAZWA+'@'+FORMULA.FORMULA+'@')
?}


\m4_export_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Eksport ustawień marży IV - modele
::   WE: _a - uchwyt do pliku
::       _b - ref modelu
::----------------------------------------------------------------------------------------------------------------------
{? _b & SKID_MBN.seek(_b)
|| _exp:='N'+'@'+SKID_MBN.KOD+'@'+SKID_MBN.NAZ+'@'+SKID_MBN.EWID+'@'+SKID_MBN.CZY_OFZ+'@';
   K_INSMBN.prefix(REF.FIRMA,SKID_MBN.ref());
   {? K_INSMBN.first()
   || _exp+=K_INSMBN.CZY_INST+'@'+K_INSMBN.CZY_DOK+'@'+K_INSMBN.CZY_LIST+'@'+K_INSMBN.CZY_NAR+'@';
      _exp+=K_INSMBN.WART_NAR+'@'+K_INSMBN.KLIK+'@'
   ?};
   fwrite(_a,_exp);
   SKID_MBP.prefix(SKID_MBN.ref());
   {? SKID_MBP.first()
   || {! |?
          _exp:='P'+'@'+$SKID_MBP.LP+'@'+SKID_MBP.UD_SCH().UD_TYP().SYMBOL+'@'+SKID_MBP.UD_SCH().SYMBOL+'@';
          _exp+=UD_SCH.OPIS+'@'+UD_SCH.SYSTEM+'@'+UD_SCH.BLOKADA+'@'+UD_SCH.DOMYSLNY+'@'+UD_SCH.AKTYWNY+'@';
          _exp+=UD_SCH.UWAGI+'@'+SKID_MBP.NAZ+'@'+SKID_MBP.KONTROLA+'@';
          fwrite(_a,_exp);
          SKID_MBP.next()
      !}
   ?}
?}


\m4_export_ustawienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Eksport ustawień marży IV - ustawienia
::   WE: _a - uchwyt do pliku
::       _b - ref formuły
:: ~OST: INFOPEN
::----------------------------------------------------------------------------------------------------------------------
_f:=fopen('@'+path_lok+'m4_ustawienia.txt','w',0);
{? _f
||
:: modele
   _exp:='M@';
   _exp+={? XINFO.MMODEW
         || XINFO.MMODEW().KOD+'@'
         || '@'
         ?};
   _exp+={? XINFO.MMODBUD
         || XINFO.MMODBUD().KOD+'@'
         || '@'
         ?};
   _exp+={? XINFO.MMODRAP
         || XINFO.MMODRAP().KOD+'@'
         || '@'
         ?};
   fwrite(_f,_exp);
:: schematy
   _exp:='S@';
   _exp+={? XINFO.MSCHWIEL
         || XINFO.MSCHWIEL().UD_TYP().SYMBOL+'@'+UD_SCH.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MSCHMKLP
         || XINFO.MSCHMKLP().UD_TYP().SYMBOL+'@'+UD_SCH.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MSCHKLPR
         || XINFO.MSCHKLPR().UD_TYP().SYMBOL+'@'+UD_SCH.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MSCHKLI
         || XINFO.MSCHKLI().UD_TYP().SYMBOL+'@'+UD_SCH.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MSCHPROD
         || XINFO.MSCHPROD().UD_TYP().SYMBOL+'@'+UD_SCH.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MSCHMPK
         || XINFO.MSCHMPK().UD_TYP().SYMBOL+'@'+UD_SCH.SYMBOL+'@'
         || '@@'
         ?};
   fwrite(_f,_exp);
:: domyślne elementy schematów danych
   _exp:='E@';
   _exp+={? XINFO.MDOMWP
         || XINFO.MDOMWP().UD_TYP().SYMBOL+'@'+UD_SKL.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MDOMWK
         || XINFO.MDOMWK().UD_TYP().SYMBOL+'@'+UD_SKL.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MDOMKLPR
         || XINFO.MDOMKLPR().UD_TYP().SYMBOL+'@'+UD_SKL.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MDOMKL
         || XINFO.MDOMKL().UD_TYP().SYMBOL+'@'+UD_SKL.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MDOMPR
         || XINFO.MDOMPR().UD_TYP().SYMBOL+'@'+UD_SKL.SYMBOL+'@'
         || '@@'
         ?};
   fwrite(_f,_exp);
:: formuły
   _exp:='F@';
   _exp+={? XINFO.MFORMKLI
         || XINFO.MFORMKLI().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MFORMPRO
         || XINFO.MFORMPRO().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MFORMKP
         || XINFO.MFORMKP().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MFORMKPR
         || XINFO.MFORMKPR().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   fwrite(_f,_exp);
:: dokumenty sprzedaży
   _exp:='D@'+$XINFO.MSPR+'@';
   _exp+={? XINFO.MSPF
         || XINFO.MSPF().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MSPE
         || XINFO.MSPE().UD_TYP().SYMBOL+'@'+UD_SKL.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MSOKPF
         || XINFO.MSOKPF().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   fwrite(_f,_exp);
:: magazynowe zewnętrzne
   _exp:='Z@'+$XINFO.MMZPR+'@';
   _exp+={? XINFO.MMZPF
         || XINFO.MMZPF().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MMZPE
         || XINFO.MMZPE().UD_TYP().SYMBOL+'@'+UD_SKL.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MMZOKPF
         || XINFO.MMZOKPF().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   fwrite(_f,_exp);
:: magazynowe wewnętrzne
   _exp:='W@'+$XINFO.MMWPR+'@';
   _exp+={? XINFO.MMWPF
         || XINFO.MMWPF().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MMWPE
         || XINFO.MMWPE().UD_TYP().SYMBOL+'@'+UD_SKL.SYMBOL+'@'
         || '@@'
         ?};
   _exp+={? XINFO.MMWOKPF
         || XINFO.MMWOKPF().RODZAJ+'@'+FORMULA.SKROT+'@'
         || '@@'
         ?};
   fwrite(_f,_exp);
   fclose(_f)
?}


\m4_import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Import ustawień marży IV
:: ~OST: INFILECHOOSER,INFOPEN
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Zaimportować ustawienia dla marży IV?'@)
|| m4_imp:=1;
   exec('init_wer','#stalesys');
   exec('fun_wer_sys','#stalesys');

   exec('czytaj','#stalesys');
:: wersje budżetu
   _wariant:=null;
   K_W_OBL.cntx_psh();
   K_W_OBL.index('NAZ'); K_W_OBL.prefix('Marża IV',);
   {? K_W_OBL.first()
   || _wariant:=K_W_OBL.ref()
   || K_W_OBL.blank();
      K_W_OBL.NAZ:='Marża IV';
      {? K_W_OBL.add() || _wariant:=K_W_OBL.ref() ?}
   ?};
   K_W_OBL.cntx_pop();
   {? _wariant
   || K_WERSJE.cntx_psh(); K_WERSJE.index('SYM'); K_WERSJE.prefix();
      {? ~K_WERSJE.find_key('Budżet1',)
      || K_WERSJE.blank();
         K_WERSJE.SYM:='Budżet1';
         K_WERSJE.OPIS:='Budżet wersji 1';
         K_WERSJE.K_W_OBL:=_wariant;
         K_WERSJE.add(1)
      ?};
      {? ~K_WERSJE.find_key('Budżet2',)
      || K_WERSJE.blank();
         K_WERSJE.SYM:='Budżet2';
         K_WERSJE.OPIS:='Budżet wersji 2';
         K_WERSJE.K_W_OBL:=_wariant;
         K_WERSJE.add(1)
      ?};
      {? ~K_WERSJE.find_key('Prognoza',)
      || K_WERSJE.blank();
         K_WERSJE.SYM:='Prognoza';
         K_WERSJE.OPIS:='Prognoza - z wartościami wykonań i budżetów';
         K_WERSJE.K_W_OBL:=_wariant;
         K_WERSJE.add(1)
      ?};
      K_WERSJE.cntx_pop()
   ?};
   exec('dodaj_ud_typ','schemat','KLI_PROD','Klient / produkt');

   path_lok:=exec('filechooser','#file','Wybierz folder dla eksportowanych plików'@,0,,,,'DIRECTORIES_ONLY',1);
   {? path_lok<>'' & (path_lok+1)<>'\\' || path_lok+='\\' ?};
   {? path_lok<>''
   ||
:: Formuły
      FORMULA.cntx_psh(); FORMULA.index('FORMULA4'); FORMULA.prefix();
      _path:='@'+path_lok+'m4_formula.txt';
      _f1:=fopen(_path,'r');
      {? _f1
      || {! |?
            _l:=fread(_f1);
            {? _l<>'\n'
            || _t:=spli_str(_l,'@');
               FORMULA.prefix(_t[1],_t[2],);
               {? FORMULA.first()
               || FORMULA.NAZWA:=_t[3];
                  FORMULA.FORMULA:=_t[4];
                  FORMULA.put(1)
               || FORMULA.blank();
                  FORMULA.RODZAJ:=_t[1];
                  FORMULA.SKROT:=_t[2];
                  FORMULA.NAZWA:=_t[3];
                  FORMULA.FORMULA:=_t[4];
                  FORMULA.add(1)
               ?};
               obj_del(_t);
               1
            ?}
         !};
         fclose(_f1)
      ?};
      FORMULA.cntx_pop();
:: Modele
      SKID_MBN.cntx_psh(); SKID_MBN.index('KOD'); SKID_MBN.prefix();
      SKID_MBP.cntx_psh(); SKID_MBP.index('LP');
      K_INSMBN.cntx_psh(); K_INSMBN.index('INST2'); K_INSMBN.prefix();
      UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
      UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix();
      _path:='@'+path_lok+'m4_modele.txt';
      _f2:=fopen(_path,'r');
      {? _f2
      || _model:=null;
         {! |?
            _l:=fread(_f2);
            {? _l<>'\n'
            || _t:=spli_str(_l,'@');
               {? (2+_l)='N@'
               || _model:=null;
                  SKID_MBN.prefix(_t[2],);
                  {? ~SKID_MBN.first()
                  || SKID_MBN.blank();
                     SKID_MBN.KOD:=_t[2];
                     SKID_MBN.NAZ:=_t[3];
                     SKID_MBN.EWID:=_t[4];
                     SKID_MBN.CZY_OFZ:=_t[5];
                     {? SKID_MBN.add(1)
                     || _model:=SKID_MBN.ref();
                        K_INSMBN.blank();
                        K_INSMBN.SKID_MBN:=SKID_MBN.ref();
                        K_INSMBN.FIRMA:=REF.FIRMA;
                        K_INSMBN.CZY_INST:=_t[6];
                        K_INSMBN.CZY_DOK:=_t[7];
                        K_INSMBN.CZY_LIST:=_t[8];
                        K_INSMBN.CZY_NAR:=_t[9];
                        K_INSMBN.WART_NAR:=_t[10];
                        K_INSMBN.KLIK:=_t[11];
                        K_INSMBN.add(1)
                     ?}
                  ?}
               |? (2+_l)='P@' & _model
               || UD_TYP.prefix(_t[3],_t[3]);
                  _typ:={? UD_TYP.first() || UD_TYP.ref() || null ?};
                  {? _typ
                  || UD_SCH.prefix(UD_TYP.ref(),_t[4],);
                     _sch:=null;
                     {? UD_SCH.first()
                     || _sch:=UD_SCH.ref()
                     || UD_SCH.blank();
                        UD_SCH.UD_TYP:=_typ;
                        UD_SCH.SYMBOL:=_t[4];
                        UD_SCH.OPIS:=_t[5];
                        UD_SCH.SYSTEM:=_t[6];
                        UD_SCH.BLOKADA:=_t[7];
                        UD_SCH.DOMYSLNY:=_t[8];
                        UD_SCH.AKTYWNY:=_t[9];
                        UD_SCH.UWAGI:=_t[10];
                        {? UD_SCH.add(1) || _sch:=UD_SCH.ref() ?}
                     ?};
                     {? _sch
                     || SKID_MBP.blank();
                        SKID_MBP.SKID_MBN:=_model;
                        SKID_MBP.LP:=#_t[2];
                        SKID_MBP.UD_SCH:=_sch;
                        SKID_MBP.NAZ:=_t[11];
                        SKID_MBP.KONTROLA:=_t[12];
                        SKID_MBP.add(1)
                     ?}
                  ?}
               ?};
               obj_del(_t);
               1
            ?}
         !};
         fclose(_f2)
      ?};
      SKID_MBN.cntx_pop(); SKID_MBP.cntx_pop(); K_INSMBN.cntx_pop(); UD_SCH.cntx_pop(); UD_TYP.cntx_pop();
:: Schematy danych
      _path:='@'+path_lok+'m4_schematy.txt';
      _f3:=fopen(_path,'r');
      _typ1:=_typ2:=_sch1:=_sch2:='';
      {? _f3
      || _l:=fread(_f3);
         {? _l<>'\n'
         || _t:=spli_str(_l,'@');
            _typ1:=_t[1]; _sch1:=_t[2];
            _typ2:=_t[3]; _sch2:=_t[4];
            obj_del(_t)
         ?};
         fclose(_f3)
      ?};
      UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
      UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix();
      UD_DEF.cntx_psh(); UD_DEF.index('SYMBOL'); UD_SCH.prefix();
      m4_dud:=fopen('@'+path_lok+'m4_skl_skidxdud.txt','r');
      {? m4_dud
      || exec('m4_import_sch','control_marza',_typ1,_sch1);
         exec('m4_import_sch','control_marza',_typ2,_sch2);
         SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ'); SKIDXDUD.prefix();
         UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
         JM.cntx_psh(); JM.index('KOD'); JM.prefix();
         SLO.cntx_psh(); SLO.index('SL'); SLO.prefix();
         UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix('POZ_BUD','POZ_BUD');
         {? UD_TYP.first()
         || {! |?
               _l:=fread(m4_dud);
               {? _l<>'\n'
               || _t:=spli_str(_l,'@');
                  {? UD_SKL.find_key(UD_TYP.ref(),_t[1],_t[1])
                  || _add:=0;
                     {? ~SKIDXDUD.find_key(UD_SKL.ref())
                     || _add:=1;
                        SKIDXDUD.blank()
                     ?};
                     SKIDXDUD.WART_IL:=_t[2];
                     {? _t[3]<>''
                     || {? JM.find_key(_t[3],_t[3])
                        || SKIDXDUD.JM:=JM.ref()
                        || JM.blank();
                           JM.KOD:=_t[3];
                           JM.NAZ:=_t[4];
                           {? JM.add(1) || SKIDXDUD.JM:=JM.ref() ?}
                        ?}
                     ?};
                     SKIDXDUD.PREC:=#_t[5];
                     SKIDXDUD.STR:=_t[6];
                     SKIDXDUD.WSK_WAL:=_t[7];
                     {? _t[8]<>'' & SLO.find_key(XINFO.SLWAL,_t[8])
                     || SKIDXDUD.WAL:=SLO.ref()
                     ?};
                     SKIDXDUD.CIW:=_t[9];
                     SKIDXDUD.ZN_AGR:=_t[10];
                     SKIDXDUD.ONLY_L:=_t[11];
                     {? _add || SKIDXDUD.add(1) || SKIDXDUD.put(1) ?}
                  ?};
                  obj_del(_t);
                  1
               ?}
            !}
         ?};
         UD_SKL.cntx_pop(); SKIDXDUD.cntx_pop(); JM.cntx_pop(); UD_TYP.cntx_pop(); SLO.cntx_pop();
         fclose(m4_dud)
      ?};
      UD_SCH.cntx_pop(); UD_TYP.cntx_pop(); UD_DEF.cntx_pop();
:: parametry systemu (XINFO)
      SKID_MBN.cntx_psh(); SKID_MBN.index('KOD'); SKID_MBN.prefix();
      SKID_MBP.cntx_psh(); SKID_MBP.index('LP');
      UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
      UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix();
      UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
      FORMULA.cntx_psh(); FORMULA.index('FORMULA4'); FORMULA.prefix();
      _path:='@'+path_lok+'m4_ustawienia.txt';
      _f4:=fopen(_path,'r');
      {? _f4
      || _model:=null;
         {! |?
            _l:=fread(_f4);
            {? _l<>'\n'
            || _t:=spli_str(_l,'@');
               {? (2+_l)='M@'
               || {? _t[2]<>'' & SKID_MBN.find_key(_t[2]) || XINFO.MMODEW:=SKID_MBN.ref() ?};
                  {? _t[3]<>'' & SKID_MBN.find_key(_t[3]) || XINFO.MMODBUD:=SKID_MBN.ref() ?};
                  {? _t[4]<>'' & SKID_MBN.find_key(_t[4]) || XINFO.MMODRAP:=SKID_MBN.ref() ?}
               |? (2+_l)='S@'
               || {? _t[2]<>'' & _t[3]<>'' & UD_TYP.find_key(_t[2],_t[2]) & UD_SCH.find_key(UD_TYP.ref(),_t[3],_t[3])
                  || XINFO.MSCHWIEL:=UD_SCH.ref()
                  ?};
                  {? _t[4]<>'' & _t[5]<>'' & UD_TYP.find_key(_t[4],_t[4]) & UD_SCH.find_key(UD_TYP.ref(),_t[5],_t[5])
                  || XINFO.MSCHMKLP:=UD_SCH.ref()
                  ?};
                  {? _t[6]<>'' & _t[7]<>'' & UD_TYP.find_key(_t[6],_t[6]) & UD_SCH.find_key(UD_TYP.ref(),_t[7],_t[7])
                  || XINFO.MSCHKLPR:=UD_SCH.ref()
                  ?};
                  {? _t[8]<>'' & _t[9]<>'' & UD_TYP.find_key(_t[8],_t[8]) & UD_SCH.find_key(UD_TYP.ref(),_t[9],_t[9])
                  || XINFO.MSCHKLI:=UD_SCH.ref()
                  ?};
                  {? _t[10]<>'' & _t[11]<>'' & UD_TYP.find_key(_t[10],_t[10]) &
                     UD_SCH.find_key(UD_TYP.ref(),_t[11],_t[11])
                  || XINFO.MSCHPROD:=UD_SCH.ref()
                  ?};
                  {? _t[12]<>'' & _t[13]<>'' & UD_TYP.find_key(_t[12],_t[12]) &
                     UD_SCH.find_key(UD_TYP.ref(),_t[13],_t[13])
                  || XINFO.MSCHMPK:=UD_SCH.ref()
                  ?}
               |? (2+_l)='E@'
               || {? _t[2]<>'' & _t[3]<>'' & UD_TYP.find_key(_t[2],_t[2]) & UD_SKL.find_key(UD_TYP.ref(),_t[3],)
                  || XINFO.MDOMWP:=UD_SKL.ref()
                  ?};
                  {? _t[4]<>'' & _t[5]<>'' & UD_TYP.find_key(_t[4],_t[4]) & UD_SKL.find_key(UD_TYP.ref(),_t[5],)
                  || XINFO.MDOMWK:=UD_SKL.ref()
                  ?};
                  {? _t[6]<>'' & _t[7]<>'' & UD_TYP.find_key(_t[6],_t[6]) & UD_SKL.find_key(UD_TYP.ref(),_t[7],)
                  || XINFO.MDOMKLPR:=UD_SKL.ref()
                  ?};
                  {? _t[8]<>'' & _t[9]<>'' & UD_TYP.find_key(_t[8],_t[8]) & UD_SKL.find_key(UD_TYP.ref(),_t[9],)
                  || XINFO.MDOMKL:=UD_SKL.ref()
                  ?};
                  {? _t[10]<>'' & _t[11]<>'' & UD_TYP.find_key(_t[10],_t[10]) & UD_SKL.find_key(UD_TYP.ref(),_t[11],)
                  || XINFO.MDOMPR:=UD_SKL.ref()
                  ?}
               |? (2+_l)='F@'
               || {? _t[2]<>'' & _t[3]<>'' & FORMULA.find_key(_t[2],_t[3]) || XINFO.MFORMKLI:=FORMULA.ref() ?};
                  {? _t[4]<>'' & _t[5]<>'' & FORMULA.find_key(_t[4],_t[5]) || XINFO.MFORMPRO:=FORMULA.ref() ?};
                  {? _t[6]<>'' & _t[7]<>'' & FORMULA.find_key(_t[6],_t[7]) || XINFO.MFORMKP:=FORMULA.ref() ?};
                  {? _t[8]<>'' & _t[9]<>'' & FORMULA.find_key(_t[8],_t[9]) || XINFO.MFORMKPR:=FORMULA.ref() ?}
               |? (2+_l)='D@'
               || XINFO.MSPR:=#_t[2];
                  {? _t[3]<>'' & _t[4]<>'' & FORMULA.find_key(_t[3],_t[4]) || XINFO.MSPF:=FORMULA.ref() ?};
                  {? _t[5]<>'' & _t[6]<>'' & UD_TYP.find_key(_t[5],_t[5]) & UD_SKL.find_key(UD_TYP.ref(),_t[6],)
                  || XINFO.MSPE:=UD_SKL.ref()
                  ?};
                  {? _t[7]<>'' & _t[8]<>'' & FORMULA.find_key(_t[7],_t[8]) || XINFO.MSOKPF:=FORMULA.ref() ?}
               |? (2+_l)='Z@'
               || XINFO.MMZPR:=#_t[2];
                  {? _t[3]<>'' & _t[4]<>'' & FORMULA.find_key(_t[3],_t[4]) || XINFO.MMZPF:=FORMULA.ref() ?};
                  {? _t[5]<>'' & _t[6]<>'' & UD_TYP.find_key(_t[5],_t[5]) & UD_SKL.find_key(UD_TYP.ref(),_t[6],)
                  || XINFO.MMZPE:=UD_SKL.ref()
                  ?};
                  {? _t[7]<>'' & _t[8]<>'' & FORMULA.find_key(_t[7],_t[8]) || XINFO.MMZOKPF:=FORMULA.ref() ?}
               |? (2+_l)='W@'
               || XINFO.MMWPR:=#_t[2];
                  {? _t[3]<>'' & _t[4]<>'' & FORMULA.find_key(_t[3],_t[4]) || XINFO.MMWPF:=FORMULA.ref() ?};
                  {? _t[5]<>'' & _t[6]<>'' & UD_TYP.find_key(_t[5],_t[5]) & UD_SKL.find_key(UD_TYP.ref(),_t[6],)
                  || XINFO.MMWPE:=UD_SKL.ref()
                  ?};
                  {? _t[7]<>'' & _t[8]<>'' & FORMULA.find_key(_t[7],_t[8]) || XINFO.MMWOKPF:=FORMULA.ref() ?}
               ?};
               obj_del(_t);
               1
            ?}
         !};
         fclose(_f4);
         exec('zapisz','#stalesys',1,XINFO,'MMODEW','MMODBUD','MMODRAP','MSCHWIEL','MSCHMKLP',
              'MSCHKLPR','MSCHKLI','MSCHPROD','MSCHMPK','MDOMWP','MDOMWK','MDOMKLPR','MDOMKL','MDOMPR',
              'MFORMKLI','MFORMPRO','MFORMKP','MFORMKPR');
         exec('zapisz','#stalesys',1,XINFO,'MMZPR','MMZPF','MMZPE','MMZOKPF','MMWPR','MMWPF','MMWPE','MMWOKPF',
              'MSPR','MSPF','MSPE','MSOKPF')
      ?};
      SKID_MBN.cntx_pop(); SKID_MBP.cntx_pop(); UD_SCH.cntx_pop(); UD_TYP.cntx_pop(); UD_SKL.cntx_pop();
      FORMULA.cntx_pop();
:: Uzupełnienie schematów Klient-Produkt i MPK-Klient-Produkt o elementy grupujące
      exec('m4_el_grup','control_marza',XINFO.MSCHMKLP);
      exec('m4_el_grup','control_marza',XINFO.MSCHKLPR)
   ?};
   VAR_DEL.delete('path_lok','m4_imp','m4_dud')
?}


\m4_el_grup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Import ustawień marży IV - elementy dodatkowe schematów
::   WE: _a - Ref schematu
::----------------------------------------------------------------------------------------------------------------------
UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix();
UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_SCH.prefix();
UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
{? UD_SCH.seek(_a)
|| _do_roz:=exec('m4_el_grup_skl','control_marza','__DO_ROZLICZENIA','Elementy do rozliczenia');
   {? _do_roz & UD_SCH.ref()=XINFO.MSCHKLPR
   || {? UD_SKL.find_key(XINFO.MSCHKLPR().UD_TYP,'_BRAK-_BRAK','_BRAK-_BRAK') &
         ~UD_DEF.find_key(UD_SCH.ref(),UD_SKL.ref())
      || UD_DEF.blank(1);
         UD_DEF.UD_SCH:=UD_SCH.ref();
         UD_DEF.UD_SKL:=UD_SKL.ref();
         UD_DEF.UD_POZ:=null;
         UD_DEF.SYMBOL:=UD_SKL.SYMBOL;
         UD_DEF.OPIS:=UD_SKL.OPIS;
         UD_DEF.UD_DEF:=_do_roz;
         UD_DEF.add(1)
      ?}
   ?};
   exec('m4_el_grup_skl','control_marza','__KLIENT-PRODUKT','Klient-produkt')
?};
UD_SCH.cntx_pop(); UD_TYP.cntx_pop(); UD_DEF.cntx_pop(); UD_SKL.cntx_pop()


\m4_el_grup_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Import ustawień marży IV - elementy dodatkowe schematów
::   WE: _a - symbol elementu
::       _b - opis elementu
::   WY: UD_DEF.ref()
::----------------------------------------------------------------------------------------------------------------------
_skl:=null; _zwrot:=0;
{? UD_SKL.find_key(UD_SCH.UD_TYP,_a,_a)
|| _skl:=UD_SKL.ref()
|| UD_SKL.blank(1);
   UD_SKL.UD_TYP:=UD_SCH.UD_TYP;
   UD_SKL.SYMBOL:=_a;
   UD_SKL.OPIS:=_b;
   UD_SKL.LISTA:='N';
   UD_SKL.AKTYWNY:='T';
   {? UD_SKL.add(1) || _skl:=UD_SKL.ref() ?}
?};
{? _skl
|| {? UD_DEF.find_key(UD_SCH.ref(),_skl)
   || _zwrot:=#UD_DEF.ref()
   || UD_DEF.blank(1);
      UD_DEF.UD_SCH:=UD_SCH.ref();
      UD_DEF.UD_SKL:=_skl;
      UD_DEF.UD_POZ:=null;
      UD_DEF.SYMBOL:=_a;
      UD_DEF.OPIS:=_b;
      UD_DEF.UD_DEF:=0;
      {? UD_DEF.add(1) || _zwrot:=#UD_DEF.ref() ?}
   ?}
?};
_zwrot


\m4_import_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Import ustawień marży IV - schematy
::   WE: _a - symbol typu
::       _b - symbol opisu
::----------------------------------------------------------------------------------------------------------------------
{? _a<>'' & _b<>''
|| UD_TYP.prefix(_a,_a);
   _typ:={? UD_TYP.first() || UD_TYP.ref() || null ?};
   {? _typ
   || UD_SCH.prefix(UD_TYP.ref(),_b,);
      {? UD_SCH.first()
      || UD_DEF.prefix(UD_SCH.ref());
         {? ~UD_DEF.first()
         || _path:='@'+path_lok+'m4_schemat_'+(-UD_SCH.SYMBOL)+'.txt';
            exec('imp_sch','!zws_par_cmod',_path)
         ?}
      ?}
   ?}
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:16 86c2a53aa232589d561ad633fcee8e16b0f3218ed4d1f2cfe406556a3aaf68e418c07bbb9ac72d9d5cde9c7497c93a6a361378f61e637af83bce6c5af624a15f484c83fa1edb20cadf6577ae12d02069ab7c8f1c0b50335765c7143f55543888ba353ed8b9f40f1ff1d1b2b425b04952f3c568dfa01898302434f68c464bdd13

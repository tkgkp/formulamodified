:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_wpw.fml [12.51]
:: Utworzony: 2020/04/10
:: Autor: jaws
:: Systemy: PPK
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę kartoteki deklaracji i wniosków PPK.
::======================================================================================================================


\_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz "po popraw" tabeli PPK_WPW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: zgodny ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
:: podstawowy warunek dalszego działania
{? ~_a | do_state()<>1 || return() ?};
~~


\_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed usunięciem wiersza tabeli PPK_WPW.
::   WE:
::   WY: 0/1 - wiersz może/nie może być usunięty
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',PPK_PNW,'PPK_WPW',PPK_WPW.ref())


\_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz po usunięciu wiersza tabeli PPK_WPW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: zgodny ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
:: podstawowy warunek dalszego działania
{? ~_a | do_state()<>1 || return() ?};
~~


\zm_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed wyświetleniem pola ZMIANA w wierszu tabeli PPK_WPW.
::   WE:
::   WY: 0/1 - zawartość pola widoczna/ukryta
::----------------------------------------------------------------------------------------------------------------------
PPK_WPW.BLOKADA<>'T'


\st_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed wyświetleniem pola STAWKA w wierszu tabeli PPK_WPW.
::   WE:
::   WY: 0/1 - zawartość pola widoczna/ukryta
::----------------------------------------------------------------------------------------------------------------------
PPK_WPW.BLOKADA<>'T' & PPK_WPW.ZMIANA='T'


\pop_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed poprawieniem wiersza tabeli PPK_WPW.
::   WE:
::   WY: 0/1 - wiersz niedostępny do edycji/wiersz może być poprawiony
::----------------------------------------------------------------------------------------------------------------------
exec('st_bd','ppk_wpw')


\rek_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Formuła akcji "rekord przed" okienek tabeli PPK_WPW.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? KST_PPK.PWP_UKW<>'T'
|| POLA_GRP.REAL_1:=
      {? PPK_WPW.BLOKADA='T'
::       wpłaty zablokowane
      || 0

      |? PPK_WPW.ZMIANA='T'
::       stawka indywidualna
      || PPK_WPW.STAWKA

      || exec('x_st','ppk',PPK_WPW.R().RN,PPK_WNU.OD)
      ?}
?};
0


\rek_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Po edycji wiersza tabeli PPK_WPW.
::   WE: _a INTEGER - rodzaj edycji: 0/1 - dołączenie/poprawianie
::   WY: 1 w przypadku gdy rekord jest poprawny, akronim wymaganego pola lub 0 gdy doszło do powielenia klucza.
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(PPK_WPW,-menu_txt()='popraw',,'R');
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & _chk=0)
|| return(_chk)
?};

{? PPK_WPW.BLOKADA='T' | PPK_WPW.ZMIANA='N'
:: blokada wpłat lub kwota ze stałych
|| return(1)
?};

_min:=_max:=0;
_od:=PPK_WPW.PPK_WNU().OD;
_rn:=PPK_WPW.R().RN;

:: dopuszczalne procenty wpłat mogły ulec zmianie
exec('czytaj','#stalesys',_od,KST_PPK);

{? _rn=__RUB.sys_kod(710,_od,1)
:: % skł.pod. PPK firma
|| _max:=KST_PPK.SK_PF

|? _rn=__RUB.sys_kod(711,_od,1)
:: % skł.dod. PPK firma
|| _max:=KST_PPK.SK_MAXDF

|? _rn=__RUB.sys_kod(712,_od,1)
:: % skł.pod. PPK zatr.
|| _min:=KST_PPK.SK_MINPU;
   _max:=KST_PPK.SK_MAXPU

|? _rn=__RUB.sys_kod(713,_od,1)
:: % skł.dod. PPK zatr.
|| _max:=KST_PPK.SK_MAXDU

|| return(0)
?};

{? _min>0 | _max>0
|| {? PPK_WPW.STAWKA<_min | _max<PPK_WPW.STAWKA
   || FUN.emsg(
         'Wartość "%1" musi być z zakresu od %2 do %3.'@
         [MS.name(PPK_WPW,'STAWKA'),form(_min,,2),form(_max,,2)]
      );
      'STAWKA'
   || 1
   ?}
?}


\wnd_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: "Okienko przed" wszystkich okienek tabeli PPK_WPW.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,KST_PPK);

{? KST_PPK.PWP_UKW='T'
|| PPK_WPW.fld_fml('ZMIANA','BEFORE_DISPLAY',"*");
   POLA_GRP.fld_fml('REAL_1','BEFORE_DISPLAY',"exec('st_bd','ppk_wpw')")
|| PPK_WPW.fld_fml('ZMIANA','BEFORE_DISPLAY',"1")
?};

R.cntx_psh();
R.win_dict('SLO');
1


\wnd_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: "Okienko po" wszystkich okienek tabeli PPK_WPW.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
PPK_WPW.fld_fml('ZMIANA','BEFORE_DISPLAY',"*");
POLA_GRP.fld_fml('REAL_1','BEFORE_DISPLAY',"*");

R.f_clear();
R.cntx_pop();
1

:Sign Version 2.0 jowisz:1028 2019/10/14 09:21:32 7dd6c69081299bb54058dd69a892bb1a2df834439982ebaa8d8d46b0e4cf4c3c6bfee03adae231ecba03d4fae3466aaf1c4056c74d7b8bfb91f1c07aafc7c6061fe8b8cee18975797b06eb2c2376428d03870a98ca45bbf159310fa40a466d68f5fceacb89194d888c960db8e4d7361e0a9e5488d4bfbe6a752cb79ea52ef9e9

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ctr_bha.fml
:: Utworzony: 12.11.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły obszaru CTR_BHA - Harmonogramy budżetowania
::======================================================================================================================


\add_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Budzety czastkowe
::   WE: _a - nazwa zakładki
::       _b - tabela
::       _c - okno grupowe
::  OLD: \usr_plan/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__TTKHPS')<=0
|| __TTKHPS:=tab_tmp(2,
      'LP','INTEGER',,
      'NAZ','STRING[20]','Budżety',
      'TYP','STRING[1]','ID'
   );
   _fml:="__TTKHPS.LP:=_a; __TTKHPS.NAZ:=_b; __TTKHPS.TYP:=_c; __TTKHPS.add()";
   {? exec('hasAction','users','CTR_HBD_DZAA') || _fml(1,'Do uzupełnienia','W') ?};
   {? exec('hasAction','users','CTR_HBD_DZBA') || _fml(2,'Do akceptacji','U') ?};
   _fml(3,'Do przeglądania','V');
   _fml(4,'Przydzielone innym','P');
   __TTKHPS.first();
   _o:=__TTKHPS.mk_sel('Zadania budżetowe'@);
   __TTKHPS.win_fld(_o,,'NAZ',,,,,,'Nazwa'@)
?};
_b.grp_sel(_c,__TTKHPS,_o,_a,"exec('set_zak','ctr_bha')",,,,,,,,'maximized_with_title');
task_attach('CTR_HBD_DZAA');
task_attach('CTR_HBD_DZBA');
_b.tab_splt(_c,,'vertical','right',',20%');
_b.grp_sel(_c,K_HARM_P,'WER_UZU',,,,,,,,,,'maximized_with_title');
__TTKHPS.win_sel(_o);
__TTKHPZ:=1;
exec('ini_win_harm','control','WER_UZU',K_HARM_P,'STAT_NAZ');
~~


\set_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zakres prezentowanych zadań budżetowych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__TTPRZY');
_sort:='NAZ';
_join:='';
_where:='';
{? 'WU'*__TTKHPS.TYP>0
|| _sort:={? __TTKHPS.TYP='W' || 'D_P_UZU' || 'D_P_AKC' ?};
   {? __TTKHPZ=1
   || _where:='K_HARM_P.STATUS=\''+__TTKHPS.TYP+'\' and '
   || _where:='K_HARM_P.AKTYWNY=\'T\' and '
   ?};
   {? __TTKHPS.TYP='W'
   || _where+='K_HARM_P.KTO_UZU=\''+$OPERATOR.USER+'\''
   || _where+='K_HARM_P.KTO_AKC=\''+$OPERATOR.USER+'\''
   ?};
   _join:=' join K_HARM'
|? __TTKHPS.TYP='V'
|| _join:=' join K_HARM_U join K_HARM';
   _where:='K_HARM_U.USERS=\''+$OPERATOR.USER+'\''
|? __TTKHPS.TYP='P'
|| __TTPRZY:=sql('select K_HARM_P.REFERENCE as REF, K_HARM_P.NAZ NAZ, max(to_string(K_HARM_H.DATA)|| \' \'||to_string(K_HARM_H.CZAS)) as STEMPEL '+
   'from K_HARM_H '+
   'join K_HARM_P '+
   'where K_HARM_H.USERS=\':_a\' '+
   'group by K_HARM_P.REFERENCE, K_HARM_P.NAZ '+
   'order by 1 ', $OPERATOR.USER);
   K_HARM_H.cntx_psh();
   K_HARM_H.index('K_HARM_P');
   K_HARM_H.prefix();
   {? __TTPRZY.first
   || {! |?
            _d1:=__TTPRZY.STEMPEL*' '+(__TTPRZY.STEMPEL);
            _t1:=__TTPRZY.STEMPEL*' '-(__TTPRZY.STEMPEL);
            {? K_HARM_H.find_key(BB.sqlint(__TTPRZY.REF),date(#(4+_d1),#((7+_d1)+2),#((10+_d1)+2)),time(#(2+_t1),#((5+_t1)+2),#(_t1+2)))
            || {? K_HARM_H.CAN_VIEW=0
               || __TTPRZY.del
               || __TTPRZY.next
               ?}
            || __TTPRZY.del
            ?}
      !}
   ?};
   K_HARM_H.cntx_pop();
   _join:=' join K_HARM_H join K_HARM';
   _where:='K_HARM_H.USERS=\''+$OPERATOR.USER+'\' and K_HARM_P.REFERENCE in (select ref from :_a)'
?};
{? _where<>'' || _where+=' and' ?};
_where+=' K_HARM.FIRMA=\''+$REF.FIRMA+'\'';

K_HARM_P.win_sel('WER_UZU');
K_HARM_P.clear();
{? __TTKHPS.TYP='P'
|| K_HARM_P.f_set(_sort,_join,_where,__TTPRZY)
|| K_HARM_P.f_set(_sort,_join,_where)
?};
_hide:='';
{? __TTKHPS.TYP<>'P'
|| _hide:='H'
?};
K_HARM_P.actions('WER_UZU',_hide,,1);
grp_disp(K_HARM_P,'WER_UZU');
~~


\usr_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Budzety czastkowe - zmiana zakresu danych
::   WE: _a - czy zmieniac zakres - 1-tak 0-nie
::  OLD: \usr_zak/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a || __TTKHPZ:=~__TTKHPZ ?};
K_HARM_P.win_sel('WER_UZU');
K_HARM_P.hdr_sel();
{? __TTKHPZ=0
|| K_HARM_P.hdr_sel('Zadania budżetowe - wszystkie'@)
|| K_HARM_P.hdr_sel('Zadania budżetowe - moje'@)
?};
exec('set_zak','ctr_bha')


\ed_xlsx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Edycja arkusza z budżetem
::  OLD: \ed_xlsx/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? 'WU'*__TTKHPS.TYP>0
|| _params:=exec('mp_run_a','#b__box');
   {? __TTKHPS.TYP='W'
   || _params.ACT_UID:='CTR_HBD_DZAA'
   |? __TTKHPS.TYP='U'
   || _params.ACT_UID:='CTR_HBD_DZBA'
   ?};
   _params.UIDREF:=K_HARM_P.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'K_HARM_P',K_HARM_P.ref());
   exec('mp_run','#b__box',_params)
|| exec('ed_xlsx','control')
?}


\koniec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zakończenie zadania budżetowego
::   WE: _a - typ zadania - A-uzupelnienie B-akceptacja
::       _b - użytkownik
::----------------------------------------------------------------------------------------------------------------------
_usr:=OPERATOR.USER;
OPERATOR.USER:=exec('kuser_seek','control',_b);
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='CTR_HBD_DZ'+_a+'A';
_params.AKCJA:='Zakończ';
_params.UIDREF:=K_HARM_P.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.WT_SKIP:=1;
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'K_HARM_P',K_HARM_P.ref());
exec('mp_run','#b__box',_params);
OPERATOR.USER:=_usr


\access
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności uzupełniania i akceptacji zadania budżetowego
::   WE: _a - typ czynności A-uzupelnianie B-akceptacji
::           params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
{? var_press('_in')<=0 | var_press('K_HARM_P',_in)<=0 || return(1) ?};
_user:=params_get().user;
_mp:=params_get().mp;
_result:=0;
K_HARM_P.cntx_psh();
K_HARM_P.prefix();
{? K_HARM_P.seek(_in.K_HARM_P)
|| _result:={? _a='A'
            || _user=K_HARM_P.KTO_UZU
            || K_HARM_P.ZN_AKC='N' |
               K_HARM_P.ZN_AKC='T' & _user=K_HARM_P.KTO_AKC
            ?};
   {? _result=0 & var_press('khpusr')>0 & _user=khpusr || _result:=1 ?};
   {? _result=0 & var_press('khpAll')>0 || _result:=1 ?}
?};
K_HARM_P.cntx_pop();
_result


\set_web_win_title
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia tytuł okna
::   WE: _a - 0-init 1-wpp
::       _b - typ zakresu
::----------------------------------------------------------------------------------------------------------------------
{? _b='U'
|| _txt:='do uzupełnienia'@
|? _b='A'
|| _txt:='do akceptacji'@
|? _b='P'
|| _txt:='do przeglądania'@
|| _txt:='przydzielone innym'@
?};
_tyt:='Zadania budżetowe użytkownika %1'@[USERS.KOD]+' - '+_txt;
{? _a
|| K_HARM_W.web_win_opt('WEB',,'title='+_tyt)
|| K_HARM_W.web_win_init('WEB',,'title='+_tyt)
?}


\trig_khw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: uzupelnienie tabeli K_HARM_W
::   WE: _a - 1 - na podstawie zadania
::            2 - uzupełnienie
::       _b - 1-dodawanie, poprawianie; 0-usuwanie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? _b=1
   || K_HARM_W.index('UNIK');
      K_HARM_W.prefix(K_HARM_P.ref(),'U');
      {? K_HARM_W.first() || {! |? K_HARM_W.del !} ?};
      K_HARM_W.prefix(K_HARM_P.ref(),'A');
      {? K_HARM_W.first() || {! |? K_HARM_W.del !} ?};
      {? K_HARM_P.AKTYWNY='T'
      || K_HARM_W.prefix();
         {? K_HARM_P.KTO_UZU & K_HARM_P.STATUS='W'
         || K_HARM_W.K_HARM_P:=K_HARM_P.ref();
            K_HARM_W.USERS:=K_HARM_P.KTO_UZU;
            K_HARM_W.ACTION:='U';
            K_HARM_W.K_HARM:=K_HARM_P.K_HARM;
            K_HARM_W.add(1)
         ?};
         {? K_HARM_P.KTO_AKC & K_HARM_P.STATUS='U'
         || K_HARM_W.K_HARM_P:=K_HARM_P.ref();
            K_HARM_W.USERS:=K_HARM_P.KTO_AKC;
            K_HARM_W.ACTION:='A';
            K_HARM_W.K_HARM:=K_HARM_P.K_HARM;
            K_HARM_W.add(1)
         ?}
      ?}
   || K_HARM_W.index('UNIK');
      K_HARM_W.prefix(K_HARM_P.ref());
      {? K_HARM_W.first() || {! |? K_HARM_W.del !} ?}
   ?}
|? _a=2
|| {? _b=1
   || K_HARM_W.prefix();
      K_HARM_W.K_HARM_P:=K_HARM_U.K_HARM_P;
      K_HARM_W.USERS:=K_HARM_U.USERS;
      K_HARM_W.ACTION:='P';
      K_HARM_W.K_HARM:=K_HARM_P.K_HARM;
      K_HARM_W.add(1)
   || K_HARM_W.index('UNIK');
      K_HARM_W.prefix(K_HARM_U.K_HARM_P,K_HARM_U.USERS,'P');
      {? K_HARM_W.first() || {! |? K_HARM_W.del !} ?}
   ?}
|? _a=3
|| {? _b=1
   || K_HARM_W.prefix();
      K_HARM_W.K_HARM_P:=K_HARM_H.K_HARM_P;
      K_HARM_W.USERS:=K_HARM_H.USERS;
      K_HARM_W.ACTION:='H';
      K_HARM_W.K_HARM:=K_HARM_P.K_HARM;
      K_HARM_W.add(1)
   ?}
?}


\trig_k_harm_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Triger po add K_HARM_U
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? _b=1
   || exec('trig_khw','ctr_bha',2,1)
   ?};
   ~~
|| exec('trig_khw','ctr_bha',2,0);
   1
?}


\trig_k_harm_h
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Triger po add K_HARM_H
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? _b=1
   || exec('trig_khw','ctr_bha',3,1)
   ?};
   ~~
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:16 083b5ab96b222e9285bc42a535216fa7f97398e397535ba2d86ce7f211c083ac09818a1351d501d5eb43f69768cc5b6ffc521904151b8635db59701505cfe2eb0cdee1f5f3679e8c92dfdf67c4463c37640e4d369d07a504a79eb536d10ed3d9942c681279b6907c54b34a46228aecbb55561d2a979c4aa8796a0fed883b4953

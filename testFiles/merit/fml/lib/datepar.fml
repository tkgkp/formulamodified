:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: datepar.fml [12.51]
:: Utworzony: 22.01.2020
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do ustawień parametrów zaleznych od daty
::======================================================================================================================


\obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obiekt do obsługi parametrów zaleznych od czasu
::----------------------------------------------------------------------------------------------------------------------
:: Dodawanie parametru do systemu
:: _dpar:=exec('obj','datepar');
:: _dpar.system('FKS')
::    .addPar('PAR','Opis parametru PAR typu INTEGER','I')
::       .addVal(date(2020,1,22),100)
::
:: Odczytywanie wartości parametru
:: _dpar.system('FKS').par('PAR').value(date())
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'SYS','NAG','LAST','poz','buf',
   'system','par','value','addPar','addVal',
   'pvalue',
   'this'
);
_obj.poz:=0;
_obj.this:=_obj;
_obj.system:="
   .SYS:={? var_pres('_a')>0 || _a || 'FKS' ?};
   .this
";
_obj.par:="
   {? .SYS='' || .SYS:='FKS' ?};
   DPARNAG.index('CODE');
   DPARNAG.prefix(.SYS,_a,);
   {? DPARNAG.first()
   || .NAG:=DPARNAG.ref()
   || .NAG:=null
   ?};
   {? var_pres('buf',.this)>0
   || obj_del(.buf)
   ?};
   {? .NAG & var_pres('_b')>0 & _b
   || DPARVAL.cntx_psh();
      DPARVAL.index('DPARNAG'); DPARVAL.prefix(.SYS,.NAG);
      _size:=DPARVAL.size();
      {? _size<=20
      || {? DPARVAL.first()
         || .buf:=obj_new(_size);
            _lp:=0;
            {!
            |? _lp+=1;
               .buf[_lp]:=obj_new('DATE','VALUE');
               .buf[_lp].DATE:=DPARVAL.DATE;
               .buf[_lp].VALUE:=.pvalue();
               DPARVAL.next()
            !}
         ?}
      ?};
      DPARVAL.cntx_pop()
   ?};
   .this
";
_obj.value:="
   _value:=~~;
   {? var_pres('buf',.this)>0
   || {! _i:=obj_len(.buf) //-1..1
      |? _value=~~
      |! {? _a>=.buf[_i].DATE
         || _value:=.buf[_i].VALUE
         ?}
      !}
   |? .NAG
   || DPARVAL.cntx_psh();
      DPARVAL.index('DPARNAG');
      DPARVAL.prefix(.SYS,.NAG);
      {? DPARVAL.find_le(_a)
      || _value:=.pvalue()
      ?};
      DPARVAL.cntx_pop()
   ?};
   {? _value=~~ & var_pres('_b')>0
   || _b
   || _value
   ?}
";
_obj.pvalue:="
   _value:=~~;
   {? DPARNAG.TYPE='I'
   || _value:=#DPARVAL.VALUE
   |? DPARNAG.TYPE='R'
   || _value:=#STR.gsub(DPARVAL.VALUE,',','.')
   |? DPARNAG.TYPE='D'
   || _value:=date(#(4+DPARVAL.VALUE),#(5-DPARVAL.VALUE-3),#(DPARVAL.VALUE+2))
   ?};
   _value
";
_obj.addPar:="
   DPARNAG.index('CODE'); DPARNAG.prefix();
   {? DPARNAG.find_key(.SYS,_a,)
   || .NAG:=DPARNAG.ref();
      DPARNAG.NAME:=_b;
      DPARNAG.TYPE:=_c;
      DPARNAG.put()
   || DPARNAG.SYSTEM:=.SYS;
      DPARNAG.CODE:=_a;
      DPARNAG.NAME:=_b;
      DPARNAG.TYPE:=_c;
      {? DPARNAG.add()
      || .NAG:=DPARNAG.ref()
      || .NAG:=null
      ?}
   ?};
   .this
";
_obj.addVal:="
   {? .NAG
   || DPARVAL.index('DPARNAG');
      DPARVAL.prefix();
      {? ~DPARVAL.find_key(.SYS,.NAG,_a)
      || DPARVAL.blank(1);
         DPARVAL.SYSTEM:=.SYS;
         DPARVAL.DPARNAG:=.NAG;
         DPARVAL.DATE:=_a;
         DPARVAL.VALUE:={? DPARVAL.DPARNAG().TYPE='I' | DPARNAG.TYPE='R' | DPARNAG.TYPE='D'
                        || $_b
                        || _b
                        ?};
         DPARVAL.add()
      ?}
   ?};
   .this
";
_obj


\get_val
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca wartość parametru, na podaną datę
::   WE:  _a  - system
::        _b  - kod parametru
::        _c  - data
::       [_d] - wartość domyślna
::----------------------------------------------------------------------------------------------------------------------
_value:=~~;
_typ:='';
DPARNAG.index('CODE');
DPARNAG.prefix(_a,_b,);
{? DPARNAG.first()
|| _typ:=DPARNAG.TYPE;
   DPARVAL.index('DPARNAG');
   DPARVAL.prefix(_a,DPARNAG.ref());
   {? DPARVAL.find_le(_c)
   || {? DPARNAG.TYPE='I'
      || _value:=#DPARVAL.VALUE
      |? DPARNAG.TYPE='R'
      || _value:=#STR.gsub(DPARVAL.VALUE,',','.')
      |? DPARNAG.TYPE='D'
      || _value:=date(#(4+DPARVAL.VALUE),#(5-DPARVAL.VALUE-3),#(DPARVAL.VALUE+2))
      ?}
   ?}
?};
{? _value=~~ & var_pres('_d')>0
|| _d
|| _value
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wyświetlanie parametrów
::   WE: _a - system
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('SYSTEM',POMOC)<=0
|| FUN.info('Funkcja niedostępna.\nBrak wymaganych zmian w definicji.');
   return()
?};
POMOC.SYSTEM:={? var_pres('_a')>0
              || _a
              || 'FKS'
              ?};
DPARVAL.index('CODE');
DPARVAL.prefix(POMOC.SYSTEM,);
DPARVAL.win_sel('WER');
DPARVAL.select();
''


\modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Dodawanie i poprawianie parametrów
::   WE: _a - 1-dołacz 2-popraw
::----------------------------------------------------------------------------------------------------------------------
_add:=_a=1;
{? _add
|| DPARVAL.blank()
?};
DPARVAL.win_edit('RED');
DPARVAL.efld_opt('RED','mark=1',DPARVAL,'DPARNAG','CODE');
DPARVAL.efld_opt('RED','mark=1',DPARVAL,'DATE');
{? DPARVAL.edit("exec('record','datepar')")
|| {? _add
   || DPARVAL.add()
   || DPARVAL.put()
   ?}
?}


\bldparsys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość poczatkowa pola SYSTEM
::----------------------------------------------------------------------------------------------------------------------
POMOC.SYSTEM


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redkacji wartości parametru
::----------------------------------------------------------------------------------------------------------------------
_r:=chk_rec('DPARNAG','DATE','VALUE');
{? _r=''
|| _ref:=DPARVAL.ref();
   DPARVAL.cntx_psh();
   DPARVAL.index('DPARNAG'); DPARVAL.prefix(DPARVAL.SYSTEM,DPARVAL.DPARNAG,DPARVAL.DATE);
   {? DPARVAL.first() & (-menu_txt()<>'popraw' | DPARVAL.ref()<>_ref)
   || FUN.info('Istnieje już wartość na wskazany dzień.'@);
      _r:='DATE'
   ?};
   DPARVAL.cntx_pop()
?};
_r


\before_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redagowanie pól parametrów
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
{? _fld='DPARNAG'
|| DparTyp:=DPARVAL.DPARNAG().TYPE;
   DPARVAL.efld_opt('RED','mark='+$(DparTyp<>'' & 'IRD'*DparTyp),DPARVAL,'VALUE');
   1
|| 1
?}


\after_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji pól parametrów
:: ~OST: INPATTERN
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
{? _fld='DPARNAG'
|| _typ:=DPARVAL.DPARNAG().TYPE;
   {? _typ='I'
   || DPARVAL.fld_fml('VALUE','PATTERN',"'I9!'")
   |? _typ='R'
   || DPARVAL.fld_fml('VALUE','PATTERN',"100*'F'+{? exec('interm','#system') || '' || '$' ?}")
   |? _typ='D'
   || DPARVAL.fld_fml('VALUE','PATTERN',"'2999/19/39#'")
   || DPARVAL.fld_fml('VALUE','PATTERN',"*")
   ?};
   {? var_pres('DparTyp')>0 & DparTyp<>_typ
   || DPARVAL.VALUE:=''
   ?};
   DPARVAL.efld_opt('RED','mark='+$(_typ<>'' & 'IRD'*_typ),DPARVAL,'VALUE');
   1
|? _fld='VALUE'
|| _typ:=DPARVAL.DPARNAG().TYPE;
   {? _typ='R'
   || _str:=DPARVAL.VALUE;
      _val:='';
      _first:=1;
      {! _i:=1..+_str
      |! _zn:=_i+_str+1;
         {? _zn='.' || _zn:=',' ?};
         {? _zn=','
         || {? _first
            || _val+=_zn;
               _first:=0
            ?}
         |? _zn<>'e'
         || _val+=_zn
         ?}
      !};
      fld(_val)
   ?};
   _a
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 40eb595415a11f6fd09fc381ad433da0c49b98fb8c70e64fdcb6d1d43a202b74bfce82736ff29ff32c3e174c5feef3695b750d54d6d90fc3523ba0d0c6f918db63ee0f80d153b6bcda48e6c8a0cb280d6f365d0a08f2fbf7aa7084dc43b8354bb5495d3b169d1b19a0b1f37f2f16bcaaabf7a73101ad552973e4315d754d4ddb

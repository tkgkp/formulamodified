:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_ucz.fml [12.51]
:: Utworzony: 10.05.2019
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Funkcje do obsługi uwzględnionych w raporcie uczestników.
::======================================================================================================================


\_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed dołączeniem lub poprawieniem wiersza tabeli PPK_RUC.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
PPK_UCZ.cntx_psh();
PPK_RUC.OSOBA:=PPK_RUC.PPK_UCZ().OSOBA;
PPK_UCZ.cntx_pop();
1


\_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli PPK_RUC.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_ruc')


\_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli PPK_RUC.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_ruc')


\_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed usunięciem wiersza tabeli PPK_RUC.
::   WE:
::   WY: 0/1 - wiersz może/nie może być usunięty
::----------------------------------------------------------------------------------------------------------------------
PPK_RPL.cntx_psh();
_ok:=(PPK_RUC.PPK_RPL().WYS_STAT<>'T');
PPK_RPL.cntx_pop();

_ok & exec('del_ndx','#table',PPK_RAP,'UNIQUE',PPK_RUC.ref())


\_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz po usunięciu wiersza tabeli PPK_RUC.
::   WE:
::   WY: zgodny ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
:: podstawowy warunek dalszego działania
{? ~_a | do_state()<>1 || return() ?};

exec('usun','ppk_rda',bfld('PPK_RDA'));
~~


\akt_ir
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Aktualizuje liczbę raportów utworzonych dla uczestnika.
::   WE: _a _PPK_RUC - wystąpienie uczestnika w raportach
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_ref:=_a;

PPK_RUC.cntx_psh();
PPK_RUC.prefix();
{? PPK_RUC.seek(_ref)
|| PPK_RAP.cntx_psh();
   PPK_RAP.index('OPIS');
   PPK_RAP.prefix(PPK_RUC.ref());
   PPK_RUC.ILOSC:=PPK_RAP.size();
   {? PPK_RUC.ILOSC>0
   || PPK_RUC.put()
   || PPK_RUC.del()
   ?};
   PPK_RAP.cntx_pop()
?};
PPK_RUC.cntx_pop();
~~


\zaw_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Obsługa akcji "Zawartość" okienek tabeli PPK_RUC.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('zaw_a','ppk_rda',PPK_RUC.PPK_RPL,#PPK_RUC.PPK_RDA);
~~


\szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyszukuje informacje o raporcie.
::   WE: _a _PPK_RPL - plik raportu
::       _b _PPK_UCZ - uczestnik
::   WY: wskazanie istniejącego zapisu lub null
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_rpl:=_a;
_ucz:=_b;

PPK_RUC.index('UNIQUE');
PPK_RUC.prefix(_rpl);
{? PPK_RUC.find_key(_ucz)
|| PPK_RUC.ref()
|| null
?}


\dodaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Dodaje informacje o raporcie.
::   WE: _a _PPK_RPL - plik raportu
::       _b _PPK_UCZ - uczestnik
::       _c _PPK_RDA - dane raportu
::   WY: wskazanie dodanego/istniejącego zapisu lub null
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_rpl:=_a;
_ucz:=_b;
_rda:=_c;

{? _rpl=null | _ucz=null | _rda=null
:: zakończ działanie
|| return(null)
?};

:: sprawdź, czy element już istnieje
_ref:=exec('szukaj','ppk_ruc',_rpl,_ucz);
{? _ref<>null
:: znaleziono
|| {? PPK_RUC.PPK_RDA<>_rda
   || PPK_RUC.PPK_RDA:=_rda;
      PPK_RUC.put()
   ?};
   return(_ref)
?};

PPK_RUC.blank();
PPK_RUC.PPK_RPL:=_rpl;
PPK_RUC.PPK_UCZ:=_ucz;
PPK_RUC.PPK_RDA:=_rda;
{? PPK_RUC.add()
|| _ref:=PPK_RUC.ref()
?};

_ref


\jest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Sprawdza, czy dla bieżącego uczestnika istnieją jakiekolwiek raporty dla instytucji finansowej.
::   WE:
::   WY: 0/1 - brak/znaleziono
::----------------------------------------------------------------------------------------------------------------------
PPK_RUC.cntx_psh();
PPK_RUC.index('FK_2');
PPK_RUC.prefix(PPK_UCZ.ref());
_test:=PPK_RUC.first();
PPK_RUC.cntx_pop();
_test

:Sign Version 2.0 jowisz:1048 2020/10/16 15:22:56 dde6cbc11be12184c44812844e48882d57c3b8c636585fcebec1791ff18c7da7b3d4cb1a00cc12d0add14d649bf076ec209cccbd1c9688fca68a7db799d4b02250547857ea7347b62f007ffc27f92d017772ca70c41ea279aa85873200b0fb5a42dbc2145b81e1327e3a33dc18ecee5ab0c31d72bedf1b790d78726d34ce7b13

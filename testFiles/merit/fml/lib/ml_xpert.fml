:!UTF-8
::(c) Macrologic SA odział Wrocław Wszelkie prawa zastrzeżone
::========================================================================================================================
::Nazwa pliku: ml_xpert.fml
::Utworzony: 200?-??-??
::Autor: AK
::========================================================================================================================
::Zawartość: Formuły wspólne  do obsługi wersji Maclex Merit ERP
::Uwaga: Poprzednia nazwa pliku to skid_ml.fml
::========================================================================================================================


\filtr_slo_bud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [10.20]
:: OPIS: Filtrowanie wartości przy redakcji konta z użyciem słowników powiązanych ze strukturami budżetu rodzajowego
::        i zadadaniowego. Funkcja filtruje dziedzine SLO w kilku miejscach:
::        przy redakcji konta, przy redakcji wyróżników
::   WE: _a - string juz zredagowany
::----------------------------------------------------------------------------------------------------------------------
_ilebzad:=_ilebrodz:=0;
_dalej:=1;
_kod:=_typ:='';

_typ:={? ('CzęśćDziałRozdziałParagrafPozycjaFunkcjaZadaniePodzadanieDziałanie'*SLU.WZ)>=34 || 'Z' || 'R' ?};
{? 'CzęśćDziałRozdziałParagrafPozycjaFunkcjaZadaniePodzadanieDziałanie'*SLU.WZ
|| _budzad:=obj_new(5);
   _budrodz:=obj_new(5);
   {! _i..5 |! _budzad[_i]:=_budrodz[_i]:='' !};
   _kod_sl:=SLU.WZ;
   _poz:=0;
   {? _kod_sl<>'' & ~(_kod_sl*'?')
   || {? _kod_sl='Część'     || _poz:=1
      |? _kod_sl='Dział'     || _poz:=2
      |? _kod_sl='Rozdział'  || _poz:=3
      |? _kod_sl='Paragraf'  || _poz:=4
      |? _kod_sl='Pozycja'   || _poz:=5
      |? _kod_sl='Funkcja'   || _poz:=1
      |? _kod_sl='Zadanie'   || _poz:=2
      |? _kod_sl='Podzadanie'|| _poz:=3
      |? _kod_sl='Działanie' || _poz:=4
      ?}
   ?};
   SLU.cntx_psh();
   {? '_E1_E2_E3_E4_E5_E6'*('_'+cur_afld())
   || 'Powiązania w wyróżnikach dla konta. Zakładamy,że zawsze na aktulnym POZ';
      {! __i:=1..6
      |! {? ($('KA.S'+$__i+'=\'\''))()=0
         || _wyb:=($(' KA.KW'+$__i+'().SLU().WZ'))();
            _kod0:=($('KA.E'+$__i))();
            {? _wyb='Część'
            || {? _poz>1 || _budrodz[1]:=_kod0 || _budrodz[1]:='' ?}
            |? _wyb='Dział'
            || {? _poz>2 || _budrodz[2]:=_kod0 || _budrodz[2]:='' ?}
            |? _wyb='Rozdział'
            || {? _poz>3 || _budrodz[3]:=_kod0 || _budrodz[3]:='' ?}
            |? _wyb='Paragraf'
            || {? _poz>4 || _budrodz[4]:=_kod0 || _budrodz[4]:='' ?}
            |? _wyb='Pozycja'
            || _budrodz[5]:=''
            |? _wyb='Funkcja'
            || {? _poz>1 || _budzad[1]:=_kod0 ?}
            |? _wyb='Zadanie'
            || {? _poz>2 || _budzad[2]:=_kod0 || _budzad[2]:='' ?}
            |? _wyb='Podzadanie'
            || {? _poz>3 || _budzad[3]:=_kod0 || _budzad[3]:='' ?}
            |? _wyb='Działanie'
            || _budzad[4]:=''
            ?}
         ?}
      !};
      &__i;
      _filtr:=exec('get_filtr','ml_xpert',_budrodz,_budzad,_typ,_poz,_kod_sl)
   |? '_KON_KONTO1_KONTO2_KONTO3_KK_KK1_MASKA_PREFIX'*('_'+cur_afld())
   || 'Powiązania przy redakcji konta';
      BUD.cntx_psh();
      _budind:=BUD.ndx_tmp(,1,'KS','ROK',0,'KS','SYM',0,'POZ',,0);
      BUD.index(_budind);
      BUD.prefix(SSTALE.AR,SSTALE.AR().SYNT+_a);
      _str:=(SSTALE.AR().SYNT+1)-_a;
      {? BUD.first()
      || {!
         |?
            _wyb:=BUD.SLU().SLU().WZ;
            _kod0:=((_str*SSTALE.AR().SEP)-1)+_str;
            _str:=((_str*SSTALE.AR().SEP))-_str;
            {? _kod0<>'' & ~(_kod0*'?')
            || {? _wyb='Część'
               || {? _poz>1 || _budrodz[1]:=_kod0 || _budrodz[1]:='' ?}
               |? _wyb='Dział'
               || {? _poz>2 || _budrodz[2]:=_kod0 || _budrodz[2]:='' ?}
               |? _wyb='Rozdział'
               || {? _poz>3 || _budrodz[3]:=_kod0 || _budrodz[3]:='' ?}
               |? _wyb='Paragraf'
               || {? _poz>4 || _budrodz[4]:=_kod0 || _budrodz[4]:='' ?}
               |? _wyb='Pozycja'
               || _budrodz[5]:=''
               |? _wyb='Funkcja'
               || {? _poz>1 || _budzad[1]:=_kod0 ?}
               |? _wyb='Zadanie'
               || {? _poz>2 || _budzad[2]:=_kod0 || _budzad[2]:='' ?}
               |? _wyb='Podzadanie'
               || {? _poz>3 || _budzad[3]:=_kod0 || _budzad[3]:='' ?}
               |? _wyb='Działanie'
               || _budzad[4]:=''
               ?}
            ?};
            BUD.next()
         !}
      ?};
      BUD.ndx_drop(_budind);
      BUD.cntx_pop();
      _filtr:=exec('get_filtr','ml_xpert',_budrodz,_budzad,_typ,_poz,_kod_sl)
   || _dalej:=0
   ?};
   SLU.cntx_pop();
   {? _dalej
   || SLO.prefix();
      {? ~SLO.f_set('KOD',,'SLU=\':_a\' and KOD '+_filtr,$SLU.ref()) || SLO.f_clear(1)  ?};
      {? var_pres('__refslo')>0 & __refslo<>null || SLO.f_seek(__refslo) ?}
   || SLO.f_clear(1)
   ?}
|| SLO.f_clear(1)
?};
1


\get_filtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AZ [10.40]
:: OPIS: Ustawienie stringa
::        Miejesce wywołanie: \filtr_slo_bud
::   WE: _a - tablica _budrodz
::       _b - tablica _budzad
::       _c - typ  'Z'- budzet zadania   'R'
::       _d - poziom w strukturze
::       _e - wzorzec słownika
::   WY: _wyj - string juz zredagowany
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
SLO.cntx_psh();
SLO.index('SL_WZ');
SLO.prefix(_e);
{? SLO.first()
|| {!
   |?
        {? {? _c='R'
           || _a[_d]:=SLO.KOD;
              _typ:='';
              {? _a[4]<>'' || _typ:=exec('typ_kon','ml_conn',POZ.KON) ?};
              exec('budcorct','ml_fiks',_a[1],_a[2],_a[3],_a[4],_a[5],_typ)
           |? _c='Z'
           || _b[_d]:=SLO.KOD;
              exec('budcorct','ml_zad',_b[1],_b[2],_b[3],_b[4])
           ?}
        || _wyn+='\''+SLO.KOD+'\','
        ?};
        SLO.next()
   !}
?};
{? _wyn='' || _wyn+='\'xxx\',' ?};
SLO.cntx_pop();
_wyn:='in('+(_wyn-1)+')'


\slo_br
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RB [8.10]
:: OPIS: Funkcja 'Rekord-przed' - tabela SLO. W przypadku gdy słownik dotyczy konta mającego związek ze strukturą
::       budżetową (tabele ML_DZ,ML_RZ,ML_PG,ML_PP),czy to przez analityke poziomów konta czy przez wyróźniki - zadanie
::       formuły polega na zwracaniu wartości 0 dla rekordów spójnych z innymi symbolami użytymi podczas
::       redakcji zapisu i rekordów z innych tabel oraz 1 dla niezgodnych.
::   WE: [_a] - wymuszony tryb pracy 'K'/'F'
::       [_b] - tryb kolorowania 0 - wynik daje 0/1; 1 - wynik daje kolor 'SLO#10'
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};
{? _a='' || _a:=exec('conn','ml_zad','GLOBAL','FILTR',SSTALE.AR().KOD) ?};
RS.cntx_psh;
RS.index('RS'); RS.prefix();
{? MLEX.FIKSB & RS.find_key(SLO.SLU().WZ) &
   (RS.TAB='ML_CZ' | RS.TAB='ML_DZ' | RS.TAB='ML_RZ' | RS.TAB='ML_PG' | RS.TAB='ML_PP'
   | RS.TAB='ML_FUN' | RS.TAB='ML_ZAD' | RS.TAB='ML_PODZ' | RS.TAB='ML_DZLAN') &
   (_a='K')
|| _wyn:=0;
   {? RS.TAB='ML_CZ' | RS.TAB='ML_DZ' | RS.TAB='ML_RZ' | RS.TAB='ML_PG' | RS.TAB='ML_PP'
   ||
::      jeśli wypełniana ma być częśc
      {? BD_C=BD_LVL
      || {? _wyn:=~exec('budcorct','ml_fiks',SLO.KOD)
         || BD_CR:=ML_CZ.ref
         || BD_CR:=null
         ?}
::      jeśli wypełniany ma być dział
      |? BD_D=BD_LVL
      || {? _wyn:=~exec('budcorct','ml_fiks',BD_CZ,SLO.KOD)
         || BD_CR:=ML_CZ.ref;
            BD_DR:=ML_DZ.ref
         || BD_DR:=null
         ?}
      |? BD_R=BD_LVL
::       jeśli wypełniany ma być rozdział
      || {? _wyn:=~exec('budcorct','ml_fiks',BD_CZ,BD_DZ,SLO.KOD)
         || BD_CR:=ML_CZ.ref;
            BD_DR:=ML_DZ.ref;
            BD_RR:=ML_RZ.ref
         || BD_RR:=null
         ?}

      |? BD_P=BD_LVL
::       jeśli wypełniany ma być paragraf
      ||
         _typ:=exec('typ_kon','ml_conn',POZ.KON);
         {? _wyn:=~exec('budcorct','ml_fiks',BD_CZ,BD_DZ,BD_RZ,SLO.KOD,,_typ)
         || BD_CR:=ML_CZ.ref;
            BD_DR:=ML_DZ.ref;
            BD_RR:=ML_RZ.ref;
            BD_PR:=ML_PG.ref
         || BD_PR:=null
         ?}
      |? BD_Z=BD_LVL
::      sprawdzenie poprawności jeśli ostatnio wypełniana była pozycja paragrafowa
      ||
         _typ:=exec('typ_kon','ml_conn',POZ.KON);
         {? _wyn:=~exec('budcorct','ml_fiks',BD_CZ,BD_DZ,BD_RZ,BD_PG,SLO.KOD,_typ)
         || BD_CR:=ML_CZ.ref;
            BD_DR:=ML_DZ.ref;
            BD_RR:=ML_RZ.ref;
            BD_PR:=ML_PG.ref;
            BD_ZR:=ML_PP.ref
         || BD_ZR:=null
         ?}
      ?}
   ||
::      jeśli wypełniana ma być funkcja
      {? BD_ZF=BD_LVL
      || {? _wyn:=~exec('budcorct','ml_zad',SLO.KOD)
         || BD_ZFR:=ML_FUN.ref
         || BD_ZFR:=null
         ?}
      |?
::      jeśli wypełniane ma być zadanie
         BD_ZZ=BD_LVL
      || {? _wyn:=~exec('budcorct','ml_zad',BD_FUN,SLO.KOD)
         || BD_ZFR:=ML_FUN.ref;
            BD_ZZR:=ML_ZAD.ref
         || BD_ZZR:=null
         ?}

      |?
::      jeśli wypełniane ma być podzadanie
         BD_ZP=BD_LVL
      ||
         {? _wyn:=~exec('budcorct','ml_zad',BD_FUN,BD_ZAD,SLO.KOD)
         || BD_ZFR:=ML_FUN.ref;
            BD_ZZR:=ML_ZAD.ref;
            BD_ZPR:=ML_PODZ.ref
         || BD_ZPR:=null
         ?}
      |?
::      sprawdzenie poprawności jeśli ostatnio wypełniane było działanie
         BD_ZD=BD_LVL
      ||
         {? _wyn:=~exec('budcorct','ml_zad',BD_FUN,BD_ZAD,BD_PODZ,SLO.KOD)
         || BD_ZFR:=ML_FUN.ref;
            BD_ZZR:=ML_ZAD.ref;
            BD_ZPR:=ML_PODZ.ref;
            BD_ZDR:=ML_DZLAN.ref
         || BD_ZDR:=null
         ?}
      ?}
   ?};
   {? _b
   || {? _wyn || _wyn:='SLO#10#01' ?}
   || _wyn
   ?}
|| _wyn:=0
?};
RS.cntx_pop;
_wyn


\p_con_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [8.70]
:: OPIS: Pokazuje okienko wertowania tabeli MLCONNEC
::   WE: _a - typ parametrow
::       _b - pole zwracane na sel_exit
::  OLD: \p_con_f3/ml_xpert.fml
::----------------------------------------------------------------------------------------------------------------------
_wsel:=MLCONNEC.mk_sel('Typy kont budżetowych'@);
MLCONNEC.win_fld(_wsel,,'ID',,,3,,,'Kod parametru'@);
MLCONNEC.win_fld(_wsel,,'WR',,,50,,,'Wartość parametru'@);
MLCONNEC.win_fld(_wsel,,'OP',,,50,,1,'Opis parametru'@);
MLCONNEC.win_act(_wsel,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
MLCONNEC.win_sel(_wsel);
MLCONNEC.cntx_psh();
MLCONNEC.index('ATNPNRTP');
MLCONNEC.prefix('ROK_F', ref_name(ALG_PAR.ROK), #ALG_PAR.ROK,_a);
{? MLCONNEC.select() || par:=($('MLCONNEC.'+_b))() ?};
MLCONNEC.cntx_pop()


\p_con_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [8.70]
:: OPIS: Sprawdza czy przekazany parametr pola (_b) jest w tabeli MLCONNEC
::   WE: _a - numer parametru w ALG_PAR
::       _b - parametr (wartosc w MLCONNEC.+_d)
::       _c - typ parametrow  w MLCONNEC
::       _d - pole przechowujace parametr
::  OLD: \p_con_po/ml_xpert.fml
::----------------------------------------------------------------------------------------------------------------------
MLCONNEC.cntx_psh();
MLCONNEC.index('ATNPNRTP');
MLCONNEC.prefix('ROK_F', ref_name(ALG_PAR.ROK), #ALG_PAR.ROK,_c,_b);
_x:={? MLCONNEC.first() || ($('ALG_PAR.P'+$_a+':=MLCONNEC.'+_d))();1 ||0?};
MLCONNEC.cntx_pop();
_x


\msdbf3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ACH [2006]
:: OPIS: F3 dla wyboru sparwozdania dla funkcji uniwersalnej MSDB
::  OLD: \msdbf3/ml_xpert.fml
::----------------------------------------------------------------------------------------------------------------------
GR_SIK.cntx_psh();
GR_SIK.index('AKCEPT'); GR_SIK.prefix(REF.FIRMA,'KALI','T');
{? GR_SIK.select() || par:=GR_SIK.SKROT ?};
GR_SIK.cntx_pop()


\kmsdbf3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ACH [2006]
:: OPIS: F3 dla wyboru kolumny dla funkcji uniwersalnej MSDB
::  OLD: \kmsdbf3/ml_xpert.fml
::----------------------------------------------------------------------------------------------------------------------
GR_SIK.cntx_psh();
GR_KOL.cntx_psh();
GR_KOL.win_sel('SLO');
GR_SIK.index('SKROT'); GR_SIK.prefix(REF.FIRMA,ALG_PAR.P1);
{? GR_SIK.first()
|| GR_KOL.index('LP');
   GR_KOL.prefix(GR_SIK.ref);
   {? GR_KOL.first()
   || {? GR_KOL.select() || par:=$GR_KOL.LP ?}
   ?}
?};
GR_KOL.cntx_pop();
GR_SIK.cntx_pop()


\wmsdbf3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ACH [2006]
:: OPIS: F3 dla wyboru wiersz dla funkcji uniwersalnej MSDB
::  OLD: \wmsdbf3/ml_xpert.fml
::----------------------------------------------------------------------------------------------------------------------
GR_SIK.cntx_psh();
DEFW.cntx_psh();
DEFW.win_sel('SLOTEN');
GR_SIK.index('SKROT'); GR_SIK.prefix(REF.FIRMA,ALG_PAR.P1);
{? GR_SIK.first()
|| DEFW.index('GRUPA');
   DEFW.prefix(GR_SIK.ref);
   {? DEFW.first()
   || {? DEFW.select() || par:=$DEFW.LP ?}
   ?}
?};
DEFW.cntx_pop();
GR_SIK.cntx_pop()


\planwpf3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ACH [2006]
:: OPIS: F3 dla paragrafu
::  OLD: \planwpf3/ml_xpert.fml
::----------------------------------------------------------------------------------------------------------------------
ML_PG.cntx_psh(); ML_PG.prefix();
{? ML_PG.select || par:=ML_PG.SP ?};
ML_PG.cntx_pop()


\planwsf3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ACH [2006]
:: OPIS: F3 dla statusu zatrudnienia
::  OLD: \planwsf3/ml_xpert.fml
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.STATRB70<>null
|| SLO.cntx_psh(); SLO.index('SL'); SLO.win_sel('ONE_SEL');
   SLO.prefix(FINFO.STATRB70().SLU);
   {? SLO.select() || par:=SLO.TR ?};
   SLO.cntx_pop()
|| FUN.info('Nie określono słownika statusów zatrudnienia.'@)
?}

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:55 10581c832e574eecb68d6e953a2cac4bf9d19c08aaae744410e643e95ab808cfaf85989ced0608f4b24a9e7a96c5d17b6083b6f0f4ab43f0d9ef1136c0d6690863010f9e875b996055bd9d26a2bb19616cae7584c3151001a05d59072919bfda02953389f8868a3dc0e5c62efb9081b173f5d466460835006895b6263fb9c8ec

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: display.fml
:: Utworzony: 01.02.2018
:: Autor: WH
::======================================================================================================================
:: Zawartość: Formuły obsługujące mechanizm podglądów dla konkretnej czynności
::======================================================================================================================


\bi_todo_display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Akcja wyświetl na liście TODO
::       Kontekst pracy - rekord TODO
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_webterm:=+app_info('web_sesid');
BI_TODO.cntx_psh();
BI_PREL.cntx_psh();
B_PREL.cntx_psh();
BI_STAT.cntx_psh();
USERS.cntx_psh();
B_DOMAIN.cntx_psh();
BI_TODO.BI_PREL();
BI_TODO.USERS();
BI_TODO.BI_STAT();
BI_TODO.B_DOMAIN();
B_ACTION.cntx_psh();
B_ACTION.index('B_ELE');
B_ACTION.prefix(BI_PREL.B_PREL().B_ELE);
{? B_ACTION.first()
|| _can_continue:=1;
   {? B_ACTION.MANUAL='T'
   ||
::    Obsługa czynności ręcznej
      _fml:=B_PREL.FMANVIEW;
      {? _fml<>''
      || _class:={? __develop || @.Class.cProMan || @.CLASS.cProMan ?};
         _mp:=obj_new(_class);
         {? _mp.setCntx(BI_TODO.BI_PREL)>0 || params_set('mp',_mp) ?};
         ($_fml)();
         _can_continue:=0
      ?}
::   |? _webterm
::   || exec('env_wt_core','#web_srv');
::      BPMN.PRIOR_R:=BI_TODO.BI_PREL().PRIOR_R;
::      BI_TODO.web_display('RED_WEB')
   |? exec('is_fun','#file',-('!'+B_ACTION.UID),'display')
   ||
::    1. Istnieje formuła dedykowana czynności, wywołuję ją
      _fml:=-('params_exec(\'display\',\'!'+B_ACTION.UID+'\')');
      _class:={? __develop || @.Class.cProMan || @.CLASS.cProMan ?};
      _mp:=obj_new(_class);
      {? _mp.setCntx(BI_TODO.BI_PREL)>0
      || params_set('mp',_mp);
         _rezultat:=($_fml)();
         {? type_of(_rezultat)=1 & _rezultat<0
         || _can_continue:=1
         || _can_continue:=0
         ?}
      ?};
      ~~
   ||
::    Sprawdzam czy są jakieś kluczowe rekordy
::    Tworzę obiekt Menadżera Procesu
      _class:={? __develop || @.Class.cProMan || @.CLASS.cProMan ?};
      _mp:=obj_new(_class);
      {? _mp.setCntx(BI_TODO.BI_PREL)>0
      || _keyRefs:=_mp.getRefs();
         {? type_of(_keyRefs)>0
         ||
            _len:=obj_len(_keyRefs);
            {! _it:=1.._len
            |? _can_continue>0
            |! {? var_pres('['+$_it+']',_keyRefs)>0
               || {? exec('record','display',_keyRefs[_it])>0
                  || _can_continue:=0
                  ?}
               ?}
            !}
         ?}
      ?}
   ?};
   {? _can_continue>0
   ||
      _todo_lang:=exec('lang','#bi_todo');
      {? _webterm
      || exec('env_wt_core','#web_srv');
         BPMN.PRIOR_R:=BI_TODO.BI_PREL().PRIOR_R;
         _red_akr:={? _todo_lang || 'RED_WEBL' || 'RED_WEB' ?};
         BI_TODO.web_display(_red_akr)
      ||
::    Standardowe wyświetl
         _ident:={? _todo_lang || '#bi_tododislang' || '#bi_tododisplay' ?};
         _red:=BI_TODO.mk_edit('Szczegóły zadania'@,,_ident);
         BI_TODO.win_esep(_red,'Terminy'@);
         BI_TODO.win_efld(_red,,'DT',,,20,,1,'Data utworzenia'@);
         BI_TODO.win_efld(_red,,'TM',,,23,,1,'Czas utworzenia'@);
         BI_TODO.win_efld(_red,,'DEADLINE',,,23,,1,'Wykonaj do'@);

         BI_TODO.win_esep(_red,'Dane zadania'@);
         BI_TODO.win_efld(_red,B_DOMAIN,'NAME',,,100,,1,'Dziedzina produktowa'@);
         BI_TODO.win_efld(_red,BI_STAT,'NAME',,,100,,1,'Status'@);
         BI_TODO.win_efld(_red,USERS,'DANE',,,100,,1,'Użytkownik'@);
         {? _todo_lang
         || BI_TODO.win_efld(_red,BI_TODO,'DESC',,,100,,1,'Opis'@)
         || BI_TODO.win_efld(_red,BI_PREL,'DESC',,,100,,1,'Opis'@)
         ?};

         BI_TODO.win_esep(_red,'Priorytety'@);
         BI_TODO.win_efld(_red,,'PRIOR_W',,,20,,1,'Priorytet użytkownika'@,,,'radio-buttons',,'Zwykły'@,"0",'Wysoki'@,"1");
         BI_TODO.win_efld(_red,BI_PREL,'PRIOR_R',,,20,,1,'Priorytet rekordu'@,,,'radio-buttons',,'Niski'@,"0",'Średni'@,"1",'Wysoki'@,"2",'Krytyczny'@,"3");

         BI_TODO.win_ebtn(_red,'text=%1'['&Zamknij'@]+',display=1','key:Esc');
         BI_TODO.win_edit(_red);
         BI_TODO.display()
      ?}
   ?}
?};
B_DOMAIN.cntx_pop();
B_ACTION.cntx_pop();
B_PREL.cntx_pop();
BI_PREL.cntx_pop();
BI_TODO.cntx_pop();
USERS.cntx_pop();
BI_STAT.cntx_pop();
~~


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Podstawowa formuła mechanizmu podglądu. Na podstawie przekazanego rekordu uruchamia odpowiednią
::       formułę z tego pliku która ma na celu wyświetlenie szczegółów dotyczących tego rekordu
::   WE: _a - TAB.ref, $TAB.ref, TAB.uidref - ref lub identyfikator rekordu
::   WY  0 - nie znaleziono formuły dla podanej tabeli
::       1 - znaleziono i uruchomiono formułę dla podanej tabeli
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;

_ref:=_a;
_TAB:=ref_tab(_ref);
{? type_of(_TAB)=type_of(~~) || return(0) ?};

_maska:=ref_name(_ref);

_formula:=2-!_TAB;

_result:=0;

_ret:='';
_TAB.cntx_psh();
{? _TAB.name()<>_maska
|| _TAB.use(_maska)
?};
_TAB.prefix();
{? _TAB.seek(_ref)
|| {? exec('is_fun','#file','display',_formula)
   || exec(_formula,'display');
      _result:=1
   ?}
|| FUN.emsg('Rekord o identyfikatorze: %1 nie istnieje,'@[exec('val2display','#convert',_ref)])
?};
_TAB.cntx_pop();
_result


\TKTL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Wyświetl dla tabeli TKTL
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('init','tte');
VAR.A_TORW:=TKTL.TORW;
VAR.A_KTL:=TKTL.ref();

_red:=exec('tktl_display_red_full','tech_head',,'Zamknij'@,0);
TKTL.win_edit(_red);
TKTL.display();
~~


\ZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Wyświetl dla tabeli ZL
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('init','tte');
_btn_open:=1;
{? ZL.STAN='N'
|| _btn_open:=0
?};
_red:=exec('zl_display_red_full','zl_head',,'Zamknij'@,0,0,_btn_open,_btn_open,1);
ZL.win_edit(_red);
__PARSES.setVal('OddzialLogProd',ZL.ODDZ);
__PARSES.setEnv(BI_TODO.ACT_UID);
ZL.display();
~~


\FAKS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.02]
:: OPIS: Wyświetl dla tabeli FAKS
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ:=FAKS.SZ;
_czyn:=exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF);
__PARSES.setEnv(_czyn);
params_exec('fall_rek','faktury_nag', 2);
~~


\FPACZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Wyświetl dla tabeli FPACZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FPACZ.win_edit('RED');
FPACZ.display()


\REM_ZGL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Wyświetl dla tabeli REM_ZGL
::----------------------------------------------------------------------------------------------------------------------
_red:=REM_ZGL.mk_edit('Zgłoszenie remontowe'@);
REM_ZGL.win_ewin(_red,,'DISP');
REM_ZGL.win_ebtn(_red,
   'text=%1,display=1,panel=bottom,align=begin'['Godziny'@],
   "exec('select_godziny','remonty_zgloszenia',,0);''"
);
REM_ZGL.win_ebtn(_red,
   'text=%1,display=1,panel=bottom,align=begin'['Materiały'@],
   "exec('select_materialy','remonty_zgloszenia',,0);''"
);
REM_ZGL.win_ebtn(_red,
   'text=%1,display=1,panel=bottom,align=begin'['Zakupy'@],
   "exec('select_zakupy','remonty_zgloszenia',,0);''"
);

REM_ZGL.win_ebtn(_red,
   'text=%1,display=1,panel=bottom,align=begin'['Z&ałączniki'@],
   "exec('attachments','remonty_zgloszenia',1);''"
);

REM_ZGL.win_ebtn(_red,'text=%1,display=1,panel=bottom,align=end'['Zamknij'@],"'key:Esc'");
REM_ZGL.win_edit(_red);
REM_ZGL.display();
~~


\ZLP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Wyświetl dla tabeli ZLP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_x:=(ZLP.name())+5;
exec('zle_open','open_tab',1+_x,2+(_x+4),_x+2);
params_exec('zlp_zwys','!lum_zgl_dzgl')


\ZK_N
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Wyświetl dla tabeli ZK_N
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('zknag_display_pozycje','zamsiw_nag')


\TR_NZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Wyświetl dla tabeli TR_NZL
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_czyn:=exec('tr_act_uid','transport_wspolne',TR_NZL.WHERE);
__PARSES.setEnv(_czyn);
exec('tr_nzl_display','transport_wspolne');
~~


\UPMZAD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.42]
:: OPIS: Wyświetl dla tabeli UPMZAD
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
UPMZAD.memo_get(,'UW');
UPMZAD.win_edit('RED');
UPMZAD.display();
~~


\GROP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Wyświetl dla tabeli GROP
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('init','tpp');
exec('init','tte');

_red:=exec('grop_display_red_full','zl_grop',{? GROP.STATUS='O' || 1 || 0 ?},1);
GROP.win_edit(_red);
__PARSES.setVal('OddzialLogProd',GROP.ODDZ);
__PARSES.setEnv(BI_TODO.ACT_UID);
GROP.display();
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:39 689433d31b36e503fd0de6a6c89c315c5ade3b3932812837b2d5d5a092c95b67718bf9a6b40ea4f46c0a757a07a5206bcc3415afcd19ee6062ed1d911008b6879ccf22adac2881699f2aa7930cc96e3ce1e99bb93eb6d4217a5701e85158c5fce4bdfc816c918725421ca2cd87a1f88a28bc9d07548cad096effda4dc096fdce

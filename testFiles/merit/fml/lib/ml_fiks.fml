:!UTF-8
::(c) Macrologic S.A. Wszelkie prawa zastrzezone
::========================================================================================================================
::Nazwa pliku: ml_fiks.fml [2010]
::Utworzony: ????/??/??
::Autor: RB
::Systemy: FIKS,FIKS_B
::========================================================================================================================
::Zawartość: Formuły podstawowe do  obsługi Finanse i księgowość wersja budżetowa
::Uwaga: Poprzednia nazwa pliku to maclex.fml
::========================================================================================================================


\tab_sl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [$1121]
:: OPIS:
::   WE: _a - kod roku, _b-wzorzec
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tabs:=sql('select distinct SLU.reference as SLUREF, SLU.NAZ
            from BUD
               left join KS
               left join ROK_F
               left join SLUAPPL
               left join SLU
            where '+{? _a<>'' || 'ROK_F.KOD='':_a'' and ' || '' ?}+' SLU.WZ='':_b''
            union all
            select distinct SLU.reference as SLUREF, SLU.NAZ
            from KS_W
               left join KS
               left join ROK_F
               left join SLUAPPL
               left join SLU
            where '+{? _a<>'' || 'ROK_F.KOD='':_a'' and ' || '' ?}+' SLU.WZ='':_b'' ',_a,_b);
_tabs2:=sql('select distinct NAZ,SLUREF from :_a',_tabs);
_wyn:='';
{? _tabs2.first
||
   {!
    |?
      _wyn+=','+_tabs2.NAZ;
      _tabs2.next
   !};
   _wyn+=','
|| _wyn:='-'
?};
_wyn


\budcorct
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RB [7.52]
:: OPIS: Formuła sprawdza czy podana struktura budżetowa istnieje, tzn czy
::       w podanym dziale istnieje dany rozdział, w nim paragraf, dalej pozycja.
::       Jeżli któryś z parametrów jest stringiem pustym lub go brak formuła przyjmuje,
::       że może tam znajdować się dowolny symbol z określonego poziomu.
::   WE: _a czesc _b dzial, _c rozdzial, _d paragraf, _e pozycja,
::       _f 'w'ydatkowe/'d'ochodowe/'k'oszotwe/'p'rzychodowe/'n'aleznosci/'z'obowiazan.
::       _g - ref do ROK_F - rok bilansowy
::       [_h] - kontrola pozycji techicznej 1-domyslne - tak, 0 - nie (kontrola nie zawsze jest potrzebna)
::       [_i] - rodzaj paragrafu - jesli podana i podany paragraf to sprawdza zgodnosc
::       Wszystkie parametry są opcjonalne.
::   WY: 1 - ścieżka istnieje, ustawione odpowiednie rekordy w tabelach ML_CZ,ML_DZ,ML_RZ,ML_PG,ML_PP
::       0 - nie istnieje
:: HIST: AK [8.70] 17.01.2006 - poprawa obsługi tabeli ROK_F
::       RB [8.70] 18.01.2006
::    1) usunięcie fragmentów martwego kodu
::    2) ustawianie rekordów o zgodnych z podanymi parametrami symbolach w tabelach
::       ML_CZ,ML_DZ,ML_RZ,ML_PG,ML_PP jeśli odpowiedni dla danej tabeli parametr jest pusty
::    3) umożliwienie podania pozycji (_d) i typu (_e) jako jedynych parametrów
::       AK [8.70] 09.02.2006 - poprawa obsługi paragrafów o tym samym kodzie na
::                 kilku rozdziałach i dodanie obsługi zaangażowania
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0; _rok_f:=null;
{? _<1 | (type_of(_a)<>2) || _a:='' ?};
{? _<2 | (type_of(_b)<>2) || _b:='' ?};
{? _<3 | (type_of(_c)<>2) || _c:='' ?};
{? _<4 | (type_of(_d)<>2) || _d:='' ?};
{? _<5 | (type_of(_e)<>2) || _e:='' ?};
{? _<6 | (type_of(_f)<>2) || _f:='' ?};
{? _f='XXX' || _f:='' ?};
{? _<7 | (type_of(_g)<>7) || _g:=SSTALE.AR ?};
{? _<8 | (type_of(_h)<>1) || _h:=1 ?};
{? _<9 | (type_of(_i)<>2) || _i:='' ?};
_cz:=_a;
_dz:=_b;
_rz:=_c;
_pg:=_d;
_pgr:=_i;
_pp:=_e;
_rodz:=_f;
_rok:=_g;
_techk:=_h;
ROK_F.cntx_psh;
ROK_F.prefix;
{? ROK_F.seek(_rok)
|| {? _pp<>''
   || ML_PP.index('PPKPNP');
      ML_PP.prefix(ROK_F.ref,_pp);
      {? ML_PP.first
      || {! |?
              ML_PP.PG().RZ().DZ().CZ();
              _wynr:={? _rodz=''
                     || 1
                     |? _techk=1 & ML_PG.RP='I'
                     || 1
                     || {? 'ZOB,NAL'*_rodz
                        || 1
                        || ML_PG.RP*{? 'WYD,KOS,PLW,SKO,SWY,SZO,ZNG,FSO'*_rodz
                                    || 'W'
                                    |? 'DOC,PRZ,PLD,SDO,SPR,SNA'*_rodz
                                    || 'D'
                                    || '?'
                                    ?}
                        ?}
                     ?};
               _wyn:={? _pg<>''
                     || ML_PG.SP=_pg & {? _pgr<>'' || ML_PG.RP=_pgr || 1 ?}
                     || 1
                     ?}
                     & _wynr
                    &{? _pg<>'' || ML_PG.SP=_pg  & {? _pgr<>'' || ML_PG.RP=_pgr || 1 ?} || 1 ?}
                    &{? _rz<>'' || ML_RZ.SR=_rz || 1 ?}
                    &{? _dz<>'' || ML_DZ.SD=_dz || 1 ?}
                    &{? _cz<>'' || ML_CZ.SYM=_cz || 1 ?};
               {? ~_wyn || ML_PP.next ?}
         !}
      ?}
   |? _pg<>''
   || ML_PG.index('PGSPNP');
      ML_PG.prefix(ROK_F.ref,_pg);
      {? ML_PG.first
      || {! |?
              ML_PG.RZ().DZ().CZ();
              _wynr:={? _rodz=''
                     || 1
                     |? _techk=1 & ML_PG.RP='I'
                     || 1
                     || {? 'ZOB,NAL'*_rodz
                        || 1
                        || ML_PG.RP*{? 'WYD,KOS,PLW,SKO,SWY,SZO,ZNG,FSO'*_rodz
                                    || 'W'
                                    |? 'DOC,PRZ,PLD,SDO,SPR,SNA'*_rodz
                                    || 'D'
                                    || '?'
                                    ?}
                        ?}
                     ?};
              _wyn:=_wynr
                    &{? _rz<>'' || ML_RZ.SR=_rz || 1 ?}
                    &{? _dz<>'' || ML_DZ.SD=_dz || 1 ?}
                    &{? _cz<>'' || ML_CZ.SYM=_cz || 1 ?}
                    & {? _pgr<>'' || ML_PG.RP=_pgr || 1 ?};
               {? ~_wyn || ML_PG.next ?}
         !}
      ?}
   |? _rz<>''
   || ML_RZ.index('RZSRNR');
      ML_RZ.prefix(ROK_F.ref,_rz);
      {? ML_RZ.first
      || {!
         |?
            ML_RZ.DZ().CZ();
            _wyn:={? _dz<>'' || ML_DZ.SD=_dz || 1 ?}
                  &{? _cz<>'' || ML_CZ.SYM=_cz || 1 ?};
            {? ~_wyn || ML_RZ.next ?}
         !}
      ?}
   |? _dz<>''
   || ML_DZ.index('DZSDND');
      ML_DZ.prefix(ROK_F.ref,_dz);
      {? ML_DZ.first
      || {!
         |?
            ML_DZ.CZ();
            _wyn:={? _cz<>'' || ML_CZ.SYM=_cz || 1 ?};
            {? ~_wyn || ML_DZ.next ?}
         !}
      ?}
   |? _cz<>''
   || ML_CZ.index('CZSYMN');
      ML_CZ.prefix(ROK_F.ref,_cz);
      _wyn:=ML_CZ.first
   ?}
?};
ROK_F.cntx_pop;
_wyn


\uslo_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Ustawia _b w pop_slo w wspol.fml
::   WE: _a RS.TAB
::----------------------------------------------------------------------------------------------------------------------
$('exec(\'aputbz\',\'ml_fiks\','+{? _a='ML_CZ' || '0' |? _a='ML_DZ' || '1' |? _a='ML_RZ' || '2' |? _a='ML_PG' || '3' || '4' ?}+');0')


\aputbz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [$1120]
:: OPIS: Po akcji popraw pola ML_DZ, ML_RZ, ML_PG, ML_PP
::   WE: _a miejsce wywołania
::          0-czesc
::          1-dzial
::          2-rozdzial
::          3-paragraf
::          4-pozycja par.
::----------------------------------------------------------------------------------------------------------------------
exec('ini_kom','#message','Uwagi poprawy słownika struktury rodzajowej');
{? _a=0
|| _kod:=ML_CZ.SYM;
   {? ML_CZ.edit
   || do();
      ML_CZ.put();
:      exec('akt_kody','ml_fiks',0,ML_CZ.ref);
      {? ~exec('spr_slo','ml_zad','ML_CZ',_kod) | ~exec('pop_slo','ml_zad','ML_CZ',_kod)
      || undo()
      ?};
      end()
   ?}
|? _a=1
|| _kod:=ML_DZ.SD;
   {? ML_DZ.edit
   || do();
      ML_DZ.put();
:      exec('akt_kody','ml_fiks',1,ML_DZ.ref);
      {? ~exec('spr_slo','ml_zad','ML_DZ',_kod) | ~exec('pop_slo','ml_zad','ML_DZ',_kod)
      || undo()
      ?};
      end()
   ?}
|? _a=2
|| _kod:=ML_RZ.SR;
   {? ML_RZ.edit
   || do();
      ML_RZ.put();
:      exec('akt_kody','ml_fiks',1,ML_RZ.ref);
      {? ~exec('spr_slo','ml_zad','ML_RZ',_kod) | ~exec('pop_slo','ml_zad','ML_RZ',_kod)
      || undo()
      ?};
      end()
   ?}
|? _a=3
|| _kod:=ML_PG.SP;
   {? ML_PG.edit
   || do();
      ML_PG.put();
:      exec('akt_kody','ml_fiks',1,ML_PG.ref);
      {? ~exec('spr_slo','ml_zad','ML_PG',_kod) | ~exec('pop_slo','ml_zad','ML_PG',_kod)
      || undo()
      ?};
      end()
   ?}
|? _a=4
|| _kod:=ML_PP.KP;
   {? ML_PP.edit
   || do();
      ML_PP.put();
:      exec('akt_kody','ml_fiks',1,ML_PP.ref);
      {? ~exec('spr_slo','ml_zad','ML_PP',_kod) | ~exec('pop_slo','ml_zad','ML_PP',_kod)
      || undo()
      ?};
      end()
   ?}
?};
exec('end_kom','#message');
0


\kod_cz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1210]
:: OPIS: Podaje domyślny kod części
::----------------------------------------------------------------------------------------------------------------------
SSTALE.CZ


\f3kodcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1210]
:: OPIS: Podaje domyślny kod części
::----------------------------------------------------------------------------------------------------------------------
ML_CZ.index('CZSYMN');
ML_CZ.prefix(STALE.AR);
{? ML_CZ.first
|| {? ML_CZ.select
   || SSTALE.CZ:=ML_CZ.SYM
   ?}
|| SSTALE.CZ:=''
?}


\aekodcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1210]
:: OPIS: Podaje domyślny kod części
::----------------------------------------------------------------------------------------------------------------------
::ML_CZ.index('CZSYMN');
::ML_CZ.prefix(SSTALE.AR,SSTALE.CZ);
::{? SSTALE.CZ='' | (ML_CZ.first & STALE.CZ=ML_CZ.SYM)
::|| SSTALE.CZ:=STALE.CZ;
::1
::|| FUN.error('Błędny kod części'@);
::   0
::?}


\slo_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS []
:: OPIS:
::   WE:
::----------------------------------------------------------------------------------------------------------------------
{? ($("~"+RS.TAB+".find_key(ROK_F.ref,SLO.KOD,SLO.TR)"))()
|| 'SLO#11#01'
|| ''
?}


\dl_sb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [$1210]
:: OPIS: Ustala dlugosc slownika
::   WE: _a - miejsce wywolania
::            0 - czesc
::            1 - dzial
::            2 - rozdzial
::            3 - paragraf
::            4 - pozycja pg
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__Rokbud')=7
|| {? ~ROK_F.seek(__Rokbud) || return(0) ?};
   _kodr:=ROK_F.KOD
|| _kodr:=SSTALE.AR().KOD
?};
{? _a=0
|| _dl:=#exec('conn','ml_zad','GLOBAL','DL_CZ',_kodr);
   {? _dl<>0 || _dl || 5 ?}
|? _a=1
|| _dl:=#exec('conn','ml_zad','GLOBAL','DL_DZ',_kodr);
   {? _dl<>0 || _dl || 3 ?}
|? _a=2
|| _dl:=#exec('conn','ml_zad','GLOBAL','DL_RZ',_kodr);
   {? _dl<>0 || _dl || 5 ?}
|? _a=3
|| _dl:=#exec('conn','ml_zad','GLOBAL','DL_PG',_kodr);
   {? _dl<>0 || _dl || 4 ?}
|? _a=4
|| _dl:=#exec('conn','ml_zad','GLOBAL','DL_PP',_kodr);
   {? _dl<>0 || _dl || 1 ?}
|| 0
?}


\edit_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [12.30]
:: OPIS: edycja wyroznikow w ewidencji pomocniczej
::   WE: _a - 1 okres nie zamkniety, 0 - zamkniety
::----------------------------------------------------------------------------------------------------------------------
_edit:=_a;
{? exec('conn','ml_zad','GLOBAL','ED_WBZ')='T'
|| _edit:=1
?};
__T_wyr:=tab_tmp(5,'TREE', 'TREE_REF', 'Drzewo',
                   'POZ', 'INTEGER', 'Poziom',
                   'NRW', 'INTEGER', 'Nr poziomu wyróżnika',
                   'KONS', 'STRING[35]', 'Konto/Słownik',
                   'KON', 'STRING[35]', 'Konto',
                   'SUM', 'REAL', 'Kwota',
                   'STR', 'STRING[2]', 'Strona',
                   'DOK', 'STRING[40]', 'Dokument',
                   'DZAP', 'DATE', 'Data zapisu',
                   'DOP', 'DATE', 'Data operacji',
                   'REJ', 'STRING[10]', 'Rejestr',
                   'NR', 'INTEGER', 'Nr w rejestrze',
                   'CZY_WYR', 'STRING[1]', 'Znacznik wyróżnika',
                   'CZY_BZ', 'STRING[1]', 'Znacznik budżetu zadaniowego',
                   'AR', 'STRING[4]', 'Rok',
                   'AO', 'STRING[2]', 'Miesiąc',
                   'ODD', 'STRING[8]', 'Oddział',
                   'OP', 'STRING[100]', 'Opis',
                   'SREF', 'STRING[16]', 'SQL ref POZ',
                   'SREFW', 'STRING[16]', 'SQL ref POW',
                   'REF', 'INTEGER', 'Ref do rekordu');
:: parametr czy tylko puste
__par_p:=0;
:: parametr czy ograniczenie do slownikow i lista slownikow
__par_s:=0;
__Tpar_sl:=tab_tmp(,'SL', 'STRING[20]', 'Słownik'
                  ,'F', 'STRING[1]', 'Użyto w filtrze'
                  ,'SREF', 'STRING[16]' ,'SQL ref SLO');

exec('get_wyr','ml_fiks');

_ind:=__T_wyr.ndx_tmp(,,'TREE',,0,'KON',,0,'NRW',,0,'DOK',,0);
__T_wyr.index(_ind);

POZ.first();
__sslo:=__Tpar_sl.mk_sel('Dostępne słowniki wyróżników'@,'P',,'#mleswyr43');
__Tpar_sl.win_fld(__sslo,,'SL',,,20,,,'Nazwa słownika'@);
__Tpar_sl.win_fld(__sslo,,'F',,,-3,,,'Z?'@,,'Wybrany do filtra?'@,2,,"'T'","'N'");
__Tpar_sl.win_act(__sslo,,'Szukaj');
_fb:="
   __Tpar_sl.F:={? __Tpar_sl.F='N' || 'T' || 'N' ?};
   __Tpar_sl.put
";
__Tpar_sl.win_act(__sslo,,'Formuła','Wybierz'@@,,'Zaznacz/Usuń zaznaczenie słownika',_fb,,1,,,,'W');
_fb:="
   sel_exit()
";
__Tpar_sl.win_act(__sslo,,'Formuła','Wyjście'@@,,'Wyjście i naniesienie filtra',_fb,,,,,,'W');

__Tpar_sl.win_sel(__sslo);

__swyr:=__T_wyr.mk_sel('Edycja wyróżników '@,'P',,'#ml_ewyr23',,,,1);
__T_wyr.win_fld(__swyr,,'KONS',,,25,,,'Konto/Słownik wyróżnika'@);
__T_wyr.win_fld(__swyr,,'DOK',,,25,,,'Dokument/Kod wyróżnika'@);
__T_wyr.win_fld(__swyr,,'SUM',,,12,2,,'Kwota'@);
__T_wyr.win_fld(__swyr,,'STR',,,3,,,'Strona'@,,'Strona księgowania'@);
__T_wyr.win_fld(__swyr,,'DZAP',,,10,,,'Data zap.'@,,'Data zapisu księgowania'@);
__T_wyr.win_fld(__swyr,,'OP',,,24,,,'Opis'@);
__T_wyr.win_fld(__swyr,,'CZY_WYR',,,2,,,'Wyr'@);
__T_wyr.win_fld(__swyr,,'CZY_BZ',,,2,,,'BZ'@);

__T_wyr.win_act(__swyr,,'Formuła','Zwiń/&rozwiń'@@,,'Zwija/rozwija wszystkie gałęzie'@,"",
        "__T_wyr.prefix();
         {! |? {? __T_wyr.TREE || __T_wyr.seek(__T_wyr.TREE,) || 0 ?} !};
         {? __T_wyr.tr_state || __T_wyr.tr_set(0,__swyr) || __T_wyr.tr_set(1,__swyr) ?}
        ",,,,,'R');
__T_wyr.win_act(__swyr,,'Szukaj');
{? _edit
|| _fb:="
      {? POZ.seek(BB.sqlint(__T_wyr.SREF),POZ.name())
      || KS.cntx_psh();
         KS.index('SYM'); KS.prefix(ROK_F.ref());
         {? KS.find_key(ROK_F.SYNT+POZ.KON)
         || POW.cntx_psh();
            _wyn:=exec('akt_wyr','dok_fks');
            POW.cntx_pop();
            {? _wyn
            || exec('ini_kom','#message','Uwagi do aktualizacji',,,,200);
               KOMT:='';
               _zm:=0;
               {? __T_wyr.POZ=0
               || _zm:=POZ.edit()
               |? __T_wyr.POZ>0
               ||
                  ROK_F.cntx_psh();
                  ROK_F.index('KOD'); ROK_F.prefix(REF.FIRMA); ROK_F.find_key(4-POZ.name()-2);
                  _k:=exec('jak_kon','konto',ROK_F.SYNT+POZ.KON);
                  {? _k<>null
                  ||
                     exec('pr_kw','wyrozniki',__T_wyr.NRW);
                     KS_W.cntx_psh();
                     KS_W.index('LP');
                     KS_W.prefix(_k);
                     {? KS_W.find_key(__T_wyr.NRW)

                     || exec('f3_kw','wyrozniki',__T_wyr.NRW);
                        _zm:=1
                     ?};
                     KS_W.cntx_pop()
                  ?};
                  ROK_F.cntx_pop()
               ?};
               {? _zm=1
               || exec('pop_wyr','wyrozniki');
                  exec('get_wyr','ml_fiks')
               ?};
               {? KOMT<>''
               || exec('add_kom','#message',KOMT,4,'Dokument '+POZ.DOK().NK,,1)
               ?};
               exec('end_kom','#message')
            ?}
         ?};
         KS.cntx_pop()
      ?}
   ";
   __T_wyr.win_act(__swyr,,'Formuła','Popraw wyróżniki'@@,,,_fb,,1,,,,'P')
?};
__T_wyr.win_act(__swyr,1,'Menu','Zakres'@,,,"");
__T_wyr.win_act(__swyr,,'Menu','Zakres'@,,,"");
_fb:="
   exec('zak_wyr','wyrozniki',1);
   {?  KA.PREFIX=''
   || __T_wyr.act_icn(__swyr,1,'ZP');
      __T_wyr.act_icn(__swyr,,'ZP')
   || __T_wyr.act_icn(__swyr,1,'ZP','CHECKED');
      __T_wyr.act_icn(__swyr,,'ZP','CHECKED')
   ?};
   exec('get_wyr','ml_fiks')
";
__T_wyr.win_act(__swyr,1,'Formuła','Prefix konta'@@,'Zakres'@,'Nałóż prefix na konto'@,_fb,,,,,,'P');
__T_wyr.win_act(__swyr,,'Formuła','Prefix konta'@@,'Zakres'@,'Nałóż prefix na konto'@,_fb,,,,,,'P');
_fb:="
   {? __par_p=1
   || __par_p:=0;
      __T_wyr.act_icn(__swyr,,'ZU')
   || __par_p:=1;
      __T_wyr.act_icn(__swyr,,'ZU','CHECKED')
   ?};
   exec('get_wyr','ml_fiks')
";
__T_wyr.win_act(__swyr,1,'Formuła','P&uste'@@,'Zakres'@,'Pokaż tylko z pustymi zapisami'@,_fb,,,,,,'P');
__T_wyr.win_act(__swyr,,'Formuła','P&uste'@@,'Zakres'@,'Pokaż tylko z pustymi zapisami'@,_fb,,,,,,'P');
_fb:="
   __Tpar_sl.select;
   __par_s:=0;
   {? __Tpar_sl.first()
   || {!
      |?
         {? __Tpar_sl.F='N' || __par_s:=1 ?};
         __Tpar_sl.next()
      !}
   ?};
   {? __par_s=1
   || __T_wyr.act_icn(__swyr,1,'ZS','CHECKED');
      __T_wyr.act_icn(__swyr,,'ZS','CHECKED')
   || __T_wyr.act_icn(__swyr,1,'ZS');
      __T_wyr.act_icn(__swyr,,'ZS')
   ?};
   exec('get_wyr','ml_fiks')
";
__T_wyr.win_act(__swyr,1,'Formuła','Wybór &słownika'@@,'Zakres'@,'Wybór słownika wyróżników'@,_fb,,,,,,'S');
__T_wyr.win_act(__swyr,,'Formuła','Wybór &słownika'@@,'Zakres'@,'Wybór słownika wyróżników'@,_fb,,,,,,'S');
__T_wyr.win_act(__swyr,1,'Formuła','--Y','Zakres'@,,"",,,,,,'Y');
__T_wyr.win_act(__swyr,,'Formuła','--Y','Zakres'@,,"",,,,,,'Y');
_fb:="
   __par_p:=__par_s:=0;
   {? __Tpar_sl.first()
   || {!
      |?
         __Tpar_sl.F:='T';
         __Tpar_sl.put();
         __Tpar_sl.next()
      !}
   ?};
   __Tpar_sl.first();
   exec('zak_wyr','wyrozniki',0);
   exec('get_wyr','ml_fiks');
   __T_wyr.act_icn(__swyr,1,'ZU');
   __T_wyr.act_icn(__swyr,,'ZU');
   __T_wyr.act_icn(__swyr,1,'ZS');
   __T_wyr.act_icn(__swyr,,'ZS');
   __T_wyr.act_icn(__swyr,1,'ZP');
   __T_wyr.act_icn(__swyr,,'ZP')
";
__T_wyr.win_act(__swyr,1,'Formuła','Wszystko'@@,'Zakres'@,,_fb,,,,,,'W');
__T_wyr.win_act(__swyr,,'Formuła','Wszystko'@@,'Zakres'@,,_fb,,,,,,'W');
__T_wyr.win_act(__swyr,,'Formuła','Op&is konta'@@,,,"exec('op_konta','konto',__T_wyr.KON)",,,,,,'I');
__T_wyr.win_act(__swyr,,'Formuła','Wydruk'@@,,,,"exec('rep_exec','#b_report','','poz_wyr',,,,,,,,'W')",,,,,'W');
__T_wyr.win_act(__swyr,,'Formuła','Wyświetl &e-dokument'@@,,,,
   "{? POZ.seek(BB.sqlint(__T_wyr.SREF),POZ.name()) || exec('efakview','dok_fks') ?}",,,,,'E');
__T_wyr.win_act(__swyr,,'Rekord','',,'',"exec('rekprzed','color','MZWY#01#')");
__T_wyr.win_act(__swyr,,'Formuła','Legenda'@@,,,"exec('legenda','color','MZWY#0')",,,,,,'L');
__T_wyr.win_act(__swyr,,'Kolejność');

::   __T_wyr.win_act(_w_acr,,'Formuła','Zmień widok',,'Zmienia widok na edycję każdego słownika osobno',"__wid:=1;sel_exit");

:   _grp:=POZ.grp_make('Edycja wyróżników ',,'#ml_edwyr65');
:   POZ.grp_sel(_grp,POZ,'WYRD',,"",0,0,,"","",,,'maximized_with_title');
:   POZ.grp_splt(_grp,,'horizontal','gora');

:   POZ.grp_sel(_grp,__T_wyr,__swyr,,,,,,"","",,,'maximized_with_title');
:   POZ.win_sel(_grp);
:   POZ.select();

__T_wyr.win_sel(__swyr);
__T_wyr.select();
__T_wyr.ndx_drop();
VAR_DEL.delete('__T_wyr','__par_p','__par_s','__Tpar_sl','__swyr','__sslo')


\get_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [12.30]
:: OPIS: Pobranie danych o pozycjach i wyroznikach
::----------------------------------------------------------------------------------------------------------------------
POW.index('POZ');
ML_BZAD.index('MLBZP');
_kon:=__T_wyr.KON;
_kons:=__T_wyr.KONS;
_nrw:=__T_wyr.NRW;
_sref:=__T_wyr.SREF;
__T_wyr.erase();
{? POZ.first()
|| {!
   |?
      __T_wyr.blank();
      {? SSTALE.AO().NR=0 | POZ.KOM=null
      || {? POZ.KON<>''
         || SSTALE.AR();
            KS.index('SYM'); KS.prefix(ROK_F.ref(),ROK_F.SYNT+POZ.KON);
            _ks:={? KS.first() || KS.ref || null ?}
         || _ks:=null
         ?}
      || _ks:=POZ.KOM().KS
      ?};
      _k:={? _ks || exec('czy_wyr','wyrozniki',_ks) || 0 ?};
      {? _k>0 || _p:=exec('czy_pow','wyrozniki',POZ.ref()) ?};
      __T_wyr.CZY_WYR:={? _k>0
                       || {? _k*POZ.SUM$2<>_p || '-'|| '+' ?}
                       || ''
                       ?};
      KS.cntx_psh();
      __T_wyr.CZY_BZ:={? _ks<>null & KS.seek(_ks)
                      || {? KS.BZ='R' || '-' |? KS.BZ='T' || 'A' || '' ?}
                      || ''
                      ?};
      KS.cntx_pop();
      {? __T_wyr.CZY_WYR<>'' & (__par_p=0 | __T_wyr.CZY_WYR='-')
      || POZ.KOM().KS();
         __T_wyr.TREE:=0;
         __T_wyr.POZ:=0;
         __T_wyr.KON:=POZ.KON;
         __T_wyr.KONS:=POZ.KON;
         __T_wyr.SUM:=POZ.SUM;
         __T_wyr.STR:=POZ.STR;
         __T_wyr.DOK:=POZ.DOK().NK;
         __T_wyr.DZAP:=POZ.DOK().DTW;
         __T_wyr.DOP:=POZ.DOK().DOP;
         __T_wyr.REJ:=POZ.DOK().REJ().KOD;
         __T_wyr.NR:=POZ.DOK().NR;
         __T_wyr.SREF:=BB.refsql(POZ.ref);
         {? __T_wyr.CZY_BZ='-'
         ||
            ML_BZAD.prefix(POZ.ref());
            {? ML_BZAD.first()
            || __T_wyr.CZY_BZ:='+'
            ?}
         ?};
         {? __T_wyr.add(1)
         || _del:=0;
            _tref:=__T_wyr.ref() ;
            {? PAR_SKID.get(35)='T' & (_k:=exec('jak_kon','konto',ROK_F.SYNT+POZ.KON))<>null
            || KS_W.cntx_psh();
               SLU.cntx_psh();
               SLUAPPL.cntx_psh();
               POW.cntx_psh();
               POW.index('POZ');
               POW.prefix(POZ.ref());
               KS_W.index('LP');
               KS_W.prefix(_k);
               _bo:=exec('roz_bo','dok_fks');
               {! _i:=1..6 |!
                  {? KS_W.find_key(_i)
                  || {? __par_s=0 | __Tpar_sl.find_key(KS_W.SLU().SLU().NAZ,'T')
                     || _del:=0;
                        {? ~__Tpar_sl.find_key(KS_W.SLU().SLU().NAZ)
                        || __Tpar_sl.SL:=KS_W.SLU().SLU().NAZ;
                           __Tpar_sl.F:='T';
                           __Tpar_sl.add()
                        ?};
                        {? POW.find_key(_i) & POW.SLU=KS_W.SLU & KS_W.ROZDZ<>'T' & ~_bo
                        || __T_wyr.blank();
                           __T_wyr.TREE:=_tref;
                           __T_wyr.POZ:=1;
                           __T_wyr.NRW:=_i;
                           __T_wyr.KON:=POZ.KON;
                           __T_wyr.KONS:=POW.SLU().SLU().NAZ;
                           __T_wyr.DOK:=POW.SLO().KOD;
                           __T_wyr.SUM:=POW.KW;
                           __T_wyr.NR:=POZ.DOK().NR;
                           __T_wyr.SREF:=BB.refsql(POZ.ref);
                           __T_wyr.SREFW:=BB.refsql(POW.ref);
                           __T_wyr.add()
                        |? KS_W.ROZDZ='T' | _bo
                        || __T_wyr.blank();
                           __T_wyr.TREE:=_tref;
                           __T_wyr.POZ:=1;
                           __T_wyr.NRW:=_i;
                           __T_wyr.KON:=POZ.KON;
                           __T_wyr.KONS:=KS_W.SLU().SLU().NAZ;
                           __T_wyr.DOK:='';
                           __T_wyr.SUM:=POZ.SUM;
                           __T_wyr.NR:=POZ.DOK().NR;
                           __T_wyr.CZY_WYR:='R';
                           __T_wyr.SREF:=BB.refsql(POZ.ref);
                           {? __T_wyr.add(1)
                           || _tref2:=__T_wyr.ref();
                              POW.cntx_psh();
                              POW.prefix(POZ.ref(),_i);
                              {? POW.first()
                              || {!
                                 |?
                                    __T_wyr.blank();
                                    __T_wyr.TREE:=_tref2;
                                    __T_wyr.POZ:=2;
                                    __T_wyr.NRW:=_i;
                                    __T_wyr.KON:=POZ.KON;
                                    __T_wyr.KONS:=POW.SLU().SLU().NAZ;
                                    __T_wyr.DOK:=POW.SLO().KOD;
                                    __T_wyr.SUM:=POW.KW;
                                    __T_wyr.CZY_WYR:='R';
                                    __T_wyr.NR:=POZ.DOK().NR;
                                    __T_wyr.SREF:=BB.refsql(POZ.ref);
                                    __T_wyr.SREFW:=BB.refsql(POW.ref);
                                    __T_wyr.add();
                                    POW.next()
                                 !}
                              ?};
                              POW.cntx_pop()
                           ?}
                        || __T_wyr.blank();
                           __T_wyr.TREE:=_tref;
                           __T_wyr.POZ:=1;
                           __T_wyr.NRW:=_i;
                           __T_wyr.KON:=POZ.KON;
                           __T_wyr.KONS:=KS_W.SLU().SLU().NAZ;
                           __T_wyr.DOK:='';
                           __T_wyr.NR:=POZ.DOK().NR;
                           __T_wyr.SUM:=POZ.SUM;
                           __T_wyr.SREF:=BB.refsql(POZ.ref);
                           __T_wyr.add()
                        ?}
                     || _del:=1
                     ?}
                  ?}
               !};
               KS_W.cntx_pop();
               SLU.cntx_pop();
               SLUAPPL.cntx_pop();
               POW.cntx_pop()
            ?};
            {? _del=1 || __T_wyr.del ?}
         ?}
      ?};
      POZ.next()
   !}
?};
{? _kon<>''
|| __T_wyr.blank(1);
   __T_wyr.KON:=_kon;
   __T_wyr.KONS:=_kons;
   __T_wyr.NRW:=_nrw;
   __T_wyr.SREF:=_sref;
   {? ~__T_wyr.find_rec()
   || __T_wyr.first()
   ?}
|| __T_wyr.first()
?}


\usun_pozml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ACH [ML 2006]
:: OPIS: Usuniecie dekretacji z MLBUD_KR podczas usuwania pozycji dokumentu
::   WE: _a - POZ.ref()
::  OLD: \usun_poz/ml_plany.fml
::  OLD: \usun_pozml/!fks_dks_dark.fml
::----------------------------------------------------------------------------------------------------------------------
MLBUD_KR.cntx_psh();
MLBUD_KR.index('BUD_POZ');
_fn:="
   MLBUD_KR.prefix(SSTALE.AR().POCZ_ROK,_a);
   {? MLBUD_KR.first()
   ||
      {!|?
         {? MLBUD_KR.POZYCJA=_b
         || {? MLBUD_KR.ZATW='D'
            || MLBUD_KR.POZYCJA:=null;
               MLBUD_KR.DOK:=null;
               MLBUD_KR.SYMB_DOK:=''
            || MLBUD_KR.APOZ:=null;
               MLBUD_KR.ADOK:=null;
               MLBUD_KR.ASYMB_DO:=''
            ?};
            MLBUD_KR.ZATW:={? MLBUD_KR.ZATW='D' || 'T' || 'D' ?};
            MLBUD_KR.cntx_psh();
            MLBUD_KR.prefix();
            MLBUD_KR.put();
            MLBUD_KR.cntx_pop()
         ?};
         MLBUD_KR.next()
      !}
   ?}
";
_fn('D',_a);
_fn('A',_a);
MLBUD_KR.cntx_pop()

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 0243fcc7055da9ea03dfc24985b2c88898824c80b4af0bccd6a365cf9dbc9f34ea527c0c3de5c5c318e010b360160d0ae9eb9030142d7f6e9897d117b546b7e0fe461d9039a41a602d53f7191fab949be270c346162e0503532747ba735966f18ce54ed0be29a46da71491a086644779a5c73eca4ef6286de74f43653f6a7a41

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_por.fml
:: Utworzony: 01.07.2019
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Formuły wspólne dla czynności dziedziny PPK dla webterma.
::======================================================================================================================

:: ------ Obszar roboczy --------

\ppk_por
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Główna formuła obszaru roboczego PPK_POR - Pracownicze plany kapitałowe - webterm.
::----------------------------------------------------------------------------------------------------------------------
{? exec('env_wt','b_proces') & exec('init','ppk_por')
|| __PERSONEL:=1;
   web_params_set(
      exec('obj_ntab_set','#array',,'CUR_PRAC_SETID','JA')
   );
   {? exec('load_cur_prac','p_web')
   || AreaTitle.setTitle();

:: wyświetlamy okno grupowe z wizytówką zapamiętanego pracownika oraz z czynnościami podpiętymi pod przyciski
      P.web_select('WEBGRPPK')
   ?}
?}


\ppk_umo_web_ppk1_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Przed obsługą okna WEB_PPK1 w oknie grupowym obszaru WEBGRPPK
::----------------------------------------------------------------------------------------------------------------------
{? exec('set_env','ppk_por')
|| _firma:=exec('ref_firma','ustawienia');
   _osoba:=P.OSOBA;
   _txt:='';
   PPK_UMO.index('F_OD');
   PPK_UMO.prefix(_firma);
   _ref:=exec('web_global_params_get','pkw','PPK_POR_','PPK_UMO',null());
   {? _ref & PPK_UMO.seek(_ref)
   || _txt:=PPK_UMO.PPK_UZA().ADRES().NAZWA
   || {? PPK_UMO.find_tab(,'OD',,'<=',date()) | PPK_UMO.last()
      || _ref:=PPK_UMO.ref();
         _txt:=PPK_UMO.PPK_UZA().ADRES().NAZWA;
         exec('web_global_params_set','pkw','PPK_POR_','PPK_UMO',_ref)
      || PPK_UMO.blank(1)
      ?}
   ?};
   _ucz:=exec('ppk_ucz_first','ppk_por',_ref,_osoba);
   {? ~_ucz
::    jeżeli nie znajdziemy osoby jako uczestnika to nie pokazujemy również informacji o umowie o prowadzenie
   || _txt:='';
      PPK_UCZ.blank(1);
      PPK_UMO.blank(1)
   ?};
   POLA_GRP.TXT_1:=_txt;
:: jeżeli osoba nie jest uczestnikiem to wyszarzamy przycisk ze zmianą umów
   P.web_btn_eopt('WEBGRPPK','PPK_UMO','ZMIEŃ','state=%1'[{? _ucz || 'normal' || 'grayed' ?}])
?}


\ppk_ucz_first
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Ustala pierwszy rekord w dziedzinie PPK_UCZ na podstawie parametrów
::   WE: _a [REFERENCE] - wskazanie na PPK_UMO
::       _b [REFERENCE] - wskazanie na OSOBA
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_ppk_umo:={? var_pres('_a')=type_of(null()) || _a || return(_result) ?};
  _osoba:={? var_pres('_b')=type_of(null()) || _b || return(_result) ?};
PPK_UCZ.index('UNIQUE');
PPK_UCZ.prefix(_ppk_umo,_osoba);
PPK_UCZ.first()


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Formuła inicjująca dla dziedziny PPK.
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
BPMN.SYM_DOM:='PPK';

:: Otwarcie masek - pracownicy i zatrudnienie
exec('open_ppk','open_tab');

exec('czytaj','#stalesys',,KST,KST_PAR,KST_PPK,XINFO);
exec('__F_ZATR','object');
exec('__KAL','object');
exec('__RUB','object');

exec('p_web_cx_set','p_web')>0


\set_env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Ustawienie środowiska
::   WE: [_a] [INTEGER] - czy przekazywać parametry przez web_params_set (domyślnie tak)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_push:={? var_pres('_a')=type_of(0) || _a || 1 ?};
{? +app_info('web_sesid')
||
:: Przekazanie parametrów "dalej".
   {? _push
   || web_params_set(web_params_get())
   ?};
:: Ustawiam środowisko i odczytuje zapamiętany kontekst tabeli P
   exec('env_wt','b_proces') & exec('init','ppk_por') & exec('load_cur_prac','p_web') & exec('open_ppk','open_tab')
?}

:: ------ Akcje przycisków --------

\ppk_umo_web_btn_cntx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Funkcja zmieniająca kontekst danych umowy o prowadzenie
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','ppk_por');
{? exec('env_wt','b_proces') & exec('load_cur_prac','p_web')
|| PPK_UMO.web_select('WEB_WER','#ppk_umo_web',1)
?};
~~


\ppk_umo_web_wer_wybierz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Obsługa akcji/przycisku "Wybierz" z okna wertowania WEB_WER tabeli PPK_UMO.
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return() ?};

{? exec('set_env','ppk_por')
|| PPK_UMO.web_close('WEB_WER');
   exec('web_global_params_set','pkw','PPK_POR_','PPK_UMO',PPK_UMO.ref());
   POLA_GRP.TXT_1:=PPK_UMO.PPK_UZA().ADRES().NAZWA;
   exec('ppk_ucz_first','ppk_por',PPK_UMO.ref(),P.OSOBA);

   {? web_top_kind(1)='g'
::    Zakładamy, że odświeżana jest wizytówka w oknie grupowym.
   || web_top_update(1)
   ?}
?}


\p_web_ppk_pkom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Akcja przycisku "Komunikaty"
::----------------------------------------------------------------------------------------------------------------------
exec('pkw_run','pkw','PPK_POR_PKOM','PPK_POR_PKOM')


\p_web_ppk_ppwp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Akcja przycisku "Procentowe wpłaty"
::----------------------------------------------------------------------------------------------------------------------
exec('pkw_run','pkw','PPK_POR_PPWP','PPK_POR_PPWP')


\p_web_ppk_pzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Akcja przycisku "Procentowe wpłaty"
::----------------------------------------------------------------------------------------------------------------------
exec('pkw_run','pkw','PPK_POR_PZAL','PPK_POR_PZAL')

:: ------ Formuły okien --------

\ppk_kom_web_gr_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Przed obsługą dla okna WEB tabeli PPK_KOM w oknie grupowym WEB_GR
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','ppk_por');
_ref:=exec('web_global_params_get','pkw','PPK_POR_','PPK_UMO',null());
_firma:=exec('ref_firma','ustawienia');
PPK_KOM.index('OS_DATA');
PPK_UMO.cntx_psh();
PPK_UMO.index('F_OD');
PPK_UMO.prefix(_firma);
{? type_of(_ref) & PPK_UMO.seek(_ref)
|| USERS.cntx_psh();
   _osoba:=OPERATOR.USER().OSOBA;
   USERS.cntx_pop();
   PPK_KOM.prefix(_firma,_osoba,_ref)
|| PPK_KOM.prefix(_firma,null(),null(),date(9999,12,31))
?};
PPK_UMO.cntx_pop();
~~


\ppk_pwp_web_gr_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Przed obsługą dla okna WEB tabeli PPK_WPW w oknie grupowym WEB_GR
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','ppk_por');
_ref:=exec('web_global_params_get','pkw','PPK_POR_','PPK_UMO',null());
_firma:=exec('ref_firma','ustawienia');
PPK_PWP.index('ODRN');
PPK_UMO.cntx_psh();
PPK_UMO.index('F_OD');
PPK_UMO.prefix(_firma);
{? type_of(_ref) & PPK_UMO.seek(_ref)
|| USERS.cntx_psh();
   _osoba:=OPERATOR.USER().OSOBA;
   USERS.cntx_pop();
   PPK_PWP.prefix();
   PPK_PWP.f_set(,'left join P using (PPK_PWP.P,P.REFERENCE) left join ZC using (PPK_PWP.ZC,ZC.REFERENCE)',
      '(PPK_PWP.FIRMA=:_a and PPK_PWP.OSOBA=:_b and PPK_PWP.OD>=to_date(:_c) and (PPK_PWP.OD<=to_date(:_d) '
      'or to_date(:_d) is null)) or'
      '(PPK_PWP.FIRMA=:_a and P.OSOBA=:_b and PPK_PWP.OD>=to_date(:_c) and (PPK_PWP.OD<=to_date(:_d) '
      'or to_date(:_d) is null)) or'
      '(PPK_PWP.FIRMA=:_a and ZC.OSOBA=:_b and PPK_PWP.OD>=to_date(:_c) and (PPK_PWP.OD<=to_date(:_d) '
      'or to_date(:_d) is null))'
      ,_firma,_osoba,PPK_UMO.OD,PPK_UMO.DO
   )
|| PPK_PWP.prefix(_firma,$_osoba,date(9999,12,31))
?};
PPK_UMO.cntx_pop();
~~


\ppk_pwp_web_br
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Rekord przed dla okna WEB tabeli PPK_PWP
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::----------------------------------------------------------------------------------------------------------------------
{? KST_PPK.PWP_UKW<>'T'
|| POLA_GRP.REAL_1:=
      {? PPK_PWP.BLOKADA='T'
::       wpłaty zablokowane
      || 0
      |? PPK_PWP.ZMIANA='T'
::       stawka indywidualna
      || PPK_PWP.STAWKA
      || exec('x_st','ppk',PPK_PWP.R().RN,PPK_PWP.OD)
      ?}
?};
POLA_GRP.TXT_1:=
   {? PPK_PWP.P
::    procent przypisany pracownikowi
   || 'Nr teczki: %1'@[form(PPK_PWP.P().T)]
   |? PPK_PWP.ZC
::    procent przypisany do umowy zlecenia
   || 'Nr umowy: %1'@[PPK_PWP.ZC().NU]
   || 'Domyślny procent wpłaty'@
   ?};
0


\ppk_zal_web_gr_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Przed obsługą dla okna WEB tabeli PPK_ZAL w oknie grupowym WEB_GR
::----------------------------------------------------------------------------------------------------------------------
exec('set_env','ppk_por');
_ref:=exec('web_global_params_get','pkw','PPK_POR_','PPK_UMO',null());
_firma:=exec('ref_firma','ustawienia');
PPK_ZAL.index('PPK_ZAL');
PPK_ZAL.f_clear();
PPK_UMO.cntx_psh();
PPK_UMO.index('F_OD');
PPK_UMO.prefix(_firma);
{? type_of(_ref) & PPK_UMO.seek(_ref)
|| USERS.cntx_psh();
   _osoba:=OPERATOR.USER().OSOBA;
   USERS.cntx_pop();

:: UID umowy o prowadzenie
   _uid_up:=PPK_UMO.UID;
:: UID umowy o zarządzenie
   PPK_UZA.cntx_psh();
   _uid_uz:=PPK_UMO.PPK_UZA().UID;
   PPK_UZA.cntx_pop();
:: UID uczestnika
   _uid_uc:=0;
   _ref_uc:=null();
   PPK_UCZ.cntx_psh();
   {? exec('ppk_ucz_first','ppk_por',_ref,_osoba)
   || _uid_uc:=PPK_UCZ.UID;
      _ref_uc:=PPK_UCZ.ref()
::    jeżeli osoba nie jest uczestnikiem to nie pokazujemy załączników do umowy o prowadzenie
   || _uid_up:=0
   ?};
   PPK_UCZ.cntx_pop();
:: UID-y wniosków i deklaracji
   _uid_wn:=0;
   _tabWN:=sql(''+"select PPK_WNU.UID from PPK_WNU where PPK_WNU.PPK_UCZ=:_a",_ref_uc);

   PPK_ZAL.prefix();
   PPK_ZAL.f_set(,'join PPK_ZRD',
      "(PPK_ZRD.PORTAL='T' ) and "+
      "(PPK_ZAL.UID=:_a or PPK_ZAL.UID=:_b or PPK_ZAL.UID=:_c or "+
      " PPK_ZAL.UID in (select :_d.UID from :_d))"
      ,_uid_uc,_uid_up,_uid_uz,_tabWN
   )
?};
{? PPK_ZAL.f_active()
|| PPK_ZAL.f_first()
|| PPK_ZAL.prefix(0,date(9999,12,31))
?};
PPK_UMO.cntx_pop();
~~


\ppk_zal_web_br
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Rekord przed dla okna WEB tabeli PPK_ZAL
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::----------------------------------------------------------------------------------------------------------------------
:: ustalenie źródla pochodzenia załącznika
POLA_GRP.TXT_1:=
   {? PPK_ZAL.TABELA='PPK_UCZ'
   || 'Uczestnik'@
   |? PPK_ZAL.TABELA='PPK_WNU'
   || 'Deklaracje i wnioski'@
   |? PPK_ZAL.TABELA='PPK_UMO'
   || 'Umowa o prowadzenie'@
   |? PPK_ZAL.TABELA='PPK_UZA'
   || 'Umowa o zarządzanie'@
   || ''
   ?};
:: wyróżniamy rekordy będące powiązane z umową o zarządzanie lub prowadzenie
POLA_GRP.TXT_1<>'Uczestnik' & POLA_GRP.TXT_1<>'Deklaracje i wnioski'


\ppk_zal_pobierz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [19.22]
:: OPIS: Pobiera załącznik rekordu PPK_ZAL
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('exist','#record') || return(~~) ?};

PPK_ZAL.web_bl_download('PLIK');
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:22 5f6d4fb434c56f0f5a06de76ebee769bc9783e96f459f14f1ff0955e2f4a73345bed265d40e7ad7f3610d6b6afc529ae22c93bee1ff19fee7845b23926e5c173715c5f68dd2d47a99f1016742a5982f265991ce0250e3b7dba7406a197c51f43cd094a1db74084a84685893dba8bb79eac56628273dff871884625c9c417d215

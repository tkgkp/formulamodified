:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tech_prod.fml
:: Utworzony: 23.02.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa produktów kart technologicznych - tabela TKTLW
::            Plik biblioteczny - wspólna obsługa dla TTE_TEC
::======================================================================================================================


\tktlw_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50] PR/WRT/PRODUKCJ/7.53/0002
:: OPIS: Dodaje do TKTLW produkt z nagłówka karty
::       Dodaje produkt z nagłówka tylko jak jest to karta nieprototypowa oraz w produktach karty brak rekordów
::       KONTEKST: VAR.A_KTL
::   WE: [_a] - INTEGER - [0]/1 - czy ustawiać produkt jako domyślny
::   WY: 0 - porażka
::       1 - sukces
::  OLD: \tktlw_add/tech.fml
::----------------------------------------------------------------------------------------------------------------------
_default:=0;
{? var_pres('_a')=type_of(0)
|| _default:=_a
?};

_result:=0;

TKTLW.index('TP');
TKTLW.prefix(VAR.A_KTL);
{? ~TKTLW.first()
|| TKTLW.blank();
   TKTLW.TKTL:=VAR.A_KTL;
   TKTLW.KTM:=VAR.A_KTL().KTM;
   {? TKTLW.KTM<>null()
   || _result:=TKTLW.add();
      {? _result>0
      || exec('tktlw_addpar','tech_param')
      ?};
      {? _result>0 & _default>0
      || exec('tktlw_default','tech_prod',0)
      ?}
   ?}
|| {? TKTLW.KTM=null()
   || TKTLW.KTM:=VAR.A_KTL().KTM;
      TKTLW.put()
   ?};
   TKTLW.index('REF');
   TKTLW.prefix(VAR.A_KTL,VAR.A_KTL().KTM);
   {? ~TKTLW.first()
   || TKTLW.blank();
      TKTLW.TKTL:=VAR.A_KTL;
      TKTLW.KTM:=VAR.A_KTL().KTM;
      {? TKTLW.KTM<>null()
      || _result:=TKTLW.add();
         {? _result>0
         || exec('tktlw_addpar','tech_param')
         ?};
         {? _result>0 & _default>0
         || exec('tktlw_default','tech_prod',0)
         ?}
      ?}
   ?}
?};
_result


\tktlw_modyf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: Modyfikuje w TKTLW produkt z nagłówka karty
::       Modyfikuje produkt z nagłówka tylko jak jest to karta nieprototypowa oraz w produktach karty jest jeden rekord
::       KONTEKST: VAR.A_KTL
::  OLD: \tktlw_modyf/tech.fml
::----------------------------------------------------------------------------------------------------------------------
TKTLW.index('TP');
TKTLW.prefix(VAR.A_KTL);
{? TKTLW.first()
|| {? ~TKTLW.next()
   || TKTLW.KTM:=VAR.A_KTL().KTM;
      {? TKTLW.put()
      || TWPAR.index('TPR');
         TWPAR.prefix(TKTLW.ref());
         {? TWPAR.first()
         || {!
            |? TWPAR.KTM:=VAR.A_KTL().KTM;
               TWPAR.put;
               TWPAR.next
            !}
         ?}
      ?}
   ?}
?};
~~


\tktlw_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Usuwa TKTLW do danego TKTL tylko jak jest jeden produkt karty
::   WE: _a - TKTL.ref()
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;

TKTLW.cntx_psh(); TWPAR.cntx_psh();
TKTLW.index('REF');
TKTLW.prefix(_tktl);
{? TKTLW.size()=1
|| {? TKTLW.first()
   || TWPAR.index('TPR');
      TWPAR.prefix(TKTLW.ref());
      {? TWPAR.first()
      || {! |? TWPAR.del() !}
      ?};
      TKTLW.del()
   ?}
?};
TKTLW.cntx_pop(); TWPAR.cntx_pop();
~~


\TKTLW_kasuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: kasuje produkty karty z ich parametrami
::  OLD: \TKTLW/tkasuj.fml
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;
TKTLW.index('REF');
TKTLW.prefix(_tktl);
{? TKTLW.first()
|| {!
   |? TWPAR.index('TPR');
      TWPAR.prefix(TKTLW.ref());
      {? TWPAR.first()
      || {! |? TWPAR.del() !}
      ?};
      TKTLW.del()
   !}
?};
1


\tktlw_display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: wyświetl w oknie TKTLW.WER_P
::  OLD: \tktlw_display/tech.fml
::----------------------------------------------------------------------------------------------------------------------
TKTLW.TKTL();
VAR.A_TORW:=TKTL.TORW;
exec('win_edit','tech_head','WYŚWIETL');
TKTL.display();
~~


\tktlw_rkpry
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: rekord przed TKTLW w oknie WER_P
::   WY: schemat kolorownia [STRING]
::  OLD: \tktlw_rkpry/tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? TKTLW.DEFAULT='T'
|| 'TKTLW#03#01'
|| ''
?}


\tktlw_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: wyświetla produkty dla danej karty
::   WE: _a - TKTL.ref()
::       [_b] - karta używana na wyższym poziomie wywołania
::       [_c] - INTEGER - 0/[1] - czy akcja 'Domyślny' ma być dostępna
::  OLD: \tktlw_sel/tech.fml
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;
_used:={? var_pres('_b')=type_of(0) || _b || 0 ?};

_def_act:=1;
{? var_pres('_c')=type_of(0)
|| _def_act:=_c
?};

TKTL.cntx_psh(); TKTLW.cntx_psh();
TKTL.prefix();
{? TKTL.seek(_tktl)
||
   {? _used>0
   || _locked:=0
   || _locked:=exec('tktl_lock','tech_common',,'P')
   ?};
   _used:=_used | ~_locked;
   params_set('used',_used);

   VAR.A_KTL:=TKTL.ref();
   exec('menu_start','tech_head');

:: Takie czary, żeby zadziałały tłumaczenia poniżej (teksty MUSZĄ być identyczne)
   'Należy wskazać podstawowy produkt karty.'@;
   _close:="
      _close:=0;
      TKTLW.cntx_psh();
      {? TKTLW.size()=0
      || _close:=1
      |? TKTLW.size()=1
      || {? VAR.A_KTL().KTM<>TKTLW.KTM || exec('tktlw_podst','tech_prod',0) ?};
         _close:=1
      || _ustawione:=0;
         {? TKTLW.first()
         || {!
            |? {? VAR.A_KTL().KTM=TKTLW.KTM || _ustawione:=1 ?};
               TKTLW.next()
            !}
         ?};
         {? _ustawione || _close:=1 || FUN.info('Należy wskazać podstawowy produkt karty.'@) ?}
      ?};
      TKTLW.cntx_pop();
      _close
   ";
   {? _used
   || _title:='Produkty karty: %1 wersja: %2'@[VAR.A_KTL().NRK,TKTL.WER]+' — '+'PODGLĄD'@
   || _title:='Produkty karty: %1 wersja: %2'@[VAR.A_KTL().NRK,TKTL.WER]
   ?};
   _grp:=TKTLW.grp_make(_title,,'#tktlw_sel',50,10,_close,,'normal');
   TKTLW.grp_sel(_grp,,'WER_T',,,,,15,,,,,'maximized');
   TKTLW.win_sel(_grp);
   TKTLW.win_edit();

   {? TKTL.STAT_N='N' | _used || _keys:='dpUO:d'; _default:='M' || _keys:=':'; _default:='d' ?};
   {? TKTL.STAT_P='T'
   || _keyO:=0;
      TKTLW.cntx_psh();
      {? TKTLW.first()
      || {!
         |? {? VAR.A_KTL().KTM=TKTLW.KTM || _keyO:=1 ?};
            TKTLW.next()
         !}
      ?};
      TKTLW.cntx_pop();
      _keys:='pU'+{? _keyO || 'O' || '' ?}+_keys
   ?};
   {? TKTL.STAN<>'T' || _keys:='M'+_keys ?};
   {? TKTL.ARCH<>'N' || _keys:='M'+_keys ?};
   {? VAR.FILTER='T'
   || {? FILTER.KTM<>null() || _keys:='O'+_keys ?};
      {? FILTER.DEFAULT<>'' || _keys:='M'+_keys ?}
   ?};
   {? VAR.ACTION='A' | _def_act=0 || _keys:='M'+_keys ?};

   {? TKTL.TYP().PAR='N'
   || TKTLW.actions('WER_T','WA'+_keys,_default+':d')
   || TKTLW.actions('WER_T','W'+_keys,'A:d')
   ?};
   {? exec('enabled','zl_uslugi')
   || exec('mat_grp_f3','zl_uslugi',1,'W')
   || exec('slo_m_ok','material','T',1,,'W');
      exec('filter','material','WP')
   ?};

   TKTLW.index('TP');
   TKTLW.prefix(TKTL.ref());
   TKTLW.select();
   _size:=TKTLW.size();
   _count:=0;
   {? TKTLW.first()
   || {!
      |?
         {? TKTLW.DEFAULT='T' || _count+=1 ?};
         TKTLW.next()
      !}
   ?};
   {? _count=0
   || _default:='N'
   |? _size=_count
   || _default:='T'
   || _default:='P'
   ?};
   VAR.A_KTL();

:: Oznaczenie karty jako 'Ulepszenie'
   {? ~_used
   || {? exec('is_improvement','tech_head',TKTL.ref())
      || TKTL.IMPROVE:='T'
      || TKTL.IMPROVE:='N'
      ?}
   ?};
   TKTL.DEFAULT:=_default;
   TKTL.put();

   exec('menu_stop','tech_head');

   {? _locked || exec('tktl_unlock','tech_common',,'P') ?}
?};
TKTL.cntx_pop(); TKTLW.cntx_pop();
''


\ae_tktlwktm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: po redakcji pola TKTLW.KTM
::  OLD: \ae_tktlwktm/tech.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? 'WP'*TKTLW.KTM().R<>0
||
:: Sprawdzenie wyłączone, obecnie można utworzyć kartę, której surowcem jest jej produkt
::   _res:=sql('
::      select TMAT.PT
::      from TMAT
::      where TMAT.NRK=:_a and TMAT.PT=:_b
::      union all
::      select TTGP.PT
::      from TMAT join TTGP
::      where TMAT.NRK=:_a and TTGP.PT=:_b
::      union all
::      select TCHMAT.PT
::      from TCHMAT
::      where TCHMAT.NRK=:_a and TCHMAT.PT=:_b
::      union all
::      select TTGP.PT
::      from TCHMAT join TTGP
::      where TCHMAT.NRK=:_a and TTGP.PT=:_b
::   ',TKTLW.TKTL,TKTLW.KTM);
::
::   {? type_of(_res)=type_of(~~) || exec('err_sql','#sql'); return(0) ?};
::
::   {? _res.first()
::   || FUN.emsg(
::         'KOD wprowadzonego produktu\n'
::         'jest taki sam jak KOD surowca lub zamiennika karty technologicznej.\n\n'
::         'Takie przypisanie nie jest możliwe do wykonania.'@
::      );
::      _result:=0
::   ?}
   ~~
|| FUN.emsg('Produktem karty technologicznej nie może być surowiec lub wyrób handlowy.'@);
   _result:=0
?};

{? _result>0
||
:: Obsługa usług
   _result:=exec('valid_tktlw','zl_uslugi')
?};
_result


\tktlw_dp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: 'Dołącz przed' TKTLW
::   WY: 1
::  OLD: \tktlw_dp/tech1.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('enabled','zl_uslugi')
|| exec('mat_grp_f3','zl_uslugi',0,'W')
|| exec('slo_m_ok','material','T',0,,'W');
   exec('filter','material','WP')
?};
1


\tktlw_pp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: przed popraw TKTLW
::  OLD: \tktlw_pp/tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAR.A_KTL().KTM=TKTLW.KTM
|| FUN.info('Nie można modyfikować podstawowego produktu karty.'@);
   0
|| {? exec('enabled','zl_uslugi')
   || exec('mat_grp_f3','zl_uslugi',0,'W')
   || exec('slo_m_ok','material','T',0,,'W');
      exec('filter','material','WP')
   ?};
   1
?}


\tktlw_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: usuwa produkt karty z parametrami
::  OLD: \tktlw_usun/tech.fml
::----------------------------------------------------------------------------------------------------------------------
_link:=TKTLW.testlink(2);
_link.prefix('KKTL');
{? _link.first()
|| FUN.emsg('Rekordu nie można usunąć — wykorzystany w kalkulacji technologii.'@);
   return()
?};
{? VAR.A_KTL().KTM=TKTLW.KTM
|| FUN.emsg('Nie można usunąć podstawowego produktu karty.'@);
   return()
?};
{? FUN.ask('Czy usunąć bieżący wiersz?'@)
|| TWPAR.index('TPR');
   TWPAR.prefix(TKTLW.ref());
   {? TWPAR.first() || {! |? TWPAR.del() !} ?};
   TKTLW.del()
?}


\tktlw_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: Zaznacza kartę produktu jako domyślną
::       Kontekst wywołania - rekord tabeli TKTLW
::   WE: [_a] - czy wyświetlać pytanie (domyślnie - 1), czy bez pytania (0 - zawsze ustawia domyślność kart)
::   WY: 0/1
::  OLD: \tktlw_default/tech.fml
::----------------------------------------------------------------------------------------------------------------------
_can_continue:=1;
_result:=0;

{? var_pres('_a')=type_of(0) || _ask:=_a || _ask:=1 ?};

{? _ask
|| {? TKTLW.DEFAULT='T'
   || {? FUN.ask(
            'Ta karta jest już domyślną dla produktu %1.\n\n'
            'Czy zmienić ustawienie?'@[TKTLW.KTM().KTM]
         )
      || TKTLW.DEFAULT:='N';
         TKTLW.put()
      ?};
      return()
   || {? ~FUN.ask('Czy ustawić tę kartę jako domyślną dla produktu %1?'@[TKTLW.KTM().KTM])
      || return()
      ?}
   ?}
?};

_ktm:=TKTLW.KTM;
TKTLW.cntx_psh();
_tab:=tab_tmp(1,'REF','INTEGER','Ref');
TKTLW.index('PT');
TKTLW.prefix(_ktm);
{? TKTLW.first()
|| {!
   |? TKTLW.DEFAULT:='N';
      _tab.prefix(TKTLW.TKTL);
      {? ~_tab.first()
      || _tab.REF:=TKTLW.TKTL;
         _tab.add()
      ?};
      _can_continue:=TKTLW.put();
      TKTLW.next() & _can_continue>0
   !}
?};
{? _can_continue>0
||
   TKTLW.cntx_pop();
   TKTLW.DEFAULT:='T';
   _can_continue:=TKTLW.put();
   TKTLW.cntx_psh()
?};

{? _can_continue>0
||
   _tab.clear();
   TKTL.cntx_psh();
   TKTL.clear();
   TKTLW.index('REF');
   {? _tab.first()
   || {!
      |? {? TKTL.seek(_tab.REF,)
         || TKTLW.prefix(TKTL.ref());
            _t:=0; _n:=0;
            {? TKTLW.first()
            || {!
               |? {? TKTLW.DEFAULT='T'
                  || _t+=1
                  || _n+=1
                  ?};
                  TKTLW.next()
               !}
            ?};
            {? _t>0 & _n=0
            || TKTL.DEFAULT:='T'
            |? _t=0 & _n>0
            || TKTL.DEFAULT:='N'
            || TKTL.DEFAULT:='P'
            ?};
            _can_continue:=TKTL.put()
         ?};
         _tab.next() & _can_continue>0
      !}
   ?};
   TKTL.cntx_pop()
?};
TKTLW.cntx_pop();
{? _can_continue>0
|| _result:=1
?};
_result


\tktlw_podst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: Zaznacza produkt jako podstawowy dla karty
::       Wymienia TKTL.KTM na ten z bieżącego TKTLW
::   WE: [_a] - czy z dialogiem (domyślnie 1), bez dialogu (0)
::  OLD: \tktlw_podst/tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _dialog:=_a || _dialog:=1 ?};

{? VAR.A_KTL().KTM=TKTLW.KTM
|| FUN.info('Ten produkt jest już podstawowym dla tej karty.'@);
   return()
?};
{? ~_dialog
      |
   FUN.ask(
      {? VAR.A_KTL().TYP().PAR='T'
      || 'Czy zaktualizować kod produktu i domyślne wartości parametrów karty?'@
      || 'Czy zaktualizować kod produktu karty?'@
      ?}
   )
|| TKTL.KTM:=TKTLW.KTM;
   TKTL.put();
   TPAR.index('NN');
   TPAR.prefix(VAR.A_KTL);
   TWPAR.index('TPR');
   {? TPAR.first()
   || {!
      |? TWPAR.prefix(TKTLW.ref(),TPAR.ref());
         {? TWPAR.first()
         || TPAR.DEFAULT1:=TWPAR.VAL;
            TPAR.put()
         ?};
         TPAR.next()
      !};
      exec('start_tpar','tech_param',TKTLW.KTM);
      exec('recalculate','tech_param',TKTLW.TKTL)
   ?};
:: Przepisanie zmian o produkcie podstawowym na surowce i operacje technologiczne
   exec('tktm_update','tech_common',TKTL.ref())
?}


\tktlw_rkprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: rekord przed TKTLW w oknie WER_K
::   WY: schemat kolorownia [STRING]
::  OLD: \tktlw_rkprz/tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? TKTLW.DEFAULT='T'
|| 'TKTLW#01#01'
|? VAR.A_KTL().KTM=TKTLW.KTM
|| 'TKTLW#01#02'
|| ''
?}


\tktlw_rkpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50]
:: OPIS: 'po rekord'
::       Sprawdza, czy dołączany materiał jest z tej samej grupy towarowej i ma tę samą jednostkę miary
::  OLD: \tktlw_rkpo/tech.fml
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.record(TKTLW,,'KTM');

{? _chk=''
|| _jm:=M.J; _jm_n:=JM.KOD;
   _grupa:=M.MGR; _grupa_n:=MGR.KOD;
   M.cntx_psh();
   TKTLW.cntx_psh();
   {? TKTLW.first()
   || {? TKTLW.KTM().MGR<>_grupa
      || FUN.emsg(
            {? -menu_txt()='popraw'
            || 'Poprawiany produkt musi należeć do grupy: %1.'@[TKTLW.KTM().MGR().KOD]
            || 'Dołączany produkt musi należeć do grupy: %1.'@[TKTLW.KTM().MGR().KOD]
            ?}
         );
         _chk:='KTM'
      |? TKTLW.KTM().J<>_jm
      || FUN.emsg(
            {? -menu_txt()='popraw'
            || 'Poprawiany produkt musi mieć jednostkę miary: %1.'@[TKTLW.KTM().J().KOD]
            || 'Dołączany produkt musi mieć jednostkę miary: %1.'@[TKTLW.KTM().J().KOD]
            ?}
         );
         _chk:='KTM'
      ?}
   ?};
   TKTLW.cntx_pop();
   M.cntx_pop()
?};
{? _chk='' || _chk:=__CHK.index(TKTLW,-menu_txt()='popraw')='' ?};
_chk


\valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Sprawdza, czy produkt przypisany jest do nagłówka karty
::   WE: _a - TKTL.ref()
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;

_res:=0;

TKTL.cntx_psh();
TKTL.clear();
{? TKTL.seek(_tktl)
|| {? TKTL.TORW='T' & TKTL.KTM=null()
   || FUN.emsg('Nie uzupełniono produktu karty.'@)
   || _res:=1
   ?};

   {? TKTL.TORW='T' & _res>0
   ||
::    Sprawdzam czy są jakieś rekordy TKTLW
      TKTLW.cntx_psh();
      TKTLW.index('REF');
      TKTLW.prefix(_tktl);
      {? TKTLW.size()=0
      || FUN.emsg('Nie uzupełniono produktów karty.'@);
         _res:=0
      ?};
      TKTLW.cntx_pop();
      ~~
   ?}
?};
TKTL.cntx_pop();

_res


\dflt_ktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Zwraca złączenie do domyślnej technologii dla danego produktu, wpp null()
::   WE: _a - M.ref()
::       [_b] - czy zwracać jako domyślną kartę niedomyślną, jak jest tylko jedna
::       [_c] - czy brać pod uwagę nieaktualne karty: domyślnie 0-nie, 1-tak
::   WY: TKTL.ref()
::  OLD: \dflt_ktl/tex_tmat.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')=type_of(0) || _force:=_b || _force:=0 ?};
{? var_pres('_c')=type_of(0) || _nieakt:=_c || _nieakt:=0 ?};

_ktl:=null();
TKTLW.cntx_psh();

:: Domyślnej technologii szukam zawsze w niearchiwalnej masce
_mask:=TKTLW.name()-3+'___';
{? TKTLW.name()<>_mask
|| TKTLW.use(_mask)
?};

TKTLW.index('KSN');
TKTLW.prefix('T',_a,'T','N');
_size:=0;
_force_ktl:=null();
{? TKTLW.first()
||
:: Podczytanie maski TKTL
   TKTL.cntx_psh();
   _mask:=TKTL.name()-3+'___';
   {? TKTL.name()<>_mask
   || TKTL.use(_mask)
   ?};
   TKTL.prefix();
   {!
   |?
::    Podczytanie TKTL
      TKTLW.TKTL();
      {? _nieakt=1 | ((TKTL.TERM_OD<=date() & TKTL.TERM_OD<>date(0,0,0)) | TKTL.TERM_OD=date(0,0,0))
         & ((TKTL.TERM_DO>=date() & TKTL.TERM_DO<>date(0,0,0)) | TKTL.TERM_DO=date(0,0,0))
      || _size+=1;
         {? _size=1 || _force_ktl:=TKTLW.TKTL ?};
         {? TKTLW.DEFAULT='T' || _ktl:=TKTLW.TKTL ?}
      ?};
      TKTLW.next()
   !};
   TKTL.cntx_pop()
?};
{? _ktl=null() & _force & _size=1 || {? _force_ktl<>null() || _ktl:=_force_ktl ?} ?};

TKTLW.cntx_pop();
_ktl


\first_akc_ktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Zwraca złączenie do pierwszej zatwierdzonej technologii dla danego produktu, wpp null()
::   WE: _a - M.ref()
::   WY: TKTL.ref()
::  OLD: \first_akc_ktl/tex_tma1.fml
::----------------------------------------------------------------------------------------------------------------------
_ktl:=null();
TKTLW.cntx_psh();
TKTLW.index('KSN');
TKTLW.prefix('T',_a,'T','N');
{? TKTLW.first() || _ktl:=TKTLW.TKTL ?};
TKTLW.cntx_pop();
_ktl


\first_any_ktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Zwraca złączenie do pierwszej jakiejkolwiek technologii dla danego produktu, wpp null()
::   WE: _a - M.ref()
::   WY: TKTL.ref()
::  OLD: \first_any_ktl/tex_tma1.fml
::----------------------------------------------------------------------------------------------------------------------
_ktl:=null();
TKTLW.cntx_psh();
TKTLW.index('KARC');
TKTLW.prefix('T',_a,'N');
{? TKTLW.first() || _ktl:=TKTLW.TKTL ?};
TKTLW.cntx_pop();
_ktl


\tktlwbre
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.50] PR/WRT/PRODUKCJ/7.53/0002
:: OPIS: PRZED REKORD w okienkach TKTLW.WER_K
::   WY: Schemat kolorów
::  OLD: \tktlwbre/tech.fml
::----------------------------------------------------------------------------------------------------------------------
KKTL.cntx_psh(); KPOZK.cntx_psh();
VAR.TKW:=0; VAR.DATE:=date(0,0,0);
VAR.A_KTL:=TKTLW.TKTL; TKTLW.TKTL();
KKTL.index('KZ');
KKTL.prefix(TKTLW.TKTL,'T',TKTLW.KTM);
:: Jest zatwierdzona aktywnie kalkulacja tego produktu';
{? KKTL.first()
|| KPOZK.index('KR');
   KPOZK.prefix(KKTL.ref(),TKTLW.TKTL().TYP().CRUB);
   {? KPOZK.first() || VAR.TKW:=KPOZK.WART; VAR.DATE:=KKTL.DT ?}
?};
KKTL.cntx_pop(); KPOZK.cntx_pop();
TKTLW.win_edit('RED_K');
: kolorowanie
{? ~exec('tktl_act','tech_head',5)
|| 'TKTLW#02#02'
|? TKTLW.DEFAULT='T'
|| 'TKTLW#02#01'
|| ''
?}


\action_products_button
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Podgląd produktów karty technologicznej - obsługa przycisku w oknie redagowania TKTL w czynnościach
::   WE: [_a] - INTEGER - 0/[1] - czy akcja 'Domyślny' ma być dostępna
::----------------------------------------------------------------------------------------------------------------------
_def_act:=1;
{? var_pres('_a')=type_of(0)
|| _def_act:=_a
?};
exec('tktlw_sel','tech_prod',TKTL.ref(),1,_def_act);
''

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 11ec9c4860f0b42384b588a11580683b748ddb21328e1a0fe07bb0aeeee116b5a7a092c8e07211be64ce2f1c005354e8ed06132493814b31717afb97f4c133a03f450f92dffb8c35b89a812273c2a1bf1d2f2f8e2444a856def23fff7fb20bdcff9a9910decc02f5bafd2e31d7d26ad1011e92e2f90663e0d02af92a866e880b

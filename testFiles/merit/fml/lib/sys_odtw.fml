:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: sys_odtw.fml
:: Utworzony: 31.07.2002
:: Autor: AMK
:: Systemy: FIKS
::======================================================================================================================
:: Zawartosc:  Procedury do odtwarzania kartotek po awarii systemu
::======================================================================================================================


\sys_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Glowna formula do odtwarzania systemu po sprawdzeniu
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Operacja nieodwracalna. Kontynuować?'@)
|| exec('cntx','sys_odtw',0);
   _ar:=SSTALE.AR; _ao:=SSTALE.AO;
   SYS_UTIL.cntx_psh(); SYS_UTIL.index(ind_tym2); SYS_UTIL.prefix('A');
    {? SYS_UTIL.first()
    || {! |?
          {? (SSTALE.AR().KOD<>SYS_UTIL.L1 | SSTALE.AO().NR<>SYS_UTIL.L6) & SYS_UTIL.TP_USTER<>100
          || ROK_F.find_key(SYS_UTIL.L1); SSTALE.AR:=ROK_F.ref;
             OKRO_F.find_key(ROK_F.ref,SYS_UTIL.L6); SSTALE.AO:=OKRO_F.ref;
             exec('open_tabele','open_tab');
             DOK.index('REJ'); DOK.prefix();  POZ.index('DOK'); POZ.prefix();
             AN.index('WALSYM'); AN.prefix(); OBR.index('OKR'); OBR.prefix();
             DVAT.index('NR'); DVAT.prefix(); PVAT.index('NR'); PVAT.prefix();
             VPOZ.index('VDOK'); VPOZ.prefix(); STANY.index('STAN'); STANY.prefix();
             OP.index('KON_O'); OP.prefix(); ZAP_OP.index('OP'); ZAP_OP.prefix();
             RMK.index('NAZ'); RMK.prefix();
             echo('Trwa odtwarzanie w okresie %1 %2'@[OKRO_F.NAZ,ROK_F.NAZ])
          ?};
:-----------odtwarzanie pola komentarz w pozycji dokumentu
          {? SYS_UTIL.TP_USTER=3 || exec('kom_odtw','sys_odtw') ?};
:-----------odtwarzanie pola POZ.SUM
          {? SYS_UTIL.TP_USTER=12 || exec('sum_odtw','sys_odtw',0) ?};
:-----------odtwarzanie pola POZ.SUMW
          {? SYS_UTIL.TP_USTER=13 || exec('sum_odtw','sys_odtw',1) ?};
:-----------odtwarzanie pol POZ.A, POZ.ZK, POZ.ZP
          {? SYS_UTIL.TP_USTER=24 || exec('zn_odtw','sys_odtw',1) ?};
:-----------odtwarzanie pola POZ.TID
          {? SYS_UTIL.TP_USTER=28 || exec('poz_tid','sys_odtw') ?};
:-----------odtwarzanie pol DOK.OKRVAT, DOK.A_VAT
          {? SYS_UTIL.TP_USTER=29 || exec('vpoz_dok','sys_odtw',1) ?};
          {? SYS_UTIL.TP_USTER=30 || exec('vpoz_dok','sys_odtw',0) ?};
:-----------usuwanie niepotrzebnych (bez obrotow) kont analitycznych
          {? SYS_UTIL.TP_USTER=31 || exec('ob_odtw','sys_odtw') ?};
:-----------odtwarzanie obrotow na kontach analitycznych
          {? SYS_UTIL.TP_USTER=32 |  SYS_UTIL.TP_USTER=33 || exec('ob1_odtw','sys_odtw') ?};
:-----------odtwarzanie VAT do rozliczenia
          {? SYS_UTIL.TP_USTER=36 || exec('vdr_odtw','sys_odtw') ?};
:-----------odtwarzanie braku  DVAT (+PVAT) powiazanych z DOK
          {? SYS_UTIL.TP_USTER=37 || exec('dv_odtw','sys_odtw') ?};
:-----------odtwarzanie dok. VAT gdy liczba VPOZ<>liczbie PVAT
:-----------odtwarzanie gdy jakies pole DVAT <> odpowiedniemu polu z DOK i VPOZ<>PVAT
:-----------zla numeracja w PVAT
          {? SYS_UTIL.TP_USTER=38 | SYS_UTIL.TP_USTER=39 | SYS_UTIL.TP_USTER=52 || exec('dv_odtw','sys_odtw') ?};
:-----------odtwarzanie braku rozrachunku
:-----------odtwarzanie braku zapisu na rozrachunku
          {? SYS_UTIL.TP_USTER=40 | SYS_UTIL.TP_USTER=41 || exec('roz_odtw','sys_odtw') ?};
:-----------odtwarzanie za duzej liczby zapisow na rozrachunku
          {? SYS_UTIL.TP_USTER=42 || exec('rus_odtw','sys_odtw') ?};
:-----------odtwarzanie pol OP.DO i OP.TZ
          {? SYS_UTIL.TP_USTER=43 || exec('rdt_odtw','sys_odtw') ?};
:-----------odtwarzanie pola POZ.TID
          {? SYS_UTIL.TP_USTER=45 || exec('roz_tid','sys_odtw') ?};
:-----------odtwarzanie braku konta analitycznego
          {? SYS_UTIL.TP_USTER=50 || exec('kon_odtw','sys_odtw',1) ?};
:-----------odtwarzanie ciaglosci dokumentow VAT (DVAT)
          {? SYS_UTIL.TP_USTER=51 || exec('dvc_odtw','sys_odtw') ?};
:-----------usuniecie dokumentow VAT dla ktorych DVAT.DOK=null
          {? SYS_UTIL.TP_USTER=53 || exec('dvu_odtw','sys_odtw') ?};
:-----------odtwarzanie braku komentarza systemowego konta syntetycznego
          {? SYS_UTIL.TP_USTER=60 || exec('ksk_odtw','sys_odtw') ?};
:-----------odtwarzanie pol OP.WN i OP.MA
          {? SYS_UTIL.TP_USTER=65 || exec('op_odtw','sys_odtw',0) ?};
:-----------odtwarzanie pola OP.STAN
          {? SYS_UTIL.TP_USTER=66 || exec('op_odtw','sys_odtw',1) ?};
:-----------odtwarzanie tabeli STANY
          {? SYS_UTIL.TP_USTER=67 || exec('op_odtw','sys_odtw',2) ?};
:-----------usuwanie ZAP_OP bez POZ
          {? SYS_UTIL.TP_USTER=68 || exec('op_odtw','sys_odtw',3) ?};
:-----------odtwarzanie pol OP.POCH i OP.KH
          {? SYS_UTIL.TP_USTER=72 || exec('op_odtw','sys_odtw',4) ?};
:-----------usuwanie zbednych rekordow w tabeli RMK, RRMK
          {? SYS_UTIL.TP_USTER=69 | SYS_UTIL.TP_USTER=71 || exec('rmk_odtw','sys_odtw') ?};
:-----------odtwarzanie pol ZAP_OP.WN2 i ZAP_OP.MA2
          {? SYS_UTIL.TP_USTER=70 || exec('zap_odtw','sys_odtw') ?};
:-----------odtwarzanie pola ZAP_OP.REJ
          {? SYS_UTIL.TP_USTER=44 || exec('zapop_od','sys_odtw') ?};
:-----------odtwarzanie korespondencji seryjnej
          {? SYS_UTIL.TP_USTER=101 || exec('kor_odtw','sys_odtw') ?};
:-----------odtwarzanie budowy kont analitycznych
          {? SYS_UTIL.TP_USTER=102 || exec('anb_odtw','sys_odtw') ?};
          {? SYS_UTIL.TP_USTER=105 || exec('tmp_del','sys_odtw') ?};
          {? SYS_UTIL.TP_USTER=106 || exec('sp_odtw','sys_odtw') ?};
          SYS_UTIL.next()
       !}
    ?};
    SYS_UTIL.cntx_pop();
    {? SSTALE.AR<>_ar | SSTALE.AO<>_ao || SSTALE.AR:=_ar; SSTALE.AO:=_ao; exec('open_tabele','open_tab') ?};
    exec('cntx','sys_odtw',1);
    FUN.info('Operacja zakończona.'@)
|| {? SYS_UTIL.select || exec('sys_odtw','sys_odtw') ?}
?};
echo();
1


\cntx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Zapamietanie buforow, ustawienie indeksow, prefiksow i odlozenie
::   WE: _a=0 - zapamietanie buforow _a=1 - odlozenie
::----------------------------------------------------------------------------------------------------------------------
{? ~_a
|| ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix(REF.FIRMA);
   OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
   REJ.cntx_psh(); REJ.index('KOD'); REJ.prefix();
   DOK.cntx_psh();  DOK.index('REJ'); DOK.prefix();
   POZ.cntx_psh(); POZ.index('DOK'); POZ.prefix();
   SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
   SLO.cntx_psh(); SLO.index('SL'); SLO.prefix();
   AN.cntx_psh(); AN.index('WALSYM'); AN.prefix();
   OBR.cntx_psh(); OBR.index('OKR'); OBR.prefix();
   ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   ROZLVAT.cntx_psh(); ROZLVAT.index('DOK'); ROZLVAT.prefix();
   DVAT.cntx_psh(); DVAT.index('NR'); DVAT.prefix();
   PVAT.cntx_psh();  PVAT.index('NR'); PVAT.prefix();
   VPOZ.cntx_psh(); VPOZ.index('VDOK'); VPOZ.prefix();
   KS.cntx_psh(); KS.index('SYM'); KS.prefix();
   RVAT.cntx_psh(); RVAT.index('SYM'); RVAT.prefix(REF.FIRMA);
   KOM.cntx_psh(); KOM.index('KS'); KOM.prefix();
   OP.cntx_psh(); OP.index('KON_O'); OP.prefix();
   ZAP_OP.cntx_psh(); ZAP_OP.index('OP'); ZAP_OP.prefix();
   STANY.cntx_psh(); STANY.index('STAN'); STANY.prefix();
   RMK.cntx_psh(); RMK.index('NAZ'); RMK.prefix()
|| ROK_F.cntx_pop(); OKRO_F.cntx_pop(); REJ.cntx_pop(); DOK.cntx_pop(); POZ.cntx_pop();
   SLU.cntx_pop(); SLO.cntx_pop(); AN.cntx_pop(); OBR.cntx_pop(); ODD.cntx_pop(); ROZLVAT.cntx_pop();
   DVAT.cntx_pop(); PVAT.cntx_pop(); VPOZ.cntx_pop(); KS.cntx_pop(); RVAT.cntx_pop(); KOM.cntx_pop();
   OP.cntx_pop(); ZAP_OP.cntx_pop(); STANY.cntx_pop(); RMK.cntx_pop()
?}


\kom_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie pola komentarz w pozycji dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw') & POZ.find_key(DOK.ref,SYS_UTIL.L8)
|| KS.cntx_psh(); POZ.KOM:=exec('koment','konto',POZ.KON); POZ.put(); KS.cntx_pop()
?}


\sum_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie pol POZ.SUM, POZ.SUMW - zaokraglenia
::   WE: _a=0 - POZ.SUM, _a=1 - POZ.SUMW
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw') & POZ.find_key(DOK.ref,SYS_UTIL.L8)
|| {? ~_a || POZ.SUM:=SYS_UTIL.L9 || POZ.SUMW:=SYS_UTIL.L9 ?};
   POZ.put()
?}


\ob_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Usuwanie niepotrzebnych (bez obrotow) kont analitycznych
::----------------------------------------------------------------------------------------------------------------------
OBR.cntx_psh(); OBR.index('AN');
{? SLU.find_key(SYS_UTIL.L2) & SLO.find_key(SLU.ref,SYS_UTIL.L3) &
   AN.find_key(SLO.ref,SYS_UTIL.L4)
|| OBR.prefix(AN.ref); {? OBR.first() || {!|? OBR.del() !} ?};
   exec('delanslu','!fks_ksr_zbka',AN.ref()); AN.del()
?};
OBR.cntx_pop()


\ob1_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie obrotow na kontach analitycznych
::----------------------------------------------------------------------------------------------------------------------
{? SLU.find_key(SYS_UTIL.L2) & SLO.find_key(SLU.ref,SYS_UTIL.L3) &
   AN.find_key(SLO.ref,SYS_UTIL.L4) & ODD.find_key(SYS_UTIL.L5,SYS_UTIL.L5)
|| {? OBR.find_key(SSTALE.AO().NR,AN.ref,ODD.ref,SSTALE.AO) || OBR.del ?};
   OBR.OKRO:=SSTALE.AO; OBR.AN:=AN.ref; OBR.ODD:=ODD.ref;
   OBR.WN:=SYS_UTIL.L9; OBR.MA:=SYS_UTIL.L10;  OBR.add()
?}


\vdr_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie VAT - do rozliczenia
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw')
|| ROZLVAT.cntx_psh(); ROZLVAT.index('DOK');
   VPOZ.cntx_psh(); VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
   {? VPOZ.first()
   || {! |?
         {? VPOZ.RVAT<>null
         || ROZLVAT.prefix(SSTALE.AO,DOK.ref(),VPOZ.RVAT().RVAT,VPOZ.D);
            {? ~ROZLVAT.first()
            || ROZLVAT.blank(1); ROZLVAT.DOK:=DOK.ref(); ROZLVAT.OKRO:=SSTALE.AO;
               ROZLVAT.REJ:=DOK.REJ; ROZLVAT.SYM:=DOK.NK;
               ROZLVAT.DATA:=DOK.DTW;
               ROZLVAT.DOP:=VPOZ.D; ROZLVAT.KH:=DOK.KH;
               ROZLVAT.RVAT:=VPOZ.RVAT().RVAT;
               ROZLVAT.ODD:=DOK.ODD;
               ROZLVAT.SP_WYM:=DOK.SP_WYM;
               ROZLVAT.add()
            ?}
         ?};
         VPOZ.next()
      !}
   ?};
   VPOZ.cntx_pop();
   ROZLVAT.cntx_pop()
?}


\dv_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie calego dokumentu VAT
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw')
|| exec('init','jpk_v');
   ROK_F.cntx_psh(); OKRO_F.cntx_psh();
   do();
   exec('wyc_vatg','dok_fks_ks');
   exec('vat_ks','dok_fks_ks',SSTALE.AO);
   end();
   ROK_F.cntx_pop(); OKRO_F.cntx_pop();
   1
?}


\rus_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Usuwanie zapisow na rozr. dla pozycji dokumentu ksiegowego i tworzenie nowego poprawnego
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw') &
   POZ.find_key(DOK.ref,SYS_UTIL.L8) & SLU.find_key(SYS_UTIL.L3) &
   SLO.find_key(SLU.ref,SYS_UTIL.L4)
|| do();
   OP.cntx_psh(); ZAP_OP.cntx_psh(); STANY.cntx_psh(); ROK_F.cntx_psh(); OKRO_F.cntx_psh();
   DOK.cntx_psh(); POZ.cntx_psh(); SLU.cntx_psh(); SLO.cntx_psh();
   ZAP_OP.index('ZAP');
   KOMT:='';
   {! _i:=1..SYS_UTIL.L9
   |! exec('wyc_op','rozrach',SLO.ref()<>FINFO.NAROD,0)
   !};
   &KOMT;
   OP.cntx_pop(); ZAP_OP.cntx_pop(); STANY.cntx_pop(); ROK_F.cntx_pop(); OKRO_F.cntx_pop();
   DOK.cntx_pop(); POZ.cntx_pop(); SLU.cntx_pop(); SLO.cntx_pop();
   end();
   exec('roz_odtw','sys_odtw')
?}


\rdt_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie pol OP.DO i OP.TZ na podstawie dokumentu ksiegowego
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw') &
   POZ.find_key(DOK.ref,SYS_UTIL.L8) & SLU.find_key(SYS_UTIL.L3) &
   SLO.find_key(SLU.ref,SYS_UTIL.L4) &
   OP.find_key(SLO.ref(),POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK) & SYS_UTIL.L13<OP.DO
|| SLU.cntx_psh(); SLO.cntx_psh();
   {? -POZ.TID<>'rmk'
   || OP.POCH:=exec('wys_op','dok_fks_ks',POZ.KON);
      OP.KH:=exec('kh_op','dok_fks_ks')
   ?};
   SLU.cntx_pop(); SLO.cntx_pop();
   OP.DO:=SYS_UTIL.L13; OP.TZ:=SYS_UTIL.L14; OP.TYP:=SYS_UTIL.L5; OP.put();
   exec('pop_all','rozrach',0)
?}


\zapop_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Odtwarzanie pol tabeli ZAP_OP - REJ, NR, DKS, NK, POZ, KURS, WN2, MA2, TYP
::       na podstawie pozycji dokumentu ksiegowego
::----------------------------------------------------------------------------------------------------------------------
ZAP_OP.cntx_psh();
{? exec('szuk_dok','sys_odtw') &
   POZ.find_key(DOK.ref,SYS_UTIL.L8) & SLU.find_key(SYS_UTIL.L3) &
   SLO.find_key(SLU.ref,SYS_UTIL.L4) & OP.find_key(SLO.ref(),POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK) &
   (ZAP_OP.index('ZAP'); ZAP_OP.prefix(POZ.ref(),SLO.ref(),SSTALE.AO,POZ.ODD); _zn_zap:=0;
    {? ZAP_OP.first() || {! |? {? ZAP_OP.PAR_POZ=null || _zn_zap:=1 ?}; ~_zn_zap & ZAP_OP.next() !} ?}; _zn_zap)
|| ZAP_OP.REJ:=DOK.REJ;  ZAP_OP.NR:=DOK.NR;
   ZAP_OP.DKS:=DOK.DKS;  ZAP_OP.NK:=DOK.NK;    ZAP_OP.POZ:=POZ.POZ;
   ZAP_OP.KURS:=POZ.KURS;
   ZAP_OP.WAL2:={? OP.WAL=FINFO.NAROD || POZ.WAL || FINFO.NAROD ?};
   ZAP_OP.TYP:={? DOK.DOK_REJ().ZAPL='T' || 'Z' || '' ?};
   ZAP_OP.put()
?};
ZAP_OP.cntx_pop()


\roz_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: dtwarzanie rozrachunku (z pozycji dokumentu ksiegowego)
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw') &
   POZ.find_key(DOK.ref,SYS_UTIL.L8) & SLU.find_key(SYS_UTIL.L3) &
   SLO.find_key(SLU.ref,SYS_UTIL.L4)
|| exec('ustaw_roz','dok_fks',1);
   do();
   {? ~OP.find_key(SLO.ref(),POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK)
   || OP.blank(1); OP.SYM:=POZ.ID; OP.SYM_ZEW:=POZ.SYM_ZEW; OP.DO:=POZ.DO; OP.SYM_ROK:=POZ.SYM_ROK;
      OP.ODD:={? POZ.ODD<>null || POZ.ODD || DOK.ODD ?};
      OP.TZ:=POZ.TP; OP.OP:={? POZ.OP='' || DOK.TR || POZ.OP ?};
      OP.TYP:=POZ.TID; OP.AN:=POZ.KON; OP.STAN:='N';
      SLU.cntx_psh(); SLO.cntx_psh();
      {? -POZ.TID<>'rmk'
      || OP.POCH:=exec('wys_op','dok_fks_ks',POZ.KON);
         OP.KH:=exec('kh_op','dok_fks_ks')
      ?};
      SLU.cntx_pop(); SLO.cntx_pop();
      OP.WAL:=SLO.ref;
      OP.CZY_MON:='T';
      OP.CZY_ODS:={? OP.TYP='NOD' | OP.TYP='ZOD' || 'N' || 'T' ?};
      {? ~OP.add() || undo() ?}
   |? POZ.DO<OP.DO
   || OP.DO:=POZ.DO; OP.TZ:=POZ.TP; OP.TYP:=POZ.TID;
      {? ~OP.KH & -POZ.TID<>'rmk'  || OP.KH:=exec('kh_op','dok_fks_ks') ?};
      {? ~OP.put || undo() || exec('pop_all','rozrach',0) ?}
   ?};
   {? var_press('DSP',OP)>0 & OP.DSP=date(0,0,0) & POZ.DOK().RVAT
   || OP.DSP:=DOK.DTO
   ?};
   ZAP_OP.blank(1); ZAP_OP.OP:=OP.ref;
   ZAP_OP.ODD:=OP.ODD; ZAP_OP.POZDOK:=POZ.ref;
   ZAP_OP.DATA:={? POZ.DATA_R<>date(0,0,0)
                || POZ.DATA_R
                || exec('dat_otw','dok_fks')
                ?};
   ZAP_OP.OKRO:=SSTALE.AO;
   ZAP_OP.WN:=ZAP_OP.WN2:=ZAP_OP.MA:=ZAP_OP.MA2:=0;
   {? OP.WAL<>FINFO.NAROD
   || {? -(~POZ.STR)='wn' || ZAP_OP.WN:=POZ.SUMW; ZAP_OP.WN2:=POZ.SUM || ZAP_OP.MA:=POZ.SUMW; ZAP_OP.MA2:=POZ.SUM ?}
   || {? -(~POZ.STR)='wn' || ZAP_OP.WN:=POZ.SUM; ZAP_OP.WN2:=POZ.SUMW || ZAP_OP.MA:=POZ.SUM; ZAP_OP.MA2:=POZ.SUMW ?}
   ?};
   ZAP_OP.REJ:=DOK.REJ;
   ZAP_OP.NR:=DOK.NR;
   ZAP_OP.DKS:=DOK.DKS;
   ZAP_OP.NK:=DOK.NK;
   ZAP_OP.POZ:=POZ.POZ;
   ZAP_OP.KURS:=POZ.KURS;
   ZAP_OP.WAL2:={? OP.WAL=FINFO.NAROD || POZ.WAL || FINFO.NAROD ?};
   {? ~ZAP_OP.add() || undo() ?};
   exec('mod_op','sys_odtw',1);
   end()
?}


\op_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie rozrachunku
::   WE: _a=0 - pola OP.WN i OP.MA
::       _a=1 - pole OP.STAN
::       _a=2 - tabela STANY
::       _a=3 - usuwanie ZAP_OP bez POZ
::       _a=4 - odtwarzanie pol OP.POCH i OP.KH
::----------------------------------------------------------------------------------------------------------------------
{? SLU.find_key(SYS_UTIL.L2) & SLO.find_key(SLU.ref,SYS_UTIL.L3) &
   ODD.find_key(SYS_UTIL.L11,SYS_UTIL.L11) &
   OP.find_key(SLO.ref(),ODD.ref(),SYS_UTIL.L4,SYS_UTIL.L5,SYS_UTIL.L5,SYS_UTIL.L16)
|| {? ~_a
   || OP.WN:=SYS_UTIL.L9; OP.MA:=SYS_UTIL.L10; OP.put()
   |? _a=1
   || OP.STAN:=SYS_UTIL.L12; OP.put()
   |? _a=2
   || exec('mod_op','sys_odtw',0)
   |? _a=3
   || STANY.cntx_psh(); OP.cntx_psh(); ZAP_OP.cntx_psh();
      OP_PROJ.cntx_psh(); OP_PROJ.use('rozpro'+(OP.name()+2)); OP_PROJ.index('OP');
      ZAP_PROJ.cntx_psh(); ZAP_PROJ.use('rozprz'+(OP.name()+2)); ZAP_PROJ.index('OP_PROJ');
      ZAP_OP.prefix(OP.ref());
      {? ZAP_OP.first
      || {! |?
            {? ZAP_OP.POZDOK=null
            || STANY.cntx_psh(); OP.cntx_psh(); ZAP_OP.cntx_psh();
               _dalej:={? ZAP_OP.PAR_POZ=null
                       || exec('wycofaj','rozrach') & OP.seek(ZAP_OP.OP)
                       || 1
                       ?};
              STANY.cntx_pop(); OP.cntx_pop(); ZAP_OP.cntx_pop();
              {? _dalej
              || {? ZAP_OP.size()=1
                 || STANY.prefix(OP.ref()); {? STANY.first || {! |? STANY.del() !} ?};
                    ZAP_OP.del();
                    OP_PROJ.prefix(OP.ref());
                    {? OP_PROJ.first
                    || {! |?
                          ZAP_PROJ.prefix(OP_PROJ.ref());
                          {? ZAP_PROJ.first() || {! |? ZAP_PROJ.del() !} ?};
                          OP_PROJ.del()
                       !}
                    ?};
                    OP.del()
                 || ZAP_OP.del();
                    exec('pop_optz','rozrach'); exec('mod_op','sys_odtw',1)
                 ?}
              ?}
            ?};
            ZAP_OP.next
         !}
      ?};
      STANY.cntx_pop(); OP.cntx_pop(); ZAP_OP.cntx_pop(); OP_PROJ.cntx_pop(); ZAP_PROJ.cntx_pop()
   |? _a=4
   || KH.cntx_psh(); KH.prefix(); SLO.cntx_psh(); SLO.prefix();
      {? (SYS_UTIL.L7=0 | (SYS_UTIL.L7<>0 & SLO.seek(SYS_UTIL.L7,SLO.name()))) &
         (SYS_UTIL.L8=0 | (SYS_UTIL.L8<>0 & KH.seek(SYS_UTIL.L8,KH.name())))
      || OP.POCH:={? SYS_UTIL.L7<>0 || SLO.ref() || null ?};
         OP.KH:={? SYS_UTIL.L8<>0 || KH.ref() || null ?};
         OP.put()
      ?};
      KH.cntx_pop(); SLO.cntx_pop()
   ?}
?}


\zap_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Odtwarzanie pol ZAP_OP.WN2 i ZAP_OP.MA2
::----------------------------------------------------------------------------------------------------------------------
{? SLU.find_key(SYS_UTIL.L2) & SLO.find_key(SLU.ref,SYS_UTIL.L3) &
   ODD.find_key(SYS_UTIL.L11,SYS_UTIL.L11) &
   OP.find_key(SLO.ref(),ODD.ref(),SYS_UTIL.L4,SYS_UTIL.L5,SYS_UTIL.L5,SYS_UTIL.L16) &
   ZAP_OP.seek(SYS_UTIL.L8,ZAP_OP.name())
|| ZAP_OP.WN2:=SYS_UTIL.L9; ZAP_OP.MA2:=SYS_UTIL.L10; ZAP_OP.put()
?}


\mod_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Usunięcie zapisu z rozrachunku, modyfikacja OP i STANY (naliczenie od nowa)
::   WE: _a=0 modyfikacja tylko STANow, _a=1 OP i STANY
::----------------------------------------------------------------------------------------------------------------------
_wn:=_ma:=0;
STANY.cntx_psh(); ZAP_OP.cntx_psh();
STANY.prefix(OP.ref); {? STANY.first || {! |? STANY.del !} ?};
ZAP_OP.prefix(OP.ref());
{? ZAP_OP.first
|| {! |?
     {? ZAP_OP.POZDOK<>null
     || STANY.prefix(OP.ref,ZAP_OP.OKRO);
       {? STANY.first()
       || STANY.WN+=ZAP_OP.WN; STANY.MA+=ZAP_OP.MA; STANY.put()
       || STANY.blank(1);
          STANY.OP:=OP.ref; STANY.OKRO:=ZAP_OP.OKRO;
          STANY.WN:=ZAP_OP.WN; STANY.MA:=ZAP_OP.MA; STANY.add()
       ?};  _wn+=ZAP_OP.WN; _ma+=ZAP_OP.MA
     ?};
     ZAP_OP.next
  !}
?};
STANY.cntx_pop(); ZAP_OP.cntx_pop();
{? _a
|| OP.WN:=_wn; OP.MA:=_ma;
   OP.STAN:={?OP.WN$2=OP.MA$2||'R'||'N'?};
   OP.put
?}


\rmk_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Usuwanie zbednych rekordow w tabeli RMK
::----------------------------------------------------------------------------------------------------------------------
{? |SYS_UTIL.L4<>''
|| _znalod:=ODD.find_key(SYS_UTIL.L4,SYS_UTIL.L4); _odd:=ODD.ref()
|| _znalod:=1; _odd:=null
?};
{? _znalod & RMK.find_key(_odd,SYS_UTIL.L2,SYS_UTIL.L3,SYS_UTIL.L3,SYS_UTIL.L16)
|| exec('kasujrrmk','rmk',RMK.ref()); RMK.del()
?}


\kon_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie braku konta analitycznego
::----------------------------------------------------------------------------------------------------------------------
{? SLU.find_key(SYS_UTIL.L3) & SLO.find_key(SLU.ref,SYS_UTIL.L2) &
   ODD.find_key(SYS_UTIL.L11,SYS_UTIL.L11)
|| do();
   {? ~AN.find_key(SLO.ref,SYS_UTIL.L4) & KS.find_key(ROK_F.ref,SYS_UTIL.L5)
   || AN.KS:=KS.ref; AN.SYM:=SYS_UTIL.L4; AN.WAL:=SLO.ref;  AN.STAN:='O';
      {? ~AN.add() || undo() ?}
   ?};
   _ok:=exec('addanslu','konto',AN.ref());
   {? ~_ok
   || FUN.emsg('Niepełne odtworzenie opisów konta analitycznego %1'@[AN.SYM])
   ?};
   {? OBR.find_key(OKRO_F.NR,AN.ref,ODD.ref,OKRO_F.ref)
   || OBR.WN+=SYS_UTIL.L9;  OBR.MA+=SYS_UTIL.L10; OBR.put
   || OBR.AN:=AN.ref; OBR.OKRO:=OKRO_F.ref; OBR.ODD:=ODD.ref;
      OBR.WN:=SYS_UTIL.L9;  OBR.MA:=SYS_UTIL.L10; OBR.add
   ?}; end()
?}


\dvc_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie ciaglosci dokumentow VAT (DVAT)
::----------------------------------------------------------------------------------------------------------------------
SLO.cntx_psh(); SLU.cntx_psh(); SLUAPPL.cntx_psh();
SLU.index('NAZ'); SLU.prefix();
SLUAPPL.index('NAZ'); SLUAPPL.prefix('F','~KRAJE UE');
{? RVAT.find_key(SYS_UTIL.L2,SYS_UTIL.L2) & SLUAPPL.first()
|| SLUAPPL.SLU(); SLO.index('SL'); SLO.prefix(SLU.ref());
   {? SLO.first()
   || {! |?
         DVAT.cntx_psh(); DVAT.prefix(SLO.ref(),OKRO_F.ref(),RVAT.ref());
         {? DVAT.first()
         || _num:=1;
            {! |?
               {? DVAT.NR=_num
               || _num+=1; DVAT.next()
               || DVAT.NR:=_num; DVAT.put();
                  _num:=1; DVAT.first()
               ?}
            !}
        ?};
        DVAT.cntx_pop();
        SLO.next()
      !}
   ?}
?};
SLO.cntx_pop(); SLU.cntx_pop(); SLUAPPL.cntx_pop()


\dvu_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Usuniecie dokumentow VAT dla ktorych DVAT.DOK=null
::----------------------------------------------------------------------------------------------------------------------
DVAT.cntx_psh(); DVAT.index('DOK'); DVAT.prefix(null); PVAT.cntx_psh();
{? DVAT.first
|| {! |?
    PVAT.prefix(DVAT.ref); {? PVAT.first || {! |?  PVAT.del !} ?};
    DVAT.del()
   !}
?};
DVAT.cntx_pop(); PVAT.cntx_pop()


\ksk_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie komentarza systemowego konta syntetycznego
::----------------------------------------------------------------------------------------------------------------------
{? KS.find_key(ROK_F.ref,SYS_UTIL.L2)
|| KOM.cntx_psh();
   KOM.prefix(KS.ref,KS.SYM+' '+ KS.NAZ);
   {? KOM.first
   || KOM.SYS:='T'; KOM.put
   || KOM.prefix(); KOM.blank(1); KOM.KS:=KS.ref(); KOM.NAZ:=KS.SYM+' '+KS.NAZ;
      KOM.SYS:='T'; KOM.add()
   ?};
   KOM.cntx_pop()
?}


\zn_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Odtwarzanie pol POZ.A, POZ.ZP, POZ.ZK
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw')
|| POZ.cntx_psh(); POZ.index('DOK'); POZ.prefix(DOK.ref);
   {? POZ.first()
   || {! |?
        POZ.A:=DOK.A; POZ.ZP:=DOK.ZP; POZ.ZK:=DOK.ZK; POZ.put(); POZ.next()
      !}
   ?};
   POZ.cntx_pop()
?}


\szuk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Szukanie dokumentu zrodlowego
::----------------------------------------------------------------------------------------------------------------------
ROK_F.find_key(SYS_UTIL.L1) & ODD.find_key(SYS_UTIL.L15) &
REJ.find_key(ROK_F.ref,ODD.ref,SYS_UTIL.L2,SYS_UTIL.L2) & DOK.find_key(REJ.ref,SYS_UTIL.L7)


\roz_tid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Odtwarzanie pola OP.TYP
::----------------------------------------------------------------------------------------------------------------------
{? SLU.find_key(SYS_UTIL.L2) & SLO.find_key(SLU.ref,SYS_UTIL.L3) &
   ODD.find_key(SYS_UTIL.L11,SYS_UTIL.L11) &
   OP.find_key(SLO.ref(),ODD.ref(),SYS_UTIL.L4,SYS_UTIL.L5,SYS_UTIL.L5,SYS_UTIL.L16)
|| OP.TYP:=SYS_UTIL.L12; OP.put();
   exec('pop_all','rozrach',0)
?}


\poz_tid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Odtwarzanie pola POZ.TID
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw') & POZ.find_key(DOK.ref,SYS_UTIL.L8)
|| POZ.TID:=SYS_UTIL.L12; POZ.put()
?}


\kor_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Odtwarzanie naglowkow korespondencji seryjnej
::----------------------------------------------------------------------------------------------------------------------
echo('Odtwarzanie korespondencji seryjnej'@);
SER_NAGS.cntx_psh(); SER_NAGS.index('SERNAGS1'); SER_NAGS.prefix(REF.FIRMA);
{? SER_NAGS.first()
|| {! |? SER_NAGS.del() !}
?};
{? ~SER_NAGS.first()
|| SER_POZ.cntx_psh();
   SER_POZ.use('ser_pxx'); SER_POZ.index('SER_POZO');
   SER_NAG.index('SER_NAG'); SER_NAG.prefix(REF.FIRMA);
   {? SER_NAG.first()
   || {! |?
         {? SER_NAG.T='I'
         || exec('dodajs','rozrach_kor',SER_NAG.ref(),SER_NAG.ODD,null,SER_NAG.JEZYK,SER_NAG.KH,SER_NAG.KOD);
            exec('dodajs','rozrach_kor',SER_NAG.ref(),null,null,SER_NAG.JEZYK,SER_NAG.KH,SER_NAG.KOD);
            SER_NAGS.ODD:=SER_NAG.ODD; SER_NAG.put()
         || exec('dodajs','rozrach_kor',SER_NAG.ref(),null,SER_NAG.WAL,null,SER_NAG.KH,SER_NAG.KOD);
            SER_POZ.prefix(REF.FIRMA,SER_NAG.ref());
            {? SER_POZ.first()
            || {! |?
                  exec('dodajs','rozrach_kor',SER_NAG.ref(),SER_POZ.ODD,SER_NAG.WAL,null,SER_NAG.KH,SER_NAG.KOD);
                  SER_POZ.next()
               !}
            ?};
            SER_NAGS.prefix(REF.FIRMA,SER_NAG.ref());
            SER_NAG.ODD:={? SER_NAGS.size>2 || null || SER_POZ.ODD ?}; SER_NAG.put()
         ?};
         SER_NAG.next()
      !}
   ?};
   SER_POZ.cntx_pop()
?};
SER_NAGS.cntx_pop();
echo()


\anb_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Odtwarzanie budowy kont analitycznych
::----------------------------------------------------------------------------------------------------------------------
AN.cntx_psh(); AN_SLU.cntx_psh();
ROK_F.cntx_psh(); OKRO_F.cntx_psh();
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.index('ROK');
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {! |?
     {? -ROK_F.ZAM='n'
     || AN.use('koan__'+ROK_F.KOD); AN.index('SYM'); AN.prefix();
        AN_SLU.use('koansl'+ROK_F.KOD); AN_SLU.index('DISP');
        {? AN_SLU.erase() & AN.first()
        || {! |?
             _ok:=exec('addanslu','konto',AN.ref());
             {? ~_ok
             || FUN.emsg('Niepełne odtworzenie opisów konta analitycznego %1'@[AN.SYM])
             ?};
             AN.next()
           !}
        ?}
     ?};
     ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next()
   !}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
AN.cntx_pop(); AN_SLU.cntx_pop()


\vpoz_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Odtworzenie zgodnosci pomiedzy polami DOK.OKRVAT, DOK.A_VAT a VPOZ.OKRVAT, VPOZ.A_VAT
::   WE: _a - 1 zgodne, 0 brak zgodnosci (DOK.OKRVAT, DOK.A_VAT maja miec wartosc pusta)
::----------------------------------------------------------------------------------------------------------------------
{? exec('szuk_dok','sys_odtw')
|| {? _a
   || VPOZ.cntx_psh();
      VPOZ.index('VDOK');VPOZ.prefix(DOK.ref());
      {? VPOZ.first
      || DOK.OKRVAT:=VPOZ.OKRVAT;
         DOK.A_VAT:=VPOZ.A_VAT;
         DOK.put
      ?};
      VPOZ.cntx_pop()
   || DOK.OKRVAT:=DOK.A_VAT:=null;
      DOK.put
   ?}
?}


\tmp_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Kasowanie tabel pomocniczych
::----------------------------------------------------------------------------------------------------------------------
exec('tmp_del1','sys_odtw',TMPDOK);
exec('tmp_del1','sys_odtw',TMPVAT)


\tmp_del1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Kasowanie tabel pomocniczych - wewnetrzna
::   WE: _a - alias do tabeli
::----------------------------------------------------------------------------------------------------------------------
_a.cntx_psh(); _tab:=_a.names();
{? _tab.first()
|| {! |?
      _a.use(_tab.NAME); _a.erase();
      _tab.next()
  !}
?};
_a.cntx_pop()


\sp_odtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Odtwarzanie znaczników SP
::----------------------------------------------------------------------------------------------------------------------
exec('cntx','sys_odtw',0);
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {! |?
      ROK_F.cntx_psh();
      {? -ROK_F.ZAM='n' || exec('update','fks_sp',1) ?};
      ROK_F.cntx_pop();
      ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next()
   !}
?};
ROK_F.cntx_pop();
exec('cntx','sys_odtw',1)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 215fe7898d5049e983e2a9d5db7f6c226320c98415f774dd732a90055255106ba1d1ca127bad93459803ec6ecaed8de823efb26c163053807d511598dcc7864b5c5735595313dbce3bd710d8d884bc116f9abe487c4b1e34a35ea2236614f40e45656b5f171abc7ba9bea00bf7dd3eaeeb9da7332d1ad76f2b0d11688b07f7a6

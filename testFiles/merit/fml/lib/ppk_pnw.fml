:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_pnw.fml [12.51]
:: Utworzony: 2020/04/12
:: Autor: jaws
:: Systemy: PPK
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę wpłat na podstawie wniosków PPK.
::======================================================================================================================


\_moda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Po dołączeniu lub poprawieniu wiersza tabeli PPK_PNW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi dla wyzwalaczy "po"
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: podstawowy warunek dalszego działania
{? ~_a | do_state()<>1 || return() ?};

_epilog:="
   PPK_PNW.cntx_pop();
   PPK_WPW.cntx_pop();
   PPK_WNU.cntx_pop();
   PPK_PWP.cntx_pop();
   ZC.cntx_pop();
   P.cntx_pop();
   ~~
";

P.cntx_psh();
P.index('PRACOSOW');
ZC.cntx_psh();
ZC.index('ZLECDAT');
PPK_PWP.cntx_psh();
PPK_PWP.index('RNOD');
PPK_PWP.prefix();
PPK_WNU.cntx_psh();
PPK_WNU.prefix();
PPK_WPW.cntx_psh();
PPK_WPW.prefix();
PPK_PNW.cntx_psh();
PPK_PNW.prefix();

PPK_PNW.PPK_PWP();
PPK_PNW.PPK_WPW().PPK_WNU();

_firma:=PPK_PWP.FIRMA;
_od:=PPK_PWP.OD;
_rn:=PPK_PWP.R().RN;


{? PPK_PWP.OSOBA<>null
:: wniosek osoby
|| P.prefix(exec('firma','ustawienia'),PPK_PWP.OSOBA);
   _loop:=P.first();
   {!
   |? _loop
   |! PPK_PWP.prefix(_firma,$P.ref(),_rn);
::    czy są informacje indywidualne?
      {? PPK_PWP.first()
      || {? PPK_PWP.find_key(_od)
         || _epilog();
            undo();
            return()
         || PPK_PNW.PPK_PWP();
            PPK_PWP.P:=P.ref();
            {? PPK_PWP.add()
            || PPK_PNW.PPK_PWP:=PPK_PWP.ref();
               {? ~PPK_PNW.add()
               || _epilog();
                  undo();
                  return()
               ?}
            || _epilog();
               undo();
               return()
            ?}
         ?}
      ?};
      _loop:=P.next()
   !}

|? PPK_PWP.P<>null
:: wniosek pracownika
|| ZC.prefix(exec('firma','ustawienia'),PPK_PWP.OSOBA);
   _loop:=ZC.first();
   {!
   |? _loop
   |! PPK_PWP.prefix(_firma,$ZC.ref(),_rn);
::    czy są informacje indywidualne?
      {? PPK_PWP.first()
      || {? PPK_PWP.find_key(_od)
         || _epilog();
            undo();
            return()
         || PPK_PNW.PPK_PWP();
            PPK_PWP.ZC:=ZC.ref();
            {? PPK_PWP.add()
            || PPK_PNW.PPK_PWP:=PPK_PWP.ref();
               {? ~PPK_PNW.add()
               || _epilog();
                  undo();
                  return()
               ?}
            || _epilog();
               undo();
               return()
            ?}
         ?}
      ?};
      _loop:=ZC.next()
   !}
?};

:: porządki
_epilog();
~~


\_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz "po dołącz" tabeli PPK_PNW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: zgodny ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
exec('_moda','ppk_pnw',_a)


\_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz "po popraw" tabeli PPK_PNW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: zgodny ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
exec('_moda','ppk_pnw',_a)


\_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz po usunięciu wiersza tabeli PPK_PNW.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: zgodny ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
:: podstawowy warunek dalszego działania
{? ~_a | do_state()<>1 || return() ?};

{? bfld('PPK_PWP')<>null
|| PPK_PWP.cntx_psh();
   PPK_PWP.clear();
   {? PPK_PWP.seek(bfld('PPK_PWP'))
   || PPK_PWP.del()
   ?};
   PPK_PWP.cntx_pop()
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 939b32f19a04a14bfbd8f8f69e3970fafebbe36e985dcdb2aac80ca1ea2a576560ea9bc1bc8c1d8a018155e17bced6023f01f54c72fd9394f3077ee70659d4be1d1c7f078569c10adc9b5895237d79663b871f498b293b13aa0ee5f8bf5fef93882eda3a0ed882bd981395e1a7812e5ea6e8b7c398f5c760fc97dd71662f450e

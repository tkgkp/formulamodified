:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: portal_lib.fml
:: Utworzony: 21.04.2020
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły wspólne dla obsługi portalu.
::======================================================================================================================


\gsxm_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli GSXM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('gsxm_chk','portal_lib',0)


\gsxm_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli GSXM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('gsxm_chk','portal_lib',1)


\gsxm_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli GSXM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',GSXW,'GSXM',GSXM.ref())


\gsxm_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli GSXM.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
__CHK.validate(GSXM,
   $("_a.table(_b,"+$_put+",,'TYP','FLAGA','OPIS')"),
   $("{? _b.MAX<0
      || _a.err_fld(_b,'MAX',1,'"+'Wartość nie może być ujemna.'@+"')
      || ''
      ?}
   "),
   $("{? GSXM.GSXM
      || GSXM.cntx_psh();
         GSXM.prefix();
         _maxn:={? GSXM.seek(GSXM.GSXM) || GSXM.MAX ?};
         GSXM.cntx_pop();
         {? _maxn>0 & _maxn<GSXM.MAX
         || _a.err_fld(_b,'MAX',1,'"+
               'Wartość na elemencie podrzędnym nie może być większa niż na elemencie nadrzędnym.'@+
            "')
         || ''
         ?}
      || ''
      ?}
   ")
)


\gsxm_max_df
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Format wyświetlania pola GSXM.MAX.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'empty=%1' [$(GSXM.MAX=0)]


\gsxm_have_children
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła sprawdza, czy bieżący / wskazany rekord tabeli GSXM ma rekordy podrzędne.
::   WE: [_a] [REFERENCE] - Wskazanie rekordu [domyślnie bieżący].
::   WY: 1 - Rekord ma elementy podrzędne;
::       0 - Rekord nie ma elementów podrzędnych.
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
GSXM.cntx_psh();
GSXM.index('VIEW');
GSXM.prefix();
{? {? var_pres('_a')=type_of(null()) & ref_tab(_a)=GSXM || GSXM.seek(_a) || 1 ?}
|| GSXM.prefix(GSXM.TYP,#GSXM.ref());
   _ret:=GSXM.first()
?};
GSXM.cntx_pop();
_ret


\gsxm_wybierz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - przed" w oknie WYBX tabeli GSXM.
::       Zakładamy prawidłowo ustaloną formułę na wartość początkową pola GSXW.N.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
GSXW.blank();
GSXW.GSXM:=GSXM.ref();
_ret:=GSXW.add();
{? ~GSXM.sel_size()
|| sel_exit()
?};
_ret


\gsxm_wybierz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - po" w oknie WYBX tabeli GSXM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~GSXM.sel_size()
|| sel_exit()
?}


\gsxm_wybierz_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - grupa po" w oknie WYBX tabeli GSXM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
sel_exit()


\gsxw_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli GSXW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? GSXW.GSXM & GSXW.FIRMA
|| GSXM.cntx_psh();
   GSXM.prefix();
   _max:=GSXW.GSXM().MAX;
   {? _max>0
   || GSXW.cntx_psh();
      GSXW.index('GSXM');
      GSXW.prefix(GSXW.GSXM,GSXW.FIRMA);
      _size:=GSXW.size();
      GSXW.cntx_pop()
   || _size:=-1
   ?};
   GSXM.cntx_pop();
   {? _max<=_size
   || undo('Osięgnięto maksymalną liczbę (%1) elementów w widoku.'@ [$_max]);
      return(0)
   ?}
?};
exec('gsxw_chk','portal_lib',0)


\gsxw_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Dołącz - po" dla tabeli GSXW.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};
exec('idput_refresh','#table',GSXW.N);
~~


\gsxw_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli GSXW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('gsxw_chk','portal_lib',1)


\gsxw_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Popraw - po" dla tabeli GSXW.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};
exec('idput_refresh','#table',GSXW.N);
~~


\gsxw_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli GSXW.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
GSXM.cntx_psh();
GSXM.prefix();
GSXM.index('VIEW');
{? ~GSXW.GSXM().GSXM
:: Rekord nadrzędny - czy ma podrzędne?
|| GSXM.prefix(GSXM.TYP,#GSXM.ref());
   {? GSXM.first()
   || _firma:=GSXW.FIRMA;
      _gsxn:=GSXW.N;
      GSXW.cntx_psh();
      GSXW.index('GSXM');
      {!
      |? GSXW.prefix(GSXM.ref(),_firma,_gsxn,);
         {? GSXW.first()
         || GSXW.del()
         ?};
         GSXM.next()
      !};
      GSXW.cntx_pop()
   ?}
?};
GSXM.cntx_pop();
1


\gsxw_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Wyzwalacz "Usuń - po" dla tabeli GSXW.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};
exec('idput_refresh','#table',bfld('N'));
~~


\gsxw_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli GSXW.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw.
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
__CHK.validate(GSXW,
   $("_a.table(_b,"+$_put+",,'FIRMA','N','GSXM')"),
:: Specjalna obsługa rekordów, które nie są typu "master" - można je dodać, jeżeli występują w "master".
   $("_ret:=1;
      _master:='IsInPayHistory';
      GSXM.cntx_psh();
      GSXM.prefix();
      {? ref_tab(GSXW.N)=GSPN & GSXW.GSXM().FLAGA<>_master
      || GSXW.cntx_psh();
         GSXW.index('FLAGA');
         GSXW.prefix(GSXW.FIRMA,_master,GSXW.N);
         {? ~GSXW.first()
         || FUN.emsg('"+'Dołączenie grupy do widoku zależnego nie jest możliwe.'@+"');
            _ret:=0
         ?};
         GSXW.cntx_pop()
      ?};
      GSXM.cntx_pop();
      _ret
   ")
)


\gsxw_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknie WER4G tabeli GSXW (miejsca dla bieżącego nagłówka).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Zakładamy, ze właścicielem okna grupowego jest tabela nagłówkowa.
:: Obecnie _NAG jest jedną z tabel: GSPN, GSWN.
_NAG:=cur_tab(0);

_sql:=
:: Wykluczamy widoki, które już są związane z bieżącym elementem ...
   'select GSXW.GSXM from GSXW where GSXW.N=\':_c\' '
   'union '
:: ... oraz te, kóre mają określoną maksymalna liczbe elementów i osiągnęły ją.
   'select GSXW.GSXM '
   'from GSXW join GSXM using (GSXW.GSXM,GSXM.REFERENCE) '
   'where GSXW.FIRMA=:_a and GSXM.TYP=\':_b\' '
   'group by GSXW.GSXM, GSXM.MAX '
   'having GSXM.MAX>0 and GSXM.MAX<=count(*) ';

GSXM.cntx_psh();
GSXM.prefix();
GSXM.f_set('OPIS',,'"GSXM".TYP=\':_b\' and "GSXM".REFERENCE not in (%1)' [_sql],_NAG.FIRMA,2-!_NAG,_NAG.uidref());
GSXM.win_sel('WYBX');
_fblank:=GSXW.fld_fml('N','BLANK',$(!_NAG+'.uidref()'));
GSXM.select();
GSXW.fld_fml('N','BLANK',_fblank);
GSXM.f_clear();
GSXM.cntx_pop();
~~


\gsxw_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wyświetl - przed" w oknie WER4G tabeli GSXW (miejsca dla bieżącego nagłówka).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
GSXM.display()


\gsxn_wybierz_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - grupa przed" w oknach wyboru (WYBX) tabel nagłówkowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KOMM.init(,,'Raport'@);
1


\gsxn_wybierz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - przed" w oknach wyboru (WYBX) tabel nagłówkowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_NAG:=cur_tab(1,1);
_max:=GSXM.MAX;
{? _max>0
|| GSXW.cntx_psh();
   GSXW.index('GSXM');
   GSXW.prefix(GSXM.ref(),_NAG.FIRMA);
   _size:=GSXW.size();
   GSXW.cntx_pop()
|| _size:=-1
?};
{? _size<_max
|| GSXW.blank();
   GSXW.N:=_NAG.uidref();
   GSXW.GSXM:=GSXM.ref();
   GSXW.add(1)
|? _NAG.sel_size()
|| KOMM.set_root('Elementy, których nie udało się dołączyć do widoku:'@);
   KOMM.add(exec('record','#to_string',_NAG.uidref(),1));
   KOMM.sect_end()
?}


\gsxn_wybierz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - po" w oknach wyboru (WYBX) tabel nagłówkowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,1).sel_size()=0
|| sel_exit()
?}


\gsxn_wybierz_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Wybierz - grupa po" w oknach wyboru (WYBX) tabel nagłówkowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
sel_exit();
KOMM.select()


\gsxn_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Dołącz - przed" w oknach VIEW tabel nagłówkowych.
::   WE: [_a] [STRING] - Dodatkowy warunek (filtr) na tabelę nagłówka.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_where:={? var_pres('_a')=type_of('') || _a || '' ?};

:: Zakładamy, ze właścicielem okna grupowego jest tabela nagłówkowa, dla której zdefiniowane jest okno WYBX.
_NAG:=cur_tab(0);

_NAG.cntx_psh();
_NAG.prefix();
_filtr:=exec('get','#filter',_NAG);
_tabacr:=2-!_NAG;
_idadd_acr:=_NAG.idadd_acr();
_NAG.f_set(,,
   '"%1".FIRMA=:_a and (\':\' || "%1".%2 || "%1".REFERENCE) not in (select GSXW.N from GSXW where GSXW.GSXM=:_b)'
   [_tabacr,_idadd_acr]+
   {? GSXM.GSXM
::    Jeżeli miejsce
   || ' and (\':\' || "%1".%2 || "%1".REFERENCE) in ('
         'select GSXW.N '
         'from GSXW join GSXM using (GSXW.GSXM,GSXM.REFERENCE) '
         'where GSXW.FIRMA=:_a and GSXM.TYP=\'%1\' and reference_num(GSXM.REFERENCE)=%3'
      ')'
      [_tabacr,_idadd_acr,$GSXM.GSXM]
   || ''
   ?}+
   {? _where=''
   || ''
   || ' and '+_where
   ?},
   exec('ref_firma','ustawienia'),GSXM.ref()
);
_NAG.win_sel('WYBX');
_NAG.select();
_NAG.f_clear();
exec('set','#filter',_NAG,_filtr);
_NAG.cntx_pop()


\gsxn_usun_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Usuń - grupa przed" w oknach VIEW tabel nagłówkowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('gsxm_have_children','portal_lib')
|| _msg:='Zmiany zostaną uwzględnione również w widokach zależnych.'@
|| _msg:=~~
?};
exec('del_ask','#table',,_msg)


\gsxn_usun_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Usuń - przed" w oknach VIEW tabel nagłówkowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Zakładamy, ze właścicielem okna grupowego jest tabela nagłówkowa, w której jest pole FIRMA.
_NAG:=cur_tab(0);

_ret:=0;
GSXW.cntx_psh();
GSXW.index('GSXM');
GSXW.prefix();
{? GSXW.find_key(GSXM.ref(),_NAG.FIRMA,_NAG.uidref(),)
|| _gr:=_NAG.sel_size();
   {? ~_gr & exec('gsxm_have_children','portal_lib')
   || _msg:='Zmiana zostanie uwzględniona również w widokach zależnych.'@
   || _msg:=~~
   ?};
   {? _gr | exec('del_ask','#table',,_msg)
   || _ret:=GSXW.del(,1)
   ?}
|| FUN.info('Rekord został już usuniety.'@)
?};
GSXW.cntx_pop();
_ret


\gsxn_usun_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Usuń - po" w oknach VIEW tabel nagłówkowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Zakładamy, ze właścicielem okna grupowego jest tabela nagłówkowa.
_NAG:=cur_tab(0);

{? ~_NAG.sel_size()
|| _NAG.f_del()
?};

1


\gsxn_usun_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Obsługa akcji "Usuń - grupa po" w oknach VIEW tabel nagłówkowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Zakładamy, ze właścicielem okna grupowego jest tabela nagłówkowa.
cur_tab(0).f_rfresh()


\por_conf_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Sprawdza czy przekazana firma jest poprawną w stosunku do ustawień konfiguracji synchronizacji HR Portal
::   WE: _a [REFERENCE] - wskazanie na firmę
::   WY: Zwraca obiekt z polami:
::          'ok'    - czy powinniśmy dalej przetwarzać zwrócony wynik
::          'firma' - ref firmy w zależności od konfiguracji synchronizacji HR Portal
::----------------------------------------------------------------------------------------------------------------------
_result:=obj_new('ok','firma');
_result.ok:=0;
_result.firma:=null();

_firma:={? var_pres('_a')=type_of(null()) || _a || return(_result) ?};

_result.firma:=_firma;

_conf:=XINFO.POR_CONF;
exec('czytaj','#stalesys',,XINFO,'POR_CONF');
_conf==XINFO.POR_CONF;

{? _conf='W'
:: dla konfiguracji wielofirmowej
|| _result.ok:=1;
   _result.firma:=null()
:: dla konfiguracji jednofirmowej
|? _conf='J'
|| _result.ok:=(_firma=exec('ref_firma','ustawienia'))
?};
_result


\att_limit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.14]
:: OPIS: Formuła zwraca maksymalny rozmiar załącznika akceptowany przez HR Portal.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: 3MB = 3 * 1024 * 1024 = 3 145 728
3145728


\att_size
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.14]
:: OPIS: Formuła badająca rozmiar załącznika. Formuła do wykorzystania w akcjach "Rekord - po" oraz w procedurach
::       testujących.
::       Limit rozmiaru załącznika wynika z limitów związanych z warstwą sieciową komunikacji z portalem.
::   WE:  _a  [TABLE]  - Uchwyt tabeli.
::        _b  [STRING] - Akronim pola z załącznikiem
::       [_c] [NUMBER] - Tryb wsadowy (bez komunikatu) [0*/1].
::   WY:  1 - Brak załącznika lub rozmiar załącznika został pozytywnie zweryfikowany (jest mniejszy lub równy limitowi).
::        0 - Rozmiar załącznika został negatywnie zweryfikowany (przekracza limit).
::       ~~ - Błędny parametr wywołania lub rozmiaru nie udało się zweryfikować.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(SYSBLOB)
|| _TAB:=_a
|| return()
?};
{? var_pres('_b')=type_of('') & var_pres(_b,_TAB)=33
|| _fld:=_b
|| return()
?};
_batch:=var_pres('_c')=type_of(0) & _c;

_size:=-1;
{? ($('_a.%1' [_fld]))(_TAB)<>null()
|| _size:=_TAB.bl_info(_fld,'SIZE')
|? (_fn:=_TAB.bl_file(_fld))<>''
|| _fh:=fopen(_fn,'br',0,0,1,1);
   {? _fh.is_open()
   || _size:=fgetsize(_fh);
      _fh.fclose()
   ?}
|| return(1)
?};

{? _size=-1
|| {? ~_batch
   || FUN.emsg('Rozmiaru załącznika nie udało się zweryfikować.'@)
   ?};
   ~~
|? _limit:=exec('att_limit','portal_lib');
   _size>_limit
|| {? ~_batch
   || _form:="form(_a,,,' ')";
      _in:=obj_new('size','limit');
      _in.size:=_size;
      _in.limit:=_limit;
      _sc:=obj_new('size','limit');
      _ss:=obj_new('size','limit');
      {! _lp:=1 .. 2
      |! _sc[_lp]:=exec('B2xB','#convert',_in[_lp]);
         {? _sc[_lp].p=''
         || _ss[_lp]:='%1B' [_form(_in[_lp])]
         || _ss[_lp]:='%1 %2B (%3 B)' [_form(_sc[_lp].r$4),_sc[1].p,_form(_in[_lp])]
         ?}
      !};
      FUN.emsg(
         'Załącznik ma być prezentowany na portalu,\n'
         'jednak jego rozmiar [%1]\nprzekracza dopuszczalny limit [%2].'@
         [_ss.size,_ss.limit]
      )
   ?};
   0
|| 1
?}


\flushRetention
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Oznaczenie do synchronizacji z portalem danych nie mieszczących się bądź nowo mieszczących się
::       w okresie retencji.
::   WE: _a [STRING] - kod definicji, np. 'PORTAL_N_ID'
::       _b [STRING] - akronim tabeli
::       _c [INTEGER]- liczba lat retencji
::       _d [STRING] - fragment instrukcji where zapytania SQL, który zwróci rok do porównania, np. 'N.ROK'
::      [_e][FORMULA]- formuła do wykonania po ustawieniu się na konkretnym rekordzie
::   WY: 0/1 - status powodzenia operacji
::----------------------------------------------------------------------------------------------------------------------
_ts:=type_of('');
{? var_pres('_a')<>_ts | var_pres('_b')<>_ts | var_pres('_c')<>type_of(0) | var_pres('_d')<>_ts | ~+_a | ~+_b | ~+_d
|| FUN.error('Błąd parametrów wejściowych'@);
   return(0)
?};
_kod:=_a;
_tabela:=_b;
_TAB:=($_tabela)();
_rtn:=_c;
_rok:=_d;
{? var_pres('_e')=type_of("") & +_e
|| _fml:=_e
|| _fml:="1"
?};
_wyn:=1;

:: Jeśli retencja ustawiona na 0, to nie czyścimy starych (akceptujemy wszystko):
{? _rtn=0
|| return(1)
?};

:: Tabela z identyfikatorami danego kodu definicji:
_SYNC:=exec('get_ids_tab','#sync_id',_kod);

:: Ustawienie odpowiedniej maski, zapytanie pobierające dane wymagające ponownej synchronizacji:
SYNC_ID.cntx_psh();
SYNC_ID.use(_maska);
_ID:=sql('select REFERENCE as REF'
         'from %1 '
         'where (((%2)<:_a) and %1.IDADD in (select UIDREF from :_b)) '
         'or (((%2)>=:_a) and %1.IDADD not in (select UIDREF from :_b))'[_tabela,_rok]
         ,(date()~1)-_rtn,_SYNC);
SYNC_ID.cntx_pop();

:: Przejście przez wszyskie znalezione wiersze celem "przeputowania":
_TAB.cntx_psh();
_TAB.prefix();
{? _ID.first()
|| {!
   |? _wyn:=(_TAB.seek(_ID.REF) & _TAB.put(,1));
      {? _wyn || _fml() ?};

      _wyn & _ID.next()
   !}
?};
_TAB.cntx_pop();

_wyn


\retentionPersonAbsence
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Liczba lat przechowywania danych na portalu dla encji PersonAbsence.
::
::       UWAGA! Jest już przygotowana formuła aktualizująca synchronizację encji PersonAbsence - \flushPersonAbsence
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
3


\flushPersonAbsence
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Oznaczenie do synchronizacji z portalem danych encji PersonAbsence nie mieszczących się bądź nowo mieszczących
::       się w okresie retencji.
::   WE:
::   WY: 0/1 - status powodzenia operacji
::----------------------------------------------------------------------------------------------------------------------
exec('flushRetention','portal_lib','PORTAL_N_ID','N',exec('retentionPersonAbsence','portal_lib'),'N.ROK')


\retentionAdsImprovement
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Liczba lat przechowywania danych na portalu dla encji AdsImprovement.
::
::       UWAGA! Jest już przygotowana formuła aktualizująca synchronizację encji AdsImprovement - \flushAdsImprovement
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
3


\flushAdsImprovement
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Oznaczenie do synchronizacji z portalem danych encji AdsImprovement nie mieszczących się bądź nowo mieszczących
::       się w okresie retencji.
::   WE:
::   WY: 0/1 - status powodzenia operacji
::----------------------------------------------------------------------------------------------------------------------
exec('flushRetention','portal_lib','PORTAL_BIPULP_OGUL_ID','BIPULP',exec('retentionAdsImprovement','portal_lib'),
   'extract(year from BIPULP.DT2) is not null and extract(year from BIPULP.DT2)',
   "
   BIPULP.PORTAL:='N';
   BIPULP.put();
   BIPULPG.cntx_psh();
   BIPULPG.index('LINK');
   BIPULPG.prefix(BIPULP.NP_DOK,BIPULP.ZZ_DOK);
   {? BIPULPG.first()
   || {!
      |? BIPULPG.put(,1);

         BIPULPG.next()
      !}
   ?};
   BIPULPG.cntx_pop();

   BIPODB.cntx_psh();
   BIPODB.index('LINK');
   BIPODB.prefix(BIPULP.NP_DOK,BIPULP.ZZ_DOK);
   {? BIPODB.first()
   || {!
      |? BIPODB.put(,1);

         BIPODB.next()
      !}
   ?};
   BIPODB.cntx_pop();

   BIPPYTP.cntx_psh();
   BIPPYTP.index('BIPPYTN');
   BIPPYTN.cntx_psh();
   BIPPYTN.index('LINK');
   BIPPYTN.prefix(BIPULP.NP_DOK,BIPULP.ZZ_DOK);
   {? BIPPYTN.first()
   || {!
      |? BIPPYTP.prefix(BIPPYTN.ref());
         {? BIPPYTP.first()
         || {!
            |? BIPPYTP.put(,1);

               BIPPYTP.next()
            !}
         ?};
         BIPPYTN.put(,1);

         BIPPYTN.next()
      !}
   ?};
   BIPPYTN.cntx_pop();
   BIPPYTP.cntx_pop()
   ")


\retentionPersonContract
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Liczba lat przechowywania danych na portalu dla encji PersonContract oraz powiązanych (PersonContractAccount,
::       PersonPayCalc).
::
::       UWAGA! Jest już przygotowana ogólna formuła aktualizująca synchronizację encji - \flushRetention
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
3


\retentionPersonPayCalc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Liczba lat przechowywania danych na portalu dla encji PersonPayCalc w kontekście O_P (w kontekście RH
::       używany jest okres retencji umowy cywilnoprawnej).
::
::       UWAGA! Jest już przygotowana ogólna formuła aktualizująca synchronizację encji - \flushRetention
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
3


\retentionQuestion
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Liczba lat przechowywania danych na portalu dla encji Question i powiązanych (QuestionCat, QuestionDetail).
::
::       UWAGA! Jest już przygotowana ogólna formuła aktualizująca synchronizację encji - \flushRetention
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
3


\retentionKnowledgeBFile
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Liczba lat przechowywania danych na portalu dla encji KnowledgeBFile
::       (KnowledgeBaseItem - załączniki dla współpracownika).
::
::       UWAGA! Jest już przygotowana ogólna formuła aktualizująca synchronizację encji - \flushRetention
::   WE: [_a][STRING] - typ załącznika (ZALACZ.TYP_ZAL().NAZWA)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_typ:={? var_pres('_a')=type_of('') || _a || '' ?};
_ret:=3;

:: Nazwy załączników PIT i ZUS - "systemowe" (bez możliwości zmiany)
{? ',Informacje podatkowe,Dokument RMUA,Dokument IMiR,'*',%1,'[_typ]
|| _ret:=5
?};

_ret


\retentionKnowledgeBaseItem
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Liczba lat przechowywania danych na portalu dla encji KnowledgeBaseItem
::       (dla kategorii "Wydarzenia").
::
::       UWAGA! Jest już przygotowana ogólna formuła aktualizująca synchronizację encji - \flushRetention
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
3


\retentionPersonWorkSchedule
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Liczba lat przechowywania danych na portalu dla encji PersonWorkSchedule. (Określona jest liczba lat wstecz)
::
::       UWAGA! Jest już przygotowana ogólna formuła aktualizująca synchronizację encji - \flushRetention
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
1


\retentionPerson
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.14]
:: OPIS: Liczba lat przechowywania danych na portalu dla encji Person. (ma wpływ na znacznik P.PORTAL)
::
::       UWAGA! Jest już przygotowana ogólna formuła aktualizująca synchronizację encji - \flushRetention
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
3


\overtime_code
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Kod stosowany przy zapisie wniosków o nadgodziny pochodzących z Portalu HR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'PHR'


\retentionZALACZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: Formuła sprawdza czy dla wskazanego załącznika minął już okres przechowywania na Portalu HR.
::   WE:  _a  [REFERENCE] - Wskazanie załącznika.
::   WY: Okres przechowywania minął: 0/1.
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
{? var_pres('_a')=type_of(null()) & _a<>null() & ref_tab(_a)=ZALACZ
|| _zalacz:=_a
|| return(_ret)
?};

ZALACZ.cntx_psh();
ZALACZ.prefix();
{? ZALACZ.seek(_zalacz)
|| SLO_NAZ.cntx_psh();
   SLO_NAZ.prefix();
   _rtn:=exec('retentionKnowledgeBFile','portal_lib',ZALACZ.TYP_ZAL().NAZWA);
   _ret:=_rtn<>0 & (ZALACZ.DATA~1)<((date()~1)-_rtn);
   SLO_NAZ.cntx_pop()
?};
ZALACZ.cntx_pop();
_ret


:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 c648f03c58443cf5929f68294eda87fca34cc954d22c19c758fabf11c3f0f8871c69e1ad5e573a373eaaab7c8953d24b449f322fd01b2cbe7355de0749fe658c848933e3f91a4935c9cb702ec1056395e5d2293154a66496a8bec5e45dbd25f681ea6fac95f331d8a83486183938bab05639a06b3697cdc5ab3cf62840d692e8

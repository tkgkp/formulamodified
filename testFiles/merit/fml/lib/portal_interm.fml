:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: portal_interm.fml
:: Utworzony: 14.04.2023
:: Autor: MB
::======================================================================================================================
:: Zawartość: Plik zawiera formuły wykorzystywane do obsługi wniosków inTerm na portalu ABS.
::======================================================================================================================


\params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Ustawienie parametrów wniosków inTerm na portalu ABS
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('porInte');
exec('czytaj','#stalesys',,XINFO,'POR_INTE');
porInte:=XINFO.POR_INTE;
_data:=exec('edytuj','#stalesys',XINFO,'POR_INTE',"exec('ar_params','portal_interm')",1);
{? _data<>date(0,0,0) & porInte<>XINFO.POR_INTE
|| _typ:=exec('bl_typ','obiegi',2);
   progress(,'Trwa oznaczania wniosków do ponownego wysłania ...',,1);
:: interacja po typach wniosków inTerm
   ETYPY.cntx_psh();
   ETYPY.index('IN_POR');
   ETYPY.prefix(_typ,1);
   {? ETYPY.first()
   || {!
      |? ETYPY.put(,1);
         ETYPY.next()
      !}
   ?};
   ETYPY.cntx_pop();

:: iteracja po wnioskach inTerm z bieżącegi i poprzedniego roku
   FIRMA.cntx_psh();
   FIRMA.index('EWID');
   FIRMA.prefix('T',);
   {? FIRMA.first()
   || _teraz:=date();
      _data:=date((_teraz~1)-1,1,1);
      ROK_F.cntx_psh();
      ROK_F.index('ROKPOCZ');
      {!
      |? ROK_F.prefix(FIRMA.ref());
         {? ROK_F.find_le(_data)
         || EDOKUM.cntx_psh();
            {!
            |? echo('Wnioski do ponownego wysłania z firmy: %1 z roku: %2'@[FIRMA.SYMBOL,ROK_F.NAZ]);
               EDOKUM.use('skid_v'+($(ROK_F.POCZ_ROK~1)+2));
               EDOKUM.index('IN_POR');
               EDOKUM.prefix(FIRMA.ref(),_typ,'N',1);
               {? EDOKUM.first()
               || {!
                  |? EDOKUM.put(,1);
                     EDOKUM.next()
                  !}
               ?};
               ROK_F.next()
            !};
            EDOKUM.cntx_pop()
         ?};
         FIRMA.next()
      !};
      ROK_F.cntx_pop()
   ?};
   FIRMA.cntx_pop();
   prgs_clr()
?};
VAR_DEL.delete('porInte');
echo()


\ar_params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Po redakcji okna z parametrami dla obsługi wniosków inTerm
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
{? XINFO.POR_INTE<>'' &
   (
      7+XINFO.POR_INTE<>'http://' & 8+XINFO.POR_INTE<>'https://' |
      +XINFO.POR_INTE<9
   )
|| FUN.info('Wymagany adres serwera inTerm postacie: http[s]://adres[:port].'@);
   _ret:='POR_INTE'
?};

{? _ret='' & porInte<>XINFO.POR_INTE
|| {? ~FUN.ask(
         'Zmieniono adres serwera inTerm.\n'
         'Wymusza to ponowne wysłanie wniosków inTerm na portal.\n'
         'Czy kontynuować?'@
      )
   || _ret:='POR_INTE'
   ?}
?};
_ret


\be_por_interm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Przed redakcją XINFO.POR_INTE
::----------------------------------------------------------------------------------------------------------------------
1


\ae_por_interm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Po redakcji XINFO.POR_INTE
::----------------------------------------------------------------------------------------------------------------------
1


\wybierz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Obsługa wniosków inTerm z portalu ABS
::   WE: _a - uidref EDOKUM lub ETYPY
::----------------------------------------------------------------------------------------------------------------------
_link:={? var_pres('_a')=type_of('') || _a || '' ?};
{? +_link=48
|| {? ref_tab(_link)=ETYPY
   || _typ:='';
      _dzis:=date();
      ROK_F.cntx_psh();
      ROK_F.index('ROKPOCZ');
      ROK_F.prefix(REF.FIRMA);
      {? ROK_F.find_le(_dzis) | ROK_F.last()
      || SSTALE.AR:=ROK_F.ref()
      ?};
      ROK_F.cntx_pop();
      ETYPY.cntx_psh();
      ETYPY.prefix();
      {? ETYPY.seek(_link)
      || _typ:=ETYPY.NAZWA
      ?};
      ETYPY.cntx_pop();
      title(_typ+' - rejestracja');
      _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='OBE_FAW_DARP';
      _params.AKCJA:='Dołącz';
      _params.PROC_START:='T';
      _params.CONTEXT:=obj_new('EDOKUM','ROK');
      _params.CONTEXT.EDOKUM:=null;
      _params.CONTEXT.ROK:=SSTALE.AR;
      _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'M_WYW','W');
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'R_WYW','JW');
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TYP_DOK',_typ);
      exec('mp_run','#b__box',_params);
      {? var_pres('EDK2SEND')>0 & EDK2SEND & _params.CONTEXT.EDOKUM
      || _ref:=_params.CONTEXT.EDOKUM;
         EDOKUM.cntx_psh();
         EDOKUM.prefix();
         EDOKUM.use(ref_name(_ref));
         {? EDOKUM.seek(_ref)
         || exec('EDOKUM_send_from_interm','portal_interm')
         ?};
         EDOKUM.cntx_pop()
      ?}
   |? ref_tab(_link)=EDOKUM
   ||
::    EDOKOS.trig_a('add',"debug(); ~~",'test');
::    EDOKOS.trig_a('put',"debug(); ~~",'test');
::    EDOKOS.trig_b('del',"debug(); 1",'test');
      EDOKUM.cntx_psh();
      EDOKUM.prefix();
      EDOKUM.use(ref_name(_link));
      {? EDOKUM.seek(_link)
      || exec('use_edokum','obiegi_log',1);
         _tyt:=EDOKUM.TYP().NAZWA;
         {? -EDOKUM.STATUS='s' | EDOKUM.STATUS='R'
         || _tyt+=' - etap: '+EDOKUM.B_PRELS
         |? EDOKUM.STATUS='Y'
         || _tyt+=' - zaakceptowany'
         |? EDOKUM.STATUS='Y'
         || _tyt+=' - odrzucony'
         ?};
         title(_tyt);
         _find:=0;
         EDOKOS.cntx_psh();
         EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         EDOKOS.index('PORTAL');
         EDOKOS.prefix(EDOKUM.ref(),'T',EDOKUM.B_PREL,OPERATOR.USER);
         {? EDOKOS.first()
         || _find:=1;
            _oper:=EDOKOS.OPERACJA;
            {? _oper='W'
            || exec('akc_mod','obe_faw',1)
            || _params:=exec('mp_run_a','#b__box');
               _params.ACT_UID:={? _oper='P' || 'OBE_FAW_CPRZ' || 'OBE_FAW_EAKP' ?};
               _params.UIDREF:=EDOKUM.uidref();
               _params.CONTEXT:=obj_new('ROK');
               _params.CONTEXT.ROK:=EDOKUM.ROK_F;
               exec('mp_run','#b__box',_params)
            ?}
         ?};
         EDOKOS.cntx_pop();
         {? _find=0
         || exec('akc_display','obe_faw')
         ?};
         {? var_pres('EDK2SEND')>0 & EDK2SEND
         || exec('EDOKUM_send_from_interm','portal_interm')
         ?};
         exec('use_edokum','obiegi_log',2)
      || FUN.info('Nie znaleziono wniosku.')
      ?};
      EDOKUM.cntx_pop()
   ?}
?}


\EDOKUM_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Wysyła bieżący wniosek na portal ABS
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new('res','msg');
_obj.res:=1;

_args:=obj_new('uidref');
_args.uidref:=EDOKUM.uidref();
_log_level:=-1;

:: Wysyłka wniosku
_res:=exec('run_mwac','sync_mwa','PORTAL_WNIOSKI','chr_PersonRequestModify',-1,_log_level,_args);
{? var_pres('_res')=type_of(0) & _res=0
|| _obj.res:=0;
   _obj.msg:='Nie udało się przesłać wniosku na portal ABS.'
?};

:: Wysyłka ścieżki akceptacji
{? _obj.res
|| _res:=exec('run_mwac','sync_mwa','PORTAL_WNIOSKI','cseod_AcceptanceHeaderModify',-1,_log_level,_args);
   {? var_pres('_res')=type_of(0) & _res=0
   || _obj.res:=0;
      _obj.msg:='Nie udało się przesłać ścieżki akceptacji wniosku na portal ABS.'
   ?}
?};
_obj


\EDOKUM_is_przek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Czy wniosek powinien być wysłany na portal HR
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
ETYPPROC.cntx_psh();
ETYPPROC.index('UNIK');
ETYPPROC.prefix(REF.FIRMA,EDOKUM.TYP);
{? ETYPPROC.first()
|| B_PROC.cntx_psh();
   B_PROC.index('SYMVER'); B_PROC.prefix(REF.FIRMA,ETYPPROC.PROC_SYM,ETYPPROC.PROC_VER,);
   {? B_PROC.first()
   || _el:=exec('FindInSet','#table','B_ELE','SYMBOL','OBE_FAW_CPRZ',,,1,,null);
      B_PREL.cntx_psh();
      B_PREL.index('PREL'); B_PREL.prefix(B_PROC.ref());
      {? B_PREL.find_key(_el)
      || _jest:=1
      ?};
      B_PREL.cntx_pop()
   ?};
   B_PROC.cntx_pop()
?};
ETYPPROC.cntx_pop();
_jest


\EDOKUM_to_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Wniosek do wysłania na portal ABS
::----------------------------------------------------------------------------------------------------------------------
EDK2SEND:=1


\EDOKUM_send_from_interm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Wysyłanie wniosku na portal HR z inTerma
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')
|| _id:=exec('get_id_cloud','portal_core','PORTAL_EDOKUM_ID',EDOKUM.uidref());
   exec('EDOKUM_send','portal_interm');
   ctr_set('!application',,'portalHR_refresh','csl_request');
   ctr_set('!application',,'portalHR_refresh','cseod_Acceptance');
   {? _id=0
   || ctr_set('!application',,'portalHR_message','Dołączono wniosek',EDOKUM.TYP().NAZWA)
   ?}
?}


\interm_link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca adres URL dla inTerma
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO,'POR_INTE');
XINFO.POR_INTE


\gen_link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Tworzy link do wniosków inTerm
::   WE: _a - parametr linku
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new('f','p1');
_tab.f:='PORTAL';
_tab.p1:=_a;
_interm:=exec('interm_link','portal_interm');
_grupa:=AppList.my.tab.GRP_IDEN;
FIRMA.cntx_psh();
_app:=REF.FIRMA().APP_IDEN;
FIRMA.cntx_pop();
_link:=link_uri(_tab,_interm,_grupa,_app);
_link:=gsub(_link,'?f','?_f');
::_link+='&_portal=1';
{? _interm=''
|| _link:=gsub(_link,'mbase','http')
?};
_link


\EDOKUM_link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca link do wniosku inTerm dla portalu ABS
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
_link:=exec('gen_link','portal_interm',EDOKUM.uidref());
EDOKUM.cntx_pop();
_link


\ETYPY_link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca link do dołączania wniosku inTerm dla portalu ABS
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
_link:=exec('gen_link','portal_interm',ETYPY.uidref());
ETYPY.cntx_pop();
_link


\EDOKUM_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Opis wniosku na podstawie danych opisujących komplet
::----------------------------------------------------------------------------------------------------------------------
_value:='';
EDOK_ATK.cntx_psh();
EDOK_ATK.use('edoknt'+(EDOKUM.name()+2));
EDOK_ATK.index('EDOKUM');
EDOK_ATK.prefix(EDOKUM.ref());
{? EDOK_ATK.first()
|| _value:=EDOK_ATK.OPIS
?};
EDOK_ATK.cntx_pop();
_value


\EDOKUM_action
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca akronim bieżącej czynności na wniosku
::----------------------------------------------------------------------------------------------------------------------
_action:='OBE_FAW_EAKP';
EDOKOS.cntx_psh();
EDOKOS.use('skid_y'+(EDOKUM.name()+2));
EDOKOS.index('PORTAL');
EDOKOS.prefix(EDOKUM.ref(),'T',EDOKUM.B_PREL,);
{? EDOKOS.first() & EDOKOS.OPERACJA='P'
|| _action:='OBE_FAW_CPRZ'
?};
EDOKOS.cntx_pop();
_action


\btn_akc_path
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Prezentuje ścieżkę akceptacji wniosku inTerm na portalu
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(3,
   'NO','INTEGER','Numer'@,
   'DATE','DATE','Data'@,
   'TIME','TIME','Godzina'@,
   'U_KOD','STRING[10]','Użytkownik - kod'@,
   'U_IN','STRING[20]','Użytkownik'@,
   'B_PRELS','STRING[100]','Etap'@,
   'STATUS','STRING[20]','Status'@,
   'U','STRING[255]','Uwagi'@,
   'U_D','SYS_MEMO','Szczegóły'@
);
_sel:=_tab.mk_sel('Ścieżka akceptacji','P',,'edokos0101',,,,,'U');
_tab.win_fld(_sel,,'DATE',,,-10,,,'Data'@);
_tab.win_fld(_sel,,'TIME',,,-8,,,'Godzina'@);
_tab.win_fld(_sel,,'B_PRELS',,,30);
::_tab.win_fld(_sel,,'U_KOD',,,,,,'Użytkownik'@);
_tab.win_fld(_sel,,'U_IN',,,,,,'Użytkownik'@);
_tab.win_fld(_sel,,'STATUS',,,20);
_tab.win_fld(_sel,,'U',,,50,,,'Uwagi');
_tab.win_sel(_sel);

_edit:=_tab.mk_edit('Ścieżka akceptacji',,'edokos0102');
_tab.win_esep(_edit,'Dane ogólne');
_tab.win_efld(_edit,,'DATE');
_tab.win_efld(_edit,,'TIME');
_tab.win_efld(_edit,,'B_PRELS');
_tab.win_efld(_edit,,'U_IN');
_tab.win_efld(_edit,,'STATUS');
_tab.win_esep(_edit,'Opis');
_tab.win_efld(_edit,,'U_D',,,,-5);
_tab.win_edit(_edit);

EDOKOS.cntx_psh();
EDOKOS.use('skid_y'+(ref_name(EDOKUM.ref())+2));
EDOKOS.index('SZUK11');
EDOKOS.prefix(EDOKUM.ref(),'S');
{? EDOKOS.first()
|| {!
   |? _tab.blank(1);
      _tab.NO:=EDOKOS.OPER_NUM;
      _tab.DATE:=EDOKOS.DATA;
      _tab.TIME:=EDOKOS.TIME;
      _tab.U_KOD:=EDOKOS.USERS().KOD;
      _tab.U_IN:=USERS.OSOBA().PIERWSZE+' '+USERS.OSOBA().NAZWISKO;
      _tab.B_PRELS:=EDOKOS.B_PRELS;
      _tab.STATUS:={? EDOKOS.STATUS='T' || 'Wykonane'
                   |? EDOKOS.STATUS='O' || 'Odrzucone'
                   |? EDOKOS.STATUS='W' || 'Wycofane'
                   |? EDOKOS.STATUS='x' || 'Pominięte'
                   |? EDOKOS.STATUS='N' || ''
                   || ''
                   ?};
      _memo:=EDOKOS.memo_txt(,1,'UW_DL');
      _tab.U:=gsub(_memo,'\n',' ');
      _tab.add();
      {? _memo<>''
      || _tab.memo_set(_memo,'U_D');
         _tab.memo_put(,'U_D')
      ?};
      EDOKOS.next()
   !}
?};
EDOKOS.cntx_pop();

_tab.select()


\portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Formuła wywoływana w inTerm uruchamiana z linku z portalu HR dla wniosków inTerm
::----------------------------------------------------------------------------------------------------------------------
_par:=app_info('init_param');
exec('start_proces','b_proces',0);
exec('wybierz','portal_interm',_par)

:Sign Version 2.0 jowisz:1045 2023/12/11 09:59:25 535561bd2c783ec4f116da14806b3d77803ad34d824bffebb7cbf66fc2fc2937d8601b34f2aa4aa76334bcebcf6098fb84116a6ed70323a38bfb293af4ab668995bb6b6ba84765400ddbf50a5fe2cfa028c55e86e77c6430d9c737095b6357b4769d675f7e7703d5facaff2e43b433ab29a74ef441db76d469c20d11bcdc7de1

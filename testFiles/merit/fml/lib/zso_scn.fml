:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zso_scn.fml
:: Utworzony: 06.02.2020
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły obszaru ZSO_SCN - Zeskanowane dokumenty
::======================================================================================================================


\akc_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Dołączenie zeskanowanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZSO_SCN_DARK';
_params.AKCJA:='Dołącz';
_params.PROC_START:='T';
exec('mp_run','#b__box',_params);
~~


\akc_scn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Skanowanie dokumentów
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZSO_SCN_DASK';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\akc_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Usunięcie zeskanowanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
_grp:=SKAN_DOK.sel_size();
{? _grp
|| _del:=1
|| _del:=0;
   {? SKAN_DOK.STATUS='R'
   || _del:=FUN.ask('Usunąć zeskanowany dokument?'@)
   || _del:=FUN.ask('Usunąć zeskanowany dokument?'@,'EXCLAM')
   ?}
?};
{? _del
|| {? 'DZR'*SKAN_DOK.STATUS=0
   || exec('del_doc','skanuj')
   ?};
   {? SKAN_DOK.STATUS='Z'
   || {? -SKAN_DOK.TYP='d' & +SKAN_DOK.OKRES=4
      || DOK.cntx_psh();
         DOK.use('doku'+SKAN_DOK.OKRES);
         DOK.index('DOKZRODL'); DOK.prefix();
         {? DOK.find_key($SKAN_DOK.ref(),)
         || DOK.DOKZRODL:='';
            DOK.put()
         ?};
         DOK.cntx_pop()
      |? -SKAN_DOK.TYP='f' & +SKAN_DOK.OKRES=2
      || EDOKUM.cntx_psh();
         EDOKUM.use('skid_v'+SKAN_DOK.OKRES);
         EDOKUM.index('DOK_POW'); EDOKUM.prefix();
         {? EDOKUM.find_key(SKAN_DOK.uidref(),)
         || EDOKUM.DOK_POW:='';
            EDOKUM.put()
         ?};
         EDOKUM.cntx_pop()
      ?}
   ?};
   DOKUM.cntx_psh();
   DOKUM.index('TYP'); DOKUM.prefix($SKAN_DOK.ref(),'I');
   {? DOKUM.first() || {! |? _uid:=DOKUM.uidref(); _rr:=DOKUM.del(); exec('dokumlog_del','zbl_dok',_uid); _rr !} ?};
   DOKUM.cntx_pop();
   SKAN_DOK.del();
   {? _grp || GrDel.DEL+=1 ?}
?}


\bg_akc_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Przed akcją grupową usunięcia zeskanowanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć zaznaczone zeskanowane dokumenty?'@)
|| VAR_DEL.delete('GrDel');
   GrDel:=obj_new('ALL','DEL','info');
   GrDel.ALL:=SKAN_DOK.sel_size();
   GrDel.DEL:=0;
   GrDel.info:="
      FUN.info(
         'Liczba zaznaczonych dokumentów: %1\n'
         'Liczba usuniętych dokumentów: %2'@[$.ALL,$.DEL]
      )
   ";
   1
?}


\ag_akc_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Po akcji grupowej usunięcia zeskanowanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
GrDel.info();
VAR_DEL.delete('GrDel')


\akc_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Akcja otwórz dla zeskanowanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? SKAN_DOK.PLIK
|| exec('bl_view','#blob',SKAN_DOK,'PLIK')
|| FUN.info('Nie znaleziono pliku.')
?};
''

\akc_save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Akcja zapisz dla zeskanowanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
_lp:=0;
{? SKAN_DOK.PLIK || _lp+=1 ?};
{? SKAN_DOK.PLIK_OCR || _lp+=2 ?};
_fld:='';
{? _lp=3
|| _lp:=FUN.choice('Który plik zapisać?',1,'Dokument','Dane')
?};
{? _lp=1
|| _fld:='PLIK'
|? _lp=2
|| _fld:='PLIK_OCR'
?};
{? _fld<>''
|| exec('bl_save','#blob',SKAN_DOK,_fld)
?};
''


\akc_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Wysłanie dokumentu do OCR
::   WE: [_a] - komunikaty: [1]-tak 0-nie
:: ~OST: INBLPUT
::----------------------------------------------------------------------------------------------------------------------
{? 0
|| SKAN_DOK.STATUS:='D';
   SKAN_DOK.ID:='1';
   SKAN_DOK.bl_put('PLIK_OCR','@c:/praca/tmp/skanuj.to/003.bin',0,,'003.bin.json');
   exec('set_nag','skanuj');
   SKAN_DOK.put();
   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='ZSO_SCN_OCRD';
   _params.AKCJA:='Zakończ';
   _params.UIDREF:=SKAN_DOK.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SKAN_DOK',SKAN_DOK.ref());
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params);
   return(~~)
?};

_kom:={? var_pres('_a')>0 || _a || 1 ?};
_grp:=var_pres('GrSend')>0 & SKAN_DOK.sel_size();
{? SKAN_DOK.STATUS<>'R'
|| {? _grp
   || GrSend.JEST+=1
   |? _kom
   || FUN.info('Dokument jest już wysyłany.')
   ?}
|? exec('correct','skanuj') & (_kom=0 | _grp | FUN.ask('Czy wysłać dokument?'@))
|| _obj:=exec('send_doc','skanuj');
   {? _obj.id<>-1
   || SKAN_DOK.STATUS:={? _obj.state=5 || 'P' || 'S' ?};
      SKAN_DOK.ID:=$_obj.id;
      SKAN_DOK.put();
      {? _grp
      || GrSend.OK+=1
      ?}
   |? ~_grp
   || {? _obj.msg<>''
      || {? _kom
         || FUN.info('Informacja ze skanuj.to:\n%1'@[_obj.msg])
         ?}
      |? _kom
      || FUN.info('Nie udało się wysłanie dokumentu do skanuj.to.'@)
      ?}
   ?}
?};
~~


\bg_akc_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Przed akcją grupową wysłania do OCR
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Wysłać zaznaczone dokumenty?'@)
|| VAR_DEL.delete('GrSend');
   GrSend:=obj_new('ALL','JEST','OK','info');
   GrSend.ALL:=SKAN_DOK.sel_size();
   GrSend.JEST:=GrSend.OK:=0;
   GrSend.info:="
      FUN.info(
         'Liczba zaznaczonych dokumentów: %1'@[$.ALL]+
         {? .JEST
         || '\nLiczba już wysłanych dokumentów: %1'@[$.JEST]
         || ''
         ?}+
         {? .OK
         || '\nLiczba wysłanych dokumentów: %1'@[$.OK]
         || ''
         ?}+
         {? .ALL<>.JEST+.OK
         || '\nLiczba niewysłanych dokumentów: %1'@[$(.ALL-.OK-.JEST)]
         || ''
         ?}
      )
   ";
   1
?}


\ag_akc_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Po akcji grupowej wysłania do OCR
::----------------------------------------------------------------------------------------------------------------------
GrSend.info();
VAR_DEL.delete('GrSend')


\akc_ver
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Weryfikacja dokumentu
::   WE: [_a] - zakończyć proces? [1]-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_end:={? var_pres('_a')>0 || _a || 1 ?};
{? SKAN_DOK.STATUS='R'
|| FUN.info('Dokument nie był wysyłany.')
|? 'SVG'*SKAN_DOK.STATUS=0
|| FUN.info('Dokument jest już zweryfikowany.')
|? exec('correct','skanuj') & FUN.ask('Czy zweryfikować dokument?')
|| exec('ver_doc','skanuj');
   {? SKAN_DOK.STATUS='V' & SKAN_DOK.TYP<>'' & 'dfa'*SKAN_DOK.TYP
   || exec('akc_get','zso_scn',_end,0)
   ?}
?}


\akc_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Pobranie danych dokumentu z OCR
::   WE: [_a] - zakończyć proces? [1]-tak 0-nie
::       [_b] - czy pytać o pobranie danych dokumentu: [1]-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_grp:=var_pres('GrSD')>0;
_end:={? var_pres('_a')>0 || _a || 1 ?};
_ask:={? var_pres('_b')>0 || _b || 1 ?};
{? SKAN_DOK.STATUS='R'
|| {? _grp
   || GrSD.NOT+=1
   || FUN.info('Dokument nie był wysyłany.')
   ?}
|? SKAN_DOK.STATUS='S'
|| {? _grp
   || GrSD.NOTVER+=1
   || FUN.info('Dokument nie był zweryfikowany.')
   ?}
|? 'VG'*SKAN_DOK.STATUS=0
|| {? _grp
   || GrSD.JEST+=1
   || FUN.info('Dokument jest już pobrany.')
   ?}
|? exec('correct','skanuj') & (_grp | ~_ask | FUN.ask('Czy pobrać dane dokumentu?'))
|| _obj:=exec('get_doc','skanuj');
   {? _obj.file<>''
   || {? _obj.state=2
      || SKAN_DOK.STATUS:='D'
      ?};
      {? SKAN_DOK.bl_put('PLIK_OCR',_obj.file,1)
      || SKAN_DOK.TODO:='D';
         exec('set_nag','skanuj');
         {? SKAN_DOK.put()
         || ferase(_obj.file,1);
            {? exec('del_doc','skanuj')
            || SKAN_DOK.TODO:='';
               SKAN_DOK.put()
            ?};
            {? _end
            || _params:=exec('mp_run_a','#b__box');
               _params.ACT_UID:='ZSO_SCN_OCRD';
               _params.AKCJA:='Zakończ';
               _params.UIDREF:=SKAN_DOK.uidref();
               _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
               exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SKAN_DOK',SKAN_DOK.ref());
               _params.PROC_START:='N';
               exec('mp_run','#b__box',_params)
            ?};
            {? _grp
            || GrSD.OK+=1
            ?}
         ?}
      ?}
   |? ~_grp
   || {? _obj.msg<>''
      || FUN.info('Informacja ze skanuj.to:\n%1'@[_obj.msg])
      || FUN.info('Nie udało się pobranie danych dokumentu od skanuj.to.')
      ?}
   ?}
?};
~~

\bg_akc_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Przed akcją grupowego pobierania dokumentów z OCR
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Pobrać dane zaznaczonych dokumentów?'@)
|| VAR_DEL.delete('GrSD');
   GrSD:=exec('obj_gr_get','skanuj');
   GrSD.ALL:=SKAN_DOK.sel_size();
   1
?}


\ag_akc_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Po akcji grupowego pobierania danych dokumnetu z OCR
::----------------------------------------------------------------------------------------------------------------------
GrSD.info();
VAR_DEL.delete('GrSD')


\br_skan_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Rekord przed SKAN_DOK
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
{? SKAN_DOK.sel_size()
|| ~~
|? SKAN_DOK.STATUS='R'
|| _gray+='WPT'
|? SKAN_DOK.STATUS='S'
|| _gray+='YPT'
|? SKAN_DOK.STATUS='G'
|| _gray+='YT'
|? SKAN_DOK.STATUS='V'
|| _gray+='YT'
|? SKAN_DOK.STATUS='D'
|| _gray+='YWP'+{? -SKAN_DOK.TYP='d' || 'T(F)' |? -SKAN_DOK.TYP='f' || 'T(D)' || '' ?}
|? SKAN_DOK.STATUS='Z'
|| _gray+='YWPTR'
?};
{? SKAN_DOK.PLIK_OCR=null
|| _gray+='GR'
?};
SKAN_DOK.actions_grayed(SKAN_DOK.win_sel('?'),_gray);
~~


\akc_view_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Podgląd danych z OCR
::----------------------------------------------------------------------------------------------------------------------
{? SKAN_DOK.PLIK_OCR=null
|| FUN.info('Dane nie zostały jeszcze pobrane.')
|| exec('show_dok','zso_scn')
?}


\show_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Prezentuje dane pochodzące z OCR-a
::----------------------------------------------------------------------------------------------------------------------
_obj:=exec('view_tab','skanuj');
exec('view_parse','skanuj',_obj);
_obj.select()


\akc_utw_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Akcja tworzenia dokumentu księgowego z OCR
::----------------------------------------------------------------------------------------------------------------------
_grp:=var_pres('GrSD')>0;
{? SKAN_DOK.STATUS='Z'
|| {? _grp
   || GrSD.JEST+=1
   || FUN.info('Dokument jest już zadekretowany.')
   ?}
|? SKAN_DOK.STATUS<>'D'
|| {? _grp
   || 1
   || FUN.info('Status uniemożliwia utworzenie dokumentu.')
   ?}
|? _grp
|| {? -SKAN_DOK.TYP='d' | SKAN_DOK.TYP=''
   || _ref:=SKAN_DOK.ref();
      _uid:=SKAN_DOK.uidref();
      _dok:=exec('add_dok','skanuj',1);
      {? _dok<>null
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:='ZSO_SCN_TDOK';
         _params.AKCJA:='Grupowa';
         _params.UIDREF:=_uid;
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SKAN_DOK',_ref);
         _params.PROC_START:='N';
         exec('mp_run','#b__box',_params);
         GrSD.OK+=1
      ?}
   || GrSD.INNY+=1
   ?}
|? FUN.ask('Czy utworzyć dokument księgowy?')
|| _ref:=SKAN_DOK.ref();
   SKAN_DOK.cntx_psh();
   _next:={? SKAN_DOK.next() | SKAN_DOK.prev() || SKAN_DOK.ref() || null ?};
   SKAN_DOK.cntx_pop();
   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='ZSO_SCN_TDOK';
   _params.AKCJA:='Dokument księgowy';
   _params.UIDREF:=SKAN_DOK.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SKAN_DOK',SKAN_DOK.ref());
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params);
   {? ~SKAN_DOK.seek(_ref) & _next<>null
   || SKAN_DOK.seek(_next)
   ?}
?}


\bg_akc_utw_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Przed akcją grupową tworzenia dokumentów księgowych
::   WE: _a - typ dokumentu: 1-dokument księgowy 2-faktura w obiegu
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('GrSD');
GrSD:=exec('obj_gr','skanuj');
{? GrSD.edit(_a)
|| GrSD.save();
   GrSD.ALL:=SKAN_DOK.sel_size();
   1
|| VAR_DEL.delete('GrSD');
   0
?}


\ag_akc_utw_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Po akcji grupowej tworzenia dokumentów księgowych
::----------------------------------------------------------------------------------------------------------------------
GrSD.info();
VAR_DEL.delete('GrSD')


\zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Akcja zmiany zakresu
::   WE: [_a] - zakres
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>0
|| POMOC.ZAKRES:=_a
|| POMOC.win_edit('ZAKRES');
   _zakres:=POMOC.ZAKRES;
   {? ~POMOC.edit()
   || POMOC.ZAKRES:=_zakres
   ?}
?};
{? POMOC.ZAKRES='W'
|| SKAN_DOK.index('FIRMA'); SKAN_DOK.prefix(REF.FIRMA)
|| SKAN_DOK.index('AKTYWNY'); SKAN_DOK.prefix(REF.FIRMA,POMOC.ZAKRES)
?};
SKAN_DOK.hdr_sel();
SKAN_DOK.hdr_sel(
   'Zeskanowane dokumenty - '+
   {? POMOC.ZAKRES='W' || 'Wszystkie'
   |? POMOC.ZAKRES='T' || 'Niezadekretowane'
   |? POMOC.ZAKRES='N' || 'Zadekretowane'
   || 'Inne'
   ?}
)


\akc_utw_edok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Akcja utworzenia faktury w obiegu
::----------------------------------------------------------------------------------------------------------------------
_grp:=var_pres('GrSD')>0;
{? SKAN_DOK.STATUS='Z'
|| {? _grp
   || GrSD.JEST+=1
   || FUN.info('Dokument jest już zadekretowany.')
   ?}
|? SKAN_DOK.STATUS<>'D'
|| {? _grp
   || 1
   || FUN.info('Status uniemożliwia utworzenie dokumentu.')
   ?}
|? _grp
|| {? -SKAN_DOK.TYP='f' | SKAN_DOK.TYP=''
   || _ref:=SKAN_DOK.ref();
      _uid:=SKAN_DOK.uidref();
      _context:=obj_new('EDOKUM');
      {? (_context.EDOKUM:=exec('add_dok','skanuj',2))<>null
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:='ZSO_SCN_TEDO';
         _params.AKCJA:='Grupowa';
         _params.UIDREF:=_uid;
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SKAN_DOK',_ref);
         _params.CONTEXT:=_context;
         _params.PROC_START:='N';
         exec('mp_run','#b__box',_params);
         GrSD.OK+=1
      ?}
   || GrSD.INNY+=1
   ?}
|? FUN.ask('Czy utworzyć fakturę w obiegu?')
|| _ref:=SKAN_DOK.ref();
   SKAN_DOK.cntx_psh();
   _next:={? SKAN_DOK.next() | SKAN_DOK.prev() || SKAN_DOK.ref() || null ?};
   SKAN_DOK.cntx_pop();
   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='ZSO_SCN_TEDO';
   _params.AKCJA:='Faktura';
   _params.UIDREF:=SKAN_DOK.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SKAN_DOK',SKAN_DOK.ref());
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params);
   {? ~SKAN_DOK.seek(_ref) & _next<>null
   || SKAN_DOK.seek(_next)
   ?}
?}


\akc_pass
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Akcja zmiany hasła do API skanuj.to
::----------------------------------------------------------------------------------------------------------------------
exec('auth_edit','skanuj')


\akc_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Akcja prezentująca załączniki
::----------------------------------------------------------------------------------------------------------------------
SKAN_DOK.cntx_psh();
_grp:=SKAN_DOK.grp_make('Załączniki do zeskanowanego dokumentu'@,,'grp_dok_zal');
VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB','BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND');
__CTR.BTAB:='DOKUM';
__CTR.TAB:='SKAN_DOK';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','srsr_zal','dok_dokum_ctr',':');
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('dir_sep','srsr_zal');
__CTR.DND:=1;
_bf:=$('exec(\'bobs_dokum\',\'srsr_zal\',SKAN_DOK.STATUS=\'Z\')');
_fml:=$('exec(\'dokum_rfr\',\'srsr_zal\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
SKAN_DOK.grp_ctr(_grp,DOKUM,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,_fml,,,,,_bf,,,'maximized');
SKAN_DOK.win_sel(_grp);
SKAN_DOK.select();
SKAN_DOK.cntx_pop()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:27 24e1ead5a4108d65c31e3eda193a5717a18b0274621c0f3f49a368d13d3a15819662c6318bfb9f6bb701f859e899578f7add345290cfbb25374251d4e9ab869d9657e4bd000c91592cc7f89ffea8669955131d0b73be1c1e8b8d060bc173867b88d5311b222614d6c156c9ee19af1672efe195f5f81ae3ebf73118650c7f1241

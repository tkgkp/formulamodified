:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: dok_fks1.fml
:: Utworzony: 13.10.2017 [17.42]
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły do obsługi dokumentów księgowych
::======================================================================================================================


\wyw_for
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.10]
:: OPIS: Uruchomienie formul kontrolujacych
::   WE: _a - (1/2/3) ktora formule wywolac (POCZ/POZ/KON)
::       _b - czy wyswietlac komunikaty
::       _c - miejsce wywołania
::   WY: 0/1
::  OLD: \wyw_for/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
FOR_AKC.index('AKC');
FOR_AKC.prefix(DOK.REJ,_c);
_ok:=1;
KOMUNIKAT:=_b;
{? FOR_AKC.first()
|| {!
   |? {? _a=1
      ||
::      statystyki użycia - start
        exec('stat_add','st_common','A_FOR_AKC',FOR_AKC.NAZ)
      ?};
      {? _a=1 & FOR_AKC.POCZ<>null || _ok:=($(FOR_AKC.POCZ().FORMULA))()
      |? _a=2 & FOR_AKC.POZ<>null || _ok:=($(FOR_AKC.POZ().FORMULA))()
      |? _a=3 & FOR_AKC.KON<>null || _ok:=($(FOR_AKC.KON().FORMULA))()
      ?};
      {? _a=3 | _ok=0
      ||
::      statystyki użycia - stopo
        exec('stat_add','st_common','A_FOR_AKC_E',FOR_AKC.NAZ)
      ?};
      _ok & FOR_AKC.next()
   !}
?};
&KOMUNIKAT;
_ok


\spr_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.10]
:: OPIS: Wywolanie formul kontrolujacej
::   WE: _a - wyswietlac komunikaty
::       _b - miejsce wywołania
::   WY: 0/1
::  OLD: \spr_dok/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=exec('wyw_for','dok_fks1',1,_a,_b);
{? _ok
|| POZ.index('DOK'); POZ.prefix(DOK.ref());
   {? POZ.first()
   || {! |?
         _ok:=exec('wyw_for','dok_fks1',2,_a,_b);
         _ok & POZ.next
      !}
   ?};
   {? _ok || _ok:=exec('wyw_for','dok_fks1',3,_a,_b) ?}
?};
_ok


\spr_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Formula sprawdza, czy pozycja dokumentu zrodlowego zostala prawidlowo wypelniona.
::   WE: _a - kod typu dokumentu (DOK.DOK_REJ().SLO().KOD)
::       _b -  1 = wywolanie przy akceptacji dokumentu
::             2 = wywolanie przy odtwarzaniu
::  OLD: \spr_pop/dok_zrd.fml
::----------------------------------------------------------------------------------------------------------------------
_w:='';
KS.cntx_psh(); KS.prefix();
KOM.cntx_psh(); KOM.prefix();
_rodz:=POZ.KOM().KS().ROZR;  _ob_typ:=KS.OBTYPROZ; _ks_typ:=KS.T_ROZ;
{? _a<>'BO' & (-_rodz='r' | (-_rodz='p' & POZ.ID<>''))
|| {? form(POZ.ID)=''
   || _w:='r'
   |? POZ.TID<>'NAL' & POZ.TID<>'ZOB' & POZ.TID<>'RMK' & POZ.TID<>'INN' &
      POZ.TID<>'NOD' & POZ.TID<>'ZOD' & POZ.TID<>'RMP'
   || _w:='t'
   |? POZ.TID=''
   || _w:='typ rozrachunku'
   |? POZ.DO=date(0,0,0)
   || _w:='data otwarcia'
   |? (DOK.DOK_REJ().PR='T' & exec('drb_tpfp','raty','DOK',1)=date(0,0,0))
      | (POZ.TP=date(0,0,0) & DOK.DOK_REJ().PR<>'T')
   || _w:='termin płatności'
   |? DOK.DOK_REJ().CZY_ROZL='T' & POZ.DATA_R=date(0,0,0)
   || _w:='data rozliczenia'
   |? POZ.ODD=null
   || _w:='jednostka księgowa rozrachunku'
   |? -DOK.DOK_REJ().ODD_ROZR='n' & DOK.ODD<>POZ.ODD
   || _w:='dok_poz_odd'
   ?}
?};
{? (-_rodz='z' | _a='BO')
|| {? POZ.ODD<>null
   || _w:='poz_odd'
   |? form(POZ.ID)<>''
   || _w:='z'
   |? POZ.DO<>date(0,0,0)
   || _w:='do'
   |? POZ.TP<>date(0,0,0)
   || _w:='tp'
   |? POZ.DATA_R<>date(0,0,0)
   || _w:='data_r'
   ?}
?};
{? _w=''
|| {? POZ.KOM=0 || _w:='Komentarz'
   |? POZ.STR<>'Wn' & POZ.STR<>'Ma' || _w:='Strona'
   ?}
?};
{? _w='' & _a<>'BO' &(-_rodz='r' | (-_rodz='p' & POZ.ID<>'')) &
   _ob_typ & POZ.TID<>_ks_typ
|| {? _b=1
   || {? 2+POZ.TID<>'RM' & 2+_ks_typ<>'RM'
      || POZ.TID:=_ks_typ
      || _w:='b'
      ?}
   || _w:={? 2+POZ.TID<>'RM' & 2+_ks_typ<>'RM'
          || {? var_pres('typ_roz')>0 || typ_roz:=_ks_typ ?};
             'c'
          || 'b'
          ?}
   ?}
?};
KOM.cntx_pop(); KS.cntx_pop();
_w


\spr_nab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Sprawdzenie, czy dokument ma grupę ZInwPodZ jesli posiada nabycia
::  OLD: \spr_nab/vat2.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_SR.index('DOK_N'); VAT_SR.prefix(REF.FIRMA,SSTALE.AO,DOK.ref);
{? VAT_SR.first()
|| exec('spr_vatgr','dok_fks1')
|| 1
?}


\akc_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [12.30]
:: OPIS: Sprawdza spelnienie warunkow do akceptacji bez wykonywania samej operacji - dla zbiorczej informacji o problemach
::   WE: _a = 1: pojedynczy dok.; 2: wszystkie dokumenty
::       _b = typ DOK.DOK_REJ().SLO().KOD
::  OLD: \akc_chk/dok_zrd2.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| exec('KOMM','#object');
   KOMM.init(,,'Uwagi obsługi struktury rodzajowej')
?};
_typ:=_b;
_wn:=_ma:=0;
_akcept:=1;
_dok:='Dokument: %1; rejestr: %2'@[DOK.NK,DOK.REJ().KOD];
      {? DOK.NK=''
      || _akcept:=0;
         _kom:='brak symbolu dokumentu.'@;
         KOMM.add(_dok+' - '+_kom)
      ?};
      {? DOK.ODD=null
      || _akcept:=0;
         _kom:='dokument nie jest związany z żadną jednostką księgową.'@;
         KOMM.add(_dok+' - '+_kom)
      ?};
      {? DOK.ODD<>DOK.REJ().ODD
      || _akcept:=0;
         _kom:='jednostka księgowa rejestru jest inna niż jednostka księgowa dokumentu.'@;
         KOMM.add(_dok+' - '+_kom)
      ?};
      {? DOK.DOK_REJ
      || _vat:=(_typ='VAT' | _typ='SAD');
         VPOZ.prefix(DOK.ref());
         _vpoz:=VPOZ.first();_zer:=0
      || _akcept:=0;
         _kom:='niewypełnione pole rodzaju dokumentu.'@;
         KOMM.add(_dok+' - '+_kom)
      ?};
      {? _vat=0 & _vpoz
      || _akcept:=0;
         _kom:='niewłaściwy rodzaj dokumentu. Istnieją pozycje VAT - \nrodzaj dokumentu powinien być VAT lub SAD.'@;
         KOMM.add(_dok+' - '+_kom)
      |? _vpoz & DOK.RVAT=null
      || _akcept:=0;
         _kom:='niewypełnione pole rejestru VAT mimo istnienia pozycji VAT.'@;
         KOMM.add(_dok+' - '+_kom)
      |? DOK.RVAT & ~_vat
      || _akcept:=0;
         _kom:='wypełniono rejestr VAT dla rodzaju dokumentu innego niż VAT i SAD.'@;
         KOMM.add(_dok+' - '+_kom)
      |? _vat & DOK.D3=date(0,0,0)
      || _akcept:=0;
         _kom:='niewypełnione pole \'Termin płat.\' dokumentu.'@;
         KOMM.add(_dok+' - '+_kom)
      |? _vat=0 & _vpoz=0
      || _zer:=1
      ?};
      {? _vat
      || {? ~(_vpoz)
         || _kom:='brak pozycji VAT w dokumencie.'@;
            KOMM.add(_dok+' - '+_kom)
         ?};
         {? (DOK.KRAJ=null | DOK.JORG=null)
         || _kom:='brak wypełnienia pól: Kraj opodatkowania, Waluta opodatkowania. '@;
            KOMM.add(_dok+' - '+_kom);
            _akcept:=0
         ?}
      ?};
      {? (DOK.DTW<SSTALE.AO().POCZ | DOK.DTW>SSTALE.AO().KON) & SSTALE.AO().KON<>date(0,0,0)
      || _akcept:=0;
         _kom:='data zapisu (księgowania) spoza aktywnego okresu.'@;
         KOMM.add(_dok+' - '+_kom)
      ?};
      {? DOK.DOP=date(0,0,0)
      || _akcept:=0;
         _kom:='niewypełnione pole data operacji.'@;
         KOMM.add(_dok+' - '+_kom)
      ?};
      {? DOK.DTO=date(0,0,0)
      || _akcept:=0;
         _kom:='niewypełnione pole data dokumentu.'@;
         KOMM.add(_dok+' - '+_kom)
      ?};
      {? _vat & ~exec('spr_nab','dok_fks1')
      || _akcept:=0;
         _kom:='z dokumentem są powiązane nabycia środków trwałych. '
               'Brak kwoty VAT dla grupy ZInwPodZ lub ZInwPodS w pozycjach VAT dokumentu.'@;
         KOMM.add(_dok+' - '+_kom)
      ?};
      {? _vat & DOK.OKRVAT<>null & DOK.OKRVAT().POCZ=date(0,0,0)
      || _akcept:=0;
         _kom:='okresem rozliczenia VAT nie może być '
               '"Bilans otwarcia" i "Bilans zamknięcia".'@;
         KOMM.add(_dok+' - '+_kom)
      ?};
      {? POZ.first()
      || {! |?
            {? POZ.KON=''
            || _akcept:=0;
               _kom:='pozycja %1 niewypełnione pole konto.'@[$POZ.POZ];
               KOMM.add(_dok+' - '+_kom)
            ?};
            {? (POZ.KOM=null | (|POZ.KOM().KS().SYM<>(BILANSP.ASYNT+POZ.KON)))
            || _k:=exec('koment','konto',POZ.KON);
               {? ~_k
               || _kom:='pozycja %1. Nie znaleziono komentarza konta księgowego %2.'@[$POZ.POZ,POZ.KON];
                  KOMM.add(_dok+' - '+_kom);
                  _akcept:=0
               ?}
            ?};
            {? POZ.WAL<>null & POZ.KOM().KS().WIELO='N'
            || _akcept:=0;
               _kom:='pozycja %1. Konto nie jest wielowalutowe.'@[$POZ.POZ];
               KOMM.add(_dok+' - '+_kom)
            ?};
::Poczatek modyfikacji dla Maclex Fiks 12-10-2009 AK [1120]
            KOMT:='';
            {? MLEX.FIKSB
            || {? ~exec('mlbzaddod','ml_zad',,,1,0)
               || _akcept:=0;
                  _kom:={? var_pres('KOMT')
                        || KOMT
                        || 'pozycja %1 Błąd w klasyfikacji budżetu zadaniowego.'@[$POZ.POZ]
                        ?};
                  KOMM.add(_dok+' - '+_kom)
               ?};
               {? ~exec('pozbdod','mlxstan',,,1,0)
               || _akcept:=0;
                  _kom:={? var_pres('KOMT')
                        || KOMT
                        || 'pozycja %1 Wystąpił wyjątek w klasyfikacji budżetu rodzajowego.'@[$POZ.POZ]
                        ?};
                  KOMM.add(_dok+' - '+_kom)
               ?}
            ?};
::Koniec modyfikacji dla Maclex
            {? POZ.WAL<>null
            || {? _typ<>'BO' & (POZ.SUMW$2=0 | POZ.KURS=0)
               || _akcept:=0;
                  _kom:='pozycja %1. Brak informacji walutowych.'@[$POZ.POZ];
                  KOMM.add(_dok+' - '+_kom)
               ?}
            ?};
            {? exec('akc_kont','edkonto',POZ.KON)
            || _akcept:=0;
               {? POZ.KON<>''
               || _kom:='pozycja %1. Niepoprawne konto księgowe %2.'@[$POZ.POZ,POZ.KON]
               || _kom:='pozycja %1. Niepoprawne konto księgowe.'@[$POZ.POZ]
               ?};
               KOMM.add(_dok+' - '+_kom)
            ?};
            {? -exec('akc_zabl','konto',POZ.KON)='t'
            || _akcept:=0;
               _kom:='pozycja %1 zablokowane konto księgowe %2.'@[$POZ.POZ,POZ.KON];
               KOMM.add(_dok+' - '+_kom)
            ?};
            {? POZ.KON*' '
            || _akcept:=0;
               {? POZ.KON<>''
               ||  _kom:='pozycja %1. Konto księgowe ''%2'' zawiera zabroniony znak spacji.'@[$POZ.POZ,POZ.KON]
               || _kom:='pozycja %1. Konto księgowe zawiera zabroniony znak spacji.'@[$POZ.POZ]
               ?};
               KOMM.add(_dok+' - '+_kom)
            ?};
            _p:=exec('spr_pop','dok_fks1',_typ,1);
            {? _p<>''
            || _akcept:=0;
               {? _p='r'
               || _kom:='pozycja %1. Brak identyfikatora rozrachunku.'@[$POZ.POZ]
               |? _p='t'
               || _kom:='pozycja %1. Błędny typ identyfikatora rozrachunku.'@[$POZ.POZ]
               |? _p='poz_odd'
               || _kom:='pozycja %1. Niepotrzebna jednostka księgowa rozrachunku.'@[$POZ.POZ]
               |? _p='z'
               || _kom:='pozycja %1. Niepotrzebny identyfikator rozrachunku.'@[$POZ.POZ]
               |? _p='do'
               || _kom:='pozycja %1. Niepotrzebna data otwarcia rozrachunku.'@[$POZ.POZ]
               |? _p='tp'
               || _kom:='pozycja %1. Niepotrzebny termin płatności rozrachunku.'@[$POZ.POZ]
               |? _p='data_r'
               || _kom:='pozycja %1. Niepotrzebna data rozliczenia.'@[$POZ.POZ]
               |? _p='b'
               || _kom:='pozycja %1. Identyfikator rozrachunku niezgodny z wymaganym na koncie syntetycznym.'@[$POZ.POZ]
               |? _p='dok_poz_odd'
               || _kom:='pozycja %1. Jednostka księgowa rozrachunku inna niż w dokumencie.'@[$POZ.POZ]
               || _kom:='pozycja %1. Niewypełnione pole %2.'@[$POZ.POZ,form(_p)]
               ?};
               KOMM.add(_dok+' - '+_kom)
            ?};
            {?  +|POZ.ID
            || {? PAR_SKID.get(38)='N'
               || _v:=exec('czyLista','raty',POZ,1);
                  {? (~_v & (POZ.DO>POZ.TP))
                     | (_v & (POZ.DO>exec('drb_tpfp','raty','POZ',1)))
                  || _akcept:=0;
                     _kom:='pozycja %1'
                           'Data otwarcia rozrachunku nie może być późniejsza od terminu płatności.'@[$POZ.POZ];
                     KOMM.add(_dok+' - '+_kom)
                  ?};
                  {? _v & ((POZ.WAL & POZ.SUMW<>exec('wartPlat','raty','WAR',,POZ,POZ.ref())) |
                           (~POZ.WAL & POZ.SUM<>exec('wartPlat','raty','WAR',,POZ,POZ.ref())))
                  || _kom:='pozycja %1. Lista rat niezgodna z kwotą dekretu pozycji '@[$POZ.POZ];
                     KOMM.add(_dok+' - '+_kom);
                     _akcept:=0
                  ?}
               ?};
               {? POZ.SLO_PROJ<>null & (POZ.TID='ZOB' & -POZ.STR='wn' | POZ.TID='NAL' & -POZ.STR='ma')
               || _akcept:=0;
                  _kom:='pozycja %1. Zbędnie wypełnione pole Projekt w zapisie rozliczającym rozrachunek projektowy'@[$POZ.POZ];
                  KOMM.add(_dok+' - '+_kom)
               ?};
               {? POZ.SLO_PROJ<>null & XINFO.SLU_PROJ=null
               || _akcept:=0;
                  _kom:='pozycja %1. Zbędnie wypełnione pole Projekt gdy nie podano słownika projektów'@[$POZ.POZ];
                  KOMM.add(_dok+' - '+_kom)
               |? XINFO.SLU_PROJ<>null & POZ.SLO_PROJ<>null
               || SLO.index('SL'); SLO.prefix(XINFO.SLU_PROJ);
                  {? ref_name(POZ.SLO_PROJ)<>SLO.name() | ~SLO.seek(POZ.SLO_PROJ)
                  || _akcept:=0;
                     _kom:='pozycja %1. Nieprawidłowo wypełnione pole Projekt'
                           ' Wartość nie pochodzi ze słownika projektów.'@[$POZ.POZ];
                     KOMM.add(_dok+' - '+_kom)
                  ?}
               ?}
            ?};
            {? +|POZ.ID & POZ.WAL<>null
            || {? exec('tstwal','dok_fks_ks',POZ.WAL)
               || _akcept:=0;
                  _kom:='pozycja %1. Istnieje rozrachunek o podanym symbolu otwarty w roku %2'
                        ' w innej walucie obcej.'@[$POZ.POZ,$(POZ.DO~1)];
                  KOMM.add(_dok+' - '+_kom)
               ?};
               {? exec('tstwalop','dok_fks',POZ.WAL,_a)
               || _kom:='pozycja %1. Rozrachunek o podanym identyfikatorze otwarty w roku %2'
                        ' istnieje już dla innej waluty w bieżącym dokumencie.'@[$POZ.POZ,(POZ.SYM_ROK+4)];
                  KOMM.add(_dok+' - '+_kom);
                  _akcept:=0
               ?}
            ?};
            {? +|POZ.ID & (-POZ.TID='rmk' | -POZ.TID='rmp')
            || {? RMK.index('NAZ'); RMK.prefix(); RMK.find_key(POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK)
               || {? RMK.KONZ='<- F3 ->' & (RRMK.prefix(RMK.ref()); ~RRMK.first())
                  || _kom:='pozycja %1. Rozrachunek typu \'R\' - RMK z wyróżnikiem.\nBrak rozpisanego wyróżnika.'@[$POZ.POZ];
                     KOMM.add(_dok+' - '+_kom);
                     _akcept:=0
                  ?}
               || _kom:='pozycja '+$POZ.POZ+'. Rozrachunek typu \'R\' - nie podano RMK.';
                  KOMM.add(_dok+' - '+_kom);
                  _akcept:=0
               ?}
            ?};
            {? exec('czyLista','raty',POZ) & exec('e_opdrb','rozrach',0)
            || _kom:='pozycja %1. Dokument %2'
                     ' Dla rozrachunku zdefiniowano już listę płatności.'@[$POZ.POZ,DOK.NK];
               KOMM.add(_dok+' - '+_kom);
               _akcept:=0
            ?};
            {? SSTALE.AO().NAZ<>'Bilans otwarcia'
            || _kom:=exec('spr_wyr_w','dok_fks','',_a);
               {? _kom<>''
               || _akcept:=0;
                  KOMM.add(_dok+' - '+_kom)
               ?}
            ?};
            {? -(1+POZ.KOM().KS().TYP)<>'p'
            || {? -(~POZ.STR)='wn' || _wn+=POZ.SUM$2 || _ma+=POZ.SUM$2 ?}
            ?};
            {? DOK.DOK_REJ().M_ODD<>'T' & var_pres('__dOkWtU')<=0
            || 1
            |? ~POZ.DOK_MOD | ~POZ.POZ_MOD | ~POZ.ODD_KS
            || _kom:='pozycja %1Dokument %2'
                     ' Niewypełnione pola związane z dokumentem dla wielu jednostek księgowych.'@[$POZ.POZ,DOK.NK];
               KOMM.add(_dok+' - '+_kom);
               _akcept:=0
            ?};
            POZ.next()
         !};
         {? _vat
         || VPOZ.cntx_psh();
            _akc:=1;
            {? VPOZ.first()
            || {! |?
                  _akc:=~(VPOZ.GRVAT=null);
                  _akc & VPOZ.next()
               !}
            ?};
            {? ~_akc
            || _kom:='niewypełniona grupa podatkowa w pozycji VAT.'@;
               KOMM.add(_dok+' - '+_kom);
               _akcept:=0
            ?};
            VPOZ.cntx_pop()
         ?};
         _f2wyl:=DOK.REJ().ROK().FIRMA().TYP='W';
         {? DOK.DOK_REJ().SLO().KOD<>'BO' & _wn$2<>_ma$2 & ~exec('spr_mod','dok_fks') & ~_f2wyl
         || _kom:='dokument jest niezbilansowany na kwotę %1.'@[$fabs((_wn-_ma)$2)];
            KOMM.add(_dok+' - '+_kom);
            _akcept:=0
         ?};
         1
      || _kom:='brak pozycji w dokumencie.'@;
         KOMM.add(_dok+' - '+_kom);
         _akcept:=0
      ?};
{? _a & ~KOMM.empty() || KOMM.select() ?};
_akcept


\ust_tp1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009+]
:: OPIS: Ustawia termin platnosci rozrachunku
::  OLD: \ust_tp1/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZ.TP=date(0,0,0)
|| {? DOK.RVAT |
      ((DOK.DOK_REJ().SLO().KOD='PROSTY' | DOK_REJ.SLO().KOD='WEW') & (DOK_REJ.CZY_DP='T' | DOK_REJ.CZY_DP='O') & DOK.D3<>date(0,0,0))
   || POZ.TP:=DOK.D3
   || {? DOK.DTO<>date(0,0,0) || POZ.TP:=DOK.DTO+FINFO.OKRROZ ?}
   ?}
?}


\del_dokel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2006]
:: OPIS: Usuwanie/zapisywanie dokumentu elektronicznego podczas usuwania DOK-a
::  OLD: \del_dok/efaktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK.sel_size() | var_pres('modd_kom')>0
|| _ok:=1
|| _ok:=FUN.choice('Z dokumentem: %1 , związany jest dokument elektroniczny.\n'
                   'Usunięcie dokumentu księgowego spowoduje usunięcie dokumentu elektronicznego.\n'
                   'Usunąć dokument księgowy z dokumentem elektronicznym czy \n'
                   'zapisać dokument elektroniczny przed usunięciem dokumentu księgowego?'@[DOK.NK],,
                   'Usunąć dokument elektroniczny'@,'Zapisać dokument elektroniczny'@)
?};
{? _ok=2
|| _ok:=exec('saveedok','dok_fks')
?}; _ok


\dok_usun_ml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Pawelm [1030]
:: OPIS: Funkcja wywoływana przy usuwaniu dokumentu DOK, ustawia status w TERDOK
::  OLD: \dok_usun/ml_teren.fml
::----------------------------------------------------------------------------------------------------------------------
TERDOK.cntx_psh();
TERDOK.index('FIKS');
TERDOK.prefix();
{? TERDOK.find_key('T',DOK.name(0),#DOK.ref())
|| TERDOK.STATUS:='N';
   TERDOK.FIKS_M:='';
   TERDOK.FIKS:=0;
   TERDOK.put(1)
?};
TERDOK.cntx_pop();
~~


\dok_spr_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Sprawdza, czy jest dokument wtorny bez pierwotnego
::  OLD: \dok_spr_wt/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
DOK.cntx_psh();
{? type_of(ref_tab(DOK.DOKZRODL))<>0 & ref_tab(DOK.DOKZRODL)=DOK & +DOK.DOKZRODL=16
|| DOK.use(8+DOK.DOKZRODL); DOK.prefix();
   _ret:=~DOK.seek(BB.sqlint(8-DOK.DOKZRODL),8+DOK.DOKZRODL)
?};
DOK.cntx_pop();
_ret


\del_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MGR [2011]
:: OPIS: Usuniecie dokumentow wtornych do dokumentu dla wielu jednostek ksiegowych
::   WY: 1 - powodzenie, 0 - niepowodzenie
::  OLD: \del_wt/akcept.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
DOK.cntx_psh();
_refsql:=$DOK.ref();
DOK.clear();
DOK.index('DOKZRODL');
DOK.prefix(_refsql,_refsql);
{? DOK.first()
|| _dw:=obj_new(DOK.size());
   {! _i:=1 .. DOK.size() |!
      _dw[_i]:=$DOK.ref();
      DOK.next()
   !};
   {! _i:=1 .. DOK.size() |!
      {? DOK.seek(BB.sqlint(8-_dw[_i]),8+_dw[_i])
      || modd_kom:=0;
         exec('dok_usun','dok_fks1');
         VAR_DEL.delete('modd_kom')
      ?}
   !};
   obj_del(_dw)
?};
DOK.cntx_pop();
_ret


\dok_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Funkcja usuwa dokument zrodlowy i ewentualnie dokumenty dla wielu jednostek ksiegowych
::  OLD: \dok_usun/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
_listypl:=" _wyn:=1;
           O.cntx_psh();
           O.prefix();
           {? O.find_tab(,'DOK',,'=',_a)
           || O.DOK:='';
              _wyn:=O.put(1)
           ?};
           O.cntx_pop();
           _wyn
          ";
_sel:=DOK.sel_size() | var_pres('DokWyk')>0 & DokWyk;
_dok_mod:=exec('spr_mod','dok_fks');
_a_przen:={? var_press('a_przen')>0 & a_przen=1 || 1 || 0 ?};
_error:=''; _CzyTezKor:='';
::Poczatek modyfikacji MacLex 08-25-2010 WS [2011]
{? 3+DOK.DOKZRODL='krr' & ~FUN.ask('Dokumentu nie będzie można ponownie zaksięgować automatycznie. Kontynuować?'@)
|| return(0)
?};
{? 4+DOK.KN='doku' & DOK.DOK_REJ().KOR_NAG<>'T'
|| DOK.cntx_psh(); ROZNE.cntx_psh(); OKRO_F.cntx_psh(); ROK_F.cntx_psh();
   _ref:=BB.sqlint(DOK.KN);
   DOK.use(8+DOK.KN);
   DOK.prefix();
   {? _ref<>0 & DOK.seek(_ref,)
   || _okr:={? exec('szuk_okr','okresy',DOK.DTW) || ROZNE.UT_OKROD().NAZ+' '+OKRO_F.ROK().NAZ || '' ?};
      FUN.info('Do dokumentu została wystawiona w okresie %1 \nkorekta nagłówkowa o symbolu %2\nDokumentu nie można usunąć.'@[_okr, DOK.NK]);
      DOK.cntx_pop(); ROZNE.cntx_pop(); OKRO_F.cntx_pop(); ROK_F.cntx_pop();
      return(0)
   ?};
   DOK.cntx_pop(); ROZNE.cntx_pop(); OKRO_F.cntx_pop(); ROK_F.cntx_pop()
?};
::Koniec modyfikacji MacLex
{? -(7+DOK.DOKZRODL<>'ser_nag') & -(7+DOK.DOKZRODL<>'par_nag') & DOK.EDOKUM='' & 8+DOK.DOKZRODL<>'skid_hzl'
|| {? _sel | _dok_mod | _a_przen |  (_sel=0 & FUN.ask('Usunąć dokument?'@))
   || _ok:={? DOK.WSK_XD='T'
           || {? _sel | _dok_mod | _a_przen
              || 0
              || FUN.ask('Dokument: %1 ma zaakceptowane podziały controllingowe.'
                         '\nKontynuować usuwanie dokumentu?'@[DOK.NK])
              ?}
           || 1
           ?};
      {? _ok
      || VAT_SR.index('DOK_N'); VAT_SR.prefix(REF.FIRMA,SSTALE.AO,DOK.ref());
         _ok:={? VAT_SR.first()
              || {? _sel
                 || 0
                 || FUN.ask('Z dokumentem: %1 związane są nabycia środków trwałych.\n'
                            'Usunięcie dokumentu spowoduje usunięcie nabyć\n'
                            'z ewidencji nabyć do korekt deklaracji VAT.\n'
                            '\nKontynuować usuwanie dokumentu?'@[DOK.NK])
                 ?}
              || 1
              ?}
      ?};
      {? _ok & DOK.E_DOC
      || _ok:=exec('del_dokel','dok_fks1')
      ?};
      {? _ok
      || {? DOK.r_lock(1,1)
         || _dokseek:=DOK.ref();
            {? DOK.get() & DOK.A='N'
            || {? ~_dok_mod & ~_a_przen || do() ?};
               _error:='\nIstnieją nieusunięte pozycje dokumentu.'@;
               POZ.index('DOK'); POZ.prefix(DOK.ref());
               P_V.use('p_v_'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
               {? POZ.first()
               || {! |?
                     exec('usu_wyr','wyrozniki');
                     P_V.index('P_V'); P_V.prefix(POZ.ref());
                     {? P_V.first() || {! |? P_V.del() !} ?};
                     P_W.index('MW'); P_W.prefix(POZ.ref());
                     {? P_W.first() || {! |? P_W.del() !} ?};
                     {? POZ.KOM().KS().MW='T'
                     || MAGWALP.index('POZ'); MAGWALP.prefix(POZ.ref());
                        {? MAGWALP.first() || {! |? MAGWALP.del() !} ?};
                        MAGWALR.index('POZ'); MAGWALR.prefix($POZ.ref());
                        {? MAGWALR.first() || {! |? MAGWALR.del() !} ?}
                     ?};
                     {? POZ.DOK().DOK_REJ().PR='T' & exec('czyLista','raty',POZ)
                     || {? exec('outLista','raty',POZ) || POZ.del() ?}
                     || POZ.del()
                     ?}
                  !}
               ?};
               SKIDXD.index('DOK'); SKIDXD.prefix(DOK.ref());
               {? SKIDXD.first() || {! |? SKIDXD.del() !} ?};
               POZF.index('DOK'); POZF.prefix(DOK.ref());
               {? POZF.first() || {! |? POZF.del() !} ?};
               VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
               exec('del_dok_jpk','dok_fks1');
               {? VPOZ.first()
               || _error:='\nIstnieją nieusunięte pozycje VAT.'@;
                  {! |?
                     P_V.index('V_P'); P_V.prefix(VPOZ.ref());
                     {? P_V.first() || {! |? P_V.del() !} ?};
                     VPOW.index('VPOW');
                     VROZ.index('VROZ'); VROZ.prefix(VPOZ.ref());
                     {? VROZ.first()
                     || {! |?
                           VPOW.prefix(VROZ.ref());
                           {? VPOW.first() || {! |? VPOW.del() !} ?};
                           VROZ.del()
                        !}
                     ?};
                     VPOZ.del()
                  !}
               ?};
               VAT_SR.index('DOK_N'); VAT_SR.prefix(REF.FIRMA,SSTALE.AO,DOK.ref());
               {? VAT_SR.first()
               || _error:='\nIstnieją nieusunięte pozycje nabyć środków do korekt deklaracji.'@;
                  {! |? {? VAT_SR.get() || exec('usu_vats','dok_fks',1); 1 ?} !}
               ?};
               VAT_SR.index('DOK_S'); VAT_SR.prefix(REF.FIRMA,SSTALE.AO,DOK.ref);
               {? VAT_SR.first()
               || _error:='\nIstnieją nieusunięte pozycje nabyć środków do korekt deklaracji.'@;
                  {! |?
                     {? VAT_SR.first()
                     || VAT_SR.cntx_psh();
                        VAT_SR.prefix(REF.FIRMA);
                        VAT_SR.DOK_S:=null; VAT_SR.put();
                        VAT_SR.cntx_pop()
                     ?}
                  !}
               ?};
               FAK.use('fak_'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
               FAK.index('FAK'); FAK.prefix(DOK.ref());
               {? FAK.first() || _error:='\nIstnieją nieusunięte powiązane faktury importowe.'@;  {! |? FAK.del() !} ?};
               {? DOK.DOK_REJ().PR='T' || exec('outLista','raty',DOK) ?};
               {? ~POZ.first() & ~VPOZ.first() & ~FAK.first() & ~VAT_SR.first() &
                 ((DOK.DOK_REJ().PR='T' & ~exec('czyLista','raty',DOK)) | DOK.DOK_REJ().PR<>'T')
               || _error:='\nIstnieją nieusunięte inne dokumenty.'@;
                  {? 4+DOK.DOKZRODL='UDTR'
                  || UDTR.cntx_psh; UDTR.prefix();
                     {? UDTR.seek(#(4-DOK.DOKZRODL),'umdlterr')
                     || UDTR.REJ:=null; UDTR.NREJ:=0; UDTR.DATADOK:=date(0,0,0);
                        UDTR.put
                     ?};
                     UDTR.cntx_pop
                  |? 6+DOK.DOKZRODL='pw'+(4+$DOK.DTW)
                  || _mask:=(4+$DOK.DTW);
                     PW.use('pw'+_mask); PW_OP.use('pwp'+_mask);
                     PWN.use('pn'+_mask);
                     _ind:=PW.ndx_tmp(,1,'REJ',,,'NRD',,,'NRD',,);
                     PW.index(_ind); PW.prefix(DOK.REJ,$DOK.NR+'/'+$(DOK.DTW~2),$DOK.NR+'/'+$(DOK.DTW~2));
                     PW_OP.index('PW');
                     {? PW.first()
                     || _pwn_ref:=PW.PWN;
                        _pwn_bl:=PW.PWN().BL;
                        _pwn_rbl:=PW.PWN().RBL;
                        _pwn_nr:=PW.PWN().NRW;
                        _pwn_odd:=PW.PWN().ODD;
                        {! |?
                           PW_OP.prefix(PW.ref());
                           PW.use('pwxxxx');
                           PW.prefix();
                           PWN.use('pnxxxx');
                           PWN.index('PWN');
                           PWN.prefix(_pwn_bl,_pwn_rbl,_pwn_nr,);
                           {? ~PWN.first()
                           || PWN.BL:=_pwn_bl;
                              PWN.RBL:=exec('rb_nosp','#string',_pwn_rbl);
                              PWN.ODD:=_pwn_odd;
                              PWN.NRW:=_pwn_nr;
                              PWN.PLIK:=PW.PLIK;
                              PWN.STATUS:='T';
                              PWN.add()
                           ?};
                           PW.REJ:=null;
                           PW.PWN:=PWN.ref();
                           PW.NRD:='';
                           PW.add();
                           {? PW_OP.first()
                           || {! |?
                                 PW_OP.use('pwpxxxx'); PW_OP.prefix();
                                 PW_OP.PW:=PW.ref(); PW_OP.add();
                                 PW_OP.use('pwp'+_mask); PW_OP.del()
                              !}
                           ?};
                           PW.use('pw'+_mask); _vcont:=PW.del()
                        !};
                        PWN.use('pn'+_mask);
                        PWN.prefix();
                        {? PWN.seek(_pwn_ref) & PWN.count()=0 || PWN.del() ?}
                     ?};
                     PW.ndx_drop(_ind)
                  |? 7+DOK.DOKZRODL='row_nag'
                  || ROW_NAG.cntx_psh();
                     ROW_NAG.prefix();
                     {? ROW_NAG.seek(#(7-DOK.DOKZRODL),)
                     || ROW_NAG.ZAKS:='N'; ROW_NAG.KTOZ:=ROW_NAG.SLAD:='';  ROW_NAG.put()
                     ?};
                     ROW_NAG.cntx_pop()
                  |? 8+DOK.DOKZRODL='w_regwyk'
                  || W_REGWYK.cntx_psh();
                     W_REGWYK.prefix();
                     {? W_REGWYK.seek(BIT.sqlint(8-DOK.DOKZRODL),)
                     || W_REGWYK.del()
                     ?};
                     W_REGWYK.cntx_pop()
                  |? 8+DOK.DOKZRODL='skan_dok'
                  || SKAN_DOK.cntx_psh();
                     SKAN_DOK.prefix();
                     {? SKAN_DOK.seek(BIT.sqlint(DOK.DOKZRODL),)
                     || SKAN_DOK.STATUS:='D';
                        SKAN_DOK.put()
                     ?};
                     SKAN_DOK.cntx_pop()
                  |? 4+DOK.KN='dokn'
                  || DOKN.cntx_psh();
                     DOKN.use(8+DOK.KN);
                     DOKN.prefix();
                     _ref:=BB.sqlint(DOK.KN);
                     {?  _ref<>0 & DOKN.seek(_ref,)
                     || DOKN.del()
                     ?};
                     DOKN.cntx_pop()
                  |? 6+DOK.DOKZRODL='webdok'
                  || WEB_DOK.cntx_psh();
                     _names:=WEB_DOK.names();
                     _fnd:=0;
                     {? _names.first()
                     || {!
                        |? WEB_DOK.use(_names.NAME);
                           WEB_DOK.index('UTW_DOK'); WEB_DOK.prefix(DOK.uidref());
                           {? WEB_DOK.first()
                           || WEB_DOK.prefix();
                              _fnd:=1;
                              {? WEB_DOK.MODYF=1
                              || WEB_DOK.STATUS:='zmodyfikowany'
                              || WEB_DOK.STATUS:='dodany'
                              ?};
                              WEB_DOK.ARH:='N';
                              WEB_DOK.UTW_DOK:='';
                              WEB_DOK.put()
                           ?};
                           _fnd=0 & _names.next()
                        !}
                     ?};
                     WEB_DOK.cntx_pop()
                  |? 4+DOK.KN='doku'
                  || DOK.cntx_psh();
                     DOK.trig_off('*','*'); VPOZ.trig_off('*','*');
                     _ref:=BB.sqlint(DOK.KN);
                     DOK.use(8+DOK.KN);
                     DOK.prefix();
                     {? _ref<>0 & DOK.seek(_ref,)
                     || {? DOK.DOK_REJ().KOR_NAG='T' | 3+DOK.ZAR='LSP' || DOK.LKN:='' || DOK.KN:='' ?}; DOK.put();
                        _CzyTezKor:={? DOK.DOK_REJ().KOR='T' || DOK.KOR || '' ?};
                        VPOZ.cntx_psh();
                        VPOZ.use('pozv'+(DOK.name+4));
                        VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
                        {? VPOZ.first()
                        || {! |?
                              {? VPOZ.KNAG=2 || VPOZ.KNAG:=0; VPOZ.put() ?};
                              VPOZ.next()
                           !}
                        ?};
                        VPOZ.cntx_pop()
                     ?};
                     DOK.trig_on('*','*'); VPOZ.trig_on('*','*');
                     DOK.cntx_pop()
                  ?};
                  {? _CzyTezKor<>''
                  || FAKS.cntx_psh(); DOK.cntx_psh();
                     DOK.trig_off('*','*'); VPOZ.trig_off('*','*');
                     _reffak:=#(4-DOK.DOKZRODL);
                     FAKS.use('faktu'+(3+(1-DOK.DOKZRODL)));
                     FAKS.prefix();
                     {? FAKS.seek(_reffak,FAKS.name())
                     || _msk:=exec('FindAndGet','#table','FAKS',FAKS.FKSQL,,"DOK",'');
                        _ref:=BB.sqlint(_msk);
                        {? _ref<>0
                        || DOK.use(8+_msk); DOK.prefix();
                           {? DOK.seek(_ref,) & DOK.ZP='T'
                           || DOK.LKN:=''; DOK.put();
                              VPOZ.cntx_psh();
                              VPOZ.use('pozv'+(DOK.name+4));
                              VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
                              {? VPOZ.first()
                              || {! |?
                                    {? VPOZ.KNAG=2 || VPOZ.KNAG:=0; VPOZ.put() ?};
                                    VPOZ.next()
                                 !}
                              ?};
                              VPOZ.cntx_pop()
                           ?}
                        ?}
                     ?};
                     DOK.trig_on('*','*'); VPOZ.trig_on('*','*');
                     FAKS.cntx_pop(); DOK.cntx_pop()
                  ?};
                  {? DOK.DOK_REJ().M_ODD='T'
                  || {? ~exec('del_wt','dok_fks1')
                     || _error:='\nIstnieją nieusunięte dokumenty wtórne.'@
                     ?}
                  ?};
                  exec('bl_dokum_unlink','dokum_zal',$DOK.ref(),~_dok_mod);
                  exec('del_att','dok_fks',$DOK.ref());
:: usunięcie powiązań z dokumentami OT środków trwałych
                  {? DOK.DOK_REJ().ZAK_SR='T' || exec('dok_del_srdz','fst',DOK.uidref()) ?};
::Poczatek modyfikacji dla Maclex Fiks 12-10-2009 AK [1120]
                  {? MLEX.FIKSB || exec('dok_usun_ml','dok_fks1') ?};
::Koniec modyfikacji dla Maclex
                  _dok_zar:={? 4+DOK.DOKZRODL='doku' & +DOK.DOKZRODL=16 & exec('dok_spr_wt','dok_fks1')
                            || 1
                            |? (3+DOK.ZAR)='LSP' | (3+DOK.ZAR)='LZK' | (3+DOK.ZAR)='LMG'
                            || exec('znacznik_log','dok_fks')
                            |? (3+DOK.ZAR)='KAS'
                            || exec('znacznik_kasa','dok_fks')
                            |? (3+DOK.ZAR)='FST'
                            || exec('znacznik_ke','dok_fks')
                            |? (3+DOK.ZAR)='PPL'
                            || exec('kali_dokdel','dok_fks',DOK.ref())
                            || 1
                            ?};
                  exec('fakskh_del','faktury_wspolne',DOK.ref());
                  {? _dok_zar | _dok_mod | _a_przen
                  || _dpolas:=DOKPOLA.name();
                     DOKPOLA.use('dokp'+(ref_name(DOK.ref())+4)); DOKPOLA.prefix();
                     exec('delpola','pola',DOKPOLA,DOK.ref());
                     DOKPOLA.use(_dpolas);
                     {? ~DOK.del(1,1) || _error:='\nIstnieją nieusunięte dokumenty.'@ ?}
                  || _error:='\nBrak możliwości usunięcia znacznika zaksięgowania na dokumencie źródłowym.'@
                  ?}
               ?};
               {? ~_dok_mod & ~_a_przen
               || {? ~end()
                  || {? _sel=0
                     || FUN.info('Próba usunięcia dokumentu\n %1 zakończyła się niepowodzeniem.%2'@[DOK.NK,_error])
                     ?}
                  || {? _sel || licz+=1 ?}
                  ?}
               || {? _sel || licz+=1 ?}
               ?}
            || {? _sel=0
               || FUN.info('Operacja usuwania nie powiodła się.'@)
               || {? DOK.A='T' || akc+=1 ?}
               ?}
            ?};
            {? DOK.seek(_dokseek) || DOK.r_unlock() ?}
         || {? _sel=0
            || FUN.info('Dokument obsługuje inny operator.'@)
            ?}
         ?}
      ?}
   ?}
|? (-(7+DOK.DOKZRODL)='ser_nag' | -(7+DOK.DOKZRODL)='par_nag' | DOK.EDOKUM<>'' | 8+DOK.DOKZRODL='skid_hzl')
|| {? _sel | _dok_mod | _a_przen | (_sel=0 & FUN.ask('Usunąć dokument?'@))
   || _ok:={? DOK.WSK_XD='T'
           || {? _sel
              || 0
              || FUN.ask('Dokument: %1 '
                         '\nma zaakceptowane podziały controllingowe.'
                         '\nKontynuować usuwanie dokumentu?'@[DOK.NK])
              ?}
           || 1
           ?};
      {? _ok & DOK.E_DOC
      || _ok:={? _sel || 1 || exec('del_dokel','dok_fks1') ?}
      ?};
      {? _ok
      || _nag_lok:=0; _nag_kom:=0; _nag_ref:=null;
         {? DOK.r_lock(1,1) &
            {? -(7+DOK.DOKZRODL)='par_nag'
            || PAR_NAG.cntx_psh(); PAR_NAG.prefix();
               {? PAR_NAG.seek(#(7-DOK.DOKZRODL),PAR_NAG.name())
               || PAR_NAG.r_lock(1,1); _nag_lok:=1; _nag_ref:=PAR_NAG.ref()
               ?};
               PAR_NAG.cntx_pop();
               {? ~_nag_lok
               || DOK.r_unlock();
                  {? _sel=0
                  || FUN.info('Powiązany z dokumentem plan umowy obsługuje inny operator.'@); _nag_kom:=1
                  ?}
               ?};
               _nag_lok
            |? 8+DOK.DOKZRODL='skid_hzl'
            || EZAL.cntx_psh(); EZAL.prefix();
               _ref:=BB.sqlint(DOK.DOKZRODL);
               {? _ref<>0 & EZAL.seek(_ref,)
               || _nag_lok:=EZAL.r_lock(1,1); _nag_lok:=3; _nag_ref:=EZAL.ref()
               ?};
               EZAL.cntx_pop();
               {? ~_nag_lok
               || DOK.r_unlock();
                  {? _sel=0
                  || FUN.info('Powiązana z dokumentem księgowym zaliczka jest obsługiwana przez innego operatora.'@);
                     _nag_kom:=1
                  ?}
               ?};
               _nag_lok
            |? DOK.EDOKUM<>''
            || _ref:=BB.sqlint(DOK.EDOKUM); _nam:=form(DOK.EDOKUM-8);
               {? _ref<>0 & _nam<>''
               || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
                  {? EDOKUM.seek(_ref,_nam)
                  || _nag_lok:=EDOKUM.r_lock(1,1,1); _nag_lok:=2; _nag_ref:=EDOKUM.ref()
                  ?};
                  EDOKUM.cntx_pop()
               ?};
               {? ~_nag_lok
               || DOK.r_unlock();
                  {? _sel=0
                  || FUN.info('Powiązany z dokumentem księgowym dokument w obiegu obsługuje inny operator.'@);
                     _nag_kom:=1
                  ?}
               ?};
               _nag_lok
            || 1
            ?}
         || _dokseek:=DOK.ref();
            {? DOK.get() & DOK.A='N'
            || {? ~_dok_mod & ~_a_przen || do() ?};
               P_V.use('p_v_'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
               VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
               {? VPOZ.first()
               || _error:='\nIstnieją nieusunięte pozycje VAT.'@;
                  {! |?
                     P_V.index('V_P'); P_V.prefix(VPOZ.ref());
                     {? P_V.first() || {! |? P_V.del() !} ?};
                     VPOW.index('VPOW');
                     VROZ.index('VROZ'); VROZ.prefix(VPOZ.ref());
                     {? VROZ.first()
                     || {! |?
                           VPOW.prefix(VROZ.ref());
                           {? VPOW.first() || {! |? VPOW.del() !} ?};
                           VROZ.del()
                        !}
                     ?};
                     VPOZ.del()
                  !}
               ?};
               FAK.use('fak_'+SSTALE.AR().KOD+form(SSTALE.AO().NR,-2));
               FAK.index('FAK'); FAK.prefix(DOK.ref());
               {? FAK.first() || _error:='\nIstnieją nieusunięte powiązane faktury importowe.'@;  {! |? FAK.del() !} ?};
               _error:='\nIstnieją nieusunięte pozycje dokumentu.'@;
               POZ.index('DOK'); POZ.prefix(DOK.ref);
               {? POZ.first()
               || {! |?
                     exec('usu_wyr','wyrozniki');
                     {? POZ.DOK().DOK_REJ().PR='T'
                     || _tab:=ROZRACH.TABELA; ROZRACH.TABELA:='POZ';
                        _zwrot:={? exec('outLista','raty',POZ) || POZ.del() ?};
                        ROZRACH.TABELA:=_tab;
                        _zwrot
                     || POZ.del()
                     ?}
                  !}
               ?};
               POZF.index('DOK'); POZF.prefix(DOK.ref());
               {? POZF.first() || {! |? POZF.del() !} ?};
               SKIDXD.index('DOK'); SKIDXD.prefix(DOK.ref());
               {? SKIDXD.first() || {! |? SKIDXD.del() !} ?};
               {? DOK.DOK_REJ().PR='T'
               || _tab:=ROZRACH.TABELA; ROZRACH.TABELA:='DOK';
                  exec('outLista','raty',DOK);
                  ROZRACH.TABELA:=_tab
               ?};
               exec('del_dok_jpk','dok_fks1');
               {? ~POZ.first() & ~VPOZ.first() &
                  ((DOK.DOK_REJ().PR='T' & ~exec('czyLista','raty',DOK)) | DOK.DOK_REJ().PR<>'T')
               || {? exec('znak_not','dok_fks',DOK.DOKZRODL,DOK.EDOKUM)
                  || {? DOK.DOK_REJ().M_ODD='T'
                     || {? ~exec('del_wt','dok_fks1')
                        || _error:='\nIstnieją nieusunięte dokumenty wtórne.'@
                        ?}
                     ?};
                     exec('del_att','dok_fks',$DOK.ref());
                     exec('fakskh_del','faktury_wspolne',DOK.ref());
                     exec('delpola','pola',DOKPOLA,DOK.ref());
                     {? _listypl($DOK.ref)
                     || {? ~DOK.del(1,1)
                        || _error:='\nIstnieją nieusunięte dokumenty.'@
                        ?}
                     || _error:='\nBrak możliwości usunięcia znacznika zaksięgowania na dokumencie źródłowym.'@
                     ?}
                  ?}
               ?};
               {? ~_dok_mod & ~_a_przen
               || {? ~end()
                  || {? _sel=0
                     || FUN.info('Próba usunięcia dokumentu\n%1\n zakończyła się niepowodzeniem.%2'@[DOK.NK,_error])
                     ?}
                  || {? _sel>0 || licz+=1 ?}
                  ?}
               || {? _sel>0 || licz+=1 ?}
               ?}
            || {? _sel=0
               || FUN.info('Operacja usuwania nie powiodła się.'@)
               || {? DOK.A='T' || akc+=1 ?}
               ?}
            ?};
            {? _nag_lok=1
            || PAR_NAG.cntx_psh(); PAR_NAG.prefix();
               {? PAR_NAG.seek(_nag_ref) || PAR_NAG.r_unlock() ?};
               PAR_NAG.cntx_pop()
            |? _nag_lok=2
            || EDOKUM.cntx_psh(); EDOKUM.prefix();
               {? EDOKUM.seek(_nag_ref,ref_name(_nag_ref))
               || EDOKUM.NETTONAR:=0; EDOKUM.put();
                  EDOKUM.r_unlock()
               ?};
               EDOKUM.cntx_pop()
            |? _nag_lok=3
            || EZAL.cntx_psh(); EZAL.prefix(REF.FIRMA);
               {? EZAL.seek(_nag_ref) || EZAL.r_unlock() ?};
               EZAL.cntx_pop()
            ?};
            {? DOK.seek(_dokseek) || DOK.r_unlock() ?}
         ||  {? ~_nag_kom & _sel=0
             || FUN.info('Dokument obsługuje inny operator.'@)
             ?}
         ?}
      ?}
   ?}
?}


\dod_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MGR [2011]
:: OPIS: Dodawanie dokumentu na podstawie dokumentu dla wielu jednostek ksiegowych
::   WE: _a=0 - akcja grupowa, _a=1 - pojedynczo
::   WY: 1 - powodzenie, 0 - niepowodzenie
::  OLD: \dod_mod/akcept.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
KOMT:='';
_refmod:=$DOK.ref();
_dokmod:=DOK.DOK_REJ;
_oddmod:=DOK.ODD;
POZ.cntx_psh();
POZ.index('DOK_MOD'); POZ.prefix(DOK.ref());
{? DOK.REJ().ROK().FIRMA().TYP='W' & POZ.first()
|| REJ.cntx_psh(); REJ.index('KOD'); REJ.prefix(ROK_F.ref());
   DOK_REJ.cntx_psh(); DOK_REJ.index('NAZ'); DOK_REJ.prefix();
   {!
   |? DOKM_DOK.index('DOKM'); DOKM_DOK.prefix(DOK.DOK_REJ);
      {? ~DOKM_DOK.find_key(POZ.ODD_KS)
      || DOKM_DOK.DOKM:=DOK.DOK_REJ;
         DOKM_DOK.ODD:=POZ.ODD_KS;
         {? REJ.find_key(DOKM_DOK.ODD,'~NOTY_W2',) & DOK_REJ.find_key(REJ.ref())
         || DOKM_DOK.REJ:=REJ.ref();
            DOKM_DOK.DOKW:=DOK_REJ.ref();
            DOKM_DOK.add()
         ?}
      ?};
      POZ.next()
   !};
   DOK_REJ.cntx_pop();
   REJ.cntx_pop()
?};
{? POZ.first()
|| TAB_WOD:=tab_tmp(1,'ODD','STRING[16]','J. ks.'); TAB_WOD.prefix();
   {! |?
      {? POZ.ODD_KS<>_oddmod
      || _ok:=0;
         DOK.cntx_psh(); DOK.index('ODDZRODL'); DOK.prefix();
         errno();
         DOK.cntx_psh();
         _odd_ks:=$POZ.ODD_KS; _ref_new:=null;
         {? DOK.find_key(POZ.ODD_KS,_refmod,_refmod)
         || {? TAB_WOD.find_key(_odd_ks)
            || _ref_new:=DOK.ref(); DOK.cntx_pop(); _ok:=1
            || {? DOK.count()>0
               || GR_EL.cntx_psh();
                  GR_EL.index('DOK_SQL'); GR_EL.prefix(REF.FIRMA,DOK.ref());
                  {? GR_EL.size()=DOK.count()
                  || {? GR_EL.first() || {! |? GR_EL.del() !} ?}
                  ?};
                  GR_EL.cntx_pop()
               ?};
               {? DOK.count()=0
               || _zrodl:=DOK.DOKZRODL; _nr:=DOK.NR; _rej:=DOK.REJ; _dokr:=DOK.DOK_REJ; _nk:=DOK.NK; _odd:=DOK.ODD;
                  _symz:=DOK.SYM_ZEW;
                  {? DOK.del(,1)
                  || DOK.cntx_pop();
                     DOK.DOKZRODL:=_zrodl; DOK.NR:=_nr; DOK.REJ:=_rej; DOK.DOK_REJ:=_dokr; DOK.NK:=_nk; DOK.ODD:=_odd;
                     DOK.RVAT:=DOK.SUMWAL:=DOK.SUMC:=DOK.JORG:=DOK.OKRVAT:=DOK.NRDZ:=DOK.A_VAT:=DOK.KRAJ:=DOK.TRKS_V:=DOK.DZ:=0;
                     DOK.SYM_ZEW:=_symz;
                     {? DOK.add(1)
                     || TAB_WOD.ODD:=_odd_ks; TAB_WOD.add(); _ref_new:=DOK.ref(); _ok:=1
                     || KOMT:='\nNieudana próba dodania dokumentu wtórnego.\n'@; undo(); _ret:=0
                     ?}
                  || DOK.cntx_pop();
                     KOMT:='\nNieudana próba dodania dokumentu wtórnego.\n'@; undo(); _ret:=0
                  ?}
               || _ref_new:=DOK.ref(); DOK.cntx_pop(); _ok:=1
               ?}
            ?}
         || DOK.cntx_pop();
            {? errno()
            || KOMT:='\nDokument wtórny obsługuje inny operator.\n'@; undo(); _ret:=0
            || DOKM_DOK.index('DOKM'); DOKM_DOK.prefix();
               {? DOKM_DOK.find_key(_dokmod,POZ.ODD_KS)
               || DOK.DOKZRODL:=_refmod;
                  DOK.ODD:=DOKM_DOK.ODD;
                  DOK.REJ:=DOKM_DOK.REJ;
                  DOK.DOK_REJ:=DOKM_DOK.DOKW;
                  DOK.NR:=1;
                  exec('numer','dok_fks');
                  {? +DOKM_DOK.FORM_SYM
                  || DOK.NK:=($DOKM_DOK.FORM_SYM)()
                  || {? +DOK.DOK_REJ().FORM_SYM
                     || DOK.NK:=($DOK.DOK_REJ().FORM_SYM)()
                     ?}
                  ?};
                  {? errno()
                  || KOMT:='\nNie udała się operacja ustalenia numeru dokumentu wtórnego\n'
                           '(blokada przez innego operatora).\n'@;
                     undo(); _ret:=0
                  || DOK.RVAT:=DOK.SUMWAL:=DOK.SUMC:=DOK.JORG:=DOK.OKRVAT:=DOK.NRDZ:=DOK.A_VAT:=DOK.KRAJ:=DOK.TRKS_V:=DOK.DZ:=0;
                     {? DOK.add(1)
                     || TAB_WOD.ODD:=_odd_ks; TAB_WOD.add(); _ref_new:=DOK.ref(); _ok:=1
                     || KOMT:='\nNieudana próba dodania dokumentu wtórnego.\n'@; undo(); _ret:=0
                     ?}
                  ?}
               || KOMT:='\nNie znaleziono definicji dokumentu wtórnego dla jednostki %1.\n'@[POZ.ODD_KS().OD]; undo(); _ret:=0
               ?}
            ?}
         ?};
         DOK.prefix();
         {? _ok & _ref_new & DOK.seek(_ref_new)
         || POZ.DOK:=DOK.ref();
            POZ.POZ:=exec('bl_poz','dok_fks');
            {? ~POZ.put(1)
            || KOMT:='\nNieudana próba przeniesienia pozycji dekretu do dokumentu wtórnego.\n'@;
               undo();
               _ret:=0
            ?}
         ?};
         DOK.cntx_pop()
      ?};
      POZ.next()
   !};
   VAR_DEL.delete('TAB_WOD')
?};

:: Akceptowanie dokumentow powstalych na podstawie dokumentu dla wielu jednostek ksiegowych
DOK.cntx_psh();
_refsql:=$DOK.ref(); _zp:=DOK.ZP;
DOK.index('DOKZRODL'); DOK.prefix(_refsql,_refsql);
POZ.index('DOK');
{? DOK.first()
|| _dw:=obj_new(DOK.size());
   {! _i:=1 .. DOK.size()
   |! _dw[_i]:=$DOK.ref();
      DOK.next()
   !};
   __dOkWtU:=1;
   {! _i:=1 .. DOK.size()
   |! {? DOK.seek(BB.sqlint(8-_dw[_i]),8+_dw[_i])
      || POZ.prefix(DOK.ref());
         {? POZ.size()=0
         || modd_kom:=0;
            exec('dok_usun','dok_fks1');
            VAR_DEL.delete('modd_kom')
         || {? exec('akc_dok1','dok_fks1',_a)
            || {? _zp='T' || exec('ks_dok','dok_fks_ks',0) ?};
               {? var_pres('licz2')>0 || licz2+=1 ?}
            || KOMT:='\nNieudana próba akceptacji dokumentów wtórnych!\n'@;
               undo();
               _ret:=0;
               _i:=DOK.size()
            ?}
         ?}
      ?}
   !};
   VAR_DEL.delete('__dOkWtU');
   obj_del(_dw)
?};
DOK.cntx_pop();
POZ.cntx_pop();
_ret


\akc_dok1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: akceptowanie pojedynczego dokumentu zrodlowego
::       Parametr:  _a = 1: pojedynczy dok.; 0: wszystkie dokumenty
::       Funkcja sluzy do akceptacji dokumentu zrodlowego.
::       Wykonuje kolejno:
::         kontrola dla naglowka dokumentu:
::          1. czy dokument jest przypisany do jakiejs jednostki księgowej
::          2. czy dokument VAT posiada pozycje VAT
::          3. czy data zapisu ksiegowania dokumentu jest z aktualnego okresu
::          4. czy jest wypelniona data operacji i data wystawienia dokumentu
::          5. czy sa pozycje dokumentu
::         operacje dla pozycji:
::          1. dla dokumentu obcego uzupelnienie komentarza konta
::          2. dla dokumentów VAT - sprawdzenie i uzupelnienie pol: TP,
::             DO, DATA_R
::          3. sprawdzenie poprawnosci konta
::          4. sprawdzenie czy konto zablokowane
::          5. sprawdzenie prawidlowosci wypelnienia pol pozycji
::          6. sprawdzenie poprawnosci pol: DO i TP
::          7. sprawdzenie poprawnosci identyfikatora dla pozycji walutowej
::          8. sprawdzenie RMK dla pozycji rozrachunkowej typu R
::          9. sprawdzenie wypelnienia wyroznikow
::         10. sprawdzenie bilansowania sie dokumentu
::         11. dodanie dokumentów wtórnych do dokumentu dla wielu jednostek ksiegowych
::         aktualizacja znacznikow w naglowku dokumentu
::  OLD: \akc_dok/dok_zrd2.fml
:: ---------------------------------------------------------------------------------------------------------------------
_akcept:=1; _kom:=''; _rvat:=0; _vpoz:=0; _stgrvat:=0; _pozf:=0;
_dok_mod:=exec('spr_mod','dok_fks');
{? DOK.DOK_REJ
|| _typ:=DOK.DOK_REJ().SLO().KOD;
   {? _typ='BO'
   || OKRO_F.cntx_psh(); ROK_F.cntx_psh();
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
      SSTALE.AO().ROK();
      {? exec('first_rok_obsz','zam_okr_rok','FKS',ROK_F.ref()) || _typ:='' ?};
      OKRO_F.cntx_pop(); ROK_F.cntx_pop()
   ?}
|| _typ:=''
?};
{? -DOK.A<>'t'
|| _akcept:=var_pres('__dOkWtU')>0 | (exec('spr_dok','dok_fks1',_a,'akceptacja') & DOK.get());
   _vat:=(_typ='VAT' | _typ='SAD');
   _s:=DOK.RVAT().RVAT().RT;
   _zak:={? _vat || _s<>'S' & _s<>'W' || 0 ?};
   {? _akcept & _zak & DOK.D4=date(0,0,0) & (_s<>'E' | _typ='SAD')
   || _akcept:=0; _kom:='Nie podano daty otrzymania.'@
   ?};
   {? _akcept & DOK.D4<>date(0,0,0) & (DOK.D4<SSTALE.AO().POCZ | DOK.D4>SSTALE.AO().KON) & SSTALE.AO().KON<>date(0,0,0)
   || _znal:=0;
      VPOZ.cntx_psh(); VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
      {? VPOZ.first()
      || {! |?
            _v:=VPOZ.RVAT().RVAT().KVAT().SYM; _znal:=(1+_v='Z' | 4+_v='ImpT');
            ~_znal & VPOZ.next()
         !}
      ?};
      VPOZ.cntx_pop();
      {? _znal
      || _akcept:=_a & FUN.ask('Data otrzymania dokumentu spoza aktywnego okresu.\nKontynuować?'@)
      ?}
   ?};
   {? MLEX.FIKSB & _akcept
   || _akcept:=exec('akc_chk','dok_fks1',_a,_typ)
   ?};
   {? _akcept
   || {? ~_dok_mod || do() ?};
      _wn:=_ma:=0;
      {? DOK.NK='' || _akcept:=0; _kom:='Brak symbolu dokumentu.'@ ?};
      {? _akcept & DOK.ODD=null
      || _akcept:=0; _kom:='Dokument %1 nie jest związany z żadną jednostką księgową.'@[DOK.NK]
      ?};
      {? _akcept & DOK.ODD<>DOK.REJ().ODD
      || _akcept:=0; _kom:='Dla dokumentu %1\n jednostka księgowa rejestru jest inna\n niż jednostka księgowa dokumentu.'@[DOK.NK]
      ?};
      {? _akcept
      || {? DOK.DOK_REJ
         || _vat:=(_typ='VAT' | _typ='SAD');
            _wew:=_typ='WEW';
            {? _wew
            || _vpoz:=0
            || VPOZ.prefix(DOK.ref());
               {? VPOZ.first()
               || _vpoz:=1; _rvat:=1; _zer:=0;
                  {! |? {? VPOZ.RVAT=null  || _rvat:=0  ?};
                           _rvat & VPOZ.next
                  !}
               ?}
             ?}
         || _akcept:=0; _kom:='Niewypełnione pole rodzaju dokumentu.'@
         ?}
      ?};
      {? _akcept
      || {? ~_wew & VPOZ.first()
         || _vpoz:=1; _stgrvat:=1;
            {! |? {? (VPOZ.PR().KOD='Zwol.' | VPOZ.PR().KOD=' 0 %'  | VPOZ.PR().KOD=' -' | VPOZ.PR().KOD=' -o')
                      & (1+VPOZ.GRVAT().GRVAT().KOD='Z' & 1+(VPOZ.GRVAT().GRVAT().KOD+4)<>'N')
                  || _stgrvat:=0
                  ?};
                  _stgrvat & VPOZ.next
            !}
         ?};
         {? _wew & POZF.first() || _pozf:=1 ?}
      ?};
      {? _akcept & _vat=0 & _vpoz
      || _akcept:=0;
         _kom:='Niewłaściwy rodzaj dokumentu.\n'
               'Istnieją pozycje VAT - \nrodzaj dokumentu powinien być VAT lub SAD.'@
      |? _akcept & _vpoz & DOK.RVAT=null
      || _akcept:=0; _kom:='Niewypełnione pole rejestru VAT\nmimo istnienia pozycji VAT.'@
      |? _akcept & _vpoz & ~_rvat
      || _akcept:=0; _kom:='Niewypełniony rejestr VAT pozycji dokumentu.'@
      |? _akcept & _vpoz & ~_stgrvat
      || {? _a & ~FUN.ask('Dla stawek 0%%, ''-'' i Zwol. należałoby wybrać grupę podatkową wskazującą\n na brak odliczenia VAT. Kontynuować?'@)
         || _akcept:=0
         |? ~_a
         || _akcept:=0; _kom:='Dla stawek 0%%, ''-'' i Zwol. wybierz grupę podatkową wskazującą na brak odliczenia VAT.'@
         ?}
      |? _akcept & DOK.RVAT & ~_vat
      || _akcept:=0; _kom:='Wypełniono rejestr VAT dla rodzaju dokumentu innego niż VAT i SAD.'@
      |? _akcept & _vat & DOK.D3=date(0,0,0)
      || _akcept:=0; _kom:='Niewypełnione pole \'Termin płat.\' dokumentu.'@
      |? _akcept & _wew & DOK_REJ.CZY_DP='T' & DOK.D3=date(0,0,0)
      || _akcept:=0; _kom:='Niewypełnione pole \'Termin płat.\' dokumentu.'@
      |? _akcept & _vat=0 & _vpoz=0
      || _zer:=1
      ?};
      {? _akcept & _vat
      || {? ~(_akcept:=_vpoz) || _kom:='Brak pozycji VAT w dokumencie.'@ ?};
         {? _akcept & (DOK.KRAJ=null | DOK.JORG=null)
         || _kom:='Brak wypełnienia pól:\n Kraj opodatkowania, Waluta opodatkowania. '@;
            _akcept:=0
         ?}
      ?};
      {? _akcept & _wew
      || {? ~(_akcept & (_pozf | 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'))
         || _kom:='Brak pozycji faktury w dokumencie wewnętrznym.'@;
            _akcept:=0
         ?}
      ?};
      {? _akcept & (DOK.DTW<SSTALE.AO().POCZ | DOK.DTW>SSTALE.AO().KON) &
         SSTALE.AO().KON<>date(0,0,0)
      || _akcept:=0; _kom:='Data zapisu (księgowania) spoza aktywnego okresu.'@
      ?};
      {? _akcept & DOK.DOP=date(0,0,0)
      || _akcept:=0; _kom:='\nNiewypełnione pole data operacji.'@
      ?};
      {? _akcept & DOK.DTO=date(0,0,0)
      || _akcept:=0; _kom:='\nNiewypełnione pole data dokumentu.'@
      ?};
      {? _akcept & _vat & ~exec('spr_nab','dok_fks1')
      || _akcept:=0;
         _kom:='Z dokumentem %1 są powiązane\n nabycia środków trwałych.\n'
               'Brak kwoty VAT dla grupy ZInwPodZ lub ZInwPodS w pozycjach VAT dokumentu.'@[DOK.NK]
      ?};
      {? _akcept & _vat & DOK.OKRVAT<>null & DOK.OKRVAT().POCZ=date(0,0,0)
      || _akcept:=0;
         _kom:='Okresem rozliczenia VAT nie może być\n'
               '"Bilans otwarcia" i "Bilans zamknięcia".'@
      ?};
      {? _akcept & DOK.DOK_REJ().PR='T' & exec('drb_tpfp','raty','DOK',1)=date(0,0,0)
      || _akcept:=0;
         _kom:='Niewypełniona lista płatności ratalnych dla dokumentu.'@
      ?};
      {? _akcept & DOK.DTW<date(2019,7,1) & -(4+DOK.RVAT().RVAT().KVAT().SYM='sprz' | -(5+KVAT.SYM)='_wwspd') & -DOK.DOK_REJ().KOR='t' & DOK.KOR_OKR=''
      || _akcept:=0; _kom:='Nie podano okresu korekty.'@
      ?};
      {? _akcept & -DOK.DOK_REJ().KOR='t' & DOK.KOR_PRZY=''
      || _akcept:=0; _kom:='Nie podano przyczyny korekty.'@
      ?};
      {? _akcept & -DOK.DOK_REJ().KOR='t' & DOK.KOR=''
      || _akcept:=0; _kom:='Nie podano symbolu korekty.'@
      ?};
      {? _akcept & DOK.RB_V
      || DOK.RB_V();
         {? SKID_RBK.WAL<>FINFO.NAROD
         || _akcept:=0; _kom:='Jako rachunek VAT należy wybrać rachunek w walucie narodowej.'@
         ?}
      ?};
      _sp_przel:=DOK.DOK_REJ().SP_PRZEL='T';
      _par76:=PAR_SKID.get(76);
      _obj:=exec('obj_op','dok_fks_ks');
      {? _akcept & POZ.first()
      || {! |?
            {? +|POZ.ID
            || exec('ustaw_roz','dok_fks');
               exec('ust_do','dok_fks');
               exec('ust_tp1','dok_fks1');
               POZ.SYM_ROK:=exec('op_unik_sym','rozrach',POZ.ID,POZ.DO)
            ?};
            {? (_vat | (DOK.DOK_REJ().CZY_ROZL='N' & ~_vat)) & +|POZ.ID &
               ({? SSTALE.AO().NAZ='Bilans otwarcia' || POZ.DATA_R=date(0,0,0) || 1 ?})
            || _d:=exec('dat_otw','dok_fks');
               {? POZ.DATA_R<>_d || POZ.DATA_R:=_d ?}
            ?};
            {? _typ<>'BO' & POZ.SUM=0
            || _akcept:=0;
               _kom:='Dokument %1\n pozycja %2'
                     '\n Niewypełnione pole Kwota w PLN.'@[DOK.NK,$POZ.POZ]
            ?};
            {? POZ.SUM<>POZ.SUM$2 || POZ.SUM:=POZ.SUM$2 ?};
            {? POZ.KON=''
            || _akcept:=0;
               _kom:='Dokument %1\n pozycja %2'
                     '\n Niewypełnione pole Konto.'@[DOK.NK,$POZ.POZ]
            ?};
            {? _akcept & (POZ.KOM=null | (|POZ.KOM().KS().SYM<>(BILANSP.ASYNT+POZ.KON)))
            || _k:=exec('koment','konto',POZ.KON);
               {? _k
               || POZ.KOM:=_k
               || _kom:='Dokument %1\n pozycja %2'
                        '\n Nie znaleziono komentarza konta księgowego %3.'@[DOK.NK,$POZ.POZ,POZ.KON];
                  _akcept:=0
               ?}
            ?};
            {? _akcept &  POZ.WAL<>null & POZ.KOM().KS().WIELO='N'
            || _akcept:=0;
               _kom:='Dokument %1\n pozycja %2'
                   '\n Konto nie jest wielowalutowe.'@[DOK.NK,$POZ.POZ]
            ?};
::Poczatek modyfikacji dla Maclex Fiks 12-10-2009 AK [1120]
            {? _akcept & MLEX.FIKSB
            || {? ~exec('mlbzaddod','ml_zad',,,1)
               || _akcept:=0;
                  _kom:={? var_pres('KOMT')
                        || KOMT
                        || 'Dokument %1\n pozycja %2\n Błąd w klasyfikacji budżetu zadaniowego'@[DOK.NK,$POZ.POZ]
                        ?}
               ?};
               {? _akcept
               || {? ~exec('pozbdod','mlxstan',,,1)
                  || _akcept:=0;
                     _kom:={? var_pres('KOMT')
                           || KOMT
                           || 'Dokument %1\n pozycja %2'
                                 '\n Wystąpił wyjątek w klasyfikacji budżetu rodzajowego'@[DOK.NK,$POZ.POZ]
                           ?}
                  ?}
               ?}
            ?};
::Koniec modyfikacji dla Maclex
            {? _akcept & POZ.WAL<>null
            || {? POZ.SUMW<>POZ.SUMW$2 || POZ.SUMW:=POZ.SUMW$2 ?};
               {? _typ<>'BO' & (POZ.SUMW$2=0 | POZ.KURS=0)
               || _akcept:=0;
                  _kom:='Dokument %1\n pozycja %2'
                      '\n Brak informacji walutowych.'@[DOK.NK,$POZ.POZ]
               ?};
               {? _akcept & POZ.WAL=FINFO.NAROD
               || _akcept:=0;
                  _kom:='Dokument %1\n pozycja %2'
                      '\n niedozwolona waluta narodowa w polu \'Waluta\'.'@[DOK.NK,$POZ.POZ]
               ?}
            || POZ.SUMW:=POZ.KURS:=0
            ?};
            {? _akcept & exec('akc_kont','edkonto',POZ.KON)
            || _akcept:=0;
               {? POZ.KON=''
               || _kom:='Dokument %1\n pozycja %2.\n Niepoprawne konto księgowe.'@[DOK.NK,$POZ.POZ]
               || _kom:='Dokument %1\n pozycja %2.\n Niepoprawne konto księgowe %3.'@[DOK.NK,$POZ.POZ,POZ.KON]
               ?}
            ?};
            {? _akcept & -exec('akc_zabl','konto',POZ.KON)='t'
            || _akcept:=0;
               _kom:='Dokument %1\n pozycja %2\n zablokowane konto księgowe %3.'@[DOK.NK,$POZ.POZ,POZ.KON]
            ?};
            {? _akcept & _par76='B' & POZ.ID<>''
            || ROK_F.cntx_psh();
               ROK_F.prefix();
               {? ~exec('sp_poz_next','rozrach',_obj,POZ.KON)
               || _kom:='Dokument %1,\npozycja %2,\nKonto księgowe \'%3\'.\n'
                        'Nie można ustalić konta księgowego w roku %4.'@[DOK.NK,$POZ.POZ,POZ.KON,ROK_F.NAZ];
                  _akcept:=0
               ?};
               ROK_F.cntx_pop()
            ?};
            {? _akcept & POZ.KON*' '
            || _akcept:=0;
                {? POZ.KON<>''
                || _kom:='Dokument %1\n pozycja %2.\n Konto księgowe \'%3\''
                    ' zawiera\nzabroniony znak spacji.'@[DOK.NK,$POZ.POZ,POZ.KON]
                || _kom:='Dokument %1\n pozycja %2.\n'
                 'Konto księgowe zawiera\nzabroniony znak spacji.'@[DOK.NK,$POZ.POZ]
                ?}
            ?};
            {? _akcept
            || _p:=exec('spr_pop','dok_fks1',_typ,1);
               {? _p<>''
               || _akcept:=0;
                  {? _p='r'
                  || _kom:='Dokument %1\n pozycja %2'
                           '\n Brak identyfikatora rozrachunku.'@[DOK.NK,$POZ.POZ]
                  |? _p='t'
                  || _kom:='Dokument %1\n pozycja %2\n Błędny typ identyfikatora rozrachunku.'@[DOK.NK,$POZ.POZ]
                  |? _p='poz_odd'
                  || _kom:='Dokument %1\n pozycja %2'
                           '\n Niepotrzebna jednostka księgowa rozrachunku.'@[DOK.NK,$POZ.POZ]
                  |? _p='z'
                  || _kom:='Dokument %1\n pozycja %2'
                           '\n Niepotrzebny identyfikator rozrachunku.'@[DOK.NK,$POZ.POZ]
                  |? _p='do'
                  || _kom:='Dokument %1\n pozycja %2'
                           '\n Niepotrzebna data otwarcia rozrachunku.'@[DOK.NK,$POZ.POZ]
                  |? _p='tp'
                  || _kom:='Dokument %1\n pozycja %2'
                           '\n Niepotrzebny termin płatności rozrachunku.'@[DOK.NK,$POZ.POZ]
                  |? _p='data_r'
                  || _kom:='Dokument %1\n pozycja %2'
                           '\n Niepotrzebna data rozliczenia.'@[DOK.NK,$POZ.POZ]
                  |? _p='b'
                  || _kom:='Dokument %1\n pozycja %2'
                           '\n Identyfikator rozrachunku niezgodny\n z wymaganym na koncie syntetycznym.'@[DOK.NK,$POZ.POZ]
                  |? _p='dok_poz_odd'
                  || _kom:='Dokument %1\n pozycja %2'
                           '\n Jednostka księgowa rozrachunku inna niż w dokumencie.'@[DOK.NK,$POZ.POZ]
                  || _kom:='Dokument %1\n pozycja %2'
                      '\n Niewypełnione pole %3.'@[DOK.NK,$POZ.POZ,form(_p)]
                  ?}
               ?}
            ?};
            {? _akcept & +|POZ.ID
            || {? PAR_SKID.get(38)='N'
               || _v:=exec('czyLista','raty',POZ,1);
                  {? (~_v & (POZ.DO>POZ.TP))
                     | (_v & (POZ.DO>exec('drb_tpfp','raty','POZ',1)))
                  || _akcept:=0;
                     _kom:='Dokument %1\n pozycja %2'
                        '\nData otwarcia rozrachunku nie może być późniejsza od terminu płatności.'@[DOK.NK,$POZ.POZ]
                  ?};
                  {? _v & _akcept &
                     ((POZ.WAL & POZ.SUMW<>exec('wartPlat','raty','WAR',,POZ,POZ.ref())) |
                      (~POZ.WAL & POZ.SUM<>exec('wartPlat','raty','WAR',,POZ,POZ.ref())))
                  || _kom:='Lista rat niezgodna z kwotą dekretu pozycji '@
                        +$POZ.POZ;
                     _akcept:=0
                  ?}
               ?};
               {? _akcept & POZ.SLO_PROJ<>null & (POZ.TID='ZOB' & -POZ.STR='wn' | POZ.TID='NAL' & -POZ.STR='ma')
               || _akcept:=0;
                  _kom:='\nZbędnie wypełnione pole Projekt\nw zapisie rozliczającym rozrachunek\nprojektowy (pozycja %1)'@[$POZ.POZ]
               ?};
               {? _akcept
               || {? POZ.SLO_PROJ<>null & XINFO.SLU_PROJ=null
                  || _akcept:=0;
                     _kom:='\nZbędnie wypełnione pole Projekt\ngdy nie podano słownika projektów (pozycja %1)'@[$POZ.POZ]
                  |? XINFO.SLU_PROJ<>null & POZ.SLO_PROJ<>null
                  || SLO.index('SL'); SLO.prefix(XINFO.SLU_PROJ);
                     {? ref_name(POZ.SLO_PROJ)<>SLO.name() | ~SLO.seek(POZ.SLO_PROJ)
                     || _akcept:=0;
                        _kom:='\nNieprawidłowo wypełnione pole Projekt (pozycja %1)\n'
                              'Wartość nie pochodzi ze słownika projektów.'@[$POZ.POZ]
                     ?}
                  ?}
               ?}
            ?};
            {? _akcept & +|POZ.ID & POZ.WAL<>null
            || {? exec('tstwal','dok_fks_ks',POZ.WAL)
               || _akcept:=0;
                  _kom:='\nIstnieje rozrachunek o podanym symbolu otwarty w roku %1\n'
                        ' w innej walucie obcej.\n'
                        ' Popraw dekret dla pozycji: %2.'@[$(POZ.DO~1),$POZ.POZ]
               ?};
               {? _akcept & exec('tstwalop','dok_fks',POZ.WAL,_a)
               || _kom:='Rozrachunek o podanym identyfikatorze otwarty w roku %1'
                        '\nistnieje już dla innej waluty\nw bieżącym dokumencie.\n'
                        '(pozycja %2)'@[(POZ.SYM_ROK+4),$POZ.POZ];
                  _akcept:=0
               ?}
            ?};
            {? _akcept & +|POZ.ID & (-POZ.TID='rmk' | -POZ.TID='rmp')
            || {? RMK.index('NAZ'); RMK.prefix(); RMK.find_key(POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK)
               || {? RMK.KONZ='<- F3 ->' & (RRMK.prefix(RMK.ref()); ~RRMK.first())
                  || _kom:='Rozrachunek typu \'R\' - RMK z wyróżnikiem.\nBrak rozpisanego wyróżnika.\n'
                           '(pozycja %1)'@[$POZ.POZ];
                     _akcept:=0
                  ?}
               || _kom:='Rozrachunek typu \'R\' - nie podano RMK.\n'
                        '(pozycja %1)'@[$POZ.POZ];
                  _akcept:=0
               ?}
            ?};
            {? _akcept & exec('czyLista','raty',POZ) & exec('e_opdrb','rozrach',0)
            || _kom:='Dokument %1\n pozycja %2'
                     '\nDla rozrachunku zdefiniowano już listę płatności.'@[DOK.NK,$POZ.POZ];
               _akcept:=0
            ?};
            {? _akcept & +|POZ.ID & +|POZ.SYM_ZEW
            || _sym_zew:=POZ.SYM_ZEW; _kon:=POZ.KON;
               POZ.cntx_psh(); POZ.index('SYM_ZEW'); POZ.prefix(DOK.ref(),POZ.ID,POZ.WAL,POZ.ODD);
               {? POZ.size()>1 & POZ.first()
               || {! |?
                     _akcept:=(POZ.SYM_ZEW=_sym_zew | POZ.KON<>_kon);
                     _akcept & POZ.next()
                  !}
               ?};
               POZ.cntx_pop();
               {? ~_akcept
               || _kom:='\nDokument %1 pozycja %2.\n'
                        'Istnieje inna pozycja tego dokumentu mająca ten sam\n'
                        'identyfikator rozrachunku, konto, walutę, jednostkę księgową i jednocześnie\n'
                        'inny symbol zewnętrzny rozrachunku.'@[DOK.NK,$POZ.POZ]
               ?}
            ?};
            {? _akcept & +|POZ.ID & +|POZ.SYM_ZEW
            || _wal:={? POZ.WAL=null || FINFO.NAROD || POZ.WAL ?};
               OP.cntx_psh(); OP.index('KON_O');
               OP.prefix(_wal,POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK);
               {? OP.first()
               || {! |?
                     _akcept:=(POZ.SYM_ZEW=OP.SYM_ZEW);
                     _akcept & OP.next()
                  !}
               ?};
               OP.cntx_pop();
               {? ~_akcept
               || _kom:='\nDokument %1 pozycja %2.\n'
                        'Istnieje rozrachunek mający ten sam\n'
                        'identyfikator, konto, walutę, jednostkę księgową i jednocześnie\n'
                        'inny symbol zewnętrzny rozrachunku.'@[DOK.NK,$POZ.POZ]
               ?}
            ?};
            {? _akcept & +|POZ.ID & +|POZ.SYM_ZEW
            || _mwal:={? POZ.WAL=null || FINFO.NAROD || POZ.WAL ?};
               _modd:={? POZ.ODD<>null || POZ.ODD || DOK.ODD ?};
               ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
               OKRO_F.cntx_psh(); OKRO_F.index('ROK');
               SSTALE.AR();
               {? ROK_F.prev()
               || SSTALE.AR();
                  OKRO_F.prefix(ROK_F.ref()); _kon1:=POZ.KON;
                  {? OKRO_F.first()
                  || POZ.cntx_psh();
                     POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2)); POZ.index('KON'); POZ.prefix(_kon1);
                     {? POZ.first() || _kon1:=POZ.PKON ?};
                     POZ.cntx_pop()
                  ?};
                  {? ROK_F.prev()
                  || OP.cntx_psh();
                     OP.use('operac'+ROK_F.KOD); OP.index('KON_O'); OP.prefix(_mwal,_modd,_kon1,POZ.ID,POZ.ID);
                     {? OP.first()
                     || {! |?
                           _akcept:=(POZ.SYM_ZEW=OP.SYM_ZEW);
                           _akcept & OP.next()
                        !}
                     ?};
                     OP.cntx_pop()
                  ?}
               ?};
               {? ~_akcept
               || _kom:='\nDokument %1 pozycja %2.\n'
                        'W poprzednim roku (%3)\n'
                        'istnieje rozrachunek mający ten sam\n'
                        'identyfikator, konto (z uwzględnieniem bilansu przekształcenia),\n'
                        'walutę, jednostkę księgową i jednocześnie inny symbol zewnętrzny rozrachunku.'@[DOK.NK,$POZ.POZ,ROK_F.NAZ]
               || SSTALE.AR();
                  {? ROK_F.next()
                  || OKRO_F.prefix(ROK_F.ref()); _kon1:=POZ.KON;
                     {? OKRO_F.first()
                     || POZ.cntx_psh();
                        POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2)); POZ.index('PKON'); POZ.prefix(_kon1);
                        {? POZ.first() || _kon1:=POZ.KON ?};
                        POZ.cntx_pop()
                     ?};
                     OP.cntx_psh();
                     OP.use('operac'+ROK_F.KOD); OP.index('KON_O'); OP.prefix(_mwal,_modd,_kon1,POZ.ID,POZ.ID);
                     {? OP.first()
                     || {! |?
                           _akcept:=(POZ.SYM_ZEW=OP.SYM_ZEW);
                           _akcept & OP.next()
                        !}
                     ?};
                     OP.cntx_pop();
                     {? ~_akcept
                     || _kom:='\nDokument %1 pozycja %2.\n'
                              'W kolejnym roku (%3)\n'
                              'istnieje rozrachunek mający ten sam\n'
                              'identyfikator, konto (z uwzględnieniem bilansu przekształcenia),\n'
                              'walutę, jednostkę księgową i jednocześnie inny symbol zewnętrzny rozrachunku.'@[DOK.NK,$POZ.POZ,ROK_F.NAZ]
                     ?}
                  ?}
               ?};
               ROK_F.cntx_pop(); OKRO_F.cntx_pop()
            ?};
            {? _akcept & +|POZ.ID
            || OP.cntx_psh();
               OP.index('KON_O'); OP.prefix();
               {? OP.find_key(FINFO.NAROD,POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK)
               || {? -OP.TYP<>'rmk' & -POZ.TID='rmk' | -OP.TYP<>'rmp' & -POZ.TID='rmp' |
                     -OP.TYP='rmk' & -POZ.TID<>'rmk' | -OP.TYP='rmp' & -POZ.TID<>'rmp'
                  || _akcept:=0;
                     _kom:='\nDokument %1 pozycja %2.\n'
                           'Na koncie %3 istnieje rozrachunek o symbolu %4 typu %5.\n'
                           'Niedozwolone użycie typu rozrachunku %6 we wskazanej pozycji.\n'@[DOK.NK,$POZ.POZ,OP.AN,OP.SYM,OP.TYP,POZ.TID]
                  ?}
               ?};
               OP.cntx_pop()
            ?};
            {? _akcept & SSTALE.AO().NAZ<>'Bilans otwarcia'
            || _akcept:=((_kom:=exec('spr_wyr_w','dok_fks','',_a))='')
            ?};
            {? _akcept & -(1+POZ.KOM().KS().TYP)<>'p'
            || {? -(~POZ.STR)='wn' || _wn+=POZ.SUM$2 || _ma+=POZ.SUM$2 ?}
            ?};
            {? _akcept
            || {? DOK.DOK_REJ().M_ODD<>'T' & var_pres('__dOkWtU')<=0
               || POZ.ODD_KS:=POZ.DOK_MOD:=null;
                  POZ.POZ_MOD:=0
               |? ~POZ.DOK_MOD | ~POZ.POZ_MOD | ~POZ.ODD_KS
               || _kom:='Dokument %1\n pozycja %2'
                  '\nNiewypełnione pola związane z dokumentem\ndla wielu jednostek księgowych.'@[DOK.NK,$POZ.POZ];
                  _akcept:=0
               ?}
            ?};
            {? _akcept & _sp_przel & POZ.SP_KS='TT' & POZ.ID<>'' & POZ.WAL=null & POZ.SP_V
            || {? POZ.SUM>0 & POZ.SP_V>POZ.SUM
               || _kom:='Dokument %1\npozycja %2'
                  '\nKwota Split payment VAT większa niż kwota PLN.'@[DOK.NK,$POZ.POZ];
                  _akcept:=0
               |? POZ.SUM<0 & POZ.SP_V<POZ.SUM
               || _kom:='Dokument %1\npozycja %2'
                  '\nKwota Split payment VAT mniejsza niż kwota PLN.'@[DOK.NK,$POZ.POZ];
                  _akcept:=0
               |? POZ.SUM>0 & POZ.SP_V<0 | POZ.SUM<0 & POZ.SP_V>0
               || _kom:='Dokument %1\npozycja %2'
                  '\nKwota Split payment VAT ma inny znak niż kwota PLN.'@[DOK.NK,$POZ.POZ];
                  _akcept:=0
               ?}
            ?};
            {? _akcept || POZ.A:='T'; _akcept:=POZ.put() ?};
            _akcept & POZ.next()
         !};
         {? _akcept
         || {? DOK.SP_WYM='T' & ~DOK.RVAT || DOK.SP_WYM:='N' ?}
         ?};
         {? _akcept & _vat
         || VPOZ.cntx_psh();
            {? VPOZ.first()
            || {! |?
                  _akcept:=~(VPOZ.GRVAT=null);
                  _akcept & VPOZ.next()
               !}
            ?};
            {? ~_akcept || _kom:='Niewypełniona grupa podatkowa w pozycji VAT.'@ ?};
            VPOZ.cntx_pop()
         ?};
         _f2wyl:=DOK.REJ().ROK().FIRMA().TYP='W';
         {? _akcept & DOK.DOK_REJ().SLO().KOD<>'BO' & _wn$2<>_ma$2 & ~exec('spr_mod','dok_fks') & ~_f2wyl
         || _kom:='Dokument %1 jest niezbilansowany na kwotę %2.'@[DOK.NK,$fabs((_wn-_ma)$2)];
            _akcept:=0
         ?};
         {? _akcept & (DOK.DOK_REJ().M_ODD='T' | DOK_REJ.KOR_NAG='T')
         || {? ~exec('dod_mod','dok_fks1',_a)
            || _kom:=KOMT;
               &KOMT;
               _akcept:=0
            ?}
         ?};
         {? _akcept
         || {? ~exec('magwal_spli_poz0','dok_fks_ks')
            || _akcept:=0
            ?}
         ?};
         {? _akcept
         || P_W.index('MW'); P_W.prefix(POZ.ref());
            {? P_W.first()
            || {! |?  _ok:={? P_W.MASKA='mwp'
                           || MAGWALP.prefix(); MAGWALP.seek(P_W.MWUIDREF)
                           |? P_W.MASKA='mwr'
                           || MAGWALR.prefix(); MAGWALR.seek(P_W.MWUIDREF)
                           || 0
                           ?};
                      {? _ok
                      || P_W.next()
                      || _kom:='\nDane w magazynie walut uległy zmianie.\nWykonaj ponownie naliczenie RKB dla konta %1.'@[POZ.KON];
                          _akcept:=0
                      ?}
                !}
            ?}
         ?};
         {? _vat
         ||  _rt:={? 4+-DOK.RVAT().RVAT().KVAT().SYM='sprz' | 5+-KVAT.SYM='_wwspd' || 'odbiorcyw' || 'dostawcyo' ?};
            {? DOK.GVKHSTAT='GZ' & DOK.KH_ODB=null()
            || _kom:='\nDokument dotyczy kontrahenta będącego grupą VAT.\nNie wprowadzono danych %1 w zakładce Kontrahent.'@[_rt-1];
               _akcept:=0
            ?}
         ?};
         {? _akcept
         || {? DOK.RB<>null() || exec('fakskh_auto','faktury_wspolne',DOK.ref()) ?};
            {? _vat
            || exec('czytaj','#stalesys',DOK.DTO,KST,'GRVAT','JST'); KH.cntx_psh();
               {? KST.GRVAT<>null() & KST.GRVAT().GV_OD<=DOK.DTO & (KST.GRVAT().GV_DO=date(0,0,0) | KST.GRVAT().GV_DO>=DOK.DTO)
               || exec('fakskh_auto','faktury_wspolne',DOK.ref(),'G');
                  exec('dodaj_dane_lic','edi_fo_ksef_fks',_rt+1); DOK.GVLISTAT:='GV'
               || DOK.GVLISTAT:='N'
               ?};
               exec('dodaj_dane_2str','edi_fo_ksef_fks',1+_rt);
               KH.cntx_pop();exec('czytaj','#stalesys',,KST,'GRVAT','JST')
            || exec('czytaj','#stalesys',DOK.DTO,KST,'SKROT');
               {? _wew
               || DOK.GVLISTAT:='GL'; DOK.RT:={? DOK_REJ.WEW='Z'
                                              || DOK.CZGV_WYS:=DOK.WYS().TR; DOK.CZGV_ODB:=KST.SKROT; 'N'
                                              || DOK.CZGV_ODB:=DOK.WYS().TR; DOK.CZGV_WYS:=KST.SKROT; 'D'
                                              ?}
               ?}; exec('czytaj','#stalesys',,KST,'SKROT')
            ?};
            _par75:=PAR_SKID.get(75);
            {? _vat & (_par75='T' | (_par75='W' & DOK.SUMWAL=0)) & DOK.DOK_REJ().KOR_NAG<>'T'
            || VPOZ.cntx_psh();
               _sumbr:=0;{? VPOZ.first() || {! |?  _sumbr+=VPOZ.BRUTTO; VPOZ.next !} ?};
               DOK.SUMWAL:=_sumbr$2;
               VPOZ.cntx_pop()
            ?};
            DOK.A:='T'; DOK.ZAA:=exec('usr_zar','dok_fks'); DOK.DAKC:=date();
            {? _zer || DOK.RVAT:=DOK.OKRVAT:=null ?};
            DOK.cntx_psh(); DOK.prefix(); _akcept:=DOK.put(); DOK.cntx_pop()

         ?}
      |? _akcept
      || {? DOK.DOK_REJ().PUDEK='N'
         || _kom:='Dokument %1\nBrak dekretów.'@[DOK.NK];
            _akcept:=0
         || {? _wew
            || exec('czytaj','#stalesys',DOK.DTO,KST,'SKROT');
               DOK.GVLISTAT:='GL'; DOK.RT:={? DOK_REJ.WEW='Z'
                                           || DOK.CZGV_WYS:=DOK.WYS().TR; DOK.CZGV_ODB:=KST.SKROT; 'N'
                                           || DOK.CZGV_ODB:=DOK.WYS().TR; DOK.CZGV_WYS:=KST.SKROT; 'D'
                                           ?};
               exec('czytaj','#stalesys',,KST,'SKROT')
            ?};
            DOK.A:='T'; DOK.ZAA:=exec('usr_zar','dok_fks'); DOK.DAKC:=date();
            {? _zer || DOK.RVAT:=DOK.OKRVAT:=null ?};
            DOK.cntx_psh; DOK.clear; _akcept:=DOK.put(); DOK.cntx_pop
         ?}
      ?};
      {? _dok_mod
      || {? ~_akcept || undo() ?}
      || {? _akcept || _akcept:=end() || undo(); end() ?}
      ?};
      {? _akcept
      || {? _akcept & PAR_SKID.get(80)='T' & PAR_SKID.get(81)='T'
         || {? exec('spr_dok1','dok_fks')
            || exec('tab_dok','dok_fks',1); _podz:=exec('spr_dok','dok_fks',akc_dok);
               DOK.WSK_XD:={? _podz>0 || 'T' || 'N' ?};
               {? _podz=-1
               || _akcept:=0; DOK.A:='N'; DOK.ZAA:=''; DOK.DAKC:=date(0,0,0)
               ?};
               DOK.cntx_psh(); DOK.prefix(); DOK.put(); DOK.cntx_pop();
               exec('tab_dokd','dok_fks')
            |? _a=1
            || FUN.info('Operacja akceptacji podziałów controllingowych nie powiodła się.\n'
                        'Nieprawidłowo zdefiniowano podziały dla controllingu.'@)
            ?}
         ?}
      ?};
      {? _akcept
      || {? ~exec('chk_dok','obe_budz',_a)
         || _akcept:=0; DOK.A:='N'; DOK.WSK_XD:='N'; DOK.ZAA:=''; DOK.DAKC:=date(0,0,0);
            DOK.cntx_psh(); DOK.prefix(); DOK.put(); DOK.cntx_pop()
         ?}
      ?};
      {? _akcept
      || exec('upd_dok','fks_sp');
         exec('upd_dok_jpk_proc','dok_ksef')
      ?}
   ?}
|| _akcept:=0;
   _z:={? _typ<>'BO' || 'zakończony.'@ || 'zaksięgowany.'@ ?};
   _kom:='Dokument %1 jest już %2'@[DOK.NK,_z]
?};
{? _akcept
|| exec('bl_dokum_reg','dokum_zal')
?};
{? _akcept
|| DOK.cntx_psh(); DOK.clear();
   {? DOK.get() & DOK.KSEF='T' & PAR_SKID.get(448)='T'
   || exec('dok_edokument','dok_fks1',0)
   ?};
   DOK.cntx_pop()
?};
::{? _akcept & DOK.KSEF='T' & -FINFO.AKCKSG='t' & DOK.KSEFCDAT=date(0,0,0) & PAR_SKID.get(439)='N'
::|| _kom:='Dokument księgowy nie został jeszcze potwierdzony w KSeF.'@;
::   DOK.A:='N'; DOK.ZAA:=''; DOK.DAKC:=date(0,0,0);
::   DOK.cntx_psh(); DOK.prefix(); DOK.put(); DOK.cntx_pop();
::   _akcept:=0
::?};
{? ~_akcept & _a & _kom<>''
|| _info:="{? var_pres('__kom')>0 || exec('add_kom','#message',_a,7,DOK.NK) || FUN.info(_a) ?}";
   {? _typ<>'BO'
   || _info('Nie można zakończyć rejestracji dokumentu:\n'@+_kom)
   || _info('Nie można zaksięgować dokumentu:\n'@+_kom)
   ?}
?};
_akcept


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Funkcja sluzy do akceptacji dokumentow zrodlowych.
::  OLD: \akceptuj/dok_zrd.fml
::----------------------------------------------------------------------------------------------------------------------
_akcept:=0;
{? ~exec('czy_okr_zam_con','konsolidacja',DOK.sel_size()=0 & var_pres('DokWyk')<=0) & exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER,1)=0
|| DOK.cntx_psh(); REJ.cntx_psh();
   {? DOK.sel_size()=0 & var_pres('DokWyk')<=0
   || {? DOK.r_lock(1,1)
      || {? DOK.get()
         || _ok:=1;
            {? exec('spr_mod','dok_fks')
            || FUN.info('Dokument wtórny do dokumentu dla wielu jednostek księgowych.\nAkceptacja dokumentu nie jest możliwa.'@);
               _ok:=0
            ?};
            {? _ok
            || akc_dok:=1;
               _akcept:=exec('akc_dok1','dok_fks1',1);
               VAR_DEL.delete('akc_dok')
            ?}
         || FUN.info('Operacja akceptacji nie powiodła się.'@)
         ?};
         DOK.r_unlock()
      || FUN.info('Dokument obsługuje inny operator.'@)
      ?}
   || {? DOK.r_lock(1,1)
      || {? DOK.get()
         || {? ~exec('spr_mod','dok_fks')
            || akc_dok:=0;
               {? exec('akc_dok1','dok_fks1',0)
               || _akcept:=1; licz+=1
               || {? DOK.A='T' || byl+=1 ?}
               ?};
               VAR_DEL.delete('akc_dok')
            ?}
         ?};
         DOK.r_unlock()
      || lok+=1
      ?}
   ?};
   DOK.cntx_pop(); REJ.cntx_pop()
?};
_akcept


\besymzew
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Przed redakcja pola DOK.SYM_ZEW
::  OLD: \besymzew/dol_dok.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
DOK_REJ.cntx_psh(); DOK_REJ.prefix();
{? DOK.DOK_REJ & DOK.DOK_REJ().EDSYMZEW='T' & (DOK.DOK_REJ().KOR_NAG<>'T' | POMOC.KNAG=1) || _zwrot:=1 ?};
DOK_REJ.cntx_pop();
beNkSymZew:=DOK.SYM_ZEW;
{? _zwrot || exec('be_edok_fld','dok_fks1') || 0 ?}


\zak_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Kończenie czynności rejestracji dokumentów księgowych
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('inAction')<=0
|| {? DOK.A='T'
   || {? ~DOK.sel_size()
      || FUN.info('Dokument wcześniej zakończony.'@)
      |? var_pres('byl') || byl+=1
      ?}
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='FKS_DKS_DARK';
      _params.AKCJA:='Zakończ';
      _params.UIDREF:=DOK.uidref();
      btnwwyw:=1;
      _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOK',DOK.ref());
      exec('mp_run','#b__box',_params);
      VAR_DEL.delete('btnwwyw')
   ?}
|| exec('akceptuj','dok_fks1')
?}


\akc_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.42]
:: OPIS: Akcja zakończ dla pozycji dokumentów księgowych
::----------------------------------------------------------------------------------------------------------------------
exec('zak_dok','dok_fks1');
{? DOK.get() & DOK.A='T'
|| sel_exit()
?}


\dok_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Zwraca kontrahenta z dokumentu
::   WE: _a - kontrahent
::----------------------------------------------------------------------------------------------------------------------
_kh:=null;
KH.cntx_psh();
{? _a<>'' & +_a<11
|| KH.index('SKR'); KH.prefix(2);
   {? KH.find_key(_a,)
   || _kh:=KH.ref()
   ?}
?};
KH.cntx_pop();
_kh


\obj_dto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Tworzy obiekt na potrzeby kontroli daty wystawienia dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('DokDTO')<=0
|| DokDTO:=obj_new('DTO','KH','zmiana');
   DokDTO.zmiana:="DOK.DTO<>.DTO | DOK.KH<>.KH"
?};
DokDTO.DTO:=DOK.DTO;
DokDTO.KH:=DOK.KH;
1


\dto_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Sprawdzenie czy za dokument był wykonany przelew zbiorczy lub
::       czy dokument nie trafia do istniejącego przelewu zbiorczego
::----------------------------------------------------------------------------------------------------------------------
_ok:=_fld:='';
_add:=-menu_txt()<>'popraw';
{? DOK.RVAT().RVAT().RT='Z' & (_add | var_pres('DokDTO')>0 & DokDTO.zmiana())
|| _tryb:=0;
   {? ~_add
   || _kh:=exec('dok_kh','dok_fks1',DokDTO.KH);
      {? _kh
      || _obj:=exec('is_zb','rozrach_przel',_kh,DokDTO.DTO,1);
         {? _obj.jest & (_obj.OD>DOK.DTO | _obj.DO<DOK.DTO | DokDTO.KH<>DOK.KH)
         || _tryb:=1;
            _txt:='Istnieje przelew zbiorczy split payment \ndla kontrahenta %1\nw okresie %2 - %3.\n'
                  'Dokument był ujęty w tym przelewie.\n\n'@[DokDTO.KH,$_obj.OD,$_obj.DO];
            {? DokDTO.DTO<>DOK.DTO
            || _fld:='DTO'
            || _fld:='KH'
            ?}
         ?}
      ?}
   ?};
   {? _tryb=0
   || _kh:=exec('dok_kh','dok_fks1',DOK.KH);
      {? _kh
      || {? var_pres('_obj')>0 || &_obj ?};
         _obj:=exec('is_zb','rozrach_przel',_kh,DOK.DTO,1,1);
         {? _obj.jest
         || _tryb:=2;
            _txt:='Istnieje przelew zbiorczy split payment \ndla kontrahenta %1\nw okresie %2 - %3.\n'
                  'Dokument nie jest ujęty w tym przelewie.\n\n'@[DOK.KH,$_obj.OD,$_obj.DO];
            _fld:='DTO'
         ?}
      ?}
   ?};
   {? _tryb
   || {? ~FUN.ask('%1 Czy kontynuować?'@[_txt])
      || _ok:=_fld
      ?}
   ?}
?};
_ok


\sp_wym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22_03]
:: OPIS: Formuła kontrolna - początkowa - sprawdzenia znacznika wymaganego split payment w dokumencie księgowym i
::       fakturze sprzedaży
::   WE: [_a] - w przypadku niezgodności: 1-pytanie [0]-informacja
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_pyt:=var_press('_a')>0 & _a;
{? var_press('KOMUNIKAT')>0 & KOMUNIKAT || _pyt+=1 || _pyt:=0 ?};
{? 1+DOK.DOKZRODL='D'
|| FAKS.cntx_psh();
   _ref:=#(4-DOK.DOKZRODL);
   FAKS.use('faktu'+(3+(1-DOK.DOKZRODL)));
   FAKS.prefix();
   {? FAKS.seek(_ref,)
   || {? FAKS.SP_WYM='' || FAKS.SP_WYM:='N' ?};
      {? DOK.SP_WYM<>FAKS.SP_WYM
      || _ok:=0;
         {? _pyt
         || _txt:='Znacznik "Wymagany split payment" na dokumencie księgowym\n'
                  'jest inny niż na fakturze VAT.'@;
            {? _pyt=2
            || _ok:=FUN.ask('%1\nCzy kontynuować?'@[_txt])
            || FUN.info(_txt)
            ?}
         ?}
      ?}
   ?};
   FAKS.cntx_pop()
?};
{? _ok & DOK.RVAT
|| _limit:=exec('limit','fks_sp');
   _poz_wym:=0;
   POZ.cntx_psh();
   POZ.index('DOK'); POZ.prefix(DOK.ref());
   {? POZ.first()
   || {!
      |? {? POZ.SP_WYM='W' || _poz_wym:=1 ?};
         _poz_wym=0 & POZ.next()
      !}
   ?};
   POZ.cntx_pop();
   _vat_wym:=0;
   _brutto:=0;
   VPOZ.cntx_psh();
   VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
   {? VPOZ.first()
   || {!
      |? {? VPOZ.SP_WYM='W' || _vat_wym:=1 ?};
         _brutto+=VPOZ.BRUTTO;
         VPOZ.next()
      !}
   ?};
   VPOZ.cntx_pop();
   _khsp:=1;
   {? DOK.KH<>''
   || _v_kod:='';
      {? DOK.WYS
      || SLO.cntx_psh(); SLO.index('KOD'); SLO.prefix();
         _v_kod:=DOK.WYS().KOD;
         SLO.cntx_pop()
      ?};
      KH.cntx_psh(); KH.index('SKR'); KH.prefix(2,DOK.KH);
      _v:=0;
      {? KH.first()
      || {! |? {? KH.KOD=_v_kod || _v:=1; 0 || KH.next() ?} !};
         {? _v=0 || KH.first(); _v:=1  ?}
      ?};
      {? _v || _khsp:=(KH.TYP<>'I' & KH.TYP<>'R') ?};
      KH.cntx_pop()
   ?};
   {? DOK.SP_WYM<>'T' & _vat_wym & _brutto>_limit & _khsp
   || _ok:=0;
      {? _pyt
      || _txt:='Brak oznaczenia dokumentu opcją "Wymagany split payment".\n\n'
               'Kwota brutto przekracza %1 zł i dokument posiada\n'
               'pozycję VAT z wybraną opcją "Split payment - Wymagany".\n'@[form(_limit,,2,' ,')];
         {? _pyt=2
         || _ok:=FUN.ask('%1\nCzy kontynuować?'@[_txt])
         || FUN.info(_txt)
         ?}
      ?}
   |? DOK.SP_WYM='T' & ~_vat_wym
   || _ok:=0;
      {? _pyt
      || _txt:='Oznaczono dokument opcją "Wymagany split payment".\n'
               'Nie posiada on jednak pozycji VAT z wybraną opcją "Split payment - Wymagany".';
         {? _pyt=2
         || _ok:=FUN.ask('%1\nCzy kontynuować?'@[_txt])
         || FUN.info(_txt)
         ?}
      ?}
   |? DOK.SP_WYM='T' & _brutto<=_limit
   || _ok:=0;
      {? _pyt
      || _txt:='Oznaczono dokument opcją "Wymagany split payment".\n\n'
               'Posiada on pozycje VAT z wybraną opcją "Split payment - Wymagany".\n'
               'Kwota brutto nie przekracza jednak %1 zł.'@[form(_limit,,2,' ,')];
         {? _pyt=2
         || _ok:=FUN.ask('%1\nCzy kontynuować?'@[_txt])
         || FUN.info(_txt)
         ?}
      ?}
   ?};
   {? _ok & _poz_wym<>_vat_wym & DOK.RVAT().RVAT().RT='Z'
   || _ok:=0;
      {? _pyt
      || {? _vat_wym
         || _txt:='Pozycję VAT oznaczono opcją "Wymagany split payment".\n'
                  'Brak natomiast pozycji dokumentu z tą opcją.'
         || _txt:='Pozycję dokumentu oznaczono opcją "Wymagany split payment".\n'
                  'Brak natomiast pozycji VAT z tą opcją.'
         ?};
         {? _pyt=2
         || _ok:=FUN.ask('%1\nCzy kontynuować?'@[_txt])
         || FUN.info(_txt)
         ?}
      ?}
   ?}
?};
_ok


\sp_dto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22_03]
:: OPIS: Formuła kontrolna - początkowa - sprawdzenie czy dokument nie trafia do przelewu zbiorczego
::   WE: [_a] - w przypadku niezgodności: 1-pytanie [0]-informacja
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_pyt:=var_press('_a')>0 & _a;
{? var_press('KOMUNIKAT')>0 & KOMUNIKAT || _pyt+=1 || _pyt:=0 ?};
_kh:=exec('dok_kh','dok_fks1',DOK.KH);
{? _kh
|| _obj:=exec('is_zb','rozrach_przel',_kh,DOK.DTO,1,1);
   {? _obj.jest
   || _ok:=0;
      {? _pyt
      || _txt:='Istnieje przelew zbiorczy split payment \n'
               'dla kontrahenta %1'
               '\nw okresie %2 - %3.\n'
               'Dokument nie jest ujęty w tym przelewie.'@[DOK.KH,$_obj.OD,$_obj.DO];
         {? _pyt=2
         || _ok:=FUN.ask('%1\n\nCzy mimo to zaakceptować dokumentu?'@[_txt])
         || FUN.info(_txt)
         ?}
      ?}
   ?}
?};
_ok


\bv_vpozw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed wyświetleniem pól walutowych w pozycjach VAT dokumentu księgowego
::  OLD: \bv_vpozw/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('be_vpozw','dok_fks1')
|| ''
|| exec('findfnrd','color')
?}


\be_vpozw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redagowaniem pól walutowych w pozycjach VAT dokumentu księgowego
::  OLD: \be_vpozw/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
_result:={? _fld='NETTO_W'
         || VPOZ.WARW=0 | exec('czy_brutto','dok_fks')=1
         |? _fld='BRUTTO_W'
         || VPOZ.WARW=0 | exec('czy_brutto','dok_fks')=0
         || 1
         ?};
{? _result || exec('be_edok_fld','dok_fks1') ?};
_result


\ae_vpozw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redagowaniu pól walutowych w pozycjach VAT dokumentu księgowego
::   WE: [_a] - akronim pola
::       [_b] - Wartość w walucie brutto - 1-tak 0-nie, domyśłnie wg formuły
::  OLD: \ae_vpozw/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
DOK.WAL=FINFO.NAROD;
_fld:={? var_pres('_a')<=0 || cur_afld() || _a ?};
_brutto:={? var_pres('_b')>0 || _b || exec('czy_brutto','dok_fks') ?};
{? _fld='WARW'
|| {? VPOZ.WARW
   || {? _brutto
      || VPOZ.BRUTTO_W:=VPOZ.WARW;
         exec('ae_vpozw','dok_fks1','PR',_brutto)
      || VPOZ.NETTO_W:=VPOZ.WARW;
         exec('ae_vpozw','dok_fks1','PR',_brutto)
      ?}
   ?};
   1
|? _fld='PR'
|| {? _brutto
   || VPOZ.NETTO_W:=(VPOZ.BRUTTO_W/(1+0.01*exec('vat','dok_fks',VPOZ.PR().KOD)))$2;
      VPOZ.VAT_W:=(VPOZ.BRUTTO_W-VPOZ.NETTO_W)$2
   || VPOZ.VAT_W:=(0.01*VPOZ.NETTO_W*exec('vat','dok_fks',VPOZ.PR().KOD))$2;
      VPOZ.BRUTTO_W:=(VPOZ.NETTO_W+VPOZ.VAT_W)$2
   ?}
|? _fld='BRUTTO_W' & VPOZ.NETTO_W$2=0 & VPOZ.VAT_W$2=0
|| fld(fld()$2);
   exec('ae_vpozw','dok_fks1','PR',1)
|? _fld='VAT_W'
|| fld(fld()$2);
   {? VPOZ.NETTO_W$2=0 & VPOZ.VAT_W$2
   || _pr:=exec('vat','dok_fks',VPOZ.PR().KOD);
      VPOZ.NETTO_W:={? _pr || (100*VPOZ.VAT_W/_pr)$2 || 0 ?};
      VPOZ.BRUTTO_W:=(VPOZ.NETTO_W+VPOZ.VAT_W)$2

   |? VPOZ.BRUTTO_W$2=0
   || VPOZ.BRUTTO_W:=(VPOZ.NETTO_W+VPOZ.VAT_W)$2
   ?}
|? _fld='NETTO_W' & VPOZ.NETTO_W$2
|| fld(fld()$2);
   exec('ae_vpozw','dok_fks1','PR')
|? ',NETTO_W,BRUTTO_W,'*(','+_fld+',')
|| fld(fld()$2)
?};
1


\sp_krwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Formuła kontrolna - początkowa - sprawdzenie czy dokument walutowy z wymaganym SP ma uzupełniony rachunek VAT
::   WE: [_a] - w przypadku niezgodności: 1-pytanie [0]-informacja
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_pyt:=var_press('_a')>0 & _a;
{? var_press('KOMUNIKAT')>0 & KOMUNIKAT || _pyt+=1 || _pyt:=0 ?};
{? DOK.SP_WYM='T' & DOK.WAL<>FINFO.NAROD & ~DOK.RB_V
|| _ok:=0;
   {? _pyt
   || _txt:='Dokument ma ustawioną walutę transakcji inną niż PLN\n'
            'i ma ustawiony znacznik "Wymagany split payment"\n'
            'a jednocześnie nie podano rachunku bankowego VAT.'@;
      {? _pyt=2
      || _ok:=FUN.ask(_txt+'\n\nCzy mimo to zaakceptować dokument?'@)
      || FUN.info(_txt)
      ?}
   ?}
?};
_ok


\del_dok_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.xx]
:: OPIS: Formuła usuwająca powiązane rekordy tabeli DOK_JPK dla kontekstu DOK
::----------------------------------------------------------------------------------------------------------------------
DOK_JPK.cntx_psh();
_maska:=ref_name(DOK.ref());
DOK_JPK.use('dokj'+(_maska+4));
DOK_JPK.index('UNIK');
DOK_JPK.prefix(DOK.ref());
{? DOK_JPK.first()
|| {! |?
      DOK_JPK.del()
   !}
?};
DOK_JPK.cntx_pop()


\bv_sw_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Ustala procent wykonania dla pola z informacją o stanie wysyłki załączników do Businesslink
::----------------------------------------------------------------------------------------------------------------------
exec('kh_dod_ini','kontrahent');
_ile:=_ile_b:=_ile_o:=0;
_faks:=exec('find_faks','dok_fks1');
_ref:={? _faks<>null() || $_faks || $DOK.ref() ?};
DOKUM.cntx_psh();
{? exec('is_active','businesslink3')
|| DOKUM.index('BL_2');
   DOKUM.prefix(REF.FIRMA,'T',_ref,'W');
   _ile+=DOKUM.size();
:: dokumentów wysłanych do Businesslinka
   DOKUM.index('STANB');
   DOKUM.prefix(REF.FIRMA,_ref,'T');
   _ile_b+=DOKUM.size();
:: dokumentów odebranych w Businesslinku
   DOKUM.prefix(REF.FIRMA,_ref,'T','T');
   _ile_o:=DOKUM.size();
   {? _ile
   || ROZNE.SW_BL:=$floor(((_ile_b)/_ile)*100)+'%/'+$floor((_ile_o/_ile)*100)+'%'
   || ROZNE.SW_BL:=''
   ?}
|| ROZNE.SW_BL:=''
?};
DOKUM.cntx_pop()


\status_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: sprawdzenie, czy załącznik Businesslink ma status jeden z: Oczekuje na autoryzację / Gotowy do wysłania /
::       Wysłany / Wysłany, niepotwierdzony / Wysłany, potwierdzony
::----------------------------------------------------------------------------------------------------------------------
exec('kh_dod_ini','kontrahent');
_result:=0;
_faks:=exec('find_faks','dok_fks1');
_ref:={? _faks<>null() || $_faks || $DOK.ref() ?};
DOKUM.cntx_psh();
DOKUM.index('BL_2');
:: dokumentów wysłanych do Businesslinka
DOKUM.prefix(REF.FIRMA,'T',_ref);
{? DOKUM.first()
|| _result:=DOKUM.BL_STAT=exec('waiting_auth','zbl_stat')
            | (DOKUM.BL_STAT=exec('send','zbl_stat') & DOKUM.KSEFSTAT*'ERROR'=0)
            | DOKUM.BL_STAT=exec('ready_to_send','zbl_stat')
?};
DOKUM.cntx_pop();
_result


\find_faks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Funkcja zwracająca złączenie do faktury sprzedaży z dokumentu księgowego
::   WY: FAKS.ref
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
{? 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'
|| FAKS.cntx_psh(); FAKS.use('faktu'+(3+(1-DOK.DOKZRODL))); FAKS.prefix();
   _reffak:=#(4-DOK.DOKZRODL);
   {? FAKS.seek(_reffak,FAKS.name) || _ref:=FAKS.ref() ?};
   FAKS.cntx_pop()

?};
_ref


\find_edokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Funkcja zwracająca złączenie do faktury w obiegu dla dokumentu ksiegowego
::   WY: EDOKUM.ref
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
{? DOK.EDOKUM<>''
|| EDOKUM.cntx_psh(); EDOKUM.use(8+DOK.EDOKUM); EDOKUM.prefix();
   {? EDOKUM.seek(DOK.EDOKUM,EDOKUM.name) || _ref:=EDOKUM.ref() ?};
   EDOKUM.cntx_pop()
?};
_ref


\bl_send_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Wysłanie dokumentu do Businesslink podczas księgowanie dokumentu księgowego - uruchamia czynność
::   WE: _a - 0/1 - manualne/automatyczne (domyślnie 0)
::----------------------------------------------------------------------------------------------------------------------
_aut:={? _>0 || _a || 0 ?};
exec('kh_dod_ini','kontrahent');
_result:=0;
_faks:=exec('find_faks','dok_fks1');
{? _faks<>null & exec('is_active','businesslink3')
|| DOKUM.cntx_psh();
   DOKUM.index('BL_2');
   DOKUM.prefix(REF.FIRMA,'T',$_faks,'W');
   {? DOKUM.first()
   || {!
      |? _result:=exec('bl_set_dok','efaktury');
         DOKUM.next()
      !}
   ?};
   DOKUM.cntx_pop()
?};
DOKUM.cntx_psh();
DOKUM.index('BL_2');
DOKUM.prefix(REF.FIRMA,'T',$DOK.ref(),'W');
{? DOKUM.first()
|| {!
   |? _result:=exec('bl_set_dok','efaktury') | _result;
      DOKUM.next()
   !}
?};
DOKUM.cntx_pop();
{? ~_result & ~_aut
|| {? DOK.sel_size()=0
   || FUN.info('Brak dokumentów Businesslink do wysłania.'@)
   || KOMM.add('Brak dokumentów Businesslink do wysłania.'@)
   ?}
?};
~~


\dok_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: dokumenty powiązane z dokmentem księgowym
::----------------------------------------------------------------------------------------------------------------------
_faks:=exec('find_faks','dok_fks1');
_edokum:=exec('find_edokum','dok_fks1');
_ref:={? _faks<>null() || $_faks |? _edokum<>null() || $_edokum || $DOK.ref() ?};
_tab:=ref_tab(_ref);

VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB', 'BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','BLOKUJ','PODGLAD','WIN');
__CTR.BTAB:='DOKUMZ';
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','bloby','dokum_dokz',':',120);
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('sep','#file',1);
__CTR.DND:=1;
__CTR.BLOKUJ:=0;
__CTR.PODGLAD:=1;
__CTR.WIN:='WERBL';

_okno:=DOKUM.grp_make('Powiązane dokumenty'@,,'dokum',,,,,'normal');
_fr:=$('exec(\'gr_dok\',\'dokum\');exec(\'dokum_rfr\',\'bloby\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
DOKUM.grp_sel(_okno,,'WERBL',,_fr,,,,,,,,'maximized_with_title','dokum');
DOKUM.grp_splt(_okno,,'vertical','nlook1');
DOKUM.grp_sel(_okno,DOKTYP,'CECH',,,,,,,,,,'maximized_with_title','doktyp');
DOKUM.grp_splt(_okno,'panel0','horizontal','nlook2',16);
_fb:="{? grp_empty(DOKUM,__CTR.WIN)|| __CTR.BLOKUJ:=1 || __CTR.BLOKUJ:=0 ?};
      exec('bobs_dokumz','bloby')";
_fa:="";
DOKUM.grp_ctr(_okno,DOKUMZ,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_fb,_fa,,'maximized');

_tab.cntx_psh();
KHVZ.EDIT:=1;
DOKUM.index('BL_2');
DOKUM.win_sel(_okno);
DOKUM.timer('sel',_okno,10,"win_disp()");
exec('icon','dokum');
POLA.REFSQL:=_ref;
__REFSQL:=POLA.REFSQL;
DOKUM.prefix(REF.FIRMA,'T',POLA.REFSQL);
_par:='N';
_blink_hide:='W(BIU)';
{? ref_tab(POLA.REFSQL)=FAKS
|| {? exec('FindAndGet','#table',FAKS,POLA.REFSQL,,"SZ",'')='S'
   || _blink_hide:=''
   |? ref_tab(POLA.REFSQL)=DOK
   || _blink_hide:=''
   ?}
?};
DOKUM.select();
KHVZ.EDIT:=0;
&__REFSQL;
_tab.cntx_pop();
''


\edoc_preview
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Formuła przed wyświetleniem dla pola HELP.PREVIEW do podglądu e-dokumentów w formacie pdf
::----------------------------------------------------------------------------------------------------------------------
{? DOK.E_DOC & 'pdf;bmp;png;tiff;tif;jpeg;jpg'*(-DOK.bl_info('E_DOC','EXTENSION'))>0
|| HELP.PREVIEW:=DOK.E_DOC
|| HELP.PREVIEW:=null
?}


\bl_chk_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Sprawdza, czy dokument księgowy zawiera dokumenty businesslink
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_faks:=exec('find_faks','dok_fks1');
{? _faks
|| DOKUM.cntx_psh(); DOKUM.index('BL_2'); DOKUM.prefix(REF.FIRMA,'T',$_faks);
   _result:=DOKUM.first();
   DOKUM.cntx_pop()
?};
_result


\dok_fld_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Przed redakcją pola tabeli DOK - funkcja ogólna
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_afld:='|'+cur_afld()+'|';
_result:={? DOK.DOK_REJ().KOR='N' | DOK.DOK_REJ().KOR_NAG='T'
         || {? exec('kornag_fld2kor_khodb','faktury_nag')*_afld & DOK.KH_ODB=null()
:: pola dotyczące odbiorcy nieredagowalne jeśli nie podano odbiorcy
            || 0
            || 1
            ?}
         || 0
         ?};
{? _result || exec('be_edok_fld','dok_fks1') || 0 ?}


\dokodb_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: wzorzec DOK.KH_ODB
::----------------------------------------------------------------------------------------------------------------------
BEER.KH:=exec('FindInSet','#table','KH','KOD1',DOK.WYS().KOD);
{? cur_nfld()=''
|| exec('kh_odb_f_set','kontrahent','NAZ')
|| exec('kh_odb_f_set','kontrahent')
?};
''


\ae_dokodb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [21.37]
:: OPIS: Po redakcji pola DOK.KH_ODB
::   WE:
::   WY: 1
::--------------------------------------------------------------------------------------------------------------------
{? DOK.KH_ODB
|| DOK.O_NAZ:=DOK.KH_ODB().NAZ;
   DOK.O_MIASTO:=KH_ODB.MIASTO;
   DOK.O_UL:=KH_ODB.UL;
   DOK.O_KPOCZ:=KH_ODB.KPOCZ;
   DOK.O_POCZ:=KH_ODB.POCZ;
   DOK.O_NIP:=KH_ODB.NIP;
   DOK.O_DOM:=KH_ODB.DOM;
   DOK.O_LOKAL:=KH_ODB.LOKAL;
   DOK.O_KRAJ:=KH_ODB.KRAJ;
   DOK.O_KRISO:=KH_ODB.KRAJISO
|| DOK.O_NAZ:='';
   DOK.O_MIASTO:='';
   DOK.O_UL:='';
   DOK.O_POCZ:='';
   DOK.O_KPOCZ:='';
   DOK.O_NIP:='';
   DOK.O_DOM:='';
   DOK.O_LOKAL:='';
   DOK.O_KRAJ:='';
   DOK.O_KRISO:=null()
?};
1


\jpk_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_07]
:: OPIS: Formuła kontrolna - końcowa - sprawdzenie GTU dla uproszczeń JPK (01/07/2021)
::       dla typów dokumentu JPK = RO albo WEW
::   WE: [_a] - w przypadku niezgodności: [1]-pytanie 0-informacja
::       [_b] - 1 - sprawdzanie po okresach VAT / 2 - sprawdzanie okresu, w którym zarejestrowano dokument
::----------------------------------------------------------------------------------------------------------------------
_spr_okr:={? var_press('_b')>0 || _b || 1 ?};
_ret:=1;
_okres:={? _spr_okr=2
        || SSTALE.AO().POCZ>=exec('data_upr','jpk_v')
        || {? DOK.OKRVAT=null()
           || {? VPOZ.first()
              || {!
                 |? {? VPOZ.OKRVAT=null()
                    || {? VPOZ.OKR_C().POCZ>VPOZ.OKR_Z().POCZ
                       || _okr:=VPOZ.OKR_C().POCZ>=exec('data_upr','jpk_v')
                       || _okr:=VPOZ.OKR_Z().POCZ>=exec('data_upr','jpk_v')
                       ?}
                    || _okr:=VPOZ.OKRVAT().POCZ>=exec('data_upr','jpk_v')
                    ?};
                    ~_okr & VPOZ.next()
                 !};
                 _okr
              || 0
              ?}
           || DOK.OKRVAT().POCZ>=exec('data_upr','jpk_v')
           ?}
        ?};
{? _okres
|| DOK_REJ.cntx_psh();
   _txt:='';
   _ask:={? var_press('_a')>0 || _a || 1 ?};
   {? (DOK.JPK_V_T='RO' | DOK.JPK_V_T='WEW') & DOK.JPK_GTU<>''
   || _txt+='Dla dokumentów typu JPK='@+DOK.JPK_V_T+' nie stosuje się oznaczeń Grup towarów i usług.\n'@
   ?};
   {? DOK.JPK_V_T='RO' & DOK.JPK_PROC<>''
   || _txt+='Dla dokumentów typu JPK=RO nie stosuje się oznaczeń procedur szczególnych.\n'@
   ?};
   {? DOK.JPK_PROC*'MPP'
   || _txt+='Oznaczenie procedury "MPP" jest nieaktualne dla deklaracji JPK_V7\n'@
   ?};
   {? var_pres('KOMUNIKAT')>0 & KOMUNIKAT
   || {? _txt<>''
      || {? _ask
         || _ret:=FUN.ask(_txt+'\nCzy mimo to zaakceptować dokument?'@)
         || _ret:=0;
            FUN.info(_txt)
         ?}
      ?}
   || {? _txt<>'' || _ret:=0 ?}
   ?};
   DOK_REJ.cntx_pop()
?};
_ret


\be_pozf_ef_prvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Przed redakcją POZF.EF_PRVAT
::----------------------------------------------------------------------------------------------------------------------
exec('bl_js_wersja','jpk_v');
HELPJPK.SLO_TYP:='PROC_VAT';
exec('bw_jpk_slo','jpk_v');
exec('be_edok_fld','dok_fks1')


\pozf_set_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Ustawia okno redagowania tabeli POZF
::----------------------------------------------------------------------------------------------------------------------
_rt:={? DOK_REJ.SLO().KOD='WEW' & DOK_REJ.WEW<>'N'
     || DOK_REJ.WEW + {? var_pres('TU',POZF)>0 || 'W' || '' ?}
     || exec('typ','jpk_v',DOK.RVAT().RVAT)
     ?};
{? var_pres('PozFRed')<=0
|| _obj:=obj_new(
      'RS','RZ','RSW','RZW',
      'get'
   );
   _obj.RS:='';
   _obj.RZ:='';
   _obj.RZW:='';
   _obj.RSW:='';
   _obj.get:="
      _typ:=_a;
      _red:={? _typ='S' || .RS |? _typ='SW' || .RSW |? _typ='ZW' || .RZW || .RZ ?};
      {? _red=''
      || _first:=_red:='RED_'+_typ;
         _red:=POZF.mk_edit('Pozycja faktury'@,,'#pozfred');
         POZF.win_etab(_red,'Dane podstawowe'@);
         POZF.win_ewin(_red,,_first);
         POZF.win_etab(_red,'Dane dodatkowe'@);
         POZF.win_ewin(_red,,'DODAT');
         _btnzap:=POZF.win_ebtn(_red,'text=%1,align=end'['Zapi&sz'@],\"'key:F2'\");
         _btnan:=POZF.win_ebtn(_red,'text=%1,align=end'['Anuluj'@],\"'key:Esc'\");
         POZF.btn_eopt(_red,_btnzap,'tooltip='+exec('help_red_ok','#window','Z'));
         POZF.btn_eopt(_red,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
         {? _typ='S'
         || .RS:=_red
         |? _typ='SW'
         || .RSW:=_red
         |? _typ='ZW'
         || .RZW:=_red
         || .RZ:=_red
         ?}
      ?};
      _red
   ";
   PozFRed:=_obj
?};
_red:=PozFRed.get(_rt);
POZF.win_edit(_red);
~~


\jpk_proc_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Formuła kontrolna - końcowa - sprawdzenie Procedury EE dla dokumentów po 2022-01-01
::   WE: [_a] - w przypadku niezgodności: [1]-pytanie 0-informacja
::       [_b] - 1 - sprawdzanie po okresach VAT / 2 - sprawdzanie okresu, w którym zarejestrowano dokument
::----------------------------------------------------------------------------------------------------------------------
_spr_okr:={? var_press('_b')>0 || _b || 1 ?};
_ret:=1;
_proc:=','+DOK.JPK_PROC+',';
_okres:=0;
{? _proc*',EE,'
|| VPOZ.cntx_psh();
   VPOZ.index('VDOK');
   VPOZ.prefix(DOK.ref());
   _okres:={? _spr_okr=2
           || SSTALE.AO().POCZ>=date(2022,01,01)
           || {? DOK.OKRVAT=null()
              || {? VPOZ.first()
                 || {!
                    |? {? VPOZ.OKRVAT=null()
                       || {? VPOZ.OKR_C().POCZ>VPOZ.OKR_Z().POCZ
                          || _okr:=VPOZ.OKR_C().POCZ>=date(2022,01,01)
                          || _okr:=VPOZ.OKR_Z().POCZ>=date(2022,01,01)
                          ?}
                       || _okr:=VPOZ.OKRVAT().POCZ>=date(2022,01,01)
                       ?};
                       ~_okr & VPOZ.next()
                    !};
                    _okr
                 || 0
                 ?}
              || DOK.OKRVAT().POCZ>=date(2022,01,01)
              ?}
           ?};
   VPOZ.cntx_pop()
?};
{? _okres
|| _txt:='';
   _ask:={? var_press('_a')>0 || _a || 1 ?};
   {? var_press('JPK_PROC',DOK)>0
   || _proc:=','+DOK.JPK_PROC+',';
      {? _proc*',EE,'
      || _txt+='Oznaczenie procedury "EE" jest nieaktualne dla deklaracji JPK_V7\n'
      ?}
   ?};
   {? var_pres('KOMUNIKAT')>0 & KOMUNIKAT
   || {? _txt<>''
      || {? _ask
         || _ret:=FUN.ask(_txt+'\nCzy mimo to zaakceptować dokument?')
         || _ret:=0;
            FUN.info(_txt)
         ?}
      ?}
   || {? _txt<>'' || _ret:=0 ?}
   ?}
?};
_ret


\be_nrksefdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Przed redakcją DOKPOLA.NRKSEFDK
::----------------------------------------------------------------------------------------------------------------------
0


\bl_dok_statksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Ustala wartość DOK.STATKSEF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_return:='';
{? exec('typ','jpk_v',DOK.RVAT().RVAT)='S'
|| _return:=exec('ksef_donot_applay','zbl_stat');
   {? DOK.KH<>''
   || {? DOK.DOK_REJ().KSEF='T'
      || _return:=exec('before_send','zbl_stat')
      ?}
   ?}
?};
_return


\be_statksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Przed redakcją DOK.STATKSEF
::----------------------------------------------------------------------------------------------------------------------
0


\bl_dok_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Wartość początkowa DOK.KSEF podpowiada stan z DOK_REJ
::----------------------------------------------------------------------------------------------------------------------
DOK.DOK_REJ().KSEF


\be_dok_proc_szcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Przed redakcją DOK.PROC
::----------------------------------------------------------------------------------------------------------------------
dok_proc:=DOK.PROC;
{? exec('edit_proc','dok_fks1')
|| HELPJPK.SLO_TYP:='PROC_FAKS';
   exec('be_edok_fld','dok_fks1')
?}


\ae_dok_proc_szcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Po redakcji DOK.PROC
::----------------------------------------------------------------------------------------------------------------------
:: obecnie procedura jest pusta, przed redakcją była procedura i są pozycje VAT
:: to sprawdzam czy nie jest zmieniona waluta
_dalej:=1;
{? DOK.PROC=null & dok_proc & ~exec('be_dok_proc','!fks_dks_dark') & DOK.JORG<>exec('ust_vwal','kraje',DOK.KRAJ().KOD)
|| FUN.info('W dokumencie wprowadzono pozycje VAT. Należy wybrać procedurę.'@);
   _dalej:=0
?};
{? _dalej & DOK.PROC=null
|| DOK.JORG:=exec('ust_vwal','kraje',DOK.KRAJ().KOD)
?};
_dalej


\be_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Przed redakcją DOK.RB
::   WE: [_a] - 0/1 - wołane dla DOK.RB_V
::----------------------------------------------------------------------------------------------------------------------
_rvat:={? var_pres('_a')=type_of(0) || _a || 0 ?};

_result:={? DOK.KH<>''
         || RACHBANK.KB_5R_BD:='RACHBANK.KB_5R:=RB.get_rbtx(1,SKID_RBK.ref(),SKID_RBK.KRAJ().KODISO)';
            RACHBANK.KB_4R_BD:='RACHBANK.KB_4R:=RB.get_rbtx(2,SKID_RBK.N,SKID_RBK.KRAJ().KODISO)';
            RACHBANK.KB_4R_AE:='SKID_RBK.N:=RB.get_rbel(2,RACHBANK.KB_4R,SKID_RBK.KRAJ().KODISO); '+RACHBANK.KB_4R_BD
               +'; exec(\'ae_rb\',\'rachunki\')';
            RACHBANK.KB_1R_BE:=RACHBANK.KB_1R_F3:=RACHBANK.KB_1R_AE:=RACHBANK.KB_4R_BE:=RACHBANK.KB_4R_F3:='';
            {? _rvat & exec('typ','jpk_v',DOK.RVAT().RVAT)='Z'
            || ROZNE.KKBAN:=1; PAR_WYDR.TABAKR:='KH'; PAR_WYDR.REF:=#FAKS.KH;
               RACHBANK.KB_5R_BD:='RACHBANK.KB_5R:=RB.get_rbtx(1,SKID_RBK.ref())'
            ?};

            _red:='RED_RACH'; {? exec('typ','jpk_v',DOK.RVAT().RVAT)='Z' || _red:='RED_RBO' ?};
            SKID_RBK.win_edit(_red);
            _sel:='SLO_RBL'; {? exec('typ','jpk_v',DOK.RVAT().RVAT)='Z' || _sel:='SLO_RBO' ?};
            SKID_RBK.win_dict(_sel);
            SKID_RBK.win_sel(_sel);
            SKID_RBK.actions(_sel,{? _sel='SLO_RBL' || 'WkT:Wk' || 'T' ?});
            SKID_RBK.win_patt('SZUK');
            RACHBANK.AKTYWNY:='T';
            exec('f_set_rbk','dok_fks1',_rvat);
            1
         || 0
         ?};
{? _result || exec('be_edok_fld','dok_fks1') || 0 ?}


\be_rbv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Przed redakcją DOK.RB_V
::----------------------------------------------------------------------------------------------------------------------
{? exec('be_rb','dok_fks1',1) || exec('be_edok_fld','dok_fks1') || 0 ?}


\bd_ksefcdat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [12.51_148]
:: OPIS: Przed wyświetleniem pola DOK.KSEFCDAT
::  OLD: \bd_ksefcdat/dok_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_cWin:=DOK.win_edit('?');
{? exec('typ','jpk_v',DOK.RVAT().RVAT)='Z'
|| DOK.efld_opt(_cWin,'editable=grayed',DOK,'KSEFCDAT');
   0
|| 1
?}


\ae_ksrf_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [12.51_148]
:: OPIS: Po redakcji DOK_REJ.KSRF i DOK_REJ.KSEF
::  OLD: \ae_ksrf_ksef/dok_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK_REJ.KSEF='T'
|| {? DOK_REJ.KSRF=0
   || DOK_REJ.KSRF:=1;
      FUN.info('Wybrana opcja nie jest zgodna z wartościami dopuszczalnymi w KSeF'@);
      1
   ?}
?};
1


\be_pozf_ef_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Przed redakcją POZF.EF_PROC
::----------------------------------------------------------------------------------------------------------------------
_result:={? exec('edit_proc','dok_fks1')
         || exec('bl_js_wersja','jpk_v');
            HELPJPK.SLO_TYP:='PROC_FAP';
            exec('bw_jpk_slo','jpk_v')
         || 0
         ?};
{? _result || _result:=exec('be_edok_fld','dok_fks1') ?};
_result


\bl_pozfwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Wartość początkowa POZF.WAL podpowiada walutę z nagłówka DOK
::----------------------------------------------------------------------------------------------------------------------
{? POZF.DOK || DOK.WAL || null ?}


\edit_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Czy dozwolona edycja szczególnych procedur transakcji
::   WY: 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
DOK_REJ.cntx_psh();
_edit:=3+DOK.ZAR='FKS' | DOK.DOK_REJ().JPK_EPRO='T';
DOK_REJ.cntx_pop();
_edit


\f_set_rbk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
::   WE: [_a] - 0/1 - wołane dla FAKS.VSKIDRBK
:: OPIS: Ustawia filtr dla rachunków bankowych
::  OLD: \f_set_rbk/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
_rvat:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_sort:='N,TAB';

_kh:={? DOK.KH_ODB().ROZ_NAL='T' || DOK.KH_ODB().KH_LNK || exec('FindInSet','#table','KH','KOD1',DOK.WYS().KOD,,,1) ?};
_where:={? RACHBANK.AKTYWNY<>'' || 'AKTYWNY=\':_a\' and (' || '(' ?};
{? exec('typ','jpk_v',DOK.RVAT().RVAT)='S'
|| _where+=' "TAB"=\''+__Firma+'INFO\'';
   _tyt:=' licencjobiorcy';
   {? ~_rvat
   || {? _kh<>null & exec('ps_65','rachunki')='T'
      || _where+=' or ("TAB"=\''+REF.FIRMA().SYMBOL+'KH2\' and "REF"=\''+$#_kh+'\' ) ';
         _tyt+=', subkonta'
      ?}
   ?};
   _where+=' or ("TAB"=\'KH\' and "REF" in ' +
              '(select reference_num(kh) from kh_dod '+
              'where faktor = \'T\' and firma = \''+$REF.FIRMA+'\'))';
   _tyt+=' i faktorów'
|| _where+=' "TAB"=\'KH\' and "REF"=\''+$#_kh+'\'';
   _tyt:=exec('rbk_kh_tyt','rachunki',{? _kh || KH.SKR || '' ?})
?};
{? RACHBANK.FIRMA
|| _where+=') and FIRMA=:_b'
|| _where+=') and FIRMA is null'
?};
{? _rvat
|| _where+=' and "WAL"=\''+$INFO.NAROD+'\''
?};
SKID_RBK.index('TAB');
SKID_RBK.prefix();
SKID_RBK.f_set(_sort,,_where,RACHBANK.AKTYWNY,RACHBANK.FIRMA);
SKID_RBK.hdr_sel();
SKID_RBK.hdr_sel(_tyt);
~~


\set_dok_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Ustawia flagę czy dokument jest wysyłany do KSeF
::   WE: 0/1 - ustal tylko statusy
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=0 ?};
_set_ksef:=0;
_statksef:='';
{? 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'
|| FAKS.cntx_psh();
   {? FAKS.seek(#(4-DOK.DOKZRODL),'faktu'+(3+(1-DOK.DOKZRODL)),1)
   || _set_ksef:=FAKS.STATKSEF<>'' & FAKS.STATKSEF<>exec('ksef_donot_applay','zbl_stat');
      {? _set_ksef
      || _statksef:=FAKS.STATKSEF;
         {? ~_a || DOK.KSEFAUTH:=FAKS.KSEFAUTH ?}
      ?}
   ?};
   FAKS.cntx_pop()
|| _kh:={? DOK.KH_ODB().ROZ_NAL='T' || DOK.KH_ODB().KH_LNK || exec('FindInSet','#table','KH','KOD1',DOK.WYS().KOD) ?};
   KH_DOD.cntx_psh();
   {? exec('kh_dod_ini','kontrahent',_kh,0)
   || _set_ksef:=KH_DOD.KSEFKH='T' & KH_DOD.KSEFKHOD<=DOK.DTO
   ?};
   KH_DOD.cntx_pop();
   {? _set_ksef || _set_ksef:=exec('FindAndGet','#table',DOK_REJ,DOK.DOK_REJ,,"(DOK_REJ.KSEF='T')",0) ?}
?};
{? _statksef=''
|| {? exec('typ','jpk_v',DOK.RVAT().RVAT)='S'
   ||  _statksef:=exec('ksef_donot_applay','zbl_stat');
      {? _set_ksef
      || _statksef:=exec('before_send','zbl_stat')
      ?}
   ?}
?};
{? ~_a || DOK.KSEF:={? _set_ksef || 'T' || 'N' ?} ?};
DOK.STATKSEF:=_statksef


\dok_edokument
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Dokument księgowy - edokument
::   WE: _a - 1 - dialog / 0 - bez dialogu
::       _b - tworzenie dokumentu bez wysyłki do ksef
::   WY: DOKUM.ref()
::----------------------------------------------------------------------------------------------------------------------
DOKUM.cntx_psh();
_info:="{? var_pres('__kom')>0 || exec('add_kom','#message',_a,7,DOK.NK) || FUN.info(_a) ?}";
_grp:=DOK.sel_size()>0;
_dialog:={? var_pres('_a')>0 || _a || 1 ?};
_dalej:=1;
_dokum:=null();
_manual:={? var_pres('_b')>0 || _b || 0 ?};
{? ~(_manual & DOK.DOK_REJ().KSEF='T') & (DOK.KSEF<>'T' | DOK.MANSKSEF='T')
|| _dalej:=0;
   {? _dialog
   || _info('Akcja e-dokument jest dostępna tylko dla dokumentów wysyłanych do KSeF.'@)
   ?}
|? ~_manual & DOK.EDI_W<>'N'
|| _dalej:=0;
   {? _dialog
   || _info('Dla dokumentu księgowego został już wygenerowany e-dokument.'@)
   ?}
|? ~_manual & ~exec('czy_edokument_bl_ksef_dks','zbl_dok',exec('kh_dokum_dks','zbl_dok',DOK.ref(),1),DOK.DTO)
|| {? _dialog
   || _info('E-dokument jest niedostępny dla wskazanego kontrahenta.'@)
   ?};
   _dalej:=0
?};
{? _dalej
|| _dokum:={? ~_manual || exec('find_dokum','dok_ksef') || null() ?};
   {? _dokum=null() & ~_manual & (1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K')
   || _dalej:=0;
      {? _dialog
      || _info('Nie znaleziono dokumentu Businesslink pochodzącego z dokumentu źródłowego.\n'@+
            'Tworzenie E-dokumentu należy rozpocząć w Dokumentach sprzedaży.'@)
      ?}
   |? _dokum=null()
   || exec('dol_dokum','dok_ksef');
      _dokum:=DOKUM.ref()
   ?}
?};
{? _dalej
|| {? _dokum=null()
   || {? _dialog
      || _info('Wystąpił problem podczas tworzenia E-dokumentu.'@)
      ?}
   |? ~_manual & exec('is_dokum_ksef','zbl_dok',DOKUM.ref())
   || {? DOKUM.BL_STAT=exec('before_send','zbl_stat',DOKUM.ref())
      || _bl_stat:=exec('ready_to_send','zbl_stat');
         exec('FindAndGet','#table',USERS,OPERATOR.USER,,"
              {? exec('get_usersf','users') & USERSF.KSEF_UL='T' || DOKUM.KSEFAUTH:=OPERATOR.USER ?}
           ");
         {? DOKUM.KSEFAUTH=null() || _bl_stat:=exec('waiting_auth','zbl_stat') ?};
         DOKUM.BL_STAT:=_bl_stat;
         DOKUM.BL_TERM:=exec('bl_term','zbl_dok');
         {? DOKUM.put() & _dialog
         || _info('Status dokumentu KSeF został zmieniony na %1.'@[_bl_stat])
         ?}
      || {? _dialog
         || _info('Dokument KSeF został już wygenerowany.'@)
         ?}
      ?}
   || {? -DOK.A<>'t'
      || _msg:=exec('gen_fakskh_edok','dok_fks1');
         {? _msg<>'' || {? _dialog || _info(_msg) ?}; _dalej:=0 ?}
      ?};
      {? _dalej & DOKUM.get()
      || DOKUM.SYM_SYS:=DOK.NK;
         {? DOKUM.DOKR=''
         || DOKUM.DOKR:=$DOK.ref
         ?};
         {? DOKUM.put()
         || _end_kom:=0;
            {? var_pres('__kom')<=0 || _end_kom:=1; exec('ini_kom','#message','Wynik tworzenia e-dokumentu'@) ?};
            exec('bl_act','zbl_dok','PDW',_dokum,_dialog);
            {? _end_kom || exec('end_kom','#message') ?}
         ?}
      ?};
      {? _manual & -DOK.A<>'t' || exec('fakskh_auto_del','faktury_wspolne',DOK.ref()) ?}
::      {? DOK.get() & DOK.EDI_W='N' || exec('bl_dok_unlink','zbl_dok',_dokum) ?}
   ?}
?};
{? ~_dalej || __kom_on:=1 ?};
DOKUM.cntx_pop();
_dokum


\bg_dok_edok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Dokument księgowy - edokument - grupa przed
::----------------------------------------------------------------------------------------------------------------------
exec('ini_kom','#message','Wynik tworzenia e-dokumentów');
sel_nchk()


\ag_dok_edok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Dokument księgowy - edokument - grupa po
::----------------------------------------------------------------------------------------------------------------------
exec('end_kom','#message')


\be_edok_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła przed redakcją dla pól wykorzystywanych w tabelach DOK, DOKPOLA, POZF, VPOZ, które sa wykorzystywane
::       w dokumencie elektronicznym
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt(,)<>'popraw' | var_pres('__dokSpr')>0
|| return(1)
?};
_result:=1;
_tab:=cur_tab(1,1);
_istab:=var_pres('_tab')>0;
{? PAR_SKID.get(441)='T' & _istab & (_tab=DOK | _tab=VPOZ | (_tab=POZF & POZF.DOK))
|| {? exec('status_bl','dok_fks1')
   || _fTab:=cur_tfld(); _fld:=cur_afld();
      _result:={? _fTab=DOK & (_fld='TR' | _fld='DODSLU' | _fld='DODSLO' | _fld='A_VAT')
               || 1
               |? _fTab=DOK & (_fld='PROC') & PAR_SKID.get(449)='N'
               || 1
               |? _fTab=POZF & (_fld='JPK_GTU' | _fld='EF_PROC') & PAR_SKID.get(449)='N'
               || 1
               |? _fTab=POZF & (_fld='UWAGI')
               || 1
               |? _fTab=VPOZ & (_fld='KK' | _fld='K2' | _fld='R' | _fld='K' | _fld='A_VAT' | _fld='OP' | _fld='SP_WYM')
               || 1
               || 0
               ?}
   ?}
?};
{? _istab & _tab=VPOZ & cur_tfld()=VPOZ & cur_afld()='R' || VAR_DEL.delete('__tagvpztip') ?};
_result


\f3_jpkslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Formula na F3 dla pola zmiennej obslugujacego pole o zlaczeniu do JPK_SLO
::   WE: _a - akronim pola [tabela].[pole], ktorego dotyczy wywolanie
::       _b - akronim pola zmiennej, z ktorej formula jest wywolywana
::  OLD: \f3_slu/skid_slu.fml
::----------------------------------------------------------------------------------------------------------------------
_PoleTab:=$(_a);
_PoleZm:=$(_b);
wyb:=0;
_sort:='KOD';
_from:='';
_where:='JPK_SLO.TYP=''PROC_FAKS'' or JPK_SLO.TYP=''PROC_FAP''';

JPK_SLO.cntx_psh();
JPK_SLO.index('PROCDOK');
JPK_SLO.clear();
JPK_SLO.f_set(_sort,_from,_where);
{? _PoleTab()<>null() || JPK_SLO.seek(_PoleTab()) || JPK_SLO.f_first() ?};
JPK_SLO.win_sel('SLO_KSEF');
{? JPK_SLO.select(,1)
|| _PoleTab():=JPK_SLO.ref();
   _PoleZm():=JPK_SLO.KOD
?};
_PoleZm();
JPK_SLO.f_clear();
JPK_SLO.cntx_pop()


\bd_jpkslo_p_szcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Formula przed wyswietleniem pola zmiennej obslugujacego pole o zlaczeniu do JPK_SLO
::   WE: _a - akronim pola [tabela].[pole] ktorego dotyczy wywolanie
::       _b - akronim pola zmiennej, z której formuła jest wywolywana
::  OLD: \pwys_slu/skid_slu.fml
::  OLD: \pwys_slu/slo_slu.fml
::----------------------------------------------------------------------------------------------------------------------
_PoleTab:=$(_a);
_PoleZm:=$(_b);
{? _PoleTab()<>0
|| _Naz:=$(_a+'().KOD');
   _PoleZm():=_Naz()
|| _PoleZm():=''
?};
~~


\ae_jpkslo_p_szcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Formuła przed wyświetleniem pola zmiennej obsługującego pole o złączeniu do JPK_SLO
::   WE: _a - akronim pola [tabela].[pole] którego dotyczy wywołanie
::       _b - akronim pola zmiennej, z której formuła jest wywoływana
::   WY: 1/0 poprawna nazwa słownika/zła nazwa słownika
::  OLD: \pored_slu/skid_slu.fml
::  OLD: \pored_slu/slo_slu.fml
::----------------------------------------------------------------------------------------------------------------------
_PoleTab:=$(_a);
_PoleZm:=$(_b);
_Wynik:=1;
JPK_SLO.index('PROCDOK'); JPK_SLO.clear();

{? _PoleZm()<>''
|| {? JPK_SLO.seek(_PoleTab())
   || _PoleZm():=JPK_SLO.KOD;
      {? _PoleTab<>'' || ($(_a+':=JPK_SLO.ref()'))() ?}
   || _Wynik:=0; FUN.info('Brak słownika o takiej nazwie.'@)
   ?}
|| {? _PoleTab<>'' || ($(_a+':=null'))() ?}
?};
_Wynik


\is_comon_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Funkcja ustala czy przekazany parametrem kod procedury szczególnej jest procedurą wspólną
::   WE: _a - kod procedury szczególnej
::   WY: 1/0 - procedura wspólna
::----------------------------------------------------------------------------------------------------------------------
_kodProc:={? var_pres('_a')=type_of('') || _a || '' ?};
_ret:=0;
{? _kodProc<>''
|| _txt:='';
   _wer:=exec('bl_js_wersja','jpk_v');
   JPK_SLO.cntx_psh();
   JPK_SLO.index('PROCED');
   JPK_SLO.prefix(_wer,'PROC','OSS',);
   {? JPK_SLO.first()
   || {!
      |? _txt:=_txt+'|'+JPK_SLO.KOD;
         JPK_SLO.next()
      !}
   ?};
   JPK_SLO.index('PROCED');
   JPK_SLO.prefix(_wer,'PROC','IOSS',);
   {? JPK_SLO.first()
   || {!
      |? _txt:=_txt+'|'+JPK_SLO.KOD;
         JPK_SLO.next()
      !}
   ?};
   JPK_SLO.cntx_pop();
   _txt:=_txt+(+_txt-1);
   _splAr:=spli_str(_txt,'|');
   {! _i:=1 .. obj_len(_splAr)
   |! _val:=_splAr[_i];
      {? _val=_kodProc
      || _ret:=1
      ?}
   !}
?};
_ret


\be_jpkslo_p_szcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Przed redakcją HELPJPK.JPKSLO_P
::----------------------------------------------------------------------------------------------------------------------
exec('edit_proc','dok_fks1')


\proc_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Formuła kontrolna - końcowa - sprawdzenie zgodności procedur szczególnych KSeF między emag a fiks
::   WE: [_a] - w przypadku niezgodności: [1]-pytanie 0-informacja
::       [_b] - 1 - sprawdzanie po okresach VAT / 2 - sprawdzanie okresu, w którym zarejestrowano dokument
::----------------------------------------------------------------------------------------------------------------------
_ask:={? var_press('_a')>0 || _a || 1 ?};
_spr_okr:={? var_press('_b')>0 || _b || 1 ?};
_ret:=1;
_okres:=0;
_procFaks:=_procDok:=_txt:='';
_procFaks:=DOK.memo_txt(,1,'PROC_LOG');
_procDok:=DOK.JPK_PROC;

{? (1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K') & _procFaks<>''
|| VPOZ.cntx_psh();
   VPOZ.index('VDOK');
   VPOZ.prefix(DOK.ref());
   _okres:={? _spr_okr=2
           || SSTALE.AO().POCZ>=date(2022,01,01)
           || {? DOK.OKRVAT=null()
              || {? VPOZ.first()
                 || {!
                    |? {? VPOZ.OKRVAT=null()
                       || {? VPOZ.OKR_C().POCZ>VPOZ.OKR_Z().POCZ
                          || _okr:=VPOZ.OKR_C().POCZ>=date(2022,01,01)
                          || _okr:=VPOZ.OKR_Z().POCZ>=date(2022,01,01)
                          ?}
                       || _okr:=VPOZ.OKRVAT().POCZ>=date(2022,01,01)
                       ?};
                       ~_okr & VPOZ.next()
                    !};
                    _okr
                 || 0
                 ?}
              || DOK.OKRVAT().POCZ>=date(2022,01,01)
              ?}
           ?};
   VPOZ.cntx_pop()
|? (1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K') & _procDok<>''
|| _txt+='Różne procedury szczególne między systemem Logistyka a Finanse.'@
?};
{? _okres
|| _procFaksAr:=spli_str(_procFaks,',');
   _procDokAr:=spli_str(_procDok,',');
   exec('array_sort','dok_ksef',_procFaksAr);
   _procFaksSrt:=exec('array2str','dok_ksef',_procFaksAr);
   exec('array_sort','dok_ksef',_procDokAr);
   _procDokSrt:=exec('array2str','dok_ksef',_procDokAr);
   {? _procFaksSrt<>_procDokSrt
   || _txt+='Różne procedury szczególne między systemem Logistyka a Finanse.'@
   ?}
?};
{? _txt<>''
|| {? var_pres('KOMUNIKAT')>0 & KOMUNIKAT
   || {? _txt<>''
      || {? _ask
         || _ret:=FUN.ask(_txt+'\nCzy mimo to zaakceptować dokument?'@)
         || _ret:=0;
            FUN.info(_txt)
         ?}
      ?}
   || {? _txt<>'' || _ret:=0 ?}
   ?}
?};
_ret


\be_dplat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MGO
:: OPIS: Przed redakcją daty płatności dokumentu VAT
::  OLD: \be_dplat/dok_zrd2.fml
::----------------------------------------------------------------------------------------------------------------------
_result:={? DOK.DOK_REJ().KOR_NAG='T' & cur_afld='D31'
         || {? DOKN.D31=date(0,0,0) || DOKN.D31:=DOK.D3 ?}; var_pres('__KORNAG')<=0 | POMOC.KNAG=1
         || {? (DOK_REJ.PR='T' & (1+menu_txt(,)='D' | 1+menu_txt(,)='T' | var_pres('dob_dek')>0)) |
               ((DOK_REJ.SLO().KOD='PROSTY' | DOK_REJ.SLO().KOD='WEW') & (DOK_REJ.CZY_DP='' | DOK_REJ.CZY_DP='N'))
            || 0
            || {? DOK.DTO<>date(0,0,0)
               || {? DOK.SP_PL=null
                  || {? cur_afld()='SP_PL' || FUN.info('Nie wybrano sposobu płatności'@) ?}
                  || {? DOK.DOK_REJ().PR<>'T'
                     || _rd:=exec('get_par','slo_slu',DOK.SP_PL,6);
                        DOK.D3:={? _rd='S' || DOK.DOP |? _rd='O' || DOK.D4 || DOK.DTO ?}+#exec('get_par','slo_slu',DOK.SP_PL,2)
                     ?}
                  ?}
               || DOK.D3:=date(0,0,0)
              ?};
               exec('load_tp','dok_fks');
               1
            ?}
         ?};
{? _result || exec('be_edok_fld','dok_fks1') || 0 ?}


\pozf2vpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Tworzy pozycje VAT na podstawie pozycji faktury z wypełnionymi grupami podatkowymi
::----------------------------------------------------------------------------------------------------------------------
POZF.cntx_psh();
{? POZF.first()
|| {? POZF.DOK || _typ:='D' |? POZF.EDOKUM || _typ:='E'  || _typ:='' ?};
   _loop:=1;
   {! |?
      ROZNE.GRVAT:=POZF.GRVAT;
      {? ROZNE.GRVAT<>null
      || VPOZ.index('VDGR');
         VPOZ.prefix(DOK.ref());
         {? ~VPOZ.find_key(ROZNE.GRVAT,POZF.SV)
         || VPOZ.blank(1);
            VPOZ.DOK:=DOK.ref();
            VPOZ.OKRVAT:=DOK.OKRVAT;
            VPOZ.PR:=POZF.SV;
            VPOZ.GRVAT:=ROZNE.GRVAT;
            {? exec('czy_pozf_kor','dok_fks1')=0
            || VPOZ.NETTO:=POZF.WN;
               VPOZ.VAT:=POZF.WV;
               VPOZ.BRUTTO:=POZF.WB
            ?};
            VPOZ.RVAT:=DOK.RVAT;
            VPOZ.SP_WYM:={? DOK.SP_WYM='T' || 'W' || 'T' ?};
            ROZNE.GRVAT:=null;
            exec('ae_grvat','dok_fks');
            {? VPOZ.BRUTTO<>0 || VPOZ.add() ?}
         || {? exec('czy_pozf_kor','dok_fks1')=0
            || VPOZ.NETTO+=POZF.WN;
               VPOZ.VAT+=POZF.WV;
               VPOZ.BRUTTO+=POZF.WB
            ?};
            ROZNE.GRVAT:=null;
            exec('ae_grvat','dok_fks');
            VPOZ.put()
         ?}
      || _loop:=0
      ?};
      _loop & POZF.next()
   !};
   {? _loop=0
   || FUN.emsg('W trakcie tworzenia pozycji vat na podstawie pozycji faktur znaleziono\n'
               'pozycje faktur bez uzupełnionej grupy podatkowej.\n'
               'Należy zweryfikować kartotekę pozycji faktur, uzupełnić i powtórzyć operację.'@)
   || {? _>0 & _a=1  || FUN.info('Zakończono tworzenie pozycji VAT.'@) ?};
      VPOZ.cntx_psh();
      VPOZ.index('VDGR');
      VPOZ.prefix(DOK.ref());
      VPOZ.first();
      VPOZ.cntx_pop();
      {? _>0 & _a=1 & _typ='D' || tab_sel(2,'bottom') ?}
   ?}
|| _loop:=0;
   FUN.info('Brak pozycji faktur, pozycje VAT nie mogły zostać utworzone.'@)
?};
POZF.cntx_pop();
_loop


\get_from_gus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Autmatycznie pobranie danych kontrahenta z GUS do nagłówka dokumentu księgowego
::----------------------------------------------------------------------------------------------------------------------
{? DOK.NIP<>''
|| _snip:=exec('niptostr','#string',DOK.NIP);
   ROZNE.NRNLR:={? 2+_snip='PL' || 2-_snip || _snip ?}
?};
{? +_snip>14
|| FUN.info('Długość numeru NIP kontrahenta nie jest obsługiwana przez GUS.'@)
||    {! |? _snip*'-'<>0
      |! _nip1:=((_snip*'-')-1)+_snip;
         _nip2:=_snip+(+_snip-(_snip*'-'));
         _snip:=_nip1+_nip2
      !};
      ROZNE.NRNLR:=_snip;
      {? (+ROZNE.NRNLR<>9 & +ROZNE.NRNLR<>10 & +ROZNE.NRNLR<>14)
      || FUN.info('Niepoprawny numer NIP.'@);
         return('')
      ?};
      {! _i:=1 // 1 .. +ROZNE.NRNLR
      |! _ascii:=%((_i-1)-ROZNE.NRNLR);
         {? _ascii<48 | _ascii>57
         || FUN.info('Niepoprawny numer NIP lub REGON.'@);
            return('')
         ?}
      !};
      _kh:=exec('danePobierzPelnyRaport','gus_ws',1);
      {? var_press('nazwa') & fexists(nazwa,1)
      || ferase(nazwa,1);
         &nazwa
      ?};
      {? _kh=~~
      || FUN.info('Brak kontrahenta o takim numerze NIP.'@);
         return('')
      ?};
      {? 3+_kh<>'ESC'
      || {? _kh<>''
         || _ok:=0; _pyt:=1;
            _lista:=spli_str(_kh,'\n');
            {? obj_len(_lista)>1
            || _nr:=exec('gus_lista','!zws_par_kkhr',_lista);
               {? _nr
               || _ok:=1;
                  _kh:=_lista[_nr];
                  _pyt:=0
               ?}
            || _ok:=1
            ?};
            {? _ok
            || exec('get_from_gus3','nipyue',_kh,_pyt,0)
            ?}
         ?}
      ?}
?};
''


\spr_roz_nad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła kontrolna sprawdzająca przy księgowaniu, czy płacona kwota nie jest wyższa niż Zobowiązanie/Należność
::       Jeśli tak, operator jest informowany o nadpłacie
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_txt:='';
_rozr:=0;
{? POZ.ID<>''
|| OP.cntx_psh();
   _value:=0;
   _strpoz:='';
   {? POZ.WAL=null
   || {? POZ.WN<>0
      || _value:=POZ.WN;
         _strpoz:='Wn'
      || _value:=POZ.MA;
         _strpoz:='Ma'
      ?};
      {? OP.find_tab('first','SYM',,'=',POZ.ID,'AN',,'=',POZ.KON,'ODD',,'=',POZ.ODD,'STAN',,'=','N')
      || {? _strpoz='Wn' & OP.SMA>0
         || {? OP.SMA<_value
            || _res:=0;
               _txt:='Ma';
               _rozr:=OP.SMA
            ?}
         |? _strpoz='Ma' & OP.SWN>0
         || {? OP.SWN<_value
            || _res:=0;
               _txt:='Wn';
               _rozr:=OP.SWN
            ?}
         ?};
         {? ~_res & DOK.sel_size()=0
         || _komm:='Pozycja %1, rozrachunek %2:\n\n'@[$POZ.POZ,OP.SYM];
            _nag:='Kwota pozycji wyższa od rozliczanego rozrachunku.\n'@;

            _dopr:=(+form(_value,,2,' ,'))-(+form(_rozr,,2,' ,'));
            _poz:='Kwota %1 pozycji: %2%3'@[_strpoz,(11*' '),form(_value,,2,' ,')];
            _sal:='Saldo %1 rozrachunku: %2%3'@[_txt,((2*_dopr+floor(_dopr/2)+2)*' '),form(_rozr,,2,' ,')];
            _dopr:=(+form(_value,,2,' ,'))-(+form((_value-_rozr),,2,' ,'));
            _nad:='Nadpłata: %1%2'@[((2*_dopr+26)*' '),form((_value-_rozr),,2,' ,')];
            _komm+=_nag;
            _komm+=_poz+'\n'+_sal+'\n'+_nad+'\n\n';
            _komm+='Kontynuować?'@;
            _choice:=FUN.ask(_komm);
            {? _choice || _res:=1 ?}
         ?}
      ?}
   || {? POZ.WN_WAL<>0
      || _value:=POZ.WN_WAL;
         _strpoz:='Wn'
      || _value:=POZ.MA_WAL;
         _strpoz:='Ma'
      ?};

      {? OP.find_tab('first','SYM',,'=',POZ.ID,'WAL','KOD','<>','PLN',
                     'AN',,'=',POZ.KON,'ODD',,'=',POZ.ODD,'STAN',,'=','N') &
         OP.WAL().KOD=POZ.WAL().KOD
      || _wal:=OP.WAL().KOD;
         {? _strpoz='Wn' & OP.SMA>0
         || {? OP.SMA<_value
            || _res:=0;
               _txt:='Ma';
               _rozr:=OP.SMA
            ?}
         |? _strpoz='Ma' & OP.SWN>0
         || {? OP.SWN<_value
            || _res:=0;
               _txt:='Wn';
               _rozr:=OP.SWN
            ?}
         ?};
         {? ~_res & DOK.sel_size()=0
         || _komm:='Waluta %1, pozycja %2, rozrachunek %3:\n\n'@[_wal,$POZ.POZ,OP.SYM];
            _nag:='Kwota pozycji wyższa od rozliczanego rozrachunku.\n'@;

            _dopr:=(+form(_value,,2,' ,'))-(+form(_rozr,,2,' ,'));
            _poz:='Kwota %1 pozycji: %2%3'@[_strpoz,(11*' '),form(_value,,2,' ,')];
            _sal:='Saldo %1 rozrachunku: %2%3'@[_txt,((2*_dopr+floor(_dopr/2)+2)*' '),form(_rozr,,2,' ,')];
            _dopr:=(+form(_value,,2,' ,'))-(+form((_value-_rozr),,2,' ,'));
            _nad:='Nadpłata: %1%2'@[((2*_dopr+26)*' '),form((_value-_rozr),,2,' ,')];
            _komm+=_nag;
            _komm+=_poz+'\n'+_sal+'\n'+_nad+'\n\n';
            _komm+='Kontynuować?'@;
            _choice:=FUN.ask(_komm);
            {? _choice || _res:=1 ?}
         ?}
      |? OP.find_tab('first','SYM',,'=',POZ.ID,'AN',,'=',POZ.KON,'ODD',,'=',POZ.ODD,'STAN',,'=','N') &
         (OP.WAL().KOD<>POZ.WAL().KOD | OP.WAL_OB().KOD<>POZ.WAL().KOD)
      || _wal:='';
         {? OP.WAL_OB=null
         || _wal:=OP.WAL().KOD
         || _wal:=OP.WAL_OB().KOD
         ?};
         {? POZ.WN<>0
         || _value:=POZ.WN;
            _strpoz:='Wn'
         || _value:=POZ.MA;
            _strpoz:='Ma'
         ?};
         {? _strpoz='Wn' & OP.SMA>0
         || {? OP.SMA<_value
            || _res:=0;
               _txt:='Ma';
               _rozr:=OP.SMA
            ?}
         |? _strpoz='Ma' & OP.SWN>0
         || {? OP.SWN<_value
            || _res:=0;
               _txt:='Wn';
               _rozr:=OP.SWN
            ?}
         ?};
         {? ~_res & DOK.sel_size()=0
         || _komm:='Pozycja %1, waluta %2.\nRozrachunek %3, waluta %4.\n\n'@[$POZ.POZ,POZ.WAL().KOD,OP.SYM,_wal];
            _nag:='Pozycja i rozrachunek w różnych walutach.\n'@;
            _nag+='Kwota pozycji wyższa od rozliczanego rozrachunku (w przeliczeniu na PLN).\n'@;

            _dopr:=(+form(_value,,2,' ,'))-(+form(_rozr,,2,' ,'));
            _poz:='Kwota %1 pozycji: %2%3'@[_strpoz,(11*' '),form(_value,,2,' ,')];
            _sal:='Saldo %1 rozrachunku: %2%3'@[_txt,((2*_dopr+floor(_dopr/2)+2)*' '),form(_rozr,,2,' ,')];
            _dopr:=(+form(_value,,2,' ,'))-(+form((_value-_rozr),,2,' ,'));
            _nad:='Nadpłata: %1%2'@[((2*_dopr+26)*' '),form((_value-_rozr),,2,' ,')];
            _komm+=_nag;
            _komm+=_poz+'\n'+_sal+'\n'+_nad+'\n\n';
            _komm+='Kontynuować?'@;
            _choice:=FUN.ask(_komm);
            {? _choice || _res:=1 ?}
         ?}
      ?}
   ?};
   {? _rozr=0 & OP.find_tab('first','SYM',,'=',POZ.ID,'AN',,'=',POZ.KON,'ODD',,'=',POZ.ODD,'STAN',,'=','R')
   || _res:=0;
      _komm:='Pozycja %1, rozrachunek %2:\nWskazany rozrachunek jest już rozliczony.\n\nKontynuować?'@[$POZ.POZ,OP.SYM];
      {? DOK.sel_size()=0
      || _choice:=FUN.ask(_komm);
         {? _choice || _res:=1 ?}
      ?}
   ?};
   OP.cntx_pop()
?};
_res


\spr_par_wydruk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formuła sprawdza poprawność podanych przez użytkownika parametrów wydruku.
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
{? cur_tab()=ROZNE
|| {? ROZNE.UT_OKROD().NR>ROZNE.UT_OKRDO().NR
   || FUN.info('Nieprawidłowy zakres okresów obrachunkowych.'@);
      _ret:='UT_OKROD'
   ?}
|? cur_tab()=PAR_WYDR & cur_win()='DZIESKR2'
|| {? ROZNE.UT_OKROD().NR>ROZNE.UT_OKRDO().NR
   || FUN.info('Nieprawidłowy zakres okresów obrachunkowych.'@);
      _ret:='UT_OKROD'
   || {? (PAR_WYDR.DPAR1<ROZNE.UT_OKROD().POCZ | PAR_WYDR.DPAR1>ROZNE.UT_OKROD().KON) & ROZNE.UT_OKROD().NR<>0
      || FUN.info('Data musi się zawierać w wybranym okresie'@);
         _ret:='DPAR1'
      |? (PAR_WYDR.DPAR2<ROZNE.UT_OKRDO().POCZ | PAR_WYDR.DPAR2>ROZNE.UT_OKRDO().KON) & ROZNE.UT_OKRDO().NR<>0
      ||  FUN.info('Data musi się zawierać w wybranym okresie'@);
          _ret:='DPAR2'
      ?}
   ?}
?};
_ret


\ae_utokr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Po redakcja pol ROZNE.UT_OKROD, ROZNE.UT_OKRDO
::  OLD: \be_utokr/par_wydr.fml
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld;
_ret:=1;
{? (cur_tab()=PAR_WYDR & cur_win()='DZIESKR2') | (cur_tab()=ROZNE & (cur_win()='DZIENNI3' | cur_win()='DZIENNI2' ))
|| {? _fld='UT_OKROD' | _fld='UT_OKRDO'
   || {? cur_win()='DZIESKR2'
      || {? _fld='UT_OKROD'
         || OKRO_F.cntx_psh();
            {? ROZNE.UT_OKROD().NR=0
            || OKRO_F.next();
               PAR_WYDR.DPAR1:=OKRO_F.POCZ;
               PAR_WYDR.efld_opt(cur_win(),'editable=grayed',PAR_WYDR,'DPAR1')
            |? ROZNE.UT_OKROD().NR=13
            || OKRO_F.ROK();
               OKRO_F.index('ROK'); OKRO_F.prefix(OKRO_F.ROK,12);
               OKRO_F.first();
               PAR_WYDR.DPAR1:=OKRO_F.KON;
               PAR_WYDR.efld_opt(cur_win(),'editable=grayed',PAR_WYDR,'DPAR1')
            || PAR_WYDR.DPAR1:=ROZNE.UT_OKROD().POCZ;
               PAR_WYDR.efld_opt(cur_win(),'editable=1',PAR_WYDR,'DPAR1')
            ?};
            OKRO_F.cntx_pop()
         || {? ROZNE.UT_OKRDO().NR=0
            || OKRO_F.cntx_psh();
               OKRO_F.next();
               PAR_WYDR.DPAR2:=OKRO_F.POCZ;
               OKRO_F.cntx_pop();
               PAR_WYDR.efld_opt(cur_win(),'editable=grayed',PAR_WYDR,'DPAR2')
            |? ROZNE.UT_OKRDO().NR=13
            || OKRO_F.ROK();
               OKRO_F.index('ROK'); OKRO_F.prefix(OKRO_F.ROK,12);
               OKRO_F.first();
               PAR_WYDR.DPAR2:=OKRO_F.KON;
               PAR_WYDR.efld_opt(cur_win(),'editable=grayed',PAR_WYDR,'DPAR2')
            || PAR_WYDR.DPAR2:=ROZNE.UT_OKRDO().KON;
               PAR_WYDR.efld_opt(cur_win(),'editable=1',PAR_WYDR,'DPAR2')
            ?}
         ?}
      ?}
   ?}
?};
_ret


\spr_vatgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: sprawdza grupę VAT dla pozycji VAT biezacego dokumentu
::       z grupą 'ZInwPodZ'/'ZInwPodS'
::   WY: 0/1
::  OLD: \'spr_dok1v'/dok_fks.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
VPOZ.cntx_psh();
VPOZ.index('VDGK'); VPOZ.prefix(DOK.ref());
_result:=(VPOZ.find_key('ZInwPodZ') | VPOZ.find_key('ZInwPodS'));
VPOZ.cntx_pop();
_result


\ae_kurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Po redagowaniu POMOC.KURSOPIS
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? ~(POMOC.KURSOPIS='Kurs zakupu' | POMOC.KURSOPIS='Kurs sprzedaży' | POMOC.KURSOPIS='Kurs średni' | POMOC.KURSOPIS='')
|| FUN.info('Błędna wartość typu kursu.'@); _wyn:=0
?};
DOK.TYPKRS:={? POMOC.KURSOPIS='Kurs zakupu'
            || 'K'
            |? POMOC.KURSOPIS='Kurs sprzedaży'
            || 'S'
            |? POMOC.KURSOPIS=''
            || ''
            || 'R' ?};
_wyn


\be_kurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed redagowaniu POMOC.KURSOPIS
::----------------------------------------------------------------------------------------------------------------------
{? DOK.BANK<>null
|| 1
?}


\bed_kurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.TYPKRS
::----------------------------------------------------------------------------------------------------------------------
{? DOK.win_edit('?')<>''
|| {? DOK.BANK=null
   || DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'TYPKRS');
      DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'TRKS_V');
      DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'TKRS');
      DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',POMOC,'KURSOPIS')
   || DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'TYPKRS');
      DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',POMOC,'KURSOPIS');
      DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'TRKS_V');
      DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'TKRS')
   ?}
?}; 1


\bed_docp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.E_DOCP
::----------------------------------------------------------------------------------------------------------------------
{? DOK.win_edit('?')<>''
|| {? ~DOK.E_DOC
   || DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'E_DOCP')
   || DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'E_DOCP')
   ?}
?};1


\bed_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.RB i DOK.RB_V
::----------------------------------------------------------------------------------------------------------------------
{? DOK.win_edit('?')<>''
|| {? DOK.KH=''
   || DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'RB');
      DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'RB_V')
   || DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'RB');
      DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'RB_V')
   ?}
?};1


\bed_dz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.DZ
::----------------------------------------------------------------------------------------------------------------------
{? DOK.win_edit('?')<>''
|| {? DOK.REJ
   || {? DOK.REJ().CZY_DZ='T' & var_pres('NOTREDDZ')<0
      || DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'DZ')
      || DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'DZ')
      ?}
   || DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'DZ')
   ?}
?}; 1


\bed_jpk_v_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.JPK_V_T
::----------------------------------------------------------------------------------------------------------------------
_ret:=DOK.DOK_REJ().JPK_ETYP;
{? DOK.win_edit('?')<>''
|| {? _ret='T'
   || DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'JPK_V_T')
   || DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=grayed',DOK,'JPK_V_T')
   ?}
?}; 1


\ae_dodslu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Po redakcji DOK.DODSLU
::----------------------------------------------------------------------------------------------------------------------
{? (var_pres('dokdodslu')>0 & dokdodslu<>DOK.DODSLU) | var_pres('dokdodslu')<=0 || DOK.DODSLO:=null ?};
{? DOK.DODSLU=null
|| DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'DODSLO'); win_disp()
|| DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'DODSLO'); win_disp()
?};
VAR_DEL.delete('dokdodslu');1


\bed_dodslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.DODSLO
::----------------------------------------------------------------------------------------------------------------------
{? DOK.win_edit('?')<>''
|| {? DOK.DODSLU=null
   || DOK.efld_opt(DOK.win_edit('?'),'enable=0, editable=0',DOK,'DODSLO')
   || DOK.efld_opt(DOK.win_edit('?'),'enable=1, editable=1',DOK,'DODSLO')
   ?}
?};1


\ae_vpoz_netto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Po redagowaniu VPOZ.NETTO
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab(1,1);
{? SKID.PR<>''
|| {? 4+_tab.name()='pozv'
   || exec('uzu_wart','dok_fks1')
   ?};
   fld(fld$2)
?};
{? VPOZ.NETTO<>0 & menu_txt<>'Popraw'
|| exec('tagger_tip_vpz','tagger',DOK.WYS,{? 6+DOK.EDOKUM='skid_v' || 'OF' |? 5+DOK.DOKZRODL='dokum' || 'DK' || 'DO' ?},
        {? DOK.DOK_REJ().KOR='T' || 1 || 0 ?})
?};
{? var_pres('__tagvpztip')>0 || win_disp() ?};
1


\uzu_wart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła uzupełnia wartości VPOZ po redakcji pól VAT lub NETTO
::----------------------------------------------------------------------------------------------------------------------
_vat:=VPOZ.VAT;
{? VPOZ.VAT$2=0 & VPOZ.NETTO$2=0
|| 1
|| {? exec('czy_brutto','dok_fks') & cur_afld()<>'NETTO'
   || VPOZ.NETTO:=(VPOZ.BRUTTO/(1+0.01*exec('vat','dok_fks',VPOZ.PR().KOD)))$2;
      VPOZ.VAT:=(VPOZ.BRUTTO-VPOZ.NETTO)$2
   || VPOZ.VAT:=(0.01*VPOZ.NETTO*exec('vat','dok_fks',VPOZ.PR().KOD))$2;
      VPOZ.BRUTTO:=(VPOZ.NETTO+VPOZ.VAT)$2
   ?}
?};
exec('ae_vpozw','dok_fks1');
{? _vat<>VPOZ.VAT || exec('setvatodl','dok_fks') ?};
exec('obl_vwal','dok_fks')


\spr_poz_pln
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Sprawdza złotówkowe pozycje 2??, podczas akceptacji dokumentu w wyciągach bankowych,czy istnieje
::       analogiczny rozrachunek w walucie obcej. Jeżeli tak, to operator jest informowany, że płatność
::       jest w PLN mimo istnienia walutowego rozrachunku.
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh();
_res:=1;
_value:=0;
{? POZ.WN<>0
|| _value:=POZ.WN
|| _value:=POZ.MA
?};
{? POZ.WAL=null & 1+POZ.KON='2'
|| {? OP.find_tab('first','WAL','KOD','<>','PLN','SYM',,'=',POZ.ID,'AN',,'=',POZ.KON,'ODD',,'=',POZ.ODD,'SYM_ROK',,'=',POZ.SYM_ROK)
   || _res:=0;
      {? DOK.sel_size()=0
      || _value:=form(_value,,2,' ');
         _choice:=FUN.ask('Płatność w PLN mimo istnienia rozrachunku w obcej walucie.\nRozrachunek na pozycji %1 na kwotę %2.\nKontynuować?'@[$POZ.POZ,_value],,1);
         {? _choice=1 || _res:=1 ?}
      ?}
   ?}
?};
OP.cntx_pop();
_res


\dok_sum_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Formuła dodająca kwotę pozycji do sumy obrotów WN/MA na dokumencie
::   WE: _a - 1-po add, 2-po put, 0-przed del
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_ks_typ_n:=_ks_typ_o:='';
:: Zmiana konta - ustalanie poprzedniego typu konta
{? _a=2 & (_bkon:=bfld('KON'))<>'' & _bkon<>POZ.KON
|| _bkom:=bfld('KOM');
   KS.cntx_psh();
   {? _bkom=null
   || {? _bkon<>''
      || KS.index('SYM'); KS.prefix(ROK_F.ref(),ROK_F.SYNT+POZ.KON);
         {? ~KS.first() || _ks_typ_o:=-(1+KS.TYP) ?}
      ?}
   || KOM.cntx_psh();
      {? KOM.seek(_bkom,,1)
      || _ks_typ_o:=-(1+KOM.KS().TYP)
      ?};
      KOM.cntx_pop()
   ?};
   KS.cntx_pop()
?};
:: Ustalanie aktualnego typu konta
KS.cntx_psh();
{? POZ.KOM=null
|| {? POZ.KON<>''
   || KS.index('SYM'); KS.prefix(ROK_F.ref(),ROK_F.SYNT+POZ.KON);
      {? KS.first()  || _ks_typ_n:=-(1+KS.TYP) ?}
   ?}
|| _ks_typ_n:=-(1+POZ.KOM().KS().TYP)
?};
KS.cntx_pop();
:: Operacje
{? _ks_typ_n<>'' & _ks_typ_n<>'p'
|| DOK.cntx_psh();
   POZ.DOK();
   {? POZ.STR='Wn'
:: Usuwanie pozycji
   || {? _a=0
      || DOK.SUMWN-=POZ.SUM
:: Poprawianie pozycji
      |? _a=2
      || {? bfld('STR')<>POZ.STR
         || DOK.SUMMA-=bfld('SUM');
            DOK.SUMWN+=POZ.SUM
         |? _bkon<>POZ.KON & (_ks_typ_o='p' | _ks_typ_o='')
         || DOK.SUMWN+=POZ.SUM
         || DOK.SUMWN+=POZ.SUM-bfld('SUM')
         ?}
:: Dodawanie pozycji
      |? _a=1
      || DOK.SUMWN+=POZ.SUM
      ?}
:: Usuwanie pozycji
   || {? _a=0
      || DOK.SUMMA-=POZ.SUM
:: Poprawianie pozycji
      |? _a=2
      || {? bfld('STR')<>POZ.STR
         || DOK.SUMWN-=bfld('SUM');
            DOK.SUMMA+=POZ.SUM
         |? _bkon<>POZ.KON & (_ks_typ_o='p' | _ks_typ_o='')
         || DOK.SUMMA+=POZ.SUM
         || DOK.SUMMA+=POZ.SUM-bfld('SUM')
         ?}
:: Dodawanie pozycji
      |? _a=1
      || DOK.SUMMA+=POZ.SUM
      ?}
   ?};
   DOK.trig_off('*','*');
   DOK.prefix();
   DOK.put();
   DOK.cntx_pop();
   DOK.trig_on('*','*')
:: Usuwanie kwoty podczas zmiany konta z bilansowego na pozabilansowe
|? _a=2 & _bkon<>'' & _bkon<>POZ.KON
|| {? _ks_typ_o<>'' & _ks_typ_o<>'p' & _ks_typ_n='p'
   || DOK.cntx_psh();
      POZ.DOK();
      {? bfld('STR')='Wn'
      || DOK.SUMWN-=bfld('SUM')
      || DOK.SUMMA-=bfld('SUM')
      ?};
      DOK.trig_off('*','*');
      DOK.prefix();
      DOK.put();
      DOK.cntx_pop();
      DOK.trig_on('*','*')
   ?}
?};
~~


\bed_dok_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.ODD
::----------------------------------------------------------------------------------------------------------------------
{? DOK.win_edit('?')<>''
|| {? 3+DOK.win_edit('?')='DOK'
   || DOK.efld_opt(DOK.win_edit('?'),'editable=grayed',DOK,'ODD','OD')
   || DOK.efld_opt(DOK.win_edit('?'),'editable=1',DOK,'ODD','OD')
   ?}
?};1


\bed_dok_rej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.REJ
::----------------------------------------------------------------------------------------------------------------------
{? DOK.win_edit('?')<>''
|| {? 3+DOK.win_edit('?')='DOK'
   || DOK.efld_opt(DOK.win_edit('?'),'editable=grayed',DOK,'REJ','KOD')
   || DOK.efld_opt(DOK.win_edit('?'),'editable=1',DOK,'REJ','KOD')
   ?}
?};
exec('bed_tag_tip','tagger')


\bed_dok_naz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.DOK_REJ
::----------------------------------------------------------------------------------------------------------------------
{? DOK.win_edit('?')<>''
|| {? 3+DOK.win_edit('?')='DOK'
   || DOK.efld_opt(DOK.win_edit('?'),'editable=grayed',DOK,'DOK_REJ','NAZ')
   || DOK.efld_opt(DOK.win_edit('?'),'editable=1',DOK,'DOK_REJ','NAZ')
   ?}
?};
exec('bed_tag_tip','tagger')


\bed_symzew
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.SYM_ZEW
::----------------------------------------------------------------------------------------------------------------------
_czy:=exec('besymzew','dok_fks1');
{? DOK.win_edit('?')<>''
|| {? ~_czy
   || DOK.efld_opt(DOK.win_edit('?'),'editable=grayed',DOK,'SYM_ZEW')
   || DOK.efld_opt(DOK.win_edit('?'),'editable=1',DOK,'SYM_ZEW')
   ?}
?};
1


\bed_wys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.WYS
::----------------------------------------------------------------------------------------------------------------------
_wyn:={? +SLOSLU.SLSLU & (var_pres('__KORNAG')<=0 | POMOC.KNAG=1 | cur_tfld()=DOK)
      || exec('be_edok_fld','dok_fks1')
      || 0
      ?};
{? DOK.win_edit('?')<>''
|| {? _wyn
   || DOK.efld_opt(DOK.win_edit('?'),'editable=1,enable=1',DOK,'WYS','KOD');
      DOK.efld_opt(DOK.win_edit('?'),'editable=1,enable=1',DOK,'WYS','TR')
   || DOK.efld_opt(DOK.win_edit('?'),'editable=grayed',DOK,'WYS','KOD');
      DOK.efld_opt(DOK.win_edit('?'),'editable=grayed',DOK,'WYS','TR')
   ?}
?}; 1


\bed_poz_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem POZ.ODD
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('odd_pred','!fks_dks_dark');
{? POZ.win_edit('?')<>''
|| {? _wyn
   || POZ.efld_opt(POZ.win_edit('?'),'editable=1',POZ,'ODD','OD')
   || POZ.efld_opt(POZ.win_edit('?'),'editable=grayed',POZ,'ODD','OD')
   ?}
?}; 1


\bed_poz_tid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetlenie POZ.TID
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('poz_tid','!fks_dks_dark',1);
{? POZ.win_edit('?')<>''
|| {? _wyn
   || POZ.efld_opt(POZ.win_edit('?'),'editable=1,enable=1',POZ,'TID')
   || POZ.efld_opt(POZ.win_edit('?'),'editable=grayed',POZ,'TID')
   ?}
?};1


\bed_poz_symzew
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem POZ.SYM_ZEW
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('besymzewp','!fks_dks_dark');
{? POZ.win_edit('?')<>''
|| {? _wyn
   || POZ.efld_opt(POZ.win_edit('?'),'editable=1',POZ,'SYM_ZEW')
   || POZ.efld_opt(POZ.win_edit('?'),'editable=grayed',POZ,'SYM_ZEW')
   ?}
?};1


\bed_poz_sloproj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem POZ.SLO_PROJ
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('bepozpro','!fks_dks_dark');
{? POZ.win_edit('?')<>''
|| {? _wyn
   || POZ.efld_opt(POZ.win_edit('?'),'editable=1,enable=1',POZ,'SLO_PROJ','KOD');
      POZ.efld_opt(POZ.win_edit('?'),'editable=1,enable=1',POZ,'SLO_PROJ','TR')
   || POZ.efld_opt(POZ.win_edit('?'),'editable=grayed',POZ,'SLO_PROJ','KOD');
      POZ.efld_opt(POZ.win_edit('?'),'editable=grayed',POZ,'SLO_PROJ','TR')
   ?}
?};1


\bed_poz_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem POZ.SUMW i POZ.KURS
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('wal_poz2','!fks_dks_dark');
{? POZ.win_edit('?')<>''
|| {? _wyn
   || POZ.efld_opt(POZ.win_edit('?'),'editable=1,enable=1',POZ,'SUMW');
      POZ.efld_opt(POZ.win_edit('?'),'editable=1,enable=1',POZ,'KURS')
   || POZ.efld_opt(POZ.win_edit('?'),'editable=grayed',POZ,'SUMW');
      POZ.efld_opt(POZ.win_edit('?'),'editable=grayed',POZ,'KURS')
   ?}
?};1


\bed_nrksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK.NRKSEF
::----------------------------------------------------------------------------------------------------------------------
{? DOK.win_edit('?')<>'' || DOK.efld_opt(DOK.win_edit('?'),'editable=grayed',DOK,'NRKSEF') ?}; 1


\bed_vpoz_grvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem VPOZ.GRVAT
::----------------------------------------------------------------------------------------------------------------------
exec('bed_tag_tip','tagger')


\bed_vpoz_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem VPOZ.R
::----------------------------------------------------------------------------------------------------------------------
exec('bed_tag_tip','tagger')


\bed_vpoz_kk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem VPOZ.KK
::----------------------------------------------------------------------------------------------------------------------
exec('bed_tag_tip','tagger')


\bed_vpoz_k2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem VPOZ.K2
::----------------------------------------------------------------------------------------------------------------------
exec('bed_tag_tip','tagger')


\add_srsr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła umożliwia dodanie środka trwałego na podstawie faktury zakupu
::   WE: _a - 1/0 (środek trwały/niskocenny)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(1);
_obj[1]:=obj_new('PARAMETR','VALUE');
_obj[1].PARAMETR:='DOK';
_dok:=DOK.ref();
_obj[1].VALUE:=_dok;
B_PROC.cntx_psh(); B_PROC.index('ACCEPTED'); B_PROC.prefix('N','T','T',REF.FIRMA,'FIN_RZC_NOW');
{? B_PROC.first() || _version:=B_PROC.VER || _version:='' ?};
B_PROC.cntx_pop();
{? _a
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='FST_EWI_DESN';
   _params.AKCJA:='fkstofst';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   _params.CONTEXT:=obj_new('DOK');
   _params.CONTEXT.DOK:=DOK.ref();
   _params.PROC_START:='T';
   {? _version<>''
   || _params.PROC_SYM:='FIN_RZC_NOW';
      _params.PROC_VER:=_version;
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOK',DOK.ref());
      exec('mp_run','#b__box',_params)
   || FUN.info('Brak zaakceptowanego procesu FIN_RZC_NOW'@)
   ?}
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='FST_EWI_DESR';
   _params.AKCJA:='fkstofst';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   _params.CONTEXT:=obj_new('DOK');
   _params.CONTEXT.DOK:=DOK.ref();
   _params.PROC_START:='T';
   {? _version<>''
   || _params.PROC_SYM:='FIN_RZC_NOW';
      _params.PROC_VER:=_version;
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOK',DOK.ref());
      exec('mp_run','#b__box',_params)
   || FUN.info('Brak zaakceptowanego procesu FIN_RZC_NOW'@)
   ?}
?}


\be_vpoz_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed redagowaniem pola VPOZ.D
::----------------------------------------------------------------------------------------------------------------------
{? VPOZ.DOK().DOK_REJ().VPOZ_D='T'
|| 1
|| 0
?}


\ae_vpoz_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Po redagowaniu pola VPOZ.D
::----------------------------------------------------------------------------------------------------------------------
{? VPOZ.D<>VPOZ.DOK().DOP
|| TKRS.cntx_psh(); KRS.cntx_psh();
   _wart:=VPOZ.KURS;
   _wart2:=VPOZ.KURS_D;
   TKRS.index('TKRS_DT'); TKRS.prefix(DOK.BANK,FINFO.NAROD);
   {? VPOZ.D<>date(0,0,0) & TKRS.find_le(VPOZ.D)
   || KRS.index('KRS_WAL'); KRS.prefix(TKRS.ref(),{? KA.WALUTA<>null || KA.WALUTA || DOK.WAL ?});
      {? KRS.first()
      || _wart:=KRS.SR
      ?}
   ?};
   TKRS.prefix(DOK.BANK,FINFO.NAROD);
   {? VPOZ.D<>date(0,0,0) & TKRS.find_le(VPOZ.D)
   || KRS.index('KRS_WAL'); KRS.prefix(TKRS.ref(),KA.WALUTA);
      {? KRS.first()
      || _wart2:={? DOK.TYPKRS='K'
                  || KRS.KP
                  |? DOK.TYPKRS='R'
                  || KRS.SR
                  |? DOK.TYPKRS='S'
                  || KRS.SP
                  || 0
                  ?}
      ?}
   ?};
   TKRS.cntx_pop(); KRS.cntx_pop();
   {? KA.WALUTA<>null
   || {? VPOZ.D<>date(0,0,0)
      || VPOZ.KURS:={? _wart=0 || _wart2 || _wart ?}
      || VPOZ.KURS:={? ROZNE.KRS2=0 || ROZNE.KRS || ROZNE.KRS2 ?}
      ?}
   ?};
   {? VPOZ.DOK().JORG<>FINFO.NAROD
   || {? VPOZ.D=date(0,0,0)
      || VPOZ.KOS:=ROZNE.KRS2
      || VPOZ.KOS:=_wart
      ?}
   || {? VPOZ.D=date(0,0,0)
      || {? KA.WALUTA<>null || VPOZ.KURS_D:=ROZNE.KRS || VPOZ.KURS_D:=0 ?}
      || {? KA.WALUTA<>null || VPOZ.KURS_D:=_wart2 || VPOZ.KURS_D:=0 ?}
      ?}
   ?}
?};
{? VPOZ.D=date(0,0,0)
|| VPOZ.D:=VPOZ.DOK().DOP
?};
1


\bed_vpoz_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem VPOZ.D
::----------------------------------------------------------------------------------------------------------------------
{? VPOZ.win_edit('?')<>''
|| {? VPOZ.DOK().DOK_REJ().VPOZ_D='T'
   || VPOZ.efld_opt(VPOZ.win_edit('?'),'editable=1',VPOZ,'D')
   || VPOZ.efld_opt(VPOZ.win_edit('?'),'editable=grayed',VPOZ,'D')
   ?}
?};
1


\bed_vd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem DOK_REJ.VPOZ_D
::----------------------------------------------------------------------------------------------------------------------
{? DOK_REJ.VPOZ_D='' || DOK_REJ.VPOZ_D:='N' ?};
DOK_REJ.efld_opt('RED','enable='+$({? 'VAT,SAD'*DOK_REJ.SLO().KOD || 1 || 0 ?}),DOK_REJ,'VPOZ_D')


\be_vd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed redagowaniem DOK_REJ.VPOZ_D
::----------------------------------------------------------------------------------------------------------------------
{? 'VAT,SAD'*DOK_REJ.SLO().KOD=0 || 0 || 1 ?}


\okr_vpoz_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przypisuje VPOZ.OKRVAT na podstawie VPOZ.D
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
OKRO_F.find_tab(,'POCZ',,'<=',VPOZ.D,'KON',,'>=',VPOZ.D,'ROK','FIRMA','=',REF.FIRMA);
VPOZ.OKRVAT:=OKRO_F.ref();
OKRO_F.cntx_pop()


\po_vwarw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RL] [8.50]
:: OPIS: Formula po redakcji pol VPOZ.WARW i VPOZ.KURS
::   WE: [_a] - przeliczyć pola walutowe? [1]-tak 0-nie
::  OLD: \po_vwarw/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
_wal:={? var_pres('_a')>0 || _a || 1 ?};
{? DOK.EDOKUM=''
|| VPOZ.WARW:=VPOZ.WARW$2;
   {? VPOZ.WARW$2<>0 & VPOZ.KURS$6<>0
   || _jedn:=exec('jed_wal','waluty',KA.WALUTA().KOD);
      {? exec('czy_brutto','dok_fks')
      || VPOZ.BRUTTO:=VPOZ.AKCYZA+(VPOZ.WARW*VPOZ.KURS/_jedn)$2;
         exec('set_netto','dok_fks1',1)
      || VPOZ.NETTO:=VPOZ.AKCYZA+(VPOZ.WARW*VPOZ.KURS/_jedn)$2
      ?}
   ?}
?};
{? exec('czy2krs','dok_fks',1) || exec('po_kursd','dok_fks1') ?};
{? _wal || exec('ae_vpozw','dok_fks1') ?};
1


\set_netto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2010]
:: OPIS: Po redakcji wartosci brutto - wypelnia kolumny netto i VAT
::   WE: _a - 0 - jesli netto i vat sa 0
::            1 - zawsze
::  OLD: \set_netto/dok_zrd2.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a | VPOZ.NETTO$2=0 & VPOZ.VAT$2=0
|| VPOZ.NETTO:=(VPOZ.BRUTTO/(1+0.01*exec('vat','dok_fks',VPOZ.PR().KOD)))$2;
   VPOZ.VAT:=(VPOZ.BRUTTO-VPOZ.NETTO)$2;
   exec('setvatodl','dok_fks');
   exec('obl_vwal','dok_fks')
?}


\po_kursd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RL] [8.50]
:: OPIS: Formula po red. pola VPOZ.KURS_D
::  OLD: \po_kursd/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
{? KA.ITAB=0 || VPOZ.KURS_D:=VPOZ.KURS ?};
_jedn:={? KA.WALUTA || exec('jed_wal','waluty',KA.WALUTA().KOD) || 1 ?};
_pr:=exec('vat','dok_fks',VPOZ.PR().KOD);
{? exec('czy_brutto','dok_fks')
|| VPOZ.BRUTTO_D:=VPOZ.AKCYZA+(VPOZ.WARW*VPOZ.KURS_D/_jedn)$2;
   VPOZ.NETTO_D:=(VPOZ.BRUTTO_D/(1+0.01*_pr))$2;
   VPOZ.VAT_D:=(VPOZ.BRUTTO_D-VPOZ.NETTO_D)$2
|| VPOZ.NETTO_D:=VPOZ.AKCYZA+(VPOZ.WARW*VPOZ.KURS_D/_jedn)$2;
   VPOZ.VAT_D:=(0.01*VPOZ.NETTO_D*_pr)$2;
   VPOZ.BRUTTO_D:=(VPOZ.NETTO_D+VPOZ.VAT_D)$2
?};
1


\vpz_d_trigdok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Uzupełnianie VPOZ.D po zmianie DOK.DOP
::   WE: _a - DOK.ref()
::       _b - DOK.DOP
::----------------------------------------------------------------------------------------------------------------------
VPOZ.cntx_psh();
VPOZ.trig_off('*','*');
VPOZ.index('VDOK'); VPOZ.prefix(_a);
{? VPOZ.first()
|| {! |?
      VPOZ.D:=_b;
      VPOZ.put(1);
      VPOZ.next()
   !}
?};
VPOZ.trig_on('*','*');
VPOZ.cntx_pop()


\pozf_d_trigdok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Uzupełnianie POZF.DT po zmianie DOK.DOP
::   WE: _a - DOK.ref()
::       _b - DOK.DOP
::----------------------------------------------------------------------------------------------------------------------
POZF.cntx_psh();
POZF.trig_off('*','*');
POZF.index('DOK'); POZF.prefix(_a);
{? POZF.first()
|| {! |?
      POZF.DT:=_b;
      POZF.put(1);
      POZF.next()
   !}
?};
POZF.trig_on('*','*');
POZF.cntx_pop()


\be_pozf_dt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wartość początkowa POZF.DT
::----------------------------------------------------------------------------------------------------------------------
{? POZF.DOK<>null
|| DOK.DOP
|? POZF.EDOKUM<>null
|| EDOKUM.DOP
|| date()
?}


\chck_dsp_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Sprawdza zgodność daty sprzedaży pozycji VAT i faktury z DOK.DOP po edycji nagłówka
::----------------------------------------------------------------------------------------------------------------------
{? DOK.DSP_WPOZ='T'
|| VPOZ.cntx_psh(); VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
   _same:=1;
   {? VPOZ.first()
   || {! |?
         {? VPOZ.D<>DOK.DOP
         || _same:=0
         ?};
         _same & VPOZ.next()
      !}
   ?};
   {? _same || DOK.DSP_WPOZ:='N' ?};
   VPOZ.cntx_pop()
?};
{? DOK.DSP_WPZF='T'
|| POZF.cntx_psh(); POZF.index('DOK'); POZF.prefix(DOK.ref());
   _same:=1;
   {? POZF.first()
   || {! |?
         {? POZF.DT<>DOK.DOP
         || _same:=0
         ?};
         _same & POZF.next()
      !}
   ?};
   {? _same || DOK.DSP_WPZF:='N' ?};
   POZF.cntx_pop()
?}


\ntc_ksef_auto_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła wyświtlająca powiadomienie o automatycznej rejestracji dokumentu księgowego z Businesslinka
::   WE: _a - DOK.ref() rejestrowanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
DOK.cntx_psh(); DOK.prefix();
{? exec('get','#params',700111,type_of('A'))<>'N' & DOK.seek(_a,ref_name(_a)) & DOK.NRKSEF<>''
|| DOK.put(1);
   _params:=exec('obj_ntab_set','#array',,'LINK',DOK.uidref());
   _app_ident:=exec('app_ident','#ntc');
   _cluster_group:=app_info('cluster_group');
   _link_interm:=exec('link_uri','#system',_params,,_cluster_group,_app_ident);
   _text:='<html>
           <b>Nowy dokument księgowy</b><br><br>
           Symbol dokumentu: <b>%1</b><br>
           Zarejestrowano: <b>%2</b><br>
           Kontrahent: <b>%3</b><br>
           Waluta: <b>%6</b><br>
           Wartość netto: <b>%4</b><br>
           Wartość brutto: <b>%5</b>
           </html>'@[DOK.NK,$DOK.DR,DOK.WYS().TR,form(DOK.NETTO,,2),form(DOK.BRUTTO,,2),DOK.WAL().KOD];

   _bi_prel:=exec('find_keyref_biprel','dokum','FKS_DKS_DARK',DOK.uidref());
   BI_TODO.cntx_psh();
   BI_TODO.index('ELEMENT'); BI_TODO.prefix(_bi_prel);
   _stat:=exec('TODO_WOLNE','#bi_stat');
   {? _bi_prel<>null & BI_TODO.first() & BI_TODO.BI_STAT<>_stat & BI_TODO.size()=1
   || _tabusr:=tab_tmp(,'KOD','STRING[100]','KOD');
      _tabusr.KOD:=BI_TODO.USERS().KOD;
      _tabusr.add()
   || {? _bi_prel<>null
      || _tabusr:=exec('usersAction','#b_desktop',_bi_prel)
      || _tabusr:=~~
      ?}
   ?};
   BI_TODO.cntx_pop();
   _grpkod:='ksefauto'+DOK.uidref();
   ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix(REF.FIRMA,2+(ref_name(DOK.ref())+4));
   {? ROK_F.first()
   || _rokf:=ROK_F.ref()
   || _rokf:=null
   ?};
   ROK_F.cntx_pop();
   {? var_pres('_tabusr')<=0
   || _tabusr:=sql('select distinct USERS.KOD as KOD '+
                   'from USERS join B_USRROL join B_ROLE join B_ACTROL join B_ACTION '+
                   'where B_ACTION.UID=\':_a\' and B_ROLE.FIRMA=:_b','FKS_DKS_DARK',REF.FIRMA)
   ?};
   {? var_pres('_tabusr')>0 & _tabusr.first()
   || USERS.cntx_psh(); USERS.index('USR_KKOD');
      {! |?
         _user:=_tabusr.KOD;
         {? USERS.find_key(_user) || _uref:=USERS.ref() || _uref:=null() ?};
         {? DOK.ODD<>null || _odd_upr:=exec('usr_fjks','b_perm',DOK.ODD,_uref) || _odd_upr:=1 ?};
         OPERATOR.cntx_psh(); SSTALE.cntx_psh();
         OPERATOR.USER:=_uref;
         {? _rokf<>null || SSTALE.AR:=_rokf ?};
         {? DOK.REJ<>null || _rej_upr:=exec('usr_rej','b_perm',DOK.REJ) || _rej_upr:=1 ?};
         OPERATOR.cntx_pop(); SSTALE.cntx_pop();
         {? _uref<>null & exec('get','#params',7012,,_uref)='T' & _odd_upr & _rej_upr
         || _formula:={? exec('get','#params',7011,,_uref)='T'
                      || '{"url": %1}'[json_val(_link_interm)]
                      || '{"url": %1, "delete": %2}'[json_val(_link_interm), json_val(0)]
                      ?};
            _dni:=date()+exec('get','#params',7013,,_uref);
            ntc_send(_user,_app_ident,_cluster_group,'Powiadomienie o nowym dokumencie księgowym z KSeF'@,_text,_grpkod,
                     ,,_dni,'Idź do dokumentu'@,_formula)
         ?};
         _tabusr.next()
      !};
      USERS.cntx_pop()
   ?}
|| 0
?};
DOK.cntx_pop()


\set_win_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Funkcja ustawia okna wertowania tabel: POZ, VPOZ, FAK oraz
::       okna redakcyjne tabel: POZ, FAK
::       w zaleznosci od wartosci parametru _a
::   WE: _a - rodzaj dokumentu
::  OLD: \okna_pw/dok_zrd2.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA); SSTALE.AR();
_dok_mod:=DOK.DOK_REJ().M_ODD='T';
_s_mod:={? _dok_mod || 'M' || '' ?};
{? SSTALE.AO().NAZ='Bilans otwarcia' & ~exec('first_rok_obsz','zam_okr_rok','FKS',ROK_F.ref())
|| ROK_F.prev();
   {? _a='BO'
   || {? PAR_SKID.get(36)='T' & PAR_SKID.get(35)='T'
      || POZ.win_edit('POZ_BO2')
      || POZ.win_edit('POZ_BO')
      ?};
      exec('ok_sel','dok_fks');
      POZ.win_sel('BO'); __ACTSEL:='BO';
      {? -DOK.A='t' | var_press('PozView')>0 & PozView
      || __ACTACT:='pDUK:'; exec('poz_hidactions','dok_fks');
         __ACTDOM:='O:Z'; POZ.actions('BO',__ACTACT,'O:Z',1)
      || __ACTACT:='L:'; exec('poz_hidactions','dok_fks');
         __ACTDOM:='p:Z'; POZ.actions('BO',__ACTACT,'p:Z',1)
      ?}
   |? _a='KOR_BO'
   || {? PAR_SKID.get(36)='T' & PAR_SKID.get(35)='T'
      || POZ.win_edit('POZ_PR'+{? MLEX.FIKSB || 'B' || 'R' ?}+'2')
      || POZ.win_edit('POZ_PR'+{? MLEX.FIKSB || 'B' || 'R' ?})
      ?};
      exec('ok_sel','dok_fks');
      __ACTSEL:='SEL';
      {? -DOK.A='t' | var_press('PozView')>0 & PozView
      || {? exec('dokaut','dok_fks')
         || __ACTACT:='ADvpU:ADr'; exec('poz_hidactions','dok_fks');
            POZ.actions('SEL',__ACTACT,'O:Z',1)
         || __ACTACT:='ADvpUF:ADrF'; exec('poz_hidactions','dok_fks');
            POZ.actions('SEL',__ACTACT,'O:Z',1)
         ?};
         __ACTDOM:='O:Z'
      || {? exec('dokaut','dok_fks')
         || __ACTACT:='k:'; exec('poz_hidactions','dok_fks');
            POZ.actions('SEL',__ACTACT,'D:D',1)
         || __ACTACT:='kF:F'; exec('poz_hidactions','dok_fks');
            POZ.actions('SEL',__ACTACT,'D:D',1)
         ?};
         __ACTDOM:='D:D'
      ?}
   ?}
|| {? (-SSTALE.AO().ZAM<>'t' & exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER,0)=0) & -DOK.A<>'t' & (var_press('PozView')<=0 | ~PozView)
   || {? _a='VAT'
      || {? DOK_REJ.KOR_NAG='T' & 4+DOK.KN='doku'
         || VPOZ.win_sel('SEL_KOR')
         || VPOZ.win_sel('SEL')
         ?};
         exec('ok_vsel','dok_fks');
         __ACTSEL:='VSEL'+_s_mod;
         {? exec('dokaut','dok_fks')
         || __ACTACT:='k:'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'D:D',1)
         || __ACTACT:='kF:F'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'D:D',1)
         ?};
         __ACTDOM:='D:D';
         FAK.win_sel('WER');
         FAK.win_edit('RED2')
      |? _a='SAD'
      || VPOZ.win_sel('IMP');
         exec('ok_vsel','dok_fks');
         __ACTSEL:='VSEL'+_s_mod;
         {? exec('dokaut','dok_fks')
         || __ACTACT:='k:'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'D:D',1)
         || __ACTACT:='kF:F'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'D:D',1)
         ?};
         __ACTDOM:='D:D';
         FAK.win_sel('WER');
         FAK.win_edit('RED')
      |? _a='WEW'
      || VPOZ.win_sel('SEL_WEW');

         exec('ok_vsel','dok_fks');
         __ACTSEL:='VSEL'+_s_mod;
         {? exec('dokaut','dok_fks')
         || __ACTACT:='k:'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'D:D',1)
         || __ACTACT:='kF:F'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'D:D',1)
         ?};
         __ACTDOM:='D:D'
      || exec('ok_sel','dok_fks');
         __ACTSEL:='SEL'+_s_mod;
         {? exec('dokaut','dok_fks')
         || __ACTACT:='k:'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'D:D',1)
         || __ACTACT:='kF:F'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'D:D',1)
         ?};
         __ACTDOM:='D:D'
      ?}
   || {? _a='VAT'
      || VPOZ.win_sel('Z_SEL');
         VPOZ.actions_grayed('Z_SEL',':Z');
         exec('ok_vsel','dok_fks');
         __ACTSEL:='VSEL'+_s_mod;
         {? exec('dokaut','dok_fks')
         || __ACTACT:='ADvpUC:AD'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'O:M',1)
         || __ACTACT:='ADvpUFC:ADF'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'O:M',1)
         ?};
         __ACTDOM:='O:M';
         FAK.win_sel('WER1'); FAK.win_edit('RED2')
      |? _a='SAD'
      || VPOZ.win_sel('Z_IMP');
         VPOZ.actions_grayed('Z_IMP',':Z');
         exec('ok_vsel','dok_fks');
         __ACTSEL:='VSEL'+_s_mod;
         {? exec('dokaut','dok_fks')
         || __ACTACT:='ADvpU:AD'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'O:M',1)
         || __ACTACT:='ADvpU:AD'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'O:M',1)
         ?};
         __ACTDOM:='O:M';
         FAK.win_sel('WER1'); FAK.win_edit('RED')
      |? _a='WEW'
      || VPOZ.win_sel('Z_WEW');
         VPOZ.actions_grayed('Z_WEW','AZ:Z');
         exec('ok_vsel','dok_fks');
         __ACTSEL:='VSEL'+_s_mod;
         {? exec('dokaut','dok_fks')
         || __ACTACT:='ADvpUC:AD'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'O:M',1)
         || __ACTACT:='ADvpUFC:ADF'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'O:M',1)
         ?};
         __ACTDOM:='O:M'
      || exec('ok_sel','dok_fks');
         __ACTSEL:='SEL'+_s_mod;
         {? exec('dokaut','dok_fks')
         || __ACTACT:='ADvpU:ADr'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'O:M',1)
         || __ACTACT:='ADvpUF:ADrF'; exec('poz_hidactions','dok_fks');
            POZ.actions(__ACTSEL,__ACTACT,'O:M',1)
         ?};
         __ACTDOM:='O:M'
      ?}
:: TODO: pozycje DOK w controllingu
::      {? FL.SYSTEM='CONTROL'
::      || {? exec('dokaut','dok_fks')
::         || __ACTACT:='TORZADvpU:ADr'; POZ.actions(__ACTSEL,__ACTACT)
::         || __ACTACT:='TORZADvpUF:ADrF'; POZ.actions(__ACTSEL,__ACTACT)
::         ?}
::      ?}
   ?};
   {? PAR_SKID.get(36)='T' & PAR_SKID.get(35)='T'
   || {? DOK.DOK_REJ().M_ODD='T' & ~MLEX.FIKSB
      || POZ.win_edit('POZ_MOD2')
      || POZ.win_edit('POZ_PR'+{? MLEX.FIKSB || 'B' || 'R' ?}+'2')
      ?}
   || {? DOK.DOK_REJ().M_ODD='T' & ~MLEX.FIKSB
      || POZ.win_edit('POZ_MOD')
      || POZ.win_edit('POZ_PR'+{? MLEX.FIKSB || 'B' || 'R' ?})
      ?}
   ?}
?};
{? var_pres('__ACTSEL')>0 & __ACTSEL<>'' || POZ.win_sel(__ACTSEL) ?};
exec('pozf_set_red','dok_fks1');
1


\grp_sel_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2008]
:: OPIS: Formula wyswietla pozycje dokumentu w okienku grupowym z zakladkami
::  OLD: \grp_sel_poz/dok_zrd4.fml
::----------------------------------------------------------------------------------------------------------------------
_zaklpoz:="
   ROZNE.KRS:=ROZNE.WARW:=ROZNE.WCLO:=0;
   exec('obl_net','dok_fks');
   {? KA.WALUTA<>null & ROZNE.KRS2=0 || ROZNE.KRS2:=exec('ust_krs2','dok_fks',KA.WALUTA) ?};
   {? KA.WALUTA<>null & ROZNE.KRS=0 || ROZNE.KRS:=exec('ust_krs','dok_fks',KA.WALUTA) ?};
   _before:='R'; _after:='';
   {? PAR_SKID.get(80)='N' | var_press('PozView')>0 & PozView || _before+='ZO'; _after+='O' ?};
   _dsv:=exec('slimvat','dok_fks');
   {? KA.WALUTA=null | (_dsv<>date(0,0,0) & DOK.DTO>=_dsv & PAR_SKID.get(104)='T') || _before+='K'; _after+='K' ?};
   _menustr:=_before; {? _after<>'' || _menustr+=(':'+_after) ?};
   _hid:={? PAR_SKID.get(441)='T' & exec('status_bl','dok_fks1') || _hid:='DPUVJ:DVJ' || _hid:='' ?};
   grp_disp(VPOZ,__o_vpoz);
   _vpoz_grey_act:=VPOZ.actions_grayed(__o_vpoz);
   {? _hid=''
   || _hid:=_vpoz_grey_act
   |? _vpoz_grey_act*':'
   || _hid:=STR.gsub(_vpoz_grey_act,':',_hid)
   ?};
   VPOZ.actions_grayed(__o_vpoz,_hid);
   VPOZ.actions(__o_vpoz,_menustr,{? KA.WALUTA<>null || 'K:K' || '' ?},1);
   exec('hide_act','!fks_ksf_zprz')
";
VAR_DEL.delete('ok_nk');
_zakldek:="
   ROZRACH.TABELA:='POZ';
   SLO.win_sel('ONE'); SLO.win_dict('ONE');
   POZ.hdr_sel(); POZ.hdr_sel(' %1'@[DOK.DOK_REJ().NAZ]);
   exec('vpoz_dek','dok_fks');
   exec('hide_act','!fks_ksf_zprz')
";
_b_poz:="exec('suma_dok','dok_fks')";
{? DOK.DOK_REJ().POW='T'
|| korekta:=exec('czy_korekta','dok_fks');
   _tab:={? DOK_REJ.JPK_FA_P='T'
         || __PozTabs:=4;
            POZF
         || __PozTabs:=3;
            FAK
         ?};
   __o_vpoz:=VPOZ.win_sel('?');
   _default:=VPOZ.actions(__o_vpoz);
   _okno:=_tab.grp_make('Pozycje dokumentu: %1'@[DOK.NK],"exec('bw_poz_sel','control',__PozTabs+1,1)",'#tabpoz_wg');
   _tab.grp_edit(_okno,DOK,'RDOKPOZ',,,,,,'maximized');
   _tab.grp_splt(_okno,,'horizontal','bottom',',30%');
   {? DOK_REJ.JPK_FA_P='T'
   || _win_pzf:={? DOK.EDI='P' | DOK.EDI='S' || 'WEREDI' || 'WER' ?};
      POZF.win_sel(_win_pzf);
      POZF.hdr_sel(); POZF.hdr_sel(' w '+{? DOK.WAL || DOK.WAL().KOD || 'PLN' ?});
      POZF.actions(_win_pzf,exec('pozf_actions','dok_fks'));
      {? DOK.A='T' | var_press('PozView')>0 & PozView
      || POZF.actions_grayed(POZF.win_sel('?'),'Z:Z')
      || POZF.actions_grayed(POZF.win_sel('?'))
      ?};
      POZF.index('DOK'); POZF.prefix(DOK.ref());
      POZF.fld_opt(POZF.win_sel('?'),'col_name="%1"'[HELPJPK.KOL_CEN],POZF,'C');
      _tab.grp_sel(_okno,POZF,_win_pzf,'Pozycje faktury'@,_zaklpoz;"",0,0);
      POZF.fld_fml('WN','AFTER_EDIT',"exec('ae_pf_netto','!fks_dks_dark')")
   ?};
   _tab.grp_sel(_okno,FAK,FAK.win_sel('?'),'Powiązania'@,
          "VPOZ.hdr_sel();
           VPOZ.hdr_sel(': %1'@[DOK.DOK_REJ().NAZ]);
           ROZNE.KRS:=exec('ust_krs','dok_fks',DOK.WAL);
           {? DOK.JORG<>FINFO.NAROD
           || ROZNE.KRS2:=exec('ust_krs','dok_fks',DOK.JORG)
           ?};
           ROZNE.WAL:={? DOK.WAL=FINFO.NAROD || ROZNE.KRS:=0; null || DOK.WAL ?};
           FAK.index('FAK'); FAK.prefix(DOK.ref());
           ROZNE.WCLO:=0
          ",0,0,,,,,1,'maximized',,1);
   _tab.grp_sel(_okno,VPOZ,__o_vpoz,'Pozycje VAT'@,_zaklpoz,,,,,,,,'maximized','poz_vsel');
   __o_poz:=POZ.win_sel('?');
   _tab.grp_sel(_okno,POZ,__o_poz,'Dekret'@,_zakldek,,,,_b_poz,,,,'maximized','poz_sel');
   {? PAR_SKID.get(80)='T'
   || _tab.grp_sel(_okno,SKIDXD,'WERV','Podziały controllingowe'@,"exec('hide_act','!fks_ksf_zprz')",,,,"exec('beobskxd','control')",,,,'maximized');
      task_attach('CTR_PCO_DDOK');
      task_attach('CTR_PCO_ZDOK');
      {? exec('lic','#b_domain','CTR')=0
      || task_attach('FKS_DKS_ZDOK')
      ?}
   ?};
   exec('gtu4dok','jpk_v',_tab,_okno,,,,1);
   exec('btn_win_poz','dok_fks',_tab,_okno);
   _tab.win_sel(_okno);
   FAK.index('FAK'); FAK.prefix(DOK.ref());
   {? var_pres('__fks_link')>0 & type_of(__fks_link)=type_of('') & ref_tab(__fks_link)=VPOZ
   || _tab.select(,1,2,,,"VPOZ.seek(__fks_link); VAR_DEL.delete('__fks_link'); win_activate('poz_vsel')")
   |? var_pres('__fks_link')>0 & type_of(__fks_link)=type_of('') & ref_tab(__fks_link)=POZ
   || _tab.select(,1,2,,,"POZ.seek(__fks_link); VAR_DEL.delete('__fks_link'); win_activate('poz_sel')")
   || _tab.select()
   ?};
   VPOZ.actions(__o_vpoz,,_default)
|? DOK.DOK_REJ().SLO().KOD='VAT' | (DOK.DOK_REJ().SLO().KOD='SAD' & DOK_REJ.POW='N')
|| _tab:={? DOK_REJ.JPK_FA_P='T'
         || __PozTabs:=3;
            POZF
         || __PozTabs:=2;
            VPOZ
         ?};
   __o_vpoz:=VPOZ.win_sel('?');
   _default:=VPOZ.actions(__o_vpoz);
   __o_poz:=POZ.win_sel('?');
   {? __o_poz='VSELGM'
   || __o_poz:='VSELM'
   |? __o_poz+1='G'
   || __o_poz:=__o_poz-1
   ?};
   POZF.index('DOK'); POZF.prefix(DOK.ref());
   FAK.index('FAK'); FAK.prefix(DOK.ref());
   _okno:=_tab.grp_make('Pozycje dokumentu: %1'@[DOK.NK],"exec('bw_poz_sel','control',__PozTabs+1,1)",'#tabpoz_wg');
   _tab.grp_edit(_okno,DOK,'RDOKPOZ',,,,,,'maximized');
   _tab.grp_splt(_okno,,'horizontal','bottom',',30%');
   {? DOK_REJ.JPK_FA_P='T'
   || _win_pzf:={? DOK.EDI='P' | DOK.EDI='S' || 'WEREDI' || 'WER' ?};
      POZF.win_sel(_win_pzf);
      POZF.hdr_sel(); POZF.hdr_sel(' w '+{? DOK.WAL || DOK.WAL().KOD || 'PLN' ?});
      POZF.actions(_win_pzf,exec('pozf_actions','dok_fks'));
      {? DOK.A='T' | var_press('PozView')>0 & PozView
      || POZF.actions_grayed(POZF.win_sel('?'),'Z:Z')
      || POZF.actions_grayed(POZF.win_sel('?'))
      ?};
      POZF.fld_opt(POZF.win_sel('?'),'col_name="%1"'[HELPJPK.KOL_CEN],POZF,'C');
      _tab.grp_sel(_okno,POZF,_win_pzf,'Pozycje faktury'@,_zaklpoz;"",,,,,,,1,'maximized',,1);
      _tab.grp_sel(_okno,VPOZ,__o_vpoz,'Pozycje VAT'@,_zaklpoz,,,,,,,0,'maximized','poz_vsel',0);
      POZF.fld_fml('WN','AFTER_EDIT',"exec('ae_pf_netto','!fks_dks_dark')")
   || _tab.grp_sel(_okno,VPOZ,__o_vpoz,'Pozycje VAT'@,_zaklpoz,,,,,,,1,'maximized','poz_vsel',1)
   ?};
   _tab.grp_sel(_okno,POZ,__o_poz,'Dekret'@,_zakldek,,,,_b_poz,,,,'maximized','poz_sel');
   {? PAR_SKID.get(80)='T'
   || _tab.grp_sel(_okno,SKIDXD,'WERV','Podziały controllingowe'@,"exec('hide_act','!fks_ksf_zprz')",,,,"exec('beobskxd','control')",,,,'maximized');
      task_attach('CTR_PCO_DDOK');
      task_attach('CTR_PCO_ZDOK');
      {? exec('lic','#b_domain','CTR')=0
      || task_attach('FKS_DKS_ZDOK')
      ?}
   ?};
   exec('gtu4dok','jpk_v',_tab,_okno,,,,1);
   {? exec('upgrade2325_blbc1','zbl') & DOK.DOK_REJ().KSEF_BC<>0
   || _tab.grp_sel(_okno,DOKUMZBC,'WER1','Rezultat kontroli Businesscheck'@,,,,,"exec('fin_dokumzbc_seek','zbl',DOK)",,,,'maximized','bc_wer')
   ?};
   exec('btn_win_poz','dok_fks',_tab,_okno);
   _tab.win_sel(_okno);
::   _tab.select();
   korekta:=exec('czy_korekta','dok_fks');
   {? var_pres('__fks_link')>0 & type_of(__fks_link)=type_of('') & ref_tab(__fks_link)=VPOZ
   || _tab.select(,1,2,,,"VPOZ.seek(__fks_link); VAR_DEL.delete('__fks_link'); win_activate('poz_vsel')")
   |? var_pres('__fks_link')>0 & type_of(__fks_link)=type_of('') & ref_tab(__fks_link)=POZ
   || _tab.select(,1,2,,,"POZ.seek(__fks_link); VAR_DEL.delete('__fks_link'); win_activate('poz_sel')")
   || _tab.select()
   ?};
   VPOZ.actions(__o_vpoz,,_default)
|? DOK_REJ.SLO().KOD='WEW'
|| _tab:=POZF; __PozTabs:=3;
   __o_vpoz:=VPOZ.win_sel('?');
   _default:=VPOZ.actions(__o_vpoz);
   __o_poz:=POZ.win_sel('?');
   {? __o_poz='VSELGM'
   || __o_poz:='VSELM'
   |? __o_poz+1='G'
   || __o_poz:=__o_poz-1
   ?};
   POZF.index('DOK'); POZF.prefix(DOK.ref());
   FAK.index('FAK'); FAK.prefix(DOK.ref());
   _okno:=_tab.grp_make('Pozycje dokumentu: %1'@[DOK.NK],"exec('bw_poz_sel','control',__PozTabs+1,1)",'#tabpoz_wg');
   _tab.grp_edit(_okno,DOK,'RDOKPOZ',,,,,,'maximized');
   _tab.grp_splt(_okno,,'horizontal','bottom',',30%');
   {? DOK_REJ.JPK_FA_P='T'
   || _win_pzf:={? DOK.EDI='P' | DOK.EDI='S' || 'WEREDI' || 'WER' ?};
      POZF.win_sel(_win_pzf);
      POZF.hdr_sel(); POZF.hdr_sel(' w '+{? DOK.WAL || DOK.WAL().KOD || 'PLN' ?});
      POZF.actions(_win_pzf,exec('pozf_actions','dok_fks'));
      {? DOK.A='T' | var_press('PozView')>0 & PozView
      || POZF.actions_grayed(POZF.win_sel('?'),'Z:Z')
      || POZF.actions_grayed(POZF.win_sel('?'),'G')
      ?};
      POZF.fld_opt(POZF.win_sel('?'),'col_name="%1"'[HELPJPK.KOL_CEN],POZF,'C');
      _tab.grp_sel(_okno,POZF,_win_pzf,'Pozycje faktury'@,_zaklpoz;"",,,,,,,1,'maximized',,1);
      _tab.grp_sel(_okno,VPOZ,__o_vpoz,'Pozycje VAT'@,_zaklpoz,,,,,,,0,'maximized','poz_vsel',0);
      POZF.fld_fml('WN','AFTER_EDIT',"exec('ae_pf_netto','!fks_dks_dark')")
   || _tab.grp_sel(_okno,VPOZ,__o_vpoz,'Pozycje VAT'@,_zaklpoz,,,,,,,1,'maximized','poz_vsel',1)
   ?};
   _tab.grp_sel(_okno,POZ,__o_poz,'Dekret'@,_zakldek,,,,_b_poz,,,,'maximized','poz_sel');
   {? PAR_SKID.get(80)='T'
   || _tab.grp_sel(_okno,SKIDXD,'WERV','Podziały controllingowe'@,"exec('hide_act','!fks_ksf_zprz')",,,,"exec('beobskxd','control')",,,,'maximized');
      task_attach('CTR_PCO_DDOK');
      task_attach('CTR_PCO_ZDOK');
      {? exec('lic','#b_domain','CTR')=0
      || task_attach('FKS_DKS_ZDOK')
      ?}
   ?};
   exec('btn_win_poz','dok_fks',_tab,_okno);
   _tab.win_sel(_okno);
::   _tab.select();
   korekta:=exec('czy_korekta','dok_fks');
   {? var_pres('__fks_link')>0 & type_of(__fks_link)=type_of('') & ref_tab(__fks_link)=VPOZ
   || _tab.select(,1,2,,,"VPOZ.seek(__fks_link); VAR_DEL.delete('__fks_link'); win_activate('poz_vsel')")
   |? var_pres('__fks_link')>0 & type_of(__fks_link)=type_of('') & ref_tab(__fks_link)=POZ
   || _tab.select(,1,2,,,"POZ.seek(__fks_link); VAR_DEL.delete('__fks_link'); win_activate('poz_sel')")
   || _tab.select()
   ?};
   VPOZ.actions(__o_vpoz,,_default)
|| korekta:=exec('czy_korekta','dok_fks');
   __PozTabs:=1;
   exec('suma_dok','dok_fks');
   POZ.hdr_sel(' %1'@[DOK.DOK_REJ().NAZ]);
   __o_poz:=POZ.win_sel('?');
   {? __o_poz<>'BO'
   || {? __o_poz='SELGM'
      || __o_poz:='SELM'
      |? __o_poz+1='G'
      || __o_poz:=__o_poz-1
      ?}
:: TODO: Obsługa pozycji w Controllingu
::      {? FL.SYSTEM='CONTROL'
::      || _before:='NATORZ'; _after:='AZ'; _dom1:='P'; _dom2:='D'
::      ?}
   ?};
   _okno:=DOK.grp_make('Pozycje dokumentu: %1'@[DOK.NK],"exec('bw_poz_sel','control',2,1)",'#tabpoz_wg');
   DOK.grp_edit(_okno,,'RDOKPOZ',,,,,,'maximized');
   DOK.grp_splt(_okno,,'horizontal','bottom',',30%');
   DOK.grp_sel(_okno,POZ,__o_poz,'Dekret'@,,,,,,,,,'maximized','dok_poz_sel',1);
   {? PAR_SKID.get(80)='T'
   || DOK.grp_sel(_okno,SKIDXD,'WER','Podziały controllingowe'@,,,,,,,,,'maximized',,);
      task_attach('CTR_PCO_DDOK');
      task_attach('CTR_PCO_ZDOK');
      {? exec('lic','#b_domain','CTR')=0
      || task_attach('FKS_DKS_ZDOK')
      ?}
   ?};
   exec('btn_win_poz','dok_fks',DOK,_okno);
   DOK.win_sel(_okno);
   exec('set_var','control');
   SUM.fld_fml('SUMWN','BEFORE_DISPLAY',"exec('suma_dok_new','dok_fks')");
   {? var_pres('__fks_link')>0 & type_of(__fks_link)=type_of('') & ref_tab(__fks_link)=POZ
   || DOK.select(,1,2,,,"POZ.seek(__fks_link); VAR_DEL.delete('__fks_link'); win_activate('dok_poz_sel')")
   || DOK.select()
   ?}
?};
ROZRACH.TABELA:='DOK';
VAR_DEL.delete('__o_vpoz','__o_poz','korekta')


\srsr_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Tworzy okienko tymczasowe z podglądem środków trwałych powiązanych przez OT z dokumentem zakupu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__SRSROT')<=0 || exec('srsr_tab','dok_fks1') ?};
_win:=__SRSROT.mk_sel('Środki powiązane z dokumentem'@,'T',0,'srsrfromdok',,,,0,'U','T',,,,'normal');
__SRSROT.win_fld(_win,,'GR',,,6,,1,'Grupa'@);
__SRSROT.win_fld(_win,,'NRI',,,20,,1,'Numer inwentarzowy'@);
__SRSROT.win_fld(_win,,'NST',,,30,,1,'Nazwa'@);
__SRSROT.win_fld(_win,,'OD',,,14,,1,'J. księgowa'@);
__SRSROT.win_fld(_win,,'SYMBOL',,,15,,1,'J. organizacyjna'@);
__SRSROT.win_fld(_win,,'DE',,,15,,1,'W eksploatacji'@);
__SRSROT.win_fld(_win,,'OT',,,20,,1,'Sym. dokumentu OT'@);
__SRSROT.win_fld(_win,,'OTDATE',,,15,,1,'Data wystawienia OT'@);
__SRSROT.win_fld(_win,,'R',,,10,,1,'Rodzaj śr.'@);

__SRSROT.win_act(_win,0,'Formuła','&Idź do środka'@,,'Zobacz środek w kartotece'@,
                 "exec('srsr_link','dok_fks1',__SRSROT.REF)",,1);
__SRSROT.win_act(_win,0,'Formuła','Zamknij'@,,'Zobacz środek w kartotece'@,
                 "exec('sel_exit_','#window')");
__SRSROT.win_act(_win,1,'Formuła','Zamknij'@,,'Zobacz środek w kartotece'@,
                 "exec('sel_exit_','#window')");
__SRSROT.win_act(_win,0,'Kolejność');

__SRSROT.win_btn(_win,'text=&Idź do środka,panel=right,align=begin'@,'menu:I');
__SRSROT.win_btn(_win,'text=&Zamknij,panel=right,align=begin'@,'menu:Z');

_win


\srsr_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Tworzy tabelę tymczasową środków trwałych powiązanych przez OT z dokumentem zakupu
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__SRSROT');
__SRSROT:=tab_tmp(4,'NRI','STRING[18]','Numer inwentarzowy',
                   'NST','STRING[100]','Nazwa',
                   'GR','STRING[15]','Grupa',
                   'OD','STRING[8]','J.księgowa',
                   'SYMBOL','STRING[16]','J. organizacyjna',
                   'DE','DATE','W eksploatacji',
                   'OT','STRING[30]','Dokument OT',
                   'OTDATE','DATE','Data wystawienia OT',
                   'REF','STRING[16]','Ref',
                   'R','STRING[10]','Rodzaj środka');
__SRSROT.fld_attr('REF',2,2);
__SRSROT


\srsr_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wypełnia tabelę tymczasową środków trwałych powiązanych przez OT z dokumentem zakupu
::   WE: _a - DOK.uidref()
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__SRSROT')<=0 || exec('srsr_tab','dok_fks1') ?};
_sql:='select distinct SRSR.NRI, SRSR.NST, SRSR.REFERENCE as REF, TAM.GR, ODD.OD, UD_SKL.SYMBOL, SRSR.DE, '+
             'SRDO.SYMBOL as OT, SRDO.DW as OTDATE, SRSR.R '+
      'from SRDZ '+
           'join SRDO using(SRDZ.SRDO,SRDO.REFERENCE) '+
           'join SRSR using(SRDO.SRSR,SRSR.REFERENCE) '+
           'join TAM using(SRSR.GR,TAM.REFERENCE) '+
           'join ODD using(SRSR.ODD,ODD.REFERENCE) '+
           'join UD_SKL using(SRSR.JORG,UD_SKL.REFERENCE) '+
      'where SRDZ.DOK_REF=\':_a\' '+
      'order by 1 asc';
_sql_e:='select distinct SRSR.NRI, SRSR.NST, SRSR.REFERENCE as REF, TAM.GR, ODD.OD, UD_SKL.SYMBOL, SRSR.DE, '+
             'SRDO.SYMBOL as OT, SRDO.DW as OTDATE, SRSR.R '+
      'from SRDZ '+
           'join SRDO using(SRDZ.SRDO,SRDO.REFERENCE) '+
           'join SRSR using(SRDO.SRSR_E,SRSR.REFERENCE) '+
           'join TAM using(SRSR.GR,TAM.REFERENCE) '+
           'join ODD using(SRSR.ODD,ODD.REFERENCE) '+
           'join UD_SKL using(SRSR.JORG,UD_SKL.REFERENCE) '+
      'where SRDZ.DOK_REF=\':_a\' '+
      'order by 1 asc';
_tmp:=sql(_sql,_a);
_tmp_e:=sql(_sql_e,_a);
__SRSROT.prefix();
{? _tmp.first()
|| {! |?
      __SRSROT.blank();
      __SRSROT.NRI:=_tmp.NRI;
      __SRSROT.NST:=_tmp.NST;
      __SRSROT.REF:=_tmp.REF;
      __SRSROT.GR:=_tmp.GR;
      __SRSROT.OD:=_tmp.OD;
      __SRSROT.SYMBOL:=_tmp.SYMBOL;
      __SRSROT.DE:=_tmp.DE;
      __SRSROT.OT:=_tmp.OT;
      __SRSROT.OTDATE:=_tmp.OTDATE;
      __SRSROT.R:={? _tmp.R='N' || 'Niskocenny' || 'Trwały' ?};
      __SRSROT.add();
      _tmp.next()
   !}
?};
{? _tmp_e.first()
|| {! |?
      __SRSROT.blank();
      __SRSROT.NRI:=_tmp_e.NRI;
      __SRSROT.NST:=_tmp_e.NST;
      __SRSROT.REF:=_tmp_e.REF;
      __SRSROT.GR:=_tmp_e.GR;
      __SRSROT.OD:=_tmp_e.OD;
      __SRSROT.SYMBOL:=_tmp_e.SYMBOL;
      __SRSROT.DE:=_tmp_e.DE;
      __SRSROT.OT:=_tmp_e.OT;
      __SRSROT.OTDATE:=_tmp_e.OTDATE;
      __SRSROT.R:={? _tmp_e.R='N' || 'Niskocenny' || 'Trwały' ?};
      __SRSROT.add();
      _tmp_e.next()
   !}
?}


\srsr_link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Otwiera środek w kartotece
::   WE: _a - SQL REF tabeli SRSR
:: ~OST: INFORK
::----------------------------------------------------------------------------------------------------------------------
SRSR.cntx_psh();
SRST.cntx_psh();
_uidref:='';
SRSR.prefix();
{? SRSR.seek(_a,ref_name(_a))
|| SRST.index('SROD'); SRST.prefix(SRSR.ref(),SSTALE.AR,SSTALE.AO);
   {? SRST.first()
   || _uidref:=SRST.uidref()
   || SRST.prefix(SRSR.ref());
      {? SRST.last()
      || _uidref:=SRST.uidref()
      ?}
   ?}
?};
SRST.cntx_pop();
SRSR.cntx_pop();
{? _uidref<>''
|| {? exec('interm','#system')
   || _params:=exec('obj_ntab_set','#array',,'LINK',_uidref);
      _link_interm:=link_uri(_params);
      cli_open_link(_link_interm)
   || _color:=exec('FindInSet','#table','B_DOMAIN','SYMBOL','FST',,"B_DOMAIN.COLOR",1,,'');
      mb_fork(,'START_PROCES'
      ,'color='+exec('color_conv','#convert',_color)
      ,'LINK',_uidref)
   ?}
|| FUN.info('Środek nie istnieje w kartotece.'@)
?}


\srsr_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Akcja w okienku wertowania DOK - wyświetlenie powiązanych środków trwałych
::----------------------------------------------------------------------------------------------------------------------
SRSR.cntx_psh(); SRST.cntx_psh(); SRDO.cntx_psh(); SRDZ.cntx_psh();
exec('czytaj','#stalesys',,XINFO,FINFO);
exec('SRD','object');
SRD.maski('r');
exec('srsr_tab','dok_fks1');
_win:=exec('srsr_win','dok_fks1');
exec('srsr_fill','dok_fks1',DOK.uidref());
__SRSROT.win_sel(_win);
__SRSROT.select();
SRSR.cntx_pop(); SRST.cntx_pop(); SRDO.cntx_pop(); SRDZ.cntx_pop();
VAR_DEL.delete('__SRSROT')


\pozf_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: f3 dla wyboru pozycji faktury do korekty
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__pfkor');
__pfkor:=POZF.KOR;
POZF.cntx_psh();
{? POZF.DOK<>null()
|| POZF.index('DOK');
   POZF.prefix(DOK.ref())
|| POZF.index('EDOKUM');
   POZF.prefix(EDOKUM.ref())
?};
POZF.win_sel('WYB');
{? POZF.select()
|| _lp:=POZF.LP;
   POZF.cntx_pop();
   POZF.KOR:=_lp
|| POZF.cntx_pop()
?};
VAR_DEL.delete('__pfkor');
POZF.KOR


\ae_pozf_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Ustawia wartość zmiennej dla nazwy korekty pozycji faktury
::----------------------------------------------------------------------------------------------------------------------
_kor:=POZF.KOR;
{? POZF.KOR=0
|| POZF.efld_opt(POZF.win_edit('?'),'mark=1',POZF,'IL'); ROZNE.POZF_NAZ:=''; return(1)
|| POZF.efld_opt(POZF.win_edit('?'),'mark=0',POZF,'IL')
?};
{? var_pres('__fkor')>0 & POZF.KOR=__fkor || VAR_DEL.delete('__fkor'); return(1) ?};
{? -menu_txt()='popraw'
|| POZF.cntx_psh();
   {? POZF.get() & POZF.KOR=_kor
   || POZF.cntx_pop(); return(1)
   ?};
   POZF.cntx_pop()
?};
_pozfret:=0;
{? POZF.LP=POZF.KOR || FUN.info('Pozycja faktury nie może korygować samej siebie.'@); _pozfret:=1 ?};
{? ~_pozfret & exec('czy_pozf_kor','dok_fks1')
|| FUN.info('Skorygowana pozycja nie może być korektą.'@); _pozfret:=1
?};
{? _pozfret || return(0) ?};
_ret:=1;
POZF.cntx_psh();
{? POZF.DOK<>null()
|| POZF.index('DOK');
   POZF.prefix(DOK.ref(),POZF.KOR)
|| POZF.index('EDOKUM');
   POZF.prefix(EDOKUM.ref(),POZF.KOR)
?};
{? POZF.first()
|| {? POZF.KOR
   || FUN.info('Wskazana pozycja koryguje inną pozycję.'@);
      _ret:=0
   |? exec('czy_pozf_kor','dok_fks1')=0
   || _c:=POZF.C;
      _il:=POZF.IL;
      _nazw:=POZF.NAZ;
      _splitp:=POZF.SP_WYM;
      _jm:=POZF.JM;
      _sv:=POZF.SV;
      _rabat:=POZF.RABAT;
      _wn:=POZF.WN;
      _wv:=POZF.WV;
      _wb:=POZF.WB;
      _kurs:=POZF.KURS;
      _wal:=POZF.WAL;
      _jpk_gtu:=POZF.JPK_GTU;
      _ef_prvat:=POZF.EF_PRVAT;
      _ef_proc:=POZF.EF_PROC;
      _uwagi:=POZF.memo_txt(,1,'UWAGI');
      ROZNE.POZF_NAZ:=POZF.NAZ
   || FUN.info('Wskazana pozycja jest już skorygowana.'@);
      _ret:=0
   ?}
|| {? _kor
   || FUN.info('W dokumencie nie istnieje pozycja faktury o liczbie porządkowej %1'@[$_kor]);
      _pozfret:=2
   ?};
   ROZNE.POZF_NAZ:=''
?};
POZF.cntx_pop();
{? _pozfret=2 || POZF.KOR:=0; return(0) ?};
{? var_press('_il')>0 & POZF.C=0 & POZF.IL=0 & POZF.JM=null()
      & POZF.RABAT=0 & POZF.WN=0 & POZF.WV=0 & POZF.WB=0 & POZF.KURS=0
|| POZF.C:=_c;
   POZF.IL:=_il;
   POZF.JM:=_jm;
   POZF.SV:=_sv;
   POZF.RABAT:=_rabat;
   POZF.WN:=_wn;
   POZF.WV:=_wv;
   POZF.WB:=_wb;
   {? POZF.NAZ='' || POZF.NAZ:=_nazw ?};
   POZF.SP_WYM:=_splitp;
   POZF.KURS:=_kurs;
   POZF.WAL:=_wal;
   POZF.JPK_GTU:=_jpk_gtu;
   POZF.EF_PRVAT:=_ef_prvat;
   POZF.EF_PROC:=_ef_proc;
   POZF.memo_set(_uwagi,'UWAGI'); POZF.memo_put(,'UWAGI')
?};
_ret


\ustaw_rozne_pozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Ustawia wartość zmiennej dla nazwy korygowanej pozycji
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
POZF.cntx_psh();
POZF.index('DOK');
POZF.prefix(DOK.ref(),POZF.KOR);
{? POZF.first()
|| ROZNE.POZF_NAZ:=POZF.NAZ
|| ROZNE.POZF_NAZ:=''
?};
POZF.cntx_pop();
_ret


\wart_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Ustawia wartość zmiennej dla wartości przed korektą i po korekcie dla okna wertowania pozycji faktury
::----------------------------------------------------------------------------------------------------------------------
POZF.cntx_psh();
{? POZF.DOK<>null()
|| POZF.index('KOR');
   POZF.prefix(DOK.ref(),POZF.LP)
|| POZF.index('KOR2');
   POZF.prefix(EDOKUM.ref(),POZF.LP)
?};
HELPJPK.CZY_KOR:='';
{? POZF.KOR<>0
|| HELPJPK.CZY_KOR:='Po korekcie'
?};
{? POZF.first()
|| HELPJPK.CZY_KOR:='Przed korektą'
?};
POZF.cntx_pop()


\czy_pozf_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Sprawdza, czy rekord POZF z buforu jest korygowany
::   WY: 1 - jeżeli rekord jest korygowany, 0 - jeżeli rekord nie jest korygowany
::----------------------------------------------------------------------------------------------------------------------
POZF.cntx_psh();
{? POZF.DOK<>null()
|| POZF.index('KOR');
   POZF.prefix(DOK.ref(),POZF.LP)
|| POZF.index('KOR2');
   POZF.prefix(EDOKUM.ref(),POZF.LP)
?};
{? POZF.first()
|| _ret:={? POZF.LP || POZF.LP || 1 ?}
|| _ret:=0
?};
POZF.cntx_pop();
_ret


\bd_pozf_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem pola POZF.IL
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' || return(1) ?};
{? POZF.win_edit('?')<>''
|| {? POZF.KOR
   || POZF.efld_opt(POZF.win_edit('?'),'mark=0',POZF,'IL')
   || POZF.efld_opt(POZF.win_edit('?'),'mark=1',POZF,'IL')
   ?}
?};
1


\beokn_pozf_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Akcja okienko przed dla WYB tabeli POZF
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__pfkor')>0
|| POZF.find_key(__pfkor)
?};
1


\be_pozf_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed redagowaniem pola POZF.KOR
::----------------------------------------------------------------------------------------------------------------------
{? {? POZF.DOK<>null() || POZF.DOK().DOK_REJ().KOR<>'T' || POZF.EDOKUM().KOREKTA<>'T' ?}
|| 0
|| VAR_DEL.delete('__fkor');
   __fkor:=POZF.KOR;
   1
?}


\bd_pozf_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem pola POZF.KOR
::----------------------------------------------------------------------------------------------------------------------
{? POZF.win_edit('?')<>''
|| {? {? POZF.DOK<>null() || POZF.DOK().DOK_REJ().KOR<>'T' || POZF.EDOKUM().KOREKTA<>'T' ?}
   || POZF.efld_opt(POZF.win_edit('?'),'editable=grayed',POZF,'KOR')
   || POZF.efld_opt(POZF.win_edit('?'),'editable=1',POZF,'KOR')
   ?}
?};1


\rodz_sprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: SPRZ_GV - Rodzaj dokumentu sprzedaży
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.GV_WEW<>'T'
|| {? EKSG.TDK='P'
   || FAPOW.cntx_psh(); FAPOW.index('END_K'); FAPOW.prefix($FAKS.ref);
      _roz:={? FAPOW.first() & FAPOW.KW_ROZ<=FAKS.BRUTTO || 1 || 0 ?};
      FAPOW.cntx_pop();
      {? _roz
      || 'Sprzedaż-rozl.zal'
      || ''
      ?}
   |? EKSG.TDK='V'
   || 'Sprzedaż kraj'
   |? EKSG.TDK='S'
   || 'Sprzedaż ratalna'
   || ''
   ?}
|| 'Sprzedaż wewnątrz grupy VAT'
?}


\rodz_sprz_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: KOR_S_GV - Rodzaj dokumentu sprzedaży - korekta
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.GV_WEW<>'T'
|| 'Sprzedaż kraj - korekta'
|| 'Sprzedaż wewnątrz grupy VAT - korekta'
?}


\rodz_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: ZAK_GV - Rodzaj dokumentu zakupu
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.GV_WEW<>'T'
|| {? EKSG.TDK='P'
   || FAPOW.cntx_psh(); FAPOW.index('END_K'); FAPOW.prefix($FAKS.ref);
      _roz:={? FAPOW.first() & FAPOW.KW_ROZ <= FAKS.BRUTTO || 1 || 0 ?};
      FAPOW.cntx_pop();
      {? _roz
      || 'Zakup-rozl.zal.'
      || ''
      ?}
   |? EKSG.TDK='V'
   || 'Zakup krajowy'
   |? EKSG.TDK='S'
   || 'Zakup ratalny'
   ||''
   ?}
|| 'Zakup wewnątrz grupy VAT'
?}


\rodz_zak_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: KOR_Z_GV - Rodzaj dokumentu zakupu - korekta
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.GV_WEW<>'T'
|| 'Korekta zakupu krajowego'
|| 'Zakup wewnątrz grupy VAT - korekta'
?}


\dekr_nie_gv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Warunek dekretacji dokumentów spoza grupy VAT
::----------------------------------------------------------------------------------------------------------------------
FAKS.GV_WEW<>'T'


\dekr_gv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Warunek dekretacji dokumentów w obrębie grupy VAT
::----------------------------------------------------------------------------------------------------------------------
FAKS.GV_WEW='T'


\dokumzbc_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Wyświetlanie rezultatu kontroli businesscheck z przycisku
::----------------------------------------------------------------------------------------------------------------------
DOKUMZBC.cntx_psh();
_win:=DOKUMZBC.win_sel('?');
DOKUMZBC.win_sel('WER1');
DOKUMZBC.select();
{? _win<>'' || DOKUMZBC.win_sel(_win) ?};
DOKUMZBC.cntx_pop();
''


\gen_fakskh_edok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Generuje podmioty powiązane z dokumentem księgowym
::----------------------------------------------------------------------------------------------------------------------
_info:='';
_typ:=DOK.DOK_REJ().SLO().KOD;
_vat:=(_typ='VAT' | _typ='SAD');
{? _vat
||  _rt:={? 4+-DOK.RVAT().RVAT().KVAT().SYM='sprz' | 5+-KVAT.SYM='_wwspd' || 'odbiorcyw' || 'dostawcyo' ?};
   {? DOK.GVKHSTAT='GZ' & DOK.KH_ODB=null()
   || _info:='\nDokument dotyczy kontrahenta będącego grupą VAT.\nNie wprowadzono danych %1 w zakładce Kontrahent.'@[_rt-1]
   ?}
?};
{? _info=''
|| {? DOK.RB<>null() || exec('fakskh_auto','faktury_wspolne',DOK.ref()) ?};
   {? _vat
   || exec('czytaj','#stalesys',DOK.DTO,KST,'GRVAT','JST');
      KH.cntx_psh();
      {? KST.GRVAT<>null() & KST.GRVAT().GV_OD<=DOK.DTO & (KST.GRVAT().GV_DO=date(0,0,0) | KST.GRVAT().GV_DO>=DOK.DTO)
      || exec('fakskh_auto','faktury_wspolne',DOK.ref(),'G');
         exec('dodaj_dane_lic','edi_fo_ksef_fks',_rt+1); DOK.GVLISTAT:='GV'
      || DOK.GVLISTAT:='N'
      ?};
      exec('dodaj_dane_2str','edi_fo_ksef_fks',1+_rt);
      KH.cntx_pop();
      exec('czytaj','#stalesys',,KST,'GRVAT','JST')
   || exec('czytaj','#stalesys',DOK.DTO,KST,'SKROT');
      {? _wew
      || DOK.GVLISTAT:='GL'; DOK.RT:={? DOK_REJ.WEW='Z'
                                     || DOK.CZGV_WYS:=DOK.WYS().TR; DOK.CZGV_ODB:=KST.SKROT; 'N'
                                     || DOK.CZGV_ODB:=DOK.WYS().TR; DOK.CZGV_WYS:=KST.SKROT; 'D'
                                     ?}
      ?}; exec('czytaj','#stalesys',,KST,'SKROT')
   ?};
   DOK.put()
?};
_info


\dokumxml_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Wyświetlanie danych xml dla dokumentu księgowego
::----------------------------------------------------------------------------------------------------------------------
{? DOK.DOK_REJ().JPK_FA_T='S'
|| exec('dokumxml_sel2','edi_xml',$DOK.ref(),DOK.IDADD)
|| exec('dokumxml_sel','edi_xml',DOK)
?}


\pozf_st_b_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła przed redakcją pola POZF.ST_B
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? var_pres('PozFEdok')<0
|| DOK.cntx_psh();
   _result:=POZF.DOK().DOK_REJ().JPK_FA_T='Z';
   DOK.cntx_pop()
||
   ETYPY.cntx_psh();
   _result:=EDOKUM.TYP().RODZ_DOK='Z';
   ETYPY.cntx_pop()
?};
{? _result || exec('be_edok_fld','dok_fks1') || 0 ?}


\set_pozf_stb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Ustawia znacznik stan przed, jeżeli rekord jest korygowany
::   WE: _a - DOK.ref/EDOKUM.ref
::       _b - 1 - zaznacz/ 0 - odznacz
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_set:=_b;
_tab:=ref_tab(_ref);
POZF.cntx_psh();
{? _tab=DOK
|| POZF.index('DOK')
|| POZF.index('EDOKUM')
?};
POZF.prefix(_ref,{? _set || POZF.KOR || bfld('KOR') ?});
{? POZF.first() & POZF.ST_B<>_set
|| POZF.trig_off('*','*');
   POZF.ST_B:=_set;
   POZF.put();
   POZF.trig_on('*','*')
?};
POZF.cntx_pop();
~~

:Sign Version 2.0 jowisz:1045 2024/02/13 12:11:06 64519f188236e59e74ca0f97eff31a360a0a45bf36bfcafc04b52e47a5d852a6548dd993c5dc060d184e405521b1c987d37052d905cf3b1b2dfc82ce9e4a0959cdee765a49822fa1196595b62495e3c9b94c9c44f019c042811425ae19e621276b998cfffbf9cb6dd55021c2198c96416cf7ba9e465ffe3a0492d3292d2218bd

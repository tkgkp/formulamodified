:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_sesta.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa stanów sesji dla systemu Statystyki
::======================================================================================================================


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Czyści powiązania do rekordu tabeli ST_SEST2
::   WE: _a - ST_SEST2.ref()
::   WY: >0  -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PRYWATNA><CLEAN>
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---
ST_SEST2.cntx_psh(); ST_SEST2.clear();
{? ST_SEST2.seek(_ref)
||
   {? _can_continue>0 & VAR1.ERASE=0 & ST_SEST2.count()>0
   ||
::    1. Usuwam ST_LOG które wskazują na ten element

      _rule:="
         _ref:=_b;
         _result:=1;
         _can_continue:=1;
         ST_LOG.cntx_psh();
         ST_LOG.index('ST_SEST2');
         ST_LOG.prefix(_ref);

         {? ST_LOG.first()
         || {!
            |? _next:=0;
               _ref_nxt:=null();
               ST_LOG.cntx_psh();
               {? ST_LOG.next()
               || _ref_nxt:=ST_LOG.ref()
               ?};
               ST_LOG.cntx_pop();

               {? ref_name(ST_LOG.ST_SEST2)=ref_name(_ref)
               || _can_continue:=exec('delete','st_log',ST_LOG.ref())
               ?};

               {? _ref_nxt<>null()
               || _next:=ST_LOG.seek(_ref_nxt)
               ?};
               _next>0 & _can_continue>0
            !}
         ?};
         ST_LOG.cntx_pop();

         {? _can_continue<=0
         || _result:=0
         ?};
         _result
      ";

::    Uruchamiam dla bieżącej maski ST_LOG - powinna być zgodna z maską ST_SEST2
      _can_continue:=_rule(,_ref);

      {? _can_continue>0 & ST_SEST2.count()>0
      || _can_continue:=exec('for_each_mask','#table',ST_LOG,_rule,,,_ref,1)
      ?}
   ?};
   ~~
?};
ST_SEST2.cntx_pop();
:: --- wszystkie powiazania usuniete? ---

{? _can_continue>0
|| _result:=1
|| undo()
?};

{? _mydo || end() ?};

_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Kasuje podany rekord tabeli ST_SEST2 (wykonywane w transakcji!!!)
::   WE: _a - ST_SEST2.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_SEST2.cntx_psh(); ST_SEST2.clear();
{? ST_SEST2.seek(_ref)
|| {? exec('clean','st_sest2',_ref)>0
   || {? ST_SEST2.del(,1)>0
      || _result:=1
      || undo();
         _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};

{? _result<0
|| undo()
?};

ST_SEST2.cntx_pop();
{? _mydo || end() ?};
_result


\st_sesta_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Dołącz dla ST_SEST2
::----------------------------------------------------------------------------------------------------------------------
ST_SEST2.win_edit('RED');
ST_SEST2.blank();
{? VAR.A_ST_SES<>null()
|| ST_SEST2.ST_SESJ2:=VAR.A_ST_SES
?};
{? ST_SEST2.edit()
|| ST_SEST2.add()
?};
~~


\st_sesta_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń dla ST_SEST2
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRUPA='T' | FUN.ask('Czy usunąć bieżący wiersz?'@)
|| exec('delete','st_sest2',ST_SEST2.ref())
?};
~~


\st_sesta_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Popraw dla ST_SEST2
::----------------------------------------------------------------------------------------------------------------------
ST_SEST2.win_edit('RED');
{? ST_SEST2.edit()
|| ST_SEST2.put()
?};
~~


\st_sesta_del_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń przed dla grupy rekordów (tabela ST_SEST2)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?'@)
|| VAR.GRUPA:='T';
   1
|| 0
?}


\st_sesta_del_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń po dla grupy rekordów (tabela ST_SEST2)
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~


\delete_not_used
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Usuwa wszystkie nieużywane rekordy
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};

ST_SEST2.cntx_psh();
ST_SEST2.index('ST_SESJ2');
ST_SEST2.prefix();
{? ST_SEST2.first()
|| {!
   |? _next:=0;
      _ref_nxt:=null();
      ST_SEST2.cntx_psh();
      {? ST_SEST2.next()
      || _ref_nxt:=ST_SEST2.ref()
      ?};
      ST_SEST2.cntx_pop();

      {? ST_SEST2.count()=0
      || _can_continue:=exec('delete','st_sest2',ST_SEST2.ref())
      ?};

      {? _ref_nxt<>null()
      || _next:=ST_SEST2.seek(_ref_nxt)
      ?};
      _next>0 & _can_continue>0
   !}
?};
ST_SEST2.cntx_pop();
{? _can_continue>0
|| _result:=1
|| undo()
?};
{? _mydo || end() ?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 8817e9c38310c74fbc9ba7e0725bea251a3baa074905068a4ff2394cdfffa0928a1096637e915f6d6b4915ab767b2be819474126b2b4dc94cee51c33d5f90ff9f59aaf2d4668b56b2bb329beb9728dd4a36187af0982a73c0d432dda2db234064255189121a1c6a1907acc19c4ff3a98ab2646c58e93ad247fd958c4c0c6bb4b

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: opakow.fml [2008]
:: Utworzony: 2007-03-28
:: Autor: MZ
::======================================================================================================================
:: Zawartosc: Formuły do obsługi opakowań zwrotnych
::======================================================================================================================


\be_mg_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcją magazynu opakowań
::   WE: [_a] - treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='~~' ?} || _a:='~~' ?};
_wyn:=1;
{? MG.MG_OPAK<>''
||
:: ustawia tekst komunikatu
   {? _a='~~'
   || _txt:='Istnieją dokumenty powiązane z magazynem opakowań %1.\nEdycja pola niemożliwa.'@[MG.MG_OPAK]
   || _txt:=_a
   ?};
:: sprawdze we wszystkich maskach czy sa powiazane dokumenty opakowan
   _opn_tab:=OPAK_N.names();
   OPAK_N.cntx_psh();
   {? _opn_tab.first()
   ||
      {!
      |?
         OPAK_N.use(_opn_tab.NAME);
         OPAK_N.index('MP');
         OPAK_N.prefix(MG.MG_OPAK,'');
         {? OPAK_N.first()
         ||
::          dodatkowo sprawdza czy sa dokumenty powiazane z biezacym magazynem MG.SYM
            _qq:=$"
               select
                  count(OPAK_N.SYM) as ILE
               from
                  OPAK_N
                  join ND using (OPAK_N.ND,ND.reference)
                  join MG using (ND.MAG,MG.reference)
               where
                  MG.SYM=':_a'
            ";
            {? sql(_qq,MG.SYM).ILE>0 & +form(_txt)>0
            ||
               FUN.info(_txt);
               _wyn:=0
            ?}
         ?};
         _wyn=1 & _opn_tab.next()
      !}
   ?};
   OPAK_N.cntx_pop()
?};
_wyn


\f3_mg_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: lista już istniejących magazynów opakowań
::   WE: [_a] czy funkcja Dołącz - edycja magazynów
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__mg_opak','__win_sel');
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};
_oddz:=ST.ODDZ;
{? _a=1 || _oddz:=MG.ODDZ ?};
_wyn:={? 1+menu_pth()='D' || MG.MG_OPAK || '' ?};
_q_mg:=$"
select distinct
   MG.ODDZ as Oddz,
   MG.MG_OPAK as Symbol,
   'X' as X
from
   MG
where
   MG.MG_OPAK<>''
   and MG.ODDZ=':_a'
";
__mg_opak:=sql(_q_mg,_oddz);
_win_sel:=__mg_opak.mk_sel('Magazyny opakowań'@,'P',,'haotxmolphfsdss',,,,,'U');
__mg_opak.win_fld(_win_sel,,'ODDZ',,,7,,1,'Oddział'@);
__mg_opak.win_fld(_win_sel,,'SYMBOL',,,30,,,'Symbol'@);
__mg_opak.win_act(_win_sel,0,'Formuła','&Wybierz'@@,,,,"sel_exit",1,,,,'W');
{? _a=1
||
   _rek_przed:="
      {? __mg_opak.X='X'
      || __mg_opak.actions(__mg_opak.win_sel('?'),'up',,1)
      || __mg_opak.actions(__mg_opak.win_sel('?'),'',,1)
      ?};
      {? __mg_opak.ODDZ='' || __mg_opak.ODDZ:=MG.ODDZ; __mg_opak.put() ?}
   ";
   __mg_opak.win_act(_win_sel,0,'Dołącz');
   __mg_opak.win_act(_win_sel,1,'Dołącz',,,,,,1);
   __mg_opak.win_act(_win_sel,0,'Popraw');
   __mg_opak.win_act(_win_sel,0,'Usuń');
   __mg_opak.win_act(_win_sel,0,'Rekord',,,,_rek_przed,"__CHK.record(__mg_opak,,'SYMBOL')");
   __mg_opak.win_act(_win_sel,1,'Rekord',,,,,"__CHK.record(__mg_opak,,'SYMBOL')")
?};
_sel:=1;
__mg_opak.win_sel(_win_sel);
{? _a=0 & __mg_opak.first() & __mg_opak.size()=1
||
   _sel:=0;
   _wyn:=__mg_opak.SYMBOL
?};
{? _sel & __mg_opak.select()
||
   _wyn:=__mg_opak.SYMBOL
?};
VAR_DEL.delete('__mg_opak','__win_sel');
_wyn


\opkp_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: usuwanie pozycji dokumentu opakowan, OPAK_P w buforze
::   WE: [_a] - 1-pytanie   0-bez pytania (nie sprawdza tez czy mozna modyfikowac)
::       [_b] - miejsce wywołania: 'D'-dokumenty 'F'-faktury 'R'-realizacje lub pusty string
::   WY: -1-modyfikacje zablokowane 0/1-wynik funkcji del()
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};
{? _>=2 || {? type_of(_b)<>2 || _b:='' ?} || _b:='' ?};
_wyn:=0;
OPAK_N.cntx_psh();
{? exec('lock','opakow',_b)
||
   _ok:=exec('czy_roz','opakow');
   {? _ok
   ||
      {? OPAK_P.r_lock(1,1,1)
      ||
         OPAK_P.r_unlock()
      ||
         _ok:=0;
         {? FUN.ask('Pozycje obsługuje inny użytkownik.\nCzy chcesz zobaczyć kto?'@) & OPAK_P.r_lock(1,,1)
         || OPAK_P.r_unlock()
         ?}
      ?}
   ?};
   {? _ok=1 & _a || _ok:=(exec('czy_mod','opakow',OPAK_P.OPAK_N().AKC) & FUN.ask('Usunąć pozycję?'@)) ?};
   {? _ok=1
   ||
      _do_state:=do_state();
      {? _do_state=0 || do() ?};
      _zwr_del:=exec('opak_zwr_del','opakow_stany',$OPAK_P.ref(),'');
      {? _zwr_del=1
      ||
         _mag:=OPAK_P.OPAK_N().MAG;
         _kh:=OPAK_P.OPAK_N().KH().KOD;
         _mat:=OPAK_P.M().KTM;
         _wyn:=OPAK_P.del(0,1);
::       przenumerowanie dla dokumentu
         OPAK_P.cntx_psh();
         OPAK_P.index('OPAK_N');
         OPAK_P.prefix(POMOC.OPAK_N);
::       przed przenumerowaniem trzeba by sprawdzic czy zadna z pozycji nie jest zablokowana
         exec('ReNumAfterDel','#table','OPAK_P','POZ');
         OPAK_P.cntx_pop()
      ?};
      {? _do_state=0 || end() ?}
   ?};
   OPAK_N.r_unlock()
?};
OPAK_N.cntx_pop();
_wyn


\lock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: lock naglowka dokumentu opakowan
::       (tylko podczas edycji biezacego dokumentu - buforuje OPAK_N poprzez POMOC.OPAK_N !!!)
::   WE: _a - miejsce wywołania: 'D' - dokumenty 'F'-faktury 'R'-realizacje lub pusty napisy
::   WY: _wyn   0 - edycja niemozliwa   1 - rekord zostal zablokowany, dozwolona dalsza edycja
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? POMOC.OPAK_N<>null
||
:: sprawdzam czy wykonywana akcja nie zostala zablokowana - tylko w oknach pozycji opakowan lub pozycji dok. powiazanych
:: dla menu glownego (brak aktywnego okna wertowania) POMOC.OKNO='XXX'
   OPAK_N.get();
   {? POMOC.OKNO='XXX' | (';DFR'*_a)>1 |(-exec('opkp_act','opakow',_a))*(-(menu_pth()+1))=0
   ||
      _wyn:=(#OPAK_N.ref())>0 & OPAK_N.r_lock(1,1,1,#OPAK_N.ref(),OPAK_N.name());
      {? _wyn=0 & do_state()=0
         & FUN.ask('Dokument %1 obsługuje inny użytkownik.\nCzy chcesz zobaczyć kto?'@[OPAK_N.SYM])
         & OPAK_N.r_lock(1,,1)
      || OPAK_N.r_unlock()
      ?}
   ||
      {? do_state()=0 || FUN.info('Akcja [%1] jest niedostępna.'@[menu_txt()]) ?};
      _wyn:=0
   ?}
?};
_wyn


\opkp_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcje dla pozycji dokumentow opakowan
::   WE: [_a] - miejsce wywołania: 'D' - dokumenty 'F'-faktury 'R'-realizacje lub pusty napisy
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:=POMOC.OPAK_MJS ?} || _a:=POMOC.OPAK_MJS ?};
_act1:=_act0:='';
POMOC.OPAK_N:=exec('FindAndGet','#table',OPAK_N,POMOC.OPAK_N,,"ref()",null());
_czyzdok:=POMOC.OPAK_N().ND<>'' | POMOC.OPAK_N().FAKS<>'';
_opak_p_win_sel:=OPAK_P.win_sel('?');
{? POMOC.OPAK_N=null
||
   {? _opak_p_win_sel='WER' | _opak_p_win_sel='TWER'
   ||
::    sprawdza czy dokument ND lub FAKS nie jest zaakceptowny
      {? (cur_tab()=DK & ND.STAT_REJ<>'N')
            |
         ((cur_tab()=FAKS | cur_tab()=FAP) & (FAKS.STAT_REJ<>'N' | FAKS.WHERE='U'))
            |
         (cur_tab()=OPAK_P & POMOC.OPAK_N().AKC='T')
      ||
         _act1+='DUP';_act0+='D'
      ?}
   ||
::    w oknie grupowym gdy brak naglowkow
     _act0+='D'
   ?}
||
   {? POMOC.OPAK_N().AKC='T'
      | ((POMOC.OPAK_N().ND<>'' | POMOC.OPAK_N().FAKS<>'') & (cur_tab()<>FAKS & cur_tab()<>FAP & cur_tab()<>DK))
   ||
      {? _opak_p_win_sel='WER' | _opak_p_win_sel='TWER'
      ||
         _act1+='DU';
::       sprawdza czy pozycja nie byla juz rozliczana
         {? POMOC.IL_ZWR>0 || _act1+='P' ?}
      ||
         _act1+='DUP'
      ?};
      _act0+='D'
   ?};
   {? POMOC.OPAK_N().PLUS='N'
:: Rozchody
   || _act1+='R'
   |? POMOC.OPAK_N().PLUS='T' & OPAK_P.ZWROT='N'
:: Skladowe ilosci
   || _act1+='I'
   ?};
:: Zwroty kaucji
   {? OPAK_P.CZY_KAUC='N' | OPAK_P.ZWROT='T'
   || _act1+='Z'
   ?}
?};

{? BPMN.DISPLAY
||
   _act1:='DPURIZ';
   _act0:='D'
?};

OPAK_P.actions('SEL_NAG',{? _czyzdok || 'DUP' || '' ?}+_act1+':'+_act0+{? _czyzdok || 'D' || '' ?},,1);
OPAK_P.actions('WER',_act1+':'+_act0,,1);
OPAK_P.actions('TWER',_act1+':'+_act0,,1);
_act1+':'+_act0


\czy_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: sprawdza czy byl rozchod dla pozycji przychodowych - OPAK_P w buforze
::   WY: _wyn - 1-ok mozna edytowac / 0-stop
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
OPAK_ZWR.index('REFTPOZ');
OPAK_ZWR.prefix('N',$OPAK_P.ref());
{? OPAK_ZWR.first()
||
   _txt:='Pozycja %1 dokumentu opakowań jest powiązana z '@[$OPAK_P.POZ]
      +{? OPAK_P.OPAK_N().PLUS='T' || 'dokumentem rozchodowym.'@ || 'dokumentem zwrotu.'@ ?};
   {? var_pres('__kom')>100
   ||
      {? var_pres('__lp')>100 || __lp:=0 ?};
      exec('add_kom','#message',_txt,7,,__lp+=1)
   ||
      FUN.info(_txt+'\nModyfikacja niemożliwa.'@)
   ?};
   _wyn:=0
?};
_wyn


\czy_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: sprawdza czy mozliwa modyfikacja (stan akceptacji dokumentu opakowan + komunikat o stanie)
::   WE:  _a  - AKC - znacznik akceptacji
::       [_b] - operacja 0-wycofaj, 1-domyslnie (standardowa kontrola czy mozna edytowac, usuwac..)
::   WY: _wyn - 1-zaakceptowany 0-niezaakceptowany
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};
_wyn:=0;
{? _a='T'
||
   {? _b=1 || FUN.info('Dokument jest zaakceptowany.'@) ?};
   _wyn:=0
||
   {? _b=0 || FUN.info('Dokument nie jest zaakceptowany.'@) ?};
   _wyn:=1
?};
_wyn




\opks_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: utworzenie tabeli __opks_tab
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__opks_tab')>100
||
   __opks_tab.prefix(); {? __opks_tab.first() || {! |? __opks_tab.del() !} ?}
||
   VAR_DEL.delete('__opks_tab');
   __opks_tab:=tab_tmp(1
      ,'INDEKS','STRING[50]','Indeks'
      ,'NAZWA','STRING[100]','Nazwa'
      ,'S','REAL','Stan wg dokumentow'
      ,'BKC','REAL','Bilans kaucji'
      ,'SK','REAL','Stan po rozliczeniu'
      ,'JM','STRING[8]','Jm'
      ,'FKC','REAL','Zafakturowana kaucja'
      ,'SFK','REAL','Zafakturowane po terminie'
   )
?};
''


\stanm_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2012]
:: OPIS: obliczanie stanu __opks_tab na rekord
::   WE: _a - ref materialu
::       _b - ref magazynu
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
OPAK_SM.cntx_psh();
OPAK_SM.index('M');
OPAK_SM.prefix(_a,_b,);
{? OPAK_SM.first()
|| {!
   |? _wyn+=OPAK_SM.S;
      OPAK_SM.next()
   !}
?};
OPAK_SM.cntx_pop();
_wyn


\zwr_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: oblicza ilosc rozliczona (tylko Zwroty kaucji)
::       dla pozycji z kaucja CZY_KAUC='T' (zwroty+zafakturowane wydania)
::   WE: _a - $OPAK_P.ref() pozycji rozliczanej
::       [_b] - kontrahent
::       [_c] - 'T' -zafakturowane pozycje, domyślnie bez znaczenia
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>7 || _b:=null() ?} || _b:=null() ?};
_fakt:={? var_pres('_c')=type_of('') || _c || '' ?};
_ref_rozl:={? var_pres('_d')=type_of('') || _d || '' ?};

_il:=0;
OPAK_ZWR.cntx_psh();
:: po wszystkich okresach
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |?
      OPAK_ZWR.use('opakz'+ST.ODDZ+($OKR.ROK+2));
      OPAK_ZWR.index('REF_POZ');
      OPAK_ZWR.prefix(_a);
      {? OPAK_ZWR.first()
      ||
         {!
         |? {? {? _ref_rozl='' | OPAK_ZWR.REF_ROZL<>_ref_rozl | exec('zwrzopak','opakow',_ref_rozl,'OPAK_N().AKC')='T'
               || OPAK_ZWR.TMP='N'
               || OPAK_ZWR.TMP='T'
               ?}
             & (_b=null() | exec('zwrzopak','opakow',OPAK_ZWR.REF_POZ,'OPAK_N().KH')=_b)
             & (_fakt<>'T' | OPAK_ZWR.FAKT='T')
            || _il+=OPAK_ZWR.IL
            ?};
            OPAK_ZWR.next()
         !}
      ?};
      OKR.next()
   !}
?};
OKR.cntx_pop();
OPAK_ZWR.cntx_pop();
_il


\zwrzopak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: zwraca wartosc pola tabeli na postawie ref-a SQL-owego tabeli OPAK_P
::   WE: _a - ref SQL
::       _b - pole
::   WY: wartosc pola lub 0
::----------------------------------------------------------------------------------------------------------------------
_mask:=form(8+_a);
_wyn:=0;
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();
{? _mask<>OPAK_P.name()
|| OPAK_N.use('opakn'+(_mask+3));
   OPAK_P.use(_mask)
?};
OPAK_P.clear();
_wyn:={? ($('OPAK_P.seek(\''+_a+'\',\''+form(8+_a)+'\')'))()
      || ($('OPAK_P.'+_b))()
      || _typ:=type_of(($('OPAK_P.'+_b))());
         {? _typ=1 || 0
         |? _typ=2 || ''
         |? _typ=4 || date(0,0,0)
         |? _typ=5 || time(0,0)
         |? _typ=7 || null
         || 0
         ?}
      ?};
OPAK_N.cntx_pop();
OPAK_P.cntx_pop();
_wyn


\opow_mk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: pozycje powiazane - rozliczenia, OPAK_P.ref w buforze
::       jesli _b=0 & _a=0 => formula oblicza wynik i dotyczy biezacej pozycji OPAK_P z bufora
::   WE: _a  - 0-Rozchody/Zwroty kaucji 1-Skladowe ilosci
::      [_b] - czy select
::      [_c] - 0/1 brać niezaakceptowane
::   WY: _wyn[] - _wyn[1]-bilans kaucji, _wyn[2]-Stan po rozliczeniu
::----------------------------------------------------------------------------------------------------------------------
_roz:=_zwr:=_sil:=0; {? _a=1 || _sil:=1 |? OPAK_P.CZY_KAUC='T' & OPAK_P.ZWROT='N' || _zwr:=1 || _roz:=1 ?};
_czy_sel:={? var_pres('_b')=type_of(0) || _b || 1 ?};
_akc_n:={? var_pres('_c')=type_of(0) || _c || 0 ?};

VAR_DEL.delete('__opow_bl');
__opow_bl:=tab_tmp(4
         ,'KH_KOD','STRING[5]','Kod'
         ,'KH_SKR','STRING[10]','Kontrahent'
         ,'SYMBOL','STRING[20]','Symbol'
         ,'DATA','DATE','Data'
         ,'POZ','INTEGER','Poz.'
         ,'INDEKS','STRING[50]','Indeks'
         ,'NAZWA','STRING[100]','Nazwa'
         ,'CZY_KAUC','STRING[1]','CZY_KAUC'
         ,'ZWROT','STRING[1]','ZWROT'
         ,'KAUCJA','REAL','Kaucja'
         ,'IL','REAL','Ilość rozliczona'
         ,'WAR','REAL','Wartość rozliczona'
         ,'BKC','REAL','Bilans kaucji'
         ,'AKC','STRING[1]','Akc'
         ,'MAX','REAL','Maksymalna ilość do rozliczenia'
         ,'OPKP_REF','STRING[16]','Wskazanie na pozycje rozliczaną'
      );
__opow_bl.blank();
__opow_bl.KH_KOD:=OPAK_P.OPAK_N().KH().KOD;
__opow_bl.KH_SKR:=OPAK_P.OPAK_N().KH().SKR;
__opow_bl.SYMBOL:=OPAK_P.OPAK_N().SYM;
__opow_bl.DATA:=OPAK_P.OPAK_N().DATA;
__opow_bl.ZWROT:=OPAK_P.ZWROT;
__opow_bl.POZ:=OPAK_P.POZ;
__opow_bl.INDEKS:=OPAK_P.M().KTM;
__opow_bl.NAZWA:=OPAK_P.M().N;
__opow_bl.CZY_KAUC:=OPAK_P.CZY_KAUC;
__opow_bl.KAUCJA:=OPAK_P.KAUCJA;
__opow_bl.IL:=OPAK_P.IL;
__opow_bl.WAR:=(__opow_bl.IL*__opow_bl.KAUCJA)$2;
__opow_bl.AKC:=OPAK_P.OPAK_N().AKC;
__opow_bl.MAX:=OPAK_P.IL;
__opow_bl.OPKP_REF:=$OPAK_P.ref();
__opow_bl.add();
_ok:=1;
_wyn:=obj_new(2);
_wyn[1]:=_wyn[2]:=0;
POMOC.IL_ZWR:=0;
_opkp_ref:=$OPAK_P.ref();
OPAK_N.cntx_psh();
{? OPAK_P.OPAK_N || OPAK_N.use(ref_name(OPAK_P.OPAK_N)) ?};
_kh:=OPAK_P.OPAK_N().KH;
_kaucja:=OPAK_P.KAUCJA;

{? _czy_sel | _sil
||
   {? var_pres('__opow_tab')>100
   ||
      __opow_tab.prefix(); {? __opow_tab.first() || {! |? __opow_tab.del() !} ?}
   ||
      VAR_DEL.delete('__opow_tab');
      __opow_tab:=tab_tmp(4
         ,'KH_KOD','STRING[5]','Kod'
         ,'KH_SKR','STRING[10]','Kontrahent'
         ,'SYMBOL','STRING[20]','Symbol'
         ,'DATA','DATE','Data'
         ,'POZ','INTEGER','Poz.'
         ,'INDEKS','STRING[50]','Indeks'
         ,'NAZWA','STRING[100]','Nazwa'
         ,'CZY_KAUC','STRING[1]','CZY_KAUC'
         ,'ZWROT','STRING[1]','ZWROT'
         ,'KAUCJA','REAL','Kaucja'
         ,'IL','REAL','Ilość rozliczona'
         ,'WAR','REAL','Wartość rozliczona'
         ,'BKC','REAL','Bilans kaucji'
         ,'AKC','STRING[1]','Akc'
         ,'OPAK_ZWR','STRING[16]','Ref SQL OPAK_ZWR'
      )
   ?};

   {? _a=1
   ||
      _title:='Składowe ilości';
      {? OPAK_P.OPAK_N().PLUS='T' & OPAK_P.ZWROT='N'
      ||
         FUN.info('Funkcja dotyczy zwrotów lub pozycji rozchodowych.'@);
         _ok:=0
      ?}
   ||
      _title:='Rozliczenie kaucji'@;
      {? _roz
      ||
         {? OPAK_P.OPAK_N().PLUS='N'
         || FUN.info('Funkcja dotyczy pozycji przychodowych.'@); _ok:=0
         ?}
      ||
         {? OPAK_P.CZY_KAUC='N' | (OPAK_P.CZY_KAUC='T' & OPAK_P.ZWROT='T')
         || FUN.info('Funkcja dotyczy pozycji z pobraną kaucją.'@); _ok:=0
         ?}
      ?}
   ?};
   {? __opow_bl.MAX>0 || VAR_DEL.delete('__opow_sel') ?};
   {? var_pres('__opow_sel')<=0
   ||
      __opow_sel:=__opow_tab.mk_sel('','N',,'_opow_mk_poz_',,,,,'U',,,,,'html_maximized');
      __opow_tab.win_fld(__opow_sel,,'KH_KOD',,,5,,1,'Kod'@);
      __opow_tab.win_fld(__opow_sel,,'KH_SKR',,,10,,1,'Kontrahent'@);
      __opow_tab.win_fld(__opow_sel,,'SYMBOL',,,16,,1,'Dokument'@);
      __opow_tab.win_fld(__opow_sel,,'DATA',,,10,,1,'Data'@);
      __opow_tab.win_fld(__opow_sel,,'ZWROT',,,3,,1,'Zwrot'@,,,2,,"'T'","'N'","'F'");
      __opow_tab.win_fld(__opow_sel,,'POZ',,,-3,,1,'Pozycja'@);
      __opow_tab.win_fld(__opow_sel,,'NAZWA',,,20,,1,'Nazwa'@);
      __opow_tab.win_fld(__opow_sel,,'IL',,,12,3,,'Ilość'@);
      __opow_tab.win_fld(__opow_sel,,'CZY_KAUC',,,-3,,1,'Czy kaucja'@,,,2,,"'T'","'N'");
      __opow_tab.win_fld(__opow_sel,,'KAUCJA',,,8,2,1,'Kaucja'@);
      __opow_tab.win_fld(__opow_sel,,'WAR',,,12,2,1,'Wartość'@);
      __opow_tab.win_fld(__opow_sel,,'AKC',,,-3,,1,'Zaakceptowany'@,,,2,,"'T'","'N'");
      __opow_tab.win_act(__opow_sel,,'Formuła','Dołącz'@@,,,"exec('addRozl','opakow')",,,,,,'D');
      __opow_tab.win_act(__opow_sel,,'Formuła','Usuń'@@,,,"exec('delRozl','opakow')",,,,,,'U');
      __opow_tab.win_act(__opow_sel,,'Szukaj',,,,,,1);
      __opow_tab.win_act(__opow_sel,,'Rekord',,,,"exec('rekRozl','opakow')");
      {? OPAK_N.AKC='T' & __opow_bl.MAX>0
      || __opow_tab.win_act(__opow_sel,1,'Formuła','Dołącz'@@,,,"exec('addRozl','opakow')",,1,,,,'D')
      ?}
   ?};
   __opow_tab.win_sel(__opow_sel);
   __opow_tab.hdr_sel('');
   __opow_tab.hdr_sel(_title)
?};
OPAK_N.cntx_pop();

M.cntx_psh();
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();
OPAK_ZWR.cntx_psh();
FAKS.cntx_psh();
FAP.cntx_psh();

OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? _ok=1 & OKR.first()
||
   {!
   |?
      OPAK_ZWR.use('opakz'+ST.ODDZ+($OKR.ROK+2));
      {? _a=1 || OPAK_ZWR.index('REFTROZL') || OPAK_ZWR.index('REFTPOZ') ?};
      OPAK_ZWR.prefix('N',_opkp_ref);
      {? OPAK_ZWR.first()
      ||
         {!
         |?
            _il:=_war:=0;
            _opak_zwr:=$OPAK_ZWR.ref();
            _refsql:={? _a=1 || OPAK_ZWR.REF_POZ || OPAK_ZWR.REF_ROZL ?};
            {? (5+_refsql)='opakp'
            ||
               OPAK_N.use('opakn'+((8+_refsql)+3));
               OPAK_P.use(8+_refsql);
               {? (  _akc_n & exec('zwrzopak','opakow',_refsql,'OPAK_N().PLUS')='N'
                        |
                     exec('zwrzopak','opakow',_refsql,'OPAK_N().AKC')='T'
                  )
                & (_czy_sel | exec('zwrzopak','opakow',_refsql,'OPAK_N().KH')=_kh)
               ||
                  OPAK_N.clear();
                  OPAK_P.clear();
                  {? OPAK_P.seek(_refsql,8+_refsql) & (_zwr=0 | OPAK_P.ZWROT='T' | OPAK_ZWR.FAKT='T')
                  ||
                     {? _czy_sel | _sil
                     ||

                        __opow_tab.blank();
                        __opow_tab.KH_KOD:=OPAK_P.OPAK_N().KH().KOD;
                        __opow_tab.KH_SKR:=OPAK_P.OPAK_N().KH().SKR;
                        __opow_tab.SYMBOL:=OPAK_P.OPAK_N().SYM;
                        __opow_tab.DATA:=OPAK_P.OPAK_N().DATA;
                        __opow_tab.ZWROT:=OPAK_P.ZWROT;
                        __opow_tab.POZ:=OPAK_P.POZ;
                        __opow_tab.INDEKS:=OPAK_P.M().KTM;
                        __opow_tab.NAZWA:=OPAK_P.M().N;
                        __opow_tab.CZY_KAUC:=OPAK_P.CZY_KAUC;
                        __opow_tab.KAUCJA:={? _a=1 || OPAK_P.KAUCJA || _kaucja ?};
                        __opow_tab.IL:=_il:=OPAK_ZWR.IL;
                        __opow_tab.WAR:=_war:=(__opow_tab.IL*__opow_tab.KAUCJA)$2;
                        __opow_tab.AKC:=OPAK_P.OPAK_N().AKC;
                        __opow_tab.OPAK_ZWR:=_opak_zwr;
                        {? __opow_tab.add()
                        || __opow_bl.MAX-=__opow_tab.IL;
                           __opow_bl.put(1)
                        ?}
                     ||
                        _il:=OPAK_ZWR.IL;
                        _war:=(OPAK_ZWR.IL*_kaucja)$2
                     ?}
                  ?}
               ?}

            |? (5+_refsql)='fakpo'
            || FAKS.use('faktu'+((8+_refsql)+3));
               FAP.use(8+_refsql);
               {? (_akc_n | exec('FindAndGet','#table','FAP',_refsql,,"FAP.FAKS().AKC",'')='T')
                & (_czy_sel | exec('FindAndGet','#table','FAP',_refsql,,"FAP.FAKS().KH",null())=_kh)
               || FAKS.clear();
                  FAP.clear();
                  {? FAP.seek(_refsql,8+_refsql)
                  ||
                     {? _czy_sel
                     ||
                        __opow_tab.blank();
                        __opow_tab.KH_KOD:=FAP.FAKS().KH().KOD;
                        __opow_tab.KH_SKR:=FAP.FAKS().KH().SKR;
                        __opow_tab.SYMBOL:=FAP.FAKS().SYM;
                        __opow_tab.DATA:=FAP.FAKS().DW;
                        __opow_tab.ZWROT:='F';
                        __opow_tab.POZ:=FAP.POZ;
                        __opow_tab.INDEKS:=FAP.M().KTM;
                        __opow_tab.NAZWA:=FAP.M().N;
                        __opow_tab.CZY_KAUC:='T';
                        __opow_tab.KAUCJA:=FAP.CB;
                        __opow_tab.IL:=_il:=FAP.IL;
                        __opow_tab.WAR:=_war:=(FAP.CB*FAP.IL)$2;
                        __opow_tab.AKC:=FAP.FAKS().AKC;
                        __opow_tab.OPAK_ZWR:=_opak_zwr;
                        {? __opow_tab.add()
                        || __opow_bl.MAX-=__opow_tab.IL;
                           __opow_bl.put(1)
                        ?}
                     ||
                        _il:=FAP.IL;
                        _war:=(FAP.CB*FAP.IL)$2
                     ?}
                  ?}
               ?}
            ?};

            POMOC.IL_ZWR+=_il;
            {? OPAK_ZWR.FAKT<>'N' | _a=0 || _wyn[1]+=_war; _wyn[2]+=_il ?};

            OPAK_ZWR.next()
         !}
      ?};

      OKR.next()
   !}
?};
OKR.cntx_pop();

FAP.cntx_pop();
FAKS.cntx_pop();
OPAK_ZWR.cntx_pop();
OPAK_P.cntx_pop();
OPAK_N.cntx_pop();
M.cntx_pop();

{? _ok=1 & _czy_sel
||
   __opow_tab.first();
   __opow_tab.select()
?};
VAR_DEL.delete('__opow_ref');
_wyn


\opkn_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: usuwanie calego dokumentu opakowan
::   WE: [_a] - 1-pytanie   0-bez pytania (nie sprawdza tez czy mozna modyfikowac)
::       [_b] - miejsce wywołania: 'D'-dokumenty 'F'-faktury 'R'-realizacje lub pusty string
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};
{? _>=2 || {? type_of(_b)<>2 || _b:='' ?} || _b:='' ?};
OPAK_N.get();
_pom_tab:=POM.TAB;
_pom_typdok:=POM.TYPDOK;
POM.TAB:='OPAK_N';
POM.TYPDOK:='OPK';

OPAK_N.cntx_psh();
_ok:=exec('lock','opakow',_b);
{? _ok=1 & _a=1 || _ok:=(exec('czy_mod','opakow',OPAK_N.AKC) & FUN.ask('Usunąć dokument opakowań?'@)) ?};
{? _ok=1
||
   _do_state:=do_state();
   {? _do_state=0 || do() ?};

   OPAK_P.cntx_psh();
   OPAK_P.index('OPAK_N');
   OPAK_P.prefix(OPAK_N.ref());
   {? OPAK_P.first()
   ||
      _del:=1;
      {!
      |?
         _del:=exec('opkp_del','opakow',0,_b);
         _del>1
      !};
      {? _del=0 || _ok:=0 ?}
   ?};
   OPAK_P.cntx_pop();

   {? _ok=1
   ||
::    wszystkie pozycje usuniete
      numer:=OPAK_N.NR;
      oldnumer:=1;
      exec('nr_old','numery');
      OPAK_N.del();
      POMOC.OPAK_N:=null
   ||
      undo()
   ?};

   {? _do_state=0 || end() ?}
?};
OPAK_N.r_unlock();
OPAK_N.cntx_pop();

POM.TAB:=_pom_tab;
POM.TYPDOK:=_pom_typdok;
''


\opak_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: widok okna dla DK/FAP - powiazany dokument opakowan
::       jesli brak opakowan - generuje naglowek i pozycje opakowan dla pozycji dokumentu magazynowego
::   WE: [_a] - ref() naglowka dokumentu magazynowego/faktury (null=przelaczanie zakladek)
::   WE: [_b] - 1-zakładka 0-nie (domyślnie)
::       [_c] - grupowa akceptacja
::       _b - 0-korekta zbiorcza; 1-korekty pojedyncza
::       _c - komunikaty - funkcja pomocnicza
::       _d - komunikaty - identyfikator
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>7 || _a:=null ?} || _a:=null ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};
_tab:=_b;
{? var_pres('_c')=type_of(0)
||
   _group:=_c;
   _nieKorZb:=_d;
   _komroot:=_e;
   _komid:=_f
?};
_mjs:='';
{? _a=null
||
:: wywolane przez zakladke
   _tab:=1;
   DISP.DOK_MAG:='';
   {? cur_tab()=FAP
   ||
      {? FAKS.f_active()
      || FAKS.f_get();
         _size:=FAKS.f_size()
      || FAKS.get();
         _size:=FAKS.size()
      ?};
      {? _size
      ||
         _a:=FAKS.ref();
         DISP.DOK_MAG:=FAKS.SYM
      ?}
   ||
      POMOC.OPAK_N:=null;
      {? ND.sel_size()
      || _size:=1
      |? ND.f_active()
      || ND.f_get();
         _size:=ND.f_size()
      || ND.get();
         _size:=ND.size()
      ?};
      {? _size
      ||
         _a:=ND.ref();
         DISP.DOK_MAG:=ND.SYM
      ?}
   ?}
?};

POMOC.RODZ:='T';
POMOC.OPAKOW:='T';
POMOC.OPAK_N:=null;
_multi:=0;

{? 5+$_a='faktu'
|| _mjs:='F';
   FAKS.cntx_psh();
   FAP.cntx_psh();
   FAKS.prefix();
   FAKS.seek(_a);
   _typ:=FAKS.WHERE='P' | (FAKS.WHERE='G' & (FAKS.MAG=null | FAKS.MAG().MG_OPAK<>''));
   {? FAKS.SZ='S' & _typ=1 & FAKS.T().PAR='N' & FAKS.SYMF='' & exec('FindInSet','#table','FAP','FAP',FAKS.ref())<>null()
   ||
      {? var_pres('__pomoc_mg_opak')<>type_of(~~)
      ||
         {? POMOC.OPAK_N=null()
         ||
            OPAK_N.index('FAKS');
            OPAK_N.prefix($_a);
            {? OPAK_N.first()
            || POMOC.OPAK_N:=OPAK_N.ref()
            ?}
         ?};
         exec('opfap_gen','opakow',_a)
      ?};
      {? var_pres('__pomoc_mg_opak')=type_of(~~) & _>2
      ||
         _komroot(_komid,_c); __kom.add('Pominięto generowanie dokumentu opakowań.'@,7)
      ?}
   ||
::    tylko przeglad pozycji opakowan powiazanych z dokumentem zakupu poprzez dokumenty magazynowe
      _multi:=1
   ?}
|? 5+$_a='nagdo'
|| _mjs:='D';
   _where:='';
   ND.cntx_psh();
   ND.prefix();
   {? ND.seek(_a) & ND.FAKS & ND.FAKS().T().KOR='N' || _where:=ND.FAKS().WHERE ?};
   ND.cntx_pop();

   ND.cntx_psh();
   DK.cntx_psh();
   {? _where<>'G'
   ||
      OPAK_N.index('ND');
      OPAK_N.prefix($_a);
      {? OPAK_N.first()
      || POMOC.OPAK_N:=OPAK_N.ref()
      ?};
      exec('opdk_gen','opakow',_a)
   ?}
?};

{? _tab=1
||
   {? _multi
   ||
      OPAK_P.index('FAKS');
      OPAK_P.prefix($_a)
   ||
      OPAK_P.index('OPAK_N');
      OPAK_P.prefix(POMOC.OPAK_N)
   ?};

   OPAK_N.win_sel('WER');
   OPAK_P.win_sel('TWER');
   DISP.STAN:=0;
   DISP.BKC:=0;
   {? OPAK_P.first()
   ||
      {!
      |?
         exec('init_var','opakow');
         DISP.STAN+=OPAK_P.IL;
         DISP.BKC+=POMOC.BKC;
         OPAK_P.next()
      !}
   ?};
   OPAK_P.first();
   exec('opkp_act','opakow',_mjs)
?};

{? 5+$_a='faktu'
||
   FAKS.cntx_pop();
   FAP.cntx_pop()
|? 5+$_a='nagdo'
||
   ND.cntx_pop();
   DK.cntx_pop()
?};
POMOC.OPAKOW:='N';
''


\opfap_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: generowanie pozycji opakowan wg pozycji dok.mag.
::       POMOC.OPAK_N - jesli <>null tzn ze naglowek opakowan istnieje, gdy =null -> generuje naglowek opakowan
::   WE: _a - ref naglowka faktury FAKS.ref()
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
FAKS.cntx_psh();
FAP.cntx_psh();
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();
FAKS.clear();
{? FAKS.seek(_a) & FAKS.AKC='N'
||
   FAP.index('FAP');
   FAP.prefix(FAKS.ref());
   {? FAP.first()
   ||
      {!
      |?
         {? FAP.OPAK_GEN='N'
         ||
::          usuwa wszystkie powiazane ale niezmodyfikowane pozycje opakowan
            OPAK_P.index('DK');
            OPAK_P.prefix($FAP.ref(),'N');
            {? OPAK_P.first()
            ||
               _del:=1;
               {!
               |?
                  _del:=exec('opkp_del','opakow',0,'F');
                  _del>1
               !};
               {? _del=0 || _ok:=0 ?}
            ?};

            _gen:=0;
            {? _ok=1
            ||
               {? FAP.M().OPAKOW<>'T'
               ||
                  M_OPAKOW.index('M_OPAKOW');
                  M_OPAKOW.prefix(FAP.M);
                  {? M_OPAKOW.first()
                  ||
                     {!
                     |?
                        _gen:=exec('fap_one','opakow',M_OPAKOW.OPAKOW,M_OPAKOW.POJ);
                        _gen=1 & M_OPAKOW.next()
                     !}
                  ?}
               ||
                  _gen:=exec('fap_one','opakow',FAP.M,1)
               ?}
            ?};

            {? _gen=1
            ||
               FAP.OPAK_GEN:='T';
               FAP.put()
            ?}
         ?};
         _ok=1 & FAP.next()
      !}
   ?}
?};
OPAK_P.cntx_pop();
OPAK_N.cntx_pop();
FAP.cntx_pop();
FAKS.cntx_pop();
''


\fap_one
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: generowanie pojedynczej pozycji opakowan wg pozycji dok.mag.
::       POMOC.OPAK_N - jesli <>null tzn ze naglowek opakowan istnieje, gdy =null -> generuje naglowek opakowan
::       FAKS, FAP w buforze, OPAK_P.index() ustawiony
::   WE: _a - M.ref
::       _b - pojemnosc opakowania
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? POMOC.OPAK_N=null
||
   {? var_pres('__pomoc_mg_opak')=type_of(~~)
   ||
:: zrezygnowano z generacji dokumentu opakowań podczas grupowego zakończ
      _wyn:=1
   ||
      _mag:=
         {? var_pres('__pomoc_mg_opak')=type_of('') & __pomoc_mg_opak<>''
         || __pomoc_mg_opak
         || {? FAKS.MAG().MG_OPAK<>'' || FAKS.MAG().MG_OPAK || exec('f3_mg_op','opakow',0) ?}
         ?};
      {? _mag<>''
      || POMOC.OPAK_N:=exec('opkn_add','opakow',_mag,'N',0,,,,FAKS.D,FAKS.KH,,$FAKS.ref());
         {? var_pres('__pomoc_mg_opak')=type_of('') || __pomoc_mg_opak:=_mag ?}
      |? var_pres('__pomoc_mg_opak')=type_of('') || __pomoc_mg_opak:=~~
      ?}
   ?}
?};
{? POMOC.OPAK_N<>null
||
   OPAK_P.cntx_psh();
   OPAK_P.index('OPAK_N');
   OPAK_P.prefix(POMOC.OPAK_N);
   OPAK_P.blank();
   OPAK_P.M:=_a;
   OPAK_P.IL:=ceil(FAP.IL/_b);
   OPAK_P.CZY_MOD:='N';
   OPAK_P.DK:=$FAP.ref();
   OPAK_N.cntx_psh();
   OPAK_P.KH:=OPAK_P.OPAK_N().KH;
   OPAK_P.KH_ODB:=OPAK_N.KH_ODB;
   OPAK_N.cntx_pop();
   {? OPAK_P.add()
   ||
      _wyn:=exec('zwr_rozp','opakow',OPAK_P.ref(),,1)
   ?};
   OPAK_P.cntx_pop()
?};
_wyn


\opkn_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: utworzenie naglowka dok. opakowan
::   WE: [_a] - magazyn opakowan
::       [_b] - przychod/rozchod
::       [_c] - czy wyswietlac okno redakcji (domyslnie tak=1)
::       [_d] - oddzial
::       [_e] - rok
::       [_f] - miesiac
::       [_g] - data
::       [_h] - KH.ref kontrahent
::       [_i] - $ND.ref
::       [_j] - $FAKS.ref
::   WY: _wyn - OPAK_N.ref()
::----------------------------------------------------------------------------------------------------------------------
_czytab:=var_pres('__mg_opak')>100;
{? _>=1
|| {? type_of(_a)<>2 || {? _czytab || _a:=__mg_opak.MAGAZYN || return(0) ?} ?}
|| {? _czytab || _a:=__mg_opak.MAGAZYN || return(0) ?}
?};
{? _>=2
|| {? type_of(_b)<>2 || {? _czytab || _b:=__mg_opak.PLUS || return(0) ?} ?}
|| {? _czytab || _b:=__mg_opak.PLUS || return(0) ?}
?};
{? _>=3 || {? type_of(_c)<>1 || _c:=1 ?} || _c:=1 ?};
{? _>=4 || {? type_of(_d)<>2 || _d:=ST.ODDZ ?} || _d:=ST.ODDZ ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=ST.AR ?} || _e:=ST.AR ?};
{? _>=6 || {? type_of(_f)<>1 || _f:=ST.AM ?} || _f:=ST.AM ?};
{? _>=7 || {? type_of(_g)<>4 || _g:=date() ?} || _g:=date() ?};
{? _>=8 || {? type_of(_h)<>7 || _h:=null ?} || _h:=null ?};
{? _>=9 || {? type_of(_i)<>2 || _i:='' ?} || _i:='' ?};
{? _>=10|| {? type_of(_j)<>2 || _j:='' ?} || _j:='' ?};
_wyn:=null;
{? _a<>''
||
   _pom_tab:=POM.TAB;
   _pom_typdok:=POM.TYPDOK;
   POM.TAB:='OPAK_N';
   POM.TYPDOK:='OPK';

   OPAK_N.cntx_psh();
   OPAK_N.clear();
   OPAK_N.blank();
   OPAK_N.MAG:=_a;
   OPAK_N.PLUS:=_b;
   {? _c=1
   ||
      OPAK_N.win_edit('RED');
      {? OPAK_N.add() & OPAK_N.r_lock(1,1,1)
      ||
         {? OPAK_N.edit("exec('chk_opkn','opakow')")
         ||
            {? OPAK_N.put() || _wyn:=OPAK_N.ref() ?};
            OPAK_N.r_unlock()
         ||
::          usuwanie numeru
            OPAK_N.r_unlock();
            numer:=OPAK_N.NR;
            oldnumer:=1;
            exec('nr_old','numery');
            OPAK_N.del(1)
         ?}
      ||
         FUN.info('Nieudana próba zablokowania tabeli nagłówków dokumentów opakowań.\nSpróbuj ponownie.'@)
      ?}
   ||
::    generowanie naglowka
      OPAK_N.DATA:=_g;
      OPAK_N.KH:=_h;
      OPAK_N.ND:=_i; exec('FindAndGet','#table',ND,_i,,"OPAK_N.KH_ODB:=KH_ODB");
      OPAK_N.FAKS:=_j; exec('FindAndGet','#table',FAKS,_j,,"OPAK_N.KH_ODB:=KH_ODB");
      {? OPAK_N.add()
      ||
         _nr:=exec('numer_new','numery','PACZKA');
         {? _nr>0
         ||
            OPAK_N.NR:=_nr;
            exec('znak','numery','OPAK_N');
            {? OPAK_N.put() || _wyn:=OPAK_N.ref() ?}
         ?}
      ?}
   ?};
   OPAK_N.cntx_pop();
   OPAK_N.seek(_wyn);

   POM.TAB:=_pom_tab;
   POM.TYPDOK:=_pom_typdok
?};
_wyn


\chk_opkn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord po dla naglowkow dokumentow opakowan
::----------------------------------------------------------------------------------------------------------------------
_wyn:=__CHK.record(OPAK_N,,'NR','SYM');
{? _wyn=''
||
   {? OPAK_N.DATA<date(ST.AR,ST.AM,1) | OPAK_N.DATA>date(ST.AR,ST.AM,0)
   || FUN.info('Data dokumentu musi pochodzić z bieżącego okresu rozliczeniowego\n%1.'@[ST.OKR]);
      _wyn:='DATA'
   ?}
?};
{? _wyn='' || _wyn:=__CHK.record(OPAK_N,,'KH') ?};
_wyn


\zwr_rozp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: utworzenie pozycji skladowych dla rozchodu - FIFO - dla kontrahenta a potem reszta
::       OPAK_P.ref w buforze
::   WE: _a - OPAK_P.ref()
::       [_b] - 1-kontroluj ilość, 0-domyślnie nie
::       [_c] - 0/1 - twórz zapisy tymczasowe w OPAK_ZWR
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_ctrlil:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_opak_zwr_tmp:=var_pres('_c')=type_of(0) & _c;

_wyn:=1;
M.cntx_psh();
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();
{? OPAK_P.OPAK_N().PLUS='N'
||
   _do_rozp:=OPAK_P.IL;
   exec('opkc_mk','opakow',,OPAK_P.OPAK_N().MAG,'',OPAK_P.M().KTM,'T',$OPAK_P.ref(),0,OPAK_P.OPAK_N().DATA
    ,{? _ctrlil || _do_rozp || -1 ?});
   {? var_pres('__opkc_tab')>100 & __opkc_tab.first()
   ||
      {!
      |?
         {? _do_rozp>=__opkc_tab.IL_POZ
         ||
            __opkc_tab.IL:=__opkc_tab.IL_POZ;
            _do_rozp-=__opkc_tab.IL_POZ
         ||
            __opkc_tab.IL:=_do_rozp;
            _do_rozp:=0
         ?};
         __opkc_tab.WAR:=(__opkc_tab.IL*__opkc_tab.KAUCJA)$2;
         {? __opkc_tab.put() & _opak_zwr_tmp
         ||
            _ref_rozl:=$OPAK_P.ref();
            _ref_poz:=__opkc_tab.REF_POZ;
            _il:=__opkc_tab.IL;
            exec('opak_zwr_update','opakow_stany',_ref_rozl,_ref_poz,'',_il,'N','T')
         ?};
         _do_rozp>0 & __opkc_tab.next()
      !};
      {? _do_rozp>0
      ||
         _txt1:='W magazynie opakowań %1 zabrakło wymaganej ilości do wydania dla opakowania:'@[OPAK_P.OPAK_N().MAG];
         _txt2:=OPAK_P.M().KTM+' - '+OPAK_P.M().N;
         _txt3:='Pomniejszono wydaną ilość.'@;
         {? var_pres('__kom')>100
         ||
            exec('add_kom','#message',_txt1,4);
            exec('add_kom','#message',_txt2+'. '+_txt3,4)
         ||
            FUN.info(_txt1+'\n'+_txt2+'.\n\n'+_txt3)
         ?};
         OPAK_P.IL:=OPAK_P.IL-_do_rozp;
         OPAK_P.WAR:=(OPAK_P.IL*OPAK_P.KAUCJA)$2;
         OPAK_P.put()
      ?}
   || _wyn:=0;
      _txt1:='W magazynie opakowań %1 brak ilości do wydania dla opakowania:'@[OPAK_P.OPAK_N().MAG];
      _txt2:=OPAK_P.M().KTM+' - '+OPAK_P.M().N;
      _txt3:='Nie utworzono pozycji z opakowaniem.'@;
      {? var_pres('__kom')>100
      ||
         exec('add_kom','#message',_txt1,4);
         exec('add_kom','#message',_txt2+'. '+_txt3,4)
      ||
         FUN.info(_txt1+'\n'+_txt2+'.\n\n'+_txt3)
      ?};
      OPAK_P.del();
      {? POMOC.OPAK_N<>null
      ||
         _pom_tab:=POM.TAB;
         _pom_typdok:=POM.TYPDOK;
         POM.TAB:='OPAK_N';
         POM.TYPDOK:='OPK';

         OPAK_P.index('OPAK_N');
         OPAK_P.prefix(POMOC.OPAK_N);
         {? ~OPAK_P.size() & OPAK_N.seek(POMOC.OPAK_N)
         ||
            numer:=OPAK_N.NR;
            oldnumer:=1;
            exec('nr_old','numery');
            POMOC.OPAK_N:=null;
            OPAK_N.del()
         ?};
         POM.TAB:=_pom_tab;
         POM.TYPDOK:=_pom_typdok
      ?}
   ?};

   exec('opkc_akc','opakow')
?};
OPAK_P.cntx_pop();
OPAK_N.cntx_pop();
M.cntx_pop();
_wyn


\opkc_mk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: utworzenie szczegolowej tabeli stanow opakowan __opkc_tab
::       dla magazynu, kontrahenta, szczegoly dla opakowania (OPAKOWANIE, KAUCJA) - F3 dla OPAK_P.IL
::       zalozenie: tabela __opkc_tab jest zdefiniowana
::   WE: _a - nie używany
::       [_b] - Magazyn
::       [_c] - KOD Kontrahenta
::       [_d] - M.KTM Indeks
::       [_e] - PLUS - przychod/rozchod - dla szczegolowych danych
::       [_f] - OPAK_P.refsql pozycji edytowanej
::       [_g] - czy select
::       [_h] - na dzien
::       [_i] - ilość do rozpisania - jesli większa od 0 to pobiera dostawy tylko do tej ilości, -1(domyślnie)-wszystkie
::       [_j] - odbiorca kontrahenta
::----------------------------------------------------------------------------------------------------------------------
_ildo:={? var_pres('_i')=type_of(0) || _i || -1 ?};
_break:=0;

_brakdt:=0;

{? _>=2 || {? type_of(_b)<>2 || _b:='' ?} || _b:='' ?};
{? _>=3 || {? type_of(_c)<>2 || _c:='' ?} || _c:='' ?};
{? _>=4 || {? type_of(_d)<>2 || _d:='' ?} || _d:='' ?};
{? _>=5 || {? type_of(_e)<>2 || _e:='' ?} || _e:='' ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=1 ?} || _g:=1 ?};
{? _>=8 || {? type_of(_h)<>4 || _brakdt:=1; _h:=date(0,0,0) ?} || _brakdt:=1; _h:=date(0,0,0) ?};
{? var_pres('_j')<>type_of(null()) || _j:=null() ?};

_kh_odb:=_j;

VAR_DEL.delete('__disp');

_wyn:=0;
_ok:=0;
SUM.S:=SUM.WAR:=0;

M.cntx_psh();
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();

_kh:=exec('kh_ref','opakow',_c);

{? var_pres('__opkc_tab')>100
||
   __opkc_tab.prefix(); {? __opkc_tab.first() || {! |? __opkc_tab.del() !} ?}
||
   VAR_DEL.delete('__opkc_tab','__opak_sd','__opak_roz');
:: stany dostaw lub stany kaucji
   __opkc_tab:=tab_tmp(4
      ,'KH_KOD','STRING[5]','Kod'
      ,'KH_SKR','STRING[10]','Kontrahent'
      ,'ODB_KOD','STRING[5]','Kod'
      ,'ODB_SKR','STRING[10]','Odbiorca'
      ,'SYMBOL','STRING[20]','Symbol'
      ,'DATA','DATE','Data'
      ,'ZWROT','STRING[1]','Z'
      ,'POZ','INTEGER','Poz.'
      ,'INDEKS','STRING[50]','Indeks'
      ,'NAZWA','STRING[100]','Nazwa'
      ,'CUR_IL','REAL','Ilość związana z pozycją'
      ,'IL_POZ','REAL','Pozostało'
      ,'CZY_KAUC','STRING[1]','K'
      ,'KAUCJA','REAL','Kaucja'
      ,'WAR_POZ','REAL','Wartość pozostała'
      ,'IL','REAL','Ilość'
      ,'WAR','REAL','Wartość'
      ,'REF_ROZL','STRING[16]','REF_ROZL'
      ,'REF_POZ','STRING[16]','REF_POZ'
   )
?};

{? _g=1
||
   {? var_pres('__opkc_sel')>0
   ||
      __opkc_tab.win_sel(''); __opkc_tab.win_del(__opkc_sel); VAR_DEL.delete('__opkc_sel')
   ?};
   {? var_pres('__opkc_sel')<=0
   ||
      __opkc_sel:=__opkc_tab.mk_sel('Szczegółowy stan opakowania'@,'N',,'_opkc_mk_poz_');
      __opkc_tab.win_fld(__opkc_sel,,'KH_KOD',,,5,,1,'Kontrahent'@);
      __opkc_tab.win_fld(__opkc_sel,,'KH_SKR',,,10,,1,'Skrót'@);
      __opkc_tab.win_fld(__opkc_sel,,'ODB_KOD',,,5,,1,'Odbiorca'@);
      __opkc_tab.win_fld(__opkc_sel,,'ODB_SKR',,,10,,1,'Skrót'@);
      __opkc_tab.win_fld(__opkc_sel,,'SYMBOL',,,10,,1,'Dokument'@);
      __opkc_tab.win_fld(__opkc_sel,,'DATA',,,10,,1,'Data'@);
      __opkc_tab.win_fld(__opkc_sel,,'ZWROT',,,-3,,1,'Zwrot'@,,,2,,"'T'","'N'");
      __opkc_tab.win_fld(__opkc_sel,,'POZ',,,-3,,1,'Pozycja'@);
      __opkc_tab.win_fld(__opkc_sel,,'INDEKS',,,15,,1,'Indeks'@);
      __opkc_tab.win_fld(__opkc_sel,,'NAZWA',,,7,,1,'Nazwa'@);
      __opkc_tab.win_fld(__opkc_sel,,'IL_POZ',,,10,3,1,'Pozostało'@);
      __opkc_tab.win_fld(__opkc_sel,,'IL',,,10,3,,'Do rozliczenia'@);
      __opkc_tab.win_fld(__opkc_sel,DISP,'JM','KOD',,3,,,'jm'@);
      __opkc_tab.win_fld(__opkc_sel,,'CZY_KAUC',,,-3,,1,'Czy kaucja'@,,,2,,"'T'","'N'");
      __opkc_tab.win_fld(__opkc_sel,,'KAUCJA',,,8,2,1,'Kaucja'@);
      __opkc_tab.win_fld(__opkc_sel,,'WAR',,,10,2,1,'Wartość'@);
      __opkc_tab.win_act(__opkc_sel,,'Szukaj');
      __opkc_tab.win_act(__opkc_sel,,'Formuła','&Wybierz'@@,,,,"exec('opkc_sel_wybierz','opakow')",1,1,,,'W');
      __opkc_tab.win_act(__opkc_sel,,'Popraw',,,,"__opkc_tab.IL_POZ+__opkc_tab.IL>0");
      __opkc_tab.win_act(__opkc_sel,,'Formuła','&Akceptuj'@@,,,,"sel_exit()",,,,,'A');
      __opkc_tab.win_act(__opkc_sel,,'Rekord',,,,"exec('opkc_rek','opakow')","exec('opkc_rpo','opakow')")
   ?};
   __opkc_tab.win_sel(__opkc_sel)
?};

KH.cntx_psh();
KH_ODB.cntx_psh();
M.cntx_psh();
_mag:=_b;
_mat:=exec('FindInSet','#table','M','MATKTM',_d,,,1,,null());
{? _kh
||
:: zwrot
   _params:=obj_new('REF_ROZL','PLUS_ROZL');
   OPAK_SKD.index('UNIK');
   OPAK_SKD.prefix(_mag,_kh,_mat);
   _loop:=OPAK_SKD.first();
   {!
   |? _loop
   |!
      {? OPAK_SKD.DATA<=OPAK_N.DATA
      || __plus:=OPAK_N.PLUS;
         _params.REF_ROZL:=$OPAK_P.ref();
         _params.PLUS_ROZL:=OPAK_N.PLUS;
         exec('FindAndGet','#table',OPAK_P,OPAK_SKD.POZ_DOK,,"
            _params:=_b;
            exec(\'FindAndGet\',\'#table\',@.OPAK_N,OPAK_P.OPAK_N,,\"
               _params:=_b;
               _ref_rozl:=_params.REF_ROZL;
               {? _params.PLUS_ROZL<>OPAK_N.PLUS
               ||
                  _il_poz:=
                     {? OPAK_N.PLUS='T'
                     || OPAK_SKD.KH_SK-exec('stanydost_w_wydaniu','opakow_stany',$OPAK_P.ref(),_ref_rozl)
                     || OPAK_SKD.N_SK-exec('stanydost_w_przyjeciu','opakow_stany',$OPAK_P.ref(),_ref_rozl)
                     ?};
                  {? _il_poz
                  ||
                     _cur_il:=exec('zwr_rozl','opakow',_ref_rozl,$OPAK_P.ref(),'T');
                     __opkc_tab.KH_KOD:=OPAK_N.KH().KOD;
                     __opkc_tab.KH_SKR:=OPAK_N.KH().SKR;
                     __opkc_tab.ODB_KOD:=OPAK_N.KH_ODB().KOD;
                     __opkc_tab.ODB_SKR:=OPAK_N.KH_ODB().SKR;
                     __opkc_tab.SYMBOL:=OPAK_N.SYM;
                     __opkc_tab.DATA:=OPAK_N.DATA;
                     __opkc_tab.ZWROT:=OPAK_P.ZWROT;
                     __opkc_tab.POZ:=OPAK_P.POZ;
                     __opkc_tab.INDEKS:=OPAK_P.M().KTM;
                     __opkc_tab.NAZWA:=OPAK_P.M().N;
                     __opkc_tab.IL_POZ:=_il_poz-_cur_il;
                     __opkc_tab.CZY_KAUC:=OPAK_P.CZY_KAUC;
                     __opkc_tab.KAUCJA:=OPAK_P.KAUCJA;
                     __opkc_tab.WAR_POZ:=(__opkc_tab.IL_POZ*__opkc_tab.KAUCJA)$2;
                     __opkc_tab.CUR_IL:=__opkc_tab.IL:=_cur_il;
                     __opkc_tab.WAR:=(__opkc_tab.IL*__opkc_tab.KAUCJA)$2;
                     __opkc_tab.REF_ROZL:=_params.REF_ROZL;
                     __opkc_tab.REF_POZ:=$OPAK_P.ref();
                     __opkc_tab.add()
                  ?}
               ?}
            \",,_params)
         ",,_params)
      ?};
      _loop:=OPAK_SKD.next()
   !}
||
:: wydanie
   OPAK_SD.index('MAG');
   OPAK_SD.prefix(_mag,_mat);
   _loop:=OPAK_SD.first();
   {!
   |? _loop
   |!
      {? OPAK_SD.DATA<=OPAK_N.DATA
      ||
         exec('FindAndGet','#table',OPAK_P,OPAK_SD.OPAK_P,,"
            exec(\'FindAndGet\',\'#table\',@.OPAK_N,OPAK_P.OPAK_N,,\"
               __opkc_tab.KH_KOD:=OPAK_N.KH().KOD;
               __opkc_tab.KH_SKR:=OPAK_N.KH().SKR;
               __opkc_tab.ODB_KOD:=OPAK_N.KH_ODB().KOD;
               __opkc_tab.ODB_SKR:=OPAK_N.KH_ODB().SKR;
               __opkc_tab.SYMBOL:=OPAK_N.SYM;
               __opkc_tab.DATA:=OPAK_N.DATA;
               __opkc_tab.ZWROT:=OPAK_P.ZWROT;
               __opkc_tab.POZ:=OPAK_P.POZ;
               __opkc_tab.INDEKS:=OPAK_P.M().KTM;
               __opkc_tab.NAZWA:=OPAK_P.M().N;
               __opkc_tab.IL_POZ:=OPAK_SD.SD;
               __opkc_tab.CZY_KAUC:=OPAK_P.CZY_KAUC;
               __opkc_tab.KAUCJA:=OPAK_P.KAUCJA;
               __opkc_tab.WAR_POZ:=(__opkc_tab.IL_POZ*__opkc_tab.KAUCJA)$2;
               __opkc_tab.CUR_IL:=__opkc_tab.IL:=exec('zwr_rozl','opakow',_b,$OPAK_P.ref());
               __opkc_tab.WAR:=(__opkc_tab.IL*__opkc_tab.KAUCJA)$2;
               __opkc_tab.REF_ROZL:=_b;
               __opkc_tab.REF_POZ:=$OPAK_P.ref();
               __opkc_tab.add()
            \",,_b)
         ",,$OPAK_P.ref())
      ?};
      _loop:=OPAK_SD.next()
   !}
?};
M.cntx_pop();
KH_ODB.cntx_pop();
KH.cntx_pop();

{? _g=1
||
   __opkc_tab.first();
   {? __opkc_tab.select()
   ||
      exec('opkc_sum','opakow');
      _wyn:=1
   ||
      __opkc_tab.erase()
   ?}
?};

:: UWAGA nie mozna kasowac tabeli __opkc_tab - jest wykorzystywana podczas akceptacji pozycji
VAR_DEL.delete('__disp');

OPAK_P.cntx_pop();
OPAK_N.cntx_pop();
M.cntx_pop();
_wyn


\opkc_sel_wybierz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Szczegółowy stan opakowania - Wybierz
::   WE: _a - ref_rozl
::       _b - ref_poz
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_il:={? __opkc_tab.IL || 0 || __opkc_tab.IL_POZ ?};
exec('opkc_update_do_rozliczenia','opakow',__opkc_tab.REF_ROZL,__opkc_tab.REF_POZ,_il)


\opkc_update_do_rozliczenia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizacja ilości do rozliczenia
::   WE: _a - pozycja rozliczająca
::       _b - pozycja rozliczana
::       _c - ilość rozliczana
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: _ref_rozl - $OPAK_P.ref() redagowanej pozycji
_ref_rozl:=_a;
_ref_poz:=_b;
_il:=_c;
do();
exec('opak_zwr_del','opakow_stany',_ref_rozl,'T',_ref_poz);
{? OPAK_N.PLUS='N'
||
   {? OPAK_P.ZWROT='T'
   ||
:: rozchód - zwrot kaucji kontrahenta
:: _ref_poz - $OPAK_P.ref() dostawy z kaucją kontrahenta
      exec('stanydost_rozpisz','opakow_stany',OPAK_N.MAG,OPAK_P.M,OPAK_N.DATA,_il,_ref_poz,_ref_rozl)
   ||
:: rozchód
:: _ref_poz - $OPAK_P.ref() dostawy
      exec('opak_zwr_update','opakow_stany',_ref_rozl,_ref_poz,'',_il,'N','T')
   ?};
   __opkc_tab.CUR_IL:=__opkc_tab.IL:=exec('zwr_rozl','opakow',_ref_rozl,_ref_poz,'T');
   __opkc_tab.WAR:=(__opkc_tab.IL*__opkc_tab.KAUCJA)$2;
   __opkc_tab.IL_POZ:=0;
   {? OPAK_P.ZWROT='T'
   ||
:: rozchód - zwrot kaucji kontrahenta
      OPAK_SKD.cntx_psh();
      OPAK_SKD.index('UNIK');
      OPAK_SKD.prefix(OPAK_N.MAG,OPAK_N.KH,OPAK_P.M,_ref_poz);
      {? OPAK_SKD.first() || __opkc_tab.IL_POZ:=OPAK_SKD.KH_SK-__opkc_tab.CUR_IL ?};
      OPAK_SKD.cntx_pop()
   ||
:: rozchód
      _ilw:=exec('zwr_rozl','opakow',_ref_rozl,_ref_poz,'N');
      OPAK_SD.cntx_psh();
      OPAK_SD.index('DOST');
      OPAK_SD.prefix(_ref_poz);
      {? OPAK_SD.first() || __opkc_tab.IL_POZ:=OPAK_SD.SD+_ilw-__opkc_tab.CUR_IL ?};
      OPAK_SD.cntx_pop()
   ?};
   __opkc_tab.put()
||
   {? OPAK_P.ZWROT='T'
   ||
:: przychód - zwrot naszych kaucji
:: _ref_poz - $OPAK_P.ref() rozchodu z kaucją naszą
      exec('opak_zwr_update','opakow_stany',_ref_rozl,_ref_poz,'',_il,'N','T');
      __opkc_tab.CUR_IL:=__opkc_tab.IL:=exec('zwr_rozl','opakow',_ref_rozl,_ref_poz,'T');
      __opkc_tab.WAR:=(__opkc_tab.IL*__opkc_tab.KAUCJA)$2;
      OPAK_SKD.cntx_psh();
      OPAK_SKD.index('UNIK');
      OPAK_SKD.prefix(OPAK_N.MAG,OPAK_N.KH,OPAK_P.M,_ref_poz);
      {? OPAK_SKD.first() || __opkc_tab.IL_POZ:=OPAK_SKD.N_SK-__opkc_tab.CUR_IL ?};
      OPAK_SKD.cntx_pop();
      __opkc_tab.put()
   ||
:: przychód
      ~~
   ?}
?};
end()


\opkc_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: oblicza ilosc dostepna dla biezacej dostawy __opkc_tab w danej chwili
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();
OPAK_N.use('opakn'+((8+__opkc_tab.REF_POZ)+3));
OPAK_N.prefix();
OPAK_P.use(8+__opkc_tab.REF_POZ);
OPAK_P.prefix();
{? OPAK_P.seek(__opkc_tab.REF_POZ,8+__opkc_tab.REF_POZ) & OPAK_P.OPAK_N().AKC='T'
|| _kontr:={? exec('zwrzopak','opakow',__opkc_tab.REF_ROZL,'OPAK_N().KH')<>OPAK_P.OPAK_N().KH
           || null()
           || OPAK_P.OPAK_N().KH
           ?};
   _il_zwr:=exec('zwr_poz','opakow',$OPAK_P.ref(),_kontr,,__opkc_tab.REF_ROZL);

   {? _il_zwr>0
    | (OPAK_P.OPAK_N().KH<>null() & exec('zwrzopak','opakow',__opkc_tab.REF_ROZL,'OPAK_N().PLUS')='T')
   || _il_poz:=OPAK_P.IL
   || _aktstn:=exec('stanm_mg','opakow',OPAK_P.M,OPAK_P.OPAK_N().MAG);
      _il_poz:={? OPAK_P.IL<=_aktstn || OPAK_P.IL || _aktstn ?}
   ?};

   _il_poz:={? _il_zwr<=_il_poz || _il_poz-_il_zwr || 0 ?};
   _wyn:=_il_poz
?};
OPAK_P.cntx_pop();
OPAK_N.cntx_pop();
_wyn


\opkc_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord przed dla szczegolow stanow opakowan __opkc_tab
::----------------------------------------------------------------------------------------------------------------------
__opkc_tab.IL>0


\opkc_rpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord po dla szczegolow stanow opakowan __opkc_tab
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? _wyn='' & __opkc_tab.IL<0
||
   FUN.info('Podana ilość jest mniejsza od zera.'@);
   _wyn:='IL'
?};
:: ustalenie ilosci dostepnej przed zatwierdzeniem popraw
{? exec('FindAndGet','#table',OPAK_P,__opkc_tab.REF_ROZL,,"
      exec('FindAndGet','#table',@.OPAK_N,@.OPAK_P.OPAK_N,,\"
         @.OPAK_N.PLUS='T'
      \",0)
   ",0)
||
   {? _wyn='' & __opkc_tab.IL>__opkc_tab.IL_POZ+__opkc_tab.CUR_IL
   ||
      FUN.info('Podana ilość %1 jest większa\n'
               'od ilości do rozliczenia %2.'@[form(__opkc_tab.IL,,3),form(__opkc_tab.IL_POZ+__opkc_tab.CUR_IL,,3)]);
      _wyn:='IL'
   ?}
||
   _opkc_sd:=exec('opkc_sd','opakow');
   {? _wyn='' & __opkc_tab.IL>__opkc_tab.CUR_IL+_opkc_sd
   ||
      FUN.info('Podana ilość %1 jest większa\n'
               'od ilości dostępnej %2.'@[form(__opkc_tab.IL,,3),form(__opkc_tab.CUR_IL+_opkc_sd,,3)]);
      _wyn:='IL'
   ?}
?};
{? _wyn=''
||
   exec('opkc_update_do_rozliczenia','opakow',__opkc_tab.REF_ROZL,__opkc_tab.REF_POZ,__opkc_tab.IL)
?};
_wyn


\zwr_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: oblicza ilosc jednej pozycji skladowej
::   WE: _a - $OPAK_P.ref() pozycji rozliczajacej
::       _b - $OPAK_P.ref() pozycji rozliczanej
::       _c - T/N - tymczasowe
::----------------------------------------------------------------------------------------------------------------------
_tmp:={? var_pres('_c')=type_of('') || _c || 'N' ?};
_il:=0;
OPAK_ZWR.cntx_psh();
:: po wszystkich okresach
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |?
      OPAK_ZWR.use('opakz'+ST.ODDZ+($OKR.ROK+2));
      OPAK_ZWR.index('REFTROZL');
      OPAK_ZWR.prefix(_tmp,_a,);
      _loop:=OPAK_ZWR.first();
      {!
      |? _loop
      |!
         {? OPAK_ZWR.REF_POZ=_b | OPAK_ZWR.REF_POZK=_b || _il+=OPAK_ZWR.IL ?};
         _loop:=OPAK_ZWR.next()
      !};
      OKR.next()
   !}
?};
OKR.cntx_pop();
OPAK_ZWR.cntx_pop();
_il


\opkc_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: sumuje __opkc_tab
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__opkc_tab')>100 & __opkc_tab.first()
||
   SUM.S:=SUM.WAR:=0;
   {!
   |?
      SUM.S+=__opkc_tab.IL;
      SUM.WAR+=(__opkc_tab.IL*__opkc_tab.KAUCJA)$2;
      __opkc_tab.next()
   !}
?};
1


\opkc_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akceptuj __opkc_tab - rozpisuje tabele zwrotow OPAK_ZWR
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__opkc_tab')>100 & __opkc_tab.first()
||
   exec('opak_zwr_akc','opakow_stany',$OPAK_P.ref);
   SUM.S:=SUM.WAR:=0;
   {!
   |?
      SUM.S+=__opkc_tab.IL;
      SUM.WAR+=(__opkc_tab.IL*__opkc_tab.KAUCJA)$2;
      __opkc_tab.next()
   !}
?};
1


\opdk_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: generowanie pozycji opakowan wg wszystkich pozycji dokumentu magazynowego
::   WE: _a - ref naglowka dok. magazynowego ND.ref()
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
ND.cntx_psh();
DK.cntx_psh();
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();
ND.clear();
{? ND.seek(_a) & ND.Z='N'
||
   DK.index('DOKMAG');
   DK.prefix(ND.ref());
   {? DK.first()
   ||
      {!
      |?
         _ok:=exec('dk_one','opakow');
         _ok=1 & DK.next()
      !}
   ?}
?};
OPAK_P.cntx_pop();
OPAK_N.cntx_pop();
DK.cntx_pop();
ND.cntx_pop();
''


\dk_one
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: dla pojedynczej pozycji dokumentu magazynowego, DK w buforze
::       jesli nie wygenerowano z pozycji opkowan, analizuje kartoteke opakowan i generuje pozycje dokumentu opakowan
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? DK.FAP<>''
||
   FAP.cntx_psh();
   FAKS.cntx_psh();
   FAP.use(8+DK.FAP);
   FAKS.use('faktu'+(FAP.name()+3));
   FAP.prefix();
   {? FAP.seek(DK.FAP,8+DK.FAP) & FAP.FAKS().T().KOR='N' & DK.OPAK_GEN<>FAP.OPAK_GEN
   ||
      DK.OPAK_GEN:=FAP.OPAK_GEN;
      DK.put()
   ?};
   FAP.cntx_pop();
   FAKS.cntx_pop()
?};
{? DK.OPAK_GEN='N'
||
:: usuwa wszystkie powiazane ale niezmodyfikowane pozycje opakowan
   OPAK_P.index('DK');
   OPAK_P.prefix($DK.ref(),'N');
   {? OPAK_P.first()
   ||
      _del:=1;
      {!
      |?
         _del:=exec('opkp_del','opakow',0,'D');
         _del>1
      !};
      {? _del=0 || _wyn:=0 ?}
   ?};

   {? _wyn=1
   ||
      {? DK.M().OPAKOW<>'T'
      ||
::       dodaje wg kartoteki opakowan
         M_OPAKOW.index('M_OPAKOW');
         M_OPAKOW.prefix(DK.M);
         {? M_OPAKOW.first()
         ||
            {!
            |?
               _wyn:=exec('opkp_one','opakow',M_OPAKOW.OPAKOW,M_OPAKOW.POJ);
               _wyn=1 & M_OPAKOW.next()
            !}
         ?}
      ||
         _wyn:=exec('opkp_one','opakow',DK.M,1)
      ?}
   ?};

   {? _wyn=1
   ||
      DK.OPAK_GEN:='T';
      _wyn:=DK.put()
   ?}
?};
_wyn


\opkp_one
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: generowanie pojedynczej pozycji opakowan wg pozycji dok.mag. - ND, DK w buforze
::       POMOC.OPAK_N - jesli <>null tzn ze naglowek opakowan istnieje, gdy =null -> generuje naglowek opakowan
::   WE: _a - M.ref
::       _b - pojemnosc opakowania
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
:: sprawdzam czy nie ma powiazanego z pozycja ale poprawionego recznie
OPAK_P.index('DK');
OPAK_P.prefix($DK.ref(),'T',_a);
{? ~OPAK_P.first()
||
   {? POMOC.OPAK_N=null & ND.MAG().MG_OPAK<>''
   || POMOC.OPAK_N:=exec('opkn_add','opakow',ND.MAG().MG_OPAK,ND.TYP().P,0,,,,ND.D,ND.KH,$ND.ref(),ND.FAKS_REF)
   ?};
   {? POMOC.OPAK_N<>null
   ||
      OPAK_P.cntx_psh();
      OPAK_P.index('OPAK_N');
      OPAK_P.prefix(POMOC.OPAK_N);
      OPAK_P.blank();
      OPAK_P.M:=_a;
      OPAK_P.IL:=ceil(DK.IL/_b);
      OPAK_P.CZY_MOD:='N';
      OPAK_P.DK:=$DK.ref();
      OPAK_N.cntx_psh();
      OPAK_P.KH:=OPAK_P.OPAK_N().KH;
      OPAK_P.KH_ODB:=OPAK_N.KH_ODB;
      OPAK_N.cntx_pop();
      {? OPAK_P.add()
      || _wyn:=exec('zwr_rozp','opakow',OPAK_P.ref(),,1)
      ?};
      OPAK_P.cntx_pop()
   ?}
||
:: _wyn=1 poniewaz dana pozycja juz istnieje - nie trzeba jej generowac
   _wyn:=1
?};
_wyn


\opkp_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord przed dla pozycji dokumentow opakowan
::----------------------------------------------------------------------------------------------------------------------
exec('init_var','opakow');
{? _a || exec('opkp_act','opakow') ?};
exec('rekprzed','color','OPK#01')


\opak_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: po obsludze zakladki opakowania
::       sprawdza czy dokument POMOC.OPAK_N ma pozycje, jesli nie ma pozycji usuwa caly dokument OPAK_N
::----------------------------------------------------------------------------------------------------------------------
{? POMOC.OPAK_N<>null
||
   OPAK_P.cntx_psh();
   OPAK_P.index('OPAK_N');
   OPAK_P.prefix(POMOC.OPAK_N);
   {? ~OPAK_P.first()
   ||
      {? cur_tab()=DK
      || exec('nd_del','magdok_nag',$ND.ref())
      |? cur_tab()=FAKS | cur_tab()=FAP
      || exec('faks_del','faktury_nag',$FAKS.ref())
      ?}
   ?};
   OPAK_P.cntx_pop()
?};
''


\opdk_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: modyfikacja pozycji DK, funkcja \dk_one ktora szuka powiazanych pozycji opakowan
::       bedzie uruchomiana po przelaczniu na zakladke opakowania lub przy akceptacji
::----------------------------------------------------------------------------------------------------------------------
DK.OPAK_GEN:='N'


\nd_z
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akceptacja/wycofanie powiazanego dokumentu magazynowego
::       ND.ref() w buforze
::   WE: _a - 1-akceptacja   0-wycofanie
::   WY: domyslnie=1 tzn nie ma powiazanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
OPAK_N.index('ND');
OPAK_N.prefix($ND.ref());
{? OPAK_N.first()
||
   POMOC.OPAK_N:=OPAK_N.ref();
   _wyn:={? _a=1 || exec('opkn_akc','opakow',0,'D') || exec('opkn_wyc','opakow',0,'D') ?}
?};
_wyn


\opkn_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akceptacja dokumentu opakowan
::   WE: [_a] - 1-pytania/komunikaty   0-bez komunikatow (tryb auto)
::       [_b] - miejsce wywołania: 'D'-dokumenty 'F'-faktury 'R'-realizacje lub pusty string
::   WY: _wyn - 0 - nie zostal zaakceptowany   1 - zaakceptowany   -1 - dokument usuniety
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.get();
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};
{? _>=2 || {? type_of(_b)<>2 || _b:='' ?} || _b:='' ?};

_wyn:=0;
_ok:=1;
OPAK_N.cntx_psh();
:: sprawdza czy ma pozycje
OPAK_P.index('OPAK_N');
OPAK_P.prefix(OPAK_N.ref());
{? ~OPAK_P.first()
||
   {? _a=1
   ||
      FUN.info('Dokument nie posiada pozycji.\nAkceptacja niemożliwa.'@)
   ||
::    w trybie auto - usuwa dokument bez pozycji
      exec('opkn_del','opakow',0,_b);
      _wyn:=-1
   ?};
   _ok:=0
?};
{? _ok=1 || _ok:=exec('lock','opakow',_b) ?};
{? _ok=1 & _a=1 || _ok:=(exec('czy_mod','opakow',OPAK_N.AKC) & FUN.ask('Akceptować dokument?'@)) ?};
{? _ok=1
||
   OPAK_N.AKC:='T';
   OPAK_N.DA:=date();
   OPAK_N.TA:=time();
   _wyn:=OPAK_N.put();
   {? _wyn=1 || _wyn:=exec('opak_obl','opakow') ?}
?};
OPAK_N.r_unlock();
OPAK_N.cntx_pop();
_wyn


\opkn_wyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: wycofuje akceptacje dokumentu opakowan
::   WE: [_a] - 1-pytanie
::       [_b] - miejsce wywołania: 'D'-dokumenty 'F'-faktury 'R'-realizacje lub pusty string
::   WY: _wyn - 0/1
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.get();
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};
{? _>=2 || {? type_of(_b)<>2 || _b:='' ?} || _b:='' ?};
_wyn:=0;
OPAK_N.cntx_psh();
_ok:=exec('lock','opakow',_b);
{? _ok=1 & _a=1 || _ok:=~exec('czy_mod','opakow',OPAK_N.AKC,0) ?};
{? _ok=1 & _a=1 || _ok:=FUN.ask('Wycofać akceptację dokumentu?'@) ?};
:: dla dok. przychodowych sprawdza czy nie ma powiazanych rozchodow
{? _ok=1
||
   OPAK_P.index('OPAK_N');
   OPAK_P.prefix(OPAK_N.ref());
   {? OPAK_P.first()
   ||
      {!
      |?
         _ok:=exec('czy_roz','opakow');
         _ok=1 & OPAK_P.next()
      !}
   ?}
?};
{? _ok=1
||
   OPAK_N.AKC:='N';
   OPAK_N.DA:=date(0,0,0);
   OPAK_N.TA:=time(0,0,0);
   _wyn:=OPAK_N.put();
   {? _wyn=1 || _wyn:=exec('opak_obl','opakow') ?}
?};
OPAK_N.r_unlock();
OPAK_N.cntx_pop();
_wyn


\opak_obl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: petla po pozycjach opakowan i obliczanie stanow (wlasciwy OPAK_N.ref w buforze)
::   WY: _ok - 0/1
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;

_do_state:=do_state();
{? _do_state=0 || do() ?};
OPAK_P.cntx_psh();
OPAK_P.index('OPAK_N');
OPAK_P.prefix(OPAK_N.ref());
{? OPAK_P.first()
||
   OPAK_P.OPAK_N();
   {!
   |?
      _ok:=exec('rebuild_poz','opakow_stany',0);
      _ok=1 & OPAK_P.next()
   !}
?};
OPAK_P.cntx_pop();
{? _ok=0 || undo() ?};
{? _do_state=0 || _ok:=end() ?};
_ok


\opnf_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: dodaje $FAKS.ref() do powiazanego dok. opakowan
::   WE: _a - $ND.ref() - powiazany dokument
::       _b - $FAKS.ref() - powiazana faktura
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.index('ND');
OPAK_N.prefix(_a);
{? OPAK_N.first() || OPAK_N.FAKS:=_b; OPAK_N.put() ?}


\opkw_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: dodanie pozycji opakownia z okna pozycji dokumentu magazynowego
::       dla pierwszej pozycji generuje naglowek
::   WE: 1-dodano 0-nie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
:: kasowanie tabeli szczegolow
{? var_pres('__opkc_tab')>100
|| __opkc_tab.prefix(); {? __opkc_tab.first() || {! |? __opkc_tab.del() !} ?}
?};

_pom_tab:=POM.TAB;
_pom_typdok:=POM.TYPDOK;
POM.TAB:='OPAK_N';
POM.TYPDOK:='OPK';

POMOC.RODZ:='T';
POMOC.OPAKOW:='T';
POMOC.OPAK_N:=null;
DISP.JM:=null;

{? cur_tab()=FAKS | cur_tab()=FAP
||
   DISP.DOK_MAG:=FAKS.SYM;
   OPAK_N.index('FAKS');
   OPAK_N.prefix($FAKS.ref());
   {? OPAK_N.first()
   ||
      POMOC.OPAK_N:=OPAK_N.ref()
   ||
      _mag:=exec('f3_mg_op','opakow',0);
      {? _mag<>'' || POMOC.OPAK_N:=exec('opkn_add','opakow',_mag,'N',0,,,,FAKS.D,FAKS.KH,,$FAKS.ref()) ?}
   ?}
||
   DISP.DOK_MAG:=ND.SYM;
   OPAK_N.index('ND');
   OPAK_N.prefix($ND.ref());
   {? OPAK_N.first()
   ||
      POMOC.OPAK_N:=OPAK_N.ref()
   ||
      {? ND.MAG().MG_OPAK<>''
      || POMOC.OPAK_N:=exec('opkn_add','opakow',ND.MAG().MG_OPAK,ND.TYP().P,0,,,,ND.D,ND.KH,$ND.ref(),ND.FAKS_REF)
      || FUN.info('Dla magazynu %1 nie przypisano magazynu opakowań.'@[ND.MAG().SYM])
      ?}
   ?}
?};

{? POMOC.OPAK_N<>null
|| _wyn:=exec('opkp_add','opakow',POMOC.OPAK_N,,,,,'D')<>null();
   OPAK_P.index('OPAK_N');
   OPAK_P.prefix(POMOC.OPAK_N);
   {? ~OPAK_P.size() & OPAK_N.seek(POMOC.OPAK_N)
   ||
      numer:=OPAK_N.NR;
      oldnumer:=1;
      exec('nr_old','numery');
      POMOC.OPAK_N:=null;
      OPAK_N.del(1)
   ?}
?};

POM.TAB:=_pom_tab;
POM.TYPDOK:=_pom_typdok;
_wyn


\opkp_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: utworzenie pozycji dokumentu opakowan
::   WE:  _a - OPAK_N.ref naglowek
::       [_b] - czy wyswietlac okno redakcji (domyslnie tak=1)
::       [_c] - M.ref opakowanie
::       [_d] - ilosc
::       [_e] - kaucja
::       [_f] - miejsce wywołania: 'D'-dokumenty 'F'-faktury 'R'-realizacje lub pusty string
::   WY: _wyn - OPAK_P.ref()
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>7 || _a:=null() ?} || _a:=OPAK_N.ref() ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};
{? _>=3 || {? type_of(_c)<>7 || _c:=null ?} || _c:=null ?};
{? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};
_wyn:=null;

:: kasowanie tabeli szczegolow
{? var_pres('__opkc_tab')>100
|| __opkc_tab.prefix(); {? __opkc_tab.first() || {! |? __opkc_tab.del() !} ?}
?};

OPAK_N.cntx_psh();
{? exec('lock','opakow',_f)
||
   POMOC.RODZ:='T';
   POMOC.OPAKOW:='T';
   POMOC.OPAK_N:=_a;
   OPAK_P.index('OPAK_N');
   OPAK_P.prefix(_a);
   OPAK_P.blank();
   {? _b=1 & exec('czy_mod','opakow',POMOC.OPAK_N().AKC)
   ||
      OPAK_N.cntx_psh();
      OPAK_P.KH:=OPAK_P.OPAK_N().KH;
      OPAK_P.KH_ODB:=OPAK_N.KH_ODB;
      OPAK_N.cntx_pop();
      OPAK_P.win_edit('RED');
      {? OPAK_P.add() & OPAK_P.r_lock(1,1,1)
      ||
         _loop:=1;
         {!
         |? _loop
         |!
            _loop:=0;
            {? OPAK_P.edit("exec('chk_opkp','opakow')")
            ||
               do();
               {? OPAK_P.put() || exec('opkc_akc','opakow') ?};
               {? end()
               ||
                  _wyn:=OPAK_P.ref();
                  OPAK_P.r_unlock()
               ||
                  _loop:=1
               ?}
            ||
               OPAK_P.r_unlock();
               exec('opkp_del','opakow',0,_f)
            ?}
         !}
      ?}
   ?};
   OPAK_N.r_unlock()
?};
OPAK_N.cntx_pop();
_wyn


\chk_opkp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord po dla pozycji dokumentow opakowan
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_zwrot:=OPAK_P.ZWROT;
{? OPAK_P.M=null
||
   FUN.info('Nie wypełnione pole Indeks opakowania.'@);
   _wyn:='ZWROT'
?};
{? _wyn='' & OPAK_P.IL=0
||
   FUN.info('Ilość musi być większa od zera.'@);
   {? OPAK_P.OPAK_N().PLUS='N' & exec('be_opil','opakow')
   || _wyn:=''
   || _wyn:={? OPAK_P.OPAK_N().PLUS='N' | OPAK_P.ZWROT='T' || 'UWAGI' || 'IL' ?}
   ?}
?};
:: sprawdza czy nadal jest dostepny stan dla poszczegolnych dostaw/pozycji do zwrotu
:: tabela __opkc_tab utworzona jest dla rozchodow i dla zwrotow, kasowana jest przed dolacz, popraw
{? _wyn='' & var_pres('__opkc_tab')>100 & __opkc_tab.first() & ~(OPAK_P.OPAK_N().PLUS='T' & OPAK_P.ZWROT='N')
||
   OPAK_N.cntx_psh();
   OPAK_P.cntx_psh();
   {!
   |?
      OPAK_N.use('opakn'+((8+__opkc_tab.REF_POZ)+3));
      OPAK_N.prefix();
      OPAK_P.use(8+__opkc_tab.REF_POZ);
      OPAK_P.prefix();
      {? OPAK_P.seek(__opkc_tab.REF_POZ,8+__opkc_tab.REF_POZ)
      ||
         {? OPAK_P.OPAK_N().AKC='T'
         ||
            {? OPAK_N.r_lock(1,1,1)
            ||
               {? OPAK_P.r_lock(1,1,1)
               ||
                  __opkc_tab.IL_POZ:=__opkc_tab.CUR_IL+exec('opkc_sd','opakow');
                  {? __opkc_tab.IL>__opkc_tab.IL_POZ
                  || _wyn:='UWAGI'
                  ?};
                  OPAK_P.r_unlock()
               ||
                  {? FUN.ask({? _zwrot='T'
                             || 'Wybrane pozycje do zwrotu obsługuje inny użytkownik.'@
                             || 'Wybrane dostawy obsługuje inny użytkownik.'@
                             ?}+'\n'+'Czy chcesz zobaczyć kto?'@) & OPAK_P.r_lock(1,,1)
                  || OPAK_P.r_unlock()
                  ?};
                  _wyn:='UWAGI'
               ?};
               OPAK_N.r_unlock()
            ||
               {? FUN.ask('Dokument %1 obsługuje inny użytkownik.\nCzy chcesz zobaczyć kto?'@[OPAK_N.SYM])
                     & OPAK_N.r_lock(1,,1)
               || OPAK_N.r_unlock()
               ?};
               _wyn:='UWAGI'
            ?}
         ||
            _wyn:='UWAGI'
         ?}
      ?};
      _wyn='' & __opkc_tab.next()
   !};
   OPAK_P.cntx_pop();
   OPAK_N.cntx_pop();
   {? _wyn<>''
   ||
      FUN.info({? _zwrot='T'
               || 'Brak dostępnej ilości dla wybranych zwrotów opakowań.'@
               || 'Brak dostępnej ilości dla wybranych dostaw opakowań.'@
               ?});
      exec('be_opil','opakow')
   ?}
?};
{? _wyn='' & OPAK_P.DATA_ZWR<>date(0,0,0) & OPAK_P.DATA_ZWR<OPAK_P.OPAK_N().DATA
||
   FUN.info('Data zwrotu jest wcześniejsza od daty dokumentu.'@);
   _wyn:='DATA_ZWR'
?};
_wyn


\be_opil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja pola OPAK_P.IL - tworzenie tabeli zwrotow
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? OPAK_P.OPAK_N & OPAK_P.OPAK_N().AKC<>'T'
||
   _ref_rozl:=$OPAK_P.ref();
:: tymczasowe rozliczenie
   exec('opak_zwr_copy_to_tmp','opakow_stany',_ref_rozl);
   _plus:={? OPAK_N.PLUS='T' || 'N' || 'T' ?};
   {? OPAK_P.ZWROT='T'
   ||
      exec('opkc_mk','opakow',,OPAK_N.MAG,OPAK_N.KH().KOD,OPAK_P.M().KTM,_plus,_ref_rozl,1,OPAK_N.DATA,,OPAK_N.KH_ODB);
      OPAK_P.IL:=SUM.S;
      _wyn:=0
   |? OPAK_N.PLUS='N' & OPAK_P.ZWROT='N'
   ||
::    rozchody (nie zwrot)
      exec('opkc_mk','opakow',,OPAK_N.MAG,'',OPAK_P.M().KTM,_plus,_ref_rozl,1,OPAK_N.DATA);
      {? OPAK_P.IL<>SUM.S
      ||
         OPAK_P.IL:=SUM.S;
         exec('ae_opkc','opakow')
      ?};
      _wyn:=0
   ||
::    przychody (nie zwrot)
      _wyn:=1
   ?};
   {? OPAK_P.ZWROT='T' || OPAK_P.WAR:=SUM.WAR ?};
   win_disp()
?};
_wyn


\ae_opkc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po redakcji pola OPAK_P.KAUCJA
::----------------------------------------------------------------------------------------------------------------------
{? OPAK_P.KAUCJA>0 | OPAK_P.ZWROT='T'
|| OPAK_P.CZY_KAUC:='T';OPAK_P.WAR:=(OPAK_P.IL*OPAK_P.KAUCJA)$2
|| OPAK_P.CZY_KAUC:='N';OPAK_P.WAR:=0
?};
exec('init_var','opakow');
win_disp();
1


\opakn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Arkadiusz Wielgus [2010]
:: OPIS: POMOC.OPAK_N
::----------------------------------------------------------------------------------------------------------------------
POMOC.OPAK_N


\bd_mopak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: formula przed wyswietleniem pola D_HELP.M_OPAK
::----------------------------------------------------------------------------------------------------------------------
D_HELP.M_OPAK:={? OPAK_P.M<>null || OPAK_P.M().KTM || '' ?}


\be_mopak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja zminnej D_HELP.M_OPAK
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
POMOC.RODZ:='T';
POMOC.OPAKOW:='T';
{? OPAK_P.OPAK_N().AKC='T'
||
:: zaakceptowany
   _wyn:=0
|? OPAK_P.M<>null & (OPAK_P.OPAK_N().PLUS='N' | OPAK_P.ZWROT='T')
||
:: powiazane dostawy dla dokumentu rozchodowego lub zwrot
   _wyn:=0
||
:: init tabeli stanow __opks_tab
   exec('opks_init','opakow');
:: okienko _wer dla tabeli __opks_tab
   _wer:=__opks_tab.mk_sel('Stan opakowań'@,'P',,'_mopak_f3_');
   __opks_tab.win_fld(_wer,,'INDEKS',,,20,,,'Indeks'@);
   __opks_tab.win_fld(_wer,,'NAZWA',,,25,,,'Nazwa'@);
   __opks_tab.win_fld(_wer,,'S',,,12,3,,'Stan'@);
   {? OPAK_P.ZWROT='T'
   || __opks_tab.win_fld(_wer,,'SK',,,12,3,,'Stan kaucji'@)
   ?};
   __opks_tab.win_fld(_wer,,'JM',,,5,,,'jm'@);
   __opks_tab.win_act(_wer,0,'Kolejność');
   __opks_tab.win_act(_wer,,'Szukaj');
   __opks_tab.win_act(_wer,,'Formuła','&Wybierz'@@,,,"sel_exit",,1,,,,'W');
   __opks_tab.win_sel(_wer);
:: wygenerowanie tabeli stanow opakowan
   {? OPAK_P.ZWROT='T'
   || exec('opks_mk','opakow_stany',1,OPAK_P.OPAK_N().MAG,OPAK_N.KH,,,OPAK_N.PLUS,OPAK_P.ZWROT)
   || exec('opks_mk','opakow_stany',1,OPAK_P.OPAK_N().MAG)
   ?}
?};
_wyn


\f3_mopak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: f3 na M w pozycjach dokumentow opakowan
::   WY: wybrany KTM lub string pusty
::----------------------------------------------------------------------------------------------------------------------
{? cur_kwin()='e_patt'
|| M.win_sel('SEL');
   M.index('AROP');
   M.prefix('T','T',POMOC.OPAKOW);
   {? M.select()
   || D_HELP.M_OPAK:=M.KTM
   ?}
|? __opks_tab.select(,exec('mopak_seek','opakow'),10)
||
   M.index('AROP');
   M.prefix('T','T',POMOC.OPAKOW,__opks_tab.INDEKS,__opks_tab.INDEKS);
   {? M.first()
   ||
      D_HELP.M_OPAK:=M.KTM;
      OPAK_P.M:=M.ref()
   ?}
?};
D_HELP.M_OPAK


\ae_mopak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po edycji M na pozycji opakowan
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? D_HELP.M_OPAK=''
||
   FUN.info('Niewypełnione pole.'@)
||
   __opks_tab.cntx_psh();
   _ndx:=__opks_tab.ndx_tmp(,,'INDEKS',,);
   __opks_tab.index(_ndx);
   __opks_tab.prefix(D_HELP.M_OPAK);
   {? __opks_tab.first()
   ||
      M.index('AROP');
      M.prefix('T',POMOC.RODZ,POMOC.OPAKOW,__opks_tab.INDEKS,__opks_tab.INDEKS);
      {? M.first()
      ||
         DISP.JM:=OPAK_P.M().J;
         D_HELP.M_OPAK:=M.KTM;
         OPAK_P.M:=M.ref()
      ?};
      _wyn:=1
   ||
      FUN.info('Brak opakowania w słowniku.'@)
   ?};
   __opks_tab.ndx_drop(_ndx);
   __opks_tab.cntx_pop();
   win_disp()
?};
_wyn


\mopak_seek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: ustawia sie na indeksie D_HELP.M_OPAK
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_ndx:=__opks_tab.ndx_tmp(,,'INDEKS',,,'INDEKS',,);
__opks_tab.index(_ndx);
__opks_tab.prefix(D_HELP.M_OPAK,D_HELP.M_OPAK);
_wyn:=__opks_tab.first();
__opks_tab.ndx_drop(_ndx);
__opks_tab.clear();
_wyn


\bd_opkc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po wyswietl pola OPAK_P.KAUCJA
::----------------------------------------------------------------------------------------------------------------------
{? OPAK_N.KH_ODB | OPAK_P.ZWROT='T'
|| exec('findfnv','#color')
|| ''
?}


\be_opkc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja pola OPAK_P.KAUCJA
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_wyn:={? OPAK_N.KH_ODB | OPAK_P.ZWROT='T' || 0 || 1 ?};
_wyn


\bd_opdzw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed wyswietl pola OPAK_P.D_ZWR - planowana data zwrotu
::----------------------------------------------------------------------------------------------------------------------
{? OPAK_N.KH_ODB=null() & OPAK_P.ZWROT='N' || '' ||  exec('findfnv','#color') ?}


\be_opdzw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja pola OPAK_P.D_ZWR - planowana data zwrotu
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.KH_ODB=null() & OPAK_P.ZWROT='N'


\bd_bkc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: formula przed wyswietleniem pola POMOC.BKC - bilans wartosci kaucji
::----------------------------------------------------------------------------------------------------------------------
{? OPAK_P.CZY_KAUC='T' & OPAK_P.ZWROT='N' || '' || exec('findfnv','#color') ?}


\be_opzwr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja pola OPAK_P.ZWROT
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.KH_ODB=null() & OPAK_P.M=null


\ae_opzwr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po redakcji pola OPAK_P.ZWROT
::----------------------------------------------------------------------------------------------------------------------
{? OPAK_P.ZWROT='T'
|| OPAK_P.CZY_KAUC:='T'
|| OPAK_P.CZY_KAUC:='N'
?};
win_disp();
1


\opkw_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: edycja pozycji opakowania z okna pozycji dokumentu magazynowego
::----------------------------------------------------------------------------------------------------------------------
{? ND.TYP().P='T' & OPAK_P.ZWROT='N' & OPAK_P.CZY_MOD='N' & OPAK_P.OPAK_N().AKC<>'T'
|| _opc:=FUN.choice('Wybierz jedną z opcji:'@,,'Edycja pozycji'@,'Zwrot kaucji'@)
|| _opc:=1
?};
{? _opc=1
|| exec('opkp_edit','opakow','D')
|? _opc=2
|| _buf:=obj_new(OPAK_P.fld_num());
   exec('bufRec','#table','OPAK_P',_buf);
   exec('opkp_del','opakow',0,'D');
   exec('opak_out','opakow');
   {? ~exec('opkw_add','opakow')
   || OPAK_P.blank();
      exec('unbufRec','#table','OPAK_P',_buf);
      OPAK_N.cntx_psh();
      {? (OPAK_N.clear(); ~OPAK_N.seek(OPAK_P.OPAK_N))
      || OPAK_P.OPAK_N:=exec('opkn_add','opakow',ND.MAG().MG_OPAK,ND.TYP().P,0,,,,ND.D,ND.KH,$ND.ref(),ND.FAKS_REF)
      ?};
      OPAK_P.KH:=OPAK_P.OPAK_N().KH;
      OPAK_P.KH_ODB:=OPAK_N.KH_ODB;
      OPAK_N.cntx_pop();
      {? OPAK_P.OPAK_N<>null() & OPAK_P.add(1)
      || exec('opkc_akc','opakow')
      ?}
   ?};
   obj_del(_buf)
?}


\opkp_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: edycja pozycji dokumentu opakowan
::   WE: [_a] - miejsce wywołania: 'D'-dokumenty 'F'-faktury 'R'-realizacje lub pusty string
::   WY: _wyn - 0/1
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};
OPAK_N.cntx_psh();
{? exec('lock','opakow',_a)
||
:: kasowanie tabeli szczegolow
   {? var_pres('__opkc_tab')>100
   || __opkc_tab.prefix(); {? __opkc_tab.first() || {! |? __opkc_tab.del() !} ?}
   ?};

   OPAK_P.cntx_psh();
   {? exec('czy_roz','opakow')
   ||
      {? OPAK_P.r_lock(1,1,1)
      ||
         OPAK_P.win_edit('RED');
         _il_p:=OPAK_P.IL;
         _warp:=OPAK_P.WAR;
         _zn_p:=OPAK_P.CZY_KAUC+OPAK_P.ZWROT;
         _d_zw:=OPAK_P.DATA_ZWR;
         {? OPAK_P.edit("exec('chk_opkp','opakow')")
         ||
            {? (OPAK_P.IL<>_il_p | OPAK_P.WAR<>_warp | (OPAK_P.CZY_KAUC+OPAK_P.ZWROT)<>_zn_p)
            ||
               OPAK_P.cntx_psh();
               {? OPAK_P.OPAK_N().AKC='T' & OPAK_P.get() || exec('rebuild_poz','opakow_stany',0,1) ?};
               OPAK_P.cntx_pop();
               OPAK_P.CZY_MOD:='T';
               {? OPAK_P.put()
               ||
                  {? OPAK_P.OPAK_N().AKC='N'
                  ||
                     exec('opkc_akc','opakow')
                  ||
                     exec('rebuild_poz','opakow_stany',0,0)
                  ?}
               ?}

            |? OPAK_P.DATA_ZWR<>_d_zw
            ||
               OPAK_P.CZY_MOD:='T';
               {? OPAK_P.put() || exec('opak_zwr_del','opakow_stany',$OPAK_P.ref(),'T') ?}
            ||
               {? OPAK_P.put() || exec('opak_zwr_del','opakow_stany',$OPAK_P.ref(),'T') ?}
            ?}
         ||
            {? OPAK_P.get() || exec('opak_zwr_del','opakow_stany',$OPAK_P.ref(),'T') ?}
         ?};
         OPAK_P.r_unlock()
      ||
         {? FUN.ask('Pozycje obsługuje inny użytkownik.\nCzy chcesz zobaczyć kto?'@) & OPAK_P.r_lock(1,,1)
         || OPAK_P.r_unlock()
         ?}
      ?}
   ?};
   OPAK_P.cntx_pop();
   OPAK_N.r_unlock()
?};
OPAK_N.cntx_pop();
''


\opkw_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: usuwanie pozycji opakownia z okna pozycji dokumentu magazynowego
::       dla ostatniej pozycji usuwa naglowek
::       [_a] - miejsce wywołania: 'D'-dokumenty 'F'-faktury 'R'-realizacje lub pusty string
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};
exec('opkp_del','opakow',,_a);
exec('opak_out','opakow')


\dok_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po popraw faktury lub dok.magazynowego
::   WE: _a - $ref() dokumentu
::       _b - KH.ref()
::       _c - date()
::----------------------------------------------------------------------------------------------------------------------
{? 5+_a='nagdo'
|| OPAK_N.index('ND')
|| OPAK_N.index('FAKS')
?};
OPAK_N.prefix(_a);
{? OPAK_N.first()
|| OPAK_N.KH:=_b;
   OPAK_N.DATA:=_c;
   OPAK_N.put()
?}


\spr_zz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: sprawdza czy dla dokumentu zwrotu są pozycje opakowań
::   WY: KH.ref
::----------------------------------------------------------------------------------------------------------------------
{? ND.F='T'
|| _opak_n:=exec('FindInSet','#table','OPAK_N','ND',$ND.ref());
   _wyn:=_opak_n<>null() & exec('FindInSet','#table','OPAK_P','OPAK_N',_opak_n)<>null()

|| _wyn:=0
?};
_wyn


\opkp_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: kolorowanie pozycji dokumentow opakowan
::----------------------------------------------------------------------------------------------------------------------
_wyn:='OPK#01#0';
_nr:=0;
_nr:={? OPAK_P.OPAK_N().PLUS='T' || 1 || 3 ?};
{? OPAK_P.CZY_KAUC='T' & OPAK_P.ZWROT='N' || _nr+=1 ?};
_wyn:=_wyn+$_nr;
_wyn


\opak_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: redagowanie dokumentow opakowan
::----------------------------------------------------------------------------------------------------------------------
POMOC.OPAK_MJS:='';
POMOC.RODZ:='T';
POMOC.OPAKOW:='T';
POMOC.OPAK_N:=null;
OPAK_N.clear();
OPAK_N.win_edit('RED');
OPAK_P.clear();

VAR_DEL.delete('__mg_opak','__mg_sel','__opkn_sel','__opkp_sel');

:: drzewko magazynow
_q_mg:=$"
   select distinct
      MG.ODDZ ODDZ,
      cast (null as TREE_REF_TYPE) Tree_ref,
      MG.MG_OPAK as Opis,
      MG.MG_OPAK as Magazyn,
      ' ' as Plus
   from
      MG
   where
      MG.MG_OPAK<>''
   order by
      1,2,3
";
__mg_opak:=sql(_q_mg,ST.ODDZ);
:: okienko drzewka
_icon:="
   {? __mg_opak.TREE_REF=0
   ||
      {? __mg_opak.tr_state()=1
      || 'xwin16.png:75'
      || 'xwin16.png:74'
      ?}
   ||
      {? __mg_opak.PLUS='T'
      || 'xwin16.png:162'
      |? __mg_opak.PLUS='N'
      || 'xwin16.png:30'
      ?}
   ?}
";
_exg_fml:="
      __mg_opak.cntx_psh();
      _ref:=__mg_opak.ref();
      _mag:=__mg_opak.MAGAZYN;
      __mg_opak.prefix(__mg_opak.ODDZ,_ref);
      {? __mg_opak.first() || {!|? __mg_opak.del() !} ?};
      {? _a & __mg_opak.TREE_REF=0
      ||
         __mg_opak.ODDZ:=__mg_opak.ODDZ; __mg_opak.TREE_REF:=_ref;__mg_opak.OPIS:='Przychód';__mg_opak.MAGAZYN:=_mag;__mg_opak.PLUS:='T';
         __mg_opak.add();
         __mg_opak.ODDZ:=__mg_opak.ODDZ; __mg_opak.TREE_REF:=_ref;__mg_opak.OPIS:='Rozchód'; __mg_opak.MAGAZYN:=_mag;__mg_opak.PLUS:='N';
         __mg_opak.add()
      ?};
      __mg_opak.cntx_pop()
";
__mg_sel:=__mg_opak.mk_sel('Magazyn opakowań'@,'P',,'_opak_red_tree_',,,,1);
__mg_opak.win_fld(__mg_sel,,'OPIS',,,16,,,'Magazyn'@);
__mg_opak.win_fml(__mg_sel,,'OPIS',,'ICON_BEFORE',_icon);
__mg_opak.tr_fml(__mg_sel,_exg_fml,"{? _a || __mg_opak.TREE_REF=0 || 0 ?}","__mg_opak.TREE_REF=0");
:: dodawanie pozycji w drzewku
{? __mg_opak.first()
|| {!
   |?
      __mg_opak.cntx_psh();
      __mg_opak.clear();
      _exg_fml(1);
      __mg_opak.tr_set(1,__mg_sel,,1);
      __mg_opak.cntx_pop();
      __mg_opak.next()
   !}
?};
:: okno wertowania naglowkow
__opkn_sel:='SEL';
:: okno wertowania pozycji
__opkp_sel:='SEL_NAG';
OPAK_N.win_fml(__opkn_sel,,'SYM',,'ICON_BEFORE',exec('opak_sym_ib','opakow'));

:: okno grupowe
_grp_sel:=_a;
_far:="
   params_set(params_get());
   grp_disp(OPAK_N,__opkn_sel)
";
_fb:="
   __mg_opak.prefix(ST.ODDZ);
   _env:=params_get().env;
   {? __mg_opak.find_key(0,_env.CUR_MAG,) || __mg_opak.find_key(#__mg_opak.ref(),_env.CUR_TYP,) ?};
   win_set('cur_row_pos=%1'[$_env.DOK_OPAK_POZ],'dok_opak');
   params_set(params_get());
   {? _a || grp_disp(OPAK_N,__opkn_sel) ?}
";
_fa:="
   _env:=params_get().env;
   _env.DOK_OPAK_POZ:=win_get('cur_row_pos','dok_opak');
   _env.CUR_TYP:={? __mg_opak.TREE_REF || __mg_opak.OPIS || '' ?};
   __mg_opak.cntx_psh();
   _env.CUR_MAG:={? __mg_opak.TREE_REF & __mg_opak.seek(__mg_opak.TREE_REF) || __mg_opak.OPIS || __mg_opak.OPIS ?};
   __mg_opak.cntx_pop();
   _env.CUR_KH:=_env.CUR_KH_ODB:=null(); {? OPAK_N.get() || _env.CUR_KH:=OPAK_N.KH; _env.CUR_KH_ODB:=OPAK_N.KH_ODB ?};
   _env.CUR_M:=null(); {? OPAK_P.get() || _env.CUR_M:=OPAK_P.M ?}
";
__opak.grp_sel(_grp_sel,__mg_opak,__mg_sel,'Dokumenty opakowań zwrotnych'@,_far,,,20,_fb,_fa,,,'maximized','dok_opak');
__mg_opak.win_sopt(__mg_sel,'select_record_checkbox=0');
__opak.tab_splt(_grp_sel,,'vertical','naglowek','0, 20%');
_far:="
   params_set(params_get());
   grp_disp(OPAK_P,__opkp_sel)
";
_fb:="
   params_set(params_get());
   exec('opkn_disp','opakow',0);
   grp_disp(OPAK_P,__opkp_sel);
   {? _a || grp_disp(__mg_opak,__mg_sel) ?}
";
__opak.grp_sel(_grp_sel,OPAK_N,__opkn_sel,,_far,,,18,_fb,_fa,,,'maximized_with_title','nagl_opak');
__opak.tab_splt(_grp_sel,'naglowek','horizontal','pozycje');
_fb:="
   exec('opkp_disp','opakow')
";
__opak.grp_sel(_grp_sel,OPAK_P,__opkp_sel,,,,20,,_fb,_fa,,,'maximized_with_title','pozy_opak');

AreaTitle.setTabWin(__mg_opak,~~);
AreaTitle.setTitle();

__mg_opak.first();

POMOC.OPAKOW:='N';
''


\opkn_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: grp_disp okno naglowkow dokumentow opakowan
::   WE: _a - wraz z odświeżaniem innych okienek
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_press('_a')=type_of(1) || _a || 1 ?};
OPAK_N.get();
:: naglowki
OPAK_N.index('RMMP');
{? __mg_opak.TREE_REF=0
|| OPAK_N.prefix(ST.AR,ST.AM,__mg_opak.MAGAZYN,'')
|| OPAK_N.prefix(ST.AR,ST.AM,__mg_opak.MAGAZYN,__mg_opak.PLUS)
?};
{? _a
||
   OPAK_N.first();
   exec('opkn_act','opakow')
?};
''


\opkn_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord przed dla naglowkow dokumentow opakowan
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.get();
POMOC.OPAK_N:=OPAK_N.ref();
exec('opkn_act','opakow');
:: tu odszuka powiazanych dokumentow
ND.cntx_psh();
ND.clear();
{? OPAK_N.ND<>'' & ND.seek(OPAK_N.ND,8+OPAK_N.ND) || DISP.DOK_MAG:=ND.SYM || DISP.DOK_MAG:='' ?};
ND.cntx_pop();
FAKS.cntx_psh();
FAKS.clear();
{? OPAK_N.FAKS<>'' & FAKS.seek(OPAK_N.FAKS,8+OPAK_N.FAKS) || DISP.FAKS:=FAKS.SYM || DISP.FAKS:='' ?};
FAKS.cntx_pop();
''


\opkn_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcje dla naglowkow dokumentow opakowan
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.get();
_act1:=_act0:='';
{? POMOC.OPAK_MJS='' & __mg_opak.TREE_REF=0 || _act1+='D';_act0+='D' ?};
{? OPAK_N.AKC='T' || _act1+='PUA' || _act1+='C' ?};
{? OPAK_N.PLUS='T' || _act1+='Z' || _act1+='' ?};
{? OPAK_N.ND<>'' & OPAK_N.FAKS='' || _act1+='Z' ?};
{? OPAK_N.ND='' & OPAK_N.FAKS<>'' || _act1+='M' ?};
{? OPAK_N.ND<>'' | OPAK_N.FAKS<>'' || _act1+='PUAC' ?};
OPAK_N.actions_grayed('SEL',_act1+':'+_act0);
''


\opkp_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: grp_disp okno pozycji dokumentow opakowan
::----------------------------------------------------------------------------------------------------------------------
POMOC.OPAK_N:=null;
{? OPAK_N.size()>0 || POMOC.OPAK_N:=OPAK_N.ref() ?};
OPAK_P.index('OPAK_N');
OPAK_P.prefix(POMOC.OPAK_N);
OPAK_P.first();
exec('opkp_act','opakow');
''


\opkn_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: edycja naglowka dokumentu opakowan
::   WY: _wyn - 0/1
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.get();
OPAK_N.cntx_psh();
{? exec('lock','opakow','') & exec('czy_mod','opakow',OPAK_N.AKC)
||
   OPAK_N.win_edit('RED');
   {? OPAK_N.edit("exec('chk_opkn','opakow')")
   || OPAK_N.put()
   ?}
?};
OPAK_N.r_unlock();
OPAK_N.cntx_pop();
''


\be_op_dt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja OPAK_N.DATA
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
:: sprawdza czy ma pozycje
{? exec('czy_poz','opakow',OPAK_N.ref())
|| _wyn:=0
?};
_wyn


\be_op_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja OPAK_N.KH
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
:: powiazane z innymi dokumentami
{? OPAK_N.ND<>'' | OPAK_N.FAKS<>'' || _wyn:=0 ?};
:: sprawdza czy ma pozycje
{? _wyn=1 & exec('czy_poz','opakow',OPAK_N.ref())
|| _wyn:=0
?};
_wyn


\czy_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: sprawdza czy do naglowka dodano pozycje
::   WE: _a - OPAK_N.ref()
::   WY: 1-ma 0-brak pozycji
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
OPAK_P.cntx_psh();
OPAK_P.index('OPAK_N');
OPAK_P.prefix(_a);
_wyn:=OPAK_P.first();
OPAK_P.cntx_pop();
_wyn


\ae_op_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: po redakcji pola OPAK_N.KH
::----------------------------------------------------------------------------------------------------------------------
exec('ae_kh','kontrahent')


\opkn_nd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: lista powiazanych dok.magazynowych
::----------------------------------------------------------------------------------------------------------------------
_sel:=OPAK_P.win_sel('?');
OPAK_N.get();
OPAK_N.cntx_psh();
{? exec('lock','opakow','')
||
   {? OPAK_N.ND=''
   ||
      {? FUN.ask('Brak powiązanych dokumentów magazynowych.\nWyświetlić listę dokumentów do powiązania?'@)
      ||
         _dok_tab:=tab_tmp(1
            ,'MAGAZYN','STRING[8]','Magazyn'
            ,'DOKUMENT','STRING[20]','Dokument'
            ,'DATA','DATE','Data'
            ,'KH_SKR','STRING[10]','Kontrahent'
            ,'AKC','STRING[1]','Akc'
            ,'ZAK','STRING[1]','Zak'
            ,'ND_REF','STRING[16]','ND_REF'
            ,'FAKS_REF','STRING[16]','FAKS_REF'
         );

         _dok_sel:=_dok_tab.mk_sel('Dokumenty do powiązania'@,'P',,'__opkn_nd__');
         _dok_tab.win_fld(_dok_sel,,'MAGAZYN',,,8,,,'Magazyn'@);
         _dok_tab.win_fld(_dok_sel,,'DOKUMENT',,,16,,,'Dokument'@);
         _dok_tab.win_fld(_dok_sel,,'DATA',,,10,,,'Data'@);
         _dok_tab.win_fld(_dok_sel,,'KH_SKR',,,10,,,'Kontrahent'@);
         _dok_tab.win_fld(_dok_sel,,'AKC',,,-5,,,'Zaakceptowany'@,,,2,,"'T'","'N'");
         _dok_tab.win_fld(_dok_sel,,'ZAK',,,-5,,,'Zaksięgowany'@,,,2,,"'T'","'N'");
         _dok_tab.win_act(_dok_sel,0,'Formuła','&Wybierz'@@,,,"sel_exit",,1,,,,'W');
         _dok_tab.win_act(_dok_sel,0,'Kolejność');
         _dok_tab.win_act(_dok_sel,,'Szukaj');
         _dok_tab.win_sel(_dok_sel);

         ND.index('ZEW');
         ND.prefix(OPAK_N.AR,OPAK_N.AM,OPAK_N.KH,'T',OPAK_N.PLUS,OPAK_N.AKC);
         {? ND.first()
         ||
            {!
            |?
               _ok:=1;
               OPAK_N.cntx_psh();
               OPAK_N.index('ND');
               OPAK_N.prefix($ND.ref());
               {? OPAK_N.first() || _ok:=0 ?};
               OPAK_N.cntx_pop();

               {? _ok & ND.MAG().MG_OPAK=OPAK_N.MAG
               ||
                  _dok_tab.MAGAZYN:=ND.MAG().SYM;
                  _dok_tab.DOKUMENT:=ND.SYM;
                  _dok_tab.DATA:=ND.D;
                  _dok_tab.KH_SKR:=ND.KH().SKR;
                  _dok_tab.AKC:=ND.Z;
                  _dok_tab.ZAK:=ND.ZAK;
                  _dok_tab.ND_REF:=$ND.ref();
                  _dok_tab.FAKS_REF:=ND.FAKS_REF;
                  _dok_tab.add()
               ?};
               ND.next()
            !}
         ?};

         ND.cntx_psh();
         {? _dok_tab.select()
         ||
            ND.clear();
            {? ND.seek(_dok_tab.ND_REF,8+_dok_tab.ND_REF)
            ||
               {? ND.r_lock(1,1,1)
               ||
                  {? ND.Z='N'
                  ||
                     OPAK_N.ND:=_dok_tab.ND_REF;
                     OPAK_N.FAKS:=_dok_tab.FAKS_REF;
                     OPAK_N.put()
                  ||
                     FUN.info('Dokument został już zaakceptowany.\nPowiązanie niemożliwe.'@)
                  ?};
                  ND.r_unlock()
               ||
                  {? FUN.ask('Dokument %1 obsługuje inny użytkownik.\n'
                             'Czy chcesz zobaczyć kto?'@[ND.SYM]) & ND.r_lock(1,,1)
                  || ND.r_unlock()
                  ?}
               ?}
            ||
               FUN.info('Nie udało sie otworzyć dokumentu do powiązania.'@)
            ?}
         ?};
         ND.cntx_pop()
      ?}
   ||
      exec('disp_dk','magdok_wspolne',1,OPAK_N.ND)
   ?};
   OPAK_N.r_unlock()
?};
OPAK_N.cntx_pop();
OPAK_P.win_sel(_sel);
''


\opkn_faks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: lista powiazanych dok. sprzedazy/zakupu
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.get();
{? OPAK_N.FAKS=''
||
   _txt:={? OPAK_N.PLUS='T' || 'Brak powiązanych dokumentów zakupu'@ || 'Brak powiązanych dokumentów sprzedaży'@ ?};
   _txt+='.\nWyświetlić listę dokumentów do powiązania?'@;
   {? FUN.ask(_txt)
   ||
      _dok_tab:=tab_tmp(1
         ,'STS','STRING[8]','Stanowisko'
         ,'DOKUMENT','STRING[20]','Dokument'
         ,'DATA','DATE','Data wystawienia'
         ,'KH_SKR','STRING[10]','Kontrahent'
         ,'AKC','STRING[1]','Akc'
         ,'ZAK','STRING[1]','Zak'
         ,'ND_REF','STRING[16]','ND_REF'
         ,'FAKS_REF','STRING[16]','FAKS_REF'
      );

      _dok_sel:=_dok_tab.mk_sel('Dokumenty do powiązania'@,'P',,'__opkn_faks__');
      _dok_tab.win_fld(_dok_sel,,'STS',,,8,,,'Stanowisko'@);
      _dok_tab.win_fld(_dok_sel,,'DOKUMENT',,,16,,,'Dokument'@);
      _dok_tab.win_fld(_dok_sel,,'DATA',,,10,,,'Data wystawienia'@);
      _dok_tab.win_fld(_dok_sel,,'KH_SKR',,,10,,,'Kontrahent'@);
      _dok_tab.win_fld(_dok_sel,,'AKC',,,-5,,,'Zaakceptowany'@,,,2,,"'T'","'N'");
      _dok_tab.win_fld(_dok_sel,,'ZAK',,,-5,,,'Zaksięgowany'@,,,2,,"'T'","'N'");
      _dok_tab.win_act(_dok_sel,0,'Formuła','&Wybierz'@@,,,"sel_exit",,1,,,,'W');
      _dok_tab.win_act(_dok_sel,0,'Kolejność');
      _dok_tab.win_act(_dok_sel,,'Szukaj');
      _dok_tab.win_sel(_dok_sel);

      FAKS.index('FAK_KH');
      FAKS.prefix({? OPAK_N.PLUS='T' || 'Z' || 'S' ?},OPAK_N.KH().KOD);
      {? FAKS.first()
      ||
         {!
         |?
            _ok:=1;
            OPAK_N.cntx_psh();
            OPAK_N.index('FAKS');
            OPAK_N.prefix($FAKS.ref());
            {? OPAK_N.first() || _ok:=0 ?};
            OPAK_N.cntx_pop();

            {? _ok & OPAK_N.AKC=FAKS.AKC & FAKS.SZ='S' & 'PG'*FAKS.WHERE>0 & FAKS.T().KOR='N'
            ||
               _dok_tab.STS:=FAKS.STS().KOD;
               _dok_tab.DOKUMENT:=FAKS.SYM;
               _dok_tab.DATA:=FAKS.DW;
               _dok_tab.KH_SKR:=FAKS.KH().SKR;
               _dok_tab.AKC:=FAKS.AKC;
               _dok_tab.ZAK:=FAKS.ZAK;
               _dok_tab.FAKS_REF:=$FAKS.ref();
               _dok_tab.ND_REF:=$FAKS.ref();
               _dok_tab.add()
            ?};
            FAKS.next()
         !}
      ?};

      FAKS.cntx_psh();
      {? _dok_tab.select()
      ||
         {? FAKS.seek(_dok_tab.FAKS_REF,8+_dok_tab.FAKS_REF)
         ||
            {? _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref())
            ||
               {? FAKS.AKC='N'
               ||
                  OPAK_N.FAKS:=_dok_tab.FAKS_REF;
                  OPAK_N.put()
               ||
                  FUN.info('Dokument został już zaakceptowany.\nPowiązanie niemożliwe.'@)
               ?};
               exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
            ||
               exec('who_rlock_faks','faktury_nag')
            ?}
         ||
            FUN.info('Nie udało sie otworzyć dokumentu do powiązania.'@)
         ?}
      ?};
      FAKS.cntx_pop()
   ?}
||
   FAKS.cntx_psh();
   DK.cntx_psh();
   _rfk:=OPAK_N.FAKS;
   _nfk:=8+OPAK_N.FAKS;
   _mask:=0;
   {? FAKS.name<>_nfk
   || _mask:=1;exec('open','open_tab',5-_nfk-2,6-_nfk)
   ?};
   FAKS.clear();
   {? FAKS.seek(_rfk,_nfk)
   ||
      BEER.SZ:=FAKS.SZ;
      exec('ustaw_ww','eurusd','F'); exec('ust_lw','eurusd',1);
      FAP.index('FAP');
      FAP.prefix(FAKS.ref);
      _edit:={? FAKS.KOR<>'' ||'REDK' ||'RED' ?};
      FAP.win_edit('W'+_edit);
      FAP.win_sel('WER_DOK');
      _lock:=POMOC.LOCK;
      POMOC.LOCK:='T';
      FAP.select();
      POMOC.LOCK:=_lock
   ?};
   {? _mask || exec('open','open_tab',ST.ODDZ,2-$(ST.AR)) ?};
   DK.cntx_pop();
   FAKS.cntx_pop()
?}


\opnf_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: usuwa $FAKS.ref() do powiazanego dok. opakowan
::   WE: _a - $ND.ref() - powiazany dokument
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.index('ND');
OPAK_N.prefix(_a);
{? OPAK_N.first() || OPAK_N.FAKS:=''; OPAK_N.put() ?}


\faks_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akceptacja/wycofanie powiazanego dokumentu sprzedazy/zakupu
::       FAKS.ref() w buforze
::   WE: _a - 1-akceptacja   0-wycofanie
::   WY: domyslnie=1 tzn nie ma powiazanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
OPAK_N.index('FAKS');
OPAK_N.prefix($FAKS.ref());
{? OPAK_N.first() & OPAK_N.ND=''
|| _wyn:={? _a=1 || exec('opkn_akc','opakow',0,'F') || exec('opkn_wyc','opakow',0,'F') ?}
|| _wyn:=0
?};
{? ~_wyn
||
   FAP.cntx_psh();
   FAP.index('FAP');
   FAP.prefix(FAKS.ref());
   {? FAP.first()
   || {!
      |?
         exec('rebuild_fap','opakow_stany',0,$FAP.ref());
         FAP.next()
      !}
   ?};
   FAP.cntx_pop()
?};
_wyn


\opak_ppt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: lista przeterminowanych pozycji do zwrotu
::   WE: [_a] - typ dokumentu sprzedaży, domyślnie brak = null()
::----------------------------------------------------------------------------------------------------------------------
_typysp:={? var_pres('_a')=type_of(null()) || _a || null() ?};

BEER.WYSTFAKS:=null;
FAKS.cntx_psh();
POMOC.LOCK:='T';
_end:=date(ST.AR,ST.AM,0);
{? exec('czy_sts','ustawienia')
||
   VAR_DEL.delete('__przet','__przet_sel','__typ_sp','__kh_sel','__zmcn','__zmcw');
   __typ_sp:=_typysp;
   __kh_kod:='';
   _qq:=$"
      select
         KH.KOD as KH_KOD,
         KH.SKR as KH_SKR,
         OPAK_N.MAG as MAG,
         OPAK_N.SYM as Dokument,
         OPAK_P.POZ as Pozycja,
         M.KTM as Indeks,
         M.N as Nazwa,
         OPAK_P.KAUCJA as Kaucja,
         OPAK_P.KAUCJA as Kauwal,
         OPAK_P.IL as IL_POZ,
         OPAK_P.WAR as WAR_POZ,
         to_string(OPAK_P.DATA_ZWR) as Data_ZWR,
         OPAK_P.IL as DO_FAKT,
         OPAK_P.reference as OPKP_REF,
         space(5) as VAT,
         space(16) as REFVAT,
         M.DOKL as DOKL_M,
         M.DOKL_S as DOKL_S,
         'x' as X
      from
         @OPAK_P
         join M using (OPAK_P.M,M.reference)
         join @OPAK_N using (OPAK_P.OPAK_N,OPAK_N.reference)
         join KH using (OPAK_N.KH,KH.reference)
      where
         OPAK_N.PLUS='N'
         and OPAK_N.AKC='T'
         and (OPAK_P.DATA_ZWR between to_date('2000-1-1') and to_date(:_a))
         and (':_b'='W' or (':_b'='T' and OPAK_P.CZY_KAUC='T'))
         and OPAK_N.DATA<=to_date(:_c)
      order by
         1,2,4,5,6
   ";

   DISP.win_edit('OPAK');
   DISP.TERM_OPK:=date();
   DISP.KAUCJA:='T';
   {? DISP.edit()
   ||
      __przet:=sql(_qq,DISP.TERM_OPK,~-DISP.KAUCJA,_end);
      {? __przet.first()
      ||
         {!
         |?
            __przet.IL_POZ:=__przet.IL_POZ-exec('zwr_poz','opakow',__przet.OPKP_REF);
            {? __przet.IL_POZ>0
            ||
               __przet.DO_FAKT:=0;
               __przet.WAR_POZ:=0;
               __przet.KAUWAL:=0;
               _mat:=exec('FindInSet','#table','M','MATKTM',__przet.INDEKS,__przet.INDEKS);
               _kh:=exec('FindInSet','#table','KH','KOD',__przet.KH_KOD,2,1);
               _sv:=exec('m_vat','material',_mat,_kh);
               __przet.REFVAT:=$_sv;
               __przet.VAT:={? __przet.REFVAT<>''
                            || exec('FindAndGet','#table',SLO,__przet.REFVAT,,"KOD",'')
                            || ''
                            ?};
               __przet.put();
               __przet.next()
            ||
               __przet.del()
            ?}
         !}
      ?};
      {? __przet.first()
      ||

::       grupowanie tabeli przeterminowanych wg KH
         _qq:=$"
            select distinct
               :_a.KH_KOD as KH_KOD,
               :_a.KH_SKR as KH_SKR
            from
               :_a
            order by 1
         ";
         _kh_przet:=sql(_qq,__przet);
         _file:='(';
         {? _kh_przet.first()
         || {!
            |? _file+='\''+_kh_przet.KH_KOD+'\',';
               _kh_przet.next()
            !}
         ?};
         _file:=(_file-1)+')';
         KH.cntx_psh();
         KH.prefix();
         KH.f_clear();
         KH.f_set('KOD,SKR',,'KH.P=2 and KH.KOD in '+_file);

         ZAKR.ACCESS:='N';
         __kh_sel:=KH.mk_sel('Kontrahenci do zafakturowania'@,'P',,'_kh_przet_sel_',,,,,'U','T');
         KH.win_fld(__kh_sel,ZAKR,'ACCESS',,,-11,,,'Do rozliczenia'@,,,2,,"'T'","'N'");
         KH.win_fld(__kh_sel,,'KOD',,,8,,1,'Kod'@);
         KH.win_fld(__kh_sel,,'SKR',,,6,,1,'Skrót'@);
         KH.win_fld(__kh_sel,,'NAZ',,,8,,1,'Nazwa'@);
         _formula:="params_exec('ppt_fakt','opakow')";
         KH.win_act(__kh_sel,,'Formuła','&Fakturuj'@@,,,_formula,,1,,,,'F');
         _formulb:="params_exec('wal_fakt','opakow')";
         KH.win_act(__kh_sel,,'Formuła','&Zmień walutę'@@,,,_formulb,,,,,,'Z');
         KH.win_btn(__kh_sel,'text=%1,panel=right,align=begin'['Fakturuj'@],'menu:F',,,,,,'noempty');
         KH.win_act(__kh_sel,,'Rekord',,,,"exec('rekAllKh','opakow')");
         KH.win_act(__kh_sel,,'Kolejność');
         KH.win_act(__kh_sel,,'Szukaj');

         __przet_sel:=__przet.mk_sel('Przeterminowane pozycje do zwrotu'@,'P',,'_przet_sel_',,,,,'U');
         __przet.win_fld(__przet_sel,,'MAG',,,8,,1,'Magazyn'@);
         __przet.win_fld(__przet_sel,,'DOKUMENT',,,15,,1,'Dokument'@);
         __przet.win_fld(__przet_sel,,'POZYCJA',,,7,,1,'Pozycja'@);
         __przet.win_fld(__przet_sel,,'INDEKS',,,6,,1,'Indeks'@);
         __przet.win_fld(__przet_sel,,'NAZWA',,,10,,1,'Nazwa'@);
         __przet.win_fld(__przet_sel,,'DATA_ZWR',,,10,,1,'Do zwrotu'@);
         __przet.win_fld(__przet_sel,,'KAUCJA',,,9,ST.DOKL_S,,'Cena brutto'@);
         __przet.win_fld(__przet_sel,,'KAUWAL',,,9,ST.DOKL_S,,'Cena walutowa'@);
         __przet.win_fld(__przet_sel,FAKSZ,'SV','KOD','SL',5,,,'%% VAT'@);
         __przet.win_fld(__przet_sel,,'IL_POZ',,,8,3,ST.DOKL,'Ilość'@);
         __przet.win_fld(__przet_sel,,'DO_FAKT',,,10,ST.DOKL,,'Na fakturę'@);
         __przet.win_fld(__przet_sel,,'WAR_POZ',,,9,2,1,'Wartość'@);
         __przet.win_act(__przet_sel,,'Formuła','&Wybierz'@@,,,"exec('ppt_mark','opakow',1)"
          ,,,1,"exec('ppt_mark','opakow',1)",,'W');
         __przet.win_act(__przet_sel,,'Formuła','&Odbierz'@@,,,"exec('ppt_mark','opakow',0)"
          ,,,1,"exec('ppt_mark','opakow',0)",,'O');
         __przet.win_act(__przet_sel,,'Popraw',,,,,"grp_disp(KH,__kh_sel)",1);
         __przet.win_act(__przet_sel,,'Kolejność');
         __przet.win_act(__przet_sel,,'Szukaj');
         __przet.win_act(__przet_sel,,'Rekord',,,,"exec('ppt_rek','opakow')","exec('ppt_aepp','opakow')");
         __przet.win_fml(__przet_sel,,'DO_FAKT',,'ICON_BEFORE',exec('bedispre','opakow'));
         __przet.win_sel(__przet_sel);

         _win_grp:=KH.grp_make('Pobrane kaucje zwrotne'@,"''",'przet_kh_grp');
         KH.grp_sel(_win_grp,KH,__kh_sel,,"exec('dispAllKh','opakow')",,,10,"''",,,,'maximized');
         KH.grp_splt(_win_grp,,'vertical','zwroty',',33%');
         KH.grp_sel(_win_grp,__przet,__przet_sel,,"''",,,20,"''",,,,'maximized');
         KH.grp_splt(_win_grp,'zwroty','horizontal','waluty');
         KH.grp_edit(_win_grp,FAKSZ,'DOPK',,"~~",,,,'maximized','faksopk_wal');
         KH.win_sel(_win_grp);
         KH.f_first();

         __przet.fld_fml('KAUCJA','BEFORE_EDIT',"__zmcn:=__przet.KAUCJA; 1");
         __przet.fld_fml('KAUCJA','AFTER_EDIT',
            "  {? FAKSZ.WAL<>FAKSZ.NWAL & FAKSZ.KRS<>0 & __zmcn<>__przet.KAUCJA
               || __przet.KAUWAL:=__przet.KAUCJA/FAKSZ.KRS $__przet.DOKL_S
               ?};
               1
            "
         );
         __przet.fld_fml('KAUWAL','BEFORE_EDIT',"__zmcw:=__przet.KAUWAL; FAKSZ.WAL<>FAKSZ.NWAL");
         __przet.fld_fml('KAUWAL','AFTER_EDIT',
            "  {? __przet.KAUWAL<=0
               || FUN.info('Należy podać cenę w walucie większą od zera.'@);
                  0
               || {? __zmcw<>__przet.KAUWAL
                  || __przet.KAUCJA:=__przet.KAUWAL*FAKSZ.KRS $__przet.DOKL_S
                  ?};
                  1
               ?}
            "
         );
         FAKSZ.blank();
         FAKSZ.WAL:=exec('bl_wal','ustawienia');
         FAKSZ.NWAL:=exec('bl_nwal','ustawienia');
         exec('set_efld_opt_opk','faktury_nag','DOPK');
         _context:=params_get();
         _mp:=_context.mp;
         params_set('out',_context.out,'mp',_mp,'in',_context.in,'akcja',_mp.akcja());
         KH.select();
         exec('ppt_unlock','opakow');
         KH.f_clear();
         KH.cntx_pop()
      ||
         FUN.info('Brak przeterminowanych pozycji do zwrotu.'@)
      ?}
   ?}
?};
POMOC.LOCK:='';
VAR_DEL.delete('__przet','__przet_sel','__typ_sp','__kh_sel','__zmcn','__zmcw');
FAKS.cntx_pop();
{? (8+$BEER.WYSTFAKS)=FAKS.name() || exec('ustaw_fk','faktury_nag',BEER.WYSTFAKS) ?};
''


\ppt_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: szuka zaznaczonego kontrahenta
::   WY: KH.KOD zaznaczonego kontrahenta
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
__przet.cntx_psh();
_ndx:=__przet.ndx_tmp(,,'X',,);
__przet.index(_ndx);
__przet.prefix('*');
{? __przet.first()
|| _wyn:=__przet.KH_KOD
?};
__przet.ndx_drop(_ndx);
__przet.cntx_pop();
_wyn


\ppt_mark
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: zaznaczanie rekordu - pozycja przeterminowana
::   WE: _a - 1-wybierz 0-odbierz
::----------------------------------------------------------------------------------------------------------------------
_mark:={? _a=1
       || "{? __przet.DO_FAKT=0
           || {? exec('ppt_lock','opakow',__przet.OPKP_REF)
              || __przet.DO_FAKT:=__przet.IL_POZ;__przet.X:='*'
              || __przet.DO_FAKT:=0;__przet.X:='x'
              ?}
           ?};
           __przet.WAR_POZ:=(__przet.KAUCJA*__przet.DO_FAKT)$2;
           __przet.put()"
       || "{? __przet.DO_FAKT>0
           || __przet.DO_FAKT:=0;__przet.X:='x'
           ?};
           __przet.WAR_POZ:=(__przet.KAUCJA*__przet.DO_FAKT)$2;
           __przet.put()"
       ?};
:: szuka zaznaczonego kontrahenta
_kh_kod:=exec('ppt_kh','opakow');
:: zaznacza jesli zgodny kontrahent
{? _kh_kod<>'' & __przet.KH_KOD<>_kh_kod
||
   FUN.info('Wybierz i zaznacz pozycje tylko dla kontrahenta z kodem: %1.'@[_kh_kod])
|| _tab:=__przet.sel_aget();
   __przet.sel_adel();
   {? _tab.first()
   || __przet.cntx_psh();
      {!
      |? {? (__przet.clear(); __przet.seek(_tab.REF,)) || _mark() ?};
         _tab.next()
      !};
      __przet.cntx_pop();
      obj_del(_tab)
   || _mark()
   ?};
   grp_disp(KH,__kh_sel)
?};
''


\ppt_lock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: r_lock dla zaznaczonej pozycji OPAK_P
::   WE: _a - $OPAK_P.ref
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
OPAK_P.cntx_psh();
OPAK_P.use(8+_a);
OPAK_P.clear();
{? OPAK_P.seek(_a,8+_a)
||
   {? OPAK_P.r_lock(1,1,1)
   ||
      _wyn:=1
   ||
      {? FUN.ask('Pozycje obsługuje inny użytkownik.\nCzy chcesz zobaczyć kto?'@) & OPAK_P.r_lock(1,,1)
      || OPAK_P.r_unlock()
      ?}
   ?}
?};
OPAK_P.cntx_pop();
_wyn


\ppt_unlock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: r_unlock dla wskazanej pozycji OPAK_P
::   WE: _a - $OPAK_P.ref
::----------------------------------------------------------------------------------------------------------------------
OPAK_P.cntx_psh();
{? __przet.first()
||
   {!
   |?
      {? __przet.OPKP_REF<>''
      ||
         OPAK_P.use(8+__przet.OPKP_REF);
         OPAK_P.clear();
         {? OPAK_P.seek(__przet.OPKP_REF,8+__przet.OPKP_REF) || OPAK_P.r_unlock() ?}
      ?};
      __przet.next()
   !}
?};
OPAK_P.cntx_pop()


\ppt_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed rekord pozycji przeterminowanej
::----------------------------------------------------------------------------------------------------------------------
_kh_kod:=exec('ppt_kh','opakow');
{? _kh_kod<>'' & __przet.KH_KOD<>_kh_kod
|| __przet.actions(__przet_sel,'ZP',,1)
|| __przet.actions(__przet_sel,'',,1)
?};
__przet.X='*'


\ppt_aepp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po popraw pozycje przeterminowana
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
M.index('MATKTM');
M.prefix(__przet.INDEKS,__przet.INDEKS);
{? M.first()
|| _dokl:=exec('jaka_dok_m','jm',M.ref())
|| _dokl:=0
?};
__przet.DO_FAKT:=__przet.DO_FAKT$_dokl;
{? __przet.DO_FAKT<0
|| FUN.info('Należy podać ilość większą od zera.'@)
|? __przet.DO_FAKT>__przet.IL_POZ
|| FUN.info('Należy podać ilość mniejszą lub równą %1.'@[form(__przet.IL_POZ,,3)])
|| _wyn:=1
?};
{? _wyn=0 || __przet.DO_FAKT:=__przet.IL_POZ ?};
{? __przet.DO_FAKT>0
||
   {? exec('ppt_lock','opakow',__przet.OPKP_REF)
   || __przet.X:='*'
   || __przet.DO_FAKT:=0;__przet.X:='x'
   ?}
|| __przet.X:='x'
?};
__przet.WAR_POZ:=(__przet.KAUCJA*__przet.DO_FAKT)$2;
__przet.put();
_wyn


\ppt_fakt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: faktura do zaznaczonych przeterminowanych pozycji do zwrotu
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_wyn:=0;

{? exec('check_wal','opakow')<>'' || return(_wyn) ?};

__przet.cntx_psh();
_ndx:=__przet.ndx_tmp(,,'X',,);
__przet.index(_ndx);
__przet.prefix('*');
{? __przet.first()
||
   {!
   |?
      {? __przet.KAUCJA=0
      ||
         FUN.info('Podaj cenę dla pozycji zaznaczonych do fakturowania.'@);
         _ok:=0

      |? __przet.VAT=''
      ||
         FUN.info('Podaj stawkę vat dla pozycji zaznaczonych do fakturowania.'@);
         _ok:=0
      ?};
      _ok=1 & __przet.next()
   !}
||
   FUN.info('Nie zaznaczono żadnych pozycji do fakturowania\nWystawienie dokumentu sprzedaży niemożliwe.'@)
?};
{? _ok=1 & __przet.first()
||
   FAKS.cntx_psh();
   {? params_exec('ppt_begr','opakow')
   ||
      params_exec('ppt_aegr','opakow');
      _wyn:=BEER.WYSTFAKS<>null()
   ?};
   FAKS.cntx_pop()
?};
__przet.ndx_drop(_ndx);
__przet.cntx_pop();
_wyn


\ppt_begr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed akcja - fakturowanie przeterminowanych
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
BEER.SZ:='S';
{? FUN.ask('Czy dokument powiązany z magazynem (rezerwujący indeksy materiałowe)?'@)
||
   HELP.WHERE:='G';
   HELP.WMAG:=1
||
   HELP.WHERE:='P';
   HELP.WMAG:=0
?};
exec('ustaw_ww','eurusd',{? BEER.SZ='S' || 'F' || 'K' ?}); exec('ust_lw','eurusd',0);
{? exec('czy_z_ok','okresy',-2)
||
   _typ:={? __typ_sp<>null() || __typ_sp || exec('tsp_fak','typysp',HELP.WHERE) ?};
   {? _typ<>null
   || POM.TYPDOK:=TYPYSP.KOD;
      POM.NRT:=TYPYSP.NRT;
      FAKS.memo_set(,'UWAGI');
      KH.index('KOD');
      KH.prefix(2,__przet.KH_KOD);
      {? KH.first()
      ||
         {? HELP.WHERE='G'
         || TYPYDOK.f_clear();
            TYPYDOK.index('TYP');
            TYPYDOK.prefix();
            TYPYDOK.f_set('T',,'"TYPYDOK".Z=\'T\' and "TYPYDOK".P=\'N\' and "TYPYDOK".DS=\'T\' '+
             'and "TYPYDOK".DK=\'N\' ');
            TYPYDOK.win_dict('WYST')
         ?};
         {? exec('add','faktury_nag',TYPYSP.ref,,,KH.ref(),,,,,,,,'K')
          & (_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref()))
         || FAKS.OPAK:='T'; FAKS.put(1);
::          przepisanie pozycji
            {!
            |?
               M.index('MATKTM');
               M.prefix(__przet.INDEKS,__przet.INDEKS);
               {? M.first()
               ||
                  M.VAT:=exec('m_vat','material',M.ref,FAKS.KH,,FAKS.AR,FAKS.AM);
                  {? FAKS.T().FIS='T'
                  ||
::                   liczone od brutto
                     _cpr:=__przet.KAUCJA;
                     _cwl:={? FAKSZ.WAL<>FAKSZ.NWAL & FAKSZ.KRS>0 || __przet.KAUWAL || 0 ?}
                  ||
::                   liczone od netto
                     _cpr:=(__przet.KAUCJA/(1+(#((M.VAT().KOD*'%')+M.VAT().KOD-1)/100)))$__przet.DOKL_S;
                     _cwl:={? FAKSZ.WAL<>FAKSZ.NWAL & FAKSZ.KRS>0 || _cpr/FAKSZ.KRS $__przet.DOKL_S || 0 ?}
                  ?};
                  _vat:={? FAKSZ.VATZERO
                        || exec('m_vat','material',M.ref,FAKS.KH,exec('exp_ue','typysp'),FAKS.AR,FAKS.AM)
                        || exec('FindAndGet','#table',SLO,__przet.REFVAT,,,null())
                        ?};
                  {? exec('ADDPOZF','faktury_poz',M.ref,__przet.DO_FAKT,_cpr,0,_vat,,,_cwl,FAKS.KRS,FAKS.WAL,M.J)
                  || _ok:=1;
                     {? FAP.FAKS().WHERE='G' & FAP.M<>null & FAP.M().RODZ='T'
                     ||
                        FAP.IL:=0;
                        FAP.J2:=FAP.JM;
                        _tab:=exec('f3_il','faktury_poz',__przet.DO_FAKT,,-1);
                        {? _tab[1]=__przet.DO_FAKT
                        ||
                           FAP.IL2:=_tab[1];
                           exec('ae_il','faktury_poz',0)
                        ||
                           FUN.info('Indeks: %1\nIlość: %2 %3'
                              '\n\nNie rozpisano ilości do wydania.'
                              '\nPozycja nie zostanie dodana.'@[FAP.M().KTM,form(__przet.DO_FAKT,,3),FAP.JM().KOD]);
                           _ok:=0
                        ?};
                        obj_del(_tab)
                     ?};
                     {? _ok=1
                     ||
                        FAP.OPAK_GEN:='X';
                        FAP.put();
                        exec('opak_zwr_update','opakow_stany',$FAP.ref(),__przet.OPKP_REF,'',__przet.DO_FAKT,'T','N')
                     ||
                        FAP.del()
                     ?}
                  ?}
               ?};
               __przet.next()
            !};

::          redakcja nagłówka faktury
            _var_fakpop:=exec('var_fakpop','faktury_nag');
            _var_fakpop.RAB:=FAKS.RAB;
            _var_fakpop.KRS:=FAKS.KRS;
            _var_fakpop.NKRS:=FAKS.NKRS;
            _var_fakpop.R_LOCK:=_r_lock;
            _var_fakpop.PO_FIRST:=1;
            params_set('context',params_get(),'var_fakpop',_var_fakpop);

            _faks_red:=exec('faks_win_edit_set','faktury_nag');
            _wyn:=_dod_nag:=exec('put','faktury_nag',_faks_red);
            {? FAKS.STAT_REJ='N'
            || _dod_nag:=exec('fak_pop_po','faktury_nag',{? _dod_nag || 1 || -1 ?},_var_fakpop.PO_FIRST<>1)
            ?};
            exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
         ?};
         TYPYDOK.f_clear()
      ?};
      KH.prefix()
   ?}
?};
_wyn


\ppt_aegr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po akcji - fakturowanie pozycji przeterminowanych
::----------------------------------------------------------------------------------------------------------------------
exec('sumfak','faktury_wspolne');
:: aktualizacja wartosci platnosci - jeszcze przed drukowaniem
{? FAKS.AKC<>'T' || exec('plat_przel','faktury_plat',FAKS.ref()) ?};
FAKS.r_unlock();
_ref_faks:=FAKS.ref();
exec('ppt_unlock','opakow');
sel_exit();
:: czy sa pozycje
FAP.cntx_psh();
FAP.index('FAP');
FAP.prefix(_ref_faks);
_poz:=FAP.first();
FAP.cntx_pop();
{? _poz=0 & FUN.ask('Utworzony dokument nie posiada pozycji.\nUsunąć dokument sprzedaży?'@)
||
   exec('usun_fak','faktury_nag',0);
   _ref_faks:=null
?};
BEER.WYSTFAKS:=_ref_faks


\rekAllKh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: rekord dla kontrahenta opakowań zbiorczych
::----------------------------------------------------------------------------------------------------------------------
__przet.cntx_psh();
__przet.clear();
__przet.prefix(KH.KOD,);
ZAKR.ACCESS:={? __przet.find_tab('first','X',,'=','*') || 'T' || 'N' ?};
__przet.cntx_pop();
~~


\dispAllKh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: wyświetla pozycje opakowań zbiorczych dla danego kontrahenta
::----------------------------------------------------------------------------------------------------------------------
__przet.clear();
__przet.prefix(KH.KOD,);
__przet.first();
grp_disp(__przet,__przet_sel);
grp_edisp(FAKSZ,'DOPK');
~~


\zwr_mago
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: zwraca magazyn opakowan na podstawie
::   WE: _a - refsql pozycji rozliczajacej $FAP.ref
::   WY: zwraca symbol magazynu opakowan
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_ref_op:='';

OPAK_ZWR.cntx_psh();
:: po wszystkich okresach
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |?
      OPAK_ZWR.use('opakz'+((8+_a)+3));
      OPAK_ZWR.index('REF_ROZL');
      OPAK_ZWR.prefix(_a);
      {? OPAK_ZWR.first()
      || _ref_op:=OPAK_ZWR.REF_POZ
      ?};
      _ref_op='' & OKR.next()
   !}
?};
OKR.cntx_pop();
OPAK_ZWR.cntx_pop();

{? _ref_op=''
|| OPAK_P.index('DK');
   OPAK_P.prefix(_a,'T');
   {? OPAK_P.first()
   || {!
      |? {? _wyn='' || _wyn:=OPAK_P.OPAK_N().MAG ?};
         _wyn='' & OPAK_P.next()
      !}
   ?};
   {? _wyn=''
   || OPAK_P.prefix(_a,'N');
      {? OPAK_P.first()
      || {!
         |? {? _wyn='' || _wyn:=OPAK_P.OPAK_N().MAG ?};
            _wyn='' & OPAK_P.next()
         !}
      ?}
   ?}
|| OPAK_P.clear;
   {? OPAK_P.seek(_ref_op,form(8+_ref_op))
   || _wyn:=OPAK_P.OPAK_N().MAG
   ?}
?};
_wyn


\opak_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: rekordy tabeli tymczasowej - pozycje faktury zakupowej i powiazane pozycje opakowan
::----------------------------------------------------------------------------------------------------------------------
__opkz_tab.erase();
{? __zakinf.first()
||
   OPAK_P.cntx_psh();
   OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   {? OKR.last()
   ||
      {!
      |?
         OPAK_P.use('opakp'+ST.ODDZ+($OKR.ROK+2));
         OPAK_P.index('DK');
         {!
         |?
            OPAK_P.prefix(__zakinf.DK_REF);
            {? OPAK_P.first()
            ||
               {!
               |?
                  __opkz_tab.blank();
                  __opkz_tab.ZWROT:=OPAK_P.ZWROT;
                  __opkz_tab.POZ:=OPAK_P.POZ;
                  __opkz_tab.INDEKS:=OPAK_P.M().KTM;
                  __opkz_tab.NAZWA:=OPAK_P.M().N;
                  __opkz_tab.IL:=OPAK_P.IL;
                  __opkz_tab.CZY_KAUC:=OPAK_P.CZY_KAUC;
                  __opkz_tab.OPKP_REF:=$OPAK_P.ref();
                  __opkz_tab.add();
                  OPAK_P.next()
               !}
            ?};
            __zakinf.next()
         !};
         OKR.prev()
      !}
   ?};
   OKR.cntx_pop();
   OPAK_P.cntx_pop()
?};
__opkz_tab.first();
grp_disp(__opkz_tab,__opkz_sel);
''


\opkz_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: utworzenie tabeli __opkz_tab i okna __opkz_sel
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__opkz_tab')>100
||
   __opkz_tab.prefix(); {? __opkz_tab.first() || {! |? __opkz_tab.del() !} ?}
||
   VAR_DEL.delete('__opkz_tab');
   __opkz_tab:=tab_tmp(1
      ,'ZWROT','STRING[1]','Zwrot'
      ,'POZ','INTEGER','Poz.'
      ,'INDEKS','STRING[50]','Indeks'
      ,'NAZWA','STRING[100]','Nazwa'
      ,'IL','REAL','Ilość'
      ,'CZY_KAUC','STRING[1]','Kaucja'
      ,'OPKP_REF','STRING[16]','OPAK_P_REF'
   )
?};
:: okno wertowania pozycji
_disp_fml:="
   OPAK_P.clear();
   {? OPAK_P.seek(__opkz_tab.OPKP_REF,form(8+__opkz_tab.OPKP_REF))
   ||
      OPAK_P.win_edit('RED');
      OPAK_P.display()
   ?}
";
{? var_pres('__opkz_sel')<=0
||
   __opkz_sel:=__opkz_tab.mk_sel('Opakowania powiązane z pozycją magazynową'@,'N',,'_opak_zak_poz_');
   __opkz_tab.win_fld(__opkz_sel,,'ZWROT',,,3,,,'Zwrot'@,,,2,,"'T'","'N'");
   __opkz_tab.win_fld(__opkz_sel,,'POZ',,,4,,,'Pozycja'@);
   __opkz_tab.win_fld(__opkz_sel,,'INDEKS',,,30,,,'Indeks'@);
   __opkz_tab.win_fld(__opkz_sel,,'NAZWA',,,28,,,'Nazwa'@);
   __opkz_tab.win_fld(__opkz_sel,,'IL',,,15,3,,'Ilość'@);
   __opkz_tab.win_fld(__opkz_sel,,'CZY_KAUC',,,3,,,'Kaucja'@,,,2,,"'T'","'N'");
   __opkz_tab.win_act(__opkz_sel,,'Wyświetl',,,,_disp_fml);
   __opkz_tab.win_act(__opkz_sel,,'Szukaj')
?};
__opkz_tab.win_sel(__opkz_sel);
''


\prnDokOpak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: wydruk dokumentu
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','LMG_OPK_ZWYD','lmg_doko_*','Wydruki dokumentów opakowań',2,,,,,,'W')


\opak_sym_ib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła ikony dla OPAK_N
::----------------------------------------------------------------------------------------------------------------------
"  _stat_rej:={? OPAK_N.ND<>'' || exec('FindAndGet','#table',ND,OPAK_N.ND,,\"STAT_REJ\",'N')
              |? OPAK_N.FAKS<>'' || exec('FindAndGet','#table',FAKS,OPAK_N.FAKS,,\"STAT_REJ\",'N')
              || 'O'
              ?};
   {? _stat_rej='T'
    | (_stat_rej='O' & OPAK_N.AKC='T')  || exec('zaakceptowany','icon')
   |? _stat_rej='Z'
    | (_stat_rej='O' & exec('FindInSet','#table','OPAK_P','OPAK_N',OPAK_N.ref)<>null()) || exec('zarejestrowany','icon')
   || exec('pusta','#icon')
   ?}
"


\opkn_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: kolorowanie nagłówków dokumentow opakowan
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_wyn


\wal_fakt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Zmiana waluty dla dokumentu sprzedaży fakturującego opakowania zbiorcze
::----------------------------------------------------------------------------------------------------------------------
FAKSZ.win_edit('WOPK');
_krs:=FAKS.KRS;
_lock:=POMOC.LOCK;
_vatzero:=FAKSZ.VATZERO;
POMOC.LOCK:='O';
exec('set_efld_opt_opk','faktury_nag','WOPK');
{? FAKSZ.edit("exec('check_wal','opakow')")
|| __przet.cntx_psh();
   __przet.prefix();
   __przet.first();
   {? FAKSZ.WAL<>FAKSZ.NWAL
   || {!
      |?
         {? FAKSZ.KRS & FAKSZ.KRS<>_krs
         ||
            __przet.KAUWAL:=__przet.KAUCJA/FAKSZ.KRS $__przet.DOKL_S
         ||
            __przet.KAUWAL:=0
         ?};
         _mat:=exec('FindInSet','#table','M','MATKTM',__przet.INDEKS,__przet.INDEKS);
         _kh:=exec('FindInSet','#table','KH','KOD',__przet.KH_KOD,2,1);
         _sv:=exec('m_vat','material',_mat,_kh,FAKSZ.VATZERO);
         __przet.REFVAT:=$_sv;
         __przet.VAT:={? __przet.REFVAT<>''
                      || exec('FindAndGet','#table',SLO,__przet.REFVAT,,"KOD",'')
                      || ''
                      ?};
         __przet.put(1);
         __przet.next()
      !}
   |? FAKSZ.WAL=FAKSZ.NWAL
   || {!
      |?
         __przet.KAUWAL:=0;
         _mat:=exec('FindInSet','#table','M','MATKTM',__przet.INDEKS,__przet.INDEKS);
         _kh:=exec('FindInSet','#table','KH','KOD',__przet.KH_KOD,2,1);
         _sv:=exec('m_vat','material',_mat,_kh,0);
         __przet.REFVAT:=$_sv;
         __przet.VAT:={? __przet.REFVAT<>''
                      || exec('FindAndGet','#table',SLO,__przet.REFVAT,,"KOD",'')
                      || ''
                      ?};
         __przet.put(1);
         __przet.next()
      !}
   ?};
   __przet.cntx_pop();
   exec('set_efld_opt_opk','faktury_nag','DOPK');
   grp_edisp(FAKSZ,'DOPK')
?};
POMOC.LOCK:=_lock;
~~


\check_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Kontrola wypełnienia pól dla waluty
::   WY: ''-jak dobrze lub akronim pola
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? _wyn='' & (exec('spr_ww','eurusd',0) | FAKSZ.WAL<>INFO.NAROD)
||
   {? FAKSZ.WAL<>FAKSZ.NWAL
   ||
      {? FAKSZ.KRS=0
      ||
         FUN.info('Niewypełniony kurs.'@);
         _wyn:='KRS'

      |? FAKSZ.DTK=date(0,0,0)
      ||
         FUN.info('Należy podać datę kursu.'@);
         _wyn:='DTK'
      ?}
   ?};
   {? _wyn='' & FAKSZ.WAL<>INFO.NAROD
   ||
      {? FAKSZ.NKRS=0
      ||
         FUN.info('Niewypełniony kurs.'@);
         _wyn:='NKRS'

      |? FAKSZ.NDTK=date(0,0,0)
      ||
         FUN.info('Należy podać datę kursu.'@);
         _wyn:='NDTK'
      ?}
   ?};
   exec('set_efld_opt_opk','faktury_nag','WOPK')
?};
_wyn


\bedispre
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: przed wyświetleniem DO_FAKT
::----------------------------------------------------------------------------------------------------------------------
"{? __przet.DO_FAKT>0
 || 'xwin16.png:76'
 || exec('pusta','#icon')
?}"


\raporty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: raporty
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
OPAK_N.cntx_psh();
exec('rep_exec','#b_report','LMG_OPK_ZWYD','lmg_opak*','Zestawienia opakowań zwrotnych',1);
OPAK_N.cntx_pop();
~~


\zapytania
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: zapytania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
OPAK_N.cntx_psh();
exec('m_sql','#sql','LMG_OPK_ZSQL','Opakowania');
OPAK_N.cntx_pop();
~~


\addRozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: dodaje rozliczenie kaucji bez faktury
::----------------------------------------------------------------------------------------------------------------------
_edit:=__opow_tab.mk_edit('Rozliczenie kaucji'@,0);
__opow_tab.win_esep(_edit,'Dane do rozliczenia'@);
__opow_tab.win_efld(_edit,,'KH_KOD',,,23,,1,'Kod'@);
__opow_tab.win_efld(_edit,,'KH_SKR',,,23,,1,'Skrót'@);
__opow_tab.win_efld(_edit,,'SYMBOL',,,23,,1,'Dokument'@);
__opow_tab.win_efld(_edit,,'DATA',,,20,,1,'Z dnia'@);
__opow_tab.win_efld(_edit,,'INDEKS',,,23,,1,'Opakowanie'@);
__opow_tab.win_efld(_edit,,'KAUCJA',,,23,3,1,'Kaucja'@);
__opow_tab.win_esep(_edit,'Rozlicz'@);
__opow_tab.win_efld(_edit,,'IL',,,20,3,,'Ilość'@);
exec('ok_esc','#window',__opow_tab,_edit);
__opow_tab.win_edit(_edit);

__opow_tab.blank();
__opow_tab.KH_KOD:=__opow_bl.KH_KOD;
__opow_tab.KH_SKR:=__opow_bl.KH_SKR;
__opow_tab.SYMBOL:=__opow_bl.SYMBOL;
__opow_tab.DATA:=__opow_bl.DATA;
__opow_tab.ZWROT:=__opow_bl.ZWROT;
__opow_tab.POZ:=__opow_bl.POZ;
__opow_tab.INDEKS:=__opow_bl.INDEKS;
__opow_tab.NAZWA:=__opow_bl.NAZWA;
__opow_tab.CZY_KAUC:=__opow_bl.CZY_KAUC;
__opow_tab.KAUCJA:=__opow_bl.KAUCJA;
__opow_tab.IL:=__opow_bl.MAX;
__opow_tab.WAR:=(__opow_tab.IL*__opow_tab.KAUCJA)$2;
__opow_tab.AKC:=__opow_bl.AKC;
{? __opow_tab.edit("exec('chkRozl','opakow')")
|| _ref:=null();
   _il_zwr:=__opow_tab.IL;
   _ref_zwr:='';
   OPAK_ZWR.cntx_psh();
   OPAK_ZWR.use('opakz'+((8+__opow_bl.OPKP_REF)+3));
   OPAK_ZWR.index('REF_ROZL');
   OPAK_ZWR.prefix(__opow_bl.OPKP_REF,__opow_bl.OPKP_REF);
   {? OPAK_ZWR.first()
   || exec('rebuild_fap','opakow_stany',0,$OPAK_P.ref(),'N');
      OPAK_ZWR.IL+=_il_zwr;
      {? OPAK_ZWR.put() || _ref_zwr:=$OPAK_ZWR.ref() ?}
   || OPAK_ZWR.blank();
      OPAK_ZWR.REF_ROZL:=__opow_bl.OPKP_REF;
      OPAK_ZWR.REF_POZ:=__opow_bl.OPKP_REF;
      OPAK_ZWR.IL:=_il_zwr;
      OPAK_ZWR.FAKT:='T';
      {? OPAK_ZWR.add() || _ref_zwr:=$OPAK_ZWR.ref() ?}
   ?};
   OPAK_ZWR.cntx_pop();

   {? _ref_zwr<>''
   ||
      __opow_tab.cntx_psh();
      __opow_tab.prefix();
      {? __opow_tab.first() & __opow_tab.find_tab('first','OPAK_ZWR',,'=',_ref_zwr)
      || __opow_tab.IL+=_il_zwr;
         __opow_tab.WAR:=(__opow_tab.IL*__opow_tab.KAUCJA)$2;
         {? __opow_tab.put(1) || _ref:=__opow_tab.ref() ?}
      ?};
      __opow_tab.cntx_pop();
      {? _ref=null()
      || __opow_tab.WAR:=(__opow_tab.IL*__opow_tab.KAUCJA)$2;
         __opow_tab.OPAK_ZWR:=_ref_zwr;
         {? __opow_tab.add() || _ref:=__opow_tab.ref() ?}
      || __opow_tab.seek(_ref)
      ?}
   ?};
   OPAK_N.cntx_psh();
   OPAK_P.cntx_psh();
   OPAK_P.prefix();
   {? OPAK_P.seek(__opow_bl.OPKP_REF) || exec('rebuild_fap','opakow_stany',0,$OPAK_P.ref(),'T') ?};
   OPAK_N.cntx_pop();
   OPAK_P.cntx_pop();
   {? _ref<>null() & (__opow_bl.prefix(); __opow_bl.first())
   || __opow_bl.MAX-=__opow_tab.IL;
      __opow_bl.put()
   ?}
?};
~~


\chkRozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Kontrola wprowadzonej kaucji do rozliczenia
::   WY: ''-ok lub pole do poprawy
::----------------------------------------------------------------------------------------------------------------------
_res:='';
{? __opow_tab.IL<=0
|| FUN.info('Należy podać ilość rozliczoną.'@);
   _res:='IL'
|? __opow_tab.IL>__opow_bl.MAX
|| FUN.info('Maksymalnie można rozliczyć %1.'@[$__opow_bl.MAX]);
   _res:='IL'
?};
_res


\delRozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: usuwa rozliczenie kaucji bez faktury
::----------------------------------------------------------------------------------------------------------------------
{? __opow_tab.OPAK_ZWR<>''
 & exec('FindAndGet','#table',OPAK_ZWR,__opow_tab.OPAK_ZWR,,"REF_ROZL=REF_POZ & REF_POZ=__opow_bl.OPKP_REF",0)
 & FUN.ask('Czy usunąć rozliczenie?'@)
||
   do();
   OPAK_N.cntx_psh();
   OPAK_P.cntx_psh();
   OPAK_P.prefix();
   {? OPAK_P.seek(__opow_bl.OPKP_REF) || exec('rebuild_fap','opakow_stany',0,$OPAK_P.ref(),'N') ?};
   OPAK_N.cntx_pop();
   OPAK_P.cntx_pop();
   OPAK_ZWR.cntx_psh();
   OPAK_ZWR.use('opakz'+((8+__opow_bl.OPKP_REF)+3));
   OPAK_ZWR.prefix();
   {? OPAK_ZWR.seek(__opow_tab.OPAK_ZWR)
   || OPAK_ZWR.del();
      __opow_tab.del()
   ?};
   OPAK_ZWR.cntx_pop();
   {? (__opow_bl.prefix(); __opow_bl.first())
   || __opow_bl.MAX:=exec('FindAndGet','#table',OPAK_P,__opow_bl.OPKP_REF,,"IL",0);
      __opow_bl.put()
   ?};
   end();
   exec('rekRozl','opakow')
?};
~~


\rekRozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: wygaszanie akcji
::----------------------------------------------------------------------------------------------------------------------
_rozl:=__opow_tab.size()=0 | (__opow_tab.OPAK_ZWR<>''
 & exec('FindAndGet','#table',OPAK_ZWR,__opow_tab.OPAK_ZWR,,"REF_ROZL=REF_POZ & REF_POZ=__opow_bl.OPKP_REF",0));

{? ~_rozl
|| {? __opow_bl.MAX>0 || _act:='U' || _act:='DU:D' ?}
|? __opow_bl.MAX<=0
|| _act:='D:D'
|| _act:=''
?};
__opow_tab.actions_grayed(__opow_sel,_act);
''


\opak_n_kh_odb_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: wzorzec OPAK_N.KH_ODB
::----------------------------------------------------------------------------------------------------------------------
BEER.KH:=OPAK_N.KH;
exec('kh_odb_f_set','kontrahent');
''


\opak_n_kh_odb_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: przed redakcja OPAK_N.KH_ODB
::----------------------------------------------------------------------------------------------------------------------
exec('be_op_kh','opakow') & OPAK_N.KH<>null


\kopiuj_dokument
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Kopiuje dokument opakowań
::   WE: _a - OPAK_N
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_opak_n_src:={? var_pres('_a')=type_of(null()) || _a || OPAK_N.ref() ?};
_default:={? OPAK_N.PLUS='N' || 1 || 2 ?};
_choice:=FUN.choice('Jaki dokument utworzyć?'@,_default,'Przychodowy'@,'Rozchodowy'@);
OPAK_N.cntx_psh();
::OPAK_N.use(_opak_n);
OPAK_N.prefix();
{? _choice & OPAK_N.seek(_opak_n_src)
||
   OPAK_N.DATA:=date();
   OPAK_N.PLUS:={? _choice=1 || 'T' || 'N' ?};
   OPAK_N.ND:='';
   OPAK_N.FAKS:='';
   OPAK_N.AKC:='N';
   POM.TAB:='OPAK_N';
   POM.TYPDOK:='OPK';
   OPAK_N.NR:=0;
   OPAK_N.SYM:='';
   OPAK_N.cntx_psh();
   {? OPAK_N.add()
   ||
      _opak_n_dst:=OPAK_N.ref();
      exec('wol_nr','numery',POM.TAB)
   ?};
   OPAK_N.cntx_pop();
   {? _opak_n_dst
   ||
      OPAK_P.cntx_psh();
      OPAK_P.index('OPAK_N');
      OPAK_P.prefix(_opak_n_src);
      _loop:=OPAK_P.first();
      {!
      |? _loop
      |!
         _ref_poz:=$OPAK_P.ref();
         OPAK_P.cntx_psh();
         OPAK_P.prefix();
         OPAK_P.OPAK_N:=_opak_n_dst;
         OPAK_P.DK:='';
         OPAK_P.KH:=OPAK_N.KH;
         OPAK_P.KH_ODB:=OPAK_N.KH_ODB;
         {? OPAK_P.ZWROT='T'
         ||
            OPAK_P.CZY_KAUC:='N';
            OPAK_P.WAR:=0
         ?};
         OPAK_P.ZWROT:='N';
         {? OPAK_P.add()
         ||
            exec('zwr_rozp','opakow',OPAK_P.ref(),,1)
         ?};
         OPAK_P.cntx_pop();
         _loop:=OPAK_P.next()
      !};
      OPAK_P.cntx_pop()
   ?}
?};
OPAK_N.cntx_pop()


\kh_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Zwraca KH.ref()
::   WE: _a - KH.KOD
::   WY: KH.ref()
::----------------------------------------------------------------------------------------------------------------------
exec('FindInSet','#table','KH','KOD',_a,2,,1,,null())


\odb_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Zwraca KH_ODB.ref()
::   WE: _a - KH.ref()
::       _b - KH_ODB.KOD
::   WY: KH_ODB.ref()
::----------------------------------------------------------------------------------------------------------------------
exec('FindInSet','#table','KH_ODB','KH_ODB',_b,_a,,1,,null())


\init_var
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Inicjuje zmienne
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
POMOC.OPAK_N:=OPAK_P.OPAK_N;
POMOC.KH:=OPAK_P.OPAK_N().KH;
_pow:=exec('opow_mk','opakow',0,0);
POMOC.IL_ZWR:=_pow[1];
DISP.JM:=OPAK_P.M().J;
{? OPAK_P.CZY_KAUC='T' & OPAK_P.ZWROT='N'
|| POMOC.BKC:=OPAK_P.WAR-_pow[1]
|| POMOC.BKC:=0
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:39 dc3113314f6f94c53a25de5f5af1d1012ebe4d46a11b8bd14fecba15fd42450d644279e4e1ddf51a0b052e5876f698f37212049207c8f3249669bea503730e7e49d88d9c6365d473e927ece35e8461ac6f8f7a429168d6dc90fd215be61bfc43c9e21bad219e314f70ed268b0d3ba45e4de9b8fb8ff598e23ff47a46c31b2b9b

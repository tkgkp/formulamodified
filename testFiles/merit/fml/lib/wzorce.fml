:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: wzorce.fml
:: Utworzony: 14.05.2007
:: Autor: Mario
::======================================================================================================================
:: Zawartość:  Formuły do obsługi wzorców słowników
::======================================================================================================================


\wz_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [???]
:: OPIS: Ustawia słownik walut
::   WE: [_a] - czy wyświetlać komunikat o braku słownika walut - domyślnie 1 (tak); 0 (nie)
::   WY: 1 - słownik został ustawiony, 0 - słownik nie został ustawiony
::  OLD: (\wz_wal/wzorce.fml) \jakisluw/wspol.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' || return('') ?};
{? var_pres('_a')=type_of(0) || _display:=_a || _display:=1 ?};
{? var_pres('def_slo')<=0 || exec('czytaj','#stalesys',,XINFO,'SLWAL') ?};
_slwal:=XINFO.SLWAL;
{? _slwal<>null()
|| XINFO.SLWAL();
   {? cur_tab(1,1)=OP
   || _wer:=SLO.mk_sel('Waluty'@,'P',,'slo_werxx',,,,,'U');
      SLO.win_fld(_wer,,'KOD',,,8,,1,'Kod'@,,'Kod pozycji słownika walut'@);
      SLO.win_fld(_wer,,'TR',,,40,,1,,,'Treść pozycji słownika walut'@);
      SLO.win_act(_wer,0,'Wyświetl',,,,"exec('pw_kodsl','slo_slu')","SLO.display()");
      SLO.win_sel(_wer); SLO.win_dict(_wer)
   ?};
   exec('slu_okn','slo_slu')
?};
''


\jakislw
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: ustalenie slownika wydzialow - obsluga przed redakcja
::  OLD: \jakislw/wsp_mag.fml
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SLWYDZIA<>null()
|| XINFO.SLWYDZIA();
   SLO.win_sel('ONE');
   SLO.win_dict('ONE');
   SLO.hdr_sel();
   SLO.hdr_sel('Wydziały'@);
   1
|| FUN.emsg('Nie ustalono słownika wydziałów w parametrach pracy programu.'@);
   0
?}


\wz_wydz
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Ustalenie słownika wydziałów - obsługa na wzorzec
::  OLD: \jakislwx/wsp_mag.fml
::       \wz_wydz/wzorce.fml
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SLWYDZIA<>null()
|| XINFO.SLWYDZIA();
   SLO.win_sel('ONE');
   SLO.win_dict('ONE');
   SLO.hdr_sel();
   SLO.hdr_sel('Wydziały'@);
   exec('slu_okn','slo_slu')
?};
''


\zero_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: przed redakcją pola typu KK (pola złączeniowe do KK) - usuwa wzorzec
::  OLD: \zero_kon/skid_edk.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.WZ_KON:='';
1


\wz_bank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Ustawia słownik banków
::   WE: [_a] - czy wyświetlać komunikat o braku słownika banków - domyślnie 1 (tak); 0 (nie)
::   WY: 1 - słownik został ustawiony, 0 - słownik nie został ustawiony
::  OLD: (\wz_bank/wzorce.fml) \blslowban/stalesys.fml
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SLBANK=null() || exec('czytaj','#stalesys',,XINFO,'SLBANK') ?};
{? var_pres('_a')=type_of(0) || _display:=_a || _display:=1 ?};
_slbank:=XINFO.SLBANK;
{? _slbank<>null()
|| XINFO.SLBANK();
   exec('slu_okn','slo_slu');
   1
|| {? _display || FUN.info('Nie ustalono słownika banków w parametrach pracy programu.'@) ?};
   0
?};
''


\wz_sppl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: wzorzec sposobu platnosci dla kontrahenta
::  OLD: \wz_sppl/skid_kh.fml
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SLP<>null
|| SLO.win_edit('RED');
   SLO.hdr_sel();
   SLO.hdr_sel('Formy płatności'@);
   XINFO.SLP()
|| FUN.info('W systemie brak zdefiniowanego słownika form płatności.'@);
   SLU.blank()
?};
''


\wz_llccc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: wzorzec
::  OLD: \wz_llccc/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| {? cur_tab(1,1)=FAKS & FAKS.T().PAR='T'
   || ''
   || 'uu9!'
   ?}
|| ''
?}


\wz_grrmk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.40]
:: OPIS: Ustawienie slownika dla grup RMK lub RMP
::----------------------------------------------------------------------------------------------------------------------
{? RMK.TYP='RMK' & FINFO.GRUPYRMK<>null | RMK.TYP='RMP' & FINFO.GRUPYRMP<>null
|| SLO.win_edit('RED');
   {? RMK.TYP='RMK' || FINFO.GRUPYRMK().SLU() || FINFO.GRUPYRMP().SLU() ?}
?};
''


\wz_rmodb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: wzorzec OFE.KH_ODB
::  OLD: \wz_rmodb/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.KH:=ROKMC.KH;
''


\spr_sppl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: sprawdzenie sposobu platności
::  OLD: \spr_sppl/umowy.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_S:=$fld;
SLO.hdr_sel();
SLO.hdr_sel('Formy płatności'@);

exec('wz_sppl','wzorce')='' & XINFO.SLP<>null


\wz_kh_msc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: miejsce dostawy - wzorzec
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| {? cur_kwin()='e_patt'
   || {? cur_tfld()=ND
      || KH_MSC.prefix();
         KH_MSC.f_set('NAZ')
      || KH_MSC.prefix();
         KH_MSC.f_set('NAZ',,'KH_MSC.KH is null')
      ?}
   ?}
?};
''


\wz_uuu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2010]
:: OPIS: Napis 'uuu'
::  OLD: \duze2/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
'uuu'


\formulaValid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: ustawia typ formuły do walidacji
::----------------------------------------------------------------------------------------------------------------------
FORMULA.fld_fml('FORMULA','F3',"");
exec('fl_rodz','formula','v');
1


\validate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: uruchamia formułe do walidacji (zakończenia lub akceptacji)
::   WE: _a - formuła ref
::       _b - tabela
::       _c - wskazanie na rekord
::   WY: 1-OK lub 0-nie spełniono warunków
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_reff:={? var_pres('_a')<>type_of(null()) || null() || _a ?};
_tab:={? var_pres('_b')<>type_of(SYSLOG) || ~~ || _b ?};
_ref:={? var_pres('_c')<>type_of(null()) || null() || _c ?};

_cntx:=0;
_oki:=1;
{? var_pres('_tab')=type_of(SYSLOG) & $_ref<>''
|| _cntx:=1;
   _tab.cntx_psh();
   _tab.prefix();
   {? ~_tab.seek(_ref) || _oki:=0 ?}
?};

{? ~_oki
|| _msg:='Nie znaleziono nagłówka.'@
|? _reff<>null()
|| _name:=exec('FindAndGet','#table',FORMULA,_reff,,"SKROT+{? NAZWA<>'' || ' - '+NAZWA || '' ?}",'');
   _form:=form(exec('FindAndGet','#table',FORMULA,_reff,,"FORMULA",''));
   {? _form=''
   || _msg:='Brak treści formuły: %1 do wykonania.'@[_name]
   || _msg:='';
      on_error(2);
      no_msg(1);
      errno();
      _wyn:=($_form)();
      no_msg(0);
      on_error();
      _err:=errno();
      {? _err
      || _msg:='Formuła: %1\nzwróciła błąd o numerze %2.'@[_name,$_err]
      |? ~_err & var_pres('_wyn')<>type_of(0)
      || _msg:='Nieprawidłowy wynik zwracany przez formułę: %1.\nOczekiwany typ liczbowy.'@[_name]
      ?}
   ?}
|| _msg:='Nieprawidłowy parametr formuły.'@
?};
{? _msg<>''
|| FUN.emsg(_msg)
|| _res:=_wyn
?};
{? _cntx || _tab.cntx_pop(); _tab.get() ?};
_res


\formula_valid_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: po formule do walidacji
::----------------------------------------------------------------------------------------------------------------------
FORMULA.fld_fml('FORMULA','F3',"*");
1


\formulaAccept
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: ustawia typ formuły do akceptacji
::----------------------------------------------------------------------------------------------------------------------
FORMULA.fld_fml('FORMULA','F3',"");
exec('fl_rodz','formula','a');
1


\formulaValrec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: ustawia typ formuły na wycofanie
::----------------------------------------------------------------------------------------------------------------------
FORMULA.fld_fml('FORMULA','F3',"");
exec('fl_rodz','formula','r');
1


\formulaWspJM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.14]
:: OPIS: ustawia typ formuły dla współczynnika przeliczenia
::----------------------------------------------------------------------------------------------------------------------
FORMULA.fld_fml('FORMULA','F3',"");
exec('fl_rodz','formula','j');
1


\formulaGS1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.14]
:: OPIS: przed redakcją pola EAND.FORM
::----------------------------------------------------------------------------------------------------------------------
{? EAND.R='F'
|| FORMULA.fld_fml('FORMULA','F3',"");
   exec('fl_rodz','formula','g');
   1
|| 0
?}


\formulacGS1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.14]
:: OPIS: przed redakcją pola EAND.FCON
::----------------------------------------------------------------------------------------------------------------------
{? EAND.R='A'
|| FORMULA.fld_fml('FORMULA','F3',"");
   exec('fl_rodz','formula','g');
   1
|| 0
?}


\formuladgs1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.14]
:: OPIS: przed redakcją pola EAND.FCON
::----------------------------------------------------------------------------------------------------------------------
FORMULA.fld_fml('FORMULA','F3',"");
exec('fl_rodz','formula','g');
1


\adrK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.14]
:: OPIS: tabela nazwana dla adresu korespondencyjnego
::   WY: tabela nazwana
::----------------------------------------------------------------------------------------------------------------------
_res:=obj_new('NAZ_P','KPOCZ','POCZ','MIASTO','UL','KRAJ');
_res

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:26 2b92fe34cb4b3ada2fe7b4ecb37f06fa79157b8dc198495fa643462d0dd48adfcc83ca13dce69a74a2ecc5a78457fa34d3d4da4e26d00657c004707168923272af6425f41815414ffeb28ac79f793dcb89fc17cbf2d6e830777e9f63729c61b4abb97c1e09d576080b0cd916d5cf6d46538bb8f96e2f1739eec1af2a7d71a475

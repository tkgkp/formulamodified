:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_apl.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa aplikacji dla systemu Statystyki
::======================================================================================================================


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Czyści powiązania do rekordu tabeli ST_APL
::   WE: _a - ST_APL.ref()
::   WY: >0  -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PRYWATNA><CLEAN>
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---
ST_APL.cntx_psh(); ST_APL.clear();
{? ST_APL.seek(_ref)
||
   {? _can_continue>0
   ||
::    1. Usuwam ST_SESJA które wskazują na ten element
      ST_SESJA.cntx_psh();
      ST_SESJA.index('ST_APL');
      ST_SESJA.prefix(ST_APL.ref());
      {? ST_SESJA.first()
      || {!
         |? _can_continue:=exec('delete','st_sesja',ST_SESJA.ref());
            ST_SESJA.first() & _can_continue>0
         !}
      ?};
      ST_SESJA.cntx_pop();

      ST_SESJ2.cntx_psh();
      ST_SESJ2.index('ST_APL');
      ST_SESJ2.prefix(ST_APL.ref());
      {? ST_SESJ2.first()
      || {!
         |? _can_continue:=exec('delete','st_sesj2',ST_SESJ2.ref());
            ST_SESJ2.first() & _can_continue>0
         !}
      ?};
      ST_SESJ2.cntx_pop();
      ~~
   ?};
   ~~
?};

ST_APL.cntx_pop();
:: --- wszystkie powiazania usuniete? ---

{? _can_continue>0
|| _result:=1
|| undo()
?};

{? _mydo || end() ?};

_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Kasuje podany rekord tabeli ST_APL (wykonywane w transakcji!!!)
::   WE: _a - ST_APL.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_APL.cntx_psh(); ST_APL.clear();
{? ST_APL.seek(_ref)
|| {? exec('clean','st_apl',_ref)>0
   || {? ST_APL.del(,1)>0
      || _result:=1
      || undo();
         _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};

{? _result<0
|| undo()
?};

ST_APL.cntx_pop();
{? _mydo || end() ?};
_result


\st_apl_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Dołącz dla ST_APL
::----------------------------------------------------------------------------------------------------------------------
ST_APL.win_edit('RED');
ST_APL.blank();
{? ST_APL.edit()
|| ST_APL.add()
?};
~~


\st_apl_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń dla ST_APL
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRUPA='T' | FUN.ask('Czy usunąć bieżący wiersz?'@)
|| exec('delete','st_apl',ST_APL.ref())
?};
~~


\st_apl_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Popraw dla ST_APL
::----------------------------------------------------------------------------------------------------------------------
ST_APL.win_edit('RED');
{? ST_APL.edit()
|| ST_APL.put()
?};
~~


\st_apl_del_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń przed dla grupy rekordów (tabela ST_APL)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?'@)
|| VAR.GRUPA:='T';
   1
|| 0
?}


\st_apl_del_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń po dla grupy rekordów (tabela ST_APL)
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~


\aplikacja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Aplikacja
::   WE: _a - obj_new() - środowisko parsera, wynik działania exec('env_parse','st_common')
::       _b - obj_new() - obiekt zawierający treść wiersza, wynik działania exec('POPUPITEME','st_parse_obj')
::       _c - nazwa definicji
::       _d - nazwa podsystemu (xpertis) / symbol dziedziny (merit)
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_obj:=_b;
_def_name:=_c;
_symbol:=_d;

_result:=0;
_can_continue:=1;

_kind:=_obj.KIND;
_error:=_env.TXT_ERROR;
_warning:=_env.TXT_WARNING;
_env.REG.ST_APL:=null();

{? _def_name<>'xpertis' & _def_name<>'merit'
||
   _env.message('%1: %2 - Nieznana nazwa systemu: %3.'@[ _error, _kind, _def_name]);
   _can_continue:=0
?};
{? _can_continue>0
||
   _apl_symbol:=
      {? _symbol=''
      || '%1'[ _def_name]
      || '%1_%2'[ _def_name, _symbol]
      ?};
   ST_APL.index('SYMBOL');
   ST_APL.prefix(_apl_symbol,);
   {? ST_APL.first()
   ||
      _env.REG.ST_APL:=ST_APL.ref()
   ||
      ST_APL.blank();
      ST_APL.SYMBOL:=_apl_symbol;
      ST_APL.MERIT:={? (5+_apl_symbol)='merit' || 'T' || 'N' ?};
      {? ~ST_APL.add()
      ||
         _env.message('%1: %2 - ST_APL.add()'@[ _error, _kind]);
         _can_continue:=0
      ||
         _env.REG.ST_APL:=ST_APL.ref()
      ?}
   ?}
?};

{? _can_continue>0
|| _result:=1
?};
_result

:Sign Version 2.0 jowisz:1048 2021/04/09 15:26:32 5e929c00eb4a41e924fd3e82ac8db913fa3a1e2dcaa0e79f5b6e1012be8d55713c5f3c015a5990f3800dcc68d5fb0e87103ac029ba89791127e9ec0df499bdae232b34ea77397230a1cc421e221aa8469bbfab5b4aef47773ba02e92635a454176a9c741128bb9258cde7a379bc5de98691405f597e5195b711cf99163502967

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: kasa_dokument.fml
:: Utworzony: 01.03.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Kasa - dokument
::======================================================================================================================


\dost_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: Wybór odpowiednich okien dla dokumentów - w zależności
::       od uprawnień, statusu raportu; ewentualna zmiana statusu raportu
::   WE: [_a] - czy prezentowac komunikat? [1]-tak 0-nie
::  OLD: \dost_dok/okna.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=1 ?};
WAL.cntx_psh();
_st_wal:=STANKAS.WALUTA; _st_waln:=STANKAS.WALUTA().SYM;
WAL.cntx_pop();
_lock:=0;
{? RAPORT.get() & {? KUSERUPR.UPR='N' || 1 || RAPORT.r_lock(1,1,1) ?}
|| DOKUMENT.win_sel('WER');
   _lock:=1
|| {? _a & ~RAPORT.sel_size || FUN.info('Raport %1 jest obsługiwany przez innego operatora.'@[$RAPORT.NUM_RAP]) ?};
   _lock:=0
?};
_lock


\blankrap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Wartosc Poczatkowa DOKUMENT.RAPORT
::  OLD: \blankrap/okna.fml
::----------------------------------------------------------------------------------------------------------------------
RAPORT.ref()


\be_dokop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Przed redakcja pola DOKUMENT.OPIS i DOKUMENT.LP
::  OLD: \be_dokop/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
var_pres('dok1poz')<0


\dok_dtbl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BL  [2006]
:: OPIS: blank pola DOKUMENT.DATA (MG)
::   WY: _dData - data
::  OLD: \dok_dtbl/skid_plb.fml
::----------------------------------------------------------------------------------------------------------------------
_dData:=#0;
DOKUMENT.RAPORT();
{? RAPORT.DATA_OD=RAPORT.DATA_DO
|| _dData:=RAPORT.DATA_DO
|| DOKUMENT.cntx_psh();
   _dData:={? DOKUMENT.last()
           || DOKUMENT.DATA
           || RAPORT.DATA_OD
           ?};
   DOKUMENT.cntx_pop()
?};
_dData


\dok_datb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przed redakcję pola data dokumentu
::       jesli raport jednodniowy to jedna mozliwosc i nie ma redakcji
::       wpp - podpowiedź na datę ostatniego dokumentu
::   WE:
::   WY:
::  OLD: \dok_datb/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__DOK_DATA0');
_wyn:=(RAPORT.DATA_OD<>RAPORT.DATA_DO & var_pres('dok1poz')<0);
{? _wyn
|| __DOK_DATA0:=DOKUMENT.DATA
?};
_wyn


\dok_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: po redakcji pola data dokumentu - musi zawierać się w zakresie dat
::       wyznaczonych przez raport oraz data kolejnego dokumantu musi byc>=daty
::       poprzedniego
::   WE:
::   WY:
::  OLD: \dok_data/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMENT.RAPORT();
_b:=0;
_c:=0;
_d:=date;

:: szukamy ostatniego/nastepnego i zapamiętujemy jego date
DOKUMENT.cntx_psh();
{? {? Popraw=1 || DOKUMENT.next() || DOKUMENT.last() ?}
|| _b:=1; _a:=DOKUMENT.DATA
?};
DOKUMENT.cntx_pop();

:: szukamy poprzedniego jesli jest poprawiany i zapisujemy jego date
DOKUMENT.cntx_psh();
{? {? Popraw=1 || DOKUMENT.prev() || DOKUMENT.last() ?}
||   _c:=1; _d:=DOKUMENT.DATA
?};
DOKUMENT.cntx_pop();

{? DOKUMENT.DATA>=RAPORT.DATA_OD & DOKUMENT.DATA<=RAPORT.DATA_DO
|| {? _b=1
   ||
::    jesli jest akcja popraw to sprawdzamy tylko z data poprzedniego
::    dokumentu
      {? DOKUMENT.DATA<=_a | Popraw=0
      || _wyn:=exec('prev_chk','kasa_dokument',_d,_c)
      || FUN.info('\nData dokumentu nie może być późniejsza'
            '\nniż data dokumentu następnego.'@);
         _wyn:=0
      ?}
   || _wyn:=exec('prev_chk','kasa_dokument',_d,_c)
   ?}
||  FUN.info('\nData dokumentu musi zawierać się w zakresie'
      '\ndat raportu.\n'@);
    _wyn:=0
?};
{? _wyn & var_press('__DOK_DATA0') > 0
|| {? DOKUMENT.DATA<>__DOK_DATA0
   || exec('dok_kurb','kasa_dokument',1,1)
   ?};
   VAR_DEL.delete('__DOK_DATA0')
?};
_wyn


\prev_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: sprawdzenie czy data poprzedniego dokumentu nie jest czasem
::       większa od daty bieżącego
::   WE:
::   WY:
::  OLD: \prev_chk/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
:: _b=1 - jest dokument następny
{? _b=1
|| {? DOKUMENT.DATA<_a
   || FUN.info('\nData dokumentu nie może być wcześniejsza'
         '\nniż data dokumentu poprzedniego.'@);
      0
   || 1
   ?}
|| 1
?}


\poper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Wartosc poczatkowa pola DOKUMENT.OPER
::  OLD: \poper/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
MLKAS.OPER


\dok_tdb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: typ dokumentu - KP/KW/INNY - mozna poprawiać dopóki nie wystawiono dokumentu
::   WE:
::   WY:
::  OLD: \dok_tdb/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMENT.DOK_NUM = 0


\dok_wbla
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BL  [2006]
:: OPIS: blank pola DOKUMENT.WALUTA (MG)
::   WE: WAL.ref/brak
::   WY: _rWal
::  OLD: \dok_wbla/skid_plb.fml
::----------------------------------------------------------------------------------------------------------------------
_rWal:=0;
USRCONST.STANKAS();
KUSERUPR.STANKAS();
{? STANKAS.WALUTA<>0
|| _rWal:=STANKAS.WALUTA
?};
_rWal


\dok_walb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przed redakcją pola waluta - jeśli do stanowiska jest przypisana waluta to
::       pole ustawiane na tę walutę a redakcja zabroniona
::       wpp - zmiana dozwolona jeśli nie ma pozycji dokumentu
::   WE:
::   WY:
::  OLD: \dok_walb/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
USRCONST.STANKAS();
KUSERUPR.STANKAS();
{? STANKAS.WALUTA<>0
|| DOKUMENT.WALUTA:=STANKAS.WALUTA; 0
|| ~exec('is_poz','kasa_dokument')
?}


\is_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: sprawdzenie czy są pozycje dokumentu
::   WE:
::   WY:
::  OLD: \is_poz/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? Popraw=1
||
   POZDOK.index('DOKUMENT');
   POZDOK.prefix(DOKUMENT.ref);
   POZDOK.first()
||
   0
?}


\dokwalae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Po redakcji pola DOKUMENT.WALUTA
::   WE:
::   WY:
::  OLD: \dokwalae/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
 _zwrot:=1;
{? DOKUMENT.PLUS_MIN='-' & STANKAS.CZY_KASA='T'
|| {? DOKUMENT.WALUTA<>null
   || OBROTY.cntx_psh();
      OBROTY.index('RAPORTR');
      OBROTY.clear();
      {? ~OBROTY.find_key(RAPORT.ref(),DOKUMENT.WALUTA().SYM,'W')
      || FUN.info('Zerowy stan środków w kasie dla waluty %1'
            '.\nNie można wydać gotówki.\n'@[WAL.SYM]);
         _zwrot:=0
      || _zwrot:=1
      ?};
      OBROTY.cntx_pop()
   || FUN.info('Proszę wypełnić pole waluta.'@); _zwrot:=0
   ?}
?}; _zwrot


\dok_kurb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przed redakcją pola kurs - jeśli do stanowiska jest przypisana waluta PLN
::       pole ustawiane na 1 a redakcja zabroniona
::       wpp - jeśli to rozchód to kurs średni od salda w kasie (jak w magazynie)
::       wpp - podpowiedź kursu jeśli nie ma pozycji dokumentu
::       redakcja mozliwa jeśli na to zezwalaja param. stanowiska
::   WE: _a - [1]-wyznaczyć średni kurs, 0-wpp
::       _b - [0]-podpowiedzieć kurs tylko gdy DOKUMENT.KURS-1, 1-zawsze
::   WY:
::  OLD: \dok_kurb/dok_pola
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0) || _a:=1 ?};
{? var_pres('_b')<>type_of(0) || _b:=0 ?};
XINFO.SLBANK();
USRCONST.STANKAS();
KUSERUPR.STANKAS();
{? DOKUMENT.WALUTA=0
|| 0
|? DOKUMENT.WALUTA = exec('wal_nar','kasa_wspolne')
|| DOKUMENT.KURS:=1; DOKUMENT.efld_opt(DOKUMENT.win_edit('?'),'mark=0',,'KURS'); 0
|| DOKUMENT.efld_opt(DOKUMENT.win_edit('?'),'mark=1',,'KURS');
   {? DOKUMENT.PLUS_MIN<>'+' & DOKUMENT.PLUS_MIN<>'-'
   || 0
   |? DOKUMENT.PLUS_MIN='-' & STANKAS.CZY_KASA='T'
   || {? _a
      || DOKUMENT.KURS:=exec('sred_krs','kasa_dokument',DOKUMENT.WALUTA)
      ?};
      KPARAM.CZY_ZMKU='T'
   || {? ~exec('is_poz','kasa_dokument')
      ||
::       czy w param. stanowiska jest podpowiedz kursu?
         {? STANKAS.CZY_KTIP='N'
         || 1
         || {? DOKUMENT.KURS=1 | _b
            || _data:=DOKUMENT.DATA;
               _dni_wstecz:=0;
               {? DOKUMENT.PLUS_MIN='+'
               || _dni_wstecz:=exec('get','#params',300611,type_of(0))
               |? DOKUMENT.PLUS_MIN='-'
               || _dni_wstecz:=exec('get','#params',300612,type_of(0))
               ?};
               {? _dni_wstecz>0
               || _data:=exec('dzienRob','kalendarz',_data-_dni_wstecz,1,1,0,-1)
               ?};
               DOKUMENT.KURS:=exec('find_krs','kasa_dokument',
                  exec('slo_wal','kasa_dokument',DOKUMENT.WALUTA),
                  exec('slo_bank','kasa_dokument',STANKAS.TAB_KURS),
                  _data,
                  STANKAS.RODZ_KUR);
               {? DOKUMENT.KURS$6=0 || DOKUMENT.KURS:=1 ?}
            ?};
            STANKAS.CZY_ZMKU='T'
         ?}
      ?}
   ?}
?}


\sred_krs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: Średni kurs
::   WE:
::   WY:
::  OLD: \sred_krs/sumy.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMENT.cntx_psh();
exec('rap_suma','kasa_wspolne');
DOKUMENT.cntx_pop();
OBROTY.index('RAPORTR');
OBROTY.clear();
WAL.seek(_a);
{? OBROTY.find_key(RAPORT.ref(),WAL.SYM,'W')
|| {? (OBROTY.BO_WAL+OBROTY.PLUS_WAL-OBROTY.MIN_WAL)<>0 & WAL.J<>0
   || ((OBROTY.BO_PLN+OBROTY.PLUS_PLN-OBROTY.MIN_PLN)/
       ((OBROTY.BO_WAL+OBROTY.PLUS_WAL-OBROTY.MIN_WAL) / WAL.J ) )$6
   || FUN.info('Zerowe saldo kasy: waluta %1.'@[WAL.SYM]); 0
   ?}
?}


\find_krs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: dla argumentów znajduje kurs
::   WE: _a - ref waluty z tabeli walut
::       _b - ref banku z tabeli SLO
::       _c - data kusru
::       _d - rodzaj kursu (R,Z,S)
::   WY:
::  OLD: \find_krs/kursy.fml
::----------------------------------------------------------------------------------------------------------------------
_k:=0;
{? _a<>null & _b<>null
|| SLO.cntx_psh();
   SLO.seek(_b);
   TKRS.index('TKRS_DT');
   TKRS.clear();
   TKRS.prefix(_b,KINFO.NAROD);
   {? ~TKRS.find_key(_c)
   || {? TKRS.find_le(_c)
      || FUN.info('\nBrak tabeli kursów dla %1\nna dzień %2.\n'
            'Przyjęto tabelę z dnia %3.'@[SLO.TR,$_c,$TKRS.DT]); _k:=1
      || _k:=0
      ?}
   || _k:=1
   ?};
   TKRS.clear();
   SLO.cntx_pop();
   KRS.index('KRS_WAL');
   KRS.clear();
:: jesli nie ma tabeli lub nie ma kursu dla danej waluty w tej tabeli
   {? ~KRS.find_key(TKRS.ref(),_a) | _k=0
   || _k:=0
   || {? _d='R'
      || _k:=KRS.SR
      |? _d='Z'
      || _k:=KRS.KP
      |? _d='S'
      || _k:=KRS.SP
      || _k:=0
      ?}
   ?}
?};
_k


\slo_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: dla argumentu - refa waluty zwraca ref jego odpowienika w SLO
::   WE:
::   WY:
::  OLD: \slo_wal/slo_slu.fml
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,KINFO);
WAL.seek(_a);
SLU.prefix(); SLUAPPL.prefix();
SLO.index('SL'); SLO.prefix(KINFO.SLWAL().SLU,WAL.SYM);
_zwrot:={? SLO.first()
        || SLO.ref()
        || 0
        ?};
_zwrot


\slo_bank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: dla argumentu - refa banku zwraca ref jego odpowienika w SLO
::   WE:
::   WY:
::  OLD: \slo_bank/slo_slu.fml
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,KINFO);
SLO.index('SL');
{? B.seek(_a) & +B.KOD
|| SLO.prefix(KINFO.SLBANK().SLU,B.KOD);
   {? SLO.first()
   || SLO.ref()
   || null
   ?}
|| null
?}


\dok_kurf3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: akcja F3 dla kursu (wybor banku oraz kursu)
::   WY: 1/0
::  OLD: \dok_kurf3/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=cur_tfld.KURS;

TKRS.cntx_psh();
KRS.cntx_psh();
SLO.cntx_psh();

SLO.index('SL');

SLO.prefix(XINFO.SLWAL,SLOSLU.WAL_N);
{? SLO.first() || _wal_n:=SLO.ref() ?};

SLO.prefix(XINFO.SLWAL,DOKUMENT.WALUTA().SYM);
{? SLO.first() || _wal:=SLO.ref() ?};

{? _wal<>null & _wal_n<>null
||
   DPOZ.WAL:=_wal;
   _ok:=1;

   SLU.cntx_psh();
   {? KINFO.SLBANK().SLU<>null
   ||
      KINFO.SLBANK().SLU();
      _bank:=null;
      SLO.index('SL');
      SLO.prefix(SLU.ref(),DOKUMENT.BTK);
      {? SLO.first() || _bank:=SLO.ref() ?};
      SLO.prefix(SLU.ref());
      SLO.seek(_bank);
      SLO.win_sel('BANKI');
      exec('slu_okn','slo_slu')
   ||
      FUN.info('Nie zdefiniowano słownika banków.'@);
      _ok:=0
   ?};
   SLU.cntx_pop();

   {? _ok=1
   ||
      RS.cntx_psh();
      SLU.cntx_psh();
      {? SLO.select(,1)
      ||
         _slo:=SLO.ref();
         {? _slo<>null
         ||
            DPOZ.B:=_slo;
            _acr:=KRS.ndx_tmp('',0,'TKRS','BANK',0,'TKRS','WAL',0,'WAL',,0,'TKRS','DT',0);
            KRS.index(_acr);
            KRS.prefix(_slo,_wal_n,_wal);
            KRS.win_sel('SLO');
            KRS.hdr_sel();
            KRS.hdr_sel(' banku '+SLO.TR);
            {? KRS.select(,KRS.last,10)
            ||
               _rodz_kur:={? ST.STANKAS().RODZ_KUR='S' || 1 |? STANKAS.RODZ_KUR='Z' || 2 || 3 ?};
               _opc:=FUN.choice('Wybierz typ kursu: '@,_rodz_kur,'Sprzedaży'@,'Zak&upu'@,'Ś&redni'@);
               DOKUMENT.DTK:=KRS.TKRS().DT;
               DOKUMENT.RTK:=#KRS.TKRS;
               DOKUMENT.NTK:=ref_name(TKRS.ref());
               DOKUMENT.BTK:=KRS.TKRS().BANK().KOD;
               _wyn:=DOKUMENT.KURS:={? _opc=1 || KRS.SP |? _opc=2 || KRS.KP |? _opc=3 || KRS.SR ?}
            ?};
            KRS.ndx_drop(_acr)
         ?}
      ?};
      SLU.cntx_pop();
      RS.cntx_pop()
   ?}
||
   FUN.info('Brak waluty w słownikach uzytkownika.'@)
?};

SLO.cntx_pop();
KRS.cntx_pop();
TKRS.cntx_pop();
_wyn


\dok_kura
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: formula po redakcji kursu
::   WY: 1/0
::  OLD: \dok_kura/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
fld(fld$6);
{? fld<=0 || FUN.info('Proszę wprowadzić wartość większą od zera.'@) ?};
fld>0


\dok_dotb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przed redakcja pola dotyczy - słownika z którego wybieramy kontrahenta
::       pole niedostępne do modyfikacji
::   WE:
::   WY:
::  OLD: \dok_dotb/dok_pola
::----------------------------------------------------------------------------------------------------------------------
0


\dok_dlab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przed redakcja pola dla - wybór kontrahenta
::       pole dostępne do modyfikacji tylko jeśli niewystawiono KP/KW
::   WE:
::   WY:
::  OLD: \dok_dlab/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOKUMENT.DOTYCZY<>0 & DOKUMENT.DOK_NUM=0
|| {? Popraw=1
   || POZDOK.cntx_psh();
      POZDOK.index('DOKUMENT'); POZDOK.prefix(DOKUMENT.ref());
      _ok:=~POZDOK.first();
      POZDOK.cntx_pop();
      _ok
   || _ok:=1
   ?};
   {? _ok
   || SLO.hdr_sel();
      SLO.hdr_sel('Słownik '+DOKUMENT.DOTYCZY().NAZ); 1
   || 0
   ?}
|| 0
?}


\dok_dlaa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: po redakcji pola dla - wybór kontrahenta
::       wypełnienie pola nazwa
::   WE:
::   WY:
::  OLD: \dok_dlaa/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOKUMENT.DLA<>0
|| DOKUMENT.DLA();
   DOKUMENT.NAZWA:=SLO.TR
?};
1


\dok_nazb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: nazwa kontrahenta w dokumencie - przed redakcją - formuła z
::   WE:
::   WY:
::  OLD: \dok_nazb/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
var_pres('dok1poz')<0


\bfw_dl2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: przed wyswietleniem pola dla - wybor kontrahenta 2
::       sprawdza czy pole ma byc dostepne do wyswietlenia
::  OLD: \bfw_dl2/ml_znaki.fml
::----------------------------------------------------------------------------------------------------------------------
1


\bfr_dl2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: przed redakcja pola dla - wybor kontrahenta 2
::       pole dostępne do modyfikacji tylko jesli nie minelo 7 dni od wystawienia i kontrahent jest jednorazowy
::   WY: 1 - ok
::       0 - nie mozna edytowac
::  OLD: \bfr_dl2/ml_znaki.fml
::----------------------------------------------------------------------------------------------------------------------
{? ($(KPARAM.CZY_NAZW().FORMULA))()=1 & DOKUMENT.FAK=''
:{? ($(KPARAM.CZY_NAZW().FORMULA))()=1 & DOKUMENT.FAK='' & DOKUMENT.DATA+7>=date
:{? 0 & DOKUMENT.FAK='' & DOKUMENT.DATA+7>=date
|| SLO.win_sel('SLO_FO'); SLO.win_dict('SLO_FO');  SLO.hdr_sel();
   SLO.hdr_sel(DOKUMENT.DOTYCZY().NAZ);
   1
|| 0
?}


\afr_dl2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: po redakcja pola dla - wybor kontrahenta 2
::       wypelnienie pola nazwa2
::  OLD: \afr_dl2/ml_znaki.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOKUMENT.DLA2<>0
|| DOKUMENT.DLA2();
   DOKUMENT.NAZWA2:=SLO.TR
?};
1


\stmin_bw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: przed wyswietleniem stanu minimalnego na raporcie
::  OLD: \stmin_bw/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
exec('flddisp','color','DOKUMENT#02')


\kstmin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS:
::  OLD: \kstmin/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? MLKAS.MIN=0
|| 'DOKUMENT#02#01'
|? MLKAS.MIN<={? STANKAS.WALUTA<>null || SALDO.WAL || SALDO.PLN ?}
|| 'DOKUMENT#02#02'
|| 'DOKUMENT#02#03'
?}


\dokument_win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zwraca okno redakcji tabeli DOKUMENT
::   WE: _a - typ
::       _b - 0-nie tworzyć okna, 1-tworzyć okno
::   WY: akronim okna redakcji
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
_mk_edit:={? var_press('_b')=type_of(0) || _b || 1 ?};

WAL.cntx_psh();
_st_wal:=STANKAS.WALUTA;
_st_waln:=STANKAS.WALUTA().SYM;
WAL.cntx_pop();

_wal:=_st_wal=null | (_st_wal<>null & _st_waln<>KINFO.NAROD().KOD);

_title:='Dokument kasowy'@;

_win_akr:='RED';

_win_red:=
   {? _mk_edit
   ||
      _interm:=exec('interm','#system');
      {? _interm
      || _win_red:=DOKUMENT.mk_edit(_title,,'dokument',,,'maximized')
      || _win_red:=DOKUMENT.mk_edit(_title,,'dokument')
      ?};
      DOKUMENT.win_ewin(_win_red,,_win_akr);
      _win_red
   || _win_akr
   ?};

_win_red


\dokument_win_edit_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Ustawia okno redakcji tabeli DOKUMENT, wymagane pola, okna słowników
::   WE: [_a] - 1-tryb Display 0-(domyślnie) nie
::       [_b] - typ
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_disp:={? var_pres('_a')=type_of(0) || _a || 0 ?};
 _typ:={? var_pres('_b')=type_of('') || _b || '' ?};

_win_red:=exec('dokument_win_edit','kasa_dokument',_typ);

{? ~_disp
|| _ff:="params_exec('dokument_pozycje_red','kasa_dokument')";
   DOKUMENT.win_ebtn(_win_red,'text='+'Poz&ycje'@+',btn_label_align=center,panel=bottom,align=begin',_ff);
   _ff:="params_exec('dokument_zakoncz_red','kasa_dokument')";
   exec('zakoncz','#window',DOKUMENT,_win_red,1,_ff,0, exec('help_red_zakoncz','#window','DBA'),
   exec('text_red_zakoncz','#window','D'));
   exec('ok_esc','#window',DOKUMENT,_win_red,1,,,,,exec('help_red_ok','#window','Z'),exec('text_red_ok','#window'),
   exec('help_red_esc','#window','A'))
?};
DOKUMENT.win_edit(_win_red);

exec('set_efld_opt','kasa_dokument')


\set_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zaznacza wymagalne pola w dokumencie kasowym
::   WE: _a - akronim okna redakcji
::       _b - tabela
::----------------------------------------------------------------------------------------------------------------------
_win_red:={? var_pres('_a')=type_of('') || _a || DOKUMENT.win_edit('?') ?};
_Tab:={? var_pres('_b')=type_of(DOKUMENT) || _b || DOKUMENT ?};

:: dotyczy
_enable:={? DOKUMENT.DOTYCZY<>0 || 'enable=1' || 'enable=0' ?};
_Tab.efld_opt(_win_red,_enable,DOKUMENT,'DOTYCZY');
_Tab.efld_opt(_win_red,_enable,DOKUMENT,'DLA');
_Tab.efld_opt(_win_red,_enable,DOKUMENT,'NAZWA');
_mark:={? _enable*'=1' || 'mark=1' || 'mark=0' ?};
_Tab.efld_opt(_win_red,_mark,DOKUMENT,'DLA');
:: waluta, kurs
_enable:={? STANKAS.WALUTA=null() | STANKAS.WALUTA<>exec('wal_nar','kasa_wspolne') || 'enable=1' || 'enable=0' ?};
_Tab.efld_opt(_win_red,_enable,DOKUMENT,'WALUTA');
_Tab.efld_opt(_win_red,_enable,DOKUMENT,'KW_WAL');
_Tab.efld_opt(_win_red,_enable,DOKUMENT,'KURS');
_mark:={? _enable*'=1' || 'mark=1' || 'mark=0' ?};
_Tab.efld_opt(_win_red,_mark,DOKUMENT,'WALUTA');

:: Kurs jest wymagany, gdy waluty są różne
{? DOKUMENT.WALUTA = exec('wal_nar','kasa_wspolne')
|| _Tab.efld_opt(_win_red,'mark=0',DOKUMENT,'KURS')
|| _Tab.efld_opt(_win_red,'mark=1',DOKUMENT,'KURS')
?}


\dok_lpbl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BL  [2006]
:: OPIS: Wyznaczenie lp dokumentu kasowego, blank pola DOKUMENT.LP (MG)
::   WY: _nLp - lp. - integer
::  OLD: \dok_lpbl/skid_plb.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt<>'wydruki'
|| DOKUMENT.cntx_psh();
   DOKUMENT.index('RAPORT');
   _nLp:={? DOKUMENT.last() || DOKUMENT.LP ?} + 1;
   DOKUMENT.cntx_pop()
|| _nLp:=0
?};
_nLp


\dokument_pozycje_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument kasowy - Pozycje - okno redagowania
::       params_get()   - ustawiane w o_dokpop w !kas_dok_ddok.fml
::                      - ustawiane w o_dokdol w !kas_dok_ddok.fml
::   WY: 'key:F2' - jeśli pola nagłówka dokumentu sprzedażyprawidłowo wypełnione
::       'edit:ACRONYM' - akronim niewłaściwie wypełnionego pola nagłówka dokumentu sprzedaży
::       '' - wartość domyślna jeśli main nie zmieni na powyższe
::----------------------------------------------------------------------------------------------------------------------
_btnRuleResult:='';

_fld:=params_exec('dokument_edit_finisher','kasa_dokument',DOKUMENT.ref());
{? _fld<>''
|| _btnRuleResult:='edit:'+_fld
|| exec('o_dokpoz','kasa_pozdok')
?};

_btnRuleResult


\dokument_zakoncz_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument kasowy - Zakończ - okno redagowania
::       params_get()   - ustawiane w o_dokpop w !kas_dok_ddok.fml
::                      - ustawiane w o_dokdol w !kas_dok_ddok.fml
::   WY: 'key:F2' - jeśli pola nagłówka dokumentu sprzedażyprawidłowo wypełnione
::       'edit:ACRONYM' - akronim niewłaściwie wypełnionego pola nagłówka dokumentu sprzedaży
::       '' - wartość domyślna jeśli main nie zmieni na powyższe
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().context.mp;

_btnRuleResult:='';

_fld:=params_exec('dokument_edit_finisher','kasa_dokument',DOKUMENT.ref());
{? _fld<>''
|| _btnRuleResult:='edit:'+_fld
|| {? exec('zakoncz','kasa_dokument')
   || _btnRuleResult:='key:F2';
      _mp.done()
   ?}
?};

_btnRuleResult


\dokument_edit_finisher
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Kod wykańczający po DOKUMENT.edit() w różnych kontekstach
::   WE: _a - DOKUMENT.ref()
::       params_get()   - ustawiane w o_dokpop w !kas_dok_ddok.fml
::                      - ustawiane w o_dokdol w !kas_dok_ddok.fml
::----------------------------------------------------------------------------------------------------------------------
:: Sprawdzenie poprawności wypełnienia pól nagłówka dokumentu sprzedaży
_fld:=exec('dok_chk','kasa_dokument');
{? _fld<>''
|| _fld
|| _dokument:=_a;
   _out:=params_get().context.out;
   _mp:=params_get().context.mp;
   _akcja:=params_get().context.akcja;
   _var_dokumentpop:=params_get().var_dokumentpop;

   {? _akcja='Dołącz' | _akcja='START_TODO' | _mp.pathProc()
   || {? _var_dokumentpop.PO_FIRST=1
      || {? ~_mp.pathArea('KAS_DOK') & ~_mp.pathArea('KAS_MUL') || DOKUMENT.prefix() ?};
         {? DOKUMENT.add()
         || ROZNE.BLOK_RAP:=DOKUMENT.r_lock(1,1,1);
            Popraw:=1
         ?}
      ?};
      _var_dokumentpop.PO_FIRST:=2
   |? _akcja='Popraw' | _mp.pathTodo()
   || DOKUMENT.put()
   ?};
   ''
?}


\dok_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AM] [2000]
:: OPIS: walidacja rekordu DOKUMENT
::   WY: ''-jesli OK lub akronim niewypelnionego pola
::  OLD: \dok_chk/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
exec('dok_kurb','kasa_dokument',0);
STANKAS.cntx_psh;
STANKAS.index('KOD');
STANKAS.prefix(APARAM.STANKAS);
{? STANKAS.first
||  {? -STANKAS.CZY_KTIP='t' ||  _a:=1; _b:=STANKAS.TAB_KURS().NB ||  _a:=0 ?}
|| _a:=0
?};
STANKAS.cntx_pop;
_spr:='';
{? ~exec('chk_lp','kasa_dokument')
|| FUN.info('Dokument z liczbą porządkową %1 już istnieje.'@[$DOKUMENT.LP]);
   _spr:='LP'
|? DOKUMENT.LP<=0
|| FUN.info('Wymagana jest liczba porządkowa większa od zera.'@);
   _spr:='LP'
?};
{? _spr='' & |SLOSLU.OPER=''
|| FUN.info('\nNależy wybrać operację.\n'
      '\nWprowadź kod lub skorzystaj ze słownika.'@);
   _spr:='OPER'
?};
{? _spr='' || _spr:={? DOKUMENT.KURS<=0 & STANKAS.WALUTA=0 || 'WALUTA' || '' ?} ?};
{? _spr='' & STANKAS.WALUTA=0 & ~exec('czy_euro','waluty',DOKUMENT.WALUTA().SYM,1) || _spr:='WALUTA' ?};
{? _spr='' & STANKAS.WALUTA=0 & ~exec('dokwalae','kasa_dokument') || _spr:='WALUTA' ?};
{? _spr=''
|| {? ~_a
   ||  {? OPER.DOTYCZY<>0
       || _spr:=__CHK.record(DOKUMENT,,'LP','DLA','WALUTA','KURS')
       || _spr:=__CHK.record(DOKUMENT,,'LP','WALUTA','KURS')
       ?}
   ||  {? OPER.DOTYCZY<>0
       || _spr:=__CHK.record(DOKUMENT,,'LP','DLA','WALUTA')
       || _spr:=__CHK.record(DOKUMENT,,'LP','WALUTA')
       ?};
       {? ~DOKUMENT.KURS & _spr=''
       || FUN.info('Bank\n%1\n'
            'nie posiada tabeli kursów\n'
            'lub nie ma kursu dla wybranej waluty.'@[_b]); _spr:='OPER'
       ?}
   ?}
?};
{? _spr='LP' || DOKUMENT.LP:=exec('dok_lpbl','kasa_dokument',1) ?};
_spr


\dok_buf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Kolory
::   WE:
::   WY: \dok_buf/dok_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
POZDOK.cntx_psh();
DOKUMENT.RAPORT();
POZDOK.index('DOK_ZAKS'); POZDOK.prefix(DOKUMENT.ref);
{? POZDOK.first() & POZDOK.ZAKS='T'
|| _zwrot:='DOKUMENT#01#01'
?};
POZDOK.cntx_pop();
{? var_pres('__multik')>0
|| exec('dost_dok1','kasa_dokument')
|| exec('actions_grayed','kasa_dokument')
?};
_zwrot


\dost_dok1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: ustawienie okien dla dokumentow
::   WE: [_a] =1 - RAPORT.r_lock; =0 - bez RAPORT.r_lock
::       [_b] = KUSERUPR.UPR
::  OLD: \dost_dok1/okna.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};

_upr:=
   {? USRCONST.STANKAS().KU
   || USRCONST.STANKAS().UPR
   || KUSERUPR.cntx_psh;
      KUSERUPR.index('KU_ST'); KUSERUPR.prefix(OPERATOR.USER,USRCONST.STANKAS().STANKAS);
      {? KUSERUPR.first || _upr:=KUSERUPR.UPR ?};
      KUSERUPR.cntx_pop;
      _upr
   ?};

_lock:=0;

DOKUMENT.win_sel('WER');

_act_d:='';
_act_p:='';
{? RAPORT.get() & (_a=0 | _a=1 & RAPORT.r_lock(1,1))
||
   {? (exec('rap_code','kasa_raport',RAPORT.STATUS)=0 | exec('rap_code','kasa_raport',RAPORT.STATUS)=1)
      & _upr='T' & USRCONST.STANKAS().POZDOK='T'
   ||
      {? DOKUMENT.OPER().CZY1_POZ<>'N' || _act_p:='dUPv:d' ?};
      1
   ||
      {? ~((exec('rap_code','kasa_raport',RAPORT.STATUS)=0 | exec('rap_code','kasa_raport',RAPORT.STATUS)=1)& _upr='T')
      ||
         _act_d:='AKsADUp:rDAW' ;
         _act_p:='dUPv:d'
      ?};
      _act_d:={? PAR_SKID.get(80)='N' || 'Z' || '' ?}+_act_d
   ?};
:   {? KPARAM.D='C' | DOKUMENT.OPER().ZNAKI<>'T' || _act_d:='F'+_act_d ?};
   RAPORT.cntx_psh;
   _act_d:=_act_d+{? RAPORT.first || '' || 'DI' ?};
   RAPORT.cntx_pop;
   _lock:=1
||
   _act_d:='AKsDPUZ:rDAW';
   RAPORT.cntx_psh;
   _act_d:=_act_d+{? RAPORT.first || '' || 'DI' ?};
   RAPORT.cntx_pop;
   _act_p:='AdUPv:dA';
   _lock:=0
?};
{? USRCONST.STANKAS().STANKAS().WALUTA<>null()
|| _act_d:='I'+_act_d
?};
DOKUMENT.actions('WER',_act_d,,1);
POZDOK.actions('WER',_act_p,,1);
exec('actions_grayed','kasa_dokument');
_lock


\zakoncz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Raport kasowy - Zakończ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Zakończyć rejestrację dokumentu kasowego?'@)
|| DOKUMENT.STAT_REJ:='Z';
   DOKUMENT.put()
?}


\o_kpkw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: wywolywana na akcje Kp/kw w okienku dokumentow
::  OLD: \o_kpkw/okna.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOKUMENT.DOK_NUM=0
|| exec('kp_kw','kasa_dokument');
   {? var_pres('g_druk')>0 & g_druk & DOKUMENT.OPER().WYDRUK='T' || exec('o_dokdru','kasa_dokument') ?}
|| {? DOKUMENT.sel_size()=0 || FUN.info('Dokument jest już wystawiony.'@) ?}
?}


\dokwysbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula poczatkowa dla grupowego wystawiania dokumentow KP/KW/INN
::  OLD: \dokwysbe/nr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
_pyt:=FUN.choice('Czy wystawić dokumenty KP/KW/INN?'@,1,'Tak'@,'Tak i &wydrukować'@);
{? _pyt<>0 ||
   licz_gr:=0;
   g_druk:=_pyt=2;
   {? g_druk || exec('bp_kpkw','kasa_dokument') ?}
?};
_pyt


\bp_kpkw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Przed akcja grupowa wydruk dla okien tabeli DOKUMENT
::  OLD: \bp_kpkw/okna.fml
::----------------------------------------------------------------------------------------------------------------------
{? (var_pres('g_druk')>0 & g_druk) | FUN.ask('Drukować zaznaczone dokumenty?'@)
|| {? var_pres('TT_DOK')>100 || obj_del(TT_DOK) ?};
   TT_DOK:=tab_tmp(1,'REF','INTEGER','');
   g_size:=DOKUMENT.sel_size();
   1
|| 0
?}


\dokwysae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula koncowa dla grupowego wystawiania dokumentow KP/KW/INN
::  OLD: \dokwysae/nr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
{? g_druk
|| FUN.info('Liczba zaznaczonych dokumentów KP/KW/INN: %1'
      '\nw tym wystawionych: %2'
      '\ni drukowanych: %3'@[$g_size,$licz_gr,$TT_DOK.size()]);
   exec('ap_kpkw','kasa_dokument')
|| FUN.info('Liczba wystawionych dokumentów KP/KW/INN: %1'@[$licz_gr])
?};
&licz_gr;
&g_druk;
1


\ap_kpkw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Po akcji grupowej wydruk dla okien tabeli DOKUMENT
::  OLD: \ap_kpkw/okna.fml
::----------------------------------------------------------------------------------------------------------------------
_kom:=~(var_pres('g_druk')>0 & g_druk);
{? TT_DOK.first()
|| {? _kom || FUN.info('Zaznaczonych dokumentów: %1\nw tym wystawionych: %2.'@[$g_size,$TT_DOK.size()]) ?};
   DOKUMENT.cntx_psh();
   rep_exec('kas_kpkw');
   DOKUMENT.cntx_pop()
|| {? _kom || FUN.info('Drukowanie wstrzymane.\nBrak wystawionych dokumentów wsród zaznaczonych.'@) ?}
?};
{? var_pres('TT_DOK')>100 || obj_del(TT_DOK) ?};
&g_size


\kp_kw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2011]
:: OPIS: Nadanie numeru dokumentu wedlug formuly w parametrach stanowiska
::       po nadaniu numeru - wydruk dokumentu
::   WE: [_a] - webserwis 0-nie(domyslnie) 1-tak
::  OLD: \kp_kw/nr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
_size:=DOKUMENT.sel_size(); _is_poz:=_vat:=0;
DOKUMENT.OPER();
POZDOK.cntx_psh();
POZDOK.index('DOKUMENT'); POZDOK.prefix(DOKUMENT.ref());
{? POZDOK.first()
|| _is_poz:=1;
   {? OPER.CZY_BLOK='T' & PAR_SKID.get(90)='T'
   || {!
      |?
         {? POZDOK.POZOPER().VAT='T' || _vat:=1 ?};
         ~_vat & POZDOK.next()
      !}
   ?}
?};
POZDOK.cntx_pop();
_is_zero:=DOKUMENT.KW_WAL$2=0 & DOKUMENT.KW_PLN$2=0;
{? ~_is_zero | _is_poz
|| _pyt:={? _size=0
         || FUN.ask(
            {? _is_zero
            || 'Czy wystawić %1 z zerową wartością dokumentu?'@[DOKUMENT.TYP_DOK]
            || 'Czy wystawić %1?'@[DOKUMENT.TYP_DOK]
            ?}
            )
         || 1
         ?};
   {? _a || _pyt:=1 ?};
   {? _pyt
   || USRCONST.STANKAS(); KUSERUPR.STANKAS();
      {? DOKUMENT.TYP_DOK='KW'
      || STANKAS.F_KW()
      |? DOKUMENT.TYP_DOK='KP'
      || STANKAS.F_KP()
      |? DOKUMENT.TYP_DOK='KWT'
      || STANKAS.F_KWT()
      || STANKAS.F_INNY()
      ?};
      WAR.clr_fld();
      DOKUMENT.DOK_NUM:=FL.run();
      DOKUMENT.USER:=OPERATOR.USER;
      {? DOKUMENT.DOK_NUM=0
      || FUN.info('Wyznaczenie numeru KP/KW nie powiodło się.\nNależy sprawdzić definicję numeracji.'@)
      ?};

      {? DOKUMENT.DOK_NUM
      ||
         {? DOKUMENT.put() & var_pres('licz_gr')>0 || licz_gr+=1 ?}
      ?};
      DOKUMENT.get();

::    wydruk kp/kw tylko jesli w operacja ma taki parametr
      {? OPER.WYDRUK='T' & _size=0 || exec('o_dokdru','kasa_dokument') ?}
   ?}
|| {? _size=0
   || FUN.info('Dokument bez pozycji z zerową wartością.\nNie można wystawić dokumentu %1.'@[DOKUMENT.TYP_DOK])
   ?}
?}


\o_dokdru
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: wywolywana na akcje wydruk w okienku dokumentow
::   WE:
::   WY:
::  OLD: \o_dokdru/okna.fml
::----------------------------------------------------------------------------------------------------------------------
_s:=DOKUMENT.sel_size();
{? DOKUMENT.DOK_NUM<>0
|| {? _s
   || TT_DOK.REF:=#DOKUMENT.ref(); TT_DOK.add()
   || {? KPARAM.CZY_B='T' || rep_exec('kas_kpkwb') || rep_exec('kas_kpkw') ?}
   ?}
|| {? _s=0 || FUN.info('\nNie można wydrukować dokumentu.\n\nNie został wystawiony dokument %1.'@[DOKUMENT.TYP_DOK]) ?}
?};
1


\o_srodki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Akcja Srodki wg walut dla tab. DOKUMENT
::  OLD: \o_srodki/okna.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('TT_S')>0 || &TT_S ?};
TT_S:=tab_tmp(1,'SYM','STRING[3]','Waluta',
               'NAZ','STRING[20]','',
               'BO','REAL','BO raportu',
               'PLUS','REAL','Przychód',
               'MIN','REAL','Rozchód',
               'SALDO','REAL','Saldo raportu',
               'SALDOPLN','REAL','Saldo raportu w '+KINFO.NAROD().KOD,
               'LP','INTEGER','');
WAL.prefix();
{? WAL.first() ||
   _i:=BO.PLN; _j:=BO.WAL; _k:=SUMA.PLN_PLUS; _l:=SUMA.WAL_PLUS;
   _m:=SUMA.PLN_MIN; _n:=SUMA.WAL_MIN; _o:=SALDO.PLN; _p:=SALDO.WAL;
   DOKUMENT.cntx_psh(); RAPORT.cntx_psh();
   DOKUMENT.index('WALUTA'); _bo:=_plus:=_min:=_saldo:=0;
   {! |?
      WAL.cntx_psh();
      exec('bo_rap','kasa_wspolne',WAL.SYM);
      WAL.cntx_pop();
      DOKUMENT.prefix(RAPORT.ref(),WAL.SYM);
      exec('dok_suma','kasa_wspolne',WAL.SYM);
      {? BO.WAL | SUMA.WAL_PLUS | SUMA.WAL_MIN ||
         TT_S.SYM:=WAL.SYM;
         TT_S.NAZ:=WAL.NAZ;
         TT_S.BO:=BO.WAL; _bo+=BO.PLN;
         TT_S.PLUS:=SUMA.WAL_PLUS; _plus+=SUMA.PLN_PLUS;
         TT_S.MIN:=SUMA.WAL_MIN; _min+=SUMA.PLN_MIN;
         TT_S.SALDO:=SALDO.WAL;
         _saldo+=TT_S.SALDOPLN:=SALDO.PLN;
         TT_S.add()
      ?};
      WAL.next()
   !};
   DOKUMENT.cntx_pop(); RAPORT.cntx_pop();
   BO.PLN:=_i; BO.WAL:=_j; SUMA.PLN_PLUS:=_k; SUMA.WAL_PLUS:=_l;
   SUMA.PLN_MIN:=_m; SUMA.WAL_MIN:=_n; SALDO.PLN:=_o; SALDO.WAL:=_p
?};
{? TT_S.first()
|| _lp:=0;
   {? TT_S.first() || {! |? TT_S.LP:=_lp; TT_S.put(); _lp+=1; TT_S.next() !} ?};
   TT_S.blank();
   TT_S.LP:=_lp;
   TT_S.NAZ:='SUMA w '+KINFO.NAROD().KOD;
   TT_S.BO:=_bo;
   TT_S.PLUS:=_plus;
   TT_S.MIN:=_min;
   TT_S.SALDO:=_saldo;
   TT_S.SALDOPLN:=_saldo;
   TT_S.add();
   _o:=TT_S.mk_sel('Dostępne środki: '@,,0,'tt_s_dost_sr');
   TT_S.win_fld(_o,,'SYM');
   TT_S.win_fld(_o,,'NAZ');
   TT_S.win_fld(_o,,'BO');
   TT_S.win_fld(_o,,'PLUS');
   TT_S.win_fld(_o,,'MIN');
   TT_S.win_fld(_o,,'SALDO');
   TT_S.win_fld(_o,,'SALDOPLN');
   TT_S.win_act(_o,0,'Rekord',,,,"{? TT_S.SYM='' || '' ?}");
   TT_S.win_sel(_o); TT_S.hdr_sel(STANKAS.NAZWA);
   _i:=TT_S.ndx_tmp(,,'LP',,0);
   TT_S.index(_i);
   TT_S.select()
|| FUN.info('Brak dostępnych środków pieniężnych.'@)
?};
&TT_S


\obca_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: ustawienie waluty obcej
::  OLD: \obca_p/wzorce.fml
::----------------------------------------------------------------------------------------------------------------------
SLOSLU.WAL_OP:='';
{? DOKUMENT.WALUTA
|| SLOSLU.WAL_OP:=DOKUMENT.WALUTA().SYM
?}


\find_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Akca Szukaj tabela DOKUMENT
::  OLD: \find_bl/okna.fml
::----------------------------------------------------------------------------------------------------------------------
SLOSLU.OPER:=SLOSLU.WAL_N:='';
1


\dokument_raport_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Przed redakcja DOKUMENT.RAPORT
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? DOKUMENT.RAPORT=null()
   & var_press('context',params_get())<>type_of(~~)
   & var_press('mp',params_get().context)<>type_of(~~)
|| {? params_get().context.mp.pathArea()=0
   || RAPORT.win_dict('WER');
      RAPORT.prefix();
      RAPORT.f_clear();
      RAPORT.f_set('NUM_RAP',,'RAPORT.STATUS=\':_a\' or RAPORT.STATUS=\':_b\''
         ,exec('rap_stat','kasa_raport',0)
         ,exec('rap_stat','kasa_raport',1));
      1
   || 0
   ?}
|| 0
?}


\dokument_raport_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Po redakcja DOKUMENT.RAPORT
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? fld() & DOKUMENT.LP=0
|| DOKUMENT.RAPORT();
   DOKUMENT.index('RAPORT');
   DOKUMENT.prefix(fld());
   DOKUMENT.blank();
   SLOSLU.OPER:=MLKAS.OPER().KOD;
   {? SLOSLU.OPER<>'' || exec('oper_aft','kasa_wspolne') ?}
?};
{? __CHK.record(DOKUMENT,,'RAPORT')=''
|| RAPORT.f_clear();
   1
|| 0
?}


\saldo_wal_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Przed wyświetleniem SALDO.WAL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
STANKAS.WALUTA<>null()


\suma_walplus_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Przed wyświetleniem SUMA.WAL_PLUS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
STANKAS.WALUTA<>null()


\saldo_walmin_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Przed wyświetleniem SUMA.WAL_MIN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
STANKAS.WALUTA<>null()


\chk_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Czy dokumenty maja prawidłowe - kolejne LP
::   WY: 1-tak 0-nie
::  OLD: \chk_kol/nr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMENT.cntx_psh();
{? DOKUMENT.first()
|| _lp:=_ok:=0;
   {! |?
      _lp+=1;
      _ok:=DOKUMENT.LP=_lp;
      _ok & DOKUMENT.next()
   !}
|| _ok:=1
?};
DOKUMENT.cntx_pop();
_ok


\gen_lp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Przenumerowuje dokumenty w raporcie
::  OLD: \gen_lp/nr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMENT.cntx_psh();
DOKUMENT.index('RAPORT'); DOKUMENT.prefix(RAPORT.ref());
{? DOKUMENT.first()
|| _lp:=0;
   do();
   {! |?
      _lp+=1;
      {? _lp<>DOKUMENT.LP ||
         DOKUMENT.LP:=_lp;
         DOKUMENT.put()
      ?};
      DOKUMENT.next()
   !};
   _lp:=end()
?};
DOKUMENT.cntx_pop();
_lp


\actions_grayed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Ustala akcje dla tabeli DOKUMENT
::   WE:
::   WY: Akcje do wygaszenia
::----------------------------------------------------------------------------------------------------------------------
_grayed:='';
{? DOKUMENT.STAT_REJ='Z'
   || _grayed:='ZPU'+_grayed;
      {? DOKUMENT.OPER().CZY1_POZ='T' || _grayed:='Y'+_grayed ?}
   || _grayed:='W'
?};
{? RAPORT.STATUS=exec('rap_stat','kasa_raport',2)
|| _grayed:='DPUZW'
?};
DOKUMENT.actions_grayed('WER',_grayed)


\ma6_num
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: wyswietla numeracje dokumentow kasowych
::  OLD: \ma6_num/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=tab_tmp(1
   ,'ROK'      ,'STRING[20]'  ,'Rok'
   ,'ROK_KOD'  ,'STRING[16]'  ,'$ROK_F.ref()'
   ,'SK'       ,'STRING[20]'  ,'Stanowisko kasowe'
   ,'SK_KOD'   ,'STRING[16]'  ,'$STANKAS.ref()');
_Tab.index(_Tab.ndx_tmp(,,'ROK',,1,'SK',,));

ROK_F.cntx_psh();
STANKAS.cntx_psh();
ROK_F.index('KOD');
ROK_F.prefix(REF.FIRMA);
_loop:=ROK_F.first();
{!
|? _loop
|!
   _loop:=STANKAS.first();
   {!
   |? _loop
   |!
      _Tab.ROK:=ROK_F.NAZ;
      _Tab.ROK_KOD:=ROK_F.KOD+2;
      _Tab.SK:=STANKAS.NAZWA;
      _Tab.SK_KOD:=form(STANKAS.KOD,-3);
      _loop:=_Tab.add() & STANKAS.next()
   !};
   _loop:=ROK_F.next()
!};
STANKAS.cntx_pop();
ROK_F.cntx_pop();

_wer:=_Tab.mk_sel('',,,'num_dok_zb_def',,,,,'U');
_Tab.win_fld(_wer,,'ROK',,,,,,'Rok');
_Tab.win_fld(_wer,,'SK',,,,,,'Stanowisko kasowe');

_wer_gr:=_Tab.grp_make('Numery dokumentów kasowych',,'num_dok_def');
_far:="
   _Tab:=cur_tab(1,1);
   {? {? _Tab.f_active() || _Tab.f_get() || _Tab.get() ?}
   || NUM_DOK.use('knd'+_Tab.SK_KOD+_Tab.ROK_KOD);
      NUM_DOK.actions('WER','',,1)
   || NUM_DOK.use('knd_____');
      NUM_DOK.actions('WER','D:D',,1)
   ?};
   grp_disp(NUM_DOK,'WER')
";
_Tab.grp_sel(_wer_gr,,_wer,,_far,,,,,,,,'maximized');
_Tab.grp_splt(_wer_gr,,'vertical','num_dok');
_Tab.grp_sel(_wer_gr,NUM_DOK,'WER',,,,,,,,,,'maximized');

_Tab.win_sel(_wer_gr);
_Tab.select()


\wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Raport kasowy - Wycofaj
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Wycofać zakończoną rejestrację dokumentu kasowego?'@)
|| DOKUMENT.STAT_REJ:='N';
   DOKUMENT.put()
?}


\rponumdok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Rekord po dla tabeli NUM_DOK
::  OLD: \rponumdok/nr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? NUM_DOK.TYP_DOK=''
|| FUN.info('Nie wprowadzono typu dokumentu.'@); _zwrot:='TYP_DOK'
?};
::Poczatek modyfikacji dla Maclex 21.04.2009 AK [1040]
{? _zwrot='' & ~(NUM_DOK.TYP_DOK='KP' | NUM_DOK.TYP_DOK='KW' | NUM_DOK.TYP_DOK='INN'| NUM_DOK.TYP_DOK='KWT')
|| FUN.info('Typ dokumentu może przyjmować jedną z czterech wartości:\n'
            '"KP", "KW", "KWT" lub "INN".'@); _zwrot:='TYP_DOK'
?};
::Koniec modyfikacji dla Maclex
{? _zwrot='' & NUM_DOK.OKRES=''
|| FUN.info('Nie wprowadzono okresu.'@); _zwrot:='OKRES'
?};
{? _zwrot='' & NUM_DOK.NUMER<0
|| FUN.info('Numer dokumentu musi być większy od zera.'@); _zwrot:='NUMER'
?};
_zwrot


\chk_lp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Czy wartosc DOKUMENT.LP jest dozwolona ?
::   WY: 1-tak 0-nie
::  OLD: \chk_lp/nr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
_lp:=DOKUMENT.LP;
_ref:=DOKUMENT.ref();
DOKUMENT.cntx_psh();
DOKUMENT.index('RAPORT'); DOKUMENT.prefix(DOKUMENT.RAPORT,_lp);
_ok:=~DOKUMENT.first() | ((-menu_txt()='popraw' | Popraw) & DOKUMENT.ref()=_ref);
DOKUMENT.cntx_pop();
_ok

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 91cb376317fcb8b7569ec1f25874c903dc829051c718c327ff55a3f190c469b57abc3f33fdc9d4706092b79524f50f6a2ca7d59141a5c1746e147bae76eaaeec1adcb8de9ce097c238ed1251260d6d6969b9dc65640f9fc83cce936ce6159df583f08b5110f3bc091bf2a9741736be147a4de5ce5bb5c64b8d609ed7095811da

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: bloby.fml
:: Utworzony: 11.12.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa blobów systemowych
::======================================================================================================================


\mk_ctr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Utworzenie okienka z kontrolka
::   WE: _a - identyfikator dla okienka
::       _b - sposob wyszukiwania kontrolki
::       _c - szerokość, domyślnie 125
::   WY: _win - akronim utworzonego okienka z kontrolka
::  OLD: \mk_ctr/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<>type_of(0)
|| _c:=125
?};
_w_ident:=_a;
_btab:=($(__CTR.BTAB))();
_win:=_btab.mk_ctr('Załączniki'@,_w_ident,1,15,_c,10);
__win:=-(1-_win);
__CTR.WIN_ID:=_b+__win;
_btab.win_cctr(_win,'ctr_id', '@filelistpanel', 'dnd');
_form:=$('exec(\'callb\',\'bloby\',\''+__CTR.WIN_ID+'\')');
_btab.win_cfml(_win,'ctr_id','CONTROL_CALLBACK',_form);
_form:=$('exec(\'dsk_ini\',\'bloby\',\''+__CTR.WIN_ID+'\')');
_btab.win_cfml(_win,'ctr_id','CONTROL_INIT',_form);
return(_win)


\mk_ctr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Utworzenie okienka z kontrolka
::   WE: _a - identyfikator dla okienka
::       _b - sposob wyszukiwania kontrolki
::       _c - szerokość, domyślnie 125
::   WY: _win - akronim utworzonego okienka z kontrolka
::  OLD: \mk_ctr/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<>type_of(0)
|| _c:=125
?};
_w_ident:=_a;
_btab:=($(__CTR.BTAB))();
_win:=_btab.mk_ctr('Załączniki dokumentów'@,_w_ident,1,15,_c,10);
__win:=-(1-_win);
__CTR.WIN_ID:=_b+__win;
_btab.win_cctr(_win,'ctr_id', '@filelistpanel', 'dnd');
_form:=$('exec(\'callb\',\'bloby\',\''+__CTR.WIN_ID+'\')');
_btab.win_cfml(_win,'ctr_id','CONTROL_CALLBACK',_form);
_form:=$('exec(\'dsk_ini\',\'bloby\',\''+__CTR.WIN_ID+'\')');
_btab.win_cfml(_win,'ctr_id','CONTROL_INIT',_form);
return(_win)


\callb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Obsluga akcji w kontrolce
::   WE: _a - identyfikator kontrolki
::  OLD: \callb/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_win_id:=_a;
__CTR.WIN_ID:=_win_id;
__select:=0;
{? ctr_info('action_id') = 'dnd' & __CTR.DND
|| {? exec('blob_add','bloby',2)
   || exec('getblobs','bloby',_win_id)
   ?}
|? ctr_info('action_id') = 'add'
|| {? exec('blob_add','bloby',1)
   || exec('getblobs','bloby',_win_id)
   ?}
|? ctr_info('action_id') = 'del'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   {? exec('no_edit','bloby',_tab1) || return(0) ?};
   {? _tab1.size()=1
   || _czy:=(__CTR.BTAB='EDOKUMZ' | exec('dokumz_moz_usun','dokum_zal',_tab1.INT_DATA) )
            & FUN.ask('Czy usunąć zaznaczony załącznik?'@)
   || _czy:=FUN.ask('Czy usunąć zaznaczone załączniki?'@)
   ?};
   {? _czy
   || exec('dellist','bloby',_tab1);
      exec('getblobs','bloby',_win_id)
   ?}
|? ctr_info('action_id') = 'save'
|| {? (_dir:=exec('get_dir','bloby'))<>'' | exec('interm','#system')
   || _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
      exec('savelist','bloby',_tab1, _dir);
      exec('getblobs','bloby',_win_id)
   ?}
|? ctr_info('action_id') = 'view'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('view','bloby',_tab1)
|? ctr_info('action_id') = 'copy'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('copy','bloby',_tab1)
|? ctr_info('action_id') = 'scan'
||
   {? exec('cli_functions','#system')=0
   || FUN.emsg(exec('indevice_nacc_msg','#system'));
      return(0)
   ?};
   _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('scan','bloby',_tab1);
   exec('getblobs','bloby',_win_id)
|? ctr_info('action_id') = 'put'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   {? exec('no_edit','bloby',_tab1) || return(0) ?};
   exec('blob_put','bloby',_tab1);
   exec('getblobs','bloby',_win_id)
|? ctr_info('action_id') = 'edit'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   {? exec('no_edit','bloby',_tab1) || return(0) ?};
   exec('edit','bloby',_tab1);
   exec('getblobs','bloby',_win_id)
|? ctr_info('action_id') = 'disp'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('disp','bloby',_tab1)
|? ctr_info('action_id') = 'print'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('print','bloby',_tab1)
|? ctr_info('action_id') = 'setview'
|| {? ctr_call(_win_id,'ctr_id','getViewName')='icons'
   || _view:='table'
   || _view:='icons'
   ?};
   ctr_call(_win_id,'ctr_id','setView',_view)
|? ctr_info('action_id') = 'sign'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('sign','bloby',_tab1)
|? ctr_info('action_id') = 'signv'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('sign_show','bloby',_tab1)
?};
{? __select
|| exec('selectitem','bloby',_tab1)
?}


\blob_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja Dodaj dla Zalacznika
::   WE: _a - flaga wyboru plikow
::       _a=0 - akcja Skanuj
::       _a=1 - akcja Dodaj
::       _a=2 - akcja DnD
::  OLD: \blob_add/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
{? __CTR.BTAB='DOKUMZ' || DOKUMZ.blank() ?};
_btab:=($(__CTR.BTAB))();
_btab.KR_OP:='';
_ocr:=var_pres('P_OCR',_btab)>0 & _btab=DOKUMZ & DOKUM.RODZAJ_K='P';
{? _ocr || _btab.P_OCR:='N' ?};
_btab.cntx_psh();
_edit:=_btab.mk_edit('Załączniki'@,,'btab',1,1);
_btab.win_efld(_edit,,'KR_OP',,,80,,,'Krótki opis'@);
{? __CTR.BTAB='EDOKUMZ'
|| EDIT_VAR.SLO_TYP:='ZAL';
   _btab.TYP_ZAL:=null;
   OBIEGI.TYP_ZAL:='';
   {? EDOKUM.PAPERLES
   || _btab.win_efld(_edit,OBIEGI,'TYP_ZAL',,,77,,,'Typ załącznika'@,,,,'F3_button=1');
      _btab.efld_opt(_edit,'mark=1',OBIEGI,'TYP_ZAL')
   ?}
?};
{? _ocr
|| _btab.win_efld(_edit,,'P_OCR',,,,,,'Dokument do OCRa'@,,,'check-box',,"'T'","'N'")
?};
_ok:=_btab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&OK'@],"'key:F2'");
_anuluj:=_btab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
_btab.btn_eopt(_edit,_ok,'tooltip='+exec('help_red_ok','#window','P'));
_btab.btn_eopt(_edit,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
_btab.win_edit(_edit);
_add:=0;
{? _btab.edit("exec('blob_ar','bloby','add')")
|| VAR_DEL.delete('__OCR_EXT_CHK_INFO');
   _typ_zal:={? var_pres('TYP_ZAL',_btab)>0 || _btab.TYP_ZAL || null ?};
   _kr_op:=_btab.KR_OP;
   _p_ocr:={? _ocr || _btab.P_OCR || ~~ ?};
   {? _a=0
   || _tab0:=ctr_call(__CTR.WIN_ID,'ctr_id','getDroppedFiles');
      _tab0.add();
      {? _ocr & _p_ocr='T'
      || {? _tab0.size()>1 | ~exec('ocr_il_chk','bloby')
         || _p_ocr:='N';
            exec('ocr_il_chk_info','bloby')
         ?}
      ?};
      do();
      _obj:=exec('addblobs','bloby',_tab0,_kr_op,_p_ocr,_typ_zal,1);
      {? exec('a_skanuj','dok_fks',_btab,__CTR.BBLOB)=0
      || undo()
      ?};
      _add:=1;
      end()
   |? _a=1
   || _tmp_dir:=fmk_tmp_dir(0);
      {? type_of(_tmp_dir)<>type_of(~~)
      || _pth:=_tmp_dir.get_path();
         {? _pth<>''
         || _tab0:=exec('files_upload','bloby',_pth);
            {? _tab0.size()>0
            || {? _ocr & _p_ocr='T'
               || {? _tab0.size()>1 | ~exec('ocr_il_chk','bloby')
                  || _p_ocr:='N';
                     exec('ocr_il_chk_info','bloby')
                  ?}
               ?};
               do();
               _obj:=exec('addblobs','bloby',_tab0,_kr_op,_p_ocr,_typ_zal);
               _add:=1;
               end()
            ?}
         ?}
      ?}
   |? _a=2
   || _tab0:=ctr_call(__CTR.WIN_ID,'ctr_id','getDroppedFiles');
      {? _ocr & _p_ocr='T'
      || {? _tab0.size()>1 | ~exec('ocr_il_chk','bloby')
         || _p_ocr:='N';
            exec('ocr_il_chk_info','bloby')
         ?}
      ?};
      do();
      _obj:=exec('addblobs','bloby',_tab0,_kr_op,_p_ocr,_typ_zal,1);
      _add:=1;
      end()
   ?};
   {? var_pres('__OCR_EXT_CHK_INFO')=type_of(0) & __OCR_EXT_CHK_INFO
   || exec('ocr_ext_chk_info','bloby')
   ?}
?};
_btab.cntx_pop();
VAR_DEL.delete('__OCR_EXT_CHK_INFO');
{? var_pres('_obj')>0
|| {? _typ_zal
   || exec('EDOKUMZS_upd','portal_paperless',_obj)
   ?};
   {? _obj.no_pdf
   || FUN.info('Nie dodano załączników innych niż PDF.')
   ?}
?};
return(_add)


\addblobs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Dodanie nowych zalacznikow
::   WE: _a - tabela z wybranymi plikami
::       _b - opis dla pola OPIS tabeli z zalacznikami
::       _c - znacznik, czy dokument do OCR
::       _d - typ załacznika (_SLO_NAZ)
::       _e - dropped (pliki z 'getDroppedFiles')
::   WY: Zwraca obiekt z tabelą z dodanymi załacznikami oraz czy były sód nich pliki PDF
::  OLD: \addblobs/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_blobs:=_a;
_opis:=_b;
{? var_press('_c')<=0 || _docr:='N' || _docr:=_c ?};
_typ_zal:={? var_pres('_d')>0 & type_of(_d)=type_of(null) || _d || null ?};
_dropped:={? var_pres('_e')=type_of(0)|| _e || 0 ?};
_obj:=exec('obj_add_res','bloby');
{? ~_blobs.first() || return(_obj) ?};
_dir0:={? _dropped & ~ exec('interm','#system') || '@' || '' ?};
{! |?
   {? __CTR.BTAB='EDOKUMZ'
   || _pdf:=-(_blobs.FILENAME+4)='.pdf';
      {? _pdf | _typ_zal=null
      || EDOKUMZ.blank(1);
         {? __CTR.TAB='EDOK_ZAL'
         || EDOKUMZ.EDOK_ZAL:=EDOK_ZAL.ref()
         || EDOKUMZ.DOKUM:=EDOKUM.ref()
         ?};
         EDOKUMZ.KR_OP:=_opis;
         EDOKUMZ.DATE:=date();
         EDOKUMZ.TIME:=time();
         EDOKUMZ.B_PREL:=OBIEGI.B_PREL;
         EDOKUMZ.B_PRELS:=OBIEGI.B_PRELS;
         EDOKUMZ.USER:=exec('name','users');
         EDOKUMZ.TYP_ZAL:=_typ_zal;
         {? EDOKUMZ.add()
         || _file:=_blobs.FILENAME;
            {? _file<>''
            || EDOKUMZ.bl_put('EDOKUM',_dir0+_blobs.DIR+ __CTR.DIR_SEP +_blobs.FILENAME,,,,);
               EDOKUMZ.NAZWA:=EDOKUMZ.bl_info('EDOKUM','NAME');
               EDOKUMZ.put()
            ?};
            _obj.add(EDOKUMZ.ref())
         ?}
      || _obj.no_pdf:=1
      ?}
   |? __CTR.BTAB='DOKUMZ'
   || exec('add_dokumz','bloby',_opis,_blobs.FILENAME,_dir0+_blobs.DIR+ __CTR.DIR_SEP +_blobs.FILENAME,_docr)
   ?};
   _blobs.next()
!};
_obj


\add_dokumz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje załącznik DOKUMZ
::   WE: _a - opis
::       _b - nazwa pliku
::       _c - ścieżka do pliku
::       _d - znacznik czy dokument do OCR
::----------------------------------------------------------------------------------------------------------------------
{? _>3 || _docr:=_d || _docr:='N' ?};
{? (+_b>64) || msg('Za długa nazwa pliku'); return(0) ?};
DOKUMZ.blank(1);
DOKUMZ.DOK:=DOKUM.ref();
{? exec('upgrade2325_blbc1','zbl')
|| DOKUMZ.REFSQL:=DOKUM.REFSQL
?};
DOKUMZ.KR_OP:=_a;
DOKUMZ.DATE:=date();
DOKUMZ.TIME:=time();
DOKUMZ.P_OCR:=_docr;
DOKUMZ.bl_file('DOKUM',1);
{? DOKUMZ.add()
||  _file:=_b;
   {? _file<>''
   ||
      DOKUMZ.bl_put('DOKUM',_c,,,_file);
      DOKUMZ.NAZWA:=DOKUMZ.bl_info('DOKUM','NAME');
      {? ~exec('ocr_ext_chk','bloby') & do_state()
      || VAR_DEL.delete('__OCR_EXT_CHK_INFO');
         __OCR_EXT_CHK_INFO:=1
      ?};
      DOKUMZ.put()
   ?};
   DOKUM.get();
   {? DOKUM.ZAL<>'T'
   || DOKUM.ZAL:='T';
      DOKUM.put(1)
   ?}
?}


\disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja Wlasciwosci dla Zalacznika
::   WE: _a - tabela z rekordem do poprawienia
::  OLD: \disp/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   || {? __CTR.BTAB='EDOKUMZ'
      || _disp:=_btab.mk_edit('Załącznik'@,,'dbtab',1,1);
         _btab.win_efld(_disp,,'DATE');
         _btab.win_efld(_disp,,'TIME');
         _btab.win_efld(_disp,,'USERS','KOD',,,,,'Użytkownik'@);
         _btab.win_efld(_disp,,'B_PRELS',,,,,,'Czynność'@);
         _btab.win_efld(_disp,,'KR_OP',,,80);
         _btab.win_efld(_disp,,'TYP_ZAL','NAZWA',,77,,,'Typ załącznika'@);
         _btab.win_efld(_disp,,'EDOKUM',,,80);
         _btab.win_edit(_disp);
         _btab.display()
      |? __CTR.BTAB='DOKUMZ'
      || _disp:=_btab.mk_edit('Załącznik'@,,'dzbtab',1,1);
         _btab.win_efld(_disp,,'DATE');
         _btab.win_efld(_disp,,'TIME');
         _btab.win_efld(_disp,,'KR_OP',,,80);
         _btab.win_efld(_disp,,'DOKUM',,,80);
         _btab.win_efld(_disp,,'BL_GUID',,,80);
         _btab.win_efld(_disp,,'P_OCR');
         _btab.win_efld(_disp,,'BL_TYPE');
         _btab.win_edit(_disp);
         _btab.display()
      ?};
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      exec('getblobs','bloby',__CTR.WIN_ID);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\print
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [12.51_148]
:: OPIS: Akcja Drukuj dla Zalacznika
::   WE: _a - tabela z rekordem do poprawienia
::  OLD: \disp/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   || {? __CTR.BTAB='DOKUMZ' | __CTR.BTAB='EDOKUMZ' | __CTR.BTAB='DOKUM'
      || exec('print','dok_ksef',__CTR.BTAB)
      ?};
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      exec('getblobs','bloby',__CTR.WIN_ID);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\getblobs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Pobranie zalacznikow
::   WE: _a - identyfikator kontrolki
::       _b - czy wczytywać rekord bazowy (zadeklarowane=nie)
::  OLD: \getblobs/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=2 || _nwt:=1 || _nwt:=0 ?};
_tab:=exec('files_data','bloby',_nwt);
ctr_tab('files_data', _a, 'ctr_id', '', _tab);
ctr_call(_a,'ctr_id','updateItems', 'files_data')


\files_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Wypelnienie tabeli z multizalacznikami dla tabeli EDOKUMZ
::   WY: _zal - tabela tymczasowa przekazywana do kontrolki
::  OLD: \files_data/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_zal:=exec('tab_tmp','bloby');
_zal.blank();
_tab:=($(__CTR.TAB))();
_btab:=($(__CTR.BTAB))();
{? _tab=DOKUM & _btab=DOKUMZ & DOKUM.name()<>'dokum'
|| _fget:=0
|? _ & _a
|| _fget:=1
|| _fget:=_tab.get();
   _f_act:={? _fget & var_pres('RedZal')<=0 || _tab.f_active() ?};
   {? _f_act>=2
   || _fget:=_tab.f_test()
   |? _f_act>0
   || _fget:=_tab.f_get()
   ?}
?};
{? _fget
||
   ctr_set(__CTR.WIN_ID,'ctr_id','setEnabled',1);
   {? __CTR.TAB='EDOK_ZAL'
   || _btab.index('WYDATEK')
   || _btab.index('DISP')
   ?};
   _btab.prefix(_tab.ref())
||
   ctr_set(__CTR.WIN_ID,'ctr_id','setEnabled',0);
   {? __CTR.BTAB='EDOKUMZ'
   ||
      _btab.index('NULL'); _btab.prefix(null,null)
   ||
      _btab.prefix(-1)
:: nie _btab.prefix(null()), ponieważ mogą istnieć DOKUMZ.DOK=null()
   ?}
?};
{? _btab.first()
||
   {! |?
      {? ($(__CTR.BTAB+'.'+__CTR.BBLOB))() || _size:=_btab.bl_info(__CTR.BBLOB,'SIZE') || _size:=0 ?};
      {? _btab.NAZWA='' || _btab.NAZWA:='nofile' ?};
      _zal.NAME:=_btab.NAZWA;
      _zal.SIZE:=_size;
      {? __CTR.BTAB='DOKUMZ'
      || {? _btab.P_OCR='T'
         || _zal.P_OCR:='Tak'
         |? _btab.P_OCR='N'
         || _zal.P_OCR:='Nie' ?};
         {? _btab.W_OCR='T'
         || _zal.W_OCR:='Tak'
         |? _btab.W_OCR='N'
         || _zal.W_OCR:='Nie'
         |? _btab.W_OCR='P'
         || _zal.W_OCR:='Powielony'
         ?}
      ?};
      _zal.MOD_DATE:=((_btab.DATE) $ 1 )+' '+ (_btab.TIME) $ 1;
      _zal.INT_DATA:=#_btab.ref();
      {? __CTR.BTAB='EDOKUMZ'
      || _zal.B_PRELS:=_btab.B_PRELS;
         _zal.TYP_ZAL:=_btab.TYP_ZAL().NAZWA
      ?};
      _zal.KR_OP:=_btab.KR_OP;
      _zal.add();
      _btab.next()
   !}
?};
return(_zal)


\tab_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: struktura tabeli tymczasowej dla zalacznikow
::   WY: _zal - tabela tymczasowa przekazywana do kontrolki
::  OLD: \tab_tmp/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
{? __CTR.BTAB='EDOKUMZ'
|| _zal:=tab_tmp(1,
      'NAME', 'STRING[255]', 'Nazwa' ,
      'SIZE', 'INTEGER', 'Rozmiar',
      'MOD_DATE', 'STRING[30]','Data modyfikacji',
      'INT_DATA', 'INTEGER',,
      'STR_DATA','STRING[10]',,
      'TYP_ZAL','STRING[150]','Typ załącznika',
      'USER','STRING[30]','Użytkownik',
      'B_PRELS','STRING[50]','Czynność',
      'KR_OP','STRING[200]','Opis'
   )
|? __CTR.BTAB='DOKUMZ' | __CTR.BTAB='DOKUMZA'
|| _zal:=tab_tmp(1,
      'NAME', 'STRING[255]', 'Nazwa' ,
      'SIZE', 'INTEGER', 'Rozmiar',
      'MOD_DATE', 'STRING[30]','Data modyfikacji',
      'INT_DATA', 'INTEGER',,
      'STR_DATA','STRING[10]',,
      'KR_OP','STRING[200]','Opis',
      'P_OCR','STRING[3]','Dokument do OCR',
      'W_OCR','STRING[10]','Wysłany do OCR'
   )
?};
_zal


\selectitem
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: zaznaczenie elementu po akcji w kontrolce
::   WE: _a - tabela z rekordem do poprawienia
::  OLD: \selectitem/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
{? _tab.first()
|| ctr_set(__CTR.WIN_ID,'ctr_id','selectItem',_tab.INT_DATA, '', 1)
?}


\edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja Redaguj dla Zalacznika
::   WE: _a - tabela z rekordem do poprawienia
::  OLD: \edit/skid_blb.fml
:: ~OST: INBLEDIT
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   || {? exec('cli_functions','#system')=0
      || FUN.emsg(exec('indevice_nacc_msg','#system'))
      || {? _btab.r_lock(1)
         || _btab.bl_edit(__CTR.BBLOB);
            _btab.r_unlock()
         ?}
      ?};
      __select:=1
   || FUN.info('Załącznik został usunięty.'@);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\no_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Test czy mozna poprawiac, usuwac lub redagowac zalacznik
::   WE: _a - tabela z rekordem/rekordami do obslugi
::   WY: 1 - zalacznik nieedytowalny, 0 - mozna edytowac itp.
::  OLD: \no_edit/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;

_wy:=0;

_btab:=($__CTR.BTAB)();
_btab.cntx_psh();
_btab.clear();
_size:=_tab.size();
{? _tab.first()
|| {!
   |? {? _btab.seek(_tab.INT_DATA,)
      || {? _btab=EDOKUMZ
         || {? EDOKUMZ.B_PREL='#EDOK#'
            || {? _size=1
               || FUN.info('Załącznik jest kopią e-dokumentu.\nAkcji nie można wykonać.'@)
               || FUN.info('Jeden z zaznaczonych załączników jest kopią e-dokumentu.\nAkcji nie można wykonać.'@)
               ?};
               _wy:=1
            |? EDOKUMZ.B_PREL<>EDOKUM.B_PREL
            || {? _size=1
               || FUN.info('Załącznik dodano na innym etapie obiegu.\nAkcji nie można wykonać.'@)
               || FUN.info('Jeden z zaznaczonych załączników dodano na innym etapie obiegu.\nAkcji nie można wykonać.'@)
               ?};
               _wy:=1
            ?}
         |? _btab=DOKUMZ & DOKUMZ.BL_GUID<>''
         || {? _size=1
            || FUN.info('Załącznik jest powiązany z Businesslink.\nAkcji nie można wykonać.'@)
            || FUN.info('Jeden z zaznaczonych załączników jest powiązany z Businesslink.\nAkcji nie można wykonać.'@)
            ?};
            _wy:=1
         ?}
      ?};
      _wy=0 & _tab.next()
   !}
?};
_btab.cntx_pop();

_wy


\blob_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja Popraw dla Zalacznika
::   WE: _a - tabela z rekordem do poprawienia
::  OLD: \blob_put/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   || {? _btab.r_lock(1)
      || exec('ded_pop','bloby');
         _btab.r_unlock()
      ?};
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\ded_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Zalaczniki dla EDOKUM - EDOKUMZ - popraw
::  OLD: \ded_pop/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_btab:=($(__CTR.BTAB))();
_btab.bl_file(__CTR.BBLOB,1);
{? _btab=DOKUMZ
|| _edit:=_btab.mk_edit('Załączniki'@,,'btab',1,1);
   _btab.win_efld(_edit,,'KR_OP',,,80,,,'Krótki opis'@);
   {? DOKUM.RODZAJ_K='P'
   || {? DOKUMZ.W_OCR='T' | DOKUMZ.W_OCR='P'|| _widoczny:=1 || _widoczny:=0 ?};
      _btab.win_efld(_edit,,'P_OCR',,,,,_widoczny,'Dokument do OCR'@,,,'check-box',,"'T'","'N'");
      {? exec('lic','#b_domain','ZSO') || _btab.win_efld(_edit,,'W_OCR',,,,,1,'Wysłany do OCR'@,,,'radio-buttons',,'Tak',"'T'",'Nie',"'N'",'Powielony',"'P'") ?}
   ?};
   _ok:=_btab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&OK'@],"'key:F2'");
   _anuluj:=_btab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
   _btab.btn_eopt(_edit,_ok,'tooltip='+exec('help_red_ok','#window','P'));
   _btab.btn_eopt(_edit,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   _btab.fld_fml('P_OCR','AFTER_EDIT',"
      {? ~exec('ocr_ext_chk','bloby')
      || DOKUMZ.put()
      ?};
      1
   ");
   _btab.win_edit(_edit)
|? _btab=EDOKUMZ & EDOKUM.TYP().W_PORTAL='P'
|| OBIEGI.TYP_ZAL:=EDOKUMZ.TYP_ZAL().NAZWA;
   _btab.win_edit('RED_P')
|| _btab.win_edit('RED')
?};
{? _btab=DOKUMZ
|| _chk:="_wyn:=exec('blob_ar','bloby','put');
          {? _wyn='' & DOKUMZ.P_OCR='T' & ~exec('ocr_il_chk','bloby',1)
          || _wyn:='P_OCR';
             exec('ocr_il_chk_info','bloby')
          ?};
          _wyn"
|| _chk:="exec('blob_ar','bloby','put')"
?};
{? _btab.edit(_chk)
|| _file:=_btab.bl_file(__CTR.BBLOB);
   {? _btab.put()
   ||
      {? _file<>''
      || _btab.bl_put(__CTR.BBLOB,_file);
         _btab.NAZWA:=_btab.bl_info(__CTR.BBLOB,'NAME')
      ?};
      _btab.put()
   ?}
?};
{? var_pres('P_OCR',_btab)>0
|| _btab.fld_fml('P_OCR','AFTER_EDIT',"*")
?};
_btab.bl_file(__CTR.BBLOB,1)


\scan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja skanowania dokumentu
::   WE: _a - tabela z rekordem do obslugi
::  OLD: \scan/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {! |?
      {? _btab.seek(_tab.INT_DATA,)
      || {? _btab.r_lock(1)
         || {? exec('b_skanuj','dok_fks')
            || exec('a_skanuj','dok_fks',_btab,__CTR.BBLOB)
            ?};
            _btab.r_unlock()
         ?};
         __select:=1
      || FUN.emsg('Załącznik został usunięty.'@); __select:=0
      ?};
      _tab.next()
   !}
|| exec('blob_add','bloby',0);
   __select:=1
?};
_btab.cntx_pop()


\copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Tworzenie kopii zalacznika
::   WE: _a - tabela z rekordem do obslugi
::  OLD: \copy/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($__CTR.BTAB)();
{? _btab=EDOKUMZ & EDOKUM.PAPERLES
|| FUN.info('Akcja niedostępna dla wniosków typu paperless.');
   return(0)
?};
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   ||
      {? _btab.r_lock(1)
      ||
         _btab.cntx_psh();
         _ref:=#_btab.ref();
         _file:=fopen(($(__CTR.BTAB+'.'+__CTR.BBLOB))(), 'b', 0, 1, 1, 0);
         {? ~_file.is_open
         || FUN.emsg('Błąd otwarcia pliku'@)
         ||
            _edit:=_btab.mk_edit('Załącznik'@,,'btab',1,1);
            _btab.win_efld(_edit,,'KR_OP',,,80,,,'Krótki opis'@);
            _btab.win_efld(_edit,,'NAZWA',,,80,,,'Nowa nazwa załącznika'@);
            _ok:=_btab.win_ebtn(_edit,'text=%1'['OK'@],'key:F2');
            _anuluj:=_btab.win_ebtn(_edit,'text=%1'['Anuluj'@],'key:Esc');
            _btab.btn_eopt(_edit,_ok,'tooltip='+exec('help_red_ok','#window','P'));
            _btab.btn_eopt(_edit,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
            _btab.win_edit(_edit);
            {? _btab.edit("{? exec('chk_blob','bloby',cur_tab(1,1).NAZWA)=0 || 'NAZWA' || 1 ?}")
            ||
               do();
               _btab.DATE:=date();
               _btab.TIME:=time();
               {? _btab=DOKUMZ || _btab.BL_GUID:='' ?};
               {? _btab.add()
               || _btab.bl_put(__CTR.BBLOB,_file,,,_btab.NAZWA);
                  _ref:=#_btab.ref();
                  __select:=1
               ?};
               end()
            ?}
         ?};
         &_file;
         _btab.cntx_pop();
         _btab.r_unlock();
         {? __select
         || exec('getblobs','bloby',__CTR.WIN_ID);
            ctr_set(__CTR.WIN_ID,'ctr_id','selectItem',_ref, '', 1)
         ?};
         __select:=0
      ?}
   || FUN.emsg('Załącznik został usunięty.'@);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\chk_blob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Sprawdzenie poprawnosci nazwy zalacznika
::   WE: _a - nazwa zalacznika
::   WY: _err - znacznik poprawnosci nazwy
::  OLD: \chk_blob/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_name:=_a;
_ax:=0;
_zx:=0;
_err:=0;
_l:=+_name;
{? _name=''
|| _ax:=1
|? _l>64
|| _ax:=3
|| _cx:=_name+1;
   {? _cx=' '
   || _ax:=2
   ||
      {! _i:=1.._l |? _ax=0 |!
        _cx:=(_i+_name)+1;
        {? ('><\\/*"?:|\t'*_cx>0)
        || _ax:=2
        ?}
      !}
   ?}
?};
{? _ax=1
|| undo();
   FUN.info('Niedozwolona pusta nazwa pliku.\n\n*** Niepoprawna wartość. ***'@)
|? _ax=2
|| undo();
   FUN.info('Niedozwolone znaki w nazwie pliku: %1\n\n*** Niepoprawna wartość. ***'@[_name])
|? _ax=3
|| undo();
   FUN.info('Nazwa pliku za długa.\n\n*** Niepoprawna wartość. ***'@)
|| _err:=1
?};
return(_err)


\view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Pokaz dla bloba
::   WE: _a - tabela z rekordem do obejrzenia
::  OLD: \view/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   ||  exec('bl_view','#blob',_btab,__CTR.BBLOB);
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      exec('getblobs','bloby',__CTR.WIN_ID);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\dellist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja usuniecia zalacznikow
::   WE: _a - tabela z elementami do usuniecia
::  OLD: \dellist/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_dtab:=_a;
_dokumz_czy:=(__CTR.BTAB='DOKUMZ');
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _dtab.first()
||
   {! |?
      {? _btab.seek(_dtab.INT_DATA,)
      || {? _dokumz_czy
         || _dref:=_btab.DOK().ref();
            {? _btab.BL_GUID='' & ~exec('dokumz_glowny','dokum_zal') &
               _btab.BL_TYPE<>exec('bl_type_ksef_inv','zbl_dok') &
               _btab.BL_TYPE<>exec('bl_type_ksef_inv_corr','zbl_dok') &
               _btab.BL_TYPE<>exec('bl_type_inv','zbl_dok') &
               _btab.BL_TYPE<>exec('bl_type_inv_corr','zbl_dok')
            || _btab.del()
            ?}
         || {? __CTR.BTAB='EDOKUMZ'
            || EDOKUMZS.cntx_psh();
               EDOKUMZS.use('skids_'+(ref_name(EDOKUMZ.ref())+2));
               EDOKUMZS.index('EDOKUMZ');
               EDOKUMZS.prefix(EDOKUMZ.ref());
               {? EDOKUMZS.first() || {! |? EDOKUMZS.del !} ?};
               EDOKUMZS.cntx_pop()
            ?};
            _dref:=null();
            _btab.del()
         ?};
         {? _dref<>null()
         || DOKUM.cntx_psh();
            DOKUM.clear();
            {? DOKUM.seek(_dref)
            || exec('dokum_zal_ustaw','dokum')
            ?};
            DOKUM.cntx_pop()
         ?}
      ?};
      _dtab.next()
   !}
?};
_btab.cntx_pop()


\get_dir
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Wybor katalogu
::   WY: sciezka z wybranym katalogiem lub ciag pusty w przypadku rezygnacji
::  OLD: \get_dir/skid_blb.fml
:: ~OST: INFILECHOOSER
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')
|| return('')
|| ctr_set('!application', 'filechooser','reset');
   ctr_set('!application', 'filechooser','setFileSelectionMode','DIRECTORIES_ONLY');
   ctr_set('!application', 'filechooser','setDialogTitle', 'Wybór katalogu');
   _ret:=ctr_call('!application', 'filechooser','showSaveDialog');
   {? _ret
   || return(ctr_call('!application', 'filechooser','getSelectedFile'))
   || return('')
   ?}
?}


\savelist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Zapisywanie plikow do katalogu
::   WE: _a - tabela z lista plikow
::       _b - katalog do zapisu
::  OLD: \savelist/skid_blb.fml
:: ~OST: INFEXISTS
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_dir:=_b;
_group:=0;
{? _tab.size()>1
|| _group:=1
?};
{? _group>0
|| KOMM.init(250,,'Zapisywanie załączników'@)
?};
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? exec('interm','#system')
   || {? _group
      || _tmp_dir:=fmk_tmp_dir(0);
         {? type_of(_tmp_dir)<>type_of(~~)
         || _pth:=_tmp_dir.get_path();
            {!
            |? {? _btab.seek(_tab.INT_DATA,,1)
               || __select:=1;
                  _what:='';
                  _blob_ref:=($(__CTR.BTAB+'.'+__CTR.BBLOB))();
                  {? _blob_ref<>null()
                  || _what:=_btab.bl_info(__CTR.BBLOB,'NAME')
                  ?};
                  {? exec('blk_lock','#table',__CTR.BTAB,_btab.ref(),,1,'Załącznik: %1 jest redagowany.'@[_what],,_group)
                  || {? _blob_ref<>null()
                     || _name:=_btab.bl_info(__CTR.BBLOB,'NAME');
                        _file:=_pth+exec('sep','#file',2)+_name;
                        _can_continue:=_btab.bl_get(__CTR.BBLOB,_file,0)
                     || KOMM.add('Brak pliku w bazie danych. Załącznik jest pusty.'@)
                     ?};
                     exec('blk_unlock','#table',__CTR.BTAB,_btab.ref())
                  ?}
               ?};
               _tab.next() & _can_continue>0
            !}
         || _can_continue:=0;
            KOMM.add('Nie udało się utworzenie katalogu tymczasowego po stronie serwera.'@,2,,1)
         ?};
         {? _can_continue>0
         || _tmp_dir2:=fmk_tmp_dir(0);
            {? type_of(_tmp_dir2)<>type_of(~~)
            || _pth2:=_tmp_dir2.get_path();
               _name:='Załączniki'@+'_'+$SYSLOG.tm_stamp()+'.zip';
               _arch:=_pth2+exec('sep','#file',2)+_name;
               _can_continue:=fpack_add(_arch,_pth);
               {? _can_continue>0
               || dlg_save(_arch,0,_name)
               ?}
            || _can_continue:=0;
               KOMM.add('Nie udało się utworzenie katalogu tymczasowego po stronie serwera.'@,2,,1)
            ?}
         ?}
      || {? _btab.seek(_tab.INT_DATA,,1)
         || __select:=1;
            _what:='';
            _blob_ref:=($(__CTR.BTAB+'.'+__CTR.BBLOB))();
            {? _blob_ref<>null()
            || _what:=_btab.bl_info(__CTR.BBLOB,'NAME')
            ?};
            {? exec('blk_lock','#table',__CTR.BTAB,_btab.ref(),,1,'Załącznik: %1 jest redagowany.'@[_what],,_group)
            || {? _blob_ref<>null()
               || exec('bl_save','#blob',_btab,__CTR.BBLOB)
               || FUN.emsg('Brak pliku w bazie danych. Załącznik jest pusty.'@)
               ?};
               exec('blk_unlock','#table',__CTR.BTAB,_btab.ref())
            ?}
         ?}
      ?}
   || {!
      |? {? _btab.seek(_tab.INT_DATA,)
         || __select:=1;
            {? _btab.r_lock(1)
            || _file:=_btab.bl_info(__CTR.BBLOB,'NAME');
               {? _dir+1<>__CTR.DIR_SEP
               || _dir_sep:=__CTR.DIR_SEP
               || _dir_sep:=''
               ?};
               {? ~fexists('@'+_dir+_dir_sep+_file,) |
                  FUN.ask('Plik ' + _file + ' już istnieje. Czy zapisać?')
               || _btab.bl_get(__CTR.BBLOB, '@'+ _dir+_dir_sep+_file)
               ?};
               _btab.r_unlock()
            ?}
         || FUN.emsg('Załącznik został usunięty.'@)
         ?};
         _tab.next()
      !}
   ?}
?};
_btab.cntx_pop()


\dir_sep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Separator katalogu
::  OLD: \dir_sep/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
{? sys_name='U_LINUX'
|| return('/')
|| return('\\\\')
?}


\bobs_dokumz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG
:: OPIS: Przed obsluga DOKUMZ
::  OLD: \bobs_dokumz/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
__CTR.BTAB:='DOKUMZ';
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
_Interm:=exec('interm','#system');
{? DOKUM.name()<>'dokum' | ~DOKUM.get() | (var_pres('__KONTIN')>0 & KHVZ.UPRAW='B')
   | (var_pres('BLOKUJ',__CTR)>0 & __CTR.BLOKUJ)
|| __CTR.DND:=0;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   _btab:=($(__CTR.BTAB))();
   {? _btab.size()
   || ctr_set(__CTR.WIN_ID,'ctr_id','addAction','_','P&OKAŻ','','Pokazanie dokumentu',1,1);
      ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar');
      ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions')
:: zostawała domyslna akcja!!!
   ?};
   ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar')
::|? DOKUM.EDOKUMR<>'' | DOKUM.FIRMA<>REF.FIRMA | (var_pres('__KONTIN')>0 & (__KONTIN=1 | KHVZ.UPRAW='P' |
::   DOKUM.RECEIVED='T' | DOKUM.BSEND<>'N' | DOKUM.WO='T' | DOKUM.ESEND='T' | DOKUM.K_NAD='T'))
|? DOKUM.EDOKUMR<>'' | DOKUM.FIRMA<>REF.FIRMA | (var_pres('__KONTIN')>0 & (__KONTIN=1 | KHVZ.UPRAW='P' |
   DOKUM.RECEIVED='T' | DOKUM.BSEND<>'N' | DOKUM.WO='T' | DOKUM.ESEND='T' | KHVZ.KHVP='kn_nag'))
   | (var_pres('PODGLAD',__CTR)>0 & __CTR.PODGLAD)
|| __CTR.DND:=0;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   _btab:=($(__CTR.BTAB))();
   {? _btab.size()
   || ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarAction','setview','','xwin16.png:47','Widok',0,0);
      ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarSeparator')
   ?};
   {? var_pres('PODGLAD',__CTR)>0 & __CTR.PODGLAD=-1
   || {? _Interm
      || ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarAction','add','','@system|add-circle','Dołączanie załącznika',0,0);
         ctr_set(__CTR.WIN_ID,'ctr_id','addMenuAction','add','&Dołącz',0,0)
      || ctr_set(__CTR.WIN_ID,'ctr_id','addAction','add','&Dołącz','','Dołączanie załącznika',0,0)
      ?}
   ?};
   {? _btab.size()
   || {? var_pres('PODGLAD',__CTR)>0 & __CTR.PODGLAD=-1
      || {? _Interm
         || ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarAction','del','','@system|delete','Usuwanie załącznika',2,0);
            ctr_set(__CTR.WIN_ID,'ctr_id','addMenuAction','del','&Usuń',2,0)
         || ctr_set(__CTR.WIN_ID,'ctr_id','addAction','del','&Usuń','','Usuwanie załącznika',2,0)
         ?}
      ?};
      {? _Interm
      || ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarAction', 'print','','@system|print','Wydruk dokumentu',1,0);
         ctr_set(__CTR.WIN_ID,'ctr_id','addMenuAction', 'print','&Drukuj',1,0)
      ?};
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'view','P&OKAŻ','','Pokazanie dokumentu',1,1);
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'save','&Zapisz','','Kopiowanie dokumentu na wybraną lokalizację',2,0);
      {? ~_Interm
      || ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'print','&Drukuj','','Wydruk dokumentu',1,0)
      ?};
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'disp','&Właściwości','','Wyświetlenie większej ilości informacji',1,0)
   ?};
   ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar')
|| __CTR.DND:=1;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   exec('initf','bloby',__CTR.WIN_ID)
?}


\initf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: utworzenie belki z akcjami dla kontrolki
::   WE: _a - identyfikator kontrolki
::  OLD: \initf/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_Interm:=exec('interm','#system');
ctr_set(_a,'ctr_id','addToolbarAction','setview','','xwin16.png:47','Widok',0,0);
ctr_set(_a,'ctr_id','addToolbarSeparator');
{? _Interm
|| ctr_set(_a,'ctr_id','addToolbarAction','add','','@system|add-circle','Dołączanie załącznika',0,0);
   ctr_set(_a,'ctr_id','addMenuAction','add','&Dołącz',0,0);
   ctr_set(_a,'ctr_id','addToolbarAction','put','','@system|mode-edit','Poprawianie załącznika',1,0);
   ctr_set(_a,'ctr_id','addMenuAction','put','&Popraw',1,0);
   ctr_set(_a,'ctr_id','addToolbarAction','del','','@system|delete','Usuwanie załącznika',2,0);
   ctr_set(_a,'ctr_id','addMenuAction','del','&Usuń',2,0);
   ctr_set(_a,'ctr_id','addToolbarAction', 'print','','@system|print','Wydruk dokumentu',1,0);
   ctr_set(_a,'ctr_id','addMenuAction', 'print','&Drukuj',1,0)
|| ctr_set(_a,'ctr_id','addAction','add','&Dołącz','','Dołączanie załącznika',0,0);
   ctr_set(_a,'ctr_id','addAction','put','&Popraw','','Poprawianie załącznika',1,0);
   ctr_set(_a,'ctr_id','addAction','del','&Usuń','','Usuwanie załącznika',2,0)
?};
ctr_set(_a,'ctr_id','addAction','scan','&Skanuj','','Skanowanie dokumentu źrodłowego',0,0);

:: Akceje dla wniosków w obiegu
{? var_pres('__CTR')>0 & var_pres('BTAB',__CTR)=type_of('') & __CTR.BTAB='EDOKUMZ' &
   var_pres('typobi')=type_of(1) & typobi=2 &
   exec('lic','#b_domain','PEP')
|| ctr_set(_a,'ctr_id','addAction','sign','Podp&isz','','Podpisanie dokumentu',1,1);
   ctr_set(_a,'ctr_id','addAction','signv','Podpis&y','','Prezentuje podpisy załącznika',1,1)
?};

ctr_set(_a,'ctr_id','addAction','view','P&OKAŻ','','Pokazanie dokumentu',1,1);
ctr_set(_a,'ctr_id','addAction','edit','&Redaguj','','Redagowanie dokumentu',1,0);
ctr_set(_a,'ctr_id','addAction','save','&Zapisz','','Kopiowanie dokumentu na wybraną lokalizację',2,0);
{? ~_Interm
|| ctr_set(_a,'ctr_id','addAction', 'print','&Drukuj','','Wydruk dokumentu',1,0)
?};
ctr_set(_a,'ctr_id','addAction','copy','Kop&iuj','','Wykonanie kopii dokumentu',1,0);
ctr_set(_a,'ctr_id','addAction','disp','&Właściwości','','Wyświetlenie większej ilości informacji',1,0);
ctr_call(_a,'ctr_id','initToolbar')


\dsk_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Wstepne ustawienia dla kontrolki
::   WE: _a - identyfikator kontrolki
::  OLD: \dsk_ini/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
__CTR.WIN_ID:=_a;
_tab:=exec('tab_tmp','bloby');
ctr_tab('files_data',_a, 'ctr_id', '', _tab);
ctr_call(_a,'ctr_id','setSortColumn','MOD_DATE',1);
{? __CTR.BTAB='EDOKUMZ'
|| ctr_call(_a,'ctr_id','setColumnWidth','B_PRELS',25);
   ctr_call(_a,'ctr_id','setColumnWidth','TYP_ZAL',25)
?};
ctr_call(_a,'ctr_id','setColumnWidth','KR_OP',50);
ctr_call(_a,'ctr_id','updateItems', 'files_data')


\dokum_rfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Po odswiezeniu DOKUM
::   WE: _a - identyfikator kontrolki
::       _b - akronim okienka kontrolki
::       _c - BTAB
::       _d - 0/1 - odświeżenie wszystkich wystąpień okienka w grupie / tylko na bieżącej zakładce
::  OLD: \dokum_rfr/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_btab:={? var_pres('_c')=type_of('') || _c || 'DOKUMZ' ?};
_cur_tab:={? var_pres('_d')=type_of(0) || _d || 1 ?};
{? _btab='DOKUMZ'
||
   DOKUMZ.index('DISP');
   {? DOKUM.sel_size()=0
         &
      {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
   || DOKUMZ.prefix(DOKUM.ref())
   || DOKUMZ.prefix(null)
   ?};
   {? ~DOKUMZ.get() || DOKUMZ.first() ?}
||
   DOKUMZA.use((DOKUMZA.name()-2)+(DOKUM.name()+2));
   DOKUMZA.index('DISP');
   {? DOKUM.sel_size()=0
         &
      {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
   || DOKUMZA.prefix(DOKUM.ref())
   || DOKUMZA.prefix(null)
   ?};
   {? ~DOKUMZA.get() || DOKUMZA.first() ?}
?};
__CTR.BTAB:=_btab;
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ID:=_a;
__CTR.WIN_ACR:=_b;
exec('getblobs','bloby',__CTR.WIN_ID);
grp_disp(($(__CTR.BTAB))(),__CTR.WIN_ACR,,_cur_tab);
1


\find_dokumz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Poszukuje załącznik DOKUMZ
::   WE: _a - nagłówek - DOKUM.ref
::       _b - istniejący blob BLOBRAW
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
DOKUMZ.index('DISP');
DOKUMZ.prefix(_a);
{? DOKUMZ.first()
|| {!
   |? _jest:=DOKUMZ.DOKUM=_b;
      _jest=0 & DOKUMZ.next()
   !}
?};
_jest


\add_dokumz2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Dodaje załącznik DOKUMZ na podstawie DOKUM
::   WE:  _a  - opis
::       [_b] - dodać? 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
{? _b
|| DOKUMZ.blank(1);
   DOKUMZ.DOK:=DOKUM.ref()
?};
{? exec('upgrade2325_blbc1','zbl')
|| DOKUMZ.REFSQL:=DOKUM.REFSQL
?};
DOKUMZ.KR_OP:=_a;
DOKUMZ.DATE:=date();
DOKUMZ.TIME:=time();
DOKUMZ.bl_file('DOKUM',1);
DOKUMZ.DOKUM:=DOKUM.DOKUM;
DOKUMZ.NAZWA:=DOKUMZ.bl_info('DOKUM','NAME');
{? _b
|| DOKUMZ.add()
|| DOKUMZ.put()
?}


\dokum_rfr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Po odswiezeniu KN_POZ
::   WE: _a - identyfikator kontrolki
::       _b - akronim okienka kontrolki
::----------------------------------------------------------------------------------------------------------------------
DOKUMZ.index('DISP');
{? DOKUM.get()
|| DOKUMZ.prefix(DOKUM.ref())
|| DOKUMZ.prefix(null)
?};
{? ~DOKUMZ.get() || DOKUMZ.first() ?};
__CTR.BTAB:='DOKUMZ';
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ID:=_a;
__CTR.WIN_ACR:=_b;
exec('getblobs','bloby',__CTR.WIN_ID);
grp_disp(($(__CTR.BTAB))(),__CTR.WIN_ACR,,1);
1


\knp_rfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Po odswiezeniu DOKUM
::   WE: _a - identyfikator kontrolki
::       _b - akronim okienka kontrolki
::  OLD: \dokum_rfr/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
{? KN_NAG.f_active()
|| _kn_jest:=(KN_NAG.f_size()>0)
|| _kn_jest:=(KN_NAG.size()>0)
?};
{? _kn_jest
|| {? KN_POZ.f_active()
   || _knp_jest:=(KN_POZ.f_size()>0)
   || _knp_jest:=(KN_POZ.size()>0)
   ?}
|| _knp_jest:=0
?};

DOKUM.cntx_psh();
{? _knp_jest
|| DOKUM.use(ref_name(KN_POZ.DOKUM));
   DOKUM.prefix();
   DOKUM.seek(KN_POZ.DOKUM)
|| DOKUM.use('dokum');
   DOKUM.index('DOKNRF');
   DOKUM.prefix(REF.FIRMA,-1)
?};
__CTR.BTAB:='DOKUMZ';
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ID:=_a;
__CTR.WIN_ACR:=_b;
exec('getblobs','bloby',__CTR.WIN_ID);
grp_disp(($(__CTR.BTAB))(),__CTR.WIN_ACR,,1);
DOKUM.cntx_pop();
1


\kn_rfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [12.30]
:: OPIS: Po odswiezeniu DOKUM
::   WE: _a - identyfikator kontrolki
::       _b - akronim okienka kontrolki
::  OLD: \dokum_rfr/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMZ.index('DISP');
:KN_POZ.prefix(KN_NAG.ref);
DOKUM.index('DOKUM'); DOKUM.prefix(REF.FIRMA,$KN_NAG.ref);
:{? KN_POZ.get()=0 || DOKUM.index('DOKF'); DOKUM.prefix(date(2099,09,09)) ?};
{? DOKUM.get()
|| DOKUMZ.prefix(DOKUM.ref())
|| DOKUMZ.prefix(null)
?};
{? ~DOKUMZ.get() || DOKUMZ.first() ?};

__CTR.BTAB:='DOKUMZ';
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ID:=_a;
__CTR.WIN_ACR:=_b;
exec('getblobs','bloby',__CTR.WIN_ID);
grp_disp(($(__CTR.BTAB))(),__CTR.WIN_ACR,,1);
:?};
1


\bobs_knnagz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG
:: OPIS: Przed obsluga DOKUMZ
::  OLD: \bobs_dokumz/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
__CTR.BTAB:='KN_NAGZ';
__CTR.TAB:='KN_NAG';
__CTR.BBLOB:='KN_NAG';
{? ~KN_NAG.get() | (var_pres('__KONTIN')>0 & KHVZ.UPRAW='B')
:{? ~KN_NAG.get()
|| __CTR.DND:=0;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   _btab:=($(__CTR.BTAB))();
   {? _btab.size()
   || ctr_set(__CTR.WIN_ID,'ctr_id','addAction','_','P&OKAŻ','','Pokazanie dokumentu',1,1);
      ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar');
      ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions')
:: zostawała domyslna akcja!!!
   ?};
   ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar')
|? KN_NAG.FIRMA<>REF.FIRMA | (var_pres('__KONTIN')>0 & (__KONTIN=1 | KHVZ.UPRAW='P' | KN_NAG.STATUS='Wysłany'))
:|? KN_NAG.FIRMA<>REF.FIRMA | ( KN_NAG.STATUS='Otwarty')
|| __CTR.DND:=0;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   _btab:=($(__CTR.BTAB))();
   {? _btab.size()
   || ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarAction','setview','','xwin16.png:47','Widok',0,0);
      ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarSeparator');
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'view','P&OKAŻ','','Pokazanie dokumentu',1,1);
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'save','&Zapisz','','Kopiowanie dokumentu na wybraną lokalizację',2,0);
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'disp','&Właściwości','','Wyświetlenie większej ilości informacji',1,0)
   ?};
   ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar')
|| __CTR.DND:=1;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   exec('initf2','bloby',__CTR.WIN_ID)
?}


\initf2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: utworzenie belki z akcjami dla kontrolki
::   WE: _a - identyfikator kontrolki
::  OLD: \initf/skid_blb.fml
::----------------------------------------------------------------------------------------------------------------------
_Interm:=exec('interm','#system');
ctr_set(_a,'ctr_id','addToolbarAction','setview','','xwin16.png:47','Widok',0,0);
ctr_set(_a,'ctr_id','addToolbarSeparator');
{? _Interm
|| ctr_set(_a,'ctr_id','addToolbarAction','add','','@system|add-circle','Dołączanie załącznika',0,0);
   ctr_set(_a,'ctr_id','addMenuAction','add','&Dołącz',0,0);
   ctr_set(_a,'ctr_id','addToolbarAction','put','','@system|mode-edit','Poprawianie załącznika',1,0);
   ctr_set(_a,'ctr_id','addMenuAction','put','&Popraw',1,0);
   ctr_set(_a,'ctr_id','addToolbarAction','del','','@system|delete','Usuwanie załącznika',2,0);
   ctr_set(_a,'ctr_id','addMenuAction','del','&Usuń',2,0)
|| ctr_set(_a,'ctr_id','addAction','add','&Dołącz','','Dołączanie załącznika',0,0);
   ctr_set(_a,'ctr_id','addAction','put','&Popraw','','Poprawianie załącznika',1,0);
   ctr_set(_a,'ctr_id','addAction','del','&Usuń','','Usuwanie załącznika',2,0)
?};
ctr_set(_a,'ctr_id','addAction','view','P&OKAŻ','','Pokazanie dokumentu',1,1);
ctr_set(_a,'ctr_id','addAction','save','&Zapisz','','Kopiowanie dokumentu na wybraną lokalizację',2,0);
ctr_set(_a,'ctr_id','addAction','disp','&Właściwości','','Wyświetlenie większej ilości informacji',1,0);
ctr_call(_a,'ctr_id','initToolbar')


\dokum_rfr3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Odczyt załaczników DOKUM reklamacji REK_N
::   WE: _a - identyfikator kontrolki
::       _b - akronim okienka kontrolki
::----------------------------------------------------------------------------------------------------------------------
DOKUM.index('DOKUM');
{? REK_N.get()
|| DOKUM.prefix(REF.FIRMA,$(REK_N.ref()))
|| DOKUM.prefix(null())
?};
{? ~DOKUM.get()
|| DOKUM.first()
?};
__CTR.WIN_ID:=_a;
__CTR.WIN_ACR:=_b;
exec('getblobs','srsr_zal',__CTR.WIN_ID);
grp_disp(($(__CTR.BTAB))(),__CTR.WIN_ACR,,1);
1


\bobs_dokumza
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Przed obsluga DOKUMZA (załączniki archiwalnych kontaktów)
::----------------------------------------------------------------------------------------------------------------------
__CTR.BTAB:='DOKUMZA';
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.DND:=0;
ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
_btab:=($(__CTR.BTAB))();
{? ~_btab.size() | (var_pres('__KONTIN')>0 & KHVZ.UPRAW='B') | (var_pres('BLOKUJ',__CTR)>0 & __CTR.BLOKUJ)
|| __CTR.DND:=0;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   {? _btab.size()
   || ctr_set(__CTR.WIN_ID,'ctr_id','addAction','_','P&OKAŻ','','Pokazanie dokumentu',1,1);
      ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar');
      ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions')
:: zostawała domyslna akcja!!!
   ?}
|| ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarAction','setview','','xwin16.png:47','Widok',0,0);
   ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarSeparator');
   ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'view','P&OKAŻ','','Pokazanie dokumentu',1,1);
   ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'save','&Zapisz','','Kopiowanie dokumentu na wybraną lokalizację',2,0);
   ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'disp','&Właściwości','','Wyświetlenie większej ilości informacji',1,0)
?};
ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar')


\blob_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Rekord po tabeli z załacznikami
::   WE: _a - typ operacji: 'add' - dołaczanie, 'put' - poprawianie
::----------------------------------------------------------------------------------------------------------------------
_add:=_a='add';
_tab:=cur_tab();
_ret:='';
_paperless:=_tab=EDOKUMZ & EDOKUM.TYP().W_PORTAL='P';

{? _ret='' & _paperless
|| _ret:=__CHK.record(_tab,,'TYP_ZAL')
?};
{? _ret='' & _paperless
|| _ref:=EDOKUMZ.ref();
   EDOKUMZ.cntx_psh();
   EDOKUMZ.index('TYPY_ZAL'); EDOKUMZ.prefix(EDOKUM.ref(),EDOKUMZ.TYP_ZAL);
   {? EDOKUMZ.first() & (_add | EDOKUMZ.ref()<>_ref)
   || FUN.info('Istnieje załącznik o typie: %1.'@[EDOKUMZ.TYP_ZAL().NAZWA]);
      _ret:='TYP_ZAL'
   ?};
   EDOKUMZ.cntx_pop()
?};
_ret


\sign
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Podpisanie załącznika
::   WE: _a - tabela tymczasowa z załacznikiem
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   || {? __CTR.BTAB='EDOKUMZ'
      || {? exec('sign','portal_paperless')
         || exec('getblobs','bloby',__CTR.WIN_ID)
         ?}
      ?};
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      exec('getblobs','bloby',__CTR.WIN_ID);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\sign_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Prezentuje podpisy załacznika
::   WE: _a - tabela tymczasowa z załacznikiem
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   || {? __CTR.BTAB='EDOKUMZ'
      || exec('sign_show','portal_paperless')
      ?};
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      exec('getblobs','bloby',__CTR.WIN_ID);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\sign_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Zwraca okno dla tabeli z podpisami
::   WE: _a - tabela z podpisami
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_win:=_tab.mk_sel('Informacje o podpisach','P',,'#zalsigninfo',,,,,'U');
_tab.win_fld(_win,,'DTSIGN',,,-10,,,'Data');
_tab.win_fld(_win,,'TMSIGN',,,-8,,,'Czas');
_tab.win_fld(_win,POMOC,'K1',,,-40,,,'Sygnatariusz');
_tab.win_fld(_win,,'CERTTYPE',,,,,,'Kwalifikowany',,,2,,"'qualified'","'nonqualified'");
_tab.win_fld(_win,,'VRESTXT',,,-40,,,'Status');
_tab.win_act(_win,,'Szukaj');
_tab.win_act(_win,,'Kolejność');
_tab.win_act(_win,,'Rekord',,,,"POMOC.K1:=exec('x509_str','#convert',cur_tab().memo_txt(,1,'SUBJECT'),'CN')");
_tab.win_sel(_win);
_win


\ocr_ext_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Sprawdzenie wg rozszerzenia pliku czy może być znacznik DOKUMZ.P_OCR
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? DOKUMZ.DOKUM<>null()
|| _ext:=-DOKUMZ.bl_info('DOKUM','EXTENSION');
:: OCR przez Businesslink obsługuje obecnie tylko jpg i pdf:
   {? _ext<>'jpg' & _ext<>'pdf' & DOKUMZ.P_OCR='T'
   || DOKUMZ.P_OCR:='N';
      _wyn:=0;
      {? ~do_state()
      || exec('ocr_ext_chk_info','bloby')
      ?}
   ?}
?};
_wyn


\ocr_ext_chk_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Sprawdzenie wg rozszerzenia pliku czy może być znacznik DOKUMZ.P_OCR - komunikat
::----------------------------------------------------------------------------------------------------------------------
:: OCR przez Businesslink obsługuje obecnie tylko jpg i pdf:
FUN.info(
   'Dostępne rozszerzenia dla dokumentów przekazywanych do OCR to *.jpg i *.pdf.'
   '\nNie można przekazać dokumentu do OCR.'@)


\ocr_il_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Sprawdzenie czy może być znacznik DOKUMZ.P_OCR (dopuszczalny tylko 1)
::   WE: _a - 0 - sprawdzić wszystko (domyślnie), 1 - pominąć aktualny
::----------------------------------------------------------------------------------------------------------------------
_ref:=-1;
{? var_pres('_a')=type_of(0) & _a
|| _ref:=DOKUMZ.ref()
?};
_moz:=1;
DOKUMZ.cntx_psh();
DOKUMZ.clear();
DOKUMZ.index('DISP');
DOKUMZ.prefix(DOKUM.ref());
{? DOKUMZ.first()
|| {!
   |? {? DOKUMZ.P_OCR='T' & (_ref=-1 | DOKUMZ.ref()<>_ref)
      || _moz:=0
      ?};
      _moz & DOKUMZ.next()
   !}
?};
DOKUMZ.cntx_pop();
_moz


\ocr_il_chk_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Sprawdzenie czy może być znacznik DOKUMZ.P_OCR (dopuszczalny tylko 1) - komunikat
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Można przekazać tylko jeden dokument do OCR'@)


\obj_add_res
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Zwraca obiekt z informacją o dodanych załącznikach
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'TAB','no_pdf',
   'add'
);
_obj.no_pdf:=0;
_obj.TAB:=tab_tmp(1,
   'REF','INTEGER','ref'
);
_obj.add:="
   _tab:=.TAB;
   _tab.blank(1);
   _tab.REF:=_a;
   _tab.add()
";
_obj


\files_upload
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Wybór plików
::   WE: _a tymczasowy katalog
::   WY: tabela plików
::----------------------------------------------------------------------------------------------------------------------
_pth:=_a;
_files:=tab_tmp(1,
       'LP','REAL','Lp.',
       'DIR','STRING[255]','Katalog',
       'FILENAME','STRING[255]','Nazwa pliku');
{? _pth<>''
|| _uploaded:=dlg_upload(_pth,0,,1);
   {? _uploaded<>''
   || _split:=spli_str(_uploaded,'\n');
      _len:=obj_len(_split);
      {! _it:=1.._len
      |! _filename:=_split[_it];
         {? _filename<>''
         || _files.blank();
            _files.LP:=_files.size()+1;
            _files.DIR:=_pth;
            _files.FILENAME:=_filename;
            _files.add()
         ?}
     !}
  ?}
?};
_files


\files_size
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [22.26]
:: OPIS: Wylicza zajętość załączników - blobów
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_sep:=exec('sep','#file',1);
_dir:=pth_dir('sysblob')+_sep+'sysblob';
_tab:=fdir(_dir,1,1);
_sum:=0;
{? _tab.first()
|| {!
   |? _sum+=_tab.SIZE;
      _tab.next()
   !}
?};
_sum


\info_blob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [22.26]
:: OPIS: Informacja o przestrzeni zajmowanej przez załączniki i możliwość ustalenia quoty
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
: Pobranie wartości do wyświetlenia
_quota:=exec('get','#params',100950);
VAR.ILD:=_quota;
VAR.ILO:=exec('files_size','bloby')/(1024*1024*1024);

: okno do wyświetlenia danych
_edit:=VAR.mk_edit('Ustalanie quoty na objętość załączników'@,0,'quotazal0001');
VAR.win_esep(_edit,'Zajętość załączników'@);
VAR.win_efld(_edit,VAR,'ILD',,,,2,1,'Przydzielona zajętość dysku [GB]'@);
VAR.win_efld(_edit,VAR,'ILO',,,,2,1,'Wykorzystana zajętość dysku [GB]'@);

{? sec_superuser()>0
|| _cfg:=',btn_label_align=center,panel=bottom,align=begin,display=1';
   _rule:="exec('change_quota','bloby')";
   _bok:=VAR.win_ebtn(_edit,'text=%1' ['&Zmień przydział'@]+_cfg,_rule);
   _tooltip:='Zmiana dozwolonej przestrzeni dyskowej do wykorzystania przez załączniki'@;
   VAR.btn_eopt(_edit,_bok,'tooltip='+_tooltip)
?};

_cfg:=',btn_label_align=center,panel=bottom,align=end,display=1';
_rule:='key:F2';
_bok:=VAR.win_ebtn(_edit,'text=%1' ['&OK'@]+_cfg,_rule);
VAR.btn_eopt(_edit,_bok,'tooltip='+exec('help_act_ok','#window'));

_rule:='key:Esc';
_besc:=VAR.win_ebtn(_edit,'text=%1' ['&Anuluj'@]+_cfg,_rule);
VAR.btn_eopt(_edit,_besc,'tooltip='+exec('help_act_esc','#window'));

VAR.win_edit(_edit);
VAR.display();
''


\change_quota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [22.26]
:: OPIS: Pozwala na zmianę przydzielonej przestrzeni dyskowej na załączniki
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_quota:=exec('get','#params',100950);
_val:=exec('edit_number','#edit',_quota,'Zmiana przydziału dysku na załączniki [GB]',2,0);
{? _val<>~~
|| exec('set','#params',100950,_val);
   VAR.ILD:=_val
?};
''

:Sign Version 2.0 jowisz:1045 2023/10/25 11:20:42 ba1ac3f2b0a13c9f36da8ffe9f952f47ff0b8af454d3b947a6cefcbdf2c7fed772299974feecfab6574eba686147fdca4f82735ec144882f12710f272fc644aa58a3e03f6a9b203f84652321a26f346d6c6097b5b0ad6eadb2cafc955e7f7582a6fc76bbe0a9d23f73cf5dffafca0328b369641b36793b04f0ffc30f407dba27

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: zal_pobh.fml [12.41]
:: Utworzony: 2014/05/23
:: Autor: TMR
::======================================================================================================================
:: Zawartosc: Formuly do obslugi historii pobierania zalacznikow
::======================================================================================================================


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.41]
:: OPIS: Dodaje rekord w historii pobierania załącznika.
::       Historia jest uzupełniana na podstawie bufora wskazanego rekordu tabeli ZALACZ
::   WE: _a - [REFERENCE] - wskazanie na rekord tabeli ZALACZ
::       [_b] - [REFERENCE] - wskazanie na rekord w tabeli USERS
::   WY: >0 - udało się,
::      <=0 - nie udało się
::----------------------------------------------------------------------------------------------------------------------
 _ref:={? var_pres('_a')=type_of(null)                     || _a || return(0) ?};
_user:={? var_pres('_b')=type_of(null) & ref_tab(_b)=USERS || _b || null()    ?};
_res:=0;

ZALACZ.cntx_psh();
ZALACZ.prefix();
{? ZALACZ.seek(_ref)
|| ZAL_POBH.cntx_psh();
   ZAL_POBH.prefix();
   ZAL_POBH.blank(1);
   ZAL_POBH.POTW:=ZALACZ.POTW;
   ZAL_POBH.POB_D:=ZALACZ.POB_D;
   ZAL_POBH.POB_G:=ZALACZ.POB_G;
   ZAL_POBH.ZALACZ:=ZALACZ.ref();
   ZAL_POBH.USER:=_user;
   {? ZAL_POBH.add()
   || _text:=exec('get_TEXT','zal_kom',exec('get','zal_kom',ZALACZ.TYP_ZAL));
      ZAL_POBH.memo_set(_text);
      ZAL_POBH.memo_put()
   ?};
   ZAL_POBH.cntx_pop()
?};
ZALACZ.cntx_pop();
_res


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.41]
:: OPIS: Kasuje podany rekord tabeli ZAL_POBH (wykonywane w transakcji!!!)
::   WE: _a - ZAL_POBH.ref()
::   WY: >0 -wyczyszczone,
::      <=0 -niewyczyszczone
::UWAGA: Parametry bez [] sa wymagane, formula moze nie sprawdzac czy zostaly podane i moze wystapic blad.
::----------------------------------------------------------------------------------------------------------------------
_ref:={? var_pres('_a')=type_of(null) || _a || return(0) ?};
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ZAL_POBH.cntx_psh(); ZAL_POBH.clear();
{? ZAL_POBH.seek(_ref)
|| {? ZAL_POBH.del(,1)>0
   || _result:=1
   || undo();
      _result:=-3
   ?}
|| _result:=0
?};
ZAL_POBH.cntx_pop();

{? _result<0
|| undo()
?};
{? _mydo || end() ?};
_result


\delete_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.41]
:: OPIS: Kasuje wszystkie rekordy ZAL_POBH
::   WE: _a - [REFERENCE] - wskazanie na ZALACZ
::   WY: >0 -wyczyszczone,
::      <=0 -niewyczyszczone
::UWAGA: Parametry bez [] sa wymagane, formula moze nie sprawdzac czy zostaly podane i moze wystapic blad.
::----------------------------------------------------------------------------------------------------------------------
_ref:={? var_pres('_a')=type_of(null) || _a || return(0) ?};
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};

_result:=1;

ZAL_POBH.cntx_psh();
ZAL_POBH.index('ZDG');
ZAL_POBH.prefix(_ref);
{? ZAL_POBH.first()
|| {!
   |? _result*=exec('delete','zal_pobh',ZAL_POBH.ref());
      ZAL_POBH.first()
   !}
?};
ZAL_POBH.cntx_pop();

{? _result<0
|| undo()
?};
{? _mydo || end() ?};
_result


\getLastKom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.41]
:: OPIS: Pobiera komunikat z ostatniego zapisu
::   WE: _a - [REFERENCE] - wskazanie na ZALACZ
::   WY:      [STRING] - tekst ostatnio potwierdzonego komunikatu
::----------------------------------------------------------------------------------------------------------------------
_ref:={? var_pres('_a')=type_of(null) || _a || return(0) ?};
_text:='';
ZAL_POBH.cntx_psh();
ZAL_POBH.index('ZDG');
ZAL_POBH.prefix(_ref);
:: index malejący po dacie i godzinie
{? ZAL_POBH.first()
|| _text:=ZAL_POBH.memo_txt(,1,'TEXT')
?};
ZAL_POBH.cntx_pop();
_text


\isNotEmpty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.41]
:: OPIS: Sprawdza czy historia pobierania załączników nie jest pusta
::   WE: _a - [REFERENCE] - wskazanie na ZALACZ
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_ref:={? var_pres('_a')=type_of(null) || _a || return(0) ?};
_result:=0;
ZAL_POBH.cntx_psh();
ZAL_POBH.index('ZDG');
ZAL_POBH.prefix(_ref);
_result:=ZAL_POBH.first();
ZAL_POBH.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:04 decaa0051829f40070b0a98126c63d654907d277974c36cd4e1a20f4306bbc8e049c4dfbdef3d523b9765a0900fac3377ca61b93836437975128b264826e77cba803525d96a0c4eb01f8ecfb3f8a4f9f2d00de5da140a6478ed12f515eba41f59e6af62ff76af48662969b0abb0b8847ef5badb86a3a975f39405e76cd7d9af5

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obiegi2.fml
:: Utworzony: 14.08.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa obiegów dokumentów
::======================================================================================================================

\be_form_etschpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Przed redakcją formułowych pól tabeli ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
_pole:=1-cur_afld();
ZMIENNE.RODZAJ:='C';
{? cur_afld()<>'KW_F' & ($('ETSCHPOD.ZN_'+_pole))()='S'
|| ($('ETSCHPOD.F'+_pole+':=null'))(); 0
|| 1
?}


\be_con_mb_etschpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Formuła przed redakcją pola ETSCHPOD.SKID_MB
::----------------------------------------------------------------------------------------------------------------------
UD_POM.SKID_MBN:=ETSCHPOD.SKID_MB; 1


\ae_con_mb_etschpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Formuła po redakcji pola ETSCHPOD.SKID_MB
::----------------------------------------------------------------------------------------------------------------------
{? ETSCHPOD.SKID_MB<>UD_POM.SKID_MBN
|| ETSCHPOD.JORG:=null; ETSCHPOD.FJORG:=null;
   ETSCHPOD.OKOSZ:=null; ETSCHPOD.FOKOSZ:=null;
   ETSCHPOD.PBUD:=null; ETSCHPOD.FPBUD:=null;
   ETSCHPOD.WYM4:=null; ETSCHPOD.FWYM4:=null;
   ETSCHPOD.WYM5:=null; ETSCHPOD.FWYM5:=null;
   UD_POM.PBUD:=UD_POM.JORG:=UD_POM.OKOSZ:=UD_POM.WYM4:=UD_POM.WYM5:=''
?};
exec('set_enabled_etschpod','obiegi2');
win_disp()


\set_enabled_etschpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawia dostępność pól
::----------------------------------------------------------------------------------------------------------------------
_okno:=ETSCHPOD.win_edit('?');
SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(ETSCHPOD.SKID_MB);
{! _nr:=1..5
|! _jest:=SKID_MBP.find_key(_nr);
   _pole:={? _nr=1 || 'PBUD'
          |? _nr=2 || 'JORG'
          |? _nr=3 || 'OKOSZ'
          |? _nr=4 || 'WYM4'
          |? _nr=5 || 'WYM5'
          || ''
          ?};
   _stala:=($('ETSCHPOD.ZN_'+_pole))()='S';
   ETSCHPOD.efld_opt(_okno,'enable='+$(_jest & _stala),UD_POM,_pole);
   ETSCHPOD.efld_opt(_okno,'enable='+$(_jest & _stala),ETSCHPOD,_pole);
   ETSCHPOD.efld_opt(_okno,'enable='+$(_jest & ~_stala),ETSCHPOD,'F'+_pole);
   ETSCHPOD.efld_opt(_okno,'enable='+$_jest,UD_POM,'NAZ_WYM'+$_nr);
   ETSCHPOD.efld_opt(_okno,'enable='+$_jest,ETSCHPOD,'ZN_'+_pole);
   ETSCHPOD.efld_opt(_okno,'mark='+$(_jest & _stala),UD_POM,_pole);
   ETSCHPOD.efld_opt(_okno,'mark='+$(_jest & ~_stala),ETSCHPOD,'F'+_pole)
!};
SKID_MBP.cntx_pop();
~~


\etschpod_dsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Na wyświetl dla tabeli ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
ETSCHPOD.win_edit('RED');
exec('set_enabled_etschpod','obiegi2');
ETSCHPOD.display();
1


\ae_chkbx_etschpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Po redakcji pol typu Check boX okien tabeli ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
{? fld()='S'
|| ($('ETSCHPOD.F'+(3-cur_afld)+':=null'))()
|| {? cur_afld='ZN_JORG' || UD_POM.JORG:=''
   |? cur_afld='ZN_PBUD' || UD_POM.PBUD:=''
   |? cur_afld='ZN_OKOSZ' || UD_POM.OKOSZ:=''
   |? cur_afld='ZN_WYM4' || UD_POM.WYM4:=''
   |? cur_afld='ZN_WYM5' || UD_POM.WYM5:=''
   ?};
   ($('ETSCHPOD.'+(3-cur_afld)+':=null'))()
?};
exec('set_enabled_etschpod','obiegi2');
1


\set_fml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Ustawia formuły zmiennych dla tabeli ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
{! _lp:=1..5
|! exec('set_fml','#field',UD_POM,'NAZ_WYM'+$_lp,"
      _nr:=#(cur_afld()+1);
      fld(exec('dim_name','control',ETSCHPOD.SKID_MB,_nr))
   ")
!};
exec('set_fml','#field',UD_POM,'PBUD',
   "UD_POM.PBUD:=ETSCHPOD.PBUD().SYMBOL;~~",
   "exec('be_dim','control',ETSCHPOD.SKID_MB,1)",
   "exec('f3_dim','control',ETSCHPOD.SKID_MB,1)",
   "  {? fld()='' || ETSCHPOD.PBUD:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ETSCHPOD.SKID_MB,1);
      {? _ref
      || ETSCHPOD.PBUD:=_ref;
         fld(ETSCHPOD.PBUD().SYMBOL);
         1
      ?}
   "
);
exec('set_fml','#field',UD_POM,'JORG',
   "UD_POM.JORG:=ETSCHPOD.JORG().SYMBOL; ~~",
   "exec('be_dim','control',ETSCHPOD.SKID_MB,2)",
   "exec('f3_dim','control',ETSCHPOD.SKID_MB,2)",
   "  {? fld()='' || ETSCHPOD.JORG:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ETSCHPOD.SKID_MB,2);
      {? _ref
      || ETSCHPOD.JORG:=_ref;
         fld(ETSCHPOD.JORG().SYMBOL)
      ?}
   "
);
exec('set_fml','#field',UD_POM,'OKOSZ',
   "UD_POM.OKOSZ:=ETSCHPOD.OKOSZ().SYMBOL; ~~",
   "exec('be_dim','control',ETSCHPOD.SKID_MB,3)",
   "exec('f3_dim','control',ETSCHPOD.SKID_MB,3)",
   "  {? fld()='' || ETSCHPOD.OKOSZ:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ETSCHPOD.SKID_MB,3);
      {? _ref
      || ETSCHPOD.OKOSZ:=_ref;
         fld(ETSCHPOD.OKOSZ().SYMBOL)
      ?}
   "
);
exec('set_fml','#field',UD_POM,'WYM4',
   "UD_POM.WYM4:=ETSCHPOD.WYM4().SYMBOL; ~~",
   "exec('be_dim','control',ETSCHPOD.SKID_MB,4)",
   "exec('f3_dim','control',ETSCHPOD.SKID_MB,4)",
   "  {? fld()='' || ETSCHPOD.WYM4:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ETSCHPOD.SKID_MB,4);
      {? _ref
      || ETSCHPOD.WYM4:=_ref;
         fld(ETSCHPOD.WYM4().SYMBOL)
      ?}
   "
);
exec('set_fml','#field',UD_POM,'WYM5',
   "UD_POM.WYM5:=ETSCHPOD.WYM5().SYMBOL; ~~",
   "exec('be_dim','control',ETSCHPOD.SKID_MB,5)",
   "exec('f3_dim','control',ETSCHPOD.SKID_MB,5)",
   "  {? fld()='' || ETSCHPOD.WYM5:=null; return(1) ?};
      _ref:=exec('ae_dim','control',ETSCHPOD.SKID_MB,5);
      {? _ref
      || ETSCHPOD.WYM5:=_ref;
         fld(ETSCHPOD.WYM5().SYMBOL)
      ?}
   "
);
~~

\sch_pod_etypy_up
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KS [22.26]
:: OPIS: Schemat podziałów dla typów dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
exec('zmien_lpa','#dragdrop','LP','DISP',,,'U')


\sch_pod_etypy_down
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KS [22.26]
:: OPIS: Schemat podziałów dla typów dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
exec('zmien_lpa','#dragdrop','LP','DISP',,,'D')


\sch_pod_etypy_number
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KS [22.26]
:: OPIS: Schemat podziałów dla typów dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
exec('zmien_lpa','#dragdrop','LP','DISP',,,'N')


\sch_pod_etypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Schemat podziałów dla typów dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
exec('set_fml','obiegi2');
ETSCHPOD.cntx_psh(); ETSCHPDR.cntx_psh(); FIRMA.cntx_psh();
ETSCHPOD.win_edit('RED');
ETSCHPOD.dnd_sel('WER',,'records.ETSCHPOD',
                 "ETSCHPOD.cntx_psh();
                  _ref:=dnd_info('dest_record');
                  {? _ref & ETSCHPOD.seek(_ref) || exec('zmien_lp','#dragdrop','LP','DISP') ?};
                  ETSCHPOD.cntx_pop()
                 ");
ETSCHPOD.dnd_sel('WER1',,'records.ETSCHPOD',
                 "ETSCHPOD.cntx_psh();
                  _ref:=dnd_info('dest_record');
                  {? _ref & ETSCHPOD.seek(_ref) || exec('zmien_lp','#dragdrop','LP','DISP') ?};
                  ETSCHPOD.cntx_pop()
                 ");
_firma:=REF.FIRMA;
ETSCHPOD.index('DISP'); ETSCHPOD.prefix(null);
ETSCHPDR.index('DISP'); ETSCHPDR.prefix(null,null);
ETSCHPOD.actions_grayed('WER','DPU:D');
ETSCHPOD.actions_grayed('WER1','DPU:D');
_fo:=FIRMA.mk_sel('Firmy'@,,,'firmy_etypy');
FIRMA.win_fld(_fo,,'SYMBOL');
FIRMA.win_fld(_fo,,'OPIS',,,20);
_grp:=FIRMA.grp_make('Schematy tworzenia podzialow'@,,'firmy_schp',,,,,'normal');
FIRMA.grp_sel(_grp,,_fo,,"exec('ae_refr_firma','obiegi2')",,,10,,,,,'maximized_with_title');
FIRMA.grp_splt(_grp,,'vertical','panel1');
FIRMA.grp_sel(_grp,ETSCHPDR,'WER',,"exec('ae_refr_etschpdr','obiegi2')",,,,,,,,'maximized_with_title');
FIRMA.grp_splt(_grp,'panel1','horizontal','panel2',15);
FIRMA.grp_sel(_grp,ETSCHPOD,'WER1',,"",,,,,,,,'maximized_with_title');
FIRMA.win_sel(_grp);
REF.FIRMA();
FIRMA.select(,1);
ETSCHPOD.cntx_pop(); ETSCHPDR.cntx_pop(); FIRMA.cntx_pop();
REF.FIRMA:=_firma


\ae_refr_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Po odświeżeniu tabeli FIRMA
::----------------------------------------------------------------------------------------------------------------------
REF.FIRMA:=FIRMA.ref();
ETSCHPDR.index('DISP'); ETSCHPDR.prefix(ETYPY.ref(),REF.FIRMA);
{? ~ETSCHPDR.get() || ETSCHPDR.first() ?};
grp_disp(ETSCHPDR,'WER',1,1);
exec('ae_refr_etschpdr','obiegi2')


\ae_refr_etschpdr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Po odświeżeniu tabeli ETSCHPDR
::----------------------------------------------------------------------------------------------------------------------
ETSCHPOD.index('DISP');
{? ETSCHPDR.get() & ETSCHPDR.ETSCHPDN
|| ETSCHPOD.actions_grayed('WER1','');
   ETSCHPOD.prefix(ETSCHPDR.ETSCHPDN)
|| ETSCHPOD.actions_grayed('WER1','DPU:D');
   ETSCHPOD.prefix(null)
?};
{? ~ETSCHPOD.get() || ETSCHPOD.first() ?};
grp_disp(ETSCHPOD,'WER1',1,1)


\etschpod_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Dołączanie rekordu w tabeli ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
_renum:=1; _ref:=null;
{? ~ETSCHPOD.get()
|| _lp:=1; _renum:=0
|? _a=1
|| _lp:=ETSCHPOD.LP; _ref:=ETSCHPOD.ref()
|| _lp:=ETSCHPOD.LP+1; _renum:=0
?};
ETSCHPOD.blank();
ETSCHPOD.ETSCHPDN:=ETSCHPDN.ref();
ETSCHPOD.ETYPY:=ETYPY.ref();
ETSCHPOD.LP:=_lp;
exec('set_enabled_etschpod','obiegi2');
{? ETSCHPOD.edit("exec('ar_etschpod','obiegi2')") & ETSCHPOD.add() & _renum
|| _ref_new:=ETSCHPOD.ref();
   ETSCHPOD.cntx_psh();
   {? ETSCHPOD.seek(_ref) || ETSCHPOD.LP+=1; ETSCHPOD.put() ?};
   exec('renum_etschpod_d','obiegi2',_ref_new);
   ETSCHPOD.cntx_pop()
?}


\etschpod_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Poprawianie rekordu w tabeli ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
exec('set_enabled_etschpod','obiegi2');
{? ETSCHPOD.edit("exec('ar_etschpod','obiegi2')")
|| ETSCHPOD.put()
?}


\etschpod_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Usuwanie rekordu w tabeli ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć schemat podziałów controllingowych?'@)
|| ETSCHPOD.del();
   exec('renum_etschpod_u','obiegi2')
?}


\renum_etschpod_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Przenumerowanie tabeli ETSCHPOD przy dołączaniu
::   WE: _a - ref dołączanego rekordu
::----------------------------------------------------------------------------------------------------------------------
ETSCHPOD.cntx_psh(); ETSCHPOD.index('DISP'); ETSCHPOD.prefix(ETSCHPDN.ref());
{? ETSCHPOD.last()
|| _lp:=ETSCHPOD.size();
   {! |?
      {? ETSCHPOD.ref()<>_a
      || ETSCHPOD.LP:=_lp; ETSCHPOD.put()
      ?};
      _lp-=1;
      ETSCHPOD.prev()
   !}
?};
ETSCHPOD.cntx_pop()


\renum_etschpod_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Przenumerowanie tabeli ETSCHPOD przy usuwaniu
::----------------------------------------------------------------------------------------------------------------------
ETSCHPOD.cntx_psh(); ETSCHPOD.index('DISP'); ETSCHPOD.prefix(ETSCHPDN.ref());
{? ETSCHPOD.first()
|| _lp:=1;
   {! |?
      ETSCHPOD.LP:=_lp; ETSCHPOD.put(); _lp+=1;
      ETSCHPOD.next()
   !}
?};
ETSCHPOD.cntx_pop()


\ar_etschpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Po redakcji rekordu w tabeli ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? ETSCHPOD.SKID_MB=null
|| FUN.emsg('Nie wybrano modelu controllingowego.'@); _zwrot:='SKID_MB'
|| _ref_mb:=ETSCHPOD.SKID_MB;
   SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(_ref_mb);
   VAR_DEL.delete('_wym','_naz','_typ');
   _wym:=obj_new(5); _naz:=obj_new(5); _typ:=obj_new(5);
   {! _i:=1..5
   |! {? SKID_MBP.find_key(_i)
      || _wym[_i]:=SKID_MBP.UD_SCH().SYMBOL;
         _naz[_i]:=-SKID_MBP.NAZ;
         _typ[_i]:=SKID_MBP.UD_SCH().UD_TYP().SYMBOL
      || _wym[_i]:=_naz[_i]:=_typ[_i]:=''
      ?}
   !};
   SKID_MBP.cntx_pop();
   {? _wym[1]<>''
   || {? ETSCHPOD.ZN_PBUD='S' & UD_POM.PBUD=''
      || __CHK.err_empty(UD_POM.NAZ_WYM1+' - stała wartość'); _zwrot:='PBUD'
      |? ETSCHPOD.ZN_PBUD='F' & ETSCHPOD.FPBUD=null
      || __CHK.err_empty(UD_POM.NAZ_WYM1+' - formuła'); _zwrot:='FPBUD'
      ?}
   ?};
   {? _wym[2]<>'' & _zwrot=''
   || {? ETSCHPOD.ZN_JORG='S' & UD_POM.JORG=''
      || __CHK.err_empty(UD_POM.NAZ_WYM2+' - stała wartość'); _zwrot:='JORG'
      |? ETSCHPOD.ZN_JORG='F' & ETSCHPOD.FJORG=null
      || __CHK.err_empty(UD_POM.NAZ_WYM2+' - formuła'); _zwrot:='FJORG'
      ?}
   ?};
   {? _wym[3]<>'' & _zwrot=''
   || {? ETSCHPOD.ZN_OKOSZ='S' & UD_POM.OKOSZ=''
      || __CHK.err_empty(UD_POM.NAZ_WYM3+' - stała wartość'); _zwrot:='OKOSZ'
      |? ETSCHPOD.ZN_OKOSZ='F' & ETSCHPOD.FOKOSZ=null
      || __CHK.err_empty(UD_POM.NAZ_WYM3+' - formuła'); _zwrot:='FOKOSZ'
      ?}
   ?};
   {? _wym[4]<>'' & _zwrot=''
   || {? ETSCHPOD.ZN_WYM4='S' & UD_POM.WYM4=''
      || __CHK.err_empty(UD_POM.NAZ_WYM4+' - stała wartość'); _zwrot:='WYM4'
      |? ETSCHPOD.ZN_WYM4='F' & ETSCHPOD.FWYM4=null
      || __CHK.err_empty(UD_POM.NAZ_WYM4+' - formuła'); _zwrot:='FWYM4'
      ?}
   ?};
   {? _wym[5]<>'' & _zwrot=''
   || {? ETSCHPOD.ZN_WYM5='S' & UD_POM.WYM5=''
      || __CHK.err_empty(UD_POM.NAZ_WYM5+' - stała wartość.'); _zwrot:='WYM5'
      |? ETSCHPOD.ZN_WYM5='F' & ETSCHPOD.FWYM5=null
      || __CHK.err_empty(UD_POM.NAZ_WYM5+' - formuła.'); _zwrot:='FWYM5'
      ?}
   ?};
   {? _zwrot='' & ETSCHPOD.KW_F=null
   || __CHK.err_empty('Formuła na kwotę'); _zwrot:='KW_F'
   ?};
   {? _zwrot=''
   || ETSCHPOD.UDZIAL:=ETSCHPOD.UDZIAL$2;
      {? ~ETSCHPOD.UDZIAL
      || __CHK.err_empty('Procent kwoty'); _zwrot:='UDZIAL'
      |? ETSCHPOD.UDZIAL<0 | ETSCHPOD.UDZIAL>100
      || FUN.emsg('Nieprawidłowy procent kwoty. Dopuszczalne wartości większe od 0 i nie większe niż 100.'@);
         _zwrot:='UDZIAL'
      ?}
   ?};
   {? _wym[1]<>'' & ETSCHPOD.ZN_PBUD='S'
   || ETSCHPOD.PBUD:=exec('szukaj_ud_skl','schemat',_typ[1],UD_POM.PBUD);
      ETSCHPOD.FPBUD:=null
   || ETSCHPOD.PBUD:=null
   ?};
   {? _wym[2]<>'' & ETSCHPOD.ZN_JORG='S'
   || ETSCHPOD.JORG:=exec('szukaj_ud_skl','schemat',_typ[2],UD_POM.JORG);
      ETSCHPOD.FJORG:=null
   || ETSCHPOD.JORG:=null
   ?};
   {? _wym[3]<>'' & ETSCHPOD.ZN_OKOSZ='S'
   || ETSCHPOD.OKOSZ:=exec('szukaj_ud_skl','schemat',_typ[3],UD_POM.OKOSZ);
      ETSCHPOD.FOKOSZ:=null
   || ETSCHPOD.OKOSZ:=null
   ?};
   {? _wym[4]<>'' & ETSCHPOD.ZN_WYM4='S'
   || ETSCHPOD.WYM4:=exec('szukaj_ud_skl','schemat',_typ[4],UD_POM.WYM4);
      ETSCHPOD.FWYM4:=null
   || ETSCHPOD.WYM4:=null
   ?};
   {? _wym[5]<>'' & ETSCHPOD.ZN_WYM5='S'
   || ETSCHPOD.WYM5:=exec('szukaj_ud_skl','schemat',_typ[5],UD_POM.WYM5);
      ETSCHPOD.FWYM5:=null
   || ETSCHPOD.WYM5:=null
   ?}
?};
win_disp();
_zwrot


\epodz_add_aut
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Automatyczne generowanie podziałów dla dokumentu w obiegu
::----------------------------------------------------------------------------------------------------------------------
{?  app_info('web_sesid')=''
|| EPODZ.cntx_psh(); ETSCHPDN.cntx_psh(); ETSCHPDR.cntx_psh()
?};
EPODZ.use('skid_j'+(EDOKUM.name()+2)); EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
ETSCHPDN.index('UNIK'); ETSCHPDN.prefix();
ETSCHPDR.index('WYB'); ETSCHPDR.prefix(EDOKUM.TYP,REF.FIRMA,'T');
{? ETSCHPDR.first()
|| {? ETSCHPDR.size()=1
   || ETSCHPDR.ETSCHPDN(); exec('e_con_sch','tagger'); exec('epodz_add_aut1','obiegi2');
      {? app_info('web_sesid')<>'' || web_top_refresh() ?}
   || {? app_info('web_sesid')<>''
      || EDOKUM.web_cntx_save();
         ETSCHPDR.web_select('WYB_WT')
      || exec('etschpdr_wyb_hlp','tagger'); ETSCHPDR.win_sel('WYB'); ETSCHPDR.select()
      ?}
   ?}
|| FUN.info('Nie zdefiniowano schematów tworzenia podziałów dla wybranego typu faktury\n'+
            'i firmy, w której została zarejestrowana.'@)
?};
{? app_info('web_sesid')=''
|| ETSCHPDR.cntx_pop(); ETSCHPDN.cntx_pop(); EPODZ.cntx_pop()
?}


\epodz_add_aut1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Automatyczne generowanie podziałów dla dokumentu w obiegu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
ETSCHPOD.cntx_psh();
ETSCHPOD.index('GENERUJ'); ETSCHPOD.prefix(ETSCHPDN.ref(),'D');
{? ETSCHPOD.first()
|| exec('epodz_add_aut2','obiegi2')
?};
ETSCHPOD.index('GENERUJ'); ETSCHPOD.prefix(ETSCHPDN.ref(),'V');
{? ETSCHPOD.first()
|| EVAT.cntx_psh(); EVAT.use('skid_a'+(EDOKUM.name()+2));
   EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
   {? EVAT.first()
   || {! |?
         exec('epodz_add_aut2','obiegi2');
         EVAT.next()
      !}
   ?};
   EVAT.cntx_pop()
?};
ETSCHPOD.cntx_pop()


\epodz_add_aut2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Automatyczne generowanie podziałów dla dokumentu w obiegu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? ETSCHPOD.first()
|| {! |?
      _warunek:={? ETSCHPOD.FORM_WAR || ($(ETSCHPOD.FORM_WAR().FORMULA))() || 1 ?};
      {? _warunek || exec('add_w_etschpod','obiegi2') ?};
      ETSCHPOD.next()
   !}
?}


\etschpdr_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Automatyczne generowanie podziałów dla dokumentu w obiegu - wybór schematu
::----------------------------------------------------------------------------------------------------------------------
ETSCHPDN.index('UNIK'); ETSCHPDN.prefix(); ETSCHPDR.ETSCHPDN(); exec('e_con_sch','tagger');
{? app_info('web_sesid')=''
|| exec('epodz_add_aut1','obiegi2');
   sel_exit()
|| EDOKUM.web_cntx_load();
   exec('maski_w','obiegi',1);
   EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
   exec('epodz_add_aut1','obiegi2');
   ETSCHPDR.web_close();
   web_top_refresh(1)
?}


\add_w_etschpod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Tworzenie podzialów dla ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ');
UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_DEF.prefix();
SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix();
EPODZ.blank();
EPODZ.EDOKUM:=EDOKUM.ref();
EPODZ.EZAPOT:=EPODZ.EDOKRZAP:=EPODZ.EDOKUMPR:=null;
EPODZ.USERS:=OPERATOR.USER;
EPODZ.SKID_MB:=ETSCHPOD.SKID_MB;
{? EPODZ.SKID_MB
|| EPODZ.SKID_MB();
   SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
   _ile_wym:=SKID_MBP.size();
   SKID_MBP.cntx_pop();
   _dalej:=0;
   {? _ile_wym>0
   || SKID_MBP.prefix(SKID_MBN.ref(),1);
      {? SKID_MBP.first()
      || _skl:={? ETSCHPOD.ZN_PBUD='F'
               || ($(ETSCHPOD.FPBUD().FORMULA))()
               || ETSCHPOD.PBUD
               ?};
         {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
         || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.PBUD:=UD_DEF.UD_SKL
            ?}
         |? type_of(_skl)=type_of('') & _skl<>''
         || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.PBUD:=UD_DEF.UD_SKL
            ?}
         ?}
      ?};
      SKID_MBP.prefix(SKID_MBN.ref(),2);
      {? _dalej & SKID_MBP.first()
      || _dalej:=0;
         _skl:={? ETSCHPOD.ZN_JORG='F'
               || ($(ETSCHPOD.FJORG().FORMULA))()
               || ETSCHPOD.JORG
               ?};
         {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
         || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.JORG:=UD_DEF.UD_SKL
            ?}
         |? type_of(_skl)=type_of('') & _skl<>''
         || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.JORG:=UD_DEF.UD_SKL
            ?}
         ?}
      ?};
      SKID_MBP.prefix(SKID_MBN.ref(),3);
      {? _dalej & SKID_MBP.first()
      || _dalej:=0;
         _skl:={? ETSCHPOD.ZN_OKOSZ='F'
               || ($(ETSCHPOD.FOKOSZ().FORMULA))()
               || ETSCHPOD.OKOSZ
               ?};
         {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
         || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.OKOSZ:=UD_DEF.UD_SKL
            ?}
         |? type_of(_skl)=type_of('') & _skl<>''
         || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.OKOSZ:=UD_DEF.UD_SKL
            ?}
         ?}
      ?};
      SKID_MBP.prefix(SKID_MBN.ref(),4);
      {? _dalej & SKID_MBP.first()
      || _dalej:=0;
         _skl:={? ETSCHPOD.ZN_WYM4='F'
               || ($(ETSCHPOD.FWYM4().FORMULA))()
               || ETSCHPOD.WYM4
               ?};
         {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
         || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.WYM4:=UD_DEF.UD_SKL
            ?}
         |? type_of(_skl)=type_of('') & _skl<>''
         || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.WYM4:=UD_DEF.UD_SKL
            ?}
         ?}
      ?};
      SKID_MBP.prefix(SKID_MBN.ref(),5);
      {? _dalej & SKID_MBP.first()
      || _dalej:=0;
         _skl:={? ETSCHPOD.ZN_WYM5='F'
               || ($(ETSCHPOD.FWYM5().FORMULA))()
               || ETSCHPOD.WYM5
               ?};
         {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
         || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.WYM5:=UD_DEF.UD_SKL
            ?}
         |? type_of(_skl)=type_of('') & _skl<>''
         || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
            {? UD_DEF.first()
            || _dalej:=1;
               EPODZ.WYM5:=UD_DEF.UD_SKL
            ?}
         ?}
      ?}
   ?}
|| _ok:=0
?};
{? _dalej
|| _prec:=2;
   SKIDXDUD.prefix(EPODZ.PBUD);
   {? SKIDXDUD.first()
   || _il:=(SKIDXDUD.WART_IL='I'); _prec:=SKIDXDUD.PREC;
      {? SKIDXDUD.WART_IL='W'
      || EPODZ.WAL:={? SKIDXDUD.WSK_WAL='T'
                    || EDOKUM.WAL
                    || exec('wal_nar','dok_fks')
                    ?}
      || EPODZ.JM:=SKIDXDUD.JM
      ?}
   || _il:=0
   ?};
   _kwota:=($(ETSCHPOD.KW_F().FORMULA))()*ETSCHPOD.UDZIAL/100;
   {? _il
   || EPODZ.WART:=_kwota$_prec
   || EPODZ.WART:=_kwota$2
   ?};
   {? EPODZ.WART <> 0
   || EPODZ.add()
   ?}
?};
SKID_MBP.cntx_pop(); UD_DEF.cntx_pop(); SKIDXDUD.cntx_pop()


\etschpdn_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Usuwanie rekordów w ETSCHPDN
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć schemat tworzenia podziałów?'@)
|| ETSCHPOD.cntx_psh(); ETSCHPOD.index('UNIK'); ETSCHPOD.prefix(ETSCHPDN.ref());
   ETSCHPDR.cntx_psh(); ETSCHPDR.index('ETSCHPDN'); ETSCHPDR.prefix(ETSCHPDN.ref());
   do();
   {? ETSCHPOD.first()
   || {! |?
         _delr:=ETSCHPOD.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !}
   ?};
   {? ETSCHPDR.first()
   || {! |?
         _delr:=ETSCHPDR.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !}
   ?};
   _delr:=ETSCHPDN.del(,1);
   {? _delr=0 || undo() ?};
   end();
   ETSCHPOD.cntx_pop(); ETSCHPDR.cntx_pop()
?};
1


\etschpdn_rpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Rekord po dla tabeli ETSCHPDN
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=__CHK.record(ETSCHPDN,,'NAZWA');
{? _zwrot=''
|| {? __CHK.index(ETSCHPDN,-menu_txt(,)='popraw')<>'' || _zwrot:='NAZWA' ?}
?};
_zwrot


\etschpdn_arfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Po odświeżeniu dla tabeli ETSCHPDN
::----------------------------------------------------------------------------------------------------------------------
ETSCHPOD.index('DISP');
{? ETSCHPDN.get()
|| ETSCHPOD.actions_grayed('WER','');
   ETSCHPOD.prefix(ETSCHPDN.ref())
|| ETSCHPOD.actions_grayed('WER','DPU:D');
   ETSCHPOD.prefix(null)
?};
{? ~ETSCHPOD.get() || ETSCHPOD.first() ?};
grp_disp(ETSCHPOD,'WER',1,1)


\etschpod_bobs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Przed obsługą dla tabeli ETSCHPOD
::----------------------------------------------------------------------------------------------------------------------
1


\etschpdr_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Dołączanie rekordów do tabeli ETPSCHPDR
::----------------------------------------------------------------------------------------------------------------------
ETSCHPDN.win_sel('WER_GR');
ETSCHPDN.index('UNIK'); ETSCHPDN.prefix();
ETSCHPDN.select();
1


\etschpdr_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Usuwanie rekordów z tabeli ETSCHPDR
::----------------------------------------------------------------------------------------------------------------------
ETSCHPDR.del()


\etschpdn_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Dołączanie rekordów do tabeli ETPSCHPDR
::----------------------------------------------------------------------------------------------------------------------
ETSCHPDR.cntx_psh();
ETSCHPDR.index('UNIK'); ETSCHPDR.prefix(REF.FIRMA,ETYPY.ref(),ETSCHPDN.ref());
{? ETSCHPDR.first()
|| FUN.info('Schemat jest już wybrany dla bieżącego typu i firmy.'@)
|| ETSCHPDR.blank();
   ETSCHPDR.FIRMA:=REF.FIRMA;
   ETSCHPDR.ETYPY:=ETYPY.ref();
   ETSCHPDR.ETSCHPDN:=ETSCHPDN.ref();
   ETSCHPDR.add();
   sel_exit()
?};
ETSCHPDR.cntx_pop();
1


\etschpdr_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Aktywacja rekordów w tabeli ETSCHPDR
::----------------------------------------------------------------------------------------------------------------------
{? ETSCHPDR.AKT<>'T'
|| ETSCHPDR.cntx_psh(); ETSCHPDR.prefix(); ETSCHPDR.AKT:='T'; ETSCHPDR.put(); ETSCHPDR.cntx_pop()
?};
1


\etschpdr_deakt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Dezaktywacja rekordów w tabeli ETSCHPDR
::----------------------------------------------------------------------------------------------------------------------
{? ETSCHPDR.AKT='T'
|| ETSCHPDR.cntx_psh(); ETSCHPDR.prefix(); ETSCHPDR.AKT:='N'; ETSCHPDR.put(); ETSCHPDR.cntx_pop()
?};
1


\etschpdr_rkprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Rekord przed dla tabeli ETSCHPDR
::----------------------------------------------------------------------------------------------------------------------
{? ETSCHPDR.AKT='T'
|| ETSCHPDR.actions_grayed('WER','A')
|| ETSCHPDR.actions_grayed('WER','E')
?};
0


\etschpdr_bobs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.42]
:: OPIS: Przed obsługą dla tabeli ETSCHPDR (webterm)
::----------------------------------------------------------------------------------------------------------------------
1


\procvat4xml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca forme platnosci na podstawie napisu z xml-a
::   WE: _a - platnosc z xml-a (np.: przelew)
::   WY: SLO.ref
::  OLD: \procvat4xml/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
SLU.index('NAZ'); SLU.prefix('~STAWKI VAT PL');
{? SLU.first()
|| SLO.index('SL'); SLO.prefix();
   {? SLO.find_key(SLU.ref(),_a,) | SLO.find_key(SLU.ref(),' '+_a,)
   || SLO.ref()
   || {? -_a*'zw'>0
      || _kod:='Zwol.'
      || _a:=STR.gsub(_a,'%','');
         _pr:=#STR.gsub(_a,',','.');
         _kod:={? _pr<10 || ' ' || '' ?}+$(_pr$0)+' %'
      ?};
      {? SLO.find_key(SLU.ref(),_kod,) || SLO.ref() || null ?}
   ?}
|| null
?}


\win_red_wnioski
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Zwraca okno dynamiczne do edycji wniosków w obiegu
::   WE:  _a  - typ okna: 'W'-wprowadzanie, 'P'-przekazanie, 'A'-akceptacja, 'D'-wyświetlenie, 'I','i'-informacje dodatkowe
::       [_b] - numer kolekcji informacji dodatkowych
::       [_c] - edycja innych danych?
::----------------------------------------------------------------------------------------------------------------------
_akc:={? var_press('_a')>0 || _a || 'W' ?};
_kolekcja:={? var_press('_b')>0 || _b || 1 ?};
_ed_inn:={? var_press('_c')>0 || _c || 1 ?} & (_akc='W' | _akc='I');
_editInnd:={? var_press('_c')>0 || _c || 1 ?} & 'AP'*_akc>0;
_all:=0;
_edit:="'editable='+{? _a || '1' || {? var_pres('_b')>0 & _b=0 || '0' || 'grayed' ?} ?}+{? 0 & _a=0 || ',label_color=200:200:200' || '' ?}";
exec('set_wt_par','obiegi2','dynamic',1);
exec('set_wt_par','obiegi2','akc',_akc);
_w_str:=60;
_w_number:=13;
_red:=web_define();
EDOKUM.TYP();
{? -_akc<>'i'
|| web_def_sep(_red,'Dane ogólne'@);
   {? ETYPY.CZY_DOPE='T' | _all
   || web_def_fld(_red,'DOP',EDOKUM.DOP,'Operacja z dnia'@,,,10,,{? _akc='W'
                                                                 || 'mark=1'
                                                                 || _edit(0,0)
                                                                 ?});
      web_def_fld_fml(_red,'DOP','AFTER_EDIT',"
         EDOKUM.web_cntx_load();
         _flds:=web_def_get(web_top_win());
         exec('set_edokum','obiegi2',_flds);
         _wyn:=exec('aedopdob','obiegi',0);
         {? var_press('DW',_flds)>0 || _flds.DW:=EDOKUM.DW ?};
         {? var_press('TP',_flds)>0 || _flds.TP:=EDOKUM.TP ?};
         web_def_update(web_top_win(),_flds,);
         _wyn
      ");
      ~~
   ?};
   {? exec('be_id_edokum1','obiegi')
   || web_def_fld(_red,'ID',EDOKUM.ID,'Identyfikator',,30,_w_str,,'mark=1'+{? _akc='W' || ',editable=1' || ','+_edit(0,0) ?})
   ?};
   web_def_rel(_red,'TYP',EDOKUM.TYP,ETYPY,'NAZWA','UNIK','SLO_WYBW','RED','Typ wniosku'@,,50,_w_str-3,,'mark=0,'+_edit(0));
   OPERATOR.USER:=exec('user_ident_web','#users');
   web_def_fld(_red,'DOSTAWCA',{? EDOKUM.DOSTAWCA<>null
                               || OBIEGI.OSOBAID:=EDOKUM.DOSTAWCA().IMEX;
                                  OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO
                               || OBIEGI.OSOBAID:=0;
                                  ''
                               ?},'Osoba'@,,,_w_str-3,,'mark=1'+{? ETYPY.CZY_PRAC & _akc='W' || '' || ','+_edit(0) ?});
   web_def_fld_fml(_red,'DOSTAWCA','F3',"{? EDOKUM.DOSTAWCA<>null
                                         || _imie:=EDOKUM.DOSTAWCA().PIERWSZE;
                                            _nazwisko:=OSOBA.NAZWISKO;
                                            {? (30+(-(_imie+' '+_nazwisko)))=OBIEGI.OSOBAOBI
                                            || OBIEGI.OSOBAOBI:=_imie+' '+_nazwisko;
                                               OBIEGI.OSOBAID:=OSOBA.IMEX
                                            ?}
                                         || OBIEGI.OSOBAID:=0;
                                            OBIEGI.OSOBAOBI:=''
                                         ?};
                                         EDOKUM.web_cntx_load(); exec('env_wt','b_proces');
                                         exec('prac_firm_f3','obiegi2')");
   web_def_fld_fml(_red,'DOSTAWCA','AFTER_EDIT',"exec('env_wt','b_proces');
                                                 OBIEGI.OSOBAOBI:=web_def_get().DOSTAWCA;
                                                 {? var_pres('__osobaF3')>0 & __osobaF3<>OBIEGI.OSOBAOBI
                                                 || VAR_DEL.delete('__osobaF3')
                                                 ?};
                                                 EDOKUM.web_cntx_load();
                                                 exec('prac_firm_af_www','obiegi2')
                                                ");
   web_def_fld(_red,'ALERTY',EDOKUM.ALERTY,'Wysyłanie email'@,,3,_w_str,,{? _akc='W' || '' || _edit(0,0) ?},'check-box','T','N');
   {? ETYPY.CZY_WAR | _all
   || web_def_sep(_red,'Wartości'@);
      web_def_rel(_red,'WAL',EDOKUM.WAL,SLO,'KOD','SL','DICTW_WT','RED','Waluta transakcji'@,,10,_w_number-3,,{? _akc='W' || '' || _edit(0,0) ?});
      {? ETYPY.CZY_WAR=2 | ETYPY.CZY_WAR=3
      || web_def_fld(_red,'NETTO',EDOKUM.NETTO,'Wartość netto'@,,,_w_number,2,{? _akc='W' || '' || _edit(0,0) ?})
      ?};
      {? ETYPY.CZY_WAR=1 | ETYPY.CZY_WAR=3
      || web_def_fld(_red,'WART',EDOKUM.WART,'Wartość brutto'@,,,_w_number,2,{? _akc='W'
                                                                             || {? ETYPY.CZY_ZALP='T'
                                                                                || 'mark=1'
                                                                                || ''
                                                                                ?}
                                                                             || _edit(0,0)
                                                                             ?})
      ?};
      {? ETYPY.ZAPOT || web_def_fld(_red,'TP',EDOKUM.TP,'Termin'@,,,10,,{? _akc='W' || '' || _edit(0,0) ?}) ?}
   ?};
   {? ETYPY.CZY_DATY | _all
   || web_def_sep(_red,'Zakres dat i godzin'@);
      web_def_fld(_red,'DATA_OD',EDOKUM.DATA_OD,'Od dnia'@,,,10,,{? _akc='W' || '' || _edit(0,0) ?});
      {? ETYPY.CZY_DATY=2 | _all
      || web_def_fld(_red,'GODZ_OD',EDOKUM.GODZ_OD,'Od godziny'@,,,10,,{? _akc='W' || '' || _edit(0,0) ?})
      ?};
      web_def_fld(_red,'DATA_DO',EDOKUM.DATA_DO,'Do dnia'@,,,10,,{? _akc='W' || '' || _edit(0,0) ?});
      {? ETYPY.CZY_DATY=2 | _all
      || web_def_fld(_red,'GODZ_DO',EDOKUM.GODZ_DO,'Do godziny'@,,,10,,{? _akc='W' || '' || _edit(0,0) ?})
      ?}
   ?};
   web_def_sep(_red,'Opis'@);
   {? ETYPY.ED_TEMAT='T'
   || web_def_fld(_red,'TR',EDOKUM.TR,'Temat'@,,100,_w_str,,'mark=1'+{? _akc='W' || '' || ','+_edit(0,0) ?})
   ?};
   web_def_fld(_red,'UW_OPDL',EDOKUM.memo_get(,'UW_OPDL'); EDOKUM.memo_txt(,1,'UW_OPDL'),'Treść'@,,3000,_w_str,,{? _akc='W' || '' || _edit(0,0) ?});
   {? ETYPY.CZY_KH | _all
   || web_def_sep(_red,'Kontrahent'@);
      SKID.SL_KH:={? ETYPY.DOM_SL || ETYPY.DOM_SL().SLU().NAZ || '' ?};
      web_def_fld(_red,'SL_KH',SKID.SL_KH,'Źródło pochodzenia'@,,,_w_str-3,,{? _akc='W' || '' || _edit(0,0) ?});
      web_def_fld_fml(_red,'SL_KH','F3',"EDOKUM.web_cntx_load();exec('f3sldob','obiegi')");
      SKID.NIP:={? EDOKUM.KHKH || EDOKUM.KHKH().NIP || '' ?};
      web_def_fld(_red,'NIP',SKID.NIP,'NIP',,,_w_number,,{? _akc='W' || '' || _edit(0,0) ?});
      web_def_fld(_red,'KH',EDOKUM.KH().KOD,'Kod'@,,,_w_number-3,,{? _akc='W' || '' || _edit(0,0) ?});
      web_def_fld_fml(_red,'KH','F3',"EDOKUM.web_cntx_load(); exec('f3kh','obiegi2')");
      web_def_fld_fml(_red,'KH','AFTER_EDIT',"
         EDOKUM.web_cntx_load();
         _upd:=0;
         _slo:=null;
         _flds:=web_def_get();
         {? _flds.SL_KH<>'' & _flds.KH<>''
         || _flds.KH_SLO:=_slo:=exec('find_slo_slu','slo_slu',_flds.SL_KH,_flds.KH);
            _upd:=1
         |? _flds.KH<>''
         || RS.index('RS'); RS.prefix();
            SLO.cntx_psh();
            SLO.index('KOD');
            SLO.prefix(_flds.KH,);
            {? SLO.first()
            || {!
               |? {? RS.find_key(SLO.SLU().WZ,) & RS.TAB='KH'
                  || _slo:=SLO.ref()
                  ?};
                  _slo=null & SLO.next()
               !}
            ?};
            SLO.cntx_pop();
            _flds.KH_SLO:=_slo;
            _upd:=1
         ?};
         {? _slo
         || SLO.prefix();
            {? SLO.seek(_slo)
            || KH.index('KOD'); KH.prefix(2,SLO.KOD);
               {? KH.first()
               || _flds.NIP:=KH.NIP
               ?}
            ?}
         || _flds.KH_SLO:=null
         ?};
         web_def_update(web_top_win(),_flds,);
         1
      ");
      web_def_rel(_red,'KH_SLO',EDOKUM.KH,SLO,'TR','SL','DICTW_WT','RED','Nazwa'@,,50,_w_str-3,,_edit(0))
   ?};
   {? ETYPY.CZY_ZALP='T' | _all
   || web_def_sep(_red,'Dane zaliczki'@);
      web_def_fld(_red,'KASPRZEL',EDOKUM.KASPRZEL,'Realizacja poprzed'@,,1,,,{? _akc='W' || '' || _edit(0,0) ?},'radio-buttons','Kasę'@,'K','Przelew'@,'P');
      web_def_fld_fml(_red,'KASPRZEL','AFTER_EDIT',$('exec(\'wni_aekasprz_web\',\'zaliczki\')'));
      web_def_fld(_red,'N',EDOKUM.N,'Rachunek bankowy pracownika'@,,50,_w_str,,{? _akc='W'
                                                                               || {? EDOKUM.KASPRZEL='P' || 'mark=1' || 'mark=0' ?}
                                                                               || _edit(0,0)
                                                                               ?});
      web_def_fld_fml(_red,'N','F3',"exec('wni_f3_ezaln','zaliczki')");
      web_def_fld(_red,'PLACE',EDOKUM.PLACE,'Rozliczane w płacach'@,,1,,,{? _akc='W' || '' || _edit(0,0) ?},'check-box','T','N');
      web_def_fld_fml(_red,'PLACE','AFTER_EDIT',$('exec(\'wni_aeplace_web\',\'zaliczki\')'));
      web_def_fld(_red,'POTRODDN',EDOKUM.POTRODDN,'Od dnia'@,,,10,,{? _akc='W' || {? EDOKUM.PLACE='T' || 'mark=1' || 'mark=0' ?} || _edit(0,0) ?})
   ?};
   {? ETYPY.DEKR='T' | _all | EDOKUM.ODD=null
   || web_def_sep(_red,'Dekretacja wniosku'@);
      web_def_rel(_red,'ODD',EDOKUM.ODD,ODD,'OD','ODDZIALY','SLO_WT','RED','Jednostka księgowa'@,,50,_w_str-3,,'mark=0'+{? _akc='W' || '' || ','+_edit(0,0) ?});
      {? ETYPY.DEKR='T'
      || web_def_fld(_red,'DATADOK',EDOKUM.DATDOK,'Data dokumentu'@,,,10,,_edit(0));
         web_def_fld(_red,'REJ',EDOKUM.REJ().KOD,'Rejestr'@,,,10,,_edit(0));
         web_def_fld(_red,'NREJ',EDOKUM.NREJ,'Numer w rejestrze'@,,,10,,_edit(0))
      ?}
   ?}
?};
:: Informacje dodatkowe
{? EDOKUM.TYP().IDINHEAD | -_akc='i'
|| _dodac:=_kolekcja=-1;
   EDOK_ATR.cntx_psh();
   EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
   EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',);
   {? (~EDOK_ATR.first() | _dodac)
   || refr_okn:=0;
      OBIEGI.TYP_ID:='D';
      _msg:=exec('edk_atr_dolacz','obiegi',0,0,1);
      VAR_DEL.delete('refr_okn');
      {? _dodac
      || _kolekcja:=exec('edk_atr_lastpoz','obiegi',EDOKUM.ref())
      ?}
   ?};
   exec('set_wt_par','obiegi2','NrID',_kolekcja);
   ETYP_ATR.cntx_psh();
   ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null);
   _is_def_atr:=ETYP_ATR.first();
   EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_kolekcja);
   {? EDOK_ATR.first()
   || _first:=1;
      {? EDOKUM.TYP().ATR_ONE=0
      || web_def_sep(_red,'Informacja dodatkowa'@);
         web_def_fld(_red,'POZ',EDOK_ATR.REKORD,'Pozycja',,,_w_number,,_edit(0));
         _first:=0
      ?};
      {!
      |? {? EDOK_ATR.TAT
         || _pole:='P'+$EDOK_ATR.KOL;
            _typ:=EDOK_ATR.TAT().TYP;
            ETYP_ATR.find_key(EDOK_ATR.KOL);
            _opt:=_edit(ETYP_ATR.EDIT='N' & _ed_inn,{? ETYP_ATR.EDIT='N' || 0 || 1 ?})+',mark='+$(ETYP_ATR.WYMAG='T');
            {? _first
            || _first:=0;
               {? _typ<>'O'
               || web_def_sep(_red,'Informacja dodatkowa'@)
               ?}
            ?};
            {? _typ='O'
:: Nagłowek
            || {? EDOK_ATR.WAR<>'' || web_def_sep(_red,EDOK_ATR.WAR) ?}
            |? _typ='L'
:: Liczba
            || web_def_fld(_red,_pole,#gsub(EDOK_ATR.WAR,',','.'),EDOK_ATR.TAT().OPIS,,,_w_number,EDOK_ATR.TAT().PREC,_opt)
            |? _typ='B'
:: Tak/nie
            || web_def_fld(_red,_pole,1+EDOK_ATR.WAR,EDOK_ATR.TAT().OPIS,,1,1,,_opt,'check-box','T','N')
            |? _typ='D'
:: Data
            || _data:={? EDOK_ATR.WAR='' | +EDOK_ATR.WAR<>10
                      || date(0,0,0)
                      || date(#(4+EDOK_ATR.WAR),#(5-EDOK_ATR.WAR-3),#(EDOK_ATR.WAR+2))
                      ?};
               web_def_fld(_red,_pole,_data,EDOK_ATR.TAT().OPIS,,,10,,_opt)
            |? _typ='C'
:: czas
            || _czas:={? EDOK_ATR.WAR=''
                      || time(0,0,0)
                      |? +EDOK_ATR.WAR=8
                      || time(#(2+EDOK_ATR.WAR),#(3-EDOK_ATR.WAR-3),#(EDOK_ATR.WAR+2))
                      |? +EDOK_ATR.WAR=5
                      || time(#(2+EDOK_ATR.WAR),#(EDOK_ATR.WAR+2),0)
                      || time(0,0,0)
                      ?};
               web_def_fld(_red,_pole,_czas,EDOK_ATR.TAT().OPIS,,10,10,,_opt)
            |? _akc<>'D' & _typ='S' & EDOK_ATR.TAT().SLU
:: Słownik
            || SLO.cntx_psh();
               SLO.index('SL');
               SLO.prefix(EDOK_ATR.TAT().SLU);
               {? SLO.first()
               || _fml:='web_def_fld(_a,_b,EDOK_ATR.WAR,EDOK_ATR.TAT().OPIS,,255,_c,,_d,\'combo-box\',\'\',\'\'';
                  {!
                  |? _etykieta:=SLO.KOD+' '+SLO.TR;
                     _value:={? ETYP_ATR.SLOPOLE='K' || SLO.KOD || SLO.TR ?};
                     _fml+=',\''+_etykieta+'\',\''+_value+'\'';
                     SLO.next()
                  !};
                  _fml+=')';
                  ($_fml)(_red,_pole,_w_str-3,_opt)
               ?};
               SLO.cntx_pop()
            |? _typ='U' & EDOK_ATR.TAT().UD_SCH
:: Schemat struktury
            || _ud_def:=null;
               UD_DEF.cntx_psh();
               {? EDOK_ATR.UD_SKL
               || UD_DEF.index('PODTEC'); UD_DEF.prefix(EDOK_ATR.TAT().UD_SCH,EDOK_ATR.UD_SKL);
                  {? UD_DEF.first()
                  || _ud_def:=UD_DEF.ref()
                  ?}
               ?};
               UD_SCH.cntx_psh();
               UD_SCH.prefix();
               UD_SCH.seek(EDOK_ATR.TAT().UD_SCH);
               UD_SCH.web_cntx_save(1);
               UD_SCH.cntx_pop();
               {? ETYP_ATR.SLOPOLE='K'
               || UD_DEF.index('SYMBOL'); UD_DEF.prefix(EDOK_ATR.TAT().UD_SCH);
                  web_def_rel(_red,_pole,_ud_def,UD_DEF,'SYMBOL','SYMBOL','WER_WTTR','RED_SMPW',EDOK_ATR.TAT().OPIS,,,_w_str-3,,_opt)
               || UD_DEF.index('OPIS'); UD_DEF.prefix(EDOK_ATR.TAT().UD_SCH);
                  web_def_rel(_red,_pole,_ud_def,UD_DEF,'OPIS','OPIS','WER_WTTR','RED_SMPW',EDOK_ATR.TAT().OPIS,,,_w_str-3,,_opt)
               ?};
               UD_DEF.cntx_pop()
            |? _akc<>'D' & _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
            || {? ETYP_ATR.SLO_CHG
               || web_def_fld(_red,_pole,EDOK_ATR.WAR,EDOK_ATR.TAT().OPIS,,255,_w_str-3,,_opt);
                  EDOK_ATR.web_cntx_save();
                  web_def_fld_fml(_red,_pole,'F3',$('exec(\'f3_x\',\'obiegi2\','+$EDOK_ATR.KOL+')'))
               || _tab:=($ETYP_ATR.TSLO)();
                  {? type_of(_tab)>100 & var_press('KOD',_tab)>0 & var_press('TR',_tab)>0 & var_press('REF_SQL',_tab)>0 &
                     _tab.first()
                  || _fml:='web_def_fld(_a,_b,EDOK_ATR.WAR,EDOK_ATR.TAT().OPIS,,255,_c,,_d,\'combo-box\',\'\',\'\'';
                     {? EDOK_ATR.WAR<>'' || _fml+=',\''+EDOK_ATR.WAR+'\',\''+EDOK_ATR.WAR+'\'' ?};
                     {!
                     |? _txt:=gsub(gsub({? ETYP_ATR.SLOPOLE='K' || _tab.KOD || _tab.TR ?},'\\','\\\\'),'''','\\''');
                        _fml+=',\''+{? ETYP_ATR.SLOPOLE='K' || _tab.KOD+' - '+_tab.TR || _tab.TR ?}+'\',\''+_txt+'\'';
                        _tab.next()
                     !};
                     _fml+=')';
                     ($_fml)(_red,_pole,_w_str-3,_opt)
                  || web_def_fld(_red,_pole,EDOK_ATR.WAR,EDOK_ATR.TAT().OPIS,,255,_w_str,,_opt)
                  ?};
                  &_tab
               ?}
            || web_def_fld(_red,_pole,EDOK_ATR.WAR,EDOK_ATR.TAT().OPIS,,255,_w_str,,_opt)
            ?};
            {? ETYP_ATR.AE<>'' | _typ='X' | _typ='U' | _typ='S'
            || exec('set_wt_par','obiegi2','edok',$EDOKUM.ref());
               web_def_fld_fml(_red,_pole,'AFTER_EDIT',$('exec(\'ae_ud\',\'obiegi2\','+$EDOK_ATR.KOL+')'))
            ?}
         ?};
         EDOK_ATR.next()
      !}
   ?};
   EDOK_ATR.cntx_pop();
   ETYP_ATR.cntx_pop()
|| _is_def_atr:=0
?};

_grey:=Plugin.run('ACT_GREY_EDOKUM_001',OPERATOR.USER,'tmp',_akc);
{? type_of(_grey)<>type_of('') || _grey:='' ?};
:: Przyciski
{? -_akc='i'
|| {? _akc='I'
   || web_def_btn(_red,'OK','text=OK,key=F2,default=1'+{? _grey*'OK' || ',state=grayed' || '' ?})
   ?}
|| {? (~ETYPY.IDINHEAD & ETYPY.W_PORTAL='N')
   || web_def_btn(_red,'INNE_DANE','text=&Informacje'@+ {? _grey*'INNE_DANE' || ',state=grayed' || '' ?})
   ?};
   {? ETYPY.OBS_PROJ
   || web_def_btn(_red,'PROJEKTY','text=&Projekty'@+ {? _grey*'PROJEKTY' || ',state=grayed' || '' ?})
   ?};
   {? ETYPY.ED_PODZ='T'
   || web_def_btn(_red,'SKIDXD','text=Po&działy'@+ {? _grey*'SKIDXD' || ',state=grayed' || '' ?})
   ?};
   {? EDOKUM.DOK_POW<>''
   || web_def_btn(_red,'DOK_POW','text=Dokument z logi&styki'@+ {? _grey*'DOK_POW' || ',state=grayed' || '' ?})
   ?};
   web_def_btn(_red,'DOKUM','text=&Załączniki'@+ {? _grey*'DOKUM' || ',state=grayed' || '' ?});
   {? ETYPY.ATR_ONE=0 & _is_def_atr
   || _max:=exec('edk_atr_lastpoz','obiegi',EDOKUM.ref());
      web_def_btn(_red,'PREV','text=Pop&rzednia,state=grayed,tooltip=Poprzednia pozycja informacji dodatkowych');
      web_def_btn(_red,'NEXT','text=&Następna'+{? ((_akc='D' & _max<=1) | ~_ed_inn) || ',state=grayed' || '' ?}+',tooltip=Następna pozycja informacji dodatkowych');
      {? _akc<>'D' & _ed_inn
      || web_def_btn(_red,'DEL','text=&Usuń,state='+{? _grey*'DEL' | ~_ed_inn || 'grayed' || {? _max>1 || 'normal' || 'grayed' ?} ?}+',tooltip=Usuń pozycję informacji dodatkowych')
      ?}
   ?};
   {? ETYPY.IDINHEAD & ETYPY.W_PORTAL='N' & _editInnd
   || web_def_btn(_red,'EDIT','text=&Edycja,tooltip=Włączenie redakcji informacji dodatkowych')
   ?};
   {? _akc='W'
   || web_def_btn(_red,'END','text=Zakończ'@+ {? _grey*'END' || ',state=grayed' || '' ?});
      web_def_btn(_red,'OK','text=OK,key=F2,default=1'+ {? _grey*'OK' || ',state=grayed' || '' ?})
   |? _akc='P'
   || web_def_btn(_red,'HISTORIA','text=Historia'@+ {? _grey*'HISTORIA' || ',state=grayed' || '' ?});
      web_def_btn(_red,'PRZEKAZ','text=Przekaż'@+ {? _grey*'PRZEKAZ' || ',state=grayed' || '' ?});
      {? exec('chk_role','#b__box',OPERATOR.USER,'OBE_FAW_CPRO')
      || web_def_btn(_red,'ODRZUCP','text=&Odrzuć'@+ {? _grey*'ODRZUCP' || ',state=grayed' || '' ?})
      ?}
   |? _akc='A'
   || web_def_btn(_red,'AKC','text=&Akceptuj'@+ {? _grey*'AKC' || ',state=grayed' || '' ?});
      web_def_btn(_red,'ODRZUC','text=&Odrzuć'@+ {? _grey*'ODRZUC' || ',state=grayed' || '' ?})
   ?}
?};
web_def_btn(_red,'ANULUJ','text=Anuluj,key=Esc'@+ {? _grey*'ANULUJ' || ',state=grayed' || '' ?});
exec('set_wt_par','obiegi2','dynamic',1);
_red


\set_edokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Ustawia pola wniosku na podstawie dynamicznego okna redagowania
::   WE: _a - tablica z wartościami okna redagowania
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
ETYPY.prefix();
ETYPY.seek(EDOKUM.TYP);
{? ETYPY.CZY_DOPE='T' || EDOKUM.DOP:=_a.DOP ?};
{? exec('be_id_edokum1','obiegi')
|| EDOKUM.ID:=_a.ID
?};
exec('env_wt','b_proces');
exec('__F_ZATR','object');
OBIEGI.OSOBAOBI:=_a.DOSTAWCA;
{? EDOKUM.DOSTAWCA=null() | (-(30+(EDOKUM.DOSTAWCA().PIERWSZE+' '+EDOKUM.DOSTAWCA().NAZWISKO))<>-(30+OBIEGI.OSOBAOBI))
|| EDOKUM.DOSTAWCA:=exec('prac_firm_af_www1','obiegi2',OBIEGI.OSOBAOBI)
?};
EDOKUM.WYCOFAJ:='';
{? ETYPY.ED_TEMAT='T'
|| EDOKUM.TR:=_a.TR
?};
{? ETYPY.CZY_WAR
|| EDOKUM.WAL:=_a.WAL;
   {? var_press('NETTO',_a)>0 || EDOKUM.NETTO:=_a.NETTO ?};
   {? var_press('WART',_a)>0 || EDOKUM.WART:=_a.WART ?};
   {? var_press('TP',_a)>0 || EDOKUM.TP:=_a.TP ?}
?};
{? ETYPY.CZY_DATY
|| EDOKUM.DATA_OD:=_a.DATA_OD;
   {? ETYPY.CZY_DATY=2
   || EDOKUM.GODZ_OD:=_a.GODZ_OD
   ?};
   EDOKUM.DATA_DO:=_a.DATA_DO;
   {? ETYPY.CZY_DATY=2
   || EDOKUM.GODZ_DO:=_a.GODZ_DO
   ?}
?};
EDOKUM.memo_set(_a.UW_OPDL,'UW_OPDL');
{? ETYPY.CZY_KH
|| {? _a.KH<>''
   || _slo:=null;
      RS.index('RS'); RS.prefix();
      SLO.index('KOD'); SLO.prefix(_a.KH,);
      {? SLO.first()
      || {!
         |? {? RS.find_key(SLO.SLU().WZ,) & RS.TAB='KH'
            || _slo:=SLO.ref()
            ?};
            _slo=null & SLO.next()
         !}
      ?};
      EDOKUM.KH:=_slo;
      KH.index('KOD'); KH.prefix(2,_a.KH,);
      EDOKUM.KHKH:={? KH.first() || KH.ref() || null ?}
   || EDOKUM.KHKH:=null
   ?}
?};
{? ETYPY.CZY_ZALP='T'
|| EDOKUM.KASPRZEL:=_a.KASPRZEL;
   EDOKUM.N:=_a.N;
   EDOKUM.PLACE:=_a.PLACE;
   EDOKUM.POTRODDN:=_a.POTRODDN
?};
{? ETYPY.DEKR='T' | var_press('ODD',_a)>0
|| EDOKUM.ODD:=_a.ODD;
   {? ETYPY.DEKR='T' & var_pres('DATDOK',_a)>0
   || EDOKUM.DATDOK:=_a.DATDOK
   ?}
?};
ETYPY.cntx_pop()


\get_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Ustawia pola dynamicznego okna redagowania na podstawie informacji dodatkowych
::   WE: _a - tablica z wartościami okna redagowania
::----------------------------------------------------------------------------------------------------------------------
_nr:=exec('get_wt_par','obiegi2','NrID');
{? _nr=~~ || _nr:=1 ?};
EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null);
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_nr);
{? EDOK_ATR.first()
|| {? var_press('POZ',_a)>0 || _a.POZ:=_nr ?};
   {!
   |? {? EDOK_ATR.TAT & ETYP_ATR.find_key(EDOK_ATR.KOL) & EDOK_ATR.TAT().TYP<>'O'
      || _pole:='P'+$EDOK_ATR.KOL;
         _typ:=TAT.TYP;
         {?  var_pres(_pole,_a)>0
         || _fml:='_a.'+_pole+':=_b';
            {? _typ='L'
:: Liczba
            || ($_fml)(_a,#gsub(EDOK_ATR.WAR,',','.'))
            |? _typ='B'
:: Tak/nie
            || ($_fml)(_a,1+EDOK_ATR.WAR)
            |? _typ='D'
:: Data
            || _data:={? EDOK_ATR.WAR='' | +EDOK_ATR.WAR<>10
                      || date(0,0,0)
                      || date(#(4+EDOK_ATR.WAR),#(5-EDOK_ATR.WAR-3),#(EDOK_ATR.WAR+2))
                      ?};
               ($_fml)(_a,_data)
            |? _typ='C'
:: Czas
            || _czas:={? EDOK_ATR.WAR=''
                      || time(0,0,0)
                      |? +EDOK_ATR.WAR=8
                      || time(#(2+EDOK_ATR.WAR),#(3-EDOK_ATR.WAR-3),#(EDOK_ATR.WAR+2))
                      |? +EDOK_ATR.WAR=5
                      || time(#(2+EDOK_ATR.WAR),#(EDOK_ATR.WAR+2),0)
                      || time(0,0,0)
                      ?};
               ($_fml)(_a,_czas)
            |? _typ='S' & EDOK_ATR.TAT().SLU
:: Słownik
            || ($_fml)(_a,EDOK_ATR.WAR)
            |? _typ='U' & EDOK_ATR.TAT().UD_SCH
:: Schemat struktury
            || ($_fml)(_a,EDOK_ATR.WAR)
            |? _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
            || ($_fml)(_a,EDOK_ATR.WAR)
            || ($_fml)(_a,EDOK_ATR.WAR)
            ?}
         ?}
      ?};
      EDOK_ATR.next()
   !}
?};
EDOK_ATR.cntx_pop();
ETYP_ATR.cntx_pop()


\set_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Ustawia pola wniosku na podstawie dynamicznego okna redagowania
::   WE: _a - tablica z wartościami okna redagowania
::----------------------------------------------------------------------------------------------------------------------
_nr:=exec('get_wt_par','obiegi2','NrID');
{? _nr=~~ || _nr:=1 ?};
EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null);
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_nr);
{? EDOK_ATR.first()
|| {!
   |? {? EDOK_ATR.TAT & ETYP_ATR.find_key(EDOK_ATR.KOL) & EDOK_ATR.TAT().TYP<>'O'
      || _pole:='P'+$EDOK_ATR.KOL;
         {?  var_pres(_pole,_a)>0
         || _typ:=TAT.TYP;
            _value:=($('_a.'+_pole))(_a);
            {? _typ='L'
:: Liczba
            || _value:=_value$EDOK_ATR.TAT().PREC;
               EDOK_ATR.WAR:=$_value
            |? _typ='B'
:: Tak/nie
            || EDOK_ATR.WAR:={? 1+_value='T' || 'Tak' || 'Nie' ?}
            |? _typ='D'
:: Data
            || EDOK_ATR.WAR:=$_value
            |? _typ='C'
:: Czas
            || EDOK_ATR.WAR:=_value$3
            |? _typ='S' & EDOK_ATR.TAT().SLU
:: Słownik
            || SLO.cntx_psh();
               SLO.index('SL');
               {? ETYP_ATR.SLOPOLE='K' || SLO.index('SL') || SLO.index('SL_TR') ?};
               SLO.prefix(EDOK_ATR.TAT().SLU,_value,);
               {? SLO.first()
               || EDOK_ATR.WAR:=_value;
                  EDOK_ATR.SLO:=SLO.ref()
               || EDOK_ATR.WAR:='';
                  EDOK_ATR.SLO:=null
               ?};
               SLO.cntx_pop()
            |? _typ='U' & EDOK_ATR.TAT().UD_SCH
:: Schemat struktury
            || {? _value
               || UD_DEF.cntx_psh();
                  UD_DEF.prefix();
                  {? UD_DEF.seek(_value)
                  || EDOK_ATR.UD_SKL:=UD_DEF.UD_SKL;
                     EDOK_ATR.WAR:={? ETYP_ATR.SLOPOLE='K' || UD_DEF.SYMBOL || UD_DEF.OPIS ?}
                  || EDOK_ATR.UD_SKL:=null;
                     EDOK_ATR.WAR:=''
                  ?};
                  UD_DEF.cntx_pop()
               || EDOK_ATR.UD_SKL:=null;
                  EDOK_ATR.WAR:=''
               ?}
            |? _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
            || EDOK_ATR.WAR:=_value;
               _tab:=($ETYP_ATR.TSLO)();
               {? type_of(_tab)>100 & var_press('KOD',_tab)>0 & var_press('TR',_tab)>0 &
                  var_press('REF_SQL',_tab)>0
               || {? _tab.find_tab(,{? ETYP_ATR.SLOPOLE='K' || 'KOD' || 'TR' ?},,'=',_value)
                  || EDOK_ATR.REF_SQL:=_tab.REF_SQL
                  ?}
               ?};
               &_tab
            || EDOK_ATR.WAR:=_value
            ?};
            EDOK_ATR.put()
         ?}
      ?};
      EDOK_ATR.next()
   !}
?};
EDOK_ATR.cntx_pop();
ETYP_ATR.cntx_pop()


\chk_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Sprawdzenie wartości informacji dodatkowych
::   WE: _a - tablica z wartościami okna redagowania
::   WY: akronim pola z niepoprawną daną lub '' gdy dane poprawne
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
_nr:=exec('get_wt_par','obiegi2','NrID');
{? _nr=~~ || _nr:=1 ?};
EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_nr);
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(ETYPY.ref(),null);
{? EDOK_ATR.first()
|| {!
   |? _err:=0;
      {? EDOK_ATR.TAT & ETYP_ATR.find_key(EDOK_ATR.KOL) & ETYP_ATR.EDIT='N' & EDOK_ATR.TAT().TYP<>'O'
      || _pole:='P'+$EDOK_ATR.KOL;
         _typ:=TAT.TYP;
         {? var_pres(_pole,_a)>0
         || _value:=($('_a.'+_pole))(_a);
            {? _typ='L'
:: Liczba
            || _err:=_value=0 & ETYP_ATR.CZY_ZERO='P'
            |? _typ='B'
:: Tak/nie
            || 1
            |? _typ='D'
:: Data
            || _err:=_value=date(0,0,0)
            |? _typ='C'
:: Czas
            || _err:=0
            |? _typ='S' & EDOK_ATR.TAT().SLU
:: Słownik
            || _err:=_value='';
               {? _err=0
               || _typ:=ETYP_ATR.SLOPOLE;
                  SLO.cntx_psh();
                  SLO.index('SL');
                  {? _typ='K' || SLO.index('SL') || SLO.index('SL_TR') ?};
                  SLO.prefix(EDOK_ATR.TAT().SLU,_value,);
                  {? ~SLO.first()
                  || _err:=2;
                     FUN.info('Nie znaleziono wartości pola "%1" w słowniku użytkownika.'@[EDOK_ATR.TAT().OPIS])
                  ?};
                  SLO.cntx_pop()
               ?}
            |? _typ='U' & EDOK_ATR.TAT().UD_SCH
:: Schemat struktury
            || _err:=_value=null
            |? _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
            || _err:=_value='';
               {? _err=0
               || _typ:=ETYP_ATR.SLOPOLE;
                  _tab:=($ETYP_ATR.TSLO)();
                  {? type_of(_tab)>100 & var_press('KOD',_tab)>0 & var_press('TR',_tab)>0 & var_press('REF_SQL',_tab)>0
                  || _tab.blank(1);
                     {? _typ='K' || _tab.KOD:=_value || _tab.TR:=_value ?};
                     {? ~_tab.find_rec()
                     || _err:=2;
                        FUN.info('Nie znaleziono wartości pola "%1" w słowniku.'@[EDOK_ATR.TAT().OPIS])
                     ?}
                  ?};
                  &_tab
               ?}
            || _err:=_value=''
            ?};
            {? _err=1
            || {? ETYP_ATR.WYMAG='T'
               || _ret:=_pole;
                  FUN.info(__CHK.empty_msg(EDOK_ATR.TAT().OPIS))
               || _err:=0
               ?}
            |? _err=2
            || _ret:=_pole
            ?}
         ?}
      ?};
      _err=0 & EDOK_ATR.next()
   !}
?};
EDOK_ATR.cntx_pop();
ETYP_ATR.cntx_pop();
_ret


\inne_akcje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Akcje dla przycisków w oknie redagowania
::   WE: _a - akcja
::       _b - 1 - uruchamiane z TODO
::            0 - uruchamiane z obszaru roboczego
::     [_c] - akcja podczas wyświetlania
::----------------------------------------------------------------------------------------------------------------------
_todo:={? var_pres('_b')>0 || _b || 1 ?};
_disp:={? var_pres('_c')>0 || _c || 0 ?};
{? _a='ANULUJ'
|| web_def_close()
|? _a='EDOKUM'
||  EDOKUM.web_display('WW01',,"")
|? _a='INNE_DANE'
|| _akc:=exec('get_wt_par','obiegi2','akc');
   {? type_of(_akc)=type_of('') & _akc<>'' & 'WAP'*_akc>0
   || exec('inne_dane','obiegi',_akc+'W',2,1)
   || exec('inne_dane','obiegi','WW',2,1,1)
   ?}
|? _a='PROJEKTY'
|| exec('proj_dob','obiegi_proj','WW',2)
|? _a='SKIDXD'
|| exec('podz_dob','obiegi','WW',2)
|? _a='DOKUM'
|| exec('zalaczniki','obiegi','WW',2)
|? _a='HISTORIA'
|| exec('okn_opis_wt','obiegi',2)
|? _a='END'
|| exec('wni_przek','obiegi2',_todo,1)
|? _a='PRZEKAZ'
|| exec('wn_ob_prz','obiegi_akc',_todo,1)
|? _a='AKC'
|| exec('wn_ob_akc','obiegi_akc',_todo,1)
|? _a='ODRZUC'
|| exec('wn_ob_odrz','obiegi',_todo,'AW')
|? _a='ODRZUCP'
|| exec('wn_ob_odrz','obiegi',_todo,'PW')
|? _a='NEXT'
|| exec('btn_atr','obiegi2',1,_disp)
|? _a='PREV'
|| exec('btn_atr','obiegi2',-1)
|? _a='DEL'
|| web_params_set(web_def_get());
   EDOKUM.web_cntx_save();
   EDOKUMW.web_cntx_save();
   web_ask("
      EDOKUM.web_cntx_load();
      EDOKUMW.web_cntx_load();
      {? _a=1
      || _nr:=exec('get_wt_par','obiegi2','NrID');
         EDOK_ATR.cntx_psh();
         EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
         EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_nr);
         {? EDOK_ATR.first()
         || exec('edk_atr_usun1','obiegi',0,EDOKUM)
         ?};
         EDOK_ATR.cntx_pop();
         _max:=exec('edk_atr_lastpoz','obiegi',EDOKUM.ref());
         {? _nr>_max
         || exec('set_wt_par','obiegi2','NrID',_max)
         ?};
         _par:=web_global_params_get();
         _get:=web_params_get();
         exec('atr_update','obiegi2',_get);
         {? ~(type_of(_par)>100 & ((var_pres('path_run',_par)>0 & _par.path_run='Proc')
            | (var_pres('todo_run',_par)>0 & _par.todo_run>0)))
         || EDOKUMW.web_refresh('WPRGW',{? var_pres('zakres')>0
                                        || {? zakres=2 || 'AKC'
                                           |? zakres=5 || 'PRZ'
                                           || 'WPR'
                                           ?}
                                        || 'WPR'
                                        ?})
         ?}
      ?}
   ",'Czy usunąć bieżący komplet informacji dodatkowych?'
   )
|? _a='DOK_POW'
|| exec('dob_log_wt','obiegi_log')
|? _a='EDIT'
|| exec('edit_red','obiegi2')
?}


\btn_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Akcja dla przycisku z atrybutami
::   WE: _a - 1-next -1-prev
::      [_b] - 0/1 - tylko wyświetlanie
::----------------------------------------------------------------------------------------------------------------------
_disp:={? var_pres('_b')>0 || _b || 0 ?};
_nr:=exec('get_wt_par','obiegi2','NrID');
_max:=exec('edk_atr_lastpoz','obiegi',EDOKUM.ref());
_nr+=_a;
{? _nr>_max
|| EDOKUM.web_cntx_save();
   web_params_set(web_def_get());
   web_ask("
      EDOKUM.web_cntx_load();
      {? _a=1
      || refr_okn:=0;
         OBIEGI.TYP_ID:='D';
         _msg:=exec('edk_atr_dolacz','obiegi',0,0,1);
         VAR_DEL.delete('refr_okn');
         _max:=exec('edk_atr_lastpoz','obiegi',EDOKUM.ref());
         exec('set_wt_par','obiegi2','NrID',_max);
         _flds:=web_params_get();
         exec('get_atr','obiegi2',_flds);
         web_def_update(web_top_win(),_flds,);
         web_def_btn_opt(web_top_win(),'state=normal','PREV');
         web_def_btn_opt(web_top_win(),'state=normal','DEL')
      ?}
   ",'Czy dodać nowy komplet informacji dodatkowych?')
|? _nr<=_max & _nr>0
|| exec('set_wt_par','obiegi2','NrID',_nr);
   exec('atr_update','obiegi2',,_disp)
?}


\atr_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Aktualizuje okno o informacje dodatkowe
::   WE: [_a] - tablica z zawartością pól okna tymczasowego
::       [_b] - 0/1 - tylko wyświetlanie
::----------------------------------------------------------------------------------------------------------------------
_disp:={? var_pres('_b')>0 || _b || 0 ?};
_akc:=exec('get_wt_par','obiegi2','akc');
_max:=exec('edk_atr_lastpoz','obiegi',EDOKUM.ref());
_nr:=exec('get_wt_par','obiegi2','NrID');
_edit:=_disp=0 & exec('blok_innd','obiegi')=0;
_flds:={? var_press('_a')>0 || _a || web_def_get() ?};
exec('get_atr','obiegi2',_flds);
web_def_update(web_top_win(),_flds,);
web_def_btn_opt(web_top_win(),'state='+{? _nr=1 || 'grayed' || 'normal' ?},'PREV');
web_def_btn_opt(web_top_win(),'state='+{? ~_edit & _nr>=_max || 'grayed' || 'normal' ?},'NEXT');
web_def_btn_opt(web_top_win(),'state='+{? ~_edit | _max<2 || 'grayed' || 'normal' ?},'DEL')


\wni_przek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zakończenie wniosków w obiegu
::   WE:  _a  - 1 - odpalanie z TODO na WEBTERM
::              2 - obszar roboczy tabela EDOKUM
::              0 - obszar roboczy akcja dla EDOKUMW
::              3 - odpalanie z pulpitu
::       [_b] - z okna dynamicznego? 1-tak [0]-nie
::----------------------------------------------------------------------------------------------------------------------
_dynamic:=exec('get_wt_par','obiegi2','dynamic');
_miejsce:=exec('get_wt_par','obiegi2','path_run');
{? var_press('_a')<=0
|| {? _miejsce='Todo'
   || _a:=1
   |? _miejsce='Proc'
   || _a:=3
   || _a:=0
   ?}
?}; _dalej:=1;
{? var_press('_b')<=0 || _b:=0 ?};
{? _a=1
|| exec('todo_run','obiegi',1); todo_run:=1;
   _dalej:=exec('env_edokum','obiegi','WW',0)
|? _a=3
|| exec('todo_run','obiegi',-1); todo_run:=0;
   _dalej:=exec('env_edokum','obiegi','WW',0)
|? _a=0
|| {? EDOKUMW.get()
   || exec('todo_run','obiegi',0);
      _dalej:=exec('env_edokum','obiegi','WW',1);
      EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
      EDOKUM.prefix(); EDOKUMW.EDOKUM()
   || FUN.info('Nie znaleziono wniosku.'@);
      exec('dob_refresh','obiegi','W','W');
      _dalej:=0
   ?}
|| EDOKUMW.prefix();
   exec('todo_run','obiegi',0);
   _dalej:=exec('env_edokum','obiegi','WW',0)
?};
VAR_DEL.delete('todo_run');
{? _dalej
|| _get:=web_params_get();
   {? type_of(_get)>100 & var_pres('akcja',_get)>0
   || {? _get.akcja='popraw'
      || _dalej:=(params_exec('pop_dob_wt2','obiegi',0,_a<>1 & _a<>3)='')
      |? _get.akcja='dołącz'
      || _par:=web_global_params_get();
         _dalej:={? type_of(_par)>100 & var_pres('act_ok',_par)>0 & ~_par.act_ok
                 || params_exec('dol_dob_wt2','obiegi',0,_a<>1)=''
                 || params_exec('pop_dob_wt2','obiegi',0,_a<>1 & _a<>3)=''
                 ?}
      ?}
   ?}
?};
{? _dalej
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FAW_DARP';
   _params.AKCJA:='chkWT';
   _params.UIDREF:=EDOKUM.uidref();
   _params.WT_SKIP:=1;
   _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
   _params.CONTEXT.EDOKUM:=EDOKUM.ref();
   _params.CONTEXT.SPR_WT:=0;
   exec('mp_run','#b__box',_params);
   {? _params.CONTEXT.SPR_WT
   || BPMN.END:=1;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
      params_exec('akceptuj','obiegi_akc','W')
   ?}
?}


\set_wt_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Dodaje globalny parametr webterma
::   WE: _a - nazwa
::       _b - wartość parametru
::----------------------------------------------------------------------------------------------------------------------
web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),_a,_b))


\get_wt_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Zwraca globalny parametr webterma
::   WE: _a - nazwa parametru
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| _par:=web_global_params_get();
   {? var_press('_par')>0 & var_press(_a,_par)>0
   || ($('_a.'+_a))(_par)
   || ~~
   ?}
|| ~~
?}


\f3kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Klawisz F3 dla pola z kontrahentem w dynamicznym oknie wniosu w obiegu
::----------------------------------------------------------------------------------------------------------------------
_flds:=web_def_get();
exec('set_wt_par','obiegi2','SL_KH',_flds.SL_KH);
_slu:=null;
{? _flds.SL_KH<>''
|| SLU.cntx_psh();
   SLU.index('NAZ'); SLU.prefix(_flds.SL_KH,);
   _slu:={? SLU.first() || SLU.ref() || null ?};
   SLU.cntx_pop()
?};
SLO.index('SL');
{? _slu || SLO.prefix(_slu) || SLO.prefix(null) ?};
_nip:=exec('niptostr','#string',_flds.NIP);
KH.index('SNIP');
{? _nip<>'' || KH.prefix(2,_nip) || KH.prefix(2) ?};
_tcid:=app_info('web_tcid');
_mbid:=app_info('web_mbid');
_sesid:=app_info('web_sesid');
_tabid:=app_info('web_tabid');
TAB_WT.index('INDEX01'); TAB_WT.prefix(_tcid,_tabid,4);
{? TAB_WT.first() || {! |? TAB_WT.del !} ?};
{? KH.first()
|| {!
   |? TAB_WT.blank(1);
      TAB_WT.SESID:=_sesid;
      TAB_WT.MBID:=_mbid;
      TAB_WT.TCID:=_tcid;
      TAB_WT.TABID:=_tabid;
      TAB_WT.TYP:=4;
      TAB_WT.KOD:=KH.KOD;
      TAB_WT.NAZ:=KH.NAZ;
      TAB_WT.STR01:=KH.SKR;
      TAB_WT.STR02:=KH.NIP;
      TAB_WT.INT01:={? SLO.find_key(KH.KOD) & KH.KOD=SLO.KOD || SLO.ref() ?};
      TAB_WT.INT02:=KH.ref();
      TAB_WT.INT03:=0;
      TAB_WT.add();
      KH.next()
   !}
?};

_zwrot:=0;
EDOKUM.web_cntx_save(1);
exec('tab_form','obiegi','TAB_WT','wyb_kh_po_nip','obiegi','','');
TAB_WT.web_select('WER04')


\trig_edok_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Wyzwalacz dla tabeli EDOK_ATR
::   WE: _a - typ: 0-przed del, 1-po add, 2-po put
::----------------------------------------------------------------------------------------------------------------------
{? 6+EDOK_ATR.name()='edokat'
|| {? _a=1 | _a=2
   || _ref1:=EDOKUM.ref();
      _ref2:=null;
      EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(EDOK_ATR.EDOKUM));
      EDOKUM.prefix();
      _opis:=0;
      _id_wp:='';
      {? EDOKUM.seek(EDOK_ATR.EDOKUM)
      || ETYP_ATR.cntx_psh();
         ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null,EDOK_ATR.KOL);
         {? ETYP_ATR.first()
         || _opis:=ETYP_ATR.OP_KPL;
            _id_wp:=ETYP_ATR.ID_WP
         ?};
         ETYP_ATR.cntx_pop();
         ETYPY.cntx_psh();
         {? (_id_wp='Amount' | _id_wp='NetValue') & EDOKUM.TYP().W_PORTAL='s'
         || {? _id_wp='Amount'
            || EDOKUM.WART:=#EDOK_ATR.WAR
            || EDOKUM.NETTO:=#EDOK_ATR.WAR
            ?};
            EDOKUM.put();
            _ref2:=EDOKUM.ref()
         ?};
         ETYPY.cntx_pop()
      ?};
      EDOKUM.cntx_pop();
      {? _ref1=_ref2
      || EDOKUM.get()
      ?}
   ?};
   {? _a=1
   || {? EDOK_ATR.EDOKUM & EDOK_ATR.REKORD
      || EDOK_ATK.cntx_psh();
         EDOK_ATK.use('edoknt'+(EDOK_ATR.name()+2));
         EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOK_ATR.EDOKUM,EDOK_ATR.REKORD);
         {? ~EDOK_ATK.first()
         || EDOK_ATK.blank(1);
            EDOK_ATK.EDOKUM:=EDOK_ATR.EDOKUM;
            EDOK_ATK.REKORD:=EDOK_ATR.REKORD;
            {? _opis
            || EDOK_ATK.OPIS:=exec('edok_atk_opis','obiegi2')
            ?};
            EDOK_ATK.add()
         ?};
         EDOK_ATK.cntx_pop()
      ?}
   |? _a=2
   || _rek:=bfld('REKORD')<>EDOK_ATR.REKORD;
      {? _rek | _opis
      || EDOK_ATK.cntx_psh();
         EDOK_ATK.use('edoknt'+(EDOK_ATR.name()+2));
         EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOK_ATR.EDOKUM);
         {? _rek & EDOK_ATK.find_key(bfld('REKORD'))
         || _ref:=EDOK_ATK.ref();
            {? EDOK_ATK.find_key(EDOK_ATR.REKORD)
            || EDOK_ATK.OPIS:=exec('edok_atk_opis','obiegi2');
               EDOK_ATK.put();
               {? EDOK_ATK.seek(_ref)
               || EDOK_ATK.del()
               ?}
            || EDOK_ATK.REKORD:=EDOK_ATR.REKORD;
               {? _opis
               || EDOK_ATK.OPIS:=exec('edok_atk_opis','obiegi2')
               ?};
               EDOK_ATK.put()
            ?}
         |? ~EDOK_ATK.find_key(EDOK_ATR.REKORD)
         || EDOK_ATK.blank(1);
            EDOK_ATK.EDOKUM:=EDOK_ATR.EDOKUM;
            EDOK_ATK.REKORD:=EDOK_ATR.REKORD;
            {? _opis
            || EDOK_ATK.OPIS:=exec('edok_atk_opis','obiegi2')
            ?};
            EDOK_ATK.add()
         |? _opis & bfld('WAR')<>EDOK_ATR.WAR
         || EDOK_ATK.OPIS:=exec('edok_atk_opis','obiegi2');
            EDOK_ATK.put()
         ?};
         EDOK_ATK.cntx_pop()
      ?}
   |? _a=0
   || EDOK_ATR.cntx_psh();
      EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOK_ATR.EDOKUM,null,'',EDOK_ATR.REKORD);
      {? EDOK_ATR.first() & ~EDOK_ATR.next()
      || EDOK_ATK.cntx_psh();
         EDOK_ATK.use('edoknt'+(EDOK_ATR.name()+2));
         EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOK_ATR.EDOKUM,EDOK_ATR.REKORD);
         {? EDOK_ATK.first()
         || EDOK_ATK.del()
         ?};
         EDOK_ATK.cntx_pop()
      ?};
      EDOK_ATR.cntx_pop()
   ?}
?};
{? _a=0 || 1 || ~~ ?}


\edok_atk_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Zwraca opis dla kompletu danych
::----------------------------------------------------------------------------------------------------------------------
_opis:='';
_name:=ref_name(EDOK_ATK.EDOKUM);
EDOKUM.cntx_psh();
EDOKUM.use(_name);
EDOKUM.prefix();
{? EDOKUM.seek(EDOK_ATK.EDOKUM)
|| ETYP_ATR.cntx_psh();
   ETYP_ATR.index('OP_KPL'); ETYP_ATR.prefix(EDOKUM.TYP,1);
   {? ETYP_ATR.first()
   || EDOK_ATR.cntx_psh();
      EDOK_ATR.use('edokat'+(EDOK_ATK.name()+2));
      EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOK_ATK.EDOKUM,null,'',EDOK_ATK.REKORD);
      {!
      |? _typ:=ETYP_ATR.TAT().TYP;
         _val:='';
         {? EDOK_ATR.find_key(ETYP_ATR.KOL)
         || _val:=EDOK_ATR.WAR
         ?};
         _opis+={? _val<>'' || _val+', ' || '' ?};
         ETYP_ATR.next()
      !};
      EDOK_ATR.cntx_pop()
   || _opis:='Pozycja '+$EDOK_ATK.REKORD+', '
   ?};
   ETYP_ATR.cntx_pop()
?};
EDOKUM.cntx_pop();
_opis-2


\am_edok_atk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Dołaczanie, poprawianie kompletu informacji dodatkowych
::   WE: _a - 1-dołacz 0-popraw
::----------------------------------------------------------------------------------------------------------------------
_ref:=web_win_ref(web_top_ident()+3);
_edokum:=null();
{? type_of(_ref)=type_of(~~)
|| EDOKUM.web_cntx_load();
   _edokum:=EDOKUM.ref()
|? ref_tab(_ref)=EDOKUMW
|| EDOKUMW.cntx_psh();
   EDOKUMW.use(ref_name(_ref));
   EDOKUMW.prefix();
   {? EDOKUMW.seek(_ref)
   || _edokum:=EDOKUMW.EDOKUM
   ?};
   EDOKUMW.cntx_pop()
?};
{? _edokum<>null()
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(_edokum));
   EDOKUM.prefix();
   {? EDOKUM.seek(_edokum)
   || {? _a & Plugin.runnable('EDOK_ATK_F_ADD_001') & Plugin.run('EDOK_ATK_F_ADD_001',EDOKUM.ref(),1)<>-1
      || 1
      || _red:=exec('win_red_wnioski','obiegi2','I',{? _a=1 || -1 || EDOK_ATK.REKORD ?});
         EDOK_ATK.web_cntx_save();
         EDOKUM.web_cntx_save();
         web_params_set('typ',_a);
         web_def_edit(_red,"
            EDOK_ATK.web_cntx_load();
            EDOKUM.web_cntx_load();
            {? _a='OK'
            || _pole:=exec('chk_atr','obiegi2',_b);
               {? type_of(_pole)=type_of('') & _pole<>''
               || web_def_update(web_top_win(),_b,_pole)
               || exec('set_atr','obiegi2',_b);
                  web_def_close();
                  web_top_refresh(1)
               ?}
            || {? web_params_get().typ=1
               || _nr:=exec('get_wt_par','obiegi2','NrID');
                  EDOK_ATR.cntx_psh();
                  EDOK_ATR.use('edokat'+(EDOK_ATK.name()+2));
                  EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOK_ATK.EDOKUM,null,'',_nr);
                  {? EDOK_ATR.first()
                  || exec('edk_atr_usun1','obiegi',0,EDOKUM)
                  ?};
                  EDOK_ATR.cntx_pop()
               ?};
               web_def_close()
            ?}
         ",'Informacja dodatkowa'@)
      ?}
   ?};
   EDOKUM.cntx_pop()
?}


\trig_etyp_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Wyzwalacz po put dla tabeli ETYP_ATR
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
{? ETYP_ATR.ETYPY().TYPOBIEG().NAZWA='Obieg wniosków' & bfld('OP_KPL')<>ETYP_ATR.OP_KPL
|| _tab:=EDOKUM.names();
   {? _tab.first()
   || EDOKUM.cntx_psh();
      EDOK_ATR.cntx_psh();
      {!
      |? EDOKUM.use(_tab.NAME);
         EDOKUM.index('TYP'); EDOKUM.prefix(ETYP_ATR.ETYPY);
         {? EDOKUM.first()
         || EDOK_ATK.use('edoknt'+(_tab.NAME+2));
            EDOK_ATK.index('EDOKUM');
            {!
            |? EDOK_ATK.prefix(EDOKUM.ref());
               {? EDOK_ATK.first()
               || {!
                  |? EDOK_ATK.OPIS:=exec('edok_atk_opis','obiegi2');
                     EDOK_ATK.put();
                     EDOK_ATK.next()
                  !}
               ?};
               EDOKUM.next()
            !}
         ?};
         _tab.next()
      !};
      EDOK_ATR.cntx_pop();
      EDOKUM.cntx_pop()
   ?}
?};
{? ETYPY.W_PORTAL='T' || ETYPY.put(,1) ?};
exec('t_etyp_atr','portal_wnioski',1);
ETYPY.cntx_pop();
~~


\arf_edok_atk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Po odświeżeniu okna tabeli EDOK_ATK
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
{? exec('blok_innd','obiegi')
|| _gray+='DPUSGW:D'
|? EDOK_ATK.get() & EDOK_ATK.EDOKUM().TYP().ATR_ONE=1
|| _gray+='DUGW:D'
|? exec('edk_atr_lastpoz','obiegi',EDOKUM.ref())<2
|| _gray+='GW'
?};
web_top_tab().web_win_opt(web_top_win(),web_top_ident(),'grayed='+_gray)


\ad_edok_atk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Usuwanie kompletu informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokat'+(EDOK_ATK.name()+2));
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOK_ATK.EDOKUM,null,'',EDOK_ATK.REKORD);
{? EDOK_ATR.first()
|| _nr:=EDOK_ATK.REKORD;
   EDOK_ATK.cntx_psh();
   EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOK_ATK.EDOKUM);
   _ref:={? EDOK_ATK.next() | EDOK_ATK.prev() || EDOK_ATK.ref() || null ?};
   EDOK_ATK.cntx_pop();
   EDOK_ATR.prefix(EDOK_ATK.EDOKUM,null,'',);
   OBIEGI.TYP_ID:='D';
   exec('edk_atr_usun','obiegi');
   EDOK_ATK.cntx_psh();
   EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOK_ATK.EDOKUM);
   {!
   |? _nr+=1;
      {? EDOK_ATK.find_key(_nr)
      || EDOK_ATK.OPIS:=exec('edok_atk_opis','obiegi2');
         EDOK_ATK.put();
         1
      ?}
   !};
   EDOK_ATK.cntx_pop()
?};
EDOK_ATR.cntx_pop();
{? _ref || EDOK_ATK.seek(_ref) ?}


\edok_atk_move
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Przesuniecie kompletów informacji dodatkowych
::   WE: _a - 1 - do góry, -1 - w dół
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokat'+(EDOK_ATK.name()+2));
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOK_ATK.EDOKUM,null,'',EDOK_ATK.REKORD);
{? EDOK_ATR.first()
|| {? _a=1
   || exec('edk_atr_wgore','obiegi')
   || exec('edk_atr_wdol','obiegi')
   ?}
?};
EDOK_ATR.cntx_pop();
{? _ref || EDOK_ATK.seek(_ref) ?}


\edok_atk_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Wyświetlenie okna z kompletem informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_red:=exec('win_red_wnioski','obiegi2','i',EDOK_ATK.REKORD);
web_def_fld_opt(_red, 'editable=0',);
web_def_disp(_red,"web_def_close()",'Informacja dodatkowa'@)


\edokum_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Zwraca domyślną jednostkę księgową dla wniosku w obiegu
::----------------------------------------------------------------------------------------------------------------------
_odd:=null;
{? EDOKUM.TYP & EDOKUM.TYP().ODD_TYP<>''
|| {? ETYPY.ODD_TYP='S'
   || _odd:=ETYPY.ODD_S
   |? ETYPY.ODD_TYP='F'
   || _val:=($ETYPY.ODD_F)();
      {? type_of(_val)=type_of('')
      || ODD.cntx_psh();
         ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA,_val,);
         _odd:={? ODD.first() || ODD.ref() || null ?};
         ODD.cntx_pop()
      |? type_of(_val)=type_of(null)
      || _odd:=_val
      |? type_of(_val)=type_of(1)
      || ODD.cntx_psh();
         ODD.prefix();
         _odd:={? ODD.seek(_val,) || ODD.ref() || null ?};
         ODD.cntx_pop()
      ?}
   ?}
?};
_odd


\bv_etypy_odd_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Przed wyświetleniem pola ETYPY.ODD_TYP
::----------------------------------------------------------------------------------------------------------------------
_win:=ETYPY.win_edit('?');
ETYPY.efld_opt(_win,'enable='+$(ETYPY.ODD_TYP='S'),ETYPY,'ODD_S');
ETYPY.efld_opt(_win,'mark='+$(ETYPY.ODD_TYP='S'),ETYPY,'ODD_S');
ETYPY.efld_opt(_win,'enable='+$(ETYPY.ODD_TYP='F'),ETYPY,'ODD_F');
ETYPY.efld_opt(_win,'mark='+$(ETYPY.ODD_TYP='F'),ETYPY,'ODD_F');
~~


\ae_etypy_odd_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Po redagowaniu pola ETYPY.ODD_TYP
::----------------------------------------------------------------------------------------------------------------------
exec('bv_etypy_odd_typ','obiegi2');
{? ETYPY.ODD_TYP='S'
|| ETYPY.ODD_F:=''
|? ETYPY.ODD_TYP='F'
|| ETYPY.ODD_S:=null
|| ETYPY.ODD_F:='';
   ETYPY.ODD_S:=null
?};
1


\f3_x
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Klawisz F3 na polu z innymi danymi dla słownika definiowalnego
::   WE:
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.web_cntx_load();
EDOK_ATR.web_cntx_load();
EDOK_ATR.index('REKKOLED');
EDOK_ATR.prefix(EDOKUM.ref(),null,'',EDOK_ATR.REKORD,_a);
{? EDOK_ATR.first()
|| exec('tab_form','obiegi','UD_DEF','af3_x','obiegi2','bobs_tat_x_wt','obiegi');
   exec('tat_x_wt','obiegi',EDOKUM)
?}


\af3_x
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na wybór rekordu w słowniku innch danych - webterm
::----------------------------------------------------------------------------------------------------------------------
EDOK_ATR.web_cntx_load(1);
EDOKUM.use('skid_v'+(EDOK_ATR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOK_ATR.EDOKUM();
exec('env_edokum_get1','obiegi');
_pole:=exec('get_atr_slopol','obiegi',EDOKUM.TYP,EDOK_ATR.KOL);
_val:={? _pole='T' || TAB_WT.TR || TAB_WT.KOD ?};
TAB_WT.web_close();
web_top_def_fld_update(1,1,_val)


\ae_ud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Po redakcji innej danej typu struktura hierarchiczna
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_ok:=0;
_edok:=exec('get_wt_par','obiegi2','edok');
{? _edok<>''
|| EDOKUM.use(8+_edok);
   EDOKUM.prefix();
   {? EDOKUM.seek(_edok)
   || EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
      _ok:=1
   ?}
?};
{? _ok
|| _nr:=exec('get_wt_par','obiegi2','NrID');
   EDOK_ATR.index('REKKOLED');
   EDOK_ATR.prefix(EDOKUM.ref(),null,'',_nr,_a);
   {? EDOK_ATR.first()
   || ETYP_ATR.cntx_psh();
      ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null,EDOK_ATR.KOL);
      {? ETYP_ATR.first()
      || VAR_DEL.delete('dynTab');
         dynTab:=web_def_get();
         _fld:='dynTab.P'+$_a;
::         exec('set_atr','obiegi2',dynTab);
         {? ETYP_ATR.AE<>''
         ||
            _wyn:=($ETYP_ATR.AE)()
         ||
            _typ:=ETYP_ATR.TAT().TYP;
            _pole:=ETYP_ATR.SLOPOLE;
            {? _typ='X' & ETYP_ATR.TSLO<>''
            || _tab:=($ETYP_ATR.TSLO)();
               {? _pole='K' &  type_of(_tab)=118 & var_pres('KOD',_tab)>0
               || _tab.blank(1);
                  _tab.KOD:=($_fld)();
                  {? _tab.find_rec()
                  || ($_fld)():=_tab.KOD
                  ?}
               |? type_of(_tab)=118 & var_pres('TR',_tab)>0
               || _tab.blank(1);
                  _tab.TR:=($_fld)();
                  {? _tab.find_rec()
                  || ($_fld)():=_tab.TR
                  ?}
               ?}
            |? _typ='S' & ETYP_ATR.TAT().SLU
            ||
               SLO.cntx_psh();
               {? _pole='K' || SLO.index('SL') || SLO.index('SL_TR') ?};
               SLO.prefix(TAT.SLU,($_fld)(),);
               {? SLO.first()
               || ($_fld)():={? _pole='K' || SLO.KOD || SLO.TR ?}
               ?};
               SLO.cntx_pop()
            ?}
         ?};
         web_def_update(web_top_win(),dynTab);
         VAR_DEL.delete('dynTab')
      ?};
      ETYP_ATR.cntx_pop()
   ?}
?};
_wyn


\ob_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Zamknięcie obiegu dokumentu.
::   WE: _a - Zakres dokumentu (W - Rejestracja,A - Akceptacja ,P - Przekazanie, D - Deketacja)
::       _b - Typ dokumentu w obiegu (F - Faktura,W - Wniosek ,D - Delegacja)
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_b')
|| _log:={? _b='F'
         || 'Zamknięcie obiegu faktury'
         |? _b='W'
         || 'Zamknięcie obiegu wniosku'
         |? _b='D'
         || 'Zamknięcie obiegu delegacji'
         || ''
         ?}
|| _log:=''
?};
{? app_info('web_sesid')<>''
|| {? EDOKUMW.get()
   || _dalej:=exec('env_edokum','obiegi',_a+_b,1);
      {? _dalej
      || {? _a='W'
         || EDOKUM.use('skid_v'+(EDOKUMW.name()+2)); EDOKUM.index('TYP');
            EDOKUM.prefix(); EDOKUMW.EDOKUM();
            _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:={? _b='F'
                             || 'OBG_FZO_DARK'
                             |? _b='W'
                             || 'OBE_FAW_DARP'
                             |? _b='D'
                             || 'OBE_FDL_DBRP'
                             ?};
            _params.AKCJA:='chkWT';
            _params.UIDREF:=EDOKUM.uidref();
            _params.WT_SKIP:=1;
            _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
            _params.CONTEXT.EDOKUM:=EDOKUM.ref();
            _params.CONTEXT.SPR_WT:=0;
            exec('mp_run','#b__box',_params);
            {? _params.CONTEXT.SPR_WT
            || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
               exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
               params_exec('zamknij','obiegi_akc')
            ?}
         || EDOKUMW.web_cntx_save();
            web_params_set('zakres',_a,'typ',_b,'log',_log);
            _zam:="{? _a
                   || _zakres:=web_params_get().zakres;
                      _typ:=web_params_get().typ;
                      EDOKUMW.web_cntx_load();
                      exec('env_edokum','obiegi',_zakres+_typ,1);
                      _mas:=EDOKUMW.name()+2;
                      EDOKUM.use('skid_v'+_mas); EDOKUM.index('TYP');
                      EDOKUM.prefix(); EDOKUMW.EDOKUM();
                      EDOKUM.ZAM:='T';
                      {? EDOKUM.put()
                      || EDOKOS.use('skid_y'+_mas); EDOKOS.index('EDOKUM');
                         EDOKOS.prefix(EDOKUM.ref(),EDOKUM.B_PREL);
                         {? ~EDOKOS.last() || EDOKOS.blank() ?};
                         EDOKLOG.use('skid_d'+_mas); EDOKLOG.index('DISP');
                         OPERATOR.USER:={? exec('getUser','users',app_info('web_user')) || USERS.ref() || null ?};
                         exec('add_elog','obiegi_akc',web_params_get().log,1)
                      ?};
                      exec('dob_refresh','obiegi',_zakres,_typ)
                   ?}
                   ";
            web_ask(_zam,'Operacja nieodwracalna.\nZamknąć obieg dokumentu?'@,FUN.TYT)
         ?}
      ?}
   || FUN.info('Nie znaleziono delegacji.'@);
      exec('dob_refresh','obiegi',_a,_b)
   ?}
|| _jest:=exec('is_act_user','#b__box','OBG_FZO_DARK',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
   {? zakres=1 & _b='F' & _jest
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='OBG_FZO_DARK';
      _params.AKCJA:='Zamknij';
      _params.UIDREF:=EDOKUM.uidref();
      _params.WT_SKIP:=1;
      _params.CONTEXT:=obj_new('EDOKUM');
      _params.CONTEXT.EDOKUM:=EDOKUM.ref();
      exec('mp_run','#b__box',_params)
   |? zakres=2 | zakres=3 | zakres=5 | (zakres=1 & _b='F' & ~_jest)
   || {? EDOKUM.ZAM<>'T'
      || _dalej:=0;
         _pyt:=1;
         {? 'TX'*EDOKUM.TYP().W_PORTAL
         || _pyt:=0;
            EDOKOS.cntx_psh();
            EDOKOS.index('PORTAL'); EDOKOS.prefix(EDOKUM.ref(),'T',EDOKUM.B_PREL,OPERATOR.USER);
            {? EDOKOS.first()
            || {? exec('kom_edit','obiegi_akc','Z')
               || EDOKOS.STATUS:='E';
                  EDOKOS.memo_put(,'UW_DL');
                  EDOKOS.put();
                  _dalej:=1
               ?}
            || _pyt:=1
            ?};
            EDOKOS.cntx_pop()
         ?};
         {? _pyt
         || _dalej:=FUN.ask('Operacja nieodwracalna.\nZamknąć obieg dokumentu?'@)
         ?};
         {? _dalej
         || EDOKUM.ZAM:='T';
            {? EDOKUM.put || exec('add_elog','obiegi_akc',_log,1) ?};
            {? EDOKUM.f_active() || EDOKUM.f_rfresh() ?}
         ?}
      ?}
   || FUN.info('Operacja zamykania dotyczy wyłącznie rejestracji, przekazania i akceptacji.'@)
   ?}
?}


\wni_refr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Po odświeżeniu dla tabeli EDOKUMW
::   WE: _a - 'AKC' - akceptacja
::            'PRZ' - przekazanie
::            'WPR' - rejestracja
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
_gray:='';
{? EDOKUMW.get()
|| {? EDOKUMW.EDOKUM().ZAM='T'
   || {? _a='WPR'
      || _gray+='PUCI'
      |? _a='PRZ'
      || _gray+='JO'
      |? _a='AKC'
      || _gray+='CRI'
      ?}
   ?};
   {? ~(PAR_SKID.get(80)='T' & EDOKUMW.TYP().ED_PODZ='T')
   || _gray+='N'
   ?};
   {? ~EDOKUMW.TYP().OBS_PROJ
   || _gray+='T'
   ?};
   {? ~exec('dok_pow_log','obiegi_log') || _gray+='L' ?}
?};
_win:=_a;
_grey:=Plugin.run('ACT_GREY_EDOKUM_001',OPERATOR.USER,'EDOKUMW',_win);
{? type_of(_grey)=type_of('') & _grey<>''
|| STR.split(_grey,',');
   _gray+=STR.get_word()
?};
EDOKUMW.web_win_opt(web_top_win(),_a,'grayed='+_gray);
1


\del_refr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Po odświeżeniu dla tabeli EDOKUMW
::   WE: _a - 'AKC' - akceptacja
::            'PRZ' - przekazanie
::            'WPR' - rejestracja
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
_gray:='';
{? EDOKUMW.get()
|| {? EDOKUMW.EDOKUM().ZAM='T'
   || {? _a='WPR'
      || _gray+='PUCJ'
      |? _a='PRZ'
      || _gray+='JK'
      |? _a='AKC'
      || _gray+='CRJ'
      ?}
   ?}
?};
_grey:=Plugin.run('ACT_GREY_EDOKUM_001',OPERATOR.USER,'EDOKUMW',_a+'D');
{? type_of(_grey)=type_of('') & _grey<>''
|| STR.split(_grey,',');
   _gray+=STR.get_word()
?};
EDOKUMW.web_win_opt(web_top_win(),_a,'grayed='+_gray);
1


\jeden_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.42]
:: OPIS: Przekazanie zadania do tylko jednego operatora
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Przekazanie zadania tylko do jednego operatora?'@)
|| 'Tak'
|| 'Nie'
?}


\czy_zazn_usrrol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.42]
:: OPIS: Zaznaczenie roli użytkownika
::----------------------------------------------------------------------------------------------------------------------
_mark:=0;
{? EDOKPAR.JEDEN_OP='T'
|| B_USRROL.cntx_psh();
   {? B_USRROL.first()
   || {!
      |? exec('zazn_us_obi1','obiegi_akc');
         B_USRROL.next()
      !}
   ?};
   B_USRROL.cntx_pop()
?};
_mark


\prac_firm_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AB] [19.42]
:: OPIS: obsługa f3 pola OBIEGI.OSOBAOBI
::   WE: [_a] - Imię
::       [_b] - Nazwisko
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' & web_top_kind(0)='d'
|| OBIEGI.OSOBAOBI:=web_def_get().DOSTAWCA
?};
_prefix:=_=2;
__osobaF3:='';
_tcid:=app_info('web_tcid');
_mbid:=app_info('web_mbid');
_sesid:=app_info('web_sesid');
_tabid:=app_info('web_tabid');
_tym:=TAB_WT.ndx_tmp('',1,'TCID',,0,'TABID',,0,'TYP',,0,'STR04',,0);
TAB_WT.index(_tym); TAB_WT.prefix(_tcid,_tabid,5);
do();
{? TAB_WT.first()
|| {!
   |? _delr:=TAB_WT.del(,1);
      {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
   !}
?};
_data:=date();
{? PAR_SKID.get(460)='T'
|| exec('__F_ZATR','object');
   P.cntx_psh();
   P.use('pracowni');
   P.index('PRACONAZ');
   {? _prefix
   || P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,_b,_a,)
   || P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,)
   ?};
   H.cntx_psh();
   H.use('_hist');
   H.index('_HISTKOD');
   OSOBA.cntx_psh();
   OSOBA.index('OSOBA'); OSOBA.prefix();
   {? P.first()
   || {!
      |? _ok:=0;
         H.prefix(P.ref(),'Z');
         {? H.first()
         || {!
            |? {? _data>=H.OD & (_data<=H.DO | H.DO=date(0,0,0))
               || _ok:=1;
                  TAB_WT.blank(1);
                  TAB_WT.SESID:=_sesid;
                  TAB_WT.MBID:=_mbid;
                  TAB_WT.TCID:=_tcid;
                  TAB_WT.TABID:=_tabid;
                  TAB_WT.TYP:=5;
                  TAB_WT.STR01:=form(P.T+
                                     {? H.WYDZIAL || ' - '+H.WYDZIAL().SYMBOL || ''?}+
                                     {? H.ST ||  ' - '+H.ST().ST || ''?}
                                    );
                  TAB_WT.STR02:=$P.OSOBA().IMEX;
                  TAB_WT.STR03:=P.OSOBA().PIERWSZE;
                  TAB_WT.STR04:=P.OSOBA().NAZWISKO;
                  TAB_WT.STR05:=$P.OSOBA().ref();
                  TAB_WT.add()
               ?};
               ~_ok & H.next()
            !}
         ?};
         P.next()
      !}
   ?};
   OSOBA.cntx_pop(); P.cntx_pop(); H.cntx_pop()
|| OSOBA.cntx_psh();
   OSOBA.index('OSOBA'); OSOBA.prefix();
   {? OSOBA.first()
   || {!
      |? TAB_WT.blank(1);
         TAB_WT.SESID:=_sesid;
         TAB_WT.MBID:=_mbid;
         TAB_WT.TCID:=_tcid;
         TAB_WT.TABID:=_tabid;
         TAB_WT.TYP:=5;
         TAB_WT.STR02:=$OSOBA.IMEX;
         TAB_WT.STR03:=OSOBA.PIERWSZE;
         TAB_WT.STR04:=OSOBA.NAZWISKO;
         TAB_WT.STR05:=$OSOBA.ref();
         TAB_WT.add();
         OSOBA.next()
      !}
   ?};
   OSOBA.cntx_pop()
?};
end();
_wynik:=0;
{? OBIEGI.OSOBAOBI<>''
|| OSOBA.cntx_psh();
   OSOBA.index('OSOBA'); OSOBA.prefix();
   {? OSOBA.first()
   || {!
      |? {? -(OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO)=-(OBIEGI.OSOBAOBI)
         || _wynik:=TAB_WT.find_tab(0,'STR04',,'like',OSOBA.NAZWISKO,'STR03',,'like',OSOBA.PIERWSZE)
         || TAB_WT.first()
         ?};
         OSOBA.next() & _wynik<>1
      !}
   ?};
   OSOBA.cntx_pop()
|| TAB_WT.first()
?};
{? app_info('web_sesid')<>''
|| EDOKUM.web_cntx_save(1);
   TAB_WT.web_select('WER13',,1)
|| TAB_WT.win_sel('WER12');
   {? TAB_WT.select(,1)
   || __osobaF3:=OBIEGI.OSOBAOBI:=TAB_WT.STR03+' '+TAB_WT.STR04;
      OBIEGI.OSOBAID:=#TAB_WT.STR02;
      OSOBA.cntx_psh();
      OSOBA.index('OSOBA'); OSOBA.prefix();
      {? OSOBA.seek(TAB_WT.STR05)
      || EDOKUM.DOSTAWCA:=OSOBA.ref()
      ?};
      OSOBA.cntx_pop()
   ?}
?}


\prac_firm_af
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AB] [19.42]
:: OPIS: formuła po redakcji pola OBIEGI.OSOBAOBI
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| _result:=1;
   {? OBIEGI.OSOBAOBI=''
   || __dost_old:=EDOKUM.DOSTAWCA;
      EDOKUM.DOSTAWCA:=null()
   |? {? var_press('__osobaOLD')>0 || __osobaOLD<>OBIEGI.OSOBAOBI || 1 ?} | var_press('__osobaF3')>0
   || _liczbaOsob:=_znaleziono:=0;
      _imie:=_nazwisko:='';
      {? var_pres('__osobaF3')>0 & __osobaF3<>OBIEGI.OSOBAOBI || VAR_DEL.delete('__osobaF3') ?};
      {? PAR_SKID.get(460)='N'
      || OSOBA.cntx_psh(); OSOBA.index('OSOBA'); OSOBA.prefix();
         {? OSOBA.first()
         || {!
            |? _imie:=OSOBA.PIERWSZE; _nazwisko:=OSOBA.NAZWISKO;
               {? -(30+(_imie+' '+_nazwisko))=-(30+OBIEGI.OSOBAOBI)
               || _liczbaOsob:=exec('licz_osoby','obiegi2',_imie,_nazwisko);
                  {? _liczbaOsob=1
                  || {? OSOBA.IMEX=OBIEGI.OSOBAID
                     || EDOKUM.DOSTAWCA:=OSOBA.ref();
                        _znaleziono:=1
                     ?}
                  ?}
               ?};
               (~_znaleziono & _liczbaOsob<=1) & OSOBA.next()
            !}
         ?};
         OSOBA.cntx_pop()
      || P.cntx_psh(); P.use('pracowni'); P.index('PRACOSOB');
         P.prefix(exec('ref_firma','ustawienia'),);
         {? P.first()
         || {!
            |? _imie:=P.OSOBA().PIERWSZE; _nazwisko:=P.OSOBA().NAZWISKO;
               {? -(30+(_imie+' '+_nazwisko))=-(30+OBIEGI.OSOBAOBI)
               || {? var_press('__osobaF3')>0
                  || {? P.OSOBA().IMEX=OBIEGI.OSOBAID
                     || EDOKUM.DOSTAWCA:=P.OSOBA().ref();
                        _znaleziono:=1
                     ?}
                  || _liczbaOsob:=exec('licz_osoby','obiegi2',_imie,_nazwisko);
                     {? _liczbaOsob=1
                     || OBIEGI.OSOBAID:=P.OSOBA().IMEX;
                        EDOKUM.DOSTAWCA:=P.OSOBA().ref();
                        _znaleziono:=1
                     ?}
                  ?}
               ?};
               (~_znaleziono & _liczbaOsob<=1) & P.next()
            !}
         ?};
         P.cntx_pop()
      ?};
      {? _liczbaOsob>1 & _imie<>'' & _nazwisko<>''
      || exec('prac_firm_f3','obiegi2',_imie,_nazwisko);
         _znaleziono:=EDOKUM.DOSTAWCA().IMEX=OBIEGI.OSOBAID
      ?};
      VAR_DEL.delete('__osobaF3','__osobaOLD');
      {? _znaleziono
      || __dost_old:=EDOKUM.DOSTAWCA;
         exec('aewniosek','obiegi')
      || FUN.info('Nie znaleziono takiej osoby'@);
         _result:=0
      ?}
   || __dost_old:=EDOKUM.DOSTAWCA;
      exec('aewniosek','obiegi')
   ?};
   _result
?}


\prac_firm_af_www
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AB] [19.42]
:: OPIS: formuła po redakcji pola OBIEGI.OSOBAOBI (webterm)
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? OBIEGI.OSOBAOBI=''
|| web_msg('Pole musi być wypełnione'@,,'EXCLAM'); _result:=0
|| __liczbaOsob:=0;
   _znaleziono:={? var_press('__osobaF3')>0
                || exec('prac_firm_af_www1','obiegi2',OBIEGI.OSOBAOBI,OBIEGI.OSOBAID)
                |? -(30+OBIEGI.OSOBAOBI)<>-(30+(EDOKUM.DOSTAWCA().PIERWSZE+' '+EDOKUM.DOSTAWCA().NAZWISKO))
                || exec('prac_firm_af_www1','obiegi2',OBIEGI.OSOBAOBI)
                || EDOKUM.DOSTAWCA
                ?};
    {? _znaleziono
    || EDOKUM.DOSTAWCA:=_znaleziono;
       OBIEGI.OSOBAOBI:=EDOKUM.DOSTAWCA().PIERWSZE+' '+OSOBA.NAZWISKO;
       OBIEGI.OSOBAID:=OSOBA.IMEX;
       {? web_top_kind(0)<>'d' & typobi=3 || exec('aewniosek','obiegi') ?}
    || {? __liczbaOsob>1
       || web_msg('Znaleziono więcej niż jedną osobę. Wybierz za pomocą klawisza F3.'@,,'EXCLAM')
       || web_msg('Nie znaleziono takiej osoby.'@,,'EXCLAM')
       ?};
       _result:=0
    ?};
    VAR_DEL.delete('__osobaF3','__liczbaOsob')
?};
_result


\prac_firm_af_www1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AB] [19.42]
:: OPIS: Wyszukiwanie osoby
::   WE: _a - Imię i nazwisko osoby
::       [_b] - Identyfikator osoby
::   WY: OSOBA.ref
::----------------------------------------------------------------------------------------------------------------------
_osoba:={? var_press('_a')>0 || _a || OBIEGI.OSOBAOBI ?};
_osoba_id:={? var_press('_b')>0 || _b || OBIEGI.OSOBAID ?};
_liczbaOsob:={? var_press('__liczbaOsob')>0 || __liczbaOsob || 0 ?};
_znaleziono:=null;
_imie:=_nazwisko:='';
{? PAR_SKID.get(460)='N'
|| OSOBA.cntx_psh(); OSOBA.index('OSOBA'); OSOBA.prefix();
   {? OSOBA.first()
   || {!
      |? _imie:=OSOBA.PIERWSZE; _nazwisko:=OSOBA.NAZWISKO;
         {? var_press('__osobaF3')>0
         ||
            {? -(30+(_imie+' '+_nazwisko))=-(30+_osoba) & OSOBA.IMEX=_osoba_id
            || _znaleziono:=EDOKUM.DOSTAWCA:=OSOBA.ref()
            ?}
         ||
            {? -(30+(_imie+' '+_nazwisko))=-(30+_osoba)
            || _liczbaOsob:=exec('licz_osoby','obiegi2',_imie,_nazwisko);
               {? _liczbaOsob=1
               || _znaleziono:=EDOKUM.DOSTAWCA:=OSOBA.ref()
               ?}
            ?}
         ?};
         ~_znaleziono & _liczbaOsob<=1 & OSOBA.next()
      !}
   ?};
   OSOBA.cntx_pop()
|| OSOBA.cntx_psh(); P.cntx_psh();
   P.use('pracowni'); P.index('PRACOSOB'); P.prefix(exec('ref_firma','ustawienia'),);
   {? P.first()
   || {!
      |? _imie:=P.OSOBA().PIERWSZE; _nazwisko:=P.OSOBA().NAZWISKO;
         {? var_press('__osobaF3')>0
         ||
            {? -(30+(_imie+' '+_nazwisko))=-(30+_osoba) & P.OSOBA().IMEX=_osoba_id
            || _znaleziono:=EDOKUM.DOSTAWCA:=P.OSOBA().ref()
            ?}
         ||
            {? -(30+(_imie+' '+_nazwisko))=-(30+_osoba)
            || _liczbaOsob:=exec('licz_osoby','obiegi2',_imie,_nazwisko);
               {? _liczbaOsob=1
               || _znaleziono:=EDOKUM.DOSTAWCA:=P.OSOBA().ref()
               ?}
            ?}
         ?};
         ~_znaleziono & _liczbaOsob<=1 & P.next()
      !}
   ?};
   OSOBA.cntx_pop(); P.cntx_pop()
?};
{? var_press('__liczbaOsob')>0 || __liczbaOsob:=_liczbaOsob ?};
_znaleziono


\f_okro_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS: Formuła wyszukuje okres obrachunkowy dla podanej daty
::   WE: _a - data,
::   WY: OKRO_F.ref() lub null
::----------------------------------------------------------------------------------------------------------------------
{? _<2 || _b:=0 ?};
_ref:=null;
OKRO_F.cntx_psh();
OKRO_F.index('KON');
OKRO_F.prefix(REF.FIRMA);
{? OKRO_F.first()
|| {! |?
      {? OKRO_F.POCZ<>date(0,0,0) & _a>=OKRO_F.POCZ & _a<=OKRO_F.KON
      || _ref:=OKRO_F.ref();
         _dalej:=0
      || _dalej:=OKRO_F.next()
      ?};
      _dalej
   !}
?};
OKRO_F.cntx_pop();
_ref


\form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS:
::   WE: [_a] - link do dokumentu
::   WY: Formuła do ustalenia kontekstu okna
::----------------------------------------------------------------------------------------------------------------------
_maska:=ref_name(_a);
EDOKUM.cntx_psh();
EDOKUM.use(_maska);
EDOKUM.prefix();

_form:='';

::wybortyp:=0;
:: dla zalogowanego użytkownika
::exec('zakres','obiegi',3,3,0);
::{? EDOKUM.f_seek(_a,EDOKUM.name())
::|| _form:='win_activate(\'fobg_dekr\')'
::|| exec('zakres','obiegi',2,4,0);
::   {? EDOKUM.f_seek(_a,EDOKUM.name())
::   || _form:='win_activate(\'fobg_akc\')'
::   || exec('zakres','obiegi',5,4,0);
::      {? EDOKUM.f_seek(_a,EDOKUM.name())
::      || _form:='win_activate(\'fobg_prze\')'
::      || exec('zakres','obiegi',1,4,0);
::         {? EDOKUM.f_seek(_a,EDOKUM.name())
::         || _form:='win_activate(\'fobg_rej\')'
::         ?}
::      ?}
::   ?}
::?};
_typobieg:=exec('FindAndGet','#table',EDOKUM,_a,,"TYPOBIEG().NAZWA",'');

{? _typobieg<>'Obieg wniosków'
|| _up_rej:=exec('fobg_upr_zakl','obiegi2','rejestracja');
   _up_akc:=exec('fobg_upr_zakl','obiegi2','akceptacja');
   _up_dek:=exec('fobg_upr_zakl','obiegi2','dekretacja')
|| _up_rej:=exec('wobg_upr_zakl','obiegi2','rejestracja');
   _up_akc:=exec('wobg_upr_zakl','obiegi2','akceptacja');
   _up_dek:=exec('wobg_upr_zakl','obiegi2','dekretacja')
?};

_etap:=exec('FindAndGet','#table',EDOKUM,_a,,"B_PRELS",'');

{? _typobieg<>'Obieg wniosków'
|| {? -_etap*'dekretacja' & _up_dek  || _form:='win_activate(\'fobg_dekr\')'
   |? -_etap*'akceptacja' & _up_akc  || _form:='win_activate(\'fobg_akc\')'
   |? -_etap*'rejestracja' & _up_rej  || _form:='win_activate(\'fobg_rej\')'
   ?};
   {? _form=''
   || {? -_etap*'dekretacja' & _up_akc || _form:='win_activate(\'fobg_akc\')' ?};
      {? _form='' & _etap*'dekretacja' & _up_rej || _form:='win_activate(\'fobg_rej\')' ?}
   ?};
   {? _form=''
   || {? -_etap*'akceptacja' & _up_akc || _form:='win_activate(\'fobg_akc\')' ?}
   ?}
|| {? -_etap*'dekretacja' || _form:='win_activate(\'wobg_dekr\')'
   |? -_etap*'akceptacja' || _form:='win_activate(\'wobg_akc\')'
   |? -_etap*'rejestracja' || _form:='win_activate(\'wobg_rej\')'
   ?};
   {? _form=''
   || {? -_etap*'dekretacja' || _form:='win_activate(\'wobg_akc\')' ?};
      {? _form='' & _etap*'dekretacja' || _form:='win_activate(\'wobg_rej\')' ?}
   ?};
   {? _form=''
   || {? -_etap*'akceptacja' || _form:='win_activate(\'wobg_akc\')' ?}
   ?}
?};

EDOKUM.cntx_pop();
_form


\fobg_upr_zakl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS: Formuła zwraca informację 1/0 czy bieżący użytkownik ma uprawnienia do wyświetlenia zakładki
::       w obszarze faktur w obiegu
::   WE: _a - zakładka ('rejestracja','akceptacja','dekretacja')
::   WY: 1/l
::----------------------------------------------------------------------------------------------------------------------
{? _a='rejestracja'
|| exec('hasAction','users','OBG_FZO_DARK') | exec('hasAction','users','OBG_FZO_ZBRK')
   | exec('hasAction','users','OBG_FZO_ZAMK') | exec('hasAction','users','OBG_FZO_DASK')
   | exec('hasAction','users','CTR_OBA_PBKP') | exec('hasAction','users','CTR_OBA_PBUD')
|? _a='akceptacja'
|| exec('hasAction','users','OBG_FZO_EAKC') | exec('hasAction','users','OBG_FZO_ZAMK')
   | exec('hasAction','users','CTR_OBA_PBKP') | exec('hasAction','users','CTR_OBA_PBUD')
|? _a='dekretacja'
|| exec('hasAction','users','FKS_DKS_DFZO')
|| 0
?}


\link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS: Funkcja do obsługi przekazanego linku
::   WE: _a - link (STRING[48])
::   WY: 1/0 - czy ustawienia na podstawie linku poprawnml
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
_typobieg:=exec('FindAndGet','#table',EDOKUM,_a,,"TYPOBIEG().NAZWA",'');
{? ref_tab(_a)=EDOKUM
|| _maska:=ref_name(_a);
   EDOKUM.use(_maska);
   {? ~EDOKUM.seek(_a,EDOKUM.name())
   || FUN.info('Nie odnaleziono rekordu zgodnego z przekazanym linkiem.'@);
      _dalej:=0
   ?};

   {? _dalej & (~EDOKUM.TYPOBIEG().NAZWA='Obieg faktur' | ~EDOKUM.TYPOBIEG().NAZWA='Obieg wniosków')
   || FUN.emsg('Przekazany link nie jest związany z obiegiem dokumentów.'@);
      _dalej:=0
   ?};

   {? _dalej
   || _data:=EDOKUM.DO;
      _odd:=EDOKUM.ODD;
      _okres:=VAR.OKRO_F:=exec('f_okro_f','obiegi2',_data);
      ROK_F.cntx_psh();
      {? VAR.OKRO_F().ROK<>EDOKUM.ROK_F || _okres:=null ?};
      ROK_F.cntx_pop();
      {? _okres=null
      || _rok:=EDOKUM.ROK_F;
         {? _rok<>null
         || OKRO_F.cntx_psh();
            OKRO_F.index('NAZ1');
            OKRO_F.prefix(_rok);
            {? OKRO_F.first()
            || {! |? {? OKRO_F.POCZ=date(0,0,0) || OKRO_F.next() || 0 ?} !};
               _okres:=OKRO_F.ref()
            ?};
            OKRO_F.cntx_pop()
         ?}
      ?};

      {? _typobieg<>'Obieg wniosków'
      || {? _odd & _okres
         || {? exec('usr_fjks','b_perm',_odd)
            || __PARSES.setVal('JednostkaKsięgowa',_odd);
               __PARSES.setVal('OkresRok',_okres)
            || FUN.emsg('Brak uprawnień do jednostki księgowej związanej z przekazanym linkiem.'@);
               _dalej:=0
            ?}
         || FUN.emsg('Nie udało się ustawić prawidłowych parametrów pracy na podstawie przekazanego linku.'@);
            _dalej:=0
         ?}
      || {? _odd & _okres
         || {? exec('usr_fjks','b_perm',_odd)
            || __PARSES.setVal('JednostkaKsięgowa',_odd);
               __PARSES.setVal('OkresRok',_okres)
            || FUN.emsg('Brak uprawnień do jednostki księgowej związanej z przekazanym linkiem.'@);
               _dalej:=0
            ?}
         |? _okres
         || __PARSES.setVal('OkresRok',_okres)
         ?}
      ?};

      _chk_edokos:="EDOKOS.cntx_psh(); EDOKOS.use('skid_y'+(EDOKUM.name()+2)); EDOKOS.index('SZUK');
                    EDOKOS.prefix(EDOKUM.ref(),OPERATOR.USER); _zwrot:=EDOKOS.first();
                    EDOKOS.cntx_pop(); _zwrot
      ";
      _akcja:={? _typobieg='Obieg wniosków'
              || 'OBE_FAW_ZBRP'
              |? _typobieg='Obieg delegacji'
              || 'OBE_FDL_ZBSP'
              || 'OBG_FZO_ZBRK'
              ?};
      {? _dalej & ~(-EDOKUM.B_PRELS*'dekretacja') & ~exec('hasAction','users',_akcja) & ~_chk_edokos()
      || _dalej:=0;
         FUN.info('Brak uprawnień do wyświetlania dokumentów innych użytkowników.'@)
      ?};

      {? _dalej
      || OBIEGI.B_PREL:=EDOKUM.B_PREL;
         _akcja:={? _typobieg='Obieg wniosków'
                 || 'FKS_DKS_DFWO'
                 |? _typobieg='Obieg delegacji'
                 || 'FKS_DKS_DFDM'
                 || 'FKS_DKS_DFZO'
                 ?};
         {? ~exec('upr_etap','obiegi')
            & (~(-EDOKUM.B_PRELS*'dekretacja')
            | (-EDOKUM.B_PRELS*'dekretacja' & ~exec('hasAction','users',_akcja)))
         || FUN.info('Brak uprawnień do dokumentu w obiegu na jego bieżącym etapie.\n'
                     'Oczekiwany dokument może być niewidoczny dla bieżącego użytkownika.'@)
         ?}
      ?}
   ?}
|| FUN.emsg('Przekazany link nie jest związany z obiegiem dokumentów.'@);
   _dalej:=0
?};
_dalej


\after_select_action_web
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AB [19.42]
:: OPIS: Formuła wykonująca sie po akcji wybierz okienka webowego przy rejestracji dokumentu w obiegu
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.web_cntx_load(1);
OBIEGI.OSOBAOBI:=TAB_WT.STR03+' '+TAB_WT.STR04;
{? var_pres('__osobaF3')>0 || __osobaF3:=OBIEGI.OSOBAOBI ?};
OBIEGI.OSOBAID:=#TAB_WT.STR02;
exec('set_wt_par','obiegi2','STR05',TAB_WT.STR05);
EDOKUM.DOSTAWCA:=exec('prac_firm_af_www1','obiegi2',OBIEGI.OSOBAOBI,OBIEGI.OSOBAID);
TAB_WT.web_close();
{? web_top_kind(1)='e'
|| web_top_fld_update(1,1)
|| web_top_def_fld_update(1,1,OBIEGI.OSOBAOBI)
?};
1


\link_test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS: Czy rekord przekazany linkiem należy do bieżącej dziedziny
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUM.f_seek(__edoref) || 1 || 0 ?}


\okn_fakt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Tworzenie okienka grupowego
::   WE:  _a  - 1 - dekretacja z poziomu dokumentów księgowych (DOK)
::              0 - wszystkie dokumenty
::  OLD: \okn_grup/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('dekrobizm','dok_zbl');
dekrobizm:={? var_pres('_a')>0 || _a || 0 ?};
{? typobi=1
|| _okn_grp:=EDOKUM.grp_make(,,'edokum_sel',,,"{? ~dekrobizm || exec('exit','zws') || 1 ?}");
   exec('make_ctr','obiegi2');
   _ref_edok:=$('exec(\'edokum_rfr\',\'obiegi\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\');'
                'exec(\'edokum_rfr1\',\'obiegi\');'
                'tab_hide(4,,''tab2'')');
   {? _a=0
   || EDOKUM.grp_sel(_okn_grp,EDOKUM,'FWPR','Rejestracja'@,_ref_edok,,,,"dok_zbl:=0; exec('po_edokum','obiegi',1)",,,,'maximized','fobg_rej');
      task_attach('OBG_FZO_DARK');
      task_attach('OBG_FZO_ZBRK');
      task_attach('OBG_FZO_ZAMK');
      task_attach('OBG_FZO_DASK');
      task_attach('CTR_OBA_PBKP');
      task_attach('CTR_OBA_PBUD');
      EDOKUM.grp_sel(_okn_grp,EDOKUM,'FPRZ','Przekazanie'@,_ref_edok,,,,"dok_zbl:=0; exec('po_edokum','obiegi',5)",,,,'maximized','fobg_prze');
      task_attach('OBG_FZO_CPRZ');
      task_attach('CTR_OBA_PBKP');
      task_attach('CTR_OBA_PBUD');
      EDOKUM.grp_sel(_okn_grp,EDOKUM,'FAKC','Akceptacja'@,_ref_edok,,,,"dok_zbl:=0; exec('po_edokum','obiegi',2)",,,,'maximized','fobg_akc');
      task_attach('OBG_FZO_EAKC');
      task_attach('OBG_FZO_ZAMK');
      task_attach('CTR_OBA_PBKP');
      task_attach('CTR_OBA_PBUD');
      EDOKUM.grp_sel(_okn_grp,EDOKUM,'FDEK','Dekretacja'@,_ref_edok,,,,"dok_zbl:=0; exec('po_edokum','obiegi',3)",,,,'maximized','fobg_dekr');
      task_attach('FKS_DKS_DFZO');
      {? PAR_SKID.get(464)='T' || VAR_DEL.delete('OBGZAPOT'); exec('win_obg','obiegi_zap',_okn_grp) ?};
      {? PAR_SKID.get(444)='T' || exec('winObg','zbl_dok',_okn_grp) ?}
   || EDOKUM.grp_sel(_okn_grp,EDOKUM,'FDEK','Dekretacja faktur'@,_ref_edok,,,,
                     "dok_zbl:=0; exec('po_edokum','obiegi',3)",,,,'maximized');
      task_attach('FKS_DKS_DFZO')
   ?};
   EDOKUM.grp_splt(_okn_grp,'panel0','horizontal','tab2',22);
   EDOKUM.grp_sel(_okn_grp,EDOKUMPR,'WER','Projekty'@,,,,,"exec('bobs_projekty_jt','obiegi_proj')",,,,'maximized');
   EDOKUM.grp_sel(_okn_grp,EPODZ,'WER_FAK','Podziały dla controllingu'@,,,,3,"exec('bobs_epodz_jt','obiegi')",,,,'maximized');
   _befor:="exec('bobs_edokumz','obiegi')";
   EDOKUM.grp_ctr(_okn_grp,EDOKUMZ,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_befor,,,'maximized');
   {? PAR_SKID.get(444)='T'
   || EDOKUM.grp_ctr(_okn_grp,DOKUM,__CTH.WIN_ACR,,'Log'@,,,,,,"exec('bl_log_html','zbl_dok')",,,'maximized')
   ?};
   {? exec('upgrade2325_blbc1','zbl') || EDOKUM.grp_sel(_okn_grp,DOKUMZBC,'WER_EDOK','Rezultat kontroli Businesscheck'@,,,,,,,,,'maximized') ?}
|? typobi=2
|| _okn_grp:=EDOKUM.grp_make(,,'edokumw_sel',,,"{? ~dekrobizm || exec('exit','zws') || 1 ?}");
   _okno:='WDEK';
   exec('make_ctr','obiegi2');
   _ref_edok:=$('exec(\'edokum_rfr\',\'obiegi\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\');'
                'exec(\'edokum_rfr1\',\'obiegi\');'
                'tab_hide(3,,''tab2''); {? exec(''upgrade2325_blbc1'',''zbl'') || tab_hide(4,,''tab2'') ?} ');
   {? var_pres('OBEWin')>0
   || EDOKUM.grp_sel(_okn_grp,EDOKUM,OBEWin.rwin,'Rejestracja'@,_ref_edok,,,,"dok_zbl:=0; exec('po_edokum','obiegi',1,'W')",,,,'maximized','wobg_rej');
      task_attach('OBE_FAW_DARP');
      EDOKUM.grp_sel(_okn_grp,EDOKUM,OBEWin.pwin,'Przekazanie'@,_ref_edok,,,,"dok_zbl:=0; exec('po_edokum','obiegi',5,'W')",,,,'maximized','wobg_prze');
      task_attach('OBE_FAW_CPRZ');
      EDOKUM.grp_sel(_okn_grp,EDOKUM,OBEWin.awin,'Akceptacja'@,_ref_edok,,,,"dok_zbl:=0; exec('po_edokum','obiegi',2,'W')",,,,'maximized','wobg_akc');
      task_attach('OBE_FAW_EAKP')
   ?};
   EDOKUM.grp_sel(_okn_grp,EDOKUM,_okno,'Dekretacja'@,_ref_edok,,,,"dok_zbl:=0; exec('po_edokum','obiegi',3)",,,,'maximized','wobg_dekr');
   task_attach('FKS_DKS_DFWO');
   {? PAR_SKID.get(445)='T' || exec('winObe','zbl_dok',_okn_grp) ?};
   EDOKUM.grp_splt(_okn_grp,'panel0','horizontal','tab2',22);
   _befor:="exec('bobs_edokumz','obiegi')";
   EDOKUM.grp_ctr(_okn_grp,EDOKUMZ,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_befor,,,'maximized');
   EDOKUM.grp_sel(_okn_grp,EDOK_ATK,'WER2','Informacje dodatkowe'@,,,,,,,,,'maximized');
  {? PAR_SKID.get(445)='T'
  || EDOKUM.grp_ctr(_okn_grp,DOKUM,__CTH.WIN_ACR,,'Log'@,,,,,,"exec('bl_log_html','zbl_dok')",,,'maximized')
  ?};
  {? exec('upgrade2325_blbc1','zbl') || EDOKUM.grp_sel(_okn_grp,DOKUMZBC,'WER_EDOK','Rezultat kontroli Businesscheck'@,,,,,,,,,'maximized') ?}
|? typobi=3
|| _okn_grp:=EDOKUM.grp_make(,,'edokumw_sel',,,"{? ~dekrobizm || exec('exit','zws') || 1 ?}");
   _okno:='DDEK';
   exec('make_ctr','obiegi2');
   _ref_edok:=$('exec(\'edokum_rfr\',\'obiegi\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\'); exec(\'edokum_rfr1\',\'obiegi\')');
   EDOKUM.grp_sel(_okn_grp,EDOKUM,_okno,'Dekretacja'@,_ref_edok,,,,
                      "exec('po_edokum','obiegi',3)",,,,'maximized');
   EDOKUM.grp_splt(_okn_grp,'panel0','horizontal','tab2',20);
   _befor:="exec('bobs_edokumz','obiegi')";
   EDOKUM.grp_ctr(_okn_grp,EDOKUMZ,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_befor,,,'maximized');
   EDOKUM.grp_sel(_okn_grp,EDOK_ATR,'WER','Informacje dodatkowe'@,,,,,,,,,'maximized')
?};
EDOKUM.win_sel(_okn_grp);
AreaTitle.setTabWin(EDOKUM,_okn_grp);
AreaTitle.setTitle()


\blp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Formuła sprawdzająca aktywność podatnika VAT w dokumencie w obiegu
::   WE: [_a] - blokować? 1-tak [0]-nie
::       [_b] - tabela na kounikaty
::----------------------------------------------------------------------------------------------------------------------
_blok:={? var_pres('_a')>0 || _a ?};
_snip:=exec('niptostr','#string',SKID.NIP);
_is_tab:=var_pres('_b')=type_of(SYSLOG) & var_pres('BLOKAKC',_b)=type_of('') & var_pres('OPIS',_b)=type_of('');
{? _snip<>'' & EDOKUM.DOP<>date(0,0,0)
|| _tab:={? _is_tab
         || _b
         || tab_tmp(2,
               'BLOKAKC','INTEGER','Blokada akceptacji'@,
               'OPIS','STRING[255]','Opis'@
            )
         ?};
   _obj:=exec('check','blp','K',_snip,EDOKUM.DOP);
   {? _obj.ACTIVE<>'T'
   || _tab.BLOKAKC:=_blok;
      _tab.OPIS:={? _obj.ACTIVE='N'
                 || 'Nieaktywny podatnik VAT ('+$EDOKUM.DOP+')'
                 || 'Nie udało się sprawdzenie aktywności podatnika VAT ('+$EDOKUM.DOP+')'
                 ?};
      _tab.add()
   ?};
   _tab
|? _is_tab
|| _b
|| ~~
?}


\nazwa_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja zwraca nazwę dla wniosku o zmianę danych osobowych
::  OLD: \nazwa_adres/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Zmiana adresu'


\nazwa_awans
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja zwraca nazwę dla wniosku o przeszeregowanie/awans
::  OLD: \nazwa_awans/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Przeszeregowanie/Awans'


\nazwa_dane_os
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja zwraca nazwę dla wniosku o zmianę danych osobowych
::  OLD: \nazwa_dane_os/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Zmiana danych osobowych'


\nazwa_opieka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja zwraca nazwę dla wniosku o dni opieki
::  OLD: \nazwa_opieka/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Dni opieki nad dzieckiem do lat 14'


\nazwa_zatr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja zwraca nazwę dla wniosku o zatrudnienie
::  OLD: \nazwa_zatr/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Zatrudnienie'


\nazwa_nast_H_UM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja zwraca nazwę dla wniosku o kolejna umowe o prace
::  OLD: \nazwa_nast_H_UM/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Kolejna umowa o pracę'


\nazwa_zwoln
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja zwraca nazwę dla wniosku o zwolnienie
::  OLD: \nazwa_zwoln/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Zwolnienie'


\nazwa_adoUS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [mk] Michal Kocot [12.50]
:: OPIS: Funkcja zwraca nazwę dla wniosku Aktualizacja danych o Urzedzie Skarbowym
::  OLD: \nazwa_adoUS/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych o Urzędzie Skarbowym'


\nazwa_zdoNFZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [mk] Michal Kocot [12.50]
:: OPIS: Funkcja zwraca nazwe dla wniosku o zmiane danych o NFZ
::  OLD: \nazwa_zdoNFZ/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Zmiana danych o NFZ'


\nazwa_worr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [mk] Michal Kocot [12.50]
:: OPIS: Funkcja zwraca nazwe dla wniosku o realizacje rachunku
::  OLD: \nazwa_worr/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Wniosek o realizację rachunku'


\nazwa_wop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [mk] Michal Kocot [12.50]
:: OPIS: Funkcja zwraca nazwe dla wniosku o przeszeregowanie
::  OLD: \nazwa_wop/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Wniosek o przeszeregowanie'


\nazwa_woku
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [mk] Michal Kocot [12.50]
:: OPIS: Funkcja zwraca nazwe dla wniosku o kontynuacje umowy
::  OLD: \nazwa_woku/etypy.fml
::----------------------------------------------------------------------------------------------------------------------
'Wniosek o kontynuację umowy'


\spr_sym_fakt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Sprawdza czy nie wprowadzono juz faktury o podanym symbolu. Jezeli sa faktury o podanym symbolu wyswietla liste
::       tych faktur
::  OLD: \spr_sym_fakt/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
{? form(EDOKUM.SYM)=''
|| return(1)
?};
_zwrot:=1; _sym:=EDOKUM.SYM; _typobi:=$EDOKUM.TYPOBIEG;
_fun:="_a+((60-(+_a))*' ')";
_fedok:='';
ROK_F.cntx_psh();
ROK_F.index('KOD');
ROK_F.prefix(REF.FIRMA);
{? ROK_F.seek(SSTALE.AR)
|| _fedok+='\'skid_v'+($(ROK_F.POCZ_ROK~1)+2)+'\',';
   {? ROK_F.prev() || _fedok+='\'skid_v'+($(ROK_F.POCZ_ROK~1)+2)+'\',' ?};
   _fedok:=_fedok-1
?};
ROK_F.cntx_pop();
_refd:={? -menu_txt()='popraw' || $EDOKUM.ref() || '' ?};
_symsql:=STR.gsub(_sym,'\'','\'\'');
_tab:=sql('select '+
          '/*+MASK_FILTER(EDOKUM,:_b) '+
          'INDEX (EDOKUM,TYPOBI) */ '+
          'ODD.OD ODDZIAL, EDOKUM.ID, EDOKUM.SYM SYMBOL, EDOKUM.DW DATA, EDOKUM.REFERENCE REF, '+
          'KH.KOD KH_KOD, KH.NAZ KH_NAZ '+
          'from @EDOKUM '+
          'join ODD using (EDOKUM.ODD,ODD.REFERENCE) '+
          'left join KH using (EDOKUM.KHKH,KH.REFERENCE) '+
          'where EDOKUM.SYM=\':_a\' and EDOKUM.TYPOBIEG=\':_c\''+
          {? _refd='' || '' || 'and EDOKUM.REFERENCE<>\':_d\' ' ?}+
          'order by 1,2,4',_symsql,_fedok,_typobi,_refd);
{? _tab.first()
|| _zwrot:=FUN.choice('Faktura o takim symbolu została wprowadzona wcześniej'@,0,'Pokaż'@,'Kontynuuj'@);
   {? _zwrot=1
   || _wer:=_tab.mk_sel('Dokumenty z symbolem %1'@[_sym],,0);
      _tab.win_fld(_wer,,'ODDZIAL',,,8,,,'Jednostka księgowa'@);
      _tab.win_fld(_wer,,'ID',,,20,,,'Identyfikator'@);
      _tab.win_fld(_wer,,'SYMBOL',,,30,,,'Symbol'@);
      _tab.win_fld(_wer,,'DATA',,,11,,,'Data wystawienia'@);
      _tab.win_fld(_wer,,'KH_KOD',,,5,,,'Kod kontrahenta'@);
      _tab.win_fld(_wer,,'KH_NAZ',,,40,,,'Nazwa kontrahenta'@);
      _tab.win_act(_wer,0,'Formuła','Kontynuuj'@@,,
                   'Kontynuuj redakcję dokumentu - symbol pozostanie bez zmian.'@,,"sel_exit()",1);
      _tab.win_sel(_wer);
      _zwrot:=_tab.select()
   ?}
?};
_zwrot


\ustaw_pola_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawia pola dla webterma
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| exec('czytaj','#stalesys');
   _okno:=web_top_win();
   {? EDOKUM.TYP
   || EDOKUM.TYP();
      {? ETYPY.ED_PODZ='T'
      || EDOKUM.web_btn_eopt(_okno,'','btnPODZ','state=normal')
      || EDOKUM.web_btn_eopt(_okno,'','btnPODZ','state=grayed')
      ?};
      {? ETYPY.OBS_PROJ
      || EDOKUM.web_btn_eopt(_okno,,'btnPROJ','state=normal')
      || EDOKUM.web_btn_eopt(_okno,,'btnPROJ','state=grayed')
      ?};
      {? exec('be_id_edokum1','obiegi')
      || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',EDOKUM,'ID')
      || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=1, editable=0',EDOKUM,'ID')
      ?};
      {? typobi=2
      || {? ETYPY.CZY_ZALP='T' & ETYPY.ETYPZAL
         || {? ETYPY.CZY_PRAC
            || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',OBIEGI,'OSOBAOBI')
            || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=0',OBIEGI,'OSOBAOBI')
            ?};
            EDOKUM.web_efld_opt(_okno,,'mark=1',EDOKUM,'TP')
         || {? ETYPY.CZY_PRAC
            || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=1, editable=1',OBIEGI,'OSOBAOBI')
            || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=1, editable=0',OBIEGI,'OSOBAOBI')
            ?};
            EDOKUM.web_efld_opt(_okno,,'mark=0',EDOKUM,'TP')
         ?};
         {? ETYPY.CZY_DOPE='N'
         || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=0',EDOKUM,'DOP')
         || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',EDOKUM,'DOP')
         ?};
         {? ETYPY.CZY_WAR | (ETYPY.CZY_ZALP='T' & ETYPY.ETYPZAL & ETYPY.ETYPZAL().ED_WAL='T')
         || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',EDOKUM,'WAL')
         || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=1, editable=0',EDOKUM,'WAL')
         ?};
         {? ETYPY.CZY_DATY=0
         || EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'DATA_OD');
            EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'DATA_DO');
            EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'GODZ_OD');
            EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'GODZ_DO')
         |? ETYPY.CZY_DATY=1
         || EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'DATA_OD');
            EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'DATA_DO');
            EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'GODZ_OD');
            EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'GODZ_DO')
         || EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'DATA_OD');
            EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'DATA_DO');
            EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'GODZ_OD');
            EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'GODZ_DO')
         ?};
         {? (ETYPY.CZY_WAR=2 | ETYPY.CZY_WAR=3) & ~(ETYPY.CZY_ZALP='T' & ETYPY.ETYPZAL)
         || EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'NETTO')
         || EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'NETTO')
         ?};
         {? ETYPY.CZY_ZALP='T' & ETYPY.ETYPZAL
         || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',EDOKUM,'WART')
         |? ETYPY.CZY_WAR=1 | ETYPY.CZY_WAR=3
         || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=1, editable=1',EDOKUM,'WART')
         || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=0, editable=0',EDOKUM,'WART')
         ?};
         EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',EDOKUM,'TR');
         {? ETYPY.CZY_ZALP='T' & ETYPY.ETYPZAL
         || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',EDOKUM,'KASPRZEL');
            {? EDOKUM.KASPRZEL='P' & EDOKUM.DOSTAWCA
            || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',EDOKUM,'N')
            || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=0',EDOKUM,'N')
            ?};
            EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'PLACE');
            EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'POTRODDN')
         || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=0',EDOKUM,'KASPRZEL');
            EDOKUM.web_efld_opt(_okno,,'mark=0, enable=0',EDOKUM,'N');
            EDOKUM.web_efld_opt(_okno,,'mark=0, enable=0',EDOKUM,'PLACE');
            EDOKUM.web_efld_opt(_okno,,'mark=0, enable=0',EDOKUM,'POTRODDN')
         ?}
      |? typobi=3
      || {? XINFO.CZY_CEL
         || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',EDOKUM,'DEL_CEL')
         || EDOKUM.web_efld_opt(_okno,,'mark=0, enable=1, editable=1',EDOKUM,'DEL_CEL')
         ?};
         EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',EDOKUM,'DEL_MIE');
         {? ETYPY.CZY_PRAC
         || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=1',OBIEGI,'OSOBAOBI')
         || EDOKUM.web_efld_opt(_okno,,'mark=1, enable=1, editable=0',OBIEGI,'OSOBAOBI')
         ?};
         {? ETYPY.CZY_DATY
         || {? ETYPY.CZY_DATY=2
            || EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=1',EDOKUM,'DATA_OD');
               EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=1',EDOKUM,'DATA_DO')
            || EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=0',EDOKUM,'DATA_OD');
               EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=0',EDOKUM,'DATA_DO')
            ?}
         || EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'DATA_OD');
            EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'DATA_DO')
         ?};
         {? ETYPY.CZY_GODR
         || {? ETYPY.CZY_GODR=2
            || EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=1',EDOKUM,'GODZ_OD')
            |? ETYPY.CZY_GODR=1
            || EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1',EDOKUM,'GODZ_OD')
            || EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'GODZ_OD')
            ?}
         || EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'GODZ_OD')
         ?};
         {? ETYPY.CZY_GODZ
         || {? ETYPY.CZY_GODZ=2
            || EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=1',EDOKUM,'GODZ_DO')
            |? ETYPY.CZY_GODZ=1
            || EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1',EDOKUM,'GODZ_DO')
            || EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'GODZ_DO')
            ?}
         || EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'GODZ_DO')
         ?};
         {? ETYPY.DEL_ZAGR
         || EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=0',EDOKUM,'GRAN_DAT');
            EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=0',EDOKUM,'GRAN_GDZ');
            EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=0',EDOKUM,'GRAN_DPW');
            EDOKUM.web_efld_opt(_okno,,'editable=1, enable=1, mark=0',EDOKUM,'GRAN_GPW')
         || EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'GRAN_DAT');
            EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'GRAN_GDZ');
            EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'GRAN_DPW');
            EDOKUM.web_efld_opt(_okno,,'enable=0, mark=0',EDOKUM,'GRAN_GPW')
         ?};
         exec('del_etat','obiegi')
      ?};
      {? typobi=2 | typobi=3
      || {? ETYPY.CZY_KH
         || EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',SKID,'SL_KH');
            web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'ed_kh_ed',1))
         || EDOKUM.web_efld_opt(_okno,,'enable=0',SKID,'SL_KH');
            web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'ed_kh_ed',0))
         ?};
         {? ETYPY.CZY_KH & +SKID.SL_KH
         || EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',SKID,'NIP')
         || EDOKUM.web_efld_opt(_okno,,'enable=0',SKID,'NIP')
         ?};
         {? ETYPY.CZY_KH & +SKID.SL_KH
         || SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
            {? SLU.find_key(SKID.SL_KH) & (SLU.NAZ=SKID.SL_KH)
            || EDOKUM.web_efld_opt(_okno,,'enable=1, editable=1',EDOKUM,'KH')
            || EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'KH')
            ?};
            SLU.cntx_pop()
         || EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'KH')
         ?}
      ?}
   || {? typobi=2 | typobi=3
      || EDOKUM.web_efld_opt(_okno,,'enable=0',SKID,'SL_KH');
         web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'ed_kh_ed',0));
         {? SKID.SL_KH=''
         || EDOKUM.web_efld_opt(_okno,,'enable=0',SKID,'NIP');
            EDOKUM.web_efld_opt(_okno,,'enable=0',EDOKUM,'KH')
         ?}
      ?}
   ?};
   {? typobi=2
   || {? exec('dok_pow_log','obiegi_log')
      || EDOKUM.web_btn_einit(_okno,,'btnDOK_POW','state=normal')
      || EDOKUM.web_btn_einit(_okno,,'btnDOK_POW','state=grayed')
      ?}
   ?}
?}


\set_tak_nie_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.30]
:: OPIS: Zwraca wartosc odpowiadajaca prawdzie lub falszowi dla atrybutow typu tak/nie
::   WE: _a - tresc pytania ['']
::----------------------------------------------------------------------------------------------------------------------
{? (_<1) | (_a=~~) || _a:='' ?};
{? type_of(_a)<>type_of('') || &_a;_a:='' ?};
EDOK_ATR.web_cntx_save();
_us:="EDOK_ATR.web_cntx_load();
      EDOKUM.use('skid_v'+(EDOK_ATR.name()+2)); EDOK_ATR.EDOKUM();
      {? _a=0 | _a=1
      || EDOK_ATR.WAR:={? _a=0
                       || exec('get_war_true','obiegi')
                       |? _a=1
                       || exec('get_war_false','obiegi')
                       ?};
         {? EDOK_ATR.put() || exec('po_red','obiegi',EDOKUM) ?};
         web_top_refresh(0)
      ?}
    ";
web_choice(_us,_a,,'ASK',2,3,'Tak'@,'Nie'@,'Rezygnacja'@)


\wyb_slo_innd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na wybór rekordu w SLO - webterm
::----------------------------------------------------------------------------------------------------------------------
exec('env_edokum_get1','obiegi');
EDOK_ATR.web_cntx_load(1);
EDOKUM.use('skid_v'+(EDOK_ATR.name()+2)); EDOKUM.prefix(); EDOK_ATR.EDOKUM();
EDOK_ATR.SLO:=SLO.ref();
_pole:=exec('get_atr_slopol','obiegi',EDOKUM.TYP,EDOK_ATR.KOL);
EDOK_ATR.WAR:={? _pole='T' || SLO.TR || SLO.KOD ?};
{? EDOK_ATR.put() || exec('po_red','obiegi',EDOKUM) ?};
SLO.web_close();
web_top_refresh(1)


\tat_t_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Edycja atrybutu typu T (string) - webterm
::----------------------------------------------------------------------------------------------------------------------
_def:=web_define();
EDOKUM.use('skid_v'+(EDOK_ATR.name()+2)); EDOKUM.prefix();
web_def_fld(_def,'WAR',EDOK_ATR.WAR,EDOK_ATR.TAT().NA,EDOK_ATR.TAT().OPIS,80,80,,);
EDOK_ATR.web_cntx_save();
web_def_edit(_def,"EDOK_ATR.web_cntx_load();
                   EDOKUM.use('skid_v'+(EDOK_ATR.name()+2)); EDOK_ATR.EDOKUM();
                   {? _a='OK'
                   || EDOK_ATR.WAR:=web_def_get(web_top_win()).WAR;
                      {? EDOK_ATR.put() || exec('po_red','obiegi',EDOKUM) ?}
                   ?};
                   web_def_close(web_top_win());
                   web_top_refresh(1)
                  ",'Informacje dodatkowe'@)


\clean_k_bud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Usuwa dane kontroli budzetów dla sesii webterma
::   WE: _a - id sesji
::  OLD: \usun/!obe_faw_pbkp.fml
::----------------------------------------------------------------------------------------------------------------------
K_BUDN.cntx_psh(); K_BUDP.cntx_psh();
K_BUDP.index('UNIK');
K_BUDN.index('UNIK'); K_BUDN.prefix(_a);
{? K_BUDN.first()
|| {!
   |? K_BUDP.prefix(K_BUDN.ref());
      {? K_BUDP.first() || {! |? K_BUDP.del() !} ?};
      K_BUDN.del()
   !}
?};
K_BUDS.index('SYM');
K_BUDS.prefix(_a);
{? K_BUDS.first() || {! |? K_BUDS.del !} ?};
K_BUDN.cntx_pop(); K_BUDP.cntx_pop();
~~


\spr_edok_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Sprawdzanie wydatkow
::  OLD: \spr_edok_zal/delegdan.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
EDOK_ZAL.ETYPWYD();
{? (ETYPWYD.ED_FULL=1 | ETYPWYD.ED_FULL=3) & EDOK_ZAL.NETTO$2<=0
|| FUN.info('Kwota netto musi być większa od zera.'@); _zwrot:='NETTO'
|? (ETYPWYD.ED_FULL=2 | ETYPWYD.ED_FULL=3) & EDOK_ZAL.BRUTTO$2<=0
|| FUN.info('Kwota brutto musi być większa od zera.'@); _zwrot:='BRUTTO'
|? ETYPWYD.ED_FULL=3 & EDOK_ZAL.BRUTTO$2<EDOK_ZAL.NETTO$2
|| FUN.info('Kwota netto nie może być większa od kwoty brutto.'@); _zwrot:='NETTO'
?};
_zwrot


\dol_dob_wt_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołaczanie dokumentów w obiegu na WEBTERMIE - init
::   WE: _a - 1 - uruchamiane z TODO
::            0 - uruchamiane z obszaru roboczego
::            2 - uruchamiane z pulpitu
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
exec('czytaj','#stalesys');
{? typobi=3 & ~XINFO.SLU_CEL
|| FUN.info('Nie wybrano słownika celów delegacji.'@); _zwrot:=0
|? ~FINFO.SLWAL
|| FUN.info('Nie wybrano słownika walut.'@); _zwrot:=0
?};
{? _zwrot
|| VAR_DEL.delete('numer','oldnumer');
   dol_dob:=1;
   OBIEGI.OBIEGKON:=OBIEGI.FAKT_DEL:=1;
   exec('typobi','obiegi');
   SKID.SL_KH:='';
   OPERATOR.USER();
   EDOKUM.blank();
   {? EDOKUM.ODD=null || EDOKUM.ODD:=exec('one_odd','obiegi') ?};
   EDOKUM.FIRMA:=REF.FIRMA;
   EDOKUM.ROK_F:=SSTALE.AR;
   EDOKUM.UNIK_ID:=tm_stamp();
   EDOKUM.TYP:=typ_ust;
   {? EDOKUM.TYP
   || EDOKUM.TYP();
      OSOBA.cntx_psh();
      OPERATOR.USER().OSOBA();
      {? ETYPY.CZY_PRAC & ~exec('prac_firm_af_www1','obiegi2',OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO,OSOBA.IMEX)
      || EDOKUM.DOSTAWCA:=null()
      || EDOKUM.DOSTAWCA:=OSOBA.ref()
      ?};
      OSOBA.cntx_pop();
      {? ETYPY.ED_TEMAT='N'
      || EDOKUM.TR:=EDOKUM.TYP().NAZWA
      ?}
   ?};
   {? EDOKUM.ODD=null || EDOKUM.ODD:=exec('edokum_odd','obiegi2') ?};
   exec('tabkhini','kontrahent');
   _okno:={? typobi=2
          || {? _a=1 || 'WW021' |? _a=2 || 'WW022' || 'WW020' ?}
          || {? _a=1 || 'WD021' |? _a=2 || 'WD022' || 'WD020' ?}
          ?};
   {? exec('beodddob','obiegi')
   || EDOKUM.web_efld_init(_okno,,'win_dict=SLO_WT, mark=0, enable=1, editable=1',EDOKUM,'ODD')
   || EDOKUM.web_efld_init(_okno,,'win_dict=SLO_WT, enable=1, editable=0',EDOKUM,'ODD')
   ?};
   {? exec('betyped','obiegi')
   || EDOKUM.web_efld_init(_okno,,'win_dict=SLO_WYBW, mark=1, enable=1, editable=1',EDOKUM,'TYP')
   || EDOKUM.web_efld_init(_okno,,'win_dict=SLO_WYBW, mark=1, enable=1, editable=0',EDOKUM,'TYP')
   ?};
   {? typobi=2 & EDOKUM.TYP
   || EDOKUM.TYP();
      exec('ae_typ_zal','obiegi')
   ?};
   exec('okna_ini','obiegi',_okno);
   {? EDOKUM.TYP
   || {? ETYPY.ED_PODZ='T'
      || EDOKUM.web_btn_einit(_okno,,'btnPODZ','state=normal')
      || EDOKUM.web_btn_einit(_okno,,'btnPODZ','state=grayed')
      ?};
      {? ETYPY.OBS_PROJ
      || EDOKUM.web_btn_einit(_okno,,'btnPROJ','state=normal')
      || EDOKUM.web_btn_einit(_okno,,'btnPROJ','state=grayed')
      ?}
   ?};
   {? typobi=2
   || {? exec('dok_pow_log','obiegi_log')
      || EDOKUM.web_btn_einit(_okno,,'btnDOK_POW','state=normal')
      || EDOKUM.web_btn_einit(_okno,,'btnDOK_POW','state=grayed')
      ?}
   ?};
   SKID.SL_KH:=SKID.NIP:=''
?};
_zwrot


\dol_dob_wt1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Dołączanie dokumentów w obiegu - WEBTERM + wewnętrzna
::   WE: [_a] - dodawać inne dane? [1]-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<0 || _a:=1 ?};
EDOKUM.prefix();
_ref:=null;
EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
{? EDOKUM.put() & EDOKUM.memo_put(,'UW_OPDL')
|| _par:=web_global_params_get();
   web_global_params_set(exec('obj_ntab_set','#array',_par,
                         'act_ok',1
                        ));
   _web_params:=web_params_get();
   {? type_of(_par)>100 & var_pres('path_run',_par)>0 & _par.path_run='Proc'
   || _prel:=exec('FindAndGet','#table',B_PREL,_web_params.B_PREL);
      _akcja:='';
      exec('web_run','#b__box',_prel,_akcja,,'web_Proc')
   || exec('mp_run','#b__box',_web_params)
   ?};
   zakres:=1;
   exec('dol_edokos','obiegi');
   OBIEGI.EDOKOS();
   {? exec('szuk_edokpar','obiegi') & EDOKPAR.FORM_ADD
   || ($EDOKPAR.FORM_ADD().TRESC)()
   ?};
   {? EDOKUM.TYP().ATR_G1R>0 & _a
   || _edit:=exec('blok_innd','obiegi')=0;
      refr_okn:=0;
      EDOK_ATR.cntx_psh();
      EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
      EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',);
      {? _edit & ~EDOK_ATR.first()
      || exec('edk_atr_dolacz','obiegi',0,0,1)
      ?};
      EDOK_ATR.cntx_pop();
      VAR_DEL.delete('refr_okn')
   ?}
?}


\update_edokumw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Aktualizuje dokument w obiegu w webtermie
::----------------------------------------------------------------------------------------------------------------------
EDOKUMW.index('UNIK1');
EDOKUMW.prefix(EDOKUMW.EDOKUM,EDOKUMW.RODZAJ,EDOKUMW.TCID,EDOKUMW.TABID);
{? EDOKUMW.first()
|| {!
   |? EDOKUMW.KH:=EDOKUM.KH;
      EDOKUMW.TYP:=EDOKUM.TYP;
      EDOKUMW.ALERTY:=EDOKUM.ALERTY;
      EDOKUMW.STDEKRD:=EDOKUM.STDEKRD;
      EDOKUMW.DOSTAWCA:=EDOKUM.DOSTAWCA;
      EDOKUMW.DEL_CEL:=EDOKUM.DEL_CEL;
      EDOKUMW.ZAM:=EDOKUM.ZAM;
      EDOKUMW.USERS:=EDOKUM.USERS;
      EDOKUMW.put();
      EDOKUMW.next()
   !}
?}


\pop_dob_wt1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Poprawianie dokumentów w obiegu - WEBTERM - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.prefix();
_ref:=null;
{? ~(var_pres('zakres')>0 & zakres=2)
|| EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
   EDOKUM.USERS:=OPERATOR.USER
?};
{? EDOKUM.put() & EDOKUM.memo_put(,'UW_OPDL')
|| web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),
                         'act_ok',1
                        ));
   _params:=exec('mp_run_a','#b__box');
   {? typobi=2
   || _params.ACT_UID:={? var_pres('zakres')>0 & zakres=2
                       || 'OBE_FAW_EAKP'
                       || 'OBE_FAW_DARP'
                       ?}
   || _params.ACT_UID:='OBE_FDL_DBRP'
   ?};
   _params.AKCJA:='Popraw'@;
   _params.UIDREF:=EDOKUM.uidref();
   _params.CONTEXT:=obj_new('EDOKUM');
   _params.CONTEXT.EDOKUM:=EDOKUM.ref();
   _params.WT_SKIP:=1;
   exec('mp_run','#b__box',_params)
?}


\pop_dob_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzanie dokumentu przed poprawianiem
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? exec('spr_edokrzap','obiegi',1) || _dalej:=0 ?};
{? _dalej & EDOKUM.ZAM='T'
|| {? typobi=1
   || FUN.info('Faktura zamknięta.'@)
   |? typobi=2
   || FUN.info('Wniosek zamknięty.'@)
   |? typobi=3
   || FUN.info('Delegacja zamknięta.'@)
   ?};
   _dalej:=0
?};
{? _dalej & EDOKUM.STDEKRD
|| {? typobi=1
   || FUN.info('Faktura zadekretowana.'@)
   |? typobi=2
   || FUN.info('Wniosek zadekretowany.'@)
   || FUN.info('Delegacja zadekretowana.'@)
   ?};
   _dalej:=0
?};
{? _dalej & typobi=3 & EDOKUM.REFLISTA<>''
|| FUN.info('Delegacja wykorzystywana w module kadrowo - płacowym.'@); _dalej:=0
?};
{? _dalej & zakres=3
|| EDOKOS.cntx_psh(); EDOKOS.index('SZUK5'); EDOKOS.prefix(EDOKUM.ref(),'D','T');
   {? ~EDOKOS.first()
   || {? typobi=1
      || FUN.info('Faktura nie jest jeszcze w czynności dekretacji.'@)
      |? typobi=2
      || FUN.info('Wniosek nie jest jeszcze w czynności dekretacji.'@)
      |? typobi=3
      || FUN.info('Delegacja nie jest jeszcze w czynności dekretacji.'@);
         _dalej:=0
      ?}
   ?};
   EDOKOS.cntx_pop()
?};
{? _dalej & zakres=3 & EDOKUM.TYP().DEKR='N'
|| {? typobi=1
   || FUN.info('Faktura nie podlega dekretacji.'@)
   |? typobi=2
   || FUN.info('Wniosek nie podlega dekretacji.'@)
   |? typobi=3
   || FUN.info('Delegacja nie podlega dekretacji.'@)
   ?};
   _dalej:=0
?};
{? _dalej
|| {? zakres=1
   || _dalej:=exec('spr_czy_edit','obiegi','W')
   |? zakres=3
   || _dalej:=exec('spr_czy_edit','obiegi','D')
   ?};
   {? ~_dalej
   || {? typobi=1
      || FUN.info('Faktura przekazana do dalszego obiegu.'@)
      |? typobi=2
      || FUN.info('Wniosek przekazany do dalszego obiegu.'@)
      || FUN.info('Delegacja przekazana do dalszego obiegu.'@)
      ?};
      _dalej:=0
   ?}
?};
_dalej


\us_dob_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzanie dokumentu przed usuwaniem
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
_size:={? app_info('web_sesid')=''
       || EDOKUM.sel_size()
       || 0
       ?};
{? EDOKUM.ZAM='T'
|| {? ~_size
   || {? typobi=1
      || FUN.info('Faktura zamknięta.'@)
      |? typobi=2
      || FUN.info('Wniosek zamknięty.'@)
      |? typobi=3
      || FUN.info('Delegacja zamknięta.'@)
      ?}
   ?}; _dalej:=0
?};
{? _dalej & EDOKUM.STDEKRD
|| {? ~_size
   || {? typobi=1
      || FUN.info('Faktura zadekretowana.'@)
      |? typobi=2
      || FUN.info('Wniosek zadekretowany.'@)
      |? typobi=3
      || FUN.info('Delegacja zadekretowana.'@)
      ?}
   ?}; _dalej:=0
?};
{? _dalej & EDOKUM.REFLISTA<>'' & typobi=3
|| {? ~_size || FUN.info('Delegacja wykorzystana w module kadrowo - płacowym.'@) ?};
   _dalej:=0
?};
{? _dalej & ~exec('spr_czy_edit','obiegi','W')
|| {? ~_size
   || {? typobi=1
      || FUN.info('Faktura przekazana kolejnemu użytkownikowi.'@)
      |? typobi=2
      || FUN.info('Wniosek przekazany kolejnemu użytkownikowi.'@)
      |? typobi=3
      || FUN.info('Delegacja przekazana kolejnemu użytkownikowi.'@)
      ?}
   ?}; _dalej:=0
?};
{? _dalej & EDOKUM.ZD_NAG<>''
|| FUN.info('Wniosek typu zapotrzebowanie powstały z zamówienia.'@); _dalej:=0
?};
_dalej


\dol_edokos1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [1210]
:: OPIS: Dolaczanie rekordow w tabeli EDOKOS - akceptacja
::   WE: _a - user
::       _b - numer operacji
::----------------------------------------------------------------------------------------------------------------------
{? OBIEGI.B_PREL<>''
|| EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL,_a);
   {? ~EDOKOS.first()
   || EDOKOS.blank(1); EDOKOS.STATUS:='N'; _add:=1
   || {? var_pres('wym_stat')>0 || EDOKOS.STATUS:={? wym_stat=2 || 'O' || 'N' ?} ?}; _add:=0
   ?};
   EDOKOS.EDOKUM:=EDOKUM.ref();
   {? PAR_SKID.get(457)='T' || EDOKOS.UW_KR:=exec('szuk_uwagi','obiegi',EDOKUM.ref()) ?};
   EDOKOS.OPERACJA:='A';
   EDOKOS.OPER_NUM:=2;
   EDOKOS.USERS:=_a;
   EDOKOS.B_PREL:=OBIEGI.B_PREL;
   EDOKOS.B_PRELS:=OBIEGI.B_PRELS;
   EDOKOS.WID:={? var_pres('wid_eds')>0 || wid_eds || 'T' ?};
   {? EDOKOS.B_PREL<>''
   || {? _add
      || EDOKOS.LP:=exec('lp_edokos','obiegi'); EDOKOS.add()
      || EDOKOS.put()
      ?}
   ?};
   OBIEGI.EDOKOS:=EDOKOS.ref();
   {? (EDOKUM.TYP().W_PORTAL<>'N' | ETYPY.IN_POR) & (var_pres('_b')<=0 | _b=3 | _b=4 & _add=1)
   || USERS.cntx_psh();
      USERS.prefix();
      _email:={? USERS.seek(_a) || exec('wp_email','obiegi2')<>'' ?};
      USERS.cntx_pop();
      {? _email
      || EDOKOS.cntx_psh();
         EDOKOS.index('PORTAL'); EDOKOS.prefix(EDOKUM.ref(),'S',OBIEGI.B_PREL,_a,'N');
         {? ~EDOKOS.first()
         || EDOKOS.cntx_psh();
            EDOKOS.prefix(EDOKUM.ref(),'S',OBIEGI.B_PREL,);
            {? EDOKOS.first()
            || _no:=EDOKOS.OPER_NUM
            || EDOKOS.index('DISP'); EDOKOS.prefix('S',EDOKUM.ref());
               _no:={? EDOKOS.last() || EDOKOS.OPER_NUM+1 || 1 ?}
            ?};
            _data:=date();
            _time:=time();
            {? var_pres('TmpToDo')>0 & TmpToDo.find_key(OBIEGI.B_PREL,_a)
            || _data:=TmpToDo.DATE;
               _time:=TmpToDo.TIME
            ?};
            EDOKOS.cntx_pop();
            EDOKOS.blank(1);
            EDOKOS.EDOKUM:=EDOKUM.ref();
            EDOKOS.USERS:=_a;
            EDOKOS.OPERACJA:='Y';
            EDOKOS.DATA:=_data;
            EDOKOS.TIME:=_time;
            EDOKOS.B_PREL:=OBIEGI.B_PREL;
            EDOKOS.B_PRELS:=OBIEGI.B_PRELS;
            EDOKOS.STATUS:='N';
            EDOKOS.WID:='S';
            EDOKOS.OPER_NUM:=_no;
            EDOKOS.add()
         ?};
         EDOKOS.prefix(EDOKUM.ref(),'P');
         {? EDOKOS.first()
         || {!
            |? {? 'DY'*EDOKOS.OPERACJA
               || EDOKOS.del()
               || EDOKOS.next()
               ?}
            !}
         ?};
         EDOKOS.cntx_pop();
         {? EDOKUM.STATUS<>'s'
         || EDOKUM.STATUS:='s';
            EDOKUM.put()
         ?};
         {? var_pres('StartProc')>0
         || StartProc:=2
         ?}
      ?}
   ?};
   EDOKOS.cntx_pop()
?}


\dol_edokos2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [1210]
:: OPIS: Dolaczanie rekordow w tabeli EDOKOS - przekazanie do akceptacji
::   WE: _a - user
::----------------------------------------------------------------------------------------------------------------------
{? OBIEGI.B_PREL<>''
|| {? var_pres('non_eds')>0 || return(1) ?};
   EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL,_a);
   {? ~EDOKOS.first()
   || EDOKOS.blank(1); EDOKOS.STATUS:='N'; _add:=1
   || {? var_pres('wym_stat')>0 || EDOKOS.STATUS:={? wym_stat=2 || 'O' || 'N' ?} ?}; _add:=0
   ?};
   EDOKOS.EDOKUM:=EDOKUM.ref();
   EDOKOS.OPERACJA:='P';
   EDOKOS.OPER_NUM:=4;
   EDOKOS.USERS:=_a;
   EDOKOS.B_PREL:=OBIEGI.B_PREL;
   EDOKOS.B_PRELS:=OBIEGI.B_PRELS;
   EDOKOS.WID:={? var_pres('wid_eds')>0 || wid_eds || 'T' ?};
   {? EDOKOS.B_PREL<>''
   || {? _add
      || EDOKOS.LP:=exec('lp_edokos','obiegi'); EDOKOS.add()
      || EDOKOS.put()
      ?}
   ?};
   OBIEGI.EDOKOS:=EDOKOS.ref();
   {? (EDOKUM.TYP().W_PORTAL='N' & ETYPY.IN_POR) & (var_pres('_b')<=0 | _b=3 | _b=4 & _add=1)
   || USERS.cntx_psh();
      USERS.prefix();
      _email:={? USERS.seek(_a) || exec('wp_email','obiegi2')<>'' ?};
      USERS.cntx_pop();
      {? _email
      || EDOKOS.cntx_psh();
         EDOKOS.index('PORTAL'); EDOKOS.prefix(EDOKUM.ref(),'S',OBIEGI.B_PREL,_a,'N');
         {? ~EDOKOS.first()
         || EDOKOS.cntx_psh();
            EDOKOS.prefix(EDOKUM.ref(),'S',OBIEGI.B_PREL,);
            {? EDOKOS.first()
            || _no:=EDOKOS.OPER_NUM
            || EDOKOS.index('DISP'); EDOKOS.prefix('S',EDOKUM.ref());
               _no:={? EDOKOS.last() || EDOKOS.OPER_NUM+1 || 1 ?}
            ?};
            _data:=date();
            _time:=time();
            {? var_pres('TmpToDo')>0 & TmpToDo.find_key(OBIEGI.B_PREL,_a)
            || _data:=TmpToDo.DATE;
               _time:=TmpToDo.TIME
            ?};
            EDOKOS.cntx_pop();
            EDOKOS.blank(1);
            EDOKOS.EDOKUM:=EDOKUM.ref();
            EDOKOS.USERS:=_a;
            EDOKOS.OPERACJA:='Y';
            EDOKOS.DATA:=_data;
            EDOKOS.TIME:=_time;
            EDOKOS.B_PREL:=OBIEGI.B_PREL;
            EDOKOS.B_PRELS:=OBIEGI.B_PRELS;
            EDOKOS.STATUS:='N';
            EDOKOS.WID:='S';
            EDOKOS.OPER_NUM:=_no;
            EDOKOS.add()
         ?};
         EDOKOS.prefix(EDOKUM.ref(),'P');
         {? EDOKOS.first()
         || {!
            |? {? 'DY'*EDOKOS.OPERACJA
               || EDOKOS.del()
               || EDOKOS.next()
               ?}
            !}
         ?};
         EDOKOS.cntx_pop();
         {? EDOKUM.STATUS<>'s'
         || EDOKUM.STATUS:='s';
            EDOKUM.put()
         ?};
         {? var_pres('StartProc')>0
         || StartProc:=2
         ?}
      ?}
   ?};
   EDOKOS.cntx_pop()
?}


\ust_biez_czynn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawia bieżącą czynność w EDOKUM dla EDOKOS
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.use('skid_v'+(EDOKOS.name()+2)); EDOKUM.prefix(); EDOKOS.EDOKUM();
{? var_pres('no_bprel')>0 & no_bprel=1
|| no_bprel:=0
|| {? EDOKOS.WID='T'
   || EDOKUM.B_PREL:=EDOKOS.B_PREL;
      EDOKUM.B_PRELS:=EDOKOS.B_PRELS;
      {? EDOKUM.DOKUM<>''
      ||
         DOKUM.cntx_psh(); DOKUM.prefix();
         {? DOKUM.seek(EDOKUM.DOKUM) & DOKUM.OBKONETP<>EDOKUM.B_PRELS
         || DOKUM.OBKONETP:=EDOKUM.B_PRELS; DOKUM.put()
         ?};
         DOKUM.cntx_pop()
      ?};
      EDOKUM.put()
   ?}
?};
1


\make_ctr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów dla kontrolki z załącznikami
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB','BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND');
__CTR.BTAB:='EDOKUMZ';
__CTR.TAB:='EDOKUM';
__CTR.BBLOB:='EDOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','bloby','edokumf_ctr',':');
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('sep','#file',1);
__CTR.DND:=1


\b_w_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Formuła na wartość początkową zmiennej POMOC.W_PORTAL
::----------------------------------------------------------------------------------------------------------------------
{? POMOC.W_PORTAL<>'' || POMOC.W_PORTAL || 'N' ?}


\typ_wportal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Sprawdza, czy typ dokumentu jest tylko dla Portalu ABS 2.0
::   WE: _a - typ dokumentu w obiegu (string)
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? var_pres('TYP_DOK',_a)>0 & _a.TYP_DOK<>''
|| ETYPY.cntx_psh(); ETYPY.index('UNIK'); ETYPY.prefix(exec('bl_typ','obiegi',typobi));
   {? ETYPY.find_key(_a.TYP_DOK) || {? ETYPY.W_PORTAL='T' || _result:=1 ?} ?};
   ETYPY.cntx_pop()
?};
_result


\etyp_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Formula na wyswietl w oknie WERW tabeli ETYP
::----------------------------------------------------------------------------------------------------------------------
{? typobi=2
|| {? ETYPY.W_PORTAL='P'
   || ETYPY.win_edit('REDWPL')
   |? ETYPY.W_PORTAL='T'
   || ETYPY.win_edit('REDWWP')
   |? ETYPY.W_PORTAL='s'
   || ETYPY.win_edit('REDWPSEO')
   || ETYPY.win_edit(exec('REDW','obiegi2'))
   ?};
   {? 'PTs'*ETYPY.W_PORTAL
   || exec('etypy_efld_opt','obiegi_typy','*',ETYPY,ETYPY.win_edit('?'),,'put')
   ?}
|| ETYPY.win_edit('REDD')
?};
ETYPY.display();
1


\etp_atr_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Po redakcji rekordow w tabeli etp_atr
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? _zwrot='' & 'TPs'*POMOC.W_PORTAL & ETYP_ATR.ID_WP=''
|| FUN.info('Nie wprowadzono identyfikatora.'@); _zwrot:='ID_WP'
?};
{? _zwrot='' & 'TPs'*POMOC.W_PORTAL & ETYP_ATR.ID_WP<>'' & ~exec('etp_atr_chk_id','obiegi2',ETYP_ATR.ID_WP)
|| FUN.info('Istnieje już atrybut dokumentu o tym identyfikatorze.'@); _zwrot:='ID_WP'
?};
{? _zwrot='' & ETYP_ATR.TAT=null
|| FUN.info('Nie wprowadzono typu atrybutu.'@); _zwrot:='TAT'
?};
_zwrot


\ae_etp_atr_id_wp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Po edycji pola ID_WP tabeli ETYP_ATR - identyfikator - portal
::----------------------------------------------------------------------------------------------------------------------
_id_wp:=exec('rb_nosp','#string',fld());
{? _id_wp<>fld()
|| FUN.info('Identyfikator nie może zawierać spacji.'@);
   fld():=_id_wp
?};
1


\ae_etypy_id_wp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Po edycji pola ID_WP tabeli ETYPY - identyfikator - portal
::----------------------------------------------------------------------------------------------------------------------
_id_wp:=exec('rb_nosp','#string',fld());
{? _id_wp<>fld()
|| FUN.info('Identyfikator nie może zawierać spacji.'@);
   fld():=_id_wp
?};
1


\etypy_rodz_wp_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Zwraca tabelę z rodzajamu typów wykorzystywanych na portalu (pole ETYPY.RODZ_WP)
::   WE:
::   WY: tabela
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1
::   rodzaj typu - określenie numeryczne
   ,'KOD','INTEGER','Kod'
::   nazwa rodzaju
   ,'NAZWA','STRING[50]','Nazwa'
::   typ akceptacji wniosku - tą wartość wysyłamy przez API
   ,'AKC_TYPE','STRING[50]','Typ akceptacji'
);

_add:="
   _a.blank();
   _a.KOD:=_b;
   _a.NAZWA:=_c;
   _a.AKC_TYPE:=_d;
   _a.add()
";

_add(_tab,1,'Wniosek pracownika'@,'Wniosek pracownika');
_add(_tab,2,'Wniosek managera'@,'Wniosek managera');
_add(_tab,3,'Zapotrzebowanie'@,'Wniosek administracyjny');
_add(_tab,7,'Wniosek paperless'@,'Wniosek paperless');
{? ETYPY.W_PORTAL='s' | ETYPY.W_PORTAL='N' & ETYPY.IN_POR
|| _add(_tab,9,'Wniosek pozostały'@,'Wniosek z podziałem kosztowym')
?};
{? exec('is_pzd03','ppsf')
|| _add(_tab,10,'Wniosek o świadczenie pracy'@,'Wniosek pracownika')
?};
_tab


\spr_pvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Sprawdzanie pozycji VAT w dokumencie
::   WE: _a - 1 - wyjscie z okienka z pozycjami VAT, 0 - zwykla akceptacja
::       _b - (integer/0) - czy operacja grupowa
::   WY: (1/0) - czy pozycje VAT sa prawidlowo wprowadzone
::  OLD: \spr_pvat/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? app_info('web_sesid')<>'' || exec('czytaj','#stalesys',,FINFO) ?};
{? (EDOKUM.WAL=FINFO.NAROD | (EDOKUM.WAL<>FINFO.NAROD & EDOKUM.TYP().DOK_VAT='T')) & ~EDOKUM.TYP().TYPVATPR
|| {? _zwrot
   || EVAT.cntx_psh(); EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
      {? EVAT.first()
      || {! |? brutdob+=EVAT.BRUTTO; netdob+=EVAT.NETTO; EVAT.next() !}
      |? EDOKPAR.POZV_WYM='T'
      || {? var_pres('KOM_DEL')>0
         || exec('add_kom_del','obiegi','Nie wprowadzono pozycji VAT faktury wydatkowej %1'@[EDOKUM.SYM])
         || _kom:='Nie wprowadzono pozycji VAT.'@;
            exec('add_kom','obiegi',~_b & ~_a,1,_kom)
         ?};
         _zwrot:=0
      ?};
      EVAT.cntx_pop()
   ?};
   {? _zwrot & EDOKUM.WART<>brutdob
   || {? var_pres('KOM_DEL')>0
      || exec('add_kom_del','obiegi','Suma brutto z pozycji VAT nie jest zgodna z sumą wprowadzoną w nagłówku faktury wydatkowej %1'@[EDOKUM.SYM])
      || _kom:='Suma brutto z pozycji VAT nie jest zgodna z sumą wprowadzoną w nagłówku faktury.'@;
         exec('add_kom','obiegi',~_b & ~_a,1,_kom)
      ?};
      _zwrot:=0
   ?};
   {? _zwrot & EDOKUM.NETTO<>netdob
   || {? var_pres('KOM_DEL')>0
      || exec('add_kom_del','obiegi','Suma netto z pozycji VAT nie jest zgodna z sumą wprowadzoną w nagłówku faktury wydatkowej %1'@[EDOKUM.SYM])
      || _kom:='Suma netto z pozycji VAT nie jest zgodna\nz sumą wprowadzoną w nagłówku faktury.'@;
         exec('add_kom','obiegi',~_b & ~_a,1,_kom)
      ?};
      _zwrot:=0
   ?};
   _pr:=0;
   EVAT.cntx_psh();
   EVAT.index('EDOKUM');
   EVAT.prefix(EDOKUM.ref);
   {? EVAT.first()
   || {! |? {? EVAT.PR=null || _pr:=1; 0 || EVAT.next() ?} !}
   ?};
   EVAT.cntx_pop();
   {? _zwrot & _pr
   || {? var_pres('KOM_DEL')>0
      || exec('add_kom_del','obiegi','Brak stawki VAT w dokumencie %1'@[EDOKUM.SYM])
      || _kom:='W pozycji VAT nie jest uzupełniona stawka VAT.'@;
         exec('add_kom','obiegi',,~_b & ~_a,1,_kom)
      ?};
      _zwrot:=0
   ?}
?};
_zwrot


\spr_pozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Sprawdzanie pozycji faktury w dokumencie przy rejestracji
::----------------------------------------------------------------------------------------------------------------------
_akcept:=1;
POZF.cntx_psh();
POZF.use('pozf__'+(EDOKUM.name()+2));
POZF.index('EDOKUM'); POZF.prefix(EDOKUM.ref());
{? exec('szuk_edokpar','obiegi') & (var_pres('POZF_WYM',EDOKPAR)>0) & EDOKPAR.POZF_WYM='T' & ~POZF.first()
|| _kom:='Nie wprowadzono pozycji faktury dla dokumentu.'@;
   exec('add_kom','obiegi',0,1,_kom);
   _akcept:=0
?};
POZF.cntx_pop();
_akcept


\import_pozf_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Import pozycji faktur
::----------------------------------------------------------------------------------------------------------------------
{? Plugin.exists('OBG_IMP_POZF_001') || Plugin.run('OBG_IMP_POZF_001') ?}


\blok_pozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Sprawdza, czy można dać akcje edycyjne w pozycjach faktury
::   WY: 1/0 - czy założona blokada
::----------------------------------------------------------------------------------------------------------------------
_blok:=0;
{? var_pres('zakres')>0 || exec('rkprz1','obiegi',zakres) ?};
{? EDOKUM.ZAM='T' | EDOKUM.STDEKRD | EDOKUM.REFLISTA<>'' | EDOKUM.ZD_NAG<>'' | zakres=5
|| _blok:=1
?};
{? ~_blok & zakres<>3
|| {? OBIEGI.EDOKOS
   || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      OBIEGI.EDOKOS();
      {? ~(exec('szuk_edokpar','obiegi') & {? var_pres('ED_POZF',EDOKPAR) > 0
                                           || EDOKPAR.ED_POZF='T'
                                           || 0 ?} )
         &
         OBIEGI.B_PREL=OBIEGI.EDOKOS().B_PREL
      || _blok:=1
      ?};
      {? ~_blok
      || _stat:=exec('spr_stat','obiegi');
         _blok:=(_stat='T' | _stat='X')
      ?}
   ?}
?};
_blok


\blok_pozv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Sprawdza, czy można dać akcje edycyjne w pozycjach VAT
::   WY: 1/0 - czy założona blokada
::----------------------------------------------------------------------------------------------------------------------
_blok:=0;
{? var_pres('zakres')>0 || exec('rkprz1','obiegi',zakres) ?};
{? EDOKUM.ZAM='T' | EDOKUM.STDEKRD | EDOKUM.REFLISTA<>'' | EDOKUM.ZD_NAG<>'' | zakres=5
|| _blok:=1
?};
{? ~_blok & zakres<>3
|| {? OBIEGI.EDOKOS
   || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      OBIEGI.EDOKOS();
      {? ~(exec('szuk_edokpar','obiegi') & {? var_pres('ED_POZV',EDOKPAR) > 0
                                           || EDOKPAR.ED_POZV='T'
                                           || 0 ?} )
         &
         OBIEGI.B_PREL=OBIEGI.EDOKOS().B_PREL
      || _blok:=1
      ?};
      {? ~_blok
      || _stat:=exec('spr_stat','obiegi');
         _blok:=(_stat='T' | _stat='X')
      ?}
   ?}
?};
_blok


\pozf_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Akcja dołącz pozycje faktur dokumentu w obiegu w WebTerm
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' & web_top_kind()='s'
||
   PozFEdok:=1;
   EDOKUM.web_cntx_load(1);
   EDOKUM.web_cntx_save(1);
   POZF.blank();
   _form:="
            EDOKUM.web_cntx_load(1);
            exec('env_wt','b_proces');
            {? _a='OK'
            || exec('ar_pozf','!fks_dks_dark','ADD')
            |? _a='ANULUJ'
            ||  POZF.web_eclose()
            ?}
            ";
   POZF.web_efld_init('REDFW',,'win_dict=SLOW',,'JM');
   POZF.web_edit('REDFW','_pozf_red',,,_form)
?}


\pozf_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Akcja popraw pozycje faktur dokumentu w obiegu w WebTerm
::----------------------------------------------------------------------------------------------------------------------
{? POZF.get()
||
   {? app_info('web_sesid')<>'' & web_top_kind()='s'
   ||
      EDOKUM.web_cntx_load(1);
      EDOKUM.web_cntx_save(1);
      {? POZF.EDOKUM().ZAM='T'
      || FUN.info('Dokument zamknięty, nie podlega modyfikacjom.'@);
         return()
      ?};
      _form:="
               EDOKUM.web_cntx_load(1);
               exec('env_wt','b_proces');
               {? _a='OK'
               || exec('ar_pozf','!fks_dks_dark','PUT')
               |? _a='ANULUJ'
               ||  POZF.web_eclose()
               ?}
               ";
      POZF.web_efld_init('REDFW',,'win_dict=SLOW',,'JM');
      POZF.web_edit('REDFW','_pozf_red',,,_form)
   ?}
|| FUN.info('Nie znaleziono pozycji VAT.'@);
   web_top_refresh()
?}


\evat_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Akcja dołącz pozycje VAT dokumentu w obiegu w WebTerm
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' & web_top_kind()='s'
||
   ETYPY.index('UNIK'); ETYPY.prefix(exec('bl_typ','obiegi',1),'Faktura VAT');
   ETYPY.first();
   EVAT.blank();
   _form:="
           EDOKUM.web_cntx_load(1);
           exec('env_wt','b_proces');
            {? _a='OK'
            || _chk:=exec('chk_vat','!obg_fzo_dark');
               {? _chk=''
               || EVAT.add();
               exec('sum_vat','obiegi',1);
               EVAT.web_eclose();
               EVAT.web_refresh('WERFKW')
               |? _chk='NETTO+VAT<>BRUTTO'
               ||
                  _ask:='Wartości pozycji netto+VAT nie zgadzają się z'
                         '\nwartością pozycji brutto na kwotę: %1'
                         '\nnetto        = %2'
                         '\nvat            = %3'
                         '\nnetto+vat = %4'
                         '\nbrutto       = %5'
                         '\nAkceptować?\n'@[form((EVAT.NETTO$2+EVAT.VAT$2-EVAT.BRUTTO$2)$2,,2),form(EVAT.NETTO$2,16,2),
                         form(EVAT.VAT$2,16,2),form(EVAT.NETTO$2+EVAT.VAT$2,16,2),form(EVAT.BRUTTO$2,16,2)];
                  exec('obs_kom_evat','obiegi2',_ask,'ADD')
               |? _chk='KWOTA_VAT<>NETTO*VAT'
               ||
                  _ask:='Wartość pozycji VAT nie zgadza się z procentem VAT. Akceptować?'@;
                  exec('obs_kom_evat','obiegi2',_ask,'ADD')
               ?}
           |? _a='ANULUJ'
           ||  EVAT.web_eclose()
           ?}";
   EVAT.web_cntx_save(1);
   EDOKUM.web_cntx_save(1);
   EVAT.web_edit('REDW','_evat_red',,,_form)
?}


\evat_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Akcja popraw pozycje VAT dokumentu w obiegu w WebTerm
::----------------------------------------------------------------------------------------------------------------------
{? EVAT.get()
||
   {? app_info('web_sesid')<>'' & web_top_kind()='s'
   ||
      {? EVAT.EDOKUM().ZAM='T'
      || FUN.info('Dokument zamknięty, nie podlega modyfikacjom.'@);
         return()
      ?};
      _form:="
               EDOKUM.web_cntx_load(1);
               exec('env_wt','b_proces');
               {? _a='OK'
               || _chk:=exec('chk_vat','!obg_fzo_dark');
                  {? _chk=''
                  || EVAT.put();
                  exec('sum_vat','obiegi',1);
                  EVAT.web_eclose();
                  EVAT.web_refresh('WERFKW')
                  |? _chk='NETTO+VAT<>BRUTTO'
                  ||
                  _ask:='Wartości pozycji netto+VAT nie zgadzają się z'
                         '\nwartością pozycji brutto na kwotę: %1'
                         '\nnetto        = %2'
                         '\nvat            = %3'
                         '\nnetto+vat = %4'
                         '\nbrutto       = %5'
                         '\nAkceptować?\n'@[form((EVAT.NETTO$2+EVAT.VAT$2-EVAT.BRUTTO$2)$2,,2),form(EVAT.NETTO$2,16,2),
                         form(EVAT.VAT$2,16,2),form(EVAT.NETTO$2+EVAT.VAT$2,16,2),form(EVAT.BRUTTO$2,16,2)];
                     exec('obs_kom_evat','obiegi2',_ask,'PUT')
                  |? _chk='KWOTA_VAT<>NETTO*VAT'
                  ||
                     _ask:='Wartość pozycji VAT nie zgadza się z procentem VAT. Akceptować?'@;
                     exec('obs_kom_evat','obiegi2',_ask,'PUT')
                  ?}
               |? _a='ANULUJ'
               ||  EVAT.web_eclose()
               ?}";
      EVAT.web_cntx_save(1);
      EDOKUM.web_cntx_save(1);
      EVAT.web_edit('REDW','_evat_red',,,_form)
   ?}
|| FUN.info('Nie znaleziono pozycji VAT.'@);
   web_top_refresh()
?}


\obs_kom_evat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Obsługa komunikatu z błędem w WebTerm
::   WE: _a - Treść komunikatu
::       _b - Typ operacji ADD/PUT
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 & type_of(_a)=type_of('') & type_of(_b)=type_of('')
||
   EVAT.web_cntx_save(1);
   EDOKUM.web_cntx_save();
   {? _b='ADD'
   ||
      _us:="
            EVAT.web_cntx_load(1);
            EDOKUM.use('skid_v'+(EVAT.name()+2)); EVAT.EDOKUM();
            {? _a=1 | _a=2 || web_top_update(0,,,'NETTO')
            |? _a=0 || EVAT.add();
                       exec('sum_vat','obiegi',1);
                       EVAT.web_eclose();
                       EVAT.web_refresh('WERFKW')
            ?}
            "
   |? _b='PUT'
   ||
      _us:="
      EVAT.web_cntx_load(1);
            EDOKUM.use('skid_v'+(EVAT.name()+2)); EVAT.EDOKUM();
            {? _a=1 | _a=2 || web_top_update(0,,,'NETTO')
            |? _a=0 || EVAT.put();
                       exec('sum_vat','obiegi',1);
                       EVAT.web_eclose();
                       EVAT.web_refresh('WERFKW')
            ?}
            "
   ?};
   web_choice(_us,_a,,'ASK',1,1,'Tak'@,'Nie'@)
?}


\obs_kom_pozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Obsługa komunikatu z błędem w WebTerm
::   WE: _a - Treść komunikatu
::       _b - Typ operacji ADD/PUT
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 & type_of(_a)=type_of('') & type_of(_b)=type_of('')
|| POZF.web_cntx_save(1);
   {? _b='ADD'
   || _us:="POZF.web_cntx_load(1);
            EDOKUM.use('skid_v'+(POZF.name()+2)); POZF.EDOKUM();
            {? _a=1 | _a=2 || web_top_update(0,,,'WN')
            |? _a=0 || POZF.add();
                       POZF.web_eclose();
                       POZF.web_refresh(web_top_win(1))
            ?}
            "
   |? _b='PUT'
   || _us:="POZF.web_cntx_load(1);
            EDOKUM.use('skid_v'+(POZF.name()+2)); POZF.EDOKUM();
            {? _a=1 | _a=2 || web_top_update(0,,,'WN')
            |? _a=0 || POZF.put();
                       POZF.web_eclose();
                       POZF.web_refresh(web_top_win(1))
            ?}
            "
   ?};
   web_choice(_us,_a,,'ASK',1,1,'Tak'@,'Nie'@)
?}


\ae_evat_net
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Po redakcji pole EVAT.NETTO i EVAT.VAT
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| {? web_top_fld_acr()='NETTO'
   || EVAT.NETTO:=EVAT.NETTO$2
   || EVAT.VAT=EVAT.VAT$2
   ?}
|| fld(fld()$2)
?};
1


\a_zam_evat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Po zamknięciu okna EVAT webterm
::----------------------------------------------------------------------------------------------------------------------
web_top_close();
{? web_top_ident(1)='AKC'
|| exec('env_wt','b_proces');
   exec('dob_refresh','obiegi','A','F')
?}


\wyb_slo_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Akcja wybierz procent VAT - SKID.PR
::----------------------------------------------------------------------------------------------------------------------
EVAT.web_cntx_load(1);
EDOKUM.use('skid_v'+(EVAT.name()+2)); EDOKUM.prefix(); EVAT.EDOKUM();
SKID.PR:=SLO.KOD;
EVAT.PR:=SLO.ref();
SLO.web_close();
web_top_update(1)


\chk_pozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Rekord po tabeli POZF
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? POZF.WN=0 & POZF.WV=0 & POZF.WB=0
|| _zwrot:='NETTO*VAT*BUTTO=0'
|? POZF.WN<0 | POZF.WB<0
|| _zwrot:='NETTO<0|BRUTTO<0'
|? (POZF.WN+POZF.WV)$2<>POZF.WB$2
|| _zwrot:='NETTO+VAT<>BRUTTO'
|? POZF.WV$2<>(0.01*POZF.WN*exec('vat','dok_fks',POZF.SV().KOD))$2
|| _zwrot:='KWOTA_VAT<>NETTO*VAT'
?};
_zwrot


\etp_atr_be_section
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Formuła nadająca nazwę sekcji dla rekordów w schemacie informacji dodatkowych dokumentu portalu ABS
::----------------------------------------------------------------------------------------------------------------------
ETYP_ATR.SECTION:=OBIEGI.SECTION;
ETYP_ATR.put()


\etp_atr_bg_section
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Przed grupą dla formuły nadającej nazwy sekcji dla rekordów w schemacie informacji dodatkowych
::       dokumentu portalu ABS
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.SECTION:='';
OBIEGI.win_edit('NAD_SEKC');
OBIEGI.edit()


\etp_atr_ag_section
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Po grupie dla formuły nadającej nazwy sekcji dla rekordów w schemacie informacji dodatkowych
::       dokumentu portalu ABS
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.SECTION:=''


\etypy_chk_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Sprawdzenie unikalności id dla typu wniosku w obiegu
::   WE: _a - id typu
::   WY: Typ wniosku: T lub P
::----------------------------------------------------------------------------------------------------------------------
_ref:={? -menu_txt()='popraw' || ETYPY.ref() || null ?};
ETYPY.cntx_psh();
ETYPY.index('ID_WP'); ETYPY.prefix();
_zwrot:={? ETYPY.find_key(_a,) & _ref<>ETYPY.ref || ETYPY.W_PORTAL || '' ?};
ETYPY.cntx_pop();
_zwrot


\etypy_chk_na
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Sprawdzenie unikalności nazwy dla typu wniosku w obiegu
::   WE: _a - typ obiegi
::       _b - wspólny portal
::       _c - nazwa
::   WY: 0/1 - nieunikalny id / unikalny id
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
_ref:={? -menu_txt()='popraw' || ETYPY.ref() || null ?};
{? 'TPs'*_b
|| ETYPY.cntx_psh();
   ETYPY.index('UNIK_WP'); ETYPY.prefix(_a,_b,_c,);
   _zwrot:=(ETYPY.first() & _ref<>ETYPY.ref());
   ETYPY.cntx_pop()
?};
_zwrot


\etp_atr_chk_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Sprawdzenie unikalności id dla typu wniosku w obiegu
::   WE: _a - id typu
::   WY: 0/1 - nieunikalny id / unikalny id
::----------------------------------------------------------------------------------------------------------------------
_ref:={? -menu_txt()='popraw' || ETYP_ATR.ref() || null ?};
ETYP_ATR.cntx_psh();
ETYP_ATR.index('ID_WP'); ETYP_ATR.prefix(ETYPY.ref());
_zwrot:=~(ETYP_ATR.find_key(_a,) & _ref<>ETYP_ATR.ref );
ETYP_ATR.cntx_pop();
_zwrot


\etp_atr_wzor_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Wzorzec ETYP_ATR.DEFAULT
::----------------------------------------------------------------------------------------------------------------------
{? ETYP_ATR.TAT().W_PORTAL='T'
|| {? TAT.TYP='L'
   || {? TAT.CZY_PREC='T' || 'F!' || 'I!' ?}
   |? TAT.TYP='B'
   || '1&'
   |? TAT.TYP='D'
   || '9999-99-99&'
   |? TAT.TYP='I'
   || '9999-99-99 99:99&'
   |? TAT.TYP='T'
   || 'x!'
   |? TAT.TYP='C'
   || '99:99&'
   |? TAT.TYP='W'
   || 'x!'
   || ''
   ?}
|| ''
?}


\etp_atr_ae_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Po redakcji ETYP_ATR.DEFAULT
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? ETYP_ATR.TAT().W_PORTAL='T' & ETYP_ATR.DEFAULT<>''
|| {? TAT.TYP='D' | TAT.TYP='I'
   || _txt:=ETYP_ATR.DEFAULT;
      {? _txt='    -  -'  | _txt='    -  -     -'
      || ETYP_ATR.DEFAULT:=''
      || _msg:=no_msg(1);
         on_error(3); errno();
         {? TAT.TYP='D'
         || date(#(4+_txt),#(5-_txt-3),#(_txt+2))
         || date(#(4+_txt),#(5-_txt-9),#(8-_txt-6));
            time(#(11-txt-3),#_txt+2)
         ?};
         on_error(0);
         no_msg(_msg);
         {? errno()
         || {? TAT.TYP='D'
            || FUN.info('Nieprawidłowa data.'@)
            || FUN.info('Nieprawidłowa data lub czas.'@)
            ?};
            _ok:=0
         ?}
      ?}
   ?}
?};
_ok


\ob_wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Wycofanie dokumentu do wybranego zakresu.
::   WE: _a - Zakres dokumentu (W - Rejestracja,A - Akceptacja ,P - Przekazanie, D - Deketacja)
::       _b - Typ dokumentu w obiegu (F - Faktura,W - Wniosek ,D - Delegacja)
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_b')>0
|| _log:={? _b='F'
         || 'Wycofanie faktury'
         |? _b='W'
         || 'Wycofanie wniosku'
         |? _b='D'
         || 'Wycofanie delegacji'
         || ''
         ?}
|| _log:=''
?};
{? app_info('web_sesid')<>''
|| {? EDOKUMW.get()
   || _dalej:=exec('env_edokum','obiegi',_a+_b,1);
      {? _dalej
      || EDOKUMW.web_cntx_save();
         web_params_set('zakres',_a,'typ',_b,'log',_log);
         _zam:="{? _a
                || _zwrot:=1;
                   _zakres:=web_params_get().zakres;
                   _typ:=web_params_get().typ;
                   _error:='';
                   EDOKUMW.web_cntx_load();
                   exec('env_edokum','obiegi',_zakres+_typ,1);
                   _mas:=EDOKUMW.name()+2;
                   EDOKUM.use('skid_v'+_mas); EDOKOS.use('skid_y'+_mas);
                   EDOKUM.index('TYP');
                   EDOKUM.prefix(); EDOKUMW.EDOKUM();
                   {? _zwrot
                   || _res:=exec('isActiveProcOrProblems','#b__box',EDOKUM.uidref());
                      _zwrot:=(_res.isActive & ~_res.isProblem);
                      {? ~_zwrot
                      || FUN.info('Bieżący rekord nie jest powiązany z żadnym uruchomionym procesem '
                                 'lub został zgłoszony błąd.\nDokument nie może zostać wycofany.'@)
                      ?}
                   ?};
                   {? _zwrot & exec('edokos_last_status','obiegi2')
                   || _zwrot:=0;
                      FUN.info('Ostatnia czynność została zakończona.\nDokument nie może zostać wycofany.'@)
                   ?};
                   {? _zwrot
                   || OPERATOR.USER:={? exec('getUser','users',app_info('web_user')) || USERS.ref() || null ?};
                      EDOKUM.WYCOFAJ:=_zakres;
                      _edokos_ref:=exec('search_last_act','obiegi2',_zakres);
                      EDOKUM.WYCOF_DO:=exec('edokos_action','obiegi2',_edokos_ref);
                      {? EDOKUM.WYCOF_DO<>'' & EDOKUM.put()
                      || exec('edokos_status_w','obiegi2',_edokos_ref);
                         EDOKOS.index('EDOKUM');
                         EDOKOS.prefix(EDOKUM.ref(),EDOKUM.B_PREL);
                         {? ~EDOKOS.last() || EDOKOS.blank() ?};
                         EDOKLOG.use('skid_d'+_mas); EDOKLOG.index('DISP');
                         exec('add_elog','obiegi_akc',web_params_get().log,1)
                      || FUN.info('Wskazano nieprawidłową czynność.\nDokument nie może zostać wycofany.'@);
                         _zwrot:=0
                      ?}
                   ?};
                   exec('dob_refresh','obiegi',_zakres,_typ)
                ?}";
         web_ask(_zam,'Czy na pewno wycofać dokument?'@,FUN.TYT)
      ?}
   || FUN.info('Nie znaleziono dokumentu.'@);
      exec('dob_refresh','obiegi',_a,_b)
   ?}
|| _zwrot:=1;
   _error:='';
   {? _zwrot
   || _res:=exec('isActiveProcOrProblems','#b__box',EDOKUM.uidref());
      _zwrot:=(_res.isActive & ~_res.isProblem);
     {? ~_zwrot
     || _error:='Bieżący rekord nie jest powiązany z żadnym uruchomionym procesem '
                'lub został zgłoszony błąd.\n'@
     ?}
   ?};
   {? _zwrot & exec('edokos_last_status','obiegi2')
   || _zwrot:=0;
      _error:='Ostatnia czynność została zakończona.\n'@
   ?};
   {? _zwrot & FUN.ask('Czy na pewno wycofać dokument?'@)
   || EDOKUM.WYCOFAJ:=_a;
      _edokos_ref:=exec('search_last_act','obiegi2',_a);
      EDOKUM.WYCOF_DO:=exec('edokos_action','obiegi2',_edokos_ref);
      {? EDOKUM.WYCOF_DO<>'' & EDOKUM.put()
      || exec('edokos_status_w','obiegi2',_edokos_ref);
         EDOKOS.cntx_psh();
         EDOKOS.index('EDOKUM');
         EDOKOS.prefix(EDOKUM.ref(),EDOKUM.B_PREL);
         {? ~EDOKOS.last() || EDOKOS.blank() ?};
         exec('add_elog','obiegi_akc',_log,1);
         EDOKOS.cntx_pop()
      || _error:='Wskazano nieprawidłową czynność.\n'@;
         _zwrot:=0
      ?}
   ?};
   {? ~_zwrot
   || {? typobi=1
      || FUN.info(_error+'Faktura nie może zostać wycofana.'@)
      |? typobi=2
      || FUN.info(_error+'Wniosek nie może zostać wycofany.'@)
      |? typobi=3
      || FUN.info(_error+'Delegacja nie może zostać wycofana.'@)
      ?}
   ?}
?}


\search_last_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Zwraca nazwę czynności do której proces zostanie wycofany.
::   WE: _a - operacja
::       _b - user
::   WY: EDOKOS.ref
::----------------------------------------------------------------------------------------------------------------------
_edokos:=null();
_prev:=0;
_user:={? _=2 || _b || OPERATOR.USER ?};
{? type_of(_a)=1
|| _zakres:={? _a=1 || 'W' |? _a=2 || 'A' |? _a=3 || 'D' |? _a=5 || 'P' || '' ?}
|| _zakres:=_a
?};
EDOKOS.cntx_psh();
EDOKOS.use('skid_y'+(EDOKUM.name()+2));
EDOKOS.index('SZUK');
{? _zakres<>''
|| EDOKOS.prefix(EDOKUM.ref,_user,_zakres)
|| EDOKOS.prefix(EDOKUM.ref,_user,)
?};
{? EDOKOS.last()
|| {? EDOKOS.STATUS<>'W' & EDOKOS.B_PRELS<>EDOKUM.B_PRELS
   || _edokos:=EDOKOS.ref()
   || {!
      |? _prev:=EDOKOS.prev();
         {? EDOKOS.STATUS<>'W' & EDOKOS.B_PRELS<>EDOKUM.B_PRELS
         || _edokos:=EDOKOS.ref()
         ?};
         _edokos=null() & _prev
      !}
   ?}
?};
EDOKOS.cntx_pop();
_edokos


\edokos_status_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Zmienia status dla etapów, które zostały wycofane
::   WE: _a - EDOKOS.ref
::----------------------------------------------------------------------------------------------------------------------
EDOKOS.cntx_psh();
EDOKOS.index('DATA');
EDOKOS.prefix(EDOKUM.ref,);
{? EDOKOS.seek(_a)
|| EDOKOS.STATUS:='N';
   {? EDOKOS.put()
   || {? EDOKOS.next()
      || {!
         |? EDOKOS.STATUS:='W';
            {? EDOKOS.WID='S' || EDOKOS.B_PREL:='A'+$EDOKOS.OPER_NUM ?};
            EDOKOS.put();
            EDOKOS.next()
         !}
      ?}
   ?}
?};
EDOKOS.cntx_pop()


\edokos_action
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Zwraca nazwę czynności do której proces zostanie wycofany.
::   WE: _a - EDOKOS.ref
::----------------------------------------------------------------------------------------------------------------------
{? _=0 | _a=null() || return('') ?};
_action:='';
EDOKOS.cntx_psh();
EDOKOS.use(ref_name(_a));
EDOKOS.index('SZUK');
EDOKOS.prefix();
{? EDOKOS.seek(_a) || _action:=EDOKOS.B_PRELS ?};
EDOKOS.cntx_pop();
_action


\edokos_last_status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Formuła sprawdzająca, czy dekretacja dokumentu została zakończona
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
EDOKOS.cntx_psh();
EDOKOS.index('EDOKUM');
EDOKOS.prefix(EDOKUM.ref(),EDOKUM.B_PREL);
{? EDOKOS.last() & EDOKOS.STATUS='T' || _result:=1 ?};
EDOKOS.cntx_pop();
_result


\etp_prc_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Dolaczenie nowego rekordu tabeli ETYPPROC
::----------------------------------------------------------------------------------------------------------------------
ETYPPROC.cntx_psh(); B_PROC.cntx_psh();
ETYPPROC.prefix();
ETYPPROC.win_edit('RED');
ETYPPROC.index('UNIK');
ETYPPROC.blank();
POMOC.B_PROC:=null();
ETYPPROC.ETYPY:=ETYPY.ref();
ETYPPROC.FIRMA:=REF.FIRMA;
B_PROC.f_clear();
B_PROC.prefix();
_wer:=B_PROC.mk_sel('Wybierz proces'@,,,'editproces',,,,,'U');
B_PROC.win_fld(_wer,,'SYMBOL');
B_PROC.win_fld(_wer,,'NAME');
B_PROC.win_fld(_wer,,'VER');
B_PROC.win_act(_wer,,'Formuła','&Wybierz'@@,,,"sel_exit()",,1,,,,'W');
B_PROC.win_sel(_wer);
B_PROC.f_set(,'JOIN B_PREL using (B_PREL.B_PROC,B_PROC.REFERENCE) JOIN B_ELE using (B_ELE.REFERENCE,B_PREL.B_ELE)',
   'B_ELE.SYMBOL=\':_b\' and B_PROC.MICRO=\'N\' and B_PROC.ACTIVE=\'T\' and B_PROC.FIRMA=:_a and B_PROC.ACCEPTED=\'T\'',
   REF.FIRMA,{? exec('EDOKUM_typ','portal_seod','W',,ETYPY.TYPOBIEG) || 'OBE_FAW_DARP' || 'OBE_FDL_DBRP' ?}
);
{? B_PROC.select()
|| ETYPPROC.prefix(ETYPPROC.FIRMA,ETYPPROC.ETYPY,B_PROC.SYMBOL,B_PROC.VER);
   {? ~ETYPPROC.first()
   || ETYPPROC.PROC_VER:=B_PROC.VER;
      ETYPPROC.PROC_SYM:=B_PROC.SYMBOL;
      ETYPPROC.add()
   || FUN.info('Wskazany proces jest już powiązany z tym typem wniosku')
   ?}
?};
B_PROC.cntx_pop(); ETYPPROC.cntx_pop()


\etp_prc_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Akcja popraw tabeli ETYPPROC
::----------------------------------------------------------------------------------------------------------------------
ETYPPROC.cntx_psh();
ETYPPROC.prefix();
ETYPPROC.win_edit('RED');
ETYPPROC.index('UNIK');
ETYPPROC.ETYPY:=ETYPY.ref();
ETYPPROC.FIRMA:=REF.FIRMA;
B_PROC.cntx_psh();
B_PROC.index('SYMVER');
B_PROC.prefix(REF.FIRMA,ETYPPROC.PROC_SYM);
{? B_PROC.first()
|| POMOC.B_PROC:=B_PROC.ref
?};
B_PROC.index('AKT');
B_PROC.prefix('N','T',REF.FIRMA);
B_PROC.f_clear();
B_PROC.prefix();
_wer:=B_PROC.mk_sel('Wybierz proces'@,,,'editproces',,,,,'U');
B_PROC.win_fld(_wer,,'SYMBOL');
B_PROC.win_fld(_wer,,'NAME');
B_PROC.win_fld(_wer,,'VER');
B_PROC.win_act(_wer,,'Formuła','&Wybierz'@@,,,"sel_exit()",,1,,,,'W');
B_PROC.win_sel(_wer);
B_PROC.f_set(,'JOIN B_PREL using (B_PREL.B_PROC,B_PROC.REFERENCE) JOIN B_ELE using (B_ELE.REFERENCE,B_PREL.B_ELE)',
   'B_ELE.SYMBOL=\':_b\' and B_PROC.MICRO=\'N\' and B_PROC.ACTIVE=\'T\' and B_PROC.FIRMA=:_a and B_PROC.ACCEPTED=\'T\'',
   REF.FIRMA,{? exec('EDOKUM_typ','portal_seod','W',,ETYPY.TYPOBIEG) || 'OBE_FAW_DARP' || 'OBE_FDL_DBRP' ?}
);
B_PROC.win_sel(_wer);
{? B_PROC.select()
|| ETYPPROC.prefix(ETYPPROC.FIRMA,ETYPPROC.ETYPY,B_PROC.SYMBOL,B_PROC.VER);
   {? ~ETYPPROC.first()
   || ETYPPROC.PROC_VER:=B_PROC.VER;
      ETYPPROC.PROC_SYM:=B_PROC.SYMBOL;
      ETYPPROC.put()
   || FUN.info('Wskazany proces jest już powiązany z tym typem wniosku')
   ?}
?};
ETYPPROC.cntx_pop(); B_PROC.cntx_pop()


\ae_pomoc_b_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: AE dla zmiennej POMOC pola B_PROC
::----------------------------------------------------------------------------------------------------------------------
ETYPPROC.PROC_VER:=B_PROC.VER;
ETYPPROC.PROC_SYM:=B_PROC.SYMBOL


\disp_wni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.42]
:: OPIS: Podgląd dokumentu obiegu z listy zadań
::   WE: _a - 2 - wnioski
::            3 - delegacje
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || return(0) ?};
_mp:=params_get().mp;
_wy:=_mp.load(exec('kind_out','#b_port'));
_rfd:=null();
{? _wy.EDOKUM <> ~~
|| _rfd:=_wy.EDOKUM
|| _in:=_mp.load(exec('kind_in','#b_port'));
   _rfd:=_in.EDOKUM
?};
{? var_pres('_rfd')>0 & _rfd
|| EDOKUM.use(8+$_rfd);
   EDOKUM.prefix();
   _ok:=EDOKUM.seek(_rfd);
   {? _ok
   || {? app_info('web_sesid')<>''
      || {? _a=2
         || EDOKUM.web_cntx_save();
            _red:=exec('win_red_wnioski','obiegi2','D',,0);
            web_def_fld_opt(_red, 'editable=0',);
            web_def_disp(_red,"EDOKUM.web_cntx_load(); exec('inne_akcje','obiegi2',_a)",'Wniosek w obiegu'@)
         ||
            EDOKUM.web_cntx_save();
            SKID.SL_KH:=SKID.NIP:='';
            EDOKUM.web_display({? _a=3 || 'WD01' || 'WW01' ?},,
               "EDOKUM.web_cntx_load(); EDOKUM.web_eclose()")
         ?}
      || {? _a=2
         || exec('akc_display','obe_faw')
         |? _a=3
         || EDOKUM.win_edit('KSD');
            EDOKUM.display()
         ?}
      ?}
   || FUN.info('Nie znaleziono dokumentu obiegu.'@)
   ?}
|| FUN.info('Nie znaleziono dokumentu obiegu.'@)
?}


\build_etyp_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła czyszcząca tabelę ETYPY_P oraz budująca ją od nowa
::   WE: _a [INTEGER] - 1/0 - tryb bez komunikatów / tryb z komunikatami
::----------------------------------------------------------------------------------------------------------------------
_mode:={? var_pres('_a')=type_of(0) || _a || 0 ?};
{? ~_mode
|| {? ~FUN.ask('Czy na pewno chcesz zbudować uprawnienia do wniosków od nowa?'@) || return() ?}
?};
ETYPY_P.cntx_psh();
ETYPY_P.index('UNIK');
ETYPY_P.prefix();
{? ETYPY_P.first()
|| {! |? ETYPY_P.del()
   !}
?};
ETYPY_P.cntx_pop();
_cnt:=0;
ETYPY_P.index('UNIK');
ETYPY_P.prefix();
exec('czytaj','#stalesys',,XINFO,'POR_CONF');
ETYPPROC.cntx_psh();
{? XINFO.POR_CONF='J'
|| ETYPPROC.index('UNIK');
   ETYPPROC.prefix(REF.FIRMA)
|| ETYPPROC.index('DISP');
   ETYPPROC.prefix()
?};
{? ETYPPROC.first()
|| {!
   |? ETYPY.cntx_psh();
      _act:={? exec('EDOKUM_typ','portal_seod','W',,ETYPPROC.ETYPY().TYPOBIEG) || 'OBE_FAW_DARP' || 'OBE_FDL_DBRP' ?};
      ETYPY.cntx_pop();
      _usrs:=exec('get_proc_users','#b__box',ETYPPROC.PROC_SYM,ETYPPROC.PROC_VER,_act);
      OSOBA.cntx_psh(); P.cntx_psh();
      OSOBA.prefix();
      USERS.cntx_psh(); USERS.prefix();
      {? _usrs.first()
      || {!
         |? {? OSOBA.seek(_usrs.OSOBA)
            || P.index('OSOZATR');
               P.prefix(REF.FIRMA,OSOBA.ref());
               {? P.first()
               || {!
                  |? _cnt+=1;
                     exec('add_etypy_p','obiegi2',P.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA);
                     P.next()
                  !}
               ?}
            |? USERS.seek(_usrs.REF)
            || _cnt+=1;
               exec('add_etypy_p','obiegi2',USERS.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA)
            ?};
            _usrs.next()
         !}
      ?};
      &_usrs; OSOBA.cntx_pop(); P.cntx_pop();
      USERS.cntx_pop();
      ETYPPROC.next()
   !}
?};
ETYPPROC.cntx_pop();
_cnt


\show_etyp_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła pomocnicza do wyświetlenia uprawnień wygenerowanych w systemie
::----------------------------------------------------------------------------------------------------------------------
TMP_ETYPY:=sql('select ETYPY_P.CNT, ETYPY.NAZWA, USERS.WEBLOGIN from ETYPY_P '+
'join ETYPY join P join OSOBA using(P.OSOBA, OSOBA.REFERENCE)  join USERS using (USERS.OSOBA, OSOBA.REFERENCE)');
_tab:=TMP_ETYPY.mk_sel(,,1);
TMP_ETYPY.win_sel(_tab);
TMP_ETYPY.select();
&TMP_ETYPY


\dnull_etyp_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła usuwająca uprawnienia, których licznik doszedł do 0
::----------------------------------------------------------------------------------------------------------------------
ETYPY_P.cntx_psh();
ETYPY_P.index('CNT');
ETYPY_P.prefix(0);
{? ETYPY_P.first()
|| {!
   |? ETYPY_P.del()
   !}
?};
ETYPY_P.cntx_pop()


\aa_etypproc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Trigger after add dla tabeli etypproc
::   WE: _a - wynik operacji add
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1 || return() ?};
ETYPY.cntx_psh();
_act:={? exec('EDOKUM_typ','portal_seod','W',,ETYPPROC.ETYPY().TYPOBIEG) || 'OBE_FAW_DARP' || 'OBE_FDL_DBRP' ?};
ETYPY.cntx_pop();
_usrs:=exec('get_proc_users','#b__box',ETYPPROC.PROC_SYM,ETYPPROC.PROC_VER,_act);
OSOBA.cntx_psh(); P.cntx_psh(); USERS.cntx_psh();
OSOBA.prefix();
USERS.prefix();
{? _usrs.first()
|| {!
   |? {? OSOBA.seek(_usrs.OSOBA)
      || P.index('OSOZATR');
         P.prefix(REF.FIRMA,OSOBA.ref());
         {? P.first()
         || {!
            |? exec('add_etypy_p','obiegi2',P.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA,_usrs.CNT);
               P.next()
            !}
         ?}
      |? USERS.seek(_usrs.REF)
      || exec('add_etypy_p','obiegi2',USERS.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA,_usrs.CNT)
      ?};
      _usrs.next()
   !}
?};
&_usrs; OSOBA.cntx_pop(); P.cntx_pop; USERS.cntx_pop();
ETYPY.cntx_psh();
ETYPY.prefix();
{? ETYPY.seek(ETYPPROC.ETYPY)
|| ETYPY.put(,1)
?};
ETYPY.cntx_pop();
~~


\ap_etypproc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Trigger after put dla tabeli etypproc
::   WE: _a - wynik operacji put
::---------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1 || return() ?};
_etyp:=bfld('ETYPY');
_sym:=bfld('PROC_SYM');
_ver:=bfld('PROC_VER');
_firma:=bfld('FIRMA');
::usuwanie poprzednich uprawnień
_usrs:=exec('get_proc_users','#b__box',_sym,_ver,'OBE_FAW_DARP');
OSOBA.cntx_psh(); P.cntx_psh(); USERS.cntx_psh();
OSOBA.prefix();
USERS.prefix();
{? _usrs.first()
|| {!
   |? {? OSOBA.seek(_usrs.OSOBA)
      || P.index('OSOZATR');
         P.prefix(REF.FIRMA,OSOBA.ref());
         {? P.first()
         || {!
            |? exec('del_etypy_p','obiegi2',P.ref(),_etyp,_firma);
               P.next()
            !}
         ?}
      |? USERS.seek(_usrs.REF)
      || exec('del_etypy_p','obiegi2',USERS.ref(),_etyp,_firma)
      ?};
      _usrs.next()
   !}
?};
&_usrs; OSOBA.cntx_pop(); P.cntx_pop; USERS.cntx_pop();
exec('dnull_etyp_p','obiegi2');
:: dodawanie nowych uprawnień
ETYPY.cntx_psh();
_act:={? exec('EDOKUM_typ','portal_seod','W',,ETYPPROC.ETYPY().TYPOBIEG) || 'OBE_FAW_DARP' || 'OBE_FDL_DBRP' ?};
ETYPY.cntx_pop();
_usrs:=exec('get_proc_users','#b__box',ETYPPROC.PROC_SYM,ETYPPROC.PROC_VER,_act);
OSOBA.cntx_psh(); P.cntx_psh(); USERS.cntx_psh();
OSOBA.prefix();
USERS.prefix();
{? _usrs.first()
|| {!
   |? {? OSOBA.seek(_usrs.OSOBA)
      || P.index('OSOZATR');
         P.prefix(REF.FIRMA,OSOBA.ref());
         {? P.first()
         || {!
            |? exec('add_etypy_p','obiegi2',P.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA,_usrs.CNT);
               P.next()
            !}
         ?}
      |? USERS.seek(_usrs.REF)
      || exec('add_etypy_p','obiegi2',USERS.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA,_usrs.CNT)
      ?};
      _usrs.next()
   !}
?};
&_usrs; OSOBA.cntx_pop(); P.cntx_pop; USERS.cntx_pop();
ETYPY.cntx_psh();
ETYPY.prefix();
{? ETYPY.seek(ETYPPROC.ETYPY)
|| ETYPY.put(,1)
?};
ETYPY.cntx_pop();
~~


\ad_etypproc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Trigger after del dla tabeli etypproc
::   WE: _a - wynik operacji del
::---------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1 || return() ?};
_etyp:=bfld('ETYPY');

::usuwanie uprawnień w tabeli ETYPY_P
ETYPY_P.cntx_psh();
ETYPY_P.index('ETYPY');
ETYPY_P.prefix(_etyp);
{? ETYPY_P.first()
|| {!
   |? ETYPY_P.del()
   !}
?};
ETYPY_P.cntx_pop();
ETYPY.cntx_psh();
ETYPY.prefix();
{? ETYPY.seek(_etyp)
|| ETYPY.put(,1)
?};
ETYPY.cntx_pop();
~~


\add_etypy_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Dodaje rekord ETYPY_P albo zwiększa licznik
::   WE:  _a  - P.ref() lub USERS.ref()
::        _b  - ETYPY.ref()
::        _c  - FIRMA.ref()
::       [_d] - licznik, domyślnie 1
::   WY: wynik add()
::---------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO,'POR_CONF');
_prac:=_a;
_etypy:=_b;
_firma:=_c;
_cnt:={? var_pres('_d')=type_of(1) || _d || 1 ?};
_add:=1;
_ret:=0;
_dalej:=1;
ETYPY_P.cntx_psh();
ETYPY_P.prefix();
ETYPY_P.blank();
{? ref_tab(_prac)=P
|| ETYPY_P.P:=_prac
|? ref_tab(_prac)=USERS
|| ETYPY_P.USERS:=_prac
?};
ETYPY_P.ETYPY:=_etypy;
{? XINFO.POR_CONF='J'
|| ETYPY_P.FIRMA:=_firma;
   _dalej:=_firma=REF.FIRMA
?};
{? _dalej
|| ETYPY_P.cntx_psh();
   ETYPY_P.index('UNIK');
   ETYPY_P.prefix(ETYPY_P.FIRMA,ETYPY_P.ETYPY,ETYPY_P.P,ETYPY_P.USERS);
   {? ETYPY_P.first()
   || _add:=0;
      ETYPY_P.CNT+=_cnt;
      ETYPY_P.put()
   ?};
   ETYPY_P.cntx_pop();

   {? _add=1
   || ETYPY_P.CNT:=_cnt;
      _ret:=ETYPY_P.add()
   ?}
?};
ETYPY_P.cntx_pop();
_ret


\del_etypy_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Usuwa licznik z rekordów etypy_p
::   WE:  _a  - P.ref() lub USERS.ref()
::        _b  - ETYPY.ref()
::        _c  - FIRMA.ref()
::       [_d] - licznik, domyślnie 1
::   WY: wynik add()
::---------------------------------------------------------------------------------------------------------------------
_prac:={? ref_tab(_a)=P || _a || null ?};
_user:={? ref_tab(_a)=USER || _a || null ?};
_etypy:=_b;
_firma:=_c;
_cnt:={? var_pres('_d')=type_of(1) || _d || 1 ?};
_add:=1;
_ret:=0;
ETYPY_P.cntx_psh();
{? XINFO.POR_CONF<>'J'
|| _firma:=null()
?};
ETYPY_P.index('UNIK');
ETYPY_P.prefix(_firma,_etypy,_prac,_user);
{? ETYPY_P.first() || ETYPY_P.CNT-=_cnt; ETYPY_P.put()  ?};
ETYPY_P.cntx_pop();
_ret


\add_etypyp_users
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: synchronizacja tabeli etypy_p po zmianie USERS.OSOBA.
::   WE: _a - bfld(USERS.OSOBA)
::       kontekst nowego rekordu USERS
::----------------------------------------------------------------------------------------------------------------------
_osoba:=_a;

ETYPPROC.cntx_psh();
ETYPPROC.index('UNIK');
ETYPPROC.prefix();
{? ETYPPROC.first()
|| {!
   |? ETYPY.cntx_psh();
      _act:={? exec('EDOKUM_typ','portal_seod','W',,ETYPPROC.ETYPY().TYPOBIEG) || 'OBE_FAW_DARP' || 'OBE_FDL_DBRP' ?};
      ETYPY.cntx_pop();
      _usrs:=exec('get_proc_users','#b__box',ETYPPROC.PROC_SYM,ETYPPROC.PROC_VER,_act);
      OSOBA.cntx_psh(); P.cntx_psh();
      {? _usrs.first()
      || {!
         |?
:: usuwanie starych powiązan
            {? $_usrs.OSOBA=$_osoba
            || P.index('OSOZATR');
               P.prefix(REF.FIRMA,OSOBA.ref());
               {? P.first()
               || {!
                  |? exec('del_etypy_p','obiegi2',P.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA);
                     P.next()
                  !}
               ?}
            ?};
::dodawanie nowych powiązań
            {? USERS.OSOBA & $_usrs.OSOBA=$USERS.OSOBA
            || P.index('OSOZATR');
               P.prefix(REF.FIRMA,OSOBA.ref());
               {? P.first()
               || {!
                  |? exec('add_etypy_p','obiegi2',P.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA);
                     P.next()
                  !}
               ?}
            |? $_usrs.REF=$USERS.ref()
            || exec('add_etypy_p','obiegi2',USERS.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA)
            ?};
            _usrs.next()
         !}
      ?};
      &_usrs; OSOBA.cntx_pop(); P.cntx_pop;
      ETYPPROC.next()
   !}
?};
exec('dnull_etyp_p','obiegi2');
ETYPPROC.cntx_pop()


\add_etypyp_usrrol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: synchronizacja tabeli etypy_p po add B_USRROL.
::   WE: kontekst nowego rekordu B_USRROL
::----------------------------------------------------------------------------------------------------------------------
ETYPPROC.cntx_psh();
ETYPPROC.index('UNIK');
ETYPPROC.prefix();
{? ETYPPROC.first()
|| {! |?
      ETYPY.cntx_psh();
      _act:={? exec('EDOKUM_typ','portal_seod','W',,ETYPPROC.ETYPY().TYPOBIEG) || 'OBE_FAW_DARP' || 'OBE_FDL_DBRP' ?};
      ETYPY.cntx_pop();
      _usrs:=exec('get_proc_users','#b__box',ETYPPROC.PROC_SYM,ETYPPROC.PROC_VER,_act,B_USRROL.B_ROLE);
      OSOBA.cntx_psh(); P.cntx_psh(); USERS.cntx_psh();
      {? _usrs.first()
      || {!
         |?
:: dodawanie nowych powiązań
            {? _usrs.REF=$B_USRROL.USERS
            || P.index('OSOZATR');
               P.prefix(REF.FIRMA,B_USRROL.USERS().OSOBA);
               {? P.first()
               || {!
                  |? exec('add_etypy_p','obiegi2',P.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA);
                     P.next()
                  !}
               || exec('add_etypy_p','obiegi2',B_USRROL.USERS,ETYPPROC.ETYPY,ETYPPROC.FIRMA)
               ?}
            ?};
            _usrs.next()
         !}
      ?};
      &_usrs; OSOBA.cntx_pop(); P.cntx_pop; USERS.cntx_pop();
      ETYPPROC.next()
   !}
?};
ETYPPROC.cntx_pop()


\del_etypyp_usrrol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: synchronizacja tabeli etypy_p po del B_USRROL.
::   WE: _a - bfld(USERS)
::       _b - B_USRROL.B_ROLE
::----------------------------------------------------------------------------------------------------------------------
_user:=_a;
_b_role:=_b;
ETYPPROC.cntx_psh();
ETYPPROC.index('UNIK');
ETYPPROC.prefix();
{? ETYPPROC.first()
|| {! |?
      _usrs:=exec('get_proc_users','#b__box',ETYPPROC.PROC_SYM,ETYPPROC.PROC_VER,'OBE_FAW_DARP',_b_role);
      OSOBA.cntx_psh(); P.cntx_psh(); USERS.cntx_psh();
      {? _usrs.first()
      || {! |?
:: usuwanie starych powiązań
            {? _usrs.REF=$_user
            || P.index('OSOZATR');
               P.prefix(REF.FIRMA,B_USRROL.USERS().OSOBA);
               {? P.first()
               || {! |?
                  exec('del_etypy_p','obiegi2',P.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA);
                  P.next()
                  !}
               ?}
            ?};
            _usrs.next()
         !}
      ?};
      &_usrs; OSOBA.cntx_pop(); P.cntx_pop; USERS.cntx_pop();
      ETYPPROC.next()
   !}
?};
exec('dnull_etyp_p','obiegi2');
ETYPPROC.cntx_pop()


\put_etypyp_usrrol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: synchronizacja tabeli etypy_p po put B_USRROL.
::   WE: _a - bfld(USERS)
::       kontekst nowego rekordu B_USRROL
::----------------------------------------------------------------------------------------------------------------------
_user:=_a;
ETYPPROC.cntx_psh();
ETYPPROC.index('UNIK');
ETYPPROC.prefix();
USERS.cntx_psh();
{? ETYPPROC.first()
|| {!
   |? ETYPY.cntx_psh();
      _act:={? exec('EDOKUM_typ','portal_seod','W',,ETYPPROC.ETYPY().TYPOBIEG) || 'OBE_FAW_DARP' || 'OBE_FDL_DBRP' ?};
      ETYPY.cntx_pop();
      _usrs:=exec('get_proc_users','#b__box',ETYPPROC.PROC_SYM,ETYPPROC.PROC_VER,_act);
      OSOBA.cntx_psh(); P.cntx_psh();
      {? _usrs.first()
      || {!
         |?
:: usuwanie starych powiązań
            {? _usrs.REF=$_user
            || P.index('OSOZATR');
               P.prefix(REF.FIRMA,OSOBA.ref());
               {? P.first()
               || {!
                  |? exec('del_etypy_p','obiegi2',P.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA);
                     P.next()
                  !}
               |? USERS.seek(_user)
               || exec('del_etypy_p','obiegi2',USERS.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA)
               ?}
            ?};
:: dodawanie nowych powiązań
            {? _usrs.REF=$B_USRROL.USERS
            || P.index('OSOZATR');
               P.prefix(REF.FIRMA,OSOBA.ref());
               {? P.first()
               || {!
                  |? exec('add_etypy_p','obiegi2',P.ref(),ETYPPROC.ETYPY,ETYPPROC.FIRMA);
                     P.next()
                  !}
               || exec('add_etypy_p','obiegi2',B_USRROL.USERS,ETYPPROC.ETYPY,ETYPPROC.FIRMA)
               ?}
            ?};
            _usrs.next()
         !}
      ?};
      &_usrs; OSOBA.cntx_pop(); P.cntx_pop;
      ETYPPROC.next()
   !}
?};
USERS.cntx_pop();
exec('dnull_etyp_p','obiegi2');
ETYPPROC.cntx_pop()


\wp_email
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Zraca adres email użytkownika wspólnego portalu
::----------------------------------------------------------------------------------------------------------------------
_sysobj:=exec(,'__sysusr',1);
_sysusr:=_sysobj.getUser(USERS.GUID);
{? var_pres('_sysusr')>100
|| {? _sysusr.Auth.Web.Login*'@'
   || _sysusr.Auth.Web.Login
   || USERS.EMAIL
   ?}
|| ''
?}


\sch_form_fml_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Formuła eksportująca wartość parametru typu _SCH_FORM
::   WE: _a [REFERENCE] - Wartość parametru. Wskazanie pozycji słownika [SCH_FORM.ref()]
::   WY: STRING - Treść formuły do wykonania podczas importu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null()) & _a<>null() & ref_tab(_a)=SCH_FORM
|| _sch_form:=_a
|| return('')
?};

_fml:='';

SCH_FORM.cntx_psh();
SCH_FORM.prefix();
{? SCH_FORM.seek(_sch_form)
|| _fml:=exec('formulizer_common','#b_export');
   _fml+='
      SCH_FORM.cntx_psh();
      SCH_FORM.index(\'UNIK\');
      SCH_FORM.prefix(B_PROC.FIRMA,\''+SCH_FORM.TYP+'\',\''+SCH_FORM.SKROT+'\',);
      {? SCH_FORM.first()
      || _result.REF:=SCH_FORM.ref();
         _result.RESULT:=1
      || _result.RESULT:=0;
         _result.REF:=null();
         {? _komm>0
         || _msg:=\'Nie znaleziono formuły o skrócie: %1\'@[\''+SCH_FORM.SKROT+'\'];
            exec(\'import_komm\',\'#b_export\',_msg)
         ?}
      ?};
      SCH_FORM.cntx_pop();
      _result
   ';
   _fml:=exec('formulizer_clean','#b_export',_fml)
?};
SCH_FORM.cntx_pop();
_fml


\b_role_fml_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Formuła eksportująca wartość parametru typu _B_ROLE
::   WE: _a [REFERENCE] - Wartość parametru. Wskazanie pozycji słownika [B_ROLE.ref()]
::   WY: STRING - Treść formuły do wykonania podczas importu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null()) & _a<>null() & ref_tab(_a)=B_ROLE
|| _b_role:=_a
|| return('')
?};

_fml:='';

B_ROLE.cntx_psh();
B_ROLE.prefix();
{? B_ROLE.seek(_b_role)
|| _fml:=exec('formulizer_common','#b_export');
   _fml+='
      B_ROLE.cntx_psh();
      B_ROLE.index(\'UNIK\');
      B_ROLE.prefix(B_PROC.FIRMA,\''+B_ROLE.NAME+'\',);
      {? B_ROLE.first()
      || _result.REF:=B_ROLE.ref();
         _result.RESULT:=1
      || _result.RESULT:=0;
         _result.REF:=null();
         {? _komm>0
         || _msg:=\'Nie znaleziono roli: %1\'@[\''+B_ROLE.NAME+'\'];
            exec(\'import_komm\',\'#b_export\',_msg)
         ?}
      ?};
      B_ROLE.cntx_pop();
      _result
   ';
   _fml:=exec('formulizer_clean','#b_export',_fml)
?};
B_ROLE.cntx_pop();
_fml


\etypy_fml_exp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Formuła eksportująca wartość parametru typu _ETYPY
::   WE: _a [INT] 1 - faktury
::          [INT] 2 - wnioski
::          [INT] 3 - delegacje
::       _b [STRING] - Bieżąca wartość parametru. Wskazanie pozycji słownika [ETYPY.NAZWA]
::   WY: [STRING] - Treść formuły do wykonania podczas importu
::----------------------------------------------------------------------------------------------------------------------
_etypy:=_b;
typobi:=_a;

_fml:='';

ETYPY.cntx_psh();
ETYPY.prefix();
{? ETYPY.find_key(_etypy)
|| _fml:=exec('formulizer_common','#b_export');
   _fml+='
      ETYPY.cntx_psh();
      ETYPY.index(\'UNIK\');
      ETYPY.prefix(exec(\'bl_typ\',\'obiegi\',typobi),\''+ETYPY.NAZWA+'\',);
      {? ETYPY.first()
      || _result.REF:=ETYPY.ref();
         _result.RESULT:=1
      || _result.RESULT:=0;
         _result.REF:=null();
         {? _komm>0
         || _msg:=\'Nie znaleziono typu: %1\'@[\''+ETYPY.NAZWA+'\'];
            exec(\'import_komm\',\'#b_export\',_msg)
         ?}
      ?};
      ETYPY.cntx_pop();
      _result
   ';
   _fml:=exec('formulizer_clean','#b_export',_fml)
?};
ETYPY.cntx_pop();
_fml


\korekty_jpk_v7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła dla Dokumentów księgowych, w menu Ewidencje obszaru akcja Korekty JPK_V7
::----------------------------------------------------------------------------------------------------------------------
DOK.cntx_psh();
_grp:=DOK.grp_make('Korekty JPK_V7'@,"",);
DOK_JPK.cntx_psh();
DOK_JPK.use('dokj'+(DOK.name()+4));
DOK_JPK.index('TIME');
_wer:=DOK.mk_sel('Dokumenty księgowe z korektami'@,,,'editproces',,,,,'U');
DOK.prefix();
DOK.f_clear();
DOK.f_set(,'JOIN DOK_JPK using (DOK_JPK.DOK,DOK.REFERENCE)',
'',);
{? DOK.f_first()
|| {!
   |? DOK_JPK.prefix(DOK.ref());
      _size:=DOK_JPK.size();
      {? DOK_JPK.first()
      || DOK_JPK.prefix(DOK.ref(),DOK_JPK.DATE, DOK_JPK.TIME)
      || _size:=-1
      ?};
      {? _size<>DOK_JPK.size()
      || DOK.f_next()
      || DOK.f_del()
      ?}
   !}
?};
DOK.win_fld(_wer,REJ,'KOD',,,8,,0,'Rejestr');
DOK.win_fld(_wer,,'NR',,,7,,,'Nr w rejestrze');
DOK.win_fld(_wer,,'NK',,,20);
DOK.win_fld(_wer,,'DTW',,,10);
DOK.win_fld(_wer,,'DTO',,,10,,,'Wystawiono');
DOK.win_fld(_wer,,'TR',,,18,,,'Opis');
DOK.win_fld(_wer,,'A',,,7,,,'Rejestracja',,,2,,"'T'","'N'",,);
DOK.win_fld(_wer,,'ZP',,,13,,,'Próbne księgowanie',,,2,,"'T'","'N'",,);
DOK.win_fld(_wer,,'ZK',,,15,,,'Końcowe księgowanie',,,2,,"'T'","'N'",,);
DOK.win_act(_wer,,'Formuła','Drukuj',,,"exec('rep_exec','#b_report','','fks_dkor*',,,,,,,,'W')");
DOK.win_act(_wer,,'Rekord',,,,"DOK.REJ()");
DOK.grp_sel(_grp,,_wer,,"
   DOK_JPK.prefix();
{? ~DOK.sel_size()
   || {? DOK.f_size()<>0
      || DOK_JPK.f_set('DATE, TIME',,'DOK_JPK.DOK=:_a',DOK.ref());
         {? DOK_JPK.f_first()
         || _date:=DOK_JPK.DATE;
            _time:=DOK_JPK.TIME;
            _ref:=$DOK_JPK.ref();
            {? DOK_JPK.f_next()
            || {!
               |? {? _date=DOK_JPK.DATE & _time=DOK_JPK.TIME & _ref<>$DOK_JPK.ref()
                  || DOK_JPK.f_del()
                  || _date:=DOK_JPK.DATE;
                     _time:=DOK_JPK.TIME;
                     _ref:=$DOK_JPK.ref();
                     DOK_JPK.f_next()
                  ?}
               !}
            ?}
         ?}
      || DOK_JPK.f_set('DATE, TIME',,'1=0',)
      ?}
   || DOK_JPK.f_set('DATE, TIME',,'DOK_JPK.DOK=:_a',DOK.ref());
      {? DOK_JPK.f_first()
      || _date:=DOK_JPK.DATE;
         _time:=DOK_JPK.TIME;
         _ref:=$DOK_JPK.ref();
         {? DOK_JPK.f_next()
         || {!
            |? {? _date=DOK_JPK.DATE & _time=DOK_JPK.TIME & _ref<>$DOK_JPK.ref()
               || DOK_JPK.f_del()
               || _ref:=$DOK_JPK.ref();
                  _date:=DOK_JPK.DATE;
                  _time:=DOK_JPK.TIME;
                  DOK_JPK.f_next()
               ?}
            !}
         ?}
      ?}
   ?};
      grp_disp(DOK_JPK,'KOR')
           ",0,0,,"","",,,'maximized_with_title');
DOK.grp_splt(_grp,,'horizontal','bottom',25);
DOK.grp_sel(_grp,DOK_JPK,'KOR','',,0,0,,"","",,,'maximized_with_title');
DOK.win_sel(_grp);
DOK.select();
DOK.f_clear();
DOK.cntx_pop(); DOK_JPK.cntx_pop()


\add_dok_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła pomocnicza dla trigera put i add dodająca rekord dla gtu
::----------------------------------------------------------------------------------------------------------------------
{? 0
|| DOK_JPK.cntx_psh();
   _maska:=DOK.name();
   DOK_JPK.use('dokj'+(_maska+4));
   DOK_JPK.index('UNIK');
   DOK_JPK.prefix(DOK.ref());
   {? ~DOK_JPK.first()
   || DOK_JPK.blank();
      DOK_JPK.JPK_GTU:='';
      DOK_JPK.JPK_PROC:='';
      DOK_JPK.DATE:=date();
      DOK_JPK.TIME:=time();
      DOK_JPK.DOK:=DOK.ref();
      DOK_JPK.OPERATOR:=exec('usr_zar','dok_fks');
      DOK_JPK.add()
   ?};
   DOK_JPK.cntx_pop()
?}


\trig_dok_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła na triger przed add tabeli DOK_JPK
::----------------------------------------------------------------------------------------------------------------------
_gtu:=DOK_JPK.JPK_GTU;
_proc:=DOK_JPK.JPK_PROC;
_ret:=1;
DOK_JPK.cntx_psh();
DOK_JPK.index('TIME');
DOK_JPK.prefix(DOK_JPK.DOK);
{? DOK_JPK.last()
|| {? DOK_JPK.VAT_DEK='' || DOK_JPK.prev() ?};
   {? DOK_JPK.JPK_GTU=_gtu & DOK_JPK.JPK_PROC=_proc || _ret:=0 ?}
?};
DOK_JPK.cntx_pop();
_ret


\dol_edokos
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Dolaczanie rekordow w tabeli EDOKOS - wprowadzanie
::   WE: _a - user
::----------------------------------------------------------------------------------------------------------------------
{? OBIEGI.B_PREL<>''
|| EDOKOS.cntx_psh(); EDOKOS.index('SZUK'); EDOKOS.prefix(EDOKUM.ref(),_a,'W');
   {? ~EDOKOS.first()
   || EDOKOS.blank(1); EDOKOS.STATUS:='N'; _add:=1
   || {? var_pres('wym_stat')>0 || EDOKOS.STATUS:={? wym_stat=2 || 'O' || 'N' ?} ?}; _add:=0
   ?};
   EDOKOS.EDOKUM:=EDOKUM.ref();
   {? PAR_SKID.get(457)='T' || EDOKOS.UW_KR:=exec('szuk_uwagi','obiegi',EDOKUM.ref()) ?};
   EDOKOS.OPERACJA:='W';
   EDOKOS.OPER_NUM:=1;
   EDOKOS.USERS:=_a;
   EDOKOS.B_PREL:=OBIEGI.B_PREL;
   EDOKOS.B_PRELS:=OBIEGI.B_PRELS;
   EDOKOS.WID:='T';
   {? _add
   || EDOKOS.LP:=exec('lp_edokos','obiegi'); EDOKOS.add()
   || EDOKOS.put()
   ?};
   OBIEGI.EDOKOS:=EDOKOS.ref();
   EDOKOS.cntx_pop()
?}


\typ_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła na typ dokumentu dla kontekstu DOK
::   Wy: [STRING] - typ dokumentu JPK
::----------------------------------------------------------------------------------------------------------------------
{? DOK.DOK_REJ<>null()
|| _ret:=DOK.DOK_REJ().JPK_V_T
|| _ret:=''
?};
_ret


\b_rodz_wp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Formuła na wartość początkową ETYPY.RODZ_WP
::----------------------------------------------------------------------------------------------------------------------
POMOC.RODZ_WP


\dol_edok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Ręczne dołączanie dokumentów z DOKUM
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
_we:=_args.in;
_wy:=_args.out;
exec('typ_ust','obiegi',_we);
VAR_DEL.delete('numer','oldnumer');
exec('tabkhini','kontrahent');
SKID.SL_KH:='';
OPERATOR.USER();
::SKID.fld_fml('NIP','F3',"exec('aenipdob','obiegi',1)");
EDOKUM.blank();
EDOKUM.TYP:=typ_ust;
{? EDOKUM.ODD=null || EDOKUM.ODD:=exec('one_odd','obiegi') ?};
{? EDOKUM.ODD=null || EDOKUM.ODD:=exec('edokum_odd','obiegi2') ?};
EDOKUM.DOSTAWCA:=OPERATOR.USER().OSOBA;
EDOKUM.FIRMA:=REF.FIRMA;
EDOKUM.ROK_F:=SSTALE.AR;
EDOKUM.UNIK_ID:=tm_stamp();
EDOKUM.DOKUM:=$DOKUM.ref();
KH.cntx_psh();
DOKUM.KH();
EDOKUM.KH_FULL:=KH.NAZ_P;
EDOKUM.MIASTO:=KH.MIASTO;
EDOKUM.UL:=KH.UL;
EDOKUM.DOM:=KH.DOM;
EDOKUM.LOKAL:=KH.LOKAL;
EDOKUM.POCZ:=KH.POCZ;
EDOKUM.KPOCZ:=KH.KPOCZ;
SKID.NIP:=KH.NIP;
{? EDOKUM.TYP().DOM_SL<>null
|| RS.cntx_psh(); RS.index('RS'); RS.prefix();
   {? RS.find_key(EDOKUM.TYP().DOM_SL().SLU().WZ,) & RS.TAB='KH'
   || _kod:=($('KH.'+RS.KOD))();
      SLO.cntx_psh();
      SLO.index('SL'); SLO.prefix(SLU.ref());
      {? ~SLO.find_key(_kod) | _kod<>SLO.KOD
      || SLO.blank();
         SLO.SLU:=SLU.ref();
         SLO.KOD:=_kod;
         SLO.TR:=KH.NAZ;
         SLO.add()
      ?};
      EDOKUM.KH:=SLO.ref();
      SLO.cntx_pop()
   ?};
   RS.cntx_pop()
?};
KH.cntx_pop();
EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
OBIEGI.PREVIEW:=EDOKUM.EDOKUM:=DOKUM.DOKUMI;
_data_odb:={? DOKUM.DATA_ODB<>date(0,0,0)
           || DOKUM.DATA_ODB
           |? DOKUM.DATAKONT<>date(0,0,0)
           || DOKUM.DATAKONT
           || date()
           ?};
{? exec('dat_not','dok_fks',_data_odb) || EDOKUM.DO:=_data_odb ?};
EDOKUM.add();
{? EDOKUM.TYP & EDOKUM.TYP().AUT_ID='T' & EDOKUM.ID=''
|| exec('ustal_numer','obiegi',typobi,SSTALE.AR,1);
   EDOKUM.ID:={? var_pres('id_edok')>0 || id_edok || '' ?}
?};
EDOKUM.bl_file('EDOKUM',1); EDOKUM.bl_file('EPODPIS',1);
EDOKUM.memo_set('','UW_OPDL');
dol_dob:=1;
EDOKUM.win_edit({? EDOKUM.EDOKUM || 'WPR_TEST' || 'WPR' ?});
{? EDOKUM.TYP || exec('aetyped','obiegi') ?};
VAR_DEL.delete('dol_dob');
OBIEGI.DEL_ETAT:='';
exec('kolor_et','obiegi','EDOKUM');
{? typobi=1 & PAR_SKID.get(458)='T' || EDOKUM.fld_fml('SYM','AFTER_EDIT',"exec('spr_sym_fakt','obiegi2')") ?};
exec('dok_edit','obiegi');
_zak_btn:={? EDOKPAR.POZV_WYM='N' & EDOKPAR.POZF_WYM='N' || 1 || 0 ?};
exec('okna_r_btn','obiegi',1,1,1,1,_zak_btn,1,0,0);
_eddok:=0; zawin:=0; zak_rej_ed:=0; pozvedit:=0; zakres:=1; _formadd:=0; kh_ask:=0;
exec('odd_filtr','fst');
{! |?
   {? zawin || EDOKUM.get() ?};
   _ref:=EDOKUM.ref();
   {? EDOKUM.edit("exec('spr_dok','obiegi')")
   || menu_txt(,'Popraw'@);
      EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
      exec('zak_akt','obiegi');
      EDOKUM.cntx_psh(); EDOKUM.prefix();
      do();
      {? EDOKUM.put() & EDOKUM.memo_put(,'UW_OPDL')
      || exec('dol_edokos','obiegi',_we);
         {? EDOKUM.TYP().ATR_G1R>0
         || exec('edk_atr_dolacz','obiegi',0)
         ?};
         OBIEGI.EDOKOS();
         {? ~_formadd & exec('szuk_edokpar','obiegi') & EDOKPAR.FORM_ADD
         || ($EDOKPAR.FORM_ADD().TRESC)();
            _formadd:=1
         ?}
      ?};
      end();
      _wy.EDOKUM:=EDOKUM.ref();
      _mp.save(,_wy);
      _eddok:=1;
      exec('putbloby','obiegi');
      {? zawin
      || {? zawin=2
         || params_exec('pozvdob','obiegi')
         |? zawin=3
         || params_exec('podz_dob','obiegi')
         |? zawin=4
         || exec('zalaczniki','obiegi')
         |? zawin=5
         || exec('inne_dane','obiegi')
         |? zawin=6
         || exec('zak_rej_dok','obiegi');
            {? zak_rej_ed || zawin:=0 ?}
         |? zawin=7
         || exec('pozf_edokum','obiegi')
         |? zawin=8
         || exec('proj_dob','obiegi_proj')
         ?}
      || {? ~pozvedit & EDOKUM.WAL=FINFO.NAROD
         || exec('pozvdob','obiegi',1); EDOKUM.get();
            zawin:=1
         ?}
      ?};
      EDOKUM.cntx_pop();
      {? zawin
      || exec('okna_r_btn','obiegi',1,1,1,1,1,1,1,1); zawin:=0; EDOKUM.get(); 1
      || 0
      ?}
   |? ~_eddok
   || exec('ustal_numer','obiegi',typobi,SSTALE.AR,2);
      EDOKUMW.cntx_psh(); EDOKUMW.use('skidv1'+(EDOKUM.name()+2));
      EDOKUMW.index('UNIK1'); EDOKUMW.prefix(EDOKUM.ref());
      {? EDOKUMW.first() || {! |? EDOKUMW.del() !} ?};
      EDOKUMW.cntx_pop();
      exec('edokum_del_tab','obiegi2');
      EDOKUM.del();
      _mp.cancel(); 0
   ?}
!};
{? DOKUM.get() & _eddok
|| exec('dokum_put','obiegi2');
   exec('edokumz','dokum',_wy.EDOKUM)
?};
::SKID.fld_fml('NIP','F3',"*");
{? typobi=1 & PAR_SKID.get(458)='T' || EDOKUM.fld_fml('SYM','AFTER_EDIT',"*") ?};
exec('tabkhdel','kontrahent');
VAR_DEL.delete('numer','oldnumer','id_edok','typ_ust','zawin','pozvedit','kh_ask')


\rel_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: dokumenty powiązane z dokmentem w obiegu
::----------------------------------------------------------------------------------------------------------------------
_ref:=$EDOKUM.ref();
_tab:=ref_tab(_ref);

VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB', 'BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','BLOKUJ','PODGLAD','WIN');
__CTR.BTAB:='DOKUMZ';
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','bloby','dokum_dokz',':',120);
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('dir_sep','bloby');
__CTR.DND:=1;
__CTR.BLOKUJ:=0;
__CTR.PODGLAD:=1;
__CTR.WIN:='WERK4';

_okno:=DOKUM.grp_make('Powiązane dokumenty'@,,'dokum',,,,,'normal');
_fr:=$('exec(\'gr_dok\',\'dokum\');exec(\'dokum_rfr\',\'bloby\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
DOKUM.grp_sel(_okno,,'WERK4',,_fr);
DOKUM.grp_splt(_okno,,'vertical','nlook1');
DOKUM.grp_sel(_okno,DOKTYP,'CECH',,,,,,,,,,'maximized_with_title','doktyp');
DOKUM.grp_splt(_okno,'panel0','horizontal','nlook2',16);
_fb:="{? grp_empty(DOKUM,__CTR.WIN)|| __CTR.BLOKUJ:=1 || __CTR.BLOKUJ:=0 ?}";
_fa:="";
DOKUM.grp_ctr(_okno,DOKUMZ,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_fb,_fa,,'maximized');

_tab.cntx_psh();
KHVZ.EDIT:=1;
DOKUM.index('DOKUM');
DOKUM.win_sel(_okno);
DOKUM.timer('sel',_okno,10,"win_disp()");
exec('icon','dokum');
POLA.REFSQL:=_ref;
__REFSQL:=POLA.REFSQL;
DOKUM.prefix(REF.FIRMA,POLA.REFSQL);
_par:='N';
_blink_hide:='W(BIU)';
{? exec('is_active','businesslink3')
|| {? ref_tab(POLA.REFSQL)=FAKS
   || {? exec('FindAndGet','#table',FAKS,POLA.REFSQL,,"SZ",'')='S'
      || _blink_hide:=''
      ?}
   ?}
?};
DOKUM.select();
KHVZ.EDIT:=0;
&__REFSQL;
_tab.cntx_pop();
''


\edokos_oper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Zwraca znacznik aktualnie wykonywanej operacji.
::   WE: _a - EDOKUM.ref
::   WY: EDOKOS.OPERACJA
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_edokum:={? _>0 || _a || EDOKUM.ref() ?};
EDOKOS.cntx_psh();
EDOKOS.use('skid_y'+((8+$_edokum)+2)); EDOKOS.index('SZUK9'); EDOKOS.prefix(_edokum,);
{? EDOKOS.last() || _result:=EDOKOS.OPERACJA ?};
EDOKOS.cntx_pop();
_result


\ob_stat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Pokaż statystyki dla dokumentów w obiegu
::   WE: 1 - Faktury, 2 - Wnioski
::----------------------------------------------------------------------------------------------------------------------
SIK.win_edit('STATKON');
SIK.FROMCNT:=1;
{? SIK.edit()
|| {? _a=1 || typobi:=1 || typobi:=2 ?};
   exec('dob_stat','ob_stat');
   VAR_DEL.delete('typobi')
?};
''


\ob_stat_obieg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Pokaż statystyki dla dokumentów w obiegu - obszary Wnioski w obiegu oraz Faktury w obiegu
::   WE: 1 - Faktury, 2 - Wnioski
::----------------------------------------------------------------------------------------------------------------------
SIK.FROMCNT:=0;
{? _a=1 || typobi:=1 || typobi:=2 ?};
exec('dob_stat','ob_stat');
''


\ob_stat_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Akcja Pokaż statystyki dla dokumentów w obiegu - obszary Wnioski w obiegu oraz Faktury w obiegu
::   WE: 1 - Faktury, 2 - Wnioski
::----------------------------------------------------------------------------------------------------------------------
{? var_press('zakres')>0 || _tmp1:=zakres; _ind1:=1 || _ind1:=0 ?};
exec('ob_stat_obieg','obiegi2',_a);
{? _ind1 || zakres:=_tmp1 ?};
''


\dokum_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Aktualizacja powiązanego kontaktu z dokumentem w obiegu
::----------------------------------------------------------------------------------------------------------------------
DOKUM.cntx_psh(); DOKUM.prefix();
{? DOKUM.seek(EDOKUM.DOKUM)
|| DOKUM.EDOKUMR:=DOKUM.REFSQL:=$EDOKUM.ref();
   {? DOKUM.KH_OSOB<>null & DOKUM.KH<>EDOKUM.KHKH || DOKUM.KH_OSOB:=null() ?};
   DOKUM.KH:=EDOKUM.KHKH;
   DOKUM.OPER:=exec('name','users');
   {? 'F;W'*DOKUM.TYP_K>0
   || DOKUM.KR_OP:=EDOKUM.TR;
      DOKUM.ODD:=EDOKUM.ODD;
      DOKUM.OBKONETP:=EDOKUM.B_PRELS;
      DOKUM.OSOBA:=EDOKUM.HAN().NAZ
   || {? DOKUM.KR_OP='' || DOKUM.KR_OP:=EDOKUM.TR ?};
      {? ~DOKUM.ODD || DOKUM.ODD:=EDOKUM.ODD ?};
      {? DOKUM.OBKONETP='' || DOKUM.OBKONETP:=EDOKUM.B_PRELS ?};
      {? DOKUM.OSOBA='' || DOKUM.OSOBA:=EDOKUM.HAN().NAZ ?}
   ?};
   {? DOKUM.BL='T'
   || DOKUM.BL_STAT:={? var_press('zak_rej_ed')>0 & zak_rej_ed=1
                     || exec('registered','zbl_stat')
                     || exec('during_registration','zbl_stat')
                     ?}
   ?};
   DOKUM.put()
?};
DOKUM.cntx_pop();
1


\rodz_biez_etap_abs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Zwraca rodzaj bieżącego etapu dla wniosków ze wspólnego portalu ABS.
::   WE: _a [REFERENCE] - EDOKUM.ref (bieżący)
::   WY: Rodzaj bieżącego etapu dla wniosków portalowych lub '' w przypadku gdy nie został znaleziony
::----------------------------------------------------------------------------------------------------------------------
_ref:={? var_pres('_a')=type_of(null) & ref_name(_a)=EDOKUM.name() || _a || null ?};
_type:='';

{? _ref<>null()
|| EDOKOS.cntx_psh();
   {? (EDOKOS.name()+2)<>(EDOKUM.name()+2)
   || EDOKOS.use((EDOKOS.name(1)-2)+(EDOKUM.name()+2))
   ?};
   EDOKOS.index('SZUK2');
   EDOKOS.prefix(_ref,'D');
   {? EDOKOS.first() || _type:='D' ?};
   {? _type=''
   || EDOKOS.index('SZUK11');
      EDOKOS.prefix(_ref,'P');
      {? EDOKOS.last() || _type:=EDOKOS.OPERACJA ?}
   ?};
   EDOKOS.cntx_pop()
?};
_type


\settypobi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Ustawia zmienną typobi
::   WE: [_a] - wymuszone ustawienie: [1]-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_set:={? var_pres('_a')=type_of(1) || _a || 1 ?};
{? _set | var_pres('typobi')<=0
|| {? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg faktur'
   || typobi:=1
   |? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg wniosków'
   || typobi:=2
   |? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg delegacji'
   || typobi:=3
   ?}
?}


\parses_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Ustawia parametr OkresRok na podstawie rekordu EDOKUM
::   WE: EDOKUM.ref
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
ROK_F.cntx_psh(); EDOKUM.cntx_psh();
{? EDOKUM.seek(_a,ref_name(_a))
|| _rok:=EDOKUM.ROK_F().KOD;
   {? _rok<>''
   || ROK_F.index('KODG'); ROK_F.prefix(_rok);
      {? ROK_F.first()
      || OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
         {? OKRO_F.first() & OKRO_F.next()
         || __PARSES.setVal('OkresRok',OKRO_F.ref());
            _result:=1
         ?};
         OKRO_F.cntx_pop()
      ?}
   ?}
?};
ROK_F.cntx_pop(); EDOKUM.cntx_pop();
_result


\usr_per_etyp_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Zwraca grupy do których bieżący użytkownik ma uprawnienie
::   WY: _ret [ARRAY] (REF) - Ref grupy
::----------------------------------------------------------------------------------------------------------------------
_ret:=obj_new('counter','TAB');
_ret.counter:=0;
_ret.TAB:=tab_tmp(,'REF','STRING[16]','Ref');

USERSOGR.cntx_psh();
USERSOGR.index('UNIK');
USERSOGR.prefix(exec('ref_firma','ustawienia'),OPERATOR.USER);
{? USERSOGR.first()
|| {! |?
      _ret.TAB.REF:=$USERSOGR.OGRP;
      _ret.TAB.add();
      _ret.counter+=1;

      USERSOGR.next()
   !}
?};
USERSOGR.cntx_pop();

_ret


\where_for_grpdocs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Zwraca warunek where dla zapytania SQL w kontekście grup dokumentów
::   WE: _a [STRING] - Aktualny wejściowy warunek
::   WY: _ret [STRING] - Warunek dookreślony dla grup dokumentów
::----------------------------------------------------------------------------------------------------------------------
_where:=_a;

{? __PARSES.getVal('GrupaObiegu').KOD <> ''
|| _where+=' and ETYPY.GRP_DOC=\''+$__PARSES.getVal('GrupaObiegu').REF+'\''
|| _grpPerm:=exec('usr_per_etyp_grp','obiegi2');
   {? _grpPerm.TAB.size()>0
   || _grpSym:=''; _cnt:=0;
      {? _grpPerm.TAB.first()
      || _cnt:=1;
         {! |? _grpSym+='ETYPY.GRP_DOC=\''+_grpPerm.TAB.REF+'\''+{? _cnt<_grpPerm.TAB.size() || ' or ' || '' ?};
            _cnt+=1; _grpPerm.TAB.next()
         !}
      ?}; _where+=' and ('+_grpSym+')'
   || _grpSym:='ETYPY.GRP_DOC=\'NOGRPACCESS\'';
      _where+=' and ('+_grpSym+')'
   ?}
?};

_ret:=_where;
_ret


\has_grp_perm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Czy dany użytkownik ma uprawnienie do grupy dokumentów w obiegu
::   WE: _a [STRING] - Kod grupy dokumentów w obiegu
::       _b [STRING] - ID czynności
::   WY: _ret [INTEGER] - 0 Nieuprawniony 1 Uprawniony
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;

{? var_pres('_a')<=0 || _a:='' ?};
{? var_pres('_b')<=0 || _b:='' ?};

{? _a<>''
|| USERSOGR.index('USERSGRP'); USERSOGR.prefix(REF.FIRMA,OPERATOR.USER);
   {? (exec('chk_role','#b__box',OPERATOR.USER,_b) &
      Perm.hasPermissions(exec('FindInSet','#table','B_ACTION','UNIK',_b,,"B_ACTION.ref()",1,,null())) &
      USERSOGR.find_key(_a))
   || _ret:=1
   ?}
?};

_ret


\zakres0
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2008]
:: OPIS: Zakres dokumentow lub wnioskow
::   WE: _a - 1 - rejestracja
::            2 - akceptacja
::            3 - dekretacja
::            5 - przekazanie do akceptacji
::
::       _b - 1 - niezakończone (wprowadzanie)
::            2 - zakończone (wprowadzanie)
::            3 - odrzucone (wprowadzanie)
::            4 - wszystkie (wprowadzanie)
::
::            1 - nieprzekazane (przekazanie)
::            2 - przekazane (przekazanie)
::            3 - odrzucone (przekazanie)
::            4 - wszystkie (przekazanie)
::
::            1 - niezaakceptowane (akceptacja)
::            2 - zaakceptowane (akceptacja)
::            3 - odrzucone (akceptacja)
::            4 - wszystkie (akceptacja)
::
::            1 - niezadekretowane (dekretacja)
::            2 - zadekretowane (dekretacja)
::            3 - wszystkie dekretowane (dekretacja)
::            4 - zarejestrowane (dekretacja)
::
::       _c - 0 - bieżącego użytkownika
::            1 - wszystkich użytkowników
::            2 - wszystkich użytkowników wg grup
::
::       _d - 0 - nie blokować wtyczki na zakres (domyślnie)
::            1 - blokować wtyczkę na zakres
::  OLD: \zakres/skid_dob.fml
::  OLD: \zakres/obiegi.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_d')>0 & type_of(_d)=type_of(1)
|| _lock_plugin:=_d
|| _lock_plugin:=0
?};
{? (_lock_plugin=0 | (var_press('unlock_permanent')>0 & unlock_permanent=1)) & app_info('web_sesid')=''
   & Plugin.exists('EDOKUM_WDEK_ZAKR_001') & cur_tab(1,1)=EDOKUM & cur_win(1,1)='WDEK' & _a=3
|| PLUGINS.cntx_psh();
   PLUGINS.index('KOD');
   PLUGINS.prefix('EDOKUM_WDEK_ZAKR_001',);
   _plugin_exec:=PLUGINS.first() & PLUGINS.FORMULA<>'';
   PLUGINS.cntx_pop();
   unlock_permanent:=1
|| _plugin_exec:=0
?};
{? _plugin_exec
|| {? Plugin.run('EDOKUM_WDEK_ZAKR_001',_b)=0
   || unlock_permanent:=0
   ?}
|| {? app_info('web_sesid')='' & ~(var_pres('wybortyp')>0 & wybortyp=0)
   || OBIEGI.win_edit('TYP');
      {? OBIEGI.CZY_TYP='' || OBIEGI.CZY_TYP:='W' ?};
      {? ~OBIEGI.edit() || return(0) ?};
      wyb_zam:={? OBIEGI.CZY_ZAM='N' | OBIEGI.CZY_ZAM='Z' || 1 || 0 ?}
   ?};
   wyb_typ:={? OBIEGI.CZY_TYP='B' & OBIEGI.TYP || 1 || 0 ?};
   {? app_info('web_sesid')=''
   || AreaTitle.addTitleParams({? typobi=1 || 'OBG_FZO' |? typobi=2 || 'OBE_FAD' || 'OBE_FAD' ?},'FKS_OKRO','FKS_ODD','OBG_ZAKR');
      AreaTitle.setTitle()
   ?};
   EDOKUM.prefix(); _hdr:=''; _where:='EDOKUM.FIRMA=:_d and '; _odd:={? OPERATOR.DEPT || ' and (EDOKUM.ODD=:_c or EDOKUM.ODD is null) ' || '' ?};
   {? _a=1
   || {? _c<=1 || wla_dok:=~_c || wla_dok:=_c ?};
      {? _b=1 || _zakr:='''N'''; _hdr:='niezakończone'@
      |? _b=2 || _zakr:='''T'''; _hdr:='zakończone'@
      |? _b=3 || _zakr:='''O'''; _hdr:='odrzucone'@
      || _hdr:='wszystkie'@
      ?};
      {? _b<>4
      || {? wla_dok=1
         || _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''W'' and EDOKOS.USERS=:_a and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', zalogowanego użytkownika'@
         |? wla_dok=2
         || {? app_info('web_sesid')=''
            || AreaTitle.addTitleParams({? typobi=1 || 'OBG_FZO' |? typobi=2 || 'OBE_FAD' || 'OBE_FAD' ?},'FKS_OKRO','FKS_ODD','OBE_GRP','OBG_ZAKR');
               AreaTitle.setTitle()
            ?};
            _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''W'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _where:=exec('where_for_grpdocs','obiegi2',_where);
            _hdr+=', wszystkich użytkowników'@
         || _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''W'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', wszystkich użytkowników'@
         ?}
      || {? wla_dok=1
         || _where+='EDOKOS.OPERACJA=''W'' and EDOKOS.USERS=:_a and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', zalogowanego użytkownika'@
         |? wla_dok=2
         || {? app_info('web_sesid')=''
            || AreaTitle.addTitleParams({? typobi=1 || 'OBG_FZO' |? typobi=2 || 'OBE_FAD' || 'OBE_FAD' ?},'FKS_OKRO','FKS_ODD','OBE_GRP','OBG_ZAKR');
               AreaTitle.setTitle()
            ?};
            _where+='EDOKOS.OPERACJA=''W'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _where:=exec('where_for_grpdocs','obiegi2',_where);
            _hdr+=', wszystkich użytkowników'@
         || _where+='EDOKOS.OPERACJA=''W'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', wszystkich użytkowników'@
         ?}
      ?};
      zakr_wpr:=_b
   |? _a=2
   || {? _c<=1 || wla_dok1:=~_c || wla_dok1:=_c ?};
      {? _b=1 || _zakr:='''N'''; _hdr:='niezaakceptowane'@
      |? _b=2 || _zakr:='''T'''; _hdr:='zaakceptowane'@
      |? _b=3 || _zakr:='''O'''; _hdr:='odrzucone'@
      || _hdr:='wszystkie'@
      ?};
      {? _b<>4
      || {? wla_dok1=1
         || _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''A'' and EDOKOS.USERS=:_a and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', zalogowanego użytkownika'@
         |? wla_dok1=2
         || {? app_info('web_sesid')=''
            || AreaTitle.addTitleParams({? typobi=1 || 'OBG_FZO' |? typobi=2 || 'OBE_FAD' || 'OBE_FAD' ?},'FKS_OKRO','FKS_ODD','OBE_GRP','OBG_ZAKR');
               AreaTitle.setTitle()
            ?};
            _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''A'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _where:=exec('where_for_grpdocs','obiegi2',_where);
            _hdr+=', wszystkich użytkowników'@
         || _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''A'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', wszystkich użytkowników'@
         ?}
      || {? wla_dok1=1
         || _where+='EDOKOS.OPERACJA=''A'' and EDOKOS.USERS=:_a and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', zalogowanego użytkownika'@
         |? wla_dok1=2
         || {? app_info('web_sesid')=''
            || AreaTitle.addTitleParams({? typobi=1 || 'OBG_FZO' |? typobi=2 || 'OBE_FAD' || 'OBE_FAD' ?},'FKS_OKRO','FKS_ODD','OBE_GRP','OBG_ZAKR');
               AreaTitle.setTitle()
            ?};
            _where+='EDOKOS.OPERACJA=''A'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _where:=exec('where_for_grpdocs','obiegi2',_where);
            _hdr+=', wszystkich użytkowników'@
         || _where+='EDOKOS.OPERACJA=''A'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', wszystkich użytkowników'@
         ?}
      ?};
      zakr_mer:=_b
   |? _a=3
   || {? _b=1
      || _zakr:='''N'''; _hdr:='niezadekretowane'@;
         {? typobi=3
         || _where+='EDOKUM.STDEKRD=0 and EDOKOS.OPERACJA=''D'' and ETYPY.DEKR=''T'' and '
         || _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''D'' and ETYPY.DEKR=''T'' and '
         ?}
      |? _b=2
      || _zakr:='''T'''; _hdr:='zadekretowane'@;
         _where+='EDOKUM.STDEKRD=1 and EDOKOS.OPERACJA=''D'' and ETYPY.DEKR=''T'' and '
      |? _b=3
      || _hdr:='wszystkie dekretowane'@; _where+='ETYPY.DEKR=''T'' and '
      |? _b=4
      || _hdr:='zarejestrowane'@;  _where+=''
      ?};
      _where+='EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
      zakr_ks:=_b
   |? _a=5
   || {? _c<=1 || wla_dok2:=~_c || wla_dok2:=_c ?};
      {? _b=1 || _zakr:='''N'''; _hdr:='nieprzekazane'@
      |? _b=2 || _zakr:='''T'''; _hdr:='przekazane'@
      |? _b=3 || _zakr:='''O'''; _hdr:='odrzucone'@
      || _hdr:='wszystkie'@
      ?};
      {? _b<>4
      || {? wla_dok2=1
         || _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''P'' and EDOKOS.USERS=:_a and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', zalogowanego użytkownika'@
         |? wla_dok2=2
         || {? app_info('web_sesid')=''
            || AreaTitle.addTitleParams({? typobi=1 || 'OBG_FZO' |? typobi=2 || 'OBE_FAD' || 'OBE_FAD' ?},'FKS_OKRO','FKS_ODD','OBE_GRP','OBG_ZAKR');
               AreaTitle.setTitle()
            ?};
            _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''P'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _where:=exec('where_for_grpdocs','obiegi2',_where);
            _hdr+=', wszystkich użytkowników'@
         || _where+='EDOKOS.STATUS='+_zakr+' and EDOKOS.OPERACJA=''P'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', wszystkich użytkowników'@
         ?}
      || {? wla_dok2=1
         || _where+='EDOKOS.OPERACJA=''P'' and EDOKOS.USERS=:_a and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', zalogowanego użytkownika'@
         |? wla_dok2=2
         || {? app_info('web_sesid')=''
            || AreaTitle.addTitleParams({? typobi=1 || 'OBG_FZO' |? typobi=2 || 'OBE_FAD' || 'OBE_FAD' ?},'FKS_OKRO','FKS_ODD','OBE_GRP','OBG_ZAKR');
               AreaTitle.setTitle()
            ?};
            _where+='EDOKOS.OPERACJA=''P'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _where:=exec('where_for_grpdocs','obiegi2',_where);
            _hdr+=', wszystkich użytkowników'@
         || _where+='EDOKOS.OPERACJA=''P'' and EDOKOS.WID=''T'' and EDOKUM.TYPOBIEG=:_b'+_odd;
            _hdr+=', wszystkich użytkowników'@
         ?}
      ?};
      zakr_pak:=_b
   ?};
   {? typobi=3
   || _where+=' and ETYPY.CZY_PLAN<>2 and ETYPY.W_PORTAL=\'N\' '
   ?};
   {? typobi=2 & (_a=1 | app_info('web_sesid')<>'')
   || _where+=' and (ETYPY.W_PORTAL=\'N\''+{? app_info('web_sesid')='' || ' or ETYPY.W_PORTAL=\'P\' or ETYPY.W_PORTAL=\'s\'' || '' ?} +')'
   ?};
   {? typobi=1 || _where+=' and ETYPY.FAKT_DEL<>2 ' ?};
   {? var_pres('wyb_typ')>0 & wyb_typ
   || _where+=' and EDOKUM.TYP=:_e '
   ?};
   {? OBIEGI.CZY_ZAM='N' | OBIEGI.CZY_ZAM=''
   || _where+=' and EDOKUM.ZAM<>''T'' '; _hdr+=', niezamknięte'@
   |? OBIEGI.CZY_ZAM='Z'
   || _where+=' and EDOKUM.ZAM=''T'' '; _hdr+=', zamknięte'@
   ?};
   _join:='join EDOKOS using(EDOKOS.EDOKUM,EDOKUM.REFERENCE) '+
          'join ETYPY using(EDOKUM.TYP,ETYPY.REFERENCE) '+
          'join TYPOBIEG using(EDOKUM.TYPOBIEG,TYPOBIEG.REFERENCE)';
   {? app_info('web_sesid')=''
   || EDOKUM.sel_adel();
      hdr_obg:=_hdr;
      AreaTitle.setTitle();
      _sort:={? typobi=3
             || 'DW,DOSTAWCA(NAZWISKO),DATA_OD'
             || 'DATAW,ODD(OD),KH(KOD)'
             ?};
      EDOKUM.f_set(_sort,_join,_where,OPERATOR.USER,OBIEGI.TYPOBIEG,OPERATOR.DEPT,REF.FIRMA,OBIEGI.TYP);
      exec('zakres1','obiegi',_a)
   || _tab:=sql('select EDOKUM.REFERENCE as REF from EDOKUM '+_join+
                'where '+_where+'order by 1',OPERATOR.USER,OBIEGI.TYPOBIEG,OPERATOR.DEPT,REF.FIRMA,OBIEGI.TYP);
      _tcid:=app_info('web_tcid');
      _mbid:=app_info('web_mbid');
      _sesid:=app_info('web_sesid');
      _tabid:=app_info('web_tabid');
      _rodzaj:={? _a=1 || 'R' |? _a=2 || 'A' || 'P' ?};
      EDOKUMW.index('UNIK2'); EDOKUMW.prefix(_tcid,_tabid,_rodzaj);
      {? EDOKUMW.first()
      || {! |?
            EDOKUMW.del()
         !}
      ?};
      {? _tab.first()
      || {! |?
            {? EDOKUM.seek(_tab.REF)
            || EDOKUMW.blank(1);
               EDOKUMW.EDOKUM:=EDOKUM.ref();
               EDOKUMW.SESID:=_sesid;
               EDOKUMW.MBID:=_mbid;
               EDOKUMW.TCID:=_tcid;
               EDOKUMW.TABID:=_tabid;
               EDOKUMW.RODZAJ:=_rodzaj;
               EDOKUMW.KH:=EDOKUM.KH;
               EDOKUMW.TYP:=EDOKUM.TYP;
               EDOKUMW.ALERTY:=EDOKUM.ALERTY;
               EDOKUMW.STDEKRD:=EDOKUM.STDEKRD;
               EDOKUMW.DOSTAWCA:=EDOKUM.DOSTAWCA;
               EDOKUMW.DEL_CEL:=EDOKUM.DEL_CEL;
               EDOKUMW.ZAM:=EDOKUM.ZAM;
               EDOKUMW.USERS:=EDOKUM.USERS;
               EDOKUMW.add(1)
            ?};
            _tab.next()
         !}
      ?}
   ?}
?};
1


\chk_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Sprawdza, czy okres w parametrach jest zgodny z bieżącym.
::   WE: _a - 0/1 - uruchomienie z pulpitu / uruchomienie z obszaru (domyślnie)
::       _b - 0/1 - porównanie okresów / porównanie roku
::   WY: 0/1/-1
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_press('_a')>0 || _a || 1 ?};
_b:={? var_press('_b')>0 || _b || 0 ?};
{? +app_info('web_sesid')
|| exec('env_wt','b_proces');
   REF.FIRMA:=exec('firma_ref','#firma',app_info('app_ident'))
?};
ROK_F.cntx_psh(); OKRO_F.cntx_psh();
_result:=exec('szuk_okro','okresy',date())<>null() & exec('szuk_okr','okresy',date())<>null();
SSTALE.AO:=_okr_f:=__PARSES.getVal('OkresRok').OKRO_O;
SSTALE.AR:=_rok_f:=exec('FindAndGet','#table',OKRO_F,_okr_f,,"@.OKRO_F.ROK",null);
{? _result & ((_b=1 & _rok_f<>ROZNE.UT_OKROD().ROK) | (_b=0 & _okr_f<>ROZNE.UT_OKROD))
|| _ask:={? SSTALE.AO=null() | SSTALE.AR=null()
         || 'Nie ustawiono okresu w parametrach pracy.\nBieżący okres: %1 %2\n\nCzy ustawić okres w parametrach pracy?'@
            [ROZNE.UT_OKROD().NAZ,ROZNE.UT_OKROD().ROK().NAZ]
         |? _b=1
         || 'W parametrach pracy wybrałeś rok: %1\nBieżący rok kalendarzowy: %2\n\nCzy chcesz zmienić rok w parametrach pracy?'@
            [SSTALE.AR().NAZ,ROZNE.UT_OKROD().ROK().NAZ]
         || 'W parametrach pracy wybrałeś okres: %1 %2\nOkres wynikający z bieżącej daty: %3 %4\n\nCzy chcesz zmienić okres w parametrach pracy?'@
            [SSTALE.AO().NAZ,SSTALE.AR().NAZ,ROZNE.UT_OKROD().NAZ,ROZNE.UT_OKROD().ROK().NAZ]
         ?};
   {? +app_info('web_sesid') | FUN.ask(_ask)
   || {? OKRO_F.ZAM='T' | OKRO_F.ZAM_CON='T'
      || _result:=-1
      || SSTALE.AO:=ROZNE.UT_OKROD;
         SSTALE.AR:=ROZNE.UT_OKROD().ROK;
         ROZNE.ROKKON:=SSTALE.AR;
         __PARSES.setVal('OkresRok',SSTALE.AO);
         {? _a
         || {? +app_info('web_sesid')
            || exec('AreaTitle','#object');
               _jedn:={? OPERATOR.DEPT
                      || ', jednostka księgowa: '@+OPERATOR.DEPT().OD
                      || ', wszystkie jednostki księgowe'@
                      ?};
               web_title('%1, %2: %3'[exec('nazwa','#system'),'Firma '@+REF.FIRMA().SYMBOL,AreaTitle.getTitle()+' rok: '@+SSTALE.AR().NAZ+_jedn],'all')
            || AreaTitle.setTitle();
               exec('po_edokum','obiegi',zakres)
            ?}
         ?}
      ?}
   || _result:=0
   ?}
|| {? _result=0
   || {? _b=1
      || FUN.info('Wybrany rok w parametrach pracy jest nieaktualny.\nRok %1 nie został jeszcze założony w danych systemu.'@[$(date()~1)])
      || FUN.info('Wybrany okres w parametrach pracy jest nieaktualny.\nOkres dla daty %1 nie został jeszcze założony w danych systemu.'@[$date()])
      ?}
   ?};
   _result:=0
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
{? _result=-1
|| FUN.info('Wystąpił problem podczas zmiany parametrów na bieżący okres.'@)
?};
_result


\edokum_del_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Ususwanie rekordów z tabel powiązanych z bieżącym EDOKUM
::----------------------------------------------------------------------------------------------------------------------
EDOKUMD.cntx_psh();
EDOKUMD.use('bdiety'+(EDOKUM.name()+2));
EDOKUMD.index('DELEG');
EDOKUMD.prefix(EDOKUM.ref());
{? EDOKUMD.first() || {! |? EDOKUMD.del() !} ?};
EDOKUMD.cntx_pop();

EDOKUMP.cntx_psh();
EDOKUMP.use('skidpu'+(EDOKUM.name()+2));
EDOKUMP.index('CHRONO');
EDOKUMP.prefix(EDOKUM.ref());
{? EDOKUMP.first() || {! |? EDOKUMP.del() !} ?};
EDOKUMP.cntx_pop();

EDOK_ZAL.cntx_psh();
EDOK_ZAL.use('edokzl'+(EDOKUM.name()+2));
EDOK_ZAL.index('ROZLICZ');
EDOK_ZAL.prefix(EDOKUM.ref());
{? EDOK_ZAL.first() || {! |? EDOK_ZAL.del() !} ?};
EDOK_ZAL.cntx_pop();

EDOK_ATK.cntx_psh();
EDOK_ATK.use('edoknt'+(EDOKUM.name()+2));
EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOKUM.ref());
{? EDOK_ATK.first() || {! |? EDOK_ATK.del !} ?};
EDOK_ATK.cntx_pop();

EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref());
{? EDOK_ATR.first() || {! |? EDOK_ATR.del !} ?};
EDOK_ATR.cntx_pop();

EDOKPAR.cntx_psh();
EDOKPAR.use('skidh_'+(EDOKUM.name()+2));
EDOKPAR.index('UNIK'); EDOKPAR.prefix(EDOKUM.ref());
{? EDOKPAR.first() || {! |? EDOKPAR.del !} ?};
EDOKPAR.cntx_pop();

EPODZ.cntx_psh();
EPODZ.use('skid_j'+(EDOKUM.name()+2));
EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
{? EPODZ.first() || {! |? EPODZ.del !} ?};
EPODZ.cntx_pop();

EDOKUMZ.cntx_psh();
EDOKUMZ.use('skid_n'+(EDOKUM.name()+2));
EDOKUMZ.index('DISP'); EDOKUMZ.prefix(EDOKUM.ref());
{? EDOKUMZ.first() || {! |? EDOKUMZ.del !} ?};
EDOKUMZ.cntx_pop()


\win_del_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Ustawia pola w oknie delegacji przed popraw
::   WE: _a - akronim okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_okno:=_a;
{? XINFO.CZY_CEL
|| EDOKUM.efld_opt(_okno,'mark=1, enable=1, editable=1',EDOKUM,'DEL_CEL')
|| EDOKUM.efld_opt(_okno,'mark=0, enable=1, editable=1',EDOKUM,'DEL_CEL')
?};
EDOKUM.efld_opt(_okno,'mark=1, enable=1, editable=1',EDOKUM,'DEL_MIE');
EDOKUM.efld_opt(_okno,'mark=1, enable=1, editable=1',EDOKUM,'DW');
{? EDOKUM.TYP
|| {? ETYPY.ED_PODZ='T'
   || EDOKUM.btn_eopt(_okno,'btnPODZ','state=normal')
   || EDOKUM.btn_eopt(_okno,'btnPODZ','state=grayed')
   ?};
   {? exec('be_id_edokum1','obiegi')
   || EDOKUM.efld_opt(_okno,'mark=1, enable=1, editable=1',EDOKUM,'ID')
   || EDOKUM.efld_opt(_okno,'mark=0, enable=1, editable=0',EDOKUM,'ID')
   ?};
   {? ETYPY.CZY_PRAC
   || EDOKUM.efld_opt(_okno,'mark=1, enable=1, editable=1',EDOKUM,'DOSTAWCA')
   || EDOKUM.efld_opt(_okno,'mark=1, enable=1, editable=0',EDOKUM,'DOSTAWCA')
   ?};
   {? ETYPY.CZY_DATY
   || {? ETYPY.CZY_DATY=2
      || EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=1',EDOKUM,'DATA_OD');
         EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=1',EDOKUM,'DATA_DO')
      |? ETYPY.CZY_DATY=1
      || EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=0',EDOKUM,'DATA_OD');
         EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=0',EDOKUM,'DATA_DO')
      ?}
   || EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'DATA_OD');
      EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'DATA_DO')
   ?};
   {? ETYPY.CZY_GODR
   || {? ETYPY.CZY_GODR=2
      || EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=1',EDOKUM,'GODZ_OD')
      || EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'GODZ_OD')
      ?}
   || EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'GODZ_OD')
   ?};
   {? ETYPY.CZY_GODZ
   || {? ETYPY.CZY_GODZ=2
      || EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=1',EDOKUM,'GODZ_DO')
      || EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'GODZ_DO')
      ?}
   || EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'GODZ_DO')
   ?};
   {? ETYPY.DEL_ZAGR
   || EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=0',EDOKUM,'GRAN_DAT');
      EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=0',EDOKUM,'GRAN_GDZ');
      EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=0',EDOKUM,'GRAN_DPW');
      EDOKUM.efld_opt(_okno,'editable=1, enable=1, mark=0',EDOKUM,'GRAN_GPW')
   || EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'GRAN_DAT');
      EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'GRAN_GDZ');
      EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'GRAN_DPW');
      EDOKUM.efld_opt(_okno,'enable=0, mark=0',EDOKUM,'GRAN_GPW')
   ?};
   {? ETYPY.DEKR='N'
   || EDOKUM.efld_opt(_okno,'mark=0, enable=0',EDOKUM,'ODD');
      EDOKUM.efld_opt(_okno,,'mark=0, enable=0',EDOKUM,'DATDOK');
      EDOKUM.efld_opt(_okno,,'mark=0, enable=0',EDOKUM,'REJ');
      EDOKUM.efld_opt(_okno,,'mark=0, enable=0',EDOKUM,'NREJ')
   || EDOKUM.efld_opt(_okno,'mark=0, enable=1, editable=1',EDOKUM,'ODD');
      EDOKUM.efld_opt(_okno,'mark=1, enable=1, editable=1',EDOKUM,'DATDOK');
      EDOKUM.efld_opt(_okno,'mark=1, enable=1, editable=1',EDOKUM,'REJ');
      EDOKUM.efld_opt(_okno,'mark=1, enable=1, editable=1',EDOKUM,'NREJ')
   ?};
   exec('del_etat','obiegi');
   {? ETYPY.CZY_KH
   || EDOKUM.efld_opt(_okno,'enable=1, editable=1',SKID,'SL_KH')
   || EDOKUM.efld_opt(_okno,'enable=0',SKID,'SL_KH')
   ?};
   {? ETYPY.CZY_KH & +SKID.SL_KH
   || EDOKUM.efld_opt(_okno,'enable=1, editable=1',SKID,'NIP')
   || EDOKUM.efld_opt(_okno,'enable=0',SKID,'NIP')
   ?};
   {? +SKID.SL_KH & ETYPY.CZY_KH
   || SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
      {? SLU.find_key(SKID.SL_KH) & (SLU.NAZ=SKID.SL_KH)
      || EDOKUM.efld_opt(_okno,'enable=1, editable=1',EDOKUM,'KH')
      || EDOKUM.efld_opt(_okno,'enable=0',EDOKUM,'KH')
      ?};
      SLU.cntx_pop()
   || EDOKUM.efld_opt(_okno,'enable=0',EDOKUM,'KH')
   ?}
?}


\obj_typ_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Zwraca obiekt do obsługi typu wniosku
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'NAZWA','W_PORTAL',
   'set','get'
);
_obj.set:="
   _typ:=_a;
   _arr:=spli_str(_typ,';');
   .NAZWA:=_arr[1];
   .W_PORTAL:={? obj_len(_arr)=2 || _arr[2] || 'N' ?}
";
_obj.get:="
   _typ:=ETYPY.NAZWA+{? ETYPY.W_PORTAL<>'N' || ';'+ETYPY.W_PORTAL || '' ?}
";
_obj


\wyw_miejsce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Ustawienie parametru czynności - M_WYW - Miejsce wywołania
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Miejsce wywołania'@,1,
                    'Kontakty'@,'Wnioski'@);
{? _choice
|| {? _choice=1
   || 'K'
   |? _choice=2
   || 'W'
   ?}
|| ~~
?}


\wyw_rodzaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Ustawienie parametru czynności - M_WYW - Rodzaj wywołania
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Rodzaj wywołania'@,1,
                    'jTERM/webTERM'@,'HR Portal'@,'Paperless'@);
{? _choice
|| {? _choice=1
   || 'JW'
   |? _choice=2
   || 'HRP'
   |? _choice=3
   || 'PHR'
   ?}
|| ~~
?}


\is_act_b_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła zwraca, czy w systemie istnieje powiązany aktwyny i zaakceptowany proces dla wskazanego typu wniosku.
::   WE: ETYPY.ref
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
ETYPPROC.cntx_psh();
ETYPPROC.index('UNIK'); ETYPPROC.prefix(REF.FIRMA,_a);
{? ETYPPROC.first()
|| B_PROC.cntx_psh(); B_PROC.index('ACCEPTED');
   B_PROC.prefix('N','T','T',REF.FIRMA,ETYPPROC.PROC_SYM,ETYPPROC.PROC_VER,);
   _result:=B_PROC.first();
   {? ~_result
   || FUN.error('Brak procesu: %1 lub nie jest on zaakceptowany.'[ETYPPROC.PROC_SYM+' ('+ETYPPROC.PROC_VER+')'])
   ?};
   B_PROC.cntx_pop()
|| FUN.error('Brak przypisanego procesu do typu: %1'[ETYPY.NAZWA])
?};
ETYPPROC.cntx_pop();
_result


\put_act_b_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.37]
:: OPIS: Formuła sprawdza, czy w systemie istnieje zaakceptowany i jedyny aktwyny proces dla wskazanego typu wniosku,
::       następnie zwraca wynik, czy udało się zmienić wersję dla rekordu ETYPYPROC.
::   WE: _a - PROC_SYM przed użyciem put
::       _b - PROC_VER przed użyciem put
::   WY: 0 - jest więcej niż jeden aktywny lub zaakceptowany proces/nie udało się zmienić wersji dla ETYPYPROC
::       1 - jest jeden aktywny i zaakceptowany proces/udało się poprawić wersję dla ETYPYPROC
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
B_PROC.cntx_psh(); B_PROC.index('ACCEPTED');
B_PROC.prefix('N','T','T',REF.FIRMA,_a,);
{? B_PROC.first() & B_PROC.size()=1
|| _result:=exec('putVer_etypproc','#b_proc',_a,_b,B_PROC.VER)
?};
B_PROC.cntx_pop();
_result


\us_dob1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Usuwanie dokumentow w obiegu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
do();
EDOKUMW.index('UNIK1'); EDOKUMW.prefix(EDOKUM.ref());
{? EDOKUMW.first() || {! |? EDOKUMW.del() !} ?};
EDOKPAR.index('UNIK'); EDOKPAR.prefix(EDOKUM.ref());
{? EDOKPAR.first() || {! |? EDOKPAR.del() !} ?};
EDOKUSER.index('UNIK'); EDOKUSER.prefix(EDOKUM.ref());
{? EDOKUSER.first() || {! |? EDOKUSER.del() !} ?};
EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
{? EVAT.first() || {! |? EVAT.del() !} ?};
EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
{? EPODZ.first() || {! |? EPODZ.del() !} ?};
EDOKUMPR.index('DISP'); EDOKUMPR.prefix(EDOKUM.ref());
{? EDOKUMPR.first() || {! |? EDOKUMPR.del() !} ?};
EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref());
{? EDOKOS.first() || {! |? EDOKOS.del() !} ?};
EDOKGR.index('UNIK'); EDOKGR.prefix(EDOKUM.ref());
{? EDOKGR.first() || {! |? EDOKGR.del() !} ?};
EDOKLOG.index('DISP'); EDOKLOG.prefix(EDOKUM.ref());
{? EDOKLOG.first() || {! |? EDOKLOG.del() !} ?};
EDOKUMZ.index('DISP'); EDOKUMZ.prefix(EDOKUM.ref());
{? EDOKUMZ.first()
|| EDOKUMZS.index('EDOKUMZ');
   {!
   |? EDOKUMZS.prefix(EDOKUMZ.ref());
      {? EDOKUMZS.first() || {! |? EDOKUMZS.del !} ?};
      EDOKUMZ.del()
   !}
?};
EDOKRZAP.index('EDOKUM'); EDOKRZAP.prefix(EDOKUM.ref());
{? EDOKRZAP.first() || {! |? EDOKRZAP.del() !} ?};
POZF.cntx_psh();
POZF.use('pozf__'+(EDOKUM.name()+2));
POZF.index('EDOKUM'); POZF.prefix(EDOKUM.ref());
{? POZF.first() || {! |? POZF.del() !} ?};
POZF.cntx_pop();
exec('open_edk_atr','obiegi',EDOKUM.ref);
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref());
{? EDOK_ATR.first() || {! |? EDOK_ATR.del() !} ?};
EDOK_ATK.cntx_psh();
EDOK_ATK.use('edoknt'+(EDOKUM.name()+2));
EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOKUM.ref());
{? EDOK_ATK.first() || {! |? EDOK_ATK.del !} ?};
EDOK_ATK.cntx_pop();
EDOKPAR.cntx_psh();
EDOKPAR.use('skidh___');
EDOKPAR.index('UNIK'); EDOKPAR.prefix(EDOKUM.ref());
{? EDOKPAR.first() || {! |? EDOKPAR.del !} ?};
EDOKPAR.cntx_pop();
exec('bl_dokum_unlink','dokum_zal',$EDOKUM.ref());
{? EDOKUM.DOKUM<>''
|| DOKUM.cntx_psh(); DOKUM.prefix();
   _ref:=BB.sqlint(EDOKUM.DOKUM); _nam:=form(EDOKUM.DOKUM-8);
   {? _ref<>0 & _nam<>'' & DOKUM.seek(_ref,_nam)
   || DOKUM.EDOKUMR:=DOKUM.OBKONETP:=DOKUM.REFSQL:='';
      DOKUM.put()
   ?};
   DOKUM.cntx_pop()
?};
_co:='';
{? typobi=3
|| EDOKUMP.index('CHRONO'); EDOKUMP.prefix(EDOKUM.ref);
   {? EDOKUMP.first() || {! |? EDOKUMP.del() !} ?};
   EDOKUMD.index('DELEG'); EDOKUMD.prefix(EDOKUM.ref);
   {? EDOKUMD.first() || {! |? EDOKUMD.del() !} ?};
   EDOK_ZAL.index('WAL'); EDOK_ZAL.prefix(EDOKUM.ref);
   {? EDOK_ZAL.first()
   || {! |?
         EDOKUMZ.cntx_psh(); EVAT.cntx_psh();
         EDOKUM.cntx_psh(); EDOKUM.prefix();
         EDOKUMZ.index('WYDATEK'); EDOKUMZ.prefix(EDOK_ZAL.ref());
         EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM');
         {? EDOKUMZ.first()
         || {! |?
               _delr:=EDOKUMZ.del(,1);
               {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
            !}
         ?};
         EDOKUMZ.cntx_pop(); EVAT.cntx_pop(); EDOKUM.cntx_pop(); EDOKOS.cntx_pop();
         _delr:=EDOK_ZAL.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !}
   ?}
?};
{? EDOKUM.DOK_POW<>''
|| {? (5+(EDOKUM.DOK_POW+16))='zdnag'
   || ZD_NAG.cntx_psh();
      {? ZD_NAG.seek(EDOKUM.DOK_POW,ref_name(EDOKUM.DOK_POW),1,1)
      || ZD_NAG.OBI_POW:='';
         {? ~ZD_NAG.put() || undo() ?}
      ?};
      ZD_NAG.cntx_pop()
   |? (5+(EDOKUM.DOK_POW+16))='zknag'
   || ZK_N.cntx_psh();
      {? ZK_N.seek(EDOKUM.DOK_POW,ref_name(EDOKUM.DOK_POW),1,1)
      || ZK_N.OBI_POW:='';
         {? ~ZK_N.put() || undo() ?}
      ?};
      ZK_N.cntx_pop()
   |? 8+(EDOKUM.DOK_POW+16)='skan_dok'
   || SKAN_DOK.cntx_psh();
      SKAN_DOK.prefix();
      {? SKAN_DOK.seek(EDOKUM.DOK_POW)
      || SKAN_DOK.STATUS:='D';
         SKAN_DOK.put()
      ?};
      SKAN_DOK.cntx_pop()
   |? (8+(EDOKUM.DOK_POW+16)*'webdok')>0
   || WEB_DOK.cntx_psh();
      _names:=WEB_DOK.names();
      _fnd:=0;
      {? _names.first()
      || {!
         |? WEB_DOK.use(_names.NAME);
            WEB_DOK.prefix();
            {? WEB_DOK.seek(EDOKUM.DOK_POW,ref_name(EDOKUM.DOK_POW),1,1)
            || {? WEB_DOK.MODYF=1
               || WEB_DOK.STATUS:='zmodyfikowany'
               || WEB_DOK.STATUS:='dodany'
               ?};
               _fnd:=1;
               WEB_DOK.ARH:='N'; WEB_DOK.UTW_DOK:='';
               {? ~WEB_DOK.put() || undo() ?}
            ?};
            _fnd=0 & _names.next()
         !}
      ?};
      WEB_DOK.cntx_pop()
   || FAKS.cntx_psh(); FAKS.index('FAK_SYM1'); FAKS.prefix();
      {? FAKS.seek(EDOKUM.DOK_POW,ref_name(EDOKUM.DOK_POW),1,1)
      || FAKS.OBI_POW:='';
         {? ~FAKS.put() || undo() ?}
      ?};
      FAKS.cntx_pop()
   ?}
?};
WEB_DOK.cntx_psh(); WEB_DOK.index('UTW_DOK'); WEB_DOK.prefix(EDOKUM.uidref());
{? WEB_DOK.first() || WEB_DOK.prefix(); WEB_DOK.STATUS:='dodany'; WEB_DOK.ARH:='N'; WEB_DOK.UTW_DOK:=''; WEB_DOK.put() ?};
WEB_DOK.cntx_pop();
_typ:=EDOKUM.TYP; _nr:=EDOKUM.NR;
{? EDOKUM.del(,1)
|| exec('del_numer','obiegi',_nr,_typ)
|| undo()
?};
end();
{? ~EDOKUM.size() || SKID.SUMNETTO:=SKID.SUMVAT:=SKID.SUMBRUT:=0 ?};
{? var_pres('ile_us')>0 || ile_us+=1 ?}


\be_ed_nrksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.51_148]
:: OPIS: Przed redakcją pola EDOKUM.NRKSEF
::----------------------------------------------------------------------------------------------------------------------
1


\be_ed_nrksefk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.51_148]
:: OPIS: Przed redakcją pola EDOKUM.NRKSEFK
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.KOREKTA='T'


\licz_osoby
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Zwraca liczbę osób, którzy tak samo nazywają się.
::   WE: _a - imie
::       _b - nazwisko
::   WY: liczba osób tak samo nazywających się
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? PAR_SKID.get(460)='T'
|| exec('__F_ZATR','object');
   P.cntx_psh(); P.use('pracowni'); P.index('PRACONAZ'); P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,_b,_a,);
   H.cntx_psh(); H.use('_hist'); H.index('_HISTKOD');
   OSOBA.cntx_psh(); OSOBA.index('OSOBA'); OSOBA.prefix();
   {? P.first()
   || {!
      |? _ok:=0;
         H.prefix(P.ref(),'Z');
         {? H.first()
         || {!
            |? {? date()>=H.OD & (date()<=H.DO | H.DO=date(0,0,0))
               || _ok:=1;
                  _result+=1
               ?};
               ~_ok & H.next()
            !}
         ?};
         P.next()
      !}
   ?};
   OSOBA.cntx_pop(); P.cntx_pop(); H.cntx_pop()
|| OSOBA.cntx_psh(); OSOBA.index('OSOBA'); OSOBA.prefix(_b,_a,);
   _result:=OSOBA.size();
   OSOBA.cntx_pop()
?};
_result


\prac_firm_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Formuła przed redakcją pola OBIEGI.OSOBAOBI
::----------------------------------------------------------------------------------------------------------------------
__osobaOLD:=OBIEGI.OSOBAOBI;
1


\ob_close_ext
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Zamyka obieg wskazanego dokumentu w obiegu
::   WE: _a - EDOKUM.ref
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? _>0
|| EDOKUM.cntx_psh(); EDOKOS.cntx_psh(); EDOKLOG.cntx_psh();
   _mas:=(ref_name(_a)+2);
   {? EDOKUM.seek(_a,'skid_v'+_mas,1)
   || EDOKUM.ZAM:='T';
      {? EDOKUM.put()
      || EDOKOS.use('skid_y'+_mas); EDOKOS.index('EDOKUM');
         EDOKOS.prefix(EDOKUM.ref(),EDOKUM.B_PREL);
         {? ~EDOKOS.last() || EDOKOS.blank() ?};
         EDOKLOG.use('skid_d'+_mas); EDOKLOG.index('DISP');
         exec('add_elog','obiegi_akc','Obieg dokumentu został zamknięty',1);
         _result:=1
      ?}
   ?};
   EDOKUM.cntx_pop(); EDOKOS.cntx_pop(); EDOKLOG.cntx_pop()
?};
_result


\trig_usersf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Triger po add i put
::   WE: _a - typ operacji: 1-po add, 2-po put, 3-przed del
::       _b - status operacji
::----------------------------------------------------------------------------------------------------------------------
_tryb:=_a;
_ok:=_b;
{? _ok & (_tryb=1 | _tryb=3 | _tryb=2 & USERSF.PORTAL2<>bfld('PORTAL2')) & USERSF.FIRMA=REF.FIRMA
|| USERS.cntx_psh();
   {? USERSF.USERS().OSOBA
   || P.cntx_psh();
      P.index('OSOBA'); P.prefix(USERSF.USERS().OSOBA,USERSF.FIRMA);
      {? P.first()
      || ETYPY_P.cntx_psh();
         ETYPY_P.index('PRAC');
         {!
         |? ETYPY_P.prefix(P.ref());
            {? ETYPY_P.first()
            || {!
               |? ETYPY_P.put(,1);
                  ETYPY_P.next()
               !}
            ?};
            P.next()
         !};
         ETYPY_P.cntx_pop()
      ?};
      P.cntx_pop()
   || ETYPY_P.cntx_psh();
      ETYPY_P.index('USERS');
      ETYPY_P.prefix(USERSF.USERS);
      {? ETYPY_P.first()
      || {!
         |? ETYPY_P.put(,1);
            ETYPY_P.next()
         !}
      ?};
      ETYPY_P.cntx_pop()
   ?};
   USERS.cntx_pop()
?};
{? _tryb=3 || 1 || ~~ ?}


\wobg_upr_zakl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła zwraca informację 1/0 czy bieżący użytkownik ma uprawnienia do wyświetlenia zakładki
::       w obszarze wniosków w obiegu
::   WE: _a - zakładka ('rejestracja','akceptacja','dekretacja')
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? _a='rejestracja'
|| exec('hasAction','users','OBE_FAW_DARP')
|? _a='akceptacja'
|| exec('hasAction','users','OBE_FAW_EAKP')
|? _a='dekretacja'
|| exec('hasAction','users','FKS_DKS_DFWO')
|| 0
?}


\spr_podz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [$1210]
:: OPIS: Sprawdza wprowadzone podzialy dla dokumentu
::   WE: _a - czy pokazywac komunikaty
::  OLD: \spr_podz/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_akcept:=1;
{? PAR_SKID.get(80)='T'
|| EDOKOS.cntx_psh(); EDOKUM.cntx_psh(); OBIEGI.EDOKOS();
   EPODZ.cntx_psh(); EPODZ.use('skid_j'+(EDOKUM.name()+2));
   EDOKUMPR.cntx_psh(); EDOKUMPR.use('skid_i'+(EDOKUM.name()+2)); EDOKUMPR.index('DISP'); EDOKUMPR.prefix();
   {? EDOKUM.TYP().ED_PODZ='T' &
      exec('szuk_edokpar','obiegi') & EDOKPAR.ED_PODZ='W' &
      (typobi=1 | typobi=3 | (typobi=2 & EDOKUM.TYP().CZY_WAR>=2))
   || _akcept:=exec('spr_podz1','obiegi2',_a)
   ?};
   {? _akcept & exec('szuk_edokpar','obiegi') & EDOKPAR.KONT_BUD<>'N'
   || _kon:=exec('chk_budz','obe_budz',app_info('web_sesid')<>'',0);
      {? _kon
      || _akcept:=0;
         {? var_pres('__OBISPR')>0
         || exec('add_kom','obiegi',1,{? _kon=2 || 1 || 0 ?},'Przekroczono budżet'@,1)
         ?}
      ?}
   ?};
   EDOKOS.cntx_pop(); EDOKUM.cntx_pop(); EPODZ.cntx_pop(); EDOKUMPR.cntx_pop()
?};
_akcept


\spr_podz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [$1210]
:: OPIS: Sprawdza wprowadzone podzialy dla dokumentu
::   WE: _a - czy pokazywac komunikaty
::  OLD: \spr_podz1/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_akcept:=1;
EPODZ.cntx_psh(); EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
{? ~EPODZ.first() & EDOKUM.NETTO$2<>0
|| _kom:='Nie wprowadzono podziałów controllingowych w dokumencie.'@;
   exec('add_kom','obiegi',_a,1,_kom);
   _akcept:=0
?};
SKID_MBN.cntx_psh(); SKID_MBN.index('OFZ'); SKID_MBN.prefix('O','T');
{? _akcept & SKID_MBN.first()
|| {? typobi=3
   || {! |?
        _proc:=0;
        EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref(),SKID_MBN.ref());
        {? EPODZ.first()
        || {! |?
              _proc+=EPODZ.WART; EPODZ.next()
           !};
           {? _proc$2<>100
           || exec('add_kom','obiegi',_a,1,'Wartość sumaryczna podziałów nie równa się 100 dla modelu %1.'@[SKID_MBN.KOD]);
              _akcept:=0
           ?}
        ?};
        _akcept & SKID_MBN.next()
      !}
   || SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ');
      EPODZ.index('EDOKUM');
      {! |?
         _netpodz:=0;
         EPODZ.prefix(EDOKUM.ref(),SKID_MBN.ref());
         {? EPODZ.first()
         || {! |?
               {? EPODZ.WAL<>null & EPODZ.WAL=EDOKUM.WAL
               || SKIDXDUD.prefix(EPODZ.PBUD); SKIDXDUD.prefix(EPODZ.PBUD);
                  {? SKIDXDUD.first() & SKIDXDUD.WART_IL='W' || _netpodz+=EPODZ.WART ?}
               ?};
               EPODZ.next()
            !};
            {? _netpodz$2<>EDOKUM.NETTO$2
            || exec('add_kom','obiegi',_a,1,
                    'Wartość dokumentu nie zgadza się z wartością '
                    'wprowadzoną w podziałach controllingowych dla modelu %1.'@[SKID_MBN.KOD]
               );
               _akcept:=0
            ?}
         ?};
         _akcept & SKID_MBN.next()
      !};
      SKIDXDUD.cntx_pop()
   ?}
?};
EPODZ.cntx_pop(); SKID_MBN.cntx_pop();
_akcept


\b_prelGetDomain
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Zwraca symbol dziedziny na podstawie elementu procesu
::   WE: B_PREL.ref
::   WY: symbol dziedziny
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || return('') ?};
_result:='';
B_PREL.cntx_psh(); B_DOMAIN.cntx_psh();
B_PREL.prefix(); B_DOMAIN.prefix();
{? B_PREL.seek(_a)
|| _result:=B_PREL.B_DOMAIN().SYMBOL
?};
B_PREL.cntx_pop(); B_DOMAIN.cntx_pop();
_result


\wycofaj_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Dodatkowe sprawdzenie uprawnień podczas wycofywania, aby zadanie trafiło tylko do użytkownika, który je wycofał.
::       _a - _user
::       _b - _user_r
::       _c - _in
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_user:=_a;
_user_r:=_b;
_in:=_c;
EDOKUM.cntx_psh(); EDOKUM.prefix();
{? var_pres('EDOKUM',_in)>0 & _in.EDOKUM & EDOKUM.seek(_in.EDOKUM,ref_name(_in.EDOKUM),1) & EDOKUM.WYCOF_DO<>''
|| _wyc_user:=exec('find_last_usr','obiegi2',EDOKUM.ref);
   {? _wyc_user<>null()
   || _result:=(_user & _wyc_user=_user);
      {? ~_result & _user_r<>~~ || _result:=(_wyc_user=_user_r) ?}
   ?}
?};
EDOKUM.cntx_pop();
_result


\find_last_usr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Wyszukiwanie ostatniego użytkownika, który wykonał akcję dla czynności.
::   WE: _a - EDOKUM.ref
::   WY: USERS.ref
::----------------------------------------------------------------------------------------------------------------------
_user:=null();
EDOKLOG.cntx_psh();
{? EDOKLOG.name()+2<>EDOKUM.name()+2
|| EDOKLOG.use('skid_d'+(EDOKUM.name()+2))
?};
EDOKLOG.index('DISP'); EDOKLOG.prefix(_a);
{? EDOKLOG.last()
|| _user:=EDOKLOG.USERS
?};
EDOKLOG.cntx_pop();
_user


\ntc_ksef_auto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła wyświtlająca powiadomienie o automatycznej rejestracji faktury w obiegu z Businesslinka
::   WE: _a - EDOKUM.ref() rejestrowanej faktury
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOKUM.prefix();
{? exec('get','#params',700101,type_of('A'))<>'N' & EDOKUM.seek(_a,ref_name(_a)) & EDOKUM.NRKSEF<>''
|| _params:=exec('obj_ntab_set','#array',,'LINK',EDOKUM.uidref());
   _app_ident:=exec('app_ident','#ntc');
   _cluster_group:=app_info('cluster_group');
   _link_interm:=exec('link_uri','#system',_params,,_cluster_group,_app_ident);
   _bpr:=EDOKUM.B_PREL;
   _text:='<html>
           <b>Nowa faktura w obiegu</b><br><br>
           Symbol faktury: <b>%1</b><br>
           Zarejestrowano: <b>%2</b><br>
           Kontrahent: <b>%3</b><br>
           Waluta: <b>%6</b><br>
           Wartość netto: <b>%4</b><br>
           Wartość brutto: <b>%5</b>
           </html>'@[EDOKUM.SYM,$EDOKUM.DATAW,EDOKUM.KH().TR,
                     form(EDOKUM.NETTO,,2),form(EDOKUM.WART,,2),EDOKUM.WAL().KOD];

   B_PREL.cntx_psh(); B_PREL.prefix();
   {? B_PREL.find_tab(,'IDADD',,'=',_bpr)
   || _b_prel:=B_PREL.ref()
   || _b_prel:=null
   ?};
   B_PREL.cntx_pop();
   _users:=exec('users_role','dokum',_b_prel);
   _grpkod:='ksefauto'+EDOKUM.uidref();
   STR.split(_users,',');
   USERS.cntx_psh(); USERS.index('USR_KKOD');
   {! |?
      _user:=STR.get_word();
      {? USERS.find_key(_user) || _uref:=USERS.ref() || _uref:=null() ?};
      {? EDOKUM.ODD<>null || _odd_upr:=exec('usr_fjks','b_perm',EDOKUM.ODD,_uref) || _odd_upr:=1 ?};
      {? _uref<>null & exec('get','#params',7002,,_uref)='T' & _odd_upr
      || _formula:= {? exec('get','#params',7001,,_uref)='T'
                 || '{"url": %1}'[json_val(_link_interm)]
                 || '{"url": %1, "delete": %2}'[json_val(_link_interm), json_val(0)]
                 ?};
         _dni:=date()+exec('get','#params',7003,,_uref);
         ntc_send(_user,_app_ident,_cluster_group,'Powiadomienie o nowej fakturze w obiegu z KSeF'@,_text,_grpkod,,,
                  _dni,'Idź do faktury'@,_formula)
      ?};
      STR.next()
   !};
   USERS.cntx_pop()
|| 0
?};
EDOKUM.cntx_pop()


\edokum_del_trig_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Trigger po usunięciu
::----------------------------------------------------------------------------------------------------------------------
ntc_eat(,'ksefauto'+EDOKUM.uidref());
~~


\hist_akcept_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła grupa przed dla historii akceptacji
::----------------------------------------------------------------------------------------------------------------------
exec('hist_akcept','obiegi2','P');
0


\legenda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Legenda dla okienka WER_OBI tabeli USRROL
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','#B_USRROL#01'); 0


\hist_akcept1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Historia akceptacji faktur w obiegu - wewnętrzna
::   WE: _a - miejsce wywołania (R - okno wertowania wprowadzonych faktur/ P-wybór użytkownika lub okno przekazania)
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__edokum','__edokos','__win','__wsn');
_par:=#exec('get_par','#parametr',462);

_tmpval:=obj_new('KH','NAZ_KH','NDXED');
{? EDOKUM.get()
|| _tmpval.KH:=_kh:=EDOKUM.KHKH;
   _tmpval.NAZ_KH:=_naz_kh:=EDOKUM.KHKH().NAZ;
   _name:=(EDOKUM.name()+2);
   _odd:=EDOKUM.ODD;
   _ref:=EDOKUM.ref();
   _dw:=EDOKUM.DW
?};
_mas:=($(date()~1)+2);
_dataDo:=date()-_par;
_rokDo:=#($(_dataDo~1)+2);
_rokB:=#_mas;

{? exec('chk_role','#b__box',OPERATOR.USER,'OBG_FZO_ZBRK')
|| _user:=''
|| _user:=$OPERATOR.USER
?};

__edokum:=tab_tmp(1,
   'REF','STRING[16]','Ref kh',
   'DW','STRING[10]','Data rejestracji',
   'TP','STRING[10]','Termin płatności',
   'SYM','STRING[50]','Symbol',
   'TR','STRING[100]','Opis operacji',
   'WAL','STRING[8]','Waluta',
   'NETTO','REAL','Wartość netto',
   'BRUTTO','REAL','Wartość brutto',
   'EMPRELS','STRING[100]','Operacja',
   'USER','STRING[100]','Użytkownik'
);

__edokos:=tab_tmp(1,
   'REF','STRING[16]','Ref edokum',
   'DATA','STRING[10]','Data operacji',
   'TIME','STRING[10]','Czas operacji',
   'UW_KR','STRING[100]','Krótkie uwagi',
   'STATUS','STRING[100]','Status',
   'ESPRELS','STRING[100]','Operacja',
   'USER','STRING[50]','Użytkownik'
);
_tmpval.NDXED:=_ndxed:=EDOKUM.ndx_tmp(,,'ODD',,,'KHKH',,,'USERS',,);

{! _rok:= _rokDo .. _rokB |!

   EDOKUM.use('skid_v'+$_rok);
   EDOKOS.use('skid_y'+$(_rok-1));

   {? var_pres('_ed')>100 || &_ed ?};

   {? _a='R'
   ||
      _ed:=sql('
         select
            EDOKUM.REFERENCE as REF,
            EDOKUM.DW,
            EDOKUM.TP,
            EDOKUM.SYM,
            EDOKUM.B_PRELS,
            USERS.KOD as KOD
         from
            EDOKUM join
            TYPOBIEG using(EDOKUM.TYPOBIEG, TYPOBIEG.REFERENCE) join
            USERS using(EDOKUM.USERS,USERS.REFERENCE)
         where
           EDOKUM.KHKH=\':_a\' and TYPOBIEG.NAZWA=\'Obieg faktur\' and EDOKUM.ODD=\':_b\''+
         {? +_user || 'and EDOKUM.USERS=\':_c\'' || _user ?},
      $_kh,$_odd,_user)

   |? _a='P'
   ||
      _ed:=sql('
         select
            EDOKUM.REFERENCE as REF,
            EDOKUM.DW,
            EDOKUM.TP,
            EDOKUM.SYM,
            EDOKUM.B_PRELS,
            USERS.KOD as KOD
         from
            EDOKUM join
            TYPOBIEG using(EDOKUM.TYPOBIEG, TYPOBIEG.REFERENCE) left join
            EDOKOS using(EDOKOS.EDOKUM,EDOKUM.REFERENCE) left join
            USERS using(EDOKOS.USERS,USERS.REFERENCE)
         where
           EDOKUM.KHKH=\':_a\' and TYPOBIEG.NAZWA=\'Obieg faktur\' and EDOKUM.ODD=\':_b\''+
         {? +_user || 'and EDOKOS.USERS=\':_c\'' || '' ?},
      $_kh,$_odd,_user)
   ?};

   _loop:=_ed.first();
   {!
   |? _loop
   |!
      {? EDOKUM.seek(BIT.sqlint(_ed.REF))
      || {? EDOKUM.ref()<>_ref & EDOKUM.DW>=_dataDo & EDOKUM.DW<=_dw
         || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
            EDOKOS.index('EDOKUM');
            __edokum.REF:=$EDOKUM.ref();
            __edokum.DW:=$EDOKUM.DW;
            __edokum.TR:=EDOKUM.TR;
            __edokum.WAL:=EDOKUM.WAL().KOD;
            __edokum.NETTO:=EDOKUM.NETTO;
            __edokum.BRUTTO:=EDOKUM.WART;
            __edokum.TP:=$EDOKUM.TP;
            __edokum.SYM:=EDOKUM.SYM;
            __edokum.EMPRELS:=EDOKUM.B_PRELS;
            __edokum.USER:=EDOKUM.USERS().KOD;
            {? __edokum.add()
            || EDOKOS.prefix(BIT.sqlint(_ed.REF));
               _loop:=EDOKOS.first();
               {!
               |? _loop
               |! {? EDOKOS.OPERACJA='A'
                  || __edokos.REF:=$EDOKOS.EDOKUM;
                     __edokos.DATA:=$EDOKOS.DATA;
                     __edokos.TIME:=$EDOKOS.TIME;
                     __edokos.UW_KR:=EDOKOS.UW_KR;
                     __edokos.STATUS:=EDOKOS.STATUS;
                     __edokos.ESPRELS:=EDOKOS.B_PRELS;
                     __edokos.USER:=EDOKOS.USERS().KOD;
                     __edokos.add();
                     _loop:=EDOKOS.next()
                  || _loop:=EDOKOS.next()
                  ?}
               !}
            ?};
            _loop:=_ed.next()
         || _loop:=_ed.next()
         ?}
      || _loop:=_ed.next()
      ?}
   !}
!};
_tmpval


\bv_etypy_in_por
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Po wyświetleniem pola ETYPY.IN_POR
::----------------------------------------------------------------------------------------------------------------------
_red:=cur_win();
_enabled:=ETYPY.IN_POR;
ETYPY.efld_opt(_red,'enable='+$_enabled,ETYPY,'ID_WP');
ETYPY.efld_opt(_red,'enable='+$_enabled,ETYPY,'WID_WP');
_enabled:=ETYPY.IN_POR & ETYPY.WID_WP='T';
ETYPY.efld_opt(_red,'enable='+$_enabled,EDIT_VAR,'RODZ_WP');
ETYPY.efld_opt(_red,'enable='+$_enabled,ETYPY,'GRUPA_WP');

_mark:=ETYPY.IN_POR;
ETYPY.efld_opt(_red,'mark='+$_mark,ETYPY,'ID_WP');
1


\ae_etypy_in_por
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Po redakcji pola ETYPY.IN_POR
::----------------------------------------------------------------------------------------------------------------------
{? ~ETYPY.IN_POR
|| ETYPY.ID_WP:='';
   ETYPY.WID_WP:='N';
   ETYPY.GRUPA_WP:=''
?};
win_disp();
1


\REDW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca okno edycji typów wniosków w obiegu
::----------------------------------------------------------------------------------------------------------------------
_red:=ETYPY.mk_edit('Typy wniosków');
ETYPY.win_etab(_red,'Dane podstawowe');
ETYPY.win_ewin(_red,,'REDW1');
ETYPY.win_etab(_red,'Zaliczka');
ETYPY.win_ewin(_red,,'REDW2');
ETYPY.win_etab(_red,'Obsługa projektów');
ETYPY.win_ewin(_red,,'REDW3');
ETYPY.win_etab(_red,'Opis');
ETYPY.win_ewin(_red,,'REDW4');
ETYPY.win_etab(_red,'Finanse');
ETYPY.win_ewin(_red,,'REDW5');
{? exec('lic','#b_domain','POR')
|| ETYPY.win_etab(_red,'Portal');
   ETYPY.win_ewin(_red,,'REDW6')
?};
_zapisz:=ETYPY.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['Zapisz'@],"'key:F2'");
_anuluj:=ETYPY.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
ETYPY.btn_eopt(_red,_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
ETYPY.btn_eopt(_red,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
_red


\edokum_hist_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mas:=(4+__edokum.DW)+2;
EDOKUM.use('skid_v'+_mas);
EDOKUM.cntx_psh();
EDOKUM.prefix();
{? EDOKUM.seek(__edokum.REF)
|| {? EDOKUM.EDOKUM & 'pdf;bmp;png;tiff;tif;jpeg;jpg'*(-EDOKUM.bl_info('EDOKUM','EXTENSION'))>0
   || EDOKUM.win_edit('WPR_TEST');
      OBIEGI.PREVIEW:=EDOKUM.EDOKUM
   || EDOKUM.win_edit('WPR');
      OBIEGI.bl_file('PREVIEW',1,'')
   ?};
   EDOKUM.display()
?};
EDOKUM.cntx_pop()


\hist_akcept
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS: Historia akceptacji faktur w obiegu.
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
EDOKOS.cntx_psh();
_tmpval:=exec('hist_akcept1','obiegi2',_a);
_kh:=_tmpval.KH;
_naz_kh:=_tmpval.NAZ_KH;
_ndxed:=_tmpval.NDXED;
:: okienko wertowania z fakturami

_wnd:=__edokum.mk_sel('Faktura','P',0,'histaobg',,,,,'U','T');

__edokum.win_fld(_wnd,,'DW',,,-13,,,'Rejestrowano',,'Data rejestracji');
__edokum.win_fld(_wnd,,'USER',,,-13,,,'Rejestrował',,'Zarejestrował');
__edokum.win_fld(_wnd,,'SYM',,,-26,,,'Symbol',,'Symbol faktury');
__edokum.win_fld(_wnd,,'TR',,,-26,,,'Opis operacji',,'Opis operacji');
__edokum.win_fld(_wnd,,'WAL',,,-7,,,'Waluta',,'Waluta');
__edokum.win_fld(_wnd,,'NETTO',,,-14,2,,'Wartość netto',,'Wartość netto');
__edokum.win_fld(_wnd,,'BRUTTO',,,-14,2,,'Wartość brutto',,'Wartość brutto');
__edokum.win_fld(_wnd,,'TP',,,-19,,,'Termin płatności',,'Termin płatności');
__edokum.win_fld(_wnd,,'EMPRELS',,,-31,,,'Bieżący etap',,'Operacja');
__edokum.win_act(_wnd,0,'Wyświetl',,,,"exec('edokum_hist_view','obiegi2')");
__edokum.win_sel(_wnd);

:: okienko wertowania z osobami akceptującymi

__win:=__edokos.mk_sel('Użytkownicy akceptujący'@,'P',0,'os_ac',,,,,'U','T');

__edokos.win_fld(__win,,'DATA',,,-20,,,'Data',,'Data operacji');
__edokos.win_fld(__win,,'TIME',,,-20,,,'Godzina',,'Godzina operacji');
__edokos.win_fld(__win,,'STATUS',,,-27,,,'Wykonano',,'Operacja');
__edokos.win_fld(__win,,'UW_KR',,,-42,,,'Uwagi',,'Uwagi');
__edokos.win_fld(__win,,'USER',,,-27,,,'Użytkownik',,'Użytkownik');
__edokos.win_fld(__win,,'ESPRELS',,,-27,,,'Etap',,'Etap');
{? cur_tab()=B_USRROL
|| __edokos.win_act(__win,,'Formuła','&Zaznacz użytkowników',,'Zaznacz użytkowników',,
                    "sel_exit(); VAR_DEL.delete('czyzaznusrhist'); czyzaznusrhist:=__edokos.REF",1,1,
                    "VAR_DEL.delete('__tmphist'); __tmphist:=__edokos.sel_aget()");
__edokos.win_btn(__win,'text=&Zaznacz użytkowników,panel=bottom,align=end','menu:Z');
__edokos.win_btn(__win,'text=&Anuluj,panel=bottom,align=end','key:Esc')
?};
__edokos.win_sel(__win);

::okienko wertowania z użytkownikami sugerowanymi
{? cur_tab()=B_USRROL
|| __wsn:=exec('obiegi_sug_akc','tagger',_kh);
   __suggested.win_sel(__wsn)
?};
{? cur_tab()=B_USRROL
|| {? EDOKPAR.BLZMUSR='T'
   || __suggested.actions_grayed(__wsn,'Z');
      __edokos.actions_grayed(__win,'Z')
   || __suggested.actions_grayed(__wsn,'');
      __edokos.actions_grayed(__win,'')
   ?}
?};
:: okienko grupowe dla historii akceptacji

_grp:=__edokum.grp_make('Historia akceptacji '+_naz_kh,
:     po wyswietleniu
      "
         __sort:=__edokum.ndx_tmp(,,'DW',,1);
         __ndx:=__edokos.ndx_tmp(,,'REF',,);
         __edokum.index(__sort);
         __edokos.index(__ndx)

      ",
:     identyfikator
      'grp_histobg',
:     polozenie
      0,0,
:     zamkniecie
      "
         __edokum.ndx_drop(__sort);
         __edokos.ndx_drop(__ndx)
      ",
      "",
      'normal'
);
{? cur_tab()=B_USRROL
|| _afrfr:="grp_disp(__edokos,__win,1); grp_disp(__suggested,__wsn)"
|| _afrfr:="grp_disp(__edokos,__win,1)"
?};
__edokum.grp_sel(_grp,__edokum,_wnd,'Historia'@,
:     po odswiezeniu
      _afrfr,
:     polozenie i wysokosc
      ,,5,
:     przed obsluga
      "",
:     po obsludze
      "",
:     utrwalenie, aktywacja, wypelnienie
      0,0,'maximized_with_title'
);
__edokum.tab_splt(_grp,,'horizontal','tabs');
__edokum.grp_sel(_grp,__edokos,__win,,
:     po odswiezeniu
      "",
:     polozenie i wysokosc
      ,,5,
:     przed obsluga
      "
         __edokos.prefix(__edokum.REF)
      ",
:     po obsludze
      "",
:     utrwalenie, aktywacja, wypelnienie
      0,0,'maximized_with_title'
);
::__edokum.grp_splt(_grp,'tabs','horizontal','sug',17);
{? cur_tab()=B_USRROL
|| __edokum.grp_sel(_grp,__suggested,__wsn,'Sugerowani użytkownicy'@)
?};
__edokum.win_sel(_grp);
__edokum.select();

exec('usr_akc_zazn','tagger');
VAR_DEL.delete('czyzaznusr','czyzaznusrhist','__tmpsug','__tmphist');

EDOKOS.cntx_pop();
EDOKUM.cntx_pop();
EDOKUM.ndx_drop(_ndxed)


\be_dokvat_etypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [22.26_FWOB1]
:: OPIS: Przed redakcją ETYPYU.DOK_VAT
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.TYPVATPR=0
|| 1
|| 0
?}


\be_kursf_etypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [22.26_FWOB1]
:: OPIS: Przed redakcją ETYPYU.KURSF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.DOK_VAT='T'
|| 1
|| 0
?}


\f_set_rbk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JD [23.25]
:: OPIS: Ustawia filtr dla rachunków bankowych
::----------------------------------------------------------------------------------------------------------------------
_rvat:=0;
_sort:='N,TAB';
_kh:=exec('FindInSet','#table','KH','KOD1',EDOKUM.KH().KOD,,,1);
_where:={? RACHBANK.AKTYWNY<>'' || 'AKTYWNY=\':_a\' and (' || '(' ?};
{? EDOKUM.TYP().RODZ_DOK='S'
|| _where+=' "TAB"=\''+__Firma+'INFO\'';
   {? ~_rvat
   || {? _kh<>null & exec('ps_65','rachunki')='T'
      || _where+=' or ("TAB"=\''+REF.FIRMA().SYMBOL+'KH2\' and "REF"=\''+$#_kh+'\' ) '
      ?}
   ?};
   _where+=' or ("TAB"=\'KH\' and "REF" in ' +
              '(select reference_num(kh) from kh_dod '+
              'where faktor = \'T\' and firma = \''+$REF.FIRMA+'\'))'
|| _where+=' "TAB"=\'KH\' and "REF"=\''+$#_kh+'\''
?};
{? RACHBANK.FIRMA
|| _where+=') and FIRMA=:_b'
|| _where+=') and FIRMA is null'
?};
{? _rvat
|| _where+=' and "WAL"=\''+$INFO.NAROD+'\''
?};
SKID_RBK.index('TAB');
SKID_RBK.prefix();
SKID_RBK.f_set(_sort,,_where,RACHBANK.AKTYWNY,RACHBANK.FIRMA);
SKID_RBK.hdr_sel();
SKID_RBK.hdr_sel(exec('rbk_kh_tyt','rachunki',exec('FindAndGet','#table','KH',_kh,,"KH.SKR",null())));
~~


\edokume_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JD [23.25]
:: OPIS: Przed redakcją EDOKUM.RB
::----------------------------------------------------------------------------------------------------------------------
KH.cntx_psh();
_b:=EDOKUM.KHKH;
ROZNE.KKBAN:=1; PAR_WYDR.TABAKR:='KH'; PAR_WYDR.REF:=#_b;
RACHBANK.KB_5R_BD:='RACHBANK.KB_5R:=RB.get_rbtx(1,SKID_RBK.ref())';
SLO.cntx_psh(); SLU.cntx_psh();
_sel:='SLO_RBL'; {? EDOKUM.TYP().RODZ_DOK='Z' || _sel:='SLO_RBO' ?};
SKID_RBK.win_dict(_sel);
SKID_RBK.win_sel(_sel);
_red:='RED_RACH'; {? EDOKUM.TYP().RODZ_DOK='Z' || _red:='RED_RBO' ?};
SKID_RBK.win_edit(_red);
SKID_RBK.win_fml('WER_RBO',,'S_VAT',,'ICON_BEFORE',"exec('icona_svat','blp')");
SKID_RBK.win_fml('SLO_RBO',,'S_VAT',,'ICON_BEFORE',"exec('icona_svat','blp')");
SKID_RBK.win_patt('SZUK_RBO');
RACHBANK.AKTYWNY:='T';
SKID_RBK.actions(_sel,{? _sel='SLO_RBO' || 'T:T' ?});
SKID_RBK.win_patt('SZUK');
exec('f_set_rbk','obiegi2');
SLO.cntx_pop(); SLU.cntx_pop();
KH.cntx_pop();
1


\edokumr_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JD [23.25]
:: OPIS: Przed wyświetleniem EDOKUM.RB
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUM.win_edit('?')<>''
|| {? EDOKUM.KH().TR=''
   || EDOKUM.efld_opt(EDOKUM.win_edit('?'),'editable=grayed',EDOKUM,'RB','N')
   || EDOKUM.efld_opt(EDOKUM.win_edit('?'),'editable=1',EDOKUM,'RB','N')
   ?}
?};1


\chkrb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JD [23.25]
:: OPIS: Sprawdzenie czy rachunek bankowy pasuje do kontrahenta
::----------------------------------------------------------------------------------------------------------------------
PAR_WYDR.TABAKR:='KH';
_v:='';
_konto:=EDOKUM.RB().N;
{? _v='' & _konto<>''
   & ~((KH.index('NAZ'); KH.prefix(2, EDOKUM.KH().TR); _kh:=KH.first())
       & RB.getrrban(_konto,PAR_WYDR.TABAKR,#KH.ref(),null,1)<>0 & SKID_RBK.REF=#KH.ref())
|| {? _kh
   || RACHBANK.KB_3R:='';
      _v:='RB';
      {? PAR_WYDR.TABAKR=REF.FIRMA().SYMBOL+'KH2'
      || SKID_RBK.cntx_psh();
         SKID_RBK.index('TAB');
         SKID_RBK.prefix(RACHBANK.FIRMA,REF.FIRMA().SYMBOL+'KH2',REF.FIRMA().SYMBOL+'KH2',#KH.ref());
         _vs:=SKID_RBK.size();
         SKID_RBK.cntx_pop();
         {? _vs=0 & ~(1+_kvatsym='Z' | (6+_kvatsym)='_WWspN' )
         || _v:=''
         ?}
      || FUN.info('Niezgodność kontrahenta i numeru rachunku bankowego.'@)
      ?}
   ?}
?};
_v


\bobs_edokumz_wydatki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Przed obsluga EDOKUMZ dla wydatków delegacji
::----------------------------------------------------------------------------------------------------------------------
__CTR.BTAB:='EDOKUMZ';
__CTR.TAB:='EDOK_ZAL';
__CTR.BBLOB:='EDOKUM';
_fget:=EDOK_ZAL.get();
{? _fget & EDOKUM.ZAM<>'T'
||
   EDOKUMZ.index('WYDATEK'); EDOKUMZ.prefix(EDOK_ZAL.ref());
   __CTR.DND:=1;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   exec('initf','bloby',__CTR.WIN_ID)
||
   {? _fget=0
   || EDOKUMZ.index('NULL'); EDOKUMZ.prefix(null,null)
   ?};
   __CTR.DND:=0;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   _btab:=($(__CTR.BTAB))();
   {? _btab.size()
   ||
      ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarAction','setview','','xwin16.png:47','Widok',0,0);
      ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarSeparator');
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'view','P&OKAŻ','','Pokazanie dokumentu',1,1);
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'save','Zap&isz','','Kopiowanie dokumentu na wybraną lokalizację',2,0);
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'disp','&Właściwości','','Wyświetlenie większej ilości informacji',1,0)
   ?};
   ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar' )
?};
{? _
|| exec('getblobs','bloby',__CTR.WIN_ID,_)
?}


\etypatr_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Zwraca wartość domyślną dla pola DEFAULT w zależności, czy wielolinijkowy (pole notatnikowe) czy inne (DEFAULT)
::----------------------------------------------------------------------------------------------------------------------
{? var_press('DEF_MEMO',ETYP_ATR)>0
|| TAT.cntx_psh();
   _ret:={? ETYP_ATR.TAT().TYP='W' || ETYP_ATR.memo_txt(,1,'DEF_MEMO') || ETYP_ATR.DEFAULT ?};
   TAT.cntx_pop()
|| _ret:=ETYP_ATR.DEFAULT
?};
_ret


\edit_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Obsługa przycisku Edycja - edycja informacji dodatkowych w oknie redagowania wniosków w obiegu - jterm/webterm
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| VAR_DEL.delete('dynTab');
   dynTab:=web_def_get();
   _red:=web_top_win();
   web_def_btn_opt(_red,'state=grayed','EDIT');
   web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'editWni',1))
|| _obj:=objID;
   _tab:=_obj.getTab('REKORD');
   _red:=_tab.win_edit('?');
   _tab.btn_eopt(_red,_obj.btnEDIT,'state=grayed');
   VAR_DEL.delete('editWni');
   editWni:=1
?};
_wp:=ETYPY.W_PORTAL<>'N';
_seod:=-ETYPY.W_PORTAL='s';
ETYP_ATR.cntx_psh();
{? _wp
|| ETYP_ATR.index('SECWP')
|| ETYP_ATR.index('KOLZ')
?};
ETYP_ATR.prefix(EDOKUM.TYP,null);
{? ETYP_ATR.first()
|| {!
   |? {? ETYP_ATR.TAT & (~_wp | _seod | ETYP_ATR.VISIBLE)
      || _pole:='P'+$ETYP_ATR.KOL;
         _typ:=ETYP_ATR.TAT().TYP;
         {? _typ<>'O'
         || {? app_info('web_sesid')<>''
            || web_def_fld_opt(_red,'editable=1',_pole)
            || _tabela:=_obj.getTab(_pole);
               _tab.efld_opt(_red,'editable=1',_tabela,_pole);
               {? ETYP_ATR.EDIT<>'T'
               || _tabela.fld_fml(_pole,'BEFORE_EDIT',"")
               ?};
               &_tabela
            ?}
         ?}
      ?};
      ETYP_ATR.next()
   !}
?};
ETYP_ATR.cntx_pop();
1


\get_atr_wym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.30]
:: OPIS: Zwraca czy wymagane wypelnienie wartosci dla atrybutu podanej KOL typu ETYPY
::   WE: _a - ETYPY.ref() lub ZDARZT.ref() [biezacy]
::       _b - ETYP_ATR.KOL
::   WY: 1 - tak; 0 - nie
::  OLD: \get_atr_wym/deleg_at.fml
::----------------------------------------------------------------------------------------------------------------------
::   Uwaga PRC - formula wykorzystywana przez procedury wbudowane!
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.TYP_ID:='T';
{? (_=0) | (_a=~~) | (type_of(_a)<>type_of(null()))
   | (_a=null()) | ((ref_name(_a)<>ETYPY.name()) & (ref_name(_a)<>ZDARZT.name()))
|| {? OBIEGI.TYP_ID='K'
   || _a:=ZDARZT.ref()
   || _a:=ETYPY.ref()
   ?}
?};
{? (_<2) | (_b=~~) || _b:=0 ?};
{? (type_of(_b)<>type_of(0)) || &_b;_b:=0 ?};

_odp:=0;
{? _b>0
||
   ETYP_ATR.cntx_psh();
   ETYP_ATR.index('KOLZ');
   {? (ref_name(_a)=ZDARZT.name())
   || ETYP_ATR.prefix(null,_a,_b)
   || ETYP_ATR.prefix(_a,null,_b)
   ?};
   {? ETYP_ATR.first()
   ||
      _odp:=(ETYP_ATR.WYMAG='T')
   ?};
   ETYP_ATR.cntx_pop()
?};
_odp


\spr_inn_dan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Sprawdzanie informacji dodatkowych
::  OLD: \spr_inn_dan/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_wyp:=1;
_maska:=ref_name(EDOKUM.ref())+2;
EDOKUM.cntx_psh(); EDOKUM.use('skid_v'+_maska);
EDOKOS.cntx_psh(); OBIEGI.EDOKOS();
{? exec('szuk_edokpar','obiegi') & EDOKPAR.ED_INND='R'
|| EDOK_ATR.cntx_psh(); EDOKUM.cntx_psh();
   EDOK_ATR.use('edokat'+_maska);
   EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref());
   {? exec('czy_etp_atr','obiegi',EDOKUM.TYP)>0 & ~EDOK_ATR.first() || exec('edk_atr_dolacz','obiegi') ?};
   {? EDOK_ATR.first()
   || {!
      |? {? exec('get_atr_wym','obiegi2',EDOK_ATR.EDOKUM().TYP,EDOK_ATR.KOL) &
            (  (_typ:=EDOK_ATR.TAT().TYP)='W' & EDOK_ATR.memo_txt(,1,'WAR_W')='' |
               _typ<>'W' & EDOK_ATR.WAR='' |
               _typ='L' &
               exec('get_atr_cz','obiegi',EDOK_ATR.EDOKUM().TYP,EDOK_ATR.KOL)=0 &
               #STR.gsub(STR.gsub(EDOK_ATR.WAR,' ',''),',','.')=0
             )
         || _wyp:=0;
            exec('add_kom','obiegi',1,1,'Nie wypełniono wszystkich wymaganych "Informacji dodatkowych".'@)
         ?};
         _wyp & EDOK_ATR.next()
      !}
   ?};
   EDOK_ATR.cntx_pop(); EDOKUM.cntx_pop()
?};
EDOKOS.cntx_pop(); EDOKUM.cntx_pop();
_wyp




\clear_etypyp_users
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Czyszczenia uprawnień do wniosków
::   WE: _a - osoba przed zmianą
::       _b - osoba po zmianie
::----------------------------------------------------------------------------------------------------------------------
_przed:=_a;
_po:=_b;
{? _przed<>_po
|| {? _przed=null
   || ETYPY_P.cntx_psh();
      ETYPY_P.index('USERS');
      ETYPY_P.prefix(USERS.ref());
      {? ETYPY_P.first()
      || {!
         |? ETYPY_P.CNT-=1;
            ETYPY_P.put();
            ETYPY_P.next()
         !}
      ?};
      ETYPY_P.cntx_pop()
   || P.cntx_psh();
      P.index('OSOZATR');
      P.prefix(REF.FIRMA,_przed,'T');
      {? P.first()
      || {!
         |? ETYPY_P.cntx_psh();
            ETYPY_P.index('PRAC');
            ETYPY_P.prefix(P.ref());
            {? ETYPY_P.first()
            || {!
               |? ETYPY_P.CNT-=1;
                  ETYPY_P.put();
                  ETYPY_P.next()
               !}
            ?};
            ETYPY_P.cntx_pop();
            P.next()
         !}
      ?};
      P.cntx_pop()
   ?};
   exec('dnull_etyp_p','obiegi2')
?}

:Sign Version 2.0 jowisz:1045 2024/01/23 11:35:10 6dc70c4bfc9fa4e24799bad236b829347f990e7621679ae298043f0cb0c8f4cf2683014374ed39457c80e5d55c343c87ff595b16727c869b304676c8ea1ef467bb5dca7ef8f73b8eb5564bd3469d307daa7d00126b3f1d377d6c5aede26a4b194bc3c2ec40cf90f4bb200f3d8f8d857c50a4519d20ff2eef325aec7f0a8d4da9

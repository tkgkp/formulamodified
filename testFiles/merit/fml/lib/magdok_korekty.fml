:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: magdok_korekty.fml
:: Utworzony: 10.05.2017
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa korekt dokumentów magazynowych
::======================================================================================================================


\korekty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wyswietlanie korygowanych dokumentow magazynowych
::   WE: [_a] - 0(domyślnie)-wywołanie z obszaru 1-z czynności
::   WY: ''-dla _a=0 'akcja'-dla _a=1
::  OLD: \dok_kor2/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cm','ceny')=0 || FUN.info('Brak uprawnień.'@); return(0) ?};

HELP.OBSZAR:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_res:='';

exec('mag_psh','open_tab');
VAR_DEL.delete('__kor_set');
__kor_set:='KOR';

_ok:=1;
{? ST.MAG=null
|| FUN.info('Nie wybrano magazynu w stałych systemu.'@);
   _ok:=0
|| _typ:=(1+ST.MAG().TYP);
   _ewi:=ST.MAG().TYP*'EWI';
   _txt:={? _typ='E' || '<EWIDEN>'
         |? _typ='Ś' || '<ŚREDNIE>'
         |? ST.MAG().SKL='T'
          | _typ='D' || '<DOST%1>'[(ST.MAG().TYP+3)]
         || ''
         ?};
   {? (';EŚ'*_typ)>1 | _ewi
   || FUN.info('Funkcja niedostępna w magazynie typu %1.'@[_txt]);
      _ok:=0
   |? ST.MAG().SKL='T'
   || FUN.info('Funkcja dostępna tylko dla magazynu typu %1, bez opcji skład celny.'@[_txt]);
      _ok:=0
   ?}
?};

_kr_plus:=_kr_min:=null();
_kr_plus:=exec('zwrTypKor','magdok_korekty',1,ST.MAG().KOOP='T');
_kr_min:=exec('zwrTypKor','magdok_korekty',0,ST.MAG().KOOP='T');

{? _ok
|| _kr_plus:=_kr_min:=null();
   _kr_plus:=exec('zwrTypKor','magdok_korekty',1,ST.MAG().KOOP='T');
   _kr_min:=exec('zwrTypKor','magdok_korekty',0,ST.MAG().KOOP='T');
   {? _kr_plus=null() | _kr_min=null()
   || FUN.info('Brak kompletu typów dokumentów dla korekty.\nWymagane typy dokumentów magazynowych: KR+ i KR-.\n'@);
      _ok:=0
   || {? exec('FindInSet','#table','T2MG','T',ST.MAG)<>null()
       & (exec('FindInSet','#table','T2MG','T',_kr_plus,ST.MAG)=null()
        | exec('FindInSet','#table','T2MG','T',_kr_min,ST.MAG)=null())
      || FUN.info('W magazynie %1 aktualnie nie można wystawiać kompletu typów dokumentów dla korekty.\n'
                  'Wymagane przypisanie do magazynu typów dokumentów magazynowych: KR+ i KR-.\n'@[ST.MAG().SYM]);
         _ok:=0
      ?}

   ?}
?};

{? _ok
|| ATR.MJS:='DK';
   _dkred:=HELP.DKRED;

   HELP.WHERE:='M';
   HELP.DKRED:='N';
   ND.ndx_drop();

   ROKMC.AR:=ST.AR;
   ROKMC.AM:=ST.AM;
   ROKMC.KH:=null;
   ROKMC.KOR:='T';
   exec('kor_set','magdok_korekty');

   ND.win_sel('WER_KOR');
   ND.win_fml('WER_KOR',,'SYM',,'ICON_BEFORE',exec('nd_sym_ib','magdok_nag'));
   ND.win_fml('WER_KOR',,'NR',,'ICON_BEFORE',exec('nd_sym_it','magdok_nag'));
   ND.win_fml('WER_KOR',,'WAR',,'ICON_BEFORE',exec('nd_war_ib','magdok_nag'));

   exec('okno','magdok_korekty');

   __dkkpoz:=0;
   _refnd:=null();
   {? ND.select()
   || {? HELP.OBSZAR || _res:='Dołącz'+$ND.ref() ?}
   || {? HELP.OBSZAR || _res:='Anuluj' ?}
   ?};
   VAR_DEL.delete('__dkkpoz');

   ND.clear();

   HELP.DKRED:=_dkred;

:: powrot do aktywnej maski
   exec('open','open_tab',ST.ODDZ,2-$ST.AR);
   echo()
?};
exec('mag_pop','open_tab');
_res


\kor_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: ustawia zakres - wystawianie korekt
::  OLD: \kor_set/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
exec('open','open_tab',ST.ODDZ,2-$ROKMC.AR);
ND.clear();
ND.ndx_drop();
_ind:='ND.ndx_tmp(,,\'MAG\',\'SYM\',,\'TYP\',\'Z\',,\'TYP\',\'P\',,\'TYP\',\'DK\',,\'Z\',,,\'ZAK\',,,\'AM\',,,\'SYM\',,';
_prf:='\''+ST.MAG().SYM+'\',\'T\',\'T\',\'N\',\'T\',\'T\'';
_ind+=')';
ND.index(($_ind)());
($('ND.prefix('+_prf+')'))();
''


\okno
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: ustawia paramerty okna dla korekty
::  OLD: \okno/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('X_X_tmp_d','__dkk_ed');
X_X_tmp_d:=tab_tmp(3
   ,'D','DATE','Data'
   ,'SYM','STRING[20]','Symbol'
   ,'REF','STRING[16]','ref'
   ,'WAR','REAL','War'
   ,'ODDZ','STRING[1]','Oddział'
   ,'MAG','STRING[8]','Magazyn'
);

_poz_fml:=$($"
   ND.cntx_psh();
   DK.cntx_psh();
   ND.use(8+X_X_tmp_d.REF);
   DK.use('dokma'+((8+X_X_tmp_d.REF)+3));
   DK.index('DOKMAG');
   ND.clear();
   {? ND.seek(X_X_tmp_d.REF)
   ||
      DK.prefix(ND.ref());
      {? DK.first() || exec('dkr_okna','magdok_poz',,0) ?};
      DK.win_sel(__dkk_ed);
      exec('dk_p_ib','magdok_poz',__dkk_ed);
      __dkkpoz:=1;
      DK.select(,,,'L');
      __dkkpoz:=0
   ?};
   DK.cntx_pop();
   ND.cntx_pop()
");

_disp_fml:="
   ND.cntx_psh();
   ND.use(8+X_X_tmp_d.REF);
   ND.clear();
   {? ND.seek(X_X_tmp_d.REF) || ND.display() ?};
   ND.cntx_pop();
   ''
";

_acd:=X_X_tmp_d.mk_sel('Dokumenty korygujące'@,'P',0,'dk_pow_przy_kor',,,,,'U','T');
X_X_tmp_d.win_fld(_acd,,'ODDZ',,,7,,,'Oddział'@);
X_X_tmp_d.win_fld(_acd,,'MAG',,,8,,,'Magazyn'@);
X_X_tmp_d.win_fld(_acd,,'D',,,10,,,'Data'@);
X_X_tmp_d.win_fld(_acd,,'SYM',,,15,,,'Symbol dokumentu'@);
X_X_tmp_d.win_fld(_acd,,'WAR',,,15,2,,'Wartość korekty'@);
X_X_tmp_d.win_act(_acd,,'Formuła','&Pozycje'@@,,,,_poz_fml,1);
X_X_tmp_d.win_act(_acd,,'Szukaj');
X_X_tmp_d.win_act(_acd,,'Formuła','Legenda'@@,,,,"exec('legenda','color','NDK#01')");
X_X_tmp_d.win_sel(_acd);

_form:="
   _stat_rej:=exec('FindAndGet','#table',ND,X_X_tmp_d.REF,,\"STAT_REJ\",'N');
   {? _stat_rej='T' || exec('zaakceptowany','icon')
   |? _stat_rej='Z' || exec('zarejestrowany','icon')
   || exec('pusta','#icon')
   ?}
";

X_X_tmp_d.win_fml(_acd,,'SYM',,'ICON_BEFORE',_form);

:: dokumenty korygowane
_fml_rek:="
   POMOC.MGR:=DK.M().MGR;
   _date:=ND.D;
   {? __dkkpoz
   || DISP.C:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C,2,,_date,)
   || DISP.C:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C,2,,,0)
   ?};
   DISP.WAR:=(DISP.C*DK.IL)$2;
   {? (1+DK.N().MAG().TYP)='D' || exec('czytadkc','mat_atr',DK.DK_C,DK.RDKC) ?};
   exec('ost_cena','ceny_dok',DK.M,DK.N().KH);
   exec('rekprzed','color','DKK#01')
";

ND.win_edit('WREDPZ');
ND.win_patt('RED_SZUK');
ND.win_sel('WER_KOR');

__dkk_ed:=DK.mk_sel('Pozycje bieżącego dokumentu'@,'P',0,'dk_przy_kor',,,,,'U','T');
DK.win_fld(__dkk_ed,,'P',,,8,,,'Pozycja'@);
DK.win_fld(__dkk_ed,,'M','KTM',,30,,,'Indeks materiałowy'@);
DK.win_fld(__dkk_ed,,'M','N',,58,,,'Nazwa'@);
DK.win_fld(__dkk_ed,,'IL',,,15,ST.DOKL,,'Ilość'@);
DK.win_fld(__dkk_ed,,'C',,,15,ST.DOKL_C,,'Cena'@);
DK.win_fld(__dkk_ed,DISP,'C',,,15,ST.DOKL_C,,'Cena po korekcie'@);
DK.win_fld(__dkk_ed,,'WAR',,,15,2,,'Wartość'@);
DK.win_fld(__dkk_ed,DISP,'WAR',,,15,2,,'Wartość po korekcie'@);
DK.win_act(__dkk_ed,0,'Rekord',,,,_fml_rek);
DK.win_act(__dkk_ed,,'Szukaj');
DK.win_act(__dkk_ed,,'Kolejność');
DK.win_act(__dkk_ed,,'Formuła','Legenda'@@,,,,"exec('legenda','color','DKK#01')");
DK.win_sel(__dkk_ed);
exec('dk_p_ib','magdok_poz',__dkk_ed);

:: okno grupowe
_n_fml:=$($"
   HELP.RN:=null;
   DK.index('DOKMAG');
   {? ND.f_active() & ~ND.f_size()
   || HELP.RN:=null()
   |? {? ND.sel_size() || 1 || ND.size() ?}
   || HELP.RN:=ND.ref()
   ?};
   DK.prefix(HELP.RN);
   exec('pow_dkkr','magdok_korekty');
   DK.first();
   __dkkpoz:=DK.PLUS='T';
   grp_disp(DK,'"+__dkk_ed+$"');
   grp_disp(X_X_tmp_d,'"+_acd+$"')
");

_win_grp:=ND.grp_make('Korekty dokumentów'@,,'kor_dok_mag');
ND.grp_sel(_win_grp,ND,'WER_KOR',,_n_fml,,,18,_n_fml,,,,'maximized_with_title');
ND.grp_splt(_win_grp,'panel0','horizontal','naglowki');
ND.grp_sel(_win_grp,DK,__dkk_ed,,,,15,,,,,,'maximized_with_title');
ND.grp_splt(_win_grp,'panel0','vertical','korekty');
ND.grp_sel(_win_grp,X_X_tmp_d,_acd,,,,,,,,,,'maximized_with_title');
ND.win_sel(_win_grp);
{? HELP.OBSZAR || ND.actions('WER_KOR','U') || ND.actions('WER_KOR','') ?};
''


\pow_dkkr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: powiazane dokumenty korekt
::  OLD: \pow_dkkr/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
_x_x_rek:="
   DK.cntx_psh();
   ND.cntx_psh();
   DK.use('dokma'+((8+X_X_tmp_d.REF)+3));
   ND.use(8+X_X_tmp_d.REF);
   ND.clear();
   {? ND.seek(X_X_tmp_d.REF)
   ||
      DK.index('DOKMAG');
      DK.prefix(ND.ref());
      {? DK.first()
      ||
         exec('ustawprz','magdok_poz');
         _sum_kor:=0;
         {!
         |?
            _c_kor:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C,,,ND.D,1);
            _war_kor:=(DK.IL*_c_kor)$2;
            _sum_kor+=(_war_kor-DK.WAR);
            DK.next()
         !};
         X_X_tmp_d.WAR:=_sum_kor;
         X_X_tmp_d.put()
      ?}
   ?};
   ND.cntx_pop();
   DK.cntx_pop();
   ''
";
X_X_tmp_d.erase();
{? DK.first()
|| {!
   |? exec('sp_dkkor','magdok_korekty',DK.ref(),X_X_tmp_d);
      DK.next()
   !}
?};
_loop:=X_X_tmp_d.first();
{!
|? _loop
|!
   _x_x_rek();
   _loop:=X_X_tmp_d.next()
!};
X_X_tmp_d.first();
''


\sp_dkkor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: tworzy tabele z naglowkami korekt
::   WE:  _a  DK.ref
::       [_b] czy dodawac do podanej  tabeli (dodaje do 1 pola, ref do 2)
::   WY:       zwraca znacznik czy korygowano naglowek dokumentu
::  OLD: \sp_dkkor/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='N';

ND.cntx_psh();
DK.cntx_psh();
DK.use(ref_name(_a));
DK.prefix();
{? DK.seek(_a)
|| OKR.cntx_psh();
   _tabref:=exec('tabWithRef','magdok_korekty',$DK.ref());
   {? _tabref.first()
   || {!
      |? _ref_dk:=_tabref.REF;
         OKR.index('MC');
         OKR.prefix(REF.FIRMA,1);
         {? OKR.first()
         || {!
            |? ND.use('nagdo'+(1+((8+_ref_dk)+3))+($OKR.ROK+2));
               DK.use('dokma'+(1+((8+_ref_dk)+3))+($OKR.ROK+2));
               DK.index('KOR');
               DK.prefix(_ref_dk);
               {? DK.first()
               || _wyn:='T';
                  {? _>=2
                  || {? type_of(_b)>7
                     || {!
                        |? _b.blank();
                           {? {? _>=3
                              || ~_b.find_key(DK.N().D,DK.N().SYM,$DK.N,_c,_d,_e)
                              || ~_b.find_key(DK.N().D,DK.N().SYM,$DK.N)
                              ?}
                           || _b[1]:=DK.N().D;
                              _b[2]:=DK.N().SYM;
                              _b[3]:=$DK.N;
                              _b.ODDZ:=DK.N().MAG().ODDZ;
                              _b.MAG:=DK.N().MAG().SYM;
                              {? _>=3
                              || _b[4]:=_c;
                                 _b[5]:=_d;
                                 _b[6]:=_e
                              ?};
                              _b.add()
                           ?};
                           DK.next()
                        !}
                     ?}
                  ?}
               ?};
               OKR.next()
            !}
         ?};
         _tabref.next()
      !}
   ?};
   OKR.cntx_pop();
   obj_del(_tabref)
?};
DK.cntx_pop();
ND.cntx_pop();
_wyn


\korbdok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wystawianie korekt do dokumentow magazynowych
::   WE: _a - 0 - pierwsze uruchomienie (domyślnie), 1 - automatyczna korekta
::       _b - klucz grupujący, domyślnie tm_stamp()+'1'
::       [_c] - uruchomienie automatyczne 1-tak 0-nie(domyślnie)
::       [_d] - wskazanie na dostawę PRDK, domyślnie brak
::       [_e] - nowa cena, domyślnie 0
::       [_f] - data korekty, domyślnie data zerowa
::       [_g] - ref SQL ND do korekty
::       [_h] - FAP.ref() - pozycja korekty zakupu
::       [_i] - status
::   WY: ''-nie wystawiono, klucz grupujący
::  OLD: \korbdok/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
_autokor:={? var_pres('_a')=type_of(0)        || _a || 0 ?};
_grp_key:={? var_pres('_b')=type_of('')       || _b || tm_stamp()+'1' ?};

_autorun:={? var_pres('_c')=type_of(0)        || _c || 0 ?};
_dostprdk:={? var_pres('_d')=type_of('')      || _d || '' ?};
_newprice:={? var_pres('_e')=type_of(0)       || _e || 0 ?};
_kor_date:={? var_pres('_f')=type_of(date())  || _f || date(0,0,0) ?};
_nd_2_kor:={? var_pres('_g')=type_of('')      || _g || '' ?};
_fap:={? var_pres('_h')=type_of('')           || _h || '' ?};
_status:={? var_pres('_i')=type_of(obj_new('obj')) || _i || ~~ ?};

_res:='';
_msg:='';
VAR_DEL.delete('__tab_ed','__acr','__dok_p','__dok_r','__kr_plus','__kr_min','__plus','__k_mat','__k_cena','__k_dost'
   ,'__k_rdk','__k_ndk','__k_cecha','__k_prdk','__k_srdk','__k_zl','__k_wyd','__k_cwal','__k_krs','__k_scean'
   ,'__dok_kor','__dok_wal');

_intra:=obj_new('ND','DK');
_intra.ND:=obj_new(@.CLASS.BUFFER,'ND');
_intra.DK:=obj_new(@.CLASS.BUFFER,'DK');

{? ~_autokor
|| VAR_DEL.delete('__Dk2kor');
   __Dk2kor:=obj_new('TAB','WER','dataka');
   __Dk2kor.TAB:=~~;
   __Dk2kor.WER:=~~;
   __Dk2kor.dataka:=date(0,0,0)
?};

OKR.cntx_psh();
ND.cntx_psh();
DK.cntx_psh();

_refnd:={? _dostprdk<>'' || exec('FindAndGet','#table',DK,_dostprdk,,"N",null())
        |? _nd_2_kor<>'' || exec('FindAndGet','#table',ND,_nd_2_kor,,,null())
        || null()
        ?};
_msk:='';
_akt:=ND.name()+3;

_oki:=0;
{? ref_name(_refnd)<>''
|| _msk:=ref_name(_refnd)+3;
   exec('mag_open','open_tab',1+_msk,1-_msk);
   ND.prefix();
   {? ND.seek(_refnd) || _oki:=1 ?};
:: podano _refnd w procesie, trzeba go wynulować
   {? _dostprdk='' & _nd_2_kor<>'' || _refnd:=null() ?}
|| _oki:=1
?};
:: ustawianie typow dokumentow
__kr_plus:=__kr_min:=null;
__kr_plus:=exec('zwrTypKor','magdok_korekty',1,ST.MAG().KOOP='T');
__kr_min:=exec('zwrTypKor','magdok_korekty',0,ST.MAG().KOOP='T');

{? __kr_plus=null | __kr_min=null
|| _msg+='Brak kompletu typów dokumentów dla korekty.\nWymagane typy dokumentów magazynowych: KR+ i KR-.\n'@
?};

{? (ST.MAG().TYP*'EWI') | (1+ST.MAG().TYP='Ś')
|| _msg+='Funkcja niedostępna w magazynie typu \'%1\'.\n'@[ST.MAG().TYP]
?};

_msg+=exec('okr_otw','magdok_korekty');

__dok_r:=tab_tmp(2
   ,'P','STRING[2]','Plus'
   ,'KON','INTEGER','Konto'
   ,'REF','INTEGER','Ref n'
);

:: czy zaksiegowany filtrowane jest w \kor_set
{? ND.ZAK='N'
||
   _msg+='Dokument %1 jest niezaksięgowany.\n'@[exec('record','#to_string',ND.ref())];
   {? var_pres('_status')=type_of(obj_new('obj')) || _status.O:=_msg ?};
   _oki:=0
?};

{? _msg='' & _oki
||
   _intra.ND.save();
   __dok_kor:=ND.ref();
   __dok_wal:=ND.WAL;

   {? exec('blok_nd','magdok_nag','T')
   ||
      DK.index('DOKMAG');
      DK.prefix(ND.ref);
      {? DK.first()
      ||
         {? ~_autokor
         || __Dk2kor.TAB:=exec('dk2kor','magdok_korekty',ND.ref());
            __Dk2kor.WER:=exec('win','magdok_korekty',__Dk2kor)
         ?};

::       pozycje dokumentu oraz il do kor
         {? var_pres('__tab_ed')>100 || obj_del(__tab_ed) ?};
         __tab_ed:=tab_tmp(3
            ,'SRC_SYM','STRING[20]','Dok.źródłowy'
            ,'SRC_D','DATE','Data'
            ,'SRC_P','INTEGER','Poz.'
            ,'KTM','STRING[50]','Materiał'
            ,'N','STRING[100]','Nazwa'
            ,'IL','REAL','ilość'
            ,'C','REAL','Cena'
            ,'CP','REAL','Po korekcie'
            ,'WAL','STRING[5]','Waluta'
            ,'CWAL','REAL','Cena w walucie'
            ,'CWALP','REAL','Po korekcie'
            ,'KRS','REAL','Kurs'
            ,'RDK','INTEGER','ref dostawy'
            ,'NDK','STRING[8]','name dostawy'
            ,'REF','STRING[16]','ref pozycji'
            ,'X','STRING[1]','X'
            ,'DOKL_C','INTEGER','Dokładność ceny'
         );
         {? ~_autokor
         || _outprec:="'out_prec='+{? (2+cur_kwin())='e_' || $__tab_ed.DOKL_C || $ST.DOKL_C ?}";
            _in_prec:="'out_prec='+{? (2+cur_kwin())='e_' || $__tab_ed.DOKL_C || $ST.DOKL_C ?}";

            __tab_ed.fld_fml('C','DISPLAY_FORMAT',_outprec);
            __tab_ed.fld_fml('C','EDIT_FORMAT',_in_prec);
            __tab_ed.fld_fml('CP','DISPLAY_FORMAT',_outprec);
            __tab_ed.fld_fml('CP','EDIT_FORMAT',_in_prec);
            __tab_ed.fld_fml('CWAL','DISPLAY_FORMAT',_outprec);
            __tab_ed.fld_fml('CWAL','EDIT_FORMAT',_in_prec);
            __tab_ed.fld_fml('CWALP','DISPLAY_FORMAT',_outprec);
            __tab_ed.fld_fml('CWALP','EDIT_FORMAT',_in_prec);

            _win_ed:=__tab_ed.mk_edit('Podaj cenę po korekcie'@,0,'tab_ed_edi_wys');

            __tab_ed.win_esep(_win_ed,'Cena'@);
            {? __dok_wal<>INFO.NAROD
            ||
               __tab_ed.win_efld(_win_ed,,'WAL',,,5,,1);
               __tab_ed.win_efld(_win_ed,,'CWALP',,,15,__tab_ed.DOKL_C,,'Po korekcie'@);
               __tab_ed.efld_opt(_win_ed,'mark=1',,'CWALP')
            ||
               __tab_ed.win_efld(_win_ed,,'CP',,,15,__tab_ed.DOKL_C,,'Po korekcie'@);
               __tab_ed.efld_opt(_win_ed,'mark=1',,'CP')
            ?};
            exec('ok_esc','#window',__tab_ed,_win_ed);
            __tab_ed.win_edit(_win_ed);

            _fml_rek:="
               _akc:='A';
               __tab_ed.cntx_psh();
               {? __tab_ed.first()
               ||
                  {!
                  |?
                     {? __tab_ed.X='*' || _akc:='' ?};
                     __tab_ed.next() & _akc<>''
                  !}
               ?};
               __tab_ed.cntx_pop();

               __tab_ed.actions(__acr,_akc,,1);
               exec('rekprzed','color','DKK#01')
            ";

            __acr:=__tab_ed.mk_sel('Pozycje dokumentu magazynowego'@,'P',,'tab_ed_sel_wys',,,15,,'U','T');

            __tab_ed.win_fld(__acr,,'X',,,7,,,'Wybrano'@,,,2,,"\'*\'","\'\'","\'\'");
            __tab_ed.win_fld(__acr,,'SRC_SYM',,,20,,,'Dokument źródłowy'@);
            __tab_ed.win_fld(__acr,,'SRC_D',,,10,,,'Data'@);
            __tab_ed.win_fld(__acr,,'SRC_P',,,7,,,'Pozycja'@);
            __tab_ed.win_fld(__acr,,'KTM',,,20,,,'Indeks'@);
            __tab_ed.win_fld(__acr,,'N',,,12,,,'Nazwa'@);
            __tab_ed.win_fld(__acr,,'C',,,15,ST.DOKL_C,,'Cena przed korektą'@);
            __tab_ed.win_fld(__acr,,'CP',,,15,ST.DOKL_C,,'Cena po korekcie'@);
            __tab_ed.win_fld(__acr,,'WAL',,,6,,,'Waluta'@);
            {? __dok_wal<>INFO.NAROD
            ||
               __tab_ed.win_fld(__acr,,'CWAL',,,14,2);
               __tab_ed.win_fld(__acr,,'CWALP',,,11,2)
            ?};
            __tab_ed.win_act(__acr,0,'Formuła','Ko&ryguj'@@,,,,"exec('koryguj','magdok_korekty',1,0)",1,,,,'R');
            __tab_ed.win_btn(__acr,'text='+'Ko&ryguj'@+',btn_label_align=center,panel=right,align=begin','menu:R',,,,,,'noempty');
            _fb:="
               _msg:=exec('okr_otw','magdok_korekty');
               {? _msg<>''
               ||
                  FUN.info(_msg);
                  0
               ||
                  {? FUN.ask('Wystawić korektę?'@) || sel_exit ?}
               ?}

            ";
            __tab_ed.win_act(__acr,0,'Formuła','Akceptuj'@@,,,,_fb,0,,,,'A');
            __tab_ed.win_act(__acr,0,'Rekord',,,,_fml_rek);
            __tab_ed.win_act(__acr,0,'Szukaj');
            __tab_ed.win_act(__acr,0,'Kolejność');
            __tab_ed.win_act(__acr,0,'Formuła','Legenda'@@,,,,"exec('legenda','color','DKK#01')");
            __tab_ed.win_btn(__acr,'text='+'&Akceptuj'@+',btn_label_align=center,panel=bottom,align=begin','menu:A',,,,,,'noempty');
::          Okno grupowe
            _grp:=__tab_ed.grp_make('Pozycje dokumentu magazynowego'@,,'ahefivjqqopwjd');
            _far:="
               _Tab:=__Dk2kor.TAB;
               _wer:=__Dk2kor.WER;
               _Tab.prefix(exec('FindAndGet','#table',DK,__tab_ed.REF,,\"DK.PRDK\",''));
               _Tab.first();
               _Tab.win_sel(_wer);
               _Tab.hdr_sel();
               _Tab.hdr_sel(__tab_ed.SRC_SYM+' poz. '+$__tab_ed.SRC_P);
               grp_disp(_Tab,_wer)
               ";
            __tab_ed.grp_sel(_grp,,__acr,,_far,,,20,,,,,'maximized_with_title');
            __tab_ed.grp_splt(_grp,,'horizontal','dk2kor');
            __tab_ed.grp_sel(_grp,__Dk2kor.TAB,__Dk2kor.WER,,,,,15,,,,,'maximized_with_title');
            __tab_ed.win_sel(_grp)
         ?};
         __tab_ed.prefix();

::       sprawdza czy sa pozycje do korekty
         _do_kor:=0;

::       dla wybranego dokumentu do korekty
         {!
         |?
            {? {? _refnd<>null() || _dostprdk=$DK.ref() || _autokor=0 | (_autokor & __Dk2kor.TAB.REF=$DK.ref()) ?}
            || _rdk:=DK.RDK;
               _ndk:=DK.NDK;
               ND.cntx_psh();
               DK.cntx_psh();
               ND.use('nagdo'+(_ndk+3));
               ND.clear();
               DK.use(_ndk);
               DK.clear();
               {? DK.seek(_rdk,_ndk)
               ||
                  __tab_ed.SRC_SYM:=DK.N().SYM;
                  __tab_ed.SRC_D:=DK.N().D;
                  __tab_ed.SRC_P:=DK.P
               ?};
               DK.cntx_pop();
               ND.cntx_pop();

               __tab_ed.KTM:=DK.M().KTM;
               __tab_ed.N:=DK.M().N;
               __tab_ed.IL:=DK.IL;
               __tab_ed.C:=DK.C;
               __tab_ed.CP:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C);
               __tab_ed.WAL:=DK.N().WAL().KOD;
               __tab_ed.CWAL:=DK.CWAL;
               __tab_ed.CWALP:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.CWAL,,1);
               __tab_ed.KRS:=DK.N().KRS;
               __tab_ed.RDK:=DK.RDK;
               __tab_ed.NDK:=DK.NDK;
               __tab_ed.REF:=$DK.ref();
               __tab_ed.DOKL_C:=DK.M().DOKL_C;
               __tab_ed.add();

               _do_kor+=1
            ?};
            DK.next()
        !};
         __tab_ed.first();

         {? _do_kor>0
         || {? _autokor
            || dataka:=__Dk2kor.dataka;
               _define:=1
            |? _autorun & _refnd<>null() & _kor_date<>date(0,0,0)
            || dataka:=_kor_date;
               _define:=1
            ||
               WYDRUKIN.DATAOD:={? _kor_date<>date(0,0,0)
                                || _kor_date
                                |? date()~2<>ST.AM
                                || date(ST.AR,ST.AM,0)
                                || date()
                                ?};
               WYDRUKIN.win_edit('DATA');
               WYDRUKIN.hdr_edit('Korekta ceny'@);

               _define:=WYDRUKIN.edit("
                  {? WYDRUKIN.DATAOD~1<>ST.AR | WYDRUKIN.DATAOD~2<>ST.AM
                  ||
                     FUN.info('Data niezgodna z bieżącym okresem obrachunkowym.\nParametry systemu: %1.'@[ST.OKR]);
                     0
                  |? WYDRUKIN.DATAOD<ND.D
                  ||
                     _txt:='Data korekty nie może być wcześniejsza '@;
                     _txt+='niż data dokumentu korygowanego.\n'@;
                     _txt+='%1 wystawiono dnia %2.'@[ND.SYM,form(ND.D)];
                     FUN.info(_txt);
                     0
                  |? ~exec('ctrlENDmag','magdok_korekty',WYDRUKIN.DATAOD)
                  || 0
                  ||
                     1
                  ?}
               ");
               dataka:=__Dk2kor.dataka:=WYDRUKIN.DATAOD
            ?}
         ||
            FUN.info('Brak pozycji do korygowania.'@);
            _define:=0
         ?};

         {? _define=1
          & {? _autokor
            || __tab_ed.X:='*'; __tab_ed.CP:=__Dk2kor.TAB.CP; __tab_ed.put()
            |? _refnd<>null() & _newprice<>0
            || __tab_ed.X:='*'; __tab_ed.CP:=_newprice; __tab_ed.put()
            || __tab_ed.select()
            ?}

         ||
            do();

            _ndx:=__tab_ed.ndx_tmp(,,'X',,);
            __tab_ed.index(_ndx);
            __tab_ed.prefix('*');

::          ponowne sprawdzenie warunkow
            {? _msg='' & __tab_ed.first
            ||
               {!
               |? _msg:=exec('koryguj','magdok_korekty',0,_autokor);
                  {? _msg<>'' & var_pres('_status')=type_of(obj_new('obj')) || _status.O:=_msg ?};
                  _msg='' & __tab_ed.next
               !};
               {? _msg<>'' || undo ?}
            ?};

::          generowanie korekty
            {? _msg='' & __tab_ed.first()
            ||
               __dok_p:=null;
               {!
               |?
                  DK.clear();
                  {? DK.seek(__tab_ed.REF)
                  ||
::                   pola korygowanej pozycji
                     _intra.DK.save();
                     __k_mat:=DK.M;
                     __k_cena:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C);
                     __k_dost:=DK.DOST;
                     __k_rdk:=DK.RDK;
                     __k_ndk:=DK.NDK;
                     __k_cecha:=DK.DK_C().SYM;
                     __k_prdk:=DK.SRDK;
                     __k_srdk:=DK.PRDK;
                     __k_zl:=DK.ZL;
                     __k_wyd:=DK.WYD;
                     __k_cwal:=DK.CWAL;
                     __k_krs:=DK.KURS;
                     __k_scean:=DK.SCEAN;

::                   konto dla przychodow
                     _konto:=exec('ustalksg','magdok_korekty','Ma');
                     _kontop:=exec('ustalksg','magdok_korekty','Ma');

::                   dokumenty sa generowane w biezacej masce
                     exec('open','open_tab',ST.ODDZ,2-$ST.AR);

                     __dok_r.prefix('RN');
                     {? __dok_r.first()
                     ||
                        ND.clear();
                        ND.seek(__dok_r.REF,);
                        __dok_p:=ND.ref()
                     ||
                        __dok_p:=exec('addnag','magdok_nag',ST.MAG,ST.AR,ST.AM
                                  ,__kr_min,'N',0,dataka,,,,,,ND.O,,,-1);
                        exec('intra_nd','magdok_korekty',__dok_p,_intra);
                        __dok_r.blank();
                        __dok_r.P:='RN';
                        __dok_r.KON:=0;
                        __dok_r.REF:=#__dok_p;
                        __dok_r.add()
                     ?};

::                   rozchod calej dostawy po starej cenie
                     _dk_c:=exec('dk_c_ref','magdok_korekty',__k_cecha);
                     _dk:=exec('adddk','magdok_poz',__dok_p,__k_mat,__tab_ed.IL,__k_cena
                           ,__k_dost,,,,,,,,7,__k_rdk,__k_ndk,_dk_c,__k_srdk,__k_prdk);
                     exec('intra_dk','magdok_korekty',_dk,_intra);

::                   uzupelnia pola dla dodanej pozycji korekty
                     DK.clear();
                     {? DK.seek(_dk)
                     ||
::                        exec('aktualizacja_mp','schematy',__dok_kor,_dk,'D');
                        KK.clear();
                        {? KK.seek(_konto,)
                        || DK.KK:=KK.ref()
                        ?};
                        DK.REF_KOR:=__tab_ed.REF;
                        DK.WS2:=0;
                        DK.T2:='N';
                        DK.J2:=DK.M().J;
                        DK.IL2:=DK.IL;
                        DK.CWAL:=__k_cwal;
                        DK.KURS:=__k_krs;
                        DK.WAL:=__dok_wal;
                        DK.STATS:=exec('FindAndGet','#table',DK,DK.SRDK,,"STATS",null());
                        {? DK.M().SETW='P'
                        || DK.TW:=exec('FindAndGet','#table',DK,DK.SRDK,,"TW",DK.TW)
                        ?};
                        DK.SCEAN:=__k_scean;
                        DK.put()
                     ||
                        FUN.info('System nie odnalazł pozycji korekty dokumentu magazynowego.'@)
                     ?};

::                   powrot do maski dla wybranego zakresu
                     exec('open','open_tab',ST.ODDZ,2-$ROKMC.AR);

::                   petla po wszystkich pozycjach rozchodowych dla danej dostawy
                     OKR.index('MC');
                     OKR.prefix(REF.FIRMA,1);
                     {? OKR.first()
                     ||
                        {!
                        |?
                           exec('open','open_tab',ST.ODDZ,$OKR.ROK+2);

                           DK.index('DOST3');
                           DK.prefix(ST.MAG,__k_mat,__k_rdk,__k_ndk,'N');
                           {? DK.first()
                           ||
                              {!
                              |?
                                 {? 0 & DK.N().TYP().TD<>''
                                 ||
                                    _msg:='Nie można skorygować dokumentu\nze względu na utworzone przesunięcie %1.'@[DK.N().SYM];
                                    undo()
                                 ?};
                                 {? _msg='' & DK.N().Z='T' &
                                    2+ND.TYP().T<>'PR' & TYPYDOK.DK<>'T'
                                 ||
                                    _konto:=exec('ustalksg','magdok_korekty','Wn');
                                    _mat:=DK.M;
                                    _il:=DK.IL;
                                    _cen:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C);
                                    _dat:=DK.DOST;
                                    _rdk:=DK.RDK;
                                    _ndk:=DK.NDK;
                                    _cecha:=DK.DK_C().SYM;
                                    _prdk:=DK.PRDK;
                                    _srdk:=DK.SRDK;
                                    _zl:=DK.ZL;
                                    _wyd:=DK.WYD;
                                    _scean:=DK.SCEAN;

                                    _dok_w:=DK.N;

                                    ND.cntx_psh();
                                    DK.cntx_psh();

::                                  przyjecie wczesniej wydanych pozycji - po starej cenie
::                                  aktualizacja kosztu wlasnego

::                                  dokumenty sa generowane w biezacej masce
                                    exec('open','open_tab',ST.ODDZ,2-$ST.AR);

                                    __dok_r.prefix('PN');
                                    {? __dok_r.first()
                                    ||
                                       ND.clear();
                                       ND.seek(__dok_r.REF,);
                                       __dok_p:=ND.ref()
                                    ||
                                       __dok_p:=exec('addnag','magdok_nag',ST.MAG,ST.AR,ST.AM,__kr_plus
                                                 ,'N',0,dataka,,,,,,ND.O,,,-1);
                                       exec('intra_nd','magdok_korekty',__dok_p,_intra);
                                       __dok_r.blank();
                                       __dok_r.P:='PN';
                                       __dok_r.KON:=0;
                                       __dok_r.REF:=#__dok_p;
                                       __dok_r.add()
                                    ?};

::                                  generowanie przychodu po starej cenie
                                    _dk_c:=exec('dk_c_ref','magdok_korekty',_cecha);
                                    _dk:=exec('adddk','magdok_poz',__dok_p,_mat,_il,_cen,_dat,,,,,,,,7,_rdk,_ndk
                                          ,_dk_c,_srdk,_prdk,,_zl,_wyd);
                                    exec('intra_dk','magdok_korekty',_dk,_intra);

                                    DK.clear();
                                    {? DK.seek(_dk)
                                    ||
::                                       exec('aktualizacja_mp','schematy',_dok_w,_dk,'D');
                                       KK.clear();
                                       {? KK.seek(_konto,)
                                       || DK.KK:=KK.ref()
                                       ?};
                                       DK.REF_KOR:=__tab_ed.REF;
                                       DK.RDK:=_rdk;
                                       DK.NDK:=_ndk;
                                       DK.WS2:=0;
                                       DK.T2:='N';
                                       DK.J2:=DK.M().J;
                                       DK.IL2:=DK.IL;
                                       DK.CWAL:=__tab_ed.CWAL;
                                       DK.KURS:=__tab_ed.KRS;
                                       DK.WAL:=__dok_wal;
                                       DK.STATS:=exec('FindAndGet','#table',DK,DK.SRDK,,"STATS",null());
                                       {? DK.M().SETW='P'
                                       || DK.TW:=exec('FindAndGet','#table',DK,DK.SRDK,,"TW",DK.TW)
                                       ?};
                                       DK.SCEAN:=_scean;
                                       DK.put()
                                    ?};

::                                  generowanie rozchodu po nowej cenie
::                                  aktualizacja kosztu wlasnego
                                    __dok_r.prefix('RC');
                                    {? __dok_r.first()
                                    ||
                                       ND.clear();
                                       ND.seek(__dok_r.REF,);
                                       __dok_p:=ND.ref()
                                    ||
                                       KK.clear();
                                       KK.seek(_konto,);
                                       __dok_p:=exec('addnag','magdok_nag',ST.MAG,ST.AR,ST.AM
                                                 ,__kr_min,'N',0,dataka,,,,,,ND.O,,,-1);
                                       exec('intra_nd','magdok_korekty',__dok_p,_intra);
                                       __dok_r.blank();
                                       __dok_r.P:='RC';
                                       __dok_r.KON:=0;
                                       __dok_r.REF:=#__dok_p;
                                       __dok_r.add()
                                    ?};

::                                  generowanie rozchodu po nowej cenie
                                    _dk_c:=exec('dk_c_ref','magdok_korekty',_cecha);
                                    _dk:=exec('adddk','magdok_poz',__dok_p,_mat,_il,__tab_ed.CP,_dat,,,,,,,,7,_rdk,_ndk
                                          ,_dk_c,_srdk,_prdk,,_zl,_wyd);
                                    exec('intra_dk','magdok_korekty',_dk,_intra);

                                    DK.clear();
                                    {? DK.seek(_dk)
                                    ||
::                                       exec('aktualizacja_mp','schematy',_dok_w,_dk,'D');
                                       KK.clear();
                                       {? KK.seek(_konto,)
                                       || DK.KK:=KK.ref()
                                       ?};
                                       DK.POW:='C';
                                       DK.REF_KOR:=__tab_ed.REF;
                                       DK.WS2:=0;
                                       DK.T2:='N';
                                       DK.J2:=DK.M().J;
                                       DK.IL2:=DK.IL;
                                       DK.CWAL:=__tab_ed.CWALP;
                                       DK.KURS:=__tab_ed.KRS;
                                       DK.WAL:=__dok_wal;
                                       DK.STATS:=exec('FindAndGet','#table',DK,DK.SRDK,,"STATS",null());
                                       {? DK.M().SETW='P'
                                       || DK.TW:=exec('FindAndGet','#table',DK,DK.SRDK,,"TW",DK.TW)
                                       ?};
                                       DK.SCEAN:=_scean;
                                       DK.put()
                                    ?};

::                                  powrot do maski dla wybranego zakresu
                                    exec('open','open_tab',ST.ODDZ,2-$ROKMC.AR);

                                    DK.cntx_pop();
                                    ND.cntx_pop()
                                 ?};
                                 _msg='' & DK.next()
                              !}
                           ?};
                           OKR.next()
                        !}
                     ?};

::                   generowanie przychodu po nowej cenie
::                   aktualizacja wartosci magazynu
                     {? _msg=''
                     ||
::                      dokumenty sa generowane w biezacej masce
                        exec('open','open_tab',ST.ODDZ,2-$ST.AR);

                        __dok_r.prefix('PC');
                        {? __dok_r.first()
                        ||
                           ND.clear();
                           ND.seek(__dok_r.REF,);
                           __dok_p:=ND.ref()
                        ||
                           __dok_p:=exec('addnag','magdok_nag',ST.MAG,ST.AR,ST.AM
                                     ,__kr_plus,'N',0,dataka,,,,,,ND.O,,,-1);
                           exec('intra_nd','magdok_korekty',__dok_p,_intra);
                           __dok_r.blank();
                           __dok_r.P:='PC';
                           __dok_r.KON:=0;
                           __dok_r.REF:=#__dok_p;
                           __dok_r.add()
                        ?};

::                      generowanie przychodu po nowej cenie
                        _dk_c:=exec('dk_c_ref','magdok_korekty',__k_cecha);
                        _dk:=exec('adddk','magdok_poz',__dok_p,__k_mat,__tab_ed.IL,__tab_ed.CP,__k_dost,,,,,,,,7
                              ,__k_rdk,__k_ndk,_dk_c,__k_srdk,__k_prdk);
                        exec('intra_dk','magdok_korekty',_dk,_intra);
::                        exec('aktualizacja_mp','schematy',__dok_kor,_dk,'D');

                        DK.clear();
                        {? DK.seek(_dk)
                        ||
                           KK.clear();
                           {? KK.seek(_kontop,)
                           || DK.KK:=KK.ref()
                           ?};
::                         oznaczenie poz dok przechowujacej cene
                           DK.POW:='C';
                           DK.REF_KOR:=__tab_ed.REF;
                           DK.RDK:=__k_rdk;
                           DK.NDK:=__k_ndk;
                           DK.WS2:=0;
                           DK.T2:='N';
                           DK.J2:=DK.M().J;
                           DK.IL2:=DK.IL;
                           DK.CWAL:=__tab_ed.CWALP;
                           DK.KURS:=__tab_ed.KRS;
                           DK.WAL:=__dok_wal;
                           DK.STATS:=exec('FindAndGet','#table',DK,DK.SRDK,,"STATS",null());
                           {? DK.M().SETW='P'
                           || DK.TW:=exec('FindAndGet','#table',DK,DK.SRDK,,"TW",DK.TW)
                           ?};
                           DK.SCEAN:=__k_scean;
                           DK.put()
                        ?};

::                      powrot do maski dla wybranego zakresu
                        exec('open','open_tab',ST.ODDZ,2-$ROKMC.AR);

::                      oblicza ilosc wszystkich dok. przychodowych
                        __plus:=0;
                        ND.cntx_psh();
                        DK.cntx_psh();
                        OKR.index('MC');
                        OKR.prefix(REF.FIRMA,1);
                        {? OKR.first()
                        ||
                           {!
                           |?
                              DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
                              ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));

                              DK.index('DOST3');
                              DK.prefix(ST.MAG,__k_mat,__k_rdk,__k_ndk,'T');
                              {? DK.first()
                              ||
                                 {!
                                 |?
                                    {? DK.N().Z='T' & ND.TYP().DK<>'T'
                                    || __plus+=DK.IL
                                    ?};
                                    DK.next()
                                 !}
                              ?};

                              OKR.next()
                           !}
                        ?};
                        DK.cntx_pop();
                        ND.cntx_pop();

::                      rozchod zwrotow
::                      generowanie rozchodu po starej cenie
::                      aktualizacja wartosci magazynu
                        {? __plus-__tab_ed.IL>0
                        ||
::                         dokumenty sa generowane w biezacej masce
                           exec('open','open_tab',ST.ODDZ,2-$ST.AR);

                           __dok_r.prefix('PC');
                           {? __dok_r.first()
                           ||
                              ND.clear();
                              ND.seek(__dok_r.REF,);
                              __dok_p:=ND.ref()
                           ||
                              KK.clear();
                              KK.seek(_konto,);
                              __dok_p:=exec('addnag','magdok_nag',ST.MAG,ST.AR,ST.AM
                                        ,__kr_min,'N',0,dataka,,,,,,ND.O,,,-1);
                              exec('intra_nd','magdok_korekty',__dok_p,_intra);
                              __dok_r.blank();
                              __dok_r.P:='RC';
                              __dok_r.KON:=0;
                              __dok_r.REF:=#__dok_p;
                              __dok_r.add()
                           ?};

                           _dk_c:=exec('dk_c_ref','magdok_korekty',__k_cecha);
                           _dk:=exec('adddk','magdok_poz',__dok_p,__k_mat,__plus-__tab_ed.IL,__tab_ed.CP
                                 ,__k_dost,,,,,,,,7,__k_rdk,__k_ndk,_dk_c,__k_srdk,__k_prdk);
                           exec('intra_dk','magdok_korekty',_dk,_intra);

                           DK.clear();
                           {? DK.seek(_dk)
                           ||
::                              {? var_pres('_dok_w')=type_of(null)
::                              || exec('aktualizacja_mp','schematy',_dok_w,_dk,'D')
::                              ?};
                              KK.clear();
                              {? KK.seek(_konto,)
                              || DK.KK:=KK.ref()
                              ?};
                              DK.POW:='C';
                              DK.REF_KOR:=__tab_ed.REF;
                              DK.WS2:=0;
                              DK.T2:='N';
                              DK.J2:=DK.M().J;
                              DK.IL2:=DK.IL;
                              DK.CWAL:=__tab_ed.CWAL;
                              DK.KURS:=__tab_ed.KRS;
                              DK.WAL:=__dok_wal;
                              DK.STATS:=exec('FindAndGet','#table',DK,DK.SRDK,,"STATS",null());
                              {? DK.M().SETW='P'
                              || DK.TW:=exec('FindAndGet','#table',DK,DK.SRDK,,"TW",DK.TW)
                              ?};
                              DK.SCEAN:=__k_scean;
                              DK.put()
                           ?};

                           __dok_r.prefix('RN');
                           {? __dok_r.first()
                           ||
                              ND.clear();
                              ND.seek(__dok_r.REF,);
                              __dok_p:=ND.ref()
                           ||
                              __dok_p:=exec('addnag','magdok_nag',ST.MAG,ST.AR,ST.AM
                                        ,__kr_min,'N',0,dataka,,,,,,ND.O,,,-1);
                              exec('intra_nd','magdok_korekty',__dok_p,_intra);
                              __dok_r.blank();
                              __dok_r.P:='RN';
                              __dok_r.KON:=0;
                              __dok_r.REF:=#__dok_p;
                              __dok_r.add()
                           ?};

::                         po starej cenie
                           _dk_c:=exec('dk_c_ref','magdok_korekty',__k_cecha);
                           _dk:=exec('adddk','magdok_poz',__dok_p,__k_mat,__plus-__tab_ed.IL,__k_cena
                                 ,__k_dost,,,,,,,,7,__k_rdk,__k_ndk,_dk_c,__k_srdk,__k_prdk,,__k_zl,__k_wyd);
                           exec('intra_dk','magdok_korekty',_dk,_intra);
::                           exec('aktualizacja_mp','schematy',__dok_kor,_dk,'D');

                           DK.clear();
                           {? DK.seek(_dk)
                           ||
                              KK.clear();
                              {? KK.seek(_konto,)
                              || DK.KK:=KK.ref()
                              ?};
                              DK.REF_KOR:=__tab_ed.REF;
                              DK.WS2:=0;
                              DK.T2:='N';
                              DK.J2:=DK.M().J;
                              DK.IL2:=DK.IL;
                              DK.CWAL:=__tab_ed.CWAL;
                              DK.KURS:=__tab_ed.KRS;
                              DK.WAL:=__dok_wal;
                              DK.STATS:=exec('FindAndGet','#table',DK,DK.SRDK,,"STATS",null());
                              {? DK.M().SETW='P'
                              || DK.TW:=exec('FindAndGet','#table',DK,DK.SRDK,,"TW",DK.TW)
                              ?};
                              DK.SCEAN:=__k_scean;
                              DK.put()
                           ?};

::                         powrot do maski dla wybranego zakresu
                           exec('open','open_tab',ST.ODDZ,2-$ROKMC.AR)
                        ?}
                     ?}
                  ?};
                  _msg='' & __tab_ed.next()
               !}
            ||
               {? _msg=''
               ||
                  _msg:='Nie zaznaczono żadnej pozycji do korygowania.'@;
                  undo()
               ?}
            ?};

            {? end() & _msg='' || _msg:='OK' ?};

            ND.cntx_psh();
            DK.cntx_psh();
            ND.use(ref_name(__dok_kor));
            DK.use('dokma'+(ref_name(__dok_kor)+3));
            ND.clear();
            {? ND.seek(__dok_kor)
            ||
               ND.KOR:={? exec('czy_kor','magdok_wspolne',$__dok_kor) || 'T' || 'N' ?};
               ND.put()
            ?};
            ND.cntx_pop();
            DK.cntx_pop()
         ||
            _msg:='ESC __tab_ed.select'
         ?}
      ||
        _msg:='ESC define'
      ?}
   ?};

   {? 3+_msg='ESC'
   || _msg:=''
   |? 2+_msg='OK'
   || _msg:='Utworzono korektę.\n'@
   ?}
|? _msg<>''
|| FUN.info(_msg)
?};

unlock_r();

:: wyznaczenie numerow utworzonych dokumentow korygujacych
__dok_r.clear();
{? _msg='Utworzono korektę.\n' & __dok_r.first
||
   exec('open','open_tab',ST.ODDZ,2-$ST.AR);
   {!
   |?
      ND.clear();
      {? ND.seek(__dok_r.REF,)
      ||
         {? ND.r_lock(1,1,1)
         ||
            POM.TAB:='ND';
            POM.TYPDOK:={? ND.TYP<>null || ND.TYP().KOD || '' ?};
            POM.NRT:={? ND.TYP<>null || ND.TYP().NRT || 0 ?};
            ND.NR:=exec('numer_new','numery','PACZKA');
            exec('znak','numery','ND',1);
            ND.GRP_KEY:=_grp_key;
            _faks:=exec('FindAndGet','#table',FAP,_fap,,"$FAP.FAKS",'');
            _kz:=exec('FindAndGet','#table',FAKS,_faks,,"FAKS.KZ",'');
            ND.FAKS_REF:={? +_kz || _kz || _faks ?};
            ND.FAKS:=exec('FindAndGet','#table',FAKS,ND.FAKS_REF,,,null());
            {? ~ND.put
            ||
               numer:=ND.NR;
               oldnumer:=1;
               exec('nr_old','numery')
            || exec('dk_sum','magdok_wspolne',ND.ref());
               _res:=_grp_key;
::             powiązanie pozycji przez FAP2DK
               exec('fap2dk','magdok_korekty',_faks,_fap,ND.ref())
            ?};
            ND.r_unlock
         ?}
      ?};
      __dok_r.next
   !}
?};
{? _msk<>'' || exec('mag_open','open_tab',1+_akt,1-_akt) ?};

DK.cntx_pop();
ND.cntx_pop();
OKR.cntx_pop();

VAR_DEL.delete('__tab_ed','__acr','__dok_p','__dok_r','__kr_plus','__kr_min','__plus','__k_mat','__k_cena','__k_dost'
   ,'__k_rdk','__k_ndk','__k_cecha','__k_prdk','__k_srdk','__k_zl','__k_wyd','__k_cwal','__k_krs'
   ,'__dok_kor','__dok_wal');

{? _msg='Utworzono korektę.\n'@ & ~_autokor & var_pres('TAB',__Dk2kor)<>type_of(~~)
|| VAR_DEL.delete('__grpkey');
   __grpkey:=_grp_key;
   _Tab:=__Dk2kor.TAB;
   _st_mag:=ST.MAG;
   _Tab.index(_Tab.ndx_tmp(,,'X',,,'MG_SYM',,));
   _Tab.prefix('*');
   _loop:=_Tab.first();
   _msk:=DK.name();
   {!
   |? _loop
   |!
      ST.MAG:=exec('FindAndGet','#table',MG,_Tab.MG_REF,,,null());
      {? ST.MAG
      || exec('FindAndGet','#table',DK,_Tab.REF,,"
            ND.cntx_psh();
            ND.prefix();
            {? DK.N
            || DK.N();
               exec('korbdok','magdok_korekty',1,__grpkey)
            ?};
            ND.cntx_pop()
            ")
      ?};
      _loop:=_Tab.next()
   !};
   VAR_DEL.delete('__Dk2kor','__grpkey');
   ST.MAG:=_st_mag;
   {? _msg<>'' & ~_autorun || FUN.info(_msg) ?}
?};

_res


\okr_otw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: sprawdza czy otwarty okres obrachunkowy
::   WY: informacja o zamknietym okresie
::  OLD: \okr_otw/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
OKR.cntx_psh; OKRD.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,ST.AM,ST.AR);
{? OKR.first
||
   {? OKR.ZAM='TTTT'
   ||
      _wyn+='Funkcja niedostępna. Okres obrachunkowy zamknięty.\n'@
   ||
      OKRD.index('OKR'); OKRD.prefix(OKR.ref,ST.ODDZ_REF,ST.MAG);
      {? OKRD.first & OKRD.M='T'
      ||
         _wyn:='Funkcja niedostępna. Okres obrachunkowy zamknięty.\n'@
      ?}
   ?}
?};
OKR.cntx_pop; OKRD.cntx_pop;
_wyn


\koryguj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2010]
:: OPIS: korekta przychodu
::   WE: _a - =1-edycja ceny, 0-sprawdzenie warunkow
::       _b - 0 - pierwsze uruchomienie, 1 - automatyczna korekta
::   WY: komunikat
::  OLD: \koryguj/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
_autokor:=_a;
_wyn:='';

VAR_DEL.delete('X_Xc');

X_Xc:=tab_tmp(4,'REFN','STRING[16]',''
       ,'AKC','STRING[1]',''
       ,'DATA','DATE',''
       ,'SYMBOL','STRING[20]',''
       ,'MAGAZYN','STRING[20]',''
       ,'TYP','STRING[10]',''
       ,'NR','INTEGER',''
       ,'OKRES','INTEGER',''
       ,'POZ','INTEGER',''
       ,'INDEKS','STRING[50]','');

_acr:=X_Xc.mk_sel('Dokumenty blokujące korektę'@,'P',0,'list_dok_kor',,,,,'U','T');
X_Xc.win_fld(_acr,,'MAGAZYN',,,'8, 10, 10',,,'Magazyn'@);
X_Xc.win_fld(_acr,,'TYP',,,'8, 14, 14',,,'Typ dokumentu'@);
X_Xc.win_fld(_acr,,'NR',,,'9, 9, 9',,,'Numer'@);
X_Xc.win_fld(_acr,,'DATA',,,,,,'Data'@);
X_Xc.win_fld(_acr,,'SYMBOL',,,'10, 20, 0',,,'Symbol'@);
X_Xc.win_fld(_acr,,'OKRES',,,6,,,'Okres'@);
X_Xc.win_fld(_acr,,'AKC',,,,,,'Zakceptowany'@);
X_Xc.win_act(_acr,,'Rekord',,,,"exec('rek_nddk','magazyn_inw')");
X_Xc.win_act(_acr,,'Formuła','Pokaż dokument'@@,,,"exec('dispnddk','magazyn_inw')");
:: X_Xc.win_act(_acr,,'Formuła','Usuń dokument'@@,,,"exec('usunnddk','magazyn_inw')");
X_Xc.win_act(_acr,,'Kolejność');
X_Xc.win_sel(_acr);

DK.prefix();
{? DK.seek(__tab_ed.REF)
||
   TYPYDOK.cntx_psh;
   TYPYDOK.index('TYP'); TYPYDOK.prefix('INW+','INW+');
   _inwpnzw:={? TYPYDOK.first || TYPYDOK.DN<>'T' || 1 ?};
   TYPYDOK.cntx_pop;
   OKR.cntx_psh; INN.cntx_psh; INP.cntx_psh;
   OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
   {? OKR.first
   ||
      {!
      |?
         INPD.use((6+INPD.name)+($OKR.ROK+2));
         INPD.index('DK');
         INPD.prefix($DK.ref());
         {? INPD.first()
         ||
            INP.use(ref_name(INPD.INP));
            {!
            |?
               {? INPD.INP
               ||
                  INPD.INP();
                  {? INP.IN
                  ||
                     INN.use(ref_name(INP.IN));
                     INP.IN();
                     {? INN.D=date(0,0,0)
                     || _wyn:='Pozycja jest powiązana z otwartym arkuszem inwentaryzacyjnym.\nKorekta niemożliwa.'@;
                        {? _a || FUN.info(_wyn) ?}
                     |? INN.D>=dataka
                     || _wyn:='Pozycja jest powiązana z arkuszem inwentaryzacyjnym wystawionym w tym samym dniu'
                              ' lub później.\nKorekta niemożliwa.'@;
                        {? _a || FUN.info(_wyn) ?}
                     |? INP.SS>INP.SE & _inwpnzw
                     || _wyn:='Pozycja jest powiązana z arkuszem inwentaryzacyjnym.\nKorekta niemożliwa.'@;
                        {? _a || FUN.info(_wyn) ?}
                     ?}
                  ?}
               ?};
               _wyn='' & INPD.next
            !}
         ?};
         _wyn='' & OKR.next
      !}
   ?};
   OKR.cntx_pop; INN.cntx_pop; INP.cntx_pop;
   {? _wyn=''
   ||
      _chk_tab:="
         {? __tab_ed.WAL=INFO.NAROD().KOD
         ||
            __tab_ed.CP:=__tab_ed.CP$M.DOKL_C;
            {? __tab_ed.CP<=0
            || FUN.info('Należy podać cenę większą od zera.'@);0
            |? __tab_ed.CP=__cena
            || FUN.info('Należy podać cenę różną od aktualnej.'@);0
            || 1
            ?}
         ||
            __tab_ed.CWALP:=__tab_ed.CWALP$M.DOKL_C;
            {? __tab_ed.CWALP<=0
            || FUN.info('Należy podać cenę większą od zera.'@);0
            |? __tab_ed.CWALP=__cena
            || FUN.info('Należy podać cenę różną od aktualnej.'@);0
            || 1
            ?}
         ?}
      ";

      _chk:=0;
      OKR.cntx_psh;
      OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
      {? OKR.first
      ||
         ND.cntx_psh; DK.cntx_psh;
         {!
         |? _oddz:=1+(__tab_ed.NDK+3);

            ND.use('nagdo'+_oddz+(($OKR.ROK)+2));
            DK.use('dokma'+_oddz+(($OKR.ROK)+2));
            ND.cntx_psh; DK.cntx_psh;
            DK.index('DK_SC');
            DK.prefix(ST.MAG,DK.M,__tab_ed.RDK,__tab_ed.NDK,'T');
            {? DK.find_ge(dataka)
            ||

               {!
               |?
                  {? DK.N().D>dataka
                  || _chk:=1;
                     X_Xc.clear();
                     {? ~X_Xc.find_key($DK.N)
                     || X_Xc.clear();
                        X_Xc.blank();
                        X_Xc.AKC:=DK.N().Z;
                        X_Xc.DATA:=DK.N().D;
                        X_Xc.SYMBOL:=DK.N().SYM;
                        X_Xc.MAGAZYN:=DK.N().MAG().SYM;
                        X_Xc.TYP:=DK.N().TYP().T;
                        X_Xc.NR:=DK.N().NR;
                        X_Xc.OKRES:=DK.N().AM;
                        X_Xc.REFN:=$DK.N;
                        X_Xc.add()
                     ?}
                  ?};
                  ND.D=dataka & DK.next
               !}
            ?};
            {? _chk=0
            || DK.prefix(ST.MAG,DK.M,__tab_ed.RDK,__tab_ed.NDK,'N');
               {? DK.find_le(dataka)
               || _chk:=2;
                  {!
                  |? X_Xc.clear();
                     {? ~X_Xc.find_key($DK.N)
                     || X_Xc.clear();
                        X_Xc.blank();
                        X_Xc.AKC:=DK.N().Z;
                        X_Xc.DATA:=DK.N().D;
                        X_Xc.SYMBOL:=DK.N().SYM;
                        X_Xc.MAGAZYN:=DK.N().MAG().SYM;
                        X_Xc.TYP:=DK.N().TYP().T;
                        X_Xc.NR:=DK.N().NR;
                        X_Xc.OKRES:=DK.N().AM;
                        X_Xc.REFN:=$DK.N;
                        X_Xc.add()
                     ?};
                     DK.prev()
                  !}
               ?}
            ?};
            {? _chk=0
            || DK.index('DOKPLUS');
               DK.prefix(ST.MAG,DK.M,'T','T',__tab_ed.RDK,__tab_ed.NDK,'C');
               {? DK.find_ge(dataka)
               || _chk:=3;
                  {!
                  |? X_Xc.clear();
                     {? ~X_Xc.find_key($DK.N)
                     || X_Xc.clear();
                        X_Xc.blank();
                        X_Xc.AKC:=DK.N().Z;
                        X_Xc.DATA:=DK.N().D;
                        X_Xc.SYMBOL:=DK.N().SYM;
                        X_Xc.MAGAZYN:=DK.N().MAG().SYM;
                        X_Xc.TYP:=DK.N().TYP().T;
                        X_Xc.NR:=DK.N().NR;
                        X_Xc.OKRES:=DK.N().AM;
                        X_Xc.REFN:=$DK.N;
                        X_Xc.add()
                     ?};
                     DK.next()
                  !}
               ?}
            ?};
            ND.cntx_pop; DK.cntx_pop;
            _chk=0 & OKR.next
         !};
         ND.cntx_pop; DK.cntx_pop
      ?};
      OKR.cntx_pop;

::    sprawdzenie czy istnieja niezaakceptowane faktury G
      {? _chk=0
      || _rsql:=exec('MaskInt2RefSql','#convert',__tab_ed.NDK,__tab_ed.RDK);
         _rktm:=exec('FindInSet','#table','M','MATKTM',__tab_ed.KTM,__tab_ed.KTM);
         {? _rktm<>null
         || REZ.index('SC');
            REZ.prefix(_rktm,_rsql,_rsql,'F');
            {? REZ.first
            || {!
               |? {? REZ.DATA<=dataka
                  || _chk:=4;
                     X_Xc.clear();
                     {? ~X_Xc.find_key($DK.N)
                     || X_Xc.clear();
                        X_Xc.blank();
                        X_Xc.AKC:=REZ.FAP().FAKS().AKC;
                        X_Xc.DATA:=REZ.FAP().FAKS().D;
                        X_Xc.SYMBOL:=REZ.FAP().FAKS().SYM;
                        X_Xc.MAGAZYN:=REZ.MG().SYM;
                        X_Xc.TYP:=REZ.FAP().FAKS().T().T;
                        X_Xc.NR:=REZ.FAP().FAKS().NR;
                        X_Xc.OKRES:=REZ.FAP().FAKS().AM;
                        X_Xc.REFN:=$REZ.FAP().FAKS;
                        X_Xc.add()
                     ?}
                  ?};
                  REZ.next
               !}
            ?}
         ?}
      ?};

      __cena:={? __tab_ed.WAL=INFO.NAROD().KOD || __tab_ed.CP || __tab_ed.CWALP ?};
      M.cntx_psh();
      M.index('MATKTM');
      M.prefix();
      {? M.find_key(__tab_ed.KTM,)
      ||
         _prdk:=exec('FindAndGet','#table',DK,__tab_ed.REF,,"DK.PRDK",'');
         __tab_ed.fld_fml('C','EDIT_FORMAT',"'in_prec='+$M.DOKL_C");
         __tab_ed.fld_fml('CP','EDIT_FORMAT',"'in_prec='+$M.DOKL_C");
         __tab_ed.fld_fml('CWAL','EDIT_FORMAT',"'in_prec='+$M.DOKL_C");
         __tab_ed.fld_fml('CWALP','EDIT_FORMAT',"'in_prec='+$M.DOKL_C");
         {? _chk=0 & {? _a=1 || __tab_ed.edit(_chk_tab) ?}
         ||
            {? exec('korygujPRDK','magdok_korekty',_prdk)
            || __tab_ed.X:='*';
               {? __tab_ed.WAL<>INFO.NAROD().KOD
               ||
                  __tab_ed.CP:=(__tab_ed.CWALP*__tab_ed.KRS)$M.DOKL_C
               ?};
               do();
               __tab_ed.put();
               exec('set2kor','magdok_korekty',_prdk,__tab_ed.CP);
               end()
            ?}
         ||
            _wyn:=
               {? _chk=1 || 'Istnieją dokumenty zaakceptowane po %1.'@[$dataka]
               |? _chk=2 || 'Istnieją dokumenty niezaakceptowane do daty %1 włącznie.'@[$dataka]
               |? _chk=3 || 'Istnieją korekty wystawione w dniu %1 lub później.'@[$dataka]
               |? _chk=4 || 'Istnieją niezaakceptowane faktury z rezerwacją na daną dostawę.'@
               || ''
               ?};
            {? _chk
            ||
               _wyn+='\n'+'Korekta niemożliwa.'@;
               {? _a
               || {? _chk<=4 & FUN.ask(_wyn+'\n\n'+'Wyświetlić dokumenty?'@)
                  || X_Xc.clear();
                     X_Xc.select()
                  || FUN.info(_wyn)
                  ?}
               |? _a=0
               || exec('korygujPRDK','magdok_korekty',_prdk)
               ?}
           ?};
            VAR_DEL.delete('__cena')
         ?}
      || FUN.info('Nie odnalazłem indeksu materiałowego: %1.'@[__tab_ed.KTM])
      ?};
      M.cntx_pop()
   ?}
?};
VAR_DEL.delete('X_Xc');
_wyn


\ustalksg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: ustalanie konta ksiegowego DK, ND w buforze (dla naglowka dokumentu) - 1 konto po stronie Wn
::   WY: #KK.ref() konto ksiegowe
::  OLD: \ustalksg/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';

:: sprawdza naglowek dokumentu magazynowego
_wyn:=DK.KK().SYM;
{? _wyn='' || _wyn:=ND.KK().SYM ?};

_wy:=-1;
{? _wyn<>''
|| KK.index('KONTASYM');
   KK.prefix(REF.FIRMA,_wyn,_wyn);
   {? KK.first()
   ||
      _wy:=#KK.ref()
   ||
      KK.clear();
      KK.blank();
      KK.SYM:=_wyn;
      KK.add();
      _wy:=#KK.ref()
   ?}
?};
_wy


\dk_c_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: zwraca ref cechy wg symbolu lub tworzy nowy zapis
::   WE: _a - symbol cechy
::   WY: DK_C.ref()
::  OLD: \dk_c_ref/mag_dun.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
{? _a<>''
||
   DK_C.cntx_psh();
   DK_C.index('SYM');
   DK_C.prefix(_a,_a);
   {? DK_C.first()
   ||
      _wyn:=DK_C.ref()
   ||
      DK_C.blank();
      DK_C.SYM:=_a;
      DK_C.NAZ:=_a;
      DK_C.KOD:=exec('kod2matr','mat_atr');
      {? DK_C.add(1)
      || _wyn:=DK_C.ref()
      ?}
   ?};
   DK_C.cntx_pop()
?};
_wyn


\dkkordel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: usuwanie kompletu korekt dla dokumentu magazynowego
::       ND.ref w buforze
::   WY: czy usunieto
::  OLD: \dkkordel/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
:: tworzy tab_tmp z powiazanymi korektami
{? var_pres('X_korekt')>=100 || obj_del(X_korekt) ?};
X_korekt:=tab_tmp(
   ,'D','DATE','Data'
   ,'SYM','STRING[20]','Symbol'
   ,'REF','STRING[16]','ref'
   ,'RDK','INTEGER','rdk'
   ,'NDK','STRING[8]','ndk'
   ,'M','STRING[16]','$M.ref'
   ,'S','STRING[1]','Status'
   ,'ODDZ','STRING[1]','Oddział'
   ,'MAG','STRING[8]','Magazyn'
);
_ok:=1;
_nd_ref:=ND.ref();
_data:=ND.D;
DK.index('DOKMAG');
DK.prefix(_nd_ref);
:: dodaje pozycje do tab_tmp z powiazanymi korektami
{? DK.first
||
   {!
   |?
      exec('sp_dkkor','magdok_korekty',DK.ref(),X_korekt,DK.RDK,DK.NDK,$DK.M);
      DK.next
   !}
?};
exec('ini_kom','#message','Usuń korektę magazynową');
_kom:=0;
_rdk:=0; _ndk:='';
:: usunięcie korekt powiązanych z korektą zakupu
_nds:='';
{? X_korekt.first
||
   {!
   |?
      {? X_korekt.REF<>'' & (_nds*X_korekt.REF)=0
      ||
         __kom_gr:='Dotyczy koreky z dnia: %1'@[$X_korekt.D];
         _kor_zak:=exec('FindAndGet','#table',ND,X_korekt.REF,,"ND.FAKS_REF",'');
         _kor_zak:=exec('FindAndGet','#table',FAKS,_kor_zak,,"FAKS.SYM",'');
         {? _kor_zak=''
         || X_korekt.next
         || exec('add_kom','#message','Korekta jest powiązana z dokumentem zakupu %1'@[_kor_zak],4);
            _nds+=X_korekt.REF;
            _kom:=1
         ?}
      ?}
   !}
?};
:: usuniecie korekt dla ktorych istnieja pozniejsze korekty
{? X_korekt.first
||
   {!
   |?
      __kom_gr:='Dotyczy koreky z dnia: %1'@[$X_korekt.D];
      {? exec('dok_po_kor','magdok_wspolne',X_korekt.D+1,X_korekt.RDK,X_korekt.NDK,ND.MAG,
            exec('FindAndGet','#table',M,X_korekt.M,,,null()))
      || X_korekt.next
      || exec('add_kom','#message','Istnieją kolejne korekty. Usunięcie korekty niedostępne.'@,4);
         _kom:=1;
         _date:=X_korekt.D;
         X_korekt.cntx_psh;
         X_korekt.prefix(X_korekt.D);
         {! |? X_korekt.del !};
         X_korekt.cntx_pop;
         X_korekt.find_ge(_date)
      ?}
   !}
?};
:: sprawdzenie czy nie ma dokumentow rozchodowych
{? X_korekt.first()
||
   {!
   |?
      _komn:=1;
      {? _rdk=X_korekt.RDK & _ndk=X_korekt.NDK
      ||
         X_korekt.next
      ||
         __kom_gr:='Dotyczy korekty z dnia: %1'@[$X_korekt.D];
         _rdk:=X_korekt.RDK;
         _ndk:=X_korekt.NDK;

::       szuka dokumentow wystawionych w nowej cenie
         {? exec('czy_kor','magdok_wspolne',$DK.ref())
         ||
            _mat:=exec('FindAndGet','#table',M,X_korekt.M,,,null());
            _cena_mag:=exec('cena_mag','ceny',_mat,_rdk,_ndk,,,1);

            ND.cntx_psh();
            DK.cntx_psh();
            OKR.cntx_psh();
            OKR.index('MC');
            OKR.prefix(REF.FIRMA,1);

            {? OKR.first()
            ||
               {!
               |?
                  DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
                  ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));

                  _dk_ndx:=DK.ndx_tmp(,,'N','MAG',,'M',,,'RDK',,,'NDK',,,'N','Z',,'C',,,'POW',,1,'N','D',,'P',,);
                  DK.index(_dk_ndx);
                  DK.prefix(ST.MAG,_mat,_rdk,_ndk,'T',_cena_mag,'N');
                  {? DK.first()
                  ||
                     {!
                     |?
                        {? DK.N().D>X_korekt.D
                        || {? _komn=1
                           || exec('add_kom','#message'
                               ,'Istnieją dokumenty magazynowe z pozycjami z ceną po korekcie.'@,7);
                              exec('add_kom','#message'
                               ,'Należy wycofać ich akceptację:'@,7);
                              _komn:=0
                           ?};
                           exec('add_kom','#message'
                            ,'%1 z dnia %2, poz. %3. Indeks: %4'@[DK.N().SYM,$DK.N().D,$DK.P,DK.M().KTM],4);
                           X_korekt.S:='D';
                           X_korekt.put;
                           _kom:=1
                        ?};
                        DK.next()
                     !}
                  ?};
                  DK.ndx_drop(_dk_ndx);

                  OKR.next()
               !}
            ?};
            OKR.cntx_pop();
            DK.cntx_pop();
            ND.cntx_pop()
         ?};
         X_korekt.next()
      ?}
   !}
?};
{? _kom || exec('end_kom','#message') ?};
:: usuniecie korekt dla ktorych istnieja dokumenty rozchodowe
{? X_korekt.first
||
   {!
   |?
      {? X_korekt.S<>'D'
      || X_korekt.next
      || _date:=X_korekt.D;
         X_korekt.cntx_psh;
         X_korekt.prefix(X_korekt.D);
         {! |? X_korekt.del !};
         X_korekt.cntx_pop;
         X_korekt.find_ge(_date)
      ?}
   !}
?};
:: pozostawienie w X_korekt korekt z wybranego dnia
_date:=date(0,0,0);
{? X_korekt.first
||
   _Selkor:=sql('select distinct :_a.D from :_a',X_korekt);

   _wer:=_Selkor.mk_sel('Usuń korektę z dnia'@,,,'selkormagreffdl',,,10,,'U','T');
   _Selkor.win_fld(_wer,,'D',,,30,,,'Data'@,1);
   _Selkor.win_act(_wer,,'Formuła','&Wybierz'@@,,,"sel_exit()",,1);
   _Selkor.win_sel(_wer);
   {? _Selkor.first & _Selkor.next=0
   || _date:=_Selkor.D;
      _ok:=FUN.ask('Usunąć korektę z dnia %1?'@[$_date])
   |? ~_Selkor.select
   || _ok:=0
   || _date:=_Selkor.D;
      {? X_korekt.first
      ||
         {!
         |? {? X_korekt.D=_date
            || X_korekt.next
            || X_korekt.del
            ?}
         !}
      ?}
   ?}
?};
:: petla po korektach
{? _ok=1 & X_korekt.last()
||
   _ok:=0;
   _ref:='';
   _ini_kom:=1;
   ND.cntx_psh();
:: dokumenty moga byc generowane w innej masce
   exec('open','open_tab',ST.ODDZ,(8+X_korekt.REF)+2);
   do();
   {!
   |?
      {? _ref=X_korekt.REF
      ||
         X_korekt.prev
      ||
         _ref:=X_korekt.REF;
         ND.clear();
         {? ND.seek(X_korekt.REF,8+X_korekt.REF)
         ||
            _ok:=0;
            {? ND.ZAK<>'T'
            || _oki:=1;
               {? (';ZT'*ND.STAT_REJ)>1 || _oki:=exec('wyc_dok','magdok_nag',0,'N',,_ini_kom) ?};

               {? _oki
               || ND.get();
                  {? exec('n_usun','magdok_nag',0,0)
                  ||
                     _ok:=1
                  ||
                     undo();
                     FUN.info('Nie powiodło się usunięcie dokumentu %1.'@[ND.SYM])
                  ?}
               || undo();
                  FUN.info('Nie powiodło się wycofanie akceptacji dokumentu %1.'@[ND.SYM])
               ?};
               _ini_kom:=0
            ||
               undo();
               FUN.info('Dokument %1 jest zaksięgowany.\nUsunięcie niemożliwe.'@[ND.SYM])
            ?};

            {? _ok
            || X_korekt.prev()
            || undo();0
            ?}
         ||
            undo()
         ?}
      ?}
   !};
   end();
   {? ~_ini_kom || exec('end_kom','#message') ?};
:: powrot do maski dla wybranego zakresu
   exec('open','open_tab',ST.ODDZ,2-$ROKMC.AR);
   ND.cntx_pop();
:: dla biezacego dokumentu ustawia znacznik korekty
   ND.cntx_psh();
   ND.clear();
   {? _ok=1 & ND.seek(_nd_ref)
   ||
      ND.KOR:={? exec('czy_kor','magdok_wspolne',$_nd_ref) || 'T' || 'N' ?};
      ND.put()
   ?};
   ND.cntx_pop()
?};
''


\n_kolor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: ustawia kolor w wertowaniu ND, info o powiazanej fakturze
::----------------------------------------------------------------------------------------------------------------------
INTRAST.NIP:=ND.NIP_UE;
DISP.FAKS:=exec('zwr_symbol_fak','magdok_wspolne');
ST.MAG_SYM='';
{? ND.MD<>null || ST.MAG_SYM:=ND.MD().SYM ?};
DISP.TYP:=ND.TYP().T;
DISP.KHNAZ:=ND.KH().NAZ;

_actions:={? ND.KOR='N' || 'U'
          || ''
          ?};
ND.actions_grayed('WER_KOR',_actions);

exec('rekprzed','color','ND#01')


\dk2kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Listuje dostawy powiązane z dostawą pierwotną. Dostawa pierwotna wyłuskiwana z pozycji _a.
::   WE: _a - ND.ref()
::       _b - PRDK
::----------------------------------------------------------------------------------------------------------------------
_nd:=_a;

_Tab:=sql($"
   select
      DK.PRDK        PRDK,
      DK.RDK         RDK,
      DK.NDK         NDK,
      MG.SYM         MG_SYM,
      MG.REFERENCE   MG_REF,
      ND.D           D,
      TYPYDOK.T      TD_T,
      ND.NR          NR,
      DK.P           P,
      DK.C           C,
      DK.C           CP,
      '-'            X,
      'PLN'          WAL,
      DK.REFERENCE   REF
   from
      DK
      join ND using(DK.N,ND.reference)
      join TYPYDOK using( ND.TYP,TYPYDOK.reference)
      join MG using(ND.MAG,MG.reference)
   where
      DK.PRDK in
         (select
            PRDK
         from
            DK
         where
            DK.N=:_a)
      and TYPYDOK.P='T'
      and TYPYDOK.DN='N'
      and (TYPYDOK.Z='T' or TYPYDOK.T='MP')
      and ND.REFERENCE<>:_a
   order by
      PRDK, D, MG_SYM, NR
   ",_nd);
_loop:=_Tab.first();
{!
|? _loop
|!
   _ff:="exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,DK.N().MAG,DK.C)";
   _Tab.CP:=exec('FindAndGet','#table',DK,_Tab.REF,,_ff,0);
   _Tab.WAL:=exec('FindAndGet','#table',DK,_Tab.REF,,"N().WAL().KOD",'PLN');
   _loop:=_Tab.put() & _Tab.next()
!};
_Tab


\win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Okno pozycji dokumentów do korekety
::   WE: _a - środowisko
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a.TAB;

_win:=_Tab.mk_sel('Pozycje do automatycznej korekty związane z '@,'P',,'fuihfiqadfwef',1,,,,'U','T');
_Tab.win_fld(_win,,'D',,,16,,,'Data dokumentu'@);
_Tab.win_fld(_win,,'MG_SYM',,,15,,,'Magazyn'@);
_Tab.win_fld(_win,,'TD_T',,,16,,,'Typ dokumentu'@);
_Tab.win_fld(_win,,'NR',,,15,,,'Numer dokumentu'@);
_Tab.win_fld(_win,,'P',,,15,,,'Pozycja'@);
_Tab.win_fld(_win,,'C',,,10,ST.DOKL_C,,'Cena przed korektą'@);
_Tab.win_fld(_win,,'CP',,,10,ST.DOKL_C,,'Cena po korekcie'@);
_Tab.win_fld(_win,,'WAL',,,6,,,'Waluta'@);
_win


\set2kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [--.--]
:: OPIS: Zaznacza dokumenty do korekty
::   WE: _a - PRDK
::       _b - cena po korekcie
::----------------------------------------------------------------------------------------------------------------------
_Tab:=__Dk2kor.TAB;
_ndx:=_Tab.index('?');
_Tab.prefix(_a);
_loop:=_Tab.first();
{!
|? _loop
|!
   _Tab.X:='*';
   _Tab.CP:=_b;
   _loop:=_Tab.put() & _Tab.next()
!}


\korygujPRDK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Sprawdzenie czy dokumenty do korekty spełniają kryteria kwalifikacyjne
::   WE: _a - PRDK
::   WY: 0 - dokumety nie kwalifikują się do korekty, 1 - dokumenty kwalifikują się do korekty
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;

__tab_ed.cntx_psh();
_st_mag:=ST.MAG;
_Tab:=__Dk2kor.TAB;
_ndx:=_Tab.index('?');
_Tab.prefix(_a);
_loop:=_Tab.first();
_old:=DK.name();
{!
|? _loop
|!
   _nd:=exec('FindAndGet','#table',DK,_Tab.REF,,"$DK.N",'');
   _zak:=exec('FindAndGet','#table',ND,_nd,,"ND.ZAK",'');
   _sym:=exec('FindAndGet','#table',ND,_nd,,"ND.SYM",'');

   {? _nd<>'' & _zak<>'T'
   ||
      FUN.info('Niezaksięgowany dokument %1 w magazynie %2.'@[_sym,_Tab.MG_SYM]);
      _wyn:=0
   || __tab_ed.REF:=_Tab.REF;
      __tab_ed.RDK:=_Tab.RDK;
      __tab_ed.NDK:=_Tab.NDK;
      __tab_ed.C:=_Tab.C;
      ST.MAG:=exec('FindAndGet','#table',MG,_Tab.MG_REF,,,null());
      {? (ST.MAG().TYP*'EWI') | (1+ST.MAG().TYP='Ś')
      || FUN.info('Funkcja niedostępna w magazynie typu \'%3\'.\n'
                  'Dotyczy dokumentu %1 w magazynie %2.'@[_sym,_Tab.MG_SYM,ST.MAG().TYP]);
         _wyn:=0
      ?};
      {? _wyn & ST.MAG || _wyn:=exec('koryguj','magdok_korekty',2,0)='' ?}
   ?};
   _loop:=_wyn & _Tab.next()
!};
ST.MAG:=_st_mag;
__tab_ed.cntx_pop();

_wyn


\tabWithRef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Zwraca tabelę wszystkich powiązanych refów
::   WE: _a - ref SQL DK
::   WY: tabela danych
::----------------------------------------------------------------------------------------------------------------------
_wyn:=tab_tmp(1,'REF','STRING[16]','');
_wyn.blank();
_wyn.REF:=_a;
_wyn.add(1);
ND.cntx_psh();
DK.cntx_psh();
OKR.cntx_psh();
ODDZ.cntx_psh();
ODDZ.index('KOD');
ODDZ.prefix();
{? ODDZ.first()
|| {!
   |? OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      || {!
         |? ND.use('nagdo'+ODDZ.KOD+($OKR.ROK+2));
            DK.use('dokma'+ODDZ.KOD+($OKR.ROK+2));
            DK.index('PRDK');
            DK.prefix(_a,);
            {? DK.first()
            || {!
               |? {? DK.N().TYP().P='T' & DK.N().ZAK='T' & DK.N().TYP().DN='N'
                  || _wyn.blank();
                     _wyn.REF:=$DK.ref();
                     _wyn.add(1)
                  ?};
                  DK.next()
               !}
            ?};
            OKR.next()
         !}
      ?};
      ODDZ.next()
   !}
?};
ND.cntx_pop();
DK.cntx_pop();
OKR.cntx_pop();
ODDZ.cntx_pop();
_wyn


\kor_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: zakres dokumentow - korekty przychodow
::  OLD: \kor_zakr/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
FKOR.WHAT:='F';
{? ROKMC.AR=0 || ROKMC.AR:=ST.AR ?};
ROKMC.win_edit('ROK');
{? ROKMC.edit()
|| exec('kor_set','magdok_korekty')
?};
''

\zwrTypKor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.14]
:: OPIS: zwraca typ korekty
::   WE: _a - 1-KR+, 0-KR-
::       _b - magazyn kooperacyjny
::   WY: typ dokumentu lub null()
::----------------------------------------------------------------------------------------------------------------------
_res:=null();
_przy:={? _a || 'T' || 'N' ?};
_koop:={? _b || 'T' || 'N' ?};
TYPYDOK.cntx_psh();
TYPYDOK.index('KRZ');
TYPYDOK.prefix(_przy,'T','N');
{? TYPYDOK.first()
|| {!
   |? {? TYPYDOK.KOOP=_koop || _res:=TYPYDOK.ref() ?};
      _res=null() & TYPYDOK.next()
   !}
?};
TYPYDOK.cntx_pop();
_res


\ctrlENDmag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: sprawdzaczy magazyny są zamknięte
::   WE: _a - data do sprawdzenia
::   WY: 1-jest ok
::----------------------------------------------------------------------------------------------------------------------
_data:={? var_pres('_a')=type_of(date()) || _a || date(0,0,0) ?};
_res:=1;

{? _data<>date(0,0,0)
|| {? var_pres('__lp')<=0 || __lp:=0 ?};
   _rr:=_data~1;
   _mm:=_data~2;
:: najpierw dany magazyn
   {? ~exec('czy_z_ok','okresy',1,2,_rr,_mm,ST.MAG)
   || _res:=0
   ||
::    teraz powiązane magazyny
      _buf:=tab_tmp(1,'MG','STRING[16]','');
      _tab:=__Dk2kor.TAB;
      _tab.prefix();
      {? _tab.first()
      || {!
         |? {? (_buf.prefix(); ~_buf.find_key(_tab.MG_REF))
            || _buf.blank();
               _buf.MG:=_tab.MG_REF;
               _buf.add(1);
               _res:=exec('czy_z_ok','okresy',1,2,_rr,_mm,exec('FindAndGet','#table',MG,_tab.MG_REF,,,null()))>0
            ?};
            _res & _tab.next()
         !}
      ?}
   ?}
?};
_res


\fap2dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Powiazanie pozycji korekty magazynowej z pozycją korekty zakupu
::   WE: _a - FAKS.ref()
::       _b - FAP.ref()
::       _c - ND.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_faks:=_a;
_fap:=exec('FindAndGet','#table',FAP,_b,,"FAP.ref",null());
_nd:=_c;
_where:=exec('FindAndGet','#table',FAKS,_faks,,"FAKS.WHERE",'');

{? _fap=null() | _where='' || return() ?};

::_out:=fopen('debug.txt','w',,,1);
::{? _out.is_open()
::||
::   fwrite(_out,'fap2dk');
   DK.cntx_psh();
   DK.index('DOKMAG');
   DK.prefix(_nd);
   _loop:=DK.first();
   {!
   |? _loop
   |!
::      fwrite(_out,'- FAP: %1'[$_fap]);
::      fwrite(_out,'- DK: %1'[$DK.ref()]);
::      fwrite(_out,'- ND: %1'[$_nd]);
::      fwrite(_out,'- DK.IL: %1'[$DK.IL]);
::      fwrite(_out,'- WHERE: %1'[_where]);
      exec('adfap2dk','magdok_wspolne',$_fap,$DK.ref(),_faks,$_nd,DK.IL,_where);
      _loop:=DK.next()
   !};
   DK.cntx_pop()
::?}


\intra_nd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wypełnia dane Intrastat w nagłówku korekty
::   WE: _a - ND.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('FindAndGet','#table',ND,_a,,"
   _ND:=_b.ND;
   ND.KPW:=_ND.get('KPW');
   ND.WD:=_ND.get('WD');
   ND.RTRANSAK:=_ND.get('RTRANSAK');
   ND.RTRANSPO:=_ND.get('RTRANSPO');
   ND.IST_TYP:=_ND.get('IST_TYP');
   ND.IST_OKR:=_ND.get('IST_OKR');
   ND.INTRAKC:=_ND.get('INTRAKC');
   ND.INTRA:='N';
   ND.put()
",,_b)


\intra_dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wypełnia dane Intrastat w pozycji korekty
::   WE: _a - DK.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('FindAndGet','#table',DK,_a,,"
   _DK:=_b.DK;
   DK.KCN:=_DK.get('KCN');
   DK.KPW:=_DK.get('KPW');
   DK.WD:=_DK.get('WD');
   DK.RTRANSAK:=_DK.get('RTRANSAK');
   DK.RTRANSPO:=_DK.get('RTRANSPO');
   DK.KP:=_DK.get('KP');
   DK.MASAN:=0;
   DK.ILUJM:=0;
   DK.WF:={? DK.PLUS='T' || 1 || -1 ?}*DK.WAR;
   {? _DK.get('WS') || DK.WS:={? DK.PLUS || 1 || -1 ?}*DK.WAR ?};
   DK.IDKH:=_DK.get('IDKH');
   DK.put()
",,_b)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 beba3ed7021b3349b3108aba2e3a1a0cb83c712d95e6989bf02b9d613c6969d755b04b866098b57e6d140dd053bcc6a39c3160c722641bdaecbeccc352467ad93ad1adbfc9df5b406034f1a494832b5ea05cb1be1a3c283ba9cf39635abde3bab33c0210844c9b345aadd1454dd71659a6eb8a4a1b4763a2de9786b66f40c589

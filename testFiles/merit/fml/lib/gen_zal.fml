:!UTF-8
::(c) Macrologic SA
::======================================================================================================================
::Nazwa pliku: gen_zal.fml
::Utworzony: 2012-04-18
::Autor: AdamT (ATA)
::Systemy: KALI
::======================================================================================================================
::Zawartosc: Formula do generowania zalacznikow dokumentow
::======================================================================================================================

\pit_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [ATA] [12.30]
:: OPIS: Formula generujaca zalacznik PDF dla PIT-11 lub PIT-40 dla danego pracownika
::----------------------------------------------------------------------------------------------------------------------
{? ~FUN.ask('Czy na pewno generować załącznik?'@) || return(0) ?};

_vern:=VAT_DEK.NR().NR;
_nrf:=VAT_VER.NRF;
_podpis:=0;
_ext:='bmp';
_rpm:=
   {? SKID.DEKL_NAZ='PIT11'
   || {? _vern>=5 || _podpis:=1 ?};
      {? _vern>=8 || _ext:='png' ?};
      {? _vern=1 || 'ppl_dek1116'
      |? _vern=2 || 'ppl_dek1117'
      |? _vern=3 || 'ppl_dek1118'
      |? _vern=4 || 'ppl_dek1119'
      |? _vern=5 || 'ppl_dek1120'
      |? _vern=6 || 'ppl_dek1121'
      |? _vern=7 || 'ppl_dek1122'
      |? _vern=8 || 'ppl_dek1123'
      |? _vern=9 || 'ppl_dek1124'
      |? _vern=10 || 'ppl_dek1125'
      |? _vern=11 || 'ppl_dek1126'
      || {? _nrf<>0 || 'ppl_dek11'+$_nrf || '' ?}
      ?}
   |? SKID.DEKL_NAZ='PIT40'
   || {? _vern>=4 || _podpis:=1 ?};
      {? _vern>=6 || _ext:='png' ?};
      {? _vern=2 || 'ppl_dek4016'
      |? _vern=3 || 'ppl_dek4017'
      |? _vern=4 || 'ppl_dek4019'
      |? _vern=5 || 'ppl_dek4020'
      |? _vern=6 || 'ppl_dek4022'
      || ''
      ?}
   |? SKID.DEKL_NAZ='PIT8C'
   || {? _vern=6
      || 'ppl_dek8c06'
      |? _vern=7
      || 'ppl_dek8c07'
      |? _vern=8
      || 'ppl_dek8c08'
      |? _vern=9
      || 'ppl_dek8c09'
      |? _vern=10
      || 'ppl_dek8c10'
      || {? _nrf<>0 || 'ppl_dek8c'+$_nrf || '' ?}
      ?}
   |? SKID.DEKL_NAZ='IFT1' | SKID.DEKL_NAZ='IFT1R'
   || {? _vern=10 || 'ppl_dekift110'
      |? _vern=11 || 'ppl_dekift111'
      |? _vern=12 || 'ppl_dekift112'
      |? _vern=13 || 'ppl_dekift113'
      |? _vern=14 || 'ppl_dekift114'
      |? _vern=15 || 'ppl_dekift115'
      |? _vern=16 || 'ppl_dekift116'
      || ''
      ?}
   || ''
   ?};
{? _rpm<>''
||
:: wybór pliku podpisu
   {? _podpis
   || exec('ask4File','podpis',_ext,1,0,'dek')
   ?};
   _ovr:=obj_new('warunek','war','byl','ok');
   _ovr.warunek:="
      _text1:='Załącznik został już pobrany przez Pracownika'@;
      _text2:='Załącznik został już wcześniej utworzony'@;
      FUN.choice({? exec('isNotEmpty','zal_pobh',ZALACZ.ref()) || _text1 || _text2?}+'.\n'+
                  'Czy zastąpić?'@,,'&Tak'@,'&Dołącz nową wersję'@)
   ";
   VAT_DEK.P();
   _wydruk:=username()+$VAT_DEK.tm_stamp()+'.pdf';
   {? 1+_wydruk='@' || _wydruk:=1-_wydruk ?};
   {? rep_exec(_rpm,,0,_wydruk,1)
   || _ret:=exec('add_zal','zal','de',,_wydruk,,_ovr,SKID.DEKL_NAZ,
          {? (3+SKID.DEKL_NAZ)='IFT' || form(VAT_DEK.OD~2,-2)+'-'+form(VAT_DEK.DO~2,-2)+'.' || '' ?});
      {? PAR_SKID.get(337)='T' & +|_ret.UIDREF & +_ret.NAZWA & ZALACZ.seek(_ret.UIDREF,,1)
         & FUN.ask('Podpisać dokument "%1"?'@[_ret.NAZWA])
      || exec('esign_decl','#esign');
         _epodpis:=obj_new(@.CLASS.ESIGN);
         _epodpis.start();
         _epodpis.s_blob_add(ZALACZ,'ZAL','ZAL','DTSIGN');
         _epodpis.sign();
         _epodpis.stop();
         obj_del(&_epodpis)
      ?}
   ?}
?}


\gen_pit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [ATA] [12.30]
:: OPIS: Generuje zalaczniki dokumentow zawierajacych informacje podatkowe
::----------------------------------------------------------------------------------------------------------------------
VAT_POZ.cntx_psh();
VAT_POZ.index('VAT_POZ');
VAT_DEK.cntx_psh();
VAT_DEK.index('VAT_PITG');
VAT_DEK.prefix(PIT_GRUP.ref());
OSOBA.cntx_psh();
P.cntx_psh();
{? VAT_DEK.first()
|| _vern:=PIT_GRUP.NR().NR;
   _nrf:=VAT_VER.NRF;
   _podpis:=0;
   _ext:='bmp';
   _rpm:=
      {? SKID.DEKL_NAZ='PIT11'
      || {? _vern>=5 || _podpis:=1 ?};
         {? _vern>=8 || _ext:='png' ?};
         {? _vern=1 || 'ppl_dek1116'
         |? _vern=2 || 'ppl_dek1117'
         |? _vern=3 || 'ppl_dek1118'
         |? _vern=4 || 'ppl_dek1119'
         |? _vern=5 || 'ppl_dek1120'
         |? _vern=6 || 'ppl_dek1121'
         |? _vern=7 || 'ppl_dek1122'
         |? _vern=8 || 'ppl_dek1123'
         |? _vern=9 || 'ppl_dek1124'
         |? _vern=10 || 'ppl_dek1125'
         |? _vern=11 || 'ppl_dek1126'
         || {? _nrf<>0 || 'ppl_dek11'+$_nrf || '' ?}
         ?}
      |? SKID.DEKL_NAZ='PIT40'
      || {? _vern>=4 || _podpis:=1 ?};
         {? _vern>=6 || _ext:='png' ?};
         {? _vern=2 || 'ppl_dek4016'
         |? _vern=3 || 'ppl_dek4017'
         |? _vern=4 || 'ppl_dek4019'
         |? _vern=5 || 'ppl_dek4020'
         |? _vern=6 || 'ppl_dek4022'
         || ''
         ?}
      |? SKID.DEKL_NAZ='PIT8C'
      || {? _vern=6
         || 'ppl_dek8c06'
         |? _vern=7
         || 'ppl_dek8c07'
         |? _vern=8
         || 'ppl_dek8c08'
         |? _vern=9
         || 'ppl_dek8c09'
         |? _vern=10
         || 'ppl_dek8c10'
         || {? _nrf<>0 || 'ppl_dek8c'+$_nrf || '' ?}
         ?}
      |? SKID.DEKL_NAZ='IFT1' | SKID.DEKL_NAZ='IFT1R'
      || {? _vern=10 || 'ppl_dekift110'
         |? _vern=11 || 'ppl_dekift111'
         |? _vern=12 || 'ppl_dekift112'
         |? _vern=13 || 'ppl_dekift113'
         |? _vern=14 || 'ppl_dekift114'
         |? _vern=15 || 'ppl_dekift115'
         |? _vern=16 || 'ppl_dekift116'
         || ''
         ?}
      || ''
      ?};
   _ok:=_rpm<>'';
   {? _ok
   || _par:=tab_tmp(1,'OPT','INTEGER','Opcja');
      _we:=_par.mk_edit('Generowane załączników'@,,'#gen_pit_par');
      _par.win_efld(_we,,'OPT',,,,,,'Postępowanie w przypadku, gdy załącznik już istnieje:'@,,
                                    'Jak postąpić w sytuacji gdy plik (określonego typu) był już utworzony.'@,
                                    'radio-buttons','left_label=0',
         'Pozostaw wcześniej utworzoną wersję'@,"0",
         'Zastąp nową wersją, jeżeli poprzednia nie została pobrana'@,"1",
         'Zawsze zastępuj'@,"2",
         'Dołącz nową wersję'@,"3"
      );
      exec('ok_esc','#window',_par,_we);
      _par.win_edit(_we);

      {? _ok:=_par.edit()
      || _ovr:=obj_new('warunek','war','byl','ok');
         _ovr.warunek:=
            {? _par.OPT=0 || "0"
            |? _par.OPT=1 || "~exec('isNotEmpty','zal_pobh',ZALACZ.ref())"
            |? _par.OPT=2 || "1"
            |? _par.OPT=3 || "2"
            ?};
         _ovr.war:=_ovr.byl:=_ovr.ok:=0
      ?}
   ?};
   _log:=tab_tmp(3,
      'NAZWISKO','STRING['+$MS.fld_len(OSOBA,'NAZWISKO')+']','Nazwisko'@,
      'PIERWSZE','STRING['+$MS.fld_len(OSOBA,'PIERWSZE')+']','Imię'@,
      'PESEL','STRING['+$MS.fld_len(OSOBA,'PESEL')+']','PESEL',
      'STATUS','STRING[70]','Status'@
   );

:: wybór pliku podpisu
   {? _ok & _podpis
   || exec('ask4File','podpis',_ext,1,0,'dek')
   ?};
   _par337:=PAR_SKID.get(337)='T';
   {? _par337 || _tab:=exec('tab_sign','gen_zal') ?};
:: generowanie plików PDF
   PROGRESS.set(VAT_DEK.size(),'Generowanie załączników...'@);
   {!
   |? _ok
   |! VAT_POZ.prefix(VAT_DEK.ref());
      {? VAT_POZ.first()
      || VAT_POZ.VAT_DEK();
         VAT_DEK.P();
         _log.blank();
         _log.NAZWISKO:=P.OSOBA().NAZWISKO;
         _log.PIERWSZE:=OSOBA.PIERWSZE;
         _log.PESEL:=OSOBA.PESEL;
         _wydruk:=username()+$VAT_DEK.tm_stamp()+'.pdf';
         {? 1+_wydruk='@' || _wydruk:=1-_wydruk ?};
         {? rep_exec(_rpm,,0,_wydruk,1)
         || {? var_pres('_ret')>0 || &_ret ?};
            _ret:=exec('add_zal','zal','de',,_wydruk,,_ovr,SKID.DEKL_NAZ,
                {? (3+SKID.DEKL_NAZ)='IFT' || form(VAT_DEK.OD~2,-2)+'-'+form(VAT_DEK.DO~2,-2)+'.' || '' ?});
            _log.STATUS:='Załącznik '+
               {? _ovr.byl & _par.OPT=1
               || '('+{? _ovr.war || 'nie' || '' ?}+'pobrany) '
               || ''
               ?}+
               {? _ovr.ok || '' || 'nie ' ?}+'został '+
               {? _ovr.byl & _par.OPT=1 || 'zastąpiony' || 'utworzony' ?};
            {? _par337 & _ovr.ok & +|_ret.UIDREF & +_ret.NAZWA & ZALACZ.seek(_ret.UIDREF,,1)
            || _tab.add(_tab.TAB)
            ?}
         || _ok:=0
         ?};
         {? _log.STATUS<>'' || _log.add() ?}
      ?};
      PROGRESS.next();
      _ok:=_ok & VAT_DEK.next()
   !};
   PROGRESS.close();
   {? _log.first()
   || _ws:=_log.mk_sel('Raport z generowania załączników PDF'@,,0,'#gen_pit_log',,,,,'U');
      _log.win_fld(_ws,,'NAZWISKO',,,,,,,,'Aktualne nazwisko osoby'@);
      _log.win_fld(_ws,,'PIERWSZE',,,,,,,,'Pierwsze imię osoby'@);
      _log.win_fld(_ws,,'PESEL',,,,,,,,'Numer PESEL osoby'@);
      _log.win_fld(_ws,,'STATUS',,,,,,,,'Status operacji'@);
      _log.win_act(_ws,,'Szukaj');
      _log.win_act(_ws,,'Kolejność');
      _log.win_sel(_ws);
      _log.win_edit(_log.mk_edit(,1));
      _log.select()
   ?};
   {? _par337 & _tab.TAB.first()
   || params_set('ret',_tab);
      _tab.TAB.select()
   ?}
?};
VAT_DEK.cntx_pop();
VAT_POZ.cntx_pop();
P.cntx_pop();
OSOBA.cntx_pop();
1


\podpis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Podpisywanie załączników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('tab_sign','gen_zal');
ZALACZ.cntx_psh();
ZALACZ.clear();
_typ:=exec('slo_naz','ext_slo',exec('slo_typ','ext_slo','ZAL'),'Informacje podatkowe');
{? _typ
|| VAR_EDIT.D_OD:=date(,,1);
   VAR_EDIT.D_DO:=date(,,0);
   VAR_EDIT.win_edit('ZAKR_DAT');
   _fv:="
      {? VAR_EDIT.D_OD=#0
      || FUN.emsg('Data \"%1\" nie może być zerowa.'@[MS.name(VAR_EDIT,'D_OD')]); return('D_OD')
      ?};
      {? VAR_EDIT.D_DO=#0
      || FUN.emsg('Data \"%1\" nie może być zerowa.'@[MS.name(VAR_EDIT,'D_DO')]); return('D_DO')
      ?};
      {? VAR_EDIT.D_OD>VAR_EDIT.D_DO
      || FUN.emsg('Data \"%1\" nie może być późniejsza niż \"%2\".'@
            [MS.name(VAR_EDIT,'D_DO'),MS.name(VAR_EDIT,'D_OD')]);
         return('D_OD')
      ?};
      1
   ";
   {? VAR_EDIT.edit(_fv)
   || _where:='
         ZAL is not null and
         TYP_ZAL=\':_a\' and
         DTSIGN is null and
         DATA>=to_date(:_b) and
         DATA<=to_date(:_c) and
         NAG in (select UIDR from :_d)
      '
   || _where:='
         ZAL is not null and
         TYP_ZAL=\':_a\' and
         DTSIGN is null and
         NAG in (select UIDR from :_d)
      '
   ?};
   _sql:=sql('
      select (\':\' || cast(IDADD as STRING_TYPE) || REFERENCE) as UIDR
      from VAT_DEK
      where PIT_GRUP like \':_a\'
         and P in (select distinct :_b.REF from :_b)',
      $PIT_GRUP.ref(),
      exec('dostepne_p','schemat','PPL',P_FILTER.F_ZATR().KOD,'*')
   );
   ZALACZ.f_set(,,_where,
      $_typ,
      VAR_EDIT.D_OD,
      VAR_EDIT.D_DO,
      _sql
   );
   {? ZALACZ.f_first()
   || {! |?
         _tab.add(_tab.TAB);
         ZALACZ.f_next()
      !}
   ?};
   params_set('ret',_tab);
   _tab.TAB.select()
?};
{? ZALACZ.f_active() || ZALACZ.f_clear() ?};
ZALACZ.cntx_pop()


\tab_sign
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [12.51]
:: OPIS: Mechanizm podpisu
::   WE:
::   WY:
:: ~OST: INWINBTN
::----------------------------------------------------------------------------------------------------------------------
_ret:=obj_new('TAB','WS','add','put','esign');

_ret.TAB:=tab_tmp(3,
   _acr:='NAZWISKO','STRING['+$MS.fld_len(OSOBA,_acr)+']','Nazwisko'@,
   _acr:='PIERWSZE','STRING['+$MS.fld_len(OSOBA,_acr)+']','Imię'@,
   _acr:='T','STRING['+$MS.fld_len(P,_acr)+']','Numer teczki'@,
   _acr:='SYMBOL','STRING['+$MS.fld_len(UD_SKL,_acr)+']','Jednostka organizacyjna'@,
   _acr:='CP','STRING['+$MS.fld_len(CP,_acr)+']','Charakter pracy'@,
   _acr:='ZA','STRING['+$MS.fld_len(P,_acr)+']','Znacznik zatrudnienia (T/N)'@,
   'DATA','DATE','Data'@,
   'NAZWA','STRING[64]','Załącznik'@,
   'DESCR','STRING['+$MS.fld_len(ZALACZ,'LOK')+']','Opis'@,
   'SIZE','INTEGER','Rozmiar'@,
   'DTSIGN','DATE','Data podpisu'@,
   'CRT_DATE','DATE','Data utworzenia'@,
   'CRT_TIME','TIME','Godzina utworzenia'@,
   'MOD_DATE','DATE','Data modyfikacji'@,
   'MOD_TIME','TIME','Godzina modyfikacji'@,
   'UIDREF','STRING[48]','Wskazanie załącznika'@
);
_ret.WS:=_ret.TAB.mk_sel('Załączniki do podpisu'@,,0,'#tabsigngenzal',,,,,'U',,,,,'maximized');
_ret.TAB.win_fld(_ret.WS,,'T');
_ret.TAB.win_fld(_ret.WS,,'NAZWISKO');
_ret.TAB.win_fld(_ret.WS,,'PIERWSZE');
_ret.TAB.win_fld(_ret.WS,,'SYMBOL',,,-16);
_ret.TAB.win_fld(_ret.WS,,'CP');
_ret.TAB.win_fld(_ret.WS,,'ZA',,,-3);
_ret.TAB.win_fld(_ret.WS,,'DATA');
_ret.TAB.win_fld(_ret.WS,,'NAZWA',,,-50);
_ret.TAB.win_sel(_ret.WS);

_fbg:="
   _ret:=params_get().ret;
   _ret.esign.start()
";
_fag:="
   _ret:=params_get().ret;
   _ret.esign.sign();
   _ret.esign.stop();
   _tab:=cur_tab(1,1);
   _tab.cntx_psh();
   {? _tab.first()
   || {!
      |? {? ZALACZ.seek(_tab.UIDREF,,1) & ZALACZ.ZAL || _ret.put(_tab) ?};
         _tab.next()
      !}
   ?};
   _tab.cntx_pop()
";
_fb:="
   _ret:=params_get().ret;
   _tab:=cur_tab(1,1);
   _grupa:=_tab.sel_size();
   {? ~_grupa || _ret.esign.start() ?};
   _jest:=
      {? _tab.DTSIGN=#0 & +|_tab.UIDREF & +_tab.NAZWA & ZALACZ.seek(_tab.UIDREF,,1)
      || _ret.esign.s_blob_add(ZALACZ,'ZAL','ZAL','DTSIGN')
      || 0
      ?};
   {? ~_grupa & _jest
   || _ret.esign.sign();
      _ret.esign.stop();
      {? ZALACZ.seek(_tab.UIDREF,,1) || _ret.put(_tab) ?}
   |? ~_grupa
   || _ret.esign.stop()
   ?};
   1
";
_ret.TAB.win_act(_ret.WS,0,'Formuła','Podpisz'@@,,,_fb,,1,1,_fbg,_fag,'P',,'target=record');
_ret.TAB.win_btn(_ret.WS,'text=%1,btn_label_align=center,text_len=8,panel=right'['Podpisz'@@],'menu:P');
_fa:="sel_exit()";
_ret.TAB.win_act(_ret.WS,0,'Formuła','Anuluj'@@,,,,_fa,,,,,'A',,'target=window');
_ret.TAB.win_btn(
   _ret.WS,
   'text=%1,btn_label_align=center,text_len=8,panel=bottom'['Anuluj'@@],
   'menu:A');
_fb:="exec('legenda','color','VAT_DEK#02#01')";
_ret.TAB.win_act(_ret.WS,0,'Formuła','Legenda'@@,,,_fb,,,,,,'L',,'target=window');
_ret.TAB.win_act(_ret.WS,0,'Kolejność');
_fb:="{? cur_tab.DTSIGN<>#0 || Color.fnd_kol('VAT_DEK#02#01') || '' ?}";
_ret.TAB.win_act(_ret.WS,0,'Rekord',,,,_fb);

_ret.add:="
   _tab:=_a;
   _tab.NAZWISKO:=ZALACZ.P().OSOBA().NAZWISKO;
   _tab.PIERWSZE:=OSOBA.PIERWSZE;
   _tab.T:=P.T;
   _tab.SYMBOL:=P.WYDZIAL().SYMBOL;
   _tab.CP:=P.CP().CP;
   _tab.ZA:=P.ZA;
   _tab.DATA:=ZALACZ.DATA;
   _tab.NAZWA:=ZALACZ.ZAL_NAME;
   _tab.DESCR:=ZALACZ.LOK;
   _tab.SIZE:=ZALACZ.SIZE;
   _tab.DTSIGN:=ZALACZ.DTSIGN;
   _tab.CRT_DATE:=ZALACZ.bl_info('ZAL','CREAT_DATE');
   _tab.CRT_TIME:=ZALACZ.bl_info('ZAL','CREAT_TIME');
   _tab.MOD_DATE:=ZALACZ.bl_info('ZAL','MODIFY_DATE');
   _tab.MOD_TIME:=ZALACZ.bl_info('ZAL','MODIFY_TIME');
   _tab.UIDREF:=ZALACZ.uidref();
   _tab.add()
";
_ret.put:="
   _tab:=_a;
   _tab.SIZE:=ZALACZ.SIZE;
   _tab.DTSIGN:=ZALACZ.DTSIGN;
   _tab.MOD_DATE:=ZALACZ.bl_info('ZAL','MODIFY_DATE');
   _tab.MOD_TIME:=ZALACZ.bl_info('ZAL','MODIFY_TIME');
   _tab.put()
";
exec('esign_decl','#esign');
_ret.esign:=obj_new(@.CLASS.ESIGN);
&_ret

:Sign Version 2.0 jowisz:1045 2023/08/25 11:56:13 c68a634bd36a2466daad395b5d6eba002875997437c43324788013ca569ef73dc18c0f6d72efa1c6ead982f87d6df697fdb12f98a2cbdbf2282d4c60721ce9d3b7d8d4c3b525c7fd1fbfae217ea157e844b737a3b9d22ccc8e5e94beb5f3f835f841e8456dca690f016a978fb4f2c99e9ce7a19b332a809304d4e9ef344fb99d

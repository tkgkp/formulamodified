:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obe_budz.fml
:: Utworzony: 15.06.2016
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły obszaru OBE do obsługi kontroli budżetów
::======================================================================================================================


\param_glob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Prezentacja parametrow globalnych kontroli budzetu
::  OLD: \param_glob/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
exec('edytuj','#stalesys',FINFO,'KONTROLA',
   "{? FINFO.KBUDZET='T' || __CHK.record(FINFO,,'KWERBUD') || 1 ?}"
)<>date(0,0,0)


\bvkbudzet
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed wyswietleniem FINFO.KBUDZET
::----------------------------------------------------------------------------------------------------------------------
FINFO.efld_opt('KONTROLA','enable='+$(FINFO.KBUDZET='T'),FINFO,'KWERBUD');
FINFO.efld_opt('KONTROLA','mark='+$(FINFO.KBUDZET='T'),FINFO,'KWERBUD');
FINFO.efld_opt('KONTROLA','enable='+$('PA'*FINFO.KWTYPW>0),FINFO,'KWW_PROC');
FINFO.efld_opt('KONTROLA','enable='+$('WA'*FINFO.KWTYPW>0),FINFO,'KWW_WART');
FINFO.efld_opt('KONTROLA','enable='+$('PA'*FINFO.KWTYPI>0),FINFO,'KWI_PROC');
FINFO.efld_opt('KONTROLA','enable='+$('WA'*FINFO.KWTYPI>0),FINFO,'KWI_WART');
FINFO.efld_opt('KONTROLA','enable='+$('PA'*FINFO.KBTYPW>0),FINFO,'KBW_PROC');
FINFO.efld_opt('KONTROLA','enable='+$('WA'*FINFO.KBTYPW>0),FINFO,'KBW_WART');
FINFO.efld_opt('KONTROLA','enable='+$('PA'*FINFO.KBTYPI>0),FINFO,'KBI_PROC');
FINFO.efld_opt('KONTROLA','enable='+$('WA'*FINFO.KBTYPI>0),FINFO,'KBI_WART');
~~


\aekbudzet
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Po redakcji FINFO.KBUDZET
::  OLD: \aekbudzet/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
win_disp();
1


\bvkwerbud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Przed wyświetleniem FINFO.KWERBUD
::  OLD: \bvkwerbud/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.KBUDZET='T' || '' || exec('findfnrd','color') ?}


\bekwerbud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Przed redakcja FINFO.KWERBUD
::  OLD: \bekwerbud/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
FINFO.KBUDZET='T'


\ae_kwtyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Po redakcji
::   WE:
::   WY:
::  OLD: \ae_kwtyp/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
win_disp();
1


\bv_kwprog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Przed wyswietleniem pol z wartosciami progu kontroli
::  OLD: \bv_kwprog/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('be_kwprog','obe_budz')
|| ''
|| exec('findfnrd','color')
?}


\be_kwprog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Przed redakcja pol z wartosciami progu kontroli
::  OLD: \be_kwprog/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
_z2:=1+(1-cur_afld());
_z3:=1+(2-cur_afld());
_proc:=(cur_afld()+4)='PROC';
_vp:=($('FINFO.K'+_z2+'TYP'+_z3))();
_proc & 'PA'*_vp>0 | ~_proc & 'WA'*_vp>0


\param_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Modele dla kontroli
::  OLD: \param_model/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBN.win_sel('WER_OG');
SKID_MBN.index('EWID_KOD'); SKID_MBN.prefix('T');
SKID_MBN.select()


\arg_skidmbnkon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Po odswiezeniu w oknie grupowym WER_OG tabeli SKID_MBN
::  OLD: \arg_skidmbnkon/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBP.index('LP');
SKID_MBA.index('DISP');
{? SKID_MBN.get()
|| SKID_MBP.prefix(SKID_MBN.ref());
   SKID_MBA.prefix(SKID_MBN.ref())
|| SKID_MBP.prefix(null);
   SKID_MBA.prefix(null)
?};
grp_disp(SKID_MBP,'KON');
grp_disp(SKID_MBN,'WER_O')


\skid_mbn_kontr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Akcja kontroluj/nie kontroluj w oknie KON tabeli SKID_MBN
::   WE: _a - Kontrolować? 1-tak 0-nie
::  OLD: \skid_mbn_kontr/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
SKID_MBN.KONTRPOZ:={? _a
                   || _pyt:=FUN.choice('Czy kontrolować budżet?',,'Tak dla &firmy','Tak dla &grupy');
                      {? _pyt=1
                      || 'F'
                      |? _pyt=2
                      || 'G'
                      || ''
                      ?}
                   || 'N'
                   ?};
{? SKID_MBN.KONTRPOZ='' || return() ?};
SKID_MBN.KONTROLA:={? SKID_MBN.KONTRPOZ='N' || 'N' || 'T' ?};
{? SKID_MBN.KONTROLA='N'
|| SKID_MBA.index('SKID_MBA');
   SKID_MBA.prefix(SKID_MBN.ref());
   {? SKID_MBA.first()
   || FUN.info('Model ma przypisane automaty.'@);
      _ok:=0
   || SKID_MBN.LW:=0
   ?}
?};
{? _ok & SKID_MBN.put()
|| SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
   {? SKID_MBP.first()
   || {!
      |? SKID_MBP.KONTROLA:=SKID_MBN.KONTROLA;
         SKID_MBP.put();
         SKID_MBN.KONTROLA='N' & SKID_MBP.next()
      !}
   ?};
   SKID_MBP.cntx_pop()
?}


\ar_skid_mbn_lw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [1210]
:: OPIS: Rekord po tabeli SKID_MBN (okno WER_O)
::  OLD: \ar_skid_mbn_lw/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
{? SKID_MBN.LW<0
|| FUN.info('Liczba lat wstecz do kontroli powinna być nieujemna.'@);
   'LW'
|| 1
?}


\br_skid_mbn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed okna WER_O tabeli SKID_MBN
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
_mark:='';
{? SKID_MBN.KONTROLA<>'N'
|| _gray:='J'; _mark:='N'
|| _gray:='NL'; _mark:='J'
?};
SKID_MBN.actions_grayed('WER_O',_gray);
SKID_MBN.actions('WER_O',,_mark,1);
exec('br_skid_mbn','control')


\skid_mbp_kontr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Akcja kontroluj/nie kontroluj w oknie WER_O tabeli SKID_MBP
::  OLD: \skid_mbp_kontr/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
{? SKID_MBN.KONTROLA='N'
|| FUN.info('Kontrola na modelu nie jest włączona.'@)
|? SKID_MBP.LP=1
|| FUN.info('Nie można zmieniać kontroli na pozycji budżetowej.'@)
|| SKID_MBP.KONTROLA:={? SKID_MBP.KONTROLA='T' || 'N' || 'T' ?};
   SKID_MBP.put()
?}


\br_skid_mbp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed okna KON tabeli SKID_MBP
::----------------------------------------------------------------------------------------------------------------------
_gray:=_mark:='';
{? SKID_MBN.KONTROLA='N' | SKID_MBP.LP=1
|| _gray:='JN'
|? SKID_MBP.KONTROLA='T'
|| _gray:='J'; _mark:='N'
|| _gray:='N'; _mark:='J'
?};
SKID_MBP.actions_grayed('KON',_gray);
SKID_MBP.actions('KON',,_mark,1);
~~


\ba_skid_mba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Dołącz dla automatow dla kontroli
::  OLD: \ba_skid_mba/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__Tau');
__Tau:=tab_tmp(2,
   'ROK','STRING[20]','Rok',
   'NAZ','STRING[8]','Nazwa',
   'OP','STRING[40]','Opis',
   'REF','INTEGER',
);
SKID_MBA.cntx_psh();
SKID_MBA.index('SKID_MBA'); SKID_MBA.prefix(SKID_MBN.ref());
AUTOKSIE.index('TYP2'); AUTOKSIE.prefix(REF.S_FIRMA,'T','O');
{? AUTOKSIE.first()
|| {!
   |? {? ~SKID_MBA.find_key(AUTOKSIE.ref())
      || __Tau.ROK:=AUTOKSIE.ROK_F().NAZ;
         __Tau.NAZ:=AUTOKSIE.NAZ;
         __Tau.OP:=AUTOKSIE.OP;
         __Tau.REF:=AUTOKSIE.ref();
         __Tau.add()
      ?};
      AUTOKSIE.next()
   !}
?};
SKID_MBA.cntx_pop();
_o:=__Tau.mk_sel('Formuły automatycznej rejestracji'@,'P',,'#selautoksie',,,,,'U');
__Tau.win_fld(_o,,'ROK');
__Tau.win_fld(_o,,'NAZ');
__Tau.win_fld(_o,,'OP');
__Tau.win_act(_o,,'Formuła','Dołącz'@@,,,"
   SKID_MBA.cntx_psh();
   SKID_MBA.index('SKID_MBA'); SKID_MBA.prefix(SKID_MBN.ref);
   {? AUTOKSIE.seek(__Tau.REF,) & ~SKID_MBA.find_key(AUTOKSIE.ref())
   || SKID_MBA.blank(1);
      SKID_MBA.ROK_F:=AUTOKSIE.ROK_F;
      SKID_MBA.SKID_MBN:=SKID_MBN.ref();
      SKID_MBA.AUTOKSIE:=AUTOKSIE.ref();
      {? SKID_MBA.add()
      || __Tau.del()
      ?}
   ?};
   SKID_MBA.cntx_pop()
","",1,,,,'D');
__Tau.win_btn(_o,'text='+'Dołącz'@+',btn_label_align=center,panel=right,align=begin','menu:D');
__Tau.win_sel(_o);
__Tau.select();
VAR_DEL.delete('__Tau')


\new_wersje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodaje wersje planów związane z kontrolą budżetów
::----------------------------------------------------------------------------------------------------------------------
K_WERSJE.index('SYM'); K_WERSJE.prefix();
{? ~K_WERSJE.find_key('Kontrola zapotrzebo.',)
|| K_WERSJE.SYM:='Kontrola zapotrzebo.';
   K_WERSJE.CZY_SYS:='Z';
   K_WERSJE.CZY_KALK:='N';
   K_WERSJE.K_W_OBL:=null;
   K_WERSJE.add()
?};
{? ~K_WERSJE.find_key('Kontrola faktur',)
|| K_WERSJE.SYM:='Kontrola faktur';
   K_WERSJE.CZY_SYS:='F';
   K_WERSJE.CZY_KALK:='N';
   K_WERSJE.K_W_OBL:=null;
   K_WERSJE.add()
?}


\get_knag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Ustawia (gdy brak dodaje) naglowek zagregowanych podzialow dla contrlingu
::   WE: _a - model
::       _b - wersja
::       _c - rok
::       _d - typ opisu
::  OLD: \get_knag/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
K__NAG.index('UNIK1');
K__NAG.prefix(REF.S_FIRMA,_a,_b,_c,null);
{? ~K__NAG.first()
|| K__NAG.cntx_psh(); K__NAG.index('UNIK'); K__NAG.prefix();
   _kod_nr:=
   {? K__NAG.last()
   || #K__NAG.KOD
   || -1
   ?}+1;
   _kod:=('00000'+$_kod_nr)+6;
   K__NAG.cntx_pop();
   K__NAG.FIRMA:=REF.S_FIRMA;
   K__NAG.KOD:=_kod;
   K__NAG.SKID_MB:=_a;
   K__NAG.K_WERSJE:=_b;
   K__NAG.K_PODWER:=null;
   K__NAG.ROK_F:=_c;
   K__NAG.OPIS:={? _d='Z'
                || 'Rezerwacje budżetu dla zapotrzebowań'
                |? _d='F'
                || 'Rezerwacje budżetu dla dokumentów w obiegu'
                || 'Wykonanie'
                ?};
   K__NAG.add()
|| 1
?}


\add_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Dodaje zagregowane zapisy controllingowe
::   WE: _a - okres
::       _b - mnoznik
::       _c - tabela z podzialami (EPODZ lub SKIDXD)
::  OLD: \add_poz/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
OKRO_F.prefix(); OKRO_F.seek(_a);
_maska:=exec('maska','control');
K__POZ.use('yb'+_maska);
K__POZ.index('FIRST5');
K__POZ.prefix(K__NAG.ref());
_kw:=0;
{? _b=-1
|| _suma:=0;
   K__POZ.cntx_psh();
   OKRO_F.index('ROK'); OKRO_F.prefix(K__NAG.ROK_F);
   {? OKRO_F.first()
   || {!
      |? K__POZ.use('yb'+exec('maska','control'));
         K__POZ.index('FIRST5');
         K__POZ.prefix(K__NAG.ref(),_c.PBUD,_c.JORG,_c.OKOSZ,_c.WYM4,_c.WYM5);
         {? K__POZ.first()
         || {!
            |? _suma+=K__POZ.WART;
               K__POZ.next()
            !}
         ?};
         OKRO_F.next()
      !}
   ?};
   K__POZ.cntx_pop()
?};
{? K__POZ.find_key(_c.PBUD,_c.JORG,_c.OKOSZ,_c.WYM4,_c.WYM5,_a)
|| {? _b=1
   || K__POZ.WART+=_c.WART;
      _kw:=_c.WART
   || {? _suma>_c.WART
      || K__POZ.WART-=_c.WART;
         _kw:=-_c.WART
      || _kw:=-_suma;
         K__POZ.WART-=_suma
      ?}
   ?};
   K__POZ.put()
|| K__POZ.blank(1);
   K__POZ.K__NAG:=K__NAG.ref();
   K__POZ.OKRES:=_a;
   K__POZ.WART:={? _b=1
                || _kw:=_c.WART
                || {? _suma>_c.WART
                   || K__POZ.WART-=_c.WART;
                      _kw:=-_c.WART
                   || _kw:=-_suma;
                      K__POZ.WART-=_suma
                   ?}
                ?};
   K__POZ.WYMIAR01:=_c.PBUD;
   K__POZ.WYMIAR02:=_c.JORG;
   K__POZ.WYMIAR03:=_c.OKOSZ;
   K__POZ.WYMIAR04:=_c.WYM4;
   K__POZ.WYMIAR05:=_c.WYM5;
   K__POZ.add()
?};
K__WAR.use('yx'+_maska);
K__WAR.index('K__POZ'); K__WAR.prefix(K__POZ.ref());
K__WAR.blank(1);
K__WAR.K__POZ:=K__POZ.ref();
{? _c=EPODZ
|| K__WAR.REF_DOK:=$EPODZ.ref();
   {? EPODZ.EDOKRZAP & EPODZ.EDOKRZAP().EZAPOT
   || EPODZ.cntx_psh();
      EDOKRZAP.cntx_psh();
      EDOKRZAP.prefix();
      _name:=ref_name(EPODZ.EDOKRZAP().EZAPOT);
      EPODZ.use('skid_j'+(_name+2));
      EPODZ.index('EDOKUM');
      EPODZ.prefix(EDOKRZAP.EZAPOT,EPODZ.SKID_MB,EPODZ.PBUD,EPODZ.JORG,EPODZ.OKOSZ,EPODZ.WYM4,EPODZ.WYM5);
      {? EPODZ.first()
      || K__WAR.REF_DOK2:=$EPODZ.ref()
      ?};
      EDOKRZAP.cntx_pop();
      EPODZ.cntx_pop()
   || K__WAR.REF_DOK2:=$EPODZ.ref()
   ?};
   K__WAR.DATA:=date();
   K__WAR.SYM:=EDOKUM.ID;
   K__WAR.OPIS:=EDOKUM.TR;
   K__WAR.KW:=_kw
|| K__WAR.DATA:=DOK.DTW;
   K__WAR.SYM:=DOK.NK;
   K__WAR.REJ:=DOK.REJ;
   K__WAR.POZ:=DOK.NR;
   K__WAR.OPIS:=DOK.TR;
   K__WAR.KW:=_kw;
   K__WAR.REF_DOK:=$DOK.ref();
   K__WAR.KON:=SKIDXD.AN
?};
OKRO_F.cntx_pop();
K__WAR.add()


\usun_rez_budz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Zdjecie rezerwacji budzetu dla zapotrzebowan
::   WE: _a - wersja
::       _b - rok
::       _c - tabela z podzialami (EPODZ lub SKIDXD)
::       _d - okres
::  OLD: \usun_rez_budz1/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
K__NAG.index('UNIK1');
{? _c.first()
|| {!
   |? _ok:=1;
      {? _c.SKID_MB().KONTROLA='T'
      || K__NAG.prefix(REF.S_FIRMA,SKID_MBN.ref(),_a,_b);
         {? K__NAG.first()
         || OKRO_F.cntx_psh();
            OKRO_F.prefix();
            OKRO_F.seek(_d);
            _maska:=exec('maska','control');
            K__POZ.use('yb'+_maska);
            K__WAR.use('yx'+_maska);
            K__WAR.index('REF_DOK'); K__WAR.prefix({? _c=EPODZ || $EPODZ.ref() || $DOK.ref() ?} );
            {? K__WAR.first()
            || K__POZ.prefix();
               {!
               |? K__WAR.K__POZ();
                  _kw:=K__WAR.KW;
                  K__WAR.del();
                  K__POZ.WART-=_kw;
                  K__WAR.cntx_psh();
                  K__WAR.index('K__POZ'); K__WAR.prefix(K__POZ.ref());
                  {? ~K__WAR.first()
                  || K__POZ.del()
                  || K__POZ.put()
                  ?};
                  K__WAR.cntx_pop();
                  K__WAR.first()
               !}
            ?};
            OKRO_F.cntx_pop()
         || _ok:=0
         ?}
      ?};
      _ok & _c.next()
   !};
   _ok
|| 1
?}


\show_budzet
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Pokazuje okno z budzetem
::   WE: _a - czy webterm? 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' || exec('env_wt','b_proces') ?};
{? exec('tab_con','obe_budz',_a)
|| exec('czytaj','#stalesys',,FINFO);
   _rok:=SSTALE.AR;
   {? _rok
   || ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
      _lata:=SSTALE.AR().NAZ;
      _lw:=UD_POM.K_MODEL().LW+1;
      {!
      |? _lw-=1;
         _lw & ROK_F.prev()
      !};
      {? _a=1
      || _ses:=exec('get_ses','obe_budz');
         exec('clean_k_bud','obiegi2',_ses);
         K_BUDN.index('UNIK');
         K_BUDN.prefix(_ses,)
      ?};
      _rokf:=exec('firma_rok','obe_budz',SSTALE.AR().POCZ_ROK);
      {? UD_POM.K_MODEL().KONTRPOZ='F' & _rokf.find_key(REF.FIRMA) |
         SKID_MBN.KONTRPOZ='G' & _rokf.first()
      || {!
         |? exec('gen_tab_con','obe_budz',_rokf.ROK_F,UD_POM.K2_SKL,UD_POM.K3_SKL,UD_POM.K4_SKL,UD_POM.K5_SKL,UD_POM.K1_SKL);
            SKID_MBN.KONTRPOZ='G' & _rokf.next()
         !}
      ?};
      _tyt:=': '+{? UD_POM.K_MODEL().LW=0 | ROK_F.NAZ=_lata || _lata || ROK_F.NAZ+' - '+_lata ?};
      exec('show_budzet2','obe_budz',_a,_tyt,2)
   ?}
?}


\show_budzet2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Pokazuje okno z budżetem
::   WE: _a - czy webterm? 1-tak 0-nie
::       _b - tytuł okna
::       _c - 1-przy kontroli 2-przy podgladzie budzetu
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| _tcid:=app_info('web_tcid');
   _webses:=app_info('web_sesid');
   _ses:=_tcid+_webses;
   K_BUDN.index('UNIK'); K_BUDN.prefix(_ses,);
   K_BUDN.web_select('GRP'+$_c)
|| {? exec('set_kontr_win','obe_budz',_c,_a)
   || __KONTR.hdr_sel();
      __KONTR.hdr_sel(_b);
      __KONTR.select()
   ?}
?}


\spac_budzet
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Na wyświetl dla modelu - budżet
::----------------------------------------------------------------------------------------------------------------------
K_BUDN.web_display('RED',,"K_BUDN.web_eclose(); web_top_refresh(1)")


\tab_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Tworzy tabele na dane do kontroli
::   WE: _a - czy webterm? 1-tak 0-nie
::  OLD: \tab_con/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
exec('tab_con_del','obe_budz');
__KONTR:=null;
SKID_MBN.index('KONTROLA'); SKID_MBN.prefix('T');
{? SKID_MBN.first()
|| K_WERSJE.index('CZY_SYS'); K_WERSJE.prefix();
   __wwykon:={? K_WERSJE.find_key('T') || K_WERSJE.ref() || null ?};
   __w_zap:={? K_WERSJE.find_key('Z') || K_WERSJE.ref() || null ?};
   __w_ob:={? K_WERSJE.find_key('F') || K_WERSJE.ref() || null ?};
   __Tkon:=obj_new(5);
   __Tsym:=obj_new(5);
   __Tsym0:=obj_new(5);
   {? _a=1
   || __KONTR:=K_BUDN;
      __KONTR1:='UNIK';
      __KONTR2:='OK';
      __KONTRP:=K_BUDP;
      K_BUDP.index('UNIK'); K_BUDP.prefix();
      _ses:=exec('get_ses','obe_budz');
      K_BUDN.index('UNIK'); K_BUDN.prefix(_ses)
   || __KONTR:=tab_tmp(6,
         'MODEL','STRING[8]','Model',
         'W1','STRING[16]','Wym. 1',
         'W2','STRING[16]','Wym. 2',
         'W3','STRING[16]','Wym. 3',
         'W4','STRING[16]','Wym. 4',
         'W5','STRING[16]','Wym. 5',
         'R1','INTEGER',,
         'R2','INTEGER',,
         'R3','INTEGER',,
         'R4','INTEGER',,
         'R5','INTEGER',,
         'B','REAL','Budżet',
         'Z','REAL','Zapotrzeb.',
         'F','REAL','Faktury',
         'D','REAL','Dokumenty',
         'SUM','REAL','Suma',
         'NOWE','REAL','Dodane',
         'PROC','REAL','% wyk.',
         'R','REAL','Różnica',
         'P','REAL','Pozostało',
         'OK','REAL','OK',
         'IW','STRING[1]','Ilość/wartość'
      );
      __KONTR1:=__KONTR.index('?');
      __KONTR2:=__KONTR.ndx_tmp('',1,'OK',,0);
      __KONTRP:=tab_tmp(9,
         'KONTR','INTEGER',,
         'TYP','STRING[20]','Typ',
         'ROK','STRING[20]','Rok',
         'FIRMA','STRING[4]','Firma',
         'R1','INTEGER','R 1',
         'R2','INTEGER','R 2',
         'R3','INTEGER','R 3',
         'R4','INTEGER','R 4',
         'R5','INTEGER','R 5',
         'W1','STRING[16]','Wym. 1',
         'W2','STRING[16]','Wym. 2',
         'W3','STRING[16]','Wym. 3',
         'W4','STRING[16]','Wym. 4',
         'W5','STRING[16]','Wym. 5',
         'WART','REAL','Wartość',
         'WER','INTEGER',,
         'ROK_REF','INTEGER',
      )
   ?};
   __KONTRF:=tab_tmp(6,
      'ROK','INTEGER',,
      'W1','STRING[16]',,
      'W2','STRING[16]',,
      'W3','STRING[16]',,
      'W4','STRING[16]',,
      'W5','STRING[16]',
   );
   1
?}


\tab_con_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Usuniecie tabel zwiazanych z kontrola
::  OLD: \tab_con_del/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('skid_mbn_kon','__KONTR','__KONTRP','__Tsym','__Tsym0','__Tkon','__KONTR1','__KONTR2');
VAR_DEL.delete('__KONTRF');
VAR_DEL.delete('__wwykon','__w_zap','__w_ob')


\gen_tab_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Tworzy tabele z danymi do kontroli
::   WE: _a   - rok
::       _b   - wymiar 2
::       _c   - wymiar 3
::       _d   - wymiar 4
::       _e   - wymiar 5
::       _f.. - wymiary 1
::  OLD: \gen_tab_con/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
UD_SKL.cntx_psh();
UD_SKL.index('SYMBOL'); UD_SKL.prefix();
__Tsym0[2]:={? UD_SKL.seek(_b) || UD_SKL.SYMBOL || '' ?};
__Tsym0[3]:={? UD_SKL.seek(_c) || UD_SKL.SYMBOL || '' ?};
__Tsym0[4]:={? UD_SKL.seek(_d) || UD_SKL.SYMBOL || '' ?};
__Tsym0[5]:={? UD_SKL.seek(_e) || UD_SKL.SYMBOL || '' ?};
SKID_MBP.index('LP');
SKID_MBP.prefix(SKID_MBN.ref());
{? SKID_MBP.first()
|| {! _i:=1..5
   |! __Tkon[_i]:=0;
      __Tsym[_i]:=''
   !};
   {!
   |? __Tkon[SKID_MBP.LP]:=SKID_MBP.KONTROLA='T';
      {? SKID_MBP.LP<>1
      || __Tsym[SKID_MBP.LP]:={? __Tkon[SKID_MBP.LP] || __Tsym0[SKID_MBP.LP] || '' ?}
      ?};
      SKID_MBP.next()
   !}
?};
{! _v:=6.._
|! _pbud:=_[_v];
   {? UD_SKL.seek(_pbud)
   || __Tsym0[1]:=UD_SKL.SYMBOL;
      SKIDXDUD.index('POZ'); SKIDXDUD.prefix(UD_SKL.ref());
      _iw:={? SKIDXDUD.first()
           || _iw:=SKIDXDUD.WART_IL
           || _iw:=''
           ?}
   || __Tsym0[1]:=_iw:=''
   ?};
   __Tsym[1]:={? __Tkon[1] || __Tsym0[1] || '' ?};
   _lw:=SKID_MBN.LW+1;
   ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ'); ROK_F.prefix();
   {? type_of(_a)=type_of(0) & ROK_F.seek(_a,) | type_of(_a)=type_of(null) & ROK_F.seek(_a)
   || ROK_F.prefix(ROK_F.FIRMA);
      {!
      |? _lw-=1;
         {? ~__KONTRF.find_key(ROK_F.ref(),__Tsym[1],__Tsym[2],__Tsym[3],__Tsym[4],__Tsym[5],)
         || __KONTRF.ROK:=ROK_F.ref();
            __KONTRF.W1:=__Tsym[1];
            __KONTRF.W2:=__Tsym[2];
            __KONTRF.W3:=__Tsym[3];
            __KONTRF.W4:=__Tsym[4];
            __KONTRF.W5:=__Tsym[5];
            __KONTRF.add();
            ROK_F.cntx_psh();
            {? FINFO.KBUDZET='T' || exec('gen_tab_con1','obe_budz',ROK_F.ref(),_pbud,_b,_c,_d,_e,'B',FINFO.KWERBUD,_iw) ?};
            {? FINFO.KZAPOT='T' || exec('gen_tab_con1','obe_budz',ROK_F.ref(),_pbud,_b,_c,_d,_e,'Z',__w_zap,_iw) ?};
            {? FINFO.KFAKTURY='T' || exec('gen_tab_con1','obe_budz',ROK_F.ref(),_pbud,_b,_c,_d,_e,'F',__w_ob,_iw) ?};
            {? FINFO.KDOKKS='T' || exec('gen_tab_con1','obe_budz',ROK_F.ref(),_pbud,_b,_c,_d,_e,'D',__wwykon,_iw) ?};
            ROK_F.cntx_pop()
         ?};
         _lw & ROK_F.prev()
      !}
   ?};
   ROK_F.cntx_pop()
!};
UD_SKL.cntx_pop()


\gen_tab_con1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Uzupelnia tabele z danymi do kontroli
::   WE: _a - rok
::       _b - pozycja budzetowa
::       _c - jednostka organizacyjna
::       _d -
::       _e -
::       _f -
::       _g - pole do uzupelnienia
::       _h - wersja budzetu
::       _i - 'I'losc lub 'W'artosc
::  OLD: \gen_tab_con1/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
ROK_F.prefix();
_firma:={? ROK_F.seek(_a) || ROK_F.FIRMA || REF.S_FIRMA ?};
ROK_F.cntx_pop();
K__NAG.index('UNIK1');
K__NAG.prefix(_firma,SKID_MBN.ref(),_h,_a);
{? K__NAG.first()
|| OKRO_F.index('ROK'); OKRO_F.prefix(_a);
   {? OKRO_F.first()
   || {!
      |? _maska:=exec('maska','control');
         K__POZ.use('yb'+_maska);
         K__POZ.index('FIRST5');
         K__POZ.prefix(K__NAG.ref(),_b);
         {? K__POZ.first()
         || _wnaz:={? _h=FINFO.KWERBUD
                   || 'Budżet'
                   |? _h=__w_zap
                   || 'Zapotrzebowania'
                   |? _h=__w_ob
                   || 'Faktury w obiegu'
                   || 'Dokumenty księgowe'
                   ?};
            {!
            |? {? (_b=null | K__POZ.WYMIAR01=_b) &
                  (__Tkon[2]=0 | __Tkon[2] & (_c=null | K__POZ.WYMIAR02=_c) ) &
                  (__Tkon[3]=0 | __Tkon[3] & (_d=null | K__POZ.WYMIAR03=_d) ) &
                  (__Tkon[4]=0 | __Tkon[4] & (_e=null | K__POZ.WYMIAR04=_e) ) &
                  (__Tkon[5]=0 | __Tkon[5] & (_f=null | K__POZ.WYMIAR05=_f) )
               || {? __KONTR.find_key(SKID_MBN.KOD,__Tsym[1],__Tsym[2],__Tsym[3],__Tsym[4],__Tsym[5],)
                  || _v:=($('__KONTR.'+_g))();
                     ($('__KONTR.'+_g+':=_a'))(_v+K__POZ.WART);
                     exec('upd_tab_con','obe_budz');
                     __KONTR.put();
                     _ref:=__KONTR.ref()
                  || __KONTR.blank(1);
                     __KONTR.MODEL:=SKID_MBN.KOD;
                     __KONTR.W1:=__Tsym[1];
                     __KONTR.W2:=__Tsym[2];
                     __KONTR.W3:=__Tsym[3];
                     __KONTR.W4:=__Tsym[4];
                     __KONTR.W5:=__Tsym[5];
                     {? __Tkon[1] & _b || __KONTR.R1:=_b ?};
                     {? __Tkon[2] & _c || __KONTR.R2:=_c ?};
                     {? __Tkon[3] & _d || __KONTR.R3:=_d ?};
                     {? __Tkon[4] & _e || __KONTR.R4:=_e ?};
                     {? __Tkon[5] & _f || __KONTR.R5:=_f ?};
                     __KONTR.IW:=_i;
                     {? app_info('web_sesid')<>''
                     || __KONTR.SESID:=exec('get_ses','obe_budz')
                     ?};
                     ($('__KONTR.'+_g+':=_a'))(K__POZ.WART);
                     exec('upd_tab_con','obe_budz');
                     __KONTR.cntx_psh(); __KONTR.prefix(); __KONTR.add(); _ref:=__KONTR.ref(); __KONTR.cntx_pop()
                  ?};
                  {? __KONTRP.find_key(_ref,_wnaz,ROK_F.NAZ,ROK_F.FIRMA().SYMBOL,#K__POZ.WYMIAR01,#K__POZ.WYMIAR02,#K__POZ.WYMIAR03,#K__POZ.WYMIAR04,#K__POZ.WYMIAR05)
                  || __KONTRP.WART+=K__POZ.WART;
                     __KONTRP.put()
                  || __KONTRP.KONTR:=_ref;
                     __KONTRP.ROK:=ROK_F.NAZ;
                     __KONTRP.ROK_REF:=#ROK_F.ref();
                     __KONTRP.FIRMA:=FIRMA.SYMBOL;
                     __KONTRP.TYP:=_wnaz;
                     __KONTRP.WER:=_h;
                     __KONTRP.R1:=#K__POZ.WYMIAR01;
                     __KONTRP.R2:=#K__POZ.WYMIAR02;
                     __KONTRP.R3:=#K__POZ.WYMIAR03;
                     __KONTRP.R4:=#K__POZ.WYMIAR04;
                     __KONTRP.R5:=#K__POZ.WYMIAR05;
                     __KONTRP.W1:=K__POZ.WYMIAR01().SYMBOL;
                     __KONTRP.W2:=K__POZ.WYMIAR02().SYMBOL;
                     __KONTRP.W3:=K__POZ.WYMIAR03().SYMBOL;
                     __KONTRP.W4:=K__POZ.WYMIAR04().SYMBOL;
                     __KONTRP.W5:=K__POZ.WYMIAR05().SYMBOL;
                     __KONTRP.WART:=K__POZ.WART;
                     __KONTRP.add()
                  ?}
               ?};
               K__POZ.next()
            !}
         ?};
         OKRO_F.next()
      !}
   ?}
?}


\upd_tab_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Aktualizacja danych
::  OLD: \upd_tab_con/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
__KONTR.SUM:=__KONTR.Z+__KONTR.F+__KONTR.D;
__KONTR.PROC:={? __KONTR.B=0 || 1000 || ((__KONTR.SUM+__KONTR.NOWE)/__KONTR.B*100)$2 ?};
__KONTR.R:=__KONTR.B-__KONTR.SUM-__KONTR.NOWE;
__KONTR.OK:={? __KONTR.IW='I'
            || {? 'AP'*FINFO.KBTYPI>0 & __KONTR.PROC>FINFO.KBI_PROC+100 |
                  'AW'*FINFO.KBTYPI>0 & -__KONTR.R>FINFO.KBI_WART
               || __KONTR.OK:=2
               |? 'AP'*FINFO.KWTYPI>0 & __KONTR.PROC>FINFO.KWI_PROC+100 |
                  'AW'*FINFO.KWTYPI>0 & -__KONTR.R>FINFO.KWI_WART
               || __KONTR.OK:=1
               || __KONTR.OK:=0
               ?}
            || {? 'AP'*FINFO.KBTYPW>0 & __KONTR.PROC>FINFO.KBW_PROC+100 |
                  'AW'*FINFO.KBTYPW>0 & -__KONTR.R>FINFO.KBW_WART
               || __KONTR.OK:=2
               |? 'AP'*FINFO.KWTYPW>0 & __KONTR.PROC>FINFO.KWW_PROC+100 |
                  'AW'*FINFO.KWTYPW>0 & -__KONTR.R>FINFO.KWW_WART
               || __KONTR.OK:=1
               || __KONTR.OK:=0
               ?}
            ?}


\set_kontr_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Ustawia okno dla prezentacji budzetu
::   WE: _a - typ 1 lub 2
::       _b - czy webterm? 1-tak 0-nie
::  OLD: \set_kontr_win/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
{? type_of(__KONTR)<100
|| FUN.info('W systemie nie znaleziono modeli do kontroli budżetu.'@);
   return(0)
?};
kontrT:=_a;
_o:=__KONTR.mk_sel('Kontrola budżetu'@,'P',,'#kontrolab'+_a,,,,,'U');
__KONTR.win_fld(_o,,'MODEL');
__KONTR.win_fld(_o,,'W1',,,6);
__KONTR.fld_opt(_o,'col_name="%1"'[UD_POM.KH1],,'W1');
__KONTR.win_fld(_o,,'W2',,,6);
__KONTR.fld_opt(_o,'col_name="%1"'[UD_POM.KH2],,'W2');
__KONTR.win_fld(_o,,'W3',,,6);
__KONTR.fld_opt(_o,'col_name="%1"'[UD_POM.KH3],,'W3');
__KONTR.win_fld(_o,,'W4',,,6);
__KONTR.fld_opt(_o,'col_name="%1"'[UD_POM.KH4],,'W4');
__KONTR.win_fld(_o,,'W5',,,6);
__KONTR.fld_opt(_o,'col_name="%1"'[UD_POM.KH5],,'W5');
{? FINFO.KBUDZET='T' || __KONTR.win_fld(_o,,'B',,,10) ?};
{? FINFO.KZAPOT='T' || __KONTR.win_fld(_o,,'Z',,,10) ?};
{? FINFO.KFAKTURY='T' || __KONTR.win_fld(_o,,'F',,,10) ?};
{? FINFO.KDOKKS='T' || __KONTR.win_fld(_o,,'D',,,10) ?};
{? _a=1 || __KONTR.win_fld(_o,,'NOWE',,,10) ?};
__KONTR.win_fld(_o,,'PROC',,,10);
{? _a=1
|| __KONTR.win_fld(_o,,'R',,,10)
|| __KONTR.win_fld(_o,,'R',,,10,,,'Pozostało'@)
?};
__KONTR.win_act(_o,,'Kolejność');
__KONTR.win_act(_o,,'Formuła','Legenda'@@,,,"
   exec('legenda','color','__KONTR#01')
",,1);
__KONTR.win_act(_o,,'Rekord',,,,"
   {? __lst_mod<>__KONTR.MODEL
   || __lst_mod:=__KONTR.MODEL;
      {! _i:=1..5
      |! ($('UD_POM.KH'+$_i+':=\'\'\'\''))()
      !};
      SKID_MBN.cntx_psh();
      SKID_MBN.index('KOD'); SKID_MBN.prefix(__KONTR.MODEL,);
      {? SKID_MBN.first()
      || SKID_MBP.cntx_psh();
         SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
         {? SKID_MBP.first()
         || {!
            |? ($('UD_POM.KH'+$SKID_MBP.LP+':=_a'))(SKID_MBP.NAZ);
               SKID_MBP.next()
            !}
         ?};
         SKID_MBP.cntx_pop()
      ?};
      SKID_MBN.cntx_pop()
   ?};
   {? __KONTR.OK=0
   || ''
   || Color.fnd_kol('__KONTR#01#0'+$__KONTR.OK)
   ?}
");
__KONTR.win_act(_o,,'Wyświetl',,,,"
   undefine();
   define('MODEL',__KONTR.MODEL,'Model',,8);
   SKID_MBN.cntx_psh(); SKID_MBP.cntx_psh();
   SKID_MBN.index('KOD'); SKID_MBN.prefix(__KONTR.MODEL,);
   {? SKID_MBN.first()
   || UD_SKL.prefix();
      SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
      {? SKID_MBP.first()
      || {!
         |? _ref:=($('__KONTR.R'+$SKID_MBP.LP))();
            {? _ref & UD_SKL.seek(_ref,)
            || _sym:=UD_SKL.SYMBOL;
               _opis:=UD_SKL.OPIS
            || _sym:=_opis:=''
            ?};
            {? _sym<>''
            || define('W'+$SKID_MBP.LP,_sym,SKID_MBP.NAZ,,16);
               define('O'+$SKID_MBP.LP,_opis,'',,100)
            ?};
            SKID_MBP.next()
         !}
      ?}
   ?};
   SKID_MBN.cntx_pop(); SKID_MBP.cntx_pop();
   {? FINFO.KBUDZET='T' || define('B',__KONTR.B,'Budżet'@,,16,,2) ?};
   {? FINFO.KZAPOT='T' || define('Z',__KONTR.Z,'Zapotrzebowania'@,,16,,2) ?};
   {? FINFO.KFAKTURY='T' || define('F',__KONTR.F,'Faktury'@,,16,,2) ?};
   {? FINFO.KDOKKS='T' || define('D',__KONTR.Z,'Dokumenty'@,,16,,2) ?};
   {? kontrT=1 || define('NOWE',__KONTR.NOWE,'Dodane'@,,16,,2) ?};
   define('PROC',__KONTR.PROC,'%% wykonania'@,,16,,2);
   define('R',__KONTR.R,'Pozostało'@,,16,,2);
   def_disp('Kontrola budżetu'@);
   undefine()
");
_o2:=__KONTRP.mk_sel('Pozycje kontroli'@,'P',,'#kontrolabp',,,,,'U');
__KONTRP.win_fld(_o2,,'FIRMA',,,3);
__KONTRP.win_fld(_o2,,'ROK',,,5);
__KONTRP.win_fld(_o2,,'TYP',,,16);
SKID_MBP.cntx_psh();
SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
{? SKID_MBP.first()
|| {!
   |? __KONTRP.win_fld(_o2,,'W'+$SKID_MBP.LP,,,,,,SKID_MBP.NAZ);
      SKID_MBP.next()
   !}
?};
SKID_MBP.cntx_pop();
__KONTRP.win_fld(_o2,,'WART');
__KONTRP.win_act(_o2,,'Formuła','Szczegóły'@@,,,"exec('pokaz_budz_poz2','obe_budz',0)",,1);
__KONTRP.win_act(_o2,,'Kolejność');
__KONTRP.win_act(_o2,,'Wyświetl',,,,"
   undefine();
   define('TYP',__KONTRP.TYP,'Typ',,20);
   SKID_MBN.cntx_psh(); SKID_MBP.cntx_psh();
   SKID_MBN.index('KOD'); SKID_MBN.prefix(__KONTR.MODEL,);
   {? SKID_MBN.first()
   || UD_SKL.prefix();
      SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
      {? SKID_MBP.first()
      || {!
         |? _ref:=($('__KONTRP.R'+$SKID_MBP.LP))();
            {? _ref & UD_SKL.seek(_ref,)
            || _sym:=UD_SKL.SYMBOL;
               _opis:=UD_SKL.OPIS
            || _sym:=_opis:=''
            ?};
            {? _sym<>''
            || define('W'+$SKID_MBP.LP,_sym,SKID_MBP.NAZ,,16);
               define('O'+$SKID_MBP.LP,_opis,'',,100)
            ?};
            SKID_MBP.next()
         !}
      ?}
   ?};
   SKID_MBN.cntx_pop(); SKID_MBP.cntx_pop();
   define('WART',__KONTRP.WART,'Wartość',,16,,2);
   def_disp('Pozycja kontroli'@);
   undefine()
");
__KONTRP.win_sel(_o2);
_o3:=__KONTR.grp_make('Kontrola budżetu'@,,'#kontrgrp');
__KONTR.grp_sel(_o3,,_o,,"
   {? __KONTR.get()
   || __KONTRP.prefix(__KONTR.ref())
   || __KONTRP.prefix(0)
   ?};
   grp_disp(__KONTRP,__KONTRP.win_sel('?'))
",,,10,,,,,'maximized_with_title');
__KONTR.grp_splt(_o3,,'horizontal','bottom',);
__KONTR.grp_sel(_o3,__KONTRP,_o2,,"",,,20,,,,,'maximized_with_title');
__KONTR.win_sel(_o3);
__lst_mod:='';
1


\pokaz_budz_poz2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Prezentacja pozycji kontroli budzetu - szczegoly
::   WE: _a - czy webterm? 1-tak 0-nie
::  OLD: \pokaz_budz_poz2/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__KONTRS');
{? _a=1
|| _ses:=exec('get_ses','obe_budz');
   __KONTRS:=K_BUDS;
   K_BUDS.index('SYM'); K_BUDS.prefix(_ses);
   {? K_BUDS.first() || {! |? K_BUDS.del() !} ?};
   VAR_DEL.delete('__KONTRP');
   __KONTRP:=K_BUDP
|| __KONTRS:=tab_tmp(2,
      'DATA','DATE','Data',
      'SYM','STRING[20]','Symbol',
      'OPIS','STRING[200]','Opis',
      'WART','REAL','Wartość',
      'REF','INTEGER',
   )
?};
ROK_F.cntx_psh();
ROK_F.prefix();
_firma:={? ROK_F.seek(__KONTRP.ROK_REF,) || ROK_F.FIRMA || REF.S_FIRMA ?};
ROK_F.cntx_pop();
K__NAG.index('UNIK1');
K__NAG.prefix(_firma,SKID_MBN.ref(),__KONTRP.WER,__KONTRP.ROK_REF);
{? K__NAG.first()
|| OKRO_F.index('ROK'); OKRO_F.prefix(K__NAG.ROK_F);
   {? OKRO_F.first()
   || {!
      |? _maska:=exec('maska','control');
         K__POZ.use('yb'+_maska);
         K__POZ.index('FIRST5');
         K__POZ.prefix(K__NAG.ref(),__KONTRP.R1,__KONTRP.R2,__KONTRP.R3,__KONTRP.R4,__KONTRP.R5);
         K__WAR.use('yx'+_maska);
         K__WAR.index('K__POZ');
         {? K__POZ.first()
         || {!
            |? K__WAR.prefix(K__POZ.ref());
               {? K__WAR.first()
               || {!
                  |? __KONTRS.DATA:=K__WAR.DATA;
                     __KONTRS.SYM:=K__WAR.SYM;
                     __KONTRS.OPIS:=K__WAR.OPIS;
                     __KONTRS.WART:=K__WAR.KW;
                     __KONTRS.REF:=K__WAR.ref();
                     {? _a=1
                     || __KONTRS.SESID:=_ses
                     ?};
                     __KONTRS.add();
                     K__WAR.next()
                  !}
               ?};
               K__POZ.next()
            !}
         ?};
         OKRO_F.next()
      !}
   ?};
   {? _a=1
   || VAR_DEL.delete('__KONTRS');
      K_BUDS.index('SYM'); K_BUDS.prefix(_ses);
      K_BUDS.web_select('WER')
   || _o:=__KONTRS.mk_sel('Szczegóły'@,'P',,'#kontrolabs',,,,,'U');
      __KONTRS.win_fld(_o,,'DATA');
      __KONTRS.win_fld(_o,,'SYM');
      __KONTRS.win_fld(_o,,'OPIS',,,50);
      __KONTRS.win_fld(_o,,'WART');
      __KONTRS.win_act(_o,,'Wyświetl',,,,"exec('bv_kontrs','obe_budz')");
      __KONTRS.win_sel(_o);
      __KONTRS.select();
      VAR_DEL.delete('__KONTRS')
   ?}
?}


\bv_kontrs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Przed wyswietl okna z szczegolami kontroli
::  OLD: \bv_kontrs/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
K__WAR.prefix();
{? K__WAR.seek(__KONTRS.REF,)
|| {? 6+K__WAR.REF_DOK='skid_j'
   || EPODZ.cntx_psh(); EPODZ.prefix();
      {? EPODZ.seek(BIT.sqlint(K__WAR.REF_DOK),)
      || EDOKUM.cntx_psh();
         EPODZ.EDOKUM();
         {? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur'
         || EDOKUM.win_edit('WPR')
         || EDOKUM.win_edit('KSW')
         ?};
         EDOKUM.display();
         EDOKUM.cntx_pop()
      ?};
      EPODZ.cntx_pop()
   |? 4+K__WAR.REF_DOK='doku'
   || DOK.cntx_psh(); DOK.prefix();
      {? DOK.seek(BIT.sqlint(K__WAR.REF_DOK),8+K__WAR.REF_DOK)
      || exec('dok_spac','dok_fks')
      ?};
      DOK.cntx_pop()
   ?}
?}


\chk_budz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Kontrola budzetu przy akceptacji zapotrzebowan
::   WE: _a - czy z webterma? 1-tak 0-nie
::       _b - pokazywać komunikaty? 1-tak 0-nie
::  OLD: \chk_zapot/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~_ || _a:=0 ?};
_ok:=1;
exec('tab_con_del','obe_budz');
{? _a
|| exec('clean_k_bud','obiegi2',exec('get_ses','obe_budz'))
?};
{? exec('tab_con','obe_budz',_a)
|| EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
   {? EPODZ.first()
   || _rok:=exec('firma_rok','obe_budz',EDOKUM.DOP);
      {!
      |? {? EPODZ.SKID_MB().KONTROLA='T'
         || {? SKID_MBN.KONTRPOZ='F' & _rok.find_key(REF.S_FIRMA) |
               SKID_MBN.KONTRPOZ='G' & _rok.first()
            || {!
               |? exec('gen_tab_con','obe_budz',_rok.ROK_F,EPODZ.JORG,EPODZ.OKOSZ,EPODZ.WYM4,EPODZ.WYM5,EPODZ.PBUD);
                  SKID_MBN.KONTRPOZ='G' & _rok.next()
               !}
            ?}
         ?};
         EPODZ.prefix(EDOKUM.ref(),EPODZ.SKID_MB,EPODZ.PBUD,EPODZ.JORG,EPODZ.OKOSZ,EPODZ.WYM4,EPODZ.WYM5);
         EPODZ.last();
         EPODZ.prefix(EDOKUM.ref());
         EPODZ.next()
      !};
      EPODZ.first();
      {!
      |? {? EPODZ.SKID_MB().KONTROLA='T'
         || exec('add_podz','obe_budz',EPODZ)
         ?};
         EPODZ.next()
      !}
   ?};
   __KONTR.index(__KONTR2);
   {? _a || __KONTR.prefix(exec('get_ses','obe_budz')) || __KONTR.prefix() ?};
   budz_cond:=~__KONTR.find_key(2) & __KONTR.find_key(1);
   {? _a | _b=0
   || __KONTR.index(__KONTR2);
      {? _a || __KONTR.prefix(exec('get_ses','obe_budz')) || __KONTR.prefix() ?};
      _ok:={? __KONTR.find_key(2) || 2 |? __KONTR.find_key(1) || 1 || 0 ?}
   || exec('set_kontr_win','obe_budz',1);
      __KONTR.index(__KONTR2);
      {? __KONTR.find_key(2)
      || {!
         |? _pyt:=choice('Przekroczono budżet (%1).'@[__KONTR.MODEL],'Kontrola budżetu'@,,,1,2,'Szczegóły'@,'Anuluj'@);
            {? _pyt=0
            || __KONTR.index(__KONTR1); __KONTR.prefix();
               __KONTR.select();
               1
            ?}
         !};
         _ok:=2
      |? __KONTR.find_key(1)
      || {!
         |? _pyt:=choice('Przekroczono budżet (%1).'@[__KONTR.MODEL],'Kontrola budżetu'@,,,1,3,'Szczegóły'@,'Kontynuuj'@,'Anuluj'@);
            {? _pyt=0
            || __KONTR.index(__KONTR1); __KONTR.prefix();
               __KONTR.select();
               1
            ?}
         !};
         _ok:={? _pyt=1 || 0 || 1 ?}
      ?}
   ?}
|| {? _a=1 || _ok:=0 ?}
?};
_ok


\add_podz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Dodanie do tabeli z kontrola zapisow z zapotrzebowan
::   WE: _a - tabela z podzialami (EPODZ lub SKIDXD)
::  OLD: \add_podz/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a.SKID_MB().KONTROLA='T'
|| _wym:=obj_new(5);
   _wym[1]:=_a.PBUD().SYMBOL;
   SKIDXDUD.index('POZ'); SKIDXDUD.prefix(UD_SKL.ref());
   _iw:={? SKIDXDUD.first() || SKIDXDUD.WART_IL || '' ?};
   _wym[2]:=_a.JORG().SYMBOL;
   _wym[3]:=_a.OKOSZ().SYMBOL;
   _wym[4]:=_a.WYM4().SYMBOL;
   _wym[5]:=_a.WYM5().SYMBOL;
   SKID_MBP.cntx_psh();
   SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
   {? SKID_MBP.first()
   || {!
      |? {? SKID_MBP.KONTROLA='N' || _wym[SKID_MBP.LP]:='' ?};
         SKID_MBP.next()
      !}
   ?};
   SKID_MBP.cntx_pop();
   {? __KONTR.find_key(SKID_MBN.KOD,_wym[1],_wym[2],_wym[3],_wym[4],_wym[5],)
   || __KONTR.NOWE+=_a.WART;
      {? _a=SKIDXD
      || {? __KONTR.F>_a.WART
         || __KONTR.F-=_a.WART
         || __KONTR.F:=0
         ?}
      |? _a.EDOKRZAP
      || {? __KONTR.Z>_a.WART
         || __KONTR.Z-=_a.WART
         || __KONTR.Z:=0
         ?}
      ?};
      exec('upd_tab_con','obe_budz');
      __KONTR.put()
   || __KONTR.blank(1);
      __KONTR.MODEL:=SKID_MBN.KOD;
      __KONTR.W1:=_wym[1];
      __KONTR.W2:=_wym[2];
      __KONTR.W3:=_wym[3];
      __KONTR.W4:=_wym[4];
      __KONTR.W5:=_wym[5];
      {? _wym[1]<>'' || __KONTR.R1:=_a.PBUD ?};
      {? _wym[2]<>'' || __KONTR.R2:=_a.JORG ?};
      {? _wym[3]<>'' || __KONTR.R3:=_a.OKOSZ ?};
      {? _wym[4]<>'' || __KONTR.R4:=_a.WYM4 ?};
      {? _wym[5]<>'' || __KONTR.R5:=_a.WYM5 ?};
      __KONTR.IW:=_iw;
      __KONTR.NOWE:=_a.WART;
      {? app_info('web_sesid')<>''
      || __KONTR.SESID:=exec('get_ses','obe_budz')
      ?};
      exec('upd_tab_con','obe_budz');
      __KONTR.cntx_psh(); __KONTR.prefix(); __KONTR.add(); __KONTR.cntx_pop()
   ?}
?}


\get_ses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Znacznik sesji
::----------------------------------------------------------------------------------------------------------------------
app_info('web_tcid')+app_info('web_sesid')


\chk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Kontrola budzetu przy akceptacji dokumentu
::   WE: _a - czy wyswietlac komunikat
::  OLD: \chk_dok/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? exec('tab_con','obe_budz',0)
|| SKIDXD.index('SKIDXD'); SKIDXD.prefix(DOK.ref());
   {? SKIDXD.first()
   || _rok:=exec('firma_rok','obe_budz',SSTALE.AR().POCZ_ROK);
      {!
      |? {? SKIDXD.SKID_MB().KONTROLA='T'
         || {? SKID_MBN.KONTRPOZ='F' & _rok.find_key(REF.S_FIRMA) |
               SKID_MBN.KONTRPOZ='G' & _rok.first()
            || {!
               |? exec('gen_tab_con','obe_budz',_rok.ROK_F,SKIDXD.JORG,SKIDXD.OKOSZ,SKIDXD.WYM4,SKIDXD.WYM5,SKIDXD.PBUD);
                  SKID_MBN.KONTRPOZ='G' & _rok.next()
               !}
            ?}
         ?};
         SKIDXD.prefix(DOK.ref(),SKIDXD.SKID_MB,SKIDXD.PBUD,SKIDXD.JORG,SKIDXD.OKOSZ,SKIDXD.WYM4,SKIDXD.WYM5);
         SKIDXD.last();
         SKIDXD.prefix(DOK.ref());
         SKIDXD.next()
      !};
      SKIDXD.first();
      {!
      |? {? SKIDXD.SKID_MB().KONTROLA='T'
         || exec('add_podz','obe_budz',SKIDXD)
         ?};
         SKIDXD.next()
      !}
   ?};
   __KONTR.index(__KONTR2);
   {? _a=0
   || _ok:=~__KONTR.find_key(2) & ~__KONTR.find_key(1)
   |? __KONTR.find_key(2)
   || exec('set_kontr_win','obe_budz',1);
      {!
      |? _pyt:=choice('Przekroczono budżet.'@,'Kontrola budżetu'@,,,1,2,'Szczegóły'@,'OK'@);
         {? _pyt=0
         || __KONTR.index(__KONTR1); __KONTR.prefix();
            __KONTR.select();
            1
         ?}
      !};
      _ok:=0
   |? __KONTR.find_key(1)
   || exec('set_kontr_win','obe_budz',1);
      {!
      |? _pyt:=choice('Przekroczono budżet.'@,'Kontrola budżetu'@,,,1,3,'Szczegóły'@,'Kontynuuj'@,'Anuluj'@);
         {? _pyt=0
         || __KONTR.index(__KONTR1); __KONTR.prefix();
            __KONTR.select();
            1
         ?}
      !};
      _ok:=_pyt=1
   ?}
?};
exec('tab_con_del','obe_budz');
_ok


\rez_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Rezerwacja budzetu dla dokumentow ksiegowych
::   WY: 0 - nie dalo sie wykonac rezerwacji na budzecie
::       1 - nie dotyczy rezerwacji
::       2 - zaktualizowano wartosci w rezerwacji i wykonaniu
::  OLD: \rez_dok/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
SKIDXD.index('SKIDXD'); EPODZ.prefix(DOK.ref());
{? SKIDXD.first()
|| K_WERSJE.index('CZY_SYS'); K_WERSJE.prefix();
   _ref_wer:=null;
   _wer_typ:='';
   {? K_WERSJE.find_key('F')
   || _ref_wer:=K_WERSJE.ref();
      _wer_typ:='F'
   ?};
   _ok:=1;
   {? K_WERSJE.find_key('T')
   || {!
      |? {? SKIDXD.SKID_MB().KONTROLA='T'
         || _ok:=0;
            {? exec('get_knag','obe_budz',SKID_MBN.ref(),K_WERSJE.ref(),SSTALE.AR,'D')
            || exec('add_poz','obe_budz',SSTALE.AO,1,SKIDXD);
               {? exec('get_knag','obe_budz',SKID_MBN.ref(),_ref_wer,SSTALE.AR,'F')
               || exec('add_poz','obe_budz',SSTALE.AO,-1,SKIDXD);
                  _ok:=2
               ?}
            ?}
         ?};
         _ok & SKIDXD.next()
      !}
   ?};
   _ok
|| 1
?}


\usun_rez_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Zdjecie rezerwacji budzetu dla dokumentow ksiegowych
::  OLD: \usun_rez_dok/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
SKIDXD.index('SKIDXD'); SKIDXD.prefix(DOK.ref());
{? SKIDXD.first()
|| K_WERSJE.index('CZY_SYS'); K_WERSJE.prefix();
   _ref_wer:=null();
   {? K_WERSJE.find_key('F')
   || _ref_wer:=K_WERSJE.ref()
   ?};
   {? K_WERSJE.find_key('T')
   || _ok:=_ok & exec('usun_rez_budz1','obe_budz',K_WERSJE.ref(),SSTALE.AR,SKIDXD,SSTALE.AO);
      _ok:=_ok & exec('usun_rez_budz1','obe_budz',_ref_wer,SSTALE.AR,SKIDXD,SSTALE.AO)
   ?}
?};
_ok


\firma_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Zwraca tabele z firmą i rokiem dla podanej daty
::   WE: _a - data
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,
   'FIRMA','INTEGER',,
   'ROK_F','INTEGER',
);
FIRMA.index('SYMBOL'); FIRMA.prefix();
{? FIRMA.first()
|| {!
   |? _rok:=null;
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(FIRMA.ref());
      {? ROK_F.first()
      || {!
         |? {? _a>=ROK_F.POCZ_ROK & _a<=ROK_F.KON_ROK
            || _rok:=ROK_F.ref()
            ?};
            _rok=null & ROK_F.next()
         !}
      ?};
      {? _rok
      || _tab.FIRMA:=FIRMA.ref();
         _tab.ROK_F:=_rok;
         _tab.add()
      ?};
      FIRMA.next()
   !}
?};
_tab

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:08 ccfe4a7804c1151554b54e3e6603e7c999d43bfd57f4a45a1f4804247822e1d7c159a33d61b21cdb5c69951aff4a722af9b93eb858036525729b9e1f270691196bb6a8bca3fb4025c107ccc9269dd60f682cccb8f9c04b70f3cbc5534ae4b445e91bb94c98890d9eb25c24be86f76829c7fd13859277918094ac45b31cafcafc

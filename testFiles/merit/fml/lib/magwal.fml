:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: magwal.fml
:: Utworzony: [21.37]
:: Autor: IFZ
::======================================================================================================================
:: Zawartość: Formuły do obsługi Magazynu walut
::======================================================================================================================


\clear_mw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Magazyn walut - formuła usuwająca przychody i rozchody konta od początku - cała tabela
::----------------------------------------------------------------------------------------------------------------------
{? KS.MW='T' || return(0) ?};
ROK_F.cntx_psh(); OKRO_F.cntx_psh(); OKRO_F.index('ROK');
ROK_F.index('NAZWA');
ROK_F.prefix(REF.FIRMA,SSTALE.AR().NAZ);
{? ROK_F.first()
|| _fmaska:={? +REF.FIRMA().SYMBOL>3 || -REF.FIRMA().SYMBOL || 'r'+REF.FIRMA().SYMBOL ?};
   MAGWALP.use('mwp'+_fmaska);
   MAGWALR.use('mwr'+_fmaska);
   AN.index('KS'); MAGWALP.index('KON');MAGWALR.index('MW');
   AN.prefix(KS.ref());
   {? AN.first()
   || {! |?
            MAGWALP.prefix(AN.SYM,);
            {? MAGWALP.first()
            || {! |? MAGWALR.prefix(MAGWALP.ref());
                    {? MAGWALR.first() || {! |? MAGWALR.del() !} ?};
                     MAGWALP.del()
               !}
            ?};
            MAGWALR.cntx_psh();
            MAGWALR.index('KON');
            MAGWALR.prefix(AN.SYM,);
            {? MAGWALR.first() || {! |? MAGWALR.del() !} ?};
            MAGWALR.cntx_pop();
            AN.next()
       !}
    ?}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
FUN.info('Stany walut zostały usunięte.'@);
1

\akt_mw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Magazyn walut - formuła obliczająca przychody i rozchody konta od początku roku
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); OKRO_F.cntx_psh(); OKRO_F.index('ROK');
ROK_F.index('NAZWA');
ROK_F.prefix(REF.FIRMA,SSTALE.AR().NAZ);
{? ROK_F.first()
|| _fmaska:={? +REF.FIRMA().SYMBOL>3 || -REF.FIRMA().SYMBOL || 'r'+REF.FIRMA().SYMBOL ?};
   MAGWALP.use('mwp'+_fmaska);
   MAGWALR.use('mwr'+_fmaska);
   MAGWALR.prefix(); MAGWALP.prefix();
   DOK.cntx_psh(); POZ.cntx_psh(); KS.cntx_psh();
   OKRO_F.prefix(ROK_F.ref());
   {? OKRO_F.first()
   || {! |?
         _war:="{? OKRO_F.POCZ<>date(0,0,0)
                || 1
                || POZ.DOK().DOK_REJ().SLO().KOD='KOR_BO';1
                ?}";
            exec('open','rozrach',OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
            POZ.index('KOM');
            POZ.prefix(KS.ref(),'T');
            {? POZ.first()
            || {! |?
                  {? KS.MW='T' & POZ.WAL<>FINFO.NAROD & _war()
                  || {? KS.SALDO='A' & 1+POZ.STR='W'
                     || MAGWALP.blank();
                        MAGWALP.POZ:=POZ.ref();
                        MAGWALP.KON:=POZ.KON;
                        MAGWALP.ODD:=POZ.DOK().ODD;
                        MAGWALP.OKRO:=OKRO_F.ref();
                        MAGWALP.WN:=POZ.SUMW;
                        MAGWALP.KURS:=POZ.KURS$6;
                        MAGWALP.WAL:=POZ.WAL;
                        MAGWALP.DATA:=DOK.DTW;
                        MAGWALP.add()
                     |? KS.SALDO='A' & 1+POZ.STR='M'
                     || MAGWALR.blank();
                        MAGWALR.POZ:=$POZ.ref();
                        MAGWALR.KON:=POZ.KON;
                        MAGWALR.MA:=POZ.SUMW;
                        MAGWALR.ODD:=POZ.DOK().ODD;
                        MAGWALR.OKRO:=OKRO_F.ref();
                        MAGWALR.KURS:=POZ.KURS$6;
                        MAGWALR.WAL:=POZ.WAL;
                        MAGWALR.DATA:=DOK.DTW;
                        MAGWALR.add()
                     |? KS.SALDO='P'
                     || MAGWALR.blank();
                        MAGWALR.POZ:=$POZ.ref();
                        {? 1+POZ.STR='W' || MAGWALR.WN:=POZ.SUMW || MAGWALR.MA:=POZ.SUMW ?};
                        MAGWALR.KURS:=POZ.KURS$6;
                        MAGWALR.WAL:=POZ.WAL;
                        MAGWALR.DATA:=POZ.DOK().DTW;
                        MAGWALR.add()
                     ?}
                  ?};
                  POZ.next()
               !}
            ?};
            OKRO_F.next()
      !}
   ?};
   DOK.cntx_pop(); POZ.cntx_pop(); KS.cntx_pop()
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
{? MAGWALP.size() | MAGWALR.size()
|| FUN.info('Stany walut zostały zaktualizowane.'@)
|| FUN.info('Stany walut nie wymagały aktualizacji.'@)
?};
1


\rekpmwp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: rekord przed przy wyswietlaniu tabeli MAGWALP
::----------------------------------------------------------------------------------------------------------------------
POMOC.POZ:=''; POMOC.F1:=0; POMOC_DM.NK_W:=POMOC_DM.NR_W:=''; POMOC_DM.REJW:=POMOC_DM.ODDW:=null();
{? MAGWALP.POZ<>null()
|| _maska:=4-(8+$MAGWALP.POZ);
   DOK.cntx_psh(); POZ.cntx_psh();
   DOK.use('doku'+_maska);
   POZ.use('pozy'+_maska);
   MAGWALP.POZ(); POZ.DOK();
   POMOC.POZ:=POZ.OP;
   POMOC_DM.REJW:=DOK.REJ;
   POMOC_DM.NK_W:=DOK.NK;
   POMOC_DM.NR_W:=$DOK.NR;
   POMOC_DM.ODDW:=DOK.ODD;
   POMOC.F1:={? MAGWALP.PRZY_DEB='D' || -POZ.SUMW || POZ.SUMW?};
   DOK.cntx_pop(); POZ.cntx_pop()
?};
{? MAGWALP.WN=MAGWALP.MA
|| {? MAGWALP.PRZY_DEB='D'
   || Color.fnd_kol('MAGWALP#01#03')
   || Color.fnd_kol('MAGWALP#01#01')
   ?}
|? MAGWALP.PRZY_DEB='D'
|| Color.fnd_kol('MAGWALP#01#04')
|| ''
?}


\rekpmwr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: rekord przed przy wyswietlaniu tabeli MAGWALR
::----------------------------------------------------------------------------------------------------------------------
POMOC.POZ1:=POMOC.POZ2:=POMOC_DM.NK:=POMOC_DM.NR:=''; POMOC_DM.REJ:=POMOC_DM.ODD:=null();
{? MAGWALR.POZ<>''
|| _maska:=4-(8+MAGWALR.POZ);
   DOK.cntx_psh(); POZ.cntx_psh();
   DOK.use('doku'+_maska); POZ.use('pozy'+_maska);
   _ref:=BB.sqlint(MAGWALR.POZ);
   POZ.prefix();
   {? _ref<>0 & POZ.seek(_ref,)
   || POMOC.POZ1:=POZ.DOK().REJ().KOD+'/'+$DOK.NR+' '+DOK.NK;
      POMOC.F2:={? MAGWALR.ROZ_SPL='S' || -POZ.SUMW || POZ.SUMW ?}; POMOC.F3:=0;
      POMOC_DM.REJ:=DOK.REJ; POMOC_DM.NK:=DOK.NK; POMOC_DM.NR:=$DOK.NR; POMOC_DM.ODD:=DOK.ODD
   ?};
   DOK.cntx_pop(); POZ.cntx_pop()
?};
{? MAGWALR.POZR<>null()
|| _maska:=4-(8+$MAGWALR.POZR);
   DOK.cntx_psh(); POZ.cntx_psh();
   DOK.use('doku'+_maska); POZ.use('pozy'+_maska);
   MAGWALR.POZR();
   POMOC.POZ2:=POZ.DOK().REJ().KOD+'/'+$DOK.NR+' '+DOK.NK;
   DOK.cntx_pop(); POZ.cntx_pop()
?};
MAGWALR.OKRO();
POMOC.OKROTXT:={? +OKRO_F.NAZ || OKRO_F.ROK().NAZ+' '+OKRO_F.NAZ || '' ?};
_act:={? POMOC.ZAKRES='T' || 'O' || 'K' ?};
{? MAGWALR.POZR<>null()
|| MAGWALR.actions_grayed('WER', _act)
|| MAGWALR.actions_grayed('WER', _act+'R')
?};
{? MAGWALR.WN=MAGWALR.MA
|| {? MAGWALR.ROZ_SPL='S'
   || Color.fnd_kol('MAGWALR#01#01')
   || Color.fnd_kol('MAGWALR#01#02')
   ?}
|| {? MAGWALR.ROZ_SPL='S'
   || Color.fnd_kol('MAGWALR#01#03')
   || ''
   ?}
?}


\roz_mw_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Podgląd na rozdzielnik przy pozycji do Magazynu walut
::----------------------------------------------------------------------------------------------------------------------
P_W.index('LP');
P_W.prefix(POZ.ref());
P_W.select


\mw_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Magazyn walut
::----------------------------------------------------------------------------------------------------------------------
POMOC.POZ:=POMOC.POZ1:=POMOC.POZ2:='';
POMOC_DM.REJW:=POMOC_DM.REJ:=POMOC_DM.ODDW:=POMOC_DM.ODD:=null();
POMOC_DM.NK_W:=POMOC_DM.NR_W:=POMOC_DM.NK:=POMOC_DM.NR:='';
MAGWALP.win_sel('WERG');
MAGWALR.index('MW');
MAGWALR.hdr_sel(' związane z przychodem ');
MAGWALR.actions_grayed('WER', 'O');
POMOC.ZAKRES:='N';
{? OPERATOR.DEPT
|| MAGWALP.index('KON_O');
   MAGWALP.prefix(OPERATOR.DEPT,AN.SYM,SSTALE.WAL)
|| MAGWALP.index('KON');
   MAGWALP.prefix(AN.SYM,SSTALE.WAL)
?};
{? POMOC.ZAKRES='T' & MAGWALP.first()
|| MAGWALR.index('MW');MAGWALR.prefix(MAGWALP.ref())
|| {? OPERATOR.DEPT
   || MAGWALR.index('KON_O');
      MAGWALR.prefix(OPERATOR.DEPT,AN.SYM,SSTALE.WAL)
   || MAGWALR.index('KON');
      MAGWALR.prefix(AN.SYM,SSTALE.WAL)

   ?}


?};
MAGWALP.select()


\arf_werg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Po odświeżeniu okna grupowego w Magazyn walut
::----------------------------------------------------------------------------------------------------------------------
POMOC_DM.NK:=POMOC_DM.NR_W:=POMOC_DM.NK:=POMOC_DM.NR:=''; POMOC_DM.REJ:=POMOC_DM.ODD:=null();
POMOC.POZ1:=POMOC.POZ2:='';
{? MAGWALP.f_active() & MAGWALP.sel_size() || FUN.info('Niedostępne zaznaczanie rekordów przy nałożonym filtrze.'@); MAGWALP.sel_adel();  win_disp() ?};
_ok:={? MAGWALP.f_active() || MAGWALP.f_size() || MAGWALP.size() ?};
{?  _ok
|| {? POMOC.ZAKRES='T' & ~MAGWALR.f_active()
   || MAGWALR.index('MW');MAGWALR.prefix(MAGWALP.ref())
   || {? OPERATOR.DEPT
      || MAGWALR.index('KON_O');
         MAGWALR.prefix(OPERATOR.DEPT,AN.SYM,SSTALE.WAL)
      || MAGWALR.index('KON');
         MAGWALR.prefix(AN.SYM,SSTALE.WAL)
      ?}
   ?}
|| {? POMOC.ZAKRES='T'
   || MAGWALR.index('MW');MAGWALR.prefix(-1)
   || {? OPERATOR.DEPT
      || MAGWALR.index('KON_O');
         MAGWALR.prefix(OPERATOR.DEPT,AN.SYM,SSTALE.WAL)
      || MAGWALR.index('KON');
         MAGWALR.prefix(AN.SYM,SSTALE.WAL)
      ?}
   ?}
?};
_ok:={? MAGWALR.f_active() || MAGWALR.f_first() || MAGWALR.first() ?};
{? _ok
|| {? MAGWALR.POZ<>''
   || _maska:=4-(8+MAGWALR.POZ);
      DOK.cntx_psh(); POZ.cntx_psh();
      DOK.use('doku'+_maska); POZ.use('pozy'+_maska);
      _ref:=BB.sqlint(MAGWALR.POZ);
      POZ.prefix();
      {? _ref<>0 & POZ.seek(_ref,)
      || POMOC.POZ1:=POZ.DOK().REJ().KOD+'/'+$DOK.NR+' '+DOK.NK; POMOC.F2:=POZ.SUMW; POMOC.F3:=0;
         POMOC_DM.REJ:=DOK.REJ; POMOC_DM.NK:=DOK.NK; POMOC_DM.NR:=$DOK.NR; POMOC_DM.ODD:=DOK.ODD
      ?};
      DOK.cntx_pop(); POZ.cntx_pop()
   ?};
   {? MAGWALR.POZR<>null()
   || _maska:=4-(8+$MAGWALR.POZR);
      DOK.cntx_psh(); POZ.cntx_psh();
      DOK.use('doku'+_maska); POZ.use('pozy'+_maska);
      MAGWALR.POZR();
      POMOC.POZ2:=POZ.DOK().REJ().KOD+'/'+$DOK.NR+' '+DOK.NK;
      DOK.cntx_pop(); POZ.cntx_pop()
   ?}
?};
exec('opis','konto',AN.SYM, AN.KS());
exec('ka_opis','konto');
grp_disp(MAGWALR,'WER',,1)


\arf_wer
''


\zakr_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Formuły Pokaż wszystkie i Ogranicz w Magazyn walut
::----------------------------------------------------------------------------------------------------------------------
{? POMOC.ZAKRES='T'
||
:: pokaż wszystkie
   {? OPERATOR.DEPT
   || MAGWALR.index('KON_O');
      MAGWALR.prefix(OPERATOR.DEPT,AN.SYM,SSTALE.WAL)
   || MAGWALR.index('KON');
      MAGWALR.prefix(AN.SYM,SSTALE.WAL)
   ?};
   POMOC.ZAKRES:='N'
||
:: ogranicz do związanych z przychodem
   {? MAGWALP.first()
   || MAGWALR.index('MW');MAGWALR.prefix(MAGWALP.ref())
   || {? OPERATOR.DEPT
      || MAGWALR.index('KON_O');
         MAGWALR.prefix(OPERATOR.DEPT,AN.SYM,SSTALE.WAL)
      || MAGWALR.index('KON');
         MAGWALR.prefix(AN.SYM,SSTALE.WAL)
      ?}
   ?};
   POMOC.ZAKRES:='T'
?};
MAGWALR.first();
grp_disp(MAGWALR,'WER')


\zapisy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Wyświetlenie zapisów z dokumentu związanego z pozycją
::   WE: _a - 0 dla dokumentu pierwotnego rozchodu,
::            1 dla dokumentu rozliczającego rozchodu,
::            2 dla dokumentu przychodu
::----------------------------------------------------------------------------------------------------------------------
MAGWALR.cntx_psh(); ROK_F.cntx_psh();
{? _a=0
|| _maska:=4-(8+MAGWALR.POZ); _ref:=BB.sqlint(MAGWALR.POZ)
|? _a=1
|| _maska:=(ref_name(MAGWALR.POZR)+4); _ref:=#MAGWALR.POZR
|| _maska:=(ref_name(MAGWALP.POZ)+4); _ref:=#MAGWALP.POZ
?};
exec('zapisy','dok_fks',_maska,_ref,0);
MAGWALR.cntx_pop(); ROK_F.cntx_pop()


\pocz_kb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Formula poczatkowa automatu liczacego roznice kursowe na koncie
::       srodkow pienieznych w walucie
::       [_a] - symbol waluty
::       [_b] - symbol konta
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
SIK.ROK1:=SSTALE.AR;
POMOC.D2:=DOK.DTW;
POMOC.CO:='F'; POMOC.SALDO:='A';
KONTO.K1:='';
KONTO.K1_RODZ:=1;KONTO.K1_WYM:=1;KONTO.K1_SYNT:=1;
KONTO.K1_BE:=''; KONTO.K1_AE:='';
POMOC.WAL:=null; UNPAR.P10:='';
{? _>0 & type_of(_a)=2
|| SLO.index('SL'); SLO.prefix(FINFO.SLWAL().SLU);
   {? SLO.find_key(_a) || POMOC.WAL:=SLO.ref ?}
?};
{? _>1 & type_of(_b)=2 || KONTO.K1:=_b ?};
POMOC.win_edit('ROZ_KRSF');
{? POMOC.edit("exec('spr_par','fks_auto')") || 1 || wyb:=0 ?}


\roz_kb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Funkcja inicjująca obliczanie różnic dla konta
::----------------------------------------------------------------------------------------------------------------------
end();
echo();
{? var_pres('TROZ')>0 || VAR_DEL.delete('TROZ') ?};
TROZ:=tab_tmp(1,'T','STRING[1]','Przychód/Rozchód',
                'DEBET','STRING[1]','Debet/Spłata',
                'ROK','STRING[6]','Rok',
                'OKRES','INTEGER','Okres',
                'DATA','DATE','Data',
                'LP','INTEGER','Lp',
                'POCZ_ROK','DATE','Początek roku',
                'WN','REAL','Wn',
                'MA','REAL','Ma',
                'KURS','REAL','Kurs',
                'PLN_P','REAL','Kwota (PLN) po kursie P',
                'PLN_R','REAL','Kwota (PLN) po kursie R',
                'ROZNICA','REAL','Różnica kursowa',
                'OPIS','STRING[35]','Opis',
                'DOKUMENT','STRING[35]','Dokument',
                'MWUIDREF','STRING[48]','Złączenie do MAGWAL',
                'KWOTA','REAL','Kwota walutowa',
                'X','INTEGER','Znacznik pomocniczy'
                );
tmpind:=TROZ.ndx_tmp('',1,'T','',0,'X',,0,'POCZ_ROK',,0,'DATA',,0,'LP',,0);
tmpind1:=TROZ.ndx_tmp('',1,'T','',0,'POCZ_ROK',,0,'DATA',,0);
TROZ.index(tmpind);
tyt:='Przychody i rozchody (Waluta: '+POMOC.WAL().KOD+', Konto: '+KONTO.K1+', do daty: '+$POMOC.D2+')';
tyt2:='Przychody i rozchody (Waluta: '+POMOC.WAL().KOD+', Konto: '+KONTO.K1+')';
tyt+=', Różnice kursowe wg metody FIFO';
tyt+={? POMOC.SALDO='A' || ', Typ konta: aktywne' || ', Typ konta: pasywne' ?};
_ttmpwer:=TROZ.mk_sel(tyt,'N',,'troz_wer');
:TROZ.win_fld(_ttmpwer,,'ROK');  TROZ.win_fld(_ttmpwer,,'OKRES',,,5);
TROZ.win_fld(_ttmpwer,,'DATA',,,12);
TROZ.win_fld(_ttmpwer,,'T',,,3);
TROZ.win_fld(_ttmpwer,,'DEBET',,,3);
TROZ.win_fld(_ttmpwer,,'WN',,,14,2);
TROZ.win_fld(_ttmpwer,,'MA',,,14,2);
TROZ.win_fld(_ttmpwer,,'KURS',,,10,6);
TROZ.win_fld(_ttmpwer,,'PLN_P',,,14,2,,{? POMOC.SALDO='A' || 'Wn (PLN) po kursie P' || 'Ma (PLN) po kursie P' ?});
TROZ.win_fld(_ttmpwer,,'PLN_R',,,14,2,,{? POMOC.SALDO='A' || 'Wn (PLN) po kursie R' || 'Ma (PLN) po kursie R' ?});
TROZ.win_fld(_ttmpwer,,'ROZNICA',,,14,2);
TROZ.win_fld(_ttmpwer,,'KWOTA',,,14,2);
TROZ.win_fld(_ttmpwer,,'DOKUMENT',,,25);
TROZ.win_fld(_ttmpwer,,'OPIS',,,25);
g_tt_w:=obj_new(TROZ.fld_num());
g_tt_p:=obj_new(TROZ.fld_num());
g_tt_e:=obj_new(TROZ.fld_num());
{! _i:=1..TROZ.fld_num() |! g_tt_e[_i]:=1; g_tt_w[_i]:=g_tt_p[_i]:=0 !};
g_tt_e[6]:=0;
g_tt_e[14]:=g_tt_e[15]:=g_tt_e[16]:=g_tt_e[17]:=g_tt_e[18]:=0;

g_tt_w[2]:=5;
g_tt_w[4]:=5;
g_tt_w[5]:=12;

g_tt_w[8]:=g_tt_w[9]:=14; g_tt_w[10]:=10; g_tt_w[11]:=14; g_tt_w[12]:=14; g_tt_w[13]:=14;

g_tt_p[5]:=0; g_tt_p[8]:=g_tt_p[9]:=g_tt_p[11]:=g_tt_p[12]:=g_tt_p[13]:=2; g_tt_p[10]:=6; g_tt_p[6]:=g_tt_p[4]:=0;
TROZ.win_act(_ttmpwer,0,'Formuła','Oblicz różnicę'@@,,'Uruchomienie funkcji obliczającej różnicę kursową'@,
,"{? exec('obl_kb','magwal') || TROZ.actions_grayed(cur_win(),'OU') || TROZ.actions_grayed(cur_win(),'OUG') ?}",1,,,,'O');
TROZ.win_act(_ttmpwer,0,'Formuła','Generuj dokument'@@,,'Tworzenie dokumentu księgowego'@,
,"PAR_WYDR.TTMPAKR:='TROZ';PAR_WYDR.TITLE:='Przychody i rozchody\n'+(20-tyt); exec('ks_rkb','magwal')",0,,,,'G');
TROZ.win_act(_ttmpwer,,'Formuła','Wydruk'@@,,'Wydruk przychodów i rozchodów konta wyświetlanych w okienku'@,,
             "PAR_WYDR.TTMPAKR:='TROZ';PAR_WYDR.TITLE:='Przychody i rozchody\n'+(20-tyt); exec('rep_exec','#b_report','','wsp_tt',,,,,,,,'W')",,,,,'W');
TROZ.win_act(_ttmpwer,0,'Rekord',,,,"{? TROZ.T='S' || exec('findtmp','#color') ?}");
TROZ.win_act(_ttmpwer,0,'Wyświetl',,,,"TROZ.hdr_edit(); TROZ.hdr_edit(tyt2); TROZ.display; 0");
TROZ.win_act(_ttmpwer,0,'Kolejność');
_ttmpred:=TROZ.mk_edit(,,'troz_red');
TROZ.win_efld(_ttmpred,,'OKRES',,,2,,,'Okres'@);
TROZ.win_efld(_ttmpred,,'T',,,3,,,'Lp'@);
TROZ.win_efld(_ttmpred,,'WN',,,15,2,,'Kwota Wn'@);
TROZ.win_efld(_ttmpred,,'MA',,,15,2,,'Kwota Ma'@);
TROZ.win_efld(_ttmpred,,'KURS',,,10,4,,'Kurs'@);
TROZ.win_efld(_ttmpred,,'OPIS');
TROZ.win_efld(_ttmpred,,'DOKUMENT');
_waluta:=SSTALE.WAL;
exec('proz_kb','magwal');
TROZ.win_sel(_ttmpwer); TROZ.win_edit(_ttmpred);
{? TROZ.first()
|| TROZ.hdr_sel();
   {? POMOC.NAL_AUT='T'
   || {? exec('obl_kb','magwal') || TROZ.actions_grayed(_ttmpwer,'OU') || TROZ.actions_grayed(_ttmpwer,'OUG') ?}
   || TROZ.actions_grayed(_ttmpwer,'G')
   ?};
   TROZ.select()
|| FUN.info('W podanym zakresie brak zapisów do rozliczenia w walucie %1\ndla konta %2.'@[POMOC.WAL().KOD,KONTO.K1])
?};
SSTALE.WAL:=_waluta;
VAR_DEL.delete('TROZ','g_tt_p','g_tt_w','g_tt_e','__RKB_SLU','__RKB_LP','tmpind','tmpind1','tyt','tyt2');
0


\utw_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Tworzy załącznik z zestawieniem naliczenia RKB i podpina do DOK
::----------------------------------------------------------------------------------------------------------------------
_file:='Polecenie księgowania '+exec('str_to_pth','#string',DOK.NK)+'.pdf';
_ok:=exec('rep_exec','#b_report','','wsp_tt',0,,_file,1,,,,'W');
{? _ok & _file<>''
|| DOK.bl_put('E_DOC',pth_dir(_file)+'/'+_file,0);
:  _wyn:=exec('save_dok','dokum','DOK',_file,_file,'','','');
   ferase(pth_dir(_file)+'/'+_file,0)
?}

\obl_kb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Akcja 'Oblicz różnice' w okienku wertowania przychodów i rozchodów konta
::----------------------------------------------------------------------------------------------------------------------
TROZ.cntx_psh(); TROZ.index(tmpind); TROZ.prefix('S'); _roznica:=0;
{? TROZ.first() & TROZ.WN$2>=TROZ.MA$2
|| TROZ.index(tmpind); TROZ.prefix('R');
   {? ~TROZ.find_le(0,SSTALE.AR().POCZ_ROK,POMOC.D2)
   || FUN.info('Brak rozchodów dla konta na dzień %1.'@[$POMOC.D2])
   || jedn:=exec('jed_wal','waluty',POMOC.WAL().KOD);
      _roznica:={? POMOC.SALDO='A' || exec('obl_filia','magwal') || exec('obl_filip','magwal') ?};
      {? _roznica=0
      || FUN.info('Nie stwierdzono potrzeby naliczenia różnic kursowych.'@)
      || {? POMOC.NAL_AUT='N' || TROZ.actions_grayed(cur_win(),'O') ?};
          TROZ.index(tmpind); TROZ.prefix('S');
          {? TROZ.first() || TROZ.ROZNICA:=_roznica; TROZ.put() ?};
          TROZ.prefix();
          TROZ.for_each("TROZ.X:=0; {? TROZ.KWOTA=0 || TROZ.MWUIDREF:='' ?};TROZ.put()");
          TROZ.first();
          win_disp();
          &jedn
      ?}
   ?}
|| FUN.info('Suma przychodów jest mniejsza od sumy rozchodów.\nObliczenie różnicy niemożliwe.'@)
?};
TROZ.cntx_pop();
_roznica


\ks_rkb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Akcja 'Generuj dokument' w okienku wertowania przychodów i rozchodów konta
::----------------------------------------------------------------------------------------------------------------------
:exec('utw_pdf','magwal');
TROZ.index(tmpind); TROZ.prefix('S');
{? TROZ.first()
|| exec('utw_pozr','fks_auto',TROZ.ROZNICA,KONTO.K1,'','',date(0,0,0),date(0,0,0),'Różnice kursowe',,'T')
?};
POZ.cntx_psh();
POZ.index('RK');
POZ.prefix(DOK.ref(),'T',KONTO.K1,);
{? POZ.first()
|| _PozRef:=POZ.ref()
|| _PozRef:=null()
?};
POZ.cntx_pop();
{? _PozRef=null() || FUN.info('Błąd przy tworzeniu pozycji dokumentu.'@); return(0) ?};
P_W.cntx_psh();
P_W.prefix();
{? var_pres('TmpPw')<=0 || FUN.info('Błąd przy tworzeniu pozycji dokumentu.'@); P_W.cntx_pop(); return(0) ?};
{? TmpPw.first()
||
   {! |? {? TmpPw.KWOTA<>0
         || P_W.blank(1);
            P_W.LP:=TmpPw.LP;
            P_W.POZ:=_PozRef;
            P_W.KWOTA:=TmpPw.KWOTA;
            P_W.MWUIDREF:=TmpPw.MWUIDREF;
            P_W.MASKA:={? TmpPw.MWUIDREF*'mwp' || 'mwp' || 'mwr' ?};
            P_W.add()
         ?};
         TmpPw.next()
   !}
?};
::exec('roz_mw','magwal');
P_W.cntx_pop();
sel_exit()


\new_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Funkcja tworzy rekord w tabeli TROZ
::   WE: _a - Typ (P/R/S)
::       _b - data
::       _c - kwota Wn lub Ma
::       _d - kurs
::       _e - opis
::       _f - oznaczenie dokumentu
::       _g - nazwa roku
::       _h - kod roku
::       _i - znacznik pomocniczy: 0 - przy dodawaniu, 1 - po oblicz różnicę
::       _j - (D) Debet/(S) Spłata
::----------------------------------------------------------------------------------------------------------------------
{? _c<>0
|| TROZ.prefix(); TROZ.blank();
   TROZ.T:=_a; TROZ.DATA:=_b; TROZ.OKRES:=_b~2;
   {? _a='S'
   || TROZ.WN:=_c; TROZ.MA:=_d; TROZ.ROK:=_e
   || {? _a='P' || TROZ.WN:=_c || TROZ.MA:=_c ?};
      {? _d$6=0
      || _jed:=exec('jed_wal','waluty',POMOC.WAL().KOD);
         TROZ.KURS:=((POZ.SUM/POZ.SUMW)*_jed)$6
      || TROZ.KURS:=_d

      ?};
      TROZ.OPIS:=_e; TROZ.ROK:=_g

   ?};
   TROZ.DOKUMENT:=_f; TROZ.POCZ_ROK:=_h; TROZ.X:=_i; TROZ.DEBET:=_j;
   TROZ.MWUIDREF:={? _a='P' || MAGWALP.uidref() |? _a='R' || MAGWALR.uidref() || '' ?};
   TROZ.add()
?};
{? _a='S' & TROZ.first()
|| TROZ.cntx_psh(); TROZ.index(tmpind1);
   _fun:="{? TROZ.first() || _lp:=1; {! |? TROZ.LP:=_lp; TROZ.put(); _lp+=1; TROZ.next() !} ?}";
   TROZ.prefix('P'); _fun();
   TROZ.prefix('R'); _fun();
   TROZ.cntx_pop()
?}


\obl_filia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Formuła obliczająca różnicę metodą FIFO (POMOC.CO='F') dla konta aktywnego
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TmpPw');
TmpPw:=sql('select * from P_W');
TmpPw.win_sel(TmpPw.mk_sel(,,1));
_add:="TmpPw.blank(1);
       _lp:={? TmpPw.last() || TmpPw.LP || 0 ?};
       TmpPw.LP:=_lp+1;
       TmpPw.KWOTA:=_a;
       TmpPw.MWUIDREF:=TROZ.MWUIDREF;
       TmpPw.MASKA:={? TROZ.MWUIDREF*'mwp' || 'mwp' || 'mwr' ?};
       TmpPw.add()";
_zn:=1; _ma:=_kurs:=_roznica:=_okres:=0;
_do:=POMOC.D2; _rok:=SSTALE.AR().POCZ_ROK;
TROZ.cntx_psh();
TROZ.index(tmpind); TROZ.prefix('R',0);
:: GŁÓWNA pętla po ROZCHODACH
{? TROZ.first()
|| {! |?
      {? TROZ.MA$2-TROZ.KWOTA<>0
      || _ma:=TROZ.MA$2-TROZ.KWOTA;_kurs:=TROZ.KURS; _KwotaWal:=0;
         TROZ.cntx_psh();
:: ---   przychody
         TROZ.prefix('P',0);
         {? TROZ.first()
         || {? TROZ.WN$2-TROZ.KWOTA>_ma$2
            || TROZ.PLN_P+=((_ma*TROZ.KURS)/jedn)$2;
               TROZ.PLN_R+=((_ma*_kurs)/jedn)$2;
               TROZ.ROZNICA:=(TROZ.PLN_R-TROZ.PLN_P)$2;
               {? TROZ.DEBET='D' | TROZ.DEBET='S' || TROZ.ROZNICA:=TROZ.ROZNICA*-1 ?};
               TROZ.KWOTA+=_ma;
               {? TROZ.put() || _add(_ma) ?};
               _KwotaWal:=_ma


            |? TROZ.WN$2-TROZ.KWOTA<=_ma$2
            || _KwotaWal:=TROZ.WN-TROZ.KWOTA;
               TROZ.PLN_P+=((_KwotaWal*TROZ.KURS)/jedn)$2;
               TROZ.PLN_R+=((_KwotaWal*_kurs)/jedn)$2;
               TROZ.ROZNICA:=(TROZ.PLN_R-TROZ.PLN_P)$2;
               {? TROZ.DEBET='D' | TROZ.DEBET='S' || TROZ.ROZNICA:=TROZ.ROZNICA*-1 ?};
               TROZ.KWOTA+=_KwotaWal;
               {? TROZ.put() || _add(_KwotaWal) ?};
               TROZ.X:=1; TROZ.cntx_psh(); TROZ.prefix(); TROZ.put(); TROZ.cntx_pop()
            ?}
         ?};
:: ---   przychody koniec
         TROZ.cntx_pop();
:: wracam do rozliczanego rozchodu
         {? TROZ.MA=_KwotaWal
         || TROZ.KWOTA:=_KwotaWal; TROZ.X:=1; TROZ.cntx_psh(); TROZ.prefix(); {? TROZ.put() || _add(_KwotaWal) ?}; TROZ.cntx_pop(); TROZ.first()
         |? TROZ.MA>_KwotaWal
         || TROZ.KWOTA+=_KwotaWal; {? TROZ.put() || _add(_KwotaWal) ?}; 1
         || TROZ.KWOTA=_KwotaWal; {? TROZ.put() || _add(_KwotaWal) ?}; TROZ.next()
         ?}
      || TROZ.next()
      ?}
    !}
?};
: zliczenie wiersza z sumą róznic do zaksięgowania
TROZ.prefix('P');
{? TROZ.first || {! |? _roznica+=TROZ.ROZNICA; TROZ.next() !} ?};
TROZ.cntx_pop();
_roznica


\obl_filip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Formuła obliczająca różnicę metodą FIFO (POMOC.CO='F') dla konta pasywnego
::----------------------------------------------------------------------------------------------------------------------
_zn:=1; _ma:=_kurs:=_roznica:=_okres:=0;
_do:=POMOC.D2; _rok:=SSTALE.AR().POCZ_ROK;
TROZ.cntx_psh();
TROZ.index(tmpind); TROZ.prefix('R',0);
:: pętla po rozchodach
{? TROZ.first()
|| {! |?
      {? TROZ.WN$2<>0
      || _wn:=TROZ.WN; _kurs:=TROZ.KURS; _KwotaWal:=0;
         TROZ.cntx_psh();
         TROZ.prefix('P',0);
         {? TROZ.first()
         || {? TROZ.MA$2>=_wn$2
            || TROZ.PLN_P+=((_wn*TROZ.KURS)/jedn)$2;
               TROZ.PLN_R+=((_wn*_kurs)/jedn)$2;
               TROZ.ROZNICA:=(TROZ.PLN_R-TROZ.PLN_P)$2;
               {? TROZ.DEBET='D' | TROZ.DEBET='S' || TROZ.ROZNICA:=TROZ.ROZNICA*-1 ?};
               _roznica+=TROZ.ROZNICA;
               TROZ.KWOTA:=_wn;
               TROZ.put();
               TROZ.X:=1; TROZ.cntx_psh(); TROZ.prefix(); TROZ.put(); TROZ.cntx_pop();
               _KwotaWal:=_wn


            |? TROZ.MA$2<_wn$2
            || TROZ.PLN_P+=((TROZ.MA*TROZ.KURS)/jedn)$2;
               TROZ.PLN_R+=((TROZ.MA*_kurs)/jedn)$2;
               TROZ.ROZNICA:=(TROZ.PLN_R-TROZ.PLN_P)$2;
               {? TROZ.DEBET='D' | TROZ.DEBET='S' || TROZ.ROZNICA:=TROZ.ROZNICA*-1 ?};
               _roznica+=TROZ.ROZNICA;
               TROZ.KWOTA:=TROZ.MA;
               TROZ.put();
               _KwotaWal:=TROZ.MA
            ?}
         ?};
         TROZ.cntx_pop();
:: wracam do rozliczanego rozchodu
         {? TROZ.WN=_KwotaWal
         || TROZ.KWOTA:=_KwotaWal; TROZ.X:=1; TROZ.cntx_psh(); TROZ.prefix(); TROZ.put(); TROZ.cntx_pop(); TROZ.first()
         || TROZ.KWOTA=_KwotaWal; TROZ.put(); TROZ.next()
         ?}


      || TROZ.next()
      ?}
    !}
?};
TROZ.cntx_pop();
_roznica


\proz_kb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Formuła zliczająca przychody i rozchody konta od początku roku do tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_data:=POMOC.D2; SSTALE.AR();
DOK.cntx_psh(); POZ.cntx_psh();
_fmaska:={? +REF.FIRMA().SYMBOL>3 || -REF.FIRMA().SYMBOL || 'r'+REF.FIRMA().SYMBOL ?};
MAGWALP.use('mwp'+_fmaska);
MAGWALP.index('KON_O');
MAGWALP.prefix(DOK.ODD,KONTO.K1,POMOC.WAL);
{? MAGWALP.first()
|| {! |?
       {? MAGWALP.DATA<=_data
       || {? MAGWALP.POZ<>null()
          || _maska:=4-(8+$MAGWALP.POZ);
             DOK.use('doku'+_maska);
             POZ.use('pozy'+_maska);
             MAGWALP.POZ();
             exec('new_tmp','magwal','P',MAGWALP.DATA,
                            MAGWALP.WN,MAGWALP.KURS$6,MAGWALP.POZ().OP,POZ.DOK().REJ().KOD+'/'+$DOK.NR+' '+DOK.NK,
                            ROK_F.NAZ,ROK_F.POCZ_ROK,0,{? MAGWALP.PRZY_DEB='D' || 'D' || '' ?})
          ?}
       ?};
       MAGWALP.next()
   !}
?};
MAGWALR.use('mwr'+_fmaska);
MAGWALR.index('KON_O');
MAGWALR.prefix(DOK.ODD,KONTO.K1,POMOC.WAL);
{? MAGWALR.first()
|| {! |?
       {? MAGWALR.DATA<=_data & MAGWALR.POZR=null()
       || _maska:=4-(8+MAGWALR.POZ);
          DOK.use('doku'+_maska);
          POZ.use('pozy'+_maska);
          _ref:=BB.sqlint(MAGWALR.POZ);
          POZ.prefix();
          {? _ref<>0 & POZ.seek(_ref,)
          || exec('new_tmp','magwal','R',MAGWALR.DATA,
                          MAGWALR.MA,MAGWALR.KURS$6,POZ.OP,POZ.DOK().REJ().KOD+'/'+$DOK.NR+' '+DOK.NK,ROK_F.NAZ,
                          ROK_F.POCZ_ROK,0,{? MAGWALR.ROZ_SPL='S' || 'S' || '' ?})
          ?}
       ?};
       MAGWALR.next()
   !}
?};
DOK.cntx_pop(); POZ.cntx_pop();

: Uaktualnienie rekordu z podsumowaniem przychodów i rozchodów konta
TROZ.cntx_psh(); TROZ.index(tmpind); TROZ.prefix('S');
{? TROZ.first() || TROZ.del() ?};
TROZ.prefix();
_ma:=_wn:=0;
{? TROZ.first()
|| {! |? {? TROZ.T<>'S' || _wn+=TROZ.WN; _ma+=TROZ.MA ?}; TROZ.next !}
?};
exec('new_tmp','magwal','S',POMOC.D2,_wn,_ma,'','','',date(0,0,0),0,'');
TROZ.cntx_pop();
1


\czy_rk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Formuły kontrolne do rejestru związane z różnicami kursowymi
::   WE: _a - 1 czy są naliczone różnice kursowe w wycofywanym dokumencie (pytanie)
::            2 czy są naliczone różnice kursowe w wycofywanym dokumencie (blokada)
::            3 czy są naliczone RKB w nast. okresach (blokada)
::   WY: 1 - dokument może być wycofany
::----------------------------------------------------------------------------------------------------------------------
_ret:=1; SSTALE.AO();
POZ.cntx_psh();
POZ.index('RK');
POZ.prefix(DOK.ref(),'T');
{? _a=1
|| {? POZ.first()
   || _ret:=FUN.ask('Dokument ma naliczone różnice kursowe.\n'
                    '\nCzy wycofać księgowanie dokumentu?'@)
   ?}
|? _a=2
|| {? POZ.first()
   || FUN.info('Dokument ma naliczone różnice kursowe.\n'
               '\nWycofanie księgowania nie jest możliwe.'@); _ret:=0
   ?}
|? _a=3
|| {? POZ.first()
   || _roz:=sql('select MAGWALR.DATA,MAGWALP.KON from MAGWALR '+
               'left join MAGWALP using (MAGWALR.MAGWAL,MAGWALP.REFERENCE) '+
               'where MAGWALR.DATA>to_date(:_b) and MAGWALP.KON like '':_a''', POZ.KON,OKRO_F.KON);
      {? var_pres('_roz')>0 & _roz.first()
      || FUN.info('W następnych okresach zostały naliczone RKB.'
                     '\nWycofanie księgowania nie jest możliwe.'@); _ret:=0
      ?}
   ?}
?};
POZ.cntx_pop();
_ret


\czy_mag_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Formuły kontrolne do rejestru związane z magazynem walut
::   WE: _a - 4 czy są pozycje związane z magazynem walut (pytanie)
::            5 czy są pozycje związane z magazynem walut (blokada)
::   WY:  1 - dokument może być wycofany
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
POZ.cntx_psh();
POZ.index('RK');
POZ.prefix(DOK.ref());
KOM.cntx_psh(); KS.cntx_psh();
{? POZ.first()
|| {! |? POZ.KOM().KS();
         _ok:=KS.MW='T';
        ~_ok & POZ.next()
   !}
?};
KOM.cntx_pop(); KS.cntx_pop();

{? _ok & _a=4
|| _ret:=FUN.ask('Dokument posiada zapisy na koncie związanym z magazynem walut.\n'
                 '\nCzy wycofać księgowanie dokumentu?'@)
|? _ok & _a=5
|| FUN.info('Dokument posiada zapisy na koncie związanym z magazynem walut.\n'
            '\nWycofanie księgowania nie jest możliwe.'@); _ret:=0
?};
POZ.cntx_pop();
_ret


\czy_rkb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Formuła kontrolna do rejestru związane z naliczaniem różnic dla rachunku własnych środków RKB
::       sprawdzenie, czy są naliczone różnice kursowe w bieżącym okresie dla konta występującego w dokumencie (blokada)
::   WY:  1 - w zależności od stanu dokument może być wycofany/zaksięgowany
::----------------------------------------------------------------------------------------------------------------------
_ret:=1; SSTALE.AO();
POZ.cntx_psh(); KOM.cntx_psh(); KS.cntx_psh();
POZ.index('RK');
POZ.prefix(DOK.ref());
{? POZ.first()
|| {! |? POZ.KOM().KS();
        {? KS.MW='T'
        || _roz:=sql('select MAGWALR.DATA,MAGWALP.KON, MAGWALR.POZR from MAGWALR '+
                     'left join MAGWALP using (MAGWALR.MAGWAL,MAGWALP.REFERENCE) '+
                     'where MAGWALR.DATA>=to_date(:_b) and MAGWALR.DATA<=to_date(:_c) '+
                     'and MAGWALP.KON like '':_a'' and MAGWALR.POZR not like '':_d''', POZ.KON,OKRO_F.POCZ,OKRO_F.KON,$POZ.ref());
:          _roz.win_sel(_roz.mk_sel(,,1)); _roz.select();
           {? var_pres('_roz')>0
           || {? _roz.first()
              || {? DOK.ZP='T'
                 || FUN.info('W bieżącym okresie zostały naliczone różnice kursowe dla konta występującego w dokumencie.'
                             '\nWycofanie księgowania nie jest możliwe.'@); _ret:=0
                 || FUN.info('W bieżącym okresie zostały naliczone różnice kursowe dla konta występującego w dokumencie.'
                             '\nZaksięgowanie nie jest możliwe.'@); _ret:=0
                 ?}
              || obj_del(_roz)
              ?}
           ?}
        ?};
        _ret & POZ.next()
   !}
?};
POZ.cntx_pop(); KOM.cntx_pop(); KS.cntx_pop();
_ret

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:39 03b2b59055ee3f04c4a66969a6f02c9996098c9577fabd622364b1b52c4b185fc2be8bf3cc968997ac54daccc21aaa156e10b2650fe8b833bc6a37b44f28978132f24cfc0ebd051203d6bc4bb2547c44375bcdc9384833559043d49ad9179bd8a93424f1b5ea2d75fc80e174a98454cdcac1cb51e99793ccd710c250bada9d2a

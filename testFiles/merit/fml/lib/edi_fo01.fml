:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: edi_fo01.fml
:: Utworzony: 21.03.2019
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: edi - formuły wykorzystywane w definicji komunikatu
::======================================================================================================================


\r_faks_gs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: read - dokument sprzedaży - start grupa
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
1


\r_faks_ge
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: read - dokument sprzedaży - end grupa
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
1


\r_faks_ds
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: read - dokument sprzedaży - start document
::   WE: _a - [F]aktura, [K]orekta
::   WY: -1/0/1
::----------------------------------------------------------------------------------------------------------------------
_typ:={? var_pres('_a')=type_of('') || _a || '' ?};
{? _typ='' | 'FK'*_typ=0
||
   FUN.info('Nieprawidłowy parametr funkcji r_faks_ds.'@);
   return(-1)
?};
_result:=1;
:: _def_pid - identyfikator pozycji definicji: cbc:CustomizationID - weryfikacja wersji komunikatu
_def_pid:={? _typ='F' || '62a13f2232bf29aea545199e90dfa24ee9b3e19a'  || '294d17b632a359fc1cd1ded9996059d8f4ac26a4' ?};
_valID:=0;
_val:=exec('id_val','edi_xml',_def_pid);
{? var_pres('_val')<>type_of(~~)
      &
   var_pres('[1]',_val)<>type_of(~~)
||
   _valID:=
      {? _typ='F'
      ||
         _val[1]='urn:cen.eu:en16931:2017#compliant#urn:fdc:peppol.eu:2017:poacc:billing:3.0'
      ||
         _val[1]='urn:cen.eu:en16931:2017#compliant#urn:fdc:peppol.eu:2017:poacc:billing:3.0#extended#urn:fdc:www.efaktura.gov.pl:ver1.0'
      ?}
?};
{? _valID=0
||
   exec('add_kom','#message','Niepoprawna wersja komunikatu.',4,__XML.ID);
   return(-1)
?};
VAR_DEL.delete('__N','__V','__P');
{? _result
||
   EDI_Z.POMIN:='N';
   BEER.WYSTFAKS:=null()
?};
_result


\r_faks_de
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: read - dokument sprzedaży - end document
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? BEER.WYSTFAKS
||
   {? EDI_Z.POMIN='T'
   ||
      exec('usun_fak','faktury_nag',0,,0)
   ||
      FAKS.EDI_R:='T';
      FAKS.put()
   ?}
?};
VAR_DEL.delete('__N','__V','__P','__OL','__OtherParties');
~~


\faks_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: poczatek dokumentu zakupu
::   WE: _a - typ dokumentu zakupu
::       _b - identyfikator dokumentu
::       _c - oddział
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_typysp:=_a;
_id:=_b;
_oddz:=_c;

_result:=1;

_verify:=exec('verify','edi_xml');
VAR_DEL.delete('__N');
__N:=obj_new(
   'TYPYSP','FIRST',
   'DW',
   'FAK_ID','FAK_DW',
   'EP_ID','EP_SID',
   'NETTO','VAT','BRUTTO',
   'VT',
   'EP_CBB');
__N.TYPYSP:=null(); __N.FIRST:=1;
__N.DW:=date(0,0,0);
__N.FAK_ID:=''; __N.FAK_DW:=date(0,0,0);
__N.EP_ID:=''; __N.EP_SID:='';
__N.NETTO:=0; __N.VAT:=0; __N.BRUTTO:=0;
__N.VT:=~~;
__N.EP_CBB:='';

BEER.WYSTFAKS:=null();
__N.TYPYSP:=exec('FindInSet','#table','TYPYSP','TYPYKOD',_typysp,_typysp,,1,,'');
POM.TYPYSP:=__N.TYPYSP;
_kor:=POM.TYPYSP().KOR='T';

{? POM.TYPYSP=null()
||
   EDI_Z.POMIN:='T';
   exec('add_kom','#message','Brak typu dokumentu %1.'[_typysp],4,_id);
   _result:=0
||
   POM.TAB:='FAKS';
   POM.TYPDOK:=POM.TYPYSP().T;
   POM.NRT:=POM.TYPYDOK().NRT;
   BEER.SZ:='Z';
   HELP.WHERE:='Z';
   FIND_KH2:='';
   {? _verify=1
   ||
      FAKS.blank(1); FAKS.memo_set(,'UWAGI');
      VAR_DEL.delete('__N1');
      {? _kor=0
      ||
         __N1:=obj_new('DW');
         __N1.DW:=date(0,0,0)
      ||
         __N1:=obj_new('DW','KH','FAK_ID','FAK_DW');
         __N1.DW:=date(0,0,0);
         __N1.KH:=null();
         __N1.FAK_ID:='';
         __N1.FAK_DW:=date(0,0,0)
      ?};
      ~~
   ||
      _result:=0;
      _dw:=__N1.DW;
      __PARSES.setVal('OddzialLogProd',_oddz);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LZK';
      _args.AR:=_dw~1;
      _args.AM:=_dw~2;
      __PARSES.setVal('OkresRok',_args);

      {? _kor=0
      ||
         {? __PARSES.setEnv('LZK_ZAK_DZAK')
         ||
            FAKS.clear(); FAKS.blank(); FAKS.memo_set(,'UWAGI');
            {? exec('add','faktury_nag',POM.TYPYSP,,,null,,,,,,,_dw,'N')
            ||
               FAKS.SP_WYM:='N';
               BEER.WYSTFAKS:=FAKS.ref();
               _result:=1
            ?}
         ?}
      ||
         {? __PARSES.setEnv('LZK_ZAK_DKOR')
         ||
            FAKS.clear(); FAKS.blank(); FAKS.memo_set(,'UWAGI');
            _result:=exec('bl_faks_zk','edi_fo01',POM.TYPYSP,__N1.KH,__N1.FAK_ID,__N1.FAK_DW,__N1.DW,_id)
         ?}
      ?};
      {? _result
      ||
         FAKS.memo_set('','UWAGI');
         FAKS.D:=date(0,0,0);
         FAKS.DO:=date(0,0,0)
      ?};
      VAR_DEL.delete('__N1')
   ?}
?};
_result


\faks_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: sprawdzenie czy
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
:: Pomijamy następujące dane:
:: 3.1 Dane podstawowe faktury
:: - (1) – wersja komunikatu
:: - (2) – identyfikator profilu
:: DRO_TODO_AWI: (6) – identyfikator typu faktury !!! może wykorzystać do wyznaczania typu dokumentu ???
:: - (6) – identyfikator typu faktury
:: - (11) – znak stanowiska kosztów u nabywcy
:: - (12) – referencja nabywcy – informacja na potrzeby wewnętrzne nabywcy
:: - (13) – data początku okresu fakturowania
:: - (14) – data końca okresu fakturowania
:: - (15) – kod daty naliczania podatku (wg UNTDID 2005)
:: 3.2 Referencje do dokumentów powiązanych z fakturą
:: 3.2.1 Załączniki
:: 3.3 Podmioty
:: 3.3.1 Adres
:: 3.3.2 Dostawca (AccountingSupplierParty)
:: (2) – identyfikator dostawcy
:: (3) – nazwa dostawcy
:: (4) – dane adresowe [zgodnie z opisem w p. 3.3.1]
:: (6) – typ podatku (VAT)
:: (7) – nazwa dostawcy (rejestrowa)
:: (8) – inny identyfikator dostawcy, rejestrowy (KRS lub REGON w Polsce)
:: (9) – dodatkowe informacje o dostawcy (udziały, kapitał itp.)
:: (10) – osoba do kontaktu (imię i nazwisko)
:: (11) – numer telefonu osoby do kontaktu
:: (12) – adres poczty elektronicznej osoby do kontaktu
:: DRO_TODO_AWI: 3.3.3 Nabywca (AccountingCustomerParty) !!! weryfikacja nabywcy aby sprawdzić czy faktura dla nas ???
:: 3.3.3 Nabywca (AccountingCustomerParty)
:: 3.3.4 Dane odbiorcy płatności
::       Brak danych w Xpertis
:: 3.3.5 Dane przedstawiciela podatkowego
::       Brak danych w Xpertis
:: 3.3.6 Dane odbiorcy fakturowanej dostawy
::       Brak danych w Xpertis
:: DRO_TODO_AWI: Dane dotyczące płatności
:: 3.4 Dane dotyczące płatności
:: 3.4.1 Dane karty płatniczej
:: 3.4.2 Odbiorca płatności
:: 3.4.3 Konto płatnika
:: 3.4.4 Warunki płatności
:: 3.5 Obciążenia i upusty na poziomie dokumentu
:: 3.6 Podsumowanie podatku
:: 3.7 Podsumowanie dla faktury
:: (1) – suma wartości netto z pozycji faktury
:: DRO_TODO_AWI:  (4) – suma upustów na poziomie dokumentu !!! jak to zarejestrować w systemie ???
::                faktury niewalutowe mają FAKSV "ręczne" w celu kontroli zgodności z pozycjami
:: (4) – suma upustów na poziomie dokumentu
:: DRO_TODO_AWI:  (5) – suma obciążeń na poziomie dokumentu !!! jak to zarejestrować w systemie ???
::                faktury niewalutowe mają FAKSV "ręczne" w celu kontroli zgodności z pozycjami
:: (5) – suma obciążeń na poziomie dokumentu
:: DRO_TODO_AWI: faktura rozliczająca
:: (6) – suma przedpłaty
::       Brak mechanizmu umożliwiającego automatyczne powiązanie z zaliczką. Na sprzed
:: (7) – suma zaokrągleń
:: (8) – wartość do zapłaty
_result:=1;
_verify:=exec('verify','edi_xml');
_kor:=POM.TYPYSP().KOR='T';
{? _verify=1
||
   {? _kor=0
   ||
      _Chk:=sql('
         select SYM
         from @FAKS
         where KH=:_a and ID=\':_b\'
      '
      ,FAKS.KH,FAKS.ID);
      {? _Chk.first()
      ||
         EDI_Z.SYM:=_Chk.SYM;
         exec('add_kom','#message','Dokument %1 (%2) jest już w systemie'@[FAKS.ID,EDI_Z.SYM],2,FAKS.ID);
         EDI_Z.SYM:='';
         EDI_Z.POMIN:='T';
         _result:=0
      ?};
      __N1.DW:=__N.DW
   ||
      __N1.DW:=__N.DW;
      __N1.KH:=FAKS.KH;
      __N1.FAK_ID:=__N.FAK_ID;
      __N1.FAK_DW:=__N.FAK_DW;
      _result:=exec('bl_faks_zk','edi_fo01',POM.TYPYSP,FAKS.KH,__N1.FAK_ID,__N1.FAK_DW,__N1.DW,FAKS.ID)
   ?};
:: waluta opodatkowania niezgodna z walutą opodatkowania stanowiska
::   _nwal:=exec('bl_nwal','ustawienia');
::   {? FAKS.NWAL<>_nwal
::   ||
::      _dok_w:=FAKS.NWAL().KOD;
::      FAKS.NWAL==_nwal;
::      _oddz_w:=FAKS.NWAL().KOD;
::      FAKS.NWAL==_nwal;
::      exec('add_kom','#message','Waluta opodatkowania dokumentu (%1)'
::         ' jest niezgodna z walutą opodatkowania oddziału (%2)'[_dok_w,_oddz_w],2,__XML.ID);
::      EDI_Z.POMIN:='T'
::   ?};
   ~~
||
   {? _kor=0
   ||
      exec('add_faks_z','edi_form')
   ||
      exec('add_faks_zk','edi_form')
   ?};
   {? EDI_Z.POMIN='N'
   ||
      FAKS.memo_put(,'UWAGI');
::    jeśli nie podano daty sprzedaży to ustawiamy na datę wystawienia
      {? FAKS.D=date(0,0,0) & FAKS.DW<>date(0,0,0)
      || FAKS.D:=FAKS.DW;
         _msg:='Zmieniono pustą datę sprzedaży na datę wystawienia.'@;
         exec('add_kom','#message',_msg,2,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
      ?};
::    jeśli nie podano daty otrzymania to ustawiamy na datę bieżącą (wczytania dakumentu)
      {? FAKS.DO=date(0,0,0)
      || _msg:='Zmieniono pustą datę otrzymania na datę bieżącą.'@;
         exec('add_kom','#message',_msg,2,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?});
         FAKS.DO:=date()
      ?};
      {? FAKS.put()
      ||
         EDI_Z.DOK:=$FAKS.ref(); EDI_Z.SYM:=FAKS.SYM; EDI_Z.KH:=FAKS.KH;
         _result:=1
      ?}
   ||
      _result:=0
   ?}
?};
_result


\faks_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: zakonczenie dokumentu zakupu
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_verify:=exec('verify','edi_xml');
_kor:=POM.TYPYSP().KOR='T';

_ar:=FAKS.DW~1;
_am:=FAKS.DW~2;
{? EDI_Z.POMIN='N' & ~exec('czy_z_ok','okresy',-3,0,_ar,_am)
||
:: kontrola zamkniętego okresu
   _okr:=form(date(_ar,_am,0),,8);
   exec('add_kom','#message','Okres %1 aktualnie jest zamknięty brak możliwości rejestracji dokumentu.'@[_okr]
    ,2,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?});
   EDI_Z.POMIN:='T';
   _result:=0
?};

{? ~_result
|| exec('usun_fak','faktury_nag',0,,0)

|? _verify
||
   1

|? EDI_Z.POMIN='N'
||
:: aktualizacja wartości dokumentu
   exec('sumfak','faktury_wspolne');
:: aktualizacja płatności
   exec('plat_przel','faktury_plat',FAKS.ref());
   _msg:='Dodano dokument %1.'@[FAKS.SYM];
   exec('add_kom','#message',_msg,1,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?});
   exec('check','faktury_nag',1);
   FAKS.put(1);
:: dane do zapisu w dzienniku
   EDI_Z.SYM:=FAKS.SYM;
   EDI_Z.KH:=FAKS.KH
?};
_result


\faksv_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: wartość w stawkach - start
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
VAR_DEL.delete('__V');
__V:=obj_new(
   'WN','WV','WAL',
   'SV_ID','SV_P','SV_TSID',
   'UDF_CZY','WN_WAL','WV_WAL');
__V.WN:=0; __V.WV:=0; __V.WAL:='';
__V.SV_ID:=''; __V.SV_P:='bd'; __V.SV_TSID:='';
__V.UDF_CZY:=0;__V.WN_WAL:=0; __V.WV_WAL:=0;
{? _verify
||
   ~~
||
   FAKSV.index('FAKS_SV');
   FAKSV.prefix()
?};
FAKSV.blank();
:: włączenie kontroli zgodności wartości dokumentu w stawkach z wartościami pozycji dokumentu
:: dla dokumentów niewalutowych
{? FAKS.WAL=FAKS.NWAL
||
   FAKSV.R:=1
?};

1


\faksv_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: wartość w stawkach - koniec
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_verify:=exec('verify','edi_xml');
_kor:=POM.TYPYSP().KOR='T';
_wal:=FAKS.WAL<>FAKS.NWAL;
{? _verify
||
   {? _wal=0
   ||
::      _nwal:=exec('bl_nwal','ustawienia');
      FAKSV.SV:=exec('sv','edi_fo01',__V.SV_ID,__V.SV_P,__V.SV_TSID);
      FAKSV.WAL:=exec('wal','edi_fo01',__V.WAL);
      {? FAKSV.SV=null()
      ||
         _msg:='Brak stawki vat (%1,%2,%3) w systemie.'@[__V.SV_ID,__V.SV_P,__V.SV_TSID];
         exec('add_kom','#message',_msg,2,__XML.ID);
         exec('add_kom','#message',_msg,2,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?});
         _result:=0

      |? FAKSV.WAL=null()
      ||
         _msg:='Brak waluty %1 w systemie.'@[__V.WAL];
         exec('add_kom','#message',_msg,2,__XML.ID);
         exec('add_kom','#message',_msg,2,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?});
         _result:=0

::      |? FAKS.WAL<>_nwal
::      ||
::         _dok_w:=FAKSV.WAL().KOD;
::         FAKSV.WAL==_nwal;
::         _oddz_w:=FAKSV.WAL().KOD;
::         FAKSV.WAL==_nwal;
::         exec('add_kom','#message','Waluta opodatkowania dokumentu (%1)'
::            ' jest niezgodna z walutą opodatkowania oddziału (%2)'[_dok_w,_oddz_w],2,__XML.ID);
::         _result:=0
      ?}
   ?}

|? _wal=0
||
   _sv:=exec('sv','edi_fo01',__V.SV_ID,__V.SV_P,__V.SV_TSID);
   {? _sv=null()
   ||
      _result=0
   ||
      _wn:=0;
      _wv:=0;
      {? _kor
      ||
         _VT:=__N.VT;
         {? _VT.find_key(__V.SV_ID,__V.SV_P,)
         ||
            _wn:=_VT.WN;
            _wv:=_VT.WV
         ?}
      ?};
      FAKSV.FAKS:=$BEER.WYSTFAKS;
      FAKSV.WN:=__V.WN-_wn;
      FAKSV.WV:=__V.WV-_wv;
      FAKSV.WB:=FAKSV.WN+FAKSV.WV;
      FAKSV.WW:=FAKSV.WB;
      FAKSV.SV:=_sv;
      _result:=FAKSV.add()
   ?}
?};
_result


\fap_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: pozycja dokumentu zakupu - start
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_verify:=exec('verify','edi_xml');
_kor:=POM.TYPYSP().KOR='T';
VAR_DEL.delete('__P');
__P:=obj_new(
   'SV_ID','SV_P','SV_TSID',
   'PRICEA',
   'JM',
   'MKODK_KODK','MKODK_KTM','MKODK_N');
__P.SV_ID:=''; __P.SV_P:='bd'; __P.SV_TSID:='';
__P.PRICEA:=0;
__P.JM:='';
__P.MKODK_KODK:=''; __P.MKODK_KTM:=''; __P.MKODK_N:='';
_first:=__N.FIRST; __N.FIRST:=0;
FIND_M:='';
{? _first=1 & exec('faks_update','edi_fo01')=0
||
   _result:=0

|? EDI_Z.POMIN='T'
||
   _result:=0
||
   {? _kor=0
   ||
      FAP.prefix();
      ATR.MJS:='FAP';
      TAZ.RAB_TYP:=FAKS.RAB_TYP;
      BEER.MW:='K';
      exec('ustaw_ww','eurusd',BEER.MW); BEER.LW:={? BEER.WW || 'W' || '' ?};
      exec('ustafprz','faktury_wspolne');
      BEER.TAB:='FAP';
      FAP.blank();
      FAP.POZ:=-1;
      FAP.WAL:={? FAKS.WAL<>null || FAKS.WAL || INFO.NAROD ?};
      FAP.WAL_H:=FAP.WAL;
      exec('istatr_faks2fap','faktury_nag')
   ||
      ~~
   ?};
   {? _verify=1
   ||
      {? _kor=0
      ||
         ~~
      ||
         FAP.index('FAP');
::       w BEER.WYSTFAKS jest ref faktury korygowanej lub ostatniej korekty
         FAP.prefix(BEER.WYSTFAKS)
      ?};
      ~~
   ||
      {? _kor=0
      ||
         _result:=FAP.add()
      ||
         FAP.index('FAP');
::       w BEER.WYSTFAKS jest ref dodawanej korekty
         FAP.prefix(BEER.WYSTFAKS);
         ~~
      ?}
   ?}
?};
_result


\fap_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: pozycja dokumentu zakupu - koniec
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
:: Pomijamy następujące dane:
:: 3.8 Podstawowe dane pozycji faktury
:: (5) – pozycja kosztów u nabywcy
:: 3.9 Przedział dat dla linii faktury
:: 3.10 Numer pozycji zamówienia na poziomie linii faktury
:: 3.11 Referencja do dokumentu powiązanego
:: 3.12 Obciążenia i upusty dla pozycji (na poziomie linii faktury)
:: 3.13 Nazwa i opis towaru
:: DRO_TODO_AWI: 3.14 Identyfikator pozycji wg kupującego
:: 3.14 Identyfikator pozycji wg kupującego
:: DRO_TODO_AWI: Identyfikator pozycji wg sprzedającego
:: 3.15 Identyfikator pozycji wg sprzedającego
:: DRO_TODO_AWI: 3.17 Kraj pochodzenia pozycji
:: 3.17 Kraj pochodzenia pozycji
:: 3.18 Klasyfikacja pozycji faktury
:: 3.20 Dodatkowe parametry pozycji
:: 3.21 Cena pozycji
:: (1) – cena bazowa dla pozycji
:: (2) – upust w cenie pozycji
:: (3) – wskaźnik (upust - obowiązkowa wartość „false”)
:: (4) – ilość podstawowa dla ceny jednostkowej
{? EDI_Z.POMIN='T' || return(1) ?};
_result:=1;
_verify:=exec('verify','edi_xml');
_edi:=exec('edi','edi_wspolne');
_context:=_edi.context;
_kor:=POM.TYPYSP().KOR='T';
:: jednostki miary
FAP.JM:=FAP.M().J;
FAP.J2:=FAP.JM; FAP.WS2:=1; FAP.IL2:=FAP.IL;
{? _verify=1
||
   {? _kor=0
   ||
      {? FAP.M=null()
      ||
         {? ~_kor & _context.TYPE=exec('context_type_bl','edi_wspolne') & FAKS.KH
         ||
            exec('kodt_add','edi_fo_ufd',FAKS.KH,__P.MKODK_KODK,__P.MKODK_KTM,__P.MKODK_N);
            _msg:='Pozycja %1: wymagane mapowanie indeksu.'@[$FAP.POZ];
            {? __P.MKODK_KODK<>'' | __P.MKODK_KTM<>'' | __P.MKODK_N<>''
            || _msg:='Pozycja %1: wymagane mapowanie indeksu (%2; %3; %4).'@[$FAP.POZ,__P.MKODK_KODK,__P.MKODK_KTM,
                  __P.MKODK_N]
            ?};
            _result:=0
         ||
            {? var_pres('FIND_M')=type_of('')
            || {? FIND_M=''
               || _msg:='Pozycja %1: nie wypełniono materiału (nie podano kodu).'[$FAP.POZ]
               || _msg:='Pozycja %1: nie wypełniono materiału (nieznany kod: %2).'[$FAP.POZ,FIND_M]
               ?}
            || _msg:='Pozycja %1: nie wypełniono materiału.'[$FAP.POZ]
            ?}
         ?};
         exec('add_kom','#message',_msg,2,'%1 (%2)'[__XML.ID,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?}])
      ||
         _jm:=exec('jm','edi_fo01',__P.JM);
         _jmm:=FAP.M().J().KOD;
         {? _jm<>'' & _jmm<>_jm
         ||
            _msg:='Pozycja %1: jednostka (%2) niezgodna z jednostką materiału (%3).'[$FAP.POZ,_jm,_jmm];
            exec('add_kom','#message',_msg,2,_XML.ID);
            exec('add_kom','#message',_msg,2,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
         ?}
      ?}
   ||
      ~~
   ?}
:: DRO_TODO_AWI: kontrola dokładności ilości
:: DRO_TODO_AWI: kontrola dokładności ceny

|? EDI_Z.POMIN='N'
||
   exec('liczfak','faktury_wspolne');
   {? _kor=0
   ||
      ~~
   ||
      exec('spr_poz_kor','faktury_poz',1);
      exec('kor_end','edi_fo01')
   ?};
   exec('fill_fld2prn','faktury_poz');
:sprawdzenie czy pozycja ma wymagany split payment
   exec('fap_sp','edi_fo03');
   {? _kor=0
   ||
      _env_fak_poz:=exec('env','faktury_poz');
      params_set('env_fak_poz',_env_fak_poz);
      exec('chk_fakp','faktury_poz',1)
   ?};
   {? FAP.put()
   ||
      exec('inf_dod','fakso',0,5+FAP.name())
   ?}
?};
_result


\kor_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: pozycja korekty zakupu - start
::   WE: _a - pozycja
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_verify:=exec('verify','edi_xml');
{? BEER.WYSTFAKS=null() | ~FAP.find_key(_a)
||
   EDI_Z.POMIN:='P';
   {? _verify & BEER.WYSTFAKS<>null()
   ||
      exec('add_kom','#message','Pozycja %1: brak pozycji przed korektą. Pozycję należy dodać ręcznie.'[$_a],2,
         __XML.ID);
      exec('add_kom','#message','Pozycja %1: brak pozycji przed korektą. Pozycję należy dodać ręcznie.'[$_a],2,
           {? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   ?}
||
   exec('wysw_kor','faktury_poz');
   FAP.CWAL:=FAP.CWAL+FKOR.CWAL;
   FAP.CPR:=FAP.CPR+FKOR.CPR;
   FAP.CN:=FAP.CN+FKOR.CN;
   FAP.CB:=FAP.CB+FKOR.CB;

   {? FAP.POZP=0
   ||
      FAP.IL:=FAP.IL+FKOR.IL;
      FAP.IL2:=FAP.IL2+FKOR.IL2;
      FAP.WS2:=FAP.WS2+FKOR.WS2;

::    narodowa
      FAP.WN:=FAP.WN+FKOR.WN;
      FAP.WV:=FAP.WV+FKOR.WV;
      FAP.WB:=FAP.WB+FKOR.WB;

::    handlowa
      FAP.WWAL:=FAP.WWAL+FKOR.WWAL;
      FAP.VWAL:=FAP.VWAL+FKOR.VWAL;
      FAP.BWAL:=FAP.BWAL+FKOR.BWAL;

::    opodatkowania
      FAP.WWAL_P:=FAP.WWAL_P+FKOR.WWAL_P;
      FAP.VWAL_P:=FAP.VWAL_P+FKOR.VWAL_P;
      FAP.BWAL_P:=FAP.BWAL_P+FKOR.BWAL_P;

::    intrastat
      FAP.MASAN:=FAP.MASAN+FKOR.PMASAN;
      FAP.ILUJM:=FAP.ILUJM+FKOR.PILUJM;
      FAP.WF:=FAP.WF+FKOR.PWF;
      FAP.WS:=FAP.WS+FKOR.PWS
   ?}
?};
_result


\kor_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: pozycja korekty zakupu - koniec
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FKOR.M:=FAP.M;
FAP.CWAL:=FAP.CWAL-FKOR.CWAL;
FAP.CPR:=FAP.CPR-FKOR.CPR;
FAP.CN:=FAP.CN-FKOR.CN;
FAP.CB:=FAP.CB-FKOR.CB;

{? FAP.POZP=0
||
   _vat:=#((FAP.SV().KOD*'%') + FAP.SV().KOD-1) / 100;

   FAP.IL:=FAP.IL-FKOR.IL;
   FAP.IL2:=FAP.IL2-FKOR.IL2;
   FAP.WS2:=FAP.WS2-FKOR.WS2;

   FAP.WN:=FAP.WN-FKOR.WN;
   FAP.WV:=FAP.WV-FKOR.WV;
   FAP.WB:=FAP.WB-FKOR.WB;

:: handlowa
   FAP.WWAL:=FAP.WWAL-FKOR.WWAL;
   FAP.VWAL:=FAP.VWAL-FKOR.VWAL;
   FAP.BWAL:=FAP.BWAL-FKOR.BWAL;

:: opodatkowania
   FAP.WWAL_P:=FAP.WWAL_P-FKOR.WWAL_P;
   FAP.VWAL_P:=FAP.VWAL_P-FKOR.VWAL_P;
   FAP.BWAL_P:=FAP.BWAL_P-FKOR.BWAL_P;

:: intrastat
   FAP.MASAN:=FAP.MASAN-FKOR.PMASAN;
   FAP.ILUJM:=FAP.ILUJM-FKOR.PILUJM;
   FAP.WF:=FAP.WF-FKOR.PWF;
   FAP.WS:=FAP.WS-FKOR.PWS
?};
1


\w_faks_sg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: write - dokument sprzedaży - start grupa
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('efallkor','edi_wspolne');
1


\w_faks_eg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: write - dokument sprzedaży - end grupa
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('efdelkor','edi_wspolne');
1


\w_faks_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: write - dokument sprzedaży - start document
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__N','__V','__P','__CDATA');
EDI_Z.POMIN:='N';
__CDATA:=1;
{? FAKS.EDI_W='T'
||
   exec('add_kom','#message','Dokument %1 jest już zapisany.'@[FAKS.SYM],2,EDI_Z.SYM);
   0
||
   1
?}


\w_faks_ed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: write - dokument sprzedaży - end document
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__sumkor.erase();
VAR_DEL.delete('__N','__V','__P','__CDATA');
{? EDI_Z.POMIN='N'
||
   FAKS.EDI_W:='T';
   FAKS.put()
?};
~~


\dok_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: dokumenty do zapisu - sprzedaży
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('dok_faks','edi_fo01',0)


\dok_spr_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: dokumenty do zapisu - sprzedazy korygujące
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('dok_faks','edi_fo01',1)


\dok_faks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: dokumenty do zapisu - faks
::       gdy zmienna BEER.MW='F' to wywolanie dla biezacej faktury w buforze bez dodatkowego dialogu
::   WE: _a - 0-dokument sprzedaży, 1-dokument korygujący
::       [_b] - S - sprzedaż (domyślnie), Z - zakup (RR rolnik ryczałtowy)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kor:=_a;
_spr_zak:={? var_pres('_b')=type_of('') || _b || 'S' ?};

_result:=obj_new('DOK','TXT');
_result.DOK:=~~;
_result.TXT:='';

_edi:=__ZWSEDIUNIW;
_ref:=_edi.REF;

_Sel:=exec('doc2write','edi_xml');

:: jeden dokument
{? BEER.MW='F' | _ref
||
   _add:=0;
   {? _ref
   ||
      FAKS.cntx_psh();
      FAKS.use(ref_name(_ref));
      FAKS.prefix();
      {? FAKS.seek(_ref)
      ||
         _faks:=FAKS.ref();
         _sym:=FAKS.SYM;
         _kh:=FAKS.KH;
         _add:=1
      ?};
      FAKS.cntx_pop()

   |? FAKS.get()
   ||
      _faks:=FAKS.ref();
      _sym:=FAKS.SYM;
      _kh:=FAKS.KH;
      _add:=1
   ?};
   {? _add
   ||
      _Sel.REF:=$_faks;
      _Sel.ID:=_sym;
      _Sel.KH:=$_kh;
      DEZEZW.cntx_psh();
      DEZEZW.index('WR');
      DEZEZW.prefix('ZWS',EDI_Z.C,EDI_Z.WR,_kh,);
      _Sel.PATH:={? DEZEZW.last() || DEZEZW.FILE_PTH || '' ?};
      DEZEZW.cntx_pop();
      {? _Sel.add() || _result.DOK:=_Sel ?}
   ?};
   return(_result)
?};
{? _edi.sza
||
   _result.TXT:='Przetwarzanie bezdialogowe. Nie wskazano dokumentu.';
   return(_result)
?};
:: wiele dokumentów
{? 0
||
   _Faks:=sql("
      select
         'N' MARK,
         KH.NAZ KH_NAZ,
         FAKS.DW DW,
         FAKS.SYM SYM,
         FAKS.BRUTTO BRUTTO,
         FAKS.REFERENCE REF,
         KH.REFERENCE KH_REF
      from
         @FAKS
         join KH using(FAKS.KH,KH.reference)
         join DEZEZW using(DEZEZW.KH,KH.reference)
         join TYPYSP using(FAKS.T,TYPYSP.reference)
      where
         DEZEZW.ISTDEF like :_a
         and DEZEZW.WR = ':_b'
         and DEZEZW.B = 'N'
         and TYPYSP.PAR = 'N'
         and TYPYSP.ZAL = 'N'
         and FAKS.SZ = "+{? _spr_zak='Z' || "'Z'" || "'S'" ?}+"
         and FAKS.EDI_W = 'N'
         and FAKS.AKC = 'T'
         and FAKS.DW >= DEZEZW.OD and (FAKS.DW <= DEZEZW.DO or DEZEZW.DO is null)
   "+
   {? _kor
   || "  and TYPYSP.KOR = 'T' and FAKS.KZ=''"
   || "  and TYPYSP.KOR = 'N'"
   ?}+
   {? _spr_zak='Z'
   || " and KH.TYP='R'"
   || ""
   ?}+"
      order by
         KH_NAZ, DW, SYM
   ",EDI_Z.C,EDI_Z.WR);
   {? _Faks.first()
   ||
      _result.DOK:=_Sel;
      DEZEZW.cntx_psh();
      _wer:=_Faks.mk_sel('EDI - dokumenty do zapisu'@,,0,'edi_dok2w_faks',,,,,'U');
      _Faks.win_sel(_wer);
      _Faks.win_fld(_wer,,'MARK',,,10,,,'Wybrano'@,,,2,,"'T'","'N'");
      _Faks.win_fld(_wer,,'KH_NAZ',,,30,,1,'Kontrahent'@);
      _Faks.win_fld(_wer,,'DW',,,10,,1,'Data'@);
      _Faks.win_fld(_wer,,'SYM',,,20,,1,'Symbol'@);
      _Faks.win_fld(_wer,,'BRUTTO',,,12,2,1,'Wartość'@);
      _Faks.win_act(_wer,0,'Szukaj',,,,,,0);
      _fb:="
         _Faks:=cur_tab();
         _Faks.MARK:='T';
         _Faks.put()
      ";
      _Faks.win_act(_wer,,'Formuła','Wybierz'@@,,,_fb,,1,1);
      _Faks.win_btn(_wer,'text=%1, panel=right'['Wybierz'@@],'menu:W');
      _fb:="
         _Faks:=cur_tab();
         _Faks.MARK:='N';
         _Faks.put()
      ";
      _Faks.win_act(_wer,,'Formuła','Odbierz'@@,,,_fb,,,1);
      _Faks.win_btn(_wer,'text=%1, panel=right'['Odbierz'@@],'menu:O');
      _fb:="
         {? _a
         ||
            _Faks:=cur_tab();
            _win_sel:=cur_win();
            _default:={? _Faks.sel_size()=0 & _Faks.MARK='T' || 'O' || 'W' ?};
            _Faks.actions(_win_sel,,_default,1)
         ?}
      ";
      _Faks.win_act(_wer,,'Rekord',,,,_fb);
      _fb:="exec('FindAndGet','#table',FAKS,cur_tab().REF,,\"exec('fall_rek','faktury_nag')\")";
      _Faks.win_act(_wer,0,'Wyświetl',,,,_fb,,0);
      _fb:="{? FUN.ask('Czy zatwierdzić wybór?'@) || sel_exit() ?}";
      _Faks.win_act(_wer,,'Formuła','Zapisz'@@,,'Uruchamia zapis wybranych dokumentów'@,_fb);
      _Faks.win_btn(_wer,'text=%1, panel=bottom'['&Zapisz'@],'menu:Z');
      _Faks.win_btn(_wer,'text=%1, panel=bottom'['A&nuluj'@@],'key:Esc');

      {? _Faks.select()
      ||
         _loop:=_Faks.first();
         {!
         |? _loop
         |!
            {? _Faks.MARK='T'
            ||
               _Sel.REF:=_Faks.REF;
               _Sel.ID:=_Faks.SYM;
               _Sel.KH:=_Faks.KH_REF;
               _kh:=exec('FindAndGet','#table',KH,_Faks.KH_REF,,,null());
               DEZEZW.index('WR');
               DEZEZW.prefix('ZWS',EDI_Z.C,EDI_Z.WR,_kh);
               _Sel.PATH:={? DEZEZW.last() || DEZEZW.FILE_PTH || '' ?};
               _Sel.add()
            ?};
            _loop:=_Faks.next()
         !}
      ?};
      DEZEZW.cntx_pop()
   ?}
||
   _sort:='KH,DW,SYM';
   _from:='join KH using("FAKS".KH,KH.reference) '+
      'join DEZEZW using(DEZEZW.KH,KH.reference) '+
      'join TYPYSP using("FAKS".T,TYPYSP.reference) ';
   _where:='DEZEZW.ISTDEF like :_a'+
      ' and DEZEZW.WR = \':_b\''+
      ' and DEZEZW.B = \'N\''+
      {? _kor || ' and TYPYSP.KOR = \'T\' and FAKS.KZ=\'\''  || ' and TYPYSP.KOR = \'N\'' ?}+
      ' and TYPYSP.PAR = \'N\''+
      ' and TYPYSP.ZAL = \'N\''+
      ' and "FAKS".SZ = \''+_spr_zak+'\''+
      ' and "FAKS".EDI_W = \'N\''+
      ' and "FAKS".AKC = \'T\''+
      ' and "FAKS".DW >= DEZEZW.OD and ("FAKS".DW <= DEZEZW.DO or DEZEZW.DO is null)'+
      {? _spr_zak='Z' || ' and "KH".TYP=\'R\'' || '' ?};
   FAKS.prefix();
   FAKS.f_set(_sort,_from,_where,EDI_Z.C,EDI_Z.WR);

   {? ~FAKS.f_first()
   ||
      _result.TXT:='Rok %1.'[form(ST.AR,,,'99')]
   ||
      _result.DOK:=_Sel;
      DEZEZW.cntx_psh();
      VAR_DEL.delete('__SEL');
      __SEL:=_Sel;
      _wer:=FAKS.mk_sel('EDI - dokumenty do zapisu'@,,0,,,,,,'U');
      FAKS.win_sel(_wer);
      FAKS.win_fld(_wer,,'KH','NAZ',,30,,1,'Kontrahent'@);
      FAKS.win_fld(_wer,,'DW',,,10,,1,'Data'@);
      FAKS.win_fld(_wer,,'SYM',,,20,,1,'Symbol'@);
      FAKS.win_fld(_wer,,'BRUTTO',,,12,2,1,'Wartość'@);
      FAKS.win_act(_wer,0,'Szukaj',,,,,,0);
      _fb:="
         _Sel:=FAKS.sel_aget();
         {? ~_Sel.first() || _Sel.REF:=#FAKS.ref(); _Sel.add() ?};
         _loop:=_Sel.first();
         {!
         |? _loop
         |!
            {? FAKS.seek(_Sel.REF,)
            ||
               __SEL.REF:=$FAKS.ref();
               __SEL.ID:=FAKS.SYM;
               __SEL.KH:=$FAKS.KH;
               DEZEZW.index('WR');
               DEZEZW.prefix('ZWS',EDI_Z.C,EDI_Z.WR,FAKS.KH);
               __SEL.PATH:={? DEZEZW.last() || DEZEZW.FILE_PTH || '' ?};
               __SEL.add()
            ?};
            _loop:=_Sel.next()
         !};
         sel_exit();
         0
      ";
      FAKS.win_act(_wer,0,'Formuła','Zapisz'@,,'Uruchamia zapis wybranych dokumentów'@,_fb,,1,1,_fb);
      FAKS.win_act(_wer,0,'Wyświetl',,,,"FAKS.f_get(); exec('fall_rek','faktury_nag')",,0);

      FAKS.select();

      VAR_DEL.delete('__SEL');
      DEZEZW.cntx_pop()
   ?};
   FAKS.f_clear()
?};
_result


\zkn_b
::      VAR_DEL.delete('__matakt','__EDITMS');
::      __matakt:=tab_tmp(2
::         ,'REF','STRING[16]',''
::         ,'NRK','INTEGER',''
::         ,'ILR','REAL',''
::         ,'ZKP','STRING[16]',''
::         ,'ZKN','STRING[16]','');

\zkn_a
::      odtwarzanie statusow zamowien
::      exec('akt_rezy','rezerw');
::      VAR_DEL.delete('__matakt','__EDITMS');


\slo2sv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: wyznaczenie kodu stawki vat dla SLO.ref()
::   WE: _a - SLO.ref()
::       _b - [0/1] eksportowa
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:='';
SLU.cntx_psh();
SLU.index('NAZ');
SLU.prefix('~STAWKI VAT PL');
{? SLU.first()
||
   SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(SLU.ref());
   {? SLO.seek(_a)
   ||
      _result:=
         {? SLO.KOD=' -' & _b || 'G'
         |? SLO.KOD=' -'      || 'O'
         |? SLO.KOD=' -o'     || 'AE'
         |? SLO.KOD=' -zw0'   || 'K'
         |? SLO.KOD='Zwol.'   || 'E'
         |? SLO.KOD+1='%'     || 'S'
         || ''
         ?}
   ?};
   SLO.cntx_pop();
   ~~
?};
SLU.cntx_pop();
_result


\sv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: wyznacza SLO.ref() dla kodu stawki vat _a
::   WE: _a - kategoria
::             Kod   Opis
::             AE    Odwrotne opodatkowanie - Vat Reverse Charge
::             E     Nie podlega opodatkowaniu - Exempt from Tax
::             S     Standardowa stawka - Standard rate
::             Z     0% opodatkowania - Zero rated goods
::             G     Pozycja eksportowana, bez naliczania podatku - Free export item, tax not charged
::             O     Usługi nieopodatkowane - Services outside scope of tax
::             K     Wyłączenie opodatkowania  dla wewnątrz wspólnotowej dostawy towarów
::                   lub usług - VAT exempt for EEA intra-community supply of goods and services
::             L     Podatek pośredni na Wyspach Kanaryjskich (Canary
::             M     Podatek w przypadku produkcji, usług i importu w Ceucie i Melilli
::       _b - [STRING] procent
::       _c - rodzaj
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
{? _c='VAT'
||
   SLO.cntx_psh();
   SLO.index('SL');
   SLU.cntx_psh();
   SLU.index('NAZ');
   SLU.prefix('~STAWKI VAT PL');
   {? ~SLU.first()
   ||
      ~~

   |? _a='S'
   ||
::    'bd' przypisywane w formule - start document
      {? _b<>'bd'
      ||
         _val:=(' '+_b)+2;
         SLO.prefix(SLU.ref(),_val+' %');
         {? SLO.first() || _result:=SLO.ref() ?}
      ?}

   |? _a='AE'
   ||
      SLO.prefix(SLU.ref(),' -o');
      {? SLO.first() || _result:=SLO.ref() ?}

   |? _a='E'
   ||
      SLO.prefix(SLU.ref(),'Zwol.');
      {? SLO.first() || _result:=SLO.ref() ?}

   |? _a='Z'
   ||
      SLO.prefix(SLU.ref(),' 0 %');
      {? SLO.first() || _result:=SLO.ref() ?}

   |? _a='G'
   ||
      SLO.prefix(SLU.ref(),' -');
      {? SLO.first() || _result:=SLO.ref() ?}

   |? _a='O'
   ||
      SLO.prefix(SLU.ref(),' -');
      {? SLO.first() || _result:=SLO.ref() ?}

   |? _a='K'
   ||
      SLO.prefix(SLU.ref(),' -zw0');
      {? SLO.first() || _result:=SLO.ref() ?}
   ?};
   SLU.cntx_pop();
   SLO.cntx_pop()
?};
_result


\wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: wyznacza ref waluty
::   WE: _a - kod waluty
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
{? XINFO.SLWAL
||
   SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(XINFO.SLWAL,_a,);
   {? SLO.first() || _result:=SLO.ref() ?};
   SLO.cntx_pop()
?};
_result


\jm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: translacja jm wg:
::       UN/ECE Recommandation 20
::       UN/ECE Recommandation 21
::   WE: _a - jm
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: sztuka
{? _a='H87' || 'szt' |? _a='szt' || 'H87'
:: kilogram
|? _a='KGM' || 'kg'  |? _a='kg'  || 'KGM'
:: kilowat
|? _a='KWT' || 'kwt' |? _a='kwt' || 'KWT'
:: kilowatogodzina
|? _a='KWH' || 'kwh' |? _a='kwh' || 'KWH'
:: liczba artykułów
|? _a='NAR' || 'nar' |? _a='nar' || 'KWT'
:: litr
|? _a='LTR' || 'l'   |? _a='l'   || 'LTR'
:: metr
|? _a='MTR' || 'm'   |? _a='m'   || 'MTR'
:: metr kwadratowy
|? _a='MTK' || 'm2'  |? _a='m2'  || 'MTK'
:: metr sześcienny
|? _a='MTQ' || 'm3'  |? _a='m3'  || 'MTQ'
:: opakowanie
|? _a='C62' || 'op'  |? _a='op'  || 'C62'
:: rok
|? _a='ANN' || 'ann' |? _a='ann' || 'ANN'
:: miesiąc
|? _a='MON' || 'mon' |? _a='mon' || 'MON'
:: tydzień
|? _a='WEE' || 'wee' |? _a='wee' || 'WEE'
:: dzień
|? _a='DAY' || 'day' |? _a='day' || 'DAY'
:: godzina
|? _a='HUR' || 'h'   |? _a='h'   || 'HUR'
            || ''
?}


\kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS:
::   WE: _a - kod
::       _b - typ
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: gln
{? _b='0088'
||
   exec('find_kh','edi_form',_a)
:: nip
|? _b='9945'
||
   exec('FindInSet','#table','KH','SNIP',_a,2,,1,,null())
?}


\bl_faks_zk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: poczatek dokumentu korekty zakupu
::   WE: _a - typ dokumentu korekty zakupu
::       _b - KH.ref
::       _c - numer dokumentu korygowanego
::       _d - data dokumentu korygowanego
::       _e - data wystawienia korekty
::       _f - numer korekty
::----------------------------------------------------------------------------------------------------------------------
_typsp:=_a;
_kh:=_b;
_kid:=_c;
_kda:=_d;
_dw:=_e;
_did:=_f;

_verify:=exec('verify','edi_xml');

:: szukanie dokumentu do korekty
:: _kref - FAKS.ref() dokumentu korygowanego
_kref:=null();
{? _kh & _kid<>'' & _kda<>date(0,0,0)
|| KH.cntx_psh();
   KH.seek(_kh);
   _kh_naz:=KH.NAZ;
   KH.cntx_pop();
   _Chk:=sql('
      select REFERENCE as REF
      from @FAKS
      where KH=:_a and ID=\':_b\' and DW=to_date(:_c)'
      ,_kh,_kid,_kda);
   {? _Chk.first() & _Chk.REF<>''
   || {? exec('FindAndGet','#table',FAKS,_Chk.REF,,"FAKS.STAT_REJ",'N')='T'
      || _fda:=exec('FindAndGet','#table',FAKS,_Chk.REF,,"FAKS.D",date(0,0,0));
         _szuk_kor:=exec('FindAndGet','#table',FAKS,_Chk.REF,,"
            _fda:=_b;
            exec('szuk_kor','faktury_nag',$FAKS.ref(),_fda,1)
         ",0,_fda);
         {? _szuk_kor
         || _kref:=exec('FindAndGet','#table',FAKS,_Chk.REF,,,null())
         || exec('add_kom','#message','Nie wszystkie korekty (%1, %2, %3) są zaakceptowane.'@[_kid,$_kda,_kh_naz],2,_did)
         ?}
      || exec('add_kom','#message','Korygowany dokument (%1, %2, %3) jest niezaakceptowany.'@[_kid,$_kda,_kh_naz],2,_did)
      ?}
   || exec('add_kom','#message','Korygowany dokument (%1, %2, %3) nie istnieje w systemie.'@[_kid,$_kda,_kh_naz],2,_did)
   ?}
|| {? _kh = null()
   || _msg:='Nie podano kontrahenta'@;
      {? var_pres('FIND_KH2')=type_of('')
      || {? FIND_KH2<>''
         || _msg:=_msg+' (nieznany kod: %1)'@[FIND_KH2]
         || _msg:=_msg+' (nie podano kodu)'@
         ?}
      ?};
      _msg+='.';
      exec('add_kom','#message',_msg,2,_did)
   ?};
   {? _kid=''
   || _msg:='Nie podano numeru korygowanego dokumentu.'@;
      exec('add_kom','#message',_msg,2,_did)
   ?};
   {? _kda=date(0,0,0)
   || _msg:='Nie podano daty korygowanego dokumentu.'@;
      exec('add_kom','#message',_msg,2,_did)
   ?};
   _msg:='Nie można odnaleźć korygowanego dokumentu. Nie utworzono dokumentu korygującego.'@;
   exec('add_kom','#message',_msg,4,_did)
?};

{? _kref=null()
||
   {? _verify=0 || EDI_Z.POMIN:='T' ?};
   BEER.WYSTFAKS:=null();
   return(0)

|? _verify
||
   BEER.WYSTFAKS:=_kref;
   return(1)
?};
:: dodanie nagłówka korekty
_result:=0;
_kor:=exec('FindAndGet','#table',FAKS,$_kref,,"
   _kda:=_b;
   exec('szuk_kor','faktury_nag',$FAKS.ref(),_kda,1)
",0,_kda);
{? _kor=0 | xx_nam=''
|| _fksql:=$_kref;
   _lksql:=$_kref;
   _ofnum:=1;
   _frab:=exec('FindAndGet','#table',FAKS,$_kref,,"FAKS.RAB",0)
|| _fksql:=$_kref;
   _lksql:=form(xx_nam)+((('0'*8)+(-BB.hex(xx_ref)))+8);
   _ofnum:=xx_num+1;
   _frab:=xx_rab
?};
_dsprz:=exec('FindAndGet','#table',FAKS,$_kref,,"FAKS.D",date(0,0,0));
_fsym:=exec('FindAndGet','#table',FAKS,$_kref,,"FAKS.SYM",'');
_kor_id:=exec('FindAndGet','#table',FAKS,$_kref,,"FAKS.ID",'');
_wal:=exec('FindAndGet','#table',FAKS,$_kref,,"FAKS.WAL",null());
_krs:=exec('FindAndGet','#table',FAKS,$_kref,,"FAKS.KRS",0);
_nkrs:=exec('FindAndGet','#table',FAKS,$_kref,,"FAKS.NKRS",0);
_dtk:=exec('FindAndGet','#table',FAKS,$_kref,,"FAKS.DTK",date(0,0,0));
_ndtk:=exec('FindAndGet','#table',FAKS,$_kref,,"FAKS.NDTK",date(0,0,0));

FAKS.clear(); FAKS.blank(); FAKS.memo_set(,'UWAGI');
{? exec('add','faktury_nag',_typsp,_dsprz,_lksql,_kh,_fksql,_fsym,,_frab,,,_dw,,_ofnum,,,_kor_id)
||
   FAKS.SP_WYM:='N';
   BEER.WYSTFAKS:=FAKS.ref();
   {? _wal<>null() & _wal<>FAKS.WAL
   || FAKS.WAL:=_wal;
      FAKS.KRS:=_krs;
      FAKS.NKRS:=_nkrs;
      FAKS.DTK:=_dtk;
      FAKS.NDTK:=_ndtk
   ?};
   exec('akt_kor','faktury_nag');
   _result:=1
?};
_result


\war_nc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: wartość walutowa i opodatkowania dokumentu bez dokumentu korygowanego
::   WE: _a - FAKS.ref() lub $FAKS.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('war_n','edi_fo01',_a,,0)


\war_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: wartość walutowa i opodatkowania dokumentu
::   WE: _a - FAKS.ref() lub $FAKS.ref()
::       [_b] - wartość dokumentu
::       [_c] - 0-wartość korekty, [1]-po korekcie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_faks:=_a;
{? var_pres('_b')=type_of(obj_new('obj'))
||
   _T:=_b
||
   _T:=obj_new('NETTO','VAT','BRUTTO','NETW','VATW','BRTW');
   _T.NETTO:=0; _T.VAT:=0; _T.BRUTTO:=0;
   _T.NETW:=0; _T.VATW:=0; _T.BRTW:=0
?};
_po_kor:={? var_pres('_c')=type_of(0) || _c || _po_kor:=1 ?};

FAKS.cntx_psh();
FAKS.use(ref_name(_faks));
FAKS.cntx_psh();
FAKS.prefix();
{? FAKS.seek(_faks)
||
   {? FAKS.LKSQL<>'' & (_po_kor | FAKS.LKSQL<>FAKS.FKSQL)
   ||
      exec('war_n','edi_fo01',FAKS.LKSQL,_T,_po_kor)
   ?};
   _T.NETTO+=FAKS.NETTO;
   _T.VAT+=FAKS.VAT;
   _T.BRUTTO+=FAKS.BRUTTO;
   _T.NETW+=FAKS.NETW;
   _T.BRTW+=FAKS.BRTW;
   _T.VATW+=(FAKS.BRTW-FAKS.NETW)
?};
FAKS.cntx_pop();
FAKS.cntx_pop();

_T


\war_vc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: rozpiska vat dokumentu bez dokumentu korygowanego
::   WE: _a - FAKS.ref() lub $FAKS.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('war_v','edi_fo01',_a,,0)


\war_v
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: rozpiska vat dokumentu
::   WE: _a - FAKS.ref() lub $FAKS.ref()
::       [_b] - tabela z rozpiską
::       [_c] - 0-wartość korekty, [1]-po korekcie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_faks:=_a;
{? var_pres('_b')=type_of(FIRMA)
||
   _T:=_b
||
   _T:=tab_tmp(2
      ,'SV_ID',   'STRING[2]',   'Stawka vat - ID'
      ,'SV_P',    'STRING[2]',   'Stawka vat - procent'
      ,'WN',      'REAL',        'Netto'
      ,'WV',      'REAL',        'VAT'
      ,'WB',      'REAL',        'Brutto')
?};
_po_kor:={? var_pres('_c')=type_of(0) || _c || _po_kor:=1 ?};

_name:=ref_name(_faks);
{? _name='' || return(_T) ?};

FAKS.cntx_psh();
FAKS.use(_name);
FAKS.cntx_psh();
FAKS.prefix();
{? FAKS.seek(_faks)
||
   {? FAKS.LKSQL<>'' & (_po_kor | FAKS.LKSQL<>FAKS.FKSQL)
   ||
      exec('war_v','edi_fo01',FAKS.LKSQL,_T,_po_kor)
   ?};
   FAKSV.cntx_psh();
   FAKSV.use(gsub(FAKSV.name(1),'?','')+(FAKS.name()+3));
   FAKSV.cntx_psh();
   FAKSV.index('FAKS_SV');
   FAKSV.prefix($FAKS.ref());
   _loop:=FAKSV.first();
   {!
   |? _loop
   |!
      _sv_id:=exec('slo2sv','edi_fo01',FAKSV.SV,FAKS.T().EXPORT='T');
      _sv_p:=FAKSV.SV().KOD;
      {? _sv_p+1='%' || _sv_p:=form(_sv_p-1) ?};
      {? _sv_id<>''
      ||
         {? _T.find_key(_sv_id,_sv_p)
         ||
            _T.WN+=FAKSV.WN;
            _T.WV+=FAKSV.WV;
            _T.WB+=FAKSV.WB;
            _T.put()
         ||
            _T.SV_ID:=_sv_id;
            _T.SV_P:=_sv_p;
            _T.WN:=FAKSV.WN;
            _T.WV:=FAKSV.WV;
            _T.WB:=FAKSV.WB;
            _T.add()
         ?}
      ?};
      _loop:=FAKSV.next()
   !};
   FAKSV.cntx_pop();
   FAKSV.cntx_pop()
?};
FAKS.cntx_pop();
FAKS.cntx_pop();

_T


\dokum_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: dodaje załączniki dokumentu
::   WE: _a - mimeCode
::       _b - filename
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_mimec:=_a;
_filen:=_b;
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
{? __XML.VAL='' || return(1) ?};
DOKUM.cntx_psh();
DOKUM.prefix();
DOKUM.blank();
DOKUM.bl_file('DOKUM',1);
_dest:=fopen(DOKUM.DOKUM,'bw',,,1);
{? _dest.is_open()
||
   DOKUM.REFSQL:=$FAKS.ref();
   DOKUM.TYP:='D';
   DOKUM.NAZWA:=_filen;
   DOKUM.KR_OP:='mimeCode="%1", filename="%2"'[_mimec,_filen];
   _tab:=POM.TAB;
   _typdok:=POM.TYPDOK;
   POM.TAB:='DOKUM';
   POM.TYPDOK:='SYS';
   exec('add_grnr','numery','SYS');
   DOKUM.NR:=exec('numer_new','numery','PACZKA');
   {? DOKUM.add()
   ||
      exec('znak','numery','DOKUM');
      {? __XML.T.VTRUNC='T'
      ||
         _src:=fopen(__XML.T.BVAL,'br',,,1);
         {? _src.is_open()
         ||
            base64('decode',_src,_dest)
         ?}
      ||
         base64('decode',__XML.VAL,_dest)
      ?};
      DOKUM.bl_put('DOKUM',_dest,,,_filen)
   ?};
   POM.TAB:=_tab;
   POM.TYPDOK:=_typdok
?};
DOKUM.cntx_pop();
1


\w_zdnag_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: write - zamówienie dostawy - start document
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CDATA');
__CDATA:=1;
EDI_Z.POMIN:='N';
1


\w_zdnag_ed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: write - zamówienie dostawy - end document
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CDATA');
{? EDI_Z.POMIN='N'
||
   ZD_NAG.EDI_W:='T';
   ZD_NAG.put()
?};
~~


\w_nd_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: write - dokument magazynowy - start document
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CDATA');
__CDATA:=1;
EDI_Z.POMIN:='N';
1


\w_nd_ed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: write - dokument magazynowy - end document
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CDATA');
{? EDI_Z.POMIN='N'
||
   ND.EDI_W:='T';
   ND.put()
?};
~~


\kor_find_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: pozycja korekty zakupu - sprawdzenie zgodności towaru
::   WE: _a - EAN
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: FAP - pozycja dokumentu korygowanego
_result:=1;
_verify:=exec('verify','edi_xml');
{? _verify
|| _msg:='';
   _mat:=exec('find_m','edi_form',_a,FAKS.KH);
   {? _mat=null()
   || {? var_pres('FIND_M')=type_of('')
      || {? FIND_M=''
         || _msg:='Pozycja %1: nie wypełniono materiału (nie podano kodu).'@[$FAP.POZ]
         || _msg:='Pozycja %1: nie wypełniono materiału (nieznany kod: %2).'@[$FAP.POZ,FIND_M]
         ?}
      || _msg:='Pozycja %1: nie wypełniono materiału.'[$FAP.POZ]
      ?}
   |? _mat<>FAP.M
   || M.cntx_psh();
      M.clear();
      M.seek(_mat);
      _ktm:=M.KTM;
      M.cntx_pop();
      _msg:='Pozycja %1: inny materiał (%2) niż w pozycji przed korektą.'@[$FAP.POZ,_ktm]
   ?};
   {? _msg<>''
   || _msg+=' Zastąpione materiałem z pozycji przed korektą.'@;
      exec('add_kom','#message',_msg,2,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   ?}
?};
_result


\dok_zak_rr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: dokumenty do zapisu - zakupu RR
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('dok_faks','edi_fo01',0,'Z')


\dok_zak_rr_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: dokumenty do zapisu - zakup korygujące RR
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('dok_faks','edi_fo01',1,'Z')

:Sign Version 2.0 jowisz:1045 2023/11/30 12:42:34 28546cc1c1d4c5747456e40ee15b2285ca3dc4734138d0cce0670b30d5bf6b0a3ca1ace6a53dfbfc954b5e5efed601be0e78010659b841399bf2e5485b2b4bf1d6ec65ad8f0c524935cd58aae22b281ac4000c457c32a2daaef6614092c83544532b1af479d5da1218f09557fa72c9ca42d4b87254a62abe0783645bd3a5f386

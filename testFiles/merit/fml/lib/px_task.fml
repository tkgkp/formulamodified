:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: px_task.fml
:: Utworzony: 19.09.2011
:: Autor: WH
::======================================================================================================================
:: Zawartość: Plan strategiczny - obsługa zadań zdalnie przeliczających plan
::======================================================================================================================


\plan_licz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Zadanie przeliczenia planu
::   WY: _res[1] wynik (0 /1), _res[2] opis
::----------------------------------------------------------------------------------------------------------------------
_res:=obj_new(2);
PX_TASK.index('GO');
PX_TASK.prefix('T');

{? exec('is_one_version','px_param')>0
||
   _res[1]:=0;
   _res[2]:='W parametrach systemu ustawiono \'Pracę na jednej wersji planu\'. '
            'Należy wyłączyć zadanie przeliczania planu przez system ALERT.'
||
:: przetwarzam zawsze tylko jedno zadanie - pierwszy do przeliczenia plan
   {? PX_TASK.first()
   ||
::    przed przeliczaniem powoluje niezbedne do zycia obiekty
      exec('init_all','px_init');

:: TODO - TOMEK! NAPRAW!
::      __SUB:='Rozpoczęcie przeliczania planu';
::      exec('add_eml','skid_eml','PLAN_LICZ_START');
      PX_TASK.cntx_psh();
      PX_TASK.clear();
      PX_TASK.GO:='N';
      PX_TASK.RUN:='T';
      PX_TASK.COMPLETE:='N';
      PX_TASK.put();
      PX_TASK.cntx_pop();

::    czyszcze prefix bo podczas przeliczania prefix na PX_TASK psuje PX_TASK.get()
      PX_TASK.clear();

      _res[1]:=1;
      _res[2]:='Nieznany błąd';

      _argsplan:=exec('px_ver_plan_a','px_logix');
      _argsplan.DEST:=PX_TASK.PX_VER;
      _argsplan.VIEW_MODE:=0;
      _argsplan.UPDATE_Q:=0;
      _argsplan.VERIFY_Q:=0;

      {? exec('px_ver_plan','px_logix',_argsplan)<=0
      || _res[1]:=0;
         _res[2]:='Zdalne przeliczenie planu zakończone błędem.'
      ?};

      PX_TASK.get();
      {? PX_TASK.BREAK='T'
      || _res[2]:=exec('break_msg','px_task')
      ?};
      {? PX_TASK.COMPLETE='T'
      || _res[2]:=exec('done_msg','px_task')
      ?}
   || _res[1]:=1;
      _res[2]:='Brak planu do przeliczenia.'
   ?}
?};
_res


\task_go
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Uruchomienie przetwarzania z okna
::   WE: _a - PX_TASK.ref() - zadanie do uruchomienia
::       [_b] - [0] / 1 - pyta czy nie pyta o uruchomienie
::----------------------------------------------------------------------------------------------------------------------
_pyta:=0;
_task:=_a;

{? _>1
|| _pyta:=_b
?};
_ok:=1;
{? _pyta>0
|| _ok:=FUN.ask('Czy uruchomić przeliczanie planu?'@)
?};
PX_TASK.cntx_psh();
PX_TASK.clear();

{? PX_TASK.seek(_task) &_ok>0
||
   PX_TASK.GO:='T';
   PX_TASK.put()
?};
PX_TASK.cntx_pop();
PX_TASK.get();
~~


\task_stop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Zastopowanie przetwarznia
::   WE: _a - PX_TASK.ref() - zadanie do zatrzymania
::       [_b] - [0] / 1 - pyta czy nie pyta o uruchomienie
::----------------------------------------------------------------------------------------------------------------------
_pyta:=0;
_task:=_a;

{? _>1
|| _pyta:=_b
?};
_ok:=1;
{? _pyta>0
|| _ok:=FUN.ask('Czy przerwać przeliczanie planu?'@)
?};
PX_TASK.cntx_psh();
PX_TASK.clear();
{? PX_TASK.seek(_task) & _ok>0
|| exec('break','px_task',PX_TASK.PX_VER)
?};
PX_TASK.cntx_pop();
PX_TASK.get();
~~


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Dodaje rekord PX_TASK - informacja o stanie przetworzenia planu
::   WE: _a - PX_VER.ref() - wersja planu
::----------------------------------------------------------------------------------------------------------------------
_px_ver:=_a;
PX_TASK.cntx_psh();
PX_TASK.index('PX_VER');
{? _px_ver<>null()
||
   PX_TASK.prefix(_px_ver);
   {? PX_TASK.first()=0
   ||
      PX_TASK.blank();
      PX_TASK.ID:='PLAN';
      PX_TASK.PX_VER:=_px_ver;
      PX_TASK.TM_ADD:=PX_TASK.tm_stamp();
      PX_TASK.add()
   ||
      PX_TASK.PROGRESS:=0;
      PX_TASK.RUN:='N';
      PX_TASK.BREAK:='N';
      PX_TASK.COMPLETE:='N';
      PX_TASK.GO:='N';
      PX_TASK.TM_ADD:=PX_TASK.tm_stamp();
      PX_TASK.put()
   ?}
?};
PX_TASK.cntx_pop();
PX_TASK.get();
~~


\delete
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [12.10]
:: OPIS: Usuwa rekord PX_TASK - informacja o stanie przeliczenia planu
::   WE: _a - PX_VER.ref() - wersja planu
::   WY: 0 - porazka
::       >0 - sukces
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_px_ver:=_a;

PX_TASK.cntx_psh();
PX_TASK.index('PX_VER');
PX_TASK.prefix(_px_ver);
{? PX_TASK.first()
|| _result:=PX_TASK.del(,1)
?};
PX_TASK.cntx_pop();
_result


\done
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [12.10]
:: OPIS: Oznacza zadanie jako przeliczone
::   WE: _a - PX_VER.ref() - wersja planu
::----------------------------------------------------------------------------------------------------------------------
_px_ver:=_a;

PX_TASK.cntx_psh();
PX_TASK.index('PX_VER');
PX_TASK.prefix(_px_ver);
{? PX_TASK.first()
|| PX_TASK.RUN:='N';
   PX_TASK.BREAK:='N';
   PX_TASK.COMPLETE:='T';
   PX_TASK.PROGRESS:=100;
   PX_TASK.put();

:: TODO - TOMEK! NAPRAW!
:: wysylanie wiadomosci
::   __SUB:=exec('done_msg','px_task');
::   exec('add_eml','skid_eml','PLAN_LICZ_STOP')
   ~~
?};
PX_TASK.cntx_pop();
PX_TASK.get();
~~


\done_msg
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PRIVATE
::  UTW: WH [12.10]
:: OPIS: Zwraca string mowiacy o zakonczeniu przeliczania planu
::       Kontekst pracy - PX_TASK
::   WY: STRING
::----------------------------------------------------------------------------------------------------------------------
_msg:='Przeliczono wersje planu: %1'[PX_TASK.PX_VER().SYMBOL];
_msg


\break
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [12.10]
:: OPIS: Przerywa przeliczanie zadania
::   WE: _a - PX_VER.ref() - wersja planu
::----------------------------------------------------------------------------------------------------------------------
_px_ver:=_a;

PX_TASK.cntx_psh();
PX_TASK.index('PX_VER');
PX_TASK.prefix(_px_ver);
{? PX_TASK.first()
|| PX_TASK.RUN:='N';
   PX_TASK.BREAK:='T';
   PX_TASK.COMPLETE:='N';
   PX_TASK.put();
:: TODO - TOMEK! NAPRAW!
:: wysylanie wiadomosci
::   __SUB:=exec('break_msg','px_task');
::   exec('add_eml','skid_eml','PLAN_LICZ_STOP')
   ~~
?};
PX_TASK.cntx_pop();
PX_TASK.get();
~~


\break_msg
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PRIVATE
::  UTW: WH [12.10]
:: OPIS: Zwraca string mowiacy o przerwaniu przeliczania planu
::       Kontekst pracy - PX_TASK
::   WY: STRING
::----------------------------------------------------------------------------------------------------------------------
_msg:='Przeliczanie planu wersji: %1 przerwane na etapie: %2%%'[PX_TASK.PX_VER().SYMBOL,$PX_TASK.PROGRESS];
_msg


\px_task_fld_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Przed wyswietl dla pol tabeli PX_TASK
::----------------------------------------------------------------------------------------------------------------------
{? PX_TASK.GO='T'
|| exec('flddisp','color','PX_TASK#FLD#RED#01')
|? PX_TASK.COMPLETE='T'
|| exec('flddisp','color','PX_TASK#FLD#RED#04')
|? PX_TASK.BREAK='T'
|| exec('flddisp','color','PX_TASK#FLD#RED#03')
|? PX_TASK.RUN='T'
|| exec('flddisp','color','PX_TASK#FLD#RED#02')
|| ''
?}


\px_task_leg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Legenda w oknie wertowania PX_TASK
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','PX_TASK#FLD#RED#');
~~


\px_navi_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Opcje przyciskow w panelu nawigacyjnym
::   WE: [_a] - INTEGER - czy praca na jednej wersji planu: 1 - tak, [0] - nie
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _one_ver:=_a || _one_ver:=0 ?};

{? _one_ver=1
|| exec('przelicz_btn','px_plan')
|? _one_ver=0
||
:: ID elementow panelu
   _ac_que:='1338384844607';
   _tsk_stp:='1337088199416';
   _ac_plan:='1336988930413';
   _progres:='1337858577279';

   _chk_go:='1337953017188';
   _chk_run:='1337953216379';
   _chk_brk:='1337953548168';
   _chk_cmp:='1337953586542';

   PX_TASK.cntx_psh();
   PX_TASK.index('PX_VER');
   PX_TASK.prefix(PX_VAR.VER_QUE);
   {? PX_TASK.first()
   ||
      {? PX_VAR.VER_QUE().STATUS=exec('status_ready','px_ver') & PX_TASK.PX_VER=PX_VAR.VIE_VER
      ||
::       Symulacja lub podglad
         exec('set_enabled','#desktop','','nawigacja',_ac_que,0);
         exec('set_enabled','#desktop','','nawigacja',_ac_plan,1)
      ||
::       Plan glowny
         {? PX_VAR.EDIT
         ||
            {? (PX_TASK.GO='T' | PX_TASK.RUN='T') & PX_TASK.COMPLETE='N'
            || exec('set_enabled','#desktop','','nawigacja',_ac_que,0)
            || exec('set_enabled','#desktop','','nawigacja',_ac_que,1)
            ?}
         ||
            exec('set_enabled','#desktop','','nawigacja',_ac_que,0)
         ?};
         exec('set_enabled','#desktop','','nawigacja',_ac_plan,0)
      ?};
::    Jak uruchomione przetwarzanie, to aktywna opcja przerwania
      {? PX_TASK.RUN='T'
      || exec('set_enabled','#desktop','','nawigacja',_tsk_stp,1)
      || exec('set_enabled','#desktop','','nawigacja',_tsk_stp,0)
      ?};
::    Procent przeliczenia
      exec('set_enabled','#desktop','','nawigacja',_progres,PX_TASK.PROGRESS>0);
      exec('set_value','#desktop','','nawigacja',_progres,form(PX_TASK.PROGRESS,,0,'99'));
::    Znaczniki
      exec('set_enabled','#desktop','','nawigacja',_chk_go,PX_TASK.GO='T');
      exec('set_enabled','#desktop','','nawigacja',_chk_run,PX_TASK.RUN='T');
      exec('set_enabled','#desktop','','nawigacja',_chk_brk,PX_TASK.BREAK='T');
      exec('set_enabled','#desktop','','nawigacja',_chk_cmp,PX_TASK.COMPLETE='T')
   ||
::    Brak PX_TASK, raczej niemozliwe, ale obsluzone
      exec('set_enabled','#desktop','','nawigacja',_ac_que,0);
      exec('set_enabled','#desktop','','nawigacja',_ac_plan,0);
      exec('set_enabled','#desktop','','nawigacja',_tsk_stp,0);
::    Znaczniki
      exec('set_enabled','#desktop','','nawigacja',_chk_go,0);
      exec('set_enabled','#desktop','','nawigacja',_chk_run,0);
      exec('set_enabled','#desktop','','nawigacja',_chk_brk,0);
      exec('set_enabled','#desktop','','nawigacja',_chk_cmp,0)
   ?};
   PX_TASK.cntx_pop()
?};
~~


\pxq_navi_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Opcje przyciskow w panelu nawigacyjnym
::   WE: [_a] - INTEGER - czy praca na jednej wersji planu: 1 - tak, [0] - nie
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _one_ver:=_a || _one_ver:=0 ?};

{? _one_ver=1
|| exec('przelicz_btn','px_plan')
|? _one_ver=0
||
:: ID elementow panelu
   _ac_que:='1338384844607';
   _tsk_stp:='1337088199416';
   _ac_plan:='1336988930413';
   _progres:='1337858577279';

   _chk_go:='1337953017188';
   _chk_run:='1337953216379';
   _chk_brk:='1337953548168';
   _chk_cmp:='1337953586542';

   PX_TASK.cntx_psh();
   PX_TASK.index('PX_VER');
   PX_TASK.prefix(PX_VAR.VER_QUE);
   {? PX_TASK.first()
   ||
      {? PX_VAR.VER_QUE().STATUS=exec('status_ready','px_ver') & PX_TASK.PX_VER=PX_VAR.VIE_VER
      ||
::       Symulacja lub podglad
         exec('set_enabled','#desktop','','nawigacja',_ac_que,0);
         exec('set_enabled','#desktop','','nawigacja',_ac_plan,1)
      ||
::       Plan glowny
         {? PX_VAR.EDIT
         ||
            {? (PX_TASK.GO='T' | PX_TASK.RUN='T') & PX_TASK.COMPLETE='N'
            || exec('set_enabled','#desktop','','nawigacja',_ac_que,0)
            || exec('set_enabled','#desktop','','nawigacja',_ac_que,1)
            ?}
         ||
            exec('set_enabled','#desktop','','nawigacja',_ac_que,0)
         ?};
         exec('set_enabled','#desktop','','nawigacja',_ac_plan,0)
      ?};
::    Jak uruchomione przetwarzanie, to aktywna opcja przerwania
      {? PX_TASK.RUN='T'
      || exec('set_enabled','#desktop','','nawigacja',_tsk_stp,1)
      || exec('set_enabled','#desktop','','nawigacja',_tsk_stp,0)
      ?};
::    Procent przeliczenia
      exec('set_enabled','#desktop','','nawigacja',_progres,PX_TASK.PROGRESS>0);
      exec('set_value','#desktop','','nawigacja',_progres,form(PX_TASK.PROGRESS,,0,'99'));
::    Znaczniki
      exec('set_enabled','#desktop','','nawigacja',_chk_go,PX_TASK.GO='T');
      exec('set_enabled','#desktop','','nawigacja',_chk_run,PX_TASK.RUN='T');
      exec('set_enabled','#desktop','','nawigacja',_chk_brk,PX_TASK.BREAK='T');
      exec('set_enabled','#desktop','','nawigacja',_chk_cmp,PX_TASK.COMPLETE='T')
   ||
::    Brak PX_TASK, raczej niemozliwe, ale obsluzone
      exec('set_enabled','#desktop','','nawigacja',_ac_que,0);
      exec('set_enabled','#desktop','','nawigacja',_ac_plan,0);
      exec('set_enabled','#desktop','','nawigacja',_tsk_stp,0);
::    Znaczniki
      exec('set_enabled','#desktop','','nawigacja',_chk_go,0);
      exec('set_enabled','#desktop','','nawigacja',_chk_run,0);
      exec('set_enabled','#desktop','','nawigacja',_chk_brk,0);
      exec('set_enabled','#desktop','','nawigacja',_chk_cmp,0)
   ?};
   PX_TASK.cntx_pop()
?};
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:00 275f6207026c14a4c105f29691600dd76e0d7e57bcd9a5d89a521a16351f88c6ee95817a921c79febcdc7c78f3b1d96144fc2d2ab09ad44ed2fa601499c6df70e1b6b7ceedb453e7a261398b90834e536e3bba7e3fb949320861e27492d8c6a5a6edaed1268654c82f29274ae6d75d1bf2767cc23f03cc773537bffefea1caec

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: prc_zdalna.fml
:: Utworzony: 04.08.2021 [TMR]
:: Autor:
:: Systemy:
::======================================================================================================================
:: Zawartosc: Plik z formułami do obsługi pracy zdalnej na webTermie w planowaniu i grafikach
::======================================================================================================================


\get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: Wyszukanie informacji o pracy zdalnej okazjonalnej dla pracownika na podaną datę
::   WE: _a   - [REFERENCE] - wskazanie na pracownika, dla którego wyszukujemy pracę zdalną
::       _b   - [DATE]      - data badania
::   WY: Tabela z danymi
::----------------------------------------------------------------------------------------------------------------------
_p_ref:={? var_pres('_a')=type_of(null()) || _a || null() ?};
 _data:={? var_pres('_b')=type_of(date()) || _b || #0     ?};

_sql:=''+"select PPSFN.G, PPSFN.PARTDAY, PPSFT.OPIS from PPSFN
          join PPSFT using(PPSFN.PPSFT, PPSFT.REFERENCE)
          where PPSFN.P=:_a
          and to_date(:_b) is not null
          and PPSFN.OD<=to_date(:_b) and PPSFN.DO>=to_date(:_b)
          ";
sql(_sql,_p_ref,_data)


\get_ppsf_h
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [23.25]
:: OPIS: Wyszukanie informacji o pracy zdalnej stałej dla pracownika na podaną datę
::   WE: _a   - [REFERENCE] - wskazanie na pracownika, dla którego wyszukujemy pracę zdalną
::       _b   - [DATE]      - data badania
::   WY: Tabela z danymi
::----------------------------------------------------------------------------------------------------------------------
_p_ref:={? var_pres('_a')=type_of(null()) || _a || null() ?};
 _data:={? var_pres('_b')=type_of(date()) || _b || #0     ?};

_sql:=''+"select
            PPSFT.OPIS,
            PPSFROZD.NAZWA,
            PPSFROZD.WYBDNI,
            PPSFROZD.D1,
            PPSFROZD.D2,
            PPSFROZD.D3,
            PPSFROZD.D4,
            PPSFROZD.D5,
            PPSFROZD.D6,
            PPSFROZD.D7
         from
            PPSF_H join
            PPSFT using(PPSF_H.PPSFT, PPSFT.REFERENCE) join
            PPSFROZD using(PPSF_H.PPSFROZD, PPSFROZD.REFERENCE)
         where
            PPSF_H.P=:_a and
            PPSF_H.AKT='T' and
            to_date(:_b) is not null and
            PPSF_H.OD<=to_date(:_b) and
            (PPSF_H.DO>=to_date(:_b) or PPSF_H.DO is null)
          ";
sql(_sql,_p_ref,_data)


\start_stop_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Procedura zwracajaca informacje o rejestracjach START/STOP.
::       UWAGA - pozostawiona jest zgodność z 12.51, choć formuła jest w Mericie wykorzystywana tylko do pobierania
::       informacji do dymka podpowiedzi w grafikach.
::   WE: _a   - [REFERENCE] - wskazanie na pracownika, dla którego wyszukujemy pracę zdalną
::       _b   - [DATE]      - data badania
::   WY: Tabela z danymi
::  OLD: \start_stop_get/ppsf.prc
::----------------------------------------------------------------------------------------------------------------------
_p_ref:={? var_pres('_a')=type_of(null()) || _a || null() ?};
 _data:={? var_pres('_b')=type_of(date()) || _b || date() ?};
 _prev:={? var_pres('_c')=type_of(0)      || _c || 1      ?};

_sql:=''+"
   select
      R_POR_WW.REFERENCE as REF, R_POR_WW.DZK, R_POR_WW.DZ, R_POR_WW.GD, R_POR_WW.TP, '          ' as TP_OPIS,
      R_POR_WW.NR, R_POR_WW.POR, R_POR_WW.PPSFT, 'N' as DEL,
      '1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890' ||
      '12345678901234567890123456789012345678901234567890' as TYP_O,
      '1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890' ||
      '12345678901234567890123456789012345678901234567890' as ADR_O
   from
      R_POR_WW
   where
      2=1";

_TAB:=sql(_sql);

::  Aktualizacja 22.26_PZD01 Praca zdalna i kontrola trzeźwości
{? exec('is_pzd01','ppsf')
|| P.cntx_psh();
   P.index('OSOBA');
   P.prefix();
   {? P.seek(_p_ref,)
   || _par340:=exec('get_par','#parametr',340,2);

      PPSFT.cntx_psh();
      PPSF_ADR.cntx_psh();
      R_POR_WW.cntx_psh();
::    najpierw dodajemy niezakończone rejestracje z dnia poprzedniego (tylko, jeżeli parametr 340 jest inny niż 'P')
      _start:={? _par340<>'P' & _prev || -1 || 0 ?};
      {! _ind:=_start .. 0
      |! _year:='00'+($((_data+_ind)~1))+2;
         _month:='00'+($((_data+_ind)~2))+2;
         R_POR_WW.use('r_ph%1%2'[_year,_month]);
         {? _prev
         || R_POR_WW.index('NR')
         || R_POR_WW.index('UNIQUE')
         ?};
         R_POR_WW.prefix(_p_ref,_data+_ind);
         {? {? _ind=-1
            || R_POR_WW.last() &
               (R_POR_WW.TP=exec('r_por_ww_tp_we','ppsf') |
                (R_POR_WW.TP=exec('r_por_ww_tp_wy','ppsf') &  & R_POR_WW.DZK<R_POR_WW.DZ &
                 R_POR_WW.prev() &
                 R_POR_WW.TP=exec('r_por_ww_tp_we','ppsf'))
               )
            || R_POR_WW.first()
            ?}
         || {!
            |? _TAB.blank();
               _TAB.REF:=$R_POR_WW.ref();
               _TAB.DZK:=R_POR_WW.DZK;
               _TAB.DZ:=R_POR_WW.DZ;
               _TAB.GD:=R_POR_WW.GD;
               _TAB.TP:=R_POR_WW.TP;
               _TAB.TP_OPIS:={? R_POR_WW.TP='WE' || 'Start' || 'Stop' ?};
               _TAB.NR:=R_POR_WW.NR;
               _TAB.PPSFT:=$R_POR_WW.PPSFT;
               _TAB.POR:=R_POR_WW.POR;
               _TAB.TYP_O:=R_POR_WW.PPSFT().OPIS;
               _adr_o:={? R_POR_WW.PPSF_ADR & R_POR_WW.PPSF_ADR().TYP
                       || '%1, %2 %3 %4'[R_POR_WW.PPSF_ADR().MIASTO, PPSF_ADR.ULICA, PPSF_ADR.DOM, PPSF_ADR.LOKAL]
                       || ''
                       ?};
               _TAB.ADR_O:=_adr_o;
               _TAB.DEL:='N';
               _TAB.add();
               R_POR_WW.next()
            !}
         ?}
      !};
::       tylko ostatni zapis może być usunięty jeżeli parametr 340 jest ustawiony na wartość 'E'
      {? _par340='E' & _TAB.last()
      || _TAB.DEL:='T';
         _TAB.put()
      ?};
      R_POR_WW.cntx_pop();
      PPSF_ADR.cntx_pop();
      PPSFT.cntx_pop()
   ?};
   P.cntx_pop()
?};
_TAB.prefix();
_TAB

:Sign Version 2.0 jowisz:1045 2023/07/21 13:25:28 4ae15a719550c545630d27c4d5fde91773f3d6cca706c21964a575b1c3f5865f04b085eefa916d3780925157c6bcdd9d568a8fad28d527d093d7281894cfa54ffb4fdb73a29ee4bd1ed35f36580e1d6844244308df61cf40ad7f77da55f9557a64097dec9069969557030d2004fa43a5739d65bab83bd059605b29423674f79c

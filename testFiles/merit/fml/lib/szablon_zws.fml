:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: szablon_zws.fml
:: Utworzony: 20.03.2020
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły wykorzystywane w szablonach dokumentów WORD.
::======================================================================================================================


\firma_prolog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Prolog funkcji pobrania danych firmy
::   WE: _a - obiekt z parametrami wywołania
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',_a.DATA,XINFO,KST)


\firma_tabela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Funkcja pobrania danych firmy
::   WE: _a - obiekt z parametrami wywołania
::----------------------------------------------------------------------------------------------------------------------
_json:='
   {  "REGON": %1,
      "NIP": %2,
      "BDO": %3,
      "NAZ": %4,
      "MIA": %5,
      "UL": %6,
      "NR_D": %7,
      "PRZEDST": %8,
      "POCZTA": %9,
      "NKP": %10,
      "PKD": %11,
      "ADRES": %12,
      "KODP": %13,
      "SKROT": %14,
      "LOKAL": %15,
      "REGON_PKD": %16,
      "KRS": %17,
      "TEL": %18,
      "FAX": %19
   }
'
[  json_value(XINFO.REGON),
   json_value(XINFO.NIP),
   json_value(KST.BDO),
   json_value(XINFO.NAZ),
   json_value(XINFO.MIA),
   json_value(XINFO.UL),
   json_value(XINFO.NR_D),
   json_value(KST.PRZEDST),
   json_value(exec('poczta','stalesys')),
   json_value(KST.NKP),
   json_value(KST.PKD),
   json_value(exec('ulica','stalesys')),
   json_value(XINFO.KOD_P),
   json_value(XINFO.SKROT),
   json_value(KST.LOKAL),
   json_value({? +KST.PKD & +XINFO.REGON || XINFO.REGON+'-'+KST.PKD || XINFO.REGON ?}),
   json_value(XINFO.NREJ),
   json_value(XINFO.TEL),
   json_value(XINFO.FAX)
];
json_parse(_json)


\firma_epilog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Epilog funkcji pobrania danych firmy
::   WE: _a - obiekt z parametrami wywołania
::----------------------------------------------------------------------------------------------------------------------
1


\generuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Formuła obsługująca zapis jako załącznika wygenerowanego dokumentu WORD.
::   WE: _a  STRING    - symbol raportu
::       _b  STRING    - referencja dokumentu
::       _c  0/1       - zapis do załącznika
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_pdf:=exec('get','#params',100234)='T';
_dokum:={? var_pres('_c')=type_of(1) || _c || 0 ?};

{? _pdf
||
   {? exec('cli_functions','#system')=0
   || FUN.emsg(exec('indevice_nacc_msg','#system'));
      return(~~)
   ?}
?};

{? (exec('get','#params',100233)='T' | _dokum)  & var_pres('_b')>0
||
   _szablon:=exec('wypelnij','szablon',_a,,,-1);
   {? type_of(_szablon)=0 || FUN.info('Nie wygenerowano dokumentu.'@);return(~~) ?};
   {? ~(_szablon.INFO*'\~') || FUN.info('Nie wygenerowano dokumentu.'@);return(~~) ?};
   {? _pdf
   || _TAB:=exec('konwersja','paperless',_szablon.INFO,'TAB');
      _info:=''
   || _file:=fopen(_szablon.INFO,'r');
      {? ~_file || FUN.info('Nie wygenerowano dokumentu.'@);return(~~) ?};
      _info:=''
   ?};
   {? ref_tab(_b)=REM_ZGL
   ||
      REM_ZGLA.cntx_psh();
      do();
      REM_ZGLA.prefix();
      REM_ZGLA.blank();
      REM_ZGLA.REM_ZGL:=REM_ZGL.ref();
      {? _pdf || REM_ZGLA.ATA:=_TAB.BLOB ?};
      {? REM_ZGLA.add()
      || {? ~_pdf || REM_ZGLA.bl_put('ATA',_file,,,(_szablon.INFO*'\~')-_szablon.INFO) ?}
      || undo();_info:='Nie wygenerowano dokumentu.'@
      ?};
      end();
      {? _info<>''
      || FUN.info(_info)
      || {? FUN.ask('Wyświetlić wygenerowany dokument?'@)|| exec('bl_view','#blob',REM_ZGLA,'ATA') ?}
      ?};
      REM_ZGLA.cntx_pop()
   ||
      POM.cntx_psh();
      POM.TAB:='DOKUM';
      POM.TYPDOK:='SYS';
      exec('add_grnr','numery','SYS');
      DOKUM.cntx_psh();
      do();
      DOKUM.prefix();
      DOKUM.blank();
      DOKUM.FIRMA:=REF.FIRMA;
      {? ref_tab(_b)=PROJEKTY
      ||
         PROJEKTY.cntx_psh();
         PROJEKTY.prefix();
         {? PROJEKTY.seek(_b) || DOKUM.PROJEKTY:=PROJEKTY.ref() ?};
         PROJEKTY.cntx_pop()
      ?};
      DOKUM.REFSQL:=_b;
      DOKUM.NR:=exec('numer_new','numery','PACZKA');
      DOKUM.SYM:=form(DOKUM.NR,-8,,'99');
      {? _pdf || DOKUM.DOKUM:=_TAB.BLOB ?};
      _wyn:=0;
      {? DOKUM.add()
      || exec('znak','numery','DOKUM');
         {? ~_pdf || _wyn:=DOKUM.bl_put('DOKUM',_file,,,(_szablon.INFO*'\~')-_szablon.INFO) ?}
      || undo();_info:='Nie wygenerowano dokumentu.'@
      ?};
      {? _wyn
      ||
         DOKUM.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
         _extension:=DOKUM.bl_info('DOKUM','EXTENSION');
         {? _extension<>'' & ';bmp;jpg;jpeg;png;tif;tiff;pdf;'*_extension
         ||
            DOKUM.DOKUMI:=DOKUM.DOKUM
         ?};
         DOKUM.put();
         DOKUMZ.cntx_psh();
         DOKUMZ.prefix();
         DOKUMZ.blank();
         DOKUMZ.DOK:=DOKUM.ref();
         {? exec('upgrade2325_blbc1','zbl')
         || DOKUMZ.REFSQL:=DOKUM.REFSQL
         ?};
         DOKUMZ.KR_OP:=DOKUM.bl_info('DOKUM','NAME');
         DOKUMZ.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
         DOKUMZ.DOKUM:=DOKUM.DOKUM;
         {? ~DOKUMZ.add()|| undo();_info:='Nie wygenerowano dokumentu.'@ ?};
         DOKUMZ.cntx_pop()
      ?};
      end();
      POM.cntx_pop();
      {? _info<>''
      || FUN.info(_info)
      || {? FUN.ask('Wyświetlić wygenerowany dokument?'@)|| exec('pok_dok','dokum') ?}
      ?};
      DOKUM.cntx_pop()
   ?};
   {? ~_pdf & _file || fclose(_file); ferase(_szablon.INFO) ?}
||
   {? _pdf
   || _szablon:=exec('wypelnij','szablon',_a,,,-1);
      _TAB:=exec('konwersja','paperless',_szablon.INFO,'TAB');
      exec('bl_view','#blob',_TAB,'BLOB')
   || exec('wypelnij','szablon',_a)
   ?}
?};
~~

:Sign Version 2.0 jowisz:1045 2023/09/27 10:38:09 c46657c91025dd8414fb1ad8114e72384bdc3194ab8a4c72b99dca6e2608839163825af23d16d78d88152b2616528c07c4d939234b0c03b728d57ded19e56c5079862586b0fd3d3398e5e24dbb100bd77bac028986fed32b526fafbc8b804af7d2b43c31660595fb202aaba3f95ff7e4929613353bff4c4e596e79ed294c9838

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: dokum_zal.fml
:: Utworzony: 04.03.2015
:: Autor: DRO
::======================================================================================================================
:: Zawartość:  Procedury do obsługi załączników
::======================================================================================================================


\icon_att
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Ustawia ikonke w polu okna
::   WE: _a - tabela
::       _b - akronim okna
::  OLD: \icon_att/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
POMOC.ICON_ZAL:='';
_a.win_fml(_b,POMOC,'ICON_ZAL',,'ICON_BEFORE',"
DOKUM.index('DOKUM'); DOKUM.prefix(REF.FIRMA,$cur_tab(1,1).ref());
{? DOKUM.first() || 'xwin16.png:119' || '' ?}
")


\dokzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Akcja wyświetlająca dokumenty lotus notes i inne załączniki.
::   WE: [_a] - Blokada modyfikacji danych (brak możliwości dołączania/poprawiania załączników).
::              0 - Nie [domyślnie];
::              1 - Tak.
::       [_b] - Ref rekordu "właściciela" załącznika [domyślnie: bieżący rekord]
::       [_c] - Wyświetlać okno wertowania?
::              0 - Nie.
::              1 - Tak [domyślnie].
::       [_d] - Akronim okna redagowania [domyślnie: RED_INNY].
::  OLD: \doklotus/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
_blok_mod:=var_press('_a')=type_of(0) & _a;
__REFSQL:={? var_press('_b')=7 || $_b || $cur_tab(1,1).ref() ?};
_sel:={? var_press('_c')=type_of(0) || _c || 1 ?};
_we:={? var_press('_d')=type_of('') || _d || 'RED_INNY' ?};

{? 4+cur_tab(1,1).name(1)='doku'
|| {? exec('spr_mod','dok_fks')
   || _blok_mod:=1
   || _blok_mod:=_blok_mod & ~exec('czy_rej_zal','dok_fks')
   ?}
?};
DOKUM.index('BL_2'); DOKUM.prefix(REF.FIRMA,'N',__REFSQL);
DOKUM.win_sel('WERD');
_ha:={? _blok_mod || 'DIUp:DI' || '' ?};
DOKUM.actions('WERD',_ha);
{? _sel
|| _weo:=DOKUM.win_edit('?');
   DOKUM.win_edit(_we);
   DOKUM.select();
   DOKUM.win_edit(_weo);
   VAR_DEL.delete('__REFSQL')
?};
DOKUM.actions('WERD')


\addlotus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Dodaje dokument lotus notes
::  OLD: \addlotus/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')
|| FUN.emsg(exec('interm_nacc_msg','#system'));
   return(0)
?};
VAR_DEL.delete('F_LINE','F_OUT');
F_LINE:='';
F_OUT:=0;
_ok:=exec('get_link','dokum_zal');
{? _ok=-1
|| FUN.info('Schowek jest pusty.'@)
|? _ok=0
|| FUN.info('Schowek nie zawiera dokumentu Lotus Notes.'@)
|| POM.TAB:='DOKUM';
   POM.TYPDOK:='SYS';
   exec('add_grnr','numery','SYS');
   DOKUM.blank();
   DOKUM.TYP:='L';
   DOKUM.REFSQL:=__REFSQL;
   DOKUM.NR:=exec('numer_new','numery','PACZKA');
   exec('znak','numery','DOKUM',0);
   DOKUM.KR_OP:=F_LINE;
   {? DOKUM.edit("exec('ar_dokum','dokum_zal')")
   || {? DOKUM.add()
      || {? var_press('F_OUT')>100 & F_OUT.is_open()
         || DOKUM.bl_put('DOKUM',F_OUT,,,'clip.txt')
         ?}
      ?}
   || numer:=DOKUM.NR;
      oldnumer:=1;
      exec('nr_old','numery')
   ?}
?};
VAR_DEL.delete('F_LINE','F_OUT')


\add_inny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Dodaje inny dokument (zalacznik)
::  OLD: \add_inny/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
POM.TAB:='DOKUM';
POM.TYPDOK:='SYS';
exec('add_grnr','numery','SYS');
DOKUM.blank();
DOKUM.TYP:='I';
DOKUM.REFSQL:=__REFSQL;
DOKUM.NR:=exec('numer_new','numery','PACZKA');
DOKUM.memo_set(,'OPIS');
DOKUM.bl_file('DOKUM',1);
{? DOKUM.add()
|| {? DOKUM.r_lock(1,1,1)
   || exec('znak','numery','DOKUM');
      _del_oldnumer:=1;
      {? DOKUM.edit("exec('ar_dokum','dokum_zal')")
      || {? DOKUM.put()
         || _del_oldnumer:=0;
            DOKUM.memo_put(,'OPIS');
            {? (_fn:=DOKUM.bl_file('DOKUM'))<>''
            || DOKUM.bl_put('DOKUM',_fn)
            ?}
         ?}
      ?};
      {? _del_oldnumer
      || numer:=DOKUM.NR;
         oldnumer:=1;
         exec('nr_old','numery');
         _uid:=DOKUM.uidref(); _rr:=DOKUM.del(); exec('dokumlog_del','zbl_dok',_uid);
         _rr
      ?}
   ?}
?};
DOKUM.bl_file('DOKUM',1)


\get_link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Tworzy tekst linku na podstawie schowka
::  OLD: \get_link/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;F_LINE:='';
_lib:=lib_load('clipboard.jar',1);
_clipb:=lib_decl(_lib,,'String','getClipboard');
_txt:=lib_call(_clipb);
{? _txt*XINFO.HOSTLOTU
||
   _jest:=1
||
   STR.split(_txt,'\n');
   {!
   |? ~_jest & STR.next()
   |!
      _t:=STR.get_word();
      {? F_LINE='' || F_LINE:=_t ?};
      {? 3+_t='ERR' || _jest:=-1 ?};
      {? 5+_t='<NDL>' || _jest:=1 ?};
      ~_jest
   !}
?};
lib_free(_lib);
{? _jest=1
||
   F_OUT:=fopen('null','w',,,1);
   {? F_OUT.is_open() || fwrite(F_OUT,_txt) ?}
?};
_jest


\ar_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Rekord po okna WER tabeli DOKUM
::  OLD: \ar_dokum/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
_r:='';
_ref:=DOKUM.ref();
_nr:=DOKUM.NR;
DOKUM.cntx_psh(); DOKUM.index('DOKUM'); DOKUM.prefix(REF.FIRMA,__REFSQL);
{? DOKUM.find_key(_nr) & DOKUM.ref()<>_ref
|| FUN.info('Istnieje dokument o numerze %1.'@[$_nr]);
   _r:='NR'
?};
DOKUM.cntx_pop();
{? _r='' & DOKUM.TYP<>'L' & DOKUM.bl_file('DOKUM')='' & DOKUM.DOKUM=null
|| FUN.info('Nie podano pliku w załączniku.'@); _r:='DOKUM'
?};
_r


\set_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Ustawia okno redagowania
::  OLD: \set_edit/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.TYP='L'
|| DOKUM.win_edit('RED_L')
|| DOKUM.win_edit('RED_INNY')
?};
1


\rek_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: przed rekord tabeli DOKUM
::   WE: _a=0/1-ostatni odrysowany rekord
::       _b=1-zalaczniki WERD
::       _b=2-zalaczniki dokumentow WER
::       _b=3-kontakty WERK
::  OLD: \rek_dokum/dokum.fml
::----------------------------------------------------------------------------------------------------------------------
{? _b=1
:: brak wywołania - wygląda, że do usunięcia
|| exec('br_dokum','dokum_zal')
|? _b=2
|| exec('br_dokum2','dokum_zal',_a)
|? _b=3
|| exec('br_dokum3','dokum_zal',_a)
|| ''
?}


\br_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Rekord przed okna WER tabeli DOKUM
::  OLD: \br_dokum/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
exec('set_edit','dokum_zal');
''


\dellotus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Usuwa dokument lotus notes
::  OLD: \dellotus/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.TYP='L' & FUN.ask('Czy usunąć połączenie\nz dokumentem Lotus Notes?'@) |
   DOKUM.TYP<>'L' & FUN.ask('Czy usunąć załącznik?'@)
|| POM.TAB:='DOKUM';
   POM.TYPDOK:='SYS';
   numer:=DOKUM.NR;
   oldnumer:=1;
   exec('nr_old','numery');
   exec('dokumz_del','dokum_zal');
   _uid:=DOKUM.uidref(); _rr:=DOKUM.del(); exec('dokumlog_del','zbl_dok',_uid);
   _rr
?}


\openlotus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Otwiera wskazany dokument lotus notes z lacza
::  OLD: \openlotus/skid_att.fml
:: ~OST: INSYSEXEC
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.TYP='L'
|| _plik:='lotus.ndl';
   {? DOKUM.bl_get('DOKUM',_plik,1)
   || {? exec('interm','#system')
      || dlg_save(_plik,1)
      || sys_exec(_plik)
      ?}
   || FUN.info('Nie istnieje połączenie z dokumentem Lotus Notes.'@)
   ?}
|| {? DOKUM.DOKUM
   || exec('bl_view','#blob',DOKUM,'DOKUM')
   || FUN.info('Nie podano pliku w załączniku.'@)
   ?}
?}


\save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Zapisywanie na dysk zalacznika
::  OLD: \save/skid_att.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
{? DOKUM.TYP='L'
|| FUN.info('Akcja dostępna dla innych dokumentów.'@)
|? ~DOKUM.DOKUM
|| FUN.info('Brak załącznika.'@)
|| _fn:=DOKUM.bl_info('DOKUM','NAME');
   _ext:=_fn;
   {! |? _pos:=_ext*'.'; _ext:=_pos-_ext; _pos !};

   __dir_controling:=fmk_tmp_dir(0);
   _path:=__dir_controling.get_path()+'\\'+_fn;
   {? 2+_path<>'\\\\' & 2+_path<>'//' || _path:='@'+_path ?};

   {? 1+_path = '@'
   ||
       _path := 1-_path
   ?};
   {? +_path>1 || DOKUM.bl_get('DOKUM',_path,0) ?};

   __dir_controling.get_path();
   dlg_save(_path,0,DOKUM.bl_info('DOKUM','NAME'));
   VAR_DEL.delete('__dir_controling')

?}


\dokumz_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Kasowanie zalacznikow do dokumentu
::   WE: _a - DOKUM.ref()
::  OLD: \dokumz_del/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_dokum:={? var_pres('_a')=type_of(null()) || _a || DOKUM.ref() ?};

_archiwum:=ref_name(_dokum)<>'dokum';
_maska:=ref_name(_dokum)+2;

{? _archiwum
||
   DOKUMZA.cntx_psh();
   DOKUMZA.use((DOKUMZA.name()-2)+_maska);
   DOKUMZA.cntx_psh();
   DOKUMZA.index('DISP');
   DOKUMZA.prefix(_dokum);
   {? DOKUMZA.first()
   ||
      {!
      |?
         {? DOKUMZA.EDI_I<>''
         ||
            exec('FindAndGet','#table',EDI_I,DOKUMZA.EDI_I,,"
               exec('dzien_anul','edi_wspolne',date(),time(),OPERATOR.USER)
            ")
         ?};
         DOKUMZA.del()
      !}
   ?};
   DOKUMZA.cntx_pop();
   DOKUMZA.cntx_pop()
||
   DOKUMZ.cntx_psh();
   DOKUMZ.index('DISP');
   DOKUMZ.prefix(_dokum);
   {? DOKUMZ.first()
   ||
      {!
      |?
         {? DOKUMZ.EDI_I<>''
         ||
            exec('FindAndGet','#table',EDI_I,DOKUMZ.EDI_I,,"
               exec('dzien_anul','edi_wspolne',date(),time(),OPERATOR.USER)
            ")
         ?};
         {? exec('upgrade2325_blbc1','zbl') & DOKUMZ.BC='T'
         || _ref:=DOKUMZ.ref();
            _next:=DOKUMZ.next();
            DOKUMZ.cntx_psh();
            DOKUMZ.prefix();
            DOKUMZ.seek(_ref);
            DOKUMZ.DOK:=null();
            DOKUMZ.put(1);
            DOKUMZ.cntx_pop();
            _next
         || DOKUMZ.del()
         ?}
      !}
   ?};
   DOKUMZ.cntx_pop()
?}


\refsql
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: POLA.REFSQL
::  OLD: \refsql/dokum.fml
::----------------------------------------------------------------------------------------------------------------------
POLA.REFSQL


\bds_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: DOKUM - na wyswietl dla pol
::  OLD: \bds_dokum/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
_pole:=cur_afld();
DOKUM.DOT().RODZINF();
{? RODZINF.NAZ='Dokument definiowalny'
|| {? (_pole='SYM' & ~ZDARZT.WYB_SYM) |
      ((_pole='DATA_OD' | _pole='DATA_DO') & ~ZDARZT.WYB_DAT) |
      ((_pole='WAL' | _pole='WART') & ~ZDARZT.WYB_KW) |
      (_pole='OSOBA' & ~ZDARZT.WYB_OSK) |
      (_pole='PRAC' & ~ZDARZT.WYB_PRAC) |
      (_pole='KH_OSOB' & ~ZDARZT.WYB_OSK)
   || _zwrot:=exec('findfnv','#color')
   ?}
?};
_zwrot


\dokum_tr_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Trigger dla DOKUM
::  OLD: \dokum_tr_b/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_send2bc:={? exec('upgrade2325_blbc1','zbl') || DOKUM.BLC_KIND<>'' || 0 ?};
DOKUM.DLA_KH:={? DOKUM.KH=null() | _send2bc || 0 || 1 ?};
{? DOKUM.REFSQL<>'' & type_of(ref_tab(DOKUM.REFSQL))>100 & ref_tab(DOKUM.REFSQL)=PAR_NAG & DOKUM.ESEND<>''
|| exec('mail_sendk','rozrach')
|? DOKUM.REFSQL<>'' & type_of(ref_tab(DOKUM.REFSQL))>100 & ref_tab(DOKUM.REFSQL)=SER_NAG & DOKUM.ESEND<>''
|| exec('mail_sendw','rozrach')
?};
1


\dokumz_tr_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Trigger przed del dla DOKUMZ
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(118)='T'
|| SKAN_DOK.index('UID_REF');
   SKAN_DOK.prefix(DOKUMZ.uidref());
   {? SKAN_DOK.first()
   || exec('del_doc','skanuj');
      SKAN_DOK.del()
   ?}
?};
1


\dokumz_tr_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Trigger przed add dla DOKUMZ
::----------------------------------------------------------------------------------------------------------------------
{? DOKUMZ.P_OCR='' || DOKUMZ.P_OCR:='N' ?};
{? DOKUMZ.W_OCR='' || DOKUMZ.W_OCR:='N' ?};
1


\br_dokum2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: DOKUM - rekord przed
::   WE: _a=0/1-wygaszać akcje
::  OLD: \br_dokum/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_user:=exec('name','users');
_super:=sec_superuser();
_win:=exec('dokum_okno_red','dokum');
DOKUM.win_edit(_win);
KHVZ.UPRAW:='';
DOKUM.DOT().RODZINF();
{? RODZINF.NAZ='Dokument definiowalny'
|| DOKUM.win_edit(_win);
   {? ZDARZT.WYB_SYM=2
   || DOKUM.efld_opt(_win,'mark=1',,'SYM')
   || DOKUM.efld_opt(_win,'mark=0',,'SYM')
   ?};
   {? ZDARZT.WYB_KW=2
   || DOKUM.efld_opt(_win,'mark=1',,'WAL','KOD');
      DOKUM.efld_opt(_win,'mark=1',,'WART')
   || DOKUM.efld_opt(_win,'mark=0',,'WAL','KOD');
      DOKUM.efld_opt(_win,'mark=0',,'WART')
   ?};
   {? ZDARZT.WYB_DAT=2
   || DOKUM.efld_opt(_win,'mark=1',,'DATA_OD')
   || DOKUM.efld_opt(_win,'mark=0',,'DATA_OD')
   ?};
   {? ZDARZT.WYB_PRAC=2
   || DOKUM.efld_opt(_win,'mark=1',,'PRAC','NAZWISKO')
   || DOKUM.efld_opt(_win,'mark=0',,'PRAC','NAZWISKO')
   ?};
   {? ZDARZT.WYB_OSK=2
   || DOKUM.efld_opt(_win,'mark=1',,'KH_OSOB','NAZWISKO')
   || DOKUM.efld_opt(_win,'mark=0',,'KH_OSOB','NAZWISKO')
   ?};
   {? _super | _user=DOKUM.OPER | _user=DOKUM.OPER1
   || DOKUM.btn_eopt(_win,'UPRAW','state=normal')
   || DOKUM.btn_eopt(_win,'UPRAW','state=grayed')
   ?};
   {? (KHVZ.KONTAKTY='T' & KHVZ.KHVP='dokum') | (KHVZ.KHVP='projekty' & PROJEKTY.KH=null())
   || DOKUM.efld_opt(_win,'mark=1',,'KH','KOD')
   || DOKUM.efld_opt(_win,'mark=0',,'KH','KOD')
   ?};
   {? KHVZ.KONTAKTY='T' & KHVZ.KHVP='kh_osob'
   || DOKUM.efld_opt(_win,'editable=0',,'KH_OSOB','NAZWISKO')
   || DOKUM.efld_opt(_win,'editable=1',,'KH_OSOB','NAZWISKO')
   ?}
|? RODZINF.NAZ='Dokument prosty'
|| {? DOKUM.REFSQL<>''
   || _refx:=ref_tab(DOKUM.REFSQL);
      {? type_of(_refx)<>type_of(~~) & _refx=POS
      || _win:='REDK'
      ?}
   ?};
   DOKUM.win_edit(_win);
   {? _win='REDK' | _win='REDK_M' | _win='REDK_P' | _win='REDK_S'
   || {? _super | _user=DOKUM.OPER | _user=DOKUM.OPER1
      || DOKUM.btn_eopt(_win,'UPRAW','state=normal')
      || DOKUM.btn_eopt(_win,'UPRAW','state=grayed')
      ?};
      {? (KHVZ.KONTAKTY='T' & KHVZ.KHVP='dokum') | (KHVZ.KHVP='projekty' & PROJEKTY.KH=null())
      || DOKUM.efld_opt(_win,'mark=1',,'KH','KOD')
      || DOKUM.efld_opt(_win,'mark=0',,'KH','KOD')
      ?};
      {? KHVZ.KONTAKTY='T' & KHVZ.KHVP='kh_osob'
      || DOKUM.efld_opt(_win,'editable=0',,'KH_OSOB','NAZWISKO')
      || DOKUM.efld_opt(_win,'editable=1',,'KH_OSOB','NAZWISKO')
      ?}
   ?}
?};
_wyn:=0;
{? KHVZ.KONTAKTY='T'
|| {? _a
   || exec('dokum_act_grey','dokum_zal','K')
   || KHVZ.UPRAW:=exec('dokum_upraw','dokum')
   ?};
   _wyn:=exec('rekprzed','color','DOKUM#02')
?};
_wyn


\dok_attach
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wyświetla okno z załącznikami do dokumentu (DOK)
::----------------------------------------------------------------------------------------------------------------------
exec('sel_dok','dokum','DOK',,var_pres('ERP_fks')>0 |
                              exec('chk_role','#b__box',OPERATOR.USER,'FKS_DKS_DARK')=0 |
                              DOK.ZP='T' |
                              DOK.ZK='T',,1
    )


\attach
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wyświetla okno z załącznikami do tabeli
::   WE: _a - akronim tabeli
::----------------------------------------------------------------------------------------------------------------------
_tab:=($_a)();
_tab.cntx_psh();
_grp:=_tab.grp_make('Załączniki'@,,'grp_dok_zal');
VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB','BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','ADDAKC','SELAKC','CUR_WIN');
__CTR.BTAB:='DOKUM';
__CTR.TAB:=_a;
{? __CTR.TAB='SER_NAG' || SSTALE.cntx_psh(); SSTALE.WAL:=SER_NAG.WAL ?};
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','srsr_zal',15+(-(_a+'dokum_ctr')),':');
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('dir_sep','srsr_zal');
__CTR.DND:=1;
__CTR.CUR_WIN:=cur_win(1,1);
__CTR.ADDAKC:="
   _akc:={? __CTR.TAB='PAR_NAG'
         || 'FKS_RUC_KMPK'
         |? __CTR.TAB='SER_NAG' & __CTR.CUR_WIN='SER_NAGN'
         || 'FKS_RRK_DKPS'
         || 'FKS_RRK_DKAS'
         ?};
   {? exec('hasAction','users',_akc)
   || ctr_set(__CTR.WIN_ID,'ctr_id','addAction','addpdf','Dołącz PD&F'@,'','Dołącz PDF'@,0,0)
   ?};
   {? exec('hasAction','users','ZPR_SND_DOKU')
   || ctr_set(__CTR.WIN_ID,'ctr_id','addAction','sendPdf','W&yślij'@,'','Wyślij załącznik'@,1,0);
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction','unblock','Od&blokuj wysyłanie'@,'','Odblokowuje możliwość wysyłania załącznika.'@,1,0)
   ?}
";
__CTR.SELAKC:="
   {? _a='addpdf'
   || {? __CTR.TAB='PAR_NAG'
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:='FKS_RUC_KMPK';
         _params.UIDREF:=PAR_NAG.uidref();
         _params.AKCJA:='AddPDF';
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'PAR_NAG',PAR_NAG.ref());
         exec('mp_run','#b__box',_params)
      |? __CTR.TAB='SER_NAG' & __CTR.CUR_WIN<>'SER_NAGN'
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:='FKS_RRK_DKAS';
         _params.UIDREF:=SER_NAG.uidref();
         _params.AKCJA:='AddPDF';
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SER_NAG',SER_NAG.ref());
         exec('mp_run','#b__box',_params)
      |? __CTR.TAB='SER_NAG' & __CTR.CUR_WIN='SER_NAGN'
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:='FKS_RRK_DKPS';
         _params.UIDREF:=SER_NAG.uidref();
         _params.AKCJA:='AddPDF';
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'SER_NAG',SER_NAG.ref());
         exec('mp_run','#b__box',_params)
      ?};
      1
   |? _a='sendPdf'
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='ZPR_SND_DOKU';
      _params.UIDREF:=DOKUM.uidref();
      _params.AKCJA:='WYŚLIJ';
      _params.PROC_START:='N';
      _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOKUM',DOKUM.ref());
      exec('mp_run','#b__box',_params);
      0
   |? _a='unblock'
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='ZPR_SND_DOKU';
      _params.UIDREF:=DOKUM.uidref();
      _params.AKCJA:='unblock';
      _params.PROC_START:='N';
      _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOKUM',DOKUM.ref());
      exec('mp_run','#b__box',_params);
      0
   ?}
";
_bf:=$('exec(\'bobs_dokum\',\'srsr_zal\',0)');
_fml:=$('exec(\'dokum_rfr\',\'srsr_zal\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
_tab.grp_ctr(_grp,DOKUM,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,_fml,,,,,_bf,,,'maximized');
_tab.win_sel(_grp);
_tab.select();
{? __CTR.TAB='SER_NAG' || SSTALE.cntx_pop() ?};
AreaTitle.setTabWin(_tab,_grp);
AreaTitle.setTitle();
_tab.cntx_pop()


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodanie załacznika
::   WE: _a - ref sql tabeli
::       _b - plik
::       _c - dodać? 1-tak 0-poprawić
::       _d - e-mail odbiorcy
::       _e - nazwa pliku w bazie danych
::       _f - kontrahent
::       _g - osoba kontaktowa
::       _h - jednostka księgowa
::       _i - wskazanie na typ kontaktu z kontrahentem
::       _j - krótki opis
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_file:=fopen(_b,'b',~(var_pres('ObjForMerge')>0),0,1);
{? _file.is_open()
|| {? _c
   || POM.TAB:='DOKUM';
      POM.TYPDOK:='SYS';
      DOKUM.prefix();
      exec('add_grnr','numery','SYS');
      DOKUM.blank(1);
      DOKUM.FIRMA:=REF.FIRMA;
      DOKUM.TYP:='P';
      DOKUM.REFSQL:=_a;
      DOKUM.KR_OP:=_j;
      _ok:=DOKUM.add();
      {? _ok
      || DOKUM.NR:=exec('numer_new','numery','PACZKA');
         exec('znak','numery','DOKUM')
      ?}
   || _ok:=1
   ?};
   {? _ok
   || DOKUM.bl_put('DOKUM',_file,,,_e);
      DOKUM.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
      DOKUM.EM:=_d;
      DOKUM.KH:=_f;
      DOKUM.DOT:=_i;
      DOKUM.DATAKONT:=date();
      DOKUM.OPER:=exec('name','users');
      DOKUM.OSOBA:=_g;
      DOKUM.ODD:=_h;
      DOKUM.DATA:=date();
      DOKUM.CZAS:=time();
      DOKUM.put()
   ?}
?}


\zdarzt_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła zwraca nazwę typu kontaktu
::----------------------------------------------------------------------------------------------------------------------
_sel:=~~;
ZDARZT.cntx_psh();
ZDARZT.index('ZDARZT'); ZDARZT.prefix('N');
ZDARZT.win_sel('ZDARZT2');
{? ZDARZT.select()
|| _sel:=ZDARZT.ZDARZ
?};
ZDARZT.cntx_pop();
_sel


\dok_zal
::----------------------------------------------------------------------------------------------------------------------
:: UTW: Artur Kozłowski [wersja: 12.41] 04.04.2016  - 15:53
:: OPIS: _a - maska  POZ
::       _b - ref POZ
::       _c - 0 bez okienka 1 (domyslnie) z okienkiem - dla akcji
:: WE:
:: WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_ || _a:=1 ?};
{? var_pres('_c')<>type_of(1) || _c:=1 ?};

DOKUM.index('DOKUM');
{? cur_tab(1,1)=DVAT
|| DOK.cntx_psh();
   exec('pw_rejv','dok_fks');
   DOK.prefix();
   {? DOK.seek(DVAT.DOK)
   || DOKUM.index('DOKUM');
      DOKUM.prefix(REF.FIRMA,$DOK.ref);
      {? DOKUM.first()
      || exec('dok_attach','dokum_zal')
      || FUN.info('Dokument %1 nie posiada załączników'@[DOK.NK])
      ?}
   ?};
   DOK.cntx_pop()
|? cur_tab(1,1)=ROZLVAT
|| DOK.cntx_psh();
   DOK.prefix();
   {? DOK.seek(ROZLVAT.DOK)
   || DOKUM.index('DOKUM');
      DOKUM.prefix(REF.FIRMA,$DOK.ref);
      {? DOKUM.first()
      || exec('dok_attach','dokum_zal')
      || FUN.info('Dokument %1 nie posiada załączników'@[DOK.NK])
      ?}
   ?};
   DOK.cntx_pop()
|? cur_tab(1,1)=POZ
|| DOK.cntx_psh();
   DOK.prefix();
   {? DOK.seek(POZ.DOK)
   || DOKUM.index('DOKUM');
      DOKUM.prefix(REF.FIRMA,$DOK.ref);
      {? DOKUM.first()
      || exec('dok_attach','dokum_zal')
      || FUN.info('Dokument %1 nie posiada załączników'@[DOK.NK])
      ?}
   ?};
   DOK.cntx_pop()
|? cur_tab(1,1)=ZAP_OP | {? var_press('X_Xa')>0 || cur_tab(1,1)=X_Xa || 0  ?}
||
   _maska:=(ref_name(ZAP_OP.POZDOK)+4);
   _ref:=#ZAP_OP.POZDOK;
   DOK.cntx_psh(); POZ.cntx_psh();
   _maska2:={? (DOK.name()+2)='__' || (DOK.name()+4) || SSTALE.AR().KOD+form(SSTALE.AO().NR,-2) ?};
   {? _maska=_maska2
   || _ok:=1
   || _ok:=0;
      POZ.use('pozy'+_maska);
      DOK.use('doku'+_maska)
   ?};
   POZ.prefix();
   {? _ref<>0 & POZ.seek(_ref,)
   || {? _ref<>0 & DOK.seek(POZ.DOK)
      || DOKUM.index('DOKUM');
         DOKUM.prefix(REF.FIRMA,$DOK.ref);
         {? DOKUM.first()
         || exec('dok_attach','dokum_zal')
         || FUN.info('Dokument %1 nie posiada załączników'@[DOK.NK])
         ?}
      ?}
   ?};
   POZ.cntx_pop(); DOK.cntx_pop()
|? {? var_press('TABTMP')>0 || cur_tab(1,1)=TABTMP || 0  ?}
   | {? var_press('ZAP_SEL')>0 || cur_tab(1,1)=ZAP_SEL || 0  ?} |
   var_press('ZapPoz')>0 & cur_tab(1,1)=ZapPoz.TAB
|| DOK.cntx_psh; POZ.cntx_psh;
   {? _a<>SSTALE.AR().KOD+form(SSTALE.AO().NR,-2)
   || POZ.use('pozy'+_a);
      DOK.use('doku'+_a)
   ?};
   POZ.index('DOK'); POZ.prefix();
   {? POZ.seek(_b,)
   || DOK.prefix();
      {? DOK.seek(POZ.DOK)
      || DOKUM.index('DOKUM');
         DOKUM.prefix(REF.FIRMA,$DOK.ref);
         {? DOKUM.first()
         || exec('dok_attach','dokum_zal')
         || FUN.info('Dokument %1 nie posiada załączników'@[DOK.NK])
         ?}
      ?}
   ?};
   POZ.cntx_pop; DOK.cntx_pop()
|? cur_tab(1,1)=DOK & _c=0
|| DOKUM.index('DOKUM');
   DOKUM.prefix(REF.FIRMA,$DOK.ref);
   {? DOKUM.first()
   ||  ''
   || 'Z'
   ?}
|? cur_tab(1,1)=OPTMP
|| OP.cntx_psh();
   OP.index('KON_O');
   OP.prefix(WYDRUKI.WAL,OPTMP.ODD,OPTMP.KONTO,OPTMP.SYM,OPTMP.SYM,OPTMP.SYM_ROK);
   {? OP.size=1 & OP.first()
   || ZAP_OP.cntx_psh();
      ZAP_OP.index('OP');
      ZAP_OP.prefix(OP.ref);
      _maska:='';_ref:=0;
      {? ZAP_OP.first()
      || {!
         |? {? ZAP_OP.MA<>0
            || _maska:=(ref_name(ZAP_OP.POZDOK)+4);
               _ref:=#ZAP_OP.POZDOK ;
               DOK.cntx_psh(); POZ.cntx_psh();
               _maska2:={? (DOK.name()+2)='__' || (DOK.name()+4) || SSTALE.AR().KOD+form(SSTALE.AO().NR,-2) ?};
               {? _maska=_maska2
               || _ok:=1
               || _ok:=0;
                  POZ.use('pozy'+_maska);
                  DOK.use('doku'+_maska)
               ?};
               POZ.prefix();
               {? _ref<>0 & POZ.seek(_ref,)
               || {? _ref<>0 & DOK.seek(POZ.DOK)
                  || DOKUM.index('DOKUM');
                     DOKUM.prefix(REF.FIRMA,$DOK.ref);
                     {? DOKUM.first()
                     ||  exec('dok_attach','dokum_zal')
                     || FUN.info('Dokument %1 nie posiada załączników'@[DOK.NK])
                     ?}
                  ?}
               ?};
               POZ.cntx_pop(); DOK.cntx_pop()
            ?};
            ZAP_OP.next() & _maska<>''
         !}
      ?};
      ZAP_OP.cntx_pop()
   ?};
   OP.cntx_pop()
?}


\pojazdy_attach
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Wyświetla okno z załącznikami do pojazdu (POJAZDY)
::----------------------------------------------------------------------------------------------------------------------
POJAZDY.cntx_psh();
_grp:=POJAZDY.grp_make('Załączniki dla pojazdu'@,,'pojazdy_zal');
VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB','BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND');
__CTR.BTAB:='DOKUM';
__CTR.TAB:='POJAZDY';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','srsr_zal','poj_dokum_ctr',':');
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('sep','#file',1);
__CTR.DND:=1;
_bf:=$('exec(\'bobs_dokum\',\'srsr_zal\',exec(\'chk_role\',\'#b__box\',OPERATOR.USER,\'ZWS_POJ_EDIT\')=0)');
_fml:=$('exec(\'dokum_rfr\',\'srsr_zal\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
POJAZDY.grp_ctr(_grp,DOKUM,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,_fml,,,,,_bf,,,'maximized');
POJAZDY.win_sel(_grp);
POJAZDY.select();
POJAZDY.cntx_pop()


\nrkrtsim_attach
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Wyświetla okno z załącznikami do pojazdu (POJAZDY)
::----------------------------------------------------------------------------------------------------------------------
NRKRTSIM.cntx_psh();
_grp:=NRKRTSIM.grp_make('Załączniki dla kart SIM'@,,'nrkrtsim_zal');
VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB','BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND');
__CTR.BTAB:='DOKUM';
__CTR.TAB:='NRKRTSIM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','srsr_zal','sim_dokum_ctr',':');
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('sep','#file',1);
__CTR.DND:=1;
_bf:=$('exec(\'bobs_dokum\',\'srsr_zal\',exec(\'chk_role\',\'#b__box\',OPERATOR.USER,\'ZWS_SIM_EDIT\')=0)');
_fml:=$('exec(\'dokum_rfr\',\'srsr_zal\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
NRKRTSIM.grp_ctr(_grp,DOKUM,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,_fml,,,,,_bf,,,'maximized');
NRKRTSIM.win_sel(_grp);
NRKRTSIM.select();
NRKRTSIM.cntx_pop()


\bl_dokum_unlink
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Usuwa powiazanie z dokumentem BL podczas usuwania rekordu.
::   WE: [_a] - refsql rekordu dla ktorego zostana rozlaczone zalaczniki
::       [_b] - transakcja
::----------------------------------------------------------------------------------------------------------------------
_refsql:={? _=0 || $cur_tab(1,1).ref() || _a ?};
_do:={? var_pres('_b')>0 || _b || 0 ?};
DOKUM.cntx_psh();
DOKUM.index('BL_2');
DOKUM.prefix(REF.FIRMA,'T',_refsql);
{? DOKUM.first()
|| {!
   |? {? DOKUM.RODZAJ_K<>'W'
      || exec('bl_dokum_unlink','zbl_dok',DOKUM.ref)
      ?};
      DOKUM.next()
   !}
?};
{? ~_do
|| _names:=DOKUM.names();
   {? _names.first()
   || {!
      |? {? _names.NAME<>'dokum'
         || DOKUM.use(_names.NAME);
            DOKUM.index('BL_2');
            DOKUM.prefix(REF.FIRMA,'T',_refsql);
            {? DOKUM.first()
            || {!
               |? {? DOKUM.RODZAJ_K<>'W'
                  || exec('bl_dokum_unlink','zbl_dok',DOKUM.ref)
                  ?};
                  DOKUM.next()
               !}
            ?}
         ?};
         _names.next()
      !}
   ?}
?};
DOKUM.cntx_pop()


\bl_dokum_reg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Zmienia status na zarejestrowany dokumentu BL.
::   WE: [_a] - refsql rekordu
::----------------------------------------------------------------------------------------------------------------------
_refsql:={? _=0 || $cur_tab(1,1).ref() || _a ?};
DOKUM.cntx_psh();
DOKUM.index('BL_2');
DOKUM.prefix(REF.FIRMA,'T',_refsql,'P');
{? DOKUM.first()
|| {!
   |? exec('bl_dokum_reg','zbl_dok',DOKUM.ref);
      DOKUM.next()
   !}
?};
DOKUM.cntx_pop()


\br_dokum3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: DOKUM, okno WER - rekord przed
::   WE: _a=0/1-wygaszać akcje
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_pres('_a')=type_of(0) || _a || 0 ?};
{? _a
|| exec('dokum_act_grey','dokum_zal','D')
?};
_wyn:=exec('rekprzed','color','DOKUM#01');
_wyn


\dokumz_moz_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Sprawdzenie, czy można usunąć załącznik DOKUMZ, nie można gdy DOKUMZ.DOKUM=DOKUM.DOKUM lub DOKUMZ.BL_GUID<>''
::   WE: _a DOKUMZ.ref
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_moz:=1;
DOKUMZ.cntx_psh();
{? var_pres('_a')>0 & DOKUMZ.seek(_a)
|| {? DOKUMZ.BL_GUID<>''
   || _moz:=0;
      FUN.info('Nie można usunąć - Businesslink.'@)
   |? DOKUMZ.BL_TYPE=exec('bl_type_ksef_inv','zbl_dok') |
      DOKUMZ.BL_TYPE=exec('bl_type_ksef_inv_corr','zbl_dok')
   || _moz:=0;
      FUN.info('Nie można usunąć - plik KSeF dla Businesslink.'@)
   |? DOKUMZ.BL_TYPE=exec('bl_type_inv','zbl_dok') |
      DOKUMZ.BL_TYPE=exec('bl_type_inv_corr','zbl_dok')
   || _moz:=0;
      FUN.info('Nie można usunąć - plik UFD dla Businesslink.'@)
   || {? exec('dokumz_glowny','dokum_zal')
      || _moz:=0;
         FUN.info('Nie można usunąć - główny załącznik.'@)
      ?}
   ?}
?};
DOKUMZ.cntx_pop();
_moz


\kkh_upraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Przygotowanie zmiennej __KKH_UPRAW z zapisanymi uprawnieniami do kontaktów
::   WE: _a 0 - przygotować, gdy jeszcze nie ma / 1 - przygotować zawsze
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0)
|| _a:=0
?};
{? _a | var_pres('__KKH_UPRAW')<0
|| VAR_DEL.delete('__KKH_UPRAW');
   __KKH_UPRAW:=obj_new('LIC','EDIT','VIEW');
   __KKH_UPRAW.LIC:=(exec('kkh_lic','zws')='T');
   {? __KKH_UPRAW.LIC
   || __KKH_UPRAW.EDIT:=exec('hasAction','users','KKH_KON_DRKO');
      {? __KKH_UPRAW.EDIT
      || __KKH_UPRAW.VIEW:=1
      || __KKH_UPRAW.VIEW:=exec('hasAction','users','KKH_KON_PKON')
      ?}
   || __KKH_UPRAW.EDIT:=0;
      __KKH_UPRAW.VIEW:=0
   ?}
?};
~~


\dokumz_tr_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.37]
:: OPIS: Wyzwalacz 'po' dla tabeli DOKUMZ
::   WE: _a - rodzaj wyzwalacza: 'add', 'put', 'del'
::       _b - parametr systemowy
::----------------------------------------------------------------------------------------------------------------------
{? _b>0
|| {? _a='put'
   || DOKUM.cntx_psh(); DOKUMZ.cntx_psh();
      DOKUM.prefix();
      {? DOKUMZ.P_OCR='T' & DOKUMZ.DOK().KH<>null() & DOKUM.RODZAJ_K='P' & DOKUM.BL_OCR=''
      || exec('FindAndGet','#table',DOKUM,DOKUMZ.DOK,,"
            @.DOKUM.BL_OCR:=exec('ready_to_ocr','zbl_stat');
            @.DOKUM.BL_STAT:=exec('ready_to_send','zbl_stat');
            @.DOKUM.put()",
         ~~)
      |? DOKUMZ.BL_GUID='' & bfld('P_OCR')='T' & DOKUMZ.P_OCR='N'
      || exec('FindAndGet','#table',DOKUM,DOKUMZ.DOK,,"
            @.DOKUM.BL_OCR:='';
            @.DOKUM.BL_STAT:='';
            @.DOKUM.put()",
         ~~)
      ?};
      DOKUM.cntx_pop(); DOKUMZ.cntx_pop()
   |? _a='del'
   || {? bfld('BL_GUID')='' & bfld('P_OCR')='T'
      || exec('FindAndGet','#table',DOKUM,bfld('DOK'),,"
            @.DOKUM.BL_OCR:='';
            @.DOKUM.BL_STAT:='';
            @.DOKUM.put()",
         ~~)
      ?}
   ?}
?};
~~


\dokum_refsql_kkh_moz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Sprawdzenie, czy DOKUM.REFSQL pozwala na zmianę DOKUM z załącznika w kontakt
::   WE: _a : DOKUM.REFSQL
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_dref:=DOKUM.REFSQL;
{? var_pres('_a')>0
|| _dref:=_a
?};
_moz:=0;
{? _dref<>''
|| _refx:=ref_tab(_dref);
   {? type_of(_refx)<>type_of(~~) & (_refx=FAKS | _refx=ZD_NAG | _refx=ZDP_NAG | _refx=OFE | _refx=UM | _refx=DOK)
   || _moz:=1
   |? _refx=ZK_N
   || _moz:=exec('FindAndGet','#table','ZK_N',DOKUM.REFSQL,,"T().R",'')='Z'
   ?}
?};
_moz


\dokum_refsql_arch_moz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Sprawdzenie, czy DOKUM.REFSQL pozwala na archiwizację załącznika DOKUM, gdy nie jest kontaktem
::   WE: _a : DOKUM.REFSQL
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_dref:=DOKUM.REFSQL;
{? var_pres('_a')>0
|| _dref:=_a
?};
_moz:=0;
{? _dref<>''
|| _refx:=ref_tab(_dref);
   {? type_of(_refx)<>type_of(~~) & (_refx=FAKS | _refx=ZD_NAG | _refx=ZK_N | _refx=OFE | _refx=ND)
   || _moz:=1
   ?}
?};
_moz


\dokum_act_grey
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Wyszarzenie akcji w niepustym oknie wertowania tabeli DOKUM (wywoływane przed rekordem)
::   WE: _a : K - DOKUM jako kontakty (okna WERK1, WERK2, WERK3, WERK4)
::            D - DOKUM jako załączniki (okno WER)
::----------------------------------------------------------------------------------------------------------------------
_jak:='';
{? var_pres('_a')>0
|| _jak:=_a
?};

{? _jak='K'
|| {? KHVZ.KONTAKTY<>'T' | (KHVZ.KHVP<>'kh_all' & KHVZ.KHVP<>'kh_osob' & KHVZ.KHVP<>'dokum'
      & KHVZ.KHVP<>'projekty' & KHVZ.KHVP<>'windykacja')
   || _jak:=''
   ?}
?};

_win:='';
_bez:='';

_zal:=0;
{? _jak='K' | (_jak = 'D' & DOKUM.KH<>null())
|| _zal:=0;
   DOKUMZ.cntx_psh();
   DOKUMZ.index('DISP');
   DOKUMZ.prefix(DOKUM.ref());
   _zal:=DOKUMZ.first();
   DOKUMZ.cntx_pop()
?};
_selected:=0;
{? _jak='K' | _jak = 'D'
|| _selected:=(DOKUM.sel_size()>0)
?};

{? _jak='K'

|| KHVZ.UPRAW:=exec('dokum_upraw','dokum');

   {? KHVZ.KHVP='kh_osob'
   || _win:='WERK3'
   |? KHVZ.KHVP='dokum'
   || _win:='WERK4'
   |? KHVZ.KHVP='windykacja'
   || _win:='WERK1'
   || _win:='WERK2'
   ?};

:: Popraw, Usuń
   {? DOKUM.TYP<>'K' | KHVZ.UPRAW<>''
   || _bez+='PU'
   ?};

:: Zapisz
   {? DOKUM.DOKUM=null() | (KHVZ.UPRAW<>'' & KHVZ.UPRAW<>'P')
   || _bez+='Z'
   ?};

   {? _win<>'WERK1' & _win<>'WERK4'
:: Załączniki, Treść
   || {? KHVZ.UPRAW<>'' & KHVZ.UPRAW<>'P'
      || _bez+='AT'
      ?}
   ?};

   {? _win<>'WERK1'

:: Kopiuj, Zmiany
   || {? KHVZ.UPRAW<>'' & KHVZ.UPRAW<>'P'
      || _bez+='OY'
      ?};

:: Funkcje -> Książka nadawcza
      {? KHVZ.UPRAW<>'' | DOKUM.RODZAJ_K<>'W' | DOKUM.ESEND='T'
      || _bez+='F(K)'
      ?};

:: Rejestracja
      _bez_r:='';
      {? DOKUM.BL='T' & DOKUM.RODZAJ_K<>'P'
      || _bez_r='R'
      |? DOKUM.REFSQL<>''
      || {? (-DOKUM.REFSQL)*'skid'=0
         || _bez_r:='R'
         || _typobieg:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"@.EDOKUM.TYP().TYPOBIEG().NAZWA",'');
            {? _typobieg<>'Obieg faktur'
            || _bez_r+='R(F)'
            |? _typobieg<>'Obieg wniosków'
            || _bez_r+='R(W)'
            ?}
         ?}
      ?};
      {? _bez_r<>''
      || _bez+=_bez_r
      ?};

:: Wysyłanie
      _bez_w:='';
      {? ~_selected & KHVZ.UPRAW<>''
      || _bez_w:='*'
      ?};
      {? _bez_w<>'*'
:: Wyślij, Odblokuj znacznik, Raport wysyłki
      || {? (DOKUM.TYP<>'K' | DOKUM.DOT().RODZ<>'M' | (DOKUM.RODZAJ_K<>'W' & DOKUM.RODZAJ_K<>'N'))
            & DOKUM.TYP<>'D'
         || _bez_w+='WOR'
         ?};
:: Wyślij do OCR
         {? ~_selected & ~_zal
         || _bez_w+='J'
         ?};
:: Synchronizuj z Businesslink
         {? ~_selected & (DOKUM.RODZAJ_K<>'P' | (DOKUM.BL_OCR='' & DOKUM.BL_GUID=''))
         || _bez_w+='S'
         ?};
:: Wyślij do Businesslink, Informacja z Businesslink, Unieważnij w Businesslink, Doślij załączniki
         {? DOKUM.RODZAJ_K<>'W' | ~_zal
         || _bez_w+='BDIU'
         ?}
      ?};
      {? _bez_w<>''
      || {? _bez_w='*'
         || _bez_w:='W'
         || _bez_w:='W('+_bez_w+')'
         ?};
         _bez+=_bez_w
      ?}

   ?}

|? _jak='D'

|| KHVZ.UPRAW:=exec('dokum_upraw','dokum');
   exec('kkh_upraw','dokum_zal');
   _kkh_lic:=__KKH_UPRAW.LIC;
   _kkh_edit:=__KKH_UPRAW.EDIT;
   _kkh_view:=__KKH_UPRAW.VIEW;
   _selected:=(DOKUM.sel_size()>0);
   _win:='WER';

:: Kontakty
   _bez_t:='';
   {? ~_kkh_lic | ~_kkh_edit | ~exec('dokum_refsql_kkh_moz','dokum_zal',DOKUM.REFSQL)
   || _bez_t:='*'
   ?};
   {? _bez_t<>'*'
   || {? DOKUM.KH=null()
      || _bez_t+='U'
      || _bez_t+='D';
         {? KHVZ.UPRAW<>''
         || _bez_t+='U'
         ?}
      ?}
   ?};
   {? _bez_t<>''
   || {? _bez_t='*'
      || _bez_t:='T'
      || _bez_t:='T('+_bez_t+')'
      ?};
      _bez+=_bez_t
   ?};

:: Popraw, Usuń, Edytuj
   {? DOKUM.KH<>null()
   || {? (DOKUM.TYP='K' & (~_kkh_lic | ~_kkh_edit)) | KHVZ.UPRAW<>''
      || _bez+='PUE'
      ?}
   ?};

:: Pokaż, Zapisz
   {? DOKUM.KH<>null()
   || {? (DOKUM.TYP='K' & (~_kkh_lic | ~_kkh_view)) | KHVZ.UPRAW='B'
      || _bez+='OZ'
      ?}
   ?};

:: Pokaż, edytuj, zapisz
   {? DOKUM.DOKR<>'' & DOKUM.DOKUM=null()
   || _bez+='OEZ'
   ?};

:: Wysyłanie
   _bez_w:='';
   {? ~_selected & (DOKUM.KH<>null & ((DOKUM.TYP='K' & (~_kkh_lic | ~_kkh_edit)) | KHVZ.UPRAW<>''))
   || _bez_w:='*'
   ?};
   {? _bez_w<>'*'
:: Powiadomienia, Przypomnienia
   || {? DOKUM.KH=null()
      || _bez_w+='PZ'
      ?};
:: Wyślij do OCR
      {? DOKUM.KH=null() | ~_zal
      || _bez_w+='J'
      ?};
:: Synchronizuj z Businesslink
      {? ~_selected & (DOKUM.KH=null() | DOKUM.RODZAJ_K<>'P' | (DOKUM.BL_OCR='' & DOKUM.BL_GUID=''))
      || _bez_w+='S'
      ?};
:: Wyślij do Businesslink, Informacja z Businesslink, Unieważnij w Businesslink, Doślij załączniki
      {? DOKUM.KH=null() | DOKUM.RODZAJ_K<>'W' | ~_zal
      || _bez_w+='BDIU'
      ?}
   ?};
   {? _bez_w<>''
   || {? _bez_w='*'
      || _bez_w:='W'
      || _bez_w:='W('+_bez_w+')'
      ?};
      _bez+=_bez_w
   ?};
   {? exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"exec('doc_in_ksef','faktury_nag1')",0)
   ||
      _bez+='U'
   ?}

?};

{? _win<>''
|| DOKUM.actions_grayed(_win,_bez)
?};
~~


\dokum_act_hide
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Ukrywanie akcji w oknach wertowania tabeli DOKUM (wywoływane przed pokazaniem okien)
::   WE: _a : K - DOKUM jako kontakty (okna WERK2, WERK3, WERK4)
::            D - DOKUM jako załączniki (okno WER)
::       _b : czy dostępna edycja: 0 - tak (domyślnie), 1 - nie, 2 - nie, ale dostępne wysyłanie
::       _c : DOKUM.REFSQL, POLA.REFSQL (pokazuje, do której tabeli są załączniki, dla _a=D)
::       _d : uprawnienie do obiegów kontaktów: 0 - nie (domyślnie), 1 - tak (dla _a=K)
::----------------------------------------------------------------------------------------------------------------------
_jak:='';
{? var_pres('_a')>0
|| _jak:=_a
?};
_blok:=0;
{? var_pres('_b')>0
|| _blok:=_b
?};
_dref:='';
{? var_pres('_c')>0 & _c<>''
|| _dref:=_c
?};
_obg_upr:='';
{? var_pres('_d')>0
|| _obg_upr:=_d
?};

_params:=params_get();
{? var_press('_params') = 117 & var_press('env_prj',_params) = 117
||
   _bez_p:=''
||
   _bez_p:='I'
?};

{? _jak='K'

:: Dołącz, Dołącz kontakt
|| {? _obg_upr
   || _bez:='C'+_bez_p;
      _bez0:='C'+_bez_p;
      _domysl:='D:D'
   || _bez:='D'+_bez_p;
      _bez0:='D'+_bez_p;
      _domysl:='C:C'
   ?};

   DOKUM.actions('WERK2',_bez+':'+_bez0, _domysl);
   DOKUM.actions('WERK3',_bez+':'+_bez0, _domysl);
   DOKUM.actions('WERK4',_bez+':'+_bez0, _domysl)

|? _jak='D'

|| exec('czytaj','#stalesys',,XINFO,'BL_TNNT');

   _bez:=_bez_p;
   _bez0:=_bez_p;

:: Dołącz
   {? _blok
   || _bez+='D';
      _bez0+='D'
   ?};

:: Popraw, Usuń, Edytuj
   {? _blok
   || _bez+='PUE'
   ?};

:: Wysyłanie
   _bez_w:='';
   {? _blok=1
   || _bez_w:='*'
   || {? exec('dokum_refsql_kkh_moz','dokum_zal',_dref)
      || {? ~exec('is_active','businesslink3')
         || _bez_w:='SBDIUG'
         ?}
      || _bez_w:='PZJSBDIUG'
      ?}
   ?};
   {? _bez_w<>''
   || {? _bez_w='*'
      || _bez_w:='W'
      || _bez_w:='W('+_bez_w+')'
      ?};
      _bez+=_bez_w
   ?};

:: Kontakty
   {? ~exec('dokum_refsql_kkh_moz','dokum_zal',_dref)
   || _bez+='T'
   ?};

:: Przeglądaj archiwum
   {? ~exec('dokum_refsql_kkh_moz','dokum_zal',_dref) & ~exec('dokum_refsql_arch_moz','dokum_zal',_dref)
   || _bez+='R';
      _bez0+='R'
   ?};

:: Legenda
   {? ~exec('dokum_refsql_kkh_moz','dokum_zal',_dref)
   || _bez+='N'
   ?};

   DOKUM.actions('WER',_bez+':'+_bez0)
||
   DOKUM.actions('WERK2',_bez_p+':'+_bez_p)
?};

~~


\bl_dokum_status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Ustawia pole bieżący etap dla kontaktów.
::   WE: [_a] - refsql rekordu
::----------------------------------------------------------------------------------------------------------------------
_refsql:={? _=0 || $cur_tab(1,1).ref() || _a ?};
DOKUM.cntx_psh();
DOKUM.index('BL_2');
DOKUM.prefix(REF.FIRMA,'T',_refsql);
{? DOKUM.first()
|| DOKUM.trig_off('*','*');
   {!
   |? {? (-_refsql)*'doku'
      || _status:={? DOK.ZK='T'
                  || 'Dokument księgowy: Zaksięgowany końcowo'@
                  |? DOK.ZP='T'
                  || 'Dokument księgowy: Zaksięgowany próbnie'@
                  |? DOK.A='T'
                  || 'Dokument księgowy: Zakończono rejestrację'@
                  || 'Dokument księgowy: Rejestracja'@
                  ?};
         {? _status<>DOKUM.OBKONETP || DOKUM.OBKONETP:=_status; DOKUM.put() ?}
      ?};
      DOKUM.next()
   !};
   DOKUM.trig_on('*','*')
?};
DOKUM.cntx_pop()


\dokumz_glowny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Sprawdzenie, czy załącznik DOKUMZ jest główny, czyli DOKUMZ.DOKUM=DOKUM.DOKUM i jest pierwszym takim załącznikiem
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_glowny:=0;
_dkz_ref:=DOKUMZ.ref();
_dkz_plik:=DOKUMZ.DOKUM;
_dref:=DOKUMZ.DOK().ref();
{? _dref<>null()
|| DOKUM.cntx_psh();
   DOKUM.clear();
   {? DOKUM.seek(_dref)
   || {? DOKUM.DOKUM=DOKUMZ.DOKUM
      || _glowny:=1;
         DOKUMZ.cntx_psh();
         DOKUMZ.index('DISP');
         DOKUMZ.prefix(_dref);
         {? DOKUMZ.first()
         || _knc:=0;
            {!
            |? ~_knc & _glowny & DOKUMZ.ref()<>_dkz_ref
            |! {? DOKUMZ.DOKUM=_dkz_plik
               || _glowny:=0
               || _knc:=DOKUMZ.next()
               ?}
            !}
         ?};
         DOKUMZ.cntx_pop()
      ?}
   ?};
   DOKUM.cntx_pop()
?};
_glowny


\web_dok_attach
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Wyświetla okno z załącznikami do dokumentu (WEB_DOK)
::----------------------------------------------------------------------------------------------------------------------
WEB_DOK.cntx_psh();
_grp:=WEB_DOK.grp_make('Załączniki do zewnętrznego dokumentu'@,,'grp_dok_zal');
VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB','BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND');
__CTR.BTAB:='DOKUM';
__CTR.TAB:='WEB_DOK';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','srsr_zal','dok_dokum_ctr',':');
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('dir_sep','srsr_zal');
__CTR.DND:=1;
::_fml:=$('exec(\'ar_dok_win\',\'!fks_dks_zdok\');exec(\'dokum_rfr\',\'srsr_zal\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
::DOK.grp_edit(_grp,DOK,'RDOKED',,_fml,"","",,'maximized');
::DOK.grp_splt(_grp,,'horizontal','');
_bf:=$('exec(\'bobs_dokum\',\'srsr_zal\',WEB_DOK.ARH=\'T\')');
_fml:=$('exec(\'dokum_rfr\',\'srsr_zal\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
WEB_DOK.grp_ctr(_grp,DOKUM,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,_fml,,,,,_bf,,,'maximized');
WEB_DOK.win_sel(_grp);
WEB_DOK.select();
WEB_DOK.cntx_pop()


:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 b0f129c157829a3a3f946f03e9be93f165c3f3926434cc2833726b21774c9f929a477249c52d7b7ab78375c4276b6b24b9db66482f6a8bf14b505ebbce219453f5c4897c92073ef03297faa2dbe8167ce651578fc62056b3792cce1aca1a40e102247691a44363fabb4fecd2ca67561dccd49c370259e780188e7df9d27f090e

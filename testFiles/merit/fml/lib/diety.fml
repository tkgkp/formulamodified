:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: diety.fml
:: Utworzony: 30.06.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły obszaru OBE, obsługa delegacji - diety
::======================================================================================================================


\edokumd_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed obsługą okna wertowania diet
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| EDOKUM.web_cntx_load(1);
   EDOKUMD.use('bdiety'+(EDOKUM.name()+2));
   EDOKUMD.index('DELEG'); EDOKUMD.prefix(EDOKUM.ref())
|| EDOKUMD.index('DELEG'); EDOKUMD.prefix(EDOKUM.ref());
   {? ~EDOKUMD.get() || EDOKUMD.first() ?}
?};
1


\edokumd_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na Dołącz dla tabeli diet EDOKUMD
::----------------------------------------------------------------------------------------------------------------------
web_params_set(web_params_get());
exec('env_wt','b_proces');
EDOKUM.web_cntx_load(1);
EDOKUM.web_cntx_save(1);
exec('czytaj','#stalesys',,XINFO);
{? EDOKUM.ZAM='T'
|| FUN.info('Delegacja zamknięta, nie podlega modyfikacjom.'@);
   return()
?};
EDOKUM.web_cntx_load(1);
EDOKUMD.blank();
OBIEGI.DIETKWOT:=0;
EDOKUMD.EDOKUM:=EDOKUM.ref();
EDOKUMD.CZY_DIET:=1;
EDOKUMD.SNIAD:=EDOKUMD.OBIAD:=EDOKUMD.KOLACJA:=0;
EDOKUMD.W_SNI:=EDOKUMD.W_OBI:=EDOKUMD.W_KOL:=0;
EDOKUMD.LICZ_SNI:=EDOKUMD.LICZ_OBI:=EDOKUMD.LICZ_KOL:=0;
_find:=0;
{? EDOKUM.TYP().DEL_ZAGR=0
||
:: kraj
   KRAJE.cntx_psh();
   KRAJE.index('KODISO');
   KRAJE.prefix('PL','PL');
   {? KRAJE.first() || EDOKUMD.KRAJ:=KRAJE.ref() ?};
   KRAJE.cntx_pop();
:: waluta
   WAL.cntx_psh();
   WAL.index('WAL_SYM');
   WAL.prefix('PLN');
   {? WAL.first() || EDOKUMD.WAL:=WAL.ref() ?};
   WAL.cntx_pop();
:: oszacowanie dat jesli nie pierwsza dieta
   EDOKUMD.cntx_psh();
   EDOKUMD.index('DELEG');
   EDOKUMD.prefix(EDOKUM.ref());
   _p_d_od:=_p_d_do:=date(0,0,0);
   _p_g_od:=_p_g_do:=time(0,0,0);
   {? EDOKUMD.last()
   || _find:=1;
      {! |?
         {? EDOKUMD.KRAJ().SYM='PL' & EDOKUMD.Z_CZY_G='Z'
         || _find:=2;
            _p_d_od:=EDOKUMD.DATA_DO;
            _p_d_do:=EDOKUM.DATA_DO;
            _p_g_od:=EDOKUMD.GODZ_DO;
            _p_g_do:=EDOKUM.GODZ_DO
         ?};
         _find<2 & EDOKUMD.prev()
      !}
   || _find:=0
   ?};
   EDOKUMD.cntx_pop();
   {? _find=2 & _p_d_od<>date(0,0,0) & _p_d_do<>date(0,0,0) & _p_g_od<>time(0,0,0)
      & _p_g_do<>time(0,0,0)
   || EDOKUMD.DATA_OD:=_p_d_od;
      EDOKUMD.GODZ_OD:=_p_g_od;
      EDOKUMD.DATA_DO:=_p_d_do;
      EDOKUMD.GODZ_DO:=_p_g_do
   ?}
:: jeżeli zagraniczna
|| EDOKUMD.cntx_psh();
   EDOKUMD.index('DELEG');
   EDOKUMD.prefix(EDOKUM.ref());
   {? EDOKUMD.last()
   || _find:=1;
      _p_d_od:=_p_d_do:=date(0,0,0);
      _p_g_od:=_p_g_do:=time(0,0,0);
      {! |?
         {? EDOKUMD.Z_CZY_G='Z'
         || _find:=2;
            {?  EDOKUMD.KRAJ<>null & EDOKUMD.KRAJ().SYM<>'PL' & EDOKUM.GRAN_DPW<>date(0,0,0)
                & EDOKUMD.DATA_DO<EDOKUM.GRAN_DPW
            || _p_d_od:=EDOKUMD.DATA_DO;
               _p_d_do:=EDOKUM.GRAN_DPW;
               _p_g_od:=EDOKUMD.GODZ_DO;
               _p_g_do:=EDOKUM.GRAN_GPW
            |? EDOKUMD.KRAJ<>null & EDOKUMD.KRAJ().SYM<>'PL' & EDOKUM.GRAN_DPW<>date(0,0,0)
               & EDOKUMD.DATA_DO=EDOKUM.GRAN_DPW & EDOKUMD.GODZ_DO<EDOKUM.GRAN_GPW
            || _p_d_od:=EDOKUMD.DATA_DO;
               _p_d_do:=EDOKUM.GRAN_DPW;
               _p_g_od:=EDOKUMD.GODZ_DO;
               _p_g_do:=EDOKUM.GRAN_GPW
            || _p_d_od:=EDOKUMD.DATA_DO;
               _p_d_do:=EDOKUM.DATA_DO;
               _p_g_od:=EDOKUMD.GODZ_DO;
               _p_g_do:=EDOKUM.GODZ_DO
            ?}
         ?};
         _find<2 & EDOKUMD.prev()
      !}
   || _find:=0
   ?};
   EDOKUMD.cntx_pop();
   {? _find=2 & _p_d_od<>date(0,0,0) & _p_d_do<>date(0,0,0) & _p_g_od<>time(0,0,0)
      & _p_g_do<>time(0,0,0)
   || EDOKUMD.DATA_OD:=_p_d_od;
      EDOKUMD.GODZ_OD:=_p_g_od;
      EDOKUMD.DATA_DO:=_p_d_do;
      EDOKUMD.GODZ_DO:=_p_g_do
   |? _find=0
   || {? EDOKUM.GRAN_DAT<>date(0,0,0) || EDOKUMD.DATA_OD:=EDOKUM.GRAN_DAT ?};
      {? EDOKUM.GRAN_GDZ<>time(0,0,0) || EDOKUMD.GODZ_OD:=EDOKUM.GRAN_GDZ ?};
      {? EDOKUM.GRAN_DPW<>date(0,0,0) || EDOKUMD.DATA_DO:=EDOKUM.GRAN_DPW ?};
      {? EDOKUM.GRAN_GPW<>time(0,0,0) || EDOKUMD.GODZ_DO:=EDOKUM.GRAN_GPW ?}
   ?}
?};
exec('przelicz_diet','diety');
exec('czytaj','#stalesys',,XINFO);
:: słownik dodatkowy
{? XINFO.SLU_INNY<>null
|| SLU.prefix(); SLU.seek(XINFO.SLU_INNY);
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'INNEDANE')
|| EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0, editable=0',EDOKUMD,'INNEDANE')
?};


exec('be_win_edokumd','diety');
_form:="web_params_set(web_params_get());
        exec('env_wt','b_proces');
        {? _a='OK'
        || _chk:=exec('chk_dieta','diety','ADD');
           {? _chk=''
           || EDOKUMD.add();
              EDOKUMD.web_eclose();
              EDOKUMD.web_refresh('POZ_DEL','poz_del_diety')
           || web_top_update(0,,,_chk)
           ?}
        |? _a='ANULUJ'
        ||  EDOKUMD.web_eclose()
        ?}";
_del:='delegacja: '@;
_txt:=_del+EDOKUM.ID+', '+$EDOKUM.DATA_OD+' - '+$EDOKUM.DATA_DO+', '+EDOKUM.DEL_MIE;
EDOKUMD.web_ewin_init('RED_WW',,'title=", '+_txt+'"');
EDOKUMD.web_edit('RED_WW','_edokumd_red',,,_form)


\edokumd_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na Popraw dla tabeli diet EDOKUMD
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| {? EDOKUMD.get()
   || web_params_set(web_params_get());
      exec('env_wt','b_proces');
      exec('czytaj','#stalesys',,XINFO);
      OBIEGI.DIETKWOT:=0;
      EDOKUM.web_cntx_load(1);
      EDOKUM.web_cntx_save(1);
      {? EDOKUMD.EDOKUM().ZAM='T'
      || FUN.info('Delegacja zamknięta, nie podlega modyfikacjom.'@);
         return()
      ?};
:: słownik dodatkowy
      {? XINFO.SLU_INNY<>null
      || SLU.prefix(); SLU.seek(XINFO.SLU_INNY);
         EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'INNEDANE')
      || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0, editable=0',EDOKUMD,'INNEDANE')
      ?};
      exec('be_win_edokumd','diety');
      _form:="web_params_set(web_params_get());
              exec('env_wt','b_proces');
              {? _a='OK'
              || _chk:=exec('chk_dieta','diety','EDIT');
                 {? _chk=''
                 || EDOKUMD.put();
                    EDOKUMD.web_eclose();
                    EDOKUMD.web_refresh('POZ_DEL','poz_del_diety')
                 || web_top_update(0,,,_chk)
                 ?}
              |? _a='ANULUJ'
              ||  EDOKUMD.web_eclose()
              ?}";
      _del:='delegacja: '@;
      _txt:=_del+EDOKUM.ID+', '+$EDOKUM.DATA_OD+' - '+$EDOKUM.DATA_DO+', '+EDOKUM.DEL_MIE;
      EDOKUMD.web_ewin_init('RED_WW',,'title=", '+_txt+'"');
      EDOKUMD.web_edit('RED_WW','_edokumd_red',,,_form)
   || FUN.info('Nie znaleziono diety.'@);
      web_top_refresh()
   ?}
|| {? EDOKUMD.get()
   || EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix();
      {? EDOKUMD.EDOKUM().ZAM='T'
      || FUN.info('Delegacja zamknięta, nie podlega modyfikacjom.'@);
         return()
      ?};
      OBIEGI.DIETKWOT:=0;
      exec('czytaj','#stalesys',,XINFO);
      {? XINFO.SLU_INNY<>null
      || SLU.prefix(); SLU.seek(XINFO.SLU_INNY);
         EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'INNEDANE')
      || EDOKUMD.efld_opt('RED','mark=0, enable=0, editable=0',EDOKUMD,'INNEDANE')
      ?};
      exec('be_win_edokumd_jt','diety');
      _del:='delegacja: '@;
      _txt:=_del+EDOKUM.ID+', '+$EDOKUM.DATA_OD+' - '+$EDOKUM.DATA_DO+', '+EDOKUM.DEL_MIE;
      EDOKUMD.win_edit('RED');
      {? EDOKUMD.edit("exec('chk_dieta','diety','EDIT')") || EDOKUMD.put() ?}
   || FUN.info('Nie znaleziono diety.'@)
   ?}
?}


\edokumd_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła na Usuń dla tabeli diet EDOKUMD
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.get()
|| EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix();
   {? EDOKUMD.EDOKUM().ZAM='T'
   || FUN.info('Delegacja zamknięta, nie podlega modyfikacjom.'@);
      return()
   ?};
   EDOKUMD.web_cntx_save();
   _form:="{? _a=1
           || {? EDOKUMD.web_cntx_load() || EDOKUMD.del() ?};
              EDOKUMD.web_refresh('POZ_DEL','poz_del_diety')
           ?}";
   web_ask(_form,'Czy usunąć bieżącą dietę?'@,FUN.TYT)
|| FUN.info('Nie znaleziono diety.'@);
   web_top_refresh()
?}


\edokumd_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła wyświetla rekord w tabeli diet
::----------------------------------------------------------------------------------------------------------------------
:: słownik dodatkowy
exec('czytaj','#stalesys',,XINFO);
{? XINFO.SLU_INNY<>null
|| SLU.prefix();
   SLU.seek(XINFO.SLU_INNY)
?};
OBIEGI.DIETKWOT:=EDOKUMD.KWOTA_W+EDOKUMD.RYCZ_NOC+EDOKUMD.RYCZ_DOJ+EDOKUMD.KWOTA_L;
{? app_info('web_sesid')=''
|| EDOKUMD.win_edit('RED'); EDOKUMD.display()
|| _form:="EDOKUMD.web_eclose()";
   EDOKUMD.web_display('RED_WWD','_edokumd_red',_form)
?}


\be_win_edokumd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed oknem redagowania RED_WW diety EDOKUMD, ustawia dostępność pól
::  OLD: \be_diet_kraj/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix();
{? EDOKUMD.EDOKUM().TYP().DEL_ZAGR=0
|| EDOKUMD.web_efld_init('RED_WW',,'win_dict=SLO_WW, mark=0, enable=1, editable=0',EDOKUMD,'KRAJ','NAZ');
   EDOKUMD.web_efld_init('RED_WW',,'win_dict=SLO_WW, mark=0, enable=1, editable=0',EDOKUMD,'WAL','SYM')
|| EDOKUMD.web_efld_init('RED_WW',,'win_dict=SLO_WW, mark=0, enable=1, editable=1',EDOKUMD,'KRAJ','NAZ');
   EDOKUMD.web_efld_init('RED_WW',,'win_dict=SLO_WW, mark=0, enable=1, editable=1',EDOKUMD,'WAL','SYM')
?};
{? EDOKUMD.Z_CZY_G='G'
|| EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'DATA_OD');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'DATA_DO');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'GODZ_OD');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'GODZ_DO');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'L_G');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'CZAS_GDZ');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'CZAS_MIN')
|| EDOKUMD.web_efld_init('RED_WW',,'mark=1, enable=1, editable=1',EDOKUMD,'DATA_OD');
   EDOKUMD.web_efld_init('RED_WW',,'mark=1, enable=1, editable=1',EDOKUMD,'DATA_DO');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'GODZ_OD');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'GODZ_DO');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'L_G');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'CZAS_GDZ');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'CZAS_MIN')
?};
exec('czytaj','#stalesys',,XINFO);
{? EDOKUMD.CZY_NOC
|| EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'RYCZ_NOC');
   EDOKUMD.web_efld_init('RED_WW',,'mark=1, enable=1, editable=1',EDOKUMD,'LICZ_NOC')
|| EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'RYCZ_NOC');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_NOC')
?};
{? EDOKUMD.CZY_DOJ
|| EDOKUMD.web_efld_init('RED_WW',,'mark=1, enable=1, editable=1',EDOKUMD,'LICZ_DOJ');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'RYCZ_DOJ')
|| EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_DOJ');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'RYCZ_DOJ')
?};
{? ~EDOKUMD.CZY_PORT
|| EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'L_PORT');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'KWOTA_L')
|| EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'L_PORT');
   EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'KWOTA_L')
?};
{? (EDOKUMD.EDOKUM().TYP().DEL_ZAGR=0 & EDOKUMD.KRAJ().SYM='PL')
   | (EDOKUMD.EDOKUM().TYP().DEL_ZAGR>0 & EDOKUMD.KRAJ().SYM='PL')
|| EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'CZY_PORT')
|| EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'CZY_PORT')
?};
exec('ustaw_pola_diety','diety',1)


\ae_z_czy_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.Z_CZY_G
::  OLD: \ae_z_czy_g/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.Z_CZY_G='G' || exec('godz_szac','diety') ?};
EDOKUMD.LICZ_NOC:=exec('wylicz_nocleg','diety');
EDOKUMD.RYCZ_NOC:=exec('rycz_noc','diety');
EDOKUMD.LICZ_DOJ:=exec('wylicz_nocleg','diety',1);
EDOKUMD.RYCZ_DOJ:=exec('rycz_doj','diety');
exec('czytaj','#stalesys',,XINFO);
exec('kwota_refresh','diety');
{? app_info('web_sesid')<>''
|| {? EDOKUMD.Z_CZY_G='G'
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'DATA_OD');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'DATA_DO');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'GODZ_OD');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'GODZ_DO');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'L_G');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'CZAS_GDZ');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'CZAS_MIN')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=1, enable=1, editable=1',EDOKUMD,'DATA_OD');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=1, enable=1, editable=1',EDOKUMD,'DATA_DO');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'GODZ_OD');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'GODZ_DO');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'L_G');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'CZAS_GDZ');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'CZAS_MIN')
   ?};
   exec('ustaw_pola_diety','diety')
|| {? EDOKUMD.Z_CZY_G='G'
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'DATA_OD');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'DATA_DO');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'GODZ_OD');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'GODZ_DO');
      EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'L_G');
      EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'CZAS_GDZ');
      EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'CZAS_MIN')
   || EDOKUMD.efld_opt('RED','mark=1, enable=1, editable=1',EDOKUMD,'DATA_OD');
      EDOKUMD.efld_opt('RED','mark=1, enable=1, editable=1',EDOKUMD,'DATA_DO');
      EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'GODZ_OD');
      EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'GODZ_DO');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'L_G');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'CZAS_GDZ');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'CZAS_MIN')
   ?};
   exec('ustaw_pola_diety_jt','diety')
?};
1


\ae_diet_rycz_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula po redakcji pola EDOKUMD.CZY_NOC
::  OLD: \ae_diet_rycz_n/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKUMD.LICZ_NOC:=exec('wylicz_nocleg','diety');
EDOKUMD.RYCZ_NOC:=exec('rycz_noc','diety');
exec('kwota_refresh','diety');
{? app_info('web_sesid')<>''
|| {? EDOKUMD.CZY_NOC
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'RYCZ_NOC');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=1, enable=1, editable=1',EDOKUMD,'LICZ_NOC')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'RYCZ_NOC');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_NOC')
   ?}
|| {? EDOKUMD.CZY_NOC
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'RYCZ_NOC');
      EDOKUMD.efld_opt('RED','mark=1, enable=1, editable=1',EDOKUMD,'LICZ_NOC')
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'RYCZ_NOC');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_NOC')
   ?}
?};
1


\ae_diet_rycz_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula po redakcji pola EDOKUMD.CZY_DOJ
::  OLD: \ae_diet_rycz_d/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKUMD.LICZ_DOJ:=exec('wylicz_nocleg','diety',1);
EDOKUMD.RYCZ_DOJ:=exec('rycz_doj','diety');
exec('kwota_refresh','diety');
exec('czytaj','#stalesys',,XINFO);
{? app_info('web_sesid')<>''
|| {? EDOKUMD.CZY_DOJ
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=1, enable=1, editable=1',EDOKUMD,'LICZ_DOJ');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'RYCZ_DOJ')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_DOJ');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'RYCZ_DOJ')
   ?}
|| {? EDOKUMD.CZY_DOJ
   || EDOKUMD.efld_opt('RED','mark=1, enable=1, editable=1',EDOKUMD,'LICZ_DOJ');
      EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'RYCZ_DOJ')
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_DOJ');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'RYCZ_DOJ')
   ?}
?};
1


\ustaw_pola_diety
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Ustawia dostępność pól w sekcji diet
::   WE: _a - zadeklarowane i równe 1 - init okna
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.CZY_DIET=0
|| EDOKUMD.KWOTA_W:=0;
   EDOKUMD.SNIAD:=EDOKUMD.OBIAD:=EDOKUMD.KOLACJA:=0;
   EDOKUMD.W_SNI:=EDOKUMD.W_OBI:=EDOKUMD.W_KOL:=0;
   EDOKUMD.LICZ_SNI:=EDOKUMD.LICZ_OBI:=EDOKUMD.LICZ_KOL:=0
?};
{? app_info('web_sesid')='' || return() ?};
exec('czytaj','#stalesys',,XINFO);
{? _ & _a=1
|| {? EDOKUMD.CZY_DIET
   || {? XINFO.EDI_DIET
      || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'KWOTA_W')
      || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=0',EDOKUMD,'KWOTA_W')
      ?};
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'SNIAD');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'OBIAD');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'KOLACJA');
      {? EDOKUMD.SNIAD
      || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'W_SNI');
         {? EDOKUMD.W_SNI=1
         || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=0',EDOKUMD,'LICZ_SNI')
         || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_SNI')
         ?}
      || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_SNI');
         EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_SNI')
      ?};
      {? EDOKUMD.OBIAD
      || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'W_OBI');
         {? EDOKUMD.W_OBI=1
         || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=0',EDOKUMD,'LICZ_OBI')
         || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_OBI')
         ?}
      || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_OBI');
         EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_OBI')
      ?};
      {? EDOKUMD.KOLACJA
      || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'W_KOL');
         {? EDOKUMD.W_KOL=1
         || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=0',EDOKUMD,'LICZ_KOL')
         || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_KOL')
         ?}
      || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_KOL');
         EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_KOL')
      ?}
   || EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'KWOTA_W');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'SNIAD');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_SNI');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_SNI');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'OBIAD');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_OBI');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_OBI');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'KOLACJA');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_KOL');
      EDOKUMD.web_efld_init('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_KOL')
   ?}
|| {? EDOKUMD.CZY_DIET
   || {? XINFO.EDI_DIET
      || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'KWOTA_W')
      || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=0',EDOKUMD,'KWOTA_W')
      ?};
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'SNIAD');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'OBIAD');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'KOLACJA');
      {? EDOKUMD.SNIAD
      || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'W_SNI');
         {? EDOKUMD.W_SNI=1
         || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=0',EDOKUMD,'LICZ_SNI')
         || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_SNI')
         ?}
      || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_SNI');
         EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_SNI')
      ?};
      {? EDOKUMD.OBIAD
      || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'W_OBI');
         {? EDOKUMD.W_OBI=1
         || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=0',EDOKUMD,'LICZ_OBI')
         || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_OBI')
         ?}
      || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_OBI');
         EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_OBI')
      ?};
      {? EDOKUMD.KOLACJA
      || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'W_KOL');
         {? EDOKUMD.W_KOL=1
         || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=0',EDOKUMD,'LICZ_KOL')
         || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_KOL')
         ?}
      || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_KOL');
         EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_KOL')
      ?}
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'KWOTA_W');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'SNIAD');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_SNI');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_SNI');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'OBIAD');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_OBI');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_OBI');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'KOLACJA');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_KOL');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_KOL')
   ?}
?}


\ae_diet
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.CZY_DIET
::  OLD: \ae_diet/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKUMD.SNIAD:=EDOKUMD.OBIAD:=EDOKUMD.KOLACJA:=0;
{? app_info('web_sesid')<>''
|| exec('ustaw_pola_diety','diety')
|| exec('ustaw_pola_diety_jt','diety')
?};
exec('przelicz_diet','diety');
1


\ae_diet_sniad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.SNIAD
::  OLD: \ae_diet_sniad/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.SNIAD=0
|| EDOKUMD.W_SNI:=0
|? EDOKUMD.SNIAD=1
|| EDOKUMD.LICZ_SNI:={? EDOKUMD.DNI$0=0 || 1 || EDOKUMD.DNI$0 ?};
   EDOKUMD.W_SNI:=1
?};
{? app_info('web_sesid')<>''
|| {? ~EDOKUMD.CZY_DIET | EDOKUMD.SNIAD=0 | EDOKUMD.W_SNI=1
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_SNI')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_SNI')
   ?};
   {? EDOKUMD.SNIAD
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'W_SNI')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_SNI')
   ?}
|| {? ~EDOKUMD.CZY_DIET | EDOKUMD.SNIAD=0 | EDOKUMD.W_SNI=1
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_SNI')
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'LICZ_SNI')
   ?};
   {? EDOKUMD.SNIAD
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'W_SNI')
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'W_SNI')
   ?}
?};
exec('przelicz_diet','diety')


\ae_diet_obiad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.OBIAD
::  OLD: \ae_diet_obiad/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.OBIAD=0
|| EDOKUMD.W_OBI:=0
|? EDOKUMD.OBIAD=1
|| EDOKUMD.LICZ_OBI:={? EDOKUMD.DNI$0=0 || 1 || EDOKUMD.DNI$0 ?};
   EDOKUMD.W_OBI:=1
?};
{? app_info('web_sesid')<>''
|| {? ~EDOKUMD.CZY_DIET | EDOKUMD.OBIAD=0 | EDOKUMD.W_OBI=1
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_OBI')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_OBI')
   ?};
   {? EDOKUMD.OBIAD
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'W_OBI')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_OBI')
   ?}
|| {? ~EDOKUMD.CZY_DIET | EDOKUMD.OBIAD=0 | EDOKUMD.W_OBI=1
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_OBI')
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'LICZ_OBI')
   ?};
   {? EDOKUMD.OBIAD
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'W_OBI')
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'W_OBI')
   ?}
?};
exec('przelicz_diet','diety')


\ae_diet_kolacja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.KOLACJA
::  OLD: \ae_diet_kolacja/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.KOLACJA=0
|| EDOKUMD.W_KOL:=0
|? EDOKUMD.KOLACJA=1
|| EDOKUMD.LICZ_KOL:={? EDOKUMD.DNI$0=0 || 1 || EDOKUMD.DNI$0 ?};
   EDOKUMD.W_KOL:=1
?};
{? app_info('web_sesid')<>''
|| {? ~EDOKUMD.CZY_DIET | EDOKUMD.KOLACJA=0 | EDOKUMD.W_KOL=1
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_KOL')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_KOL')
   ?};
   {? EDOKUMD.KOLACJA
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'W_KOL')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'W_KOL')
   ?}
|| {? ~EDOKUMD.CZY_DIET | EDOKUMD.KOLACJA=0 | EDOKUMD.W_KOL=1
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_KOL')
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'LICZ_KOL')
   ?};
   {? EDOKUMD.KOLACJA
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'W_KOL')
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'W_KOL')
   ?}
?};
exec('przelicz_diet','diety')


\ae_diet_czy_por
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.CZY_PORT
::  OLD: \ae_diet_czy_por/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| {? ~EDOKUMD.CZY_PORT
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'L_PORT');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'KWOTA_L')
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'L_PORT');
      EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'KWOTA_L')
   ?}
|| {? ~EDOKUMD.CZY_PORT
   || EDOKUMD.web_efld_opt('RED','mark=0, enable=0',EDOKUMD,'L_PORT');
      EDOKUMD.web_efld_opt('RED','mark=0, enable=0',EDOKUMD,'KWOTA_L')
   || EDOKUMD.web_efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'L_PORT');
      EDOKUMD.web_efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'KWOTA_L')
   ?}
?};
{? ~EDOKUMD.CZY_PORT
|| EDOKUMD.L_PORT:=0;
   EDOKUMD.KWOTA_L:=0;
   exec('kwota_refresh','diety')
?};
1


\ae_diet_kraj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.KRAJ
::  OLD: \ae_diet_kraj/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKUMD.WAL:=null();
{? app_info('web_sesid')<>''
|| EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMD.EDOKUM()
?};
_rkraj:=EDOKUMD.KRAJ; _skraj:=EDOKUMD.KRAJ().SYM;
{? _rkraj=null
|| FUN.info('Pole Kraj nie moze być puste.'@);
   return(0)
?};
{? _skraj<>'PL'
|| _st:=exec('stawki_zagr','diety',_skraj,EDOKUMD.EDOKUM().DATA_OD,'D')
|| _st:=1
?};
{? ~_st
|| FUN.info('Brak zdefiniowanych stawek diet zagranicznych dla kraju: %1.'@[EDOKUMD.KRAJ().NAZ]);
   return(0)
?};
{? _rkraj<>null
|| {? _skraj<>'PL'
   || EDOKUMD.WAL:=exec('wal_diet_zagr','diety', _skraj, EDOKUMD.DATA_OD);
      {? EDOKUM.TYP().DEL_ZAGR & EDOKUMD.Z_CZY_G='G' || exec('ae_z_czy_g','diety') ?}
   || WAL.cntx_psh();
      WAL.index('WAL_SYM');
      WAL.prefix('PL');
      {? WAL.first() || EDOKUMD.WAL:=WAL.ref() ?};
      WAL.cntx_pop();
      EDOKUMD.L_PORT:=0;
      EDOKUMD.KWOTA_L:=0;
      EDOKUMD.CZY_PORT:=0;
      {? EDOKUM.TYP().DEL_ZAGR & ~exec('on','#mwapi')
      || EDOKUMD.Z_CZY_G:='G';
         exec('ae_z_czy_g','diety')
      ?}
   ?}
?};
exec('przelicz_diet','diety');
EDOKUMD.RYCZ_NOC:=exec('rycz_noc','diety');
EDOKUMD.RYCZ_DOJ:=exec('rycz_doj','diety');
exec('ae_diet_l_port','diety');
1


\stawki_zagr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Zwraca stawke diety zagranicznej lub stawke ryczaltu za nocleg
::   WE: _a - symbol kraju
::       _b - data dla ktorej formula ma zwrocic obowiazujace stawki
::       _c - typ stawki 'D' - dieta, 'N' - ryczałt za nocleg, 'W' - waluta, domyslnie, jesli nie podano to 'D'
::  OLD: \stawki_zagr/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? _<3 || _c:='D' ?};
_stawka:=0;
DIETZAGR.index('KRAJ');
DIETZAGR.prefix(_a);
{? DIETZAGR.first()
|| {? _b<>date(0,0,0)
   || _od:=_b
   || _od:=date()
   ?};
   {! |?
        {? DIETZAGR.OD<_od
        || {? _c='D' || _stawka:=DIETZAGR.STAWKA
           |? _c='N' || _stawka:=DIETZAGR.RYCZ_NOC
           |? _c='W' || _stawka:=DIETZAGR.WAL
           ?};
           DIETZAGR.next()
        || 0
        ?}
   !}
?};
_stawka


\wal_diet_zagr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [25.10.2013]
:: OPIS: Zwraca walute rozliczania delegacji zagranicznej dla podanej daty i kraju
::   WE: _a - symbol kraju (np. z EDOKUMD.KRAJ().SYM)
::       _b - data (np. EDOKUMD.DATA_OD)
::  OLD: \wal_diet_zagr/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=null;
DIETZAGR.cntx_psh();
DIETZAGR.index('KRAJ');
DIETZAGR.prefix(_a);
{? DIETZAGR.last()
|| {? _b<>date(0,0,0) || _od:=_b || _od:=date() ?};
   {! |?
      {? DIETZAGR.OD<_od
      || _res:=DIETZAGR.WAL;
         0
      || DIETZAGR.prev()
      ?}
   !}
?};
DIETZAGR.cntx_pop();
_res


\przelicz_diet
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula odswiezajaca kwote diety
::  OLD: \przelicz_diet/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_dieta:=0;
_dni:=0;
{? app_info('web_sesid')<>''
|| EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMD.EDOKUM()
?};
{? EDOKUMD.CZY_DIET & ((EDOKUMD.Z_CZY_G='Z' & EDOKUMD.DATA_OD<>date(0,0,0) & EDOKUMD.DATA_DO<>date(0,0,0))
   | (EDOKUMD.Z_CZY_G='G'))
|| exec('czytaj','#stalesys',{? EDOKUMD.DATA_OD<>date(0,0,0) || EDOKUMD.DATA_OD || EDOKUMD.EDOKUM().DOP ?},XINFO);
   {? EDOKUMD.Z_CZY_G='Z'
   || _tmp_dni:=EDOKUMD.DATA_DO-EDOKUMD.DATA_OD;
      _godz:=_tmp_dni*24+((EDOKUMD.GODZ_DO~1)-(EDOKUMD.GODZ_OD~1));
      _tmp_min:=((EDOKUMD.GODZ_DO~2)-(EDOKUMD.GODZ_OD~2));
      _tmp_sec:=((EDOKUMD.GODZ_DO~3)-(EDOKUMD.GODZ_OD~3));
      _godz+=(_tmp_min/60)+(_tmp_sec/60/60)
   |? EDOKUMD.Z_CZY_G='G'
   || _godz:=EDOKUMD.CZAS_GDZ+(EDOKUMD.CZAS_MIN/60)
   ?};

   {? EDOKUMD.KRAJ().SYM='PL'
   ||
:: obliczenie diety
      {? _godz<8
      || _dieta:=0;
         _dni:=0
      |? _godz>=8 & _godz<=12
      || _dieta:=XINFO.DIETA*0.5;
         _dni:=0.5
      |? _godz>12 & _godz<=24
      || _dieta:=XINFO.DIETA;
         _dni:=1
      |? _godz>24
      || _dni:=_godz%24;
         _dieta:=_dni*XINFO.DIETA;
         _tmp_g:=_godz-(_dni*24);
         {? _tmp_g>0
         || {? _tmp_g<=8
            || _dieta+=XINFO.DIETA*0.5;
               _dni+=0.5
            || _dieta+=XINFO.DIETA;
               _dni+=1
            ?}
         ?}
      ?};

:: zmiejszenia
      _zmniejsz:=0;
      _licz_sni:=EDOKUMD.LICZ_SNI;
      _licz_obi:=EDOKUMD.LICZ_OBI;
      _licz_kol:=EDOKUMD.LICZ_KOL;
      _st_s:=XINFO.DIETA*(XINFO.SNIAD/100);
      _st_o:=XINFO.DIETA*(XINFO.OBIAD/100);
      _st_k:=XINFO.DIETA*(XINFO.KOLACJA/100);
      {? EDOKUMD.W_SNI=1 || _licz_sni:=(_dni$0) ?};
      {? EDOKUMD.W_OBI=1 || _licz_obi:=(_dni$0) ?};
      {? EDOKUMD.W_KOL=1 || _licz_kol:=(_dni$0) ?};

      {? EDOKUMD.SNIAD=1 || _zmniejsz+=(_licz_sni*_st_s) ?};
      {? EDOKUMD.OBIAD=1 || _zmniejsz+=(_licz_obi*_st_o) ?};
      {? EDOKUMD.KOLACJA=1 || _zmniejsz+=(_licz_kol*_st_k) ?};
::web_msg($EDOKUMD.SNIAD+' - '+$EDOKUMD.OBIAD+' - '+$EDOKUMD.KOLACJA);
::web_msg('Kraj: '+EDOKUMD.KRAJ().SYM+', godziny: '+$_godz+', stawka: '+$XINFO.DIETA+', dieta: '+$_dieta+', zmniejszenia: '+$_zmniejsz);
      {? _zmniejsz>0 || _dieta:=_dieta-_zmniejsz ?};
      {? _dieta<0 || _dieta:=0 ?}
   || {? EDOKUMD.KRAJ<>null & EDOKUMD.KRAJ().SYM<>'PL'
      || _doba:=exec('stawki_zagr','diety',EDOKUMD.KRAJ().SYM,EDOKUMD.EDOKUM().DATA_OD,'D');
         {? _doba<>0
         || {? _godz=0
            || _dieta:=0
            |? _godz<=8
            || _dieta:=_doba/3;
               _dni:=(1/3)
            |? _godz>8 & _godz<=12
            || _dieta:=_doba*0.5;
               _dni:=0.5
            |? _godz>12 & _godz<=24
            || _dieta:=_doba;
               _dni:=1
            |? _godz>24
            || _dni:=_godz%24;
               _dieta:=_dni*_doba;
               _tmp_g:=_godz-(_dni*24);
               {? _tmp_g>0
               || {? _tmp_g<=8
                  || _dieta+=_doba/3;
                     _dni+=(1/3)
                  |? _tmp_g<=12
                  || _dieta+=_doba*0.5;
                     _dni+=0.5
                  || _dieta+=_doba;
                     _dni+=1
                  ?}
               ?}
            ?};
            _zmniejsz:=0;
            _licz_sni:=EDOKUMD.LICZ_SNI;
            _licz_obi:=EDOKUMD.LICZ_OBI;
            _licz_kol:=EDOKUMD.LICZ_KOL;
            _st_s:=_doba*(XINFO.ZSNIAD/100);
            _st_o:=_doba*(XINFO.ZOBIAD/100);
            _st_k:=_doba*(XINFO.ZKOLACJA/100);

            {? EDOKUMD.W_SNI=1 || _licz_sni:=ceil(_dni) ?};
            {? EDOKUMD.W_OBI=1 || _licz_obi:=ceil(_dni) ?};
            {? EDOKUMD.W_KOL=1 || _licz_kol:=ceil(_dni) ?};

            {? EDOKUMD.SNIAD=1 || _zmniejsz+=(_licz_sni*_st_s) ?};
            {? EDOKUMD.OBIAD=1 || _zmniejsz+=(_licz_obi*_st_o) ?};
            {? EDOKUMD.KOLACJA=1 || _zmniejsz+=(_licz_kol*_st_k) ?};
            {? _zmniejsz>0
            || _minimum:=0.25*_dieta;
               _dieta:=_dieta-_zmniejsz;
               {? _dieta<_minimum || _dieta:=_minimum ?}
            ?}
         ?}
      ?}
   ?};
   {? _dieta<0 || _dieta:=0 ?}
?};
EDOKUMD.KWOTA_W:=_dieta;
EDOKUMD.DNI:=_dni;
exec('kwota_refresh','diety');
1


\rycz_noc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Zwraca kwote ryczaltu
::  OLD: \rycz_noc/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.CZY_NOC
|| {? app_info('web_sesid')<>''
   || EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix()
   ?};
   exec('czytaj','#stalesys',{? EDOKUMD.DATA_OD<>date(0,0,0) || EDOKUMD.DATA_OD || EDOKUMD.EDOKUM().DOP ?},XINFO);
   {? EDOKUMD.EDOKUM().TYP().DEL_ZAGR & EDOKUMD.KRAJ<>null & EDOKUMD.KRAJ().SYM<>'PL'
   || _rycz_noc:=exec('stawki_zagr','diety',EDOKUMD.KRAJ().SYM,EDOKUMD.EDOKUM().DATA_OD,'N')*(XINFO.PRC_NOCL/100)
   |? EDOKUMD.EDOKUM().TYP().DEL_ZAGR=0 | EDOKUMD.KRAJ().SYM='PL'
   || _rycz_noc:=XINFO.DIETA*(XINFO.RYCZ_NOC/100)
   || _rycz_noc:=0
   ?}
|| _rycz_noc:=0
?};
EDOKUMD.LICZ_NOC*_rycz_noc


\rycz_doj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Wylicza ryczalt za dojazd
::  OLD: \rycz_doj/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.CZY_DOJ
|| {? app_info('web_sesid')<>''
   || EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix()
   ?};
   exec('czytaj','#stalesys',{? EDOKUMD.DATA_OD<>date(0,0,0) || EDOKUMD.DATA_OD || EDOKUMD.EDOKUM().DOP ?},XINFO);
   {? EDOKUMD.EDOKUM().TYP().DEL_ZAGR & EDOKUMD.KRAJ<>null & EDOKUMD.KRAJ().SYM<>'PL'
   || _stawka:=exec('stawki_zagr','diety',EDOKUMD.KRAJ().SYM,EDOKUMD.EDOKUM().DATA_OD,'D');
      _rycz_doj:=(XINFO.ZRYCZKOM/100)*_stawka
   |? EDOKUMD.EDOKUM().TYP().DEL_ZAGR=0 | EDOKUMD.KRAJ().SYM='PL'
   || _rycz_doj:=XINFO.DIETA*(XINFO.RYCZ_KOM/100)
   || _rycz_doj:=0
   ?}
|| _rycz_doj:=0
?};
EDOKUMD.LICZ_DOJ*_rycz_doj


\ae_diet_l_port
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.L_PORT
::  OLD: \ae_diet_l_port/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix()
?};
EDOKUMD.KWOTA_L:=EDOKUMD.L_PORT*0.5*exec('stawki_zagr','diety',EDOKUMD.KRAJ().SYM,EDOKUMD.EDOKUM().DATA_OD,'D');
exec('kwota_refresh','diety');
1


\godz_szac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PJ] [12.41]
:: OPIS: Szacowanie liczby godzin dla diety z czasem trwania wprowadzanym
::       jako liczba godzin
::  OLD: \godz_szac/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_ref_d:={? -menu_txt()='dołącz' || null || EDOKUMD.ref() ?};
_tab:=exec('u_godz_szac','diety',EDOKUMD.EDOKUM, _ref_d, EDOKUMD.KRAJ().SYM, EDOKUMD.DATA_OD, EDOKUMD.GODZ_OD, EDOKUMD.DATA_DO, EDOKUMD.GODZ_DO);
EDOKUMD.CZAS_GDZ:=_tab[1];
EDOKUMD.CZAS_MIN:=_tab[2];
1


\u_godz_szac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PJ] [12.41]
:: OPIS: Szacowanie liczby godzin dla diety z czasem trwania wprowadzanym
::       jako liczba godzin
::   WE: _a - EDOKUM.ref()
::       _b - jesli popraw to EDOKUMD.ref(), jeżeli dołącz to null
::       _c - symbol kraju
::       _d - data od z okna diety
::       _e - godz od z okna diety
::       _f - data do z okna diety
::       _g - godz do z okna diety
::   WY: tablica 2 wartościowa z liczbą godzin i liczbą minut
::  OLD: \u_godz_szac/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
:: tablica na wynik
_tab:=obj_new(2);
_tab[1]:=0;
_tab[2]:=0;

_tryb:='';
_CZAS_G:=_CZAS_M:=0;
{? _>0 || EDOKUM.use(ref_name(_a)) ?};
EDOKUM.index('ID'); EDOKUM.prefix();
{? _>0 & EDOKUM.seek(_a)
||
:: liczba godzin wynikajaca z naglowka delegacji
   {? EDOKUM.DATA_OD<>date(0,0,0) & EDOKUM.DATA_DO<>date(0,0,0)
   || _tab_g:=exec('licz_godz','diety', EDOKUM.DATA_OD, EDOKUM.GODZ_OD, EDOKUM.DATA_DO, EDOKUM.GODZ_DO)
   ?};
:: liczba godzin z przekazanych dat i godzin z okna diety
   {? _>=7 & _d<>date(0,0,0) & _e<>time(0,0,0) & _f<>date(0,0,0) & _g<>time(0,0,0)
   || _tab_d:=exec('licz_godz','diety', _d, _e, _f, _g)
   ?};
   {? _b<>null
   || _tryb:='Popraw'
   |? _b=null
   || _tryb:='Dołącz'
   ?};

:: zlicza diety do biezacej delegacji
   _pl:=_npl:=0;
   EDOKUMD.cntx_psh();
   EDOKUMD.index('DELEG');
   EDOKUMD.prefix(EDOKUM.ref());
   {? EDOKUMD.first()
   || {! |?
         {? EDOKUMD.KRAJ().SYM='PL' || _pl+=1  || _npl+=1 ?};
         EDOKUMD.next()
      !}
   ?};
   EDOKUMD.cntx_pop();

:: jezeli krajowa
   {? EDOKUM.TYP().DEL_ZAGR=0
   || {? var_pres('_tab_d')>0
      || _CZAS_G:=_tab_d[1];
         _CZAS_M:=_tab_d[2]
      |? var_pres('_tab_g')>0
      || _CZAS_G:=_tab_g[1];
         _CZAS_M:=_tab_g[2]
      ?}
   ||
:: jezeli delegacja zagraniczna to liczy czas pobytu zagranica
      _tab_z:=exec('licz_godz','diety', EDOKUM.GRAN_DAT, EDOKUM.GRAN_GDZ, EDOKUM.GRAN_DPW, EDOKUM.GRAN_GPW);
:: jezeli dieta na czesc krajowa
      {? _c='PL'
      || _godz:=_tab_g[1]-_tab_z[1];
         _min:=_tab_g[2]-_tab_z[2];
         {? _min<0 || _min:=60+_min; _godz-=1 ?};
         _CZAS_G:=_godz;
         _CZAS_M:=_min
:: jezei dieta na czesc zagraniczna
      || {? var_pres('_tab_d')>0
         || _CZAS_G:=_tab_d[1];
            _CZAS_M:=_tab_d[2]
         |? var_pres('_tab_g')>0
         || _CZAS_G:=_tab_g[1];
            _CZAS_M:=_tab_g[2]
         ?}
      ?}
   ?}
?};
:: jezeli liczba minut = 60 to zamiana na godzine
{? _CZAS_M=60
|| _CZAS_G+=1;
   _CZAS_M:=0
?};
:: jezeli podano bledny zakres i liczba godzin, minut wyszla ujemna
{? _CZAS_G<0 || _CZAS_G:=_CZAS_M:=0 ?};
{? _CZAS_M<0 || _CZAS_M:=0 ?};
:: zapis do zwracanej tablicy
_tab[1]:=_CZAS_G;
_tab[2]:=_CZAS_M;
_tab


\wylicz_nocleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula szacuje liczbe noclegow lub ryczaltow za komunikacje
::   WE: _a - jesli _a=1 - wylicza liczbe ryczaltow za komunikacje
::  OLD: \wylicz_nocleg/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_znacznik:={? _>0 & _a=1 & EDOKUMD.CZY_DOJ || 1
           |? (_=0 | _a=0) & EDOKUMD.CZY_NOC || 1
           || 0
           ?};
{? _znacznik & (_=0 | _a=0)
|| _licz_noc:=exec('licz_noc','diety',EDOKUMD.DATA_OD, EDOKUMD.DATA_DO, EDOKUMD.GODZ_OD, EDOKUMD.GODZ_DO, EDOKUMD.Z_CZY_G, EDOKUMD.CZAS_GDZ, EDOKUMD.CZAS_MIN, EDOKUMD.KRAJ().SYM)
|? _znacznik & (_>0 & _a=1)
|| _licz_noc:=exec('licz_kom','diety',EDOKUMD.DATA_OD, EDOKUMD.DATA_DO, EDOKUMD.GODZ_OD, EDOKUMD.GODZ_DO, EDOKUMD.Z_CZY_G, EDOKUMD.CZAS_GDZ, EDOKUMD.CZAS_MIN)
|| _licz_noc:=0
?};
_licz_noc


\kwota_refresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Odswiezenie kwoty w polu OBIEGI.DIETKWOT
::  OLD: \kwota_refresh/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.DIETKWOT:=EDOKUMD.KWOTA_W+EDOKUMD.RYCZ_NOC+EDOKUMD.RYCZ_DOJ+EDOKUMD.KWOTA_L


\licz_godz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: oblicza liczbe godzin z podanego zakresu
::   WE: _a - data od
::       _b - godz od
::       _c - data do
::       _d - godz do
::   WY: tablica 2 elementowa: liczba godzin, liczba minut
::  OLD: \licz_godz/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new(2);
_tab[1]:=0;
_tab[2]:=0;
_tmp_dni:=_c-_a;
_godz:=_tmp_dni*24+((_d~1)-(_b~1));
_tmp_min:=((_d~2)-(_b~2));
_tmp_sec:=((_d~3)-(_b~3));
_godz+=(_tmp_min/60)+(_tmp_sec/60/60);
_tab[1]:=floor(_godz);
_tab[2]:=((_godz-floor(_godz))*60)$0;
_tab


\licz_kom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula szacuje liczbe ryczaltow za komunikacje miejska,
::       ryczalt nalezy sie za kazda rozpoczeta dobe
::   WE: _a = EDOKUMD.DATA_OD, poczatek diety
::       _b = EDOKUMD.DATA_DO, koniec diety
::       _c = EDOKUMD.GODZ_OD, godzina poczatkowa
::       _d = EDOKUMD.GODZ_DO, godzina koncowa
::       _e = EDOKUMD.Z_CZY_G, zakres dat Z czy liczba godzin G
::       _f = EDOKUMD.CZAS_GDZ, liczba godzin
::       _g = EDOKUMD.CZAS_MIN, liczba minut
::  OLD: \licz_kom/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ((_a<>date(0,0,0) & _b<>date(0,0,0) & _a<=_b) | (_e='G' & _f>0))
|| {? _e='Z'
   || _tmp_dni:=_b-_a;
      _godz:=_tmp_dni*24+((_d~1)-(_c~1))+(((_d~2)-(_c~2))/60)+((((_d~3)-(_c~3))/60)/60)
   |? _e='G'
   || _godz:=_f+(_g/60)
   ?};
   _licz_noc:=ceil(_godz/24)
|| _licz_noc:=0
?};
_licz_noc


\licz_noc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula szacuje liczbe noclegow
::   WE: _a = EDOKUMD.DATA_OD, poczatek diety
::       _b = EDOKUMD.DATA_DO, koniec diety
::       _c = EDOKUMD.GODZ_OD, godzina poczatkowa
::       _d = EDOKUMD.GODZ_DO, godzina koncowa
::       _e = EDOKUMD.Z_CZY_G, zakres dat Z czy liczba godzin G
::       _f = EDOKUMD.CZAS_GDZ, liczba godzin
::       _g = EDOKUMD.CZAS_MIN, liczba minut
::       _h = EDOKUMD.KRAJ, symbol kraju diety
::  OLD: \licz_noc/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ((_a<>date(0,0,0) & _b<>date(0,0,0) & (_a<_b | (_a=_b & _c<_d))) | (_e='G' & _f>0))
|| {? _e='Z'
   || _tmp_dni:=_b-_a;
      _godz:=_tmp_dni*24+((_d~1)-(_c~1))+(((_d~2)-(_c~2))/60)+((((_d~3)-(_c~3))/60)/60)
   |? _e='G'
   || _godz:=_f+(_g/60)
   ?};
   {? _godz<24
   || _licz_noc:=0
   |? _godz>=24
   || _licz_noc:=ceil(_godz/24)
   ?};
:: dla delegacji krajowych weryfikacja czy spelnia warunek z rozporzadzenia:
:: 4. Ryczałt za nocleg przysługuje, jeżeli nocleg trwa co najmniej 6 godzin pomiędzy godzinami 21 i 7.
   {? _godz>=6 & (_h='PL' | _h='') & _e='Z'
   || _od_d:=_a;
      _do_d:=_b;
      _od_g:=_start:=_c;
      _do_g:=_d;
      _zlicz:=_nocleg:=0;
      {? _do_g>=time(24,0,0)
      || _do_g:=_do_g-time(24,0,0);
         _do_d+=1
      ?};
      {! |?
            _od_g+=60;
            {? _od_g>=time(24,0,0) & ~(_od_d>=_do_d & _od_g>=_do_g)
            || _od_g:=_od_g-time(24,0,0);
               _od_d+=1
            ?};
            {? _od_g>=time(22,0,0) | _od_g<time(8,0,0)
            || _zlicz+=1
            |? _start<time(21,0,0) & _start>time(7,0,0) & (_start~2>0 | _start~3>0) & _zlicz=0 & _od_g>time(21,0,0) & _od_g<time(22,0,0)
            || _zlicz+=((_start~2)/60)+(_start~3/60/60)
            || {? _start<time(21,0,0) & _start>time(7,0,0) & (_start~2>0 | _start~3>0)
               || _zlicz+=((_start~2)/60)+(_start~3/60/60)
               |? _start>time(21,0,0) | _start<time(7,0,0) & (_start~2>0 | _start~3>0)
               || _zlicz-=((_start~2)/60)+(_start~3/60/60)
               ?};
               {? _zlicz>=6 || _nocleg+=1 ?};
               _zlicz:=0; _start:=_od_g
            ?};
            ~(_od_d>=_do_d & _od_g>=_do_g)
      !};
      {? _od_g>_do_g
      || _zlicz-=((_od_g-_do_g)~1)+(((_od_g-_do_g)~2)/60)+(((_od_g-_do_g)~3)/60/60)
      ?};
      {? _zlicz>=6 || _nocleg+=1 ?};
      {? _licz_noc<>_nocleg || _licz_noc:=_nocleg ?}
   ?}
|| _licz_noc:=0
?};
_licz_noc


\bl_diet_data_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Wartosc poczatkowa pola EDOKUMD.DATA_OD
::  OLD: \bl_diet_data_od/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' || EDOKUM.web_cntx_load(1) ?};
EDOKUM.DATA_OD


\ae_diet_godz_do
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.GODZ_DO
::  OLD: \ae_diet_godz_do/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.GODZ_OD>time(24,00,00) | EDOKUMD.GODZ_DO>time(24,00,00)
|| FUN.emsg('Podany czas nie może być większy od 24:00:00.'@);
   return(0)
?};
EDOKUMD.LICZ_NOC:=exec('wylicz_nocleg','diety');
EDOKUMD.LICZ_DOJ:=exec('wylicz_nocleg','diety',1);
EDOKUMD.RYCZ_NOC:=exec('rycz_noc','diety');
EDOKUMD.RYCZ_DOJ:=exec('rycz_doj','diety');
{? EDOKUMD.KRAJ().SYM<>'PL' &  EDOKUMD.KRAJ().SYM<>''
|| EDOKUMD.WAL:=exec('wal_diet_zagr','diety', EDOKUMD.KRAJ().SYM, EDOKUMD.DATA_OD)
|? EDOKUMD.KRAJ().SYM=''
|| EDOKUMD.WAL:=null
?};
{? EDOKUMD.CZY_DIET
|| _ile:=exec('wylicz_sniad','diety');
   {? EDOKUMD.SNIAD || EDOKUMD.LICZ_SNI:=_ile || EDOKUMD.LICZ_SNI:=0 ?};
   {? EDOKUMD.OBIAD || EDOKUMD.LICZ_OBI:=_ile || EDOKUMD.LICZ_OBI:=0 ?};
   {? EDOKUMD.KOLACJA || EDOKUMD.LICZ_KOL:=_ile || EDOKUMD.LICZ_KOL:=0 ?}
|| EDOKUMD.LICZ_SNI:=EDOKUMD.LICZ_OBI:=EDOKUMD.LICZ_KOL:=0
?};
{? app_info('web_sesid')<>''
|| exec('ustaw_pola_diety','diety')
|| exec('ustaw_pola_diety_jt','diety')
?};
exec('ae_diet_l_port','diety');
exec('przelicz_diet','diety')


\wylicz_sniad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula szacuje liczbe posilkow
::  OLD: \wylicz_sniad/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_dni:=0;
{? EDOKUMD.Z_CZY_G='Z'
|| _tmp_dni:=EDOKUMD.DATA_DO-EDOKUMD.DATA_OD;
   _godz:=_tmp_dni*24+((EDOKUMD.GODZ_DO~1)-(EDOKUMD.GODZ_OD~1));
   _tmp_min:=((EDOKUMD.GODZ_DO~2)-(EDOKUMD.GODZ_OD~2));
   _godz+=(_tmp_min/60)$2
|? EDOKUMD.Z_CZY_G='G'
|| _godz:=EDOKUMD.CZAS_GDZ+(EDOKUMD.CZAS_MIN/60)
?};
{? EDOKUMD.KRAJ().SYM='PL'
|| {? _godz<8 || _dni:=0
   |? _godz>=8 & _godz<=12 || _dni:=0.5
   |? _godz>12 & _godz<=24 || _dni:=1
   |? _godz>24
   || _dni:=_godz%24;
      _tmp_g:=_godz-(_dni*24);
      {? _tmp_g>0
      || {? _tmp_g<=8 || _dni+=0.5 || _dni+=1 ?}
      ?}
   ?}
|| {? EDOKUMD.KRAJ<>null & EDOKUMD.KRAJ().SYM<>'PL'
   || {? _godz<=8             || _dni:=(1/3)
      |? _godz>8 & _godz<=12  || _dni:=0.5
      |? _godz>12 & _godz<=24 || _dni:=1
      |? _godz>24
      || _dni:=_godz%24;
          _tmp_g:=_godz-(_dni*24);
          {? _tmp_g>0
          || {? _tmp_g<=8 || _dni+=(1/3) |? _tmp_g<=12 || _dni+=0.5 || _dni+=1 ?}
          ?}
       ?}
   ?}
?};
ceil(_dni)


\bl_diet_data_do
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Wartosc poczatkowa pola EDOKUMD.DATA_DO
::  OLD: \bl_diet_data_do/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' || EDOKUM.web_cntx_load(1) ?};
EDOKUM.DATA_DO


\ae_licz_noc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.LICZ_NOC
::  OLD: \ae_licz_noc/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.LICZ_NOC<=0
|| FUN.emsg('Liczba noclegów musi być większa od zera.'@); 0
|| {? EDOKUMD.CZY_NOC
   || EDOKUMD.RYCZ_NOC:=exec('rycz_noc','diety');
      exec('kwota_refresh','diety')
   ?};
   1
?}


\ae_rycz_noc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.RYCZ_NOC
::  OLD: \ae_rycz_noc/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
exec('kwota_refresh','diety');
1


\ae_licz_doj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.LICZ_DOJ
::  OLD: \ae_licz_doj/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.LICZ_DOJ<=0
|| FUN.emsg('Liczba dni musi być większa od zera.'@); 0
|| {? EDOKUMD.CZY_DOJ
   || EDOKUMD.RYCZ_DOJ:=exec('rycz_doj','diety');
      exec('kwota_refresh','diety')
   ?};
   1
?}


\be_innedane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed redakcja pola INNEDANE tabeli EDOKUMD
::  OLD: \be_innedane/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO);
{? XINFO.SLU_INNY<>null
|| SLU.prefix();
   SLU.seek(XINFO.SLU_INNY)
|| 0
?}


\bl_diet_godz_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Wartosc poczatkowa pola EDOKUMD.GODZ_OD
::  OLD: \bl_diet_godz_od/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' || EDOKUM.web_cntx_load(1) ?};
EDOKUM.GODZ_OD


\bl_diet_godz_do
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Wartosc poczatkowa pola EDOKUMD.GODZ_DO
::  OLD: \bl_diet_godz_do/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' || EDOKUM.web_cntx_load(1) ?};
EDOKUM.GODZ_DO


\ae_licz_sni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.LICZ_SNI
::  OLD: \ae_licz_sni/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_dni:={? EDOKUMD.DNI$0=0 || 1 || ceil(EDOKUMD.DNI) ?};
{? EDOKUMD.LICZ_SNI<0
|| FUN.emsg('Liczba pomniejszeń diety za śniadania nie może być ujemna.'@);
   0
|? _dni<EDOKUMD.LICZ_SNI
|| FUN.emsg('Liczba pomniejszeń diety za śniadania nie może być większa niż liczba dni delegacji (%1).'@[$(_dni)]);
   0
|| exec('przelicz_diet','diety');
   1
?}

\ae_w_sni
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.W_SNI
::  OLD: \ae_w_sni/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.W_SNI=1
|| EDOKUMD.LICZ_SNI:={? EDOKUMD.DNI$0=0 || 1 || EDOKUMD.DNI$0 ?};
   {? app_info('web_sesid')<>''
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_SNI')
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_SNI')
   ?}
|| {? app_info('web_sesid')<>''
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_SNI')
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'LICZ_SNI')
   ?}
?};
exec('przelicz_diet','diety');
1


\ae_w_obi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.W_OBI
::  OLD: \ae_w_obi/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.W_OBI=1
|| EDOKUMD.LICZ_OBI:={? EDOKUMD.DNI$0=0 || 1 || EDOKUMD.DNI$0 ?};
   {? app_info('web_sesid')<>''
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_OBI')
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_OBI')
   ?}
|| {? app_info('web_sesid')<>''
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_OBI')
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'LICZ_OBI')
   ?}
?};
exec('przelicz_diet','diety');
1


\ae_w_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed wyswietleniem pola EDOKUMD.W_SNI
::  OLD: \ae_w_kol/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.W_KOL=1
|| EDOKUMD.LICZ_KOL:={? EDOKUMD.DNI$0=0 || 1 || EDOKUMD.DNI$0 ?};
   {? app_info('web_sesid')<>''
   || EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=0',EDOKUMD,'LICZ_KOL')
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_KOL')
   ?}
|? app_info('web_sesid')<>''
|| EDOKUMD.web_efld_opt('RED_WW',,'mark=0, enable=1, editable=1',EDOKUMD,'LICZ_KOL')
|| EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'LICZ_KOL')
?};
exec('przelicz_diet','diety');
1


\czas_gdz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.41]
:: OPIS: Po redakcji pola EDOKUMD.CZAS_GDZ
::  OLD: \czas_gdz_a/delegdan.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.CZAS_GDZ<0
|| FUN.emsg('Liczba godzin nie może być mniejsza od zera.'@);
   0
|| exec('przelicz_all','diety');
   1
?}


\czas_min_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.41]
:: OPIS: Po redakcji pola EDOKUMD.CZAS_MIN
::  OLD: \czas_min_a/delegdan.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.CZAS_MIN<0
|| FUN.emsg('Liczba minut nie może być mniejsza od zera.'@);
   0
|? EDOKUMD.CZAS_MIN>59
|| FUN.emsg('Liczba minut nie może być większa od 59.'@);
   0
|| exec('przelicz_all','diety');
   1
?}


\przelicz_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.41]
:: OPIS: Przeliczanie diet po redakcji pól z godziną i minuta
::  OLD: \przelicz_all/delegdan.fml
::----------------------------------------------------------------------------------------------------------------------
exec('przelicz_diet','diety');
EDOKUMD.LICZ_NOC:=exec('wylicz_nocleg','diety');
EDOKUMD.RYCZ_NOC:=exec('rycz_noc','diety');
EDOKUMD.LICZ_DOJ:=exec('wylicz_nocleg','diety',1);
EDOKUMD.RYCZ_DOJ:=exec('rycz_doj','diety');
exec('kwota_refresh','diety');
1


\chk_dieta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Weryfikacja danych w okienku redagowania diety
::   WE:  _a  - typ sprawdzania: 'ADD' lub 'PUT'
::       [_b] - webservice
::  OLD: \chk_dieta/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:='';
{? var_press('_b')<=0 || _b:=0 ?};
{? _b=1
|| {? 'ZG'*EDOKUMD.Z_CZY_G=0
   || FUN.emsg('Niedozwolona zawartość pola %1.\nDopuszczalne wartości to: %2'@['Z_CZY_G','Z lub G']);
      return('Z_CZY_G')
   |? EDOKUMD.CZAS_GDZ<0
   || FUN.emsg('Liczba godzin nie może być mniejsza od zera.'@);
      return('CZAS_GDZ')
   |? EDOKUMD.CZAS_MIN<0
   || FUN.emsg('Liczba minut nie może być mniejsza od zera.'@);
      return('CZAS_MIN')
   |? EDOKUMD.CZAS_MIN>59
   || FUN.emsg('Liczba minut nie może być większa od 59.'@);
      return('CZAS_MIN')
   ?}
|| EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMD.EDOKUM()
?};
{? EDOKUMD.Z_CZY_G='Z'
|| {? EDOKUMD.DATA_OD=date(0,0,0)
   || FUN.emsg('Należy podać datę OD.'@);
      _dalej:='DATA_OD'
   ?};
   {? _dalej=''
   || {? EDOKUMD.DATA_DO=date(0,0,0)
      || FUN.emsg('Należy podać datę DO.'@);
         _dalej:='DATA_DO'
      ?}
   ?};
   {? _dalej=''
   || {? EDOKUMD.DATA_OD>EDOKUMD.DATA_DO
      || FUN.emsg('Data OD nie może być późniejsza od daty DO.'@);
         _dalej:='DATA_OD'
      ?}
   ?};
   {? _dalej='' & EDOKUMD.EDOKUM().DATA_OD<>date(0,0,0) & EDOKUMD.DATA_OD<EDOKUMD.EDOKUM().DATA_OD
   || FUN.emsg('Data OD nie może być wcześniejsza niż data początkowa delegacji (%1).'@[$EDOKUMD.EDOKUM().DATA_OD]);
      _dalej:='DATA_OD'
   ?};
   {? _dalej='' & EDOKUMD.EDOKUM().DATA_DO<>date(0,0,0) & EDOKUMD.DATA_DO>EDOKUMD.EDOKUM().DATA_DO
   || FUN.emsg('Data DO nie może być późniejsza niż data końcowa delegacji (%1).'@[$EDOKUMD.EDOKUM().DATA_DO]);
      _dalej:='DATA_DO'
   ?};
   {? _dalej=''
   || _wyn:=1;
      _do:=EDOKUMD.DATA_OD;
      _dd:=EDOKUMD.DATA_DO;
      _go:=EDOKUMD.GODZ_OD;
      _gd:=EDOKUMD.GODZ_DO;
      _er:=EDOKUMD.ref();
      EDOKUMD.cntx_psh();
      EDOKUMD.index('DELEG');
      EDOKUMD.prefix(EDOKUM.ref());
      {? EDOKUMD.first()
      || {! |? {? _a='ADD' | EDOKUMD.ref()<>_er
               || _wyn:=((_do<EDOKUMD.DATA_OD | (_do=EDOKUMD.DATA_OD & _go<EDOKUMD.GODZ_OD))
                       & (_dd<EDOKUMD.DATA_OD | (_dd=EDOKUMD.DATA_OD & _gd<=EDOKUMD.GODZ_OD)))
                       | ((_do>EDOKUMD.DATA_DO | (_do=EDOKUMD.DATA_DO & _go>=EDOKUMD.GODZ_DO))
                       & (_dd>EDOKUMD.DATA_DO | (_dd=EDOKUMD.DATA_DO & _gd>EDOKUMD.GODZ_DO)));
                  _wyn & EDOKUMD.next()
               || EDOKUMD.next()
               ?}
         !}
      ?};
      EDOKUMD.cntx_pop();
      {? ~_wyn
      || FUN.emsg('Wprowadzone daty pokrywają się z terminami innej diety.'@);
        _dalej:='DATA_OD'
      ?}
   ?}
|| {? EDOKUMD.CZAS_GDZ=0 & EDOKUMD.CZAS_MIN=0
   || FUN.emsg('Liczby minut i godzin nie mogą być jednocześnie równe zero.'@);
      _dalej:='CZAS_MIN'
   ?}
?};
{? _dalej='' & EDOKUMD.CZY_DIET & EDOKUMD.SNIAD & ceil(EDOKUMD.DNI)<EDOKUMD.LICZ_SNI
|| FUN.emsg('Liczba pomniejszeń diety za śniadania nie może być większa niż liczba dni delegacji (%1).'@[$EDOKUMD.DNI]);
   _dalej:='LICZ_SNI'
?};
{? _dalej='' & EDOKUMD.CZY_DIET & EDOKUMD.OBIAD & ceil(EDOKUMD.DNI)<EDOKUMD.LICZ_OBI
|| FUN.emsg('Liczba pomniejszeń diety za obiady nie może być większa niż liczba dni delegacji (%1).'@[$EDOKUMD.DNI]);
   _dalej:='LICZ_OBI'
?};
{? _dalej='' & EDOKUMD.CZY_DIET & EDOKUMD.KOLACJA & ceil(EDOKUMD.DNI)<EDOKUMD.LICZ_KOL
|| FUN.emsg('Liczba pomniejszeń diety za kolacje nie może być większa niż liczba dni delegacji (%1).'@[$EDOKUMD.DNI]);
   _dalej:='LICZ_KOL'
?};
{? _dalej='' & EDOKUMD.CZY_NOC & EDOKUMD.LICZ_NOC<=0
|| FUN.emsg('Liczba noclegów musi być większa od zera.'@); _dalej:='LICZ_NOC'
?};
{? _dalej='' & EDOKUMD.CZY_DOJ & EDOKUMD.LICZ_DOJ<=0
|| FUN.emsg('Liczba dni musi być większa od zera.'@); _dalej:='LICZ_DOJ'
?};
_dalej


\rp_edokumd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.DIETKWOT:=EDOKUMD.KWOTA_W+EDOKUMD.RYCZ_NOC+EDOKUMD.RYCZ_DOJ+EDOKUMD.KWOTA_L; 0


\bobs_red_edokumd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą okienka redagowania tabeli EDOKUMD
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys');
1


\ae_licz_obi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.LICZ_OBI
::  OLD: \ae_licz_obi/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_dni:={? EDOKUMD.DNI$0=0 || 1 || ceil(EDOKUMD.DNI) ?};
{? EDOKUMD.LICZ_OBI<0
|| FUN.emsg('Liczba pomniejszeń diety za obiady nie może być ujemna.'@); 0
|? _dni<EDOKUMD.LICZ_OBI
|| FUN.emsg('Liczba pomniejszeń diety za obiady nie może być większa niż liczba dni delegacji (%1).'@[$(_dni)]);
   0
|| exec('przelicz_diet','diety');
   1
?}


\ae_licz_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola EDOKUMD.LICZ_KOL
::  OLD: \ae_licz_kol/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_dni:={? EDOKUMD.DNI$0=0 || 1 || ceil(EDOKUMD.DNI) ?};
{? EDOKUMD.LICZ_KOL<0
|| FUN.emsg('Liczba pomniejszeń diety za kolacje nie może być ujemna.'@); 0
|? _dni<EDOKUMD.LICZ_KOL
|| FUN.emsg('Liczba pomniejszeń diety za kolacje nie może być większa niż liczba dni delegacji (%1).'@[$(_dni)]);
   0
|| exec('przelicz_diet','diety');
   1
?}


\be_win_edokumd_jt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed oknem redagowania RED diety EDOKUMD, ustawia dostępność pól
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.use('skid_v'+(EDOKUMD.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix();
{? EDOKUMD.EDOKUM().TYP().DEL_ZAGR=0
|| EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=0',EDOKUMD,'KRAJ','NAZ');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=0',EDOKUMD,'WAL','SYM')
|| EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'KRAJ','NAZ');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'WAL','SYM')
?};
{? EDOKUMD.Z_CZY_G='G'
|| EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'DATA_OD');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'DATA_DO');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'GODZ_OD');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'GODZ_DO');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'L_G');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'CZAS_GDZ');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'CZAS_MIN')
|| EDOKUMD.efld_opt('RED','mark=1, enable=1, editable=1',EDOKUMD,'DATA_OD');
   EDOKUMD.efld_opt('RED','mark=1, enable=1, editable=1',EDOKUMD,'DATA_DO');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'GODZ_OD');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'GODZ_DO');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'L_G');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'CZAS_GDZ');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'CZAS_MIN')
?};
exec('czytaj','#stalesys',,XINFO);
{? EDOKUMD.CZY_NOC
|| EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'RYCZ_NOC');
   EDOKUMD.efld_opt('RED','mark=1, enable=1, editable=1',EDOKUMD,'LICZ_NOC')
|| EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'RYCZ_NOC');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_NOC')
?};
{? EDOKUMD.CZY_DOJ
|| EDOKUMD.efld_opt('RED','mark=1, enable=1, editable=1',EDOKUMD,'LICZ_DOJ');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'RYCZ_DOJ')
|| EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_DOJ');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'RYCZ_DOJ')
?};
{? ~EDOKUMD.CZY_PORT
|| EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'L_PORT');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'KWOTA_L')
|| EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'L_PORT');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'KWOTA_L')
?};
{? (EDOKUMD.EDOKUM().TYP().DEL_ZAGR=0 & EDOKUMD.KRAJ().SYM='PL')
   | (EDOKUMD.EDOKUM().TYP().DEL_ZAGR>0 & EDOKUMD.KRAJ().SYM='PL')
|| EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'CZY_PORT')
|| EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'CZY_PORT')
?};
exec('ustaw_pola_diety_jt','diety')


\ustaw_pola_diety_jt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Ustawia dostępność pól w sekcji diet
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMD.CZY_DIET=0
|| EDOKUMD.KWOTA_W:=0;
   EDOKUMD.SNIAD:=EDOKUMD.OBIAD:=EDOKUMD.KOLACJA:=0;
   EDOKUMD.W_SNI:=EDOKUMD.W_OBI:=EDOKUMD.W_KOL:=0;
   EDOKUMD.LICZ_SNI:=EDOKUMD.LICZ_OBI:=EDOKUMD.LICZ_KOL:=0
?};
exec('czytaj','#stalesys',,XINFO);
{? EDOKUMD.CZY_DIET
|| {? XINFO.EDI_DIET
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'KWOTA_W')
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=0',EDOKUMD,'KWOTA_W')
   ?};
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'SNIAD');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'OBIAD');
   EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'KOLACJA');
   {? EDOKUMD.SNIAD
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'W_SNI');
      {? EDOKUMD.W_SNI=1
      || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=0',EDOKUMD,'LICZ_SNI')
      || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'LICZ_SNI')
      ?}
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'W_SNI');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_SNI')
   ?};
   {? EDOKUMD.OBIAD
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'W_OBI');
      {? EDOKUMD.W_OBI=1
      || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=0',EDOKUMD,'LICZ_OBI')
      || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'LICZ_OBI')
      ?}
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'W_OBI');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_OBI')
   ?};
   {? EDOKUMD.KOLACJA
   || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'W_KOL');
      {? EDOKUMD.W_KOL=1
      || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=0',EDOKUMD,'LICZ_KOL')
      || EDOKUMD.efld_opt('RED','mark=0, enable=1, editable=1',EDOKUMD,'LICZ_KOL')
      ?}
   || EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'W_KOL');
      EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_KOL')
   ?}
|| EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'KWOTA_W');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'SNIAD');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'W_SNI');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_SNI');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'OBIAD');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'W_OBI');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_OBI');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'KOLACJA');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'W_KOL');
   EDOKUMD.efld_opt('RED','mark=0, enable=0',EDOKUMD,'LICZ_KOL')
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 063a53a65a52a8a78514b4e2aa7da4feedfd9aed2caca02b917b480bd1e3ab7f7f820757d3cf5716d9d51cdde37871e0039dc3166ad128f942069eddff1d9f770c5bece6b32dc939533036080d537801e79b23dca239a525a84f8311d8be9f464a96f85b8fd8af09571084579fafc67362466843eaba8c5d1fbee225d6b85e9a

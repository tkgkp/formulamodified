:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: pba.fml
:: Utworzony: 20.07.2017
:: Autor: RO
::======================================================================================================================
:: Zawartość: Formuły oraz obszary robocze dziedziny PBA.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Formuła inicjująca dla dziedziny PBA.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_batch:=var_pres('_a')=type_of(0) & _a;
BPMN.SYM_DOM:='PBA';
ZZ_POM.MODUL:='A';
_jTerm:=app_info('web_sesid')='';
exec('__WND','#object');

exec('init_pars','phr_tab');
exec('czytaj','#stalesys',,KST,KST_PAR,XINFO);
exec('otworz_dok','phr_tab');

{? _jTerm
|| exec('init_bufs','phr_tab');
   1
|| P.use('pracowni');
   exec('otworz_h_um','pracownik','h_um');
   H.use('_hist');
   {? app_info('app_user')<>'ankieta' || exec('p_web_cx_set','p_web',,tab_tmp(1,'P_SQL','STRING[16]',),_batch) || 1 ?}
?}


\pba_bpb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Główna formuła obszaru roboczego PBA_BPB - Badanie opinii.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','pba');

:: konfiguracja widoku okna grupowego
_cfg:=exec('pba_bpb_config','pba');
params_set('cfg',_cfg);

: badania jednorazowe
ZA_INST.cntx_psh();
ZA_INST.index('DATA');
ZA_INST.prefix(exec('ref_firma','ustawienia'));
ZA_INST.win_sel(_cfg.window);

: badania cykliczne - programy badań
ZO_PROG.cntx_psh();
ZO_PROG.index('NAZWA');
ZO_PROG.prefix(exec('ref_firma','ustawienia'),ZZ_POM.MODUL);

: badania cykliczne - sesje
ZO_PROC.cntx_psh();
ZO_PROC.index('OKRES_OD');

AreaTitle.setTabWin(ZA_INST,_cfg.window);
AreaTitle.setTitle();

ZA_INST.select();

ZO_PROC.cntx_pop();
ZO_PROG.cntx_pop();
ZA_INST.cntx_pop()


\pba_bpb_config
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Formuła tworząca interfejs użytkownika dla obszaru roboczego BPB w dziedzinie PBA.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_cfg:=obj_new('window','upr','ZA_INST','ZA_WAR','ZO_PROG','ZO_PROC');

_cfg.upr:=
   "  _chk:=$('exec(\\\'chk_role\\\',\\\'#b__box\\\',OPERATOR.USER,_a)');
      _chk('PBA_BPB_DRLP') | _chk('PBA_BPB_PLPA') | _chk('PBA_BPB_DLPR') | _chk('PBA_BPB_PLPR')
   ";

_cfg.ZO_PROG:=obj_new('ws');
_cfg.ZO_PROG.ws:='WERA';

_cfg.ZO_PROC:=obj_new('ws');
_cfg.ZO_PROC.ws:='ANK';

_cfg.ZA_INST:=obj_new('ws');
_cfg.ZA_INST.ws:='WER';

_cfg.ZA_WAR:=obj_new('ws');
_cfg.ZA_WAR.ws:='WER';
_mode:='maximized';
: grupowe okienko modułu badań opinii
_wnd:=ZA_INST.grp_make(,
   "  ZA_WAR.index('OPIS');
      params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_RLBA') |
         exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_PLBA')
      || tab_show(1,'')
      || tab_hide(1,,'')
      ?};
      {? _cfg.upr()
      || tab_show(2,'')
      || tab_hide(2,,'')
      ?}
   ",
   'pba_bpb',,,"exec('exit','zws')",
);
:  badania jednorazowe
ZA_INST.grp_sel(_wnd,ZA_INST,_cfg.ZA_INST.ws,'Badania jednorazowe'@,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _ag:='';
      _kod:=ZA_INST.STATUS().KOD;
      {? _kod='Z'
      || _ag:='pZ(Z)'
      || _ag:='Z(A)'
      ?};
      _ag+='W';
      ZA_INST.actions_grayed(_cfg.ZA_INST.ws,_ag);
      grp_disp(ZA_WAR,_cfg.ZA_WAR.ws)
   ",,,
   15,,,,,
   _mode
);
ZA_INST.tab_splt(_wnd,,'horizontal','bottom');
ZA_INST.grp_sel(_wnd,ZA_WAR,_cfg.ZA_WAR.ws,,,,,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      {? grp_empty(ZA_INST,_cfg.ZA_INST.ws) | ~exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_PWAN')
      || '#disable'
      || ZA_WAR.prefix(ZA_INST.NP_DOK,ZA_INST.ZZ_DOK)
      ?}
   ",,,,
   'maximized_with_title'
);
:  badania cykliczne
ZA_INST.grp_sel(_wnd,ZO_PROG,_cfg.ZO_PROG.ws,'Badania cykliczne'@,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      grp_disp(ZO_PROC,_cfg.ZO_PROC.ws,1)
   ",,,
   8,,,,,
   'maximized_with_title'
);
ZA_INST.tab_splt(_wnd,,'horizontal','middle');
ZA_INST.grp_sel(_wnd,ZO_PROC,_cfg.ZO_PROC.ws,,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _ag:='';
      {? ZO_PROC.Z='T'
      || _ag:='PZ'
      |? ZO_PROC.A='T'
      || _ag+='PJ'
      |? ZO_PROC.A='N' & ZO_PROC.Z='N'
      || _ag:='Z'
      ?};
      _ag+='W';
      ZO_PROC.actions_grayed(_cfg.ZO_PROC.ws,_ag);
      grp_disp(ZA_WAR,_cfg.ZA_WAR.ws)
   ",,,
   8,
   "  params_set(_par:=params_get());
      _cfg:=_par.cfg;
      _upr:=(exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_DLPR') |
             exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_PLPR'));
      {? grp_empty(ZO_PROG,_cfg.ZO_PROG.ws) | ~_upr
      || '#disable'
      || ZO_PROC.prefix(ZO_PROG.ref)
      ?}
   ",,,,
   'maximized_with_title'
);
:  sumaryczne wyniki badań
ZA_INST.tab_splt(_wnd,,'horizontal','bottom',',66%');
ZA_INST.grp_sel(_wnd,ZA_WAR,_cfg.ZA_WAR.ws,,,,,,
    "  params_set(_par:=params_get());
       _cfg:=_par.cfg;
       {? grp_empty(ZO_PROC,_cfg.ZO_PROC.ws) | ~exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_PWAN')
       || '#disable'
       || ZA_WAR.prefix(ZO_PROC.NP_DOK,ZO_PROC.ZZ_DOK)
       ?}
    ",,,,
    'maximized_with_title'
);

_cfg.window:=_wnd;
_cfg


\za_inst_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Okienko - przed" dla okien wertowania tabeli ZA_INST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_psh','phr_widok','PBA_BPB_RLBA');
ZA_INST.win_edit('RED');
ZA_INST.win_patt('WZO');
exec('setup_icon','phr_widok','NAZWA');
1


\za_inst_ao
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Okienko - po" dla okien wertowania tabeli ZA_INST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zz_pom_pop','phr_widok')


\za_zest_def_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Formuła akcji 'Definicja ankiet' - uruchamia czynność PBA_BPB_DLAN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_DLAN')
|| exec('np_run','#b__box','PBA_BPB_DLAN')
|? exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_PLAN')
|| exec('np_run','#b__box','PBA_BPB_PLAN')
?};
~~


\za_zest_form_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Formuła akcji 'Formularze ankiet' - w zależności od uprawnień uruchamia odpowiednią czynność.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_gen:=exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_DGFA');
_form:=exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_DRFA');
_view:=exec('chk_role','#b__box',OPERATOR.USER,'PBA_BPB_PWAN');

{? _form
|| exec('np_run','#b__box','PBA_BPB_DRFA')
|? ~_form & _gen
|| exec('np_run','#b__box','PBA_BPB_DGFA')
|? ~_form & ~_gen & _view
|| exec('np_run','#b__box','PBA_BPB_PWAN')
?};
~~


\za_inst_aktywuj_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Aktywuj - przed" dla tabeli ZA_INST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZA_INST.STATUS:=exec('kod','ext_slo','ZA_INSTS','A');
_choice:=FUN.ask(
   'Czy na pewno zmienić status badania na "%1"?\n'
   'Możliwe będzie redagowanie ankiet i generowanie formularzy.'@[ZA_INST.STATUS().NAZWA]
);
_choice


\za_inst_aktywuj_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Aktywuj - po" dla tabeli ZA_INST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZA_INST.put()


\za_inst_zakoncz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Aktywuj - przed" dla tabeli ZA_INST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZA_INST.STATUS:=exec('kod','ext_slo','ZA_INSTS','Z');
_choice:=FUN.ask(
   'Czy na pewno zmienić status badania na "%1"?\n'
   'Możliwość dodawania nowych ankiet i formularzy zostanie zablokowana.\n'
   'Zakończonego badania nie można edytować.'@[ZA_INST.STATUS().NAZWA]
);
_choice


\za_inst_zakoncz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Aktywuj - po" dla tabeli ZA_INST.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZA_INST.put()


\za_inst_aw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Po "Wyświetl" okienka WER tabeli ZA_INST.
::----------------------------------------------------------------------------------------------------------------------
ZA_INST.display;
ZA_INST.win_edit('RED')


\za_inst_status_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [12.10]
:: OPIS: Wartość początkowa pola 'Status' z tabeli SLO_KOD.
::----------------------------------------------------------------------------------------------------------------------
exec('kod','ext_slo','ZA_INSTS','A')


\zo_proc_otworz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Otwórz" z obszaru PBA_BPB (akcja wywoływana z okna ANK tabeli ZO_PROC).
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='PBA_BPB_DUPR';
_params.UIDREF:=ZO_PROC.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZO_PROC',ZO_PROC.ref());

exec('mp_run','#b__box',_params)


\zo_proc_zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Obsługa akcji "Zamknij" z obszaru PBA_BPB (akcja wywoływana z okna ANK tabeli ZO_PROC).
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='PBA_BPB_RSBZ';
_params.UIDREF:=ZO_PROC.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZO_PROC',ZO_PROC.ref());

exec('mp_run','#b__box',_params)


\pba_ocena_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [17.42]
:: OPIS: Usunięcie oceny powiązanej z tabelą w obszarze badan opinii.
::   WE: _a - alias tabeli powiazanej z usuwana ocena.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZZ_OCENA.cntx_psh();
_ref:=_a.ZZ_OCENA;
_a.ZZ_OCENA:=null();
{? _a.put()
|| ZZ_OCENA.prefix();
   {? ZZ_OCENA.seek(_ref) & ZZ_OCENA.count()=0
   || {? ZZ_OCENA.del()
      || 1
      || ZZ_OCENA.cntx_pop();
         return(0)
      ?}
   ?}
?};
ZZ_OCENA.cntx_pop()

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 9b9a70f88f23935b986243ae8cadf5fa5ccae558a8b1649fd236917b9d45bb5376f6ccdb05ce988c59846c53ac8919473b0f0f470dfd288c98b40d2164e91aff46f1abf09057fe06b59f4eb5689971a8ccc12d71ff13d9afd710f6e4291d485de255656416208b64862569d908b2d557ff2343759f369d858904a244ea93ba3f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: hbn_filtr.fml
:: Utworzony: 16.03.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Obsługa filtrów dla przelewów i pozycji wyciągów
::======================================================================================================================


\save_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS: Zapisanie roku do domyslnych parametrow filtra
::   WE: _a = tryb pracy, 'EA' - archiwum przelewow, 'WA' - archiwum wyciagow
::       _b = konfiguracja: 'D' - domyslna; 'B' - biezaca
::  OLD: \save_rok/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
PFZ.cntx_psh();
PFZ.index('KONF');
PFZ.prefix(OPERATOR.USER,_a,_b);
{? ~PFZ.first()
|| PFZ.USER:=OPERATOR.USER;
   PFZ.RODZ:=_a;
   PFZ.KONF:=_b;
   PFZ.add()
?};
PFP.index('POZ');
PFP.prefix(PFZ.ref(),'TT_ROK');
{? PFP.first() || {! |? PFP.del() !} ?};
{? var_pres('TT_ROK')>0
|| TT_ROK.cntx_psh();
   exec('cr_pfp','hbn_filtr',0,PFZ.RODZ,'TT_ROK',TT_ROK,_b);
   TT_ROK.cntx_pop()
?};
PFZ.cntx_pop();
1


\par_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS: Zwraca wartosc parametru _b dla formuly SETF_PW
::  OLD: \par_b/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
'I'


\par_bn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS: Zwraca wartosc parametru _b dla formuly SETF_PWN
::  OLD: \par_b/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
'W'


\par_bna
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS: Zwraca wartosc parametru _b dla formuly SETF_PWNA
::  OLD: \par_b/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
'WA'


\par_ba
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS: Zwraca wartosc parametru _b dla formuly SETF_PWA
::  OLD: \par_b/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
'IA'


\par_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS: Zwraca wartosc parametru _b dla formuly SETF_PB
::  OLD: \par_be/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
:: uzupełnić po powstaniu czynności przeglądania archiwów: 'E' - przelewy, 'EA' - archiwa
'E'


\par_bea
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS: Zwraca wartosc parametru _b dla formuly SETF_PBA
::  OLD: \par_be/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
'EA'


\czy_upro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Formula sprawdza czy uzytkownik ma uprawnienia do jednostek ksiegowych (choc 1)
::   WY: 1 - tak; 0 - nie, brak uprawnien
::  OLD: \czy_upro/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
exec('usr_fjks_any','b_perm')


\tt_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Zeruje tabele filtrow przed pobraniem wartosci domyslnych
::  OLD: \tt_clear/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='ODD'
|| TT_JKS.prefix();
   {? TT_JKS.first()
   || {! |?
         TT_JKS.Z:=0;
         TT_JKS.put();
         TT_JKS.next()
      !}
   ?}
|? _a='SKID_RBK'
|| TAB_SPEC.prefix();
   {? TAB_SPEC.first()
   || {! |?
         TAB_SPEC.Z:=0;
         TAB_SPEC.put();
         TAB_SPEC.next()
      !}
   ?}
|? _a='TZL'
|| TT_TYPP.prefix();
   {? TT_TYPP.first()
   || {! |?
         TT_TYPP.Z:=0;
         TT_TYPP.put();
         TT_TYPP.next()
      !}
   ?}
?}


\tab_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Kopiuje zawartosc tabeli _a do _b (tabela b jest przedtem kasowana)
::       tabele musza miec zgodne struktury - acronimy, polozenie i wielkosc pol
::   WE: _a = [STRING] - tabel zrodlowa, _b = [STRING] - tabela docelowa
::  OLD: \tab_copy/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
($(_b+'.erase()'))();
{? ($(_a+'.first()'))()
|| {! |?
      ($(_b+'.blank()'))();
      {! i_licz:=1..($(_a+'.fld_num()'))() |!
         _acr:=($(_a+'.fld_acr(i_licz)'))();
         ($(_b+'.'+_acr+':='+_a+'.'+_acr))()
      !};
      ($(_b+'.add()'))();
      ($(_a+'.next()'))()
   !}
?}


\czy_uprt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Formula sprawdza czy uzytkownik ma uprawnienia do typow platnosci (choc 1)
::   WY: 1 - tak; 0 - nie, brak uprawnien
::  OLD: \czy_uprt/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
exec('usr_hrp_any','b_perm')


\czy_uprb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Formula sprawdza czy uzytkownik ma uprawnienia do rachunkow bankowych (choc 1)
::   WY: 1 - tak; 0 - nie, brak uprawnien
::  OLD: \czy_uprb/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
exec('usr_hrb_any','b_perm')


\parfil_backup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Tworzy kopie (przed wywolaniem okna) lub przywraca z kopii (po Esc w oknie) parametry filtra
::   WE: _a=1 - tworzy kopie parametrow, _a=2 - przywraca parametry,
::       _b = tryb pracy: 'E' - przelewy, 'I' - pozycje wyciągów, 'W' - wyciągi
::  OLD: \parfil_backup/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| TT_JKS.cntx_psh(); TAB_SPEC.cntx_psh();
   {? 1+_b='E'
   || TT_SORT.cntx_psh(); TT_TYPP.cntx_psh()
   ?};
   exec('tab_copy','hbn_filtr','TT_JKS','STT_JKS');
   exec('tab_copy','hbn_filtr','TAB_SPEC','STAB_SPE');
   {? 1+_b='E'
   || exec('tab_copy','hbn_filtr','TT_SORT','STT_SORT');
      exec('tab_copy','hbn_filtr','TT_TYPP','STT_TYPP')
   ?};
   TT_JKS.cntx_pop(); TAB_SPEC.cntx_pop();
   {? 1+_b='E'
   || TT_SORT.cntx_pop(); TT_TYPP.cntx_pop()
   ?}
|? _a=2
|| exec('tab_copy','hbn_filtr','STT_JKS','TT_JKS');
   exec('tab_copy','hbn_filtr','STAB_SPE','TAB_SPEC');
   {? 1+_b='E'
   || exec('tab_copy','hbn_filtr','STT_SORT','TT_SORT');
      exec('tab_copy','hbn_filtr','STT_TYPP','TT_TYPP')
   ?}
?}


\cr_pfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Tworzenie parametrow filtra, jesli nie istnieja
::   WE: _a: tryb pracy: 1 - parametry na podst. uprawnien; 0 - parametry na podst. wyboru
::       _b: tryb pracy: 'E' - eksport platnosci; 'I' - pozycje wyciagow, 'W' - wyciagi
::       _c: wartosc pola PFP.TAB: SKID_RBK, TZL, ODD, TT_SORT lub TT_ROK
::       _d: tabela tymczasowa parametrow filtra: TAB_SPEC, TT_TYPP, TT_JKS, TT_SORT lub TT_ROK
::       _e: konfiguracja domyslna (D) lub biezaca (B) lub tymczasowy zapis (H)
::  OLD: \cr_pfp/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
{? _<5 || _e:='D'?};
PFZ.index('KONF'); PFZ.prefix(OPERATOR.USER,_b,_e);
{? PFZ.first()
|| PFP.index('POZ'); PFP.prefix(PFZ.ref(),_c);
   {? (~PFP.first()) & _d.first()
   || {! |?
         {? _a | _c='TT_SORT' | _c='TT_ROK' | _d.Z
         || PFP.blank();
            PFP.PFZ:=PFZ.ref();
            PFP.TAB:=_c;
            {? _c='SKID_RBK'
            || PFP.REF:=#RB.getrrban(TAB_SPEC.RB,REF.INFO,0)
            |? _c='TZL'
            || {? TZL.find_key(TT_TYPP.KOD) || PFP.REF:=#TZL.ref() ?}
            |? _c='ODD'
            || ODD.prefix(REF.FIRMA,TT_JKS.OD,TT_JKS.OD); {? ODD.first() || PFP.REF:=#ODD.ref() ?}
            |? _c='TT_SORT'
            || {? TT_SORT.FLD='DP'
               || PFP.REF:=10+TT_SORT.POZ
               |? TT_SORT.FLD='RD'
               || PFP.REF:=20+TT_SORT.POZ
               |? TT_SORT.FLD='KD'
               || PFP.REF:=30+TT_SORT.POZ
               |? 2+TT_SORT.FLD='OD'
               || PFP.REF:=40+TT_SORT.POZ
               ?}
            |? _c='TT_ROK'
            || PFP.REF:=#TT_ROK.ROK
            || PFP.REF:=0
            ?};
            PFP.add(1);
            {? _c<>'TT_SORT' & _c<>'TT_ROK'
            || _d.Z:=1;
               _d.put()
            ?}
         ?};
         _d.next()
      !}
   ?}
?}; 1


\clr_pfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Czyszczenie parametrow filtra
::   WE: _a: tryb pracy: 'E' - eksport platnosci; 'I' - pozycje wyciągów, 'W' - wyciągi
::       _b: wartosc pola PFP.TAB: SKID_RBK, TZL, ODD, TT_SORT lub TT_ROK
::       _c: konfiguracja domyslna (D) lub biezaca (B) lub fork HB z FIKS (H)
::  OLD: \clr_pfp/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
PFZ.index('KONF'); PFZ.prefix(OPERATOR.USER,_a,_c);
{? PFZ.first()
|| PFP.index('POZ'); PFP.prefix(PFZ.ref(),_b);
   {? PFP.first()
   || {! |?
         PFP.del()
      !}
   ?}
?}; 1


\cr_pfz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Tworzenie zestawu parametrow filtra, jesli nie istnieje
::   WE: _a: tryb pracy: 'E' - eksport platnosci; 'I' - pozycje wyciagow, 'W' - wyciągi
::          'EA' - archiwum platnosci; 'IA' - archiwum pozycji wyciagow, 'WA' - archiwum wyciągów
::       _b: konfiguracja: 'D' - domyslna; 'B' - biezaca
::   WY: 1 - utworzono zestaw, 0 - nie utworzono, istniał wcześniej
::  OLD: \cr_pfz/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
PFZ.index('KONF'); PFZ.prefix(OPERATOR.USER,_a,_b);
{? ~PFZ.first()
|| PFZ.prefix();
   PFZ.blank();
   PFZ.USER:=OPERATOR.USER;
   PFZ.RODZ:=_a;
   PFZ.KONF:=_b;
   PFZ.add();
   1
|| 0
?}


\get_pfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Pobranie domyslnych parametrow filtra
::   WE: _a: wartosc pola PFP.TAB: SKID_RBK, TZL, ODD lub TT_SORT
::       _b: tabela tymczasowa parametrow filtra: TAB_SPEC, TT_TYPP, TT_JKS lub TT_SORT
::  OLD: \get_pfp/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
PFP.prefix(PFZ.ref(),_a);
{? PFP.first()
|| exec('tt_clear','hbn_filtr',_a);
   {! |?
      {? _a='SKID_RBK'
      || SKID_RBK.clear(); TAB_SPEC.prefix();
         {? SKID_RBK.seek(PFP.REF,'skid_rbk')
         || {? TAB_SPEC.find_key(RB.get_rbtx(1,SKID_RBK.ref()))
            || TAB_SPEC.Z:=1; TAB_SPEC.put()
            ?};
            PFP.next()
         ||  PFP.del()
         ?}
      |? _a='ODD'
      || ODD.clear(); TT_JKS.prefix();
         {? ODD.seek(PFP.REF,'oddzialy') & TT_JKS.find_key(ODD.OD)
         || TT_JKS.Z:=1; TT_JKS.put()
         ?};
         PFP.next()
      |? _a='TZL'
      || TZL.clear(); TT_TYPP.prefix();
         {? TZL.seek(PFP.REF,'typy_zl') & TT_TYPP.find_key(TZL.KOD)
         || TT_TYPP.Z:=1; TT_TYPP.put()
         ?};
         PFP.next()
      |? _a='TT_SORT'
      || TT_SORT.prefix();
         {? TT_SORT.first()
         || {! |?
               {? (1+$PFP.REF='1' & TT_SORT.FLD='DP')
                  | (1+$PFP.REF='2' & TT_SORT.FLD='RD')
                  | (1+$PFP.REF='3' & TT_SORT.FLD='KD')
                  | (1+$PFP.REF='4' & 2+TT_SORT.FLD='OD')
               || TT_SORT.POZ:=#($PFP.REF+1); TT_SORT.put(); 0
               || TT_SORT.next()
               ?}
            !}
         ?};
         PFP.next()
      |? _a='TT_ROK'
      || {? TT_ROK.first()
         || TT_ROK.ROK:=$PFP.REF; TT_ROK.put()
         ?}; 0
      ?}
   !}
?};
1


\save_pfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Zapisanie domyslnych parametrow filtra
::   WE: [_a] - 'B' - konfiguracja bieżąca, 'D' - domyślna, 'H' - tymczasowa (zapis przed wywołaniem archiwum)
::  OLD: \save_pfp/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:='D' ?};
TAB_SPEC.cntx_psh();
TT_JKS.cntx_psh();
{? var_pres('TT_TYPP')>0
|| TT_TYPP.cntx_psh();
   TT_SORT.cntx_psh()
?};
_dalej:={? var_pres('tmp_typ')>0
        || PFZ.index('KONF'); PFZ.prefix(PFZ.USER,PFZ.RODZ,_a);
           {? ~PFZ.first()
           || {? _a='H' || PFZ.KONF:=_a; PFZ.add(); 1  || 0 ?}
           || 1
           ?}
        || PFZ.get()
        ?};
{? _dalej
|| PFP.index('POZ'); PFP.prefix(PFZ.ref());
   {? PFP.first() || {! |? PFP.del() !} ?};
   exec('cr_pfp','hbn_filtr',0,PFZ.RODZ,'SKID_RBK',TAB_SPEC,_a);
   exec('cr_pfp','hbn_filtr',0,PFZ.RODZ,'ODD',TT_JKS,_a);
   {? var_pres('TT_TYPP')>0
   || exec('cr_pfp','hbn_filtr',0,PFZ.RODZ,'TZL',TT_TYPP,_a);
      exec('cr_pfp','hbn_filtr',0,PFZ.RODZ,'TT_SORT',TT_SORT,_a)
   ?};
   {? var_pres('TT_ROK')>0
   || TT_ROK.cntx_psh();
      exec('cr_pfp','hbn_filtr',0,PFZ.RODZ,'TT_ROK',TT_ROK,_a);
      TT_ROK.cntx_pop()
   ?}
?};
TAB_SPEC.cntx_pop();
TT_JKS.cntx_pop();
{? var_pres('TT_TYPP')>0
|| TT_TYPP.cntx_pop();
   TT_SORT.cntx_pop()
?};
1


\get_pfz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Pobranie parametrow filtra
::   WE: _a: tryb pracy: 1 - z odswiezaniem okienek; 0 - bez odswiezania
::       _b: tryb pracy: 'E' - eksport płatności; 'I' - pozycje wyciągów; 'W' - wyciągi
::       _c: konfiguracja: 'D' - domyslna; 'B' - biezaca
::  OLD: \get_pfz/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
PFZ.index('KONF'); PFZ.prefix(OPERATOR.USER,_b,_c);
{? PFZ.first()
|| PFP.index('POZ');
   exec('get_pfp','hbn_filtr','SKID_RBK');
   {? 1+_b='E'
   || exec('get_pfp','hbn_filtr','TT_SORT');
      exec('get_pfp','hbn_filtr','TZL')
   ?};
   exec('get_pfp','hbn_filtr','ODD');
   TAB_SPEC.first();
   TT_JKS.first();
   {? 1+_b='E' || TT_TYPP.first(); TT_SORT.first() ?};
   {? +_b=2 & _b<>'IA'
   || TT_ROK.first();
      exec('get_pfp','hbn_filtr','TT_ROK')
   ?};
   {? _a
   || grp_disp(TT_JKS,ws_jk);
      {? 1+_b='E'
      || grp_disp(TT_TYPP,ws_tp); grp_disp(TT_SORT,ws_so)
      ?}
   ?}
?}; 1


\tt_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Tworzy tabele tymczasowa TT_ROK z oznaczeniem roku
::  OLD: \tt_rok/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
TT_ROK:=tab_tmp(1,'ROK','STRING[20]','');
TT_ROK.ROK:=4+(date()$0); TT_ROK.add();
:: Utworzenie zestawu domyslnych parametrow, jesli nie istnieja
exec('cr_pfz','hbn_filtr',_a,'D');
exec('cr_pfp','hbn_filtr',1,_a,'TT_ROK',TT_ROK); 1


\tt_sort
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Tworzy tabele tymczasowa TT_SORT do ustalenia porzadku sortowania
::  OLD: \tt_sort/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
TT_SORT:=tab_tmp(1,'POZ','INTEGER','','FLD','STRING[15]',,'NAZ','STRING[20]','');
STT_SORT:=tab_tmp(1,'POZ','INTEGER','','FLD','STRING[15]',,'NAZ','STRING[20]','');
TT_SORT.FLD:='DP'; TT_SORT.NAZ:='Data płatności'; TT_SORT.POZ:=1; TT_SORT.add();
TT_SORT.FLD:='RD'; TT_SORT.NAZ:='Rachunek bankowy'; TT_SORT.POZ:=2; TT_SORT.add();
TT_SORT.FLD:='KD'; TT_SORT.NAZ:='Typ płatności'; TT_SORT.POZ:=3; TT_SORT.add();
TT_SORT.FLD:='ODD(OD)'; TT_SORT.NAZ:='Jednostka księgowa'; TT_SORT.POZ:=4; TT_SORT.add();
:: Utworzenie zestawu domyslnych parametrow, jesli nie istnieja
exec('cr_pfz','hbn_filtr',_a,'D');
exec('cr_pfp','hbn_filtr',1,_a,'TT_SORT',TT_SORT); 1


\tt_typp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Tworzy tabele tymczasowa TT_TYPP z typami platnosci,
::       do ktorych uzytkownik ma uprawnienia
::  OLD: \tt_typp/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
TT_TYPP:=tab_tmp(2,'KOD','STRING[20]',,'OPIS','STRING[50]','','Z','INTEGER','');
STT_TYPP:=tab_tmp(2,'KOD','STRING[20]',,'OPIS','STRING[50]','','Z','INTEGER','');
TZL.clear();
{? TZL.first()
|| {! |?
      {? exec('usr_hrp','b_perm',TZL.KOD)
      || {? TZL.KOD<>'WSZYSTKIE'
         || TT_TYPP.KOD:=TZL.KOD;
            TT_TYPP.OPIS:=TZL.OPIS;
            TT_TYPP.Z:=0;
            TT_TYPP.add()
         ?}
      ?};
      TZL.next()
   !}
?};
:: Utworzenie zestawu domyslnych parametrow, jesli nie istnieja
exec('cr_pfz','hbn_filtr',_a,'D');
TZL.index('KOD');
TZL.prefix();
exec('cr_pfp','hbn_filtr',1,_a,'TZL',TT_TYPP);
1


\tt_jks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Tworzy tabele tymczasowa TT_JKS z jednostkami ksiegowymi,
::       do ktorych uzytkownik ma uprawnienia
::  OLD: \tt_jks/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
TT_JKS:=tab_tmp(1,'OD','STRING[8]','','Z','INTEGER','');
STT_JKS:=tab_tmp(1,'OD','STRING[8]','','Z','INTEGER','');
ODD.clear();
{? REF.WFIRM
|| ODD.index('ODDZIALY');
   ODD.prefix(REF.FIRMA)
?};
USERSDEP.index('USERSDEP'); USERSDEP.prefix(REF.FIRMA,OPERATOR.USER);
{? ODD.first()
|| {! |?
      exec('get_usersf','users');
      {? (Perm.hasFull('FJKS') | USERSDEP.find_key(ODD.OD))
      || TT_JKS.OD:=ODD.OD; TT_JKS.Z:=0;
         TT_JKS.add()
      ?};
      ODD.next()
   !}
?};
:: Utworzenie zestawu domyslnych parametrow, jesli nie istnieja
exec('cr_pfz','hbn_filtr',_a,'D');
ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
exec('cr_pfp','hbn_filtr',1,_a,'ODD',TT_JKS); 1


\tt_rbspec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Tworzy tabele tymczasowa TAB_SPEC z rachunkami bankowymi, dla ktorych istnieja specyfikacje
::       i do ktorych uzytkownik ma uprawnienia
::   WE: _a: oznaczenie operacji: I - pozycje wyciągów, E - eksport płatności, 'W' - wyciągi
::  OLD: \tt_rbspec/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:="
   tab_tmp(3,
      'RB','STRING[51]','',
      'NB','STRING[80]','Bank',
      'SKID_RBK','REAL',,
      'NUMER','STRING[8]',,
      'KRAJ','STRING[2]','Kod kraju',
      'WAL','STRING[3]','Waluta',
      'INNWAL','STRING[1]','Inna waluta',
      'Z','INTEGER','',
      'AKTYWNY','STRING[1]','Aktywny'
   )
";
TAB_SPEC:=_tab();
STAB_SPE:=_tab();
SKID_RBK.index('BANK'); SKID_RBK.prefix(REF.INFO);
HBPKI.index('HBPKILPR'); HBPKI.prefix({? _a='W' || 'I' || _a ?}); HBP.index('HBNAZWA');
SKID_RBK.index('BANKNR'); SKID_RBK.prefix(REF.INFO); FINFO.SLWAL().SLU();
{? HBPKI.first() & SKID_RBK.first()
|| {! |?
      HBP.prefix(HBPKI.ref()); SKID_RBK.prefix(REF.INFO,HBPKI.KDB().NUMER,HBPKI.KDB().NUMER);
      {? HBP.first() & SKID_RBK.first()
      || {! |?
            _vrb:=RB.get_rbtx(1,SKID_RBK.ref(),SKID_RBK.KRAJ().KODISO); _vnb:=SKID_RBK.BANK().NB;
            _vref:=SKID_RBK.ref(); _vkraj:=SKID_RBK.KRAJ().KODISO; _vwal:=SKID_RBK.WAL().KOD;
            _viwal:=SKID_RBK.INNWAL; _akt:=SKID_RBK.AKTYWNY;
            TAB_SPEC.cntx_psh(); _v:=TAB_SPEC.find_key(_vrb,_vnb); TAB_SPEC.cntx_pop();
            {? ~_v
            || TAB_SPEC.RB:=_vrb; TAB_SPEC.NB:=_vnb; TAB_SPEC.SKID_RBK:=_vref;
               TAB_SPEC.NUMER:=HBPKI.KDB().NUMER; TAB_SPEC.KRAJ:=_vkraj;
               TAB_SPEC.WAL:=_vwal; TAB_SPEC.INNWAL:=_viwal; TAB_SPEC.Z:=0;
               TAB_SPEC.AKTYWNY:=_akt;
               TAB_SPEC.add()
            ?};
            SKID_RBK.next()
         !}
      ?};
      HBPKI.next()
   !}
?};
{? TAB_SPEC.first()
:: Usuniecie rachunkow bankowych, do ktorych uzytkownik nie ma uprawnien
|| exec('get_usersf','users');
   {? ~Perm.hasFull('HRB')
   || UPR_RB.index('RB'); UPR_RB.prefix(OPERATOR.USER);
      {? UPR_RB.first()
      || {? TAB_SPEC.first()
         || {! |?
               _ref:=RB.getrrban(TAB_SPEC.RB,REF.INFO,0);
               {? ~(_ref & UPR_RB.find_key(_ref))
               || TAB_SPEC.del()
               || TAB_SPEC.next()
               ?}
            !}
         ?}
      || TAB_SPEC.erase()
      ?}
   ?}
?};
:: Utworzenie zestawu domyslnych parametrow, jesli nie istnieja
{? exec('cr_pfz','hbn_filtr',_a,'D')
|| exec('cr_pfp','hbn_filtr',1,_a,'SKID_RBK',TAB_SPEC)
?};
1


\setf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2008]
:: OPIS: Redakcja parametrow filtra i jego wykonanie
::   WE: _a: redakcja parametrow: 0 - nie, 1 - tak
::       _b: tryb pracy: 'E' - przelewy; 'I' - pozycje wyciagow; 'W' - wyciągi
::          'EA' - archiwum platnosci; 'IA' - archiwum pozycji wyciagow; 'WA' - archiwum wyciągów
::       _c: konfiguracja: 'D' - domyslna; 'B' - biezaca;
::       pb_sql: zmienna globalna z dodatkowym warunkiem, ktory moze byc nakladany na bazy PB/PWN bez
::               wzgledu na ustawienia filtra (typ STRING, np. 'KH=0001')
::  OLD: \setf/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
_exit:=1;
:: rachunki
{? var_pres('TAB_SPEC')<0 || exec('tt_rbspec','hbn_filtr',_b) ?};
:: jednostki księgowe
{? var_pres('TT_JKS')<0 || exec('tt_jks','hbn_filtr',_b) ?}; TT_JKS.first();
:: typy płatności i sposób sortowania
{? 1+_b='E'
|| {? var_pres('TT_TYPP')<0 || exec('tt_typp','hbn_filtr',_b) ?}; TT_TYPP.first();
   {? var_pres('TT_SORT')<0 || exec('tt_sort','hbn_filtr',_b) ?}; TT_SORT.first()
?};
:: dla archiwalnych pozycji wyciągów brak wyboru lat - lata wybiera się na poziomie archiwum wyciągów
{? +_b=2
|| {? var_pres('TT_ROK')<0 || exec('tt_rok','hbn_filtr',_b) ?};
   TT_ROK.first()
?};
:: rachunki
ws_rb:=TAB_SPEC.mk_sel('Rachunki bankowe'@,,0,'rach_bank');
TAB_SPEC.win_fld(ws_rb,,'RB',,,43);
TAB_SPEC.win_fld(ws_rb,,'Z',,,3,,,,,,2,,"1","0");
TAB_SPEC.win_act(ws_rb,,'Formuła','Z&mień'@@,,'Zaznacza lub odznacza pozycję'@,,"TAB_SPEC.Z:=~TAB_SPEC.Z; TAB_SPEC.put()",1,,,,'M');
TAB_SPEC.win_act(ws_rb,,'Formuła','Zastosuj'@@,,'Zastosuj bieżące ustawienia filtra'@,,"sel_exit()");
{? 1+_b='E'
|| {? _b+1='A'
   || TAB_SPEC.win_act(ws_rb,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'EA','D')")
   || TAB_SPEC.win_act(ws_rb,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'E','D')")
   ?}
|? 1+_b='W'
|| {? _b+1='A'
   || TAB_SPEC.win_act(ws_rb,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'WA','D')")
   || TAB_SPEC.win_act(ws_rb,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'W','D')")
   ?}
?};
TAB_SPEC.win_act(ws_rb,,'Formuła','Zapi&sz domyślne'@@,,'Zapisuje bieżące ustawienia filtra jako domyślne'@,,"exec('save_pfp','hbn_filtr')",,,,,'S');
TAB_SPEC.win_act(ws_rb,,'Rekord',,,,"{? TAB_SPEC.AKTYWNY<>'T' || Color.fnd_kol('RBK#02#01') || '' ?}");
TAB_SPEC.win_act(ws_rb,,'Formuła','Legenda',,,"exec('legenda','color','RBK#02#')");
:: jednostki księgowe
ws_jk:=TT_JKS.mk_sel('Jednostka księgowa'@,,0,'jedn_ks');
TT_JKS.win_fld(ws_jk,,'OD',,,20);
TT_JKS.win_fld(ws_jk,,'Z',,,3,,,,,,2,,"1","0");
TT_JKS.win_act(ws_jk,,'Formuła','Z&mień'@@,,'Zaznacza lub odznacza pozycję'@,,"TT_JKS.Z:=~TT_JKS.Z; TT_JKS.put()",1,,,,'M');
TT_JKS.win_act(ws_jk,,'Formuła','Zastosuj'@@,,'Zastosuj bieżące ustawienia filtra'@,,"sel_exit()");
{? 1+_b='E'
|| {? _b+1='A'
   || TT_JKS.win_act(ws_jk,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'EA','D')")
   || TT_JKS.win_act(ws_jk,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'E','D')")
   ?}
|| {? 1+_b<>'W'
   || {? _b+1='A'
      || TT_JKS.win_act(ws_jk,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'IA','D')")
      || TT_JKS.win_act(ws_jk,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'I','D')")
      ?}
   ?}
?};
TT_JKS.win_act(ws_jk,,'Formuła','Zapi&sz domyślne'@@,,'Zapisuje bieżące ustawienia filtra jako domyślne'@,,"exec('save_pfp','hbn_filtr')",,,,,'S');
:: typy płatności
{? 1+_b='E'
|| ws_tp:=TT_TYPP.mk_sel('Typ płatności'@,,0,'typ_plat');
   TT_TYPP.win_fld(ws_tp,,'OPIS',,,43);
   TT_TYPP.win_fld(ws_tp,,'Z',,,3,,,,,,2,,"1","0");
   TT_TYPP.win_act(ws_tp,,'Formuła','Z&mień'@@,,'Zaznacza lub odznacza pozycję'@,,"TT_TYPP.Z:=~TT_TYPP.Z; TT_TYPP.put()",1,,,,'M');
   TT_TYPP.win_act(ws_tp,,'Formuła','Zastosuj'@@,,'Zastosuj bieżące ustawienia filtra'@,,"sel_exit()");
   {? _b+1='A'
   || TT_TYPP.win_act(ws_tp,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'EA','D')")
   || TT_TYPP.win_act(ws_tp,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'E','D')")
   ?};
   TT_TYPP.win_act(ws_tp,,'Formuła','Zapi&sz domyślne'@@,,'Zapisuje bieżące ustawienia filtra jako domyślne'@,,"exec('save_pfp','hbn_filtr')",,,,,'S');
   ws_so:=TT_SORT.mk_sel('Kolejność sortowania'@,,0,'kol_sort');
   TT_SORT.win_fld(ws_so,,'NAZ',,,24);
   TT_SORT.win_act(ws_so,,'Formuła','Przesuń w &górę'@@,,'Przesuwa pozycję w górę',,"
      {? TT_SORT.POZ>1
      || _poz:=TT_SORT.POZ;
         TT_SORT.prev();
         TT_SORT.POZ:=0; TT_SORT.put();
         TT_SORT.find_key(_poz);
         TT_SORT.POZ-=1; TT_SORT.put();
         TT_SORT.find_key(0);
         TT_SORT.POZ:=_poz; TT_SORT.put();
         TT_SORT.find_key(_poz-1)
      ?}",1,,,,'G');
   TT_SORT.win_act(ws_so,,'Formuła','Przesuń w &dół'@@,,'Przesuwa pozycję w dół'@,,"
      {? TT_SORT.POZ<4
      || _poz:=TT_SORT.POZ;
         TT_SORT.next();
         TT_SORT.POZ:=0; TT_SORT.put();
         TT_SORT.find_key(_poz);
         TT_SORT.POZ+=1; TT_SORT.put();
         TT_SORT.find_key(0);
         TT_SORT.POZ:=_poz; TT_SORT.put();
         TT_SORT.find_key(_poz+1)
      ?}",,,,,'D');
   TT_SORT.win_act(ws_so,,'Formuła','Zastosuj'@@,,'Zastosuj bieżące ustawienia filtra'@,,"sel_exit()");
   {? _b+1='A'
   || TT_SORT.win_act(ws_so,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'EA','D')")
   || TT_SORT.win_act(ws_so,,'Formuła','Pobierz domyślne'@@,,'Pobiera domyślne ustawienia filtra'@,,"exec('get_pfz','hbn_filtr',1,'E','D')")
   ?};
   TT_SORT.win_act(ws_so,,'Formuła','Zapi&sz domyślne'@@,,'Zapisuje bieżące ustawienia filtra jako domyślne'@,,"exec('save_pfp','hbn_filtr')",,,,,'S')
?};
{? 1+_b='E'
|| _wfilter:=TAB_SPEC.grp_make('Parametry filtra'@,"grp_disp(TT_TYPP,ws_tp); grp_disp(TT_JKS,ws_jk);
      grp_disp(TT_SORT,ws_so)",'#hbn_filtr_prz');
   TAB_SPEC.grp_sel(_wfilter,TAB_SPEC,ws_rb,,,,,12,,,,,'maximized_with_title');
   TAB_SPEC.grp_splt(_wfilter,'panel0','vertical','góra_prawo');
   TAB_SPEC.grp_sel(_wfilter,TT_JKS,ws_jk,,,52,,12,,,,,'maximized_with_title');
   TAB_SPEC.grp_splt(_wfilter,'panel0','horizontal','dół');
   TAB_SPEC.grp_sel(_wfilter,TT_TYPP,ws_tp,,,,13,12,,,,,'maximized_with_title');
   TAB_SPEC.grp_splt(_wfilter,'góra_prawo','horizontal','dół_prawo');
   TAB_SPEC.grp_sel(_wfilter,TT_SORT,ws_so,,,52,13,12,,,,,'maximized_with_title');
   TAB_SPEC.win_sel(_wfilter)
|? 1+_b='W'
|| _tyt:='Parametry filtra'@;
   _wfilter:=TAB_SPEC.grp_make(_tyt,"grp_disp(TT_JKS,ws_jk)",'#hbn_filtr_wyc');
   TAB_SPEC.grp_sel(_wfilter,TAB_SPEC,ws_rb,,,,,12,,,,,'maximized_with_title');
   TAB_SPEC.grp_splt(_wfilter,'panel0','vertical','góra_prawo');
   TAB_SPEC.grp_sel(_wfilter,TT_JKS,ws_jk,,,52,,12,,,,,'maximized_with_title');
   TAB_SPEC.win_sel(_wfilter)
?};
{? _c='D' | _c='H' || exec('get_pfz','hbn_filtr',0,_b,_c) ?};
{? _a
|| exec('parfil_backup','hbn_filtr',1,_b);
   _wybor:=TAB_SPEC.select()
|| _wybor:=0
?};
{? (_a & _wybor) | ~_a
|| _ssort:='';
   {? 1+_b='E' & TT_SORT.first()
   || {! |?
         _ssort+=TT_SORT.FLD+',';
         TT_SORT.next()
      !};
      _ssort:=_ssort-1
   ?};
   _emsg:='';
:: Uprawnienia do rachunkow bankowych
   _is:=0;
   _srb:='';
   {? TAB_SPEC.first()
   || _fld:={? 1+_b='E' || 'RD' |? 1+_b='W' || 'RBL' ?};
      {? TAB_SPEC.first()
      || {! |?
            {? TAB_SPEC.Z || _srb+=_fld+'=\''+RB.get_rbel(2,TAB_SPEC.RB)+'\' or '; _is:=1 ?};
            TAB_SPEC.next()
         !};
         _srb:=_srb-4
      ?}
   ?};
   {? ~_is
   || {? 1+_b='E'
      || HBPKI.cntx_psh();
         HBPKI.index('HBPKILPR'); HBPKI.prefix('E');
         {? ~HBPKI.first()
         || _emsg:='W systemie brak zdefinowanych specyfikacji eksportu przelewów.\n'
                   'Należy zaimportować specyfikacje uruchamiając w obszarze roboczym:\n'
                   'Ustawienia i parametryzacja funkcję Specyfikacje bankowe->Importuj.'@;
            _srb:=' 0=1 ';
            _exit:=0
         ?};
         HBPKI.cntx_pop()
      ?};
      {? _emsg=''
      || {? exec('czy_uprb','hbn_filtr')
         || {? TAB_SPEC.size()>0
            || _emsg:='Nie wybrano żadnego rachunku bankowego w filtrze.'@
            || _emsg:='Brak rachunków bankowych w filtrze. Należy sprawdzić czy dla oddziałów banków,\n'
                      'w których prowadzone są rachunki licencjobiorcy zdefiniowano formaty przelewów.'@
            ?};
            _srb:=' 0=1 '
         || _emsg:='Nie nadano uprawnień do żadnego rachunku bankowego.'@;
            _srb:=' 0=1 ';
            _exit:=0
         ?}
      ?}
   ?};
:: Uprawnienia do typow platnosci
   _is:=0;
   _stp:='';
   {? 1+_b='E' & TT_TYPP.first()
   || {? TT_TYPP.first()
      || {! |?
            {? TT_TYPP.KOD<>'WSZYSTKIE' & TT_TYPP.Z
            || _stp+='KD=\''+TT_TYPP.KOD+'\' or '; _is:=1
            ?};
            TT_TYPP.next()
         !};
         _stp:=_stp-4
      ?}
   ?};
   {? 1+_b='E' & ~_is
   || {? exec('czy_uprt','hbn_filtr')
      || {? +_emsg || _emsg+='\n' ?};
         _emsg:='Nie wybrano żadnego typu przelewu w filtrze.'@;
         _stp:=' 0=1 '
      || {? +_emsg || _emsg+='\n' ?};
         _emsg:='Nie nadano uprawnień do żadnego typu przelewu.'@;
         _stp:=' 0=1 ';
         _exit:=0
      ?}
   ?};
:: Uprawnienia do jednostek ksiegowych
   _is:=0;
   _sjks:='';
   _sfrom:='left join ODD';
   {? TT_JKS.first()
   || {! |?
         {? TT_JKS.Z || _sjks+='ODD.OD=\''+TT_JKS.OD+'\' or '; _is:=1 ?};
         TT_JKS.next()
      !};
      _sjks:=_sjks-4
   ?};
   {? OPERATOR.DEPT=null
   || _sjks+={? _is || ' or' || '' ?}+' (ODD is null) ';
      _is:=1
   ?};
   {? ~_is
   || {? exec('czy_upro','hbn_filtr')
      || {? +_emsg || _emsg+='\n' ?};
         _emsg:='Nie wybrano żadnej jednostki księgowej w filtrze.'@;
         _sjks:=' 0=1 '
      || {? +_emsg || _emsg+='\n' ?};
         _emsg:='Nie nadano uprawnień do żadnej jednostki księgowej.'@;
         _sjks:=' 0=1 ';
         _exit:=0
      ?}
   ?};
   {? +_emsg
   || _emsg+='\n\n';
      _emsg+='Wyświetlenie przelewów niemożliwe.'@;
      {? var_pres('__setf_m')<0 | __setf_m=0
      || FUN.emsg(_emsg);
         {? var_pres('__setf_m')>=0 || __setf_m+=1 ?}
      ?}
   ?};
   _swal:='';
   {? 1+_b='E'
   || {? PAR_SKID.get(68)='N'
      || _sfrom+=' left join SLO using("PB".WAL,SLO.reference)';
         _swal:='SLO.KOD=\''+SSTALE.WAL().KOD+'\''
      ?}
::   |? 1+_b='I'
::   || _swal:='"PW".WAL=\''+SSTALE.WAL().KOD+'\''
   ?};
   {? +_srb
   || _swhere:='('+_srb+')'
   || _swhere:=''
   ?};
:: jeżeli dla pozycji wyciągów to na początku ograniczenie do bieżącego wyciągu
:: dalsze elementy budujące warunek
   {? +_swhere & +_stp || _swhere+=' and ('+_stp+')' |? +_stp || _swhere+='('+_stp+')' ?};
   {? +_swhere & +_sjks || _swhere+=' and ('+_sjks+')' |? +_sjks || _swhere+='('+_sjks+')' ?};
   {? +_swal || {? +_swhere || _swhere+=' and '+_swal || _swhere:=_swal ?} ?};
   {? ~(var_pres('pb_sql')<0)
   || {? +_swhere || _swhere+=' and '+pb_sql?}
   ?};
   {? _b='E'
   || PB.prefix(); PB.f_set(_ssort,_sfrom,_swhere);
      {? MLEX.FIKSB
      || ssort:=_ssort; sfrom:=_sfrom; swhere:=_swhere
      ?}
   |? _b='W'
   || PWN.index('PWN'); PWN.prefix(); PWN.f_set(_ssort,_sfrom,_swhere)
   |? _b='EA'
   || PB.use('pb'+TT_ROK.ROK); PB_OP.use('pop'+TT_ROK.ROK); PBHIST.use('ph'+TT_ROK.ROK);
      PB.prefix(); PB.f_set(_ssort,_sfrom,_swhere)
   |? _b='WA'
   || PWN.use('pn'+TT_ROK.ROK); PW.use('pw'+TT_ROK.ROK); PW_OP.use('pwp'+TT_ROK.ROK);
      PWN.index('PWN'); PWN.prefix(); PWN.f_set(_ssort,_sfrom,_swhere)
   ?}
|| {? _a & _wybor=0 || exec('parfil_backup','hbn_filtr',2,_b) ?}
?};
_exit


\sum_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.60]
:: OPIS: Sumuje przelewy zatwierdzone i niezatwierdzone w dziedzinie filtra
::  OLD: \sum_pb/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
PB.cntx_psh();
_ref:=PB.ref();
{? 1 | PB.win_sel('?')='PB_WER1'
|| PAR_WYDR.RPAR1:=PAR_WYDR.RPAR2:=PAR_WYDR.RPAR3:=0;
   {? PB.f_first()
   || {! |? {? PB.ZT='T' || PAR_WYDR.RPAR1+=PB.KW || PAR_WYDR.RPAR2+=PB.KW ?}; PB.f_next() !};
      PAR_WYDR.RPAR3:=PAR_WYDR.RPAR1+PAR_WYDR.RPAR2
   ?}
|| {? PB.f_first()
   || TT_PB:=tab_tmp(1,'WAL','STRING[3]','Waluta','Z','REAL','Zatwierdzone',
         'N','REAL','Niezatwierdzone','W','REAL','Wszystkie');
      {! |?
         {? TT_PB.find_key(PB.WAL().KOD)
         || {? PB.ZT='T' || TT_PB.Z+=PB.KW || TT_PB.N+=PB.KW ?}; TT_PB.W+=PB.KW; TT_PB.put()
         || TT_PB.blank(); TT_PB.WAL:=PB.WAL().KOD;
            {? PB.ZT='T' || TT_PB.Z+=PB.KW || TT_PB.N+=PB.KW ?}; TT_PB.W+=PB.KW; TT_PB.add()
         ?};
         PB.f_next()
      !};
      {? TT_PB.first()
      || TT_PB.win_sel(TT_PB.mk_sel('Podsumowania przelewów'@,'N',1,,,,20)); TT_PB.select()
      ?};
      obj_del(TT_PB); &TT_PB
   ?}
?};
PB.f_seek(_ref);
PB.cntx_pop(); 1


\st_filtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Formula zaklada lub zdejmuje filtr w bazie plikow z wyciagami
::  OLD: \st_filtr/proc_01.fml
::----------------------------------------------------------------------------------------------------------------------
{? pwb_fil
|| PWB.f_set(,,'');
   PWB.hdr_sel();PWB.hdr_sel(' [WSZYSTKIE]'@);
   pwb_fil:=0
|| _where:='Z=\'N\'';
   PWB.f_set(,,_where);
   PWB.hdr_sel();PWB.hdr_sel(' [NIEPRZETWORZONE]'@);
   pwb_fil:=1
?}


\setfoptm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Akcja Filtr dla okien tabeli OPTMP
::  OLD: \setfoptm/przelewy.fml
::----------------------------------------------------------------------------------------------------------------------
__setf_m:=0;
{? exec('setf','hbn_filtr',1,'E','B')
|| OPTMP.hdr_sel(); OPTMP.prefix(v_user3);
   {? OPTMP.first() || {! |? OPTMP.del() !} ?}; &v_user3; &v_zakr; exec('set_rbop','rozrach_przel');
   exec('wind_pb_viscera','rozrach_przel',__par_a,__par_b)
?};
VAR_DEL.delete('__setf_m')


\set_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA,PJ] [2008]
:: OPIS: Zmiana roku dla archiwum
::   WE: _a - Tryb pracy: 'EA' - archiwum platnosci; 'WA' - archiwum wyciagow
::  OLD: \set_rok/skid_pel.fml
::----------------------------------------------------------------------------------------------------------------------
_lata:=tab_tmp(1,'ROK','STRING[4]','Rok',
                 'LICZBA','INTEGER',{? _a='EA' || 'Przelewy'@ |? _a='WA' || 'Wyciągi'@ ?});
_lata_sl:=_lata.mk_sel(,,0,'_latapbar',,,,,'U');
_lata.win_fld(_lata_sl,,'ROK',,,20,,,,,'Rok'@);
_lata.win_fld(_lata_sl,,'LICZBA',,,12,,,,,'Liczba zapisów w roku'@);
_lata.win_act(_lata_sl,,'Formuła','Wybierz'@@,,'Wczytuje archiwum ze wskazanego roku'@,,"sel_exit()",1);
_lata.win_sel(_lata_sl);
_lata.hdr_sel('Archiwum'@);
{? _a='EA'
|| PB.cntx_psh();
   _names:=PB.names()
|? _a='WA'
|| PWN.cntx_psh();
   _names:=PWN.names()
?};
{? _names.first()
|| {! |?
      _lata.blank();
      _lata.ROK:=_names.NAME+4;
      {? _names.SIZE=-1
      || {? _a='EA'
         || PB.cntx_psh();
            PB.use(_names.NAME);
            PB.index('PBZP');
            PB.prefix();
            _lata.LICZBA:=PB.size();
            PB.cntx_pop()
         |? _a='IA'
         || PWN.cntx_psh();
            PWN.use(_names.NAME);
            PWN.index('PWN');
            PWN.prefix();
            _lata.LICZBA:=PWN.size();
            PWN.cntx_pop()
         ?}
      || _lata.LICZBA:=_names.SIZE
      ?};
      {? #_lata.ROK>0 & _lata.LICZBA>=0 || _lata.add() ?};
      _names.next()
   !}
?};
{? _a='EA'
|| PB.cntx_pop()
|? _a='WA'
|| PWN.cntx_pop()
?};
{? _lata.first()
|| _lata.find_key(TT_ROK.ROK);
   {?_lata.select(,1,2)
   || TT_ROK.ROK:=_lata.ROK;
      {? TT_ROK.first()
      || TT_ROK.ROK:=_lata.ROK; TT_ROK.put();
         {? _a='EA'
         || PB.hdr_sel();
            PB.hdr_sel(' - archiwum z roku %1'@[_lata.ROK])
         |? _a='WA'
         || PWN.hdr_sel();
            PWN.hdr_sel(' - archiwum z roku %1'@[_lata.ROK])
         ?};
         exec('setf','hbn_filtr',0,_a,'B');
         exec('save_rok','hbn_filtr',_a,'B');
         exec('save_rok','hbn_filtr',_a,'D')
      ?}
   ?}
|| FUN.emsg('Brak danych archiwalnych.'@)
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 838527744c3044c6aa17b41c5e8d2262c352c99a179e227f7ffd1b6cf50d952928e1cde7ce513069bca512da015ffcd473562932597e8a5d5757b1980bec838bd400d55aade82b6cb7cb2c96b22ed26941b4f56c98e9692c017542e517a231565a3ee5596b510b435f56b8bfe42de572bcda61c5edc751408a0e6ed1c5377d0f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: jpk_v.fml [12.51]
:: Utworzony: 29.03.2016
:: Autor: MB
:: Systemy: FIKS
::======================================================================================================================
:: Zawartosc: Formuly odpowiedzialne za obsługę pliku JPK_V7
::======================================================================================================================


\init_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Inicjuje słownik wartości JPK_SLO
::   WE: [_a] - numer wersji - domyślnie wszystkie
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('SLO_TYP',HELPJPK)<=0 || return ?};
_wer:={? var_pres('_a')>0 || _a ?};
{? _wer=0
|| exec('init_slo','jpk_v',1)
|? _wer=1
|| _typ:='GTU';
   exec('add_slo','jpk_v',_wer,_typ,'GTU_01',
      'Dostawa napojów alkoholowych',
      'Dostawa napojów alkoholowych – alkoholu etylowego, piwa, wina, napojów fermentowanych i wyrobów pośrednich, w '+
      'rozumieniu przepisów o podatku akcyzowym'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_02','Dostawa towarów, o których mowa w art. 103 ust. 5aa ustawy');
   exec('add_slo','jpk_v',_wer,_typ,'GTU_03',
      'Dostawa oleju opałowego',
      'Dostawa oleju opałowego w rozumieniu przepisów o podatku akcyzowym oraz olejów smarowych, pozostałych olejów o '+
      'kodach CN od 2710 19 71 do 2710 19 99, z wyłączeniem wyrobów o kodzie CN 2710 19 85 (oleje białe, parafina '+
      'ciekła) oraz smarów plastycznych zaliczanych do kodu CN 2710 19 99, olejów smarowych o kodzie CN 2710 20 90, '+
      'preparatów smarowych objętych pozycją CN 3403, z wyłączeniem smarów plastycznych objętych tą pozycją'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_04',
      'Dostawa wyrobów tytoniowych, suszu tytoniowego, płynu do papierosów elektronicznych i wyrobów nowatorskich',
      'Dostawa wyrobów tytoniowych, suszu tytoniowego, płynu do papierosów elektronicznych i wyrobów nowatorskich, w '+
      'rozumieniu przepisów o podatku akcyzowym'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_05',
      'Dostawa odpadów – wyłącznie określonych w poz. 79-91 załącznika nr 15 do ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_06',
      'Dostawa urządzeń elektronicznych oraz części i materiałów do nich',
      'Dostawa urządzeń elektronicznych oraz części i materiałów do nich, wyłącznie określonych w poz. 7-9, 59-63, '+
      '65, 66, 69 i 94-96 załącznika nr 15 do ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_07',
      'Dostawa pojazdów oraz części samochodowych o kodach wyłącznie CN 8701 – 8708 oraz CN 8708 10'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_08',
      'Dostawa metali szlachetnych oraz nieszlachetnych',
      'Dostawa metali szlachetnych oraz nieszlachetnych – wyłącznie określonych w poz. 1-3 załącznika nr 12 do ustawy '+
      'oraz w poz. 12-25, 33-40, 45, 46, 56 i 78 załącznika nr 15 do ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_09',
      'Dostawa leków oraz wyrobów medycznych',
      'Dostawa leków oraz wyrobów medycznych – produktów leczniczych, środków spożywczych specjalnego przeznaczenia '+
      'żywieniowego oraz wyrobów medycznych, objętych obowiązkiem zgłoszenia, o którym mowa w art. 37av ust. 1 ustawy '+
      'z dnia 6 września 2001 r. – Prawo farmaceutyczne (Dz. U. z 2019 r. poz. 499, z późn. zm.)'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_10',
      'Dostawa budynków, budowli i gruntów'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_11',
      'Świadczenie usług w zakresie przenoszenia uprawnień do emisji gazów cieplarnianych',
      'Świadczenie usług w zakresie przenoszenia uprawnień do emisji gazów cieplarnianych, o których mowa w ustawie '+
      'z dnia 12 czerwca 2015 r. o systemie handlu uprawnieniami do emisji gazów cieplarnianych '+
      '(Dz. U. z 2018 r. poz. 1201 i 2538 oraz z 2019 r. poz. 730, 1501 i 1532)'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_12',
      'Świadczenie usług o charakterze niematerialnym',
      'Świadczenie usług o charakterze niematerialnym – wyłącznie: doradczych, księgowych, prawnych, zarządczych, '+
      'szkoleniowych, marketingowych, firm centralnych (head offices), reklamowych, badania rynku i opinii '+
      'publicznej, w zakresie badań naukowych i prac rozwojowych'
   );
   exec('add_slo','jpk_v',_wer,_typ,'GTU_13',
      'Świadczenie usług transportowych i gospodarki magazynowej – Sekcja H PKWiU 2015 symbol ex 49.4, ex 52.1'
   );

   _typ:='PROC';
   exec('add_slo','jpk_v',_wer,_typ,'SW',
      'Dostawa w ramach sprzedaży wysyłkowej z terytorium kraju, o której mowa w art. 23 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'EE',
      'Świadczenie usług telekomunikacyjnych, nadawczych i elektronicznych, o których mowa w art. 28k ustawy',,'OSS'
   );
   exec('add_slo','jpk_v',_wer,_typ,'TP',
      'Istniejące powiązania między nabywcą a dokonującym dostawy towarów lub usługodawcą, o których mowa w art. '+
      '32 ust. 2 pkt 1 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'TT_WNT',
      'Wewnątrzwspólnotowe nabycie towarów dokonane przez drugiego w kolejności podatnika VAT',
      'Wewnątrzwspólnotowe nabycie towarów dokonane przez drugiego w kolejności podatnika VAT w ramach transakcji '+
      'trójstronnej w procedurze uproszczonej, o której mowa w dziale XII rozdziale 8 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'TT_D',
      'Dostawa towarów poza terytorium kraju dokonana przez drugiego w kolejności podatnika VAT',
      'Dostawa towarów poza terytorium kraju dokonana przez drugiego w kolejności podatnika VAT w ramach transakcji '+
      'trójstronnej w procedurze uproszczonej, o której mowa w dziale XII rozdziale 8 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'MR_T',
      'Świadczenie usług turystyki opodatkowane na zasadach marży zgodnie z art. 119 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'MR_UZ',
      'Dostawa towarów używanych, dzieł sztuki, przedmiotów kolekcjonerskich i antyków, opodatkowana na zasadach '+
      'marży zgodnie z art. 120 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'I_42',
      'Wewnątrzwspólnotowa dostawa towarów następująca po imporcie tych towarów w ramach procedury celnej 42 (import)'
   );
   exec('add_slo','jpk_v',_wer,_typ,'I_63',
      'Wewnątrzwspólnotowa dostawa towarów następująca po imporcie tych towarów w o ramach procedury celnej 63 (import)'
   );
   exec('add_slo','jpk_v',_wer,_typ,'B_SPV',
      'Transfer bonu jednego przeznaczenia dokonany przez podatnika działającego we własnym imieniu, opodatkowany '+
      'zgodnie z art. 8a ust. 1 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'B_SPV_DOSTAWA',
      'Dostawa towarów oraz świadczenie usług, których dotyczy bon jednego przeznaczenia na rzecz podatnika, który '+
      'wyemitował bon zgodnie z art. 8a ust. 4 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'B_MPV_PROWIZJA',
      'Świadczenie usług pośrednictwa oraz innych usług dotyczących transferu bonu różnego przeznaczenia, '+
      'opodatkowane zgodnie z art. 8b ust. 2 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'MPP',
      'Transakcja objęta obowiązkiem stosowania mechanizmu podzielonej płatności'
   );

   exec('add_slo','jpk_v',_wer,_typ,'IMP',
      'Oznaczenie dotyczące podatku naliczonego z tytułu importu towarów, w tym importu towarów rozliczanego zgodnie '+
      'z art. 33a ustawy'
   );

   exec('add_slo','jpk_v',_wer,_typ,'WSTO_EE',
      'Wewnątrzwspólnotowa sprzedaż towarów na odległość',,'OSS'
   );
   exec('add_slo','jpk_v',_wer,_typ,'IED',
      'Dostawy dokonywane na terytorium państwa członkowskiego',,'OSS'
   );
   exec('add_slo','jpk_v',_wer,_typ,'SOTI',
      'Sprzedaż na odległość towarów importowanych',,'IOSS'
   );

   _typ:='PROC_FAKS';
   exec('add_slo','jpk_v',_wer,_typ,'EE',
      'Świadczenie usług telekomunikacyjnych, nadawczych i elektronicznych, o których mowa w art. 28k ustawy',,'OSS'
   );

   exec('add_slo','jpk_v',_wer,_typ,'WSTO_EE',
      'Wewnątrzwspólnotowa sprzedaż towarów na odległość',,'OSS'
   );
   exec('add_slo','jpk_v',_wer,_typ,'IED',
      'Dostawy towarów, o których mowa w art. 7a ust. 1  i 2 Ustawy, na terytorium kraju',,'OSS'
   );
   exec('add_slo','jpk_v',_wer,_typ,'SOTI',
      'Sprzedaż na odległość towarów importowanych',,'IOSS'
   );

   _typ:='PROC_FAP';
   exec('add_slo','jpk_v',_wer,_typ,'I_42',
      'Wewnątrzwspólnotowa dostawa towarów następująca po imporcie tych towarów w ramach procedury celnej 42 (import)'
   );
   exec('add_slo','jpk_v',_wer,_typ,'I_63',
      'Wewnątrzwspólnotowa dostawa towarów następująca po imporcie tych towarów w o ramach procedury celnej 63 (import)'
   );
   exec('add_slo','jpk_v',_wer,_typ,'B_SPV',
      'Transfer bonu jednego przeznaczenia dokonany przez podatnika działającego we własnym imieniu, opodatkowany '+
      'zgodnie z art. 8a ust. 1 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'B_SPV_DOSTAWA',
      'Dostawa towarów oraz świadczenie usług, których dotyczy bon jednego przeznaczenia na rzecz podatnika, który '+
      'wyemitował bon zgodnie z art. 8a ust. 4 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'B_MPV_PROWIZJA',
      'Świadczenie usług pośrednictwa oraz innych usług dotyczących transferu bonu różnego przeznaczenia, '+
      'opodatkowane zgodnie z art. 8b ust. 2 ustawy'
   );

   _typ:='TD_S';
   exec('add_slo','jpk_v',_wer,_typ,'RO',
      'Dokument zbiorczy o sprzedaży z kas rejestrujących'
   );
   exec('add_slo','jpk_v',_wer,_typ,'WEW',
      'Dowód wewnętrzny'
   );
   exec('add_slo','jpk_v',_wer,_typ,'FP',
      'Faktura, o której mowa w art. 109 ust. 3d ustawy'
   );

   _typ:='TD_Z';
   exec('add_slo','jpk_v',_wer,_typ,'MK',
      'Faktura wystawiona przez podatnika będącego dostawcą lub usługodawcą, który wybrał metodę kasową rozliczeń '+
      'określoną w art. 21 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'VAT_RR',
      'Faktura VAT RR, o której mowa w art. 116 ustawy'
   );
   exec('add_slo','jpk_v',_wer,_typ,'WEW',
      'Dowód wewnętrzny'
   );

   _typ:='PROC_VAT';
   exec('add_slo','jpk_v',_wer,_typ,'1','Stawka 0% stosowana w ramach sprzedaży krajowej');
   exec('add_slo','jpk_v',_wer,_typ,'2','Stawka 0% - wewnątrzwspólnotowa dostawa towarów');
   exec('add_slo','jpk_v',_wer,_typ,'3','Stawka 0% - eksport towarów');
   exec('add_slo','jpk_v',_wer,_typ,'4','Dostawa, świadczenie poza terytorium kraju');
   exec('add_slo','jpk_v',_wer,_typ,'5','Świadczenie usług z art. 100 ust. 1 pkt 4 ustawy');
   exec('add_slo','jpk_v',_wer,_typ,'6','Towar/usługa wymienione w załączniku 15');
   exec('add_slo','jpk_v',_wer,_typ,'7','Pozostała sprzedaż krajowa');

   JPK_SLO.cntx_psh();
   JPK_SLO.index('KOD');
   JPK_SLO.prefix(_wer,'PROC_S',);
   {? JPK_SLO.first() || {! |? JPK_SLO.del !} ?};
   JPK_SLO.prefix(_wer,'PROC_Z',);
   {? JPK_SLO.first() || {! |? JPK_SLO.del !} ?};
   JPK_SLO.cntx_pop()
?}


\add_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Dodaje pozycje słownika JPK_SLO
::   WE:  _a  - wersja
::        _b  - typ
::        _c  - kod
::        _d  - opis
::       [_e] - pełny opis
::       [_f] - Procedura wspólna
::----------------------------------------------------------------------------------------------------------------------
JPK_SLO.index('KOD');
JPK_SLO.prefix();
_ok:=1;
{? ~JPK_SLO.find_key(_a,_b,_c,)
|| JPK_SLO.blank(1);
   JPK_SLO.WERSJA:=_a;
   JPK_SLO.TYP:=_b;
   JPK_SLO.KOD:=_c;
   JPK_SLO.PROCED:={? var_press('_f')>0 || _f || '' ?};
   _ok:=JPK_SLO.add()
|| {? JPK_SLO.PROCED<>{? var_press('_f')>0 || _f || '' ?}
   || JPK_SLO.PROCED:={? var_press('_f')>0 || _f || '' ?};
      _ok:=JPK_SLO.put()
   ?}
?};
{? _ok
|| _put:=0;
   {? JPK_SLO.OPIS<>_d || JPK_SLO.OPIS:=_d; _put:=1 ?};
   {? var_pres('_e')<=0 || _e:=_d ?};
   {? _e<>JPK_SLO.memo_txt(,1,'MEMO')
   || JPK_SLO.memo_set(_e,'MEMO');
      JPK_SLO.memo_put(,'MEMO');
      _put:=1
   ?};
   {? _put || JPK_SLO.put() ?}
?}


\bl_js_wersja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa JPK_SLO.WERSJA
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.SLO_WER=0
|| JPK_SLO.cntx_psh();
   JPK_SLO.index('KOD');
   {? JPK_SLO.last()
   || HELPJPK.SLO_WER:=JPK_SLO.WERSJA
   ?};
   JPK_SLO.cntx_pop()
?};
HELPJPK.SLO_WER


\bl_js_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa JPK_SLO.TYP
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.SLO_TYP='' || HELPJPK.SLO_TYP:='GTU' ?};
HELPJPK.SLO_TYP


\slo_tyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca opis typu słownika JPK
::   WE: _a - kod typu
::----------------------------------------------------------------------------------------------------------------------
_tyt:='';
{? _a='GTU'
|| _tyt:='Grupy towarów i usług'
|? 4+_a='PROC'
|| _tyt:='Szczególne procedury dla transakcji '
|? 2+_a='TD'
|| _tyt:='Typy dokumentów ';
   {? _a+1='S'
   || _tyt+='sprzedaży'
   |? _a+1='Z'
   || _tyt+='zakupu'
   ?}
?};
_tyt


\bw_jpk_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Okno przed tabeli JPK_SLO
::----------------------------------------------------------------------------------------------------------------------
JPK_SLO.hdr_sel();
JPK_SLO.hdr_sel(exec('slo_tyt','jpk_v',HELPJPK.SLO_TYP));
1


\test
_e:=SYSLOG.mk_edit('Test');
HELPJPK.SLO_TYP:='GTU';
SYSLOG.win_efld(_e,HELPJPK,'JPK_SLO','KOD','KOD');
SYSLOG.win_edit(_e);
SYSLOG.edit()


\obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obiekt do obsługi słownika JPK
::   WE: [_a] - w trybie słownika
::       [_b] - rejestracja dokumentu księgowego
::----------------------------------------------------------------------------------------------------------------------
_slo:={? var_pres('_a')>0 || _a ?};
::VAR_DEL.delete('DokGTU');
{? var_pres('DokGTU')>0
|| _obj:=DokGTU
|| _obj:=obj_new(
      'tab','wer','doktyp','SLO','TAB_REF','SHOW',
      'fill','set_all','set','get'
   );
   DokGTU:=_obj;
   _rej_dok:=var_pres('_b')>0 & _b;
   {? var_pres('JpkV7')<=0
   || exec('init','jpk_v')
   ?};
   VPOZ.cntx_psh();
   VPOZ.index('VDOK');
   VPOZ.prefix(DOK.ref());
   _okres:={? DOK.OKRVAT=null()
           || {? VPOZ.first()
              || {!
                 |? {? VPOZ.OKRVAT=null()
                    || {? VPOZ.OKR_C().POCZ>VPOZ.OKR_Z().POCZ
                       || _okr:=VPOZ.OKR_C().POCZ>=JpkV7.DATA
                       || _okr:=VPOZ.OKR_Z().POCZ>=JpkV7.DATA
                       ?}
                    || _okr:=VPOZ.OKRVAT().POCZ>=JpkV7.DATA
                    ?};
                    ~_okr & VPOZ.next()
                 !};
                 _okr
              || 0
              ?}
           || DOK.OKRVAT().POCZ>=JpkV7.DATA
           ?};
   VPOZ.cntx_pop();
   _obj.SHOW:=var_pres('JPK_GTU',DOK)>0 & JpkV7.DATA<>date(0,0,0) &
             (SSTALE.AO().POCZ>=JpkV7.DATA | _rej_dok & (PAR_SKID.get(211)='T' | _okres));
   _obj.SLO:=_slo;
   _obj.tab:=exec('tab_slo','jpk_v');
   _obj.wer:=_obj.tab.win_sel('?');
   _obj.fill:="exec('fill_slo','jpk_v',.tab)";
   _obj.set_all:="
      _tab:=.tab;
      {? var_pres('_a')>0 || .set('GTU',_a) ?};
      {? var_pres('_b')>0 || .set('PROC',_b) ?};
      _tab.prefix();
      _tab.first()
   ";
   _obj.set:="
      _tab:=.tab;
      _tab.cntx_psh();
      _tab.index(__ndxkod);
      _tab.prefix(_a,);
      {? _tab.first()
      || {!
         |? _tab.SEL:=0;
            _tab.EDIT:=_tab.TYP='GTU';
            _tab.put();
            _tab.next()
         !}
      ?};
      _lista:=spli_str(_b,',');
      {! _ii:=1..obj_len(_lista)
      |! {? _lista[_ii]<>'' & _tab.find_key(_lista[_ii])
         || _tab.SEL:=1;
            _tab.put()
         ?}
      !};
      _edit_str:='B_MPV_PROWIZJA,B_SPV,B_SPV_DOSTAWA,IMP,I_42,I_63';
      _edit:=spli_str(_edit_str,',');
      {! _ii:=1..obj_len(_edit)
      |! {? _edit[_ii]<>'' & _tab.find_key(_edit[_ii])
         || _tab.EDIT:=1;
            _tab.put()
         ?}
      !};
      _dok_str:=exec('get_dok_jpk_proc','dok_ksef');
      _dok:=spli_str(_dok_str,',');
      {! _ii:=1..obj_len(_dok)
      |! {? _dok[_ii]<>'' & _dok[_ii]<>'TP' & _tab.find_key(_dok[_ii])
         || _tab.SEL:=1;
            _tab.EDIT:=-1;
            _tab.put()
         ?}
      !};
      _tab.cntx_pop()
   ";
   _obj.get:="
      _tab:=.tab;
      _txt:='';
      _tab.cntx_psh();
      _tab.prefix(_a,);
      {? _tab.first()
      || {!
         |? {? _tab.SEL
            || _txt+=_tab.KOD+','
            ?};
            _tab.next()
         !}
      ?};
      _tab.cntx_pop();
      _txt-1
   "
?};
_obj


\tab_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca tabelę do przechowywania informacji o pozycjach słownika
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(2,
   'TYP','STRING[10]','Typ',
   'KOD','STRING[20]','Kod',
   'OPIS','STRING[255]','Opis',
   'PROCED','INTEGER','Czy procedura wspólna',
   'PROCTXT','STRING[20]','Procedura wspólna',
   'SEL','INTEGER','Zaznaczenie',
   'REF','INTEGER','Ref',
   'EDIT','INTEGER','Edycja'
);
__ndxprc:=_tab.ndx_tmp(,,'TYP',,,'PROCED',,);
__ndxkod:=_tab.ndx_tmp(,,'TYP',,,'KOD',,);
_o:=_tab.mk_sel('','P',,'#jpkslodok',,,,,'U');
_tab.win_fld(_o,,'SEL',,,3,,,'Wybrano',,,2,,"1","0");
_tab.win_fld(_o,,'KOD');
_tab.win_fld(_o,,'OPIS',,,100);
_tab.win_fld(_o,,'PROCTXT',,,10);
_tab.win_act(_o,,'Formuła','Wybierz',,,"
   {? {? DOK.f_active() || DOK.f_get() || DOK.get() ?}
   || {? PAR_SKID.get(211)='T' & ~((PAR_SKID.get(107)<>'T' & DOK.r_lock(0,1)) | DOK.r_lock(1,1))
      || FUN.info('Dokument obsługuje inny operator.'@);
         return(0)
      ?}
   ?};
   {? korekta=0 || korekta:=FUN.ask('Czy na pewno chcesz dokonać korekty danych?') ?};
   {? korekta=0 || return() ?};
   _tab:=cur_tab(1,1);
   {? _tab.KOD='EE' & _tab.SEL=0 & exec('chk_ee_v7','jpk_v') || return() ?};
   _tab.SEL:=~_tab.SEL;
   _tab.put();
   {? DokGTU.SLO=0
   || {? _tab.KOD='MPP' & var_pres('SP_WYM',DOK)>0 & PAR_SKID.get(106)='T'
      || DOK.SP_WYM:={? _tab.SEL || 'T' || 'N' ?}
      ?};
      _typ:=_tab.TYP;
      _txt:=DokGTU.get(_typ);
      {? _typ='GTU'
      || DOK.JPK_GTU:=_txt
      || DOK.JPK_PROC:=_txt
      ?};
      _maska:=ref_name(DOK.ref());
      DOK_JPK.use('dokj'+(_maska+4));
      DOK_JPK.index('UNIK');
      DOK_JPK.prefix(DOK.ref(),'',);
      {? ~DOK_JPK.first()
      || DOK_JPK.index('TIME');
         DOK_JPK.prefix(DOK.ref());
         _gtu:=_proc:='';
         {? DOK_JPK.last()
         || _gtu:=DOK_JPK.JPK_GTU;
            _proc:=DOK_JPK.JPK_PROC
         ?};
         DOK_JPK.blank();
         DOK_JPK.OPERATOR:=exec('usr_zar','dok_fks');
         DOK_JPK.JPK_GTU:=_gtu;
         DOK_JPK.JPK_PROC:=_proc;
         {? _typ='GTU'
         || DOK_JPK.JPK_GTU:=_txt
         || DOK_JPK.JPK_PROC:=_txt
         ?};
         DOK_JPK.DATE:=date();
         DOK_JPK.TIME:=time();
         DOK_JPK.DOK:=DOK.ref();
         DOK_JPK.add()
      || {? _typ='GTU'
         || DOK_JPK.JPK_GTU:=_txt
         || DOK_JPK.JPK_PROC:=_txt
         ?};
         DOK_JPK.DATE:=date();
         DOK_JPK.TIME:=time();
         DOK_JPK.OPERATOR:=exec('usr_zar','dok_fks');
         DOK_JPK.put();
         DOK_JPK.index('TIME');
         DOK_JPK.prefix(DOK.ref());
         _gtu:=_proc:='';
         {? DOK_JPK.last()
         || _gtu:=DOK_JPK.JPK_GTU;
            _proc:=DOK_JPK.JPK_PROC;
            {? DOK_JPK.prev()
            || {? _gtu=DOK_JPK.JPK_GTU & _proc=DOK_JPK.JPK_PROC
               || DOK_JPK.next(); DOK_JPK.del()
               ?}
            ?}
         ?}
      ?};
      DOK.put()
   ?}
",,1,1);
_tab.win_act(_o,,'Wyświetl',,,,"
   _tab:=cur_tab(1,1);
   JPK_SLO.cntx_psh();
   JPK_SLO.prefix();
   {? JPK_SLO.seek(_tab.REF,)
   || JPK_SLO.win_edit('RED');
      JPK_SLO.hdr_edit(); JPK_SLO.hdr_edit(exec('slo_tyt','jpk_v',JPK_SLO.TYP));
      JPK_SLO.display()
   ?};
   JPK_SLO.cntx_pop()
");
{? DokGTU.SLO
|| _tab.win_act(_o,,'Formuła','Dalej',,,"
      sel_exit()
   ")
?};
_tab.win_act(_o,,'Rekord',,,,"
   _tab:=cur_tab(1,1);
   {? _tab.TYP='PROC'
   || _gray:='';
      {? _tab.EDIT<>1 & DOK.A<>'T'
      || _gray:='W'
      ?};
      _tab.actions_grayed(_tab.win_sel('?'),_gray);
      {? _tab.EDIT=1
      || Color.fnd_kol('GtuProc#01#01')
      |? _tab.EDIT=0
      || Color.fnd_kol('GtuProc#01#02')
      || Color.fnd_kol('GtuProc#01#03')
      ?}
   || _tab.actions_grayed(_tab.win_sel('?'));
      ''
   ?}
");
_tab.win_act(_o,,'Formuła','Legenda',,,"
   exec('legenda','color','GtuProc#01#')
");
_tab.win_sel(_o);
_tab


\fill_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zasila tabelę pozycji słownika
::   WE: _a - tabela
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('JPK_SLO')<=0 || return() ?};
_tab:=_a;
{? ~_tab.first()
|| _wer:=exec('bl_js_wersja','jpk_v');
   JPK_SLO.cntx_psh();
   JPK_SLO.index('KOD'); JPK_SLO.prefix(_wer);
   {? JPK_SLO.first()
   || {!
      |? _tab.blank(1);
         _tab.TYP:=JPK_SLO.TYP;
         _tab.KOD:=JPK_SLO.KOD;
         _tab.OPIS:=JPK_SLO.OPIS;
         _tab.PROCED:={? JPK_SLO.PROCED<>'' || 1 || 0 ?};
         _tab.PROCTXT:={? JPK_SLO.PROCED<>'' || JPK_SLO.PROCED || '' ?};
         _tab.REF:=#JPK_SLO.ref();
         _tab.EDIT:=JPK_SLO.TYP='GTU';
         _tab.add();
         JPK_SLO.next()
      !}
   ?};
   JPK_SLO.cntx_pop()
?}


\gtu4dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Dodaje okno z GTU do okna z pozycjami dokumentu
::   WE:  _a  - tabela okna grupowego
::        _b  - akronim okna grupowego
::       [_c] - odświeżanie w zakładkach
::       [_d] - obie zakładki
::       [_e] - ikony w zakładkach
::       [_f] - sprawdzanie okresu VAT dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('JPK_GTU',DOK)>0
|| _tab:=_a;
   _wer:=_b;
   _dwie:=var_pres('_d')>0 & _d;
   _ikony:=0;
   _obj:=exec('obj','jpk_v',,var_pres('_f')>0 & _f);
   _obj.fill();
   _rt:=DOK.RVAT().RVAT().RT;
   _obj.doktyp:=exec('typ','jpk_v',DOK.RVAT().RVAT);
   _obj.TAB_REF:=var_pres('_c')>0 & _c;
   {? _obj.TAB_REF=0
   || _obj.set_all(DOK.JPK_GTU,DOK.JPK_PROC)
   ?};
   {? _obj.SHOW
   || {? _dwie | _obj.doktyp='S'
      || _tab.grp_sel(_wer,_obj.tab,_obj.wer,'Grupy towarów i usług',"",0,0,,$("
            _tab:=cur_tab(1,1);
            {? DokGTU.TAB_REF || DokGTU.set_all(DOK.JPK_GTU) ?};
            _tab.prefix('GTU');
            _tab.first();
            _tab.actions(DokGTU.wer,'L'+{? (DOK.A='T' & PAR_SKID.get(107)<>'T') | DOK.DOK_REJ().JPK_EGTU<>'T' | var_press('PozView')<0 | PozView<>0 || 'W' || '' ?},,1);"
            +{? var_pres('objZakl')>0 || $(' zakl_dok:=%1;'[$(objZakl.addZakl('JPK','GRUPY'))]) || "" ?}
            +" {? PAR_SKID.get(211)='T' || DOK.r_lock(1,1) ?};
            1
         "),"{? PAR_SKID.get(211)='T' || {? DOK.r_lock(0,1) | DOK.r_lock(1,1) || DOK.r_unlock() ?} ?}",,,'maximized')
      ?};
      _tab.grp_sel(_wer,_obj.tab,_obj.wer,'Szczególne procedury transakcji',"",0,0,,$("
         _tab:=cur_tab(1,1);
         {? DokGTU.TAB_REF || DokGTU.set_all(,DOK.JPK_PROC) ?};
         _tab.index(__ndxprc);
         _tab.prefix('PROC',);
         _tab.first();
         _tab.actions(DokGTU.wer,{? (DOK.A='T' & PAR_SKID.get(107)<>'T') | ~exec('edit_proc','dok_fks1')  | var_press('PozView')<0 | PozView<>0 || 'W' || '' ?},,1);"
         +{? var_pres('objZakl')>0 || $(' zakl_dok:=%1;'[$(objZakl.addZakl('JPK','PROC'))]) || "" ?}
         +" {? PAR_SKID.get(211)='T' || DOK.r_lock(1,1) ?};
         1
      "),"{? PAR_SKID.get(211)='T' || {? DOK.r_lock(0,1) | DOK.r_lock(1,1) || DOK.r_unlock() ?} ?}",,,'maximized')
   ?}
?};
~~


\be_dok_jvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Przed redakcją pola DOK.JPK_V_T
::----------------------------------------------------------------------------------------------------------------------
_ret:=DOK.DOK_REJ().JPK_ETYP;
{? _ret='T'
|| _kod:=DOK_REJ.SLO().KOD;
   {? _kod='SAD' | _kod='VAT'
   || _typ:='';
      VAT_REJ.cntx_psh();
      VAT_REJ.index('REJ_SYM'); VAT_REJ.prefix(DOK_REJ.REJ);
      {? VAT_REJ.first()
      || _ts:=_tz:=0;
         {!
         |? {? exec('typ','jpk_v',VAT_REJ.RVAT)='S' || _ts:=1 || _tz:=1 ?};
            VAT_REJ.next()
         !}
      || _ts:=_tz:=1
      ?};
      VAT_REJ.cntx_pop();
      HELPJPK.SLO_TYP:='TD'+{? _ts & _tz || '' |? _ts || '_S' || '_Z' ?};
      1
   ?}
|| 0
?}


\be_dr_jvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją pola DOK_REJ.JPK_V_T
::----------------------------------------------------------------------------------------------------------------------
_kod:=DOK_REJ.SLO().KOD;

{? _kod='SAD' | _kod='VAT'

|| _typ:='';
   VAT_REJ.cntx_psh();
   VAT_REJ.index('REJ_SYM'); VAT_REJ.prefix(DOK_REJ.REJ);
   {? VAT_REJ.first()
   || _ts:=_tz:=0;
      {!
      |? {? exec('typ','jpk_v',VAT_REJ.RVAT)='S' || _ts:=1 || _tz:=1 ?};
         VAT_REJ.next()
      !}
   || _ts:=_tz:=1
   ?};
   VAT_REJ.cntx_pop();
   HELPJPK.SLO_TYP:='TD'+{? _ts & _tz || '' |? _ts || '_S' || '_Z' ?};
   1
?}


\f3_dr_jvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla pola DOK_REJ.JPK_V_T
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.SLO_WER:=exec('bl_js_wersja','jpk_v');
JPK_SLO.index('KOD'); JPK_SLO.prefix(HELPJPK.SLO_WER);
_tab:=sql(
   'select distinct KOD, OPIS, \'Sprzedaż i zakup\' as TYP '+
   'from JPK_SLO '+
   'where WERSJA=:_a and (TYP like \':_b\') '+
   'order by 1',
   HELPJPK.SLO_WER,HELPJPK.SLO_TYP+'%'
);
{? _tab.first()
|| {!
   |? _s:=JPK_SLO.find_key('TD_S',_tab.KOD,);
      _z:=JPK_SLO.find_key('TD_Z',_tab.KOD,);
      {? _s & _z=0
      || _tab.TYP:='Sprzedaż'; _tab.put()
      |? _s=0 & _z
      || _tab.TYP:='Zakup'; _tab.put()
      ?};
      _tab.next()
   !}
?};
_o:=_tab.mk_sel(exec('slo_tyt','jpk_v',HELPJPK.SLO_TYP),'P',,'#jpkslosel',,,,,'U');
_tab.win_fld(_o,,'KOD',,,,,,'Kod');
{? HELPJPK.SLO_TYP='TD'
|| _tab.win_fld(_o,,'TYP',,,,,,'Typ')
?};
_tab.win_fld(_o,,'OPIS',,,100,,,'Opis');
_tab.win_act(_o,,'Formuła','Wybierz',,,"sel_exit()",,1);
_tab.win_act(_o,,'Wyświetl',,,,"
   JPK_SLO.win_edit('RED');
   JPK_SLO.index('KOD');
   JPK_SLO.prefix(HELPJPK.SLO_WER);
   {? JPK_SLO.find_key('TD_S',cur_tab().KOD,) | JPK_SLO.find_key('TD_Z',cur_tab().KOD,)
   || JPK_SLO.display()
   ?}
");
_tab.win_sel(_o);
{? fld<>'' || _tab.find_key(fld(),) | _tab.first() || _tab.first() ?};
{? _tab.select(,1,5)
|| fld(_tab.KOD)
?}


\ae_dr_jvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji pola DOK_REJ.JPK_V_T
::----------------------------------------------------------------------------------------------------------------------
{? fld()='' || return(1) ?};
_wer:=exec('bl_js_wersja','jpk_v');
JPK_SLO.index('KOD'); JPK_SLO.prefix(_wer);
{? HELPJPK.SLO_TYP='TD' & (JPK_SLO.find_key('TD_S',fld()) | JPK_SLO.find_key('TD_Z',fld())) |
   HELPJPK.SLO_TYP<>'TD' & JPK_SLO.find_key(HELPJPK.SLO_TYP,fld())
|| fld(JPK_SLO.KOD);
   1
|| FUN.info('Nie znaleziono typu dokumentu: '+fld()+'.');
   0
?}


\mpp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Aktualizuje MMP w procedurach na podstawie znacznika wymagalności SP
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('SP_WYM',DOK)>0 & var_pres('JPK_PROC',DOK)>0 & PAR_SKID.get(106)='T'
|| {? DOK.SP_WYM='T'
   || {? DOK.JPK_PROC*'MPP'=0
      || DOK.JPK_PROC+={? DOK.JPK_PROC<>'' || ',' || '' ?}+'MPP'
      ?}
   || {? (_poz:=DOK.JPK_PROC*'MPP')
      || DOK.JPK_PROC:=((_poz-1)+DOK.JPK_PROC)+((_poz+3)-DOK.JPK_PROC)
      ?};
      {? DOK.JPK_PROC+1=',' || DOK.JPK_PROC:=DOK.JPK_PROC-1 ?}
   ?}
?}


\vat_dok_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia dane dokumentu
::----------------------------------------------------------------------------------------------------------------------
exec('proc_upr','jpk');
VAT_PS.JPK_TD


\vat_gtu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca znacznik grupy towarów
::----------------------------------------------------------------------------------------------------------------------
{? VAT_PS.TYP=' ' | VAT_PS.TYP='J'
|| {? 3+ISTDEFS.OPIS='GTU'
   || _txt:=','+VAT_PS.JPK_GTU+',';
      {? exec('upr_gtu_del','jpk_v') & _txt*(','+ISTDEFS.OPIS+',') || '1' || '' ?}
   || _txt:=','+VAT_PS.JPK_PROC+',';
      _txt+=exec('jpk_upr_grudz','jpk_v',_txt);
      _txt+=exec('jpk_mapowanie','jpk_v',_txt);
      {? exec('del_mpp','jpk_v') & _txt*(','+ISTDEFS.OPIS+',') || '1' || '' ?}
   ?}
|| ''
?}


\jpk2html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Transormuje plik JPK do html
:: ~OST: INBLGET,INCLIEXEC,INFCOPY,INFERASE,INFEXISTS,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
_interm:=exec('interm','#system');
{? _interm
|| _tmpdir:=fmk_tmp_dir(0);
{? type_of(_tmpdir) <> type_of(~~)
|| _fdir:=_tmpdir.get_path
|| _fdir:=''
?};
   _fc:="{? ~fexists(_b+'/'+_a) || fcopy(_a,_b+'/'+_a,1,0,1) || 1 ?}"
|| _fc:="{? ~fexists('@'+tmp_dir()+'/'+_a) || fcopy(_a,'@'+tmp_dir()+'/'+_a,1,0,1) || 1 ?}";
   _fdir:=tmp_dir()
?};
_fxsl:=JPK.ISTDEF().VER+'.xsl';
_fxsl2:=JPK.ISTDEF().VER+'_dek.xsl';
_fin:=_fdir+'/jpk.xml';
_fout:=_fdir+'/jpk.html';

{? fexists(_fxsl,1)
|| _pyt:=1;
   {? 2+JPK.ISTDEF().RODZAJ='V7' & fexists(_fxsl2,1)
   || _pyt:=FUN.choice('Którą część pliku wydrukować?'@,1,'Deklaracyjną i ewidencyjną'@,'Deklaracyjną'@);
      {? _pyt=2
      || _fxsl:=_fxsl2
      ?}
   ?};
   {? _pyt
   || _fc(_fxsl,_fdir);
      JPK.bl_get('PLIK',{? _interm || '' || '@' ?}+_fin,0);
      {? _interm
      || exec('xml2html','xml',_fin,_fdir+'/'+_fxsl,_fout,_fdir)
      || exec('xml2html','xml',_fin,_fdir+'/'+_fxsl,_fout)
      ?};
      _us:=exec('us_name','jpk_v',{? _interm || '' || '@' ?}+_fin);
      exec('replace','xml',_fout,50*'Y',_us);
      exec('replace','xml',_fout,32*'#',JPK.REF_ID);
      ferase({? _interm || '' || '@' ?}+_fdir+'/'+_fxsl,0);
      ferase({? _interm || '' || '@' ?}+_fin,0);
      {? _interm || dlg_save(_fout) || cli_exec(_fout) ?}
   ?}
|| echo('Brak na serwerze pliku '+_fxsl+'.');
   FUN.info('Wydruk niedostępny.');
   echo()
?}


\win_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tworzy okno pozycji JPK_V7
::----------------------------------------------------------------------------------------------------------------------
_grp:=VAT_POZ.grp_make('Pozycje',,'vatdekjpk');
VAT_POZ.grp_sel(_grp,,'V7','Deklaracja',,,,,,,,,'maximized');
VAT_POZ.grp_sel(_grp,VAT_PS,'WER','Szczegóły',,,,,"
   VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
   VAT_PS.index('VAT_PS');
   VAT_PS.prefix(VAT_DEK.ref());
   VAT_PS.first();
   VAT_PS.actions('WER','DAPU:DA',,1);
   1
","
   VAT_PS.actions('WER')
",,1,'maximized');
_grp


\start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Data od kiedy można tworzyć JPK_V7M/K
::----------------------------------------------------------------------------------------------------------------------
_data:=PAR_SKID.get(105);
date(#(4+_data),#(5-_data-3),#(_data+2))


\valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Sprawdza poprawność uzupełnienia słowników JPK
::   WE: _a - typ słownika
::       _b - lista kodów słownika oddzielona znakiem ','
::   WY: Niepoprawny kod słownika lub '' gdy ok
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
_wer:=exec('bl_js_wersja','jpk_v');
JPK_SLO.cntx_psh();
JPK_SLO.index('KOD');
JPK_SLO.prefix(_wer,_a,);
_tab:=spli_str(_b,',');
{! _ii:=1..obj_len(_tab)
|? _ret=''
|! {? ~JPK_SLO.find_key(_tab[_ii],)
   || _ret:=_tab[_ii]
   ?}
!};
JPK_SLO.cntx_pop();
_ret


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia zmienne do obsługi JPK_V7
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('JpkV7')<=0
|| JpkV7:=obj_new('MrT','MrUz','DATA')
?};
:: Wartość zmiennych określa sposób zachowania dokumentów ze szczególnymi procedurami transakcji dla MR_T oraz MR_UZ
:: w pliku JPK_v7
::
:: wartość 1 (domyślna)
::           powoduje ujęcie dokumetów w częsci deklaracyjnej i ewidencyjnej
:: wartośc 0
::           powoduje, że dokumenty nie będą ujęte w częsci deklaracyjnej a w częsci ewidencyjnej tylko
::           z wartością w polach marży (wyjątkiem jest dokument oznaczony typem WEW)
JpkV7.MrT:=1;
JpkV7.MrUz:=1;
JpkV7.DATA:=exec('start','jpk_v');
JpkV7


\dvat_pomin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia pole DVAT.POMIN
::   WE: [_a] - typ dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? DVAT.OBR().POCZ>=JpkV7.DATA
|| _typ:={? var_pres('_a')>0 || _a || DOK.DOK_REJ().JPK_V_T ?};
   _typ='FP' |
   JpkV7.MrT=0 & DOK.JPK_PROC*'MR_T' & _typ<>'WEW' |
   JpkV7.MrUz=0 & DOK.JPK_PROC*'MR_UZ' & _typ<>'WEW'
?}


\vat_pomin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy pomijać pozycję w JPK_V7
::   WE: _a - 1-wiersz 2-suma
::----------------------------------------------------------------------------------------------------------------------
2+JPK.TYP='V7' & var_pres('JPK_TD',VAT_PS)>0 &
(
_a=2 & VAT_PS.JPK_TD='FP' |
JpkV7.MrT=0 & VAT_PS.JPK_PROC*'MR_T' & VAT_PS.JPK_TD<>'WEW' |
JpkV7.MrUz=0 & VAT_PS.JPK_PROC*'MR_T' & VAT_PS.JPK_TD<>'WEW'
)


\ks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obsługa definicji struktur księgowych
::   WE: _a - 0-przed 1-po
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('JPK_V_T',DOK_REJ)<=0 || return() ?};
{? var_pres('JPK_V_T',DOK)>0 || return() ?};
{? _a=0
|| VAR_DEL.delete('JpkKS');
   JpkKS:=sql(
      'select DOK_REJ.JPK_V_T as OLD, DOK_REJ.JPK_V_T as NEW, DOK_REJ.REFERENCE as REF, 0 as Z '+
      'from DOK_REJ join REJ '+
      'where REJ.ROK=:_a '+
      'order by REF',
      SSTALE.AR
   )
|| {? JpkKS.first()
   || _jest:=0;
      DOK_REJ.cntx_psh();
      DOK_REJ.prefix();
      {!
      |? {? DOK_REJ.seek(BIT.sqlint(JpkKS.REF),) & DOK_REJ.JPK_V_T<>JpkKS.NEW &
            ('FP,WEW'*DOK_REJ.JPK_V_T | 'FP,WEW'*JpkKS.NEW)
         || JpkKS.Z:=1;
            JpkKS.NEW:=DOK_REJ.JPK_V_T;
            _jest:=1;
            JpkKS.put()
         ?};
         JpkKS.next()
      !};
      DOK_REJ.cntx_pop();
      {? _jest
      || DVAT.cntx_psh();
         DVAT.use('dvat__'+SSTALE.AR().KOD);
         _sql:=sql(
            'select DVAT.REFERENCE as REF, DVAT.DOK, DOK.DOK_REJ '+
            'from DVAT join @DOK '+
            'where DOK.DOK_REJ in (select REF from :_a where Z=1)',
            JpkKS
         );
         {? _sql.first()
         || _pyt:=~choice(
               '~~Zmieniono typ dokumentu dla JPK_V7, '+
               'co wpływa na dokumenty VAT.\n'+
               'Czy uaktualnić dokumenty VAT?'
               ,FUN.TYT,,,0,2,'Tak, uaktualnić','Nie, przywrócić typy dokumentów'
            );
            {? _pyt
            || exec('init','jpk_v');
               _ile:=_sql.size();
               _lp:=0;
               DVAT.prefix();
               {!
               |? {? DVAT.seek(BIT.sqlint(_sql.REF),)
                  || DOK.cntx_psh();
                     DOK.use(ref_name(DVAT.DOK));
                     DOK.prefix();
                     {? DOK.seek(DVAT.DOK)
                     || _pomin:=exec('dvat_pomin','jpk_v',DOK.JPK_V_T);
                        {? DVAT.POMIN<>_pomin
                        || DVAT.POMIN:=_pomin;
                           DVAT.put()
                        ?}
                     ?};
                     DOK.cntx_pop()
                  ?};
                  _lp+=1;
                  echo('Aktualizacja dokumentów VAT: '+$_lp+' z '+$_ile);
                  _sql.next()
               !};
               echo()
            || _i2:=JpkKS.ndx_tmp('',1,'Z',,0);
               JpkKS.index(_i2);
               JpkKS.prefix(1);
               {? JpkKS.first()
               || DOK_REJ.cntx_psh();
                  DOK_REJ.prefix();
                  {!
                  |? {? DOK_REJ.seek(BIT.sqlint(JpkKS.REF),) & DOK_REJ.JPK_V_T=JpkKS.NEW
                     || DOK_REJ.JPK_V_T:=JpkKS.OLD;
                        DOK_REJ.put()
                     ?};
                     JpkKS.next()
                  !};
                  DOK_REJ.cntx_pop()
               ?}
            ?}
         ?};
         DVAT.cntx_pop()
      ?}
   ?};
   VAR_DEL.delete('JpkKS')
?}


\typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Typ dokumentu na podtsawie rejestru VAT
::   WE: _a - wskazanie na rejestr VAT (_RVAT)
::   WY: Typ dokumentu: S-sprzedaż Z-zakup
::  OLD: \typ/jpk_v.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:='S';
RVAT.cntx_psh();
RVAT.prefix();
{? RVAT.seek(_a)
|| _rt:=RVAT.RT;
   _typ:={? 'SE'*_rt || 'S'
         |? 'ZI'*_rt || 'Z'
         |? 2+RVAT.KVAT().SYM='_W' & 1+(5-KVAT.SYM)='N' | -KVAT.SYM*'zak' || 'Z'
         || 'S'
         ?}
?};
RVAT.cntx_pop();
_typ


\synt_jpk_v7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.xx]
:: OPIS: Formuła dla wydruku syntetycznego JPK_V7
::----------------------------------------------------------------------------------------------------------------------
VAT_POZ.cntx_psh(); VAT_PS.cntx_psh();
VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
:: czy aktywne są uproszczenia do JPK_V7
__upr:=0;
_okres:=VAT_DEK.OKRES;
{? 1+(_okres+2)='/'
|| _mc:=(#(_okres+1)-1)*3+1
|| _mc:=#(_okres+2)
?};
_data:=date(#(4+_okres),_mc,1);
{? _data>=exec('data_upr','jpk_v')
|| __upr:=1
?};
HELPJPK.RODZAJ:=3+VAT_DEK.TYP;
_mc:={? 1+(VAT_DEK.OKRES+3)='/'
     || #(VAT_DEK.OKRES+2)
     || _kw:=#(VAT_DEK.OKRES+1);
        _kw*3
     ?};
_date:=date(#(4+VAT_DEK.OKRES),_mc,1);
OKRO_F.cntx_psh();
OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
{? OKRO_F.find_ge(_date)
|| HELPJPK.OKR_OD:=OKRO_F.ref()
|| HELPJPK.OKR_OD:=SSTALE.AO
?};
OKRO_F.cntx_pop();
HELPJPK.KOR:=(VAT_DEK.TYP+3)='KOR';
exec('find_sch','jpk',HELPJPK.OKR_OD().POCZ);

exec('gen_jpk_syn','jpk_v');
_grp:=exec('win_jpk_syn','jpk_v');
_ndx:=JPK_SYN.ndx_tmp(,,'ZAKL',,,'KOL',,);
JPK_SYN.index(_ndx);
JPK_SYN.win_sel(_grp);
JPK_SYN.select();
VAT_POZ.cntx_pop(); VAT_PS.cntx_pop();
VAR_DEL.delete('__upr');
1


\gen_jpk_syn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.xx]
:: OPIS: Formuła tworząca tabele przechowujące zestawienia
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('JPK_SYN','SYN_TMP');
JPK_SYN:=tab_tmp(3,'ZAKL','INTEGER','Zakładka',
                   'KOD','STRING[16]','Symbol',
                   'NAZWA','STRING[255]','Nazwa',
                   'STR','STRING[50]','Wartość',
                   'VALUE','REAL','Wartość',
                   'VALUE2','REAL','Wartość2',
                   'KOL','INTEGER','Kolejność w zakładce'
                );
SYN_TMP:=tab_tmp(11,'TYP','STRING[1]','Typ',
                    'OKRO_F','INTEGER','Okres',
                    'NIP','STRING[15]','NIP',
                    'KH','STRING[150]','Kontrahent',
                    'ADRES','STRING[255]','Adres',
                    'DATA','DATE','Data',
                    'DATA2','DATE','Data 2',
                    'SYM','STRING[35]','Symbol',
                    'KOR','INTEGER','Korekta',
                    'REJ_KS','STRING[16]','Rejestr księgowy',
                    'NR_KS','INTEGER','Numer księgowy'
                );
exec('pocz_jpk_syn','jpk_v');
1


\pocz_jpk_syn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.xx]
:: OPIS: Formuła tworząca struktury JPK_SYN oraz wstępnie wypełniająca je
::----------------------------------------------------------------------------------------------------------------------
JPK_SYN.ZAKL:=6; _kol:=0;

_wer:=exec('bl_js_wersja','jpk_v');
JPK_SLO.cntx_psh();
JPK_SLO.index('KOD');
JPK_SLO.prefix(_wer,'GTU',);
{? JPK_SLO.first()
|| {!
   |?
      JPK_SYN.KOD:=JPK_SLO.KOD;
      JPK_SYN.NAZWA:=JPK_SLO.OPIS;
      JPK_SYN.STR:='0';
      _kol+=1; JPK_SYN.KOL:=_kol;
      JPK_SYN.add();
      JPK_SLO.next()
   !}
?};

JPK_SYN.ZAKL:=7; _kol:=0;

JPK_SLO.index('KOD');
JPK_SLO.prefix(_wer,'PROC',);
{? JPK_SLO.first()
|| {!
   |? JPK_SYN.KOD:=JPK_SLO.KOD;
      JPK_SYN.NAZWA:=JPK_SLO.OPIS;
      JPK_SYN.STR:='0';
      _kol+=1; JPK_SYN.KOL:=_kol;
      JPK_SYN.add();
      JPK_SLO.next()
   !}
?};
JPK_SLO.cntx_pop();

JPK_SYN.ZAKL:=1; _kol:=0;

JPK_SYN.KOD:='IDPOD';
JPK_SYN.NAZWA:='Identyfikator podatkowy NIP podatnika';
JPK_SYN.STR:=XINFO.NIP;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='NRDOK';
JPK_SYN.NAZWA:='Numer wersji deklaracji';
JPK_SYN.STR:=$VAT_DEK.WER;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='MSC';
JPK_SYN.NAZWA:='Miesiąc';
JPK_SYN.STR:=VAT_DEK.OKRES+2;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='ROK';
JPK_SYN.NAZWA:='Rok';
JPK_SYN.STR:=4+VAT_DEK.OKRES;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='DATA';
JPK_SYN.NAZWA:='Data utworzenia';
JPK_SYN.STR:=$VAT_DEK.DATA;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='US';
JPK_SYN.NAZWA:='Urząd skarbowy, do której jest adresowany plik JPK';
JPK_SYN.STR:={? var_press('US_JPK',XINFO)>0 & XINFO.US_JPK<>null()
|| XINFO.US_JPK().NU
|| XINFO.URZAD().NU
?};
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='KOR';
JPK_SYN.NAZWA:='Cel złożenia';
JPK_SYN.STR:={? VAT_DEK.TYP+3<>'KOR' || 'Złożenie deklaracji JPK_V7' || 'Korekta deklaracji JPK_V7' ?};
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='RODZPOD';
JPK_SYN.NAZWA:='Rodzaj podatnika';
JPK_SYN.STR:={? VAT_DEK.POD=1 || 'Osoba fizyczna' || 'Niebędący osobą fizyczną' ?};
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();

{? VAT_DEK.POD=1
|| JPK_SYN.KOD:='NAZWISKO';
   JPK_SYN.NAZWA:='Nazwisko';
   JPK_SYN.STR:=VAT_DEK.NAZ;
   JPK_SYN.add();
   JPK_SYN.KOD:='IMIE';
   JPK_SYN.NAZWA:='Imię';
   JPK_SYN.STR:=VAT_DEK.IMIE;
   JPK_SYN.add();
   JPK_SYN.KOD:='DATA_UR';
   JPK_SYN.NAZWA:='Data urodzenia';
   JPK_SYN.STR:=$VAT_DEK.DATA_UR;
   JPK_SYN.add()
|| JPK_SYN.KOD:='NAZWA';
   JPK_SYN.NAZWA:='Nazwa pełna';
   JPK_SYN.STR:=XINFO.NAZ;
   JPK_SYN.add()
?};
JPK_SYN.KOD:='TEL';
JPK_SYN.NAZWA:='Telefon kontaktowy';
JPK_SYN.STR:=VAT_DEK.TEL;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='MAIL';
JPK_SYN.NAZWA:='Adres e-mail';
JPK_SYN.STR:=VAT_DEK.EMAIL;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();

JPK_SYN.STR:='0';
JPK_SYN.ZAKL:=2; _kol:=0;

VAT_POZ.index('VAT_POZ');
VAT_POZ.prefix(VAT_DEK.ref(),'C');
{? VAT_POZ.first()
|| {!
   |? JPK_SYN.KOD:=VAT_POZ.LP;
      JPK_SYN.NAZWA:=VAT_POZ.OP;
      JPK_SYN.VALUE:=VAT_POZ.NETTO;
      JPK_SYN.VALUE2:=VAT_POZ.VAT;
      _kol+=1; JPK_SYN.KOL:=_kol;
      JPK_SYN.add();
      VAT_POZ.next
   !}
?};

JPK_SYN.ZAKL:=3; _kol:=0;

JPK_SYN.KOD:='NUM';
JPK_SYN.NAZWA:='Liczba wierszy ewidencji sprzedaży';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='KORGTU';
JPK_SYN.NAZWA:='W tym wiersze dotyczące korekt GTU albo Procedur';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='FP';
JPK_SYN.NAZWA:='Liczba dokumentów typu faktura do paragonu (FP)';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='RO';
JPK_SYN.NAZWA:='Liczba dokumentów typu zbiorcza sprzedaż z kas (RO)';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='WEW';
JPK_SYN.NAZWA:='Liczba dokumentów typu dokument wewnętrzny (WEW)';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='KOR';
JPK_SYN.NAZWA:='Liczba dokumentów korekt podstawy opodatkowania oraz podatku należnego';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();

VAT_POZ.index('VAT_POZ');
VAT_POZ.prefix(VAT_DEK.ref(),'C');
{? VAT_POZ.first()
|| {!
   |? _istotne:=exec('istotne','jpk');
      {? (VAT_POZ.POZ1 & _istotne*(';'+$VAT_POZ.POZ1+';'))
          |  (VAT_POZ.POZ2 & _istotne*(';'+$VAT_POZ.POZ2+';'))
      || VAT_PS.index('VAT_PS');
         VAT_PS.prefix(VAT_DEK.ref(),VAT_POZ.ref());
         {? VAT_PS.first()
         || {!
            |? SYN_TMP.blank(1);
               SYN_TMP.TYP:=VAT_PS.TYP_OPER;
               SYN_TMP.NIP:=VAT_PS.NIP;
               SYN_TMP.KH:=VAT_PS.KH;
               SYN_TMP.ADRES:=VAT_PS.ADRES;
               SYN_TMP.DATA:=VAT_PS.DATA;
               SYN_TMP.DATA2:=VAT_PS.DATA2;
               SYN_TMP.SYM:={? var_press('SYM_ZEW',VAT_PS)>0 || VAT_PS.SYM_ZEW || VAT_PS.SYM ?};
               SYN_TMP.KOR:=VAT_PS.JPK_GTU+1=':';
               SYN_TMP.REJ_KS:=$VAT_PS.REJ_KS;
               SYN_TMP.NR_KS:=VAT_PS.NR_KS;
               SYN_TMP.OKRO_F:={? var_pres('OKRO_F',VAT_PS)>0 || VAT_PS.OKRO_F().NR ?};
               SYN_TMP.prefix(SYN_TMP.TYP,SYN_TMP.OKRO_F,SYN_TMP.NIP,SYN_TMP.KH,SYN_TMP.ADRES,SYN_TMP.DATA,SYN_TMP.DATA2,SYN_TMP.SYM,SYN_TMP.KOR,SYN_TMP.REJ_KS,SYN_TMP.NR_KS);
               {? VAT_PS.TYP=' ' & (PAR_SKID.get(86)<>'T' | ~SYN_TMP.first())
               || {? VAT_PS.JPK_GTU+1=';' | VAT_PS.JPK_GTU+1=':'
                  || JPK_SYN.cntx_psh();
                     JPK_SYN.prefix(3,'KORGTU');
                     {? JPK_SYN.first()
                     || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
                        JPK_SYN.put()
                     ?};
                     JPK_SYN.cntx_pop()
                  || exec('rek_jpk_syn','jpk_v',3)
                  ?};
                  SYN_TMP.add()
               |?  VAT_PS.TYP<>' ' & (PAR_SKID.get(86)<>'T' | ~SYN_TMP.first())
               || {? VAT_PS.TYP='N'
                  || JPK_SYN.cntx_psh();
                     JPK_SYN.prefix(3,'KOR');
                     {? JPK_SYN.first()
                     || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
                        JPK_SYN.put()
                     ?};
                     JPK_SYN.cntx_pop()
                  ?};
                  SYN_TMP.add()
               ?};
               VAT_PS.next()
            !}
         ?}
      ?};
      VAT_POZ.next()
   !}
?};

JPK_SYN.cntx_psh();
SYN_TMP.prefix();
JPK_SYN.prefix(3,'NUM');
{? JPK_SYN.first()
|| JPK_SYN.STR:=$SYN_TMP.size();
   JPK_SYN.put()
?};
JPK_SYN.cntx_pop();

JPK_SYN.ZAKL:=4; _kol:=0;

SYN_TMP.prefix();
{? SYN_TMP.first()
|| {!
   |? SYN_TMP.del()
   !}
?};

VAT_POZ.index('VAT_POZ');
VAT_POZ.prefix(VAT_DEK.ref(),'D');
{? VAT_POZ.first()
|| {!
   |? JPK_SYN.KOD:=VAT_POZ.LP;
      JPK_SYN.NAZWA:=VAT_POZ.OP;
      JPK_SYN.VALUE:=VAT_POZ.NETTO;
      JPK_SYN.VALUE2:=VAT_POZ.VAT;
      _kol+=1; JPK_SYN.KOL:=_kol;
      JPK_SYN.add();
      VAT_POZ.next()
   !}
?};

JPK_SYN.ZAKL:=5; _kol:=0;

JPK_SYN.KOD:='NUM';
JPK_SYN.NAZWA:='Liczba wierszy ewidencji zakupu';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='KORGTU';
JPK_SYN.NAZWA:='W tym wiersze dotyczące korekt GTU albo Procedur';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='MK';
JPK_SYN.NAZWA:='Liczba dokumentów typu metoda kasowa (MK)';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='VAT_RR';
JPK_SYN.NAZWA:='Liczba dokumentów typu faktura VAT RR (VAT_RR)';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
JPK_SYN.KOD:='WEW';
JPK_SYN.NAZWA:='Liczba dokumentów typu dokument wewnętrzny (WEW)';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();
{? __upr=0
|| JPK_SYN.KOD:='SP';
   JPK_SYN.NAZWA:='Liczba dokumentów z transakcją objętą obowiązkiem stosowania mechanizmu podzielonej płatności';
   JPK_SYN.VALUE:=0;
   JPK_SYN.VALUE2:=0;
   _kol+=1; JPK_SYN.KOL:=_kol;
   JPK_SYN.add()
?};
JPK_SYN.KOD:='IMP';
JPK_SYN.NAZWA:='Liczba dokumentów z oznaczeniem dotyczącym podatku naliczonego z tytułu importu towarów';
JPK_SYN.VALUE:=0;
JPK_SYN.VALUE2:=0;
_kol+=1; JPK_SYN.KOL:=_kol;
JPK_SYN.add();

VAT_POZ.index('VAT_POZ');
VAT_POZ.prefix(VAT_DEK.ref(),'D');
{? VAT_POZ.first()
|| {!
   |? _istotne:=exec('istotne','jpk');
      {? (VAT_POZ.POZ1 & _istotne*(';'+$VAT_POZ.POZ1+';'))
          |  (VAT_POZ.POZ2 & _istotne*(';'+$VAT_POZ.POZ2+';'))
      || VAT_PS.index('VAT_PS');
         VAT_PS.prefix(VAT_DEK.ref(),VAT_POZ.ref());
         {? VAT_PS.first()
         || {!
            |? SYN_TMP.blank(1);
               SYN_TMP.TYP:=VAT_PS.TYP_OPER;
               SYN_TMP.NIP:=VAT_PS.NIP;
               SYN_TMP.KH:=VAT_PS.KH;
               SYN_TMP.ADRES:=VAT_PS.ADRES;
               SYN_TMP.DATA:=VAT_PS.DATA;
               SYN_TMP.DATA2:=VAT_PS.DATA2;
               SYN_TMP.SYM:={? var_press('SYM_ZEW',VAT_PS)>0 || VAT_PS.SYM_ZEW || VAT_PS.SYM ?};
               SYN_TMP.KOR:=VAT_PS.JPK_GTU+1=':';
               SYN_TMP.REJ_KS:=$VAT_PS.REJ_KS;
               SYN_TMP.NR_KS:=VAT_PS.NR_KS;
               SYN_TMP.OKRO_F:={? var_pres('OKRO_F',VAT_PS)>0 || VAT_PS.OKRO_F().NR ?};
               SYN_TMP.prefix(SYN_TMP.TYP,SYN_TMP.OKRO_F,SYN_TMP.NIP,SYN_TMP.KH,SYN_TMP.ADRES,SYN_TMP.DATA,SYN_TMP.DATA2,SYN_TMP.SYM,SYN_TMP.KOR,SYN_TMP.REJ_KS,SYN_TMP.NR_KS);
               {? VAT_PS.TYP=' ' & (PAR_SKID.get(86)<>'T' | ~SYN_TMP.first())
               || {? VAT_PS.JPK_GTU+1=';' | VAT_PS.JPK_GTU+1=':'
                  || JPK_SYN.cntx_psh();
                     JPK_SYN.prefix(5,'KORGTU');
                     {? JPK_SYN.first()
                     || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
                        JPK_SYN.put()
                     ?};
                     JPK_SYN.cntx_pop()
                  || exec('rek_jpk_syn','jpk_v',5)
                  ?};
                  SYN_TMP.add()
               |? VAT_PS.TYP<>' ' & (PAR_SKID.get(86)<>'T' | ~SYN_TMP.first())
               || {? VAT_PS.TYP='Z'
                  || JPK_SYN.cntx_psh();
                     JPK_SYN.prefix(5,'KOR');
                     {? JPK_SYN.first()
                     || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
                        JPK_SYN.put()
                     ?};
                     JPK_SYN.cntx_pop()
                  ?};
                  SYN_TMP.add()
               ?};
               VAT_PS.next()
            !}
         ?}
      ?};
      VAT_POZ.next()
   !}
?};

JPK_SYN.cntx_psh();
JPK_SYN.prefix(5,'NUM');
SYN_TMP.prefix();
{? JPK_SYN.first()
|| JPK_SYN.STR:=$SYN_TMP.size();
   JPK_SYN.put()
?};
JPK_SYN.cntx_pop()


\win_jpk_syn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.xx]
:: OPIS: Formuła tworząca okna dla tabeli JPK_SYN
::----------------------------------------------------------------------------------------------------------------------
__grp:=JPK_SYN.grp_make('Zestawienie syntetyczne',,'syntjpkv7');
__wer1:=JPK_SYN.mk_sel(,,,,,,,,'U','T');
JPK_SYN.win_fld(__wer1,,'KOD',,,15);
JPK_SYN.win_fld(__wer1,,'NAZWA',,,120);
JPK_SYN.win_fld(__wer1,,'STR',,,20,,,'Liczba dokumentów');
JPK_SYN.win_act(__wer1,,'Formuła','Drukuj',,,
                "exec('rep_exec','#b_report','','fks_jpk_synt',,,,,,,,'W')",,1);

__wer2:=JPK_SYN.mk_sel(,,,,,,,,'U','T');
JPK_SYN.win_fld(__wer2,,'NAZWA',,,60);
JPK_SYN.win_fld(__wer2,,'STR',,,20,,,'Wartość');
JPK_SYN.win_act(__wer2,,'Formuła','Drukuj',,,
                "exec('rep_exec','#b_report','','fks_jpk_synt',,,,,,,,'W')",,1);

__wer4:=JPK_SYN.mk_sel(,,,,,,,,'U','T');
JPK_SYN.win_fld(__wer4,,'NAZWA',,,80);
JPK_SYN.win_fld(__wer4,,'STR',,,20,,,'Wartość');
JPK_SYN.win_act(__wer4,,'Formuła','Drukuj',,,
                "exec('rep_exec','#b_report','','fks_jpk_synt',,,,,,,,'W')",,1);

__wer3:=JPK_SYN.mk_sel(,,,,,,,,'U','T');
JPK_SYN.win_fld(__wer3,,'KOD',,,5);
JPK_SYN.win_fld(__wer3,,'NAZWA',,,60);
JPK_SYN.win_fld(__wer3,,'VALUE',,,20,,,'Netto');
JPK_SYN.win_fld(__wer3,,'VALUE2',,,20,,,'Kwota VAT');
JPK_SYN.win_act(__wer3,,'Formuła','Drukuj',,,
                "exec('rep_exec','#b_report','','fks_jpk_synt',,,,,,,,'W')",,1);


JPK_SYN.grp_sel(__grp,,__wer2,'Dane podstawowe',,,,,"JPK_SYN.prefix(1)",,,,'maximized','zakladka1');
JPK_SYN.grp_sel(__grp,,__wer3,'Sprzedaż',,,,,"JPK_SYN.prefix(3); grp_disp(JPK_SYN,__wer4); JPK_SYN.prefix(2)",,,,'maximized','zakladka3');
JPK_SYN.tab_splt(__grp,,'horizontal','bottom',25);
JPK_SYN.grp_sel(__grp,,__wer4,,,,,,"JPK_SYN.prefix(3)","",,,'maximized_with_title');
JPK_SYN.grp_sel(__grp,,__wer3,'Zakup',,,,,"JPK_SYN.prefix(5); grp_disp(JPK_SYN,__wer4); JPK_SYN.prefix(4)",,,,'maximized','zakladka4');
JPK_SYN.tab_splt(__grp,,'horizontal','bottom',25);
JPK_SYN.grp_sel(__grp,,__wer4,,,,,,"JPK_SYN.prefix(5)","",,,'maximized_with_title');
JPK_SYN.grp_sel(__grp,,__wer1,'GTU',,,,,"JPK_SYN.prefix(6)",,,,'maximized','zakladka6');
JPK_SYN.grp_sel(__grp,,__wer1,'Procedury',,,,,"JPK_SYN.prefix(7)",,,,'maximized','zakladka7');
__grp


\rek_jpk_syn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.xx]
:: OPIS: Formuła dla kontekstu VAT_PS, odnoszący się unikalnie dla dokumentów
::   WE: _a [NUMBER] - zakładka, z której została wywołana formuła
::----------------------------------------------------------------------------------------------------------------------
_wer:=exec('bl_js_wersja','jpk_v');
JPK_SYN.cntx_psh();

_zakl:=_a;
{? VAT_PS.JPK_TD<>''
|| JPK_SYN.prefix(_zakl,VAT_PS.JPK_TD);
   {? JPK_SYN.first()
   || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
      JPK_SYN.put()
   ?}
?};

JPK_SYN.prefix();
JPK_SLO.cntx_psh();
{? VAT_PS.JPK_GTU<>'' & (__upr=0 | (VAT_PS.JPK_TD<>'RO' & VAT_PS.JPK_TD<>'WEW'))
|| JPK_SLO.index('KOD');
   JPK_SLO.prefix(_wer,'GTU',);
   {? JPK_SLO.first()
   || {!
      |? {? VAT_PS.JPK_GTU*JPK_SLO.KOD & (__upr=0 | JPK_SLO.KOD<>'SW')
         || JPK_SYN.prefix(6,JPK_SLO.KOD);
            {? JPK_SYN.first()
            || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
               JPK_SYN.put()
            ?}
         ?};
         JPK_SLO.next()
      !}
   ?}
?};

{? VAT_PS.JPK_PROC<>'' & (__upr=0 | VAT_PS.JPK_TD<>'RO')
|| _proc:=','+VAT_PS.JPK_PROC+',';
   {? _zakl=3
   || _proc:=gsub(_proc,'IMP','');
      JPK_SLO.index('KOD');
      JPK_SLO.prefix(_wer,'PROC',);
      {? JPK_SLO.first()
      || {!
         |? {? (_proc*(','+JPK_SLO.KOD+',')) & (__upr=0 | ((',MPP,IED,SOTI,SW,WSTO_EE,')*(','+JPK_SLO.KOD+','))=0) &
               JPK_SLO.KOD<>'IMP'
            || JPK_SYN.prefix(7,JPK_SLO.KOD);
               {? JPK_SYN.first()
               || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
                  JPK_SYN.put()
               ?}
            ?};
            JPK_SLO.next()
         !}
      ?};
      {? __upr & (_proc*',SW,' | _proc*',WSTO_EE,') & _proc*',EE,'=0
      || JPK_SYN.prefix(7,'EE');
         {? JPK_SYN.first()
         || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
            JPK_SYN.put()
         ?}
      ?}
   |? _zakl=5
   || {? _proc*',MPP,'
      || {? __upr=0
         || JPK_SYN.prefix(7,'MPP');
            {? JPK_SYN.first()
            || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
               JPK_SYN.put()
            ?}
         ?};
         JPK_SYN.prefix(5,'SP');
         {? JPK_SYN.first()
         || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
            JPK_SYN.put()
         ?}
      ?};
      {? _proc*',IMP,'
      || JPK_SYN.prefix(7,'IMP');
         {? JPK_SYN.first()
         || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
            JPK_SYN.put()
         ?};
         JPK_SYN.prefix(5,'IMP');
         {? JPK_SYN.first()
         || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
            JPK_SYN.put()
         ?}
      ?}
   ?}
?};
JPK_SLO.cntx_pop();

VAT_PS.cntx_psh();
VAT_PS.index('PVAT_SHA');
VAT_PS.prefix(VAT_DEK.ref(),VAT_PS.PVAT_REF,VAT_PS.SHA,);
_dalej:=1;
{? VAT_PS.first()
|| {!
   |? {? VAT_PS.JPK_GTU+1=':'
       || JPK_SYN.prefix(1,'KORGTU');
         {? JPK_SYN.first()
         || JPK_SYN.STR:=$((#JPK_SYN.STR)+1);
            _dalej:=0;
            JPK_SYN.put()
         ?}
      ?};
      VAT_PS.next() & _dalej
   !}
?};
VAT_PS.cntx_pop();

JPK_SYN.cntx_pop()


\upr_gtu_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_07]
:: OPIS: Usuwanie oznaczeń GTU dla uproszczeń z dnia 01.07.2021
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
{? VAT_PS.JPK_TD='RO' | VAT_PS.JPK_TD='WEW'
||  VAT_DEK.cntx_psh();
   _okres:=VAT_PS.VAT_DEK().OKRES;
   VAT_DEK.cntx_pop();
   {? 1+(_okres+2)='/'
   || _mc:=(#(_okres+1)-1)*3+1
   || _mc:=#(_okres+2)
   ?};
   _data:=date(#(4+_okres),_mc,1);
   {? _data>=exec('data_upr','jpk_v')
   || _ret:=0
   ?}
?};
_ret


\jpk_upr_grudz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_07]
:: OPIS: Mapowanie 'WSTO_EE' oraz 'SW' na 'EE' w związku z uproszczeniami dla JPK_V7 lipiec 2021
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
{? ISTDEFS.OPIS='EE' & exec('d_upr_chk','jpk_v') & _a*',EE,'<>1 & (_a*',WSTO_EE,' | _a*',SW,')
|| _ret:='EE,'
?};
_ret


\jpk_mapowanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Formuła mapująca procedury podczas tworzenia pliku JPK_V7
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
:: mapowanie EE na WSTO_EE od 01.01.2022
{? ISTDEFS.OPIS='WSTO_EE' & PAR_SKID.get(489)='T' & exec('dv7_v2_chk','jpk_v') & _a*',WSTO_EE,'<>1 & _a*',EE,'
|| _ret:='WSTO_EE,'
?};
_ret


\dv7_v2_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Sprawdzanie daty 2022,01,01 dla bufora VAT_PS
::----------------------------------------------------------------------------------------------------------------------
VAT_DEK.cntx_psh();
_okres:=VAT_PS.VAT_DEK().OKRES;
VAT_DEK.cntx_pop();
{? 1+(_okres+2)='/'
|| _mc:=(#(_okres+1)-1)*3+1
|| _mc:=#(_okres+2)
?};
_data:=date(#(4+_okres),_mc,1);
{? _data>=date(2022,01,01)

|| _ret:=1
|| _ret:=0
?};
_ret


\d_upr_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_07]
:: OPIS: Sprawdzanie daty do której występuje mapowanie dla bufora rekordu VAT_PS
::----------------------------------------------------------------------------------------------------------------------
VAT_DEK.cntx_psh();
_okres:=VAT_PS.VAT_DEK().OKRES;
VAT_DEK.cntx_pop();
{? 1+(_okres+2)='/'
|| _mc:=(#(_okres+1)-1)*3+1
|| _mc:=#(_okres+2)
?};
_data:=date(#(4+_okres),_mc,1);
{? _data>=exec('data_upr_end','jpk_v')
|| _ret:=0
|| _ret:=1
?};
_ret


\del_mpp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_07]
:: OPIS: Dla uproszczeń JPK usuwa oznaczenie MPP z procedur - od dnia 01.07.2021
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
{? ISTDEFS.OPIS='MPP' | VAT_PS.JPK_TD='RO' | ISTDEFS.OPIS='SW'
|| VAT_DEK.cntx_psh();
   _okres:=VAT_PS.VAT_DEK().OKRES;
   VAT_DEK.cntx_pop();
   {? 1+(_okres+2)='/'
   || _mc:=(#(_okres+1)-1)*3+1
   || _mc:=#(_okres+2)
   ?};
   _data:=date(#(4+_okres),_mc,1);
   {? _data>=exec('data_upr','jpk_v')
   || _ret:=0
   ?}
?};
_ret


\data_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_07]
:: OPIS: Data od kiedy zostały wprowadzone uproszczenia do JPK_V7
::----------------------------------------------------------------------------------------------------------------------
date(2021,7,1)


\data_upr_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14_07]
:: OPIS: Data, do kiedy występuje mapowanie WSTO_EE i SW na EE
::----------------------------------------------------------------------------------------------------------------------
date(2021,12,31)


\chk_ee_v7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51_144]
:: OPIS: Komunikat o usuniętym oznaczanie "EE" od 2022/01/01
::----------------------------------------------------------------------------------------------------------------------
VPOZ.cntx_psh();
VPOZ.index('VDOK');
VPOZ.prefix(DOK.ref());
_okres:={? DOK.OKRVAT=null()
        || {? VPOZ.first()
           || {!
              |? {? VPOZ.OKRVAT=null()
                 || {? VPOZ.OKR_C().POCZ>VPOZ.OKR_Z().POCZ
                    || _okr:=VPOZ.OKR_C().POCZ>=date(2022,01,01)
                    || _okr:=VPOZ.OKR_Z().POCZ>=date(2022,01,01)
                    ?}
                 || _okr:=VPOZ.OKRVAT().POCZ>=date(2022,01,01)
                 ?};
                 ~_okr & VPOZ.next()
              !};
              _okr
           || 0
           ?}
        || DOK.OKRVAT().POCZ>=date(2022,01,01)
        ?};
VPOZ.cntx_pop();
{? _okres
|| ~FUN.ask('Oznaczenie procedury "EE" jest nieaktualne dla deklaracji JPK_V7(2)\n\nCzy mimo to dołączyć procedurę?')
|| 0
?}


\trig_put_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Teigger po put tabeli JPK
::----------------------------------------------------------------------------------------------------------------------
{? 2+JPK.TYP='V7' & (JPK.STAT='W' & bfld('STAT')<>'W') || ntc_eat(,'jpk_v7_'+JPK.OKRES) ?};
~~


\us_name
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Zwraca nazwę urzędu z xml na podstawie wartości elementu KodUrzedu
::   WE: _a - ścieżka do pliku
::   WY: Nazwa urzędu
::----------------------------------------------------------------------------------------------------------------------
_path:=_a;
_result:='';
_xml:=_row:=_kus:='';
_fxml:=fopen(_path,'ur',0,,1);
{? _fxml.is_open()
|| VAR_DEL.delete('__RES');
   __RES:=obj_new('flag','value');
   __RES.flag:=0;
   __RES.value:='';
   x_parse(_fxml,"1","1",
   "
      {? _a='KodUrzedu'
      || __RES.flag:=1;
         __RES.value:=''
      ?};
      1
   ",,
   "
      {? __RES.flag
      || __RES.flag:=0;
        __RES.value+=_a
      ?};
      1
   ");
   fclose(_fxml);
   {? __RES.value<>''
   || US.cntx_psh();
      US.index('URZSKNAZ'); US.prefix(); US.blank(1);
      US.EDEK_SYM:=__RES.value;
      {? US.find_rec()
      || _result:=US.NU
      ?};
      US.cntx_pop()
   ?};
   VAR_DEL.delete('__RES')
?};
{? _result='' || _result:=XINFO.URZAD().NU ?};
_result

:Sign Version 2.0 jowisz:1045 2024/02/26 13:37:04 374d9fbecea548c77ae30b8a62152971932c1ec55aeb395e10654944e807dbffd13ebecfd48dde50707c8e3cc28ad64d4d9e617e6e00ed1ed1694c0097e8030e8316f24c24ac2707f4524b4b5ef1d92c0144d47f84d2b0ac96621b59e625b748f68dd622a1e0ccc2059f20dbad829261e8c8b92a203c7440cc3fd84900cfc761

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ob_stat.fml
:: Utworzony: 26.02.2021
:: Autor: JK
::======================================================================================================================
:: Zawartość: Formuły do obsługi statystyk dokumentów w obiegu
::======================================================================================================================


\dolpozan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Dolaczanie poziomu analizy
::   WE: _a - 1 - zwykle dolacz
::            2 - dolacz na strzalke w dol
::----------------------------------------------------------------------------------------------------------------------
_ref_new:={? EOS_STAT.size() || EOS_STAT.ref() || null ?};
TAB_POZ:=tab_tmp(2,'LP','INTEGER','Poziom',
                   'POZIOM','STRING[30]','Nazwa poziomu'
                 );
_lp:=0;
EOS_STAT.cntx_psh();
EOS_STAT.index('POZIOM'); EOS_STAT.prefix(USERS.ref(),VAR.STATYP);
{? VAR.STATYP<>'P' & VAR.STATYP<>'D'
|| {? ~EOS_STAT.find_key('Waluty','Waluty') || _lp+=1; TAB_POZ.LP:=_lp; TAB_POZ.POZIOM:='Waluty'; TAB_POZ.add() ?}
?};
{? ~EOS_STAT.find_key('Statusy','Statusy') || _lp+=1; TAB_POZ.LP:=_lp; TAB_POZ.POZIOM:='Statusy'; TAB_POZ.add() ?};
{? ~EOS_STAT.find_key('Użytkownicy','Użytkownicy') || _lp+=1; TAB_POZ.LP:=_lp; TAB_POZ.POZIOM:='Użytkownicy'; TAB_POZ.add() ?};
EOS_STAT.cntx_pop();
EOS_STAT.cntx_psh();
EOS_STAT.index('DISP'); EOS_STAT.prefix(USERS.ref(),VAR.STATYP);
{? TAB_POZ.size()
|| _sel_poz:=TAB_POZ.mk_sel('Poziomy analiz'@,,0,'tab_poz_wer',,,TAB_POZ.size());
   TAB_POZ.win_fld(_sel_poz,,'LP',,,6);
   TAB_POZ.win_fld(_sel_poz,,'POZIOM');
   TAB_POZ.win_act(_sel_poz,,'Formuła','Ten'@,,'Wybór poziomu'@,,"sel_exit()",1);
   TAB_POZ.win_sel(_sel_poz);
   {? TAB_POZ.select()
   || {? _a=1
      || {? EOS_STAT.size()
         || EOS_STAT.NAZWA:=TAB_POZ.POZIOM; EOS_STAT.OPER_DOB:=USERS.ref();
            EOS_STAT.TYP:=VAR.STATYP;
            {? EOS_STAT.add()
            || _ref_new:=EOS_STAT.ref(); _lp:=EOS_STAT.LP;
               {? EOS_STAT.last()
               || {! |?
                     {? EOS_STAT.LP>=_lp
                     || {? EOS_STAT.ref()<>_ref_new || EOS_STAT.LP+=1; EOS_STAT.put() ?};
                        EOS_STAT.prev()
                     || 0
                     ?}
                  !}
               ?}
            ?}
         || EOS_STAT.LP:=1; EOS_STAT.NAZWA:=TAB_POZ.POZIOM; EOS_STAT.OPER_DOB:=USERS.ref();
            EOS_STAT.TYP:=VAR.STATYP;
            {? EOS_STAT.add() || _ref_new:=EOS_STAT.ref() ?}
         ?}
      || EOS_STAT.LP:=(EOS_STAT.LP+1); EOS_STAT.NAZWA:=TAB_POZ.POZIOM; EOS_STAT.OPER_DOB:=USERS.ref();
         EOS_STAT.TYP:=VAR.STATYP;
         {? EOS_STAT.add() || _ref_new:=EOS_STAT.ref() ?}
      ?}
   ?}
|| FUN.info('Dodano już wszystkie możliwe poziomy.'@)
?};
EOS_STAT.cntx_pop();
{? _ref_new<>null || EOS_STAT.seek(_ref_new) ?};
VAR_DEL.delete('TAB_POZ')


\usupozan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Usuwanie poziomu analizy
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć pozycję?'@)
|| {? EOS_STAT.del() & EOS_STAT.last()
   || _lp:=EOS_STAT.size()*2;
      {! |?
         EOS_STAT.LP:=_lp; _lp-=1; EOS_STAT.put();
         EOS_STAT.prev()
      !};
      _lp:=1;
      {? EOS_STAT.first()
      ||  {! |?
             EOS_STAT.LP:=_lp; _lp+=1; EOS_STAT.put();
             EOS_STAT.next()
          !}
      ?};
      EOS_STAT.first()
   ?}
?}


\poppozan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Przesuwanie poziomu w gore lub w dol
::   WE: _a - 1 - w gore
::            2 - w dol
::       _b - alias do tabeli
::----------------------------------------------------------------------------------------------------------------------
_b.cntx_psh(); _ref:=null;
_przesun:={? (_a=1 & _b.prev()) | (_a=2 & _b.next())
          || _lp:=_b.LP; _ref:=_b.ref(); 1
          || 0
          ?};
_b.cntx_pop();
_b.cntx_psh();
do();
{? _przesun
|| _b.LP:=_lp;
   {? _b.put() & _b.seek(_ref)
   || _b.LP:={? _a=1 || _lp+1 || _lp-1 ?}; _b.put()
   ?}
?};
end();
_b.cntx_pop()


\doldompa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Dolaczanie domyslnych poziomow analizy dla uzytkownikow
::----------------------------------------------------------------------------------------------------------------------
_lp:=0;
EOS_STAT.OPER_DOB:=USERS.ref();
EOS_STAT.TYP:=VAR.STATYP;
{? VAR.STATYP='F' | VAR.STATYP='W'
|| _lp+=1; EOS_STAT.LP:=_lp; EOS_STAT.NAZWA:='Waluty';  EOS_STAT.add()
?};
_lp+=1; EOS_STAT.LP:=_lp; EOS_STAT.NAZWA:='Statusy'; EOS_STAT.add();
_lp+=1; EOS_STAT.LP:=_lp; EOS_STAT.NAZWA:='Użytkownicy'; EOS_STAT.add();
EOS_STAT.first()


\dob_stat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Statystyka dokumentow w obiegu
::----------------------------------------------------------------------------------------------------------------------
USERS.cntx_psh(); USERS.index('USR_GUID'); USERS.prefix();
{? typobi=1 || VAR.STATYP:='F'
|? typobi=2 || VAR.STATYP:='W'
|? typobi=3 & pdeleg=0 || VAR.STATYP:='D'
|? typobi=3 & pdeleg=1 || VAR.STATYP:='P'
?};
{? OPERATOR.USER<>null
|| OPERATOR.USER();
   _dalej:=1;
   EOS_STAT.cntx_psh();
   EOS_STAT.index('DISP'); EOS_STAT.prefix(USERS.ref(),VAR.STATYP);
   {? ~EOS_STAT.first()
   || EOS_STAT.win_sel('WER'); EOS_STAT.select();
      {? ~EOS_STAT.size()
      || FUN.info('Nie zdefiniowano poziomów analizy dla operatora.'@);
         _dalej:=0
      ?}
   ?};
   EOS_STAT.cntx_pop();
   {? _dalej
   || SSTALE.AR(); strok:=SSTALE.AR; stroknaz:=ROK_F.NAZ;
      {! |?
         EOS_STAT.index('DISP'); EOS_STAT.prefix(USERS.ref(), VAR.STATYP);
         {? EOS_STAT.first()
         || stzmiana:=0;
            exec('dob_stat1','ob_stat',strok);
            VAR_DEL.delete('DOB_STAT');
            stzmiana
         || 0
         ?}
      !};
      VAR_DEL.delete('strok','stroknaz')
   ?}
|| FUN.info('Nie znaleziono operatora.'@)
?};
USERS.cntx_pop()


\dob_stat1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Statystyka dokumentow w obiegu - stworzenie tabelki
::   WE: _a - roku obrachunkowy (pusty - wszystkie lata)
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('DOB_STAT');
EOS_STAT.cntx_psh();
EOS_STAT.index('DISP'); EOS_STAT.prefix(USERS.ref(),VAR.STATYP);
czy_us:=czy_wal:=czy_sta:=0;
zakres:=1;
{? EOS_STAT.first()
|| EOS_STAT.index('POZIOM');
   EOS_STAT.prefix(USERS.ref(),VAR.STATYP,'Użytkownicy','Użytkownicy'); czy_us:=EOS_STAT.first();
   {? VAR.STATYP='F' | VAR.STATYP='W'
   || EOS_STAT.prefix(USERS.ref(),'Waluty','Waluty'); czy_wal:=EOS_STAT.first()
   || czy_wal:=0
   ?};
   EOS_STAT.prefix(USERS.ref(),VAR.STATYP,'Statusy','Statusy'); czy_sta:=EOS_STAT.first()
?};
EOS_STAT.cntx_pop();
ROK_F.cntx_psh(); EDOKUM.cntx_psh(); EDOKOS.cntx_psh(); EVAT.cntx_psh(); EPODZ.cntx_psh();
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? (_a<>null & ROK_F.seek(_a)) | (_a=null & ROK_F.first())
|| exec('sta_tab','ob_stat');
   {! |?
      EDOKUM.use('skid_v'+($(ROK_F.POCZ_ROK~1)+2));
      EDOKOS.use('skid_y'+($(ROK_F.POCZ_ROK~1)+2));
      EVAT.use('skid_a'+($(ROK_F.POCZ_ROK~1)+2));
      EPODZ.use('skid_j'+($(ROK_F.POCZ_ROK~1)+2));
      exec('sta_gen','ob_stat');
      _a=null & ROK_F.next()
   !};
   DOB_STAT.index(ind_sta1); DOB_STAT.prefix();
   DOB_STAT.select()
?};
VAR_DEL.delete('czy_us','czy_wal','czy_sta','ind_sta1','ind_sta2','ind_sta3','ind_sta4','ind_sta5','dob_sta','zakres');
ROK_F.cntx_pop(); EDOKUM.cntx_pop(); EDOKOS.cntx_pop(); EVAT.cntx_pop(); EPODZ.cntx_pop()


\sta_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Statystyka dokumentow w obiegu dla innych poziomow
::       niz tylko waluty - tworzenie tabeli i okienka
::----------------------------------------------------------------------------------------------------------------------
DOB_STAT:=tab_tmp(2,'TREE','TREE_REF','',
                    'WART','STRING[51]','',
                    'PELWART','STRING[100]','',
                    'NETTO','REAL','Netto',
                    'BRUTTO','REAL','Brutto',
                    'DOB_REF','STRING[20]','',
                    'STATUS','INTEGER','',
                    'WALUTA','STRING[8]','',
                    'USER','STRING[51]','',
                    'POZIOM','INTEGER','',
                    'CONAPOZ','INTEGER','',
                    'ROK','STRING[20]','Rok',
                    'INT','STRING[14]','Dni realizacji',
                    'TP','STRING[10]','Term. płatn.',
                    'ETAP','STRING[16]','Etap',
                    'ETAPNAZ','STRING[83]','Etap',
                    'DELEG','STRING[35]','Delegowany'
                 );
ind_sta1:=DOB_STAT.index('?');
ind_sta2:=DOB_STAT.ndx_tmp('',1,'DOB_REF',,0,'DOB_REF',,0);
ind_sta3:=DOB_STAT.ndx_tmp('',1,'POZIOM',,0,'PELWART',,0,'PELWART',,0);
ind_sta5:=DOB_STAT.ndx_tmp('',1,'DOB_REF',,0,'STATUS',,0,'USER',,0,'WALUTA',,0,'POZIOM',,0,'ETAP',,0);
dob_sta:=DOB_STAT.mk_sel({? typobi=1 || 'Statystyka faktur w obiegu '
                         |? typobi=2 || 'Statystyka wniosków '
                         |? typobi=3 & pdeleg=0 || 'Statystyka delegacji '
                         |? typobi=3 & pdeleg=1 || 'Statystyka planów delegacji '
                         ?}+
                         {? stroknaz<>'' || 'z roku: '+stroknaz || 'ze wszystkich lat bilansowych' ?},
                         'N',,'#dob_stat2',,,,1);
DOB_STAT.win_fld(dob_sta,,'WART',,,30);
{? strok=null || DOB_STAT.win_fld(dob_sta,,'ROK',,,4) ?};
{? typobi=1
|| DOB_STAT.win_fld(dob_sta,,'TP');
   DOB_STAT.fld_fml('TP','DISPLAY_FORMAT',"'alignment=center'");
   DOB_STAT.win_fld(dob_sta,,'NETTO',,,16,2)
?};
{? typobi<3
|| DOB_STAT.win_fld(dob_sta,,'BRUTTO',,,16)
|| DOB_STAT.win_fld(dob_sta,,'DELEG',,,16)
?};
DOB_STAT.win_fld(dob_sta,,'INT');
DOB_STAT.win_fld(dob_sta,,'ETAPNAZ',,,28);
DOB_STAT.fld_fml('INT','DISPLAY_FORMAT',"'alignment=right'");
DOB_STAT.win_act(dob_sta,,'Wyświetl',,,,"exec('stdspdok','ob_stat')");
DOB_STAT.win_fml(dob_sta,,'WART',,'ICON_BEFORE',"{? DOB_STAT.CONAPOZ=0
                                                 || 'xwin16.png:70'
                                                 |? DOB_STAT.CONAPOZ=1
                                                 || 'xwin16.png:49'
                                                 |? DOB_STAT.CONAPOZ=2
                                                 || 'xwin16.png:100'
                                                 || DOB_STAT.cntx_psh();
                                                    DOB_STAT.index(ind_sta1); DOB_STAT.prefix(DOB_STAT.ref());
                                                    {? DOB_STAT.first()
                                                    || DOB_STAT.cntx_pop();
                                                       {? DOB_STAT.tr_state()=1
                                                       || 'xwin16.png:75'
                                                       || 'xwin16.png:74'
                                                       ?}
                                                    || DOB_STAT.cntx_pop(); ''
                                                    ?}
                                                 ?}");
DOB_STAT.win_act(dob_sta,,'Formuła','Zwiń/&rozwiń całość'@,,'Zwijanie/rozwijanie całości'@,
                 "exec('zwrw_all','#tree','DOB_STAT','TREE',dob_sta)");
DOB_STAT.win_act(dob_sta,,'Formuła','Zmień r&ok'@,,'Zmiana roku obrachunkowego analizy'@,
                 "exec('sta_rok','ob_stat')",,1);
DOB_STAT.win_act(dob_sta,1,'Formuła','Zmień r&ok'@,,'Zmiana roku obrachunkowego analizy'@,
                 "exec('sta_rok','ob_stat')",,1);
DOB_STAT.win_act(dob_sta,,'Formuła','Poziomy analizy'@,,'Zmiana poziomów analizy'@,
                 "exec('stazmpoz','ob_stat')");
{? typobi=1
|| DOB_STAT.win_act(dob_sta,,'Formuła','Pozycje &VAT'@,,'Pozycje VAT faktury w obiegu'@,
                    "exec('staakcje','ob_stat',1)");
   DOB_STAT.win_act(dob_sta,,'Formuła','Zapotrzebowania'@,,'Lista realizowanych zapotrzebowań'@,
                    "exec('staakcje','ob_stat',2)")
?};
DOB_STAT.win_act(dob_sta,,'Formuła','Podziały dla co&ntr.'@,,'Podziały controllingowe'@,
                 "exec('staakcje','ob_stat',3)");
DOB_STAT.win_act(dob_sta,,'Menu','Doku&ment źródłowy'@,,'Obejrzenie lub pobranie dokumentu źródłowego'@);
DOB_STAT.win_act(dob_sta,,'Formuła','Wyświetl e-dokument'@,'Doku&ment źródłowy','Wyświetlanie dokumentu źródłowego'@,
                 "exec('staakcje','ob_stat',4)");
DOB_STAT.win_act(dob_sta,,'Formuła','Pobierz e-dokument'@,'Doku&ment źródłowy','Pobranie dokumentu z bazy'@,
                 "exec('staakcje','ob_stat',5)");
DOB_STAT.win_act(dob_sta,,'Formuła','Druku&j'@,,'Wydruk'@,"exec('staakcje','ob_stat',6)",,,,,,,,'icon=print' );
DOB_STAT.win_act(dob_sta,,'Formuła','O&bieg'@,,'Etapy obiegu - szczegóły'@,"exec('staakcje','ob_stat',7)");
DOB_STAT.win_act(dob_sta,,'Formuła','Legenda'@,,'Opis znaczenia ikon'@,,"exec('legenda','color','DOB_STAT#01')");
DOB_STAT.win_sel(dob_sta)


\sta_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Statystyka dokumentow w obiegu - zmiana roku
::----------------------------------------------------------------------------------------------------------------------
SIK.win_edit('ROK1'); SIK.ROK1:=strok;
{? SIK.edit()
|| strok:=SIK.ROK1; stroknaz:={? strok || SIK.ROK1().NAZ || '' ?};
   stzmiana:=1; sel_exit()
?}


\sta_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [$1210]
:: OPIS: Statystyka dokumentow w obiegu
::----------------------------------------------------------------------------------------------------------------------
DOB_STAT.index(ind_sta2); DOB_STAT.prefix();
EDOKOS.cntx_psh(); EDOKOS.index('SZUK4');
EDOKUM.cntx_psh(); EDOKUM.index('ODD');
{? OPERATOR.DEPT || EDOKUM.prefix(OPERATOR.DEPT) || EDOKUM.prefix() ?};
{? EDOKUM.first()
|| {!
   |? {? {? var_pres('FROMCNT',SIK)>0 & SIK.FROMCNT || EDOKUM.DOKUM<>'' || 1 ?}
      ||
:: niezaakceptowane przez operatorow wprowadzajacych
         exec('sta_in1','ob_stat',1);
:: odrzucone do operatorow wprowadzajacych
         exec('sta_in1','ob_stat',2);
:: zaakceptowane przez operatorow wprowadzajacych
         exec('sta_in1','ob_stat',3);
:: nieprzekazane przez operatorow przekazujących
         exec('sta_in1','ob_stat',4);
:: odrzucone do operatorow przekazujących
         exec('sta_in1','ob_stat',5);
:: przekazane przez operatorow przekazujących
         exec('sta_in1','ob_stat',6);
:: niezaakceptowane przez operatorow akceptujacych
         exec('sta_in1','ob_stat',7);
:: odrzucone do operatorow akceptujacych
         exec('sta_in1','ob_stat',8);
:: zaakceptowane przez operatorow akceptujacych
         exec('sta_in1','ob_stat',9)
      ?};
      EDOKUM.next()
   !}
?};
DOB_STAT.prefix();
{? DOB_STAT.first() || exec('gen_stat','ob_stat') ?};
EDOKUM.cntx_pop(); EDOKOS.cntx_pop();
1


\sta_in1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [$1210]
:: OPIS: Statystyka dokumentow w obiegu - wewnetrzna
::   WE: _a - 1 - niezaakceptowane przez operatorow wprowadzajacych
::            2 - odrzucone do operatorow wprowadzajacych
::            3 - zaakceptowane przez operatorow wprowadzajacych
::            4 - nieprzekazane przez operatorow przekazujacych
::            5 - odrzucone do operatorow przekazujacych
::            6 - przekazane przez operatorow przekazujacych
::            7 - niezaakceptowane przez operatorow akceptujacych
::            8 - odrzucone do operatorow akceptujacych
::            9 - zaakceptowane przez operatorow akceptujacych
::----------------------------------------------------------------------------------------------------------------------
USERS.cntx_psh();
EDOKUM.USERS();
_waluta:={? czy_wal || EDOKUM.WAL().KOD || '' ?};
_status:={? czy_sta || _a || 0 ?};
_typ:=exec('bl_typ','obiegi',typobi);
{? typobi=3
|| {? pdeleg=0 & EDOKUM.TYP().CZY_PLAN=1
   || _dod:=1
   |? pdeleg=1 & EDOKUM.TYP().CZY_PLAN=2
   || _dod:=1
   || _dod:=0
   ?}
|| _dod:=1
?};
{? _dod
|| DOB_STAT.cntx_psh();
   DOB_STAT.index(ind_sta5); DOB_STAT.prefix();
   {? _a=1
   || EDOKOS.prefix(_typ,EDOKUM.ref(),USERS.ref(),'W','N','T')
   |? _a=2
   || EDOKOS.prefix(_typ,EDOKUM.ref(),USERS.ref(),'W','O','T')
   |? _a=3
   || EDOKOS.prefix(_typ,EDOKUM.ref(),USERS.ref(),'W','T','T')
   |? _a=4
   || EDOKOS.prefix(_typ,EDOKUM.ref(),USERS.ref(),'P','N','T')
   |? _a=5
   || EDOKOS.prefix(_typ,EDOKUM.ref(),USERS.ref(),'P','O','T')
   |? _a=6
   || EDOKOS.prefix(_typ,EDOKUM.ref(),USERS.ref(),'P','T','T')
   |? _a=7
   || EDOKOS.prefix(_typ,EDOKUM.ref(),USERS.ref(),'A','N','T')
   |? _a=8
   || EDOKOS.prefix(_typ,EDOKUM.ref(),USERS.ref(),'A','O','T')
   |? _a=9
   || EDOKOS.prefix(_typ,EDOKUM.ref(),USERS.ref(),'A','T','T')
   ?};
   {? EDOKOS.first()
   || {! |?
         _user:={? czy_us || EDOKOS.USERS().DANE || '' ?};
         {? typobi=3 || _deleg:=EDOKOS.EDOKUM().DOSTAWCA().NAZWISKO+' '+EDOKOS.EDOKUM().DOSTAWCA().PIERWSZE || _deleg:='' ?};
         {? ~DOB_STAT.find_key($EDOKUM.ref(),_status,_user,_waluta,0) & EDOKUM.ZAM<>'T'
         || exec('add_stat','ob_stat',0,{? typobi=1 || EDOKUM.SYM || EDOKUM.ID ?},'',EDOKUM.NETTO$2,EDOKUM.WART$2,
                 $EDOKUM.ref(),_status,_waluta,_user,0,0,ROK_F.NAZ,
                 $exec('dni_realizacji','obiegi'),$EDOKUM.TP,,EDOKUM.B_PRELS,_deleg)
         ?};
         EDOKOS.next()
      !}
   ?};
   DOB_STAT.cntx_pop()
?};
USERS.cntx_pop()


\stdspdok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Spacja na pozycji analizy
::----------------------------------------------------------------------------------------------------------------------
{? DOB_STAT.DOB_REF<>''
|| _ref:=BB.sqlint(DOB_STAT.DOB_REF); _nam:=form(DOB_STAT.DOB_REF-8);
   {? _ref<>0 & _nam<>''
   || exec('edok_psh','open_tab');
      exec('open_edok','open_tab',_nam+2);
      EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || exec('settypobi','obiegi2');
         {? EDOKUM.r_lock(1,1,1)
         || OBIEGI.EDOKOS:=null;
            {? typobi=1
            || EDOKUM.win_edit('WPR');
               EDOKUM.display()
            |? typobi=2
            || exec('akc_display','obe_faw')
            |? typobi=3
            || EDOKUM.win_edit('KSD');
               EDOKUM.display()
            ?};
            EDOKUM.r_unlock()
         ?}
      ?};
      exec('edok_pop','open_tab')
   ?}
?}


\gen_stat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Tworzenie indeksu dla pozycji wg zdefiniowanych poziomow i
::       generowanie drzewka
::----------------------------------------------------------------------------------------------------------------------
EOS_STAT.cntx_psh();
EOS_STAT.index('DISP'); EOS_STAT.prefix(USERS.ref(),VAR.STATYP);
{? EOS_STAT.first()
|| _index:='DOB_STAT.ndx_tmp(\'\','+$(EOS_STAT.size()*2)+',';
   _poziom:=obj_new(3); {! _i:=1..3 |! _poziom[_i]:='' !}; _ile_poz:=0;
   {! |?
      _ile_poz+=1; _poziom[_ile_poz]:=EOS_STAT.NAZWA;
      _index+={? EOS_STAT.NAZWA='Waluty'
              || '\'WALUTA\',,0,\'WALUTA\',,0,'
              |? EOS_STAT.NAZWA='Statusy'
              || '\'STATUS\',,0,\'STATUS\',,0,'
              || '\'USER\',,0,\'USER\',,0,'
              ?};
      EOS_STAT.next()
   !};
   _index:=_index-1; _index+=')';
   ind_sta4:=(($_index)());
   DOB_STAT.cntx_psh();
   DOB_STAT.index(ind_sta4); DOB_STAT.prefix();
   {? DOB_STAT.first()
   || {! |?
         {? DOB_STAT.DOB_REF<>''
         || _netto:=DOB_STAT.NETTO; _brutto:=DOB_STAT.BRUTTO;
            DOB_STAT.cntx_psh();
            DOB_STAT.index(ind_sta3); DOB_STAT.prefix();
            _pelwart:=_wart:=''; _ref_pop:=0; _i:=_licz:=1;
            {! _i:=1.._ile_poz
            |! {? _poziom[_i]='Waluty'
               || _wart:=DOB_STAT.WALUTA; _pelwart+=_wart;
                  DOB_STAT.cntx_psh();
                  {? DOB_STAT.find_key(_licz,_pelwart,_pelwart) |
                     exec('add_stat','ob_stat',_ref_pop,_wart,_pelwart,0,0,'',0,'','',_licz,1,'')
                  || _ref_pop:=DOB_STAT.ref()
                  ?};
                  DOB_STAT.cntx_pop();
                  _licz+=1
               |? _poziom[_i]='Statusy'
               || _wart:={? DOB_STAT.STATUS=1 | DOB_STAT.STATUS=2 | DOB_STAT.STATUS=3
                         || 'Rejestracja'
                         |? DOB_STAT.STATUS=4 | DOB_STAT.STATUS=5 | DOB_STAT.STATUS=6
                         || 'Przekazanie'
                         || 'Akceptacja'
                         ?};
                  _pelwart+=_wart;
                  DOB_STAT.cntx_psh();
                  {? DOB_STAT.find_key(_licz,_pelwart,_pelwart) |
                     exec('add_stat','ob_stat',_ref_pop,_wart,_pelwart,0,0,'',0,'','',_licz,3,'')
                  || _ref_pop:=DOB_STAT.ref()
                  ?};
                  DOB_STAT.cntx_pop();
                  _wart:={? DOB_STAT.STATUS=1
                         || 'Niezakończone'
                         |? DOB_STAT.STATUS=4
                         || 'Nieprzekazane'
                         |? DOB_STAT.STATUS=7
                         || 'Niezaakceptowane'
                         |? DOB_STAT.STATUS=2 | DOB_STAT.STATUS=5 | DOB_STAT.STATUS=8
                         || 'Odrzucone'
                         |? DOB_STAT.STATUS=3
                         || 'Zarejestrowane'
                         |? DOB_STAT.STATUS=6
                         || 'Przekazane'
                         |? DOB_STAT.STATUS=9
                         || 'Zaakceptowane'
                         ?};
                  _pelwart+=_wart;
                  DOB_STAT.cntx_psh();
                  {? DOB_STAT.find_key(_licz+1,_pelwart,_pelwart) |
                     exec('add_stat','ob_stat',_ref_pop,_wart,_pelwart,0,0,'',0,'','',_licz+1,3,'')
                  || _ref_pop:=DOB_STAT.ref()
                  ?};
                  DOB_STAT.cntx_pop();
                  _licz+=2
               |? _poziom[_i]='Użytkownicy'
               || _wart:=DOB_STAT.USER; _pelwart+=_wart;
                  DOB_STAT.cntx_psh();
                  {? DOB_STAT.find_key(_licz,_pelwart,_pelwart) |
                     exec('add_stat','ob_stat',_ref_pop,_wart,_pelwart,0,0,'',0,'','',_licz,2,'')
                  || _ref_pop:=DOB_STAT.ref()
                  ?};
                  DOB_STAT.cntx_pop();
                  _licz+=1
               ?}
            !};
            DOB_STAT.cntx_pop();
            DOB_STAT.TREE:=_ref_pop; DOB_STAT.put()
         ?};
         DOB_STAT.next()
      !}
   ?};
   DOB_STAT.cntx_pop()
?};
EOS_STAT.cntx_pop()


\add_stat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Dodanie zapisu do tabeli ze statystyka
::   WE: _a - wskazanie na element nadrzedny
::       _b - wartosc
::       _c - pelna wartosc
::       _d - netto
::       _e - brutto
::       _f - ref sql dokumentu w obiegu
::       _g - status
::       _h - waluta
::       _i - user
::       _j - poziom
::       _k - co jest na poziomie
::            (0 - dokkument, 1 - waluta, 2 - uzytkownik, 3 - status)
::       _l - nazwa roku
::      [_m]- liczba dni realizacji dokumentu
::      [_n]- termin platnosci dokumentu
::      [_o]- etap obiegu (ref sql)
::      [_p]- etap obiegu (nazwa)
::      [_q]- delegowany
::   WY: 0/1 - czy udalo sie dodanie zapisu
::----------------------------------------------------------------------------------------------------------------------
DOB_STAT.blank(1);
DOB_STAT.TREE:=_a;
DOB_STAT.WART:=_b;
DOB_STAT.PELWART:=_c;
DOB_STAT.NETTO:=_d;
DOB_STAT.BRUTTO:=_e;
DOB_STAT.DOB_REF:=_f;
DOB_STAT.STATUS:=_g;
DOB_STAT.WALUTA:=_h;
DOB_STAT.USER:=_i;
DOB_STAT.POZIOM:=_j;
DOB_STAT.CONAPOZ:=_k;
DOB_STAT.ROK:=_l;
DOB_STAT.INT:={? var_pres('_m')>0 || _m || '' ?};
DOB_STAT.TP:={? var_pres('_n')>0 || _n || '' ?};
DOB_STAT.ETAP:={? var_pres('_o')>0 || _o || '' ?};
DOB_STAT.ETAPNAZ:={? var_pres('_p')>0 || _p || '' ?};
DOB_STAT.DELEG:={? var_pres('_q')>0 || _q || '' ?};
DOB_STAT.add()


\stazmpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Zmiana poziomow analizy od strony wygenerowanej analizy
::----------------------------------------------------------------------------------------------------------------------
EOS_STAT.index('DISP'); EOS_STAT.prefix(USERS.ref(),VAR.STATYP);
_old_poz:=obj_new(3); {! _i:=1..3 |! _old_poz[_i]:='' !}; _i:=0;
{? EOS_STAT.first()
|| {! |?
      _i+=1; _old_poz[_i]:=EOS_STAT.NAZWA;
      EOS_STAT.next()
   !}
?};
_old_size:=EOS_STAT.size();
EOS_STAT.win_sel('WER');
EOS_STAT.select();
_zmiana:=0;
{? EOS_STAT.size()<>_old_size
|| _zmiana:=1
|| {? EOS_STAT.first()
   || _i:=0;
      {! |?
         _i+=1;
         _zmiana:=(_old_poz[_i]<>EOS_STAT.NAZWA);
         ~_zmiana & EOS_STAT.next()
      !}
   ?}
?};
{? _zmiana || stzmiana:=1; sel_exit() ?}


\staakcje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Akcje z poziomu analizy (statystyka)
::   WE: _a - 1 - pozycje VAT
::            2 - zapotrzebowania
::            3 - podzialy dla kontrolingu
::            4 - pokazanie zalacznika
::            5 - pobranie zalacznika
::            6 - wydruk dokumentu w obiegu
::            7 - obieg
::----------------------------------------------------------------------------------------------------------------------
{? DOB_STAT.DOB_REF<>''
|| _ref:=BB.sqlint(DOB_STAT.DOB_REF); _nam:=form(DOB_STAT.DOB_REF-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || {? EDOKUM.r_lock(1,1,1)
         || staakcje:=dsp_wyd:=1;
            EVAT.cntx_psh(); EVAT.use('skid_a'+(EDOKUM.name()+2));
            EPODZ.cntx_psh(); EPODZ.use('skid_j'+(EDOKUM.name()+2));
            {? _a=1
            || exec('pozvdob','obiegi','PDG')
            |? _a=2
            || exec('zapprzeg','obiegi_zap','PGD')
            |? _a=3
            || exec('podz_dob','obiegi','',1,1)
            |? _a=4
            || exec('dspedob','obiegi',EDOKUM)
            |? _a=5
            || exec('pobierz','obiegi')
            |? _a=6
            || exec('fakt_print','obe','A')
            |? _a=7
            || exec('okn_opis','obiegi')
            ?};
            EVAT.cntx_pop(); EPODZ.cntx_pop();
            VAR_DEL.delete('staakcje','dsp_wyd');
            EDOKUM.r_unlock()
         ?}
      ?};
      EDOKUM.cntx_pop()
   ?}
|| {? typobi=1
   || FUN.info('Akcja dostępna wyłącznie dla najniższego poziomu - faktur w obiegu.'@)
   |? typobi=2
   || FUN.info('Akcja dostępna wyłącznie dla najniższego poziomu - wniosków.'@)
   || FUN.info('Akcja dostępna wyłącznie dla najniższego poziomu - delegacji.'@)
   ?}
?}



:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:20 d59c72b688e1b0f76b6af13e68860547dfb714eb3afe155ffdb7b08f308455bcafd80dd16e486e7a717884e7521347c7916300507a337fe9051ea17a226b3e354695fffce175c46853e4e76a635b0e6155787e2bd02ef3fafc040c82472111796bbe3408f44a1e19215c7be0ae703a54897cc8b6ad3ba30f9c9b46928d7ae3af

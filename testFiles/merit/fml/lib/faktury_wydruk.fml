:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: faktury_wydruk.fml
:: Utworzony: 04.05.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa wydruku dokumentu sprzedaży, zakupu
::======================================================================================================================


\begin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: kluzula BEGIN fak_new.rpm
::   WE:  _a  - nazwa wydruku
::       [_b] - czy pokazywac panel dialogowy z parametrami ([1] -tak, 0 - nie) - gdy nie pokazuje panel nie zapisuje
::              rowniez ustawien wybranych
::  OLD: \begin/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};

FAP.cntx_psh;

exec('end','faktury_wydruk',0);
headfoot:=0;

__wydrfa:=obj_new('uproszcz','samof');

:: do numeracji stron przy drukowaniu paczek faktur
pages:=0;

:: dla wydruku graficznego 1, dla iglowki 0
graf:=exec('get','#params',{? FAKS.SZ='S' || 2121 || 6121 ?},2,OPERATOR.USER)<>'T';
:: dla wydruku na skladance 1 (nie drukuje ORYGINAL/KOPIA)
skladanka:=exec('get','#params',{? FAKS.SZ='S' || 2122 || 6122 ?},2,OPERATOR.USER)='T';
duplex:=0;
:: odstep miedzykolumnowy
__ods:=exec('get','#params',{? FAKS.SZ='S' || 2125 || 6125 ?},2,OPERATOR.USER)='T';
__odsram:={? __ods || 3 || 1 ?};
__odsskr:={? __ods || 2 || 1 ?};
:: czy walutowa
__walh:=FAKS.WAL().KOD;
__walp:=FAKS.NWAL().KOD;
__waln:=INFO.NAROD().KOD;
walutowa:=__walh<>__walp;
:: czy kwota vat liczona od ceny burtto
odbrutto:=FAKS.T().FIS='T';
:: uproszczony wydruk faktury
__wydrfa.uproszcz:=_a='fak_newu' | _a='fakpnewu';
:: czy samofakturowanie
__wydrfa.samof:=_a='zak_wew1' | _a='zakpwew1';
::[PD]sprawdzenie dla starszych werscji
VAR_DEL.delete('mpp');
mpp:=0;
mpp:={? exec('sp_active','faktury_wspolne') || 1 || 0 ?};

color:=prn1:=prn2:='';
lm:=ll:=vp:=lmt:=lmt_org:=rsep:=0;
wyjscie:=0;

:: parametry cenników
TAZ.RAB_TYP:=FAKS.RAB_TYP;
DISP.RAB:=FAKS.RAB;

FAP.cntx_psh();
FAP.index('FAP'); FAP.prefix(FAKS.ref());
{? ~FAP.first() || FUN.info('Brak pozycji faktury.'@); wyjscie:=1 ?};
FAP.cntx_pop();

_wydr_naz:=_a;

PARAM_W.KOPIA:=2;
PARAM_W.ZAM:='N';
PARAM_W.WZ:='N';
PARAM_W.DR_DUPL:='N';
PARAM_W.DUPLIKAT:=date(0,0,0);
_par:='IN';
PARAM_W.SLOWNIE:='T';
PARAM_W.JEZYK:=null;
PARAM_W.DR_RAB:='T';

WYDRUKIL.index('WYDRUKI');
WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
{? ~WYDRUKIL.first()
||
   WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.add()
||
   PARAM_W.KOPIA:=WYDRUKIL.TYP_ZEST;
   PARAM_W.ZAM:=WYDRUKIL.STR1;
   PARAM_W.WZ:=WYDRUKIL.STR2;
:   PARAM_W.DR_DUPL:=WYDRUKIL.STR3;
   PARAM_W.DUPLIKAT:={? PARAM_W.DR_DUPL='T' || date() || date(0,0,0) ?};
   _par:=WYDRUKIL.FAKS_OD;
   PARAM_W.SLOWNIE:={? WYDRUKIL.CHKBOX01 || 'T' || 'N' ?};
   PARAM_W.JEZYK:=WYDRUKIL.JEZYK;
   PARAM_W.DR_RAB:={? WYDRUKIL.CHKBOX02 | FAKS.SPOBLRAB='W'  || 'T' || 'N' ?}
?};
_jezyk_:=PARAM_W.JEZYK;
_djezyk:=exec('kh_jezyk','tlumaczenia',FAKS.KH);
{? _b
|| PARAM_W.JEZYK:={? _djezyk || _djezyk || _jezyk_ ?};
   PARAM_W.JEZYK_KH:={? _djezyk & _djezyk=PARAM_W.JEZYK || 'T' || 'N' ?}
|? exec('czy_grp_dr','faktury_wydruk')
|| PARAM_W.JEZYK:={? __GRP_DR.JEZYK_KH='T' & _djezyk || _djezyk || __GRP_DR.JEZYK ?}
?};

PARAM_W.INDEKS:='N';
PARAM_W.NAZWA:='N';
PARAM_W.MC:='N';
PARAM_W.UWAGI:='N';
PARAM_W.KODOBCY:='N';
PARAM_W.DK_C:='N';
{? _par*'I' || PARAM_W.INDEKS:='T' ?};
{? _par*'N' || PARAM_W.NAZWA:='T' ?};
{? _par*'M' || PARAM_W.MC:='T' ?};
{? _par*'U' || PARAM_W.UWAGI:='T' ?};
{? _par*'K' || PARAM_W.KODOBCY:='T' ?};
{? _par*'C' || PARAM_W.DK_C:='T' ?};

{? FAKS.KRAJ_VAT & __walh<>__walp
|| PARAM_W.WALH:='H'
|? _b
|| PARAM_W.WALH:={? __walh<>__walp || 'T' || 'N' ?}
|| PARAM_W.WALH:={? __walh<>__walp || __pwwalh || 'N' ?}
?};

:: zmiana okienka - inf o zamowieniach/inf o dok. mag.
PARAM_W.win_edit('WYDR_FAK');

{? wyjscie=0
||
   _chk_par:="
      _wyn:='';
      {? PARAM_W.KOPIA<0
      || FUN.info('Ilość kopii nie może być ujemna.'@);
         _wyn:='KOPIA'
      ?};
      {? _wyn='' & PARAM_W.DR_DUPL='T'
      || {? PARAM_W.DUPLIKAT=date(0,0,0)
         || FUN.info('Niewypełniona data wystawienia duplikatu.'@);
            _wyn:='DUPLIKAT'
         ?};
         {? _wyn=''
         || {? PARAM_W.DUPLIKAT<FAKS.DW
            || FUN.info('Data duplikatu wcześniejsza od daty wystawienia dokumentu.'@);
               _wyn:='DUPLIKAT'
            ?}
         ?}
      ?};
      {? _wyn=''
      || {? exec('czy_grp_dr','faktury_wydruk')=0
         || {? FAKS.KRAJ_VAT & __walh<>__walp
            || {? PARAM_W.WALH<>'H'
               || FUN.info('Wybrano kraj vat w nagłówku dokumentu.\n''Wydruk dostępny tylko w walucie handlowej'@);
                  _wyn='WALH'
               ?}
            |? PARAM_W.WALH='H' & __walh=__walp
            || FUN.info('Waluta handlowa identyczna z walutą opodatkowania.\n'@+
                        'Wydruk w walucie handlowej niedostępny.'@);
               _wyn:='WALH'
            |? PARAM_W.WALH='T' & __walh=__walp
            || FUN.info('Waluta handlowa identyczna z walutą opodatkowania.\n'@+
                        'Wydruk w obu walutach niedostępny.'@);
               _wyn:='WALH'
            ?}
         ?}
      ?};
      _wyn
   ";
   {? {? _b=1 & exec('noparam','param_wydr',FAKS.SZ)=0 & exec('upr_cscz','ceny')
      || exec('set_efld_opt_paramw','faktury_wydruk',FAKS.SZ='S' & FAKS.SPOBLRAB='C');
         {? PARAM_W.edit(_chk_par)
         || 1
         || {? exec('czy_grp_dr','faktury_wydruk') || __GRP_DR.ESC:=1 ?}; 0
         ?}
      || 1
      ?}
   ||
      {? _b
      || WYDRUKIL.index('WYDRUKI');
         WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
         {? WYDRUKIL.first()
         ||
            WYDRUKIL.TYP_ZEST:=PARAM_W.KOPIA;
            WYDRUKIL.STR1:=PARAM_W.ZAM;
            WYDRUKIL.STR2:=PARAM_W.WZ;
            WYDRUKIL.STR3:=PARAM_W.DR_DUPL;
            WYDRUKIL.OD_DATY:=PARAM_W.DUPLIKAT;
            WYDRUKIL.FAKS_OD:='';
            {? PARAM_W.INDEKS='T'  || WYDRUKIL.FAKS_OD+='I' ?};
            {? PARAM_W.NAZWA='T'   || WYDRUKIL.FAKS_OD+='N' ?};
            {? PARAM_W.MC='T'      || WYDRUKIL.FAKS_OD+='M' ?};
            {? PARAM_W.UWAGI='T'   || WYDRUKIL.FAKS_OD+='U' ?};
            {? PARAM_W.KODOBCY='T' || WYDRUKIL.FAKS_OD+='K' ?};
            {? PARAM_W.DK_C='T'    || WYDRUKIL.FAKS_OD+='C' ?};
            WYDRUKIL.CHKBOX01:=PARAM_W.SLOWNIE='T';
            WYDRUKIL.JEZYK:={? _djezyk & PARAM_W.JEZYK=_djezyk || _jezyk_ || PARAM_W.JEZYK ?};
            {? exec('czy_grp_dr','faktury_wydruk')
            || __GRP_DR.JEZYK:=PARAM_W.JEZYK;
               __GRP_DR.JEZYK_KH:=PARAM_W.JEZYK_KH
            ?};
            {? PARAM_W.JEZYK_KH='T' & _djezyk || PARAM_W.JEZYK:=_djezyk ?};
            WYDRUKIL.CHKBOX02:=PARAM_W.DR_RAB='T';
            WYDRUKIL.put();
            __pwwalh:=PARAM_W.WALH
         ?}
      ?}
   ||
      wyjscie:=1
   ?}
?};

{? wyjscie=0
||
:: szerokosc kolumn
   VAR_DEL.delete('k');    k:=obj_new(16);
:: szerokosc kolumn dla podsumowania VAT dla dokumentow gdzie VAT liczony od sumy w stawkach
   VAR_DEL.delete('k1');   k1:=obj_new(4);
:: dane drukowane
   VAR_DEL.delete('w');    w:=obj_new(obj_len(k));
:: nazwy kolumn
   VAR_DEL.delete('n');    n:=obj_new(obj_len(k));
:: sumy
   VAR_DEL.delete('s');    s:=obj_new(6);
   VAR_DEL.delete('sp');   sp:=obj_new(13);
   VAR_DEL.delete('spz');  spz:=obj_new(3);
:: do przeniesienia
   VAR_DEL.delete('do_p'); do_p:=obj_new(6);
:: dane naglowkowe
   VAR_DEL.delete('na');   na:=obj_new(10);
:: przeniesienie
   VAR_DEL.delete('prz');  prz:=obj_new(6);
:: szerokosci kolumn naglowkowych
   VAR_DEL.delete('szer'); szer:=obj_new(4);
:: szerokosci pozycji faktur
   VAR_DEL.delete('fak');  fak:=obj_new(obj_len(k));
:: w tym VAT
   VAR_DEL.delete('vat');  vat:=obj_new(3);
   VAR_DEL.delete('vatk'); vatk:=obj_new(3);

   VAR_DEL.delete('PRN1'); PRN1:=obj_new(@.CLASS.PRINT,obj_len(k));
   {? __ods || {? graf || PRN1.s_frame() || PRN1.t_frame() ?} || {? graf || PRN1.sl_frame() || PRN1.tl_frame() ?} ?};
   VAR_DEL.delete('PRN2'); PRN2:=obj_new(@.CLASS.PRINT,5);
   {? __ods || {? graf || PRN2.s_frame() || PRN2.t_frame() ?} || {? graf || PRN2.sl_frame() || PRN2.tl_frame() ?} ?};
   VAR_DEL.delete('PRN3'); PRN3:=obj_new(@.CLASS.PRINT,5);
   {? __ods || {? graf || PRN3.s_frame() || PRN2.t_frame() ?} || {? graf || PRN3.sl_frame() || PRN2.tl_frame() ?} ?};
   VAR_DEL.delete('PRN4'); PRN4:=obj_new(@.CLASS.PRINT,2);            PRN4.n_frame();
   VAR_DEL.delete('PRN5'); PRN5:=obj_new(@.CLASS.PRINT,4);            PRN5.n_frame();

:: opisy w obiektach PRN4 i PRN5
   op_1:=op_2:=op_3:=op_4:='';
:: Poz
   n[1]:=exec('tr','tlumaczenia','1000180');           k[1]:=4+{? mpp || (FAKS.SP_WYM='T')?};
:: Towar / usluga
   n[2]:=exec('tr','tlumaczenia','1000181');           k[2]:=40;
:: CN / PKWiU
   n[3]:=
      {? exec('get','#params',300208,2)='T'
      ||
         exec('tr','tlumaczenia','1000218')+'/'
      ||
         ''
      ?}+
      exec('tr','tlumaczenia','1000182');              k[3]:=10;
:: Ilosc
   n[4]:=exec('tr','tlumaczenia','1000183');           k[4]:=6;
:: jm
   n[5]:=exec('tr','tlumaczenia','1000184');           k[5]:=4;
   {? FAKS.T().FIS='T'
:: Cena brutto przed rab.
   || n[6]:=
         {? exec('czy_rab','faktury_wspolne')
         || {? WYDRUKIL.WYDRUK='zak_rr'
            || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
               +' '+exec('tr','tlumaczenia','1000240')
            || exec('tr','tlumaczenia','1000185')
            ?}
         || {? WYDRUKIL.WYDRUK='zak_rr'
            || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000193')
            ?}
         ?};                                            k[6]:=11
:: Cena netto przed rab.
   || n[6]:=
         {? exec('czy_rab','faktury_wspolne')
         || {? WYDRUKIL.WYDRUK='zak_rr'
            || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
               +' '+exec('tr','tlumaczenia','1000240')
            || exec('tr','tlumaczenia','1000186')
            ?}
         || {? WYDRUKIL.WYDRUK='zak_rr'
            || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000188')
            ?}
         ?};                                             k[6]:=10
   ?};
:: % rab.
   {? exec('czyrabp','ceny',FAKS.RAB_TYP)
   || n[7]:=exec('tr','tlumaczenia','1000187');          k[7]:=5
   || n[7]:=exec('tr','tlumaczenia','1000199');          k[7]:=6
   ?};
   {? odbrutto=1 & FAKS.NDVAT='N'
:: Cena brutto
   || n[8]:={? WYDRUKIL.WYDRUK='zak_rr'
            || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000193')
            ?};                                          k[8]:=10
   ||
:: Cena netto
      n[8]:={? WYDRUKIL.WYDRUK='zak_rr'
            || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000188')
            ?};                                          k[8]:=10
   ?};
:: Wartosc netto
   n[9]:={? WYDRUKIL.WYDRUK='zak_rr'
         || exec('tr','tlumaczenia','1000241')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
         || exec('tr','tlumaczenia','1000189')
         ?};                                             k[9]:=10;  k1[1]:=10;
:: Stawka VAT
   n[10]:={? WYDRUKIL.WYDRUK='zak_rr'
          || exec('tr','tlumaczenia','1000242')+' '+exec('tr','tlumaczenia','1000236')
          || exec('tr','tlumaczenia','1000190')
          ?};                                           k[10]:=6;
:: Kwota VAT
   n[11]:=  {? WYDRUKIL.WYDRUK='zak_rr'
            || exec('tr','tlumaczenia','1000243')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000191')
            ?};                                         k[11]:=10;  k1[2]:=10;
:: Wartosc brutto
   n[12]:={? WYDRUKIL.WYDRUK='zak_rr'
          || exec('tr','tlumaczenia','1000241')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
          || exec('tr','tlumaczenia','1000192')
          ?};                                           k[12]:=10;  k1[3]:=10;
:: Waluta
   {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
   || n[13]:=exec('tr','tlumaczenia','1000152');        k[13]:=6
   ?};
   {? odbrutto=1 & FAKS.NDVAT='N'
   ||
:: Wartosc brutto przed rabatem
      n[14]:={? WYDRUKIL.WYDRUK='zak_rr'
             || exec('tr','tlumaczenia','1000241')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
                +' '+exec('tr','tlumaczenia','1000240')
             || exec('tr','tlumaczenia','1000198')
             ?};                                         k[14]:=14
   ||
:: Wartosc netto przed rabatem
      n[14]:={? WYDRUKIL.WYDRUK='zak_rr'
             || exec('tr','tlumaczenia','1000241')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
                +' '+exec('tr','tlumaczenia','1000240')
             || exec('tr','tlumaczenia','1000197')
             ?};                                         k[14]:=13
   ?};
:: Kwota rabatu
   n[15]:=exec('tr','tlumaczenia','1000199');            k[15]:=8;
:: Data sprzzedaży pozycji
   n[16]:=exec('tr','tlumaczenia','1000112');            k[16]:=10;

:: wyliczenie szerokosci kolumn wg etykiet
   {! _i:=1..obj_len(n)
   |!
      {? var_pres('['+$_i+']',n)=type_of('stringi')
      ||
         _max:=0;
         STR.split(n[_i]);
         {! |? _word:=STR.get_word(); {? +_word>_max || _max:=+_word ?}; STR.next() !};
         k[_i]:=_max
      ?}
   !};
   k1[1]:=k[9];
   k1[2]:=k[11];
   k1[3]:=k[12];

   {! _i:=1..obj_len(k) |! w[_i]:=0 !};
   {! _i:=1..obj_len(s) |! s[_i]:=0 !};
   zero_na:="{! _i:=1..obj_len(na)|! na[_i]:='' !}";
   w[2]:=w[6]:='';

:: ilosc znakow w wierszu
   max_T8B:=max_T10B:=max_T8G:=max_T8:=0;
   licznik:=ilkop:=stron:=0;
   Text:=Text1:='';
:: ilosc pozycji
   poz:=0;

   ndvat:=FAKS.NDVAT;
   __oo:=0;
   vatzpoz:=FAKS.T().VATZPOZ='T';

:: spr. ile miejsc po przecinku drukowac
   FAP.index('FAP');
   FAP.prefix(FAKS.ref);
:: dokładność ilości
   ddokl:=exec('fld_prec','#field',FAP,'IL');
:: dokładność ceny przed i po rabacie
   _cdokl_cwal:=exec('fld_prec','#field',FAP,'CWAL');
   _cdokl_cpr:=exec('fld_prec','#field',FAP,'CPR');
   _cdokl_cb:=exec('fld_prec','#field',FAP,'CB');
   _cdokl_cn:=exec('fld_prec','#field',FAP,'CN');
   cprdokl:=0;
   cdokl:=0;
   {? walutowa=0 | walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='N')
   ||
      _spp:=__walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & FAKS.T().SPP='T';
      {? _spp=0 || cprdokl:=_cdokl_cpr ?};
      cdokl:={? odbrutto=1 || _cdokl_cb || _cdokl_cn ?}
   ?};
   {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
   ||
      {? cprdokl=0 | _cdokl_cwal>cprdokl || cprdokl:=_cdokl_cwal ?};
      FAP.cntx_psh();
      FAP.index('FAP');
      FAP.prefix(FAKS.ref());
      _loop:=FAP.first();
      {!
      |? _loop
      |!
         _cdokl:=
            {? FAKS.SZ='S'
            || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
            || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
            ?};
         _cdokl:=+(2-$
            fabs(
               frac(
                  {? exec('czyrabp','ceny',FAKS.RAB_TYP)
                  || _rabat:=1-exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)/100;
                     (FAP.CWAL*_rabat)$_cdokl
                  || FAP.CWAL-FAP.RABK-(FAKS.RAB*FAP.CWAL/100)$_cdokl
                  ?}
               )
            )
         );
         {? cdokl=0 | _cdokl>cdokl || cdokl:=_cdokl ?};
         _loop:=FAP.next()
      !};
      FAP.cntx_pop()
      ?};
:: dokładność rabatu
   _rozn:=cdokl-2;
   {? _rozn<0 || _rozn:=0 ?};
   k[6]+=_rozn;
   k[8]+=_rozn;
   rdokl:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || _rozn:=0; 2 || cdokl ?};
   k[7]+=_rozn;
:: Jeżeli parametr jest włączony to sugerujemy się tylko dokładnością na materiale
   {? exec('get','#params', 100235, 2)='T'
   || FAP.cntx_psh();
      FAP.index('FAP');
      FAP.prefix(FAKS.ref());
      {? FAP.first()
      || {!|?
            _cdokl:=
               {? FAKS.SZ='S'
               || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
               || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
               ?};
            {? _cdokl>cdokl || cdokl:=_cdokl ?};
            FAP.next()
         !}
      ?};
      FAP.cntx_pop();
      {? cdokl>cprdokl  || cprdokl:=cdokl ?};
      {? cdokl>rdokl    || rdokl:=cdokl   ?}
   ?};

   print1:="
      VAR_DEL.delete('PRN1');
      PRN1:=obj_new(@.CLASS.PRINT,obj_len(k));
      {? __ods || {? graf || PRN1.s_frame() || PRN1.t_frame() ?} || {? graf || PRN1.sl_frame() || PRN1.tl_frame() ?} ?};
      {? k[1] || PRN1.add($('w[1]'),               k[1], n[1],1) ?};
      {? k[2] || PRN1.add($('form(w[2])'),         k[2], n[2]) ?};
      {? k[3] || PRN1.add($('form(w[3])'),         k[3], n[3]) ?};
      {? k[4] || PRN1.add($('form(w[4],,ddokl)'),  k[4], n[4],1  ) ?};
      {? k[5] || PRN1.add($('form(w[5],,2)'),      k[5], n[5]) ?};
      {? k[6] || PRN1.add($('form(w[6],,cprdokl)'),k[6], n[6],1) ?};
      {? k[7] || PRN1.add($('form(w[7],,rdokl)'),  k[7], n[7],1) ?};
      {? k[8] || PRN1.add($('form(w[8],,cdokl)'),  k[8], n[8],1) ?};
      {? k[16] || PRN1.add($('form(w[16])'),       k[16], n[16]) ?};
      {? k[14] || PRN1.add($('form(w[14],,2)'),    k[14], n[14],1) ?};
      {? k[15] || PRN1.add($('form(w[15],,2)'),    k[15], n[15],1) ?};
      {? k[9] | k[12]
      || {? vatzpoz=1 | odbrutto=0
        ||      PRN1.add($('form(w[9],,2)'),      k[9], n[9],1)
         |? odbrutto=1
         ||      PRN1.add($('form(w[12],,2)'),     k[12],n[12],1)
         ?}
      ?};
      {? k[10] & ~('TO'*ndvat)
      ||         PRN1.add($('form(w[10])'),        k[10],n[10],1)
      ?};
      {? k[11] & ~('TO'*ndvat) & vatzpoz=1
      ||         PRN1.add($('form(w[11],,2)'),     k[11],n[11],1)
      ?};
      {? k[12] & ~('TO'*ndvat) & vatzpoz=1
      ||         PRN1.add($('form(w[12],,2)'),     k[12],n[12],1)
      ?};
      {? k[13] & walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      ||         PRN1.add($('form(w[13],,2)'),     k[13],n[13])
      ?}
   ";
   print2:="
::    WE:   _a - 0-uwzgledniac kolumne waluty, 1-wpp
::          _b - 0-podsumowanie pozycji, 1-tabelka vat
      {? var_pres('_a')<>type_of(0) || _a:=0 ?};
      {? var_pres('_b')<>type_of(0) || _b:=0 ?};
      VAR_DEL.delete('PRN2');
      PRN2:=obj_new(@.CLASS.PRINT,6);
      {? __ods || {? graf || PRN2.s_frame() || PRN2.t_frame() ?} || {? graf || PRN2.sl_frame() || PRN2.tl_frame() ?} ?};
      {? _b=0 & k[1]
      ||
         PRN2.add($('form(w[1])')
            ,_ret:=0; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13)|| k[_ii]+__odsram || 0 ?}) !}; _ret-__odsram
            ,n[1])
      ?};
      {? ~('TO'*ndvat)
      ||
         {? _b=0
         ||
            {? vatzpoz
         ||
            {? k[9]  || PRN2.add($('form(w[9],,2)')   ,k[9] ,n[9] ,1) ?};
            {? k[10] || PRN2.add($('form(w[10])')     ,k[10],n[10],1) ?};
            {? k[11] || PRN2.add($('form(w[11],,2)')  ,k[11],n[11],1) ?};
            {? k[12] || PRN2.add($('form(w[12],,2)')  ,k[12],n[12],1) ?}
            ||
               {? odbrutto
               ||
                  {? k[12] || PRN2.add($('form(w[12],,2)')  ,k[12],n[12],1) ?};
                  {? k[10] || PRN2.add($('form(w[10])')     ,k[10],n[10],1) ?}
               ||
                  {? k[9]  || PRN2.add($('form(w[9],,2)')   ,k[9] ,n[9] ,1) ?};
                  {? k[10] || PRN2.add($('form(w[10])')     ,k[10],n[10],1) ?}
               ?}
            ?}
         |? _b=1
         ||
            {? k1[1] || PRN2.add($('form(w[9],,2)')   ,k1[1],n[9] ,1) ?};
            {? k[10] || PRN2.add($('form(w[10])')     ,k[10],n[10],1) ?};
            {? k1[2] || PRN2.add($('form(w[11],,2)')  ,k1[2],n[11],1) ?};
            {? k1[3] || PRN2.add($('form(w[12],,2)')  ,k1[3],n[12],1) ?}
         ?}
      |? k[9]
      ||
         {? k[9] || PRN2.add($('form(w[9],,2)'),k[9],n[9],1) ?}
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & _a=0 & k[13] || PRN2.add($('form(w[13],,2)'),  k[13],n[13]) ?}
   ";
   print3:="
      VAR_DEL.delete('PRN3');
      PRN3:=obj_new(@.CLASS.PRINT,6);
      {? __ods || {? graf || PRN3.s_frame() || PRN3.t_frame() ?} || {? graf || PRN3.sl_frame() || PRN3.tl_frame() ?} ?};
      {? k[1]
      || PRN3.add($('form(prz[1])'),
            _ret:=0; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13) || k[_ii]+__odsram || 0 ?}) !};_ret-__odsram,n[1])
      ?};
      {? ~('TO'*ndvat) & vatzpoz=0 & odbrutto=1
      ||
         {? k[12] || PRN3.add($('form(prz[5],,2)') ,k[12],n[12],1) ?};
         {? k[10] || PRN3.add($('form(prz[3])')    ,k[10],n[10],1) ?}
      |? ~('TO'*ndvat) & vatzpoz=0 & odbrutto=0
      ||
         {? k[9]  || PRN3.add($('form(prz[2],,2)') ,k[9] ,n[9] ,1) ?};
         {? k[10] || PRN3.add($('form(prz[3])')    ,k[10],n[10],1) ?}
      |? ~('TO'*ndvat)
      ||
         {? k[9]  || PRN3.add($('form(prz[2],,2)') ,k[9] ,n[9] ,1) ?};
         {? k[10] || PRN3.add($('form(prz[3])')    ,k[10],n[10],1) ?};
         {? k[11] || PRN3.add($('form(prz[4],,2)') ,k[11],n[11],1) ?};
         {? k[12] || PRN3.add($('form(prz[5],,2)') ,k[12],n[12],1) ?}
      ||
         {? k[9] || PRN3.add($('form(prz[2],,2)'),k[9],n[9],1) ?}
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & k[13] || PRN3.add($('form(prz[6],,2)'),  k[13],n[13]) ?}
   ";
   print4:="
      prn1:='PRN4';
      op_1:=op_2:='';
      VAR_DEL.delete('PRN4');
      PRN4:=obj_new(@.CLASS.PRINT,2);
      PRN4.n_frame();
      {? _a || PRN4.add($('form(op_1)'),_a,'') ?};
      {? _b || PRN4.add($('form(op_2)'),_b,'',1) ?}
   ";

:: zamowienia
   zam:='';
   {? -PARAM_W.ZAM='t'
   ||
      {? var_pres('X_Xp')>0 || obj_del(X_Xp) ?};
      {? FAKS.WHERE='L'
      ||
         _qq:=$"
            select
               FAPOW.ZK_N_SYM SYM
            from
               FAPOW
            where
               FAPOW.FAKS=':_a'
               and FAPOW.ZK_N<>''
            group by
               FAPOW.ZK_N_SYM
            order by
               1
         ";
         X_Xp:=sql(_qq,$FAKS.ref())
      |? FAKS.SZ='S'
      ||
         X_Xp:=sql('select ZK_N.SYM SYM  from @ZK_RN join @ZK_N using (ZK_RN.N, ZK_N.REFERENCE)
                     where ZK_RN.FAKS=\':_a\' group by ZK_N.SYM order by 1',$FAKS.ref)
      |? FAKS.SZ='Z'
      ||
         X_Xp:=sql('select ZD_NAG.SYM SYM  from @ZD_RN join @ZD_NAG using (ZD_RN.ZD_NAG, ZD_NAG.REFERENCE)
                     where ZD_RN.FAKS=\':_a\' group by ZD_NAG.SYM order by 1',$FAKS.ref)
      ?};
      {? X_Xp.first
      || zam:=exec('tr','tlumaczenia','1000148');
         _sep:=' '; {! |? zam+=_sep+X_Xp.SYM; _sep:=', ';X_Xp.next !}
      ?}
   ?};

:: dokumenty magazynowe
   mag:='';
   {? -PARAM_W.WZ='t'
   ||
      exec('dok_maga','powdok',0); __tt.prefix();
      {? __tt.first || _sep:=' '; {! |? mag+=_sep+'%1 %3 %2'@[__tt.SYM,$__tt.DATA,exec('tr','tlumaczenia','1000219')];_sep:=', ';__tt.next !} ?};
      {? mag<>'' || mag:=exec('tr','tlumaczenia','1000149')+mag ?}
   ?};

:: lista paragonów
   paragon:='';
   {? FAKS.SZ='S' & FAKS.T().PAR='N' & FAKS.T().FIS='T' & FAKS.SYMF<>''
   ||

      VAR_DEL.delete('X_Xp');
      X_Xp:=sql($"select FAKS.SYM SYM, FAKS.PAR PAR, FAKS.D DS from @FAKS where FAKS.SYMF=':_a'",$FAKS.ref());
      {? X_Xp.first
      || paragon:=exec('tr','tlumaczenia','1000169');
         _ds:=' '+exec('tr','tlumaczenia','1000112')+' ';
         _sep:=' ';
         {!
         |? paragon+=_sep+{? X_Xp.PAR=''
                          || X_Xp.SYM
                          || X_Xp.PAR
                          ?}+_ds+$X_Xp.DS; _sep:=', ';
            X_Xp.next
         !}
      ?};
      VAR_DEL.delete('X_Xp')
   ?};

:: przypisanie wartosci dla pozycji dokumentu
   pozycja:="
      _spp:=walutowa=0 & __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & FAKS.T().SPP='T';
      {? mpp
      ||
      w[1]:='';
      {? walutowa=0 | PARAM_W.WALH='H'
      ||
         _mpp:='';
         {? FAKS.SP_WYM='T' & FAKS.SZ='S' & FAKS.KOR=''
         ||
            _mpp:=' ';
            {? FAP.SP_WYM='T' || _mpp:='*' ?}
         ?};
         w[1]:=form(FAP.POZ,,0,'99')+_mpp
         ?}
      ||
          w[1]:={? walutowa=0 | PARAM_W.WALH='H' || form(FAP.POZ,,0,'99') || '' ?}
      ?};
      w[2]:='';
      _tlumacz:='';
      {? PARAM_W.JEZYK<>null
      || _tlumacz:=exec('trans_m','tlumaczenia',FAP.M,PARAM_W.JEZYK)
      ?};
      {? PARAM_W.INDEKS='T'
      ||
         {? +FAP.MX
         || w[2]+=FAP.MX+' '
         || w[2]+={? FAP.M<>null || FAP.M().KTM+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.NAZWA='T' & (PARAM_W.JEZYK=null | _tlumacz='')
      ||
         {? +FAP.NX
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+FAP.NX+' '
         |? FAP.M
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+{? FAP.M<>null || FAP.M().N+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.JEZYK<>null & _tlumacz<>''
      ||
         {? +FAP.MX
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_tlumacz+' '
         || w[2]+={? w[2]<>''||' - ' || '' ?}+{? FAP.M<>null || _tlumacz+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.UWAGI='T' & FAP.memo_lin('UWAGI')<>'\n' & (FAP.M<>null | +FAP.NX)
      || FAP.memo_get(,'UWAGI');
         w[2]+={? w[2]<>'' || ' - ' || '' ?}+gsub(FAP.memo_txt(0,1,'UWAGI'),'\n',' ')
      ?};
      {? PARAM_W.KODOBCY='T' & FAP.M<>null & FAKS.KH<>null
      || _kod_obcy:=exec('kod_obcy','kody_kresk',FAP.M,FAKS.KH);
         {? _kod_obcy<>''
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_kod_obcy
         ?}
      ?};
      {? PARAM_W.DK_C='T' & FAP.DK_C<>null
      || _dk_c:=form(exec('sym_dk_c','mat_atr',FAP.DK_C));
         {? _dk_c<>''
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_dk_c
         ?}
      ?};
      w[2]:={? walutowa=0 | PARAM_W.WALH='H' || w[2] || '' ?};

      w[3]:={? walutowa=0 | PARAM_W.WALH='H' || exec('pkwiu','material',FAKS.DW,FAP.M,1,FAP.PKWIU_CN) || '' ?};
      w[4]:={? walutowa=0 | PARAM_W.WALH='H' || FAP.IL || '' ?};
      {? PARAM_W.JEZYK<>null
      || _tlumacz:=exec('trans_jm','tlumaczenia',FAP.JM,PARAM_W.JEZYK)
      ?};
      w[5]:={? walutowa=0 | PARAM_W.WALH='H' || {? PARAM_W.JEZYK<>null & _tlumacz<>'' || _tlumacz || FAP.JM().KOD ?} || '' ?};
      w[10]:=FAP.SV().KOD;
      w[13]:={? walutowa=1 & (PARAM_W.WALH='T'  | PARAM_W.WALH='H') || __walh || __walp ?};

      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      || _cdokl:={? FAKS.SZ='S'
                 || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
                 || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
                 ?};
         w[6]:=FAP.CWAL;
         w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP)
               || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)
               || FAP.RABK+(FAKS.RAB*FAP.CWAL/100)$_cdokl
               ?};
         w[8]:=
            {? exec('czyrabp','ceny',FAKS.RAB_TYP)
            || _rabat:=1-exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)/100;
               (FAP.CWAL*_rabat)$_cdokl
            || FAP.CWAL-FAP.RABK-(FAKS.RAB*FAP.CWAL/100)$_cdokl
            ?};
         s[4]+=w[9]:=FAP.WWAL;
         s[5]+=w[11]:=FAP.VWAL;
         s[6]+=w[12]:=FAP.BWAL
      ||
         w[6]:={? _spp || '' || FAP.CPR ?};
         w[8]:={? odbrutto=1 || FAP.CB || FAP.CN ?};
         w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP) || FAP.CPR-w[8] ?};
         s[1]+=w[9]:=FAP.WWAL_P;
         s[2]+=w[11]:=FAP.VWAL_P;
         s[3]+=w[12]:=FAP.BWAL_P;
         sp[1]+=FAP.WWAL_P;sp[2]+=FAP.VWAL_P;sp[3]+=FAP.BWAL_P
      ?};
      w[14]:={? _spp || '' || (FAP.IL*w[6])$2 ?};
      w[15]:=w[14]-{? odbrutto=1 || w[12] || w[9] ?};
      w[16]:={? FAP.D<>date(0,0,0) & (walutowa=0 | PARAM_W.WALH='N' | PARAM_W.WALH='H')
             || gsub(form(FAP.D,,3),'/','-')
             || ''
             ?};
::    szacowanie ilosci lini pozycji
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+1
   ";

:: agregowanie wartosci do przeniesienia
   do_prze:="
      do_p[1]+=FAP.WWAL_P;
      do_p[2]+=FAP.VWAL_P;
      do_p[3]+=FAP.BWAL_P;
      do_p[4]+=FAP.WWAL;
      do_p[5]+=FAP.VWAL;
      do_p[6]+=FAP.BWAL
   ";
   do_przep:="
      do_p[1]+=FAP.WWAL_P;
      do_p[2]+=FAP.VWAL_P;
      do_p[3]+=FAP.BWAL_P;
      do_p[4]+=0;
      do_p[5]+=0;
      do_p[6]+=0";
   do_przeh:="
      do_p[1]+=0;
      do_p[2]+=0;
      do_p[3]+=0;
      do_p[4]+=FAP.WWAL;
      do_p[5]+=FAP.VWAL;
      do_p[6]+=FAP.BWAL";

   zero_na();
   kw_fak:=0;
   faks:=BB.refsql(FAKS.ref);
   FAKSV.clear;
   FAKSV.index('FF_SV');
   FAKSV.prefix('',faks);
   {? FAKS.SZ='S' & ~FAKSV.first
   ||
      exec('faksv_mk','faktury_vat',faks)
   ?}
?}


\end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: klauzula END w fak_new.rpm
::   WE: [_a] =1-zdejmowac konteksty; =0-wpp
::  OLD: \end/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};

{? _a || FAP.cntx_pop ?};

VAR_DEL.delete('__wydrfa');

VAR_DEL.delete('color','prn1','prn2');
VAR_DEL.delete('lm','ll','vp','lmt','lmt_org','rsep');
VAR_DEL.delete('faks');
VAR_DEL.delete('wyjscie');

undefine();

VAR_DEL.delete('k','w','n','s','sp','spz','do_p','na','prz');
VAR_DEL.delete('szer','fak','vat','vatk');
VAR_DEL.delete('PRN1','PRN2','PRN3','PRN4','PRN5','STR1');

VAR_DEL.delete('ndvat','__oo','kw_fak');
VAR_DEL.delete('graf','skladanka','walutowa','odbrutto','vatzpoz');
VAR_DEL.delete('op_1','op_2','op_3','op_4');
VAR_DEL.delete('zero_na','licznik','ilkop','stron','pages');
VAR_DEL.delete('max_T8','max_T8G','max_T10B','max_T8B');
VAR_DEL.delete('Text','Text1','poz');
VAR_DEL.delete('ddokl','cdokl','rdokl');
VAR_DEL.delete('print1','print2','print3','print4');
VAR_DEL.delete('zam','mag','pozycja','do_prze','paragon');
VAR_DEL.delete('il_lin');
VAR_DEL.delete('M_GD','S_GD');
VAR_DEL.delete('__walh','__walp','__waln');
VAR_DEL.delete('bpage','slownie','zal','ok','head','duplex','__odsram','__odsskr');
VAR_DEL.delete('headfoot')


\rep_mask
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Ustala maske raportu
::   WE: _a - Czy pdf 0-nie 1-tak
::   WY: maska
::  OLD: \rep_mask/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.T().KOR='T' | FAKS.T().KOR='Z'
|| {? _a=1
   || {? BEER.SZ='S' || {? FAKS.WHERE='K' || 'korzp*' || 'korsp*' ?} || 'korp*' ?}
   || {? BEER.SZ='S' || {? FAKS.WHERE='K' || 'korzb*' || 'kors_*' ?} || 'kor_*' ?}
   ?}
|| {? _a=1
   || {? BEER.SZ='S' || 'lsp_fakp' || 'zakp' ?}+'*'
   || {? BEER.SZ='S' || 'lsp_fak_' || 'zak_' ?}+'*'
   ?}
?}


\czy_grp_dr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Czy wydruk grupowy?
::   WY: 0-nie 1-tak
::  OLD: \czy_grp_dr/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
var_pres('__GRP_DR')>100 & __GRP_DR.GR


\w_tym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wylicza udzial stawki VAT na fakturze - dla wydruku faktury VAT
::   WE: _a - ref faktury
::       _b - ref stawki VAT
::       _c - tabela kwot _vat
::       _d - ceny : 0 - PLN , 1 - WAL 2- WAL podatkowa
::       [_e] - pozycje do sprawdzenia - string, domyslnie pusty
::       [_f] - tabela kwot _vat
::  OLD: \w_tym/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=4  || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
{? _>=5  || {? type_of(_e)<>2 || _e:='' ?} || _e:='' ?};
{? _>=6  || {? type_of(_f)<>type_of(obj_new(3)) || _f:=_c ?} || _f:=_c ?};

{? _e<>'' | FAKS.SZ='S' | FAKS.SZ='Z' & FAKS.T().KOR='T'
||
   FAP.cntx_psh();
   FAP.index('FAP');
   FAP.prefix(_a);
   _c[1]:=_c[2]:=_c[3]:=0;
   _f[1]:=_f[2]:=_f[3]:=0;
   _stvat:='';
   {? _e<>'' & FAKS.T().VATZPOZ='N'
   || _loop:=FAP.first();
      {!
      |? _loop
      |!
         {? _e*('/'+$FAP.POZ+'/') & _stvat*($FAP.SV)=0 || _stvat+=$FAP.SV ?};
         _loop:=FAP.next()
      !}
   ?};
   {? FAP.first
   ||
      {!
      |?
         _warunek:=(_e='' | _e*('/'+$FAP.POZ+'/') | _stvat*($FAP.SV));
         {? FAP.FAKS().KOR<>'' & _warunek
         || exec('wysw_kor','faktury_poz');
            {? FAP.SV=_b
            ||
               {? _d=0
               || _c[1]+=FKOR.BWN;_c[2]+=FKOR.BWV;_c[3]+=FKOR.BWB;
                  _f[1]+=FKOR.WN;_f[2]+=FKOR.WV;_f[3]+=FKOR.WB
               |? _d=1
               || _c[1]+=FKOR.BWWAL;_c[2]+=FKOR.BVWAL;_c[3]+=FKOR.BBWAL;
                  _f[1]+=FKOR.WWAL;_f[2]+=FKOR.VWAL;_f[3]+=FKOR.BWAL
               |? _d=2
               || {? FKOR.BWWAL_P<>0
                  || _c[1]+=FKOR.BWWAL_P;_c[2]+=FKOR.BVWAL_P;_c[3]+=FKOR.BBWAL_P;
                     _f[1]+=FKOR.WWAL_P;_f[2]+=FKOR.VWAL_P;_f[3]+=FKOR.BWAL_P
                  || _c[1]+=FKOR.BWN;_c[2]+=FKOR.BWV;_c[3]+=FKOR.BWB;
                     _f[1]+=FKOR.WN;_f[2]+=FKOR.WV;_f[3]+=FKOR.WB
                  ?}
               ?}
            ?}
         |? _warunek
         || {? FAP.SV=_b
            ||
               {? _d=0
               || _c[1]+=FAP.WN;_c[2]+=FAP.WV;_c[3]+=FAP.WB
               |? _d=1
               || _c[1]+=FAP.WWAL;_c[2]+=FAP.VWAL;_c[3]+=FAP.BWAL
               |? _d=2
               || {? FAP.WWAL_P<>0
                  || _c[1]+=FAP.WWAL_P;_c[2]+=FAP.VWAL_P;_c[3]+=FAP.BWAL_P
                  || _c[1]+=FAP.WN;_c[2]+=FAP.WV;_c[3]+=FAP.WB
                  ?}
               ?}
            ?}
         ?};
         FAP.next
      !}
   ?};
   FAP.cntx_pop
|| FAKSV.cntx_psh();
   FAKSV.use('faksv'+(8+$_a+3));
   FAKSV.index('FAKS_SV');
   FAKSV.prefix($_a);
   _c[1]:=_c[2]:=_c[3]:=0;
   {? FAKSV.first()
   || {!
      |? {? FAKSV.SV=_b
         || _c[1]+=FAKSV.WN;_c[2]+=FAKSV.WV;_c[3]+=FAKSV.WB
         ?};
         FAKSV.next()
      !}
   ?};
   FAKSV.cntx_pop()
?}


\ae_zn_duplikat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: formula po redakcji pola czy duplikatu
::   WY: 1
::  OLD: \ae_zn_duplikat/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
PARAM_W.DUPLIKAT:={? PARAM_W.DR_DUPL='T' || date() || date(0,0,0) ?};
exec('set_efld_opt_paramw','faktury_wydruk',FAKS.SZ='S' & FAKS.SPOBLRAB='C');
win_disp();
_wyn


\be_duplikat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: formula przed edycja daty duplikatu
::   WY: czy mozna redagowac 1/0
::  OLD: \be_duplikat/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? PARAM_W.DR_DUPL<>'T' || _wyn:=0 ?};
_wyn


\print5_set1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: ustawienia print5 konfiguracja 1
::  OLD: \print5_set1/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
szer[1]:={? __STRTAB='ZD_NAG' || 12 |? __STRTAB='OPAK_N' || 13 || 11 ?};   "--Sprzedawca,Nabywca,Platnik T10B'--";
szer[2]:=(max_T10B-13-(2*szer[1]))/2$0;
szer[3]:=szer[1];
szer[4]:=1;
print5(szer[1],szer[2],szer[3],szer[4]);
szer[4]+=charleft-PRN5.width();
print5(szer[1],szer[2],szer[3],szer[4])


\print5_set2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: ustawienia print5 konfiguracja 2
::  OLD: \print5_set2/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
_sz1:=szer[1];
_sz2:=_sz3:=_sz4:=1;
print5(_sz1,_sz2,_sz3,_sz4);
_sz2+=charleft-PRN5.width();
print5(_sz1,_sz2,_sz3,_sz4); op_2:=na[1]


\print5_set3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: ustawienia print5 konfiguracja 3
::  OLD: \print5_set3/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
_sz1:=szer[1];
_sz2:=1;
_sz3:=1;
_sz4:=32;
print5(_sz1,_sz2,_sz3,_sz4);
_sz2+=charleft-PRN5.width();
print5(_sz1,_sz2,_sz3,_sz4); op_2:=na[1]


\kpocz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca jak w wyniku
::   WE: _a - kod pocztowy
::       _b - poczta
::       _c - miasto
::       _d - ulica
::   WY: '99-999 MIASTO' lub '99-999 POCZTA, MIASTO'
::  OLD: \kpocz/wydruki.fml
::----------------------------------------------------------------------------------------------------------------------
_miasto:=-gsub(_c,' ','');
{? form(_a)=form(_b) | _b*_a || _a:='' ?};
_dr_miasto:=_miasto<>'';
{? _dr_miasto
||
   _poczta:=_b;
   {? _a<>'' || _poczta:=gsub(_poczta,_a,'') ?};
   _poczta:=-gsub(_poczta,' ','');
   _dr_miasto:=((_poczta*_miasto)=0) & ~(var_press('_d')=2 & (_d=''| #_d<>0))
?};
_a+{? +_b || {? +_a || ' '+_b || _b ?} || '' ?}+{? _dr_miasto || {? (+_a + 1 + +_poczta)>7 || ', ' || ' ' ?}+_c || '' ?}


\begin_k1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: kluzula BEGIN kor_new.rpm
::   WE:  _a  - nazwa wydruku
::       [_b] - czy pokazywac panel dialogowy z parametrami ([1] -tak, 0 - nie) - gdy nie pokazuje panel nie zapisuje
::              rowniez ustawien wybranych
::  OLD: \begin_k1/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
_wydr_naz:=_a;
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};

exec('end_k1','faktury_wydruk');
headfoot:=0;

__wydrfa:=obj_new('uproszcz','samof');

:: do numeracji stron przy drukowaniu paczek faktur
pages:=0;

:: dla wydruku graficznego 1, dla iglowki 0
graf:=exec('get','#params',{? FAKS.SZ='S' || 2121 || 6121 ?},2,OPERATOR.USER)<>'T';
:: dla wydruku na skladance 1 (nie drukuje ORGINAL/KOPIA)
skladanka:=exec('get','#params',{? FAKS.SZ='S' || 2122 || 6122 ?},2,OPERATOR.USER)='T';
duplex:=0;
:: odstep miedzykolumnowy
__ods:=exec('get','#params',{? FAKS.SZ='S' || 2125 || 6125 ?},2,OPERATOR.USER)='T';
__odsram:={? __ods || 3 || 1 ?};
__odsskr:={? __ods || 2 || 1 ?};
:: czy walutowa
__walh:=FAKS.WAL().KOD;
__walp:=FAKS.NWAL().KOD;
__waln:=INFO.NAROD().KOD;
walutowa:=__walh<>__walp;
:: czy kwota vat liczona od ceny burtto
odbrutto:=FAKS.T().FIS='T';
:: pozycje korygowne
p_druk:='';
:: uproszczony wydruk faktury
__wydrfa.uproszcz:=0;
:: czy samofakturowanie
__wydrfa.samof:=0;
::[PD]sprawdzenie dla starszych werscji
VAR_DEL.delete('mpp');
mpp:=0;
mpp:={? _wydr_naz<>'korzb_poz' & exec('sp_active','faktury_wspolne') || 1 || 0 ?};

:: korekta zbiorcza
loop:=obj_new('loop','faks_ref','kz');
loop.loop:=1;
loop.faks_ref:=FAKS.ref();
loop.kz:=exec('FindInSet','#table','FAKS_KZF','POZ',FAKS.ref())<>null();

color:=prn1:=prn2:='';
lm:=ll:=vp:=lmt:=lmt_org:=rsep:=0;
wyjscie:=0;

:: parametry cenników
TAZ.RAB_TYP:=FAKS.RAB_TYP;
DISP.RAB:=FAKS.RAB;

{? ~loop.kz
||
   FAP.cntx_psh();
   FAP.index('FAP'); FAP.prefix(FAKS.ref());
   {? ~FAP.first() || FUN.info('Brak pozycji faktury.'@); wyjscie:=1 ?};
   FAP.cntx_pop()
?};

PARAM_W.KOPIA:=2;
PARAM_W.ZAM:='N';
PARAM_W.WZ:='N';
PARAM_W.POZY:='W';
PARAM_W.DR_TERM:='N';
PARAM_W.DR_DUPL:='N';
PARAM_W.DUPLIKAT:=date(0,0,0);
_par:='IN';
PARAM_W.SLOWNIE:='T';
PARAM_W.JEZYK:=null;
PARAM_W.DR_RAB:='T';

WYDRUKIL.index('WYDRUKI');
WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
{? ~WYDRUKIL.first()
||
   WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.add()
||
   PARAM_W.KOPIA:=WYDRUKIL.TYP_ZEST;
   PARAM_W.POZY:=WYDRUKIL.STR1;
   PARAM_W.DR_TERM:=WYDRUKIL.STR2;
:   PARAM_W.DR_DUPL:=WYDRUKIL.STR3;
   PARAM_W.ZAM:={? WYDRUKIL.CHKBOX04 || 'T' || 'N' ?};
   PARAM_W.WZ:={? WYDRUKIL.CHKBOX05 || 'T' || 'N' ?};
   PARAM_W.DUPLIKAT:=WYDRUKIL.OD_DATY;
   _par:=WYDRUKIL.FAKS_OD;
   PARAM_W.SLOWNIE:={? WYDRUKIL.CHKBOX01 || 'T' || 'N' ?};
   PARAM_W.JEZYK:=WYDRUKIL.JEZYK;
   PARAM_W.PODNALDR:={? WYDRUKIL.CHKBOX02 || 'T' || 'N' ?};
   PARAM_W.DR_RAB:={? WYDRUKIL.CHKBOX03 | FAKS.SPOBLRAB='W' || 'T' || 'N' ?}
?};
_jezyk_:=PARAM_W.JEZYK;
_djezyk:=exec('kh_jezyk','tlumaczenia',FAKS.KH);
{? _b
|| PARAM_W.JEZYK:={? _djezyk || _djezyk || _jezyk_ ?};
   PARAM_W.JEZYK_KH:={? _djezyk & _djezyk=PARAM_W.JEZYK || 'T' || 'N' ?}
|? exec('czy_grp_dr','faktury_wydruk')
|| PARAM_W.JEZYK:={? __GRP_DR.JEZYK_KH='T' & _djezyk || _djezyk || __GRP_DR.JEZYK ?}
?};

PARAM_W.INDEKS:='N';
PARAM_W.NAZWA:='N';
PARAM_W.MC:='N';
PARAM_W.UWAGI:='N';
PARAM_W.KODOBCY:='N';
PARAM_W.DK_C:='N';
{? _par*'I' || PARAM_W.INDEKS:='T' ?};
{? _par*'N' || PARAM_W.NAZWA:='T' ?};
{? _par*'M' || PARAM_W.MC:='T' ?};
{? _par*'U' || PARAM_W.UWAGI:='T' ?};
{? _par*'K' || PARAM_W.KODOBCY:='T' ?};
{? _par*'C' || PARAM_W.DK_C:='T' ?};

{? _b
|| PARAM_W.WALH:={? __walh<>__walp || 'T' || 'N' ?}
|| PARAM_W.WALH:={? __walh<>__walp || __pwwalh || 'N' ?}
?};

PARAM_W.win_edit('WYDR_KOR');
{? wyjscie=0
||
   _chk_par:="
      _wyn:='';
      {? PARAM_W.KOPIA<0
      || FUN.info('Ilość kopii nie może być ujemna.'@);
         _wyn:='KOPIA'
      ?};
      {? _wyn='' & PARAM_W.DR_DUPL='T'
      || {? PARAM_W.DUPLIKAT=date(0,0,0)
         || FUN.info('Niewypełniona data wystawienia duplikatu.'@);
            _wyn:='DUPLIKAT'
         ?};
         {? _wyn=''
         || {? PARAM_W.DUPLIKAT<FAKS.DW
            || FUN.info('Data duplikatu wcześniejsza od daty wystawienia dokumentu.'@);
               _wyn:='DUPLIKAT'
            ?}
         ?}
      ?};
      {? _wyn=''
      || {? PARAM_W.WALH='H' & __walh=__walp
         || FUN.info('Waluta handlowa identyczna z walutą opodatkowania.\nWydruk w walucie handlowej niedostępny.'@);
            _wyn:='WALH'
         |? PARAM_W.WALH='T' & __walh=__walp
         || FUN.info('Waluta handlowa identyczna z walutą opodatkowania.\nWydruk w obu walutach niedostępny.'@);
            _wyn:='WALH'
         ?}
      ?};
      _wyn
   ";
   {? {? _b=1 & exec('noparam','param_wydr',FAKS.SZ)=0
      || exec('set_efld_opt_paramw','faktury_wydruk',FAKS.SZ='S' & FAKS.SPOBLRAB='C');
         {? PARAM_W.edit(_chk_par)
         || 1
         || {? exec('czy_grp_dr','faktury_wydruk') || __GRP_DR.ESC:=1 ?}; 0
         ?}
      || 1
      ?}
   ||
      {? _b
      || WYDRUKIL.index('WYDRUKI');
         WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
         {? WYDRUKIL.first()
         ||
            WYDRUKIL.TYP_ZEST:=PARAM_W.KOPIA;
            WYDRUKIL.STR1:=PARAM_W.POZY;
            WYDRUKIL.STR2:=PARAM_W.DR_TERM;
            WYDRUKIL.STR3:=PARAM_W.DR_DUPL;
            WYDRUKIL.CHKBOX04:=PARAM_W.ZAM='T';
            WYDRUKIL.CHKBOX05:=PARAM_W.WZ='T';
            WYDRUKIL.OD_DATY:=PARAM_W.DUPLIKAT;
            WYDRUKIL.FAKS_OD:='';
            {? PARAM_W.INDEKS='T'  || WYDRUKIL.FAKS_OD+='I' ?};
            {? PARAM_W.NAZWA='T'   || WYDRUKIL.FAKS_OD+='N' ?};
            {? PARAM_W.MC='T'      || WYDRUKIL.FAKS_OD+='M' ?};
            {? PARAM_W.UWAGI='T'   || WYDRUKIL.FAKS_OD+='U' ?};
            {? PARAM_W.KODOBCY='T' || WYDRUKIL.FAKS_OD+='K' ?};
            {? PARAM_W.DK_C='T'    || WYDRUKIL.FAKS_OD+='C' ?};
            WYDRUKIL.CHKBOX01:=PARAM_W.SLOWNIE='T';
            WYDRUKIL.JEZYK:={? _djezyk & PARAM_W.JEZYK=_djezyk || _jezyk_ || PARAM_W.JEZYK ?};
            {? exec('czy_grp_dr','faktury_wydruk')
            || __GRP_DR.JEZYK:=PARAM_W.JEZYK;
               __GRP_DR.JEZYK_KH:=PARAM_W.JEZYK_KH
            ?};
            {? PARAM_W.JEZYK_KH='T' & _djezyk || PARAM_W.JEZYK:=_djezyk ?};
            WYDRUKIL.CHKBOX03:=PARAM_W.DR_RAB='T';
            WYDRUKIL.put();
            __pwwalh:=PARAM_W.WALH
         ?}
      ?};
      {? ~loop.kz
      ||
         FAP.cntx_psh();
         FAP.index('FAP');
         FAP.prefix(FAKS.ref());
         {? FAP.first() || {! |? {? exec('spr_kor','faktury_poz') || p_druk+='/'+$FAP.POZ+'/' ?}; FAP.next() !} ?};
         FAP.cntx_pop();
         {? -PARAM_W.POZY='k' & p_druk=''
         || FUN.info('Brak korygowanych pozycji.'@);
            wyjscie:=1
         ?}
      ?}
   ||
      wyjscie:=1
   ?}
?};

{? wyjscie=0
||
:: szerokosc kolumn
   k:=obj_new(16);
:: szerokosc kolumn dla podsumowania VAT dla dokumentow gdzie VAT liczony od sumy w stawkach
   VAR_DEL.delete('k1');   k1:=obj_new(3);
:: dane drukowane
   w:=obj_new(obj_len(k));
:: nazwy kolumn
   n:=obj_new(obj_len(k));
:: sumy
   s:=obj_new(6);
   VAR_DEL.delete('sp');   sp:=obj_new(13);
:: do przeniesienia
   do_p:=obj_new(6);
:: dane naglowkowe
   na:=obj_new(10);
:: przeniesienie
   prz:=obj_new(6); {! _i..6 |! prz[_i]:=0 !};
:: szerokosci kolumn nagłówkowych
   szer:=obj_new(4);
:: szerokosci pozycji faktur
   fak:=obj_new(obj_len(k));
:: w tym VAT
   vat:=obj_new(3);

   PRN1:=obj_new(@.CLASS.PRINT,obj_len(k));
   {? __ods || {? graf || PRN1.s_frame() || PRN1.t_frame() ?} || {? graf || PRN1.sl_frame() || PRN1.tl_frame() ?} ?};
   PRN2:=obj_new(@.CLASS.PRINT,5);
   {? __ods || {? graf || PRN2.s_frame() || PRN2.t_frame() ?} || {? graf || PRN2.sl_frame() || PRN2.tl_frame() ?} ?};
   PRN3:=obj_new(@.CLASS.PRINT,5);
   {? __ods || {? graf || PRN3.s_frame() || PRN2.t_frame() ?} || {? graf || PRN3.sl_frame() || PRN2.tl_frame() ?} ?};
   PRN4:=obj_new(@.CLASS.PRINT,2);                 PRN4.n_frame();
   PRN5:=obj_new(@.CLASS.PRINT,4);                 PRN5.n_frame();

   ndvat:=FAKS.NDVAT;
   __oo:=0;
   vatzpoz:=FAKS.T().VATZPOZ='T';

:: opisy w obiektach PRN4 i PRN5
   op_1:=op_2:=op_3:=op_4:='';
:: Poz
   n[1]:=exec('tr','tlumaczenia','1000180');           k[1]:=4+{? mpp || (FAKS.SP_WYM='T') ?};
:: Material / usluga
   n[2]:=exec('tr','tlumaczenia','1000181');           k[2]:={? ~('TO'*ndvat) || 40 || 60 ?};
:: CN / PKWiU
   n[3]:=
      {? exec('get','#params',300208,2)='T'
      ||
         exec('tr','tlumaczenia','1000218')+'/'
      ||
         ''
      ?}+
      exec('tr','tlumaczenia','1000182');              k[3]:=10;
:: Ilosc
   n[4]:=exec('tr','tlumaczenia','1000183');           k[4]:=6;
:: jm
   n[5]:=exec('tr','tlumaczenia','1000184');           k[5]:=4;
   {? FAKS.T().FIS='T'
:: Cena brutto przed rab.
   || n[6]:=
      {? exec('czy_rab','faktury_wspolne','FAKS')
      || {? WYDRUKIL.WYDRUK='kor_rr'
         || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
            +' '+exec('tr','tlumaczenia','1000240')
         || exec('tr','tlumaczenia','1000185')
         ?}
      || {? WYDRUKIL.WYDRUK='kor_rr'
         || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
         || exec('tr','tlumaczenia','1000193')
         ?}
      ?};                                            k[6]:=11
:: Cena netto przed rab.
   || n[6]:=
      {? exec('czy_rab','faktury_wspolne','FAKS')
      || {? WYDRUKIL.WYDRUK='kor_rr'
         || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
            +' '+exec('tr','tlumaczenia','1000240')
         || exec('tr','tlumaczenia','1000186')
         ?}
      || {? WYDRUKIL.WYDRUK='kor_rr'
         || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
         || exec('tr','tlumaczenia','1000188')
         ?}
      ?};                                             k[6]:=10
   ?};
:: % rab.
   {? exec('czyrabp','ceny',FAKS.RAB_TYP)
   || n[7]:=exec('tr','tlumaczenia','1000187');          k[7]:=5
   || n[7]:=exec('tr','tlumaczenia','1000199');          k[7]:=6
   ?};
   {? odbrutto=1 & FAKS.NDVAT='N'
:: Cena brutto
   || n[8]:={? WYDRUKIL.WYDRUK='kor_rr'
            || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000193')
            ?};                                          k[8]:=10
   ||
:: Cena netto
      n[8]:={? WYDRUKIL.WYDRUK='kor_rr'
            || exec('tr','tlumaczenia','1000239')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000188')
            ?};                                          k[8]:=10
   ?};
:: Wartosc netto
   n[9]:={? WYDRUKIL.WYDRUK='kor_rr'
         || exec('tr','tlumaczenia','1000241')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
         || exec('tr','tlumaczenia','1000189')
         ?};                                             k[9]:=10;  k1[1]:=10;
:: Stawka VAT
   n[10]:=  {? WYDRUKIL.WYDRUK='kor_rr'
            || exec('tr','tlumaczenia','1000242')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000190')
            ?};                                           k[10]:=6;
:: Kwota VAT
   n[11]:=  {? WYDRUKIL.WYDRUK='kor_rr'
            || exec('tr','tlumaczenia','1000243')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000191')
            ?};                                         k[11]:=10;  k1[2]:=10;
:: Wartosc brutto
   n[12]:=  {? WYDRUKIL.WYDRUK='kor_rr'
            || exec('tr','tlumaczenia','1000241')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
            || exec('tr','tlumaczenia','1000192')
            ?};                                           k[12]:=10;  k1[3]:=10;
:: Waluta
   {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
   || n[13]:=exec('tr','tlumaczenia','1000152');        k[13]:=6
   ?};
   {? odbrutto=1 & FAKS.NDVAT='N'
   ||
:: Wartosc brutto przed rabatem
      n[14]:={? WYDRUKIL.WYDRUK='kor_rr'
             || exec('tr','tlumaczenia','1000241')+' '+exec('tr','tlumaczenia','0000018')+' '+exec('tr','tlumaczenia','1000236')
                +' '+exec('tr','tlumaczenia','1000240')
             || exec('tr','tlumaczenia','1000198')
             ?};                                         k[14]:=14
   ||
:: Wartosc netto przed rabatem
      n[14]:={? WYDRUKIL.WYDRUK='kor_rr'
             || exec('tr','tlumaczenia','1000241')+' '+exec('tr','tlumaczenia','1000237')+' '+exec('tr','tlumaczenia','1000236')
                +' '+exec('tr','tlumaczenia','1000240')
             || exec('tr','tlumaczenia','1000197')
             ?};                                         k[14]:=13
   ?};
:: Kwota rabatu
   n[15]:=exec('tr','tlumaczenia','1000199');            k[15]:=8;
:: Data sprzzedaży pozycji
   n[16]:=exec('tr','tlumaczenia','1000112');            k[16]:=10;

:: wyliczenie szerokosci kolumn wg etykiet
   {! _i:=1..obj_len(n)
   |!
      {? var_pres('['+$_i+']',n)=type_of('stringi')
      ||
         _max:=0;
         STR.split(n[_i]);
         {! |? _word:=STR.get_word(); {? +_word>_max || _max:=+_word ?}; STR.next() !};
         k[_i]:=_max
      ?}
   !};
   k1[1]:=k[9];
   k1[2]:=k[11];
   k1[3]:=k[12];

   {! _i:=1..obj_len(k) |! w[_i]:=0 !};
   {! _i:=1..obj_len(s) |! s[_i]:=0 !};
   zero_na:="{! _i:=1..obj_len(na)|! na[_i]:='' !}";
   w[2]:=w[6]:='';

:: ilosc znakow w wierszu
   max_T8B:=max_T10B:=max_T8G:=max_T8:=0;
   licznik:=ilkop:=stron:=0;
   Text:=Text1:='';
:: ilosc pozycji
   poz:=0;

:: spr. ile miejsc po przecinku drukowac

   ddokl:=0;
   ddokl_c:=2;
   rdokl_c:=2;
   _dokl_c:="
      _dokl:=+form((frac(FKOR.CPR)))-2;
      {? ddokl_c<_dokl || ddokl_c:=_dokl ?};
      _dokl:=+form((frac(FKOR.BCPR)))-2;
      {? ddokl_c<_dokl || ddokl_c:=_dokl ?};
      _cdokl:=
         {? FAKS.SZ='S'
         || {? FKOR.M<>null() || FKOR.M().DOKL_S || ST.DOKL_S ?}
         || {? FKOR.M<>null() || FKOR.M().DOKL_Z || ST.DOKL_Z ?}
         ?};
      {? _cdokl>ddokl_c || ddokl_c:=_cdokl ?}
   ";
   _dokl_cw:="
      _dokl:=+form((frac(FKOR.CWAL)))-2;
      {? ddokl_c<_dokl || ddokl_c:=_dokl ?};
      _dokl:=+form((frac(FKOR.BCWAL)))-2;
      {? ddokl_c<_dokl || ddokl_c:=_dokl ?};
      _cdokl:=
         {? FAKS.SZ='S'
         || {? FKOR.M<>null() || FKOR.M().DOKL_S || ST.DOKL_S ?}
         || {? FKOR.M<>null() || FKOR.M().DOKL_Z || ST.DOKL_Z ?}
         ?};
      {? _cdokl>ddokl_c || ddokl_c:=_cdokl ?}
   ";
   FAP.index('FAP');
   FAP.prefix(FAKS.ref);
   {? FAP.first
   ||
      {!
      |?
::       dokładność ilości
         exec('wysw_kor','faktury_poz');
         _dokl:=+form((frac(FKOR.IL)))-2;
         {? ddokl<_dokl || ddokl:=_dokl ?};
         _dokl:=+form((frac(FKOR.BIL)))-2;
         {? ddokl<_dokl || ddokl:=_dokl ?};
::       dokładność ceny
         {? ~walutowa
         || _dokl_c()
         || {? PARAM_W.WALH='T'
            || _dokl_c(); _dokl_cw()
            |? PARAM_W.WALH='H'
            || _dokl_cw()
            || _dokl_c()
            ?}
         ?};

         FAP.next
      !}
   ?};
   {? exec('czy_rab','faktury_wspolne','FAKS') || rdokl_c:=ddokl_c ?};

   print1:="
      VAR_DEL.delete('PRN1');
      PRN1:=obj_new(@.CLASS.PRINT,obj_len(k));
      {? __ods || {? graf || PRN1.s_frame() || PRN1.t_frame() ?} || {? graf || PRN1.sl_frame() || PRN1.tl_frame() ?} ?};
      {? k[1] || PRN1.add($('form(w[1])'),         k[1], n[1],1) ?};
      {? k[2] || PRN1.add($('form(w[2])'),         k[2], n[2]) ?};
      {? k[3] || PRN1.add($('form(w[3])'),         k[3], n[3]) ?};
      {? k[4] || PRN1.add($('form(w[4],,ddokl)'),  k[4], n[4],1) ?};
      {? k[5] || PRN1.add($('form(w[5],,2)'),      k[5], n[5]) ?};
      {? k[6] || PRN1.add($('form(w[6],,ddokl_c)'),k[6], n[6],1) ?};
      {? k[7] || PRN1.add($('form(w[7],,rdokl_c)'),k[7], n[7],1) ?};
      {? k[8] || PRN1.add($('form(w[8],,ddokl_c)'),k[8], n[8],1) ?};
      {? k[16] || PRN1.add($('form(w[16])'),       k[16], n[16]) ?};
      {? k[14] || PRN1.add($('form(w[14],,2)'),    k[14], n[14],1) ?};
      {? k[15] || PRN1.add($('form(w[15],,2)'),    k[15], n[15],1) ?};
      {? k[9] | k[12]
      || {? vatzpoz=1 | odbrutto=0
         ||      PRN1.add($('form(w[9],,2)'),      k[9], n[9],1)
         |? odbrutto=1
         ||      PRN1.add($('form(w[12],,2)'),     k[12],n[12],1)
         ?}
      ?};
      {? k[10] & ~('TO'*ndvat)
      ||         PRN1.add($('form(w[10])'),        k[10],n[10],1)
      ?};
      {? k[11] & ~('TO'*ndvat) & vatzpoz=1
      ||         PRN1.add($('form(w[11],,2)'),     k[11],n[11],1)
      ?};
      {? k[12] & ~('TO'*ndvat) & vatzpoz=1
      ||         PRN1.add($('form(w[12],,2)'),     k[12],n[12],1)
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      ||         PRN1.add($('form(w[13],,2)'),     k[13],n[13])
      ?}
   ";
   print2:="
      {? var_pres('_a')<>type_of(0) || _a:=0 ?};
      {? var_pres('_b')<>type_of(0) || _b:=0 ?};
      VAR_DEL.delete('PRN2');
      PRN2:=obj_new(@.CLASS.PRINT,6);
      {? __ods || {? graf || PRN2.s_frame() || PRN2.t_frame() ?} || {? graf || PRN2.sl_frame() || PRN2.tl_frame() ?} ?};
      {? k[1]
      || PRN2.add($('form(w[1])'),
            _ret:=0; {!_ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13)|| k[_ii]+__odsram || 0 ?})!};_ret-__odsram,n[1])
      ?};
      {? ~('TO'*ndvat)
      ||
         {? _b=0
         ||
            {? vatzpoz
            ||
                  PRN2.add($('form(w[9],,2)')   ,k1[1],n[9] ,1);
                  PRN2.add($('form(w[10])')     ,k[10],n[10],1);
                  PRN2.add($('form(w[11],,2)')  ,k1[2],n[11],1);
                  PRN2.add($('form(w[12],,2)')  ,k1[3],n[12],1)
            ||
               {? odbrutto
               ||
                  PRN2.add($('form(w[12],,2)'), k[12],n[12], 1  );
                  PRN2.add($('form(w[10])'),    k[10],n[10], 1  )
               ||
                  PRN2.add($('form(w[9],,2)'),  k[9] ,n[9] , 1  );
                  PRN2.add($('form(w[10])'),    k[10],n[10], 1  )
               ?}
            ?}
         |? _b=1
         ||
            PRN2.add($('form(w[9],,2)'),  k1[1],n[9],1);
            PRN2.add($('form(w[10])'),    k[10],n[10], 1  );
            PRN2.add($('form(w[11],,2)'),    k1[2],n[11], 1  );
            PRN2.add($('form(w[12],,2)'), k1[3],n[12], 1  )
         ?}
      ||
         PRN2.add($('form(w[9],,2)'),  k[9] ,n[9] , 1  )
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & _a=0 || PRN2.add($('form(w[13],,2)'),  k[13],n[13]) ?}
   ";
   print3:="
      VAR_DEL.delete('PRN3');
      PRN3:=obj_new(@.CLASS.PRINT,6);
      {? __ods || {? graf || PRN3.s_frame() || PRN3.t_frame() ?} || {? graf || PRN3.sl_frame() || PRN3.tl_frame() ?} ?};
      {? k[1]
      || PRN3.add($('form(prz[1])'),
            _ret:=0; {! _ii:=1..16 |! _ret+=({? k[_ii]<>0 & (_ii<9 | _ii>13) || k[_ii]+__odsram || 0 ?}) !};_ret-__odsram,n[1])
      ?};
      {? ~('TO'*ndvat) & vatzpoz=0 & odbrutto=1
      ||
         PRN3.add($('form(prz[5],,2)'), k[12],n[12] , 1  );
         PRN3.add($('form(prz[3])'),    k[10],n[10] , 1  )
      |? ~('TO'*ndvat) & vatzpoz=0 & odbrutto=0
      ||
         PRN3.add($('form(prz[2],,2)'),  k[9] ,n[9] , 1  );
         PRN3.add($('form(prz[3])'),     k[10],n[10], 1  )
      |? ~('TO'*ndvat)
      ||
         PRN3.add($('form(prz[2],,2)'),  k1[1],n[9] , 1  );
         PRN3.add($('form(prz[3])'),     k[10],n[10], 1  );
         PRN3.add($('form(prz[4],,2)'),     k1[2],n[11], 1  );
         PRN3.add($('form(prz[5],,2)'),  k1[3],n[12], 1  )
      ||
         PRN3.add($('form(prz[2],,2)'),  k[9] ,n[9] , 1  )
      ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || PRN3.add($('form(prz[6],,2)'),  k[13],n[13]) ?}
   ";
   print4:="
      prn1:='PRN4';
      op_1:=op_2:='';
      VAR_DEL.delete('PRN4');
      PRN4:=obj_new(@.CLASS.PRINT,2); PRN4.n_frame();
      {? _a || PRN4.add($('form(op_1)'),_a,'') ?};
      {? _b || PRN4.add($('form(op_2)'),_b,'',1) ?}
   ";

:: zamowienia
   zam:='';
   {? -PARAM_W.ZAM='t'
   ||
::      FAKS.cntx_psh();
::      FAKS.index('FAK_SYM1'); FAKS.prefix(FAKS.KOR);
::      _ref_dok_spr:={? FAKS.first() || FAKS.ref() || null ?};
::      FAKS.cntx_pop();
      _ref_dok_spr:=$(FAKS.FKSQL);

      {? var_pres('X_Xp')>0 || obj_del(X_Xp) ?};
      {? FAKS.WHERE='L'
      ||
         _qq:=$"
            select
               FAPOW.ZK_N_SYM SYM
            from
               FAPOW
            where
               FAPOW.FAKS=':_a'
               and FAPOW.ZK_N<>''
            group by
               FAPOW.ZK_N_SYM
            order by
               1
         ";
::         X_Xp:=sql(_qq,$FAKS.ref())
         X_Xp:=sql(_qq,$_ref_dok_spr)
      |? FAKS.SZ='S'
      ||
         X_Xp:=sql('select ZK_N.SYM SYM  from @ZK_RN join @ZK_N using (ZK_RN.N, ZK_N.REFERENCE)
                      where ZK_RN.FAKS=\':_a\' group by ZK_N.SYM order by 1',$_ref_dok_spr)
::                     where ZK_RN.FAKS=\':_a\' group by ZK_N.SYM order by 1',$FAKS.ref)
      |? FAKS.SZ='Z'
      ||
         X_Xp:=sql('select ZD_NAG.SYM SYM  from @ZD_RN join @ZD_NAG using (ZD_RN.ZD_NAG, ZD_NAG.REFERENCE)
                     where ZD_RN.FAKS=\':_a\' group by ZD_NAG.SYM order by 1',$_ref_dok_spr)
::                     where ZD_RN.FAKS=\':_a\' group by ZD_NAG.SYM order by 1',$FAKS.ref)
      ?};
      {? X_Xp.first
      || zam:=exec('tr','tlumaczenia','1000148');
         _sep:=' '; {! |? zam+=_sep+X_Xp.SYM; _sep:=', ';X_Xp.next !}
      ?}
   ?};

:: dokumenty magazynowe
   mag:='';
   {? -PARAM_W.WZ='t'
   ||
      exec('dok_maga','powdok',0); __tt.prefix();
      {? __tt.first || _sep:=' '; {! |? mag+=_sep+'%1 z dnia %2'@[__tt.SYM,$__tt.DATA];_sep:=', ';__tt.next !} ?};
      {? mag<>'' || mag:=exec('tr','tlumaczenia','1000149')+mag ?}
   ?};
:: pozycje przed korekta
   pozycjaa:="
      {? var_pres('_a')<>type_of(null()) || _a:=FAP.ref() ?};

      FAKS.cntx_psh();
      FAP.cntx_psh();

      {? ref_name(FAP.FAKS)<>FAKS.name() || FAKS.use(ref_name(FAP.FAKS)) ?};
      FAP.FAKS().SYM;
      exec('wysw_kor','faktury_poz');

      {? ($_a)<>($FAP.ref())
      ||
         FAP.use('fakpo'+(5-form(8+FAKS.LKSQL)));
         FAKS.use(form(8+FAKS.LKSQL));
         FAP.prefix();
         FAP.seek(_a);
         FAP.FAKS().SYM
      ?};

      xktm.clear; xktm.find_key(FAP.POZ);
      _spp:=walutowa=0 & __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & FAKS.T().SPP='T';

      w[1]:='';
      {? mpp
      ||
         {? walutowa=0 | PARAM_W.WALH='H'
         ||
            _mpp:='';
            {? FAKS.SP_WYM='T' & FAKS.SZ='S'
            ||
               _mpp:=' ';
               {? FAP.SP_WYM='T' || _mpp:='*' ?}
            ?};
            w[1]:=form(FAP.POZ,,0,'99')+_mpp
         ?}
      ||
         w[1]:={? walutowa=0 | PARAM_W.WALH='H' || form(FAP.POZ,,0,'99') || '' ?}
      ?};
      w[2]:='';

      {? PARAM_W.JEZYK<>null
      ||_tlumacz:=exec('trans_m','tlumaczenia',exec('FindInSet','#table','M','MATKTM',xktm.KTM,xktm.KTM),PARAM_W.JEZYK)
      ?};

      {? PARAM_W.INDEKS='T'
      ||
         {? +xktm.KTX
         || w[2]+=xktm.KTX+' '
         || w[2]+={? xktm.KTM<>'' || xktm.KTM+' ' || xktm.UWA+' ' ?}
         ?}
      ?};
      {? PARAM_W.NAZWA='T' & (PARAM_W.JEZYK=null | _tlumacz='')
      ||
         {? +xktm.NAX
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+xktm.NAX+' '
         |? +xktm.NAZ
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+{? xktm.NAZ<>'' || xktm.NAZ+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.JEZYK<>null & _tlumacz<>''
      ||
         {? +xktm.KTX
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_tlumacz+' '
         || w[2]+={? w[2]<>''||' - ' || '' ?}+{? FAP.M<>null || _tlumacz+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.UWAGI='T' & FAP.memo_lin('UWAGI')<>'\n' & (FAP.M<>null | +FAP.NX)
      || FAP.memo_get(,'UWAGI');
         w[2]+={? w[2]<>'' || ' - ' || '' ?}+gsub(FAP.memo_txt(0,1,'UWAGI'),'\n',' ')
      ?};
      {? PARAM_W.KODOBCY='T' & xktm.KTM<>''  & FAKS.KH<>null
      ||  _kod_obcy:=exec('kod_obcy','kody_kresk',exec('FindInSet','#table','M','MATKTM',xktm.KTM,xktm.KTM),FAKS.KH);
         {? _kod_obcy<>''
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+_kod_obcy
         ?}
      ?};
      {? PARAM_W.DK_C='T' & xktm.DKC<>''
      || _dk_c:=xktm.DKC;
         {? _dk_c<>''
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_dk_c
         ?}
      ?};
      w[2]:={? walutowa=0 | PARAM_W.WALH='H' || w[2] || '' ?};

      w[3]:={? walutowa=0 | PARAM_W.WALH='H' || FAP.M().PKWIU().KOD; xktm.PKW || '' ?};
      w[4]:={? walutowa=0 | PARAM_W.WALH='H' || FKOR.IL || '' ?};
      {? PARAM_W.JEZYK<>null
      || _tlumacz:=exec('trans_jm','tlumaczenia',FAP.JM,PARAM_W.JEZYK)
      ?};
      w[5]:={? walutowa=0 | PARAM_W.WALH='H' || {? PARAM_W.JEZYK<>null & _tlumacz<>'' || _tlumacz || FAP.JM().KOD ?} || '' ?};
      w[6]:={? _spp || '' || {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.CPR || FKOR.CWAL ?} ?};
      _cdokl:={? FAKS.SZ='S'
              || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
              || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
              ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      || w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP)
               || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)
               || FAP.RABK+(FAKS.RAB*FKOR.CWAL/100)$_cdokl
               ?}
      || w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP)
               || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)
               || FKOR.CPR-{? odbrutto=1 || FKOR.CB || FKOR.CN ?}
               ?}
      ?};
      _doklFix:=FAKS.name()+2<>'hs' | exec('get','#params',300215,2)='T';
      _dokl_c:=
         {? _doklFix
         || {? FAKS.SZ='S' || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
            |? FAKS.SZ='Z' || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
            || 2
            ?}
         || _dokl_c:=+(2-$fabs(frac({? __walh<>__walp || FKOR.CWAL || FKOR.CPR ?})));
            {? _dokl_c<2 || 2 || _dokl_c ?}
         ?};
      w[8]:=
         {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
         || {? odbrutto=1 || FKOR.CB || FKOR.CN ?}
         || {? exec('czyrabp','ceny',FAKS.RAB_TYP)
            || _rabat:=1-w[7]/100;
               FKOR.CWAL*_rabat $_dokl_c
            || FKOR.CWAL-FAP.RABK-(FKOR.CWAL*FAKS.RAB/100)$_dokl_c
            ?}
         ?};
      {? walutowa=0 || $!s[1] || $!s[4] ?}()+=(w[9]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.WWAL_P || FKOR.WWAL ?});
      w[10]:=FAP.SV().KOD;
      {? walutowa=0 || $!s[2] || $!s[5] ?}()+=(w[11]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.VWAL_P || FKOR.VWAL ?});
      {? walutowa=0 || $!s[3] || $!s[6] ?}()+=(w[12]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BWAL_P || FKOR.BWAL ?});
      w[13]:={? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || __walh || __walp ?};
      w[14]:={? _spp || '' || (FKOR.IL*w[6])$2 ?};
      w[15]:=w[14]-{? odbrutto=1 || w[12] || w[9] ?};
      w[16]:={? FAP.D<>date(0,0,0) & (walutowa=0 | PARAM_W.WALH='N' | PARAM_W.WALH='H')
             || gsub(form(FAP.D,,3),'/','-')
             || ''
             ?};

::    szacowanie ilosci lini pozycji
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+1;

      FAP.cntx_pop();
      FAKS.cntx_pop()
   ";
:: pozycje po korekcie
   pozycjab:="
      exec('wysw_kor','faktury_poz');
      _spp:=walutowa=0 & __walh<>__walp & (PARAM_W.WALH='T' | PARAM_W.WALH='H') & FAKS.T().SPP='T';

      w[1]:='';
      {? mpp
      ||
         {? walutowa=0 | PARAM_W.WALH='H'
         ||
            _mpp:='';
            {? FAKS.SP_WYM='T' & FAKS.SZ='S'
            ||
               _mpp:=' ';
               {? FAP.SP_WYM='T' || _mpp:='*' ?}
            ?};
            w[1]:=form(FAP.POZ,,0,'99')+_mpp
         ?}
      ||
         w[1]:={? walutowa=0 | PARAM_W.WALH='H' || form(FAP.POZ,,0,'99') || '' ?}
      ?};
      w[2]:='';

      {? PARAM_W.JEZYK<>null
      ||_tlumacz:=exec('trans_m','tlumaczenia',FAP.M,PARAM_W.JEZYK)
      ?};

      {? PARAM_W.INDEKS='T'
      ||
         {? +FAP.MX
         || w[2]+=FAP.MX+' '
         || w[2]+={? FAP.M<>null || FAP.M().KTM+' ' || xktm.UWA+' ' ?}
         ?}
      ?};
      {? PARAM_W.NAZWA='T' & (PARAM_W.JEZYK=null | _tlumacz='')
      ||
         {? +FAP.NX
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+FAP.NX+' '
         |? FAP.M
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+{? FAP.M<>null || FAP.M().N+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.JEZYK<>null & _tlumacz<>''
      ||
         {? +FAP.MX
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_tlumacz+' '
         || w[2]+={? w[2]<>''||' - ' || '' ?}+{? FAP.M<>null || _tlumacz+' ' || '' ?}
         ?}
      ?};
      {? PARAM_W.UWAGI='T' & FAP.memo_lin('UWAGI')<>'\n' & (FAP.M<>null | +FAP.NX)
      || FAP.memo_get(,'UWAGI');
         w[2]+={? w[2]<>'' || ' - ' || '' ?}+gsub(FAP.memo_txt(0,1,'UWAGI'),'\n',' ')
      ?};
      {? PARAM_W.KODOBCY='T' & FAP.M<>null & FAKS.KH<>null
      ||  _kod_obcy:=exec('kod_obcy','kody_kresk',FAP.M,FAKS.KH);
         {? _kod_obcy<>''
         || w[2]+={? w[2]<>'' || ' - ' || '' ?}+_kod_obcy
         ?}
      ?};
      {? PARAM_W.DK_C='T' & FAP.DK_C
      || _dk_c:=form(exec('sym_dk_c','mat_atr',FAP.DK_C));
         {? _dk_c<>''
         || w[2]+={? w[2]<>''||' - ' || '' ?}+_dk_c
         ?}
      ?};
      w[2]:={? walutowa=0 | PARAM_W.WALH='H' || w[2] || '' ?};

      w[3]:={? walutowa=0 | PARAM_W.WALH='H' || exec('pkwiu','material',FAKS.DW,FAP.M,1,FAP.PKWIU_CN) || '' ?};
      w[4]:={? walutowa=0 | PARAM_W.WALH='H' || FKOR.BIL || '' ?};
      {? PARAM_W.JEZYK<>null
      || _tlumacz:=exec('trans_jm','tlumaczenia',FAP.JM,PARAM_W.JEZYK)
      ?};
      w[5]:={? walutowa=0 | PARAM_W.WALH='H' || {? PARAM_W.JEZYK<>null & _tlumacz<>'' || _tlumacz || FAP.JM().KOD ?} || '' ?};
      w[6]:={? _spp || '' || {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BCPR || FKOR.BCWAL ?} ?};
      {? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H')
      || w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP) || FAP.RABK+(FAKS.RAB*FKOR.BCWAL/100)$2 ?}
      || w[7]:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP) || FKOR.BCPR-{? odbrutto=1 || FKOR.BCB || FKOR.BCN ?} ?}
      ?};
      w[8]:=
         {? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H')
         || {? odbrutto=1 || FKOR.BCB || FKOR.BCN ?}
         || {? exec('czyrabp','ceny',FAKS.RAB_TYP)
            || _rabat:=1-w[7]/100;
               FKOR.BCWAL*_rabat ${? FAKS.SZ='S'
                                  || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
                                  || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
                                  ?}
            || FKOR.BCWAL-FAP.RABK-(FKOR.BCWAL*FAKS.RAB/100)${? FAKS.SZ='S'
                                                             || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
                                                             || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
                                                             ?}
            ?}
         ?};
      {? walutowa=0 || $!s[1] || $!s[4] ?}()+=w[9]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BWWAL_P || FKOR.BWWAL ?};
      w[10]:=FAP.SV().KOD;
      {? walutowa=0 || $!s[2] || $!s[5] ?}()+=w[11]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BVWAL_P || FKOR.BVWAL?};
      {? walutowa=0 || $!s[3] || $!s[6] ?}()+=w[12]:={? walutowa=0 | (PARAM_W.WALH<>'T' & PARAM_W.WALH<>'H') || FKOR.BBWAL_P || FKOR.BBWAL ?};
      w[13]:={? walutowa=1 & (PARAM_W.WALH='T' | PARAM_W.WALH='H') || __walh || __walp ?};
      w[14]:={? _spp || '' || (FKOR.BIL*w[6])$2 ?};
      w[15]:=w[14]-{? odbrutto=1 || w[12] || w[9] ?};
      w[16]:={? FAP.D<>date(0,0,0) & (walutowa=0 | PARAM_W.WALH='N' | PARAM_W.WALH='H')
             || gsub(form(FAP.D,,3),'/','-')
             || ''
             ?};

::    szacowanie ilosci lini pozycji
      _f:=prn1+'.ini_line()'; _v:=($(_f))();
      _f:=prn1+'.length()'; il_lin:=($(_f))()+1
   ";

:: agregowanie wartosci do przeniesienia
   do_przep:="
      {? przed_po='przed'
      ||
         do_p[1]+=FKOR.WWAL_P;
         do_p[2]+=FKOR.VWAL_P;
         do_p[3]+=FKOR.BWAL_P
      ||
         do_p[1]+=FKOR.BWWAL_P;
         do_p[2]+=FKOR.BVWAL_P;
         do_p[3]+=FKOR.BBWAL_P
      ?}";
   do_przeh:="
      {? przed_po='przed'
      ||
         do_p[4]+=FKOR.WWAL;
         do_p[5]+=FKOR.VWAL;
         do_p[6]+=FKOR.BWAL
      ||
         do_p[4]+=FKOR.BWWAL;
         do_p[5]+=FKOR.BVWAL;
         do_p[6]+=FKOR.BBWAL
      ?}";

   sk:=obj_new(obj_len(s));
   vatk:=obj_new(3);
   kw_zal:=0;
   zal_wal:='';
   slownie:='';
   VAR_DEL.delete('xktm');
   xktm:=tab_tmp(1,
      'POZ','INTEGER','',
      'KTM','STRING[50]','',
      'NAZ','STRING[200]','',
      'KTX','STRING[50]','',
      'NAX','STRING[200]','',
      'UWA','STRING[255]','',
      'JEM','STRING[20]','',
      'PKW','STRING[50]','',
      'DKC','STRING[255]','');
   kw_fak:=0;
   faks:=BB.refsql(FAKS.ref)
?}


\end_k1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: klauzula END w kor_new.rpm
::  OLD: \end_k1/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__wydrfa');

VAR_DEL.delete('color','prn1','prn2');
VAR_DEL.delete('lm','ll','vp','lmt','lmt_org','rsep');
VAR_DEL.delete('faks');
VAR_DEL.delete('wyjscie');

undefine();

VAR_DEL.delete('k','w','n','s','sp','do_p','na','prz');
VAR_DEL.delete('szer','fak','vat');
VAR_DEL.delete('PRN1','PRN2','PRN3','PRN4','PRN5');

VAR_DEL.delete('ddokl','ddokl_c','rdokl_c');
VAR_DEL.delete('ndvat','__oo','walutowa','kw_fak','odbrutto','vatzpoz');
VAR_DEL.delete('graf','skladanka');
VAR_DEL.delete('op_1','op_2','op_3','op_4');
VAR_DEL.delete('zero_na','licznik','ilkop','stron','pages');
VAR_DEL.delete('max_T8','max_T8G','max_T10B');
VAR_DEL.delete('Text','Text1','poz');
VAR_DEL.delete('print1','print2','print3','print4');
VAR_DEL.delete('sk','vatk','p_druk','kw_zal','zal_wal','slownie','xktm','loop');
VAR_DEL.delete('il_lin','strona','duplex','__odsram','__odsskr');
VAR_DEL.delete('M_GD','S_GD');
VAR_DEL.delete('headfoot')


\begin_kz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: BEGIN wydruku korekty zbiorczej
::       [_a] - czy pokazywac panel dialogowy z parametrami ([1] -tak, 0 - nie) - gdy nie pokazuje panel nie zapisuje
::              rowniez ustawien wybranych
::  OLD: \begin_kz/wydr_fak.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};

done:="
   VAR_DEL.delete('__Tab');
   VAR_DEL.delete('ww','kk','nn','PRN');
   VAR_DEL.delete('ww1','kk1','nn1','PRN1','print1');
   VAR_DEL.delete('color','prn1');
   VAR_DEL.delete('lm','ll','vp','lmt','rsp');
   VAR_DEL.delete('graf','duplex','stron','pages','skladanka','wyjscie','ilkop');
   VAR_DEL.delete('__walh','__walp','__waln','walutowa');
   VAR_DEL.delete('__oo');
   VAR_DEL.delete('__wydrfa','__Ind')";
done();

:: do numeracji stron przy drukowaniu paczek faktur
pages:=0;

color:=prn1:='';
lm:=ll:=vp:=lmt:=rsep:=0;

graf:=exec('get','#params',2121,2,OPERATOR.USER)<>'T';
skladanka:=exec('get','#params',2122,2,OPERATOR.USER)='T';
duplex:=0;
stron:=0;
wyjscie:=0;
ilkop:=0;
__wydrfa:=obj_new('uproszcz','samof');
:: uproszczony wydruk faktury
__wydrfa.uproszcz:=0;
:: czy samofakturowanie
__wydrfa.samof:=0;

"--czy walutowa--";
__walh:=FAKS.WAL().KOD;
__walp:=FAKS.NWAL().KOD;
__waln:=INFO.NAROD().KOD;
walutowa:=__walh<>__walp;

:: odwrotne obciążenie
__oo:=0;

:: PRN
_ilk:=4;
ww:=obj_new(_ilk); {! _i.._ilk |! ww[_i]:=0 !};
kk:=obj_new(_ilk);
nn:=obj_new(_ilk);

PRN:=obj_new(@.CLASS.PRINT,4); PRN.s_frame();

:: PRN1
_ilk1:=2;
ww1:=obj_new(_ilk1); {! _i.._ilk1 |! ww1[_i]:=0 !};
kk1:=obj_new(_ilk1);
nn1:=obj_new(_ilk1);

print1:="
   VAR_DEL.delete('PRN1');
   PRN1:=obj_new(@.CLASS.PRINT,1); PRN1.s_frame();
   kk1[1]:=charleft()-4; nn1[1]:=exec('tr','tlumaczenia','1000212');
   PRN1.add($'ww1[1]',kk1[1],nn1[1])";

FAP.cntx_psh();
_rab1:=0;
FAP.index('KZ');
FAP.prefix($FAKS.ref());
_loop:=FAP.first();
{!
|? _loop
|!
   _rab1:=FAP.RAB1;
   _loop:=_rab1=0 & FAP.next()
!};
FAP.cntx_pop();
__Ind:=sql("
   select distinct
      (M.KTM || ' - ' || M.N) as N
   from
      FAP
      join FAKS using(FAP.FAKS,FAKS.reference)
      join M using(FAP.M,M.reference)
   where
      FAKS.KZ=:_a
   "
   +
   {? _rab1 || "and FAP.RAB1<>0" || "" ?}
   ,FAKS.ref());

_wydr_naz:='korzb';

PARAM_W.KOPIA:=2;
PARAM_W.DR_DUPL:='N';
PARAM_W.DUPLIKAT:=date(0,0,0);
PARAM_W.SLOWNIE:='T';
PARAM_W.JEZYK:=null;

WYDRUKIL.index('WYDRUKI');
WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydr_naz);
{? ~WYDRUKIL.first()
||
   WYDRUKIL.blank();
   WYDRUKIL.USERS:=OPERATOR.USER().KOD;
   WYDRUKIL.WYDRUK:=_wydr_naz;
   WYDRUKIL.add()
||
   PARAM_W.KOPIA:=WYDRUKIL.TYP_ZEST;
::   PARAM_W.DR_DUPL:=WYDRUKIL.STR3;
   PARAM_W.DUPLIKAT:={? PARAM_W.DR_DUPL='T' || date() || date(0,0,0) ?};
   PARAM_W.SLOWNIE:={? WYDRUKIL.CHKBOX01 || 'T' || 'N' ?};
   PARAM_W.JEZYK:=WYDRUKIL.JEZYK
?};
_jezyk_:=PARAM_W.JEZYK;
_djezyk:=exec('kh_jezyk','tlumaczenia',FAKS.KH);
{? _a
|| PARAM_W.JEZYK:={? _djezyk || _djezyk || _jezyk_ ?};
   PARAM_W.JEZYK_KH:={? _djezyk & _djezyk=PARAM_W.JEZYK || 'T' || 'N' ?}
|? exec('czy_grp_dr','faktury_wydruk')
|| PARAM_W.JEZYK:={? __GRP_DR.JEZYK_KH='T' & _djezyk || _djezyk || __GRP_DR.JEZYK ?}
?};

_chk_par:="
   _wyn:='';
   {? PARAM_W.KOPIA<0
   || FUN.info('Ilość kopii nie może być ujemna.'@);
      _wyn:='KOPIA'
   ?};
   {? _wyn='' & PARAM_W.DR_DUPL='T'
   || {? PARAM_W.DUPLIKAT=date(0,0,0)
      || FUN.info('Niewypełniona data wystawienia duplikatu.'@);
         _wyn:='DUPLIKAT'
      ?};
      {? _wyn=''
      || {? PARAM_W.DUPLIKAT<FAKS.DW
         || FUN.info('Data duplikatu wcześniejsza od daty wystawienia dokumentu.'@);
            _wyn:='DUPLIKAT'
         ?}
      ?}
   ?};
   {? _wyn=''
   || {? PARAM_W.WALH='H' & __walh=__walp
      || FUN.info('Waluta handlowa identyczna z walutą opodatkowania.\nWydruk w walucie handlowej niedostępny.'@);
         _wyn:='WALH'
      ?}
   ?};
   _wyn
";
PARAM_W.win_edit('WYDR_KZ');
{? _a
|| exec('set_efld_opt_paramw','faktury_wydruk',0);
   {? PARAM_W.edit(_chk_par)
   ||
      WYDRUKIL.TYP_ZEST:=PARAM_W.KOPIA;
      WYDRUKIL.STR3:=PARAM_W.DR_DUPL;
      WYDRUKIL.OD_DATY:=PARAM_W.DUPLIKAT;
      WYDRUKIL.CHKBOX01:=PARAM_W.SLOWNIE='T';
      WYDRUKIL.JEZYK:={? _djezyk & PARAM_W.JEZYK=_djezyk || _jezyk_ || PARAM_W.JEZYK ?};
      {? exec('czy_grp_dr','faktury_wydruk')
      || __GRP_DR.JEZYK:=PARAM_W.JEZYK;
         __GRP_DR.JEZYK_KH:=PARAM_W.JEZYK_KH
      ?};
      {? PARAM_W.JEZYK_KH='T' & _djezyk || PARAM_W.JEZYK:=_djezyk ?};
      WYDRUKIL.put()
   || wyjscie:=1
   ?}
?};

{? wyjscie=0
|| kk[1]:=+exec('tr','tlumaczenia','1000189');  {? kk[1]<12 || kk[1]:=12 ?}; nn[1]:=exec('tr','tlumaczenia','1000189');
   kk[2]:=+exec('tr','tlumaczenia','1000190');  {? kk[2]<8 || kk[2]:=8 ?}; nn[2]:=exec('tr','tlumaczenia','1000190');
   kk[3]:=+exec('tr','tlumaczenia','1000191');  {? kk[3]<12 || kk[3]:=12 ?}; nn[3]:=exec('tr','tlumaczenia','1000191');
   kk[4]:=+exec('tr','tlumaczenia','1000192');  {? kk[4]<12 || kk[4]:=12 ?}; nn[4]:=exec('tr','tlumaczenia','1000192');

   PRN.add("ww[1]",kk[1],nn[1],1);
   PRN.add("ww[2]",kk[2],nn[2],1);
   PRN.add("ww[3]",kk[3],nn[3],1);
   PRN.add("ww[4]",kk[4],nn[4],1)
?}


\set_efld_opt_paramw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Opcje pól w oknie redakcji zmiennej PARAM_W
::   WE: _a - 0-wyłączone pole drukować rabat, 1-włączone pole drukować rabat
::----------------------------------------------------------------------------------------------------------------------
_dr_rab:=_a;

_win_red:=PARAM_W.win_edit('?');

{? PARAM_W.DR_DUPL='T'
|| PARAM_W.efld_opt(_win_red,'enable=1',,'DUPLIKAT')
|| PARAM_W.efld_opt(_win_red,'enable=0',,'DUPLIKAT')
?};

{? _dr_rab
|| PARAM_W.efld_opt(_win_red,'enable=1',,'DR_RAB')
|| PARAM_W.efld_opt(_win_red,'enable=0',,'DR_RAB')
?}


\dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: wypelnia tabele tymczasowa XXX danymi z zakresu okreslonymi parametrami
::   WE: _a - grupa kontrah
::       _b - kontrahent
::       _c - odbiorca
::       _d - material
::       _e - od daty
::       _f - do daty
::       _g - analiza iloŁci
::       _h - analiza wartoŁci sprzedazy
::       _i - analiza wartoŁci zakupu
::       _j - analizowac rok poprzedni
::       _k - magazyn
::       _l - rodzaj analizy - 1 wg klienta, 0 - wg. materialu
::       _m - 1-grupowac wg grupy kontrahenta, 0-bez grupowania
::       _n - 0-data wystawienia, 1-data sprzedazy
::   WY: 1
::  OLD: \dane/stat.fml
::----------------------------------------------------------------------------------------------------------------------

VAR_DEL.delete('XXX');
XXX:=tab_tmp(3,
   'GRKH'   ,'STRING[20]'  ,'GRKH',
   'KH'     ,'STRING[10]'  ,'KH',
   'ODB'    ,'STRING[10]'  ,'KH',
   'KTM'    ,'STRING[50]'  ,'KTM',
   'ROKP_O' ,'REAL'        ,'ROKP_O',
   'ROKP_S' ,'REAL'        ,'ROKP_S',
   'ROKP_Z' ,'REAL'        ,'ROKP_Z',
   'ROK_O'  ,'REAL'        ,'ROK_O',
   'ROK_S'  ,'REAL'        ,'ROK_S',
   'ROK_Z'  ,'REAL'        ,'ROK_Z',
   'M1_O'   ,'REAL'        ,'M1_O',
   'M1_S'   ,'REAL'        ,'M1_S',
   'M1_Z'   ,'REAL'        ,'M1_Z',
   'M2_O'   ,'REAL'        ,'M2_O',
   'M2_S'   ,'REAL'        ,'M2_S',
   'M2_Z'   ,'REAL'        ,'M2_Z',
   'M3_O'   ,'REAL'        ,'M3_O',
   'M3_S'   ,'REAL'        ,'M3_S',
   'M3_Z'   ,'REAL'        ,'M3_Z',
   'M4_O'   ,'REAL'        ,'M4_O',
   'M4_S'   ,'REAL'        ,'M4_S',
   'M4_Z'   ,'REAL'        ,'M4_Z',
   'M5_O'   ,'REAL'        ,'M5_O',
   'M5_S'   ,'REAL'        ,'M5_S',
   'M5_Z'   ,'REAL'        ,'M5_Z',
   'M6_O'   ,'REAL'        ,'M6_O',
   'M6_S'   ,'REAL'        ,'M6_S',
   'M6_Z'   ,'REAL'        ,'M6_Z',
   'M7_O'   ,'REAL'        ,'M7_O',
   'M7_S'   ,'REAL'        ,'M7_S',
   'M7_Z'   ,'REAL'        ,'M7_Z',
   'M8_O'   ,'REAL'        ,'M8_O',
   'M8_S'   ,'REAL'        ,'M8_S',
   'M8_Z'   ,'REAL'        ,'M8_Z',
   'M9_O'   ,'REAL'        ,'M9_O',
   'M9_S'   ,'REAL'        ,'M9_S',
   'M9_Z'   ,'REAL'        ,'M9_Z',
   'M10_O'  ,'REAL'        ,'M10_O',
   'M10_S'  ,'REAL'        ,'M10_S',
   'M10_Z'  ,'REAL'        ,'M10_Z',
   'M11_O'  ,'REAL'        ,'M11_O',
   'M11_S'  ,'REAL'        ,'M11_S',
   'M11_Z'  ,'REAL'        ,'M11_Z',
   'M12_O'  ,'REAL'        ,'M12_O',
   'M12_S'  ,'REAL'        ,'M12_S',
   'M12_Z'  ,'REAL'        ,'M12_Z',
   'JM'     ,'STRING[10]'  ,'JM');

ndx1:=XXX.ndx_tmp(,,'GRKH',,,'KH',,,'ODB',,,'KTM',,,'KTM',,,'JM',,,'JM',,);
ndx2:=XXX.ndx_tmp(,,'GRKH',,,'KTM',,,'KTM',,,'JM',,,'JM',,);

:: okres poprzedni do zadanego
_odp:=date((_e~1)-1, (_e~2), 1 );
_dop:=date((_f~1)-1, (_f~2), 0 );
:: okres zadany
_od_rok:=(_e~1);
_do_rok:=(_f~1);
_od:=_e;
_do:=_f;

FAKS.cntx_psh;
FAP.cntx_psh;
ND.cntx_psh;
DK.cntx_psh;
{? _j
||
:: analizowaC okres poprzedni jako porownanie z okresem zadanym
   exec('sprzedaz','faktury_wydruk',_odp, _dop, 0, _a, _b, _c, _d, _k, _l,_m,_n,_g)
?};
:: analizujemy sprzedaz z okresu zadanego parametrem
exec('sprzedaz','faktury_wydruk',_od, _do, 1, _a, _b, _c, _d, _k, _l,_m,_n,_g);

{? _i
||
  {? _j
  ||
:: analizowac okres poprzedni jako porownanie z okresem zadanym
     exec('zakup','faktury_wydruk',_odp, _dop, 0, _a, _b, _c, _d, _k, _l, _m)
  ?};
:: analizujemy zakup z okresu zadanego parametrem
  exec('zakup','faktury_wydruk',_od, _do, 1, _a, _b, _c, _d, _k, _l, _m)
?};
FAKS.cntx_pop;
FAP.cntx_pop;
ND.cntx_pop;
DK.cntx_pop;
{? var_pres(ndx1)>0 || $ndx1 ?};
{? var_pres(ndx2)>0 || $ndx2 ?};
1


\sprzedaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: zbiera dane dotyczace sprzedazy dla zadanych parametrow
::   WE: _a - od daty
::       _b - do daty
::       _c - 0 - dane zbieraj do okresu poprzedniego
::          - 1 - dane zbieraj do okresu zadanego
::       _d - grupa kontrah
::       _e - kontrahent
::       _f - odbiorca
::       _g - material
::       _h - magazyn
::       _i - rodzaj analizy - 1 wg klienta, 0 - wg. materialu
::       _j - 1-grupowac wg grupy kontrahenta, 0-bez grupowania
::       _k - 0-data wystawienia, 1-data sprzedazy
::       _l - czy analiza ilosci
::   WY: 1
::  OLD: \sprzedaz/stat.fml
::----------------------------------------------------------------------------------------------------------------------

OKR.cntx_psh; FAKS.cntx_psh; FAP.cntx_psh;
OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
{? OKR.first
||
   {!
   |? FAKS.use('faktu'+ST.ODDZ+(($OKR.ROK)+2) );
      FAP.use('fakpo'+ST.ODDZ+(($OKR.ROK)+2) );
      FAKS.clear;
      {? FAKS.first
      ||
         _size:=FAKS.size();
         {!
         |? echo('TRWA ANALIZA - SPRZEDAŻ - '+$OKR.ROK+' - '+$_size);
            _size-=1;
            _mag:=null;
            ND.index('FAKS_REF');
            ND.prefix(BB.refsql(FAKS.ref()));
            {? ND.first
            ||
               _mag:=ND.MAG
            ?};
            _data:={? _k || FAKS.D || FAKS.DW ?};
            {? FAKS.AKC='T' & FAKS.SZ='S' & _data>=_a & _data<=_b & FAKS.T().ZEST='T'
               & (FAKS.KH().GRKH=_d | _d=null)
               & (FAKS.KH=_e | _e=null)
               & (FAKS.KH_ODB=_f | _f=null)
               & (_mag=_h | _h=null | _mag=null)
            ||
               FAP.index('FAP');
               FAP.prefix(FAKS.ref);
               {? FAP.first
               ||
                  {!
                  |? {? FAP.M=_g | _g=null
                     ||
                        _il:=FAP.IL;
                        _war:=FAP.WWAL_P;
                        {?
                           {? _i
                           ||
                              XXX.index(ndx1);
                              XXX.prefix();
                              XXX.find_key({? _j || FAKS.KH().GRKH().KOD || '' ?}, FAKS.KH().KOD, FAKS.KH_ODB().KOD)
                           ||
                              XXX.index(ndx2);
                              XXX.prefix();
                              {? _l
                              || XXX.find_key({? _j || FAKS.KH().GRKH().KOD || '' ?}
                                  ,FAP.M().KTM,FAP.M().KTM,FAP.JM().KOD,FAP.JM().KOD)
                              || XXX.find_key({? _j || FAKS.KH().GRKH().KOD || '' ?}
                                  ,FAP.M().KTM,FAP.M().KTM)
                              ?}
                           ?}
                        ||
                           {? _c
                           ||
                              ( $('XXX.M'+$(_data~2)+'_O+=FAP.IL') )();
                              ( $('XXX.M'+$(_data~2)+'_S+=FAP.WWAL_P') )();
                              XXX.ROK_O+=FAP.IL;
                              XXX.ROK_S+=FAP.WWAL_P
                           ||
                              XXX.ROKP_O+=FAP.IL;
                              XXX.ROKP_S+=FAP.WWAL_P
                           ?};
                           XXX.put()
                        ||
                           XXX.blank();
                           XXX.GRKH:={? _j || FAKS.KH().GRKH().KOD || '' ?};
                           XXX.KH:=FAKS.KH().KOD;
                           XXX.ODB:=FAKS.KH_ODB().KOD;
                           XXX.KTM:=FAP.M().KTM;
                           XXX.JM:=FAP.JM().KOD;
                           {? _c
                           ||
                              ( $('XXX.M'+$(_data~2)+'_O:=FAP.IL') )();
                              ( $('XXX.M'+$(_data~2)+'_S:=FAP.WWAL_P') )();
                              XXX.ROK_O:=FAP.IL;
                              XXX.ROK_S:=FAP.WWAL_P
                           ||
                              XXX.ROKP_O:=FAP.IL;
                              XXX.ROKP_S:=FAP.WWAL_P
                           ?};
                           XXX.add()
                        ?}
                     ?};
                     FAP.next
                  !}
               ?}
            ?};
            FAKS.next
         !}
      ?};
      OKR.next
   !}
?};
OKR.cntx_pop; FAKS.cntx_pop; FAP.cntx_pop;
1


\zakup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: zbiera dane dotyczace zakupu dla zadanych parametrow
::   WE: _a - od daty
::       _b - do daty
::       _c - 0 - dane zbieraj do okresu poprzedniego
::          - 1 - dane zbieraj do okresu zadanego
::       _d - grupa kontrah
::       _e - kontrahent
::       _f - odbiorca
::       _g - material
::       _h - magazyn
::       _i - rodzaj analizy - 1 wg klienta, 0 - wg. materialu
::       _j - 1-grupowac wg grupy kontrahenta, 0-bez grupowania
::   WY: 1
::  OLD: \zakup/stat.fml
::----------------------------------------------------------------------------------------------------------------------

_rokx:=_od_rokp:=(_a~1);
_do_rokp:=(_b~1);
ND.cntx_psh;
DK.cntx_psh;
{!
|? ND.use('nagdo'+ST.ODDZ+(($_rokx)+2) );
   DK.use('dokma'+ST.ODDZ+(($_rokx)+2) );

   ND.clear;
   {? ND.first
   ||
      _size:=ND.size();
      {!
      |? echo('TRWA ANALIZA - ZAKUP - '+$_rokx+' - '+$_size);
         _size-=1;
         {? ND.Z='T' & ND.D>=_a & ND.D<=_b & (ND.TYP().T='WZ' | ND.TYP().T='ZZ')
            & (ND.KH().GRKH=_d | _d=null)
            & (ND.KH=_e | _e=null)
            & (ND.KH_ODB=_f | _f=null)
            & (ND.MAG=_h | _h=null)
         ||
            DK.index('DOKMAG');
            DK.prefix(ND.ref);
            {? DK.first
            ||
               {!
               |? {? DK.M=_g | _g=null
                  ||
                     {? ND.TYP().P='T'
                     || _kier:=-1
                     || _kier:=1
                     ?};
                     {?
                        {? _i
                        ||
                           XXX.index(ndx1);
                           XXX.prefix();
                           XXX.find_key({? _j || ND.KH().GRKH().KOD || '' ?}, ND.KH().KOD, ND.KH_ODB().KOD)
                        ||
                           XXX.index(ndx2);
                           XXX.prefix();
                           XXX.find_key({? _j || ND.KH().GRKH().KOD || '' ?}, DK.M().KTM, M.KTM)
                        ?}
                     ||
                        {? _c
                        ||
                           ( $('XXX.M'+$(ND.D~2)+'_Z'+{? _kier=-1 || '-' || '+' ?}+'=DK.WAR') )();
                           XXX.ROK_Z+=_kier*DK.WAR
                        ||
                           XXX.ROKP_Z+=_kier*DK.WAR
                        ?};
                        XXX.put()
                     ||
                        XXX.blank();
                        XXX.GRKH:={? _j || ND.KH().GRKH().KOD || '' ?};
                        XXX.KH:=ND.KH().KOD;
                        XXX.ODB:=ND.KH_ODB().KOD;
                        XXX.KTM:=DK.M().KTM;
                        {? _c
                        ||
                           ( $('XXX.M'+$(ND.D~2)+'_Z:='+{? _kier=-1 || '-' || '' ?}+'DK.WAR') )();
                           XXX.ROK_Z:=_kier*DK.WAR
                        ||
                           XXX.ROKP_Z:=_kier*DK.WAR
                        ?};
                        XXX.add()
                     ?}
                  ?};
                  DK.next
               !}
            ?}
         ?};
         ND.next
      !}
   ?};

  _rokx+=1;
  _rokx<=_do_rokp
!};
ND.cntx_pop;
DK.cntx_pop;
1


\ilStVat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: sprawdza ile stawek VAT mamy na dokumencie
::   WE: _a - FAKS.ref() - wskazanie na dokument
::       _b - SLU.ref() - wskazanie na słownik stawek
::   WY: liczba stawek
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
SLO.cntx_psh();
FAKS.cntx_psh();
FAP.cntx_psh();
FAP.index('FAP');
FAP.prefix(_a);
_stvat:='';
{? FAP.first()
|| {!
   |? {? _stvat*($FAP.SV)=0 || _res+=1; _stvat+=$FAP.SV ?};
      {? FAP.FAKS().KOR<>''
      || FAKS.cntx_psh();
         FAP.cntx_psh();
         exec('wysw_kor','faktury_poz');
         {? _stvat*($FAP.SV)=0 || _res+=1; _stvat+=$FAP.SV ?};
         FAKS.cntx_pop();
         FAP.cntx_pop()
      ?};
      FAP.next()
   !}
|| SLO.index('SL');
   SLO.prefix(_b);
   _res:=SLU.size()
?};
SLO.cntx_pop();
FAKS.cntx_pop();
FAP.cntx_pop();
_res


\adr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca jak w wyniku
::   WE: _a - adres
::       _b - nr domu
::       _c - nr lokalu
::       _d - miasto
::       _e - poczta
::   WY: 'adres' lub 'adres nr domu' lub 'adres nr domu/nr lokalu'
::  OLD: \adr/wydruki.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('stringi') || _a:='' ?};
{? var_pres('_b')<>type_of('stringi') || _b:='' ?};
{? var_pres('_c')<>type_of('stringi') || _c:='' ?};
{? var_pres('_d')<>type_of('stringi') || _d:='' ?};
{? var_pres('_e')<>type_of('stringi') || _e:='' ?};
{? #_a<>0 & _b='' || _b:=_a; _a:='' ?};

{? _a='' & _d<>'' & ~{? _b='' ||(_e*_d) ||(_e*(_d + '' + _b))?}
||
   _d+{? +_b || ' '+_b || '' ?}+{? +_b & +_c || '/'+_c || '' ?}
||
   _a+{? +_b || ' '+_b || '' ?}+{? +_b & +_c || '/'+_c || '' ?}
?}


\rbkvat_drukuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.51_52_2]
:: OPIS: Czy drukować rachunek vat?
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
PARAM_W.WALH<>'N' & FAKS.VSKIDRBK<>null() & FAKS.VNB<>'' & FAKS.VRBK<>''


\faks_word
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Wydruk dokumentu do WORD
::----------------------------------------------------------------------------------------------------------------------
_env:=obj_new('DOKL'); _env.DOKL:=ST.DOKL;
params_set('env',_env);

{? FAP.f_active() || FAP.f_clear() ?};
FAKS.KH(); FAKS.KH_ODB();
params_exec('generuj','szablon_zws','LSP_FAKTURA',$FAKS.ref())


\rb_prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Wyznacza prefiks rachunku bankowego
::   WE: _a - 'skid_rbk' lub 'vskid_rbk'
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_skid_rbk:={? var_pres('_a')=type_of('') || _a || 'skid_rbk' ?};
_rb_prefix:=Plugin.run('RB_PREFIX_001',FAKS.ref(),_skid_rbk);
{? _rb_prefix<>'~~'
||
:: wtyczka
   _rb_prefix

|? {? exec('get','#params',100221,2)='T' || FAKS.T().UE='T' || FAKS.NIP_UE<>'' ?}
      |
   FAKS.KH().KRAJISO().IBAN='T'
      |
   1+(-exec('iban','zamsiw_wydruk','FAKS',FAKS.ref()))='t'
||
   {? _skid_rbk='skid_rbk'
   ||
      FAKS.SKID_RBK().BANK().KODISO().KODISO+' '

   |? _skid_rbk='vskid_rbk'
   ||
      FAKS.VSKIDRBK().BANK().KODISO().KODISO+' '
   ||
      ''
   ?}
||
   ''
?}


\faks_ul_dom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Zwraca ulicę z numerem domu i lokalu dla FAKS.KH
::   WY: ulica z numerem domu i lokalu
::----------------------------------------------------------------------------------------------------------------------
_ul:=exec('adr','faktury_wydruk',FAKS.UL,FAKS.DOM,FAKS.LOKAL,FAKS.MIASTO,FAKS.POCZ);
_ul


\faks_kpocz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Zwraca kod pocztowy dla FAKS
::   WY: kod pocztowy
::----------------------------------------------------------------------------------------------------------------------
_kpocz:=FAKS.KPOCZ;
{? _kpocz='' & FAKS.POCZ<>'' & +FAKS.POCZ>=6 & exec('chkForm','#string',6+FAKS.POCZ,'%c%c-%c%c%c',0)
|| _kpocz:=6+FAKS.POCZ
?};
_kpocz


\faks_odb_ul_dom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Zwraca ulicę z numerem domu i lokalu dla FAKS.KH_ODB
::   WY: ulica z numerem domu i lokalu
::----------------------------------------------------------------------------------------------------------------------
_ul:=exec('adr','faktury_wydruk',FAKS.O_UL,FAKS.O_DOM,FAKS.O_LOKAL,FAKS.O_MIASTO,FAKS.O_POCZ);
_ul


\kh_ul_dom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Zwraca ulicę z numerem domu i lokalu dla KH
::   WE: KH.ref()
::   WY: ulica z numerem domu i lokalu
::----------------------------------------------------------------------------------------------------------------------
_ul:='';
KH.cntx_psh();
KH.prefix();
{? KH.seek(_a)
|| _ul:=exec('adr','faktury_wydruk',KH.UL,KH.DOM,KH.LOKAL,KH.MIASTO,KH.POCZ)
?};
KH.cntx_pop();
_ul


\kh_kpocz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Zwraca kod pocztowy dla KH
::   WE: KH.ref()
::   WY: kod pocztowy
::----------------------------------------------------------------------------------------------------------------------
_kpocz:='';
KH.cntx_psh();
KH.prefix();
{? KH.seek(_a)
|| _kpocz:=KH.KPOCZ;
   {? _kpocz='' & KH.POCZ<>'' & +KH.POCZ>=6 & exec('chkForm','#string',6+KH.POCZ,'%c%c-%c%c%c',0)
   || _kpocz:=6+KH.POCZ
   ?}
?};
KH.cntx_pop();
_kpocz

:Sign Version 2.0 jowisz:1045 2024/01/23 10:33:51 25e7b1d55dc87bcfa1b0d5bd0133053698487ca8cb88fb0541753dacb60b8094c7e5aea3875816a136b007577288d5bb9333d14455f2cce66f86b6ceee704b173f42ba319cc833db2ba83bc12530218dbdf4eca0f3320b59b003064bcb75550e3926c9fa1f34b5b6e18de67df1678f163b5581209382ac7e20eac69560dc2b75

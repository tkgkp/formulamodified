:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: magdok_poz1.fml
:: Utworzony: 23.11.2022
:: Autor: AWI
::======================================================================================================================
:: Zawartość:
::======================================================================================================================


\dk_c2_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Przed wyświetleniem DK.C2
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pwmj2','magdok_poz')


\dk_c2_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Przed redkacją DK.C2
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? DK.T2='A' | DK.T2='R' | DK.T2='M'
||
   DPOZ.WPR_R:=fld();
   {? DK.WAL<>INFO.NAROD
   ||
      {? DK.PLUS='T' & DK.DOST<>date(0,0,0) & DK.RDK<>0 & DK.RDK<>#DK.ref() & exec('czy_bo','magdok_wspolne',ND.TYP)<>1
      ||
         0

      |? exec('spr_npoz','eurusd')
      ||
         exec('taz_sd_set','ceny');
         {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?} & DK.C2=0
         ||
            KALK.JM:=KALK.J2:=DK.J2; KALK.WS2:=1;
            exec('ceny_mat','ceny_mat',DK.M,DK.N().KH,'DK.C2',ND.KH_ODB,ST.MAG,'','N',DK.WAL,'',,0
             ,{? DK.J2<>null || DK.J2().KOD || '' ?}
             ,{? DK.J2<>null & DK.WS2<>0 || 1/DK.WS2 || 0 ?}
            ,0,0);
            {? DK.C2 || DPOZ.WPR_R:=-1; exec('dk_c2_ae','magdok_poz1',1); DPOZ.WPR_R:=fld ?}
         ?};
         1
      ?}
   ||
      _wyn:=exec('czy_rwal','eurusd','M') & exec('dk_b_cen','magdok_poz');
      {? ND.TYP().P='T' & ND.KRS=0
      || _wyn:=1;
         _tmg:={? ND.MAG().TYP*'EWI' || 'E' || (1+ND.MAG().TYP) ?};
         {? 'E'*_tmg & (DK.C<>0 | exec('get','#params',600104,2)='T')
         || DK.IL:=DK.IL;
            DK.WAR:=(DK.IL*DK.C)$2;
            exec('obl_mwb','magdok_poz');
            {? exec('biez_cen','ceny_dok',DK.M,ST.MAG,DK.N().D)=0
            || _wyn:=exec('biez_cen','ceny_dok',DK.M,ST.MAG,DK.N().D,3)<>-1
            || _wyn:=0
            ?};
            win_disp()
         ?};
         {? _wyn & DK.DOST<>date(0,0,0) & DK.RDK<>0 & DK.RDK<>#DK.ref()
          & ~exec('FindAndGet','#table',TYPYDOK,ND.TYP,,"TYPYDOK.T=\'BO\'",0)
         || _wyn:=0
         ?}
      ?};
      {? _wyn & (~(DK.N().MAG().TYP*'EWI') | exec('biez_cen','ceny_dok',DK.M,ST.MAG,DK.N().D)=0)
       & 'TW'*exec('get','#params',600300,2) & DK.N().TYP().DN='K'
      || _wyn:=0
      ?};
      {? _wyn & DK.C2=0
      ||
         exec('taz_sd_set','ceny');
         {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?}
         ||
            KALK.JM:=KALK.J2:=DK.J2; KALK.WS2:=1;
            exec('stplicz','ceny',DK.IL);
            _params:=exec('ceny_mat_a','ceny_mat');
            _params.M:=DK.M;
            _params.KH:=ND.KH;
            _params.CZY_WAL:='N';
            _params.WAL:=INFO.NAROD;
            _params.FLD_CENA:='DK.C2';
            _params.J2:={? DK.J2<>null || DK.J2().KOD || '' ?};
            _params.WS2:={? DK.J2<>null & DK.WS2<>0 || 1/DK.WS2 || 0 ?};
            _params.S_TAZ:=0;
            _params.S_DPOZ:=1;
            exec('ceny_mat','ceny_mat',_params);
            {? DK.C2 || DPOZ.WPR_R:=-1; exec('dk_c2_ae','magdok_poz1',1); DPOZ.WPR_R:=fld; win_disp() ?}
         ?}
      ?};
      _wyn
   ?}
?}


\dk_c2_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: F3 DK.C2
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: cena walutowa magazynowa na dokumentach przychodowych
TAZ.RABOFF:=1;
exec('f3_zdcen','ceny_mat',DK.M,DK.N().KH,DK.C2,,'DK2',DK.WAL);
TAZ.RABOFF:=0;
''


\dk_c2_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Po redakcji DK.C2
::   WE: _a - 0/1 - przeliczaj
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0) || _a:=0 ?};
_wyn:=1;
{? (_a | DPOZ.WPR_R<>fld()) & DK.WS2>0
||
   _dokl_c:=DK.M().DOKL_C;
   {? DK.C2<0
   ||
      FUN.info('Cena musi być dodatnia.'@);
      _wyn:=0

   |? DK.WAL<>INFO.NAROD
   ||
      DK.CWAL:=(DK.C2/DK.WS2)$_dokl_c;
      _wyn:=params_exec('po_cenaw','ceny')
   ||
      DK.C:=(DK.C2/DK.WS2)$_dokl_c;
      _wyn:=params_exec('war_mag','magdok_poz')
   ?}
?};
_wyn


\dk_braki_r_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Przed redakcją pola DK.BRAKI_R (przyjęcie z kooperacji)
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? DK.IL_B>0
|| _result:=1
?};
{? _result>0
|| BRAKI_R.win_dict('WER');
   BRAKI_R.actions('WER','O');
   ZGP.cntx_psh();
   _ref:='';
   {? DK.ZGP().TOPER<>null()
   || _ref:=exec('FindAndGet','#table',TOPER,DK.ZGP().TOPER,,"TTOUT",null())
   ?};
   ZGP.cntx_pop();
   exec('braki_r_filter','braki',,'N',$_ref)
?};
_result


\dk_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Wyzwalacz "Dołącz - po" dla tabeli DK
::----------------------------------------------------------------------------------------------------------------------
{? DK.ZLIM<>null() & DK.Z='T'
|| exec('zlim_update','magdok_prod',DK.IL,DK.WAR,DK.PLUS,DK.ZLIM)
?};
{? DK.ZL<>null()
|| exec('zl_rok_update','magdok_prod',DK.ZL,DK.AR)
|? DK.GROP<>null()
|| exec('grop_rok_update','magdok_prod',DK.GROP,DK.AR)
?};
~~


\dk_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Wyzwalacz "Popraw - po" dla tabeli DK
::----------------------------------------------------------------------------------------------------------------------
{? DK.ZLIM<>null()
|| {? (bfld('IL')<>DK.IL | bfld('WAR')<>DK.WAR | bfld('ZLIM')<>DK.ZLIM | bfld('Z')<>DK.Z)
   ||
      {? (bfld('ZLIM')<>DK.ZLIM & bfld('ZLIM')<>null()) | (bfld('Z')<>DK.Z & DK.Z='N')
      ||
::       Zmieniono powiązanie do ZLIMa lub stan na dokumencie, odkręcam ilość/wartość na poprzednim ZLIMie
         exec('zlim_update','magdok_prod',bfld('IL')*(-1),bfld('WAR')*(-1),bfld('PLUS'),bfld('ZLIM'))
      ?};
      {? DK.Z='T'
      ||
::       Jeżeli zmienił się ZLIM lub stan dokumentu to działanie jak podczas dołączania, brana cała ilość/wartość
::       W przeciwnym razie wyliczana różnica
         _il:={? bfld('ZLIM')<>DK.ZLIM | bfld('Z')<>DK.Z || DK.IL || DK.IL-bfld('IL') ?};
         _war:={? bfld('ZLIM')<>DK.ZLIM | bfld('Z')<>DK.Z || DK.WAR || DK.WAR-bfld('WAR') ?};
          exec('zlim_update','magdok_prod',_il,_war,DK.PLUS,DK.ZLIM)
      ?}
   ?}
?};
{? DK.ZL<>null() & (bfld('ZL')<>DK.ZL | bfld('AR')<>DK.AR)
|| exec('zl_rok_update','magdok_prod',DK.ZL,DK.AR)
|? DK.GROP<>null() & (bfld('GROP')<>DK.GROP | bfld('AR')<>DK.AR)
|| exec('grop_rok_update','magdok_prod',DK.GROP,DK.AR)
?};
~~


\dk_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Wyzwalacz "Usuń - po" dla tabeli DK
::----------------------------------------------------------------------------------------------------------------------
{? bfld('ZLIM')<>null() & bfld('Z')='T'
|| exec('zlim_update','magdok_prod',bfld('IL')*(-1),bfld('WAR')*(-1),bfld('PLUS'),bfld('ZLIM'))
?};
~~


\dk_grop_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Przed redakcją pola DK.GROP
::----------------------------------------------------------------------------------------------------------------------
0

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 1e2c1f888c52a8752b6ff43e44d0249f56438c1d35dcb29c1f35ab83d9f20f1211acb5ce4b58fd5d3579c0a78b4518ff3c58185b92caca9aa4a4fece54758ec8a65bdcb75c85d7428eb7538783b70934996cfcd77a39b25f243e1d99157fedbeea73240d12b80734ed5ca2bd9756399b8257c1d2ce37685a9546736db9bd718c

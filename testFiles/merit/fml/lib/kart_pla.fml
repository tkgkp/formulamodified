:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: kart_pla.fml [2000]
:: Utworzony: 2000/01/13
:: Autor: jaws
::======================================================================================================================
:: Zawartosc: Formuły obsługujące kartę zarobkową, podatkową i ubezpieczeniową
::======================================================================================================================


\kzup_rozmiar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Zwraca liczbę pól przewidzianych do zapisu kwot składników
::   WE: _a [TABLE] - alias tabeli karty: KZ/KP/KU
::   WY: liczba pól
::----------------------------------------------------------------------------------------------------------------------
_cnt:=0;
{! _ndx:=1
|? var_press('S'+$_ndx,_a)>0
|! _cnt+=1
!};
_cnt


\kzup_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ustala kontekst dla działania funkcji obsługi karty
::   WE: _a - oznaczenie karty
::          alias tabeli [_TABLE] KZ/KP/KU
::          akronim tabeli [STRING] @.KZ/KZ/@.KP/KP/@.KU/KU
::          symbol kartoteki Z/P/U
::   WY: wskazanie tablicy nazwanej
::----------------------------------------------------------------------------------------------------------------------
_ctx:=obj_new(
:: tabela karty
   'TABLE',
:: indeks karty
   'KEY',
:: rozmiar karty
   'LEN',
:: alias do zmiennej nazw
   'N_VAR',
:: informacje ze słownika rubryk
   'R_FLD', 'R_KEY',
:: kod karty
   'CODE',
:: akronim okna wertowania
   'WINDOW'
);

{? var_press('_a')=type_of('')
|| {? _a=!KZ | _a='KZ' | _a='Z' || _TAB:=KZ
   |? _a=!KP | _a='KP' | _a='P' || _TAB:=KP
   |? _a=!KU | _a='KU' | _a='U' || _TAB:=KU
   ?}
|? var_press('_a')=type_of(SYSLOG)
|| _TAB:=_a
?};

{? var_press('_TAB')<>type_of(SYSLOG)
|| return(_ctx)
?};

{? _TAB=KZ
|| _ctx.KEY:='_KARTAZA';
   _ctx.R_FLD:='RSKZ';
   _ctx.R_KEY:='RUBRSKZ';
   _ctx.N_VAR:=KART_ZAR

|? _TAB=KP
|| _ctx.KEY:='_KARTAPO';
   _ctx.R_FLD:='RSKP';
   _ctx.R_KEY:='RUBRSKP';
   _ctx.N_VAR:=KART_POD

|? _TAB=KU
|| _ctx.KEY:='KARTAUB';
   _ctx.R_FLD:='RSKU';
   _ctx.R_KEY:='RUBRSKU';
   _ctx.N_VAR:=KART_ZUS
?};

_ctx.TABLE:=_TAB;
_ctx.CODE:=!_TAB+1;
_ctx.LEN:=exec('kzup_rozmiar','kart_pla',_TAB);
_ctx.WINDOW:='WER';

_ctx


\kzup_nazwy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ustala treści nagłówków kolumn i etykiet pól
::   WE: _a.. - aliasy tabel kart: KZ, KP, KU
::   WY:
::  OLD: \czyt_naz/kart_pla.fml
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
{! _arg:=1.._
|! _ctx:=exec('kzup_info','kart_pla',_[_arg]);
   _field:=_ctx.R_FLD;
   _order:=_ctx.R_KEY;
   _names:=_ctx.N_VAR;

   {! _ndx:=1
   |? {? var_press('S'+$_ndx,_names)>0
      || +($(!_names+'.S'+$_ndx+':=\'Składnik '+$_ndx+'\''))()
      ?}
   !};
   R.index(_order);
   R.prefix();
   R.last();
   {!
   |? {? _pos:=($('R.'+_field))()
      || ($(!_names+'.S'+$_pos+':=R.RT'))();
         ($(!_names+'.H1:=\'Rok\''))();
         ($(!_names+'.H2:=\'Mc\''))();
         1
      ?} & R.prev()
   !};
   &_names
!};
R.cntx_pop();
KZUP_UST.cntx_psh();
{? !_a*'KZ'
|| _kart:=KART_ZAR
|? !_a*'KP'
|| _kart:=KART_POD
|? !_a*'KU'
|| _kart:=KART_ZUS
?};
{? _kart.KZUP_UST=null | ~KZUP_UST.seek(_kart.KZUP_UST,,1)
|| _kart.KZUP_UST:=exec('kzup_pref_ust','kart_pla')
?};

{! _ii:=1..7
|! _hd:='VAL_S%1'[$_ii];
   {? (_ip:=($'_a.S%1'[$_ii])(KZUP_UST))<>0 &
      (_rt:=($'_a.S%1'[$_ip])(_ctx.N_VAR))<>''
   || _ctx.TABLE.fld_opt(_ctx.WINDOW,'col_name=%1'[_rt],_ctx.N_VAR,_hd)
   ?}
!};
KZUP_UST.cntx_pop()


\kzup_czytaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [2009+]
:: OPIS: Podsumowuje kolumny w karcie zarobkowej, podatkowej i ubezpieczeniowej.
::   WE: _a [TABLE] - alias tabeli karty: KZ/KP/KU
::       _b [TABLE] - alias zmiennej: KART_ZAR/KART_POD/KART_ZUS
::  OLD: \sum_kzup/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
_b.SUM_S1:=_b.SUM_S2:=_b.SUM_S3:=_b.SUM_S4:=_b.SUM_S5:=_b.SUM_S6:=_b.SUM_S7:=0;

EDIT_VAR.KZUP_TYP:=exec('kzup_info','kart_pla',_a).CODE;

KZUP_UST.clear();
{? _b.KZUP_UST=null | ~KZUP_UST.seek(_b.KZUP_UST)
|| _b.KZUP_UST:=exec('kzup_pref_ust','kart_pla')
?};
_b.KZUP_UST();

{! _i:=1..7
|! _n:=($('KZUP_UST.S'+$_i))();
   ($('_a.HDR_S'+$_i+':=_a.S'+$_n))(_b)
!};

_a.cntx_psh();
{? _a.first()
|| {!
   |? exec('kzup_wiersz','kart_pla',_a,_b);
      {! _s:=1..7
      |! ($(!_b+'.SUM_S'+$_s+'+='+!_b+'.VAL_S'+$_s))()
      !};
      _a.next()
   !}
?};
_a.cntx_pop()


\kzup_wiersz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [2009+]
:: OPIS: Ustala wartości w wierszu karty
::   WE: _a [TABLE] - alias tabeli karty: KZ/KP/KU
::       _b [TABLE] - alias zmiennej: KART_ZAR/KART_POD/KART_ZUS
::  OLD: \rek_kzup/kartprac.fml
::----------------------------------------------------------------------------------------------------------------------
{! _i:=1..7
|! _n:=($('KZUP_UST.S'+$_i))();
   ($('_a.VAL_S'+$_i+':=_b.S'+$_n))(_b,_a)
!}


\kzup_pref_ust
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [2009+]
:: OPIS: Wyszukuje domyślne ustawienia
::  OLD: \pref_ust/kart_pla.fml
::----------------------------------------------------------------------------------------------------------------------
KZUP_UST.clear();
KZUP_UST.blank(1);
KZUP_UST.TYP:=EDIT_VAR.KZUP_TYP;
KZUP_UST.PREF:='T';
{? ~KZUP_UST.find_rec()
|| KZUP_UST.index('OPIS');
   KZUP_UST.prefix(EDIT_VAR.KZUP_TYP);
   {? KZUP_UST.first()
   || KZUP_UST.PREF:='T';
      KZUP_UST.put()
   || exec('kzup_dodaj_ust','kart_pla')
   ?}
?};
KZUP_UST.ref()


\kzup_dodaj_ust
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [2009+]
:: OPIS: Dodaje standardowe ustawienia
::  OLD: \dod_ust/kart_pla.fml
::----------------------------------------------------------------------------------------------------------------------
KZUP_UST.TYP:=EDIT_VAR.KZUP_TYP;
KZUP_UST.OPIS:='Standardowy (dodany automatycznie)';

{! _n:=1..7
|! ($('KZUP_UST.S'+$_n+':=_a'))(_n)
!};

{? KZUP_UST.add(1)
|| KZUP_UST.blank(1);
   KZUP_UST.TYP:=EDIT_VAR.KZUP_TYP;
   KZUP_UST.PREF:='T';
   {? ~KZUP_UST.find_rec()
   || KZUP_UST.PREF:='T';
      KZUP_UST.put(1)
   ?}
?}


\widok_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przygotowuje widok karty
::   WE: _a [TABLE] - alias tabeli karty: KZ/KP/KU
::   WE: wskazanie tablicy nazwanej zawierającej parametry działania funkcji
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
{? type_of(_par)>=100
|| return(_par)
?};

exec('widok_konf','kart_pla',_a)


\widok_konf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Tworzy bufor zawierający konfigurację karty
::   WE: _a [TABLE] - alias tabeli karty: KZ/KP/KU
::   WE: wskazanie tablicy nazwanej zawierającej parametry działania funkcji
::----------------------------------------------------------------------------------------------------------------------
_par:=obj_new(
:: akronim tabeli, dla której wykonywane będą funkcje
   'TABLE',
:: klucz przeglądania danych karty
   'INFO',
:: ref pracownika / osoby
   'REF',
:: rok
   'R',
:: alias tabeli tymczasowej zawierającej dane do prezentacji
   'DATA',
:: bufor optymalizacji odczytu danych
   'BUF'
);

_par.TABLE:=_a;
_par.INFO:=exec('kzup_info','kart_pla',_a);

_par.DATA:=tab_tmp(1,
:: akronim pola w karcie
   'NUMER','INTEGER','Pozycja',
:: wyświetlana nazwa składnika
   'NAZWA','STRING[20]','Nazwa',
:: wszystkie nazwy składników
   'OPIS','STRING[255]','Składniki',
:: wartości składników karty
   'M1', 'REAL','Styczeń',
   'M2', 'REAL','Luty',
   'M3', 'REAL','Marzec',
   'M4', 'REAL','Kwiecień',
   'M5', 'REAL','Maj',
   'M6', 'REAL','Czerwiec',
   'M7', 'REAL','Lipiec',
   'M8', 'REAL','Sierpień',
   'M9', 'REAL','Wrzesień',
   'M10','REAL','Październik',
   'M11','REAL','Listopad',
   'M12','REAL','Grudzień',
   'SUM','REAL','Razem'
);

_len:=0;
{! _ndx:=1
|? var_press('S'+$_ndx,_a)>0
|! _len+=1
!};
:: przydziel pamięć na cały rok
_par.BUF:=obj_new(_len*12);

_par


\widok_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Uzupełnia dane widoku karty
::   WE: _a - wskazanie tablicy nazwanej zawierającej parametry działania funkcji
::       _b - wskazanie pracownika / osoby
::       _c - rok
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a.REF:=_b;
_a.R:=_c;

_SRC:=_a.TABLE;
_TAB:=_a.DATA;
_buf:=_a.BUF;
_len:=obj_len(_buf);
{! _ndx:=1.._len
|! _buf[_ndx]:=0
!};
_len:=obj_len(_buf)/12;

R.cntx_psh();
R.index(_a.INFO.R_KEY);

_TAB.erase();
_SRC.cntx_psh();
_SRC.index(_a.INFO.KEY);
{? _SRC=KU
|| _SRC.prefix(exec('ref_firma','ustawienia'),_a.REF,_a.R)
|| _SRC.prefix(_a.REF,_a.R)
?};
_loop:=_SRC.first();
{!
|? _loop
|! {! _ndx:=1.._len
   |! _buf[(_SRC.M-1)*_len+_ndx]+=($('_a.S'+$_ndx))(_SRC)
   !};
   _loop:=_SRC.next()
!};
_SRC.cntx_pop();

{! _row:=1.._len
|! _TAB.blank();
   R.prefix(_row);
   {? R.first()
   || _TAB.NUMER:=_row;
      _TAB.NAZWA:=R.RT;
      {!
      |? _TAB.OPIS+=R.RT+', ';
         R.next()
      !};
      _TAB.OPIS-=2;
      {! _col:=1..12
      |! _val:=_buf[(_col-1)*_len+_row];
         ($('_a.M'+$_col+':=_b'))(_TAB,_val);
         _TAB.SUM+=_val
      !};
      _TAB.add()
   ?}
!};

R.cntx_pop();
~~


\widok_okno
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Tworzy okno dla widoku karty
::   WE: _a - wskazanie tablicy nazwanej zawierającej parametry działania funkcji
::   WY: akronim utworzonego okienka
::----------------------------------------------------------------------------------------------------------------------
_TAB:=_a.DATA;

_mob:='mobile_visible=1';
_wnd:=_TAB.mk_sel(_a.TABLE.comment(),,0,'#kzup_widok',,,,,'U',,,,,'html_maximized');
_TAB.win_fld(_wnd,,'NUMER',,,-3,,,,,'Pozycja w karcie'@,,,,,,_mob);
_TAB.win_fld(_wnd,,'OPIS',,,20,,,,,'Składniki list płac'@,,,,,,_mob);
_TAB.win_fld(_wnd,,'SUM',,,-9,2,,,,'Podsumowanie roku'@,,,,,,_mob);
:: dodaj kolumny miesięcy
{! _col:=1..12
|! _TAB.win_fld(_wnd,,'M'+$_col,,,-9,2,,,,'Podsumowanie miesiąca'@)
!};

_wnd


\widok_kzup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Przygotowuje widoku dane katy
::   WE: _a [TABLE] - alias tabeli karty: KZ/KP/KU
::       _b [_P/OSOBA] - wskazanie osoby / pracownika
::       _c [INTEGER] - rok
::   WY: wskazanie tablicy nazwanej zawierającej parametry działania funkcji
::----------------------------------------------------------------------------------------------------------------------
_par:=exec('widok_init','kart_pla',_a);
exec('widok_dane','kart_pla',_par,_b,_c);

{? ref_tab(_b)=P
|| SEEK.P:=_b;
   SEEK.P().OSOBA()
|| SEEK.OSOBA:=_b;
   SEEK.OSOBA()
?};

_par


\widok_tabela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS:
::   WE: _a [TABLE] - alias tabeli karty: KZ/KP/KU
::       _b [_P/OSOBA] - wskazanie osoby / pracownika
::       _c [INTEGER] - rok
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=exec('widok_kzup','kart_pla',_a,_b,_c);
_wnd:=exec('widok_okno','kart_pla',_par);

_par.DATA.win_sel(_wnd);
_par.DATA.hdr_sel(' za rok '+$_c+' - '+OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE);
{? cli_ver()='interm'
|| _par.DATA.win_sopt(_wnd,'select_record_checkbox=0');
   _wnd_grp:=_par.DATA.grp_make(_par.TABLE.comment(),,,,,,,'html_maximized');
   _par.DATA.grp_sel(_wnd_grp,,_wnd);
   _par.DATA.win_sel(_wnd_grp)
?};
_par.DATA.select()


\widok_drukuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS:
::   WE: _a [TABLE] - alias tabeli karty: KZ/KP/KU
::       _b [_P/OSOBA] - wskazanie osoby / pracownika
::       _c [INTEGER] - rok
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set('cfg',exec('widok_kzup','kart_pla',_a,_b,_c));

rep_exec('ppl_kzup_w')

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 73388f847eab720a4190f6f8ace6cecf32c96a74735e0a1de5065306f6844b7ae9d1d7c5d1e2e73df11101bb3911bdb1c46d4a31277808a78e42b43f2460b4271ff3004f25092d05a81c7c70d1058eba5b4c14104cc3c5969dd586599a54ba7e5b580314f51a8cfc1d4d8821c5c95e7824261184e7835501e2012c21dfdff6f0

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_rodz.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa rodzajów obiektów dla systemu Statystyki
::======================================================================================================================


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Czyści powiązania do rekordu tabeli ST_RODZ
::   WE: _a - ST_RODZ.ref()
::   WY: >0  -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PRYWATNA><CLEAN>
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---
ST_RODZ.cntx_psh(); ST_RODZ.clear();
{? ST_RODZ.seek(_ref)
||
   {? _can_continue>0
   ||
::    1. Usuwam ST_OBJ które wskazują na ten element
      ST_OBJ.cntx_psh();
      ST_OBJ.index('OPIS');
      ST_OBJ.prefix(ST_RODZ.ref());
      {? ST_OBJ.first()
      || {!
         |? _can_continue:=exec('delete','st_obj',ST_OBJ.ref());
            ST_OBJ.first() & _can_continue>0
         !}
      ?};
      ST_OBJ.cntx_pop()
   ?};
   ~~
?};
ST_RODZ.cntx_pop();
:: --- wszystkie powiazania usuniete? ---

{? _can_continue>0
|| _result:=1
|| undo()
?};

{? _mydo || end() ?};

_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Kasuje podany rekord tabeli ST_RODZ (wykonywane w transakcji!!!)
::   WE: _a - ST_RODZ.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_RODZ.cntx_psh(); ST_RODZ.clear();
{? ST_RODZ.seek(_ref)
|| {? exec('clean','st_rodz',_ref)>0
   || {? ST_RODZ.del(,1)>0
      || _result:=1
      || undo();
         _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};

{? _result<0
|| undo()
?};

ST_RODZ.cntx_pop();
{? _mydo || end() ?};
_result


\st_rodz_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Dołącz dla ST_RODZ
::----------------------------------------------------------------------------------------------------------------------
ST_RODZ.win_edit('RED');
ST_RODZ.blank();
{? ST_RODZ.edit()
|| ST_RODZ.add()
?};
~~


\st_rodz_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń dla ST_RODZ
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRUPA='T' | FUN.ask('Czy usunąć bieżący wiersz?'@)
|| exec('delete','st_rodz',ST_RODZ.ref())
?};
~~


\st_rodz_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Popraw dla ST_RODZ
::----------------------------------------------------------------------------------------------------------------------
ST_RODZ.win_edit('RED');
{? ST_RODZ.edit()
|| ST_RODZ.put()
?};
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Import słownika ST_RODZ (rodzaje obiektów) z pliku.
::----------------------------------------------------------------------------------------------------------------------
_added:=0;_modified:=0;_processed:=0;

_tab:=exec('legenda_read','st_common');
_tab.prefix(0);
{? _tab.first()
||
   ST_RODZ.cntx_psh();
   ST_RODZ.index('SYMBOL');
   ST_RODZ.prefix();
   {!
   |?
      _processed+=1;
      _sym:=_tab.SYMBOL;
      _opis:=_tab.NAME;
      {? ~ST_RODZ.find_key(_sym,)
      || ST_RODZ.blank(1);
         ST_RODZ.SYMBOL:=_sym;
         ST_RODZ.OPIS:=_opis;
         {? ST_RODZ.add()
         || _added+=1
         ?}
      |? ST_RODZ.OPIS<>_opis
      || ST_RODZ.OPIS:=_opis;
         {? ST_RODZ.put()
         || _modified+=1
         ?}
      ?};
      _tab.next()
   !};
   ST_RODZ.cntx_pop()
?};

{? _processed>0
|| FUN.info('Pomyślnie zaimportowano rodzaje obiektów.\n'
            'Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów oraz dołączono: %3 nowych zapisów.'@
            [$_processed,$_modified,$_added])
?};
~~


\st_rodz_del_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń przed dla grupy rekordów (tabela ST_RODZ)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?'@)
|| VAR.GRUPA:='T';
   1
|| 0
?}


\st_rodz_del_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń po dla grupy rekordów (tabela ST_RODZ)
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:22:58 65fc041c23381cb85e445b056c656601ad974ddc72ef184ae28fe83cdc1bfd09ec464e817929e4635179602741b0cdc748ee23a70c5051e25b9c29742b4aeff3918ceaef271b57d5810c1ed5c74fde97f3a140e7691bd5507d29e00ea2ba835317ef5247ce0fdda55a40ff5ca4df985252e2534fbb50c5657fd6e2447df1e769

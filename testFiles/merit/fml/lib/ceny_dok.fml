:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ceny_dok.fml
:: Utworzony: 29.04.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Zbiorcza zmiana cen na dokumencie
::======================================================================================================================


\ceny_mat_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: ceny w ramach dokumentu
::   WE: [_a] - 0-pozycje dokumentu i agregowanie, 1-tylko pozycje, 2-kontrola zmian
::       [_b] - 1-wołać ini_kom i end_kom, 0-wpp
::       [_c] - tabela
::       [_d] - 0-podczytać nagłówek, 1-nie podczytywać nagłówka
::       [_e] - 1-zdjąć _Nag.r_lock, 0-wpp
::       [_f] - 0-priorytet cenniki, 1-priorytet dokument
::       [_g] - 0-waluta niezmieniona, 1-zmiana waluty
::  OLD: \ceny_mat_dok/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=0 ?};
{? var_pres('_b')<>type_of(1) || _b:=1 ?};
{? var_pres('_c')<>type_of(FIRMA) || _c:=cur_tab(1) ?};
{? var_pres('_d')<>type_of(1) || _d:=0 ?};
{? var_pres('_e')<>type_of(1) || _e:=1 ?};
{? var_pres('_f')<>type_of(1) || _f:=0 ?};
{? var_pres('_g')<>type_of(1) || _g:=0 ?};

_wyn:=0;
_ctrl:=_a=2;
_change:=0;

_czysc:="VAR_DEL.delete('__wer1','__wer2','__wer3','__Ceny','__Poz','__Poztmp','__Suma','__cendok','__tms')";
_czysc();

_Nag:=_c;
__Poz:=
   {? _Nag=OFE || OFP
   |? _Nag=ZK_N || ZK_P
   |? _Nag=FAKS || FAP
   |? _Nag=ND || DK
   || cur_tab(1)
   ?};
&_Nag;
_Nag:=
   {? __Poz=OFP || OFE
   |? __Poz=ZK_P || ZK_N
   |? __Poz=FAP || FAKS
   |? __Poz=DK || ND
   || cur_tab(1)
   ?};

:: przestrzen nazw __cendok
__cendok:=obj_new('NAGREF','NAGWAL','NAGPUT');
__cendok.NAGREF:=_Nag.ref();
__cendok.NAGWAL:=_Nag.WAL;
__cendok.NAGPUT:=
   {? _d=0
   || ""
   || {? _Nag=OFE || "OFE.put()"
      |? _Nag=ZK_N || "ZK_N.put()"
      |? _Nag=FAKS || "FAKS.put()"
      |? _Nag=ND || "ND.put()"
      ?}
   ?};

{? var_pres('__Poz')<>type_of(FIRMA) || return(_wyn) ?};

{? _Nag=FAKS & FAKS.STS<>null
|| exec('ini_kom','#message','Przeliczenie dokumentu wg cenników'@);
   _ok:=
      {? FAKS.SZ='S'
      || exec('spr_upr2','users','STS',FAKS.STS,'Brak uprawnień do stanowiska sprzedaży %1.'@[FAKS.STS().KOD])
      || exec('spr_upr2','users','STZ',FAKS.STS,'Brak uprawnień do stanowiska zakupu %1.'@[FAKS.STS().KOD])
      ?};

   {? ~_ok || __kom_on:=1 ?};
   exec('end_kom','#message');
   {? ~_ok || return(_wyn) ?}
?};

{? _b || exec('ini_kom','#message','Przeliczenie dokumentu wg cenników'@) ?};
_ok:=exec('ceny_mat_dokchk','ceny_dok',_Nag);
_r_lock:=_ok; {? _ok=2 || _ok:=1 ?};
{? _b || exec('end_kom','#message') ?};

{? _ok=-1 | _ok=0 || return({? _ok=0 || _wyn || 1 ?}) ?};

__tms:=FIRMA.tm_stamp;
_oddz:=ST.ODDZ;
_sd:=TAZ.SD:='S';
SLO.cntx_psh(); SLO.prefix(); _walkod:={? SLO.seek(_Nag.WAL) || SLO.KOD || '' ?}; SLO.cntx_pop();

__Poztmp:=tab_tmp(1
   ,'POZ'   ,'INTEGER'     ,'Pozycja'
   ,'KTM'   ,'STRING[50]'  ,'Indeks'
   ,'IL'    ,'REAL'        ,'Ilość'
   ,'JM'    ,'STRING[10]'  ,'Jm'
   ,'TAR'   ,'STRING[41]'  ,'Cennik'
   ,'CEN'   ,'REAL'        ,'Cena'
   ,'RABP'  ,'REAL'        ,'Rabat pozycji'
   ,'WAR'   ,'REAL'        ,'Wartość'
   ,'AGREG' ,'STRING[11]'  ,'Agregacja'
   ,'TIL'   ,'REAL'        ,'wg typu - Ilość'
   ,'TCEN'  ,'REAL'        ,'wg typu - Cena'
   ,'TRABP' ,'REAL'        ,'wg typu - Rabat pozycji'
   ,'TWAR'  ,'REAL'        ,'wg typu - Wartość'
   ,'CENY'  ,'STRING[16]'  ,'$__Ceny.ref()'
   ,'REF'   ,'STRING[16]'  ,'Ref pozycji'
);

_ff:="
   _Nag:=
      {? 5+ref_name(__cendok.NAGREF)='faktu' || FAKS
      |? 5+ref_name(__cendok.NAGREF)='nagdo' || ND
      |? 5+ref_name(__cendok.NAGREF)='zknag' || ZK_N
      |? ref_name(__cendok.NAGREF)='oferty' || OFE
      ?};
   _kh:=_Nag.KH;
   _grkh:=_Nag.KH().GRKH;
   _m:=exec('FindInSet','#table','M','MATKTM',__Poztmp.KTM,__Poztmp.KTM);
   _mgr:=__Poz.M().MGR;
   _mgrp:=__Poz.M().MGRP;
   _kh_odb:={? _Nag=FAKS | _Nag=ND || _Nag.KH_ODB || _Nag.ODB ?};
   exec('get','#params',800830,2)='N' | exec('upr_chk','ceny',_kh_odb,_kh,_grkh,_m,_mgrp,_mgr)
";
__Poztmp.fld_fml('TAR','BEFORE_DISPLAY',_ff);
_brak_upr_cs:=exec('upr_cs','ceny')=0;
{? _brak_upr_cs
||
   _ff:="0";
   __Poztmp.fld_fml('CEN','BEFORE_DISPLAY',_ff);
   __Poztmp.fld_fml('RABP','BEFORE_DISPLAY',_ff);
   __Poztmp.fld_fml('WAR','BEFORE_DISPLAY',_ff);
   __Poztmp.fld_fml('TCEN','BEFORE_DISPLAY',_ff);
   __Poztmp.fld_fml('TRABP','BEFORE_DISPLAY',_ff);
   __Poztmp.fld_fml('TWAR','BEFORE_DISPLAY',_ff)
?};

__Suma:=tab_tmp(1
   ,'LP'    ,'INTEGER'     ,'Lp.'
   ,'OPIS'  ,'STRING[27]'  ,'Opis'
   ,'KWOTA' ,'REAL'        ,'Kwota'
);
__Suma.LP:=1; __Suma.OPIS:='Wartość wg dokumentu'; __Suma.add();
__Suma.LP:=2; __Suma.OPIS:='Wartość wg naliczenia'; __Suma.add();
__Suma.LP:=3; __Suma.OPIS:='Kwota rabatu wg dokumentu'; __Suma.add();
__Suma.LP:=4; __Suma.OPIS:='Kwota rabatu wg naliczenia'; __Suma.add();
{? _brak_upr_cs
||
   _ff:="0";
   __Suma.fld_fml('KWOTA','BEFORE_DISPLAY',_ff)
?};

_Nag.cntx_psh(); __Poz.cntx_psh();
{? _Nag=OFE
||
   OFP.index('OFE_P'); OFP.prefix(_Nag.ref);
   exec('st_licz_wz','ceny','OFE')
|? _Nag=ZK_N
||
   ZK_P.index('TYPN'); ZK_P.prefix('A','Z',_Nag.ref,1);
   exec('st_licz_wz','ceny','ZK_N')
|? _Nag=FAKS
||
   FAP.index('FAP'); FAP.prefix(_Nag.ref);
   exec('st_licz_wz','ceny','FAKS')
|? _Nag=ND
||
   DK.index('DOKMAG'); DK.prefix(_Nag.ref);
   exec('st_licz_wz','ceny','ND')
?};

_loop:=__Poz.first;
{!
|? _loop
|!
   _ok:=1;
   {? __Poz.TAR_H
   ||
      TAR_H.cntx_psh();
      TAR_H.use(ref_name(__Poz.TAR_H));
      _ok:=__Poz.TAR_H().CZY_GRA<>'T';
      TAR_H.cntx_pop()
   ?};

   {? _ok
   ||
      {? __Poz=ZK_P & _g
      ||
         ZK_P.WAL:=ZK_N.WAL;
         ZK_P.KRS:=ZK_N.KRS;
         _walj:=exec('FindInSet','#table','WAL','WAL_SYM',TAZ.WAL().KOD,,"WAL.J",,,1);
         {? ZK_P.WAL<>INFO.NAROD & ZK_P.CWAL>0
         ||
            ZK_P.CENA:=ZK_P.CWAL*ZK_P.KRS/_walj $ZK_P.M().DOKL_S
         |? ZK_P.WAL<>INFO.NAROD & ZK_P.CWAL=0 & ZK_P.KRS>0
         ||
            ZK_P.CWAL:=ZK_P.CENA/ZK_P.KRS*_walj $ZK_P.M().DOKL_S;
            ZK_P.CENA:=ZK_P.CWAL*ZK_P.KRS/_walj $ZK_P.M().DOKL_S
         |? ZK_P.WAL=INFO.NAROD
         ||
            ZK_P.CWAL:=0
         ?};
         exec('war_pozz','zamsiw_poz')
      |? __Poz=OFP & _g
      || _narod:=exec('bl_nwal','ustawienia');
         OFP.WAL:=OFE.WAL;
         OFP.KRS:=OFE.KRS;
         _walj:=exec('FindInSet','#table','WAL','WAL_SYM',OFP.WAL().KOD,,"WAL.J",,,1);
         {? OFE.WAL=_narod
         || OFP.CWAL:=0
         |? OFE.WAL<>_narod & OFP.CWAL=0 & OFP.KRS>0
         || OFP.CWAL:=(OFP.C/OFP.KRS*_walj)$OFP.M().DOKL_S;
            OFP.C:=(OFP.CWAL*OFP.KRS/_walj)$OFP.M().DOKL_S
         |? OFP.WAL<>_narod
         || OFP.C:=(OFP.CWAL*OFP.KRS/_walj)$OFP.M().DOKL_S
         ?};
         exec('ae_ofpww','ceny_dok',1)
      ?};
      __Poztmp.POZ:=exec('dok_pozp','ceny_dok',__Poz);
      __Poztmp.KTM:=__Poz.M().KTM;
      __Poztmp.IL:=__Poztmp.TIL:=exec('dok_pozil','ceny_dok',__Poz);
      __Poztmp.JM:=exec('FindAndGet','#table','JM',$exec('dok_pozjm','ceny_dok',__Poz),,"JM.KOD",'');
      __Poztmp.TAR:=exec('kodnaz_tar','ceny',__Poz.TAR_H);
      __Poztmp.CEN:=__Poztmp.TCEN:=exec('dok_pozcen','ceny_dok',__Poz);
      __Poztmp.RABP:=__Poztmp.TRABP:={? exec('czyrabp','ceny',TAZ.RAB_TYP) || __Poz.RAB || __Poz.RABK ?};
      __Poztmp.WAR:=__Poztmp.TWAR:=exec('dok_pozwa','ceny_dok',__Poz);
      __Poztmp.AGREG:='brak';
      __Poztmp.REF:=$__Poz.ref;
      __Poztmp.add()
   ?};
   _loop:=__Poz.next
!};

:: agregowanie pozycji dokumentu
_Sel:=_Nag.sel_aget(); _Sel.REF:=#_Nag.ref; _Sel.add;
_Tabagr:=exec('dok_agr','ceny_dok',_Sel,_Nag,__Poz,1,_a,_d);

:: wyznaczenie cen
_loop:=__Poz.first();
{!
|? _loop
|!
   _mat:=__Poz.M;
   _sv:=#((__Poz.M().VAT().KOD*'%'-1)+__Poz.M().VAT().KOD)/100;
   _kh:=_Nag.KH;
   _grkh:=_Nag.KH().GRKH;
   _mgr:=__Poz.M().MGR;
   _mgrp:=__Poz.M().MGRP;
   _wal:=_Nag.WAL;
   _jm:=__Poz.M().J().KOD;
   _j2:=exec('FindAndGet','#table',JM,$exec('dok_pozjm','ceny_dok',__Poz),,"JM.KOD",'');
   _prz:=exec('dok_pozprz','ceny_dok',__Poz);
   exec('set_kalkjm','ceny_dok',__Poz);
   _kh_odb:={? _Nag=FAKS | _Nag=ND || _Nag.KH_ODB || _Nag.ODB ?};
   _pl:={? _Nag=ND || null || _Nag.PL ?};
   TAZ.IL:=exec('dok_pozil','ceny_dok',__Poz);
   {? var_pres('__Ceny')>100
   || exec('pob_ceny','ceny_mat',__tms,_mat,_oddz,_kh,_grkh,_mgr,_wal,_jm,_j2,_prz,_sd,_kh_odb,_pl,__Ceny,$__Poz.ref,'1',_sv,'brak')
   || __Ceny:=exec('pob_ceny','ceny_mat',__tms,_mat,_oddz,_kh,_grkh,_mgr,_wal,_jm,_j2,_prz,_sd,_kh_odb,_pl,null,$__Poz.ref,'1',_sv,'brak');
      _cenyndx:=__Ceny.ndx_tmp('',,'POZ',,,'TREE',,,'THEBEST',,1)
   ?};
:: agregacja wg indeksu
   _Tabagr.index(__tnxind); _Tabagr.prefix('2',$_Nag.ref(),$_mat,{? __Poz=OFP || $__Poz.M().J || $__Poz.JM ?});
   {? _Tabagr.first()
   || TAZ.IL:=_Tabagr.IL;
      exec('pob_ceny','ceny_mat',__tms,_mat,_oddz,_kh,_grkh,_mgr,_wal,_jm,_j2,_prz,_sd,_kh_odb,_pl,__Ceny,$__Poz.ref,'2',_sv,'wg indeksu')
   ?};
:: agregacja wg grupy
   _Tabagr.index(__tnxmgr); _Tabagr.prefix('3',$_Nag.ref(),$_mgr,{? __Poz=OFP || $__Poz.M().J || $__Poz.JM ?});
   {? _Tabagr.first()
   || TAZ.IL:=_Tabagr.IL;
      exec('pob_ceny','ceny_mat',__tms,_mat,_oddz,_kh,_grkh,_mgr,_wal,_jm,_j2,_prz,_sd,_kh_odb,_pl,__Ceny,$__Poz.ref,'3',_sv,'wg grupy')
   ?};
:: agregacja wg podgrupy
   _Tabagr.index(__tnxmgp); _Tabagr.prefix('4',$_Nag.ref(),$_mgrp,{? __Poz=OFP || $__Poz.M().J || $__Poz.JM ?});
   {? _Tabagr.first()
   || TAZ.IL:=_Tabagr.IL;
      exec('pob_ceny','ceny_mat',__tms,_mat,_oddz,_kh,_grkh,_mgr,_wal,_jm,_j2,_prz,_sd,_kh_odb,_pl,__Ceny,$__Poz.ref,'4',_sv,'wg podgrupy')
   ?};
:: usuniecie cennikow niezgodnych z jm
   __Ceny.cntx_psh();
   __Ceny.index(_cenyndx); __Ceny.prefix($__Poz.ref());
   _loop1:=__Ceny.first();
   {!
   |? _loop1
   |!
      _loop1:=
         {? __Ceny.MIARA=_j2
         || __Ceny.next()
         || _ref:=__Ceny.ref();
            __Ceny.cntx_psh();
            __Ceny.prefix($__Poz.ref(),__Ceny.ref());
            {? __Ceny.first() || {! |? __Ceny.del !} ?};
            __Ceny.cntx_pop();
            {? __Ceny.seek(_ref) || __Ceny.del() ?}
         ?}
   !};
   __Ceny.cntx_pop();
:: dopisanie ceny z pozycji
   {? _f & __Poztmp.find_key(exec('dok_pozp','ceny_dok',__Poz))
   ||
      exec('pob_ceny','ceny_mat',0,_mat,_oddz,_kh,_grkh,_mgr,_wal,_jm,_j2,_prz,_sd,_kh_odb,_pl,__Ceny,$__Poz.ref,'5',_sv
         ,'brak',__Poztmp.CEN,__Poztmp.RABP,__Poztmp.WAR,_f)
   ?};
:: analiza cennikow
   TAZ.IL:=exec('dok_pozil','ceny_dok',__Poz);
   exec('cen_ilwar','ceny',__Ceny,$__Poz.ref,_f);
   {? _ctrl & ~_change
   || __Ceny.cntx_psh();
      __Ceny.index(_cenyndx);
      __Ceny.prefix($__Poz.ref());
      {? __Ceny.first() & __Poztmp.find_key(exec('dok_pozp','ceny_dok',__Poz))
      || {!
         |? _change:=__Poztmp.CEN<>__Ceny.CEN;
            ~_change & __Ceny.next()
         !}
      ?};
      __Ceny.cntx_pop()
   ?};
   _loop:=__Poz.next
!};

:: wybor najlepszego cennika
__Ceny.cntx_psh();
__Ceny.index(_cenyndx); __Ceny.prefix();
_loop:=__Poz.first;
{!
|? _loop
|!
   {? __Ceny.find_key($__Poz.ref,null,'T') & __Poztmp.find_key(exec('dok_pozp','ceny_dok',__Poz))
   || exec('alter_cenrab','ceny_dok',_a,0)
   |? __Poztmp.find_key(exec('dok_pozp','ceny_dok',__Poz))
   || {? exec('tarh_jest_w_poz','ceny_dok',__Poztmp.REF)
      ||
         __Poztmp.TCEN:=0;
         __Poztmp.TRABP:=0;
         __Poztmp.TWAR:=0;
         __Poztmp.put()
      ?}
   ?};
   _loop:=__Poz.next
!};
__Ceny.cntx_pop();

:: wyliczenie wartosc po wybraniu poszczegolnych pozycji cennika
__Ceny.cntx_psh();
_loop:=__Ceny.first();
{!
|? _loop
|!
   {? __Ceny.OK='T' & __Poz.seek(__Ceny.POZ) & __Poztmp.find_key(exec('dok_pozp','ceny_dok',__Poz))
   || exec('alter_cenrab','ceny_dok',2,0);
      __Ceny.WAR_PO:=__Poztmp.TWAR;
      __Ceny.put()
   ?};
   _loop:=__Ceny.next()
!};
__Ceny.cntx_pop();

:: podsumowanie
exec('pozy_suma','ceny_dok');

__Ceny.prefix();
{? _a=0 & __Ceny.first()=0
|| _loop:=__Poztmp.first();
   {!
   |? _loop
   |!
      _wyn:=__Poztmp.TAR='';
      _loop:=_wyn & __Poztmp.next()
   !};
   FUN.info('Brak cenników dla pozycji dokumentu.'@)
|? _ctrl
|| _wyn:=_change>0
:: kontrola cenników na pozycjach
|| _ver:='';
:: __wer1 - pozycje dokumentu
   __wer1:=__Poztmp.mk_sel('Pozycje'@,,,'vnnwopqfashglad',1,,,,'U');
   __Poztmp.win_fld(__wer1,,'POZ',,,-5,,,'Pozycja'@);
   __Poztmp.win_fld(__wer1,,'KTM',,,20,,,'Indeks'@);
   __Poztmp.win_fld(__wer1,,'JM',,,5,,,'jm'@);
   __Poztmp.win_fld(__wer1,,'IL',,,5,,,'Ilość'@);
   __Poztmp.win_fld(__wer1,,'TAR',,,20,,,'Cennik'@);
   __Poztmp.win_fld(__wer1,,'CEN',,,-8,ST.DOKL_S,,{? TAZ.CB='T' || 'Cena z dokumentu brutto'@ || 'Cena z dokumentu netto'@ ?});
   __Poztmp.win_fld(__wer1,,'RABP',,,-8,2,,'Rabat z pozycji dokumentu'@);
   __Poztmp.win_fld(__wer1,,'WAR',,,-10,2,,{? TAZ.CB='T'
                                           || 'Wartość z dokumentu brutto'@
                                           || 'Wartość z dokumentu netto'@
                                           ?});
   __Poztmp.win_fld(__wer1,,'AGREG',,,11,,,'Agregacja'@);
   __Poztmp.win_fld(__wer1,,'TIL',,,5,,,'Ilość'@);
   __Poztmp.win_fld(__wer1,,'TCEN',,,-5,ST.DOKL_S,,{? TAZ.CB='T' || 'Naliczona cena brutto'@ || 'Naliczona cena netto'@ ?});
   __Poztmp.win_fld(__wer1,,'TRABP',,,-8,2,,'Naliczony rabat z pozycji'@);
   __Poztmp.win_fld(__wer1,,'TWAR',,,-10,2,,{? TAZ.CB='T'
                                            || 'Naliczona wartość brutto'@
                                            || 'Naliczona wartość netto'@
                                            ?});
   _fb:="exec('akc_cenrab','ceny_dok')";
   __Poztmp.win_act(__wer1,0,'Formuła','&Akceptuj'@@,,,_fb,,1,,,,'A');
   _fb:="exec('legenda','color','TAP#05')";
   __Poztmp.win_act(__wer1,0,'Formuła','&Legenda'@@,,,_fb,,,,,,'L');
   _fb:="exec('rekprzed','color','TAP#05')";
   __Poztmp.win_act(__wer1,0,'Rekord',,,,_fb);
   __Poztmp.win_btn(__wer1,'text='+'&Akceptuj'@+', panel=bottom, align=begin','menu:A',,,,,,'noempty');

:: __wer2 - ceny
   {? _brak_upr_cs
   ||
      _ff:="0";
      __Ceny.fld_fml('CEN_OD','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('CEN','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('CENB_OD','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('CENB','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('PRC','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('RABNET','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('RABBRT','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('WAR','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('WAR_DO','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('WARV','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('WARDOV','BEFORE_DISPLAY',_ff);
      __Ceny.fld_fml('WAR_PO','BEFORE_DISPLAY',_ff)
   ||
      __Ceny.fld_fml('PRC','BEFORE_DISPLAY',"exec('censlo_kolrabb','ceny')");
      __Ceny.fld_fml('RABNET','BEFORE_DISPLAY',"exec('censlo_kolrabb','ceny')");
      __Ceny.fld_fml('RABBRT','BEFORE_DISPLAY',"exec('censlo_kolrabb','ceny')");
      __Ceny.fld_fml('CEN_OD','BEFORE_DISPLAY',"exec('censlo_kolcenod','ceny')");
      __Ceny.fld_fml('CENB_OD','BEFORE_DISPLAY',"exec('censlo_kolcenod','ceny')")
   ?};

   __wer2:=__Ceny.mk_sel({? TAZ.SD='S'
                         || 'Ceny sprzedaży w'@
                         || 'Ceny dostaw w'@
                         ?}+' '+_walkod,'P',,'#ceny_matxcen',1,,,1);
   __Ceny.win_fld(__wer2,,'NAZ',,,30,,0,'Nazwa cennika'@);
   __Ceny.win_fld(__wer2,,'OP',,,20,,0,'Opis pozycji cennika'@);
   {? TAZ.CB='N'
   || _ver+='a';
      {? TAZ.SD='S'
      || __Ceny.win_fld(__wer2,,'CEN_OD',,,10,{? (';SG'*TAZ.SD)>1 || ST.DOKL_S || ST.DOKL_Z ?},0,'Cena netto od'@)
      ?};
      __Ceny.win_fld(__wer2,,'CEN',,,10,{? (';SG'*TAZ.SD)>1 || ST.DOKL_S || ST.DOKL_Z ?},0,'Cena netto'@)
   || _ver+='b';
      {? TAZ.SD='S'
      || __Ceny.win_fld(__wer2,,'CENB_OD',,,10,{? (';SG'*TAZ.SD)>1 || ST.DOKL_S || ST.DOKL_Z ?},0,'Cena brutto od'@)
      ?};
      __Ceny.win_fld(__wer2,,'CENB',,,10,{? (';SG'*TAZ.SD)>1 || ST.DOKL_S || ST.DOKL_Z ?},0,'Cena brutto'@)
   ?};
   {? exec('czyrabp','ceny',TAZ.RAB_TYP)
   || _ver+='a';
      __Ceny.win_fld(__wer2,,'PRC',,,5,2,0,'Rabat %%'@)
   ||
      {? TAZ.CB='N'
      || _ver+='b';
         __Ceny.win_fld(__wer2,,'RABNET',,,10,{? (';SG'*TAZ.SD)>1 || ST.DOKL_S || ST.DOKL_Z ?},0,'Rabat kwota netto'@)
      || _ver+='c';
         __Ceny.win_fld(__wer2,,'RABBRT',,,10,{? (';SG'*TAZ.SD)>1 || ST.DOKL_S || ST.DOKL_Z ?},0,'Rabat kwota brutto'@)
      ?}
   ?};
   __Ceny.win_fld(__wer2,,'MIARA',,,10,,0,'jm'@);
   {? TAZ.SD='S'
   || _ver+='a';
      __Ceny.win_fld(__wer2,,'IL',,,10,ST.DOKL,0,'Ilość od'@);
      __Ceny.win_fld(__wer2,,'IL_DO',,,10,ST.DOKL,0,'Ilość do'@);
      __Ceny.win_fld(__wer2,,'WARV',,,10,2,0,{? TAZ.CB='T' || 'Wartość brutto od'@ || 'Wartość netto od'@ ?});
      __Ceny.win_fld(__wer2,,'WARDOV',,,10,2,0,{? TAZ.CB='T' || 'Wartość brutto do'@ || 'Wartość netto do'@ ?})
   || _ver+='b'
   ?};
   __Ceny.win_fld(__wer2,,'WAR_PO',,,10,ST.DOKL,0,{? TAZ.CB='T'
                                                  || 'Wartość brutto po wybraniu cennika'@
                                                  || 'Wartość netto po wybraniu cennika'@
                                                  ?});
   __Ceny.win_fld(__wer2,,'AGREG',,,11,,0,'Agregacja'@);
   _fb:="
      {? __Ceny.OK='N'
      || FUN.info('Cennik niedostępny.'@)
      |? __Poz.seek(__Poztmp.REF)
      || exec('alter_cenrab','ceny_dok',1,1);
         exec('pozy_suma','ceny_dok');
         grp_disp(__Poztmp,__wer1);
         grp_disp(__Suma,__wer3)
      ?}
   ";
   __Ceny.win_act(__wer2,0,'Formuła','&Wybierz'@@,,,_fb,,1,,,,'W');
   _fb:="exec('legenda','color','TAP#02')";
   __Ceny.win_act(__wer2,0,'Formuła','&Legenda'@@,,,_fb,,,,,,'L');
   _fb:="exec('rekprzed','color','TAP#02')";
   __Ceny.win_act(__wer2,0,'Rekord',,,,_fb);

:: __wer3 - podsumowanie
   __wer3:=__Suma.mk_sel({? TAZ.CB='T' || 'Razem brutto w %1'@[_walkod] || 'Razem netto w %1'@[_walkod] ?},,,'#eqpeidkvnzalke',1,,,,'U');
   __Suma.win_fld(__wer3,,'OPIS',,,21,,,'Opis'@);
   __Suma.win_fld(__wer3,,'KWOTA',,,12,2,,'Kwota'@);
   _fb:="exec('legenda','color','TAP#06')";
   __Suma.win_act(__wer3,0,'Formuła','&Legenda'@@,,,_fb,,,,,,'L');
   _fb:="exec('rekprzed','color','TAP#06')";
   __Suma.win_act(__wer3,0,'Rekord',,,,_fb);

:: _wergr - okno grupowe
   _wergr:=__Poztmp.grp_make('Naliczenie cen i rabatów w ramach dokumentu %1'@[_Nag.SYM],,'#djfcvndaerk'+_ver);
   _fr:=" grp_disp(__Ceny,__wer2); grp_disp(__Suma,__wer3)";
   __Poztmp.grp_sel(_wergr,,__wer1,,_fr,,,15,,,,,'maximized_with_title');
   __Poztmp.grp_splt(_wergr,,'vertical','suma','0, 75%');
   __Poztmp.grp_sel(_wergr,__Suma,__wer3,,,,,15,,,,,'maximized_with_title');
   __Suma.win_sopt(__wer3,'select_record_checkbox=0');
   __Poztmp.grp_splt(_wergr,,'horizontal','ceny');
   _fb:="__Ceny.prefix(__Poztmp.REF); __Ceny.first()";
   __Poztmp.grp_sel(_wergr,__Ceny,__wer2,,,,,15,_fb,,,,'maximized_with_title');

   __Ceny.ndx_drop(_cenyndx);
   _cenyndx:=__Ceny.ndx_tmp('',,'POZ',,,'TREE',,,'P',,1,'THEBEST',,1);
   __Ceny.index(_cenyndx);
   __Poztmp.win_sel(_wergr);
   _wyn:={? __Poztmp.select || 1 || 0 ?};
   __Ceny.ndx_drop(_cenyndx)
?};

_Nag.cntx_pop(); __Poz.cntx_pop();
_Nag.get();
{? _e
|| {? _Nag=FAKS
   || exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
   || _Nag.r_unlock()
   ?}
?};
_czysc();
_wyn


\ceny_mat_dokchk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: weryfikacja dokumentu do przeliczenia wartosci
::   WE: _a - tabela
::   WY: -1 - przeliczenie pomijane
::       0 - dokument nie spelnia kryteriow,
::       1-wpp
::  OLD: \ceny_mat_dokchk/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;

_Tab:=_a;

{? _Tab=OFE
|| {? ~OFE.r_lock(1,1,1)
   || exec('add_kom','#message','Dokument obsługuje inny użytkownik. Przeliczenie niedostępne.'@,81,OFE.SYM);
      _wyn:=0
   ?};
   {? _wyn
   || _Ofe:=sql('select ZK_N.REFERENCE ref from @ZK_N where ZK_N.OFE =  :_a ',OFE.ref);
      {? _Ofe.first()
      || exec('add_kom','#message',
            'Do tej oferty utworzono już zamówienie. Przeliczenie niedostępne.'@,81,OFE.SYM);
         _wyn:=0
      ?}
   ?};
   {? _wyn & exec('FindInSet','#table','OFP','OFE',OFE.ref())=null
   || exec('add_kom','#message','Dokument nie posiada pozycji. Przeliczenie niedostępne.'@,81,OFE.SYM);
      _wyn:=-1
   ?}
|? _Tab=ZK_N
|| {? ~ZK_N.r_lock(1,1,1)
   || exec('add_kom','#message','Dokument obsługuje inny użytkownik. Przeliczenie niedostępne.'@,81,ZK_N.SYM);
      _wyn:=0
   ?};
   {? _wyn & ZK_N.AKC='T'
   || exec('add_kom','#message','Dokument zaakceptowany. Przeliczenie niedostępne.'@,81,ZK_N.SYM);
      _wyn:=0
   ?};
   {? _wyn & exec('FindInSet','#table','ZK_P','NAG',ZK_N.ref())=null
   || exec('add_kom','#message','Dokument nie posiada pozycji. Przeliczenie niedostępne.'@,81,ZK_N.SYM);
      _wyn:=-1
   ?}
|? _Tab=FAKS
|| _wyn:=exec('r_lock_one','#table',FAKS,FAKS.ref);

   {? ~_wyn
   || exec('add_kom','#message','Dokument obsługuje inny użytkownik. Przeliczenie niedostępne.'@,81,FAKS.SYM)
   ?};
   {? _wyn & FAKS.STAT_REJ='T'
   || exec('add_kom','#message','Dokument zaakceptowany. Przeliczenie niedostępne.'@,81,FAKS.SYM);
      _wyn:=0
   |? _wyn & FAKS.STAT_REJ='Z'
   || exec('add_kom','#message','Dokument zakończony. Przeliczenie niedostępne.'@,81,FAKS.SYM);
      _wyn:=0
   |? _wyn & FAKS.STAT_REJ='A'
   || exec('add_kom','#message','Dokument anulowany. Przeliczenie niedostępne.'@,81,FAKS.SYM);
      _wyn:=0
   ?};
   {? _wyn & (FAKS.T().KOR<>'N')
   || exec('add_kom','#message','Przeliczenie niedostępne dla tego typu dokumentu.'@,81,FAKS.SYM);
      _wyn:=0
   ?};
   {? _wyn & exec('FindInSet','#table','FAP','FAP',FAKS.ref())=null
   || exec('add_kom','#message','Dokument nie posiada pozycji. Przeliczenie niedostępne.'@,81,FAKS.SYM);
      _wyn:=-1
   ?}
|? _Tab=ND
|| {? ~ND.r_lock(1,1,1)
   || exec('add_kom','#message','Dokument obsługuje inny użytkownik. Przeliczenie niedostępne.'@,81,ND.SYM);
      _wyn:=0
   ?};
   {? _wyn & ND.Z='T'
   || exec('add_kom','#message','Dokument zaakceptowany. Przeliczenie niedostępne.'@,81,ND.SYM);
      _wyn:=0
   ?};
   {? _wyn & exec('FindInSet','#table','DK','DOKMAG',ND.ref())=null
   || exec('add_kom','#message','Dokument nie posiada pozycji. Przeliczenie niedostępne.'@,81,ND.SYM);
      _wyn:=-1
   ?}
?};

_wyn


\dok_pozil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca ilość z pozycji tabeli _a
::   WE: _a - tabela
::   WY: ilość
::  OLD: \dok_pozil/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? _Tab=OFP || OFP.IL
|? _Tab=ZK_P || ZK_P.IL2
|? _Tab=FAP || FAP.IL
|? _Tab=DK || DK.IL2
?}


\dok_pozp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca nr pozycji dokumentu
::   WE: _a - tabela
::   WY: waluta
::  OLD: \dok_pozp/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? _Tab=OFP || OFP.POZ
|? _Tab=ZK_P || ZK_P.POZ
|? _Tab=FAP || FAP.POZ
|? _Tab=DK || DK.P
?}


\dok_pozjm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca jm z pozycji tabeli _a
::   WE: _a - tabela
::   WY: jm
::  OLD: \dok_pozjm/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? _Tab=OFP || OFP.M().J
|? _Tab=ZK_P || ZK_P.J2
|? _Tab=FAP || FAP.JM
|? _Tab=DK || DK.J2
?}


\dok_pozsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca sv z pozycji tabeli _a
::   WE: _a - tabela
::   WY: sv
::  OLD: \dok_pozsv/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? _Tab=OFP || _sv:=OFP.SV().KOD; #((_sv*'%'-1)+_sv)/100
|? _Tab=ZK_P || _sv:=ZK_P.SV().KOD; #((_sv*'%'-1)+_sv)/100
|? _Tab=FAP || _sv:=FAP.SV().KOD; #((_sv*'%'-1)+_sv)/100
|? _Tab=DK || _sv:=DK.SV().KOD; #((_sv*'%'-1)+_sv)/100
|| 0
?}


\dok_pozwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca wartość z pozycji tabeli _a
::   WE: _a - tabela
::   WY: wartość
::  OLD: \dok_pozwa/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? _Tab=OFP
|| exec('st_licz_wz','ceny','OFE');
   {? TAZ.CZY_WAL
   || exec('obl_war','ceny',{? exec('czyrabp','ceny',TAZ.RAB_TYP) || OFP.RAB || OFP.RABK ?},OFP.CWAL
         ,exec('dok_pozil','ceny_dok',OFP))
   || {? TAZ.CB='T' || OFP.WB || OFP.WN ?}
   ?}
|? _Tab=ZK_P
|| exec('st_licz_wz','ceny','ZK_N');
   {? TAZ.CZY_WAL
   || {? TAZ.CB='T' || ZK_P.BRUTTOW  || ZK_P.NETTOW ?}
   || {? TAZ.CB='T' || ZK_P.BRUTTO || ZK_P.NETTO ?}
   ?}
|? _Tab=FAP
|| exec('st_licz_wz','ceny','FAKS');
   {? TAZ.CZY_WAL
   || {? TAZ.CB='T' || FAP.BWAL || FAP.WWAL ?}
   || {? TAZ.CB='T' || FAP.BWAL_P || FAP.WWAL_P ?}
   ?}
|? _Tab=DK
|| exec('st_licz_wz','ceny','ND');
   {? TAZ.CZY_WAL
   || exec('obl_war','ceny',{? exec('czyrabp','ceny',TAZ.RAB_TYP) || DK.RAB || DK.RABK ?},DK.CWAL
         ,exec('dok_pozil','ceny_dok',DK))
   || {? TAZ.CB='T' || DK.WB || DK.WN ?}
   ?}
|| 0
?}


\dok_pozcen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca cene z pozycji tabeli _a
::   WE: _a - tabela
::   WY: ilosc
::  OLD: \dok_pozcen/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? _Tab=OFP || {? TAZ.CZY_WAL || OFP.CWAL || OFP.C ?}
|? _Tab=ZK_P || {? TAZ.CZY_WAL || ZK_P.CWAL || ZK_P.CENA ?}
|? _Tab=FAP || {? TAZ.CZY_WAL || FAP.CWAL || FAP.CPR ?}
|? _Tab=DK || {? TAZ.CZY_WAL || DK.CWAL || {? TAZ.CB='T' || DK.CB || DK.CN ?} ?}
?}


\dok_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca ref dokumentu
::   WE: _a - tabela
::   WY: waluta
::  OLD: \dok_ref/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? _Tab=OFP || OFE.ref
|? _Tab=ZK_P || ZK_N.ref
|? _Tab=FAP || FAKS.ref
|? _Tab=DK || ND.ref
?}


\dok_agr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: agregowanie dokumentow
::   WE: _a - tabela z lista dokumentow
::       _b - tabela naglowka
::       _c - tabela pozycji
::       _d - 0-pomin gratisy, 1-wpp
::       _e - 0-pozycje dokumentu i agregowanie, 1-tylko pozycje
::       _f - 0-podczytac naglowek, 1-nie podczytywac naglowka
::   WY:
::  OLD: \dok_agr/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_f')<>type_of(1) || _f:=0 ?};

_Sel:=_a;
_Nag:=_b;
_Poz:=_c;
_czy_gra:=_d;

_Tabagr:=tab_tmp(4
   ,'TYP'      ,'STRING[1]'   ,'1-poz, 2-indeks, 3-grupa, 4-podgrupa, 5-gratis'
   ,'DOK'      ,'STRING[16]'  ,'Dokument - $_Tab.ref'
   ,'DOK_SYM'  ,'STRING[20]'  ,'Symbol dokumentu'
   ,'POZ'      ,'INTEGER'     ,'Pozycja'
   ,'INDEKS'   ,'STRING[16]'  ,'Indeks - $M.ref'
   ,'MGR'      ,'STRING[16]'  ,'$MGR.ref'
   ,'MGRP'     ,'STRING[16]'  ,'$MGRP.ref'
   ,'GRATIS'   ,'STRING[16]'  ,'Gratis - $M.ref'
   ,'IL'       ,'REAL'        ,'Ilość'
   ,'GRA_IL'   ,'REAL'        ,'Gratis - ilość'
   ,'JM'       ,'STRING[16]'  ,'$JM.ref'
   ,'JM_KOD'   ,'STRING[8]'   ,'JM.KOD - ilość'
   ,'JM_G_KOD' ,'STRING[8]'   ,'JM.KOD - ilość gratis'
   ,'WAR'      ,'REAL'        ,'Wartość'
   ,'GRA_CEN'  ,'REAL'        ,'Gratis - cena'
   ,'WAL'      ,'STRING[16]'  ,'Waluta'
   ,'WAL_KOD'  ,'STRING[8]'   ,'Waluta'
   ,'OPIS'     ,'STRING[50]'  ,'Opis'
   ,'IND'      ,'STRING[50]'  ,'Indeks'
   ,'NAZ'      ,'STRING[100]' ,'Nazwa'
   ,'MGR_KOD'  ,'STRING[8]'   ,'Grupa nazwa'
   ,'MGRP_KOD' ,'STRING[8]'   ,'Podgrupa nazwa'
   ,'TAP'      ,'STRING[16]'  ,'Reference'
   ,'OK'       ,'INTEGER'     ,'Ok'
   ,'P'        ,'REAL'        ,'Priorytet cennika'
   ,'PP'       ,'INTEGER'     ,'Priorytet T/U/M/G');

__tnxind:=_Tabagr.ndx_tmp(,,'TYP',,,'DOK',,,'INDEKS',,,'JM',,);
__tnxmgr:=_Tabagr.ndx_tmp(,,'TYP',,,'DOK',,,'MGR',,,'JM',,);
__tnxmgp:=_Tabagr.ndx_tmp(,,'TYP',,,'DOK',,,'MGRP',,,'JM',,);

{? _Sel.first
||
   {!
   |?
      {? _f | _Nag.seek(_Sel.REF,)
      ||
         {? _Poz=OFP
         ||
            OFE.prefix;
            OFP.index('OFE_P'); OFP.prefix(_Nag.ref)
         |? _Poz=ZK_P
         ||
            ZK_N.prefix;
            ZK_P.index('TYPN'); ZK_P.prefix('A','Z',_Nag.ref,1)
         |? _Poz=FAP
         ||
            FAKS.prefix;
            FAP.index('FAP'); FAP.prefix(_Nag.ref)
         |? _Poz=DK
         ||
            ND.prefix;
            DK.index('DOKMAG'); DK.prefix(_Nag.ref)
         ?};

         {? _Poz.first
         ||
            TAR_H.cntx_psh(); TAP.cntx_psh(); TAR.cntx_psh();
            {!
            |?
               {? _Poz.TAR_H
               || TAR_H.use(ref_name(_Poz.TAR_H));
                  {? _Poz.TAR_H().TAP || TAP.use(ref_name(_Poz.TAR_H().TAP)) ?}
               ?};
               {? _czy_gra | _czy_gra=0 & _Poz.TAR_H().TAP().TAR().SD<>'G'
               ||
                  _Tabagr.DOK:=$exec('dok_ref','ceny_dok',_Poz);
                  _Tabagr.DOK_SYM:=exec('zwr_war','ceny_dok',_Tabagr.DOK,'SYM');
                  _Tabagr.POZ:=exec('dok_pozp','ceny_dok',_Poz);
                  _Tabagr.TYP:='1';
                  _Tabagr.INDEKS:=$_Poz.M;
                  _Tabagr.IND:=exec('FindAndGet','#table',M,_Tabagr.INDEKS,,"M.KTM",'');
                  _Tabagr.NAZ:=exec('FindAndGet','#table',M,_Tabagr.INDEKS,,"M.N",'');
                  _Tabagr.MGR:=$_Poz.M().MGR;
                  _Tabagr.MGR_KOD:=exec('FindAndGet','#table',MGR,_Tabagr.MGR,,"MGR.KOD",'');
                  _Tabagr.MGRP:=$_Poz.M().MGRP;
                  _Tabagr.MGRP_KOD:=exec('FindAndGet','#table',MGRP,_Tabagr.MGRP,,"MGRP.KOD",'');
                  _Tabagr.IL:=exec('dok_pozil','ceny_dok',_Poz);
                  _Tabagr.JM:=$exec('dok_pozjm','ceny_dok',_Poz);
                  _Tabagr.JM_KOD:=exec('FindAndGet','#table',JM,_Tabagr.JM,,"JM.KOD",'');
                  _Tabagr.WAR:=exec('dok_pozwa','ceny_dok',_Poz);
                  _Tabagr.WAL:=$exec('dok_pozwl','ceny_dok',_Poz);
                  _Tabagr.WAL_KOD:=exec('dok_pozwl','ceny_dok',_Poz,0);
                  _Tabagr.add;
                  {? _e=0
                  ||
                     _Tabagr.index(__tnxind);
                     {? _Tabagr.find_key('2',$_Nag.ref,$_Poz.M,$exec('dok_pozjm','ceny_dok',_Poz))
                     || _Tabagr.IL+=exec('dok_pozil','ceny_dok',_Poz);
                        _Tabagr.WAR+=exec('dok_pozwa','ceny_dok',_Poz);
                        _Tabagr.put
                     || _Tabagr.blank;
                        _Tabagr.DOK:=$exec('dok_ref','ceny_dok',_Poz);
                        _Tabagr.DOK_SYM:=exec('zwr_war','ceny_dok',_Tabagr.DOK,'SYM');
                        _Tabagr.TYP:='2';
                        _Tabagr.INDEKS:=$_Poz.M;
                        _Tabagr.IL:=exec('dok_pozil','ceny_dok',_Poz);
                        _Tabagr.JM:=$exec('dok_pozjm','ceny_dok',_Poz);
                        _Tabagr.JM_KOD:=exec('FindAndGet','#table',JM,_Tabagr.JM,,"JM.KOD",'');
                        _Tabagr.WAR:=exec('dok_pozwa','ceny_dok',_Poz);
                        _Tabagr.WAL:=$exec('dok_pozwl','ceny_dok',_Poz);
                        _Tabagr.WAL_KOD:=exec('dok_pozwl','ceny_dok',_Poz,0);
                        _Tabagr.add
                     ?};
                     _Tabagr.index(__tnxmgr);
                     {? _Tabagr.find_key('3',$_Nag.ref,$_Poz.M().MGR,$exec('dok_pozjm','ceny_dok',_Poz))
                     || _Tabagr.IL+=exec('dok_pozil','ceny_dok',_Poz);
                        _Tabagr.WAR+=exec('dok_pozwa','ceny_dok',_Poz);
                        _Tabagr.put
                     || _Tabagr.blank;
                        _Tabagr.DOK:=$exec('dok_ref','ceny_dok',_Poz);
                        _Tabagr.DOK_SYM:=exec('zwr_war','ceny_dok',_Tabagr.DOK,'SYM');
                        _Tabagr.TYP:='3';
                        _Tabagr.MGR:=$_Poz.M().MGR;
                        _Tabagr.IL:=exec('dok_pozil','ceny_dok',_Poz);
                        _Tabagr.JM:=$exec('dok_pozjm','ceny_dok',_Poz);
                        _Tabagr.JM_KOD:=exec('FindAndGet','#table','JM',_Tabagr.JM,,"JM.KOD",'');
                        _Tabagr.WAR:=exec('dok_pozwa','ceny_dok',_Poz);
                        _Tabagr.WAL:=$exec('dok_pozwl','ceny_dok',_Poz);
                        _Tabagr.WAL_KOD:=exec('dok_pozwl','ceny_dok',_Poz,0);
                        _Tabagr.add
                     ?};
                     {? _Poz.M().MGRP
                     ||
                        _Tabagr.index(__tnxmgp);
                        {? _Tabagr.find_key('4',$_Nag.ref,$_Poz.M().MGRP,$exec('dok_pozjm','ceny_dok',_Poz))
                        || _Tabagr.IL+=exec('dok_pozil','ceny_dok',_Poz);
                           _Tabagr.WAR+=exec('dok_pozwa','ceny_dok',_Poz);
                           _Tabagr.put
                        || _Tabagr.blank;
                           _Tabagr.DOK:=$exec('dok_ref','ceny_dok',_Poz);
                           _Tabagr.DOK_SYM:=exec('zwr_war','ceny_dok',_Tabagr.DOK,'SYM');
                           _Tabagr.TYP:='4';
                           _Tabagr.MGR:=$_Poz.M().MGR;
                           _Tabagr.MGRP:=$_Poz.M().MGRP;
                           _Tabagr.IL:=exec('dok_pozil','ceny_dok',_Poz);
                           _Tabagr.JM:=$exec('dok_pozjm','ceny_dok',_Poz);
                           _Tabagr.JM_KOD:=exec('FindAndGet','#table','JM',_Tabagr.JM,,"JM.KOD",'');
                           _Tabagr.WAR:=exec('dok_pozwa','ceny_dok',_Poz);
                           _Tabagr.WAL:=$exec('dok_pozwl','ceny_dok',_Poz);
                           _Tabagr.WAL_KOD:=exec('dok_pozwl','ceny_dok',_Poz,0);
                           _Tabagr.add
                        ?}
                     ?}
                  ?}
               ?};
               _Poz.next
            !};
            TAR_H.cntx_pop(); TAP.cntx_pop(); TAR.cntx_pop()
         ?}
      ?};
      _Sel.next
   !}
?};

_Tabagr


\zwr_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca wartosc pola _b rekordu _a
::   WE: _a - $reference rekordu
::       _b - akronim pola
::   WY: wartosc pola
::  OLD: \zwr_war/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? 6+_Tab='oferty' || exec('FindAndGet','#table',OFE,_a,,$_b)
|? 8+_Tab='ofer_poz' || exec('FindAndGet','#table','OFP',_a,,$_b)
|? 5+_Tab='zknag' || exec('FindAndGet','#table','ZK_N',_a,,$_b)
|? 5+_Tab='zkpoz' || exec('FindAndGet','#table','ZK_P',_a,,$_b)
|? 5+_Tab='faktu' || exec('FindAndGet','#table','FAKS',_a,,$_b)
|? 5+_Tab='fakpo' || exec('FindAndGet','#table','FAP',_a,,$_b)
|? 5+_Tab='nagdo' || exec('FindAndGet','#table','ND',_a,,$_b)
|? 5+_Tab='dokma' || exec('FindAndGet','#table','DK',_a,,$_b)
?}


\dok_pozwl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca walute dokumentu
::   WE: _a    - tabela
::       [_b]  - 0-zwracaj SLO.KOD, 1-zwracaj SLO.ref()
::   WY: waluta
::  OLD: \dok_pozwl/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of(1) || _b:=1 ?};

_Tab:=_a;

SLO.cntx_psh();
_wyn:=
   {? _Tab=OFP || {? _b || OFE.WAL || OFE.WAL().KOD ?}
   |? _Tab=ZK_P || {? _b || ZK_N.WAL || ZK_N.WAL().KOD ?}
   |? _Tab=FAP || {? _b || FAKS.WAL || FAKS.WAL().KOD ?}
   |? _Tab=DK || {? _b || ND.WAL || ND.WAL().KOD ?}
   ?};
SLO.cntx_pop();
_wyn


\dok_pozprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca przelicznik z pozycji tabeli _a
::   WE: _a - tabela
::   WY: przelicznik
::  OLD: \dok_pozprz/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? _Tab=OFP || 1
|? _Tab=ZK_P || ZK_P.WS2
|? _Tab=FAP || {? FAKS.WHERE='G' || {? FAP.WS2<>0 || 1/FAP.WS2 || 1 ?} || FAP.WS2 ?}
|? _Tab=DK || DK.WS2
?}


\set_kalkjm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: ustawienie KALK.JM, KALK.J2, KALK.WS2
::   WE: _a - tabela
::  OLD: \set_kalkjm/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

{? _Tab=OFP
||
   KALK.JM:=KALK.J2:=OFP.M().J; KALK.WS2:=1
|? _Tab=ZK_P
||
   KALK.JM:=ZK_P.JM; KALK.J2:=ZK_P.J2; KALK.WS2:=ZK_P.WS2
|? _Tab=FAP
||
   {? FAP.T2<>'G'
   || KALK.JM:=KALK.J2:=FAP.JM; KALK.WS2:=1
   || KALK.JM:=FAP.J2; KALK.J2:=FAP.JM; KALK.WS2:=FAP.WS2
   ?}
|? _Tab=DK
||
   KALK.JM:=DK.JM; KALK.J2:=DK.J2; KALK.WS2:=DK.WS2
?}


\alter_cenrab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zapisuje alternatywną cene i rabat w tymczasowej tabeli pozycji
::   WE: _a - 0 - aktualizowac jesli lepsze warunki, 1 - aktualizowac zawsze, 2 - nie aktualizowac
::       _b - 0 - bez edycji ceny, 1 - z edycja ceny
::  OLD: \alter_cenrab/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_tcen:={? TAZ.CB='T' || __Ceny.CENB || __Ceny.CEN ?};
_trabp:={? exec('czyrabp','ceny',TAZ.RAB_TYP) || __Ceny.PRC || {? TAZ.CB='T' || __Ceny.RABBRT || __Ceny.RABNET ?} ?};

_edit:=1;
{? _b & __Ceny.CEN_ZAKR='T'
||
   undefine();
   define('CEN',_tcen,'Cena',,,,2);
   {? def_edit("
         {? {? TAZ.CB='T'
            || __Ceny.CENB_OD>DEFINE.CEN | DEFINE.CEN>__Ceny.CENB
            || __Ceny.CEN_OD>DEFINE.CEN | DEFINE.CEN>__Ceny.CEN
            ?}
         || FUN.info('Cena poza zakresem.'@)
         || ''
         ?}")
   || _tcen:=DEFINE.CEN$2
   || _edit:=0
   ?};
   undefine()
|? _b & {? TAZ.CB='T' || __Ceny.CENB || __Ceny.CEN ?}=0
||
   undefine();
   define('CEN',_tcen,'Cena',,,,2);
   {? def_edit("
         {? DEFINE.CEN<=0
         || FUN.info('Cena musi być większa od zera.'@)
         || ''
         ?}")
   || _tcen:=DEFINE.CEN$2
   || _edit:=0
   ?};
   undefine()
?};

{? _edit
||
   __Poztmp.TCEN:=_tcen;
   __Poztmp.TRABP:=_trabp;
   exec('poz_obl','ceny_dok');
   __Poztmp.TWAR:=exec('dok_pozwa','ceny_dok',__Poz);
   __Poztmp.AGREG:=__Ceny.AGREG;
   __Poztmp.CENY:=$__Ceny.ref();
   {? _a<>2 & (_a | __Poztmp.CEN=0 | __Poztmp.WAR>__Poztmp.TWAR) || __Poztmp.put ?}
?}


\poz_obl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: oblicza pozycje wg alternatywnej ceny
::  OLD: \poz_obl/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
{? __Poz=OFP
|| {? TAZ.CZY_WAL || OFP.CWAL:=__Poztmp.TCEN || OFP.C:=__Poztmp.TCEN ?};
   {? exec('czyrabp','ceny',TAZ.RAB_TYP) || OFP.RAB:=__Poztmp.TRABP || OFP.RABK:=__Poztmp.TRABP ?}
|? __Poz=ZK_P
|| {? TAZ.CZY_WAL || ZK_P.CWAL:=__Poztmp.TCEN || ZK_P.CENA:=__Poztmp.TCEN ?};
   {? exec('czyrabp','ceny',TAZ.RAB_TYP) || ZK_P.RAB:=__Poztmp.TRABP || ZK_P.RABK:=__Poztmp.TRABP ?}
|? __Poz=FAP
|| {? TAZ.CZY_WAL || FAP.CWAL:=__Poztmp.TCEN || FAP.CPR:=__Poztmp.TCEN ?};
   {? exec('czyrabp','ceny',TAZ.RAB_TYP) || FAP.RAB:=__Poztmp.TRABP || FAP.RABK:=__Poztmp.TRABP ?}
|? __Poz=DK
|| {? TAZ.CZY_WAL || DK.CWAL:=__Poztmp.TCEN || {? TAZ.CB='T' || DK.CB:=__Poztmp.TCEN || DK.CN:=__Poztmp.TCEN ?} ?};
   {? exec('czyrabp','ceny',TAZ.RAB_TYP) || DK.RAB:=__Poztmp.TRABP || ZK_P.RABK:=__Poztmp.TRABP ?}
?};
exec('licz','ceny',__Poz)


\tarh_jest_w_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: sprawdza czy cennik zapisany w pozycji
::   WE: _a - $ref pozycji
::   WY: 0-brak cennika w pozycji, 1-jest cennik w pozycji
::  OLD: \tarh_jest_w_poz/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

_Tab:=
   {? 8+_a='ofer_poz' || OFP
   |? 5+_a='zkpoz' || ZK_P
   |? 5+_a='fakpo' || FAP
   |? 5+_a='dokma' || DK
   || return(1)
   ?};

_Tab.cntx_psh();
_Tab.use(8+_a);
_Tab.prefix();
{? _Tab.seek(_a) || _wyn:=_Tab.TAR_H<>null ?};
_Tab.cntx_pop();

_wyn


\pozy_suma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: oblicza podsumowanie pozycji przed i po zmianie cennikow
::  OLD: \pozy_suma/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_kw1:=_kw2:=_kwrab1:=_kwrab2:=0;

__Poztmp.cntx_psh();
_loop:=__Poztmp.first();
{!
|? _loop
|!
   _kw1+=__Poztmp.WAR;
   _kw2+=__Poztmp.TWAR;
   _kwrab1+=((__Poztmp.IL*__Poztmp.CEN)$2-__Poztmp.WAR);
   _kwrab2+=((__Poztmp.IL*__Poztmp.TCEN)$2-__Poztmp.TWAR);
   _loop:=__Poztmp.next()
!};
__Poztmp.cntx_pop();

{? __Suma.first()
|| __Suma.KWOTA:=_kw1;
   {? __Suma.put() & __Suma.next()
   || __Suma.KWOTA:=_kw2;
      {? __Suma.put() & __Suma.next()
      || __Suma.KWOTA:=_kwrab1;
         {? __Suma.put() & __Suma.next()
         || __Suma.KWOTA:=_kwrab2;
            __Suma.put()
         ?}
      ?}
   ?}
?};
__Suma.first()


\akc_cenrab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zapisuje nowa cene i rabat w pozycjach dokumentu
::  OLD: \akc_cenrab/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_chk:=1;
{? exec('get','#params',2131,2,OPERATOR.USER)='T'
||
   _loop:=__Poztmp.first();
   {!
   |? _loop
   |!
      _chk:=__Poztmp.CENY<>'';
      _loop:=_chk & __Poztmp.next()
   !}
?};

{? _chk=0 || FUN.info('Wymagany cennik w pozycji nr %1.'@[$__Poztmp.POZ]); return() ?};

_msg:='Zapisać nowe ceny i rabaty w dokumencie?'@;
{? FAKS.SYMF<>''
|| _msg:=_msg+'\n'+'Uwaga! Jest to dokument do paragonu.'@
?};
{? ~FUN.ask(_msg) || return() ?};

:: put na naglowku dokumentu
__cendok.NAGPUT();

_put:=0;
__Ceny.prefix();

_loop:=__Poztmp.first();
{!
|? _loop
|!
   {? __Poz.seek(__Poztmp.REF)
   ||
      exec('poz_obl','ceny_dok');
      {? __Ceny.seek(__Poztmp.CENY)
      ||
         _tap:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,,null());
         {? _tap & __Ceny.NAZ<>'Z dokumentu'
         || _tapb:=exec('FindAndGet','#table',TAP,__Ceny.TAPB,,,null());
            _jm:=exec('FindInSet','#table','JM','KOD',__Ceny.MIARA,__Ceny.MIARA);
            _tap_il:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.IL",0);
            _tap_ild:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.IL_DO");
            _tap_war:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.WAR",0);
            _tap_wad:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.WAR_DO",0);
            _tap_od:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.OD",date(0,0,0));
            _tap_do:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.DO",date(0,0,0));
            _tap_tod:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.T_OD",time(0,0,0));
            _tap_tdo:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.T_DO",time(0,0,0));
            _tap_pl:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.PL",null());
            _tap_pro:=exec('FindAndGet','#table',TAP,__Ceny.TAP,,"TAP.PROMO",null());
            _rab:={? exec('czyrabp','ceny',TAZ.RAB_TYP)
                  || __Ceny.PRC
                  || {? TAZ.CB='T'
                     || __Ceny.RABBRT
                     || __Ceny.RABNET
                     ?}
                  ?};
            __Poz.TAR_H:=exec('tar_h','ceny_dok','DK',_tap,_tapb,__cendok.NAGWAL,_jm,_tap_il,_tap_war,_tap_pl
               ,__Ceny.CEN_OD,__Ceny.CEN,_rab,__Ceny.RAB_B,_tap_pro,_tap_od,_tap_do,_tap_tod,_tap_tdo,_tap_ild,_tap_wad
               ,0,0);
            __Poz.PROMO:=_tap_pro;
            __Poz.TAR_TMS:=__tms
         || exec('clr_promo_tap','ceny',__Poz)
         ?}
      |? +__Poztmp.TAR=0
      ||
         exec('clr_promo_tap','ceny',__Poz)
      ?};
      _put+=__Poz.put
   ?};
   _loop:=__Poztmp.next()
!};
{? _put
||
   exec('del_gradok','ceny_dok',$__cendok.NAGREF);
   exec('obl_dokwar','ceny_dok',$__cendok.NAGREF)
?};

sel_exit()


\tar_h
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zapis uzycia cennika
::  OLD: \tar_h/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
TAR_H.cntx_psh;
{? _a='ZK_P' || TAR_H.use('taryh'+ZK_N.ODDZ+($(ZK_N.DP~1)+2)) ?};
TAR_H.index('HIS'); TAR_H.prefix;
_wyn:=
   {? TAR_H.find_key(_b,_c,_d,_e,_f,_g,_h,_i,_j,_k,_l,_m,_n,_o,_p,_q,_r,_s,_t,_u)
   ||
      TAR_H.ref
   ||
      TAR_H.TAP:=_b;
      TAR_H.TAPB:=_c;
      TAR_H.WAL:=_d;
      TAR_H.JM:=_e;
      TAR_H.IL:=_f;
      TAR_H.WAR:=_g;
      TAR_H.PL:=_h;
      TAR_H.CEN_OD:=_i;
      TAR_H.CEN:=_j;
      TAR_H.RAB:=_k;
      TAR_H.RAB_B:=_l;
      TAR_H.PROMO:=_m;
      TAR_H.OD:=_n;
      TAR_H.DO:=_o;
      TAR_H.T_OD:=_p;
      TAR_H.T_DO:=_q;
      TAR_H.IL_DO:=_r;
      TAR_H.WAR_DO:=_s;
      TAR_H.GRA_IL:=_t;
      TAR_H.GRA_CEN:=_u;
      TAP.cntx_psh();
      TAP.use(ref_name(TAR_H.TAP));
      TAP.prefix();
      {? TAP.seek(TAR_H.TAP)
      ||
         TAR_H.CZY_GRA:={? TAP.TAR().SD='G' || 'T' || 'N' ?}
      ?};
      TAP.cntx_pop();
      {? TAR_H.add || TAR_H.ref ?}
   ?};
TAR_H.cntx_pop;
_wyn


\del_gradok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: usuwa gratisy z dokumentu
::  OLD: \del_gradok/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_dok:=_a;

{? 6+_dok='oferty'
||
   OFE.cntx_psh;
   OFE.prefix;
   {? OFE.seek(_dok)
   ||
      OFP.index('OFE_P');
      OFP.prefix(OFE.ref);
      {? OFP.last || {! |? exec('del_grapoz','ceny_dok',OFP) !} ?}
   ?};
   OFE.cntx_pop
|? 5+_dok='zknag'
||
   exec('utw_zk_tymc','zamsiw_wspolne');
   ZK_N.cntx_psh;
   ZK_N.prefix;
   {? ZK_N.seek(_dok)
   ||
      TAR_H.use('taryh'+ZK_N.ODDZ+($ZK_N.R+2));
      ZK_P.index('TYPN');
      ZK_P.prefix('A','Z',ZK_N.ref,1);
      {? ZK_P.last || {! |? exec('del_grapoz','ceny_dok',ZK_P) !} ?}
   ?};
   ZK_N.cntx_pop
|? 5+_dok='faktu'
||
   FAKS.cntx_psh;
   FAKS.prefix;
   {? FAKS.seek(_dok)
   ||
      FAP.index('FAP');
      FAP.prefix(FAKS.ref);
      {? FAP.last || {! |? exec('del_grapoz','ceny_dok',FAP) !} ?}
   ?};
   FAKS.cntx_pop
|? 5+_dok='nagdo'
||
   ND.cntx_psh;
   ND.prefix;
   {? ND.seek(_dok)
   ||
      DK.index('DOKMAG');
      DK.prefix(ND.ref);
      {? DK.last || {! |? exec('del_grapoz','ceny_dok',DK) !} ?}
   ?};
   ND.cntx_pop
?}


\del_grapoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: usuwa pozycje typu gratis z dokumentu
::   WE: _a - tabela
::   WY: waluta
::  OLD: \del_grapoz/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;

TAR_H.cntx_psh; TAP.cntx_psh; TAR.cntx_psh;
{? _Tab.TAR_H || TAR_H.use(ref_name(_Tab.TAR_H)) ?};
_wyn:=
   {? _Tab=OFP
   ||
      {? OFP.TAR_H().TAP || TAP.use(ref_name(TAR_H.TAP)) ?};
      {? OFP.TAR_H().TAP().TAR().SD='G'
      || _prev:=OFP.del;
         OFP.cntx_psh();
         OFP.index('OFE_P');
         OFP.prefix(OFE.ref);
         exec('ReNumAfterDel','#table','OFP','POZ');
         OFP.cntx_pop();
         _prev
      || OFP.prev
      ?}
   |? _Tab=ZK_P
   ||
      {? ZK_P.TAR_H().TAP || TAP.use(ref_name(TAR_H.TAP)) ?};
      {? ZK_P.TAR_H().TAP().TAR().SD='G'
      || BEER.ZK_N:=ZK_P.N;
         exec('del_pozy','zamsiw_poz',0)=''
      || ZK_P.prev
      ?}
   |? _Tab=FAP
   ||
      {? FAP.TAR_H().TAP || TAP.use(ref_name(TAR_H.TAP)) ?};
      {? FAP.TAR_H().TAP().TAR().SD='G'
      || {? _prev:=exec('fap2del','faktury_poz',0)
         ||
            FAP.cntx_psh;
            exec('ReNumAfterDel','#table','FAP','POZ');
            FAP.cntx_pop
         ?};
          exec('sumfak','faktury_wspolne');
         _prev
      || FAP.prev
      ?}
   |? _Tab=DK
   ||
      {? DK.TAR_H().TAP || TAP.use(ref_name(TAR_H.TAP)) ?};
      {? DK.TAR_H().TAP().TAR().SD='G'
      || exec('usun_dk','magdok_poz',1);
         DK.get
      || DK.prev
      ?}
   ?};
TAR_H.cntx_pop; TAP.cntx_pop; TAR.cntx_pop;
_wyn


\obl_dokwar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: oblicza wartosc dokumentu
::   WE: _a - ref sql dokumentu
::  OLD: \obl_dokwar/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_dok:=_a;

{? 6+_dok='oferty'
|| SUM.SUM:=exec('kosztofe','lsp');
   exec('sumofe','lsp',SUM.SUM);
   OFE.put
|? 5+_dok='zknag'
|| ZK_N.cntx_psh();
   ZK_N.prefix();
   {? ZK_N.seek(_dok)
   || exec('obl_warz','zamsiw_nag',ZK_P.N);
      exec('odt_zk_tymc','zamsiw_wspolne');
      _gratisy:='N';
      ZK_P.cntx_psh();
      ZK_P.index('NAG');
      ZK_P.prefix(ZK_N.ref());
      _loop:=ZK_P.first();
      {!
      |? _loop
      |!
         TAR_H.cntx_psh();
         {? ZK_P.TAR_H || TAR_H.use(ref_name(ZK_P.TAR_H)) ?};
         {? _gratisy='N' & ZK_P.TAR_H().CZY_GRA='T' || _gratisy:='T' ?};
         TAR_H.cntx_pop();
         _loop:=ZK_P.next() & _gratisy='N'
      !};
      ZK_P.cntx_pop();
      ZK_N.GRATISY:=_gratisy;
      ZK_N.put()
   ?};
   ZK_N.cntx_pop()
|? 5+_dok='faktu'
|| FAKS.cntx_psh;
   FAKS.prefix;
   {? FAKS.seek(_dok)
   || exec('sumfak','faktury_wspolne');
      exec('plat_przel','faktury_plat',FAKS.ref());
      FAKS.put;
      _gratisy:='N';
      FAP.cntx_psh();
      FAP.index('FAP');
      FAP.prefix(FAKS.ref());
      _loop:=FAP.first();
      {!
      |? _loop
      |!
         TAR_H.cntx_psh();
         {? FAP.TAR_H || TAR_H.use(ref_name(FAP.TAR_H)) ?};
         {? _gratisy='N' & FAP.TAR_H().CZY_GRA='T' || _gratisy:='T' ?};
         TAR_H.cntx_pop();
         _loop:=FAP.next() & _gratisy='N'
      !};
      FAP.cntx_pop();
      FAKS.GRATISY:=_gratisy;
      FAKS.put()
   ?};
   FAKS.cntx_pop
|? 5+_dok='nagdo'
|| _gratisy:='N';
   ND.cntx_psh();
   ND.prefix();
   {? ND.seek(_dok)
   || DK.cntx_psh();
      DK.index('DOKMAG');
      DK.prefix(ND.ref());
      _loop:=DK.first();
      {!
      |? _loop
      |!
         TAR_H.cntx_psh();
         {? DK.TAR_H || TAR_H.use(ref_name(DK.TAR_H)) ?};
         {? _gratisy='N' & DK.TAR_H().CZY_GRA='T' || _gratisy:='T' ?};
         TAR_H.cntx_pop();
         _loop:=DK.next() & _gratisy='N'
      !};
      DK.cntx_pop();
      ND.GRATISY:=_gratisy;
      ND.put
   ?};
   ND.cntx_pop()
?}


\tar_h_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: kopiuje rekord _a do maski _b jesli (ref_name(_a)+3)<>_b
::   WE: _a - TAR_H.ref
::       _b - maska 'ORR' gdzie O-oddzial RR-rok
::   WY: TAR_H.ref
::  OLD: \tar_h_copy/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
{? _a & ref_name(_a)+3<>_b
||
   TAR_H.cntx_psh();
   TAR_H.use(ref_name(_a));
   TAR_H.prefix;
   {? TAR_H.seek(_a)
   ||
      TAR_H.use((TAR_H.name-3)+_b);
      _wyn:=exec('tar_h','ceny_dok'
         ,'?'
         ,TAR_H.TAP
         ,TAR_H.TAPB
         ,TAR_H.WAL
         ,TAR_H.JM
         ,TAR_H.IL
         ,TAR_H.WAR
         ,TAR_H.PL
         ,TAR_H.CEN_OD
         ,TAR_H.CEN
         ,TAR_H.RAB
         ,TAR_H.RAB_B
         ,TAR_H.PROMO
         ,TAR_H.OD
         ,TAR_H.DO
         ,TAR_H.T_OD
         ,TAR_H.T_DO
         ,TAR_H.IL_DO
         ,TAR_H.WAR_DO
         ,TAR_H.GRA_IL
         ,TAR_H.GRA_CEN)
   ?};
   TAR_H.cntx_pop()
||
   _wyn:=_a
?};
_wyn


\ilwar_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: sprawdza kryterium ilosciowo-wartosciowe cennika
::   WE: _a - TAP.ref
::       _b - ilosc
::       _c - wartosc
::       _d - 'T'-cena brutto, 'N'-cena netto
::       _e - uchwyt tabeli pozycji
::   WY: 0-NOK, 1-OK
::  OLD: \ilwar_chk/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

TAP.cntx_psh();

TAP.use(ref_name(_a));
TAP.prefix;
{? TAP.seek(_a)
||
   {? _d='T'
   ||
      _sv:=exec('dok_pozsv','ceny_dok',_e);
      _war:=((1+_sv)*TAP.WAR)$2;
      _war_do:=((1+_sv)*TAP.WAR_DO)$2
   ||
      _war:=TAP.WAR;
      _war_do:=TAP.WAR_DO
   ?};
   _wyn:=
      (TAP.IL=0 | TAP.IL<=_b & (TAP.IL_DO=0 | _b<TAP.IL_DO))
      & (_war=0 | _war<=_c & (_war_do=0 | _c<_war_do))
?};

TAP.cntx_pop();

_wyn


\gratisy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zagregowane pozycje dokumentu
::  OLD: \gratisy/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_czysc:="VAR_DEL.delete('__tnxind','__tnxmgr','__tnxmgp','__wer1','__wer2','__ts','__zzam','__choice')";
_czysc();

__zzam:=0;
_Nag:=cur_tab(1);
_Poz:=
   {? _Nag=OFE || OFP
   |? _Nag=ZK_N || __zzam:=1; ZK_P
   |? _Nag=FAKS || FAP
   |? _Nag=ND || DK
   || cur_tab(1)
   ?};
&_Nag;
_Nag:=
   {? _Poz=OFP || OFE
   |? _Poz=ZK_P || __zzam:=1; ZK_N
   |? _Poz=FAP || FAKS
   |? _Poz=DK || ND
   ?};

_Sel:=_Nag.sel_aget;
{? ~_Sel.first || _Sel.REF:=#_Nag.ref; _Sel.add ?};
_Nag.sel_adel;

{? ~_Sel.first
||
   _czysc(); return
||
   _selsize:=_Sel.size();
   _nags:='';
   __choice:=0;
   exec('ini_kom','#message','Naliczanie gratisów'@);
   __kom.sect_beg('Komunikaty ogólne'@,,,1);
   __kom.sect_end();
   {!
   |?
      _chk:=1;
      {? _Nag.seek(_Sel.REF,) & ~(_nags*$_Nag.ref())
      ||
         _nags+=$_Nag.ref();
         _chk:=exec('gratisy_chk','ceny_dok',_Nag)
      ?};
      {? _chk=-1
      || 0
      |? _chk
      || _Sel.next()
      || _Sel.del
      ?}
   !};
   {? _chk=-1
   || unlock_r; _czysc(); return
   || _ret:=
         {? _Sel.first
         || {? __kom_on
            || __kom.set_root('Komunikaty ogólne'@,,,1);
               __kom.add('Zamknij okno w celu naliczenia gratisów.'@,7,,1);
               __kom.sect_end()
            || __kom.del('Komunikaty ogólne'@)
            ?};
            0
         || __kom_on:=1;
            __kom.set_root('Komunikaty ogólne'@,,,1);
            __kom.add('Brak dokumentów zakwalifikowanych do naliczenia gratisów.'@,7,,1);
            __kom.sect_end();
            1
         ?};
      exec('end_kom','#message');
      {? _ret || unlock_r; _czysc(); return ?}
   ?}
?};

_Nag.cntx_psh(); _Poz.cntx_psh();

_date:=date;
_ok:=1;
_first:=0;
__ts:=$FIRMA.tm_stamp;

:: agregowanie pozycji w podziale na zaznaczone dokumenty
_Tabagr:=exec('dok_agr','ceny_dok',_Sel,_Nag,_Poz,0,0);
_tanxwin:=_Tabagr.ndx_tmp(,,'TYP',,,'DOK_SYM',,,'P',,);
_tanxtyp:=_Tabagr.ndx_tmp(,,'TYP',,,'DOK',,);

:: analiza gratisow
_Tabagr.index(_tanxtyp);
{! _ii:=2..4
|!
   _Tabagr.prefix($_ii);
   {? _Tabagr.first
   ||
      {!
      |?
         {? _Poz=OFP || OFE.prefix; _ok:=OFE.seek(_Tabagr.DOK); exec('st_licz_wz','ceny','OFE')
         |? _Poz=ZK_P || ZK_N.prefix; _ok:=ZK_N.seek(_Tabagr.DOK); exec('st_licz_wz','ceny','ZK_N')
         |? _Poz=FAP || FAKS.prefix; _ok:=FAKS.seek(_Tabagr.DOK); exec('st_licz_wz','ceny','FAKS')
         |? _Poz=DK || ND.prefix; _ok:=ND.seek(_Tabagr.DOK); exec('st_licz_wz','ceny','ND')
         ?};
         {? _ok
         ||
            _grkh:=_Nag.KH().GRKH;
            _kh:=_Nag.KH;
            _odb:={? _Nag=OFE | _Nag=ZK_N || _Nag.ODB || _Nag.KH_ODB ?}
         ?};

         {? _ok=0
         ||
            FUN.info('Nie znaleziono nagłówka dokumentu.'@)
         |? _ii=2
::       gratisy materialu
         ||
            TAP.index('ODWAL_M');
            TAP.prefix(ST.ODDZ,'G',exec('FindAndGet','#table','SLO',_Tabagr.WAL,,,null())
             ,exec('FindAndGet','#table','M',_Tabagr.INDEKS,,,null()));
            {? TAP.first
            ||
               {!
               |?
                  {? TAP.OD<=_date & (TAP.DO>=_date | TAP.DO=date(0,0,0))
                     & TAP.JM=exec('FindAndGet','#table','JM',_Tabagr.JM,,,null())
                     & TAP.GRA_IL
                  ||
                     TAP.TAR();
                     _prior:=TAR.P;
                     _Tabagr.cntx_psh;
                     _Tabagr.prefix;
                     _Tabagr.TAP:=$TAP.ref;
                     _Tabagr.TYP:='5';
                     _Tabagr.OPIS:=
                        {? TAR.KH=null & TAR.GRKH=null || _Tabagr.P:=_prior; 'Gratis z: '+TAR.KOD+' '+TAR.NAZ
                        |? _odb<>null & TAR.KH_ODB=_odb || _Tabagr.P:=_prior; 'Gratis odbiorcy z: '+TAR.KOD+' '+TAR.NAZ
                        |? _kh<>null & TAR.KH=_kh & TAR.KH_ODB=null || _Tabagr.P:=_prior; 'Gratis kontrahenta z: '+TAR.KOD+' '+TAR.NAZ
                        |? _grkh<>null & TAR.GRKH=_grkh || _Tabagr.P:=_prior; 'Gratis grupy z: '+TAR.KOD+' '+TAR.NAZ
                        || _Tabagr.P:=0; '???'
                        ?};
                     {? _Tabagr.P
                     ||
                        _Tabagr.PP:=3;
                        _Tabagr.P:=_Tabagr.P*10+_Tabagr.PP;
                        _Tabagr.GRATIS:=$TAP.GRATIS;
                        _Tabagr.IND:=exec('FindAndGet','#table','M',_Tabagr.GRATIS,,"M.KTM",null());
                        _Tabagr.NAZ:=exec('FindAndGet','#table','M',_Tabagr.GRATIS,,"M.N",null());
                        _Tabagr.GRA_IL:=TAP.GRA_IL;
                        _Tabagr.JM_G_KOD:=TAP.GRATIS().J().KOD;
                        _Tabagr.GRA_CEN:=TAP.GRA_CEN;
                        _Tabagr.OK:=exec('ilwar_chk','ceny_dok',TAP.ref,_Tabagr.IL,_Tabagr.WAR,TAZ.CB,_Poz);
                        _Tabagr.add
                     ?};
                     _Tabagr.cntx_pop
                  ?};
                  TAP.next
               !}
            ?}
         |? _ii=3
::       gratisy grupy
         ||
            TAP.index('ODWAL_G');
            TAP.prefix(ST.ODDZ,'G',exec('FindAndGet','#table','SLO',_Tabagr.WAL,,,null())
             ,exec('FindAndGet','#table','MGR',_Tabagr.MGR,,,null()),null);
            {? TAP.first
            ||
               {!
               |?
                  {? TAP.OD<=_date & (TAP.DO>=_date | TAP.DO=date(0,0,0))
                     & TAP.JM=exec('FindAndGet','#table','JM',_Tabagr.JM,,,null())
                     & TAP.GRA_IL
                  ||
                     TAP.TAR();
                     _prior:=TAR.P;
                     _Tabagr.cntx_psh;
                     _Tabagr.prefix;
                     _Tabagr.TAP:=$TAP.ref;
                     _Tabagr.TYP:='5';
                     _Tabagr.OPIS:=
                        {? TAR.KH=null & TAR.GRKH=null || _Tabagr.P:=_prior; 'Gratis z: '+TAR.KOD+' '+TAR.NAZ
                        |? _odb<>null & TAR.KH_ODB=_odb || _Tabagr.P:=_prior; 'Gratis odbiorcy z: '+TAR.KOD+' '+TAR.NAZ
                        |? _kh<>null & TAR.KH=_kh & TAR.KH_ODB=null || _Tabagr.P:=_prior; 'Gratis kontrahenta z: '+TAR.KOD+' '+TAR.NAZ
                        |? _grkh<>null & TAR.GRKH=_grkh || _Tabagr.P:=_prior; 'Gratis grupy z: '+TAR.KOD+' '+TAR.NAZ
                        || _Tabagr.P:=0; '???'
                        ?};
                     {? _Tabagr.P
                     ||
                        _Tabagr.PP:=3;
                        _Tabagr.P:=_Tabagr.P*10+_Tabagr.PP;
                        _Tabagr.GRATIS:=$TAP.GRATIS;
                        _Tabagr.IND:=exec('FindAndGet','#table','M',_Tabagr.GRATIS,,"M.KTM",null());
                        _Tabagr.NAZ:=exec('FindAndGet','#table','M',_Tabagr.GRATIS,,"M.N",null());
                        _Tabagr.GRA_IL:=TAP.GRA_IL;
                        _Tabagr.JM_G_KOD:=TAP.GRATIS().J().KOD;
                        _Tabagr.GRA_CEN:=TAP.GRA_CEN;
                        _Tabagr.OK:=exec('ilwar_chk','ceny_dok',TAP.ref,_Tabagr.IL,_Tabagr.WAR,TAZ.CB,_Poz);
                        _Tabagr.add
                     ?};
                     _Tabagr.cntx_pop
                  ?};
                  TAP.next
               !}
            ?}
         |? _ii=4
::       gratisy podgrupy
         ||
            TAP.index('ODWAL_G');
            TAP.prefix(ST.ODDZ,'G',exec('FindAndGet','#table','SLO',_Tabagr.WAL,,,null())
             ,exec('FindAndGet','#table','MGR',_Tabagr.MGR,,,null())
             ,exec('FindAndGet','#table','MGRP',_Tabagr.MGRP,,,null()));
            {? TAP.first
            ||
               {!
               |?
                  {? TAP.OD<=_date & (TAP.DO>=_date | TAP.DO=date(0,0,0))
                     & TAP.JM=exec('FindAndGet','#table','JM',_Tabagr.JM,,,null())
                     & TAP.GRA_IL
                  ||
                     TAP.TAR();
                     _prior:=TAR.P;
                     _Tabagr.cntx_psh;
                     _Tabagr.prefix;
                     _Tabagr.TAP:=$TAP.ref;
                     _Tabagr.TYP:='5';
                     _Tabagr.OPIS:=
                        {? TAR.KH=null & TAR.GRKH=null || _Tabagr.P:=_prior; 'Gratis z: '+TAR.KOD+' '+TAR.NAZ
                        |? _odb<>null & TAR.KH_ODB=_odb || _Tabagr.P:=_prior; 'Gratis odbiorcy z: '+TAR.KOD+' '+TAR.NAZ
                        |? _kh<>null & TAR.KH=_kh & TAR.KH_ODB=null || _Tabagr.P:=_prior; 'Gratis kontrahenta z: '+TAR.KOD+' '+TAR.NAZ
                        |? _grkh<>null & TAR.GRKH=_grkh || _Tabagr.P:=_prior; 'Gratis grupy z: '+TAR.KOD+' '+TAR.NAZ
                        || _Tabagr.P:=0; '???'
                        ?};
                     {? _Tabagr.P
                     ||
                        _Tabagr.PP:=3;
                        _Tabagr.P:=_Tabagr.P*10+_Tabagr.PP;
                        _Tabagr.GRATIS:=$TAP.GRATIS;
                        _Tabagr.IND:=exec('FindAndGet','#table','M',_Tabagr.GRATIS,,"M.KTM",null());
                        _Tabagr.NAZ:=exec('FindAndGet','#table','M',_Tabagr.GRATIS,,"M.N",null());
                        _Tabagr.GRA_IL:=TAP.GRA_IL;
                        _Tabagr.JM_G_KOD:=TAP.GRATIS().J().KOD;
                        _Tabagr.GRA_CEN:=TAP.GRA_CEN;
                        _Tabagr.OK:=exec('ilwar_chk','ceny_dok',TAP.ref,_Tabagr.IL,_Tabagr.WAR,TAZ.CB,_Poz);
                        _Tabagr.add
                     ?};
                     _Tabagr.cntx_pop
                  ?};
                  TAP.next
               !}
            ?}
         ?};
         _Tabagr.next
      !}
   ?}
!};

:: wybor i dopisanie gratisow
_Tabagr.index(_tanxwin);
_Tabagr.prefix('5');
__wer1:=_Tabagr.mk_sel('Gratisy'@,'P',,'snvepadwidnzdwq',1,,,,'U');
_Tabagr.win_fld(__wer1,,'DOK_SYM',,,20,,,'Dokument'@);
_Tabagr.win_fld(__wer1,,'OPIS',,,20,,,'Opis'@);
_Tabagr.win_fld(__wer1,,'IND',,,15,,,'Indeks'@);
_Tabagr.win_fld(__wer1,,'NAZ',,,15,,,'Nazwa'@);
_Tabagr.win_fld(__wer1,,'GRA_IL',,,10,2,,'Ilość'@);
_Tabagr.win_fld(__wer1,,'JM_G_KOD',,,3,,,'jm'@);
_Tabagr.win_fld(__wer1,,'GRA_CEN',,,10,2,,'Cena'@);
_Tabagr.win_fld(__wer1,,'IL',,,10,2,,'Podstawa — ilość(suma)'@);
_Tabagr.win_fld(__wer1,,'JM_KOD',,,3,,,'jm'@);
_Tabagr.win_fld(__wer1,,'WAR',,,15,2,,'Podstawa — wartość(suma)'@);
_Tabagr.win_fld(__wer1,,'WAL_KOD',,,6,,,'Waluta'@);
_fb:="
   _Tab:=cur_tab(1);
   _Sel:=_Tab.sel_aget;
   {? ~_Sel.first || _Sel.REF:=_Tab.ref; _Sel.add ?};
   {? ~FUN.ask({? _Sel.first & _Sel.next() || 'Dodać wybrane gratisy?'@ || 'Dodać wybrany gratis?'@ ?}) || return(0) ?};

   {? __zzam
   || BEER.RMAG:={? ZK_N.MG<>null || ZK_N.MG || null ?};
      {? BEER.RMAG=null & exec('jakieupr','zamsiw_wspolne',0)
      || BUFMG.index('US');
         BUFMG.prefix(OPERATOR.USER,2);
         BUFMG.win_sel('WYB');
         {? BUFMG.select() || BEER.RMAG:=BUFMG.MG; BEER.ZAMUPR:=1 || return(0) ?}
      ?}
   ?};

   {? _Sel.first
   ||

      {? _Tab.seek(_Sel.REF,)
      ||
         _dok:=_Tab.DOK;
         _obldokw:=0;
         _delgrad:=1;
         {!
         |?
            {? _Tab.seek(_Sel.REF,)
            ||
               {? _dok<>_Tab.DOK
               ||
                  {? _obldokw || exec('obl_dokwar','ceny_dok',_dok) ?};
                  _dok:=_Tab.DOK;
                  _obldokw:=0;
                  _delgrad:=1
               ?};
               {? _Tab.OK
               ||
                  {? _delgrad
                  ||
                     exec('del_gradok','ceny_dok',_dok);
                     _delgrad:=0
                  ?};
                  exec('add_gra2dok','ceny_dok',_Tab);
                  _obldokw:=1
               ?}
            ?};
            _Sel.next
         !};
         {? _obldokw || exec('obl_dokwar','ceny_dok',_dok) ?}
      ?}
   ?};

   _Tab.sel_adel;
   sel_exit
";
_Tabagr.win_act(__wer1,,'Formuła','&Akceptuj'@@,,,_fb,,1,1,_fb,,'A');
_fb:="
   _tap:=cur_tab(1).TAP;
   TAP.cntx_psh(); TAR.cntx_psh();
   TAP.use(8+_tap);
   TAP.prefix;
   {? TAP.seek(_tap)
   ||
      TAP.TAR();
      TAP.win_edit('RED_G');
      TAP.display()
   ?};
   TAP.cntx_pop(); TAR.cntx_pop()
";
_Tabagr.win_act(__wer1,,'Wyświetl',,,,_fb);
_fb:="
   BEER.RS:={? 5+cur_tab.DOK='nagdo' || 1 |? 5+cur_tab.DOK='zknag' | cur_tab.DOK='oferty' || 2 || 3 ?};
:: [rr] DRO
   exec('info_zam','magazyn_stan',BEER.RS,exec('FindAndGet','#table','M',cur_tab.GRATIS))
";
_Tabagr.win_act(__wer1,,'Formuła','S&tan'@@,,,_fb,,,,,,'T');
_Tabagr.win_act(__wer1,,'Szukaj');
_Tabagr.win_act(__wer1,,'Kolejność');
_fb:="exec('legenda','color','TAP#04')";
_Tabagr.win_act(__wer1,,'Formuła','&Legenda'@@,,,_fb,,,,,,'L');
_fb:="exec('rekprzed','color','TAP#04')";
_Tabagr.win_act(__wer1,,'Rekord',,,,_fb);
__wer2:=_Tabagr.mk_sel('Podstawa — pozycje dokumentu'@,,,'jskwporacfakele',1);
_Tabagr.win_fld(__wer2,,'POZ',,,-5,,,'Pozycja'@);
_Tabagr.win_fld(__wer2,,'MGR_KOD',,,8,,,'Grupa'@);
_Tabagr.win_fld(__wer2,,'MGRP_KOD',,,8,,,'Podgrupa'@);
_Tabagr.win_fld(__wer2,,'IND',,,40,,,'Indeks'@);
_Tabagr.win_fld(__wer2,,'NAZ',,,34,,,'Nazwa'@);
_Tabagr.win_fld(__wer2,,'IL',,,10,2,,'Ilość'@);
_Tabagr.win_fld(__wer2,,'JM_KOD',,,3,,,'jm'@);
_Tabagr.win_fld(__wer2,,'WAR',,,15,2,,'Wartość'@);
_wer3:=_Tabagr.grp_make('Gratisy'@,,'wsweirjojfvnakw');
_fr:="grp_disp(cur_tab,__wer2)";
_Tabagr.grp_sel(_wer3,,__wer1,,_fr,,,15,,,,,'maximized');
_Tabagr.grp_splt(_wer3,,'horizontal','podstawa');
_fb:="
   cur_tab.cntx_psh;
   {? cur_tab.INDEKS<>'' || cur_tab.index(__tnxind); cur_tab.prefix('1',cur_tab.DOK,cur_tab.INDEKS,cur_tab.JM)
   |? cur_tab.MGR<>'' || cur_tab.index(__tnxmgr); cur_tab.prefix('1',cur_tab.DOK,cur_tab.MGR,cur_tab.JM)
   |? cur_tab.MGRP<>'' || cur_tab.index(__tnxmgp); cur_tab.prefix('1',cur_tab.DOK,cur_tab.MGRP,cur_tab.JM)
   ?}
";
_fa:="cur_tab.cntx_pop";
_Tabagr.grp_sel(_wer3,,__wer2,,"",,,15,_fb,_fa,,,'maximized_with_title');

_Tabagr.win_sel(_wer3);

{? ~_Tabagr.first
|| FUN.info('Nie znaleziono gratisów.'@)
|? _Tabagr.select & _Poz=FAP
||
:: uzupelnienie dostaw dla faktur typu G
   _ind:=FAP.ndx_tmp(,,'POZF',,,'FAKS','SYM',,'POZ',,);
   FAP.index(_ind); FAP.prefix(__ts);
   {? FAP.first
   ||
      _werfap:=FAP.mk_sel('Pozycje'@,,,'nnwiioeyhqdjkla');
      FAP.win_fld(_werfap,,'FAKS','SYM',,20,,,'Symbol'@);
      FAP.win_fld(_werfap,,'POZ',,,-5,,,'Pozycja'@);
      FAP.win_fld(_werfap,,'M','KTM',,20,,,'Indeks'@);
      FAP.win_fld(_werfap,,'IL',,,10,2,,'Ilość'@);
      _fb:="
         _f3_il:=exec('f3_il_fak','faktury_poz',FAP.IL);
         {? _f3_il[1]=FAP.IL
         || FAP.cntx_psh;
            FAP.prefix;
            FAP.POZF:='';
            FAP.put;
            FAP.cntx_pop
         ?}
      ";
      FAP.win_act(_werfap,,'Formuła','&Dostawa'@@,,,_fb,,1,,,,'D');
      _fc:="
         _ok:=1;
         {? FAP.first
         ||
            ZK_P.index('FAP');
            {!
            |?
               ZK_P.prefix(FAP.ref);
               _ok:=ZK_P.first;
               _ok & FAP.next
            !}
         ?};
         {? ~_ok
         || {? FUN.ask('Pozostały pozycje niepowiązane z magazynem.\n'
                       'Opuszczenie tego okna spowoduje ich usunięcie.\n\n'
                       'Czy chcesz zakończyć uzupełnianie powiązań?'@)
            ||
               _ok:=1;
               {? FAP.first
               ||
                  ZK_P.index('FAP');
                  {!
                  |?
                     ZK_P.prefix(FAP.ref);
                     {? ZK_P.first
                     ||
                        FAP.next
                     ||
                        FAKS.cntx_psh;
                        FAP.FAKS();
                        _next:=exec('del_grapoz','ceny_dok',FAP);
                        exec('obl_dokwar','ceny_dok',$FAKS.ref);
                        FAKS.cntx_pop;
                        _next
                     ?}
                  !}
               ?}
            ?}
         ?};
         _ok
      ";
      _werg:=FAP.grp_make('Uzupełnianie powiązań pozycji z magazynami'@,,'zzzkkeadhvbhoqp',,,_fc);
      _far:="exec('fap_zk_p','ceny_dok')";
      FAP.grp_sel(_werg,,_werfap,,_far,,,15,,,,,'maximized_with_title');
      FAP.grp_splt(_werg,,'horizontal','rez');
      FAP.grp_sel(_werg,REZ,'FAP',,,,,5,,,,,'maximized_with_title');
      FAP.win_sel(_werg);
      FAP.select
   ?};
   FAP.ndx_drop(_ind)
?};

_Nag.cntx_pop(); _Poz.cntx_pop();
_Nag.get();
_czysc();
unlock_r


\gratisy_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: weryfikacja dokumentow do naliczenia gratisow
::   WE: _a - tabela
::   WY: 0 - dokument nie spelnia kryteriow, 1-wpp
::  OLD: \gratisy_chk/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;

_Tab:=_a;

{? _Tab=OFE
|| {? ~OFE.r_lock(1,1,1)
   || __kom_on:=1;
      __kom.sect_beg(OFE.SYM,,,1);
      __kom.add('Dokument obsługuje inny operator. Naliczenie gratisów niedostępne.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?};
   {? _wyn
   || _Ofe:=sql('select ZK_N.REFERENCE ref from @ZK_N where ZK_N.OFE =  :_a ',OFE.ref);
      {? _Ofe.first()
      || __kom_on:=1;
         __kom.sect_beg(OFE.SYM,,,1);
         __kom.add('Do tej oferty utworzono już zamówienie. Naliczenie gratisów niedostępne.'@,81,,1);
         __kom.sect_end();
         _wyn:=0
      ?}
   ?}
|? _Tab=ZK_N
|| {? ~ZK_N.r_lock(1,1,1)
   || __kom_on:=1;
      __kom.sect_beg(ZK_N.SYM,,,1);
      __kom.add('Dokument obsługuje inny operator. Naliczenie gratisów niedostępne.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?};
   {? _wyn & ZK_N.MG().PAL='T'
   || __kom_on:=1;
      __kom.sect_beg(ZK_N.SYM,,,1);
      __kom.add('Zamówienie do magazynu paletowego. Naliczenie gratisów niedostępne.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?};
   {? _wyn & ZK_N.AKC='T'
   || __kom_on:=1;
      __kom.sect_beg(ZK_N.SYM,,,1);
      __kom.add('Dokument zakończony. Naliczenie gratisów niedostępne.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?}
|? _Tab=FAKS
|| {? ~FAKS.r_lock(1,1,1)
   || __kom_on:=1;
      __kom.sect_beg(FAKS.SYM,,,1);
      __kom.add('Dokument obsługuje inny operator. Naliczenie gratisów niedostępne.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?};
   {? _wyn & FAKS.AKC='T'
   || __kom_on:=1;
      __kom.sect_beg(FAKS.SYM,,,1);
      __kom.add('Dokument zakończony. Naliczenie gratisów niedostępne.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?};
   {? _wyn & (FAKS.T().KOR<>'N' | FAKS.WHERE='M')
   || __kom_on:=1;
      __kom.sect_beg(FAKS.SYM,,,1);
      __kom.add('Naliczenie gratisów niedostępne dla tego typu dokumentu.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?};
   {? _wyn & FAKS.STS
   || {? {? FAKS.SZ='S'
         || ~exec('spr_upr2','users','STS',FAKS.STS,'-')
         || ~exec('spr_upr2','users','STZ',FAKS.STS,'-')
         ?}
      || __kom_on:=1;
         __kom.sect_beg(FAKS.SYM,,,1);
         {? FAKS.SZ='S'
         || __kom.add('Brak uprawnień do stanowiska sprzedaży %1.'@[FAKS.STS().KOD],81,,1)
         || __kom.add('Brak uprawnień do stanowiska zakupu %1.'@[FAKS.STS().KOD],81,,1)
         ?};
         __kom.sect_end();
         _wyn:=0
      ?}
   ?}
|? _Tab=ND
|| {? ~ND.r_lock(1,1,1)
   || __kom_on:=1;
      __kom.sect_beg(ND.SYM,,,1);
      __kom.add('Dokument obsługuje inny operator. Naliczenie gratisów niedostępne.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?};
   {? _wyn * ND.MAG().PAL='T'
   || __kom_on:=1;
      __kom.sect_beg(ND.SYM,,,1);
      __kom.add('Magazyn paletowy. Naliczenie gratisów niedostępne.'@,81,,1);
      _wyn:=0
   ?};
   {? _wyn & ND.Z='T'
   || __kom_on:=1;
      __kom.sect_beg(ND.SYM,,,1);
      __kom.add('Dokument zakończony. Naliczenie gratisów niedostępne.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?};
   {? _wyn & ND.MAG().PAL='T'
   || __kom_on:=1;
      __kom.sect_beg(ND.SYM,,,1);
      __kom.add('W magazynie paletowym naliczenie gratisów niedostępne.'@,81,,1);
      __kom.sect_end();
      _wyn:=0
   ?}
?};

{? _wyn & _Tab.GRATISY='T'
||
   {? (__choice=0 | __choice=1 | __choice=3)
   || __choice:=FUN.choice(
            'Gratisy dla dokumentu %1 są już naliczone. Usunąć gratisy i naliczyć ponownie?'@[_Tab.SYM],,
            'Dokument'@,'Wszystkie dokumenty'@,'Pomiń dokument'@,'P&omiń wszystkie dokumenty'@)
   ?};
   {? __choice=0
   || _wyn:=-1
   || _wyn:=
         {? __choice=1 | __choice=2
         || __kom_on:=1;
            __kom.sect_beg(_Tab.SYM,,,1);
            __kom.add('Gratisy dla dokumentu są już naliczone. Ponowne naliczenie gratisów.'@,13,,1);
            __kom.sect_end();
            1
         || __kom_on:=1;
            __kom.sect_beg(_Tab.SYM,,,1);
            __kom.add('Gratisy dla dokumentu są już naliczone. Pominięcie naliczania gratisów.'@,81,,1);
            __kom.sect_end();
            0
         ?}
   ?}
?};

_wyn


\dr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: naliczanie rabatu na zaznaczonych pozycjach dokumentu
::  OLD: \dr/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_czysc:="
   VAR_DEL.delete('__Poz','__Nag');
   TAZ.DR_WARD:=TAZ.DR_WARZP:=TAZ.DR_RAB:=TAZ.DR_WARO:=0
";
_czysc();

__Poz:=cur_tab(1);
_poz_akr:='';
__Nag:=
   {? __Poz=OFP || _poz_akr:='OFP'; OFE
   |? __Poz=ZK_P || _poz_akr:='ZK_P'; ZK_N
   |? __Poz=FAP || _poz_akr:='FAP'; FAKS
   |? __Poz=DK || _poz_akr:='DK'; ND
   ?};

{? __Nag=FAKS
||
   _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
   {? ~_r_lock
   ||
      exec('who_rlock_faks','faktury_nag');
      _czysc();
      return(0)
   ?}
?};

__Poz.cntx_psh();

_Sel:=__Poz.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#__Poz.ref(); _Sel.add() ?};

:: werfyfikacja pozycji do rabatu
exec('ini_kom','#message','Usuwanie pozycji'@);
_loop:=_Sel.first();
{!
|? _loop
|!
   _loop:=
      {? __Poz=ZK_P & __Poz.seek(_Sel.REF,) & __Poz.ILZ<>__Poz.ILP
      || __kom_on:=1;
         __kom.sect_beg($ZK_P.POZ);
         __kom.add('Pozycja była realizowana. Została pomnięta.'@,7,,1);
         __kom.sect_end();
         _Sel.del()
      || _Sel.next()
      ?}
!};
{? ~_Sel.first()
|| __kom_on:=1;
   __kom.sect_beg('Podsumowanie'@);
   __kom.add('Brak pozycji do udzielenia rabatu.'@,7,,1)
?};
exec('end_kom','#message');

{? ~_Sel.first() || __Poz.cntx_pop(); return(0) ?};

:: wyliczenie wartosci dokumentu i wartosci zaznaczonych pozycji
_loop:=_Sel.first();
{!
|? _loop
|!
   {? __Poz.seek(_Sel.REF,)
   ||
      _war:=exec('dok_pozwa','ceny_dok',__Poz);
      {? _Sel.find_tab(,'REF',,'=',#__Poz.ref()) || TAZ.DR_WARZP+=_war ?};
      TAZ.DR_WARD+=_war
   ?};
   _loop:=_Sel.next()
!};

:: oczekiwana wartosc dokumentu
TAZ.DR_WARO:=TAZ.DR_WARD;

:: naliczenie rabatu na zaznaczonych pozycjach
TAZ.win_edit('RED_DR');
_valid:="
   {? TAZ.DR_WARO<=0 | TAZ.DR_RAB<=0
   || FUN.info('Wartość musi być większa od zera.'@);
      'DR_WARO'
   |? TAZ.DR_RAB>TAZ.DR_WARZP
   || FUN.info('Wartość rabatu nie może być większa od wartości zaznaczonych pozycji.'@);
      'DR_WARO'
   || ''
   ?}
";
{? TAZ.edit(_valid)
||
   do();
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      {? __Poz.seek(_Sel.REF,)
      ||
         _war:=exec('dok_pozwa','ceny_dok',__Poz);
         {? TAZ.RAB_TYP='P'
         ||
            _warp:=(exec('dok_pozil','ceny_dok',__Poz)*exec('dok_pozcen','ceny_dok',__Poz))$2;
            {? _warp & TAZ.DR_WARZP
            ||
               _w_prop:=(_war*TAZ.DR_RAB/TAZ.DR_WARZP)$2;
               _proc:=100-((_war-_w_prop)*100/_warp)$2;
               __Poz.RAB:=_proc-__Nag.RAB;
               exec('licz','ceny',__Poz);
               __Poz.put();
               _war1:=exec('dok_pozwa','ceny_dok',__Poz);
               TAZ.DR_RAB-=_war-_war1;
               TAZ.DR_WARZP-=_war
            ?}
         |? TAZ.RAB_TYP='S'
         ||
            _warp:=(exec('dok_pozil','ceny_dok',__Poz)*exec('dok_pozcen','ceny_dok',__Poz))$2;
            {? _warp & TAZ.DR_WARZP
            ||
               _w_prop:=(_war*TAZ.DR_RAB/TAZ.DR_WARZP)$2;
               _proc:=100-((_war-_w_prop)*100/_warp)$2;
               __Poz.RAB:=100-((100-_proc)*100/(100-__Nag.RAB))$2;
               exec('licz','ceny',__Poz);
               __Poz.put();
               _war1:=exec('dok_pozwa','ceny_dok',__Poz);
               TAZ.DR_RAB-=_war-_war1;
               TAZ.DR_WARZP-=_war
            ?}
         |? TAZ.RAB_TYP='K'
         ||
            {? TAZ.DR_WARZP
            ||
               _w_prop:=(_war*TAZ.DR_RAB/TAZ.DR_WARZP)$2;
               _il:=exec('dok_pozil','ceny_dok',__Poz);
               {? _il
               ||
                  _w_prop:=(_w_prop/_il)$2;
                  __Poz.RABK+=_w_prop;
                  TAZ.DR_RAB-=(_w_prop*_il)$2;
                  TAZ.DR_WARZP-=_war;
                  exec('licz','ceny',__Poz);
                  __Poz.put()
               ?}
            ?}
         |? TAZ.RAB_TYP='W'
         ||
            {? TAZ.DR_WARZP
            ||
               _w_prop:=(_war*TAZ.DR_RAB/TAZ.DR_WARZP)$2;
               __Poz.RABK+=_w_prop;
               TAZ.DR_RAB-=_w_prop;
               TAZ.DR_WARZP-=_war;
               exec('licz','ceny',__Poz);
               __Poz.put()
            ?}
         ?};
         {? __Poz.TAR_H
         ||
            {? exec('czyrabp','ceny',TAZ.RAB_TYP)
            || exec('clr_promo_tap','ceny',__Poz,__Poz.RAB)
            || exec('clr_promo_tap','ceny',__Poz,__Poz.RABK)
            ?};
            __Poz.put()
         ?};

         _chk:=exec('czy_cena_z_tap','ceny',_poz_akr,0);
         {? _chk.WYN
         || undo();
            FUN.info('Pozycja '@+$exec('dok_pozp','ceny_dok',__Poz)+'.\n\n'+_chk.MSG)
         ?};
         &_chk
      ?};
      _loop:=_Sel.next()
   !};
   exec('obl_dokwar','ceny_dok',$__Nag.ref());
   FAKS.get();
   end();
   {? TAZ.DR_RAB
   || FUN.info('Nie udało się rozliczyć kwoty '@+form(TAZ.DR_RAB,,2)+' '
         +exec('FindAndGet','#table','SLO',$exec('dok_pozwl','ceny_dok',__Poz),,"SLO.KOD",'')+'.')
   ?}
?};

__Poz.sel_adel();
__Poz.cntx_pop();
{? __Nag=FAKS || exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock) ?};
_czysc();

0


\ae_ofpww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redakcji ilosci i ceny  - przeliczanie wartosci
::   WE: [_a] - kontrola w naglowku-1 domyslnie nie-0
::       [_b] - 0 - obliczenia dla ceny w walucie narodowej, 1 - ceny w walucie handlowej
::   WY: 1 - jest OK 0 - nie jest
::  OLD: \ae_ofpww/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};

_dokl_m:=exec('jaka_dok_m','jm',OFP.M);
OFP.IL:=OFP.IL$_dokl_m;

exec('st_licz_wz','ceny','OFE');

_rabat:=_rabat1:=kwrab:=_kwrabj:=0;

{? exec('czyrabp','ceny',TAZ.RAB_TYP)
|| _rabat:=exec('rab_proc','ceny',OFP.RAB,TAZ.RAB,TAZ.RAB_TYP);
   _rabat1:=1-(_rabat)/100
|| _dokl_c:=2;
   _cena:={? TAZ.CZY_WAL || OFP.CWAL || OFP.C ?};
   _kwil:=OFP.IL;
   _nagwsp:=-TAZ.RAB/100;
   _kwpoz:=-OFP.RABK;
   _kwrab:=
      {? TAZ.RAB_TYP='W'
      || _kwpoz+(_nagwsp*((_cena*_kwil)$2))$2
      || ((_kwpoz+(_nagwsp*_cena)$_dokl_c)*_kwil)$2
      ?};
   _kwrabj:=
      {? TAZ.RAB_TYP='W'
      || {? _kwil || (_kwpoz/_kwil)$_dokl_c ?}+(_nagwsp*_cena)$_dokl_c
      || _kwpoz+(_nagwsp*_cena)$_dokl_c
      ?}
?};

_vat:=#((OFP.SV().KOD*'%')+OFP.SV().KOD-1)/100;
_walj:={? TAZ.CZY_WAL || exec('FindInSet','#table','WAL','WAL_SYM',TAZ.WAL().KOD,,"WAL.J",,,1) || 1 ?};
_krs:={? _b | ~TAZ.CZY_WAL || 1 || TAZ.KRS/_walj ?};

exec('ceny2rab','ceny_dok');

{? TAZ.CB='T'
||
   {? TAZ.SPOBLRAB='W'
   || {? TAZ.SPP='N' & ~exec('czyrabp','ceny',TAZ.RAB_TYP) || _kwrab:=(_kwrab*_krs)$2; _kwrabj:=(_kwrabj*_krs)$2 ?};
      _cpr:={? ~_b & (~TAZ.CZY_WAL | TAZ.SPP='N') || _krs:=1; OFP.C || OFP.CWAL ?};
      OFP.WB:=
         {? exec('czyrabp','ceny',TAZ.RAB_TYP)
         || (exec('obl_rabat','ceny',OFP.IL*_cpr,_rabat)$2*_krs)$2
         || (((_cpr*OFP.IL)$2+_kwrab)*_krs)$2
         ?}
   || _cpr:={? _b || OFP.CWAL || OFP.C ?};
      {? TAZ.CZY_WAL & OFE.SPP='T'
      || _cena:={? exec('czyrabp','ceny',TAZ.RAB_TYP)
                || (_cpr*_rabat1)$2
                || {? TAZ.CZY_WAL || _kwrabj:=(_kwrabj*_krs)$2 ?};
                   _cpr+_kwrabj
                ?};
         _cenw:={? exec('czyrabp','ceny',TAZ.RAB_TYP)
                || (OFP.CWAL*_rabat1)$2
                || OFP.CWAL+_kwrabj
                ?};
         OFP.WB:=((_cenw*OFP.IL)*_krs)$2
      || _cena:={? exec('czyrabp','ceny',TAZ.RAB_TYP)
                || (_cpr*_rabat1)$2
                || {? TAZ.CZY_WAL || _kwrabj:=(_kwrabj*_krs)$2 ?};
                   _cpr+_kwrabj
                ?};
         OFP.WB:=(OFP.IL*_cena)$2
      ?}
   ?};
   OFP.WN:={? _vat || (OFP.WB/(1+_vat)) || OFP.WB ?}$2
||
   {? TAZ.SPOBLRAB='W'
   || {? TAZ.SPP='N' & ~exec('czyrabp','ceny',TAZ.RAB_TYP) || _kwrab:=(_kwrab*_krs)$2; _kwrabj:=(_kwrabj*_krs)$2 ?};
      _cpr:={? ~_b & (~TAZ.CZY_WAL | TAZ.SPP='N') || _krs:=1; OFP.C || OFP.CWAL ?};

      OFP.WN:=
         {? exec('czyrabp','ceny',TAZ.RAB_TYP)
         || (exec('obl_rabat','ceny',OFP.IL*_cpr,_rabat)$2*_krs)$2
         || (((_cpr*OFP.IL)$2+_kwrab)*_krs)$2
         ?}
   || _cpr:={? _b || OFP.CWAL || OFP.C ?};
      {? TAZ.CZY_WAL & OFE.SPP='T'
      || _cena:={? exec('czyrabp','ceny',TAZ.RAB_TYP)
                || (_cpr*_rabat1)$2
                || {? TAZ.CZY_WAL || _kwrabj:=(_kwrabj*_krs)$2 ?};
                   _cpr+_kwrabj
                ?};
         _cenw:={? exec('czyrabp','ceny',TAZ.RAB_TYP)
                || (OFP.CWAL*_rabat1)$2
                || OFP.CWAL+_kwrabj
                ?};
         OFP.WN:=((_cenw*OFP.IL)*_krs)$2
      || _cena:={? exec('czyrabp','ceny',TAZ.RAB_TYP)
                || (_cpr*_rabat1)$2
                || {? TAZ.CZY_WAL || _kwrabj:=(_kwrabj*_krs)$2 ?};
                   _cpr+_kwrabj
                ?};
         OFP.WN:=(OFP.IL*_cena)$2
      ?}
   ?};
   _wv:=(OFP.WN*_vat)$2;
   OFP.WB:=(OFP.WN+_wv)$2
?};

OFP.MKW:=OFP.MPR:=0;
OFP.MKW:=OFP.WN - ((OFP.CZAK*OFP.IL)+OFP.KOSZT);
{? (OFP.CZAK*OFP.IL)<>0
|| OFP.MPR:=({? OFP.WN<>0 || ((OFP.WN-(OFP.CZAK*OFP.IL+OFP.KOSZT))*100)/OFP.WN || 0 ?})$2
|| OFP.MPR:=100
?};
1


\ceny2rab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: obliczenie cen po rabacie
::  OLD: \ceny2rab/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_rabat:=_kwrab:=_kwrabj:=0;

exec('st_licz_wz','ceny','OFE');

{? exec('czyrabp','ceny',TAZ.RAB_TYP)
|| _rabat:=1-exec('rab_proc','ceny',OFP.RAB,TAZ.RAB,TAZ.RAB_TYP)/100
|| _dokl_c:=2;
   _cena:={? TAZ.CZY_WAL || OFP.CWAL || OFP.C ?};
   _kwil:=OFP.IL;
   _nagwsp:=-TAZ.RAB/100;
   _kwpoz:=-OFP.RABK;
   _kwrabj:=
      {? TAZ.RAB_TYP='W'
      || {? _kwil || (_kwpoz/_kwil)$_dokl_c ?}+(_nagwsp*_cena)$_dokl_c
      || _kwpoz+(_nagwsp*_cena)$_dokl_c
      ?}
?};

_walj:={? TAZ.CZY_WAL || exec('FindInSet','#table','WAL','WAL_SYM',TAZ.WAL().KOD,,"WAL.J",,,1) || 1 ?};
_krs:={? TAZ.CZY_WAL || TAZ.KRS/_walj || 1 ?};

_cpr:={? TAZ.CZY_WAL || OFP.CWAL || OFP.C ?};

{? OFP.SV<>null
||
   _vat:=#((OFP.SV().KOD*'%')+OFP.SV().KOD-1)/100;
   {? TAZ.CB='T'
   ||
      OFP.CB:=
         {? exec('czyrabp','ceny',TAZ.RAB_TYP)
         || OFP.C*_rabat $2
         || ((_cpr+_kwrabj)*_krs)$2
         ?};
      OFP.CN:=(OFP.CB/(1+_vat))$OFP.M().DOKL_S
   ||
      OFP.CN:=
         {? exec('czyrabp','ceny',TAZ.RAB_TYP)
         || OFP.C*_rabat$OFP.M().DOKL_S
         || ((_cpr+_kwrabj)*_krs)$OFP.M().DOKL_S
         ?};
      OFP.CB:=OFP.CN*(1+_vat)$OFP.M().DOKL_S
   ?}
||
   OFP.CN:=
      {? exec('czyrabp','ceny',TAZ.RAB_TYP)
      || OFP.C*_rabat$OFP.M().DOKL_S
      || ((_cpr+_kwrabj)*_krs)$OFP.M().DOKL_S
      ?};
   OFP.CB:=OFP.CN
?};
1


\add_gra2dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: dodaje gratis do dokumentu
::   WE: _a - tabela
::  OLD: \add_gra2dok/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;
_dok:=_Tab.DOK;
_mat:=exec('FindAndGet','#table','M',_Tab.GRATIS,,,null());
_sv:=exec('m_vat','material',_mat,null,exec('exp_ue','typysp'),ST.AR,ST.AM);
_tap:=exec('FindAndGet','#table','TAP',_Tab.TAP,,,null());
_wal:=exec('FindAndGet','#table','SLO',_Tab.WAL,,,null());
_jm:=exec('FindAndGet','#table','JM',_Tab.JM,,,null());
_tap_il:=exec('FindAndGet','#table','TAP',_Tab.TAP,,"TAP.IL",0);
_tap_ild:=exec('FindAndGet','#table','TAP',_Tab.TAP,,"TAP.IL_DO",0);
_tap_war:=exec('FindAndGet','#table','TAP',_Tab.TAP,,"TAP.WAR",0);
_tap_wad:=exec('FindAndGet','#table','TAP',_Tab.TAP,,"TAP.WAR_DO",0);
_tap_od:=exec('FindAndGet','#table','TAP',_Tab.TAP,,"TAP.OD",date(0,0,0));
_tap_do:=exec('FindAndGet','#table','TAP',_Tab.TAP,,"TAP.DO",date(0,0,0));
_tms:=FIRMA.tm_stamp;

{? 6+_dok='oferty'
||
   OFE.cntx_psh;
   OFE.prefix;
   {? OFE.seek(_dok)
   ||
      OFP.blank();
      OFP.M:=_mat;
      OFP.IL:=_Tab.GRA_IL;
      {? OFE.WAL<>exec('bl_nwal','ustawienia')
      || OFP.CWAL:=_Tab.GRA_CEN
      || OFP.C:=_Tab.GRA_CEN
      ?};
      OFP.SV:=_sv;
      exec('licz','ceny',OFP);
      OFP.TAR_H:=exec('tar_h','ceny_dok','OFP',_tap,null,_wal,_jm,_tap_il,_tap_war,null,0,0,0,'N',null
         ,_tap_od,_tap_do,time(0,0,0),time(0,0,0),_tap_ild,_tap_wad,_Tab.GRA_IL,_Tab.GRA_CEN);
      OFP.TAR_TMS:=_tms;
      OFP.add()
   ?};
   OFE.cntx_pop
|? 5+_dok='zknag'
|| exec('utw_zk_tymc','zamsiw_wspolne');
   ZK_N.cntx_psh;
   ZK_N.prefix;
   {? ZK_N.seek(_dok)
   ||
      BEER.ZK_N:=ZK_N.ref;
      ZK_P.prefix();
      ZK_P.blank();
      ZK_P.SV:=exec('m_vat','material',M.ref(),ZK_N.KH,,ZK_N.DP~1,ZK_N.DP~2,,,,,ZK_N.KRAJ_VAT);
      ZK_P.POJAZD:=ZK_N.POJAZD;
      ZK_P.DT:=ZK_N.DT;
      ZK_P.ILZ:=ZK_P.ILP:=_Tab.GRA_IL;
      _war:={? date<(ZK_P.DT+1) || exec('get','#params',3001,2,OPERATOR.USER) || 'N' ?};
      ZK_P.REZ:={? _war='T' || 1 |? _war='N' || 0 || 1 ?};
      {? ZK_P.REZ
      || ZK_P.DOR:=ZK_P.ILP;
         ZK_P.ODDT:={? ZK_P.N().DP>date() || ZK_P.N().DP || date() ?};
         ZK_P.DODT:=ZK_P.DT
      ?};
      ZK_P.RR:=ZK_N.RR;
      ZK_P.RM:=ZK_N.RM;
      ZK_P.RT:=ZK_N.RT;
      ZK_P.MG:={? ZK_P.ONLY | ZK_P.RMAG<>null || ZK_P.RMAG || null ?};
      ZK_P.ZL:=null;
      ZK_P.M:=_mat;
      ZK_P.JM:=_jm;
      {? ZK_N.WAL<>exec('bl_nwal','ustawienia') || ZK_P.CWAL:=_Tab.GRA_CEN || ZK_P.CENA:=_Tab.GRA_CEN ?};
      ZK_P.WS2:=1; ZK_P.J2:=ZK_P.JM; ZK_P.T2:='A'; ZK_P.IL2:=ZK_P.ILZ;
      ZK_P.TAR_H:=exec('tar_h','ceny_dok','ZK_P',_tap,null,_wal,_jm,_tap_il,_tap_war,null,0,0,0,'N',null
         ,_tap_od,_tap_do,time(0,0,0),time(0,0,0),_tap_ild,_tap_wad,_Tab.GRA_IL,_Tab.GRA_CEN);
      ZK_P.TAR_TMS:=_tms;
      ZK_P.KH:=ZK_P.N().KH;
      ZK_P.KH_ODB:=ZK_P.N().ODB;
      {? ZK_P.add
      || __uzup_z(ZK_P.M);
         exec('inf_dod','fakso',0,'zkpoz');
         ZK_P.NR:=exec('blnr_kol','zamsiw_poz',ZK_P.M);
         {? ZK_P.REZ
         || ZK_P.ILR:=ZK_P.ILP:=ZK_P.ILZ; ZK_P.DOR:=0
         || ZK_P.ILP:=ZK_P.ILZ; ZK_P.ILR:=0; ZK_P.DOR:=0
         ?};
         exec('licz','ceny',ZK_P);
         ZK_P.put(1);
         {? ZK_P.REZ
         || ZK_P.REZ:=0; ZK_P.put(1); exec('rez_pozy','rezerwacje',0,,,,,0);
            exec('aktznzkp','zamsiw_poz',ZK_P.ref(),1)
         || _zkp:=tab_tmp(1,'MAT','STRING[16]','','ZKP','STRING[16]','');
            _zkp.blank(); _zkp.MAT:=$ZK_P.M; _zkp.ZKP:=$ZK_P.ref(); _zkp.add(1);
            exec('aktu_rez','rezerwacje',ZK_P.M,ZK_P.NR,ZK_P.ILZ,$ZK_P.N,_zkp);
            obj_del(_zkp)
         ?};
         exec('obl_warz','zamsiw_nag',ZK_P.N)
      ?}
   ?};
   ZK_N.cntx_pop
|? 5+_dok='faktu'
||
   FAKS.cntx_psh;
   FAKS.prefix;
   {? FAKS.seek(_dok)
   ||
      _sv:=exec('m_vat','material',_mat,null,exec('exp_ue','typysp'),ST.AR,ST.AM,,,,,FAKS.KRAJ_VAT);
      {? FAKS.WAL<>FAKS.NWAL
      || exec('ADDPOZF','faktury_poz',_mat,_Tab.GRA_IL,0,0,_sv,,,_Tab.GRA_CEN,FAKS.KRS,FAKS.WAL)
      || exec('ADDPOZF','faktury_poz',_mat,_Tab.GRA_IL,_Tab.GRA_CEN,0,_sv)
      ?};
      FAP.J2:=FAP.JM;
      FAP.T2:='A';
      FAP.WS2:=1;
      FAP.IL2:=FAP.IL;
      FAP.TAR_H:=exec('tar_h','ceny_dok','FAP',_tap,null,_wal,_jm,_tap_il,_tap_war,null,0,0,0,'N',null
         ,_tap_od,_tap_do,time(0,0,0),time(0,0,0),_tap_ild,_tap_wad,_Tab.GRA_IL,_Tab.GRA_CEN);
      FAP.TAR_TMS:=_tms;
      {? FAKS.WHERE='G' || FAP.POZF:=__ts ?};
      {? FAP.put()
      ||
::       naliczenie opłat dodatkowych
         exec('actTAXS','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),FAP.M,null());
         exec('inf_dod','fakso',0,'fakpo')
      ?}
   ?};
   FAKS.cntx_pop
|? 5+_dok='nagdo'
||
   ND.cntx_psh;
   ND.prefix;
   {? ND.seek(_dok)
   ||
      _sv:=exec('m_vat','material',_mat,null,exec('exp_ue','typysp'),ST.AR,ST.AM,,,,,ND.KRAJ_VAT);
      SLO.cntx_psh;
      SLO.prefix;
      _wsp:=1+{? SLO.seek(_sv) || (#((SLO.KOD*'%')+SLO.KOD-1))*0.01 || 0 ?};
      SLO.cntx_pop;
      _ref:=exec('adddk','magdok_poz',ND.ref,_mat,_Tab.GRA_IL,0,date(0,0,0),0,_Tab.GRA_CEN,(_Tab.GRA_CEN*_wsp)$2,,_sv,,,7,0,'');
      DK.cntx_psh;
      DK.prefix;
      {? DK.seek(_ref)
      ||
         DK.J2:=_jm;
         DK.T2:='A';
         DK.WS2:=1;
         TAZ.IL:=DK.IL2:=DK.IL;
         DK.KURS:=ND.KRS;
         {? ND.WAL<>ND.NWAL
         || DK.CWAL:=_Tab.GRA_CEN; exec('po_cenaw','ceny',,0)
         || exec('licz','ceny',DK)
         ?};
         DK.TAR_H:=exec('tar_h','ceny_dok','DK',_tap,null,_wal,_jm,_tap_il,_tap_war,null,0,0,0,'N',null
            ,_tap_od,_tap_do,time(0,0,0),time(0,0,0),_tap_ild,_tap_wad,_Tab.GRA_IL,_Tab.GRA_CEN);
         DK.TAR_TMS:=_tms;
         {? DK.put || exec('inf_dod','fakso',0,'dokma') ?};
         exec('update','rezerwacje','DK',DK.ref(),ND.MAG,DK.M,DK.IL,DK.SRDK);
         exec('obl_stan','magazyn_stan',DK.M,1,ND.MAG)
      ?};
      DK.cntx_pop
   ?};
   ND.cntx_pop
?}


\biez_cen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [Mario] [2008]
:: OPIS: dodanie jednej pozycji do arkusza
::   WE: _a - ref indeksu materialowego M.ref
::       [_b] - ref magazynu (domyslnie ST.MAG)
::       [_c] - data na kiedy ma byc cena (do kiedy ma szukac) (domyslnie data biezaca)
::       [_d] - czy gdy nie ma przeceny szukac ceny w tabeli SC (0-tak, 1-nie, 2-nie tworzyc nowej,
::              3-sprawdzenie zaistnienia ceny - przecena lub dostawa) (domyslnie 0)
::       [_e] - godzina na kiedy ma byc cena (do kiedy ma szukac) (domyslnie zerowa)
::       [_f] - 1-jeśli jest stan i cena 0 to zwraca -1, 0-domyślnie bez dodatkowego rezultatu
::   WY: cena lub jesli _d=3 zwraca -1 jesli jest przecena lub dostawa
::  OLD: \biez_cen/pomoc.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>7 || _b:=ST.MAG ?} || _b:=ST.MAG ?};
{? _>=3 || {? type_of(_c)<>4 || _c:=date() ?} || _c:=date() ?};
{? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
_time:={? var_pres('_e')=type_of(time()) || _e || time(0,0,0) ?};
_ctrldst:={? var_pres('_f')=type_of(0) || _f || 0 ?};

_dokl_c:=exec('FindAndGet','#table',M,#_a,ref_name(_a),"DOKL_C",0);
_wyn:=0;

INN.cntx_psh();
INP.cntx_psh();
INX.cntx_psh();
INY.cntx_psh();
INN.use('inw_nwyc');
INP.use('inw_pwyc');
INX.use('inw_xwyc');
INY.use('inw_ywyc');
INP.index('MG');
INP.prefix(_b,_a);
{? INP.last
|| {!
   |?
      {? INP.IN().D<>date(0,0,0) & INP.SE<>0 & INP.IN().D<=_c
      || _wyn:={? _d=3 || -1 || INP.SE ?};
         0
      || INP.prev & _wyn>=0
      ?}
   !}
?};
{? _wyn=0 & _d<>1
||
   {? _d=0
   || {? ~(var_pres('__sc')>100 & __sc.first() & __sc.MAT=$_a & __sc.MAG=$_b)
      || exec('sc_tymczas','magazyn_stan',_b,_a,'',,,0)
      ?};
      _wyn:=exec('cena2dat','ceny_dok',_b,_a,_c,_time);
      {? _ctrldst & _wyn=0 & __sc.first() || _wyn:=-1 ?}
   || __sc.cntx_psh();
      {? __sc.first || _wyn:={? _d=3 || -1 || __sc.C ?} ?};
      {? _ctrldst & _wyn=0 & __sc.first() || _wyn:=-1 ?};
      __sc.cntx_pop()
   ?}
?};
INP.cntx_pop();
INN.cntx_pop();
INX.cntx_pop();
INY.cntx_pop();
_wyn$_dokl_c


\cena2dat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: cena z dostawy do danej daty
::   WE: _a - magazyn
::       _b - ref materialu
::       _c - data
::   WY: cena
::  OLD: \cena2dat/pomoc.fml
::----------------------------------------------------------------------------------------------------------------------
_time:={? var_pres('_e')=type_of(time()) || _e || time(0,0,0) ?};

_wyn:=0;
_odr:=_c~1;

SC.cntx_psh();
DK.cntx_psh();
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.last()
||
   {!
   |?
      {? OKR.ROK<=_odr | ~_odr
      || SC.use('stc__'+ST.ODDZ+($OKR.ROK+2));
         SC.index('SC');
         SC.prefix(_a,_b);
         {? SC.last()
         || DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
            {!
            |? _timd:=exec('FindAndGet','#table',DK,SC.SRDK,
                       ,"ND.cntx_psh();ND.use('nagdo'+(name()+3));_rr:=N().T;ND.cntx_pop();_rr",time(0,0,0));
               {? SC.D<_c | (SC.D=_c & (_time=time(0,0,0) | _timd<=_time))
               || _wyn:=exec('cena_mag','ceny',SC.M,SC.RDK,SC.NDK,SC.MAG,SC.C);
                  0
               || SC.prev()
               ?}
            !}
         ?}
      ?};
      ~_wyn & OKR.prev()
   !}
?};

OKR.cntx_pop();
DK.cntx_pop();
SC.cntx_pop();
_wyn


\ost_cena
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: zwraca ostatnia cena magazynowa dla podanego dostawcy i materialu
::       dla ewidencyjnego cene ewidencyjna
::   WE:  _a  - ref() materialu
::        _b  - ref() kontrahenta
::  OLD: \ost_cena/magazyn2.fml
::----------------------------------------------------------------------------------------------------------------------
_tmg:={? ST.MAG().TYP*'EWI' || 'E' || (1+ST.MAG().TYP) ?};
_cen:=0;
{? 'E'*_tmg || _cen:=exec('biez_cen','ceny_dok',DK.M,ST.MAG,DK.N().D,,DK.N().T) ?};
_msk:=6+DK.name();
_wyn:=0;
DK.cntx_psh();
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.last()
|| {!
   |? _ok:=0;
      DK.use(_msk+($OKR.ROK+2));
      DK.index('KH');
      DK.prefix(_b,'T',_a,'T');
      {? DK.last()
      || {!
         |? {? ~('E'*_tmg) & DK.C>0
            || _ok:=1;
               0
            || DK.prev()
            ?}
         !}
      ?};
      {? _ok
      || _wyn:=exec('cena_mag','ceny',DK.M,DK.RDK,DK.NDK,,DK.C,,,,0)
      ?};
      ~_wyn & OKR.prev()
   !}
?};
DK.cntx_pop();
OKR.cntx_pop();
{? ~_wyn || _wyn:=_cen ?};
{? DK.fld_read('C') || DPOZ.OCZ:=_wyn || DPOZ.OCZ:=0 ?};
''


\cen2pal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: poprawia ceny na pozycji palety
::   WE: [_a] - stara cena
::       [_b] - nowa cena
::       [_c] - cecha
::  OLD: \cen2pal/mws.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=0;
{? _>=1 || {? type_of(_a)<>1 || _a:=0 || _par:=1 ?} || _a:=0 ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 || _par:=1 ?} || _b:=0 ?};
{? _>=3 || {? type_of(_c)<>7 || _c:=null || _par:=1 ?} || _c:=null ?};

_mat:=DK.M;
_ok:=0;
_ref:=DK.ref();

{? ~_par
|| _old:=0;
   _new:=DK.C;
   DK.cntx_psh();
   DK.clear();
   {? DK.seek(_ref)
   || _ok:=1;
      _old:=DK.C
   ?};
   DK.cntx_pop()
|| _old:=_a;
   _new:=_b;
   _ok:=1
?};
{? _ok & _old<>_new
|| DK_L.cntx_psh();
   DK_L.index('DK');
   DK_L.prefix(_ref,null);
   {? DK_L.first()
   || {!
      |? {? DK_L.PAL<>null
         || {? DK_L.PAL().AKT='N'
            || PAL_POZ.use('palet'+form(PAL.AR-2000,-2,0,'99'))
            || PAL_POZ.use('paletyp')
            ?};
            PAL_POZ.index('PAL');
            PAL_POZ.prefix(PAL.ref());
            {? PAL_POZ.first()
            || {!
               |? {? PAL_POZ.M=_mat & PAL_POZ.C=_old
                  || PAL_POZ.C:=_new;
                     {? _c<>null || PAL_POZ.DK_C:=_c ?};
                     PAL_POZ.put(1)
                  ?};
                  PAL_POZ.next()
               !}
            ?}
         ?};
         DK_L.next()
      !}
   ?};
   DK_L.cntx_pop()
?}


\cena
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: wyznacza cene dla dokumentu tworzonego z wniosku celnego
::   WE: _a - DK.reference
::       _b - kurs
::       _c - wartosc
::       _d - ilosc
::   WY: cena
::  OLD: \cena/koszty.fml
::----------------------------------------------------------------------------------------------------------------------
_dk:=_a;
_krs:=_b;
_war:=_c;
_il:=_d;

_koszt:=0;

FAP_K.cntx_psh(); FAKS_K.cntx_psh();
FAP_K.index('DKSQL'); FAP_K.prefix($_dk);
{? FAP_K.first() || {! |? {? FAP_K.FAKS_K().SC='T' || _koszt+=FAP_K.WB ?}; FAP_K.next() !} ?};
FAP_K.cntx_pop(); FAKS_K.cntx_pop();
_cena:=(_war+_koszt*_krs)$2;
_cena:=(((_cena$0 * (DK.CLO().CLO/100)) +_cena$2)/DK.IL)$2;
_cena


\add_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: wyznacza cene dla dokumentu magazynowego
::   WE: _a - 'cn' - cena netto 'cb'-cena brutto
::   WY: cena
::  OLD: \add_fak/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
exec('st_licz_wz','ceny','FAKS');
exec('stplicz','ceny',DK.IL2);
{? _a='cn'
:: cena netto
|| exec('ceny_mat','ceny_mat',DK.M,DK.N().KH,'DK.CN'
      ,ND.KH_ODB,ST.MAG,'DK.RAB','N',DK.WAL,'DK.CWAL'
      ,,0,{? KALK.J2<>null || KALK.J2().KOD || '' ?},{? KALK.J2<>null || KALK.WS2 || 0 ?}
      ,0,,,0)
:: cena brutto
|| exec('ceny_mat','ceny_mat',DK.M,DK.N().KH,'DK.CB'
      ,ND.KH_ODB,ST.MAG,'DK.RAB','N',DK.WAL,'DK.CWAL'
      ,,1,{? KALK.J2<>null || KALK.J2().KOD || '' ?},{? KALK.J2<>null || KALK.WS2 || 0 ?}
      ,0,,,0
      ,exec('m_vat','material',DK.M,FAKS.KH,exec('exp_ue','typysp'),FAKS.AR,FAKS.AM,DK.SV,2,!__zmst,,FAKS.KRAJ_VAT))
?}


\taz_drrab_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po redakcji TAZ.DR_RAB
::  OLD: \taz_drrab_ae/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
TAZ.DR_RAB:=TAZ.DR_RAB$2;
TAZ.DR_WARO:=TAZ.DR_WARD-TAZ.DR_RAB;
1


\taz_drwaro_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po redakcji TAZ.DR_WARO
::  OLD: \taz_drwaro_ae/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
TAZ.DR_WARO:=TAZ.DR_WARO$2;
TAZ.DR_RAB:=TAZ.DR_WARD-TAZ.DR_WARO;
1


\fap_zk_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: wyswietlenie informacji o rozpisaniu ilosci z faktury po magazynach
::  OLD: \fap_zk_p/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
REZ.clear;
REZ.index('FAPR');
{? REZ.size || REZ.prefix($FAP.ref(),) || REZ.prefix('xx','xx') ?};
REZ.first;
grp_disp(REZ,'FAP')


\tar_khodb_jest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Sprawdza, czy dany odbiorca kontrahenta ma przypisany jakikolwiek cennik sprzedaży
::   WE: _a - odbiorca kontrahenta, KH_ODB.ref()
::   WY: 0 - nie, 1 - tak
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
{? _a<>null()
|| TAR.cntx_psh();
   TAR.index('S3');
   TAR.prefix(_a);
   {? TAR.first()
   || {!
      |? {? TAR.SD='S'
         || _jest:=1
         ?};
         ~_jest & TAR.next()
      !}
   ?};
   TAR.cntx_pop()
?};
_jest


\khodb_tar_zm_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Sprawdza, czy zmiana odbiorcy kontrahenta może spowodować zmianę cennika sprzedaży
::   WE: _a - odbiorca kontrahenta, KH_ODB.ref() - przed zmianą
::       _b - odbiorca kontrahenta, KH_ODB.ref() - po zmianie
::   WY: 0 - nie, 1 - tak
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? _a<>_b
|| _wyn:=exec('tar_khodb_jest','ceny_dok',_a) | exec('tar_khodb_jest','ceny_dok',_b)
?};
_wyn


\ceny_min
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_do:={? var_pres('_b')=type_of(date()) || _b || date() ?};
_il_dni:={? var_pres('_c')=type_of(0) || _c || 30 ?};
_od:={? var_pres('_a')=type_of(date()) || _a || _do-_il_dni ?};

_rok_od:={? _il_dni || _od~1 || _od~1 ?};
_rok_do:=_do~1;

_env:=exec('ceny_min_env','ceny_dok');
_env.OD:=_od;
_env.DO:=_do;
_env.IL_DNI:=_il_dni;
_env.ROK_OD:=_rok_od;
_env.ROK_DO:=_rok_do;

{? _env.initObszar() || _env.select() ?}


\ceny_min_env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie - środowisko
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablice i komentować poszczególne jej elementy
::       powiedzmy, że to bedzie pole
         _fld:="31+form(_a)";
::       powiedzmy, że to bedzie metoda
         _mth:="31+form(_a)";

_env:=obj_new(
::             pola
                _fld('OD',                   'data początku analiz')
               ,_fld('DO',                   'data końca analizy')
               ,_fld('IL_DNI',               'ilość dni analizy')
               ,_fld('ROK_OD',               'rok początku analizy')
               ,_fld('ROK_DO',               'rok końca analizy')
               ,_fld('TAB',                  'dane')

::             METODY
               ,_mth('initObszar',           'Init obszaru')
               ,_mth('select',               'Wyświetlenie danych')
               );
_env.initObszar:="params_set('env',.); exec('ceny_min_env_init','ceny_dok')";
_env.select:="params_set('env',.); exec('ceny_min_select','ceny_dok')";
_env


\ceny_min_env_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie - środowisko - init
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_env.TAB:=tab_tmp(3
   ,'KTM','STRING[50]','Indesk'@
   ,'WAL','STRING[3]','Waluta'@
   ,'TYP','STRING[10]','Typ'@
   ,'N','STRING[100]','Nazwa'@
   ,'CN','REAL','Min. cena netto'@
   ,'CB','REAL','Min. cena brutto'@
);
params_exec('ceny_min_cen','ceny_dok');
params_exec('ceny_min_dok','ceny_dok');
1


\ceny_min_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie - środowisko - select
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_Tab:=_env.TAB;
_od:=_env.OD;
_do:=_env.DO;

{? _Tab.first()
||
   _wer:=_Tab.mk_sel('Najniższe ceny w okresie od %1 do %2'@[$_od,$_do],,0,'#mincenwokr',,,,,'U');
   _Tab.win_fld(_wer,,'KTM',,,20,,,'Indeks'@);
   _Tab.win_fld(_wer,,'N',,,20,,,'Nazwa'@);
   _Tab.win_fld(_wer,,'WAL',,,3,,,'Waluta'@);
   _Tab.win_fld(_wer,,'TYP',,,10,,,'Typ'@);
   _Tab.win_fld(_wer,,'CN',,,20,ST.DOKL_S,,'Min. cena netto'@);
   _Tab.win_fld(_wer,,'CB',,,20,ST.DOKL_S,,'Min. cena brutto'@);
   _fb:="params_exec('ceny_min_szcz','ceny_dok')";
   _Tab.win_act(_wer,,'Formuła','Więcej danych'@,,,_fb,,1);
   _Tab.win_sel(_wer);
   params_set(params_get());
   _Tab.select()
?}


\ceny_min_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie - pobranie danych z dokumentów
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_Tab:=_env.TAB;
_od:=_env.OD;
_do:=_env.DO;
_rok_od:=_env.ROK_OD;
_rok_do:=_env.ROK_DO;

_typ:='dokument';
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
_loop:=OKR.last();
{!
|? _loop
|!
   {? _rok_od<=OKR.ROK & OKR.ROK<=_rok_do
   ||
      FAKS.cntx_psh();
      FAKS.use(gsub(FAKS.name(1),'???',ST.ODDZ+(($OKR.ROK)+2)));
      FAP.cntx_psh();
      FAP.use(gsub(FAP.name(1),'???',ST.ODDZ+(($OKR.ROK)+2)));
      FAP.index('ALL');
      FAP.prefix('S');
      _loop:=FAP.find_le(_do);
      {!
      |? _loop
      |!
         {? FAP.FAKS & FAP.IL & FAP.CN
         ||
            FAP.FAKS();
            {? FAKS.DW<_od
            ||
:: kończymy bo dokument spoza zakresu dat
               _loop:=0

            |? FAKS.LKSQL<>'' | FAKS.AKC<>'T'
            ||
:: pomijamy:
:: - korekty
:: - niezakończone dokumenty
               ~~

            |? FAKS.DW<=_do
            ||
:: dokument w zakresie dat
               _walh:=FAP.WAL_H().KOD;
               _walo:=FAP.WAL_P().KOD;
:: zapis dla waluty opodatkowania
               _ktm:=FAP.M().KTM;
               {? _Tab.find_key(_ktm,_walo,_typ,)
               ||
                  {? _Tab.CN>FAP.CN
                  ||
                     _Tab.CN:=FAP.CN;
                     _Tab.CB:=FAP.CB;
                     _Tab.put()
                  ?}
               ||
                  _Tab.KTM:=M.KTM;
                  _Tab.WAL:=_walo;
                  _Tab.TYP:=_typ;
                  _Tab.N:=M.N;
                  _Tab.CN:=FAP.CN;
                  _Tab.CB:=FAP.CB;
                  _Tab.add()
               ?};
:: zapis dla waluty handlowej
               {? _walh<>_walo
               ||
                  _cn:=(FAP.WWAL/FAP.IL)$M.DOKL_S;
                  {? _Tab.find_key(_ktm,_walh,_typ,)
                  ||
                     {? _Tab.CN>FAP.CN
                     ||
                        _Tab.CN:=_cn;
                        _Tab.CB:=(FAP.BWAL/FAP.IL)$M.DOKL_S;
                        _Tab.put()
                     ?}
                  ||
                     _Tab.KTM:=M.KTM;
                     _Tab.WAL:=_walh;
                     _Tab.TYP:=_typ;
                     _Tab.N:=M.N;
                     _Tab.CN:=_cn;
                     _Tab.CB:=(FAP.BWAL/FAP.IL)$M.DOKL_S;
                     _Tab.add()
                  ?}
               ?}
            ?}
         ?};
         _loop:=FAP.prev()
      !};
      FAP.cntx_pop();
      FAKS.cntx_pop()
   ?};
   _loop:=OKR.prev()
!};
OKR.cntx_pop()


\ceny_min_cen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie - pobranie danych z cenników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_Tab:=_env.TAB;
_od:=_env.OD;
_do:=_env.DO;
_rok_od:=_env.ROK_OD;
_rok_do:=_env.ROK_DO;

_typ:='cennik';
TAP.cntx_psh();
TAP.use(gsub(TAP.name(1),'?','')+'__');
TAP.index('A_M');
TAP.prefix(ST.ODDZ,'S','C');
_loop:=TAP.first();
{!
|? _loop
|!
   {? TAP.M & TAP.JM=TAP.M().J
         &
      (  _od<=TAP.OD & TAP.OD<=_do | TAP.OD<_od & TAP.DO=date(0,0,0)
            |
         _od<=TAP.DO & TAP.DO<=_do
      )  &
      TAP.IL=0 & TAP.IL_DO=0 & TAP.WAR=0 & TAP.WAR_DO=0 & TAP.PL=null()
         &
      TAP.CEN_ZAKR='N' & TAP.FO_CEN=null() & TAP.FO_RAB=null() & TAP.PRC=0 & TAP.RABNET=0 & TAP.RABBRT=0
         &
      TAP.CEN
   ||
      _wal:=TAP.WAL().KOD;
      {? _Tab.find_key(TAP.M().KTM,_wal,_typ,)
      ||
         {? _Tab.CN>TAP.CEN
         ||
            _Tab.CN:=TAP.CEN;
            _Tab.CB:=TAP.CENB;
            _Tab.put()
         ?}
      ||
         _Tab.KTM:=M.KTM;
         _Tab.WAL:=_wal;
         _Tab.TYP:=_typ;
         _Tab.N:=M.N;
         _Tab.CN:=TAP.CEN;
         _Tab.CB:=TAP.CENB;
         _Tab.add()
      ?}
   ?};
   _loop:=TAP.next()
!};
TAP.cntx_pop()


\ceny_min_szcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie - Więcej danych
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_Tab:=_env.TAB;
{? _Tab.TYP='cennik' || params_exec('ceny_min_szcz_cen','ceny_dok')
|? _Tab.TYP='dokument' || params_exec('ceny_min_szcz_dok','ceny_dok')
|| FUN.info('Nie obsługiwany typ.'@)
?}


\ceny_min_szcz_cen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie - Więcej danych - cennik
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_Tab:=_env.TAB;
_od:=_env.OD;
_do:=_env.DO;

_mat:=exec('FindInSet','#table','M','MATKTM',_Tab.KTM,,,1,,null());
_jm:=exec('FindAndGet','#table',M,_mat,,"M.J",null());

TAP.cntx_psh();
TAP.use(gsub(TAP.name(1),'?','')+'__');
TAP.prefix;
TAP.f_clear();

_sort:=
   'OD';
_from:=
   'join TAR using("TAP".TAR,TAR.reference) '
   'join M using("TAP".M,M.reference) ';
_where:=
   'TAR.SD=\'S\' and TAR.ODDZ=\':_e\' and TAP.RODZ=\'C\''+
   ' and TAP.M=:_d and TAP.JM=:_a'+
   ' and (  to_date(:_b)<=TAP.OD and TAP.OD<=to_date(:_c)'+
            ' or TAP.OD<to_date(:_b) and (TAP.DO is null)'+
            ' or to_date(:_b)<=TAP.DO and TAP.DO<=to_date(:_c)'+
   '     )'+
   ' and TAP.IL=0 and TAP.IL_DO=0 and TAP.WAR=0 and TAP.WAR_DO=0 and (TAP.PL is null)'+
   ' and TAP.CEN_ZAKR=\'N\' and (TAP.FO_CEN is null) and (TAP.FO_RAB is null)'+
   ' and TAP.PRC=0 and TAP.RABNET=0 and TAP.RABBRT=0 and TAP.CEN<>0';
TAP.f_set(_sort,_from,_where
   ,'_a:='; _jm
   ,'_b:='; _od
   ,'_c:='; _do
   ,'_d:='; _mat
   ,'_e:='; ST.ODDZ);

{? ~TAP.f_first()
||
   FUN.info('Brak danych.',ST.MSG)
||
   TAZ.SD:='S';
   exec('tap_okno','ceny');
   _wer:=TAP.mk_sel('Pozycje cenników'@,,0,'cenminszczcen',5,10,15,,'U');
   TAP.win_sel(_wer);
   TAP.win_fld(_wer,,'OD',,,10,,1,'Od'@);
   TAP.win_fld(_wer,,'DO',,,10,,1,'Do'@);
   TAP.win_fld(_wer,,'TAR','KOD',,20,,1,'Cennik'@);
   TAP.win_fld(_wer,,'M','KTM',,20,,1,'Indeks'@);
   TAP.win_fld(_wer,,'CEN',,,20,,1,'Cena netto'@);
   TAP.win_fld(_wer,,'CENB',,,20,,1,'Cena brutto'@);
   _fb:="params_exec('kol_tap','ceny',_a)";
   TAP.win_act(_wer,,'Rekord',,,,_fb);
   TAP.select()
?};
TAP.cntx_pop()


\ceny_min_szcz_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie - Więcej danych - dokument
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_Tab:=_env.TAB;
_rok_od:=_env.ROK_OD;
_rok_do:=_env.ROK_DO;

_wal:=_Tab.WAL<>FAP.WAL().KOD;

_wer:=FAP.mk_sel('Dokumenty'@,,0,'cenminszczfap',5,10,15,,'U');
FAP.win_fld(_wer,,'FAKS','SYM',,20,,1,'Symbol'@);
FAP.win_fld(_wer,,'FAKS','DW',,10,,1,'Data'@);
FAP.win_fld(_wer,,'POZ',,,5,,1,'Poz.'@);
FAP.win_fld(_wer,,'M','N',,35,,1,'Nazwa materiału'@);
FAP.win_fld(_wer,,'IL',,,12,4,1,'Ilość'@);
{? _wal
||
   FAP.win_fld(_wer,,'CWAL',,,12,4,1,'Cena przed rabatem'@)
||
   FAP.win_fld(_wer,,'CPR',,,12,4,1,'Cena przed rabatem'@);
   FAP.win_fld(_wer,,'CN',,,12,4,1,'Cena netto'@);
   FAP.win_fld(_wer,,'CB',,,12,4,1,'Cena brutto'@)
?};
FAP.WAL:=exec('bl_wal','ustawienia');
_facr:={? _wal || 'WWAL' || 'WWAL_P' ?};
FAP.win_fld(_wer,,_facr,,,12,2,1,'Wartość netto'@);
FAP.win_act(_wer,0,'Wyświetl',,,,"exec('disp_fap','faktury_poz')",,0);

_wer_gr:=FAP.grp_make('Pozycje dokumentów'@,,'cenminszczdok');
{! _ii:=_rok_do // -1 .. _rok_od
|!
   _fb:=($'
      params_exec(\'ceny_min_szcz_dok_fb\',\'ceny_dok\',\'%1\')
   '[$_ii+2]);
   FAP.grp_sel(_wer_gr,,_wer,$_ii,,,,,_fb)
!};

exec('fak_psh','open_tab');
FAP.f_clear();
FAP.win_sel(_wer_gr);
params_set(params_get());
FAP.select();
exec('fak_pop','open_tab')


\ceny_min_szcz_dok_fb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Najniższe ceny w okresie - Więcej danych - dokument - formuła przed
::   WE: _a - rok do maski
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_rok:=_a;

_env:=params_get().env;
_Tab:=_env.TAB;
_od:=_env.OD;
_do:=_env.DO;

_mat:=exec('FindInSet','#table','M','MATKTM',_Tab.KTM,,,1,,null());
_jm:=exec('FindAndGet','#table',M,_mat,,"M.J",null());

exec('fak_open','open_tab',ST.ODDZ,_a);
FAP.f_clear();
FAP.prefix();

_sort:='FAKS(SYM),POZ';
_from:=
   'join FAKS using("FAP".FAKS,FAKS.reference) '+
   'join M using("FAP".M,M.reference) '+
   'join KH using(FAKS.KH,KH.reference)';
_where:=
   'FAKS.SZ=\'S\' and FAKS.AKC=\'T\' and FAKS.LKSQL=\'\''+
   ' and FAKS.DW BETWEEN to_date(:_a) AND to_date(:_b)'+
   ' and (FAP.IL is not null) and (FAP.CN is not null)'+
   ' and FAP.M=:_c';
FAP.f_set(_sort,_from,_where
   ,'_a:='; _od
   ,'_b:='; _do
   ,'_c:='; _mat)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 8fccbace0ec438e58c4cf456f91a91ec6177345a37ff3fb5381b0a3a9bbc9af4cdf72c47367c0c70d3390e8f2f668779389bfc5a643d85c179e948c31c26d068bb5ef27a8317661d277925cec922b441e8068a73c79bdf181a6742f549774506f0221297ccbffb1e34083f628371cfd471b5ce57eee891572164d51fc55c942d

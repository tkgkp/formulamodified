:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: kasa_wplaty.fml
:: Utworzony: 24.05.2016 [17.00]
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły do obsługi wpłat gotówkowych
::======================================================================================================================

\mob_wpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: obsluga wplat gotowkowych dla handlowca
::  OLD: \mob_wpl/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
_windict:=KH.win_dict('?');
_win_sel:=KH.win_sel('?');
AreaTitle.setTabWin(MBWPL,~~);
AreaTitle.setTitle();
KH.win_sel('UDTSLO');
KH.win_dict('UDTSLO');
MBWPL.index('STAN');
MBWPL.prefix(EANX.STNZKR);
_grp:=MBWPL.grp_make('GRP',,,,,"exec('exit','zws',_a)");
MBWPL.grp_sel(_grp,,'WER');
MBWPL.win_sel(_grp);
MBWPL.select();
KH.win_sel(_win_sel);
KH.win_dict(_windict);
~~


\zkrmbwpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: zmiana zakresu wyswietlania wplat gotowkowych
::----------------------------------------------------------------------------------------------------------------------
EANX.win_edit('WPLZKR');
{? EANX.edit() || MBWPL.prefix(EANX.STNZKR) ?}


\pwstwpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: ustawia zmienna dla wplat gotowkowych - stan
::  OLD: \pwstwpl/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
EANX.STWPL:=MBWPL.STAN


\pwstmail
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: ustawia zmienna dla wplat gotowkowych - email
::  OLD: \pwstmail/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
EANX.STEMAIL:=MBWPL.GOEMAIL


\rekwplgt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: rekord przed dla wplat gotowkowych
::  OLD: \rekwplgt/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
:: informacje o fakturze zaliczkowej
exec('mbwpl_actions_grayed','kasa_wplaty');
EANX.SZAL:='';
EANX.DZAL:=date(0,0,0);
{? MBWPL.ZAL<>''
|| FAKS.cntx_psh();
   FAKS.use(form(8+MBWPL.ZAL));
   FAKS.clear();
   {? FAKS.seek(MBWPL.ZAL)
   || EANX.SZAL:=FAKS.SYM;
      EANX.DZAL:=FAKS.DW
   ?};
   FAKS.cntx_pop()
?};
{? MBWPL.GOEMAIL<>'W' & exec('FindInSet','#table','DOKUM','DOKUM',$MBWPL.ref(),REF.FIRMA,"DOKUM.ESEND",,,'')='T'
|| MBWPL.GOEMAIL:='W';
   MBWPL.put(1)
?};
{? MBWPL.STAN='P'
|| 'MBWPL#01#01'
|? MBWPL.STAN='R'
|| 'MBWPL#01#02'
|? MBWPL.STAN='A'
|| 'MBWPL#01#03'
|| ''
?}


\stwplhan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: zmiana statusu wplaty gotowkowej na pobrana
::   WE: _a - ref SQL DOKUMENT.ref()
::  OLD: \stwplhan/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
MBWPL.cntx_psh();
MBWPL.index('RSQL');
MBWPL.prefix(_a,_a);
{? MBWPL.first()
|| {!
   |? _ref:=MBWPL.ref();
      _ok:=MBWPL.next();
      MBWPL.cntx_psh();
      MBWPL.clear();
      {? MBWPL.seek(_ref)
      || MBWPL.STAN:='P';
         MBWPL.RSQL:='';
         MBWPL.put(1)
      ?};
      MBWPL.cntx_pop();
      _ok
   !}
?};
MBWPL.cntx_pop();
_wyn


\drukmwpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: wystawienie potwierdzenia i wydruk wplaty
::  OLD: \drukmwpl/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
_czypot:=MBWPL.NRPTW;
:: w przypadku braku PDF-a dogenerowuje go
{? _czypot & ~(#MBWPL.PTW)
|| _pth:='Potwiedzenie_'+form(MBWPL.NRPTW,-5,0,'99')+form(MBWPL.ROKPTW,4,0,'99')+'.pdf';
   POLA.NAZ_WYDR:='Potwierdzenie_'+form(MBWPL.NRPTW,-5,0,'99')+form(MBWPL.ROKPTW,4,0,'99')+'.pdf';
   _wyn:=rep_exec('kas_dok_ptw_wpl',,0,_pth,1);
   {? _wyn & _pth<>''
   || MBWPL.bl_put('PTW',pth_dir(_pth)+'/'+_pth,0);
      ferase(pth_dir(_pth)+'/'+_pth,0)
   ?}
?};
{? {? ~_czypot
   || FUN.ask('Wystawić i wydrukować potwierdzenie wpłaty gotówkowej?'@)
   || FUN.ask('Wydrukować potwierdzenie wpłaty gotówkowej?'@)
   ?}
|| {? ~_czypot || exec('wystpotw','kasa_wplaty') ?};
   rep_exec('kas_dok_ptw_wpl')
?};
~~


\wystpotw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: wystawienie potwierdzenia
::   WY: 0-nie utworzono PDF-a 1-jest OK
::  OLD: \wystpotw/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
:: sprawdzenie numeracji
NRDOK.index('NRDOK');
NRDOK.prefix('SYS','');
{? ~NRDOK.first()
|| FUN.info('Nie zdefiniowano numeracji SYS dla dokumentów powiązanych.'@)
|| POLA.NAZ_WYDR:='';
   _symb:=form(MBWPL.NRPTW,-5,0,'99')+'/'+form(MBWPL.ROKPTW,4,0,'99');

   MBWPL.lock(1);
   MBWPL.cntx_psh();
   MBWPL.index('PTW');
   MBWPL.prefix(MBWPL.DATA~1);
   _num:={? MBWPL.last() || MBWPL.NRPTW || 0 ?}+1;
   MBWPL.cntx_pop();
   MBWPL.NRPTW:=_num;
   MBWPL.ROKPTW:=MBWPL.DATA~1;
   {? MBWPL.put(1)
   || _pth:='Potwiedzenie_'+form(MBWPL.NRPTW,-5,0,'99')+form(MBWPL.ROKPTW,4,0,'99')+'.pdf';
      POLA.NAZ_WYDR:='Potwierdzenie_'+form(MBWPL.NRPTW,-5,0,'99')+form(MBWPL.ROKPTW,4,0,'99')+'.pdf';
      _wyn:=rep_exec('kas_dok_ptw_wpl',,0,_pth,1);
      {? _wyn & _pth<>''
      || MBWPL.bl_put('PTW',pth_dir(_pth)+'/'+_pth,0);
         _wyn:=exec('save_dok','dokum','MBWPL',_pth,POLA.NAZ_WYDR,_symb,MBWPL.EMAIL
               ,'Potwierdzenie wpłaty gotówkowej '+exec('record','#to_string',MBWPL.ref()),,MBWPL.STAN);
         ferase(pth_dir(_pth)+'/'+_pth,0)
      ?}
   ?};
   MBWPL.unlock()
?};
_wyn


\wpl2fap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: utworzenie jednej pozycji na postawie wplaty gotowkowej
::   WY: 1-dodano, 0-niestety nie dodano
::  OLD: \wpl2fap/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_stvat:=exec('get','#params',300600,2);
_jm:=exec('get','#params',300601,2);
_slu:=exec('FindInSet','#table','SLU','NAZ','~STAWKI VAT '+KSTE.KRAJ().KOD);

:: szukamy odwolan wg parametrow
_ref_st:=null;
{? _stvat<>''
|| SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(_slu,_stvat);
   {? SLO.first()
   || {!
      |? {? SLO.KOD=_stvat
         || _ref_st:=SLO.ref();
            0
         || SLO.next()
         ?}
      !}
   ?};
   SLO.cntx_pop()
?};

_ref_jm:={? _jm<>'' || exec('FindInSet','#table','JM','KOD',_jm,_jm) || null ?};

{? _ref_jm<>null & _ref_st<>null
|| FAP.index('FAP');
   FAP.prefix(FAKS.ref());
   FAP.blank();
   FAP.M:=null;
   FAP.IL:=1;
   FAP.U:='Wpłata gotówkowa pobrana przez handlowca '+MBWPL.HAN().NAZ+', w dniu '+form(MBWPL.DATA)
           +', na kwotę: '+form(MBWPL.WAR,,2)+'.';
   FAP.NX:=FAP.U;
   FAP.memo_set(FAP.U,'UWAGI');
   FAP.SV:=_ref_st;
   _wsp:={? FAKS.T().FIS='T' || 1 || 1+(#((FAP.SV().KOD*'%')+FAP.SV().KOD-1))*0.01 ?};
   FAP.CPR:=(MBWPL.WAR/_wsp)$2;
   FAP.WAL:=FAP.FAKS().WAL;
   FAP.WAL_H:=FAP.WAL;
   FAP.JM:=_ref_jm;
   exec('istatr_faks2fap','faktury_nag');
   exec('liczfak','faktury_wspolne');
   exec('liczfak_ist','faktury_wspolne',1,1);
   exec('uzupjdod','magdok_poz','FAP');
   FAP.KH:=FAP.FAKS().KH;
   FAP.KH_ODB:=FAP.FAKS().KH_ODB;
   _wyn:=FAP.add();
   FAP.memo_put(,'UWAGI')
|| _wyn:=0
?};
_wyn


\fzal_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: odwiazanie faktury zaliczkowej od wplaty gotowkowej
::   WE: _a - ref FAKS
::  OLD: \fzal_del/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
MBWPL.cntx_psh();
MBWPL.index('ZAL');
MBWPL.prefix();
{? MBWPL.find_key(_a,_a)
|| MBWPL.ZAL:='';
   MBWPL.put(1)
?};
MBWPL.cntx_pop();
~~


\wpl_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Załączniki dla wpłat gotówkowych
::  OLD: \drukmwpp/mobhan.fml
::----------------------------------------------------------------------------------------------------------------------
exec('sel_dok','dokum','MBWPL',,2)


\mbwpl_actions_grayed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PS] [17.00]
:: OPIS: Wyszarza akcje na wpłatach gotówkowych
::   WE: [_a] - akronim okienka wertowania, domyślnie - aktualnie wyświetlane
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _wer:=_a || _wer:=cur_win(1,1) ?};

_actions_grayed:='';
{? MBWPL.sel_size()=0
|| {? MBWPL.STAN='P' || _actions_grayed:='W' || _actions_grayed:='AP' ?}
?};
MBWPL.actions_grayed(_wer,_actions_grayed);
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 8b08a381c0c2a352826cd1f5563f30ec49fd87da8f2e5fde8d2554a27fcd3d13df26c27eef8fb66b39b0d6606d52a0101f559515e5dc02f0ef9f7e4de3155a5c66b0745ee2aaeff2b2ad436f05104c6cc3f8424ce7e0f74c6a1e4552e5f88897aaa93020ba6601a58133ec89490282eb2b2c8a53876074ae25aa923251e3af6b

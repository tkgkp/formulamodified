:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: kas.fml
:: Utworzony: 29.02.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obszar kasa
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła inicjująca dla obszaru KAS
::----------------------------------------------------------------------------------------------------------------------
exec('stalesys','#stalesys');
:: Podczytanie INFO.KRAJ - kraj waluty opodatkowania
exec('czytaj','#stalesys',,KINFO,XINFO,KPARAM);

exec('fl_decl','dekret_aut');
:: RB - objekt obsługujący rachunki bankowe
exec('RB','object');
~~


\kas_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Główna formuła obszaru KAS_DOK - Raporty kasowe
::----------------------------------------------------------------------------------------------------------------------
exec('init','kas');

AreaTitle.setTabWin(RAPORT,'WER');
AreaTitle.setTitle();

_rap:=exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_PRAP')
    | exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_DRAP')
    | exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_DDOK')
    | exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_ERAP')
    | exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_ZSQL')
    | exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_ZWYD');
_dok:=exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_PDOK');

__MULTIKASA:=0;
{? USRCONST.STANKAS=null
|| FUN.info('Nie wybrano stanowiska kasowego w stałych systemu.'@)
|? _rap & _dok
|| _win_grp:=RAPORT.grp_make('',"tab_sel(1)",'raporty_kas',,,"exec('exit','zws')");
   DOKUMENT.win_fml('DOK_PRZE',DOKUMENT,'OPER','KOD','ICON_BEFORE',exec('dok_kas_i','kasa_wspolne'));
   DOKUMENT.win_fml('WER',DOKUMENT,'OPER','KOD','ICON_BEFORE',exec('dok_kas_i','kasa_wspolne'));
   RAPORT.grp_sel(_win_grp,RAPORT,'WER','Raporty'@,,,,10,"exec('md2_rap','kas',,0)",,,,'maximized');
   RAPORT.grp_sel(_win_grp,DOKUMENT,'DOK_PRZE','Dokumenty'@,,,,10,"exec('md3_dok','kas',,0)",,,,'maximized');
   RAPORT.grp_sel(_win_grp,POZDOK,'POZ_PRZE','Pozycje dokumentów'@,,,,10,"exec('md4_poz','kas',,0)",,,,'maximized');
   exec('md2_rap','kas',_win_grp,1)
|? _rap
|| exec('md2_rap','kas')
|? _dok
|| _win_grp:=DOKUMENT.grp_make('',"tab_sel(1)",'dokumenty_kas');
   DOKUMENT.grp_sel(_win_grp,DOKUMENT,'DOK_PRZE','Dokumenty'@,,,,10,"exec('md3_dok','kas',,0)",,,,'maximized');
   DOKUMENT.grp_sel(_win_grp,POZDOK,'POZ_PRZE','Pozycje dokumentów'@,,,,10,"exec('md4_poz','kas',,0)",,,,'maximized');
   exec('md3_dok','kas',_win_grp,1)
?}


\md2_rap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: raporty kasowe
::   WE: [_a] - okienko selekcji domyślnie WER
::       [_b] - wyświetlić select domyślnie 1-tak 0-nie
::  OLD: \md2_rap/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='WER' ?} || _a:='WER' ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};

{? USRCONST.STANKAS<>null
|| _actions:=
      {? USRCONST.STANKAS().UPR='T'
      || ''
      || 'DPUZ:D'
      ?};
   OPERATOR.DEPT:=USRCONST.STANKAS().STANKAS().ODD;
   RAPORT.actions('WER',_actions);
   {? _b || RAPORT.win_sel(_a) ?};
   RAPORT.index('NUMER');
   RAPORT.prefix();
   RAPORT.last();
   {? _b || RAPORT.select(,1,10) ?}
|| FUN.info('Nie wybrano stanowiska kasowego w stałych systemu.'@)
?};
0


\kas_dok_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zmiana parametrów pracy obszaru KAS_DOK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? {? __MULTIKASA
   || __PARSES.editPar('OkresRok','Mkasa')
   || __PARSES.editPar('OkresRok','StKasowe')
   ?}
|| OPERATOR.DEPT:=USRCONST.STANKAS().STANKAS().ODD;
   RAPORT.index('NUMER');
   RAPORT.prefix();
   AreaTitle.setTitle();
   {? __MULTIKASA || sel_exit() ?}
?}


\kas_mul
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Główna formuła obszaru KAS_MUL - Multikasa
::----------------------------------------------------------------------------------------------------------------------
exec('init','kas');

AreaTitle.setTabWin(RAPORT,~~);
AreaTitle.setTitle();

DOKUMENT.win_fml('WER',DOKUMENT,'OPER','KOD','ICON_BEFORE',exec('dok_kas_i','kasa_wspolne'));

__MULTIKASA:=1;
{!
|?
   exec('md2_rap1','kas')
!}


\md2_rap1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: praca z raportami kasowymi na wielu stanowiskach - mulitkasa
::   WY: 0-zakończono pracę z multikasą, 1-kontynuacja pracy przy zmienionych parametrach pracy
::  OLD: \md2_rap1/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_stankas:=USRCONST.STANKAS;
_czy_sel:=0;
__multik:=1;

MKASAUPR.index('MKASANAZ'); MKASAUPR.prefix(OPERATOR.USER);
{? USRCONST.MKASA=null
||
   {? MKASAUPR.first
   ||
      {? MKASAUPR.next
      ||
         MKASAUPR.first;
         {? ~exec('multikasa_sel','kas') || return(_wyn) ?}
      ?}
   ||
      FUN.info('Brak uprawnionych multikas.'@); return(_wyn)
   ?}
?};

KUSERUPR.index('MKASA'); KUSERUPR.prefix({? USRCONST.MKASA || USRCONST.MKASA().MKASA || MKASAUPR.MKASA ?});
{? ~KUSERUPR.first
||
   FUN.info('Brak kas w definicji multikasy.'@)
||
   _chk_role:=exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_DDOK')
      | exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_PDOK');
   {!
   |?
      _uprlist:=0;
      KUSERUPR.cntx_psh;
      KUSERUPR.index('KU_ST'); KUSERUPR.prefix(OPERATOR.USER,KUSERUPR.STANKAS);
      {? KUSERUPR.first || _uprlist:=1 ?};
      KUSERUPR.cntx_pop;
      KUSERUPR.cntx_psh;
      {? KUSERUPR.MTAB='T' & _uprlist
      ||
         _czy_sel:=1;
         _high1:={? KUSERUPR.POZDOK='T' || 12 || 0 ?};
         _high2:={? _high1>0 || _high1-6 || 0 ?};
         {? var_pres('__stk_w')<0
         ||
            USRCONST.STANKAS:=KUSERUPR.ref;
::          okno multikasa
            __stk_w:=RAPORT.grp_make('Multikasa'@,,'multikasa',,,"exec('exit','zws')")
         ?};
::       widok raportow
         __rap_w:='WER';
         _far:=
            {? _chk_role
            || "
               {? "+$(KUSERUPR.DOKUMENT='T')+"
               || exec('dost_dok1','kasa_dokument',0);
                  grp_disp(DOKUMENT,DOKUMENT.win_sel('?'),,1)
               ?};
               {? "+$(KUSERUPR.POZDOK='T')+" || grp_disp(POZDOK,'WER',,1) ?}
               "
            || ""
            ?}; {? _far<>"" || _far:=$_far ?};
         _fb:="
            USRCONST.STANKAS:=exec('FindAndGet','#table',KUSERUPR,\'"+$KUSERUPR.ref+"\',,,null());
            __PARSES.setVal('StKasowe',USRCONST.STANKAS().STANKAS);
            RAPORT.cntx_psh;
            RAPORT.win_sel(__rap_w);
            RAPORT.hdr_sel; RAPORT.hdr_sel(' '+USRCONST.STANKAS().STANKASN);
            RAPORT.cntx_pop;
            exec('get_puzy','kasa_wspolne');
            exec('open_tabele','open_tab');
            exec('titlebar','kas');
            RAPORT.index('NUMER'); RAPORT.prefix();
            {? USRCONST.STANKAS().UPR='T'
            || RAPORT.actions(__rap_w,,,1)
            || RAPORT.actions(__rap_w,+'DPUZ:D',,1)
            ?}
         "; _fb:=$_fb;
         _act:=_stankas=KUSERUPR.ref;
         _tab:={? KUSERUPR.TAB='' || KUSERUPR.STANKAS().NAZWA || KUSERUPR.TAB ?};
         RAPORT.grp_sel(__stk_w,,__rap_w,_tab,_far,,,0,_fb,,,_act,'maximized_with_title');
::       widok dokumentow
         {? _chk_role & KUSERUPR.DOKUMENT='T'
         ||
            _far:="
               {? "+$(KUSERUPR.POZDOK='T')+" || exec('dost_dok1','kasa_dokument',1); grp_disp(POZDOK,'WER',,1) ?}
            "; _far:=$_far;
            _fb:="
               DOKUMENT.index('RAPORT'); DOKUMENT.prefix(RAPORT.ref);
               DOKUMENT.hdr_sel;
               {? ROZNE.BLOK_RAP:=exec('dost_dok1','kasa_dokument'); ROZNE.BLOK_RAP<>0
               ||
                  DOKUMENT.hdr_sel(' nr '+form(RAPORT.NUM_RAP)+', stanowisko '+STANKAS.NAZWA)
               ?};
               exec('bo_rap','kasa_wspolne');
               exec('dok_suma','kasa_wspolne');
               MLKAS.MIN:=STANKAS.MIN
            ";
            _fa:="
               POZDOK.cntx_psh;
               {? _a
               || exec('rap_suma','kasa_wspolne')
               || exec('bo_rap','kasa_wspolne');
                  exec('dok_suma','kasa_wspolne')
               ?};
               POZDOK.cntx_pop;
               {? ROZNE.BLOK_RAP=1 || RAPORT.r_unlock() ?};
               DOKUMENT.actions(DOKUMENT.win_sel('?'))
            ";
            USRCONST.STANKAS:=KUSERUPR.ref;
            exec('dost_dok1','kasa_dokument',0);
            RAPORT.tab_splt(__stk_w,,'vertical','gora_prawo');
            RAPORT.grp_sel(__stk_w,DOKUMENT,DOKUMENT.win_sel('?'),,_far,,,_high2,_fb,_fa,,,'maximized_with_title');
::          widoku pozycji
            {? KUSERUPR.POZDOK='T'
            ||
               _fb:="
                  DOKUMENT.cntx_psh;
                  _first:=DOKUMENT.first;
                  DOKUMENT.cntx_pop;
                  {? _first
                  ||
                     DOKUMENT.OPER();
                     POZDOK.index('DOKUMENT'); POZDOK.prefix(DOKUMENT.ref());
                     exec('pozdok_win_edit_set','kasa_pozdok')
                  ||
                     POZDOK.index('DOKUMENT'); POZDOK.prefix(null)
                  ?}
               ";
               _fa:="
                  1
               ";
               RAPORT.tab_splt(__stk_w,'gora_prawo','horizontal','gol');
               RAPORT.grp_sel(__stk_w,POZDOK,'WER',,,,,,_fb,_fa,,,'maximized_with_title')
            ?}
         ?}
      ?};
      KUSERUPR.cntx_pop;

      KUSERUPR.next
   !};

   {? _czy_sel
   ||
      RAPORT.win_sel(__stk_w);
      _wyn:=RAPORT.select(,1,10)
   ||
      FUN.info('Brak uprawnionych kas w widoku multikasa.'@)
   ?}
?};
VAR_DEL.delete('__stk_w','__multik','__rap_w');

_wyn


\multikasa_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: wybor multikasy
::  OLD: \multikasa_sel/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
MKASAUPR.win_sel(exec('multikasa_slo','kas',1));
MKASAUPR.select


\multikasa_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: definiowanie okna selekcji uprawnien do multkas
::   WE: _a-1 dodac akcje Ta, 0-wpp
::  OLD: \multikasa_slo/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
_wer:=MKASAUPR.mk_sel('Uprawnione widoki multikas'@);
MKASAUPR.win_fld(_wer,,'MKASA','NAZ','NAZ',50,,,'Nazwa'@,1);
{? _a || MKASAUPR.win_act(_wer,,'Formuła','&Wybierz'@@,,,,"sel_exit",1,,,,'W') ?};
_wer


\titlebar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Ustawia tytul glownego okna aplikacji
::   WE: [_a] - nazwa stanowiska - domyslnie ze stalych
::  OLD: \titlebar/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
_jest:=_jest1:=0;
_title:={? XINFO.SKROT<>'' || _jest:=1; ' ['+XINFO.SKROT+']' || '' ?};
_title+={? USRCONST.STANKAS<>null
        || _jest1:=1;
           {? _jest || ' - ' || ' ' ?}+
           {? _ || _a || USRCONST.STANKAS().STANKAS().NAZWA ?}
        || ''
        ?};
_title+={? _jest | _jest1 || ': ' || ' '?}+$APARAM.ROK;
title(_title)


\kas_got
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Główna formuła obszaru KAS_GOT - Wpłaty gotówkowe
::----------------------------------------------------------------------------------------------------------------------
exec('init','kas');

AreaTitle.setTabWin(MBWPL,'WER');
AreaTitle.setTitle();

exec('mob_wpl','kasa_wplaty')


\md3_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: przegladanie dokumentow
::   WE: [_a] - okienko selekcji domyślnie DOK_PRZE
::       [_b] - wyświetlić select domyślnie 1-tak 0-nie
::  OLD: \md3_dok/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='DOK_PRZE' ?} || _a:='DOK_PRZE' ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};

{? USRCONST.STANKAS<>null
|| USRCONST.STANKAS().STANKAS();
   DOKUMENT.win_edit('RED');
   DOKUMENT.actions('DOK_PRZE',{? PAR_SKID.get(80)='N' || 'Z' || '' ?});
   {? _b || DOKUMENT.win_sel(_a) ?};
   DOKUMENT.hdr_sel(': '+STANKAS.NAZWA);
   DOKUMENT.index('RAPORT'); DOKUMENT.prefix();
   {? ~DOKUMENT.first() || RAPORT.blank(1) ?};
   {? _b || DOKUMENT.select() ?}
|| FUN.info('Nie wybrano stanowiska kasowego w stałych systemu.'@)
?};
0


\md4_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: przegladanie pozycji dokumentow
::   WE: [_a] - okienko selekcji domyślnie POZ_PRZE
::       [_b] - wyświetlić select domyślnie 1-tak 0-nie
::  OLD: \md4_poz/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='POZ_PRZE' ?} || _a:='POZ_PRZE' ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};

{? USRCONST.STANKAS<>null
|| USRCONST.STANKAS().STANKAS();
   POZDOK.actions('POZ_PRZE',{? PAR_SKID.get(80)='N' || 'Z' || '' ?});
   {? _b || POZDOK.win_sel('POZ_PRZE') ?};
   POZDOK.hdr_sel(': '+STANKAS.NAZWA);
   POZDOK.index('DOKUMENT'); POZDOK.prefix();
   {? ~POZDOK.first() || DOKUMENT.blank(1) ?};
   {? _b || POZDOK.select() ?}
|| FUN.info('Nie wybrano stanowiska kasowego w stałych systemu.'@)
?};
0

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:18 b296f888f90186ec791907a29a22e4caa3eaa8947835b80d00162098b6205ba651294f36ec05a84481678c4c58e17806ecac7276988c9405393294a8382db87835859a2fddb04ddf89361ddf03652d03facd6840a096fd3ba60bfe76a4561220ecb40d54e26e0a9204a330b196176b0b8639d214856d7c12351f8ea5af236a23

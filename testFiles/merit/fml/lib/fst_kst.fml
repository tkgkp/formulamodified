:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: fst_kst.fml
:: Utworzony: 14.11.2017
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Obszar FST - zmiana klasyfikacji środków trwałych
::======================================================================================================================


\funkcje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Funkcje techniczne
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')=0
|| _red:=EDIT_ES.mk_edit('Funkcje techniczne'@,0,'_fst_fun_tech',30,10);
   _name:='text=%1,btn_label_align=center,panel=left,align=begin,display=1'['&Przegląd klasyfikacji'@];
   EDIT_ES.win_ebtn(_red,_name,"exec('tab_krst','fst_kst');''");
   task_attach('ZWS_PAR_FTGR');
   _name:='text=%1,btn_label_align=center,panel=left,align=begin,display=1'['&Tabela konwersji grup'@];
   EDIT_ES.win_ebtn(_red,_name,"exec('tab_konw','fst_kst');''");
   task_attach('FST_EWI_ZKST');
   _name:='text=%1,btn_label_align=center,panel=left,align=begin,display=1'['&Zmiana klasyfikacji'@];
   EDIT_ES.win_ebtn(_red,_name,"exec('zmiana','fst_kst');''");
   task_attach('FST_EWI_ZKST');
   _name:='text=%1,btn_label_align=center,panel=left,align=begin,display=1'['&Uzupełnianie klasyfikacji'@];
   EDIT_ES.win_ebtn(_red,_name,"exec('uzup_kla','fst_kst');''");
   task_attach('FST_EWI_ZKST');
   _name:='text=%1,btn_label_align=center,panel=left,align=begin,display=1'['&Wydruki klasyfikacji'@];
   EDIT_ES.win_ebtn(_red,_name,"exec('wydruki','fst_kst');''");
   task_attach('FST_EWI_ZKST');
   _name:='text=%1,btn_label_align=center,panel=left,align=begin,display=1'['W&ycofanie zmiany klasyfikacji'@];
   EDIT_ES.win_ebtn(_red,_name,"exec('wycofanie','fst_kst');''");
   task_attach('FST_EWI_ZKST');
   _name:='text=%1,btn_label_align=center,panel=left,align=begin,display=1'['Dane na BO - metoda degresywna'@];
   EDIT_ES.win_ebtn(_red,_name,"exec('bo_degresywna','fst_kst');''");
   _name:='text=%1,btn_label_align=center,panel=left,align=begin,display=1'['Aktualizacja danych'@];
   EDIT_ES.win_ebtn(_red,_name,"exec('st_napraw','napraw_f');''");
   task_attach('FST_EWI_ZKST');
   EDIT_ES.win_edit(_red);
   EDIT_ES.edit()
|| _szer:=60;

   _TAB:=tab_tmp(1
      ,'LP','INTEGER','Lp.'
      ,'NAME','STRING[%1]' [$_szer],'Nazwa'
      ,'HELP','STRING[60]','Podpowiedź'
      ,'FORMULA','STRING[255]','Formuła do wykonania'
   );
   _add:="
      _a.blank();
      _a.LP:=_a.size()+1;
      {? var_pres('_d')=3
      || _a.FORMULA:=$_d
      ?};
      _a.NAME:=
         {? _a.FORMULA=''
         || '[ %1 ]' [_b]
         || _b
         ?};
      _a.HELP:={? var_pres('_c')=type_of('') || _c || _a.NAME ?};
      _a.add()
   ";
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
   {? exec('hasAction','users','ZWS_PAR_FTGR')
   || _add(_TAB,'Przegląd klasyfikacji'@,'Przegląd klasyfikacji'@,"exec('tab_krst','fst_kst');''")
   ?};
   {? exec('hasAction','users','FST_EWI_ZKST')
   || _add(_TAB,'Tabela konwersji grup'@,'Tabela konwersji grup'@,"exec('tab_konw','fst_kst');''");
      _add(_TAB,'Zmiana klasyfikacji'@,'Zmiana klasyfikacji'@,"exec('zmiana','fst_kst');''");
      _add(_TAB,'Uzupełnianie klasyfikacji'@,'Uzupełnianie klasyfikacji'@,"exec('uzup_kla','fst_kst');''");
      _add(_TAB,'Wydruki klasyfikacji'@,'Wydruki klasyfikacji'@,"exec('wydruki','fst_kst');''");
      _add(_TAB,'Wycofanie zmiany klasyfikacji'@,'Wycofanie zmiany klasyfikacji'@,"exec('wycofanie','fst_kst');''");
      _add(_TAB,'Dane na BO - metoda degresywna'@,'Dane na BO - metoda degresywna'@,"exec('bo_degresywna','fst_kst');''");
      _add(_TAB,'Aktualizacja danych'@,'Aktualizacja danych'@,"exec('st_napraw','napraw_f');''")
   ?};
   _ws:=_TAB.mk_sel('Funkcje techniczne'@,'N',,'_fst_fun_tech',,,,,'U');
   _TAB.win_fld(_ws,,'NAME');
   _TAB.win_act(_ws,,'Formuła','Uruchom'@,,
      ," _TAB:=cur_tab(1,1);
         _ts:=time();
         ($_TAB.FORMULA)();
         _TAB.put()
      "
      ,,1,,,,'U'
   );
   _TAB.win_sopt(_ws,'select_record_checkbox=0');
   _TAB.win_sel(_ws);
   params_set(
      'TAB',_TAB,
      'ws',_ws,
      'szer',_szer
   );
   _TAB.select()
?}


\is_kst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Czy we wskazanym roku jest nowa klasyfikacja środków
::   WE: Rok (integer)
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
TAB_KRST.cntx_psh();
TAB_KRST.index('TABKRST');
TAB_KRST.prefix(_a);
{? TAB_KRST.first() || _dalej:=1 || _dalej:=0 ?};
TAB_KRST.cntx_pop();
_dalej


\is_first
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Czy we wskazanym roku obowiązuje pierwsza automatycznie założona klasyfikcja?
::   WE: Rok (integer)
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_dalej:=0;
TAB_KRST.cntx_psh();
TAB_KRST.index('TABKRST');
TAB_KRST.prefix(_a);
{? TAB_KRST.first()
|| _tab:=TAB_KRST.ref();
   TAB_KRST.prefix();
   {? TAB_KRST.first() & TAB_KRST.ref()=_tab
   || _dalej:=1
   ?}
?};
TAB_KRST.cntx_pop();
_dalej


\tab_konw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Formuła wyświetla tabelę konwersji
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('is_kst','fst_kst',SSTALE.AO().RES)
|| FUN.emsg('Brak klasyfikacji środków obowiązującej od bieżącego roku. Uruchomienie funkcji niemożliwe.'@);
   return(0)
?};
TAB_KRST.cntx_psh();
TAB_KRST.index('TABKRST');
TAB_KRST.prefix();
{? TAB_KRST.last() & TAB_KRST.AR>SSTALE.AO().RES || _next:=TAB_KRST.AR || _next:=0 ?};
TAB_KRST.cntx_pop();

{? _next
|| FUN.emsg('W systemie istnieje tabela klasyfikacji obowiązująca od roku %1.\n'
            'Modyfikacja tabeli konwersji na wcześniejszą klasyfikację nie jest możliwa.'@[$_next]);
   return(0)
?};

TAB_KONW.index('SGR');
TAB_KONW.prefix(SSTALE.AO().RES);
_win:=TAB_KONW.mk_sel('Tabela konwersji grup (z %1 na %2)'@[$(SSTALE.AO().RES-1),$SSTALE.AO().RES],'P',0,'_tab_konw',10,5,15,,'U',,,,,'normal');
TAB_KONW.win_fld(_win,,'AR',,,12,,1,'Rok zmiany'@,,'Rok od którego obowiązuje zmiana'@);
TAB_KONW.win_fld(_win,,'SGR','GR','KST',15,,,'Stara grupa'@,,'Dotychczasowa grupa'@);
TAB_KONW.win_fld(_win,,'GR','GR','KST',15,,,'Nowa grupa'@,,'Nowa grupa'@);
TAB_KONW.win_act(_win,0,'Dołącz',,,'Dołącz'@);
TAB_KONW.win_act(_win,1,'Dołącz',,,'Dołącz'@);
TAB_KONW.win_act(_win,0,'Popraw',,,'Popraw'@,,,1);
TAB_KONW.win_act(_win,0,'Usuń',,,'Usuń'@);
TAB_KONW.win_act(_win,1,'Formuła','Import'@,,'Import tabeli konwersji'@,,"exec('tab_konw_imp','fst_kst')",1,,,,'I');
TAB_KONW.win_act(_win,0,'Formuła','Import'@,,'Import tabeli konwersji'@,,"exec('tab_konw_imp','fst_kst')",,,,,'I');
TAB_KONW.win_sel(_win);
{? SSTALE.AR().ZK='T'
|| TAB_KONW.actions(_win,'DPUi:Di',,1)
|| {?  exec('is_first','fst_kst',SSTALE.AO().RES)
   || TAB_KONW.actions(_win,'DPUI:DI',,1)
   || TAB_KONW.actions(_win,'',,1)
   ?}
?};
TAB_KONW.select()


\tab_konw_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Import tabeli konwersji
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.AR().ZK='T'
|| FUN.emsg('W bieżącym roku przeprowadzono już zmianę klasyfikacji środków trwałych\n'
            'Uruchomienie funkcji nie jest możliwe.'@);
   return(0)
?};
{? ~exec('is_kst','fst_kst',SSTALE.AO().RES)
|| FUN.emsg('Brak klasyfikacji środków obowiązującej od bieżącego roku. Uruchomienie funkcji niemożliwe.'@);
   return(0)
?};
{? exec('is_first','fst_kst',SSTALE.AO().RES)
|| FUN.emsg('Od bieżącego roku obowiązuje pierwsza klasyfikacja środków w systemie.\n'
            'Uruchomienie funkcji zmiany klasyfikacji nie jest możliwe.'@);
   return(0)
?};
TAB_KONW.cntx_psh();
TAB_KONW.index('SGR');
TAB_KONW.prefix(SSTALE.AO().RES);
{? TAB_KONW.first()
|| _refresh:=1;
   _txt:='Odświeżyć istniejącą tabelę konwersji na podstawie pliku na serwerze?\n'
         '(Uzupełnione zostaną puste zapisy dotyczące nowych grup, istniejące\n'
         'zapisy nie zostaną zmodyfikowane).'@
|| _refresh:=0;
   _txt:='Wczytać tabelę konwersji z pliku na serwerze?'@
?};
TAB_KONW.cntx_pop();

{? ~FUN.ask(_txt) || return(0) ?};
_sep:='@';
_files:=files('tamk_*.dfg');
{? _files.first()
|| _win:=_files.mk_sel('Wybór pliku'@,'P',1,,,,,,'U',,,,,'normal');
   _files.win_act(_win,,'Formuła'@,'Wybierz'@,,,,"sel_exit()",1,,,,'W');
   _files.win_sel(_win);
   {? _files.select()
   || _f:=fopen(_files.FILENAME,'ur',1);
      {? _f
      || TAB_KONW.cntx_psh(); TAM.cntx_psh();
         TAB_KONW.index('SGR'); TAB_KONW.prefix();
         TAM.index('KST'); TAM.prefix();

         _old:=exec('krs_prev','fst_kst');
         _new:=exec('kst_obow','fst_kst',SSTALE.AO().RES,1);
:: uzupelnienie tabeli konwersji o wszystkie grupy z dotychczasowej klasyfikacji w bazie TAM systemu Estra
:: w poprzednim roku
         {? _old<>null
         || TAB_KONW.cntx_psh();
            TAB_KONW.index('TAB_KONW'); TAB_KONW.prefix();
            TAM.index('KST');
            TAM.prefix(_old);
            {? TAM.first()
            || {! |?
                  {? ~TAB_KONW.find_key(SSTALE.AO().RES,TAM.ref())
                  || TAB_KONW.blank();
                     TAB_KONW.AR:=SSTALE.AO().RES;
                     TAB_KONW.SGR:=TAM.ref();
                     TAB_KONW.add()
                  ?};
                  TAM.next()
               !}
            ?};
            TAB_KONW.cntx_pop()
         ?};

         TAB_KONW.index('SGR'); TAB_KONW.prefix();
         TAM.index('KST'); TAM.prefix();
         {! |? (_line:=fread(_f))<>'\n'
         |! {? form(_line)<>''
            || _tmp:=spli_str(_line,_sep);
:: W pliku np. tamk_2018.dfg polu 1 - grupa przed 2018, w polu 2 - grupa od 2018
               TAB_KONW.prefix(SSTALE.AO().RES,_tmp[1],);
               {? TAB_KONW.first() & TAB_KONW.GR=null
               || TAM.prefix(_new);
                  {? |_tmp[2]<>'' & TAM.find_key(_tmp[2])
                  || TAB_KONW.GR:=TAM.ref();
                     TAB_KONW.put()
                  ?}
               ?};
               {? var_pres('_tmp')>0 || obj_del(_tmp) ?}
            ?}
         !};

         TAB_KONW.cntx_pop(); TAM.cntx_pop();
         fclose(_f)
      ?};
      {? _refresh
      || FUN.info('Procedura oświeżenia tabeli konwersji została zakończona.'@)
      || FUN.info('Procedura importu tabeli konwersji została zakończona.'@)
      ?}
   ?}
|| FUN.info('Brak dostępnych plików z tabelami konwersji grup środków.'@)
?}


\uzup_kla
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Formuła pozwala na uzupełnianie zmiany klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.AR().ZK<>'T'
|| FUN.emsg('W bieżącym roku nie przeprowadzono zmiany klasyfikacji środków trwałych\n'
            'Uruchomienie funkcji nie jest możliwe.'@);
   return(0)
?};

{? SSTALE.AO().AMOR='T'
|| FUN.emsg('Bieżący okres jest zamknięty w obszarze Środki trwałe.'@);
   return(0)
?};

VAR_DEL.delete('__TSROD');
__TSROD:=tab_tmp(2,'GR','STRING[15]','Grupa',
                   'NRI','STRING[20]','Nr inwentarzowy',
                   'NST','STRING[100]','Nazwa',
                   'DE','DATE','Data eksploatacji',
                   'REF','STRING[16]','Ref środka');

_win:=__TSROD.mk_sel('Środki wymagające uzupełnienia'@,'P',0,'__tsrod',10,5,15,,'U',,,,,'normal');
__TSROD.win_fld(_win,__TSROD,'GR',,,15,,,'Grupa'@,,'Grupa w starej klasyfikacji'@);
__TSROD.win_fld(_win,__TSROD,'NRI',,,18,,,'Nr inwentarzowy'@,,'Numer inwentarzowy środka'@);
__TSROD.win_fld(_win,__TSROD,'NST',,,40,,,'Nazwa'@,,'Nazwa środka'@);
__TSROD.win_fld(_win,__TSROD,'DE',,,14,,,'W eksploatacji'@,,'Data wprowadzenia do eksploatacji'@);

__TSROD.win_act(_win,,'Formuła','Zmiana klasyfikacji'@,,'Funkcja zmieniająca grupę środka'@,,
                "exec('gen_mg','fst_kst')",1,1,"exec('gb_gen_mg','fst_kst')","exec('ga_gen_mg','fst_kst')",'Z');

__TSROD.win_sel(_win);

SRSR.cntx_psh(); SRST.cntx_psh(); SRDO.cntx_psh();
SRSR.prefix();
SRST.index('ROK');
SRST.prefix(SSTALE.AR,SSTALE.AO);
SRDO.index('AUTO');
{? SRST.first()
|| {! |?
      {? SRST.GRP<>'E'
      || SRDO.prefix(SRST.SRSR,SSTALE.AR,SSTALE.AO,FINFO.KST_MT,'T');
         {? ~SRDO.first()
         || __TSROD.blank();
            __TSROD.GR:=SRST.GR().GR;
            __TSROD.NRI:=SRST.NRI;
            __TSROD.NST:=SRST.NST;
            __TSROD.DE:=SRST.SRSR().DE;
            __TSROD.REF:=$SRST.SRSR;
            __TSROD.add()
         ?}
      ?};
      SRST.next()
   !};
   _dalej:=1
|| FUN.emsg('Brak środków trwałych w systemie w bieżącym okresie.'@);
   _dalej:=0
?};
SRDO.cntx_pop(); SRST.cntx_pop(); SRSR.cntx_pop();

{? _dalej
|| {? __TSROD.first()
   || SRDO.prefix();
      __TSROD.select()
   || FUN.emsg('Brak środków trwałych wymagających uzupełnienia po zmianie klasyfikacji.'@)
   ?}
?};
VAR_DEL.delete('__TSROD')


\gb_gen_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Przed akcją grupową uzupełniania dokumentów zmiany grupy dla środków nie objętych automatyczną
::       zmianą klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
VAR.PFX_GR:=null;
:: ustalenie starej i nowej klasyfikacji
exec('ster_zkst','fst_kst');
_red:=VAR.mk_edit('Grupa środków w nowej klasyfikacji'@,0,'__pfx_gr',10,5);
VAR.win_efld(_red,VAR,'PFX_GR','GR','KST',15,,,'Nowa grupa'@,,'Grupa środków w nowej klasyfikacji'@);
_ok:=VAR.win_ebtn(_red,'text=&OK'+',btn_label_align=center,panel=bottom,align=end','key:F2');
_an:=VAR.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Anuluj'@],'key:Esc');
VAR.btn_eopt(_red,_ok,'tooltip='+exec('help_red_ok','#window','P'));
VAR.btn_eopt(_red,_an,'tooltip='+exec('help_red_esc','#window','A'));
VAR.win_edit(_red);
{? VAR.edit("{? VAR.PFX_GR=null || FUN.emsg('Należy wskazać grupę'@); 'PFX_GR' || 1 ?}")
|| 1
|| VAR.PFX_GR:=null; 0
?}


\ster_zkst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Ustala wartość zmiennej wskazującej na poprzednią tabelę klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
TAB_KRST.cntx_psh();
TAB_KRST.index('TABKRST');
TAB_KRST.prefix(SSTALE.AO().RES);
{? TAB_KRST.first() || STER.KST:=STER.NKST:=TAB_KRST.ref() ?};
TAB_KRST.prefix();
{? TAB_KRST.seek(STER.KST) & TAB_KRST.prev() || STER.ZKST:=TAB_KRST.ref() ?};
TAB_KRST.cntx_pop()


\ga_gen_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Po akcji grupowej uzupełniania dokumentów zmiany grupy dla środkow nie objętych automatyczną
::       zmianą klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
VAR.PFX_GR:=null;
1


\gen_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Formula do uzupełniania dokumentów zmiany grupy dla środków nie objętych automatyczną
::       zmianą klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
{? __TSROD.sel_size()=0
|| VAR.PFX_GR:=null;
   _red:=VAR.mk_edit('Grupa środków w nowej klasyfikacji'@,0,'__pfx_gr',10,5);
   VAR.win_efld(_red,VAR,'PFX_GR','GR','KST',15,,,'Nowa grupa'@,,'Grupa środków w nowej klasyfikacji'@);
   VAR.win_edit(_red);
   _ok:=VAR.win_ebtn(_red,'text=&OK'+',btn_label_align=center,panel=bottom,align=end','key:F2');
   _an:=VAR.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Anuluj'@],'key:Esc');
   VAR.btn_eopt(_red,_ok,'tooltip='+exec('help_red_ok','#window','P'));
   VAR.btn_eopt(_red,_an,'tooltip='+exec('help_red_esc','#window','A'));
   {? ~VAR.edit("{? VAR.PFX_GR=null || FUN.emsg('Należy wskazać grupę.'@); 'PFX_GR' || 1 ?}")
   || VAR.PFX_GR:=null;
      return(0)
   ?}
?};

SRSR.cntx_psh();
SRSR.prefix();
{? VAR.PFX_GR<>null & SRSR.seek(__TSROD.REF,ref_name(__TSROD.REF))
|| exec('add_mg','fst_kst',SRSR.ref(), VAR.PFX_GR)
?};
SRSR.cntx_pop()


\tab_krst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Formuła pozwala na przeglądanie klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
TAB_KRST.index('TAB_KRST');
TAB_KRST.prefix();
_win:=TAB_KRST.mk_sel('Tabele klasyfikacji środków trwałych'@,'P',0,'__tab_krst',10,5,,,'U',,,,,'normal');
TAB_KRST.win_fld(_win,,'KST',,,6,,,'Symbol'@,,'Symbol klasyfikacji'@);
TAB_KRST.win_fld(_win,,'O',,,40,,,'Opis'@,,'Opis klasyfikacji'@);
TAB_KRST.win_fld(_win,,'AR',,,7,,,'Od roku'@,,'Od roku'@);
TAB_KRST.win_act(_win,,'Formuła','Wyświetl'@,,'Wyświetla tabelę klasyfikacji',,"exec('sel_tam','fst_kst',TAB_KRST.ref())",1,,,,'W');
TAB_KRST.win_sel(_win);
TAB_KRST.select()


\wydruki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Wydruki dotyczące zmiany klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.AR().ZK<>'T'
|| FUN.emsg('W bieżącym roku nie przeprowadzono zmiany klasyfikacji środków trwałych\n'
            'Uruchomienie funkcji nie jest możliwe.'@);
   return(0)
?};
{? ~exec('is_kst','fst_kst',SSTALE.AO().RES)
|| FUN.emsg('Brak klasyfikacji środków obowiązującej od bieżącego roku. Uruchomienie funkcji niemożliwe.'@);
   return()
?};
exec('rep_exec','#b_report','','fst_kst*','Wydruki dotyczące zmiany klasyfikacji'@)


\be_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Przed redakcja TAB_KONW.GR
::----------------------------------------------------------------------------------------------------------------------
STER.KST:=exec('kst_obow','fst_kst',SSTALE.AO().RES,1);
1


\be_sgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Przed redakcja TAB_KONW.SGR
::----------------------------------------------------------------------------------------------------------------------
STER.KST:=TAB_KONW.SGR().KST;
1


\bl_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Formuła na blank dla pola TAB_KONW.AR
::   WY: Rok (integer)
::----------------------------------------------------------------------------------------------------------------------
SSTALE.AO().RES


\krs_prev
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Zwraca poprzednią klasyfikację
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
TAB_KRST.cntx_psh();
TAB_KRST.index('TABKRST');
TAB_KRST.prefix();
{? TAB_KRST.find_key(SSTALE.AO().RES)
|| {? TAB_KRST.prev() || _ret:=TAB_KRST.ref() ?}
?};
TAB_KRST.cntx_pop();
_ret


\sel_tam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Formula wyswietla pozycje klasyfikacji srodkow
::   WE: _a - wskazanie na klasyfikację
::----------------------------------------------------------------------------------------------------------------------
{? _=0 | type_of(_a)<>type_of(null) | _a=null
|| FUN.emsg('Nieprawidłowy parametr wywołania funkcji.'@);
   return(0)
?};
TAM.cntx_psh();
TAM.index('KST');
TAM.prefix(_a);
TAM.win_sel('TABELSTR');
TAM.actions('TABELSTR','DUPC:D',,1);
TAM.select();
TAM.actions('TABELSTR','',,1);
TAM.cntx_pop()


\zmiana
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Procedura zmiany klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.AO().AMOR='T'
|| FUN.emsg('Bieżący okres jest zamknięty w obszarze Środki trwałe.'@);
   return(0)
?};
SRST.cntx_psh();
_sql:='select SRST.NRI from SRST where SRST.ROK>=:_a and SRST.OKRES>=:_b and SRST.DEKRET=''T'' ';
_tmp:=sql(_sql,SSTALE.AO().RES,SSTALE.AO().OES);
{? _tmp.first()
|| FUN.emsg('Amortyzacja w bieżącym okresie (lub w późniejszych) została już zadekretowana dla co najmniej jednego\n'
            'toru amortyzacji. Wprowadzenie zmiany klasyfikacji środków nie jest możliwe.'@);
   SRST.cntx_pop();
   return(0)
?};
SRST.cntx_pop();
{? ~exec('is_kst','fst_kst',SSTALE.AO().RES)
|| FUN.emsg('Brak klasyfikacji środków obowiązującej od bieżącego roku. Uruchomienie funkcji niemożliwe.'@);
   return(0)
?};
{? exec('is_first','fst_kst',SSTALE.AO().RES)
|| FUN.emsg('Od bieżącego roku obowiązuje pierwsza klasyfikacja środków w systemie.\n'
            'Uruchomienie funkcji zmiany klasyfikacji nie jest możliwe.'@);
   return(0)
?};
{? SSTALE.AR().ZK='T'
|| FUN.emsg('W bieżącym roku przeprowadzono już zmianę klasyfikacji środków trwałych\n'
            'Uruchomienie funkcji nie jest możliwe.'@);
   return(0)
?};
{? SSTALE.AO().POCZ<>date(SSTALE.AO().RES,1,1)
|| FUN.emsg('Zmianę klasyfikacji środków należy przeprowadzić w pierwszym okresie roku kalendarzowego.'@);
   return(0)
?};
{? FINFO.KST_MT=null
|| FUN.emsg('Brak ustawionego typu dokumentu zmiany klasyfikacji w parametrach programu,'
            'w obszarze Ustawienia i parametryzacja -> Środki trwałe -> Podstawowe parametry środków trwałych.'@);
   return(0)
?};
{? ~FUN.ask('Uruchomić procedurę zmiany klasyfikacji środków trwałych?'@)
|| return(0)
?};

SRDO.cntx_psh();
SRDO.index('AUTO2');
SRDO.prefix(SSTALE.AR,SSTALE.AO,FINFO.KST_MT,'T');
_kst_mt:=SRDO.first();
SRDO.cntx_pop();
{? _kst_mt
|| FUN.emsg('W sytemie znajdują się już dokumenty zmiany grupy wystawione automatycznie\n'
            'podczas procedury zmiany klasyfikacji. Przed ponownym uruchomieniem procedury\n'
            'należy takie dokumenty usunąć funkcją Wycofanie zmiany klasyfikacji.'@);
   return(0)
?};

SRST.cntx_psh(); SRSR.cntx_psh();
SRSR.index('NRI');
SRSR.prefix();
VAR_DEL.delete('__li_rec','__li_mod');
__li_rec:=0;
__li_mod:=0;

KOMM.init(150,,'Uwagi dotyczące zmiany klasyfikcji środków trwałych.'@);
SRD.KOMM:='T';

{? SRSR.first()
|| _size:=SRSR.size(); _i:=0;
   {! |?
      _i+=1;
      progress((_i/_size)*100,'Trwa zmiana klasyfikacji środków trwałych %1 ...'@[SRSR.NRI],'Zmiana klasyfikacji'@,0);
      {? SRSR.DE<SSTALE.AO().POCZ & (SRSR.DES=date(0,0,0) | SRSR.DES>SSTALE.AO().POCZ)
      || __li_rec+=1;
         _wy:=exec('add_mg','fst_kst',SRSR.ref());
        {? _wy
        || __li_mod+=1
        || KOMM.add('Nie wykonano zmiany klasyfikacji dla środka z grupy %3: %1\n(%2).'@[SRSR.NRI,SRSR.NST,SRSR.GR().GR])
        ?}
      ?};
      SRSR.next()
   !};
   prgs_clr();
   {? __li_mod>0
   || ROK_F.cntx_psh();
      ROK_F.prefix();
      SSTALE.AR();
      ROK_F.ZK:='T';
      ROK_F.put();

:: zmiana wskazania na klasyfikację w okresach
      OKRO_F.cntx_psh();
      OKRO_F.index('ROK');
      OKRO_F.prefix(ROK_F.ref());
      {? OKRO_F.first()
      || _krst:=null;
         TAB_KRST.cntx_psh();
         TAB_KRST.index('TABKRST');
         TAB_KRST.prefix(SSTALE.AO().RES);
         {? TAB_KRST.first() || _krst:=TAB_KRST.ref() ?};
         TAB_KRST.cntx_pop();
         {? _krst
         || {! |?
               OKRO_F.TAB_KRST:=_krst;
               OKRO_F.put();
               OKRO_F.next()
            !}
         ?}
      ?};
      OKRO_F.cntx_pop();
      ROK_F.cntx_pop();

      {? __li_rec>__li_mod
      || {? FUN.ask('Niektóre środki (%1) nie zostały obsłużone przez automatyczną zmianę klasyfkacji,\n'
                    'np. ze względu na niejednoznaczą zmianę grup między klasyfikacjami. Dla takich\n'
                    'środków należy uzupełnić zmianę klasyfikacji korzystając'
                    ' z funkcji Uzupełnianie klasyfikacji.\n\n Wyświetlić listę środków?'@[$(__li_rec-__li_mod)])
         || {? ~KOMM.empty() || KOMM.select() || FUN.emsg('Brak zapisów.'@) ?}
         ?}
      ?}
   || FUN.emsg('Podczas przeprowadzania zmiany klasyfikacji środków trwałych wystąpiły błędy.'
              'Zmiana klasyfikacji nie została w pełni ukończona i należy ją powtórzyć.'@)
   ?}
?};
SRD.KOMM:='N';

VAR_DEL.delete('__li_rec','__li_mod');
SRST.cntx_pop(); SRSR.cntx_pop();
1


\wycofanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Wycofanie zmiany klasyfikacji
::----------------------------------------------------------------------------------------------------------------------
{? SSTALE.AR().ZK<>'T'
|| FUN.emsg('W bieżącym roku nie przeprowadzono zmiany klasyfikacji środków trwałych\n'
            'Uruchomienie funkcji nie jest możliwe.'@);
   return(0)
?};

{? SSTALE.AO().AMOR='T'
|| FUN.emsg('Bieżący okres jest zamknięty w obszarze Środki trwałe.'@);
   return(0)
?};
{? SRST.DEKRET='T'
|| FUN.emsg('Amortyzacja w bieżącym okresie została już zadekretowana dla co najmniej jednego\n'
            'toru amortyzacji. Wprowadzenie zmiany klasyfikacji środków nie jest możliwe.'@);
   return(0)
?};

_dalej:=0;
SRDO.cntx_psh();
SRDO.index('AUTO2');
SRDO.prefix(SSTALE.AR,SSTALE.AO,FINFO.KST_MT,'T');
{? SRDO.first()
|| _dalej:=1;
   {! |?
      {? SRDO.K='T' || _dalej:=0 ?};
      _dalej=1 & SRDO.next()
   !}
|| FUN.emsg('W systemie nie znaleziono dokumentów zmiany grupy wystawionych w ramach\n'
            'zmiany klasyfikacji. Wycofanie nie jest możliwe.'@);
   SRDO.cntx_pop();
   return(0)
?};
SRDO.cntx_pop();

{? ~_dalej
|| FUN.emsg('Co najmniej jeden z dokumentów zmiany grupy środka został zaksięgowany.'
            'Wycofanie zmiany klasyfikacji nie jest możliwe.'@);
   return(0)
?};

{? ~FUN.ask('Uruchomić procedurę wycofania zmiany klasyfikacji środków trwałych?'@)
|| return(0)
?};

SRDO.cntx_psh(); SRSR.cntx_psh(); SRST.cntx_psh();

KOMM.init(150,,'Uwagi dotyczące anulowania dokumentów zmiany klasyfikacji.'@);
SRD.KOMM:='T';
SRSR.index('NRI'); SRSR.prefix();
SRDO.index('AUTO2');
SRDO.prefix(SSTALE.AR,SSTALE.AO,FINFO.KST_MT,'T');
{? SRDO.first()
|| _size:=SRDO.size();
   _i:=0;
   {! |?
      _i+=1;
      progress((_i/_size)*100,'Trwa wycofywanie zmiany klasyfikacji: %1 ...'@[SRDO.SRSR().NRI],'Zmiana klasyfikacji środków'@,0);
      _symbol:=SRDO.SYMBOL;
      {? SRSR.seek(SRDO.SRSR,ref_name(SRDO.SRSR))
      || SRST.index('SROD'); SRST.prefix(SRSR.ref(),SRDO.ROK_F,SRDO.OKRO_F);
         {? SRST.first()
         || _okr:=SRDO.OKRO_F;
            {? SRDO.count()=0 & SRDO.del(1,1)
            || _dalej:=1;
               SRD.updateRecState(_okr);
               {? SRD.isSet() || SRD.updateElements() ?}
            || KOMM.add('Podczas usuwania dokumentu %1 pojawił się problem uniemożliwiający zakończenie operacji.'@[_symbol]);
               _dalej:=0
            ?}
         || KOMM.add('Podczas usuwania dokumentu %1 pojawił się problem: błąd w danych rejestru stanu.'@[_symbol]);
            _dalej:=0
         ?}
       || KOMM.add('Podczas usuwania dokumentu %1 pojawił się problem: nie odnleziono środka trwałego.'@[_symbol]);
          _dalej:=0
       ?};
      _dalej=1 & SRDO.first()
   !};
   prgs_clr()
?};

SRDO.cntx_pop(); SRSR.cntx_pop(); SRST.cntx_pop();

{? ~KOMM.empty()
|| KOMM.select()
|| ROK_F.cntx_psh();
   ROK_F.prefix();
   SSTALE.AR();
   ROK_F.ZK:='N';
   ROK_F.put();
   ROK_F.cntx_pop()
?};
SRD.KOMM:='N';
1


\add_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Formuła generuje dokument zmiany klasyfikacji środka
::   WE: _a - ref środka
::       [_b] - opcjonalnie grupa docelowa, jeżeli generowanie ręczne (uzupełnianie)
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
{? _<2 || _b:=null ?};
SRDT.cntx_psh(); SRST.cntx_psh(); SRSR.cntx_psh(); SRDO.cntx_psh();
EDIT_ES.cntx_psh();
SRSR.prefix();
SRST.index('SROD');
SRST.prefix(_a,SSTALE.AR,SSTALE.AO);
{? SRST.first()
|| SRST.SRSR();
   FINFO.KST_MT();
   EDIT_ES.RODZ:='L';
   SRDO.index('AUTO');
   SRDO.prefix();
   SRDO.blank();
   SRDO.TYP:=FINFO.KST_MT;
   SRDO.SRSR:=SRSR.ref();
   OKRO_F.cntx_psh();
   OKRO_F.prefix();
   SRDO.OKRO_F:=SSTALE.AO;
   OKRO_F.cntx_pop();
   SRDO.ROK_F:=SRDO.OKRO_F().ROK;
   SRDO.DO:=SSTALE.AO().POCZ;
   SRDO.DW:=SSTALE.AO().POCZ;
   SRDO.ROK:=SRDO.OKRO_F().RES;
   SRDO.NR:=exec('bl_srdo_nr','fst');
   exec('be_srdo_symbol','fst');
   SRDO.UWAGI:='Wygenerowany automatycznie w wyniku zmiany klasyfikacji środków';
   SRDO.SRXI:=null;
:: wartości pól
   _break:=0;
   TAB_KONW.index('TAB_KONW');
   TAB_KONW.prefix(SSTALE.AO().RES,SRST.GR);
   {? _b<>null & SRDO.TYP().GR='T'
   || SRDO.GR:=_b
   |? (TAB_KONW.first() & TAB_KONW.GR<>null) & SRDO.TYP().GR='T'
   || SRDO.GR:=TAB_KONW.GR
   || _break:=1
   ?};
   {? ~_break
:: dokument automatyczny
   || SRDO.AUTO:='T';
:: zapis dokumentu i uaktualnienie rejestru stanów
      _wy:=SRDO.add();
      {? _wy
      || SRD.updateRecState(SRDO.OKRO_F,2);
         {? SRD.isSet() || SRD.updateElements() ?}
      ?}
   || _wy:=0
   ?}
?};
EDIT_ES.cntx_pop();
SRDO.cntx_pop(); SRSR.cntx_pop(); SRST.cntx_pop(); SRDT.cntx_pop();
{? _wy & _b<>null || __TSROD.del() ?};
_wy


\temp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Tymczasowa formuła naprawcza
::----------------------------------------------------------------------------------------------------------------------
{? ~FUN.ask('Rok: %1, okres: %2, uruchomić procedurę naprawczą?'@[SSTALE.AR().NAZ,SSTALE.AO().NAZ]) || return(0) ?};
SRST.cntx_psh(); SRSR.cntx_psh();
SRSR.index('NRI');
SRSR.prefix();
{? SRSR.first()
|| _i:=0; _size:=SRSR.size();
   {! |?
      _i+=1;
      echo('Przetwarzanie: %1'@[SRSR.NRI]);
      SRST.index('SROD');
      SRST.prefix(SRSR.ref(),SSTALE.AR,SSTALE.AO);
      {? SRST.first()
      || SRD.updateRecState(SSTALE.AO);
        {? SRD.isSet() || SRD.updateElements() ?}
      ?};
      SRSR.next()
   !};
   echo()
?};
SRST.cntx_pop(); SRSR.cntx_pop()


\chk_tab_krst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.42]
:: OPIS: Walidacja rekordu tabelo TAB_KRST
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
{? TAB_KRST.KST='' || _wy:='KST'
|? TAB_KRST.AR=0 || _wy:='AR'
?};
_wy


\kst_obow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.02]
:: OPIS: Zwraca obowiązującą we wskazanym roku klasyfikację
::   WE: [_a] - Rok dla którego sprawdzamy (INTEGER), jeśli brak to bieżący
::       [_b] - jeżeli podano i jest = 1 to zwraca ref
::   WY: Symbol obowiązującej klasyfikacji środków
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=SSTALE.AO().RES ?};
{? _>1 & _b=1 || _ret:=null || _ret:='' ?};
TAB_KRST.cntx_psh();
TAB_KRST.index('TABKRST');
TAB_KRST.prefix();
{? TAB_KRST.last()
|| _dalej:=1;
   {! |?
      {? TAB_KRST.AR<=_a
      || {? _>1 & _b=1
         || _ret:=TAB_KRST.ref()
         || _ret:=TAB_KRST.KST
         ?}
      ?};
      {? (_>1 & _b=1 & _ret<>null) || _dalej:=0
      |? (_<=1 | _b=0) & _ret<>'' || _dalej:=0
      ?};
      _dalej & TAB_KRST.prev()
   !}
?};
TAB_KRST.cntx_pop();
_ret


\test_degr_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Test czy bieżący środek (bufor SRSR) jest środkiem z metodą degresywną wprowadzonym w trakcie roku
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.GRP<>'T'
|| {? ~(1+SRSR.MP().T='D' | 1+SRSR.MF().T='D' | (FINFO.TOR_D='T' & 1+SRSR.MD().T='D'))
   || return(0)
   ?};
   {? (1+SRSR.MP().T='D' & SRSR.UMOP>0) | (1+SRSR.MF().T='D' & SRSR.UMOF>0)
      | (FINFO.TOR_D='T' & 1+SRSR.MD().T='D' & SRSR.UMOD>0)
   || _umo:=1
   || _umo:=0
   ?};
   {? ~(_umo & SRSR.OKRO_F().POCZ>SRSR.OKRO_F().ROK().POCZ_ROK & SRSR.DE~1<SRSR.OKRO_F().ROK().POCZ_ROK~1)
   || return(0)
   ?}
|| _test:=0;
   SRSR.cntx_psh();
   _tree:=SRSR.ref();
   SRSR.index('TREE');
   SRSR.prefix(_tree);
   {? SRSR.first()
   || _loop:=1;
      {! |?
         _test:=exec('test_degr_sys','fst_kst');
         {? _test || _loop:=0 ?};
         _loop & SRSR.next()
      !}
   ?};
   SRSR.cntx_pop();
   return(_test)
?};
1


\bo_degresywna
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Uzupełnienie danych dla środków z metodą degresywną prowadzanych w trakcie roku z innych systemów
::  OLD: \bo_degresywna/srodobj.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('test_degr_sys','fst_kst')
|| FUN.info('Funkcja dotyczy środków: amortyzowanych metodą degresywną, '
            'częściowo umorzonych w poprzednim systemie i wprowadzanych w trakcie roku.'@);
   return(0)
?};
{? SRD.isAmort()
|| FUN.info('Dla bieżącego środka naliczono już amortyzację, zmiany nie są możliwe.'@);
   return(0)
?};

{? SRSR.GRP='T'
|| exec('bo_zestaw_degr','fst_kst');
   return(0)
?};

SRDO.cntx_psh();

SRDO.index('RODZAJ');
SRDO.prefix('X',$SRSR.ref(),);
{? ~SRDO.first()
|| SRDO.blank(1);
   SRDO.ROK:=SRSR.OKRO_F().RES;
   SRDO.ODD_DOK:=SRSR.ODD;
   SRDO.NR:=#SRSR.ref();
   SRDO.SYMBOL:=$SRSR.ref();
   SRDO.RODZ:='X';
   _add:=1
|| _add:=0
?};

_win:=SRDO.mk_edit('Dane na początek roku'@,0,'srdo_bo');
{? 1+SRSR.MP().T='D'
|| SRDO.win_efld(_win,SRDO,'WARP',,,15,2,,'Wartość podatkowa'@,,'Wartość podatkowa - BO roku'@);
   SRDO.win_efld(_win,SRDO,'UMOP',,,15,2,,'Umorzenie podatkowe'@,,'Umorzenie podatkowe - BO roku'@)
?};

{? 1+SRSR.MF().T='D'
|| SRDO.win_efld(_win,SRDO,'WARF',,,15,2,,'Wartość bilansowa'@,,'Wartość bilansowa - BO roku'@);
   SRDO.win_efld(_win,SRDO,'UMOF',,,15,2,,'Umorzenie bilansowa'@,,'Umorzenie bilansowe - BO roku'@)
?};

{? FINFO.TOR_D='T' & 1+SRSR.MD().T='D'
|| SRDO.win_efld(_win,SRDO,'WARF',,,15,2,,'Wartość dodatkowa'@,,'Wartość dodatkowa - BO roku'@);
   SRDO.win_efld(_win,SRDO,'UMOF',,,15,2,,'Umorzenie dodatkowe'@,,'Umorzenie dodatkowe - BO roku'@)
?};

SRDO.win_edit(_win);
{? SRDO.edit("exec('srdo_bo_check','fst_kst')")
|| {? SRDO.WARP<>0 | SRDO.WARF<>0 | SRDO.UMOP<>0 | SRDO.UMOF<>0
      | (FINFO.TOR_D='T' & SRDO.WARD<>0) | (FINFO.TOR_D='T' & SRDO.UMOD<>0)
   || {? _add || SRDO.add() || SRDO.put() ?}
   || {? ~_add
      || {? FUN.ask('Wprowadzono zerowe wartości, usunąć zapis dotyczący wartości na BO?'@)
         || SRDO.del()
         ?}
      || FUN.info('Wprowadzono zerowe wartości, zapis nie będzie dodany.'@)
      ?}
   ?}
?};

SRDO.cntx_pop()


\degr_podstawa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Zwraca wartość podstawy amortyzacji dla metody degresywnej
::   WE: _a - tor ('P','F' lub 'D')
::   WY: Wartość podstawy amortyzacji dla metody degresywnej
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
SRDO.cntx_psh();
SRDO.index('RODZAJ');
SRDO.prefix('X',$SRSR.ref(),);
{? SRDO.first()
|| _wy:={? _a='P' || SRDO.WARP-SRDO.UMOP
        |? _a='F' || SRDO.WARF-SRDO.UMOF
        |? _a='D' || SRDO.WARD-SRDO.UMOD
        ?}
|| _wy:={? _a='P' || SRST.WARP-SRST.UMOP
        |? _a='F' || SRST.WARF-SRST.UMOF
        |? _a='D' || SRST.WARD-SRST.UMOD
        ?}
?};
SRDO.cntx_pop();
_wy


\bo_zestaw_degr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Wartość na BO dla metody degresywnej - obsługa elementów zestawów
::----------------------------------------------------------------------------------------------------------------------
_tree:=SRSR.ref();
VAR_DEL.delete('__DEG_BO');
SRSR.cntx_psh();
SRSR.index('TREE');
SRSR.prefix(_tree);
{? SRSR.first()
|| __DEG_BO:=tab_tmp(1,'NRI','STRING[18]','NRI',
                       'REF','STRING[16]','REF',
                       'WARP','REAL','Wartość podatkowa'@,
                       'WARF','REAL','Wartość bilansowa'@,
                       'WARD','REAL','Wartość dodatkowa'@,
                       'UMOP','REAL','Umorzenie podatkowe'@,
                       'UMOF','REAL','Umorzenie bilansowe'@,
                       'UMOD','REAL','Umorzenie dodatkowe'@
                       );
   _win:=__DEG_BO.mk_sel('Wartość na początek roku'@,'P',0,'__deg_bo');
   __DEG_BO.win_fld(_win,__DEG_BO,'NRI',,,18,,,'Nr inwentarzowy');
   __DEG_BO.win_fld(_win,__DEG_BO,'WARP',,,12,2,,'Wartość podatkowa');
   __DEG_BO.win_fld(_win,__DEG_BO,'UMOP',,,12,2,,'Umorzenie podatkowe');
   __DEG_BO.win_fld(_win,__DEG_BO,'WARF',,,12,2,,'Wartość bilansowa');
   __DEG_BO.win_fld(_win,__DEG_BO,'UMOF',,,12,2,,'Umorzenie bilansowe');
   {? FINFO.TOR_D='T'
   || __DEG_BO.win_fld(_win,__DEG_BO,'WARD',,,12,2,,'Wartość dodatkowa');
      __DEG_BO.win_fld(_win,__DEG_BO,'UMOD',,,12,2,,'Umorzenie dodatkowe')
   ?};
   __DEG_BO.win_act(_win,,'Formuła','Uzupełnij',,,,"exec('bo_zestaw_uzup','fst_kst')",1);
   __DEG_BO.win_sel(_win);
   _dalej:=1;
   {! |?
      SRDO.cntx_psh();
      __DEG_BO.blank();
      __DEG_BO.NRI:=SRSR.NRI;
      __DEG_BO.REF:=$SRSR.ref();
      SRDO.index('RODZAJ');
      SRDO.prefix('X',$SRSR.ref(),);
      {? SRDO.first()
      || __DEG_BO.WARP:=SRDO.WARP;
         __DEG_BO.WARF:=SRDO.WARF;
         {? FINFO.TOR_D='T' || __DEG_BO.WARD:=SRDO.WARD ?};
         __DEG_BO.UMOP:=SRDO.UMOP;
         __DEG_BO.UMOF:=SRDO.UMOF;
         {? FINFO.TOR_D='T' || __DEG_BO.UMOD:=SRDO.UMOD ?}
      ?};
      __DEG_BO.add();
      SRDO.cntx_pop();
      SRSR.next()
   !}
|| FUN.emsg('Bieżący zestaw nie posiada elementów.'@);
   _dalej:=0
?};
SRSR.cntx_pop();
{? _dalej || __DEG_BO.select() ?}


\bo_zestaw_uzup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Uzupełnia wartości na BO dla elementu
::----------------------------------------------------------------------------------------------------------------------
SRSR.cntx_psh(); SRST.cntx_psh();
SRSR.prefix();
{? SRSR.seek(__DEG_BO.REF,SRSR.name())
|| {? SRD.isAmort()
   || FUN.info('Dla bieżącego środka naliczono już amortyzację, zmiany nie są możliwe.'@);
      SRSR.cntx_pop(); SRST.cntx_pop();
      return(0)
   ?};

   SRDO.cntx_psh();

   SRDO.index('RODZAJ');
   SRDO.prefix('X',__DEG_BO.REF,);
   {? ~SRDO.first()
   || SRDO.blank(1);
      SRDO.ROK:=SRSR.OKRO_F().RES;
      SRDO.ODD_DOK:=SRSR.ODD;
      SRDO.NR:=#SRSR.ref();
      SRDO.SYMBOL:=__DEG_BO.REF;
      SRDO.RODZ:='X';
      _add:=1
   || _add:=0
   ?};

   _win:=SRDO.mk_edit('Dane na początek roku'@,0,'srdo_bo');
   {? 1+SRSR.MP().T='D'
   || SRDO.win_efld(_win,SRDO,'WARP',,,15,2,,'Wartość podatkowa'@,,'Wartość podatkowa - BO roku'@);
      SRDO.win_efld(_win,SRDO,'UMOP',,,15,2,,'Umorzenie podatkowe'@,,'Umorzenie podatkowe - BO roku'@)
   ?};

   {? 1+SRSR.MF().T='D'
   || SRDO.win_efld(_win,SRDO,'WARF',,,15,2,,'Wartość bilansowa'@,,'Wartość bilansowa - BO roku'@);
      SRDO.win_efld(_win,SRDO,'UMOF',,,15,2,,'Umorzenie bilansowa'@,,'Umorzenie bilansowe - BO roku'@)
   ?};

   {? FINFO.TOR_D='T' & 1+SRSR.MD().T='D'
   || SRDO.win_efld(_win,SRDO,'WARF',,,15,2,,'Wartość dodatkowa'@,,'Wartość dodatkowa - BO roku'@);
      SRDO.win_efld(_win,SRDO,'UMOF',,,15,2,,'Umorzenie dodatkowe'@,,'Umorzenie dodatkowe - BO roku'@)
   ?};

   _delete:=0;
   SRDO.win_edit(_win);
   {? SRDO.edit("exec('srdo_bo_check','fst_kst')")
   || {? SRDO.WARP<>0 | SRDO.WARF<>0 | SRDO.UMOP<>0 | SRDO.UMOF<>0
         | (FINFO.TOR_D='T' & SRDO.WARD<>0) | (FINFO.TOR_D='T' & SRDO.UMOD<>0)
      || {? _add || SRDO.add() || SRDO.put() ?}
      || {? ~_add
         || {? FUN.ask('Wprowadzono zerowe wartości, usunąć zapis dotyczący wartości na BO?'@)
            || SRDO.del();
               _delete:=1
            ?}
         || FUN.info('Wprowadzono zerowe wartości, zapis nie będzie dodany.'@)
         ?}
      ?};
      {? ~_delete
      || __DEG_BO.WARP:=SRDO.WARP;
         __DEG_BO.UMOP:=SRDO.UMOP;
         __DEG_BO.WARF:=SRDO.WARF;
         __DEG_BO.UMOF:=SRDO.UMOF;
         {? FINFO.TOR_D='T'
         || __DEG_BO.WARD:=SRDO.WARD;
            __DEG_BO.UMOD:=SRDO.UMOD
         ?}
      || __DEG_BO.WARP:=0;
         __DEG_BO.UMOP:=0;
         __DEG_BO.WARF:=0;
         __DEG_BO.UMOF:=0;
         {? FINFO.TOR_D='T'
         || __DEG_BO.WARD:=0;
            __DEG_BO.UMOD:=0
         ?}
      ?};
      __DEG_BO.put()
   ?};

   SRDO.cntx_pop()
?};

SRSR.cntx_pop(); SRST.cntx_pop()


\srdo_bo_check
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.22]
:: OPIS: Kontrola poprawności wypełnienia pól w oknie z danymi na BO dla nowego środka z metodą degresywną
::   WY: '' lub akronim pola z błędem
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
{? SRDO.WARP<0
|| FUN.emsg('Wartość podatkowa nie może być mniejsza od zera.'@);
   _wy:='WARP'
?};
{? _wy='' & SRDO.WARF<0
|| FUN.emsg('Wartość bilansowa nie może być mniejsza od zera.'@);
   _wy:='WARF'
?};
{? _wy='' & SRDO.UMOP<0
|| FUN.emsg('Umorzenie podatkowe nie może być mniejsze od zera.'@);
   _wy:='UMOP'
?};
{? _wy='' & SRDO.UMOF<0
|| FUN.emsg('Umorzenie bilansowe nie może być mniejsze od zera.'@);
   _wy:='UMOF'
?};
{? _wy='' & FINFO.TOR_D='T' & SRDO.WARD<0
|| FUN.emsg('Wartość dodatkowa nie może być mniejsza od zera.'@);
   _wy:='WARD'
?};
{? _wy='' & FINFO.TOR_D='T' & SRDO.UMOD<0
|| FUN.emsg('Umorzenie dodatkowe nie może być mniejsze od zera.'@);
   _wy:='UMOD'
?};
{? _wy='' & SRDO.WARP<SRDO.UMOP
|| FUN.emsg('Wartość podatkowa nie może być mniejsza od umorzenia.'@);
   _wy:='WARP'
?};
{? _wy='' & SRDO.WARF<SRDO.UMOF
|| FUN.emsg('Wartość bilansowa nie może być mniejsza od umorzenia.'@);
   _wy:='WARF'
?};
{? _wy='' & FINFO.TOR_D='T' & SRDO.WARD<SRDO.UMOD
|| FUN.emsg('Wartość dodatkowa nie może być mniejsza od umorzenia.'@);
   _wy:='WARD'
?};
_wy


\create_t_srdz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła tworząca tabelę tymczasową na powiązane faktury
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__T_SRDZ','__T_SRDD','__T_SRDS');
__T_SRDZ:=tab_tmp(1,'NK','STRING[50]','Symbol',
                    'DTW','DATE','Data zapisu',
                    'DTO','DATE','Data wystawienia',
                    'TR','STRING[40]','Opis dokumentu',
                    'SRDO','STRING[48]','Dokument przyjęcia',
                    'DOK','STRING[48]','Dokument księgowy',
                    'SRDZ_REF','STRING[16]','Połączenie');
__T_SRDD:=__T_SRDZ.ndx_tmp(,,'DOK',,);
__T_SRDS:=__T_SRDZ.ndx_tmp(,,'SRDO',,);
_sel:=__T_SRDZ.mk_sel('Faktury zakupu środka'@,'P',0,'_srdz_fzak',10,5,15,,'U','T',,,,'normal');
__T_SRDZ.win_fld(_sel,,'NK',,,50,,,'Symbol'@,,'Symbol dokumentu księgowego'@);
__T_SRDZ.win_fld(_sel,,'DTW',,,10,,,'Data zapisu'@,,'Data zapisu dokumentu'@);
__T_SRDZ.win_fld(_sel,,'DTO',,,10,,,'Data wystawienia'@,,'Data wystawienia dokumentu'@);
__T_SRDZ.win_fld(_sel,,'TR',,,30,,,'Opis dokumentu'@,,'Opis dokumentu'@);

__T_SRDZ.win_act(_sel,,'Formuła','Dołącz'@,,'Dołączanie powiązania z dokumentem zakupu',,"exec('srdz_add','fst')",1,,,,'D');
__T_SRDZ.win_act(_sel,1,'Formuła','Dołącz'@,,'Dołączanie powiązania z dokumentem zakupu',,"exec('srdz_add','fst')",1,,,,'D');
__T_SRDZ.win_act(_sel,,'Formuła','Usuń'@,,'Usunięcie powiązania z dokumentem zakupu',,"exec('srdz_del','fst')",0,,,,'U');
__T_SRDZ.win_act(_sel,,'Wyświetl',,,,,"exec('srdz_dok_show','fst')");
__T_SRDZ.win_btn(_sel,'text=%1'['Dołącz'@],'menu:D',,,,,,'');
__T_SRDZ.win_btn(_sel,'text=%1'['Usuń'@],'menu:U',,,,,,'noempty');
__T_SRDZ.win_sel(_sel)


:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 bebfe229e0307eacc76c0cb2bc1ed3f586e1e106d088c5ad4df9ccb5dbf45d49d15639949b962117cab48af74f10bccba66ad0342a14a053c38b802b667738a7699a189c3b7b86b34b7f4ac61ab50e44a049d52b9d858cdc288399b8215ac730448ad38bddeeb96133e83689da5f61a99a1b9dd42911d8f6f9d2d735cbb786fd

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: edi_fo_ksef_fks.fml
:: Utworzony: 10.11.2021
:: Autor: JK
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły definicji komunikatów EDI dotyczących KSEF - finanse
::======================================================================================================================


\w_dok_sg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: write - dokument księgowy - start grupa
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('efallkor','edi_wspolne');
1


\w_dok_eg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: write - dokument księgowy - end grupa
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('efdelkor','edi_wspolne');
1


\w_dok_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: write - dokument księgowy - start document
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__MANSKSEF')=type_of('') & __MANSKSEF='BC'
||
   ~~

|? exec('is_ksef','dok_ksef')
|| exec('add_kom','#message','Dokument %1 jest już zapisany.'[DOK.NK],2,EDI_Z.SYM);
   return(0)
?};
VAR_DEL.delete('__czyFaks');
__czyFaks:={? form(DOK.DOKZRODL)<>'' & (1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K')
           || FAKS.use('faktu'+(3+(1-DOK.DOKZRODL)));
              FAKS.prefix();
              FAKS.seek(#(4-DOK.DOKZRODL),FAKS.name)
           || 0
           ?};
exec('czytaj','#stalesys',,KST,XINFO);
exec('edi_buf','edi_fo_ufd');
::exec('edi_buf_fill','edi_fo_ksef_fks',DOK.ref());
EDI_Z.POMIN:='N';
_edi:=exec('edi','edi_wspolne');
_edi.xml_xsd_validate:=1;
1


\w_dok_ed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: write - dokument księgowy - end document
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__sumkor.erase();
VAR_DEL.delete('__N','__V','__P');
{? EDI_Z.POMIN='N'
||
   _is_redy_to_send:=1;
   DOKUM.cntx_psh();
   {? exec('bl_dokum_seek','zbl',DOK)
   ||
      _is_redy_to_send:=exec('is_redy_to_send','zbl_dok',DOKUM.ref())
   ?};
   DOKUM.cntx_pop();
   {? var_pres('__MANSKSEF')=type_of('') & (__MANSKSEF='BC' | __MANSKSEF='BC_ATTA_ADD')
   ||
      ~~
   ||
      DOK.EDI_W:={? _is_redy_to_send || 'T' || 'N' ?};
      DOK.put()
   ?}
?};
~~


\kh_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła - start element dla Podmiot2
::----------------------------------------------------------------------------------------------------------------------
KH.index('SKR'); KH.prefix(2,DOK.KH);
_result:=KH.first();
OS_ADRES.index('RODZAJ1');OS_ADRES.prefix(KH.ref,'H'); __OS_ADRES:=OS_ADRES.last();
_result


\kh_faktor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Znalenie kontrahenta będącego faktorem dokumentu sprzedaży
::   WY: KH.ref() albo null()
::----------------------------------------------------------------------------------------------------------------------
_kh:=null();
{? DOK.RB().TAB='KH'
|| KH.cntx_psh(); KH.prefix();
   {? KH.seek(DOK.RB().REF)
   || _kh:=KH.ref()
   ?};
   KH.cntx_pop()
?};
_kh


\kh_faktor_v
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.51]
:: OPIS: Znalenie kontrahenta będącego faktorem dokumentu sprzedaży - rachunek VAT
::   WY: KH.ref() albo null()
::----------------------------------------------------------------------------------------------------------------------
_kh:=null();
{? DOK.RB_V().TAB='KH'
|| KH.cntx_psh(); KH.prefix();
   {? KH.seek(DOK.RB_V().REF) || _kh:=KH.ref() ?};
   KH.cntx_pop()
?};
_kh


\podmiot3_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - formuła start
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
VAR_DEL.delete('__P3');
__P3:=obj_new('JAK','KH');
__P3.JAK:=0;
__P3.KH:=null();
FAKSKH.use('fkpd'+(4+(4-$DOK.ref())));
FAKSKH.index('DOK');
FAKSKH.prefix(DOK.ref());
{? DOK.KH_ODB<>null() & DOK.KH_ODB().KH_LNK<>null()
|| __P3.JAK:=1;
   __P3.KH:=DOK.KH_ODB().KH_LNK
|? FAKSKH.first()
|| __P3.JAK:=2;
   __P3.KH:=FAKSKH.KH
?};
{? __P3.JAK>0
|| _ret:=1
|| VAR_DEL.delete('__P3')
?};
_ret


\podmiot3_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - formuła end
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || {? FAKSKH.first()
      || __P3.JAK:=2;
         __P3.KH:=FAKSKH.KH
      || __P3.JAK:=0
      ?}
   |? __P3.JAK=2
   || {? FAKSKH.next()
      || __P3.KH:=FAKSKH.KH
      || __P3.JAK:=0
      ?}
   || __P3.JAK:=0
   ?};
   {? __P3.JAK>0
   || _ret:=1
   || VAR_DEL.delete('__P3')
   ?}
?};
_ret


\podmiot3_nip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - NIP
::   WY: NIP
::----------------------------------------------------------------------------------------------------------------------
_nip:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _nip:=DOK.O_NIP
   |? __P3.JAK=2
   || _nip:=FAKSKH.NIP
   ?}
::   {? _nip=''
::   || _nip:=exec('kh_pole','edi_fo_ksef',__P3.KH,'NIP')
::   ?}
?};
_nip:=exec('niptostr','#string',_nip);
_nip


\podmiot3_nrid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - miasto
::   WY: nr id
::----------------------------------------------------------------------------------------------------------------------
_nr:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _nr:=DOK.KH_ODB().KH_LNK().OSOBA().PESEL
   |? __P3.JAK=2
   || _nr:=FAKSKH.KH().OSOBA().PESEL
   ?}
::   {? _nr=''
::   || _nr:=exec('kh_pole','edi_fo_ksef',__P3.KH,'PESEL')
::   ?}
?};
_nr


\podmiot3_naz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - nazwa
::   WY: Nazwa
::----------------------------------------------------------------------------------------------------------------------
_naz:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _naz:=DOK.O_NAZ
   |? __P3.JAK=2
   || _naz:=FAKSKH.NAZ
   ?}
::   {? _naz=''
::   || _naz:=exec('kh_pole','edi_fo_ksef',__P3.KH,'NAZ_P')
::   ?}
?};
_naz


\podmiot3_krajiso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - symbol kraju
::   WY: kraj iso
::----------------------------------------------------------------------------------------------------------------------
_sym:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _sym:=DOK.O_KRISO().SYM
   |? __P3.JAK=2
   || _sym:=FAKSKH.KRAJISO().SYM
   ?}
::   {? _sym=''
::   || _sym:=exec('kh_pole','edi_fo_ksef',__P3.KH,'KRAJISO().SYM')
::   ?}
?};
_sym


\podmiot3_miasto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - miasto
::   WY: miasto
::----------------------------------------------------------------------------------------------------------------------
_mia:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _mia:=DOK.O_MIASTO
   |? __P3.JAK=2
   || _mia:=FAKSKH.MIASTO
   ?};
   {? _mia=''
   || _mia:=exec('kh_pole','edi_fo_ksef',__P3.KH,'MIASTO')
   ?}
?};
_mia


\podmiot3_ulica
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - ulica
:   WY: ulica
::----------------------------------------------------------------------------------------------------------------------
_ul:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _ul:=DOK.O_UL
   |? __P3.JAK=2
   || _ul:=FAKSKH.UL
   ?}
::   {? _ul=''
::   || _ul:=exec('kh_pole','edi_fo_ksef',__P3.KH,'UL')
::   ?}
?};
_ul


\podmiot3_kdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - kod pocztowy
::   WY: kod pocztowy
::----------------------------------------------------------------------------------------------------------------------
_kdp:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _kdp:=exec('KodPoczta','edi_fo_ksef',DOK.O_POCZ,DOK.O_KPOCZ)
   |? __P3.JAK=2
   || _kdp:=exec('KodPoczta','edi_fo_ksef',FAKSKH.POCZ,FAKSKH.KPOCZ)
   ?}
::   {? _kdp=''
::   || _kdp:=exec('kh_pole','edi_fo_ksef',__P3.KH,'KPOCZ')
::   ?}
?};
_kdp


\podmiot3_poczta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - poczta
::   WY: poczta
::----------------------------------------------------------------------------------------------------------------------
_poczta:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _poczta:=exec('KodPoczta','edi_fo_ksef',DOK.O_POCZ,DOK.O_KPOCZ,'P')
   |? __P3.JAK=2
   || _poczta:=exec('KodPoczta','edi_fo_ksef',FAKSKH.POCZ,FAKSKH.KPOCZ,'P')
   ?}
::   {? _poczta=''
::   || _poczta:=exec('kh_pole','edi_fo_ksef',__P3.KH,'POCZ')
::   ?}
?};
_poczta


\podmiot3_rola
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - rola
::   WY: rola
::----------------------------------------------------------------------------------------------------------------------
_rola:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _rola:='2'
   |? __P3.JAK=3
   || _rola:='1'
   || _rola:=exec('rola_podmiotu_ksef','edi_fo_ksef',FAKSKH.ROLA).ROLA
   ?}
?};
_rola


\podmiot3_rola_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot3" - opis roli
::   WY: opis roli
::----------------------------------------------------------------------------------------------------------------------
_op:='';
{? var_press('__P3')>0
|| {? __P3.JAK=2
   || _op:=exec('rola_podmiotu_ksef','edi_fo_ksef',FAKSKH.ROLA).OPISROLI
   ?}
?};
_op


\podmiot3_dom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: "Podmiot3" - nr domu
::   WY: dom
::----------------------------------------------------------------------------------------------------------------------
_dom:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _dom:=DOK.O_DOM
   |? __P3.JAK=2
   || _dom:=FAKSKH.DOM
   ?}
::   {? _dom=''
::   || _dom:=exec('kh_pole','edi_fo_ksef',__P3.KH,'DOM')
::   ?}
?};
_dom


\podmiot3_lokal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: "Podmiot3" - nr lokalu
::   WY: lokal
::----------------------------------------------------------------------------------------------------------------------
_lok:='';
{? var_press('__P3')>0
|| {? __P3.JAK=1
   || _lok:=DOK.O_LOKAL
   |? __P3.JAK=2
   || _lok:=FAKSKH.LOKAL
   ?}
::   {? _lok=''
::   || _lok:=exec('kh_pole','edi_fo_ksef',__P3.KH,'LOKAL')
::   ?}
?};
_lok


\podmiot_upow_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot upoważniony" - formuła start
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
VAR_DEL.delete('__PU');
__PU:=obj_new('JAK','KH');
__PU.JAK:=0;
__PU.KH:=null();
DOKPOLA.index('UNIK');
DOKPOLA.prefix(DOK.ref());
{? DOKPOLA.first()
|| {? DOKPOLA.EGZ_KH<>null()
   || __PU.JAK:=1;
      __PU.KH:=DOKPOLA.EGZ_KH
   |? DOKPOLA.PP_KH<>null()
   || __PU.JAK:=3;
      __PU.KH:=DOKPOLA.PP_KH
   ?}
?};
{? __PU.JAK>0
|| _ret:=1
|| VAR_DEL.delete('__PU')
?};
_ret


\podmiot_upow_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot upoważniony" - formuła end
::   WY: 0
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
VAR_DEL.delete('__PU');
_ret


\fa_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Fa" - formuła start
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
DOKPOLA.index('UNIK');
DOKPOLA.prefix(DOK.ref());
{? ~DOKPOLA.first() || DOKPOLA.blank() ?};
exec('fa_suma','edi_fo_ksef_fks');
1


\fa2_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Fa" - formuła start
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
DOKPOLA.index('UNIK');
DOKPOLA.prefix(DOK.ref());
{? ~DOKPOLA.first() || DOKPOLA.blank() ?};
exec('fa2_suma','edi_fo_ksef_fks');
1


\var_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: usuwa zmienne zainicjowane dla potrzeb xml ksef
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('sFN23','sFV23','sFVW23','sFN8','sFV8','sFVW8','sFN5','sFV5','sFVW5','sFN0','sFV0','sFNZ','sFB',
               'sFV','sOO','sNO','sNW','nBW','__vx','__vfp','__eb_sum','__vk','__nrfazal','__lnrfazal','__wz','__lwz');
VAR_DEL.delete('__nkk','__nkkl','__czyFaks','sP_13_6_1','sP_13_6_2','sP_13_6_3','sP_13_8','sP_13_9','sP_13_10',
               '__nrfakor');
1


\fa_suma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Suma pozycji VAT dokumentu
::   WE: _a - korekta 0/1
::  OLD: \fa_suma/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_korekta:={? var_pres('_a')>0 || _a || 0 ?};
sFN23:=sFV23:=sFVW23:=~~;
sFN8:=sFV8:=sFVW8:=~~;
sFN5:=sFV5:=sFVW5:=~~;
sFN0:=sFV0:=~~;
sFNZ:=~~;
sFB:=~~;
sFN:=~~;
sFV:=~~;
sOO:=~~;
sNO:=~~;
sNW:=sBW:=~~;
VPOZ.index('VDPR'); VPOZ.prefix(DOK.ref());
{? VPOZ.first()
|| _wal:=DOK.WAL<>null & DOK.WAL<>FINFO.NAROD;
   {!
   |? _pr:=3+|VPOZ.PR().KOD;
      _netto:={? _wal || VPOZ.NETTO_W || VPOZ.NETTO ?};
      _vat:={? _wal || VPOZ.VAT_W || VPOZ.VAT ?};
      _brutto:={? _wal || VPOZ.BRUTTO_W || VPOZ.BRUTTO ?};
      {? _pr='22 ' | _pr='23 '
      || {? var_press('sFN23')=0 || sFN23:=0 ?};
         {? var_press('sFV23')=0 || sFV23:=0 ?};
         sFN23+=_netto;
         sFV23+=_vat;
         {? _wal
         || {? var_press('sFVW23')=0 || sFVW23:=0 ?};
            sFVW23+=VPOZ.VAT
         ?}
      |? _pr='8 %' | _pr='7 %'
      || {? var_press('sFN8')=0 || sFN8:=0 ?};
         {? var_press('sFV8')=0 || sFV8:=0 ?};
         sFN8+=_netto;
         sFV8+=_vat;
         {? _wal
         || {? var_press('sFVW8')=0 || sFVW8:=0 ?};
            sFVW8+=VPOZ.VAT
         ?}
      |? _pr='5 %'
      || {? var_press('sFN5')=0 || sFN5:=0 ?};
         {? var_press('sFV5')=0 || sFV5:=0 ?};
         sFN5+=_netto;
         sFV5+=_vat;
         {? _wal
         || {? var_press('sFVW5')=0 || sFVW5:=0 ?};
            sFVW5+=VPOZ.VAT
         ?}
      |? _pr='0 %'
      || {? var_press('sFN0')=0 || sFN0:=0 ?};
         sFN0+=_netto
      |? _pr='Zwo'
      || {? var_press('sFNZ')=0 || sFNZ:=0 ?};
         sFNZ+=_netto
      ?};
      {? VPOZ.RVAT().RVAT().KVAT().SYM='SprzNOp' & 7+VPOZ.GRVAT().KOD='SprPDos'
         & (VPOZ.PR().KOD=' -' | VPOZ.PR().KOD=' -o')
         | KVAT.SYM='ZakOpod' & VPOZ.GRVAT().KOD='SprPDost' & (VPOZ.PR().KOD=' -' | VPOZ.PR().KOD=' -o')
      || {? var_press('sOO')=0 || sOO:=0 ?};
         sOO+=_netto
      |? KVAT.SYM*'SprzNOp' & (VPOZ.PR().KOD=' -' | VPOZ.PR().KOD=' -o')
      || {? var_press('sNO')=0 || sFN23:=0 ?};
         sNO+=_netto
      ?};
      {? var_press('sFV')=0 || sFV:=0 ?};
      {? var_press('sFB')=0 || sFB:=0 ?};
      {? var_press('sFN')=0 || sFN:=0 ?};
      sFV+=_vat;
      sFB+=_brutto;
      sFN+=_netto;
      {? var_press('sNW')=0 || sNW:=0 ?};
      sNW+=VPOZ.NETTO_W;
      {? var_press('sBW')=0 || sBW:=0 ?};
      sBW+=VPOZ.BRUTTO_W;
      VPOZ.next()
   !};
   {? _korekta=1
   || {? var_press('sFN23')>0 || sFN23:=-sFN23 ?};
      {? var_press('sFV23')>0 || sFV23:=-sFV23 ?};
      {? var_press('sFVW23')>0 || sFVW23:=-sFVW23 ?};
      {? var_press('sFN8')>0 || sFN8:=-sFN8 ?};
      {? var_press('sFV8')>0 || sFV8:=-sFV8 ?};
      {? var_press('sFVW8')>0 || sFVW8:=-sFVW8 ?};
      {? var_press('sFN5')>0 || sFN5:=-sFN5 ?};
      {? var_press('sFV5')>0 || sFV5:=-sFV5 ?};
      {? var_press('sFVW5')>0 || sFVW5:=-sFVW5 ?};
      {? var_press('sFN0')>0 || sFN0:=-sFN0 ?};
      {? var_press('sFV0')>0 || sFV0:=-sFV0 ?};
      {? var_press('sFNZ')>0 || sFNZ:=-sFNZ ?};
      {? var_press('sFB')>0 || sFB:=-sFB ?};
      {? var_press('sFV')>0 || sFV:=-sFV ?};
      {? var_press('sOO')>0 || sOO:=-sOO ?}
   ?}
?}


\fa2_suma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Suma pozycji VAT dokumentu
::   WE: _a - korekta 0/1
::  OLD: \fa_suma/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_korekta:={? var_pres('_a')>0 || _a || 0 ?};
sFN23:=sFV23:=sFVW23:=~~;
sFN8:=sFV8:=sFVW8:=~~;
sFN5:=sFV5:=sFVW5:=~~;
sFN0:=sFV0:=~~;
sFNZ:=~~;
sFB:=~~;
sFN:=~~;
sFV:=~~;
sP_13_6_1:=sP_13_6_2:=sP_13_6_3:=~~;
sP_13_8:=sP_13_9:=sP_13_10:=~~;
sOO:=~~;
sNO:=~~;
sNW:=sBW:=~~;
VPOZ.index('VDPR'); VPOZ.prefix(DOK.ref());
{? VPOZ.first()
|| _wal:=DOK.WAL<>null & DOK.WAL<>FINFO.NAROD;
   {!
   |? _pr:=3+|VPOZ.PR().KOD;
      _netto:={? _wal || VPOZ.NETTO_W || VPOZ.NETTO ?};
      _vat:={? _wal || VPOZ.VAT_W || VPOZ.VAT ?};
      _brutto:={? _wal || VPOZ.BRUTTO_W || VPOZ.BRUTTO ?};
      {? VPOZ.RVAT().RVAT().KVAT().SYM*'SprzNOp'=0
      || {? _pr='22 ' | _pr='23 '
         || {? var_press('sFN23')=0 || sFN23:=0 ?};
            {? var_press('sFV23')=0 || sFV23:=0 ?};
            sFN23+=_netto;
            sFV23+=_vat;
            {? _wal
            || {? var_press('sFVW23')=0 || sFVW23:=0 ?};
               sFVW23+=VPOZ.VAT
            ?}
         |? _pr='8 %' | _pr='7 %'
         || {? var_press('sFN8')=0 || sFN8:=0 ?};
            {? var_press('sFV8')=0 || sFV8:=0 ?};
            sFN8+=_netto;
            sFV8+=_vat;
            {? _wal
            || {? var_press('sFVW8')=0 || sFVW8:=0 ?};
               sFVW8+=VPOZ.VAT
            ?}
         |? _pr='5 %'
         || {? var_press('sFN5')=0 || sFN5:=0 ?};
            {? var_press('sFV5')=0 || sFV5:=0 ?};
            sFN5+=_netto;
            sFV5+=_vat;
            {? _wal
            || {? var_press('sFVW5')=0 || sFVW5:=0 ?};
               sFVW5+=VPOZ.VAT
            ?}
         ?}
      ?};
:: stawka vat 0 oraz zwolniona
      {? _pr='0 %'
      || {? var_press('sFN0')=0 || sFN0:=0 ?};
         sFN0+=_netto;
         {? VPOZ.RVAT().RVAT().KVAT().SYM*'_WWspDo'=0 & KVAT.SYM<>'SprzEksp'
         || {? var_press('sP_13_6_1')=0 || sP_13_6_1:=0 ?};
            sP_13_6_1+=_netto
         |? KVAT().SYM*'_WWspDo'>0
         || {? var_press('sP_13_6_2')=0 || sP_13_6_2:=0 ?};
            sP_13_6_2+=_netto
         |? KVAT.SYM='SprzEksp'
         || {? var_press('sP_13_6_3')=0 || sP_13_6_3:=0 ?};
            sP_13_6_3+=_netto
         ?}
      |? _pr='Zwo'
      || {? var_press('sFNZ')=0 || sFNZ:=0 ?};
         sFNZ+=_netto
      |? (_pr=' -' | _pr=' -o')
      || {? VPOZ.RVAT().RVAT().KVAT().SYM*'_WWspD'>0
         || {? VPOZ.GRVAT().KOD<>'SprPKWSU'
            || {? var_press('sP_13_8')=0 || sP_13_8:=0 ?};
               sP_13_8+=_brutto
            || {? var_press('sP_13_9')=0 || sP_13_9:=0 ?};
               sP_13_9+=_brutto
            ?}
         ?};
         {? VPOZ.GRVAT().KOD*'SprPDos'>0
         || {? var_press('sP_13_10')=0 || sP_13_10:=0 ?};
            sP_13_10+=_brutto
         ?}
      ?};
      {? var_press('sFV')=0 || sFV:=0 ?};
      {? var_press('sFB')=0 || sFB:=0 ?};
      {? var_press('sFN')=0 || sFN:=0 ?};
      sFV+=_vat;
      sFB+=_brutto;
      sFN+=_netto;
      {? var_press('sNW')=0 || sNW:=0 ?};
      sNW+=VPOZ.NETTO_W;
      {? var_press('sBW')=0 || sBW:=0 ?};
      sBW+=VPOZ.BRUTTO_W;
      VPOZ.next()
   !};
   {? _korekta=1
   || {? var_press('sFN23')>0 || sFN23:=-sFN23 ?};
      {? var_press('sFV23')>0 || sFV23:=-sFV23 ?};
      {? var_press('sFVW23')>0 || sFVW23:=-sFVW23 ?};
      {? var_press('sFN8')>0 || sFN8:=-sFN8 ?};
      {? var_press('sFV8')>0 || sFV8:=-sFV8 ?};
      {? var_press('sFVW8')>0 || sFVW8:=-sFVW8 ?};
      {? var_press('sFN5')>0 || sFN5:=-sFN5 ?};
      {? var_press('sFV5')>0 || sFV5:=-sFV5 ?};
      {? var_press('sFVW5')>0 || sFVW5:=-sFVW5 ?};
      {? var_press('sFN0')>0 || sFN0:=-sFN0 ?};
      {? var_press('sFV0')>0 || sFV0:=-sFV0 ?};
      {? var_press('sFNZ')>0 || sFNZ:=-sFNZ ?};
      {? var_press('sFB')>0 || sFB:=-sFB ?};
      {? var_press('sFV')>0 || sFV:=-sFV ?};
      {? var_press('sOO')>0 || sOO:=-sOO ?}
   ?}
?}


\kodwaluty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Zwraca kod waluty
::----------------------------------------------------------------------------------------------------------------------
{? DOK.WAL<>null
|| DOK.WAL().KOD
|| FINFO.NAROD().KOD
?}


\nst_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Czy nowy środek transportu
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_czy:=(DOKPOLA.NST_DATA<>date(0,0,0) | DOKPOLA.NST_RODZ<>'')


\nst_poj_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Czy nowy środek transportu z danymi pojazdu
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
exec('nst_czy','edi_fo_ksef_fks') & DOKPOLA.NST_POJ<>null()


\nst_ladowy_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Czy nowy środek transportu lądowy
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
exec('nst_czy','edi_fo_ksef_fks') & DOKPOLA.NST_RODZ=exec('nst_ladowy','jpk_log')


\nst_plywajacy_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Czy nowy środek transportu pływający
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
exec('nst_czy','edi_fo_ksef_fks') & DOKPOLA.NST_RODZ=exec('nst_plywajacy','jpk_log')


\nst_powietrzny_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Czy nowy środek transportu powietrzny
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
exec('nst_czy','edi_fo_ksef_fks') & DOKPOLA.NST_RODZ=exec('nst_powietrzny','jpk_log')


\uds_opis_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Numer procedury marży UDS (używane, dzieła sztuki)
::   WY: 0 / 1 / 2 / 3
::----------------------------------------------------------------------------------------------------------------------
_nr:=3;
_jest:=0;
{!
|? {? DOKPOLA.UDS_OPIS=exec('uds_opis','jpk_log',_nr)
   || _jest:=1
   || _nr-=1
   ?};
   ~_jest & _nr>=1
!};
_nr


\r_faktury
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Rodzaj faktury
::   WE: 1-7 - rodzaj faktury
::   WY: nazwa rodzaju faktury
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_result:={? __czyFaks
         || exec('RodzajFaktury','edi_fo_ksef')
         || {? _a=2
            || 'KOR'
            |? _a=3
            || 'ZAL'
            |? _a=4
            || 'ROZ'
            |? _a=5
            || 'UPR'
            |? _a=6
            || 'KOR_ZAL'
            |? _a=7
            || 'KOR_ROZ'
            || 'VAT'
            ?}
         ?};
_result


\fa_wiersze_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: formuła start element dla fa wiersze
::----------------------------------------------------------------------------------------------------------------------
_result:=0; __pozWN:=__pozWB:=0; __pozPR:=0;
{? __czyFaks
|| FAP.use('fakpo'+(3+(1-DOK.DOKZRODL)));
   FAP.index('FAP');FAP.prefix(FAKS.ref());
   _result:=FAP.first() & exec('RodzajFaktury','edi_fo_ksef')<>'ZAL'
|| POZF.index('DOK'); POZF.prefix(DOK.ref);
   _result:=POZF.first() & DOK.DOK_REJ().KSRF<>3;
   {? _result
   || POZF.cntx_psh();
      {!
      |? __pozWN+=POZF.WN;
         __pozWB+=POZF.WB;
         {? __pozPR=0 & POZF.EF_PROC<>null() || __pozPR:=1 ?};
         POZF.next()
      !};
      POZF.cntx_pop()
   ?}
?};
_result


\zamowienie_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: formuła start element dla zamowienie
::----------------------------------------------------------------------------------------------------------------------
_result:=0; __pozWN:=__pozWB:=0; __pozPR:=0;
{? __czyFaks
|| FAP.use('fakpo'+(3+(1-DOK.DOKZRODL)));
   FAP.index('FAP');FAP.prefix(FAKS.ref());
   _result:=FAP.first() & exec('RodzajFaktury','edi_fo_ksef')='ZAL'
|| POZF.index('DOK'); POZF.prefix(DOK.ref);
   _result:=POZF.first() & DOK.DOK_REJ().KSRF=3;
   {? _result
   || POZF.cntx_psh();
      {!
      |? __pozWN+=POZF.WN;
         __pozWB+=POZF.WB;
         {? __pozPR=0 & POZF.EF_PROC<>null() || __pozPR:=1 ?};
         POZF.next()
      !};
      POZF.cntx_pop()
   ?}
?};
_result


\fa_wiersze_k_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: formuła start element dla fa wiersze
::----------------------------------------------------------------------------------------------------------------------
_result:=0; __pozWN:=__pozWB:=0; __pozPR:=0;
{? __czyFaks
|| __vk:=1;
   FAP.use('fakpo'+(3+(1-DOK.DOKZRODL)));
   FAP.index('FAP');FAP.prefix(FAKS.ref());
   _result:={? FAP.first()
            || exec('wysw_kor','faktury_poz');
               exec('RodzajFaktury','edi_fo_ksef')<>'KOR_ZAL'
            || 0
            ?}
|| POZF.index('DOK'); POZF.prefix(DOK.ref);
   _result:=POZF.first() & DOK.DOK_REJ().KSRF<>3;
   {? _result
   || POZF.cntx_psh();
      {!
      |? __pozWN+=POZF.WN;
         __pozWB+=POZF.WB;
         {? __pozPR=0 & POZF.EF_PROC<>null() || __pozPR:=1 ?};
         POZF.next()
      !};
      POZF.cntx_pop()
   ?}
?};
_result


\fa_wiersze_k_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: formuła end element dla fa wiersz
::----------------------------------------------------------------------------------------------------------------------
_result:={? __czyFaks
         || {? __vk=2
            || __vk:=1;
               FAP.next()
            || __vk:=2;
               1
            ?}
         || POZF.next()
         ?};
{? _result=0 || exec('wiersze_ee','edi_fo_ksef_fks') || 1 ?}


\zamowienie_k_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: formuła start element dla zamowienie
::----------------------------------------------------------------------------------------------------------------------
_result:=0; __pozWN:=__pozWB:=0; __pozPR:=0;
{? __czyFaks
|| __vk:=1;
   FAP.use('fakpo'+(3+(1-DOK.DOKZRODL)));
   FAP.index('FAP'); FAP.prefix(FAKS.ref());
   _result:={? FAP.first()
            || exec('wysw_kor','faktury_poz');
               exec('RodzajFaktury','edi_fo_ksef')='KOR_ZAL'
            || 0
            ?}
|| POZF.index('DOK'); POZF.prefix(DOK.ref);
   _result:=POZF.first() & DOK.DOK_REJ().KSRF=3;
   {? _result
   || POZF.cntx_psh();
      {!
      |? __pozWN+=POZF.WN;
         __pozWB+=POZF.WB;
         {? __pozPR=0 & POZF.EF_PROC<>null() || __pozPR:=1 ?};
         POZF.next()
      !};
      POZF.cntx_pop()
   ?}
?};
_result


\wiersze_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: formuła end element dla faWiersze i Zamowienie
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__pozWN','__pozWB','__pozPR'); 0


\prefiks_nabywcy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: zwraca kod iso nabywcy
::   WE: _a - REF KH
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=KH.ref() ?};
_kod:='';
NIPY.cntx_psh();
NIPY.index('SNIP'); NIPY.prefix(_a);
_kod:={? NIPY.first()
      || {? NIPY.KRAJ().KOD='GR' || 'EL' || NIPY.KRAJ().KOD ?}
      || ''
      ?};
NIPY.cntx_pop();
_kod


\sv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: stawka vat w postaci 7
::   WE: _a - SLO.KOD
::----------------------------------------------------------------------------------------------------------------------
_sv:=exec('sv','edi_form',_a);
{? _sv+3='.00' || _sv:=_sv-3
|? _a=' -'     || _sv:='np'
|? _a=' -o'    || _sv:='oo'
|? _a=' -zw0'  || _sv:='zw'
|? _a='Zwol.'  || _sv:='zw'
?};
_sv


\procedura
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Zwraca wartość dla elementu procedura
::----------------------------------------------------------------------------------------------------------------------
_result:='';
{? __pozPR
|| _result:=POZF.EF_PROC().KOD
|| {? DOK.JPK_PROC<>''
   || _text:='WSTO_EE,IED,TT_D,I_42,I_63,B_SPV,B_SPV_DOSTAWA,B_MPV_PROWIZJA';
      _proc:=spli_str(_text,',');
      _dok_proc:=spli_str(DOK.JPK_PROC,',');
      _ii:=0;
      {!
      |? _ii+=1; _jj:=0;
         {!
         |? _jj+=1;
            {? _dok_proc[_ii]=_proc[_jj] || _result:=_dok_proc[_ii] ?};
            _result='' & _jj<obj_len(_proc)
         !};
         _result='' & _ii<obj_len(_dok_proc)
      !};
      &_proc; &_dok_proc
   ?}
?};
_result


\dok_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: dokumenty do zapisu - sprzedaży
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('dok_dok','edi_fo_ksef_fks',0)


\dok_spr_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: dokumenty do zapisu - sprzedazy korygujące
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('dok_dok','edi_fo_ksef_fks',1)


\dok_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: dokumenty do zapisu - dok
::       gdy zmienna BEER.MW='F' to wywolanie dla biezacej faktury w buforze bez dodatkowego dialogu
::   WE: _a - 0-dokument księgowy, 1-dokument korygujący
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kor:=_a;

_result:=obj_new('DOK','TXT');
_result.DOK:=~~;
_result.TXT:='';

_edi:=__ZWSEDIUNIW;
_ref:=_edi.REF;

_Sel:=exec('doc2write','edi_xml');

:: jeden dokument
{? _ref
||
   _add:=0;
   {? _ref
   ||
      DOK.cntx_psh();
      DOK.use(ref_name(_ref));
      DOK.prefix();
      {? DOK.seek(_ref)
      ||
         _dok:=DOK.ref();
         _sym:=DOK.NK;
         KH.cntx_psh();
         KH.index('SKR'); KH.prefix(2,DOK.KH);
         _kh:={? KH.first() || KH.ref || null ?};
         KH.cntx_pop();
         _add:=1
      ?};
      DOK.cntx_pop()

   |? DOK.get()
   ||
      _dok:=DOK.ref();
      _sym:=DOK.NK;
       KH.cntx_psh();
       KH.index('SKR'); KH.prefix(2,DOK.KH);
       _kh:={? KH.first() || KH.ref || null ?};
       KH.cntx_pop();
      _add:=1
   ?};
   {? _add
   ||
      _Sel.REF:=$_dok;
      _Sel.ID:=_sym;
      _Sel.KH:=$_kh;
      DEZEZW.cntx_psh();
      DEZEZW.index('WR');
      DEZEZW.prefix('ZWS',EDI_Z.C,EDI_Z.WR,_kh,);
      _Sel.PATH:={? DEZEZW.last() || DEZEZW.FILE_PTH || '' ?};
      DEZEZW.cntx_pop();
      {? _Sel.add() || _result.DOK:=_Sel ?}
   ?};
   return(_result)
?};
{? _edi.sza
||
   _result.TXT:='Przetwarzanie bezdialogowe. Nie wskazano dokumentu.';
   return(_result)
?}


\dok_korekta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Wyszukiwanie nagłówka dokumentu korygowanego - 12 okresow wstecz
::   WY: DOK.ref
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_fdok:='';
_okrod:={? SSTALE.AO().NR-12<=0 || 0 || SSTALE.AO().NR-12 ?};
OKRO_F.cntx_psh; ROK_F.cntx_psh();
OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(FIRMA.ref());
SSTALE.AO(); SSTALE.AR();
_fdok+='\'doku'+ ROK_F.KOD+form(OKRO_F.NR,-2)+'\',';
_ii:=12;
{? OKRO_F.prev()
|| {!
   |? OKRO_F.ROK();
      {? OKRO_F.NAZ<>'Bilans zamknięcia' & OKRO_F.NAZ<>'Bilans otwarcia'
      || _ii-=1;
         _fdok+='\'doku'+ ROK_F.KOD+form(OKRO_F.NR,-2)+'\','
      ?};
      OKRO_F.prev() & _ii>0
   !};
   _fdok:=_fdok-1
?};
OKRO_F.cntx_pop(); ROK_F.cntx_pop();
_nk:=DOK.KOR; _rej:=DOK.REJ().KOD;
_kh:=DOK.WYS().KOD;
_dk:=DOK.DWKOR;
_refd:=$DOK.ref();
_nksql:=STR.gsub(_nk ,'\'','\'\'');
_rejsql:=STR.gsub(_rej,'\'','\'\'');
_sql:='/*+MASK_FILTER(DOK,:_c) '+
          'INDEX (DOK,REJ_SYM) */ '+
          'ODD.OD ODDZIAL, REJ.KOD REJESTR, DOK.NR NR, SLO.KOD KH, DOK.SYM_ZEW, DOK.NK SYMBOL, DOK.DTO DATA, DOK.REFERENCE REF '+
          'from @DOK left join REJ using (DOK.REJ,REJ.REFERENCE) '+
          'left join SLO using (DOK.WYS,SLO.REFERENCE) '+
          'join ODD using (DOK.ODD,ODD.REFERENCE) ';
_where:='where DOK.NK=\':_a\' and DOK.DTO=to_date(:_f) and SLO.KOD=\':_e\' ';
_where+={? _refd='' || '' || 'and DOK.REFERENCE<>\':_d\' ' ?};
_tab:=sql('select '+_sql+_where+'order by 1,2,3',_nksql,_rejsql,_fdok,_refd,_kh,_dk);
{? _tab.first()
|| _result:=_tab.REF
||  exec('add_kom','#message',
            'Nie znaleziono dokumentu księgowego o symbolu %1. Należy zweryfikować dane faktury korygowanej.'@[DOK.KOR],
            2,EDI_Z.SYM)
?};
_result


\inv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formułą główna komunikatu ECOD-INV v7.53 | Faktura (z dnia 2013-12-18)
::   WE: _a - instrukacja
::       _b - B_PREL.ref()
::   WY: _a='ports' - zwraca porty
::----------------------------------------------------------------------------------------------------------------------
{? _a='ports'
|| exec('FindAndGet','#table',B_PREL,_b,,"exec('ports','#b_port','edi_fo_ksef_fks','b_port_dok')")
|? _a='parses'
|| 1
|? _a='setEnv'
|| exec('FindAndGet','#table',B_PREL,_b,,"
      __PARSES.setEnv('FKS');
      exec('init','fks');
      exec('fks_dks','fks')
      ");
   ~~
|| ~~
?}


\b_port_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Porty DOK - dokument księgowy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::# kind=WE,   symbol=DOK,      type=_DOK,    name=Dokument księgowy,   required=N,    fml_val="NOT_EDITABLE"
::# kind=WY,   symbol=DOK,      type=_DOK,    name=Dokument księgowy,   required=N
1


\edokument
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Uruchamia tworzenie e-dokumentu
::   WE:
::   WY
::----------------------------------------------------------------------------------------------------------------------
exec('test_ksef','edi_fo_ksef_fks')


\test_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: formuła testowa
::   WE: _a - DOK.ref
::   WY
::----------------------------------------------------------------------------------------------------------------------
_make_ufd:=0;
_make_ksef:=1;
_sza:=0;
_result:=0;
_continue:=1;
{? exec('find_dokum','edi_fo_ksef_fks')=null()
|| exec('dol_dokum','edi_fo_ksef_fks')
?};
DOKUM.cntx_psh();
_dokum:=exec('find_dokum','edi_fo_ksef_fks');
{? _dokum=null()
|| FUN.info('Nie znaleziono dokumentu Businesslink.'@);
   _continue:=0
||
   _dok:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,,null());
   DOK.cntx_psh();
   DOK.use(ref_name(_dok));
   DOK.prefix();
   {? ~DOK.seek(_dok)
   ||
      FUN.info('Nie znaleziono dokumentu.'@,7);
      _continue:=0
::         |? DOK.A='N'
::         ||
:: Dokument niezaakceptowany jest pomijany
::            FUN.info('Niezaakecptowany dokument');
::            _continue:=0
   ||

:: Edokument - pdf, ufd, ksef
      {? DOKUM.BL='N' | 1
      ||
         DOKUM.BL:='T';
         DOKUM.BL_TYPE:=exec('bl_type','zbl_dok',DOK.ref());
         DOKUM.BL_STAT:=exec('before_send','zbl_stat');
         DOKUM.BL_LOG:=null();
         KH.index('SKR'); KH.prefix(2,DOK.KH);
         {? KH.first()
         || DOKUM.KH:=KH.ref()
         ?};
         DOKUM.put()
      ?};
      _edi:=exec('env','edi_wspolne');
      _edi.sza:=1;
      _edi.REF:=_dok;
      {? ~_edi.init()
      ||
       FUN.info('Nie powiodło się zainicjowanie środowiska.'@);
       _continue:=0
      ||
         _edi.context:=exec('context_obj_bl','edi_wspolne');
         _bl_type_ufd:=exec('bl_type','zbl_dok',DOK.ref());
         _bl_type_ksef:='KSeF_'+_bl_type_ufd+'_DKS';
         ISTDEF.cntx_psh();
         ISTDEF.index('BL_DATA');
         ISTDEF.prefix(__Firma,'ZWS','E',_bl_type_ufd,'N','T',);
         _istdef_ufd:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?};
         ISTDEF.prefix(__Firma,'ZWS','E',_bl_type_ksef,'N','T',);
         _istdef_ksef:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?};
         ISTDEF.cntx_pop();
         {? _make_ufd & _istdef_ufd=null()
         ||
::         __kom.add('Nie ustalono definicji komunikatu typu %1.'@[_bl_type_ufd],7);
            _continue:=0

         |? _make_ksef=1 & _istdef_ksef=null()
         ||
::         __kom.add('Nie ustalono definicji komunikatu typu %1.'@[_bl_type_ksef],7);
            _continue:=0
         ||
            {? var_pres('__kom')>0 || _kom:=__kom ?};
            {? var_pres('__kom_gr')>0 || _kom_gr:=__kom_gr ?};
            {? var_pres('__kom_on')>0 || _kom_on:=__kom_on ?};
            _edi.context.DOK2REJKOM:='A';
            _edi.context.DOK:=_dok;
            _edi.context.DOKUM:=_dokum;
            _edi.context.BL_TYPE:=_bl_type_ksef;
::         _edi.context.SET_READY_TO_SEND:=_set_ready_to_send;
            _edi.context.SET_READY_TO_SEND:=1;
:: załącznik KSeF
            _log_ksef:=null();
            {? _make_ksef=1 & DOKUM.get()
            ||
               _Pliki:=_edi.PLIKI;
               _loop:=_Pliki.first();
               {!
               |? _loop
               |!
                  _loop:=_Pliki.del()
               !};
::                      dodatkowy zapis w dzienniku inforumujący o starcie odczytu lub zapisu edi
               _dzien_a:=exec('dzien_add','edi_wspolne',_istdef_ksef,'W',date(),time(),OPERATOR.USER,'LOG',''
                  ,null(),'Start','',,,'B');
               DOKUM.cntx_psh();
               exec('edi_write','edi_wspolne',_istdef_ksef,_dok);
                     {? DOKUM.get() || DOKUM.bl_put('BL_LOG',_edi.LOG,,,'log.txt') ?};
               DOKUM.cntx_pop();
::            (_out.LOG) - zapisanie na wyjściu komunikatów wygenerowanych podczas działania czynności
               _log_ksef:=_edi.logSave(_dzien_a);
:: Log dokumentu Businesslink
               {? DOKUM.get()
               ||
                  _dokum_log:=fopen(DOKUM.BL_LOG,'ua',,,1);
                  {? _dokum_log.is_open()
                  ||
::                     {? _log_ufd
::                     ||
::                        _log:=fopen(_log_ufd,'ur',,,1);
::                        {? _log.is_open()
::                        ||
::                           fwrite(_dokum_log,'\nStart: %1\n'[_bl_type_ufd]);
::                           {!
::                           |? (_txt:=fread(_log))<>'\n'
::                           |!
::                              fwrite(_dokum_log,_txt)
::                           !}
::                        ?};
::                        obj_del(_log)
::                     ?};
                     {? _log_ksef
                     ||
                        _log:=fopen(_log_ksef,'ur',,,1);
                        {? _log.is_open()
                        ||
                           fwrite(_dokum_log,'\nStart: %1\n'[_bl_type_ksef]);
                           {!
                           |? (_txt:=fread(_log))<>'\n'
                           |!
                              fwrite(_dokum_log,_txt)
                           !}
                        ?}
                     ?};
                     {? _log_ksef ||  DOKUM.bl_put('BL_LOG',_dokum_log,,,'log.txt') ?}
                  ?}
               ?}
            ?}
         ?}
      ?}
   ?}
?};
DOKUM.cntx_pop();
_result


\podmiot2k_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - formuła start
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
VAR_DEL.delete('__P2K');
__P2K:=obj_new('JAK','KH');
__P2K.JAK:=0;
__P2K.KH:=null();
DOK.cntx_psh();
FAKSKH.cntx_psh();
_dok:=exec('dok_korekta','edi_fo_ksef_fks');
{? _dok<>''
|| DOK.use(form(8+_dok));
   FAKSKH.use('fkpd'+(4+(4-$DOK.ref())) );
   DOK.prefix();
   {? DOK.seek(BB.sqlint(_dok),)
   || FAKSKH.index('DOK');
      FAKSKH.prefix(DOK.ref());
      __P2K.JAK:=1;
      KH.cntx_psh();
      KH.index('SKR'); KH.prefix(2,DOK.KH);
      __P2K.KH:={? KH.first() || KH.ref || null ?};
      KH.cntx_pop()
   ?}
?};

{? __P2K.JAK>0
|| _ret:=1
|| DOK.cntx_pop();
   FAKSKH.cntx_pop();
   VAR_DEL.delete('__P2K')
?};
_ret


\podmiot2k_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - formuła end
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
{? var_press('__P2K')>0
|| {? __P2K.JAK=1 & DOK.KH_ODB<>null() & DOK.KH_ODB().KH_LNK<>null()
   || __P2K.JAK:=2;
      __P2K.KH:=DOK.KH_ODB().KH_LNK
   |? __P2K.JAK=2 | (__P2K.JAK=1 & (DOK.KH_ODB=null() | DOK.KH_ODB().KH_LNK=null()))
   || {? FAKSKH.first()
      || __P2K.JAK:=3;
         __P2K.KH:=FAKSKH.KH
      || __P2K.JAK:=0
      ?}
   |? __P2K.JAK=3
   || {? FAKSKH.next()
      || __P2K.KH:=FAKSKH.KH
      || __P2K.JAK:=0
      ?}
   ?};
   {? __P2K.JAK>0
   || _ret:=1
   || DOK.cntx_pop();
      FAKSKH.cntx_pop();
      VAR_DEL.delete('__P2K')
   ?}
?};
_ret


\podmiot2k_nab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - prefiks nabywcy
::   WY: NIP
::----------------------------------------------------------------------------------------------------------------------
_nip:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   || _nip:=exec('prefiks_nabywcy','edi_fo_ksef_fks',__P2K.KH)
   ?}
?};
_nip


\podmiot2k_nip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - NIP
::   WY: NIP
::----------------------------------------------------------------------------------------------------------------------
_nip:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   || _nip:=DOK.NIP
   |? __P2K.JAK=2
   || _nip:=DOK.O_NIP
   |? __P2K.JAK=3
   || _nip:=FAKSKH.NIP
   ?}
?};
_nip:=exec('niptostr','#string',_nip);
_nip


\podmiot2k_naz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - nazwa
::   WY: NIP
::----------------------------------------------------------------------------------------------------------------------
_naz:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   || _naz:=DOK.KH_FULL
   |? __P2K.JAK=2
   || _naz:=DOK.O_NAZ
   |? __P2K.JAK=3
   || _naz:=FAKSKH.NAZ
   ?}
?};
_naz


\podmiot2k_krajiso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - symbol kraju
::   WY: symbol kraju
::----------------------------------------------------------------------------------------------------------------------
_sym:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   ||
      KH.cntx_psh();
      KH.prefix();
      {? KH.seek(__P2K.KH)
      || _sym:=KH.KRAJISO().SYM
      ?};
      KH.cntx_pop()
   |? __P2K.JAK=2
   || _sym:=DOK.O_KRISO().SYM
   |? __P2K.JAK=3
   || _sym:=FAKSKH.KRAJISO().SYM
   ?}
?};
_sym


\podmiot2k_miasto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: "Podmiot3" - miasto
::   WY: miasto
::----------------------------------------------------------------------------------------------------------------------
_mia:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   || _mia:=DOK.MIASTO
   |? __P2K.JAK=2
   || _mia:=DOK.O_MIASTO
   |? __P2K.JAK=3
   || _mia:=FAKSKH.MIASTO
   ?}
?};
_mia


\podmiot2k_ulica
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - ulica
::   WY: ulica
::----------------------------------------------------------------------------------------------------------------------
_ul:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   || _ul:=DOK.UL
   |? __P2K.JAK=2
   || _ul:=DOK.O_UL
   |? __P2K.JAK=3
   || _ul:=FAKSKH.UL
   ?}
?};
_ul


\podmiot2k_dom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - nr domu
::   WY: dom
::----------------------------------------------------------------------------------------------------------------------
_dom:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   || _dom:=DOK.DOM
   |? __P2K.JAK=2
   || _dom:=DOK.O_DOM
   |? __P2K.JAK=3
   || _dom:=FAKSKH.DOM
   ?}
?};
_dom


\podmiot2k_lokal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - nr lokalu
::   WY: lokal
::----------------------------------------------------------------------------------------------------------------------
_lok:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   || _lok:=DOK.LOKAL
   |? __P2K.JAK=2
   || _lok:=DOK.O_LOKAL
   |? __P2K.JAK=3
   || _lok:=FAKSKH.LOKAL
   ?}
?};
_lok


\podmiot2k_kdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - kod pocztowy
::   WY: kod pocztowy
::----------------------------------------------------------------------------------------------------------------------
_kdp:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   || _kdp:=exec('KodPoczta','edi_fo_ksef',DOK.POCZ,DOK.KPOCZ)
   |? __P2K.JAK=2
   || _kdp:=exec('KodPoczta','edi_fo_ksef',DOK.O_POCZ,DOK.O_POCZ)
   |? __P2K.JAK=3
   || _kdp:=exec('KodPoczta','edi_fo_ksef',FAKSKH.POCZ,FAKSKH.KPOCZ)
   ?}
?};
_kdp


\podmiot2k_poczta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2K" - poczta
::   WY: poczta
::----------------------------------------------------------------------------------------------------------------------
_poczta:='';
{? var_press('__P2K')>0
|| {? __P2K.JAK=1
   || _poczta:=exec('KodPoczta','edi_fo_ksef',DOK.POCZ,DOK.KPOCZ,'P')
   |? __P2K.JAK=2
   || _poczta:=exec('KodPoczta','edi_fo_ksef',DOK.O_POCZ,DOK.O_KPOCZ,'P')
   |? __P2K.JAK=3
   || _poczta:=exec('KodPoczta','edi_fo_ksef',FAKSKH.POCZ,FAKSKH.KPOCZ,'P')
   ?}
?};
_poczta


\dol_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: dołącza nagłówek załącznika
::----------------------------------------------------------------------------------------------------------------------
exec('sel_dok','dokum','DOK',1);
exec('wyb_dok','dokum',,0)


\find_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Szuka nagłówek edokumentu
::   WY: DOKUM.ref
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
DOKUM.index('DOKUM');
DOKUM.prefix(REF.FIRMA,$DOK.ref());
_loop:=DOKUM.first();
{!
|? _loop
|! _seek:={? DOKUM.BL='T' || _ref:=DOKUM.ref; 1 || 0 ?};
   _loop:=~_seek & DOKUM.next()
!};
_ref


\raty_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła start element dla TerminyPlatnosci
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? DOK.DOK_REJ().PR='T'
|| D_RB.use('dr__fk'+(4-$DOK.ref-10));
   D_RB.index('DOK'); D_RB.prefix(SSTALE.AO,DOK.ref());
   _result:=D_RB.first()
|? DOK.D3<>date(0,0,0)
|| _result:=1
?};
_result


\raty_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Formuła end element dla TerminyPlatnosci
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? DOK.DOK_REJ().PR='T'
|| _result:=D_RB.next()
?};
_result


\tp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Formuła na termin płatności w sekcji płatność
::----------------------------------------------------------------------------------------------------------------------
{? DOK.DOK_REJ().PR='T'
|| exec('date_f','edi_fo03',D_RB.TERMPLAT)
|? DOK.D3<>date(0,0,0)
|| exec('date_f','edi_fo03',DOK.D3)
?}


\dok_wart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Zwraca wartość brutto/netto dla dokumentu księgowego
::   WE: _a - DOK.ref
::       _b - 'BRUTTO' / 'NETTO'
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,FINFO,'NAROD');
_result:=0;
_dokm:=ref_name(_a);
DOK.cntx_psh(); VPOZ.cntx_psh();
DOK.use(_dokm); VPOZ.use('pozv'+(_dokm+4));
DOK.prefix();
{? DOK.seek(_a,_dokm)
|| VPOZ.index('VDPR'); VPOZ.prefix(DOK.ref());
   {? VPOZ.first()
   || _wal:=DOK.WAL<>null & DOK.WAL<>FINFO.NAROD;
      {!
      |? _result+={? _b='NETTO'  || {? _wal || VPOZ.NETTO_W || VPOZ.NETTO ?}
                  |? _b='BRUTTO' || {? _wal || VPOZ.BRUTTO_W || VPOZ.BRUTTO ?}
                  ?};
         VPOZ.next()
      !}
   ?}
?};
DOK.cntx_pop(); VPOZ.cntx_pop();
_result


\nrfazal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: numery faktur zaliczkowych
::----------------------------------------------------------------------------------------------------------------------
DOKPOLA.memo_get(,'NRFAZAL',0);
{? DOKPOLA.memo_txt(0,1,'NRFAZAL')<>''
|| VAR_DEL.delete('__nrfazal','__lnrfazal');
   __nrfazal:=spli_str(DOKPOLA.memo_txt(0,1,'NRFAZAL'),'\n');
   __lnrfazal:=1;
   1
|| 0
?}


\fazal_poza_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Sprawdza, czy faktura zaliczkowa pochodzi z KSeF
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__wynfpk');
__wynfpk:=0;
_znacznik:=exec('znacznik_ksef','dok_ksef');
{? __nrfazal[__lnrfazal]*_znacznik
|| __nrfazal[__lnrfazal]:=gsub(__nrfazal[__lnrfazal],_znacznik,'')
|| __wynfpk:=1
?};
__wynfpk


\wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: numery dokumentów magazynowych
::----------------------------------------------------------------------------------------------------------------------
DOKPOLA.memo_get(,'WZ',0);
{? DOKPOLA.memo_txt(0,1,'WZ')<>''
|| VAR_DEL.delete('__wz','__lwz');
   __wz:=spli_str(DOKPOLA.memo_txt(0,1,'WZ'),'\n');
   __lwz:=1;
   1
|| 0
?}


\nrksefkor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Zwraca numer ksef faktury korygowanej
::   WY: numer ksef
::----------------------------------------------------------------------------------------------------------------------
_wyn:=obj_new('NR','IS');
_nr:='';
_is:=0;
DOK.cntx_psh();
_dok:=exec('dok_korekta','edi_fo_ksef_fks');
{? _dok<>''
|| DOK.use(form(8+_dok)); DOK.prefix();
   {? DOK.seek(BB.sqlint(_dok),)
   || _nr:=DOK.NRKSEF;
      _is:=1
   ?}
?};
DOK.cntx_pop();
_wyn.NR:=_nr;
_wyn.IS:=_is;
_wyn


\dataksefkor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Zwraca datę ksef faktury korygowanej
::   WY: numer ksef
::----------------------------------------------------------------------------------------------------------------------
_data:=date(0,0,0);
DOK.cntx_psh(); DOKUM.cntx_psh();
_dok:=exec('dok_korekta','edi_fo_ksef_fks');
{? _dok<>''
|| DOK.use(form(8+_dok)); DOK.prefix();
   {? DOK.seek(BB.sqlint(_dok),)
   || _data:={? exec('bl_dokum_seek','zbl',DOK) || DOKUM.KSEFSDAT || DOK.DTO ?}
   ?}
?};
DOK.cntx_pop(); DOKUM.cntx_pop();
_data


\podmiot2_os_fiz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2" - czy jest osobą fizyczną
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_osfiz:=exec('podmiot_os_fiz','edi_fo_ksef',KH.ref);
_osfiz


\podmiot2_imie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2" - imię
::   WY: imię
::----------------------------------------------------------------------------------------------------------------------
_imie:=exec('podmiot_im_naz','edi_fo_ksef',KH.ref,'I');
_imie


\podmiot2_nazwisko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2" - nazwisko
::   WY: nazwisko
::----------------------------------------------------------------------------------------------------------------------
_nazw:=exec('podmiot_im_naz','edi_fo_ksef',KH.ref);
_nazw


\podmiot2_im_naz_jest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: "Podmiot2" - czy jest imię i nazwisko
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('podmiot2_imie','edi_fo_ksef_fks')<>'' & exec('podmiot2_nazwisko','edi_fo_ksef_fks')<>'';
_wyn


\licenc_kraj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Zwraca kraj licencjobiorcy
::   WY: symbol kraju licencjobiorcy
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_kst_kra:='';
KRAJE.cntx_psh();
{? var_press('__czyFaks')>0 & __czyFaks
|| {? FAKS.ODDZ<>'' & KSTE.name()<>'permane'+FAKS.ODDZ
   || KSTE.use('permane'+FAKS.ODDZ);
      KSTE.prefix();
      {? KSTE.first()
      || _kst_kod:=KSTE.KRAJ().KOD;
         KRAJE.index('KRAJE'); KRAJE.prefix(_kst_kod);
         {? KRAJE.first
         ||
            {!
            |?
               {? KRAJE.SYM=_kst_kod || _kst_kra:=KRAJE.KODISO ?};
               _kst_kra='' & KRAJE.next
            !}
         ?}
      ?}
   ?}
?};
_result:={? _kst_kra<>'' || _kst_kra || XINFO.KRAJ().KODISO ?};
KRAJE.cntx_pop;
_result


\pozf_idadd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Zwraca UU_ID dla rekordu POZF, dla korekty zwraca IDADD rekordu korygowanego
::----------------------------------------------------------------------------------------------------------------------
_result:='';
{? __czyFaks
|| _result:=exec('uu_id','edi_fo_ksef',FAP.ref())
|| {? POZF.KOR<>0
   || _result:=POZF.IDADD;
      POZF.cntx_psh();
      POZF.index('DOK');
      POZF.prefix(DOK.ref(),POZF.KOR);
      {? POZF.first()
      || _result:=POZF.IDADD
      ?};
      POZF.cntx_pop()
   || _result:=POZF.IDADD
   ?}
?};
_result


\val_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Zmienia znak podanej wartości, jeżeli rekord jest korygowany
::   WE: _a - kwota
::   WY: Jeżeli rekord jest skorygowany, to wartość o przeciwnym znaku
::----------------------------------------------------------------------------------------------------------------------
_ret:=_a;
::POZF.cntx_psh();
::POZF.index('KOR');
::POZF.prefix(DOK.ref(),POZF.LP);
::{? POZF.first()
::|| _ret:=-_a
::?};
::POZF.cntx_pop();
_ret


\pozf_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Ustawia znacznik stan przed, jeżeli rekord jest korygowany
::   WY: _a - 0/1
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
POZF.cntx_psh();
POZF.index('KOR');
POZF.prefix(DOK.ref(),POZF.LP);
{? POZF.first()
|| _ret:=1
?};
POZF.cntx_pop();
_ret


\p6_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła - start element dla pola P_6
::----------------------------------------------------------------------------------------------------------------------
(DOKPOLA.OKRES_OD=date(0,0,0) | DOKPOLA.OKRES_DO=date(0,0,0)) &
{? __czyFaks || FAKS.DSP_WPOZ<>'T' || DOK.DSP_WPZF<>'T' ?}


\p6a_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła - start element dla pola P_6A pozycji faktury
::----------------------------------------------------------------------------------------------------------------------
DOK.DSP_WPZF='T' | {? __czyFaks || FAKS.DSP_WPOZ='T' || 0 ?}


\p6a_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła - text dla pola P_6A pozycji faktury
::----------------------------------------------------------------------------------------------------------------------
{? __czyFaks || exec('date_f','edi_fo03',exec('TaxDate_Poz','edi_fo_ufd')) || exec('date_f','edi_fo03',POZF.DT) ?}


\dodaj_dane_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Dodaje dane licencjobiorcy do podmiotów powiązanych
::   WE: _a - 'w' jako wystawca, 'o' jako odbiorca
::       [b] - D - DOK (domyślnie), F - FAKS
::----------------------------------------------------------------------------------------------------------------------
_jak:={? var_pres('_b')=type_of('') || _b || 'D' ?};
{? _jak<>'D' & _jak<>'F'
|| _jak:='D'
?};
_ret:=0;
{? _a='w'
|| _rola:={? KST.JST='T'
          || exec('rola_jst_wyst','edi_fo_ksef')
          || exec('rola_cz_grvat_wyst','edi_fo_ksef')
          ?}
|| _rola:={? KST.JST='T'
          || exec('rola_jst_odb','edi_fo_ksef')
          || exec('rola_cz_grvat_odb','edi_fo_ksef')
          ?}
?};
{? _rola='' || return(1) ?};
{? _jak='F'
|| exec('czytaj','#stalesys',FAKS.DW,XINFO);
   FAKSKH.use('fkpf_'+(3+(5-$FAKS.ref())));
   FAKSKH.index('UNIK');
   FAKSKH.prefix(FAKS.ref(),_rola)
|| exec('czytaj','#stalesys',DOK.DTO,XINFO);
   FAKSKH.use('fkpd'+(4+(4-$DOK.ref())));
   FAKSKH.index('DOK');
   FAKSKH.prefix(DOK.ref(),_rola)
?};
{? ~FAKSKH.first()
|| FAKSKH.blank();
   FAKSKH.KH:=null();
   FAKSKH.NAZ:=XINFO.NAZ;
   FAKSKH.MIASTO:=XINFO.MIA;
   FAKSKH.UL:=XINFO.UL;
   FAKSKH.DOM:=XINFO.NR_D;
   FAKSKH.LOKAL:=XINFO.NR_L;
   FAKSKH.KPOCZ:=XINFO.KOD_P;
   FAKSKH.POCZ:=XINFO.POCZ;
   FAKSKH.NIP:=XINFO.NIP;
   FAKSKH.KRAJISO:=XINFO.KRAJ;
   FAKSKH.KRAJ:=XINFO.KRAJ().NAZ;
   FAKSKH.ROLA:=_rola;
   FAKSKH.SYS:='T';
   {? exec('upgrade2325_jst01','zbl')
   || FAKSKH.JST:=KST.JST;
      FAKSKH.ID_WEW:=KST.ID_WEW
   ?};
   {? _jak='F'
   || FAKSKH.FAKS:=FAKS.ref()
   || FAKSKH.DOK:=DOK.ref()
   ?};
   _ret:=FAKSKH.add()
?};
exec('czytaj','#stalesys',,XINFO);
{? exec('upgrade2325_jst01','zbl') || exec('czytaj','#stalesys',,KST,'JST') ?};
_ret


\dodaj_dane_2str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Dodaje dane Członka grupy VAT/ (lub JST) do podmiotów powiązanych
::   WE: 'o' odbiorca, 'd' dostawca
::       [b] - D - DOK (domyślnie), F - FAKS
::----------------------------------------------------------------------------------------------------------------------
_jak:={? var_pres('_b')=type_of('') || _b || 'D' ?};
{? _jak<>'D' & _jak<>'F'
|| _jak:='D'
?};
_ret:=0;
{? _jak='F'
|| {? _a='o'
   || _rola:={? FAKS.KH_ODB().JST='T'
             || exec('rola_jst_odb','edi_fo_ksef')
             |? FAKS.KH_ODB().CGVAT='T'
             || exec('rola_cz_grvat_odb','edi_fo_ksef')
             || ''
             ?}
   || _rola:={? FAKS.KH_ODB().JST='T'
             || exec('rola_jst_wyst','edi_fo_ksef')
             |? FAKS.KH_ODB().CGVAT='T'
             || exec('rola_cz_grvat_wyst','edi_fo_ksef')
             || ''
             ?}
   ?}
|| {? _a='o'
   || _rola:={? DOK.KH_ODB().JST='T'
             || exec('rola_jst_odb','edi_fo_ksef')
             |? DOK.KH_ODB().CGVAT='T'
             || exec('rola_cz_grvat_odb','edi_fo_ksef')
             || ''
             ?}
   || _rola:={? DOK.KH_ODB().JST='T'
             || exec('rola_jst_wyst','edi_fo_ksef')
             |? DOK.KH_ODB().CGVAT='T'
             || exec('rola_cz_grvat_wyst','edi_fo_ksef')
             || ''
             ?}
   ?}
?};
{? _rola='' || return(1) ?};
{? _jak='F'
|| FAKSKH.use('fkpf_'+(3+(5-$FAKS.ref())));
   FAKSKH.index('UNIK');
   FAKSKH.prefix(FAKS.ref(),_rola)
|| FAKSKH.use('fkpd'+(4+(4-$DOK.ref())));
   FAKSKH.index('DOK');
   FAKSKH.prefix(DOK.ref(),_rola)
?};
{? ~FAKSKH.first()
|| FAKSKH.blank();
   {? _jak='F'
   || FAKSKH.KH:=FAKS.KH_ODB().KH_LNK;
      FAKSKH.NAZ:=FAKS.KH_ODB().NAZ;
      FAKSKH.MIASTO:=FAKS.KH_ODB().MIASTO;
      FAKSKH.UL:=FAKS.KH_ODB().UL;
      FAKSKH.DOM:=FAKS.KH_ODB().DOM;
      FAKSKH.LOKAL:=FAKS.KH_ODB().LOKAL;
      FAKSKH.KPOCZ:=FAKS.KH_ODB().KPOCZ;
      FAKSKH.POCZ:=FAKS.KH_ODB().POCZ;
      FAKSKH.NIP:=FAKS.KH_ODB().NIP;
      FAKSKH.KRAJ:=FAKS.KH_ODB().KRAJ;
      FAKSKH.KRAJISO:=FAKS.KH_ODB().KRAJISO;
      {? exec('upgrade2325_jst01','zbl')
      || FAKSKH.ID_WEW:=FAKS.KH_ODB().ID_WEW;
         FAKSKH.JST:=FAKS.KH_ODB().JST
      ?}
   || FAKSKH.NAZ:=DOK.KH_ODB().NAZ;
      FAKSKH.MIASTO:=DOK.KH_ODB().MIASTO;
      FAKSKH.UL:=DOK.KH_ODB().UL;
      FAKSKH.DOM:=DOK.KH_ODB().DOM;
      FAKSKH.LOKAL:=DOK.KH_ODB().LOKAL;
      FAKSKH.KPOCZ:=DOK.KH_ODB().KPOCZ;
      FAKSKH.POCZ:=DOK.KH_ODB().POCZ;
      FAKSKH.NIP:=DOK.KH_ODB().NIP;
      FAKSKH.KRAJ:=DOK.KH_ODB().KRAJ;
      FAKSKH.KRAJISO:=DOK.KH_ODB().KRAJISO;
      {? exec('upgrade2325_jst01','zbl')
      || FAKSKH.ID_WEW:=DOK.KH_ODB().ID_WEW;
         FAKSKH.JST:=DOK.KH_ODB().JST
      ?}
   ?};
   FAKSKH.ROLA:=_rola;
   FAKSKH.SYS:='T';
   {? _jak='F'
   || FAKSKH.FAKS:=FAKS.ref()
   || FAKSKH.DOK:=DOK.ref()
   ?};
   _ret:=FAKSKH.add()
?};
_ret


\podmiot_dane_wg_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 1 / Podmiot 2 jako kontrahent - przygotowanie danych
::----------------------------------------------------------------------------------------------------------------------
__PODMIOT_DANE.NIP:=exec('niptostr','#string',DOK.NIP);
{? __PODMIOT_DANE.NIP='' & #(2+DOK.NIP)=0
|| __PODMIOT_DANE.KOD_UE:=2+DOK.NIP;
   __PODMIOT_DANE.NRVAT_UE:=2-DOK.NIP
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.NRVAT_UE=''
|| __PODMIOT_DANE.BRAK_ID:='1'
?};

KH.cntx_psh();
KH.index('SKR'); KH.prefix(2,DOK.KH);
{? KH.first()
|| __PODMIOT_DANE.EORI:=KH.EORI;
   _naz:='';
   {? exec('podmiot_os_fiz','edi_fo_ksef',KH.ref())
   || {? exec('podmiot_im_naz','edi_fo_ksef',KH.ref())<>''
      || _naz:=form(exec('podmiot_im_naz','edi_fo_ksef',KH.ref(),'I')+' '+exec('podmiot_im_naz','edi_fo_ksef',KH.ref()))
      ?}
   || _naz:=KH.NAZ_P
   ?};
   __PODMIOT_DANE.NAZWA:=_naz;

   OS_ADRES.cntx_psh();
   OS_ADRES.index('RODZAJ1');
   OS_ADRES.prefix(KH.ref(),'H');
   _os_adres:=OS_ADRES.last();
   __PODMIOT_DANE.EMAIL:={? _os_adres & OS_ADRES.EMAIL<>'' || OS_ADRES.EMAIL || KH.EM ?};
   __PODMIOT_DANE.TEL:={? _os_adres & OS_ADRES.TEL<>'' || OS_ADRES.TEL || KH.TEL ?};
   OS_ADRES.cntx_pop()
?};
KH.cntx_pop();
~~


\podmiot_adres_wg_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 1 / Podmiot 2 jako kontrahent - przygotowanie adresu
::----------------------------------------------------------------------------------------------------------------------
KH.cntx_psh();
KH.index('SKR'); KH.prefix(2,DOK.KH);
{? KH.first()
|| _kdp:=exec('KodPoczta','edi_fo_ksef',DOK.POCZ,DOK.KPOCZ);
   _poczta:=exec('KodPoczta','edi_fo_ksef',DOK.POCZ,DOK.KPOCZ,'P');
   __PODMIOT_ADRES.KRAJ:=KH.KRAJISO().SYM;
   __PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',DOK.UL,DOK.DOM,DOK.LOKAL,DOK.MIASTO,_poczta);
   __PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',_kdp,_poczta,DOK.MIASTO,DOK.UL);
   __PODMIOT_ADRES.GLN:=KH.GLN;
   {? __PODMIOT_ADRES.ADRES1=''
   || __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
      __PODMIOT_ADRES.ADRES2:=''
   ?}
?};
KH.cntx_pop();
~~


\podmiot_adresk_wg_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 1 / Podmiot 2 jako kontrahent - przygotowanie adresu korespondencyjnego
::----------------------------------------------------------------------------------------------------------------------
KH.cntx_psh();
KH.index('SKR'); KH.prefix(2,DOK.KH);
{? KH.first()
|| OS_ADRES.cntx_psh();
   OS_ADRES.index('RODZAJ1');
   OS_ADRES.prefix(KH.ref(),'H');
   {? OS_ADRES.last()
   || exec('podmiot_adres_osadres','edi_fo_ksef2')
   ?};
   OS_ADRES.cntx_pop()
?};
KH.cntx_pop();
~~


\podmiot1_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 1 - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');
{? DOK.DOK_REJ().JPK_FA_T='Z'
|| exec('podmiot_dane_wg_kh','edi_fo_ksef_fks')
|| exec('podmiot_dane_wg_firma','edi_fo_ksef2')
?};
_wyn


\podmiot1_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 1 - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
{? DOK.DOK_REJ().JPK_FA_T='Z'
|| exec('podmiot_adres_wg_kh','edi_fo_ksef_fks')
|| exec('podmiot_adres_wg_firma','edi_fo_ksef2')
?};
_wyn


\podmiot1_adresk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 1 - przygotowanie adresu korespondencyjnego
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
{? DOK.DOK_REJ().JPK_FA_T='Z'
|| exec('podmiot_adresk_wg_kh','edi_fo_ksef_fks')
|| exec('podmiot_adresk_wg_firma','edi_fo_ksef2')
?};
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\podmiot2_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 2 - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');
{? DOK.DOK_REJ().JPK_FA_T='Z'
|| exec('podmiot_dane_wg_firma','edi_fo_ksef2')
|| exec('podmiot_dane_wg_kh','edi_fo_ksef_fks')
?};
_wyn


\podmiot2_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 2 - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
{? DOK.DOK_REJ().JPK_FA_T='Z'
|| exec('podmiot_adres_wg_firma','edi_fo_ksef2')
|| exec('podmiot_adres_wg_kh','edi_fo_ksef_fks')
?};
_wyn


\podmiot2_adresk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 2 - przygotowanie adresu korespondencyjnego
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
{? DOK.DOK_REJ().JPK_FA_T='Z'
|| exec('podmiot_adresk_wg_firma','edi_fo_ksef2')
|| exec('podmiot_adresk_wg_kh','edi_fo_ksef_fks')
?};
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\podmiot3_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 3 - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');

__PODMIOT_DANE.EORI:=exec('kh_pole','edi_fo_ksef',__P3.KH,'EORI');
__PODMIOT_DANE.NIP:=exec('podmiot3_nip','edi_fo_ksef_fks');
{? __PODMIOT_DANE.NIP=''
|| __PODMIOT_DANE.ID_WEW:=exec('podmiot3_nip','edi_fo_ksef_fks',1)
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.ID_WEW=''
|| _nip_ue:=exec('podmiot3_nip','edi_fo_ksef_fks');
   {? _nip_ue<>''
   || __PODMIOT_DANE.KOD_UE:=2+_nip_ue;
      __PODMIOT_DANE.NRVAT_UE:=2-nip_ue
   ?}
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.ID_WEW='' & __PODMIOT_DANE.NRVAT_UE=''
|| __PODMIOT_DANE.BRAK_ID:='1'
?};
::__PODMIOT_DANE.ID_NABYWCY:=exec('kh_pole','edi_fo_ksef',__P3.KH,'IDADD');
__PODMIOT_DANE.EMAIL:=exec('kh_pole','edi_fo_ksef',__P3.KH, 'EM');
__PODMIOT_DANE.TEL:=exec('kh_pole','edi_fo_ksef',__P3.KH, 'TEL');

_naz:='';
{? exec('podmiot3_os_fiz','edi_fo_ksef')
|| {? exec('podmiot3_nazwisko','edi_fo_ksef')<>''
   || _naz:=form(exec('podmiot3_imie','edi_fo_ksef')+' '+exec('podmiot3_nazwisko','edi_fo_ksef'))
   ?}
|| _naz:=DOK.KH_FULL
?};
__PODMIOT_DANE.NAZWA:=_naz;

_wyn


\podmiot3_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 3 - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
_kdp:=exec('podmiot3_kdp','edi_fo_ksef_fks');
_poczta:=exec('podmiot3_poczta','edi_fo_ksef_fks');
_miasto:=exec('podmiot3_miasto','edi_fo_ksef_fks');
_ulica:=exec('podmiot3_ulica','edi_fo_ksef_fks');
_dom:=exec('podmiot3_dom','edi_fo_ksef_fks');
_lokal:=exec('podmiot3_lokal','edi_fo_ksef_fks');
__PODMIOT_ADRES.KRAJ:=exec('podmiot3_krajiso','edi_fo_ksef_fks');
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',_ulica,_dom,_lokal,_miasto,_poczta);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',_kdp,_poczta,_miasto,_ulica);
__PODMIOT_ADRES.GLN:=exec('kh_pole','edi_fo_ksef',__P3.KH, 'GLN');
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
_wyn


\podmiot3_adresk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 2 - przygotowanie adresu korespondencyjnego
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
OS_ADRES.cntx_psh();
OS_ADRES.index('RODZAJ1');
OS_ADRES.prefix(__P3.KH,'H');
{? OS_ADRES.last()
|| exec('podmiot_adres_osadres','edi_fo_ksef2')
?};
OS_ADRES.cntx_pop();
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\podmiotU_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot upoważniony - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');
__PODMIOT_DANE.EORI:=exec('kh_pole','edi_fo_ksef',__PU.KH,'EORI');
__PODMIOT_DANE.NIP:=exec('niptostr','#string',exec('kh_pole','edi_fo_ksef',__PU.KH,'NIP'));
{? __PODMIOT_DANE.NIP=''
|| __PODMIOT_DANE.BRAK_ID:='1'
?};
__PODMIOT_DANE.EMAIL:=exec('kh_pole','edi_fo_ksef',__PU.KH,'EM');
__PODMIOT_DANE.TEL:=exec('kh_pole','edi_fo_ksef',__PU.KH,'TEL');

_naz:='';
{? exec('podmiotU_os_fiz','edi_fo_ksef')
|| {? exec('podmiotU_nazwisko','edi_fo_ksef')<>''
   || _naz:=form(exec('podmiotU_imie','edi_fo_ksef')+' '+exec('podmiotU_nazwisko','edi_fo_ksef'))
   ?}
|| _naz:=exec('kh_pole','edi_fo_ksef',__PU.KH,'NAZ_P')
?};
__PODMIOT_DANE.NAZWA:=_naz;

_wyn


\podmiotU_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot upoważniony - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
_kdp:=exec('kh_pole','edi_fo_ksef',__PU.KH,'KPOCZ');
_poczta:=exec('kh_pole','edi_fo_ksef',__PU.KH,'POCZ');
_miasto:=exec('kh_pole','edi_fo_ksef',__PU.KH,'MIASTO');
_ulica:=exec('kh_pole','edi_fo_ksef',__PU.KH,'UL');
_dom:=exec('kh_pole','edi_fo_ksef',__PU.KH,'DOM');
_lokal:=exec('kh_pole','edi_fo_ksef',__PU.KH,'LOKAL');
__PODMIOT_ADRES.KRAJ:=exec('podmiotU_krajiso','edi_fo_ksef_fks',__PU.KH);
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',_ulica,_dom,_lokal,_miasto,_poczta);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',_kdp,_poczta,_miasto,_ulica);
__PODMIOT_ADRES.GLN:=exec('kh_pole','edi_fo_ksef',__PU.KH,'GLN');
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
_wyn


\podmiotU_adresk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot upoważniony - przygotowanie adresu korespondencyjnego
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
OS_ADRES.cntx_psh();
OS_ADRES.index('RODZAJ1');
OS_ADRES.prefix(__PU.KH,'H');
{? OS_ADRES.last()
|| exec('podmiot_adres_osadres','edi_fo_ksef2')
?};
OS_ADRES.cntx_pop();
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\podmiotU_krajiso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: "PodmiotUpowazniony" - symbol kraju
::   WE: KH.ref
::   WY: symbol kraju
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
KH.cntx_psh();
KH.clear();
{? KH.seek(_a)
|| _wyn:=KH.KRAJISO().SYM
?};
KH.cntx_pop();
_wyn


\podmiot2k_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 2K - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');

__PODMIOT_DANE.PREFIKS:=exec('podmiot2k_nab','edi_fo_ksef_fks');
__PODMIOT_DANE.NIP:=exec('podmiot2k_nip','edi_fo_ksef_fks');
{? __PODMIOT_DANE.NIP=''
|| _nip_ue:=exec('podmiot2k_nip','edi_fo_ksef_fks');
   {? _nip_ue<>'' & #(2+_nip_ue)=0
   || __PODMIOT_DANE.KOD_UE:=2+_nip_ue;
      __PODMIOT_DANE.NRVAT_UE:=2-nip_ue
   ?}
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.NRVAT_UE=''
|| __PODMIOT_DANE.BRAK_ID:='1'
?};
::__PODMIOT_DANE.ID_NABYWCY:=exec('kh_pole','edi_fo_ksef',__P2K.KH, 'IDADD');

_naz:='';
{? exec('podmiot2k_os_fiz','edi_fo_ksef')
|| {? exec('podmiot2k_nazwisko','edi_fo_ksef')<>''
   || _naz:=form(exec('podmiot2k_imie','edi_fo_ksef')+' '+exec('podmiot2k_nazwisko','edi_fo_ksef'))
   ?}
|| _naz:=exec('podmiot2k_naz','edi_fo_ksef_fks')
?};
__PODMIOT_DANE.NAZWA:=_naz;

_wyn


\podmiot2k_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Podmiot 2K - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
_kdp:=exec('podmiot2k_kdp','edi_fo_ksef');
_poczta:=exec('podmiot2k_poczta','edi_fo_ksef');
_miasto:=exec('podmiot2k_miasto','edi_fo_ksef');
_ulica:=exec('podmiot2k_ulica','edi_fo_ksef');
_dom:=exec('podmiot2k_dom','edi_fo_ksef');
_lokal:=exec('podmiot2k_lokal','edi_fo_ksef');
__PODMIOT_ADRES.KRAJ:=exec('podmiot2k_krajiso','edi_fo_ksef_fks');
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',_ulica,_dom,_lokal,_miasto,_poczta);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',_kdp,_poczta,_miasto,_ulica);
__PODMIOT_ADRES.GLN:=exec('kh_pole','edi_fo_ksef',__P2K.KH, 'GLN');
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\RachBank_Opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Rachunek bankowy - opis
::   WE: [_a] - 0 - dla DOK.RB (domyslne), 1 - dla DOK.RB_V
::   WY: nazwa
::----------------------------------------------------------------------------------------------------------------------
_jak:=0;
{? var_pres('_a')=type_of(0)
|| _jak:=_a
?};
_opi:='';
{? _jak
|| _opi:='Rachunek VAT'
|| {? DOK.RB().WAL().KOD='PLN'
   || _opi:='Rachunek PLN'
   || _opi:='Rachunek walutowy'
   ?}
?};
_opi


\p_13_1_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Warunek dla P_13_5
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=DOKPOLA.UDS<>'T' & DOKPOLA.USL_TUR<>'T' & var_press('sFN23')>0;
_wyn


\p_13_5_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Warunek dla P_13_5
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=(DOK.PROC().KOD='WSTO_EE');
_wyn


\p_13_8_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Warunek dla P_13_8
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=(var_pres('sP_13_8')>0 & ~exec('p_13_5_czy','edi_fo_ksef_fks'));
_wyn


\p_13_9_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Warunek dla P_13_9
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=(var_pres('sP_13_9')>0 & ~exec('p_13_5_czy','edi_fo_ksef2'));
_wyn


\p_13_10_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Warunek dla P_13_10
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=(DOKPOLA.OO='T' & var_pres('sP_13_9')>0 & ~exec('p_13_5_czy','edi_fo_ksef2'));
_wyn


\DFaKor_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: "DaneFaKorygowanej" - formuła start element
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__nrfakor');
_fakor:=exec('nrksefkor','edi_fo_ksef_fks');
__nrfakor:=_fakor.NR;
_fakor.IS


\DFaKorPK_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: "DaneFaKorygowanejPozaKSEF" - formuła start element
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:={? var_pres('__nrfakor')>0
         || __nrfakor=''
         || 0
         ?};
_result


\podmiot1_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Podmiot 1 - przygotowanie danych
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
VAR_DEL.delete('__P1','__P1K');
__P1:=obj_new('NIP','NAZWA','SKR','UL','DOM','LOKAL','MIASTO','KPOCZ','POCZ','EM','TEL','KRAJ','ADRES1','ADRES2',
              'GLN','EORI','PREFIKS');
__P1.NIP:=__P1.NAZWA:=__P1.UL:=__P1.DOM:=__P1.LOKAL:=__P1.MIASTO:=__P1.POCZ:=__P1.KPOCZ:=__P1.EM:=__P1.TEL:='';
__P1.KRAJ:=__P1.ADRES1:=__P1.ADRES2:=__P1.GLN:=__P1.EORI:=__P1.PREFIKS:='';
::ADRES KORESPONDECYNJNY
__P1K:=obj_new('UL','DOM','LOKAL','MIASTO','POCZ','KPOCZ','KRAJ','ADRES1','ADRES2','GLN');
__P1K.UL:=__P1K.DOM:=__P1K.LOKAL:=__P1K.MIASTO:=__P1K.POCZ:=__P1K.KRAJ:=__P1K.ADRES1:='';
__P1K.KPOCZ:=__P1K.ADRES2:=__P1K.GLN:='';
:: Złączenie do kontrahenta
_kh_ref:=null;

{? DOK.DOK_REJ().JPK_FA_T='Z'
|| __P1.NIP:=DOK.NIP;  __P1.NAZWA:=DOK.KH_FULL; __P1.SKR:=DOK.KH; __P1.KRAJ:=DOK.KH_KRISO().KODISO;
   __P1.UL:=DOK.UL; __P1.DOM:=DOK.DOM; __P1.LOKAL:=DOK.LOKAL; __P1.MIASTO:=DOK.MIASTO;
   __P1.KPOCZ:=DOK.KPOCZ;  __P1.POCZ:=DOK.POCZ;
   {? DOK.KH<>''
   || KH.index('SKR'); KH.prefix(2);
      {? KH.find_key(DOK.KH,DOK.KH) & KH.SKR=DOK.KH
      || _kh_ref:=KH.ref();
         __P1.EORI:=KH.EORI;
         __P1.EM:=KH.EM;
         __P1.TEL:=KH.TEL;
         __P1.GLN:=KH.GLN
      ?}
   ?}
|| FAKSKH.cntx_psh();
   FAKSKH.use('fkpd'+(4+(4-$DOK.ref())));
   FAKSKH.index('DOK');
   FAKSKH.prefix(DOK.ref(),'Grupa VAT - licencjobiorca');
   {? FAKSKH.first()
   || _kh_ref:=FAKSKH.KH;
      __P1.NIP:=FAKSKH.NIP; __P1.KRAJ:=FAKSKH.KRAJISO().KODISO;
      __P1.NAZWA:=FAKSKH.NAZ; __P1.SKR:=FAKSKH.KH().SKR;
      __P1.UL:=FAKSKH.UL; __P1.DOM:=FAKSKH.DOM; __P1.LOKAL:=FAKSKH.LOKAL; __P1.MIASTO:=FAKSKH.MIASTO;
      __P1.KPOCZ:=FAKSKH.KPOCZ;  __P1.POCZ:=FAKSKH.POCZ; __P1.EM:=KH.EM; __P1.TEL:=KH.TEL; __P1.GLN:=KH.GLN;
      __P1.EORI:=KH.EORI
   || exec('czytaj','#stalesys',DOK.DTO,KST);
      {? KST.GRVAT=null()
      || __P1.NIP:=KST.NIP; __P1.NAZWA:=KST.NAZWA; __P1.SKR:=KST.SKROT; __P1.KRAJ:=KST.KRAJ().KODISO;
         __P1.UL:=KST.ULICA; __P1.DOM:=KST.DOM; __P1.LOKAL:=KST.LOKAL; __P1.MIASTO:=KST.MIASTO;
         __P1.KPOCZ:=KST.KOD; __P1.POCZ:=KST.POCZTA; __P1.EM:=KST.EMAIL; __P1.TEL:=KST.TEL; __P1.GLN:=KST.KGLN;
         __P1.EORI:=KST.EORI;
:: Adres korespondencyjny
         __P1K.UL:=KST.KULICA; __P1K.DOM:=KST.KDOM; __P1K.LOKAL:=KST.KLOKAL;
         __P1K.MIASTO:=KST.KMIASTO;  __P1K.POCZ:=KST.KPOCZTA; __P1K.KPOCZ:=KST.KKOD;
         __P1K.KRAJ:=KST.KKRAJ().KODISO; __P1K.GLN:=KST.KGLN
      ?}
   ?};
   FAKSKH.cntx_pop()
?};
::NIPY.index('SNIP_F');
::NIPY.prefix(REF.S_FIRMA);
::{? NIPY.first()
::|| __P1.PREFIKS:={? NIPY.KRAJ().KOD='GR' || 'EL' || NIPY.KRAJ().KOD ?}
::?};
{? DOK.RVAT().RVAT().KVAT().SYM*'_WWspD'>0
|| __P1.PREFIKS='PL'
?};
:: Adres korespondencyjny
{? _kh_ref<>null() & __P1K.KRAJ=''
||  OS_ADRES.cntx_psh();
    OS_ADRES.index('RODZAJ1');
    OS_ADRES.prefix(_kh_ref,'H');
    {? OS_ADRES.last()
    || __P1K.UL:=OS_ADRES.ULICA;
       __P1K.DOM:=OS_ADRES.DOM;
       __P1K.LOKAL:=OS_ADRES.LOKAL;
       __P1K.MIASTO:=OS_ADRES.MIASTO;
       __P1K.POCZ:=OS_ADRES.POCZTA;
       __P1K.KPOCZ:=OS_ADRES.KOD;
       __P1K.KRAJ:=OS_ADRES.KRAJ().KODISO;
       __P1K.GLN:=OS_ADRES.GLN;
       {? OS_ADRES.EMAIL<>'' || __P1.EM:=OS_ADRES.EMAIL ?};
       {? OS_ADRES.TEL<>'' || __P1.TEL:=OS_ADRES.TEL ?}
    ?};
    OS_ADRES.cntx_pop()
?};
__P1.ADRES1:=exec('adr','faktury_wydruk',__P1.UL,__P1.DOM,__P1.LOKAL,__P1.MIASTO,__P1.POCZ);
__P1.ADRES2:=exec('kpocz','faktury_wydruk',__P1.KPOCZ,__P1.POCZ,__P1.MIASTO,__P1.UL);
{? __P1K.KRAJ<>''
|| __P1K.ADRES1:=exec('adr','faktury_wydruk',__P1K.UL,__P1K.DOM,__P1K.LOKAL,__P1K.MIASTO,__P1K.POCZ);
   __P1K.ADRES2:=exec('kpocz','faktury_wydruk',__P1K.KPOCZ,__P1K.POCZ,__P1K.MIASTO,__P1K.UL)
?};
{? __P1.NIP<>''
|| _ret:=1
|| VAR_DEL.delete('__P1','__P1K')
?};
_ret


\podmiot2_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Podmiot 2 - przygotowanie danych
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
VAR_DEL.delete('__P2','__P2K');
__P2:=obj_new('NIP','NAZWA','SKR','UL','DOM','LOKAL','MIASTO','KPOCZ','POCZ','EM','TEL','KRAJ','ADRES1','ADRES2',
              'GLN','EORI','NRVAT_UE','NR_ID','BRAK_ID','ID_NABYWCY','ID_WEW','KOD_UE','ID_KRAJ');
__P2.NIP:=__P2.NAZWA:=__P2.UL:=__P2.DOM:=__P2.LOKAL:=__P2.MIASTO:=__P2.POCZ:=__P2.KPOCZ:=__P2.EM:=__P2.TEL:='';
__P2.KRAJ:=__P2.ADRES1:=__P2.ADRES2:=__P2.GLN:=__P2.EORI:=__P2.NRVAT_UE:=__P2.NR_ID:=__P2.BRAK_ID:=__P2.ID_NABYWCY:='';
__P2.ID_WEW:=__P2.KOD_UE:=__P2.ID_KRAJ:='';
::ADRES KORESPONDECYNJNY
__P2K:=obj_new('UL','DOM','LOKAL','MIASTO','POCZ','KPOCZ','KRAJ','ADRES1','ADRES2','GLN');
__P2K.UL:=__P2K.DOM:=__P2K.LOKAL:=__P2K.MIASTO:=__P2K.POCZ:=__P2K.KRAJ:=__P2K.ADRES1:='';
__P2K.KPOCZ:=__P2K.ADRES2:=__P2K.GLN:='';
:: Złączenie do kontrahenta
_kh_ref:=null;
{? DOK.DOK_REJ().JPK_FA_T='Z'
|| FAKSKH.cntx_psh();
   FAKSKH.use('fkpd'+(4+(4-$DOK.ref())));
   FAKSKH.index('DOK');
   FAKSKH.prefix(DOK.ref(),'Grupa VAT - licencjobiorca');
   {? FAKSKH.first()
   || _kh_ref:=FAKSKH.KH;
      __P2.NIP:=FAKSKH.NIP; __P2.NAZWA:=FAKSKH.NAZ; __P2.SKR:=FAKSKH.KH().SKR; __P2.KRAJ:=FAKSKH.KRAJISO().KODISO;
      __P2.UL:=FAKSKH.UL; __P2.DOM:=FAKSKH.DOM; __P2.LOKAL:=FAKSKH.LOKAL; __P2.MIASTO:=FAKSKH.MIASTO;
      __P2.KPOCZ:=FAKSKH.KPOCZ; __P2.POCZ:=FAKSKH.POCZ; __P2.EM:=KH.EM; __P2.TEL:=KH.TEL; __P2.GLN:=KH.GLN;
      __P2.EORI:=FAKSKH.KH().EORI;
      _ret:=1
   || exec('czytaj','#stalesys',DOK.DTO,KST);
      {? KST.GRVAT=null()
      || __P2.NIP:=KST.NIP; __P2.NAZWA:=KST.NAZWA; __P2.SKR:=KST.SKROT; __P2.KRAJ:=KST.KRAJ().KODISO;
         __P2.UL:=KST.ULICA; __P2.DOM:=KST.DOM; __P2.LOKAL:=KST.LOKAL; __P2.MIASTO:=KST.MIASTO;
         __P2.KPOCZ:=KST.KOD; __P2.POCZ:=KST.POCZTA; __P2.EM:=KST.EMAIL; __P2.TEL:=KST.TEL; __P2.GLN:=KST.KGLN;
         __P2.EORI:=KST.EORI;
:: Adres korespondencyjny
         __P2K.UL:=KST.KULICA; __P2K.DOM:=KST.KDOM; __P2K.LOKAL:=KST.KLOKAL;
         __P2K.MIASTO:=KST.KMIASTO; __P2K.POCZ:=KST.KPOCZTA; __P2K.KPOCZ:=KST.KKOD;
         __P2K.KRAJ:=KST.KKRAJ().KODISO; __P2K.GLN:=KST.KGLN;
         _ret:=1
      ?}
   ?};
   FAKSKH.cntx_pop()
|| __P2.NIP:=DOK.NIP;  __P2.NAZWA:=DOK.KH_FULL; __P2.SKR:=DOK.KH; __P2.KRAJ:=DOK.KH_KRISO().KODISO;
   __P2.UL:=DOK.UL; __P2.DOM:=DOK.DOM; __P2.LOKAL:=DOK.LOKAL; __P2.MIASTO:=DOK.MIASTO;
   __P2.KPOCZ:=DOK.KPOCZ; __P2.POCZ:=DOK.POCZ;
   {? DOK.KH<>''
   || KH.index('SKR'); KH.prefix(2);
      {? KH.find_key(DOK.KH,DOK.KH) & KH.SKR=DOK.KH
      || _kh_ref:=KH.ref();
         __P2.EORI:=KH.EORI;
         __P2.EM:=KH.EM;
         __P2.TEL:=KH.TEL;
         __P2.GLN:=KH.GLN
      ?}
   ?};
   _ret:=1
?};
:: Adres korespondencyjny
{? _kh_ref<>null() & __P2K.KRAJ=''
||  OS_ADRES.cntx_psh();
    OS_ADRES.index('RODZAJ1');
    OS_ADRES.prefix(_kh_ref,'H');
    {? OS_ADRES.last()
    || __P2K.UL:=OS_ADRES.ULICA;
       __P2K.DOM:=OS_ADRES.DOM;
       __P2K.LOKAL:=OS_ADRES.LOKAL;
       __P2K.MIASTO:=OS_ADRES.MIASTO;
       __P2K.POCZ:=OS_ADRES.POCZTA;
       __P2K.KPOCZ:=OS_ADRES.KOD;
       __P2K.KRAJ:=OS_ADRES.KRAJ().KODISO;
       __P2K.GLN:=OS_ADRES.GLN;
       {? OS_ADRES.EMAIL<>'' || __P2.EM:=OS_ADRES.EMAIL ?};
       {? OS_ADRES.TEL<>'' || __P2.TEL:=OS_ADRES.TEL ?}
    ?};
    OS_ADRES.cntx_pop()
?};
__P2.NIP:=exec('niptostr','#string',__P2.NIP);
{? (#(2+__P2.NIP))=0 || __P2.KOD_UE:=2+__P2.NIP; __P2.NRVAT_UE:=2-__P2.NIP; __P2.NIP:='' ?};
__P2.ADRES1:=exec('adr','faktury_wydruk',__P2.UL,__P2.DOM,__P2.LOKAL,__P2.MIASTO,__P2.POCZ);
__P2.ADRES2:=exec('kpocz','faktury_wydruk',__P2.KPOCZ,__P2.POCZ,__P2.MIASTO,__P2.UL);
{? __P2K.KRAJ<>''
|| __P2K.ADRES1:=exec('adr','faktury_wydruk',__P2K.UL,__P2K.DOM,__P2K.LOKAL,__P2K.MIASTO,__P2K.POCZ);
   __P2K.ADRES2:=exec('kpocz','faktury_wydruk',__P2K.KPOCZ,__P2K.POCZ,__P2K.MIASTO,__P2K.UL)
?};
{? _ret=0
|| VAR_DEL.delete('__P2')
?};
_ret


\podmiot32_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: "Podmiot3" - formuła start
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('FF_KK');
FAKSKH.cntx_psh();
FAKSKH.use('fkpd'+(4+(4-$DOK.ref())));
FF_KK:=sql('select distinct FAKSKH.ROLA from FAKSKH '+
           'where FAKSKH.DOK=:_a and not FAKSKH.ROLA=\'Grupa VAT - licencjobiorca\'',DOK.ref());
FAKSKH.cntx_pop();
FF_KK.first()


\podmiot32_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: "Podmiot3" - formuła end
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
FF_KK.first()


\podmiot32_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Podmiot 3 - przygotowanie danych
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
VAR_DEL.delete('__P3','__P3K');
__P3:=obj_new('NIP','NAZWA','SKR','UL','DOM','LOKAL','MIASTO','KPOCZ','POCZ','EM','TEL','KRAJ','ADRES1','ADRES2','GLN',
              'ROLA','UDZIAL','EORI','NRVAT_UE','NR_ID','BRAK_ID','ID_NABYWCY','ID_WEW','KOD_UE','ID_KRAJ');
__P3.NIP:=__P3.NAZWA:=__P3.UL:=__P3.DOM:=__P3.LOKAL:=__P3.MIASTO:=__P3.POCZ:=__P3.KPOCZ:=__P3.EM:=__P3.TEL:='';
__P3.KRAJ:=__P3.ADRES1:=__P3.ADRES2:=__P3.GLN:=__P3.ROLA:=__P3.EORI:=''; __P3.UDZIAL:=0;
__P3.NRVAT_UE:=__P3.NR_ID:=__P3.BRAK_ID:=__P3.ID_NABYWCY:=__P3.ID_WEW:=__P3.KOD_UE:=__P3.ID_KRAJ:='';
::ADRES KORESPONDECYNJNY
__P3K:=obj_new('UL','DOM','LOKAL','MIASTO','POCZ','KPOCZ','KRAJ','ADRES1','ADRES2','GLN');
__P3K.UL:=__P3K.DOM:=__P3K.LOKAL:=__P3K.MIASTO:=__P3K.POCZ:=__P3K.KRAJ:=__P3K.ADRES1:='';
__P3K.KPOCZ:=__P3K.ADRES2:=__P3K.GLN:='';
:: Złączenie do kontrahenta
_kh_ref:=null;
{? FF_KK.ROLA<>''
|| FAKSKH.cntx_psh();
   FAKSKH.use('fkpd'+(4+(4-$DOK.ref())));
   FAKSKH.index('DOK');
   FAKSKH.prefix(DOK.ref(),FF_KK.ROLA);
   {? FAKSKH.first()
   || _kh_ref:=FAKSKH.KH;
      {? exec('upgrade2325_jst01','zbl') & FAKSKH.JST='T'
      || {? FAKSKH.ID_WEW<>''
         || __P3.ID_WEW:=FAKSKH.ID_WEW
         || __P3.NIP:=FAKSKH.NIP
         ?}
      || __P3.NIP:=FAKSKH.NIP
      ?};
      __P3.NAZWA:=FAKSKH.NAZ; __P3.SKR:=FAKSKH.KH().SKR; __P3.KRAJ:=FAKSKH.KRAJISO().SYM;
      __P3.UL:=FAKSKH.UL; __P3.DOM:=FAKSKH.DOM; __P3.LOKAL:=FAKSKH.LOKAL; __P3.MIASTO:=FAKSKH.MIASTO;
      __P3.KPOCZ:=FAKSKH.KPOCZ;  __P3.POCZ:=FAKSKH.POCZ; __P3.EM:=KH.EM; __P3.TEL:=KH.TEL; __P3.GLN:=KH.GLN;
      __P3.UDZIAL:=FAKSKH.UDZIAL;
      __P3.EORI:=FAKSKH.KH().EORI;
      __P3.ROLA:={? FAKSKH.ROLA='Faktor'   || '1'
                 |? FAKSKH.ROLA='Odbiorca' || '2'
                 |? FAKSKH.ROLA='Podmiot pierwotny' || '3'
                 |? FAKSKH.ROLA='Dodatkowy nabywca' || '4'
                 |? FAKSKH.ROLA='Wystawca faktury'  || '5'
                 |? FAKSKH.ROLA='Dokonujący płatności' || '6'
                 |? FAKSKH.ROLA='Jednostka samorządu terytorialnego - wystawca' || '7'
                 |? FAKSKH.ROLA='Jednostka samorządu terytorialnego - odbiorca' || '8'
                 |? FAKSKH.ROLA='Członek grupy VAT - wystawca' || '9'
                 |? FAKSKH.ROLA='Członek grupy VAT - odbiorca' || '10'
                 ?};
      _ret:=1
   ?};
   FAKSKH.cntx_pop();
   FF_KK.del()
?};
:: Adres korespondencyjny
{? _kh_ref<>null() & __P3K.KRAJ=''
||  OS_ADRES.cntx_psh();
    OS_ADRES.index('RODZAJ1');
    OS_ADRES.prefix(_kh_ref,'H');
    {? OS_ADRES.last()
    || __P3K.UL:=OS_ADRES.ULICA;
       __P3K.DOM:=OS_ADRES.DOM;
       __P3K.LOKAL:=OS_ADRES.LOKAL;
       __P3K.MIASTO:=OS_ADRES.MIASTO;
       __P3K.POCZ:=OS_ADRES.POCZTA;
       __P3K.KPOCZ:=OS_ADRES.KOD;
       __P3K.KRAJ:=OS_ADRES.KRAJ().KODISO;
       __P3K.GLN:=OS_ADRES.GLN;
       {? OS_ADRES.EMAIL<>'' || __P3.EM:=OS_ADRES.EMAIL ?};
       {? OS_ADRES.TEL<>'' || __P3.TEL:=OS_ADRES.TEL ?}
    ?};
    OS_ADRES.cntx_pop()
?};
__P3.NIP:=exec('niptostr','#string',__P3.NIP);
{? (#(2+__P3.NIP))=0 || __P3.KOD_UE:=2+__P3.NIP; __P3.NRVAT_UE:=2-__P3.NIP; __P3.NIP:='' ?};
__P3.ADRES1:=exec('adr','faktury_wydruk',__P3.UL,__P3.DOM,__P3.LOKAL,__P3.MIASTO,__P3.POCZ);
__P3.ADRES2:=exec('kpocz','faktury_wydruk',__P3.KPOCZ,__P3.POCZ,__P3.MIASTO,__P3.UL);
{? __P3K.KRAJ<>''
|| __P3K.ADRES1:=exec('adr','faktury_wydruk',__P3K.UL,__P3K.DOM,__P3K.LOKAL,__P3K.MIASTO,__P3K.POCZ);
   __P3K.ADRES2:=exec('kpocz','faktury_wydruk',__P3K.KPOCZ,__P3K.POCZ,__P3K.MIASTO,__P3K.UL)
?};
_ret


\fa_o_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: dodawanie pozycji VAT podczat odczytu - start
::   WE: _a - FKS/OBG
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? _a='FKS' || exec('advance_references_se','edi_fo_ufd') ?};

VAR_DEL.delete('__FA');
__FA:=obj_new(
   'KODWALUTY','D_WYST','M_WYST','SYM','WZ','DOP','OKR_OD','OKR_DO','KURSWALUTYZ',
   'sFN23','sFV23','sFVW23',
   'sFN8','sFV8','sFVW8',
   'sFN5','sFV5','sFVW5',
   'sFN0','sFV0','sFNZ','sFB','sFN','sFV','sP_13_6_1','sP_13_6_2','sP_13_6_3',
   'sP_13_8','sP_13_9','sP_13_10','sP_13_11','sOO','sNO','sNW','sBW','DS_P_6A');

__FA.KODWALUTY:=__FA.D_WYST:=__FA.M_WYST:=__FA.SYM:=__FA.WZ:=__FA.DOP:=~~;
__FA.OKR_OD:=__FA.OKR_DO:=__FA.KURSWALUTYZ:=__FA.DS_P_6A:=~~;
__FA.sFN23:=__FA.sFV23:=__FA.sFVW23:=~~;
__FA.sFN8:=__FA.sFV8:=__FA.sFVW8:=~~;
__FA.sFN5:=__FA.sFV5:=__FA.sFVW5:=~~;
__FA.sFN0:=__FA.sFV0:=~~;
__FA.sFNZ:=~~;
__FA.sFB:=~~;
__FA.sFN:=~~;
__FA.sFV:=~~;
__FA.sP_13_6_1:=__FA.sP_13_6_2:=__FA.sP_13_6_3:=~~;
__FA.sP_13_8:=__FA.sP_13_9:=__FA.sP_13_10:=__FA.sP_13_11:=~~;
__FA.sOO:=~~;
__FA.sNO:=~~;
__FA.sNW:=sBW:=~~;

_edi:=exec('edi','edi_wspolne');
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
::{? DOK.WAL<>FINFO.NAROD || return(1) ?};
{? _a='FKS'
|| _param:=Plugin.runnable('BL_DKS_001');
   _grbef:=VPOZ.GRVAT;
   _rk:=VPOZ.R;
   _kk:=VPOZ.KK;
   _kkk:=VPOZ.K2;
   VPOZ.cntx_psh();
   VPOZ.use('pozv'+(DOK.name()+4));
   VPOZ.index('VDOK'); VPOZ.prefix();
   VPOZ.blank(1);
   VPOZ.DOK:=DOK.ref();
   {? var_pres('obdekredi')>0 & obdekredi=1
   || VPOZ.GRVAT:=_grbef;
      VPOZ.R:=_rk;
      VPOZ.KK:=_kk;
      VPOZ.K2:=_kkk
   ?};
:: PARAMETRYZACJA
   {? var_press('__PAR')>0
   || VPOZ.R:=__PAR.R;
      VPOZ.GRVAT:=__PAR.GRVAT;
      VPOZ.RVAT:=__PAR.RVAT;
      VPOZ.KK:=__PAR.KK;
      VPOZ.K2:=__PAR.K2;
      VPOZ.OKRVAT:=__PAR.OKRVAT
   || VPOZ.RVAT:=DOK.RVAT;
      VPOZ.OKRVAT:=DOK.OKRVAT
   ?};
:: Wywołanie formuły na domyślne parametry
   {? _param & ~(var_pres('obdekredi')>0 & obdekredi=1)
   || Plugin.run('BL_DKS_001',2)
   ?};
   KH.cntx_psh();
   DOKUM.KH(); _czytagger:=KH.TAGGER; _khuid:=KH.uidref(); _khref:=KH.ref();
   KH.cntx_pop();
   TAGGER.cntx_psh();
   {? _czytagger='T'
   || exec('tag_mask','tagger',_khref,'g');
      exec('tagger_tip_vpz_ksef','tagger','DK',_khuid,$DOK.ODD().FIRMA,$DOK.ODD,DOK.REJ().KOD)
   ?};
   TAGGER.cntx_pop()
|? _a='OBG'
|| EVAT.cntx_psh();
   EVAT.use('skid_a'+(EDOKUM.name()+2));
   EVAT.index('EDOKUM'); EVAT.prefix();
   EVAT.blank(1);
   EVAT.EDOKUM:=EDOKUM.ref()
?};
1


\fa_o_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: dodawanie pozycji VAT - end
::   WE: _a - FKS/OBG
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
_edi:=exec('edi','edi_wspolne');
_context:=_edi.context;
{? _a='FKS'
|| {? _verify || return(1) ?};
   {? var_pres('edipozfv')>0 & ~edipozfv || VPOZ.cntx_pop(); return(1) ?};
   {? var_pres('KODWALUTY',__FA)>0 || DOK.WAL:=exec('wal','edi_fo01',__FA.KODWALUTY) || DOK.WAL:=FINFO.NAROD ?};
   {? var_pres('KODWALUTY',__FA)>0 & DOK.WAL<>null & DOK.WAL<>FINFO.NAROD
   || SLU.cntx_psh(); SLO.cntx_psh();
      SLU.index('NAZ');
      SLO.index('SL'); SLU.prefix();
      {? SLU.find_key('WALUTY')
      || SLO.prefix(SLU.ref,exec('kodwaluty','edi_fo_ksef_fks'));
         {? SLO.first()
         || DOK.WAL:=SLO.ref();
            exec('dok_bank','!fks_dks_dark');
            DOK.put()
         ?}
      ?};
      SLU.cntx_pop(); SLO.cntx_pop()
   ?};
   {? var_pres('SYM',__FA)>0 || DOK.NK:=__FA.SYM ?};
   {? var_pres('sFB',__FA)>0 || DOK.BRUTTO:=__FA.sFB ?};
   {? var_pres('OKR_OD',__FA)>0 || DOKPOLA.OKRES_OD:=__FA.OKR_OD ?};
   {? var_pres('OKR_DO',__FA)>0
   || DOKPOLA.OKRES_DO:=__FA.OKR_DO;
      {? var_pres('DOP',__FA)=type_of(~~) || DOK.DOP:=__FA.OKR_DO ?}
   ?};
   {? var_pres('DOP',__FA)=type_of(~~) & var_pres('OKR_DO',__FA)=type_of(~~) & var_pres('DS_P_6A',__FA)>0
   || DOK.DOP:=__FA.DS_P_6A
   ?};
   {? var_pres('DOP',__FA)=type_of(~~) & var_pres('OKR_DO',__FA)=type_of(~~) & var_pres('DS_P_6A',__FA)=type_of(~~)
   || DOK.DOP:=exec('FindAndGet','#table',DOKUM,_context.DOKUM,,"@.DOKUM.KSEFSDAT",date(0,0,0))
   ?};
   {? var_pres('__POZFDATE')>0 & __POZFDATE.first()
   || VPOZ.D:=__POZFDATE.D;
      __POZFDATE.del()
   ?};
   _vpoz_add:="
:: _a - wartość netto - _b - wartość brutto - _c - % vat - _d - kategoria - _e - 'VAT' - f - vat w walucie
      VPOZ.cntx_psh();
      VPOZ.NETTO:=_a;
      VPOZ.VAT:=_b;
      VPOZ.PR:=exec('ufd2sv','edi_fo_ufd',_d,_c,_e);
      VPOZ.BRUTTO:=VPOZ.NETTO+VPOZ.VAT;
      {? DOK.WAL<>null & DOK.WAL<>FINFO.NAROD
      || _f:={? var_pres('_f')>0 || _f || 0 ?};
         {? var_pres('KURSWALUTYZ',__FA)>0 || VPOZ.KURS:=__FA.KURSWALUTYZ ?};
         VPOZ.WARW:=VPOZ.BRUTTO;
         VPOZ.NETTO_W:=VPOZ.NETTO;
         VPOZ.VAT_W:=VPOZ.VAT;
         VPOZ.BRUTTO_W:=VPOZ.BRUTTO;
         VPOZ.NETTO:=(VPOZ.NETTO_W*VPOZ.KURS/1)$2;
         VPOZ.VAT:=_f;
         VPOZ.BRUTTO:=(VPOZ.BRUTTO_W*VPOZ.KURS/1)$2
      ?};
      exec('setvatodl','dok_fks');
      {? ~VPOZ.add()
      || exec('add_kom','#message','Błąd podczas dodawania pozycji VAT.'@,2,__XML.ID)
      ?};
      VPOZ.cntx_pop()
   "
|| {? _verify || return(1) ?};
   {? var_pres('KODWALUTY',__FA)>0 || EDOKUM.WAL:=exec('wal','edi_fo01',__FA.KODWALUTY) || EDOKUM.WAL:=FINFO.NAROD ?};
   {? var_pres('D_WYST',__FA)>0 || EDOKUM.DW:=__FA.D_WYST ?};
   {? var_pres('SYM',__FA)>0 || EDOKUM.SYM:=__FA.SYM ?};
   {? var_pres('sFB',__FA)>0 || EDOKUM.WART:=__FA.sFB ?};
   {? var_pres('sFN',__FA)>0 || EDOKUM.NETTO:=__FA.sFN ?};
   {? var_pres('OKR_DO',__FA)>0 & var_pres('DOP',__FA)=type_of(~~)
   || EDOKUM.DOP:=__FA.OKR_DO
   ?};
   {? var_pres('DOP',__FA)=type_of(~~) & var_pres('OKR_DO',__FA)=type_of(~~) & var_pres('DS_P_6A',__FA)>0
   || EDOKUM.DOP:=__FA.DS_P_6A
   ?};
   {? var_pres('DOP',__FA)=type_of(~~) & var_pres('OKR_DO',__FA)=type_of(~~) & var_pres('DS_P_6A',__FA)=type_of(~~)
   || EDOKUM.DOP:=exec('FindAndGet','#table',DOKUM,_context.DOKUM,,"@.DOKUM.KSEFSDAT",date(0,0,0))
   ?};
   _vpoz_add:="
:: _a - wartość netto - _b - wartość brutto - _c - % vat - _d - kategoria - _e - 'VAT'
      EVAT.NETTO:=_a;
      EVAT.VAT:=_b;
      EVAT.PR:=exec('ufd2sv','edi_fo_ufd',_d,_c,_e);
      EVAT.BRUTTO:=EVAT.NETTO+EVAT.VAT;
      {? ~EVAT.add()
      || exec('add_kom','#message','Błąd podczas dodawania pozycji VAT.'@,2,__XML.ID)
      ?}
   "
?};
{? var_pres('sFV23',__FA)>0
|| _vpoz_add(__FA.sFN23,__FA.sFV23,'23','','VAT',{? var_pres('sFVW23',__FA)>0 || __FA.sFVW23 ?})
?};
{? var_pres('sFN8',__FA)>0
|| _vpoz_add(__FA.sFN8,__FA.sFV8,'8','','VAT',{? var_pres('sFVW8',__FA)>0 || __FA.sFVW8 ?})
?};
{? var_pres('sFN5',__FA)>0
|| _vpoz_add(__FA.sFN5,__FA.sFV5,'5','','VAT',{? var_pres('sFVW5',__FA)>0 || __FA.sFVW5 ?})
?};
{? var_pres('sP_13_6_1',__FA)>0 || {? var_pres('sFN0',__FA)>0 || __FA.sFN0+=__FA.sP_13_6_1 || __FA.sFN0:=__FA.sP_13_6_1 ?} ?};
{? var_pres('sP_13_6_2',__FA)>0 || {? var_pres('sFN0',__FA)>0 || __FA.sFN0+=__FA.sP_13_6_2 || __FA.sFN0:=__FA.sP_13_6_2 ?} ?};
{? var_pres('sP_13_6_3',__FA)>0 || {? var_pres('sFN0',__FA)>0 || __FA.sFN0+=__FA.sP_13_6_3 || __FA.sFN0:=__FA.sP_13_6_3 ?} ?};
{? var_pres('sFN0',__FA)>0 || _vpoz_add(__FA.sFN0,0,'0','','VAT') ?};
{? var_pres('sFNZ',__FA)>0 || _vpoz_add(__FA.sFNZ,0,'','zw','VAT') ?};
{? var_pres('sP_13_10',__FA)>0 || _vpoz_add(__FA.sP_13_10,0,'','oo','VAT') ?};
{? var_pres('sP_13_11',__FA)>0 || _vpoz_add(__FA.sP_13_11,0,'','np','VAT') ?};
VAR_DEL.delete('__FA');
{? _a='FKS'
|| VPOZ.cntx_pop();
   exec('advance_references_ee','edi_fo_ufd')
|| EVAT.cntx_pop()
?};
1


\p_pmarzy_3_1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: formuła na wartość DOKPOLA - używane dzieła sztuki
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? __XML.VAL='1'
|| DOKPOLA.UDS:='T';
   DOKPOLA.UDS_OPIS:='procedura marży - towary używane'
?};
1


\p_pmarzy_3_2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: formuła na wartość DOKPOLA - używane dzieła sztuki
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? __XML.VAL='1'
|| DOKPOLA.UDS:='T';
   DOKPOLA.UDS_OPIS:='procedura marży - dzieła sztuki'
?};
1


\p_pmarzy_3_3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: formuła na wartość DOKPOLA - używane dzieła sztuki
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? __XML.VAL='1'
|| DOKPOLA.UDS:='T';
   DOKPOLA.UDS_OPIS:='procedura marży - przedmioty kolekcjonerskie i antyki'
?};
1


\pozf_sv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: formuła na wartość POZF.SV
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
POZF.SV:=exec('procvat4xml','obiegi2',__XML.VAL);
1


\debit_value
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła na numery faktur zaliczkowych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
{? ~_verify
|| exec('edit','dok_ksef',1);
   _btxt:=_txt:=DOKPOLA.memo_txt(,0,'');
   _txt:=__XML.VAL;
   {? _txt<>'' & _txt<>_btxt
   || DOKPOLA.memo_set(_txt,'NRFAZAL');
      exec('edit','dok_ksef',2)
   ?}
?};
1


\deductions_value
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła na obiążenia - kwota
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
{? ~_verify
|| exec('edit','dok_ksef',1);
   _txt:=__XML.VAL;
   {? _txt<>''
   || DOKPOLA.ZALKWOD:=#_txt;
      exec('edit','dok_ksef',2)
   ?}
?};
1


\deductions_reason
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła na obiążenia - powód
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
{? ~_verify
|| exec('edit','dok_ksef',1);
   _btxt:=_txt:=DOKPOLA.memo_txt(,0,'ZALPOW');
   _txt:=__XML.VAL;
   {? _txt<>'' & _txt<>_btxt
   || DOKPOLA.memo_set(_txt,'ZALPOW');
      exec('edit','dok_ksef',2)
   ?}
?};
1


\wz_o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła na odczyt numerów dokumentów magazynowych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
{? ~_verify
|| exec('edit','dok_ksef',1);
   _btxt:=_txt:=DOKPOLA.memo_txt(,0,'WZ');
   _txt:=__XML.VAL;
   {? _txt<>'' & _txt<>_btxt
   || DOKPOLA.memo_set(_txt,'WZ');
      exec('edit','dok_ksef',2)
   ?}
?};
1


\payment_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła end element dla Platnosc - odczyt
::----------------------------------------------------------------------------------------------------------------------
{? __N.P_PAYMENT.first() || {! |? __N.P_PAYMENT.del() !} ?};
1


\payment_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła end element dla Platnosc - odczyt
::   WE: _a - OBG/FKS
::----------------------------------------------------------------------------------------------------------------------
__N.P_MEANS:=
   {? __N.P_FORM='1' || __N.P_DESC:='gotówka'; '10'
   |? __N.P_FORM='2' || __N.P_DESC:='karta'; '48'
   |? __N.P_FORM='3' || __N.P_DESC:='bon'; ''
   |? __N.P_FORM='4' || __N.P_DESC:='czek'; '20'
   |? __N.P_FORM='5' || __N.P_DESC:='kredyt'; ''
   |? __N.P_FORM='6' || __N.P_DESC:='przelew'; '31'
   |? __N.P_FORM='7' || __N.P_DESC:='mobilna'; ''
   || ''
   ?};
{? __N.P_PAYMENT.first()
|| {! |? {? _a='FKS' || DOK.D3:=__N.P_PAYMENT.DATE || EDOKUM.TP:=__N.P_PAYMENT.DATE ?}; __N.P_PAYMENT.next() !}
?};
exec('formplat_fks','edi_fo_ufd',_a)


\payment_terms_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła start element dla TerminyPlatnosci
::---------------------------------------------------------------------------------------------------------------------
__N.P_PAYMENT.DATE:=date(0,0,0);
__N.P_PAYMENT.DESC:='';
1


\payment_terms_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła end element dla TerminyPlatnosci
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
__N.P_PAYMENT.add();
1


\dokpola_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Formuła end element po redakcji pól z DOKPOLA
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
{? ~_verify
|| exec('edit','dok_ksef',2)
?}; 1


\pu_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Start element dla PodmiotUpoważniony
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
VAR_DEL.delete('__PU');
__PU:=obj_new('TAXID','NAME','ROLE','EORI');
__PU.TAXID:=__PU.NAME:=__PU.ROLE:=__PU.EORI:=~~;
1


\pu_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: End element dla PodmiotUpoważniony
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_nip:=__PU.TAXID;
_kh_ref:={? _nip<>'' || exec('FindInSet','#table','KH','SNIP',_nip,2) || null() ?};
{? exec('verify','edi_xml')
||
   {? _nip=''
   || exec('add_kom','#message','Nie znaleziono kontrahenta - nie podano NIP.'@,4,DOK.NK);
      _result:=0
   |? _kh_ref=null()
   || exec('add_kom','#message','Nie znaleziono kontrahenta - NIP %1.'@[_nip],4,DOK.NK);
      _result:=0
   ?};
   _result
||
   {? _kh_ref<>null()
   ||
      {? __PU.ROLE='1'
      || DOKPOLA.EGZ:='T';
         DOKPOLA.EGZ_KH:=_kh_ref;
         DOKPOLA.EGZ_ADR:=exec('kpocz','faktury_wydruk',DOKPOLA.EGZ_KH().KPOCZ,DOKPOLA.EGZ_KH().POCZ,
                       DOKPOLA.EGZ_KH().MIASTO,DOKPOLA.EGZ_KH().UL) + ' ' +
                       exec('adr','faktury_wydruk',DOKPOLA.EGZ_KH().UL,,,DOKPOLA.EGZ_KH().MIASTO,DOKPOLA.EGZ_KH().POCZ);
         DOKPOLA.EGZ_NAZ:=DOKPOLA.EGZ_KH().NAZ_P
      |? __PU.ROLE='3'
      || DOKPOLA.PP:='T';
         DOKPOLA.PP_KH:=_kh_ref;
         DOKPOLA.PP_ID:=_nip;
         DOKPOLA.PP_NAZ:=DOKPOLA.PP_KH().NAZ_P;
         DOKPOLA.PP_ADR:=exec('kpocz','faktury_wydruk',DOKPOLA.PP_KH().KPOCZ,DOKPOLA.PP_KH().POCZ,
                   DOKPOLA.PP_KH().MIASTO,DOKPOLA.PP_KH().UL)+ ' ' +
                   exec('adr','faktury_wydruk',DOKPOLA.PP_KH().UL,,,DOKPOLA.PP_KH().MIASTO,DOKPOLA.PP_KH().POCZ)
      ?};
      exec('edit','dok_ksef',2)
   || _result:=0
   ?}
?};
VAR_DEL.delete('__PU');
_result


\fa_p6_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: formuła end element dla pola Fa/P6
::   WE: _a - FKS/OBG
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('DOP',__FA)>0
|| {? _a='FKS'
   || DOK.DOP:=__FA.DOP;
      exec('chk_dtw','edi_fo02')
   || EDOKUM.DOP:=__FA.DOP;
      1
   ?}
|| 0
?}


\fa_p1_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: formuła end element dla pola Fa/P1
::   WE: _a - FKS/OBG
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('D_WYST',__FA)>0
|| {? _a='FKS'
   || DOK.DOP:=__FA.D_WYST
   || EDOKUM.DOP:=__FA.D_WYST
   ?};
   1
|| 0
?}


\fa_ds_p_6a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: formuła end element dla pola Fa/FaWiersz/P_6A
::   WE: _a - FKS/OBG
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('DS_P_6A',__FA)=type_of(~~)
|| __FA.DS_P_6A:=date(0,0,0)
?};
{? var_pres('DS_P_6A',__FA)>0
|| _date:=date(#(4+__XML.VAL),#(5-__XML.VAL-3),#(__XML.VAL+2));
   {? _date>__FA.DS_P_6A || __FA.DS_P_6A:=_date ?}
?};
1

:Sign Version 2.0 jowisz:1045 2024/02/21 12:10:57 a37c01416ae43095e6c5ce6607532a25734007e8940fb94cd4f0bef1ee7240c7a9dea21b704f8bd4dabc372c8c988a6001490f72f84571de0241831a6ba01df651192df197ef802e4f027b25d227c9bb063cb5cb05925316cd617205053f1f531b49b9a258858d00dac4d78332167ffaee926bdb8bc0b26f1041a65d6fd9d5de

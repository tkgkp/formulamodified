:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: lsp.fml
:: Utworzony: 12.01.2015
:: Autor: mario
::======================================================================================================================
:: Zawartość: Formuły oraz wywoływacze obszaru LSP
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: mario [17.00]
:: OPIS: Formuła inicjująca dla obszaru LSP
::----------------------------------------------------------------------------------------------------------------------
:: Podczytanie INFO.KRAJ - kraj waluty opodatkowania
exec('czytaj','#stalesys',,INFO,FINFO);
exec('czytaj','#stalesys',,XINFO,'SLWYDZIA','SLP','SLJEZYK','SLWAL','WIELKOSC','L_D_POZ','L_D_PUB','BL_TNNT');
exec('czytaj','#stalesys',,KST,'BDO','KRAJ','OSS','IOSS');

:: Podczytanie parametrów oddziału
KSTE.prefix; KSTE.first();

:: Ustawia zmienne informujące o dostępie do wartości sprzedaży, zakupu, magazynowych
exec('upr_set','ceny');

:: Ustawienie dostępności sumowania kolumn wg dostępu do wartości sprzedaży
exec('sum_kolumn','lsp');

:: Formuła podczytująca dane specyficzne dla firmy
__War_f:="exec('FindInSet','#table',_a,_a,_c,REF.FIRMA,$(_a+'.'+_b))";

:: Obiekt do obsługi parametrów
exec('Tpar_decl','tech_param');

:: RB - objekt obsługujący rachunki bankowe
exec('RB','object');

:: Ustawienie prezycji pól w oknach
exec('fld_prec','win_ust');

:: Inicjowanie kalsy DRUKARKA - dotyczy drukarki fiskalnej
exec('deklar','fis_drukarka');
exec('deklar_fsp','fis_posnet');
exec('myDIALOG','object');

:: Formy zatrudnienia
exec('__F_ZATR','object');

:: Salda i obroty kont i rozrachunków
exec('F','object');

:: format dla reklamacji
exec('f_DISP_EDIT','reklamacje');

:: konta kosztowe
BPMN.KK_DOM:='LMG';

ZK_N.fld_fml('PR','DISPLAY_FORMAT',"'out_prec=2'");
~~


\init_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła inicjująca dla obszaru LSP_OFE
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: blank FAKS.SZ - sprzedaż
{? INFO.WW='' || exec('czytaj','#stalesys',,INFO) ?};
BEER.SZ:='S';
:: Walutowość dla ofert
BEER.MW:='O';
:: Ustawia BEER.WW
exec('ustaw_ww','eurusd',BEER.MW)


\init_zkn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła inicjująca dla obszaru LSP_ZKN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: blank FAKS.SZ - sprzedaż
{? INFO.WW='' || exec('czytaj','#stalesys',,INFO) ?};
BEER.SZ:='S';
:: Walutowość dla zamówień
BEER.MW:='Z';
:: Ustawia BEER.WW
exec('ustaw_ww','eurusd',BEER.MW);
:: Ustawia uprawnienia do magazynów
exec('usuprmag','parses')


\init_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła inicjująca dla obszaru LSP_FAK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: blank FAKS.SZ - sprzedaż
{? INFO.WW='' || exec('czytaj','#stalesys',,INFO) ?};
BEER.SZ:='S';
:: Walutowość dla sprzedaży
BEER.MW:='F';
:: Ustawia BEER.WW
exec('ustaw_ww','eurusd',BEER.MW);
:: Ustawia uprawnienia do magazynów
{? exec('chk_role','#b__box',OPERATOR.USER,'LSP_FAK_DGDM') || exec('usuprmag','parses') ?}


\permissions_domain
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła na uprawnienia obszaru
::   WE: params_get().in - parametry wejściowe czynności
::       params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::       params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_wyn:=1;

_opis:='';

:: Sprawdzenie czy jest ustawiony rok
{? _wyn
|| _wyn:=ST.AR>0;
   {? _wyn=0 || _opis+='\n- okresu' ?}
?};

{? _wyn=0 & var_pres('opis',params_get())
|| params_get().opis.TXT:='Brak uprawnień do:'+_opis
?};

_wyn


\permissions_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Sprawdzenie uprawnień
::   WE: _a - nazwa pliku czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0-brak uprawnień, 1-są uprawnienia
::  OLD: \permissions_chk/!lsp_fak_drfp.fml
::----------------------------------------------------------------------------------------------------------------------
_fml:=_a;

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

:: Sprawdzenie uprawnień obszaru
_opis:=obj_new('TXT'); _opis.TXT:='';
params_set('in',_in,'user',OPERATOR.USER,'mp',_mp,'opis',_opis);
{? exec('permissions_domain','lsp')=0
|| _txt:='Brak uprawnień do uruchomienia czynności.'@;
   {? _opis.TXT<>'' || FUN.info(_txt+'\n\n'+_opis.TXT) ?};
   _mp.error(_txt);
   return(0)
?};
:: Sprawdzenie uprawnień czynności
{? _fml<>''
|| params_set('in',_in,'user',OPERATOR.USER,'mp',_mp);
   _perm_res:=exec('permissions',_fml);
   {? var_pres('_perm_res')=type_of(0)
   || _permissions_res:=exec('permissions_res','lsp');
      _permissions_res.RES:=_perm_res;
      _permissions_res.TXT:=''
   || _permissions_res:=_perm_res
   ?};
   {? _permissions_res.RES=0
   || _txt:='Brak uprawnień do uruchomienia czynności.'@;
      FUN.info(_txt+'\n'+_permissions_res.TXT);
      _mp.error(_txt+_permissions_res.TXT);
      return(0)
   ?}
?};
1


\permissions_res
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Wynik permissions
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
obj_new('RES','TXT')


\lsp_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: mario [17.00]
:: OPIS: Główna formuła obszaru roboczego LSP_OFE - Oferty sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_params:=params_get();
params_set(params_get());
{? type_of(_params)>0 & var_pres('LINK',_params)>0
|| _link:=_params.LINK
|| _link:=''
?};
{? _link<>''
||
   {? ref_tab(_link)=OFE
   ||
:: Ustalenie parametrów na podstawie przekazanego rekordu
      _dobrze:=1;
      _oddz:=exec('FindAndGet','#table',OFE,_link,,"OFE.ODDZ",'');
      {? ST.ODDZ<>_oddz
      ||
         {? _oddz=''
         || _dobrze:=0;
            FUN.info('Nie odnaleziono nagłówka oferty: %1'@[exec('record','#to_string',_link)])
         ?};

         {? _dobrze
         || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
            {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
            || _dobrze:=0
            ?}
         ?};

         {? _dobrze
::   Ustalenie parametrów sesji
         || __PARSES.setVal('OddzialLogProd',_oddz);
            ST.ODDZ:=_oddz
         || return(0)
         ?}
      ?}
   |? ref_tab(_link)=OFP
   ||
:: Ustalenie parametrów na podstawie przekazanego rekordu
      _dobrze:=1;
      _ofe:=exec('FindAndGet','#table',OFP,_link,,"OFP.OFE",'');
      _oddz:=exec('FindAndGet','#table',OFE,_ofe,,"OFE.ODDZ",'');
      {? ST.ODDZ<>_oddz
      ||
        {? _oddz=''
        || _dobrze:= 0;
           FUN.info('Nie odnaleziono nagłówka oferty: %1'@[exec('record','#to_string',_link)])
        ?};

        {? _dobrze
        || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
           {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
           || _dobrze:=0
           ?}
        ?};

        {? _dobrze
::   Ustalenie parametrów sesji
         || __PARSES.setVal('OddzialLogProd',_oddz);
            ST.ODDZ:=_oddz
         || return(0)
         ?}
      ?}
   ?}
?};

exec('init','lsp');
exec('init_ofe','lsp');
{? ST.AR=0 | ST.ODDZ_KOD=''
|| FUN.info('Ustaw parametry pracy.'@)
||

:: Obsługa linku
   {? _link<>''
   || VAR_DEL.delete('__typofe','__typz_o','__zakr_o');
:: dostępne typy ofert
      _p3007:=exec('get','#params',3007,2,OPERATOR.USER);

      _sql:="
         select
         T, NAZ, REFERENCE REF, ' ' INF, MOB, ZAP
         from TYPYZAM
         where TYPYZAM.R='O'
         and TYPYZAM.PROJ='N'";
      _sql+={? _p3007=''
            || ""
            || " and ':_a' like '% '||TYPYZAM.T||' %' "
            ?};
      _sql+=" order by T, T ";
      {? _p3007<>'' || _p3007:=' '+_p3007+' ' ?};
      __typofe:=sql(_sql,_p3007);
:: okienko typów zamówień
      __typz_o:=__typofe.mk_sel('Typy ofert'@,'P',0,'__typofe_win',,,,,'U','T');
      __typofe.win_fld(__typz_o,,'T',,,8,,0,'Typ'@);
      __typofe.win_fld(__typz_o,,'NAZ',,,32,,0,'Nazwa'@);
      __typofe.win_act(__typz_o,0,'Szukaj');
      __typofe.win_act(__typz_o,0,'Kolejność');

      OFE.win_fml('WER',,'SYM',,'ICON_BEFORE',exec('ofe_sym_ib','lsp'));
      _grp:=OFE.grp_make('GRP',,'#ofe_grpokn',,,"exec('exit','zws',_a)");

      _win_grp:=OFE.grp_make('',"params_exec('oferty_be','lsp')",'oferty_grp',,,"exec('exit','zws',_a)");
      OFE.grp_sel(_win_grp,__typofe,__typz_o,,"exec('oferty_typ_ar','lsp')"
          ,,,10,"params_exec('oferty_typ_be','lsp')",,,,'maximized');
      __typofe.win_sopt(__typz_o,'select_record_checkbox=0');
      OFE.grp_splt(_win_grp,,'vertical','ofe','0, 20%');
      OFE.grp_sel(_win_grp,,'WER',,,,,,,,,,'maximized','WER',1);
      OFE.win_sel(_win_grp);
      OFE.win_edit('RED');
      OFE.win_fml('WER',,'SYM',,'ICON_BEFORE',exec('ofe_sym_ib','lsp'));
      TYPYZAM.cntx_psh();

      {? ref_tab(_link)=OFE & OFE.seek(_link) & __typofe.find_key(OFE.T().T)
      ||

         _kh:=ZAKR.KH;
         _han:=ZAKR.H;
         _us:=OPERATOR.USER;
         ZAKR.T:=ZAKR.AKT:=OFE.A;
         ZAKR.WG:='O';
         ZAKR.US:=null;
         ZAKR.KH:=null;
         ZAKR.HAN:=null;

         BEER.MW:='O';
         BEER.SZ:='S';
::exec('ustaw_ww','eurusd','O');
         DISP.SN_OFE:=0;
         DISP.SZ_OFE:=0;
         exec('zakr_all','lsp');

         AreaTitle.setTabWin(OFE,~~);
         AreaTitle.setTitle();
         OFE.select(,1,,,'WER',,1);
         ZAKR.KH:=_kh;
         ZAKR.HAN:=_han;
         ZAKR.US:=_us

      |? ref_tab(_link)=OFP & OFP.seek(_link) & OFE.seek(OFP.OFE) & __typofe.find_key(OFE.T().T)
      ||
         _kh:=ZAKR.KH;
         _han:=ZAKR.H;
         _us:=OPERATOR.USER;
         ZAKR.T:=ZAKR.AKT:=OFE.A;
         ZAKR.WG:='O';
         ZAKR.US:=null;
         ZAKR.KH:=null;
         ZAKR.HAN:=null;

         BEER.MW:='O';
         BEER.SZ:='S';
::exec('ustaw_ww','eurusd','O');
         DISP.SN_OFE:=0;
         DISP.SZ_OFE:=0;
         AreaTitle.setTabWin(OFE,~~);
         AreaTitle.setTitle();
         OFE.select(,1,,,'WER','Y',1);
         ZAKR.KH:=_kh;
         ZAKR.HAN:=_han;
         ZAKR.US:=_us

      || FUN.info('Nie odnaleziono oferty lub pozycji \n%1.'@[exec('record','#to_string',_link)])
      ?};
      TYPYZAM.cntx_pop();
      VAR_DEL.delete('__typofe','__typz_o','__zakr_o')
   ||
:: okno wywoływacza
      exec('ofe_sel','lsp')
   ?}
?};
~~


\lsp_fak_env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Środowisko obszaru LSP_FAK - Dokumenty sprzedaży
:: ~OST: INFCOPY,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablice i komentować poszczególne jej elementy
::       powiedzmy, że to bedzie pole
         _fld:="31+form(_a)";
::       powiedzmy, że to bedzie metoda
         _mth:="31+form(_a)";

_env:=obj_new(
::             pola
                _fld('DATE',                 'przy kopiowaniu - data wystawienia nowych dokumentów')
               ,_fld('DATE_S',               'przy kopiowaniu - data sprzedaży nowych dokumentów')
               ,_fld('OUT_FAKS',             'przy kopiowaniu - ostatnia dołączona faktura')
               ,_fld('B_PREL',               'przy kopiowaniu - zapamiętuje wybrany proces w pętli')
               ,_fld('B_DOMAIN',             'dziedzina')
               ,_fld('TABLE',                'główna tabela obszaru')
               ,_fld('POS',                  'tabela obszaru POS')
               ,_fld('LINK',                 'obiekt do obsługi linków')
               ,_fld('TTABS_CUR_TAB',        'górny panel - id bieżacej zakładki')
               ,_fld('BTABS',                'dolny panel - tabela zakładek')
               ,_fld('BTABS_NDX_ID',         'dolny panel - tabela zakładek - index NDX_ID')
               ,_fld('BTABS_NDX_NR',         'dolny panel - tabela zakładek - index NDX_NR')
               ,_fld('BTABS_ON',             'dolny panel - 0/1 - panel włączony')
               ,_fld('BTABS_NR',             'dolny panel - nr ostatnio dodanej zakładki')
               ,_fld('BTABS_CUR_TAB',        'dolny panel - id bieżacej zakładki')
               ,_fld('BTABS_REF_REFRESH',    'dolny panel - ref odświeżonego rekordu')
               ,_fld('BTABS_CRC_REFRESH',    'dolny panel - crc odświeżonego rekordu')
               ,_fld('POZYCJE_FAK_VAR',      'dolny panel - zmienna używana w zakładkach pozycji')

::             identyfikatory okienek
               ,_fld('wid_win_main',         'ID okna glownego')

::             tytuly okienek
               ,_fld('tit_main',             'Tytul okienka glownego')

::             tabele tymczasowe

::             indeksy tymczasowych tabel

::             uchwyty do okien
               ,_fld('WIN_MAIN',             'Główne okno grupowe')

::             identyfikatory przycisków

::             METODY
               ,_mth('initObszar',           'Init obszaru')
               ,_mth('doneObszar',           'Done obszaru')
               ,_mth('winMain',              'Ustawienie głównego okna')
               ,_mth('btabs_add',            'Panel dolny - Dodanie zakładki')
               ,_mth('btabs_show',           'Panel dolny - Wyświetlenie zakładek')
               ,_mth('btabs_pozycje',        'Panel dolny - Nazwa zakładki pozycji dokumentu')
               );

_env.DATE:=date(0,0,0);
_env.OUT_FAKS:=null();
_env.LINK:=exec('Link','#object');
_env.LINK.FORMULA:="";
_env.BTABS:=tab_tmp(1
   ,'ID','STRING[50]','Id'
   ,'NR','INTEGER','Nr'
   ,'STAN','INTEGER','Stan'
   ,'ZSTAN','INTEGER','Zadany stan'
   ,'FAR','STRING[150]','Formuła na odśwież');
_env.BTABS_NDX_ID:=_env.BTABS.index('?');
_env.BTABS_NDX_NR:=_env.BTABS.ndx_tmp(,,'NR',,);
_env.BTABS_ON:=0;
_env.BTABS_NR:=0;
_env.BTABS_CUR_TAB:='';
_env.BTABS_REF_REFRESH:='';
_env.BTABS_CRC_REFRESH:=0;
_env.POZYCJE_FAK_VAR:=exec('pozycje_fak_var','faktury_poz');
_env.btabs_add:="
   {? ~.BTABS.find_key(_a)
   ||
      .BTABS.blank();
      .BTABS.ID:=_a;
      .BTABS.NR:=.BTABS_NR+=1;
      .BTABS.STAN:=1;
      .BTABS.FAR:=_b;
      .BTABS.add()
   ?}
";
_env.btabs_show:="
   {? ~.BTABS_ON || return() ?};
   _Tab:=.BTABS;
   _Cur_tab:=cur_tab(1,1);
   {? {? _Cur_tab.f_active() || {? _Cur_tab.sel_size() || 1 || ~_Cur_tab.f_get() ?} || ~_Cur_tab.get() ?}
   ||
      tab_hide(,1,'bottom');
      .BTABS_REF_REFRESH:='';
      .BTABS_CRC_REFRESH:=0;
      _loop:=_Tab.first(); {! |? _loop |! _Tab.STAN:=0; _loop:=_Tab.put() & _Tab.next() !};
      return()
   ?};
   _loop:=_Tab.first(); {! |? _loop |! _Tab.ZSTAN:=0; _loop:=_Tab.put() & _Tab.next() !};
   {! _ii:=1.._ |! {? _Tab.find_key(_[_ii],) || _Tab.ZSTAN:=1; _Tab.put() ?} !};
   {? _Tab.find_key(.BTABS_CUR_TAB,) & _Tab.ZSTAN=0 || .BTABS_CUR_TAB:='' ?};
   _Tab.cntx_psh();
   _Tab.index(.BTABS_NDX_NR);
   _first_tab:='';
   _loop:=_Tab.first();
   {!
   |? _loop
   |!
      {? _Tab.STAN<>_Tab.ZSTAN
      ||
         {? _Tab.STAN
         || tab_hide(_Tab.NR,,'bottom')
         || tab_show(_Tab.NR,'bottom');
            {? .BTABS_CUR_TAB='' || .BTABS_CUR_TAB:=_Tab.ID  ?}
         ?};
         _Tab.STAN:=_Tab.ZSTAN;
         _Tab.put()
      ?};
      {? _first_tab='' & _Tab.STAN || _first_tab:=_Tab.ID ?};
      _loop:=_Tab.next()
   !};
   _Tab.cntx_pop();
   {? .BTABS_CUR_TAB='' || .BTABS_CUR_TAB:=_first_tab ?};
   {? (.BTABS_REF_REFRESH<>$_Cur_tab.ref() | .BTABS_CRC_REFRESH<>$_Cur_tab.crc())
         &
      .BTABS_CUR_TAB<>'' & _Tab.find_key(.BTABS_CUR_TAB,)
   ||
      tab_sel(_Tab.NR,'bottom');
      params_set(params_get());
      ($_Tab.FAR)()
   ?};
   .BTABS_REF_REFRESH:=$_Cur_tab.ref();
   .BTABS_CRC_REFRESH:=$_Cur_tab.crc()
";
_env.btabs_pozycje:="
   {? FAKS.WHERE='K' || 'Pozycje korekty zbiorczej'
   |? FAKS.KOR='' || 'Pozycje faktury'
   |? FAKS.WHERE='N' || 'Pozycje korekty nagłówkowej'
   |? FAKS.T().HIS='T' || 'Pozycje korekty historycznej'
   || 'Pozycje korekty'
   ?}
";

_env.wid_win_main:='lspdok_winmain';

:: Tytuł okna ustawiany w obiekcie AreaTitle
_env.tit_main:='';

_env.WIN_MAIN:='';

_env.initObszar:="
   .B_DOMAIN:=_a;
   _wyn:=1;

   exec('init','lsp');
   exec('lsp_fak_var','lsp');
   {? .B_DOMAIN='LSS'
   || exec('lss_pos_var','lss');
      .wid_win_main:='#gpr_w_pos';
      .tit_main:='Punkt sprzedaży detalicznej';
      .POS:=obj_new('prev_faks_ref','faks_ref')
   ?};

   .TABLE:={? .B_DOMAIN='LSS' || __POSWIN || FAKS ?};

   .BTABS_ON:=1& .B_DOMAIN='LSP';

   _wyn
";

_env.doneObszar:="
   BEER.NAWIG:=''
";

:: Takie czary, żeby zadziałały tłumaczenia poniżej (teksty MUSZĄ być identyczne)
:: panel górny
'Zakres dokumentów'@; 'Dokumenty'@; 'Stany magazynowe'@; 'Zlecenia fakturowania'@; 'Pozycje dokumentów'@;
:: panel dolny
'Pozycje faktury'@;'Dane dokumentu'@;
_env.winMain:="
   FILTER.STATUS:='M';
   FILTER.HIDDEN:='N';

   _upr:=exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_ZPTM')
       | exec('chk_role','#b__box',OPERATOR.USER,'LSP_FAK_PZLF')
       | exec('chk_role','#b__box',OPERATOR.USER,'LSP_FAK_DZLF')
       | exec('chk_role','#b__box',OPERATOR.USER,'LSP_FAK_EZLF');
   _formula:=\"
      _env:=params_get().env;
      exec('lsp_fak_filtr','lsp');
      {? _env.B_DOMAIN='LSS'
      || exec('load','#desktop','pos1','pos1.dsk',,,,,exec('pos_translate','pos'));
         _sep:=exec('sep','#file');
         _file:='xwin16.png';
         _filepath:=tmp_dir()+_sep+_file;
         fcopy(_file,'@'+_filepath,1,0,1);
         exec('set_icons','#desktop','','pos1',_filepath);
         exec('grab_focus','#desktop','','pos1','calc_19@pos1');
         __pos_cashback:=0;
         exec('cashback_load','pos');
         {? _env.BTABS_ON || tab_sel(1,'panel0') || tab_sel(1) ?}
      ?};
      params_set(params_get()); _env.btabs_show();
      _env.LINK.FORMULA();
      1
   \";
::
:: okno grupowe
::
   _grp_w:=.WIN_MAIN:=
      {? .B_DOMAIN='LSP'
      || FAKS.grp_make(.tit_main,_formula,.wid_win_main,,,\"exec('exit','zws',_a)\")
      ||
:: Funkcja wykonująca się po zamknięciu okienka, wykonuje anulowanie dokumentu,
:: za komentowana ze względu przeniesienia do obszaru, funkcja się nie wykonywała
:: __temp_pos_close
         _fc:=\"return(exec('close_window','pos'))\";
         __POSWIN.grp_make(.tit_main,_formula,.wid_win_main,1,1,_fc,,'maximized')
      ?};
:: zakładka - POS
   {? .B_DOMAIN='LSS'
   ||
      exec('create','#desktop',__POSWIN,'pos1',_grp_w,40,,,'POS',,,,,\"FUN.STANDARD:=0;params_exec('pos_ini','pos')\")
   ?};
:: zakładka - Dokumenty
   _far:=\"
      params_set(params_get());
      _env:=params_get().env;
      exec('lsp_fak_var','lsp');
      FILTER.FAKS:=null();
      params_exec('faks_far_wers','lsp','LSP_FAK');
      {? _env.BTABS_ON
      ||
         {? FAKS.MAG & FAKS.MAG().MG_OPAK='' | (exec('wydaFaks','faktury_wspolne') & FAKS.WHERE<>'G') | FAKS.WHERE='L'
         || {? FAKS.WHERE='K' | FAKS.WHERE='N'
            || {? exec('upgrade2325_blbc1','zbl')
               || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Dane dokumentu','Rezultat kontroli Businesscheck')
               || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Dane dokumentu')
               ?}
            || {? exec('upgrade2325_blbc1','zbl')
               || _env.btabs_show(_env.btabs_pozycje(),'Dane dokumentu','Rezultat kontroli Businesscheck')
               || _env.btabs_show(_env.btabs_pozycje(),'Dane dokumentu')
               ?}
            ?}
         || {? FAKS.WHERE='K' | FAKS.WHERE='N'
            || {? exec('upgrade2325_blbc1','zbl')
               || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Opakowania','Dane dokumentu','Rezultat kontroli Businesscheck')
               || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Opakowania','Dane dokumentu')
               ?}
            || {? exec('upgrade2325_blbc1','zbl')
               || _env.btabs_show(_env.btabs_pozycje(),'Opakowania','Dane dokumentu','Rezultat kontroli Businesscheck')
               || _env.btabs_show(_env.btabs_pozycje(),'Opakowania','Dane dokumentu')
               ?}
            ?}
         ?}
      ||
         grp_edisp(FAKS,exec('faks_win_info','faktury_nag'))
      ?}
   \";
   _fb:=\"
      {? _a
      ||
:: aktywowanie okna
         FUN.STANDARD:=1;
         params_set(params_get());
         _env:=params_get().env;
         _env.TTABS_CUR_TAB:='Dokumenty';
         exec('lsp_fak_var','lsp');
         {? FILTER.FAKS<>null() || {? FAKS.f_active() || FAKS.f_seek(FILTER.FAKS) || FAKS.seek(FILTER.FAKS) ?} ?};
         FILTER.FAKS:=null();
         _env.TABLE.timer('sel',_env.WIN_MAIN,2,\\\"{? FAKS.sel_size()=0 || FAKS.f_rfresh() ?}\\\");
         {? _env.B_DOMAIN='LSS'
         ||
            {? var_pres('__temp_pos_nag_ref')>0
            ||
               {? FAKS.sel_size()=0 || FAKS.f_rfresh() ?};
               _pos:=_env.POS;
               {? FAKS.f_active() || FAKS.f_seek(_pos.prev_faks_ref) || FAKS.seek(_pos.prev_faks_ref) ?}
            ?}
         ?};
         {? FAKS.MAG & FAKS.MAG().MG_OPAK='' | (exec('wydaFaks','faktury_wspolne') & FAKS.WHERE<>'G') | FAKS.WHERE='L'
         || {? FAKS.WHERE='K' | FAKS.WHERE='N'
            || {? exec('upgrade2325_blbc1','zbl')
               || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Dane dokumentu','Rezultat kontroli Businesscheck')
               || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Dane dokumentu')
               ?}
            || {? exec('upgrade2325_blbc1','zbl')
               || _env.btabs_show(_env.btabs_pozycje(),'Dane dokumentu','Rezultat kontroli Businesscheck')
               || _env.btabs_show(_env.btabs_pozycje(),'Dane dokumentu')
               ?}
            ?}
         || {? FAKS.WHERE='K' | FAKS.WHERE='N'
            || {? exec('upgrade2325_blbc1','zbl')
               || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Opakowania','Dane dokumentu','Rezultat kontroli Businesscheck')
               || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Opakowania','Dane dokumentu')
               ?}
            || {? exec('upgrade2325_blbc1','zbl')
               || _env.btabs_show(_env.btabs_pozycje(),'Opakowania','Dane dokumentu','Rezultat kontroli Businesscheck')
               || _env.btabs_show(_env.btabs_pozycje(),'Opakowania','Dane dokumentu')
               ?}
            ?}
         ?}
      ?}
   \";
   _fa:=\"
      FUN.STANDARD:=1;
      _env:=params_get().env;
      _env.TABLE.timer('sel',_env.WIN_MAIN,0)
   \";
   _wer:='WERS';
   FAKS.win_fml(_wer,,'SYM',,'ICON_BEFORE',exec('faks_sym_ib','faktury_nag'));
   FAKS.win_fml(_wer,,'NR',,'ICON_BEFORE',exec('faks_sym_it','faktury_nag'));
   FAKS.win_fml(_wer,,'NETTO',,'ICON_BEFORE',exec('faks_war_ib','faktury_nag'));
   FAKS.win_fml(_wer,,'STATKSEF',,'ICON_BEFORE',exec('faks_statksef_ib','faktury_nag1'),2);
   {? exec('upgrade2325_blbc1','zbl')
   || FAKS.win_fml(_wer,,'STATBC',,'ICON_BEFORE',exec('faks_statbc_ib','faktury_nag1'),2)
   ?};
   FAKS.win_sel(_wer);
   {? _upr
   || .TABLE.grp_sel(_grp_w,FAKS,_wer,'Dokumenty'@,_far,,,23,_fb,_fa,,,'maximized','WER')
   || .TABLE.grp_sel(_grp_w,FAKS,_wer,'Dokumenty'@,_far,,,23,_fb,,,,'maximized','WER')
   ?};
   {? ~.BTABS_ON
   ||
      .TABLE.tab_splt(_grp_w,,'horizontal','info');
      _fb:=\"exec('set_efld_opt','faktury_nag',exec('faks_win_info','faktury_nag'))\";
      .TABLE.grp_edit(_grp_w,FAKS,exec('faks_win_info','faktury_nag'),,,_fb,,,'maximized')
   ?};
   {? _upr
   ||
:: zakładka - Stany magazynowe
      _far:=\"
         params_set(params_get());
         _env:=params_get().env;
         _env.btabs_show('Stany magazynowe')
      \";
      _fb:=\"
         params_set(params_get());
         _env:=params_get().env;
         _env.TTABS_CUR_TAB:='Stany magazynowe';
         exec('stan_mg_up','magazyn_stan','S');
         _env.btabs_show('Stany magazynowe')
      \";
      .TABLE.grp_sel(_grp_w,M,'STANY_UP','Stany magazynowe'@,_far,,,23,_fb,,,,'maximized');
      task_attach('LMG_MAG_ZPTM');
      exec('addsmmag','magazyn_stan');
      {? ~.BTABS_ON
      ||
         .TABLE.tab_splt(_grp_w,,'vertical','stany','0, 67%');
         .TABLE.grp_sel(_grp_w,__smmag,__smmag.win_sel('?'),,,,,23,,,,,'maximized')
      ?};
:: zakładka - Zlecenia fakturowania
      _ff:=\"
         {? FAKZ.STAT_REJ='T' || exec('zaakceptowany','icon')
         |? FAKZ.STAT_REJ='Z' || exec('zarejestrowany','icon')
         || exec('pusta','#icon')
         ?}
      \";
      FAKZ.win_fml('WER',,'NR_ZEW',,'ICON_BEFORE',_ff);
      _far:=\"
         params_set(params_get());
         _env:=params_get().env;
         _env.btabs_show('Dane zlecenia fakturowania')
      \";
      _fb:=\"
         FUN.STANDARD:=1;
         params_set(params_get());
         _env:=params_get().env;
         _env.TTABS_CUR_TAB:='Zlecenia fakturowania';
         exec('lsp_fak_filtr','lsp');
         _env.btabs_show('Dane zlecenia fakturowania')
      \";
      {? .BTABS_ON
      || .TABLE.grp_sel(_grp_w,FAKZ,'TWER','Zlecenia fakturowania'@,_far,,,16,_fb,,,,'maximized','ZLF')
      || .TABLE.grp_sel(_grp_w,FAKZ,'WER','Zlecenia fakturowania'@,,,,16,_fb,,,,'maximized','ZLF')
      ?};
      task_attach('LSP_FAK_PZLF');
      task_attach('LSP_FAK_DZLF');
      task_attach('LSP_FAK_EZLF');
:: zakładka - Pozycje dokumentów
      _far:=\"
         {? (FAP.sel_size() | {? FAP.f_active() || FAP.f_size() || FAP.size() ?})
          & (FILTER.STATUS='R' | FAKS.AM=ST.AM)
         || FILTER.FAKS:=FAKS.ref()
         || FILTER.FAKS:=null()
         ?};
         params_set(params_get());
         _env:=params_get().env;
         _env.btabs_show('Dane dokumentu')
      \";
      _fb:=\"
         FUN.STANDARD:=1;
         params_set(params_get());
         _env:=params_get().env;
         _env.TTABS_CUR_TAB:='Pozycje dokumentów';
         exec('all_poz','lsp');
         _env.btabs_show('Dane dokumentu')
      \";
      {? .BTABS_ON
      || .TABLE.grp_sel(_grp_w,FAP,'TALLS','Pozycje dokumentów'@,_far,,,23,_fb,,,,'maximized_with_title','POZ')
      || FAP.win_fml('ALLS',,'FAKS','SYM','ICON_BEFORE',exec('faks_sym_ib','faktury_nag'));
         .TABLE.grp_sel(_grp_w,FAP,'ALLS','Pozycje dokumentów'@,_far,,,23,_fb,,,,'maximized_with_title','POZ')
      ?};
      task_attach('LSP_FAK_PZLF');
      task_attach('LSP_FAK_DZLF');
      task_attach('LSP_FAK_EZLF');
      task_attach('LSP_FAK_DRFP');
      task_attach('LSP_FAK_DGDM');
      task_attach('LSP_FAK_DRZA')
   ||
:: zakładka - Pozycje dokumentów
      _far:=\"
         {? (FAP.sel_size() | {? FAP.f_active() || FAP.f_size() || FAP.size() ?})
         & (FILTER.STATUS='R' | FAKS.AM=ST.AM)
         || FILTER.FAKS:=FAKS.ref()
         || FILTER.FAKS:=null()
         ?};
         params_set(params_get());
         _env:=params_get().env;
         _env.btabs_show('Dane dokumentu')
      \";
      _fb:=\"
         params_set(params_get());
         _env:=params_get().env;
         _env.TTABS_CUR_TAB:='Pozycje dokumentów';
         exec('all_poz','lsp');
         _env.btabs_show('Dane dokumentu')
      \";
      {? .BTABS_ON
      || .TABLE.grp_sel(_grp_w,FAP,'TALLS','Pozycje dokumentów'@,_far,,,23,_fb,,,,'maximized_with_title','POZ')
      || FAP.win_fml('ALLS',,'FAKS','SYM','ICON_BEFORE',exec('faks_sym_ib','faktury_nag'));
         .TABLE.grp_sel(_grp_w,FAP,'ALLS','Pozycje dokumentów'@,_far,,,23,_fb,,,,'maximized_with_title','POZ')
      ?}
   ?};
::
:: --- panel - bottom
::
   {? .BTABS_ON
   ||
::
:: Dokumenty
::
      params_set('env',.);
      .TABLE.grp_splt(_grp_w,'panel0','horizontal','bottom','0,67%');
:: zakładka - Pozycje faktury
      .btabs_add('Pozycje faktury',$\"params_set(params_get()); grp_disp(FAP,'TWER',,,'bottom')\");
      _far:=\"
         params_set(params_get());
         {? var_pres('__FAKS_GRP_DISP')>0 & __FAKS_GRP_DISP || grp_disp(FAKS,'WERS',,,'panel0'); __FAKS_GRP_DISP:=0 ?}
      \";
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Pozycje faktury';
         params_exec('pozycje_fak','faktury_poz',,exec('acts_schem_faks','faktury_poz'),,1,_a,_env.POZYCJE_FAK_VAR);
         1
      \";
      _fa:=\"
         _env:=params_get().env;
         params_exec('pozycje_fak_po','faktury_poz',1,_a,_env.POZYCJE_FAK_VAR);
         FAKS.trig_a('put',,'faks_grp_disp')
      \";
      .TABLE.grp_sel(_grp_w,FAP,'TWER','Pozycje faktury'@,_far,,,12,_fb,_fa,,,'maximized','FAP_TWER');
:: zakładka - Pozycje korekty
      .btabs_add('Pozycje korekty',$\"params_set(params_get()); grp_disp(FAP,'TWERK',,,'bottom')\");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Pozycje korekty';
         params_exec('pozycje_fak','faktury_poz',,exec('acts_schem_faks','faktury_poz'),,1,_a,_env.POZYCJE_FAK_VAR)
      \";
      .TABLE.grp_sel(_grp_w,FAP,'TWERK','Pozycje korekty'@,_far,,,12,_fb,_fa,,,'maximized','FAP_TWERK');
:: zakładka - Pozycje korekty historycznej
      .btabs_add('Pozycje korekty historycznej',$\"params_set(params_get()); grp_disp(FAP,'TWERKH',,,'bottom')\");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Pozycje korekty historycznej';
         params_exec('pozycje_fak','faktury_poz',,exec('acts_schem_faks','faktury_poz'),,1,_a,_env.POZYCJE_FAK_VAR)
      \";
      .TABLE.grp_sel(_grp_w,FAP,'TWERKH','Pozycje korekty historycznej'@,_far,,,12,_fb,_fa,,,'maximized','FAP_TWERKH');
:: zakładka - Korygowane dokumenty
      __faks_kzf_wer:=exec('faks_kzf','faktury_nag',2);
      .btabs_add('Korygowane dokumenty',$\"
         FAKS.cntx_psh();
         params_set(params_get()); grp_disp(FAKS_KZF,__faks_kzf_wer,,,'bottom');
         FAKS.cntx_pop()
      \");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         FAKS.cntx_psh();
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Korygowane dokumenty';
         FAKS_KZF.win_sel(__faks_kzf_wer);
         params_exec('faks_kzf','faktury_nag',1,_a,_env.POZYCJE_FAK_VAR)
      \";
      _fa:=\"
         _env:=params_get().env;
         params_exec('faks_kzf_po','faktury_nag',_env.POZYCJE_FAK_VAR);
         FAKS.cntx_pop();
         FAKS.trig_a('put',,'faks_grp_disp')
      \";
      .TABLE.grp_sel(_grp_w,FAKS_KZF,__faks_kzf_wer,'Korygowane dokumenty'@,_far,,,12,_fb,_fa,,,'maximized','FAKS_KZF_WER1');
:: zakładka - Pozycje korekty zbiorczej
      .btabs_add('Pozycje korekty zbiorczej',$\"
         FAKS.cntx_psh();
         params_set(params_get()); grp_disp(FAP,'TWERKZB',,,'bottom');
         FAKS.cntx_pop()
      \");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         FAKS.cntx_psh();
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Pozycje korekty zbiorczej';
         params_exec('pozycje_fak','faktury_poz',,exec('acts_schem_faks','faktury_poz'),,1,_a,_env.POZYCJE_FAK_VAR)
      \";
      _fa:=\"
         FAKS.cntx_pop();
         FAKS.trig_a('put',,'faks_grp_disp')
      \";
      .TABLE.grp_sel(_grp_w,FAP,'TWERKZB','Pozycje korekty zbiorczej'@,,,,12,_fb,_fa,,,'maximized','FAP_TWERKZB');
:: zakładka - Pozycje korekty nagłówkowej
      .btabs_add('Pozycje korekty nagłówkowej',$\"
         FAKS.cntx_psh();
         params_set(params_get()); grp_disp(FAP,'TWERKN',,,'bottom');
         FAKS.cntx_pop()
      \");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         FAKS.cntx_psh();
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Pozycje korekty nagłówkowej';
         params_exec('pozycje_fak','faktury_poz',,exec('acts_schem_faks','faktury_poz'),,1,_a,_env.POZYCJE_FAK_VAR)
      \";
      .TABLE.grp_sel(_grp_w,FAP,'TWERKN','Pozycje korekty nagłówkowej'@,,,,12,_fb,_fa,,,'maximized','FAP_TWERKN');
:: zakładka - Opakowania
      .btabs_add('Opakowania',$\"params_set(params_get()); grp_disp(OPAK_P,'TWER',,,'bottom')\");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Opakowania';
         exec('opak_wer','opakow',FAKS.ref(),1)
      \";
      _fa:=\"
         exec('opak_out','opakow');
         FAKS.trig_a('put',,'faks_grp_disp')
      \";
      .TABLE.grp_sel(_grp_w,OPAK_P,'TWER','Opakowania'@,,,,12,_fb,_fa,,,'maximized','OPAK_P_WER');
::
:: Dokumenty, Pozycje dokumentów
::
:: zakładka - Dane dokumentu
      .btabs_add('Dane dokumentu',$\"params_set(params_get()); grp_edisp(FAKS,'TINFO')\");
      _fb:=\"
         FAKS.cntx_psh();
         FAKSZ.STATOREJ:=exec('statorej','faktury_nag1');
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Dane dokumentu';
         {? _env.TTABS_CUR_TAB='Pozycje dokumentów' || FAP.FAKS() ?}
      \";
      _fa:=\"
         FAKS.cntx_pop()
      \";
      .TABLE.grp_edit(_grp_w,FAKS,'TINFO','Dane dokumentu'@,,_fb,_fa,, 'maximized');
::
:: Dokumenty
::
:: zakładka - Rezultat kontroli Businesscheck
      {? exec('upgrade2325_blbc1','zbl')
      || .btabs_add('Rezultat kontroli Businesscheck',$\"
            FAKS.cntx_psh();
            params_set(params_get()); grp_disp(DOKUMZBC,'WER',,,'bottom');
            FAKS.cntx_pop()
         \");
         _fb:=\"
            __FAKS_GRP_DISP:=0;
            FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
            FAKS.cntx_psh();
            _env:=params_get().env;
            _env.BTABS_CUR_TAB:='Rezultat kontroli Businesscheck';
            params_exec('faks_dokumzbc_seek','zbl');
            {? __DOKUMZ_BC.DOKUMZ=null()
            || DOKUMZBC.actions_grayed('WER','WH:WH')
            || DOKUMZBC.actions_grayed('WER')
            ?}
         \";
         _fa:=\"
            FAKS.cntx_pop();
            FAKS.trig_a('put',,'faks_grp_disp')
         \";
         .TABLE.grp_sel(_grp_w,DOKUMZBC,'WER','Rezultat kontroli Businesscheck'@,,,,12,_fb,_fa,,,'maximized','DOKUMZBC_WER')
      ?};
::
:: Stany magazynowe
::
:: zakładka - Stany magazynowe
      {? _upr
      ||
         .btabs_add('Stany magazynowe',$\"\");
         .TABLE.grp_sel(_grp_w,__smmag,__smmag.win_sel('?'),'Stany magazynowe'@,,,,12,,,,,'maximized','SMMAG')
      ?};
::
:: Zlecenia fakturowania
::
:: zakładka - Dane zlecenia fakturowania
      {? _upr
      ||
         .btabs_add('Dane zlecenia fakturowania',$\"params_set(params_get()); grp_edisp(FAKZ,'INFO')\");
         _fb:=\"
            _env:=params_get().env;
            _env.BTABS_CUR_TAB:='Dane zlecenia fakturowania';
            FAKZ.memo_get(,'UWAGI')
         \";
         .TABLE.grp_edit(_grp_w,FAKZ,'INFO','Dane zlecenia fakturowania'@,,_fb,,, 'maximized')
      ?}
   ?};
::
   .TABLE.win_sel(.WIN_MAIN)
";

_env


\lsp_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Główna formuła obszaru LSP_FAK - Dokumenty sprzedaży
::----------------------------------------------------------------------------------------------------------------------
:: Funkcja wołająca kartotekę materiałową w wersji 12.41
::exec('dok_all','faktury0');
_env:=exec('lsp_fak_env','lsp');

{? _env.initObszar('LSP')
|| _env.winMain();
   params_set('env',_env);

::======================================================================================================================
:: Ustalenie treści linku
   _params:=params_get();
   {? type_of(_params)>0 & var_pres('LINK',_params)
   || _link:=_params.LINK
   || _link:=''
   ?};
:: Obsługa linku
   {? _link<>''
   ||
::    Ustalenie parametrów na podstawie przekazanego rekordu
      _kz_ref:='';
      {? ref_tab(_link)=FAKS
      || _oddz:=exec('FindAndGet','#table',FAKS,_link,,"ODDZ",'');
         _rok:=exec('FindAndGet','#table',FAKS,_link,,"AR",0);
         _mc:=exec('FindAndGet','#table',FAKS,_link,,"AM",0);
         _sts:=exec('FindAndGet','#table',FAKS,_link,,"STS",null())
      |? ref_tab(_link)=FAP
      || _kz_ref:=exec('FindAndGet','#table',FAP,_link,,"exec('FindAndGet','#table',@.FAKS,FAKS,,\"KZ\",'')",'');
         {? _kz_ref=''
         || _oddz:=exec('FindAndGet','#table',FAP,_link,,"exec('FindAndGet','#table',@.FAKS,FAKS,,\"ODDZ\",'')",'');
            _rok:=exec('FindAndGet','#table',FAP,_link,,"exec('FindAndGet','#table',@.FAKS,FAKS,,\"AR\",0)",0);
            _mc:=exec('FindAndGet','#table',FAP,_link,,"exec('FindAndGet','#table',@.FAKS,FAKS,,\"AM\",0)",0);
            _sts:=exec('FindAndGet','#table',FAP,_link,,"exec('FindAndGet','#table',@.FAKS,FAKS,,\"STS\",null())",null())
         || _oddz:=exec('FindAndGet','#table',FAKS,_kz_ref,,"ODDZ",'');
            _rok:=exec('FindAndGet','#table',FAKS,_kz_ref,,"AR",0);
            _mc:=exec('FindAndGet','#table',FAKS,_kz_ref,,"AM",0);
            _sts:=exec('FindAndGet','#table',FAKS,_kz_ref,,"STS",null())
         ?}
      |? ref_tab(_link)=FAKZ
      || _oddz:=exec('FindAndGet','#table',FAKZ,_link,,"ODDZ",'');
         _rok:=exec('FindAndGet','#table',FAKZ,_link,,"AR",0);
         _mc:=exec('FindAndGet','#table',FAKZ,_link,,"AM",0);
         _sts:=exec('FindAndGet','#table',FAKZ,_link,,"exec('FindAndGet','#table',@.STS,STS,,\"ref()\",null())",null())
      ?};
      _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);

::    Wykluczenie dostępu na podstawie uprawnień do danych - oddziały i stanowiska sprzedaży
      {? exec('spr_upr','users','ODDZ',_oddz_ref) & {? _sts=null() || 1 || exec('spr_upr','users','STS',_sts) ?}
      ||
::       Ustalenie parametrów sesji
         __PARSES.setVal('OddzialLogProd',_oddz);
         _args:=__PARSES.args('OkresRok');
         _args.OBSZAR:='LSP';
         _args.AR:=_rok;
         _args.AM:=_mc;
         __PARSES.setVal('OkresRok',_args);
         {? _sts=null()
         ||
            __PARSES.setVal('StSprzedazy',_sts,1)
         ||
            __PARSES.setVal('StSprzedazy',_sts)
         ?};

::       Znalezienie i wyświetlenie obszaru roboczego w kontekście przekazanego rekordu
         {? ref_tab(_link)=FAKS & FAKS.seek(_link)
         || AreaTitle.setTabWin(FAKS,~~);
            AreaTitle.setTitle();
            FILTER.FAKS:=FAKS.ref();
            FAKS.select(,1,5,,'WER',,1)
         |? ref_tab(_link)=FAP & FAP.seek(_link)
         || AreaTitle.setTabWin(FAKS,~~);
            AreaTitle.setTitle();
            FILTER.FAKS:=FAP.FAKS;
            {? 0 & FUN.ask('Pozycje w zakładce?'@)
            || FAKS.select(,1,5,,'POZ')
            || {? (_kz_ref='' & FAKS.seek(FAP.FAKS)) | (_kz_ref<>'' & FAKS.seek(_kz_ref))
               || _env.LINK.FORMULA:=
                      $('{? FAKS.ref() || params_exec(\'faks_pozycje_wer\',\'faktury_nag\',,\'%1\') ?}'[_link]);
                  FAKS.select(,1,5,,'WER',,1)
               ?}
            ?}
         |? ref_tab(_link)=FAKZ & FAKZ.seek(_link)
         || AreaTitle.setTabWin(FAKS,~~);
            AreaTitle.setTitle();
            FILTER.FAKS:=null();
            FAKS.select(,1,5,,'ZLF',,1)
         || FUN.info('Nie odnaleziono dokumentu %1.'@[exec('record','#to_string',_link)])
         ?}
      ?}
::======================================================================================================================

:: Standardowe działanie
   || AreaTitle.setTabWin(FAKS,~~);
      AreaTitle.setTitle();
      FILTER.FAKS:=FAKS.ref();
      FAKS.select(,,,,'WER')
   ?};

   _env.doneObszar()
?}


\lsp_fak_var
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła inicjująca zmienne obszaru LSP_FAK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init_fak','lsp');
BEER.NAWIG:='S'


\lsp_fak_filtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Filtr dokumentów sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_sts:={? ST.STS || '(\''+$ST.STS+'\')' || '' ?};
{? FILTER.STATUS='M' & _sts<>''
||
   FAKS.index('HIDDEN2');
   FAKS.prefix('N',BEER.SZ,ST.AR,ST.AM,ST.STS)
|? _sts<>''
||
   FAKS.index('HIDDEN3');
   FAKS.prefix('N',BEER.SZ,ST.AR,ST.STS)
|? FILTER.STATUS='M' & _sts=''
||
   FAKS.index('HIDDEN');
   FAKS.prefix('N',BEER.SZ,ST.AR,ST.AM)
||
   FAKS.index('HIDDEN1');
   FAKS.prefix('N',BEER.SZ,ST.AR)
?};

_sort:='DF';
_from:='';
_where:="FAKZ.AR=:_a "
         +{? FILTER.STATUS='M' || "and FAKZ.AM=:_b " || "" ?}
         +{? _sts='' || "" || "and FAKZ.STS in :_c" ?};
{? FAKZ.sel_size() || FAKZ.sel_adel() ?};

{? FAKZ.win_sel('?')<>'SEL'
|| FAKZ.prefix();
   FAKZ.f_set(_sort,_from,_where,ST.AR,ST.AM,_sts)
|| {? ST.STS
   ||
      FAKZ.index('STS_DF1');
      FAKZ.prefix(ST.AR,ST.AM,REF.FIRMA,ST.ODDZ,$ST.STS,'T','N')
   ||
      FAKZ.index('DF1');
      FAKZ.prefix(ST.AR,ST.AM,REF.FIRMA,ST.ODDZ,'T','N')
   ?}
?}


\lsp_cen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.00]
:: OPIS: Główna formuła obszaru LSP_CEN - Cenniki sprzedaży
::   WE: [_a] -   jakie cenniki wyswietlać
::            0  - wszystkie
::            1  - kontrahenta
::            2  - grupy kontrahentów
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0) || _a:=0 ?};

_params:=params_get();
_link:={? type_of(_params)>0 & var_pres('LINK',_params)>0 & _params.LINK<>'' || _params.LINK || '' ?};

{? _link<>''
||
   {? ref_tab(_link)=TAR
   ||
::    Ustalenie parametrów na podstawie przekazanego rekordu
      _dobrze:=1;
      _oddz:=exec('FindAndGet','#table',TAR,_link,,"TAR.ODDZ",'');

      {? _oddz=''
      || _dobrze:=0;
         FUN.info('Nie odnaleziono nagłówka cennika: %1'@[exec('record','#to_string',_link)])
      ?};

      {? _dobrze
      || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
         {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
         || _dobrze:=0
         ?}
      ?};

      {? _dobrze
::       Ustalenie parametrów sesji
      || __PARSES.setVal('OddzialLogProd',_oddz)
      || return(0)
      ?}
::======================================================================================================================
   |? ref_tab(_link)=TAP
   ||
::    Ustalenie parametrów na podstawie przekazanego rekordu
      _dobrze:=1;
      _tar:=exec('FindAndGet','#table',TAP,_link,,"TAP.TAR",'');
      _oddz:=exec('FindAndGet','#table',TAR,_tar,,"TAR.ODDZ",'');

      {? _oddz=''
      || _dobrze:=0;
         FUN.info('Nie odnaleziono nagłówka zamówienia: %1'@[exec('record','#to_string',_link)])
      ?};

      {? _dobrze
      || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
         {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
         || _dobrze:=0
         ?}
      ?};

      {? _dobrze
::       Ustalenie parametrów sesji
      || __PARSES.setVal('OddzialLogProd',_oddz)
      || return(0)
      ?}
   ?}
?};

exec('init','lsp');
params_exec('sel_tary','ceny','S',_a);
''


\set_cntx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Ustawia kontekst dla dokumentów sprzedaży wg parametrów
::   WE: _a - rok
::       _b - miesiąc
::       _c - oddział
::----------------------------------------------------------------------------------------------------------------------
ST.AR:=_a;
ST.AM:=_b;
ST.ODDZ:=_c;
ST.ODDZ_KOD:=ST.ODDZ;
ST.ODDZ_REF:=exec('FindInSet','#table','ODDZ','KOD2',ST.ODDZ_KOD,ST.ODDZ_KOD);
exec('open_tabele','open_tab');
exec('init','lsp')


\ofe_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: pozycje ofert
::   WE: [_a] - =0-wolac OFE.r_lock i OFE.r_unlock , =1-wpp
::       [_b] - 1-blokować akcje edycyjne 0(domyślnie)-nie
::  OLD: \ofe_poz/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0  ?};
exec('init_ofe','lsp');
_display:=BPMN.DISPLAY;
{? var_pres('_b')=type_of(0)
|| _display:=_b
?};

_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)>0
|| _link:=_params.LINK
|| _link:=''
?};

:: Obsługa linku
{? _link<>'' & ref_tab(_link)=OFP & OFP.seek(_link)
|| _lspr:=1
|| _lspr:=0 ?};

:: wyszarza akcje
exec('ofp_actions','lsp');

:: zmienna dla stanow magazynowych
HELP.WMAG:=3;
ATR.MJS:='OFP';

{? {? ~_a || OFE.r_lock(1,1,1) || 1 ?}
|| exec('ustaw_ww','eurusd','O'); exec('ust_lw','eurusd',1);
   BEER.MW:='O';
   POMOC.RODZO:='T';
   DISP.SN_OFP:=0;
   DISP.SM_OFP:=0;
   TAZ.RAB_TYP:=OFE.RAB_TYP;
   DISP.RAB:=OFE.RAB;
   DISP.RABO:={? exec('czyrabp','ceny',TAZ.RAB_TYP) || '%' || OFE.WAL().KOD ?};
   DISP.RABZTYP:=exec('rabztyp','ceny',TAZ.RAB_TYP);
   POMOC.MGR:=null;
   PROJZ.PROJEKTY:=OFE.PROJEKTY;
   OFP.cntx_psh();
   OFP.index('OFE_P');
   OFP.prefix(OFE.ref());
   {? BPMN.DISPLAY || OFP.win_sel('WER_DOK') || OFP.win_sel('WER') ?};
   OFP.hdr_sel();
   OFP.hdr_sel(': '+OFE.SYM);
   OFP.win_edit('RED');
   _act:='';
   {? exec('tte_lic','tte')='N' || _act:='Y' ?};
   {? _display | OFE.STAT_REJ='T'
   || _act:='DUPRIMEYFN'+_act+':D'
   ?};
   {? _display | OFE.STAT_REJ<>'N' || _act:='Z'+_act ?};
   {? OFE.A<>'Z' & DISP.ZAM_OFE='' & KHVZ.WIDOKI=0
   || _dictm:=M.win_dict('?');
      _actm:=M.actions('SLO_M','WZR:ZR');
      M.win_dict('SLO_M');
      _dictdkc:=DK_C.win_dict('?');
      DK_C.win_dict('WER');
      {? ~exec('samgdost','mat_atr')
      || OFP.btn_eopt('RED','ATRYB','state=grayed')
      || OFP.btn_eopt('RED','ATRYB','state=normal')
      ?};
::    drag & drop
      _dnd_fml:="exec('renumofp','lsp')";
      OFP.dnd_sel('WER',,'records.OFP',_dnd_fml);
      {? _lspr
      || OFP.select(,1,,_act,,,1)
      || OFP.select(,,,_act) ?} ;
      OFP.dnd_sel('WER',,'records.OFP',"");
      M.win_dict(_dictm);
      DK_C.win_dict(_dictdkc);
      SUM.SUM:=exec('kosztofe','lsp');
      exec('sumofe','lsp',SUM.SUM);
      OFE.put(1)
   || {? KHVZ.WIDOKI
      || _dictm:=M.win_dict('?');
         _actm:=M.actions('SLO_M','WZR:ZR');
         M.win_dict('SLO_M');
         _dictdkc:=DK_C.win_dict('?');
         DK_C.win_dict('WER')
      ?};
      OFP.btn_eopt('RED','ATRYB','state=grayed');
      {? _lspr
      || OFP.select(,1,,'DUPRIZMEYFN:D',,,1)
      || OFP.select(,,,'DUPRIZMEYFN:D')?};
      {? KHVZ.WIDOKI
      || M.win_dict(_dictm);
         M.actions('SLO_M',_actm);
         DK_C.win_dict(_dictdkc)
      ?}
   ?};
   OFP.cntx_pop();
   {? ~_a || OFE.r_unlock() ?}
|| {? FUN.ask('Dokument obsługuje inny użytkownik.\nCzy chcesz zobaczyć kto?'@) & OFE.r_lock()
   || OFE.r_unlock()
   ?};
   0
?};
OFP.hdr_sel();
HELP.WMAG:=0;
1


\ofe_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: wyświetlenie ofert
::   WE: [_a] - zakres ofert - ZAKR.T
::  OLD: \ofe_sel/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='A' ?} || _a:='A' ?};

VAR_DEL.delete('__typofe','__typz_o','__zakr_o');

_kh:=ZAKR.KH;
_han:=ZAKR.H;
ZAKR.T:=ZAKR.AKT:=_a;
ZAKR.WG:='O';
ZAKR.US:=OPERATOR.USER;
ZAKR.KH:=null;
ZAKR.HAN:=null;

BEER.MW:='O';
BEER.SZ:='S';
::exec('ustaw_ww','eurusd','O');
DISP.SN_OFE:=0;
DISP.SZ_OFE:=0;

:: dostępne typy ofert
_p3007:=exec('get','#params',3007,2,OPERATOR.USER);

_sql:="
   select
    T, NAZ, REFERENCE REF, ' ' INF, MOB, ZAP
   from TYPYZAM
    where TYPYZAM.R='O'
    and TYPYZAM.PROJ='N'";
_sql+={? _p3007=''
      || ""
      || " and ':_a' like '% '||TYPYZAM.T||' %' "
      ?};
_sql+=" order by T, T ";
{? _p3007<>'' || _p3007:=' '+_p3007+' ' ?};
__typofe:=sql(_sql,_p3007);
:: okienko typów zamówień
__typz_o:=__typofe.mk_sel('Typy ofert'@,'P',0,'__typofe_win',,,,,'U','T');
__typofe.win_fld(__typz_o,,'T',,,10,,0,'Typ'@);
__typofe.win_fld(__typz_o,,'NAZ',,,32,,0,'Nazwa'@);
__typofe.win_act(__typz_o,0,'Szukaj');
__typofe.win_act(__typz_o,0,'Kolejność');

OFE.win_fml('WER',,'SYM',,'ICON_BEFORE',exec('ofe_sym_ib','lsp'));
_grp:=OFE.grp_make('GRP',,'#ofe_grpokn',,,"exec('exit','zws',_a)");

_win_grp:=OFE.grp_make('',"params_exec('oferty_be','lsp')",'oferty_grp',,,"exec('exit','zws',_a)");
OFE.grp_sel(_win_grp,__typofe,__typz_o,'Oferty'@,"exec('oferty_typ_ar','lsp')"
    ,,,10,"params_exec('oferty_typ_be','lsp')",,,,'maximized');
__typofe.win_sopt(__typz_o,'select_record_checkbox=0');
OFE.tab_splt(_win_grp,,'vertical','ofe','0, 20%');
OFE.grp_sel(_win_grp,,'WER',,,,,,,,,,'maximized','WER',1);
_before:="OFP.cntx_psh();
         {? OFE.SYM<>'' || OFP.find_key(OFE.SYM) ?}";
_after:="OFP.OFE();
         OFP.cntx_pop()";
OFE.grp_sel(_win_grp,OFP,'WER_ALL','Pozycje ofert'@,,,,,_before,_after,,,'maximized');

OFE.win_sel(_win_grp);
OFE.win_edit('RED');
exec('zakr_all','lsp');

AreaTitle.setTabWin(OFE,~~);
AreaTitle.setTitle();
OFE.select();
ZAKR.KH:=_kh;
ZAKR.HAN:=_han;
VAR_DEL.delete('__typofe','__typz_o','__zakr_o');
''


\ofe_sym_ib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła określiająca ikony
::----------------------------------------------------------------------------------------------------------------------
"
   {? OFE.STAT_REJ='T' || exec('zaakceptowany','icon')
   |? OFE.STAT_REJ='Z' || exec('zarejestrowany','icon')
   |? OFE.STAT_REJ='N' || exec('pusta','#icon')
   || exec('pusta','#icon')
   ?}
"


\zakr_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: ustawia zakres na ofertach
::  OLD: \zakr_all/oferty.fml
::   WE: _a - Czy z obszaru Projekty
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=0 ?};

{? _a
||
   ZAKR.T:=ZAKR.AKT;
   {? OFE.f_active() || OFE.f_clear() ?};
   OFE.index('SYM');
   OFE.prefix();
   _prf:='OFE.ODDZ=\'%1\' and '[ST.ODDZ];
   _prf+={? ZAKR.T<>'W'
         || 'OFE.A=\'%1\' and '[ZAKR.T]
         || '' ?};
   _prf+={? ZAKR.KH<>null || 'OFE.KH=\'%1\' and '[$ZAKR.KH] || '' ?};
   _prf+={? ZAKR.HAN<>null || 'OFE.HAN=\'%1\' and '[$ZAKR.HAN] || '' ?};
   _prf+={? ZAKR.US<>null || 'OFE.US=\'%1\' and '[$ZAKR.US] || '' ?};
   _prf+={? ZAKR.TYPYZAM<>null || 'OFE.T=\'%1\' and '[$ZAKR.TYPYZAM] || '' ?};
   _prf+='PROJEKTY.FIRMA=\'%1\'
         and PROJEKTY.RODZIC=\'%2\'
         and PROJEKTY.KOL LIKE \'%3\' and '[$PROJEKTY.FIRMA, PROJEKTY.RODZIC, ((PROJEKTY.POZ_PROJ*3)+PROJEKTY.KOL)+'%'];
   _p3007:=exec('get','#params',3007,2,OPERATOR.USER);
   _prf+={? _p3007=''
         || ''
         || '\' %1\' like \'\% \'||TYPYZAM.T||\' \%\' and '[_p3007]
         ?};
   _prf+='TYPYZAM.R=\'O\'';

   OFE.f_set('SYM','join TYPYZAM using(TYPYZAM.REFERENCE, OFE.T) join PROJEKTY using(PROJEKTY.REFERENCE, OFE.PROJEKTY)',
   _prf)
||
   _ind:='''ODDZ'',,,''T'',,,';
   _ind_p:='''OFE'',''ODDZ'',,''OFE'',''T'',,';
   _prf:='ST.ODDZ,BEER.TYPYZAM,';

   ZAKR.T:=ZAKR.AKT;
   FZ.INFO:='';

   {? ZAKR.T<>'W'
   || _ind+='''A'',,,';_prf+='ZAKR.T,';
      _ind_p+='''OFE'',''A'',,';
      FZ.INFO+={? ZAKR.T='A'
               || 'AKTYWNE  '
               |? ZAKR.T='Z'
               || 'ZARCHIWIZOWANE  '
               || 'WSZYSTKIE  '
               ?}
   ?};
   {? ZAKR.KH<>null | KHVZ.WIDOKI
   || _ind+='''KH'',,,';_prf+='ZAKR.KH,';FZ.INFO+='Kontrahent '+ZAKR.KH().KOD+' | ';
      _ind_p+='''OFE'',''KH'',,'
   ?};
   {? ZAKR.HAN<>null
   || _ind+='''HAN'',,,';_prf+='ZAKR.HAN,';FZ.INFO+='Handlowiec '+ZAKR.HAN().KOD+' | ';
      _ind_p+='''OFE'',''HAN'',,'
   ?};
   {? ZAKR.US<>null
   || _ind+='''US'',,,';_prf+='ZAKR.US,';FZ.INFO+='Użytkownik '+ZAKR.US().KOD+' | ';
      _ind_p+='''OFE'',''US'',,'
   ?};

   _ind+='''SYM'',,,';
   FZ.INFO:=FZ.INFO-2;
   _ind_p+='''OFE'',''SYM'',,';

   _ind:=_ind-1;_prf:=_prf-1;_ind_p:=_ind_p-1;
   OFE.ndx_drop();
   _ndx:=($('OFE.ndx_tmp('''',,'+_ind+')'))();
   OFE.index(_ndx);
   ($('OFE.prefix('+_prf+')'))();
   OFP.ndx_drop();
   _ndx_p:=($('OFP.ndx_tmp('''',,'+_ind_p+')'))();
   OFP.index(_ndx_p);
   ($('OFP.prefix('+_prf+')'))();

   OFE.actions_grayed('WER',{? ZAKR.T='Z' || 'DPUOZAMN(WLYQOXC):D' || '' ?})
?};
''


\ofp_reko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja rekord na pozycjach ofert
::   WY: zwraca kolor rekordu
::  OLD: \ofp_reko/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
DISP.OFP_RAB:=OFP.RAB+OFP.OFE().RAB;
POMOC.RODZO:=POMOC.RODZ:=OFP.M().RODZ;
exec('rekprzed','color','OFP#01')


\ofp_actions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PS] [17.14]
:: OPIS: wyszarza akcje na okienku pozycji oferty
::   WE: [_a] - akronim okna, domyślnie - WER
::----------------------------------------------------------------------------------------------------------------------
_wer:={? var_pres('_a')=type_of('') || _a || 'WER' ?};

OFP.actions_grayed(_wer,{? OFE.STAT_REJ='N' || '' || ':Z' ?});
~~


\kol_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: koloruje rekordy naglowkow ofert
::  OLD: \kol_ofe/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? OFE.ZYSK>0
|| 'OFE#01#01'
|? OFE.ZYSK<0
|| 'OFE#01#02'
|| ''
?}


\kol_ofp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2005]
:: OPIS: koloruje rekordy pozycji ofert
::  OLD: \kol_ofp/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? OFP.MKW>0
|| 'OFP#01#01'
|? OFP.MKW<0
|| 'OFP#01#02'
|| ''
?}


\sumofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: wylicza wartości podsumowujące oferty
::   WE: _a - koszt
::  OLD: \sumofe/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_koszt:=_a;

_mkw:=_wn:=_wb:=0;
_gratisy:='N';

OFP.index('OFE_P');
OFP.prefix(OFE.ref());
{? OFP.first()
||
   TAR_H.cntx_psh();
   {!
   |?
      {? OFP.TAR_H || TAR_H.use(ref_name(OFP.TAR_H)) ?};
      {? _gratisy='N' & OFP.TAR_H().CZY_GRA='T' || _gratisy:='T' ?};
      _wn+=OFP.WN;
      _wb+=OFP.WB;
      _mkw+=OFP.MKW;
      OFP.next()
   !};
   TAR_H.cntx_pop()
?};

OFE.NET:=_wn;
OFE.BRT:=_wb;
OFE.ZYSK:=_mkw - _koszt;
OFE.ZYSK_PR:=({? OFE.NET<>0 || (OFE.ZYSK*100)/OFE.NET || 0 ?})$2;
OFE.GRATISY:=_gratisy


\kosztofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: oblicza koszt oferty
::  OLD: \kosztofe/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
_koszt:=0;

OFK.cntx_psh;
OFK.index('OFE');
OFK.prefix(OFE.ref());
{? OFK.first()
||
   {! |? _koszt+=OFK.WN; OFK.next() !}
?};
OFK.cntx_pop;

_koszt


\disp_ofp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: okienko podgladu dla OFP
::  OLD: \disp_ofp/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
_edit:=OFP.win_edit('?');
_atrmjs:=ATR.MJS;
ATR.MJS:='OFP';
exec('czytadkc','mat_atr',OFP.DK_C,OFP.RDKC);
_ndit:='DISP';
OFP.win_edit(_ndit);
OFP.display();
OFP.win_edit(_edit);
ATR.MJS:=_atrmjs;
~~


\oferta_wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - Wycofanie akceptacji oferty
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_r_lock:=OFE.r_lock(1,1,1);

_upr:=exec('chk_EndOrAcc','#b__box','LSP_OFE_DOFE','LSP_OFE_AAOF');
_res:=1;
{? ~_r_lock
|| {? FUN.ask('Dokument obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@) & OFE.r_lock()
   || OFE.r_unlock()
   ?};
   _res:=0
|? exec('czy_zam','zamowienia')
|| FUN.info('Z oferty zostało wygenerowane zamówienie %1.\nWycofanie akceptacji niemożliwe.'@[ZK_N.SYM]);
   _res:=0
?};

{? _res & _upr
|| _ask:=0;
   {? OFE.STAT_REJ='N'
   || FUN.info('Nie zakończono jeszcze rejestracji oferty.\nWycofanie niemożliwe.'@)
   |? _upr=4 & OFE.STAT_REJ='Z'
   || FUN.info('Oferta nie została zaakceptowana.\nWycofanie akceptacji niemożliwe.'@)
   |? _upr=2 & OFE.STAT_REJ='T'
   || FUN.info('Oferta została zaakceptowana.\nWycofanie zakończenia redakcji niemożliwe.'@)
   |? OFE.T().VALREC<>null() & ~exec('validate','wzorce',OFE.T().VALREC,OFE,OFE.ref())
   || _result:=0
   |? _upr=1 & OFE.STAT_REJ='T'
   || _ask:=FUN.choice('Wybierz jedną z opcji wycofania akceptacji lub zakończenia oferty %1'@[OFE.SYM]
             ,,'Akceptację i &zakończenie','&Akceptację')
   |? (_upr=1 | _upr=2) & OFE.STAT_REJ='Z'
   || _ask:=FUN.ask('Wycofać zakończenie redakcji oferty %1?'@[OFE.SYM])
   |? _upr=4 & OFE.STAT_REJ='T'
   || _ask:=FUN.ask('Wycofać akceptację oferty %1?'@[OFE.SYM]);
      {? _ask || _ask:=2 ?}
   ?};

   {? _ask
   || exec('del_autoadd','dokum',OFE.ref());
      OFE.STAT_REJ:={? _ask=1 || 'N' || 'Z' ?};
      {? OFE.put() || _result:=1 ?}
   ?}
?};
{? _r_lock || OFE.r_unlock() ?};

_result


\oferta_dearch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Oferta - Przywrócenie z archiwum oferty
::----------------------------------------------------------------------------------------------------------------------
_tab:=OFE.sel_aget();
OFE.sel_adel();

{? _tab.size()
|| _ok:=FUN.ask('Ilość ofert do przeniesienia z archiwum: %1.\n'
                '\nCzy kontynuować?'@[$_tab.size()])
|| _ok:=2
?};
{? _ok=2
|| _lock:=OFE.r_lock(1,1,1);

   {? _lock=0
   || {? FUN.ask('Ofertę obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@) & OFE.r_lock()
      || OFE.r_unlock()
      ?}
   |? OFE.A='A'
   || FUN.info('Oferta nie jest w archiwum.\nPrzywrócenie z archiwum niemożliwe.'@)
   |? FUN.ask('Przywrócić ofertę %1 z archiwum?'@[OFE.SYM])
   || OFE.cntx_psh();
      OFE.prefix();
      OFE.A:='A';
      OFE.put(1);
      OFE.cntx_pop()
   ?};
   {? _lock || OFE.r_unlock() ?}
|? _ok=1
|| exec('ini_kom','#message','Przywrócenie ofert'@);
   OFE.cntx_psh();
   _tab.clear();
   {? _tab.first()
   || {!
      |? _lock:=0;
         {? (OFE.clear(); OFE.seek(_tab.REF,)) & (_lock:=OFE.r_lock(1,1,1)) & OFE.A<>'A'
         || OFE.cntx_psh();
            OFE.prefix();
            OFE.A:='A';
            {? OFE.put(1)
            || exec('add_kom','#message'
                 ,'Ofertę %1 przywrócono z archiwum.'@[OFE.SYM]
                 ,1,'Przywrócenie ofert',__lp+=1)
            ?};
            OFE.cntx_pop()
         |? _lock
         || exec('add_kom','#message'
              ,'Ofertę %1 obsługuje inny operator.'@[OFE.SYM]
              ,4,'Przywrócenie ofert',__lp+=1)
         ?};
         {? _lock || OFE.r_unlock() ?};
         _tab.next()
      !}
   ?};
   OFE.cntx_pop();
   exec('end_kom','#message')
?};
exec('ofe_f_rfresh','lsp');
obj_del(_tab)


\ofe_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: wyswietlanie zalacznikow do ofert
::  OLD: \old_zal/oferty.fml
::----------------------------------------------------------------------------------------------------------------------
{? OFE.A='A'
|| exec('sel_dok','dokum','OFE',,,,1)
|| exec('sel_dok','dokum','OFE',0,1,,1)
?};
''


\lsp_fak_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zmiana parametrów pracy obszaru LSP_FAK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
_sts:=ST.STS;
_ar:=ST.AR;
{? __PARSES.editDom('LSP')
|| {? _oddz<>ST.ODDZ || exec('init','lsp') ?};
   exec('lsp_fak_filtr','lsp');
   {? _sts<>ST.STS | _ar<>ST.AR || exec('all_poz','lsp') ?};
   AreaTitle.setTitle()
?}


\lsp_ofe_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zmiana parametrów pracy obszaru LSP_OFE
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
{? __PARSES.editDom('LSP')
|| {? _oddz<>ST.ODDZ || exec('init','lsp') ?};
   exec('zakr_all','lsp');
   AreaTitle.setTitle()
?}


\lsp_zkn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Główna formuła obszaru LSP_ZKN - zamówienia sprzedaży
::----------------------------------------------------------------------------------------------------------------------
exec('init_zkn','lsp');
VAR_DEL.delete('__typzsp','__typz_w','__zakr_w','__ZKP_btnREZER');

:: Środowisko
_env:=obj_new('PX','TAB_DOKUM');
_env.TAB_DOKUM:=tab_tmp(1,'REF','STRING[16]','$DOKUM.ref()');
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)>0
|| _link:=_params.LINK;
   params_set('env',_env,'LINK',_params.LINK)
|| _link:='';
   params_set('env',_env)
?};
{? type_of(_params)>0 & var_pres('PROJ_REF',_params)
|| VAR_DEL.delete('__PROJ_REF');__PROJ_REF:=_params.PROJ_REF
?};
BEER.NAWIG:='ZS';
__ZKP_btnREZER:='';

:: dostępne typy zamówień sprzedaży
_p3004:=exec('get','#params',3004,2,OPERATOR.USER);

_sql:="
   select
    T, NAZ, REFERENCE REF, ' ' INF, MOB, ZAP
   from TYPYZAM
    where TYPYZAM.R='Z'";
_sql+={? _p3004=''
      || ""
      || " and ':_a' like '% '||TYPYZAM.T||' %' "
      ?};
_sql+=" order by T, T ";
{? _p3004<>'' || _p3004:=' '+_p3004+' ' ?};

:: ikonki dla pól
exec('eanx_wys','okienka');

{? _link<>''
||
   {? ref_tab(_link)=ZK_N
   ||
::    Ustalenie parametrów na podstawie przekazanego rekordu
      _dobrze:=1;
      _oddz:=exec('FindAndGet','#table',ZK_N,_link,,"ZK_N.ODDZ",'');

      {? _oddz=''
      || _dobrze:=0;
         FUN.info('Nie odnaleziono nagłówka zamówienia: %1'@[exec('record','#to_string',_link)])
      ?};

      {? _dobrze
      || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
         {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
         || _dobrze:=0
         ?}
      ?};

      {? _dobrze
::       Ustalenie parametrów sesji
      || __PARSES.setVal('OddzialLogProd',_oddz)
      || return(0)
      ?};

      _maska:=exec('FindAndGet','#table',ZK_N,_link,,"name()");
      {? (_maska+2)<>'__'
      ||
         ZK_N.use(_maska);
         ZK_P.use((ZK_P.name()-2)+(_maska+2));
         ZAKR.ARCH_ROK:='20'+(_maska+2)
      ?}
::======================================================================================================================
   |? ref_tab(_link)=ZK_P
   ||
::    Ustalenie parametrów na podstawie przekazanego rekordu
      _dobrze:=1;
      _zkn:=exec('FindAndGet','#table',ZK_P,_link,,"ZK_P.N",'');
      _oddz:=exec('FindAndGet','#table',ZK_N,_zkn,,"ZK_N.ODDZ",'');
      _rok:=exec('FindAndGet','#table',ZK_N,_zkn,,"R",0);
      _mc:=exec('FindAndGet','#table',ZK_N,_zkn,,"M",0);

      {? _oddz='' | _rok=0 | _mc=0
      || _dobrze:=0;
         FUN.info('Nie odnaleziono nagłówka zamówienia: %1'@[exec('record','#to_string',_link)])
      ?};

      {? _dobrze
      || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
         {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
         || _dobrze:=0
         ?}
      ?};

      {? _dobrze
::       Ustalenie parametrów sesji
      || __PARSES.setVal('OddzialLogProd',_oddz)
      || return(0)
      ?};

      _link_p:=exec('FindAndGet','#table',ZK_P,_link,,"N",'');
      _maska:=exec('FindAndGet','#table',ZK_N,_link_p,,"name()");
      _maska_p:=exec('FindAndGet','#table',ZK_P,_link,,"name()");
      {? (_maska+2)<>'__'
      ||
         ZK_N.use(_maska);
         ZK_P.use(_maska_p);
         ZAKR.ARCH_ROK:='20'+(_maska+2)
      ?}
   ?};
::======================================================================================================================
   {? ref_tab(_link)=ZK_N & ZK_N.seek(_link)
   ||
      _kh:=ZAKR.KH;
      _han:=ZAKR.H;
      _us:=OPERATOR.USER;
      ZAKR.US:=null;
      ZAKR.KH:=null;
      ZAKR.HAN:=null
   |? ref_tab(_link)=ZK_P & ZK_P.seek(_link)
   ||
      {? ZK_N.seek(ZK_P.N)
      ||
         _kh:=ZAKR.KH;
         _han:=ZAKR.H;
         _us:=OPERATOR.USER;
         ZAKR.US:=null;
         ZAKR.KH:=null;
         ZAKR.HAN:=null
      ?}
   || FUN.info('Nie odnaleziono zamówienia lub pozycji \n%1.'@[exec('record','#to_string',_link)])
   ?}
?};
__typzsp:=sql(_sql,_p3004);
:: okienko typów zamówień
__typz_w:=__typzsp.mk_sel('Typy zamówień'@,'P',0,'__typzsp_win',,,,,'U','T');
__typzsp.win_fld(__typz_w,,'T',,,8,,0,'Typ'@);
__typzsp.win_fld(__typz_w,,'NAZ',,,32,,0,'Nazwa'@);
__typzsp.win_act(__typz_w,0,'Szukaj');
__typzsp.win_act(__typz_w,0,'Kolejność');

:: okienko zakresu
__zakr_w:=ZAKR.mk_edit('Zakres zamówień'@,,'#zamspr_zakres');
ZAKR.win_ewin(__zakr_w,,'ZAM_Z');
ZAKR.win_ebtn(__zakr_w,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1,edit=0'['&Zmień'@]
 ,"edit_start(); '' ");
exec('ok_esc','#window',ZAKR,__zakr_w);

_win_grp:=ZK_N.grp_make('',"params_exec('zamspr_be','lsp')",'zamspr_grp',,,"exec('exit','zws',_a)");
{? exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_ZPTM')
|| ZK_N.grp_sel(_win_grp,__typzsp,__typz_w,'Zamówienia sprzedaży'@,"exec('zamspr_typ_ar','lsp')"
    ,,,10,"params_exec('zamspr_typ_be','lsp')",,,,'maximized');
   __typzsp.win_sopt(__typz_w,'select_record_checkbox=0');
   ZK_N.tab_splt(_win_grp,,'vertical','zam',',20%');
   ZK_N.grp_sel(_win_grp,,'WERZ',,"exec('zamspr_zk_n_ar','lsp')",,,23,,,,,'maximized','zamspr_zk_n',1);
   ZK_N.tab_splt(_win_grp,'zam','horizontal','info',',66%');
   ZK_N.grp_edit(_win_grp,,'INFZ',,,"exec('zamspr_info_be','lsp')",,, 'maximized');
   ZK_N.grp_sel(_win_grp,M,'STANY_UP','Stany magazynowe'@,"",,,23,"exec('stan_mg_up','magazyn_stan','Z')",,,,'maximized');
   ZK_N.tab_splt(_win_grp,,'vertical','stany',',80%');
   _far:="";
   _fb:="";
   exec('addsmmag','magazyn_stan');
   ZK_N.grp_sel(_win_grp,__smmag,__smmag.win_sel('?'),,_far,,,23,_fb,,,,'maximized');
   __smmag.win_sopt(__smmag.win_sel('?'),'select_record_checkbox=0');
   _far:="";
   _fb:="exec('pr_allZPoz','lsp')";
   _fa:="{? ~ZK_P.sel_size() || ZK_P.f_clear() ?}";
   ZK_N.grp_sel(_win_grp,ZK_P,'ALLZ','Pozycje dokumentów'@,_far,,,23,_fb,_fa,,,'maximized');
   _far:="";
   _fb:="exec('pr_allZReal','lsp')";
   _fa:="exec('done_zaz','zamsiw_rea','ZK_RN');
   {? ~ZK_RN.sel_size() || ZK_RN.f_clear() ?}";
   ZK_N.grp_sel(_win_grp,ZK_RN,'ALLWER','Realizacje zamówień'@,_far,,,23,_fb,_fa,,,'maximized')
|| ZK_N.grp_sel(_win_grp,__typzsp,__typz_w,'Zamówienia sprzadaży'@,"exec('zamspr_typ_ar','lsp')"
    ,,,10,"params_exec('zamspr_typ_be','lsp')",,,,'maximized');
    __typzsp.win_sopt(__typz_w,'select_record_checkbox=0');
   ZK_N.tab_splt(_win_grp,,'vertical','zam');
   ZK_N.grp_sel(_win_grp,,'WERZ',,"exec('zamspr_zk_n_ar','lsp')",,,23,,,,,'maximized_with_title','zamspr_zk_n',1);
   ZK_N.tab_splt(_win_grp,'zam','horizontal','info');
   ZK_N.grp_edit(_win_grp,,'INFZ',,,"exec('zamspr_info_be','lsp')",,, 'maximized');
   _far:="";
   _fb:="exec('pr_allZPoz','lsp')";
   _fa:="{? ~ZK_P.sel_size() || ZK_P.f_clear() ?}";
   ZK_N.grp_sel(_win_grp,ZK_P,'ALLZ','Pozycje dokumentów'@,_far,,,23,_fb,_fa,,,'maximized');
   _far:="";
   _fb:="exec('pr_allZReal','lsp')";
   _fa:="exec('done_zaz','zamsiw_rea','ZK_RN');
   {? ~ZK_RN.sel_size() || ZK_RN.f_clear() ?}";
   ZK_N.grp_sel(_win_grp,ZK_RN,'ALLWER','Realizacje zamówień'@,_far,,,23,_fb,_fa,,,'maximized')
?};
:: Dokumenty Businesslink
exec('winOrd','zbl_dok',_win_grp);
ZK_N.win_sel(_win_grp);

ZK_N.win_fml('WERZ',,'SYM',,'ICON_BEFORE',exec('zknag_sym_ib','zamsiw_nag'));
ZK_N.win_fml('WERZ',,'DP',,'ICON_BEFORE',exec('zknag_tra_ib','zamsiw_nag'));
ZK_N.win_fml('WERZ',,'STAN',,'ICON_BEFORE',exec('zknag_sym_ob','zamsiw_nag'));
ZK_P.win_fml('ALLZ',,'N','SYM','ICON_BEFORE',exec('zknag_sym_ib','zamsiw_nag'));
ZK_P.win_fml('ALLZ',,'N','DP','ICON_BEFORE',exec('zknag_tra_ib','zamsiw_nag'));
ZK_P.win_fml('ALLZ',ZK_N,'STAN',,'ICON_BEFORE',exec('zknag_sym_ob','zamsiw_nag'));

{? __typzsp.size()
||
   exec('init','lsp');
   AreaTitle.setTabWin(ZK_N,~~);
   AreaTitle.setTitle();

   exec('m_icon','tech_common','LSP_ZKN');

   {? _link<>''
   ||
      VAR_DEL.delete('__spr_z_2');
      __spr_z_2:=1;
      {? ref_tab(_link)=ZK_N & ZK_N.seek(_link)
      ||
         ZK_N.select(,1,5,,,,1);
         ZAKR.KH:=_kh;
         ZAKR.HAN:=_han;
         ZAKR.US:=_us
      |? ref_tab(_link)=ZK_P & ZK_P.seek(_link)
      ||
         {? ZK_N.seek(ZK_P.N)
         ||
            VAR_DEL.delete('__spr_z');
            __spr_z:=1;
            ZK_N.select(,1,5,,,'Y',1);
            ZAKR.KH:=_kh;
            ZAKR.HAN:=_han;
            ZAKR.US:=_us
         ?}
      ||
         FUN.info('Nie odnaleziono zamówienia lub pozycji \n%1.'@[exec('record','#to_string',_link)])
      ?}
   || ZK_N.select()
   ?}
|| FUN.info('Brak uprawnionych typów zamówień sprzedaży.\n'
           'Przegląd zamówień sprzedaży nie jest możliwy.'@)
?};
VAR_DEL.delete('__typzsp','__typz_w','__zakr_w');
~~


\zamspr_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Przed obsługą dla okienka grupowego
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
__typzsp.first();
:: Ustawienie się na domyślnym typie zamówienia
_tr:=exec('get','#params',3005,2,OPERATOR.USER);
__typzsp.find_key(_tr,_tr);
grp_disp(__typzsp,__typz_w);
exec('zakr_get','zamsiw_nag');
:: grp_edisp(ZAKR,__zakr_w);
BEER.TYPYZAM:=exec('FindAndGet','#table',TYPYZAM,__typzsp.REF,,,null());
exec('zakr_set','zamsiw_nag',0);
~~


\zamspr_typ_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Po odświeżeniu dla __typzsp
::----------------------------------------------------------------------------------------------------------------------
_old:=BEER.TYPYZAM;
BEER.TYPYZAM:=exec('FindAndGet','#table',TYPYZAM,__typzsp.REF,,,null());
{? BEER.TYPYZAM=null()
|| ZK_N.index('GRP_KEY'); ZK_N.prefix('xxx')
|| exec('zakr_set','zamsiw_nag',_old=BEER.TYPYZAM)
?};
grp_disp(ZK_N,'WERZ');
grp_edisp(ZK_N,'INFZ');
~~


\zamspr_typ_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Przed obsługą dla __typzsp
::----------------------------------------------------------------------------------------------------------------------
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)>0 & __spr_z_2
|| _link:=_params.LINK;
   __spr_z_2:=0
|| _link:=''
?};

{? _link<>''
|| ref_tab(_link)=ZK_N & ZK_N.seek(_link);
   _filtrtyp:=ZK_N.T
|| _filtrtyp:=exec('FindAndGet','#table',ZK_N,FILTER.ZK_N,,"T",null())
?};

{? BEER.TYPYZAM<>_filtrtyp
|| _ref:=__typzsp.ref();
   {? __typzsp.f_active()
   || {? __typzsp.f_first()
      || {!
         |? {? __typzsp.REF=$_filtrtyp
            || _ref:=null();
               0
            || __typzsp.f_next()
            ?}
         !}
      ?};
      {? _ref<>null() || __typzsp.f_seek(_ref) ?}
   || {? __typzsp.first()
      || {!
         |? {? __typzsp.REF=$_filtrtyp
            || _ref:=null();
               0
            || __typzsp.next()
            ?}
         !}
      ?};
      {? _ref<>null() || __typzsp.seek(_ref) ?}
   ?}
?};
BEER.TYPYZAM:=exec('FindAndGet','#table',TYPYZAM,__typzsp.REF,,,null());
~~


\zamspr_zakr_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Po odświeżeniu dla ZK_N
::----------------------------------------------------------------------------------------------------------------------
exec('zakr_set','zamsiw_nag',0);
grp_disp(ZK_N,'WERZ');
~~


\zamspr_zk_n_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Po odświeżeniu dla ZK_N
::   WE: [_a] - akronim okienka
::       [_b] - 1-grp_disp (domyślnie) 0-nie
::----------------------------------------------------------------------------------------------------------------------
_win:={? var_pres('_a')<>type_of('') | _a='' || cur_win(1,1) || _a ?};
_disp:={? var_pres('_b')<>type_of(0) || _b:=1 || _b ?};
_actions:='';
_sel:=ZK_N.sel_size();
{? {? ZK_N.f_active() || {? _disp || _sel | ZK_N.f_get() || 1 ?} || ZK_N.get() ?}
|| DISP.CTRLMG:={? ZK_N.E='2' | ZK_N.MG<>null() || 1 || exec('get','#params',3541,2,OPERATOR.USER)<>'T' ?};
   ZK_N.actions_grayed(_win,'');
   _msk:=ZK_N.name()+2;
   _czykpl:={? ZK_N.E<>'2' & (';TP'*exec('get','#params',600702,2))>1 || '' || 'W' ?};
   _inftra:={? ~exec('czyTRANSPORT','transport_wspolne',ZK_N.uidref()) || 'N(B(I))' || '' ?};
   _czydtr:={? ZK_N.STAT_REJ='T' & ZK_N.STAN<>'ZRE' & ZK_N.LIM<>'T'
             & exec('czyTR_NZL','transport_wspolne',ZK_N.uidref())
            || ''
            || 'N(B(T))'
            ?}+
            {? exec('czyTR_NZL','transport_wspolne',ZK_N.uidref(),1) || 'U' || '' ?};
   _czywyc:={? _msk<>'__'
             | ZK_N.STAT_REJ='T' & ~exec('chk_role','#b__box',OPERATOR.USER,'LSP_ZKN_ERZA')
             | ZK_N.STAT_REJ='Z' & ~exec('chk_role','#b__box',OPERATOR.USER,'LSP_ZKN_DRZK')
             | ZK_N.STAT_REJ='N'
            || 'W'
            || ''
            ?};
   _czyrea:={? exec('FindInSet','#table','ZK_RN','ZAM',ZK_N.ref())=null() & ZK_N.STAT_REJ<>'A' || 'I' || '' ?};
   _actions:=_czyrea+
      {? ZK_N.T().MOB<>'T' || 'W'  || '' ?}+
      {? ZK_N.IDABSTOR=0 & ZK_N.BASLIN_O=0 || 'N(J)' ?}+
      {? ZK_N.STAT_REJ<>'T'
         | ~((#ZK_N.E)%*2)
                           || _czykpl+{? DISP.CTRLMG || 'R' || '' ?} || '' ?}+
      {? _msk<>'__'        || 'RFDPUZAWEN(PAŻR(J)ODFWNCKGYEŚÓB(TIR)):DE'
      |? ZK_N.STAT_REJ='N' || 'WAN(H'+_czywyc+'B(RH))'+_czydtr+_inftra
      |? ZK_N.STAT_REJ='Z' || 'WUZT(D)N(H'+_czywyc+'B(RH))'+_czydtr+_inftra
      |? ZK_N.STAT_REJ='T' || 'AUZN(H'+_czywyc+')'+_czydtr+_inftra
      |? ZK_N.STAT_REJ='A' || 'PAUZWN(GFHWNPJODŻB(R))'+_czydtr+_inftra
      || ''
      ?};
   {? _sel=0 & ZK_N.STAN='ZRE'
   || _actions+=_actions+'N(A)'+_czydtr+_inftra
   ?}
|? ~ZK_N.size()
|| ZK_N.actions_grayed(_win,'');
   _msk:=ZK_N.name()+2;
   _actions:=
      {? _msk<>'__'        || 'RIŻFDPUZAWEN(PAŻR(J)ODFWNCKGYEŚB(TIRH)):DE'
      || ''
      ?}
?};

{? ~exec('list_nd_without_faks','magdok_wspolne','Zamówienie',0).first()
|| _actions:='F'+_actions
?};

_pactions:=Plugin.run('SEL_ACTIONS_001','ZK_N',_actions);
ZK_N.actions_grayed(_win,{? _pactions<>'*' || _pactions || _actions ?});

{? _disp || grp_edisp(ZK_N,'INFZ') ?};
~~


\zamspr_info_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Przed obsługą dla ZK_N INFO
::----------------------------------------------------------------------------------------------------------------------
_win_red:='INFZ';
_enable:={? {? ZK_N.f_active() || ZK_N.f_size() || ZK_N.size() ?} || 'enable=1' || 'enable=0' ?};
ZK_N.efld_opt(_win_red,_enable,,'KH');
ZK_N.efld_opt(_win_red,_enable,,'HAN');
ZK_N.efld_opt(_win_red,_enable,,'US');
ZK_N.efld_opt(_win_red,_enable,,'PR');
~~


\zam_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: ustalenie zakresu dla zamowien
::  OLD: \zam_zakr/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_zakr_w:=ZAKR.mk_edit('Zakres zamówień'@,,'#zamspr2zakres');
ZAKR.win_ewin(_zakr_w,,'ZAM_Z');
exec('ok_esc','#window',ZAKR,_zakr_w);

exec('ustaw_okna','kontrahent');
ZAKR.win_edit(_zakr_w);
{? ZAKR.edit()
|| exec('zamspr_zakr_ar','lsp')
::   grp_edisp(ZAKR,__zakr_w)
?}
::|| ZAKR.win_edit('ZAM_W');
::   {? ZAKR.edit()
::   || OZ.get(); OZ.ZK_WYD:=ZAKR.WYD; OZ.ZK_ZL:=ZAKR.ZL; OZ.ZK_WUS:={? ZAKR.US=null || 2 || 1 ?}; OZ.put(1);
::      exec('zakr_ind','zk')
::   || ZAKR.ZLSYM:=''
::   ?}
::?}


\zkn_parametry
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zmiana parametrów pracy obszaru LSP_ZKN
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
{? __PARSES.editDom('LSP')
|| {? _oddz<>ST.ODDZ
   || exec('init','lsp');
      exec('zakr_set','zamsiw_nag',0);
      AreaTitle.setTitle();
      grp_disp(ZK_N,'WERZ')
   || AreaTitle.setTitle()
   ?};
   ''
?}


\lsp_int
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Główna formuła obszaru LSP_INT - Deklaracje INTRASTAT wywóz
::----------------------------------------------------------------------------------------------------------------------
:: Funkcja wołająca kartotekę materiałową w wersji 12.41
::exec('intrast','ist01');
_env:=exec('env','intrastat');

{? _env.initObszar('Wywóz')
|| _env.winMain();
   params_set('env',_env);
   AreaTitle.setArea('LSP_INT');
   AreaTitle.setTabWin(IST,_env.WIN_MAIN);
   AreaTitle.setTitle();
   _switch:=IST.select();
   _env.doneObszar();
   {? _switch || exec('lmg_int','lmg') ?}
?}


\faks_far_wers
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła przed dla okienka grupowego
::   WE: [_a] - obszar roboczy
::----------------------------------------------------------------------------------------------------------------------
_obszar_rob:={? var_pres('_a')=type_of('') || _a || '' ?};

_sel_size:=FAKS.sel_size();
{? _sel_size | {? FAKS.f_active() || FAKS.f_get() || FAKS.get() ?}
|| FAKS.actions_grayed(cur_win(1,1),'');
   _id_act:=exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF);
   _czy_z_ok:=exec('czy_z_ok','okresy',-2,0,FAKS.AR,FAKS.AM);
   _czy_zokr:=exec('czy_z_ok','okresy',-2,0,ST.AR,ST.AM);
   _czyanu:={? ~exec('chk_role','#b__box',OPERATOR.USER,'LSP_FAK_EANU') || 'N(AĘ)' || '' ?};
   _czywyc:=
      {? ~_sel_size
            &
         (FAKS.ZAK='T'
          | FAKS.STAT_REJ='T' & ~exec('chk_role','#b__box',OPERATOR.USER,'LSP_FAK_EASP')
          | FAKS.STAT_REJ='Z' & ~exec('chk_role','#b__box',OPERATOR.USER,_id_act)
          | FAKS.STAT_REJ='N')
         || 'N(W)'
         || ''
         ?};
   _czydel:={? _sel_size || '' || 'U' ?};
   _czyakc:={? _sel_size || '' || 'A' ?};
   _czyzak:={? _sel_size || '' || 'Z' ?};
   _wydaFaks:=exec('wydaFaks','faktury_wspolne');
   _notNOTA:=exec('nKorKHODB','faktury_wspolne');

   FAKS.T();
   _actions:=
      {? ~_czy_z_ok        || {? ~_czy_zokr || 'D' || '' ?}+'ZAN(MW)'
                           || ''
      ?}+
      {? FAKS.STAT_REJ='A' || 'D(R)PMGN(IJDCYAOKL(R))'+_czydel+_czyakc+_czyzak+_czywyc
      |? FAKS.STAT_REJ='N' || 'N(DKOL(RH)AĘ)D(R)'+_czywyc+_czyanu+_czyakc
      |? FAKS.STAT_REJ='Z' || 'PN(KOCL(RH)Ę)D(R)'+_czywyc+_czyanu+_czydel+_czyzak
      |? FAKS.STAT_REJ='T'
         & FAKS.ZAK='N'
      || {? FAKS.WHERE='L' || 'PAZN(DCL(RH)Ę)'+_czywyc+_czyanu+_czydel
                           || 'PN(CĘ)'+_czyanu+_czydel+_czyakc+_czyzak
         ?}
      |? FAKS.STAT_REJ='T'
         & FAKS.ZAK='T'    || 'PN(DCĘ)'+_czyakc+_czyzak+_czywyc+_czyanu+_czydel
      || ''+_czyanu
      ?}+
      {? FAKS.WHERE='K' || ''
                        || 'N(T)'
      ?}+
      {? FAKS.WHERE='K'
         | FAKS.WHERE='N'  || ''
                           || 'M'
      ?}+
      {? FAKS.T & TYPYSP.KOR='Z' || 'N(DNI)'
                                 || ''
      ?}+
      {? FAKS.T & TYPYSP.HIS='T' || 'N(C)'
                                 || ''
      ?}+
      {? FAKS.STAT_REJ<>'T'
        | FAKS.WHERE<>'G'
        | FAKS.T & TYPYSP.KOR='T'
        | exec('onlyUsl','faktury_wspolne',FAKS.ref())
        | _wydaFaks                             || 'G'
                                                || ''
      ?}+
      {? FAKS.STAT_REJ<>'T'
         | FAKS.T & TYPYSP.KOR='N'
         | FAKS.T & TYPYSP.KOR='Z'
         | FAKS.WHERE='P' & FAKS.T & TYPYSP.HIS='N'
         | _wydaFaks
         | ~exec('nKorKHODB','faktury_wspolne') || 'N(M)'
                                                || ''
      ?}+
      {? exec('czy_z_ok','okresy',-1,0,ST.AR,ST.AM)=0   || 'D(C)'
                                                            || ''
      ?}+
      {? FAKS.T & TYPYSP.PROJZAKR=exec('projzakr_nie_dotyczy','projekty') || 'N(J)' || '' ?}+
      {? FAKS.OBI_POW<>'' | FAKS.STAT_REJ='A' | FAKS.STAT_REJ='N'
      || 'N(G)'
      || ''
      ?}+
:: Dane do intrastat, akceptacja/wycofanie akceptacji danych do intrastat
      {? FAKS.STAT_REJ='A' | FAKS.T & (TYPYSP.UE<>'T' | TYPYSP.KOR='Z')
      || 'N(E)'
      |? FAKS.IST_TYP='' | FAKS.INTRA='T' | FAKS.AKC<>'T'
      || 'N(E(AW))'
      |? FAKS.INTRAKC='T'
      || 'N(E(A))'
      || 'N(E(W))'
      ?}+
:: KSeF
      {? _sel_size=0 & FAKS.EDOKUMEN<>'T'
      || 'F(AUWP)'
      || ''
      ?};
      _actions0:=
         {? ~_czy_zokr  || 'D'
                        || ''
         ?};
   FAKS.actions_grayed(cur_win(1,1),_actions+':'+_actions0)
|| FAKS.actions_grayed(cur_win(1,1),'');
   _czy_z_ok:=exec('czy_z_ok','okresy',-2,0);
   _actions0:=
      {? ~_czy_z_ok  || 'D'
                     || ''
      ?};
   FAKS.actions_grayed(cur_win(1,1),':'+_actions0)
?}


\lsp_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła sprawdzająca licencję dla obszaru LSP
::   WY: 'T' - jest licencja, 'N' - nie ma licencji
::----------------------------------------------------------------------------------------------------------------------
_result:='N';
_domain:=exec('domain_ref','#b_domain','LSP');
{? _domain<>null()
|| {? exec('lic','#b_domain',_domain)=1
   || _result:='T'
   ?}
?};
_result


\disp_all_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: wyświetlenie całego dokumentu
::----------------------------------------------------------------------------------------------------------------------
FAKS.cntx_psh();
FAP.cntx_psh();
_refn:=FAP.FAKS;
_refp:=FAP.ref();
FAP.FAKS().SYM;
_red:=exec('faks_win_edit_set','faktury_nag',1);

FAP.index('FAP');
FAP.prefix(_refn);
FAP.seek(_refp);
exec('dselWithNag','okienka','FAP','WER_DOK','FAKS',_red);
FAKS.cntx_pop();
FAP.cntx_pop();
~~


\disp_all_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: wyświetlenie całego zamówienia
::   WE: [_a] - uidref ZK_N-a
::----------------------------------------------------------------------------------------------------------------------
_uid:={? var_pres('_a')=type_of('') & (+_a)=48 || _a || '' ?};
_refn:=null();
_refp:=null();

ZK_N.cntx_psh();
ZK_P.cntx_psh();
{? _uid<>''
|| _msk:=form(8+(_uid+16));
   ZK_N.use(_msk);
   ZK_P.use('zkpoz'+(_msk+3));
   ZK_N.prefix();
   {? ZK_N.seek(_uid)
   || _refn:=ZK_N.ref();
      _refp:=exec('FindInSet','#table','ZK_P','NAG',ZK_N.ref(),,,,,null());
      ZK_N.SYM
   ?}
|| _refn:=ZK_P.N;
   _refp:=ZK_P.ref();
   ZK_P.N().SYM
?};
_beer:=BEER.ZK_N;
_edit:=ZK_N.win_edit('?');
_sele:=ZK_P.win_sel('?');
_red:=ZK_N.mk_edit('',,'zamspr_xxx');
ZK_N.win_ewin(_red,,'RED');
ZK_N.win_edit(_red);
ZK_P.win_sel('ZAMZ_DOK');
exec('set_efld_opt','zamsiw_nag');


ZK_P.index('NAG');
ZK_P.prefix(_refn);
ZK_P.seek(_refp);
BEER.ZK_N:=_refn;
exec('dselWithNag','okienka','ZK_P','ZAMZ_DOK','ZK_N',_red);
ZK_N.win_edit(_edit);
ZK_P.win_sel(_sele);
ZK_N.cntx_pop();
ZK_P.cntx_pop();
BEER.ZK_N:=_beer;
~~


\all_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: wyświetlenie wszystkich pozycji sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_fap:={? FAKS.T().KOR<>'Z'
      || exec('FindInSet','#table','FAP','FAP',1,FAKS.ref())
      || exec('FindInSet','#table','FAP','KZ',$FAKS.ref())
      ?};
FAP.index('ALL');
{? FAP.sel_size() || FAP.sel_adel() ?};
FAP.f_clear();
FAP.f_set('FAKS(DW),FAKS(SYM),POZ','join FAKS'
 ,'FAKS.SZ=\'S\''+{? ST.STS<>null() || ' and FAKS.STS=:_a' || '' ?},ST.STS);
{? FAP.f_active() || FAP.f_seek(_fap) || FAP.seek(_fap) ?};
~~


\lsp_lgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Główna formuła dodatkowych analiz - historia rezerwacji
::----------------------------------------------------------------------------------------------------------------------
_histrez:=exec('chk_role','#b__box',OPERATOR.USER,'LSP_LOG_PLRZ');
VAR_DEL.delete('__usrz','__mtrz','__anrz','__wusrz','__wmtrz','__wanrz','__akrez');
VAR_DEL.delete('__tyrz','__marz','__wtyrz','__wmarz','__warez');

{? _histrez
|| __akrez:=0;
   exec('arezSPRLOG','faktury_wspolne',-1);
   exec('arezREZ','faktury_wspolne');
   _rez:=__tyrz.size()>1;
   _tab:=__anrz;
   _win_grp:=_tab.grp_make(,"exec('init_usrz','faktury_wspolne',,,1)",'#arez_sprlog');
   _tab.grp_sel(_win_grp,REZ,__warez,'Aktualne rezerwacje'@
       ,"exec('pd_arez','faktury_wspolne')",,,30
       ,"exec('pr_arez','faktury_wspolne')",,,1,'maximized_with_title','REZA',1);
   _tab.grp_sel(_win_grp,__anrz,__wanrz,'Historia rezerwacji'@
       ,"exec('pd_anrz','faktury_wspolne')",,,30
       ,"exec('pr_anrz','faktury_wspolne')",,,1,'maximized_with_title','ANAR');
   _tab.win_sel(_win_grp);
   _tab.first();
   _tab.select()
?};
VAR_DEL.delete('__usrz','__mtrz','__anrz','__wusrz','__wmtrz','__wanrz','__akrez');
VAR_DEL.delete('__tyrz','__marz','__wtyrz','__wmarz','__warez');
~~


\lsp_lgs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Główna formuła dodatkowych analiz - sprzedaż utracona
::----------------------------------------------------------------------------------------------------------------------
_sprutra:=exec('chk_role','#b__box',OPERATOR.USER,'LSP_LOG_PSUT');
VAR_DEL.delete('__ussu','__mtsu','__ansu','__wussu','__wmtsu','__wansu');

{? _sprutra
|| exec('azkpSPRLOG','faktury_wspolne');
   _tab:=__ansu;
   _win_grp:=_tab.grp_make('Sprzedaż utracona'@,"exec('init_ussu','faktury_wspolne')",'#azkp_sprlog');
   _tab.grp_sel(_win_grp,__ansu,__wansu,
       ,"exec('pd_ansu','faktury_wspolne')",,,30
       ,"exec('pr_ansu','faktury_wspolne')",,,,'maximized_with_title','ANAS',1);

   _tab.win_sel(_win_grp);
   _tab.first();
   _tab.select()
?};
VAR_DEL.delete('__ussu','__mtsu','__ansu','__wussu','__wmtsu','__wansu');
~~


\pr_allZPoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.02]
:: OPIS: przed dla wszystkich pozycji zamówień
::----------------------------------------------------------------------------------------------------------------------
_zkp:=exec('FindInSet','#table','ZK_P','NAG',1,ZK_N.ref());
_tref:=__typzsp.ref();
_in:='(';
{? __typzsp.f_active()
|| {? __typzsp.f_first() || {! |? _in+='\''+__typzsp.REF+'\','; __typzsp.f_next() !}; __typzsp.f_seek(_tref) ?}
|| {? __typzsp.first() || {! |? _in+='\''+__typzsp.REF+'\','; __typzsp.next() !}; __typzsp.seek(_tref) ?}
?};
_in:=(_in-1)+')';
{? (+_in)=1 || _in:='(\'\')' ?};
ZK_P.prefix();
{? ZK_P.sel_size() || ZK_P.sel_adel() ?};
ZK_P.f_clear();
ZK_P.f_set('N(DP),N(SYM),POZ'
   ,'join ZK_N using(ZK_N.REFERENCE,ZK_P.N)'
   ,'\"ZK_P\".RODZ=\'Z\' and \"ZK_N\".T in '+_in);
ZK_P.f_seek(_zkp);
~~


\sum_kolumn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [21.14]
:: OPIS: Ustawienie dostępności sumowania kolumn wg dostępu do wartości sprzedaży, zakupu, magazynowych
::   WE: [_a] - S - sprzedaż (domyślnie), Z - zakup
::----------------------------------------------------------------------------------------------------------------------
_rodz:='S';
{? var_pres('_a')>0 & (_a='S' | _a='Z')
|| _rodz:=_a
?};
{? _rodz='Z'
|| _moz:=exec('upr_cz','ceny')
|| _moz:=exec('upr_cs','ceny')
?};
{? ~_moz
|| FAKS.fld_attr('BRTW',0,2);
   FAKS.fld_attr('BRUTTO',0,2);
   FAKS.fld_attr('KW_ZAL',0,2);
   FAKS.fld_attr('NETTO',0,2);
   FAKS.fld_attr('NETW',0,2);
   FAKS.fld_attr('VAT',0,2);
   FAP.fld_attr('BWAL',0,2);
   FAP.fld_attr('BWAL_P',0,2);
   FAP.fld_attr('CB',0,2);
   FAP.fld_attr('CN',0,2);
   FAP.fld_attr('CPR',0,2);
   FAP.fld_attr('CWAL',0,2);
   FAP.fld_attr('RAB',0,2);
   FAP.fld_attr('RABK',0,2);
   FAP.fld_attr('VWAL',0,2);
   FAP.fld_attr('VWAL_P',0,2);
   FAP.fld_attr('WB',0,2);
   FAP.fld_attr('WF',0,2);
   FAP.fld_attr('WN',0,2);
   FAP.fld_attr('WS',0,2);
   FAP.fld_attr('WWAL',0,2);
   FAP.fld_attr('WWAL_P',0,2);
   FAKSV.fld_attr('WB',0,2);
   FAKSV.fld_attr('WB_R',0,2);
   FAKSV.fld_attr('WB_Z',0,2);
   FAKSV.fld_attr('WN',0,2);
   FAKSV.fld_attr('WN_R',0,2);
   FAKSV.fld_attr('WN_Z',0,2);
   FAKSV.fld_attr('WV',0,2);
   FAKSV.fld_attr('WV_R',0,2);
   FAKSV.fld_attr('WV_Z',0,2);
   FAKSV.fld_attr('WW',0,2);
   FAKSV.fld_attr('WWN',0,2);
   FAKSV.fld_attr('WWN_R',0,2);
   FAKSV.fld_attr('WWN_Z',0,2);
   FAKSV.fld_attr('WWV',0,2);
   FAKSV.fld_attr('WWV_R',0,2);
   FAKSV.fld_attr('WWV_Z',0,2);
   FAKSV.fld_attr('WW_R',0,2);
   FAKSV.fld_attr('WW_Z',0,2)
?}


\pr_allZReal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: przed dla wszystkich realizacji zamówień
::----------------------------------------------------------------------------------------------------------------------
exec('init_zaz','zamsiw_rea','ZK_RN');
_zkrn:=exec('FindInSet','#table','ZK_RN','ZAM',ZK_N.ref());
_tref:=__typzsp.ref();
_in:='(';
{? __typzsp.f_active()
|| {? __typzsp.f_first() || {! |? _in+='\''+__typzsp.REF+'\','; __typzsp.f_next() !}; __typzsp.f_seek(_tref) ?}
|| {? __typzsp.first() || {! |? _in+='\''+__typzsp.REF+'\','; __typzsp.next() !}; __typzsp.seek(_tref) ?}
?};
_in:=(_in-1)+')';
{? (+_in)=1 || _in:='(\'\')' ?};
ZK_RN.prefix();
{? ZK_RN.sel_size() || ZK_RN.sel_adel() ?};
ZK_RN.f_clear();
ZK_RN.f_set('N(NR),DR,TR'
   ,'join ZK_N using(ZK_N.REFERENCE,ZK_RN.N)'
   ,'\"ZK_N\".T in '+_in);
ZK_RN.f_seek(_zkrn);
~~


\oferty_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Przed obsługą dla okienka grupowego - oferty
::----------------------------------------------------------------------------------------------------------------------
_params:=params_get();
params_set(params_get());
{? type_of(_params)>0 & var_pres('LINK',_params)>0
|| _link:=_params.LINK
|| _link:=''
?};
{? _link='' | __typofe.ref()=null()
||
   __typofe.first();
:: Ustawienie się na domyślnym typie zamówienia
   _tr:=exec('get','#params',3009,2,OPERATOR.USER);
   __typofe.find_key(_tr,_tr)
?};
grp_disp(__typofe,__typz_o);
BEER.TYPYZAM:=exec('FindAndGet','#table',TYPYZAM,__typofe.REF,,,null());
exec('zakr_all','lsp');
~~


\oferty_typ_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Po odświeżeniu dla __typofe
::----------------------------------------------------------------------------------------------------------------------
_old:=BEER.TYPYZAM;
BEER.TYPYZAM:=exec('FindAndGet','#table',TYPYZAM,__typofe.REF,,,null());
exec('zakr_all','lsp');
grp_disp(OFE,'WER');
~~


\oferty_typ_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Przed obsługą dla __typofe
::----------------------------------------------------------------------------------------------------------------------
BEER.TYPYZAM:=exec('FindAndGet','#table',TYPYZAM,__typofe.REF,,,null());
~~


\zakr_lsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: zakres dokumentów sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
FILTER.win_edit('STATUS');
{? FILTER.edit()
|| exec('lsp_fak_filtr','lsp');
   {? var_pres('POS',_env)<0
      &  (
         exec('chk_role','#b__box',OPERATOR.USER,'LSP_FAK_PZLF')
         | exec('chk_role','#b__box',OPERATOR.USER,'LSP_FAK_DZLF')
         | exec('chk_role','#b__box',OPERATOR.USER,'LSP_FAK_EZLF')
         )
   || params_set('env',_env);
      grp_disp(FAKZ,'WER',1,1)
   ?};
   params_set('env',_env);
   grp_disp(FAKS,'WERS',1,1);
   AreaTitle.setTitle()
?};
~~


\typyzam_proj_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: zakres dokumentów sprzedaży
::----------------------------------------------------------------------------------------------------------------------
TYPYZAM.cntx_psh();
TYPYZAM.win_dict('SEL');
TYPYZAM.actions('SEL','W');
{? TYPYZAM.f_active() || TYPYZAM.f_clear() ?};
_p3007:=exec('get','#params',3007,2,OPERATOR.USER);
_where:={? _p3007=''
      || ''
      || '\' %1\' like \'\% \'||TYPYZAM.T||\' \%\' and '[_p3007]
      ?};
_where+='TYPYZAM.R=\'O\'';
TYPYZAM.f_set('T',,_where)


\typyzam_proj_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: zakres dokumentów sprzedaży
::----------------------------------------------------------------------------------------------------------------------
BEER.TYPYZAM:=ZAKR.TYPYZAM;
TYPYZAM.cntx_pop();
{? TYPYZAM.f_active() || TYPYZAM.f_clear() ?}


\renumofp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: renumeracja OFP
::   WE: [_a] - ''(domyślnie) - drag&drop, 'U','D','N'-akcja do przenumerowania
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (';UDN'*_a)>1 || _a || '' ?};

OFP.cntx_psh();
{? _tryb=''
|| _ref:=dnd_info('dest_record');
   {? OFP.seek(_ref) || exec('zmien_lp','#dragdrop','POZ','OFE_P') ?}
|| exec('zmien_lpa','#dragdrop','POZ','OFE_P',,,_tryb)
?};
OFP.cntx_pop()


\ofe_f_rfresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Odświeża listę rekordów tabeli OFE
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? OFE.sel_size()=0 & OFE.f_active() || OFE.f_rfresh() ?}


\show_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Akcja pokaż ofertę
::----------------------------------------------------------------------------------------------------------------------
OFE.cntx_psh();
OFP.cntx_psh();
OFP.index('OFE');
OFP.prefix(OFP.OFE);
_red:=OFE.mk_edit('',,'oferty_red');
OFE.win_ewin(_red,,'RED');
_win_grp:=OFE.grp_make(,,'disp_grp_ofe');
OFE.grp_sel(_win_grp,OFP,'WER_ALL','Pozycje'@,,,,23,,,,,'maximized','position_ofe',1);
OFE.grp_edit(_win_grp,,_red,'Nagłówek',,,,,'maximized');
OFE.win_sel(_win_grp);
OFE.select();
OFE.cntx_pop();
OFP.cntx_pop()


\pow_ofe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Akcja pokaż powiązane oferty z poziomu pozycji
::----------------------------------------------------------------------------------------------------------------------
OFE.cntx_psh();
OFP.OFE();
params_exec('pow_ofe','powdok');
OFE.cntx_pop()

:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 7879e3dc4ab0b667c8581ea4269fff580b6d58d7861dd1e38850b13b16972cba0fcac1c4e2d166d204ec2e6454211a331a7528bdd65cac07a5ba3ff6fde38f74968acff3b0b29ade4503a0e720d7f83589f75383f023b4eac0bd0f8893bf62d66a564fc3fd7210c6d0a2d8a4877b89bea10335a6320c41f6a6723de63047b81e

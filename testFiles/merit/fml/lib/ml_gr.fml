:!UTF-8
::(c) Macrologic SA odzial Wroclaw Wszelkie prawa zastrzeżone
::======================================================================================================================
::Nazwa pliku: ml_gr.fml
::Utworzony: 2008-06-24
::Autor: PawelM
::======================================================================================================================
::Zawartosc: Formuly podstawowe do obslugi nowej dynamicznej struktury budzetowej Finanse i ksiegowosc wersja budzetowa
::======================================================================================================================

\spr_hier
::-----------------------------------------------------------------------------------------------------------------
::  UTW: Pawelm [1030]
:: OPIS: Formuła wykonywana przed kasowaniem sprawdzajaca czy Aktywna oraz czy sa powiazania
::   WE:   _a - string do sprawdzenia  _b - Ref do aktualnego roku, _c - blokowac czy pytac
::   WY:   0 oznacza ze jest powiazanie
::-----------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<0 || _c:=0 ?};
_wyn:=1;
ML_GRPOZ.cntx_psh();
ML_GR.cntx_psh();
ML_GR.index('ML_GRKOD');
ML_GR.prefix(_b);
{? ML_GR.first()
|| {!
   |?
      ML_GRPOZ.index('MLGRPOZT');
      ML_GRPOZ.prefix(ML_GR.ref());
      {? ML_GRPOZ.first()
      || {!
         |?
            {? _a=ML_GRPOZ.SLU_NAZ || _wyn*=0 ?};
            ML_GRPOZ.next()
         !}
      ?};
   ML_GR.next()
   !}
?};
ML_GR.cntx_pop();
ML_GRPOZ.cntx_pop();
{? ~_wyn & _c || _wyn:=FUN.ask('Słownik %1 jest używany w definicji hierarchi \n Czy kontynuować? '@[_a]) ?};
_wyn


\wyc_ml_gr_obr
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1030]
:: OPIS: Wycofanie obrotow ze tabeli obrotow na hierchiach slownikow ML_OBRTR na podstawie zapisow w tabeli ML_POZT
::       dla aktualnie przetwarzanego rekordu POZ.
::   WE: _a - waluta SLO.ref
::       _b - okres, domyslnie SSTALE.AO
::       _c - tryb przetwarzania: 1 - formula odtwarzajaca, 0 - ksiegowanie
::-------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<0 || _b:=SSTALE.AO ?};
{? OKRO_F.seek(_b)
|| {? _<3 || _msg:=0 || _msg:=_c ?};
   _wyn:=1;
   _komun:=_path:=_str:='';
   _maxpoz:=0;
   _i:=_j:=1;
   ML_POZTR.index('OPOZHIER');
   ML_POZTR.prefix(OKRO_F.ref(),POZ.ref());
   {? ML_POZTR.first()
   || {!
      |?
         _maxpoz:=0;
::      Obliczamy liczbe max poziomow dla danej pozycji hierarchii
         STR.split(ML_POZTR.PATH,'-'); {!|?_str:=STR.get_word(); {?_str<>'' ||_maxpoz+=1 ?};  STR.next() !};
::        Wycofujemy  obroty
         {! _i:=1.._maxpoz
         |!
            {? ML_OBRTR.lock(1)
            || ML_OBRTR.index('MLOBROTY');
               ML_OBRTR.prefix();
               _path:='';
               {! _j:=1.._i |! _path+=($('ML_POZTR.KOD'+form(_j,-2)))()+'-'!};
               {?_path+1='-'|| _path:=_path-1 ?};
               {? ML_OBRTR.find_key(ML_POZTR.ML_GR,DOK.ODD().OD,OKRO_F.ref(),ML_POZTR.TYP,_a,_path,_i)
               || ML_OBRTR.WN-=ML_POZTR.WN;
                  ML_OBRTR.MA-=ML_POZTR.MA;
                  ML_OBRTR.put()
               || _komun:='\nNieudana próba wycofania obrotów w hierarchii budżetowej.'@;
                  {? _msg=0
                  || {? KOMT=''
                     || KOMT:='\nDla konta '+POZ.KON +' w dokumencie: '+POZ.DOK().NK +
                           ' wystąpił wyjątek.'+_komun
                     ?}
                  || FUN.emsg('Dla konta %1 w dokumencie: %2 wystąpił wyjątek.'
                        '\nW hierachii budżetowej %3 %4'@[POZ.KON,POZ.DOK().NK,ML_GR.NAZWA,_komun],'Uwaga !'@)
                  ?};
                  _wyn*=0
               ?};
               ML_OBRTR.unlock();
               _wyn
            || _komun:='\nNieudana próba zablokowania tabeli obrotów na hierarchiach budzetowych.'@;
               {? _msg=0
               || {? KOMT=''
                  || KOMT:='\nDla konta '+POZ.KON +' w dokumencie: '+POZ.DOK().NK +
                        ' wystąpił wyjątek.'+_komun
                  ?}
               || FUN.emsg('Dla konta %1 w dokumencie: %2 wystąpił wyjątek.'
                     '\nW hierachii budżetowej %3 %4'@[POZ.KON,POZ.DOK().NK,ML_GR.NAZWA,_komun],'Uwaga !'@)
               ?};
               _wyn*=0
            ?}
         !};
         ML_POZTR.next()
      !}
   ?}
|| _wyn:=0
?};
_wyn


\add_ml_gr_obr
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1030]
:: OPIS: Dodanie obrotow na hierarchiach slownikow budzetowych ML_OBRTR  na podstawie zapisow w tabeli ML_POZTR,
::       dla aktualnie przetwarzanego rekordu POZ. Poniewaz ML_POZTR jest zawsze w walucie FINFO.NAROD
::       na razie wszystko jest w jednej walucie. Podczas funkcja rozbija sciezke na podpoziomy i tworzy tyle
::       zapisow w obrotach co podpoziomow, np ML_POZTR.PATH='75502-4300-430001' dla jednej z hierarchii slownikow
::       powiazanej z aktualnym POZ.ref. Zadaniem funkcji jest okreslenie liczby max poziomow (3) oraz utworzenie
::       lub aktualizacja obrotow dla podpoziomow 75502, 75502-4300 oraz 75502-4300-430001.
::   WE: _a - waluta SLO.ref
::       _b - okres, domyslnie SSTALE.AO
::       _c - tryb przetwarzania: 1 - formula odtwarzajaca, 0 - ksiegowanie
::-------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<0 || _b:=SSTALE.AO ?};
{? OKRO_F.seek(_b)
|| {? _< 3 || _msg:=0 || _msg:=_c ?};
   _wyn:=1;
   _komun:=_path:=_str:='';
   _maxpoz:=0;
   _jest:=0;
   _i:=_j:=1;
   ML_POZTR.index('OPOZHIER');
   ML_POZTR.prefix(OKRO_F.ref(),POZ.ref());
   {? ML_POZTR.first()
   || {!
      |?
         _maxpoz:=0;
::      Obliczamy liczbe max poziomow dla danej pozycji hierarchii
         STR.split(ML_POZTR.PATH,'-');
         {!|? _str:=STR.get_word(); {?_str<>'' || _maxpoz+=1 ?};  STR.next() !};
::      Uaktualniamy obroty
         {! _i:=1.._maxpoz
         |!
            {? ML_OBRTR.lock(1)
            || ML_OBRTR.index('MLOBROTY'); ML_OBRTR.prefix();
               _path:='';
               {! _j:=1.._i |! _path+=($('ML_POZTR.KOD'+form(_j,-2)))()+'-' !};
               {?_path+1='-'|| _path:=_path-1 ?};
               _jest:=ML_OBRTR.find_key(ML_POZTR.ML_GR,DOK.ODD().OD,OKRO_F.ref(),ML_POZTR.TYP,_a,_path,_i);
               {? _jest=0
               || ML_OBRTR.blank(1);
                  ML_OBRTR.ML_GR:=ML_POZTR.ML_GR;
                  ML_OBRTR.WAL:=_a;
                  ML_OBRTR.ODD:=DOK.ODD().OD;
                  ML_OBRTR.OKRO_F:=OKRO_F.ref();
                  ML_OBRTR.TYP:=ML_POZTR.TYP;
                  ML_OBRTR.POZ:=_i;
                  ML_OBRTR.MAX_POZ:=_maxpoz;
                  {! _j:=1.._i
                  |!
                     ($('ML_OBRTR.KOD'+form(_j,-2)+':='+'ML_POZTR.KOD'+form(_j,-2)))();
                     ($('ML_OBRTR.OPIS'+form(_j,-2)+':='+'ML_POZTR.OPIS'+form(_j,-2)))()
                  !};
                  ML_OBRTR.PATH:=_path
               ?};
               ML_OBRTR.WN+=ML_POZTR.WN;
               ML_OBRTR.MA+=ML_POZTR.MA;
               _wyn:={? _jest || ML_OBRTR.put() || ML_OBRTR.add() ?};
:               {? _wyn || _wyn:=exec('ml_gr_path','ml_gr',ML_OBRTR.ML_GR,_path) ?};
               ML_OBRTR.unlock();
               _wyn
            || _komun:='\nNieudana próba zablokowania tabeli obrotów na hierarchiach budzetowych.';
               {? _msg=0
               || {? KOMT=''
                  || KOMT:='\nDla konta '+@.POZ.KON+' w dokumencie: '+@.POZ.DOK().NK+
                           ' wystąpił wyjątek.'+_komun
                  ?}
::               || exec('add_err','ml_util','HIERARCHIA_OBR_LOCK','Dla konta '+@.POZ.KON +
::                     ' w dokumencie: '+@.POZ.DOK().NK +' wystąpił wyjątek.'+
::                     'W hierachii budżetowej '+ML_GR.NAZWA +(1-_komun))
               ?};
               _wyn:=0
            ?}
         !};
         ML_POZTR.next()
      !}
   ?}
|| _wyn:=0
?};
_wyn


\ml_poztr_del
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1030]
:: OPIS: Usuwa wszystkie zapisy w tabeli ML_POZTR przypisane do danego rekordu POZ
::   WE: _a - POZ.ref()
::       _b - okres, domyslnie SSTALE.AO
::-------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<0 || _b:=SSTALE.AO ?};
{? OKRO_F.seek(_b)
|| ML_POZTR.cntx_psh();
   ML_POZTR.index('OPOZHIER');
   ML_POZTR.prefix(OKRO_F.ref(),_a);
   {? ML_POZTR.first() || {!|? ML_POZTR.del() !} ?};
   ML_POZTR.cntx_pop()
?};
~~


\poz_mlgr_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1030]
:: OPIS: Prezentacja mozliwych poziomow agregacji dla wybranej wczesniej hierarchii. Funkcja do sprawozdan definio-
::       walnych
::   WE: [_b] - 1 dla planow
::  OLD: \poz_mlgr_f3/ml_gr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _<2 || _b:=0 ?};
_mlgrpoz:=ML_GRPOZ.mk_sel('Dostępne poziomy agregacji dla hierarchii o kodzie '@,'P',,'#ml_grpoz_sik',1,1,20);
ML_GRPOZ.win_fld(_mlgrpoz,ML_GRPOZ,'LP',,,2,,1,'Lp.'@);
ML_GRPOZ.win_fld(_mlgrpoz,ML_GRPOZ,'SLU_NAZ',,,13,,,'Nazwa'@);
ML_GRPOZ.win_fld(_mlgrpoz,ML_GRPOZ,'SLU_OP',,,68,,1,'Opis słownika'@);
ML_GRPOZ.win_act(_mlgrpoz,0,'Szukaj','Szukaj'@@,,'Szukaj'@,,,,,,);
ML_GRPOZ.win_act(_mlgrpoz,0,'Formuła','Ta'@@,,,"sel_exit()",,1,,,,'T');
ML_GRPOZ.win_sel(_mlgrpoz);
ML_GR.cntx_psh();
ML_GRPOZ.cntx_psh();
{? (_b=0 & ALG_PAR.P5<>'') | (_b=1 & ALG_PAR.P2<>'')
|| {? _b=0
   || ML_GR.index('ML_GRAKT')
   || ML_GR.index('ML_GRAKP')
   ?};
   {? (_b=0 & ALG_PAR.P5<>'')
   || _hier:=ALG_PAR.P5
   || _hier:=exec('znajdz','ml_conn','PLANY',ALG_PAR.P2)
   ?};
   ML_GR.prefix(ALG_PAR.ROK,'T','T',_hier);
   {? ML_GR.first()
   || ML_GRPOZ.index('MLGRPOZL');
      ML_GRPOZ.prefix(ML_GR.ref());
      {? ML_GR.first()
      || ML_GRPOZ.hdr_sel();
         ML_GRPOZ.hdr_sel(_hier);
         {? ML_GRPOZ.select() || par:=$ML_GRPOZ.LP ?}
      ?}
   || FUN.info('Brak aktywnych hierarchii słowników w roku %1'@[ALG_PAR.ROK().NAZ])
   ?}
|| FUN.info('Uzupełnij informacje o hierarchii'@)
?};
ML_GR.cntx_pop();
ML_GRPOZ.cntx_pop()


\mlgr_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1030]
:: OPIS: Prezentacja aktywnych w przekazanym roku hierarchii slownikow w kontekcie wyboru parametrow sprawozdan
::  OLD: \mlgr_f3/ml_gr.fml
::----------------------------------------------------------------------------------------------------------------------
_mlgr:=ML_GR.mk_sel('Dostępne analizy hierarchii słowników dla  roku '@,'P',,'#ml_gr_sik',1,1,20);
ML_GR.win_fld(_mlgr,ML_GR,'KOD',,,4,,,'Kod'@);
ML_GR.win_fld(_mlgr,ML_GR,'NAZWA',,,51,,,'Nazwa hierarchii'@);
ML_GR.win_fld(_mlgr,ML_GR,'SYS_FILL',,,3,,,'U?'@,,'Czy podczas agregacji uzupełniać automatycznie słowniki systemowe?'@,2,,"'T'","'N'");
ML_GR.win_act(_mlgr,0,'Szukaj','Szukaj'@@,,'Szukaj'@,,,,,,);
ML_GR.win_act(_mlgr,0,'Formuła','Ta'@@,,,"sel_exit()",,1,,,,'T');
ML_GR.win_sel(_mlgr);
ML_GR.cntx_psh();
ML_GR.index('ML_GRAKT'); ML_GR.prefix(ALG_PAR.ROK,'T','T');
{? ML_GR.first()
|| ML_GR.hdr_sel();
   ML_GR.hdr_sel(ALG_PAR.ROK().NAZ);
   {? ML_GR.select() || par:=ML_GR.KOD ?}
|| FUN.info('Brak aktywnych hierarchii słowników w roku %1'@[ALG_PAR.ROK().NAZ])
?};
ML_GR.cntx_pop()


\p_mlgr_poz_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1030]
:: OPIS: Sprawdza czy przekazany numer poziomu wystepuje w okreslonej parametrem
::   WE: _a - numer parametru w ALG_PAR
::       _b - parametr
::       _c - ROK_F.ref()
::       [_d] - 1 dla planow
::  OLD: \p_mlgr_poz_po/ml_gr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _<4 || _d:=0 ?};
ML_GR.cntx_psh();
ML_GRPOZ.cntx_psh();
{? (_d=0 & ALG_PAR.P5<>'') | (_d=1 & ALG_PAR.P2<>'')
||
   {? _d=0
   || ML_GR.index('ML_GRAKT')
   || ML_GR.index('ML_GRAKP')
   ?};
   {? (_d=0 & ALG_PAR.P5<>'')
   || _hier:=ALG_PAR.P5
   || _hier:=exec('znajdz','ml_conn','PLANY',ALG_PAR.P2)
   ?};
   ML_GR.prefix(_c,'T','T',_hier);
   {? ML_GR.first()
   || ML_GRPOZ.index('MLGRPOZL');
      ML_GRPOZ.prefix(ML_GR.ref(),#_b);
      _x:={? ML_GRPOZ.first() || ($('ALG_PAR.P'+$_a+':=$ML_GRPOZ.LP'))(); 1 || 0 ?}
   || _x:=0
   ?}
|| _x:=0
?};
ML_GR.cntx_pop();
ML_GRPOZ.cntx_pop();
_x


\p_mlgr_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1030]
:: OPIS: Sprawdza czy przekazany parametr pola (_b) jest KODem tabeli ML_GR
::   WE: _a - numer parametru w ALG_PAR
::       _b - parametr
::       _c - ROK_F.ref()
::  OLD: \p_mlgr_po/ml_gr.fml
::----------------------------------------------------------------------------------------------------------------------
ML_GR.cntx_psh();
ML_GR.index('ML_GRAKT');
ML_GR.prefix(_c,'T','T',_b);
_x:={? ML_GR.first() || ($('ALG_PAR.P'+$_a+':=ML_GR.KOD'))(); 1 || 0 ?};
ML_GR.cntx_pop();
_x

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:55 c8c7988091c5d9554249b3049b03ff0d091b04a0fb759bfe16a9da13a605cc889388abaf8f5dd58c42980755b53ef5ed9ce662f172e67a2fc9afb22dfdaf85dc70b2ea2be4e7eaeabc574a6ec3039e490eaf79cc84ff7cfea07ae7b455c509bdf06af1116d7b354b0bcb1f896cd7332014b04240d564b1a2b3ce15bb063d6954

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: fks.fml
:: Utworzony: 09.03.2015
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły obszaru FKS
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formuła inicjująca dla obszaru FKS
::----------------------------------------------------------------------------------------------------------------------
exec('rach_slo','rachunki');
exec('F','object');
exec('RB','object');
:: Formuła podczytująca dane specyficzne dla firmy
__War_f:="exec('FindInSet','#table',_a,_a,_c,REF.FIRMA,$(_a+'.'+_b))";
exec('czytaj','#stalesys',,FINFO);
exec('czytaj','#stalesys',,XINFO);
exec('czytaj','#stalesys',,INFO);
exec('init','jpk_v');
:: Ustawienie prezycji pól w oknach
exec('fld_prec','win_ust');
~~


\fks_dks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_DKS - Dokumenty księgowe
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
exec('fl_decl','dekret_aut');
exec('dek_decl','dekret_aut');
exec('sd_decl','dekret_aut');
exec('upr_set','ceny');

:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)
|| _link:=_params.LINK
|| _link:=''
?};

{? _link<>''
|| {? ~exec('link','fks',_link) || return() ?};
   exec('main','!fks_dks_zdok',_link)
|| exec('main','!fks_dks_zdok')
?};

~~


\fks_hub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Główna formuła obszaru roboczego FKS_HUB - Dokumenty księgowe w MeritHUB
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
exec('fl_decl','dekret_aut');
exec('dek_decl','dekret_aut');
exec('sd_decl','dekret_aut');
exec('upr_set','ceny');

:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)
|| _link:=_params.LINK
|| _link:=''
?};

{? _link<>''
|| {? ~exec('link','!fks_hub_link',_link) || return() ?};
   exec('main','!fks_hub_link',_link)
|| FUN.info('Otwarcie obszaru bez linku do dokumentu nie jest możliwe.'@); return()
?};

~~


\fks_ksr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_KSR - Przeglądanie wg kont
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
exec('rpm_init','dok_fks');
BILANSP.ASEP:=SSTALE.AR().SEP;
ZapPoz:=obj_new('TAB','WIN','TAB2','WIN2','TRYB','REPAINT','tryb');
ZapPoz.REPAINT:=-1;
ZapPoz.tryb:="
   {? var_press('_a')<=0 || _a:=ZapPoz.TRYB ?};
   {? var_pres('TAB',ZapPoz)>0 || obj_del(ZapPoz.TAB) ?};
   {? var_pres('TAB2',ZapPoz)>0 || obj_del(ZapPoz.TAB2) ?};
   {? _a=1
   || ZapPoz.TAB:=POZ;
      ZapPoz.WIN:='C_WYS1A'
   |? _a=2
   || ZapPoz.TAB:=exec('zap_poz','dok_fks_ks');
      ZapPoz.TAB2:=exec('zap_sum2','dok_fks_ks');
      ZapPoz.WIN:=exec('zap_win','dok_fks_ks',ZapPoz.TAB,1);
      ZapPoz.WIN2:=exec('zap_win2','dok_fks_ks',ZapPoz.TAB2);
      ZapPoz.TAB2.first();
      ZapPoz.TAB.first()
   |? _a=3
   || ZapPoz.TAB:=POZ;
      ZapPoz.WIN:='C_WYS1B'
   ?};
   ZapPoz.TRYB:=_a
";
VAR_DEL.delete('__ZOIS','__AZOIS','notfirst');
notfirst:=0;
{? SSTALE.WAL<>FINFO.NAROD || ZapPoz.tryb(3) || ZapPoz.tryb(1) ?};
zakladka:=1;
{!
|? _grp:=AN.grp_make('',"exec('bgw','!fks_ksr_zamk')",'#angrpsel',,,"exec('exit','zws',_a)");
:: Zakładka konta analityczne
   exec('add_tab','!fks_ksr_zbka','Konta analityczne'@,AN,_grp);

:: Wyswietlenie zapisów księgowań
   exec('add_tab','!fks_ksr_zamk','Zapisy'@,AN,_grp);

:: Zakładka obroty kont
   exec('ini_wal','konto');
   exec('add_tab','!fks_ksr_zbob','Obroty kont'@,AN,_grp);

:: Zakładka obroty kont walutowych
   exec('add_tab','!fks_ksr_zboc','Obroty kont walutowych'@,AN,_grp);

:: Zakładka dziennik
   exec('add_tab','!fks_ksr_zddz','Dziennik'@,AN,_grp);

:: Zakładka pozycje dokumentów
   exec('add_tab_poz','!fks_ksr_zamk','Pozycje dokumentów'@,AN,_grp);

:: Zakładka Zestawienie obrotów i sald
   exec('add_tab_zois','!fks_ksr_zbob','Zestawienie obrotów i sald'@,AN,_grp);

:: Zakładka Ewidencja prowadzona przez członków grupy VAT
   exec('add_tab_gvat','!fks_ksr_zamk','Ewidencja grupy VAT'@,AN,_grp);

   AN.win_sel(_grp);
   exec('title','dok_fks',AN);
   {? AN.select()
   || TAB_ZOIS.ndx_drop();
      {? zakladka<>7
      || ZapPoz.tryb()
      ?};
      ZapPoz.REPAINT
   ?}
!};

DZIENNIK.fld_fml('OBWN','BEFORE_DISPLAY',"*");
SUM.fld_fml('SUMWN','BEFORE_DISPLAY',"*");

VAR_DEL.delete('mask_kon','pa','__grp','__wsel','dwiewalo','ZapPoz');
VAR_DEL.delete('TAB_OBR','ind_obr1','ind_obr2','ind_obr3','ind_obr4','__KURS','TAB_POZ','lp_gen','fpoz');
VAR_DEL.delete('ind_wbr1','ind_wbr2','ind_wbr3','ind_wbr4');
VAR_DEL.delete('zm_wal','k_1','k_2','k_3','k_4','MASK_KON');
VAR_DEL.delete('__OBR','__OBRW','zakładka');
VAR_DEL.delete('__TAB');
~~


\fks_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_ROZ - Przeglądanie rozrachunków wg kont
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
__FKSROZ:=1;

:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)
|| _link:=_params.LINK
|| _link:=''
?};

{? _link<>''
|| exec('main','!fks_roz_zark',_link)
|| exec('main','!fks_roz_zark')
?};

__FKSROZ:=0;
~~


\fks_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_VAT - Ewidencje VAT
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
kraj:={? _=0 || exec('kod_pl','slo_slu') || _a ?};
_vat:=exec('tree_vat','fks_vat');
_grp:=_vat.grp_make('',"",'#vatgrpsel',,,"exec('exit','zws')");
:: Zakładka rejestry VAT
exec('add_tab','!fks_vat_zdoz','Rejestry VAT'@,_grp);
:: Zakładka rozliczenia zawieszone
exec('add_tab','!fks_vat_zroz','Rozliczenia zawieszone'@,_grp);
:: Zakładka korekty do nabyć
exec('add_tab','!fks_vat_ztro','Korekty do nabyć'@,_grp);
:: Zakładka Dokumenty wg krajów
exec('add_tab2','!fks_vat_zdoz','Dokumenty wg krajów'@,_grp);
:: Zakładka Pozycje VAT
exec('add_tab_poz','!fks_vat_zdoz','Pozycje VAT'@,_grp);
:: Zakładka Ewidencja prowadzona przez członków grupy VAT
exec('add_tab_dok','!fks_vat_zdoz','Ewidencja grupy VAT'@,_grp);
_vat.win_sel(_grp);
exec('title','fks_vat');
RVAT.cntx_psh();
RVAT.prefix(REF.FIRMA);
_oper_d:=OPERATOR.DEPT;
_pom_use:=POMOC.USE; DOK.cntx_psh(); POZ.cntx_psh(); POW.cntx_psh(); AN.cntx_psh(); AN_SLU.cntx_psh();
_vat.select();
POMOC.USE:=_pom_use; DOK.cntx_pop(); POZ.cntx_pop(); POW.cntx_pop(); AN.cntx_pop(); AN_SLU.cntx_pop();
OPERATOR.DEPT:=_oper_d;
RVAT.cntx_pop();
VAR_DEL.delete('TREE_VAT','w_acr','TMP_VAT','TT_RVAT','vpozvat')


\fks_ved
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_VED - Deklaracje podatkowe VAT,CIT
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
exec('init','fks_ved');
OkroFInd:=1;
SIK.ROK1:=SIK.ROK2:=SSTALE.AR;
SIK.OKRES2:=SSTALE.AO;
VAT_DEK.win_patt('SZUK');
BPMN.SYM_DOM:='FKS';
_grp:=VAT_DEK.grp_make('',"",'#vatdekgrpsel',,,"exec('exit','zws')");
:: Zakładka JPK_V7
exec('add_tab','!fks_ved_zvjp','JPK_V7',_grp);
:: Zakładka JPK_GV
exec('add_tab','!fks_ved_zvgp','JPK_GV',_grp);
:: Zakładka VAT-7
exec('add_tab','!fks_ved_zv7p','VAT-7',_grp);
:: Zakładka VAT-27
exec('add_tab','!fks_ved_zvop','VAT-27',_grp);
:: Zakładka VAT-UE
exec('add_tab','!fks_ved_zvup','VAT-UE',_grp);
:: Zakładka VIU-DO
exec('add_tab','!fks_ved_zvvp','VIU-DO',_grp);
:: Zakładka CIT
exec('add_tab','!fks_ved_zvcp','CIT-8',_grp);
VAT_DEK.win_sel(_grp);
AreaTitle.setTabWin(VAT_DEK,_grp);
AreaTitle.setTitle();
VAT_DEK.select();
exec('ust_fml','fks_ved',0);
VAR_DEL.delete('vat_typ','rodzdekl','TVAT','pw','pv','OkroFInd')


\roz_kosz_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Głowna formuła obszaru roboczego FKS_DKS_RK..
::  OLD: \naz_sel/roz_kosz.fml
::  OLD: \row_sel/roz_kosz.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
SSTALE.AR();
_grp:=ROZ_NAZ.grp_make('Rozliczenia kosztów'@,"",'#rozkoszsel');

ROZ_NAZ.win_sel('WER'); ROZ_NAZ.hdr_sel(); ROZ_NAZ.hdr_sel(' - '+SSTALE.AR().NAZ);
ROW_NAG.win_sel('WER'); ROW_NAG.hdr_sel(); ROW_NAG.hdr_sel(' - '+SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ);
{? SSTALE.AO().ZAM='T' | exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER) || ROW_NAG.actions('WER','DUO:D') || ROW_NAG.actions('WER') ?};

ROZ_NAZ.grp_sel(_grp,,'WER','Definicje'@,,0,0,,"SIK.AKC:='N'; 1","",,,'maximized_with_title');
ROZ_NAZ.grp_sel(_grp,ROW_NAG,'WER','Wykonania'@,,0,0,,"SIK.AKC:='T'; 1","",,,'maximized_with_title');

ROW_NAG.cntx_psh();
ROW_NAG.win_edit('RED');
{? ~OPERATOR.DEPT
|| ROW_NAG.index('OKRES'); ROW_NAG.prefix(SSTALE.AO)
|| ROW_NAG.index('OKR_ODD'); ROW_NAG.prefix(SSTALE.AO,OPERATOR.DEPT)
?};
ROZ_NAZ.cntx_psh();
ROZ_NAZ.index('TYP'); ROZ_NAZ.prefix(SSTALE.AR);
ROZ_NAZ.win_sel(_grp); ROZ_NAZ.win_edit('RED');
ROZ_NAZ.select();
ROZ_NAZ.cntx_pop(); ROW_NAG.cntx_pop()


\fks_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: maglak [17.00]
:: OPIS: Formuła sprawdzająca licencję dla obszaru FKS
::   WY: 'T' - jest licencja, 'N' - nie ma licencji
::----------------------------------------------------------------------------------------------------------------------
_result:='N';
_domain:=exec('domain_ref','#b_domain','FKS');
{? _domain<>null()
|| {? exec('lic','#b_domain',_domain)=1
   || _result:='T'
   ?}
?};
_result


\fks_ksp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_ksp - Sprawozdania definiowalne
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
{? ROK_F.FIRMA().TYP='C'
|| _naz_trig:='konsolidacja';
    ALG_PAR.trig_a('add',"{? _a || exec('t_alg_par','konsolidacja',1) || ~~ ?}",_naz_trig);
    ALG_PAR.trig_a('put',"{? _a || exec('t_alg_par','konsolidacja',2) || ~~ ?}",_naz_trig);
    ALG_PAR.trig_b('del',"exec('t_alg_par','konsolidacja',3)",_naz_trig);
    WERSJE_W.trig_a('add',"{? _a || exec('t_wersje_w','konsolidacja',1) || ~~ ?}",_naz_trig);
    WERSJE_W.trig_a('put',"{? _a || exec('t_wersje_w','konsolidacja',2) || ~~ ?}",_naz_trig)
?};
exec('load_war','sprfin');
REF.WYKON:=exec('wykon_firmy','konsolidacja',REF.FIRMA);
_tab:=GR_SIK;
_grp:=_tab.grp_make('',"",'#fkskspwork',,,"exec('exit','zws')");
:: Zakładka sprawozdania
exec('add_tab','sprfin','Sprawozdania'@,_tab,_grp);
:: Przedglądanie wartości
exec('add_tab','!fks_ksp_zspr','Przegląd wartości'@,_tab,_grp);
:: Definicje
exec('add_tab_def','sprfin','Definicje'@,_tab,_grp);

_tab.win_sel(_grp);
AreaTitle.setTabWin(_tab,~~);
AreaTitle.setTitle();
exec('set_fml','#field',SIK,'AKC',"GR_SIK.cntx_psh();SIK.AKC:=USRGRSIK.GR_SIK().AKC; GR_SIK.cntx_pop();~~");
_tab.select();
exec('set_fml','#field',SIK,'AKC')


\init_obj_um
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Inicjowanie obiektów i okienek dla umów dotyczących rozrachunków
::----------------------------------------------------------------------------------------------------------------------
exec('ust_okn','rozrach');
exec('init','fks');
exec('fl_decl','dekret_aut');
exec('dek_decl','dekret_aut');
exec('sd_decl','dekret_aut');
BPMN.SYM_DOM:='FKS';
KH.win_dict('WERHOME2')


\fks_ruc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_RUC - Umowy dotyczące rozrachunków
::   WE: _a - zadeklarowane i rowne 1 - tylko dekretacja
:             zadeklarowane i rowne 2 - dla kontrahenta
::            niezadeklarowane - wszystkie umowy
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
only_dek:=(_ & _a=1);
{? _ & _a=2
|| ROZRACH.KONTR:=WIND_KH.KH
|| ROZRACH.KONTR:=null
?};
zakr_umk:=zakr_umf:=zakr_ums:='W';
umo_k:=umo_f:=umo_s:=kh_k:=kh_f:=kh_s:=null;
exec('zdarzt_add','rozrach');
_grp:=PAR_NAG.grp_make('',"",'#parnaggrpsel',,,"{? ~only_dek || exec('exit','zws') || 1 ?}");
:: Zakładka plany umów kompensat
exec('add_tab','!fks_ruc_kzpr','Kompensaty'@,_grp);
:: Zakładka plany umowy sprzedaży wierzytelności
exec('add_tab','!fks_ruc_szpr','Sprzedaż wierzytelności'@,_grp);
:: Zakładka umowy faktoringu
exec('add_tab','!fks_ruc_tzpr','Faktoring'@,_grp);
PAR_NAG.win_sel(_grp);
AreaTitle.setTabWin(PAR_NAG,_grp);
AreaTitle.setTitle();
DOK.cntx_psh();
PAR_NAG.select();
DOK.cntx_pop();
VAR_DEL.delete('zakr_umk','zakr_umf','zakr_ums','umo_k','umo_f','umo_s','kh_k','kh_f','kh_s','only_dek');
~~


\fks_rrk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_RRK - Korespondencja z rozrachunków
::  OLD: \kores_kh/windyk1.fml
::----------------------------------------------------------------------------------------------------------------------
only_dek:=(_ & _a=1);
exec('init','fks');
exec('zdarzt_add','rozrach');
SER_KOR.ZAK_WAL:=SSTALE.WAL;
{? _ & _a=2
|| ROZRACH.KONTR:=WIND_KH.KH; kh_all:=0
|| ROZRACH.KONTR:=null; kh_all:=1
?};
grup_kor:=1; OPERATOR.USER();
zmzaknot:=zmzakpot:=zmzakwez:=1;
notzak1:=potzak1:=wezzak1:=2;
notzak2:=wezzak2:=potzak2:=2;
notzak3:=wezzak3:=potzak3:=date(0,0,0);
notzak4:=wezzak4:=potzak4:=0;
notkh:=wezkh:=potkh:=null;
RACHBANK.KB_1R:='';
_grp:=SER_NAGS.grp_make('',"",'#parnaggrpsel',,,"{? ~only_dek || exec('exit','zws') || 1 ?}");
:: Zakładka wezwania do zapłaty
{? ~only_dek
|| exec('add_tab','!fks_rrk_zkaz','Wezwania do zapłaty'@,_grp)
?};
:: Zakładka noty odsetkowe
{? only_dek
|| SER_NAGS.actions('SER_NOTY','DUALPZM:DZM','E',1)
?};
exec('add_tab','!fks_rrk_zkoz','Noty odsetkowe'@,_grp);
:: Zakładka potwierdzenia salda
{? ~only_dek
|| exec('add_tab','!fks_rrk_zkpz','Potwierdzenia sald'@,_grp)
?};
SER_NAGS.win_sel(_grp);
AreaTitle.setTabWin(SER_NAGS,_grp);
AreaTitle.setTitle();
DOK.cntx_psh();
SER_NAGS.select();
DOK.cntx_pop();
SER_NAGS.hdr_sel(); SER_NAGS.hdr_edit(); SER_KOR.TYP:='';
VAR_DEL.delete('kh_all','grup_kor','zmzaknot','zmzakpot','zmzakwez','notkh','wezkh','potkh');
VAR_DEL.delete('notzak1','notzak2','notzak3','notzak4','wezzak1','wezzak2','wezzak3','wezzak4');
VAR_DEL.delete('potzak1','potzak2','potzak3','potzak4','only_dek');
ROZRACH.KONTR:=null;
~~


\fks_rwp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_RWP - Analizy windykatora
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');
pulpit:=1;
exec('main','!fks_rwp_zlaz');
VAR_DEL.delete('pulpit');
~~


\bs_rozrach_sum1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła przed wyświetleniem pola ROZRACH.SUM1
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,1) = OPTMP
|| {? cur_win(1,1)='PB_GEN' | cur_win(1,1)='KOM' | cur_win(1,1)='FAK' | cur_win(1,1)='PAR' | cur_win(1,1)='UMU'
   | cur_win(1,1)='PB_ZB'
   || {? f_usr_changed() | ~OPTMP.f_active()
      || OPTMP.cntx_psh();
         ROZRACH.SUM1:=ROZRACH.SUM2:=ROZRACH.SUM3:=ROZRACH.SUM4:=ROZRACH.SUM5:=ROZRACH.SUM6:=0;
         ROZRACH.SUM1_VAT:=ROZRACH.SUM2_VAT:=ROZRACH.SUM3_VAT:=0;
         {? OPTMP.f_active()=0
         || {? OPTMP.first()
            || {! |? {? 'PB_GEN;PB_ZB;PAR'*cur_win(1,1)>0
                     || ROZRACH.SUM1+=OPTMP.SAL;
                        ROZRACH.SUM2+=OPTMP.ROZL;
                        ROZRACH.SUM3+=(OPTMP.SAL-OPTMP.ROZL)
                     || {? -OPTMP.STR='wn'
                        || ROZRACH.SUM1+=OPTMP.SAL
                        || ROZRACH.SUM2+=OPTMP.SAL
                        ?}
                     ?};
                     {? cur_win(1,1)='KOM'
                     || ROZRACH.SUM4+=OPTMP.KOMPWN;
                        ROZRACH.SUM3+=OPTMP.KOMPMA;
                        ROZRACH.SUM5+={? SER_KOR.TYP='K' | SER_KOR.TYP='P' | SER_KOR.TYP='' | OPTMP.ROZL$2>0
                                      || {? SER_KOR.TYP='K'
                                         || {? -OPTMP.STR='wn' & OPTMP.SAL<>0
                                            || OPTMP.SAL-OPTMP.KOMPMA
                                            || 0
                                            ?}
                                         || OPTMP.SAL-OPTMP.ROZL
                                         ?}
                                      || 0
                                      ?};
                        ROZRACH.SUM6+={? -OPTMP.STR='ma' & OPTMP.SAL<>0 || OPTMP.SAL-OPTMP.KOMPWN || 0 ?}
                     ?};
                     {? cur_win(1,1)='UMU'
                     || ROZRACH.SUM3+=OPTMP.ROZL;
                        ROZRACH.SUM4+={? OPTMP.ROZL$2>0
                                      || OPTMP.SAL-OPTMP.ROZL
                                      || 0
                                      ?}
                     ?};
                     {? 'TZ'*OPTMP.SP
                     || ROZRACH.SUM1_VAT+=OPTMP.SP_VSAL;
                        ROZRACH.SUM2_VAT+=OPTMP.SP_VDOZ;
                        ROZRACH.SUM3_VAT+=(OPTMP.SP_VSAL-OPTMP.SP_VDOZ)
                     ?};
                     {? cur_win(1,1)='FAK'
                     || ROZRACH.SUM3+=OPTMP.ROZL;
                        ROZRACH.SUM4+={? OPTMP.ROZL$2>0
                                      || OPTMP.SAL-OPTMP.ROZL
                                      || 0
                                      ?};
                        ROZRACH.SUM5+=OPTMP.KW2
                     ?};
                     OPTMP.next()
               !}
            ?};
            ROZRACH.SUM8:=F.SWn(ROZRACH.SUM4,ROZRACH.SUM3);
            ROZRACH.SUM7:=F.SMa(ROZRACH.SUM4,ROZRACH.SUM3)
         || _ref:=OPTMP.ref();
            {? OPTMP.f_first()
            || {! |? {? 'PB_GEN;PB_ZB;PAR'*cur_win(1,1)>0
                     || ROZRACH.SUM1+=OPTMP.SAL;
                        ROZRACH.SUM2+=OPTMP.ROZL;
                        ROZRACH.SUM3+=(OPTMP.SAL-OPTMP.ROZL)
                     || {? -OPTMP.STR='wn'
                        || ROZRACH.SUM1+=OPTMP.SAL
                        || ROZRACH.SUM2+=OPTMP.SAL
                        ?}
                     ?};
                     {? cur_win(1,1)='KOM'
                     || ROZRACH.SUM4+=OPTMP.KOMPWN;
                        ROZRACH.SUM3+=OPTMP.KOMPMA;
                        ROZRACH.SUM5+={? SER_KOR.TYP='K' | SER_KOR.TYP='P' | SER_KOR.TYP='' | OPTMP.ROZL$2>0
                                      || {? SER_KOR.TYP='K'
                                         || {? -OPTMP.STR='wn' & OPTMP.SAL<>0
                                            || OPTMP.SAL-OPTMP.KOMPMA
                                            || 0
                                            ?}
                                         || OPTMP.SAL-OPTMP.ROZL
                                         ?}
                                      || 0
                                      ?};
                        ROZRACH.SUM6+={? -OPTMP.STR='ma' & OPTMP.SAL<>0 || OPTMP.SAL-OPTMP.KOMPWN || 0 ?}
                     ?};
                     {? cur_win(1,1)='UMU'
                     || ROZRACH.SUM3+=OPTMP.ROZL;
                        ROZRACH.SUM4+={? OPTMP.ROZL$2>0
                                      || OPTMP.SAL-OPTMP.ROZL
                                      || 0
                                      ?}
                     ?};
                     {? 'TZ'*OPTMP.SP
                     || ROZRACH.SUM1_VAT+=OPTMP.SP_VSAL;
                        ROZRACH.SUM2_VAT+=OPTMP.SP_VDOZ;
                        ROZRACH.SUM3_VAT+=(OPTMP.SP_VSAL-OPTMP.SP_VDOZ)
                     ?};
                     {? cur_win(1,1)='FAK'
                     || ROZRACH.SUM3+=OPTMP.ROZL;
                        ROZRACH.SUM4+={? OPTMP.ROZL$2>0
                                      || OPTMP.SAL-OPTMP.ROZL
                                      || 0
                                      ?};
                        ROZRACH.SUM5+=OPTMP.KW2
                     ?};
                     OPTMP.f_next()
               !}
            ?};
            ROZRACH.SUM8:=F.SWn(ROZRACH.SUM4,ROZRACH.SUM3);
            ROZRACH.SUM7:=F.SMa(ROZRACH.SUM4,ROZRACH.SUM3);
            {? _ref<>null || OPTMP.f_seek(_ref) ?}
         ?};
         OPTMP.cntx_pop()
      ?}
   ?};
''
|? cur_tab(1,1) = PAR_POZ
|| {? cur_win(1,1) = 'FAK_WER' | cur_win(1,1) = 'KOM_WER' | cur_win(1,1) = 'UMU_WER'
   || {? f_usr_changed() | PAR_POZ.f_active()
      || PAR_POZ.cntx_psh();
         ROZRACH.SUM1:=ROZRACH.SUM3:=ROZRACH.SUM5:=ROZRACH.SUM6:=0;
         ROZRACH.SUM2:=ROZRACH.SUM4:=0;
         {? PAR_POZ.f_active()=0
         || PAR_POZ.index('PAR_POZ');
            {? PAR_NAG.get()
            || PAR_POZ.prefix(PAR_NAG.ref());
               {? PAR_POZ.first()
               || {! |?
                     {? 1+PAR_POZ.STR='W'
                     || ROZRACH.SUM1+=PAR_POZ.SAL$2;
                        {? SER_KOR.TYP='K' || ROZRACH.SUM4+=PAR_POZ.KWOTA$2 || ROZRACH.SUM3+=PAR_POZ.KWOTA$2 ?}
                     || ROZRACH.SUM2+=PAR_POZ.SAL$2;
                        {? SER_KOR.TYP='K' || ROZRACH.SUM3+=PAR_POZ.KWOTA$2 || ROZRACH.SUM4+=PAR_POZ.KWOTA$2 ?}
                     ?};
                     ROZRACH.SUM6+=PAR_POZ.REGRES;
                     PAR_POZ.next()
                  !};
                  PAR_POZ.first();
                  {? SER_KOR.TYP='K'
                  || ROZRACH.SUM5:=ROZRACH.SUM1$2-ROZRACH.SUM4$2;
                     ROZRACH.SUM6:=ROZRACH.SUM2$2-ROZRACH.SUM3$2
                  || ROZRACH.SUM5:=ROZRACH.SUM1$2-ROZRACH.SUM3$2
                  ?}
               ?}
            || PAR_POZ.prefix(null)
            ?}
         || {? PAR_POZ.f_first()
            || {! |?
                  {? 1+PAR_POZ.STR='W'
                  || ROZRACH.SUM1+=PAR_POZ.SAL$2;
                     {? SER_KOR.TYP='K' || ROZRACH.SUM4+=PAR_POZ.KWOTA$2 || ROZRACH.SUM3+=PAR_POZ.KWOTA$2 ?}
                  || ROZRACH.SUM2+=PAR_POZ.SAL$2;
                     {? SER_KOR.TYP='K' || ROZRACH.SUM3+=PAR_POZ.KWOTA$2 || ROZRACH.SUM4+=PAR_POZ.KWOTA$2 ?}
                  ?};
                  ROZRACH.SUM6+=PAR_POZ.REGRES;
                  PAR_POZ.f_next()
               !};
               PAR_POZ.f_first();
               {? SER_KOR.TYP='K'
               || ROZRACH.SUM5:=ROZRACH.SUM1$2-ROZRACH.SUM4$2;
                  ROZRACH.SUM6:=ROZRACH.SUM2$2-ROZRACH.SUM3$2
               || ROZRACH.SUM5:=ROZRACH.SUM1$2-ROZRACH.SUM3$2
               ?}
            ?};
            _ref:=PAR_POZ.ref();
            {? _ref<>null || PAR_POZ.f_seek(_ref) ?}
         ?};
         PAR_POZ.cntx_pop()
      || 1
      ?}
   || 1
   ?}
?}


\fks_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Główna formuła obszaru roboczego FKS_JPK - Pliki JPK
::----------------------------------------------------------------------------------------------------------------------
exec('main','!fks_jpk_zprz')


\ae_optmp_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Po redakcji kwoty do rozliczenia
::----------------------------------------------------------------------------------------------------------------------
OPTMP.ROZL:=OPTMP.ROZL$2;
{? OPTMP.SP='T'
|| {? OPTMP.ROZL>0
   || _spvtmp:=((OPTMP.ROZL/OPTMP.SAL)*OPTMP.SP_VSAL)$2;
      OPTMP.SP_VDOZ:={? _spvtmp>OPTMP.ROZL
                     || OPTMP.ROZL
                     || _spvtmp
                     ?};
      ROZRACH.KW3:=(OPTMP.SAL-OPTMP.ROZL)$2
   || OPTMP.SP_VDOZ:=0;
      ROZRACH.KW3:=OPTMP.SAL
   ?}
?};
1

\ae_optmp_vdoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Po redakcji kwoty vat do rozliczenia
::----------------------------------------------------------------------------------------------------------------------
OPTMP.SP_VDOZ:=OPTMP.SP_VDOZ$2; 1


\link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS: Funkcja do obsługi przekazanego linku
::   WE: _a - link (STRING[48])
::   WY: 1/0 - czy ustawienia na podstawie linku poprawne
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? ref_tab(_a)=DOK
|| _maska:=ref_name(_a);
   DOK.use(_maska);
   DOK.prefix();
   {? ~DOK.seek(_a,DOK.name())
   || FUN.info('Nie odnaleziono rekordu zgodnego z przekazanym linkiem.'@);
      _dalej:=0
   ?}
|? ref_tab(_a)=VPOZ
|| _maska:=ref_name(_a);
   VPOZ.use(_maska);
   VPOZ.prefix();
   {? ~VPOZ.seek(_a,VPOZ.name())
   || FUN.info('Nie odnaleziono rekordu zgodnego z przekazanym linkiem.'@);
      _dalej:=0
   || DOK.use('doku'+(VPOZ.name()+4));
      DOK.prefix();
      {? ~DOK.seek(VPOZ.DOK,DOK.name())
      || FUN.info('Nie odnaleziono dokumentu związanego z przekazanym linkiem.'@);
         _dalej:=0
      ?}
   ?}
|? ref_tab(_a)=POZ
|| _maska:=ref_name(_a);
   POZ.use(_maska);
   POZ.prefix();
   {? ~POZ.seek(_a,POZ.name())
   || FUN.info('Nie odnaleziono rekordu zgodnego z przekazanym linkiem.'@);
      _dalej:=0
   || DOK.use('doku'+(POZ.name()+4));
      DOK.prefix();
      {? ~DOK.seek(POZ.DOK,DOK.name())
      || FUN.info('Nie odnaleziono dokumentu związanego z przekazanym linkiem.'@);
         _dalej:=0
      ?}
   ?}
|| FUN.emsg('Przekazany link nie jest związany z obszarem dokumentów księgowych.'@);
   _dalej:=0
?};

{? _dalej
|| _odd:=DOK.ODD;
   _okres:=null;
      _tokres:=DOK.name()+2;
      _trok:=2+(DOK.name()+4);
      ROK_F.cntx_psh(); OKRO_F.cntx_psh();
      ROK_F.index('KOD');
      ROK_F.prefix(REF.FIRMA,_trok);
      {? ROK_F.first()
      || OKRO_F.index('ROK');
         OKRO_F.prefix(ROK_F.ref(),#_tokres);
         {? OKRO_F.first() || _okres:=OKRO_F.ref() ?}
      ?};
      ROK_F.cntx_pop(); OKRO_F.cntx_pop();

      {? _odd & _okres
      || {? exec('usr_fjks','b_perm',_odd)
         || __PARSES.setVal('JednostkaKsięgowa',_odd);
            __PARSES.setVal('OkresRok',_okres)
         || FUN.emsg('Brak uprawnień do jednostki księgowej związanej z przekazanym linkiem.'@);
            _dalej:=0
         ?}
      || FUN.emsg('Nie udało się ustawić prawidłowych parametrów pracy na podstawie przekazanego linku.'@);
         _dalej:=0
      ?}
?};

_dalej


\link_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.42]
:: OPIS: Funkcja do obsługi przekazanego linku
::   WE: _a - link (STRING[48])
::   WY: 1/0 - czy ustawienia na podstawie linku poprawne
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? ref_tab(_a)=OP
|| _maska:=ref_name(_a);
   OP.use(_maska);
   OP.prefix();
   {? ~OP.seek(_a,OP.name())
   || FUN.info('Nie odnaleziono rekordu zgodnego z przekazanym linkiem.'@);
      _dalej:=0
   ?}
|? ref_tab(_a)=ZAP_OP
|| _maska:=ref_name(_a);
   ZAP_OP.use(_maska);
   ZAP_OP.prefix();
   {? ~ZAP_OP.seek(_a,ZAP_OP.name())
   || FUN.info('Nie odnaleziono rekordu zgodnego z przekazanym linkiem.'@);
      _dalej:=0
   || OP.use('operac'+(ZAP_OP.name()+2));
      OP.prefix();
      {? ~OP.seek(ZAP_OP.OP,OP.name())
      || FUN.info('Nie odnaleziono rozrachunku związanego z przekazanym linkiem.'@);
         _dalej:=0
      ?}
   ?}
|| FUN.emsg('Przekazany link nie jest związany z obszarem rozrachunków.'@);
   _dalej:=0
?};

{? _dalej
|| _odd:=OP.ODD;
   _okres:=null;
   _trok:=OP.name()+2;
   ROK_F.cntx_psh(); OKRO_F.cntx_psh();
   ROK_F.index('KOD');
   ROK_F.prefix(REF.FIRMA,_trok);
   {? ROK_F.first()
   || OKRO_F.index('ROK');
      OKRO_F.prefix(ROK_F.ref(),1);
      {? OKRO_F.first() || _okres:=OKRO_F.ref() ?}
   ?};
   ROK_F.cntx_pop(); OKRO_F.cntx_pop();

   {? _odd & _okres
   || {? exec('usr_fjks','b_perm',_odd)
      || __PARSES.setVal('JednostkaKsięgowa',_odd);
         __PARSES.setVal('OkresRok',_okres)
      || FUN.emsg('Brak uprawnień do jednostki księgowej związanej z przekazanym linkiem.'@);
         _dalej:=0
      ?}
   || FUN.emsg('Nie udało się ustawić prawidłowych parametrów pracy na podstawie przekazanego linku.'@);
      _dalej:=0
   ?}
?};

_dalej


\biq_fks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Analiza BI w dziedzinie Finanse
::----------------------------------------------------------------------------------------------------------------------
exec('np_run','#b__box','BIQ_%1A'[AreaTitle.area])


\fks_ksf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Główna formuła obszaru roboczego FKS_KSF - Dokumenty KSeF
::----------------------------------------------------------------------------------------------------------------------
exec('init','fks');

:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)
|| _link:=_params.LINK
|| _link:=''
?};

{? _link<>''
|| {? ~exec('link','fks',_link) || return() ?};
   exec('main','!fks_ksf_zprz',_link)
|| exec('main','!fks_ksf_zprz')
?};

~~


\sd_ins_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Udostępnia do edycji listę ustawień wydruków.
::   WE: _a [STRING] - symbol dziedziny
::  OLD: \settings/skid_xr.fml
::  OLD: \xr_ins_select/rap_zew.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | _a=''
|| return()
?};
_sym:=_a;

exec('sd_ins_dane','fks',_sym);

_TAB:=exec('sd_ins_panel','fks',_sym);
_TAB.select()


\sd_ins_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Uzupełnia dane konfiguracji i ustawień SD.
::   WE: _a [STRING] - symbol dziedziny
::   WY: ~~
::  OLD: \xr_ins_dane/rap_zew.fml
::----------------------------------------------------------------------------------------------------------------------
_dom:=exec('domain_ref','#b_domain',_a);
{? _dom=null
|| return()
?};

:: szablony dokumentów
SD_UINS.cntx_psh();
SD_UINS.index('STAN');
SD_BDEF.cntx_psh();
SD_BDEF.index('UNIQUE');
SD_BDEF.prefix(_dom);
_loop:=SD_BDEF.first();
{!
|? _loop
|! {? ~SD_UINS.find_key(SD_BDEF.SD_DEF,OPERATOR.USER,'U')
   || SD_UINS.blank();
      SD_UINS.SD_DEF:=SD_BDEF.SD_DEF;
      SD_UINS.USERS:=OPERATOR.USER;
      SD_UINS.RODZAJ:='U';
      SD_UINS.STAN:='N';
      SD_UINS.add()
   ?};
   _loop:=SD_BDEF.next()
!};
SD_BDEF.cntx_pop();
SD_UINS.cntx_pop();
~~


\sd_ins_panel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Tworzy tabelę i okna ustawień SD.
::   WE: _a [STRING] - symbol dziedziny
::   WY: alias tabeli tymczasowej
::  OLD: \xr_ins_panel/rap_zew.fml
::----------------------------------------------------------------------------------------------------------------------
_TAB:=exec('sd_ins_tabela','fks',_a);

_TAB.win_sel(_wnd:=_TAB.mk_sel('Ustawienia wydruków'@,'P',,'#sd_ins_config',,,,,'U'));
_TAB.win_fld(_wnd,,'SD_STAN',,,,,,,,MS.comment(SD_UINS,'STAN'),2,,"'T'","'N'","'X'");
_TAB.win_fld(_wnd,,'NAZWA',,,,,,,,MS.comment(SD_DEF,'NAZWA'));
_TAB.win_act(_wnd,,'Formuła','Zmień'@,,'Zmiana sposobu przygotowania wydruku.'@,
   "exec('sd_ins_zmien_b','fks',cur_tab(1,1))",
   "exec('sd_ins_zmien_a','fks',cur_tab(1,1))",
   1
);
_TAB.win_act(_wnd,,'Rekord',,,,"
   _TAB:=cur_tab(1,1);
   _mod:=0;
   {? _TAB.SD_STAN<>'X' & SD_UINS.seek(_TAB.SD_UINS,,1) & _TAB.SD_STAN<>SD_UINS.STAN
   || _TAB.SD_STAN:=SD_UINS.STAN;
      _mod:=1
   ?};
   {? _mod<>0
   || _TAB.put()
   ?};
   ~~
");
_TAB.win_act(_wnd,,'Szukaj');
_TAB.win_act(_wnd,,'Kolejność');
_TAB.win_act(_wnd,,'Okienko',,,,
   "  SD_UINS.cntx_psh();
      1
   ",
   "  SD_UINS.cntx_pop();
      1
   "
);
_TAB.win_btn(_wnd,'text=%1'['Zmień'@],'menu:Z');

_TAB.win_edit(_wnd:=_TAB.mk_edit('Ustawienie wydruku'@,,'#xr_ins_edit'));
_TAB.win_esep(_wnd,'Dane podstawowe'@);
_TAB.win_efld(_wnd,,'SD_STAN',,,,,,,,MS.comment(SD_UINS,'STAN'),'check-box',
   'left_label=1,check_label="%1"'['Użyj szablonów dokumentów'@],
   "'T'","'N'"
);
exec('ok_esc','#window',_TAB,_wnd,,,,,1);

_TAB.win_patt(_wnd:=_TAB.mk_edit('Ustawienie wydruku'@,,'#xr_ins_patt'));
_TAB.win_esep(_wnd,'Dane podstawowe'@);
_TAB.win_efld(_wnd,,'SD_STAN',,,,,,,,MS.comment(SD_UINS,'STAN'),'radio-buttons',,
   'Dowolny'@,"''",
   'Włączony'@,"'T'",
   'Wyłączony'@,"'N'",
   'Niedostępny'@,"'X'"
);
_TAB.win_efld(_wnd,,'NAZWA',,,MS.fld_len(XR_DEF,'OPIS'),,1,,,MS.comment(SD_DEF,'NAZWA'));
exec('ok_esc','#window',_TAB,_wnd);

_TAB


\sd_ins_tabela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Tworzy tabelę ustawień SD.
::   WE: _a [STRING] - symbol dziedziny
::   WY: alias tabeli tymczasowej
::  OLD: \xr_ins_tabela/rap_zew.fml
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_sym:=_a;

_dom:=exec('domain_ref','#b_domain',_sym);
{? _dom=null
|| return()
?};

:: tabela połączonych ustawień
_TAB:=tab_tmp(1,
   'NAZWA','STRING[%1]'[$MS.fld_len(SD_DEF,'NAZWA')],'Nazwa szablonów dokumentów'@,
   'SD_UINS','STRING[16]',SD_UINS.comment(),
   'SD_SYM','STRING[%1]'[$MS.fld_len(SD_DEF,'SYMBOL')],'Symbol szablonów dokumentów'@,
   'SD_STAN','STRING[1]','Szablon dokumentów'@
);

_TAB.fld_fml('NAZWA','BEFORE_DISPLAY',"cur_tab(1,1).SD_UINS<>''");

_TAB.fld_fml('SD_STAN','BEFORE_EDIT',"
   _TAB:=cur_tab(1,1);
   _TAB.SD_UINS<>'' & _TAB.SD_STAN<>'X'
");

:: zablokuj kolumny
_TAB.fld_attr(,2);

:: uzupełnij dane
SD_DEF.cntx_psh();
SD_UINS.cntx_psh();
SD_UINS.index('STAN');
SD_UINS.prefix();
SD_BDEF.cntx_psh();
SD_BDEF.index('UNIQUE');
SD_BDEF.prefix(_dom);
_loop:=SD_BDEF.first();
{!
|? _loop
|! {? SD_UINS.find_key(SD_BDEF.SD_DEF,OPERATOR.USER,'U')
   || _sds:=SD_BDEF.SD_DEF().SYMBOL;
      _TAB.blank();
      _TAB.NAZWA:=SD_BDEF.SD_DEF().NAZWA;
      _TAB.SD_UINS:=$SD_UINS.ref();
      _TAB.SD_SYM:=_sds;
      {? SD_BDEF.SD_DEF().DOSTEPNY='N'
      || _TAB.SD_STAN:='X';
         {? SD_UINS.STAN='T'
         || SD_UINS.STAN:='N';
            SD_UINS.put()
         ?}
      || _TAB.SD_STAN:=SD_UINS.STAN
      ?};
      _TAB.add()
   ?};
   _loop:=SD_BDEF.next()
!};
SD_BDEF.cntx_pop();
SD_UINS.cntx_pop();
SD_DEF.cntx_pop();

_TAB


\sd_ins_zmien_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Przed akcją "Zmień" okna ustawień SD.
::   WE: _a [TABLE] - alias tabeli ustawień
::   WY: 0/1 - porzucono edycję/wprowadzić zmiany
::  OLD: \xr_ins_zmien_b/rap_zew.fml
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;
_wnd:=_TAB.win_edit('?');

_TAB.efld_opt(_wnd,'enable=%1'[$(_TAB.SD_UINS<>'' & _TAB.SD_STAN<>'X')],,'SD_STAN');

_TAB.edit() & _TAB.put()


\sd_ins_zmien_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Obsługa akcji "Zmień" okna ustawień SD.
::   WE: _a [TABLE] - alias tabeli ustawień
::   WY: ~~
::  OLD: \xr_ins_zmien_a/rap_zew.fml
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_TAB:=_a;

SD_UINS.prefix();

{? _TAB.SD_STAN<>'X' & SD_UINS.seek(_TAB.SD_UINS)
|| SD_UINS.STAN:=_TAB.SD_STAN;
   SD_UINS.put()
?};
~~


\set_rej_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO/SG [23.25]
:: OPIS: Uprawnienia do rejestrów księgowych dla użytkownika
::   WE: _a - wskazanie na użytkownika lub tabela z użytkownikami.
::----------------------------------------------------------------------------------------------------------------------
:: lata bilansowe dla firmy
_grp_action:=var_pres('_a')=type_of(SYSLOG);
_res:=0;
VAR_DEL.delete('ROKF');
ROKF:=sql('
      select
         ROK_F.NAZ,
         ROK_F.REFERENCE as REF
      from
         ROK_F left join
         FIRMA using(ROK_F.FIRMA,FIRMA.REFERENCE)
      where
         FIRMA.REFERENCE=\':_a\'',
      $REF.FIRMA
   );
{? _grp_action
|| _usersTab:=exec('usr_tab','b_perm',_a);
::=== Tworzenie tymczaswoej tabeli w celu dodania pola typu checkbox dotyczącego uprawnień ===
   VAR_DEL.delete('tmp_REJ');
   tmp_REJ:=sql('select \'\' as ACCESS, ODD.OD, REJ.KOD, REJ.NAZ, REJ.ROK, REJ.REFERENCE as REF
                      from
                        REJ join ODD
                      where ODD.REFERENCE=REJ.ODD');
:: Tworzenie okna wertowania
   _tmp_win:=tmp_REJ.mk_sel('Uprawnienie do rejestrów'@,'T',0,,,,,,'U',,,,,'maximized');
   tmp_REJ.win_fld(_tmp_win,,'ACCESS',,,,,,'Uprawnienie'@,,,2,,"'T'","'N'","''");
   tmp_REJ.win_fld(_tmp_win,,'KOD',,,,,,'Symbol'@);
   tmp_REJ.win_fld(_tmp_win,,'NAZ',,,30,,,'Nazwa rejestru'@);
   tmp_REJ.win_fld(_tmp_win,,'OD',,,20,,,'Oddział'@);
:: Dodawanie akcji i odpowiedających przycisków do okna wertowania
   _formula:="tmp_REJ.ACCESS:='T'; tmp_REJ.put()";
   tmp_REJ.win_act(_tmp_win,,'Formuła','Nadaj'@@,,,_formula,,1,1,,,'N');
   tmp_REJ.win_btn(_tmp_win,'text=%1,panel=right,align=begin'['Nadaj'@],'menu:N');
   _formula:="tmp_REJ.ACCESS:='N'; tmp_REJ.put()";
   tmp_REJ.win_act(_tmp_win,,'Formuła','Odbierz'@@,,,_formula,,,1,,,'O');
   tmp_REJ.win_btn(_tmp_win,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O');
   _formula:="tmp_REJ.ACCESS:=''; tmp_REJ.put()";
   tmp_REJ.win_act(_tmp_win,,'Formuła','Nie zmieniaj'@@,,,_formula,,,1,,,'M');
   tmp_REJ.win_btn(_tmp_win,'text=%1,panel=right,align=begin'['Nie zmieniaj'@],'menu:M');
   accepted:=0;
   _formula:="accepted:=1;sel_exit()";
   tmp_REJ.win_act(_tmp_win,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A');
   tmp_REJ.win_btn(_tmp_win,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A');
   tmp_REJ.win_btn(_tmp_win,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
:: Indeks tymczasowy
   tmp_index:=tmp_REJ.ndx_tmp('Kolejność',0,'ROK',,0);
   tmp_REJ.index(tmp_index);
::  Tworzenie okna grupowego i zakładek
   _tmp_grpwin:=tmp_REJ.grp_make('Uprawnienia do rejestrów'@,,'upr_rej');
   {? ROKF.first()
   ||
      {!
      |? _id:=(6+ROKF.REF)+(ROKF.REF+8);
         tmp_REJ.grp_sel(_tmp_grpwin,tmp_REJ,_tmp_win,ROKF.NAZ,"",,,,
:     przed obsluga
            "tmp_REJ.prefix(6+cur_win_id+'  '+(cur_win_id+8))",,,,'maximized',_id
         );
         ROKF.next()
      !}
   ?};
   tmp_REJ.win_sel(_tmp_grpwin);
   tmp_REJ.select();
   tmp_REJ.prefix();
:: Przeszukanie tabeli tmp_REJ w poszukiwaniu zmiań w uprawnieniach
   {? tmp_REJ.first() & accepted
   || {!
      |?
:: Nadanie uprawnień wybranym użytkownikom
         {? tmp_REJ.ACCESS='T'
         || {? _usersTab.first()
            ||
               {!
               |? USERSREJ.prefix();
                  USERSREJ.blank();
                  USERS.seek(_usersTab.REF,);
                  REJ.seek(tmp_REJ.REF,);
                  USERSREJ.USER:=USERS.ref();
                  USERSREJ.ODD:=tmp_REJ.OD;
                  USERSREJ.REJ:=REJ.ref();
                  USERSREJ.add(1);
                  _usersTab.next()
               !}
            ?}
         ?};
:: Odebranie uprawnień wybranym użytkownikom
         {? tmp_REJ.ACCESS='N'
         || {? _usersTab.first()
            || REJ.seek(tmp_REJ.REF,);
               {!
               |?
                  USERS.seek(_usersTab.REF,);
                  USERSREJ.cntx_psh();
                  USERSREJ.index('REJ_ROK');
                  USERSREJ.prefix(USERS.ref(),REJ.ROK,REJ.KOD);
                  {? USERSREJ.first()
                  || USERSREJ.del()
                  ?};
                  USERSREJ.cntx_pop();
                  _usersTab.next()
               !}
            ?}
         ?};
         tmp_REJ.next()
      !}
   ?};
   VAR_DEL.delete('tmp_REJ');
   VAR_DEL.delete('accepted')
||
   USERSREJ.cntx_psh();
   USERSREJ.index('REJ_ROK');
   _grp:=USERSREJ.grp_make('Uprawnienia do rejestrów'@,,'upr_rej');
   _loop:=ROKF.first();
   {!
   |? _loop
   |!
      _id:=(6+ROKF.REF)+(ROKF.REF+8);
      USERSREJ.grp_sel(_grp,USERSREJ,'WER',ROKF.NAZ,
:     po odswiezeniu
         "",
:     polozenie i wysokosc
         ,,,
:     przed obsluga
         "
            _ref:=(cur_win_id+8);
            USERSREJ.prefix(USERS.ref(),BIT.sqlint(_ref))
         ",
:     po obsludze
         "",,,'maximized',_id
:     utrwalenie, aktywacja, wypelnienie

      );
      _loop:=ROKF.next()
   !};
   USERSREJ.win_sel(_grp);
   USERSREJ.select();
   USERSREJ.cntx_pop()
?};
VAR_DEL.delete('ROKF');

_perm:=null();
B_PERM.cntx_psh();
B_PERM.index('NAME'); B_PERM.prefix();
{? B_PERM.find_key('FREJ')
|| _perm:=B_PERM.ref()
?};
B_PERM.cntx_pop();

B_PERM_U.cntx_psh();
B_PERM_U.index('USER');
B_PERM_U.prefix(REF.FIRMA,USERS.ref(),_perm);
{? B_PERM_U.first()
|| B_PERM_U.FULL:={? exec('usersrej_check','fks',$USERS.ref()) || 'T' || 'N' ?};
   B_PERM_U.put()
?};
B_PERM_U.cntx_pop()


\usersrej_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS: Formuła na akcję "Dołącz - przed" dla tabeli USERSREJ, okienko wertowanie WER.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: weryfikacja uprawnień do jednostek księgowych dla użytkownia - ograniczenie dziedziny rejestrów
:: uprawnienia do oddziałów USERSDEP ograniczone są już w wyżej instancji

:: pobranie informacji o roku bilansowym w danej zakładce
_rokb:=(6+cur_win_id)+'  '+(cur_win_id+8);
ROK_F.cntx_psh();
ROK_F.prefix();
{? ROK_F.seek(_rokb)
|| _rokn:=ROK_F.NAZ
|| _rokn:=''
?};
ROK_F.cntx_pop();
REJ.cntx_psh();
:REJ.index('FIRMA');
REJ.f_clear(1);
_usersdep:=sql('select USERSDEP.DEPT as REF from USERSDEP join USERS join FIRMA where USERS.REFERENCE=\':_a\' and FIRMA.REFERENCE=\':_b\'',$USERS.ref(),$REF.FIRMA);
{? _usersdep.size()
|| REJ.f_set(,'join ROK_F using(REJ.ROK, ROK_F.REFERENCE) join ODD using(REJ.ODD, ODD.REFERENCE) join FIRMA using(ODD.FIRMA,FIRMA.REFERENCE)',
              'FIRMA.REFERENCE=\':_a\' and ROK_F.REFERENCE=\':_b\' and REJ.ODD IN (select REF from :_c)',$REF.FIRMA,_rokb,_usersdep)
|| REJ.f_set(,'join ROK_F using(REJ.ROK, ROK_F.REFERENCE) join FIRMA using(ROK_F.FIRMA,FIRMA.REFERENCE)',
              'FIRMA.REFERENCE=\':_a\' and ROK_F.REFERENCE=\':_b\'',$REF.FIRMA,_rokb)
?};
REJ.win_sel('WYB_GRPU');
REJ.select();
REJ.cntx_pop();
0


\usersrej_dolacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS: Formuła na akcję "Dołącz - po" dla tabeli USERSREJ, okienko wertowanie WER.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
1


\rej_grp_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS: Formuła na akcję "Wybierz - przed" dla tabeli USERSREJ, okienko wertowanie WER.
::   WE: _a - ref wybranego rejestru
::----------------------------------------------------------------------------------------------------------------------
_reg:=REJ.sel_aget();
_loop:=_reg.first();
{? _loop
|| {!
   |? _loop
   |! {? REJ.f_seek(_reg.REF,)
      || USERSREJ.REJ:=REJ.ref();
         USERSREJ.USER:=USERS.ref();
         USERSREJ.ODD:=REJ.ODD().OD;
         USERSREJ.add(1);
         _loop:=_reg.next()
      || _loop:=_reg.next()
      ?}
   !}
|| USERSREJ.REJ:=REJ.ref();
   USERSREJ.USER:=USERS.ref();
   USERSREJ.ODD:=REJ.ODD().OD;
   USERSREJ.add(1)
?};
sel_exit()


\usersrej_copy_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS: Kopiuje wskazane przez użytkownika uprawnienia do rejestrów księgowych (Akcja - przed)
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__usrrej');
_rok:=ROKF.mk_sel('Lata bilansowe');

ROKF.win_fld(_rok,,'NAZ');
ROKF.win_act(_rok,0,'Formuła','Wybierz'@,,'Kopiuj zaznaczone uprawnienia do wybranego roku',"","sel_exit()",1);
ROKF.win_sel(_rok);
__usrrej:=USERSREJ.sel_aget();
ROKF.select()


\usersrej_copy_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS: Kopiuje wskazane przez użytkownika uprawnienia do rejestrów księgowych (Akcja - po)
::----------------------------------------------------------------------------------------------------------------------
_tmp:=tab_tmp(,'KOD','STRING[8]',,'ODD','INTEGER',);

{? __usrrej.size()=0
|| {? USERSREJ.get()
   || _tmp.KOD:=USERSREJ.REJ().KOD;
      _tmp.ODD:=USERSREJ.REJ().ODD;
      _tmp.add()
   ?}
||
   USERSREJ.cntx_psh();
   USERSREJ.prefix();
   _loop:=__usrrej.first();
   {!
   |? _loop
   |! {? USERSREJ.seek(__usrrej.REF,)
      || _tmp.KOD:=USERSREJ.REJ().KOD;
         _tmp.ODD:=USERSREJ.REJ().ODD;
         _tmp.add();
         _loop:=__usrrej.next()
      || _loop:=__usrrej.next()
      ?}
   !};
   USERSREJ.cntx_pop()
?};

REJ.cntx_psh();
REJ.index('KOD');
USERSREJ.cntx_psh();
USERSREJ.index('REJ_ROK');
USERSREJ.prefix(USERS.ref(),BIT.sqlint(ROKF.REF));

_loop:=_tmp.first();
{!
|? _loop
|! REJ.prefix(BIT.sqlint(ROKF.REF),_tmp.ODD,_tmp.KOD);
   {? REJ.first()
   || USERSREJ.REJ:=REJ.ref();
      USERSREJ.USER:=USERS.ref();
      USERSREJ.add(1)
   ?};
   _loop:=_tmp.next()
!};
USERSREJ.cntx_pop();
REJ.cntx_pop();
win_disp()


\usersrej_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS: Usuwanie uprawnień do rejestrów użytkownika (formuła wykonywana po usunięciu uprawnień do Jednostki Księgowej)
::   WE: _a - nazwa jednostki księgowej
::----------------------------------------------------------------------------------------------------------------------
USERSREJ.cntx_psh();
USERSREJ.index('REJ');
USERSREJ.prefix(USERS.ref());
_loop:=USERSREJ.first();
{!
|? _loop
|! {? USERSREJ.ODD=_a
   || _loop:=USERSREJ.del()
   || _loop:=USERSREJ.next()
   ?}
!};
USERSREJ.cntx_pop()


\usersrej_check
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS: Sprawdza czy użytkownik ma wszystkie uprawnienia do rejestrów księgowych
::       (W wybranym w parametrach pracy roku bilansowym)
::   WE: _a [STRING] - wskazanie na użytkownika (opcjonalne, ref_sql)
::----------------------------------------------------------------------------------------------------------------------
_user:={? var_pres('_a')=type_of('') || _a || $OPERATOR.USER ?};
_rej:=0;
_userupr:=sql('
   select
      USERSREJ.REJ
   from
      USERSREJ join
      USERS using(USERSREJ.USER,USERS.REFERENCE) left join
      REJ using(USERSREJ.REJ,REJ.REFERENCE) join
      ROK_F using(REJ.ROK,ROK_F.REFERENCE)
   where
      ROK_F.REFERENCE=\':_a\' and USERSREJ.USER=\':_b\''
   ,$SSTALE.AR,_user
);

REJ.cntx_psh();
REJ.index('KOD');
REJ.prefix(SSTALE.AR);
{? REJ.first()
|| _rej:=REJ.size()
?};
REJ.cntx_pop();

{? _userupr.size()=_rej
|| _wyn:=1
|? _userupr.size()=0
|| _wyn:=1
|| _wyn:=0
?};
_wyn

:Sign Version 2.0 jowisz:1045 2023/12/21 13:22:35 221c4d12951772b6323375122a1ab0c6bb387982fb8fc0744d8a81324a8ca62566770f285379143f27b978abfb990c5dcc50dd009a973828be27bddf959ee60678fda762601abfce97166d0b6ec6a9a80539a45b690bcd4d6ad7a910f038b2595af377bdf34348f75a86dfdc338f96bf74993d1ad37932289ce1518c48982a21

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: auto_cit.fml
:: Utworzony: 17.01.2020
:: Autor: MB
:: Systemy: FIKS
::======================================================================================================================
:: Zawartosc: Formuly do obslugi korekt CIT za złe długi
::======================================================================================================================


\kor_cit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Główna formuła automatu księgującego korektę za złe długi dla CIT
::   WE: [_a] - Rodzaj kwoty: [N]-netto B-brutto
::        _b  - maska konta lub formuła zwracająca konto korekty
::       [_c] - korekta po stronie rozrachunku: [1]-tak 0-po przeciwnej
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('C_DATA',ROZRACH)<=0 || FUN.info('Brak wymaganych zmian w definicji systemu.'@) ?};
_netto:=var_pres('_a')<=0 | -_a='n';
_maska:=_b;
_kor:={? var_pres('_c')>0 || _c || 1 ?};
ROZRACH.win_edit('KOR_CIT');
exec('set_fml','auto_cit',1);
exec('set_mark','auto_cit');
ROZRACH.C_OPIS:='Korekta ('+{? _netto || 'netto' || 'brutto' ?}+') CIT za złe długi';
{? ROZRACH.edit("exec('after_edit','auto_cit')")
|| _tab:=exec('op','auto_cit',_netto,_maska,_kor);
   _tab.hdr_sel();
   _tab.hdr_sel(' - '+POMOC.WAL().KOD);
   {? _tab.select()
   || {? _tab.first()
      || _jedn:={? POMOC.WAL<>FINFO.NAROD || exec('jed_wal','waluty',POMOC.WAL().KOD) ?};
         {!
         |? {? _tab.LWN
            || {? POMOC.WAL=FINFO.NAROD
               || exec('poz_add','dekret_aut',_tab.LWN,'Wn',_tab.KON,_tab.SYM,'INN',_tab.DO,_tab.TZ,_tab.OPIS)
               || exec('poz_add','dekret_aut',(_tab.LWN*_tab.KURS/_jedn)$2,'Wn',_tab.KON,_tab.SYM,'INN',_tab.DO,_tab.TZ,
                     _tab.OPIS,POMOC.WAL().KOD,_tab.LWN,_tab.KURS
                  )
               ?}
            ?};
            {? _tab.LMA
            || {? POMOC.WAL=FINFO.NAROD
               || exec('poz_add','dekret_aut',_tab.LMA,'Ma',_tab.KON,_tab.SYM,'INN',_tab.DO,_tab.TZ,_tab.OPIS)
               || exec('poz_add','dekret_aut',(_tab.LMA*_tab.KURS/_jedn)$2,'Ma',_tab.KON,_tab.SYM,'INN',_tab.DO,_tab.TZ,
                     _tab.OPIS,POMOC.WAL().KOD,_tab.LMA,_tab.KURS
                  )
               ?}
            ?};
            _tab.next()
         !}
      ?}
   ?}
?};
exec('set_fml','auto_cit',0);
1


\set_fml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia obsługę pól okna parametrów automatu do złych długów CIT
::   WE: _a - 1-ustawienie 0-wyczyszczenie
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| ROZRACH.fld_fml('C_T_KON','AFTER_EDIT',"
      {? ROZRACH.C_T_KON=1
      || ROZRACH.C_K2:=ROZRACH.C_K3:=ROZRACH.C_K4:='';
         ROZRACH.C_K1:=35*'?'
      |? ROZRACH.C_T_KON=2
      || ROZRACH.C_K1:=ROZRACH.C_K3:=ROZRACH.C_K4:=''
      |? ROZRACH.C_T_KON=3
      || ROZRACH.C_K1:=ROZRACH.C_K2:=''
      ?};
      exec('set_mark','auto_cit');
      win_disp();
      1
   ");
   {! _i:=1..4
   |! ROZRACH.fld_fml('C_K'+$_i,'PATTERN',"exec('wz_konto','edkonto','ROZRACH',cur_afld())");
      ROZRACH.fld_fml('C_K'+$_i,'BEFORE_DISPLAY',"
         {? exec('be_konto','auto_cit')
         || ''
         || exec('findfnrd','color')
         ?}
      ");
      ROZRACH.fld_fml('C_K'+$_i,'BEFORE_EDIT',"
         {? exec('be_konto','auto_cit')
         || exec('be_konto','edkonto','ROZRACH',cur_afld())
         ?}
      ");
      ROZRACH.fld_fml('C_K'+$_i,'F3',"exec('f3_konto','edkonto','ROZRACH',cur_afld())")
   !};
   ROZRACH.fld_fml('C_K1','AFTER_EDIT',"exec('ae_konto','edkonto',_a,'ROZRACH','C_K1',0,2,0)");
   ROZRACH.fld_fml('C_K2','AFTER_EDIT',"exec('ae_konto','edkonto',_a,'ROZRACH','C_K2',0,3,0)");
   ROZRACH.fld_fml('C_K3','AFTER_EDIT',"exec('ae_konto','edkonto',_a,'ROZRACH','C_K3',0,3,0)");
   ROZRACH.fld_fml('C_K4','AFTER_EDIT',"exec('ae_konto','edkonto',_a,'ROZRACH','C_K4',0,3,0)");

   ROZNE.ROKKON:=SSTALE.AR;
   ROZRACH.C_DATA:=DOK.DTW;
   ROZRACH.C_TYP:='N';
   ROZRACH.C_T_KON:=1;
   ROZRACH.C_K1:=35*'?';
   ROZRACH.C_K2:=ROZRACH.C_K3:=ROZRACH.C_K4:='';
   POMOC.WAL:=SSTALE.WAL;
   ROZRACH.C_KOR:='T'
|| ROZRACH.fld_fml('C_T_KON','AFTER_EDIT',"*");
   {! _i:=1..4
   |! ROZRACH.fld_fml('C_K'+$_i,'PATTERN',"*");
      ROZRACH.fld_fml('C_K'+$_i,'BEFORE_EDIT',"*");
      ROZRACH.fld_fml('C_K'+$_i,'F3',"*");
      ROZRACH.fld_fml('C_K'+$_i,'AFTER_EDIT',"*")
   !}
?};
1


\set_mark
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Ustawia znacznik wymagalności pola w oknie redagowania
::----------------------------------------------------------------------------------------------------------------------
_red:='KOR_CIT';
ROZRACH.efld_opt(_red,'mark=1',ROZRACH,'C_DATA');
ROZRACH.efld_opt(_red,'mark='+$(ROZRACH.C_T_KON=1),ROZRACH,'C_K1');
ROZRACH.efld_opt(_red,'mark='+$(ROZRACH.C_T_KON=2),ROZRACH,'C_K2');
ROZRACH.efld_opt(_red,'mark='+$(ROZRACH.C_T_KON=3),ROZRACH,'C_K3');
ROZRACH.efld_opt(_red,'mark='+$(ROZRACH.C_T_KON=3),ROZRACH,'C_K4');
ROZRACH.efld_opt(_red,'mark=1',POMOC,'WAL');
~~

\be_konto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Formuła przed redakcją kont
::   WE: _a - akronim pola
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
ROZRACH.C_T_KON=1 & _fld='C_K1' |
ROZRACH.C_T_KON=2 & _fld='C_K2' |
ROZRACH.C_T_KON=3 & 'C_K4,C_K3'*_fld


\after_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po zakończeniu redakcji okna parametrów automatu
::----------------------------------------------------------------------------------------------------------------------
_r:=chk_rec('C_DATA');
{? _r=''
|| {? ROZRACH.C_T_KON=1
   || _r:=chk_rec('C_K1')
   |? ROZRACH.C_T_KON=2
   || _r:=chk_rec('C_K2')
   |? ROZRACH.C_T_KON=3
   || _r:=chk_rec('C_K3','C_K4');
      {? _r='' & ROZRACH.C_K3>ROZRACH.C_K4
      || _r:='C_K3';
         FUN.info('Nieprawidłowy zakres kont.')
      ?}
   ?}
?};
{? _r=''
|| _r:=chk_rec('WAL')
?};
_r


\op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca tabelę z rozrachunkami
::   WE:  _a - Rodzaj kwoty: 1-netto 0-brutto
::        _b - maska konta lub formuła na konto przeksięgowań
::        _c - korekta po stronie rozrachunku: 1-tak 0-po przeciwnej
::----------------------------------------------------------------------------------------------------------------------
_netto:=_a;
_konta:=_kon1:=_kon2:='';
{? ROZRACH.C_T_KON=1 | ROZRACH.C_T_KON=2
|| _konta:='and OP.AN like \':_d\'';
   _kon1:={? ROZRACH.C_T_KON=1 || ROZRACH.C_K1 || ROZRACH.C_K2+'*' ?};
   {? _kon1+1='?'
   || {!
      |? _kon1:=_kon1-1;
         _kon1+1='?'
      !};
      _kon1+='*'
   ?};
   _kon1:=STR.gsub(_kon1,'?','_');
   _kon1:=STR.gsub(_kon1,'*','%')
|? ROZRACH.C_T_KON=3
|| _konta:='and (OP.AN between \':_d\' and \':_e\') ';
   _kon1:=STR.gsub(ROZRACH.C_K3,'?','_'); _kon1:=STR.gsub(_kon1,'*','%');
   _kon2:=STR.gsub(ROZRACH.C_K4,'?','_'); _kon1:=STR.gsub(_kon2,'*','%');
   _kon2+=%255
?};
_tab:=sql(
   'select distinct '+
      'OP.REFERENCE as REF, ODD.OD, OP.AN, OP.SYM, OP.TZ, OP.TYP, KH.SKR, KH.SNIP, OP.DO, '+
      'cast(0 as REAL_TYPE) as WN, cast(0 as REAL_TYPE) as MA, '+
      'cast(0 as REAL_TYPE) as KURS, '+
      'cast(0 as REAL_TYPE) as SWN, cast(0 as REAL_TYPE) as SMA, '+
      'cast(0 as REAL_TYPE) as LWN, cast(0 as REAL_TYPE) as LMA, '+
      '\'                                   \' as KON, '+
      '\'                                                                                                                                                                                                                                                               \' as OPIS, '+
      '0 as ZW, '+
      '0 as ERR '+
   'from '+
      'OP join KH join ODD left join KH_DOD '+
   'where '+
      'OP.TYP like \':_a\' '+
      'and OP.WAL=:_b '+
      {? OPERATOR.DEPT || 'and OP.ODD=:_c' || '' ?}+
      'and OP.TZ>=to_date(:_f) '+
      {? var_pres('DSP',OP)>0 || 'and OP.DSP>to_date(:_g)' || '' ?}+
      {? _konta<>'' || _konta || '' ?}+
      'and KH_DOD.STATUS!=\'T\' '+
   'order by AN,TZ',
   ROZRACH.C_TYP+'%',POMOC.WAL,OPERATOR.DEPT,_kon1,_kon2,date(2020,1,1),
   exec('data','auto_cit',ROZRACH.C_DATA,2)
);
OP.cntx_psh();
OP.prefix();
{? _tab.first()
|| _obj:=exec('obj','auto_cit');
   _wal:=POMOC.WAL<>FINFO.NAROD;
   _par:=exec('obj','datepar');
   _par.system('FKS').par('KOR_CIT',1);
   {!
   |? _del:=1;
      {? OP.seek(BIT.sqlint(_tab.REF),) &
         (_wal | exec('one_wal','auto_cit')) &
         OP.TZ<=ROZRACH.C_DATA-_par.value(OP.TZ,90)
      || _obj.Saldo(OP.ref(),ROZRACH.C_DATA,_netto);
         _wn:={? _netto || _obj.WN-_obj.WNV || _obj.WN ?};
         _ma:={? _netto || _obj.MA-_obj.MAV || _obj.MA ?};
         _tab.WN:=F.SWn(_wn,_ma);
         _tab.MA:=F.SMa(_wn,_ma);
         _tab.ZW:=ROZRACH.C_DATA-OP.TZ;
         _tab.KON:=exec('konto','auto_cit',_b,OP.AN);
         {? exec('kon_ok','auto_cit',_tab.KON)=0
         || _tab.ERR:=1
         ?};
         {? _wal
         || _tab.KURS:=exec('kurs','auto_cit')
         ?};
         OP.cntx_psh();
         OP.index('KON_O');
         OP.prefix(OP.WAL,OP.ODD,_tab.KON,OP.SYM,OP.SYM,OP.SYM_ROK,);
         {? OP.first()
         || _obj.Saldo(OP.ref(),ROZRACH.C_DATA,0);
            _tab.SWN:=_obj.WN;
            _tab.SMA:=_obj.MA
         || _tab.SWN:=_tab.SMA:=0
         ?};
         OP.cntx_pop();
         {? _c=1
         || _wn:=_tab.WN-_tab.SWN;
            _ma:=_tab.MA-_tab.SMA;
            {? _wn>_ma
            || _tab.LWN:=_wn-_ma;
               _tab.LMA:=0
            || _tab.LWN:=0;
               _tab.LMA:=_ma-_wn
            ?}
         || _wn:=_tab.WN-_tab.SMA;
            _ma:=_tab.MA-_tab.SWN;
            {? _wn
            || {? _wn<0
               || _tab.LWN:=-_wn;
                  _tab.LMA:=0
               || _tab.LMA:=_wn;
                  _tab.LWN:=0
               ?}
            || {? _ma<0
               || _tab.LMA:=-_ma;
                  _tab.LWN:=0
               || _tab.LWN:=_ma;
                  _tab.LMA:=0
               ?}
            ?}
         ?};
         {? _tab.LWN | _tab.LMA | ROZRACH.C_KOR='N'
         || _tab.OPIS:=ROZRACH.C_OPIS;
            _tab.put();
            _del:=0
         ?}
      ?};
      {? _del || _tab.del() || _tab.next() ?}
   !}
?};
OP.cntx_pop();
exec('set_win','auto_cit',_tab,_netto);
_tab


\set_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tworzy okno wertowania dla tabeli
::   WE: _a - tabela
::       _b - 1-netto 0-brutto
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_o:=_tab.mk_sel('Korekta ('+{? _b || 'netto' || 'brutto' ?}+') CIT za złe długi','P',,'#opkorzd',,,,,'U');
_tab.win_fld(_o,,'OD',,,,,,'Jedn. ks.');
_tab.win_fld(_o,,'AN',,,20,,,'Konto');
_tab.win_fld(_o,,'SYM',,,,,,'Symbol');
_tab.win_fld(_o,,'TZ',,,,,,'Termin zapłaty');
_tab.win_fld(_o,,'ZW',,,,,,'Zwłoka');
_tab.win_fld(_o,,'TYP',,,,,,'Typ');
_tab.win_fld(_o,,'SKR',,,,,,'Kontrahent');
_tab.win_fld(_o,,'SNIP',,,,,,'NIP');
_tab.win_fld(_o,,'WN',,,,2,,'Saldo Wn');
_tab.win_fld(_o,,'MA',,,,2,,'Saldo Ma');
_tab.win_fld(_o,,'SWN',,,,2,,'Korekta Wn');
_tab.win_fld(_o,,'SMA',,,,2,,'Korekta Ma');
_tab.win_fld(_o,,'LWN',,,,2,,'Do korekty Wn');
_tab.win_fld(_o,,'LMA',,,,2,,'Do korekty Ma');
_tab.win_fld(_o,,'OPIS',,,30,,,'Opis');
_tab.win_act(_o,,'Formuła','Popraw',,,"exec('modify','auto_cit')",,,1,"exec('bg_modify','auto_cit')");
_tab.win_act(_o,,'Formuła','pokaż Rozrachunek',,,"exec('show','auto_cit',1)");
_tab.win_act(_o,,'Formuła','pokaż kOrekty',,,"exec('show','auto_cit',2)");
_tab.win_act(_o,,'Formuła','Dekretuj',,,"sel_exit()");
_tab.win_act(_o,,'Formuła','Legenda',,,"exec('legenda','color','KOR_CIT#01')");
_tab.win_act(_o,,'Kolejność');
_tab.win_act(_o,,'Wyświetl',,,,"exec('display','auto_cit')");
_tab.win_sel(_o);
_tab.win_fml(_o,,'AN',,'ICON_BEFORE',"{? cur_tab().ERR || 'xwin16.png:4' || '' ?}");

_e:=_tab.mk_edit('Korekta CIT za złe długi',,'opkord');
_tab.win_efld(_e,,'LWN',,,,2,,'Do korekty Winien');
_tab.win_efld(_e,,'LMA',,,,2,,'Do korekty Ma');
_tab.win_efld(_e,,'OPIS',,,100,,,'Opis');
_tab.win_edit(_e)


\obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obiekt do obliczeń sald rozrachunku
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'Saldo',
   'WN','MA','WNV','MAV'
);
_obj.Saldo:="
   .WN:=.MA:=.WNV:=.MAV:=0;
   ZAP_OP.cntx_psh();
   ZAP_OP.index('OP'); ZAP_OP.prefix(_a);
   {? ZAP_OP.first()
   || _brutto:=_vat:=0;
      {? _c
      || OP.cntx_psh();
         _typ:=ZAP_OP.OP().TYP;
         OP.cntx_pop();
         {!
         |? {? _brutto=0
            || {? 1+_typ='N' & ZAP_OP.WN || _brutto:=ZAP_OP.WN; _vat:=ZAP_OP.SP_V_WN
               |? 1+_typ='Z' & ZAP_OP.MA || _brutto:=ZAP_OP.MA; _vat:=ZAP_OP.SP_V_MA
               ?}
            ?};
            _brutto=0 & ZAP_OP.next()
         !};
         ZAP_OP.first()
      ?};
      {!
      |? {? ZAP_OP.DATA<=_b
         || .WN+=ZAP_OP.WN;
            .MA+=ZAP_OP.MA;
            {? _c
            || {? _brutto & ZAP_OP.WN & ZAP_OP.SP_V_WN=0
               || ZAP_OP.SP_V_WN:=(ZAP_OP.WN*_vat/_brutto)$2
               |? _brutto & ZAP_OP.MA & ZAP_OP.SP_V_MA=0
               || ZAP_OP.SP_V_MA:=(ZAP_OP.MA*_vat/_brutto)$2
               ?};
               .WNV+=ZAP_OP.SP_V_WN;
               .MAV+=ZAP_OP.SP_V_MA
            ?};
            ZAP_OP.next()
         ?}
      !}
   ?};
   ZAP_OP.cntx_pop()
";
_obj


\konto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca konto przeksięgowań
::   WE: _a - maska konta lub formuła
::       _b - konto rozrachunku
::----------------------------------------------------------------------------------------------------------------------
{? type_of(_a)=type_of('')
|| _str:='';
   {! _i:=1..+_b
   |! _zm:=_i+_a+1;
      _zk:=_i+_b+1;
      {? _zm='?'
      || _str+=_zk
      || _str+=_zm
      ?}
   !};
   _str
|| _a(_b)
?}


\kh_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy kontrahent powiązany z firmą
::----------------------------------------------------------------------------------------------------------------------


\modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Akcja popraw
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
{? _tab.sel_size()
|| _tab.LWN:=GrpMod.LWN$2;
   _tab.LMA:=GrpMod.LMA$2;
   _tab.OPIS:=GrpMod.OPIS;
   _tab.put()
|? _tab.edit()
|| _tab.LWN:=_tab.LWN$2;
   _tab.LMA:=_tab.LMA$2;
   _tab.put()
?}


\bg_modify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed grupową akcją popraw
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
{? FUN.ask('Czy poprawić zaznaczone korekty?') & _tab.edit()
|| VAR_DEL.delete('GrpMod');
   GrpMod:=obj_new('LWN','LMA','OPIS');
   GrpMod.LWN:=_tab.LWN;
   GrpMod.LMA:=_tab.LMA;
   GrpMod.OPIS:=_tab.OPIS;
   1
?}


\display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Formuła na wyświetl prezentująca rozrachunek
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
OP.cntx_psh();
OP.prefix();
{? OP.seek(BIT.sqlint(_tab.REF),)
|| OP.win_edit('RED');
   OP.display()
|| FUN.info('Nie udało się znaleźć rozrachunku.')
?};
OP.cntx_pop()


\show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Pokazuje rozrachunek
::   WE: _a - 1 bazowy
::            2 korekty
::----------------------------------------------------------------------------------------------------------------------
_esc:=BEER.ESC;
BEER.ESC:=1;
_tab:=cur_tab();
_op:=null();
OP.cntx_psh();
OP.prefix();
{? OP.seek(BIT.sqlint(_tab.REF),)
|| {? _a=1
   || _op:=OP.ref()
   || OP.index('KON_O');
      OP.prefix(POMOC.WAL,OP.ODD,_tab.KON,OP.SYM,OP.SYM,OP.SYM_ROK,);
      {? OP.first()
      || {!
         |? {? OP.TYP='INN' || _op:=OP.ref() ?};
            _op=null() & OP.next()
         !}
      ?}
   ?}
?};
OP.cntx_pop();
{? _op<>null()
|| exec('op_przeg','!fks_roz_zark',_op,'DPOMNJ')
|| FUN.info('Nie udało się znaleźć rozrachunku.'@)
?};
BEER.ESC:=_esc


\kon_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy konto dla korekty jest poprawne
::   WE: _a - konto
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
KS.cntx_psh();
KS.index('SYM'); KS.prefix(SSTALE.AR,SSTALE.AR().SYNT+_a,);
{? KS.first()
|| _ok:=KS.TYP<>'Z' & (KS.OBTYPROZ=0 | KS.T_ROZ='' | KS.T_ROZ='INN') & (POMOC.WAL=FINFO.NAROD | KS.WIELO='T')
?};
KS.cntx_pop();
_ok


\one_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy rozrachunek jednowalutowy
::----------------------------------------------------------------------------------------------------------------------
_one:=1;
OP.cntx_psh();
OP.index('KON_O2');
OP.prefix(OP.ODD,OP.AN,OP.SYM,OP.SYM_ROK,);
{? OP.first()
|| _one:=~OP.next()
?};
OP.cntx_pop();
_one


\kurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca kurs dla rozrachunku walutowego
::----------------------------------------------------------------------------------------------------------------------
_kurs:=0;
ZAP_OP.cntx_psh();
ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
{? ZAP_OP.first()
|| {!
   |? _kurs:=ZAP_OP.KURS;
      _kurs=0 & ZAP_OP.next()
   !}
?};
ZAP_OP.cntx_pop();
_kurs


\data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca datę za _b lat wstecz
::   WE: _a - data
::       _b - liczba lat wstecz
::----------------------------------------------------------------------------------------------------------------------
{!
|? _msg:=no_msg(1);
   on_error(3);
   _data:=date(_a~1-_b,_a~2,_a~3);
   on_error(0);
   no_msg(_msg);
   {? errno()
   || _a-=1;
      1
   ?}
!};
_data

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:16 6b6cb7f8c347ce8de6f366253a7d2726ad484ca2ae05790b54549e21c073088fc5b80b0aa24011c1d0381813dcb3b0ca3b2ef96889f9c0c5e9627fcbc52d62f3ba3c6b957cd3d2f31d107a78b7bcfb9a04a0d8e8443dff032c8a92897e4fe92cc3c9abd56d08023f9088da08e72a2372289ca141a205aa0f5f66852e7e098c93

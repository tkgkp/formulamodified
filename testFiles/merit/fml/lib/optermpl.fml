:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: optermpl.fml [12.51]
:: Utworzony: 16.12.2020
:: Autor: MB
:: Systemy: FIKS
::======================================================================================================================
:: Zawartosc: Formuly uzywane w sprawozdaniu o stosowanych terminach zapłaty w transakcjach handlowych
::======================================================================================================================


\start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Główna formuła uruchamiająca analizę
::   WE: [_a] - czy analizować rozrachunki dla dokumentów VAT: [1]-tak 0-wszystkie
::       [_b] - formuła warunkowa na rozrachunek
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('ParObj');
ParObj:=obj_new('SYNT','SA','VAT','FML','PAR37');
ParObj.VAT:={? var_pres('_a')>0 || _a || 1 ?};
ParObj.FML:={? var_pres('_b')>0
            || {? var_pres('_b')=type_of('')
               || $_b
               || _b
               ?}
            || ~~
            ?};
ParObj.PAR37:=PAR_SKID.get(37);
{? exec('par','optermpl')
|| VAR_DEL.delete('SprObj');
   _tab:=exec('create_tab','optermpl');
   exec('raty_obj','optermpl');
   exec('fill_tab','optermpl',_tab);
   _obj:=exec('spr_obj','optermpl',_tab);
   _tab.select();
   VAR_DEL.delete('SprObj','RatyObj')
?};
VAR_DEL.delete('ParObj');
0


\par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Edycja parametrów analizy
::----------------------------------------------------------------------------------------------------------------------
_edit:=PAR_WYDR.mk_edit('Sprawozdanie o stosowanych terminach zapłaty');
PAR_WYDR.win_efld(_edit,UNPAR,'PRF1','NAZ','NAZWA',50,,,'Rok');
PAR_WYDR.win_efld(_edit,,'ZESTKONP','KOD','UNIK',50,,,'Zestaw kont');
PAR_WYDR.win_efld(_edit,POMOC,'BANK','KOD','SL',50,,,'Bank');
PAR_WYDR.win_efld(_edit,,'PAR1',,,,,,'Pokazywać rozrachunki z zerowymi wartościami',,,'check-box',,"1","0");
PAR_WYDR.efld_opt(_edit,'mark=1',UNPAR,'PRF1');
PAR_WYDR.efld_opt(_edit,'mark=1',POMOC,'BANK');
_ok:=PAR_WYDR.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['OK'@],"'key:F2'");
_anuluj:=PAR_WYDR.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
PAR_WYDR.btn_eopt(_edit,_ok,'tooltip='+exec('help_red_ok','#window','P'));
PAR_WYDR.btn_eopt(_edit,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
PAR_WYDR.win_edit(_edit);
PAR_WYDR.ZESTKONP:=exec('bl_konwy','dok_fks','S');
UNPAR.PRF1:=SSTALE.AR;
POMOC.BANK:=exec('nbp','optermpl');
PAR_WYDR.PAR1:=0;
POMOC.fld_fml('BANK','BEFORE_EDIT',"FINFO.SLBANK().SLU(); 1");
_edit:=0;
{? PAR_WYDR.edit("exec('ar_par','optermpl')")
|| PAR_WYDR.DATA_OD:=UNPAR.PRF1().POCZ_ROK;
   PAR_WYDR.DATA_DO:=ROK_F.KON_ROK;
   ParObj.SYNT:=ROK_F.SYNT;
   ParObj.SA:=PAR_WYDR.ZESTKONP().SA;
   _edit:=1
?};
POMOC.fld_fml('BANK','BEFORE_EDIT',"*");
_edit


\ar_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji parametrów analizy
::----------------------------------------------------------------------------------------------------------------------
__CHK.record3(
   UNPAR,'PRF1','Rok',
   POMOC,'BANK','Bank'
)


\create_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzy tabelę na potrzeby analizy
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(7,
   'ODD','STRING[8]','Jednostka księgowa',
   'AN','STRING[35]','Konto',
   'SYM','STRING[20]','Symbol',
   'KH','STRING[10]','Kontrahent',
   'TYP','STRING[3]','Typ',
   'DO','DATE','Data otwarcia',
   'TP','DATE','Termin płatności',
   'K1','REAL','Do 30 dni',
   'K2','REAL','Od 31 do 60 dni',
   'K3','REAL','Od 61 do 120 dni',
   'K4','REAL','Powyżej 120 dni',
   'KN','REAL','Nieotrzymane/niespełnione',
   'KO','REAL','Do otrzymania/spełnienia',
   'KURS','REAL','Kurs',
   'WAL','STRING[3]','Waluta',
   'KURS_OK','STRING[1]','Kurs OK? (T/N)',
   'REF','INTEGER','Ref sql'
);
_win:=_tab.mk_sel('Sprawozdanie o stosowanych terminach zapłaty w transakcjach handlowych','P',,'#sostzwth',,,,,'U');
_tab.win_fld(_win,,'ODD',,,-10,,,'Jednostka księgowa');
_tab.win_fld(_win,,'AN',,,15,,,'Konto');
_tab.win_fld(_win,,'SYM',,,20,,,'Symbol');
_tab.win_fld(_win,,'DO',,,-10,,,'Data otwarcia');
_tab.win_fld(_win,,'TP',,,-10,,,'Termin płatności');
_tab.win_fld(_win,,'KH',,,10,,,'Kontrahent');
_tab.win_fld(_win,,'TYP',,,,,,'Typ');
_tab.win_fld(_win,,'WAL',,,,,,'Waluta');
_tab.win_fld(_win,,'KURS_OK',,,,,,'Kurs OK',,,2,,"'T'","'N'");
_tab.win_fld(_win,,'KURS',,,10,4,,'Kurs');
_tab.win_fld(_win,,'K1',,,12,2,,'Do 30');
_tab.win_fld(_win,,'K2',,,12,2,,'od 31 do 60');
_tab.win_fld(_win,,'K3',,,12,2,,'od 61 do 120');
_tab.win_fld(_win,,'K4',,,12,2,,'Powyżej 120');
_tab.win_fld(_win,,'KN',,,-12,2,,'Nieotrzymane/niespełnione');
_tab.win_act(_win,,'Dołącz',,,,"exec('akc_add','optermpl')");
_tab.win_act(_win,1,'Dołącz',,,,"exec('akc_add','optermpl')");
_tab.win_act(_win,,'Popraw',,,,);
_tab.win_act(_win,,'Usuń',,,,,,,1);
_tab.win_act(_win,,'Formuła','Pod&sumowanie',,,"exec('akc_zest','optermpl')",,1);
_tab.win_act(_win,1,'Formuła','Pod&sumowanie',,,"exec('akc_zest','optermpl')",,1);
_tab.win_act(_win,,'Formuła','Druku&j podsumowanie',,,"exec('akc_print','optermpl')");
_tab.win_act(_win,1,'Formuła','Druku&j podsumowanie',,,"exec('akc_print','optermpl')");
_tab.win_act(_win,,'Formuła','Pokaż &rozrachunek',,,"exec('akc_show','optermpl')");
_tab.win_act(_win,,'Kolejność');
_tab.win_act(_win,,'Formuła','Legenda',,,"exec('legenda','color','OPTERMPL#01#01')");
_tab.win_act(_win,,'Rekord',,,,"exec('record','optermpl',1)","exec('record','optermpl',2)");
_tab.win_act(_win,1,'Rekord',,,,,"exec('record','optermpl',2)");
_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Dołącz'@],'menu:D');
_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Popraw'@],'menu:P');
_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Usuń'@],'menu:U');
_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Pod&sumowanie'@],'menu:S');
_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Druku&j podsumowanie'@],'menu:J');
_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Pokaż &rozrachunek'@],'menu:R');

_tab.win_sel(_win);

_edit:=_tab.mk_edit('Pozycja',,'#sostzwthe');
_tab.win_efld(_edit,,'ODD',,,35,,,,,,,'F3_button=1');
_tab.win_efld(_edit,KONTO,'K1',,,35,,,'Konto',,,,'F3_button=1');
_tab.win_efld(_edit,,'SYM',,,38,,,);
_tab.win_efld(_edit,,'DO',,,,,,);
_tab.win_efld(_edit,,'TP',,,,,,);
_tab.win_efld(_edit,,'KH',,,35,,,,,,,'F3_button=1');
_tab.win_efld(_edit,,'TYP',,,35,,,,,,'radio-buttons',,'Należność',"'NAL'",'Zobowiązanie',"'ZOB'");
_tab.win_efld(_edit,,'WAL',,,35,,,'Waluta',,,,'F3_button=1');
_tab.win_efld(_edit,,'KURS',,,15,4,,);
_tab.win_efld(_edit,,'K1',,,15,2,,);
_tab.win_efld(_edit,,'K2',,,15,2,,);
_tab.win_efld(_edit,,'K3',,,15,2,,);
_tab.win_efld(_edit,,'K4',,,15,2,,);
_tab.win_efld(_edit,,'KN',,,15,2,,);
_tab.win_efld(_edit,,'KO',,,15,2,,);
_tab.efld_opt(_edit,'mark=1',,'ODD');
_tab.efld_opt(_edit,'mark=1',KONTO,'K1');
_tab.efld_opt(_edit,'mark=1',,'SYM');
_tab.efld_opt(_edit,'mark=1',,'DO');
_tab.efld_opt(_edit,'mark=1',,'TP');
_tab.efld_opt(_edit,'mark=1',,'KH');
_tab.efld_opt(_edit,'mark=1',,'WAL');
_tab.efld_opt(_edit,'mark=1',,'KO');
_ok:=_tab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['Zapisz'@],"'key:F2'");
_anuluj:=_tab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
_tab.btn_eopt(_edit,_ok,'tooltip='+exec('help_red_ok','#window','Z'));
_tab.btn_eopt(_edit,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
_be:="cur_tab().REF=0";
_tab.fld_fml('ODD','BEFORE_EDIT',_be);
_tab.fld_fml('ODD','F3',"
   ODD.win_sel('WYB');
   ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   ODD.find_key(fld);
   {? ODD.select(,1,3)
   || fld(ODD.OD)
   ?}
");
_tab.fld_fml('ODD','AFTER_EDIT',"
   {? fld()<>''
   || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
      {? ODD.find_key(fld)
      || fld(ODD.OD); 1
      || FUN.info('Nie znaleziono jednostki księgowej: '+fld()+'.');
         0
      ?}
   || 1
   ?}
");
KONTO.K1_BE:=$_be;
KONTO.K1:=KONTO.K1_AE:=''; KONTO.K1_RODZ:=1; KONTO.K1_WYM:=0; KONTO.K1_SYNT:=0;
_tab.fld_fml('SYM','BEFORE_EDIT',_be);
_tab.fld_fml('DO','BEFORE_EDIT',_be);
_tab.fld_fml('TP','BEFORE_EDIT',_be);
_tab.fld_fml('KH','BEFORE_EDIT',_be);
_tab.fld_fml('KH','F3',"
   KH.index('SKR'); KH.prefix(2);
   {? fld()<>''
   || KH.find_key(fld())
   || KH.first()
   ?};
   KH.win_sel('SEL');
   {? KH.select(,1,3)
   || fld(KH.SKR)
   ?}
");
_tab.fld_fml('KH','AFTER_EDIT',"
   {? fld()<>''
   || KH.index('SKR'); KH.prefix(2);
      {? KH.find_key(fld())
      || fld(KH.SKR); 1
      || FUN.info('Nie znaleziono kontrahenta: '+fld()+'.');
         0
      ?}
   || 1
   ?}
");
_tab.fld_fml('TYP','BEFORE_EDIT',_be);
_tab.fld_fml('WAL','BEFORE_EDIT',"
   {? cur_tab().REF=0
   || FINFO.SLWAL().SLU();
      1
   ?}
");
_tab.fld_fml('WAL','AFTER_EDIT',"
   {? fld()<>''
   || SLO.index('SL'); SLO.prefix(FINFO.SLWAL().SLU,fld());
      {? SLO.first()
      || fld(SLO.KOD);
         {? FINFO.NAROD=SLO.ref()
         || cur_tab().KURS:=1;
            win_disp()
         |? cur_tab().KURS=0
         || cur_tab().KURS:=exec('get_kurs','optermpl',cur_tab().TP,cur_tab.WAL);
            {? cur_tab().KURS=-1
            || cur_tab().KURS:=1
            ?}
         ?};
         1
      || FUN.info('Nie znaleziono waluty: '+fld()+'.');
         0
      ?}
   || 1
   ?}
");
_tab.fld_fml('WAL','F3',"
   SLO.index('SL'); SLO.prefix(FINFO.SLWAL().SLU);
   {? fld()<>''
   || SLO.find_key(fld())
   || SLO.first()
   ?};
   SLO.win_sel('ONE_SEL');
   {? SLO.select(,1,3)
   || fld(SLO.KOD)
   ?}
");
_tab.fld_fml('KURS','BEFORE_EDIT',"cur_tab().REF=0 & cur_tab().WAL<>FINFO.NAROD().KOD");
_tab.win_edit(_edit);
_tab


\wal_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca tabelę walut
::----------------------------------------------------------------------------------------------------------------------
_tab:=sql(
   'select '+
   'case when SLO.REFERENCE=:_a then 1 else 0 end as PLN, '+
   'REFERENCE as REF '+
   'from SLO where SLO.SLU=:_b order by PLN'
   ,FINFO.NAROD,XINFO.SLWAL
);
_tab


\pomin_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tabela zawierająca rozrachunki w walucie PLN do pominięcia (analizowane jako walutowe)
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'TAB',
   'analize'
);
_obj.TAB:=tab_tmp(1,
   'REF','INTEGER','Ref'
);
_obj.analize:="
   _tab:=.TAB;
   {? OP.WAL<>FINFO.NAROD
   || OP.cntx_psh();
      OP.index('KON_O');
      OP.prefix(FINFO.NAROD,OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK,);
      {? OP.first()
      || _tab.REF:=OP.ref();
         _tab.add()
      ?};
      OP.cntx_pop();
      1
   || ~_tab.find_key(OP.ref())
   ?}
";
_obj


\fill_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Uzupełnia tabelę analizy
::   WE: _a - tabela analizy
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_wal:=exec('wal_tab','optermpl');
_pomin:=exec('pomin_obj','optermpl');
_maska:=UNPAR.PRF1().KOD;
OP.cntx_psh();
OP.use('operac'+_maska);
OP.index('PRZEG'); OP.prefix();
PROGRESS.set(OP.size(),'Analiza rozrachunków ...');
ZAP_OP.cntx_psh();
ZAP_OP.use('rozzap'+_maska);
D_RB.cntx_psh();
D_RB.use('dr__fk'+_maska);
SLO.cntx_psh();
SLO.prefix();
{? _wal.first()
|| {!
   |? {? SLO.seek(BIT.sqlint(_wal.REF),)
      || OP.prefix(SLO.ref());
         {? OP.first()
         || {!
            |? OP.prefix(SLO.ref(),OP.AN);
               {? OP.first()
               || {? PAR_WYDR.ZESTKONP=null | exec('in_zest','optermpl')
                  || {!
                     |? {? (OP.TYP='NAL' | OP.TYP='ZOB') & _pomin.analize() & exec('analize','optermpl')
                        || exec('fill_op','optermpl',_tab)
                        ?};
                        PROGRESS.next();
                        OP.next()
                     !}
                  || OP.last()
                  ?}
               ?};
               OP.prefix(SLO.ref());
               OP.next()
            !}
         ?}
      ?};
      _wal.next()
   !}
?};
SLO.cntx_pop();
D_RB.cntx_pop();
ZAP_OP.cntx_pop();
OP.cntx_pop();
PROGRESS.close()


\in_zest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy rozrachunek należy do zestawu kont
::----------------------------------------------------------------------------------------------------------------------
_an:=OP.AN;
_ok:=0;
KON_SCH.cntx_psh();
KON_SCH.index('IND_LP');
KON_SCH.prefix(PAR_WYDR.ZESTKONP);
{? KON_SCH.first()
|| {? ParObj.SA='S'
   || _an:=ParObj.SYNT+_an
   ?};
   {!
   |? {? KON_SCH.MZ='M'
      || WYDRUKI.MASKA:=KON_SCH.MASKA;
         WYDRUKIN.DLKON:=+KON_SCH.MASKA;
         _ok:=exec('mask','konto',_an)
      || _ok:=_an>=KON_SCH.ZOD & _an<=KON_SCH.ZDO
      ?};
      _ok=0 & KON_SCH.next()
   !}
?};
KON_SCH.cntx_pop();
_ok


\fill_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Uzupełnia dane analizy dla bieżącego rozrachunku
::   WE: _a - tabela analizy
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
_data:=date(0,0,0);
ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
{? ZAP_OP.first()
|| {!
   |? _jest:={? OP.TYP='ZOB' || F.SMa(ZAP_OP.WN,ZAP_OP.MA)<>0 || F.SWn(ZAP_OP.WN,ZAP_OP.MA)<>0 ?};
      {? _jest
      || _data:=ZAP_OP.DATA
      ?};
      _jest=0 & ZAP_OP.next()
   !}
?};
{? PAR_SKID.get(37)='T' || _data:=OP.DO ?};
_tab:=_a;
_tab.blank(1);
_tab.REF:=#OP.ref();
_tab.ODD:=OP.ODD().OD;
_tab.AN:=OP.AN;
_tab.SYM:=OP.SYM;
_tab.DO:=OP.DO;
_tab.TP:=exec('dzienRob','kalendarz',OP.TZ,1,1,1);
_tab.KH:=OP.KH().SKR;
_tab.TYP:=OP.TYP;
_tab.WAL:=OP.WAL().KOD;
_raty:=RatyObj;
{? _raty.fill()
|| _op:=_raty.OP;
   _zap:=_raty.ZAP_OP;
   {? _op.first()
   || {!
      |? _jest_zap:=0;
         _tab.cntx_psh();
         _tab.TP:=_op.TP;
         _tab.KO+=_op.RATA;
         _zap.prefix(_op.ref());
         {? _zap.first()
         || {!
            |? {? _zap.DATA<PAR_WYDR.DATA_OD | _zap.DATA>PAR_WYDR.DATA_DO
               || _tab.KO-=_zap.ZAP
               ?};
               {? _zap.DATA>=PAR_WYDR.DATA_OD & _zap.DATA<=PAR_WYDR.DATA_DO
               || _kw:=_zap.ZAP;
                  {? _kw & _jest_zap=0 || _jest_zap:=1 ?};
                  {? _kw & _jest
                  || {? _zap.DATA>_tab.TP
                     || _tab.KN+=_kw
                     ?};
                     _zw:=_zap.DATA-_data;
                     {? _zw>120
                     || _tab.K4+=_kw
                     |? _zw>60
                     || _tab.K3+=_kw
                     |? _zw>30
                     || _tab.K2+=_kw
                     || _tab.K1+=_kw
                     ?}
                  ?}
               ?};
               _zap.next()
            !}
         ?};
         {? _tab.TP<=PAR_WYDR.DATA_DO
         || _tab.KN+=_op.ZAP
         |? _jest_zap
         || _tab.KN:=0;
            _tab.KO:=_tab.K1+_tab.K2+_tab.K3+_tab.K4
         ?};
         {? _tab.KURS<>1
         || _tab.K1:=(_tab.K1*_tab.KURS)$2;
            _tab.K2:=(_tab.K2*_tab.KURS)$2;
            _tab.K3:=(_tab.K3*_tab.KURS)$2;
            _tab.K4:=(_tab.K4*_tab.KURS)$2;
            _tab.KN:=(_tab.KN*_tab.KURS)$2;
            _tab.KO:=(_tab.KO*_tab.KURS)$2
         ?};
         {? (PAR_WYDR.PAR1=1 | _tab.K1 | _tab.K2 | _tab.K3 | _tab.K4 | _tab.KN | _tab.KO) &
            (_jest_zap | _tab.TP<=PAR_WYDR.DATA_DO)
         || _tab.add()
         ?};
         _tab.cntx_pop();
         _op.next()
      !}
   ?}
|| exec('set_kurs','optermpl',_tab,_tab.TP);
   _jest_zap:=0;
   {? ZAP_OP.first()
   || {!
      |? _zap_data:=exec('zap_op_data','optermpl');
         _tab.KO+={? OP.TYP='ZOB' || F.SMa(ZAP_OP.WN,ZAP_OP.MA) || F.SWn(ZAP_OP.WN,ZAP_OP.MA) ?};
         {? _zap_data<PAR_WYDR.DATA_OD | _zap_data>PAR_WYDR.DATA_DO
         || _tab.KO-={? OP.TYP='ZOB' || F.SWn(ZAP_OP.WN,ZAP_OP.MA) || F.SMa(ZAP_OP.WN,ZAP_OP.MA) ?}
         ?};
         {? _zap_data>=PAR_WYDR.DATA_OD & _zap_data<=PAR_WYDR.DATA_DO
         || _kw:={? OP.TYP='ZOB' || F.SWn(ZAP_OP.WN,ZAP_OP.MA) || F.SMa(ZAP_OP.WN,ZAP_OP.MA) ?};
            {? _kw & _jest_zap=0 || _jest_zap:=1 ?};
            {? _kw & _jest
            || {? _zap_data>_tab.TP
               || _tab.KN+=_kw
               ?};
               _zw:=_zap_data-_data;
               {? _zw>120
               || _tab.K4+=_kw
               |? _zw>60
               || _tab.K3+=_kw
               |? _zw>30
               || _tab.K2+=_kw
               || _tab.K1+=_kw
               ?}
            ?}
         ?};
         ZAP_OP.next()
      !};
      {? _tab.TP<=PAR_WYDR.DATA_DO
      || _tab.KN+={? OP.TYP='ZOB' || F.SMa(OP.WN,OP.MA) || F.SWn(OP.WN,OP.MA) ?}
      |? _jest_zap
      || _tab.KN:=0;
         _tab.KO:=_tab.K1+_tab.K2+_tab.K3+_tab.K4
      ?}
   ?};
   {? _tab.KURS<>1
   || _tab.K1:=(_tab.K1*_tab.KURS)$2;
      _tab.K2:=(_tab.K2*_tab.KURS)$2;
      _tab.K3:=(_tab.K3*_tab.KURS)$2;
      _tab.K4:=(_tab.K4*_tab.KURS)$2;
      _tab.KN:=(_tab.KN*_tab.KURS)$2;
      _tab.KO:=(_tab.KO*_tab.KURS)$2
   ?};
   {? (PAR_WYDR.PAR1=1 | _tab.K1 | _tab.K2 | _tab.K3 | _tab.K4 | _tab.KN | _tab.KO) &
      (_jest_zap | _tab.TP<=PAR_WYDR.DATA_DO)
   || _tab.add()
   ?}
?}


\raty_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obiekt na potrzeby rat
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('RatyObj')>0
|| RatyObj
|| _obj:=obj_new(
      'OP','ZAP_OP',
      'fill'
   );
   RatyObj:=_obj;
   _obj.OP:=tab_tmp(1,
      'TP','DATE','Nazwa w oknie',
      'RATA','REAL','Rata',
      'ZAP','REAL','Zapłata'
   );
   _obj.ZAP_OP:=tab_tmp(3,
      'REF','INTEGER','Nagłówek',
      'DATA','DATE','Data',
      'ZAP','REAL','Zapłata'
   );
   _obj.fill:="
      D_RB.index('OP'); D_RB.prefix(OP.ref());
      {? D_RB.first()
      || _op:=.OP;
         _zap:=.ZAP_OP;
         _op.prefix();
         _op.erase();
         _zap.prefix();
         _zap.erase();
         {!
         |? _op.blank(1);
            _op.TP:=exec('dzienRob','kalendarz',D_RB.TERMPLAT,1,1,1);
            _op.RATA:=D_RB.WAR;
            _op.ZAP:=D_RB.WAR;
            _op.add();
            D_RB.next()
         !};
         _op.first();
         ZAP_OP.cntx_psh();
         ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
         {? ZAP_OP.first()
         || {!
            |? _kw:={? OP.TYP='ZOB' || ZAP_OP.WN || ZAP_OP.MA ?};
               {!
               |? {? _kw
                  || _rata:=_op.ZAP;
                     {? _kw>_rata
                     || {? _rata=0
                        || _rata:=_kw;
                           _op.RATA+=_kw
                        ?};
                        _op.ZAP:=0;
                        _op.put();
                        _zap.blank(1);
                        _zap.REF:=_op.ref();
                        _zap.DATA:=exec('zap_op_data','optermpl');
                        _zap.ZAP:=_rata;
                        _zap.add();
                        _kw-=_rata;
                        _op.next();
                        1
                     |? _kw<_rata
                     || _op.ZAP-=_kw;
                        _op.put();
                        _zap.blank(1);
                        _zap.REF:=_op.ref();
                        _zap.DATA:=exec('zap_op_data','optermpl');
                        _zap.ZAP:=_kw;
                        _zap.add();
                        _kw:=0;
                        0
                     || _op.ZAP:=0;
                        _op.put();
                        _zap.blank(1);
                        _zap.REF:=_op.ref();
                        _zap.DATA:=exec('zap_op_data','optermpl');
                        _zap.ZAP:=_kw;
                        _zap.add();
                        _kw:=0;
                        _op.next();
                        0
                     ?}
                  ?}
               !};
               ZAP_OP.next()
            !}
         ?};
         ZAP_OP.cntx_pop();
         1
      ?}
   ";
   _obj
?}


\set_kurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustala kurs waluty dla bieżącego rozrachunku na potrzeby analizy
::   WE:  _a - tabela z analizą
::        _b - data
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_data:=_b;
{? OP.WAL=FINFO.NAROD
|| _tab.KURS:=1;
   _tab.KURS_OK:='T'
|| _kurs:=exec('get_kurs','optermpl',_data,OP.WAL);
   {? _kurs=-1
   || _tab.KURS_OK:='N';
      OP.cntx_psh();
      OP.index('KON_O'); OP.prefix(FINFO.NAROD,OP.ODD,OP.AN,OP.SYM,OP.SYM,OP.SYM_ROK,);
      {? OP.first()
      || ZAP_OP.cntx_psh();
         ZAP_OP.index('OP');
         ZAP_OP.prefix(OP.ref());
         {? ZAP_OP.find_key(OP.DO)
         || _kurs:=ZAP_OP.KURS
         |? ZAP_OP.first()
         || _kurs:=ZAP_OP.KURS
         ?};
         ZAP_OP.cntx_pop()
      ?};
      OP.cntx_pop()
   || _tab.KURS_OK:='T'
   ?};
   _tab.KURS:=_kurs
?}


\get_kurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca kurs waluty
::   WE: _a - data
::       _b - kod waluty lub złączenie do SLO
::----------------------------------------------------------------------------------------------------------------------
_kurs:=-1;
_data:=_a;
{? var_pres('_b')=type_of('')
|| SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(FINFO.SLWAL,_b,);
   {? SLO.first()
   || _wal:=SLO.ref()
   || _wal:=null
   ?};
   SLO.cntx_pop()
|| _wal:=_b
?};
_rob:=_data~4<6;
TKRS.cntx_psh();
TKRS.index('TKRS_DT'); TKRS.prefix(POMOC.BANK,FINFO.NAROD);
{? TKRS.find_le(_data) & (_rob & TKRS.DT=_data | ~_rob & _data-TKRS.DT<3)
|| KRS.cntx_psh();
   KRS.index('KRS_WAL'); KRS.prefix(TKRS.ref(),_wal);
   {? KRS.first()
   || _kurs:=KRS.SR
   ?};
   KRS.cntx_pop()
?};
TKRS.cntx_pop();
_kurs


\nbp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca pozycję słownika z NBP
::----------------------------------------------------------------------------------------------------------------------
_id:='10100000';
_slo_nbp:=null;
B.cntx_psh();
B.index('BANKID');
B.prefix(_id);
{? B.first()
|| SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(FINFO.SLBANK().SLU,B.KOD,);
   {? SLO.first()
   || _slo_nbp:=SLO.ref()
   ?};
   SLO.cntx_pop()
?};
B.cntx_pop();
_slo_nbp


\spr_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca tablicę z wartościami sprawozdania
::   WE: _a - tabela z analizą
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('SprObj')<=0
|| _obj_kw:="
      _obj:=obj_new('K','N','A','P');
      _obj.K:=obj_new(4);
      _obj
   ";
   _obj:=obj_new(
      'FIRMA','NAL','ZOB','TAB',
      'fill','clear'
   );
   _obj.TAB:=_a;
   _obj.FIRMA:=obj_new('NAZ','NIP');
   _obj.FIRMA.NAZ:=XINFO.NAZ;
   _obj.FIRMA.NIP:=XINFO.NIP;
   _obj.NAL:=_obj_kw();
   _obj.ZOB:=_obj_kw();
   _obj.clear:="
      {! _o:=1..2
      |! _obj:={? _o=1 || .NAL || .ZOB ?};
         {! _i:=1..obj_len(_obj.K)
         |! _obj.K[_i]:=0
         !};
         _obj.N:=_obj.A:=_obj.P:=0;
         &_obj
      !}
   ";
   _obj.fill:="
      _tab:=.TAB;
      .clear();
      _tab.cntx_psh();
      {? _tab.first()
      || {!
         |? {? _tab.TYP='ZOB'
            || _kw:=.ZOB
            || _kw:=.NAL
            ?};
            _kw.K[1]+=_tab.K1;
            _kw.K[2]+=_tab.K2;
            _kw.K[3]+=_tab.K3;
            _kw.K[4]+=_tab.K4;
            _kw.N+=_tab.KN;
            _kw.A+=_tab.KO;
            &_kw;
            _tab.next()
         !}
      ?};
      .NAL.P:={? .NAL.A=0 || 0 || .NAL.N/.NAL.A*100 ?};
      .ZOB.P:={? .ZOB.A=0 || 0 || .ZOB.N/.ZOB.A*100 ?};
      _tab.cntx_pop()
   ";
   SprObj:=_obj
|| _obj:=SprObj
?};
_obj


\akc_zest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Akcja - Zestawienie
::----------------------------------------------------------------------------------------------------------------------
_obj:=exec('spr_obj','optermpl');
_obj.fill();
_tab:=tab_tmp(1,
   'NAZ','STRING[150]','Firma',
   'NIP','STRING[12]','NIP',

   'O_K1','REAL','Otrzymane do 30',
   'O_K2','REAL','Otrzymane od 31 do 60',
   'O_K3','REAL','Otrzymane od 61 do 120',
   'O_K4','REAL','Otrzymane pow. 120',
   'O_N' ,'REAL','Nieotrzymane - kwota',
   'O_NP','REAL','Nieotrzymane - procent',

   'S_K1','REAL','Spełnione do 30',
   'S_K2','REAL','Spełnione od 31 do 60',
   'S_K3','REAL','Spełnione od 61 do 120',
   'S_K4','REAL','Spełnione pow. 120',
   'S_N' ,'REAL','Niespełnione - kwota',
   'S_NP','REAL','Niespełnione - procent'
);
_tab.NAZ:=_obj.FIRMA.NAZ;
_tab.NIP:=_obj.FIRMA.NIP;
_tab.O_K1:=_obj.NAL.K[1];
_tab.O_K2:=_obj.NAL.K[2];
_tab.O_K3:=_obj.NAL.K[3];
_tab.O_K4:=_obj.NAL.K[4];
_tab.O_N:=_obj.NAL.N;
_tab.O_NP:=_obj.NAL.P;
_tab.S_K1:=_obj.ZOB.K[1];
_tab.S_K2:=_obj.ZOB.K[2];
_tab.S_K3:=_obj.ZOB.K[3];
_tab.S_K4:=_obj.ZOB.K[4];
_tab.S_N:=_obj.ZOB.N;
_tab.S_NP:=_obj.ZOB.P;
_tab.add();
_win:=_tab.mk_edit('Sprawozdanie o stosowanych terminach zapłaty w transakcjach handlowych',,'#sprtemp1');
_tab.win_efld(_win,,'NAZ',,,,,,'Firma');
_tab.win_efld(_win,,'NIP',,,20,,,'NIP');
_tab.win_efld(_win,AH,'H',,,,,,'');
_tab.win_efld(_win,AH,'H',,,,,,'Świadczenia pieniężne otrzymane');
_tab.win_efld(_win,,'O_K1',,,20,2,,'Do 30 dni');
_tab.win_efld(_win,,'O_K2',,,20,2,,'Od 31 do 60 dni');
_tab.win_efld(_win,,'O_K3',,,20,2,,'Od 61 do 120 dni');
_tab.win_efld(_win,,'O_K4',,,20,2,,'Powyżej 120 dni');
_tab.win_efld(_win,AH,'H',,,,,,'Świadczenia pieniężne nieotrzymane');
_tab.win_efld(_win,,'O_N',,,20,2,,'Kwota');
_tab.win_efld(_win,,'O_NP',,,20,2,,'Procent');
_tab.win_efld(_win,AH,'H',,,,,,'');
_tab.win_efld(_win,AH,'H',,,,,,'Świadczenia pieniężne spełnione');
_tab.win_efld(_win,,'S_K1',,,20,2,,'Do 30 dni');
_tab.win_efld(_win,,'S_K2',,,20,2,,'Od 31 do 60 dni');
_tab.win_efld(_win,,'S_K3',,,20,2,,'Od 61 do 120 dni');
_tab.win_efld(_win,,'S_K4',,,20,2,,'Powyżej 120 dni');
_tab.win_efld(_win,AH,'H',,,,,,'Świadczenia pieniężne niespełnione');
_tab.win_efld(_win,,'S_N',,,20,2,,'Kwota');
_tab.win_efld(_win,,'S_NP',,,20,2,,'Procent');
_tab.win_edit(_win);
_tab.display();
1


\akc_print
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Akcja - wydruk podsumowania
::----------------------------------------------------------------------------------------------------------------------
_obj:=exec('spr_obj','optermpl');
_obj.fill();
exec('rep_exec','#b_report','','fks_optermpl',,,,,,,,'W')


\akc_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Akcja - pokaż rozrachunek
::----------------------------------------------------------------------------------------------------------------------
_esc:=BEER.ESC;
BEER.ESC:=1;
_tab:=cur_tab();
OP.cntx_psh();
OP.prefix();
{? OP.seek(_tab.REF,)
|| _wal:=SSTALE.WAL;
   SSTALE.WAL:=OP.WAL;
   exec('op_przeg','!fks_roz_zark',,'R(Q)');
   SSTALE.WAL:=_wal
?};
OP.cntx_pop();
BEER.ESC:=_esc


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Formuła na Rekord
::   WE: _a - 1-przed 2-po
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
{? _a=1
|| KONTO.K1:=_tab.AN;
   {? _tab.REF=0
   || KOLOR.fnd_kol('OPTERMPL#01#01')
   || ''
   ?}
|| _ret:=__CHK.record(_tab,,'ODD');
   {? _ret=''
   || _ret:=__CHK.record2(KONTO,'K1','Konto')
   ?};
   {? _ret=''
   || _ret:=__CHK.record(_tab,,'SYM','DO','TP','KH','TYP','WAL','KURS','KO')
   ?};
   {? _ret='' & ',NAL,ZOB,'*_tab.TYP=0
   || FUN.info('Nieprawidłowy typ.\nDozwolone wartości: NAL i ZOB.');
      _ret:='TYP'
   ?};
   {? _ret=''
   || _ref:=_tab.ref();
      _tab.cntx_psh();
      _tab.prefix(_tab.ODD,POMOC.K1,_tab.SYM,_tab.KH,_tab.TYP,_tab.DO,_tab.TP);
      {? _tab.first() & (-menu_txt()<>'popraw' | _tab.ref()<>_ref)
      || FUN.info('Istnieje już taka pozycja.');
         _ret:='ODD'
      ?};
      _tab.cntx_pop()
   ?};
   {? _ret=''
   || _tab.AN:=KONTO.K1;
      _tab.K1:=_tab.K1$2;
      _tab.K2:=_tab.K2$2;
      _tab.K3:=_tab.K3$2;
      _tab.K4:=_tab.K4$2;
      _tab.KN:=_tab.KN$2;
      _tab.KO:=_tab.KO$2;
      _tab.KURS:=_tab.KURS$4
   ?};
   {? _ret='' & _tab.K1+_tab.K2+_tab.K3+_tab.K4>_tab.KO
   || FUN.info('Suma kwot z poszczególnych przedziałów nie może być większa\nniż kwota do otrzymania/spełnienia.');
      _ret:='KO'
   ?};
   {? _ret='' & _tab.KN>_tab.KO
   || FUN.info('Kwota nieotrzymana/niespełniona nie może być większa\nniż kwota do otrzymania/spełnienia.');
      _ret:='KN'
   ?};
   _ret
?}

\akc_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed akcją dołącz
::----------------------------------------------------------------------------------------------------------------------
KONTO.K1:='';
1


\analize
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy analizować bieżący rozrachunek rozrachunek?
::----------------------------------------------------------------------------------------------------------------------
{? ParObj.VAT=1
|| _analize:=0;
   ZAP_OP.cntx_psh();
   ZAP_OP.index('OP');
   ZAP_OP.prefix(OP.ref());
   {? ZAP_OP.first()
   || _dalej:=0;
      {!
      |? _dalej:={? OP.TYP='ZOB' || ZAP_OP.MA || ZAP_OP.WN ?};
         {? _dalej
         || POZ.cntx_psh();
            POZ.use(ref_name(ZAP_OP.POZDOK));
            {? POZ.seek(ZAP_OP.POZDOK)
            || DOK.cntx_psh();
               DOK.use('doku'+(POZ.name()+4));
               DOK.prefix();
               _analize:=POZ.DOK().RVAT<>null;
               DOK.cntx_pop()
            ?};
            POZ.cntx_pop()
         ?};
         _dalej=0 & ZAP_OP.next()
      !}
   ?};
   ZAP_OP.cntx_pop()
|| _analize:=1
?};
{? _analize
|| {? var_pres('FML',ParObj)>0
   || _analize:=ParObj.FML()
   ?}
?};
_analize


\zap_op_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca datę wystawienia związaną z bieżącym zapisem na rozrachunku
::----------------------------------------------------------------------------------------------------------------------
_data:=ZAP_OP.DATA;
{? ParObj.PAR37='N' & ZAP_OP.POZDOK
|| POZ.cntx_psh();
   POZ.use(ref_name(ZAP_OP.POZDOK));
   POZ.prefix();
   {? POZ.seek(ZAP_OP.POZDOK)
   || DOK.cntx_psh();
      DOK.use('doku'+(POZ.name()+4));
      DOK.prefix();
      _data:=POZ.DOK().DTO;
      DOK.cntx_pop()
   ?};
   POZ.cntx_pop()
?};
_data


\set_skr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Ustala kontrahenta na podstawie kartoteki rozrachunków na potrzeby analizy
::   WE:  _a - symbol konta analitycznego
::   WY:  skrót kontrahenta
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh(); KH.cntx_psh();
OP.index('KON');
OP.prefix(FINFO.NAROD, _a,);
_ret:={? OP.first() || OP.KH().SKR || '' ?};
OP.cntx_pop(); KH.cntx_pop();
_ret


:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 a60ab04b0028e6168a8141d07904fb9efdfa3c3b6e89c4ccdee458cf4b691233c452482801915b4e07816dbc565cb2ca5a0dc90a382401803bf8a3a992445bd9d8a6d6f41d075ecaacb92febf57ba09a74aee758dab3fd49d2b3e2a07b9388ea98abf555174db2f15175346598d9511aa19bf8098892e5ff9b4ad194c385d2b6

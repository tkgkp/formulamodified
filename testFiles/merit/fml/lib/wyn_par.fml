:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: wyn_par.fml
:: Utworzony: 28.07.2021
:: Autor: JAKOLTUN
::======================================================================================================================
:: Zawartosc: Plik zawiera formuły obsługujące globalne współczynniki wynagrodzeń.
::======================================================================================================================


\apar_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Globalne współczynniki wynagrodzeń - operacja grupowa.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('no_limit','schemat','PPL')
|| return
?};

R.cntx_psh();
R.win_dict('SLO_LS');

R.f_set('RN',,'RK=\'S\'');
WYN_PAR.cntx_psh();
WYN_PAR.index('WYN_PAR');
WYN_PAR.win_sel('WER');
WYN_PAR.win_edit('RED');
WYN_PAR.select();

WYN_PAR.cntx_pop();
R.cntx_pop();
R.f_clear();

~~


\wyn_par_rok_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Po redakcji roku
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
_val:=fld();
{? _fld='ROK'
|| {? _val<=0
   || __CHK.err_fld(WYN_PAR,_fld,1,'Rok nie może być ujemny.'@);
      return(0)
   ?}
?};

1

\wyn_par_msc_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Po redakcji miesiąca
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
_val:=fld();
{? _fld='MSC'
|| {? fld()>12 | fld()<1
   || __CHK.err_fld(WYN_PAR,_fld,1,'Miesiąc musi być wartością z przedziału od 1 do 12.'@);
      return(0)
   ?}
?};

1


\wyn_par_wyd_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Przed wyświetleniem pola WYN_WYD zmiennej EDIT_VAR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
EDIT_VAR.WYN_WYD:=WYN_PAR.WYDZIAL().SYMBOL;
1


\wyn_par_wyd_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Po redakcji pola WYN_WYD zmiennej EDIT_VAR. Ustala wartość pola WYDZIAL w buforze tabeli WYN_PAR.
::   WE:
::   WY: ref lub null - zależnie od tego, czy podany symbol poprawnie identyfikuje jednostkę organizacyjną.
::----------------------------------------------------------------------------------------------------------------------
WYN_PAR.WYDZIAL:=exec('szukaj_ud_skl','schemat','PODZORG',EDIT_VAR.WYN_WYD)


\wyn_par_import_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Formuła określająca sposób importu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? Plugin.runnable('PPL_WYN_PAR_001')
|| Plugin.run('PPL_WYN_PAR_001')
|| exec('import','wyn_par')
?}


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Formuła importująca współczynniki z pliku csv.
::       Separator kolumn (;)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_user:=
   {? _psep:=username()*'~' || _psep-1
   |? _psep:=username()*'.' || _psep-1
   || +username()
   ?}+username();
_naz_sciez:='Import globalnych współczynników wynagrodzenia';
_sciez:=exec('get','#profile',,_user,_naz_sciez);
dlg_init_dir(_sciez);
_fh:=0;
_tmp_dir:=fmk_tmp_dir(0);
{? type_of(_tmp_dir)<>type_of(~~)
|| _pth:=_tmp_dir.get_path();
   {? _pth<>''
   || _uploaded:=dlg_upload(_pth,0,'.csv');
      _sep:=exec('sep','#file',1);
      {? _uploaded<>''
      || _fh:=fopen(_pth+_sep+_uploaded,'ur')
      ?}
   ?}
|| FUN.emsg('Nie udało się utworzenie katalogu tymczasowego po stronie serwera.'@)
?};

{? _fh
|| _nrl:=1;
   fread(_fh);

: Potrzebne struktury
   _TMP:=tab_tmp(2,
      'R','STRING[20]','Rubryka płac'@,
      'NAZWA','STRING[150]','Nazwa grupy'@,
      'ROK','INTEGER','Rok'@,
      'MSC','INTEGER','Miesiąc'@,
      'WYDZIAL','STRING[16]','Jednostka organizacyjna'@,
      'OPIS','STRING[80]','Opis współczynnika'@,
      'WAR1','INTEGER','Wartość 1'@,
      'WAR2','INTEGER','Wartość 2'@,
      'WAR3','INTEGER','Wartość 3'@,
:: Pola techniczne.
      'WYKONAJ','INTEGER','Znacznik wykonanania'@,
      'UWAGI','STRING[255]','Uwagi'@,
      'NRL','INTEGER','Nr linii'@
   );
   _ndx:=_TMP.ndx_tmp(,,'NRL',,);
   _buf:=obj_new('R','NAZWA','ROK','MSC','WYDZIAL','OPIS','WAR1','WAR2','WAR3');

   exec('KOMM','#object');
   KOMM.init(255);

   {!
   |? _linia:=fread(_fh);
      _linia<>'\n'
   |! _nrl+=1;
      _linia+=';';
      {! _lp:=1 .. 9
      |! _poz:=_linia*';';
         _buf[_lp]:=(_poz-1)+_linia;
         _linia:=_poz-_linia
      !};

      _TMP.blank();
      _TMP.R:=_buf.R;
      _TMP.NAZWA:=_buf.NAZWA;
      _TMP.ROK:=#_buf.ROK;
      _TMP.MSC:=#_buf.MSC;
      _TMP.WYDZIAL:=_buf.WYDZIAL;
      _TMP.OPIS:=_buf.OPIS;
      _TMP.WAR1:=#_buf.WAR1;
      _TMP.WAR2:=#_buf.WAR2;
      _TMP.WAR3:=#_buf.WAR3;
      _skla:=exec('ref_r','edi_ka',_buf.R);
      _wydz:=exec('szukaj_ud_skl','schemat','PODZORG',_buf.WYDZIAL);
      {? _buf.R<>'' & ~_skla
      || _TMP.UWAGI:='Błąd importu. Nie odnaleziono składnika płacowego.';
         _TMP.WYKONAJ:=0
      |? _buf.WYDZIAL<>'' & ~_wydz
      || _TMP.UWAGI:='Błąd importu. Nie odnaleziono Jednostki organizacyjnej.';
         _TMP.WYKONAJ:=0
      |? _TMP.WAR1<0 | _TMP.WAR2<0 | _TMP.WAR3<0
      || _TMP.UWAGI:='Błąd importu. Pola wartości nie mogą być mniejsze od zera.';
         _TMP.WYKONAJ:=0
      |? _TMP.ROK<0
      || _TMP.UWAGI:='Błąd importu. Rok nie może być ujemny.';
         _TMP.WYKONAJ:=0
      |? _TMP.MSC<1 | _TMP.MSC>12
      || _TMP.UWAGI:='Błąd importu. Miesiąc musi być wartością z przedziału od 1 do 12.';
         _TMP.WYKONAJ:=0
      || _TMP.WYKONAJ:=1
      ?};
      _TMP.NRL:=_nrl;
      _TMP.add()
   !};

   fclose(&_fh);

   {? ~_TMP.first()
   || FUN.info('Brak danych do importu'@);
      return()
   ?};

   _TMP.index(_ndx);
   _TMP.prefix();
:: Fakt istnienia rekordów w tabeli tymczasowej został sprawdzony kilka linii wyżej.
   _TMP.first();

   WYN_PAR.cntx_psh();
   WYN_PAR.index('WYN_PAR');
   WYN_PAR.prefix();

   {!
   |? {? _TMP.WYKONAJ
      || WYN_PAR.blank();
         WYN_PAR.R:=exec('ref_r','edi_ka',_TMP.R);
         WYN_PAR.NAZWA:=exec('grupwyn','wyn_par','GRUPWYN',_TMP.NAZWA);
         WYN_PAR.ROK:=_TMP.ROK;
         WYN_PAR.MSC:=_TMP.MSC;
         WYN_PAR.WYDZIAL:=exec('szukaj_ud_skl','schemat','PODZORG',_TMP.WYDZIAL);
         WYN_PAR.OPIS:=_TMP.OPIS;
         WYN_PAR.WAR1:=_TMP.WAR1;
         WYN_PAR.WAR2:=_TMP.WAR2;
         WYN_PAR.WAR3:=_TMP.WAR3;
         {? WYN_PAR.add(1)
         || _TMP.UWAGI:='Rekord poprawnie dodany.'
         || _TMP.UWAGI:='Błąd podczas importu.'
         ?};
         _TMP.put()
      ?};
      _info:=
      {? +_TMP.R || 'Kod składnika płacowego: '+_TMP.R+', ' || '' ?}+
      {? +_TMP.NAZWA || 'Grupa współczynnika: '+_TMP.NAZWA+', ' || '' ?}+
      {? +_TMP.WYDZIAL || 'Jednostka organizacyjna: '+_TMP.WYDZIAL+', ' || '' ?}+
      {? +_TMP.OPIS || 'Opis współczynnika: '+_TMP.OPIS+', ' || '' ?};
      KOMM.sect_beg(_info);
      KOMM.add(_TMP.UWAGI);
      KOMM.sect_end();
      _TMP.next()
   !};

   KOMM.select();
   _TMP.ndx_drop(_ndx);
   WYN_PAR.cntx_pop()
?};
~~

\grupwyn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JAKOLTUN [21.37]
:: OPIS: Wyszukanie lub dodanie pola słownika SLO_NAZ
::   WE: _a[STRING] - nazwa słownika
::       _b[STRING] - nazwa pola
::   WY: ref/null
::----------------------------------------------------------------------------------------------------------------------
_slo_naz:=_a;
_pole:=_b;
{? _slo_naz<>'' & _pole<>''
|| _ref:=exec('FindInSet','#table','SLO_NAZ','NAZWA',_pole,_slo:=exec('slo_typ','ext_slo',_slo_naz),,1);
   {? ~_ref
   || exec('add_slo','edi_ka','SLO_NAZ',0,'NAZWA',_pole,'SLO_TYP',_slo)
   || _ref
   ?}
|| null()
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:26 328f985bb8ba50ec82522332f49b98464ce742d6bfbdb548af29961f3a5b1ca933c6e38131d8049335a7af70aba8cc8d53fac68fd361f2f1e666fb11303d207198161ee016aff2676082332436c8e0347e8ad4737267b82c8706014b77fd714079c7938a744ed05a0e957048150d5db0b2dbbd33bc35445c73f925f7321beab1

:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zs_dod.fml [21.37]
:: Utworzony: 20.05.2021
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły obsługujące dodatkowe zależności służbowe.
::======================================================================================================================


\_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Przed dołączeniem lub poprawieniem wiersza tabeli ZS_DOD.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
{? ZS_DOD.SLO_KOD<>null
|| ZS_DOD.ZS_TYP:=exec('szukaj','zs_typ',ZS_DOD.SLO_KOD)
?};
{? ZS_DOD.KOD_DOD<>null
|| ZS_DOD.ZS_DOD:=exec('szukaj','zs_typ',ZS_DOD.KOD_DOD)
?};
1


\_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Sprawdza wypełnienie wymaganych pól. Wykorzystywana w wyzwalaczach przed dołącz i popraw.
::   WE: _a [INTEGER] - Tryb modyfikacji danych
::          0 - dołączanie (domyślnie)
::          1 - poprawianie
::   WY: wynik testu poprawności danych zależny od kontekstu wywołania:
::       - jako formuła "rekord po"
::          - akronim niewypełnionego pola
::          - akronim pola o błędnej wartości
::          - 0 w przypadku powielenia unikalnego klucza
::       - jako formuła sprawdzająca dla wyzwalaczy "dołącz przed" i "popraw przed"
::          - 1 wiersz poprawny
::          - 0 niewypełnione wymagane pole, błędna wartość pola, powielony unikalny klucz
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_mode:={? var_pres('_a')=type_of(0) || _a<>0 ?};

__CHK.validate(ZS_DOD,
   $("_a.table(_b,"+$_mode+",,'FIRMA','SLO_KOD','KOD_DOD')"),
   "__CHK.record(_b,,'ZS_TYP','ZS_DOD')",
   "  {? _b.SLO_KOD=_b.KOD_DOD
      || _fld:=MS.name(_b,'KOD_DOD');
         _a.err_incorrect(_fld,'Typ \"%1\" nie może uzupełniać samego siebie.'@[_b.KOD_DOD().KOD]);
         'KOD_DOD'
      || 1
      ?}
   ",
   "  _chk:=1;
      _b.cntx_psh();
      _b.index('TYP_DOD');
      _b.prefix(_b.ZS_DOD);
      {? _b.find_key(_b.ZS_TYP)
      || _fld:=MS.name(_b,'KOD_DOD');
         _a.err_incorrect(_fld,'Cykliczne uzupełnienie zależności.'@);
         _chk:='KOD_DOD'
      ?};
      _b.cntx_pop();
      _chk
   "
)


\_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli ZS_DOD.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','zs_dod') & exec('_chk','zs_dod',0)


\_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli ZS_DOD.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','zs_dod') & exec('_chk','zs_dod',1)


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Udostępnia do edycji zawartość tabeli ZS_DOD.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
ZS_DOD.cntx_psh();
ZS_DOD.index('KOD_DOD');
ZS_DOD.prefix(exec('ref_firma','ustawienia'));

ZS_DOD.win_sel('WER');
ZS_DOD.win_edit('RED');
ZS_DOD.win_patt('WZO');
ZS_DOD.select();

ZS_DOD.cntx_pop();
~~


\slo_kod_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Przed edycją pola SLO_KOD tabeli ZS_DOD.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('slo_kod_x_be','zs_dod')


\kod_dod_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Przed edycją pola KOD_DOD tabeli ZS_DOD.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('slo_kod_x_be','zs_dod')


\slo_kod_x_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Ustawienie filtra na zawartość tabeli SLO_KOD.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
SLO_KOD.clear();
SLO_KOD.f_set(
   'KOD',,
   'SLO_KOD.SLO_TYP=:_a and SLO_KOD.REFERENCE in ('
   '  select ZS_TYP.SLO_KOD'
   '  from ZS_TYP'
   '  where ZS_TYP.FIRMA=:_b and ZS_TYP.ZS_TYP=0'
   ')',
   exec('slo_typ','zs_typ'),exec('ref_firma','ustawienia')
);
1


\rekord_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Rekord przed wiersza tabeli ZS_DOK.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _a<>0
|| _filter:=SLO_KOD.f_active();
   {? _filter=1 | _filter=3
   || SLO_KOD.f_clear(1)
   ?}
?};
~~


\rekord_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Po edycji wiersza tabeli ZS_DOD.
::   WE:
::   WY: 1 w przypadku gdy rekord jest poprawny, akronim wymaganego pola lub 0 gdy doszło do powielenia klucza.
::----------------------------------------------------------------------------------------------------------------------
{? exec('_modb','zs_dod')
|| exec('_chk','zs_dod',-menu_txt()='popraw')
?}

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:14 af472e9317bed4e5d310bfd6c392e28839eafe27def6d0c26d4bfb0c0bf9f3544e8672341295dc7a1d32deeeb50fdfbdbe9dd816d0a66667d01d8dc31c5de4c1a6e813d166f8fb56ead1767f206f78e4127201e0e1fc64041a35ce8a750a3ef5e32378b3da088929dabd7c38eebf04b91c02ccd3073f92e4ec65893d066b5334

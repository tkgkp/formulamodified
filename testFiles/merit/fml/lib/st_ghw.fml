:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_ghw.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa GHW dla systemu Statystyki
::======================================================================================================================


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Czyści powiązania do rekordu tabeli ST_GHW
::   WE: _a - ST_GHW.ref()
::   WY: >0  -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PRYWATNA><CLEAN>
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---
ST_GHW.cntx_psh(); ST_GHW.clear();
{? ST_GHW.seek(_ref)
||
   {? _can_continue>0
   ||
::    1. Usuwam ST_KH które wskazują na ten element
      ST_KH.cntx_psh();
      ST_KH.index('ST_GHW');
      ST_KH.prefix(ST_GHW.ref());
      {? ST_KH.first()
      || {!
         |? _can_continue:=exec('delete','st_kh',ST_KH.ref());
            ST_KH.first() & _can_continue>0
         !}
      ?};
      ST_KH.cntx_pop()
   ?};
   ~~
?};
ST_GHW.cntx_pop();
:: --- wszystkie powiazania usuniete? ---

{? _can_continue>0
|| _result:=1
|| undo()
?};

{? _mydo || end() ?};

_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Kasuje podany rekord tabeli ST_GHW (wykonywane w transakcji!!!)
::   WE: _a - ST_GHW.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_GHW.cntx_psh(); ST_GHW.clear();
{? ST_GHW.seek(_ref)
|| {? exec('clean','st_ghw',_ref)>0
   || {? ST_GHW.del(,1)>0
      || _result:=1
      || undo();
         _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};

{? _result<0
|| undo()
?};

ST_GHW.cntx_pop();
{? _mydo || end() ?};
_result


\st_ghw_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Dołącz dla ST_GHW
::----------------------------------------------------------------------------------------------------------------------
ST_GHW.win_edit('RED');
ST_GHW.blank();
{? ST_GHW.edit()
|| ST_GHW.add()
?};
~~


\st_ghw_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń dla ST_GHW
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRUPA='T' |  FUN.ask('Czy usunąć bieżący wiersz?'@)
|| exec('delete','st_ghw',ST_GHW.ref())
?};
~~


\st_ghw_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Popraw dla ST_ARCH
::----------------------------------------------------------------------------------------------------------------------
ST_GHW.win_edit('RED');
{? ST_GHW.edit()
|| ST_GHW.put()
?};
~~


\st_ghw_del_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń przed dla grupy rekordów (tabela ST_GHW)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?'@)
|| VAR.GRUPA:='T';
   1
|| 0
?}


\st_ghw_del_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń po dla grupy rekordów (tabela ST_GHW)
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~

:Sign Version 2.0 jowisz:1048 2020/10/16 15:22:58 23e3e17d138517d3990e4f23223cbf2942a1e217e7a410ef1f74cca512c020d817b593d6664119b67501a020b3d34a36bb956cc31ca11cd53c426aaa405c696e2b1db1c0a7e8b5817f144d26cbe199b1645a41832473b8e8c2f75db538bb54324ca8fac567ec126c8b8530e1b82f142b6e39358fb53fcdefa0c960802fcd3fb9

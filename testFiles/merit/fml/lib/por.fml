:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: por.fml
:: Utworzony: 12.11.2019
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły oraz obszary robocze dziedziny POR.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.14]
:: OPIS: Formuła inicjująca dla dziedziny POR.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
BPMN.SYM_DOM:='POR';
ZZ_POM.MODUL:='P';

exec('otworz_dok','phr_tab');
exec('Icon','#object');

~~


\por_blt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła główna obszaru POR_BLT - Biuletyny.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','por');
exec('bipnag_select','portal_bip',exec('_cfg','por_blt'))


\por_bwd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła główna obszaru POR_BWD - Baza wiedzy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','por');
exec('bipnag_select','portal_bip',exec('_cfg','por_bwd'))


\por_pyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła główna obszaru POR_PYT - Pytania i odpowiedzi.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','por');
exec('bippytn_select','portal_bip')


\por_wdg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła główna obszaru POR_WDG - Wydarzenia grupowe.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','por');
exec('bipnag_select','portal_bip',exec('_cfg','por_wdg'))


\por_ulp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Formuła główna obszaru POR_ULP - Ulepszenia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','por');
exec('bipulp_select','portal_bip',exec('_cfg','por_ulp'))


\por_ogl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [20.42]
:: OPIS: Formuła główna obszaru POR_OGL - Ogłoszenia.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','por');
exec('bipogl_select','portal_bip',exec('_cfg','por_ogl'))


\por_zrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Formuła główna obszaru POR_ZDR - Zestawienie roczne.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','por');
exec('select','por_zrd')


\o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła wyświetla okno wertowania tabeli nagłówków list płac (O) i pozwala (również grupowo) manipulować
::       flagą O.PORTAL - Czy prezentować dane (z listy płac) na portalu?
::   WE: [_a] [STRING] - Parametr techniczny.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kod:={? var_pres('_a')=type_of('') || _a || '' ?};

{? _kod=''
|| _form:=form_stack();
   _menu:='"exec(\''+_form.name+'\',\''+_form.file+'\',\'"+_a+"\',{? var_pres(\'_a\')>0 || _a || ~~ ?})"';

   UD_SKL.cntx_psh();
   UD_SKL.prefix();
   ODD.cntx_psh();
   ODD.prefix();

   O.cntx_psh();
   O.f_clear();
   O.index('LISTYMIE');
   O.prefix(exec('firma','ustawienia'));
   _ws:=O.mk_sel('Listy płac'@,'P',,'#oporsel',,,,,'U',,,,,'html_maximized');
   O.win_fld(_ws,,'LT',,,,,,,,MS.comment(O,'LT'));
   O.win_fld(_ws,,'R',,,-5,,,'Rok'@,,MS.comment(O,'R'));
   O.win_fld(_ws,,'M',,,-3,,,'Miesiąc'@,,MS.comment(O,'M'));
   O.win_fld(_ws,,'T','T',,-4,,,'Typ'@,,'Typ listy płac'@);
   O.win_fld(_ws,,'N',,,,,,,,MS.comment(O,'N'));
   O.win_fld(_ws,,'D',,,,,,,,MS.comment(O,'D'));
   O.win_fld(_ws,,'WYDZIAL','SYMBOL',,-16,,,'Jednostka organizacyjna'@,,MS.comment(O,'WYDZIAL'));
   O.win_fld(_ws,,'ODDZIAL','OD',,,,,'Jednostka księgowa'@,,MS.comment(O,'ODDZIAL'));
   O.win_fld(_ws,,'F',,,-5,,,,,MS.comment(O,'F'));
   O.win_fld(_ws,,'Z',,,,,,,,MS.comment(O,'Z'),2,,"'T'","'N'");
   O.win_fld(_ws,,'PORTAL',,,,,,,,MS.comment(O,'PORTAL'),2,,"'T'","'N'");
   O.win_act(_ws,,'Formuła','Pu&blikuj'@@,,'Włączenie udostępnienia danych z listy na portalu'@,
      $($_menu)('publikuj_b'),,,1,$($_menu)('publikuj_bg'),$($_menu)('publikuj_ag'),'B'
   );
   O.win_btn(_ws,'text=%1' ['Pu&blikuj'@@],'menu:B');
   O.win_act(_ws,,'Formuła','Wy&cofaj'@@,,'Wyłączenie udostępnienia danych z listy na portalu'@,
      $($_menu)('wycofaj_b'),,,1,$($_menu)('wycofaj_bg'),$($_menu)('wycofaj_ag'),'C'
   );
   O.win_btn(_ws,'text=%1' ['Wy&cofaj'@@],'menu:C');
   O.win_act(_ws,,'Szukaj');
   O.win_act(_ws,,'Kolejność');
   O.win_act(_ws,,'Rekord',,,,$($_menu)('bd'));
   O.win_sel(_ws);
   O.win_edit('RED');
   O.select();
   O.cntx_pop();

   ODD.cntx_pop();
   UD_SKL.cntx_pop();
   ~~

|? _kod='bd'
|| {? _b
   || _ws:=O.win_sel('?');
      {? O.sel_size()
      || _ga:=''
      |? O.PORTAL='T'
      || _ga:='B'
      || _ga:='C'
      ?};
      O.actions_grayed(_ws,_ga)
   ?}

|? _kod='publikuj_bg'
|| {? FUN.ask('Czy włączyć udostępnianie danych na portalu dla zaznaczonych list płac?'@)
   || sel_nchk();
      KOMM.init(,,'Raport z włączenia udostępniania danych na portalu'@);
      1
   ?}

|? _kod='wycofaj_bg'
|| {? FUN.ask('Czy wyłączyć udostępnianie danych na portalu dla zaznaczonych list płac?'@)
   || sel_nchk();
      KOMM.init(,,'Raport z wyłączenia udostępniania danych na portalu'@);
      1
   ?}

|? _kod='publikuj_b'
|| _gr:=O.sel_size();
   {? O.PORTAL<>'T'
::    Sprawdzenie czy lista płac znajduje się w okresie retencji:
   || KT.cntx_psh();
      KT.prefix();
      _rtn:={? O.T().T*'ZL'
            || exec('retentionPersonContract','portal_lib')
            || exec('retentionPersonPayCalc','portal_lib')
            ?};
      KT.cntx_pop();
      {? _rtn<>0 & (O.R)<((date()~1)-_rtn)
      || {? _gr
         || KOMM.set_root('Listy płac starsze niż zakładany okres retencji.'@);
            KOMM.add(O.LT);
            KOMM.sect_end()
         || FUN.info('Lista płac starsza niż zakładany okres retencji.'@)
         ?}
      || {? _gr | FUN.ask('Czy włączyć udostępnianie danych dla bieżącej listy płac?'@)
         || O.PORTAL:='T';
            O.put()
         ?}
      ?}
   |? _gr
   || KOMM.set_root('Listy płac niewymagające włączenia udostępniania'@);
      KOMM.add(O.LT);
      KOMM.sect_end()
   ?}

|? _kod='wycofaj_b'
|| _gr:=O.sel_size();
   {? O.PORTAL<>'N'
   || {? _gr | FUN.ask('Czy wyłączyć udostępnianie danych dla bieżącej listy płac?'@)
      || O.PORTAL:='N';
         O.put()
      ?}
   |? _gr
   || KOMM.set_root('Listy płac niewymagające wyłączenia udostępniania'@);
      KOMM.add(O.LT);
      KOMM.sect_end()
   ?}

|? _kod+3='_ag'
|| KOMM.select()

?}


\p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła wyświetla okno wertowania tabeli współpracowników i pozwala (również grupowo) manipulować
::       flagą P.PORTAL - Czy prezentować dane (współpracownika) na portalu?
::   WE: [_a] [STRING] - Parametr techniczny.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kod:={? var_pres('_a')=type_of('') || _a || '' ?};


_komadd:="
   KOMM.set_root(_a);
   P.OSOBA();
   KOMM.add('%1 %2, Numer teczki - %3, Identyfikator - %4' [OSOBA.NAZWISKO,OSOBA.PIERWSZE,P.T,$P.IP]);
   KOMM.sect_end()
";

{? _kod=''
|| exec('__F_ZATR','object');

   _form:=form_stack();
   _menu:='"params_exec(\''+_form.name+'\',\''+_form.file+'\',\'"+_a+"\',{? var_pres(\'_a\')>0 || _a || ~~ ?})"';

:: Niekonwencjonalne użycie "wybierałki" - nie wszystkie parametry są wykorzystywane.
   _in:=exec('wybierz_args','pracownik');
   _in.DOMAIN:='POR';
   _in.F_ZATR:='*T';
   _in.VIEW:='W';
   _in.WIELU:=0;
   _in.GRP_IDENT:='p_portal';
   _in.XFILTER:=0;

   _args:=exec('wybierz_args','pracownik');
   _args.XFILTER:=0;
   _status:=exec('wybierz_check_in','pracownik',_in,_args);
   {? _status<>''
   || return(_status)
   ?};

:: Budowa okna pracowników.
   _ws:=P.mk_sel(,'P',,'p_portal',,,,,'U','T',,,,'maximized');
:: Pola
   P.win_fld(_ws,,'T',,,-MS.fld_len(P,'T'),,,,,MS.comment(P,'T'));
   P.win_fld(_ws,,'IP',,,-6,,,,,MS.comment(P,'IP'));
   P.win_fld(_ws,,'OSOBA','NAZWISKO',,-20,,,,,MS.comment(OSOBA,'NAZWISKO'));
   P.win_fld(_ws,,'OSOBA','PIERWSZE',,-15,,,,,MS.comment(OSOBA,'PIERWSZE'));
   P.win_fld(_ws,,'F_ZATR','KOD',,-3,,,'Forma współpracy - kod'@,,MS.comment(F_ZATR,'KOD'));
   P.win_fld(_ws,,'F_ZATR','OPIS',,-10,,,'Forma współpracy - opis'@,,MS.comment(F_ZATR,'OPIS'));
   P.win_fld(_ws,,'WYDZIAL','SYMBOL',,-16,,,'Jednostka organizacyjna'@,,'Symbol jednostki organizacyjnej'@);
   P.win_fld(_ws,,'CP','CP','CHARPRAW',-9,,,,,MS.comment(P,'CP'));
   P.win_fld(_ws,,'ST','ST',,-18,,,'Stanowisko'@,,'Nazwa stanowiska'@);
   P.win_fld(_ws,,'ZA',,,-3,,,,,MS.comment(P,'ZA'),2,,"'T'","'N'");
   P.win_fld(_ws,,'PORTAL',,,-3,,,,,MS.comment(P,'PORTAL'),2,,"'T'","'N'");
:: Akcje
   P.win_act(_ws,,'Formuła','Pu&blikuj'@@,,'Włączenie udostępnienia danych z listy na portalu'@,
      $($_menu)('publikuj_b'),,,1,$($_menu)('publikuj_bg'),$($_menu)('publikuj_ag'),'B'
   );
   P.win_btn(_ws,'text=%1' ['Pu&blikuj'@@],'menu:B');
   P.win_act(_ws,,'Formuła','Wy&cofaj'@@,,'Wyłączenie udostępnienia danych z listy na portalu'@,
      $($_menu)('wycofaj_b'),,,1,$($_menu)('wycofaj_bg'),$($_menu)('wycofaj_ag'),'C'
   );
   P.win_btn(_ws,'text=%1' ['Wy&cofaj'@@],'menu:C');
   P.win_act(_ws,,'Formuła','Inicjuj'@@,,'Wymuszenie aktualizacji danych na portalu'@,
      $($_menu)('inicjuj_b'),,,1,$($_menu)('inicjuj_bg'),$($_menu)('inicjuj_ag'),'I'
   );
   P.win_btn(_ws,'text=%1' ['Inicjuj'@@],'menu:I');
   P.win_act(_ws,,'Menu','Szukaj'@,,,,,,,,,'S');
   P.win_act(_ws,,'Formuła','Szukaj dokładnie'@@,'#S','Szukanie zapisu zgodnego ze zredagowanym wzorcem'@,
      "params_exec('p_ustaw_ezk','szukaj')","params_exec('p_d','szukaj')",,,,,'S');
   P.win_act(_ws,,'Formuła','Szukaj &kontekstowo'@@,'#S','Szukanie zapisu zgodnego ze zredagowanym wzorcem'@,
      "params_exec('p_ustaw_ezk','szukaj')","params_exec('p_k','szukaj')",,,,,'K');
   P.win_act(_ws,,'#F3',,,,
      "params_exec('p_ustaw_ezk','szukaj')","params_exec('p_f3_wnd','szukaj')");
   P.win_act(_ws,,'Kolejność');
   P.win_act(_ws,,'Wyświetl',,,,"exec('p_bw','pracownik')");
   P.win_act(_ws,,'Rekord',,,,$($_menu)('bd'));

:: Dodatkowe parametry potrzebne do budowy widoku.
   _cfg:=obj_new('nav','wyb');
   _cfg.nav:=obj_new('main','side','curr');
   _cfg.nav.side:='NAW_MIN';
   _cfg.nav.main:=_ws;
   _cfg.wyb:=-1;

   params_set('args',_args,'cfg',_cfg);

   P_FILTER.UD_SCH:=_args.UD_SCH;

   _wnd:=exec('wybierz_conf','pracownik',_args,_cfg);

   UD_DEF.cntx_psh();
   UD_DEF.index('SYMBOL');
   UD_DEF.prefix(P_FILTER.UD_SCH);
   UD_DEF.first();
   OSOBA.cntx_psh();
   OSOBA.prefix();
   OSOBA.fld_attr(,_args.OSOBA_FLD_ATTR);
   P.cntx_psh();
   P.prefix();
   P.blank();
   P.fld_attr(,_args.P_FLD_ATTR);
:: Ustawienie okna wertowania tabeli P, aby można było się do tego odwołać (np. w "Rekord - przed").
   P.win_sel(_ws);
   UD_DEF.win_sel(_wnd);
   UD_DEF.select(,1,-1);
   P.fld_attr(,1);
   P.f_clear();
   P.cntx_pop();
   OSOBA.fld_attr(,1);
   OSOBA.cntx_pop();
   UD_DEF.cntx_pop()

|? _kod='bd'
|| {? _b
   || _ws:=P.win_sel('?');
      {? P.sel_size()
      || _ga:=''
      |? P.PORTAL='T'
      || _ga:='B'
      || _ga:='IC'
      ?};
      P.actions_grayed(_ws,_ga)
   ?}

|? _kod='publikuj_bg'
|| _wyb:=FUN.choice('Czy włączyć udostępnianie danych na portalu dla zaznaczonych współpracowników?'@,,
         'Tak, wszystkich zaznaczonych'@,'Tak, z uwzględnieniem &retencji'@,,,'Nie'@);
   {? _wyb
   || sel_nchk();
      KOMM.init(,,'Raport z włączenia udostępniania danych na portalu'@);
      params_get().cfg.wyb:=_wyb;
      1
   ?}

|? _kod='wycofaj_bg'
|| {? FUN.ask('Czy wyłączyć udostępnianie danych na portalu dla zaznaczonych współpracowników?'@)
   || sel_nchk();
      KOMM.init(,,'Raport z wyłączenia udostępniania danych na portalu'@);
      1
   ?}

|? _kod='inicjuj_bg'
|| {? FUN.ask('Czy wymusić aktualizację danych na portalu dla zaznaczonych współpracowników?'@)
   || sel_nchk();
      KOMM.init(,,'Raport z wymuszenia aktualizacji danych na portalu'@);
      1
   ?}

|? _kod='publikuj_b'
|| _gr:=P.sel_size();
   {? P.PORTAL<>'T'
   || {? _gr | FUN.ask('Czy włączyć udostępnianie danych dla bieżącego współpracownika?'@)
      || _wyb:={? _gr || params_get().cfg.wyb || 0 ?};
         _rtn:={? _wyb<>1 || exec('retentionPerson','portal_lib') || -1 ?};
         {? _wyb=1 |
            P.ZA='T' |
            _rtn=0 |
            P.DZ>=date((date()~1)-_rtn,1,1) |
            (~_gr & FUN.ask('Pracownik znajduje się poza okresem retencji.\nCzy włączyć udostępnianie?'@))
         || P.PORTAL:='T';
            P.put()
::          Wymuszenie wysyłki danych poprzez aktualizację znaczników czasowych (put-owanie odpowiednich rekordów
::          z odpowiednich tabel) wywoływane jest w wyzwalaczu "put - po".
         |? _gr
         || _komadd('Współpracownicy nieudostępnieni (poza okresem retencji)'@)
         ?}
      ?}
   |? _gr
   || _komadd('Współpracownicy niewymagający włączenia udostępniania'@)
   ?}

|? _kod='wycofaj_b'
|| _gr:=P.sel_size();
   {? P.PORTAL<>'N'
   || {? exec('getPid','portal_core',P.uidref())
      || {? _gr
         || _komadd('Współpracownicy, dla których wycofanie danych nie jest możliwe'@)
         || FUN.emsg(
               'Dane współpracownika zostały już wysłane na portal.'@+'\n'+
               'Zmiana niemożliwa.'@
            )
         ?}
      |? _gr | FUN.ask('Czy wyłączyć udostępnianie danych dla bieżącego współpracownika?'@)
      || P.PORTAL:='N';
         P.put()
      ?}
   |? _gr
   || _komadd('Współpracownicy niewymagający wyłączenia udostępniania'@)
   ?}

|? _kod='inicjuj_b'
|| _gr:=P.sel_size();
   {? P.PORTAL='T'
   || {? _gr | FUN.ask('Czy wymusić aktualizację danych na portalu dla bieżącego współpracownika?'@)
      || exec('p_portal_T','por');
         P.put(,1)
      ?}
   |? _gr
   || _komadd('Współpracownicy, których dane nie zostały wysłane na portal (wymuszenie aktualizacji jest zbędne)'@)
   ?}

|? _kod+3='_ag'
|| params_get().cfg.wyb:=-1;
   KOMM.select()

?}


\p_portal_T
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [20.42]
:: OPIS: Formuła wykonywana po zmianie flagi P.PORTAL z 'N' na 'T'. Jej zadaniem jest zmiana znaczników czasowych na
::       wszystkich rekordach tabel podrzędnych, które powinny zostac wysłane na portal.
::       Formuła wywoływana również w wyzwalaczu "dołącz - po" tabeli P jeżeli P.PORTAL='T'.
::       Formuła wywoływana również w wyzwalaczu "usuń - po" tabeli P. W tym przypadku zadaniem jest put-nięcie
::       rekordów związanych z osobą, w celu ich usunięcia z portalu. W tym przypadku _b=null().
::       Zakładamy prawidłowo wypełniony bufor tabeli P oraz odłożone konteksty tabel P i OSOBA.
::   WE: [_a] [REFERENCE] - Wskazanie rekordu tabeli OSOBA związanego z pracownikiem [domyślnie: P.OSOBA].
::       [_b] [REFERENCE] - Wskazanie rekordu tabeli P [domyślnie: P.ref()].
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_osoba:={? var_pres('_a')=type_of(null()) || _a || P.OSOBA ?};
_pref:={? var_pres('_b')=type_of(null()) || _b || P.ref() ?};

_firma:=P.FIRMA;

_user:=exec('osoba2user','users',_osoba);
{? _user<>null()
|| exec('FindAndGet','#table',USERS,_user,,"put(,1)",0);
   exec('idput_ndx','#table',ZAST_NAG,'DISP',_firma,_user);
   _ndx:=ZAST_NAG.ndx_tmp(,,'ZASTEPCA',,);
   exec('idput_ndx','#table',ZAST_NAG,_ndx,_user);
   ZAST_NAG.ndx_drop(_ndx);
   exec('idput_ndx','#table',B_USRROL,'USER',_firma,_user);
   exec('idput_ndx','#table',REM_KATO,'USERS',_user)
?};

_ndxt:=obj_new('ZAL_POBH');
_ndxt.ZAL_POBH:=ZAL_POBH.ndx_tmp(,,'ZALACZ','FIRMA',,'ZALACZ','OSOBA',,'ZALACZ','P',);

exec('idput_ndx','#table',OS_ADRES,'RODZAJ',_osoba);
exec('idput_ndx','#table',RD,'_RODZINA',_osoba);
exec('idput_ndx','#table',OS_SZKOL,'OD',_osoba);
exec('idput_ndx','#table',PKO,'_PKO',_osoba);
exec('idput_ndx','#table',OS_US,'OD',_osoba);
exec('idput_ndx','#table',P_INFO,'OD',_firma,_osoba);
exec('idput_ndx','#table',KART_DOD,'S1',_osoba);
exec('idput_ndx','#table',ZJ,'JEZYK',_osoba);
exec('idput_ndx','#table',PPK_UCZ,'AKTYWNY',_osoba,_firma);
exec('idput_ndx','#table',ZALACZ,'ZALACZ',_firma,_osoba,null());
exec('idput_ndx','#table',ZAL_POBH,_ndxt.ZAL_POBH,_firma,_osoba,null());

{? _pref<>null()
|| exec('idput_ndx','#table',H_UM,'OD',_pref);
   exec('idput_ndx','#table',ZC,'ZLECPRZ',_pref);
   exec('idput_ndx','#table',H,'HISTSTR',_pref);
   exec('idput_ndx','#table',RH,'RACHPDWY',_pref);
   exec('idput_ndx','#table',BNFTP,'PBD',_pref);
   exec('idput_ndx','#table',K,'KREDYTY',_pref);
   _ndx:=KRE_HIS.ndx_tmp(,,'KREDYT','P',);
   exec('idput_ndx','#table',KRE_HIS,_ndx,_pref);
   KRE_HIS.ndx_drop(_ndx);
   exec('idput_ndx','#table',P_DODINF,'PTR',_pref);
   exec('idput_ndx','#table',BIPPYTN,'P',_pref);
   exec('update4P','pkalsync',date((date()~1)-exec('retentionPersonWorkSchedule','portal_lib'),1,1),,1);
   exec('idput_ndx','#table',N,'NIEOPRAC',_pref);
   exec('idput_ndx','#table',DS,'DYSCYPLI',_pref);
   exec('idput_ndx','#table',KART_URL,'PRAC_ROK',_pref);
   exec('idput_ndx','#table',SZK_PRAC,'PRAC_SZK',_pref);
   exec('idput_ndx','#table',BDO,'PDTO',_pref);
   exec('idput_ndx','#table',O_P,'LT',_pref);
   exec('idput_ndx','#table',PM_BUDZ,'UNIQUE',_pref);
   exec('idput_ndx','#table',KARO,'P_WYD',_pref);
   exec('idput_ndx','#table',ETYPY_P,'PRAC',_pref);
   _ndx:=EDOKUM.ndx_tmp(,,'OSOBAWWW',,);
   exec('idput_ndx','#table',EDOKUM,_ndx,$_pref);
   EDOKUM.ndx_drop(_ndx);
   exec('zs_def_update_idput','zs_def',_pref);
   exec('idput_ndx','#table',ZALACZ,'ZALACZ',_firma,_osoba,_pref);
   exec('idput_ndx','#table',ZAL_POBH,_ndxt.ZAL_POBH,_firma,_osoba,_pref);
   exec('idput_ndx','#table',BIPPYTK,'P',_pref);
   exec('idput_ndx','#table',BIPODB,'POD',_pref);
   exec('idput_ndx','#table',WAOR,'P',_pref)
?};

ZAL_POBH.ndx_drop(_ndxt.ZAL_POBH);

~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 c99976c0855fa9c792ed314d6955f028c48738e808673a7875a119302ffa4d04a5da28c54e5671f7fcf0191ce76619dca74bbbcbf10f3dcc30b5ef7fe44af96220511b8558dd0e623023a75a3e94c5a6e5cf6ae34d561444fea506addeb36e03794455ddb554fb1ff99fd724d1dda1d0815208a27e56b589687daeec0353b12a

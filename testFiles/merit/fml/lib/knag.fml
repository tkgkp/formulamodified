:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: knag.fml
:: Utworzony: 12.10.2020
:: Autor: - IFZ
:: Systemy: FIKS
::======================================================================================================================
:: Zawartosc:  Obdługa korekt nagłówkowych
::======================================================================================================================

\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: Ustawienie na potrzeby edycji dokumentu korygowanego dla korekty nagłówkowej bez wskazania dokumentu księgowego
::----------------------------------------------------------------------------------------------------------------------
DOKN.fld_fml('NK1','AFTER_EDIT',"{? DOKN.SYM_ZEW1='' | DOK.DOK_REJ().EDSYMZEW<>'T'
                                 || DOKN.SYM_ZEW1:=DOKN.NK1
                                 ?};
                                 DOK.KOR:=DOKN.NK1; DOK.KOR_ZEW:=DOKN.SYM_ZEW1 ");
UNPAR.P10:=UNPAR.P11:='';
UNPAR.P10_BV:=UNPAR.P10_BE:='';
UNPAR.P10_AE:='DOKN.KH:=UNPAR.P10';
UNPAR.P10_F3:='exec(\'f3_vat_kor_kh\',\'vat\',0)';
UNPAR.P11_BV:=UNPAR.P11_BE:='';
UNPAR.P11_AE:='DOKN.NIP:=UNPAR.P11';
UNPAR.P11_F3:='exec(\'f3_vat_kor_kh\',\'vat\',1)'


\czy_knag
var_pres('__KORNAG')<=0 | POMOC.KNAG=1 | cur_tfld()=DOK


\dok_kn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: Akcja 'Korekta nagłówkowa' w oknach wertowania tabelach DOK umozliwiajaca, przegladanie
::       powiązanego dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? DOK.KN<>'' | DOK.LKN<>''
|| _sys:={? +DOK.ZAR & DOK.ZAR*':' || (DOK.ZAR*':'-1)+DOK.ZAR || '' ?};
   {? DOK.RVAT || DOK.RVAT().RVAT().KRAJ() ?};
   {? 4+DOK.KN='doku'
   || DOK.cntx_psh();
      _ref:=BB.sqlint(DOK.KN);
      DOK.use(8+DOK.KN);
      DOK.prefix();
      {? _ref<>0 & DOK.seek(_ref,)
      || exec('dok_spac','dok_fks')
      || {? DOK.DOK_REJ().KOR_NAG='T'
         || FUN.info('Nie znaleziono źródłowego dokumentu dla korekty nagłówkowej.'@)
         ?}
      ?};
      DOK.cntx_pop()
   |? 4+DOK.LKN='doku'
   || DOK.cntx_psh();
      _ref:=BB.sqlint(DOK.LKN);
      DOK.use(8+DOK.LKN);
      DOK.prefix();
      {? _ref<>0 & DOK.seek(_ref,)
      || exec('dok_spac','dok_fks')
      || {? DOK.DOK_REJ().KOR_NAG='T'
         || FUN.info('Nie znaleziono źródłowego dokumentu dla korekty nagłówkowej.'@)
         ?}
      ?};
      DOK.cntx_pop()
   |? 4+DOK.KN='dokn'
   || DOKN.cntx_psh();
      DOKN.use(8+DOK.KN);
      DOKN.prefix();
      _ref:=BB.sqlint(DOK.KN);
     {?  _ref<>0 & DOKN.seek(_ref,)
     || DOKN.display()
     || FUN.info('Nie znaleziono źródłowego dokumentu dla korekty nagłówkowej.'@)
     ?};
     DOKN.cntx_pop()
   || FUN.info('Dokument nie posiada dokumentu źródłowego.'@)
   ?}
|| FUN.info('Dla dokumentu nie wystawiono korekty nagłówkowej.'@)
?}





\vat_nip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: Przed redakcją pola DOKN.NIP1
::----------------------------------------------------------------------------------------------------------------------
{? exec('czy_obwl','dok_fks')
|| 1
|? form(cur_tfld.NIP1)=''
|| _v:=1;
   {? cur_tfld.WYS1<>null || _v_kod:=cur_tfld.WYS1().KOD || _v_kod:='' ?};
   {? cur_tfld.KH1<>''
   || {? +cur_tfld.KH1<11 & (KH.index('SKR'); KH.prefix(2,cur_tfld.KH1,); KH.first())
      || {! |? {? KH.KOD=_v_kod || _v:=2 ;0 || KH.next() ?} !}
      || {? _v_kod<>'' || _v:=2 || _v:=0 ?}
      ?}
   || {? _v_kod<>'' || _v:=2 || _v:=0 ?}
   ?};
   {? _v=2
   || _t:=cur_tfld.WYS1().SLU().WZ;
      {? RS.index('RS'); RS.prefix(); RS.find_key(_t)
      || {? RS.TAB='KH'
         || {? KH.index('KOD');KH.prefix(2); KH.find_key(SLO.KOD)
            || KH.prefix(2,KH.KOD)
            ?}
         ?}
      ?}
   ?};
   {? _v & KH.first()
   || cur_tfld.NIP1:={? KH.TYP='R' | KH.TYP='I'
                     || {? KH.NIP<>'' || KH.NIP || KH.PESEL ?}
                        || {? cur_tfld()=DOKN | 1+DOK.RVAT().RVAT().KVAT().SYM<>'_'
                           || KH.NIP
                           || exec('jak_nue','kontrahent')
                           ?}
                        ?}
                     ?};
      DOKN.KH_FULL1:=KH.NAZ_P;
      DOKN.KH_KRAJ1:=KH.KRAJ;
      DOKN.MIASTO1:=KH.MIASTO;
      DOKN.UL1:=KH.UL;
      DOKN.POCZ1:=KH.POCZ;
      1
|| 1
?}


\be_okrvt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: Formula przed redakcja pola Okres VAT dla dokumentu korekty nagłówkowej (DOKN)
::----------------------------------------------------------------------------------------------------------------------
_red:=1; PARAM.cntx_psh; PARAM.OKRES:=null;
{? var_pres('__KORNAG')<=0 | POMOC.KNAG=1
|| _s:=DOK.RVAT().RVAT().RT; _zak:=_s<>'S' & _s<>'W';
   _data:={? DOKN.DTW1>=date(2014,1,1) || DOKN.DOP1 || DOKN.DTO1 ?};
   {? ~DOKN.OKRVAT1
   || OKRO_F.cntx_psh();
      {? _data>=SSTALE.AO().POCZ & _data<=SSTALE.AO().KON
      || _wynik:=OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ;
         {? DOKN.A_VAT1=null || PARAM.OKRES:=OKRO_F.ref ?}
      || _wynik:='';
         _dalej:=1;
         OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
         {? OKRO_F.first()
         || {!|? {? _data>=OKRO_F.POCZ & _data<=OKRO_F.KON
                 || _wynik:=OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ;
                    PARAM.OKRES:=OKRO_F.ref;
                    _dalej:=0
                 || _dalej:=OKRO_F.next()
                 ?};
                 _dalej
            !}
         ?}
      ?};
      OKRO_F.cntx_pop();
      {? DOKN.A_VAT1=null || PARAM.OKRVAT1:=_wynik; DOKN.OKRVAT1:=PARAM.OKRES ?}
   || _red:=1
   ?}
|| _red:=0
?};
PARAM.cntx_pop;
{? var_pres('typobi')<=0 || win_disp() ?};
_red


\por_okr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.42]
:: OPIS: Formula po redakcji pola PARAM.OKROVAT1 dla dokumentu korekty nagłówkowej bez wskazania (DOKN)
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.index('NAZ') ; OKRO_F.prefix(REF.FIRMA);
_a:=1;
{? PARAM.OKRVAT1<>''
|| {? {? PARAM.OKRVAT1*'/'
      || OKRO_F.find_key((PARAM.OKRVAT1*'/')-PARAM.OKRVAT1,(PARAM.OKRVAT1*'/'-1)+PARAM.OKRVAT1)
      || OKRO_F.find_key(SSTALE.AR().NAZ,PARAM.OKRVAT1)
      ?}
   || {? OKRO_F.POCZ=date(0,0,0)
      || FUN.info('Okresem rozliczenia VAT nie może być\n"Bilans otwarcia" i "Bilans zamknięcia".'@); _a:=0
      || PARAM.OKRES:=OKRO_F.ref(); DOKN.OKRVAT1:=OKRO_F.ref();
         PARAM.OKRVAT1:={? +OKRO_F.NAZ || OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ || '' ?};
         DOKN.OKRVAT1:=OKRO_F.ref();
         _a:=1
      ?}
   || FUN.info('Błędna nazwa okresu.'@); PARAM.OKRES:=null; _a:=0
   ?}
?};
{? _a
|| {? PARAM.OKRVAT1=''
   || PARAM.OKRES:=null;
      DOKN.OKRVAT1:=null
   ?}
?};
OKRO_F.index('ROK');
_a


\besymzew1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.XX]
:: OPIS: Przed redakcja pola DOKN.SYM_ZEW
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
DOK_REJ.cntx_psh(); DOK_REJ.prefix();
{? DOK.DOK_REJ & DOK.DOK_REJ().EDSYMZEW='T' & (DOK.DOK_REJ().KOR_NAG='N' | POMOC.KNAG=1) || _zwrot:=1 ?};
DOK_REJ.cntx_pop();
_zwrot


\dok_fld_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.XX]
:: OPIS: Po redakcji pól odbiorcy: DOK.O_NIP
::----------------------------------------------------------------------------------------------------------------------
{? DOK.DOK_REJ().KOR_NAG='T' & exec('get','#params',300230,2)='T' & DOKN.O_NIP1<>DOK.O_NIP
|| FUN.info('Niedostępna zmiana NIP odbiorcy w ramach korekty nagłówkowej.'@);
   0
|| 1
?}


\dok2dokn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.XX]
:: OPIS: Podstawienie pól z DOK do DOKN
::----------------------------------------------------------------------------------------------------------------------
DOKN.blank();
DOKN.DOK_REJ:=DOK.DOK_REJ;
DOKN.NK1:=DOK.NK;
DOKN.SYM_ZEW1:=DOK.SYM_ZEW;
DOKN.NIP1:=DOK.NIP; DOKN.KH1:=DOK.KH;
DOKN.WYS1:=DOK.WYS;
DOKN.DTW1:=DOK.DTW; DOKN.DTO1:=DOK.DTO; DOKN.DOP1:=DOK.DOP; DOKN.D41:=DOK.D4; DOKN.D31:=DOK.D3;
DOKN.OKRVAT1:=DOK.OKRVAT; PARAM.OKRVAT1:=OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ;
DOKN.KH_FULL1:=DOK.KH_FULL;
DOKN.KH_KRAJ1:=DOK.KH_KRAJ;
DOKN.KH_KRIS1:=DOK.KH_KRISO;
DOKN.UL1:=DOK.UL;
DOKN.MIASTO1:=DOK.MIASTO;
DOKN.POCZ1:=DOK.POCZ;
DOKN.KH_ODB1:=DOK.KH_ODB;
DOKN.DOM1:=DOK.DOM;
DOKN.LOKAL1:=DOK.LOKAL;
DOKN.KPOCZ1:=DOK.KPOCZ;
DOKN.KH_ODB1:=DOK.KH_ODB;
DOKN.O_NAZ1:=DOK.O_NAZ;
DOKN.O_UL1:=DOK.O_UL;
DOKN.O_MIAST1:=DOK.O_MIASTO;
DOKN.O_POCZ1:=DOK.O_POCZ;
DOKN.O_KRAJ1:=DOK.O_KRAJ;
DOKN.O_NIP1:=DOK.O_NIP;
DOKN.O_DOM1:=DOK.O_DOM;
DOKN.O_LOKAL1:=DOK.O_LOKAL;
DOKN.O_KPOCZ1:=DOK.O_KPOCZ

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 8b58aa3162d7c60653b0378818ae3ecda99f45692dbbc990348934abeb835e0d9f2a1e8ff6dc79e5e4347772f3ef9d4763bdb6f14f6a9f3048d64c96e56510248db46e4837cdab6c5e02b45e4cdc2e8716c8f7dfa93f884998fe2f257ac67d6e59f1a18c8f7a6f92b7b2df76e8d7a5282a4cb77da30b4c6aaaf499ac96aa782a

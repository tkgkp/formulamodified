:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: cechy.fml
:: Utworzony: 28.02.2018
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Procedury do obsługi informacji dodatkowych dla wybranych tabel: kontrahentów i środków trwałych
::            Mechanizm oparty na cechach dodatkowych dokumentów w obiegu.
::======================================================================================================================


\select_cd_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Wyświetlanie i edycja tabeli CDDEF dla kontrahentów
::----------------------------------------------------------------------------------------------------------------------
CDTAB.index('CDTAB');
CDTAB.prefix();
{? CDTAB.find_key('KH',)
|| CDDEF.index('CDKOL');
   CDDEF.prefix(CDTAB.ref());
   CDDEF.win_sel('WER');
   CDDEF.hdr_sel(' (kontrahenci)'@);
   VAR_DEL.delete('__cdtab');
   __cdtab:=KH;
   POMOC.TAT_M:='T';
   TAT.cntx_psh(); TAT.win_patt('REDO'); TAT.win_edit('REDO');
   {? CDTAB.r_lock(1,1,1)
   || CDDEF.select();
      CDTAB.r_unlock()
   || FUN.emsg('Definicje informacji dodatkowych są modyfikowane przez innego użytkownika.'@)
   ?};
   TAT.cntx_pop(); CDDEF.hdr_sel()
|| FUN.emsg('System nie został skonfigurowany do obsługi informacji dodatkowych dla kontrahentów.'@)
?}


\select_cd_sr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Wyświetlanie i edycja tabeli CDDEF dla środków trwałych
::----------------------------------------------------------------------------------------------------------------------
CDTAB.index('CDTAB');
CDTAB.prefix();
{? CDTAB.find_key('SRSR',)
|| CDDEF.index('CDKOL');
   CDDEF.prefix(CDTAB.ref());
   CDDEF.win_sel('WER');
   CDDEF.hdr_sel(' (środki trwałe)'@);
   VAR_DEL.delete('__cdtab');
   __cdtab:=SRSR;
   POMOC.TAT_M:='T';
   {? CDTAB.r_lock(1,1,1)
   || CDDEF.select();
      CDTAB.r_unlock()
   || FUN.emsg('Definicje informacji dodatkowych są modyfikowane przez innego użytkownika.'@)
   ?};
   CDDEF.hdr_sel()
|| FUN.emsg('System nie został skonfigurowany do obsługi informacji dodatkowych dla środków trwałych.'@)
?}


\find_cdtab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formuła zwraca właściwy dla tabeli danych rekord w definicji tabel CDTAB
::----------------------------------------------------------------------------------------------------------------------
CDTAB.cntx_psh();
CDTAB.index('CDTAB');
CDTAB.prefix();
_akronim:={? __cdtab=KH || 'KH' |? __cdtab=SRSR || 'SRSR' ?};
{? CDTAB.find_key(_akronim,)
|| _ref:=CDTAB.ref()
|| _ref:=null
?};
CDTAB.cntx_pop();
_ref


\atr_tat_bds
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed wyświetl dla pola CDDEF.TAT
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.TAT<>null & CDDEF.TAT().TYP='X'
|| CDDEF.efld_opt(CDDEF.win_edit('?'),'mark=0,enable=1',CDDEF,'TSLO')
|| CDDEF.efld_opt(CDDEF.win_edit('?'),'mark=0,enable=0',CDDEF,'TSLO')
?};
{? CDDEF.TAT<>null & CDDEF.TAT().TYP='O'
|| CDDEF.efld_opt(CDDEF.win_edit('?'),'mark=0,enable=1',CDDEF,'OPIS');
   CDDEF.efld_opt(CDDEF.win_edit('?'),'mark=0,enable=0',CDDEF,'AE')
|| CDDEF.efld_opt(CDDEF.win_edit('?'),'mark=0,enable=0',CDDEF,'OPIS');
   CDDEF.efld_opt(CDDEF.win_edit('?'),'mark=0,enable=1',CDDEF,'AE')
?};
1


\atr_tat_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed edycja pola CDDEF.TAT
::----------------------------------------------------------------------------------------------------------------------
ZNORMAT.TAT:=fld();
~~


\atr_tat_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Po edycji pola CDDEF.TAT
::----------------------------------------------------------------------------------------------------------------------
::jak sie zmienil atrybut to usuniecie danych ktore moga byc bledne
{? CDDEF.TAT=null
|| FUN.info('Nie wybrano atrybutu.'@); _wy:=0
|| TAT.cntx_psh();
   TAT.prefix();
   {? ~TAT.seek(CDDEF.TAT) || CDDEF.TAT:=null ?};
   TAT.cntx_pop();
   {? ZNORMAT.TAT<>fld() ||  exec('cddef_clear_def','cechy'); CDDEF.DEF_FORM:='' ?};
   VAR.TYP_ATR:={? CDDEF.TAT().TYP='L' || 'Liczba'
                |? CDDEF.TAT().TYP='D' || 'Data'
                |? CDDEF.TAT().TYP='C' || 'Czas'
                |? CDDEF.TAT().TYP='T' || 'Tekst'
                |? CDDEF.TAT().TYP='B' || 'Tak/Nie'
                |? CDDEF.TAT().TYP='S' || 'Słownik'
                |? CDDEF.TAT().TYP='U' || 'Schemat struktury'
                |? CDDEF.TAT().TYP='X' || 'Dowolny słownik'
                |? CDDEF.TAT().TYP='O' || 'Nagłówek'
                || ''
                ?};
   _wy:=1
?};
win_disp();
_wy


\atr_nag_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed wyswietleniem pola CDDEF.WYMAG, CDDEF.EDIT
::   WY: 1 - tak, 0 - nie
::---------------------------------------------------------------------------------------------------------------------
{? CDDEF.TAT().TYP<>'O' || 1 || 0 ?}


\atr_nag_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed redagowaniem CDDEF.WYMAG, CDDEF.EDIT, CDDEF.AE, CDDEF.DEFAULT, CDDEF.DEF_FORM
::   WY: 1 - tak, 0 - nie
::----------------------------------------------------------------------------------------------------------------------
CDDEF.TAT().TYP<>'O'


\atr_sp_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed wyswietleniem pola CDDEF.SLOPOLE
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.TAT().TYP='X' | CDDEF.TAT().TYP='S' | CDDEF.TAT().TYP='U' || 1 || 0 ?}


\atr_sp_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed redagowaniem CDDEF.SLOPOL
::   WY: 1 - tak, 0 - nie
::----------------------------------------------------------------------------------------------------------------------
ZNORMAT.WART_A:=fld();
CDDEF.TAT<>null & 'SUX'*CDDEF.TAT().TYP


\atr_sp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Po redagowaniu CDDEF.SLOPOL
::----------------------------------------------------------------------------------------------------------------------
{? ZNORMAT.WART_A<>fld()
|| {? (CDDEF.TAT().TYP='S') & (CDDEF.SLO<>null())
   || CDDEF.DEFAULT:={? fld()='K' || CDDEF.SLO().KOD  || CDDEF.SLO().TR ?}
   |? (CDDEF.TAT().TYP='U') & (CDDEF.UD_SKL<>null())
   || CDDEF.DEFAULT:={? fld()='K' || CDDEF.UD_SKL().SYMBOL  || CDDEF.UD_SKL().OPIS ?}
   ?}
?};
~~


\clear_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Usuniecie wartosci domyslnej z definicji atrybutu
::----------------------------------------------------------------------------------------------------------------------
CDDEF.DEFAULT:=''; CDDEF.SLO:=CDDEF.UD_SKL:=null();
~~


\atr_df_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed redagowaniem CDDEF.DEF_FORM
::----------------------------------------------------------------------------------------------------------------------
CDDEF.DEF_KIND='F'


\atr_dk_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Po redagowaniu pola CDDEF.DEF_KIND
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.DEF_KIND='F'
|| exec('cddef_clear_def','cechy')
|| CDDEF.DEF_FORM:=''
?};
win_disp()


\atr_dk_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed redagowaniem CDDEF.DEF_KIND
::   WY: 1 - tak, 0 - nie
::----------------------------------------------------------------------------------------------------------------------
CDDEF.TAT().TYP<>'O'


\atr_dk_bv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed wyswietleniem CDDEF.DEF_KIND
::----------------------------------------------------------------------------------------------------------------------
::Ustawienie statusow przyciskow
CDDEF.btn_eopt('REDDEF','BTN_SET_DEF','state=normal');
{? (CDDEF.DEFAULT='') & (CDDEF.DEF_FORM='')
|| CDDEF.btn_eopt('REDDEF','BTN_CLEAR_DEF','state=grayed')
|| CDDEF.btn_eopt('REDDEF','BTN_CLEAR_DEF','state=normal')
?};
{? CDDEF.TAT().TYP<>'O' || 1 || 0 ?}


\atr_ng1_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed wyswietleniem pola CDDEF.AE, CDDEF.DEFAULT, CDDEF.DEF_FORM
::   WY: 1 - tak, 0 - wyszarzone
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.TAT().TYP<>'O' || 1 || exec('findfnv','#color') ?}


\bda_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed wyswietleniem pola CDDEF.OPIS
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.TAT<>null & CDDEF.TAT().TYP='O' || 1 || exec('findfnv','#color') ?}


\bea_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed edycją pola CDDEF.OPIS
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.TAT().TYP='O'
|| 1
|| CDDEF.memo_set(,'OPIS');
   0
?}


\bs_tslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed wyswietleniem pola CDDEF.TSLO
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.TAT<>null & CDDEF.TAT().TYP='X' || 1 || exec('findfnv','#color') ?}


\be_tslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed redakcja pola CDDEF.TSLO
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.TAT<>null & CDDEF.TAT().TYP='X' || 1 || 0 ?}


\atr_cz_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed wyswietleniem pola CDDEF.CZY_ZERO
::   WY: 1 - tak, 0 - nie
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.TAT().TYP='L' || 1 || 0 ?}


\atr_cz_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Przed redagowaniem CDDEF.CZY_ZERO
::   WY: 1 - tak, 0 - nie
::----------------------------------------------------------------------------------------------------------------------
CDDEF.TAT().TYP='L'


\get_war_true
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwraca lancuch odpowiadajacy potwierdzeniu w atrybucie typu Tak/Nie
::   WY: Lancuch dla potwierdzenia
::----------------------------------------------------------------------------------------------------------------------
'Tak'


\get_war_false
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwraca lancuch odpowiadajacy zaprzeczeniu  w atrybucie typu Tak/Nie
::   WY: Lancuch dla zaprzeczenia
::----------------------------------------------------------------------------------------------------------------------
'Nie'


\get_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwrocenie wartosci domyslnej dla pozycji schematu atrybutow podanej refem
::   WE: _a - CDDEF.ref() [biezacy]
::   WY: obliczona wartosc
::       lub wartosc zerowa zgodna z typem atrybutu
::       lub ~~ gdy bledny parametr formula lub brak lub bledna wartosc domyslna
::----------------------------------------------------------------------------------------------------------------------
{? (_<1) | (_a=~~) || _a:=CDDEF.ref() ?};
{? (type_of(_a)<>type_of(null())) || return(~~) ?};

::opracowanie wartosci
_wyn:=~~;
{? CDDEF.DEF_KIND='W'
||
   {? CDDEF.DEFAULT<>''
   ||
      {? CDDEF.TAT().TYP='S' || _wyn:=CDDEF.SLO
      |? CDDEF.TAT().TYP='U' || _wyn:=CDDEF.UD_SKL
      |? CDDEF.TAT().TYP='D' || _wyn:=date(#(4+CDDEF.DEFAULT),#(2+(5-CDDEF.DEFAULT)),#(CDDEF.DEFAULT+2))
      |? CDDEF.TAT().TYP='C' ||
         _war:=CDDEF.DEFAULT; _pos:=_war*':'; _gg:=_mm:=_ss:=0;
         {? _pos>0 || _gg:=#((_pos-1)+_war); _mm:=#(2+(_pos-_war)); _ss:=#(_war+2) ?};
         _wyn:=time(_gg,_mm,_ss)
      |? CDDEF.TAT().TYP='L' || _wyn:=#STR.gsub(CDDEF.DEFAULT,',','.')
      || _wyn:=CDDEF.DEFAULT
      ?}
   ?}
||
   {? CDDEF.DEF_FORM<>''
   ||
      on_error(2);
      _wyn:=($CDDEF.DEF_FORM)();
      {? in_error() || _wyn:=~~ ?};
      on_error()
   ?}
?};
::sprawdzenie poprawnosci wartosci
::jezeli niepoprawna to nadanie wartosci zerowej
{? type_of(_wyn)>100 | _wyn<>~~
||
   {? CDDEF.TAT().TYP='S'
   ||
      {? (type_of(_wyn)=type_of(null())) & (ref_name(_wyn)=SLO.name())
      ||
         SLO.cntx_psh(); SLO.prefix();
         {? (~SLO.seek(_wyn)) | (SLO.SLU<>CDDEF.TAT().SLU) || _wyn:=~~ ?};
         SLO.cntx_pop()
      || _wyn:=~~
      ?}
   |? CDDEF.TAT().TYP='U'
   ||
      {? (type_of(_wyn)=type_of(null())) & (ref_name(_wyn)=UD_SKL.name())
      ||
         UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_DEF.prefix(CDDEF.TAT().UD_SCH);
         {? UD_DEF.find_key(_wyn)
         ||
            {? UD_DEF.UD_SCH<>CDDEF.TAT().UD_SCH || _wyn:=~~ ?}
         || _wyn:=~~
         ?};
         UD_DEF.cntx_pop()
      || _wyn:=~~
      ?}
   |? CDDEF.TAT().TYP='D'
   ||
      {? type_of(_wyn)<>type_of(date()) || _wyn:=~~ ?}
   |? CDDEF.TAT().TYP='C'
   ||
      {? type_of(_wyn)<>type_of(time()) || _wyn:=~~ ?}
   |? CDDEF.TAT().TYP='L'
   ||
      {? type_of(_wyn)<>type_of(0) || _wyn:=~~ ?}
   |? CDDEF.TAT().TYP='B'
   ||
      {? (type_of(_wyn)<>type_of(''))
         | ((_wyn<>exec('get_war_false','cechy')) & (_wyn<>exec('get_war_true','cechy')))
      || _wyn:=~~
      ?}
   |? CDDEF.TAT().TYP='X'
   || {? type_of(_wyn)<=100 | type_of(_wyn[1])<>type_of('') | type_of(_wyn[2])<>type_of('')
      || _wyn:=~~
      ?}
   || {? (type_of(_wyn)<>type_of('')) || _wyn:=~~ ?}
   ?}
?};
_wyn


\atr_dolacz_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Dołączanie wartosci cechy z okienka CDWAR dla kontrahentów
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__cdtab',@)>0 & __cdtab=KH
|| exec('atr_dolacz','cechy','KH')
|? var_pres('__cdtab',@)>0 & __cdtab=SRSR
|| exec('atr_dolacz','cechy','SRSR')
|| FUN.emsg('Bieżąca tabela danych nie obsługuje informacji dodatkowych.'@)
?}


\atr_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Dolaczenie zestawu danych dla biezacego rekordu tabeli
::   WE: _a - akronim tabeli
::----------------------------------------------------------------------------------------------------------------------
CDTAB.index('CDTAB');
CDTAB.prefix();
{? CDTAB.find_key(_a,) || _tab:=CDTAB.ref() || _tab:=null ?};
{? _tab=null
|| FUN.emsg('Niepoprawny parametr - tabela nieobsługiwana przez mechanizm informacji dodatkowych.'@);
   return(0)
?};

{? _a='KH'   || _uidref:=KH.uidref()
|? _a='SRSR' || _uidref:=SRSR.uidref()
?};

exec('set_cdwar_maska','cechy',_a);
CDWAR.prefix(_uidref);
{? CDWAR.first()
|| {? ~FUN.ask('Bieżący zapis zawiera już informacje dodatkowe. Zaktualizować?'@)
   || return(0)
   ?}
?};

CDDEF.cntx_psh();
CDDEF.index('CDKOL');
CDDEF.prefix(_tab);

{? CDDEF.first()
|| {! |?
         CDWAR.index('CDTAT');
         CDWAR.prefix(_uidref,CDDEF.TAT);
         {? ~CDWAR.first()
         || CDWAR.blank();
            CDWAR.REF:=_uidref;
            CDWAR.KOL:=CDDEF.KOL;
            CDWAR.TAT:=CDDEF.TAT;
            _war:=exec('get_default','cechy',CDWAR.ref());
            {? type_of(_war)>100 | _war<>~~
            ||
               {? CDWAR.TAT().TYP='L' || CDWAR.WAR:=form(_war,,,'9,')
               |? CDWAR.TAT().TYP='T' || CDWAR.WAR:=_war
               |? CDWAR.TAT().TYP='D' || CDWAR.WAR:=$_war
               |? CDWAR.TAT().TYP='C' || CDWAR.WAR:=_war $ 3
               |? CDWAR.TAT().TYP='B' || CDWAR.WAR:=_war
               |? CDWAR.TAT().TYP='S' || CDWAR.SLO:=_war;
                                         CDWAR.WAR:={? CDDEF.SLOPOLE='K'
                                                    || CDWAR.SLO().KOD
                                                    || CDWAR.SLO().TR
                                                    ?}
               |? CDWAR.TAT().TYP='U' || CDWAR.UD_SKL:=_war;
                                         CDWAR.WAR:={? CDDEF.SLOPOLE='K'
                                                    || CDWAR.UD_SKL().SYMBOL
                                                    || CDWAR.UD_SKL().OPIS
                                                    ?}
               |? CDWAR.TAT().TYP='X' || CDWAR.WAR:=_war[1];
                                         CDWAR.REF_SQL:=_war[2]
               || ~~
               ?}
            ?};
            {? CDWAR.WAR='' & CDWAR.TAT().TYP='O'
            || CDWAR.WAR:=STR.gsub(CDDEF.memo_txt(,1,'OPIS'),'\n',' ')
            ?};
            CDWAR.add()
         ?};
         CDDEF.next()
   !}
|| FUN.emsg('Brak definicji informacji dodatkowych.'@)
?};
CDDEF.cntx_pop();
CDWAR.index('CDKOL');

CDWAR.prefix(_uidref);
CDWAR.first()


\atr_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwraca czy edycyjne wypelnienie wartosci dla atrybutu podanej KOL typu CDDEF
::   WE: _a - CDTAB.ref()
::       _b - CDWAR.KOL
::   WY: 1 - tak; 0 - nie
::----------------------------------------------------------------------------------------------------------------------
_odp:='T';
CDDEF.cntx_psh();
CDDEF.index('CDKOL');
CDDEF.prefix(_a,_b);
{? CDDEF.first()
||
:sprawdź blokadę pola
   _odp:={? CDDEF.EDIT<>'T' || 'T' || 'N' ?}
?};
CDDEF.cntx_pop();
_odp


\get_atr_prec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwraca precyzje dla atrybutu typu liczbowego
::   WE: _a - tabela
::       _b - CDWAR.KOL
::   WY: precyzja
::----------------------------------------------------------------------------------------------------------------------
_odp:=0;
{? _b>0
|| CDDEF.cntx_psh();
   CDDEF.index('CDKOL');
   CDDEF.prefix(_a,_b);
   {? CDDEF.first() || _odp:=CDDEF.TAT().PREC ?};
   CDDEF.cntx_pop()
?};
_odp


\get_atr_tsep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwraca zawartość pola 'czy separator tysięcznych' dla pola liczbowego
::   WE: _a - tabela (CDTAB.ref)
::       _b - CDDEF.KOL
::   WY: 1 - tak; 0 - nie
::----------------------------------------------------------------------------------------------------------------------
_odp:=0;
{? _b>0
|| CDDEF.cntx_psh();
   CDDEF.index('CDKOL');
   CDDEF.prefix(_a,_b);
   {? CDDEF.first() || _odp:=CDDEF.TAT().TSEP ?};
   CDDEF.cntx_pop()
?};
_odp


\set_tak_nie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwraca wartosc odpowiadajaca prawdzie lub falszowi dla atrybutow typu tak/nia
::   WE: _a - tresc pytania ['']
::       _b -  wartosc przy braku odpowiedzi ['']
::   WY: wartosc dla atrybutu typu Tak/Nie
::----------------------------------------------------------------------------------------------------------------------
{? (_<1) | (_a=~~) || _a:='' ?};
{? (_<2) | (_b=~~) || _b:='' ?};
{? type_of(_a)<>type_of('') || &_a; _a:='' ?};
{? type_of(_b)<>type_of('') || &_b; _b:='' ?};

_odp:=FUN.choice(_a,'ASK','Tak','Nie');
{? _odp=1 || exec('get_war_true','cechy')
|? _odp=2 || exec('get_war_false','cechy')
|| _b
?}


\get_atr_slopol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwraca wartosc pola SLOPOLE dla atrybutu podanej KOL tabeli
::   WE: _a - tabela
::       _b - CDWAR.KOL
::   WY: '' - gdy nieokreslono SLOPOLE
::----------------------------------------------------------------------------------------------------------------------
_odp:='';
{? _b>0
|| CDDEF.cntx_psh();
   CDDEF.index('CDKOL');
   CDDEF.prefix(_a,_b);
   {? CDDEF.first() || _odp:=CDDEF.SLOPOLE ?};
   CDDEF.cntx_pop()
?};
_odp


\get_atr_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwraca czy jest formuła po edycji dla atrybutu podanej KOL definicji cechy dla tabeli
::   WE: _a - tabela (CDTAB.ref)
::       _b - CDWAR.KOL
::       _c - pole
::   WY: 1 - tak; 0 - nie
::----------------------------------------------------------------------------------------------------------------------
{? (_<2) | (_b=~~) || _b:=0 ?};
{? (type_of(_b)<>type_of(0)) || &_b;_b:=0 ?};
{? (type_of(_c)<>type_of('')) || _c:='AE' ?};

_odp:='';
{? _b>0
|| CDDEF.cntx_psh();
   CDDEF.index('CDKOL');
   CDDEF.prefix(_a,_b);
   {? CDDEF.first() || _odp:=($('CDDEF.'+_c))() ?};
   CDDEF.cntx_pop()
?};
_odp


\po_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Wykonanie formuły po edycji atrybutu
::   WE: _a - alias tabeli źródłowej
::----------------------------------------------------------------------------------------------------------------------
_form:=exec('get_atr_form','cechy',_a,CDWAR.KOL,'AE');
{? |_form<>''
|| no_msg(1); on_error(3);
   _wynik:=($_form)();
   {? in_error() || FUN.info('Formuła po edycji atrybutu zawiera błąd.'@)
   |? var_pres('_wynik')=type_of('') & _wynik<>'' || FUN.info(_wynik)
   ?};
   on_error(); no_msg(0)
?}


\atr_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Poprawa wartosci atrybutu
::----------------------------------------------------------------------------------------------------------------------
_cdt:=exec('find_cdtab','cechy',{? __cdtab=KH || 'KH' |? __cdtab=SRSR || 'SRSR' ?});
{? _cdt=null
|| FUN.emsg('Niepoprawna konfiguracja edycji informacji dodatkowych.'@);
   return(0)
?};

{? exec('atr_edit','cechy',_cdt,CDWAR.KOL)<>'T'
|| FUN.info('Założono blokadę edycji tego atrybutu.'@);
   return(0)
?};

{? __cdtab.r_lock(1,1,1)
||
   {? (CDWAR.TAT<>null()) | (CDWAR.KOL>0)
   || _po_red:=0;
      {? CDWAR.TAT().TYP='O'
      || FUN.info('Atrybutu typu "Nagłówek" nie można poprawiać.'@)
      |? TAT.TYP='L'
      || undefine();
         _value:=#STR.gsub(STR.gsub(CDWAR.WAR,' ',''),',','.');
         define('WAR',_value,CDWAR.TAT().OPIS,CDWAR.TAT().OPIS,20);
         _zapisz:=def_btn('text=%1'['Zapisz'@],'key:F2');
         _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
         def_bopt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
         def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
         {? def_edit(,CDWAR.TAT().OPIS)
         || _prec:=exec('get_atr_prec','cechy',_cdt,CDWAR.KOL);
            _tsep:=form(exec('get_atr_tsep','cechy',_cdt,CDWAR.KOL));
            CDWAR.WAR:=form(DEFINE.WAR$_prec,,,{? _tsep='T' || ' ,' || '9,'?});
            _po_red:=CDWAR.put()
         ?};
         undefine()
      |? TAT.TYP='T'
      || undefine();
         define('WAR',
                CDWAR.WAR,
                CDWAR.TAT().OPIS,CDWAR.TAT().OPIS,255,80);
         _zapisz:=def_btn('text=%1'['Zapisz'@],'key:F2');
         _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
         def_bopt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
         def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
         {? def_edit(,CDWAR.TAT().OPIS)
         || CDWAR.WAR:=DEFINE.WAR;
            _po_red:=CDWAR.put()
         ?};
         undefine()
      |? TAT.TYP='D'
      || undefine();
         define('WAR',
                date(#(4+CDWAR.WAR),
                     #(2+(5-CDWAR.WAR)),
                     #(CDWAR.WAR+2)),
                CDWAR.TAT().OPIS,CDWAR.TAT().OPIS,10,10);
         _zapisz:=def_btn('text=%1'['Zapisz'@],'key:F2');
         _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
         def_bopt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
         def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
         {? def_edit(,CDWAR.TAT().OPIS)
         || CDWAR.WAR:=$DEFINE.WAR;
            _po_red:=CDWAR.put()
         ?};
         undefine()
      |? TAT.TYP='C'
      || undefine();
         _war:=CDWAR.WAR;
         _pos:=_war*':';
         _gg:=_mm:=_ss:=0;
         {? _pos>0
         || _gg:=#((_pos-1)+_war);
            _mm:=#(2+(_pos-_war));
            _ss:=#(_war+2)
         ?};
         define('WAR',
                time(_gg,_mm,_ss),
                CDWAR.TAT().OPIS,CDWAR.TAT().OPIS,10,10);
         _zapisz:=def_btn('text=%1'['Zapisz'@],'key:F2');
         _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
         def_bopt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
         def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
         {? def_edit(,CDWAR.TAT().OPIS)
         || _time:=DEFINE.WAR;
            {? _time>=time(24,0,0) || _time:=time(23,59,59) ?};
            CDWAR.WAR:=_time $ 3;
            _po_red:=CDWAR.put()
         ?};
         undefine()
      |? TAT.TYP='B'
      || CDWAR.WAR:=exec('set_tak_nie','cechy',CDWAR.TAT().OPIS,CDWAR.WAR);
         _po_red:=CDWAR.put()
      |? TAT.TYP='S'
      || SLO.cntx_psh();
         SLO.index('SL'); SLO.prefix(CDWAR.TAT().SLU);
         {? CDWAR.SLO<>null
         || SLO.seek(CDWAR.SLO)
         || SLO.first()
         ?};
         SLO.win_sel('ONE_SEL');
         {? SLO.select(,1,1)
         || CDWAR.SLO:=SLO.ref();
            _pole:=exec('get_atr_slopol','cechy',_cdt,CDWAR.KOL);
            CDWAR.WAR:={? _pole='T' || SLO.TR || SLO.KOD ?};
            _po_red:=CDWAR.put()
         || {? (CDWAR.SLO<>null()) & FUN.ask('Usunąć istniejącą wartość?'@)
            || CDWAR.WAR:=''; CDWAR.SLO:=null(); CDWAR.put()
            ?}
         ?};
         SLO.cntx_pop()
      |? TAT.TYP='U'
      || UD_DEF.cntx_psh();
         UD_DEF.prefix();
::          odnalezienie UD_DEF odpowiadajacego wybranemu UD_SKL aby ewentualnie odpowiednio ustawic okienko
         _cur:=null();
         {? CDWAR.UD_SKL<>null()
         || UD_DEF.index('SKLREF');
            UD_DEF.prefix(CDWAR.UD_SKL);
            {? UD_DEF.first() || _cur:=UD_DEF.ref() ?}
         ?};
         UD_DEF.index('SYMBOL'); UD_DEF.prefix(CDWAR.TAT().UD_SCH);
         UD_DEF.first();
         {? _cur<>null() || UD_DEF.seek(_cur) ?};
         UD_DEF.win_sel('WYB');
         {? UD_DEF.select(,1,1)
         || CDWAR.UD_SKL:=UD_DEF.UD_SKL;
            _pole:=exec('get_atr_slopol','cechy',_cdt,CDWAR.KOL);
            CDWAR.WAR:={? _pole='T' || UD_DEF.UD_SKL().OPIS || UD_DEF.UD_SKL().SYMBOL ?};
            _po_red:=CDWAR.put()
         || {? (CDWAR.UD_SKL<>null()) & FUN.ask('Usunąć istniejącą wartość?'@)
            || CDWAR.WAR:=''; CDWAR.UD_SKL:=null(); CDWAR.put()
            ?}
         ?};
         UD_DEF.cntx_pop()
      |? TAT.TYP='X'
      || __cdtab.cntx_psh();
         __cdtab.prefix();
         {? __cdtab.seek(CDWAR.REF)
         || _t_slo:=exec('get_atr_form','cechy',_cdt,CDWAR.KOL,'TSLO');
            {? _t_slo<>''
            || no_msg(1); on_error(3);
               _t_slo:=($_t_slo)();
               {? in_error()
               || FUN.info('Formuła na słownik tymczasowy zawiera błąd.'@);
                  on_error(); no_msg(0)
               |? var_pres('_t_slo')<>type_of(tab_tmp(1,'AA','STRING[1]','aa'))
               || FUN.info('Formuła na słownik tymczasowy nie zwraca wskazania na słownik.'@);
                  on_error(); no_msg(0)
               || on_error(); no_msg(0);
                  _t_slo.index(_t_slo.ndx_tmp(,,_t_slo.fld_acr(3),,));
                  _acr_sel:=_t_slo.mk_sel(CDWAR.TAT().OPIS,,0);
                  _t_slo.win_fld(_acr_sel,,_t_slo.fld_acr(1),,,8,,1,'Kod'@);
                  _t_slo.win_fld(_acr_sel,,_t_slo.fld_acr(2),,,80,,1,'Treść'@);
                  _t_slo.win_act(_acr_sel,0,'Formuła','Wybierz'@@,,'Wybór bieżącego rekordu'@,"sel_exit()",,1);
                  _t_slo.win_act(_acr_sel,0,'Szukaj',,,,,,0);
                  _t_slo.win_sel(_acr_sel);
                  {? var_pres('_t_slo')=type_of(SYSLOG)
                     & ((CDWAR.REF_SQL<>'' & _t_slo.find_key(CDWAR.REF_SQL)) | _t_slo.first())
                     & _t_slo.select(,1,1)
                  || _pole:=exec('get_atr_slopol','cechy',_cdt,CDWAR.KOL);
                     CDWAR.WAR:={? _pole='T' || _t_slo[2] || _t_slo[1] ?};
                     CDWAR.REF_SQL:=_t_slo[3];
                     _po_red:=CDWAR.put()
                  || {? (CDWAR.REF_SQL<>'') & FUN.ask('Usunąć istniejącą wartość?'@)
                     || CDWAR.WAR:=''; CDWAR.REF_SQL:=''; CDWAR.put()
                     ?}
                  ?}
               ?}
            || FUN.info('Brak wskazania słownika.'@)
            ?}
         || FUN.info('Nie znaleziono zapisu.'@)
         ?};
         __cdtab.cntx_pop()
      || ~~
      ?};
      {? _po_red
      || exec('po_red','cechy',_cdt)
      ?}
   || FUN.info('Ten element nie może mieć nadawanej wartości.'@)
   ?}
|| FUN.info('Dane zapisu są właśnie redagowane przez innego użytkownika. Spróbuj za chwilę.'@)
?};
__cdtab.r_unlock();
~~


\cdwar_sel_sr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Informacje dodatkowe środka trwałego
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__cdtab');
__cdtab:=SRSR;
exec('set_cdwar_maska','cechy','SRSR');
CDWAR.win_sel('WER');
CDWAR.index('CDKOL');
CDWAR.prefix(SRSR.uidref());
{! |?
   CDWAR.select();
   {? ~exec('cdwar_wymag','cechy')
   || FUN.emsg('Nie uzupełniono wszystkich wymaganych informacji dodatkowych.'@);
      _loop:=1
   || _loop:=0
   ?};
   _loop
!}


\cddef_wymag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Czy pole wymagane?
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
CDDEF.cntx_psh();
CDDEF.index('CDKOL');
CDDEF.prefix(_a,_b);
{? CDDEF.first()
|| _wy:={? CDDEF.WYMAG='T' || 1 || 0 ?}
?};
CDDEF.cntx_pop();
_wy


\cddef_czy_zero
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Czy wartosc zero dopuszczalna
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
CDDEF.cntx_psh();
CDDEF.index('CDKOL');
CDDEF.prefix(_a,_b);
{? CDDEF.first()
|| _wy:=(CDDEF.CZY_ZERO='D')
?};
CDDEF.cntx_pop();
_wy


\cdwar_wymag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Czy uzupelniono wymagane Informacje dodatkowe
::----------------------------------------------------------------------------------------------------------------------
_wy:=1;
CDWAR.cntx_psh();
{? CDWAR.first()
|| {! |?
      _cdt:=exec('find_cdtab','cechy',{? __cdtab=KH || 'KH' |? __cdtab=SRSR || 'SRSR' ?});
      {? exec('cddef_wymag','cechy',_cdt,CDWAR.KOL)
      || {? ( CDWAR.WAR='' | (CDWAR.TAT().TYP='L' & exec('cddef_czy_zero','cechy',_cdt,CDWAR.KOL)=0
                                                  & #STR.gsub(STR.gsub(CDWAR.WAR,' ',''),',','.')=0))
         || _wy:=0
         ?}
      ?};
      _wy & CDWAR.next()
   !}
?};
CDWAR.cntx_pop();
_wy


\cdwar_color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Kolorowanie rekordów informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_cdt:=exec('find_cdtab','cechy',{? __cdtab=KH || 'KH' |? __cdtab=SRSR || 'SRSR' ?});
{? exec('cddef_wymag','cechy',_cdt,CDWAR.KOL) || 'CDWAR#01#01' || '' ?}


\cdwar_rp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formuła na Rekord przed tabeli CDWAR (wartości informacji dodatkowych dla tabel)
::----------------------------------------------------------------------------------------------------------------------
exec('rekprzed','color','CDWAR#01#')


\cdwar_sel_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Informacje dodatkowe kontrahenta
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__cdtab');
__cdtab:=KH;
exec('set_cdwar_maska','cechy','KH');
CDWAR.win_sel('WER');
CDWAR.index('CDKOL');
CDWAR.prefix(KH.uidref());
{! |?
   CDWAR.select();
   {? ~exec('cdwar_wymag','cechy')
   || FUN.emsg('Nie uzupełniono wszystkich wymaganych informacji dodatkowych.'@);
      _loop:=1
   || _loop:=0
   ?};
   _loop
!}


\cddef_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Dolaczenie nowego atrybutu do schematu atrybutow tabeli
::----------------------------------------------------------------------------------------------------------------------
CDDEF.win_edit('RED');
{? __cdtab=KH
|| _txt1:='kontrahentów.'@;
   _txt2:='kontrahentach.'@
|? __cdtab=SRSR
|| _txt1:='środków trwałych.'@;
   _txt2:='środkach.'@
?};
exec('set_cdwar_maska','cechy', {? __cdtab=KH || 'KH' |? __cdtab=SRSR || 'SRSR' ?});
CDWAR.prefix();
_uzywany:=CDWAR.first();
_new_ref:=null;
{? (_uzywany=0)
   | FUN.ask('Wprowadzono już informacje dodatkowe dla %1'
             '\nDodanie nowego elementu schematu spowoduje dopisanie go'
             '\ndo pozycji informacji dodatkowych przy istniejących %2'
             '\nPo dopisaniu do środków nie będzie możliwe usunięcie elementu ze schematu.'
             '\nKontynuować?'@[_txt1,_txt2])
||
   CDDEF.cntx_psh();
   CDDEF.win_edit('RED');
   CDDEF.prefix();
   CDDEF.blank();
   CDDEF.CDTAB:=exec('find_cdtab','cechy',{? __cdtab=KH || 'KH' |? __cdtab=SRSR || 'SRSR' ?});
   VAR.TYP_ATR:='';
   CDDEF.memo_set('','OPIS');
   {? CDDEF.edit("_wy:=chk_rec('TAT');
                  {? _wy=''
                  || {? CDDEF.WYMAG='T' & CDDEF.EDIT='T'
                     || FUN.emsg('Blokada edycji i wymagane uzupełnienie nie mogą być zaznaczone jednocześnie.'@);
                        _wy:='WYMAG'
                     ?}
                  ?};
                  _wy
                 ")
   ||
      exec('cddef_clear_def','cechy');
      {? _uzywany
      ||
::       pobranie wartosci do wypelnienia pol, gdy schemat zostal uzyty
         exec('cddef_set_default','cechy')
      ?};
::    zablokowanie jakichkolwiek zmian atrybutow przez innych uzytkownikow
      {? CDDEF.lock(1,1,1) & CDDEF.lock(1,1,1)
      ||
         _do:=0;
         {? do_state()=0 || do(); _do:=1 ?};
         {? CDDEF.add(1)
         ||
            CDDEF.memo_put(,'OPIS');
            _new_ref:=CDDEF.ref();
            {? _uzywany
            ||
::             wyszukanie kontrahentow lub srodkow danego typu do kotorych dopisano atrybuty i uzupelnienie o nową cechę
               {? __cdtab=KH
               || KH.cntx_psh();
                  KH.prefix();
                  KH.for_each("exec('atr_uzup_kh','cechy',CDDEF.ref())");
                  KH.cntx_pop()
               |? __cdtab=SRSR
               || SRSR.cntx_psh();
                  FIRMA.cntx_psh();
                  FIRMA.index('SYMBOL2');
                  FIRMA.prefix('S');
                  {? FIRMA.first()
                  || {! |?
                        SRSR.use('srsrr'+FIRMA.SYMBOL);
                        SRSR.prefix();
                        CDWAR.cntx_psh();
                        _maska:='cdwsr'+FIRMA.SYMBOL;
                        {? CDWAR.name()<>_maska || CDWAR.use(_maska) ?};
                        SRSR.for_each("exec('atr_uzup_srsr','cechy',CDDEF.ref())");
                        CDWAR.cntx_pop();
                        FIRMA.next()
                     !}
                  ?};
                  FIRMA.cntx_pop();
                  SRSR.cntx_pop()
               ?}
            ?}
         || undo();
            FUN.info('Takie pole informacji dodatkowych jest już zdefiniowane dla bieżącej tabeli danych.'@)
         ?};
         {? _do || end() ?};
         CDDEF.unlock(); CDDEF.unlock()
      ||
         CDDEF.unlock(); CDDEF.unlock();
         FUN.info('Dane zablokowane przez innego użytkownika.\nDodanie pozycji nie powiodło się.'@)
      ?}
   ?};
   CDDEF.cntx_pop()
?};
{? _new_ref<>null || CDDEF.seek(_new_ref) ?};
~~


\set_cdwar_maska
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formuła ustawia maski tabeli wartości cech dodatkowych
::   WE: 'KH' lub 'SRSR'
::----------------------------------------------------------------------------------------------------------------------
_maska:={? _a='KH'   || 'cdwkontr'
        |? _a='SRSR' || 'cdwsr'+REF.FIRMA().SYMBOL
        ?};
{? CDWAR.name()<>_maska || CDWAR.use(_maska) ?}


\atr_uzup_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formuła uzupełnia cechy dla kontrahentów po dodaniu nowej cechy do informacji dodatkowych dla kontrahentów
::   WE: _a - ref rekordu CDDEF - definicji cechy
::----------------------------------------------------------------------------------------------------------------------
CDWAR.cntx_psh();
exec('set_cdwar_maska','cechy','KH');
CDWAR.index('CDKOL');
CDWAR.prefix(KH.uidref());
:: jeżeli są informacje dodatkowe to uzupełnia
{? CDWAR.first()
|| CDDEF.index('CDKOL');
   CDDEF.prefix();
   {? CDDEF.seek(_a)
   || CDWAR.index('CDKOL');
      CDWAR.prefix(KH.uidref(),CDDEF.KOL);
      {? ~CDWAR.first()
      || CDWAR.blank(1);
         CDWAR.REF:=KH.uidref();
         CDWAR.KOL:=CDDEF.KOL;
         CDWAR.TAT:=CDDEF.TAT;
         CDWAR.WAR:=CDDEF.DEFAULT;
         CDWAR.SLO:=CDDEF.SLO;
         CDWAR.UD_SKL:=CDDEF.UD_SKL;
         CDWAR.add()
      ?}
   ?}
?};
CDWAR.cntx_pop()


\atr_uzup_srsr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formuła uzupełnia cechy dla środków po dodaniu nowej cechy do informacji dodatkowych środków
::   WE: _a - ref rekordu CDDEF - definicji cechy
::----------------------------------------------------------------------------------------------------------------------
CDWAR.index('CDKOL');
CDWAR.prefix(SRSR.uidref());
:: jeżeli są informacje dodatkowe to uzupełnia
{? CDWAR.first()
|| CDDEF.index('CDKOL');
   CDDEF.prefix();
   {? CDDEF.seek(_a)
   || CDWAR.index('CDKOL');
      CDWAR.prefix(SRSR.uidref(),CDDEF.KOL);
      {? ~CDWAR.first()
      || CDWAR.blank(1);
         CDWAR.REF:=SRSR.uidref();
         CDWAR.KOL:=CDDEF.KOL;
         CDWAR.TAT:=CDDEF.TAT;
         CDWAR.WAR:=CDDEF.DEFAULT;
         CDWAR.SLO:=CDDEF.SLO;
         CDWAR.UD_SKL:=CDDEF.UD_SKL;
         CDWAR.add()
      ?}
   ?}
?}


\czy_def_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Sprawdza czy użyto atrybutu
::   WE: _a - ref atrybutu
::   WY: 1- tak, 2 - nie
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
CDWAR.cntx_psh();
{? __cdtab=KH
|| exec('set_cdwar_maska','cechy', 'KH');
   CDWAR.index('TAT');
   CDWAR.prefix(_a);
   _wy:=CDWAR.first()
|? __cdtab=SRSR
|| FIRMA.cntx_psh();
   FIRMA.index('SYMBOL2');
   FIRMA.prefix('S');
   {? FIRMA.first()
   || {! |?
         CDWAR.use('cdwsr'+FIRMA.SYMBOL);
         CDWAR.index('TAT');
         CDWAR.prefix(_a);
         _wy:=CDWAR.first();
         _wy=0 & FIRMA.next()
      !}
   ?};
   FIRMA.cntx_pop()
?};
CDWAR.cntx_pop();
_wy


\cddef_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: pJ [18.22]
:: OPIS: Formula po edycji schematu atrybutu tabeli (kontrahentów lub środków trwałych)
::----------------------------------------------------------------------------------------------------------------------
CDDEF.memo_put(,'OPIS')


\cddef_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Pozwolenie na edycje schematu atrybutow tabeli kontrahentów lub środków trwałych
::   WY: 1 - tak, 0 -nie
::----------------------------------------------------------------------------------------------------------------------
{? exec('czy_def_war','cechy',CDDEF.TAT)
|| FUN.info('Wprowadzono już zapisy do tabel dla wskazanej cechy. Edycja niemożliwa'@); 0
|| CDDEF.memo_get(,'OPIS');
   1
?}


\cddef_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formuła usuwa definicję bieżącej cechy
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('czy_def_war','cechy',CDDEF.TAT)
|| _cdt:=exec('find_cdtab','cechy');
   {? _cdt
   || {? CDDEF.get()
      || {? CDDEF.r_lock(1,1,1)
         || CDDEF.r_unlock();
            {? CDDEF.del()
            || CDDEF.cntx_psh();
               CDDEF.index('CDKOL');
               CDDEF.prefix(_cdt);
               {? CDDEF.first()
               || _i:=1;
                  {! |?
                     {? CDDEF.KOL<>_i
                     || CDDEF.KOL:=_i;
                        CDDEF.put()
                     ?};
                     _i+=1;
                     CDDEF.next()
                  !}
               ?};
               CDDEF.cntx_pop()
            ?}
         || FUN.emsg('Bieżący zapis jest obsługiwany przez innego użytkownika i nie można go usunąć.'@)
         ?}
      || FUN.emsg('Bieżący zapis został już usunięty przez innego użytkownika.'@)
      ?}
   || FUN.emsg('Bieżąca tabela danych nie została poprawnie skonfigurowana do obsługi informacji dodatkowych.'@)
   ?}
|| FUN.emsg('Wskazana definicja informacji dodatkowej jest wykorzystywana w systemie i nie można jej ususnąć.'@)
?}


\cddef_dopis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Modyfikacja opisu dodatkowego atrybutu
::----------------------------------------------------------------------------------------------------------------------
CDDEF.memo_get(,'DOPIS');
CDDEF.memo_edi(,'DOPIS');
CDDEF.memo_put(,'DOPIS')


\cddef_rp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Rekord przed CDDEF
::----------------------------------------------------------------------------------------------------------------------
VAR.TYP_ATR:={? CDDEF.TAT().TYP='L' || 'Liczba'
             |? CDDEF.TAT().TYP='D' || 'Data'
             |? CDDEF.TAT().TYP='C' || 'Czas'
             |? CDDEF.TAT().TYP='T' || 'Tekst'
             |? CDDEF.TAT().TYP='B' || 'Tak/Nie'
             |? CDDEF.TAT().TYP='S' || 'Słownik'
             |? CDDEF.TAT().TYP='U' || 'Schemat struktury'
             |? CDDEF.TAT().TYP='X' || 'Dowolny słownik'
             |? CDDEF.TAT().TYP='O' || 'Nagłówek'
             || ''
             ?};
CDDEF.cntx_psh();
CDDEF.index('CDKOL');
_cdt:=exec('find_cdtab','cechy');
CDDEF.prefix(_cdt);
_gray:='';
CDDEF.cntx_psh();
{? ~CDDEF.prev() || _gray+='G' ?};
CDDEF.cntx_pop();
CDDEF.cntx_psh();
{? ~CDDEF.next() || _gray+='W' ?};
CDDEF.cntx_pop();
CDDEF.actions_grayed(CDDEF.win_sel('?'),_gray);
CDDEF.cntx_pop();
~~


\cddef_ra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Rekord po CDDEF
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
{? CDDEF.win_edit('?')='RED'
|| {? CDDEF.WYMAG='T' & CDDEF.EDIT='T'
   || FUN.emsg('Blokada edycji i wymagane uzupełnienie nie mogą być zaznaczone jednocześnie.'@);
      _wy:='WYMAG'
   ?};
   {? _wy='' & __CHK.index(CDDEF,-menu_txt(,)='popraw')<>'' || _wy:='TAT' ?}
?};
_wy


\cddef_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formula na wyswietl w oknie WER tabeli CDDEF
::----------------------------------------------------------------------------------------------------------------------
CDDEF.win_edit('DISP');
CDDEF.display();
CDDEF.win_edit('RED');
1


\cddef_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Edycja danych wartosci domyslnej atrybutu
::----------------------------------------------------------------------------------------------------------------------
CDDEF.memo_get(,'OPIS');
CDDEF.win_edit('REDDEF');
{? CDDEF.TAT<>null & CDDEF.TAT().TYP='O'
|| CDDEF.efld_opt(CDDEF.win_edit('?'),'mark=0,enable=1',CDDEF,'OPIS')
|| CDDEF.efld_opt(CDDEF.win_edit('?'),'mark=0,enable=0',CDDEF,'OPIS')
?};
{? CDDEF.edit()
|| CDDEF.put();
   CDDEF.memo_put(,'OPIS')
?};
CDDEF.win_edit('RED');
~~


\cddef_clear_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Usuniecie wartosci domyslnej z definicji atrybutu
::----------------------------------------------------------------------------------------------------------------------
CDDEF.DEFAULT:=''; CDDEF.SLO:=CDDEF.UD_SKL:=null();
~~


\clear_def_for
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Usunięcie wartości domyślnej lub formuły zależnie od CDDEF.DEF_KIND
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.DEF_KIND='W'
|| exec('cddef_clear_def','cechy')
|| CDDEF.DEF_FORM:=''
?};
win_disp();
''


\set_def_for
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Umozliwienie podania wartosci domyslnej w zaleznosci od CDDEF.DEF_KIND
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.DEF_KIND='W'
|| exec('cddef_set_default','cechy')
|| undefine();
   define('FOR',CDDEF.DEF_FORM,'Formuła'@,,100,100);
   _ok:=def_btn('text=%1'['OK'@],'key:F2');
   _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
   def_bopt(_ok,'tooltip='+exec('help_red_ok','#window','P'));
   def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   {? def_edit(,'Formuła na obliczenie wartości początkowej'@)
   || CDDEF.DEF_FORM:=DEFINE.FOR
   ?};
   undefine()
?};
win_disp();
''


\cddef_set_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Redagowanie wartosci domyslnej
::----------------------------------------------------------------------------------------------------------------------
_title:='Podaj wartość, dla tego pola informacji dodatkowych dla tabeli?'@;
{? CDDEF.TAT().TYP='L'
|| undefine();
   _value:=#STR.gsub(CDDEF.DEFAULT,',','.');
   define('WAR',_value,CDDEF.TAT().NA,CDDEF.TAT().OPIS,20);
   _zapisz:=def_btn('text=%1'['Zapisz'@],'key:F2');
   _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
   def_bopt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
   def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   {? def_edit(,_title)
   || CDDEF.DEFAULT:=form(DEFINE.WAR,,,'9,')
   ?};
   undefine()
|? CDDEF.TAT().TYP='T'
|| undefine();
   define('WAR',CDDEF.DEFAULT,CDDEF.TAT().NA,CDDEF.TAT().OPIS,80,80);
   _zapisz:=def_btn('text=%1'['Zapisz'@],'key:F2');
   _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
   def_bopt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
   def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   {? def_edit(,_title)
   || CDDEF.DEFAULT:=DEFINE.WAR
   ?};
   undefine()
|? CDDEF.TAT().TYP='D'
|| undefine();
   define('WAR',date(0,0,0),CDDEF.TAT().NA,CDDEF.TAT().OPIS,10,10);
   _zapisz:=def_btn('text=%1'['Zapisz'@],'key:F2');
   _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
   def_bopt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
   def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   {? def_edit(,_title)
   || CDDEF.DEFAULT:=$DEFINE.WAR
   ?};
   undefine()
|? CDDEF.TAT().TYP='C'
|| undefine();
   define('WAR',time(0,0,0),CDDEF.TAT().NA,CDDEF.TAT().OPIS,10,10);
   _zapisz:=def_btn('text=%1'['Zapisz'@],'key:F2');
   _anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
   def_bopt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
   def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   {? def_edit(,_title)
   || _time:=DEFINE.WAR;
      {? _time>=time(24,0,0) || _time:=time(23,59,59) ?};
      CDDEF.DEFAULT:=_time $ 3
   ?};
   undefine()
|? CDDEF.TAT().TYP='B'
|| CDDEF.DEFAULT:=exec('set_tak_nie','cechy',CDDEF.TAT().OPIS,'')
|? CDDEF.TAT().TYP='S'
|| SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(CDDEF.TAT().SLU);
   SLO.win_sel('ONE_SEL');
   {? SLO.select(,1,1)
   || CDDEF.SLO:=SLO.ref();
      CDDEF.DEFAULT:={? CDDEF.SLOPOLE='T' || SLO.TR || SLO.KOD ?}
   ?};
   SLO.cntx_pop()
|? CDDEF.TAT().TYP='U'
|| UD_DEF.cntx_psh(); UD_DEF.index('SYMBOL'); UD_DEF.prefix(CDDEF.TAT().UD_SCH);
   UD_DEF.win_sel('WYB');
   {? UD_DEF.select(,1,1)
   || CDDEF.UD_SKL:=UD_DEF.UD_SKL;
      CDDEF.DEFAULT:={? CDDEF.SLOPOLE='T' || UD_DEF.UD_SKL().OPIS || UD_DEF.UD_SKL().SYMBOL ?}
   ?};
   UD_DEF.cntx_pop()
|| ~~
?}


\cddef_wg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zamiana wartosci w polu CDDEF.KOL pomiedzy biezacym i poprzednim rekordem, tak, ze zamieniaja sie miejscami
::----------------------------------------------------------------------------------------------------------------------
{? CDDEF.KOL>1
||
   _ja:=CDDEF.ref();
   _cdt:=CDDEF.CDTAB;
   _mojkol:=CDDEF.KOL;
   CDDEF.cntx_psh();
   CDDEF.index('CDKOL');
   CDDEF.prefix(_cdt);
   {? CDDEF.seek(_ja) & CDDEF.prev()
   ||
      _poprzkol:=CDDEF.KOL;
      _poprz:=CDDEF.ref();
      _do:=0;
      {? do_state()=0 || do();_do:=1 ?};
      CDDEF.KOL:=-1; CDDEF.put();
      {? CDDEF.seek(_ja)
      || CDDEF.KOL:=_poprzkol;
         CDDEF.put();
         {? CDDEF.seek(_poprz)
         || CDDEF.KOL:=_mojkol;
            CDDEF.put()
         || undo()
         ?}
      || undo()
      ?};
      {? _do || end() ?}
   ?};
   CDDEF.cntx_pop();
   CDDEF.seek(_ja)
?};
~~


\cddef_wd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zamiana wartosci w polu CDDEF.KOL pomiedzy biezacym i nastepnym rekordem, tak, ze zamieniaja sie miejscami
::----------------------------------------------------------------------------------------------------------------------
_ja:=CDDEF.ref();
_cdt:=CDDEF.CDTAB;
_mojkol:=CDDEF.KOL;
CDDEF.cntx_psh();
CDDEF.index('CDKOL');
CDDEF.prefix(_cdt);
{? CDDEF.seek(_ja) & CDDEF.next()
||
   _nastkol:=CDDEF.KOL;
   _nast:=CDDEF.ref();
   _do:=0;
   {? do_state()=0 || do();_do:=1 ?};
   CDDEF.KOL:=-1; CDDEF.put();
   {? CDDEF.seek(_ja)
   || CDDEF.KOL:=_nastkol;
      CDDEF.put();
      {? CDDEF.seek(_nast)
      || CDDEF.KOL:=_mojkol;
         CDDEF.put()
      || undo()
      ?}
   || undo()
   ?};
   {? _do || end() ?}
?};
CDDEF.cntx_pop();
CDDEF.seek(_ja);
~~


\cddef_kol_wp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Wartosc poczatkowa CDDEF.KOL
::   WY: wartosc o 1 > od najwyszszego numeru pola
::----------------------------------------------------------------------------------------------------------------------
CDDEF.cntx_psh();
CDDEF.index('CDKOL');
_cdt:=exec('find_cdtab','cechy',{? __cdtab=KH || 'KH' |? __cdtab=SRSR || 'SRSR' ?});
CDDEF.prefix(_cdt);
{? CDDEF.last()
|| _odp:=CDDEF.KOL+1
|| _odp:=1
?};
CDDEF.cntx_pop();
_odp


\cdwar_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Formuła usuwa wartości cech dla bieżącego zapisu
::----------------------------------------------------------------------------------------------------------------------
{? __cdtab=KH || _txt:='kontrahenta'@
|? __cdtab=SRSR || _txt:='środka trwałego'@
?};
:: test czy można usunąć
{? ~__cdtab.r_lock(1,1,1)
|| FUN.info('Dane są obecnie redagowane przez innego użytkownika. Informacji nie można usunąć.'@);
   return(0)
?};
_uidref:=__cdtab.uidref();
exec('set_cdwar_maska','cechy',{? __cdtab=KH || 'KH' |? __cdtab=SRSR || 'SRSR' ?});
CDWAR.index('CDKOL');
CDWAR.prefix(_uidref);
{? CDWAR.first()
|| {? FUN.ask('Usunąć wszystkie informacje dodatkowe dla bieżącego %1?'@[_txt])
   || __cdtab.r_unlock();
      {! |? CDWAR.del() !}
   || __cdtab.r_unlock()
   ?}
|| FUN.info('Brak danych do usunięcia. Prawdopodobnie informacje dodatkowe zostały usunięte przez innego użytkownika.'@)
?}


\cdwar_srsr_cnt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [18.22]
:: OPIS: Zwraca liczbę informacji dodatkowych przypisanych do bieżącego środka trwałego
::   WY: Liczba informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
CDWAR.cntx_psh();
exec('set_cdwar_maska','cechy','SRSR');
CDWAR.win_sel('WER');
CDWAR.index('CDKOL');
CDWAR.prefix(SRSR.uidref());
_wy:=CDWAR.size();
CDWAR.cntx_pop();
_wy

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 0a8d90c95d84c815fbcb3f0e1d7b7b311346a613b09bc1344d0114750bc6e090f9ba7d18d97b8feb31478c90086cb4680f9920befa67c2fc426fed17cd484eeae1d819972a4fec2704b11b6f32b3c0cf13afddb96f1a68776a4b61488497d0e18363d79d6630256217fe117452e2d43ee7f2399302c6b89592a9f56a909d8c8e

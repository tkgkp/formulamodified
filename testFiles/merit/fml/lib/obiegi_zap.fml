:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obiegi_zap.fml
:: Utworzony: 28.09.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa obiegów dokumentów - zapotrzebowania
::======================================================================================================================


\zapprzeg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Realizacje zapotrzebowan
::   WE: niezadeklarowane (=1) - wejscie na akcje, zadeklarowane - przy dolaczaniu dokumentu
::         _a - opcja dla jterm - 'PGD' - tylko do podglądu
::              opcja dla WT - 'AF' - akceptacja faktur
::                             'PF' - przekazanie faktur
::
::         _b - opcja dla WT- 1 - dla EDOKUMW
::                            2 - dla EDOKUM
::  OLD: \zapprzeg/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),
                         'typobi',1
                        ));
   {? _b=1
   || EDOKUM.use('skid_v'+(EDOKUMW.name()+2)); EDOKUM.prefix(); EDOKUMW.EDOKUM()
   ?};
   EDOKUM.web_cntx_save(1);
   EDOKOS.use('skid_y'+(EDOKUM.name()+2));
   {? _a='AF'
   || zakres:=2; exec('rkprz1','obiegi',2)
   || zakres:=5; exec('rkprz1','obiegi',5)
   ?};
   OBIEGI.EDOKOS();
   {? _a='AF' & exec('spr_stat','obiegi')='N' & EDOKUM.BUDZ_AKT<>'T' &
      exec('szuk_edokpar','obiegi') & (EDOKPAR.ED_PODZ='R' | EDOKPAR.ED_PODZ='W')
   || EDOKRZAP.web_win_init('WERGR_WT','EDOKRZAP','grayed=')
   || EDOKRZAP.web_win_init('WERGR_WT','EDOKRZAP','grayed=DPU:D')
   ?}
?};
{? EDOKUM.r_lock(1,1,1)
|| {? EDOKUM.TYP().ZAP_REAL='T'
   || {? app_info('web_sesid')=''
      || EDOKUM.cntx_psh(); EDOKRZAP.cntx_psh();
         EDOKRZAP.win_sel('WER_GR'); EDOKRZAP.win_edit('RED');
         EDOKRZAP.index('EDOKUM'); EDOKRZAP.prefix(EDOKUM.ref());
         EPODZ.win_edit('RED');
         exec('rzapsum','obiegi_zap');
         __edokr:=1;
         {? exec('spr_stat','obiegi')='N' & (zakres=1 | zakres=2) & EDOKUM.BUDZ_AKT<>'T' &
            exec('szuk_edokpar','obiegi') & (EDOKPAR.ED_PODZ='R' | EDOKPAR.ED_PODZ='W') &
            {? _>0 & _a='PGD' || 0 || 1 ?}
         || EDOKRZAP.select()
         || _default:=EDOKRZAP.actions('WER','DUP:D');
            _default2:=EPODZ.actions('WER3','dpu:d');
            EDOKRZAP.select();
            EDOKRZAP.actions('WER',,_default);
            EPODZ.actions('WER3',,_default2)
         ?};
         EDOKRZAP.cntx_pop(); EDOKUM.cntx_pop();
         VAR_DEL.delete('__edokr')
      || web_params_set(_get:=web_params_get());
         exec('env_wt','b_proces');
         EDOKRZAP.index('EDOKUM'); EDOKRZAP.prefix(EDOKUM.ref());
         EDOKRZAP.web_select('WERGR_WT')
      ?};
      exec('settypobi','obiegi2')
   || FUN.info('Wybrany rodzaj faktury w obiegu nie umożliwia rejestracji realizacji zapotrzebowań.'@)
   ?};
   EDOKUM.r_unlock()
|| exec('err_kom','obiegi')
?}


\bobs_edokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą okienka wertowania EDOKRZAP
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.web_cntx_load(1);
1


\rzapsum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Ustawianie podsumowan w okienku wertowania tabeli EDOKRZAP
::  OLD: \rzapsum/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| EDOKUM.web_cntx_load(1)
?};
SKID.SUMARZAP:=0;
EDOKRZAP.cntx_psh();
{? EDOKRZAP.f_active()
|| {? EDOKRZAP.f_first()
   || {! |?
         SKID.SUMARZAP+=EDOKRZAP.NETTO; EDOKRZAP.f_next()
      !}
   ?}
|| EDOKRZAP.index('EDOKUM'); EDOKRZAP.prefix(EDOKUM.ref());
   {? EDOKRZAP.first()
   || {! |?
         SKID.SUMARZAP+=EDOKRZAP.NETTO; EDOKRZAP.next()
      !}
   ?}
?};
EDOKRZAP.cntx_pop();
SKID.POZOSZAP:=EDOKUM.NETTO-SKID.SUMARZAP;
1


\arg_edokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Przed odswieżeniem w oknie grupowym WER_GR tabeli EDOKRZAP
::  OLD: \arg_edokrzap/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKRZAP.get()
|| EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref())
|| EPODZ.index('DSP_NULL'); EPODZ.prefix(null,null)
?};
grp_disp(EPODZ,'WER3');
grp_disp(EPODZ,'WER5')


\bu_edokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed obsluga okna WER3 tabeli EPODZ okna grupowego WER_GR tabeli EDOKRZAP
::  OLD: \bu_edokrzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| EPODZ.cntx_psh();
   {? EDOKRZAP.get()
   || {? ~exec('blok_podz','obiegi')
      || EPODZ.actions('WER3',,,1)
      || EPODZ.actions('WER3','dpu:d',,1)
      ?};
      EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref())
   || EPODZ.actions('WER3','dpu:d',,1);
      EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref())
   ?};
   exec('sum_podz','obiegi_zap')
|| {? var_pres('zakres')>0 || web_params_set('zakres',zakres) ?};
   EDOKRZAP.prefix();
   _edr:=web_win_ref('EDOKRZAP');
   {? type_of(_edr)<>type_of(~~) & EDOKRZAP.seek(_edr,ref_name(_edr),1)
   || EPODZ.use('skid_j'+(EDOKRZAP.name()+2));
      EDOKUMPR.use('skid_i'+(EDOKRZAP.name()+2)); EDOKUMPR.index('DISP'); EDOKUMPR.prefix();
      EDOKUM.use('skid_v'+(EDOKRZAP.name()+2));
      EDOKRZAP.EDOKUM();
      EPODZ.index('EDOKUMDS'); EPODZ.prefix(EDOKUM.ref());
      exec('sum_podz','obiegi')
   || EPODZ.index('EDOKUMDS'); EPODZ.prefix(null);
      SKID.SKXDRAZE:=SKID.SKXDPOZO:=0
   ?}
?};
1


\au_edokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Po obsludze okna WER3 tabeli EPODZ okna grupowego WER_GR tabeli EDOKRZAP
::  OLD: \au_edokrzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
EPODZ.cntx_pop();
1


\sum_podz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Naliczenie podsumowan w okienku wertowania tabeli EPODZ
::  OLD: \sum_podz/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
SKID.SKXDRAZE:=SKID.SKXDPOZO:=0;
EPODZ.cntx_psh();
{? EPODZ.first()
|| _model:=EPODZ.SKID_MB;
   {!
   |? _dsp_sum:=(_model=EPODZ.SKID_MB);
      _dsp_sum & EPODZ.next()
   !};
   {? _dsp_sum & EPODZ.first()
   || {!
      |? SKIDXDUD.cntx_psh();
         SKIDXDUD.index('POZ'); SKIDXDUD.prefix(EPODZ.PBUD);
         _wart:=(SKIDXDUD.first() & SKIDXDUD.WART_IL='W');
         SKIDXDUD.cntx_pop();
         {? _wart || SKID.SKXDRAZE+=EPODZ.WART ?};
         EPODZ.next()
      !};
      SKID.SKXDPOZO:={? __edokr=1 || EDOKRZAP.NETTO-SKID.SKXDRAZE ||  EDOKUM.NETTO-SKID.SKXDRAZE ?}
   ?}
?};
EPODZ.cntx_pop();
1


\bobs_red_edokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą okienka redakcji tabeli EDOKRZAP (WWW)
::----------------------------------------------------------------------------------------------------------------------
web_params_set(web_params_get());
{? app_info('web_sesid')<>'' || exec('env_wt','b_proces') ?};
EDOKUM.web_cntx_load(1);
EDOKUM.web_cntx_save(1);
1


\dolrezap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Dolaczanie realizacji zapotrzebowania
::  OLD: \dolrezap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| web_params_set(_get:=web_params_get());
   exec('env_wt','b_proces');
   EDOKUM.web_cntx_load(1);
   EDOKUM.web_cntx_save(1);
   EDOKRZAP.blank();
   EDOKRZAP.ROK_F:=SSTALE.AR;
   EDOKRZAP.EDOKUM:=EDOKUM.ref();
   EDOKRZAP.web_edit('RED_WT',,,,
                            "web_params_set(_get:=web_params_get());
                             exec('env_wt','b_proces');
                             {? _a='OK'
                             || _chk:=exec('sprrezap','obiegi_zap');
                                {? _chk=''
                                || EDOKRZAP.cntx_psh(); EDOKRZAP.prefix();
                                   {? EDOKRZAP.add()
                                   || exec('edokrzap_add','obiegi_zap')
                                   ?};
                                   EDOKRZAP.cntx_pop();
                                   EDOKRZAP.web_eclose();
                                   web_top_refresh(1)
                                ?}
                             || EDOKRZAP.web_eclose()
                             ?}")
|| EDOKUM.cntx_psh();
   EDOKRZAP.blank();
   EDOKRZAP.ROK_F:=SSTALE.AR;
   EDOKRZAP.EDOKUM:=EDOKUM.ref();
   zapotnam:=''; zapotref:=0;
   {? EDOKRZAP.edit("exec('sprrezap','obiegi_zap')")
   || EDOKRZAP.cntx_psh(); EDOKRZAP.prefix();
      {? EDOKRZAP.add()
      || exec('edokrzap_add','obiegi_zap')
      ?};
      EDOKRZAP.cntx_pop()
   ?};
   exec('rzapsum','obiegi_zap');
   exec('del_var','obiegi_zap');
   EDOKUM.cntx_pop()
?}


\poprezap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Poprawianie realizacji zapotrzebowania
::  OLD: \poprezap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| web_params_set(_get:=web_params_get());
   exec('env_wt','b_proces');
   EDOKUM.web_cntx_load(1);
   EDOKUM.web_cntx_save(1);
   EDOKRZAP.web_edit('RED_WT',,,,
                            "web_params_set(_get:=web_params_get());
                             exec('env_wt','b_proces');
                             {? _a='OK'
                             || _chk:=exec('sprrezap','obiegi_zap');
                                {? _chk=''
                                || EDOKRZAP.cntx_psh(); EDOKRZAP.prefix();
                                   {? EDOKRZAP.put()
                                   || EDOKUM.use('skid_v'+(EDOKRZAP.name()+2)); EDOKUM.prefix(); EDOKRZAP.EDOKUM();
                                      EPODZ.use('skid_j'+(EDOKRZAP.name()+2));
                                      EPODZ.index('EDOKRZAP');  EPODZ.prefix(EDOKUM.ref(),EDOKRZAP.ref());
                                      {? EPODZ.first() || {! |? EPODZ.del !} ?};
                                      exec('edokrzap_add','obiegi_zap')
                                   ?};
                                   EDOKRZAP.cntx_pop();
                                   EDOKRZAP.web_eclose();
                                   web_top_refresh(1)
                                ?}
                             || EDOKRZAP.web_eclose()
                             ?}")
|| zapotnam:=''; zapotref:=0;
   _ref:=EDOKRZAP.EZAPOT; _kwota:=EDOKRZAP.NETTO;
   {? EDOKRZAP.edit("exec('sprrezap','obiegi_zap')")
   || {? EDOKRZAP.put() & (_ref<>EDOKRZAP.EZAPOT | _kwota<>EDOKRZAP.NETTO)
      || EPODZ.index('EDOKRZAP');
         EPODZ.prefix(EDOKUM.ref(),EDOKRZAP.ref());
         {? EPODZ.first() || {! |? EPODZ.del !} ?};
         exec('edokrzap_add','obiegi_zap')
      ?}
   ?};
   exec('rzapsum','obiegi_zap');
   exec('del_var','obiegi_zap')
?}


\del_var
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Kasowanie zmiennych - wybor zapotrzebowan
::  OLD: \del_var/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('TABZAPOT','zaprind1','zaprind2','zaprind3','zaprind4','zaprind5');
VAR_DEL.delete('zakzapot','zapotnam','zapotref')


\sprrezap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Sprawdzenie parametrow realizacji zapotrzebowania
::  OLD: \sprrezap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? ~EDOKRZAP.ROK_F
|| FUN.info('Nie wybrano roku.'@); _zwrot:='ROK_F'
?};
{? _zwrot='' & SKID.ZAPOTID=''
|| FUN.info('Nie wybrano zapotrzebowania.'@); _zwrot:='ZAPOTID'
?};
_zwrot


\edokrzap_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.20]
:: OPIS: Kopia podzialow z zapotrzebowan do dokumentu w obiegu
::  OLD: \edokrzap_add/zam_pub.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKRZAP.EZAPOT
|| _kwota:=0;
   EDOKUM.cntx_psh();
   {? EDOKRZAP.EDOKUM
   || EDOKUM.use(ref_name(EDOKRZAP.EZAPOT)); EDOKUM.prefix();
      _kwota:=EDOKRZAP.EZAPOT().NETTO
   ?};
   EDOKUM.cntx_pop();
   {? _kwota$2 & EDOKRZAP.NETTO$2
   || EPODZ.cntx_psh();
      _name:='skid_j'+(EDOKRZAP.name()+2);
      EPODZ.use('skid_j'+($(EDOKRZAP.ROK_F().POCZ_ROK~1)+2));
      EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKRZAP.EZAPOT);
      {? EPODZ.first()
      || {! |?
            {? EPODZ.SKID_MB().KONTROLA='T'
            || EPODZ.cntx_psh(); EPODZ.use(_name); EPODZ.prefix();
               EPODZ.EDOKUM:=EDOKRZAP.EDOKUM;
               EPODZ.EZAPOT:=EDOKRZAP.EZAPOT;
               EPODZ.EDOKRZAP:=EDOKRZAP.ref();
               _prec:=2;
               SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ'); SKIDXDUD.prefix(EPODZ.PBUD);
               {? SKIDXDUD.first()
               || _il:=SKIDXDUD.WART_IL='I'; _prec:=SKIDXDUD.PREC
               || _il:=0
               ?};
               {? _il
               || EPODZ.WART:=((EDOKRZAP.NETTO/_kwota)*EPODZ.WART)$_prec
               || EPODZ.WART:=((EDOKRZAP.NETTO/_kwota)*EPODZ.WART)$2
               ?};
               SKIDXDUD.cntx_pop();
               EPODZ.add();
               EPODZ.cntx_pop()
            ?};
            EPODZ.next()
         !}
      ?};
      EPODZ.cntx_pop()
   ?}
?}


\bd_edokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed usun EDOKRZAP
::  OLD: \bd_edokrzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| {? FUN.ask('Usunąć realizację zapotrzebowania?'@)
   || EPODZ.cntx_psh();
      EPODZ.index('EDOKRZAP'); EPODZ.prefix(EDOKRZAP.EDOKUM,EDOKRZAP.ref());
      {? EPODZ.first() || {! |? EPODZ.del !} ?};
      EPODZ.cntx_pop();
      EDOKRZAP.del()
   ?}
|| web_params_set('edr_ref',#EDOKRZAP.ref(),'edr_nam',EDOKRZAP.name());
   _form:="{? _a=1
           || _get:=web_params_get();
              _ref:=_get.edr_ref;
              _nam:=_get.edr_nam;
              {? _ref & _nam<>''
              || EDOKRZAP.use(_nam); EDOKRZAP.prefix();
                 {? EDOKRZAP.seek(_ref,_nam)
                 || EDOKUM.cntx_psh(); EDOKUM.use('skid_v'+(EDOKRZAP.name()+2)); EDOKUM.prefix();
                    EDOKRZAP.EDOKUM();
                    EPODZ.cntx_psh(); EPODZ.use('skid_j'+(EDOKRZAP.name()+2));
                    EPODZ.index('DSP_NULL'); EPODZ.prefix(EDOKUM.ref(),EDOKRZAP.ref());
                    do();
                    {? EPODZ.first()
                    || {! |?
                          _delr:=EPODZ.del(,1);
                          {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
                       !}
                    ?};
                    {? ~EDOKRZAP.del(,1) || undo() ?};
                    end();
                    EPODZ.cntx_pop(); EDOKUM.cntx_pop();
                    EDOKRZAP.web_refresh('WERGR_WT','EDOKRZAP');
                    {? EDOKRZAP.get()
                    || EDOKRZAP.web_refresh('WERGR_WT','EPODZ')
                    ?}
                 ?}
              ?}
           ?}";
   web_ask(_form,'Usunąć realizację zapotrzebowania?',FUN.TYT)
?}


\dspzapr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Wyswietlanie na spacje danych zapotrzebowania (z realizacji)
::   WE: _a - 1 - EDOKRZAP
::            2 - TABZAPOT
::  OLD: \dspzapr/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_znal:=0;
EDOKUM.cntx_psh();
{? _a=1
|| {? EDOKRZAP.EZAPOT
   || EDOKUM.use(ref_name(EDOKRZAP.EZAPOT)); EDOKUM.index('ID'); EDOKUM.prefix();
      _znal:=EDOKUM.seek(EDOKRZAP.EZAPOT)
   ?}
|? _a=2 & TABZAPOT.ZAPOTREF & TABZAPOT.ZAPOTNAM<>''
|| EDOKUM.use(TABZAPOT.ZAPOTNAM); EDOKUM.index('ID'); EDOKUM.prefix();
   _znal:=EDOKUM.seek(TABZAPOT.ZAPOTREF,TABZAPOT.ZAPOTNAM,1)
?};
{? _znal
|| EDOKUM.memo_get(,'UW_OPDL');
   {? app_info('web_sesid')=''
   || EDOKUM.win_edit('KSW'); EDOKUM.display()
   || EDOKUM.web_display('WW01',,"EDOKUM.web_eclose()")
   ?}
?};
EDOKUM.cntx_pop()


\br_edokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Rekord przed EDOKRZAP
::  OLD: \br_edokrzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' || exec('env_wt','b_proces') ?};
EDOKUM.cntx_psh();
EDOKUM.use(ref_name(EDOKRZAP.EZAPOT));
EDOKUM.prefix();
SKID.SUMVAT:=EDOKRZAP.EZAPOT().NETTO;
SKID.SUMWN:=exec('poz2real_zapot','obiegi_zap');
EDOKUM.cntx_pop();
''


\poz2real_zapot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Zwraca pozostala wartosc do zrealizowania dla zapotrzebowania
::  OLD: \poz2real_zapot/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
_ref:=EDOKRZAP.EDOKUM;
EDOKUM.cntx_psh();
EDOKUM.use(ref_name(EDOKRZAP.EZAPOT));
EDOKUM.index('ID'); EDOKUM.prefix();
{? EDOKUM.seek(EDOKRZAP.EZAPOT)
|| _zwrot:=EDOKUM.NETTO;
   _ed:=EDOKUM.ref();
   ROK_F.cntx_psh();
   ROK_F.index('KODG'); ROK_F.prefix(EDOKUM.name()+2);
   {? ROK_F.first()
   || ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
      {? ROK_F.first()
      || EDOKRZAP.cntx_psh();
         {!
         |? EDOKRZAP.use('skid_q'+ROK_F.KOD);
            EDOKRZAP.index('EZAPOT'); EDOKRZAP.prefix(_ed);
            {? EDOKRZAP.first()
            || {! |?
                  {? ref_name(_ed)=ref_name(EDOKRZAP.EZAPOT) & _ref<>EDOKRZAP.EDOKUM
                  || _zwrot-=EDOKRZAP.NETTO
                  ?};
                  EDOKRZAP.next()
               !}
            ?};
            ROK_F.next()
         !};
         EDOKRZAP.cntx_pop()
      ?}
   ?};
   ROK_F.cntx_pop()
?};
EDOKUM.cntx_pop();
_zwrot


\tabzapot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Tabela tymczasowa z zapotrzebowaniami
::   WE: _a - ref kontrahenta (SLO) - null - wszyscy
::       _b - zadeklarowane - na podstawie EDOKUM
::  OLD: \tabzapot/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_odd:=OPERATOR.DEPT;
{? var_pres('TABZAPOT')<=0
|| TABZAPOT:=tab_tmp(6,'DATA','DATE','Wprowadzono',
                       'KHKOD','STRING[8]','Kontrahent',
                       'KHTR','STRING[80]','',
                       'TYP','STRING[50]','Typ',
                       'ID','STRING[30]','Identyfikator',
                       'TRESC','STRING[200]','Temat',
                       'NETTO','REAL','Netto',
                       'POZOST','REAL','Pozostało',
                       'FAKTURA','INTEGER','Faktury w obiegu',
                       'ZAMK','INTEGER','Zamknięty',
                       'CON','INTEGER','Podziały controllingowe',
                       'US_REJ','STRING[50]','Rejestrujący',
                       'OSOBA','STRING[30]','Osoba',
                       'UIDREFZ','STRING[50]','',
                       'PROSTY','STRING[1]','Bez akceptacji',
                       'ZAPOTNAM','STRING[8]','',
                       'ZAPOTREF','INTEGER','',
                       'ODD','STRING[8]','Jednostka księgowa'
                    );
   {? app_info('web_sesid')=''
   || _wer:=TABZAPOT.mk_sel(,'P',,'#tabzapot_wer',,,,,'U');
      TABZAPOT.win_fld(_wer,,'DATA');
      TABZAPOT.win_fld(_wer,,'US_REJ',,,10);
      TABZAPOT.win_fld(_wer,,'OSOBA',,,10);
      TABZAPOT.win_fld(_wer,,'KHKOD');
      TABZAPOT.win_fld(_wer,,'KHTR',,,20);
      TABZAPOT.win_fld(_wer,,'TYP',,,15);
      TABZAPOT.win_fld(_wer,,'ID');
      TABZAPOT.win_fld(_wer,,'TRESC',,,30);
      TABZAPOT.win_fld(_wer,,'NETTO',,,14,2);
      TABZAPOT.win_fld(_wer,,'POZOST',,,14,2);
      TABZAPOT.win_fld(_wer,,'PROSTY',,,14,,,,,,2,,"'T'","'N'");
      TABZAPOT.win_fld(_wer,,'FAKTURA',,,,,,,,,2,,"1","0");
      TABZAPOT.win_fld(_wer,,'ZAMK',,,,,,,,,2,,"1","0");

      TABZAPOT.win_act(_wer,,'Formuła','Wybierz'@@,,,,"sel_exit()",1);
      TABZAPOT.win_act(_wer,,'Wyświetl',,,,"exec('dspzapr','obiegi_zap',2)");
      {? EDOKUM.KH
      || TABZAPOT.win_act(_wer,,'Formuła','&Zapotrzebowania kontrahenta'@@,,,,
                          "zakzapot:=0; exec('rzapkh','obiegi_zap')",,,,,'Z');
         TABZAPOT.win_act(_wer,,'Formuła','W&szystkie zapotrzebowania'@@,,,,
                          "zakzapot:=1; exec('rzapkh','obiegi_zap')",,,,,'S');
         TABZAPOT.win_act(_wer,1,'Formuła','&Zapotrzebowania kontrahenta'@@,,,,
                          "zakzapot:=0; exec('rzapkh','obiegi_zap')",,,,,'Z');
         TABZAPOT.win_act(_wer,1,'Formuła','W&szystkie zapotrzebowania'@@,,,,
                          "zakzapot:=1; exec('rzapkh','obiegi_zap')",,,,,'S')
      ?};
      TABZAPOT.win_act(_wer,,'Formuła','Podziały dla &controllingu'@,,'Wyświetl podziały'@,
                 "exec('podz_con','obiegi_zap',TABZAPOT.UIDREFZ)");
      TABZAPOT.win_act(_wer,,'Kolejność');
      TABZAPOT.win_act(_wer,,'Rekord',,,,"exec('rkp_tabzapot','obiegi_zap')");
      TABZAPOT.win_sel(_wer);
      _red:=TABZAPOT.mk_edit('Zapotrzebowania'@,0,'tabzapot_red');
      TABZAPOT.win_efld(_red,,'DATA');
      TABZAPOT.win_efld(_red,,'KHKOD');
      TABZAPOT.win_efld(_red,,'TYP');
      TABZAPOT.win_efld(_red,,'ID');
      TABZAPOT.win_efld(_red,,'TRESC');
      TABZAPOT.win_patt(_red);
      zakzapot:=0;
      zaprind1:=TABZAPOT.ndx_tmp(,,'DATA',,1);
      zaprind2:=TABZAPOT.ndx_tmp('',1,'KHKOD',,0,'KHKOD',,0,'DATA',,1,'TYP',,0,'ID',,0);
      zaprind3:=TABZAPOT.ndx_tmp('',1,'ZAPOTNAM',,0,'ZAPOTREF',,0);
      zaprind4:=TABZAPOT.ndx_tmp('',1,'KHKOD',,0,'KHKOD',,0,'ID',,0,'ID',,0);
      zaprind5:=TABZAPOT.ndx_tmp('',1,'ID',,0,'ID',,0)
   ?}
?};
TABZAPOT.prefix(); TABZAPOT.erase();
{? Plugin.runnable('EDOKRZAP_ZAPOT_ZAKR_001')
|| Plugin.run('EDOKRZAP_ZAPOT_ZAKR_001',_a,_=2)
|| {? _=2 || EDOKRZAP.web_cntx_load(1) ?};
   EDOKUM.cntx_psh();
   EDOKUM.use('skid_v'+(EDOKRZAP.name()+2)); EDOKUM.prefix();
   EDOKUM.cntx_pop();
   _par:=#exec('get_par','#parametr',463);
   _mas:=($(date()~1)+2);
   _dataDo:=date()-_par;
   _pfirm:=exec('rok_firm','obiegi_zap');
   {? _dataDo<_pfirm || _dataDo:=_pfirm ?};
   _rokDo:=#($(_dataDo~1)+2);
   _rokB:=#_mas;
   EDOKUM.cntx_psh(); ETYPY.cntx_psh();
   {! _rok:= _rokDo .. _rokB
   |! EDOKUM.use('skid_v'+form(_rok,-2));
      EDOKUM.index('ZAP_AKT');
      {? _a
      || EDOKUM.prefix(REF.FIRMA,1,'T',_a)
      || EDOKUM.prefix(REF.FIRMA,1,'T')
      ?};
      {? EDOKUM.first()
      || {! |?
            {? (EDOKUM.ODD=null | exec('usr_fjks','b_perm',EDOKUM.ODD))
               & (_odd=null | _odd=EDOKUM.ODD | EDOKUM.ODD=null)
               & EDOKUM.DATAW>=_dataDo
            || TABZAPOT.blank(1);
               TABZAPOT.DATA:=EDOKUM.DATAW;
               {? EDOKUM.KH
               || TABZAPOT.KHKOD:=EDOKUM.KH().KOD;
                  TABZAPOT.KHTR:=EDOKUM.KH().TR
               ?};
               TABZAPOT.TYP:=EDOKUM.TYP().NAZWA;
               TABZAPOT.ID:=EDOKUM.ID;
               TABZAPOT.ODD:=EDOKUM.ODD().OD;
               TABZAPOT.TRESC:=EDOKUM.TR;
               TABZAPOT.ZAPOTNAM:=EDOKUM.name();
               TABZAPOT.ZAPOTREF:=#EDOKUM.ref();
               TABZAPOT.NETTO:=EDOKUM.NETTO;
               TABZAPOT.POZOST:=exec('rea_edokrzap','obiegi_zap');
               TABZAPOT.FAKTURA:=exec('is_pow_obg','obiegi_zap');
               TABZAPOT.ZAMK:={? EDOKUM.ZAM='T' || 1 || 0 ?};
               TABZAPOT.CON:={? EDOKUM.TYP().ED_PODZ='T' || 1 || 0 ?};
               TABZAPOT.OSOBA:=EDOKUM.DOSTAWCA().NAZWISKO;
               TABZAPOT.US_REJ:=EDOKUM.USERS().DANE;
               TABZAPOT.UIDREFZ:=EDOKUM.uidref();
               TABZAPOT.PROSTY:={? EDOKUM.TYP().ZAPOT=2 || 'T' || 'N' ?};
               TABZAPOT.add()
            ?};
            EDOKUM.next()
         !}
      ?}
   !};
   EDOKUM.cntx_pop(); ETYPY.cntx_pop()
?};
{? app_info('web_sesid')<>''
|| exec('tab_form','obiegi','TAB_WT','wyb_tab_wt','obiegi_zap','','');
   _tcid:=app_info('web_tcid');
   _mbid:=app_info('web_mbid');
   _sesid:=app_info('web_sesid');
   _tabid:=app_info('web_tabid');
   TAB_WT.index('INDEX04'); TAB_WT.prefix(_tcid,_tabid,7);
   do();
   {? TAB_WT.first()
   || {! |?
         _delr:=TAB_WT.del(,1);
         {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
      !}
   ?};
   {? TABZAPOT.first()
   || {! |?
         TAB_WT.blank(1);
         TAB_WT.SESID:=_sesid;
         TAB_WT.MBID:=_mbid;
         TAB_WT.TCID:=_tcid;
         TAB_WT.TABID:=_tabid;
         TAB_WT.TYP:=7;
         TAB_WT.DATE01:=TABZAPOT.DATA;
         TAB_WT.STR01:=TABZAPOT.KHKOD;
         TAB_WT.STR02:=TABZAPOT.KHTR;
         TAB_WT.STR03:=TABZAPOT.TYP;
         TAB_WT.STR04:=TABZAPOT.ID;
         TAB_WT.STR05:=TABZAPOT.TRESC;
         TAB_WT.REAL01:=TABZAPOT.NETTO;
         TAB_WT.REAL02:=TABZAPOT.POZOST;
         TAB_WT.STR06:=TABZAPOT.ZAPOTNAM;
         TAB_WT.INT01:=TABZAPOT.ZAPOTREF;
         TAB_WT.add();
         TABZAPOT.next()
      !}
   ?};
   end();
   TABZAPOT.ndx_drop();
   VAR_DEL.delete('TABZAPOT')
?}


\wyb_tab_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na wybór rekordu w TAB_WT - zapotrzebowania
::----------------------------------------------------------------------------------------------------------------------
SKID.ZAPOTID:=TAB_WT.STR04;
EDOKRZAP.web_cntx_load(1);
EDOKUM.cntx_psh();
EDOKUM.use(TAB_WT.STR06); EDOKUM.index('ID'); EDOKUM.prefix();
{? EDOKUM.seek(TAB_WT.INT01,)
|| EDOKRZAP.EZAPOT:=EDOKUM.ref();
   EDOKRZAP.ID:=EDOKUM.ID; EDOKRZAP.TR:=EDOKUM.TR;
   SKID.ZAPOTID:=EDOKUM.ID
|| SKID.ZAPOTID:='';
   EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:=''
?};
EDOKUM.cntx_pop();
TAB_WT.web_close();
web_top_update(1);
web_top_fld_update(1,1);
1


\rea_edokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Sprawdzenie wartosci realizacji biezacego zapotrzebowania
::   WE: [_a] - jeśli podane i _a=1 to uwzględniaj zawsze wszystkie zapisy
::  OLD: \rea_edokrzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=EDOKUM.NETTO;
{? -menu_txt()='popraw' || _ref:=$EDOKRZAP.ref() ?};
ROK_F.cntx_psh();
ROK_F.index('KODG'); ROK_F.prefix();
{? ROK_F.seek(EDOKUM.ROK_F)
|| _rok:=ROK_F.ref(); _ed:=EDOKUM.ref();
   ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   {? ROK_F.first()
   || EDOKRZAP.cntx_psh();
      {! |?
         EDOKRZAP.use('skid_q'+(($(ROK_F.POCZ_ROK~1))+2));
         EDOKRZAP.index('EZAPOT'); EDOKRZAP.prefix(_ed,_rok);
         {? EDOKRZAP.first()
         || {! |?
               {? (-menu_txt()<>'popraw' | (-menu_txt()='popraw' & _ref<>$EDOKRZAP.ref())) | (var_pres('_a')>0 & _a=1)
               || _zwrot-=EDOKRZAP.NETTO
               ?};
               EDOKRZAP.next()
            !}
         ?};
         ROK_F.next()
      !};
      EDOKRZAP.cntx_pop()
   ?}
?};
ROK_F.cntx_pop();
_zwrot


\aerokzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji pola EDOKRZAP.ROK_F
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| {? ~EDOKRZAP.ROK_F
   || FUN.info('Nie wybrano roku.'@); 0
   || 1
   ?}
|| 1
?}


\besymzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja pola SKID.ZAPOTID
::  OLD: \besymzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKRZAP.ROK_F<>null


\dssymzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Na wyswietl dla pola SKID.ZAPOTID
::  OLD: \dssymzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKRZAP.EZAPOT<>null
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(EDOKRZAP.EZAPOT)); EDOKUM.index('ID'); EDOKUM.prefix();
   SKID.ZAPOTID:={? EDOKUM.seek(EDOKRZAP.EZAPOT) || EDOKUM.ID || '' ?};
   EDOKUM.cntx_pop()
|| SKID.ZAPOTID:=''
?};
1


\f3symzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Na klawisz F3 dla pola SKID.ZAPOTID
::  OLD: \f3symzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| exec('tabzapot','obiegi_zap',null);
   {? SKID.ZAPOTID<>''
   || {? zapotnam=''
      || exec('f3symzap1','obiegi_zap')
      || TABZAPOT.index(zaprind3); TABZAPOT.prefix(zapotnam,zapotref);
         {? TABZAPOT.first() & TABZAPOT.ID=SKID.ZAPOTID
         || zakzapot:=(EDOKUM.KH().KOD<>TABZAPOT.KHKOD);
            exec('rzapkh','obiegi_zap')
         || exec('f3symzap1','obiegi_zap')
         ?}
      ?}
   || EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:='';
      zapotnam:=''; zapotref:=0;
      zakzapot:={? EDOKUM.KH || 0 || 1 ?};
      exec('rzapkh','obiegi_zap'); TABZAPOT.first()
   ?};
   {? TABZAPOT.select(,1)
   || EDOKUM.cntx_psh();
      EDOKUM.index('ID'); EDOKUM.prefix();
      {? EDOKUM.seek(TABZAPOT.UIDREFZ,ref_name(TABZAPOT.UIDREFZ))
      || EDOKRZAP.EZAPOT:=EDOKUM.ref();
         EDOKRZAP.ROK_F:=EDOKUM.ROK_F;
         EDOKRZAP.ID:=EDOKUM.ID; EDOKRZAP.TR:=EDOKUM.TR;
         SKID.ZAPOTID:=EDOKUM.ID;
         zapotnam:=EDOKUM.name(); zapotref:=#EDOKUM.ref()
      || SKID.ZAPOTID:='';
         EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:='';
         zapotnam:=''; zapotref:=0
      ?};
      EDOKUM.cntx_pop()
   ?};
   SKID.ZAPOTID
|| exec('env_wt','b_proces');
   exec('tabzapot','obiegi_zap',null);
   EDOKRZAP.web_cntx_save(1);
   {? SKID.ZAPOTID<>''
   || TAB_WT.index('INDEX07'); TAB_WT.prefix(app_info('web_tcid'),app_info('web_tabid'),7,SKID.ZAPOTID);
      _first:=TAB_WT.first()
   || _first:=0
   ?};
   TAB_WT.index('INDEX08'); TAB_WT.prefix(app_info('web_tcid'),app_info('web_tabid'),7);
   {? ~_first || TAB_WT.first() ?};
   _tytul:='Wszystkie zapotrzebowania'@;
   TAB_WT.web_win_init('WER07',,'title="'+_tytul+'"');
   TAB_WT.web_select('WER07',,1)
?}


\f3symzap1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Na klawisz F3 dla pola SKID.ZAPOTID - wewnetrzna
::  OLD: \f3symzap1/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:=''; zapotnam:=''; zapotref:=0;
{? EDOKUM.KH
|| TABZAPOT.index(zaprind4); TABZAPOT.prefix(EDOKUM.KH().KOD,SLO.KOD,SKID.ZAPOTID);
   {? TABZAPOT.first()
   || zakzapot:=0; exec('rzapkh','obiegi_zap')
   || TABZAPOT.index(zaprind5); TABZAPOT.prefix(SKID.ZAPOTID);
      {? TABZAPOT.first()
      || zakzapot:=1; exec('rzapkh','obiegi_zap')
      || zakzapot:=0; exec('rzapkh','obiegi_zap'); TABZAPOT.first()
      ?}
   ?}
|| TABZAPOT.index(zaprind5); TABZAPOT.prefix(SKID.ZAPOTID); TABZAPOT.first();
   zakzapot:=1; exec('rzapkh','obiegi_zap')
?}


\rzapkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Kontrahent biezacy\wszyscy
::   WE: _a - opcja dla WT - 0 - zapotrzebowania kontrahenta
::                           1 - wszystkie zapotrzebowania
::  OLD: \rzapkh/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| EDOKUM.web_cntx_load(1);
   EDOKRZAP.use('skid_q'+(EDOKUM.name()+2));
   {? _a=1 | ~EDOKUM.KH
   || exec('tabzapot','obiegi_zap',null,1);
      _tytul:='Wszystkie zapotrzebowania'@;
      TAB_WT.web_win_opt('WER07',,'title='+_tytul)
   || exec('tabzapot','obiegi_zap',EDOKUM.KH,1);
      _tytul:='Zapotrzebowania kontrahenta %1'@[EDOKUM.KH().TR];
      TAB_WT.web_win_opt('WER07',,'title='+_tytul)
   ?};
   TAB_WT.web_refresh('WER07')
|| {? zakzapot=0
   || TABZAPOT.index(zaprind2); TABZAPOT.prefix(EDOKUM.KH().KOD,SLO.KOD);
      TABZAPOT.hdr_sel(); TABZAPOT.hdr_sel('Zapotrzebowania kontrahenta %1'@[SLO.TR]);
      zakzapot:=1
   || TABZAPOT.index(zaprind1); TABZAPOT.prefix();
      TABZAPOT.hdr_sel(); TABZAPOT.hdr_sel('Wszystkie zapotrzebowania'@);
      zakzapot:=0
   ?}
?}


\aesymzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Po redakcji pola SKID.ZAPOTID
::  OLD: \aesymzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? SKID.ZAPOTID<>''
|| {? app_info('web_sesid')<>''
   || exec('env_wt','b_proces');
      exec('tabzapot','obiegi_zap',null);
      TAB_WT.index('INDEX07'); TAB_WT.prefix(app_info('web_tcid'),app_info('web_tabid'),7,SKID.ZAPOTID);
      {? TAB_WT.first()
      || EDOKUM.cntx_psh(); EDOKUM.use(TAB_WT.STR06); EDOKUM.index('ID'); EDOKUM.prefix();
         {? EDOKUM.seek(TAB_WT.INT01,)
         || EDOKRZAP.EZAPOT:=EDOKUM.ref();
            EDOKRZAP.ID:=EDOKUM.ID; EDOKRZAP.TR:=EDOKUM.TR;
            SKID.ZAPOTID:=TAB_WT.STR04;
            {? EDOKRZAP.NETTO$2=0 || EDOKRZAP.NETTO:=TAB_WT.REAL02$2 ?}
         || FUN.info('Należy wybrać poprawną wartość ze słownika.'@);
            EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:=''; SKID.ZAPOTID:='';
            _zwrot:=0
         ?};
         EDOKUM.cntx_pop()
      || FUN.info('Należy wybrać poprawną wartość ze słownika.'@); _zwrot:=0
      ?}
   || _szuk:=0; _utw_tab:=0;
      {? zapotnam=''
      || EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:=''; _szuk:=1;
         exec('tabzapot','obiegi_zap',null); _utw_tab:=1
      || TABZAPOT.cntx_psh();
         TABZAPOT.index(zaprind3); TABZAPOT.prefix(zapotnam,zapotref);
         {? TABZAPOT.first() & TABZAPOT.ID=SKID.ZAPOTID
         || EDOKUM.cntx_psh();
            EDOKUM.use(zapotnam); EDOKUM.index('ID'); EDOKUM.prefix();
            {? EDOKUM.seek(zapotref,)
            || EDOKRZAP.EZAPOT:=EDOKUM.ref();
               EDOKRZAP.ID:=EDOKUM.ID; EDOKRZAP.TR:=EDOKUM.TR;
               {? EDOKRZAP.NETTO$2=0 || EDOKRZAP.NETTO:=TABZAPOT.POZOST$2 ?}
            || FUN.info('Należy wybrać poprawną wartość ze słownika.'@);
               EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:=''; SKID.ZAPOTID:='';
               zapotnam:=''; zapotref:=0; _zwrot:=0
            ?};
            EDOKUM.cntx_pop()
         || zapotnam:=''; zapotref:=0;
            EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:=''; _szuk:=1
         ?};
         TABZAPOT.cntx_pop()
      ?};
      {? _szuk
      || {? ~_utw_tab || exec('tabzapot','obiegi_zap',null) ?};
         TABZAPOT.index(zaprind4);
         TABZAPOT.prefix(EDOKUM.KH().KOD,SLO.KOD,SKID.ZAPOTID);
         _zwrot:=TABZAPOT.first();
         {? ~_zwrot
         || TABZAPOT.index(zaprind5); TABZAPOT.prefix(SKID.ZAPOTID);
            _zwrot:=TABZAPOT.first()
         ?};
         {? _zwrot
         || EDOKUM.cntx_psh();
            EDOKUM.use('skid_v'+($(EDOKRZAP.ROK_F().POCZ_ROK~1)+2)); EDOKUM.index('ID'); EDOKUM.prefix();
            {? EDOKUM.seek(TABZAPOT.ZAPOTREF,)
            || EDOKRZAP.EZAPOT:=EDOKUM.ref();
               EDOKRZAP.ID:=EDOKUM.ID; EDOKRZAP.TR:=EDOKUM.TR;
               SKID.ZAPOTID:=EDOKUM.ID;
               {? EDOKRZAP.NETTO$2=0 || EDOKRZAP.NETTO:=TABZAPOT.POZOST$2 ?};
               zapotnam:=EDOKUM.name(); zapotref:=#EDOKUM.ref()
            || FUN.info('Należy wybrać poprawną wartość ze słownika.'@);
               EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:=''; SKID.ZAPOTID:='';
               zapotnam:=''; zapotref:=0; _zwrot:=0
            ?};
            EDOKUM.cntx_pop()
         || FUN.info('Należy wybrać poprawną wartość ze słownika.'@);
            EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:=''; SKID.ZAPOTID:='';
            zapotnam:=''; zapotref:=0; _zwrot:=0
         ?}
      ?}
   ?}
|| EDOKRZAP.EZAPOT:=null; EDOKRZAP.ID:=EDOKRZAP.TR:='';
   zapotnam:=''; zapotref:=0; _zwrot:=1
?};
{? app_info('web_sesid')='' || win_disp() ?};
_zwrot


\bm_epodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed popraw w EPODZ
::  OLD: \bm_epodz/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? EPODZ.EZAPOT
|| _zwrot:=FUN.ask('Podział wynikający z realizacji zapotrzebowań. Czy na pewno edytować?'@)
?};
{? _zwrot & EPODZ.EDOKUMPR
|| FUN.info('Podział związany z projektem dla dokumentu.'@); _zwrot:=0
?};
{? app_info('web_sesid')<>''
|| web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),
                         'mod_war',#EPODZ.SKID_MB
                        ))
?};
{? app_info('web_sesid')<>'' & _zwrot
|| no_ust:=1; exec('pola_epodz','obiegi',3); VAR_DEL.delete('no_ust')
?};
_zwrot


\ae_netto_edokrzap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po redakcji pola EDOKRZAP.NETTO
::----------------------------------------------------------------------------------------------------------------------
EDOKRZAP.NETTO:=EDOKRZAP.NETTO$2;
1


\is_pow_obg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Sprawdza, czy zapotrzebowanie ma podpiętą jakąś fakturę w obiegu
::   WE: [_a] - jeśli podane i _a=1 to uwzględniaj zawsze wszystkie zapisy
::   WY: 1 - jest, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
_rok:=EDOKUM.ROK_F; _ed:=EDOKUM.ref();
_wyn:=0;
{? -menu_txt()='popraw' || _ref:=$EDOKRZAP.ref() ?};
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.first()
|| EDOKRZAP.cntx_psh();
   {! |?
      EDOKRZAP.use('skid_q'+(($(ROK_F.POCZ_ROK~1))+2));
      EDOKRZAP.index('EZAPOT'); EDOKRZAP.prefix(_ed,_rok);
      {? EDOKRZAP.first()
      || {! |?
            {? (-menu_txt()<>'popraw' | (-menu_txt()='popraw' & _ref<>$EDOKRZAP.ref())) | (var_pres('_a')>0 & _a=1)
            || _wyn:=1
            ?};
            EDOKRZAP.next() & ~_wyn
         !}
      ?};
      ROK_F.next()
   !};
   EDOKRZAP.cntx_pop()
?};
ROK_F.cntx_pop();
_wyn


\win_obg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Tworzy zakładkę z zapotrzebowaniami bez akceptacji w fakturach w obiegu
::   WE: _a -Akronim tymczasowego okienka grupowego
::----------------------------------------------------------------------------------------------------------------------
{? _<1 || return(0) ?};
_okn_grp:=_a;

{? var_pres('OBGZAPOT')<=0
|| OBGZAPOT:=tab_tmp(6,'DATA','DATE','Rejestrowano',
                       'KHKOD','STRING[8]','Kontrahent',
                       'KHTR','STRING[80]','',
                       'TYP','STRING[50]','Typ',
                       'ID','STRING[30]','Identyfikator',
                       'TRESC','STRING[200]','Temat',
                       'NETTO','REAL','Netto',
                       'POZOST','REAL','Pozostało',
                       'FAKTURA','STRING[1]','Faktury w obiegu',
                       'ZAMK','STRING[1]','Zamknięty',
                       'CON','INTEGER','Podziały controllingowe',
                       'US_REJ','STRING[50]','Rejestrujący',
                       'OSOBA','STRING[30]','Osoba',
                       'ZAPOTNAM','STRING[8]','',
                       'ZAPOTREF','INTEGER','',
                       'UIDREFZ','STRING[50]','',
                       'PROSTY','STRING[1]','Bez akceptacji'
                    )
?};

VAR_DEL.delete('__nobgzapot');
__nobgzapot:=OBGZAPOT.ndx_tmp(,,'DATA',,1);
OBGZAPOT.index(__nobgzapot); OBGZAPOT.prefix();
_wer:=OBGZAPOT.mk_sel(,'T',,'tabzapot_wer',,,,,'U');
OBGZAPOT.win_fld(_wer,,'DATA',,,12);
OBGZAPOT.win_fld(_wer,,'US_REJ',,,15);
OBGZAPOT.win_fld(_wer,,'OSOBA',,,12);
OBGZAPOT.win_fld(_wer,,'KHKOD',,,10);
OBGZAPOT.win_fld(_wer,,'KHTR',,,12);
OBGZAPOT.win_fld(_wer,,'TYP',,,12);
OBGZAPOT.win_fld(_wer,,'ID',,,15);
OBGZAPOT.win_fld(_wer,,'TRESC',,,15);
OBGZAPOT.win_fld(_wer,,'NETTO',,,12,2);
OBGZAPOT.win_fld(_wer,,'POZOST',,,12,2);
OBGZAPOT.win_fld(_wer,,'PROSTY',,,-5,,,,,,2,,"'T'","'N'");
OBGZAPOT.win_fld(_wer,,'FAKTURA',,,-5,,,,,,2,,"'T'","'N'");
OBGZAPOT.win_fld(_wer,,'ZAMK',,,-5,,,,,,2,,"'T'","'N'");

OBGZAPOT.win_act(_wer,,'Formuła','&Zamknij obieg'@,,'Zamknij obieg wniosku o zapotrzebowanie'@,
                 "exec('akc_close','obiegi_zap')",,1);
OBGZAPOT.win_act(_wer,,'Formuła','Podziały dla &controllingu'@,,'Wyświetl podziały'@,
                 "exec('podz_con','obiegi_zap',OBGZAPOT.UIDREFZ,OBGZAPOT.ZAPOTNAM)");
OBGZAPOT.win_act(_wer,,'Formuła','&Odśwież'@,,,
                 "_d1:=OBGZAPOT.UIDREFZ; exec('zapot_fill','obiegi_zap');
                  exec('rfr_obgzapot','obiegi_zap',_d1)");
OBGZAPOT.win_act(_wer,,'Menu','Funkcje',,,,);
OBGZAPOT.win_act(_wer,,'Formuła','Pokaż &budżet','Funkcje',,
                 "_typ:=typobi; exec('main','!ctr_oba_pbkp'); typobi:=_typ",);
::EDOKUM.win_act(_sel,,'Formuła','Z&mień zakres',,,"exec('akc_zakres','obe_faw')",);
OBGZAPOT.win_act(_wer,0,'Formuła','Pokaż statystykę','Funkcje','Statystyka dla wniosków w obiegu',
                 "_typ:=typobi; exec('ob_stat_act','obiegi2',2); typobi:=_typ",);
task_attach('OBE_FAW_KOBI');
OBGZAPOT.win_act(_wer,1,'Menu','Funkcje',,,,);
OBGZAPOT.win_act(_wer,1,'Formuła','Pokaż &budżet','Funkcje',,
                 "_typ:=typobi; exec('main','!ctr_oba_pbkp'); typobi:=_typ",);
::EDOKUM.win_act(_sel,,'Formuła','Z&mień zakres',,,"exec('akc_zakres','obe_faw')",);
OBGZAPOT.win_act(_wer,1,'Formuła','Pokaż statystykę','Funkcje','Statystyka dla wniosków w obiegu',
                 "_typ:=typobi; exec('ob_stat_act','obiegi2',2); typobi:=_typ",);
task_attach('OBE_FAW_KOBI');
OBGZAPOT.win_act(_wer,,'Formuła','&Drukuj'@,,,"exec('drukuj','obiegi_zap')");
OBGZAPOT.win_act(_wer,1,'Formuła','&Odśwież'@,,,"exec('zapot_fill','obiegi_zap')");
OBGZAPOT.win_act(_wer,,'Formuła','&Parametry pracy'@,,,
                 "_d1:=OBGZAPOT.UIDREFZ; exec('obg_params','obiegi');
                  exec('rfr_obgzapot','obiegi_zap',_d1)");
OBGZAPOT.win_act(_wer,1,'Formuła','&Parametry pracy'@,,,"exec('obg_params','obiegi')");
OBGZAPOT.win_act(_wer,,'Wyświetl',,,,"exec('akc_display','obiegi_zap')",);
OBGZAPOT.win_act(_wer,,'Kolejność');
OBGZAPOT.win_act(_wer,,'Rekord',,,,"exec('rkprz','obiegi_zap')");

exec('zapot_fill','obiegi_zap');

_ref_edok:=$('exec(\'get_zal\',\'obiegi_zap\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
_fb:="EDOKUMZ.cntx_psh(); tab_hide(1,,'tab2'); tab_hide(2,,'tab2'); tab_show(3,'tab2'); tab_hide(4,,'tab2');
      {? exec('upgrade2325_blbc1','zbl') || tab_hide(5,,'tab2') ?};
      {? EDOKUM.f_active() || EDOKUM.f_clear() ?};
       VAR_DEL.delete('zapobobs');
      zapobobs:=1";
_fa:="EDOKUMZ.cntx_pop()";
EDOKUM.grp_sel(_okn_grp,OBGZAPOT,_wer,'Zapotrzebowania'@,_ref_edok,,,,
               _fb,_fa,,,'maximized','fobg_zapt');
task_attach('OBG_FZO_ZAPT')


\zapot_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Uzupełnia tabelę z zapotrzebowaniami bez akceptacji
::----------------------------------------------------------------------------------------------------------------------
OBGZAPOT.prefix(); OBGZAPOT.erase();
_odd:=OPERATOR.DEPT;
_par:=#exec('get_par','#parametr',463);
_mas:=($(date()~1)+2);
_dataDo:=date()-_par;
_pfirm:=exec('rok_firm','obiegi_zap');
{? _dataDo<_pfirm || _dataDo:=_pfirm ?};
_rokDo:=#($(_dataDo~1)+2);
_rokB:=#_mas;
EDOKUM.cntx_psh(); ETYPY.cntx_psh();
ETYPY.prefix();
{! _rok:= _rokDo .. _rokB
|! EDOKUM.use('skid_v'+form(_rok,-2));
   EDOKUM.index('ZAP_AKT');
   EDOKUM.prefix(REF.FIRMA,1,'T');
   {? EDOKUM.first()
   || {! |?
         {? EDOKUM.DATAW>=_dataDo
            & (EDOKUM.ODD=null | exec('usr_fjks','b_perm',EDOKUM.ODD))
            & (_odd=null | _odd=EDOKUM.ODD)
         || OBGZAPOT.blank(1);
            OBGZAPOT.DATA:=EDOKUM.DATAW;
            {? EDOKUM.KH
            || OBGZAPOT.KHKOD:=EDOKUM.KH().KOD;
               OBGZAPOT.KHTR:=EDOKUM.KH().TR
            ?};
            OBGZAPOT.TYP:=EDOKUM.TYP().NAZWA;
            OBGZAPOT.ID:=EDOKUM.ID;
            OBGZAPOT.TRESC:=EDOKUM.TR;
            OBGZAPOT.ZAPOTNAM:=EDOKUM.name();
            OBGZAPOT.ZAPOTREF:=#EDOKUM.ref();
            OBGZAPOT.NETTO:=EDOKUM.NETTO;
            OBGZAPOT.POZOST:=exec('rea_edokrzap','obiegi_zap',1);
            OBGZAPOT.FAKTURA:={? exec('is_pow_obg','obiegi_zap',1) || 'T' || 'N' ?};
            OBGZAPOT.ZAMK:={? EDOKUM.ZAM='T' || 'T' || 'N' ?};
            OBGZAPOT.CON:={? EDOKUM.TYP().ED_PODZ='T' || 1 || 0 ?};
            OBGZAPOT.OSOBA:=EDOKUM.DOSTAWCA().NAZWISKO;
            OBGZAPOT.US_REJ:=EDOKUM.USERS().DANE;
            OBGZAPOT.UIDREFZ:=EDOKUM.uidref();
            OBGZAPOT.PROSTY:={? EDOKUM.TYP().ZAPOT=2 || 'T' || 'N' ?};
            OBGZAPOT.add()
         ?};
         EDOKUM.next()
      !}
   ?}
!};
{? OBGZAPOT.first() || 1 ?};
EDOKUM.cntx_pop(); ETYPY.cntx_pop()


\akc_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Akcja zamknięcia wniosku o zapotrzebowanie bez akceptacji
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOKUM.prefix();
OBIEGI.cntx_psh();
_typ:=typobi;
{? EDOKUM.seek(OBGZAPOT.UIDREFZ,ref_name(OBGZAPOT.UIDREFZ))
|| {? var_pres('zakres')>0 || _zak:=zakres || _zak:=-1 ?};
   zakres:=1;
   {? OPERATOR.USER
   || EDOKOS.index('SZUK'); EDOKOS.prefix(EDOKUM.ref(),OPERATOR.USER,'W');
      {? EDOKOS.first()
      || OBIEGI.EDOKOS:=EDOKOS.ref();
         OBIEGI.B_PREL:=EDOKOS.B_PREL;
         OBIEGI.B_PRELS:=EDOKOS.B_PRELS
      ?}
   ?};
   {? OBIEGI.EDOKOS=null
   || EDOKOS.index('SZUK2'); EDOKOS.prefix(EDOKUM.ref(),'W');
      {? EDOKOS.first()
      || {! |?
            {? EDOKOS.STATUS<>'X'
            || OBIEGI.EDOKOS:=EDOKOS.ref();
               OBIEGI.B_PREL:=EDOKOS.B_PREL;
               OBIEGI.B_PRELS:=EDOKOS.B_PRELS
            ?};
            OBIEGI.EDOKOS=null & EDOKOS.next()
         !}
      ?}
   ?};
   exec('akc_close','obe_faw');
   {? EDOKUM.seek(OBGZAPOT.UIDREFZ,ref_name(OBGZAPOT.UIDREFZ)) & EDOKUM.ZAM='T'
   || OBGZAPOT.ZAMK:='T';
      OBGZAPOT.put()
   ?}
|| FUN.emsg('Nie można odnaleźć wniosku o zapotrzebowanie.'@)
?};
typobi:=_typ;
EDOKUM.cntx_pop(); OBIEGI.cntx_pop()


\rfr_obgzapot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Funkcja znajduje rekord na jakim stał użytkownik przed oświeżeniem
::   WE: _a - OBGZAPOT.UIDREFZ
::----------------------------------------------------------------------------------------------------------------------
_ndxb:=OBGZAPOT.index('?');
_ndx:=OBGZAPOT.ndx_tmp(,1,'UIDREFZ',,);
OBGZAPOT.index(_ndx);
{? OBGZAPOT.find_key(_a) || 1 ?};
OBGZAPOT.index(_ndxb);
OBGZAPOT.ndx_drop(_ndx);
1


\akc_display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wyświetl dla zakładki zapotrzebowania bez akc.
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); OBIEGI.cntx_psh();
EDOKUM.prefix();
_typ:=typobi;
{? EDOKUM.seek(OBGZAPOT.UIDREFZ,ref_name(OBGZAPOT.UIDREFZ))
|| exec('akc_display','obe_faw')
|| FUN.emsg('Nie można odnaleźć wniosku o zapotrzebowanie.'@)
?};
typobi:=_typ;
EDOKUM.cntx_pop(); OBIEGI.cntx_pop()


\rkprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Rekord przed okienka dla zakładki zapotrzebowań b. a.
::----------------------------------------------------------------------------------------------------------------------
{? OBGZAPOT.ZAMK='T'
|| _gray:='Z'
|| _gray:=''
?};
{? OBGZAPOT.CON=1
|| _gray+=''
|| _gray+='C'
?};
OBGZAPOT.actions_grayed(cur_win(1,1),_gray);
~~


\podz_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Podziały controllingowe dla zapotrzebowań
::   WE: _a - EDOKUM.uidref()
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); OBIEGI.cntx_psh();
EDOKUM.prefix();
_typ:=typobi;
{? EDOKUM.seek(_a,ref_name(_a))
|| exec('podz_dob','obiegi','WW',,1)
|| FUN.emsg('Nie można odnaleźć wniosku o zapotrzebowanie.'@)
?};
typobi:=_typ;
EDOKUM.cntx_pop(); OBIEGI.cntx_pop()


\rkp_tabzapot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Rekord przed tabeli tymczasowej TABZAPOT
::----------------------------------------------------------------------------------------------------------------------
{? TABZAPOT.CON=1
|| _gray:=''
|| _gray:='C'
?};
TABZAPOT.actions_grayed(cur_win(1,1),_gray);
~~


\get_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Odświeża okienko z załącznikami
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOKUM.prefix();
{? ~OBGZAPOT.sel_size()
|| {? (OBGZAPOT.f_active()=0 & OBGZAPOT.size()=0) | (OBGZAPOT.f_active() & OBGZAPOT.f_size()=0)
   || EDOKUMZ.index('NULL'); EDOKUMZ.prefix(null,null)
   |? EDOKUM.seek(OBGZAPOT.UIDREFZ,ref_name(OBGZAPOT.UIDREFZ))
   || EDOKUMZ.use('skid_n'+(OBGZAPOT.ZAPOTNAM+2));
      EDOKUMZ.index('DISP'); EDOKUMZ.prefix(EDOKUM.ref())
   || EDOKUMZ.index('NULL'); EDOKUMZ.prefix(null,null)
   ?};
   {? ~EDOKUMZ.get() || EDOKUMZ.first() ?};
   __CTR.BTAB:='EDOKUMZ';
   __CTR.TAB:='EDOKUM';
   __CTR.BBLOB:='EDOKUM';
   __CTR.WIN_ID:=_a;
   __CTR.WIN_ACR:=_b;
   exec('getblobs','bloby',__CTR.WIN_ID);
   grp_disp(($(__CTR.BTAB))(),__CTR.WIN_ACR,1,)
?};
EDOKUM.cntx_pop();
1


\drukuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Drukuje wniosek o zapotrzebowanie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOKUM.prefix();
EDOKUMW.cntx_psh();
{? EDOKUM.seek(OBGZAPOT.UIDREFZ,ref_name(OBGZAPOT.UIDREFZ))
|| {? app_info('web_sesid')<>''
   || EDOKUMW.use('skidv1'+(OBGZAPOT.ZAPOTNAM+2)); EDOKUMW.index('UNIK1'); EDOKUMW.prefix(EDOKUM.ref());
      {? EDOKUMW.first()
      || exec('wnio_print','obe','A')
      ?}
   || exec('wnio_print','obe','A')
   ?}
|| FUN.emsg('Nie można odnaleźć wniosku o zapotrzebowanie.'@)
?};
EDOKUM.cntx_pop();
EDOKUMW.cntx_pop();
1

\bu_edokrzap_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Przed obsluga okna WER3 tabeli EPODZ okna grupowego WER_GR tabeli EDOKRZAP
::  OLD: \bu_edokrzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
EPODZ.cntx_psh();
{? app_info('web_sesid')=''
|| {? EDOKRZAP.get()
   || EDOKUM.cntx_psh(); {? EDOKRZAP.EZAPOT<>null || EDOKUM.use(ref_name(EDOKRZAP.EZAPOT)) ?}; EDOKUM.prefix();
      EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKRZAP.EZAPOT);
      EDOKUM.cntx_pop()
   || EPODZ.index('DSP_NULL'); EPODZ.prefix(null)
   ?};
   exec('sum_podz1','obiegi_zap')
|| {? var_pres('zakres')>0 || web_params_set('zakres',zakres) ?};
   EDOKRZAP.prefix();
   _edr:=web_win_ref('EDOKRZAP');
   {? type_of(_edr)<>type_of(~~) & EDOKRZAP.seek(_edr,ref_name(_edr),1)
   || EPODZ.use('skid_j'+(EDOKRZAP.name()+2));
      EDOKUMPR.use('skid_i'+(EDOKRZAP.name()+2)); EDOKUMPR.index('DISP'); EDOKUMPR.prefix();
      EDOKUM.use('skid_v'+(EDOKRZAP.name()+2));
      EDOKRZAP.EZAPOT();
      EPODZ.index('EDOKUMDS'); EPODZ.prefix(EDOKUM.ref());
      exec('sum_podz','obiegi')
   || EPODZ.index('EDOKUMDS'); EPODZ.prefix(null);
      SKID.SKXDRAZE:=SKID.SKXDPOZO:=0
   ?}
?};
1


\au_edokrzap_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Po obsludze okna WER3 tabeli EPODZ okna grupowego WER_GR tabeli EDOKRZAP
::  OLD: \au_edokrzap/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
EPODZ.cntx_pop();
1


\sum_podz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Podsumowanie podziałów zapotrzebowania
::----------------------------------------------------------------------------------------------------------------------
SKID.SKXDRAZE:=SKID.SKXDPOZO:=0;
EPODZ.cntx_psh();
{? EPODZ.first()
|| _model:=EPODZ.SKID_MB;
   {!
   |? _dsp_sum:=(_model=EPODZ.SKID_MB);
      _dsp_sum & EPODZ.next()
   !};
   {? _dsp_sum & EPODZ.first()
   || {!
      |? SKIDXDUD.cntx_psh();
         SKIDXDUD.index('POZ'); SKIDXDUD.prefix(EPODZ.PBUD);
         _wart:=(SKIDXDUD.first() & SKIDXDUD.WART_IL='W');
         SKIDXDUD.cntx_pop();
         {? _wart || SKID.SKXDRAZE+=EPODZ.WART ?};
         EPODZ.next()
      !};
      EDOKUM.cntx_psh();
      {? EDOKRZAP.EZAPOT<>null || EDOKUM.use(ref_name(EDOKRZAP.EZAPOT)) ?}; EDOKUM.prefix();
      SKID.SKXDPOZO:={? __edokr=1 || EDOKRZAP.EZAPOT().NETTO-SKID.SKXDRAZE ||  EDOKUM.NETTO-SKID.SKXDRAZE ?};
      EDOKUM.cntx_pop()
   ?}
?};
EDOKUM.cntx_psh();
{? EDOKRZAP.EZAPOT<>null || EDOKUM.use(ref_name(EDOKRZAP.EZAPOT)) ?}; EDOKUM.prefix();
SKID.SKXDPOZ2:={? __edokr=1 || EDOKRZAP.EZAPOT().NETTO-SKID.SKXDRAZE ||  EDOKUM.NETTO-SKID.SKXDRAZE ?};
EDOKUM.cntx_pop();
EPODZ.cntx_pop();
1


\rok_firm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Zwraca datę początkową pierwszego roku dla firmy
::   WY: Data początku roku w firmie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=date(0,0,0);
ROK_F.cntx_psh();
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.first() || _wyn:=ROK_F.POCZ_ROK ?};
ROK_F.cntx_pop();
_wyn

:Sign Version 2.0 jowisz:1045 2023/09/27 10:38:09 df4770fa4c3055e64938d3f16981f2b8d622fbdb30816f42ef367daf28338fc848d66e338f7962d63bdc0a731c8868c873e35d73f6ff3b989b40ec8d7068897cd3e0612bcf037345000470b9ed0218ef461301c61c2cc9d121474eb6b7f829743b55b5d82ed9e583b4207bcf12eacd4a8c17b64f4cc0eaceeef5c69463b53353

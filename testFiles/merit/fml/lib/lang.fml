:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: lang.fml
:: Utworzony: 2015.01.30
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Funkcje odpowiedzialne za obsługę wersji językowych
::======================================================================================================================


\del_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Usunięcie tłumaczeń
::   WE: [_a] - ref rekordu
::  OLD: \del_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('LANG_SIK')>100
|| LANG_SIK.index('JEZYK');
   {? _=0
   || LANG_SIK.prefix(cur_tab(1,1).name(),#cur_tab(1,1).ref())
   || LANG_SIK.prefix(ref_name(_a),#_a)
   ?};
   {? LANG_SIK.first() || {! |? LANG_SIK.del() !} ?}
?}


\set_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Ustawienie języka tłumaczeń
::   WE: [_a] - tylko zmiana 1-tak [0]-nie
::  OLD: \set_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
SLU.cntx_psh();
{? exec('slu_lang','lang')
|| SLO.index('SL'); SLO.prefix(SLU.ref());
   SLO.win_sel('ONE_SEL');
   SKID.LANG_SLO();
   {? SLO.select(,1,10)
   || SKID.LANG_SLO:=SLO.ref();
      {? OPERATOR.USER
      || OPERATOR.USER();
         {? exec('get_usersf','users')
         || exec('put_usersf','users',,,,,,,,,,SLO.ref())
         ?}
      ?};
      {? _=0 | _a=0
      || {? cur_tab(1,1)=OKRO_F
         || exec('okrtitle','lang',ROK_F,OKRO_F)
         |? cur_tab(1,1)=GR_SIK & cur_win(0)=cur_win(1)
         || exec('title','lang',GR_SIK)
         |? cur_tab(1,1)=GR_SIK & cur_win(0)<>cur_win(1)
         || grp_disp(GR_SIK,'WER');
            grp_disp(GR_KOL,'WERG');
            AreaTitle.setTitle()
         |? cur_tab(1,1)=DEFW
         || exec('title','lang',DEFW)
         ?}
      ?}
   ?}
|| FUN.info('W parametrach programu\nnie wskazano słownika języków.'@)
?};
SLU.cntx_pop()


\slu_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Ustawienie słownika języków
::  OLD: \slu_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SLJEZYK
|| XINFO.SLJEZYK(); 1
?}


\title
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Ustawia tytuł okna
::   WE:  _a  - tabela z ustawionym oknem wertowania
::       [_b] - czyścić tytuł okna? [1]-tak 0-nie
::  OLD: \title/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? SKID.LANG_SLO<>null
|| {? _=1 | _b || _a.hdr_sel() ?};
   _a.hdr_sel(' - język %1'@[-SKID.LANG_SLO().TR])
?}


\okrtitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Ustawia tytuł okna tabeli okresów
::   WE: _a - tabela fiksowego Roku bilansowego
::       _b - tabela fiksowego okresu obrachunkowego
::  OLD: \okrtitle/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
_b.hdr_sel();
_b.hdr_sel(': %1'@[_a.NAZ]);
{? _b=OKRO_F || exec('title','lang',_b,0) ?}


\kol_et
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Kolory etykiet i ustawienia języków dla GR_KOL
::  OLD: \kol_et/def_wsk.fml
::----------------------------------------------------------------------------------------------------------------------
GR_KOL.cntx_psh();
GR_KOL.index('LP');
GR_KOL.prefix(GR_SIK.ref());
ILE:=GR_KOL.size();
KOL_S:={? ILE<5 || ILE || 4 ?};
{? NRYKOL.find_key(GR_SIK.SKROT,KOL+1)
|| GR_KOL.find_key(NRYKOL.LPKOL)
?};
{! _i:=1..KOL_S |! ($('ROZNE.NAZ'+$_i+':=exec(\'get_lang\',\'lang\',GR_KOL.ref())'))(); GR_KOL.next() !};
GR_KOL.cntx_pop()


\get_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Zwraca tłumaczenie
::   WE: [_a] - ref  - wskazanie z jakiego rekordu pobrać tłumaczenie
::              brak - pobrać tłumaczenie z bieżącego rekordu bieżącej tabeli
::       [_b] - gdy brak tłumaczenia czy podać nazwe w języku narodowym?
::             [1]-tak; -1-tak ze znakiem '!' na początku; 0-nie
::       [_c] - [1]-pełna nazwa 0-skrót
::  OLD: \get_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
LANG_SIK.index('JEZYK');
{? var_pres('_a')<=0
|| LANG_SIK.prefix(cur_tab(1,1).name(),#cur_tab(1,1).ref(),SKID.LANG_SLO)
|| LANG_SIK.prefix(ref_name(_a),#_a,SKID.LANG_SLO)
?};
{? var_pres('_c')<=0 || _c:=1 ?};
{? LANG_SIK.first() & (_c & LANG_SIK.NAZWA<>'' | ~_c & LANG_SIK.SKROT<>'')
|| {? _c || LANG_SIK.NAZWA || LANG_SIK.SKROT ?}
|? var_pres('_b')<=0 | _b
|| {? var_pres('_b')>0 & _b<0 & SKID.LANG_SLO<>null & SKID.LANG_SLO().KOD<>'POL' || '!' || '' ?}+
   exec('lang_nar','lang',{? var_pres('_a')>0 || ref_name(_a) || cur_tab(1,1).name() ?},_c)
|| ''
?}


\lang_nar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Nazwa w języku narodowum
::   WE: _a - maska tabeli
::       _b - 1-pełna nazwa 0-skrót
::  OLD: \lang_nar/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=GR_SIK.name || GR_SIK.NAZWA
|? _a=GR_KOL.name || GR_KOL.NAZWA
|? _a=DEFW.name || DEFW.NAZWA
|? _a=OKRO_F.name || OKRO_F.NAZ
|? _a=OSPR.name || {? _b || OSPR.NAZWA || OSPR.SKROT ?}
|? _a=WERKVIEW.name || WERKVIEW.ETYKIETA
|| ''
?}


\sel_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Wyświetla tłumaczenia
::   WE: [_a] - akronim tłumaczonego pola tabeli (domyslnie NAZWA)
::       [_b] - ref rekordu do tlumaczenia
::  OLD: \lang_nar/skid_lng.fml
::  OLD: \sel_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('slu_lang','lang')
|| {? var_press('_a')<=0 || _a:='NAZWA' ?};
   {? var_press('_b')<=0 || _b:=cur_tab(1,1).ref() ?};
   SKID.LANG_NAR:=($('cur_tab(1,1).'+_a))();
   LANGMASK:=ref_name(_b);
   LANG_REF:=#_b;
   LANG_SIK.index('JEZYK'); LANG_SIK.prefix(LANGMASK,LANG_REF);
   LANG_SIK.win_sel('WER'+{? LANGMASK='okr_spr' || '_FULL' || '' ?});
   SLU.cntx_psh();
   SLO.win_dict('ONE'); SLO.win_sel('ONE');
   LANG_SIK.select();
   SLU.cntx_pop();
   VAR_DEL.delete('LANGMASK','LANG_REF')
|| FUN.info('Nie wybrano słownika języków tłumaczeń.'@)
?}


\bv_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Przed wyświetleniem pola SKID.LANG_NAZ
::   WE: [_a] - null - czyści pole
::              ref  - wskazanie z jakiego rekordu pobrać tłumaczenie
::              brak - pobrać tłumaczenie z bieżącego rekordu bieżącej tabeli
::  OLD: \bv_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0
|| SKID.LANG_NAZ:=exec('get_lang','lang',,0);
   {? var_pres('LANG_SKR',SKID)>0 || SKID.LANG_SKR:=exec('get_lang','lang',,0,0) ?}
|? _a<>null
|| SKID.LANG_NAZ:=exec('get_lang','lang',_a,0);
   {? var_pres('LANG_SKR',SKID)>0 || SKID.LANG_SKR:=exec('get_lang','lang',_a,0,0) ?}
|? _a=null
|| SKID.LANG_NAZ:='';
   {? var_pres('LANG_SKR',SKID)>0 || SKID.LANG_SKR:='' ?}
?}


\upd_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Uaktualnienie tłumaczenia
::   WE: [_a] - wskazanie na rekord którego dotyczy tłumaczenie
::            - brak - rekord bieżacy bieżącej tabeli
::       [_b] - tłumaczenie pełnej nazwy
::            - brak - tłumaczenie pochodzi ze zmiennej SKID.LANG_NAZ
::       [_c] - tłumaczenie skrotu
::            - brak - tłumaczenie pochodzi ze zmiennej SKID.LANG_SKR
::  OLD: \upd_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? SKID.LANG_SLO & SKID.LANG_SLO().KOD<>'POL'
|| LANG_SIK.index('JEZYK');
   {? var_pres('_a')<=0
   || _name:=cur_tab(1,1).name(); _ref:=#cur_tab(1,1).ref()
   || _name:=ref_name(_a); _ref:=#_a
   ?};
   {? var_pres('_b')<=0 || _b:=SKID.LANG_NAZ ?};
   _c:={? var_pres('_c')<=0 & var_pres('LANG_SKR',SKID)>0 || SKID.LANG_SKR || '' ?};
   LANG_SIK.prefix(_name,_ref,SKID.LANG_SLO);
   {? LANG_SIK.first()
   || {? _b<>'' | _c<>''
      || LANG_SIK.NAZWA:=_b;
         LANG_SIK.SKROT:=_c;
         LANG_SIK.put()
      || LANG_SIK.del()
      ?}
   |? _b<>'' | _c<>''
   || LANG_SIK.MASKA:=_name;
      LANG_SIK.REF:=_ref;
      LANG_SIK.SLO:=SKID.LANG_SLO;
      LANG_SIK.NAZWA:=_b;
      LANG_SIK.SKROT:=_c;
      LANG_SIK.add()
   ?}
?}


\bv_l_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Przed wyświetleniem pole SKID.LANG_WER
::  OLD: \bv_l_wer/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
SKID.LANG_WER:=exec('get_lang','lang')


\be_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Przed redakcja pol SKID.LANG_???
::  OLD: \be_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('slu_lang','lang')
|| cur_afld()='LANG_SLO' | SKID.LANG_SLO<>null & SKID.LANG_SLO().KOD<>'POL'
?}


\be_lang2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Czy pole ze słownikiem dostępne do redakcji
::----------------------------------------------------------------------------------------------------------------------
exec('slu_lang','lang') & SKID.LANG_SLO<>null & SKID.LANG_SLO().KOD<>'POL'


\enable
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia dostepność pól do tłumaczeń
::   WE:  _a  - tabela
::       [_b] - okno
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_b')<=0 || _b:=_a.win_edit('?') ?};
_a.efld_opt(_b,'enable='+$exec('be_lang2','lang'),SKID,'LANG_SLO');
_a.efld_opt(_b,'enable='+$exec('be_lang2','lang'),SKID,'LANG_NAZ');
~~


\bl_maska
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Wartosc poczatkowa pola LANG_SIK.MASKA
::  OLD: \bl_maska/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
LANGMASK


\bl_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Wartosc poczatkowa pola LANG_SIK.REF
::  OLD: \bl_ref/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
LANG_REF


\ar_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Rekord po w tabeli LANG_SIK
::  OLD: \ar_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
_r:=chk_rec('SLO','NAZWA');
{? _r=''
|| {? LANG_SIK.SLO().KOD='POL'
   || FUN.info('Tłumaczenie z języka narodowego\nna narodowy jest zbędne.'@); _r:='SLO'
   || LANG_SIK.cntx_psh();
      _ref:=LANG_SIK.ref();
      LANG_SIK.index('JEZYK'); LANG_SIK.prefix(exec('bl_maska','lang'),exec('bl_ref','lang'),LANG_SIK.SLO);
      {? LANG_SIK.first() & (-menu_txt()<>'popraw' | _ref<>LANG_SIK.ref())
      || FUN.info('Istnieje tłumaczenie w języku %1.'@[LANG_SIK.SLO().KOD]);
         _r:='SLO'
      ?};
      LANG_SIK.cntx_pop()
   ?}
?};
_r


\ini_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Inicjuje zmienne zwiazane z jezykiem tlumaczen
::  OLD: \ini_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('get_usersf','users')
|| SKID.LANG_SLO:=USERSF.LANGUAGE
?}


\icon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Formula ustawiajaca ikony
::   WE: [_a] - ref rekordu lub gdy brak biezacy
::       [_b] - [1]-dla pelnej nazwy 0-dla skrotu
::  OLD: \icon/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
{? SKID.LANG_SLO & SKID.LANG_SLO().KOD<>'POL'
|| LANG_SIK.index('JEZYK');
   {? var_pres('_a')<=0
   || LANG_SIK.prefix(cur_tab(1,1).name(),#cur_tab(1,1).ref(),SKID.LANG_SLO)
   || LANG_SIK.prefix(ref_name(_a),#_a,SKID.LANG_SLO)
   ?};
   {? LANG_SIK.first()
   || {? var_pres('_b')<=0 || _b:=1 ?};
      {? _b & LANG_SIK.NAZWA='' | ~_b & LANG_SIK.SKROT=''
      || 'xwin16.png:4'
      || ''
      ?}
   || 'xwin16.png:4'
   ?}
|| ''
?}


\cpy_lang
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Kopiuje tlumaczenia
::   WE: [_a] - wskazanie na rekord zrodlowy, ktorego dotyczy tlumaczenie
::       [_b] - wskazanie na rekord docelowy, ktorego dotyczy tlumaczenie
::  OLD: \cpy_lang/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
LANG_SIK.index('JEZYK'); LANG_SIK.prefix(ref_name(_a),#_a);
{? LANG_SIK.first()
|| _maska:=ref_name(_b);
   _ref:=#_b;
   {!
   |? LANG_SIK.cntx_psh(); LANG_SIK.prefix();
      LANG_SIK.MASKA:=_maska;
      LANG_SIK.REF:=_ref;
      LANG_SIK.add();
      LANG_SIK.cntx_pop();
      LANG_SIK.next()
   !}
?}


\tran_sik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Zwraca tlumaczenie lub oryginal w przypadku braku zwraca ciag pusty
::   WE:  _a  - kod do wyszukania (string) lub numer kodu (number)
::       [_b] - jezyk (ref SLO) - domyslnie SKID.LANG_SLO
::       [_c] - informowac o braku tlumaczenia "!"? [1]-tak 0-nie
::   WY: Tlumaczenie
::  OLD: \tran_sik/skid_tra.fml
::----------------------------------------------------------------------------------------------------------------------
_r:='';
{? (type_of(_a)=1 & _a<>0) | _a<>''
|| {? var_pres('_b')<=0 || _b:=SKID.LANG_SLO ?};
   {? type_of(_a)=1 || _a:=('0000000'+$_a)+7 ?};
   {? _b<>null
   || TRANSLAT.index('TR'); TRANSLAT.prefix(_b,_a);
      {? TRANSLAT.first() || _r:=TRANSLAT.T ?}
   ?};
   {? _r=''
   || TRANS_PL.index('KOD'); TRANS_PL.prefix(_a,_a);
      {? TRANS_PL.first()
      || SLO.prefix(); {? SLO.seek(_b) & SLO.KOD='POL' || _b:=null ?};
         _r:={? _b<>null & (var_pres('_c')<=0 | _c) || '!' || '' ?}+TRANS_PL.ORG
      ?}
   ?}
?};
_r


\get_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Tłumaczy nazwe okresu obrachunkowego
::   WE: _a - OKRO_F.ref()
::  OLD: \get_okr/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
_r:=exec('get_lang','lang',_a,-1);
{? 1+_r='!' & (OKRO_F.prefix(); OKRO_F.seek(_a) & OKRO_F.NR<14)
|| _r1:=exec('tran_sik','lang',OKRO_F.NR+100);
   {? _r1<>'' || _r:=_r1 ?}
?};
_r

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:53 ad5f1840ec47633815703ab6bd4a4b2cfd9633440a859abf7b986ca0f25e59ab3b855ab6618acc5d1a7f1a488a7203853471d0f4054818a81ebad680e0053dc9a0af3edc04e91a8482d026e7bf2156a4483a6a99a78435c02e474b850b4a6d9b932d1b01c77b18a0317e2a916d099b8fe923a1aa4618e92ac6ee719873163a53

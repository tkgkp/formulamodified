:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: luo.fml
:: Utworzony: 12.02.2019
:: Autor: MW
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły oraz wywoływanie obszaru LUO
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: inicjuje obszar LUO
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Podczytanie INFO.KRAJ - kraj waluty opodatkowania
exec('czytaj','#stalesys',,INFO);
exec('czytaj','#stalesys',,XINFO,'SLWYDZIA','SLJEZYK','SLWAL');
exec('czytaj','#stalesys',,KST,'BDO');

:: Podczytanie parametrów oddziału
KSTE.prefix;
KSTE.first();

DISP.R:='P';
exec('jm_kpo','jm');

{? ST.ODDZ <> ''
|| ~~
::   exec('kpo_open','open_tab', ST.ODDZ, ($ST.AR)+2)
|| _par:=params_get();
   {? type_of(_par)<>type_of(~~) & var_pres('in',_par)<>type_of(~~)
   || _in:=_par.in;
      {? var_pres('KPO',_in)=type_of(null()) & _in.KPO
      || _name:=ref_name(_in.KPO);
         _oddz:=(6+_name)+1;
         _rok:=2000+#((8+_name)+2);
         _mc:=#((10+_name)+2);
         __PARSES.setVal('OddzialLogProd',_oddz);
         _args:=__PARSES.args('OkresRok');
         _args.OBSZAR:='LUO';
         _args.AR:=_rok;
         _args.AM:=_mc;
         __PARSES.setVal('OkresRok',_args)
      ?}
   ?}
?};

~~


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Ustawienie parametrów sesji w obszarze roboczym dziedziny LUO
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
__PARSES.editDom('LUO');

AreaTitle.setTitle();

~~


\luo_kpo_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Zmiana parametrów pracy obszaru LUO_KPO
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
_okr_ref:=ST.OKR_REF;
{? __PARSES.editDom('LUO')
|| {? _oddz<>ST.ODDZ || exec('init','luo') ?};
::  Odświeżenie aktywnej zakładki
   {? VAR.A_TABKPO='WKPO'
   || exec('before_luokpo_wkpo','luo')
   |? VAR.A_TABKPO='OKPO'
   || exec('before_luokpo_okpo','luo')
   |? VAR.A_TABKPO='KPO'
   || exec('before_luokpo_kpo','luo')
   |? VAR.A_TABKPO='GKPO'
   || exec('before_luokpo_gpo','luo')
   ?};
   AreaTitle.setTitle()
?}


\luo_kpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Uruchamia obszar roboczy LUO_KPO
::----------------------------------------------------------------------------------------------------------------------
exec('init','luo');

KH.win_dict('WER_LUO');

_env:=exec('env_create','odpady');
params_set('env',_env);

_p_kpo:=_p_wkpo:=_p_okpo:=(exec('chk_role','#b__box',OPERATOR.USER,'LUO_KPO_PKPO')
        | exec('chk_role','#b__box',OPERATOR.USER,'LUO_KPO_DRKP')
        | exec('chk_role','#b__box',OPERATOR.USER,'LUO_KPO_EAKP'));
_p_gpo:=exec('chk_role','#b__box',OPERATOR.USER,'LUO_KPO_PGPO');
_p_odp:=exec('chk_role','#b__box',OPERATOR.USER,'LUO_KPO_PODP');
_p_zgl:=( exec('chk_role','#b__box',OPERATOR.USER,'LUM_ZGL_PZGL')
        | exec('chk_role','#b__box',OPERATOR.USER,'LUM_ZGL_RZGL')
        | exec('chk_role','#b__box',OPERATOR.USER,'LUM_ZGL_DZGZ')
        | exec('chk_role','#b__box',OPERATOR.USER,'LUM_ZGL_DZGL')
        | exec('chk_role','#b__box',OPERATOR.USER,'LUM_ZGL_DZGP'));

_win_grp:=KPO.grp_make(,"params_set(params_get()); grp_edisp(VAR,'KPO_KP');~~",'luokpo_grp',,,"exec('exit','zws',_a)",,'maximized');
::_win_grp:=KPO.grp_make(,,'luokpo_grp',,,"exec('exit','zws',_a)");

:: Karty przekazania odpadów
{? _p_kpo
|| _fb:="exec('before_luokpo_kpo','luo')";
   _fa:="exec('after_luokpo_kpo','luo')";
::   _fr:="exec('refresh_luokpo_kpo','luo')";
   KPO.grp_sel(_win_grp,KPO,'WER','Karty przekazania odpadów'@,,,,,_fb,_fa,,,'maximized')
::   KPO.tab_splt(_win_grp,,'vertical','karty1');
::   KPO.grp_sel(_win_grp,KPOZ,'WER',,,,,,,,,,'maximized_with_title');
::   KPO.tab_splt(_win_grp,'karty1','horizontal','karty2',16);
::   KPO.grp_sel(_win_grp,KPOT,'WER',,,,,,,,,,'maximized_with_title')
?};

:: Wzorce kart przekazania odpadów
{? _p_wkpo
|| _fb:="exec('before_luokpo_wkpo','luo')";
   _fa:="exec('after_luokpo_wkpo','luo')";
   KPO.grp_sel(_win_grp,KPO,'WER_W','Wzorce kart przekazania odpadów'@,,,,,_fb,_fa,,,'maximized')
?};

:: Obsłużone karty przekazania odpadów
{? _p_okpo
|| _fb:="exec('before_luokpo_okpo','luo')";
   KPO.grp_sel(_win_grp,KPO,'WER','Obsłużone karty przekazania odpadów'@,,,,,_fb,,,,'maximized')
?};

:: Gminne punkty odbioru odpadów
{? _p_gpo
|| _fb:="exec('before_luokpo_gpo','luo')";
   _fa:="exec('after_luokpo_gpo','luo')";
   _fr:="exec('refresh_luokpo_gpo','luo')";
   KPO.grp_sel(_win_grp,GPOD,'WER','Gminne punkty odbioru odpadów'@,_fr,,,,_fb,_fa,,,'maximized')
?};

:: Zgłoszenia jednorazowe
{? _p_zgl
|| _fb:="exec('before_luokpo_zgl','luo')";
   _fa:="exec('after_luokpo_zgl','luo',_a)";
   KPO.grp_sel(_win_grp,ZLP,'UZUP','Zgłoszenia KPO'@,,,,,_fb,_fa,,,'maximized');
   _fb:="exec('before_luokpok_zgl','luo')";
   KPO.grp_sel(_win_grp,ZLP,'UZUP','Zgłoszenia KPOK'@,,,,,_fb,_fa,,,'maximized');
   _formikon:="
   {? exec('zlp2TR_NAG','transport_wspolne',ZLP.uidref())=2
   || exec('transpDO','icon')
   || exec('pusta','#icon')
   ?}";
   ZLP.win_fml('UZUP',,'DZP',,'ICON_BEFORE',_formikon);
   _formikon:="
   {? exec('zlp2TR_NAG','transport_wspolne',ZLP.uidref(),,1)=1
   || exec('transpZ','icon')
   || exec('pusta','#icon')
   ?}";
   ZLP.win_fml('UZUP',,'DZ',,'ICON_BEFORE',_formikon)
?};

:: Rodzaje odpadów
{? _p_odp
|| _fb:="exec('before_luokpo_odp','luo')";
   _fa:="exec('after_luokpo_odp','luo')";
   _fr:="exec('refresh_luokpo_odp','luo')";
   KPO.grp_sel(_win_grp,ODP,'WER','Rodzaje odpadów'@,_fr,,,,_fb,_fa,,,'maximized')
?};

:: Kontekst pracy
KPO.grp_splt(_win_grp,'panel0','horizontal','bottom','24,67%');
KPO.grp_edit(_win_grp,VAR,'KPO_KP','Kontekst pracy'@,,,,,'maximized','kpo_kp');

KPO.win_fml('WER',,'SYM',,'ICON_BEFORE',exec('kpo_sym_ib','odpady'));
KPO.fld_opt('WER','col_name=%1 %2'['Masa'@,UMO.JM_KPO],,'IL');

KPO.win_sel(_win_grp);
__kpo_okno_grupowe:=1;
VAR.POD:=_env.KH;
VAR.MPD:=_env.KH_ODB;

AreaTitle.setTabWin(KPO,~~);
AreaTitle.setTitle();

KPO.select();
VAR_DEL.delete('__kpo_okno_grupowe');
~~


\before_luokpo_kpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Przed obsługą okienka z kartami przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
__kpo_okno_grupowe:=1;
VAR.A_TABKPO:='KPO';
exec('kpo_open','open_tab',ST.ODDZ,$(ST.AR)+2);
KPO.index('R');
KPO.prefix('K','N');
:: Akcje zawsze wyłączone:
:: BDO->Zmodyfikuj kartę w BDO
:: BDO->Potwierdzenie PDF->Pobierz
:: BDO->Wydruk karty PDF->Pobierz
_actions:='B(MP(P)K(P)):';
_bdo_act:=exec('is_active','bdo_fun');
{? _bdo_act=0
|| _actions:='B'+_actions+'B'
|? __BDO_TOKEN=''
|| _actions:=_actions+'B(I)'
?};
ODP.actions('WER',{? _bdo_act & __BDO_TOKEN<>'' || '' || 'B' ?});
ODPG.actions('WER',{? _bdo_act & __BDO_TOKEN<>'' || '' || 'B' ?});
ODPP.actions('WER',{? _bdo_act & __BDO_TOKEN<>'' || '' || 'B' ?});
ODPR.actions('WER',{? _bdo_act & __BDO_TOKEN<>'' || '' || 'B' ?});
KPO.actions('WER',_actions,,1);
~~


\after_luokpo_kpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Po obsłudze okienka z kartami przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
~~


\refresh_luokpo_kpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Po odświeżeniu okienka z kartami przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
exec('kpo_disp_wer','odpady');
~~


\before_luokpo_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Rodzaje odpadów
::----------------------------------------------------------------------------------------------------------------------
__kpo_okno_grupowe:=0;
VAR.A_TABKPO:='GKPO';
GPOD.win_sel('WER');
{? GPOD.sel_size() || GPOD.sel_adel() ?};
GPOD.f_clear();
GPOD.index('KOD');
GPOD.prefix(ST.ODDZ);
~~


\after_luokpo_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Rodzaje odpadów
::----------------------------------------------------------------------------------------------------------------------
~~


\refresh_luokpo_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Rodzaje odpadów
::----------------------------------------------------------------------------------------------------------------------
~~


\before_luokpo_odp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Rodzaje odpadów
::----------------------------------------------------------------------------------------------------------------------
__kpo_okno_grupowe:=0;
_bdo_act:=exec('is_active','bdo_fun');
ODP.f_clear();
ODP.index('KOD');
ODP.prefix();
ODP.actions('WER',{? _bdo_act & __BDO_TOKEN<>'' || '' || 'B' ?},,1);
ODPG.actions('WER',{? _bdo_act & __BDO_TOKEN<>'' || '' || 'B' ?});
ODPP.actions('WER',{? _bdo_act & __BDO_TOKEN<>'' || '' || 'B' ?});
~~


\after_luokpo_odp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Rodzaje odpadów
::----------------------------------------------------------------------------------------------------------------------
~~


\refresh_luokpo_odp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.22]
:: OPIS: Rodzaje odpadów
::----------------------------------------------------------------------------------------------------------------------
~~


\before_luokpo_wkpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Przed obsługą okienka ze wzorcami kart przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
__kpo_okno_grupowe:=0;
VAR.A_TABKPO:='WKPO';
exec('kpo_open','open_tab','_','__');
KPO.index('R');
KPO.prefix('W');
~~


\after_luokpo_wkpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Po obsłudze okienka ze wzorcami kart przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
__kpo_okno_grupowe:=0;
exec('kpo_open','open_tab',ST.ODDZ,$(ST.AR)+2);
KPO.index('R');
KPO.prefix('W');
~~


\before_luokpo_okpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Przed obsługą okienka z obsłużonymi kartami przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
__kpo_okno_grupowe:=0;
VAR.A_TABKPO:='OKPO';
exec('kpo_open','open_tab',ST.ODDZ,$(ST.AR)+2);
KPO.index('R');
KPO.prefix('K','T');
KPO.actions('WER','DPUON(U)B(WRMUZOETYDGPKI)ZAN(W):DB',,1);
~~


\before_luokpo_zgl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Przed obsługą okienka zgłoszeń jednorazowych w kontekście kart przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
VAR.A_TABKPO:='ZKPO';
ZLP.index('R');
ZLP.prefix();
ZLP.f_set(,'left join ZLE using (ZLP.ZLE,ZLE.REFERENCE)','ZLE.CZUM=\':_a\' and ZLP.TKPO=\':_b\'','N',exec('typ_kpo','odpady'));
ZLP.actions('UZUP','DPURJZN(W):D',,1);
~~


\before_luokpok_zgl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Przed obsługą okienka zgłoszeń jednorazowych w kontekście kart przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
VAR.A_TABKPO:='ZKPOK';
ZLP.index('R');
ZLP.prefix();
ZLP.f_set(,'left join ZLE using (ZLP.ZLE,ZLE.REFERENCE)'
 ,'ZLE.CZUM=\':_a\' and ZLP.TKPO<>\':_b\' and substr(ZLP.TKPO,1,4)<>\'OSWK\'','N',exec('typ_kpo','odpady'));
ZLP.actions('UZUP','DPURJZN(W):D',,1);
~~


\after_luokpo_zgl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.14]
:: OPIS: Przed obsługą okienka zgłoszeń jednorazowych w kontekście kart przekazania odpadów
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| ZLP.f_clear()
?};
exec('kpo_open','open_tab',ST.ODDZ,$(ST.AR)+2);
~~


\luo_keo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.37]
:: OPIS: Uruchamia obszar roboczy kart ewidencji odpadów
::----------------------------------------------------------------------------------------------------------------------
exec('init','luo');

_env:=exec('env_create','odpady');
_wers:=_env.WERS;
params_set('env',_env);

_grp:=KEO.grp_make('',"params_set(params_get()); grp_edisp(VAR,'KPO_KP');~~",'keogrpsel',,,"exec('exit','zws',_a)",,
                   'maximized');
:: NAGŁÓWKI
_far:="
   params_set(params_get());
   _env:=params_get().env;
:: Nie jest odświeżane okno ze wszystkimi pozycjami
   {? _env.KEOP_WER<>_env.WERS.KEOP_ALL
   || grp_disp(KEOP,params_get().env.KEOP_WER)
   ?}
";
:: KEO
_fb:="params_exec('before_luokeo_keo','luo')";
KEO.grp_sel(_grp,,'WER_KEO','Odpady'@,_far,,,,_fb,,,,'maximized');
:: KEOK
_fb:="params_exec('before_luokeo_keok','luo')";
KEO.grp_sel(_grp,,'WER_KEOK','Odpady komunalne'@,_far,,,,_fb,,,,'maximized');
:: KEON
_fb:="params_exec('before_luokeo_keon','luo')";
KEO.grp_sel(_grp,,'WER_KEON','Odpady niebezpieczne'@,_far,,,,_fb,,,,'maximized');
:: POZYCJE zbiorczo
_fb:="params_exec('before_luokeo_keop_all','luo')";
KEO.grp_sel(_grp,KEOP,_wers.KEOP_ALL,'Wszystkie pozycje'@,_far,,,,_fb,,,,'maximized');
:: POZYCJE
KEO.grp_splt(_grp,,'horizontal','bottom','20,50%');
:: Kontekst
KEO.grp_edit(_grp,VAR,'KPO_KP','Kontekst pracy'@,,,,,'maximized');
:: KEO
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keo_generated','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEO_GEN,'Wytworzone'@,,,,,_fb,,,,'maximized',exec('typ_keo_generated','odpady_keo'));
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keo_excavated','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEO_EXC,'Wydobyte'@,,,,,_fb,,,,'maximized',exec('typ_keo_excavated','odpady_keo'));
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keo_collected','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEO_COL,'Przyjęte'@,,,,,_fb,,,,'maximized',exec('typ_keo_collected','odpady_keo'));
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keo_treated','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEO_TRE,'Przetwarzane'@,,,,,_fb,,,,'maximized',exec('typ_keo_treated','odpady_keo'));
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keo_forwarded','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEO_FOR,'Przekazane'@,,,,,_fb,,,,'maximized',exec('typ_keo_forwarded','odpady_keo'));
:: KEOK
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keok_collected','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEOK_COL,'Odbiór'@,,,,,_fb,,,,'maximized',exec('typ_keok_collected','odpady_keo'));
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keok_received','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEOK_REC,'Przyjęte'@,,,,,_fb,,,,'maximized',exec('typ_keok_received','odpady_keo'));
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keok_processed','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEOK_PRO,'Przetwarzane'@,,,,,_fb,,,,'maximized',exec('typ_keok_processed','odpady_keo'));
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keok_transferred','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEOK_TRA,'Przekazane'@,,,,,_fb,,,,'maximized',exec('typ_keok_transferred','odpady_keo'));
:: KEON
_fb:="params_exec('before_luokeo_keop','luo',exec('typ_keon_item','odpady_keo'))";
KEO.grp_sel(_grp,KEOP,_wers.KEON_ITE,'Pozycje'@,,,,,_fb,,,,'maximized',exec('typ_keon_item','odpady_keo'));

KEO.win_sel(_grp);
KEOP.index('KEO_TYP');

AreaTitle.setTabWin(KEO,~~);
AreaTitle.setTitle();

KEO.select();

~~


\before_luokeo_keo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Przed obsługą okienka z kartami ewidencji odpadów
::----------------------------------------------------------------------------------------------------------------------
VAR.A_TABKEO:=exec('typ_keo','odpady_keo');
KEO.index('MPD_TSYM');
KEO.prefix(VAR.MPD,VAR.A_TABKEO,);
tab_show(1,'bottom'); tab_show(2,'bottom'); tab_show(3,'bottom');
tab_show(4,'bottom'); tab_show(5,'bottom'); tab_show(6,'bottom');
tab_hide(7,,'bottom'); tab_hide(8,,'bottom'); tab_hide(9,,'bottom');
tab_hide(10,,'bottom'); tab_hide(11,,'bottom');
~~


\before_luokeo_keok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Przed obsługą okienka z kartami ewidencji odpadów komunalnych
::----------------------------------------------------------------------------------------------------------------------
VAR.A_TABKEO:=exec('typ_keok','odpady_keo');
KEO.index('MPD_TSYM');
KEO.prefix(VAR.MPD,VAR.A_TABKEO,);
tab_show(1,'bottom'); tab_hide(2,,'bottom'); tab_hide(3,,'bottom');
tab_hide(4,,'bottom'); tab_hide(5,,'bottom'); tab_hide(6,,'bottom');
tab_show(7,'bottom'); tab_show(8,'bottom'); tab_show(9,'bottom');
tab_show(10,'bottom'); tab_hide(11,,'bottom');
~~


\before_luokeo_keon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Przed obsługą okienka z kartami ewidencji odpadów niebezpiecznych
::----------------------------------------------------------------------------------------------------------------------
VAR.A_TABKEO:=exec('typ_keon','odpady_keo');
KEO.index('MPD_TSYM');
KEO.prefix(VAR.MPD,VAR.A_TABKEO,);
tab_show(1,'bottom'); tab_hide(2,,'bottom'); tab_hide(3,,'bottom');
tab_hide(4,,'bottom'); tab_hide(5,,'bottom'); tab_hide(6,,'bottom');
tab_hide(7,,'bottom'); tab_hide(8,,'bottom'); tab_hide(9,,'bottom');
tab_hide(10,,'bottom'); tab_show(11,'bottom');
~~


\before_luokeo_keop_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.37]
:: OPIS: Przed obsługą okienka z wszystkimi pozycjami kart ewidencji odpadów
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_wers:=_env.WERS;

VAR.A_TABKEO:='';
VAR.A_TABKEP:='';

KEOP.index('ALL');
KEOP.prefix(VAR.MPD);
_env.KEOP_WER:=_wers.KEOP_ALL;

tab_show(1,'bottom'); tab_hide(2,,'bottom'); tab_hide(3,,'bottom');
tab_hide(4,,'bottom'); tab_hide(5,,'bottom'); tab_hide(6,,'bottom');
tab_hide(7,,'bottom'); tab_hide(8,,'bottom'); tab_hide(9,,'bottom');
tab_hide(10,,'bottom'); tab_hide(11,,'bottom');
~~


\before_luokeo_keop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Przed obsługą okienek z pozycjami kart ewidencji odpadów
::   WE: _a - STRING - nazwa obsługiwanego rodzaju ewidencji.
::----------------------------------------------------------------------------------------------------------------------
_result:=~~;

{? grp_empty(KEO,'WER_%1'[VAR.A_TABKEO])
|| _result:='#disable'
|| VAR.A_TABKEP:={? var_pres('_a')=type_of('') || _a || '' ?};
   KEOP.index('KEO_TYP');
   KEOP.prefix(KEO.ref(),VAR.A_TABKEP,);
:: Ustalenie aktywnego okna wertowania
   _env:=params_get().env;
   _wers:=_env.WERS;
   _typ:=VAR.A_TABKEP;
   _env.KEOP_WER:=
      {? _typ=exec('typ_keo_collected','odpady_keo')
      || _wers.KEO_COL
      |? _typ=exec('typ_keo_excavated','odpady_keo')
      || _wers.KEO_EXC
      |? _typ=exec('typ_keo_forwarded','odpady_keo')
      || _wers.KEO_FOR
      |? _typ=exec('typ_keo_generated','odpady_keo')
      || _wers.KEO_GEN
      |? _typ=exec('typ_keo_treated','odpady_keo')
      || _wers.KEO_TRE
      |? _typ=exec('typ_keok_collected','odpady_keo')
      || _wers.KEOK_COL
      |? _typ=exec('typ_keok_processed','odpady_keo')
      || _wers.KEOK_PRO
      |? _typ=exec('typ_keok_received','odpady_keo')
      || _wers.KEOK_REC
      |? _typ=exec('typ_keok_transferred','odpady_keo')
      || _wers.KEOK_TRA
      |? _typ=exec('typ_keon_item','odpady_keo')
      || _wers.KEON_ITE
      ?}
?};
_result


\luo_keo_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [21.37]
:: OPIS: Zmiana parametrów pracy obszaru LUO_KEO
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

_oddz:=ST.ODDZ;
_okr_ref:=ST.OKR_REF;
{? __PARSES.editDom('LUO')
|| {? _oddz<>ST.ODDZ || exec('init','luo') ?};
::  Odświeżenie aktywnej zakładki
   {? VAR.A_TABKEO=exec('typ_keo','odpady_keo')
   || exec('before_luokeo_keo','luo')
   |? VAR.A_TABKEO=exec('typ_keok','odpady_keo')
   || exec('before_luokeo_keok','luo')
   |? VAR.A_TABKEO=exec('typ_keon','odpady_keo')
   || exec('before_luokeo_keon','luo')
   |? VAR.A_TABKEO=''
   || exec('before_luokeo_keop_all','luo')
   ?};
   AreaTitle.setTitle()
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 85c56cbe565d16ca594416b55ad560600c9c4428d49e3a000e00335f009da918c3b3dc6801b99d23ac19ae807e75fe5becc151cecc4f191622baef0d65274f3fff676ed7fdb46c5a507a3e6af8054a1c1c4ec97aecc83f2b27d405b0e759a18e595c8aa36e1bf15029a8d56ff7ea5cc7cfe4e1412854941b168f8e6f2bf4cc42

:!UTF-8
::(c) Macrologic SA odzial Wroclaw Wszelkie prawa zastrzeżone
::========================================================================================================================
::Nazwa pliku: mlxstan.fml
::Utworzony: 23.08.2005
::Autor: RB
::========================================================================================================================
::Zawartosc: Naliczanie obrotow w strukturach budzetow Finanse i ksiegowosc wersja budzetowa
::========================================================================================================================


\pozbdod
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [10.30]
:: OPIS: Dla zapisow z analitykami lub wyroznikami opartymi o klasyfikacje  struktury budzetowej formula
::       tworzy dodatkowe rekordy w tabeli ML_POZB. Cel - m.in. mozliwosc naliczania naleznosci/zobowiazan wymagalnych.
::       Dodatkowo formula ze wzgledow optymalizacyjnych dodaje dane do pozycji hierarchii slownikow (ML_POZTR).
::       Podczas analizy do tabeli ML_SLU nie sa dodawane pozycje zwiazane z budzetem zadaniowym
::   WE: _a - tryb przetwarzania: 1 - formula odtwarzajaca, 0 - ksiegowanie
::       _b - czy dodawac pozycje w slownikach hierarchii budzetowych. 1 - tak[domyslna], 0 - nie
::       [_c] - 0 - wykonanie operacji (domyslnie); 1 - tryb kontroli bez operacji.
::       [_d] - rodzaj komunukatow dla zmiennej KOMT - 1 - normalne, 0 - skrocone jednolinijkowe
::   WY: 0/1
::-------------------------------------------------------------------------------------------------------------------
_wyn:=_dalej:=_msg:=_ml_gr:=1;
_rok_f:=null();
_licznik:=0;
_kodslo:=_mlgrtyp:='';
{? _>0 & (type_of(_a)=1) & _a
|| _msg:=_a;
   _rok_f:=ROK_F.ref()
|| _msg:=0;
   _rok_f:=SSTALE.AR
?};
{? _>1 & (type_of(_b)=1) || _ml_gr:=_b || _ml_gr:=1 ?};
{? var_pres('_c')<>1 || _c:=0 ?};
{? var_pres('_d')<>1 || _d:=1 ?};
ML_SLU.prefix();
{? ~_c & _ml_gr & ML_SLU.first()
|| {!|? ML_SLU.del() !}
?};
KS.cntx_psh(); POW.cntx_psh();
ML_POZB.index('POZBPOZ'); ML_POZB.prefix(POZ.ref);
_typ:=_str:=_komun:='';
ROK_F.cntx_psh();
ROK_F.clear();
{? ROK_F.seek(_rok_f)
|| _typ:=exec('typ_kon','ml_conn',POZ.KON);
::1)sekcja dla konta o analityce związanej z klasyfikacją budżetową
   _cz:=_dz:=_rz:=_pg:=_pp:='';
   KS.index('SYM');  KS.prefix();
   KS.find_key(ROK_F.ref, ROK_F.SYNT+POZ.KON);
   _ciag:=ROK_F.SYNT-POZ.KON;
   BUD.cntx_psh();
   BUD.index('KS');
   BUD.prefix(KS.ref);
   {? BUD.first()
   || {!
      |? _ciag:=(+ROK_F.SEP)-_ciag; _licznik+=1;
         _kodslo:=BUD.SLU().SLU().DL+_ciag;
         _ciag:=BUD.SLU().SLU().DL-_ciag;
         {? BUD.SLU().SLU().WZ='Część'
         || _cz:=_kodslo; _mlgrtyp:='CZ'
         |? BUD.SLU().SLU().WZ='Dział'
         || _dz:=_kodslo; _mlgrtyp:='DZ'
         |? BUD.SLU().SLU().WZ='Rozdział'
         || _rz:=_kodslo; _mlgrtyp:='RZ'
         |? BUD.SLU().SLU().WZ='Paragraf'
         || _pg:=_kodslo; _mlgrtyp:='PG'
         |? BUD.SLU().SLU().WZ='Pozycja'
         || _pp:=_kodslo; _mlgrtyp:='PP'
         || _mlgrtyp:='USE'
         ?};
         {? _ml_gr
         || ML_SLU.LP:=_licznik;
            ML_SLU.R:='K';
            ML_SLU.KWOTA:=POZ.SUM;
            ML_SLU.USE:='N';
            ML_SLU.TYP:=_mlgrtyp;
            ML_SLU.SLOKOD:=_kodslo;
            ML_SLU.SLUNAZ:=BUD.SLU().SLU().NAZ;
:            ML_SLU.SLOOPIS:=exec('sloopis','ml_xpert',BUD.SLU().SLU,ML_SLU.SLOKOD);
            {? ('FunkcjaZadaniePodzadanieDziałanie'*SLU.WZ)=0 || ML_SLU.add(1) ?}
         ?};
         BUD.next()
      !};
      {? ((_cz<>'')|(_dz<>'')|(_rz<>'')|(_pg<>'')|(_pp<>''))
      || _dal:={?_typ<>'XXX'
               || exec('budcorct','ml_fiks',_cz,_dz,_rz,_pg,_pp,_typ,ROK_F.ref())
               || exec('budcorct','ml_fiks',_cz,_dz,_rz,_pg,_pp,,ROK_F.ref())
               ?};
         {? _dal & _c
         || _wyn:=1
         |? _dal
         || ML_POZB.blank();
            ML_POZB.TYP:=_typ;
            ML_POZB.TW:=POZ.TP;
            ML_POZB.CZ:=ML_CZ.ref();
            {? _dz<>''
            || ML_POZB.DZ:=ML_DZ.ref()
            ?};
            {? _rz<>''
            || ML_POZB.DZ:=ML_DZ.ref();
               ML_POZB.RZ:=ML_RZ.ref()
            ?};
            {? _pg<>''
            || ML_POZB.DZ:=ML_DZ.ref();
               ML_POZB.RZ:=ML_RZ.ref();
               ML_POZB.PG:=ML_PG.ref()
            ?};
            {? _pp<>''
            || ML_POZB.DZ:=ML_DZ.ref();
               ML_POZB.RZ:=ML_RZ.ref();
               ML_POZB.PG:=ML_PG.ref();
               ML_POZB.PP:=ML_PP.ref()
            ?};
            {? POZ.STR='Wn'
            || ML_POZB.WN:=POZ.SUM
            || ML_POZB.MA:=POZ.SUM
            ?};
            {? ~exec('czw_klsf','mlxstan', KS.ref()) || ML_POZB.add() ?}
         || _komun:='\nNiespójność struktury klasyfikacji budżetowej dla kont. ';
            {? _msg=0
            || {? KOMT=''
               || KOMT:={? _d
                        || '\nDla konta '+POZ.KON +' w dokumencie: '+POZ.DOK().NK +' wystąpił wyjątek.'+_komun
                        || 'Pozycja '+$POZ.POZ+' dla konta '+POZ.KON +' niespójność struktury budżetowej.'
                        ?}
               ?}
            || FUN.emsg('Dla konta '+POZ.KON +' w dokumencie: '+POZ.DOK().NK +' wystąpił wyjątek.'+_komun,'Uwaga !')
            ?};
           _wyn:=0
          ?}
      ?}
  ?};
  BUD.cntx_pop();

::2)sekcja dla konta o wyróżnikach klasyfikacji budżetowej. Zalozono ze nie ma pozycji zwiazanych z DZ,RZ,PG i PP
::jednoczesnie na koncie i na wyroznikach.
  POW.index('POZ');  POW.prefix(POZ.ref());
:: zapisanie ilosci rozdzielnikow na poszczegolnych poziomach i "pierwszych"
:: elementow na kazdym poziomie. _lp oznacza ILOSC wykorzystywanych przez
:: wyrozniki TYPOW elementów klasyfikacji
  {? POW.first()
  || _budpw:=_licz:=_i:=_kw:=0;
     'może być tylko jeden rozdzielnik wyróżnika klasyfikacji budżetowej';
     _rozdzielnik:='';
     KS_W.index('LP');
     KS_W.prefix(KS.ref);
     {? KS_W.first()
     || {!
        |?
           {? 'CzęśćDziałRozdziałParagrafPozycja'*KS_W.SLU().SLU().WZ
           ||
              _budpw:=1;
              {? (KS_W.ROZDZ='T') | (exec('roz_bo','mlxstan',POZ.ref(),KS_W.LP,KS_W.SLU,OKRO_F.ref())>1)
              || _rozdzielnik:=KS_W.SLU().SLU().WZ
              ?}
           ?};
           KS_W.next&_rozdzielnik=''
        !}
     ?};
     {? _budpw
     || {? _rozdzielnik<>''
        || {!
           |? {? POW.SLU().SLU().WZ=_rozdzielnik
              || _licz+=1
              ?};
              POW.next()
           !};
           {? _licz>0
           ||
              _tabrzd:=obj_new(_licz*2);
              POW.first();
              {!
              |? {? POW.SLU().SLU().WZ=_rozdzielnik
                 || _i+=1;
                    _tabrzd[(2*_i)-1]:=POW.SLO().KOD;
                    _tabrzd[2*_i]:=POW.KW
                 |? POW.SLU().SLU().WZ='Część'
                 || _cz:=POW.SLO().KOD;
                    _kw:=POW.KW
                 |? POW.SLU().SLU().WZ='Dział'
                 || _dz:=POW.SLO().KOD;
                    _kw:=POW.KW
                 |? POW.SLU().SLU().WZ='Rozdział'
                 || _rz:=POW.SLO().KOD;
                    _kw:=POW.KW
                 |? POW.SLU().SLU().WZ='Paragraf'
                 || _pg:=POW.SLO().KOD;
                    _kw:=POW.KW
                 |? POW.SLU().SLU().WZ='Pozycja'
                 || _pp:=POW.SLO().KOD;
                    _kw:=POW.KW
                 ?};
                 POW.next()
              !};
              'Przygotowanie poziomu z rozdzielnikiem i zapis';
              {! _i:=1.._licz
              |! {? _rozdzielnik='Część'
                 || _cz:=_tabrzd[(2*_i)-1];
                    _kw:=_tabrzd[2*_i]
                 |? _rozdzielnik='Dział'
                 || _dz:=_tabrzd[(2*_i)-1];
                    _kw:=_tabrzd[2*_i]
                 |? _rozdzielnik='Rozdział'
                 || _rz:=_tabrzd[(2*_i)-1];
                    _kw:=_tabrzd[2*_i]
                 |? _rozdzielnik='Paragraf'
                 || _pg:=_tabrzd[(2*_i)-1];
                    _kw:=_tabrzd[2*_i]
                 |? _rozdzielnik='Pozycja'
                 || _pp:=_tabrzd[(2*_i)-1];
                    _kw:=_tabrzd[2*_i]
                 ?};
                 {? (_cz<>'')|(_dz<>'')|(_rz<>'')|(_pg<>'')|(_pp<>'')
                 || _dal:=exec('budcorct','ml_fiks',_cz,_dz,_rz,_pg,_pp,,ROK_F.ref);
                    {? _dal & _c
                    || _wyn:=1
                    |? _dal
                    || ML_POZB.blank();
                       ML_POZB.TYP:=_typ;
                       ML_POZB.TW:=POW.POZ().TP;
                       ML_POZB.CZ:=ML_CZ.ref();
                       {? _dz<>''
                       || ML_POZB.DZ:=ML_DZ.ref()
                       ?};
                       {? _rz<>''
                       || ML_POZB.DZ:=ML_DZ.ref();
                          ML_POZB.RZ:=ML_RZ.ref()
                       ?};
                       {? _pg<>''
                       || ML_POZB.DZ:=ML_DZ.ref();
                          ML_POZB.RZ:=ML_RZ.ref();
                          ML_POZB.PG:=ML_PG.ref()
                       ?};
                       {? _pp<>''
                       || ML_POZB.DZ:=ML_DZ.ref();
                          ML_POZB.RZ:=ML_RZ.ref();
                          ML_POZB.PG:=ML_PG.ref();
                          ML_POZB.PP:=ML_PP.ref()
                       ?};
                       {? POW.POZ().STR='Wn'
                       || ML_POZB.WN:=_kw
                       || ML_POZB.MA:=_kw
                       ?};
                       ML_POZB.add()
                    || _komun:='\nNiespójność struktury klasyfikacji budżetowej ze słownikami wyróżników.';
                       {? _msg=0
                       || {? KOMT=''
                          || KOMT:={? _d
                                   || '\nDla konta '+POZ.KON +' w dokumencie: '+POZ.DOK().NK +' wystąpił wyjątek.'+_komun
                                   || 'Pozycja '+$POZ.POZ+' dla konta '+POZ.KON +' niespójność struktury budżetowej ze słownikami wyróżników.'
                                   ?}
                          ?}
                       || FUN.emsg('Dla konta '+POZ.KON +' w dokumencie: '+POZ.DOK().NK +' wystąpił wyjątek.'+_komun,'Uwaga !')
                       ?};
                       _wyn:=0;
                       _i:=_licz
                    ?}
                 ?}
              !};
              obj_del(_tabrzd)
           ?}
        || POW.first();
           {!
           |? {? POW.SLU().SLU().WZ='Część'
              || _cz:=POW.SLO().KOD;
                 _kw:=POW.KW
              |? POW.SLU().SLU().WZ='Dział'
              || _dz:=POW.SLO().KOD;
                 _kw:=POW.KW
              |? POW.SLU().SLU().WZ='Rozdział'
              || _rz:=POW.SLO().KOD;
                 _kw:=POW.KW
              |? POW.SLU().SLU().WZ='Paragraf'
              || _pg:=POW.SLO().KOD;
                 _kw:=POW.KW
              |? POW.SLU().SLU().WZ='Pozycja'
              || _pp:=POW.SLO().KOD;
                 _kw:=POW.KW
              ?};
              POW.next()
           !};
           {? (_cz<>'')|(_dz<>'')|(_rz<>'')|(_pg<>'')|(_pp<>'')
           || _dal:=exec('budcorct','ml_fiks',_cz,_dz,_rz,_pg,_pp,,ROK_F.ref());
              {? _dal & _c
              || _wyn:=1
              |? _dal
              || ML_POZB.blank();
                 ML_POZB.TYP:=_typ;
                 ML_POZB.TW:=POW.POZ().TP;
                 ML_POZB.CZ:=ML_CZ.ref();
                 {? _dz<>''
                 || ML_POZB.DZ:=ML_DZ.ref()
                 ?};
                 {? _rz<>''
                 || ML_POZB.DZ:=ML_DZ.ref();
                    ML_POZB.RZ:=ML_RZ.ref()
                 ?};
                 {? _pg<>''
                 || ML_POZB.DZ:=ML_DZ.ref();
                    ML_POZB.RZ:=ML_RZ.ref();
                    ML_POZB.PG:=ML_PG.ref()
                 ?};
                 {? _pp<>''
                 || ML_POZB.DZ:=ML_DZ.ref();
                    ML_POZB.RZ:=ML_RZ.ref();
                    ML_POZB.PG:=ML_PG.ref();
                    ML_POZB.PP:=ML_PP.ref()
                 ?};
                 {? POW.POZ().STR='Wn'
                 || ML_POZB.WN:=_kw
                 || ML_POZB.MA:=_kw
                 ?};
                 ML_POZB.add()
              || _komun:='\nNiespójność struktury klasyfikacji budżetowej ze słownikami wyróżników.';
                 {?_msg=0
                 || {? KOMT=''
                    || KOMT:={? _d
                             || _komun
                             || 'Pozycja '+$POZ.POZ+' dla konta '+POZ.KON +' niespójność struktury budżetowej ze słownikami wyróżników.'
                             ?}
                    ?}
                 || FUN.emsg('Dla konta '+POZ.KON +' w dokumencie: '+POZ.DOK().NK +' wystąpił wyjątek.'+_komun,'Uwaga !')
                 ?};
                 _wyn:=0
              ?}
           ?}
        ?}
     ?}
  ?}
?};
::Dodatkowo zapamietujemy info o wartosciach wyroznikowach w celu analizy hierarchii slownikow
{? ~_c & _ml_gr & POW.first()
|| {!|?
      ML_SLU.LP:=_licznik+POW.LP; ML_SLU.R:='W'; ML_SLU.KWOTA:=POW.KW; ML_SLU.USE:='N';
      ML_SLU.SLOKOD:=POW.SLO().KOD;
      ML_SLU.SLUNAZ:=POW.SLU().SLU().NAZ;
      ML_SLU.SLOOPIS:=POW.SLO().TR;
      {? POW.SLU().SLU().WZ='Część'
      || ML_SLU.TYP:='CZ'
      |? POW.SLU().SLU().WZ='Dział'
      || ML_SLU.TYP:='DZ'
      |? POW.SLU().SLU().WZ='Rozdział'
      || ML_SLU.TYP:='RZ'
      |? POW.SLU().SLU().WZ='Paragraf'
      || ML_SLU.TYP:='PG'
      |? POW.SLU().SLU().WZ='Pozycja'
      || ML_SLU.TYP:='PP'
      || ML_SLU.TYP:='USE'
      ?};
      {? ('FunkcjaZadaniePodzadanieDziałanie'*SLU.WZ)=0 || ML_SLU.add(1) ?};
      POW.next()
   !}
?};
: {? ~_c & _ml_gr || exec('add_ml_poztr','ml_gr',ML_SLU,_rok_f,_typ,POZ.ref(),_msg,OKRO_F.ref()) ?};
KS.cntx_pop(); POW.cntx_pop(); ROK_F.cntx_pop();
ML_SLU.prefix(); {? ~_c & _ml_gr & ML_SLU.first() || {!|? ML_SLU.del() !} ?};
{? _c & KOMT<>''
|| _wyn:=0
?};
_wyn


\pozbmod
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [8.70]
:: OPIS: Formula podczas poprawiania danych w POZ. Poprawiane sa dane w strukturach budzetowych oraz w hierarchiach
::       slownikow aktywnych w danym roku.
::   WE: _a - tryb przetwarzania: 1 - formula odtwarzajaca, 0 - ksiegowanie
::       _b - czy zmieniac pozycje w slownikach hierarchii budzetowych. 1 - tak[domyslna], 0 - nie
::       _c- okres, domyslnie SSTALE.AO
::-------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
{? var_pres('_c')<0 || _c:=SSTALE.AO ?};
{? OKRO_F.seek(_c)
|| exec('pozbusu','mlxstan',,_b,OKRO_F.ref());
   exec('mlbzaddod','ml_zad',_a,_c);
   exec('pozbdod','mlxstan',_a,_b)
?};
OKRO_F.cntx_pop();
~~

\pozbusu
::-------------------------------------------------------------------------------------------------------------------
::  UTW: RB [8.70]
:: OPIS: Usuwa wszystkie zapisy w tabelach ML_POZB i ML_POZTR przypisane do danego rekordu POZ
::   WE: _a - POZ.ref(), domyslnie aktualny w buforze
::       _b - czy usuwac pozycje w slownikach hierarchii budzetowych. 1 - tak[domyslna], 0 - nie
::       _c - okres, domyslnie SSTALE.AO
::-------------------------------------------------------------------------------------------------------------------
_dalej:=1;
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
{? var_pres('_c')<0 || _c:=SSTALE.AO ?};
{? OKRO_F.seek(_c)
|| {? _>0 & (type_of(_a)=7)
   || POZ.cntx_psh(); POZ.index('DOK'); POZ.prefix();
      {? POZ.seek(_a)
      || _pozref:=_a
      || FUN.info('Nie odnaleziono przekazanej pozycji tabeli');
         _dalej:=0
      ?};
      POZ.cntx_pop()
   || _pozref:=POZ.ref()
   ?};
   {? _>1 & (type_of(_b)=1) || _ml_gr:=_b || _ml_gr:=1 ?};
   ML_POZB.cntx_psh();
   ML_POZB.index('POZBPOZ');
   ML_POZB.prefix(POZ.ref);
   {? _dalej & ML_POZB.first() || {!|? ML_POZB.del() !} ?};
   ML_POZB.cntx_pop();
   {? _dalej & _ml_gr || exec('ml_poztr_del','ml_gr',_pozref,OKRO_F.ref()) ?}
?};
OKRO_F.cntx_pop();
~~

\add_bud_obr
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [8.70]
:: OPIS: Dodanie obrotow na strukturach budzetowych ML_OBRCZ,ML_OBRDZ,ML_OBRRZ,ML_OBRPG,ML_OBRPP
::       na podstawie zapisow w tabeli ML_POZB, dla aktualnie przetwarzanego rekordu POZ.
::       Poniewaz ML_POZB jest zawsze w walucie FINFO.NAROD na razie wszystko jest w
::       jednej walucie.
::   WE: _a - waluta SLO.ref
::       _b - okres, domyslnie SSTALE.AO
::       _c - tryb przetwarzania: 1 - formula odtwarzajaca, 0 - ksiegowanie
::-------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<0 || _b:=SSTALE.AO ?};
{? OKRO_F.seek(_b)
|| {? _<3 || _msg:=0 || _msg:=_c ?};
   _wyn:=1; _obrcz:=_obrdz:=_obrrz:=_obrpg:=_obrpp:=0; _komun:='';
   ML_POZB.index('POZBPOZ'); ML_POZB.prefix(POZ.ref);
   ML_OBRCZ.index('OTYPCZ'); ML_OBRCZ.prefix();
   ML_OBRDZ.index('OTYPDZ'); ML_OBRDZ.prefix();
   ML_OBRRZ.index('OTYPRZ'); ML_OBRRZ.prefix();
   ML_OBRPG.index('OTYPPG'); ML_OBRPG.prefix();
   ML_OBRPP.index('OTYPPP'); ML_OBRPP.prefix();
   {? ML_POZB.first
   ||   {!
         |?
           _obrcz:=_obrdz:=_obrrz:=_obrpg:=_obrpp:=0;
           {? ML_POZB.CZ<>null() & ML_POZB.TYP<>'' & _wyn
           ||   {? ML_OBRCZ.lock(1)
                ||   _obrcz:=ML_OBRCZ.find_key(_a,ML_POZB.TYP,ML_POZB.CZ,DOK.ODD,OKRO_F.KOD);
                     {? _obrcz=0
                     ||   ML_OBRCZ.blank(1);
                          ML_OBRCZ.CZ:=ML_POZB.CZ;
                          ML_OBRCZ.WAL:=_a;
                          ML_OBRCZ.ODD:=DOK.ODD;
                          ML_OBRCZ.OKRO:=OKRO_F.ref;
                          ML_OBRCZ.TYP:=ML_POZB.TYP
                     ?};
                     ML_OBRCZ.WN+=ML_POZB.WN;
                     ML_OBRCZ.MA+=ML_POZB.MA;
                     _wyn:={? _obrcz || ML_OBRCZ.put() || ML_OBRCZ.add() ?};
                     ML_OBRCZ.unlock;
                     _wyn
                ||   _komun:='\nNieudana próba zablokowania tabeli obrotów budżetowych - części.';
                     {? _msg=0
                     ||   {? KOMT=''|| KOMT:=_komun ?}
                     ||   FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                                 ' wystąpił wyjątek.'+_komun,'Uwaga !')
                     ?};
                     _wyn:=0
                ?}
           ?};
           {? ML_POZB.DZ<>null() & ML_POZB.TYP<>'' & _wyn
           ||   {? ML_OBRDZ.lock(1)
                ||   _obrdz:=ML_OBRDZ.find_key(_a,ML_POZB.TYP,ML_POZB.DZ,DOK.ODD,OKRO_F.KOD);
                     {? _obrdz=0
                     ||   ML_OBRDZ.blank(1);
                          ML_OBRDZ.CZ:=ML_POZB.CZ;
                          ML_OBRDZ.DZ:=ML_POZB.DZ;
                          ML_OBRDZ.WAL:=_a;
                          ML_OBRDZ.ODD:=DOK.ODD;
                          ML_OBRDZ.OKRO:=OKRO_F.ref;
                          ML_OBRDZ.TYP:=ML_POZB.TYP
                     ?};
                     ML_OBRDZ.WN+=ML_POZB.WN;
                     ML_OBRDZ.MA+=ML_POZB.MA;
                     _wyn:={? _obrdz || ML_OBRDZ.put() || ML_OBRDZ.add() ?};
                     ML_OBRDZ.unlock;
                     _wyn
                ||   _komun:='\nNieudana próba zablokowania tabeli obrotów budżetowych - działy.';
                     {? _msg=0
                     ||   {? KOMT=''|| KOMT:=_komun ?}
                     ||   FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                                 ' wystąpił wyjątek.'+_komun,'Uwaga !')
                     ?};
                     _wyn:=0
                ?}
           ?};
           {? ML_POZB.RZ<>null() & ML_POZB.TYP<>'' & _wyn
           ||   {? ML_OBRRZ.lock(1)
                ||   _obrrz:=ML_OBRRZ.find_key(_a,ML_POZB.TYP,ML_POZB.RZ,DOK.ODD,OKRO_F.KOD);
                     {? _obrrz=0
                     ||   ML_OBRRZ.blank(1);
                          ML_OBRRZ.CZ:=ML_POZB.CZ;
                          ML_OBRRZ.DZ:=ML_POZB.DZ;
                          ML_OBRRZ.RZ:=ML_POZB.RZ;
                          ML_OBRRZ.WAL:=_a;
                          ML_OBRRZ.ODD:=DOK.ODD;
                          ML_OBRRZ.OKRO:=OKRO_F.ref;
                          ML_OBRRZ.TYP:=ML_POZB.TYP
                     ?};
                     ML_OBRRZ.WN+=ML_POZB.WN;
                     ML_OBRRZ.MA+=ML_POZB.MA;
                     _wyn:={? _obrrz || ML_OBRRZ.put() || ML_OBRRZ.add() ?};
                     ML_OBRRZ.unlock;
                     _wyn
                ||   _komun:='\nNieudana próba zablokowania tabeli obrotów budżetowych - rozdziały.';
                     {? _msg=0
                     ||   {? KOMT='' || KOMT:=_komun ?}
                     ||   FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                                  ' wystąpił wyjątek.'+_komun,'Uwaga !')
                     ?};
                     _wyn:=0
                ?}
           ?};
           {? ML_POZB.PG<>null() & ML_POZB.TYP<>'' & _wyn
           ||   {? ML_OBRPG.lock(1)
                ||   _obrpg:=ML_OBRPG.find_key(_a,ML_POZB.TYP,ML_POZB.PG,DOK.ODD,OKRO_F.KOD);
                     {? _obrpg=0
                     ||   ML_OBRPG.blank(1);
                          ML_OBRPG.CZ:=ML_POZB.CZ;
                          ML_OBRPG.DZ:=ML_POZB.DZ;
                          ML_OBRPG.RZ:=ML_POZB.RZ;
                          ML_OBRPG.PG:=ML_POZB.PG;
                          ML_OBRPG.WAL:=_a;
                          ML_OBRPG.ODD:=DOK.ODD;
                          ML_OBRPG.OKRO:=OKRO_F.ref;
                          ML_OBRPG.TYP:=ML_POZB.TYP
                     ?};
                     ML_OBRPG.WN+=ML_POZB.WN;
                     ML_OBRPG.MA+=ML_POZB.MA;
                     _wyn:={? _obrpg || ML_OBRPG.put() || ML_OBRPG.add() ?};
                     ML_OBRPG.unlock;
                     _wyn
                ||   _komun:='\nNieudana próba zablokowania tabeli obrotów budżetowych - paragrafy.';
                     {? _msg=0
                     ||   {? KOMT='' || KOMT:=_komun ?}
                     ||   FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                                  ' wystąpił wyjątek.'+_komun,'Uwaga !')
                     ?};
                     _wyn:=0
                ?}
           ?};
           {? ML_POZB.PP<>null() & ML_POZB.TYP<>'' & _wyn
           ||   {? ML_OBRPP.lock(1)
                ||   _obrpp:=ML_OBRPP.find_key(_a,ML_POZB.TYP,ML_POZB.PP,DOK.ODD,OKRO_F.KOD);
                     {?_obrpp=0
                     ||   ML_OBRPP.blank(1);
                          ML_OBRPP.CZ:=ML_POZB.CZ;
                          ML_OBRPP.DZ:=ML_POZB.DZ;
                          ML_OBRPP.RZ:=ML_POZB.RZ;
                          ML_OBRPP.PG:=ML_POZB.PG;
                          ML_OBRPP.PP:=ML_POZB.PP;
                          ML_OBRPP.WAL:=_a;
                          ML_OBRPP.ODD:=DOK.ODD;
                          ML_OBRPP.OKRO:=OKRO_F.ref;
                          ML_OBRPP.TYP:=ML_POZB.TYP
                     ?};
                     ML_OBRPP.WN+=ML_POZB.WN;
                     ML_OBRPP.MA+=ML_POZB.MA;
                     _wyn:={? _obrpp || ML_OBRPP.put() || ML_OBRPP.add() ?};
                     ML_OBRPP.unlock;
                     _wyn
                ||   _komun:='\nNieudana próba zablokowania tabeli obrotów budżetowych - pozycycje paragrafowe.';
                     {? _msg=0
                     ||   {? KOMT=''|| KOMT:=_komun ?}
                     ||   FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                                  ' wystąpił wyjątek.'+_komun,'Uwaga !')
                     ?};
                     _wyn:=0
                ?}
           ?};
           ML_POZB.next
        !}
   ?}
|| _wyn:=0
?};
_wyn


\wyc_bud_obr
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [8.70]
:: OPIS: Wycofanie obrotow ze struktur budzetowych ML_OBRCZ,ML_OBRDZ,ML_OBRRZ,ML_OBRPG,ML_OBRPP
::       na podstawie zapisow w tabeli ML_POZB, dla aktualnie przetwarzanego rekordu POZ.
::   WE: _a - waluta SLO.ref
::       _b - okres, domyslnie SSTALE.AO
::       _c - tryb przetwarzania: 1 - formula odtwarzajaca, 0 - ksiegowanie
::-------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<0 || _b:=SSTALE.AO ?};
{? OKRO_F.seek(_b)
|| {? _< 3 || _msg:=0 || _msg:=_c ?};
   _wyn:=1; _komun:='';
   ML_POZB.index('POZBPOZ'); ML_POZB.prefix(@.POZ.ref);
   {? ML_POZB.first
   || {!
      |?
         {? ML_POZB.CZ<>null() & ML_POZB.TYP<>'' & _wyn
         || ML_OBRCZ.index('OTYPCZ');
            ML_OBRCZ.clear();
            {? ML_OBRCZ.find_key(_a,ML_POZB.TYP,ML_POZB.CZ,DOK.ODD,OKRO_F.KOD)
            || ML_OBRCZ.WN-=ML_POZB.WN;
               ML_OBRCZ.MA-=ML_POZB.MA;
               ML_OBRCZ.put
            || _komun:='\nNie znaleziono rekordu w tabeli obrotów - części.';
               {? _msg=0
               || {? KOMT='' || KOMT:=_komun ?}
               || FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                     ' wystąpił wyjątek.'+_komun,'Uwaga !')
               ?};
              _wyn:=0
            ?}
         ?};
         {? ML_POZB.DZ<>null() & ML_POZB.TYP<>'' & _wyn
         || ML_OBRDZ.index('OTYPDZ');
            ML_OBRDZ.clear();
            {? ML_OBRDZ.find_key(_a,ML_POZB.TYP,ML_POZB.DZ,DOK.ODD,OKRO_F.KOD)
            || ML_OBRDZ.WN-=ML_POZB.WN;
               ML_OBRDZ.MA-=ML_POZB.MA;
               ML_OBRDZ.put
            || _komun:='\nNie znaleziono rekordu w tabeli obrotów - działy.';
               {? _msg=0
               || {? KOMT='' || KOMT:=_komun ?}
               || FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                     ' wystąpił wyjątek.'+_komun,'Uwaga !')
               ?};
               _wyn:=0
            ?}
         ?};
         {? ML_POZB.RZ<>null() & ML_POZB.TYP<>'' & _wyn
         || ML_OBRRZ.index('OTYPRZ');
            ML_OBRRZ.clear();
            {? ML_OBRRZ.find_key(_a,ML_POZB.TYP,ML_POZB.RZ,DOK.ODD,OKRO_F.KOD)
            || ML_OBRRZ.WN-=ML_POZB.WN;
               ML_OBRRZ.MA-=ML_POZB.MA;
               ML_OBRRZ.put
            || _komun:='\nNie znaleziono rekordu w tabeli obrotów - rozdziały.';
               {? _msg=0
               || {? KOMT='' || KOMT:=_komun ?}
               || FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                     ' wystąpił wyjątek.'+_komun,'Uwaga !')
               ?};
               _wyn:=0
            ?}
         ?};
         {? ML_POZB.PG<>null() & ML_POZB.TYP<>'' & _wyn
         || ML_OBRPG.index('OTYPPG');
            ML_OBRPG.clear();
            {? ML_OBRPG.find_key(_a,ML_POZB.TYP,ML_POZB.PG,DOK.ODD,OKRO_F.KOD)
            || ML_OBRPG.WN-=ML_POZB.WN;
               ML_OBRPG.MA-=ML_POZB.MA;
               ML_OBRPG.put
            || _komun:='\nNie znaleziono rekordu w tabeli obrotów - paragrafy.';
               {? _msg=0
               || {? KOMT='' || KOMT:=_komun ?}
               || FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                     ' wystąpił wyjątek.'+_komun,'Uwaga !')
               ?};
               _wyn:=0
            ?}
         ?};
         {? ML_POZB.PP<>null() & ML_POZB.TYP<>'' & _wyn
         || ML_OBRPP.index('OTYPPP');
            ML_OBRPP.clear();
            {? ML_OBRPP.find_key(_a,ML_POZB.TYP,ML_POZB.PP,DOK.ODD,OKRO_F.KOD)
            || ML_OBRPP.WN-=ML_POZB.WN; ML_OBRPP.MA-=ML_POZB.MA; ML_OBRPP.put
            || _komun:='\nNie znaleziono rekordu w tabeli obrotów - pozycje paragrafowe.';
               {? _msg=0
               || {? KOMT='' || KOMT:=_komun ?}
               || FUN.emsg('Dla konta '+POZ.KON+' w dokumencie: '+POZ.DOK().NK+
                     ' wystąpił wyjątek.'+_komun,'Uwaga !')
               ?};
               _wyn:=0
            ?}
         ?};
         ML_POZB.next
      !}
   ?}
|| _wyn:=0
?};
_wyn


\pozbodtw
::-------------------------------------------------------------------------------------------------------------------
::  UTW: RB [x.xx]
:: OPIS: Odtwarzanie
::   WE: _a - czy odtwarzac hierarchie slownikow
::-------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.index('ROK');
_ar:=SSTALE.AR; _ao:=SSTALE.AO;
AN.cntx_psh(); DOK.cntx_psh(); POZ.cntx_psh();
POW.cntx_psh(); ML_POZB.cntx_psh(); ML_POZTR.cntx_psh();
ML_OBRTR.cntx_psh();
ML_BZAD.cntx_psh();
_an_name:=AN.name(); _pozname:=POZ.name(); _powname:=POW.name();
_pozbnam:=ML_POZB.name(); _dokname:=DOK.name();
_mlobrna:=ML_OBRTR.name();
_mlbzadna:=ML_BZAD.name();
_mlpozna:=ML_POZTR.name();
_rok_od:=ROZNE.UT_ROKOD().KOD; _rok_do:=ROZNE.UT_ROKDO().KOD;
_okr_od:=ROZNE.UT_OKROD().NR; _okr_do:=ROZNE.UT_OKRDO().NR;
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {!
   |? {? -ROK_F.ZAM='n'
      || POW.index('KONTO');
         AN.use('koan__'+ROK_F.KOD); AN.index('KS');
         OKRO_F.prefix(ROK_F.ref());
         {? OKRO_F.first()
         || {!
            |? {? {? ROK_F.KOD=_rok_od || OKRO_F.NR>=_okr_od  || 1 ?} &
                  {? ROK_F.KOD=_rok_do || OKRO_F.NR<=_okr_do   || 1 ?}
               || DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2)); DOK.index('REJ'); DOK.prefix;
                  POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2)); POZ.index('SYMKON'); POZ.prefix;
                  POW.use('pow_'+ROK_F.KOD+form(OKRO_F.NR,-2)); POW.index('KONTO'); POW.prefix;
                  ML_POZB.use('pozb'+ROK_F.KOD+form(OKRO_F.NR,-2)); ML_POZB.index('POZBPOZ'); ML_POZB.prefix;
                  ML_BZAD.use('mlbzad'+ROK_F.KOD); ML_BZAD.index('MLBZOKRP'); ML_BZAD.prefix;
                  ML_OBRTR.use('mlobrt'+ROK_F.KOD); ML_OBRTR.index('MLOBROTY'); ML_OBRTR.prefix;
                  ML_POZTR.use('mlpozt'+ROK_F.KOD); ML_POZTR.index('OPOZHIER');
                  KS.index('SYM'); KS.prefix(ROK_F.ref);
                  {? KS.first()
                  || {!
                     |?
::                           'trzeba sprawdzic juz w KS bo z AN mogloby trwac bardzo dlugo,ale tylko gdy odtwarzamy';
::                           'same obroty na strukturach.Przy hierarchiach nalezy przeanalizowac wszystkie konta ktore';
::                           'maja budowe lub wyrozniki oparte slowniki budzetowe oraz slowniki powiazane hierarchiami';
::                           'slownikow aktywnymi w aktualnie przetwarzanym roku';
                        _dalej:={? _a=0
                                || exec('czk_klsf','mlxstan',KS.ref())
:                                || exec('czy_mlgr','ml_gr',KS.ref(),ROK_F.ref())
                                ?};
                        {? _dalej
                        || AN.prefix(); AN.prefix(KS.ref);
                           {? AN.first()
                           || {!
                              |?
                                 POZ.prefix();
                                 echo();
                                 echo('Trwa odtwarzanie pozycji struktury budżetowej - konta '+
                                       AN.SYM+' '+form(OKRO_F.NR,-2)+' '+ROK_F.NAZ);
                                 {? AN.WAL<>FINFO.NAROD
                                 || POZ.prefix('T','T',AN.SYM,AN.WAL)
                                 || POZ.prefix('T','T',AN.SYM)
                                 ?};
                                 {? POZ.first()
                                 || {!
                                    |? ML_POZB.prefix(POZ.ref);
                                       {? _a || ML_POZTR.prefix(OKRO_F.ref(),POZ.ref()) ?};
                                       exec('pozbmod','mlxstan',1,_a,OKRO_F.ref());
                                       POZ.next
                                    !}
                                 ?};
                                 POZ.prefix(); ML_POZB.prefix();
                                 {? AN.WAL<>FINFO.NAROD
                                 || POZ.prefix('N','N',AN.SYM,AN.WAL)
                                 || POZ.prefix('N','N',AN.SYM)
                                 ?};
                                 {? POZ.first()
                                 || {!
                                    |? ML_POZB.prefix(POZ.ref);
                                       {? _a || ML_POZTR.prefix(OKRO_F.ref(),POZ.ref()) ?};
                                       exec('pozbusu','mlxstan',,_a,OKRO_F.ref());
                                       POZ.next
                                    !}
                                 ?};
                                 AN.next
                              !}
                           ||
::                               'Jesli nie ma zapisow dla danego konta syntetycznego w AN to usuwamy wszystkie';
::                               'ML_POZTR i ML_POZB powiazane z danym kontem syntetycznym';
                              POZ.prefix('N','N',KS.SYM);
                              {? POZ.first()
                              || {!
                                 |? ML_POZB.prefix(POZ.ref);
                                    {? _a || ML_POZTR.prefix(OKRO_F.ref(),POZ.ref()) ?};
                                    exec('pozbusu','mlxstan',,_a,OKRO_F.ref());
                                    POZ.next
                                 !}
                              ?}
                           ?}
                        ?};
::                        'jesli analityka nie jest budz. sprawdzenie czy wyrozniki';
                        _dalej:=exec('czw_klsf','mlxstan',KS.ref());
                        {? _dalej
                        || echo();
                           echo('Trwa odtwarzanie pozycji struktury budżetowej - wyróżniki '+KS.SYM+' '+form(OKRO_F.NR,-2)+' '+ROK_F.NAZ);
                           POZ.prefix('T','T',KS.SYM);
                           {? POZ.first()
                           || {!
                              |? ML_POZB.prefix(POZ.ref);
                                 {? _a || ML_POZTR.prefix(OKRO_F.ref(),POZ.ref()) ?};
                                 exec('pozbmod','mlxstan',1,0,OKRO_F.ref());
                                 POZ.next
                              !}
                           ?};
                           POZ.prefix; ML_POZB.prefix;
                           POZ.prefix('N','N',KS.SYM);
                           {? POZ.first
                           || {!
                              |? ML_POZB.prefix(POZ.ref);
                                 {? _a || ML_POZTR.prefix(OKRO_F.ref(),POZ.ref()) ?};
                                 exec('pozbusu','mlxstan',,0,OKRO_F.ref());
                                 POZ.next
                              !}
                           ?}
                        ?};
                        KS.next
                     !}
                  ?}
               ?};
               OKRO_F.next()
            !}
         ?}
      ?};
      ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next
   !}
?};
echo();
AN.use(_an_name);
DOK.use(_dokname); POZ.use(_pozname);
POW.use(_powname); ML_POZB.use(_pozbnam);
ML_POZTR.use(_mlpozna);
ML_OBRTR.use(_mlobrna);
ML_BZAD.use(_mlbzadna);
ROK_F.cntx_pop(); OKRO_F.cntx_pop(); AN.cntx_pop();
DOK.cntx_pop(); POZ.cntx_pop(); POW.cntx_pop(); ML_POZB.cntx_pop();
ML_OBRTR.cntx_pop();
ML_BZAD.cntx_pop();
ML_POZTR.cntx_pop();
{? SSTALE.AR<>_ar | SSTALE.AO<>_ao
|| SSTALE.AR:=_ar; SSTALE.AO:=_ao
?}


\czk_klsf
::-------------------------------------------------------------------------------------------------------------------
::  UTW: RB [8.60]
:: OPIS: Czy budowa konta zawiera slowniki oparte o wzorce budzetowe
::   WE: _a - KS.ref()
::   WY: 0/1
::-------------------------------------------------------------------------------------------------------------------
_wyn:=0;
KS.cntx_psh();
BUD.cntx_psh;
KS.prefix();
{? KS.seek(_a)
|| BUD.index('KS');
   BUD.prefix(KS.ref);
   {? BUD.first()
   || {!
      |? {? 'CzęśćDziałRozdziałParagrafPozycja'*BUD.SLU().SLU().WZ
         || _wyn:=1
         ?};
         BUD.next&~_wyn
      !}
   ?}
?};
KS.cntx_pop();
BUD.cntx_pop;
_wyn


\czw_klsf
::-------------------------------------------------------------------------------------------------------------------
::  UTW: RB [8.60]
:: OPIS: Czy wyrozniki konta zawieraja slowniki oparte o wzorce budzetowe
::   WE: _a - KS.ref
::   WY: 0/1
::-------------------------------------------------------------------------------------------------------------------
_wyn:=0;
KS.cntx_psh();
KS_W.cntx_psh;
KS.prefix();
{? KS.seek(_a)
|| KS_W.index('LP'); KS_W.prefix(KS.ref);
   {? KS_W.first
   || {!
      |? {? 'CzęśćDziałRozdziałParagrafPozycja'*KS_W.SLU().SLU().WZ
         || _wyn:=1
         ?};
         KS_W.next
      !}
   ?}
?};
KS.cntx_pop();KS_W.cntx_pop;
_wyn


\obxxodtw
::-------------------------------------------------------------------------------------------------------------------
::  UTW: RB [x.xx]
:: OPIS: Odtwarzanie tabel obrotow struktury klasyfikacji budzetowej. Funkcja za kazdym razem kasuje wszystkie zapisy
::       z obrotow. Co w konsekwencji sprowadza sie do tego ze trzeba dawac zakres odtwarzania od Bilansu otwarcia
::       do aktualnego okresu.
::   WE: _a - czy odtwarzac obroty hierarchii slownikow
::-------------------------------------------------------------------------------------------------------------------
_ar:=SSTALE.AR; _ao:=SSTALE.AO;
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); DOK.cntx_psh(); POZ.cntx_psh();
_ind_obrtr:=ML_OBRTR.ndx_tmp('',1,'OKRO_F','NR',0);
_an_name:=AN.name(); _dokname:=DOK.name(); _pozname:=POZ.name(); _pozbnam:=ML_POZB.name();

_rok_od:=ROZNE.UT_ROKOD().KOD; _rok_do:=ROZNE.UT_ROKDO().KOD;
_okr_od:=ROZNE.UT_OKROD().NR; _okr_do:=ROZNE.UT_OKRDO().NR;

ML_POZB.cntx_psh();
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {!
    |?
      {? -ROK_F.ZAM='n'
      || AN.use('koan__'+ROK_F.KOD); AN.index('WALSYM'); AN.prefix();
         ML_OBRCZ.use('obrcz_'+ROK_F.KOD);
         ML_OBRDZ.use('obrdz_'+ROK_F.KOD);
         ML_OBRRZ.use('obrrz_'+ROK_F.KOD);
         ML_OBRPG.use('obrpg_'+ROK_F.KOD);
         ML_OBRPP.use('obrpp_'+ROK_F.KOD);
         {? _a
         || ML_OBRTR.use('mlobrt'+ROK_F.KOD);
            ML_POZTR.use('mlpozt'+ROK_F.KOD); ML_POZTR.index('OPOZHIER')
         ?};
         OKRO_F.prefix(ROK_F.ref);
         {? OKRO_F.first
         || {!
            |? {? {? ROK_F.KOD=_rok_od || OKRO_F.NR>=_okr_od  || 1 ?} &
                  {? ROK_F.KOD=_rok_do || OKRO_F.NR<=_okr_do   || 1 ?}
               || echo();
                  echo('Trwa odtwarzanie obrotów struktury budżetowej - konta '+form(OKRO_F.NR,-2)+' '+ROK_F.NAZ);
                  ML_OBRCZ.index('OOKRCZ'); ML_OBRCZ.prefix(OKRO_F.NR);
                  {? ML_OBRCZ.first || {!|? ML_OBRCZ.del !} ?};
                  ML_OBRCZ.index('OTYPCZ'); ML_OBRCZ.prefix;
                  ML_OBRDZ.index('OOKRDZ'); ML_OBRDZ.prefix(OKRO_F.NR);
                  {? ML_OBRDZ.first || {!|? ML_OBRDZ.del !} ?};
                  ML_OBRDZ.index('OTYPDZ'); ML_OBRDZ.prefix;
                  ML_OBRRZ.index('OOKRRZ'); ML_OBRRZ.prefix(OKRO_F.NR);
                  {? ML_OBRRZ.first || {!|? ML_OBRRZ.del !} ?};
                  ML_OBRRZ.index('OTYPRZ'); ML_OBRRZ.prefix;
                  ML_OBRPG.index('OOKRPG'); ML_OBRPG.prefix(OKRO_F.NR);
                  {? ML_OBRPG.first || {!|? ML_OBRPG.del !} ?};
                  ML_OBRPG.index('OTYPPG'); ML_OBRPG.prefix;
                  ML_OBRPP.index('OOKRPP'); ML_OBRPP.prefix(OKRO_F.NR);
                  {? ML_OBRPP.first || {!|? ML_OBRPP.del !} ?};
                  ML_OBRPP.index('OTYPPP'); ML_OBRPP.prefix;
                  {? _a
                  || ML_OBRTR.index(_ind_obrtr);
                     ML_OBRTR.prefix(OKRO_F.NR);
                     {? ML_OBRTR.first() || {!|? ML_OBRTR.del() !} ?};
                     ML_OBRTR.index('MLOBROTY'); ML_OBRTR.prefix
                  ?};
                  DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
                  POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2));
                  ML_POZB.use('pozb'+ROK_F.KOD+form(OKRO_F.NR,-2));
                  POZ.index('SYMKON'); POZ.prefix('T','T');
                  {? POZ.first
                  || {!
                     |? POZ.DOK();'ustawienie odpowiednich rekordow tabel powiazanych';
                        exec('add_bud_obr','mlxstan',SSTALE.WAL,OKRO_F.ref,1);
                        {? _a || exec('add_ml_gr_obr','ml_gr',SSTALE.WAL,OKRO_F.ref,1) ?};
                        POZ.next()
                     !}
                  ?}
               ?};
               OKRO_F.next()
            !}
         ?}
      ?};
      ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next()
   !}
?};
echo();
ML_OBRTR.ndx_drop(_ind_obrtr);
AN.use(_an_name); DOK.use(_dokname);POZ.use(_pozname); ML_POZB.use(_pozbnam);
ROK_F.cntx_pop(); OKRO_F.cntx_pop(); DOK.cntx_pop(); POZ.cntx_pop(); ML_POZB.cntx_pop();
{? SSTALE.AR<>_ar | SSTALE.AO<>_ao
|| SSTALE.AR:=_ar; SSTALE.AO:=_ao
?}


\roz_bo
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1010]
:: OPIS: Funkcja dla przekazanej pozycji sprawdza liczbe wyroznikow podpietych. Funkcja dziala tylko w BO
::   WE: _a - POZ.ref()
::       _b - LP slownika wyroznikow
::       _c - SLUAPPL.ref() slownika wyroznikow
::       _d - OKRO_F.ref()
::-------------------------------------------------------------------------------------------------------------------
_wyn:=0;
OKRO_F.cntx_psh();
POZ.cntx_psh();
POW.cntx_psh();
{? exec('roz_bo_chk','mlxstan',_d)
|| POW.index('POZ');
   POW.prefix(_a,_b,_c );
   {? POW.first() || _wyn:=POW.size() ?}
|| _wyn:=0
?};
OKRO_F.cntx_pop();
POZ.cntx_pop();
POW.cntx_pop();
_wyn

\roz_bo_chk
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1010]
:: OPIS: Czy obsluga wyroznikow jak dla rozdzielnikow: rok inny niz pierwszy, okres bilans otwarcia, typ dokumentu BO
::       Formula przeniesiona ze skid_wyr.fml
::   WE: _a - okres
::   WY: Czy obsluga wyroznikow jak dla rozdzielnikow? 1-tak, 0-nie
::-------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.prefix();
{? OKRO_F.seek(_a)
|| _fr:={? ROK_F.first() || ROK_F.ref() || null ?};
   SLO.cntx_psh();
   _ok:=_fr & OKRO_F.NR=0 & _fr<>OKRO_F.ROK & POZ.DOK().DOK_REJ().SLO().KOD='BO';
   SLO.cntx_pop()
?};
OKRO_F.cntx_pop(); ROK_F.cntx_pop();
_ok

\crea_tab
::-------------------------------------------------------------------------------------------------------------------
::  UTW: AK [1030]
:: OPIS: Tworzy tabele danymi dekretu.
::   WY: _tab
::-------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(3,'LP','INTEGER','Lp',
                'SLUNAZ','STRING[20]','Nazwa slownika ',
                'SLOKOD','STRING[8]','Kod slownika',
                'SLOOPIS','STRING[80]','Opis kodu w slowniku',
                'TYP','STRING[4]','Typ',
                'R','STRING[1]','Rodzaj K/W',
                'KWOTA','REAL','Kwota',
                'USE','STRING[1]','Uzyta');
_tab


\ut_klsf_ae
::---------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Po redakcji zmiennej UT_KLSF
::       Jesli UT_KLSF=0 to UT_MLGR rowniez 0
::---------------------------------------------------------------------------------------------------------------------
{? MLEX_OKN.UT_KLSF=0
|| MLEX_OKN.UT_MLGR:=0
?};
1


\ut_mlgr_be
::---------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Przed redakcja zmiennej UT_MLGR
::       Pola nie mozna redagwac jesli UT_KLSF=0
::---------------------------------------------------------------------------------------------------------------------
{? MLEX_OKN.UT_KLSF=0
|| MLEX_OKN.UT_MLGR:=0;
   0
|| 1
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 1303132a0bd2d0118cb430611616a4d33407f21f374487e01169da39c6a845328de493b928b987f563ed7ec7de8d9c4bd45a2167b226f5dbb0b0128bfb3552f07429278428d3fac3198071ad83094d190c786c0a1e7000411593203ab07dd8d6f65e79288022691bc92eb47ae08d82f862c2c71f28f11837790705841e112cca

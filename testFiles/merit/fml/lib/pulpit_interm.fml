:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: #pulpit_interm.fml
:: Utworzony: 30.06.2021 [21.37]
:: Autor: WH
::======================================================================================================================
:: Zawartosc: Formuły do obsługi pulpitu na interm
::======================================================================================================================

\panel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DSYS [21.37]
:: OPIS: Uruchamia pulpit na interm
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
exec('AreaTitle','#object');
AreaTitle.setArea('#PULPIT');
AreaTitle.setTitle();

inst_dsk('@dashboard');
__default_h:=11;
__default_w:=6;

progress(,'Trwa odświeżanie pulpitu'@,exec('nazwa','#system'),1);
exec('fill_pulpit','#b_desktop');
OBJ_P.TMP.index(OBJ_P.IND1);
exec('send_tree','pulpit');
prgs_clr();

:: inicjalizacja kontrolki pulpitu
ctr_call('!desktop',,'initDesktop');

ctr_set('!desktop',,'setTodoInfoKeys','own_0','free_0');

:: Kontrolka Sposób pracy
ctr_call('!desktop',,'addWindowControl','actionlists','@dashboard>actionlists','Sposób pracy'@,0,0,__default_w,__default_h);
ctr_set('!desktop','desktop:actionlists','initControl');


exec('todo_install','pulpit_interm');

:: Tworzenie i zasilanie kontrolki "Macrologic Anywhere"
{? exec('mobile','#system')=0
|| ctr_call('!desktop',,'addWindowControl','mobile_qrcode','@dashboard>qrcode','Macrologic Anywhere'@,__default_w*3,0,__default_w,__default_h,1,4);
   ctr_set('!desktop','desktop:mobile_qrcode','initControl')
?};
:: Kontrolka skróty
ctr_call('!desktop',,'addWindowControl','shortcuts','@dashboard>shortcuts','Skróty'@,0,__default_h,__default_w*4,__default_h-2);
ctr_set('!desktop','desktop:shortcuts','initControl');

exec('parses_install','pulpit');

exec('funpar_load','pulpit_interm');

:: inicjalizacja pozostałych elementów
ctr_call('!desktop',,'addUserWindows');


:: ustawienie Timera
_time:=exec('get','#params',1250,type_of(0),OPERATOR.USER)*1000;
ctr_call('!desktop',,'setTimer',_time,'counters','INIT_FML','COUNTERS');

:: pierwsze zasilenie liczników liczby zadań na pasku
exec('todo_titlebar_count','pulpit_interm');

:ElasticSearch
: WYSZUKIWARKA
: dodawanie opcji wyszukiwania, opcja szukania w Pulpicie dodaje się automatycznie i jest wybrana jako domyślna
: SEARCH - formuła startowa dla zakładki z wynikiem, należy podać akronim formuły z definicji
_es:=exec('elastic','elastic');
_es.info();

{? _es.ESERROR=0
||
   ctr_set('!application',,'setAppTitleBarButtonLeft','Szukaj w danych','@system|material-search','%'+app_info('app_ident'),app_info('cluster_group'),app_info('app_ident'),'SEARCH','%'+app_info('app_ident'))
?};
: /ElasticSearch

:Genius
exec('czytaj','#stalesys',,XINFO);
{? XINFO.GEN_PO = 'T' ||
	ctr_call('!application',,'setAppTitleBarButtonLeft','Asystent Genius', 'Kliknij aby rozpocząć rozmowe z asystentem Genius','@system|genius', '_genius',app_info('cluster_group'),app_info('app_ident'), 'BOT',   '%'+app_info('app_ident'))
?};
: /Genius
~~


\ctr_callback
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DSYS [17.00]
:: OPIS:
::----------------------------------------------------------------------------------------------------------------------
{? ctr_info('INIT_FML')='dsk_export'
|| exec('export_cfg','pulpit');
   return(0)
|? ctr_info('INIT_FML')='dsk_import'
|| exec('import_cfg','pulpit');
   return(0)
|? ctr_info('INIT_FML')='REPORT'
|| {? exec('is_okres','pulpit',__PARSES.getVal('OkresRok').OKRO)
   || exec('proc_exec_mod','#b_desktop',ctr_info('UID_EXE'))
   ?};
   return(0)
|? ctr_info('INIT_FML')='EVENT'
|| {? ctr_info('ACTION_TYPE')='modeler'
   || exec('proc_exec_mod','#b_desktop',ctr_info('UID_MOD'))
   || {? exec('is_okres','pulpit',__PARSES.getVal('OkresRok').OKRO)
      || exec('proc_exec_mod','#b_desktop',ctr_info('UID_EXE'))
      ?}
   ?};
   return(0)
|? ctr_info('INIT_FML')='COUNTERS'
|| exec('todo_titlebar_count','pulpit_interm');
   ~~
|? ctr_info('INIT_FML')='PANELPLUS'
|| params_set(params_get());
   exec('bridge','#desktop')
|? ctr_info('INIT_FML')='FUNPAR'
|| {? exec('is_okres','pulpit',__PARSES.getVal('OkresRok').OKRO,,,1)
   || exec('proc_exec_mod','#b_desktop',ctr_info('UID_EXE'))
   ?}
|| FUN.info('ctr_callback: '+ctr_info('INIT_FML'))
?}


\todo_install
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Dodaje na pulpit widget z listą TODO
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
::_id:='widget_todo_ex';
::exec('pulpit_add','#desktop','Przykład'@,_id,55,30,30,150,1,'org.mcl.czest.xdesk.interm.pulpit.todo.TodoControlExample','interm_todo_ex');
_id:='widget_todo';
exec('pulpit_add','#desktop','Lista zadań'@,_id,__default_w*2,__default_h,__default_w,0,1,'org.mcl.czest.xdesk.interm.pulpit.todo.TodoControl','panelplus_interm');

exec('todo_refresh','pulpit_interm');
exec('set_timer','#desktop','','desktop:widget_todo','lista@widget_todo',exec('get','#params',1250,type_of(0),OPERATOR.USER));
~~


\todo_refresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Odświeża widget TODO na pulpicie
::   WY: 0 - nie udało się odświeżyć
::       1 - odświeżono
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
::exec('refresh_element','#desktop','','desktop:widget_todo','lista@widget_todo');
_data_id:='tabs_'+$SYSLOG.tm_stamp();
::exec('param_write','#desktop','','desktop:widget_todo','widget_todo_ntc',$BI_TODO.tm_stamp(),,OPERATOR.USR_KOD);

exec('fetch_data','#desktop',_data_id,'desktop:widget_todo',exec('todo_tabs','pulpit_interm'),'LP,SYMBOL,ICON,LABEL,TRA_C1,TRA_C2,TRA_RUN,TRA_PRO,TRA_DESC,TRA_PRI,TRA_RFR,TRA_WIE,TRA_OPT,TRA_DISP,TRA_PERS','');
exec('grab_data','#desktop','','desktop:widget_todo','lista@widget_todo',_data_id);

:: zasilenie liczników liczby zadań na pasku
exec('todo_titlebar_count','pulpit_interm');
1


\todo_refresh_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Wystawia rekordy listy TODO dla kontrolki (wywoływane z java)
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_data_id:='todo_'+$SYSLOG.tm_stamp();
exec('fetch_data','#desktop',_data_id,'desktop:widget_todo',exec('todo_list','pulpit',0),'REF,DESC,PRIOR_R,PRIOR_W,TOOLTIP,MEGA_NAZ','');
exec('grab_data','#desktop','','desktop:widget_todo','lista@widget_todo',_data_id);
~~


\todo_refresh_auto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Automatyczne odświeżenie co podany interwał czasu (wywoływane z java)
::   WY: 0 - nie udało się odświeżyć
::       1 - odświeżono
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

::exec('refresh_element','#desktop','','desktop:widget_todo','lista@widget_todo');
_data_id:='auto_'+$SYSLOG.tm_stamp();
::exec('param_write','#desktop','','desktop:widget_todo','widget_todo_ntc',$BI_TODO.tm_stamp(),,OPERATOR.USR_KOD);

exec('fetch_data','#desktop',_data_id,'desktop:widget_todo',exec('todo_tabs','pulpit_interm'),'LP,SYMBOL,ICON,LABEL,TRA_C1,TRA_C2,TRA_RUN,TRA_PRO,TRA_DESC,TRA_PRI,TRA_RFR,TRA_WIE,TRA_OPT,TRA_DISP,TRA_PERS','');
exec('grab_data','#desktop','','desktop:widget_todo','lista@widget_todo',_data_id);
1


\todo_tabs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Tworzy tabelkę z zakładkami dla kontrolki i ją zasila
::   WY: tab_tmp - konfiguracja zakładek
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1
   ,'LP','INTEGER','Lp'
   ,'SYMBOL','STRING[100]','SYMBOL'
   ,'ICON','STRING[100]','Ikona'
   ,'LABEL','STRING[100]','Druga linia nazwy'
   ,'TRA_C1','STRING[100]','Nazwa kolumny z symbolem zadania'
   ,'TRA_C2','STRING[100]','Nazwa kolumny z priorytetem'
   ,'TRA_RUN','STRING[100]','Nazwa akcji RUN'
   ,'TRA_PRO','STRING[100]','Nazwa akcji PROC'
   ,'TRA_DESC','STRING[100]','Nazwa akcji DESC'
   ,'TRA_PRI','STRING[100]','Nazwa akcji PRIOR'
   ,'TRA_RFR','STRING[100]','Nazwa akcji REFRESH'
   ,'TRA_WIE','STRING[100]','Nazwa akcji WIECEJ'
   ,'TRA_OPT','STRING[100]','Nazwa akcji OPTIONS'
   ,'TRA_DISP','STRING[100]','Nazwa akcji DISPLAY'
   ,'TRA_PERS','STRING[100]','Nazwa akcji PERSONALIZE'
);

_mega_names:=exec('mega_names','#b_domain');
_mega_names_tra:=exec('mega_names_tra','#b_domain');
_sum:=exec('count_todo_group','#bi_todo',REF.FIRMA,OPERATOR.USER);
_domains:=exec('user_list_megad','#users',OPERATOR.USR_KOD,'M');

_tab_kaf:=tab_tmp(1
   ,'KAF_ALL','STRING[1]','Wartość'
   ,'KAF_PRI','STRING[1]','Wartość'
   ,'KAF_FKS','STRING[1]','Wartość'
   ,'KAF_PKD','STRING[1]','Wartość'
   ,'KAF_LMG','STRING[1]','Wartość'
   ,'KAF_CTR','STRING[1]','Wartość'
   ,'KAF_INN','STRING[1]','Wartość'
);
_tab_kaf.KAF_ALL:=exec('get','#params',1260,type_of(''),OPERATOR.USER);
_tab_kaf.KAF_PRI:=exec('get','#params',1261,type_of(''),OPERATOR.USER);
_tab_kaf.KAF_FKS:=exec('get','#params',1262,type_of(''),OPERATOR.USER);
_tab_kaf.KAF_PKD:=exec('get','#params',1263,type_of(''),OPERATOR.USER);
_tab_kaf.KAF_LMG:=exec('get','#params',1264,type_of(''),OPERATOR.USER);
_tab_kaf.KAF_CTR:=exec('get','#params',1265,type_of(''),OPERATOR.USER);
_tab_kaf.KAF_INN:=exec('get','#params',1266,type_of(''),OPERATOR.USER);

_tab_act:=tab_tmp(1
   ,'BTN_RUN' ,'STRING[1]','Wartość'
   ,'BTN_HELP','STRING[1]','Wartość'
   ,'BTN_RFR' ,'STRING[1]','Wartość'
   ,'BTN_MORE','STRING[1]','Wartość'
   ,'BTN_PRI','STRING[1]','Wartość'
   ,'BTN_PERS','STRING[1]','Wartość'
   ,'BTN_DISP','STRING[1]','Wartość'
);
_tab_act.BTN_RUN:=exec('get','#params',1270,type_of(''),OPERATOR.USER);
_tab_act.BTN_HELP:=exec('get','#params',1272,type_of(''),OPERATOR.USER);
_tab_act.BTN_MORE:=exec('get','#params',1274,type_of(''),OPERATOR.USER);
_tab_act.BTN_PRI:=exec('get','#params',1275,type_of(''),OPERATOR.USER);
_tab_act.BTN_PERS:=exec('get','#params',1276,type_of(''),OPERATOR.USER);
_tab_act.BTN_DISP:=exec('get','#params',1277,type_of(''),OPERATOR.USER);

_inne_own:=_sum.own_0-_sum.own_1-_sum.own_2-_sum.own_3-_sum.own_4;
_inne_free:=_sum.free_0-_sum.free_1-_sum.free_2-_sum.free_3-_sum.free_4;

_len:=obj_len(_mega_names);
:: Zwiększam żeby było miejsce na zakładkę Wszystkie
_len+=1;

{! _it:=1.._len
|!
   _lp:=0;
   _icon:='';
   _megad:='';
   _sum_own:=0;
   _sum_free:=0;
   _add:=0;
   {? _it>obj_len(_mega_names)
   ||
::    Wszystkie lecą z LP=0, po tym też są identyfikowane po stronie java
      _lp:=0;
      _sum_own:=_sum.own_0;
      _sum_free:=_sum.free_0;
      _megad_tra:='Zadania'@;
      _icon:='system:playlist-check';
      ~~
   || _megad:=_mega_names[_it];
      _megad_tra:=_mega_names[_it];
      _icon:='system:layers';
      _lp:=_it;
      ~~
   ?};

   {? _lp=0 & _tab_kaf.KAF_ALL='T'
   || _add:=1
   |? _megad=_mega_names.ZWS & _tab_kaf.KAF_INN='T'
   || _icon:='system:folder-expand';
      _sum_own:=_inne_own;
      _sum_free:=_inne_free;
      _add:=1;
      ~~
   |? _megad=_mega_names.FINANSE & _tab_kaf.KAF_FKS='T' & _domains*'Finanse|'>0
   || _icon:='system:money-dollar';
      _sum_own:=_sum.own_1;
      _sum_free:=_sum.free_1;
      _add:=1;
      ~~
   |? _megad=_mega_names.PERSONEL & _tab_kaf.KAF_PKD='T' & _domains*'Personel|'>0
   || _icon:='system:group-three';
      _sum_own:=_sum.own_2;
      _sum_free:=_sum.free_2;
      _add:=1;
      ~~
   |? _megad=_mega_names.LOG_PROD & _tab_kaf.KAF_LMG='T' & _domains*'Logistyka i produkcja|'>0
   || _icon:='system:factory';
      _sum_own:=_sum.own_3;
      _sum_free:=_sum.free_3;
      _add:=1;
      ~~
   |? _megad=_mega_names.ZARZ & _tab_kaf.KAF_CTR='T' & _domains*'Zarządzanie|'>0
   || _icon:='system:list-user';
      _sum_own:=_sum.own_4;
      _sum_free:=_sum.free_4;
      _add:=1;
      ~~
   ?};
::   {? _lp=0 | ((_sum_own+_sum_free)>0)
   {? _add>0
   ||
      _tab.blank();
      _tab.LP:=_lp;
      _tab.SYMBOL:=_megad_tra;
      _tab.ICON:=_icon;
      _tab.LABEL:='%1/%2'[$_sum_own,$_sum_free];
      _tab.TRA_C1:='Opis zadania'@;
      _tab.TRA_C2:='Priorytet'@;
      {? _tab_act.BTN_RUN='T'
      || _tab.TRA_RUN:='Rozpocznij'@
      ?};
      {? _tab_act.BTN_HELP='T'
      || _tab.TRA_DESC:='Opis czynności'@
      ?};
      {? _tab_act.BTN_PRI='T'
      || _tab.TRA_PRI:='Ustaw'@
      ?};
      {? _tab_act.BTN_MORE='T'
      || _tab.TRA_WIE:='Więcej...'@
      ?};
      {? _tab_act.BTN_PERS='T'
      || _tab.TRA_PERS:='Personalizuj'@
      ?};
      {? _tab_act.BTN_DISP='T'
      || _tab.TRA_DISP:='Wyświetl'@
      ?};
      _tab.TRA_PRO:='Podgląd procesu'@;
      _tab.TRA_RFR:='Odśwież'@;
      _tab.TRA_OPT:='Ustawienia'@;

      _tab.add()
   ?}
!};
_tab


\todo_run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Akcja 'Rozpocznij' w widget TODO
::   WE: [_a] - STRING - ref rekordu BI_TODO który uruchomić
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
{? _ref<>''
||
   {? (6+_ref)='WIECEJ'
   ||
::    Sztuczny rekord 'Wiecej'
      params_exec('todo_wiecej','pulpit_interm','')
   ||
      exec('FindAndGet','#table',BI_TODO,_ref,,"exec('todoRun','#bi_todo','Pulpit'@)")
   ?};
   exec('AreaTitle','#object');
   AreaTitle.setArea('#PULPIT');
   AreaTitle.setTitle();
   exec('todo_refresh','pulpit_interm');
   ~~
?};
~~


\todo_help
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Akcja 'Opis czynności' na widget TODO
::   WE: [_a] - STRING - ref rekordu BI_TODO który uruchomić
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
{? _ref<>''
|| exec('FindAndGet','#table',BI_TODO,_ref,,"exec('help','#help',BI_TODO.BI_PREL().B_PREL().B_ELE().SYMBOL)")
?};
exec('todo_refresh','pulpit_interm');
~~


\todo_priority
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Akcja Priorytet na widget TODO
::   WE: [_a] - STRING - ref rekordu BI_TODO który uruchomić
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_bi_todo:=null;
{? _ref<>''
||
   BI_TODO.cntx_psh();
   {? BI_TODO.name()='' || BI_TODO.use('bi_t____') ?};
   BI_TODO.prefix();
   {? BI_TODO.seek(_ref)
   ||
      _choice:=FUN.choice('Priorytet użytkownika dla zadania.'@,,'Pilne'@,'Zwykłe'@);
      {? _choice=1
      || exec('bi_todo_prior_set','#bi_todo',0,0)
      |? _choice=2
      || exec('bi_todo_prior_del','#bi_todo',0,0)
      ?}
   ?};
   BI_TODO.cntx_pop();
   exec('todo_refresh','pulpit_interm');
   ~~
?};
~~


\todo_personalize
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Akcja 'Personalizuj' w widget TODO
::   WE: [_a] - STRING - ref rekordu BI_TODO który uruchomić
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
{? _ref<>''
||
   {? (6+_ref)='WIECEJ'
   ||
::    Sztuczny rekord 'Wiecej'
      ~~
   ||
      exec('FindAndGet','#table',BI_TODO,_ref,,"exec('personalizuj','#bi_todo')")
   ?};
   exec('todo_refresh','pulpit_interm');
   ~~
?};
~~


\todo_proc_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Akcja Podgląd procesu na widget TODO
::   WE: [_a] - STRING - ref rekordu BI_TODO którego proces podejrzeć
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_bi_todo:=null;
{? _ref<>''
||
   BI_TODO.cntx_psh();
   {? BI_TODO.name()='' || BI_TODO.use('bi_t____') ?};
   BI_TODO.prefix();
   {? BI_TODO.seek(_ref)
   ||
::      exec('select_proc_interm','#b_design',BI_TODO.B_PROC)
      exec('select_biproc_interm','#b_design',BI_TODO.BI_PROC,BI_TODO.B_PREL)
   ?};
   BI_TODO.cntx_pop();
   exec('todo_refresh','pulpit_interm');
   ~~
?};
~~


\todo_wiecej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Przycisk 'Wiecej' w widget TODO
::   WE: [_a] - STRING - ref zaznaczonego BI_TODO
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_bi_todo:=null;
{? _ref<>''
|| _bi_todo:=exec('FindAndGet','#table',BI_TODO,_ref,,,null())
?};
_bi_todo:=exec('select_user','#bi_todo',app_info('app_user'),0,_bi_todo);
:: exec('set_value','#desktop','','desktop:widget_todo','lista@widget_todo','select='+$_bi_todo);

exec('AreaTitle','#object');
AreaTitle.setArea('#PULPIT');
AreaTitle.setTitle();
exec('todo_refresh','pulpit_interm');
~~


\todo_settings
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.37]
:: OPIS: Ustawienia widgetu TODO
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_old_val:=exec('get','#params',1250,type_of(0),OPERATOR.USER);
_domains:=exec('user_list_megad','#users',OPERATOR.USR_KOD,'M');
_tab:=tab_tmp(1
   ,'TIME','REAL','Wartość'
   ,'NTC_RFR','STRING[1]','Wartość'
   ,'KAF_ALL','STRING[1]','Wartość'
   ,'KAF_PRI','STRING[1]','Wartość'
   ,'KAF_FKS','STRING[1]','Wartość'
   ,'KAF_PKD','STRING[1]','Wartość'
   ,'KAF_LMG','STRING[1]','Wartość'
   ,'KAF_CTR','STRING[1]','Wartość'
   ,'KAF_INN','STRING[1]','Wartość'
   ,'BTN_WIE','STRING[1]','Wartość'
   ,'BTN_RUN' ,'STRING[1]','Wartość'
   ,'BTN_HELP','STRING[1]','Wartość'
   ,'BTN_RFR' ,'STRING[1]','Wartość'
   ,'BTN_MORE','STRING[1]','Wartość'
   ,'BTN_PRI','STRING[1]','Wartość'
   ,'BTN_PERS','STRING[1]','Wartość'
   ,'BTN_DISP','STRING[1]','Wartość'
   ,'MAX','INTEGER','Wartość'
);
_tab.TIME:=exec('get','#params',1250,type_of(0),OPERATOR.USER);
_tab.BTN_WIE:=exec('get','#params',1251,type_of(''),OPERATOR.USER);
_tab.NTC_RFR:=exec('get','#params',1252,type_of(''),OPERATOR.USER);
_tab.KAF_ALL:=exec('get','#params',1260,type_of(''),OPERATOR.USER);
_tab.KAF_PRI:=exec('get','#params',1261,type_of(''),OPERATOR.USER);
_tab.KAF_FKS:=exec('get','#params',1262,type_of(''),OPERATOR.USER);
_tab.KAF_PKD:=exec('get','#params',1263,type_of(''),OPERATOR.USER);
_tab.KAF_LMG:=exec('get','#params',1264,type_of(''),OPERATOR.USER);
_tab.KAF_CTR:=exec('get','#params',1265,type_of(''),OPERATOR.USER);
_tab.KAF_INN:=exec('get','#params',1266,type_of(''),OPERATOR.USER);

_tab.BTN_RUN:=exec('get','#params',1270,type_of(''),OPERATOR.USER);
_tab.BTN_HELP:=exec('get','#params',1272,type_of(''),OPERATOR.USER);
:: Przycisk odśwież zawsze ukryty bo został przeniesiony na belkę, a musi zostać
:: na pulpicie ze względu na timer
_tab.BTN_RFR:='N';
_tab.BTN_MORE:=exec('get','#params',1274,type_of(''),OPERATOR.USER);
_tab.BTN_PRI:=exec('get','#params',1275,type_of(''),OPERATOR.USER);
_tab.BTN_PERS:=exec('get','#params',1276,type_of(''),OPERATOR.USER);
_tab.BTN_DISP:=exec('get','#params',1277,type_of(''),OPERATOR.USER);
_tab.MAX:=exec('get','#params',1280,type_of(0),OPERATOR.USER);

_edit:=_tab.mk_edit(FUN.TYT,0,'#todo_widget',,,'normal');

_tab.win_esep(_edit,'Automatyczne odświeżanie');
_tab.win_efld(_edit,_tab,'TIME',,,20,0,,'Liczba sekund'@);
_tab.efld_opt(_edit, 'mark=1',,'TIME');
::_tab.win_efld(_edit,_tab,'NTC_RFR',,,20,0,,'Przez Menadżera Procesów?'@,,,'check-box','left_label=1,check_label="%1"'['tak, po zmianie stanu zadania'@],"'T'","'N'");

_tab.win_esep(_edit,'Kategorie'@);
_tab.win_efld(_edit,_tab,'KAF_ALL',,,20,0,,'Zadania'@,,,'check-box',,"'T'","'N'");
::_tab.win_efld(_edit,_tab,'KAF_PRI',,,20,0,,'Priorytety'@,,,'check-box',,"'T'","'N'");
{? _domains*'Finanse|'>0
|| _tab.win_efld(_edit,_tab,'KAF_FKS',,,20,0,,'Finanse'@,,,'check-box',,"'T'","'N'")
?};
{? _domains*'Personel|'>0
|| _tab.win_efld(_edit,_tab,'KAF_PKD',,,20,0,,'Personel'@,,,'check-box',,"'T'","'N'")
?};
{? _domains*'Logistyka i produkcja|'>0
|| _tab.win_efld(_edit,_tab,'KAF_LMG',,,20,0,,'Logistyka i produkcja'@,,,'check-box',,"'T'","'N'")
?};
{? _domains*'Zarządzanie|'>0
|| _tab.win_efld(_edit,_tab,'KAF_CTR',,,20,0,,'Zarządzanie'@,,,'check-box',,"'T'","'N'")
?};
_tab.win_efld(_edit,_tab,'KAF_INN',,,20,0,,'Inne'@,,,'check-box',,"'T'","'N'");

_tab.win_esep(_edit,'Przyciski'@);
_tab.win_efld(_edit,_tab,'BTN_RUN',,,20,0,,'Rozpocznij'@,,,'check-box',,"'T'","'N'");
_tab.win_efld(_edit,_tab,'BTN_HELP',,,20,0,,'Opis czynności'@,,,'check-box',,"'T'","'N'");
_tab.win_efld(_edit,_tab,'BTN_PRI',,,20,0,,'Ustaw'@,,,'check-box',,"'T'","'N'");
_tab.win_efld(_edit,_tab,'BTN_PERS',,,20,0,,'Personalizuj'@,,,'check-box',,"'T'","'N'");
_tab.win_efld(_edit,_tab,'BTN_DISP',,,20,0,,'Wyświetl'@,,,'check-box',,"'T'","'N'");
_tab.win_efld(_edit,_tab,'BTN_MORE',,,20,0,,'Więcej'@,,,'check-box',,"'T'","'N'");

_tab.win_esep(_edit,'Dodatkowe właściwości'@);
::_tab.win_efld(_edit,_tab,'BTN_WIE',,,20,0,,'Przycisk \'Więcej\' uruchamia nową zakładkę'@,,,'check-box',,"'T'","'N'");
_tab.win_efld(_edit,_tab,'MAX',,,20,0,,'Maksymalna liczba widocznych zadań'@);

exec('ok_esc','#window',_tab,_edit);
_tab.win_edit(_edit);

_valid:="
   _result:='';
   {? cur_tab(1,1).TIME<3
   || FUN.emsg('Liczba sekund musi być większa od dwóch.'@);
      _result:='TIME'
   ?};
   {? _result='' & cur_tab(1,1).MAX<0
   || FUN.emsg('Maksymalna liczba widocznych zadań musi być większa lub równa zero.'@);
      _result:='MAX'
   ?};
   {? _result=''
   || {? cur_tab(1,1).KAF_ALL='N' &
         cur_tab(1,1).KAF_FKS='N' &
         cur_tab(1,1).KAF_PKD='N' &
         cur_tab(1,1).KAF_LMG='N' &
         cur_tab(1,1).KAF_CTR='N' &
         cur_tab(1,1).KAF_INN='N'
      || _result:='KAF_ALL';
         FUN.emsg('Przynajmniej jedna kategoria musi być aktywna.'@)
      ?}
   ?};
   _result
";

{? _tab.edit(_valid)
|| exec('set','#params',1250,_tab.TIME,OPERATOR.USER);
   exec('set_timer','#desktop','','desktop:widget_todo','lista@widget_todo',_tab.TIME);
   exec('set','#params',1251,_tab.BTN_WIE,OPERATOR.USER);
::   exec('set','#params',1252,_tab.NTC_RFR,OPERATOR.USER);

   exec('set','#params',1260,_tab.KAF_ALL,OPERATOR.USER);
::   exec('set','#params',1261,_tab.KAF_PRI,OPERATOR.USER);

   {? _domains*'Finanse|'>0
   || exec('set','#params',1262,_tab.KAF_FKS,OPERATOR.USER)
   ?};
   {? _domains*'Personel|'>0
   || exec('set','#params',1263,_tab.KAF_PKD,OPERATOR.USER)
   ?};
   {? _domains*'Logistyka i produkcja|'>0
   || exec('set','#params',1264,_tab.KAF_LMG,OPERATOR.USER)
   ?};
   {? _domains*'Zarządzanie|'>0
   || exec('set','#params',1265,_tab.KAF_CTR,OPERATOR.USER)
   ?};
   exec('set','#params',1266,_tab.KAF_INN,OPERATOR.USER);

   exec('set','#params',1270,_tab.BTN_RUN,OPERATOR.USER);
   exec('set','#params',1272,_tab.BTN_HELP,OPERATOR.USER);
   exec('set','#params',1274,_tab.BTN_MORE,OPERATOR.USER);
   exec('set','#params',1275,_tab.BTN_PRI,OPERATOR.USER);
   exec('set','#params',1276,_tab.BTN_PERS,OPERATOR.USER);
   exec('set','#params',1277,_tab.BTN_DISP,OPERATOR.USER);
   exec('set','#params',1280,_tab.MAX,OPERATOR.USER);

::   _elements:=exec('elements_table','#desktop');
::   exec('todo_kafelki_visible','pulpit',_elements,0);
::   exec('todo_przyciski_visible','pulpit',_elements,0);
::   exec('update','#desktop',_elements,'desktop:widget_todo');

:: ustawienie Timera dla liczby zadań na pasku
   _time:=_tab.TIME*1000;
   ctr_call('!desktop',,'setTimer',_time,'counters','INIT_FML','COUNTERS');

   ~~
?};
exec('todo_refresh','pulpit_interm');
~~


\todo_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Formuła wykonywana po przywróceniu listy zadań z ukrycia
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('set_timer','#desktop','','desktop:widget_todo','lista@widget_todo',exec('get','#params',1250,type_of(0),OPERATOR.USER));
exec('todo_refresh','pulpit_interm');
~~


\todo_hide
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Formuła wykonywana po ukryciu listy zadań
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('set_timer','#desktop','','desktop:widget_todo','lista@widget_todo',0);
~~


\todo_display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Akcja 'Spacja' na liście widgetu TODO
::   WE: [_a] - STRING - ref rekordu BI_TODO który uruchomić
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
{? _ref<>'' & _ref*'WIECEJ_'=0
|| exec('FindAndGet','#table',BI_TODO,_ref,,"params_exec('bi_todo_display','display')")
?};
~~


\parses_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Akcja parametry pracy na pulpicie
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
__PARSES.setACF(0);
_edit:=__PARSES.editAll();
{? _edit>0
|| AreaTitle.setTitle();
::   exec('parses_update','pulpit');
   ~~
?};
__PARSES.setACF(1);
~~


\todo_titlebar_count
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Zasila górne cyferki na belce dotyczące listy zadań
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_app_lst:=exec('firmy_user_list','#firma');
{? ~_app_lst.first() || return(0) ?};

FIRMA.cntx_psh();
FIRMA.prefix();
{? _app_lst.first()
|| {!
   |?
      {? FIRMA.seek(_app_lst.REF)
      ||
         _sum:=exec('count_todo_group','#bi_todo',FIRMA.ref);
::       sumarycznie to-do
         ctr_set('!desktop',,'setTodoIntData',_app_lst.GRP_IDEN,_app_lst.APP_IDEN,'own_0',_sum.own_0);
         ctr_set('!desktop',,'setTodoIntData',_app_lst.GRP_IDEN,_app_lst.APP_IDEN,'free_0',_sum.free_0);
         obj_del(_sum)
      ?};
      _app_lst.next()
   !}
?};
FIRMA.cntx_pop();
~~


\funpar_load
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Ładuje kontrolkę z parametryzacją
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO,'DOKUWIKI','DOKU_INT');
{? exec('interm','#system') & XINFO.DOKU_INT<>''
|| _adr_wi:=-(form(XINFO.DOKU_INT))
|| _adr_wi:=-(form(XINFO.DOKUWIKI))
?};

ctr_call('!desktop',,'addWindowControl','actionlists2','@dashboard>actionlists','Ustawienia i parametryzacja'@,__default_w,2*__default_h-2,__default_w,__default_h,0);
ctr_set('!desktop','desktop:actionlists2','addTab', 'Ustawienia i parametryzacja'@, '@system|set-settings', 'funpar');

_tab:=exec('get_list','#funpar_wid');
{? _tab.first()
|| {!
   |? _uid:='';
      {? _tab.EXEC<>''
      || _uid:=exec('bdexec_check','#b_desktop',_tab.EXEC,3+_tab.AUID)
      ?};
      _tooltip:='';
      {? _tab.TYPE='C'
      || _tooltip:='Uruchom czynność parametryzacyjną: %1 %2'@[_tab.AUID,_tab.TEXT]
      |? _tab.TYPE='F'
      || _tooltip:='Uruchom formułę parametryzacyjną: %1'@[_tab.TEXT]
      || _tooltip:='Błąd'@
      ?};
::    adres serwera dokumentacyjnego + ścieżka
      _help:='';
      {? _tab.AUID<>''
      || _help:=exec('set_help','#help',_tab.AUID)
      ?};
      ctr_set('!desktop','desktop:actionlists2','addItemFormula','funpar'
                                                         ,_tab.TEXT
                                                         ,_tooltip
                                                         ,_help
                                                         ,_tab.ICON
                                                         ,_tab.COLOR
                                                         ,'FUNPAR'
                                                         ,_uid);
      _tab.next()
   !}
?};
ctr_set('!desktop','desktop:actionlists2','initControl',0,1);
~~


:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 0faaf98c2a872e23fb06bbd7a9c44f2b4ace8b3755f94ea795a6cf78184f9f277c794cfc94e454b1eea481aa544b1533ac596bb453379b9b08bf01e01358e502b6ca14b109ec1cb0564d5a2899fd8c39792fdc18e38b197cda151e44422e8438722bf3db765128109169f9cdf5773e82423d929ad0bbf94264fde82492858051

:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: fst_ext.fml
:: Utworzony: 29.11.2018
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Obszar FST - formuły dodatkowe
::======================================================================================================================


\be_srsr_sam_el
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Formuła przed redakcją pola SRSR.SAM_EL
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.POJAZD='T' & SRSR.SAMOCHOD='T' || 1 || 0 ?}


\ae_srdo_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Po redakcji pola SRDO.NR
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.NR<=0
|| FUN.emsg('Numer dokumentu w roku powinien być większy od zera.'@);
   return(0)
?};
_tmp:=exec('symdok','fst');
{? _tmp<>SRDO.SYMBOL & (SRDO.TYP().RODZ_NUM<>'N' | SRDO.TYP().AKT_SYM='T')
|| {? FUN.ask('Uaktualnić symbol dokumentu?'@) || SRDO.SYMBOL:=_tmp ?}
?};
1


\sum_psrdo_netto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Suma wartości z dokumentów na przełomie lat
::   WE: _a - Wskazanie na środek (SRSR.ref())
::       _b - rok podatkowy (INT)
::       _c - okres podatkowy (INT)
::       _d - tor amortyzacji ('P', 'F' lub 'D')
::       _e - środek ('N'), zestaw ('T') czy element ('E')
::   WY: Suma kwot netto dokumentów wartościowych bieżącego środka
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
SRDO.cntx_psh(); OKRO_F.cntx_psh();
SRDO.index('PODAT');
{? _>=5 & _e='E' || _srsr:=exec('get_srsr_root','fst',_a) || _srsr:=_a ?};
SRDO.prefix(_srsr,_b,'W');
{? SRDO.first()
|| {! |?
      {? ~(_>=5 & _e='E') | SRDO.SRSR_E=_a
      || {? SRDO.OKRO_F().OES=1 & SRDO.OKRO_F().RES=_b+1
         || {? _d='P'
            || {? SRDO.P='+'
               || _wy+=SRDO.WARP-SRDO.UMOP
               |? SRDO.P='-'
               || _wy+=SRDO.WARP
               ?}
            |? _d='F'
            || {? SRDO.P='+'
               || _wy+=SRDO.WARF-SRDO.UMOF
               |? SRDO.P='-'
               || _wy+=SRDO.WARF
               ?}
            |? _d='D'
            || {? SRDO.P='+'
               || _wy+=SRDO.WARD-SRDO.UMOD
               |? SRDO.P='-'
               || _wy+=SRDO.WARD
               ?}
            ?}
         ?}
      ?};
      SRDO.next
   !}
?};
SRDO.cntx_pop(); OKRO_F.cntx_pop();
_wy


\win_par_waamum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: Okno parametrów wydruku F.
::----------------------------------------------------------------------------------------------------------------------
_win:=PAR_WYDR.mk_edit('Parametry wydruku F. Wartości, amortyzacja i umorzenie środków',0,'_win_waamum',10,5);
PAR_WYDR.win_ewin(_win,PAR_WYDR,'WARTAMO','');
PAR_WYDR.win_esep(_win,'Dokumenty wartościowe uwzględniane');
PAR_WYDR.win_efld(_win,PAR_WYDR,'MO_DO',,,23,,,' ',,'Dokumenty wartościowe uwzględniane',
                  'radio-buttons',,
                  'Według miesiąca obowiązywania',"1",
                  'Według daty operacji',"2");
_ok:=PAR_WYDR.win_ebtn(_win,'text=%1,btn_label_align=center,panel=bottom,align=end'['OK'@],"'key:F2'");
PAR_WYDR.btn_eopt(_win,_ok,'tooltip='+exec('help_red_ok','#window','P'));
_an:=PAR_WYDR.win_ebtn(_win,'text=%1,btn_label_align=center,panel=bottom,align=end'['Anuluj'@],"'key:Esc'");
PAR_WYDR.btn_eopt(_win,_an,'tooltip='+exec('help_red_esc','#window','A'));
PAR_WYDR.efld_opt(_win,'editable='+{? PAR_WYDR.RODZAJ=1 || 'grayed' || '1' ?},, 'PR_GR');
PAR_WYDR.fld_fml('PR_GR','BEFORE_EDIT',"PAR_WYDR.RODZAJ=2 & PAR_WYDR.PODSUM=1");
PAR_WYDR.fld_fml('RODZAJ','AFTER_EDIT',"{? PAR_WYDR.RODZAJ=1 | PAR_WYDR.PODSUM<>1
                                        || PAR_WYDR.PR_GR:=0;
                                           cur_tab.efld_opt(cur_win,'editable=grayed',,'PR_GR')
                                        || cur_tab.efld_opt(cur_win,'editable=1',,'PR_GR')
                                        ?}; win_disp;
                                        1 ");
PAR_WYDR.fld_fml('PODSUM','AFTER_EDIT',"{? PAR_WYDR.RODZAJ=1 | PAR_WYDR.PODSUM<>1
                                        || PAR_WYDR.PR_GR:=0;
                                           cur_tab.efld_opt(cur_win,'editable=grayed',,'PR_GR')
                                        || cur_tab.efld_opt(cur_win,'editable=1',,'PR_GR')
                                        ?}; win_disp;
                                        1 ");
PAR_WYDR.win_edit(_win)


\prcnaz_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Wybór pracownika na bazie stanowiska osoby
::----------------------------------------------------------------------------------------------------------------------
_fld:=fld();
_res:=null;
:: Brana poprzednia zawartość REF
{? cur_tab(1,1)=SRSR
|| _ref:=cur_tab(1,1).P
|? cur_tab(1,1)=SRDO
|| _ref:=cur_tab(1,1).SRSR().P
|? cur_tab(1,1)=SRXD
|| _ref:=cur_tab(1,1).PRC_N
?};

_prefix:=_=2;
_tcid:=app_info('web_tcid');
_mbid:=app_info('web_mbid');
_sesid:=app_info('web_sesid');
_tabid:=app_info('web_tabid');

_tym:=TAB_WT.ndx_tmp('',1,'TCID',,0,'TABID',,0,'TYP',,0,'STR04',,0);
TAB_WT.index(_tym); TAB_WT.prefix(_tcid,_tabid,5);

do();
{? TAB_WT.first()
|| {!
   |? _delr:=TAB_WT.del(,1);
      {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
   !}
?};

_data:=date();
exec('__F_ZATR','object');
P.cntx_psh();
P.use('pracowni');
P.index('PRACONAZ');
{? _prefix
|| P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,_b,_a,)
|| P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P,)
?};

H.cntx_psh();
H.use('_hist');
H.index('_HISTKOD');
OSOBA.cntx_psh();
OSOBA.index('OSOBA'); OSOBA.prefix();
{? P.first()
|| {!
   |? _ok:=0;
      H.prefix(P.ref(),'Z');
      {? H.first()
      || {!
         |? {? _data>=H.OD & (_data<=H.DO | H.DO=date(0,0,0))
            || _ok:=1;
               TAB_WT.blank(1);
               TAB_WT.SESID:=_sesid;
               TAB_WT.MBID:=_mbid;
               TAB_WT.TCID:=_tcid;
               TAB_WT.TABID:=_tabid;
               TAB_WT.TYP:=5;
               TAB_WT.STR01:=form(P.T+
                                  {? H.WYDZIAL || ' - '+H.WYDZIAL().SYMBOL || ''?}+
                                  {? H.ST ||  ' - '+H.ST().ST || ''?}
                                 );
               TAB_WT.STR02:=$P.OSOBA().IMEX;
               TAB_WT.STR03:=P.OSOBA().PIERWSZE;
               TAB_WT.STR04:=P.OSOBA().NAZWISKO;
               TAB_WT.STR05:=$P.OSOBA().ref();
               TAB_WT.STR06:=$P.ref();
               TAB_WT.add()
            ?};
            ~_ok & H.next()
         !}
      ?};
      P.next()
   !}
?};
OSOBA.cntx_pop(); P.cntx_pop(); H.cntx_pop();
end();

_wynik:=0;
{? _ref<>null()
|| OSOBA.cntx_psh();
   OSOBA.index('OSOBA'); OSOBA.prefix();
   {? OSOBA.first()
   || {!
      |? OSOBA.cntx_psh();
         {? -(OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO)=-(exec('FindAndGet','#table',P,_ref,,"P.OSOBA().PIERWSZE+' '+ P.OSOBA().NAZWISKO",''))
         || _wynik:=TAB_WT.find_tab(0,'STR04',,'like',OSOBA.NAZWISKO,'STR03',,'like',OSOBA.PIERWSZE)
         || TAB_WT.first()
         ?};
         OSOBA.cntx_pop();
         OSOBA.next() & _wynik<>1
      !}
   ?};
   OSOBA.cntx_pop()
|| TAB_WT.first()
?};
TAB_WT.ndx_drop(_tym);

{? app_info('web_sesid')<>''
|| cur_tab(1,1).web_cntx_save(1);
   TAB_WT.web_select('WER13',,1)
|| TAB_WT.win_sel('WER12');
   {? TAB_WT.select(,1)
   || P.cntx_psh();
      P.index('OSOBA'); P.prefix();
      {? P.seek(TAB_WT.STR06)
      || _res:=P.ref()
      ?};
      P.cntx_pop();
      REF.P:=_res;
      REF.OSOBA:=REF.P().OSOBA;
      {? cur_tab(1,1)=SRDO
      || SRDO.OSOBA:=REF.P().OSOBA;
         SRDO.PRC:=REF.P;
         EDIT_ES.STNAZ:=REF.P().ST().ST;
         EDIT_ES.WYDZNAZ:=REF.P().WYDZIAL().SYMBOL
      |? cur_tab(1,1)=SRSR
      || SRSR.OSOBA:=REF.P().OSOBA;
         SRSR.P:=REF.P;
         SRDO_POM.PSTN:=REF.P().ST().ST;
         SRDO_POM.PWYD:=REF.P().WYDZIAL().SYMBOL
      |? cur_tab(1,1)=SRXD
      || SRXD.OSOBA_N:=REF.P().OSOBA;
         SRXD.PRC_N:=REF.P;
         EDIT_ES.PRCNAZ_N:=SRXD.PRC_N().OSOBA().NAZWISKO;
         EDIT_ES.STNAZ:=SRXD.PRC_N().ST().ST;
         EDIT_ES.WYDZNAZ:=SRXD.PRC_N().WYDZIAL().SYMBOL
      ?};
      fld(REF.OSOBA().NAZWISKO);
      win_disp()
   || {? cur_tab(1,1)=SRSR
      || SRSR.P:=_ref;
         SRSR.P()
      |? cur_tab(1,1)=SRDO
      || SRDO.SRSR().P:=_ref;
         SRDO.SRSR().P()
      |? cur_tab(1,1)=SRXD
      || SRXD.PRC_N:=_ref;
         SRXD.PRC_N()
      ?};
      fld(_fld)
   ?}
?};
fld()


\trig_b_srsr_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Trigger przed SRSR.put() - tylko dla rzeczywistych środków (nie w planach amortyzacji)
::  OLD: \trig_b_srsr_put/fst.fml
::----------------------------------------------------------------------------------------------------------------------
{? (5+SRSR.name())+1='r'
|| OKRO_F.cntx_psh();
   SRSR.ROK:=SRSR.OKRO_F().RES;
   SRSR.OKRES:=SRSR.OKRO_F().OES;
   OKRO_F.cntx_pop()
?};
:: Jeśli środek jest benefitem
TAM_DOD.cntx_psh();
TAM_DOD.index('TAM_DOD');
TAM_DOD.prefix(exec('firma','ustawienia'),SRSR.GR().ref());
{? TAM_DOD.first()
|| _bnftt:=TAM_DOD.BNFTT
|| _bnftt:=null()
?};
TAM_DOD.cntx_pop();

{? _bnftt=null()
|| SRSR.BENEFIT:='N'
|| SRSR.BENEFIT:='T'
?};
1


\trig_a_srsr_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Trigger po SRSR.put()
::  OLD: \trig_a_srsr_put/fst.fml
::----------------------------------------------------------------------------------------------------------------------
:: Tworzenie/aktualizacja BNFTP tylko gdy wydanie/zwrot dotyczy benefitu

{? SRSR.BENEFIT='T'
|| TAM_DOD.cntx_psh();
   TAM_DOD.index('TAM_DOD');
   TAM_DOD.prefix(exec('firma','ustawienia'),SRSR.GR().ref());
   {? TAM_DOD.first()
   || _bnftt:=TAM_DOD.BNFTT
   || _bnftt:=null()
   ?};
   TAM_DOD.cntx_pop();

   BNFTP.cntx_psh();
   BNFTP.index('OWNER');
   {? SRSR.P<>null
   || BNFTP.prefix(exec('FindAndGet','#table',SRSR,SRSR.ref(),SRSR.name(),"uidref",''),);
      {? BNFTP.first()
         || {? BNFTP.P <> SRSR.P
            || BNFTP.del();
               BNFTP.prefix();
               BNFTP.blank();
               BNFTP.P:=SRSR.P;
               BNFTP.BNFTT:=_bnftt;
               BNFTP.OD:=SRSR.DE;
               BNFTP.DO:=date(0,0,0);
               BNFTP.NOMINAL:=0;
               BNFTP.INFO:='%1 — %2'[SRSR.NST,SRSR.NRI];
               BNFTP.OWNER:=SRSR.uidref();
               BNFTP.add()
            || BNFTP.OD:=SRSR.DE;
               BNFTP.put()
            ?}
         || BNFTP.prefix();
            BNFTP.blank();
            BNFTP.P:=SRSR.P;
            BNFTP.BNFTT:=_bnftt;
            BNFTP.OD:=SRSR.DE;
            BNFTP.DO:=date(0,0,0);
            BNFTP.NOMINAL:=0;
            BNFTP.INFO:='%1 — %2'[SRSR.NST,SRSR.NRI];
            BNFTP.OWNER:=SRSR.uidref();
            BNFTP.add()
      ?}
   || BNFTP.cntx_psh();
      BNFTP.index('OWNER');
      BNFTP.prefix(exec('FindAndGet','#table',SRSR,SRSR.ref(),SRSR.name(),"uidref",''),);
      {? BNFTP.first()
      || BNFTP.del()
      ?};
      BNFTP.cntx_pop()
   ?};
   BNFTP.cntx_pop()
|| BNFTP.cntx_psh();
   BNFTP.index('OWNER');
   BNFTP.prefix();
   BNFTP.prefix(SRSR.uidref(),);
   {? BNFTP.first()
   || {! |?
      {? BNFTP.P=SRSR.P
      || BNFTP.del()
      ?};
      BNFTP.next()
      !}
   ?};
   BNFTP.cntx_pop()
?};
~~


\trig_b_srsr_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Trigger przed SRSR.add()
::  OLD: \trig_b_srsr_add/fst.fml
::----------------------------------------------------------------------------------------------------------------------
{? (5+SRSR.name())+1='r'
|| OKRO_F.cntx_psh();
   SRSR.ROK:=SRSR.OKRO_F().RES;
   SRSR.OKRES:=SRSR.OKRO_F().OES;
   OKRO_F.cntx_pop()
?};
:: Jeśli środek jest benefitem
TAM_DOD.cntx_psh();
TAM_DOD.index('TAM_DOD');
TAM_DOD.prefix(exec('firma','ustawienia'),SRSR.GR().ref());
{? TAM_DOD.first()
|| _bnftt:=TAM_DOD.BNFTT
|| _bnftt:=null()
?};
TAM_DOD.cntx_pop();

{? _bnftt=null()
|| SRSR.BENEFIT:='N'
|| SRSR.BENEFIT:='T'
?};
1


\trig_b_srsr_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Trigger przed SRSR.del()
::  OLD: \trig_b_srsr_del/fst.fml
::----------------------------------------------------------------------------------------------------------------------
BNFTP.cntx_psh();
BNFTP.index('OWNER');
BNFTP.prefix();
BNFTP.prefix(SRSR.uidref(),);
{? BNFTP.first() || {! |? BNFTP.del() !} ?};
BNFTP.cntx_pop();
1


\trig_a_srsr_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Trigger po SRSR.add()
::  OLD: \trig_a_srsr_add/fst.fml
::----------------------------------------------------------------------------------------------------------------------
:: Tworzenie/aktualizacja BNFTP tylko gdy wydanie/zwrot dotyczy benefitu

{? SRSR.BENEFIT='T' & SRSR.P<>null
|| TAM_DOD.cntx_psh();
   TAM_DOD.index('TAM_DOD');
   TAM_DOD.prefix(exec('firma','ustawienia'),SRSR.GR().ref());
   {? TAM_DOD.first()
   || _bnftt:=TAM_DOD.BNFTT
   || _bnftt:=null()
   ?};
   TAM_DOD.cntx_pop();

   BNFTP.cntx_psh();
   BNFTP.index('OWNER');
   BNFTP.prefix();
   BNFTP.blank();
   BNFTP.P:=SRSR.P;
   BNFTP.BNFTT:=_bnftt;
   BNFTP.OD:=SRSR.DE;
   BNFTP.DO:=date(0,0,0);
   BNFTP.NOMINAL:=0;
   BNFTP.INFO:='%1 — %2'[SRSR.NST,SRSR.NRI];
   BNFTP.OWNER:=SRSR.uidref();
   BNFTP.add();
   BNFTP.cntx_pop()
?};
~~


\trig_b_srdo_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Trigger przed SRDO.put()
::  OLD: \trig_b_srdo_put/fst.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
{? SRDO.OKRO_F<>null & ~(SRDO.DW<SRDO.OKRO_F().ROK().POCZ_ROK)
|| SRDO.ROK:=SRDO.OKRO_F().RES;
   SRDO.OKRES:=SRDO.OKRO_F().OES
?};
OKRO_F.cntx_pop();
1


\trig_b_srdo_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Trigger przed SRDO.del()
::  OLD: \trig_b_srdo_del/fst.fml
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.TYP().TYP='PU' | SRDT.TYP='LT' | SRDT.TYP='MT'
|| BNFTP.cntx_psh();
   BNFTP.index('OWNER');
   BNFTP.prefix();
   BNFTP.prefix(SRDO.SRSR().uidref(),);
   {? BNFTP.first()
   || {! |?
      {? BNFTP.P=SRDO.PRC
      || BNFTP.del()
      ?};
      BNFTP.next()
      !}
   ?};
   BNFTP.cntx_pop()
?};
1


\trig_a_srdo_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Trigger po SRDO.del()
::----------------------------------------------------------------------------------------------------------------------
SRDO.cntx_psh();
SRDO.index('SRODZAJ');
SRDO.prefix(SRSR.ref(),EDIT_ES.RODZ);
{? SRDO.size()>0
|| {? SRDO.last()
   || _prc_last:=SRDO.PRC
   ?}
|| _prc_last:=SRDO.SRSR().P
?};
SRDO.cntx_pop();

{? SRDO.TYP().TYP='PU' | SRDT.TYP='LT' | SRDT.TYP='MT'
|| BNFTP.cntx_psh();
   BNFTP.index('OWNER');
   BNFTP.prefix();
   BNFTP.prefix(SRDO.SRSR().uidref(),);
   {? BNFTP.first()
   || {! |?
      {? BNFTP.P=_prc_last
      || BNFTP.DO:=date(0,0,0);
         BNFTP.put()
      ?};
      BNFTP.next()
      !}
   ?};
   BNFTP.cntx_pop()
?};
~~


\trig_b_srdo_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Trigger przed SRDO.add()
::  OLD: \trig_b_srdo_add/fst.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
{? SRDO.OKRO_F<>null & ~(SRDO.DW<SRDO.OKRO_F().ROK().POCZ_ROK)
|| SRDO.ROK:=SRDO.OKRO_F().RES;
   SRDO.OKRES:=SRDO.OKRO_F().OES
?};
OKRO_F.cntx_pop();
1


\trig_a_srdo_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Trigger po SRDO.add()
::----------------------------------------------------------------------------------------------------------------------
:: Tworzenie/aktualizacja BNFTP tylko gdy wydanie/zwrot dotyczy benefitu
SRSR.cntx_psh();
{? SRDO.TYP().TYP='PU' | SRDT.TYP='LT' | SRDT.TYP='MT'
|| {? SRDO.SRSR().BENEFIT='T'
   || TAM_DOD.cntx_psh();
      TAM_DOD.index('TAM_DOD');
      TAM_DOD.prefix(exec('firma','ustawienia'),SRDO.SRSR().GR().ref());
      {? TAM_DOD.first()
      || _bnftt:=TAM_DOD.BNFTT
      || _bnftt:=null()
      ?};
      TAM_DOD.cntx_pop();

      BNFTP.cntx_psh();
      BNFTP.index('OWNER');
      {? SRDO.PRC<>null
      || BNFTP.prefix(exec('FindAndGet','#table',SRSR,SRDO.SRSR().ref(),SRSR.name(),"uidref",''),);
         {? BNFTP.first()
         || {! |?
               {? BNFTP.P=SRDO_POM.PRC
               || BNFTP.DO:={? date(SRDO.DO~1,SRDO.DO~2,0) <= BNFTP.OD || BNFTP.OD || date(SRDO.DO~1,SRDO.DO~2,0) ?};
                  _tmp:=BNFTP.DO;
                  BNFTP.put()
               ?};
            BNFTP.next()
            !};
            BNFTP.cntx_psh();
            BNFTP.prefix();
            BNFTP.blank();
            BNFTP.P:=SRDO.PRC;
            BNFTP.BNFTT:=_bnftt;
            BNFTP.OD:={? date(SRDO.DO~1,SRDO.DO~2,0)+1 <= _tmp || _tmp+1 || date(SRDO.DO~1,SRDO.DO~2,0)+1 ?};
            BNFTP.DO:=date(0,0,0);
            BNFTP.NOMINAL:=0;
            BNFTP.INFO:='%1 — %2'[SRDO.SRSR().NST,SRDO.SRSR().NRI];
            BNFTP.OWNER:=SRDO.SRSR().uidref();
            BNFTP.add();
            BNFTP.cntx_pop()
         || BNFTP.cntx_psh();
            BNFTP.prefix();
            BNFTP.blank();
            BNFTP.P:=SRDO.PRC;
            BNFTP.BNFTT:=_bnftt;
            BNFTP.OD:=SRDO.DO;
            BNFTP.DO:=date(0,0,0);
            BNFTP.NOMINAL:=0;
            BNFTP.INFO:='%1 — %2'[SRDO.SRSR().NST,SRDO.SRSR().NRI];
            BNFTP.OWNER:=SRDO.SRSR().uidref();
            BNFTP.add();
            BNFTP.cntx_pop()
         ?}
      || BNFTP.prefix(exec('FindAndGet','#table',SRSR,SRDO.SRSR().ref(),SRSR.name(),"uidref",''),);
         {? BNFTP.first()
         || {! |?
               {? BNFTP.DO=date(0,0,0)
               || BNFTP.DO:=SRDO.DO;
                  BNFTP.put()
               ?};
               BNFTP.next()
            !}
         ?}
      ?};
      BNFTP.cntx_pop()
   ?}
?};
SRSR.cntx_pop();
~~


\bnftp_szczegoly
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [20.42]
:: OPIS: Obsługa akcji "Szczegóły - przed" w oknie WER tabeli BNFTP - prezentacja informacji źródłowej.
::       Akcja dla wskazanego rekordu tabeli SRSR
::   WE: _a - SRSR.uidref()
::----------------------------------------------------------------------------------------------------------------------
SRSR.cntx_psh();
_name:=ref_name(_a);
SRSR.use(_name);
{? SRSR.seek(_a)
|| SRSR.memo_get(,'OPIS',0);
   EDIT_ES.JORG:=SRSR.JORG().SYMBOL;
   SRSR.win_edit('RED_U');
   SRSR.display()
?};
SRSR.cntx_pop();
~~


\ae_edit_es_prcnaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła po redakcji pola EDIT_ES.PRCNAZ
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,1)=SRDO & EDIT_ES.PRCNAZ=''
|| P.ST:=null();
   P.WYDZIAL:=null();
   SRDO.OSOBA:=null();
   SRDO.PRC:=null();
   EDIT_ES.STNAZ:='';
   EDIT_ES.WYDZNAZ:=''
|? cur_tab(1,1)=SRSR & cur_win()='RED_T' & EDIT_ES.PRCNAZ=''
|| SRSR.P:=null();
   SRSR.OSOBA:=null();
   SRDO_POM.PSTN:='';
   SRDO_POM.PWYD:=''
|? cur_tab(1,1)=SRXD & EDIT_ES.PRCNAZ_N=''
|| SRXD.OSOBA_N:=null();
   SRXD.PRC_N:=null();
   EDIT_ES.STNAZ:='';
   EDIT_ES.WYDZNAZ:=''
?};
1


\be_srdo_do
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.42]
:: OPIS: Formuła przed redakcją pola SRDO.DO
::----------------------------------------------------------------------------------------------------------------------
{? exec('not_plan','fst',SRDO.name())
|| _ref:=exec('find_okro_f','fst',SRDO.DO);
   EDIT_ES.OKRO_F:=exec('find_okro_f','fst',SRDO.DO);
   _ref_n:=EDIT_ES.OKRO_F:=exec('find_next_okro','fst',EDIT_ES.OKRO_F().ROK,EDIT_ES.OKRO_F().NR);
   {? _ref
   || {? SRDO.TYP().RODZ='I' & SRDO.TYP().SEZONOWY='T' & (SRDO.OKRO_F=null | (_ref<>null & SRDO.OKRO_F<>_ref))
      || SRDO.OKRO_F:=_ref;
         SRDO.ROK_F:=SRDO.OKRO_F().ROK
      |? SRDO.OKRO_F=null() | ((_ref<>null & SRDO.OKRO_F<>_ref) & (_ref_n<>null & SRDO.OKRO_F<>_ref_n))
      || {? _ref<>null || SRDO.OKRO_F:=_ref_n || SRDO.OKRO_F:=_ref ?};
         SRDO.ROK_F:=SRDO.OKRO_F().ROK
      ?}
   || FUN.emsg('Nie znaleziono aktywnego okresu obrachunkowego w obszarze FST dla podanej daty.'@);
      _wy:=0
   ?}
|| SRDO.ROK:=SRDO.DO~1;
   SRDO.OKRES:=SRDO.DO~2;
:: dla dokumentów w planach uaktualnienie symbolu
   _sym:=exec('symdok','fst');
   {? _sym<>SRDO.SYMBOL || SRDO.SYMBOL:=_sym ?}
?}


\wybierz_pracownika
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Wybór pracownika dla środków trwałych - uwzględnia uprawnienia do danych
::       (formy zatrudnienia, struktura organizacyjna)
::   WE: _a - możliwość wielowyboru (1 / 0)
::       [_b] - zakres pracowników - zatrudnieni (T), niezatrudnieni (N), domyślnie wszyscy (W)
::       [_c] - zakres jednostek organizacyjnych - wybrane (W), domyślnie wszystkie (A)
::----------------------------------------------------------------------------------------------------------------------
_wielu:=_a;
{? var_pres('_b')=type_of('') || _view:=_b || _view:='W' ?};
{? var_pres('_c')=type_of('') || _jorg:=_c || _jorg:='A' ?};

_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='FST';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
{? _jorg='A'
|| UD_FIR.cntx_psh();
   UD_FIR.index('UD_SKL');
   UD_FIR.prefix(REF.FIRMA);
   {? UD_FIR.first()
   || _args.UD_SKL:=UD_FIR.UD_SKL().SYMBOL
   || _args.UD_SKL:=''
   ?};
   UD_FIR.cntx_pop()
|| {? ST.WYD=null()
   || _ret:=obj_new('STATUS');
      _ret.STATUS:='Należy wybrać wydział w parametrach pracy albo zmienić zakres pracowników na "wszystkie jednostki".'@;
      return(_ret)
   || _args.UD_SKL:=ST.WYD().SYMBOL
   ?}
?};
_args.VIEW:=_view;
_args.HDR_SEL:={? _wielu || 'Wybór pracowników'@ || 'Wybór pracownika'@ ?};
_args.F_ZATR:=exec('kody','f_zatr');
_args.WIELU:=_wielu;
_args.POMIN_KOLUMNY:='IP, PESEL, DOWOD, PASZPORT';
_args.GRP_IDENT:='pracnaz_sel';
_args.GRP_MODE:='normal';
_ret:=exec('wybierz','pracownik',_args);
_ret


\bl_benefit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Formuła na wartośc początkową SRSR.BENEFIT
::----------------------------------------------------------------------------------------------------------------------
TAM_DOD.cntx_psh();
TAM_DOD.index('TAM_DOD');
TAM_DOD.prefix(exec('firma','ustawienia'),SRSR.GR().ref());
_ret:={? TAM_DOD.first() || 'T' || 'N' ?};
TAM_DOD.cntx_pop();
_ret


\be_benefit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Formuła przed redakcją EDIT_ES.BENEFIT
::----------------------------------------------------------------------------------------------------------------------
0


\ae_benefit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Formuła po redakcji EDIT_ES.BENEFIT
::----------------------------------------------------------------------------------------------------------------------
''


\add_dok_el
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła umożliwiająca dodanie dokumentu zmiany metody amortyzacji po dodaniu elementu do zestawu
::   WE: _a - ref SRST
::----------------------------------------------------------------------------------------------------------------------
_tmp:=EDIT_ES.RODZ;
EDIT_ES.RODZ:='M';
{? _>0 & _a<>null
|| SRDO.cntx_psh(); SRDO.prefix();
   SRST.cntx_psh(); SRST.prefix();
   SRSR.cntx_psh(); SRSR.prefix();
   SRDT.cntx_psh(); SRDT.index('TYP'); SRDT.prefix('MA');
   _ref:=null;
   {? SRST.seek(_a) & SRSR.seek(SRST.SRSR) & SRDT.first()
   || _ref:=SRDT.ref();
      _srsr:=SRST.ref()
   ?};
   {? _ref<>null
   || SRDO.blank();
      SRDO_POM.blank();
      SRDO.TYP:=_ref;
      SRDO.ODD_DOK:=SRST.ODD;
      SRDO.NR:=exec('bl_srdo_nr','fst');
      SRDO.SYMBOL:=exec('symdok','fst');
:: stan z bieżącego miesiąca
      {? SRDO.TYP().MP='T' || SRDO_POM.MP:=SRDO.MP:=SRST.MP ?};
      {? SRDO.TYP().STAP='T' || SRDO_POM.STAP:=SRDO.STAP:=SRST.STAP ?};
      {? SRDO.TYP().WKP='T' || SRDO_POM.WKP:=SRDO.WKP:=SRST.WKP ?};
      {? SRDO.TYP().MP='T' || SRDO_POM.DPLP:=SRDO.DPLP:=SRST.DPLP ?};

      {? SRDO.TYP().MF='T' || SRDO_POM.MB:=SRDO.MF:=SRST.MF ?};
      {? SRDO.TYP().STAF='T' || SRDO_POM.STAB:=SRDO.STAF:=SRST.STAF ?};
      {? SRDO.TYP().WKF='T' || SRDO_POM.WKF:=SRDO.WKF:=SRST.WKF ?};
      {? SRDO.TYP().MF='T' || SRDO_POM.DPLF:=SRDO.DPLF:=SRST.DPLF ?};

      {? FINFO.TOR_D='T'
      || {? SRDO.TYP().MD='T' || SRDO_POM.MD:=SRDO.MD:=SRST.MD ?};
         {? SRDO.TYP().STAD='T' || SRDO_POM.STAD:=SRDO.STAD:=SRST.STAD ?};
         {? SRDO.TYP().WKD='T' || SRDO_POM.WKD:=SRDO.WKD:=SRST.WKD ?};
         {? SRDO.TYP().MD='T' || SRDO_POM.DPLD:=SRDO.DPLD:=SRST.DPLD ?}
      ?};

:: jeżeli środek niskocenny ma metodę amortyzacji nieodpowiednią (degresywną lub naturalną)
:: np. po zmianie rodzaju środka to system zeruje przypisanie metody.
      {? SRST.R='N' & 1+SRDO.MP().T='D'
      || SRDO.MP:=null
      ?};
      {? SRST.R='N' & (1+SRDO.MF().T='D' | 1+SRDO.MF().T='N')
      || SRDO.MF:=null
      ?};
      {? FINFO.TOR_D='T'
      || {? SRST.R='N' & 1+SRDO.MD().T='D'
         || SRDO.MD:=null
         ?}
      ?};
      SRDO.win_edit('RED_M');
      {? EDIT_ES.R='N' || EDIT_ES.NISKO:='T' || EDIT_ES.NISKO:='' ?};
      exec('set_field_drdm','fst');
      {? SRDO.edit("exec('chk_dok','!fst_ewi_drdm',0)")
      || exec('mod_rec','fst');
         SRSR.r_unlock();
         {? SRDO.add()
         || SRD.updateRecState(SRDO.OKRO_F);
:: jeżeli grupa kapitałowa i dotyczy środka nabytego to kopia dokumentu dla środka pierwotnego
            {? FINFO.SPR_GRP='T' & SRDO.SRSR().ORG_REF<>''
            || {? ~exec('copy_org','fst',SRDO.ref(),'M')
               || FUN.emsg('Nie udało się odświeżyć danych w firmie pierwotnej bieżącego środka.'@)
               ?}
            ?};
:: jeżeli zestaw to odświeżenie danych elementów
            {? SRD.isSet() || SRD.updateElements() ?}
         ?}
      || SRSR.r_unlock()
      ?}
   ?};
   SRST.cntx_pop();
   SRSR.cntx_pop();
   SRDO.cntx_pop()
?};
EDIT_ES.RODZ:=_tmp


\f3_srdo_sta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Kalkulator stawki amortyzacji
:: _a - 'F'-bilansowa, 'P'-podatkowa, 'D'-dodatkowa
::----------------------------------------------------------------------------------------------------------------------
undefine();
define('LM',24,'Liczba miesięcy'@,'Liczba miesięcy do wyliczenia stawki'@,10,10,0);
_ok:=def_btn('text='+'&OK'@+', btn_label_align=center',"'key:F2'");
_anuluj:=def_btn('text='+'&Anuluj'@+', btn_label_align=center',"'key:Esc'");
def_bopt(_ok,'tooltip='+exec('help_red_ok','#window','P'));
def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
{? _a='F'
|| _wpw:=$'SRST.WARF'; _wnw:=$'SRST.NETF'
|? _a='P'
|| _wpw:=$'SRST.WARP'; _wnw:=$'SRST.NETP'
|| _wpw:=$'SRST.WARD'; _wnw:=$'SRST.NETD'
?};
_okrof:=SRDO.OKRO_F;
{? _okrof=null
|| FUN.info('Nie wybrano okresu obowiązywania dokumentu.\nPodpowiedź dla stawki nie jest dostępna.'@);
   return(~~)
?};
OKRO_F.cntx_psh();
SRST.cntx_psh(); SRST.index('SROD');
_wp:=_wn:=0;
_srdo_okr:=exec('find_prev_okro','fst',SRDO.OKRO_F().ROK,SRDO.OKRO_F().NR,1);
OKRO_F.prefix();
{? OKRO_F.seek(_srdo_okr)
|| SRST.prefix(SRSR.ref(),OKRO_F.ROK,OKRO_F.ref());
   {? SRST.first()
   || {? SRST.NAL='N'
      || FUN.info('Środek nie posiada naliczonej amortyzacji we wcześniejszych okresach\n'@+
                  'niż wskazany okres obowiązywania dokumentu.\nPodpowiedź nie jest dostępna.\n'@+
                  'Stawkę należy uzupełnić ręcznie'@);
         SRST.cntx_pop();
         OKRO_F.cntx_pop();
         return(~~)
      ?}
   ?}
?};
{? OKRO_F.seek(_okrof)
|| SRST.prefix(SRSR.ref(),OKRO_F.ROK,OKRO_F.ref());
   {? SRST.first()
   || _wp:=_wpw();
      _wn:=_wnw()
   ?}
?};
{? def_edit("{? DEFINE.LM<12 | DEFINE.LM>1200
             || FUN.emsg('Nieprawidłowa wartość. Kalkulator obsługuje zakres 12-1200.'@); 0
             || 1
             ?}",
             '|--|'+'Kalkulator stawki amortyzacji liniowej'@)
|| {? DEFINE.LM>0 || fld():=((100*_wn)/(_wp*DEFINE.LM/12))$2 ?}
?};
SRST.cntx_pop();
OKRO_F.cntx_pop();
~~


\bed_jm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem SRSR.JM
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.win_edit('?')<>''
|| {? 1+SRSR.MF().T='N'
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'JM')
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'JM')
   ?}
?}; 1


\bed_oke
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem SRSR.OKE
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.win_edit('?')<>''
|| {? 1+SRSR.MF().T='N'
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'OKE')
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'OKE')
   ?}
?}; 1


\bed_max
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem SRSR.MAX
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.win_edit('?')<>''
|| {? 1+SRSR.MF().T='N'
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'MAX')
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'MAX')
   ?}
?}; 1


\bed_staf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem SRSR.STAF
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.win_edit('?')<>''
|| {? (1+SRSR.MF().T<>'N' & SRSR.MF().K<>'UJB') | SRSR.MF=null
   || SRSR.efld_opt(SRSR.win_edit('?'),'editable=1',SRSR,'STAF')
   || SRSR.efld_opt(SRSR.win_edit('?'),'editable=grayed',SRSR,'STAF')
   ?}
?};1


\bed_wkf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem SRSR.WKF
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.win_edit('?')<>''
|| {? SRSR.MF=null | 1+SRSR.MF().T='D' | 1+SRSR.MF().T='L'
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'WKF')
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'WKF')
   ?}
?}; 1


\bed_dplf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem SRSR.DPLF
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.win_edit('?')<>''
|| {? (1+($('SRSR'+'.MF'+'().T'))()<>'D' & ($('SRSR'+'.MF'))()<>null)
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'DPLF')
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'DPLF')
   ?}
?}; 1


\srsr_ed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Uzupełnienie formuły po redagowaniu SRSR.MF
::----------------------------------------------------------------------------------------------------------------------
{? 1+SRSR.MF().T='N'
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'JM');
   SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'OKE');
   SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'MAX')
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'JM');
   SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'OKE');
   SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'MAX')
?};
{? (1+SRSR.MF().T<>'N' & SRSR.MF().K<>'UJB') | SRSR.MF=null
|| SRSR.efld_opt(SRSR.win_edit('?'),'editable=1',SRSR,'STAF')
|| SRSR.efld_opt(SRSR.win_edit('?'),'editable=grayed',SRSR,'STAF')
?};
{? SRSR.MF=null | 1+SRSR.MF().T='D' | 1+SRSR.MF().T='L'
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'WKF')
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'WKF')
?};
{? (1+($('SRSR'+'.MF'+'().T'))()<>'D' & ($('SRSR'+'.MF'))()<>null)
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'DPLF')
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'DPLF')
?};
win_disp(); 1


\ae_pojazd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Uzupełnienie formuły po redagowaniu SRSR.POJAZD
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.POJAZD='N'
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'SAMOCHOD');
   SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'SAM_EL');
   win_disp()
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'SAMOCHOD');
   win_disp()
?}; 1


\ae_samochod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Uzupełnienie formuły po redagowaniu SRSR.SAMOCHOD
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.SAMOCHOD='N'
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'SAM_EL');
   win_disp()
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'SAM_EL');
   win_disp()
?}; 1


\bed_samochod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem SRSR.SAMOCHOD
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.win_edit('?')<>''
|| {? SRSR.POJAZD='N'
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'SAMOCHOD')
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'SAMOCHOD')
   ?}
?}; 1


\bed_sam_el
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem SRSR.SAM_EL
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.win_edit('?')<>''
|| {? SRSR.POJAZD='N' | SRSR.SAMOCHOD='N'
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'SAM_EL')
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'SAM_EL')
   ?}
?}; 1


\ae_ext_mp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Uzupełnienie formuły po redagowaniu SRSR.MP
::----------------------------------------------------------------------------------------------------------------------
{? (1+($('SRSR'+'.MP'+'().T'))()<>'D' & ($('SRSR'+'.MP'))()<>null)
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'DPLP')
|| SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'DPLP')
?};
{? SRSR.MP().K<>'UJP' | SRSR.MP=null
|| SRSR.efld_opt(SRSR.win_edit('?'),'editable=1',SRSR,'STAP')
|| SRSR.efld_opt(SRSR.win_edit('?'),'editable=grayed',SRSR,'STAP')
?};
win_disp(); 1


\bed_dplp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Przed wyświetleniem SRSR.DPLP
::----------------------------------------------------------------------------------------------------------------------
{? SRSR.win_edit('?')<>''
|| {? (1+($('SRSR'+'.MP'+'().T'))()<>'D' & ($('SRSR'+'.MP'))()<>null)
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=0, editable=0',SRSR,'DPLP')
   || SRSR.efld_opt(SRSR.win_edit('?'),'enable=1, editable=1',SRSR,'DPLP')
   ?}
?}; 1


\srst_cechy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Ustawia widoczność akcji związanych z cechami dodatkowymi środków
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(208)='N'
|| SRST.actions('WER','F(O)',,1);
   SRST.actions('WER_N','F(O)',,1);
   SRST.actions('DWER','F(O)',,1);
   SRST.actions('DWER_O','F(O)',,1);
   SRST.actions('DWER_N','F(O)',,1);
   SRST.actions('DWER_N_O','F(O)',,1);
   SRDO.actions('WER','N',,1);
   SRDO.actions('WER_W','N',,1)
|| SRST.actions('WER','',,1);
   SRST.actions('WER_N','',,1);
   SRST.actions('DWER','',,1);
   SRST.actions('DWER_O','',,1);
   SRST.actions('DWER_N','',,1);
   SRST.actions('DWER_N_O','',,1);
   SRDO.actions('WER','',,1);
   SRDO.actions('WER_W','',,1)
?}; 1


\before_grp_barcode
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła Grupa przed dla wydruku kodów kreskowych
::----------------------------------------------------------------------------------------------------------------------
{? PARWYD.TYPDR=null
|| FUN.emsg('Nie wybrano typu drukarki w parametrach\n'
           +'wydruków użytkownika (adMinistracja -> parametry\n'
           +'Wydruków -> Użytkownika, lub akcja eTykiety podczas\n'
           +'wydruku). Nie można uruchomić wydruku.'@);
   return(0)
?};
{? PAR_WYDR.URZ_LAB=null
|| FUN.emsg('W parametrach wydruków nie ustawiono obowiązującej definicji etykiety.'@);
   return(0)
?};
1

:Sign Version 2.0 jowisz:1045 2023/09/04 12:20:41 18f581e2850d1e99fd17f28fbbcfb910d338d0faa4daf291ecdfd3fe179563ef49e9eb0503148fe19683e79a2ac6607317d2ed29b51a1504ec495bbe79719a8267c57083b91a714b9c97738c5a9d59600b4c11ebbb9450876de79b180e046649ac9aa9fa2f139bc3cf28b76c9df7df5b9e80ccc8f64e7975f505b9796ff1e6ac

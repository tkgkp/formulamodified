:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_wer.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa wersji dla systemu Statystyki
::======================================================================================================================


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Czyści powiązania do rekordu tabeli ST_WER
::   WE: _a - ST_WER.ref()
::   WY: >0  -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PRYWATNA><CLEAN>
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---
ST_WER.cntx_psh(); ST_WER.clear();
{? ST_WER.seek(_ref)
||
   {? _can_continue>0
   ||
::    1. Usuwam ST_OBJ które wskazują na ten element
      ST_OBJ.cntx_psh();
      ST_OBJ.index('OD_WER');
      ST_OBJ.prefix(ST_WER.ref());
      {? ST_OBJ.first()
      || {!
         |? _can_continue:=exec('delete','st_obj',ST_OBJ.ref());
            ST_OBJ.first() & _can_continue>0
         !}
      ?};
      ST_OBJ.cntx_pop()
   ?};
   {? _can_continue>0
   ||
::    2. Usuwam ST_OBJ_W które wskazują na ten element
      ST_OBJ_W.cntx_psh();
      ST_OBJ_W.index('ST_WER');
      ST_OBJ_W.prefix(ST_WER.ref());
      {? ST_OBJ_W.first()
      || {!
         |? _can_continue:=exec('delete','st_obj_w',ST_OBJ_W.ref());
            ST_OBJ_W.first() & _can_continue>0
         !}
      ?};
      ST_OBJ_W.cntx_pop()
   ?};
   ~~
?};

ST_WER.cntx_pop();
:: --- wszystkie powiazania usuniete? ---

{? _can_continue>0
|| _result:=1
|| undo()
?};

{? _mydo || end() ?};

_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Kasuje podany rekord tabeli ST_WER (wykonywane w transakcji!!!)
::   WE: _a - ST_WER.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_WER.cntx_psh(); ST_WER.clear();
{? ST_WER.seek(_ref)
|| {? exec('clean','st_wer',_ref)>0
   || {? ST_WER.del(,1)>0
      || _result:=1
      || undo();
         _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};

{? _result<0
|| undo()
?};

ST_WER.cntx_pop();
{? _mydo || end() ?};
_result


\st_wer_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Dołącz dla ST_WER
::----------------------------------------------------------------------------------------------------------------------
ST_WER.win_edit('RED');
ST_WER.blank();
{? ST_WER.edit()
|| ST_WER.add()
?};
~~


\st_wer_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń dla ST_WER
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRUPA='T' | FUN.ask('Czy usunąć bieżący wiersz?'@)
|| exec('delete','st_wer',ST_WER.ref())
?};
~~


\st_wer_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Popraw dla ST_SZEFO
::----------------------------------------------------------------------------------------------------------------------
ST_WER.win_edit('RED');
{? ST_WER.edit()
|| ST_WER.put()
?};
~~


\st_wer_del_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń przed dla grupy rekordów (tabela ST_WER)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?'@)
|| VAR.GRUPA:='T';
   1
|| 0
?}


\st_wer_del_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń po dla grupy rekordów (tabela ST_WER)
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~


\wersja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Wersja
::   WE: _a - obj_new() - środowisko parsera, wynik działania exec('env_parse','st_common')
::       _b - obj_new() - obiekt zawierający treść wiersza, wynik działania exec('POPUPITEME','st_parse_obj')
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_obj:=_b;

_result:=0;
_can_continue:=1;

_kind:=_obj.KIND;
_error:=_env.TXT_ERROR;
_warning:=_env.TXT_WARNING;
_env.REG.ST_WER:=null();
_symbol:=_obj.AppVersion;

ST_WER.index('SYMBOL');
ST_WER.prefix(_symbol,);
{? ~ST_WER.first()
||
   _symbol:='Nieznana';
   ST_WER.prefix(_symbol,);
   {? ~ST_WER.first()
   ||
      ST_WER.SYMBOL:=_symbol;
      ST_WER.WERSJA:=_symbol;
      ST_WER.OPIS:='Nieznana';
      {? ~ST_WER.add()
      ||
         _env.message('%1: %2 - ST_WER.add()'@[ _error, _kind]);
         _can_continue:=0
      ?}
   ?}
?};
{? _can_continue>0
||
   _env.REG.ST_WER:=ST_WER.ref()
?};

{? _can_continue>0
|| _result:=1
?};
_result

:Sign Version 2.0 jowisz:1048 2021/04/09 15:26:32 dfec84502dc74d99273ed6de8a4f22cdb815a31f8530a0be0eecf13c579dc9cac6ceec4fffdc8735d1263e8c9cf3071408a07f2269cc7df793e5994a9892b81dc970f86669aa996b5b25dac26c5972c139b3e41256e0c75ab4d09bc4444aa3f460865cb7064ba612c48f7f812febe8a2c72c3d68f2ce40a431f1f1ad1ed25930

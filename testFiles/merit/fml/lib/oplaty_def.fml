:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: oplaty_def.fml
:: Utworzony: 06.12.2021
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa opłat dodatkowych - definicje
::======================================================================================================================

\ref_TAXS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wartość początkowa pola TAXS
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.TAXS


\ref_TAXO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wartość początkowa pola TAXO
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.TAXO


\blankR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wartość początkowa pola TAXT.R
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.R


\copyTAXS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kopia opłat dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_old:=TAXS.ref();
TAXS.get();
TAXS.win_edit('COPY');
TAXXYZ.C_O:='T'; TAXXYZ.C_N:='T'; TAXXYZ.C_AC:='T'; TAXXYZ.C_AI:='T'; TAXXYZ.C_AD:='T';
{? TAXS.edit("exec('chk_taxs','oplaty_def',null(),1)")
|| TAXS.MOD:='';
   do();
   {? TAXS.add(1)
   || _new:=TAXS.ref();
      _buf:=tab_tmp(1,'OLD','STRING[16]','','NEW','STRING[16]','');
::    wersje
      {? TAXXYZ.C_O='T'
      || TAXO.cntx_psh();
         TAXO.index('TAXS');
         TAXO.prefix(_old);
         {? TAXO.first()
         || {!
            |? _ref:=TAXO.ref(); _oki:=TAXO.next();
               TAXO.cntx_psh();
               TAXO.prefix();
               {? TAXO.seek(_ref)
               || TAXO.TAXS:=_new;
                  {? ~TAXO.add(1)
                  || undo()
                  || _buf.clear();
                     _buf.blank();
                     _buf.NEW:=$TAXO.ref();
                     _buf.OLD:=$_ref;
                     _buf.add(1)
                  ?}
               ?};
               TAXO.cntx_pop();
               _oki
            !}
         ?};
         TAXO.cntx_pop()
      ?};
::    warunki
      {? TAXXYZ.C_N='T'
      || TAXN.cntx_psh();
         TAXN.index('TAXS');
         TAXN.prefix(_old);
         {? TAXN.first()
         || {!
            |? _ref:=TAXN.ref(); _oki:=TAXN.next();
               TAXN.cntx_psh();
               TAXN.prefix();
               {? TAXN.seek(_ref)
               || TAXN.TAXS:=_new;
                  {? ~TAXN.add(1) || undo() ?}
               ?};
               TAXN.cntx_pop();
               _oki
            !}
         ?};
         TAXN.cntx_pop()
      ?};
::    atrybuty
      {? TAXXYZ.C_AC='T'
      || TAXT.cntx_psh();
         TAXT.index('MNAG');
         TAXT.prefix(_old,'C');
         {? TAXT.first()
         || {!
            |? _ref:=TAXT.ref(); _oki:=TAXT.next();
               TAXT.cntx_psh();
               TAXT.prefix();
               {? TAXT.seek(_ref)
               || _taxo:={? TAXT.TAXO<>null() & (_buf.clear(); _buf.prefix($TAXT.TAXO); _buf.first())
                         || exec('FindAndGet','#table',TAXO,_buf.NEW,,,null())
                         || null()
                         ?};
                  TAXT.TAXS:=_new;
                  TAXT.TAXO:=_taxo;
                  {? ~TAXT.add(1) || undo() ?}
               ?};
               TAXT.cntx_pop();
               _oki
            !}
         ?};
         TAXT.cntx_pop()
      ?};
      {? TAXXYZ.C_AI='T'
      || TAXT.cntx_psh();
         TAXT.index('MNAG');
         TAXT.prefix(_old,'M');
         {? TAXT.first()
         || {!
            |? _ref:=TAXT.ref(); _oki:=TAXT.next();
               TAXT.cntx_psh();
               TAXT.prefix();
               {? TAXT.seek(_ref)
               || _taxo:={? TAXT.TAXO<>null() & (_buf.clear(); _buf.prefix($TAXT.TAXO); _buf.first())
                         || exec('FindAndGet','#table',TAXO,_buf.NEW,,,null())
                         || null()
                         ?};
                  TAXT.TAXS:=_new;
                  TAXT.TAXO:=_taxo;
                  {? ~TAXT.add(1) || undo() ?}
               ?};
               TAXT.cntx_pop();
               _oki
            !}
         ?};
         TAXT.cntx_pop()
      ?};
      {? TAXXYZ.C_AD='T'
      || TAXT.cntx_psh();
         TAXT.index('MNAG');
         TAXT.prefix(_old,'N');
         {? TAXT.first()
         || {!
            |? _ref:=TAXT.ref(); _oki:=TAXT.next();
               TAXT.cntx_psh();
               TAXT.prefix();
               {? TAXT.seek(_ref)
               || _taxo:={? TAXT.TAXO<>null() & (_buf.clear(); _buf.prefix($TAXT.TAXO); _buf.first())
                         || exec('FindAndGet','#table',TAXO,_buf.NEW,,,null())
                         || null()
                         ?};
                  TAXT.TAXS:=_new;
                  TAXT.TAXO:=_taxo;
                  {? ~TAXT.add(1) || undo() ?}
               ?};
               TAXT.cntx_pop();
               _oki
            !}
         ?};
         TAXT.cntx_pop()
      ?};
      obj_del(_buf)
   ?};
   end()
?};
~~


\copyTAXO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kopia opłat dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_old:=TAXO.ref();
TAXO.get();
TAXO.win_edit('COPY');
TAXXYZ.C_AC:='T'; TAXXYZ.C_AI:='T'; TAXXYZ.C_AD:='T';
{? TAXO.edit("exec('chk_taxo','oplaty_def',null(),1)")
|| do();
   {? TAXO.add(1)
   || exec('akt_taxo','oplaty_def',TAXO.TAXS);
      exec('actTAXS','oplaty_def',TAXO.ref(),1,0);
      _new:=TAXO.ref();
::    atrybuty
      {? TAXXYZ.C_AC='T'
      || TAXT.cntx_psh();
         TAXT.index('RORV');
         TAXT.prefix(_old,'C');
         {? TAXT.first()
         || {!
            |? _ref:=TAXT.ref(); _oki:=TAXT.next();
               TAXT.cntx_psh();
               TAXT.prefix();
               {? TAXT.seek(_ref)
               || TAXT.TAXO:=_new;
                  {? ~TAXT.add(1) || undo() ?}
               ?};
               TAXT.cntx_pop();
               _oki
            !}
         ?};
         TAXT.cntx_pop()
      ?};
      {? TAXXYZ.C_AI='T'
      || TAXT.cntx_psh();
         TAXT.index('RORV');
         TAXT.prefix(_old,'M');
         {? TAXT.first()
         || {!
            |? _ref:=TAXT.ref(); _oki:=TAXT.next();
               TAXT.cntx_psh();
               TAXT.prefix();
               {? TAXT.seek(_ref)
               || TAXT.TAXO:=_new;
                  {? ~TAXT.add(1) || undo() ?}
               ?};
               TAXT.cntx_pop();
               _oki
            !}
         ?};
         TAXT.cntx_pop()
      ?};
      {? TAXXYZ.C_AD='T'
      || TAXT.cntx_psh();
         TAXT.index('RORV');
         TAXT.prefix(_old,'N');
         {? TAXT.first()
         || {!
            |? _ref:=TAXT.ref(); _oki:=TAXT.next();
               TAXT.cntx_psh();
               TAXT.prefix();
               {? TAXT.seek(_ref)
               || TAXT.TAXO:=_new;
                  {? ~TAXT.add(1) || undo() ?}
               ?};
               TAXT.cntx_pop();
               _oki
            !}
         ?};
         TAXT.cntx_pop()
      ?}
   ?};
   end()
?};
~~


\atr_TAXS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Atrybuty opłat dodatkowych
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.TAXS:=TAXS.ref();
TAXXYZ.TAXO:=null();
exec('sel_TAXT','oplaty_def');
~~


\atr_TAXO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Atrybuty opłat dodatkowych
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.TAXS:=TAXS.ref();
TAXXYZ.TAXO:=TAXO.ref();
exec('sel_TAXT','oplaty_def');
~~


\notaxTAXS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wykluczenia opłat dodatkowych
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.TAXS:=TAXS.ref();
VAR_DEL.delete('__grtaxn','__wntaxn');
__grtaxn:=sql('select distinct GRP, OD, DO from TAXN where TAXN.TAXS=:_a order by 1, 2',TAXXYZ.TAXS);
__wntaxn:=__grtaxn.mk_sel('Grupy warunków opłaty','P',,'mgkos_sel',,,,);
__grtaxn.win_fld(__wntaxn,,'GRP',,,10,,1,'Grupa');
__grtaxn.win_fld(__wntaxn,,'OD',,,10,,1,'Obowiązuje od');
__grtaxn.win_fld(__wntaxn,,'DO',,,10,,1,'Obowiązuje do');
__grtaxn.win_act(__wntaxn,1,'Formuła','&Dołącz'@@,,,"exec('dol_grp_taxn','oplaty_def')",,1,,,,'D');
__grtaxn.win_act(__wntaxn,0,'Formuła','&Dołącz'@@,,,"exec('dol_grp_taxn','oplaty_def')",,0,,,,'D');
__grtaxn.win_act(__wntaxn,0,'Formuła','Popraw',,,"exec('pop_grp_taxn','oplaty_def')",,1,,,,'P');
__grtaxn.win_act(__wntaxn,0,'Formuła','&Usuń'@@,,,,"exec('del_grp_taxn','oplaty_def')",,0,,,'U');
__grtaxn.win_act(__wntaxn,,'Szukaj');
__grtaxn.win_act(__wntaxn,,'Kolejność');

_fb:="__grtaxn.prefix();
      __grtaxn.first();
      grp_disp(__grtaxn,__wntaxn)
     ";
_fr:="";

_win_grp:=__grtaxn.grp_make('Warunki dla opłaty dodatkowej'@,_fb,'#taxn_grp',,,,_fr);
_fb:="exec('wer_taxn','oplaty_def')";
_fa:="";
_fr:="";
__grtaxn.grp_sel(_win_grp,__grtaxn,__wntaxn,,_fb,,,25,,,,,'maximized_with_title');
__grtaxn.grp_splt(_win_grp,,'vertical','warunki');
_fb:="";
_fa:="";
_fr:="";
__grtaxn.grp_sel(_win_grp,TAXN,'WER',,_fb,,,25,,,,,'maximized_with_title');
TAXN.fld_fml('ANDOR','DISPLAY_FORMAT'
 ,"_ref:=TAXN.ref();
   TAXN.cntx_psh();
   TAXN.index('GRP');
   TAXN.prefix(TAXXYZ.TAXS,TAXXYZ.GRP,TAXXYZ.GRP,TAXXYZ.OD);
   _res:={? TAXN.first() & TAXN.ref=_ref || 'empty=1' || 'empty=0' ?};
   TAXN.cntx_pop();
   _res");

TAXN.dnd_sel('WER',,'records.TAXN',"exec('renumtaxn','oplaty_def')");
__grtaxn.win_sel(_win_grp);
__grtaxn.select();
TAXN.dnd_sel('WER',,'records.TAXN',"");
VAR_DEL.delete('__grtaxn','__wntaxn');
~~


\wer_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Warunki opłaty
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.GRP:=__grtaxn.GRP;
TAXXYZ.OD:=__grtaxn.OD;
TAXXYZ.DO:=__grtaxn.DO;
TAXN.index('GRP');
TAXN.prefix(TAXXYZ.TAXS,TAXXYZ.GRP,TAXXYZ.GRP,TAXXYZ.OD);
TAXN.first();
{? ~__grtaxn.size() || TAXN.actions_grayed('WER','D:D') || TAXN.actions_grayed('WER','') ?};
grp_disp(TAXN,'WER');
~~


\add_taxo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dołaczenie wersji opłaty
::----------------------------------------------------------------------------------------------------------------------
TAXO.win_edit('RED');
TAXO.blank();
{? TAXO.edit("exec('chk_taxo','oplaty_def')")
|| {? TAXO.add(1)
   || exec('akt_taxo','oplaty_def',TAXO.TAXS);
      exec('actTAXS','oplaty_def',TAXO.ref(),1,0)
   ?}
?};
~~


\mod_taxo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dołączenie wersji opłaty
::----------------------------------------------------------------------------------------------------------------------
TAXO.win_edit('RED');
TAXO.get();
_prev:=form(TAXO.OD)+'.'+form(TAXO.DO);
{? TAXO.edit("exec('chk_taxo','oplaty_def',TAXO.ref())")
|| {? TAXO.put(1)
   || exec('akt_taxo','oplaty_def',TAXO.TAXS);
      exec('actTAXS','oplaty_def',TAXO.ref(),_prev,form(TAXO.OD)+'.'+form(TAXO.DO))
   ?}
?};
~~


\del_taxo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Usunięcie wersji opłaty
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć wersję opłaty dodatkowej?'@)
|| TAXT.index('TAXO'); TAXT.prefix(TAXO.ref());  {? TAXT.first() || {! |? TAXT.del() !} ?};
   {? TAXO.del(,1) || exec('akt_taxo','oplaty_def',TAXXYZ.TAXS) ?}
?};
~~


\chk_taxo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kontrola poprawności wprowadzonych danych
::   WE: [_a] - aktualny rekord
::   WY: pusty string lub akronim pola
::----------------------------------------------------------------------------------------------------------------------
_res:='';
_ref:={? var_pres('_a')=type_of(null()) || _a || null() ?};

{? TAXO.OD=date(0,0,0)
|| FUN.info('Należy podać datę od której obowiązuje.'@);
   _res:='OD'
|? TAXO.DO<>date(0,0,0) & TAXO.DO<TAXO.OD
|| FUN.info('Nieprawidłowy zakres dat.'@);
   _res:='DO'
|? ~exec('ctrltaxo','oplaty_def',TAXXYZ.TAXS,_ref,TAXO.OD,TAXO.DO)
|| FUN.info('Nieprawidłowy zakres dat.'@);
   _res:='OD'
?};
_res


\ctrltaxo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kontroluje inne wersje opłat dodatkowych
::   WE: _a - TAXS
::       _b - ref TAXO lub null()
::       _c - data OD
::       _d - data DO
::   WY: 1-jest OK 0-nie jest dobrze
::----------------------------------------------------------------------------------------------------------------------
_res:=0;

_min:=date(0,0,0);
_max:=date(0,0,0);
TAXO.cntx_psh();
TAXO.index('TAXS');
TAXO.prefix(_a);
{? TAXO.first()
||
   {!
   |? {? TAXO.ref()<>_b
      || _min:=TAXO.OD;
         _max:=TAXO.DO;
         {? TAXO.OD=_c | TAXO.DO=_c                    || _res:=-1
         |? TAXO.OD>_c & (_d=date(0,0,0) | TAXO.OD>_d) || _res:=1
         ?}
      ?};
      _res=0 & TAXO.next()
   !};
   {? _res=0 & {? _max=date(0,0,0) || _min=date(0,0,0) | _min<_c || _max<_c ?} || _res:=1 ?}
|| _res:=1
?};
TAXO.cntx_pop();
{? _res<0 || _res:=0 ?};
_res


\akt_taxo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja dat na wersjach opłaty
::   WE: _a - ref TAXS
::----------------------------------------------------------------------------------------------------------------------
TAXO.cntx_psh();
TAXO.index('TAXS');
TAXO.prefix(_a);
{? TAXO.last()
|| _dat:=TAXO.OD;
   {? TAXO.prev()
   || {!
      |? {? TAXO.DO=date(0,0,0)
         || TAXO.DO:=_dat-1;
            TAXO.put(1)
         ?};
         _dat:=TAXO.OD;
         TAXO.prev()
      !}
   ?}
?};
TAXO.cntx_pop();
~~


\sel_TAXT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Okienko atrybutów
::----------------------------------------------------------------------------------------------------------------------
_fb:="TAXT.index('R');
      TAXT.prefix(TAXXYZ.TAXS,TAXXYZ.TAXO,'C');
      grp_disp(TAXT,'WERC');
      TAXT.index('R');
      TAXT.prefix(TAXXYZ.TAXS,TAXXYZ.TAXO,'N');
      grp_disp(TAXT,'WERM');
      TAXT.index('R');
      TAXT.prefix(TAXXYZ.TAXS,TAXXYZ.TAXO,'N');
      grp_disp(TAXT,'WERN')
     ";
_fr:="";

_win_grp:=TAXT.grp_make('Definicja atrybutów'@,_fb,'#taxt_grp',,,,_fr);
_fb:="exec('before_taxt','oplaty_def','C')";
_fa:="exec('after_taxt','oplaty_def','C')";
_fr:="";
TAXT.grp_sel(_win_grp,,'WERC','Stałe i formuły',_fr,,,10,_fb,_fa,,,'maximized_with_title');
_fb:="exec('before_taxt','oplaty_def','M')";
_fa:="exec('after_taxt','oplaty_def','M')";
_fr:="";
TAXT.grp_sel(_win_grp,,'WERM','Indeksy',_fr,,,10,_fb,_fa,,,'maximized_with_title');
_fb:="exec('before_taxt','oplaty_def','N')";
_fa:="exec('after_taxt','oplaty_def','N')";
_fr:="";
TAXT.grp_sel(_win_grp,,'WERN','Dokumenty',_fr,,,10,_fb,_fa,,,'maximized_with_title');

TAXT.win_sel(_win_grp);
TAXT.select();
~~


\before_taxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Przed dla atrybutów
::   WE: _a - rodzaj
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.R:=_a;
TAXT.cntx_psh();
TAXT.index('R');
TAXT.prefix(TAXXYZ.TAXS,TAXXYZ.TAXO,TAXXYZ.R);
~~


\after_taxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Po dla atrybutów
::   WE: _a - rodzaj
::----------------------------------------------------------------------------------------------------------------------
TAXT.cntx_pop();
~~


\add_taxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dołączenie atrybutu
::----------------------------------------------------------------------------------------------------------------------
{? TAXXYZ.R='C'
|| TAXT.win_edit('REDFC')
|? TAXXYZ.R='N'
|| TAXT.win_edit('REDNP');
   TAXT.hdr_edit(' - nagłówka'@)
|| TAXT.win_edit('REDAT');
   TAXT.hdr_edit(' - indeksu'@)
?};
TAXT.blank();
exec('po_r_taxt','oplaty_def');
{? TAXT.edit("exec('chk_taxt','oplaty_def')")
|| {? TAXT.add(1) || exec('actTAXS','oplaty_def',TAXT.ref(),1,0) ?}
?}


\mod_taxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Modyfikacje atrybutu
::----------------------------------------------------------------------------------------------------------------------
{? TAXXYZ.R='C'
|| TAXT.win_edit('REDFC')
|? TAXXYZ.R='N'
|| TAXT.win_edit('REDNP');
   TAXT.hdr_edit(' - nagłówka'@)
|| TAXT.win_edit('REDAT');
   TAXT.hdr_edit(' - indeksu'@)
?};
TAXT.get();
_prev:=TAXT.KOD+'.'+TAXT.R+'.'+TAXT.RV;
exec('po_r_taxt','oplaty_def');
{? TAXT.edit("exec('chk_taxt','oplaty_def',TAXT.ref())")
|| {? TAXT.put(1) || exec('actTAXS','oplaty_def',TAXT.ref(),_prev,TAXT.KOD+'.'+TAXT.R+'.'+TAXT.RV) ?}
?}


\del_taxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Usunięcie atrybutu
::----------------------------------------------------------------------------------------------------------------------
{? TAXT.AKT='N'
|| FUN.info('Atrybut opłaty dodatkowej zablokowany do modyfikacji.\nUsunięcie niemożliwe.'@)
|? exec('FindInSet','#table','TAXI','TAXS',TAXT.KOD,TAXT.TAXS,,1,,null())<>null()
|| FUN.info('Atrybut wykorzystany dla indeksu.\nUsunięcie niemożliwe.'@)
|? FUN.ask('Czy usunąć atrybut opłaty dodatkowej?'@)
|| TAXV.index('UNIK'); TAXV.prefix(TAXT.TAXS,TAXT.ref());  {? TAXV.first() || {! |? TAXV.del() !} ?};
   TAXT.del(,1)
?};
~~


\chk_taxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kontrola wprowadzonych danych
::   WE: [_a] - aktualny rekord
::----------------------------------------------------------------------------------------------------------------------
_res:='';
_ref:={? var_pres('_a')=type_of(null()) || _a || null() ?};

{? TAXT.KOD=''
|| FUN.info('Należy podać kod atrybutu.'@);
   _res:='KOD'
|? TAXT.DESC=''
|| FUN.info('Należy podać opis atrybutu.'@);
   _res:='DESC'
|? ~exec('valid_acronim','#string',TAXT.KOD,8)
|| FUN.info('Kod atrybutu musi rozpoczynać się od wielkiej litery oraz\n'
            'może zawierać wielkie litery, cyfry i znaki podkreślenia.'@);
   _res:='KOD'
|? exec('FindInSet','#table','TAXT','UNIK',TAXT.KOD,TAXT.TAXS,,1,TAXT.TAXO,_ref)<>_ref
|| FUN.info('Wykorzystano już kod atrybutu w definicji opłaty (stałe, formuły, indeksy, dokumenty).\n'
            'Należy podać inny kod atrybutu.'@);
   _res:='KOD'
|? TAXT.RP<0
|| {? TAXT.RV='S'
   || FUN.info('Długość napisu musi być większa lub równa zero.'@)
   || FUN.info('Precyzja liczby musi być większa lub równa zero.'@)
   ?};
   _res:='RP'
|? TAXT.R<>'C' & (';SDT'*TAXT.RV)>1 & ~((';TN'*TAXT.CTRL)>1)
|| FUN.info('Dla danego rodzaju wartości atrybutu należy wybrać kontrolę:\n'
            '"Nie",\n'
            '"Tak".');
   _res:='CTRL'
|? TAXT.R<>'C' & TAXT.RV='R' & ~((';NAB'*TAXT.CTRL)>1)
|| FUN.info('Dla danego rodzaju wartości atrybutu należy wybrać kontrolę:\n'
            '"Nie",\n'
            '"Tak (większe od zera)",\n'
            '"Tak (większe lub równe zero)".');
   _res:='CTRL'
|? TAXT.TAXO<>null()
|| _taxtrv:=TAXT.RV;
   TAXT.cntx_psh();
   TAXT.index('TAXS');
   TAXT.prefix(TAXT.TAXS,TAXT.KOD,);
   {? TAXT.first()
   || {!
      |? {? TAXT.RV<>_taxtrv & TAXT.ref()<>_ref || _res:='RV' ?};
         _res='' & TAXT.next()
      !}
   ?};
   TAXT.cntx_pop();
   {? _res<>'' || FUN.info('Podany kod w definicji opłaty ma inny rodzaj atrybutu.\n'@) ?}
?};
{? _res='' & TAXT.RV='F' & TAXT.FORM=''
|| FUN.info('Dla rodzaju formuła - należy ją podać.\n'@);
   _res:='FORM'
|? _res='' & TAXT.RV='F'
|| _taxtr:=TAXT.R;
   _taxo:=TAXT.TAXO;
   _taxs:=TAXT.TAXS;
   TAXT.cntx_psh();
   {? _taxo<>null()
   || TAXT.index('RORV');
      TAXT.prefix(_taxo,_taxtr,'F');
      {? TAXT.first() & TAXT.ref()<>_ref || _res:='RV' ?}
   || TAXT.index('R_RV');
      TAXT.prefix(_taxs,_taxtr,'F');
      {? TAXT.first()
      || {!
         |? {? TAXT.TAXO=null() & TAXT.ref()<>_ref || _res:='RV' ?};
            _res='' & TAXT.next()
         !}
      ?}
   ?};
   TAXT.cntx_pop();
   {? _res<>''
   || {? TAXT.R='C'
      || FUN.info('Wśród atrybutów opłaty podano już formułę wyliczenia.\n'@)
      |? TAXT.R='M'
      || FUN.info('Wśród atrybutów indeksu opłaty podano już formułę walidacji.\n'@)
      |? TAXT.R='N'
      || FUN.info('Wśród atrybutów dokumentu opłaty podano już formułę walidacji.\n'@)
      ?}
   ?}
?};
_res


\po_r_taxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Powyborze rodzaju zmiennej
::----------------------------------------------------------------------------------------------------------------------
_win_ed:={? TAXXYZ.R='C' || 'REDFC' |? TAXXYZ.R='N' || 'REDNP' || 'REDAT' ?};
_ens:=_enr:=_end:=_ent:=_enl:=_enf:=_pre:='enable=0';
_ctrl:={? TAXXYZ.R='C' || 'enable=0' || 'enable=1' ?};
{? TAXT.RV='S' || _ens:='enable=1'; _pre:='enable=1'
|? TAXT.RV='R' || _enr:='enable=1'; _pre:='enable=1'
|? TAXT.RV='D' || _end:='enable=1'
|? TAXT.RV='T' || _ent:='enable=1'
|? TAXT.RV='L' || _enl:='enable=1'; _ctrl:='enable=0'
|? TAXT.RV='F' || _enf:='enable=1'; _ctrl:='enable=0'
?};
TAXT.efld_opt(_win_ed,_ens,,'VALS');
TAXT.efld_opt(_win_ed,_enr,,'VALR');
TAXT.efld_opt(_win_ed,_end,,'VALD');
TAXT.efld_opt(_win_ed,_ent,,'VALT');
TAXT.efld_opt(_win_ed,_enl,,'VALL');
TAXT.efld_opt(_win_ed,_pre,,'RP');
TAXT.efld_opt(_win_ed,_enf,,'FORM');
TAXT.efld_opt(_win_ed,_ctrl,,'CTRL');
:: Czy dostępna modyfikacja definicji atrybutu
_mod:={? TAXT.AKT='N' || 'editable=0' || 'editable=1' ?};
TAXT.efld_opt(_win_ed,_mod,,'KOD');
TAXT.efld_opt(_win_ed,_mod,,'RV');
1


\rek_taxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Akcja przed dla atrybutu
::----------------------------------------------------------------------------------------------------------------------
{? TAXT.RV='S' || TAXXYZ.TYPATR:='Napis%1'[{? TAXT.RP>0 || ' [%1]'[$TAXT.RP] || '' ?}];
                  TAXXYZ.VAL:=TAXT.VALS
|? TAXT.RV='R' || TAXXYZ.TYPATR:='Liczba%1'[{? TAXT.RP>0 || ' [,%1]'[$TAXT.RP] || '' ?}];
                  TAXXYZ.VAL:=$TAXT.VALR
|? TAXT.RV='D' || TAXXYZ.TYPATR:='Data';
                  TAXXYZ.VAL:=$TAXT.VALD
|? TAXT.RV='T' || TAXXYZ.TYPATR:='Czas';
                  TAXXYZ.VAL:=$TAXT.VALT
|? TAXT.RV='L' || TAXXYZ.TYPATR:='Logika';
                  TAXXYZ.VAL:={? TAXT.VALL='T' || 'PRAWDA' || 'FAŁSZ' ?}
|? TAXT.RV='F' || TAXXYZ.TYPATR:='Formuła';
                  TAXXYZ.VAL:=TAXT.FORM
?};
{? exec('get','#params',4080,2,OPERATOR.USER)='N'
|| TAXT.actions_grayed(cur_win(1,1),'M(BO)')
|| TAXT.actions_grayed(cur_win(1,1),'')
?};
''


\disptaxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wyświetlenie atrybutu
::----------------------------------------------------------------------------------------------------------------------
{? TAXXYZ.R='C'
|| TAXT.win_edit('REDFC')
|? TAXXYZ.R='N'
|| TAXT.win_edit('REDNP');
   TAXT.hdr_edit(' - nagłówka'@)
|| TAXT.win_edit('REDAT');
   TAXT.hdr_edit(' - indeksu'@)
?};
TAXT.get();
exec('po_r_taxt','oplaty_def');
TAXT.display()


\wyp_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wypełnienie póldla TAXN
::----------------------------------------------------------------------------------------------------------------------
{? TAXN.ROP='NN'
|| {? TAXN.R='K'
   || TAXN.KH_ODB:=null(); TAXN.MG:=null(); TAXN.SP:=null(); TAXN.TM:=null(); TAXN.TSZ:=null()
   |? TAXN.R='O'
   || TAXN.MG:=null(); TAXN.SP:=null(); TAXN.TM:=null(); TAXN.TSZ:=null()
   |? TAXN.R='M'
   || TAXN.KH:=null(); TAXN.KH_ODB:=null(); TAXN.SP:=null(); TAXN.TM:=null(); TAXN.TSZ:=null()
   |? TAXN.R='S'
   || TAXN.SP:=TAXXYZ.STS; TAXN.KH:=null(); TAXN.KH_ODB:=null(); TAXN.MG:=null(); TAXN.TM:=null(); TAXN.TSZ:=null()
   |? TAXN.R='Z'
   || TAXN.SP:=TAXXYZ.STZ; TAXN.KH:=null(); TAXN.KH_ODB:=null(); TAXN.MG:=null(); TAXN.TM:=null(); TAXN.TSZ:=null()
   |? TAXN.R='T'
   || TAXN.KH:=null(); TAXN.KH_ODB:=null(); TAXN.MG:=null(); TAXN.SP:=null(); TAXN.TSZ:=null()
   |? TAXN.R='X'
   || TAXN.TSZ:=TAXXYZ.TSP; TAXN.KH:=null(); TAXN.KH_ODB:=null(); TAXN.MG:=null(); TAXN.SP:=null(); TAXN.TM:=null()
   |? TAXN.R='Y'
   || TAXN.TSZ:=TAXXYZ.TZK; TAXN.KH:=null(); TAXN.KH_ODB:=null(); TAXN.MG:=null(); TAXN.SP:=null(); TAXN.TM:=null()
   ?}
|| TAXN.R:='!';
   TAXN.KH:=null(); TAXN.KH_ODB:=null(); TAXN.MG:=null(); TAXN.SP:=null(); TAXN.TM:=null(); TAXN.TSZ:=null();
   TAXN.FORM:=''
?}

\add_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dołączenie wykluczenia
::----------------------------------------------------------------------------------------------------------------------
_beersz:=BEER.SZ;
_stoddz:=ST.ODDZ;
_beerzak:=BEER.ZAK;
_beerkh:=BEER.KH;
TAXN.win_edit('RED');
TAXN.blank();
TAXN.R:='K';
exec('po_rop_taxn','oplaty_def');
{? TAXN.edit("exec('chk_taxn','oplaty_def')")
|| exec('wyp_taxn','oplaty_def');
   {? TAXN.add(1) || exec('actTAXS','oplaty_def',TAXN.ref(),0,1) ?}
?};
BEER.SZ:=_beersz;
ST.ODDZ:=_stoddz;
BEER.ZAK:=_beerzak;
BEER.KH:=_beerkh;
~~


\mod_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Modyfikacja wykluczenia
::----------------------------------------------------------------------------------------------------------------------
TAXN.win_edit('RED');
TAXN.get();
_prev:='%1.%2.%3.%4.%5.%6.%7.%8.%9.%10.%11.%12'[form(TAXN.OD),form(TAXN.DO)
          ,TAXN.R,$TAXN.KH,$TAXN.KH_ODB,$TAXN.MG,$TAXN.SP,$TAXN.TM,$TAXN.TSZ,TAXN.ROP,TAXN.ANDOR,TAXN.YESNO];
exec('po_rop_taxn','oplaty_def');
{? TAXN.edit("exec('chk_taxn','oplaty_def')")
|| exec('wyp_taxn','oplaty_def');
   _actu:='%1.%2.%3.%4.%5.%6.%7.%8.%9.%10.%11.%12'[form(TAXN.OD),form(TAXN.DO)
          ,TAXN.R,$TAXN.KH,$TAXN.KH_ODB,$TAXN.MG,$TAXN.SP,$TAXN.TM,$TAXN.TSZ,TAXN.ROP,TAXN.ANDOR,TAXN.YESNO];
   {? TAXN.put(1) || exec('actTAXS','oplaty_def',TAXN.ref(),_prev,_actu) ?}
?}


\po_r_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Powyborze rodzaju zmiennej
::----------------------------------------------------------------------------------------------------------------------
_win_ed:='RED';
_enf:='enable=0';
{? TAXN.ROP='NN'
|| _enf:='enable=1'
?};
TAXN.efld_opt(_win_ed,_enf,,'FORM');
{? TAXN.R='S' || ST.ODDZ:=''; BEER.SZ:='S'
|? TAXN.R='Z' || ST.ODDZ:=''; BEER.SZ:='Z'
|? TAXN.R='X' || BEER.ZAK:='N'
|? TAXN.R='Y' || BEER.ZAK:='T'
?};
exec('po_taxn_form','oplaty_def');
1


\po_rop_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Po redakcji rodzaju operacji
::----------------------------------------------------------------------------------------------------------------------
_win_ed:='RED';
_enr:={? TAXN.ROP='NN' || 'enable=1' || 'enable=0' ?};
_mrk:={? TAXN.ROP='NN' || 'mark=1' || 'mark=0' ?};
TAXN.efld_opt(_win_ed,_enr,,'R');
TAXN.efld_opt(_win_ed,_mrk,,'FORM');
exec('po_r_taxn','oplaty_def');
1


\add_taxs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dołaczenie wersji opłaty
::----------------------------------------------------------------------------------------------------------------------
TAXS.win_edit('RED');
TAXS.blank();
{? TAXS.edit("exec('chk_taxs','oplaty_def')")
|| TAXS.add(1)
?};
~~


\chk_taxs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kontrola poprawności wprowadzonych danych
::   WE: [_a] - aktualny rekord
::       [_b] - kopia definicji
::   WY: pusty string lub akronim pola
::----------------------------------------------------------------------------------------------------------------------
_res:='';
 _ref:={? var_pres('_a')=type_of(null()) || _a || null() ?};
_copy:={? var_pres('_b')=type_of(0) || _b || 0 ?};

{? TAXS.TAX=''
|| FUN.info('Należy podać kod opłaty.'@);
   _res:='TAX'
|? TAXS.DESC=''
|| FUN.info('Należy podać opis opłaty.'@);
   _res:='DESC'
|| _kod:=TAXS.TAX;
   TAXS.cntx_psh();
   TAXS.index('UNIK');
   TAXS.prefix(_kod,);
   {? TAXS.first() & TAXS.ref()<>_ref || _res:='TAX' ?};
   TAXS.cntx_pop();
   {? _res<>'' || FUN.info('Istnieje już definicja opłaty %1.\nNależy podać inny kod opłaty.'@[_kod]) ?}
?};
_res


\mod_taxs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Modyfikacja opłaty
::----------------------------------------------------------------------------------------------------------------------
TAXS.win_edit('RED');
TAXS.get();
{? TAXS.edit("exec('chk_taxs','oplaty_def',TAXS.ref())")
|| TAXS.put(1)
?};
~~


\del_taxs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Usunięcie opłaty
::----------------------------------------------------------------------------------------------------------------------
{? exec('FindInSet','#table','TAXX','TAXS',TAXS.ref())<>null()
|| FUN.info('Opłata %1 została przypisana do indeksów.\nUsunięcie niemożliwe.'@[TAXS.TAX])
|? FUN.ask('Czy usunąć definicję opłaty dodatkowej?'@)
|| TAXV.index('TAXS'); TAXV.prefix(TAXS.ref());  {? TAXV.first() || {! |? TAXV.del() !} ?};
   TAXT.index('TAXS'); TAXT.prefix(TAXS.ref());  {? TAXT.first() || {! |? TAXT.del() !} ?};
   TAXN.index('TAXS'); TAXN.prefix(TAXS.ref());  {? TAXN.first() || {! |? TAXN.del() !} ?};
   TAXO.index('TAXS'); TAXO.prefix(TAXS.ref());  {? TAXO.first() || {! |? TAXO.del() !} ?};
   TAXS.del(,1)
?};
~~


\blok_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Blokada/usunięcie blokady modyfikacji dla atrybutu:
::   WE: [_a] - 1(domyślnie)-założenie blokady 0-usunięcie
::----------------------------------------------------------------------------------------------------------------------
_blok:={? var_pres('_a')=type_of(0) || _a || 1 ?};
_sel:=TAXT.sel_aget();
TAXT.sel_adel();
TAXT.cntx_psh();
{? _sel.size()
|| {? {? _blok
      || FUN.ask('Czy zablokować zaznaczone atrybuty do modyfikacji?'@)
      || FUN.ask('Czy odblokować zaznaczone atrybuty do modyfikacji?'@)
      ?} & _sel.first()
   || {!
      |? {? TAXT.seek(_sel.REF,)
         || TAXT.AKT:={? _blok || 'N' || 'T' ?};
            TAXT.put(1)
         ?};
         _sel.next()
      !}
   ?}
|? {? _blok
   || FUN.ask('Czy zablokować atrybut do modyfikacji?'@)
   || FUN.ask('Czy odblokować atrybut do modyfikacji?'@)
   ?}
|| TAXT.AKT:={? _blok || 'N' || 'T' ?};
   TAXT.put(1)
?};
obj_del(_sel);
TAXT.cntx_pop();
~~


\del_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Usunięcie warunków opłaty
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć warunek opłaty dodatkowej?'@)
|| TAXN.del(,1)
?};
~~


\pr_war_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Przed redakcją wartości
::----------------------------------------------------------------------------------------------------------------------
1


\chk_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kontrola wprowadzonych warunków
::----------------------------------------------------------------------------------------------------------------------
_res:='';
{? TAXN.ROP='NN' & TAXN.FORM=''
|| FUN.info('Należy podać wartość dla warunku szczegółowego.'@);
   _res:='FORM'
?};
_res


\rek_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Rekord przed dla warunków
::----------------------------------------------------------------------------------------------------------------------
_res:='';
TAXXYZ.STS:=TAXXYZ.STZ:=TAXXYZ.TSP:=TAXXYZ.TZK:=null();
{? TAXN.R='S' || TAXXYZ.STS:=TAXN.SP
|? TAXN.R='Z' || TAXXYZ.STZ:=TAXN.SP
|? TAXN.R='X' || TAXXYZ.TSP:=TAXN.TSZ
|? TAXN.R='Y' || TAXXYZ.TZK:=TAXN.TSZ
?};
_txt1:={? TAXN.ANDOR='&' & TAXN.YESNO='='  || {? TAXN.ROP='NN'
                                              || {? (';SZ'*TAXN.R)>1 || 'jest równe' || 'jest równy' ?}
                                              || 'Dotyczy'
                                              ?}
       |? TAXN.ANDOR='&' & TAXN.YESNO='<>' || {? TAXN.ROP='NN'
                                              || {? (';SZ'*TAXN.R)>1 || 'jest różne od' || 'jest różny od' ?}
                                              || 'Nie dotyczy'
                                              ?}
       |? TAXN.ANDOR='|' & TAXN.YESNO='='  || {? TAXN.ROP='NN'
                                              || {? (';SZ'*TAXN.R)>1 || 'może być równe' || 'może być równy' ?}
                                              || 'Może dotyczyć'
                                              ?}
       |? TAXN.ANDOR='|' & TAXN.YESNO='<>' || {? TAXN.ROP='NN'
                                              || {? (';SZ'*TAXN.R)>1 || 'może być różne od' || 'może być różny od' ?}
                                              || 'Może nie dotyczyć'
                                              ?}
       || ''
       ?};
_txt2:={? TAXN.ROP='S'  || '%1 dokumentów sprzedaży'@[_txt1]
       |? TAXN.ROP='Z'  || '%1 dokumentów zakupu'@[_txt1]
       |? TAXN.ROP='M'  || '%1 dokumentów magazynowych'@[_txt1]
       |? TAXN.ROP='TN' || '%1 dokumentów wydania'@[_txt1]
       |? TAXN.ROP='TT' || '%1 dokumentów przyjęcia'@[_txt1]
       |? TAXN.R='K'    || 'Kontrahent %1'@[_txt1]
       |? TAXN.R='O'    || 'Odbiorca %1'@[_txt1]
       |? TAXN.R='M'    || 'Magazyn %1'@[_txt1]
       |? TAXN.R='S'    || 'Stanowisko sprzedaży %1'@[_txt1]
       |? TAXN.R='Z'    || 'Stanowisko zakupów %1'@[_txt1]
       |? TAXN.R='T'    || 'Typ dokumentu magazynowego %1'@[_txt1]
       |? TAXN.R='X'    || 'Typ dokumentu sprzedaży %1'@[_txt1]
       |? TAXN.R='Y'    || 'Typ dokumentu zakupu %1'@[_txt1]
       |? TAXN.R='F'    || 'Warunek wg formuły %1'@[_txt1]
       || ''
       ?};
TAXXYZ.TYPWAR:=_txt2;
TAXXYZ.VAL:={? TAXN.ROP<>'NN' || 'Warunek ogólny wg rodzaju dokumentów'
            |? TAXN.R='K' || '%1 - %2'[TAXN.KH().KOD,TAXN.KH().NAZ]
            |? TAXN.R='O' || '%1 - %2'[TAXN.KH_ODB().KOD,TAXN.KH_ODB().NAZ]
            |? TAXN.R='M' || '%1 - %2'[TAXN.MG().SYM,TAXN.MG().NAZ]
            |? TAXN.R='S'
             | TAXN.R='Z' || '%1 - %2'[TAXN.SP().KOD,TAXN.SP().NAZ]
            |? TAXN.R='T' || '%1 - %2'[TAXN.TM().T,TAXN.TM().NAZ]
            |? TAXN.R='X'
             | TAXN.R='Y' || '%1 - %2'[TAXN.TSZ().T,TAXN.TSZ().NAZ]
            |? TAXN.R='F' || TAXN.FORM
            || ''
            ?};
''


\bl_od_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wartość początkowa TAXN.OD
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.OD


\bl_do_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wartość początkowa TAXN.DO
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.DO


\bl_grp_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wartość początkowa TAXN.GRP
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.GRP


\bl_lp_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wartość początkowa TAXN.LP
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
TAXN.cntx_psh();
TAXN.index('GRP');
TAXN.prefix(TAXXYZ.TAXS,TAXXYZ.GRP,TAXXYZ.GRP,TAXXYZ.OD);
_res:={? TAXN.last() || TAXN.LP || 0 ?}+1;
TAXN.cntx_pop();
_res


\dol_grp_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dołączenie grupy warunków
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.win_edit('TAXN');
{? TAXXYZ.edit("exec('chk_grp_taxn','oplaty_def')")
|| __grtaxn.blank();
   __grtaxn.GRP:=TAXXYZ.GRP;
   __grtaxn.OD:=TAXXYZ.OD;
   __grtaxn.DO:=TAXXYZ.DO;
   {? __grtaxn.add(1) || exec('akt_taxn','oplaty_def',TAXO.TAXS,__grtaxn.GRP) ?}
?};
~~


\pop_grp_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dołączenie grupy warunków
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.GRP:=__grtaxn.GRP;
TAXXYZ.OD:=__grtaxn.OD;
TAXXYZ.DO:=__grtaxn.DO;
TAXXYZ.win_edit('TAXN');
{? TAXXYZ.edit("exec('chk_grp_taxn','oplaty_def',__grtaxn.ref())")
|| {? __grtaxn.GRP<>TAXXYZ.GRP | __grtaxn.OD<>TAXXYZ.OD | __grtaxn.DO<>TAXXYZ.DO
   || TAXN.cntx_psh();
      TAXN.index('GRP');
      TAXN.prefix(TAXXYZ.TAXS,__grtaxn.GRP,__grtaxn.GRP,__grtaxn.OD);
      {? TAXN.first()
      || {!
         |? _ref:=TAXN.ref(); _oki:=TAXN.next();
            TAXN.cntx_psh();
            TAXN.prefix();
            {? TAXN.seek(_ref)
            || TAXN.GRP:=TAXXYZ.GRP;
               TAXN.OD:=TAXXYZ.OD;
               TAXN.DO:=TAXXYZ.DO;
               TAXN.put(1)
            ?};
            TAXN.cntx_pop();
            _oki
         !}
      ?};
      TAXN.cntx_pop();
      __grtaxn.GRP:=TAXXYZ.GRP;
      __grtaxn.OD:=TAXXYZ.OD;
      __grtaxn.DO:=TAXXYZ.DO;
      {? __grtaxn.put(1) || exec('akt_taxn','oplaty_def',TAXO.TAXS,__grtaxn.GRP) ?}
   ?}
?};
~~


\del_grp_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Usunięcie wersji opłaty
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć grupę warunków opłaty dodatkowej?'@)
|| TAXN.cntx_psh();
   TAXN.index('GRP');
   TAXN.prefix(TAXXYZ.TAXS,__grtaxn.GRP,__grtaxn.GRP,__grtaxn.OD);
   {? TAXN.first() || {! |? TAXN.del() !} ?};
   TAXN.cntx_pop();
   __grtaxn.del(,1)
?};
~~


\chk_grp_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Walidacja grupy warunków
::   WE: [_a] - aktualny rekord lub null()
::   WY: '' lub akronim pola
::----------------------------------------------------------------------------------------------------------------------
_res:='';
_ref:={? var_pres('_a')=type_of(null()) || _a || null() ?};

{? TAXXYZ.GRP=''
|| FUN.info('Należy podać grupę warunków.'@);
   _res:='GRP'
|? TAXXYZ.OD=date(0,0,0)
|| FUN.info('Należy podać datę od której obowiązują warunki.'@);
   _res:='OD'
|? TAXXYZ.DO<>date(0,0,0) & TAXXYZ.DO<TAXXYZ.OD
|| FUN.info('Nieprawidłowy zakres dat.'@);
   _res:='DO'
|? ~exec('ctrl_grp_taxn','oplaty_def',_ref,TAXXYZ.OD,TAXXYZ.DO,TAXXYZ.GRP)
|| FUN.info('Nieprawidłowy zakres dat.'@);
   _res:='OD'
?};
_res


\ctrl_grp_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kontroluje inne wersje opłat dodatkowych
::   WE: _a - ref __grtaxn lub null()
::       _b - data OD
::       _c - data DO
::       _d - kod grupy
::   WY: 1-jest OK 0-nie jest dobrze
::----------------------------------------------------------------------------------------------------------------------
_res:=0;

_min:=date(0,0,0);
_max:=date(0,0,0);
__grtaxn.cntx_psh();
__grtaxn.clear();
__grtaxn.prefix(_d,);
{? __grtaxn.first()
||
   {!
   |? {? __grtaxn.ref()<>_a
      || _min:=__grtaxn.OD;
         _max:=__grtaxn.DO;
         {? __grtaxn.OD=_b | __grtaxn.DO=_b                    || _res:=-1
         |? __grtaxn.OD>_b & (_c=date(0,0,0) | __grtaxn.OD>_c) || _res:=1
         ?}
      ?};
      _res=0 & __grtaxn.next()
   !};
   {? _res=0 & {? _max=date(0,0,0) || _min=date(0,0,0) | _min<_b || _max<_b ?} || _res:=1 ?}
|| _res:=1
?};
__grtaxn.cntx_pop();
{? _res<0 || _res:=0 ?};
_res



\akt_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja dat na grupie warunków
::   WE: _a - ref TAXS
::       _b - grupa
::----------------------------------------------------------------------------------------------------------------------
__grtaxn.cntx_psh();
TAXN.cntx_psh();
__grtaxn.clear();
__grtaxn.prefix(_b,);
{? __grtaxn.last()
|| _dat:=__grtaxn.OD;
   {? __grtaxn.prev()
   || {!
      |? {? __grtaxn.DO=date(0,0,0)
         || TAXN.index('GRP');
            TAXN.prefix(_a,__grtaxn.GRP,__grtaxn.GRP,__grtaxn.OD);
            {? TAXN.first()
            || {!
               |? TAXN.DO:=_dat-1;
                  TAXN.put(1);
                  TAXN.next()
               !}
            ?};
            __grtaxn.DO:=_dat-1;
            __grtaxn.put(1)
         ?};
         _dat:=__grtaxn.OD;
         __grtaxn.prev()
      !}
   ?}
?};
__grtaxn.cntx_pop();
TAXN.cntx_pop();
~~


\renumtaxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: renumeracja DK
::   WE: [_a] - ''(domyślnie) - drag&drop, 'U','D','N'-akcja do przenumerowania
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (';UDN'*_a)>1 || _a || '' ?};

TAXN.cntx_psh();
{? _tryb=''
|| _ref:=dnd_info('dest_record');
   {? TAXN.seek(_ref) || exec('zmien_lp','#dragdrop','LP','GRP') ?}
|| exec('zmien_lpa','#dragdrop','LP','GRP',,,_tryb)
?};
TAXN.cntx_pop()


\po_kh_taxn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: po wskazaniu kontrahenta
::----------------------------------------------------------------------------------------------------------------------
{? TAXN.R='O'
|| {? TAXN.KH=null() | (TAXN.KH_ODB<>null() & TAXN.KH_ODB().KH<>TAXN.KH) || TAXN.KH_ODB:=null() ?};
   BEER.KH:=TAXN.KH;
   exec('po_r_taxn','oplaty_def')
?};
1


\inf_mat_taxs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: wyświetla listę indeksów powiązanych z opłatą
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.TAXS:=TAXS.ref();
_formikon:="{? TAXX.MOD*'K'
            || TAXX.MOD:=''; 'xwin16.png:9'
            |? TAXX.MOD*'W'
            || TAXX.MOD:=''; 'xwin16.png:3'
            || exec('pusta','#icon')
            ?}";
TAXX.win_fml('MAT',TAXX,'MOD',,'ICON_BEFORE',_formikon);
TAXX.win_sel('TAX');
TAXX.index('TAXS');
TAXX.prefix(TAXXYZ.TAXS);
TAXX.first();
TAXX.select();
~~


\rek_taxx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Rekord przed TAXX
::----------------------------------------------------------------------------------------------------------------------
BEER.MJM:=TAXX.M;
exec('taxx_kol','oplaty_dod')


\akt_mat_taxs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: aktualizuje atrybuty dla opłat dodatkowych
::----------------------------------------------------------------------------------------------------------------------
TAXS.get();
{? (_wsk:=TAXS.MOD*'A'; _wsk>0)
|| TAXX.index('TAXS');
   TAXX.prefix(TAXS.ref());
   {? TAXX.first()
   || {? FUN.ask('Czy aktualizacja atrybutów dla indeksów?'@)
      || _res:=0;
         {!
         |? _res:=exec('act_taxi','oplaty_dod',TAXX.TAXS);
            TAXX.next()
         !};
         {? _res
         || TAXX.MOD:=((_wsk-1)+TAXX.MOD)+(_wsk-TAXX.MOD);
            TAXX.put(1)
         ?}
      ?}
   || FUN.info('Brak przypisania opłaty %1 do kartoteki indeksów.\nAktualizacja niemożliwa.'@[TAXS.TAX])
   ?}
|| FUN.info('Nie modyfikowano definicji atrybutów opłaty %1.\nAktualizacja niemożliwa.'@[TAXS.TAX])
?}


\actTAXS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: aktualizuje znacznik dla TAXX
::   WE: _a - TAXN.ref() lub TAXO.ref() lub TAXT.ref()
::       _b - wartość przed
::       _c - wartość po
::----------------------------------------------------------------------------------------------------------------------
_res:='';
_taxs:=null();
TAXS.cntx_psh();
TAXO.cntx_psh();
TAXN.cntx_psh();
TAXT.cntx_psh();
{? ref_tab(_a)=TAXO
|| TAXO.prefix();
   {? TAXO.seek(_a)
   || _taxs:=TAXO.TAXS;
      {? _b<>_c || _res+='V' ?}
   ?}
|? ref_tab(_a)=TAXN
|| TAXN.prefix();
   {? TAXN.seek(_a)
   || _taxs:=TAXN.TAXS;
      {? _b<>_c || _res+='W' ?}
   ?}
|? ref_tab(_a)=TAXT
|| TAXT.prefix();
   {? TAXT.seek(_a)
   || _taxs:=TAXT.TAXS;
      {? _b<>_c || _res+='A' ?}
   ?}
?};
{? _res<>'' & _taxs<>null() & (TAXS.prefix(); TAXS.seek(_taxs)) & ~(TAXS.MOD*_res)
|| TAXS.MOD+=_res;
   TAXS.put(1)
?};
TAXS.cntx_pop();
TAXO.cntx_pop();
TAXN.cntx_pop();
TAXT.cntx_pop();
~~


\taxs_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kolorowanie dla opłaty dodatkowej
::   WY: schemat kolorów
::----------------------------------------------------------------------------------------------------------------------
{? TAXS.MOD<>''
|| 'TAXS#01#01'
|| ''
?}


\act_taxs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Zatwierdzenie zmian
::----------------------------------------------------------------------------------------------------------------------
TAXS.get();
_txt:='';
_txt+={? TAXS.MOD*'W' || '     - dodano lub zmieniono warunki naliczenia,\n'@ || '' ?};
_txt+={? TAXS.MOD*'A' || '     - dodano lub zmieniono atrybuty,\n'@ || '' ?};
_txt+={? TAXS.MOD*'V' || '     - dodano wersję.\n'@ || '' ?};
{? _txt<>'' || _txt:=(_txt-2)+'.\n' ?};
{? TAXS.MOD=''
|| FUN.info('Definicja opłaty dodatkowej %1 nie była modyfikowana.'@[TAXS.TAX])
|? FUN.ask('Definicja opłaty dodatkowej %1 była modyfikowana w zakresie:\n%2\n'
           'O ile zmiany zostały naniesione w kartotekach oraz naliczono ponownie opłaty\n'
           'warto usunąć informację o modyfikacji definicji opłaty.\n\n'
           'Czy zatwierdzić naniesienie zmian po modyfikacji definicji opłaty?'@[TAXS.TAX,_txt])
|| TAXS.MOD:='';
   TAXS.put(1)
?};
~~


\f3_taxn_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wybór wartości dla warunku
::----------------------------------------------------------------------------------------------------------------------
{? TAXN.R='K'
|| _bak:=KH.win_sel('?');
   KH.cntx_psh();
   _win_sel:=KH.mk_sel('Kartoteka kontrahentów'@,'P',,'kh_grpopl',,,,,'U');
   KH.win_fld(_win_sel,,'KOD',,,8,,1,'Kod'@);
   KH.win_fld(_win_sel,,'NAZ',,,40,,1,'Nazwa'@);
   KH.win_fld(_win_sel,,'KRAJ',,,10,,1,'Kraj'@);
   KH.win_fld(_win_sel,,'MIASTO',,,20,,1,'Miasto'@);
   KH.win_fld(_win_sel,,'UL',,,30,,1,'Ulica'@);
   KH.win_act(_win_sel,0,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
   KH.win_act(_win_sel,0,'Szukaj');
   KH.win_act(_win_sel,0,'Kolejność');

   KH.win_sel(_win_sel);
   KH.index('KOD');
   KH.prefix(2);
   KH.seek(TAXN.KH);
   {? KH.select(,1)
   || TAXN.KH:=KH.ref();
      TAXN.FORM:=KH.KOD
   ?};
   KH.cntx_pop();
   KH.win_sel(_bak)
|? TAXN.R='O'
|| _bak1:=KH.win_sel('?');
   _bak2:=KH_ODB.win_sel('?');
   KH.cntx_psh();
   KH_ODB.cntx_psh();
   _win_sel:=KH.mk_sel('Kartoteka kontrahentów'@,'P',,'kh_grpopl',,,,,'U');
   KH.win_fld(_win_sel,,'KOD',,,8,,1,'Kod'@);
   KH.win_fld(_win_sel,,'NAZ',,,40,,1,'Nazwa'@);
   KH.win_fld(_win_sel,,'KRAJ',,,10,,1,'Kraj'@);
   KH.win_fld(_win_sel,,'MIASTO',,,20,,1,'Miasto'@);
   KH.win_fld(_win_sel,,'UL',,,30,,1,'Ulica'@);
   KH.win_act(_win_sel,0,'Szukaj');
   KH.win_act(_win_sel,0,'Kolejność');
   _win_seo:=KH_ODB.mk_sel('Kartoteka odbiorców'@,'P',,'khogrpopl',,,,,'U');
   KH_ODB.win_fld(_win_seo,,'KOD',,,8,,1,'Kod'@);
   KH_ODB.win_fld(_win_seo,,'NAZ',,,40,,1,'Skrót'@);
   KH_ODB.win_fld(_win_seo,,'KRAJ',,,10,,1,'Kraj'@);
   KH_ODB.win_fld(_win_seo,,'MIASTO',,,20,,1,'Miasto'@);
   KH_ODB.win_fld(_win_seo,,'UL',,,30,,1,'Ulica'@);
   KH_ODB.win_act(_win_seo,0,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
   KH_ODB.win_act(_win_seo,0,'Szukaj');
   KH_ODB.win_act(_win_seo,0,'Kolejność');
   KH_ODB.win_sel(_win_seo);

   _fr:="";
   _win_grp:=KH.grp_make('Wybierz odbiorcę'@,_fr,'kh_opl_grp');
   _fr:="KH_ODB.index('KH_ODB');
         KH_ODB.prefix(KH.ref());
         grp_disp(KH_ODB,KH_ODB.win_sel('?'))
        ";
   _fb:=""; _fa:="";
   KH.grp_sel(_win_grp,KH,_win_sel,,_fr,,,20,_fb,_fa,,,'maximized_with_title');
   KH.grp_splt(_win_grp,,'horizontal','odbiorcy');
   _fr:=""; _fb:=""; _fa:="";
   KH.grp_sel(_win_grp,KH_ODB,_win_seo,,_fr,,,10,_fb,_fa,,,'maximized_with_title');

   KH.win_sel(_win_grp);
   KH.index('KOD');
   KH.prefix(2);
   KH.seek(TAXN.KH_ODB().KH);
   KH_ODB.seek(TAXN.KH_ODB);
   {? KH.select(,1)
   || TAXN.KH_ODB:=KH_ODB.ref();
      TAXN.KH:=TAXN.KH_ODB().KH;
      TAXN.FORM:=KH_ODB.KOD
   ?};
   KH.cntx_pop();
   KH_ODB.cntx_pop();
   KH.win_sel(_bak1);
   KH_ODB.win_sel(_bak2)
|? TAXN.R='M'
|| _bak:=MG.win_sel('?');
   MG.cntx_psh();

   MG.win_sel('SLO');
   MG.index('MAGAZYNY');
   MG.seek(TAXN.MG);
   {? MG.select(,1)
   || TAXN.MG:=MG.ref();
      TAXN.FORM:=MG.SYM
   ?};
   MG.cntx_pop();
   MG.win_sel(_bak)
|? TAXN.R='S'
|| _bak:=STS.win_sel('?');
   STS.cntx_psh();

   STS.win_sel('SEL');
   STS.index('SZ');
   STS.prefix('S');
   STS.seek(TAXN.SP);
   {? STS.select(,1)
   || TAXXYZ.STS:=STS.ref();
      TAXN.SP:=STS.ref();
      TAXN.FORM:=STS.KOD
   ?};
   STS.cntx_pop();
   STS.win_sel(_bak)
|? TAXN.R='Z'
|| _bak:=STS.win_sel('?');
   STS.cntx_psh();

   STS.win_sel('SEL');
   STS.index('SZ');
   STS.prefix('Z');
   STS.seek(TAXN.SP);
   {? STS.select(,1)
   || TAXXYZ.STZ:=STS.ref();
      TAXN.SP:=STS.ref();
      TAXN.FORM:=STS.KOD
   ?};
   STS.cntx_pop();
   STS.win_sel(_bak)
|? TAXN.R='T'
|| _bak:=TYPYDOK.win_sel('?');
   TYPYDOK.cntx_psh();

   TYPYDOK.win_sel('WYSTPOW');
   TYPYDOK.index('TYP');
   TYPYDOK.seek(TAXN.TM);
   {? TYPYDOK.select(,1)
   || TAXN.TM:=TYPYDOK.ref();
      TAXN.FORM:=TAXN.TM().T
   ?};
   TYPYDOK.cntx_pop();
   TYPYDOK.win_sel(_bak)
|? TAXN.R='X'
|| _bak:=TYPYSP.win_sel('?');
   TYPYSP.cntx_psh();
   VAR_DEL.delete('__taxtt');
   __taxtt:=1;
   TYPYSP.win_sel('WYSB');
   TYPYSP.index('SPRZAK');
   TYPYSP.prefix('N');
   TYPYSP.seek(TAXN.TSZ);
   {? TYPYSP.select(,1)
   || TAXN.TSZ:=TYPYSP.ref();
      TAXN.FORM:=TAXN.TSZ().T
   ?};
   TYPYSP.cntx_pop();
   VAR_DEL.delete('__taxtt');
   TYPYSP.win_sel(_bak)
|? TAXN.R='Y'
|| _bak:=TYPYSP.win_sel('?');
   TYPYSP.cntx_psh();
   VAR_DEL.delete('__taxtt');
   __taxtt:=1;
   TYPYSP.win_sel('WYSB');
   TYPYSP.index('SPRZAK');
   TYPYSP.prefix('T');
   TYPYSP.seek(TAXN.TSZ);
   {? TYPYSP.select(,1)
   || TAXN.TSZ:=TYPYSP.ref();
      TAXN.FORM:=TAXN.TSZ().T
   ?};
   TYPYSP.cntx_pop();
   VAR_DEL.delete('__taxtt');
   TYPYSP.win_sel(_bak)
?};
1


\po_taxn_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Po wyborze wartości dla warunku
::----------------------------------------------------------------------------------------------------------------------
{? TAXN.R='K' & TAXN.FORM<>''
|| _ref:=exec('FindInSet','#table','KH','KOD',TAXN.FORM,2,,1,,null());
   {? _ref=null() || _ref:=exec('FindInSet','#table','KH','KOD',TAXN.FORM,2,,,,null()) ?};
   {? _ref=null()
   || TAXN.KH:=null();
      TAXN.FORM:=''
   || TAXN.KH:=_ref;
      TAXN.FORM:=TAXN.KH().KOD
   ?}
|? TAXN.R='O'
|| {? TAXN.KH_ODB<>null() & TAXN.KH<>null() & TAXN.KH_ODB().KOD=TAXN.FORM
   || 1
   || TAXN.FORM:='';
      TAXN.KH:=null();
      TAXN_KH_ODB:=null();
      exec('f3_taxn_form','oplaty_def')
   ?}
|? TAXN.R='M' & TAXN.FORM<>''
|| _ref:=exec('FindInSet','#table','MG','MAGAZYNY',TAXN.FORM,,,1,,null());
   {? _ref=null() || _ref:=exec('FindInSet','#table','MG','MAGAZYNY',TAXN.FORM,,,,,null()) ?};
   {? _ref=null()
   || TAXN.MG:=null();
      TAXN.FORM:=''
   || TAXN.MG:=_ref;
      TAXN.FORM:=TAXN.MG().SYM
   ?}
|? TAXN.R='S' & TAXN.FORM<>''
|| _ref:=exec('FindInSet','#table','STS','SZ',TAXN.FORM,'S',,1,,null());
   {? _ref=null() || _ref:=exec('FindInSet','#table','STS','SZ',TAXN.FORM,'S',,,,null()) ?};
   {? _ref=null()
   || TAXXYZ.STS:=null();
      TAXN.SP:=null();
      TAXN.FORM:=''
   || TAXXYZ.STS:=_ref;
      TAXN.SP:=_ref;
      TAXN.FORM:=TAXN.SP().KOD
   ?}
|? TAXN.R='Z' & TAXN.FORM<>''
|| _ref:=exec('FindInSet','#table','STS','SZ',TAXN.FORM,'Z',,1,,null());
   {? _ref=null() || _ref:=exec('FindInSet','#table','STS','SZ',TAXN.FORM,'Z',,,,null()) ?};
   {? _ref=null()
   || TAXXYZ.STZ:=null();
      TAXN.SP:=null();
      TAXN.FORM:=''
   || TAXXYZ.STZ:=_ref;
      TAXN.SP:=_ref;
      TAXN.FORM:=TAXN.SP().KOD
   ?}
|? TAXN.R='T' & TAXN.FORM<>''
|| _ref:=exec('FindInSet','#table','TYPYDOK','TYP',TAXN.FORM,,,1,,null());
   {? _ref=null() || _ref:=exec('FindInSet','#table','TYPYDOK','TYP',TAXN.FORM,,,,,null()) ?};
   {? _ref=null()
   || TAXN.TM:=null();
      TAXN.FORM:=''
   || TAXN.TM:=_ref;
      TAXN.FORM:=TAXN.TM().T
   ?}
|? TAXN.R='X' & TAXN.FORM<>''
|| _ref:=exec('FindInSet','#table','TYPYSP','SPRZAK',TAXN.FORM,'N',,1,,null());
   {? _ref=null() || _ref:=exec('FindInSet','#table','TYPYSP','SPRZAK',TAXN.FORM,'N',,,,null()) ?};
   {? _ref=null()
   || TAXXYZ.TSP:=null();
      TAXN.TSZ:=null();
      TAXN.FORM:=''
   || TAXXYZ.TSP:=_ref;
      TAXN.TSZ:=_ref;
      TAXN.FORM:=TAXN.TSZ().T
   ?}
|? TAXN.R='Y' & TAXN.FORM<>''
|| _ref:=exec('FindInSet','#table','TYPYSP','SPRZAK',TAXN.FORM,'T',,1,,null());
   {? _ref=null() || _ref:=exec('FindInSet','#table','TYPYSP','SPRZAK',TAXN.FORM,'T',,,,null()) ?};
   {? _ref=null()
   || TAXXYZ.TZK:=null();
      TAXN.TSZ:=null();
      TAXN.FORM:=''
   || TAXXYZ.TZK:=_ref;
      TAXN.TSZ:=_ref;
      TAXN.FORM:=TAXN.TSZ().T
   ?}
?};
1


\wyr_TAXS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wyróżniki opłat dodatkowych
::----------------------------------------------------------------------------------------------------------------------
TAXXYZ.TAXS:=TAXS.ref();
TAXT.prefix();
TAXT.f_clear();
TAXT.f_set('KOD',,'TAXT.TAXS=:_a and (TAXT.R=\'M\' or TAXT.R=\'N\')',TAXXYZ.TAXS);
TAXV.index('TAXS');
TAXV.prefix(TAXXYZ.TAXS);
TAXV.select();
TAXT.f_clear();
~~


\bl_lp_taxv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wartość początkowa pola TAXV.LP
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
TAXV.cntx_psh();
TAXV.index('TAXS');
TAXV.prefix(TAXXYZ.TAXS);
_res:={? TAXV.last() || TAXV.LP || 0 ?}+1;
TAXV.cntx_pop();
_res


\chk_taxv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kontrola wprowadzonych wyróżników
::----------------------------------------------------------------------------------------------------------------------
_res:='';
_ref:={? (1+menu_txt())='P' || TAXV.ref() || null() ?};

{? TAXV.TAXT=null()
|| FUN.info('Należy podać wartość atrybutu opłaty dodatkowej.'@);
   _res:='TAXT'
|| _taxs:=TAXV.TAXS;
   _taxt:=TAXV.TAXT;
   TAXV.cntx_psh();
   TAXV.index('UNIK');
   TAXV.prefix(_taxs,_taxt);
   {? TAXV.first() & TAXV.ref()<>_ref
   || FUN.info('Dodano już do wyróżników podany atrybut opłaty dodatkowej.'@);
      _res:='TAXT'
   ?};
   TAXV.cntx_pop()
?};
_res


\renumtaxv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: renumeracja TAXV
::   WE: [_a] - ''(domyślnie) - drag&drop, 'U','D','N'-akcja do przenumerowania
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (';UDN'*_a)>1 || _a || '' ?};

TAXV.cntx_psh();
{? _tryb=''
|| _ref:=dnd_info('dest_record');
   {? TAXV.seek(_ref) || exec('zmien_lp','#dragdrop','LP','TAXS') ?}
|| exec('zmien_lpa','#dragdrop','LP','TAXS',,,_tryb)
?};
TAXV.cntx_pop()


\pr_taxv_taxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Przed redakcją atrybutu wyróżnika
::----------------------------------------------------------------------------------------------------------------------
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:20 69b447817e9cabb02d8b74b4368b46b5324eaee025a8a3fa403f79445389f0de4c535cdc5d812de85628cac71f2495179cd48281e3c783fca65869cd5fce567eaddb4cea0aaeeecac8e038b37cdec23f5e035a53dfb57d6f59d09b05d16d24a4c5e1ed137a8a2370ce29b86aa7410a03100364cad2ebb6ed952b7c2ce228868d

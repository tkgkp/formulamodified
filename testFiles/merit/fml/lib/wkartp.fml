:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: wkartp.fml
:: Utworzony: 2018/11/29
:: Autor: TMR
:: Systemy: KALI
::======================================================================================================================
:: Zawartosc: Wspólne formuły dla wydruków kart czasu pracy
::======================================================================================================================


\oblicz_godz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Wspólna formuła dla wydruków kart czasu pracy wg godzin/kalendarzy.
::       UWAGA!!! - kontekst pracy ustawiony w wydrukach
::   WE: _a [STRING] - typ wydruku 'rok' - roczna; 'msc' - miesięczna karta czasu pracy
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_typ:={? var_pres('_a')=type_of('') || _a || return(~~) ?};
_p_ref:=P.ref();

{? _typ<>'rok' & _typ<>'msc'
|| return(~~)
?};

var[3]:=date(var[8].ROK,var[8].MC,1);
var[4]:=date(var[8].ROK,var[8].MC,0);

var[5]:=var[6]:=0;
var[7]:=var[3]~3-1;

{! _ii:=1..obj_len(dz)
|! {! _ij:=1..obj_len(dz[_ii]) |! dz[_ii][_ij]:=0 !}
!};

{? _typ='rok'
|| {! _ii:=1..obj_len(suma)
   |! suma[_ii]:=0
   !}
?};

_sql:=('select  G.D, R.RN, G.G,'
         +'/* + MASK_FILTER(G,\'godz'+$(var[8].ROK)+'\',\'godz'+$(var[8].ROK+1)+'\') */ '
         +'1 from @G join R '
         +'where G.R=\'G\' and G.P=:_a and G.D>=to_date(:_b) and G.D<=to_date(:_c) '
         +'order by 1,2');
_G_TAB:=sql(_sql,P.ref, date(var[8].ROK,var[8].MC,1), date(var[8].ROK,var[8].MC,0));

_odd:=_od:=var[3];
_doo:=_do:=var[4];
_first:=1;
H.prefix(P.ref(),'Z');
{? {? ~H.find_le(var[3]) || H.find_ge(var[3]) || 1 ?}
|| {!
   |? {? H.OD<=var[4] & (H.DO>=var[3] | H.DO=#0)
      || {? _first
         || _odd:=_od:={? var[3]<H.OD || H.OD || var[3] ?};
            _do:={? H.DO=#0 | var[4]<H.DO || var[4] || H.DO ?}
         || _od:={? var[3]<H.OD || H.OD || var[3] ?};
            _doo:=_do:={? H.DO=#0 | var[4]<H.DO || var[4] || H.DO ?}
         ?};
         _first:=0;
         {! _ii:=(_od~3)..(_do~3)
         |! dz[_ii][24]:=H.WY;
            dz[_ii][29]:=1+(-H.CP().S)='u' & ~_G_TAB.find_key(date(_od~1,_od~2,_ii),54)
         !}
      ?} & H.next()
   !}
?};
var[3]:=_odd;
var[4]:=_doo;
_kal_ok:=__KAL.set_cal(P.KAL,var[8].ROK);
{! _dn:=1..date(var[8].ROK,var[8].MC,0)~3
|! dz[_dn][1]:=_dn;
   _dt:=(_dm:=date(var[8].ROK,var[8].MC,_dn))~4;
   dz[_dn][2]:=
      {? _dt=1 || 'Po'
      |? _dt=2 || 'Wt'
      |? _dt=3 || 'Śr'
      |? _dt=4 || 'Cz'
      |? _dt=5 || 'Pt'
      |? _dt=6 || 'So'
      || 'Nd'
      ?};
   dz[_dn][3]:=' ';
   _inRange:=(_dm>=P.DZA & (_dm<=P.DZ | P.DZ=date(0,0,0)));
   {? _kal_ok & _inRange & __KAL.get_day(date(var[8].ROK,var[8].MC,_dn))
   || _wsp:=dz[_dn][24];
      dz[_dn][3]:=__KAL.type_day(_dm);
      {? dz[_dn][29] || dz[_dn][5]:=__KAL.w_hours(date(var[8].ROK,var[8].MC,_dn),date(var[8].ROK,var[8].MC,_dn)) ?};
      var[5]+=(KAL_DEF.CZAS<>*0);
      _w_h:=__KAL.w_hours(date(var[8].ROK,var[8].MC,_dn),date(var[8].ROK,var[8].MC,_dn));
      dz[_dn][27]:=_w_h;
      {? _typ='msc' & KAL_DEF.TYP='R'
      || dz[_dn][41]:=KAL_DEF.POCZATEK;
         dz[_dn][42]:=KAL_DEF.POCZATEK;
         dz[_dn][43]:=date(var[8].ROK,var[8].MC,_dn)
      ?};
      var[6]+=_w_h
   ?}
!};
N.prefix('N',P.ref());
{? N.find_le(var[4]) & var[3]<=N.DO
|| {!
   |? {? _typ='msc' | (_typ='rok' & (N.OD<=var[4] & N.OD>=var[3] ) | ( N.DO<=var[4] & N.DO>=var[3]))
      || _start:={? N.OD<var[3] || var[3] || N.OD ?}~3;
         _stop:={? var[4]<N.DO || var[4] || N.DO ?}~3;
         _godz:={? __RUB.sys_attr(N.NB,199,N.OD) || N.NG || 0 ?};
         _ii:={? __RUB.sys_attr(N.NB,1321,N.OD) || 19
              |? __RUB.sys_attr(N.NB,112,N.OD)  || 13
              |? __RUB.sys_attr(N.NB,114,N.OD)  || 12
              |? __RUB.sys_attr(N.NB,115,N.OD) & ~(__RUB.sys_attr(N.NB,122722,N.OD) | __RUB.sys_attr(N.NB,122742,N.OD))
              || 11
              |? __RUB.sys_attr(N.NB,122722,N.OD) | __RUB.sys_attr(N.NB,122742,N.OD) || 25
              |? __RUB.sys_attr(N.NB,1225,N.OD) | __RUB.sys_attr(N.NB,1162,N.OD) ||  17
              |? __RUB.sys_attr(N.NB,121,N.OD)  | __RUB.sys_attr(N.NB,1221,N.OD) |
                 __RUB.sys_attr(N.NB,1222,N.OD) || 16
              |? __RUB.sys_attr(N.NB,111,N.OD)  || 10
              |? __RUB.sys_attr(N.NB,1161,N.OD) || 14
              |? __RUB.sys_attr(N.NB,124,N.OD)  || 15
              |? __RUB.sys_attr(N.NB,1314,N.OD) || 30
              || 18
              ?};
         _n_kal:=__RUB.sys_attr(N.NB,var[2],N.OD);
         {! _ij:=_start.._stop
         |! {? dz[_ij][29] & _godz || {? dz[_ij][5]-_godz<=0 || _godz-=dz[_ij][5]; dz[_ij][5]:=0 || dz[_ij][5]-=_godz ?}
            |? dz[_ij][29] & _ii<>25 || dz[_ij][5]:=0
            ?};
            {? _start=_stop
            || _vacTime:=N.NG
            || _vacTime:=__KAL.w_hours(date(var[8].ROK,var[8].MC,_ij),date(var[8].ROK,var[8].MC,_ij))
            ?};
            {? _n_kal
            || {? _typ='msc' || dz[_ij][_ii]+=_vacTime; dz[_ij][40]+=_vacTime || dz[_ij][_ii]:=1 ?}
            || {? dz[_ij][3]*'R' || {? _typ='msc' || dz[_ij][_ii]+=_vacTime; dz[_ij][40]+=_vacTime || dz[_ij][_ii]:=1 ?} ?}
            ?}
         !}
      ?};
      N.prev() & N.OD<=var[4] & var[3]<=N.DO
   !}
?};

{! _ii:=var[8].MC..var[8].MC+3
|! _mc:=_ii;
   {? _mc>12
   || _maska:='godz'+form(var[8].ROK+1,-4,,'9.');
      {? G.name()<>_maska
      || G.cntx_psh();
         G.use(_maska);
         G.index('MSCDKW')
      ?};
      {? _mc=13 || _mc:=1
      |? _mc=14 || _mc:=2
      |? _mc=15 || _mc:=3
      ?}
   ?};
   G.prefix(P.ref(),_mc,'G');
   {? G.first()
   || {!
      |? {? var[3]<=G.D & G.D<=var[4]
         || _rn:=G.K().RN;
            _dn:=G.D~3;
            _gd:=G.G;
            'godziny przepracowane';
            dz[_dn][5]+={? ~(_rn>58 & _rn<64) & _rn<>65 & _rn<>66 & _rn<>7011 & _rn<>57 & _rn<>49 &
                         (~dz[_dn][29] | (dz[_dn][29] & _rn<>54 & _rn<>7010)) & _rn<>7135 & _rn<>7138 || _gd ?};
            'godziny nadliczbowe 50%';
            {? _rn=55 | _rn=7008 || dz[_dn][20]+=_gd; dz[_dn][9]+=_gd ?};
            'godziny nadliczbowe 100%';
            {? _rn=56 | _rn=7009 || dz[_dn][21]+=_gd; dz[_dn][9]+=_gd ?};
            'godziny nocne';
            {? _rn=57 || dz[_dn][8]+=_gd ?};
            'dni wolne i swieta';
            {? _rn=58 | _rn=65 || {? dz[_dn][3]='S' || dz[_dn][6]+=_gd || dz[_dn][7]+=_gd ?} ?};
            'godziny realnie odpracowane';
            {? _rn=48 || dz[_dn][23]+=_gd ?};
            'odbiór nadgodzin';
            {? _rn=63
            || R_WYK.prefix(P.ref(),G.D);
               {? R_WYK.first()
               || _godz:=time(0,0,0);
                  {! |? _godz+=R_WYK.G; R_WYK.next() !};
                  {? _godz>*0 || _gd:=(*_godz/60)$2 ?}
               ?};
               dz[_dn][5]+=_gd;
               dz[_dn][9]+=_gd;
               {? dz[_dn][29]  || dz[_dn][5]-=_gd; {? dz[_dn][5]<0 || dz[_dn][5]:=0 ?} ?};
               dz[_dn][22]+=_gd
            ?};
            'praca poza siedzibą firmy';
            {? _rn=7135 & _typ='msc' || dz[_dn][39]+=_gd ?}
         ?};
         G.next()
      !}
   ?}
!};
{? _ii>12 || G.cntx_pop() ?};
{! _dn:=var[3]~3..var[4]~3
|! {? _typ='msc'
   || {? dz[_dn][5]
      || {? type_of(dz[_dn][41])<>type_of(time)
         || _dt:=date(var[8].ROK,var[8].MC,_dn);
            {! _dz:=#_dt-1 // -1..#P.DZA
            |? {? _kal_ok & __KAL.get_day(#_dz)
               || ~{? KAL_DEF.TYP='R'
                   || dz[_dn][41]:=KAL_DEF.POCZATEK;
                      dz[_dn][42]:=KAL_DEF.POCZATEK;
                      dz[_dn][43]:=_dt;
                      1
                   ?}
               ?}
            !}
         ?};
         {? type_of(dz[_dn][42])=type_of(time)
         || dz[_dn][42]+=60*dz[_dn][5];
            _days:=dz[_dn][42]~1/24 # 0;
            {? _days>=1 || dz[_dn][42]-=60*(24*_days); dz[_dn][43]+=_days ?}
         ?}
      || dz[_dn][41]:=0;
         dz[_dn][42]:=0;
         dz[_dn][43]:=0
      ?}
   ?};

   dz[_dn][4]:=(dz[_dn][5]<>0);
   _size:=obj_len(dz[_dn])-{? _typ='msc' || 4 || 0 ?};
   {! _ij:=4.._size
   |! {? _ij=25 & dz[_dn][_ij]
      || dz[_dn][11]:=dz[_dn][_ij];
         dz[obj_len(dz)][11]+=dz[_dn][_ij]
      || dz[obj_len(dz)][_ij]+=dz[_dn][_ij]
      ?}
   !}
!};
~~


\oblicz_wewy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Wspólna formuła dla wydruków kart czasu pracy wg danych we/wyj.
::       UWAGA!!! - kontekst pracy ustawiony w wydrukach
::   WE: _a [STRING] - typ wydruku 'rok' - roczna; 'msc' - miesięczna karta czasu pracy
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_typ:={? var_pres('_a')=type_of('') || _a || return(~~) ?};
_p_ref:=P.ref();

{? _typ<>'rok' & _typ<>'msc'
|| return(~~)
?};

R_PRACDN.prefix(P.ref());
R_WZCZ.prefix(P.name(),P.ref());
NGodz:=time(0,0,0);
NDni:=Cnt:=0;
_rok:=__param.ROK;
_mc:=__param.MC;

__KAL.set_cal(P.KAL,_rok);

{! _ii:=1..obj_len(dzien)
|! {! _ij:=1..obj_len(dzien[_ii]) |! dzien[_ii][_ij]:=0 !}
!};

_R_SPEC:=sql('select * from R_REJ_WW where 1=2');
_R_SPECK:=_R_SPEC.ndx_tmp(,1,'DZK',,,'DZ',,,'GD',,,'ST',,);
_R_SPEC.index(_R_SPECK);

_data_p:=date(_rok,_mc,1); _data_k:=date(_rok,_mc,0);
_data_k2:=date(_rok,_mc,0);
{? P.DZ<>#0 & P.DZ<_data_k || _data_k:=P.DZ ?};
Cnt:=_data_p~3-1;
A_OKRP.cntx_psh();
A_OKRP.index('A_OKRDP');
A_OKRP.prefix(P.ref());
{! _n:=1..7 |! SKLokr[_n][1]:='' !};
{! _n:=1..7 |! SKLokr[_n][2]:=0 !};
{? A_OKRP.find_le(_data_k) & A_OKRP.DO~1=_rok &  A_OKRP.DO~2=_mc
|| _dodokr:=obj_new(6);
   SKLokr[7][1]:=A_OKRP.OD; SKLokr[7][2]:=A_OKRP.DO;
   {? exec('roznica','prc_rozlicz',A_OKRP.OD,A_OKRP.DO)
   || _dodokr[1]:=7011; _dodokr[2]:=7029; _dodokr[3]:=7035; _dodokr[4]:=7038; _dodokr[5]:=7026; _dodokr[6]:=7057
   || _dodokr[1]:=66; _dodokr[2]:=7028; _dodokr[3]:=7036; _dodokr[4]:=7037; _dodokr[5]:=7027; _dodokr[6]:=7056
   ?};
   _ndx:=0;
   G.use('godz'+$_rok);
   G.index('MSCKW_KK');
   {! _n:=1..6
   |! G.prefix(P.ref(),_mc,'G',_dodokr[_n]);
      {? G.first()
      || _ok_okr:=0;
         {!
         |? {? G.D>=A_OKRP.OD & G.D<=A_OKRP.DO
            || SKLokr[_ndx+1][2]+=G.G; _ok_okr:=1
            ?};
            G.next()
         !};
         {? _ok_okr
         || _ndx+=1;
            SKLokr[_ndx][1]:=G.K().RT
         ?}
      ?}
   !};
   {? SKLokr[1][2]=0
   || _ndx:=0;
      {? _mc+1=13 || G.use('godz'+$(_rok+1)); G.index('MSCKW_KK'); _mies:=1 || _mies:=_mc+1 ?};
      {! _n:=1..5
      |! G.prefix(P.ref(),_mies,'G',_dodokr[_n]);
         {? G.first()
         || _ok_okr:=0;
            {!
            |? {? G.D>=A_OKRP.OD & G.D<=A_OKRP.DO
               || SKLokr[_ndx+1][2]+=G.G; _ok_okr:=1
               ?};
               G.next()
            !};
            {? _ok_okr
            || _ndx+=1;
               SKLokr[_ndx][1]:=G.K().RT
            ?}
         ?}
      !}
   ?}
?};
A_OKRP.cntx_pop();

{? _data_k>=_data_p
|| {! _dn:=_data_p~3.._data_k2~3
   |! dzien[_dn][1]:=_dn;
      _dt:=(_dm:=date(_rok,_mc,_dn))~4;
      dzien[_dn][2]:=
         {? _dt=1 || 'Po'@
         |? _dt=2 || 'Wt'@
         |? _dt=3 || 'Śr'@
         |? _dt=4 || 'Cz'@
         |? _dt=5 || 'Pt'@
         |? _dt=6 || 'So'@
         || 'Nd'@
         ?};
      dzien[_dn][3]:=' ';
      KAL_DEF.index('KAL_DEF');
      KAL_DEF.prefix();
      KAL_ROK.index('KAL_ROK');
      KAL_ROK.prefix();
      _date:=date(_data_p~1,_data_p~2,_dn);
      {? _date>=P.DZA & (_date<=P.DZ  | P.DZ=date(0,0,0))
      || {? KAL_ROK.find_key(
               {? R_WZCZ.find_le(_dm)
               || {? R_WZCZ.GRAFIK='T' || R_WZCZ.CZESC || R_WZCZ.KAL ?}
               || P.KAL
               ?},_rok
            )
         || {? KAL_DEF.prefix(KAL_ROK.ref()); KAL_DEF.find_key(_dm)
            || dzien[_dn][3]:=__KAL.type_day(_dm);
               dzien[_dn][17]:=__KAL.w_hours(_dm,_dm,,1);
               {? dzien[_dn][17]=0 || dzien[_dn][17]:=*0 || dzien[_dn][17]:=*((dzien[_dn][17]*60)$0) ?};
               NDni+=(KAL_DEF.CZAS<>*0);
               NGodz+=__KAL.w_hours(_dm,_dm,,1)*60
            ?}
         ?}
      ?};
      {? R_PRACDN.find_key(_dm)
::      || dzien[_dn][5]:=R_PRACDN.CP+R_PRACDN.CN;
::         dzien[_dn][4]:=(dzien[_dn][5]<>*0);
      || 'godz. nadliczbowe 50%';
         R_KWGODZ.prefix(P.ref(),_dm,55);
         {? R_KWGODZ.first()
         || {!
            |? R_WYK.index('R_WYKDN');
               R_WYK.prefix(P.ref(),_dm,55,R_KWGODZ.KK);
               {? R_WYK.first()
               || _wart:=time(0,0,0);
                  {! |? _wart+=R_WYK.G; R_WYK.next() !};
                  dzien[_dn][20]+=(R_KWGODZ.GODZ-_wart)
               || dzien[_dn][20]+=R_KWGODZ.GODZ
               ?};
               dzien[_dn][9]+=R_KWGODZ.GODZ;
               R_KWGODZ.next()
            !}
         ?};
         'godz. nadliczbowe 100%';
         R_KWGODZ.prefix(P.ref(),_dm,56);
         {? R_KWGODZ.first()
         || {!
            |? R_WYK.index('R_WYKDN'); R_WYK.prefix(P.ref(),_dm,56,R_KWGODZ.KK);
               {? R_WYK.first()
               || _wart:=time(0,0,0);
                  {! |? _wart+=R_WYK.G; R_WYK.next() !};
                  dzien[_dn][21]+=(R_KWGODZ.GODZ-_wart)
               || dzien[_dn][21]+=R_KWGODZ.GODZ
               ?};
               dzien[_dn][9]+=R_KWGODZ.GODZ;
               R_KWGODZ.next()
            !}
         ?};
         {! _ind:=20..21 |! {? type_of(dzien[_dn][_ind])=1 || dzien[_dn][_ind]:=*0 ?} !};

         'praca wymiar';
         R_KWGODZ.prefix(P.ref(),_dm,54);
         {? R_KWGODZ.first() || {! |? dzien[_dn][6]+=R_KWGODZ.GODZ; R_KWGODZ.next() !} ?};
         'godz. w dni wolne';
         R_KWGODZ.prefix(P.ref(),_dm,58);
         {? _typ='rok'
         || {? R_KWGODZ.first()
            || _godz:=*0;
               {! |? _godz+=R_KWGODZ.GODZ; R_KWGODZ.next() !};
               {? 1+dzien[_dn][3]='W'
               || dzien[_dn][7]+=_godz
               || dzien[_dn][47]+=_godz
               ?}
            ?}
         || {? R_KWGODZ.first() || {! |? dzien[_dn][7]+=R_KWGODZ.GODZ; R_KWGODZ.next() !} ?}
         ?};
         'godz. pracy w porze nocnej';
         R_KWGODZ.prefix(P.ref(),_dm,57);
         {? R_KWGODZ.first() || {! |? dzien[_dn][8]+=R_KWGODZ.GODZ; R_KWGODZ.next() !} ?};
         'L.g. dod. 50 etat';
         R_KWGODZ.prefix(P.ref(),_dm,68);
         {? R_KWGODZ.first()
         || {! |? dzien[_dn][9]+=R_KWGODZ.GODZ; dzien[_dn][20]+=R_KWGODZ.GODZ; R_KWGODZ.next() !}
         ?};
         'L.g. dod. 100 etat';
         R_KWGODZ.prefix(P.ref(),_dm,69);
         {? R_KWGODZ.first()
         || {! |? dzien[_dn][9]+=R_KWGODZ.GODZ; dzien[_dn][21]+=R_KWGODZ.GODZ; R_KWGODZ.next() !}
         ?};
         'godz. godziny ponadwymiarowe';
         R_KWGODZ.prefix(P.ref(),_dm,64);
         {? R_KWGODZ.first() || {! |? dzien[_dn][13]+=R_KWGODZ.GODZ; R_KWGODZ.next() !} ?};
         'godz. potencjalne przekroczenia sredniotygodniowe';
         R_KWGODZ.prefix(P.ref(),_dm,65);
         {? R_KWGODZ.first() || {! |? dzien[_dn][12]+=R_KWGODZ.GODZ; R_KWGODZ.next() !} ?};
         'godz. pracy poza siedzibą firmy';
         R_KWGODZ.prefix(P.ref(),_dm,7135);
         {? R_KWGODZ.first() || {! |? dzien[_dn][40]+=R_KWGODZ.GODZ; R_KWGODZ.next() !} ?};
         'wyliczenie czasu przepracowanego';
         dzien[_dn][5]:=*dzien[_dn][9]+*dzien[_dn][6]+*dzien[_dn][7]+*dzien[_dn][13]+*dzien[_dn][12];
         dzien[_dn][4]:=(dzien[_dn][5]<>*0)
      ?};
      'odbior godzin';
      R_WYK.index('R_WYKDN'); R_WYK.prefix(P.ref(),_dm);
      {? R_WYK.first() || {! |? dzien[_dn][22]+=R_WYK.G; R_WYK.next() !} ?};
      'odebrane godziny';
      R_WYK.index('R_WYKDO'); R_WYK.prefix(P.ref(),_dm);
      {? R_WYK.first()
      || {!
         |? _ok:=1;
            _rubryka:={? R_WYK.R().RN=55 || 7015 |? R_WYK.R().RN=56 || 7016 ?};
            {? _rubryka
            || G.use('godz'+$(_rok)); G.index('MSCDKW');
               G.prefix(P.ref(),_dm~2,'G',R_WYK.DN,_rubryka);
               {? G.first()
               || {!
                  |? {? #(10-G.F)=#R_WYK.ref() || _ok:=0 ?};
                     _ok & G.next()
                  !}
               ?};
               {? _ok
               || _m:=_dm~2;
                  {? _m+1=13 || G.use('godz'+$(_rok+1)); G.index('MSCDKW'); _mies:=1 || _mies:=_m+1 ?};
                  G.prefix(P.ref(),_mies,'G',R_WYK.DN,_rubryka);
                  {? G.first()
                  || {!
                     |? {? #(10-G.F)=#R_WYK.ref() || _ok:=0 ?};
                        _ok & G.next()
                     !}
                  ?}
               ?}
            ?};
            {? _ok || dzien[_dn][15]+=R_WYK.GO ?};
            R_WYK.next()
         !}
      ?};

:: wejścia i wyjścia wg doby pracowniczej
      {? __param.ASK
      || R_REJ_WW.prefix(P.ref,_dm);
         {? R_REJ_WW.first()
         || exec('wpisz_ts_tmp','wkartp',_dm,_R_SPEC);
            _R_SPEC.prefix(_dm);
            {? _R_SPEC.first & _R_SPEC.ST=2
            || dzien[_dn][26]:=_R_SPEC.GD
            ?};
            {? _R_SPEC.last & _R_SPEC.ST=3
            || dzien[_dn][27]:=_R_SPEC.GD;
               dzien[_dn][28]:=_R_SPEC.DZ
            ?}
         ?}
      ||
::    wejścia i wyjścia wg dnia pracy
         exec('wpisz_ts_tmp','wkartp',_dm,_R_SPEC);
         _R_SPEC.prefix(_dm);
         _wejscie:=_wyjscie:=time(99,0,0);
         {? _R_SPEC.first
         || {!
            |? {? _R_SPEC.ST=2 & _R_SPEC.ZM<>'D'
               || dzien[_dn][26]:=_R_SPEC.GD;
                  _wejscie:=_R_SPEC.GD;
                  0
               || _R_SPEC.next()
               ?}
            !}
         ?};
         {? _R_SPEC.last
         || {!
            |? {? _R_SPEC.ST=3 & _R_SPEC.ZM<>'D'
               || dzien[_dn][27]:=_R_SPEC.GD;
                  dzien[_dn][28]:=_R_SPEC.DZ;
                  _wyjscie:=_R_SPEC.GD;
                  0
               || _R_SPEC.prev()
               ?}
            !}
         ?};
         exec('wpisz_ts_tmp','wkartp',_dm-1,_R_SPEC);
         _R_SPEC.prefix(_dm-1);
         {? _R_SPEC.last & _wejscie=_R_SPEC.GD & _R_SPEC.ZM='D'
         || {!
            |? {? _R_SPEC.ST=2 & _R_SPEC.ZM='D'
               || dzien[_dn][26]:=_R_SPEC.GD;
                  0
               || _R_SPEC.prev() & _R_SPEC.ZM='D'
               ?}
            !}
         ?};
         exec('wpisz_ts_tmp','wkartp',_dm+1,_R_SPEC);
         _R_SPEC.prefix(_dm+1);
         {? _R_SPEC.first() & _R_SPEC.ZM='D' & _wyjscie=_R_SPEC.GD
         || {!
            |? {? _R_SPEC.ST=3 & _R_SPEC.ZM='D'
               || dzien[_dn][27]:=_R_SPEC.GD;
                  dzien[_dn][28]:=_R_SPEC.DZ;
                  0
               || _R_SPEC.next() & _R_SPEC.ZM='D'
               ?}
            !}
         ?}
      ?};



      'godziny do odpracowania i odpracowane';
      {? var_pres('R_ODP')>=100
      || R_ODP.prefix(P.ref(),_dm);
         {? R_ODP.first() || {! |? dzien[_dn][23]+=R_ODP.G; dzien[_dn][25]+=R_ODP.GO; R_ODP.next() !} ?};
         R_ODN.prefix(P.ref(),_dm);
         {? R_ODN.first() || {! |? dzien[_dn][16]+=R_ODN.G; R_ODN.next() !} ?};
         {? type_of(dzien[_dn][25])=5 || _ile_go:=dzien[_dn][25] || _ile_go:=*0 ?};
         {? _ile_go>*0 & type_of(dzien[_dn][21])=5
         || {? dzien[_dn][21]>_ile_go
            || dzien[_dn][21]-=_ile_go; _ile_go:=*0
            || _ile_go-=dzien[_dn][21]; dzien[_dn][21]:=*0
            ?}
         ?};
         {? _ile_go>*0 & type_of(dzien[_dn][20])=5
         || {? dzien[_dn][20]>_ile_go
            || dzien[_dn][20]-=_ile_go; _ile_go:=*0
            || _ile_go-=dzien[_dn][20]; dzien[_dn][20]:=*0
            ?}
         ?}
      ?}
   !};
   N.prefix('N',P.ref(),_rok,_mc);
   {? N.first()
   || {!
      |? _start:={? N.OD<date(_rok,_mc,1) || date(_rok,_mc,1) || N.OD ?}~3;
         _stop:={? date(_rok,_mc,0)<N.DO || date(_rok,_mc,0) || N.DO ?}~3;
         N.NB().RN;
         _ii:={? __RUB.sys_attr(N.NB,1321,N.OD) || 'Nn'
              |? __RUB.sys_attr(N.NB,112,N.OD) || 'Ub'
              |? __RUB.sys_attr(N.NB,114,N.OD) || 'Uc'
              |? __RUB.sys_attr(N.NB,115,N.OD) || 'Um'
              |? __RUB.sys_attr(N.NB,1225,N.OD) | __RUB.sys_attr(N.NB,1162,N.OD) || 'Op'
              |? __RUB.sys_attr(N.NB,121,N.OD) | __RUB.sys_attr(N.NB,1221,N.OD) | __RUB.sys_attr(N.NB,1222,N.OD) || 'Ch'
              |? __RUB.sys_attr(N.NB,111,N.OD)  || 'Uw'
              |? __RUB.sys_attr(N.NB,1161,N.OD) || 'Uo'
              |? __RUB.sys_attr(N.NB,124,N.OD)  || 'Zn'
              |? __RUB.sys_attr(N.NB,1314,N.OD) || 'Pr'
              || 'Nu'
              ?};
         {! _ij:=_start.._stop
         |! dzien[_ij][10]:=_ii;
            {? _typ='rok' & R.RN=1
            || dzien[_ij][11]:=*0
            || {? _start=_stop
               || _vacTime:=*((N.NG*60)$0)
               || _vacTime:=*((__KAL.w_hours(date(_rok,_mc,_ij),date(_rok,_mc,_ij))*60)$0)
               ?};
               {? _typ='rok'
               || {? _ii='Uw' || dzien[_ij][50]:=1 ?};
                  {? _ii='Um' || dzien[_ij][51]:=1 ?};
                  {? _ii='Uc' || dzien[_ij][52]:=1 ?};
                  {? _ii='Ub' || dzien[_ij][53]:=1 ?};
                  {? _ii='Uo' || dzien[_ij][54]:=1 ?};
                  {? _ii='Zn' || dzien[_ij][55]:=1 ?};
                  {? _ii='Ch' || dzien[_ij][56]:=1 ?};
                  {? _ii='Op' || dzien[_ij][57]:=1 ?};
                  {? _ii='Nu' || dzien[_ij][58]:=1 ?};
                  {? _ii='Nn' || dzien[_ij][59]:=1 ?};
                  {? _ii='Pr' || dzien[_ij][60]:=1 ?}
               |? _typ='msc'
               || {? _ii='Uw' || dzien[_ij][50]+=_vacTime
                  |? _ii='Um' || dzien[_ij][51]+=_vacTime
                  |? _ii='Uc' || dzien[_ij][52]+=_vacTime
                  |? _ii='Ub' || dzien[_ij][53]+=_vacTime
                  |? _ii='Uo' || dzien[_ij][54]+=_vacTime
                  |? _ii='Zn' || dzien[_ij][55]+=_vacTime
                  |? _ii='Ch' || dzien[_ij][56]+=_vacTime
                  |? _ii='Op' || dzien[_ij][57]+=_vacTime
                  |? _ii='Nu' || dzien[_ij][58]+=_vacTime
                  |? _ii='Nn' || dzien[_ij][59]+=_vacTime
                  |? _ii='Pr' || dzien[_ij][60]+=_vacTime
                  ?}
               ?};
               {? _typ='msc'
               || dzien[_ij][11]+=_vacTime
               || dzien[_ij][11]:=_vacTime
               ?}
::               {? _start=_stop
::               || dzien[_ij][11]+=*((N.NG*60)$0)
::               || dzien[_ij][11]+=*((__KAL.w_hours(date(_rok,_mc,_ij),date(_rok,_mc,_ij))*60)$0)
::               ?}
            ?}
         !};
         N.next()
      !}
   ?};
   _len:=obj_len(dzien);
   {! _dn:=date(_rok,_mc,1)~3..date(_rok,_mc,0)~3
   |! {! _ii:=4..obj_len(dzien[_dn])
      |! {? (_ii>=5 & _ii<=9) | (_ii>=20 & _ii<=23) | (_ii>=12 & _ii<=17) | (_ii>=25 & _ii<=27) | _ii=40
         || {? var_pres('['+$_ii+']',dzien[_dn])<>type_of(time()) || dzien[_dn][_ii]:=time(0,0,0) ?}
         |? _ii=28
         || {? var_pres('['+$_ii+']',dzien[_dn])<>type_of(date)
            || dzien[_dn][_ii]:=date(0,0,0)
            ?}
         ?};
         {? _ii=24 & dzien[_dn][_ii]
         || dzien[_dn][11]:=dzien[_dn][_ii]; dzien[_len][11]+=dzien[_dn][_ii]
         |? _ii<>28
         || dzien[_len][_ii]+=dzien[_dn][_ii]
         ?}
      !}
   !}
?};
~~


\wpisz_ts_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [19.22]
:: OPIS: Wypełnienie tabeli tymczasowej informacjami we/wy.
::   WE: _a - data kwalifikacji
::       _b - alias tabeli tymczasowej
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_R_SPEC:=_b;
R_REJ_WW.cntx_psh();
_R_SPEC.prefix();
{? _R_SPEC.first()
|| {! |? _R_SPEC.del !}
?};
{! _ind:=-1..1
|! {? _ind=-1
   || _rok:=_a~1; _mc:=_a~2+1; {? _mc>12 || _rok+=1; _mc:=1 ?};
      _data:=date(_rok,_mc,1)
   || _data:=date(_a~1,_a~2-_ind,1)
   ?};
   MASK.Use('R_REJ_WW',_data~1,_data~2);
   R_REJ_WW.index('R_REJ_WX'); R_REJ_WW.prefix(P.ref(),_a);
   {? R_REJ_WW.first()
   || {!
      |? _R_SPEC.blank();
         _R_SPEC.CZ:=R_REJ_WW.CZ;
         _R_SPEC.DZ:=R_REJ_WW.DZ;
         _R_SPEC.GD:=R_REJ_WW.GD;
         _R_SPEC.ST:=R_REJ_WW.ST;
         _R_SPEC.TP:=R_REJ_WW.TP;
         _R_SPEC.RD:=R_REJ_WW.RD;
         _R_SPEC.ZM:=R_REJ_WW.ZM;
         _R_SPEC.DZK:=R_REJ_WW.DZK;
         _R_SPEC.add(1);
         R_REJ_WW.next()
      !};
      _R_SPEC.first
  ?}
!};
R_REJ_WW.cntx_pop()

:Sign Version 2.0 jowisz:1045 2023/07/21 13:25:28 7ea0e80096affeebf2710c3f241b450797f0fd4b86fbb99a8fea74968b224102cd48845e7d0712e9df513f3cdc6479e3f787a2d94b4df45771f6bd03c669da24290381312609ec1c82051014d7da3b607d2898435ccf730911b942b8988e4d3347bc2fd935496934b5e334b39a6cf2994b96da54288903b971ca0c22646fe6fe

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zbl.fml
:: Utworzony: 22.09.2020
:: Autor: AWI
:: Systemy: ZBL
::======================================================================================================================
:: Zawartość: Obsługa dokumentów Businesslink
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Formuła dziedziny ZBL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
~~


\zbl_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Obszar Dokumenty Businesslink
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=exec('env','zbl_dok');

{? _env.initObszar()
||
   _env.winMain();

:: Ustalenie treści linku
   _params:=params_get();
   {? type_of(_params)>0 & var_pres('LINK',_params)=type_of('')
   || _link:=_params.LINK
   || _link:=''
   ?};

   params_set('env',_env,'LINK',_link);

   {? _link=''
   || DOKUM.select()
   || DOKUM.cntx_psh();
      DOKUM.prefix();
      {? DOKUM.seek(_link)
      || _win_main:='bl_wer_%1%2'[{? DOKUM.RODZAJ_K='W' || 'out' || 'in' ?},{? DOKUM.BL_VISIB='T' || '' || '_all' ?}];
          DOKUM.select(,1,5,,_win_main)
      || FUN.info('Brak dokumentu.'@)
      ?};
      DOKUM.cntx_pop()
   ?};

   _env.doneObszar()
?}


\dokum_tr_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Formuła wyzwalacza przed tabeli DOKUM dla Dokumentów Businesslink
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
DOKUM.BL_VISIB:='N';
DOKUM.BL_VDZK:='N';
DOKUM.BL_VORD:='N';
{? DOKUM.BL_STAT<>''
||
   exec('set_stage','zbl',DOKUM.REFSQL);
   {? var_press('DOKR',DOKUM)>0 & DOKUM.DOKR<>'' & DOKUM.DOKR<>DOKUM.REFSQL || exec('set_stage','zbl',DOKUM.DOKR) ?};
   exec('current_stage','zbl')
?};
1


\set_stage
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Ustala status pól STATKSEF i STATBC tabeli podanej jako złączenie w parametrze
::   WE: _a - Ref tabeli
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_Tab:=ref_tab(_ref);

{? DOKUM.BL_STAT=exec('before_send','zbl_stat')
      |
   DOKUM.BL_STAT=exec('validation_failed','zbl_stat')
      |
   DOKUM.BL_STAT=exec('waiting_auth','zbl_stat')
      |
   DOKUM.BL_STAT=exec('ready_to_send','zbl_stat')
      |
   DOKUM.BL_STAT=exec('ready_to_registration','zbl_stat')
      |
   DOKUM.BL_STAT=exec('during_registration','zbl_stat')
      |
   DOKUM.BL_STAT=exec('registration_failed','zbl_stat')
      |
   DOKUM.BL_STAT=exec('cancelled_portal','zbl_stat')
||
   DOKUM.BL_VISIB:='T';
   {? DOKUM.RODZAJ_K='P'
   || {? 3+DOKUM.BL_TYPE='INV' | DOKUM.BL_TYPE='DOC'
      || DOKUM.BL_VDZK:='T'
      ?};
      {? DOKUM.BL_TYPE='ORD' | DOKUM.BL_TYPE='DOC'
      || DOKUM.BL_VORD:='T'
      ?}
   ?}
?};

{? type_of(_Tab)<>type_of(~~) & (_Tab=FAKS | _Tab=DOK)
||
:: Obsługa tylko gdy tabela źródłowa jest powiązana z KSeF
   {? var_pres('__MANSKSEF')=type_of('') & (__MANSKSEF='BC' | __MANSKSEF='BC_ATTA_ADD')
   ||
      ~~
   ||
      _statksef:=exec('FindAndGet','#table',_Tab,_ref,,"STATKSEF",'');

      {? DOKUM.BL_STAT=exec('before_send','zbl_stat')
            |
         DOKUM.BL_STAT=exec('validation_failed','zbl_stat')
            |
         DOKUM.BL_STAT=exec('waiting_auth','zbl_stat')
            |
         DOKUM.BL_STAT=exec('ready_to_send','zbl_stat')
      ||
         exec('FindAndGet','#table',_Tab,_ref,,"
            {? STATKSEF<>'' & STATKSEF<>exec('ksef_donot_applay','zbl_stat')
            ||
               _put:=0;
               {? STATKSEF<>DOKUM.BL_STAT || STATKSEF:=DOKUM.BL_STAT; _put:=1 ?};
               {? KSEFAUTH<>DOKUM.KSEFAUTH || KSEFAUTH:=DOKUM.KSEFAUTH; _put:=1 ?};
               {? _put || put() ?}
            ?}
         ")

      |? DOKUM.BL_STAT=exec('send','zbl_stat') & _statksef<>'' & _statksef<>exec('ksef_donot_applay','zbl_stat')
      ||
         {? DOKUM.KSEFSTAT='CONFIRMED'
         ||
            exec('FindAndGet','#table',_Tab,_ref,,"
               _put:=0;
               _stat:=exec('ksef_send_confirmed','zbl_stat');
               {? STATKSEF<>_stat
               || STATKSEF:=_stat;
                  NRKSEF:=DOKUM.NRKSEF;
:: literówka jest nieprzypadkowa - data ksef jest datą wysłania do ksef a nie potwierdzenia w ksef
                  KSEFCDAT:=DOKUM.KSEFSDAT;
                  _put:=1
               ?};
               {? _put || put() ?}
            ")

         |? DOKUM.KSEFSTAT='READY2SIGN'
         ||
            exec('FindAndGet','#table',_Tab,_ref,,"
               _put:=0;
               _stat:=exec('ksef_send_signature_required','zbl_stat');
               {? STATKSEF<>_stat
               || STATKSEF:=_stat;
                  _put:=1
               ?};
               {? _put || put() ?}
            ")

         |? DOKUM.KSEFSTAT='SIGNED'
         ||
            exec('FindAndGet','#table',_Tab,_ref,,"
               _put:=0;
               _stat:=exec('ksef_send_signed','zbl_stat');
               {? STATKSEF<>_stat
               || STATKSEF:=_stat;
                  _put:=1
               ?};
               {? _put || put() ?}
            ")

         |? DOKUM.KSEFSTAT='READY2GO'
         ||
            exec('FindAndGet','#table',_Tab,_ref,,"
               _put:=0;
               _stat:=exec('verification_required','zbl_stat');
               {? STATKSEF<>_stat
               || STATKSEF:=_stat;
                  _put:=1
               ?};
               {? _put || put() ?}
            ")

         |? DOKUM.KSEFSTAT='ERROR' |
            DOKUM.KSEFSTAT='ERROR_READY' |
            DOKUM.KSEFSTAT='ERROR_CONFIRMED' |
            DOKUM.KSEFSTAT='ERROR_SEND'
         ||
            exec('FindAndGet','#table',_Tab,_ref,,"
               _put:=0;
               _stat:=exec('ksef_send_rejected','zbl_stat');
               {? STATKSEF<>_stat
               || STATKSEF:=_stat;
                  _put:=1
               ?};
               {? KSEF_ERR<>'T'
               || KSEF_ERR:='T';
                  _put:=1
               ?};
               {? _put || put() ?}
            ")
         |? DOKUM.KSEFSTAT='SENT' |
            DOKUM.KSEFSTAT='READY' & DOKUM.INFO_END='N'
         ||
            exec('FindAndGet','#table',_Tab,_ref,,"
               _put:=0;
               _stat:=exec('ksef_send_unconfirmed','zbl_stat');
               {? STATKSEF<>_stat
               || STATKSEF:=_stat;
                  _put:=1
               ?};
               {? _put || put() ?}
            ")
         ?}
      ?}
   ?};

:: Obsługa stanu dla Businesscheck
:: >> NOTREADY, SENT, CONFIRMED, WARNING, ERROR, UNAVAILABLE
:: - odłożenie statusu biznesowego na tabelę nadrzędną (FAKS, DOK)
:: - odłożenie statusu biznesowego i znacznika kontroli BC na tabelę DOKUMZ (rekord dotyczący KSeF)
   {? exec('upgrade2325_blbc1','zbl')
   || _blc_type:=exec('sendToBusinessCheck','zbl_dok',DOKUM.ref());
      {? DOKUM.BLC_STAT='' | DOKUM.BL_STAT='NOTREADY'
      || _statbc:=
            {? _blc_type  | DOKUM.BLC_KIND='_'
            || {? DOKUM.BSEND= 'T' || exec('check_pending','zbl_stat') || exec('check_check','zbl_stat') ?}
            || exec('check_none','zbl_stat')
            ?}
      |? DOKUM.BLC_STAT='SENT' | DOKUM.BLC_STAT='READY'
      || _statbc:=exec('check_pending','zbl_stat')
      |? DOKUM.BLC_STAT='CONFIRMED'
      || _statbc:=exec('check_ok','zbl_stat')
      |? DOKUM.BLC_STAT='WARNING'
      || _statbc:=exec('check_warning','zbl_stat')
      |? DOKUM.BLC_STAT='ERROR'
      || _statbc:=exec('check_error','zbl_stat')
      |? DOKUM.BLC_STAT='UNAVAILABLE'
      || _statbc:=exec('check_na','zbl_stat')
      || _statbc:=exec('check_none','zbl_stat')
      ?};
:: Typ wysyłki do BusinessCheck do zapisu w historii kontroli
      {? DOKUM.BLC_STAT<>''
      || {? DOKUM.BLC_KIND='_'
         || _blc_type:=exec('check_hist_check_only','zbl_stat')
         |? _blc_type=0
         || _blc_type:=exec('check_hist_no_check','zbl_stat')
         |? _blc_type=1
         || _blc_type:=exec('check_hist_check_auto','zbl_stat')
         |? _blc_type=2
         || _blc_type:=exec('check_hist_check_manual','zbl_stat')
         |? _blc_type=3
         || _blc_type:=exec('check_hist_check_ok','zbl_stat')
         |? _blc_type=4
         || _blc_type:=exec('check_hist_check_always','zbl_stat')
         |? DOKUM.BLC_STAT<>'UNAVAILABLE'
         || _blc_type:=exec('check_na','zbl_stat')
         || _blc_type:=''
         ?}
      || _blc_type:=''
      ?};
      _obj:=obj_new(4);
      _obj[1]:=_statbc;
      _obj[2]:=DOKUM.KSEFSTAT;
      _obj[3]:=_blc_type;
      _obj[4]:=DOKUM.BLC_KIND;
      exec('FindAndGet','#table',_Tab,_ref,,"
         _put:=0;
         _statbc:=_b[1];
         _ksefstat:=_b[2];
         _blckind:=_b[4];
         {? STATBC<>_statbc
         || STATBC:=_statbc;
            {? _ksefstat='NOTREADY' & _blckind<>'_' &
               (_statbc=exec('check_error','zbl_stat') | _statbc=exec('check_warning','zbl_stat'))
            || STATKSEF:=exec('validation_failed','zbl_stat')
            ?};
            _put:=1
         ?};
         {? _put || put() ?}
      ",,_obj);
      exec('FindAndGet','#table',DOKUMZ,exec('dokumz','edi_fo_ufd',DOKUM.ref(),'ksef'),,"
         _put:=0;
         _statbc:=_b[1];
         _blc_type:=_b[3];
         {? STATBC<>_statbc
         || STATBC:=_statbc;
            _put:=1
         ?};
         {? _statbc<>exec('check_none','zbl_stat') & BC<>'T'
         || BC:='T';
            _put:=1
         ?};
         {? BLC_TYPE='' & _blc_type<>''
         || BLC_TYPE:=_blc_type;
            _put:=1
         ?};
         {? _put || put() ?}
      ",,_obj)
   ?}

:: Obsługa dla ZD_NAG Businesslink
|? type_of(_Tab)<>type_of(~~) & (_Tab=ZD_NAG)
|| _blstat:=exec('FindAndGet','#table',_Tab,_ref,,"BL_STAT",'');
   {? DOKUM.BL_STAT=exec('before_send','zbl_stat')
         |
      DOKUM.BL_STAT=exec('validation_failed','zbl_stat')
         |
      DOKUM.BL_STAT=exec('ready_to_send','zbl_stat')
         |
      DOKUM.BL_STAT=exec('send','zbl_stat')
         |
      DOKUM.BL_STAT=exec('cancelled_erp','zbl_stat')
         |
      DOKUM.BL_STAT=exec('cancelled_portal','zbl_stat')
         |
      DOKUM.BL_STAT=exec('abandoned','zbl_stat')
   || exec('FindAndGet','#table',_Tab,_ref,,"
         {? BL_STAT<>''
         || _put:=0;
            {? BL_STAT<>DOKUM.BL_STAT
            || BL_STAT:=DOKUM.BL_STAT;
               _put:=1
            ?};
            {? _put || put() ?}
         ?}
      ")
   ?}

?}


\dokum_tr_del_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Formuła wyzwalacza przed usuń tabeli DOKUM dla Dokumentów Businesslink
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? DOKUM.BL='T'
||
   {? DOKUM.REFSQL<>'' & ref_tab(DOKUM.REFSQL)=FAKS
   || {? exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"
            {? FAKS.SZ='S'
            ||
               {? DOKUM.BLC_KIND='' || FAKS.STATKSEF:=exec('faks_statksef','faktury_nag1') ?};
               {? exec('upgrade2325_blbc1','zbl')
               || FAKS.STATBC:=
                     {? exec('is_businesscheck','businesslink3')>0 & {? FAKS.T <> null() || FAKS.T().KSEF_BC > 0 || 0 ?} &
                        FAKS.STATKSEF<>exec('ksef_donot_applay','zbl_stat')
                     || exec('check_check','zbl_stat')
                     || exec('check_none','zbl_stat')
                     ?};
                  FAKS.KSEFAUTH:=null();
                  {? DOKUM.BLC_KIND='' || FAKS.EDI_W:='N' ?};
                  FAKS.put()
               ?}
            ||
               1
            ?}
            ",1)=0
      || _result:=0
      ?}
   ?};
   {? DOKUM.REFSQL<>'' & ref_tab(DOKUM.REFSQL)=ZD_NAG
   || {? exec('FindAndGet','#table',ZD_NAG,DOKUM.REFSQL,,"
            ZD_NAG.BL_STAT:=exec('zdnag_blstat_bl','zamdst_nag');
            ZD_NAG.EDI_W:='N';
            ZD_NAG.put()
            ",1)=0
      || _result:=0
      ?}
   ?};
   _dok_ref:={? var_pres('DOKR',DOKUM)>0 & DOKUM.DOKR<>''
             || DOKUM.DOKR
             |? DOKUM.REFSQL<>'' & ref_tab(DOKUM.REFSQL)=DOK
             || DOKUM.REFSQL
             || ''
             ?};
   {? _dok_ref<>''
   || {? exec('FindAndGet','#table',DOK,_dok_ref,,"
            {? exec('upgrade2325_blbc1','zbl')
            || {? DOKUM.BLC_KIND='' || exec('set_dok_ksef','dok_ksef',1); DOK.EDI_W:='N' ?};
               DOK.STATBC:=exec('set_dok_statbc','zbl')
            ?};
            DOK.KSEFAUTH:=null();
            {? -DOK.A<>'t' || exec('fakskh_auto_del','faktury_wspolne',DOK.ref()) ?};
            DOK.put()
            ",1)=0
      || _result:=0
      ?}
   ?}
?};
_result


\bl_dokum_seek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: ustawienie się na załączniku w celu podglądu PDF w oknie nagłówka dokumentu
::   WE: [_a] - uchwyt do tabeli FAKS, DOK lub ZD_NAG
::       [_b] - szukanie również w archiwum 0 - nie (domyślnie), 1 - tak
::       [_c] - bez pomijania DOKUM.BLC_KIND<>'' 0 - nie (domyślnie), 1 - tak
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_Tab:={? var_pres('_a')=type_of(FAKS) | var_pres('_a')=type_of(ZD_NAG) | var_pres('_a')=type_of(ZK_N) || _a || ~~ ?};
_arch:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_blc_tez:={? var_pres('_c')=type_of(0) || _c || 0 ?};
POLA.STATKSEF:='';
_blank:=type_of(_Tab)=type_of(~~);
_seek:=0;
DOKUM.use('dokum');
{? ~_blank
|| _ref:='';
   {? _Tab=DOK
   || {? 1+_Tab.DOKZRODL='D' |  1+_Tab.DOKZRODL='K'
      || FAKS.cntx_psh(); FAKS.prefix();
         {? FAKS.seek(#(4-_Tab.DOKZRODL),'faktu'+(3+(1-_Tab.DOKZRODL)))
         || _ref:=$FAKS.ref()
         ?};
         FAKS.cntx_pop()
      ?}
   ?};
   {? _ref='' || _ref:=$_Tab.ref() ?};
   {? var_pres('STATKSEF',_Tab)>0
   || POLA.STATKSEF:=_Tab.STATKSEF
   ?};
:: BL_3 - szukanie głównego załącznika: DOKUM.DG_UID=''
   DOKUM.index('BL_3');
   DOKUM.prefix(REF.FIRMA,'T',_ref,'',);
   _loop:=DOKUM.last();
   {!
   |? _loop
   |!
      {? DOKUM.BLC_KIND='' | _blc_tez
      || _seek:=1
      ?};
      _loop:=~_seek & DOKUM.prev()
   !};
   {? ~_seek & _arch
   || _names:=DOKUM.names();
      {? _names.last()
      || {!
         |? {? _names.NAME<>'dokum'
            || DOKUM.use(_names.NAME);
               DOKUM.index('BL_3');
               DOKUM.prefix(REF.FIRMA,'T',_ref,'',);
               _loop:=DOKUM.last();
               {!
               |? _loop
               |!
                  {? DOKUM.BLC_KIND='' | _blc_tez
                  || _seek:=1
                  ?};
                  _loop:=~_seek & DOKUM.prev()
               !}
            ?};
            ~_seek & _names.prev()
         !}
      ?};
      &_names;
      {? ~_seek
      || DOKUM.use('dokum')
      ?}
   ?};
:: BL_2 - szukanie dowolnego załącznika
   {? ~_seek
   || DOKUM.index('BL_2');
      DOKUM.prefix(REF.FIRMA,'T',_ref,);
      _loop:=DOKUM.last();
      {!
      |? _loop
      |!
         {? DOKUM.BLC_KIND='' | _blc_tez
         || _seek:=1
         ?};
         _loop:=~_seek & DOKUM.prev()
      !};
      {? ~_seek & _arch
      || _names:=DOKUM.names();
         {? _names.last()
         || {!
            |? {? _names.NAME<>'dokum'
               || DOKUM.use(_names.NAME);
                  DOKUM.index('BL_2');
                  DOKUM.prefix(REF.FIRMA,'T',_ref,);
                  _loop:=DOKUM.last();
                  {!
                  |? _loop
                  |!
                     {? DOKUM.BLC_KIND='' | _blc_tez
                     || _seek:=1
                     ?};
                     _loop:=~_seek & DOKUM.prev()
                  !}
               ?};
               ~_seek & _names.prev()
            !}
         ?};
         &_names;
         {? ~_seek
         || DOKUM.use('dokum')
         ?}
      ?}
   ?};
   {? ~_seek || _blank:=1 ?}
?};
{? _blank || DOKUM.blank(1) ?};
_seek


\dokum_seek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: ustawienie się na załączniku w celu podglądu PDF w oknie nagłówka dokumentu
::   WE: [_a] - uchwyt do tabeli FAKS, DOK, ZD_NAG lub ZK_N
::       [_b] - szukanie w archiwum 0 - nie (domyślnie), 1 - tak
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_Tab:={? var_pres('_a')=type_of(FAKS) | var_pres('_a')=type_of(ZD_NAG) | var_pres('_a')=type_of(ZK_N) || _a || ~~ ?};
_arch:={? var_pres('_b')=type_of(0) || _b || 0 ?};
POLA.STATKSEF:='';
_blank:=type_of(_Tab)=type_of(~~);
_seek:=0;
DOKUM.use('dokum');
{? ~_blank
|| _ref:='';
   {? _Tab=DOK
   || {? 1+_Tab.DOKZRODL='D' | 1+_Tab.DOKZRODL='K'
      || FAKS.cntx_psh(); FAKS.prefix();
         {? FAKS.seek(#(4-_Tab.DOKZRODL),'faktu'+(3+(1-_Tab.DOKZRODL)))
         || _ref:=$FAKS.ref()
         ?};
         FAKS.cntx_pop()
      ?}
   ?};
   {? _ref='' || _ref:=$_Tab.ref() ?};
   {? var_pres('STATKSEF',_Tab)>0 || POLA.STATKSEF:=_Tab.STATKSEF ?};
   DOKUM.index('AUTOADD');
   DOKUM.prefix(REF.FIRMA,_ref,'T',);
   _seek:=DOKUM.first();
   {? ~_seek & _arch
   || _names:=DOKUM.names();
      {? _names.last()
      || {!
         |? {? _names.NAME<>'dokum'
            || DOKUM.use(_names.NAME);
               DOKUM.index('AUTOADD');
               DOKUM.prefix(REF.FIRMA,_ref,'T',);
               _seek:=DOKUM.first()
            ?};
            ~_seek & _names.prev()
         !}
      ?};
      &_names;
      {? ~_seek
      || DOKUM.use('dokum')
      ?}
   ?};
   {? ~_seek || _blank:=1 ?}
?};
{? _blank || DOKUM.blank(1) ?};
_seek


\current_stage
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Bieżacy etap
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.REFSQL='' || DOKUM.OBKONETP:=DOKUM.BL_STAT
|? (5+DOKUM.REFSQL)='faktu' || DOKUM.OBKONETP:='Dokument zakupu - %1'@[DOKUM.BL_STAT]
?}


\kh_dod_ksefkhod_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wartość początkowa KH_DOD.KSEFKHOD
::   WY: data
::----------------------------------------------------------------------------------------------------------------------
_od:=exec('get','#params',100365);
KH_DOD.KSEFKH:={? _od=date(0,0,0) || 'N' || 'T' ?};
_od


\kh_dod_ksefkhod_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Po redakcji KH_DOD.KSEFKHOD
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
KH_DOD.KSEFKH:={? KH_DOD.KSEFKHOD=date(0,0,0) || 'N' || 'T' ?};
1


\kh_dod_ksefliod_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wartość początkowa KH_DOD.KSEFLIOD
::   WY: data
::----------------------------------------------------------------------------------------------------------------------
_od:=exec('get','#params',100368);
KH_DOD.KSEFLI:={? _od=date(0,0,0) || 'N' || 'T' ?};
_od


\kh_dod_ksefliod_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Po redakcji KH_DOD.KSEFLIOD
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
KH_DOD.KSEFLI:={? KH_DOD.KSEFLIOD=date(0,0,0) || 'N' || 'T' ?};
1


\faks_edokumen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Ustala wartość pola FAKS.EDOKUMEN
::   WE:
::   WY: T/N
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.SZ='S'
      &
   +exec('rodzaje_edokumentu','zbl_dok',exec('kh_dokum','zbl_dok',FAKS.ref()),FAKS.T,FAKS.DW,FAKS.WHERE,FAKS.MANSKSEF)
|| 'T'
|| 'N'
?}


\faks_drukuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Ustala wartość pola FAKS.DRUKUJ
::   WE:
::   WY: T/N
::----------------------------------------------------------------------------------------------------------------------
KH_DOD.cntx_psh();
exec('kh_dod_ini','kontrahent',FAKS.KH,0);
_return:={? FAKS.EDOKUMEN='N' | KH_DOD.EFAKTURA='N' || 'T' || 'N' ?};
KH_DOD.cntx_pop();
_return


\lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Sprawdza czy jest licencja dziedziny ZBL
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('lic','#b_domain','ZBL')


\upgrade2325_ksef2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Aktualizacja 23.25_KSEF2
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
var_pres('RB',EDOKUM)>0


\upgrade2325_blbc1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.XX]
:: OPIS: Aktualizacja 23.25_BLBC1
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
var_pres('KSEF_BC',TYPYSP)>0


\dokumzbc_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: dla DOKUMZ.ref() znalezienie maski DOKUMZBC
::   WE: _a - DOKUMZ.ref()
::----------------------------------------------------------------------------------------------------------------------
_dokumz:=_a;
_jest:=0;
{? _dokumz<>null()
|| _names:=DOKUMZBC.names();
   _names.prefix('dbc'+REF.FIRMA().SYMBOL);
   {? _names.last()
   || {!
      |? DOKUMZBC.use(_names.NAME);
         DOKUMZBC.index('DOKUMZ');
         DOKUMZBC.prefix(_dokumz);
         _jest:=DOKUMZBC.first();
         ~_jest & _names.prev()
      !}
   ?}
?};
{? ~_jest
|| DOKUMZBC.use('dbc'+REF.FIRMA().SYMBOL+($(date()~1)+2));
   DOKUMZBC.index('DOKUMZ');
   DOKUMZBC.prefix(_dokumz)
?};
~~


\dokumz_bc_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: utworzenie lub wyczyszczenie __DOKUMZ_BC
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__DOKUMZ_BC')<0
|| __DOKUMZ_BC:=obj_new('REFSQL','DOKUM','DOKUMZ')
?};
__DOKUMZ_BC.REFSQL:='';
__DOKUMZ_BC.DOKUM:=null();
__DOKUMZ_BC.DOKUMZ:=null();
~~


\faks_dokumzbc_seek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: dla FAKS ustawienie DOKUMZBC z wynikiem ostatniej kontroli Businesscheck
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
exec('dokumz_bc_clear','zbl');
__DOKUMZ_BC.REFSQL:=$FAKS.ref();
{? (FAKS.SZ='S' & FAKS.T().KSEF='T') | (FAKS.SZ='Z' & FAKS.T().BL='T')
|| _dokumz:=null();
   _bcdate:=0;
   _bctime:=0;
   DOKUMZ.cntx_psh();
   DOKUMZ.index('REFSQL');
   DOKUMZ.prefix(REF.FIRMA,__DOKUMZ_BC.REFSQL,'T');
   {? DOKUMZ.first()
   || {!
      |? exec('dokumzbc_open','zbl',DOKUMZ.ref());
         {? ~DOKUMZBC.first() & DOKUMZ.FILEBC<>null()
         || exec('blc_analyze','businesslink3')
         ?};
         {? DOKUMZBC.first() & (_dokumz=null() | DOKUMZBC.DATE>_bcdate | (DOKUMZBC.DATE=_bcdate & DOKUMZBC.TIME>_bctime))
         || _dokumz:=DOKUMZ.ref();
            _bcdate:=DOKUMZBC.DATE;
            _bctime:=DOKUMZBC.TIME
         ?};
         DOKUMZ.next()
      !};
      {? _dokumz=null()
      || DOKUMZ.first();
         _dokumz:=DOKUMZ.ref()
      ?}
   ?};
   DOKUMZ.cntx_pop();
   {? _dokumz<>null()
   || exec('dokumzbc_open','zbl',_dokumz);
      {? ~DOKUMZBC.first()
      || DOKUMZ.cntx_psh();
         DOKUMZ.prefix();
         DOKUMZ.seek(_dokumz);
         exec('blc_analyze','businesslink3');
         DOKUMZ.cntx_pop()
      ?};
      __DOKUMZ_BC.DOKUMZ:=_dokumz;
      _jest:=1
   ?}
?};
DOKUMZBC.index('DOKUMZ');
DOKUMZBC.prefix(__DOKUMZ_BC.DOKUMZ);
_jest


\dokumzbc_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: wyświetlenie DOKUMZBC - pozycja wynikiu kontroli Businesscheck
::----------------------------------------------------------------------------------------------------------------------
DOKUMZBC.win_edit('RED');
DOKUMZBC.display();
~~


\dokumz_bc_hist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: dla FAKS, DOK ustawienie listy DOKUMZ z wynikami wszystkich kontroli Businesscheck
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
_refsql:='';
{? cur_tab(0,1)=FAKS
|| _refsql:=$FAKS.ref()
|? cur_tab(0,1)=DOK | var_pres('TREE_REJ')>0 & cur_tab(0,1)=TREE_REJ
|| _faks:=exec('find_faks','dok_fks1');
   _refsql:={? _faks<>null() || $_faks || $DOK.ref() ?}
?};
{? _refsql<>''
|| _win0:=DOKUMZ.win_sel('?');
   DOKUMZ.win_sel('WER_BC');
   DOKUMZ.cntx_psh();
   DOKUMZ.index('REFSQL');
   DOKUMZ.prefix(REF.FIRMA,_refsql,'T');
   {? DOKUMZ.first()
   || _jest:=1;
      DOKUMZ.select()
   ?};
   DOKUMZ.cntx_pop();
   DOKUMZ.win_sel(_win0)
?};
{? ~_jest
|| FUN.info('Brak dokumentu KSeF podlegającego kontroli Businesscheck.'@)
?};
~~


\dokumzbc_record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Rekord przed DOKUMZBC
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('rekprzed','color','DOKUMZBC#01')


\dokumzbc_kolor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Kolor DOKUMZBC
::   WE:
::   WY: napis oznaczający kolor
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab(1,1);
_kolor:='';
_result:=spli_str(_tab.RESULT,'-')[1];
{? _result=exec('check_pos_ok','zbl_stat')
|| _kolor:='DOKUMZBC#01#01'
|? _result=exec('check_pos_warn','zbl_stat')
|| _kolor:='DOKUMZBC#01#02'
|? _result=exec('check_pos_err','zbl_stat')
|| _kolor:='DOKUMZBC#01#03'
|? _result=exec('check_pos_stop','zbl_stat')
|| _kolor:='DOKUMZBC#01#04'
|? _result=exec('check_pos_nd','zbl_stat')
|| _kolor:='DOKUMZBC#01#05'
?};
_kolor


\dokumzbc_legenda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Legenda dla DOKUMZBC
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','DOKUMZBC#01')


\dokum_dokumzbc_seek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: dla DOKUM ustawienie DOKUMZBC z wynikiem ostatniej kontroli Businesscheck
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
{? DOKUM.REFSQL<>'' & (ref_tab(DOKUM.REFSQL)=DOK | ref_tab(DOKUM.REFSQL)=EDOKUM)
|| _jest:=exec('fin_dokumzbc_seek','zbl')
|? DOKUM.REFSQL='' | ref_tab(DOKUM.REFSQL)=FAKS
|| _dokumz:=null();
   exec('dokumz_bc_clear','zbl');
   __DOKUMZ_BC.DOKUM:=DOKUM.ref();
   DOKUMZ.cntx_psh();
   DOKUMZ.index('BC');
   DOKUMZ.prefix(__DOKUMZ_BC.DOKUM,'T');
   {? DOKUMZ.first()
   || _dokumz:=DOKUMZ.ref()
   ?};
   DOKUMZ.cntx_pop();
   {? _dokumz<>null()
   || exec('dokumzbc_open','zbl',_dokumz);
      {? ~DOKUMZBC.first()
      || DOKUMZ.cntx_psh();
         DOKUMZ.prefix();
         DOKUMZ.seek(_dokumz);
         exec('blc_analyze','businesslink3');
         DOKUMZ.cntx_pop()
      ?};
      __DOKUMZ_BC.DOKUMZ:=_dokumz;
      _jest:=1
   ?}
?};
_jest


\fin_dokumzbc_seek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: dla DOK ustawienie DOKUMZBC z wynikiem ostatniej kontroli Businesscheck
::   WE: _a - tabela
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
_dokum:=_dokumz:=null();
_tab:={? var_pres('_a')>0 || _a || cur_tab() ?};
_faks:={? _tab=DOK || exec('find_faks','dok_fks1') ?};
_edokum:={? _tab=DOK & DOK.EDOKUM<>'' || DOK.EDOKUM || '' ?};
_ref:={? _faks<>null() || $_faks || $_tab.ref() ?};
{? _tab.f_active()
|| {? _tab.f_size()=0 || _ref:='' ?}
|| {? _tab.size()=0 || _ref:='' ?}
?};
:: Najpierw sprawdzenie czy jest BC dla wysyłanego do ksef
{? _ref<>''
|| DOKUM.cntx_psh();
   {? _tab=DOKUM
   || _dokum:=DOKUM.ref()
   || DOKUM.index('BL_2'); DOKUM.prefix(REF.FIRMA,'T',{? _edokum<>'' || _edokum || _ref ?});
      {? DOKUM.first()
      || {!
         |? {? DOKUM.BLC_KIND='' || _dokum:=DOKUM.ref() ?};
            _dokum=null() & DOKUM.next()
         !}
      ?}
   ?};
   DOKUM.cntx_pop();
   {? _dokum<>null()
   || DOKUMZ.cntx_psh();
      DOKUMZ.index('BC');
      DOKUMZ.prefix(_dokum,'T');
      {? DOKUMZ.first()
      || _dokumz:=DOKUMZ.ref()
      ?};
      DOKUMZ.cntx_pop()
   ?}
?};
{? _dokumz<>null()
|| exec('dokumzbc_open','zbl',_dokumz);
   {? ~DOKUMZBC.first()
   || exec('blc_analyze','businesslink3');
      DOKUMZBC.index('DOKUMZ');
      DOKUMZBC.prefix(_dokumz);
      {? DOKUMZBC.first() || _jest:=1 ?}
   || _jest:=1
   ?}
?};
:: Sprawdzenie dla ostatniego DOKUMZ
{? ~_jest
|| _dokumz:=null();
   {? _tab=DOK
   || DOKUMZ.cntx_psh();
      DOKUMZ.index('REFSQL');
      DOKUMZ.prefix(REF.FIRMA,$DOK.ref(),'T');
      {? DOKUMZ.first()
      || {!
         |? _dokumz:=DOKUMZ.ref();
            {? _dokumz<>null()
            || exec('dokumzbc_open','zbl',_dokumz);
               {? ~DOKUMZBC.first()
               || exec('blc_analyze','businesslink3');
                  DOKUMZBC.index('DOKUMZ');
                  DOKUMZBC.prefix(_dokumz);
                  {? DOKUMZBC.first() || _jest:=1 ?}
               || _jest:=1
               ?}
            ?};
            ~_jest & DOKUMZ.next()
         !}
      ?};
      DOKUMZ.cntx_pop()
   |? _ref<>''
   || DOKUMZ.cntx_psh();
      DOKUMZ.index('REFSQL');
      DOKUMZ.prefix(REF.FIRMA,_ref,'T');
      {? DOKUMZ.first()
      || {!
         |? _dokumz:=DOKUMZ.ref();
            {? _dokumz<>null()
            || exec('dokumzbc_open','zbl',_dokumz);
               {? ~DOKUMZBC.first()
               || exec('blc_analyze','businesslink3');
                  DOKUMZBC.index('DOKUMZ');
                  DOKUMZBC.prefix(_dokumz);
                  {? DOKUMZBC.first() || _jest:=1 ?}
               || _jest:=1
               ?}
            ?};
            ~_jest & DOKUMZ.next()
         !}
      ?};
      DOKUMZ.cntx_pop()
   ?}
?};
{? ~_jest
|| DOKUMZBC.index('DOKUMZ');
   DOKUMZBC.prefix(null())
?};
_jest


\dokumzbc_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Okienko przed dla DOKUMZBC
::   WE: _a - _tabela
::----------------------------------------------------------------------------------------------------------------------
_tab:={? var_pres('_a')>0 || _a || cur_tab() ?};
_win:={? _tab=DOK || 'WER_DOK' |? _tab=EDOKUM || 'WER_EDOK' ?};
{? _tab=DOK | _tab=EDOKUM
|| {? exec('fin_dokumzbc_seek','zbl',{? var_pres('dok_zbl')>0 & dok_zbl || DOKUM || _tab ?})
   || DOKUMZBC.actions_grayed(_win)
   || DOKUMZBC.actions_grayed(_win,'WH:WH')
   ?}
?};
1


\set_dok_statbc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Ustawia status Businesscheck dla rekordu tabeli DOK przy ustalonym kontekście
::----------------------------------------------------------------------------------------------------------------------
_result:='';
{? exec('is_businesscheck','businesslink3')>0
|| {? 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'
   || FAKS.cntx_psh(); FAKS.use('faktu'+(3+(1-DOK.DOKZRODL))); FAKS.prefix();
      {? FAKS.seek(#(4-DOK.DOKZRODL),FAKS.name)
      || {? {? FAKS.T<>null() || FAKS.T().KSEF_BC > 0 || 0 ?} & FAKS.STATKSEF<>exec('ksef_donot_applay','zbl_stat')
         || _result:=exec('check_check','zbl_stat')
         ?}
      ?};
      FAKS.cntx_pop()
   || {? {? DOK.DOK_REJ<>null() || DOK.DOK_REJ().KSEF_BC>0 || 0 ?} & DOK.STATKSEF<>exec('ksef_donot_applay','zbl_stat')
      || _result:=exec('check_check','zbl_stat')
      ?}
   ?}
?};
{? _result='' || _result:=exec('check_none','zbl_stat') ?};
_result


\upgrade2325_jst01
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.XX]
:: OPIS: Aktualizacja 23.25_JST01
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
var_pres('ID_WEW',KH)>0


\set_dok_blc_stat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: Ustawia status Businesscheck dla rekordu tabeli DOK na podstawie DOKUM przychodzącego
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_dokum:=null();
_faks:=exec('find_faks','dok_fks1');
_edokum:={? DOK.EDOKUM<>'' || DOK.EDOKUM || '' ?};
_ref:={? _faks<>null() || $_faks || $DOK.ref() ?};

{? _ref<>''
|| DOKUM.cntx_psh();
   DOKUM.index('BL_2'); DOKUM.prefix(REF.FIRMA,'T',{? _edokum<>'' || _edokum || _ref ?},'P');
   {? DOKUM.first()
   || {!
      |? {? DOKUM.BLC_STAT<>''
         || _result:={? DOKUM.BLC_STAT='SENT' | DOKUM.BLC_STAT='READY'
                     || exec('check_pending','zbl_stat')
                     |? DOKUM.BLC_STAT='CONFIRMED'
                     || exec('check_ok','zbl_stat')
                     |? DOKUM.BLC_STAT='WARNING'
                     || exec('check_warning','zbl_stat')
                     |? DOKUM.BLC_STAT='ERROR'
                     || exec('check_error','zbl_stat')
                     |? DOKUM.BLC_STAT='UNAVAILABLE'
                     || exec('check_na','zbl_stat')
                     || exec('check_none','zbl_stat')
                     ?}
         ?};
         _result='' & DOKUM.next()
      !}

   ?};
   DOKUM.cntx_pop()
?};
_result

:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 6528f8264af34332899da8c86ac0a7bffb4d618c06aa36749d217e83d30ea08cfee7a4feb5738debe7fe743cf842f766b9dd967356264a0a7a669358ca64becf88ba858216409d61ae5213363e17f01dd9f3d74fbca1c5f3c5f540f54d88ad047e44a32c7bcd29ac58ac245a801a27af20a33abcbb1aba6b7410484b22639a8b

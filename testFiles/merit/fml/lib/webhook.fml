:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: webhook.fml
:: Utworzony: 09.07.2020
:: Autor: PD
:: Systemy:
::======================================================================================================================
:: Zawartość: Obsługa WEBHOOK
::======================================================================================================================

\http_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: wysyła plik lub stringa postem i zapisuje w logu (jeśli podane parametry _b / _c)
::   WE: _a - plik lub string do wysłania (lub '' bez zapisu pliku w blobie)
::       _b - opis do logu
::       _c - czy zapisywać plik w logu - domyślnie 1
::       _d - 0-get status, 1-get bytes
::       _e - JSON do zapisu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_url:=DOKUMPT.URL;
{? _<3 || _c:=1 ?};
{? _<4 || _d:=0 ?};

:: Tryb cichy poza transakcją
{? do_state()=0 || _do:=0; _no_msg:=no_msg(1) || _do:=1 ?};
_http:=inet_get(_url);
{? _do=0 || no_msg(_no_msg) ?};

{? type_of(_http)<100
||
   exec('log_write','webhook','','Błąd wywołania funkcji %1.'['inet_get()'],0);
   return(-1)
?};

_http.append_header('Content-Type: application/json; charset=UTF-8');
_http.http_post(,_a);
_res:=_http.get_status();
exec('log_write','webhook', {? _c=1 || _e || '' ?} , _b, $_res );
_res


\log_write
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS:  zapis pliku / zawartości zmiennej w logu
::   WE: _a - plik (obiekt) / string ( może być '' )
::       _b - opis do logu
::       _c - status
::   WY:
::----------------------------------------------------------------------------------------------------------------------
WSLOG.blank();
WSLOG.DATA:=date();
WSLOG.CZAS:=time();
WSLOG.OPIS:=_b;
WSLOG.STATUS:={? type_of(_c)=2 || _c || $_c ?};
WSLOG.KONFIG:='WEBHOOK';
{? type_of(_a)=type_of('')
||
   {? _a<>''
   ||
:: zapis stringa do pliku
      _plik:='request.xml';
      _file:=fopen(_plik,'Uw',1,,1);
      {? ~_file.is_open || exec('log_write','firm_kur','', 'błąd otwarcia pliku',-1); return ?};
      _a:=json_tparse(_a).json_tconvert(,,,,'result',,,'noheader=0,nodata=1,indentation=1');
      fwrite(_file,_a)
   ?}
||
   _file:=_a
?};
WSLOG.add;
{? var_press('_file')>0 || WSLOG.bl_put('LOG',_file,,,'request.xml') ?};
~~


\send_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS:  wysłanie wiadomości
::   WE:_a- _courier_code
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_method:='Komunikat';
DOKUMPT.cntx_psh();
DOKUMPT.index('DISP_W');
DOKUMPT.prefix('N');
DOKUM.cntx_psh();
DOKUM.prefix();
{? DOKUMPT.first()
||
   {!|?
      DOKUMPT.DOKUM();
      _json:=exec('json','webhook');
      _wyn:=exec('http_send','webhook',_json,_method,1,1,_json);
      DOKUMPT.cntx_psh();
      _next:=DOKUMPT.next();
      _ref:=DOKUMPT.ref();
      DOKUMPT.cntx_pop();
      {? type_of(_wyn)=1 & _wyn=200
      ||
         DOKUMPT.prefix();
         DOKUMPT.WO:='T';
         DOKUMPT.put()
      ?};
      DOKUMPT.prefix('N');
      DOKUMPT.seek(_ref);
      _next
   !}
?};
DOKUMPT.cntx_pop();
DOKUM.cntx_pop();
~~


\json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: komunikat
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_json:='
{
    "@type": "MessageCard",
    "themeColor": "0076D7",
    "summary": "Powiadomienie o kontakcie",
    "title": "Powiadomienie o kontakcie",
    "sections": [
        {
            "facts": [
                {
                    "name": "Numer:",
                    "value": %1
                },
                {
                    "name": "Symbol:",
                    "value": %2
                },
                {
                    "name": "Data:",
                    "value": %3
                },
                {
                    "name": "Typ:",
                    "value": %4
                },
                {
                    "name": "Kontrahent:",
                    "value": %5
                },
                {
                    "name": "Osoba kontaktowa:",
                    "value": %6
                },
                {
                    "name": "Temat:",
                    "value": %7
                }
            ]
        }
    ]
}'
[
:: numer
json_value($DOKUM.NR),
:: symbol
json_value(DOKUM.SYM),
:: data
json_value(form(DOKUM.DATA,,6)),
:: typ
json_value(DOKUM.DOT().ZDARZ),
:: kontrahent
json_value(DOKUM.KH().NAZ),
:: osoba kontaktowa
json_value(DOKUM.KH_OSOB().IMIE+' '+DOKUM.KH_OSOB().NAZWISKO),
:: temat
json_value(DOKUM.KR_OP)
];
_json


\kh_powiad_okno
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: okno edycji zespołów powiadamianych wiadomością o kontakcie z kontrahentem
::       (czyli o dopisaniu rekordu do DOKUM) - obsługa pola KH.POWTEAMS
::   WE: [_a] - napis zawierający listę użytkowników w ustalonym formacie
::   WY: napis zawierający listę użytkowników w ustalonym formacie
::----------------------------------------------------------------------------------------------------------------------
_powiad0:='';
{? var_pres('_a')=type_of('')
|| _powiad0:=_a
?};
_powiad:=_powiad0;

_t_powiad0:=exec('kh_powiad_tab','webhook',_powiad0);

_tab:=tab_tmp(1,
              'NAZ','STRING[20]','Zespół'@,
              'URL','STRING[250]','Adres URL'@,
              'AKT','STRING[1]','Aktywny'@,
              'ACCESS','STRING[1]','Powiadomienie'@);

WEBHOOK.cntx_psh();
WEBHOOK.prefix();
{? WEBHOOK.first()
|| {!
   |? _naz:=WEBHOOK.NAZ;
      _zazn:='N';
      {? _t_powiad0.find_key(_naz,)
      || _zazn:='T'
      ?};
      _tab.NAZ:=_naz;
      _tab.URL:=WEBHOOK.URL;
      _tab.AKT:=WEBHOOK.AKT;
      _tab.ACCESS:=_zazn;
      _tab.add();
      WEBHOOK.next()
   !}
?};
WEBHOOK.cntx_pop();

_okn:=_tab.mk_sel('Zespoły'@,'P',,,,,,,'U');
_tab.win_fld(_okn,,'NAZ',,,20,,1);
_tab.win_fld(_okn,,'AKT',,,,,0,,,,2,,"'T'","'N'");
_tab.win_fld(_okn,,'ACCESS',,,,,0,,,'Czy powiadamiać o kontaktach [T/N]?'@,2,,"'T'","'N'");
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Zaznacz'@@,,,_fml,,1,1,,,'Z');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='N';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Odznacz'@@,,,_fml,,,1,,,'O');
_tab.win_act(_okn,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');
_fb:="
   _lastdraw:=1;
   {? var_pres('_a')=type_of(0)
   || _lastdraw:=_a
   ?};
   _grayed:='';
   _tab:=cur_tab();
   _sel:=cur_win();
   {? _lastdraw>0
   || {? _tab.sel_size()=0
::       Pojedyncze zaznaczenie
      || {? _tab.ACCESS='T'
         || _tab.actions(_sel,,'O',1);
            _grayed+='Z'
         |? _tab.ACCESS='N' | _tab.ACCESS=''
         || _tab.actions(_sel,,'Z',1);
            _grayed+='O'
         ?}
::       Zaznaczenie grupowe
      || _tab.actions(_sel,,'Z',1)
      ?};
      _tab.actions_grayed(_sel,_grayed)
   ?}
";
_tab.win_act(_okn,,'Rekord',,,,_fb);

_tab.win_sel(_okn);

{? _tab.select()
|| _powiad:='';
   {? _tab.first()
   || {!
      |? {? _tab.ACCESS='T'
         || {? _powiad<>''
            || _powiad+=', '
            ?};
            _powiad+=_tab.NAZ+' ('+_tab.URL+')'
         ?};
         _tab.next()
      !}
   ?}
?};

_powiad


\kh_powiad_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: tymczasowa tabela zawierająca zespoły powiadamiane wiadomością o kontakcie z kontrahentem
::       (czyli o dopisaniu rekordu do DOKUM) - obsługa pola KH.POW_TEAMS
::   WE: [_a] - napis zawierający listę użytkowników w ustalonym formacie
::----------------------------------------------------------------------------------------------------------------------
_napis:='';
{? var_pres('_a')=type_of('')
|| _napis:=_a
?};
_tab:=tab_tmp(1,
             'NAZ','STRING[20]','Zespół');
_tnap1:=spli_str(_napis,',');
{! _jj:=1..obj_len(_tnap1)
|! _nap1:=|_tnap1[_jj];
   _kk:=_nap1*' (';
   {? _kk>0
   || _kk-=1;
      _nap1:=_kk+_nap1
   ?};
   {? _nap1<>''
   || _tab.NAZ:=_nap1;
      _tab.add()
   ?}
!};
obj_del(_tnap1);
_tab


\dokumpt_zapis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: zapis do tabeli DOKUMPT (powiadomienia o kontaktach) po dopisaniu do tabeli DOKUM (kontakty)
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.KH<>null()
|| _powiad:=DOKUM.KH().memo_txt(,1,'POWTEAMS');
   _t_powiad:=exec('kh_powiad_tab','webhook',_powiad);
   {? _t_powiad.first()
   || DOKUMPT.cntx_psh();
      DOKUMPT.index('DISP');
      DOKUMPT.prefix();
      WEBHOOK.cntx_psh();
      WEBHOOK.index('NAZ');
      {!
      |? _naz:=_t_powiad.NAZ;
         {? _naz<>''
         || WEBHOOK.prefix(_naz,);
            {? WEBHOOK.first() & WEBHOOK.URL<>'' & WEBHOOK.AKT='T'
            ||
:               _upr:=exec('dokum_upraw','dokum',_user);
                _upr:='P';
               {? _upr='' | _upr='P'
               || DOKUMPT.prefix(DOKUM.ref(),WEBHOOK.ref());
                  {? ~DOKUMPT.first()
                  || DOKUMPT.blank();
                     DOKUMPT.DOKUM:=DOKUM.ref();
                     DOKUMPT.WEBHOOK:=WEBHOOK.ref();
                     DOKUMPT.URL:=WEBHOOK.URL;
                     DOKUMPT.add()
                  ?};
                  DOKUMPT.prefix()
               ?}
            ?}
         ?};
         _t_powiad.next()
      !};
      WEBHOOK.cntx_pop();
      DOKUMPT.cntx_pop()
   ?}
?};

{? DOKUM.DOKUMK<>null()
|| DOKUMKU.cntx_psh();
   DOKUMKU.index('DOKUMK');
   DOKUMKU.prefix(DOKUM.DOKUMK,'T',null());
   {? DOKUMKU.first()
   || DOKUMPT.cntx_psh();
      DOKUMPT.index('DISP');
      DOKUMPT.prefix();
      {!
      |? {? DOKUMKU.WEBHOOK().URL<>'' & DOKUMKU.WEBHOOK().AKT='T'
         || DOKUMPT.prefix(DOKUM.ref(),WEBHOOK.ref());
            {? ~DOKUMPT.first()
            || DOKUMPT.blank();
               DOKUMPT.DOKUM:=DOKUM.ref();
               DOKUMPT.WEBHOOK:=DOKUMKU.WEBHOOK;
               DOKUMPT.URL:=DOKUMKU.WEBHOOK().URL;
               DOKUMPT.add()
            ?};
            DOKUMPT.prefix()
         ?};
         DOKUMKU.next()
      !};
      DOKUMPT.cntx_pop()
   ?};
   DOKUMKU.cntx_pop()
?};

~~


\url_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: WEBHOOK.URL po edycji
::----------------------------------------------------------------------------------------------------------------------
{? WEBHOOK.URL=''
|| FUN.info('Pole \'URL\' musi być wypełnione.'@);0
|? WEBHOOK.URL<>'' &
((1+menu_txt()='D' &
(WEBHOOK.cntx_psh();
WEBHOOK.index('URL');
WEBHOOK.prefix(WEBHOOK.URL,);
_wyn:=~WEBHOOK.first();
WEBHOOK.cntx_pop();
_wyn))
|
(1+menu_txt()='P' &
(WEBHOOK.cntx_psh();
WEBHOOK.index('URL');
WEBHOOK.prefix(WEBHOOK.URL,);
_wyn:=~WEBHOOK.first();
_ref:=WEBHOOK.ref();
WEBHOOK.cntx_pop();
_wyn | (~_wyn & _ref=WEBHOOK.ref()))))
|| 1
|| FUN.info('Wartość pola \'URL\' musi być unikalna.'@);0
?}


\naz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: WEBHOOK.NAZ po edycji
::----------------------------------------------------------------------------------------------------------------------
{? WEBHOOK.NAZ=''
|| FUN.info('Pole \'Nazwa\' musi być wypełnione.'@);0
|? WEBHOOK.NAZ<>'' &
((1+menu_txt()='D' &
(WEBHOOK.cntx_psh();
WEBHOOK.index('NAZ');
WEBHOOK.prefix(WEBHOOK.NAZ,);
_wyn:=~WEBHOOK.first();
WEBHOOK.cntx_pop();
_wyn))
|
(1+menu_txt()='P' &
(WEBHOOK.cntx_psh();
WEBHOOK.index('NAZ');
WEBHOOK.prefix(WEBHOOK.NAZ,);
_wyn:=~WEBHOOK.first();
_ref:=WEBHOOK.ref();
WEBHOOK.cntx_pop();
_wyn | (~_wyn & _ref=WEBHOOK.ref()))))
|| 1
|| FUN.info('Wartość pola \'Nazwa\' musi być unikalna.'@);0
?}


\dokumpt_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.42]
:: OPIS: zapis do tabeli DOKUMPT (powiadomienia o kontaktach)
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='K' | _a='M')
|| _rodz:=_a
|| _rodz:='M'
?};
_user:=exec('name','users');

_tab:=tab_tmp(1,
              'NAZ','STRING[20]','Zespół'@,
              'AKT','STRING[1]','Aktywny'@,
              'ACCESS','STRING[1]','Powiadomienie'@,
              'WH_REF','STRING[16]','WEBHOOK'@);

WEBHOOK.cntx_psh();
WEBHOOK.prefix();
{? WEBHOOK.first()
|| {!
   |?  _tab.NAZ:=WEBHOOK.NAZ;
       _tab.AKT:=WEBHOOK.AKT;
       _tab.ACCESS:='N';
       _tab.WH_REF:=$WEBHOOK.ref();
       _tab.add();
       WEBHOOK.next()
   !}
?};
WEBHOOK.cntx_pop();

{? _tab.size()=0
|| FUN.info('Brak zdefiniowanych zespołów.'@);
   return(~~)
?};

_okn:=_tab.mk_sel('Zespoły'@,'P',,,,,,,'U');
_tab.win_fld(_okn,,'ACCESS',,,,,0,,,'Powiadamienie [T/N]?'@,2,,"'T'","'N'");
_tab.win_fld(_okn,,'NAZ',,,20,,1);
_tab.win_fld(_okn,,'AKT',,,,,0,,,,2,,"'T'","'N'");
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Zaznacz'@@,,,_fml,,1,1,,,'Z');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='N';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Odznacz'@@,,,_fml,,,1,,,'O');
_tab.win_act(_okn,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');
_fb:="
   _lastdraw:=1;
   {? var_pres('_a')=type_of(0)
   || _lastdraw:=_a
   ?};
   _grayed:='';
   _tab:=cur_tab();
   _sel:=cur_win();
   {? _lastdraw>0
   || {? _tab.sel_size()=0
::       Pojedyncze zaznaczenie
      || {? _tab.ACCESS='T'
         || _tab.actions(_sel,,'O',1);
            _grayed+='Z'
         |? _tab.ACCESS='N' | _tab.ACCESS=''
         || _tab.actions(_sel,,'Z',1);
            _grayed+='O'
         ?}
::       Zaznaczenie grupowe
      || _tab.actions(_sel,,'Z',1)
      ?};
      _tab.actions_grayed(_sel,_grayed)
   ?}
";
_tab.win_act(_okn,,'Rekord',,,,_fb);

_tab.win_sel(_okn);

{? _tab.select()
||
   WEBHOOK.cntx_psh();
   WEBHOOK.clear();
   _tab.first();
   {!
   |? {? _tab.ACCESS='T' & WEBHOOK.seek(_tab.WH_REF)
         || DOKUMPT.blank();
            DOKUMPT.DOKUM:=DOKUM.ref();
            DOKUMPT.WEBHOOK:=WEBHOOK.ref();
            DOKUMPT.URL:=WEBHOOK.URL;
            DOKUMPT.add()
      ?};
      _tab.next()
   !};
   WEBHOOK.cntx_pop()
?};

~~


\dokumku_ustaw_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: edycja zespołów powiadamianych wiadomością o kontakcie danej kategorii
::       (czyli o dopisaniu rekordu do DOKUM) - obsługa tabeli DOKUMKU
::----------------------------------------------------------------------------------------------------------------------
_dokumk_ref:=DOKUMK.ref();
{? _dokumk_ref=null()
|| return(~~)
?};

DOKUMKU.cntx_psh();
DOKUMKU.index('DOKUMK');
DOKUMKU.prefix(_dokumk_ref,'T',null());

_tab:=tab_tmp(1,
              'NAZ','STRING[20]','Zespół'@,
              'URL','STRING[250]','Adres URL'@,
              'AKT','STRING[1]','Aktywny'@,
              'ACCESS','STRING[1]','Powiadomienie'@,
              'WH_REF','STRING[16]','WEBHOOK'@);
WEBHOOK.cntx_psh();
WEBHOOK.prefix();
{? WEBHOOK.first()
|| {!
   |? _naz:=WEBHOOK.NAZ;
      _wh_ref:=WEBHOOK.ref();
      _zazn:='N';
      {? DOKUMKU.find_key(_wh_ref)
      || _zazn:='T'
      ?};
      _tab.NAZ:=_naz;
      _tab.URL:=WEBHOOK.URL;
      _tab.AKT:=WEBHOOK.AKT;
      _tab.ACCESS:=_zazn;
      _tab.WH_REF:=$_wh_ref;
      _tab.add();
      WEBHOOK.next()
   !}
?};
WEBHOOK.cntx_pop();

_okn:=_tab.mk_sel('Zespoły'@,'P',,,,,,,'U');
_tab.win_fld(_okn,,'ACCESS',,,,,0,,,'Powiadamienie [T/N]?'@,2,,"'T'","'N'");
_tab.win_fld(_okn,,'NAZ',,,20,,1);
_tab.win_fld(_okn,,'AKT',,,,,0,,,,2,,"'T'","'N'");
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Zaznacz'@@,,,_fml,,1,1,,,'Z');
task_attach('ZWS_PAR_KKKA');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='N';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Odznacz'@@,,,_fml,,,1,,,'O');
task_attach('ZWS_PAR_KKKA');
_tab.win_act(_okn,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');
task_attach('ZWS_PAR_KKKA');
_fb:="
   _lastdraw:=1;
   {? var_pres('_a')=type_of(0)
   || _lastdraw:=_a
   ?};
   _grayed:='';
   _tab:=cur_tab();
   _sel:=cur_win();
   {? _lastdraw>0
   || {? _tab.sel_size()=0
::       Pojedyncze zaznaczenie
      || {? _tab.ACCESS='T'
         || _tab.actions(_sel,,'O',1);
            _grayed+='Z'
         |? _tab.ACCESS='N' | _tab.ACCESS=''
         || _tab.actions(_sel,,'Z',1);
            _grayed+='O'
         ?}
::       Zaznaczenie grupowe
      || _tab.actions(_sel,,'Z',1)
      ?};
      _tab.actions_grayed(_sel,_grayed)
   ?}
";
_tab.win_act(_okn,,'Rekord',,,,_fb);

_tab.win_sel(_okn);

{? _tab.select()
|| {? _tab.first()
   || {!
      |? _wh_ref:=null();
         WEBHOOK.cntx_psh();
         {? WEBHOOK.seek(_tab.WH_REF,,1)
         || _wh_ref:=WEBHOOK.ref()
         ?};
         WEBHOOK.cntx_pop();
         {? _wh_ref<>null()
         || {? _tab.ACCESS='T'
            || {? ~DOKUMKU.find_key(_wh_ref)
               || DOKUMKU.blank();
                  DOKUMKU.RODZ:='T';
                  DOKUMKU.DOKUMK:=_dokumk_ref;
                  DOKUMKU.WEBHOOK:=_wh_ref;
                  DOKUMKU.add(1)
               ?}
            || {? DOKUMKU.find_key(_wh_ref)
               || DOKUMKU.del(1)
               ?}
            ?}
         ?};
         _tab.next()
      !}
   ?}
?};

DOKUMKU.cntx_pop();

~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:39 a81c25c2e9a8be6b4848dd727356586970b9f367fe331c68a35b5a57de67e5ed2c7caa79713391cda148a4dfc27cb30242549cbad4d181a0cdbef4496214bd5e427b3f804b243f2b8bd1030d727b53d14567bc03bfcbdc33360b0f3706bdff4fad096b91143c7d1bc1d92d55be04fad32e8c7396bb533c9ea2cad920f416546a

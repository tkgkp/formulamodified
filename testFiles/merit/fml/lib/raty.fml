:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: raty.fml
:: Utworzony: 13.03.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa płatności ratalnych
::======================================================================================================================


\czyLista
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BL [8.50]
:: OPIS: Funkcja sprawdza czy sa przypiete formy platnosci do _a
::   WE: _a - tabela, z ktora zwiazana jest platnosc: OP, POZ
::       _b - sprawdzanie zawartosci tabeli
::       =0 - od gory (first)
::       =1 - lub dolu (last)
::       _c - zmiana kontekstu tabeli D_RB
::       =0 - tylko zbadanie (bez zmiany kontekstu)
::       =1 - zbadanie i zmiana kontekstu tabeli D_RB (pozostanie na pierwszym badz ostatnim
::             rekordzie w dziedzinie
::   WY: wskazanie na D_RB
::  OLD: \czyLista/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
{? _<3 || _c:=0 ?};
_lista:=0;
ROZRACH.PLATREF:=null;
ROZRACH.TERMPLAT:=#0;
{? var_pres('_c')=1 & ~_c || D_RB.cntx_psh() ?};
D_RB.index(exec('ndxD_RB','raty',_a));
{? _a=@.DOK | _a=@.POZ
|| D_RB.prefix(SSTALE.AO,_a.ref)
|| D_RB.prefix(_a.ref)
?};
{? var_pres('_b')=1 & _b
|| {? D_RB.last()
   || _lista:=D_RB.ref;
      ROZRACH.PLATREF:=D_RB.FORMAPLA;
      ROZRACH.TERMPLAT:=D_RB.TERMPLAT
   ?}
|| {? D_RB.first()
   || _lista:=D_RB.ref;
      ROZRACH.PLATREF:=D_RB.FORMAPLA;
      ROZRACH.TERMPLAT:=D_RB.TERMPLAT
   ?}
?};
{? var_pres('_c')=1 & ~_c || D_RB.cntx_pop() ?};
_lista


\jakaPlat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BL [8.50]
::  OLD: \jakaPlat/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
D_RB.cntx_psh(); D_RB.prefix();
{? D_RB.seek(_a)
|| ROZRACH.FORMAPLA:=D_RB.FORMAPLA().KOD;
   ROZRACH.PLATOPIS:=D_RB.FORMAPLA().TR;
   ROZRACH.TERMPLAT:=D_RB.TERMPLAT
|| ROZRACH.FORMAPLA:='<- F3 ->';
   ROZRACH.PLATOPIS:=80*'.';
   ROZRACH.TERMPLAT:=date(0,0,0)
?};
D_RB.cntx_pop()


\ndxD_RB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BL [8.50]
:: OPIS: ustala indeks tabeli D_RB w zaleznosci OD przekazanego parametru
::   WE: _a - alias tabeli zwiazanej z D_RB
::   WY: akronim indeksu tabeli D_RB
::  OLD: \ndxD_RB/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=POZ
|| 'POZ'
|? _a=DOK
|| 'DOK'
|? _a=OP
|| 'OP'
|| D_RB.index('?')
?}


\addLista
::----------------------------------------------------------------------------------------------------------------------
:UTWORZYL:  BL  [8.50]
:OPIS:      Funkcja dodaje rekord do tabeli D_RB powiazany z rekordem tabeli _a
:WYWOLANIA: ()
:WE:        _a - tabela, z ktora zwiazana jest platnosc: OP, POZ
:           _b - forma platnosci =:FORMAPLA
:           _c - termin platnosci=:TERMPLAT
:           _d - wartosc   =: WAR
:           _e - czy procent wartosci (T/N), brak = N =:CZY_PROC
:           _f - procent   =: PROCENT
:WY:        =1 - dodano rekord do D_RB
:           =0 - wpp.
::  OLD: \addLista/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
_addD_RB:=0;
D_RB.cntx_psh();
D_RB.index(exec('ndxD_RB','raty',_a));
{? _a=DOK | _a=POZ
|| D_RB.prefix(SSTALE.AO,_a.ref)
|| D_RB.prefix(_a.ref)
?};
D_RB.blank();
{? _>1
|| {? var_pres('_b')=7 || D_RB.FORMAPLA:=_b ?};
   {? var_pres('_c')=4 || D_RB.TERMPLAT:=_c ?};
   {? var_pres('_d')=1 || D_RB.WAR:=_d ?};
   {? var_pres('_e')=2 & 'TN'*_e || D_RB.CZY_PROC:=_e ?};
   {? var_pres('_f')=1 || D_RB.PROCENT:=_f ?};
   {? D_RB.add()
   || _addD_RB:=D_RB.ref();
      ROZRACH.FORMAPLA:=D_RB.FORMAPLA().KOD
   ?}
?};
D_RB.cntx_pop();
_addD_RB


\wartPlat
::----------------------------------------------------------------------------------------------------------------------
::UTWORZYL:  BL  [8.50]
::OPIS:      suma wartosci pola przekazanego parametrem _a
::           w rekordach "ustalonej" dziedziny tabeli (D_RB)
::WYWOLANIA: definicja
::WE:        _a - akronim pola "sumowanej kolumny"
::           _b - akronim pola przechowujacego uzyskana sume
::           _c - tabela, z ktora zwiazana jest platnosc: OP, POZ
::           _d - ref tabeli z _c
::WY:       suma rekordow
::  OLD: \wartPlat/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
_suma:=0;
_daty:=1;
_pole:='D_RB.'+_a;
D_RB.cntx_psh();
{? _>=4 & type_of(_c)>100 & type_of(_d)=7
|| D_RB.index(exec('ndxD_RB','raty',_c));
   {? _c=DOK | _c=POZ
   || D_RB.prefix(SSTALE.AO,_d)
   || D_RB.prefix(_d)
   ?}
?};
{? D_RB.first()
|| {!
   |? _suma+=($(_pole))();
      {? _=5
      || _daty:={? _daty || D_RB.TERMPLAT<>date(0,0,0) ?}
      ?};
      D_RB.next()
   !}
?};
D_RB.cntx_pop();
{? _>1 & type_of(_b)=2 & _b<>'' || ($_b)():=_suma ?};
{? _=5
|| {? _daty || _suma ?}
|| _suma
?}


\outLista
::----------------------------------------------------------------------------------------------------------------------
:UTWORZYL:  BL  [8.50]
:OPIS:      Funkcja kasuje powiazane z rekordem tabeli _a rekordy D_RB
:WYWOLANIA: ()
:WE:        _a - tabela, z ktora zwiazana jest platnosc: OP, POZ
:WY:        =1 - usunieto powiazane rekordy D_RB
:           =0 - wpp.
::  OLD: \outLista/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~_ || _a:=($ROZRACH.TABELA)() ?};
_del_all:=0;
{? _a.ref=null
|| _del_all=1
|| D_RB.cntx_psh();
   D_RB.index(exec('ndxD_RB','raty',_a));
   {? _a=DOK | _a=POZ
   || D_RB.prefix(SSTALE.AO,_a.ref)
   || D_RB.prefix(_a.ref)
   ?};
   {? D_RB.first()
   || {! |? D_RB.del() !}
   ?};
   {? (_del_all:=~D_RB.first())
   || ROZRACH.FORMAPLA:='<- F3 ->';
      ROZRACH.PLATOPIS:=80*'.';
      ROZRACH.TERMPLAT:=date(0,0,0)
   ?};
   D_RB.cntx_pop()
?};
_del_all


\fp_f3
::----------------------------------------------------------------------------------------------------------------------
:UTWORZYL:  BL  [8.50]
:OPIS:
:WYWOLANIA:
:WE:        _a - alias tabeli, z ktora zwiazana jest platnosc: OP, POZ, DOK
:           jesli pominiety - akronim ktorejs z ww tabel przechowywany
:           w zmiennej ROZRACH.TABELA
:           _b: 0 - uruchamianie przez F3, 1 - uruchamianie przez select
:WY:
::  OLD: \fp_f3/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
{? type_of(_a)=type_of(~~) || _a:=($ROZRACH.TABELA)() ?};
D_RB.win_sel('WER');
D_RB.index(exec('ndxD_RB','raty',_a));
{? ROZRACH.TABELA='DOK' | ROZRACH.TABELA='POZ'
|| D_RB.prefix(SSTALE.AO,_a.ref)
|| D_RB.prefix(_a.ref)
?};
{? ~D_RB.first() & ROZRACH.TABELA='POZ'
   & ((POZ.WAL & exec('wartPlat','raty','WAR',,DOK,POZ.DOK)=POZ.SUMW)
   | (~POZ.WAL & exec('wartPlat','raty','WAR',,DOK,POZ.DOK)=POZ.SUM))
|| exec('copy_drb','raty',DOK,POZ.DOK)
?};
{? (_>2 & _c)
|| _va:='RPUdp:Rd'
|| _va:=''
?};
{! |?
   {? ROZRACH.TABELA='OP'
   || _vwin:='OP';
      {? var_pres('DRB',@.CLASS)<0 || exec('obj_drbt','raty') ?};
      {? var_pres('DRB')>0 || obj_del(DRB) ?}; DRB:=obj_new(@.CLASS.DRB);
      {? _=4 & type_of(_d)=7
      || DRB.set_tab(OP.ref(),_d); _vwin+='E'
      || DRB.set_tab(OP.ref())
      ?}
   || _vwin:=''
   ?};
   D_RB.win_sel('WER'+_vwin);
   {? _vwin='OPE'
   || D_RB.hdr_sel();
      POZ.cntx_psh(); DOK.cntx_psh();
      POZ.use(ref_name(D_RB.POZ)); D_RB.POZ();
      DOK.use(ref_name(POZ.DOK)); POZ.DOK();
      _vtit:=' dla dokumentu '+DOK.NK+', pozycja '+$POZ.POZ;
      POZ.cntx_pop(); DOK.cntx_pop();
      D_RB.hdr_sel(_vtit)
   ?};
   {? D_RB.select(0,0,,_va)
   || D_RB.cntx_psh(); exec('ed_opdrb','raty'); D_RB.cntx_pop(); 1
   ?}
!};
exec('fp_bd','raty');
{? var_pres('DRB')>0 || obj_del(DRB) ?};
D_RB.FORMAPLA().KOD


\fp_f31
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Wywolanie wlasciwej fp_f3 ze skid_op.fml.
::   WE: _a: 0 - uruchamiana przez F3, 1 - select, 2 - select z OPTMP
::       _b: 0 - bez blokowania, 1 - blokowanie do edycji
::  OLD: \fp_f3/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
{? -(1+menu_txt(,))<>'s'
|| _vop:=0;
   {? _a=2
   || OP.cntx_psh(); OP.index('KON_O');
      OP.prefix(SSTALE.WAL,OPTMP.ODD,OPTMP.KONTO,OPTMP.SYM,OPTMP.SYM,OPTMP.SYM_ROK);
      _vop:=OP.first()
   ?};
   {? ~_a || _b:=_a ?};
   {? ~_b
   || {? (ROZRACH.TABELA='DOK' & DOK.A='T')
         | (ROZRACH.TABELA='POZ' & (POZ.DOK().A='T' | ROZRACH.WSK_PRAT='N'))
         | (ROZRACH.TABELA='OP' & OP.STAN='R')
         | _a=2
      || ROZRACH.LOCK:=1
      || ROZRACH.LOCK:=0
      ?}
   || ROZRACH.LOCK:=1
   ?};
   {? (ROZRACH.TABELA='DOK' & DOK.DOK_REJ().PR<>'T')
      | (ROZRACH.TABELA='POZ' & (~exec('czyLista','raty',POZ))
         & ROZRACH.WSK_PRAT<>'T')
      | (ROZRACH.TABELA='OP' & ~exec('czyLista','raty',OP)
      | (_a=2 & ~_vop))
   || {? _a | (~_a & ROZRACH.TABELA='OP')
      || {? ROZRACH.TABELA='DOK' || _vt:='dokumentu'@
         |? ROZRACH.TABELA='OP' & (_a=2 | _a=0) || _vt:='rozrachunku'@
         || _vt:='pozycji'@
         ?};
         FUN.info('Dla %1 nie zdefiniowano listy płatności.'@[_vt])
      || ''
      ?}
   || {? ROZRACH.TABELA='POZ' & exec('czyLista','raty',POZ)
         & ROZRACH.WSK_PRAT='T' & ((POZ.WAL
         & POZ.SUMW<>exec('wartPlat','raty','WAR',,POZ,POZ.ref()))
         | (~POZ.WAL & POZ.SUM<>exec('wartPlat','raty','WAR',,POZ,POZ.ref())))
      || exec('outLista','raty',POZ)
      ?};
      {? ROZRACH.TABELA='POZ' & ~exec('czyLista','raty',POZ)
         & ROZRACH.WSK_PRAT='T'
      || exec('addLista','raty',POZ,DOK.SP_PL,POZ.TP,{? POZ.WAL || POZ.SUMW || POZ.SUM ?})
      ?};
      {? ROZRACH.TABELA<>'POZ' |
         | (ROZRACH.TABELA='POZ' & (menu_txt(,)='płatNości ratalne'
         | (menu_txt(,)<>'płatNości ratalne' & ROZRACH.WSK_PRAT='T')))
      || exec('fp_f3','raty',($ROZRACH.TABELA)(),_a,ROZRACH.LOCK,{? _=3 & _c || _c || '' ?})
      ?}
   ?};
   {? _a=2 || OP.cntx_pop() ?}
?}


\copy_drb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Kopiuje do biezacego kontekstu tabeli D_RB rekordy
::       ustalonej (wg akronimu i refa tabeli) dziedziny D_RB.
::   WE: _a: akronim tabeli (DOK, POZ, OP)
::       _b: ref rekordu tabeli z paramteru _a
::   WY: 0 - nie skopiowano, 1 - kopiowanie zakonczono pomyslnie
::  OLD: \copy_drb/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
D_RB.cntx_psh(); D_RB.index(exec('ndxD_RB','raty',_a));
{? _a=DOK | _a=POZ
|| D_RB.prefix(SSTALE.AO,_b)
|| D_RB.prefix(_b)
?};
{? D_RB.first()
|| {!
   |? _vref:=D_RB.ref(); D_RB.prefix();
      {? _a<>OP
      || D_RB.DOK:=exec('dv_drb','raty','DOK');
         D_RB.POZ:=exec('dv_drb','raty','POZ')
      ?};
      D_RB.OP:=exec('dv_drb','raty','OP');
      D_RB.add();
      {? _a=DOK | _a=POZ
      || D_RB.prefix(SSTALE.AO,_b)
      || D_RB.prefix(_b)
      ?};
      D_RB.seek(_vref); D_RB.next()
   !}
?};
D_RB.cntx_pop()


\dv_drb
::----------------------------------------------------------------------------------------------------------------------
::  OLD: \dv_drb/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=ROZRACH.TABELA || ($(_a+'.ref()'))()
|? _a='POZ' & ROZRACH.TABELA='OP'
|| {? var_pres('vpozref')=7 || vpozref || POZ.ref() ?}
|| null
?}


\obj_drbt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Obiekt do obslugi platnosci ratalnych.
::       TPF      termin platnosci pierwszej raty
::       TPL      termin platnosci ostatniej raty
::       TPN      termin platnosci kolejnej raty (pierwsza niezaplacona)
::       TPZ      termin zaplaty raty (ostatnia zrealizowana platnosc)
::       LRAT     liczba rat
::       SUMT     suma rat
::       SUMS     saldo platnosci ratalnych
::       RF       kwota najblizszej platnosci
::       STR      strona ksiegowa na ktorej spodziewane sa zapisy rozliczajace
::       EXIST    wskaznik istnienia platnosci ratalnych dla rozrachunku
::       clr_fld  zeruje wartosci pol
::       cr_tab   tworzy tablice
::       fill_tab wypelnia tablice
::                _a: element tablicy
::       get_dobr zwraca obroty rat lub zaplat na podana date
::                _a: data
::                _b: 1 - raty, 2 - zaplaty
::       get_drat zwraca termin platnosci ostatniej na podana date raty
::                _a: data
::       get_rzap zwraca kwote do zaplaty (raty lub rat) na podana date
::                _a: data
::       get_str  zwraca strone ksiegowa zapisow rozliczajacych raty
::                _a: OP.ref()
::       get_szap zwraca sume zapisow rozliczajacych platnosci ratalne
::                _a: OP.ref()
::                _b: D_RB.ref()
::       set_fld  ustala wartosci pol obiektu
::                _a: element tablicy
::                _b: termin platnosci raty
::                _c: kwota raty
::                _d: saldo platnosci
::                _e: kwota zapisu rozliczajacego po rozliczeniu raty
::       set_tab  wypelnia tablice i ustala wartosci pol obiektu
::                _a: OP.ref()
::                _b: POZ.ref() [opcjonalny]
::       set_tab1 pomocnicza do set_tab
::  OLD: \obj_drbt/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('DRB');

obj_decl('DRB',

obj_fld('TPF',date(0,0,0)),
obj_fld('TPL',date(0,0,0)),
obj_fld('TPN',date(0,0,0)),
obj_fld('TPZ',date(0,0,0)),
obj_fld('LRAT',0),
obj_fld('SUMT',0),
obj_fld('SUMS',0),
obj_fld('RF',0),
obj_fld('STR',''),
obj_fld('EXIST',0),

obj_meth('clr_fld',"
   .TPF:=.TPL:=.TPN:=.TPZ:=date(0,0,0); .SUMT:=.SUMS:=.EXIST:=.LRAT:=0;
   .STR:=''"),

obj_meth('cr_tab',"
   VAR_DEL.delete('t_drb');
   t_drb:=obj_new(D_RB.size())"),

obj_meth('fill_tab',"
   t_drb[_a]:=obj_new(4);
   t_drb[_a][1]:=_b;
   t_drb[_a][2]:=_c;
   t_drb[_a][3]:=_d;
   t_drb[_a][4]:=_e"),

obj_meth('get_dobr',"
   _ret:=_i:=0;
   {! |?
      _i+=1; {? t_drb[_i][3]<=_a || _ret+=t_drb[_i][_b] ?};
      t_drb[_i][3]<=_a & _i<obj_len(t_drb)
   !}; _ret"),

obj_meth('get_drat',"
   _ret:=date(0,0,0); _i:=0;
   {! |?
      _i+=1; {? t_drb[_i][3]<=_a || _ret:=t_drb[_i][3] ?};
      t_drb[_i][3]<=_a & _i<obj_len(t_drb)
   !}; _ret"),

obj_meth('get_rzap',"
   _ret:=_i:=0;
   {! |?
      _i+=1; {? t_drb[_i][3]<=_a || _ret+=t_drb[_i][1]-t_drb[_i][2]?};
      t_drb[_i][3]<=_a  & _i<obj_len(t_drb)
   !}; _ret"),

obj_meth('get_str',"
   {? D_RB.cntx_psh(); D_RB.clear(); D_RB.seek(_a)
   || POZ.cntx_psh(); OKRO_F.cntx_psh(); ROK_F.cntx_psh();
      POZ.use('pozy'+D_RB.OKRO_F().ROK().KOD+form(OKRO_F.NR,-2));
      POZ.clear(); {? POZ.seek(D_RB.POZ) || .STR:=POZ.STR ?};
      POZ.cntx_pop(); OKRO_F.cntx_pop(); ROK_F.cntx_pop()
   ?}; D_RB.cntx_pop();
   .STR:={? .STR='Wn' || 'MA' |? .STR='Ma' || 'WN' || '' ?}; .STR"),

obj_meth('get_szap',"
   _ret:=0;
   .get_str(_b);
   {? ZAP_OP.index('OP'); ZAP_OP.prefix(_a); ZAP_OP.first()
   || {! |? _ret+=($('ZAP_OP.'+.STR))(); ZAP_OP.next() !}
   ?}; _ret"),

obj_meth('set_fld',"
   {? _a=1 || .TPF:=_b ?}; .SUMT+=_c; .TPN:=_b; .SUMS+=_d;
   {? _e<0 || .TPZ:=_b ?}; {? ~.RF || .RF:=_d; .TPN:=_b ?}"),

obj_meth('set_tab',"
.clr_fld();
{? _>=2 & type_of(_b)=7
|| D_RB.index('OPPOZ'); D_RB.prefix(_a,_b)
|| D_RB.index('OP'); D_RB.prefix(_a)
?};
{? D_RB.first()
|| .cr_tab(); .get_str(D_RB.ref()); {? _<3 || _c:=date() ?}; .set_tab1(_a,_c)
?}; .EXIST"),

obj_meth('set_tab1',"
{? ZAP_OP.index('OP'); ZAP_OP.prefix(_a); ZAP_OP.first()
|| _i:=1; _vz:=0;
   {! |?
      {? ZAP_OP.DATA<=_b
      || {? -.STR='wn'
         || _vz+=ZAP_OP.WN;
            {? ZAP_OP.MA<0 || _vz+=-ZAP_OP.MA ?}
         || _vz+=ZAP_OP.MA;
            {? ZAP_OP.WN<0 || _vz+=-ZAP_OP.WN ?}
         ?};
         {? _vz
         || {! |? _vz>=D_RB.WAR & _i<=D_RB.size() |!
               _vtp:=D_RB.TERMPLAT; _vwar:=D_RB.WAR;
               .fill_tab(_i,_vwar,_vwar,_vtp,ZAP_OP.DATA); _vz-=_vwar;
               .set_fld(_i,_vtp,_vwar,t_drb[_i][1]-t_drb[_i][2],_vz);
               {? _vz<0 || _vz:=0 || _i+=1; D_RB.next() ?}
            !}
         ?}
      ?};
      ZAP_OP.next()
   !};
   {? _vz & (_i<=D_RB.size() | _i=1)
   || .fill_tab(_i,D_RB.WAR,_vz,D_RB.TERMPLAT,ZAP_OP.DATA);
      .set_fld(_i,D_RB.TERMPLAT,_vz,t_drb[_i][1]-t_drb[_i][2],_vz)
   |? ~_vz
   || D_RB.prev(); _i-=1
   ?};
   _vn:={? _i & _i<=D_RB.size() & t_drb[_i][2] || D_RB.next() || 1 ?};
   {? _vn & _i<D_RB.size()
   || {! |?
         _i+=1; .fill_tab(_i,D_RB.WAR,0,D_RB.TERMPLAT,date(0,0,0));
         {? .TPF=date(0,0,0) || .TPF:=D_RB.TERMPLAT ?}; D_RB.next()
      !}
   ?};
   .TPL:=D_RB.TERMPLAT; .LRAT:=D_RB.size(); .EXIST:=1
?}")); 1


\fp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BL [8.50]
::   WE: [_a - alias tabeli, z ktora zwiazana jest platnosc: OP, POZ, DOK
::           jesli pominiety - akronim ktorejs z ww tabel przechowywany w zmiennej ROZRACH.TABELA ]
::  OLD: \fp_bd/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZRACH.TABELA='' || ROZRACH.TABELA:='OP' ?};
{? ~_ || _a:=($ROZRACH.TABELA)() ?};
{? ~_
|| exec('flddisp','color','DOK#02#00')
|| {? ~(_lista:=exec('czyLista','raty',_a,1))
   || ROZRACH.FORMAPLA:='<- F3 ->';
      ROZRACH.PLATOPIS:=80*'.';
      ROZRACH.TERMPLAT:=date(0,0,0)
   || exec('jakaPlat','raty',_lista)
   ?}
?}


\ed_opdrb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Przygotowuje srodowisko do edycji listy platnosci z poziomu OP.
::  OLD: \ed_opdrb/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
vpozref:=D_RB.POZ; vpozokr:=D_RB.OKRO_F;
ROZRACH.KW2RAT:=exec('wartPlat','raty','WAR',,POZ,vpozref);
{? D_RB.first()
|| _vmask:=D_RB.name();
   D_RB.use('dr__xxxx'); D_RB.index('OP'); D_RB.prefix(OP.ref());
   {? D_RB.first() || {! |? D_RB.del() !} ?}; D_RB.use(_vmask);
   {? D_RB.first()
   || {! |?
         {? D_RB.POZ=vpozref
         || D_RB.use('dr__xxxx'); D_RB.add(); D_RB.use(_vmask)
         ?};
         D_RB.next()
      !}
   ?};
   D_RB.use('dr__xxxx')
?};
exec('fp_f31','raty',1,0,vpozref)


\drb_tpfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Zwraca artybut ostatniej platnosci z listy w zaleznosci
::       od parametru _b dla listy zwiazanej z tabela _a
::   WE: _a: akronim tabeli (DOK, POZ, OP)
::       _b: wartosc zwracana: 0 - ref SLO dla formy platnosci
::           1 - termin platnosci
::   WY: dla _b=0 SLO.ref lub null
::       dla _b=1 termin platnosci lub date(0,0,0)
::  OLD: \drb_tpfp/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:={? _b || date(0,0,0) || null ?};
_vref:=exec('czyLista','raty',($(_a))(),1);
{? _vref
|| D_RB.cntx_psh(); D_RB.clear();
   {? D_RB.seek(_vref) || _ret:={? _b || D_RB.TERMPLAT || D_RB.FORMAPLA ?} ?};
   D_RB.cntx_pop()
?}; _ret


\fps0_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Wyswietlenie platnosci ratalnych
::  OLD: \fps0_f3/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:={? ROZRACH.TABELA='OP' || OP.r_lock(1,1) || 1 ?};
{? _ok
|| exec('fp_f31','raty',1,0)
|| FUN.info('Rozrachunek wykorzystywany przez innego użytkownika.'@)
?};
{? ROZRACH.TABELA='OP' & _ok || OP.r_unlock() ?}


\updt_drb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Modyfikuje w biezacym kontekscie tabeli D_RB odpowiednie
::       pole zlaczeniowe.
::       Jesli podano parametry _c i _d, to modyfikowanie nastepuje, jesli
::       pole _c ma wartosc _d - wykorzystywane przy wycofaniu ksiegowania
::   WE: _a: akronim pola (DOK, POZ, OP)
::       _b: ref
::       [_c: akronim pola (DOK, POZ, OP)]
::       [_d: ref]
::  OLD: \updt_drb/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
D_RB.cntx_psh();
{? D_RB.first()
|| {!
   |? _vs:=D_RB.size();
      {? _<3 | ($('D_RB.'+_c+'=_a'))(_d)
      || ($('D_RB.'+_a+':=_a'))(_b)
      ?};
      D_RB.cntx_psh(); D_RB.clear(); D_RB.put(); D_RB.cntx_pop();
      {? _vs>D_RB.size() || D_RB.first() || D_RB.next() ?}
   !}
?};
D_RB.cntx_pop()


\dv_okdrb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Wartosc domyslna pola OKRO_F tabeli D_RB
::  OLD: \dv_okdrb/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('vpozokr')=7 || vpozokr || SSTALE.AO ?}


\baddD_RB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BL [8.50]
:: OPIS: suma wartosci pola przekazanego parametrem _a w rekordach "ustalonej" dziedziny tabeli (D_RB)
::       wywolywane w : definicja, D_RB.win_sel('WER')
::  OLD: \baddD_RB/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
exec('wartPlat','raty','WAR','ROZRACH.SUMAWAR');
exec('be_splkh','kontrahent');
exec('wssumdrb','raty');
{? ROZRACH.TABELA='DOK'
|| ROZRACH.DATA:=DOK.DTO
|? ROZRACH.TABELA='POZ'
|| ROZRACH.DATA:=POZ.DOK().DTO
|? ROZRACH.TABELA='OP'
|| ROK_F.cntx_psh(); OKRO_F.cntx_psh(); POZ.cntx_psh(); DOK.cntx_psh();
   {? OKRO_F.prefix(); OKRO_F.seek(vpozokr)
   || DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
      POZ.use('pozy'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2)); POZ.prefix();
      {? POZ.seek(vpozref)
      || ROZRACH.DATA:=POZ.DOK().DTO
      ?}
   ?};
   ROK_F.cntx_pop(); OKRO_F.cntx_pop(); POZ.cntx_pop(); DOK.cntx_pop()
?}; 1


\wssumdrb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Wyswietlanie sumy rat w okienku D_RB.
::   WY: Suma rat
::  OLD: \wssumdrb/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.SUMAPROC:=exec('crsumdrb','raty')


\listaRat
::----------------------------------------------------------------------------------------------------------------------
:: UTW:  BL  [8.50]
:: OPIS:  funkcja generujaca liste rat platnosci
::  WE:  _a - pelna wartosc do rozbicia na raty
::       _b - data pierwszej raty nie moze byc wczesniejsza od wartosci tego parametru
::            jesli pominiety data pierwszej raty nie bedzie kontrolowana
::  WY:  1 - operacja zakonczona pomyslnie
::       0 - wpp.
::  OLD: \listaRat/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
_Uwaga:='';
_datanie0:=0;

XINFO.SLP(); _slu:=XINFO.SLP; _swieta:=1; _soboty:=1;
{? ROZRACH.TABELA='DOK'
|| _b:=ROZRACH.TERMPLAT:=DOK.DTO
|? ROZRACH.TABELA='POZ'
|| _b:=ROZRACH.TERMPLAT:=POZ.DOK().DTO;
   {? POZ.WAL
   || ROZRACH.KW2RAT:=POZ.SUMW
   || ROZRACH.KW2RAT:=POZ.SUM
   ?}
|? ROZRACH.TABELA='OP'
|| ROK_F.cntx_psh(); OKRO_F.cntx_psh(); POZ.cntx_psh(); DOK.cntx_psh();
   {? OKRO_F.prefix(); OKRO_F.seek(vpozokr)
   || DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
      POZ.use('pozy'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2)); POZ.prefix();
      {? POZ.seek(vpozref)
      || _b:=ROZRACH.TERMPLAT:=POZ.DOK().DTO
      ?};
      ROZRACH.KW2RAT:={? POZ.WAL || POZ.SUMW || POZ.SUM ?}
   ?};
   ROK_F.cntx_pop(); OKRO_F.cntx_pop(); POZ.cntx_pop(); DOK.cntx_pop()
|| _b:=ROZRACH.TERMPLAT:=DOK.DTO
?};

:/jezeli nie udalo sie "znalezc" formy platnosci kontrahenta "pobierana"
:/jest domyslna forma platnosci dla splat ratalnych z listy parametrow (19)
ROZRACH.PLATREF:=exec('SLOzLpar','slo_slu',_slu,19);
{? ROZRACH.PLATREF<>0
|| ROZRACH.COILE:=#(exec('get_par','slo_slu',ROZRACH.PLATREF,2));
   ROZRACH.JAKPLAT:='D'
?};
_ask:='';
_choice1:='Tak';
_choice2:='Rezygnacja';
_choice3:=_choice4:='';
_edit:=0;
_wybor:=1;
_choice:=2;
{? D_RB.size
|| _ask:='\n\n'+'Dla '
        +{? 'DOKD'*ROZRACH.TABELA
         || 'dokumentu '
         |? 'POZ'*ROZRACH.TABELA
         || 'pozycji '
         |? 'OP'*ROZRACH.TABELA
         || 'rozrachunku '
         |? 'ZAM'*ROZRACH.TABELA
         || 'zamówienia '
         ?}
        +'istnieją płatności!\n'
        +'Utworzyć nową listę płatności?'
|| _ask:='Utworzyć listę płatności?'
?};
_choice:=choice(_ask,
               'Zapłaty ratalne','ASK',,_wybor-1,_wybor+1,
               _choice1,_choice2,_choice3,_choice4
               );
{? _choice<_wybor
|| ROZRACH.win_edit('RATY');
   ROZRACH.TERMPLAT+=ROZRACH.COILE;
   {? var_pres('_b')=4
   || ROZRACH.DATA:=_b
   ?};
   {? {? ~(_choice=0 & _wybor>1)
      || {? var_pres('_b')=4
         || (_edit:=ROZRACH.edit("exec('chkRaty','raty',ROZRACH.DATA)"))
         || (_edit:=ROZRACH.edit("exec('chkRaty','raty')"))
         ?}
      || _edit:=1; 0
      ?}
   || _data:=ROZRACH.TERMPLAT;
      exec('outLista','raty');
      _data==ROZRACH.TERMPLAT;
      ROZRACH.SUMAWAR:=ROZRACH.SUMAPROC:=0;
      _a:=ROZRACH.KW2RAT;
      _reszta:=_a$2-ROZRACH.SUMAWAR$2;
      _rata:=(_reszta/ROZRACH.ILERAT)$2;
      _dziendobry:=@.ROZRACH.TERMPLAT~3;
      {? _reszta
      || {? ~_rata || ROZRACH.ILERAT:=1; _rata:=_reszta ?};
         _dalej:=_pytac:=1;
         {! _i:=1..ROZRACH.ILERAT
         |? _dalej
         |! {? _reszta
            || D_RB.blank();
               D_RB.WAR:=_rata;
               D_RB.FORMAPLA:=ROZRACH.PLATREF;
               {? ROZRACH.JAKPLAT='D'
               || ROZRACH.TERMPLAT+=(_i>1)*ROZRACH.COILE;
                  _rok:=ROZRACH.TERMPLAT~1;
                  D_RB.TERMPLAT:=exec('dzienRob','kalendarz',ROZRACH.TERMPLAT,_swieta,_soboty,_datanie0)
               |? ROZRACH.JAKPLAT='T'
               || ROZRACH.TERMPLAT+=(7*(_i>1)*ROZRACH.COILE);
                  _rok:=ROZRACH.TERMPLAT~1;
                  D_RB.TERMPLAT:=exec('dzienRob','kalendarz',ROZRACH.TERMPLAT,_swieta,_soboty,_datanie0)
               |? ROZRACH.JAKPLAT='M'
               || _rok:=ROZRACH.TERMPLAT~1+( (_i>1)*ROZRACH.COILE%12 );
                  _miesiac:=ROZRACH.TERMPLAT~2+((_i>1)*(ROZRACH.COILE%*12));
                  _dzien:={? ROZRACH.KONIEC<>'T' | _i=1 || ROZRACH.TERMPLAT~3 || 0 ?};
                  {? _miesiac>12
                  || _miesiac:=_miesiac-12; _rok+=1
                  ?};
                  {? ROZRACH.KONIEC<>'T'
                  || {? (_ilDni:=date(_rok,_miesiac,0)~3)<=_dzien
                     || _dzien := _ilDni
                     || _dzien:=_dziendobry
                     ?}
                  ?};
                  ROZRACH.TERMPLAT:=date(_rok,_miesiac,_dzien);
                  D_RB.TERMPLAT:=exec('dzienRob','kalendarz',ROZRACH.TERMPLAT,_swieta,_soboty,_datanie0)
               ?};
               {? D_RB.TERMPLAT=date(0,0,0) & _pytac
               || _pytac:=0;
                  {? ~exec('kal_ok','raty',_rok,1)
                  || _dalej:=0
                  ?}
               ?};
               {? _dalej
               || D_RB.add(1);
                  _reszta-=_rata
               ?}
            ?}
         !};
         {? _dalej
         || {? _reszta || D_RB.WAR+=_reszta; D_RB.put ?};
            ROZRACH.SUMAPROC:=_a
         |? D_RB.first()
         || {! |? D_RB.del() !}
         ?}
      ?}
   || {? _edit
      || exec('outLista','raty')
      ?}
   ?}
?};
_choice<>2


\kal_ok
::----------------------------------------------------------------------------------------------------------------------
:FUNKCJA:   kal_ok
:UTWORZYL:  BL [8.50]
:OPIS:      sprawdza czy zdefiniowano wzorce kalendarzy oraz
:           czy przypisano jakis w parametrach systemu oraz
:           czy zdefiniowano swieta do podanego roku
:WOLANA:    (listaRat,skid_op)
:WE:        _a - rok
:           _b - reakcja w przypadku niekompletnych danych
:           = 1 - pytanie i wybor
:           = 0 - dzialanie automatyczne zalezne OD _kal_ok
:WY:        _kal_ok
:           = 1 - poprawnie zdefiniowane kalendarze
:           = 0 - wpp
::  OLD: \kal_ok/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
_kal_ok:=0;
_ask:='';
_co_dalej:='\nKontynuowanie operacji spowoduje'
          +'\npowstanie zapisów z zerowymi terminami płatności.';
KAL_WZOR.cntx_psh();
KAL_WZOR.index('KAL_WZOR');
KAL_WZOR.prefix(exec('ref_firma','ustawienia'));
KAL_OPIS.cntx_psh();
KAL_OPIS.index('KAL_OPIS');
KAL_OPIS.prefix();
{? _ & type_of(_a)=1
|| {? (_wzorzec:=PAR_SKID.get(8))<>''
   || {? KAL_WZOR.find_key(_wzorzec)
      || {? KAL_OPIS.find_key(KAL_WZOR.ref,_a)
         || _kal_ok:=1
         || _ask:='Nie zredagowano kalendarza '+KAL_WZOR.NAZWA+' na rok '+$_a+'!'
         ?}
      || _ask:='Błędy w definicji wzorcy kalendarzy!'
              +'\nBrak wzorca '+_wzorzec+' przypisanego do systemu!'
      ?}
   || _ask:='Nie przypisano wzorca kalendarza w parametrach systemu!'
           +'\nParametr nr 8'
   ?}
|| _ask:='Błąd funkcji sprawdzającej kompletność danych wzorców kalendarzy'
?};
KAL_OPIS.cntx_pop();
KAL_WZOR.cntx_pop();
{? ~_kal_ok & _=2 & _b
|| _choice:=choice(_ask+_co_dalej,'Zapłaty ratalne','ASK',,0,1,'Przerwij','Kontynuuj');
   _kal_ok:=_choice
?};
_kal_ok


\chkRaty
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Sprawdzenie rat
::  OLD: \chkRaty/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
{? _
|| _a:='';
   {? ~ROZRACH.PLATREF
   || FUN.info('Nie wybrano sposobu płatności.'@); _a:='PLATREF'
   ?};
   {? _a='' & ROZRACH.FORMAPLA=''
   || FUN.info('Nie wybrano płatności.'@); _a:='FORMAPLA'
   ?};
   {? _a='' & ROZRACH.ILERAT<0 || FUN.info('Liczba rat nie może być mniejsza od zera.'@); _a:='ILERAT' ?};
   {? _a='' & ROZRACH.TERMPLAT=date(0,0,0) || FUN.info('Nie podano terminu platności pierwszej raty.'@); _a:='TERMPLAT' ?};
   {? _a='' & ROZRACH.DATA>ROZRACH.TERMPLAT
   || FUN.info('Data nie może być wcześniejsza od daty wystawienia dokumentu.'@); _a:='TERMPLAT'
   ?};
   {? _a='' & ROZRACH.COILE<=0 || FUN.info('Nie podano prawidłowo, co ile mają być płacone raty.'@); _a:='COILE' ?};
   {? _a='' & ROZRACH.ILERAT>0
   || _a:=exec('kal_ok','raty',exec('jaki_rok','raty'),1)
   || {? _a='' || _a:='ILERAT' ?}
   ?}
|| {? (_a:=chk_rec())='' || _a:=exec('kal_ok','raty',exec('jaki_rok','raty'),1) || _a ?}
?};
_a


\jaki_rok
::------------------------------------------------------------------------------
:FUNKCJA:   jaki_rok
:UTWORZYL:  BL [8.50]
:: OPIS:      wyznacza numer roku, w ktorym przypadnie data ostatniej raty
:WOLANA:    (listaRat,skid_op)
:WE:
:WY:        numer roku
::  OLD: \jaki_rok/skid_op.fml
::------------------------------------------------------------------------------
_data:=ROZRACH.TERMPLAT;
{? ROZRACH.JAKPLAT='D'
|| (_data+ROZRACH.COILE*ROZRACH.ILERAT)~1
|? ROZRACH.JAKPLAT='T'
|| (_data+ROZRACH.COILE*ROZRACH.ILERAT*7)~1
|| _data~1+ROZRACH.ILERAT%12+((_data~2+ROZRACH.ILERAT%*12)>12)
?}


\ra_drb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Formula po rekordzie dla D_RB.
::  OLD: \ra_drb/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~D_RB.FORMAPLA
|| FUN.info('Nie podano sposobu płatności.'@); 'FORMAPLA'
|? D_RB.TERMPLAT=date(0,0,0)
|| FUN.info('Nie podano daty.'@); 'TERMPLAT'
|? D_RB.TERMPLAT<ROZRACH.DATA
|| FUN.info('Data nie może być wcześniejsza od daty wystawienia dokumentu.'@);
   D_RB.TERMPLAT:=ROZRACH.DATA; 'TERMPLAT'
|? ~D_RB.WAR
|| FUN.info('Nie podano kwoty.'@); 'WAR'
|| ''
?}


\wa_drb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Po okienku platnosci D_RB.
::  OLD: \wa_drb/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
{? D_RB.size()
|| _vtp:=exec('drb_tpfp','raty',ROZRACH.TABELA,1);
   {? ROZRACH.TABELA='DOK'
   || DOK.D3:=_vtp; DOK.put()
   |? ROZRACH.TABELA='POZ'
   || POZ.TP:=_vtp; POZ.put()
   ?};
   {? ROZRACH.TABELA='OP'
   || {? ~exec('chdrbpoz','raty')
      || {? D_RB.win_sel('?')='WEROPE'
         || FUN.info('Niezgodność kwoty dekretu z listą płatności.\n'
                     'Kwota w pozycji dokumentu: %1\n'
                     'Kwota płatności ratalnych: %2\n'
                     'Różnica: %3'@[form(vpoz,,2,' ,'),form(vsum,,2,' ,'),form(vpoz-vsum,,2,' ,')]);
            _ret:=0
         ?}
      || OP.TZ:=_vtp; OP.put();
         exec('pop_all','rozrach',1)
      ?}
   |? ROZRACH.TABELA='POZ'
   || _vsum:=0;
      D_RB.cntx_psh(); D_RB.index('POZ'); D_RB.prefix(SSTALE.AO,POZ.ref());
      {? D_RB.first()
      || {! |? _vsum+=D_RB.WAR; D_RB.next() !}
      ?};
      _vpozsum:={? POZ.WAL || POZ.SUMW || POZ.SUM ?};
      {? (POZ.WAL & POZ.SUMW<>_vsum) | (~POZ.WAL & POZ.SUM<>_vsum)
      || FUN.info('Niezgodność kwoty pozycji z listą płatności.\n'
                  'Kwota w pozycji dokumentu: %1\n'
                  'Kwota płatności ratalnych: %2\n'
                  'Różnica: %3'@[form(_vpozsum,,2,' ,'),form(_vsum,,2,' ,'),form(_vpozsum-_vsum,,2,' ,')]);
         _ret:=0
      ?};
      D_RB.cntx_pop()
   ?}
|| {? FUN.ask('Dodać płatność'@)
   || ROZRACH.win_edit('RATA');
      XINFO.SLP(); _slu:=XINFO.SLP;
      {? ROZRACH.PLATREF=0
      || ROZRACH.PLATREF:=exec('SLOzLpar','slo_slu',_slu,19)
      ?};
      {? ROZRACH.PLATREF<>0
      || ROZRACH.COILE:=#(exec('get_par','slo_slu',ROZRACH.PLATREF,2));
         ROZRACH.JAKPLAT:='D'
      ?};
      {? ROZRACH.TABELA='DOK'
      || ROZRACH.TERMPLAT:=ROZRACH.DATA:=DOK.DTO;
         ROZRACH.KW2RAT:=DOK.SUMWAL
      |? ROZRACH.TABELA='POZ'
      || ROZRACH.TERMPLAT:=ROZRACH.DATA:=POZ.DOK().DTO;
         {? POZ.WAL
         || ROZRACH.KW2RAT:=POZ.SUMW
         || ROZRACH.KW2RAT:=POZ.SUM
         ?}
      |? ROZRACH.TABELA='OP'
      || ROK_F.cntx_psh(); OKRO_F.cntx_psh(); POZ.cntx_psh(); DOK.cntx_psh();
         {? OKRO_F.prefix(); OKRO_F.seek(vpozokr)
         || DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
            POZ.use('pozy'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2)); POZ.prefix();
            {? POZ.seek(vpozref)
            || ROZRACH.TERMPLAT:=ROZRACH.DATA:=POZ.DOK().DTO
            ?};
            ROZRACH.KW2RAT:={? POZ.WAL || POZ.SUMW || POZ.SUM ?}
         ?};
         ROK_F.cntx_pop(); OKRO_F.cntx_pop(); POZ.cntx_pop(); DOK.cntx_pop()
      ?};
      {? ROZRACH.edit("
            _ret:={? ~ROZRACH.PLATREF
                  || FUN.info('Nie wybrano sposobu płatności.'@); 'PLATREF'
                  |? ROZRACH.TERMPLAT=date(0,0,0)
                  || FUN.info('Nie podano terminu platności pierwszej raty.'@); 'TERMPLAT'
                  |? ROZRACH.KW2RAT$2<=0
                  || FUN.info('Nie podano poprawnie kwoty raty.'@); 'KW2RAT'
                  || ''
                  ?};
            {? +_ret
            || _ret
            || {? ROZRACH.TERMPLAT<ROZRACH.DATA
               || FUN.info('Data nie może być wcześniejsza\n od daty wystawienia dokumentu.'@);
                  ROZRACH.TERMPLAT:=ROZRACH.DATA; _ret:='TERMPLAT'
               ?}
            ?}; _ret")
      || exec('addLista','raty',($ROZRACH.TABELA)(),ROZRACH.PLATREF,
            ROZRACH.TERMPLAT,ROZRACH.KW2RAT);
         {? ROZRACH.TABELA='DOK'
         || DOK.D3:=ROZRACH.TERMPLAT; DOK.put()
         |? ROZRACH.TABELA='POZ'
         || POZ.TP:=ROZRACH.TERMPLAT; POZ.put()
         ?};
         win_disp();
         {? ~exec('chdrbpoz','raty')
         || {? D_RB.win_sel('?')='WEROPE'
            || FUN.info('Niezgodność kwoty dekretu z listą płatności.\n'
                        'Kwota w pozycji dokumentu: %1\n'
                        'Kwota płatności ratalnych: %2\n'
                        'Różnica: %3'@[form(vpoz,,2,' ,'),form(vsum,,2,' ,'),form(vpoz-vsum,,2,' ,')]);
               _ret:=0
            ?}
         ?}
      || _ret:=0
      ?}
   || _ret:=0
   ?}
?};
{? _ret & D_RB.win_sel('?')='WEROPE'
|| {? D_RB.first()
   || do();
      _vmask:='dr__fk'+SSTALE.AR().KOD;
      D_RB.use(_vmask); D_RB.index('OP'); D_RB.prefix(OP.ref());
      {? D_RB.first()
      || {! |? {? D_RB.POZ=vpozref || D_RB.del() || D_RB.next() ?} !}
      ?};
      D_RB.use('dr__xxxx');
      {? D_RB.first()
      || {! |?
            {? D_RB.POZ=vpozref
            || D_RB.use(_vmask); D_RB.add(); D_RB.use('dr__xxxx')
            ?};
            D_RB.del()
         !}
      ?};
      D_RB.use(_vmask);
      &vpozref; &vpozokr;
      exec('pop_all','rozrach',1);
      end()
   ?}
?};
_ret


\chdrbpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Sprawdza zgodnosc rat w OP z POZ.
::  OLD: \chdrbpoz/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
D_RB.cntx_psh(); D_RB.index('OPPOZ'); D_RB.prefix(OP.ref());
{? D_RB.first()
|| _vref:=null;
   {! |?
      {? _vref<>D_RB.POZ
      || vsum:=vpoz:=0;
         D_RB.cntx_psh(); D_RB.prefix(OP.ref(),D_RB.POZ);
         {? D_RB.first()
         || {! |? vsum+=D_RB.WAR; D_RB.next() !}
         ?};
         D_RB.cntx_pop(); POZ.cntx_psh();
         POZ.use('pozy'+D_RB.OKRO_F().ROK().KOD+form(OKRO_F.NR,-2));
         POZ.clear();
         {? POZ.seek(D_RB.POZ)
            & ((POZ.WAL & POZ.SUMW<>vsum) | (~POZ.WAL & POZ.SUM<>vsum))
         || vpoz:={? POZ.WAL || POZ.SUMW || POZ.SUM ?}; _ret:=0
         ?};
         POZ.cntx_pop();
         _vref:=D_RB.POZ
      ?};
      _ret & D_RB.next()
   !}
?};
D_RB.cntx_pop();
_ret


\be_kw2rt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2010]
:: OPIS: Przed redakcja ROZRACH.KW2RT
::  OLD: \be_kw2rt/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.TABELA<>'POZ'


\ae_kw2rt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Po redakcji kwoty do podzialu na raty.
::  OLD: \ae_kw2rt/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~ROZRACH.KW2RAT
|| FUN.info('Nie podano kwoty do podziału.'@); 0
|| 1
?}


\ae_jakpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Formula po redakcji sposobu platnosci rat
::  OLD: \ae_jakpl/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZRACH.JAKPLAT<>'M' || ROZRACH.KONIEC:='N' ?}; 1


\be_konms
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Formula przed redakcja znacznika konca miesiaca dla raty
::  OLD: \be_konms/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
ROZRACH.JAKPLAT='M'


\aewprpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MM] [8.50]
:: OPIS: Formula po redakcji znacznika rat w POZ.
::  OLD: \aewprpoz/dok_zrd3.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('dolpoz')<=0 & ROZRACH.WSK_PRAT='T' & ~exec('czyLista','raty',POZ)
|| POZ.TP:=DOK.D3;
   exec('addLista','raty',POZ,DOK.SP_PL,POZ.TP,POZ.SUM)
?};
win_disp()


\rb_zapl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Rekord przed w oknie rozliczenia rat.
::  OLD: \rb_zapl/skid_op.fml
::----------------------------------------------------------------------------------------------------------------------
_vref:=D_RB.ref(); _i:=0;
D_RB.cntx_psh(); D_RB.first(); {! |? _i+=1; D_RB.ref()<>_vref & D_RB.next() !}; D_RB.cntx_pop();
ROZRACH.ZAPL_KW:=t_drb[_i][2]; ROZRACH.ZAPL_DAT:=t_drb[_i][4];
POZ.cntx_psh(); DOK.cntx_psh();
POZ.use(ref_name(D_RB.POZ)); D_RB.POZ();
DOK.use(ref_name(POZ.DOK)); POZ.DOK();
ROZRACH.PLATINFO:=DOK.NK+'/'+$POZ.POZ;
POZ.cntx_pop(); DOK.cntx_pop();
{? ROZRACH.ZAPL_KW=D_RB.WAR || exec('findtmp','#color') || 0 ?}


\czy_raty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Czy rozrachunek jest ratalny
::  OLD: \czy_raty/auto_akt.fml
::----------------------------------------------------------------------------------------------------------------------
D_RB.index('OP'); D_RB.prefix(OP.ref());
D_RB.first()


\crsumdrb
::----------------------------------------------------------------------------
::  UTW: [AMA] [8.50]
:: OPIS: Oblicza sume rat dla biezacej dziedziny D_RB.
::   WY: Suma rat
::  OLD: \crsumdrb/skid_op.fml
::----------------------------------------------------------------------------
_ret:=0;
D_RB.cntx_psh(); {? D_RB.first() || {! |? _ret+=D_RB.WAR; D_RB.next() !} ?}; D_RB.cntx_pop(); _ret

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:00 310f37549cbd3bf28b28b6fcce0ecf7283b4cd568f97d2f673421c0747f8deccf3fb89ebd0a0f153d4ce3c9d7e596d5a19cd5a4ae4c7ca567cedebdd0177704e4b7840027d04ba791f3f6d312c4a3b21090c25696cc660c5ac92429b155d86531fb612cc94c48dfa77fda8c04481abd4ae34291cd2f438f2efb48c39c3f722b9

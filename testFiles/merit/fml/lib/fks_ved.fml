:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: fks_ved.fml
:: Utworzony: 03.08.2015 [17.00]
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi deklaracji podatkowych VAT,CIT
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawienia obszaru FKS_VED
::   WE: _a - rok
::       _b - okres
::----------------------------------------------------------------------------------------------------------------------
SIK.ROK1:={? var_press('_a')>0 || _a || SSTALE.AR ?};
VATZMDEK.index('ROK');
VATZMDEK.prefix(SIK.ROK1);
{? VATZMDEK.first() & VATZMDEK.find_le({? var_press('_b')>0 || _b || OKRO_F.NR ?})
|| SIK.ROZL_VAT:=VATZMDEK.ROZL_VAT
|| SIK.ROZL_VAT:=ROK_F.ROZL_VAT
?}


\zaok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: ustala czy zaokraglac czy obcinac grosze
::   WE: _a - typ deklaracji VAT7/POD
::       _b - data konca okresu
::  OLD: \zaok/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? 4+_a=-'VAT7'
|| _b>=date(2005,12,0)
|| 1
?}


\open
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Otwarcie maskowalnych tabel w okresie zadanym parametrami
::   WE: _a - kod roku
::       _b - numer okresu
::  OLD: \open/dol_dok.fml
::----------------------------------------------------------------------------------------------------------------------
_b:=form(_b,-2);
DOK.use('doku'+_a+_b);
DOKN.use('dokn'+_a+_b);
POZ.use('pozy'+_a+_b);
POW.use('pow_'+_a+_b);
VPOZ.use('pozv'+_a+_b);
VROZ.use('povr'+_a+_b);
FAK.use('fak_'+_a+_b);
POZF.use('pozf'+_a+_b);
P_V.use('p_v_'+_a+_b)


\zwr_rozl_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Funkcja zwraca rodzaj deklaracji dla okresu z zmiennej SIK.OKRROK2 w roku SIK.ROK2
::   WE: [_a] - rok
::       [_b] - okres
::  OLD: \zwr_rozl_okr/skid_rok.fml
::----------------------------------------------------------------------------------------------------------------------
_rozl:='';
{? var_pres('_a')>0 || _rok:=SIK.ROK2; SIK.ROK2:=_a ?};
{? var_pres('_b')>0 || _okr:=SIK.OKRROK2; SIK.OKRROK2:=_b ?};
VATZMDEK.cntx_psh();
VATZMDEK.index('ROK');
VATZMDEK.prefix(SIK.ROK2);
{? VATZMDEK.first() & VATZMDEK.find_le(SIK.OKRROK2().NR)
|| _rozl:=VATZMDEK.ROZL_VAT
|| _rozl:=SIK.ROK2().ROZL_VAT
?};
VATZMDEK.cntx_pop();
{? var_pres('_a')>0 || SIK.ROK2:=_rok ?};
{? var_pres('_b')>0 || SIK.OKRROK2:=_okr ?};
_rozl


\z_pop7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Przepisanie wartosci recznych z poprzedniej deklaracji
::  OLD: \z_pop7/nvat7.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_POZ.cntx_psh();
VAT_POZ.index('VAT_POZ');
VAT_POZ.prefix(VAT_DEK.ref());
{? VAT_POZ.first()
|| {!
   |? {? VAT_POZ.TYP='R'
      || {? VAT_POZ.POZ1<>0 || pv[VAT_POZ.POZ1]:=VAT_POZ.NETTO ?};
         {? VAT_POZ.POZ2<>0 || pv[VAT_POZ.POZ2]:=VAT_POZ.VAT ?}
      ?};
      VAT_POZ.next()
   !}
?};
VAT_POZ.cntx_pop()


\vat_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formula Usun dla okienka wert. tabeli VAT_DEK
::   WE: [_a] - bez pytania - gdy jest
::  OLD: \vat_usu/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('can_edit2','fks_ved')
|| 0
|? VAT_DEK.r_lock(1,1) & VAT_DEK.get()
|| {? (_=1 | FUN.ask('Usunąć bieżący wiersz?'@))
   || do();
      VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref());
      VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
      VAT_PS.index('VAT_PS');
      {? VAT_POZ.first()
      || {!
         |? VAT_PS.prefix(VAT_DEK.ref(),VAT_POZ.ref());
            {? VAT_PS.first() || {! |? VAT_PS.del() !} ?};
            VAT_POZ.del()
         !}
      ?};
      VAT_DEKT.index('VAT_DEK'); VAT_DEKT.prefix(VAT_DEK.ref());
      {? VAT_DEKT.first()
      || {! |? VAT_DEKT.del() !}
      ?};
      exec('del_dok_jpk','fks_ved');
      {? var_pres('VAT_DEKS')>0
      || VAT_DEKS.index('VAT_DEKS'); VAT_DEKS.prefix(VAT_DEK.ref());
         {? VAT_DEKS.first() || {! |? VAT_DEKS.del() !} ?}
      ?};
      VAT_DEK.del();
      end()
   || VAT_DEK.r_unlock()
   ?}
|| FUN.emsg('Deklarację przegląda inny operator.'@); 0
?}


\vat_uzas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB  [2011]
:: OPIS: Dodaje/usuwa rekordy do tabeli VAT_DEKT w zaleznosci od znaczników
::  OLD: \vat_uzas/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_vat:=1+VAT_DEK.TYP='V';
{? _vat
|| exec('vatuzas1','fks_ved','ZAL1','Uzasadnienie zwrotu podatku');
   exec('vatuzas1','fks_ved','ZAL2','Uzasadnienie przyspieszenia terminu zwrotu podatku')
?};
exec('vatuzas1','fks_ved','ZAL3','Uzasadnienie korekty deklaracji');
{? _vat
|| exec('vatuzas1','fks_ved','ZAL4','Uzas. zaliczenia nadpłaty w poczet przyszłych zobowiązań.')
?}


\vatuzas1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB  [2011]
:: OPIS: Dodaje/usuwa rekordy do tabeli VAT_DEKT w zaleznosci od znaczników
::   WE: _a - pole znacznika
::       _b - opis pola
::       [_c] - warunek na dodanie uzasadnienia
::  OLD: \vatuzas1/vat.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_DEKT.index('VAT_DEK'); VAT_DEKT.prefix(VAT_DEK.ref());
_jest:=VAT_DEKT.find_key(_a,_a);
{? var_pres('_c')<=0 & ($('VAT_DEK.'+_a))()=1 |
   var_pres('_c')>0 & (type_of(_c)=type_of('') & ($_c)() | type_of(_c)=type_of("") & _c())
|| {? ~_jest
   || VAT_DEKT.VAT_DEK:=VAT_DEK.ref();
      VAT_DEKT.KOD:=_a;
      VAT_DEKT.OPIS:=_b;
      VAT_DEKT.add()
   ?}
|? _jest
|| VAT_DEKT.del()
?}


\vat_dpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formula Popraw dla okienek wert. tabeli VAT_POZ
::  OLD: \vat_dpoz/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? 3+VAT_DEK.TYP='CIT'
|| exec('cit_poz','fks_ved')
|? 2+VAT_DEK.TYP='GV'
|| {? exec('bevatdek','fks_ved')
   || _okno:=exec('win_poz','jpk_gv');
      J_GV_POZ.win_sel(_okno);
      J_GV_POZ.index('VAT_DEK'); J_GV_POZ.prefix(REF.FIRMA,VAT_DEK.ref());
      J_GV_POZ.select();
      VAT_DEK.r_unlock()
   ?}
|? exec('bevatdek','fks_ved')
|| VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref());
   {? VAT_POZ.first() || VAT_POZ.VAT_DEK() ?};
   _okno:={? vat_typ='VAT7D' || 'VAT7' |? 2+vat_typ='V7' || 'V7' || vat_typ ?};
   _edit:={? vat_typ='VAT27' & VAT_DEK.TYP+3='KOR' || 'VAT27KOR' |? 2+vat_typ='V7' || 'VAT7' || _okno ?};
   {? _okno='POD'
   || _okno:=exec('win_pod_poz','fks_ved')
   ?};
   _grayed:='';
   _can:=exec('can_edit','fks_ved');
   {? ~_can
   || _grayed+='P'+{? 3+VAT_DEK.TYP='POD' || 'D' || '' ?}+{? VAT_DEK.TYP='PODK' || 'U' || '' ?}
   ?};
   {? VAT_DEK.PLIK=null
   || _grayed+='J(UZ)'
   ?};
   {? ~_can & 3+VAT_DEK.TYP='POD'
   || _grayed+=':D'
   ?};
   VAT_POZ.actions_grayed(_okno,_grayed);
   {? 2+vat_typ='V7' || _okno:=exec('win_poz','jpk_v') ?};
   VAT_POZ.win_edit(_edit); VAT_POZ.win_sel(_okno);
   VAT_PS.win_fml('WER',,'NIP',,'ICON_BEFORE',"exec('icona_svat','blp',VAT_PS.NIP,1)",2);
   HELP.fld_fml('S_VAT_D','DISPLAY_FORMAT',"exec('bd_s_vat','blp')");
   VAT_POZ.select(,,,{? _okno='POD' & VAT_DEK.TYP<>'PODK' || 'U' || '' ?});
   HELP.fld_fml('S_VAT_D','DISPLAY_FORMAT',"*");
   VAT_DEK.r_unlock()
?}


\bevatdek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMA [8.40]
:: OPIS: Formula przed redakcja naglowka deklaracji
::   WE: [_a] - czy komunikaty? [1]-tak 0-nie
::  OLD: \bevatdek/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
{? VAT_DEK.r_lock(1,1) & VAT_DEK.get()
|| 1
|? _a
|| FUN.info('Deklarację przegląda inny użytkownik.'@); 0
?}


\pr_okrr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Przed red. pola SIK.OKRROK2
::  OLD: \pr_okrr/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? SIK.ROK2
|| {? SIK.OKRROK2().ROK<>SIK.ROK2 || SIK.OKRROK2:=null ?};
   SIK.ROK2();
   {? cur_tab()=SIK & SIK.win_edit('?')='CIT8'
   || exec('unpar_p1','fks_ved','BV')
   ?};
   1
?}


\be_zal4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Przed redakcja VAT_DEK.ZAL4
::  OLD: \be_zal4/vat.fml
::----------------------------------------------------------------------------------------------------------------------
5+VAT_DEK.TYP='VAT7D' | 4+VAT_DEK.TYP='CIT8' & -menu_txt()<>'popraw'


\vat_kor_on
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Czy korekty deklaracji vat za zle dlugi obowiazuje dla danej deklaracji
::  OLD: \vat_kor_on/vat.fml
::----------------------------------------------------------------------------------------------------------------------
PAR_SKID.get(49)<>'B' & 4+VAT_DEK.OKRES>='2013'


\pw_v_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formula przed wysw. dla pola VAT_POZ.VAT
::  OLD: \pw_v_vat/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? 4+vat_typ='VAT7' | 2+vat_typ='V7'
|| {? VAT_POZ.TYP='S'
   || 'VAT_POZ#VAT#02'
   |? VAT_POZ.POZ2=0
   || 'VAT_POZ#VAT#01'
   || ''
   ?}
|? 4+VAT_DEK.TYP='CIT8' & VAT_DEK.NR().NR>=26
|| {? VAT_POZ.POZ2=0 | 'TD'*VAT_POZ.TYP
   || 'VAT_POZ#CIT#01'
   |? VAT_POZ.TYP='S'
   || 'VAT_POZ#CIT#02'
   || ''
   ?}
|| ''
?}


\pw_v_net
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formula przed wysw. dla pola VAT_POZ.NETTO
::  OLD: \pw_v_net/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? BPMN.SYM_DOM='FKS'
|| {? 4+vat_typ='VAT7' | 2+vat_typ='V7'
   || {? VAT_POZ.POZ1<>0  || '' || 'VAT_POZ#NETTO#01' ?}
   |? 4+SKID.TYP_ZAP='CIT8'
   || exec('vat_poz_enabled','fks_ved');
      {? VAT_DEK.NR().NR>=26
      || {? VAT_POZ.POZ1=0 | 'TD'*VAT_POZ.TYP
         || 'VAT_POZ#CIT#01'
         |? VAT_POZ.TYP='S'
         || 'VAT_POZ#CIT#02'
         || ''
         ?}
      || {? cur_afld()='POZ1'
         || ''
         |? 'TD'*VAT_POZ.TYP=0
         || ''
         || 'VAT_POZ#NETTO#01'
         ?}
      ?}
   ?}
|| ''
?}


\vat_poz_enabled
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia dostępnośc pól w oknie pozycji deklaracji
::----------------------------------------------------------------------------------------------------------------------
VAT_POZ.efld_opt(VAT_POZ.win_edit('?'),'enable='+$(VAT_POZ.TYP<>'D' & VAT_POZ.TYP<>'T'),VAT_POZ,'NETTO');
VAT_POZ.efld_opt(VAT_POZ.win_edit('?'),'enable='+$(VAT_POZ.TYP='D'),UNPAR,'P4');
VAT_POZ.efld_opt(VAT_POZ.win_edit('?'),'enable='+$(VAT_POZ.TYP='T'),VAT_POZ,'TEXT');
~~


\pr_vat_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formula Popraw przed dla okienek wert. tabeli VAT_POZ
::  OLD: \pr_vat_p/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('can_edit2','fks_ved')
|| 0
|? VAT_POZ.TYP='S'
|| FUN.info('Wiersz będący podsumowaniem.\nModyfikacje zabronione.'@); 0
|? VAT_POZ.TYP='A'
|| FUN.ask('Wiersz wyliczany automatycznie na podstawie\nrejestrów VAT.\n'
           'Zmiany wartości spowodują niezgodności\n z wydrukami rejestrów VAT\n'
           '\nKontynuować?'@)
|? VAT_POZ.TYP='C'
|| {? exec('vat_pod_edit','fks_ved')
   || VAT_POZ.put();
      0
   ?}
|| 1
?}


\vat_wys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Akcja 'Wyswietl' w okienkach wer. tabeli VAT_DEK
::  OLD: \vat_wys/vat.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ust_okn','fks_ved');
{? var_pres('TKRS',VAT_DEK)>0 & VAT_DEK.TKRS
|| exec('tkrs_eb','fks_viudo',1)
?};
VAT_DEK.display();
{? var_pres('TKRS',VAT_DEK)>0 & VAT_DEK.TKRS
|| exec('tkrs_eb','fks_viudo',2)
?}


\ust_okn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.50]
:: OPIS: Formula ustawia okienko red. dla tabeli VAT_DEK
::  OLD: \ust_okn/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? rodzdekl='C'
|| {? SKID.TYP_ZAP='CIT8'
   || _nr:=VAT_DEK.NR().NR;
      {? _nr>=32
      || VAT_DEK.win_edit('CIT8_8')
      |? _nr>=31
      || VAT_DEK.win_edit('CIT8_7')
      |? _nr>=29
      || VAT_DEK.win_edit('CIT8_6')
      |? _nr>=28
      || VAT_DEK.win_edit('CIT8_5')
      |? _nr>=27
      || VAT_DEK.win_edit('CIT8_4')
      |? _nr>=25
      || VAT_DEK.win_edit('CIT8_3')
      |? _nr>=22
      || VAT_DEK.win_edit('CIT8_2');
         exec('ust_fml','fks_ved',1)
      || VAT_DEK.win_edit('CIT8')
      ?}
   || VAT_DEK.win_edit('CIT')
   ?}
|? rodzdekl='V' | rodzdekl='D' |  -rodzdekl='j'
|| _nr_okr:={? 1+(VAT_DEK.OKRES+2)='/' || #(VAT_DEK.OKRES+1) || _nr_okr:=#(VAT_DEK.OKRES+2) ?};
   _data:=date(#(4+VAT_DEK.OKRES),_nr_okr,0);
   OKRO_F.cntx_psh(); OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
   _typ:={? OKRO_F.find_key(_data)
         || {? -rodzdekl='j'
            || {? OKRO_F.KON<date(2022,1,31)
               || 6
               || 7
               ?}
            || {? OKRO_F.KON<date(2005,9,30)
               || 1
               |? OKRO_F.KON<date(2018,7,31)
               || 2
               |? OKRO_F.KON<date(2019,1,1)
               || 3
               |? VAT_DEK.NR().NR<20
               || 4
               || 5
               ?}
            ?}
         ?};
   {? VAT_DEK.PROC=0 | _nr_okr=1
   || {? OKRO_F.NR=1
      || {? _typ=1
         || VAT_DEK.win_edit('VAT7_1')
         |? PAR_SKID.get(83)='T'
         || VAT_DEK.win_edit('VAT7_12P')
         || VAT_DEK.win_edit('VAT7_1'+$_typ)
         ?}
      || {? _typ=1 || VAT_DEK.win_edit('VAT7') || VAT_DEK.win_edit('VAT7'+$_typ) ?}
      ?}
   || {? _typ=1 || VAT_DEK.win_edit('VAT7') || VAT_DEK.win_edit('VAT7'+$_typ) ?}
   ?};
   OKRO_F.cntx_pop()
|? rodzdekl='Y'
|| VAT_DEK.win_edit('VIUDO')
|? rodzdekl='G'
|| VAT_DEK.win_edit('JPK_GV')
|| VAT_DEK.win_edit('POD')
?}


\wer_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.60]
:: OPIS: ustala numer wersji deklaracji przy poprawianiu pozycji i wydruku
::  OLD: \wer_pop/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_wer:=VAT_DEK.NR().NRF;
{? 3+VAT_DEK.TYP='V7K' || _wer+=6 ?};
_wer


\vat_zd_size
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Zwraca liczbe korekt deklaracji VAT zwiazana z podatkiem naleznym lub naliczonym
::   WE: [_a] - 'N' - korekta naleznego
::              'n' - zaplaty za nieuregulowane naleznosci
::              'Z' - korekta naliczony
::              'z' - zaplaty za nieuregulowane zobowiazania
::  OLD: \vat_zd_size/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:='N' ?};
VAT_PS.cntx_psh();
VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
VAT_PS.index('TYP'); VAT_PS.prefix(VAT_DEK.ref(),_a);
_size:=VAT_PS.size();
VAT_PS.cntx_pop();
_size


\drukuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wydruk deklaracji
::   WE: _a - nazwa pliku
::  OLD: \drukuj/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=files(_a+'.rpm');
{? _tab.find_key(_a+'.rpm',)
|| exec('rep_exec','#b_report','',_a,,,,,,,,'W')
|? 4+VAT_DEK.TYP='CIT8'
|| exec('rep_exec','#b_report','','fks_ps_cit',,,,,,,,'W')
|| echo('Brak pliku: %1.rpt'@[_a]);
   FUN.info('Nie znaleziono wydruku deklaracji.'@);
   echo()
?}


\edek_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2008]
:: OPIS: Przegladanie archiwum e-Deklaracji jednego typu
::   WE: _a - typ deklaracji
::  OLD: \edek_sel/skid_xml.fml
::----------------------------------------------------------------------------------------------------------------------
_typDekl:={? var_pres('_a')=type_of('') || _a || '' ?};
USERS.cntx_psh; ROK_F.cntx_psh();
_okres:={? 1+(VAT_DEK.OKRES+2)='/'
           || $(VAT_DEK.ROK().POCZ_ROK~1)+(VAT_DEK.OKRES+2)
           || $(VAT_DEK.ROK().POCZ_ROK~1)+(VAT_DEK.OKRES+3)
           ?};
{? _typDekl='VAT7'
|| {? _okres>'2006/09'
   || exec('edek_sel_vat7','fks_ved')
   || FUN.info('Deklaracja elektroniczna dostępna jest od okresu październik 2006.'@)
   ?}
|? _typDekl='CIT8'
|| {? _okres<'2015/01'
   || FUN.info('Deklaracja elektroniczna dostępna jest od okresu styczeń 2015.'@)
   || {? exec('bevatdek','fks_ved')
      || SKID.TYP_ZAP:='CIT8';
         exec('edek_sel2','fks_ved');
         VAT_DEK.r_unlock()
      ?}
   ?}
|? _typDekl='POD'
|| {? _okres<'2007/1'
   || FUN.info('Deklaracja elektroniczna dostępna jest od I kwartału 2007.'@)
   || {? exec('bevatdek','fks_ved')
      || SKID.TYP_ZAP:=VAT_DEK.TYP;
         exec('edek_sel2','fks_ved');
         VAT_DEK.r_unlock()
      ?}
   ?}
|? _typDekl='VAT27'
|| _mc:=+_okres=7;
   _okr:={? _mc || '2015/07' || '2015/3' ?};
   {? _okres<_okr
   || FUN.info('Deklaracja elektroniczna dostępna jest od 1 lipca 2015.'@)
   || {? exec('bevatdek','fks_ved')
      || _dek:={? VAT_DEK.TYP+4='_KOR' || VAT_DEK.TYP-4 || VAT_DEK.TYP ?};
         SKID.TYP_ZAP:=_dek+{? _mc || '' || 'K' ?};
         exec('edek_sel2','fks_ved');
         VAT_DEK.r_unlock()
      ?}
   ?}
|? _typDekl='VIUDO'
|| {? exec('bevatdek','fks_ved')
   || _dek:={? VAT_DEK.TYP+3='KOR' || VAT_DEK.TYP-3 || VAT_DEK.TYP ?};
      SKID.TYP_ZAP:=_dek;
      exec('edek_sel2','fks_ved');
      VAT_DEK.r_unlock()
   ?}
|? BPMN.SYM_DOM='PPL'
|| {? exec('bepitdek','edeklar')
   || {? SKID.DEKL_NAZ='IFT1' | SKID.DEKL_NAZ='IFT1R'
      || SKID.TYP_ZAP:=VAT_DEK.TYP;
         SKID.DEKL_NAZ:=VAT_DEK.TYP
      || SKID.TYP_ZAP:=SKID.DEKL_NAZ
      ?};
      E_DEK.index('DATA_ONE'); E_DEK.prefix(BPMN.SYM_DOM,SKID.TYP_ZAP,VAT_DEK.ref());
      E_DEK.win_sel('WER');
      E_DEK.hdr_sel;
      {? SKID.DEKL_NAZ='PIT11' | SKID.DEKL_NAZ='PIT40'
         | SKID.DEKL_NAZ='PIT8C' | SKID.DEKL_NAZ='IFT1' | SKID.DEKL_NAZ='IFT1R'
      || E_DEK.hdr_sel(': %1 za rok: %2'@[SKID.DEKL_NAZ,VAT_DEK.OKRES]+', %1 %2'@[VAT_DEK.NAZWISKO,VAT_DEK.PIERWSZE])
      || E_DEK.hdr_sel(': %1 za rok: %2'@[SKID.DEKL_NAZ,VAT_DEK.OKRES]+', wariant: %1'@[$VAT_DEK.WER])
      ?};
      E_DEK.select(,,,'R:R');
      VAT_DEK.r_unlock()
   ?}
?};
ROK_F.cntx_pop(); USERS.cntx_pop()


\edek_sel2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wyswietlenie okna z e-deklaracjami
::----------------------------------------------------------------------------------------------------------------------
E_DEK.index('DATA_ONE'); E_DEK.prefix(BPMN.SYM_DOM,SKID.TYP_ZAP,VAT_DEK.ref());
E_DEK.win_sel('WER');
E_DEK.hdr_sel(' %1 za okres %2, wersja %3'@[VAT_DEK.TYP,VAT_DEK.OKRES,$VAT_DEK.WER]);
_gray:={? 'NR'*VAT_DEK.STATUS>0 || 'DUP:D' || '' ?};
E_DEK.actions_grayed('WER',_gray);
E_DEK.select(,,,'R:R')


\vat_ps_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Aktualizacja wartosci podsumowania w oknie z pozycjami szczegolowymi deklaracji VAT
::  OLD: \vat_ps_upd/vat.fml
::----------------------------------------------------------------------------------------------------------------------
SUM.NETTO:=SUM.VAT:=0;
VAT.NETTO:=VAT.SVAT:=VAT.BRUTTO:=0;
VAT_PS.cntx_psh();
VAT_PS.index('VAT_PS');
VAT_PS.prefix(VAT_DEK.ref(),VAT_POZ.ref());
{? VAT_PS.first()
|| {!
   |? {? var_pres('POMIN',VAT_PS)<=0 | VAT_PS.POMIN=0
      || VAT.NETTO+=VAT_PS.NETTO;
         VAT.SVAT+=VAT_PS.VAT;
         VAT.BRUTTO+=VAT_PS.BRUTTO
      ?};
      VAT_PS.next()
   !}
?};
VAT_PS.cntx_pop();
SUM.NETTO:=VAT.NETTO;
SUM.VAT:=VAT.SVAT;
~~


\get_ord_zu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Pobiera linie uzasadnienia korekty
:: WE: _a (opcjonaly) długość linii
::  OLD: \get_ord_zu/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<0 || _a:=115 ?};
_wyn:=obj_new(2);
_wyn[1]:='';
_wyn[2]:=0;
_dl:=_a;
{? var_pres('ORD_ZU')>0
|| _napis:=ORD_ZU;
   _last:=-1;
   _nr:=0;
   {? _napis<>''
   || {!
      |? _nr+=1;
         _zn:=1+_napis;
         {? _zn=''
         || _last:=+ORD_ZU;
            _nr:=_dl+1
         |? _zn=' '
         || _last:=_nr
         |? _zn='\n'
         || _last:=_nr;
            _nr:=_dl+1
         ?};
         {? _nr>_dl
         || {? _last=-1 || _last:=_dl ?};
            _napis:=_last+ORD_ZU; 0
         || _napis:=1-_napis;
            1
         ?}
      !};
      ORD_ZU:=_last-ORD_ZU;
      _wyn[1]:=_napis;
      _wyn[2]:=_napis<>'' | ORD_ZU<>''
   || _wyn[2]:=0
   ?}
?};
_wyn


\data_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: Ustalenie daty konca okresu za jaki jest tworzona deklaracja
::  OLD: \data_dek/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_kw:=1+(VAT_DEK.OKRES+2)='/';
{? _kw
|| _okr:=#(VAT_DEK.OKRES+1)-1;
    _d2:=date(#(4+VAT_DEK.OKRES),1+_okr*3+2,0)
|| _okr:=#(VAT_DEK.OKRES+2);
   _d2:=date(#(4+VAT_DEK.OKRES),_okr,0)
?};
_d2


\vat_kor_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Zwraca okno redagowania VAT_PS
::  OLD: \vat_kor_edit/vat.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__VPSedi');
{? var_pres('__VPSedi')<=0
|| __VPSedi:=VAT_PS.mk_edit('Korekta'@);
   VAT_PS.win_efld(__VPSedi,UNPAR,'P10',,,60,,,'Kontrahent'@);
   VAT_PS.win_efld(__VPSedi,UNPAR,'P11',,,20,,,'NIP'@);
   VAT_PS.win_efld(__VPSedi,,'ADRES',,,60,,,'Siedziba'@);
   VAT_PS.win_efld(__VPSedi,,'SYM',,,,,,'Symbol faktury'@);
   VAT_PS.win_efld(__VPSedi,,'SYM_ZEW',,,,,,'Symbol zewnętrzny'@);
   VAT_PS.fld_fml('SYM_ZEW','AFTER_EDIT',"{? VAT_PS.SYM='' || VAT_PS.SYM:=20+VAT_PS.SYM_ZEW || '' ?}");
   VAT_PS.win_efld(__VPSedi,,'DATA',,,,,,'Data wystawienia'@);
   VAT_PS.win_efld(__VPSedi,,'DATA2',,,,,,'Data operacji'@);
   VAT_PS.win_efld(__VPSedi,,'TP',,,,,,'Termin płatności'@);
   {? var_press('DATA_ZAP',VAT_PS)>0
   || VAT_PS.win_efld(__VPSedi,,'DATA_ZAP',,,,,,'Termin płatności/Data zapłaty')
   ?};
   VAT_PS.win_efld(__VPSedi,,'ZWL',,,,,1,'Zwłoka w płatności'@);
   VAT_PS.win_efld(__VPSedi,,'NETTO',,,16,2,,'Korekta Netto'@);
   VAT_PS.win_efld(__VPSedi,,'VAT',,,16,2,,'Korekta VAT'@);
   UNPAR.P10_BV:=UNPAR.P10_BE:=UNPAR.P10_AE:='';
   UNPAR.P10_F3:='exec(\'f3_vat_kor_kh\',\'!fks_ved_dv7d\',0)';
   UNPAR.P11_BV:=UNPAR.P11_BE:=UNPAR.P11_AE:='';
   UNPAR.P11_F3:='exec(\'f3_vat_kor_kh\',\'!fks_ved_dv7d\',1)';
   exec('mark2','#field',VAT_PS,__VPSedi,
      UNPAR,'P10',
      UNPAR,'P11',
      VAT_PS,'ADRES',
      VAT_PS,'SYM',
      VAT_PS,'SYM_ZEW',
      VAT_PS,'DATA',
      VAT_PS,'DATA2',
      VAT_PS,'TP',
      VAT_PS,'NETTO'
   )
?};
__VPSedi


\be_vat_poz_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB  [2011]
:: OPIS: Przed redakcja pola VAT_POZ.OP
::  OLD: \be_vat_poz_op/vat.fml
::----------------------------------------------------------------------------------------------------------------------
BPMN.SYM_DOM<>'FKS' | VAT_POZ.VAT_DEK().TYP='PODK'


\okno_rok2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ  [12.41]
:: OPIS: Utworzenie okienka dla wyboru roku z dodatkowym rodzajem deklaracji
::  OLD: \okno_rok2/skid_sik.fml
::----------------------------------------------------------------------------------------------------------------------
OknROKE2:=SIK.mk_edit('Wybierz rok bilansowy'@,0,'sik_red');
SIK.win_efld(OknROKE2,,'ROK1','NAZ','NAZWA',20,,,'Rok bilansowy'@,,'Nazwa roku bilansowego'@,1);
{? 'PCYG'*rodzdekl=0
|| SIK.win_efld(OknROKE2,,'ROZL_VAT',,,,,,'Rodzaj deklaracji'@,,,'radio-buttons',,
      'Miesięczne'@,"'M'",
      'Kwartalne'@,"'K'")
?};
_ok:=SIK.win_ebtn(OknROKE2,'text=%1,panel=bottom,align=end,display=1'['&OK'@],"'key:F2'");
_an:=SIK.win_ebtn(OknROKE2,'text=%1,panel=bottom,align=end,display=1'['&Anuluj'@],"'key:Esc'");
SIK.btn_eopt(OknROKE2,_ok,'tooltip='+exec('help_red_ok','#window','P'));
SIK.btn_eopt(OknROKE2,_an,'tooltip='+exec('help_red_esc','#window','A'));
SIK.efld_opt(OknROKE2,'mark='+$(rodzdekl<>'P'),,'ROK1',);
SIK.win_edit(OknROKE2);
ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA)


\ust_fml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Ustawia formuly dla pol VAT_DEK
::   WE: _a - 1-ustawienie 0-przywrocenie
::  OLD: \ust_fml/cit.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| VAT_DEK.fld_fml('PAR2','BEFORE_EDIT',"-menu_txt()<>'popraw'")
|| VAT_DEK.fld_fml('PAR2','BEFORE_EDIT',"*")
?}


\task_attach
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia uprawnienia do zakładek
::   WE: _a - typ czynności
::----------------------------------------------------------------------------------------------------------------------
task_attach('FKS_VED_ZV'+_a+'P');
task_attach('FKS_VED_DV'+_a+'D');
task_attach('FKS_VED_DV'+_a+'E');
{? 'JVG'*_a=0
|| task_attach('FKS_VED_DV'+_a+'W')
?}


\cit_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2008]
:: OPIS: Prezentuje pozycje deklaracji CIT
::  OLD: \cit_poz/cit.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_DEK.r_lock(1,1) & VAT_DEK.get()
|| VAT_POZ.cntx_psh();
   VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref());
   {? 4+VAT_DEK.TYP='CIT8' & VAT_DEK.NR().NR>=26
   || _sel:=exec('cit82kwin','fks_ved','sel');
      _red:=exec('cit82kwin','fks_ved','red')
   || _sel:=_red:='CIT'
   ?};
   VAT_POZ.win_sel(_sel); VAT_POZ.win_edit(_red); VAT_POZ.win_patt('SZUK');
   _grayed:='';
   {? ~exec('can_edit','fks_ved')
   || _grayed+='PU'
   ?};
   VAT_POZ.actions_grayed('CIT',_grayed);
   VAT_POZ.select();
   VAT_POZ.cntx_pop();
   VAT_DEK.r_unlock()
|| FUN.info('Deklarację przegląda inny operator.'@)
?}


\cit8_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Data od i do e-deklaracji CIT-8
::   WE: _a - 1-data od, 2-data do
::  OLD: \cit8_data/cit.fml
::----------------------------------------------------------------------------------------------------------------------
_data:='';
ROK_F.cntx_psh();
OKRO_F.cntx_psh();
_rok:=#(4+VAT_DEK.OKRES);
_mc:=#(VAT_DEK.OKRES+2);
ROK_F.index('NAZWA');
ROK_F.prefix(REF.FIRMA);
{? ROK_F.seek(VAT_DEK.ROK)
|| _dd:=date(0,0,0);
   {? _a=1
   || _dd:=ROK_F.POCZ_ROK
   || _dd:=date(_rok,_mc,0)
   ?};
   {? _dd<>date(0,0,0) || _data:=STR.gsub(_dd$1,'/','-') ?}
?};
OKRO_F.cntx_pop();
ROK_F.cntx_pop();
_data


\poz2tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przepisanie pozycji deklaracji VAT do tablicy
::   WE:  _a  - prefiks dla pozycji VAT
::       [_b] - czy wszystkie wartosci? 1-tak [0]-nie
::   WY:
::  OLD: \poz2tab/cit.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<=0 || _b:=0 ?};
_nr:=VAT_DEK.NR().NR;
VAT_POZ.cntx_psh();
VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),_a);
{? VAT_POZ.last()
|| _size:={? VAT_POZ.POZ1>VAT_POZ.POZ2 || VAT_POZ.POZ1 || VAT_POZ.POZ2 ?};
   _pv:=obj_new(_size);
   {! _i:=1.._size
   |! _pv[_i]:=~~
   !};
   {? VAT_POZ.first()
   || {!
      |? {? VAT_POZ.TYP='T' | VAT_POZ.TYP='D'
         || {? _b
            || {? VAT_POZ.POZ1 || _pv[VAT_POZ.POZ1]:=VAT_POZ.TEXT ?};
               {? VAT_POZ.POZ2 || _pv[VAT_POZ.POZ2]:=VAT_POZ.TEXT ?}
            ?}
         || VAT_POZ.cntx_psh();
            _lp:=VAT_POZ.LP;
            _tryb:=VAT_POZ.next() & VAT_POZ.LP-1=_lp & #(VAT_POZ.LP+1)=0;
            VAT_POZ.cntx_pop();
            {? _tryb=0
            || {? #(VAT_POZ.LP+1)=0 & VAT_POZ.LP+1<>'0' & _nr>=32
               || _ii:={? VAT_POZ.POZ1 || VAT_POZ.POZ1 || VAT_POZ.POZ2 ?};
                  {? VAT_POZ.POZ1 || _pv[_ii].NA:=VAT_POZ.NETTO$2 ?};
                  {? VAT_POZ.POZ2 || _pv[_ii].VA:=VAT_POZ.VAT$2 ?}
               || {? VAT_POZ.POZ1 & _pv[VAT_POZ.POZ1]=~~ || _pv[VAT_POZ.POZ1]:=VAT_POZ.NETTO$2 ?};
                  {? VAT_POZ.POZ2 & _pv[VAT_POZ.POZ2]=~~ || _pv[VAT_POZ.POZ2]:=VAT_POZ.VAT$2 ?}
               ?}
            || {? _nr>=32
               || _ii:={? VAT_POZ.POZ1 || VAT_POZ.POZ1 || VAT_POZ.POZ2 ?};
                  _pv[_ii]:=obj_new('N','V','NA','VA');
                  {? VAT_POZ.POZ1 || _pv[_ii].N:=VAT_POZ.NETTO$2 ?};
                  {? VAT_POZ.POZ2 || _pv[_ii].V:=VAT_POZ.VAT$2 ?}
               ?}
            ?}
         ?};
         VAT_POZ.next()
      !}
   ?}
?};
VAT_POZ.cntx_pop();
{? var_pres('_pv')<=0 || ~~ || _pv ?}


\vd_uzas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB  [2011]
:: OPIS: Wyswietla teksty uzasadnien uzytych w zalacznikach deklaracji
::  OLD: \vd_uzas/vat.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('wer');
VAT_DEKT.index('VAT_DEK'); VAT_DEKT.prefix(VAT_DEK.ref());
{? VAT_DEKT.first()
|| wer:=VAT_DEKT.mk_sel('Teksty deklaracji/uzasadnienia'@,'P',,'vatdektsel',,,,,'U','T');
   VAT_DEKT.win_sopt(wer,'select_record_checkbox=0');
   VAT_DEKT.win_fld(wer,,'OPIS');
   VAT_DEKT.win_fld(wer,,'MEMO',,,60);
   VAT_DEKT.win_act(wer,,'Formuła','Popraw'@@,,,"exec('bmvduzas','fks_ved')","exec('amvduzas','fks_ved')",1,,,,'P');
   VAT_DEKT.win_act(wer,,'Formuła','Wydruk',,,
                    "exec('rep_exec','#b_report','','fks_vat_zz',,,,,,,,'W')","");
   VAT_DEKT.win_act(wer,,'Wyświetl',,,,"VAT_DEKT.memo_get(,'MEMO'); VAT_DEKT.memo_view(,'MEMO')");
   VAT_DEKT.win_act(wer,,'Rekord',,,,"exec('actions','fks_ved',wer)","");
   VAT_DEKT.win_sel(wer);
   {? VAT_DEK.STATUS<>'N' & VAT_DEK.STATUS<>''
   || VAT_DEKT.actions_grayed(wer,'P')
   ?};
   VAT_DEKT.select()
|| FUN.info('Brak załączników deklaracji z uzasadnieniami.'@)
?}


\bmvduzas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB  [2011]
:: OPIS: Przed akcja popraw tabeli VAT_DEKT
::  OLD: \bmvduzas/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('is_edek','xml',{? VAT_DEK.TYP+4='_KOR' || VAT_DEK.TYP-4 || VAT_DEK.TYP ?})
|| FUN.info('Istnieją e-Deklaracje.\nModyfikowanie zabronione.'@); 0
|| 1
?}


\amvduzas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Poprawianie uzasadnien
::  OLD: \amvduzas/vat.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_DEKT.memo_get(,'MEMO');
{!
|? {? VAT_DEKT.memo_edi()
   || VAT_DEKT.memo_put(,'MEMO');
      {? VAT_DEKT.KOD='ZAL3'
      || ORD_ZU:=VAT_DEKT.memo_txt();
         _lz:=+ORD_ZU;
         _v1:=_lz>3500;
         _licz:=-1;
         {!
         |? _licz+=1;
            _wyn:=exec('get_ord_zu','fks_ved');
            _r:=_wyn[2];
            &_wyn;
            _r
         !};
         _v2:=_licz>29;
         {? _v1 | _v2
         || _txt:='';
            {? _v1
            || _txt+=
               'Liczba znaków uzasadnienia korekty: '@+$(_lz)+
               '.\nW e-deklaracji zostnie wysłanych 3500 znaków.\n'@
            ?};
            {? _v2
            || _txt+={? _v1 || '\n' || '' ?}+
               'Liczba linii uzasadnienia korekty po\nsformatowaniu do obszaru wydruku: '@+$_licz+'.\n'+
               'Zostanie wydrukowanych tylko 29 pierwszych linii.\n'@
            ?};
            _txt+='\nCzy wrócić do redakcji?'@;
            _wyn:=FUN.ask(_txt)
         || _wyn:=0
         ?};
         VAR_DEL.delete('ORD_ZU');
         _wyn
      ?}
   ?}
!}


\bv_vat_isedek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed wyświetleniem VAT7.ISEDEK
::----------------------------------------------------------------------------------------------------------------------
VAT7.ISEDEK:={? exec('is_edek','xml',VAT_DEK.TYP)
 || 'T' || 'N' ?};
~~


\br_vat_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed okna tabeli VAT_DEK
::   WE: _a - akronim okna
::----------------------------------------------------------------------------------------------------------------------
_grayed:='';
{? ~exec('can_edit','fks_ved')
|| _grayed+='PU'
?};
_grayed+=exec('grayAkc','fks_ved');
VAT_DEK.actions_grayed(_a,_grayed);
''


\vzd_first
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Formula dla elementu P_B schematu e-deklaracji VAT_ZD
::   WE: _a - 1-start 0-stop
::  OLD: \vzd_first/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| VAT_PS.cntx_psh();
   VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
   VAT_PS.index('TYP');
   VAT_PS.prefix(VAT_DEK.ref(),'N');
   VAT_PS.first()
|| VAT_PS.cntx_pop()
?}


\vzd_next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Formula dla elementu P_B schematu e-deklaracji VAT-7, zalacznik VAT_ZD
::  OLD: \vzd_next/vat.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_PS.next()


\vat_zd_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.10]
:: OPIS: Formula dla elementu P_10 i P_11 schematu e-deklaracji VAT-7, zalacznik VAT_ZD
::   WE: [_a] - 1- suma NETTO 2-suma VAT wpp NETTO i VAT w tablicy
::  OLD: \vat_zd_sum/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_suma:=obj_new('N','V');
_suma.N:=_suma.V:=0;
VAT_PS.cntx_psh();
VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
VAT_PS.index('TYP');
VAT_PS.prefix(VAT_DEK.ref(),'N');
{? VAT_PS.first()
|| {!
   |? _suma.N+=-VAT_PS.NETTO;
      _suma.V+=-VAT_PS.VAT;
      VAT_PS.next()
   !}
?};
VAT_PS.cntx_pop();
_suma.N:=_suma.N$0;
_suma.V:=_suma.V$0;
{? var_pres('_a')<=0
|| _suma
|? _a=1
|| _suma.N
|| _suma.V
?}


\set_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przygotowuje i ustawia okno deklaracji
::   WE: _a - formuła sprawdzajaca pola
::       _b - formuła dodajaca rekord
::----------------------------------------------------------------------------------------------------------------------
VatSpr:=_a;
VatAdd:=_b;
_win:=VAT_DEK.win_edit('?');
VAT_DEK.efld_opt(_win,'mark='+$(2+VAT_DEK.TYP='V7' | 2+VAT_DEK.TYP='GV'),VAT_DEK,'EMAIL');
_okn:=VAT_DEK.mk_edit('Dane deklaracji'@);
VAT_DEK.win_ewin(_okn,,_win);
VAT_DEK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin'['&Pozycje'@],"
   {? Add=0
   || _spr:=VatSpr();
      {? _spr<>''
      || 'edit:'+_spr
      || ref:=VatAdd();
         {? ref
         || Add:=1;
            exec('vat_dpoz','fks_ved')
         ?};
         ''
      ?}
   || _spr:=VatSpr();
      {? _spr<>''
      || 'edit:'+_spr
      || VAT_DEK.put();
         exec('vat_uzas','fks_ved');
         exec('vat_dpoz','fks_ved')
      ?};
      ''
   ?}
");
{? VAT_DEK.TYP<>'VIUDO'
|| VAT_DEK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin'['&Uzasadnienia'@],"
      {? Add=0
      || FUN.info('Brak pozycji deklaracji.'@)
      || _spr:=VatSpr();
         {? _spr<>''
         || 'edit:'+_spr
         || VAT_DEK.put();
            exec('vat_uzas','fks_ved');
            exec('vd_uzas','fks_ved')
         ?}
      ?};
      ''
   ")
?};
_zak:=VAT_DEK.win_ebtn(_okn,
      'text=%1,btn_label_align=center,panel=bottom,align=end'[exec('text_red_zakoncz','#window','V')],"
      {? Add=0
      || FUN.info('Nie można zakończyć tworzenia deklaracji.\nBrak pozycji deklaracji.'@); ''
      |? (_spr:=VatSpr())<>''
      || 'edit:'+_spr
      || VAT_DEK.put();
         exec('vat_uzas','fks_ved');
         {? exec('vat_uzas_ok','fks_ved')
         || VAT_DEK.STATUS:='R';
            VAT_DEK.put();
            'key:Esc'
         || FUN.info('Nie uzupełniono uzasadnień.'@);
            ''
         ?}
      ?}
   ");
_ok:=VAT_DEK.win_ebtn(_okn,
     'text=%1, btn_label_align=center, panel=bottom, align=end'[exec('text_red_ok','#window')],"'key:F2'");
_an:=VAT_DEK.win_ebtn(_okn,
     'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
VAT_DEK.btn_eopt(_okn,_zak,'tooltip='+exec('help_red_zakoncz','#window','V'));
VAT_DEK.btn_eopt(_okn,_ok,'tooltip='+exec('help_red_ok','#window','Z'));
VAT_DEK.btn_eopt(_okn,_an,'tooltip='+exec('help_red_esc','#window','A'));
VAT_DEK.win_edit(_okn)


\zakoncz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zakonczenie rejestracji deklaracji
::   WE: _a - typ deklaracji
::----------------------------------------------------------------------------------------------------------------------
{? VAT_DEK.STATUS='A'
|| FUN.info('Deklaracja jest zaakceptowana.'@)
|? VAT_DEK.STATUS='R'
|| FUN.info('Rejestracja jest już zakończona.'@)
|? ~exec('vat_uzas_ok','fks_ved')
|| FUN.info('Nie uzupełniono uzasadnień.'@)
|? FUN.ask('Zakończyć rejestrację deklaracji?'@)
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='FKS_VED_DV'+_a+'D';
   _params.UIDREF:=VAT_DEK.uidref();
   _params.AKCJA:='Zakończ';
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params)
?}


\wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akcja wycofaj rejestrację deklarcji
::----------------------------------------------------------------------------------------------------------------------
{? VAT_DEK.STATUS='A'
|| FUN.info('Deklaracja jest zaakceptowana.'@)
|? VAT_DEK.STATUS='N'
|| FUN.info('Rejestracja nie została zakończona.'@)
|? FUN.ask('Wycofać rejestrację deklaracji?') & exec('bevatdek','fks_ved')
|| VAT_DEK.STATUS:='N';
   VAT_DEK.put();
   VAT_DEK.r_unlock()
?}


\can_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Czy mozna redagowac deklaracje
::----------------------------------------------------------------------------------------------------------------------
(VAT_DEK.STATUS='' | VAT_DEK.STATUS='N') & ~exec('is_edek','xml',VAT_DEK.TYP)


\set_win_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przygotowuje i ustawia okno deklaracji
::----------------------------------------------------------------------------------------------------------------------
_okn:=VAT_DEK.mk_edit('Dane deklaracji'@);
VAT_DEK.win_ewin(_okn,,VAT_DEK.win_edit('?'));
VAT_DEK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],"
   exec('vat_dpoz','fks_ved');
   ''
");
VAT_DEK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin'['&Uzasadnienia'@],"
   exec('vd_uzas','fks_ved');
   ''
");
VAT_DEK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Akceptuj'@],"
   AkcOk:=1; 'key:Esc'
");
_an:=VAT_DEK.win_ebtn(_okn,'text=%1, btn_label_align=center, panel=bottom, align=end,display=1'['&Anuluj'@],'key:Esc');
VAT_DEK.btn_eopt(_okn,_an,'tooltip='+exec('help_red_esc','#window','A'));
VAT_DEK.win_edit(_okn)


\akceptacja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akceptacja deklaracji
::   WE: _a - typ deklaracji
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_auto:=_mp.isAutoRun();
BPMN.SYM_DOM:='FKS';
_jest:=0;
VAT_DEK.cntx_psh();
{? var_press('DEKLARACJA',_we)>0
|| VAT_DEK.index('UNIK');
   VAT_DEK.prefix();
   {? VAT_DEK.seek(_we.DEKLARACJA)
   || _jest:=1;
      exec('set_var','fks_ved');
      _wy.DEKLARACJA:=VAT_DEK.ref();
      _mp.save(,_wy)
   ?}
|| _jest:=-1
?};
{? _jest
|| {? (+_a)+VAT_DEK.TYP<>_a
   || _err:='Niewłaściwy typ deklaracji %1.\nPrzekazano deklarację %2.'@[_a,VAT_DEK.TYP];
      {? ~_auto
      || FUN.info(_err)
      ?};
      _mp.error(_err)
   |? VAT_DEK.STATUS='N'
   || {? ~_auto
      || FUN.info('Nie zakończono rejestracji deklaracji.'@)
      ?}
   |? VAT_DEK.STATUS<>'R'
   || {? ~_auto
      || FUN.info('Deklaracja jest już zaakceptowana.'@)
      ?};
      _mp.done()
   || {? ~_auto
      || {? _mp.pathTodo()
         || exec('ust_okn','fks_ved');
            exec('set_win_akc','fks_ved');
            AkcOk:=0;
            {? var_pres('TKRS',VAT_DEK)>0 & VAT_DEK.TKRS
            || exec('tkrs_eb','fks_viudo',1)
            ?};
            VAT_DEK.display();
            {? var_pres('TKRS',VAT_DEK)>0 & VAT_DEK.TKRS
            || exec('tkrs_eb','fks_viudo',2)
            ?};
            _ok:=AkcOk;
            VAR_DEL.delete('AkcOK')
         || _ok:=FUN.ask('Czy zaakceptować deklarację?')
         ?}
      || _ok:=1
      ?};
      {? _ok & exec('bevatdek','fks_ved',~_auto)
      || VAT_DEK.STATUS:='A';
         VAT_DEK.put();
         VAT_DEK.r_unlock();
         _mp.done()
      ?}
   ?}
|? _jest=0
|| {? ~_auto
   || FUN.info('Nie znaleziono deklaracji.'@)
   ?};
   _wy.DEKLARACJA:=null;
   _mp.save(,_wy);
   _mp.done()
|? _jest=-1
|| _err:='Brak parametru DEKLARACJA.'@;
   {? ~_auto
   || FUN.info(_err)
   ?};
   _mp.error(_err)
?};
VAT_DEK.cntx_pop()


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akcja akceptacji deklaracji
::   WE: _a - typ deklaracji
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_VED_DV'+_a+'E';
_params.UIDREF:=VAT_DEK.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DEKLARACJA',VAT_DEK.ref());
_params.PROC_START:='N';
exec('mp_run','#b__box',_params)


\wyc_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akcja wycofania akceptacji deklaracji
::   WE: _a - typ
::----------------------------------------------------------------------------------------------------------------------
{? VAT_DEK.STATUS='R'
|| FUN.info('Deklaracja nie jest zaakceptowana.'@)
|? VAT_DEK.STATUS='N'
|| FUN.info('Nie zakończono rejestracji deklaracji.'@)
|? 2+VAT_DEK.TYP<>'V7' & exec('is_edek','xml',{? VAT_DEK.TYP+4='_KOR' || VAT_DEK.TYP-4 || VAT_DEK.TYP ?})
|| FUN.info('Istnieją e-deklaracje.'@)
|? (2+VAT_DEK.TYP='V7' | 2+VAT_DEK.TYP='GV') & exec('jpk4vat','jpk')
|| FUN.info('Istnieją pliki JPK.'@)
|? FUN.ask('Anulować akceptację deklaracji?'@) & exec('bevatdek','fks_ved')
|| VAT_DEK.STATUS:='R';
   {? exec('hasAction','users','FKS_VED_DV'+_a+'D')
   || VAT_DEK.STATUS:='N'
   ?};
   VAT_DEK.put();
   VAT_DEK.r_unlock()
?}


\set_var
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia zmienne na podstawie deklaracji
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
_okres:={? 1+(VAT_DEK.OKRES+2)='/'
           || $(VAT_DEK.ROK().POCZ_ROK~1)+(VAT_DEK.OKRES+2)
           || $(VAT_DEK.ROK().POCZ_ROK~1)+(VAT_DEK.OKRES+3)
           ?};
_rok:=#(4+_okres);
_pocz:=date(_rok,1,1);
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.find_key(_pocz)
|| _typ:={? 1+(_okres+2)='/' || 2 || 1 ?};
   {? _typ=1
   || _nr:=#(_okres+2)
   || _kw:=#(_okres+1);
      _nr:=(_kw-1)*3+1
   ?};
   OKRO_F.cntx_psh();
   OKRO_F.index('ROK');
   OKRO_F.prefix(ROK_F.ref());
   {? OKRO_F.find_key(_nr)
   || exec('init','fks_ved',ROK_F.ref(),OKRO_F.ref());
      {? 2+VAT_DEK.TYP='V7'
      || rodzdekl:={? SIK.ROZL_VAT='K' || 'J' || 'j' ?};
         vat_typ:={? SIK.ROZL_VAT='K' || 'V7K' || 'V7M' ?}
      |? 4+VAT_DEK.TYP='VAT7'
      || rodzdekl:={? SIK.ROZL_VAT='K' || 'D' || 'V' ?};
         vat_typ:={? SIK.ROZL_VAT='K' || 'VAT7D' || 'VAT7' ?}
      |? 5+VAT_DEK.TYP='VAT27'
      || rodzdekl:={? SIK.ROZL_VAT='K' || 'I' || 'i' ?};
         vat_typ:='VAT27'
      |? 3+VAT_DEK.TYP='POD'
      || rodzdekl:='P';
         vat_typ:='POD'
      |? 4+VAT_DEK.TYP='CIT8'
      || rodzdekl:='C';
         vat_typ:=SKID.TYP_ZAP:=SKID.DEKL_NAZ:='CIT8'
      |? 5+VAT_DEK.TYP='VIUDO'
      || rodzdekl:='Y';
         vat_typ:=SKID.TYP_ZAP:=SKID.DEKL_NAZ:='VIUDO'
      |? 2+VAT_DEK.TYP='GV'
      || rodzdekl:='G';
         vat_typ:=SKID.TYP_ZAP:=SKID.DEKL_NAZ:='GV'
      ?}
   ?};
   OKRO_F.cntx_pop()
?};
ROK_F.cntx_pop()


\grayAkc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zwraca akcje do wyszarzenie w zaleznosci od statusu
::----------------------------------------------------------------------------------------------------------------------
_g:='';
{? VAT_DEK.STATUS='N' | VAT_DEK.STATUS=''
|| _g+='TNW'
|? VAT_DEK.STATUS='R'
|| _g+='ON'
|? VAT_DEK.STATUS='A'
|| _g+='OWT';
   {? exec('is_edek','xml',VAT_DEK.TYP)
   || _g+='N'
   ?}
|? VAT_DEK.STATUS='W' | VAT_DEK.STATUS='T'
|| _g+='OWTN'
?};
_g


\vat_dek_trig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Triger przed add i put dla VAT_DEK
::----------------------------------------------------------------------------------------------------------------------
{? VAT_DEK.STATUS='' | VAT_DEK.STATUS='N'
|| VAT_DEK.STATOPIS:='Rejestracja'
|? VAT_DEK.STATUS='R'
|| VAT_DEK.STATOPIS:='Zarejestrowana'
|? VAT_DEK.STATUS='A'
|| VAT_DEK.STATOPIS:='Zaakceptowana'
|? VAT_DEK.STATUS='W'
|| VAT_DEK.STATOPIS:='Wysłana'
|? VAT_DEK.STATUS='T'
|| VAT_DEK.STATOPIS:='Przyjęta'
?};
1


\edek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Tworzenie i wysyłanie e-deklaracji
::   WE: _a - obszar
::       _b - typ deklaracji
::       _c - id czynności
::----------------------------------------------------------------------------------------------------------------------
VDTyp:=_b;
_args:=params_get();
_we:=_args.in;
_wy:=_args.out;
_mp:=_args.mp;
_akcja:=_mp.akcja();
BPMN.SYM_DOM:=_a;
_jest:=0;
VAT_DEK.cntx_psh();
{? var_press('DEKLARACJA',_we)>0
|| VAT_DEK.index('UNIK');
   VAT_DEK.prefix();
   {? VAT_DEK.seek(_we.DEKLARACJA)
   || _jest:=1;
      exec('set_var','fks_ved');
      _wy.DEKLARACJA:=VAT_DEK.ref();
      _mp.save(,_wy)
   ?}
|| _jest:=-1
?};
{? _jest
|| {? (+_b)+VAT_DEK.TYP<>_b
   || _err:='Niewłaściwy typ deklaracji \'%1\' (%2)'@[_b,VAT_DEK.TYP];
      FUN.info(_err);
      _mp.error(_err)
   |? VAT_DEK.STATUS='T'
   || FUN.info('Deklaracja jest przyjęta przez urząd.'@);
      _mp.done()
   |? 'NR'*VAT_DEK.STATUS>0
   || FUN.info('Status deklaracji: %1.\nNie można utworzyć e-deklaracji.'@[-VAT_DEK.STATOPIS]);
      _wy.DEKLARACJA:=null;
     _mp.save(,_wy);
     _mp.done()
   |? _akcja='Zakończ'
   || _mp.done()
   |? _mp.pathTodo()
   || exec('ust_okn','fks_ved');
      exec('set_win_send','fks_ved');
      {? var_pres('TKRS',VAT_DEK)>0 & VAT_DEK.TKRS
      || exec('tkrs_eb','fks_viudo',1)
      ?};
      VAT_DEK.display();
      {? var_pres('TKRS',VAT_DEK)>0 & VAT_DEK.TKRS
      || exec('tkrs_eb','fks_viudo',2)
      ?}
   || exec('edek_sel','fks_ved',_b)
   ?}
|? _jest=0
|| FUN.info('Nie znaleziono deklaracji.'@);
   _wy.DEKLARACJA:=null;
   _mp.save(,_wy);
   _mp.done()
|? _jest=-1
|| _err:='Brak parametru DEKLARACJA.'@;
   FUN.info(_err);
   _mp.error(_err)
?};
VAT_DEK.cntx_pop();
VAR_DEL.delete('VDTyp')


\set_win_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przygotowuje i ustawia okno deklaracji do wysyłania e-deklaracji
::----------------------------------------------------------------------------------------------------------------------
_okn:=VAT_DEK.mk_edit('Dane deklaracji'@);
VAT_DEK.win_ewin(_okn,,VAT_DEK.win_edit('?'));
VAT_DEK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],"
   exec('vat_dpoz','fks_ved');
   ''
");
VAT_DEK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&E-deklaracje'@],"
   exec('edek_sel','fks_ved',VDTyp);
   ''
");
_an:=VAT_DEK.win_ebtn(_okn,'text=%1, btn_label_align=center, panel=bottom, align=end,display=1'['&Anuluj'@],'key:Esc');
VAT_DEK.btn_eopt(_okn,_an,'tooltip='+exec('help_red_esc','#window','A'));
VAT_DEK.win_edit(_okn)


\desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Formula dla ToDo czynnosci zwiazanych z deklaracjami
::   WE: _a - typ deklaracji
::       _b - poczatek opisu
::----------------------------------------------------------------------------------------------------------------------
_desc:=_b;
_mp:=params_get().mp;
_we:=_mp.load(exec('kind_in','#b_port'));
{? var_pres('DEKLARACJA',_we)
|| VAT_DEK.cntx_psh();
   VAT_DEK.prefix();
   {? VAT_DEK.seek(_we.DEKLARACJA)
   || _desc+=exec('record','#to_string',VAT_DEK.ref())
   ?};
   VAT_DEK.cntx_pop()
|| _desc+=_a
?};
_desc


\desc_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Opis czynności na ToDo
::   WE: _a - typ deklaracji
::----------------------------------------------------------------------------------------------------------------------
_desc:='Utwórz deklarację '@@;
_mp:=params_get().mp;
_refy:=_mp.getRefs();
{? var_pres('[1]',_refy)>0 & _refy[1]<>''
|| VAT_DEK.cntx_psh();
   VAT_DEK.prefix();
   {? VAT_DEK.seek(_refy[1])
   || _desc:='Utwórz deklarację %1'@@[exec('record','#to_string',VAT_DEK.ref())]
   ?};
   VAT_DEK.cntx_pop()
|| _desc:='Utwórz deklarację %1'@@[_a]
?};
_desc


\vat_uzas_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Czy wypełniono uzasadnienia
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
VAT_DEKT.index('VAT_DEK');
VAT_DEKT.prefix(VAT_DEK.ref());
{? VAT_DEKT.first()
|| {!
   |? _ok:=VAT_DEKT.MEMO<>null | exec('pomin_uzas','fks_ved');
      _ok & VAT_DEKT.next()
   !}
?};
_ok


\wysw_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2011]
:: OPIS: Wyswietla naglowek lub pozycje dokumentu na podstawie rekordu z tabeli VAT_PS
::   WE: _a - 0  nagłówek z DOK, 1 pozycje
::  OLD: \wysw_dok/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_PS.REJ_KS=null()
|| VAT_PS.win_edit( {? VAT_PS.TYP='A' | VAT_PS.TYP='R' |  VAT_PS.TYP='J' || exec('vat_ps_okno','fks_ved') || exec('vat_kor_edit','fks_ved') ?});
   UNPAR.P10:=VAT_PS.KH;
   UNPAR.P11:=VAT_PS.NIP;
   {? var_pres('JPK_TD',VAT_PS)>0
   || {? VAT_PS.JPK_TD<>''
      || JPK_SLO.index('KOD');
         _wer:=exec('bl_js_wersja','jpk_v');
         JPK_SLO.index('KOD');
         JPK_SLO.prefix(_wer);
         HELPJPK.JPK_SLO:={? JPK_SLO.find_key('TD_S',VAT_PS.JPK_TD) | JPK_SLO.find_key('TD_Z',VAT_PS.JPK_TD) || JPK_SLO.ref() || null ?}
      || HELPJPK.JPK_SLO:=null
      ?}
   ?};
   VAT_PS.display();
   VAT_PS.win_edit();
   return()
?};
_ok:=0;
_pvat:=4+VAT_PS.PVAT_REF='pvat';
{? _pvat
|| PVAT.cntx_psh();
   PVAT.use(VAT_PS.PVAT_REF-8);
   PVAT.prefix();
   {? PVAT.seek(BB.sqlint(VAT_PS.PVAT_REF),PVAT.name())
   || _ok:=1;
      {? exec('sha1','#to_sha1','PVAT',2+VAT_PS.SHA)<>VAT_PS.SHA
      || FUN.emsg('Pozycja VAT została zmodyfikowana po wygenerowaniu deklaracji.'@)
      ?}
   ?}
|| VPOZ.cntx_psh();
   VPOZ.use(VAT_PS.PVAT_REF-8);
   VPOZ.prefix();
   {? VPOZ.seek(BB.sqlint(VAT_PS.PVAT_REF),VPOZ.name())
   || _ok:=1;
      {? exec('sha1','#to_sha1','VPOZ',2+VAT_PS.SHA)<>VAT_PS.SHA
      || FUN.emsg('Pozycja VAT została zmodyfikowana po wygenerowaniu deklaracji.'@)
      ?}
   ?}
?};
{? _ok
|| P_V.cntx_psh();
   FAK.cntx_psh();
   VPOZ.cntx_psh();
   VROZ.cntx_psh();
   VPOW.cntx_psh();
   DVAT.cntx_psh();
   DOK.cntx_psh();
   POZ.cntx_psh();
   POW.cntx_psh();
   AN.cntx_psh();
   POZF.cntx_psh();
   AN_SLU.cntx_psh();
   SSTALE.cntx_psh();
   {? _pvat
   || DVAT.use('dvat__'+(PVAT.name()+2));
      PVAT.DVAT();
      _maska:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
      SSTALE.AO:=DVAT.DOKOKRO;
      SSTALE.AR:=OKRO_F.ROK
   || _maska:=VPOZ.name()+4;
      ROK_F.index('KODG');
      ROK_F.prefix(2+_maska);
      {? ROK_F.first()
      || OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref(),#(_maska+2));
         {? OKRO_F.first()
         || SSTALE.AR:=ROK_F.ref();
            SSTALE.AO:=OKRO_F.ref()
         ?}
      ?}
   ?};
   POMOC.cntx_psh();
   POMOC.USE:=_maska;
   DOK.use('doku'+_maska);
   POZ.use('pozy'+_maska);
   VPOZ.use('pozv'+_maska);
   POW.use('pow_'+_maska);
   AN.use('koan__'+ROK_F.KOD);
   POZF.use('pozf'+_maska);
   AN_SLU.use('koansl'+ROK_F.KOD);
   DOK.prefix();
   {? _pvat
   || DVAT.DOK()
   || VPOZ.DOK()
   ?};
   exec('tkrs_eb','fks_viudo',1,'__');
   {? _a=0
   || exec('dok_spac','dok_fks')
   || SUM.cntx_psh();
      exec('poz_dok','dok_fks',1);
      SUM.cntx_pop()
   ?};
   exec('tkrs_eb','fks_viudo',2);
   POMOC.cntx_pop();
   SSTALE.cntx_pop();
   P_V.cntx_pop();
   FAK.cntx_pop();
   VPOZ.cntx_pop();
   VROZ.cntx_pop();
   VPOW.cntx_pop();
   DVAT.cntx_pop();
   DOK.cntx_pop();
   POZ.cntx_pop();
   POW.cntx_pop();
   AN.cntx_pop();
   POZF.cntx_pop();
   AN_SLU.cntx_pop()
|| FUN.emsg('Pozycja VAT została usunięta z ewidencji VAT po wygenerowaniu deklaracji.'@)
?};
{? _pvat
|| PVAT.cntx_pop()
|| VPOZ.cntx_pop()
?};
1


\vat_ps_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca typ operacji na podstawie VAT_PS
::  OLD: \vat_ps_typ/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? 2+VAT_DEK.TYP='V7'
|| _od:=39;
   _do:=67;
   {? VAT_PS.VAT_POZ().POZ1<>0 & VAT_POZ.POZ1>=_od & VAT_POZ.POZ1<=_do |
      VAT_POZ.POZ2<>0 & VAT_POZ.POZ2>=_od & VAT_POZ.POZ2<=_do
   || 'Z'
   || 'S'
   ?}
|| _gr:=42;
   {? VAT_PS.VAT_POZ().POZ1<>0 & VAT_POZ.POZ1>=_gr | VAT_POZ.POZ2<>0 & VAT_POZ.POZ2>=_gr
   || 'Z'
   || 'S'
   ?}
?}


\czy_red_szcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Dla tych pol deklaracji bedzie dostepna redakcja szegolow pozycji deklaracji
::       i nie beda one dotyczyc zlych dlugow
::  OLD: \czy_red_szcz/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? 2+VAT_DEK.TYP='V7'
|| VAT_POZ.TYP<>'S'
|| {? (VAT_DEK.TYP='VAT7' | VAT_DEK.TYP='VAT7_KOR') & VAT_DEK.NR().NR>=17
   || VAT_POZ.POZ2<>0 & '36,37,38,39,42,47,48,49,50,52,53,55,57,58,59,60'*($VAT_POZ.POZ2)
   |? (VAT_DEK.TYP='VAT7' | VAT_DEK.TYP='VAT7_KOR') & VAT_DEK.NR().NR>=16
   || VAT_POZ.POZ2<>0 & '36,37,38,41,46,47,48,50,51,53,55,56,57,58'*($VAT_POZ.POZ2)
   || 0
   ?}
?}


\add_szczegoly
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Dodaje rekord TVAT na potrzeby szczegolow w pozycjach deklaracji,
::       który zostana przepisane do pliku JPK
::   WE: _a - numer pozycji deklaracji
::       _b - kwota podatku
::      [_c] - parametr pomocniczy, wskazujący,że dla deklaracji należy wykonać dodatkowe przypisanie
::  OLD: \add_szczegoly/vat.fml
::----------------------------------------------------------------------------------------------------------------------
TVAT.cntx_psh();
TVAT.prefix();
TVAT.blank(1);
TVAT.GRPOD:=TVAT.KLVAT:='';
TVAT.DSYM:='Korekta zbiorcza' ;
TVAT.SYM_ZEW:=TVAT.DSYM;
{? var_pres('_c')>0
|| TVAT.JPK_TD:='WEW'
?};
TVAT.TYP:='A';
TVAT.POZ:=0;
TVAT.POZ1:=_a;
TVAT.TP:=TVAT.DATAW:=SSTALE.AO().POCZ;
TVAT.KH:='brak nazwy kontrahenta';
TVAT.NIP:='brak nip';
{? var_press('UL',TVAT)>0
|| TVAT.UL:='brak adresu kontrahenta'
?};
TVAT.PODAT:=_b;
TVAT.add();
TVAT.cntx_pop()


\vat_ps_okno
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Zwraca okno redagowania VAT_PS na potrzeby redakcji szczegolow pozycji deklaracji
::  OLD: \vat_ps_okno/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_zakup:=0;
{? 2+VAT_DEK.TYP='V7'
|| _zakup:=VAT_POZ.POZ2>36 & VAT_POZ.POZ2<69
|? 4+VAT_DEK.TYP='VAT7'
|| _zakup:=VAT_DEK.NR().NR=16 & VAT_POZ.POZ2>38 |
           VAT_DEK.NR().NR>=17 & VAT_POZ.POZ2>39
?};
VAR_DEL.delete('__VPSedi');
__VPSedi:=VAT_PS.mk_edit('Pozycja szczegółów'@);
VAT_PS.win_efld(__VPSedi,UNPAR,'P10',,,60,,,'Kontrahent'@,,,,'F3_button=1');
VAT_PS.win_efld(__VPSedi,UNPAR,'P11',,,13,,,'NIP',,,,'F3_button=1');
{? var_press('ADRES',VAT_PS)>0
|| VAT_PS.win_efld(__VPSedi,,'ADRES',,,63,,,'Siedziba'@)
?};
VAT_PS.win_efld(__VPSedi,,'SYM',,,,,,'Symbol faktury'@);
VAT_PS.win_efld(__VPSedi,,'SYM_ZEW',,,,,,'Symbol zewnętrzny'@);
{? var_press('DATA2',VAT_PS)>0
|| _naz:={? _zakup
         || 'Data otrzymania'@
         || 'Data sprzedaży'@
         ?};
   VAT_PS.win_efld(__VPSedi,,'DATA2',,,,,,_naz)
?};
_naz:={? _zakup
      || 'Data zakupu'@
      || 'Data wystawienia'@
      ?};
VAT_PS.win_efld(__VPSedi,,'DATA',,,,,,_naz);
{? VAT_POZ.POZ1
|| VAT_PS.win_efld(__VPSedi,,'NETTO',,,16,2,,'Netto')
?};
{? VAT_POZ.POZ2
|| VAT_PS.win_efld(__VPSedi,,'VAT',,,16,2,,'VAT')
?};
{? var_pres('JPK_TD',VAT_PS)>0
|| {? 1+(VAT_DEK.OKRES+2)='/'
   || VAT_PS.win_efld(__VPSedi,,'OKRO_F','NR','ROK',,,,'Okres VAT')
   ?};
   HELPJPK.SLO_WER:=exec('bl_js_wersja','jpk_v');
   VAR_DEL.delete('JpkSloT');
   JpkSloT:='TD_'+{? _zakup || 'Z' || 'S' ?};
   HELPJPK.fld_fml('JPK_SLO','BEFORE_EDIT',"HELPJPK.SLO_TYP:=JpkSloT;1");
   VAT_PS.win_efld(__VPSedi,HELPJPK,'JPK_SLO','KOD','KOD',,,,'Typ dokumentu');
   {? ~_zakup
   || VAT_PS.win_efld(__VPSedi,,'JPK_GTU',,,60,,,'Grupy towarów',,,,'F3_button=1')
   ?};
   VAT_PS.win_efld(__VPSedi,,'JPK_PROC',,,60,,,'Szczególne procedury',,,,'F3_button=1')
?};
UNPAR.P10_BV:=UNPAR.P10_BE:=UNPAR.P10_AE:='';
UNPAR.P10_F3:='exec(\'f3_vat_kor_kh\',\'!fks_ved_dv7d\',0)';
UNPAR.P11_BV:=UNPAR.P11_BE:=UNPAR.P11_AE:='';
UNPAR.P11_F3:='exec(\'f3_vat_kor_kh\',\'!fks_ved_dv7d\',1)';
exec('mark','#field',VAT_PS,__VPSedi,'SYM','SYM_ZEW','DATA');
{? VAT_POZ.POZ1
|| exec('mark','#field',VAT_PS,__VPSedi,'NETTO')
?};
{? VAT_POZ.POZ2
|| exec('mark','#field',VAT_PS,__VPSedi,'VAT')
?};
__VPSedi


\chk_sik_rozl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Czy istnieje rodzaj deklaracji
::----------------------------------------------------------------------------------------------------------------------
{!
|? _more:=0;
   SIK.ROK1();
   VATZMDEK.index('ROZL_VAT');
   VATZMDEK.prefix(ROK_F.ref());
   _jest:=0;
   {? VATZMDEK.find_key(SIK.ROZL_VAT)
   || _jest:=1
   |? ROK_F.ROZL_VAT=SIK.ROZL_VAT
   || _jest:=1
   || {? SIK.ROZL_VAT='K' || _r:='kwartalnego'  || _r:='miesięcznego' ?};
      {? FUN.choice('Dla roku %1 nie ustawiono rozliczenia %2 VAT.'@[ROK_F.NAZ,_r],,'Ustaw')
      || exec('zm_rozl_vat','fks_ved');
         _more:=1
      ?}
   ?};
   _more
!};
_jest


\zm_rozl_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Funkcja wyswietla zmiany rozliczen VAT dla danego roku
::  OLD: \zm_rozl_vat/skid_rok.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh(); OKRO_F.win_dict('SLO'); OKRO_F.win_sel('SLO');
VATZMDEK.cntx_psh(); VATZMDEK.index('ROK'); VATZMDEK.prefix(ROK_F.ref());
VATZMDEK.win_sel('WER');
VATZMDEK.hdr_sel(' w roku %1'@[ROK_F.NAZ]);
VATZMDEK.select();
VATZMDEK.cntx_pop(); OKRO_F.cntx_pop()


\zadania
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Prezentuje zadania dla deklaracji
::----------------------------------------------------------------------------------------------------------------------
exec('todo_select','#b__box',VAT_DEK.uidref())


\edek_sel_vat7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wyswietla e-deklaracje dla deklaracji VAT-7
::  OLD: \edek_sel_vat7/skid_xml.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('bevatdek','fks_ved')
|| SKID.TYP_ZAP:={? 5+VAT_DEK.TYP='VAT7D' || 'VAT7D' || 'VAT7' ?};
   E_DEK.index('DATA_ONE'); E_DEK.prefix(BPMN.SYM_DOM,SKID.TYP_ZAP,VAT_DEK.ref());
   E_DEK.win_sel('WER');
   E_DEK.hdr_sel(' '+VAT_DEK.TYP+' za okres '+VAT_DEK.OKRES+', wersja '+$VAT_DEK.WER);
   E_DEK.select(,,,'R:R');
   VAT_DEK.r_unlock()
?}


\can_edit2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy mozna modyfikowac deklarację
::  OLD: \can_edit/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('JPK')>0 & exec('jpk4vat','jpk')
|| FUN.emsg('Istnieją pliki JPK.\nModyfikowanie zabronione.'@); 0
|? exec('is_edek','xml',{? VAT_DEK.TYP+4='_KOR' || VAT_DEK.TYP-4 || VAT_DEK.TYP ?})
|| FUN.emsg('Istnieją e-Deklaracje.\nModyfikowanie zabronione.'@); 0
|| 1
?}


\actions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [12.41]
:: OPIS: Ukrywa akcję wydruk dla uzasadnień dla któych wydruk nie jest zdefiniowany
::   WE: _a - aktonim okna tabeli VAT_DEKT
::----------------------------------------------------------------------------------------------------------------------
{? VAT_DEKT.KOD='ZAL1' &
   VAT_DEKT.VAT_DEK().NR().NR>=17
|| VAT_DEKT.actions(_a,'',,1)
|| VAT_DEKT.actions(_a,'W',,1)
?}


\cit8_2k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy wiersz sprawozdania CIT-8 zawiera 2 kolumny
::   WE: _a - 1-cit-8 0-cit-8/O
::----------------------------------------------------------------------------------------------------------------------
{? DEFW.ALGORYTM<>'P'
|| _nr:=#(DEFW.KOD-1);
   _cit8O_max:=162;
   {? VAT_DEK.NR().NR=32
   || _cit8O_max:=164
   ?};
   {? _a=1 & _nr>38 & _nr<264 |
      _a=0 & _nr<_cit8O_max
   || _kod:=($(_nr+1))+'.';
      DEFW.cntx_psh();
      DEFW.index('GRUPA'); DEFW.prefix(GR_SIK.ref(),_kod,);
      _dwie:=~DEFW.first();
      DEFW.cntx_pop();
      _dwie
   ?}
?}


\cit82kwin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tworzy okno do wyświetlenie deklaracji CIT-8 (26) (z dwoma kolumnami)
::   WE: _a - typ okna: 'sel', 'red'
::----------------------------------------------------------------------------------------------------------------------
{? _a='sel'
|| _o:=VAT_POZ.mk_sel('Pozycje deklaracji','P',,'#vatpozcit',,,,,'U');
   VAT_POZ.win_fld(_o,,'LP',,,15,,,'Kod');
   VAT_POZ.win_fld(_o,,'OP',,,80,,,'Opis');
   VAT_POZ.win_fld(_o,,'TYP',,,,,,'Typ');
   VAT_POZ.win_fld(_o,,'NETTO',,,12,,,'Z zysków kapitałowych');
   VAT_POZ.win_fld(_o,,'VAT',,,12,,,'Z innych źródeł');
   VAT_POZ.win_act(_o,,'Popraw',,,,"exec('bm_cit_poz','!fks_ved_dvcd')","exec('am_cit_poz','!fks_ved_dvcd')",1);
   VAT_POZ.win_act(_o,,'Kolejność');
   _b_rek:='exec(\'be_vatpoz_r\',\'fks_ved\',\''+_o+'\')';
   VAT_POZ.win_act(_o,,'Rekord',,,,$_b_rek);
   _o
|| VAR_DEL.delete('CitRed');
   CitRed:=obj_new('R1','R2');
   _o:=VAT_POZ.mk_edit('Pozycja deklaracji',,'#vatpozcit2');
   VAT_POZ.win_efld(_o,,'LP',,,80,,1,'Kod');
   VAT_POZ.win_efld(_o,,'OP',,,,,1,'Opis');
   VAT_POZ.win_efld(_o,UNPAR,'P1',,,,,,'Tak',,,'check-box',,"1","0");
   _btnok:=VAT_POZ.win_ebtn(_o,'text=%1'['Zapi&sz'@],'key:F2');
   _btnan:=VAT_POZ.win_ebtn(_o,'text=%1,display=1'['Anuluj'@],'key:Esc');
   VAT_POZ.btn_eopt(_o,_btnok,'tooltip='+exec('help_red_ok','#window','Z'));
   VAT_POZ.btn_eopt(_o,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
   UNPAR.P1_BE:=UNPAR.P1_AE:=UNPAR.P1_BV:='';
   CitRed.R2:=_o;
   _o:=VAT_POZ.mk_edit('Pozycja deklaracji',,'#vatpozcit');
   VAT_POZ.win_efld(_o,,'LP',,,80,,1,'Kod');
   VAT_POZ.win_efld(_o,,'OP',,,,,1,'Opis');
   VAT_POZ.win_efld(_o,,'NETTO',,,,,,'Z zysków kapitałowych');
   VAT_POZ.win_efld(_o,,'VAT',,,,,,'Z innych źródeł');
   VAT_POZ.win_efld(_o,,'TEXT',,,80,,,'Treść');
   VAT_POZ.win_efld(_o,UNPAR,'P4',,,,,,'Data');
   _btnok:=VAT_POZ.win_ebtn(_o,'text=%1'['Zapi&sz'@],'key:F2');
   _btnan:=VAT_POZ.win_ebtn(_o,'text=%1,display=1'['Anuluj'@],'key:Esc');
   VAT_POZ.btn_eopt(_o,_btnok,'tooltip='+exec('help_red_ok','#window','Z'));
   VAT_POZ.btn_eopt(_o,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
   CitRed.R1:=_o;
   _o
?}


\rok_kal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy rok kalendarzowy
::----------------------------------------------------------------------------------------------------------------------
{? exec('cit8_data','fks_ved',1)+5='01-01' & exec('cit8_data','fks_ved',2)+5='12-31'
|| '2'
|| '1'
?}


\get_date
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Pobiera datę z pozycji deklaracji
::----------------------------------------------------------------------------------------------------------------------
_data:='0000-00-00';
_kod:=-(2-ISTDEFS.OPIS);
_dl:=+_kod;
{? 'abc'*_kod+1 || _dl-=1 ?};
_kod:=(3-_dl)*'0'+_kod;
VAT_POZ.cntx_psh();
VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),'CIT8   '+_kod);
{? VAT_POZ.first()
|| _data:=VAT_POZ.TEXT
?};
VAT_POZ.cntx_pop();
{? ISTDEFS.WYM='T' | _data<>'0000-00-00'
|| _data
|| ''
?}


\be_vatpoz_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.02]
:: OPIS: Formuła przed wyśyświetleniem rekordu tabeli VAT_POZ
::   WE: [_a] - Wskazanie na okienko wertowania.
::----------------------------------------------------------------------------------------------------------------------
{? VAT_DEK.STATUS='A'
|| _hid:='P'
|| _hid:=''
?};
VAT_POZ.actions_grayed(_a,_hid);
{? var_pres('CitRed')>0
|| {? VAT_POZ.TYP='C'
   || VAT_POZ.win_edit(CitRed.R2)
   || VAT_POZ.win_edit(CitRed.R1)
   ?}
?};
{? VAT_POZ.TYP='D'
|| UNPAR.P4:=date(#(4+VAT_POZ.TEXT),#(2+(5-VAT_POZ.TEXT)),#(VAT_POZ.TEXT+2))
|? VAT_POZ.TYP='C'
|| UNPAR.P1:={? VAT_POZ.VAT || 1 ?}
?};
~~


\set_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia/czyści formuły dla pól uzywanych w deklaracji CIT-8
::   WE: _a - 1-ustawia 0-czyści
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('PAR10',VAT_DEK)<=0 || return() ?};
{? _a & 4+VAT_DEK.TYP='CIT8' & VAT_DEK.NR().NR>=28
|| VAT_DEK.fld_fml('PAR3','AFTER_EDIT',"{? VAT_DEK.PAR3='T' || VAT_DEK.PAR5:='N' ?}; 1");
   VAT_DEK.fld_fml('PAR5','AFTER_EDIT',"{? VAT_DEK.PAR5='T' || VAT_DEK.PAR3:='N' ?}; 1");
   VAT_DEK.fld_fml('PAR4','AFTER_EDIT',"{? VAT_DEK.PAR4='T' || VAT_DEK.PAR8:='N' ?}; 1");
   VAT_DEK.fld_fml('PAR8','AFTER_EDIT',"{? VAT_DEK.PAR8='T' || VAT_DEK.PAR4:='N' ?}; 1");
   VAT_DEK.fld_fml('PAR10','BEFORE_EDIT',"VAT_DEK.TYP+3='KOR'")
|| VAT_DEK.fld_fml('PAR3','AFTER_EDIT',"*");
   VAT_DEK.fld_fml('PAR5','AFTER_EDIT',"*");
   VAT_DEK.fld_fml('PAR4','AFTER_EDIT',"*");
   VAT_DEK.fld_fml('PAR8','AFTER_EDIT',"*");
   VAT_DEK.fld_fml('PAR10','BEFORE_EDIT',"*")
?}


\pomin_uzas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42_12]
:: OPIS: Uzasadnienia niewymagane dla Okresy zawieszenia i Nazwy spółek i okresy zawieszenia
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? 4+VAT_DEK.TYP='CIT8'
|| 3+VAT_DEKT.KOD='ZAW' | VAT_DEKT.KOD='INNE'
?}


\br_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Rekord przed okna deklaracji
::   WE: _a - akronim okna
::----------------------------------------------------------------------------------------------------------------------
_win:={? var_pres('_a')>0 & type_of(_a)=type_of('') || _a |? var_pres('_b')>0 & type_of(_b)=type_of('') || _b || 'VAT7' ?};
VAT_DEKT.index('VAT_DEK');
VAT_DEKT.prefix(VAT_DEK.ref());
_grayed:='';
{? ~VAT_DEKT.first()
|| _grayed:='A'
?};
{? ~exec('can_edit','fks_ved')
|| _grayed+='PU'
?};
{? 3+VAT_DEK.TYP='VAT' & ~exec('jpk4vat','jpk')
|| _grayed+='L'
?};
_grayed+=exec('grayAkc','fks_ved');
VAT_DEK.actions_grayed(_win,_grayed);
''


\f3_jpk_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla pól słownika JPK
::  OLD: \f3_jpk_slo/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
_typ:=4-_fld;
HELPJPK.SLO_TYP:=_typ;
_obj:=exec('obj','jpk_v',1);
_obj.fill();
_obj.set(_typ,fld());
_obj.tab.hdr_sel();
_obj.tab.hdr_sel(exec('slo_tyt','jpk_v',_typ));
_obj.tab.index(__ndxprc);
_obj.tab.prefix(_typ,);
{? _obj.tab.select()
|| fld(_obj.get(_typ));
   1
?}


\rek_pr_vat_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB  [2011]
:: OPIS: Akcja rekord przed okienka wert. VAT7 tabeli VAT_POZ
::  OLD: \rek_pr_vat_poz/vat.fml
::  OLD: \rek_pr_vat_poz/!fks_ved_zv7p.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_POZ.TYP='A' & (5+VAT_DEK.TYP)='VAT7D' & ~('69'*($VAT_POZ.POZ2)) &
   ( exec('vat_kor_on','fks_ved') | exec('rek_pr_vat_poz1','vat7') )
|| _grayed:=''
|? VAT_POZ.TYP='A' & (4+VAT_DEK.TYP='VAT7' & VAT_DEK.NR().NR>=10 | 2+VAT_DEK.TYP='V7' ) & ~('69'*($VAT_POZ.POZ2)) &
    ( exec('czy_red_szcz','fks_ved') | exec('vat_kor_on','fks_ved') | exec('rek_pr_vat_poz1','vat7') )
|| _grayed:=''
|? VAT_POZ.TYP='R' & (4+VAT_DEK.TYP='VAT7' & VAT_DEK.NR().NR>=10 | 2+VAT_DEK.TYP='V7' )&
    ( exec('czy_red_szcz','fks_ved') | exec('rek_pr_vat_poz1','vat7') )
|| _grayed:=''
|| _grayed:='Z'
?};
{? ~exec('can_edit','fks_ved')
|| _grayed+='p'
?};
{? 2+VAT_DEK.TYP<>'V7'
|| {? VAT_DEK.PLIK=null
   || _grayed+='J(UZ)'
   ?}
?};
{? 2+VAT_DEK.TYP='V7'
|| VAT_POZ.actions_grayed('V7',_grayed)
|| VAT_POZ.actions_grayed('VAT7',_grayed)
?};
''


\dek2html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Prezentuje deklarację w postacji HTML
::  OLD: \dek2html/vat.fml
:: ~OST: INBLGET,INCLIEXEC,INFCOPY,INFERASE,INFEXISTS,INTMKTMPDIR,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
SKID.TYP_ZAP:=VAT_DEK.TYP;
E_DEK.cntx_psh();
E_DEK.index('DATA_ONE'); E_DEK.prefix('FKS',SKID.TYP_ZAP,VAT_DEK.ref());

{? ~E_DEK.first()
|| FUN.info('Brak e-deklaracji. Wydruk niedostępny.'@)
|| _interm:=exec('interm','#system');
   {? _interm
|| _tmpdir:=fmk_tmp_dir(0);
   {? type_of(_tmpdir) <> type_of(~~)
   || _fdir:=_tmpdir.get_path
   || _fdir:=''
   ?};
      _fc:="{? ~fexists(_b+'/'+_a) || fcopy(_a,_b+'/'+_a,1,0,1) || 1 ?}"
   || _fc:="{? ~fexists('@'+tmp_dir()+'/'+_a) || fcopy(_a,'@'+tmp_dir()+'/'+_a,1,0,1) || 1 ?}";
      _fdir:=tmp_dir()
   ?};
   _fn:=-E_DEK.TYP+'_'+$VAT_DEK.NR().NR;
   _fxsl:=_fn+'.xsl';
   _fin:=_fdir+'/'+_fn+'.xml';
   _fout:=_fdir+'/'+_fn+'.html';
   {? fexists(_fxsl,1)
   || _fc(_fxsl,_fdir);
      E_DEK.bl_get('PLIK_GEN',{? _interm || '' || '@' ?}+_fin,0);
      {? _interm
      || exec('xml2html','xml',_fin,_fdir+'/'+_fxsl,_fout,_fdir)
      || exec('xml2html','xml',_fin,_fdir+'/'+_fxsl,_fout)
      ?};
      exec('uzu_html','xml',_fout,{? _interm || '' || '@' ?}+_fin);
      ferase({? _interm || '' || '@' ?}+_fdir+'/'+_fxsl,0);
      ferase({? _interm || '' || '@' ?}+_fin,0);
      {? _interm || dlg_save(_fout) || cli_exec(_fout) ?}
   || echo('Brak na serwerze pliku '@+_fxsl+'.');
      FUN.info('Wydruk niedostępny.'@);
      echo()
   ?}
?};
E_DEK.cntx_pop()


\vat_pod_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obiekt do obsług pozycji deklaracji VAT-UE dla magazynów CALL-OF STOCK
::  OLD: \vat_pod_obj/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'KRAJ','NIP1','NIP2','PP',
   'get','set'
);
_obj.KRAJ:=_obj.NIP1:=_obj.NIP2:='';
_obj.PP:=-1;
_obj.set:="
   _txt:={? var_pres('_a')>0 || _a || VAT_POZ.OP ?};
   _tab:={? var_pres('_b')>0 || _b || ~~ ?};
   _bj:={? var_pres('_c')>0 || _c || 'J' ?};
   _str:=spli_str(_txt,',');
   {? obj_len(_str)=3
   || .KRAJ:=_str[1];
      .NIP1:=|_str[2];
      {? (|_str[3])*':'>0
      || .NIP2:='';
         .PP:={? _str[3]*'tak' || 2 || 1 ?}
      || .NIP2:=|_str[3];
         .PP:=-1
      ?}
   || .KRAJ:=.NIP1:=.NIP2:='';
      .PP:=-1
   ?};
   {? var_pres('_tab')>0
   || {? _bj='J'
      || _tab.J_KRAJ:=.KRAJ;
         _tab.J_NIP1:=.NIP1;
         _tab.J_NIP2:=.NIP2;
         _tab.J_PP:=.PP
      || _tab.B_KRAJ:=.KRAJ;
         _tab.B_NIP1:=.NIP1;
         _tab.B_NIP2:=.NIP2;
         _tab.B_PP:=.PP
      ?}
   ?}
";
_obj.get:="
   _tab:={? var_pres('_a')>0 || _a || ~~ ?};
   _bj:={? var_pres('_b')>0 || _b || 'J' ?};
   {? var_pres('_tab')>0
   || {? _bj='J'
      || .KRAJ:=_tab.J_KRAJ;
         .NIP1:=gsub(_tab.J_NIP1,'-','');
         .NIP2:=gsub(_tab.J_NIP2,'-','');
         .PP:=_tab.J_PP
      || .KRAJ:=_tab.B_KRAJ;
         .NIP1:=gsub(_tab.B_NIP1,'-','');
         .NIP2:=gsub(_tab.B_NIP2,'-','');
         .PP:=_tab.B_PP
      ?}
   ?};
   {? .KRAJ='' & .NIP1='' & .NIP2='' & .PP<1
   || ''
   || .KRAJ+', '+.NIP1+', '+{? .NIP2<>'' || .NIP2 || 'powrotne przemieszczenie: '+{? .PP=1 || 'nie' || 'tak' ?} ?}
   ?}
";
_obj



\win_pod_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca okno wertowania dla deklaracji VAT-UE
::  OLD: \win_pod_poz/vat.fml
::----------------------------------------------------------------------------------------------------------------------
:: Okno wertowania POD tabeli VAT_POZ
_sel:=VAT_POZ.mk_sel('Pozycje deklaracji','P',,'#vatpozpod2',,,,,'U');
VAT_POZ.win_fld(_sel,,'LP',,,15,,,'Kod');
VAT_POZ.win_fld(_sel,,'OP',,,80);
VAT_POZ.win_fld(_sel,,'TYP',,,3);
VAT_POZ.win_fld(_sel,,'NETTO',,,12,,,'Kwota');
VAT_POZ.win_fld(_sel,,'Z',,,8,,,'Trójstronna');
VAT_POZ.win_act(_sel,,'Formuła','Dołącz',,,"exec('vat_pod_add','fks_ved')");
VAT_POZ.win_act(_sel,1,'Formuła','Dołącz',,,"exec('vat_pod_add','fks_ved')");
VAT_POZ.win_act(_sel,,'Popraw',,,,"exec('pr_vat_p','fks_ved')",,1);
VAT_POZ.win_act(_sel,,'Formuła','Usuń',,,"exec('bd_vat_poz','!fks_ved_dvud')");
VAT_POZ.win_act(_sel,,'Szukaj',,,,);
VAT_POZ.win_act(_sel,,'Kolejność',,,,);
_sel


\vat_pod_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Dołączenia pozycji deklaracji VAT-UE dla magazynów CALL-OF STOCK
::  OLD: \vat_pod_add/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy dołączyć informację o przemieszczeniach towarów\nw procedurze magazynu typu CALL-OF STOCK?')
|| _nk:={? VAT_DEK.TYP='PODK' || 'K' || '' ?};
   _dop:={? VAT_DEK.TYP='PODK' || '000' || '   ' ?};
   _sp:={? VAT_DEK.TYP='PODK' || ' ' || '' ?};
   VAT_POZ.cntx_psh();
   VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),'VAT-UE'+_nk+' F.');
   _nr:={? VAT_POZ.last()
        || {? VAT_DEK.TYP='PODK'
           || #(|(10-VAT_POZ.LP-2))+1
           || #(|(9-VAT_POZ.LP))+1
           ?}
        || 1
        ?};
   VAT_POZ.cntx_pop();
   _ref:=VAT_POZ.ref();
   VAT_POZ.blank();
   VAT_POZ.VAT_DEK:=VAT_DEK.ref();
   VAT_POZ.LP:='VAT-UE'+_nk+' F.'+_sp+((_dop+$_nr)+3);
   VAT_POZ.TYP:='C';
   {? VAT_DEK.TYP='PODK'
   || _lp:=VAT_POZ.LP;
      VAT_POZ.LP:=_lp+' B';
      VAT_POZ.add();
      VAT_POZ.LP:=_lp+' J';
      VAT_POZ.add()
   ?};
   {? exec('vat_pod_edit','fks_ved')
   || {? VAT_DEK.TYP='PODK'
      || VAT_POZ.put()
      || VAT_POZ.add()
      ?}
   |? VAT_DEK.TYP='PODK'
   || VAT_POZ.del();
      VAT_POZ.del();
      {? _ref
      || VAT_POZ.seek(_ref)
      ?}
   ?}
?}


\vat_pod_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Usunięcie pozycji deklaracji VAT-UE dla magazynów CALL-OF STOCK
::  OLD: \vat_pod_del/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć informację o przemieszczeniach towarów\nw procedurze magazynu typu CALL-OF STOCK?')
|| VAT_POZ.cntx_psh();
   _ref:={? VAT_POZ.next() | VAT_POZ.prev() || VAT_POZ.ref() || null ?};
   VAT_POZ.cntx_pop();
   VAT_POZ.del();
   VAT_POZ.cntx_psh();
   VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),'VAT-UE F.');
   _nr:=0;
   {? VAT_POZ.first()
   || {!
      |? _nr+=1;
         VAT_POZ.LP:='VAT-UE F.'+((('   ')+$_nr)+3);
         VAT_POZ.put();
         VAT_POZ.next()
      !}
   ?};
   VAT_POZ.cntx_pop();
   {? _ref
   || VAT_POZ.seek(_ref)
   ?}
?}


\vat_pod_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tabela dla pozycji deklaracji VAT-UE dla magazynów CALL-OF STOCK
::  OLD: \vat_pod_tab/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(3,
   'LP','STRING[20]','Kod',
   'J_KRAJ','STRING[2]','Kod kraju',
   'J_NIP1','STRING[50]','NIP kontrahenta',
   'J_NIP2','STRING[50]','NIP kontrahenta zastąpionego',
   'J_PP','INTEGER','Powrotne przemieszczenie',
   'B_KRAJ','STRING[2]','Kod kraju',
   'B_NIP1','STRING[50]','NIP VAT kontrahenta',
   'B_NIP2','STRING[50]','NIP kontrahenta zastąpionego',
   'B_PP','INTEGER','Powrotne przemieszczenie'
);
_edit:=_tab.mk_edit('Przemieszczenie towarów',,'vatpozcos');
{? VAT_DEK.TYP='POD'
|| _tab.win_efld(_edit,,'LP',,,20,,,'Kod');
   _tab.win_efld(_edit,,'J_KRAJ',,,20,,,'Kod kraju',,,,'F3_button=1');
   _tab.win_efld(_edit,,'J_NIP1',,,20,,,'NIP kontrahenta',,,,'F3_button=1');
   _tab.win_efld(_edit,,'J_NIP2',,,20,,,'NIP kontrahenta zastąpionego',,,,'F3_button=1');
   _tab.win_efld(_edit,,'J_PP',,,3,,,'Powrotne przemieszczenie',,,'check-box',,"2","1","-1");
   _tab.efld_opt(_edit,'mark=1',_tab,'J_KRAJ');
   _tab.efld_opt(_edit,'mark=1',_tab,'J_NIP1')
|| _tab.win_esep(_edit,'Pozycja');
   _tab.win_efld(_edit,,'LP',,,20,,,'Kod');
   _tab.win_esep(_edit,'Było');
   _tab.win_efld(_edit,,'B_KRAJ',,,20,,,'Kod kraju',,,,'F3_button=1');
   _tab.win_efld(_edit,,'B_NIP1',,,20,,,'NIP kontrahenta',,,,'F3_button=1');
   _tab.win_efld(_edit,,'B_NIP2',,,20,,,'NIP kontrahenta zastąpionego',,,,'F3_button=1');
   _tab.win_efld(_edit,,'B_PP',,,3,,,'Powrotne przemieszczenie',,,'check-box',,"2","1","-1");
   _tab.win_esep(_edit,'Jest');
   _tab.win_efld(_edit,,'J_KRAJ',,,20,,,'Kod kraju',,,,'F3_button=1');
   _tab.win_efld(_edit,,'J_NIP1',,,20,,,'NIP kontrahenta',,,,'F3_button=1');
   _tab.win_efld(_edit,,'J_NIP2',,,20,,,'NIP kontrahenta zastąpionego',,,,'F3_button=1');
   _tab.win_efld(_edit,,'J_PP',,,3,,,'Powrotne przemieszczenie',,,'check-box',,"2","1","-1")
?};
_btnok:=_tab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['OK'@],"'key:F2'");
_btnan:=_tab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
_tab.btn_eopt(_edit,_btnok,'tooltip='+exec('help_red_ok','#window','P'));
_tab.btn_eopt(_edit,_btnan,'tooltip='+exec('help_red_esc','#window','A'));
_tab.win_edit(_edit);
_tab.fld_fml('LP','BEFORE_EDIT',"0");
_tab.fld_fml('J_KRAJ','F3',"exec('vat_pod_fml','fks_ved','F3')");
_tab.fld_fml('J_NIP1','F3',"exec('vat_pod_fml','fks_ved','F3')");
_tab.fld_fml('J_NIP2','F3',"exec('vat_pod_fml','fks_ved','F3')");
_tab.fld_fml('J_NIP2','AFTER_EDIT',"exec('vat_pod_fml','fks_ved','AE')");
_tab.fld_fml('J_PP','BEFORE_EDIT',"exec('vat_pod_fml','fks_ved','BE')");
_tab.fld_fml('J_PP','AFTER_EDIT',"exec('vat_pod_fml','fks_ved','AE')");
{? VAT_DEK.TYP='PODK'
|| _tab.fld_fml('B_KRAJ','F3',"exec('vat_pod_fml','fks_ved','F3')");
   _tab.fld_fml('B_NIP1','F3',"exec('vat_pod_fml','fks_ved','F3')");
   _tab.fld_fml('B_NIP2','F3',"exec('vat_pod_fml','fks_ved','F3')");
   _tab.fld_fml('B_NIP2','AFTER_EDIT',"exec('vat_pod_fml','fks_ved','AE')");
   _tab.fld_fml('B_PP','BEFORE_EDIT',"exec('vat_pod_fml','fks_ved','BE')");
   _tab.fld_fml('B_PP','AFTER_EDIT',"exec('vat_pod_fml','fks_ved','AE')")
?};
_obj:=exec('vat_pod_obj','fks_ved');
{? VAT_DEK.TYP='PODK'
|| _tab.LP:=VAT_POZ.LP-2;
   _bj:=VAT_POZ.LP+1;
   {? _bj='B'
   || _obj.set(VAT_POZ.OP,_tab,'B');
      {? VAT_POZ.next()
      || _obj.set(VAT_POZ.OP,_tab,'J');
         VAT_POZ.prev()
      ?}
   || _obj.set(VAT_POZ.OP,_tab,'J');
      {? VAT_POZ.prev()
      || _obj.set(VAT_POZ.OP,_tab,'B');
         VAT_POZ.next()
      ?}
   ?}
|| _tab.LP:=VAT_POZ.LP;
   _obj.set(VAT_POZ.OP,_tab)
?};
_tab.efld_opt(_tab.win_edit('?'),'enable='+$(_tab.J_NIP2=''),_tab,'J_PP');
_tab.efld_opt(_tab.win_edit('?'),'enable='+$(_tab.B_NIP2=''),_tab,'B_PP');
_tab


\vat_pod_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Edycja pozycji deklaracji VAT-UE dla magazynów CALL-OF STOCK
::  OLD: \vat_pod_edit/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('vat_pod_tab','fks_ved');
{? _tab.edit("exec('vat_pod_chk','fks_ved')")
|| _obj:=exec('vat_pod_obj','fks_ved');
   {? VAT_DEK.TYP='PODK'
   || _bj:=VAT_POZ.LP+1;
      {? _bj='B'
      || {? VAT_POZ.next()
         || VAT_POZ.OP:=_obj.get(_tab,'J');
            VAT_POZ.put();
            VAT_POZ.prev();
            VAT_POZ.OP:=_obj.get(_tab,'B')
         ?}
      || {? VAT_POZ.prev()
         || VAT_POZ.OP:=_obj.get(_tab,'B');
            VAT_POZ.put();
            VAT_POZ.next();
            VAT_POZ.OP:=_obj.get(_tab,'J')
         ?}
      ?}
   || VAT_POZ.OP:=_obj.get(_tab)
   ?};
   1
?}


\vat_pod_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wyświetl dla pozycje deklatacji VAT-UE
::  OLD: \vat_pod_view/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAT_POZ.TYP='C'
|| _tab:=exec('vat_pod_tab','fks_ved');
   _tab.display()
|| VAT_POZ.display()
?}


\vat_pod_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Sprawdzenie pozycji deklaracji VAT-UE dla magazynów CALL-OF STOCK
::   WE: [_a] - tabela, domyślnie bieżąca
::       [_b] - pokazywać komunikaty?
::  OLD: \vat_pod_chk/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:={? var_pres('_a')>0 || _a || cur_tab() ?};
_kom:={? var_pres('_b')>0 || _b || 1 ?};
_ret:='';
_dek:=VAT_DEK.TYP='POD';
{? ~_dek
|| _ret:=exec('vat_pod_chk1','fks_ved',_tab,_kom,'B')
?};
{? _ret=''
|| _ret:=exec('vat_pod_chk1','fks_ved',_tab,_kom,'J')
?};
{? _ret=''
|| {? _dek
   ||
      _obj:=exec('vat_pod_obj','fks_ved');
      _txt:=_obj.get(_tab);
      _ref:=VAT_POZ.ref();
      VAT_POZ.cntx_psh();
      VAT_POZ.prefix();
      VAT_POZ.blank(1);
      VAT_POZ.VAT_DEK:=VAT_DEK.ref();
      VAT_POZ.OP:=_txt;
      {? VAT_POZ.find_rec() & (-menu_txt()<>'popraw' | VAT_POZ.ref()<>_ref)
      || {? _kom
         || FUN.info('Istnieje już takie przemieszczenie towaru.'@)
         ?};
         _ret:='J_KRAJ'
      ?};
      VAT_POZ.cntx_pop()
   |? _tab.B_KRAJ='' & _tab.B_NIP1='' & _tab.B_NIP2='' & _tab.B_PP=-1 &
      _tab.J_KRAJ='' & _tab.J_NIP1='' & _tab.J_NIP2='' & _tab.J_PP=-1
   || {? _kom
      || FUN.info('Nie wypełniono żadnego pola.'@)
      ?};
      _ret:='B_KRAJ'
   |? _tab.B_KRAJ=_tab.J_KRAJ & _tab.B_NIP1=_tab.J_NIP1 &
      _tab.B_NIP2=_tab.J_NIP2 & _tab.B_PP=_tab.J_PP
   || {? _kom
      || FUN.info('Wartości "było" takie same jak "jest".'@)
      ?};
      _ret:='B_KRAJ'
   ?}
?};
_ret


\vat_pod_chk1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Sprawdzenie pozycji deklaracji VAT-UE dla magazynów CALL-OF STOCK
::   WE: _a - tabela, domyślnie bieżąca
::       _b - pokazywać komunikaty?
::       _c - 'B'-było 'J'-jest
::  OLD: \vat_pod_chk1/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_kom:=_b;
_bj:=_c;
_ret:='';
_dek:=VAT_DEK.TYP='POD' |
      _bj='J' & (_tab.J_KRAJ<>'' | _tab.J_NIP1<>'' | _tab.J_NIP2<>'' | _tab.J_PP<>-1) |
      _bj='B' & (_tab.B_KRAJ<>'' | _tab.B_NIP1<>'' | _tab.B_NIP2<>'' | _tab.B_PP<>-1);
{? _dek
|| _ret:={? _kom
         || __CHK.record(_tab,,_bj+'_KRAJ')
         |? _bj='J'
         || {? _tab.J_KRAJ='' || 'J_KRAJ' || '' ?}
         || {? _tab.B_KRAJ='' || 'B_KRAJ' || '' ?}
         ?}
?};
{? _ret=''
|| _kraj:={? _bj='J' || _tab.J_KRAJ || _tab.B_KRAJ ?};
   {? _kraj<>''
   || SLU.cntx_psh();
      SLU.index('NAZ'); SLU.prefix('~KRAJE UE',);
      {? SLU.first()
      || SLO.cntx_psh();
         SLO.index('SL'); SLO.prefix(SLU.ref,_kraj,);
         {? ~SLO.first() | SLO.KOD='PL'
         || {? _kom || FUN.info('Niewłaściwy kod kraju UE.'@) ?};
            _ret:=_bj+'_KRAJ'
         ?};
         SLO.cntx_pop()
      ?};
      SLU.cntx_pop()
   ?}
?};
{? _ret='' & _dek
|| _ret:={? _kom
         || __CHK.record(_tab,,_bj+'_NIP1')
         |? _bj='J'
         || {? _tab.J_NIP1='' || 'J_NIP1' || '' ?}
         || {? _tab.B_NIP1='' || 'B_NIP1' || '' ?}
         ?}
?};
_nip2:={? _bj='J' || _tab.J_NIP2 || _tab.B_NIP2 ?};
_pp:={? _bj='J' || _tab.J_PP || _tab.B_PP ?};
{? _ret='' & _nip2<>'' & _pp<>-1
|| {? _kom || FUN.info('Należy uzupełnić NIP kontrahenta zastąpionego lub powrotne przemieszczenie.'@) ?};
   _ret:=_bj+'_NIP2'
?};
_ret


\vat_pod_fml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla nipu w informacji dla magazynów CALL-OF STOCK
::  OLD: \vat_pod_fml/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_akc:=_a;
_bj:=1+cur_afld();
_fld:=2-cur_afld();
{? _akc='F3'
|| {? _fld='KRAJ'
   || _ret:=~~;
      _tab:=sql(
         'select KOD, TR from SLO join SLU where SLU.NAZ=\'~KRAJE UE\' and SLO.KOD!=\'PL\' order by 1'
      );
      _sel:=_tab.mk_sel('Kod kraju','P',,'vatpozue',,,,,'U');
      _tab.win_fld(_sel,,'KOD',,,,,,'Kod');
      _tab.win_fld(_sel,,'TR',,,60,,,'Nazwa');
      _tab.win_act(_sel,,'Formuła','Wybierz',,,"sel_exit()",,1);
      _tab.win_sel(_sel);
      {? fld()='' | ~_tab.find_key(fld()) || _tab.first() ?};
      {? _tab.select(,1,5)
      || _ret:=_tab.KOD
      ?};
      _ret
   || _kraj:={? _bj='J' || cur_tab().J_KRAJ || cur_tab().B_KRAJ ?};
      _tab:=sql(
         'select KH.SKR, KH.NAZ, SLO.KOD, NIPY.NIP, NIPY.SNIP '+
         'from NIPY join SLO using(NIPY.KRAJ,SLO.REFERENCE) join KH using(NIPY.KH,KH.REFERENCE)'+
         {? _kraj<>'' || ' where SLO.KOD=\''+_kraj+'\'' || '' ?}+
         'order by 1,3,4'
      );
      _i2:=_tab.ndx_tmp('',1,'NIP',,0, 'KOD',,0);
      _sel:=_tab.mk_sel('Nipy','P',,'nipysel2',,,,,'U');
      _tab.win_fld(_sel,,'SKR',,,,,,'Kontrahent');
      _tab.win_fld(_sel,,'NAZ',,,50,,,,1);
      _tab.win_fld(_sel,,'KOD',,,3,,,'Kod kraju');
      _tab.win_fld(_sel,,'NIP',,,,,,'Numer');
      _tab.win_act(_sel,,'Formuła','Wybierz',,,"sel_exit()",,1);
      _tab.win_sel(_sel);
      _tab.cntx_psh();
      _tab.index(_i2); _tab.prefix(fld(),_kraj);
      _ref:={? _tab.first() || _tab.ref() || null ?};
      _tab.cntx_pop();
      {? _ref || _tab.seek(_ref) ?};
      {? _tab.select(,1,5)
      || _tab.NIP
      || ~~
      ?}
   ?}
|? _akc='AE'
|| _tab:=cur_tab();
   {? _fld='NIP2'
   || {? fld()<>''
      || {? _bj='J' || _tab.J_PP:=-1 || _tab.B_PP:=-1 ?}
      |? _bj='J' & _tab.J_PP=-1 | _bj='B' & _tab.B_PP=-1
      || {? _bj='J' || _tab.J_PP:=1 || _tab.B_PP:=1 ?}
      ?};
      _enabled:=_bj='J' & _tab.J_NIP2='' | _bj='B' & _tab.B_NIP2='';
      _tab.efld_opt(_tab.win_edit('?'),'enable='+$_enabled,_tab,_bj+'_PP');
      1
   |? _fld='PP'
   || {? VAT_DEK.TYP='POD'
      || {? _bj='J' & _tab.J_NIP2<>'' | _bj='B' & _tab.B_NIP2<>''
         || {? _bj='J' || _tab.J_PP:=-1 || _tab.B_PP:=-1 ?}
         |? _bj='J' & _tab.J_PP=-1 | _bj='B' & _tab.B_PP=-1
         || {? _bj='J' || _tab.J_PP:=1 || _tab.B_PP:=1 ?};
            win_disp()
         ?}
      ?};
      1
   ?}
|? _akc='BE'
|| _tab:=cur_tab();
   {? _fld='PP'
   || _bj='J' & _tab.J_NIP2='' | _bj='B' & _tab.B_NIP2=''
   ?}
?}


\gen_pod_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Formuła tworząca pozycje deklaracji VAT-UE dla magazynów CALL-OF STOCK
::  OLD: \gen_pod_f/vat.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('vat_pod_tab','fks_ved',0);

exec('call_of_stock','%fks_ved',_tab);

{? _tab.first()
|| _obj:=exec('vat_pod_obj','fks_ved');
   _nr:=0;
   _kor:=VAT_DEK.TYP='PODK';
   {? _kor
   || _dop:='000';
      _sp:=' '
   || _dop:='   ';
      _sp:=''
   ?};
   {!
   |? {? exec('vat_pod_chk','fks_ved',_tab,0)=''
      || _nr+=1;
         {! _jj:=1..2
         |? _kor | _jj=1
         |! _bj:={? _kor || {? _jj=1 || 'B' || 'J' ?} || ~~ ?};
            VAT_POZ.blank();
            VAT_POZ.VAT_DEK:=VAT_DEK.ref();
            VAT_POZ.LP:='VAT-UE F.'+_sp+((_dop+$_nr)+3) + {? var_pres('_bj')>0 || ' '+_bj || '' ?};
            VAT_POZ.OP:=_obj.get(_tab,_bj);
            VAT_POZ.TYP:='C';
            VAT_POZ.add()
         !}
      ?};
      _tab.next()
   !}
?}


\pod_f_petla
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: _a - 1-początek pętli, 0-koniec pętki, 2-następny element
::  OLD: \pod_f_petla/vat.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| VAT_POZ.cntx_psh();
   VAT_POZ.index('VAT_POZ');
   VAT_POZ.prefix(VAT_DEK.ref(),'VAT-UE'+{? VAT_DEK.TYP='PODK' || 'K' || '' ?}+' F.');
   {? VAT_POZ.first()
   || VAR_DEL.delete('PodObj');
      PodObj:=exec('vat_pod_obj','fks_ved');
      PodObj.set(VAT_POZ.OP);
      1
   ?}
|? _a=0
|| VAR_DEL.delete('PodObj');
   VAT_POZ.cntx_pop()
|| {? VAT_POZ.next()
   || PodObj.set(VAT_POZ.OP);
      1
   ?}
?}


\del_dok_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Formuła usuwająca powiązane rekordy tabeli DOK_JPK dla kontekstu VAT_DEK
::----------------------------------------------------------------------------------------------------------------------
DOK_JPK.cntx_psh();
_maski:=DOK_JPK.names();
_vatdek:=$VAT_DEK.ref();
{? _maski.first()
|| {! |?
      DOK_JPK.use(_maski.NAME);
      DOK_JPK.index('VAT_DEK');
      DOK_JPK.prefix(_vatdek);
      {? DOK_JPK.first()
      || {! |?
            _date:=DOK_JPK.DATE;
            _time:=DOK_JPK.TIME;
            DOK_JPK.cntx_psh();
            DOK_JPK.index('TIME');
            DOK_JPK.prefix(DOK_JPK.DOK,DOK_JPK.DATE,DOK_JPK.TIME);
            {? DOK_JPK.size()>1
            || _act:=1
            || DOK_JPK.index('UNIK');
               DOK_JPK.prefix(DOK_JPK.DOK,'',);
               {? DOK_JPK.first()
               || _act:=1
               || DOK_JPK.index('TIME');
                  DOK_JPK.prefix(DOK_JPK.DOK);
                  {? DOK_JPK.last()
                  || {? _date=DOK_JPK.DATE & _time=DOK_JPK.TIME
                     || _act:=2
                     || _act:=1
                     ?}
                  ?}
               ?}
            ?};
            DOK_JPK.cntx_pop();
            {? _act=1
            || DOK_JPK.del()
            |? _act=2
            || DOK_JPK.cntx_psh();
               DOK_JPK.prefix();
               DOK_JPK.VAT_DEK:='';
               DOK_JPK.put();
               DOK_JPK.cntx_pop()
            ?};
            DOK_JPK.first()
         !}
      ?};
      _maski.next()
   !}
?};
DOK_JPK.cntx_pop();
1


\pvat_towar_usluga
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Zwraca informację, czy bieżąca pozycja VAT (PVAT) dotyczy usługi czy towaru
::   WY: U - usługa, T - towar
::----------------------------------------------------------------------------------------------------------------------
_typ:=Plugin.run('FKS_VIUDO_TU');
{? var_pres('_typ')<=0 | type_of(_typ)<>type_of('') | +_typ<>1 | 'TU'*_typ=0
|| SLO.cntx_psh();
   _ret:={? PVAT.GRVAT().KOD='SprPKWSU' || 'U' || 'T' ?};
   SLO.cntx_pop()
|| _ret:=_typ
?};
_ret


\pvat_typ_stawki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Zwraca typ stawki VAT pozycji VAT (PVAT)
::   WY: S - podstawowa, R - zredukowana, '' - inna
::----------------------------------------------------------------------------------------------------------------------
SLO.cntx_psh();
_desc:=PVAT.STVAT().TR;
SLO.cntx_pop();
{? -_desc='podstawowa'
|| 'S'
|| 'R'
?}


\unpar_p1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Formuły dla pola UNPAR.P1 okna z ustawienie okresu deklaracji
::   WE: _a - typ formuły
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
{? _typ='BV'
|| _enabled:=SIK.ROK2=null | SIK.ROK2().POCZ_ROK<date(2023,1,1);
   SIK.efld_opt('CIT8','enable='+$_enabled,UNPAR,'P1');
   ~~
?}

:Sign Version 2.0 jowisz:1045 2024/02/20 12:44:28 1b3a7723e6f62f567230c97d04a04bfb4c6fc7c5599ccc87c2149c7d53c8c920417b4e3ad294b021ad8d6bb3c154d403ccc4e927f442dcc3fc3dd20da981cc4d588c75894bab286fb048da1be774b249c3f018b5fa4b5307b9bcd24db553aadfe3262bbe3ed17e719b534c2e8119f06a0ddf4ce0886c38d75e054deb703b2855

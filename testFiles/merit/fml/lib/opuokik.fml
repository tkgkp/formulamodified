:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: opuokik.fml [12.51]
:: Utworzony: 09.02.2021
:: Autor: MB
:: Systemy: FIKS
::======================================================================================================================
:: Zawartosc: Formuly uzywane w sprawozdaniu o stosowanych terminach zapłaty w transakcjach handlowych (UOKiK)
::======================================================================================================================


\start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Główna formuła uruchamiająca analizę
::   WE: [_a] - formuła warunkowa na rozrachunek
::       [_b] - formuła warunkowa na buforze tabeli analiz, przed dodaniem rekordu do analizy.
::              Gdy zwraca 1-rekord jest dodawany, 0-rekord nie jest dodawany
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('ParObj');
ParObj:=obj_new('SYNT','SA','VAT','FML1','FML2','PAR37');
ParObj.FML1:={? var_pres('_a')>0
             || {? var_pres('_a')=type_of('')
                || $_a
                || _a
                ?}
             || ~~
             ?};
ParObj.FML2:={? var_pres('_b')>0
             || {? var_pres('_b')=type_of('')
                || $_b
                || _b
                ?}
             || ~~
             ?};
ParObj.PAR37:=PAR_SKID.get(37);
{? exec('par','opuokik')
|| _tab:=exec('create_tab','opuokik');
   exec('raty_obj','opuokik');
   exec('zap_obj','opuokik');
   exec('fill_tab','opuokik',_tab);
   _tab.select();
   VAR_DEL.delete('RatyObj')
?};
VAR_DEL.delete('ParObj','ZapisyObj');
0


\par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Edycja parametrów analizy
::----------------------------------------------------------------------------------------------------------------------
_edit:=PAR_WYDR.mk_edit('Sprawozdanie o stosowanych terminach zapłaty (UOKiK)');
PAR_WYDR.win_efld(_edit,UNPAR,'PRF1','NAZ','NAZWA',50,,,'Rok');
PAR_WYDR.win_efld(_edit,POMOC,'K1',,,,,,'Typ zestawu kont',,,'radio-buttons',,'Syntetycznie',"'S'",'Analitycznie',"'A'");
PAR_WYDR.win_efld(_edit,,'ZESTKONP','KOD','UNIK',50,,,'Zestaw kont');
PAR_WYDR.efld_opt(_edit,'mark=1',UNPAR,'PRF1');
_ok:=PAR_WYDR.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['OK'@],"'key:F2'");
_anuluj:=PAR_WYDR.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
PAR_WYDR.btn_eopt(_edit,_ok,'tooltip='+exec('help_red_ok','#window','P'));
PAR_WYDR.btn_eopt(_edit,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
PAR_WYDR.win_edit(_edit);
POMOC.K1:=SA:='S';
PAR_WYDR.ZESTKONP:=exec('bl_konwy','dok_fks',POMOC.K1);
UNPAR.PRF1:=SSTALE.AR;
POMOC.fld_fml('K1','AFTER_EDIT',"SA:=POMOC.K1;1");
_edit:=0;
{? PAR_WYDR.edit("exec('ar_par','opuokik')")
|| PAR_WYDR.DATA_OD:=UNPAR.PRF1().POCZ_ROK;
   PAR_WYDR.DATA_DO:=ROK_F.KON_ROK;
   ParObj.SYNT:=ROK_F.SYNT;
   ParObj.SA:=PAR_WYDR.ZESTKONP().SA;
   _edit:=1
?};
POMOC.fld_fml('K1','AFTER_EDIT',"*");
VAR_DEL.delete('SA');
_edit


\ar_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji parametrów analizy
::----------------------------------------------------------------------------------------------------------------------
__CHK.record3(UNPAR,'PRF1','Rok')


\create_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzy tabelę na potrzeby analizy
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(2,
   'DZ','DATE','Data zakupu',
   'TP','DATE','Termin płatności',
   'DW','DATE','Data wpływu',
   'KH_NIP','STRING[20]','NIP',
   'KH_NAZ','STRING[150]','Nazwa',
   'KH_STAT','INTEGER','Status',
   'SYM','STRING[35]','Symbol',
   'NW','REAL','Należność w walucie',
   'WAL','STRING[3]','Waluta',
   'NP','REAL','Należność w zł',
   'TP_DNI','INTEGER','Termin płatności (dni)',
   'PL','INTEGER','Wykonano płatność',
   'S_PL','STRING[40]','Sposób płatności',
   'NRB','STRING[50]','Rachunek banowy',
   'OP_TRAN','STRING[100]','Opis transakcji',
   'TZ','DATE','Termin zapłaty',
   'ZC','REAL','Zapłata częściowa',
   'ZZ','REAL','Zapłata zbiorcza',
   'ZW','REAL','Zapłata w walucie',
   'WAL_Z','STRING[3]','Waluta zapłaty',
   'DOD','STRING[255]','Dodatkowe informacje',
   'REF','INTEGER','Ref rozrachunku',
   'LP','INTEGER','LP.'
);
_win:=_tab.mk_sel('Sprawozdanie o stosowanych terminach zapłaty w transakcjach handlowych (UOKiK)','P',,'#sostzwthuokik',,,,,'U');
_tab.win_fld(_win,,'LP',,,5,,,'Lp.');
_tab.win_fld(_win,,'KH_NIP',,,10,,,'NIP');
_tab.win_fld(_win,,'KH_NAZ',,,10,,,'Nazwa');
_tab.win_fld(_win,,'KH_STAT',,,-3,,,'Duży kontrahent',,,2,,"2","0");
_tab.win_fld(_win,,'SYM',,,10,,,'Symbol');
_tab.win_fld(_win,,'DZ',,,-10,,,'Data zakupu');
_tab.win_fld(_win,,'DW',,,-10,,,'Data wpływu');
_tab.win_fld(_win,,'NW',,,10,2,,'Brutto (wal)');
_tab.win_fld(_win,,'WAL',,,-4,,,'Waluta');
_tab.win_fld(_win,,'NP',,,10,2,,'Brutto (PLN)');
_tab.win_fld(_win,,'TP_DNI',,,-7,,,'Płatność (dni)');
_tab.win_fld(_win,,'TP',,,-10,,,'Termin płatności');
_tab.win_fld(_win,,'PL',,,-3,,,'Zapłacono',,,2,,"1","0");
_tab.win_fld(_win,,'S_PL',,,-10,,,'Sposób płatności');
_tab.win_fld(_win,,'NRB',,,-10,,,'Rachunek bankowy');
_tab.win_fld(_win,,'OP_TRAN',,,-10,,,'Opis transakcji');
_tab.win_fld(_win,,'TZ',,,-10,,,'Termin zapłaty');
_tab.win_fld(_win,,'ZC',,,-10,2,,'Zapłata częściowa');
_tab.win_fld(_win,,'ZZ',,,-10,2,,'Zapłata zbiorcza');
_tab.win_fld(_win,,'ZW',,,-10,2,,'Zapłata w walucie');
_tab.win_fld(_win,,'WAL_Z',,,-4,,,'Waluta');
_tab.win_fld(_win,,'DOD',,,-10,,,'Dodatkowe informacje');

_tab.win_act(_win,,'Formuła','Dołącz',,,"exec('akc_mod','opuokik')");
_tab.win_act(_win,1,'Formuła','Dołącz',,,"exec('akc_mod','opuokik')");
_tab.win_act(_win,,'Formuła','Popraw',,,"exec('akc_mod','opuokik')");
_tab.win_act(_win,,'Usuń',,,,,"exec('akc_del','opuokik')",,1,,"exec('akc_del','opuokik')");
_tab.win_act(_win,,'Formuła','Pokaż &rozrachunek',,,"exec('akc_show','opuokik')");
_tab.win_act(_win,,'Formuła','Zapisz',,,"exec('akc_save','opuokik')");
_tab.win_act(_win,,'Kolejność');
_tab.win_act(_win,,'Formuła','Legenda',,,"exec('legenda','color','OPTERMPL#01#01')");
_tab.win_act(_win,,'Rekord',,,,"exec('record','opuokik',1)","exec('record','opuokik',2)");
_tab.win_act(_win,1,'Rekord',,,,,"exec('record','opuokik',2)");
_tab.win_act(_win,,'Wyświetl',,,,,"exec('akc_disp','opuokik')");

_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Dołącz'@],'menu:D');
_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Popraw'@],'menu:P');
_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Usuń'@],'menu:U');
_tab.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Pokaż &rozrachunek'@],'menu:R');
_tab.win_sel(_win);

_edit:=_tab.mk_edit('Pozycja',,'#sostzwthe');
_tab.win_efld(_edit,,'LP',,,5,,,'Lp.');
_tab.win_efld(_edit,,'KH_NIP',,,97,,,'NIP',,,,'F3_button=1');
_tab.win_efld(_edit,,'KH_NAZ',,,100,,,'Nazwa');
_tab.win_efld(_edit,,'KH_STAT',,,,,,'Duży kontrahent',,,'check-box',,"2","0");
_tab.win_efld(_edit,,'SYM',,,100,,,'Symbol');
_tab.win_efld(_edit,,'DZ',,,13,,,'Data zakupu');
_tab.win_efld(_edit,,'DW',,,13,,,'Data wpływu');
_tab.win_efld(_edit,,'NW',,,16,2,,'Brutto (wal)');
_tab.win_efld(_edit,,'WAL',,,13,,,'Waluta',,,,'F3_button=1');
_tab.win_efld(_edit,,'NP',,,16,2,,'Brutto (PLN)');
_tab.win_efld(_edit,,'TP_DNI',,,16,,,'Płatności (dni)');
_tab.win_efld(_edit,,'TP',,,13,,,'Termin płatności');
_tab.win_efld(_edit,,'PL',,,,,,'Zapłacono',,,'check-box',,"1","0");
_tab.win_efld(_edit,,'S_PL',,,100,,,'Sposób płatności');
_tab.win_efld(_edit,,'NRB',,,97,,,'Rachunek bankowy',,,,'F3_button=1');
_tab.win_efld(_edit,,'OP_TRAN',,,100,,,'Opis transakcji');
_tab.win_efld(_edit,,'TZ',,,13,,,'Termin zapłaty');
_tab.win_efld(_edit,,'ZC',,,16,2,,'Zapłata częściowa');
_tab.win_efld(_edit,,'ZZ',,,16,2,,'Zapłata zbiorcza');
_tab.win_efld(_edit,,'ZW',,,16,2,,'Zapłata (wal)');
_tab.win_efld(_edit,,'WAL_Z',,,13,,,'Waluta',,,,'F3_button=1');
_tab.win_efld(_edit,,'DOD',,,100,,,'Dodatkowe informacje');
_ok:=_tab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['Zapisz'@],"'key:F2'");
_anuluj:=_tab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
_tab.btn_eopt(_edit,_ok,'tooltip='+exec('help_red_ok','#window','Z'));
_tab.btn_eopt(_edit,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
_tab.win_edit(_edit);
exec('mark','opuokik',_tab);

_tab.fld_fml('LP','BEFORE_EDIT',"0");
_tab.fld_fml('TP_DNI','BEFORE_EDIT',"0");
_tab.fld_fml('KH_NIP','F3',"exec('f3_kh','opuokik')");
_tab.fld_fml('WAL','F3',"exec('f3_wal','opuokik')");
_tab.fld_fml('NRB','F3',"exec('f3_nrb','opuokik')");
_tab.fld_fml('WAL_Z','F3',"exec('f3_wal','opuokik')");
_tab.fld_fml('DZ','AFTER_EDIT',"exec('ae_data','opuokik')");
_tab.fld_fml('TP','AFTER_EDIT',"exec('ae_data','opuokik')");
_tab.fld_fml('PL','AFTER_EDIT',"exec('ae_pl','opuokik')");
_tab.fld_fml('PL','BEFORE_DISPLAY',"exec('mark','opuokik')");
_tab.fld_fml('TZ','BEFORE_DISPLAY',"exec('bd_null','opuokik')");
_tab.fld_fml('DW','BEFORE_DISPLAY',"exec('bd_null','opuokik')");
_tab.fld_fml('ZC','BEFORE_DISPLAY',"exec('bd_null','opuokik')");
_tab.fld_fml('ZZ','BEFORE_DISPLAY',"exec('bd_null','opuokik')");
_tab.fld_fml('ZW','BEFORE_DISPLAY',"exec('bd_null','opuokik')");
_tab


\fill_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Uzupełnia tabelę analizy
::   WE: _a - tabela analizy
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_wal:=exec('wal_tab','optermpl');
_pomin:=exec('pomin_obj','optermpl');
_maska:=UNPAR.PRF1().KOD;
OP.cntx_psh();
OP.use('operac'+_maska);
OP.index('PRZEG'); OP.prefix();
PROGRESS.set(OP.size(),'Analiza rozrachunków ...');
ZAP_OP.cntx_psh();
ZAP_OP.use('rozzap'+_maska);
D_RB.cntx_psh();
D_RB.use('dr__fk'+_maska);
SLO.cntx_psh();
SLO.prefix();
VAR_DEL.delete('PwInd');
PwInd:=PW.ndx_tmp(,1,'REJ',,,'NRD',,,{? var_pres('SYM_ZEW',PW)>0 || 'SYM_ZEW' || 'SYM' ?},,,'AN',,,'ODD',,);
{? _wal.first()
|| {!
   |? {? SLO.seek(BIT.sqlint(_wal.REF),)
      || OP.prefix(SLO.ref());
         {? OP.first()
         || {!
            |? OP.prefix(SLO.ref(),OP.AN);
               {? OP.first()
               || {? PAR_WYDR.ZESTKONP=null | exec('in_zest','optermpl')
                  || {!
                     |? {? OP.TYP='ZOB' & _pomin.analize() & exec('analize','opuokik',_tab,1)
                        || exec('fill_op','opuokik',_tab)
                        ?};
                        PROGRESS.next();
                        OP.next()
                     !}
                  || OP.last()
                  ?}
               ?};
               OP.prefix(SLO.ref());
               OP.next()
            !}
         ?}
      ?};
      _wal.next()
   !}
?};
SLO.cntx_pop();
D_RB.cntx_pop();
ZAP_OP.cntx_pop();
OP.cntx_pop();
PW.ndx_drop(PwInd);
VAR_DEL.delete('PwInd');
_tab.cntx_psh();
::_tab.last();
::{! _i:=1..1000000
::|! _tab.add()
::!};
{? _tab.first()
|| _lp:=0;
   {!
   |? _tab.LP:=(_lp+=1);
      _tab.put();
      _tab.next()
   !}
?};
_tab.cntx_pop();
PROGRESS.close()


\fill_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Uzupełnia dane analizy dla bieżącego rozrachunku
::   WE: _a - tabela analizy
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_tab.blank(1);
_tab.REF:=#OP.ref();
_tab.SYM:={? var_pres('SYM_ZEW',OP)>0 || OP.SYM_ZEW || OP.SYM ?};
_tab.TP:=exec('dzienRob','kalendarz',OP.TZ,1,1,1);
_tab.KH_NIP:=OP.KH().SNIP;
{? _tab.KH_NIP=''
|| _tab.KH_NIP:='brak'
?};
_tab.KH_NAZ:=KH.NAZ_P;
_tab.KH_STAT:={? var_pres('WIELKOSC',KH)>0 & KH.WIELKOSC='duży' || 2 || 0 ?};
_tab.WAL:=OP.WAL().KOD;

_fak:=0;
_wal:=OP.WAL<>FINFO.NAROD;

_zo:=ZapisyObj;
_zo.fill();
_zap_op:=_zo.ZAP_OP;
_zap_op.prefix(1);
{? _zap_op.first()
|| {!
   |? {? _fak=0
      || _tab.DZ:=_zap_op.DTO;
         {? _zap_op.DTO<>_zap_op.D4
         || _tab.DW:=_zap_op.D4
         ?};
         {? _zap_op.KOR='T'
         || _tab.DOD:=_zap_op.SYM_KOR
         ?}
      ?};
      _fak:=1;
      _ma:=_zap_op.SM;
      _ma2:=_zap_op.SM2;
      {? _ma=0
      || _ma:=-_zap_op.SW;
         _ma2:=-_zap_op.SW2
      ?};
      _tab.NW+=_ma;
      {? _wal
      || _tab.NP+=_ma2
      || _tab.NP+=_ma
      ?};
      _zap_op.next()
   !}
?};
{? _fak
|| _raty:=RatyObj;
   {? _raty.fill()
   || _op:=_raty.OP;
      _zap:=_raty.ZAP_OP;
      {? _op.first()
      || {!
         |? _tab.TP:=_op.TP;
            _tab.TP_DNI:=_tab.TP-_tab.DZ;
            _tab.NW:=_op.RATA;
            _tab.NP:=_op.RATA;
            _is_zap:=0;
            _tab.cntx_psh();
            _zap.prefix(_op.ref());
            {? _zap.first()
            || {!
               |? POZ.cntx_psh();
                  POZ.use(8+_zap.POZDOK);
                  POZ.prefix();
                  {? POZ.seek(BIT.sqlint(_zap.POZDOK),)
                  || DOK.cntx_psh();
                     DOK.use('doku'+(POZ.name()+4));
                     DOK.prefix();
                     _vat:=POZ.DOK().RVAT<>null;
                     {? ~_vat
                     || _is_zap:=1;
                        _tab.PL:=1;
                        _tab.ZC:=_zap.ZAP;
                        _tab.ZW:=_zap.ZAP;
                        _tab.WAL_Z:=OP.WAL().KOD;
                        _tab.TZ:=DOK.DTO;
                        {? 2+DOK.DOKZRODL='pw'
                        || PW.cntx_psh();
                           PW.use(6+DOK.DOKZRODL);
                           PW.index(PwInd);
                           _nrd:=$DOK.NR+'/'+$(DOK.DTW~2);
                           PW.prefix(DOK.REJ,_nrd,_tab.SYM,OP.AN,OP.ODD);
                           _jest:=PW.first();
                           {? ~_jest
                           || PW_OP.cntx_psh();
                              PW_OP.use('pwp'+(4+$_zap_op.DTW));
                              PW_OP.index('SYM'); PW_OP.prefix(OP.AN,OP.SYM,OP.ODD);
                              {? PW_OP.first()
                              || PW.cntx_psh();
                                 {!
                                 |? {? ref_name(PW_OP.PW)<>PW.name()
                                    || PW.use(ref_name(PW_OP.PW));
                                       PW.prefix()
                                    ?};
                                    _jest:=PW_OP.PW().NRD=_nrd & #PW.REJ=_zap_op.REJ;
                                    {? _jest
                                    || _ref:=PW_OP.PW;
                                       0
                                    || PW_OP.next()
                                    ?}
                                 !};
                                 PW.cntx_pop()
                              ?};
                              PW_OP.cntx_pop();
                              {? _jest
                              || {? ref_name(_ref)<>PW.name()
                                 || PW.use(ref_name(_ref))
                                 ?};
                                 PW.prefix();
                                 _jest:=PW.seek(_ref)
                              ?}
                           ?};
                           {? _jest
                           || _opis:={? PW.TYTOP<>'' || PW.TYTOP |? PW.TYPTRAN<>'' || PW.TYPTRAN || '' ?};
                              _tab.OP_TRAN:=_opis;
                              _tab.NRB:=exec('rbl','opuokik');
                              _tab.S_PL:='przelew bankowy';
                              _tab.ZW:={? PW.WAL='PLN' || PW.KW || PW.KW_WAL ?};
                              PW_OP.cntx_psh();
                              PW_OP.use('pwp'+(ref_name(PW.ref())+4));
                              PW_OP.index('PW'); PW_OP.prefix(PW.ref());
                              {? PW_OP.first() & PW_OP.next() | _tab.ZW<>_tab.ZC
                              || _tab.ZZ:=_tab.ZC
                              ?};
                              PW_OP.cntx_pop()
                           ?};
                           PW.cntx_pop()
                        |? 7+DOK.DOKZRODL='par_nag'
                        || PAR_NAG.cntx_psh();
                           PAR_NAG.use('parnag'+(2+(4-POZ.name())));
                           PAR_NAG.prefix();
                           {? PAR_NAG.seek(#(7-DOK.DOKZRODL),)
                           || _tab.S_PL:={? PAR_NAG.TYP='K' || 'kompensata'
                                         |? PAR_NAG.TYP='F' || 'faktoring'
                                         || 'sprzedaż wierzytelności'
                                         ?};
                              _tab.OP_TRAN:=PAR_NAG.PAR_UM().SYM
                           ?};
                           PAR_NAG.cntx_pop()
                        ?};
                        {? _tab.NW=_tab.ZC
                        || _tab.ZC:=0
                        ?};
                        {? exec('analize','opuokik',_tab,2)
                        || _tab.add()
                        ?}
                     ?};
                     DOK.cntx_pop()
                  ?};
                  POZ.cntx_pop();

                  _zap.next()
               !}
            ?};
            {? _is_zap=0 & exec('analize','opuokik',_tab,2)
            || _tab.add()
            ?};
            _tab.cntx_pop();
            _op.next()
         !}
      ?}
   || _tab.TP_DNI:=_tab.TP-_tab.DZ;
      _zap_op.prefix(0);
      {? _zap_op.first()
      || {!
         |? _wn:=_zap_op.SW;
            _wn2:=_zap_op.SW2;
            {? _wn=0
            || _wn:=-_zap_op.SM;
               _wn2:=-_zap_op.SM2
            ?};
            _tab.PL:=1;
            _tab.ZC:={? _wal || _wn2 || _wn ?};
            _tab.ZW:=_wn;
            _tab.WAL_Z:=OP.WAL().KOD;
            _tab.TZ:=_zap_op.DTO;
            {? 2+_zap_op.Z='pw'
            || PW.cntx_psh();
               PW.use(6+_zap_op.Z);
               PW.index(PwInd);
               _nrd:=$_zap_op.NR+'/'+$(_zap_op.DTW~2);
               PW.prefix(_zap_op.REJ,_nrd,_tab.SYM,OP.AN,OP.ODD);
               _jest:=PW.first();
               {? ~_jest
               || PW_OP.cntx_psh();
                  PW_OP.use('pwp'+(4+$_zap_op.DTW));
                  PW_OP.index('SYM'); PW_OP.prefix(OP.AN,OP.SYM,OP.ODD);
                  {? PW_OP.first()
                  || PW.cntx_psh();
                     {!
                     |? {? ref_name(PW_OP.PW)<>PW.name()
                        || PW.use(ref_name(PW_OP.PW));
                           PW.prefix()
                        ?};
                        _jest:=PW_OP.PW().NRD=_nrd & #PW.REJ=_zap_op.REJ;
                        {? _jest
                        || _ref:=PW_OP.PW;
                           0
                        || PW_OP.next()
                        ?}
                     !};
                     PW.cntx_pop()
                  ?};
                  PW_OP.cntx_pop();
                  {? _jest
                  || {? ref_name(_ref)<>PW.name()
                     || PW.use(ref_name(_ref))
                     ?};
                     PW.prefix();
                     _jest:=PW.seek(_ref)
                  ?}
               ?};
               {? _jest
               || _opis:={? PW.TYTOP<>'' || PW.TYTOP |? PW.TYPTRAN<>'' || PW.TYPTRAN || '' ?};
                  _tab.OP_TRAN:=_opis;
                  _tab.NRB:=exec('rbl','opuokik');
                  _tab.S_PL:='przelew bankowy';
                  PW_OP.cntx_psh();
                  PW_OP.use('pwp'+(ref_name(PW.ref())+4));
                  PW_OP.index('PW'); PW_OP.prefix(PW.ref());
                  {? PW_OP.first() & PW_OP.next()
                  || _tab.ZZ:=_tab.ZC
                  ?};
                  PW_OP.cntx_pop();
                  _tab.ZW:={? PW.WAL='PLN' || PW.KW || PW.KW_WAL ?}
               ?};
               PW.cntx_pop()
            |? 7+_zap_op.Z='par_nag'
            || PAR_NAG.cntx_psh();
               PAR_NAG.use('parnag'+(2+(4-POZ.name())));
               PAR_NAG.prefix();
               {? PAR_NAG.seek(#(7-_zap_op.Z),)
               || _tab.S_PL:={? PAR_NAG.TYP='K' || 'kompensata'
                             |? PAR_NAG.TYP='F' || 'faktoring'
                             || 'sprzedaż wierzytelności'
                             ?};
                  _tab.OP_TRAN:=PAR_NAG.PAR_UM().SYM
               ?};
               PAR_NAG.cntx_pop()
            ?};
            {? _tab.NW=_tab.ZC
            || _tab.ZC:=0
            ?};
            {? exec('analize','opuokik',_tab,2)
            || _tab.add()
            ?};
            _zap_op.next()
         !}
      |? exec('analize','opuokik',_tab,2)
      || _tab.add()
      ?}
   ?}
?}


\zap_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Opbiekt na potrzeby analizy zapisów rozrachunków
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('ZapisyObj')>0
|| ZapisyObj
|| _obj:=obj_new(
      'ZAP_OP',
      'fill'
   );
   ZapisyObj:=_obj;
   _obj.ZAP_OP:=tab_tmp(2,
      'VAT','INTEGER','VAT',
      'DTO','DATE','Data wystawienia',
      'DTW','DATE','Data zapisu',
      'D4','DATE','Data odebrania',
      'NR','INTEGER','Nr dokumentu',
      'SW','REAL','Saldo Wn',
      'SM','REAL','Saldo Ma',
      'SW2','REAL','Saldo Wn (waluta)',
      'SM2','REAL','Saldo Ma (waluta)',
      'Z','STRING[16]','Refsql pozycji',
      'KOR','STRING[1]','Korekta',
      'SYM_KOR','STRING[35]','Symbol korygowanego dokumentu',
      'REJ','INTEGER','Rejestr'
   );
   _obj.fill:="
      _tab:=.ZAP_OP;
      _tab.prefix();
      _tab.erase();
      ZAP_OP.cntx_psh();
      ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
      {? ZAP_OP.first()
      || POZ.cntx_psh();
         DOK.cntx_psh();
         {!
         |? POZ.use(ref_name(ZAP_OP.POZDOK));
            POZ.prefix();
            {? POZ.seek(ZAP_OP.POZDOK)
            || DOK.use('doku'+(POZ.name()+4));
               DOK.prefix();
               _tab.blank(1);
               _tab.VAT:=POZ.DOK().RVAT<>null;
               _tab.DTO:=DOK.DTO;
               _tab.DTW:=DOK.DTW;
               _tab.D4:=DOK.D4;
               _tab.Z:=DOK.DOKZRODL;
               _tab.KOR:=DOK.DOK_REJ().KOR;
               {? _tab.KOR='T'
               || _tab.SYM_KOR:={? var_pres('KOR_ZEW',DOK)>0 || DOK.KOR_ZEW || DOK.KOR ?}
               ?};
               _tab.NR:=DOK.NR;
               _tab.REJ:=DOK.REJ;
               _tab.SW:=F.SWn(ZAP_OP.WN,ZAP_OP.MA);
               _tab.SM:=F.SMa(ZAP_OP.WN,ZAP_OP.MA);
               _tab.SW2:=F.SWn(ZAP_OP.WN2,ZAP_OP.MA2);
               _tab.SM2:=F.SMa(ZAP_OP.WN2,ZAP_OP.MA2);
               _tab.add()
            ?};
            ZAP_OP.next()
         !};
         DOK.cntx_pop();
         POZ.cntx_pop()
      ?};
      ZAP_OP.cntx_pop()
   ";
   _obj
?}


\raty_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obiekt na potrzeby rat
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('RatyObj')>0
|| RatyObj
|| _obj:=obj_new(
      'OP','ZAP_OP',
      'fill'
   );
   RatyObj:=_obj;
   _obj.OP:=tab_tmp(1,
      'TP','DATE','Nazwa w oknie',
      'RATA','REAL','Rata',
      'ZAP','REAL','Zapłata'
   );
   _obj.ZAP_OP:=tab_tmp(3,
      'REF','INTEGER','Nagłówek',
      'DATA','DATE','Data',
      'ZAP','REAL','Zapłata',
      'POZDOK','STRING[16]','Ref sql'
   );
   _obj.fill:="
      D_RB.index('OP'); D_RB.prefix(OP.ref());
      {? D_RB.first()
      || _op:=.OP;
         _zap:=.ZAP_OP;
         _op.prefix();
         _op.erase();
         _zap.prefix();
         _zap.erase();
         {!
         |? _op.blank(1);
            _op.TP:=exec('dzienRob','kalendarz',D_RB.TERMPLAT,1,1,1);
            _op.RATA:=D_RB.WAR;
            _op.ZAP:=D_RB.WAR;
            _op.add();
            D_RB.next()
         !};
         _op.first();
         ZAP_OP.cntx_psh();
         ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
         {? ZAP_OP.first()
         || {!
            |? _kw:=ZAP_OP.WN;
               {!
               |? {? _kw
                  || _rata:=_op.ZAP;
                     {? _kw>_rata
                     || {? _rata=0
                        || _rata:=_kw;
                           _op.RATA+=_kw
                        ?};
                        _op.ZAP:=0;
                        _op.put();
                        _zap.blank(1);
                        _zap.REF:=_op.ref();
                        _zap.DATA:=exec('zap_op_data','optermpl');
                        _zap.ZAP:=_rata;
                        _zap.POZDOK:=$ZAP_OP.POZDOK;
                        _zap.add();
                        _kw-=_rata;
                        _op.next();
                        1
                     |? _kw<_rata
                     || _op.ZAP-=_kw;
                        _op.put();
                        _zap.blank(1);
                        _zap.REF:=_op.ref();
                        _zap.DATA:=exec('zap_op_data','optermpl');
                        _zap.ZAP:=_kw;
                        _zap.POZDOK:=$ZAP_OP.POZDOK;
                        _zap.add();
                        _kw:=0;
                        0
                     || _op.ZAP:=0;
                        _op.put();
                        _zap.blank(1);
                        _zap.REF:=_op.ref();
                        _zap.DATA:=exec('zap_op_data','optermpl');
                        _zap.ZAP:=_kw;
                        _zap.POZDOK:=$ZAP_OP.POZDOK;
                        _zap.add();
                        _kw:=0;
                        _op.next();
                        0
                     ?}
                  ?}
               !};
               ZAP_OP.next()
            !}
         ?};
         ZAP_OP.cntx_pop();
         1
      ?}
   ";
   _obj
?}


\record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Formuła na Rekord
::   WE: _a - 1-przed 2-po
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
{? _a=1
|| {? _tab.REF=0
   || Color.fnd_kol('OPTERMPL#01#01')
   || ''
   ?}
|| exec('__CHK','object');
   _ret:=__CHK.record2(_tab,
      'KH_NAZ','Nazwa',
      'SYM','Symbol',
      'DZ','Data zakupu',
      'NW','Brutto (wal)',
      'WAL','Waluta',
      'NP','Brutto (PLN)',
      'TP','Termin płatności'
   );
   {? _ret='' & _tab.PL
   || _ret:=__CHK.record2(_tab,
         'TZ','Termin zapłaty',
         'ZW','Zapłata (wal)',
         'WAL_Z','Waluta zapłaty'
      )
   ?};
   {? _ret='' & _tab.KH_NIP=''
   || _tab.KH_NIP:='brak'
   ?};
   _ret
?}


\akc_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Akcja dołącz i popraw
::----------------------------------------------------------------------------------------------------------------------
_add:=-menu_txt()<>'popraw';
_tab:=cur_tab();
{? _add
|| _tab.blank(1)
?};
VAR_DEL.delete('Edit');
Edit:=1;
_dz:=_tab.DZ;
_tp:=_tab.TP;
{? _tab.edit("exec('record','opuokik',2)")
|| {? _add
   || _tab.add();
      exec('renum','opuokik')
   || _tab.put();
      {? _dz<>_tab.DZ | _tp<>_tab.TP
      || exec('renum','opuokik')
      ?}
   ?}
?};
VAR_DEL.delete('Edit');
~~


\akc_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po usunięciu rekordu
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
{? _tab.sel_size() || return ?};
exec('renum','opuokik');
~~


\akc_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Akcja - pokaż rozrachunek
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
_maska:=UNPAR.PRF1().KOD;
OP.cntx_psh();
OP.use('operac'+_maska);
ZAP_OP.cntx_psh();
ZAP_OP.use('rozzap'+_maska);
D_RB.cntx_psh();
D_RB.use('dr__fk'+_maska);
STANY.cntx_psh();
STANY.use('stany_'+_maska);
OP.prefix();
{? OP.seek(_tab.REF,)
|| _wal:=SSTALE.WAL;
   SSTALE.WAL:=OP.WAL;
   _esc:=BEER.ESC;
   BEER.ESC:=1;
   exec('op_przeg','!fks_roz_zark',,'R(Q)');
   BEER.ESC:=_esc;
   SSTALE.WAL:=_wal
?};
STANY.cntx_pop();
D_RB.cntx_pop();
ZAP_OP.cntx_pop();
OP.cntx_pop()


\renum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przenumerowywanie
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
_ref:=_tab.ref();
{? _tab.first()
|| _lp:=0;
   {!
   |? _tab.LP:=(_lp+=1);
      _tab.put();
      _tab.next()
   !}
?};
_tab.seek(_ref)


\akc_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Akcja wyświetl
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
Edit:=1;
_tab.display();
VAR_DEL.delete('Edit')


\analize
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy analizować bieżący rozrachunek rozrachunek?
::   WE: _a - tabela z wynikiem analizy
::       _b - tryb 1-dla rozrachunku 2-przed zapisem do analizy
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_typ:=_b;
{? _typ=1
|| {? var_pres('FML1',ParObj)>0
   || ParObj.FML1()
   || 1
   ?}
|| {? var_pres('FML2',ParObj)>0
   || ParObj.FML2(_tab)
   || 1
   ?}
?}


\zap_op_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca datę wystawienia związaną z bieżącym zapisem na rozrachunku
::----------------------------------------------------------------------------------------------------------------------
_data:=ZAP_OP.DATA;
{? ParObj.PAR37='N' & ZAP_OP.POZDOK
|| POZ.cntx_psh();
   POZ.use(ref_name(ZAP_OP.POZDOK));
   POZ.prefix();
   {? POZ.seek(ZAP_OP.POZDOK)
   || DOK.cntx_psh();
      DOK.use('doku'+(POZ.name()+4));
      DOK.prefix();
      _data:=POZ.DOK().DTO;
      DOK.cntx_pop()
   ?};
   POZ.cntx_pop()
?};
_data


\f3_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla pola z nip kontrahenta
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
_ret:=~~;
KH.cntx_psh();
KH.index('SNIP'); KH.prefix(2);
KH.win_sel('SEL');
KH.find_key(fld()) | KH.first();
{? KH.select(,1,3)
|| _tab.KH_NAZ:=KH.NAZ_P;
   fld(KH.SNIP);
   _ret:=KH.SNIP
?};
KH.cntx_pop();
_ret


\f3_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla pola z walutą
::----------------------------------------------------------------------------------------------------------------------
_ret:=~~;
{? FINFO.SLWAL
|| FINFO.SLWAL().SLU();
   SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(FINFO.SLWAL().SLU);
   SLO.find_key(fld()) | SLO.first();
   SLO.win_sel('ONE_SEL');
   {? SLO.select(,1,3)
   || _ret:=SLO.KOD
   ?};
   SLO.cntx_pop()
?};
_ret


\f3_nrb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla pola numer rachunku bankowego
::----------------------------------------------------------------------------------------------------------------------
_ret:=~~;
_rb:={? #(2+fld()) || fld() || 2-fld() ?};
SKID_RBK.cntx_psh();
SKID_RBK.index('WEKTOR');
SKID_RBK.prefix(REF.INFO,);
fld()<>'' & SKID_RBK.find_key(_rb) | SKID_RBK.first();
SKID_RBK.win_sel('WYB');
{? SKID_RBK.select(,1,3)
|| _ret:=SKID_RBK.N;
   {? #(2+_ret) || _ret:=SKID_RBK.KRAJ().KODISO+_ret ?}
?};
SKID_RBK.cntx_pop();
_ret


\bd_null
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed wyświetleniem wartości pustych
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('Edit')>0 || return('') ?};
_fld:=fld();
{? type_of(_fld)=type_of(0)
|| {? _fld=0 || '255:255:255' || '' ?}
|| {? _fld=date(0,0,0) || '255:255:255' || '' ?}
?}


\ae_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji daty zakupu i terminu płatności
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
_tab.TP_DNI:=_tab.TP-_tab.DZ;
win_disp();
1


\akc_save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zapisuje analizę do pliku CSV
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
_tab.cntx_psh();
{? _tab.first()
|| _name:='Sprawozdanie dla UOKiK za '+$(UNPAR.PRF1().POCZ_ROK~1)+'.csv';
   _sep:=exec('sep','#file',1);
   __dir_controling:=fmk_tmp_dir(0);
   _fn:=__dir_controling.get_path()+_sep+_name;

   {? _fn<>''
   || {? _fn+4<>'.csv'
      || _fn+='.csv'
      ?};
      {? fexists(_fn,0) & FUN.ask('Istanieje już plik: \n%1.\nCzy nadpisać go?'@[_fn])=0
      || _fn:=''
      ?}
   ?};
   {? _fn<>''
   || _fp:=_fn-4;
      _fs:=_fn+4;
      _max:=1048576;
      _lp:=0;
      _nr:=0;
      _file:=0;
      _sep:=';';
      PROGRESS.set(_tab.size(),'Zapisywanie danych sprawozdania ...');
      {!
      |? _lp+=1;
         {? _lp=1
         || {? _file
            || fclose(_file)
            ?};
            _file:=fopen(_fn,'w',0);
            {? _file
            || _nr+=1;
               fwrite(_file,
                  '"Lp."'+_sep+
                  '"NIP dostawcy"'+_sep+
                  '"Nazwa dostawcy"'+_sep+
                  STR.maz2w95('"Dużego przedsiębiorca"')+_sep+
                  '"Numer dowodu zakupu"'+_sep+
                  '"Data zakupu (DD.MM.RRRR)"'+_sep+
                  STR.maz2w95('"Data wpływu (DD.MM.RRRR)"')+_sep+
                  STR.maz2w95('"Kwota należności (brutto) w walucie"')+_sep+
                  '"Waluta dowodu zakupu"'+_sep+
                  STR.maz2w95('"Kwota należności (brutto) w PLN"')+_sep+
                  STR.maz2w95('"Termin płatności (w dniach)"')+_sep+
                  STR.maz2w95('"Termin płatności (DD.MM.RRRR)"')+_sep+
                  STR.maz2w95('"Czy płatności"')+_sep+
                  STR.maz2w95('"Sposób zapłaty"')+_sep+
                  '"Numer rachunku bankowego wzywanego podmiotu"'+_sep+
                  '"Opis transakcji"'+_sep+
                  STR.maz2w95('"Data zapłaty (DD.MM.RRRR)"')+_sep+
                  STR.maz2w95('"Kwota zapłaty częściowej"')+_sep+
                  STR.maz2w95('"Kwota zapłaty zbiorczej"')+_sep+
                  STR.maz2w95('"Kwota zapłaty w walucie"')+_sep+
                  STR.maz2w95('"Waluta zapłaty"')+_sep+
                  STR.maz2w95('"Dodatkowe wyjaśnienia"')
               )
            ?}
         ?};
         {? _file
         || fwrite(_file,
               '"'+$_tab.LP+'"'+_sep+
               '"'+_tab.KH_NIP+'"'+_sep+
               '"'+STR.maz2w95(_tab.KH_NAZ)+'"'+_sep+
               '"'+$_tab.KH_STAT+'"'+_sep+
               '"'+STR.maz2w95(_tab.SYM)+'"'+_sep+
               '"'+_tab.DZ$4+'"'+_sep+
               {? _tab.DW=date(0,0,0) || '' || '"'+_tab.DW$4+'"' ?}+_sep+
               form(_tab.NW,,,'0,')+_sep+
               _tab.WAL+_sep+
               form(_tab.NP,,,'0,')+_sep+
               $_tab.TP_DNI+_sep+
               '"'+_tab.TP$4+'"'+_sep+
               $_tab.PL+_sep+
               '"'+STR.maz2w95(_tab.S_PL)+'"'+_sep+
               '"'+_tab.NRB+'"'+_sep+
               '"'+STR.maz2w95(_tab.OP_TRAN)+'"'+_sep+
               {? _tab.TZ<>date(0,0,0) || '"'+_tab.TZ$4+'"' || '' ?}+_sep+
               {? _tab.ZC || form(_tab.ZC,,,'0,') || '' ?}+_sep+
               {? _tab.ZZ || form(_tab.ZZ,,,'0,') || '' ?}+_sep+
               {? _tab.ZW || form(_tab.ZW,,,'0,') || '' ?}+_sep+
               _tab.WAL_Z+_sep+
               _tab.DOD
            )
         ?};
         {? _lp=_max || _lp:=0 ?};
         PROGRESS.next();
         _tab.next()
      !};
      PROGRESS.close();
      dlg_save(_fn,0);
      {? _file
      || fclose(_file)
      ?}
   ?}
|| FUN.info('Brak danych do zapisania.'@)
?};
VAR_DEL.delete('__dir_controling');
_tab.cntx_pop()


\ae_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji pola Zapłata (_tab.PL)
::----------------------------------------------------------------------------------------------------------------------
exec('mark','opuokik');
_tab:=cur_tab();
{? _tab.PL=0
|| _tab.S_PL:=_tab.NRB:=_tab.OP_TRAN:=_tab.WAL_Z:='';
   _tab.TZ:=date(0,0,0);
   _tab.ZC:=_tab.ZZ:=_tab.ZW:=0
?};
1


\rbl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca rachunek bankowy licencjobiorcy z wyciągu bankowego (PW)
::----------------------------------------------------------------------------------------------------------------------
_rb:=PW.RBL;
{? #(2+_rb)
|| SKID_RBK.cntx_psh();
   SKID_RBK.index('WEKTOR');
   SKID_RBK.prefix(REF.INFO,_rb,);
   {? SKID_RBK.first()
   || _rb:=SKID_RBK.KRAJ().KODISO+_rb
   ?};
   SKID_RBK.cntx_pop()
?};
_rb


\mark
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Ustawienie wymagalnosci pól
::----------------------------------------------------------------------------------------------------------------------
_tab:={? var_pres('_a')>0 || _a || cur_tab() ?};
_edit:=_tab.win_edit('?');
_tab.efld_opt(_edit,'mark=1',,'KH_NAZ');
_tab.efld_opt(_edit,'mark=1',,'SYM');
_tab.efld_opt(_edit,'mark=1',,'DZ');
_tab.efld_opt(_edit,'mark=1',,'NW');
_tab.efld_opt(_edit,'mark=1',,'WAL');
_tab.efld_opt(_edit,'mark=1',,'NP');
_tab.efld_opt(_edit,'mark=1',,'TP');
_mark:=_tab.PL;
_tab.efld_opt(_edit,'mark='+$_mark,,'TZ');
_tab.efld_opt(_edit,'mark='+$_mark,,'ZW');
_tab.efld_opt(_edit,'mark='+$_mark,,'WAL_Z');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 76d1b731837293189a3802bd76d8925789d89e0b06d860cdd56d82828e4cc8c1fca70d4777375152b8c0eddba2d474e982975d7dee1d53b3cc9abcee0c9d26e6ffb5028ddf1eeb99b9eb7438c82fcaad0add6604d827bc97d771acdcd5f705e9795f3867653f283a08fbe29cc64b31cc181723d603f28a7646121d7f60767476

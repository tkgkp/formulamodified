:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: material_prod.fml
:: Utworzony: 12.05.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa dodatkowych właściwości materiałów (surowców, produktów) dla produkcji - część bibioteczna
::======================================================================================================================


::======================================================================================================================
:: Wspólne
::======================================================================================================================


\a_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Zwraca wartość VAR.A_T
::   WY: VAR.A_T
::  OLD: \a_t/varget.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.A_T


::======================================================================================================================
:: Obsługa tabeli PTC - ceny "towarów produkcyjnych"
::======================================================================================================================


\PTCaddfromKL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Dodaje cenę do propozycji (z kalkulacji karty technologicznej)
::   WE: _a - M.ref()
::       _b - cena
::       _c - data
::  OLD: \PTCaddfromKL/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
do();
PTC.clear();
_pt:=_a;
_price:=_b;
_dod:=_c;
PTC.blank();
PTC.PT:=_pt;
PTC.DOD:=_dod;
PTC.DDO:=date(0,0,0);
PTC.PRICE:=_price;
{? PTC.add(1)
|| exec('chkPTC','material_prod',PTC.PT)
|| undo();
   0
?};
end();
~~


\chkPTC
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: sprawdza czy jest zachowana ciągłość dat, czy np. dwie pozycje nie mają czasu do nieskończonosci
::   WE: [_a] - M.ref()
::   WY: 1
::  OLD: \chkPTC/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
_pt:={? _>0 || _a || PTC.PT ?};
PTC.cntx_psh();
PTC.clear();
PTC.index('TO');
PTC.prefix(_pt);
{? PTC.last()
|| _to:=PTC.DOD-1;
   {? PTC.prev()
   || {!
      |? {? (PTC.DDO=date(0,0,0)) | (PTC.DDO>_to)
         || PTC.DDO:=_to; PTC.put()
         ?};
         _to:=PTC.DOD-1;
         PTC.prev()
      !}
   ?}
?};
PTC.cntx_pop();
1


\ptcdodbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: przed redakcją PTC.DOD
::  OLD: \ptcdodbe/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
red1


\ptcdodae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: po redakcji PTC.DOD
::  OLD: \ptcdodae/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
1


\ptcddobe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: przed redakcją PTC.DDO
::  OLD: \ptcddobe/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
red2


\ptcddoae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: po redakcji PTC.DDO
::  OLD: \ptcddoae/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
1


\ptcpribe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: przed redakcją PTC.PRICE
::  OLD: \ptcpribe/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
red3


\ptcpriae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: po redakcji PTC.PRICE
::  OLD: \ptcpriae/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
1


\ptcaddbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: obsługa na 'Dołącz' w PTC.WAR
::  OLD: \ptcaddbe/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
red1:=red2:=red3:=1;
dod:=ddo:=date(0,0,0);
_price:=0;
{? PTC.last()
|| dod:=PTC.DOD;
   ddo:=PTC.DDO;
   _price:=PTC.PRICE;
   red1:=1
?};
_rep:=(ddo=date(0,0,0));
_d:={? dod<>date(0,0,0)
    || {? dod>ddo || dod+1 || ddo+1 ?}
    || red:=1;
       date()
    ?};
PTC.blank();
PTC.PT:=VAR.A_T;
PTC.DOD:=_d;
PTC.PRICE:=_price;
{!
|?
   {? PTC.edit("exec('ptc_valid','material_prod')")
   || _ok:=PTC.add();
      ~_ok
   || 0
   ?}
!};
{? _ok & _rep
|| _ddo:=PTC.DOD-1;
   {? PTC.prev()
   || PTC.DDO:=_ddo;
      PTC.put();
      PTC.next()
   ?}
?};
0


\ptcpopbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: obsługa na 'Popraw' PTC.WER
::  OLD: \ptcpopbe/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
red1:=0;
red2:=1;
red3:=1;
dod:=ddo:=date();
_ref:=PTC.ref();
PTC.cntx_psh();
_ok:={? PTC.last()
     || dod:=PTC.DOD;
        ddo:=PTC.DDO;
        (PTC.ref()=_ref)
     || 0
     ?};
PTC.cntx_pop();
{? _ok
|| red1:=0;
   red2:=1;
   red3:=1;
   {!
   |?
      {? PTC.edit("exec('ptc_valid','material_prod')")
      || _ok:=PTC.put();
         ~_ok
      || 0
      ?}
   !};
   1
|| FUN.emsg('Można poprawiać tylko ostatnią cenę.'@);
   0
?}


\ptcdelbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: usuwanie ceny
::  OLD: \ptcdelbe/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=PTC.ref();
PTC.cntx_psh();
_ok:={? PTC.last() || (PTC.ref()=_ref) || 0 ?};
PTC.cntx_pop();
{? _ok
|| {? FUN.ask('Usunąć bieżący wiersz?'@)
   || PTC.del()
   ?}
|| FUN.emsg('Można usuwać tylko ostatnią cenę.'@);
   0
?}


\pt_price
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: ceny do kalkulacji technologii
::   WE: [_a] - 0 bez okienka 1 (domyślnie) z okienkiem
::   WY: ~~
::  OLD: \pt_price/ptowary.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};

{? HELP.REFMAT<>null() || M.seek(HELP.REFMAT) ?};

VAR.A_T:=M.ref();
PTC.clear();
PTC.index('TO');
PTC.prefix(VAR.A_T);
PTC.win_edit('RED');
PTC.win_sel('WER');
red1:=red2:=red3:=0;
dod:=ddo:=date(0,0,0);
{? _a || PTC.last(); PTC.select(,1,8) ?};
VAR_DEL.delete('red1','red2','red3','dod','ddo');
~~


\ptc_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Walidacja rekordu PTC
::----------------------------------------------------------------------------------------------------------------------
:: Sprawdzenie wypełnienia pól
_ok:=__CHK.record(PTC,,'DOD');

{? dod>ddo
|| _w:='początkowej';
   _d:=dod+1
|| _w:='końcowej';
   _d:=ddo+1
?};

:: Walidacja pola PTC.DOD
{? -menu_txt()='dołącz' & _ok=''
|| _ok:=
      {? PTC.DOD>=_d
      || ''
      || {? _w='początkowej'
         || FUN.emsg('Data początkowa musi być późniejsza od daty początkowej poprzedniej ceny.'@)
         || FUN.emsg('Data początkowa musi być późniejsza od daty końcowej poprzedniej ceny.'@)
         ?};
         'DOD'
      ?}
?};
{? -menu_txt()='dołącz' & _ok=''
|| _ok:=
      {? (PTC.DOD>PTC.DDO)&(PTC.DDO<>date(0,0,0))
      || FUN.emsg('Data początkowa nie może być późniejsza od daty końcowej.'@);
         'DOD'
      || ''
      ?}
?};

:: Walidacja pola PTC.DDO
{? _ok=''
|| _ok:=
      {? PTC.DDO<>date(0,0,0)
      || {? PTC.DDO<>date(0,0,0)
         || {? PTC.DOD>PTC.DDO
            || FUN.emsg('Data końcowa nie może być wcześniejsza od daty początkowej.'@);
               'DDO'
            || ''
            ?}
         || ''
         ?}
      || ''
      ?}
?};

:: Walidacja pola PTC.PRICE
{? _ok=''
|| _ok:=
      {? PTC.PRICE>=0
      || ''
      || FUN.emsg('Wartość pola nie może być mniejsza od zera.'@);
         'PRICE'
      ?}
?};

_ok


::======================================================================================================================
:: Obsługa tabeli MTPZ - wyceny produktów
::======================================================================================================================


\mptz_m_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Przed redakcją pola MPTZ.M
::  OLD: \mptz_m_be/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
::{? var_pres('__mptz')=type_of(0) & __mptz=1 || 0 || 1 ?}
0


\mptz_sel_kktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Selekcja propozycji cen dla produktu z kalkulacji technologii
::  OLD: \mptz_sel_kktl/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
{? KKTL.TKTLW=null()
|| FUN.info('Brak produktu przypisanego do kalkulacji.'@)
|| exec('tow_prop','material_prod')
?}


\mptz_sel_anzh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Selekcja propozycji cen dla produktu z kalkulacji/analizy zlecenia
::  OLD: \mptz_sel_anzh/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
ANZH.ZLEC().KTM();
exec('tow_prop','material_prod',,params_get().edit)


\tow_prop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: propozycje cen dla danego materiału
::   WE: [_a] - 0 bez okienka, 1 (domyślnie) z okienkiem
::       [_b] - 0 bez redakcji, 1 (domyślnie) z redakcją
::   WY: ''
::  OLD: \tow_prop/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=1 || _select:=_a || _select:=1 ?};
{? var_pres('_b')=1 || _keys:={? _b>0 || ':' || 'DPUAW:D' ?} || _keys:=':' ?};

MPTZ.clear();
MPTZ.index('TRD');
MPTZ.prefix(M.ref());
VAR.A_T:=M.ref();
MPTZ.win_sel('WER_TOW');
{? _select
|| MPTZ.hdr_sel(' (%1)'[M.KTM]);
   MPTZ.blank(1);
   {? cur_tab(1,0)=ANZH
   || MPTZ.ANZH:=ANZH.ref()
   |? cur_tab(1,0)=KKTL
   || MPTZ.KKTL:=KKTL.ref()
   ?};
   MPTZ.find_rec();
   MPTZ.select(,1,5,_keys);
   MPTZ.hdr_sel()
|| __mptz:=1
?};
VAR_DEL.delete('__MP_TAB');
~~


\mptz_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Dołączenie 'ręcznego' zapisu w cenniku produktów (propozycje cen)
::  OLD: \mptz_dol/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
M.cntx_psh();
exec('filter','material','WP');
MPTZ.blank();
MPTZ.R:='R';
MPTZ.DT:=date();
MPTZ.win_edit('RED_R');
{? MPTZ.edit("exec('mptz_chk','material_prod')")
|| MPTZ.add()
?};
exec('filter_clear','material');
M.cntx_pop();
~~


\mptz_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Poprawienie 'ręcznego' zapisu w cenniku produktów (propozycje cen)
::  OLD: \mptz_pop/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
M.cntx_psh();
exec('filter','material','WP');
{? MPTZ.R<>'R' || FUN.info('Tylko zapisy ręczne można poprawiać.'@); M.cntx_pop(); return() ?};
MPTZ.win_edit('RED_R');
{? MPTZ.edit("exec('mptz_chk','material_prod')")
|| MPTZ.put()
?};
exec('filter_clear','material');
M.cntx_pop();
~~


\mptz_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Usunięcie 'ręcznego' zapisu w cenniku produktów (propozycje cen)
::  OLD: \mptz_usu/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
{? MPTZ.R<>'R' || FUN.info('Tylko zapisy ręczne można usuwać.'@); return() ?};
{? MPTZ.AKT='T' || FUN.info('Zapisu aktywnego nie można usuwać.'@); return() ?};
{? FUN.ask('Czy na pewno usunąć zapis?'@) || MPTZ.del() ?};
~~


\mptz_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Kontrola wypełnienia rekordu MPTZ
::  OLD: \mptz_chk/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
{? MPTZ.PRICE<=0
|| FUN.emsg('Cena musi być większa od zera.'@);
   'PRICE'
|| chk_rec('KTM')
?}


\_aktywuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60]
:: OPIS: Aktywacja wybranej propozycji ceny
::       Kontekst wywołania: rekord tabeli MPTZ
::   WE: [_a] - INTEGER - 0/[1] - czy wyświetlać dialogi
::       [_b] - INTEGER - 0/[1] - czy wyceniać wstecznie dokumenty RP zlecenia powiązanego
::  OLD: \_aktywuj/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
_dialog:=1;
{? var_pres('_a')=type_of(0)
|| _dialog:=_a
?};

_wyceniaj:={? MPTZ.R='A' | MPTZ.R='Z' || 1 || 0 ?};
{? var_pres('_b')=type_of(0)
|| _wyceniaj:=_b
?};

{? MPTZ.AKT='N'
|| _activate:=1;
   {? _dialog>0
   || _activate:=FUN.ask('Czy na pewno chcesz aktywować tę cenę?'@)
   ?};
   {? _activate>0
   ||
      _can_continue:=1;
      _tab_mptz:=tab_tmp(1,'REF','STRING[16]','SQL Ref MPTZ');
      _can_continue:=1;
      _mydo:=do_state()=0;
      {? _mydo || do() ?};

      MPTZ.AKT:='+';
      MPTZ.ACCEPT:=OPERATOR.USER;
      MPTZ.DTA:=date();
      MPTZ.put();
      MPTZ.cntx_psh();
      MPTZ.index('TRD');
      {? MPTZ.R='K'
      || MPTZ.prefix(MPTZ.KTM,MPTZ.R)
      || MPTZ.prefix(MPTZ.KTM,MPTZ.R,MPTZ.ANZH().ZLEC)
      ?};
      MPTZ.first();
      {!
      |? {? MPTZ.AKT<>'+'
         || MPTZ.AKT:='N'
         || MPTZ.AKT:='T'
         ?};
         _can_continue:=0;
         _can_continue:=MPTZ.put();
         {? _can_continue>0 & MPTZ.AKT='T'
         || _tab_mptz.blank();
            _tab_mptz.REF:=$MPTZ.ref();
            _tab_mptz.add()
         ?};
         MPTZ.next() & _can_continue>0
      !};
      MPTZ.cntx_pop();
      {? _can_continue<=0
      || undo()
      ?};
      {? _mydo || end() ?};

::    Wyceniam dokumenty RP (o ile cena pochodzi ze zlecenia)
      {? _wyceniaj>0 & _tab_mptz.first()
      || {!
         |? exec('wycena','material_prod',exec('FindAndGet','#table',MPTZ,_tab_mptz.REF,,"ref()",null()),_dialog);
            _tab_mptz.next()
         !}
      ?}
   ?}
|| FUN.info('Cena jest już aktywna.'@)
?};
~~


\_deaktywuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Dezaktywacja wybranej propozycji ceny
::       Kontekst wywołania: rekord tabeli MPTZ
::----------------------------------------------------------------------------------------------------------------------
{? MPTZ.AKT='T'
|| {? FUN.ask('Czy na pewno chcesz wycofać aktywację tej ceny?'@)
   || MPTZ.AKT:='N';
      MPTZ.put()
   ?}
|| FUN.info('Cena nie jest aktywna.'@)
?};
~~


\szuk_mp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60]
:: OPIS: Szukanie w propozycjach cen - MPTZ
::   WE: _a - 0 - szukanie dokładne
::          - 1 - szukanie kontekstowe
::          - 2 - ponowienie szukania
::  OLD: \szuk_mp/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__MP_BUF') < 0
|| __MP_BUF:=obj_new(3);
   {! _i:=1..3 |! __MP_BUF[_i]:='' !}
?};
MPTZ.R:=__MP_BUF[1];
MPTZ.AKT:=__MP_BUF[2];
MPTZ.KEY:=__MP_BUF[3];
{? _a=2 || exec('szuk_mp_next','material_prod'); return() ?};
{? var_pres('__MP_TAB') > 0 || obj_del(__MP_TAB) ?};
_sel:=MPTZ.win_sel('?')+3;
{? _sel='TOW'
|| VAR.SZUK_KTM:=MPTZ.KTM().KTM;
   VAR.SZUK_NAZ:=MPTZ.KTM().N
?};
MPTZ.win_edit('SZUK_'+(_sel));
{? MPTZ.edit()
|| __MP_BUF[1]:=MPTZ.R;
   __MP_BUF[2]:=MPTZ.AKT;
   __MP_BUF[3]:=MPTZ.KEY;
   _prefix:={? _a || '%' || '' ?};
   _sufix:='%';
   _ktm:=_prefix+VAR.SZUK_KTM+_sufix;
   _naz:=_prefix+VAR.SZUK_NAZ+_sufix;
   _key:=_prefix+MPTZ.KEY+_sufix;
   {? MPTZ.R<>'' || _r:='and MPTZ.R=\''+MPTZ.R+'\'' || _r:='' ?};
   {? MPTZ.AKT<>'' || _akt:='and MPTZ.AKT=\''+MPTZ.AKT+'\'' || _akt:='' ?};
   __MP_TAB:=sql('
    select MPTZ.REFERENCE as REF
    from MPTZ join M
    where
     M.KTM like \':_a\'
     and M.N like \':_b\'
     and MPTZ.KEY like \':_c\'
     :_d :_e
   ', _ktm, _naz, _key, _r, _akt);
   {? __MP_TAB.first()
   || MPTZ.seek(__MP_TAB.REF,8+__MP_TAB.REF)
   || FUN.info('Brak rekordów spełniających kryterium wyszukiwania.'@)
   ?}
?};
~~


\szuk_mp_next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60]
:: OPIS: Ponowienie szukania w propozycjach cen
::   WE: bieżący kontekst tabeli tymczasowej __MP_TAB
::  OLD: \szuk_mp_next/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__MP_TAB')<=0 || return() ?};
{? __MP_TAB.next()
|| MPTZ.seek(__MP_TAB.REF,8+__MP_TAB.REF)
|| FUN.info('Nie ma więcej rekordów spełniających kryterium wyszukiwania.'@)
?};
~~


\farbuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: przed rekord w oknie MPTZ.WER_TOW, MPTZ.WER_SEL
::  OLD: \farbuj/produ.fml
::----------------------------------------------------------------------------------------------------------------------
_gray:='UP';
{? MPTZ.R='K' || VAR.STRING:='Kalkulacja TKW'@
|? MPTZ.R='Z' || VAR.STRING:='Kalkulacja zlecenia'@
|? MPTZ.R='A' || VAR.STRING:='Analiza zlecenia'@
|? MPTZ.R='R' || VAR.STRING:='Wprowadzona ręcznie'@; _gray:=''
              || VAR.STRING:=''
?};
{? MPTZ.AKT='T'
|| MPTZ.actions_grayed(cur_win(1,1),'A'+_gray);
   'MPTZ#01#01'
|| MPTZ.actions_grayed(cur_win(1,1),'W'+_gray);
   ''
?}


\mptz_dsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.60] (wycena rozdzielni)
:: OPIS: Wyświetla okno tabeli MPTZ na spację
::   WE: rekord tabeli MPTZ
::  OLD: \mptz_dsp/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
KKTL.cntx_psh();
TKTL.cntx_psh();

MPTZ.get();

:: Ustawiam zmienne do wyświetlenia info o technologii
{? MPTZ.RKKTL<>''
|| KKTL.use(form(8+MPTZ.RKKTL));
   KKTL.clear();
   {? KKTL.seek(MPTZ.RKKTL)
   || exec('display_vars','tech_common',$KKTL.NRK)
   ?}
?};

MPTZ.win_edit('RED_'+MPTZ.R);
MPTZ.display();

TKTL.cntx_pop();
KKTL.cntx_pop();
~~


\leg_mptz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: Legenda w oknie tabeli MPTZ
::  OLD: \leg_mptz/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','MPTZ#01#01')


\kktl2cena
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Wpisanie wyliczonej ceny do propozycji cen
::   WE: _a - czy pytać o wpisanie ceny?
::  OLD: \kktl2cena/tex_kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? KKTL.STAN='N'
|| FUN.emsg('Niemożliwe wpisanie propozycji ceny dla kalkulacji niezatwierdzonej.'@);
   return()
?};

{? KKTL.TKTLW=null()
|| FUN.emsg('Niemożliwe wpisanie propozycji ceny dla kalkulacji prototypowej.\n\n'
      'Proponować można wyłącznie ceny z kalkulacji z wypełnionym polem ''Kod produktu''.'@);
   return()
?};

{? KKTL.NRK().KTM=null()
|| FUN.emsg('Niemożliwe wpisanie propozycji ceny dla kalkulacji do karty prototypowej.\n'
      'Wypełnij pole ''Kod produktu'' w karcie technologicznej.'@);
   return()
?};

{? {? _a
   || FUN.ask('Czy dodać cenę produktu do propozycji cen?'@)
   || 1
   ?}
||
   KPOZK.clear();
   KPOZK.index('KR');
   KPOZK.prefix(KKTL.ref(),KKTL.NRK().TYP().CRUB);
   {? KPOZK.first()
   || _wart:=KPOZK.WART$KKTL.TKTLW().KTM().DOKL_C;
      {? _wart=0
      || FUN.info('Zerowa wartość propozycji ceny produktu nie została dodana.'@)
      ?}
   || FUN.emsg(
         'Nie znaleziono rubryki %1, która ma zawierać cenę jednostkową produktu.\n'
         'Propozycja ceny nie została dodana.'@[$KKTL.NRK().TYP().CRUB]
      );
      _wart:=0
   ?};
   {? _wart<>0
   || MPTZ.clear();
      MPTZ.blank();
      MPTZ.KTM:=KKTL.TKTLW().KTM;
      MPTZ.KKTL:=KKTL.ref();
      MPTZ.KEY:=KKTL.NRK().NRK+' wer. '+TKTL.WER;
      MPTZ.R:='K';
      MPTZ.DT:=date();
      MPTZ.PRICE:=_wart;
      _result:=MPTZ.add();

      {? _result>0
      || exec('mptz_sel_kktl','material_prod')
      ?}
   ?}
?};
~~


\anal2cena
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: Cena z analizy/kalkulacji zlecenia
::   WE: [_a] - INTEGER -  0/[1] - czy wyświetlać dialogi
::       [_b] - INTEGER - [0]/1  - czy aktywować utworzoną propozycję
::       [_c] - INTEGER - [0]/1  - czy podczas aktywacji ceny wyceniać wstecznie dokumenty?
::   WY: 0 - porażka
::       1 - sukces
::  OLD: \anal2cena/zlecanal.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

_dialog:=1;
{? var_pres('_a')=type_of(0)
|| _dialog:=_a
?};

{? _<1 || _no_ask:=0 || _no_ask:=_a ?};
_activate:=0;
{? var_pres('_b')=type_of(0)
|| _activate:=_b
?};

_wyceniaj:=0;
{? var_pres('_c')=type_of(0)
|| _wyceniaj:=_c
?};

_result:=0;
_what:=
   {? VAR.A01='0'
   || 'Kalkulacja jest redagowana przez innego użytkownika. Wykonanie operacji nie jest możliwe.'@
   || 'Analiza jest redagowana przez innego użytkownika. Wykonanie operacji nie jest możliwe.'@
   ?};
{? ~ANZH.r_lock(1,1,1)
|| FUN.info(_what);
   return(0)
?};

_can_continue:=1;
{? _dialog>0
|| {? ANZH.AKT<>'T'
   || _what:=
         {? VAR.A01='0'
         || 'Kalkulacja nie jest aktywna.\nCzy mimo to dodać propozycję ceny?'@
         || 'Analiza nie jest aktywna.\nCzy mimo to dodać propozycję ceny?'@
         ?};
      _can_continue:=FUN.ask(_what)
   || _what:=
         {? VAR.A01='0'
         || 'Czy dodać cenę z kalkulacji do propozycji cen?'@
         || 'Czy dodać cenę z analizy do propozycji cen?'@
         ?};
      _can_continue:=FUN.ask(_what)
   ?}
?};

{? _can_continue>0
|| ANZP.clear();
   ANZP.index('NRARUB');
   ANZP.prefix(ANZH.ref(),ANZH.ZLEC().TYP().CRUBJEDN);
   {? ANZP.first()
   || _wart:=ANZP.WAR$ANZH.ZLEC().KTM().DOKL_C;
      {? _wart=0
      || FUN.info('Zerowa wartość propozycji ceny produktu nie została dodana.'@)
      ?}
   || FUN.emsg(
         'Nie znaleziono rubyki %1, która ma zawierać cenę jednostkową produktu.\n'
         'Propozycja ceny nie została dodana.'@[$ANZH.ZLEC().TYP().CRUBJEDN]
      );
      _wart:=0
   ?};
   {? _wart<>0
   || MPTZ.clear();
      MPTZ.blank();
      MPTZ.KTM:=ANZH.ZLEC().KTM;
      MPTZ.R:={? VAR.A01='0' || 'Z' || 'A' ?};
      MPTZ.ANZH:=ANZH.ref();
      MPTZ.KEY:=ZL.SYM;
      MPTZ.DT:=date();
      MPTZ.PRICE:=_wart;
      _result:=MPTZ.add();

::    Aktywacja przekazanej ceny
      {? _result>0 & _activate>0
      || exec('_aktywuj','material_prod',_dialog,_wyceniaj)
      ?};

      {? _result>0 & _dialog>0
      || exec('mptz_sel_anzh','material_prod')
      ?}
   ?}
?};
ANZH.r_unlock();
~~


\MPTZ_trigger
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [12.41]
:: OPIS: Triggery dla tabeli MPTZ
::  OLD: \MPTZ/trigger.fml
::  TAG: <PRIVATE>
::----------------------------------------------------------------------------------------------------------------------
{? MPTZ.KKTL<>null()
|| MPTZ.RKKTL:=$MPTZ.KKTL
?};
1


\mptz_trig_add_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Trigger przed add dla tabeli TMAT
::----------------------------------------------------------------------------------------------------------------------
exec('MPTZ_trigger','material_prod')


\mptz_trig_put_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Trigger przed put dla tabeli TMAT
::----------------------------------------------------------------------------------------------------------------------
exec('MPTZ_trigger','material_prod')


\tech_tow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [8.10] Uwaga_190
:: OPIS: Lista technologii danego produktu
::       Kontekst wywolania: rekord tabeli M
::   WE: [_a] - 0 bez okienka 1 (domyslnie) z okienkiem
::  OLD: \tech_tow/tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};

VAR.GRUPA:='N';
VAR.ACTION:='4';
TKTLW.win_sel('WER_'+{? _a<>0 || 'W' || 'M' ?});
TKTLW.win_edit('RED_K');
TKTLW.index('KARC');
: wszystkie karty niearchiwalne
TKTLW.prefix('T',M.ref(),'N');
{? TKTLW.first()
|| {? _a || TKTLW.select() ?}
||
: wszystkie karty archiwalne
   TKTLW.prefix('T',M.ref(),'T');
   {? TKTLW.first()
   || {? _a
      || FUN.info('Brak niearchiwalnej karty technologicznej dla tego produktu.\n'
          'Wyświetlone zostaną karty archiwalne.'@)
      ?};
      TKTLW.hdr_sel(' — '+'archiwalne'@);
      {? _a || TKTLW.select() ?}
   || {? _a || FUN.info('Brak karty technologicznej dla tego produktu.'@) ?}
   ?}
?};
~~


\wycena
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [gc] [12.46]
:: OPIS: Wycena dokumentów RP po aktywacji propozycji ceny ze zlecenia
::   WE: [_a] - MPTZ.ref() lub bieżący rekord
::       [_b] - INTEGER - 0/1 - czy wyświetlać dialogi
::   WY: 0 - porażka
::       1 - sukces
::  OLD: \wycena/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
{? var_pres('_a')=type_of(MPTZ.ref())
|| _ref:=_a
?};
_dialog:=1;
{? var_pres('_b')=type_of(0)
|| _dialog:=_b
?};

_result:=0;
_can_continue:=1;

MPTZ.cntx_psh();
ANZH.cntx_psh();
KKTL.cntx_psh();
TKTL.cntx_psh();
ZL.cntx_psh();
ZL.clear();

{? _ref<>null()
|| MPTZ.prefix();
   {? MPTZ.seek(_ref)
   || _can_continue:=1
   || _can_continue:=0
   ?}
?};

{? _can_continue>0
|| _zl:=null();
   {? MPTZ.ANZH<>null()
   || _zl:=MPTZ.ANZH().ZLEC
   |? MPTZ.KKTL<>null()
   || _zl:=MPTZ.KKTL().NRK().ZL
   ?};
   {? _zl=null() || _can_continue:=0 ?}
?};

:: Wycena nie dotyczy magazynów z cenami ewidencyjnymi
{? exec('FindAndGet','#table',ZL,_zl,,"MG().TYP*'EWI'>0",1)
|| _can_continue:=0
?};

{? _can_continue>0
||
   _wyceniaj:=1;
:: Decyzja o wycenie przeniesiona dopiero po zebraniu listy dokumentów
::   {? _dialog>0
::   || _wyceniaj:=FUN.ask('Czy wycenić raporty produkcji powiązane z tym zleceniem na podstawie propozycji ceny?'@)
::   ?};

   {? MPTZ.AKT='T' & _wyceniaj>0
   ||
      _typydok:=exec('get','#params',500701,2,null());
      {? _typydok=''
      || {? _dialog>0
         || FUN.info(
               'Brak zdefiniowanych typów dokumentów raportujących produkcję.\n'
               'Należy uzupełnić parametr aplikacyjny %1.'@['500701']
            )
         ?};
         _can_continue:=0
      ?};

      {? _can_continue>0
      || _typydok:=exec('typydok_rap','zl_common',_zl);

         {? type_of(_typydok)<>type_of(SYSLOG) | _typydok.first()=0
         || FUN.wdrerror(
               '(\wycena/material_prod.fml)\n'
               'Wystąpił błąd podczas tworzenia tabeli typów dokumentów magazynowych.'
            );
            _can_continue:=0
         ?}
      ?};

      {? _can_continue>0 & _zl<>null()
      || _zlecenia:=tab_tmp(1,'REF','STRING[16]','$ZL.ref()');
         {? ZL.seek(_zl)
         || {? ZL.RODZAJ='P'
            || _zlecenia.blank();
               _zlecenia.REF:=$_zl;
               _zlecenia.add()
            ||
               ZL.index('NRNZL');
               ZL.prefix(ZL.UNRZL);
               {? ZL.first()
               || {!
                  |? _zlecenia.blank();
                     _zlecenia.REF:=$ZL.ref();
                     _zlecenia.add();
                     ZL.next()
                  !}
               ?}
            ?}
         ?};
         _ndzl:=sql('
            select
               DK.N as REF,
               ND.SYM as SYM,
               ND.ZAK as ZAK
            from @DK join @ND where DK.ZL in (select :_a.REF from :_a)
            ', _zlecenia
         );

         {? _dialog>0
         ||
::          Wersja z dialogami, najpierw zbieram listę dokumentów które będą miały zmienione ceny i jeśli
::          ta lista nie jest pusta to ją wyświetlam
            _show_warn_reply:=0;
            _show_warn_zak:=0;
            _show_info_dok:=0;
            KOMM.init(250,,'Wsteczna wycena dokumentów raportujących produkcję'@);
            {? _ndzl.first()
            || {!
               |? {? _ndzl.ZAK='T'
                  || _msg:='Dokument %1 jest zaksięgowany'@[_ndzl.SYM];
                     KOMM.add(_msg,7,,1);
                     _show_warn_zak+=1
                  || ND.cntx_psh();
                     DK.cntx_psh();
                     ND.use(8+_ndzl.REF);
                     DK.use(5+DK.name()+(ND.name+3));
                     {? ND.seek(_ndzl.REF)
                     || _typydok.prefix(form(8+$ND.TYP),#ND.TYP);
                        {? _typydok.first()
                        || DK.index('DOKMAG');
                           DK.clear();
                           DK.prefix(ND.ref());
                           {? DK.first()
                           || {!
                              |? _oldprice:=DK.C;
                                 {? _oldprice<>0
                                 || _show_warn_reply+=1;
                                    _msg:='Pozycja %1 w dokumencie %2 jest już wyceniona na: %3 %4'@
                                          [$DK.P,ND.SYM,form(DK.C,,2,'99'),INFO.NAROD().KOD];
                                    KOMM.add(_msg,7,,1)
                                 || _show_info_dok+=1;
                                    _msg:='Pozycja %1 w dokumencie %2 nie była wycenieniana'@
                                          [$DK.P,ND.SYM];
                                    KOMM.add(_msg,1,,1)
                                 ?};
                                 DK.next()
                              !}
                           ?}
                        ?}
                     ?};
                     ND.cntx_pop();
                     DK.cntx_pop()
                  ?};
                  _ndzl.next()
               !}
            ?};
            {? _show_warn_reply>0 | _show_warn_zak>0 | _show_info_dok>0
            || _msg:='';
               {? _show_info_dok>0
               || _msg+='\n'+'Liczba dokumentów z zerową ceną do wyceny: %1'@[$_show_info_dok]
               ?};
               {? _show_warn_zak>0
               || _msg+='\n'+'Liczba zaksięgowanych dokumentów: %1, '
                        'nie zostaną one wycenione.'@[$_show_warn_zak]
               ?};
               {? _show_warn_reply>0
               || _msg+='\n'+'Liczba wcześniej wycenionych dokumentów: %1. '
                        'Ich ceny zostaną zastąpione wartościami z aktywowanej propozycji.'@[$_show_warn_reply]
               ?};
               _again:=1;
               {!
               |? _choice:=FUN.choice(_msg,,'Szczegóły'@,'Kontynuuj'@);
                  {? _choice=1
                  || KOMM.select();
                     _again:=1
                  |? _choice=2
                  || _can_continue:=1;
                     _again:=0
                  || _can_continue:=0;
                     _again:=0
                  ?};
                  _again>0
               !}
            ?}
         ?};
         {? _can_continue>0
         || {? _ndzl.first()
            || {!
               |? {? _ndzl.ZAK='N'
                  || ND.cntx_psh();
                     DK.cntx_psh();
                     DK_C.cntx_psh();
                     ND.use(8+_ndzl.REF);
                     ND.prefix();
                     _mask:=5+DK.name()+(ND.name+3);
                     DK.use(_mask);
                     _mask:=5+DK_C.name()+(ND.name+3);
                     DK_C.use(_mask);
                     {? ND.seek(_ndzl.REF)
                     ||
::                      Sprawdzam czy dokument jest typu raportującego produkcję
                        _typydok.prefix(TYPYDOK.name(),#ND.TYP);
                        {? _typydok.first()
                        || DK.index('DOKMAG');
                           DK.clear();
                           DK.prefix(ND.ref());
                           {? DK.first()
                           || {!
                              |? _oldprice:=DK.C;
                                 _dk_c:=DK.DK_C;
                                 {? _can_continue>0
                                 || {? exec('is_usluga_typ','zl_uslugi',ND.TYP)
                                    || _can_continue:=exec('zmcene','zl_uslugi',,MPTZ.PRICE)
                                    || _can_continue:=exec('poprzpoz','magdok_poz',,,,,MPTZ.PRICE)
                                    ?}
                                 ?};
                                 {? _can_continue>0
                                 || {? DK.DK_C<>_dk_c || DK.DK_C:=_dk_c; DK.put(1) ?};
                                    exec('dk_sum','magdok_wspolne',ND.ref(),1)
                                 ?};
                                 DK.next() & _can_continue>0
                              !}
                           ?}
                        ?}
                     ?};
                     ND.cntx_pop();
                     DK.cntx_pop();
                     DK_C.cntx_pop()
                  ?};
                  _ndzl.next() & _can_continue>0
               !}
            ?}
         ?}
      ?}
   ?}
?};
ANZH.cntx_pop();
KKTL.cntx_pop();
TKTL.cntx_pop();
MPTZ.cntx_pop();
ZL.cntx_pop();
{? _can_continue>0
|| _result:=1
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 4d8ace46f8f5a06463c131fcab51b448e5ec8a2904e20d13f36018e3c6d8866b8cec978b5e025e3805fe6a86405542c8a4d5a70e8a1fbdc733fca4f3b39bdf7e7e99dc6b05809d5e67cd6ad30533ce7f136aa0cd318ade787db871c9288858289313fdbd4d7b789f9a20b7d24bb3d40f3ee4b4c7bf41ea8b129d3bcf39c81fcb

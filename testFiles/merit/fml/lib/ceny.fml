:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ceny.fml [2008]
:: Utworzony: 24.10.2006
:: Autor: Mario
::======================================================================================================================
:: Zawartość: Formuły do obsługi cenników
::======================================================================================================================


\rab_typ_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: wartość domyślna typu rabatu
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| {? BEER.NAWIG<>'' & cur_tab(1)=ZK_N & (ZK_N.T & ZK_N.T().R='W' | ZK_N.T=null & TYPYZAM.R='W')
   || ''
   |? BEER.NAWIG<>'' & cur_tab(1)=ND & TYPYDOK.DS<>'T'
   || ''
   |? BEER.NAWIG<>'' & cur_tab(1)=FAKS & BEER.SZ='Z'
   || 'P'
   || exec('get','#params',800812,2)
   ?}
|| ''
?}


\rab_typ_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: f3 typu rabatu
::----------------------------------------------------------------------------------------------------------------------

fld(~-fld);
_Tab:=cur_tfld();
{? _Tab.SPOBLRAB='W'
||
   popup(0,'Typy rabatu'@
      ,'P - procentowy (suma)      '@,,"fld('P')"
      ,'S - procentowy (kaskadowy) '@,,"fld('S')"
      ,'K - kwotowy (od ceny)      '@,,"fld('K')"
      ,'W - kwotowy (od wartości)  '@,,"fld('W')")
||
   popup(0,'Typy rabatu'@
      ,'P - procentowy (suma)      '@,,"fld('P')"
      ,'S - procentowy (kaskadowy) '@,,"fld('S')"
      ,'K - kwotowy (od ceny)      '@,,"fld('K')")
?}


\spp_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: wartość początkowa sposobu przeliczania pozycji walutowych
::----------------------------------------------------------------------------------------------------------------------

exec('get','#params',800814,2)


\czy_ed_rab_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: sprawdza uprawnienia do edycji rabatu w nagłówku
::   WY: 1-uprawniony do edycji, 0-wpp
::----------------------------------------------------------------------------------------------------------------------

_edit:={? cur_tab=FAKS || FAKS.SZ='Z' | FAKS.WHERE='K' || 0 ?};
_edit | exec('get','#params',2130,2,OPERATOR.USER)='W' | exec('get','#params',2130,2,OPERATOR.USER)='N'


\nag_rab_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem [tabela].RAB
::----------------------------------------------------------------------------------------------------------------------
{? exec('czy_ed_rab_n','ceny')
|| ''
|| exec('findfnrd','color')
?}


\rab_typ_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem typu rabatu
::   WE: [_a] - gdzie wywołane
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>'' || return(1) ?};
{? var_pres('_a')=type_of('') || _tab:=_a || _tab:='' ?};

DPOZ.WPR_R:=1;
{? var_pres('__gen')>0 & __gen
|| DPOZ.WPR_R:=0
|? _tab='ND' | cur_tab(1)=ND
|| DK.cntx_psh;
   DK.index('DOKMAG'); DK.prefix(ND.ref);
   DPOZ.WPR_R:=~DK.first;
   DK.cntx_pop
|? _tab='FAKS' | cur_tab(1)=FAKS
|| FAP.cntx_psh;
   FAP.index('FAP'); FAP.prefix(FAKS.ref);
   DPOZ.WPR_R:=~FAP.first;
   FAP.cntx_pop
|? _tab='ZK_N' | cur_tab(1)=ZK_N
|| ZK_P.cntx_psh;
   ZK_P.index('TYPN'); ZK_P.prefix('A','Z',ZK_N.ref);
   DPOZ.WPR_R:=~ZK_P.first;
   ZK_P.cntx_pop
|? _tab='OFE' | cur_tab(1)=OFE
|| OFP.cntx_psh;
   OFP.index('OFE_P'); OFP.prefix(OFE.ref);
   DPOZ.WPR_R:=~OFP.first;
   OFP.cntx_pop
?};
''


\spp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem sposobu przeliczania pozycji walutowych
::----------------------------------------------------------------------------------------------------------------------

exec('rab_typ_bd','ceny')


\spp_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed redakcji sposobu przeliczania pozycji walutowych
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_S:=fld;
exec('rab_typ_be','ceny')


\spp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: po redakcji sposobu przeliczania pozycji walutowych
::----------------------------------------------------------------------------------------------------------------------
fld(~-fld);
_wyn:=
   {? fld<>'T' & fld<>'N'
   || FUN.info('Niedozwolona wartość pola.\n\n Akceptowane wartości:\n'
               ' T - wartości walutowe przeliczane wg wartości\n'
               ' N - wartosci walutowe przeliczane wg ceny'@);
      0
   || 1
   ?};
{? _wyn & DPOZ.WPR_S<>fld & cur_tfld=ZK_N
||
   ZK_N.TR:=null
?};
_wyn


\czyrabp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: czy rabat procentowy
::----------------------------------------------------------------------------------------------------------------------
_a='P' | _a='S'


\rabztyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca znak zależnie od rodzaju rabatu
::   WE: _a - rodzaj rabatu
::   WY: znak
::----------------------------------------------------------------------------------------------------------------------
{? _a='S' || 'x' || '+' ?}


\d_rabp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem DISP.RABP
::   WY: kolor
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1)=REZ & ~__czycen
|| exec('findfnv','#color')
|| _tab:={? cur_tab(1)=REZ || BEER || cur_tab(1) ?};
   DISP.RABP:={? exec('czyrabp','ceny',TAZ.RAB_TYP) || _tab.RAB || _tab.RABK ?};
   {? {? _tab=FAP || exec('upr_cscz','ceny',1) || exec('upr_cs','ceny') ?}
   ||
      {? exec('czy_ed_rab_p','ceny')
      || ''
      || exec('findfnrd','color')
      ?}
   ?}
?}


\d_rab_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed wyświetleniem DISP.RAB
::   WE: [_a] - miejsce wywołania, ''(domyślnie)
::       _b-0/1 wołane z exec('wysw_kor','faktury_poz')
::   WY: zwraca kod koloru
::----------------------------------------------------------------------------------------------------------------------
_where:={? var_pres('_a')=type_of('') || _a || '' ?};
_wysw_kor:={? var_pres('_b')=type_of(0) || _b || 0 ?};

{? {? _where='' || cur_tab(1)=REZ || _where='REZ' ?} & ~__czycen
|| exec('findfnv','#color')
|| DISP.RAB:=
      {? exec('czyrabp','ceny',TAZ.RAB_TYP)
      || DISP.RAB
      || exec('kwota','ceny'
          ,exec('rabat','ceny',{? _where<>'' || ($_where)() || cur_tab(1) ?})
          ,exec('podstawa','ceny',{? _where<>'' || ($_where)() || cur_tab(1) ?},_wysw_kor)
          ,exec('precyzja','ceny',{? _where<>'' || ($_where)() || cur_tab(1) ?}))
      ?};
   {? {? {? _where='' || cur_tab(1)=FAP || _where='FAP' ?} || exec('upr_cscz','ceny',1) || exec('upr_cs','ceny') ?}
   || ''
   ?}
?}


\taz_tarsdo_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem TAZ.TAR_SDO
::  OLD: \taz_tarsdo_bd/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:={? cur_tab(1)=REZ || BEER || cur_tab(1) ?};

TAR_H.cntx_psh; TAP.cntx_psh; TAR.cntx_psh; SLO.cntx_psh();
{? _Tab.TAR_H || TAR_H.use(ref_name(_Tab.TAR_H)) ?};
{? _Tab.TAR_H().TAP || TAP.use(ref_name(TAR_H.TAP)) ?};
TAZ.TAR_SDO:={? _Tab.TAR_H().TAP().TAR().SD='G'
             || 'Gratis'@
             || 'Cennik'@+{? _Tab.TAR_H
                         || ' '+_Tab.TAR_H().WAL().KOD
                         || ''
                         ?}
             ?};
TAR_H.cntx_pop; TAP.cntx_pop; TAR.cntx_pop; SLO.cntx_pop();
~~


\taz_kod_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: przed wyświetleniem TAZ.KOD
::   WY: kolor
::----------------------------------------------------------------------------------------------------------------------

_Tab:={? cur_tab(1)=REZ || BEER || cur_tab(1) ?};
_tazupr:=1;
_p800830:=exec('get','#params',800830,2)='T';
TAZ.KOD:=TAZ.NAZ:='';
TAR.cntx_psh();
_ref:=
   {? _Tab=TAP || TAP.TARB
   |? _Tab=TAR || TAR.TARB
   |? _Tab=DISP || $DISP.TAR
   || _ref:='';
      TAR_H.cntx_psh;
      {? _Tab.TAR_H || TAR_H.use(ref_name(_Tab.TAR_H)) ?};
      {? _Tab.TAR_H().TAP
      || TAP.cntx_psh;
         TAP.use(ref_name(_Tab.TAR_H().TAP));
         _ref:=$_Tab.TAR_H().TAP().TAR;
         {? TAP.TAR().SD<>'G'
         || {? _p800830
            ||
               exec('taz_sd_set','ceny');
               _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
               _mgr:={? TAP.M || TAP.M().MGR || TAP.MGR ?};
               _mgrp:={? TAP.M || TAP.M().MGRP || TAP.MGRP ?};
               _tazupr:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr)
            ?}
         ?};
         TAP.cntx_pop
      ?};
      TAR_H.cntx_pop;
      _ref
   ?};
{? _ref<>''
||
   TAR.prefix();
   {? TAR.seek(_ref)
   ||
      TAZ.KOD:=TAR.KOD;
      TAZ.NAZ:=TAR.NAZ
   ?}
?};
TAR.cntx_pop();
{? _tazupr=0
|| 0
|| {? _Tab=DK | _Tab=FAP | _Tab=ZK_P | _Tab=OFP | _Tab=BEER
   || ''
   |? _Tab=TAP
   || {? TAP.TAR().KH=null & TAR.GRKH=null || exec('findfnv','#color') || '' ?}
   || ''
   ?}
?}


\taz_naz_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: przed wyświetleniem TAZ.NAZ
::   WY: kolor
::----------------------------------------------------------------------------------------------------------------------

exec('taz_kod_pw','ceny')


\upr_cscz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: uprawnienia do ceny sprzedaży lub ceny zakupu
::   WE: [_a]=2-rodzaj przekazany przez par._b; 1-aktualizuj bufor FAKS na podstawie FAP.FAKS; =0-wpp
::       [_b]=rodzaj ceny
::  OLD: \upr_cscz/firma.fml
::       \bd_fap_fld/faktury.fml
::       \bd_faksz_fld/faktury.fml
::       \bd_fkor_fld/faktury0.fml
::       \be_fkor_fld/faktury0.fml
::       \bd_faksv_fld/faktury0.fml
::       \bd_sum_fld/faktury0.fml
::       \bd_fakspl_fld/plat.fml
::----------------------------------------------------------------------------------------------------------------------

{? var_pres('_a')<>type_of(1) || _a:=0 ?};

FAKS.cntx_psh;
{? _a=1 || FAP.FAKS() ?};

_wyn:={? {? _=2 || _b || FAKS.SZ ?}='S' || exec('upr_cs','ceny') || exec('upr_cz','ceny') ?};

FAKS.cntx_pop;

_wyn


\upr_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: ustawia uprawnienia użytkownika
::  OLD: \upr_set/firma.fml
::----------------------------------------------------------------------------------------------------------------------

{? var_pres('LOGUPR',@.CLASS)<=100
||
   obj_decl('LOGUPR',
         obj_fld('CM',0),
         obj_fld('CS',0),
         obj_fld('CZ',0))
?};

VAR_DEL.delete('__Logupr');
__Logupr:=obj_new(@.CLASS.LOGUPR);

__Logupr.CM:=member('LOG_CM');
__Logupr.CS:=member('LOG_CS');
__Logupr.CZ:=member('LOG_CM')


\upr_cs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: uprawnienia do ceny sprzedaży
::   WY: 1-tak, 0-nie
::  OLD: \upr_cs/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('LOGUPR',@.CLASS)<=100 || exec('upr_set','ceny') ?};
__Logupr.CS=0


\upr_cz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: uprawnienia do ceny zakupu
::   WY: 1-tak, 0-nie
::  OLD: \upr_cz/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('LOGUPR',@.CLASS)<=100 || exec('upr_set','ceny') ?};
__Logupr.CM=0


\upr_cm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: uprawnienia do ceny magazynowej
::   WY: 1-tak, 0-nie
::  OLD: \upr_cm/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('LOGUPR',@.CLASS)<=100 || exec('upr_set','ceny') ?};
__Logupr.CM=0


\czy_ed_rab_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: sprawdza uprawnienia do edycji rabatu w pozycji
::   WY: 1-uprawniony do edycji, 0-wpp
::----------------------------------------------------------------------------------------------------------------------

FAKS.cntx_psh;
_edit:={? cur_tab(1)=FAP || FAP.FAKS().SZ='Z' || 0 ?};
FAKS.cntx_pop;
_tab:={? cur_tab(1)=REZ || BEER || cur_tab(1) ?};
TAR_H.cntx_psh; TAP.cntx_psh;
{? _tab.TAR_H || TAR_H.use(ref_name(_tab.TAR_H)) ?};
{? _tab.TAR_H().TAP || TAP.use(ref_name(_tab.TAR_H().TAP)) ?};
_edit:=
   _edit
   | (exec('get','#params',2130,2,OPERATOR.USER)='W' | exec('get','#params',2130,2,OPERATOR.USER)='P')
     & _tab.TAR_H().TAP().RAB_B<>'T';
TAR_H.cntx_pop; TAP.cntx_pop;
_edit


\kwota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: (procent*podstawa/100)$2
::   WE: _a-procent
::       _b-podstawa
::       [_c]-dokładność, domyślnie 2
::   WY: (procent*podstawa/100)$_c
::----------------------------------------------------------------------------------------------------------------------
_dokl:={? var_pres('_c')=type_of(0) & _c>=0 || _c || 2 ?};
{? _b || (_a*_b/100)$_dokl ?}


\podstawa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: podstawa wyliczania rabatu
::   WE: _a-uchwyt do tabeli
::       _b-0/1 wołane z exec('wysw_kor','faktury_poz')
::   WY: podstawa
::----------------------------------------------------------------------------------------------------------------------
_wysw_kor:=_b;

_wyn:=0;

_fapdisp:={? var_pres('__FAPDISP')=type_of(0) || __FAPDISP || 0 ?};

_tab:=
   {? _a=REZ
   || exec('licz','ceny',REZ);
      {? DPOZ.MJSR='F'
      || FAP
      |? DPOZ.MJSR='M'
      || DK
      |? DPOZ.MJSR='Z'
      || ZK_P
      || _a
      ?}
   || _a
   ?};

{? _tab=FAP
||
   FAP.FAKS();
   FAKS.cntx_psh;
   _wyn:=
      {? FAP.FAKS().WAL<>FAKS.NWAL
      || {? FAKS.KOR<>'' & (_wysw_kor | _fapdisp) || FKOR.BCWAL || FAP.CWAL ?}
      || {? FAKS.KOR<>'' & (_wysw_kor | _fapdisp) || FKOR.BCPR  || FAP.CPR ?}
      ?};
   _wyn:=
      {? 'W'*FAKS.RAB_TYP
      || (_wyn*{? FAKS.KOR<>'' & (_wysw_kor | _fapdisp) || FKOR.BIL || FAP.IL ?})$2
      || _wyn
      ?};
   FAKS.cntx_pop
|? _tab=DK
||
   ND.cntx_psh;
   _wyn:=
      {? DK.N().WAL<>ND.NWAL
      || DK.CWAL
      || {? FAKS.CB='T' || DK.CB  || DK.CN ?}
      ?};
   _wyn:=
      {? 'W'*ND.RAB_TYP
      || (_wyn*DK.IL2)$2
      || _wyn
      ?};
   ND.cntx_pop
|? _tab=ZK_P
||
   ZK_P.cntx_psh;
   _wyn:=
      {? ZK_P.N().WAL<>INFO.NAROD
      || ZK_P.CWAL
      || ZK_P.CENA
      ?};
   _wyn:=
      {? 'W'*ZK_N.RAB_TYP
      || (_wyn*ZK_P.IL2)$2
      || _wyn
      ?};
   ZK_P.cntx_pop
|? _tab=OFP
||
   ZK_P.cntx_psh;
   _wyn:=
      {? OFP.OFE().WAL<>INFO.NAROD
      || OFP.CWAL
      || OFP.C
      ?};
   _wyn:=
      {? 'W'*OFE.RAB_TYP
      || (_wyn*OFP.IL)$2
      || _wyn
      ?};
   ZK_P.cntx_pop
?};

_wyn


\rabat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca rabat % z nagłówka
::   WE: _a-uchwyt do tabeli
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

{? _a=FAP
||
   FAKS.cntx_psh;
   _wyn:=FAP.FAKS().RAB;
   FAKS.cntx_pop
|? _a=DK
||
   ND.cntx_psh;
   _wyn:=DK.N().RAB;
   ND.cntx_pop
|? _a=ZK_P
||
   ZK_P.cntx_psh;
   _wyn:={? ZK_P.N || ZK_P.N().RAB || TAZ.RAB ?};
   ZK_P.cntx_pop
|? _a=OFP
||
   OFP.cntx_psh;
   _wyn:=OFP.OFE().RAB;
   OFP.cntx_pop
|? _a=REZ
||
   _wyn:=TAZ.RAB
?};
_wyn


\taz_sd_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: ustawia TAZ.SD
::----------------------------------------------------------------------------------------------------------------------

TAZ.SD:={? BEER.SZ='S' || 'S' || 'D' ?}


\nag_rab_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcją [tabela].RAB
::----------------------------------------------------------------------------------------------------------------------
exec('czy_ed_rab_n','ceny')


\rab_typ_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcją typu rabatu
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_R


\spr_rab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: formuła po redakcji pola FAKS.RAB, FAP.RAB, ND.RAB, DK.RAB
::       funkcja sprawdza rabaty w systemie
::   WE: [_a] - wartość pola rabat do kontroli - domyślnie fld()
::       [_b] - 1-wolane z pola, 0-wpp
::       [_c] - ściezka dostępu do zmiennej na komunikat
::       [_d] - nazwa tabeli
::   WY: 1/0
::  OLD: \spr_rab/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=fld() ?} || _a:=fld() ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};
{? var_pres('_c')<>type_of('string') || _c:='' ?};
{? var_pres('_d')<>type_of('') || _d:=5+cur_tab.name() ?};

_wyn:=0;
:: sprawdzamy której tabeli dotyczy
_ntab:=_d;

{? 4+_ntab='rezz'
|| exec('licz','ceny',REZ);
   _ntab:=
      {? DPOZ.MJSR='F'
      || 'fakpo'
      |? DPOZ.MJSR='M'
      || 'dokma'
      |? DPOZ.MJSR='Z'
      || 'zkpoz'
      || _ntab
      ?}
?};

{? 4+_ntab='pj_n'
|| _ntab:=5+cur_tab(1).name() ?};

_rabp:=
   {? _ntab='faktu' | _ntab='fakpo'
   || exec('czyrabp','ceny',FAKS.RAB_TYP)
   |? _ntab='nagdo' | _ntab='dokma'
   || exec('czyrabp','ceny',ND.RAB_TYP)
   |? _ntab='zknag' | _ntab='zkpoz'
   || exec('czyrabp','ceny',ZK_N.RAB_TYP)
   |? _ntab='ofert' | _ntab='ofer_'
   || exec('czyrabp','ceny',OFE.RAB_TYP)
   || 0
   ?};

{? _b & TAZ.RAB_N_BE=0
   & (_ntab='faktu' | _ntab='nagdo' | _ntab='zknag' | _ntab='ofert')
||
   TAZ.RAB_N_BE:=_a
?};

{? _a=0
|| _wyn:=1
|? _ntab='fakpo'
||
:: pozycja faktury sprzedazy
   {? exec('itsPositive','#field',,,_a)
   || FAKS.cntx_psh;
      {? exec('czyrabp','ceny',FAP.FAKS().RAB_TYP)
      ||
         {? exec('rab_proc','ceny',_a,FAKS.RAB,FAKS.RAB_TYP)>=100
         || {? FAKS.RAB_TYP='P'
            || _txt:='Zbyt duży rabat z nagłówka i pozycji.\n Możliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(99.99-FAKS.RAB,,2)];
               FAP.RAB:=99.99-FAKS.RAB $2;
               {? _=0 || fld(FAP.RAB) ?};
               exec('licz','ceny',FAP)
            || _txt:='Zbyt duży rabat z nagłówka i pozycji.'@
            ?};
            {? _c=''
            || FUN.info(_txt)
            || ($_c)():=_txt
            ?}
         |? FAP.CPR>0 & FAP.WN=0 & FAP.IL>0 & _a+FAKS.RAB>0 & _c=''
         || _txt:='Zbyt duży rabat.\nWartość pozycji równa zero.'@
               +{? _rabp || '\n\nChcesz zmienić rabat?'@ || '' ?};
            _wyn:={? _rabp || ~FUN.ask(_txt) || FUN.info(_txt); 1 ?}
         || _wyn:=1
         ?}
      ||
         _war:=({? FAKS.WAL<>FAKS.NWAL || FAP.CWAL || FAP.CPR ?}*{? FAKS.RAB_TYP='K' || 1 || FAP.IL ?})$2;
         _rabn:=(FAKS.RAB*_war/100)$2;
         {? _war & _a+_rabn>=_war
         || _txt:='Za duża kwota rabatu.\nMożliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(_war-_rabn-0.01,,2)];
            {? _c=''
            || FUN.info(_txt)
            || ($_c)():=_txt
            ?};
            FAP.RABK:=_war-_rabn-0.01;
            {? _=0 || fld(FAP.RABK) ?};
            exec('licz','ceny',FAP)
         || _wyn:=1
         ?}
      ?};
      FAKS.cntx_pop
   ?}
|? _ntab='faktu'
||
:: nagłówek faktury sprzedaży
   {? exec('itsPositive','#field',FAKS.T().KOR='Z',,_a)
   ||
::    sprawdzamy po pozycjach faktury czy nie przekraczamy 100 %
      _fld:=_a $2;
      _max:=_fld;
      FAP.cntx_psh; FAKS.cntx_psh;
      FAP.index('FAP');
      FAP.prefix(FAKS.ref);
      {? FAP.first
      || {? exec('czyrabp','ceny',FAP.FAKS().RAB_TYP)
         || {? FAKS.RAB_TYP='P'
            || {!
            |?
               {? _fld+FAP.RAB>=0 || {? _max>(99.99-FAP.RAB) || _max:=99.99-FAP.RAB ?} ?};
               FAP.next
            !};
            {? _max<_fld
            || FUN.info('Z powodu wprowadzenia rabatu na pozycjach faktury\n(rabat dla ceny jest sumą rabatu z nagłówka i z pozycji faktury),\n'@
                 +'możliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(_max,,2)])
            || _wyn:=1
            ?}
            || {!
               |?
                  _wyn:=exec('rab_proc','ceny',FAP.RAB,_a,FAKS.RAB_TYP)<100;
                  _wyn & FAP.next
               !};
               {? _wyn=0 || FUN.info('Zbyt duży rabat z nagłówka i pozycji.'@) ?}
            ?}
         || _wyn:=1;
            _wal:=FAKS.WAL<>FAKS.NWAL;
            {!
            |?
               _war:=
                  ({? FAKS.KOR<>''
                  || exec('wysw_kor','faktury_poz');
                     {? _wal || FKOR.BCWAL || FKOR.BCPR ?}
                  || {? _wal || FAP.CWAL || FAP.CPR ?}
                  ?}*{? FAKS.RAB_TYP='K' || 1 || FAP.IL ?})$2;
               _rabn:=(_a*_war/100)$2;
               {? _war-FAP.RABK-_rabn<=0 || _wyn:=0 ?};
               _wyn & FAP.next
            !};
            {? _wyn=0 || FUN.info('Za duży rabat.'@) ?};
            _wyn
         ?}
      |? _max>99.99
      || FUN.info('Możliwe wprowadzenie maksymalnego rabatu o wartości: 99.99 \%'@)
      || _wyn:=1
      ?};
      FAP.cntx_pop; FAKS.cntx_pop;
      FAKS.RAB:=_max
   ?}
|? _ntab='dokma'
||
:: pozycja dokumentu magazynowego
   {? exec('itsPositive','#field',,,_a)
   || {? exec('czyrabp','ceny',DK.N().RAB_TYP)
      || {? exec('rab_proc','ceny',_a,ND.RAB,ND.RAB_TYP)>=100
         || {? ND.RAB_TYP='P'
            || _txt:='Zbyt duży rabat z nagłówka i pozycji.\nMożliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(99.99-ND.RAB,,2)];
               DK.RAB:=99.99-ND.RAB $2
            || _txt:='Zbyt duży rabat z nagłówka i pozycji.'@
            ?};
            {? _c=''
            || FUN.info(_txt)
            || ($_c)():=_txt
            ?}
         || exec('obl_wars','magdok_poz')
         ?};
         {? DK.CN>0 & DK.WN=0 & _a+ND.RAB>0 & _c=''
         || _txt:='Zbyt duży rabat.\nWartość pozycji równa zero.'@
               +{? _rabp || '\n\nChcesz zmienić rabat?'@ || '' ?};
            _wyn:={? _rabp || ~FUN.ask(_txt) || FUN.info(_txt); 1 ?}
         || _wyn:=1
         ?}
      ||
         _war:=({? ND.WAL<>ND.NWAL
                || DK.CWAL
                || {? ND.CB='T'
                   || DK.CB
                   || DK.CN
                   ?}
                ?}*{? ND.RAB_TYP='K' || 1 || DK.IL2 ?})$2;
         _rabn:=(ND.RAB*_war/100)$2;
         {? _war & _a+_rabn>=_war
         || _txt:='Za duża kwota rabatu.\nMożliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(_war-_rabn-0.01,,2)];
            {? _c=''
            || FUN.info(_txt)
            || ($_c)():=_txt
            ?};
            DK.RABK:=_war-_rabn-0.01;
            {? _=0 || fld(DK.RABK) ?};
            exec('licz','ceny',DK);
            _wyn:=0
         || _wyn:=1
         ?}
      ?}
   ?}
|? _ntab='nagdo'
||
:: nagłówek dokumentu magazynowego
   {? exec('itsPositive','#field',,,_a)
   ||
      {? ND.TYP().CS='N'
      || {? fld>99.99
         || FUN.info('Możliwe wprowadzenie maksymalnego rabatu o wartości: 99.99 \%'@)
         || _wyn:=1
         ?}
      ||
::    sprawdzamy po pozycjach faktury czy nie przekraczamy 100 %
         _fld:=_a $2;
         _max:=_fld;
         DK.cntx_psh; ND.cntx_psh;
         DK.index('DOKMAG');
         DK.prefix(ND.ref);
         {? DK.first
         || {? exec('czyrabp','ceny',DK.N().RAB_TYP)
            || {? ND.RAB_TYP='P'
               || {!
               |? {? _fld+DK.RAB>=0 || {? _max>(99.99-DK.RAB) || _max:=99.99-DK.RAB ?} ?};
                  exec('obl_wars','magdok_poz',,_fld);
                  DK.WN & DK.next
               !};
                  {? (DK.CWAL | DK.CN | DK.CB) & DK.WN=0
               || FUN.info('Za duży rabat.'@);
                  _wyn:=0
               |? _max<_fld
               || FUN.info('Z powodu wprowadzenia rabatu na pozycjach dokumentu\n(rabat dla ceny jest sumą rabatu z nagłówka i z pozycji dokumentu),\n'@+
                        'możliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(_max,,2)])
               || _wyn:=1
               ?}
               || {!
                  |?
                     _wyn:=exec('rab_proc','ceny',DK.RAB,_a,ND.RAB_TYP)<100;
                     _wyn & DK.next
                  !};
                  {? _wyn=0
                  || FUN.info('Zbyt duży rabat z nagłówka i pozycji.'@)
                  ?}
               ?}
            || _wyn:=1;
               _wal:=ND.WAL<>ND.NWAL;
               {!
               |?
                  {? {? _wal || DK.CWAL || {? ND.CB='T' || DK.CB || DK.CN ?} ?}
                  ||
                     _war:=
                        ({? _wal || DK.CWAL || {? ND.CB='T' || DK.CB || DK.CN ?} ?}
                        *{? ND.RAB_TYP='K' || 1 || DK.IL2 ?})$2;
                     _rabn:=(_a*_war/100)$2;
                     {? _war-DK.RABK-_rabn<=0 || _wyn:=0 ?}
                  ?};
                  _wyn & DK.next
               !};
               {? _wyn=0 || FUN.info('Za duży rabat.') ?};
               _wyn
            ?}
         |? _max>99.99
         || FUN.info('Możliwe wprowadzenie maksymalnego rabatu o wartości: 99.99 \%'@)
         || _wyn:=1
         ?};
         DK.cntx_pop; ND.cntx_pop;
         ND.RAB:=_max
      ?}
   ?}
|? _ntab='zkpoz'
||
:: pozycja zamówienia sprzedaży
   {? exec('itsPositive','#field',,,_a)
   ||
      {? exec('czyrabp','ceny',ZK_P.N().RAB_TYP)
      || {? exec('rab_proc','ceny',_a,ZK_N.RAB,ZK_N.RAB_TYP)>=100
         || {? ZK_N.RAB_TYP='P'
            || FUN.info('Zbyt duży rabat z nagłówka i pozycji.\nMożliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(99.99-ZK_N.RAB,,2)]);
               ZK_P.RAB:=99.99-ZK_N.RAB $2;
               exec('licz','ceny',ZK_P)
            || FUN.info('Zbyt duży rabat z nagłówka i pozycji.'@)
            ?}
         |? ZK_P.CENA>0 & ZK_P.CN=0 & _a+ZK_N.RAB>0
         || _txt:='Zbyt duży rabat.\nWartość pozycji równa zero.'@
               +{? _rabp || '\n\nChcesz zmienić rabat?'@ || '' ?};
            _wyn:={? _rabp || ~FUN.ask(_txt) || FUN.info(_txt); 1 ?}
         || _wyn:=1
         ?}
      || _war:=({? ZK_N.WAL<>INFO.NAROD || ZK_P.CWAL || ZK_P.CENA ?}*{? ZK_N.RAB_TYP='K' || 1 || ZK_P.IL2 ?})$2;
         _rabn:=(ZK_N.RAB*_war/100)$2;
         {? _war & _a+_rabn>=_war
         || FUN.info('Za duża kwota rabatu.\nMożliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(_war-_rabn-0.01,,2)]);
            ZK_P.RABK:=_war-_rabn-0.01;
            {? _=0 || fld(ZK_P.RABK) ?};
            exec('licz','ceny',ZK_P)
         || _wyn:=1
         ?}
      ?}
   ?}
|? _ntab='zknag'
||
:: nagłówek zamówienia sprzedaży
   {? exec('itsPositive','#field',,,_a)
   ||
::    sprawdzamy po pozycjach zamówienia czy nie przekraczamy 100 %
      _fld:=_a $2;
      _max:=_fld;
      ZK_P.cntx_psh; ZK_N.cntx_psh;
      ZK_P.index('TYPN');
      ZK_P.prefix('A','Z',ZK_N.ref(),1);
      {? ZK_P.first
      || {? exec('czyrabp','ceny',ZK_N.RAB_TYP)
         || {? ZK_N.RAB_TYP='P'
            || {!
            |? {? _fld+ZK_P.RAB>=0 || {? _max>(99.99-ZK_P.RAB) || _max:=99.99-ZK_P.RAB ?} ?};
               ZK_P.next
            !};
            {? _max<_fld
            || FUN.info('Z powodu wprowadzenia rabatu na pozycjach zamówienia\n(rabat dla ceny jest sumą rabatu z nagłówka i z pozycji zamówienia),\n'@+
                   'możliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(_max,,2)])
            || _wyn:=1
            ?}
            || {!
               |?
                  _wyn:=exec('rab_proc','ceny',ZK_P.RAB,_a,ZK_N.RAB_TYP)<100;
                  _wyn & ZK_P.next
               !};
               {? _wyn=0 || FUN.info('Zbyt duży rabat z nagłówka i pozycji.'@) ?}
            ?}
         || _wyn:=1;
            _wal:=ZK_N.WAL<>INFO.NAROD;
            {!
            |?
               _war:=({? _wal || ZK_P.CWAL || ZK_P.CENA ?}*{? ZK_N.RAB_TYP='K' || 1 || ZK_P.IL2 ?})$2;
               _rabn:=(_a*_war/100)$2;
               {? _war-ZK_P.RABK-_rabn<=0 || _wyn:=0 ?};
               _wyn & ZK_P.next
            !};
            {? _wyn=0 || FUN.info('Za duży rabat.'@) ?};
            _wyn
         ?}
      |? _max>99.99
      || FUN.info('Możliwe wprowadzenie maksymalnego rabatu o wartości: 99.99 \%'@)
     || _wyn:=1
      ?};
      ZK_P.cntx_pop; ZK_N.cntx_pop;
      ZK_N.RAB:=_max
   ?}
|? _ntab='ofer_'
||
:: pozycja oferty sprzedaży
   {? exec('itsPositive','#field',,,_a)
   || {? exec('czyrabp','ceny',OFP.OFE().RAB_TYP)
      || {? exec('rab_proc','ceny',_a,OFE.RAB,OFE.RAB_TYP)>=100
         || {? _c=''
            || {? OFE.RAB_TYP='P'
               ||
                  FUN.info('Zbyt duży rabat z nagłówka i pozycji.\nMożliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(99.99-OFE.RAB,,2)]);
                  OFP.RAB:=(99.99-OFE.RAB)$2;
                  exec('licz','ceny',OFP,'RABP')
               || FUN.info('Zbyt duży rabat z nagłówka i pozycji.'@)
               ?}
            || ($_c)():='Zbyt duży rabat z nagłówka i pozycji.'@
            ?}
         |? OFP.C>0 & OFP.CN=0 & _a+OFE.RAB>0
         || {? _c=''
            || _txt:='Zbyt duża rabat.\nWartość pozycji równa zero.'@
               +{? _rabp || '\n\nChcesz zmienić rabat?'@ || '' ?};
            _wyn:={? _rabp || ~FUN.ask(_txt) || FUN.info(_txt); 1 ?}
            || ($_c)():='Zbyt duża wartość rabatu - cena sprzedaży równa zero (rabat dla ceny jest sumą rabatu z nagłówka i z pozycji dokumentu).'@
            ?}
         || _wyn:=1
         ?}
      || _war:=({? OFE.WAL<>INFO.NAROD || OFP.CWAL || OFP.C ?}*{? OFE.RAB_TYP='K' || 1 || OFP.IL ?})$2;
         _rabn:=(OFE.RAB*_war/100)$2;
         {? _war & _a+_rabn>=_war
         || {? _c=''
         || FUN.info('Za duża kwota rabatu.\nMożliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(_war-_rabn-0.01,,2)])
            || ($_c)():='Za duża kwota rabatu.\nMożliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(_war-_rabn-0.01,,2)]
            ?};
            OFP.RABK:=_war-_rabn-0.01;
            {? _=0 || fld(OFP.RABK) ?};
            exec('licz','ceny',OFP,'RABP')
         || _wyn:=1
         ?}
      ?}
   ?}
|? _ntab='ofert'
||
:: nagłówek oferty sprzedaży
   {? exec('itsPositive','#field',,,_a)
   ||
::    sprawdzamy po pozycjach zamówienia czy nie przekraczamy 100 %
      _fld:=_a $2;
      _max:=_fld;
      OFP.cntx_psh; OFE.cntx_psh;
      OFP.index('OFE_P');
      OFP.prefix(OFE.ref());
      {? OFP.first
      ||
         {? exec('czyrabp','ceny',OFP.OFE().RAB_TYP)
         || {? OFE.RAB_TYP='P'
            || {!
               |?
                  {? _fld+OFP.RAB>=0 || {? _max>(99.99-OFP.RAB) || _max:=99.99-OFP.RAB ?} ?};
               OFP.next
            !};
            {? _max<_fld
            || FUN.info('Z powodu wprowadzenia rabatu na pozycjach oferty\n(rabat dla ceny jest sumą rabatu z nagłówka i z pozycji oferty),\n'@+
                   'możliwe wprowadzenie maksymalnego rabatu o wartości: %1.'@[form(_max,,2)])
            || _wyn:=1
            ?}
            || {!
               |?
                  _wyn:=exec('rab_proc','ceny',OFP.RAB,_a,OFE.RAB_TYP)<100;
                  _wyn & OFP.next
               !};
               {? _wyn=0 || FUN.info('Zbyt duży rabat z nagłówka i pozycji.'@) ?}
            ?}
         || _wyn:=1;
            _wal:=OFE.WAL<>INFO.NAROD;
            {!
            |?
               _war:=({? _wal || OFP.CWAL || OFP.C ?}*{? OFE.RAB_TYP='K' || 1 || OFP.IL ?})$2;
               _rabn:=(_a*_war/100)$2;
               {? _war-OFP.RABK-_rabn<=0 || _wyn:=0 ?};
               _wyn & OFP.next
            !};
            {? _wyn=0 || FUN.info('Za duży rabat.'@) ?};
            _wyn
         ?}
      |? _max>99.99
      || FUN.info('Możliwe wprowadzenie maksymalnego rabatu o wartości: 99.99 \%'@)
      || _wyn:=1
      ?};
      OFP.cntx_pop; OFE.cntx_pop;
      OFE.RAB:=_max
   ?}
|? cur_tab(1)=TAP
||
   _wyn:=fld()>=0 & fld()<100;
   {? _wyn=0
   || FUN.info('Rabat musi zawierać sie w przedziale <0;100).'@)
   |? TAP.CEN & TAP.CEN<TAP.RABNET
   || FUN.info('Rabat netto musi być mniejszy od ceny netto.'@);
      _wyn:=0
   |? TAP.CENB & TAP.CENB<TAP.RABBRT
   || FUN.info('Rabat brutto musi być mniejszy od ceny brutto.'@);
      _wyn:=0
   ?}
|? _ntab<>'palet'
||
:: w innych przypadkach
   _wyn:=fld()>=0 & fld()<100
?};
_wyn


\rab_typ_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: po redakcji typu rabatu
::----------------------------------------------------------------------------------------------------------------------

fld(~-fld);
_Tab:=cur_tfld();
{? exec('get','#params',800810,2)='W'
||
   {? fld<>'P' & fld<>'S' & fld<>'K' & fld<>'W' & {? _Tab=KH || fld<>'' || 1 ?}
   || FUN.info('Wybrany typ rabatu niemożliwy, ponieważ w parametrach systemu wybrano sposób obliczania rabatu wg '@+
         'wartości.\n\n Dla tej konfiguracji możliwe typy do wyboru to:      \n'@
         +{? _Tab=KH || 'Domyślny               \n'@ || '' ?}
         +' Procentowy (suma)      \n Procentowy (kaskadowy) \n Kwotowy (od ceny)      \n Kwotowy (od wartości)  '@);
      0
   || 1
   ?}
||
   {? fld<>'P' & fld<>'S' & fld<>'K' & {? _Tab=KH || fld<>'' || 1 ?}
   || FUN.info('Wybrany typ rabatu niemożliwy, ponieważ w parametrach systemu wybrano sposób obliczania rabatu wg ceny.'@
         +'\n\n Dla tej konfiguracji możliwe typy do wyboru to:      \n'@
         +{? _Tab=KH || 'Domyślny               \n'@ || '' ?}
         +' Procentowy (suma)      \n Procentowy (kaskadowy) \n Kwotowy (od ceny)      '@);
      0
   || 1
   ?}

?}


\zk_rab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: ustala rabat na zamowieniu
::   WE: FAKS.ref
::  OLD: \zk_rab/zk2.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=obj_new(2);
_wyn[1]:=_wyn[2]:=0;

FAP.cntx_psh;
FAP.index('FAP');FAP.prefix(_a);
_first:=FAP.first;
_nd:=null;

{? BEER.NAWIG='M' & _first=0
:: wystawianie faktury z biezacego dokumentu magazynowego
||
   _nd:=ND.ref
|? _first
:: pozostale przypadki
||
   DK.cntx_psh;
   DK.index('FAP'); DK.prefix($FAP.ref);
   {? DK.first || _nd:=DK.N ?};
   DK.cntx_pop
?};

FAP.cntx_pop;

{? _nd
||
   ZK_RN.cntx_psh; ZK_N.cntx_psh;
   ZK_RN.index('ND'); ZK_RN.prefix($_nd);
   {? ZK_RN.first
   ||
      _wyn[1]:=1;
      _wyn[2]:=ZK_RN.N().RAB
   ||
      OKR.cntx_psh;
      OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
      {? OKR.first
      ||
         _loop:=1;
         {!
         |?
            ZK_RN.use('zkhin'+ST.ODDZ+($OKR.ROK+2));
            ZK_RN.index('ND'); ZK_RN.prefix($_nd);
            {? ZK_RN.first
            ||
               ZK_N.use(ref_name(ZK_RN.N));
               _wyn[1]:=1;
               _wyn[2]:=ZK_RN.N().RAB;
               _loop:=0
            ?};
            _loop & OKR.next
         !}
      ?};
      OKR.cntx_pop
   ?};
   ZK_RN.cntx_pop; ZK_N.cntx_pop
?};

_wyn


\rab_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: obliczanie procentu rabatu zaleznie od _c jako sumu lub kaskadowo
::   WE: _a - % rabatu
::       _b - $ rabatu
::       _c - typ rabatu procentowego
::          ='P' - suma
::          ='S' - kaskadowy
::   WY: % rabatu
::----------------------------------------------------------------------------------------------------------------------
{? _c='S'
|| 100-((100-_a)*(100-_b)/100)$2
|| _a+_b
?}


\obl_rabat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2011]
:: OPIS: oblicza wartosc na podstawie rabatu procentowego
::   WE: _a - wartosc brutto lub netto
::       _b - rabat %
::   WY: _wyn - wyliczona wartosc
::  OLD: \obl_rabat/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

_metoda:=exec('get','#params',800811,1);
{? _metoda=1
||
:: metoda 1
:: wart_rab = ( (1- ( rabat/100 ) ) * wartosc )
   _wyn:=((1-(_b/100))*_a)$2
||
:: metoda 2
:: rabat_kw = (wartosc * rabat)/100
:: wart_rab = wartosc - rabat_kw
   _wyn:=_a-(((_a*_b)/100)$2)
?};
_wyn


\chk_rab_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: sprawdzenie rabatu po redakcji naglowka
::   WE: _a - tabela
::       _b - 0-na dołącz, 1-na popraw
::----------------------------------------------------------------------------------------------------------------------
{? TAZ.RAB_N_BE=-1
:: zmiana rabatu została zaakceptowana w ramach bieżącej edycji więc kolejne zmiany są akceptowane bez pytania
|| return('')
?};

_Tab:=_a;
_popraw:={? var_pres('_b')=type_of(0) || _b || 0 ?};

_wyn:='';

{? _Tab=ND & ND.TYP().CS<>'T' || return(_wyn) ?};

_p2130:='WN'*exec('get','#params',2130,2,OPERATOR.USER)=0;

_popraw:=
   {? type_of(params_get())>0
      & var_pres('context',params_get())>0
      & var_pres('mp',params_get().context)>0
:: pozostałe przypadki
   || _mp:=params_get().context.mp;
      _mp.pathTodo() | _mp.akcja()='Popraw'
   |? type_of(params_get())>0
      & var_pres('mp',params_get())>0
:: dla ofert
   || _mp:=params_get().mp;
      _mp.pathTodo() | _mp.akcja()='Popraw'
   ?};

exec('taz_sd_set','ceny');
_kh_rab:=exec('sp_plat','kontrahent',_Tab.KH().ref,1);
_zk_rab:=exec('zk_rab','ceny',_Tab.ref);
_txt:='Rabat z dokumentu: %1'@[form(_Tab.RAB,,2,'9,')];
_ask:=0;
_but1:=_but2:=_but3:=0;
{? _kh_rab & _Tab.RAB<>_kh_rab || _but1:=1; _txt+='\n\nRabat kontrahenta: %1'@[form(_kh_rab,,2,'9,')] ?};
{? _zk_rab[1] & _Tab.RAB<>_zk_rab[2] || _but2:=1; _txt+='\n\nRabat z zamówienia: %1'@[form(_zk_rab[2],,2,'9,')] ?};
{? _popraw & _Tab.RAB<>TAZ.RAB_N_BE || _but3:=1; _txt+='\n\nRabat przed poprawą: %1'@[form(TAZ.RAB_N_BE,,2,'9,')] ?};
_txt+={? _p2130 || '' || '\n\nKtóry rabat wybierasz?'@ ?};

_but1txt:={? _but1 || 'Kontrahenta'@ || '' ?};
_but2txt:={? _but2 || 'z Zamówienia'@ || '' ?};
_but3txt:={? _but3 || 'Przed'@ || '' ?};

_choice:=-1;
{? _but1 | _but2 | _but3
||
   {? _p2130
   ||
      _choice:=0;
      FUN.info(_txt)
   ||
      _choice:=($("FUN.choice(_a,1,'Z &dokumentu'@"
         +{? _but1 || ",'Kontrahenta'@" || "" ?}
         +{? _but2 || ",'Z &zamówienia'@" || "" ?}
         +{? _but3 || ",'Przed poprawą'@" || "" ?}
         +")"))(_txt)
   ?}
?};

_ask:=
   {? _choice=0         || 0
   |? _choice=2 & _but1 || 1
   |? _choice=2 & _but2 || 2
   |? _choice=2 & _but3 || 3
   |? _choice=3 & _but2 || 2
   |? _choice=3 & _but3 || 3
   |? _choice=4 & _but3 || 3
   || -1
   ?};

{? _ask=0 || _wyn:='RAB'
|? _ask=1 || _Tab.RAB:=_kh_rab
|? _ask=2 || _Tab.RAB:=_zk_rab[2]
|? _ask=3 || _Tab.RAB:=TAZ.RAB_N_BE
?};

{? ~exec('spr_rab','ceny',_Tab.RAB,0) || _wyn:='RAB' ?};
_wyn:=exec('rab_2130','ceny',_Tab,_wyn);
{? _wyn='' | _p2130 || TAZ.RAB_N_BE:=-1 ?};

_wyn


\rab_2130
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca odpowiedni akronim pola po uwzglednieniu formuly 2130
::   WE: _a - tabela
::       _b - wstepny akronim
::       _c - miejsce
::   WY: akronim po uwzglednieniu formuly 2130
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<>type_of(1) || _c:=0 ?};

{? _b='' || return('') ?};

{? _a=FAKS & 'WN'*exec('get','#params',2130,2,OPERATOR.USER)=0 || 'FORMAPLA'
|? _a=ND & 'WN'*exec('get','#params',2130,2,OPERATOR.USER)=0 || 'KH'
|? _a=OFE & 'WN'*exec('get','#params',2130,2,OPERATOR.USER)=0 || 'FORMAPLA'
|? _a=ZK_N & 'WN'*exec('get','#params',2130,2,OPERATOR.USER)=0 || 'FORMAPLA'
|? _a=FAP & 'WP'*exec('get','#params',2130,2,OPERATOR.USER)=0 || {? FAP.FAKS().WAL=FAKS.NWAL || 'CPR' || 'CWAL' ?}
|? _a=DK & 'WP'*exec('get','#params',2130,2,OPERATOR.USER)=0 || {? DK.N().WAL=ND.NWAL
                                                                || {? ND.CB='T' || 'CB' || 'CN' ?}
                                                                || 'CWAL'
                                                                ?}
|? _a=OFP & 'WP'*exec('get','#params',2130,2,OPERATOR.USER)=0 || {? OFP.OFE().WAL=INFO.NAROD || 'C' || 'CWAL' ?}
|? _a=ZK_P & _c=0 & 'WP'*exec('get','#params',2130,2,OPERATOR.USER)=0 || {? ZK_P.N().WAL=INFO.NAROD
                                                                         || 'CENA'
                                                                         || 'CWAL'
                                                                         ?}
:: dodawanie pozycji zamowienia z rezerwacji tymczasowych
|? _a=ZK_P & _c=1 & 'WP'*exec('get','#params',2130,2,OPERATOR.USER)=0 || {? ZK_P.N().WAL=INFO.NAROD
                                                                         || 'CENR'
                                                                         || 'CWLR'
                                                                         ?}
|| _b
?}


\clr_promo_tap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: czyści pola [tabela].PROMO, [tablea].TAP
::   WE: [_a] - tabela
::       [_b] - wartość
::       [_c] - waluta-SLO.ref
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(FIRMA)
|| _a:={? cur_tab(1)=REZ || BEER || cur_tab(1) ?}
?};
{? var_pres('_b')<>type_of(1) || _b:=0 ?};
{? var_pres('_c')<>type_of(null) || _c:=null ?};

TAR_H.cntx_psh; TAP.cntx_psh; TAR.cntx_psh;
{? _a.TAR_H || TAR_H.use(ref_name(_a.TAR_H)) ?};
{? _a.TAR_H().TAP || TAP.use(ref_name(TAR_H.TAP)) ?};

{? _a.TAR_H().TAP().TAR().SD='G'
||
   1
||
   _clr:=
      {? _<=1
::    zerowanie
      || 1
::    sprawdzenie rabatu
      |? _=2
      || 0
::    sprawdzenie ceny
      || _a.TAR_H().WAL<>_c
      ?};

   {? _clr=0 & _a.TAR_H().TAP
   ||
::    sprawdzenie ceny
      {? _c &
         {? TAR_H.CEN_OD
         || TAR_H.CEN & (TAR_H.CEN_OD>_b | _b>TAR_H.CEN)
         || TAR_H.CEN & TAR_H.CEN<>_b
         ?}
      || _clr:=1
::    sprawdzenie rabatu
      |? _c=null & TAR_H.RAB & TAR_H.RAB<>_b
      || _clr:=1
      ?}
   ?};
:: zerowanie
   {? _clr & _a.PROMO=_a.TAR_H().TAP().PROMO || _a.PROMO:=null ?};
   {? _clr || TAR_H.prefix; _a.TAR_H(); {? TAR_H.count=0; 0 || TAR_H.del ?}; _a.TAR_H:=null; _a.TAR_TMS:=0 ?}
?};

TAR_H.cntx_pop; TAP.cntx_pop; TAR.cntx_pop


\ctdt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: wartość KSTE.CT lub KSTE.DT
::----------------------------------------------------------------------------------------------------------------------
{? TAZ.SD='S' || KSTE.CT || KSTE.DT ?}


\fo8008x1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: wartość parametru 8008x1
::----------------------------------------------------------------------------------------------------------------------
{? TAZ.SD='S' || exec('get','#params',800801,2) || exec('get','#params',800821,2) ?}


\fo8008x3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: wartość parametru 8008x1
::----------------------------------------------------------------------------------------------------------------------
{? TAZ.SD='S' || exec('get','#params',800803,2) || exec('get','#params',800823,2) ?}


\licz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: wylicza wartosci pozycji po zmianie
::   WE: _a - tabela
::       _b - akronim pola
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of('') || _b:='' ?};

_wyn:=0;

_tab:=
   {? _a=REZ
   || {? DPOZ.MJSR='F'
      || FAP.blank();
         FAP.IL:=FAP.IL2:=BEER.TYMREA;
         FAP.CPR:=BEER.CSP;
         FAP.WAL:=BEER.WALREZ;
         FAP.CWAL:=BEER.CWL;
         FAP.KRS:=BEER.KRSREZ;
         FAP.RAB:=BEER.RAB;
         FAP.RABK:=BEER.RABK;
         FAP
      |? DPOZ.MJSR='M'
      || DK.blank();
         DK.IL:=DK.IL2:=BEER.TYMREA;
         {? TAZ.CB='T'
         || DK.CB:=BEER.CSP
         || DK.CN:=BEER.CSP
         ?};
         DK.WAL:=BEER.WALREZ;
         DK.CWAL:=BEER.CWL;
         DK.KURS:=BEER.KRSREZ;
         DK.RAB:=BEER.RAB;
         DK.RABK:=BEER.RABK;
         DK
      |? DPOZ.MJSR='Z'
      || FAP.blank();
         ZK_P.ILZ:=ZK_P.IL2:=BEER.TYMREA;
         ZK_P.CENA:=BEER.CSP;
         ZK_P.WAL:=BEER.WALREZ;
         ZK_P.CWAL:=BEER.CWL;
         ZK_P.KRS:=BEER.KRSREZ;
         ZK_P.RAB:=BEER.RAB;
         ZK_P.RABK:=BEER.RABK;
         ZK_P
      || _a
      ?}
   || _a
   ?};

{? _tab=FAP
|| FAKS.cntx_psh;
   _wyn:=exec('liczfak','faktury_wspolne',,,_b<>'CWAL');
   {? _wyn
   ||
      _wyn:=exec('liczfak_ist','faktury_wspolne',1,1)
   ?};
   FAKS.cntx_pop
|? _tab=DK
||
   _wyn:=exec('obl_wars','magdok_poz')
|? _tab=ZK_P
||
   exec('war_pozz','zamsiw_poz');
   _wyn:=1
|? _tab=OFP
|| _wyn:=exec('ae_ofpww','ceny_dok')
|? _tab=ZD_POZ
||
:: [rr] obliczenie wartości
   _wyn:=exec('zezdp_ce','zamdst_poz')
?};

{? _a=REZ
|| {? DPOZ.MJSR='F'
   || BEER.CSP:=FAP.CPR;
      BEER.CWL:=FAP.CWAL
   |? DPOZ.MJSR='M'
   || BEER.CSP:={? TAZ.CB='T' || DK.CB || DK.CN ?};
      BEER.CWL:=DK.CWAL
   |? DPOZ.MJSR='Z'
   || BEER.CSP:=ZK_P.CENA;
      BEER.CWL:=ZK_P.CWAL
   ?}
?};
_wyn


\st_licz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: argumenty exec('st_licz','ceny')
::----------------------------------------------------------------------------------------------------------------------
_args:=obj_new('RAB','RAB_TYP','CB','CZY_WAL','WAL','KRS','SPOBLRAB','SPP','KH','KH_ODB','PL');
:: procent rabatu z nagłówka
_args.RAB:=0;
:: typ rabatu [P/K]
_args.RAB_TYP:='';
:: czy ceny brutto [T/N]
_args.CB:='';
:: czy walutowy [1/O]
_args.CZY_WAL:=0;
:: waluta SLO.ref()
_args.WAL:=null();
:: kurs na walutę opodatkowania
_args.KRS:=0;
:: sposób liczenia rabatu [W/C]
_args.SPOBLRAB:='';
:: sposób przeliczania pozycji walutowych
_args.SPP:='';
:: kontrahent KH.ref
_args.KH:=null();
:: odbiorca KH_ODB.ref
_args.KH_ODB:=null();
:: sposob platności SLO.ref
_args.PL:=null();
:: tablica argumentów
_args


\st_licz_wz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: ustawia stałe potrzebne do obliczania wartości na pozycjach dokumentu - wzorce dla _a
::   WE: _a - objekt
::          ='FAKS' - dokumenty sprzedaży
::          ='ND' - dokumenty rozchodowe
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('st_licz_a','ceny');
{? _a='FAKS'
|| _params.RAB:=FAKS.RAB;
   _params.RAB_TYP:=FAKS.RAB_TYP;
   _params.CB:={? FAKS.SZ='S' || FAKS.T().FIS || FAKS.CB ?};
   _params.CZY_WAL:=FAKS.WAL<>FAKS.NWAL;
   _params.WAL:=FAKS.WAL;
   _params.KRS:=FAKS.KRS;
   _params.SPOBLRAB:=FAKS.SPOBLRAB;
   _params.SPP:=FAKS.T().SPP;
   _params.KH:=FAKS.KH;
   _params.KH_ODB:=FAKS.KH_ODB;
   _params.PL:=FAKS.PL;
   exec('st_licz','ceny',_params)
|? _a='ND'
|| _params.RAB:=ND.RAB;
   _params.RAB_TYP:=ND.RAB_TYP;
   _params.CB:=ND.CB;
   _params.CZY_WAL:=ND.WAL<>ND.NWAL;
   _params.WAL:=ND.WAL;
   _params.KRS:=ND.KRS;
   _params.SPOBLRAB:=ND.SPOBLRAB;
   _params.SPP:=ND.SPP;
   _params.KH:=ND.KH;
   _params.KH_ODB:=ND.KH_ODB;
   _params.PL:=null();
   exec('st_licz','ceny',_params)
|? _a='ND1'
|| _params.RAB:=ND.RAB;
   _params.RAB_TYP:=ND.RAB_TYP;
   _params.CB:=ND.CB;
   _params.CZY_WAL:={? DK.PLUS='N' || ND.WAL<>ND.NWAL || ND.WAL<>exec('bl_nwal_mg','ustawienia') ?};
   _params.WAL:=ND.WAL;
   _params.KRS:=ND.KRS;
   _params.SPOBLRAB:=ND.SPOBLRAB;
   _params.SPP:=ND.SPP;
   _params.KH:=ND.KH;
   _params.KH_ODB:=ND.KH_ODB;
   _params.PL:=null();
   exec('st_licz','ceny',_params)
|? _a='OFE'
|| _params.RAB:=OFE.RAB;
   _params.RAB_TYP:=OFE.RAB_TYP;
   _params.CB:=OFE.CB;
   _params.CZY_WAL:=OFE.WAL<>exec('bl_nwal','ustawienia');
   _params.WAL:=OFE.WAL;
   _params.KRS:=OFE.KRS;
   _params.SPOBLRAB:=OFE.SPOBLRAB;
   _params.SPP:=OFE.SPP;
   _params.KH:=OFE.KH;
   _params.KH_ODB:=OFE.ODB;
   _params.PL:=OFE.PL;
   exec('st_licz','ceny',_params)
|? _a='ZK_N'
|| _params.RAB:=ZK_N.RAB;
   _params.RAB_TYP:=ZK_N.RAB_TYP;
   _params.CB:=ZK_N.CB;
   _params.CZY_WAL:=ZK_N.WAL<>exec('bl_nwal','ustawienia');
   _params.WAL:=ZK_N.WAL;
   _params.KRS:=ZK_N.KRS;
   _params.SPOBLRAB:=ZK_N.SPOBLRAB;
   _params.SPP:=ZK_N.SPP;
   _params.KH:=ZK_N.KH;
   _params.KH_ODB:=ZK_N.ODB;
   _params.PL:=ZK_N.PL;
   exec('st_licz','ceny',_params)
|? _a='ZD_NAG'
|| _params.RAB:=0;
   _params.RAB_TYP:='';
   _params.CB:='N';
   _params.CZY_WAL:=ZD_NAG.WAL<>exec('bl_nwal','ustawienia');
   _params.WAL:=ZD_NAG.WAL;
   _params.KRS:=ZD_NAG.KRS;
   _params.SPOBLRAB:='';
   _params.SPP:='';
   _params.KH:=ZD_NAG.KH;
   _params.KH_ODB:=null();
   _params.PL:=ZD_NAG.PL;
   exec('st_licz','ceny',_params)
|? _a='PD_K'
|| _params.RAB:=0;
   _params.RAB_TYP:='';
   _params.CB:='N';
   _params.CZY_WAL:=PD_K.WAL<>exec('bl_nwal','ustawienia');
   _params.WAL:=PD_K.WAL;
   _params.KRS:=PD_K.KRS;
   _params.SPOBLRAB:='';
   _params.SPP:='';
   _params.KH:=PD_K.KH;
   _params.KH_ODB:=null();
   _params.PL:=null();
   exec('st_licz','ceny',_params)
?}


\st_licz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: ustawia stałe potrzebne do obliczenia wartości na pozycjach dokumentu - stałe wynikające z nagłówka
::   WE: exec('st_licz_a','ceny')
::----------------------------------------------------------------------------------------------------------------------
_paramsa:=_a;

TAZ.RAB:=_paramsa.RAB;
TAZ.RAB_TYP:=_paramsa.RAB_TYP;
TAZ.CB:=_paramsa.CB;
TAZ.CZY_WAL:=_paramsa.CZY_WAL;
TAZ.WAL:=_paramsa.WAL;
TAZ.KRS:=_paramsa.KRS;
TAZ.SPOBLRAB:=_paramsa.SPOBLRAB;
TAZ.SPP:=_paramsa.SPP;
TAZ.KH:=_paramsa.KH;
TAZ.KH_ODB:=_paramsa.KH_ODB;
TAZ.PL:=_paramsa.PL


\stplicz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: ustawia stałe potrzebne do obliczania wartości na pozycjach dokumentu - stałe wynikające z pozycji
::   WE: _[1] - ilość z pozycji
::----------------------------------------------------------------------------------------------------------------------
TAZ.IL:=_[1]


\ae_poz_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: po redakcji pola WAL na pozycjach dokumentu
::   WE: _a  typ MFZOW  M - magazyn F - faktury,zakupy Z - zamówienia sprzedazy
::                      O - oferty W - zamówienia dostaw
::   WY: 1-wypełnione 0-niewypełnione
::  OLD: \ae_poz_wal/eurusd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;

_przel:=1;
{? fld=null
|| FUN.info('Należy podać walutę.'@);
   _wyn:=0
|| TAR_H.cntx_psh;
   {? _a<>'W' & _a<>'P' & cur_tab(1).TAR_H || TAR_H.use(ref_name(cur_tab(1).TAR_H)) ?};
   {? _a<>'W' & _a<>'P' & (cur_tab(1).TAR_H=null()
    | DPOZ.WPR_S=$cur_tab(1).TAR_H().WAL) & DPOZ.WPR_S<>$fld & cur_tab(1).CWAL
   || cur_tab(1).CWAL:=0;
      {? _a='M' || DK.CN:=DK.CB:=0
      |? _a='F' || FAP.CPR:=0
      |? _a='Z' || ZK_P.CENA:=0
      |? _a='O' || OFP.C:=0
      ?};
      exec('clr_promo_tap','ceny');
      _przel:=0
   ?};
   {? fld=
         {? 'WP'*_a
            | _a='M' & ND.TYP().P='T'
         || exec('bl_nwal_mg','ustawienia')
         || exec('bl_nwal','ustawienia')
         ?}
   || {? _a='M' || DK.KURS:=0;DK.CWAL:=0
      |? _a='F' || FAP.KRS:=0;FAP.CWAL:=0
      |? _a='Z' || ZK_P.KRS:=0;ZK_P.CWAL:=0
      |? _a='O' || OFP.KRS:=0;OFP.CWAL:=0
      |? _a='W' || ZD_POZ.KRS:=0;ZD_POZ.CWAL:=0
      |? _a='P' & cur_afld()='WALK' || PD_K.KRS:=0; PD_K.CWAL:=0
      |? _a='P' & cur_afld()='WAL' & DPOZ.WPR_S<>$cur_tfld.WAL || exec('pd_k_obl','plan_dostaw')
      ?}
   || {? _a='P' & cur_afld()='WAL' & DPOZ.WPR_S<>$cur_tfld.WAL
      || PD_K.WALK:=PD_K.WAL; PD_K.KRS:=0; PD_K.CWAL:=0
      |? _a='P' & cur_afld()='WALK' & DPOZ.WPR_S<>$cur_tfld.WALK
      || PD_K.KRS:=0; PD_K.CWAL:=0; PD_K.CENA:=0; exec('pd_k_obl','plan_dostaw')
      ?}
   ?};
   {? DPOZ.WPR_S<>$cur_tfld.WAL
   ||
      {? _a='M' || DK.C2:=0
      |? _a='W' || ZD_POZ.CENA2:=0
      ?}
   ?};
   {? DPOZ.WPR_S<>$cur_tfld.WAL & KSTE.BANKRS<>null() & INFO.NAROD<>cur_tfld.WAL
   || exec('pkurs','eurusd',KSTE.BANKRS,KSTE.TYPKRS);
      {? _przel || exec('przel_cwal','ceny') ?};
      exec('po_cenaw','ceny')
   ?};
   TAR_H.cntx_pop
?};
{? _wyn=1 || win_disp ?};
_wyn


\przel_cwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [12.30]
:: OPIS: Procedura przelicza cenę walutową według kursu i ceny pln o ile wartości są różne od zera.
::   WE: _a - 0 - przeliczać tylko gdy cena walutowa zerowa; 1 przeliczać zawsze cenę w walucie;
::   WY: ~~
::  OLD: \przel_cwal/eurusd.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=0 ?};
 _wal:=cur_tfld.WAL;
 {? type_of(_wal)=type_of('') || _wal:=exec('FindInSet','#table','SLO','SL',_wal,XINFO.SLWAL) ?};
 _wal_tkrs:=INFO.NAROD;
 _krs:=$('cur_tfld.KRS');
 _cwal:=$('cur_tfld.CWAL');
 _cenanarod:=$('cur_tfld.CENA');
 _dokl:=2;
 _tab:=exec('tab_acr','#table',cur_tfld.name);
{? _tab='FAP'
||
    _cenanarod:=$('cur_tfld.CPR');
    _dokl:={? FAKS.SZ='Z' || FAP.M().DOKL_C || 2 ?}
|? _tab='DK'
||
     _krs:=$('cur_tfld.KURS');
     _cenanarod:=$('cur_tfld.CN');
     _dokl:={? DK.PLUS='T' || DK.M().DOKL_C || 2 ?}
|? _tab='ZK_P'
||
   _cenanarod:=$('cur_tfld.CENA');
   _dokl:=2
|? _tab='ZD_POZ'
||
   _cenanarod:=$('cur_tfld.CENA');
   _dokl:=ZD_POZ.M().DOKL_C
|? _tab='OFP'
||
   _cenanarod:=$('cur_tfld.C');
   _dokl:=2
?};
{? _wal <> _wal_tkrs &  _krs()<>0 &  _cenanarod()<>0
||
   {? _a=0 & _cwal()=0
   || _cwal():=(_cenanarod()/_krs()) $_dokl
   |? _a=1
   || _cwal():=(_cenanarod()/_krs()) $_dokl
   ?}
?};
~~


\po_cenaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przeliczenie do ceny w PLN-ach
::   WE: _a=0-zerować TAP, PROMO
::       _b=1-wołane z pola, =0-wpp
::   WY: czy można opuścić pole 1 - tak, 0 -nie
::  OLd: \po_cenaw/eurusd.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};
{? var_pres('_b')<>type_of(1) || _b:=1 ?};

_wyn:=1;
_dokl:=2;
_trans:=~_b & do_state();

{? ~_trans & type_of(cur_tab(1))>0 & cur_tab(1)=REZ
|| BEER.KRSREZ:=BEER.KRSREZ$4;
   {? _b
   || {?  cur_afld='CWL'
      || {? DPOZ.WPR_R=fld
         || return(1)
         || {? _a || exec('clr_promo_tap','ceny',,fld,BEER.WALREZ) ?}
         ?}
      |? cur_afld='KRSREZ'
      || {? DPOZ.WPR_R=fld
         || return(1)
         ?}
      ?}
   ?};
   _cwal:="BEER.CWL";
   _krs:="BEER.KRSREZ";
   _cen:="BEER.CSP";
   _spr:=BEER.WALREZ=exec('bl_nwal','ustawienia') & BEER.WALREZ<>BEER.WALCSP & BEER.CSP<>0;
   _walj:=exec('FindInSet','#table','WAL','WAL_SYM',BEER.WALREZ().KOD,,"WAL.J",,,1);
   _form:="exec('licz','ceny',REZ); {? _a || win_disp() ?}";
   _dokl:=REZ.M().DOKL_S
|? BEER.MW='M'
:: magazyn
|| DK.KURS:=DK.KURS$4;
   {? _b
   || {? cur_afld='CWAL'
      || {? DPOZ.WPR_R=fld
         || return(1)
         || {? _a || exec('clr_promo_tap','ceny',,fld,DK.WAL) ?}
         ?}
      |? cur_afld='KURS'
      || {? DPOZ.WPR_R=fld
         || return(1)
         ?}
      ?}
   ?};
   _cwal:="DK.CWAL";
   _krs:={? DK.PLUS='N' & DK.N().WAL=ND.NWAL & ND.NWAL<>INFO.NAROD || "1" || "DK.KURS" ?};
   _cen:={? DK.PLUS='T'
         || "DK.C"
         || {? DK.N().CB='T' || "DK.CB" || "DK.CN" ?}
         ?};
   _spr:=
      {? DK.PLUS='T'
      || ND.WAL=exec('bl_nwal_mg','ustawienia') & ND.WAL<>DK.WAL & DK.C<>0
      || ND.WAL=exec('bl_nwal','ustawienia') & ND.WAL<>DK.WAL & DK.CN<>0
      ?};
   _walj:=exec('FindInSet','#table','WAL','WAL_SYM',DK.WAL().KOD,,"WAL.J",,,1);
   _form:=
      {? DK.PLUS='T'
      || "exec('war_mag','magdok_poz'); {? _a || win_disp() ?}"
      || "exec('obl_wars','magdok_poz'); {? _a || win_disp() ?}"
      ?};
   _dokl:={? DK.PLUS='T' || DK.M().DOKL_C || DK.M().DOKL_S ?}
|? BEER.MW='F'
:: faktury
|| FAP.KRS:=FAP.KRS$4;
   {? _b
   || {? cur_afld='CWAL'
      || {? DPOZ.WPR_R=fld
         || return(1)
         || {? _a & (FAP.POZP<>0 | FAP.FAKS().KOR='')
            || exec('clr_promo_tap','ceny',,fld,FAP.WAL)
            ?}
         ?}
      |? cur_afld='KRS'
      || {? DPOZ.WPR_R=fld
         || return(1)
         ?};
         exec('po_krs','eurusd')
      ?}
   ?};
   {? FAP.SV=null
   || FAP.SV:=exec('m_vat','material',M.ref(),FAP.FAKS().KH,
         FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N',FAKS.AR,FAKS.AM,,,,FAKS.SZ,FAKS.KRAJ_VAT)
   ?};
   _cwal:="FAP.CWAL";
   _krs:="FAP.KRS";
   _cen:="FAP.CPR";
   _spr:=FAKS.WAL=exec('bl_nwal','ustawienia') & FAKS.WAL<>FAP.WAL & FAP.CPR<>0;
   _walj:=exec('FindInSet','#table','WAL','WAL_SYM',FAP.WAL().KOD,,"WAL.J",,,1);
   _form:="exec('liczfak','faktury_wspolne',,,0); exec('liczfak_ist','faktury_wspolne',1,0); {? _a || win_disp() ?}";
   _dokl:={? FAKS.SZ='S'
          || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
          |? FAKS.SZ='Z'
          || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
          || 2
          ?}
|? BEER.MW='Z'
:: zamowienia sprzedazy
|| ZK_P.KRS:=ZK_P.KRS$4;
   {? _b
   || {? cur_afld='CWAL' | cur_afld='CWLR'
      ||
         {? DPOZ.WPR_R=fld
         || return(1)
         || {? _a || exec('clr_promo_tap','ceny',,fld,ZK_P.WAL) ?}
         ?}
      |? cur_afld='KRS' | cur_afld='KRSR'
      || {? DPOZ.WPR_R=fld
         || return(1)
         ?}
      ?}
   ?};
   _cwal:="ZK_P.CWAL";
   _krs:="ZK_P.KRS";
   _cen:="ZK_P.CENA";
   _spr:=ZK_N.WAL=exec('bl_nwal','ustawienia') & ZK_N.WAL<>ZK_P.WAL & ZK_P.CENA<>0;
   _walj:=exec('FindInSet','#table','WAL','WAL_SYM',ZK_P.WAL().KOD,,"WAL.J",,,1);
   _form:="exec('war_pozz','zamsiw_poz'); {? _a || win_disp() ?}";
   _dokl:=ZK_P.M().DOKL_S
|? BEER.MW='O'
:: oferty
|| OFP.KRS:=OFP.KRS$4;
   {? _b
   || {? cur_afld='CWAL'
      ||
         {? DPOZ.WPR_R=fld
         || return(1)
         || {? _a || exec('clr_promo_tap','ceny',,fld,OFP.WAL) ?}
         ?}
      |? cur_afld='KRS'
      || {? DPOZ.WPR_R=fld
         || return(1)
         ?}
      ?}
   ?};
   _cwal:="OFP.CWAL";
   _krs:="OFP.KRS";
   _cen:="OFP.C";
   _spr:=OFE.WAL=exec('bl_nwal','ustawienia') & OFE.WAL<>OFP.WAL & OFP.C<>0;
   _walj:=exec('FindInSet','#table','WAL','WAL_SYM',OFP.WAL().KOD,,"WAL.J",,,1);
   _form:="exec('ae_ofpww','ceny_dok'); {? _a || win_disp() ?}";
   _dokl:=OFP.M().DOKL_S
|? BEER.MW='W'
:: zamowienia dostaw
|| ZD_POZ.KRS:=ZD_POZ.KRS$4;
   {? _b
   || {? cur_afld='CWAL' | cur_afld='KRS'
      || {? DPOZ.WPR_R=fld || return(1) ?}
      ?}
   ?};
   _cwal:="ZD_POZ.CWAL";
   _krs:="ZD_POZ.KRS";
   _cen:="ZD_POZ.CENA";
   _spr:=ZD_NAG.WAL=INFO.NAROD & ZD_NAG.WAL<>ZD_POZ.WAL & ZD_POZ.CENA<>0;
   _walj:=exec('FindInSet','#table','WAL','WAL_SYM',ZD_POZ.WAL().KOD,,"WAL.J",,,1);
   _form:="exec('zezdp_ce','zamdst_poz'); {? _a || win_disp() ?}";
   _dokl:=ZD_POZ.M().DOKL_Z
|? BEER.MW='P'
:: koszyk planu dostaw
|| PD_K.KRS:=PD_K.KRS$4;
   {? _b
   || {? cur_afld='CWAL' | cur_afld='KRS'
      || {? DPOZ.WPR_R=fld || return(1) ?}
      ?}
   ?};
   _cwal:="PD_K.CWAL";
   _krs:="PD_K.KRS";
   _cen:="PD_K.CENA";
   _spr:=PD_K.WAL=INFO.NAROD & PD_K.WAL<>PD_K.WALK & PD_K.CENA<>0;
   _walj:=exec('FindInSet','#table','WAL','WAL_SYM',PD_K.WAL().KOD,,"WAL.J",,,1);
   _form:="exec('pd_k_obl','plan_dostaw'); {? _a || win_disp() ?}";
   _dokl:=PD_K.M().DOKL_Z
|? BEER.MW='K'
:: zakup
|| FAP.KRS:=FAP.KRS$4;
   {? _b
   || {? cur_afld='CWAL'
      ||
         {? DPOZ.WPR_R=fld
         || return(1)
         || {? _a & (FAP.POZP<>0 | FAP.FAKS().KOR='')
            || exec('clr_promo_tap','ceny',,fld,FAP.WAL)
            ?}
         ?}
      |? cur_afld='KRS'
      || {? DPOZ.WPR_R=fld
         || return(1)
         ?}
      ?}
   ?};
   {? FAP.SV=null
   || FAP.SV:=exec('m_vat','material',M.ref(),FAP.FAKS().KH,
         FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N',FAKS.AR,FAKS.AM,,,,FAKS.SZ,FAKS.KRAJ_VAT)
   ?};
   _cwal:="FAP.CWAL";
   _krs:="FAP.KRS";
   _cen:="FAP.CPR";
   _spr:=FAKS.WAL=exec('bl_nwal','ustawienia') & FAKS.WAL<>FAP.WAL & FAP.CPR<>0;
   _walj:=exec('FindInSet','#table','WAL','WAL_SYM',FAP.WAL().KOD,,"WAL.J",,,1);
   _form:="exec('liczfak','faktury_wspolne',,,0); exec('liczfak_ist','faktury_wspolne',1,0); {? _a || win_disp() ?}";
   _dokl:={? FAKS.SZ='S'
          || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
          |? FAKS.SZ='Z'
          || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
          || 2
          ?}
?};

{? _spr & _krs()<>0 & _cen()<>0 & (_cen()/_krs()*_walj $_dokl)=_cwal()
|| _pln:=_cen()
|? _krs()=0
|| _cen():=_cpr:=0 ; _form(_b)
|| _pln:=_cwal()*_krs()/_walj $_dokl
?};
_cpr:=_cen() $_dokl;
{? _cwal() < 0
||
   FUN.info('Cena nie może być ujemna.'@); _wyn:=0
|? _krs() <0
||
   FUN.info('Kurs nie może być ujemny.'@); _wyn:=0
||
   {? _krs()>0
   ||
      {? _cpr & _pln<>_cpr
      || _cen():=_pln; _form(_b)
      |? ~_cpr
      || _cen():=_pln; _form(_b)
      ?}
   || _cwal()
   ?};
   {? _b || win_disp() ?}
?};
{? BEER.MW='F' || exec('set_efld_opt','faktury_poz') ?};
_wyn


\promo_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed redagowaniem promocji
::----------------------------------------------------------------------------------------------------------------------
BEER.SZ='S' & (FAP.POZP=0 & FAP.FAKS().KOR='' | FAP.POZP<>0 & FAP.FAKS().KOR<>'')


\czy_cena_z_tap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: czy wymagana cena z cennika sprzedaży
::   WE: _a - gdzie wołana
::          ='FAP_KOR' - pozycja dokumentu sprzedaży
::       _b - 0-bez komunikatow, 1-z komunikatami
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of(1) || _b:=1 ?};

_Tab:=($_a)();

_wyn:=obj_new('WYN','MSG');
_wyn.WYN:=0;
_wyn.MSG:='';

TAR_H.cntx_psh; TAP.cntx_psh; TAR.cntx_psh;
{? _Tab.TAR_H || TAR_H.use(ref_name(_Tab.TAR_H)) ?};
{? _Tab.TAR_H().TAP || TAP.use(ref_name(_Tab.TAR_H().TAP)) ?};
_ret:=_Tab.TAR_H().TAP().TAR().SD='G';
TAR_H.cntx_pop; TAP.cntx_pop; TAR.cntx_pop;

:: pomijanie kotnroli dla gratisów
{? _ret || return(_wyn) ?};

:: pomijanie kontroli dla zamówien internetowych
_zam_www:='';
{? _a='ZK_P' &  _zam_www<>'' & ZK_N.T().T=_zam_www || return(_wyn) ?};

_il:=_war:=_sv:=0;
_mat:=_jm:=_j2:=null();
_ws2:=1;
{? _a='FAP'
|| exec('st_licz_wz','ceny','FAKS');
   _il:=exec('dok_pozil','ceny_dok',FAP);
   _sv:=exec('dok_pozsv','ceny_dok',FAP);
   _war:=exec('dok_pozwa','ceny_dok',FAP);
   _mat:=FAP.M;
   _jm:={? FAP.M().J2<>null() & FAP.JM<>null() || FAP.JM || FAP.M().J ?};
   _j2:=FAP.J2;
   {? FAP.WS2 || _ws2:=FAP.WS2 ?}
|? _a='DK'
|| exec('st_licz_wz','ceny','ND');
   _il:=exec('dok_pozil','ceny_dok',DK);
   _sv:=exec('dok_pozsv','ceny_dok',DK);
   _war:=exec('dok_pozwa','ceny_dok',DK);
   _mat:=DK.M;
   _jm:={? DK.M().J2<>null() & DK.JM<>null() || DK.JM || DK.M().J ?};
   _j2:=DK.J2;
   {? DK.WS2 || _ws2:=DK.WS2 ?}
|? _a='OFP'
|| exec('st_licz_wz','ceny','OFE');
   _il:=exec('dok_pozil','ceny_dok',OFP);
   _sv:=exec('dok_pozsv','ceny_dok',OFP);
   _war:=exec('dok_pozwa','ceny_dok',OFP)
|? _a='ZK_P'
|| exec('st_licz_wz','ceny','ZK_N');
   _il:=exec('dok_pozil','ceny_dok',ZK_P);
   _sv:=exec('dok_pozsv','ceny_dok',ZK_P);
   _war:=exec('dok_pozwa','ceny_dok',ZK_P);
   _mat:=ZK_P.M;
   _jm:={? ZK_P.J2<>null() & ZK_P.JM<>null() || ZK_P.JM || ZK_P.M().J ?};
   _j2:=ZK_P.J2;
   {? ZK_P.WS2 || _ws2:=ZK_P.WS2 ?}
?};

{? _jm=_j2
|| _dokl_s:=_dokl_m:=exec('jaka_dok_m','jm',_mat)
|| _dokl_s:=exec('jaka_dok_mjm','jm',_mat,_j2,_jm);
   _dokl_m:=exec('jaka_dok_m','jm',_mat)
?};

{? _dokl_s<0 || _dokl_s:=_dokl_m ?};

{? BEER.SZ='S' & KSTE.CT='T' & exec('get','#params',2131,2,OPERATOR.USER)='T' & _Tab.TAR_H=null
|| _wyn.WYN:=1;
   _wyn.MSG:='Wymagany cennik.'@;
   {? _b || FUN.info(_wyn.MSG) ?}
||
   {? _Tab.TAR_H
   ||
      TAR_H.cntx_psh;
      TAR_H.use(ref_name(_Tab.TAR_H));
      _wgewi:=exec('czyJS','jm',_mat,_j2);
      {? _wgewi
      || {? _j2<>null() & _j2=_Tab.TAR_H().JM || _ws2:=1 ?}
      || {? _jm<>null() & _jm=_Tab.TAR_H().JM || _ws2:=1 ?}
      ?};
      _t_il:=(_Tab.TAR_H().IL*_ws2)$_dokl_s;
      _t_ildo:=(TAR_H.IL_DO*_ws2)$_dokl_s;
      {? TAZ.CB='T'
      || _t_war:=(_ws2*(1+_sv)*TAR_H.WAR)$2;
         _t_wardo:=(_ws2*(1+_sv)*TAR_H.WAR_DO)$2
      || _t_war:=(_ws2*TAR_H.WAR)$2;
         _t_wardo:=(_ws2*TAR_H.WAR_DO)$2
      ?};
      {? BEER.SZ='S' & KSTE.CT='T' & _t_ildo=0 & _t_il & _t_il>_il
      || _wyn.WYN:=1;
         _wyn.MSG:='Cennik dostępny dla minimalnej ilości %1 %2.'@[form(_t_il,,_Tab.M().DOKL,'.,'),TAR_H.JM().KOD];
         {? _b || FUN.info(_wyn.MSG) ?}
      |? BEER.SZ='S' & KSTE.CT='T' & _t_ildo<>0 & (_t_il>_il | _il>=_t_ildo)
      || _wyn.WYN:=1;
         _wyn.MSG:='Cennik dostępny dla ilości z zakresu [%1; %2) %3.'@[form(_t_il,,_Tab.M().DOKL,'.,'),form(_t_ildo,,_Tab.M().DOKL,'.,'),TAR_H.JM().KOD];
         {? _b || FUN.info(_wyn.MSG) ?}
      |? BEER.SZ='S' & KSTE.CT='T' & _t_wardo=0 & _t_war & _t_war>_war
      || _wyn.WYN:=1;
         _wyn.MSG:={? TAZ.CB='T' || 'Cennik dostępny dla minimalnej wartości brutto %1 %2.'@[form(_t_war,,2,'.,'),TAR_H.WAL().KOD]
                                 || 'Cennik dostępny dla minimalnej wartości netto %1 %2.'@[form(_t_war,,2,'.,'),TAR_H.WAL().KOD] ?};
         {? _b || FUN.info(_wyn.MSG) ?}
      |? BEER.SZ='S' & KSTE.CT='T' & _t_wardo<>0 & (_t_war>_war | _war>=_t_wardo)
      || _wyn.WYN:=1;
         _wyn.MSG:={? TAZ.CB='T' || 'Cennik dostępny dla wartości  brutto z zakresu [%1; %2) %3.'@[form(_t_war,,2,'.,'),form(_t_wardo,,2,'.,'),TAR_H.WAL().KOD]
                                 || 'Cennik dostępny dla wartości  netto z zakresu [%1; %2) %3.'@[form(_t_war,,2,'.,'),form(_t_wardo,,2,'.,'),TAR_H.WAL().KOD]
                    ?};
         {? _b || FUN.info(_wyn.MSG) ?}
      ?};
      TAR_H.cntx_pop
   ?}
?};

_wyn


\obl_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: oblicza wartość pozycji dla argumentów
::   WE: _a - rabat
::       _b - cena
::       _c - ilosc
::   WY: wartość pozycji
::----------------------------------------------------------------------------------------------------------------------
_rab:=_a;
_cen:=_b;
_il:=_c;
_dokl_c:=2;

{? exec('czyrabp','ceny',TAZ.RAB_TYP)
||
   _rabat2:=exec('rab_proc','ceny',_rab,TAZ.RAB,TAZ.RAB_TYP);
   _rabat:=1-(_rabat2)/100
||
   _dokl_c:=2;
   _cena:=_cen;
   _kwil:=_il;
   _nagwsp:=-TAZ.RAB/100;
   _kwpoz:=-_rab;
   _kwrab:=
      {? TAZ.RAB_TYP='W'
      || _kwpoz+(_nagwsp*((_cena*_kwil)$2))$2
      || ((_kwpoz+(_nagwsp*_cena)$_dokl_c)*_kwil)$2
      ?};
   _kwrabj:=
      {? TAZ.RAB_TYP='W'
      || {? _kwil || (_kwpoz/_kwil)$_dokl_c ?}+(_nagwsp*_cena)$_dokl_c
      || _kwpoz+(_nagwsp*_cena)$_dokl_c
      ?}
?};
{? exec('czyrabp','ceny',TAZ.RAB_TYP)
||
   {? TAZ.SPOBLRAB='W'
   || exec('obl_rabat','ceny',_cen*_il,_rabat2)$2
   || ((_cen*_rabat)$_dokl_c*_il)$2
   ?}
||
   {? TAZ.SPOBLRAB='W'
   || (_cen*_il)$2+_kwrab
   || ((_cen+_kwrabj)*_il)$2
   ?}
?}


\d_rabp_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed redakcją DISP.RABP
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1)=REZ & ~__czycen
|| 0
|| DPOZ.WPR_R:=fld;
   {? {? cur_tab(1)=FAP || exec('upr_cscz','ceny',1) || exec('upr_cs','ceny') ?}
   || exec('czy_ed_rab_p','ceny')
   ?}
?}


\d_rabp_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: po redakcji DISP.RABP
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? fld<0
|| FUN.info('Rabat nie może być mniejszy od zera.'@); _wyn:=0
|| _tab:={? cur_tab(1)=REZ || BEER || cur_tab(1) ?};
   {? DPOZ.WPR_R<>fld
   || {? _tab<>FAP | _tab=FAP & (FAP.POZP<>0 | FAP.FAKS().KOR='')
      || exec('clr_promo_tap','ceny',,fld)
      ?};
      _tab.RAB:=_tab.RABK:=0;
      {? exec('czyrabp','ceny',TAZ.RAB_TYP) || _tab.RAB:=fld || _tab.RABK:=fld ?};
      _wyn:=exec('licz','ceny',cur_tab(1));
      {? _wyn || win_disp ?}
   ?}
?};
_win:=cur_tab().win_edit('?');
{? _win='WREDKH' | _win='WREDK' || exec('set_efld_opt','faktury_poz') ?};
_wyn


\taz_kod_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: przed redakcją TAZ.KOD
::   WY: 1-redakcja możliwa, 0-wpp
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab=TAP
|| {? TAP.TAR().KH=null & TAR.GRKH=null || 0 || 1 ?}
|| 1
?}


\taz_kod_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: klawisz F3 TAZ.KOD
::   WY: wartość pola TAZ.KOD
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_ref:=null;
TAR.cntx_psh();
TAR.index('KHGRKH'); TAR.prefix(ST.ODDZ,TAZ.SD,null,null);
_win:=TAR.mk_sel('Cenniki standardowe'@,,0);
TAR.win_fld(_win,,'KOD',,,10,,,'Kod'@);
TAR.win_fld(_win,,'NAZ',,,30,,,'Kod'@);
TAR.win_act(_win,0,'Formuła','&Wybierz'@@,,,"sel_exit()",,1,,,,'W');
TAR.win_sel(_win);
{? TAR.seek(TAR.TARB) || _wyn:=TAR.KOD ?};
{? TAR.select(,1,2)
|| _ref:=TAR.ref;
   _wyn:=TAR.KOD;
   TAZ.NAZ:=TAR.NAZ
?};
TAR.cntx_pop();
{? _ref || {? cur_tab=TAP || TAP.TARB:=$_ref |? cur_tab=TAR || TAR.TARB:=$_ref || DISP.TAR:=_ref ?} || _wyn:=~~ ?};
_wyn


\taz_kod_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: po redakcji TAZ.KOD
::   WY: 1-pole poprawnie wypełnione, 0-wpp
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? TAZ.KOD=''
|| TAZ.NAZ:='';
   {? cur_tab=TAP || TAP.TARB:='' || TAR.TARB:='' ?}
|| _ref:='';
   TAR.cntx_psh();
   TAR.index('KOD'); TAR.prefix(ST.ODDZ,TAZ.SD,TAZ.KOD);
   {? TAR.first()
   || {!
      |? {? TAZ.KOD=TAR.KOD || _ref:=$TAR.ref(); TAZ.NAZ:=TAR.NAZ ?};
         TAR.next()
      !}
   ?};
   TAR.cntx_pop();
   {? _ref=''
   || FUN.info('Brak cennika o kodzie %1.'@[TAZ.KOD]); _wyn:=0
   || {? cur_tab=TAP || TAP.TARB:=_ref || TAR.TARB:=_ref ?}
   ?}
?};
_wyn


\sel_tary
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wyświetlenie nagłówków cenników
::   WE: [_a] - [S]przedaży, [D]ostawców, [G]ratisy
::       [_b] -   jakie cenniki wyswietlać
::            0  - wszystkie
::            1  - kontrahenta
::            2  - grupy kontrahentów
::       [_c] 1-nie usuwaj zmiennej 0(domyślnie)-usuwaj
::   WY: 0 - cenniki niedostępne, 1 - cenniki dostępne
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='S' ?} || _a:='S' ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};
{? _>=3 || {? type_of(_c)<>1 || _c:=0 ?} || _c:=0 ?};
_wyn:=1;
HELP.WHERE:='';
HELP.WMAG:=0;
TAZ.SD:=_a;
BEER.TAB:='';
{? _a='S' & (KSTE.CT='T' ) | _a='D' & KSTE.DT='T' | _a='G'
||
   {? _b=0
   ||
      KALK.KH:=null;
      KALK.GRKH:=null
   ?};
   exec('ustaw_ww','eurusd','F');

::   title(($ST.T)());
   {? KALK.KH<>null
   ||
      TAP.index('KH');
      TAP.prefix(ST.ODDZ,TAZ.SD,KALK.KH)
   |? KALK.GRKH<>null
   ||
      TAP.index('GRKH');
      TAP.prefix(ST.ODDZ,TAZ.SD,KALK.GRKH)
   ||
      TAP.index('RODZ');
      TAP.prefix(ST.ODDZ,TAZ.SD)
   ?};
   params_exec('okno_tar','ceny',_b)
||
   FUN.info('Parametr obsługi cenników nie jest włączony.\nEdycja cenników nie jest możliwa.'@);
   _wyn:=0
?};
TAZ.UPR:=1;
{? var_pres('ceny')>100 || VAR_DEL.delete(ceny) ?};
_wyn


\okno_tar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: struktura drzewa dla nagłówkow cennika
::   WE: [_a] -   jakie cenniki wyświetlać
::            0  - wszystkie
::            1  - kontrahenta
::            2  - grupy kontrahentow
::----------------------------------------------------------------------------------------------------------------------
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)>0
|| _link:=_params.LINK
|| _link:=''
?};

exec('zakr_get','ceny');
VAR_DEL.delete('__ZAKR_CEN','__sd');
__ZAKR_CEN:=_a;
__sd:=TAZ.SD;
exec('utw_ceny','ceny',__ZAKR_CEN,1);
TAZ.SD:='G'; exec('utw_ceny','ceny',__ZAKR_CEN,0); TAZ.SD:=__sd;
_kh_dict:=KH.win_dict('?');
KH.win_dict('WER');
KH.actions('WER','W');
params_set(params_get());
_win_acr:=ceny.mk_sel(,'P',,'weiajhstdkalojs',,,,1);

ceny.win_link(_win_acr,"
   _a:=obj_new('LINK');
   TAR.seek(ceny.REF);
   _a.LINK:=TAR.uidref();
   _a
");

ceny.win_sel(_win_acr);

ceny.win_fld(_win_acr,,'OPIS',,,21,,,'Rodzaje'@);
ceny.win_fld(_win_acr,,'KH_OPIS',,,-13,,,'Kontrahent/Grupa'@);

ceny.win_act(_win_acr,0,'Formuła','&Dołącz'@@,,'Dołącznie bieżącego zapisu'@,,"exec('tar_add','ceny',1,__ZAKR_CEN)",,,,,'D');
ceny.win_act(_win_acr,0,'Formuła','&Popraw'@@,,'Poprawienie bieżącego zapisu'@,,"exec('tar_add','ceny',2,__ZAKR_CEN)",,,,,'P');
ceny.win_act(_win_acr,0,'Formuła','&Usuń'@@,,'Usunięcie bieżącego zapisu'@,,"exec('tar_usun','ceny')",,,,,'U');
ceny.win_act(_win_acr,0,'Formuła','&Kopiuj'@@,,,,"exec('tar_copy','ceny')",,,,,'K');
_fb:="
   ceny.for_each(\"{? ceny.TYP=TAZ.SD & ceny.TREE || ceny.del() ?}\");
   exec('tar_utw','ceny')
   ";
ceny.win_act(_win_acr,0,'Formuła','&Odśwież'@@,,'Odświeżenie listy cenników'@,_fb,,,,,,'O');
_fb:="
   exec('tar_zakr','ceny',__ZAKR_CEN)
   ";
ceny.win_act(_win_acr,,'Formuła','Z&mień zakres'@@,,'Zmiana zakresu'@,_fb,,0,,,,'M');
_fb:="{? TAZ.SD='G'
      || exec('rep_exec','#b_report','','lsp_gra_001','Gratisy',2,,,,,,'W')
      || exec('rep_exec','#b_report','','wsp_cen_001','Cennik',2,,,,,,'W')
      ?}";
ceny.win_act(_win_acr,0,'Formuła','Druku&j'@@,,,,_fb,,,,,'J',,'icon=print');
ceny.win_act(_win_acr,0,'Szukaj');
ceny.win_act(_win_acr,0,'Rekord',,,,"
   _win_acr:=__win_acr;
   {? ~exec('chk_role','#b__box',OPERATOR.USER
        ,{? TAZ.SD='D' || 'LZK_CEN_DCDS' || 'LSP_CEN_DC'+{? TAZ.SD='S' || 'SP' || 'GR' ?} ?})
   || ceny.actions(_win_acr,'DUPK:D',,1)
   |? ceny.TREE=null
   || ceny.actions(_win_acr,'JUPKA',,1)
   || ceny.actions(_win_acr,,,1)
   ?};
   KALK.CEN_KH:='';KALK.CEN_GRKH:='';
   DISP.TAR:=null;
   {? ceny.REF<>''
   || TAR.cntx_psh();
      TAR.prefix();
      {? TAR.seek(ceny.REF)
      || DISP.TAR:=TAR.ref()
      || ceny.del()
      ?};
      TAR.cntx_pop()
   ?};
   {? DISP.TAR=null | ceny.NR =2 || 1 || 0 ?}");
ceny.win_act(_win_acr,0,'Wyświetl',,,,"exec('tar_disp','ceny')");

ceny.win_fml(_win_acr,,'OPIS',,'ICON_BEFORE',"
             {? ceny.TREE =0 & ceny.NR<>1
             || {? ceny.tr_state()=1
                || _wyn:='xwin16.png:75'
                || _wyn:='xwin16.png:74'
                ?}
             || _wyn:='xwin16.png:76'
             ?};
             _wyn");

ceny.tr_fml(_win_acr,,,"{? ceny.TREE=0 & ceny.NR>1 || 1 ?}");

ceny.dnd_sel(_win_acr,,'records.TAP',"exec('dragcopy','ceny')");

ceny.win_sopt(_win_acr,'select_record_checkbox=0');

exec('tar_utw','ceny');
TAZ.SD:='G'; exec('tar_utw','ceny'); TAZ.SD:=__sd;
{? ZAKR.TAR_AKT='N' || TAP.index('TM'); TAP.prefix(null) ?};

__zmie_a:=_a;
:: Tytuł okna ustawiany w obiekcie AreaTitle
_tartit:='';
_taretk:='tarcennik'+(-TAZ.SD);

__win_acr:=_win_acr;

_sel:=ceny.grp_make(_tartit,,_taretk,,,"exec('exit','zws',_a)");
ceny.grp_sel(_sel,ceny,_win_acr,'Cenniki'@,"exec('tar_gr2','ceny',__zmie_a)",,,,"
   TAZ.SD:=__sd; ceny.prefix(TAZ.SD);
   TAP.win_sel({? TAZ.SD='D' || 'WER_KD' || 'WER_KH' ?});
   TAP.hdr_sel(''); {? TAP.name()='taryfphs' || TAP.hdr_sel(' - ARCHIWUM') ?}
",,,,'maximized');
ceny.tab_splt(_sel,,'vertical','panel1','0, 33%');
ceny.grp_sel(_sel,TAP,{? TAZ.SD='D' || 'WER_KD' || 'WER_KH' ?},,,,,20,"
   params_exec('tar_grp','ceny',__zmie_a);
   exec('tap_act','ceny')
",,,,'maximized_with_title','main');
{? TAZ.SD='S'
||
   task_attach('LSP_CEN_DCSP');
   task_attach('LSP_CEN_ZCSP')
||
   task_attach('LZK_CEN_DCDS');
   task_attach('LZK_CEN_PCDS')
?};
exec('tar_upr','ceny_uprawn',TAZ.SD,ceny,_sel);
{? TAZ.SD='S'
||
   ceny.grp_sel(_sel,ceny,_win_acr,'Gratisy'@,"exec('tar_gr2','ceny',__zmie_a)",,,,"
      TAZ.SD:='G'; ceny.prefix(TAZ.SD);
      TAP.win_sel('WER_G');
      TAP.hdr_sel(''); {? TAP.name()='taryfphs' || TAP.hdr_sel(' - ARCHIWUM') ?}
   ",,,,'maximized');
   ceny.tab_splt(_sel,,'vertical','panel1','0, 33%');
   ceny.grp_sel(_sel,TAP,'WER_G',,,,,20,"
      params_exec('tar_grp','ceny',__zmie_a);
      exec('tap_act','ceny')
   ",,,,'maximized_with_title','main_g');
   task_attach('LSP_CEN_DCGR');
   task_attach('LSP_CEN_PCGR')
?};

ceny.win_sel(_sel);

{? _a=0
||
   ceny.actions(_win_acr,,'Z')
||
   ceny.actions(_win_acr,'Z','D:D')
?};

AreaTitle.setTabWin(ceny,~~);
AreaTitle.setTitle();

TAP.cntx_psh;
TAR.cntx_psh();

{? _link<>''
||
   {? (ref_tab(_link)=TAP)
      &(_tar:=exec('FindAndGet','#table',TAP,_link,,"TAP.TAR");
      exec('FindAndGet','#table',TAR,_tar,,"TAR.SD")='G')
   ||
      TAZ.SD:='G'; exec('tar_gr2','ceny',__zmie_a,,0);
      TAP.seek(_link);
      ceny.select(,1,5,,'main_g')
   |?
      (ref_tab(_link)=TAR)
      &(exec('FindAndGet','#table',TAR,_link,,"TAR.SD")='G')
   ||
      TAZ.SD:='G'; exec('tar_gr2','ceny',__zmie_a,,0);
      {? TAR.seek(_link)
      ||
         _ckod:=exec('FindAndGet','#table',TAR,_link,,"TAR.KOD");
         ceny.first(); {! |? (ceny.OPIS*_ckod)=0 & ceny.next() !};
         ceny.select(,1,5,,'main_g',,1)
      ||
         FUN.info('Nie odnaleziono nagłówka cennika\n%1.'@[exec('record','#to_string',_link)])
      ?}
   ||
      {? ref_tab(_link)=TAP
      ||
         _maska:=exec('FindAndGet','#table',TAP,_link,,"name()");
         {? (_maska+2)<>'__'
         ||
            TAP.use(_maska);
            {? TAP.seek(_link)
            ||
               TAP.use('taryfp__');
               VAR_DEL.delete('__spr_ok');
               __spr_ok:=1;
               params_set('aLink', 1, 'Link', _link);
               ceny.select(,1,5,,'main','NC',1)
            ||
               FUN.info('Nie odnaleziono pozycji cennika\n%1.'@[exec('record','#to_string',_link)])
            ?}
         ||
            {? TAP.seek(_link)
            ||
               ceny.select(,1,5,,'main',,1)
            ||
               FUN.info('Nie odnaleziono pozycji cennika\n%1.'@[exec('record','#to_string',_link)])
            ?}
         ?}
      |?
         ref_tab(_link)=TAR
      ||
         {? TAR.seek(_link)
         ||
            _ckod:=exec('FindAndGet','#table',TAR,_link,,"TAR.KOD");
            ceny.first(); {! |? (ceny.OPIS*_ckod)=0 & ceny.next() !};
            ceny.select(,1,5,,,,1)
         ||
            FUN.info('Nie odnaleziono nagłówka cennika\n%1.'@[exec('record','#to_string',_link)])
         ?}
      ?}
   ?}
||
   ceny.select(,,,,'main')
?};

TAP.cntx_pop;
TAR.cntx_pop();
KH.win_dict(_kh_dict);
KH.actions('WER');
TARGP.fld_fml('KH_ODB','AFTER_EDIT',"*");
TARGP.fld_fml('KH','AFTER_EDIT',"*");
TARGP.fld_fml('GRKH','AFTER_EDIT',"*");
TARGP.fld_fml('M','AFTER_EDIT',"*");
TARGP.fld_fml('MGRP','AFTER_EDIT',"*");
TARGP.fld_fml('MGR','AFTER_EDIT',"*");
VAR_DEL.delete('__zmie_a','__grp_cb','__ZAKR_CEN','__sd');
''


\utw_ceny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: tworzy tabele tymczasowa z rodzajami cenników
::   WE: [_a] -   jakie cenniki wyswietlać
::            0  - wszystkie (domyślnie)
::            1  - kontrahenta
::            2  - grupy kontrahentów
::       [_b] - 0/1 - tworzyć tabelę
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=0 ?};
{? var_pres('_b')<>type_of(1) || _b:=1 ?};

{? _b
||
   VAR_DEL.delete('ceny');
   ceny:=sql('select '+
             '       \' \' typ, '+
             '       CAST (NULL AS TREE_REF_TYPE) tree, '+
             '       \'                                        \' opis, '+
             '       \'                                        \' kh_opis, '+
             '       0 as NR, '+
             '       REFERENCE as REF '+
             'from FIRMA where 1=0 order by 1,2,3')
?};

{? _a=0
||
   ceny.blank();
   ceny.TYP:=TAZ.SD;
   ceny.OPIS:={? TAZ.SD='G' || 'Wszystkie gratisy'@ || 'Wszystkie cenniki'@ ?};
   ceny.NR:=1;
   ceny.add();
   ceny.blank();
   ceny.TYP:=TAZ.SD;
   ceny.OPIS:={? TAZ.SD='G' || 'gratisy Standardowe'@ || 'cenniki Standardowe'@ ?};
   ceny.NR:=2;
   ceny.add()
?};
{? _a<>2
||
   ceny.blank();
   ceny.TYP:=TAZ.SD;
   ceny.OPIS:={? TAZ.SD='G' || 'gratisy Kontrahentów'@ || 'cenniki Kontrahentów'@ ?};
   ceny.NR:=3;
   ceny.add()
?};
{? _a<>1
||
   ceny.blank();
   ceny.TYP:=TAZ.SD;
   ceny.OPIS:={? TAZ.SD='G' || 'gratisy Grup kontrahentów'@ || 'cenniki Grup kontrahentów'@ ?};
   ceny.NR:=4;
   ceny.add()
?};
''


\tar_utw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: tworzy listę cenników dla okienka grupowego
::----------------------------------------------------------------------------------------------------------------------

_form:="
ceny.cntx_psh();
_zakr:={? ZAKR.TAR_AKT='T' || ' and TAR.AKT=\\'T\\''
       |? ZAKR.TAR_AKT='N' || ' and TAR.AKT=\\'N\\''
       || ''
       ?};
{? ceny.NR >1
||
   ceny.prefix(TAZ.SD,ceny.ref());
   {? ceny.size()=0
   ||
      {? ceny.NR=2
      || __tar:=sql('SELECT TAR.KOD, TAR.NAZ, KH.NAZ_P kh_opis, TAR.REFERENCE as REF
                      FROM TAR left join KH
                      where TAR.KH is null and TAR.GRKH is null and TAR.ODDZ = \'':_a\'' and TAR.SD = \'':_b\'':_c
                      order by TAR.KOD',ST.ODDZ,TAZ.SD,_zakr)
      |? ceny.NR=3
      ||
         {? KALK.KH=null
         ||
            __tar:=sql('SELECT TAR.KOD, TAR.NAZ, KH.NAZ_P kh_opis, TAR.REFERENCE as REF
                         FROM TAR join KH
                         where TAR.ODDZ = \'':_a\'' and TAR.SD = \'':_b\'':_c
                         order by KOD',ST.ODDZ,TAZ.SD,_zakr)
         ||
            __tar:=sql('SELECT TAR.KOD, TAR.NAZ, KH.NAZ_P kh_opis, TAR.REFERENCE as REF
                         FROM TAR join KH
                         where TAR.KH=:_a and TAR.ODDZ = \'':_b\'' and TAR.SD = \'':_c\'':_d
                         order by KOD',KALK.KH,ST.ODDZ,TAZ.SD,_zakr)
         ?}
      |? ceny.NR=4
      ||
         {? KALK.GRKH=null
         ||
            __tar:=sql('SELECT TAR.KOD, TAR.NAZ, GRKH.KOD kh_opis, TAR.REFERENCE as REF
                        FROM TAR join GRKH
                        where TAR.ODDZ = \'':_a\'' and TAR.SD = \'':_b\'':_c
                        order by KOD',ST.ODDZ,TAZ.SD,_zakr)
         ||
            __tar:=sql('SELECT TAR.KOD, TAR.NAZ, GRKH.KOD kh_opis, TAR.REFERENCE as REF
                        FROM TAR join GRKH
                        where TAR.GRKH = :_a and TAR.ODDZ = \'':_b\'' and TAR.SD = \'':_c\'':_d
                        order by KOD',KALK.GRKH,ST.ODDZ,TAZ.SD,_zakr)
         ?}
      ?};
      ceny.TREE:=ceny.ref();
      __tar.for_each(""
         {? ceny.TYP=TAZ.SD
         ||
            ceny.TYP:=TAZ.SD;
            ceny.OPIS:=__tar.KOD+' - '+__tar.NAZ;ceny.NR:=ceny.NR;
            ceny.KH_OPIS:=__tar.KH_OPIS;ceny.REF:=__tar.REF;
            ceny.add()
         ?}
      "");
      obj_del(__tar); &__tar
   ?}
?};
ceny.cntx_pop()
";

{? ceny.first()
||
   {!|? {? ceny.TREE=null || _form() ?};ceny.next() !}
?};
ceny.first();
''


\tap_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: ukrywanie akcji
::----------------------------------------------------------------------------------------------------------------------
_tapwer:=cur_win(1,1);
_aktmask:=TAP.name+2='__';
_czyarch:=(menu_pth()+1)='P';
TAR.cntx_psh();
TAR.prefix();
_nakt:={? DISP.TAR<>null & TAR.seek(DISP.TAR)
      || TAR.AKT='N'
      || 0
      ?};
TAR.cntx_pop();

{? TAZ.SD='G'
||
   {? ~_aktmask
   || TAP.actions(_tapwer
         ,  ''
           +'D(MGQR)OoPpUuIiN(PC)G:D(MGQR)OoN(C)'
           +''
         ,,1)
   |? DISP.TAR=null
   || TAP.actions(_tapwer
         ,  ''
           +'D(MGQR)OoIiN(WH)'
           +':'
           +'D(MGQR)OoN(H)'
           +''
         ,,1)
   || TAP.actions(_tapwer
         ,  {? TAZ.CEN_VIEW='T' || 'D(GQR)O'
            |? TAZ.CEN_VIEW='U' || 'D(MQR)O'
            |? TAZ.CEN_VIEW='G' || 'D(MGQ)O'
            |? _nakt            || 'D(MGQR)O'
            || ''
            ?}
           +{? TAZ.UPR || 'N(WH)' || 'PUAIN(PHW)' ?}
           +':N(H)'
           +{? TAZ.CEN_VIEW='T' || 'D(GQR)O'
            |? TAZ.CEN_VIEW='U' || 'D(MQR)O'
            |? TAZ.CEN_VIEW='G' || 'D(MGQ)O'
            |? _nakt            || 'D(MGQR)O'
            || ''
            ?}
         ,,1)
    ?}
||
   {? TAZ.ACTIONS<>''
   || TAP.actions(_tapwer,TAZ.ACTIONS,,1)
   || {? ~_aktmask
      || TAP.actions(_tapwer
            ,  ''
              +{? TAZ.UPR || 'N(PC)' || 'N(PCW)' ?}
              +'D(MGQR)OoPpUuIiZzAaE:D(MGQR)OoN(C)'
              +''
            ,,1)
      |? DISP.TAR=null
      || TAP.actions(_tapwer
            ,  ''
              +'D(MGQR)OoIi'
              +{? TAZ.UPR || 'N(WH)' || 'PUN(PHW)R' ?}
              +':'
              +'D(MGQR)OoN(H)'
              +''
            ,,1)
      || TAP.actions(_tapwer
            ,  {? TAZ.CEN_VIEW='T' || 'D(GQR)O'
               |? TAZ.CEN_VIEW='U' || 'D(MQR)O'
               |? TAZ.CEN_VIEW='G' || 'D(MGQ)O'
               |? _nakt            || 'D(MGQR)O'
               || ''
               ?}
              +{? TAZ.UPR || 'N(WH)' || 'PUAIN(PHW)' ?}
              +':N(H)'
              +{? TAZ.CEN_VIEW='T' || 'D(GQR)O'
               |? TAZ.CEN_VIEW='U' || 'D(MQR)O'
               |? TAZ.CEN_VIEW='G' || 'D(MGQ)O'
               |? _nakt            || 'D(MGQR)O'
               || ''
               ?}
            ,,1)
      ?}
   ?}
?};
''


\tar_gr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: ustawienie zakresu dla okieniek grupowych
::   WE: [_a] -   jakie cenniki wyswietlac
::            0  - wszystkie
::            1  - kontrahenta
::            2  - grupy kontrahentow
::       [_b] - 0-bez ograniczen, 1-dla materialu, 2-dla grupy materialowej
::       [_c] - 1-wolac grp_disp(TAP,WER), 0-wpp
::       [_d] - akronim okna do odrysowania
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of(1) || _b:=0 ?};
{? var_pres('_c')<>type_of(1) || _c:=1 ?};
{? var_pres('_d')<>type_of('') || _d:='' ?};

TAP.index('TM');
::Jeżeli zakres jest ustawiony na cenniki nieaktywne to zakładamy że nie ma pozycji więc nie obciążamy systemu nowymi
::indeksami i prefiksujemy po nullu
{? ZAKR.TAR_AKT='N'
|| TAP.prefix(null)
|? ceny.NR=1
||
   {? _b=1
   || TAP.index('RODZM');
      TAP.prefix(ST.ODDZ,TAZ.SD,TAZ.M)
   |? _b=2
   || TAP.index('RODZMGR');
      TAP.prefix(ST.ODDZ,TAZ.SD,TAZ.MGR,TAZ.MGRP)
   || TAP.index('RODZ');
      TAP.prefix(ST.ODDZ,TAZ.SD)
   ?};
   TAP.first()
|? DISP.TAR<>null
||
   {? _b=1
   || TAP.index('TM');
      TAP.prefix(DISP.TAR,TAZ.M)
   |? _b=2
   || TAP.index('TMGR');
      TAP.prefix(DISP.TAR,TAZ.MGR,TAZ.MGRP)
   || TAP.index('TM');
      TAP.prefix(DISP.TAR)
   ?};
   TAP.first()
||
   {? ceny.NR=2
   ||
      {? _b=1
      || TAP.index('RODZM');
         TAP.prefix(ST.ODDZ,TAZ.SD,TAZ.M,'C')
      |? _b=2
      || TAP.index('RODZMGR');
         TAP.prefix(ST.ODDZ,TAZ.SD,TAZ.MGR,TAZ.MGRP,'C')
      || TAP.index('RODZ');
         TAP.prefix(ST.ODDZ,TAZ.SD,'C')
      ?};
      TAP.first()
   |? ceny.NR=3
   ||
      {? _a=1
      ||
::       z poziomu kontraheta (biezacy kontrahent w buforze)
         {? _b=1
         || TAP.index('KHM');
            TAP.prefix(ST.ODDZ,TAZ.SD,KH.ref(),TAZ.M)
         |? _b=2
         || TAP.index('KHMGR');
            TAP.prefix(ST.ODDZ,TAZ.SD,KH.ref(),TAZ.MGR,TAZ.MGRP)
         || TAP.index('KH');
            TAP.prefix(ST.ODDZ,TAZ.SD,KH.ref())
         ?};
         TAP.first()
      || {? _b=1
         || TAP.index('RODZM');
            TAP.prefix(ST.ODDZ,TAZ.SD,TAZ.M,'K')
         |? _b=2
         || TAP.index('RODZMGR');
            TAP.prefix(ST.ODDZ,TAZ.SD,TAZ.MGR,TAZ.MGRP,'K')
         || TAP.index('RODZ');
            TAP.prefix(ST.ODDZ,TAZ.SD,'K')
         ?};
         TAP.first()
      ?}
   |? ceny.NR=4
   ||
      {? _a=2
      ||
::       z poziomu grupy kontrahetow (biezaca grupa kontrahenow w buforze)
         {? _b=1
         || TAP.index('GRKHM');
            TAP.prefix(ST.ODDZ,TAZ.SD,GRKH.ref(),TAZ.M)
         |? _b=2
         || TAP.index('GRKHMGR');
            TAP.prefix(ST.ODDZ,TAZ.SD,GRKH.ref(),TAZ.MGR,TAZ.MGRP)
         || TAP.index('GRKH');
            TAP.prefix(ST.ODDZ,TAZ.SD,GRKH.ref())
         ?};
         TAP.first()
      || {? _b=1
         || TAP.index('RODZM');
            TAP.prefix(ST.ODDZ,TAZ.SD,TAZ.M,'G')
         |? _b=2
         || TAP.index('RODZMGR');
            TAP.prefix(ST.ODDZ,TAZ.SD,TAZ.MGR,TAZ.MGRP,'G')
         || TAP.index('RODZ');
            TAP.prefix(ST.ODDZ,TAZ.SD,'G')
         ?};
         TAP.first()
      ?}
   ?}
?};
params_set(params_get());
{? _c
|| _wer:={? _d<>'' || _d |? TAZ.SD='G' || 'WER_G' |? TAZ.SD='D' || 'WER_KD' || 'WER_KH' ?};
   grp_disp(TAP,_wer)
?};
''


\tar_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: ustawienie zakresu dla okieniek grupowych
::   WE: [_a] -   jakie cenniki wyswietlać
::            0  - wszystkie
::            1  - kontrahenta
::            2  - grupy kontrahentow
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_win_acr:=__win_acr;
{? KALK.KH<>null | KALK.GRKH<>null
|| ceny.cntx_psh();
   {? ceny.first()
   ||
      {!
      |?
         {? ceny.NR=3 | ceny.NR=4
         || ceny.tr_set(1,_win_acr,1,1)
         ?};
         ceny.next()
      !}
   ?};
   ceny.cntx_pop()
?};
grp_disp(ceny,_win_acr,,1);
''


\tar_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dodanie nagłówka cennika (z poziomu drzewa)
::   WE:  _a - czy dodawanie (1), czy poprawianie (rozne od 1 )
::        _b - jakie cenniki wyswietlać
::            0  - wszystkie (domyślnie)
::            1  - kontrahenta
::            2  - grupy kontrahentów
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of(1) || _b:=0 ?};

__tarpop:=_a<>1;

{? ceny.NR>1
||
   {? _a=1 | DISP.TAR<>null
   ||
      _ok:=0;
      {? ceny.NR=2
      ||
         {? _a=1 || TAR.blank(); TAR.P:=exec('get','#params',800804,1) || DISP.TAR().KOD ?};
         TAR.win_edit('RED'+{? TAZ.SD='G' || 'G' || '' ?});
         {? TAR.edit("exec('chk_tar','ceny',2,__tarpop)")
         ||
            TAR.prefix();
            _ok:={? _a=1 || TAR.add() || TAR.put() ?}
         ?}
      |? ceny.NR=3
      ||
         {? _a=1
         ||
            _kh:=null;
            {? DISP.TAR<>null || _kh:=DISP.TAR().KH ?};
            TAR.blank();
            TAR.P:=exec('get','#params',800806,1);
            TAR.KH:=_kh
         || DISP.TAR().KOD
         ?};
         TAR.win_edit('REDKH'+{? TAZ.SD='G' || 'G' || '' ?});
         {? TAR.edit("exec('chk_tar','ceny',1,__tarpop)")
         ||
            TAR.prefix();
            _ok:={? _a=1 || TAR.add() || TAR.put() ?};
            ceny.KH_OPIS:=TAR.KH().NAZ_P
         ?}
      |? ceny.NR=4
      ||
         {? _a=1
         ||
            _grkh:=null;
            {? DISP.TAR<>null || _grkh:=DISP.TAR().GRKH ?};
            TAR.blank();
            TAR.P:=exec('get','#params',800805,1);
            TAR.GRKH:=_grkh
         || DISP.TAR().KOD
         ?};
         TAR.win_edit('REDGRKH'+{? TAZ.SD='G' || 'G' || '' ?});
         {? TAR.edit("exec('chk_tar','ceny',0,__tarpop)")
         ||
            TAR.prefix();
            _ok:={? _a=1 || TAR.add() || TAR.put() ?};
            ceny.KH_OPIS:=TAR.GRKH().KOD
         ?}
      ?};
      {? _ok=1
      ||
         ceny.OPIS:=TAR.KOD+' - '+TAR.NAZ;
         ceny.NR:=ceny.NR;
         ceny.REF:=$TAR.ref();
         {? _a=1
         ||
            {? ceny.TREE<>null || ceny.TREE:=ceny.TREE || ceny.TREE:=ceny.ref()  ?};
            ceny.add()
         || ceny.put()
         ?}
      ?}
   ||
      {? _a=2
      || {? TAZ.SD='G'
         || FUN.info('Należy wybrać nagłówek gratisu do poprawy.'@)
         || FUN.info('Należy wybrać nagłówek cennika do poprawy.'@)
         ?}
      ?}
   ?};
   _ref:=ceny.REF;
   ceny.for_each("{? ceny.TYP=TAZ.SD & ceny.REF<>'' || ceny.del() ?}");
   exec('tar_utw','ceny',_b);
   _ndx:=ceny.index('?');
   _ndx_tmp:=ceny.ndx_tmp(,,'TYP',,,'REF',,);
   ceny.index(_ndx_tmp);
   ceny.prefix(TAZ.SD,_ref);
   ceny.first();
   ceny.prefix(TAZ.SD);
   ceny.index(_ndx);
   ceny.ndx_drop(_ndx_tmp)
||
   {? TAZ.SD='G'
   ||
      FUN.info('Należy wybrać typ gratisu. \nDodawanie lub poprawianie dla opcji "Wszystkie gratisy" niemożliwe.'@)
   ||
      FUN.info('Należy wybrać typ cennika. \nDodawanie lub poprawianie dla opcji "Wszystkie cenniki" niemożliwe.'@)
   ?}
?};
VAR_DEL.delete('__tarpop');
''


\bl_tarko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: podpowiada kod cennika na blanku
::   WY: kod cennika
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('ceny')>100
||
   _form:=exec('get','#params',800808,2);
   {? _form<>''
   || ($_form)()
   || _prf:=ST.ODDZ+TAZ.SD;
      TAR.cntx_psh();
      TAR.index('KOD');
      {? ceny.NR=4
      || _prf+='G'
      |? ceny.NR=3
      || _prf+='K'
      || _prf+='C'
      ?};
      TAR.prefix(ST.ODDZ,TAZ.SD,_prf);
      {? TAR.last()
      || _wyn:=form((#(TAR.KOD+3)+1),-3,,'99')
      || _wyn:='000'
      ?} ;
      TAR.cntx_pop();
      _prf+'-'+_wyn
   ?}
||
   ''
?}


\ae_tarko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po edycji pola kod taryfy
::   WY: czy mozna opuścić pole
::----------------------------------------------------------------------------------------------------------------------

_wyn:=1;
{? fld<>''
||
   {? -(1+menu_txt)<>'p'
   ||
      _jest:=0;
      TAR.cntx_psh();
      _next:=exec('bl_tarko','ceny');
      TAR.index('KOD');
      TAR.prefix();
      {? TAR.find_key(ST.ODDZ,TAZ.SD,TAR.KOD) & TAR.KOD<>KALK.TAR_KOD
      ||
         {? FUN.ask('Cennik o kodzie: %1 już istnieje.\n Proponowany kod to: %2.\n Przypisać?'@[TAR.KOD,_next])
         || _wyn:=1
         || _wyn:=0
         ?};
         _jest:=1
      ||
         _jest:=0
      ?};
      TAR.cntx_pop();
      {? _wyn & _jest || TAR.KOD:=_next ?}
   ||
      _wyn:=1
   ?}
||
:: kontrola wypelnienia pola KOD nastepuje po edycji calego okienka
   _wyn:=1
?};
_wyn


\kalk_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: KALK.KH
::----------------------------------------------------------------------------------------------------------------------
KALK.KH


\kalk_grkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: KALK.GRKH
::----------------------------------------------------------------------------------------------------------------------
KALK.GRKH


\taz_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: wartosc TAZ.SD
::----------------------------------------------------------------------------------------------------------------------
TAZ.SD


\bl_tapdo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: wartosc na blank
::----------------------------------------------------------------------------------------------------------------------
KALK.DO


\chk_tar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po edycji TAR
::   WE: _a - 1-KH  0-GRKH
::       _b - 1-popraw 0-dołącz
::----------------------------------------------------------------------------------------------------------------------

{? _a=2
|| _wyn:=chk_rec('KOD')
|? _a=1
|| _wyn:=chk_rec('KH','KOD')
|| _wyn:=chk_rec('GRKH','KOD')
?};

{? _wyn=''
||
   _ref:={? _b=1 || TAR.ref() || null ?};
   _kod:=TAR.KOD;
   TAR.cntx_psh();
   TAR.index('KOD'); TAR.prefix(ST.ODDZ,TAZ.SD,_kod);
   {? TAR.first()
   || {!
      |? {? _ref<>TAR.ref() & TAR.KOD=_kod
         || FUN.info('Cennik o kodzie %1 już istnieje.\nNależy poprawić kod cennika.'@[_kod]); _wyn:='KOD'
         ?};
         _wyn='' & TAR.next()
      !}
   ?};
   TAR.cntx_pop()
?};
{? _a<2
||
   {? _wyn='' & TAR.OD<>date(0,0,0) & TAR.DO<>date(0,0,0) & TAR.OD<>TAR.DO
   || {? TAR.DO<TAR.OD
      || FUN.info('Data musi być większa lub równa wartości pola ""Od daty"".'@);
         _wyn:='DO'
      ?}
   ?};
   {? _wyn=''
   ||
      {? TAR.PRC<0
      || FUN.info('Wartość nie może być mniejsza od 0\%.'@);
         _wyn:='PRC'
      |? TAR.PRC>100
      || FUN.info('Wartość nie może być większa od 100\%.'@);
         _wyn:='PRC'
      ?}
   ?}
?};
{? _wyn='' & TAR.P<=0
|| FUN.info({? TAZ.SD='G' || 'Priorytet gratisu musi być większy od zera.'@ || 'Priorytet cennika musi być większy od zera.'@ ?});
   _wyn:='P'
?};
{? _wyn='' & TAR.AKT='N' & (
   TAP.cntx_psh();
   TAP.use('taryfp__');
   TAP.index('TMGR');
   TAP.prefix(TAR.ref());
   _size:=TAP.size();
   TAP.cntx_pop();
   _size)
||
   FUN.info({? TAZ.SD='G' || 'Pozycje gratisu muszą być zarchiwizowane.'@ || 'Pozycje cennika muszą być zarchiwizowane.'@ ?});
   _wyn:='AKT'
?};
_wyn


\tar_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: usuwanie cennika wraz z pozycjami
::----------------------------------------------------------------------------------------------------------------------

{? DISP.TAR<>null
||
   _tar:=null;
   TAR.cntx_psh(); TAP.cntx_psh;
   TAR.prefix();
   {? TAR.seek(DISP.TAR)
   ||
      _info:=0;
      _ref:=TAR.ref();
      TAP.index('TM');
      TAP.prefix(_ref);
::    sprawdza czy pozycje nie sa zablokowane
      _ok:=1;
      _tap_size:=0;
      _tap_count:=0;
      _tap_ref:=TAP.ref();
      {? TAP.first()
      ||
         _tap_size:=TAP.size();
         {!
         |?
            _tap_count+=TAP.count();
            _ok:=exec('tap_lock','ceny');
            {? _ok=1 || exec('tap_unlock','ceny') ?};
            _ok & TAP.next()
         !};
         TAP.seek(_tap_ref)
      ?};
      _txt:='';
      TAR.cntx_psh(); TAP.cntx_psh();
      {! _ii..2
      |!
         {? _ii=1
         || TAP.use('taryfp__')
         || TAP.use('taryfphs')
         ?};
         TAR.prefix(); TAP.prefix();
         {? _ok=1 & (TAR.find_tab(,'TARB',,'=',$TAR.ref()) | TAP.find_tab(,'TARB',,'=',$TAR.ref()))
         || _txt:='Na bazie cennika utworzono cenniki.\n'@
         ?}
      !};
      TAR.cntx_pop(); TAP.cntx_pop();
::    sprawdzenie czy można usunąć cennik
      {? _tap_size<TAR.count() | _tap_count
      ||
         FUN.info('Cennik użyty w systemie.\nUsunięcie cennika jest niedostęne.'@);
         _ok:=0
      ?};
::    transakcja i usuwanie
      {? _ok=1 &
         {? TAR.SD='G'
         || FUN.ask(_txt+'Czy na pewno usunąć gratis wraz z przypisanymi pozycjami i archiwum?'@)
         || FUN.ask(_txt+'Czy na pewno usunąć cennik wraz z przypisanymi pozycjami i archiwum?'@)
         ?}
      ||
         ceny.cntx_psh();
         _tar:=ceny.TREE;
         ceny.prefix(TAZ.SD,ceny.TREE);
         {? ceny.seek(ceny.ref())
         || {? ceny.next() | ceny.prev() || _tar:=#ceny.ref() ?}
         ?};
         ceny.cntx_pop();
         _Tarhmsk:=TAR_H.names;

         do();
         {! _ii..2
         |!
            {? _ii=1
            || TAP.use('taryfp__')
            || TAP.use('taryfphs')
            ?};
            TAP.index('TM');
            TAP.prefix(_ref);
            {? TAP.first()
            ||
               {!
               |?
                  {? exec('tap_count','ceny',,0,_Tarhmsk)
                  || _info:=1; undo; 0
                  || {? TAR.SD='G' |
                        (TAP.TAR();
                        TAP.M();
                        _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
                        _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
                        _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
                        exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr))
                     || TAP.del
                     || _info:=2; undo; 0
                     ?}
                  ?}
               !}
            ?}
         !};
         {? _info=0 & TAR.count
         || _info:=1; undo
         || TAR.del
         ?};
         {? _info=1
         || FUN.info('Co najmniej jedna z pozycji wykorzystana jest w dokumentach.\nUsunięcie niemożliwe.'@)
         |? _info=2
         || FUN.info('Należy mieć uprawnienia do wszystkich pozycji.\nUsunięcie niemożliwe.'@)
         ?};
         end()
      ?}
   ?};
   TAR.cntx_pop(); TAP.cntx_pop;
   {? _tar<>null & _info=0
   || ceny.seek(_tar,)
   ?}
|| {? TAR.SD='G'
   || FUN.info('Należy wybrać gratis.\Usuwanie niemożliwe.'@)
   || FUN.info('Należy wybrać cennik.\nUsuwanie niemożliwe.'@)
   ?}
?};
''


\tar_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja wyświetl dla nagłówkow cenników
::----------------------------------------------------------------------------------------------------------------------

TAR.cntx_psh();
{? DISP.TAR<>null
||
   {? ceny.NR=2
   || TAR.win_edit('RED'+{? TAZ.SD='G' || 'G' || '' ?})
   |? ceny.NR=3
   || TAR.win_edit('REDKH'+{? TAZ.SD='G' || 'G' || '' ?})
   |? ceny.NR=4
   || TAR.win_edit('REDGRKH'+{? TAZ.SD='G' || 'G' || '' ?})
   ?};
   DISP.TAR().KOD;
   TAR.display()
?};
TAR.cntx_pop();
''


\tar_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: kopia danego cennika
::----------------------------------------------------------------------------------------------------------------------

{? ceny.NR>1 & DISP.TAR<>null
|| {? _choice:=FUN.choice({? TAZ.SD='G'
                          || 'Skopiować wskazany gratis na gratisu typu:'@
                          || 'Skopiować wskazany cennik na cennik typu:'@
                          ?},,
         'Standardowy'@,'Kontrahenta'@,'Grupy kontrahenta'@)
   ||
      ceny.NR:={? _choice=1 || 2 |? _choice=2 || 3 || 4 ?};
      {? DISP.TAR<>null
      ||
         _ok:=0;
         DISP.TAR().KOD;
         _ref_old:=TAR.ref;
         TAR.blank();
         {? ceny.NR=2
         ||
            TAR.KH:=TAR.GRKH:=null;
            TAR.P:=exec('get','#params',800804,1);
            TAR.win_edit('RED'+{? TAZ.SD='G' || 'G' || '' ?});
            {? TAR.edit("exec('chk_tar','ceny',2,1)")
            ||
               TAR.prefix();
               _ref_new:={? TAR.add(1) || TAR.ref || null ?};
               _ok:=_ref_new<>null;
               {? _ok || ceny.KH_OPIS:='' ?}
            ?}
         |? ceny.NR=3
         ||
            TAR.GRKH:=null;
            TAR.P:=exec('get','#params',800806,1);
            TAR.win_edit('REDKH'+{? TAZ.SD='G' || 'G' || '' ?});
            {? TAR.edit("exec('chk_tar','ceny',1,1)")
            ||
               TAR.prefix();
               _ref_new:={? TAR.add(1) || TAR.ref || null ?};
               _ok:=_ref_new<>null;
               {? _ok || ceny.KH_OPIS:=TAR.KH().NAZ_P ?}
            ?}
         |? ceny.NR=4
         ||
            TAR.KH:=null;
            TAR.P:=exec('get','#params',800805,1);
            TAR.win_edit('REDGRKH'+{? TAZ.SD='G' || 'G' || '' ?});
            {? TAR.edit("exec('chk_tar','ceny',0,1)")
            ||
               TAR.prefix();
               _ref_new:={? TAR.add(1) || TAR.ref || null ?};
               _ok:=_ref_new<>null;
               {? _ok || ceny.KH_OPIS:=TAR.GRKH().KOD ?}
            ?}
         ?};

         {? _ok
         ||
            DISP.TAR:=TAR.ref;
            ceny.OPIS:=TAR.KOD+' - '+TAR.NAZ;
            ceny.NR:=ceny.NR;
            ceny.REF:=$TAR.ref();
            ceny.cntx_psh();
            _tree:=0; _nr:=ceny.NR;
            ceny.prefix(TAZ.SD,0);
            {? ceny.first() || {! |? {? ceny.NR=_nr || _tree:=ceny.ref() ?}; _tree=0 & ceny.next() !} ?};
            ceny.cntx_pop();
            ceny.TREE:=_tree;
            ceny.add();
            TAP.cntx_psh();
            TAP.use('taryfp__');
            TAP.index('TM');
            TAP.prefix(_ref_old);
            {? TAP.first()
            || {!
               |?
                  {? TAZ.SD='G' |
                     (TAR.cntx_psh();
                     TAP.TAR();
                     TAP.M();
                     _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
                     _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
                     _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
                     TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr);
                     TAR.cntx_pop();
                     TAZ.UPR)
                  ||
                     _ref:=TAP.ref();
                     TAP.cntx_psh();
                     TAP.clear(); {? TAP.seek(_ref) || TAP.TAR:=_ref_new; TAP.add(1) ?};
                     TAP.cntx_pop()
                  ?};
                  TAP.next()
               !}
            ?};
            TAP.use('taryfphs');
            TAP.index('TM');
            TAP.prefix(_ref_old);
            {? TAP.first()
            || {!
               |?
                  {? TAZ.SD='G' |
                     (TAR.cntx_psh();
                     TAP.TAR();
                     TAP.M();
                     _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
                     _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
                     _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
                     TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr);
                     TAR.cntx_pop();
                     TAZ.UPR)
                  ||
                     _ref:=TAP.ref();
                     TAP.cntx_psh();
                     TAP.clear(); {? TAP.seek(_ref) || TAP.TAR:=_ref_new; TAP.add(1) ?};
                     TAP.cntx_pop()
                  ?};
                  TAP.next()
               !}
            ?};
            TAP.cntx_pop()
         ?}
      ?}
   ?}
|| FUN.info('Należy wybrać konkretny cennik z danej grupy cennikowej.'@)
?};
''


\tap_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dodawanie pozycji taryfy
::   WE: _a - tow/usl/grupa (T/U/G)
::       [_b] - 0 - add 2 - kopiuj
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of(1) || _b:=0 ?};
ZMIENNE.RODZAJ:='1';

TAR.cntx_psh();
{? DISP.TAR || TAR.prefix(); TAR.seek(DISP.TAR) ?};
POMOC.RODZ:='';
BEER.TAB:='JM';
{? _a='T' | _a='U' || POMOC.RODZ:=_a ?};
exec('slo_m_ok','material',POMOC.RODZ,,,'W','W');
TAZ.UPR:=1;
{? TAZ.M | TAZ.MGR
|| _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
   _mgr:={? TAZ.M || TAZ.M().MGR || TAZ.MGR ?};
   _mgrp:={? TAZ.M || TAZ.M().MGRP || TAZ.MGRP ?};
   TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAZ.M,_mgrp,_mgr)
?};
{? TAZ.UPR
|| exec('tap_ad_m','ceny',_b)
|| FUN.info('Brak uprawnień do %1.'@[{? TAZ.M || TAZ.M().KTM || TAZ.MGR().KOD ?}],7)
?};
TAR.cntx_pop();
''


\tap_ad_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dodawanie lub poprawa ceny materiału
::   WE: _a - 0 - add 1 - edit 2 - kopiuj
::----------------------------------------------------------------------------------------------------------------------
ZMIENNE.RODZAJ:='1';

{? TAP.sel_size()=0
||
   {? _a=1
   || {? TAP.M<>null
      || POMOC.RODZ:=TAP.M().RODZ
      || POMOC.RODZ:=''
      ?}
   ?};

   exec('slo_m_ok','material',POMOC.RODZ,,,'W','W');
   exec('tap_okno','ceny',1);
   {? _a=2
   || TAP.SES_ID:='';
      _a:=0
   |? _a=0
   || TAP.blank();
      TAP.M:=TAZ.M;
      TAP.MGR:=TAZ.MGR;
      TAP.MGRP:=TAZ.MGRP;
      TAP.JM:=TAP.M().J;
      TAP.TARB:=TAP.TAR().TARB;
      M.VAT:=exec('m_vat','material',TAP.M,TAP.TAR().KH,,TAP.OD~1,TAP.OD~2,,,,{? TAZ.SD='D' || 'Z' || 'S' ?});
      {? M.VAT<>null
      ||
         exec('wz_sv','ustawienia');
         SLO.prefix(SLU.ref());
         {? SLO.find_key(M.VAT().KOD)
         ||
            TAP.SV:=M.VAT
         ?}
      ?}
   ?};
   _tapr:=TAP.ref;
   {? {? _a=0 || TAP.add() || 1 ?} & exec('tap_lock','ceny')
   ||
      exec('tap_count','ceny',,,,_a<>1);
      {? TAP.edit("exec('chk_tap','ceny')")
      ||
         _dokl_c:=exec('dokl_ceny','ceny');
         TAP.CEN:=TAP.CEN$_dokl_c;
         TAP.CENB:=TAP.CENB$_dokl_c;
         TAP.OP:=
               {? TAP.MGR || TAP.MGR().KOD+{? TAP.MGRP || ' - '+TAP.MGRP().KOD || '' ?}
               |? TAP.M<>null || M.KTM
               || ''
               ?};
         TAP.put();
         exec('tap_unlock','ceny')
      ||
         exec('tap_unlock','ceny');
         {? _a=0 || TAP.del(); TAP.seek(_tapr) ?}
      ?}
   ?};
   {? var_pres('ref_tap')>0 || &ref_tap ?}
||
   {? __czy_ed<>0
   ||
      {? (TAZ.SD='G' |
         (TAR.cntx_psh();
         TAP.TAR();
         TAP.M();
         _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
         _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
         _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
         TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr);
         TAR.cntx_pop();
         {? ~TAZ.UPR
         || exec('add_kom','#message','Brak uprawnień do %1.'@[{? TAP.M || M.KTM || TAP.MGR().KOD+ {? TAP.MGRP || ' - '+TAP.MGRP().KOD || '' ?} ?}],7)
         ?};
         TAZ.UPR))
      ||
         {? TAZ.SD='G'
         ||
            {? DISP.OD<>date(0,0,0) || TAP.OD:=DISP.OD ?};
            {? DISP.DO<>date(0,0,0) & TAP.OD<=DISP.DO || TAP.DO:=DISP.DO ?};
            {? DISP.DOZ='T' || TAP.DO:=date(0,0,0) ?};
            {? DISP.WALU<>null || TAP.WAL:=DISP.WALU ?};
            {? DISP.TAPIL || TAP.IL:=DISP.TAPIL ?};
            {? DISP.TAPILZ='T' || TAP.IL:=0 ?};
            {? DISP.TAPILDO & TAP.IL<=DISP.TAPILDO || TAP.IL_DO:=DISP.TAPILDO ?};
            {? DISP.TAPILDOZ='T' || TAP.IL_DO:=0 ?};
            {? DISP.TAPWAR || TAP.WAR:=DISP.TAPWAR ?};
            {? DISP.TAPWARZ='T' || TAP.WAR:=0 ?};
            {? DISP.TAPWADO & TAP.WAR<=DISP.TAPWADO || TAP.WAR_DO:=DISP.TAPWADO ?};
            {? DISP.TAPWADOZ='T' || TAP.WAR_DO:=0 ?};
            {? DISP.GRATIS || TAP.GRATIS:=DISP.GRATIS ?};
            {? DISP.GRA_IL || TAP.GRA_IL:=DISP.GRA_IL$TAP.GRATIS().DOKL ?};
            {? DISP.GRA_CEN || TAP.GRA_CEN:=DISP.GRA_CEN$2 ?}
         ||
            {? DISP.OD<>date(0,0,0) || TAP.OD:=DISP.OD ?};
            {? DISP.DO<>date(0,0,0) & TAP.OD<=DISP.DO || TAP.DO:=DISP.DO ?};
            {? DISP.DOZ='T' || TAP.DO:=date(0,0,0) ?};
            {? DISP.TAPTOD<>time(0,0,0) || TAP.T_OD:=DISP.TAPTOD ?};
            {? DISP.TAPTODZ='T' || TAP.T_OD:=time(0,0,0) ?};
            {? TAP.DO<>date(0,0,0) & DISP.TAPTDO<>time(0,0,0)
               & (TAP.OD<TAP.DO | TAP.OD=TAP.DO & TAP.T_OD<DISP.TAPTDO) || TAP.T_DO:=DISP.TAPTDO
            ?};
            {? DISP.TAPTDOZ='T' || TAP.T_DO:=time(0,0,0) ?};
            TAR.cntx_psh;
            {? TAZ.KOD<>'' & (TAP.TAR().KH<>null | TAR.GRKH<>null) || exec('taz_kod_po','ceny') ?};
            TAR.cntx_pop;
            {? DISP.TAR & (TAP.TAR().KH<>null | TAP.TAR().GRKH<>null) || TAP.TARB:=$DISP.TAR ?};
            {? DISP.TARBZ='T' || TAP.TARB:='' ?};
            {? DISP.WALU<>null || TAP.WAL:=DISP.WALU ?};
            {? DISP.TAPIL || TAP.IL:=DISP.TAPIL ?};
            {? DISP.TAPILZ='T' || TAP.IL:=0 ?};
            {? DISP.TAPILDO & TAP.IL<=DISP.TAPILDO || TAP.IL_DO:=DISP.TAPILDO ?};
            {? DISP.TAPILDOZ='T' || TAP.IL_DO:=0 ?};
            {? DISP.TAPWAR<>0 || TAP.WAR:=DISP.TAPWAR ?};
            {? DISP.TAPWARZ='T' || TAP.WAR:=0 ?};
            {? DISP.TAPWADO & TAP.WAR<=DISP.TAPWADO || TAP.WAR_DO:=DISP.TAPWADO ?};
            {? DISP.TAPWADOZ='T' || TAP.WAR_DO:=0 ?};
            {? DISP.TAPPL<>null || TAP.PL:=DISP.TAPPL ?};
            {? DISP.TAPPLZ='T' || TAP.PL:=null ?};
            {? DISP.TAPFOC
            || TAP.CEN_ZAKR:='N';
               TAP.CEN_OD:=TAP.CENB_OD:=TAP.CEN:=TAP.CENB:=0;
               TAP.FO_CEN:=DISP.TAPFOC
            ?};
            {? DISP.TAPFOCZ='T' || TAP.FO_CEN:=null() ?};
            {? DISP.TAPZAKC<>'' || TAP.CEN_ZAKR:=DISP.TAPZAKC ?};
            {? DISP.TAPCOD<>0 & (TAP.CEN=0 | DISP.TAPCOD<TAP.CEN)|| TAP.FO_CEN:=null(); TAP.CEN_OD:=DISP.TAPCOD  ?};
            {? DISP.TAPCBOD<>0 & (TAP.CENB=0 | DISP.TAPCBOD<TAP.CENB) || TAP.FO_CEN:=null();
                                  TAP.CENB_OD:=DISP.TAPCBOD ?};
            {? DISP.TAPZAKC='N' || TAP.CEN_OD:=TAP.CENB_OD:=0  ?};
            _dokl_c:=exec('dokl_ceny','ceny');
            {? DISP.CEN<>0 & TAP.CEN_OD<DISP.CEN || TAP.FO_CEN:=null(); TAP.CEN:=DISP.CEN$_dokl_c ?};
            {? DISP.CENZ='T' || TAP.CEN:=0 ?};
            {? DISP.SV<>null || TAP.SV:=DISP.SV ?};
            {? DISP.CENB<>0 & TAP.CENB_OD<DISP.CENB || TAP.FO_CEN:=null(); TAP.CENB:=DISP.CENB$_dokl_c ?};
            {? DISP.CENBZ='T' || TAP.CENB:=0 ?};
            {? DISP.TAPFORZ<>'' || TAP.FO_RAB:=DISP.TAPFOR; TAP.PRC:=TAP.RABBRT:=TAP.RABNET:=0 ?};
            {? DISP.TAPFORZ='T' || TAP.FO_RAB:=null() ?};
            {? DISP.PRC<>0 || TAP.FO_RAB:=null(); TAP.PRC:=DISP.PRC ?};
            {? DISP.RABNET<>0 || TAP.FO_RAB:=null(); TAP.RABNET:=DISP.RABNET ?};
            {? DISP.RABBRT<>0 || TAP.FO_RAB:=null(); TAP.RABBRT:=DISP.RABBRT ?};
            {? DISP.RABZ='T' || TAP.PRC:=TAP.RABNET:=TAP.RABBRT:=0 ?};
            {? DISP.TAPRABB<>'' || TAP.RAB_B:=DISP.TAPRABB ?};
            {? DISP.PROMO<>null || TAP.PROMO:=DISP.PROMO ?};
            {? DISP.PROMOZ='T' || TAP.PROMO:=null ?};
:: aktualizacja ceny brutto poz zmianie vat
            {? DISP.SV<>null & var_press('__grp_cb')=1 & __grp_cb=1
            ||
               _svat:="TAP.SV().KOD";
               _cenn:="TAP.CEN";
               _cenb:="TAP.CENB";
               _cennod:="TAP.CEN_OD";
               _cenbod:="TAP.CENB_OD";

               _vat:=#((_svat()*'%')+_svat()-1)/100;
               _cbrt:=(_cenn()+(_cenn()*_vat))$_dokl_c;
               _cbrtod:=(_cennod()+(_cennod()*_vat))$_dokl_c;
               {? (_cbrt<>_cenb() | _cbrtod<>_cenbod())
               || _cenb():=_cbrt;
                  _cenbod():=_cbrtod
               ?}
            ?}
         ?};
         TAP.put()
      ?}
   ?}
?};
''


\tap_okno
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: ustawianie okna edycyjnego dla pozycji cenników
::----------------------------------------------------------------------------------------------------------------------
_edit:='RED_'+TAZ.SD;
_szuk:='SZUK_'+TAZ.SD;

{? POMOC.RODZ=''
|| TAP.efld_opt(_edit,'mark=0',,'M');
   TAP.efld_opt(_edit,'mark=1',,'MGR');
   TAP.efld_opt(_edit,'enable=0',,'M');
   TAP.efld_opt(_edit,'enable=1',,'MGR');
   TAP.efld_opt(_edit,'enable=1',,'MGRP')
|| TAP.efld_opt(_edit,'mark=1',,'M','KTM');
   TAP.efld_opt(_edit,'mark=0',,'MGR');
   TAP.efld_opt(_edit,'enable=1',,'M');
   TAP.efld_opt(_edit,'enable=0',,'MGR');
   TAP.efld_opt(_edit,'enable=0',,'MGRP')
?};
TAP.win_edit(_edit);
TAP.win_patt(_szuk);
''


\bl_tar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formula na blank na pozycji cennika
::----------------------------------------------------------------------------------------------------------------------
{? DISP.TAR=null || DISP.TAR:=TAR.ref() ?};
DISP.TAR


\tap_lock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: r_lock TAP
::   WE: [_a] - 0-bez komunikatów, 1-z komunikatami
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};

exec('lock','#record','TAP','SES_ID',TAP.ref,_a)


\tap_prc_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed wyświetleniem TAP.PRC
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('upr_cscz','ceny',2,TAZ.SD)
|| 0
|? TAP.FO_RAB<>null
|| exec('findfnv','#color')
|? TAZ.UPR=0
|| exec('get','#params',800830,2)='N'
|| 1
?}


\tap_rabnet_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed wyświetleniem TAP.RABNET
::----------------------------------------------------------------------------------------------------------------------
exec('tap_prc_bd','ceny')


\tap_rabbrt_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed redakcja TAP.RABBRT
::----------------------------------------------------------------------------------------------------------------------
exec('tap_rabnet_be','ceny')


\tap_rabbrt_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: po redakcji TAP.RABBRT
::----------------------------------------------------------------------------------------------------------------------
exec('tap_rabnet_ae','ceny')


\tap_rabbrt_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed wyswietleniem TAP.RABBRT
::----------------------------------------------------------------------------------------------------------------------
exec('tap_rabnet_bd','ceny')


\kol_tap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: kolorowanie pól w oknie TAP
::   WY: kod koloru
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? TAP.get
||
   TAR.cntx_psh(); M.cntx_psh();
   TAP.TAR();
   TAP.M();

   {? TAZ.SD='G'
   || TAZ.UPR:=1
   || _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
      _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
      _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
      TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr)
   ?};

   KALK.CEN_KH:={? TAR.KH || TAR.KH().NAZ_P || '' ?};
   KALK.CEN_GRKH:={? TAR.GRKH || TAR.GRKH().NAZ || '' ?};

   WYSYP.JMM:=TAP.OP;
   _tap_a:=TAP.A;
   _tap_r:=TAP.RODZ;
   TAP.A:=
      {? (TAP.OD<date()
          | TAP.OD=date & (TAP.T_OD=time(0,0,0) | TAP.T_OD<=time))
         &
         (TAP.DO=date(0,0,0)
          | date()<TAP.DO
          | date=TAP.DO & (TAP.T_DO=time(0,0,0) | time<=TAP.T_DO))
      || 'T'
      || 'N'
      ?};
   TAP.RODZ:={? TAR.KH || 'K' |? TAR.GRKH || 'G' || 'C' ?};
   {? _tap_a<>TAP.A | _tap_r<>TAP.RODZ
   || {?  exec('tap_lock','ceny',0) || TAP.put; exec('tap_unlock','ceny') ?}
   ?};
   {? _a || exec('tap_act','ceny') ?};
   exec('tap_spc','ceny');
   {? TAP.M
   || POMOC.RODZ:=M.RODZ
   || POMOC.RODZ:=''
   ?};
   TAR.cntx_pop(); M.cntx_pop()
?};
exec('rekprzed','color',{? TAZ.SD='G' || 'TAP#03' || 'TAP#01' ?})


\bd_tap_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem pól tabeli TAP
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('upr_cscz','ceny',2,TAZ.SD)
|| 0
|? TAP.FO_CEN<>null
|| exec('findfnv','#color')
|? TAZ.UPR=0
||  exec('get','#params',800830,2)='N'
|| 1
?}


\upr_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: sprawdza uprawnienia do pozycji cennika
::   WE: _a - KH_ODB.ref
::       _b - KH.ref
::       _c - GRKH.ref
::       _d - M.ref
::       _e - MGRP.ref
::       _f - MGR.ref
::   WY: 1-uprawniony, 0-nieuprwaniony
::----------------------------------------------------------------------------------------------------------------------
_iter1:=0;
_iter2:=0;
_odb:=_a; _iter1+=_odb<>null;
_kh:=_b; _iter1+=_kh<>null;
_grkh:=_c; _iter1+=_grkh<>null;
_mat:=_d; _iter2+=_mat<>null;
_mgrp:=_e; _iter2+=_mgrp<>null;
_mgr:=_f; _iter2+=_mgr<>null;

_wyn:=0;

_uprgr:='';
TARUP.index('USER');
TARUP.prefix(OPERATOR.USER);
{? TARUP.first
||
   {!
   |?
      _uprgr+=$TARUP.TARGN;
      TARUP.next
   !}
?};

USERSGRU.index('USR_GR_K');
TARUP.prefix(null);
_loop:=TARUP.first();
{!
|? _loop
|!
   USERSGRU.prefix(OPERATOR.USER,TARUP.USERSGRP().KOD);
   _loop1:=USERSGRU.first();
   {!
   |? _loop1
   |!
      _loop1:=
         {? USERSGRU.USERSGRP().KOD=TARUP.USERSGRP().KOD
         || _uprgr+=$TARUP.TARGN;
            0
         || USERSGRU.next()
         ?}
   !};
   _loop:=TARUP.next()
!};

TARGP.index('UPR');

:: pelne uprawnienia
TARGP.prefix(ST.ODDZ,TAZ.SD,null,null,null,null,null,null);
{? TARGP.first
||
   {!
   |?
      {? _uprgr*$TARGP.TARGN || return(1) ?};
      TARGP.next
   !}
?};

:: analiza uprawnien
{! _ii.._iter1+1
|? _ii<=_iter1+1 & _wyn=0
|!
   {! _jj.._iter2+1
   |? _jj<=_iter2+1 & _wyn=0
   |!
      TARGP.prefix(ST.ODDZ,TAZ.SD,_grkh,_kh,_odb,_mgr,_mgrp,_mat);
      {? TARGP.first
      ||
         {!
         |?
            _wyn:=_uprgr*$TARGP.TARGN;
            _wyn=0 & TARGP.next
         !}
      ?};
      {? _mat || _mat:=null
      |? _mgrp || _mgrp:=null
      |? _mgr || _mgr:=null
      ?}
   !};
   _mat:=_d;
   _mgrp:=_e;
   _mgr:=_f;
   {? _odb || _odb:=null
   |? _kh || _kh:=null
   |? _grkh || _grkh:=null
   ?}
!};

_wyn


\tap_spc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: ustawianie okien edycyjnych dla pozycji cenników
::----------------------------------------------------------------------------------------------------------------------

_par:=0;
{? TAP.M<>null
|| _par:=1
?};
exec('tap_okno','ceny',_par);
''


\tap_unlock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: r_unlock na TAP
::----------------------------------------------------------------------------------------------------------------------
exec('unlock','#record','TAP','SES_ID',TAP.ref)


\tap_count
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: sprawdza czy pozycja cennika TAP wykorzystana
::   WE: [_a] - tabela zwracana przez sel_aget
::       [_b] - 1-z  komunikatami, 0-bez komunikatow
::       [_c] - tabela z maskami TAR_H
::       [_d] - 1-wykonać TAR_H.del, 0-wpp
::   WY: -1/0/>1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? var_pres('_a')<>type_of(INFO) || _a:=null ?};
{? var_pres('_b')<>type_of(1) || _b:=1 ?};
{? var_pres('_c')<>type_of(INFO) || _c:=TAR_H.names ?};
{? var_pres('_d')<>type_of(1) || _d:=1 ?};

TAR_H.cntx_psh();
_Tab:=_c;

{? var_pres('_a')=type_of(null)
||
   _loop:=_Tab.first();
   {!
   |? _loop
   |!
      _loop1:=2;
      {!
      |? _loop1
      |!
         TAR_H.use(_Tab.NAME);
         {? _loop1=2 || TAR_H.index('HIS')
         |? _loop1=1 || TAR_H.index('TAPB')
         ?};
         TAR_H.prefix(TAP.ref);
         _loop2:=TAR_H.first() & _d;
         {!
         |? _loop2
         |!
            _loop2:=
               {? _loop1=2 & TAR_H.TAP=TAP.ref | _loop=1 & TAR_H.TAPB=TAP.ref
               || {? TAR_H.count()>0
                  || _wyn:=1;
                     0
                  || TAR_H.del
                  ?}
               || TAR_H.next
               ?}
         !};
         _loop1-=1;
         _loop1:=_wyn=0 & _loop1
      !};
      _loop:=_wyn=0 & _Tab.next()
   !}
||
   TAP.cntx_psh; _a.cntx_psh;
   TAP.prefix;
   {? _a.first
   ||
      {!
      |?
         {? TAP.seek(_a.REF,)
         ||
            _loop:=_Tab.first();
            {!
            |? _loop
            |!
               _loop1:=2;
               {!
               |? _loop1
               |!
                  TAR_H.use(_Tab.NAME);
                  {? _loop1=2 || TAR_H.index('HIS')
                  |? _loop1=1 || TAR_H.index('TAPB')
                  ?};
                  TAR_H.prefix(TAP.ref);
                  _loop2:=TAR_H.first() & _d;
                  {!
                  |? _loop2
                  |!
                     _loop2:=
                        {? _loop1=2 & TAR_H.TAP=TAP.ref | _loop=1 & TAR_H.TAPB=TAP.ref
                        || {? TAR_H.count()>0
                           || _wyn:=1;
                              0
                           || TAR_H.del
                           ?}
                        || TAR_H.next
                        ?}
                  !};
                  _loop1-=1;
                  _loop1:=_wyn=0 & _loop1
               !};
               _loop:=_wyn=0 & _Tab.next()
            !}
         ||
            _wyn:=-1
         ?};
         ~_wyn & _a.next
      !}
   ?};
   TAP.cntx_pop; _a.cntx_pop
?};

{? _wyn
||
   _txt:=
      {? _wyn=-1
      || 'Sprawdzenie nie powiodło się.'
      |? _wyn=1
      || {? var_pres('_a')<>type_of(null) & _a.size>1
         || 'Co najmniej jedna z zaznaczonych pozycji wykorzystana w dokumentach.'@
         || 'Pozycja wykorzystywana w dokumentach.'@
         ?}
      || ''
      ?};
   {? _b || FUN.info(_txt) ?}
?};
TAR_H.cntx_pop();

_wyn


\zmiencen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: zmiana cen na pozycjach cennika
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('upr_cscz','ceny',2,TAZ.SD)
|| FUN.info({? TAZ.SD='S' || 'Brak uprawnień do wartości sprzedaży.'@ || 'Brak uprawnień do wartości zakupu.'@ ?});
   return(0)
?};

HELP.win_edit('ZM_CENE');
HELP.WARZM:=0;
{? HELP.TYPZM='' || HELP.TYPZM:='P' ?};
exec('prtypzm','ceny');
exec('potypzm','ceny');
_tab:=TAP.sel_aget();

_lock:=0;
_ok:=0;
{? _tab.size()
||
   TAP.cntx_psh();
   {!
   |?
      {? TAP.seek(_tab.REF,) || _lock:=exec('tap_lock','ceny') ?};
      _lock & _tab.next()
   !};
   TAP.cntx_pop();
   {? _lock
   ||
      _ok:=FUN.ask('Czy zmienić cenę na zaznaczonych pozycjach cennika?'@)
   ||
      TAP.cntx_psh();
      _loop:=_tab.first();
      {!
      |? _loop
      |!
         {? TAP.seek(_tab.REF,) || _lock:=exec('tap_unlock','ceny') ?};
         _loop:=_tab.next()
      !};
      TAP.cntx_pop()
   ?}
||
   {? exec('tap_lock','ceny')
   ||
      _lock:=1;
      TAP.M();
      _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
      _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
      _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
      {? exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr)
      || _ok:=FUN.ask('Czy zmienić cenę na danej pozycji cennika?'@)
      || FUN.info('Brak uprawnień do pozycji.'@)
      ?}
   ?}
?};

{? _lock & _ok & HELP.edit("exec('sprzmcen','ceny')")
||
   TAP.sel_adel();
   {? _tab.first
   || exec('tap_count','ceny',_tab,,,0)
   || exec('tap_count','ceny',,,,0)
   ?};
   {? _tab.first()
   ||
      exec('ini_kom','#message','Grupowe dołączanie'@);
      {!
      |? TAP.cntx_psh();
         {? (TAP.clear(); TAP.seek(_tab.REF,TAP.name()))
         || TAP.M();
            _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
            _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
            _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
            {? exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr)
            ||
               _dokl_c:=exec('dokl_ceny','ceny');
               _vat:=#((TAP.SV().KOD*'%')+TAP.SV().KOD-1)/100;
               {? HELP.TYPZM='P'
               || _warcnod:={? HELP.ZCN='T' || TAP.CEN_OD*(1+(HELP.WARZM*0.01)) || TAP.CEN_OD ?}$_dokl_c;
                  _warcn:={? HELP.ZCN='T' || TAP.CEN*(1+(HELP.WARZM*0.01)) || TAP.CEN ?}$_dokl_c;
                  _warcbod:={? HELP.ZCB='T' || TAP.CENB_OD*(1+(HELP.WARZM*0.01)) || TAP.CENB_OD ?}$_dokl_c;
                  _warcb:={? HELP.ZCB='T' || TAP.CENB*(1+(HELP.WARZM*0.01)) || TAP.CENB ?}$_dokl_c
               || _warcnod:={? HELP.ZCN='T' || TAP.CEN_OD+HELP.WARZM || TAP.CEN_OD ?}$_dokl_c;
                  _warcn:={? HELP.ZCN='T' || TAP.CEN+HELP.WARZM || TAP.CEN ?}$_dokl_c;
                  _warcbod:={? HELP.ZCB='T' || TAP.CENB_OD+HELP.WARZM || TAP.CENB_OD ?}$_dokl_c;
                  _warcb:={?  HELP.ZCB='T' || TAP.CENB+HELP.WARZM || TAP.CENB ?}$_dokl_c
               ?};
               {? HELP.ZPN='T'
               || _warcbod:=_warcnod*(1+_vat) $_dokl_c;
                  _warcb:=_warcn*(1+_vat) $_dokl_c
               ?};
               {? HELP.ZPB='T'
               || _warcn:=_warcbod/(1+_vat) $_dokl_c;
                  _warcn:=_warcb/(1+_vat) $_dokl_c
               ?};
               {? TAP.CEN_ZAKR='T' & HELP.ZOD='T' & _warcnod<TAP.CEN & _warcbod<TAP.CENB
               || TAP.CEN_OD:=_warcnod;
                  TAP.CENB_OD:=_warcbod
               |? HELP.ZOD='N' & (TAP.CEN_ZAKR='N' | TAP.CEN_ZAKR='T' & TAP.CEN_OD<_warcn & TAP.CENB<_warcb)
               || TAP.CEN:=_warcn;
                  TAP.CENB:=_warcb
               || exec('add_kom','#message','Pozycja %1 nie spełnia kryteriów.'@[{? TAP.M || M.KTM
                                                                                 |? TAP.MGRP || TAP.MGRP().KOD
                                                                                 |? TAP.MGR || TAP.MGR().KOD
                                                                                 || ''
                                                                                 ?}],7)
               ?};
               TAP.put(1)
            ||
               exec('add_kom','#message','Brak uprawnień do %1.'@[{? TAP.M || M.KTM
                                                                  |? TAP.MGRP || TAP.MGRP().KOD
                                                                  |? TAP.MGR || TAP.MGR().KOD
                                                                  || ''
                                                                  ?}],7)
            ?}
         ?};
         TAP.cntx_pop();
         _tab.next()
      !};
      _loop:=_tab.first();
      {!
      |? _loop
      |!
         {? TAP.seek(_tab.REF,) || exec('tap_unlock','ceny') ?};
         _loop:=_tab.next()
      !};
      exec('end_kom','#message')
   ||
      _dokl_c:=exec('dokl_ceny','ceny');
      _vat:=#((TAP.SV().KOD*'%')+TAP.SV().KOD-1)/100;
      {? HELP.TYPZM='P'
      || _warcnod:={? HELP.ZCN='T' || TAP.CEN_OD*(1+(HELP.WARZM*0.01)) || TAP.CEN_OD ?}$_dokl_c;
         _warcn:={? HELP.ZCN='T' || TAP.CEN*(1+(HELP.WARZM*0.01)) || TAP.CEN ?}$_dokl_c;
         _warcbod:={? HELP.ZCB='T' || TAP.CENB_OD*(1+(HELP.WARZM*0.01)) || TAP.CENB_OD ?}$_dokl_c;
         _warcb:={? HELP.ZCB='T' || TAP.CENB*(1+(HELP.WARZM*0.01)) || TAP.CENB ?}$_dokl_c
      || _warcnod:={? HELP.ZCN='T' || TAP.CEN_OD+HELP.WARZM || TAP.CEN_OD ?}$_dokl_c;
         _warcn:={? HELP.ZCN='T' || TAP.CEN+HELP.WARZM || TAP.CEN ?}$_dokl_c;
         _warcbod:={? HELP.ZCB='T' || TAP.CENB_OD+HELP.WARZM || TAP.CENB_OD ?}$_dokl_c;
         _warcb:={? HELP.ZCB='T' || TAP.CENB+HELP.WARZM || TAP.CENB ?}$_dokl_c
      ?};
      {? HELP.ZPN='T'
      || _warcbod:=_warcnod*(1+_vat) $_dokl_c;
         _warcb:=_warcn*(1+_vat) $_dokl_c
      ?};
      {? HELP.ZPB='T'
      || _warcnod:=_warcbod/(1+_vat) $_dokl_c;
         _warcn:=_warcb/(1+_vat) $_dokl_c
      ?};
      {? TAP.CEN_ZAKR='T' & HELP.ZOD='T' & _warcnod<TAP.CEN & _warcbod<TAP.CENB
      || TAP.CEN_OD:=_warcnod;
         TAP.CENB_OD:=_warcbod
      |? HELP.ZOD='N'  & (TAP.CEN_ZAKR='N' | TAP.CEN_ZAKR='T' & TAP.CEN_OD<_warcn & TAP.CENB<_warcb)
      || TAP.CEN:=_warcn;
         TAP.CENB:=_warcb
      || FUN.info('Pozycja nie spełnia kryteriów.'@)
      ?};
      TAP.put(1);
      exec('tap_unlock','ceny')
   ?}
|? _lock & _ok ||  exec('tap_unlock','ceny'); 0
?}


\bd_tap_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formuła przed wyświetleniem dla materialu w pozycji cennika
::   WY: kolor lub brak koloru
::----------------------------------------------------------------------------------------------------------------------

{? TAZ.MGR | exec('tap_count','ceny',,0,,0)
|| exec('findfnrd','color')
|? POMOC.RODZ<>''
|| exec('findfnv','#color')
|| ''
?}


\bd_tap_mgrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem TAP.MGRP
::----------------------------------------------------------------------------------------------------------------------

{? TAZ.MGR | exec('tap_count','ceny',,0,,0)
|| exec('findfnrd','color')
|? POMOC.RODZ<>'' | TAP.MGR=null
|| exec('findfnv','#color')
|| ''
?}


\bd_tap_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formuła przed wyświetleniem dla materialu w pozycji cennika
::   WY: kolor lub brak koloru
::----------------------------------------------------------------------------------------------------------------------

{? TAZ.M | exec('tap_count','ceny',,0,,0)
|| exec('findfnrd','color')
|? POMOC.RODZ=''
|| exec('findfnv','#color')
|| ''
?}


\tap_focen_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyswietleniem TAP.FO_CEN
::  OLD: \tap_focen_bd/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------

{? ~exec('upr_cscz','ceny',2,TAZ.SD)
|| 0
|? ~exec('tap_focen_be','ceny')
|| exec('findfnv','#color')
|? TAZ.UPR=0
|| exec('get','#params',800830,2)='N'
|| 1
?}


\bd_tap_cenod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem TAP.CEN_OD
::----------------------------------------------------------------------------------------------------------------------

{? exec('be_tap_cenod','ceny') || '' || exec('findfnv','#color') ?}


\tap_focen_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcja TAP.FO_CEN
::  OLD: \tap_focen_be/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------

:: FL.TYP_WYN:='1';
FORMULA.win_dict('FORMULY');
FORMULA.win_edit('FORMULY');
TAP.CEN_OD=0 & TAP.CENB_OD=0 & TAP.CEN=0 & TAP.CENB=0 &
exec('upr_cscz','ceny',2,TAZ.SD) & (TAZ.UPR | TAZ.UPR=0 & exec('get','#params',800830,2)='N')


\be_tap_cenod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcją TAP.CEN_OD
::----------------------------------------------------------------------------------------------------------------------
exec('be_tap_fld','ceny') & TAP.CEN_ZAKR='T'


\be_tap_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcja pol tabeli TAP
::----------------------------------------------------------------------------------------------------------------------
exec('upr_cscz','ceny',2,TAZ.SD) & TAZ.UPR  & TAP.FO_CEN=null()


\be_tap_cenbod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcja TAP.CENB_OD
::----------------------------------------------------------------------------------------------------------------------

exec('be_tap_cenod','ceny')


\tap_forab_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyswietleniem TAP.FO_RAB
::  OLD: \tap_forab_bd/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------

{? ~exec('upr_cscz','ceny',2,TAZ.SD)
|| 0
|? ~exec('tap_forab_be','ceny')
|| exec('findfnv','#color')
|? TAZ.UPR=0
|| exec('get','#params',800830,2)='N'
|| 1
?}


\tap_promo_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem TAP.PROMO
::----------------------------------------------------------------------------------------------------------------------

{? exec('tap_promo_be','ceny')
|| ''
|| exec('findfnv','#color')
?}


\bd_tap_cenbod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyswietleniem TAP.CENB_OD
::----------------------------------------------------------------------------------------------------------------------

exec('bd_tap_cenod','ceny')


\tap_forab_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcja TAP.FO_RAB
::  OLD: \tap_forab_be/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------

::FL.TYP_WYN:='1';
FORMULA.win_dict('FORMULY');
FORMULA.win_edit('FORMULY');
TAP.PRC=0 & TAP.RABNET=0 & TAP.RABBRT=0 &
exec('upr_cscz','ceny',2,TAZ.SD) & (TAZ.UPR | TAZ.UPR=0 & exec('get','#params',800830,2)='N')


\tap_promo_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcja TAP.PROMO
::----------------------------------------------------------------------------------------------------------------------

TAZ.SD='S'


\bd_tar_khodb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem TAR.KH_ODB
::----------------------------------------------------------------------------------------------------------------------

{? exec('be_tar_khodb','ceny') || '' || exec('findfnrd','color') ?}


\be_tar_khodb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcją TAR.KH_ODB
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_S:=$fld();
{? TAZ.SD='S' || BEER.KH:=TAR.KH; BEER.KH<>null ?}


\bd_tar_prc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.20]
:: OPIS: przed wyświetleniem TAR.PRC
::----------------------------------------------------------------------------------------------------------------------

exec('be_tar_prc','ceny')


\be_tar_prc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.20]
:: OPIS: przed redakcją TAR.PRC
::----------------------------------------------------------------------------------------------------------------------

{? TAR.SD='S'
|| exec('upr_cs','ceny')
|? TAR.SD='D'
|| exec('upr_cm','ceny')
|| 1
?}


\be_tarkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed edycja pola kontrahent w cenniku
::   WY: czy mozna edytowac pole
::----------------------------------------------------------------------------------------------------------------------

_wyn:=0;
{? KALK.KH=null
|| _wyn:=1
|| TAR.KH:=KALK.KH;
   win_disp()
?};
_wyn


\be_targr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed edycją pola grupa kontrahentow w cenniku
::   WY: czy można edytować pole
::----------------------------------------------------------------------------------------------------------------------

_wyn:=0;
{? KALK.GRKH=null
|| _wyn:=1
|| TAR.GRKH:=KALK.GRKH;
   win_disp()
?};
_wyn


\ae_tar_khodb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po redakcji TAR.KH_ODB
::----------------------------------------------------------------------------------------------------------------------
{? DPOZ.WPR_S='' & fld()
|| _p800809:=exec('get','#params',800809,1);
   {? TAR.P<_p800809 & FUN.ask('Zmienić priorytet cennika z %1 na %2?'@[$TAR.P,$_p800809])
   || TAR.P:=_p800809
   ?}
?};
1


\ae_tapdo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po redkacji pola TAP.DO
::       wywoływanie również po edycji całego rekordu
::   WY: czy można opuścic redakcję pola
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? TAP.DO<>date(0,0,0)
|| {? TAP.DO<TAP.OD
   || _wyn:=0;
      FUN.info('Data musi być większa lub równa wartości pola "Od daty"\nlub niewypełniona.'@)
   ?}
?};
_wyn


\be_tap_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: Formuła przed redakcją pola TAP.MGR
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_S:=$fld();
_wyn:=0;
{? ~(TAZ.MGR | (POMOC.RODZ='T' | POMOC.RODZ ='U'))
|| _wyn:=exec('tap_count','ceny',,0,,0)=0
?};
_wyn


\be_tap_mgrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcja TAP.MGRP
::----------------------------------------------------------------------------------------------------------------------
TAP.MGR();
_wyn:=0;
{? ~(TAZ.MGR | POMOC.RODZ='T' | POMOC.RODZ ='U' | TAP.MGR=null)
|| _wyn:=exec('tap_count','ceny',,0,,0)=0
?};
_wyn


\be_tap_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: Formuła przed redakcją pola TAP.M
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------

DPOZ.WPR_S:=$TAP.M;
ZAKR.MATU:='W';
_wyn:=0;
{? ~TAZ.M & (POMOC.RODZ='T' | POMOC.RODZ='U')
|| exec('slo_m_ok','material',POMOC.RODZ,1,,'W','W');
   _wyn:=exec('tap_count','ceny',,0,,0)=0
?};
_wyn


\pr_tapjm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: sprawdzenie czy można dodawać ceny wg różnych jednostek miar
::   WY: 1-tak można 0-nie można
::----------------------------------------------------------------------------------------------------------------------
_p400000:=exec('get','#params',400000,2);
TAZ.SD='S' & _p400000*'FK'
| TAZ.SD='D' & (_p400000*'MG' | _p400000*'ZD' | _p400000*'FZ')
| (TAP.M<>null & TAP.JM<>null & TAP.JM<>TAP.M().J)
| TAP.MGR


\tap_il_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji TAP.IL
::----------------------------------------------------------------------------------------------------------------------

_dokl:={? TAP.M & TAP.M().J=TAP.JM || TAP.M().DOKL || ST.DOKL ?};
fld(fld$_dokl);
1


\tap_war_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji TAP.WAR
::----------------------------------------------------------------------------------------------------------------------

fld(fld$2); 1


\be_tap_cenzakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcją TAP.CEN_ZAKR
::----------------------------------------------------------------------------------------------------------------------

TAP.FO_CEN=null()


\ae_tapc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: podpowiadanie wartości brutto po redakcji pola cena
::   WE: [_a] - 0(domyślnie tabela TAP) 1-zmienna DISP
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};

_wyn:=1;

_dokl_c:=exec('dokl_ceny','ceny');

fld():=fld()$_dokl_c;
_svat:="TAP.SV().KOD";
_cenn:=$('TAP.'+cur_afld);
_cenb:=$('TAP.'+{? cur_afld='CEN_OD' || 'CENB_OD' || 'CENB' ?});

{? fld()>0 & ~TAP.sel_size()
|| _vat:=#((_svat()*'%')+_svat()-1)/100;
   _cbrt:=(_cenn()+(_cenn()*_vat))$_dokl_c;
   {? _cbrt<>_cenb() & FUN.ask('Czy obliczyć cenę brutto?'@)
   || _cenb():=_cbrt
   ?}
?};
{? _wyn || win_disp ?};
_wyn


\ae_tapsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: podpowiadanie wartosci brutto
::   WE: [_a] - 0(domyslnie tabela TAP) 1-zmienna DISP
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};

_wyn:=1;

_dokl_c:=exec('dokl_ceny','ceny');

::grupowa aktualizacja ceny brutto
VAR_DEL.delete('__grp_cb');
__grp_cb:=0;

_svat:="TAP.SV().KOD";
_cenn:="TAP.CEN";
_cenb:="TAP.CENB";
_cennod:="TAP.CEN_OD";
_cenbod:="TAP.CENB_OD";

{? fld()<>null
|| _vat:=#((_svat()*'%')+_svat()-1)/100;
   _cbrt:=(_cenn()+(_cenn()*_vat))$_dokl_c;
   _cbrtod:=(_cennod()+(_cennod()*_vat))$_dokl_c;
   {? (_cbrt<>_cenb() | _cbrtod<>_cenbod()) & FUN.ask('Czy obliczyć cenę brutto?'@)
   || _cenb():=_cbrt;
      _cenbod():=_cbrtod
   |? (cur_win()='TAPS' | cur_win()='TAPD') & FUN.ask('Czy obliczyć ceny brutto dla wszystkich pozycji?'@)
   || __grp_cb:=1
   ?}
?};
_wyn


\ae_tapcb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji pola cena brutto
::   WE: [_a] - 0(domyślnie tabela TAP) 1-zmienna DISP
::----------------------------------------------------------------------------------------------------------------------
_dokl_c:=exec('dokl_ceny','ceny');

fld():=fld()$_dokl_c;
_svat:="TAP.SV().KOD";
_cenb:=$('TAP.'+cur_afld);
_cenn:=$('TAP.'+{? cur_afld='CENB_OD' || 'CEN_OD' || 'CEN' ?});

{? fld()>0 & ~TAP.sel_size()
|| _vat:=#((_svat()*'%')+_svat()-1)/100;
   _cnet:=(_cenb()/(1+_vat))$_dokl_c;
   {? _cnet<>_cenn() & FUN.ask('Czy obliczyć cenę netto?'@)
   || _cenn():=_cnet
   ?}
?};
win_disp;
1


\tap_prc_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed redakcja TAP.PRC
::----------------------------------------------------------------------------------------------------------------------
exec('upr_cscz','ceny',2,TAZ.SD) & TAZ.UPR & TAP.FO_RAB=null()


\tap_rabnet_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed redakcja TAP.RABNET
::----------------------------------------------------------------------------------------------------------------------
TAZ.SD='S' & exec('upr_cscz','ceny',2,TAZ.SD) & TAZ.UPR & TAP.FO_RAB=null()


\tap_rabnet_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: po redakcji TAP.RABNET
::----------------------------------------------------------------------------------------------------------------------
_dokl_c:=exec('dokl_ceny','ceny');
fld(fld$_dokl_c);
{? fld<0
|| FUN.info('Wartość rabatu musi być większa od zera.'@); 0
|| win_disp; 1
?}


\ae_tap_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po edycji materiału na pozycji cennika
::   WY: czy opuścić redakcję pola
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
JM.win_dict('WER');
{? fld<>null  &  $fld<>DPOZ.WPR_S
||
   TAP.TAR();
   TAP.M();

   {? TAR.SD='G' |
      (_grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
      _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
      _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
      TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr); TAZ.UPR)
   ||
      M.VAT:=exec('m_vat','material',M.ref(),TAP.TAR().KH,,TAP.OD~1,TAP.OD~2,,,,{? TAZ.SD='D' || 'Z' || 'S' ?});
      {? M.VAT<>null
      ||
         exec('jakislsv','ustawienia');
         SLO.prefix(SLU.ref());
         {? SLO.find_key(M.VAT().KOD)
         ||
            TAP.SV:=M.VAT;
            _dokl_c:=exec('dokl_ceny','ceny');
            _cenb:=TAP.CENB;
            {? TAP.CEN>0
            || _vat:=#((TAP.SV().KOD*'%')+TAP.SV().KOD-1)/100;
               _cbrt:=(TAP.CEN+(TAP.CEN*_vat))$_dokl_c;
               {? _cbrt<>TAP.CENB & FUN.ask('Czy obliczyć cenę brutto?'@)
               || TAP.CENB:=_cbrt
               ?}
            ?}
         ?}
      ?}
   ||
      FUN.info('Brak uprawnień do %1.'@[TAP.M().KTM]);
      _wyn:=0
   ?}
?};
{? _wyn & (TAP.JM=null
 | (TAP.JM<>TAP.M().J
  & FUN.ask('Jednostka miary w pozycji cennika jest inna niż w kartotece %1.\n'
            'Czy zamienić na jednostkę z kartoteki?'@[{? POMOC.RODZ='T' || 'materiałowej' || 'usługowej' ?}])))
|| TAP.JM:=M.J
?};
{? _wyn || win_disp ?};
_wyn


\ae_tap_cenzakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji TAP.CEN_ZAKR
::----------------------------------------------------------------------------------------------------------------------
{? TAP.CEN_ZAKR='N' || TAP.CEN_OD:=TAP.CENB_OD:=0 ?}; win_disp; 1


\tap_prc_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: po redakcji TAP.PRC
::----------------------------------------------------------------------------------------------------------------------
{? fld<0
|| FUN.info('Wartość rabatu musi być większa od zera.'@); 0
|| win_disp; 1
?}


\chk_tap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po edycji TAP
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? exec('ae_tapdo','ceny')=0
|| _wyn:='DO'
|? TAP.T_DO<>time(0,0,0) & TAP.DO=date(0,0,0)
|| FUN.info('Należy wypełnić pole "Do daty" ponieważ wypełnione jest pole "Godzina do"\n'
            'lub usunąć wartość z pola "Godzina do".'@);
   _wyn:='DO'
|| {? POMOC.RODZ<>''
   || _wyn:=chk_rec('TAR','OD','M','JM')
   |? TAP.IL>0
   || _wyn:=chk_rec('TAR','OD','MGR','JM')
   || _wyn:=chk_rec('TAR','OD','MGR')
   ?}
?};

{? _wyn='' & INFO.SLWAL=null
||
   FUN.info('Nie ustalono słownika walut w parametrach pracy programu.'@);
   _wyn:={? POMOC.RODZ<>'' || 'M' || 'MGR' ?}
?};

{? _wyn='' || _wyn:=chk_rec('WAL') ?};

{? _wyn=''
||
   {? TAP.IL<0
   || FUN.info('Wartość nie może być mniejsza od zera.'@);
      _wyn:='IL'
   |? TAP.IL_DO<0
   || FUN.info('Wartość nie może być mniejsza od zera.'@);
      _wyn:='IL_DO'
   |? TAP.IL & TAP.IL_DO & TAP.IL>TAP.IL_DO
   || FUN.info('Ilość od nie może być większa od ilości do.'@);
      _wyn:='IL'
   |? TAP.WAR<0
   || FUN.info('Wartość nie może być mniejsza od zera'@);
      _wyn:='WAR'
   |? TAP.WAR & TAP.WAR_DO & TAP.WAR>TAP.WAR_DO
   || FUN.info('Wartość od nie może być większa od wartości do.'@);
      _wyn:='WAR'
   ?}
?};

{? _wyn='' & TAZ.SD='G'
||
   {? (_wyn:=chk_rec('GRATIS'))<>''
   || 1
   |? TAP.GRA_IL<0
   || FUN.info('Wartość nie może być mniejsza od zera'@);
      _wyn:='GRA_IL'
   |? TAP.GRA_CEN<0
   || FUN.info('Wartość nie może być mniejsza od zera'@);
      _wyn:='GRA_WAR'
   ?}
|? _wyn=''
||
   {? POMOC.RODZ=''
   ||
      TAP.TAR();
      TAP.M();

      _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
      _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
      _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
      {? ~exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr)
      || FUN.info('Brak uprawnień do %1.'@[TAP.MGR().KOD]);
         _wyn:='MGR'
      ?}
   ?};
   {? TAP.CEN_OD<0
   || FUN.info('Wartość nie może być mniejsza od zera.'@);
      _wyn:='CEN_OD'
   |? TAP.CEN_ZAKR='T' & TAP.CEN & TAP.CEN_OD=0
   || FUN.info('Źle zdefiniowany zakres ceny netto.\nWartość od nie może być równa zero.'@);
      _wyn:='CEN_OD'
   |? TAP.CEN<0
   || FUN.info('Wartość nie może być mniejsza od zera.'@);
      _wyn:='CEN'
   |? TAP.CEN<TAP.CEN_OD
   || FUN.info('Źle zdefiniowany zakres ceny netto.\nWartość od nie może być większa od wartości do.'@);
      _wyn:='CEN'
   |? TAP.CENB_OD<0
   || FUN.info('Wartość nie może być mniejsza od zera.'@);
      _wyn:='CENB_OD'
   |? TAP.CENB<0
   || _wyn:='CENB'
   |? TAP.CEN_ZAKR='T' & TAP.CENB & TAP.CENB_OD=0
   || FUN.info('Źle zdefiniowany zakres ceny brutto.\nWartość od nie może być równa zero.'@);
      _wyn:='CEN_OD'
   |? TAP.CENB<TAP.CENB_OD
   || FUN.info('Źle zdefiniowany zakres ceny brutto.\nWartość od nie może być większa od wartości do.'@);
      _wyn:='CEN'
   |? TAP.PRC<0 | TAP.PRC>99.99
   || FUN.info('Rabat musi zawierać sie w przedziale <0;100).'@);
      _wyn:='PRC'
   |? TAP.CEN & TAP.CEN<TAP.RABNET
   || FUN.info('Rabat netto musi być mniejszy od ceny netto.'@);
      _wyn:='RABNET'
   |? TAP.CENB & TAP.CENB<TAP.RABBRT
   || FUN.info('Rabat brutto musi być mniejszy od ceny brutto.'@);
      _wyn:='RABBRT'
   ?}
?};

_wyn


\ae_tap_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji TAP.MGR
::----------------------------------------------------------------------------------------------------------------------
{? DPOZ.WPR_S<>$fld() || TAP.MGRP:=null() ?};

{? TAZ.SD='G'
|| TAZ.UPR:=1
|| TAP.TAR();
   TAP.M();

   _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
   _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
   _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
   TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr);
   win_disp
?};
1


\ae_tap_mgrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji TAP.MGRP
::----------------------------------------------------------------------------------------------------------------------
{? TAZ.SD='G'
|| TAZ.UPR:=1
|? TAP.TAR();
   TAP.M();
   _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
   _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
   _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
   TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr);
   TAZ.UPR
|| win_disp();
   1
|| FUN.info('Brak uprawnień do %1.'@[{? TAP.MGRP || TAP.MGRP().KOD || TAP.MGR().KOD ?}]); 0
?}


\tap_adgm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dołącz z wyborem wielu materialów
::----------------------------------------------------------------------------------------------------------------------
__RODZ:='T'; __M_SEL:='NL_WERG'; __FADDP:='exec(''add_m_tap'',''ceny'')';
params_set();
exec('m_gr_poz','material');

&__RODZ; &__M_SEL; &__FADDP


\tap_adgu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dolacz z wyborem wielu uslug
::----------------------------------------------------------------------------------------------------------------------
__RODZ:='U'; __M_SEL:='NL_WERUG'; __FADDP:='exec(''add_m_tap'',''ceny'')';
exec('m_gr_poz','material');

&__RODZ; &__M_SEL; &__FADDP


\add_m_tap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dodaje rekordy do tabeli TAP na podstawie zaznaczenia
::----------------------------------------------------------------------------------------------------------------------

_gratis:=null;
{? TAZ.SD='G'
||
   M.cntx_psh();
   M.index('ARODZ'); M.prefix('T');
   _wer:=M.mk_sel('Gratisy'@,,,'epjvqpefvnnaaqp',,,,,'U');
   M.win_fld(_wer,,'RODZ',,,1);
   M.win_fld(_wer,,'KTM',,,20);
   M.win_fld(_wer,,'N',,,30);
   M.win_act(_wer,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,1,,,,'A');
   M.win_act(_wer,,'Wyświetl',,,,"exec('slo_m_ok','material',M.RODZ,1); M.display()");
   M.win_act(_wer,,'Szukaj',,,,"M.win_patt(''); M.win_edit('')");
   M.win_act(_wer,,'Kolejność');
   M.win_sel(_wer);
   {? M.select || _gratis:=M.ref() ?};
   M.cntx_pop();
   {? _gratis=null || TAP.sel_adel(); return() ?}
?};

{? var_pres('__tab')<100
||
   TAP.blank();
   TAP.M:=M.ref();

   {? TAZ.SD='G' |
      (_grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
      _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
      _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
      exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr))
   ||
      TAP.JM:=M.J;
      TAP.SV:=exec('m_vat','material',TAP.M,TAP.TAR().KH,,TAP.OD~1,TAP.OD~2,,,,{? TAZ.SD='D' || 'Z' || 'S' ?});
      TAP.GRATIS:=_gratis;
      TAP.OP:=
            {? TAP.MGR || TAP.MGR().KOD+{? TAP.MGRP || ' - '+TAP.MGRP().KOD || '' ?}
            |? TAP.M<>null || M.KTM
            || ''
            ?};
      TAP.add()
   ||
      FUN.info('Brak uprawnień do %1.'@[M.KTM])
   ?}
||
   _f_active:=TAP.f_active();
   TAP.sel_adel();
   {? __tab.first()
   ||
      exec('ini_kom','#message','Grupowe dołączanie'@);
      {!
      |?
         M.prefix();
         {? M.seek(__tab.REF,)
         ||
            TAP.blank();
            TAP.M:=M.ref();
            {? TAZ.SD='G' |
               (_grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
               _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
               _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
               exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr))
            ||
               TAP.JM:=M.J;
               TAP.SV:=exec('m_vat','material',TAP.M,TAP.TAR().KH,,TAP.OD~1,TAP.OD~2,,,,{? TAZ.SD='D' || 'Z' || 'S' ?});
               TAP.TARB:=TAP.TAR().TARB;
               TAP.GRATIS:=_gratis;
               TAP.OP:=
                     {? TAP.MGR || TAP.MGR().KOD+{? TAP.MGRP || ' - '+TAP.MGRP().KOD || '' ?}
                     |? TAP.M<>null || M.KTM
                     || ''
                     ?};
               {? TAP.add() & _f_active=0 || sel_add() ?}
            ||
               exec('add_kom','#message','Brak uprawnień do %1.'@[M.KTM],7)
            ?}
         ?};
         __tab.next()
      !};
      exec('end_kom','#message')
   ?}
?};
''


\prtypzm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: przed typem zmiany ceny
::   WY: 1-jeśli możliwa zmiana 0-jeśli nie
::----------------------------------------------------------------------------------------------------------------------
_buf:=TAP.sel_aget();
_wyn:={? _buf.size() || obj_del(_buf); 1 || TAP.CEN>0 ?};
{? ~_wyn || HELP.TYPZM:='W'; exec('potypzm','ceny') ?};
_wyn


\potypzm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po typie zmiany ceny
::----------------------------------------------------------------------------------------------------------------------
_buf:=TAP.sel_aget();
HELP.JEDZM:={? HELP.TYPZM='P' || ' % ' || {? _buf.size || 'Wal' || TAP.WAL().KOD ?} ?};
win_disp();
HELP.WARZM:=HELP.WARZM ${? HELP.TYPZM='P'
                        || 2
                        || {? _buf.size()
                           || {? (';SG'*TAP.TAR().SD)>1 || ST.DOKL_S || ST.DOKL_Z ?}
                           || {? (';SG'*TAP.TAR().SD)>1
                              || {? TAP.M<>null() || TAP.M().DOKL_S
                                 |? TAP.MGR<>null() || TAP.MGR().DOKL_S
                                 || ST.DOKL_S
                                 ?}
                              || {? TAP.M<>null() || TAP.M().DOKL_Z
                                 |? TAP.MGR<>null() || TAP.MGR().DOKL_Z
                                 || ST.DOKL_Z
                                 ?}
                              ?}
                           ?}
                        ?};
obj_del(_buf);
1


\taz_grajm_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyświetleniem TAZ.GRA_JM
::----------------------------------------------------------------------------------------------------------------------

TAZ.GRA_JM:=TAP.GRATIS().J;
exec('findfnrd','color')


\tap_gratis_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed redakcją TAP.GRATIS
::----------------------------------------------------------------------------------------------------------------------

_wer:=M.mk_sel('Gratisy'@,,,'epjvqcvfvnnaaqp');
M.win_fld(_wer,,'KTM',,,20);
M.win_fld(_wer,,'N',,,30);
M.win_fld(_wer,,'RODZ',,,1);
M.win_act(_wer,,'Wyświetl',,,,"exec('slo_m_ok','material',M.RODZ,1); M.display()",,1);
M.win_act(_wer,,'Szukaj',,,,"M.win_patt(''); M.win_edit('')");
M.win_act(_wer,,'Kolejność');
M.win_dict(_wer);
1


\tap_gratis_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji TAP.GRATIS
::----------------------------------------------------------------------------------------------------------------------
_wer:=M.win_dict('?'); M.win_dict('');
M.win_del(_wer);
win_disp;
1


\tap_grail_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji TAP.GRA_IL
::----------------------------------------------------------------------------------------------------------------------
_dokl:={? TAP.GRATIS || TAP.GRATIS().DOKL || ST.DOKL ?};
fld(fld$_dokl);
1


\tap_gracen_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji TAP.GRA_CEN
::----------------------------------------------------------------------------------------------------------------------
_dokl_s:={? TAP.GRATIS || TAP.GRATIS().DOKL_S || ST.DOKL_S ?};
fld(fld$_dokl_s); 1


\tap_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: usuń pozycje tabeli
::----------------------------------------------------------------------------------------------------------------------
_tab:=TAP.sel_aget;

_ok:=0;
{? _tab.first
|| TAP.sel_adel;
   exec('tap_count','ceny',_tab);
   _ok:=FUN.ask('Usunąć zaznaczone pozycje?'@)
|| _tab.REF:=#TAP.ref; _tab.add;
   {? exec('tap_count','ceny')=0 || _ok:=FUN.ask('Usunąć pozycję?'@) ?}
?};

{? _ok & _tab.first
||
   {!
   |?
      {? TAP.seek(_tab.REF,) & TAP.count=0 & exec('tap_lock','ceny') || TAP.del ?};
      _tab.next
   !}
?};

0


\tap_szczegoly
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: szczegóły cennika/gratisu
::  OLD: \tap_szczegoly/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Poz:=cur_tab(1,1);

{? _Poz.TAR_H=null()
||
   FUN.info('Brak szczegółów.'@)
||
   TAR_H.cntx_psh();
   TAR_H.use(ref_name(_Poz.TAR_H));
   {? _Poz.TAR_H().TAP
   ||
      TAP.cntx_psh(); TAR.cntx_psh();
      TAP.use(ref_name(TAR_H.TAP));
      TAR_H.TAP().TAR();
      TAP.WAL:=TAR_H.WAL;
      TAP.JM:=TAR_H.JM;
      TAP.IL:=TAR_H.IL;
      TAP.IL_DO:=TAR_H.IL_DO;
      TAP.WAR:=TAR_H.WAR;
      TAP.WAR_DO:=TAR_H.WAR_DO;
      TAP.PL:=TAR_H.PL;
      TAP.PROMO:=TAP.PROMO;
      TAP.OD:=TAR_H.OD;
      TAP.DO:=TAR_H.DO;
      TAP.T_OD:=TAR_H.T_OD;
      TAP.T_DO:=TAR_H.T_DO;
      TAP.RAB_B:=TAR_H.RAB_B;
      {? TAR_H.CZY_GRA='T'
      ||
         TAP.GRA_IL:=TAR_H.GRA_IL;
         TAP.GRA_CEN:=TAR_H.GRA_CEN;
         TAP.win_edit('RED_G')
      ||
         {? _Poz=OFP || exec('st_licz_wz','ceny','OFE')
         |? _Poz=ZK_P || exec('st_licz_wz','ceny','ZK_N')
         |? _Poz=FAP || exec('st_licz_wz','ceny','FAKS')
         |? _Poz=DK || exec('st_licz_wz','ceny','ND')
         ?};
         TAP.PRC:=TAP.RABNET:=TAP.RABBRT:=0;
         {? TAZ.CB='T'
         || TAP.CEN_OD:=TAP.CEN:=0;
            TAP.CENB_OD:=TAR_H.CEN_OD;
            TAP.CENB:=TAR_H.CEN;
            {? exec('czyrabp','ceny',TAZ.RAB_TYP)
            || TAP.PRC:=TAR_H.RAB
            || TAP.RABBRT:=TAR_H.RAB
            ?}
         || TAP.CENB_OD:=TAP.CENB:=0;
            TAP.CEN_OD:=TAR_H.CEN_OD;
            TAP.CEN:=TAR_H.CEN;
            {? exec('czyrabp','ceny',TAZ.RAB_TYP)
            || TAP.PRC:=TAR_H.RAB
            || TAP.RABNET:=TAR_H.RAB
            ?}
         ?};
         TAP.win_edit('RED_S')
      ?};
      {? TAR_H.CZY_GRA='T'
      || TAZ.UPR:=1
      || _grkh:={? TAP.TAR().KH || TAR.KH().GRKH || TAR.GRKH ?};
         _mgr:={? TAP.M || _Poz.M().MGR || TAP.MGR ?};
         _mgrp:={? TAP.M || _Poz.M().MGRP || TAP.MGRP ?};
         TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr)
      ?};
      TAP.display();

      TAP.cntx_pop(); TAR.cntx_pop()
   ?};
   TAR_H.cntx_pop()
?}


\tap_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: kopiowanie pozycji cennika
::  OLD: \tap_kopiuj/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
{? TAP.M || exec('tap_add','ceny',TAP.M().RODZ,2)
|? TAP.MGR || exec('tap_add','ceny','G',2)
?}


\cenarch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: archiwizacja lub przywracanie z archiwum pozycji cennika
::----------------------------------------------------------------------------------------------------------------------
_aktmask:=TAP.name+2='__';
_czyarch:=(menu_pth()+1)='P';
_czy_akc:=(_aktmask & _czyarch) | (~_aktmask & ~_czyarch);

TAP.cntx_psh;
TAR.cntx_psh;

_Sel:=TAP.sel_aget;
TAP.sel_adel;

_sel:=1;
{? ~_Sel.first || _sel:=0; _Sel.REF:=#TAP.ref; _Sel.add ?};

{? ~_czy_akc
|| FUN.info('Operacja niedostępna dla aktualnego widoku.'@)
|? _Sel.first & {? _aktmask || FUN.ask('Przenieść do archiwum?'@) || FUN.ask('Przywrócić z archiwum?'@) ?}
|| exec('ini_kom','#message',{? TAP.name+2='__' || 'Przeniesienie do archiwum'@ || 'Przywrócenie z archiwum'@ ?});
   _sel:=0;
   _maska:=(6+TAP.name)+{? _aktmask || 'hs' || '__' ?};
   TAP.prefix;
   {!
   |?
      {? TAP.seek(_Sel.REF,)
      ||
         {? exec('tap_lock','ceny') &
            (TAZ.SD='G' |
            (TAP.TAR();
            TAP.M();
            _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
            _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
            _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
            TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr);
            {? ~TAZ.UPR
            || exec('add_kom','#message','Brak uprawnień do %1.'@[{? TAP.M || M.KTM || TAP.MGR().KOD+
                                                                  {? TAP.MGRP || ' - '+TAP.MGRP().KOD || '' ?} ?}],7)
            ?};
            TAZ.UPR))
         ||
            _tapold:=TAP.ref;
            _tl:=TAP.testlink;
::          dodanie TAP do archiwum
            do;
            TAP.cntx_psh;
            TAP.use(_maska);
            TAP.prefix;
            TAP.SES_ID:='';
            _tapnew:={? TAP.add || TAP.ref || undo; null ?};
            TAP.cntx_pop;
::          podmiana archiwizowanego TAP w powiazanych rekordach
            {? _tl.first
            ||
               {!
               |?
                  _tab:=($_tl.TABELA)();
                  _tab.cntx_psh;
                  _tab.use(_tl.MASKA);
                  _tab.prefix;
                  {? _tab.seek(_tl.REF,)
                  ||
                     {? _tab.TAP=_tapold || _tab.TAP:=_tapnew ?};
                     {? _tab.TAPB=_tapold || _tab.TAPB:=_tapnew ?};
                     _tab.put
                  ?};
                  _tab.cntx_pop;
                  obj_del(_tab);
                  _tl.next
               !}
            ?};
            obj_del(_tl);
::          usuniecie archiwizowanego TAP
            TAP.del;
            {? ~end() || exec('tap_unlock','ceny') ?}
         ?}
      ?};
      _Sel.next
   !};
   exec('end_kom','#message')
?};

TAP.cntx_pop;
TAR.cntx_pop;
{? _sel || TAP.sel_sres(_Sel) ?};
0


\cenarchprzel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przełącza między cennikiem i archiwum cennika
::----------------------------------------------------------------------------------------------------------------------
_tapwer:={? TAZ.SD='G' || 'WER_G' |? TAZ.SD='D' || 'WER_KD' || 'WER_KH' ?};

_params:=params_get();
{? type_of(_params)>0 & var_pres('aLink',_params)>0 & TAP.name+2='__'
|| _aLink:=_params.aLink
|| _aLink:=0
?};

{? _aLink & __spr_ok
||
   TAP.use('taryfphs');
   TAP.win_sel(_tapwer);
   TAP.hdr_sel(''); TAP.hdr_sel(' - ARCHIWUM');
   _ref:=TAP.ref()
||

   {? TAP.name+2='__'
   || {? FUN.ask('Przejść do archiwum?'@)
      || TAP.use('taryfphs');
         TAP.win_sel(_tapwer);
         TAP.hdr_sel(''); TAP.hdr_sel(' - ARCHIWUM')
      || return(1)
      ?}
   |? FUN.ask('Wyjść z archiwum?'@)
   || TAP.use('taryfp__');
      TAP.win_sel(_tapwer);
      TAP.hdr_sel('')
   || return(1)
   ?}
?};
{? ceny.REF<>''
|| TAR.cntx_psh();
   TAR.prefix();
   {? TAR.seek(ceny.REF)
   || DISP.TAR:=TAR.ref()
   || ceny.del()
   ?};
   TAR.cntx_pop()
?};
exec('tar_gr2','ceny',__zmie_a,{? TAZ.RODZ='T' | TAZ.RODZ='U' || 1 |? TAZ.RODZ='G' || 2 || 0 ?},0);
exec('tap_act','ceny');
{? _aLink & __spr_ok
|| TAP.seek(_ref);
   __spr_ok:=0
?};
1


\cenkalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: kalkulacja ceny dla pozycji z cennika
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cs','ceny')=0
|| FUN.info('Brak uprawnień do wartości sprzedaży.'@)
|? TAP.M=null
||
   FUN.info('Kalkulacja dostępna jest dla pozycji cennika z podanym materiałem lub usługą.'@)
||
   TAR.cntx_psh(); M.cntx_psh();
   TAP.TAR();
   TAP.M();
   {? TAZ.SD='G'
   || TAZ.UPR:=1
   || _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
      _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
      _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
      TAZ.UPR:=exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr)
   ?};

   TAR.cntx_pop(); M.cntx_pop();

   {? TAZ.UPR
   ||
      _ok:=1;
      undefine;
      define('KRS',0,'Kurs z %1 na %2'@[TAP.WAL().KOD,INFO.NAROD().KOD],,10,,4);
      {? TAP.WAL<>INFO.NAROD
      ||
         _ok:=def_edit("{? DEFINE.KRS<=0 || FUN.info('Należy podać kurs większy od zera.'@); 0 || '' ?}",'Kurs waluty'@)
      ?};

      {? _ok
      ||
         KALK.JM:=KALK.J2:=TAP.M().J;
         _cena:=exec('zwr_cene','ceny',TAP.M,null,TAP.WAL,DEFINE.KRS$4,'N');

         {? _cena & TAP.CEN_ZAKR='N' & FUN.ask('Przeliczyć ceny wg kalkulacji?'@)
         ||
            _dokl_c:=exec('dokl_ceny','ceny');
            TAP.CEN:=_cena;
            _vat:=#((TAP.SV().KOD*'%')+TAP.SV().KOD-1)/100;
            TAP.CENB:=(TAP.CEN+(TAP.CEN*_vat))$_dokl_c;
            TAP.put
         ?}
      ?};
      undefine
   ||
      FUN.info('Brak uprawnień do pozycji cennika.'@)
   ?}
?}


\zwr_cene
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: zwraca skalkulowaną cenę
::   WE:  _a  - ref M
::       [_b] - ref magazynu jeżeli działamy na konkretnym magazynie
::       [_c] - ref waluty
::       [_d] - kurs
::       [_e] - 'T'-wg brutto 'N'-wg netto (domyślnie)
::       [_f] - 1-zatwierdzono kalkulacje, 0-wpp
::       [_g] - 1-wyświetlac okno edycji, 0-wpp
::   WY: skalkulowana cena
::  OLD: \zwr_cene/kalk.fml
::----------------------------------------------------------------------------------------------------------------------

{? _>=2 || {? type_of(_b)<>7 || _b:=null ?} || _b:=null ?};
{? _>=3 || {? type_of(_c)<>7 || _c:=null ?} || _c:=null ?};
{? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
{? _>=5 || {? type_of(_e)<>2 || _e:='N' ?} || _e:='N' ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=1 ?} || _g:=1 ?};

KALK.WAL:=_c;

KALK.X:=0;

KALK.LD:=KALK.MD:=KALK.PD:=KALK.SD:=0;

_wyn:=0;

VAR_DEL.delete('XXXD','XXXS');

_okr:=exec('get','#params',800807,1);
_emsk:=FAP.name()+2;
_oddz:=1+(FAP.name()+3);

_msk:=obj_new(_okr+1);
{! _i..(_okr+1) |! _msk[_i]:=form((#_emsk)-(_okr+1-_i),-2,0,'99') !};

_bmsk:=_msk[1];

XXXD:=tab_tmp(1,'KTM','STRING[50]',''
       ,'MD','REAL',''
       ,'PD','REAL',''
       ,'SD','REAL',''
       ,'XD','REAL','');
XXXD.clear();
XXXD.blank();
XXXD.KTM:=$_a;
XXXD.MD:=0;
XXXD.PD:=0;
XXXD.SD:=0;
XXXD.XD:=0;
XXXD.add();

XXXS:=tab_tmp(1,'KTM','STRING[50]',''
       ,'MS','REAL',''
       ,'PS','REAL',''
       ,'SS','REAL',''
       ,'XS','REAL','');
XXXS.clear();
XXXS.blank();
XXXS.KTM:=$_a;
XXXS.MS:=0;
XXXS.PS:=0;
XXXS.SS:=0;
XXXS.XS:=0;
XXXS.add();

:: jakby brakowalo kursu
{? _d=0 || _d:=1 ?};

FAKS.cntx_psh();
FAP.cntx_psh();
ND.cntx_psh();
DK.cntx_psh();
SC.cntx_psh();
SM.cntx_psh();
{! _i..(_okr+1)
|! exec('open','open_tab',_oddz,_msk[_i]);
   {? _b=null
   || SC.index('SD');
      SC.prefix(_a)
   || SC.index('SC');
      SC.prefix(_b,_a)
   ?};
   {? SC.first()
   || {!
      |? {? SC.S>0
         || _cena:=exec('cena_mag','ceny',SC.M,SC.RDK,SC.NDK,SC.MAG,SC.C);
            {? XXXD.MD=0 | (_cena/_d)<XXXD.MD || XXXD.MD:=(_cena/_d) $2 ?};
            {? (_cena/_d)>XXXD.PD || XXXD.PD:=(_cena/_d) $2 ?};
            XXXD.SD+=(_cena/_d)*SC.S;
            XXXD.XD+=SC.S;
            XXXD.put(1)
         ?};
         SC.next()
      !}
   ?};
   FAP.index('M');
   {? _b<>null || FAP.prefix(_a,_c) || FAP.prefix(_a) ?};
   {? FAP.first()
   || {!
      |? {? FAP.FAKS().T().KOR='N' & FAP.IL<>0
         || {? INFO.NAROD=_c | _c=null
            || {? _e='T'
               || {? XXXS.MS=0 | FAP.CB<XXXS.MS || XXXS.MS:=FAP.CB ?};
                  {? FAP.CB>XXXS.PS || XXXS.PS:=FAP.CB ?};
                  XXXS.SS+=FAP.CB*FAP.IL
               || {? XXXS.MS=0 | FAP.CN<XXXS.MS || XXXS.MS:=FAP.CN ?};
                  {? FAP.CN>XXXS.PS || XXXS.PS:=FAP.CN ?};
                  XXXS.SS+=FAP.CN*FAP.IL
               ?};
               XXXS.XS+=FAP.IL;
               XXXS.put(1)
            || {? FAP.WAL=_c
               ||
                  {? XXXS.MS=0 | FAP.CWAL<XXXS.MS || XXXS.MS:=FAP.CWAL ?};
                  {? FAP.CWAL>XXXS.PS || XXXS.PS:=FAP.CWAL ?};
                  XXXS.SS+=FAP.CWAL*FAP.IL;
                  XXXS.XS+=FAP.IL;
                  XXXS.put(1)
               ?}
            ?}
         ?};
         FAP.next()
      !}
   ?}
!};

FAKS.cntx_pop();
FAP.cntx_pop();
ND.cntx_pop();
DK.cntx_pop();
SC.cntx_pop();
SM.cntx_pop();

XXXD.SD:={? XXXD.XD<>0 || XXXD.SD/XXXD.XD || 0 ?}$2; XXXD.put(1);
XXXS.SS:={? XXXS.XS<>0 || XXXS.SS/XXXS.XS || 0 ?}$2; XXXS.put(1);

obj_del(_msk);

KALK.LD:=0;
_msk:=SC.name()-2;
_i:=#_emsk;
SC.cntx_psh();
{!
|? SC.index('SD');
   SC.prefix(_a);
   {? SC.last()
   ||
     _cena:=exec('cena_mag','ceny',SC.M,SC.RDK,SC.NDK,,SC.C);
     KALK.LD:={? _d<>0 || _cena/_d || _cena ?}
   ?};
   {? KALK.LD=0 & (_i>#_bmsk)
   || _i-=1;
      SC.use(_msk+form(_i,-2,0,'99'));
      1
   || 0
   ?}
!};
SC.cntx_pop;

_lit:=obj_new(2); _lit[1]:='D'; _lit[2]:='S';

KALK.M:=_a; KALK.WG:=0; KALK.X:=0; KALK.W:=8; KALK.J:=' %'; KALK.A:=1;
MDOST.cntx_psh;
MDOST.index('M');
MDOST.prefix('S',_a,null,_c);
{? MDOST.first || KALK.CM:=MDOST.CMIN || KALK.CM:=0 ?};
MDOST.cntx_pop;
KALK.win_edit({? exec('upr_cm','ceny') & (KALK.LD | KALK.MD | KALK.PD | KALK.SD) || 'KALK' || 'KALK1' ?});
{! _i..2
|! {? ($('XXX'+_lit[_i]+'.clear'))(); ($('XXX'+_lit[_i]+'.first'))()
   || ($('KALK.M'+_lit[_i]))():=($('XXX'+_lit[_i]+'.M'+_lit[_i]))();
      ($('KALK.P'+_lit[_i]))():=($('XXX'+_lit[_i]+'.P'+_lit[_i]))();
      ($('KALK.S'+_lit[_i]))():=($('XXX'+_lit[_i]+'.S'+_lit[_i]))()
   || ($('KALK.M'+_lit[_i]))():=0;
      ($('KALK.P'+_lit[_i]))():=0;
      ($('KALK.S'+_lit[_i]))():=0
   ?}
!};

exec('po_xkalk','ceny');

KALK.hdr_edit; KALK.hdr_edit(' ceny '+{? FAKS.T().FIS='T' || 'brutto' || 'netto' ?});
exec('po_wgkal','ceny');
{? _g & KALK.edit("{? KALK.A || 1 || FUN.info('Należy wybrać i zaakceptować cenę.'@);0 ?}")
|| _wyn:={? KALK.J2<>null & KALK.JM<>KALK.J2 || KALK.CE2 || KALK.CEN ?};
   {? _f<>'' || ($_f)():=1 ?}
?};

VAR_DEL.delete('XXXD','XXXS');
_wyn


\cena_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: zwraca bieżącą cenę magazynową (po korekcie - jeśli była)
::   WE:  _a - materiał (ref)
::        _b - RDK
::        _c - NDK
::       [_d] - magazyn - domyślnie bieżący ST.MAG
::       [_e] - domyślny wynik (cena)
::       [_f] - czy szukać ceny po korekcie 2-również na dokumentach niezaakceptowanych, 3-również ze zgodną datą
::       [_g] - czy cena walutowa 1/0 (tak/nie)
::       [_h] - data
::       [_i] - 0-cena po ostatnej korekcie, 1-cena wg przeceny z dnia _h, 2-cena przed data _h
::  OLD: \cena_mag/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=4 || {? type_of(_d)<>7 || _d:=ST.MAG ?}  || _d:=ST.MAG ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?}  || _e:=0  ?};
{? _>=6 || {? type_of(_f)<>1 || _f:=1 ?}  || _f:=1  ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=0 ?}  || _g:=0  ?};
{? var_pres('_h')<>type_of(date) || _h:=date(0,0,0) ?};
{? var_pres('_i')<>type_of(1) || _i:=0 ?};

_oldh:=_h;
_wyn:=_e;
ND.cntx_psh();
DK.cntx_psh();
{? _c<>'' & DK.name<>_c
|| ND.use('nagdo'+(_c+3));
   DK.use(_c)
?};
:: pierwsza dostawa dla zestawu RDK, NDK
DK.index('DOKPLUS');
DK.prefix(_d,_a,'T','T',_b,_c,'N');
{? DK.first() || _wyn:={? _g=0 || DK.C || DK.CWAL ?}; {? _i=0 || _h:=exec('FindAndGet','#table',ND,$DK.N,,"D",_h) ?} ?};
:: cena po ewentualnej korekcie
{? _f>0 & _c<>'' & (~_i | _h<>date(0,0,0))
||
   _kor:=0;
   _wardk:={? _i=0 & (_f=2 | _f=3) || '>=' |? _i=0 & _f<>2 || '>'  |? _i=2 || '<' || '=' ?};

:: petla po kolejnych okresach
   _rok_end:=_h~1;
   _rok_dost:={? _i<>1 || #('20'+(_c+2)) || _rok_end ?};
   OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   {? (_i=0 & OKR.last()) | (_i>0 & OKR.find_le(_rok_end))
   || {!
      |? DK.cntx_psh();
         ND.cntx_psh();
         DK.use((_c-2)+($OKR.ROK+2));
         ND.use('nagdo'+(5-_c-2)+($OKR.ROK+2));
         DK.index('DOKPLUS');
         DK.prefix(_d,_a,'T','T',_b,_c,'C');
         {? DK.first() & DK.find_tab('first','N','D',_wardk,_h)
         || {!
            |? _kor:={? _g=0 || DK.C || DK.CWAL ?};
               DK.find_tab('next','N','D',_wardk,_h)
            !}
         ?};
         {? _f=2
         || DK.index('DOKPLUS');
            DK.prefix(_d,_a,'T','N',_b,_c,'C');
            {? DK.first() & DK.find_tab('first','N','D',_wardk,_h)
            || {!
               |? _kor:={? _g=0 || DK.C || DK.CWAL ?};
                  DK.find_tab('next','N','D',_wardk,_h)
               !}
            ?}
         ?};
         ND.cntx_pop();
         DK.cntx_pop();
         {? _i<>1 || ~_kor & OKR.ROK>_rok_dost & OKR.prev()
         |? _i=1  || 0
         || 0
         ?}
      !}
   ?};
   OKR.cntx_pop();
   {? _kor>0 || _wyn:=_kor ?}
?};
ND.cntx_pop();
DK.cntx_pop();
_wyn


\po_xkalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: Po redakcji pola proponowana marża/cena
::   WY: 1-jeśli poprawna wartość 0-jeśli mniejsza od zera
::  OLD: \po_xkalk/kalk.fml
::----------------------------------------------------------------------------------------------------------------------
:: To jest narzut
:: [(Cs-Cz)/Cz]*100%= narzut [%]   Cs=Cz*(1+N*0.01)
:: To jest marża
:: [(Cs-Cz)/Cs]*100%= marza [%]    Cs=Cz/(1-M*0.01)
_dokl_c:=KALK.M().DOKL_C;
_dokl_s:=KALK.M().DOKL_S;
_dokl_m:={? _dokl_c>_dokl_s || _dokl_c || _dokl_s ?};

_wyn:=1;
{? KALK.WG=1 & KALK.X<0
|| FUN.info('Cena nie może mieć wartości ujemnej.'@); KALK.X:=0; _wyn:=0
|? KALK.WG=0 & KALK.X<=-100
|| FUN.info('Narzut nie może mieć wartości mniejszej lub równej -100.'@); KALK.X:=0; _wyn:=0
|? KALK.WG=2 & KALK.X>=100
|| FUN.info('Marża nie może mieć wartości większej lub równej 100.'@); KALK.X:=0; _wyn:=0
|? KALK.WG=1
|| KALK.CLD:=form({? KALK.LD<>0 || (KALK.X-KALK.LD)/(KALK.LD*0.01) || 0 ?}$2,18,2)+' %';
   KALK.CCM:=form({? KALK.CM<>0 || (KALK.X-KALK.CM)/(KALK.CM*0.01) || 0 ?}$2,18,2)+' %';
   KALK.CMD:=form({? KALK.MD<>0 || (KALK.X-KALK.MD)/(KALK.MD*0.01) || 0 ?}$2,18,2)+' %';
   KALK.CPD:=form({? KALK.PD<>0 || (KALK.X-KALK.PD)/(KALK.PD*0.01) || 0 ?}$2,18,2)+' %';
   KALK.CSD:=form({? KALK.SD<>0 || (KALK.X-KALK.SD)/(KALK.SD*0.01) || 0 ?}$2,18,2)+' %';
   KALK.CMS:=form({? KALK.X<>0 || ((KALK.X-KALK.MS)/KALK.X)*100 || 0 ?}$2,18,2)+' %';
   KALK.CPS:=form({? KALK.X<>0 || ((KALK.X-KALK.PS)/KALK.X)*100 || 0 ?}$2,18,2)+' %';
   KALK.CSS:=form({? KALK.X<>0 || ((KALK.X-KALK.SS)/KALK.X)*100 || 0 ?}$2,18,2)+' %'
|? KALK.WG=0
||
   KALK.CLD:=form(KALK.LD*(1+KALK.X*0.01)$_dokl_c,18,_dokl_m);
   KALK.CCM:=form(KALK.CM*(1+KALK.X*0.01)$_dokl_s,18,_dokl_m);
   KALK.CMD:=form(KALK.MD*(1+KALK.X*0.01)$_dokl_c,18,_dokl_m);
   KALK.CPD:=form(KALK.PD*(1+KALK.X*0.01)$_dokl_c,18,_dokl_m);
   KALK.CSD:=form(KALK.SD*(1+KALK.X*0.01)$_dokl_c,18,_dokl_m);
   KALK.CMS:=form(KALK.MS*(1+KALK.X*0.01)$_dokl_s,18,_dokl_m);
   KALK.CPS:=form(KALK.PS*(1+KALK.X*0.01)$_dokl_s,18,_dokl_m);
   KALK.CSS:=form(KALK.SS*(1+KALK.X*0.01)$_dokl_s,18,_dokl_m)
|? KALK.WG=2
||
   KALK.CLD:=form(KALK.LD/(1-KALK.X*0.01)$_dokl_c,18,_dokl_m);
   KALK.CCM:=form(KALK.CM/(1-KALK.X*0.01)$_dokl_s,18,_dokl_m);
   KALK.CMD:=form(KALK.MD/(1-KALK.X*0.01)$_dokl_c,18,_dokl_m);
   KALK.CPD:=form(KALK.PD/(1-KALK.X*0.01)$_dokl_c,18,_dokl_m);
   KALK.CSD:=form(KALK.SD/(1-KALK.X*0.01)$_dokl_c,18,_dokl_m);
   KALK.CMS:=form(KALK.MS/(1-KALK.X*0.01)$_dokl_s,18,_dokl_m);
   KALK.CPS:=form(KALK.PS/(1-KALK.X*0.01)$_dokl_s,18,_dokl_m);
   KALK.CSS:=form(KALK.SS/(1-KALK.X*0.01)$_dokl_s,18,_dokl_m)
?};
{? _wyn || exec('po_wkalk','ceny') ?};
_wyn


\po_wgkal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: Po redakcji pola WG marży/ceny
::   WY: 1
::  OLD: \po_wgkal/kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? KALK.WG=1
|| KALK.J:=KALK.WAL().KOD;
   KALK.OP2:='MARŻA'; KALK.OP1:='PROC. ZWIĘKSZ.';
   {? KALK.X<>0 & (KALK.CLD+1)<>'%'  || KALK.X:=#gsub(KALK.CMS,',','.') ?}
|? KALK.WG=2
|| KALK.J:=' %'; KALK.OP2:='CENA SPRZEDAŻY'; KALK.OP1:='WART. DOSTAWY';
   {? KALK.X<>0 & (KALK.CMS+1)='%'
   || _marza:=(#gsub((KALK.CMS-2),',','.'))$2;
      KALK.X:=_marza
   || _cenas:=(#gsub((KALK.CMS-2),',','.'))$2;
      KALK.X:={? _cenas || (_cenas-KALK.MS)/_cenas || 0 ?}*100$2
   ?}
|? KALK.WG=0
|| KALK.J:=' %'; KALK.OP2:='CENA SPRZEDAŻY'; KALK.OP1:='WART. DOSTAWY';
   {? KALK.X<>0 & (KALK.CMS+1)='%'
   || _marza:=(#gsub((KALK.CMS-2),',','.'))$2;
      _cenas:={? _marza<>100 || KALK.MS/(1-_marza*0.01) || 0 ?}$2
   || _cenas:=(#gsub((KALK.CMS-2),',','.'))$2
   ?};
   KALK.X:={? KALK.MS || (_cenas-KALK.MS)/KALK.MS || 0 ?}*100$2;
   {? KALK.X=-100 || KALK.X:=0 ?}
?};
exec('po_xkalk','ceny');
1


\po_wkalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formuła po wyborze kalkulacji
::   WY: 1
::  OLD: \po_wkalk/kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? KALK.WG=1
|| KALK.CEN:=KALK.X $KALK.M().DOKL_C;
   KALK.CE2:={? KALK.WS2<>0 || KALK.CEN/KALK.WS2 || KALK.CEN ?}$KALK.M().DOKL_S
|| {? KALK.W=3 || KALK.CEN:=#gsub(gsub(KALK.CMD,'.',''),',','.')
   |? KALK.W=4 || KALK.CEN:=#gsub(gsub(KALK.CPD,'.',''),',','.')
   |? KALK.W=5 || KALK.CEN:=#gsub(gsub(KALK.CSD,'.',''),',','.')
   |? KALK.W<=0|| KALK.CEN:=0
   |? KALK.W=1 || KALK.CEN:=#gsub(gsub(KALK.CCM,'.',''),',','.')
   |? KALK.W=2 || KALK.CEN:=#gsub(gsub(KALK.CLD,'.',''),',','.')
   |? KALK.W=6 || KALK.CEN:=#gsub(gsub(KALK.CMS,'.',''),',','.')
   |? KALK.W=7 || KALK.CEN:=#gsub(gsub(KALK.CPS,'.',''),',','.')
   |? KALK.W=8 || KALK.CEN:=#gsub(gsub(KALK.CSS,'.',''),',','.')
   ?};
   KALK.CE2:={? KALK.WS2<>0 || KALK.CEN/KALK.WS2 || KALK.CEN ?}$KALK.M().DOKL_S
?};
1


\pwfkalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: przed wyświetleniem pól w okienku kalkulacji
::  OLD: \pwfkalk/kalk.fml
::----------------------------------------------------------------------------------------------------------------------
{? KALK.WHERE='F' || exec('findfnv','#color') || '' ?}


\pr_zcn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: czy możliwa redakcja pola
::   WY: 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_fld:=cur_afld();
_wyn:={? _fld='ZCN' & HELP.ZCB='T' & HELP.ZPB='T' || 0
      |? _fld='ZCB' & HELP.ZCN='T' & HELP.ZPN='T' || 0
      |? _fld='ZPN' & HELP.ZCB='T' || 0
      |? _fld='ZPB' & HELP.ZCN='T' || 0
      || 1
     ?};
_wyn


\po_zcn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: po redakcji pola
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
{? _fld='ZPN' & HELP.ZPN='T' || HELP.ZCB:='N'; HELP.ZPB:='N'; HELP.ZCN:='T'
|? _fld='ZPB' & HELP.ZPB='T' || HELP.ZCN:='N'; HELP.ZPN:='N'; HELP.ZCB:='T'
|? _fld='ZCN' & HELP.ZCN<>'T' || HELP.ZPN:='N'
|? _fld='ZCB' & HELP.ZCB<>'T' || HELP.ZPB:='N'
?};
HELP.efld_opt('ZM_CENE',{? HELP.ZCB='T' & HELP.ZPB='T' || 'enable=0' || 'enable=1' ?},HELP,'ZCN');
HELP.efld_opt('ZM_CENE',{? HELP.ZCN='T' & HELP.ZPN='T' || 'enable=0' || 'enable=1' ?},HELP,'ZCB');
HELP.efld_opt('ZM_CENE',{? HELP.ZCB='T' || 'enable=0' || 'enable=1' ?},HELP,'ZPN');
HELP.efld_opt('ZM_CENE',{? HELP.ZCN='T' || 'enable=0' || 'enable=1' ?},HELP,'ZPB');
win_disp();
1


\tap_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: uprawnienia w kontekście pozycji cennika
::----------------------------------------------------------------------------------------------------------------------
_Tab:=sql($"
   select
      USERS.REFERENCE US,
      USERSGRP.REFERENCE USG,
      TARGN.REFERENCE TARGN,
      TARGP.GRKH GRKH,
      TARGP.KH KH,
      TARGP.KH_ODB KH_ODB,
      TARGP.MGR MGR,
      TARGP.MGRP MGRP,
      TARGP.M M,
      USERS.DANE US_DANE,
      USERSGRP.KOD USG_KOD,
      TARGN.KOD KOD,
      TARGN.OP OP,
      GRKH.KOD GRKH_KOD,
      KH.SKR KH_SKR,
      KH_ODB.NAZ KHO_NAZ,
      MGR.KOD MGR_KOD,
      MGRP.KOD MGRP_KOD,
      M.KTM M_KTM
   from
      TARGP join TARGN
      join TARUP
      left join USERS
      left join USERSGRP
      join GRKH using (TARGP.GRKH,GRKH.REFERENCE)
      join KH using (TARGP.KH,KH.REFERENCE)
      join KH_ODB using (TARGP.KH_ODB,KH_ODB.REFERENCE)
      join MGR using (TARGP.MGR,MGR.REFERENCE)
      join MGRP using (TARGP.MGRP,MGRP.REFERENCE)
      join M using (TARGP.M,M.REFERENCE)
   where
      1=0
   order by
      1,2,3,4,5,6,7,8,9");

TARUP.index('TARGN');

TARGP.index('UPR');
TARGP.prefix(ST.ODDZ,TAZ.SD,null,null,null,null,null,null);
{? TARGP.first
|| {!
   |?
      TARUP.prefix(TARGP.TARGN);
      {? TARUP.first
      ||
         {!
         |?
            {? ~_Tab.find_key($TARUP.USER,$TARUP.USERSGRP)
            ||
               _Tab.blank;
               _Tab.US:=$TARUP.USER;
               _Tab.USG:=$TARUP.USERSGRP;
               _Tab.TARGN:=$TARGP.TARGN;
               _Tab.US_DANE:=TARUP.USER().DANE;
               _Tab.USG_KOD:=TARUP.USERSGRP().KOD;
               _Tab.KOD:=TARGP.TARGN().KOD;
               _Tab.OP:=TARGN.OP;
               _Tab.add
            ?};
            TARUP.next
         !}
      ?};
      TARGP.next
   !}
?};

_iter1:=0;
_iter2:=0;

TAR.cntx_psh(); M.cntx_psh();
TAP.TAR();
TAP.M();

_wgrkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?}; _grkh_ko:={? TAR.KH || TAR.KH().GRKH().KOD || TAR.GRKH().KOD ?};
_wmgr:={? TAP.M || M.MGR || TAP.MGR ?}; _mgr_kod:={? TAP.M || M.MGR().KOD || TAP.MGR().KOD ?};
_wmgrp:={? TAP.M || M.MGRP || TAP.MGRP ?}; _mgrp_ko:={? TAP.M || M.MGRP().KOD || TAP.MGRP().KOD ?};

_odb:=TAR.KH_ODB; _iter1+=_odb<>null; _odb_naz:=TAR.KH_ODB().NAZ;
_kh:=TAR.KH; _iter1+=_kh<>null; _kh_skr:=TAR.KH().SKR;
_grkh:=_wgrkh; _iter1+=_grkh<>null;
_mat:=TAP.M; _iter2+=_mat<>null; _mat_ktm:=TAP.M().KTM;
_mgrp:=_wmgrp; _iter2+=_mgrp<>null;
_mgr:=_wmgr; _iter2+=_mgr<>null;

TAR.cntx_pop(); M.cntx_pop();

TARGP.index('UPR');

:: analiza uprawnien
{! _ii.._iter1+1
|? _ii<=_iter1+1
|!
   {! _jj.._iter2+1
   |? _jj<=_iter2+1
   |!
      TARGP.prefix(ST.ODDZ,TAZ.SD,_grkh,_kh,_odb,_mgr,_mgrp,_mat);
      {? TARGP.first
      ||
         {!
         |?
            TARUP.prefix(TARGP.TARGN);
            {? TARUP.first
            ||
               {!
               |?
                  {? ~_Tab.find_key($TARUP.USER,$TARUP.USERSGRP,$TARGP.TARGN,$_grkh,$_kh,$_odb,$_mgr,$_mgrp,$_mat)
                  ||
                     _Tab.US:=$TARUP.USER;
                     _Tab.USG:=$TARUP.USERSGRP;
                     _Tab.TARGN:=$TARGP.TARGN;
                     _Tab.GRKH:=$_grkh;
                     _Tab.KH:=$_kh;
                     _Tab.KH_ODB:=$_odb;
                     _Tab.MGR:=$_mgr;
                     _Tab.MGRP:=$_mgrp;
                     _Tab.M:=$_mat;
                     _Tab.US_DANE:=TARUP.USER().DANE;
                     _Tab.USG_KOD:=TARUP.USERSGRP().KOD;
                     _Tab.KOD:=TARGP.TARGN().KOD;
                     _Tab.OP:=TARGN.OP;
                     _Tab.GRKH_KOD:={? _grkh || _grkh_ko || '' ?};
                     _Tab.KH_SKR:={? _kh || _kh_skr || '' ?};
                     _Tab.KHO_NAZ:={? _odb || _odb_naz || '' ?};
                     _Tab.MGR_KOD:={? _mgr || _mgr_kod || '' ?};
                     _Tab.MGRP_KOD:={? _mgrp || _mgrp_ko || '' ?};
                     _Tab.M_KTM:={? _mat || _mat_ktm || '' ?};
                     _Tab.add
                  ?};
                  TARUP.next
               !}
            ?};
            TARGP.next
         !}
      ?};
      {? _mat || _mat:=null
      |? _mgrp || _mgrp:=null
      |? _mgr || _mgr:=null
      ?}
   !};
   _mat:=TAP.M;
   _mgrp:=_wmgrp;
   _mgr:=_wmgr;
   {? _odb || _odb:=null
   |? _kh || _kh:=null
   |? _grkh || _grkh:=null
   ?}
!};

_wer:=_Tab.mk_sel('Uprawnienia'@,'P',,'kkepgadpzcdwqsd',1,,,,'U');
_Tab.win_fld(_wer,,'US_DANE',,,20,,,'Uprawnieni'@);
_Tab.win_fld(_wer,,'USG_KOD',,,20,,,'Uprawniona grupa'@);
_Tab.win_fld(_wer,,'KOD',,,20,,,'Kod grupy uprawnionej'@);
_Tab.win_fld(_wer,,'OP',,,20,,,'Opis grupy uprawnionej'@);
_Tab.win_fld(_wer,,'GRKH_KOD',,,20,,,'Grupa kontrahenta'@);
_Tab.win_fld(_wer,,'KH_SKR',,,15,,,'Kontrahent'@);
_Tab.win_fld(_wer,,'KHO_NAZ',,,20,,,'Odbiorca'@);
_Tab.win_fld(_wer,,'MGR_KOD',,,8,,,'Grupa indeksu'@);
_Tab.win_fld(_wer,,'MGRP_KOD',,,8,,,'Podgrupa indeksu'@);
_Tab.win_fld(_wer,,'M_KTM',,,50,,,'Indeks'@);

_Tab.first;
_Tab.win_sel(_wer);
_Tab.select;

:: sprzątanie
{? _wer<>'' || _Tab.win_sel; _Tab.win_del(_wer) ?}


\kodnaz_tar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: zwraca kod i nazwe cennika na podstawie TAR_H.ref
::   WE: _a - TAR_H.ref
::   WY: kod i nazwa cennika
::  OLD: \kodnaz_tar/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';

{? _a
||
   TAR_H.cntx_psh();
   TAR_H.use(ref_name(_a));
   TAR_H.prefix();
   {? TAR_H.seek(_a) & TAR_H.TAP
   ||
      TAP.cntx_psh;
      TAP.use(ref_name(TAR_H.TAP));
      TAP.prefix();
      {? TAP.seek(TAR_H.TAP)
      ||
         TAR.cntx_psh();
         _wyn:=TAP.TAR().KOD+{? TAR.NAZ<>'' || '-'+TAR.NAZ || '' ?};
         TAR.cntx_pop()
      ?};
      TAP.cntx_pop
   ?};
   TAR_H.cntx_pop()
?};

_wyn


\po_chk_kalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: po polu z zaznaczeniem
::----------------------------------------------------------------------------------------------------------------------
_num:=#(cur_afld()+1);
_all:=fld()='N';
{! _i:=1..8 |! {? _all | _num<>_i || ($('KALK.CHK_'+form(_i)))():='N' ?} !};
KALK.W:={? _all || 0 || _num ?};
exec('po_wkalk','ceny');
1


\sprzmcen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: sprawdza poprawnosc parametrow  do zmiany ceny
::   WY: ''-jest OK 'WARZM'-wraca do pola wartosc
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_doda:=0;
_tab:=TAP.sel_aget();
{? _tab.size()
|| {? _tab.first()
   ||
      _lock:=0;
      TAP.cntx_psh();
      {!
      |?
         {? TAP.seek(_tab.REF,) || _lock:=exec('tap_lock','ceny') ?};
         _lock & _tab.next()
      !};
      TAP.cntx_pop();
      _loop:=_lock;
      {!
      |? _loop
      |!
         TAP.cntx_psh();
         {? TAP.clear(); TAP.seek(_tab.REF,TAP.name())
         || _dokl_c:=exec('dokl_ceny','ceny');
            {? HELP.TYPZM='P'
            || _war:={? HELP.ZCB='T'
                     || {? TAP.CENB>0 || TAP.CENB*(1+(HELP.WARZM*0.01))$_dokl_c || _doda:=1; 1 ?}
                     || {? TAP.CEN>0 || TAP.CEN*(1+(HELP.WARZM*0.01))$_dokl_c || _doda:=1; 1 ?}
                     ?};
               _warn:=1
            || _war:={? HELP.ZCB='T' || TAP.CENB+HELP.WARZM $_dokl_c || 1 ?};
               _warn:={? HELP.ZCN='T' || TAP.CEN+HELP.WARZM $_dokl_c || 1 ?}
            ?};
            {? _war<=0 | _warn<=0 || _wyn:='WARZM' ?}
         ?};
         TAP.cntx_pop();
         _loop:=_wyn='' & _tab.next()
      !};
      _loop:=_tab.first();
      {!
      |? _loop
      |!
         {? TAP.seek(_tab.REF,) || exec('tap_unlock','ceny') ?};
         _loop:=_tab.next()
      !}
   ?}
|? exec('tap_lock','ceny')
||
   _dokl_c:=exec('dokl_ceny','ceny');
   {? HELP.TYPZM='P'
   || _war:={? HELP.ZCB='T'
            || {? TAP.CENB>0 || TAP.CENB*(1+(HELP.WARZM*0.01))$_dokl_c || _doda:=1; 1 ?}
            || {? TAP.CEN>0 || TAP.CEN*(1+(HELP.WARZM*0.01))$_dokl_c || _doda:=1; 1 ?}
            ?};
      _warn:=1
   || _war:={? HELP.ZCB='T' || TAP.CENB+HELP.WARZM $_dokl_c || 1 ?};
      _warn:={? HELP.ZCN='T' || TAP.CEN+HELP.WARZM $_dokl_c || 1 ?}
   ?};
   {? _war<=0 | _warn<=0 || _wyn:='WARZM' ?};
   exec('tap_unlock','ceny')
?};
{? _wyn='WARZM'
|| FUN.info({? HELP.TYPZM='P'
            || 'Podany procent obniżenia ceny jest zbyt duży (ceny powinny być większe od zera).\n'@+
               'Niemożliwa zmiana cen o podany procent.'@
            || 'Podana wartość obniżenia ceny jest zbyt duża (ceny powinny być większe od zera).\n'@+
               'Niemożliwa zmiana cen o podaną wartość.'@
            ?})
|? HELP.ZCN<>'T' & HELP.ZCB<>'T'
|| FUN.info('Należy wybrać choć jeden z rodzajów cen (netto, brutto).'@);
   _wyn:='ZCN'
|? _doda
|| FUN.info('Na pozycjach z cenami zerowymi niemożliwa zmiana cen o podany procent.\n Pozycje te zostaną pominięte.'@)
?};
{? _wyn='' & HELP.WARZM=0 & HELP.ZPN<>'T' & HELP.ZPB<>'T'
 & FUN.ask({? HELP.TYPZM='P'
           || 'Nie podano  procentu zmiany ceny.\n Ceny nie zostaną zmienione.\n\nCzy chcesz podać procent?'@
           || 'Nie podano wartości zmiany ceny.\n Ceny nie zostaną zmienione.\n\nCzy chcesz podać wartość?'@ ?})
|| _wyn:='WARZM'
?};
_wyn


\cen_ilwar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: analiza cennikow ilosciowych, wartosciowych
::   WY: _a - tabela z cenami
::       _b - pozycja
::       [_c] - 0-priorytet cenniki, 1-priorytet dokument
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_c')<>type_of(0) || _c:=0 ?};

_wyn:=_a;

_wyn.cntx_psh;
_ind:=_wyn.ndx_tmp('',,'POZ',,,'TREE',,,'TAR',,);
_wyn.index(_ind); _wyn.prefix(_b,null);
{? _wyn.first()
||
   _first:=1;
   _rabbest:=0; _rabworst:=0;
   _cenbest:=0; _cenworst:=0;
   _warbest:=0; _warworst:=0;
   _thebest:=null; _theworst:=null();
   {!
   |?
      _wyn.OK:='N';
      _wyn.THEBEST:={? _c=0 || 'N' |? _wyn.THEBEST='' || 'N' || _wyn.THEBEST ?};
      _wyn.THEWORST:={? _c=0 || 'N' |? _wyn.THEWORST='' || 'N' || _wyn.THEWORST ?};
      _rab:={? exec('czyrabp','ceny',TAZ.RAB_TYP) || _wyn.PRC || {? TAZ.CB='N' || _wyn.RABNET || _wyn.RABBRT ?} ?};
      _cen:={? TAZ.CB='N' || _wyn.CEN || _wyn. CENB ?};
      {? _wyn.IL_POZ=0
      || {? _first
         || _first:=0;
            _cenbest:=_cen; _rabbest:=_rab; _thebest:=_wyn.ref;
            _cenworst:=_cen; _rabworst:=_rab; _theworst:=_wyn.ref
         |? _cen<_cenbest || _cenbest:=_cen; _rabbest:=_rab; _thebest:=_wyn.ref
         |? _cen=_cenbest & _rab>_rabbest || _rabbest:=_rab; _thebest:=_wyn.ref
         |? _cen>_cenworst || _cenworst:=_cen; _rabworst:=_rab; _theworst:=_wyn.ref()
         |? _cen=_cenworst & _rab<_rabworst || _cenworst:=_cen; _rabworst:=_rab; _theworst:=_wyn.ref()
         ?};
         {? _wyn.IL=0 & _wyn.IL_DO=0 & _wyn.WAR=0 & _wyn.WAR_DO=0 || _wyn.OK:='T' ?}
      ||
         {? _wyn.IL=0 | _wyn.IL<=_wyn.IL_POZ & (_wyn.IL_DO=0 | _wyn.IL_POZ<_wyn.IL_DO)
         ||
            _war:=exec('obl_war','ceny',_rab,_cen,_wyn.IL_POZ,2);
            {? _wyn.WAR=0 | _wyn.WAR<=_war & (_wyn.WAR_DO=0 | _war<_wyn.WAR_DO)
            ||
               _war:=exec('obl_war','ceny',_rab,_cen,TAZ.IL,2);
               {? _cen & _first
               || _first:=0;
                  _warbest:=_war; _thebest:=_wyn.ref();
                  _warworst:=_war; _theworst:=_wyn.ref()
               |? _cen & _war<_warbest || _warbest:=_war; _thebest:=_wyn.ref
               |? _cen & _war>_warworst  || _warworst:=_war; _theworst:=_wyn.ref
               ?};
               _wyn.OK:='T'
            ?}
         ?}
      ?};
      _wyn.put & _wyn.next
   !};
   {? _c=0 & _thebest & _wyn.seek(_thebest) || _wyn.THEBEST:='T'; _wyn.put ?};
   {? _c=0 & _theworst & _wyn.seek(_theworst) || _wyn.THEWORST:='T'; _wyn.put ?}
?};
_wyn.ndx_drop(_ind);
_wyn.cntx_pop


\censlo_kolrabb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: kolory w slowniku cen dla pol
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('Color')>0 || Color.fnd_kol({? cur_tab(1,1).RAB_B='T' || 'TAP#02#05' || '' ?}) || '' ?}


\censlo_kolcenod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: kolory w slowniku cen dla pol
::----------------------------------------------------------------------------------------------------------------------
cur_tab(1,1).CEN_ZAKR='T'


\tap_popp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formuła przed dla akcji grupowej - popraw dla pozycji cennika
::   WY: tworzona jest zmienna globalna __czy_ed - czy była edycja
::----------------------------------------------------------------------------------------------------------------------
__Sel:=TAP.sel_aget();
ZMIENNE.RODZAJ:='1';

_lock:=0;
_oneR:=__Sel.size()=0;
{? __Sel.first()
||
   TAP.cntx_psh();
   {!
   |?
      {? TAP.seek(__Sel.REF,) || _lock:=exec('tap_lock','ceny') ?};
      _lock & __Sel.next()
   !};
   TAP.cntx_pop();
   {? ~_lock
   ||
      TAP.cntx_psh();
      _loop:=__Sel.first();
      {!
      |? _loop
      |!
         {? TAP.seek(__Sel.REF,) || _lock:=exec('tap_unlock','ceny') ?};
         _loop:=__Sel.next()
      !};
      TAP.cntx_pop()
   ?}
?};

{? _lock=0 || return(0) ?};


BEER.MW:='Z';
DISP.blank(1);
DISP.OD:=DISP.DO:=date(0,0,0);
DISP.CEN:=DISP.CENB:=0;
DISP.WALU:=null;
DISP.PRC:=0;
DISP.TAR:=null;
DISP.SV:=null;
DISP.win_edit('TAP'+TAZ.SD);
DISP.DOZ:=DISP.TAPTODZ:=DISP.TAPTDOZ:='N';
DISP.TARBZ:='N';
DISP.TAPILZ:=DISP.TAPILDOZ:=DISP.TAPWARZ:=DISP.TAPWADOZ:=DISP.TAPPLZ:='N';
DISP.TAPFOCZ:=DISP.CENZ:=DISP.CENBZ:='N';
DISP.TAPFORZ:=DISP.RABZ:='N';
DISP.PROMOZ:='N';
TAP.blank(1);
TAP.TARB:='';
TAP.CEN_ZAKR:='';
TAP.CEN_OD:=TAP.CEN:=TAP.CENB_OD:=TAP.CENB:=0;
TAP.PRC:=TAP.RABNET:=TAP.RABBRT:=0;
TAP.PROMO:=null;
exec('tap_count','ceny',TAP.sel_aget,,,0);
_upr:=TAZ.UPR;
TAZ.UPR:=1;
{? ~_oneR || exec('fld_prec_tap','win_ust',0) ?};
__czy_ed:=DISP.edit("
   {? TAZ.SD='G'
   ||
      {? DISP.DO<>date(0,0,0) & DISP.DO<DISP.OD
      || FUN.info('Wartość pola \"Do daty\" musi być większa lub równa\n'
                  'wartości pola \"Od daty\" lub niewypełniona.'@);
         'OD'
      |? TAP.GRA_IL<0
      || FUN.info('Wartość nie może być mniejsza od zera'@);
         'GRA_IL'
      |? TAP.GRA_CEN<0
      || FUN.info('Wartość nie może być mniejsza od zera'@);
         'GRA_WAR'
      || ''
      ?}
   ||
      {? TAP.CEN_OD<0
      || FUN.info('Wartość nie może być mniejsza od zera.'@);
         'CEN_OD'
      |? TAP.CEN<0
      || FUN.info('Wartość nie może być mniejsza od zera.'@);
         'CEN'
      |? TAP.CEN & TAP.CEN<TAP.CEN_OD
      || FUN.info('Źle zdefiniowany zakres ceny netto.'@);
         'CEN'
      |? TAP.CENB_OD<0
      || FUN.info('Wartość nie może być mniejsza od zera.'@);
         'CENB_OD'
      |? TAP.CENB<0
      || 'CENB'
      |? TAP.CENB & TAP.CENB<TAP.CENB_OD
      || FUN.info('Źle zdefiniowany zakres ceny brutto.'@);
         'CEN'
      |? TAP.PRC<0 | TAP.PRC>99.99
      || FUN.info('Rabat musi zawierać sie w przedziale <0;100).'@);
         'PRC'
      |? TAP.CEN & TAP.CEN<TAP.RABNET
      || FUN.info('Rabat netto musi być mniejszy od ceny netto.'@);
         'RABNET'
      |? TAP.CENB & TAP.CENB<TAP.RABBRT
      || FUN.info('Rabat brutto musi być mniejszy od ceny brutto.'@);
         'RABBRT'
      |? DISP.DO<>date(0,0,0) & DISP.DO<DISP.OD
      || FUN.info('Data musi być większa lub równa wartości pola \"Od daty\"\nlub niewypełniona.'@);
         'DO'
      |? TAP.T_OD<>time(0,0,0) & TAP.T_DO<>time(0,0,0) & DISP.OD=DISP.DO & TAP.T_DO<TAP.T_OD
      || FUN.info('\"Godzina do\" musi być późniejsza od \"Godzina od\".'@);
         'T_DO'
      || ''
      ?}
   ?}
");
sel_nchk();
{? ~_oneR || exec('fld_prec_tap','win_ust',1) ?};
_wyn:=__czy_ed;
TAZ.UPR:=_upr;
{? _wyn
|| DISP.TAPTOD:=TAP.T_OD;
   DISP.TAPTDO:=TAP.T_DO;
   DISP.TAPPL:=TAP.PL;
   DISP.TAPFOC:=TAP.FO_CEN;
   DISP.TAPZAKC:=TAP.CEN_ZAKR;
   DISP.TAPCOD:=TAP.CEN_OD;
   DISP.CEN:=TAP.CEN;
   DISP.TAPCBOD:=TAP.CENB_OD;
   DISP.CENB:=TAP.CENB;
   DISP.TAPFOR:=TAP.FO_RAB;
   DISP.PRC:=TAP.PRC;
   DISP.RABNET:=TAP.RABNET;
   DISP.RABBRT:=TAP.RABBRT;
   DISP.PROMO:=TAP.PROMO;
   DISP.GRATIS:=TAP.GRATIS;
   DISP.GRA_IL:=TAP.GRA_IL;
   DISP.GRA_CEN:=TAP.GRA_CEN;
   DISP.SV:=TAP.SV;
   exec('ini_kom','#message','Zbiorcza poprawa pozycji'@)
||
   TAP.cntx_psh();
   _loop:=__Sel.first();
   {!
   |? _loop
   |!
      {? TAP.seek(__Sel.REF,) || _lock:=exec('tap_unlock','ceny') ?};
      _loop:=__Sel.next()
   !};
   TAP.cntx_pop();

   {? ceny.REF<>''
   || TAR.cntx_psh();
      TAR.prefix();
      {? TAR.seek(ceny.REF)
      || DISP.TAR:=TAR.ref()
      || ceny.del()
      ?};
      TAR.cntx_pop()
   ?};

   VAR_DEL.delete('__czy_ed','__Sel')
?};
_wyn


\tap_popa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formuła po dla akcji grupowej - popraw dla pozycji cennika
::----------------------------------------------------------------------------------------------------------------------
TAP.cntx_psh();
_loop:=__Sel.first();
{!
|? _loop
|!
   {? TAP.seek(__Sel.REF,) || _lock:=exec('tap_unlock','ceny') ?};
   _loop:=__Sel.next()
!};
TAP.cntx_pop();

{? ceny.REF<>''
|| TAR.cntx_psh();
   TAR.prefix();
   {? TAR.seek(ceny.REF)
   || DISP.TAR:=TAR.ref()
   || ceny.del()
   ?};
   TAR.cntx_pop()
?};

exec('end_kom','#message');
VAR_DEL.delete('__czy_ed','__Sel');
1


\cenywgjm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: usuwa ceny niezgodne z podaną jednostką miary - kodem
::   WE: _a - kod jednostki miary
::       _b - 0-bez struktury drzewiastej, 1-z struktura drzewiasta
::----------------------------------------------------------------------------------------------------------------------
XCEN.clear();
{? XCEN.first()
|| {!
   |? {? XCEN.MIARA=_a
      || XCEN.next()
      || _ref:=XCEN.ref();
         {? _b
         || XCEN.cntx_psh();
            XCEN.prefix(XCEN.ref());
            {? XCEN.first() || {! |? XCEN.del !} ?};
            XCEN.cntx_pop()
         ?};
         {? XCEN.seek(_ref) || XCEN.del() ?}
      ?}
   !}
?};
1


\war_hid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: wykropkowanie przed wyświetleniem zawartosci wskazanych pól - uprawnienia magazynowe
::   WE: _a - uchwyt do tabeli lub zmiennej
::       _b - akronim pola
::       [,_[_]] - akronim pola
::  OLD: \war_hid/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cm','ceny')=0
|| {! _ii.._-1
   |! _a.fld_fml(_[_ii+1],'BEFORE_DISPLAY',"0");
      _a.fld_attr(_[_ii+1],,2)
   !}
?}


\lst_dost
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: wypełnia tabelę z listą dostaw
::   WE: _a - M.ref()
::       _b - KH.ref()
::       _c - SLO.ref - waluta
::  OLD: \lst_dost/zd.fml
::----------------------------------------------------------------------------------------------------------------------
X_Xd.prefix;
{? X_Xd.first || {! |? {? X_Xd.TREE || X_Xd.del || X_Xd.next ?} !} ?};
{? X_Xd.first || {! |? X_Xd.del !} ?};

_czy_wal:=_c<>INFO.NAROD;

ODDZ.cntx_psh(); OKR.cntx_psh(); ND.cntx_psh(); DK.cntx_psh(); X_Xd.cntx_psh;
X_Xd.index(X_Xd.ndx_tmp(,,'TREE',,,'KH',,));
ODDZ.prefix();
{? ODDZ.first()
|| {!
   |? OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      || {!
         |? _maska:=ODDZ.KOD+($OKR.ROK+2);
            {? _maska<>DK.name()+3
            || DK.use((DK.name()-3)+_maska);
               ND.use((ND.name()-3)+_maska)
            ?};
            {? _b=null
            || DK.index('DOKMAT'); DK.prefix('T',_a)
            || DK.index('KH'); DK.prefix(_b,'T',_a,'T')
            ?};
            {? DK.first()
            || {!
               |? {? DK.PLUS='T' & DK.WAL=_c
                  || DK.N().TYP();
                     {? TYPYDOK.DK='N'
                     ||
                        _tree:=
                           {? X_Xd.find_key(0,$ND.KH)
                           || X_Xd.ref
                           || X_Xd.blank;
                              X_Xd.KH:=$ND.KH;
                              X_Xd.KH_NAZ:=ND.KH().NAZ;
                              X_Xd.add;
                              X_Xd.ref
                           ?};
                        X_Xd.TREE:=_tree;
                        X_Xd.KH_NAZ:='';
                        X_Xd.KH:=$ND.KH;
                        X_Xd.SYMBOL:=ND.SYM;
                        X_Xd.POZ:=DK.P;
                        X_Xd.DATA:=ND.D;
                        X_Xd.CENA:={? _czy_wal || DK.CWAL || DK.C ?};
                        X_Xd.ILOSC:=DK.IL;
                        X_Xd.MG_REF:=$ND.MAG;
                        X_Xd.C_PO_KOR:={? _czy_wal || DK.CWAL || DK.C ?};
                        X_Xd.RDK:=DK.RDK;
                        X_Xd.NDK:=DK.NDK;
                        X_Xd.JM:=DK.M().J().KOD;
                        X_Xd.add()
                     ?}
                  ?};
                  DK.next()
               !}
            ?};
            OKR.next()
         !}
      ?};
      ODDZ.next()
   !}
?};
DISP.KHNAZ:={? _b || 'Dostawca: '@+DISP.KH().SKR || 'Wszyscy dostawcy'@ ?};
ODDZ.cntx_pop(); OKR.cntx_pop(); ND.cntx_pop(); DK.cntx_pop(); X_Xd.cntx_pop


\dkk_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: kolorowanie DK - korekty
::  OLD: \dkk_kol/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
{? 5+cur_tab.name='nagdo' | 5+cur_tab.name='dokma'
||
   {? 1+DK.N().MAG().TYP='D' & DK.C<>DISP.C
   || 'DKK#01#01'
   || 'DKK#01#02'
   ?}
||
   {? var_pres('__tab_ed')>100
   ||
      {? __tab_ed.CP<>__tab_ed.C
      || 'DKK#01#01'
      || 'DKK#01#02'
      ?}
   |? var_pres('X_Xd')>100
   ||
      {? X_Xd.CENA<>X_Xd.C_PO_KOR
      || 'DKK#01#01'
      || 'DKK#01#02'
      ?}
   ?}
?}


\cenp_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: kolorowanie pozycji cenników
::----------------------------------------------------------------------------------------------------------------------
{? TAP.name+2='hs'
|| 'TAP#01#03'
|? TAP.A='T'
|| 'TAP#01#01'
|? TAP.A='N'
|| 'TAP#01#02'
?}


\censlo_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: kolory w słowniku cen
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1,1).NAZ='KALKULACJA' || 'TAP#02#04'
|? cur_tab(1,1).THEBEST='T' || 'TAP#02#03'
|? cur_tab(1,1).OK='N' || 'TAP#02#02'
|? cur_tab(1,1).OK='T' || 'TAP#02#01'
|| ''
?}


\gratis_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: gratisy - kolory
::  OLD: \gratis_kol/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_tab(1).OK || 'TAP#04#01'
|? ~cur_tab(1).OK || 'TAP#04#02'
|| ''
?}


\poznal_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: kolory pozycji naliczenia
::   WY: kolor
::  OLD: \poznal_kol/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);

{? _Tab.TCEN=0 || 'TAP#05#03'
|? _Tab.CEN & _Tab.WAR>_Tab.TWAR || 'TAP#05#01'
|? _Tab.CEN & _Tab.WAR<_Tab.TWAR || 'TAP#05#02'
|| ''
?}


\sumanal_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: kolory sumy naliczenia
::   WY: kolor
::  OLD: \sumanal_kol/ceny1.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);

{? _Tab.LP=2
|| _Tab.cntx_psh();
   _kwota:={? _Tab.first() || _Tab.KWOTA || 0 ?};
   _Tab.cntx_pop();
   {? _kwota>_Tab.KWOTA || 'TAP#06#01'
   |? _kwota<_Tab.KWOTA || 'TAP#06#02'
   || ''
   ?}
|| ''
?}


\st_licz_psh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: stale potrzebne do obliczenia wartosci na pozycjach dokumentu - stale wynikajace z naglowka - cntx_psh
::----------------------------------------------------------------------------------------------------------------------
__stlicz:=obj_new('RAB','RAB_TYP','CB','CZY_WAL','WAL','KRS','SPOBLRAB','SPP');
__stlicz.RAB:=TAZ.RAB;
__stlicz.RAB_TYP:=TAZ.RAB_TYP;
__stlicz.CB:=TAZ.CB;
__stlicz.CZY_WAL:=TAZ.CZY_WAL;
__stlicz.WAL:=TAZ.WAL;
__stlicz.KRS:=TAZ.KRS;
__stlicz.SPOBLRAB:=TAZ.SPOBLRAB;
__stlicz.SPP:=TAZ.SPP


\st_licz_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: stale potrzebne do obliczenia wartosci na pozycjach dokumentu - stale wynikajace z naglowka - cntx_pop
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__stlicz')=117
|| TAZ.RAB:=__stlicz.RAB;
   TAZ.RAB_TYP:=__stlicz.RAB_TYP;
   TAZ.CB:=__stlicz.CB;
   TAZ.CZY_WAL:=__stlicz.CZY_WAL;
   TAZ.WAL:=__stlicz.WAL;
   TAZ.KRS:=__stlicz.KRS;
   TAZ.SPOBLRAB:=__stlicz.SPOBLRAB;
   TAZ.SPP:=__stlicz.SPP;
   VAR_DEL.delete('__stlicz')
?}


\war_hids
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: wykropkowanie przed wyswietleniem zawartosci wskazanych pol - uprawnienia sprzedazy
::   WE: _a - uchwyt do tabeli lub zmiennej
::       _b - akronim pola
::       [,_[_]] - akronim pola
::  OLD: \war_hids/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cs','ceny')=0
||
   {! _ii.._-1
   |!
      _a.fld_fml(_[_ii+1],'BEFORE_DISPLAY',"0");
      _a.fld_attr(_[_ii+1],,2)
   !}
?}


\war_hidz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: wykropkowanie przed wyswietleniem zawartosci wskazanych pol - uprawnienia zakupu
::   WE: _a - uchwyt do tabeli lub zmiennej
::       _b - akronim pola
::       [,_[_]] - akronim pola
::  OLD: \war_hidz/firma.fml
::----------------------------------------------------------------------------------------------------------------------

{? exec('upr_cz','ceny')=0
||
   {! _ii.._-1
   |!
      _a.fld_fml(_[_ii+1],'BEFORE_DISPLAY',"0");
      _a.fld_attr(_[_ii+1],,2)
   !}
?}


\ceny_stan_mat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: wyswietla ceny dla materialu
::----------------------------------------------------------------------------------------------------------------------
{? KSTE.CT='T'
||
   exec('taz_sd_set','ceny');
   exec('tap_okno','ceny');
   DISP.TAR:=null;
   TAP.win_sel('WER_KH');
   TAP.clear();
   {? TAP.f_set('OD','join TAR using ("TAP".TAR,TAR.reference)','("TAP".M=:_a or "TAP".MGR=:_b) and TAR.SD=\':_c\''
         ,M.ref(),M.MGR,{? BEER.SZ='S' || 'S' || 'D' ?})
   || {? ~TAP.f_size() || KALK.CEN_KH:=KALK.CEN_GRKH:='' ?};
      TAZ.ACTIONS:='DPUOIZAN(PWCH)WER:DON(PWCH)R';
      TAP.select(,1,10,TAZ.ACTIONS);
      TAZ.ACTIONS:=''
   || FUN.info('Założenie filtra programowego nie powiodło się.'@)
   ?};
   TAP.f_clear(1)
||
   FUN.info('Parametr obsługi cenników nie jest włączony.'@)
?};
''


\tap_fld_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Po redakcji pól tabeli TAP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_acr:=cur_afld();
{? _acr='T_OD' | _acr='T_DO'
|| {? fld()>time(23,59,59)
   || FUN.info('Maksymalna dopuszczalna wartość 23:59:59'@);
      fld(time(23,59,59));
      0
   || 1
   ?}
|| 1
?}


\kh_cen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: wyswietla cenniki dla kontrahentów
::   WE: _a - [S]przedazy,[D]ostawcow
::----------------------------------------------------------------------------------------------------------------------
_kind:=_a;
BEER.ESC:=1;

{? _kind='S'
||
   __PARSES.setEnv('LSP_CEN')
||
   __PARSES.setEnv('LZK_DST')
?};
{? ST.ODDZ='' || FUN.info('Funkcja niedostępna. Brak uprawnień do oddziału.'@); return('') ?};

KSTE.cntx_psh();
:: Podczytanie KSTE
_var:=obj_new('KSTE_SET');
_var.KSTE_SET:=1;
params_set('var',_var);
:: Podczytanie parametrów pracy
exec('kste_set','oddzialy',ST.ODDZ);
:: Ustawia zmienne informujące o dostępie do wartości sprzedaży, zakupu, magazynowych
KALK.KH:=KH.ref();
KALK.GRKH:=null;
_bpmndisp:=BPMN.DISPLAY;
BPMN.DISPLAY:=3;

{? _kind='S'
||
   exec('lsp_cen','lsp',1)
||
   exec('lzk_dst','lzk',,1)
?};

BPMN.DISPLAY:=_bpmndisp;
KALK.KH:=null;
KALK.GRKH:=null;
KALK.OD:=date(0,0,0);
KALK.DO:=date(0,0,0);
KSTE.cntx_pop();
BEER.ESC:=0;
''


\dokl_ceny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: zwraca dokładność ceny
::   WY: dokładność
::----------------------------------------------------------------------------------------------------------------------
_sd:={? TAP.TAR().SD='' || TAZ.SD || TAP.TAR().SD ?};
_res:={? (';SG'*_sd)>1
      || {? TAP.M || TAP.M().DOKL_S
         |? TAP.MGR || TAP.MGR().DOKL_S
         || ST.DOKL_S
         ?}
      || {? TAP.M || TAP.M().DOKL_Z
         |? TAP.MGR || TAP.MGR().DOKL_Z
         || ST.DOKL_Z
         ?}
      ?};
_res


\disp_dokl_c
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec dokładność ceny magazynowej dla kalkulacji - wyświetlanie
::----------------------------------------------------------------------------------------------------------------------
'out_prec='+$KALK.M().DOKL_C


\edit_dokl_c
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec dokładność ceny magazynowej dla kalkulacji - edycja
::----------------------------------------------------------------------------------------------------------------------
'in_prec='+$KALK.M().DOKL_C


\disp_dokl_s
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec dokładność ceny sprzedaży dla kalkulacji - wyświetlanie
::----------------------------------------------------------------------------------------------------------------------
'out_prec='+$KALK.M().DOKL_S


\edit_dokl_s
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec dokładność ceny sprzedaży dla kalkulacji - edycja
::----------------------------------------------------------------------------------------------------------------------
msg();'in_prec='+$KALK.M().DOKL_S


\disp_kalk_x
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec KALK.X - wyświetlanie
::----------------------------------------------------------------------------------------------------------------------
'out_prec='+{? KALK.WG=1 || $KALK.M().DOKL_S || '2' ?}


\edit_kalk_x
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec KALK.X - edycja
::----------------------------------------------------------------------------------------------------------------------
'in_prec='+{? KALK.WG=1 || $KALK.M().DOKL_S || '2' ?}


\disp_kalk_cen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec KALK.CEN - wyświetlanie
::----------------------------------------------------------------------------------------------------------------------
'out_prec='+$KALK.M().DOKL_C


\edit_kalk_cen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec KALK.CEN - edycja
::----------------------------------------------------------------------------------------------------------------------
'in_prec='+$KALK.M().DOKL_C


\disp_kalk_ce2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec KALK.CE2 - wyświetlanie
::----------------------------------------------------------------------------------------------------------------------
'out_prec='+$KALK.M().DOKL_S


\edit_kalk_ce2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: wzorzec KALK.CE2 - edycja
::----------------------------------------------------------------------------------------------------------------------
'in_prec='+$KALK.M().DOKL_S


\precyzja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: zwraca precyzję przybliżenia
::   WE: _a-uchwyt do tabeli
::   WY: precyzja
::----------------------------------------------------------------------------------------------------------------------
_wyn:=2;

{? _a=FAP
|| FAKS.cntx_psh();
   _wyn:={? FAP.FAKS().SZ='S' || {? FAP.M<>null() || FAP.M().DOKL_S || ST.DOKL_S ?}
         |? FAP.FAKS().SZ='Z' || {? FAP.M<>null() || FAP.M().DOKL_Z || ST.DOKL_Z ?}
         ?};
   FAKS.cntx_pop()
|? _a=DK
|| ND.cntx_psh;
   _wyn:={? DK.PLUS='T'
         || DK.M().DOKL_Z
         || DK.M().DOKL_S
         ?};
   ND.cntx_pop
|? _a=ZK_P
|| ZK_P.cntx_psh;
   _wyn:=ZK_P.M().DOKL_S;
   ZK_P.cntx_pop
|? _a=OFP
|| OFP.cntx_psh;
   _wyn:=OFP.M().DOKL_S;
   OFP.cntx_pop
|? _a=REZ
|| _wyn:=REZ.M().DOKL_S
?};
_wyn


\tar_cenm_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: parametr exec('tar_cenm','ceny')
::----------------------------------------------------------------------------------------------------------------------
_args:=obj_new('TAR','M','WAL','JM','D','CNR','BRT','PL','RAB_TYP');
:: cennik TAR.ref()
_args.TAR:=null();
:: material M.ref()
_args.M:=null();
:: waluta SLO.ref()
_args.WAL:=SLO.ref();
:: jednostka miary JM.ref()
_args.JM:=null();
:: data
_args.D:=date(0,0,0);
:: =0-MDOSCT.C1, =2-MDOST.C2 lub MDOST.C1 jesli MDOST.C2=0
_args.CNR:=0;
:: =N-netto, =T-brutto
_args.BRT:='N';
:: forma platnosci
_args.PL:=null();
:: typ rabatu
_args.RAB_TYP:=exec('get','#params',800812,2);
:: tablica
_args


\tar_cenm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: podaje cene z cennika dla uslugi (_M) i daty
::   WE: wynik exec('tar_cenm','ceny')
::   WY: zwraca cene z cennika lub 0 gdy nie znajdzie - nie moze byc zerowych cen
::----------------------------------------------------------------------------------------------------------------------
_paramsa:=_a;
{? _paramsa.D=date(0,0,0) || _paramsa.D:=date() ?};
_data:=_paramsa.D;
_czas:=time(0,0,0);
_ceny_chk_upr:={? var_pres('__CENY_CHK_UPR')=type_of(0) || __CENY_CHK_UPR || 1 ?};

_wyn:=obj_new(3);
_wyn[1]:=_wyn[2]:=0;
_wyn[3]:='';

_p800830:=exec('get','#params',800830,2)='T';
TAZ.SD:='S';

TAP.cntx_psh();
TAP.index('TM');
TAP.prefix(_paramsa.TAR,_paramsa.M,_paramsa.WAL,_paramsa.JM);
{? TAP.first()
||
   {!
   |?
      {? _p800830
      ||
         _grkh:={? TAP.TAR().KH || TAR.KH().GRKH || TAR.GRKH ?};
         _mgr:={? TAP.M || TAP.M().MGR || TAP.MGR ?};
         _mgrp:={? TAP.M || TAP.M().MGRP || TAP.MGRP ?}
      ?};
      {? (TAP.OD<_data | TAP.OD=_data & TAP.T_OD<=_czas)
         & (TAP.DO=date(0,0,0) | (_data<TAP.DO | _data=TAP.DO & (TAP.T_DO>=_czas | TAP.T_DO=time(0,0,0))))
         & (TAP.PL=null() | TAP.PL=_paramsa.PL)
         & (_p800830=0 | ~_ceny_chk_upr | exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr))
         & TAP.IL=0 & TAP.IL_DO=0 & TAP.WAR=0 & TAP.WAR_DO=0
      ||
         _wyn[1]:={? _paramsa.BRT='N' || TAP.CEN || TAP.CENB ?};
         _wyn[2]:={? exec('czyrabp','ceny',_paramsa.RAB_TYP) || TAP.PRC || {? _paramsa.BRT='N' || TAP.RABNET || TAP.RABBRT ?} ?};
         _wyn[3]:=$TAP.ref()
      ?};
      TAP.next() & _wyn[1]=0
   !}
?};
:: jezeli nie znajdzie to pobiera cene przypisana bezposrednio do uslugi
{? _wyn[1]=0 & _paramsa.WAL=INFO.NAROD
||
   M.cntx_psh();
   M.clear();
   {? M.seek(_paramsa.M)
   ||
      MDOST.cntx_psh;
      MDOST.index('M');
      MDOST.prefix('S',M.ref,null,INFO.NAROD);
      {? MDOST.first
      ||
         {? _paramsa.CNR=2
         || _wyn[1]:={? MDOST.C2<>0 || MDOST.C2 || MDOST.C1 ?}
         || _wyn[1]:=MDOST.C1
         ?}
      ?};
      MDOST.cntx_pop
   ?};
   M.cntx_pop()
?};
TAP.cntx_pop();
_wyn


\tar_cenm_s
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: wypelnia FAP.PROMO, FAP.TAR_TMS, FAP.TAR_H
::   WE: _a - $TAP.ref()
::----------------------------------------------------------------------------------------------------------------------
_tap:=_a;
{? _tap=''
||
   FAP.PROMO:=null;
   FAP.TAR_TMS:=0;
   FAP.TAR_H:=null
||
   TAP.cntx_psh();
   TAP.use(8+_tap);
   TAP.prefix();
   {? TAP.seek(_tap)
   ||
      FAP.PROMO:=TAP.PROMO;
      FAP.TAR_TMS:=FIRMA.tm_stamp;
      FAP.TAR_H:=
         exec('tar_h','ceny_dok',
            'FAP',
            TAP.ref(),null(),
            TAP.WAL,FAP.J2,
            TAP.IL,TAP.WAR,
            TAP.PL,
            {? FAP.FAKS().T().FIS='T' || TAP.CEN_OD || TAP.CENB_OD ?},
            {? FAP.FAKS().WAL<>FAKS.NWAL || FAP.CPR || FAP.CWAL ?},
            FAP.RAB+FAP.RABK,
            TAP.RAB_B,
            TAP.PROMO,
            TAP.OD,TAP.DO,TAP.T_OD,TAP.T_DO,TAP.IL_DO,TAP.WAR_DO,0,0)
   ?};
   TAP.cntx_pop()
?}


\lsp_cen_param
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [19.42]
:: OPIS: Zmiana parametrów pracy obszaru LSP_CEN
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
_okr_ref:=ST.OKR_REF;
_st_sp:=ST.STS_KOD;
_st_fis:=ST.FIS;
__PARSES.editDom('LSP');
{? _oddz<>ST.ODDZ |
   _okr_ref<>ST.OKR_REF |
   _st_sp<>ST.STS_KOD |
   _st_fis<>ST.FIS
||
   sel_exit();
   exec('sel_tary','ceny','S',0)
?}


\lsp_cen_grt_param
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [19.42]
:: OPIS: Zmiana parametrów pracy obszaru LSP_CEN - gratisy
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
_okr_ref:=ST.OKR_REF;
_st_sp:=ST.STS_KOD;
_st_fis:=ST.FIS;
__PARSES.editDom('LSP');
{? _oddz<>ST.ODDZ |
   _okr_ref<>ST.OKR_REF |
   _st_sp<>ST.STS_KOD |
   _st_fis<>ST.FIS
||
   sel_exit();
   exec('sel_tary','ceny','G',0)
?}


\lzk_dst_param
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Zmiana parametrów pracy obszaru LZK_DST
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
_okr_ref:=ST.OKR_REF;
_st_z:=ST.STZ;
__PARSES.editDom('LZK');
{? _oddz<>ST.ODDZ |
   _okr_ref<>ST.OKR_REF |
   _st_z<>ST.STZ
||
   sel_exit();
   exec('sel_tary','ceny','D',0)
?}


\dragcopy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.42]
:: OPIS: Kopia ceny/cen do wskazanego cennika
::   WE: [_a] - tryb 0(domyślnie)-d&d, 1-akcja
::       [_b] - 0(domyślnie) 1-kartoteka materiałowa
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_mate:={? var_pres('_b')=type_of(0) || _b || 0 ?};

ceny.cntx_psh;
TAP.cntx_psh;
TAP.prefix;
_ref:=null();
_dest:=null();

{? ~_tryb
|| _dest:=dnd_info('dest_record');
   _mask:=dnd_info('table_name');
   _Sel:=dnd_info('dropped_records')
|| ceny.cntx_psh();
   _win_acr:=ceny.mk_sel(,'P',,'weiajhstdkalsel',,,,1);
   ceny.win_fld(_win_acr,,'OPIS',,,21,,,{? TAZ.SD='G' || 'Rodzaje gratisów'@ || 'Rodzaje cenników'@ ?});
   ceny.win_fld(_win_acr,,'KH_OPIS',,,-13,,,'Kontrahent/Grupa'@);
   ceny.win_act(_win_acr,0,'Formuła','&Wybierz'@@,,'Wybór cennika do kopiowania'@,,"sel_exit",1,,,,'W');
   ceny.win_act(_win_acr,0,'Rekord'
         ,,,,"ceny.actions_grayed(ceny.win_sel('?'),{? ~ceny.TREE || 'W' || '' ?});''");
   ceny.tr_fml(_win_acr,,"1");
   ceny.win_sel(_win_acr);
   {? ceny.select() || _dest:=ceny.ref() ?};
   ceny.cntx_pop();
   _mask:=TAP.name();
   {? TAP.sel_size()
   || _Sel:=TAP.sel_aget();
      TAP.sel_adel()
   || _Sel:=tab_tmp(1,'REF','INTEGER','');
      _Sel.blank();
      _Sel.REF:=#TAP.ref();
      _Sel.add(1)
   ?}
?};

{? _dest=null
|| FUN.info({? TAZ.SD='G' || 'Należy wskazać nagłówek gratisu.'@ || 'Należy wskazać nagłówek cennika.'@ ?})
|? ceny.seek(_dest)
|| {? ceny.TREE=0
   || FUN.info({? TAZ.SD='G' || 'Należy wskazać nagłówek gratisu.'@ || 'Należy wskazać nagłówek cennika.'@ ?})
   || _txt:=
         {? TAZ.SD='G'
         || 'Skopiować pozycje do gratisu %1?'@[ceny.OPIS+{? ceny.KH_OPIS<>'' || ' - '+ceny.KH_OPIS || '' ?}]
         || 'Skopiować pozycje do cennika %1?'@[ceny.OPIS+{? ceny.KH_OPIS<>'' || ' - '+ceny.KH_OPIS || '' ?}]
         ?};
      {? FUN.ask(_txt)
      || {? ceny.seek(_dest)
         || _ref:=exec('FindAndGet','#table','TAR',ceny.REF,,,null())
         ?}
      ?}
   ?}
?};
{? ~_mate || exec('ini_kom','#message','Kopiowanie pozycji'@) ?};
{? _ref
||
   _loop:=_Sel.first;
   {!
   |? _loop
   |!
      {? TAP.seek(_Sel.REF, _mask)
      ||
         {? TAZ.SD='G' |
            (TAP.TAR();
            TAP.M();
            _grkh:={? TAR.KH || TAR.KH().GRKH || TAR.GRKH ?};
            _mgr:={? TAP.M || M.MGR || TAP.MGR ?};
            _mgrp:={? TAP.M || M.MGRP || TAP.MGRP ?};
            exec('upr_chk','ceny',TAR.KH_ODB,TAR.KH,_grkh,TAP.M,_mgrp,_mgr))
         ||
            TAP.SES_ID:='';
            TAP.TAR:=_ref;
            TAR.cntx_psh();
            {? TAP.TAR().KH=null() & TAR.GRKH=null() || TAP.TARB:='' ?};
            TAR.cntx_pop();
            TAP.add
         || {? ~_mate
            || exec('add_kom','#message'
               ,'Brak uprawnień do %1.'@[{? TAP.M
                                         || M.KTM
                                         || TAP.MGR().KOD+ {? TAP.MGRP || ' - '+TAP.MGRP().KOD || '' ?}
                                         ?}],7)
            ?}
         ?}
      ?};
      _loop:=_Sel.next
   !};
   _loop:=_Sel.first()
?};
TAP.cntx_pop;
ceny.cntx_pop;
{? _ref || win_disp(); ceny.seek(_dest) ?};
{? ~_mate || exec('end_kom','#message') ?}


\tar_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Zmiana zakresu
::----------------------------------------------------------------------------------------------------------------------
ZAKR.win_edit('TAR_Z');
{? ZAKR.edit()
||
   ceny.for_each("{? ceny.TYP=TAZ.SD & ceny.REF<>'' || ceny.del() ?}");
   exec('tar_zakr_oz','ceny');
   exec('tar_utw','ceny',_a)
?};
grp_disp(ceny,cur_win(1));
grp_disp(TAP,'WER_KH')



\tar_zakr_oz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Zmiana zakresu
::----------------------------------------------------------------------------------------------------------------------
_user:=OPERATOR.USER().KOD;
OZ.index('OZ');
OZ.prefix(_user,_user);
{? ~OZ.first()
|| OZ.blank();
   OZ.USER:=_user;
   OZ.US:=OPERATOR.USER;
   OZ.add()
?};
OZ.TAR_AKT:=ZAKR.TAR_AKT;
OZ.put(1)


\zakr_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Zmiana zakresu
::----------------------------------------------------------------------------------------------------------------------
_user:=exec('name','users');
OZ.index('OZ');
OZ.prefix(_user,_user);
{? OZ.first()
|| ZAKR.TAR_AKT:=OZ.TAR_AKT
|| ZAKR.TAR_AKT:='T'
?}

:Sign Version 2.0 jowisz:1045 2023/09/18 07:00:04 c4ce3139234257274c675bc9abd5a90738d6e389a4e6ee2c4bc3a23cc1c29a53a91dc6938f7302ea616adf491bfc82792f6d0743755d1514e646b2cfd91029c9e06172f734032a0525ff033c6b1cf37231a8f85a03ddf23e6cb9218ee9952236d7039a2876dabb5d6230a8fcb8fd30a920ddaa3f04bc3a3875e9ed74cdb9a694

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: pos_win.fml
:: Utworzony: 24.03.2019
:: Autor: Markus
:: Systemy: Merit
::======================================================================================================================
:: Zawartość: Okna dla POSa
::======================================================================================================================


\pos_win_translate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Tablica tłumaczeń przycisków dla posa
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('elements_table','#desktop');
_tab.blank(); _tab.ID_SYS:='select@pos_win1'; _tab.NAME:='Wybierz'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='cancel@pos_win1'; _tab.NAME:='Anuluj'@; _tab.add();
_tab


\pos_row_icon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Grupowe okno z dolnymi przyciskami
::   WE: [_a] - alias tabeli
::       [_b] - okienko wertowania
::       [_c] - tytuł okienka
::       [_d] - dodatkowy tekst wyświetlany w górnym panel plusie
::   WY: Zwraca wybrany rekord z listy
:: ~OST: INFCOPY,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

_tab:=($_a)();

_close:="
   VAR_DEL.delete('__temp_pos_win_table','__temp_pos_win_name','__temp_pos_win_cancel','__temp_pos_win_title');
   1
";

_close();

__temp_pos_win_table:=_tab;
__temp_pos_win_name:=_b;
__temp_pos_win_cancel:=1;
__temp_pos_win_title:={? _>3 & type_of(_d)=type_of('') || _d || '' ?};

_before:="
   {? __temp_pos_win_title<>''
   ||
      _tab:=exec('elements_table','#desktop');
      _tab.blank(); _tab.ID_SYS:='faks_sym@pos_zaw1'; _tab.NAME:=__temp_pos_win_title; _tab.add();
      exec('load','#desktop','pos_zaw1','pos_zaw1.dsk',,,,,_tab);
      grp_disp(__temp_pos_win_table, __temp_pos_win_name)
   ?};
   exec('load','#desktop','pos_win1','pos_win1.dsk',,,,,exec('pos_win_translate','pos_win'));
   _sep:=exec('sep','#file');
   _file:='xwin16.png';
   _filepath:=tmp_dir()+_sep+_file;
   fcopy(_file,'@'+_filepath,1,0,1);
   exec('set_icons','#desktop','','pos_win1',_filepath);
1";

::_grp_close:="__temp_pos_win_cancel:=1;1";

_grp:=_tab.grp_make(_c,_before,'pos_win_kh3',,,,,'normal');

{? __temp_pos_win_title<>''
||
   _win2:=exec('create','#desktop',_tab,'pos_zaw1',_grp,2,120,,,,,,,,);
   _tab.grp_splt(_grp,,'horizontal','pozioma_3',0)
?};

_tab.grp_sel(_grp, _tab, _b,,,,,20,,,,,'maximized',,1);

_tab.grp_splt(_grp,,'horizontal','pozioma_4',0);

_win1:=exec('create','#desktop',_tab,'pos_win1',_grp,5,120,,,,,,,,);
_tab.win_sel(_grp);

_return:=_tab.select();

{? __temp_pos_win_cancel=1
||
   _return:=0
?};

_close();
_return


\rec_move
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Przesuwa rekord
::   WE: [_a] - 1/0 odswiezac okienko grupowe
::       [_b] - nazwa tabeli
::       [_c] - okienko wertowania w oknie grupowym do odswiezenia
::       [_d] - first,last,prev,next - domyslnie puste
::       [_e] - skok o ilosc rekordow - domyslnie o 1
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1  || _a:=0   ?} || _a:=0  ?};
{? _>=4 || {? type_of(_d)<>2  || _d:=''  ?} || _d:='' ?};
{? _>=5 || {? type_of(_e)<>1  || _e:=1   ?} || _e:=1  ?};

{? _b.f_active()=0
||
   {? _d='first' || _b.first()
   |? _d='last'  || _b.last()
   |? _d='prev'  || {! .._e |! _b.prev() !}
   |? _d='next'  || {! .._e |! _b.next() !}
   ?}
||
   {? _d='first' || _b.f_first()
   |? _d='last'  || _b.f_last()
   |? _d='prev'  || {! .._e |! _b.f_prev() !}
   |? _d='next'  || {! .._e |! _b.f_next() !}
   ?}
?};
{? _a=1
||
   grp_disp(_b,_c,1,1)
?};
~~


\win_kontrahent
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Wyświetla listę kontrahentów
::   WE: -
::   WY: KH.select()
::----------------------------------------------------------------------------------------------------------------------
win:=KH.mk_sel('Wybierz Kontrahenta'@,,,'pos_win_wert',,,,,'U',,,,,,'auto');
KH.win_fld(win,,'KOD',,,10);
KH.win_fld(win,,'SKR',,,15,,,,,,,1);
KH.win_fld(win,,'NAZ_P',,,30,,,,,,,1);
KH.win_fld(win,,'NIP',,,20,,,,,,,1);
KH.win_fld(win,,'MIASTO',,,20,,,,,,,1);
KH.win_fld(win,,'UL',,,25,,,,,,,1);
KH.win_act(win,,'Formuła','Wybierz'@@,,,"","exec('pos_row_icon_ok','pos_win')",1,1,"","",'W');
KH.win_sel(win);

::_return:=KH.select();
_return:=params_exec('pos_row_icon','pos_win','KH',win,'Wybierz Kontrahenta'@);
_return


\win_handlowiec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Wyświetla listę handlowców
::   WE: -
::   WY: HAN.select()
::----------------------------------------------------------------------------------------------------------------------
win:=HAN.mk_sel('Wybierz Handlowca'@,,,,,,,,'U',,,,,,'auto');
HAN.win_fld(win,,'NAZ',,,120);
HAN.win_act(win,,'Formuła','Wybierz'@@,,,"","exec('pos_row_icon_ok','pos_win')",1,1,"","",'W');
HAN.win_sel(win);

_return:=params_exec('pos_row_icon','pos_win','HAN',win,'Wybierz Handlowca'@);
_return


\win_material
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Wyświetla listę materiałów
::   WE: -
::   WY: M.select()
::----------------------------------------------------------------------------------------------------------------------
win:=M.mk_sel('Wybierz produkt'@,,,,,,,,'U',,,,,,'auto');
M.win_fld(win,,'N',,,80);
M.win_fld(win,,'KODK',,,40);
M.win_act(win,,'Formuła','Wybierz'@@,,,"","exec('pos_row_icon_ok','pos_win')",1,1,"","",'W');
M.win_sel(win);

_return:=params_exec('pos_row_icon','pos_win','M',win,'Wybierz produkt'@);
_return


\win_dokument
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Wyświetla listę dokumentów
::   WE: -
::   WY: FAKS.select()
::----------------------------------------------------------------------------------------------------------------------
::FAKS.win_sel('WERPOSSZ');
::_faks_temp:={? FAKS.select() || FAKS.ref() ||  null ?};
_return:=params_exec('pos_row_icon','pos_win','FAKS','WERPOSSZ','Wybierz dokument'@);
_return


\win_dokument_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Wyświetla listę dokumentów
::   WE: [_a] - symbol zawieszanego dokumentu
::   WY: SLO.select()
::----------------------------------------------------------------------------------------------------------------------
_title:={? _>0 & type_of(_a)=type_of('') || 'Zawieszenie dokumentu: %1'@[_a] || '' ?};

_return:=params_exec('pos_row_icon','pos_win','SLO','ONE_POS','Symbol zawieszonego dokumentu'@, _title);

::_return:=params_exec('pos_row_icon','pos_win','SLO','ONE_POS','Symbol zawieszonego dokumentu'@);
_return


\test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Wyświetla listę kontrahentów
::----------------------------------------------------------------------------------------------------------------------
_kh:=params_exec('win_kontrahent','pos_win');
~~


\pos_row_icon_click
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Obsługa przycisków w oknie z ikonami
::   WE: [_a] - mode (string): first, last, next, prev, next10, prev10
::   WY: -
::----------------------------------------------------------------------------------------------------------------------
_skip:=1;
_mode:='first';
{? _>0 & type_of(_a)=type_of('')
||
   {? _a='first' | _a='last' | _a='next' | _a='prev'
   ||
      _mode:=_a
   |? _a='next10'
   ||
      _mode:='next';
      _skip:=10
   |? _a='prev10'
   ||
      _mode:='prev';
      _skip:=10
   ?}
?};
exec('rec_move','pos_win',1,__temp_pos_win_table,__temp_pos_win_name,_mode,_skip);
~~


\pos_row_icon_cancel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Obsługa przycisku anulacji
::   WE: -
::   WY: -
::----------------------------------------------------------------------------------------------------------------------
__temp_pos_win_cancel:=1;
sel_exit();
~~


\pos_row_icon_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Obsługa przycisku zatwierdzenia
::   WE: -
::   WY: -
::----------------------------------------------------------------------------------------------------------------------
__temp_pos_win_cancel:=0;
sel_exit();
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 e417b38803f38bdcd2eaff04da116bf05fa671a16ec7362a5cbb716db5770f6877fd9c831441c7cbc8cdc735987304b466eb8780637c246008e2ccb7e8f79b9e82dff05cd27c3e709eed423149cd97e7d8dc3f72c95bf8ac8b8ce89a35a51b6f0f81f64e65d886f55c1740d5a7bc365319e981dbb73a742c8a19d045dc250ba0

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: korekty_zus.fml
:: Utworzony: 15.12.2015
:: Autor: jaws, DAROKR, PKOSO
::======================================================================================================================
:: Zawartosc: Funkcje obsługujące korekty ZUS
::======================================================================================================================


\usun_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [8.60]
:: OPIS: Usunięcie zapisów z tabeli korekt.
::   WE: _a - wymuszenie kasowania dla korekty z powiązaną nieobecnością
::  OLD: \kor_usu/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1)
|| _a:=0
?};
KOR.cntx_psh();
KORN.cntx_psh();
KOR.index('KOR_PNR');
KORN.index('KOR_NAPZ');
KORN.prefix('N',P.ref(),'L'+FUNKCJE.ZNLISTY());
{? KORN.first()
|| {!
   |? {? ~KORN.N | _a
      || KOR.prefix(KORN.ref());
         {? KOR.first()
         || {!
            |? KOR.del()
            !}
         ?};
         KORN.del()
      || KORN.next()
      ?}
   !}
?};
KORN.cntx_pop();
KOR.cntx_pop();
1


\kor_zc_01
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Wprowadzenie naglowka korekty dla umowy zlecenia
::   WE:
::   WY:
::  OLD: \kor_zc_01/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
ZC_INFO.actions('WYB','W');
KORN_Z.index('ZLEC');
KORN_Z.prefix(ZC.ref);
KORN_Z.first;
KORN_Z.win_sel('WER');
KORN_Z.select();
ZC_INFO.actions('WYB','')


\kor_zc_02
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Akcja przypisania rozliczenia korekty ZUS biezacym rachunkiem
::   WE:
::   WY:
::  OLD: \kor_zc_02/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
RH.cntx_psh();
KORN_Z.index('RH');
KORN_Z.prefix(RH.ref);
_oblicz:=0;
{? KORN_Z.first ||  _oblicz:=1 ?};
KORN_Z.index('ZLEC');
KORN_Z.prefix(ZC.ref);
KORN_Z.first;
KORN_Z.win_sel('WER_RH');
KORN_Z.select();
KORN_Z.index('RH');
KORN_Z.prefix(RH.ref);
O.cntx_psh();
{? (KORN_Z.first | _oblicz) & RH.BL<>'T' & RH.O().Z<>'T'
|| {? FUN.ask('Czy wykonać przeliczenie składników płacowych dla wybranego rachunku korygującego?'@)
   || FUNKCJE.OTWOLTRH();
      exec('wylicz_rhs','zlec_rh');
      RH.win_edit('RED')
   ?}
?};
O.cntx_pop();
RH.cntx_pop()


\kor_zc_03
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Wyswietlenie pozycji dla naglowka korekty ZUS od strony zlecenia
::   WE: [_a] - INTEGER od strony rachunku
::   WY:
::  OLD: \kor_zc_03/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_pres('_a')=type_of(0) || _a ?};
{? KORN_Z.DW<>#0 || _a:=1 ?};
_tab:=tab_tmp(1,'LP','INTEGER','LP',
   'R','INTEGER','Numer rubryki podstawy',
   'RODZ','STRING[50]','Rodzaj składki',
   'KW','REAL','Kwota');

{? _a
|| _wylicz:=0
|| _wylicz:=FUN.ask('Czy zaproponować korektę?\nUwaga wprowadzone dane w pozycjach zostaną nadpisane.'@)
?};
RH.cntx_psh();
ZC_INFO.cntx_psh();
{? _wylicz
|| RH.index('RACHUNKC');
   RH.prefix(ZC.ref,KORN_Z.RU,KORN_Z.MU);
   _z_rh:="_wyn:=0;
      {? RH.first()
      || {!
         |? {? KORN_Z.ZC_INFO().ZUS='N' & (_a=753 | _a=765 | _a=766 | _a=958 | _a=959)
            || _wyn+=exec('licz_rhs','lista_licz',_a)
            |? ZC_INFO.FC='N' & (_a=754 | _a=767)
            || _wyn+=exec('licz_rhs','lista_licz',_a)
            |? ZC_INFO.FW='N' & (_a=754 | _a=960)
            || _wyn+=exec('licz_rhs','lista_licz',_a)
            |? ZC_INFO.FP='N' & (_a=981 | _a=982)
            || _wyn+=exec('licz_rhs','lista_licz',_a)
            |? ZC_INFO.FG='N' & (_a=981 | _a=983)
            || _wyn+=exec('licz_rhs','lista_licz',_a)
            |? ZC_INFO.KC='N' & (_a=791 | _a=792)
            || _wyn+=exec('licz_rhs','lista_licz',_a)
            ?};
            RH.next
         !}
      ?}; _wyn";
      exec('czytaj','#stalesys',date(KORN_Z.RU,KORN_Z.MU,0),KST)
?};

_kol:=obj_new(15);
_lp:=1;
_kol[_lp]:=753; _lp+=1;
_kol[_lp]:=765; _lp+=1;
_kol[_lp]:=958; _lp+=1;
_kol[_lp]:=766; _lp+=1;
_kol[_lp]:=959; _lp+=1;
_kol[_lp]:=754; _lp+=1;
_kol[_lp]:=767; _lp+=1;
_kol[_lp]:=960; _lp+=1;
_kol[_lp]:=981; _lp+=1;
_kol[_lp]:=982; _lp+=1;
_kol[_lp]:=983; _lp+=1;
_kol[_lp]:=791; _lp+=1;
_kol[_lp]:=792; _lp+=1;
_kol[_lp]:=961; _lp+=1;
_kol[_lp]:=962;
KOR_Z.index('KORN_Z');
KOR_Z.prefix(KORN_Z.ref);

R.cntx_psh();
R.prefix;
_skl:=0;
{! _ind:=1..15
|!
   {? R.seek(__RUB.ref(_kol[_ind]))
   || _kw:=0;
      _tab.blank();
      _tab.LP:=_ind;
      _tab.R:=_kol[_ind];
      _tab.RODZ:=R.RT;
      {? _wylicz
      || _kw:=_z_rh(_kol[_ind]);
         {? _kol[_ind]=791
         || {? ~_kw || _kw+=_skl; _kw*=-1 ?}
         |? _kol[_ind]=792
         || {? ~_kw || _kw+={? KST.PRKC || KST.PRKC/100*_skl$2 ?}; _kw*=-1 ?}
         || {? _kol[_ind]=765 | _kol[_ind]=766 | _kol[_ind]=767
            || _skl+=_kw
            ?}
         ?};
         _tab.KW:=(_kw*-1)
      || _tab.KW:={? KOR_Z.find_key(R.ref) || KOR_Z.KW ?}
      ?};
      {? _a
      || {? _tab.KW || _tab.add ?}
      || _tab.add()
      ?}
   ?}
!};
{? _tab.first()
|| _wer:=_tab.mk_sel('Pozycje korekty ZUS'@,,0,'kor_zus_zc');
   _tab.win_fld(_wer,,'LP',,,3,,1,'Lp.'@);
   _tab.win_fld(_wer,,'R',,,10,,1,'Numer składnika'@);
   _tab.win_fld(_wer,,'RODZ',,,20,,1,'Nazwa składnika płacowego'@);
   _tab.win_fld(_wer,,'KW',,,15,2,,'Kwota'@);
   _tab.win_act(_wer,0,'Popraw','&Wprowadź'@@,,,,,1,,,,'W');
   _tab.win_act(_wer,0,'Kolejność');
   _tab.win_sel(_wer);
   {? _a
   || _tab.select(,,,'W')
   || _tab.select();
     {? KOR_Z.first || {! |? KOR_Z.del !} ?};
     {? _tab.first
     || {!
        |? {? _tab.KW
           || KOR_Z.blank;
              KOR_Z.KORN_Z:=KORN_Z.ref;
              KOR_Z.R:=__RUB.ref(_tab.R);
              KOR_Z.KW:=_tab.KW;
              KOR_Z.add(1)
           ?};
           _tab.next
        !}
     ?}
   ?}
?};
ZC_INFO.cntx_pop();
RH.cntx_pop();
R.cntx_pop()


\kor_zc_04
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Rekord przed tabeli KORN_Z
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::  OLD: \kor_zc_04/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| {? KORN_Z.RH<>null
   || KORN_Z.actions('WER','pU',,1)
   || KORN_Z.actions('WER','',,1)
   ?}
?};
~~


\kor_zc_05
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Rekord po dla tabeli KORN_Z
::   WE:
::   WY:
::  OLD: \kor_zc_05/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
_pole:=__CHK.record(KORN_Z,,'ZC_INFO','RU','MU');
{? _pole<>'' || return(_pole) ?};
{? KORN_Z.RU>2050 | KORN_Z.RU<1900
|| FUN.emsg('Błędny rok ubezpieczeniowy.'@);
   return('RU')
?};
{? KORN_Z.MU>12 | KORN_Z.MU<1
|| FUN.emsg('Błędny miesiąc ubezpieczeniowy.'@);
   return('MU')
?};
{? 2+KORN_Z.ZC_INFO().TTUB().KOD='01'
|| FUN.emsg('Nie można wykonać korekty dla tytułu ubezpieczenia '@+S_ZUS.KOD);
   return('ZC_INFO')
?};
RH.cntx_psh();
RH.index('RACHUNKC');
RH.prefix(ZC.ref,KORN_Z.RU,KORN_Z.MU);
{? RH.first()
|| _brak:=0
|| _brak:=1
?};
RH.cntx_pop();
{? _brak
|| FUN.emsg('Brak rachunków przypisanych do danych ubezpieczeniowych korekty.'@);
   return('RU')
?};
''


\kor_zc_06
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Akcja Rozlicz dla tabeli KORN_Z od strony rachunku
::   WE:
::   WY:
::  OLD: \kor_zc_06/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
{? date(RH.DWY~1,RH.DWY~2,1)<date(KORN_Z.RU,KORN_Z.MU,1)
|| FUN.emsg('Bieżący rachunek nie może rozliczyć korekt z późniejszych okresów.'@)
|| KORN_Z.RH:=RH.ref;
   KORN_Z.DW:=RH.DWY;
   KORN_Z.R:='T';
   KORN_Z.put
?}


\kor_zc_08
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Rekord przed tabeli KORN_Z od strony rachunku okienko WER_RH
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::  OLD: \kor_zc_08/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
KOR_Z.index('KORN_Z');
KOR_Z.prefix(KORN_Z.ref);
{? _a
|| O.cntx_psh();
   _jest_k:=KOR_Z.first();
   {? ~_jest_k
   || KORN_Z.actions('WER_RH','ZRN',,1)
   |? (KORN_Z.RH<>null & KORN_Z.RH<>RH.ref()) | RH.BL='T' | RH.O().Z='T'
   || KORN_Z.actions('WER_RH','RN',,1)
   || {? KORN_Z.RH=RH.ref()
      || KORN_Z.actions('WER_RH','R','N',1)
      || KORN_Z.actions('WER_RH','N','R',1)
      ?}
   ?};
   O.cntx_pop()
?};
KORN_Z.RH=RH.ref()


\kor_zc_09
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Akcja Nie rozliczaj tabeli KORN_Z od strony rachunkow
::   WE:
::   WY:
::  OLD: \kor_zc_09/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
KORN_Z.RH:=null;
KORN_Z.DW:=#0;
KORN_Z.R:='N';
KORN_Z.put()


\kor_zc_11
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Akcja na usun przed dla tabeli KORN_Z
::   WE:
::   WY:
::  OLD: \kor_zc_11/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy na pewno usunąć nagłówek korekty wraz z pozycjami?'@)
|| KOR_Z.index('KORN_Z');
   KOR_Z.prefix(KORN_Z.ref);
   {? KOR_Z.first || {! |? KOR_Z.del() !} ?};
   KORN_Z.del
?}


\kor_prac_zus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Znalezienie ewentualnej podstawy z ZUS dla zwrotow w danym miesiacu.
::   WE: _a - P.ref
::       _b - Rok
::       _c - Miesiac
::   WY: podstawa z ZUS lub -1 jesli nie bylo zwrotu
::  OLD: \kor_prac_zus/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
_jest:=_wart:=0;
KORN.index('KOR_NAPZ');
KORN.prefix('T',_a);
{? KORN.first
|| {!
   |? {? KORN.ROK=_b & KORN.MC=_c || _jest:=1; _wart+=KORN.ZUS ?};
      KORN.next
   !}
?};
{? _jest || _wart || -1 ?}


\kor_zkor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [8.60]
:: OPIS: wyliczenie podstawy składki z korekt w miesiącu dla wyliczeń w zleceniach
::   WE: _a - rodzaj ubezpieczenia
::       _b - 1 -podstawa 2 - składka
::       _c - miesiąc
::       _d - rok
::   WY: wyliczona podstawa / składka
::  OLD: \kor_zkor/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
_wart:=0;
KORN.cntx_psh();
P.cntx_psh();
P.index('PRACOSOB');
P.prefix(exec('ref_firma','ustawienia'),'Z',OSOBA.ref());
KOR.index('KOR_PRN');
KORN.index('KOR_NAGU');
{? P.first()
|| {!
   |? KORN.prefix(_d,_c,P.ref());
      {? KORN.first()
      || {!
         |? KOR.prefix(KORN.ref(),_a);
            {? KOR.first()
            || {!
               |? _wart+={? _b=1 || KOR.POD || KOR.SKL ?};
                  KOR.next()
               !}
            ?};
            KORN.next()
         !}
      ?};
      P.next()
   !}
?};
KORN.cntx_pop();
P.cntx_pop();
_wart


\kora_zkor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.30]
:: OPIS: Wyliczenie podstawy składki z korekt w miesiącu dla wyliczeń w zleceniach.
::   WE: _a [NUMBER] - Rodzaj ubezpieczenia (numer atrybutu)
::       _b [NUMBER] - Typ wyniku: 1 - podstawa; 2 - skladka.
::       _c [NUMBER] - Miesiac.
::       _d [NUMBER] - Rok.
::   WY: Wyliczona kwota (podstawa lub skladka).
::  OLD: \kora_zkor/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
_odp:=0;
_tab:=__RUB.sys_rub(_a,date(_d,_c,1));
{? _tab.first()
|| {!
   |? _odp+=exec('kor_zkor','korekty_zus',_tab.RN,_b,_c,_d);
      _tab.next()
   !}
?};
_odp


\kor_zc_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.41]
:: OPIS: Procedura zwraca sumę wartosci skladek ZUS dla korekty do rozliczenia na liscie dla biezacego rachunku
::   WE: _a,_b.... - INTEGER - argumenty okreslajace nr skladnika placowego
::   WY: Suma skladnikow
::  OLD: \kor_zc_skl/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0
|| return(0)
?};
_wyn:=0;
KORN_Z.cntx_psh();
KORN_Z.index('RH');
KORN_Z.prefix(RH.ref());
KOR_Z.index('KORN_Z');
{? KORN_Z.first()
|| {!
   |? KOR_Z.prefix(KORN_Z.ref());
      {? KOR_Z.first()
      || {! _ind:=1 .. _
         |! {? KOR_Z.find_key(__RUB.ref(_[_ind]))
            || _wyn+=KOR_Z.KW
            ?}
         !}
      ?};
      KORN_Z.next()
   !}
?};
KORN_Z.cntx_pop();
_wyn


\kor_prac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.30]
:: OPIS: wyliczenie podstawy / skladki z korekt w miesiacu dla pracownika
::   WE: _a - rodzaj ubezpieczenia
::       _b - 1 -podstawa 2 - składka
::       _c - miesiąc
::       _d - rok
::       [_e] - ewentualny znak listy
::   WY: wyliczona podstawa / składka
::  OLD: \kor_prac/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
_lista:={? var_pres('_e')=type_of('') & _e<>'' || _e || '' ?};
_wart:=0;
KORN.cntx_psh;
KOR.index('KOR_PRN');
{? _lista=''
|| KORN.index('KOR_NAGU');
   KORN.prefix(_d,_c,P.ref)
|| KORN.index('KOR_NAPR');
   KORN.prefix(P.ref(),_d,_c,_lista)
?};
{? KORN.first
|| {!
   |? KOR.prefix(KORN.ref,_a);
      {? KOR.first
      || {!
         |? _wart+={? _b=1 || KOR.POD || KOR.SKL ?};
            KOR.next
         !}
      ?};
      KORN.next
   !}
?};
KORN.cntx_pop;
_wart


\kora_prac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.30]
:: OPIS: Wyliczenie podstawy / skladki z korekt w miesiacu dla pracownika.
::   WE: _a - Rodzaj ubezpieczenia (numer atrybutu).
::       _b - Typ wyniku: 1 - podstawa; 2 - skladka.
::       _c - Miesiac.
::       _d - Rok.
::      [_e] - ewentualny znak listy
::   WY: Wyliczona kwota (podstawa lub skladka).
::  OLD: \kora_prac/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
_lista:={? var_pres('_e')<>type_of('') || '' || _e ?};
_odp:=0;
_tab:=__RUB.sys_rub(_a,date(_d,_c,1));
{? _tab.first()
|| {!
   |? _odp+=exec('kor_prac','korekty_zus',_tab.RN,_b,_c,_d,_lista);
      _tab.next()
   !}
?};
_odp


\kor_zc_zwrot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.51]
:: OPIS: Procedura zwraca sumę wartosci składek zwrotu ZUS dla korekty w tym samym roku.
::   WY: Suma składników
::  OLD: \kor_zc_zwrot/kor_zus.fml
::----------------------------------------------------------------------------------------------------------------------
{? RH.KOR<>'T' || return(0) ?};
_wyn:=0;
_suma:="{? _a<0 || _a ?}";
KORN_Z.cntx_psh();
KORN_Z.index('RH');
KORN_Z.prefix(RH.ref());
KOR_Z.index('KORN_Z');
{? KORN_Z.first()
|| {? KORN_Z.DW~1=KORN_Z.RU
   || _wartosc:=exec('kor_zc_skl','korekty_zus',765);
      _wyn+=_suma(_wartosc);
      _wartosc:=exec('kor_zc_skl','korekty_zus',766);
      _wyn+=_suma(_wartosc);
      _wartosc:=exec('kor_zc_skl','korekty_zus',767);
      _wyn+=_suma(_wartosc)
   ?}
?};
KORN_Z.cntx_pop();
{? _wyn<0
|| _wyn*-1
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 e17fc3720d3bbdc0e38311ac7dfe43528bf7fca1d5f72601d5f9b898eea7fa65029fcbbf670b59ebf8690aeadc2b675c91289aef59bc56ba83e680a243ab6fcdbae2f50dcb2b0e43a8399b043b963f463da045f52ea70680734b80cd6b627851078ed54b526cddd251e3e41e9fcb8bed216964abd29164422a698b15dd7e1124

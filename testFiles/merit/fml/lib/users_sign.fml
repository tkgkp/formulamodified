:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: users_sign.fml [21.37]
:: Utworzony: 15.17.2021
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi tabeli USR_SIGN - certyfikaty (podpisy) użytkownika
::======================================================================================================================


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Wyświetla certyfikaty
::   WE: [_a] - zakres: 1-użytkownika, [0]-wszystkie
::----------------------------------------------------------------------------------------------------------------------
_usr:={? var_pres('_a')>0 & type_of(_a)=type_of(1) || _a ?};
USR_SIGN.index('DATE_VAL');
{? _usr
|| USR_SIGN.prefix(OPERATOR.USER)
|| USR_SIGN.prefix()
?};
USR_SIGN.win_sel('WER');
USR_SIGN.select()


\akc_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Akcja Dołącz okna WER tabeli USR_SIGN
:: ~OST: INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
{? FUN.ask('Rejestracja certyfikatu wymaga\npodpisania dokumentu PDF.\n\nPrzygotuj podpis.\n\nKontynuować?'@)
|| _pdf:='@'+tmp_dir()+'\\usr_sign.pdf';
   _ref:=null;
   {? rep_exec('wsp_usr_sign',,,_pdf,1,,1)
   || _obj:=exec('ESIGN','#object');
      _obj.start();
      _obj.s_file_add(_pdf,1);
      _res:=_obj.sign(,1);
      {? _res.status=1
      || _tab:=_res.TAB;
         {? _tab.first()
         || USR_SIGN.cntx_psh();
            USR_SIGN.prefix();
            USR_SIGN.blank();
            USR_SIGN.USERS:=OPERATOR.USER;
            USR_SIGN.FINGER:=_tab.FINGER;
            USR_SIGN.NAME:=exec('x509_str','#convert',_tab.memo_txt(,1,'SUBJECT'),'CN');
            USR_SIGN.DATE_B:=_tab.DTFROM;
            USR_SIGN.TIME_B:=_tab.TMFROM;
            USR_SIGN.DATE_E:=_tab.DTTO;
            USR_SIGN.TIME_E:=_tab.TMTO;
            USR_SIGN.TYPE:={? _tab.CERTTYPE='qualified' || 'K' || 'N' ?};
            USR_SIGN.index('FINGER'); USR_SIGN.prefix(USR_SIGN.USERS,USR_SIGN.FINGER,);
            {? USR_SIGN.first()
            || FUN.info('Certyfikat już istnieje.')
            |? USR_SIGN.add()
            || USR_SIGN.memo_set(_tab.memo_txt(,1,'SUBJECT'),'SUBJECT');
               USR_SIGN.memo_put(,'SUBJECT');
               USR_SIGN.memo_set(_tab.memo_txt(,1,'ISSUER'),'ISSUER');
               USR_SIGN.memo_put(,'ISSUER');
               USR_SIGN.memo_set(_tab.memo_txt(,1,'X509'),'CERT');
               USR_SIGN.memo_put(,'CERT');
               USR_SIGN.put();
               _ref:=USR_SIGN.ref()
            ?};
            USR_SIGN.cntx_pop();
            {? _ref
            || USR_SIGN.seek(_ref)
            ?}
         ?}
      ?};
      _obj.stop();
      ferase(_pdf,0)
   ?}
?}


\akc_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Akcja Pokaż okna WER tabeli USR_SIGN
:: ~OST: INCLIEXEC,INFOPEN,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
_fn_cert:=tmp_dir()+'/usr_sign.cer';
_cert:=USR_SIGN.memo_txt(,1,'CERT');
{? _cert<>''
|| _file:=fopen('@'+_fn_cert,'w',0);
   {? _file
   || fwrite(_file,_cert);
      fclose(_file);
      cli_exec(_fn_cert)
   ?}
?}

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:25 9bb177bf828bd0521584a7f2c4328619de71630c71329eb0f6987f6ad23c825bda0edb8783c0e2a46647b7b2ff2167968465c8b51e78b7a61ed68f8f0f632684f2a2f2cdd25cddf33db9fc9cf3fb32d474d3772b503d1ed6e2acf19daeceda97b254397583dd2372374af5889b4591b4b2c6983835cd6f63c88049b1ac194385

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obiegi.fml
:: Utworzony: 20.03.2017
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa obiegów dokumentów - projekty
::======================================================================================================================



\aeprojwybetypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola ETYPY.OBS_PROJ
::----------------------------------------------------------------------------------------------------------------------
exec('dsprojwybetypy','obiegi_proj');
{? ~ETYPY.OBS_PROJ
|| ETYPY.PROJRODZ:=1;
   ETYPY.PROJ_RW:=ETYPY.PROJ_SJO:=null
?};
1


\dsprojwybetypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Na wyświetl dla pola ETYPY.OBS_PROJ
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.OBS_PROJ
|| ETYPY.efld_opt(ETYPY.win_edit('?'),'mark=0,enable=1',,'PROJRODZ');
   ETYPY.efld_opt(ETYPY.win_edit('?'),'mark=1,enable=1',,'PROJ_RW');
   {? ETYPY.PROJRODZ=2
   || ETYPY.efld_opt(ETYPY.win_edit('?'),'mark=0,enable=0',,'PROJ_SJO')
   || ETYPY.efld_opt(ETYPY.win_edit('?'),'mark=0,enable=1',,'PROJ_SJO')
   ?}
|| ETYPY.efld_opt(ETYPY.win_edit('?'),'mark=0,enable=0',,'PROJRODZ');
   ETYPY.efld_opt(ETYPY.win_edit('?'),'mark=0,enable=0',,'PROJ_RW');
   ETYPY.efld_opt(ETYPY.win_edit('?'),'mark=0,enable=0',,'PROJ_SJO')
?};
1


\beprojrwetypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola ETYPY.PROJ_RW
::----------------------------------------------------------------------------------------------------------------------
UD_TYP.index('SYMBOL'); UD_TYP.prefix('POZ_BUD',); UD_TYP.first();
UD_SCH.win_dict('SLO');
1


\beprojsjoetypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed redakcją pola ETYPY.PROJ_SJO
::----------------------------------------------------------------------------------------------------------------------
UD_TYP.index('SYMBOL'); UD_TYP.prefix('PODZORG',); UD_TYP.first();
UD_SCH.win_dict('SLO');
1


\def_odtw_sch_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Definicje dla projektów
::----------------------------------------------------------------------------------------------------------------------
ETYPYPOD.cntx_psh(); ETYPYDTP.cntx_psh();
ETYPYDTP.win_sel('WER_GR'); ETYPYDTP.select();
ETYPYPOD.cntx_pop(); ETYPYDTP.cntx_pop()


\bobs_dp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed obsługą okienka z definicjami projektów
::   WE: _a - 1 - zakładka z tworzeniem podziałów
::            2 - zakłądka z odtwarzaniem schematów dla projektów
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| ETYPYDTP.index('DISP'); ETYPYDTP.prefix(ETYPY.ref());
   ETYPYDTP.win_edit('RED')
|| ETYPYPOD.index('DISP'); ETYPYPOD.prefix(ETYPY.ref()); ETYPYPOD.win_edit('RED');
   exec('set_fml','#field',UD_POM,'PBUD',
         "UD_POM.PBUD:=ETYPYPOD.ROOT().SYMBOL; ~~",
         "1",
         "exec('ud_def_symbol_f3','schemat',ETYPYPOD.SCH_PROJ().UD_TYP,ETYPYPOD.SCH_PROJ)",
         "{? fld()='' || ETYPYPOD.ROOT:=null; return(1) ?};
          ETYPYPOD.SCH_PROJ().UD_TYP();
          _ref:=exec('ud_def_symbol_ae','schemat',UD_SCH.ref());
          {? _ref
          || ETYPYPOD.ROOT:=_ref;
             fld(ETYPYPOD.ROOT().SYMBOL)
          ?}
         "
      )
?}


\dol_def_odtw_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Dołączanie definicji odtwarzania schematów projektów
::----------------------------------------------------------------------------------------------------------------------
ETYPYPOD.blank();
{? ETYPYPOD.edit("exec('spr_def_odtwp','obiegi_proj')") || ETYPYPOD.add() ?}


\pop_def_odtw_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Poprawianie definicji odtwarzania schematów projektów
::----------------------------------------------------------------------------------------------------------------------
{? ETYPYPOD.edit("exec('spr_def_odtwp','obiegi_proj')") || ETYPYPOD.put() ?}


\usu_def_odtw_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Usuwanie definicji odtwarzania schematów projektów
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć definicję odtwarzania schematów projektów?'@) || ETYPYPOD.del() ?}


\be_sch_proj_dop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed redakcją pola ETYPYPOD.SCH_PROJ
::----------------------------------------------------------------------------------------------------------------------
UD_TYP.index('SYMBOL'); UD_TYP.prefix('PROJ_REM',); UD_TYP.first();
UD_SCH.win_dict('SLO');
1


\ae_sch_proj_dop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola ETYPYPOD.SCH_PROJ
::----------------------------------------------------------------------------------------------------------------------
{? ETYPYPOD.SCH_PROJ=null
|| FUN.info('Nie wybrano schematu projektów.'@); 0
|| {? ETYPYPOD.ROOT
   || ETYPYPOD.SCH_PROJ().UD_TYP();
      wart_wym:=ETYPYPOD.ROOT().SYMBOL;
      _ref:=exec('ud_def_symbol_ae','schemat',UD_SCH.ref());
      {? _ref
      || ETYPYPOD.ROOT:=_ref;
         UD_POM.PBUD:=ETYPYPOD.ROOT().SYMBOL
      || ETYPYPOD.ROOT:=null; UD_POM.PBUD:=''
      ?};
      VAR_DEL.delete('wart_wym')
   ?}; 1
?}


\bds_typdol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed wyświetl dla pola ETYPYPOD.TYP_DOL
::----------------------------------------------------------------------------------------------------------------------
{? ETYPYPOD.TYPDOL='G'
|| ETYPYPOD.efld_opt(ETYPYPOD.win_edit('?'),'mark=0,enable=0',,'ROOT');
   ETYPYPOD.efld_opt(ETYPYPOD.win_edit('?'),'mark=0,enable=0',UD_POM,'PBUD');
   ETYPYPOD.efld_opt(ETYPYPOD.win_edit('?'),'mark=0,enable=0',,'FORMULA')
|? ETYPYPOD.TYPDOL='N'
|| ETYPYPOD.efld_opt(ETYPYPOD.win_edit('?'),'mark=0,enable=1',,'ROOT');
   ETYPYPOD.efld_opt(ETYPYPOD.win_edit('?'),'mark=1,enable=1',UD_POM,'PBUD');
   ETYPYPOD.efld_opt(ETYPYPOD.win_edit('?'),'mark=0,enable=0',,'FORMULA')
|| ETYPYPOD.efld_opt(ETYPYPOD.win_edit('?'),'mark=0,enable=0',,'ROOT');
   ETYPYPOD.efld_opt(ETYPYPOD.win_edit('?'),'mark=0,enable=0',UD_POM,'PBUD');
   ETYPYPOD.efld_opt(ETYPYPOD.win_edit('?'),'mark=1,enable=1',,'FORMULA')
?};
1


\ae_typdol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola ETYPYPOD.TYP_DOL
::----------------------------------------------------------------------------------------------------------------------
exec('bds_typdol','obiegi_proj');
{? ETYPYPOD.TYPDOL='G'
|| ETYPYPOD.ROOT:=null; UD_POM.PBUD:=''; ETYPYPOD.FORMULA:=null
|? ETYPYPOD.TYPDOL='N'
|| ETYPYPOD.FORMULA:=null
|| ETYPYPOD.ROOT:=null; UD_POM.PBUD:=''
?};
1


\be_form_etp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed redakcją pola ETYPYPOD.FORMULA
::----------------------------------------------------------------------------------------------------------------------
ZMIENNE.RODZAJ:='v'; FORMULA.win_dict('FORMULY');
1


\spr_def_odtwp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Sprawdzenie rekordu w tabeli ETYPYPOD
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? -menu_txt()='popraw' || _ref:=ETYPYPOD.ref() ?};
ETYPYPOD.cntx_psh(); ETYPYPOD.index('UNIK'); ETYPYPOD.prefix(ETYPYPOD.ETYPY,ETYPYPOD.SCH_PROJ);
{? ETYPYPOD.first() & (-menu_txt<>'popraw' | _ref<>ETYPYPOD.ref())
|| FUN.info('Istnieje definicja odtwarzania schematu projektów %1.'@[ETYPYPOD.SCH_PROJ().SYMBOL]); _zwrot:='SCH_PROJ'
?};
ETYPYPOD.cntx_pop();
{? _zwrot='' & ETYPYPOD.TYPDOL='N' & ETYPYPOD.ROOT=null
|| FUN.info('Nie wybrano elementu nadrzędnego.'@); _zwrot:='PBUD'
?};
{? _zwrot='' & ETYPYPOD.TYPDOL='F' & ETYPYPOD.FORMULA=null
|| FUN.info('Nie wybrano formuły na ustalenie elementu nadrzędnego.'@); _zwrot:='FORMULA'
?};
_zwrot


\ae_dtp_mb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola ETYPYDTP.SKID_MB
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? ~ETYPYDTP.SKID_MB
|| FUN.info('Nie wybrano modelu.'@); _zwrot:=0
|| SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(ETYPYDTP.SKID_MB);
   _ile_wym:=SKID_MBP.size();
   SKID_MBP.cntx_pop();
   {? _ile_wym=0
   || FUN.info('Wybrany model nie posiada zdefiniowanych wymiarów.'@); _zwrot:=0
   || exec('bds_dtp_mb','obiegi_proj');
      {? _ile_wym<1 | ETYPYDTP.POZB_RW='R' || ETYPYDTP.POZB_F:=null ?};
      {? _ile_wym<2 | ETYPYDTP.WYM2_RW='J' || ETYPYDTP.WYM2_F:=null ?};
      {? _ile_wym<3 | ETYPYDTP.WYM3_RW='J' || ETYPYDTP.WYM3_F:=null ?};
      {? _ile_wym<4 | ETYPYDTP.WYM4_RW='J' || ETYPYDTP.WYM4_F:=null ?};
      {? _ile_wym<5 | ETYPYDTP.WYM5_RW='J' || ETYPYDTP.WYM5_F:=null ?}
   ?}
?};
_zwrot


\bds_dtp_mb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Na wyświetl dla pola ETYPYDTP.SKID_MB
::----------------------------------------------------------------------------------------------------------------------
SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(ETYPYDTP.SKID_MB);
_ile_wym:=SKID_MBP.size();
SKID_MBP.cntx_pop();
{? _ile_wym<1 | ETYPYDTP.POZB_RW='R'
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'POZB_F')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=1,enable=1',,'POZB_F')
?};
{? _ile_wym<1
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'POZB_RW')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=1',,'POZB_RW')
?};
{? _ile_wym<2 | ETYPYDTP.WYM2_RW='J'
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'WYM2_F')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=1,enable=1',,'WYM2_F')
?};
{? _ile_wym<2
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'WYM2_RW')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=1',,'WYM2_RW')
?};
{? _ile_wym<3 | ETYPYDTP.WYM3_RW='J'
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'WYM3_F')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=1,enable=1',,'WYM3_F')
?};
{? _ile_wym<3
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'WYM3_RW')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=1',,'WYM3_RW')
?};
{? _ile_wym<4 | ETYPYDTP.WYM4_RW='J'
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'WYM4_F')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=1,enable=1',,'WYM4_F')
?};
{? _ile_wym<4
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'WYM4_RW')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=1',,'WYM4_RW')
?};
{? _ile_wym<5 | ETYPYDTP.WYM5_RW='J'
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'WYM5_F')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=1,enable=1',,'WYM5_F')
?};
{? _ile_wym<5
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=0',,'WYM5_RW')
|| ETYPYDTP.efld_opt(ETYPYDTP.win_edit('?'),'mark=0,enable=1',,'WYM5_RW')
?};
1


\be_form_dtp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed redakcją pól tabeli ETYPYDTP: POZB_F, WYM2_F, WYM3_F, WYM4_F, WYM5_F
::   WE: _a - numer wymiaru - 1..5
::----------------------------------------------------------------------------------------------------------------------
ZMIENNE.RODZAJ:='v'; FORMULA.win_dict('FORMULY');
1


\etypydtp_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Dołączanie rekordów do tabeli ETYPYDTP
::----------------------------------------------------------------------------------------------------------------------
ETYPYDTP.blank();
{? ETYPYDTP.edit("exec('spr_etypydtp','obiegi_proj')") || ETYPYDTP.add() ?}


\etypydtp_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Poprawianie rekordów w tabeli ETYPYDTP
::----------------------------------------------------------------------------------------------------------------------
{? ETYPYDTP.edit("exec('spr_etypydtp','obiegi_proj')") || ETYPYDTP.put() ?}


\etypydtp_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Usuwanie rekordów w tabeli ETYPYDTP
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć definicję tworzenia podziałów?'@) || ETYPYDTP.del() ?}


\ae_form_rw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pól tabeli ETYPYDTP: POZB_RW, WYM2_RW, WYM3_RW, WYM4_RW, WYM5_RW
::   WE: _a - numer wymiaru - 1..5
::----------------------------------------------------------------------------------------------------------------------
exec('bds_dtp_mb','obiegi_proj');
{? _a=1 & ETYPYDTP.POZB_RW='R' || ETYPYDTP.POZB_F:=null ?};
{? _a=2 & ETYPYDTP.WYM2_RW='J' || ETYPYDTP.WYM2_F:=null ?};
{? _a=3 & ETYPYDTP.WYM3_RW='J' || ETYPYDTP.WYM3_F:=null ?};
{? _a=4 & ETYPYDTP.WYM4_RW='J' || ETYPYDTP.WYM4_F:=null ?};
{? _a=5 & ETYPYDTP.WYM5_RW='J' || ETYPYDTP.WYM5_F:=null ?};
1


\spr_etypydtp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Sprawdzenie rekordu w tabeli ETYPYDTP
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? -menu_txt()='popraw' || _ref:=ETYPYDTP.ref() ?};
ETYPYDTP.cntx_psh(); ETYPYDTP.index('UNIK'); ETYPYDTP.prefix(ETYPYDTP.ETYPY,ETYPYDTP.SKID_MB);
{? ETYPYDTP.first() & (-menu_txt<>'popraw' | _ref<>ETYPYDTP.ref())
|| FUN.info('Istnieje definicja tworzenia podziałów dla modelu %1.'@[ETYPYDTP.SKID_MB().KOD]);
   _zwrot:='SKID_MB'
?};
ETYPYDTP.cntx_pop();
SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix(ETYPYDTP.SKID_MB);
_ile_wym:=SKID_MBP.size();
SKID_MBP.cntx_pop();
{? _zwrot='' & ETYPYDTP.POZB_RW='F' & ~ETYPYDTP.POZB_F
|| FUN.info('Nie wybrano formuły na ustalanie pozycji budżetowej.'@); _zwrot:='POZB_F'
?};
{? _zwrot='' & _ile_wym>1 & ETYPYDTP.WYM2_RW='F' & ~ETYPYDTP.WYM2_F
|| FUN.info('Nie wybrano formuły na ustalanie wartości wymiaru 2.'@); _zwrot:='WYM2_F'
?};
{? _zwrot='' & _ile_wym>2 & ETYPYDTP.WYM3_RW='F' & ~ETYPYDTP.WYM3_F
|| FUN.info('Nie wybrano formuły na ustalanie wartości wymiaru 3.'@); _zwrot:='WYM3_F'
?};
{? _zwrot='' & _ile_wym>3 & ETYPYDTP.WYM4_RW='F' & ~ETYPYDTP.WYM4_F
|| FUN.info('Nie wybrano formuły na ustalanie wartości wymiaru 4.'@); _zwrot:='WYM4_F'
?};
{? _zwrot='' & _ile_wym>4 & ETYPYDTP.WYM5_RW='F' & ~ETYPYDTP.WYM5_F
|| FUN.info('Nie wybrano formuły na ustalanie wartości wymiaru 5.'@); _zwrot:='WYM5_F'
?};
_zwrot


\proj_dob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Projekty dla dokumentów w obiegu
::   WE: _a - opcja - 'AF' - akceptacja faktur
::                    'PF' - przekazanie faktur
::                    'AW' - akceptacja wniosków
::                    'PW' - przekazanie wniosków
::                    'WW' - wprowadzanie wniosków
::                    'WD' - wprowadzanie delegacji
::                    'AD' - akceptacja delegacji
::                    'PD' - przekazanie delegacji
::       _b - opcja - 1 - dla EDOKUMW
::                    2 - dla EDOKUM
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| _dalej:=1;
   {? _b=1
   || {? ~EDOKUMW.get()
      || {? _a='AF' | _a='PF'
         || FUN.info('Nie znaleziono faktury.'@);
            {? _a='PF'
            || exec('dob_refresh','obiegi','P','F')
            || exec('dob_refresh','obiegi','A','F')
            ?}
         |? _a='AW' | _a='PW' | _a='WW'
         || FUN.info('Nie znaleziono wniosku.'@);
            {? _a='WW'
            || exec('dob_refresh','obiegi','W','W')
            |? _a='PW'
            || exec('dob_refresh','obiegi','P','W')
            || exec('dob_refresh','obiegi','A','W')
            ?}
         || FUN.info('Nie znaleziono delegacji.'@);
            {? _a='WD'
            || exec('dob_refresh','obiegi','W','D')
            |? _a='PD'
            || exec('dob_refresh','obiegi','P','D')
            || exec('dob_refresh','obiegi','A','D')
            ?}
         ?};
         _dalej:=0
      ?}
   ?};
   {? _dalej
   || {? _b=1
      || EDOKUM.use('skid_v'+(EDOKUMW.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMW.EDOKUM()
      ?};
      _dalej:=exec('env_edokum','obiegi',_a,0)
   ?};
   {? _dalej
   || _get:=web_params_get();
      {? type_of(_get)>100 & var_pres('akcja',_get)>0 & (_get.akcja='dołącz' | _get.akcja='popraw')
      || {? _get.akcja='dołącz'
         || _par:=web_global_params_get();
            {? type_of(_par)>100 & var_pres('act_ok',_par)>0 & ~_par.act_ok
            || _pole:=params_exec('dol_dob_wt2','obiegi',0,0);
               {? type_of(_pole)=type_of('') & _pole<>''
               || web_top_update(0,,,_pole)
               || EDOKUM.get()
               ?};
               _dalej:=(_pole='')
            ?}
         || _pole:=params_exec('pop_dob_wt2','obiegi',0,0);
            {? type_of(_pole)=type_of('') & _pole<>''
            || web_top_update(0,,,_pole)
            || EDOKUM.get()
            ?};
            _dalej:=(_pole='')
         ?}
      ?}
   ?};
   {? _dalej
   || _okno:={? (_a+1)='F'
             || 'WER_WTF'
             |? (_a+1)='W'
             || 'WER_WTW'
             || 'WER_WTD'
             ?};
      _okno_r:={? (_a+1)='D'
               || 'RED_WTD'
               || 'RED_WT'
               ?};
      EDOKUMPR.index('DISP'); EDOKUMPR.prefix(EDOKUM.ref());
      EDOKUM.web_cntx_save();
      _gray:='';
      {? exec('blok_proj','obiegi_proj') || _gray:='DPU:D' ?};
      EDOKUMPR.web_win_init(_okno,,'grayed='+_gray);
      {? _b=1 & _a='PF'
      || EDOKUMW.web_cntx_save();
         EDOKUMPR.web_select(_okno,,,_okno_r,"web_top_close(); exec('dob_refresh_pr','obiegi_proj','P')")
      |? _b=1 & _a='AF'
      || EDOKUMW.web_cntx_save();
         EDOKUMPR.web_select(_okno,,,_okno_r,"web_top_close(); exec('dob_refresh_pr','obiegi_proj','A')")
      || EDOKUMPR.web_select(_okno,,,_okno_r)
      ?}
   ?}
|| EDOKUM.cntx_psh();
   {? EDOKUM.r_lock(1,1,1)
   || {? EDOKUM.TYP().OBS_PROJ
      || exec('set_var_proj','obiegi_proj');
         PROJEKTY.win_sel('SLO'); PROJEKTY.win_dict('SLO');
         EDOKUMPR.cntx_psh(); EDOKUMPR.use('skid_i'+(EDOKUM.name()+2));
         EDOKUMPR.index('DISP'); EDOKUMPR.prefix(EDOKUM.ref());
         {? typobi=2
         || EDOKUMPR.win_sel('WERW')
         |? typobi=3
         || EDOKUMPR.win_sel('WERD')
         || EDOKUMPR.win_sel('WER')
         ?};
         EDOKUMPR.win_edit('RED');
         EDOKUMPR.actions(EDOKUMPR.win_sel('?'),,'D:D',1);
         {? exec('blok_proj','obiegi_proj')
         || EDOKUMPR.select(,,,'DPU:D')
         || EDOKUMPR.select()
         ?};
         EDOKUMPR.cntx_pop()
      || FUN.info('W typie wybranym w dokumencie wyłączona jest obsługa projektów.'@)
      ?};
      EDOKUM.r_unlock()
   || exec('err_kom','obiegi')
   ?};
   EDOKUM.cntx_pop();
   ''
?}


\dob_refresh_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Odświeżanie dokumentów po wyjściu z projektów dla faktur.
::   WE: _a - 'A' - akceptacja faktur
::            'P' - przekazywanie faktur
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMW.web_cntx_load()
|| exec('env_wt','b_proces');
   exec('dob_refresh','obiegi',_a,'F')
?};
1


\set_var_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Ustawia formuły zmiennych
::----------------------------------------------------------------------------------------------------------------------
PROJEKTY.win_sel('SLO'); PROJEKTY.win_dict('SLO');
exec('set_fml','#field',UD_POM,'PBUD',
        "UD_POM.PBUD:={? EDOKUMPR.RODZWART || EDOKUMPR.RODZWART().SYMBOL || '' ?}; ~~",
        "1",
        "exec('ud_def_symbol_f3','schemat',EDOKUMPR.EDOKUM().TYP().PROJ_RW().UD_TYP,UD_SCH.ref())",
        "{? fld()=''
         || FUN.info('Nie wybrano rodzaju wartości.'@); EDOKUMPR.RODZWART:=null; return(0)
         ?};
         EDOKUMPR.EDOKUM().TYP().PROJ_RW().UD_TYP();
         _ref:=exec('ud_def_symbol_ae','schemat',UD_SCH.ref());
         _zwrot:=1;
         {? _ref
         || EDOKUMPR.RODZWART:=_ref;
            fld(EDOKUMPR.RODZWART().SYMBOL);
            exec('ae_rodz_war','obiegi_proj');
            {? EDOKUMPR.WART=0 & (-(6+menu_txt()))<>'popraw'
            || _ile_juz:=0;
               SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ');
               EDOKUMPR.cntx_psh(); EDOKUMPR.index('DISP'); EDOKUMPR.prefix(EDOKUM.ref());
               {? EDOKUMPR.first()
               || {! |?
                     SKIDXDUD.prefix(EDOKUMPR.RODZWART);
                     {? SKIDXDUD.first() & SKIDXDUD.WART_IL='W'
                     || _ile_juz+=EDOKUMPR.WART
                     ?};
                     EDOKUMPR.next()
                  !}
               ?};
               EDOKUMPR.cntx_pop();
               SKIDXDUD.cntx_pop();
               EDOKUMPR.WART:={? typobi=3
                              || 100-_ile_juz
                              || EDOKUM.NETTO-_ile_juz
                              ?}
            ?}
         || _zwrot:=0
         ?};
         exec('bds_edokumpr','obiegi_proj');
         _zwrot
        "
     );
exec('set_fml','#field',UD_POM,'JORG',
        "UD_POM.JORG:={? OBIEGI.UD_SKL
                      || OBIEGI.UD_SKL().SYMBOL
                      || ''
                      ?}; ~~",
        "1",
        "_typ:=_sch:=null;
         {? EDOKUMPR.EDOKUM().TYP().PROJ_SJO
         || _typ:=EDOKUMPR.EDOKUM().TYP().PROJ_SJO().UD_TYP; _sch:=UD_SCH.ref()
         || UD_TYP.index('SYMBOL'); UD_TYP.prefix('PODZORG',);
            {? UD_TYP.first()
            || _typ:=UD_TYP.ref();
               UD_SCH.index('SYMBOL'); UD_SCH.prefix(UD_TYP.ref(),'STRORG',);
               {? UD_SCH.first() || _sch:=UD_SCH.ref() ?}
            ?}
         ?};
         exec('ud_def_symbol_f3','schemat',_typ,_sch)",
        "{? fld()='' || OBIEGI.UD_SKL:=null; return(1) ?};
         _sch:=null;
         {? EDOKUMPR.EDOKUM().TYP().PROJ_SJO
         || _sch:=EDOKUMPR.EDOKUM().TYP().PROJ_SJO
         || UD_TYP.index('SYMBOL'); UD_TYP.prefix('PODZORG',);
            {? UD_TYP.first()
            || UD_SCH.index('SYMBOL'); UD_SCH.prefix(UD_TYP.ref(),'STRORG',);
               {? UD_SCH.first() || _sch:=UD_SCH.ref() ?}
            ?}
         ?};
         _ref:=exec('ud_def_symbol_ae','schemat',_sch);
         {? _ref
         || OBIEGI.UD_SKL:=_ref;
            fld(OBIEGI.UD_SKL().SYMBOL)
         ?};
         exec('proj_spr','obiegi_proj');
         1
        "
     );
exec('projekty_jorg','obiegi_proj')


\ae_rodz_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [14.xx]
:: OPIS: Po redakcji rodzaju wartości w projekcie pod dokumentem w obiegu.
::----------------------------------------------------------------------------------------------------------------------
SKIDXDUD.index('POZ'); SKIDXDUD.prefix(EDOKUMPR.RODZWART);
{? SKIDXDUD.first()
|| {? SKIDXDUD.WART_IL='W'
   || EDOKUMPR.WAL:={? SKIDXDUD.WSK_WAL='T'
                    || EDOKUMPR.EDOKUM().WAL
                    || exec('wal_nar','dok_fks')
                    ?};
      EDOKUMPR.JM:=null
   || {? EPODZ.JM=null || EPODZ.JM:=SKIDXDUD.JM ?};
      EPODZ.WAL:=null
   ?}
?}


\dol_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Dołączanie projektów dla dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| UD_POM.PBUD:=UD_POM.JORG:=OBIEGI.PROJEKT:='';
   OBIEGI.SZEF:=OBIEGI.KH_PROJ:=OBIEGI.UD_SKL:=OBIEGI.KH_PROJ:=null;
   EDOKUMPR.blank(1);
   exec('env_wt','b_proces');
   web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'akcjawyw','D'));
   EDOKUM.web_cntx_load();
   EDOKUMPR.USERS:=OPERATOR.USER;
   EDOKUM.TYP();
   OBIEGI.SZEF:=OPERATOR.USER().OSOBA;
   OBIEGI.R:={? ETYPY.PROJRODZ=2 || 'Zewnętrzny' || 'Wewnętrzny' ?};
   OBIEGI.UD_SKL:=OBIEGI.KH_PROJ:=null;
   EDOKUMPR.EDOKUM:=EDOKUM.ref();
   EDOKUMPR.ETYPY:=EDOKUM.TYP;
   _okno:={? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg delegacji'
          || 'RED_WTD'
          || 'RED_WT'
          ?};
   exec('ini_pola_edpr','obiegi_proj',_okno);
   EDOKUMPR.web_edit(_okno,,,,"{? _a='OK'
                               || exec('env_wt','b_proces');
                                  _pole:=exec('spr_edokumpr','obiegi_proj');
                                  {? _pole=''
                                  || {? EDOKUMPR.add()
                                     || exec('akt_tree_proj','obiegi_proj');
                                        exec('nal_podz_proj','obiegi_proj');
                                        EDOKUMPR.web_eclose();
                                        EDOKUMPR.web_refresh(web_top_win(1))
                                     ?}
                                  || web_top_update(0,,,_pole)
                                  ?}
                               || EDOKUMPR.web_eclose()
                               ?}")
|| EDOKUMPR.blank();
   EDOKUMPR.USERS:=OPERATOR.USER;
   EDOKUMPR.EDOKUM().TYP();
   OBIEGI.SZEF:=OPERATOR.USER().OSOBA;
   OBIEGI.R:={? ETYPY.PROJRODZ=2 || 'Zewnętrzny' || 'Wewnętrzny' ?};
   OBIEGI.UD_SKL:=OBIEGI.KH_PROJ:=null;
   {? EDOKUMPR.edit("exec('spr_edokumpr','obiegi_proj')") & EDOKUMPR.add()
   || exec('akt_tree_proj','obiegi_proj');
      exec('nal_podz_proj','obiegi_proj')
   ?}
?}


\pop_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Poprawianie projektów dla dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| exec('env_wt','b_proces');
   exec('rkprz_edokumpr','obiegi_proj');
   web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'akcjawyw','P'));
   UD_POM.PBUD:={? EDOKUMPR.RODZWART || EDOKUMPR.RODZWART().SYMBOL || '' ?};
   UD_POM.JORG:={? OBIEGI.UD_SKL || OBIEGI.UD_SKL().SYMBOL || '' ?};
   OBIEGI.PROJEKT:=EDOKUMPR.PROJEKT().SYM;
   EDOKUM.web_cntx_load();
   _okno:={? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg delegacji'
          || 'RED_WTD'
          || 'RED_WT'
          ?};
   exec('ini_pola_edpr','obiegi_proj',_okno);
   EDOKUMPR.web_edit(_okno,,,,"{? _a='OK'
                                  || exec('env_wt','b_proces');
                                     _pole:=exec('spr_edokumpr','obiegi_proj');
                                     {? _pole=''
                                     || {? EDOKUMPR.put()
                                        || exec('akt_tree_proj','obiegi_proj');
                                           exec('nal_podz_proj','obiegi_proj');
                                           EDOKUMPR.web_eclose();
                                           EDOKUMPR.web_refresh(web_top_win(1))
                                        ?}
                                     || web_top_update(0,,,_pole)
                                     ?}
                                  || EDOKUMPR.web_eclose()
                                  ?}")
|| {? EDOKUMPR.edit("exec('spr_edokumpr','obiegi_proj')") & EDOKUMPR.put()
   || exec('akt_tree_proj','obiegi_proj');
      exec('nal_podz_proj','obiegi_proj')
   ?}
?}


\spr_edokumpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Sprawdzanie projektów pod dokumentami w obiegu
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? exec('on','#mwapi')
|| {? EDOKUMPR.ETYPY().OBS_PROJ=0
   || FUN.info('Typ delegacji uniemożliwia dołączanie projektów.'@);
      _zwrot:='PROJEKT'
   |? EDOKUMPR.RODZWART<>null & EDOKUMPR.ETYPY().PROJ_RW().UD_TYP<>EDOKUMPR.RODZWART().UD_TYP
   || FUN.info('Rodzaj wartości niezgodny z definicją w typie delegacji.'@);
      _zwrot:='RODZWART'
   ?}
?};
{? _zwrot='' & ~EDOKUMPR.RODZWART
|| FUN.info('Nie wybrano rodzaju wartości.'@); _zwrot:='RODZWART'
?};
{? _zwrot='' & ~EDOKUMPR.PROJEKT
|| FUN.info('Nie wybrano projektu.'@); _zwrot:='PROJEKT'
?};
{? _zwrot='' & ~EDOKUMPR.WART
|| FUN.info('Nie podano wartości.'@);  _zwrot:='WART'
?};

_zwrot


\ae_projrodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola ETYPY.PROJRODZ
::----------------------------------------------------------------------------------------------------------------------
exec('dsprojwybetypy','obiegi_proj');
{? ETYPY.PROJRODZ=2 || ETYPY.PROJ_SJO:=null ?};
1


\be_obiegi_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed redakcją pola OBIEGI.R
::----------------------------------------------------------------------------------------------------------------------
EDOKUMPR.EDOKUM().TYP().PROJRODZ=1


\ae_obiegi_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola OBIEGI.R
::----------------------------------------------------------------------------------------------------------------------
exec('bds_edokumpr','obiegi_proj');
{? OBIEGI.R='Wewnętrzny'
|| OBIEGI.KH_PROJ:=null
|| UD_POM.JORG:=''; OBIEGI.UD_SKL:=null
?};
{? app_info('web_sesid')<>''
|| EDOKUM.use('skid_v'+(EDOKUMPR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMPR.EDOKUM();
   _okno:={? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg delegacji'
          || 'RED_WTD'
          || 'RED_WT'
          ?};
   {? OBIEGI.R='Wewnętrzny'
   || EDOKUMPR.web_efld_opt(_okno,,'mark=0,enable=1,editable=1',UD_POM,'JORG');
      EDOKUMPR.web_efld_opt(_okno,,'mark=0,enable=1,editable=1',OBIEGI,'UD_SKL');
      EDOKUMPR.web_efld_opt(_okno,,'mark=0,enable=0,editable=0',OBIEGI,'KH_PROJ')
   || EDOKUMPR.web_efld_opt(_okno,,'mark=0,enable=0,editable=0',UD_POM,'JORG');
      EDOKUMPR.web_efld_opt(_okno,,'mark=0,enable=0,editable=0',OBIEGI,'UD_SKL');
      EDOKUMPR.web_efld_opt(_okno,,'mark=0,enable=1,editable=1',OBIEGI,'KH_PROJ')
   ?};
   exec('proj_spr_wt','obiegi_proj')
|| exec('proj_spr','obiegi_proj')
?};
1


\bds_rodzwart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed wyświetl dla pola EDOKUMPR.RODZWART
::----------------------------------------------------------------------------------------------------------------------
exec('bds_edokumpr','obiegi_proj');
1


\bds_edokumpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed wyświetl dla pól tabeli EDOKUMPR
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| {? OBIEGI.R='Wewnętrzny'
   || EDOKUMPR.efld_opt(EDOKUMPR.win_edit('?'),'mark=0,enable=1',UD_POM,'JORG');
      EDOKUMPR.efld_opt(EDOKUMPR.win_edit('?'),'mark=0,enable=1',OBIEGI,'UD_SKL');
      EDOKUMPR.efld_opt(EDOKUMPR.win_edit('?'),'mark=0,enable=0',OBIEGI,'KH_PROJ')
   || EDOKUMPR.efld_opt(EDOKUMPR.win_edit('?'),'mark=0,enable=0',UD_POM,'JORG');
      EDOKUMPR.efld_opt(EDOKUMPR.win_edit('?'),'mark=0,enable=0',OBIEGI,'UD_SKL');
      EDOKUMPR.efld_opt(EDOKUMPR.win_edit('?'),'mark=0,enable=1',OBIEGI,'KH_PROJ')
   ?}
|| 1
?}


\ini_pola_edpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Inicjowanie pól tabeli EDOKUMPR
::   WE: _a - akronim okienka redagowania
::----------------------------------------------------------------------------------------------------------------------
{? OBIEGI.R='Wewnętrzny'
|| EDOKUMPR.web_efld_init(_a,,'mark=0,enable=1,editable=1',UD_POM,'JORG');
   EDOKUMPR.web_efld_init(_a,,'mark=0,enable=1,editable=1',OBIEGI,'UD_SKL');
   EDOKUMPR.web_efld_init(_a,,'mark=0,enable=0,editable=0',OBIEGI,'KH_PROJ')
|| EDOKUMPR.web_efld_init(_a,,'mark=0,enable=0,editable=0',UD_POM,'JORG');
   EDOKUMPR.web_efld_init(_a,,'mark=0,enable=0,editable=0',OBIEGI,'UD_SKL');
   EDOKUMPR.web_efld_init(_a,,'mark=0,enable=1,editable=1',OBIEGI,'KH_PROJ')
?};
EDOKUM.use('skid_v'+(EDOKUMPR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix();
{? EDOKUMPR.EDOKUM().TYP().PROJRODZ=1
|| EDOKUMPR.web_efld_init(_a,,'mark=0,enable=1,editable=1',OBIEGI,'R')
|| EDOKUMPR.web_efld_init(_a,,'mark=0,enable=1,editable=0',OBIEGI,'R')
?}


\edokumpr_be_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed redakcją pola EDOKUMPR.PROJEKT
::----------------------------------------------------------------------------------------------------------------------
exec('filtr_projekty','obiegi_proj');
1


\edokumpr_ae_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola EDOKUMPR.PROJEKT
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMPR.PROJEKT
|| OBIEGI.UD_SKL:=EDOKUMPR.PROJEKT().UD_SKL;
   UD_POM.JORG:=EDOKUMPR.PROJEKT().UD_SKL().SYMBOL;
   OBIEGI.SZEF:=EDOKUMPR.PROJEKT().SZEF
?};
1


\del_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Usuwanie projektów dla dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| EDOKUMPR.web_cntx_save();
   _us:="{? EDOKUMPR.web_cntx_load() & _a
         || exec('del_proj1','obiegi_proj');
            EDOKUMPR.web_refresh(web_top_win(0))
         ?}";
   web_ask(_us,'Usunąć projekt dla dokumentu?'@,FUN.TYT)
|| {? FUN.ask('Usunąć projekt dla dokumentu?'@)
   || exec('del_proj1','obiegi_proj')
   ?}
?}


\del_proj1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Usuwanie projektów dla dokumentów w obiegu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
do();
EPODZ.cntx_psh(); EPODZ.use('skid_j'+(EDOKUMPR.name()+2));
EPODZ.index('EDOKUMPR'); EPODZ.prefix(EDOKUMPR.EDOKUM,EDOKUMPR.ref());
{? EPODZ.first()
|| {! |?
      _delr:=EPODZ.del(,1);
      {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
   !}
?};
{? EDOKUMPR.del(,1)=0 || undo() ?};
EPODZ.cntx_pop();
end()


\filtr_projekty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Ustawianie filtra dla tabeli PROJEKTY
::----------------------------------------------------------------------------------------------------------------------
PROJEKTY.index('SYM'); PROJEKTY.prefix(); PROJEKTY.f_clear();
PROJEKTY.f_set('SYM,ID_KSG,NAZWA',
               'join PROJTYPY using(PROJEKTY.T,PROJTYPY.REFERENCE)',
               'PROJTYPY.R=\':_a\' and '+
               '(:_b=\'\' or PROJEKTY.SZEF=:_b) and '+
               '(:_c=\'\' or PROJEKTY.UD_SKL=:_c) and '+
               '(:_d=\'\' or PROJEKTY.KH=:_d)'
               ,
               OBIEGI.R,OBIEGI.SZEF,OBIEGI.UD_SKL,OBIEGI.KH_PROJ
              )


\ae_edp_szefp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola OBIEGI.SZEF
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| exec('proj_spr_wt','obiegi_proj')
|| exec('proj_spr','obiegi_proj')
?};
1


\ae_edp_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola OBIEGI.KH_PROJ
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| exec('proj_spr_wt','obiegi_proj')
|| exec('proj_spr','obiegi_proj')
?};
1


\proj_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Sprawdza czy w polu EDOKUMPR.PROJEKT jest projekt zgodny z filtrem
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMPR.PROJEKT
|| exec('filtr_projekty','obiegi_proj');
   {? ~PROJEKTY.f_seek(EDOKUMPR.PROJEKT,PROJEKTY.name()) || EDOKUMPR.PROJEKT:=null ?}
?};
1


\proj_spr_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Sprawdza czy w polu OBIEGI.PROJEKT jest projekt zgodny z filtrem - WEBTERM
::----------------------------------------------------------------------------------------------------------------------
{? OBIEGI.PROJEKT<>''
|| exec('tab_proj','obiegi_proj',OBIEGI.R,OBIEGI.SZEF,OBIEGI.UD_SKL,OBIEGI.KH_PROJ);
   _tcid:=app_info('web_tcid');
   _tabid:=app_info('web_tabid');
   TAB_WT.index('INDEX05'); TAB_WT.prefix(_tcid,_tabid,9,OBIEGI.PROJEKT,);
   {? ~TAB_WT.first()
   || OBIEGI.PROJEKT:=''; EDOKUMPR.PROJEKT:=null
   ?}
?};
1


\rkprz_edokumpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Rekord przed dla tabeli EDOKUMPR
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.SZEF:=EDOKUMPR.PROJEKT().SZEF;
OBIEGI.R:=EDOKUMPR.PROJEKT().T().R;
OBIEGI.UD_SKL:=EDOKUMPR.PROJEKT().UD_SKL;
OBIEGI.KH_PROJ:=EDOKUMPR.PROJEKT().KH;
0


\akt_tree_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Aktualizacja drzew projektów dla bieżącego rekordu w tabeli EDOKUMPR
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMPR.PROJEKT & EDOKUMPR.PROJEKT().SYM<>''
|| UD_SKL.cntx_psh(); UD_SKL.index('SYMBOL'); UD_SKL.prefix();
   UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_DEF.prefix();
   UD_TYP.cntx_psh(); UD_TYP.index('SYMBOL'); UD_TYP.prefix();
   UD_SCH.cntx_psh(); UD_SCH.index('SYMBOL'); UD_SCH.prefix();
   ETYPY.cntx_psh(); ETYPY.index('UNIK'); ETYPY.prefix();
   EDOKUM.cntx_psh(); EDOKUM.use('skid_v'+(EDOKUMPR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix();
   EDOKUMPR.EDOKUM().TYP();
   ETYPYPOD.cntx_psh(); ETYPYPOD.index('UNIK'); ETYPYPOD.prefix(ETYPY.ref());
   {? ETYPYPOD.first()
   || {! |?
         _root:=null;
         {? ETYPYPOD.TYPDOL='N' | ETYPYPOD.TYPDOL='F'
         || _nadrz:={? ETYPYPOD.TYPDOL='N'
                    || ETYPYPOD.ROOT
                    || ($(ETYPYPOD.FORMULA().FORMULA))()
                    ?};
            {? type_of(_nadrz)=type_of(null) & _nadrz
            || UD_DEF.index('PODTEC'); UD_DEF.prefix(ETYPYPOD.SCH_PROJ,_nadrz);
               {? UD_DEF.first() || _root:=UD_DEF.ref() ?}
            |? type_of(_nadrz)=type_of('') & _nadrz<>''
            || UD_DEF.index('SCHSYM'); UD_DEF.prefix(ETYPYPOD.SCH_PROJ,_nadrz,);
               {? UD_DEF.first() || _root:=UD_DEF.ref() ?}
            ?}
         ?};
         _skl:=null;
         UD_SKL.prefix(ETYPYPOD.SCH_PROJ().UD_TYP,PROJEKTY.SYM,);
         {? UD_SKL.first()
         || _skl:=UD_SKL.ref()
         || UD_SKL.blank(1);
            UD_SKL.UD_TYP:=ETYPYPOD.SCH_PROJ().UD_TYP;
            UD_SKL.SYMBOL:=PROJEKTY.SYM;
            UD_SKL.OPIS:=PROJEKTY.NAZWA;
            UD_SKL.LISTA:='T';
            UD_SKL.AKTYWNY:='T';
            {? UD_SKL.add(1)
            || _skl:=UD_SKL.ref()
            ?}
         ?};
         {? _skl
         || UD_DEF.index('PODTEC'); UD_DEF.prefix(ETYPYPOD.SCH_PROJ,UD_SKL.ref());
            {? ~UD_DEF.first()
            || UD_DEF.blank(1);
               UD_DEF.UD_SCH:=ETYPYPOD.SCH_PROJ;
               UD_DEF.UD_DEF:=#_root;
               UD_DEF.UD_SQL:=$_root;
               UD_DEF.UD_SKL:=UD_SKL.ref();
               UD_DEF.SYMBOL:=UD_SKL.SYMBOL;
               UD_DEF.OPIS:=UD_SKL.OPIS;
               UD_DEF.add(1)
            ?}
         ?};
         ETYPYPOD.next()
      !}
   ?};
   ETYPYPOD.cntx_pop(); ETYPY.cntx_pop(); EDOKUM.cntx_pop(); UD_SCH.cntx_pop(); UD_TYP.cntx_pop();
   UD_DEF.cntx_pop(); UD_SKL.cntx_pop()
?};
1


\nal_podz_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Naliczanie podziałów dla projektów pod dokumentem w obiegu (dla bieżącego EDOKUMPR)
::----------------------------------------------------------------------------------------------------------------------
do();
EPODZ.cntx_psh(); EPODZ.use('skid_j'+(EDOKUMPR.name()+2));
EPODZ.index('EDOKUMPR'); EPODZ.prefix(EDOKUMPR.EDOKUM,EDOKUMPR.ref());
{? EPODZ.first()
|| {! |?
      _delr:=EPODZ.del(,1);
      {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
   !}
?};
SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ');
UD_DEF.cntx_psh(); UD_DEF.index('PODTEC'); UD_DEF.prefix();
SKID_MBP.cntx_psh(); SKID_MBP.index('LP'); SKID_MBP.prefix();
EDOKUM.cntx_psh(); EDOKUM.use('skid_v'+(EDOKUMPR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix();
EDOKUMPR.EDOKUM().TYP();
ETYPYDTP.cntx_psh(); ETYPYDTP.index('UNIK'); ETYPYDTP.prefix(ETYPY.ref());
{? ETYPYDTP.first()
|| {! |?
      SKID_MBP.prefix(ETYPYDTP.SKID_MB,1);
      _dalej:=SKID_MBP.first();
      {? _dalej
      || SKID_MBP.prefix();
         EPODZ.blank();
         EPODZ.EDOKUM:=EDOKUMPR.EDOKUM;
         EPODZ.EZAPOT:=EPODZ.EDOKRZAP:=null;
         EPODZ.EDOKUMPR:=EDOKUMPR.ref();
         EPODZ.USERS:=OPERATOR.USER;
         EPODZ.SKID_MB:=ETYPYDTP.SKID_MB;
         _dalej:=0;
         _skl:={? ETYPYDTP.POZB_RW='F'
               || ($(ETYPYDTP.POZB_F().FORMULA))()
               || EDOKUMPR.RODZWART
               ?};
          {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
          || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
             {? UD_DEF.first()
             || _dalej:=1;
                EPODZ.PBUD:=UD_DEF.UD_SKL
             ?}
          |? type_of(_skl)=type_of('') & _skl<>''
          || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
             {? UD_DEF.first()
             || _dalej:=1;
                EPODZ.PBUD:=UD_DEF.UD_SKL
             ?}
          ?};
          SKID_MBP.prefix(ETYPYDTP.SKID_MB,2);
          {? _dalej & SKID_MBP.first()
          || _dalej:=0;
             _skl:={? ETYPYDTP.WYM2_RW='F'
                   || ($(ETYPYDTP.WYM2_F().FORMULA))()
                   || EDOKUMPR.PROJEKT().UD_SKL
                   ?};
             {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
             || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
                {? UD_DEF.first()
                || _dalej:=1;
                   EPODZ.JORG:=UD_DEF.UD_SKL
                ?}
             |? type_of(_skl)=type_of('') & _skl<>''
             || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
                {? UD_DEF.first()
                || _dalej:=1;
                   EPODZ.JORG:=UD_DEF.UD_SKL
                ?}
             ?}
          ?};
          SKID_MBP.prefix(ETYPYDTP.SKID_MB,3);
          {? _dalej & SKID_MBP.first()
          || _dalej:=0;
             _skl:={? ETYPYDTP.WYM3_RW='F'
                   || ($(ETYPYDTP.WYM3_F().FORMULA))()
                   || EDOKUMPR.PROJEKT().UD_SKL
                   ?};
             {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
             || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
                {? UD_DEF.first()
                || _dalej:=1;
                   EPODZ.OKOSZ:=UD_DEF.UD_SKL
                ?}
             |? type_of(_skl)=type_of('') & _skl<>''
             || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
                {? UD_DEF.first()
                || _dalej:=1;
                   EPODZ.OKOSZ:=UD_DEF.UD_SKL
                ?}
             ?}
          ?};
          SKID_MBP.prefix(ETYPYDTP.SKID_MB,4);
          {? _dalej & SKID_MBP.first()
          || _dalej:=0;
             _skl:={? ETYPYDTP.WYM4_RW='F'
                   || ($(ETYPYDTP.WYM4_F().FORMULA))()
                   || EDOKUMPR.PROJEKT().UD_SKL
                   ?};
             {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
             || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
                {? UD_DEF.first()
                || _dalej:=1;
                   EPODZ.WYM4:=UD_DEF.UD_SKL
                ?}
             |? type_of(_skl)=type_of('') & _skl<>''
             || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
                {? UD_DEF.first()
                || _dalej:=1;
                   EPODZ.WYM4:=UD_DEF.UD_SKL
                ?}
             ?}
          ?};
          SKID_MBP.prefix(ETYPYDTP.SKID_MB,5);
          {? _dalej & SKID_MBP.first()
          || _dalej:=0;
             _skl:={? ETYPYDTP.WYM5_RW='F'
                   || ($(ETYPYDTP.WYM5_F().FORMULA))()
                   || EDOKUMPR.PROJEKT().UD_SKL
                   ?};
             {? type_of(_skl)=type_of(SYSLOG.ref()) & _skl
             || UD_DEF.index('PODTEC'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl);
                {? UD_DEF.first()
                || _dalej:=1;
                   EPODZ.WYM5:=UD_DEF.UD_SKL
                ?}
             |? type_of(_skl)=type_of('') & _skl<>''
             || UD_DEF.index('SCHSYM'); UD_DEF.prefix(SKID_MBP.UD_SCH,_skl,);
                {? UD_DEF.first()
                || _dalej:=1;
                   EPODZ.WYM5:=UD_DEF.UD_SKL
                ?}
             ?}
          ?};
          {? _dalej
          || _prec:=2;
             SKIDXDUD.prefix(EPODZ.PBUD);
             {? SKIDXDUD.first()
             || _il:=SKIDXDUD.WART_IL='I'; _prec:=SKIDXDUD.PREC
             || _il:=0
             ?};
             {? _il
             || EPODZ.WART:=EDOKUMPR.WART$_prec
             || EPODZ.WART:=EDOKUMPR.WART$2
             ?};
             {? ~EPODZ.WAL || EPODZ.WAL:=EDOKUMPR.WAL ?};
             EPODZ.add()
          ?}
      ?};
      ETYPYDTP.next()
   !}
?};
ETYPYDTP.cntx_pop();
EPODZ.cntx_pop(); EDOKUM.cntx_pop(); SKID_MBP.cntx_pop(); UD_DEF.cntx_pop(); SKIDXDUD.cntx_pop();
end();
1


\beprojsjoproj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed redakcją pola PROJTYPY.SJO
::----------------------------------------------------------------------------------------------------------------------
UD_TYP.index('SYMBOL'); UD_TYP.prefix('PODZORG',); UD_TYP.first();
UD_SCH.win_dict('SLO');
1


\bds_tproj_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed wyświetl dla pola PROJTYPY.R
::----------------------------------------------------------------------------------------------------------------------
{? PROJTYPY.R='Wewnętrzny'
|| PROJTYPY.efld_opt(PROJTYPY.win_edit('?'),'mark=0,enable=1',,'SJO')
|| PROJTYPY.efld_opt(PROJTYPY.win_edit('?'),'mark=0,enable=0',,'SJO')
?};
1


\aed_tproj_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola PROJTYPY.R
::----------------------------------------------------------------------------------------------------------------------
exec('bds_tproj_r','obiegi_proj');
{? PROJTYPY.R='Zewnętrzny' || PROJTYPY.SJO:=null ?};
1


\projekty_jorg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Jednostka organizacyjna projektu - ustawianie obsługi
::----------------------------------------------------------------------------------------------------------------------
exec('set_fml','#field',UD_POM,'OKOSZ',
        "UD_POM.OKOSZ:={? PROJEKTY.UD_SKL
                       || PROJEKTY.UD_SKL().SYMBOL
                       || ''
                       ?}; ~~",
        "1",
        "_typ:=_sch:=null;
         {? PROJEKTY.T().SJO
         || _typ:=PROJEKTY.T().SJO().UD_TYP; _sch:=UD_SCH.ref()
         || UD_TYP.index('SYMBOL'); UD_TYP.prefix('PODZORG',);
            {? UD_TYP.first()
            || _typ:=UD_TYP.ref();
               UD_SCH.index('SYMBOL'); UD_SCH.prefix(UD_TYP.ref(),'STRORG',);
               {? UD_SCH.first() || _sch:=UD_SCH.ref() ?}
            ?}
         ?};
         exec('ud_def_symbol_f3','schemat',_typ,_sch)",
        "{? fld()='' || PROJEKTY.UD_SKL:=null; return(1) ?};
         _sch:=null;
         {? PROJEKTY.T().SJO
         || _sch:=PROJEKTY.T().SJO
         || UD_TYP.index('SYMBOL'); UD_TYP.prefix('PODZORG',);
            {? UD_TYP.first()
            || UD_SCH.index('SYMBOL'); UD_SCH.prefix(UD_TYP.ref(),'STRORG',);
               {? UD_SCH.first() || _sch:=UD_SCH.ref() ?}
            ?}
         ?};
         _ref:=exec('ud_def_symbol_ae','schemat',_sch);
         {? _ref
         || PROJEKTY.UD_SKL:=_ref;
            fld(PROJEKTY.UD_SKL().SYMBOL)
         ?};
         1
        "
     )


\bobs_projekty_jt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed obsługą projektów dla EDOKUM
::----------------------------------------------------------------------------------------------------------------------
exec('set_var_proj','obiegi_proj');
_fget:={? EDOKUM.f_active() & EDOKUM.sel_size()=0
       || EDOKUM.f_get()
       || EDOKUM.get()
       ?};
{? ~_fget
|| EDOKUMPR.index('DISP'); EDOKUMPR.prefix(null);
   EDOKUMPR.actions('WER','DPU:D',,1)
?};
1


\blok_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Sprawdza, czy można dać akcje edycyjne w projektach pod dokumentami w obiegu
::   WY: 1/0 - czy założona blokada
::----------------------------------------------------------------------------------------------------------------------
_blok:=0;
{? var_pres('zakres')>0 || exec('rkprz1','obiegi',zakres) ?};
{? ~EDOKUM.TYP().OBS_PROJ | EDOKUM.ZAM='T' | EDOKUM.STDEKRD | EDOKUM.REFLISTA<>'' | EDOKUM.ZD_NAG<>'' |
   zakres=5 | zakres=3
|| _blok:=1
|| {? OBIEGI.EDOKOS
   || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      OBIEGI.EDOKOS();
      {? ~(exec('szuk_edokpar','obiegi') & (EDOKPAR.ED_PROJ='R' | EDOKPAR.ED_PROJ='W')) &
         OBIEGI.B_PREL=OBIEGI.EDOKOS().B_PREL
      || _blok:=1
      ?};
      {? ~_blok
      || _stat:=exec('spr_stat','obiegi');
         _blok:=(_stat='T' | _stat='X')
      ?}
   ?}
?};
_blok


\blok_przej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Sprawdza, czy można dać akcje edycyjne w przejazdach pod delegacjami w obiegu
::   WY: 1/0 - czy założona blokada
::----------------------------------------------------------------------------------------------------------------------
_blok:=0;
{? var_pres('zakres')>0 || exec('rkprz1','obiegi',zakres) ?};
{? EDOKUM.ZAM='T' | EDOKUM.STDEKRD | EDOKUM.REFLISTA<>'' | EDOKUM.ZD_NAG<>'' | zakres=5 | zakres=3
|| _blok:=1
|| {? OBIEGI.EDOKOS
   || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      OBIEGI.EDOKOS();
      {? ~(exec('szuk_edokpar','obiegi') & (EDOKPAR.ED_PRZEJ='R' | EDOKPAR.ED_PRZEJ='W')) &
         OBIEGI.B_PREL=OBIEGI.EDOKOS().B_PREL
      || _blok:=1
      ?};
      {? ~_blok
      || _stat:=exec('spr_stat','obiegi');
         _blok:=(_stat='T' | _stat='X')
      ?}
   ?}
?};
_blok


\blok_diety
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Sprawdza, czy można dać akcje edycyjne w dietach pod delegacjami w obiegu
::   WY: 1/0 - czy założona blokada
::----------------------------------------------------------------------------------------------------------------------
_blok:=0;
{? var_pres('zakres')>0 || exec('rkprz1','obiegi',zakres) ?};
{? EDOKUM.ZAM='T' | EDOKUM.STDEKRD | EDOKUM.REFLISTA<>'' | EDOKUM.ZD_NAG<>'' | zakres=5 | zakres=3
|| _blok:=1
|| {? OBIEGI.EDOKOS
   || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      OBIEGI.EDOKOS();
      {? ~(exec('szuk_edokpar','obiegi') & (EDOKPAR.ED_DIET='R' | EDOKPAR.ED_DIET='W')) &
         OBIEGI.B_PREL=OBIEGI.EDOKOS().B_PREL
      || _blok:=1
      ?};
      {? ~_blok
      || _stat:=exec('spr_stat','obiegi');
         _blok:=(_stat='T' | _stat='X')
      ?}
   ?}
?};
_blok


\blok_wyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Sprawdza, czy można dać akcje edycyjne w wydatkach pod delegacjami w obiegu
::   WY: 1/0 - czy założona blokada
::----------------------------------------------------------------------------------------------------------------------
_blok:=0;
{? var_pres('zakres')>0 || exec('rkprz1','obiegi',zakres) ?};
{? EDOKUM.ZAM='T' | EDOKUM.STDEKRD | EDOKUM.REFLISTA<>'' | EDOKUM.ZD_NAG<>'' | zakres=5 | zakres=3
|| _blok:=1
|| {? OBIEGI.EDOKOS
   || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      OBIEGI.EDOKOS();
      {? ~(exec('szuk_edokpar','obiegi') & (EDOKPAR.ED_WYD='R' | EDOKPAR.ED_WYD='W')) &
         OBIEGI.B_PREL=OBIEGI.EDOKOS().B_PREL
      || _blok:=1
      ?};
      {? ~_blok
      || _stat:=exec('spr_stat','obiegi');
         _blok:=(_stat='T' | _stat='X')
      ?}
   ?}
?};
_blok


\edokumpr_dsp_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Na wyświetl dla tabeli EDOKUMPR (WWW)
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.SZEF:=OBIEGI.KH_PROJ:=OBIEGI.UD_SKL:=OBIEGI.KH_PROJ:=null;
exec('rkprz_edokumpr','obiegi_proj');
UD_POM.PBUD:={? EDOKUMPR.RODZWART || EDOKUMPR.RODZWART().SYMBOL || '' ?};
UD_POM.JORG:={? OBIEGI.UD_SKL || OBIEGI.UD_SKL().SYMBOL || '' ?};
EDOKUM.use('skid_v'+(EDOKUMPR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMPR.EDOKUM();
_okno:={? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg delegacji'
       || 'REDWTDSD'
       || 'REDWTDS'
       ?};
exec('ini_pola_edpr','obiegi_proj',_okno);
EDOKUMPR.web_display(_okno,,"EDOKUMPR.web_eclose()");
1


\bobs_red_edokpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed obsługą okienka redakcji tabeli EDOKUMPR (WWW)
::----------------------------------------------------------------------------------------------------------------------
_get:=web_params_get();
web_params_set(_get);
exec('env_wt','b_proces');
exec('FUN','#object');
1


\bobs_ud_def_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed obsługą dla okienka atrybutu typu U (UD_DEF) - webterm
::----------------------------------------------------------------------------------------------------------------------
EDOKUMPR.web_cntx_load();
EDOKUM.use('skid_v'+(EDOKUMPR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMPR.EDOKUM();
exec('env_edokum_get1','obiegi')


\bobs_edokumpr_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed obsługą tabeli EDOKUMPR (WWW)
::----------------------------------------------------------------------------------------------------------------------
{? web_top_kind()='s' || EDOKUM.web_cntx_load() ?};
1


\bds_edokumpr_pbud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed wyświetl dla pola UD_POM.PBUD
::----------------------------------------------------------------------------------------------------------------------
~~


\f3_edokumpr_pbud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: F3 dla pola UD_POM.PBUD
::----------------------------------------------------------------------------------------------------------------------
wart_wym:=UD_POM.PBUD;
EDOKUM.use('skid_v'+(EDOKUMPR.name()+2));
{? EDOKUMPR.EDOKUM
|| EDOKUMPR.EDOKUM().TYP().PROJ_RW().UD_TYP();
   tab_obs:='EDOKUMPR';
   EDOKUMPR.web_cntx_save();
   exec('tab_form','obiegi','UD_DEF','wyb_ud_def_edp1','obiegi_proj','bobs_ud_def_pr','obiegi_proj');
   exec('ud_def_symbol_f3','schemat',EDOKUMPR.EDOKUM().TYP().PROJ_RW().UD_TYP,UD_SCH.ref())
?}


\wyb_ud_def_edp1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Formuła na wybór rekordu w UD_DEF - webterm
::----------------------------------------------------------------------------------------------------------------------
EDOKUMPR.web_cntx_load();
EDOKUM.use('skid_v'+(EDOKUMPR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMPR.EDOKUM();
UD_POM.PBUD:=UD_DEF.SYMBOL;
UD_DEF.web_close();
web_top_fld_update(1,1)


\aed_edokumpr_pbud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola UD_POM.PBUD
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
EDOKUM.use('skid_v'+(EDOKUMPR.name()+2));
{? EDOKUMPR.EDOKUM || EDOKUMPR.EDOKUM() ?};
{? UD_POM.PBUD=''
|| FUN.info('Nie wybrano rodzaju wartości.'@); EDOKUMPR.RODZWART:=null; _zwrot:=0
|| wart_wym:=UD_POM.PBUD;
   EDOKUMPR.EDOKUM().TYP().PROJ_RW().UD_TYP();
   _ref:=exec('ud_def_symbol_ae','schemat',UD_SCH.ref());
   {? _ref
   || EDOKUMPR.RODZWART:=_ref;
      UD_POM.PBUD:=EDOKUMPR.RODZWART().SYMBOL;
      exec('ae_rodz_war','obiegi_proj');
      _podp:=0;
      _par:=web_global_params_get();
      {? type_of(_par)>100 & var_pres('akcjawyw',_par)>0 & _par.akcjawyw='D'
      || _podp:=1
      ?};
      {? EDOKUMPR.WART=0 & _podp
      || _ile_juz:=0;
         SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ');
         EDOKUMPR.cntx_psh(); EDOKUMPR.index('DISP'); EDOKUMPR.prefix(EDOKUM.ref());
         {? EDOKUMPR.first()
         || {! |?
               SKIDXDUD.prefix(EDOKUMPR.RODZWART);
               {? SKIDXDUD.first() & SKIDXDUD.WART_IL='W'
               || _ile_juz+=EDOKUMPR.WART
               ?};
               EDOKUMPR.next()
            !}
         ?};

         EDOKUMPR.cntx_pop();
         SKIDXDUD.cntx_pop();
         EDOKUMPR.WART:={? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg delegacji'
                        || 100-_ile_juz
                        || EDOKUM.NETTO-_ile_juz
                        ?}
      ?}
   || _zwrot:=0
   ?}
?};
_zwrot


\bds_edokumpr_jorg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Przed wyświetl dla pola UD_POM.JORG
::----------------------------------------------------------------------------------------------------------------------
~~


\f3_edokumpr_jorg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: F3 dla pola UD_POM.JORG
::----------------------------------------------------------------------------------------------------------------------
wart_wym:=UD_POM.JORG;
EDOKUM.use('skid_v'+(EDOKUMPR.name()+2));
{? EDOKUMPR.EDOKUM
|| _typ:=_sch:=null;
   {? EDOKUMPR.EDOKUM().TYP().PROJ_SJO
   || EDOKUMPR.EDOKUM().TYP().PROJ_SJO().UD_TYP();
      _typ:=EDOKUMPR.EDOKUM().TYP().PROJ_SJO().UD_TYP;
      _sch:=UD_SCH.ref()
   || UD_TYP.index('SYMBOL'); UD_TYP.prefix('PODZORG',);
      {? UD_TYP.first()
      || _typ:=UD_TYP.ref();
         UD_SCH.index('SYMBOL'); UD_SCH.prefix(UD_TYP.ref(),'STRORG',);
         {? UD_SCH.first() || _sch:=UD_SCH.ref() ?}
      ?}
   ?};
   tab_obs:='EDOKUMPR';
   EDOKUMPR.web_cntx_save();
   exec('tab_form','obiegi','UD_DEF','wyb_ud_def_edp2','obiegi_proj','bobs_ud_def_pr','obiegi_proj');
   exec('ud_def_symbol_f3','schemat',_typ,_sch)
?}


\aed_edokumpr_jorg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola UD_POM.JORG
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.use('skid_v'+(EDOKUMPR.name()+2));
{? EDOKUMPR.EDOKUM || EDOKUMPR.EDOKUM() ?};
{? UD_POM.JORG=''
|| OBIEGI.UD_SKL:=null
|| _sch:=null;
   {? EDOKUMPR.EDOKUM().TYP().PROJ_SJO
   || _sch:=EDOKUMPR.EDOKUM().TYP().PROJ_SJO
   || UD_TYP.index('SYMBOL'); UD_TYP.prefix('PODZORG',);
      {? UD_TYP.first()
      || UD_SCH.index('SYMBOL'); UD_SCH.prefix(UD_TYP.ref(),'STRORG',);
         {? UD_SCH.first() || _sch:=UD_SCH.ref() ?}
      ?}
   ?};
   wart_wym:=UD_POM.JORG;
   _ref:=exec('ud_def_symbol_ae','schemat',_sch);
   {? _ref
   || OBIEGI.UD_SKL:=_ref; UD_POM.JORG:=OBIEGI.UD_SKL().SYMBOL
   || OBIEGI.UD_SKL:=null; UD_POM.JORG:=''
   ?}
?};
exec('proj_spr_wt','obiegi_proj');
1


\wyb_ud_def_edp2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Formuła na wybór rekordu w UD_DEF - webterm
::----------------------------------------------------------------------------------------------------------------------
EDOKUMPR.web_cntx_load();
EDOKUM.use('skid_v'+(EDOKUMPR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMPR.EDOKUM();
UD_POM.JORG:=UD_DEF.SYMBOL;
UD_DEF.web_close();
web_top_fld_update(1,1)


\f3_projekt_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: F3 dla pola OBIEGI.PROJEKT
::----------------------------------------------------------------------------------------------------------------------
exec('tab_proj','obiegi_proj',OBIEGI.R,OBIEGI.SZEF,OBIEGI.UD_SKL,OBIEGI.KH_PROJ);
EDOKUMPR.web_cntx_save();
TAB_WT.web_select('WER09');
1


\ae_projekt_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola OBIEGI.PROJEKT
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? OBIEGI.PROJEKT=''
|| EDOKUMPR.PROJEKT:=null
|| exec('tab_proj','obiegi_proj',OBIEGI.R,OBIEGI.SZEF,OBIEGI.UD_SKL,OBIEGI.KH_PROJ);
   _tcid:=app_info('web_tcid');
   _tabid:=app_info('web_tabid');
   TAB_WT.index('INDEX05'); TAB_WT.prefix(_tcid,_tabid,9,OBIEGI.PROJEKT);
   {? ~TAB_WT.first()
   || FUN.info('Nie znaleziono projektu o podanym symbolu.'@); _zwrot:=0
   || OBIEGI.PROJEKT:=TAB_WT.NAZ;
      PROJEKTY.index('SYM'); PROJEKTY.prefix(OBIEGI.PROJEKT,);
      {? PROJEKTY.first() || EDOKUMPR.PROJEKT:=PROJEKTY.ref() ?}
   ?}
?};
_zwrot


\tab_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Napełnia tabelkę projektami
::   WE: _a - rodzaj projektów (zewnętrzne czy wewnętrzne)
::       _b - szef projektu (OSOBA.ref() lub null)
::       _c - jednostka organizacyjna (UD_SKL.ref() lub null)
::       _d - kontrahent (KH.ref() lub null)
::----------------------------------------------------------------------------------------------------------------------
exec('tab_form','obiegi','TAB_WT','wyb_projekt_wt','obiegi_proj','','');
_tcid:=app_info('web_tcid');
_mbid:=app_info('web_mbid');
_sesid:=app_info('web_sesid');
_tabid:=app_info('web_tabid');
TAB_WT.index('INDEX01'); TAB_WT.prefix(_tcid,_tabid,9);
PROJEKTY.index('SYM'); PROJEKTY.prefix(); PROJEKTY.f_clear();
PROJEKTY.f_set('SYM,ID_KSG,NAZWA',
               'join PROJTYPY using(PROJEKTY.T,PROJTYPY.REFERENCE)',
               'PROJTYPY.R=\':_a\' and '+
               '(:_b=\'\' or PROJEKTY.SZEF=:_b) and '+
               '(:_c=\'\' or PROJEKTY.UD_SKL=:_c) and '+
               '(:_d=\'\' or PROJEKTY.KH=:_d)'
               ,
               _a,_b,_c,_d
              );
do();
{? TAB_WT.first()
|| {! |?
      _delr:=TAB_WT.del(,1);
      {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
   !}
?};
{? PROJEKTY.f_first()
|| {! |?
      TAB_WT.blank(1);
      TAB_WT.SESID:=_sesid;
      TAB_WT.MBID:=_mbid;
      TAB_WT.TCID:=_tcid;
      TAB_WT.TABID:=_tabid;
      TAB_WT.TYP:=9;
      TAB_WT.NAZ:=PROJEKTY.SYM;
      TAB_WT.INT01:=PROJEKTY.ID_KSG;
      TAB_WT.OPIS:=PROJEKTY.NAZWA;
      {? PROJEKTY.KH
      || TAB_WT.STR01:=PROJEKTY.KH().SKR;
         TAB_WT.STR02:=PROJEKTY.KH().NAZ
      ?};
      {? PROJEKTY.UD_SKL
      || TAB_WT.STR03:=PROJEKTY.UD_SKL().SYMBOL;
         TAB_WT.STR04:=PROJEKTY.UD_SKL().OPIS
      ?};
      TAB_WT.add();
      PROJEKTY.f_next()
   !}
?};
end();
TAB_WT.index('INDEX05'); TAB_WT.prefix(_tcid,_tabid,9)


\wyb_projekt_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Wybór projektu w WEBTERMIE
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUMPR.web_cntx_load()
|| OBIEGI.PROJEKT:=TAB_WT.NAZ;
   TAB_WT.web_close();
   web_top_fld_update(1,1)
?};
1


\spr_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Sprawdza projekty dla dokumentu w obiegu
::----------------------------------------------------------------------------------------------------------------------
_akcept:=1;
EDOKOS.cntx_psh(); EDOKUM.cntx_psh(); OBIEGI.EDOKOS();
EDOKUMPR.cntx_psh(); EDOKUMPR.use('skid_i'+(EDOKUM.name()+2)); EDOKUMPR.index('DISP'); EDOKUMPR.prefix(EDOKUM.ref());
{? exec('szuk_edokpar','obiegi') & EDOKPAR.ED_PROJ='W' & ~EDOKUMPR.first()
|| _kom:='Nie wybrano projektów dla dokumentu.';
   exec('add_kom','obiegi',0,1,_kom);
   _akcept:=0
?};
EDOKOS.cntx_pop(); EDOKUM.cntx_pop(); EDOKUMPR.cntx_pop();
_akcept


\ae_wart_edokumpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.14]
:: OPIS: Po redakcji pola EDOKUMPR.WART
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| EDOKUM.use('skid_v'+(EDOKUMPR.name()+2)); EDOKUM.index('ID'); EDOKUM.prefix(); EDOKUMPR.EDOKUM()
?};
{? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg delegacji'
|| EDOKUMPR.WART:=EDOKUMPR.WART$2
|| _jedn:=1;
   RS.index('RS'); RS.prefix();
   {? app_info('web_sesid')<>'' || exec('czytaj','#stalesys',,FINFO) ?};
   {? EDOKUMPR.WAL<>0 & FINFO.SLWAL & RS.find_key(FINFO.SLWAL().SLU().WZ)
   || {? ($(RS.TAB+'.prefix();'+RS.TAB+'.blank(1);'+RS.TAB+'.'+RS.KOD+':=EDOKUMPR.WAL().KOD;'+RS.TAB
         +'.find_rec'))()
      || _jedn:=($(RS.TAB+'.J'))();
         {? _jedn=0 || _jedn:=1 ?}
      ?}
   ?};
   _warunek:={? app_info('web_sesid')<>''
             || EDOKUMPR.WART
             || cur_afld()='WART' & fld()
             ?};
   {? _warunek
   || {? EDOKUMPR.JM
      || _prec:=6;
         {? UD_TYP.index('SYMBOL'); UD_TYP.prefix('POZ_BUD','POZ_BUD'); UD_TYP.first()
         || {? UD_SKL.prefix(UD_TYP.ref(),UD_POM.PBUD,UD_POM.PBUD); UD_SKL.first()
            || SKIDXDUD.index('POZ'); SKIDXDUD.prefix(UD_SKL.ref());
               {? SKIDXDUD.first() || _prec:=SKIDXDUD.PREC ?}
            ?}
         ?};
         EDOKUMPR.WART:=EDOKUMPR.WART$_prec
      || EDOKUMPR.WART:=EDOKUMPR.WART$2
      ?}
   ?}
?};
1


\dsp_sumproj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Na wyśwetl dla pola OBIEGI.SUM_PROJ
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.SUM_PROJ:=OBIEGI.POZ_PROJ:=0;
SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ');
EDOKUMPR.cntx_psh();
_val:=0;
{? EDOKUMPR.f_active()
|| {? EDOKUMPR.f_first()
   || {! |?
         SKIDXDUD.prefix(EDOKUMPR.RODZWART);
         {? SKIDXDUD.first() & SKIDXDUD.WART_IL='W' || _val+=EDOKUMPR.WART ?};
         EDOKUMPR.f_next()
      !}
   ?}
|? EDOKUM.get()
|| EDOKUMPR.index('DISP'); EDOKUMPR.prefix(EDOKUM.ref());
   {? EDOKUMPR.first()
   || {! |?
         SKIDXDUD.prefix(EDOKUMPR.RODZWART);
         {? SKIDXDUD.first() & SKIDXDUD.WART_IL='W' || _val+=EDOKUMPR.WART ?};
         EDOKUMPR.next()
      !}
   ?}
?};
OBIEGI.SUM_PROJ:=_val;
OBIEGI.POZ_PROJ:={? EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg delegacji'
                 || 100-OBIEGI.SUM_PROJ
                 || EDOKUM.NETTO-OBIEGI.SUM_PROJ
                 ?};
EDOKUMPR.cntx_pop(); SKIDXDUD.cntx_pop();
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:20 40a87bc88ebc6d33642ae4b64f2d2140c414ac2a4a87fcb8f67e993cadc889c1e72bd348a80dcd2b8ad254784a207c15769ad4613169985fa942405ccf0716b51b6a95798df550e91b91fa97930d996dbef29ff7265df3154205b85652ad8846845c81ab6ef9846401e1a66ad12888c14bfe42d7c2d03b87861d24e76a924b90

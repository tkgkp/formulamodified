:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: pos.fml
:: Utworzony: 08.06.2018
:: Autor: Markus
:: Systemy: Merit
::======================================================================================================================
:: Zawartość: POS for Merit
::======================================================================================================================

\pos_translate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Tablica tłumaczeń przycisków dla posa
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('elements_table','#desktop');
_tab.blank(); _tab.ID_SYS:='summary_02@pos1'; _tab.NAME:='Anuluj dokument'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='summary_07@pos1'; _tab.NAME:='Płatność kartą'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='summary_10@pos1'; _tab.NAME:='Zawieś dokument'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='summary_08@pos1'; _tab.NAME:='Płatność gotówką'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='summary_11@pos1'; _tab.NAME:='Zawieszone dokumenty'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='summary_01@pos1'; _tab.NAME:='Inne formy płatności'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='calc_17@pos1'; _tab.NAME:='Zastosuj'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='calc_28@pos1'; _tab.NAME:='Usuń pozycję'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='calc_25@pos1'; _tab.NAME:='Ilość'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='calc_26@pos1'; _tab.NAME:='Cena'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='calc_27@pos1'; _tab.NAME:='Rabat'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='search_02@pos1'; _tab.NAME:='EAN / Lista tow.'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='Cashback_label@pos1'; _tab.NAME:='Cashback'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='Cashback_max@pos1'; _tab.NAME:='Maksymalna kwota: 50 zł'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='Etykieta_Kontrahent@pos1'; _tab.NAME:='Kontrahent'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='Etykieta_nip@pos1'; _tab.NAME:='NIP'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='pos_faks_type02@pos1'; _tab.NAME:='Typ dokumentu'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='summary_05@pos1'; _tab.NAME:='Ilość pozycji:'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='summary_03@pos1'; _tab.NAME:='Suma:'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='basic_05@pos1'; _tab.NAME:='Zmień'@; _tab.add();
_tab.blank(); _tab.ID_SYS:='basic_09@pos1'; _tab.NAME:='Zmień'@; _tab.add();
_tab


\pos_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wywołanie okienka posa
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_ctr_id:=exec('create','#desktop',SYSLOG,'pos1',_gpr,40);
_win_id:='kontrolka1';
SYSLOG.win_ctr(_ctr_id);
_before:="
   exec('load','#desktop','pos1','pos1.dsk',,,,,,0);
   1";
SYSLOG.control(_win_id,'modal=false',_before);
~~


\last_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Przy wywołaniu okienka posa sprawdza czy jest jakiś wcześniejszy dokument który nie został jeszcze zakończony,
::       jeżeli tak, ustawia go do edycji
::   WE: _a - null, FAKS.ref - wczytanie zawieszonego dokumentu
::   WY: 1 - nowy dok, 0 - dokończenie wcześniejszego dokumentu
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
:: OZ.LASTFAKS - string refa +
:: przy anulowaniu usunąć wartość +
:: przy sprawdzaniu jeżeli nie istnieje lub ... usunąć wartość +
:: przy zatwierdzaniu usunąć wartość +

:: sprawdzić wartość czy istnieje taki dok i dodać wykonanie tego w init +
_env:=params_get().env;
_pos:=_env.POS;

_a:={? _ > 0 || _a || null ?};

__temp_pos_cart_faks_sym:='';

_ret:=null;
{? var_pres('__temp_pos_nag_ref')>0 & _a=null
||
   {? FAKS.seek(__temp_pos_nag_ref)
   ||
      {? FAKS.ZEST_AKC='N' & FAKS.AKC='N'
      ||
         __temp_pos_cart_faks_sym:=FAKS.SYM;
         _ret:=__temp_pos_nag_ref
      ?}
   ?};
   {? _ret=null
   ||
      __temp_pos_cancel:=1;
      exec('cancel_doc','pos')
   ?}
||

   {? (OZ.LASTFAKS<>'' | _a<>null) & _ret=null
   ||
      _dok_ref:={? _a<>null || _a || OZ.LASTFAKS ?};
      _ret:=exec('FindAndGet','#table',FAKS,_dok_ref,,"
         __temp_pos_cart_faks_sym:=FAKS.SYM;
         {? FAKS.WHERE='P' & OZ.POSCZYN='N' | FAKS.WHERE='G' & OZ.POSCZYN='T'
         ||
            {? FAKS.ZEST_AKC='N' & FAKS.AKC='N'
            ||
               {? ST.AR=FAKS.AR & ST.AM=FAKS.AM
               ||
                  return(FAKS.ref())
               ||
                  return('OKR-' + $FAKS.ref())
               ?}
            ||
               return('ZAK-' + $FAKS.ref())
            ?}
         ||
            return(null)
         ?}
      ",null)
   ?};

   _ret1:=4 + $_ret;
   _ret2:=4 - $_ret;

   {? _ret1='OKR-'
   ||
      FUN.info('Dokument (%1) jest z innego okresu.'@[__temp_pos_cart_faks_sym]);
      OZ.get();
      OZ.LASTFAKS:='';
      OZ.put();
      _ret:=null
   |? _ret1='ZAK-'
   ||
      FUN.info('Dokument (%1) jest już zatwierdzony.'@[__temp_pos_cart_faks_sym]);
      OZ.get();
      OZ.LASTFAKS:='';
      OZ.put();
      _ret:=null
   |? type_of(_ret)=7
   ||
      {? FAKS.seek(_ret)
      ||

        {? var_pres('__temp_pos_nag_r_lock') > 0 || obj_del(__temp_pos_nag_r_lock) ?};
        _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
        {? _r_lock<>1
        ||
           _ret:=null;
           exec('who_rlock_faks','faktury_nag');
           return(_ret)
        ||
           __temp_pos_nag_r_lock:=_r_lock
        ?};

         __temp_pos_nag_ref:=FAKS.ref();
         _pos.prev_faks_ref:=FAKS.ref()

      ?}
   ?}
?};

VAR_DEL.delete('__temp_pos_cart_faks_sym');


{? _ret<>null
||

:: Wyłączenie kalkulatora
      exec('calc_hide','pos');
      __temp_pos_cart_list_refresh:=0;
      __temp_pos_calc_save:=0;
      __temp_pos_cart_select_first:=0;
      __temp_pos_cancel:=1;

:: Kontrahent
::      __temp_pos_kh_naz:=FAKS.KH().NAZ_P;
::      __temp_pos_kh_ref:=FAKS.KH().ref();
      exec('set_value','#desktop','','pos1','pos_faks_type01@pos1',$FAKS.T().ref());
      exec('pos_faks_type_par_fak','pos',1,FAKS.T().ref());
::      exec('set_value','#desktop','','pos1','basic_02@pos1',__temp_pos_kh_naz);

:: Handlowiec
::      __temp_pos_han_naz:=FAKS.HAN().NAZ;
::      __temp_pos_han_ref:=FAKS.HAN().ref();
::      exec('set_value','#desktop','','pos1','basic_04@pos1',__temp_pos_han_naz);
::      exec('set_enabled','#desktop','','pos1','basic_04@pos1',0);

:: Nip
      exec('set_value','#desktop','','pos1','basic_08@pos1',FAKS.NIP);
      exec('set_enabled','#desktop','','pos1','basic_08@pos1',1);

:: cashback
      __pos_cashback:=0;
      exec('cashback_load','pos');

      exec('set_value','#desktop','','pos1','cart_02@pos1', ' '+exec('cart_pos_create_header','pos'));
      exec('refresh_element','#desktop','','pos1','cart_01@pos1');
      exec('grab_focus','#desktop','','pos1','calc_19@pos1');

      exec('nag_add_block','pos',1);
      exec('pos_sum','pos');

      exec('set_value','#desktop','','pos1','current_document@pos1','Aktualny dokument: %1 (%2)'@[FAKS.SYM, FAKS.POSSZ().TR])


?};

return(_ret)


\pos_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Metoda wywoływana automatyczna po wczytaniu okienkna posa
::       lub po czyszczeniu posa dla nowego dokumentu
::   WE: _a - null, FAKS.ref - wczytanie zawieszonego dokumentu
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
::_env:=params_get().env;
_ld:={? _ > 0 || exec('last_dok','pos', _a) || exec('last_dok','pos') ?};
{? _ld=null
||

   {? var_pres('__temp_pos_cart_list_refresh')>0 || &__temp_pos_cart_list_refresh ?};
   __temp_pos_cart_list_refresh:=0;
   {? var_pres('__temp_pos_calc_save')>0 || &__temp_pos_calc_save ?};
   __temp_pos_calc_save:=0;

   {? var_pres('__temp_pos_cart_select_first')>0 || &__temp_pos_cart_select_first ?};
   __temp_pos_cart_select_first:=0;

   {? var_pres('__temp_pos_cancel')>0 || &__temp_pos_cancel ?};
   __temp_pos_cancel:=1;

   exec('calc_hide','pos');
   exec('kontrahent','pos');
::   exec('handlowiec','pos');
   exec('set_value','#desktop','','pos1','cart_02@pos1', ' '+exec('cart_pos_create_header','pos'));
   exec('nag_add_block','pos');
   exec('refresh_element','#desktop','','pos1','cart_01@pos1');
   exec('set_value','#desktop','','pos1','basic_08@pos1','');
   exec('grab_focus','#desktop','','pos1','calc_19@pos1');
   __pos_cashback:=0;
   exec('cashback_load','pos');
   exec('pos_faks_type_par_fak','pos',1,OZ.POSTD);
   exec('set_value','#desktop','','pos1','pos_faks_type01@pos1',$OZ.POSTD);
   exec('set_value','#desktop','','pos1','current_document@pos1','')


?};
~~


\search
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Metoda wykonywana po kliknięciu przycisku szukaj
::       lub po zdarzeniu kliknięcia entera gdy na polu szukaj jest focus
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_text:=exec('get_value','#desktop','','pos1','calc_19@pos1');

_re:=exec('post_show','pos', _text);

{? _re>0
||
   exec('pos_add','pos');
   exec('grab_focus','#desktop','','pos1','calc_19@pos1')
?};
M.cntx_pop();
~~


\calc_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Obsługa przycisków [0-9] kalkulatora
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_text:=exec('get_value','#desktop','','pos1','calc_19@pos1');
_text:={? _text = '0' | __temp_pos_cart_select_first = 1 || '' || _text ?} + $_a;
__temp_pos_cart_select_first:=0;
exec('set_value','#desktop','','pos1','calc_19@pos1',_text);
::exec('grab_focus','#desktop','','pos1','calc_19@pos1');
~~


\calc_comma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Obsługa przycisku przecinka w kalkulatorze
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_text:=exec('get_value','#desktop','','pos1','calc_19@pos1');
_text:=gsub(_text,',','.');
_search:=0;
{! _ii:=1..(+_text) |!
   _char:=(_ii+_text)+1;
   {? _char='.'
   ||
      _search:=1
   ?}
!};
{? _search=0 || _text:=_text + '.' ?};
exec('set_value','#desktop','','pos1','calc_19@pos1',_text);
::exec('grab_focus','#desktop','','pos1','calc_19@pos1');
~~


\calc_ce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Obsługa przycisku czyszczącego wartości w kalkulatorze
::   WE: [_a] -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
exec('set_value','#desktop','','pos1','calc_19@pos1','');
::exec('grab_focus','#desktop','','pos1','calc_19@pos1');
~~


\calc_c
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Obsługa przycisku usuwającego ostatni znak wartości w kalkulatorze
::   WE: [_a] -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_text:=exec('get_value','#desktop','','pos1','calc_19@pos1');
exec('set_value','#desktop','','pos1','calc_19@pos1',_text-1);
~~


\calc_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Obsługa przycisków obliczeniowych kalkulatora
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_text:=exec('get_value','#desktop','','pos1','calc_19@pos1');
_text:=gsub(_text,',','.');
_num:=#_text $ 2;
{? _a='+1'
||
   _num:=_num+1
|? _a='-1'
||
   _num:=_num-1
|? _a='*2'
||
   _num:=_num*2
|? _a='/2'
||
   _num:=_num/2
?};
{? _num<0 || _num:=0 ?};
exec('set_value','#desktop','','pos1','calc_19@pos1',$_num)


\close_window
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Metoda wywołana przed zamknięciem okna posa
::   WE: [_a] - czy usuwać dokument: 0-nie(domyslnie) 1-tak
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_win:=0;
_aa:=1;
{? _=1
||
   {? type_of(_a) = 1
   ||
      {? _a=1
      ||
         _win:=1
      ?}
   ?}
?};
{? _win & var_pres('__temp_pos_nag_ref')>0
||
   {? FUN.ask('Czy chcesz anulować dodawany dokument?'@)
   ||
      exec('cancel_doc','pos');
      return(1)
   ||
      return(0)
   ?}
||
   _fs:=FUN.STANDARD;
   FUN.STANDARD:=1;
   _aa:=exec('exit','zws','Esc');
   FUN.STANDARD:=_fs
?};
_aa


\btn_cancel_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Metoda wywołana po kliknięciu przycisku anulowania dokumentu
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
{? var_press('__temp_pos_nag_ref') > 0
||
   _czynnosc:={? OZ.POSCZYN='N' || 'LSP_FAK_DRFP' |? OZ.POSCZYN='T' || 'LSP_FAK_DGDM' || 'LSP_FAK_DRFP' ?};
   _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:=_czynnosc;
   _params.UIDREF:=FAKS.uidref();
   _params.CONTEXT:=obj_new('POS','ENV');
   _params.CONTEXT.ENV:=_env;
   _params.CONTEXT.POS:="
      params_set('env',_a);
      exec('r_unlock_one','#table',FAKS,__temp_pos_nag_ref,__temp_pos_nag_r_lock);
      exec('cancel_doc','pos');
      exec('pos_ini','pos');
   1";
   _params.AKCJA:='Usuń';
   exec('mp_run','#b__box',_params)
?};
~~


\cancel_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Czyszczenie zmiennych globalnych oraz usunięcie dokumentu wraz z powiązaniami
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
:: usunięcie dokumentu wraz z pozycjami

:: odblokowanie dokumentu
::   exec('r_unlock_one','#table',FAKS,FAKS.ref(),__temp_pos_nag_r_lock);

:: usunięcie zmiennych globalnych
VAR_DEL.delete(
'__temp_pos_nag_ref'
,'__temp_pos_kh_naz'
, '__temp_pos_kh_ref'
, '__temp_pos_han_naz'
, '__temp_pos_han_ref'
, '__temp_pos_nag_r_lock'
);
{? __temp_pos_cancel
||
::   usun dokument
::   exec('sumfak','faktury_wspolne');
::   exec('plat_przel','faktury_plat',FAKS.ref())
::   params_exec('faks_usun','faktury_nag');
   OZ.get();
   OZ.LASTFAKS:='';
   OZ.put()
?};
~~


\cart_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pobieranie listy pozycji dokumentu
::   WE: -
::   WY: __tab_cart_list
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? var_pres('__tab_cart_list')>0 || obj_del(__tab_cart_list) ?};
::__tab_cart_list:=tab_tmp(1,'TEXT','STRING[250]','Text','REF','TREE_REF','Ref');
__tab_cart_list:=tab_tmp(1
,'LP','INTEGER','Lp.'
,'TEXT','STRING[250]','Text'
,'REF','STRING[16]','Ref'
,'EAN','STRING[60]','Ean'
);
_ndx1:=__tab_cart_list.ndx_tmp(,,'REF',,);
__tab_cart_list.index(_ndx1);

_ii:=0;

_currency:='PLN';
::_currency:=FAKS.WAL().KOD;
_rab_type:={? exec('czyrabp','ceny', FAKS.RAB_TYP) || '%' || _currency ?};

{? var_pres('__temp_pos_nag_ref')>0
||
   FAP.cntx_psh();
   FAP.index('FAP');
   FAP.prefix(FAKS.ref());

   _max_dokl:=0;
   _max_dokl_s:=0;
   {? FAP.first()
   ||
      {! |?
         {? _max_dokl<FAP.M().DOKL
         ||
            _max_dokl:=FAP.M().DOKL
         ?};
         {? _max_dokl_s<FAP.M().DOKL_S
         ||
            _max_dokl_s:=FAP.M().DOKL_S
         ?};
         FAP.next()
      !}
   ?};

   {? FAP.first()
   ||
      {! |?
         _ii+=1;
         __tab_cart_list.blank();
         __tab_cart_list.LP:=_ii;
         __tab_cart_list.TEXT:=exec('cart_pos_create','pos',FAP.NX, FAP.IL, FAP.CPR,
         {? exec('czyrabp','ceny', FAKS.RAB_TYP) || FAP.RAB || FAP.RABK ?},
         FAP.JM().KOD, FAP.WB, _currency, _rab_type, _max_dokl, _max_dokl_s);
::          FAP.M().DOKL, FAP.M().DOKL_S);
         __tab_cart_list.REF:=$FAP.ref();
         __tab_cart_list.add();
         FAP.next()
      !}
   ?};
   FAP.cntx_pop();

   {? __temp_pos_cart_list_refresh = 1
   ||
      {? __tab_cart_list.size() > 0
      ||
         exec('pos_sum','pos');
         {? __temp_pos_calc_save = 0
         ||
            exec('cart_select','pos',1);
::            exec('change_color_btn','pos', 'calc_17', 1);
::            exec('calc_hide','pos');
            exec('set_value','#desktop','','pos1','calc_19@pos1','');
~~
         ?};
         __temp_pos_calc_save:=0
      ||
         exec('calc_cancel','pos')
      ?};
      __temp_pos_cart_list_refresh:=0
   ?}
?};

{? _ii = 0
||
   __tab_cart_list.blank();
   __tab_cart_list.TEXT:='Brak towarów w koszyku.'@;
   __tab_cart_list.REF:='---';
   __tab_cart_list.add()
||
   exec('cart_select','pos',1)
?};
return(__tab_cart_list)


\cart_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Metoda wykonywana po wybraniu towaru z listy koszyka i aktywowanie kalkulatora
::   WE: [_a] - 0(domyślnie) - aktualnie zaznaczona pozycja z listy
::              1 - wywołanie przed odświeżeniem listy, po odświeżeniu wybrana będzie
::                  ostatnia pozycja
::       [_b] - ustalone__temp_pos_cart_select_type 1, 2 lub 3, nie zmieniać wprowadzonej wartości
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_temp_ref_product:='---';
_atr:=0;
{? _ > 0
||
   {? type_of(_a) = 1
   ||
      _atr:=_a
   ?}
?};

{? _atr = 0
||

:: Pobranie informacji o aktualnie zaznaczonym elemencie z listy
  _tab:=exec('get_selection','#desktop','','pos1','cart_01@pos1');
  {? _tab.first()
  ||
    _text:=_tab.KEY
  ?};
   _temp_ref_product:=_text

|? _atr = 1

||
   __tab_cart_list.last();
   _temp_ref_product:=__tab_cart_list.REF
?};

{? _temp_ref_product <> '---'
||

   _atr_sel_typ:=0;
   {? _atr = 0 & _ > 1
   ||
     {? (type_of(_b) = 1) & (_b = 1 | _b = 2 | _b = 3)
     ||
       _atr_sel_typ:=_b
     ?}
   ?};

   __temp_pos_cart_select_first:=1;

:: Pobranie danych z bazy o podanej pozycji
   _temp_product:=exec('FindAndGet','#table',FAP,_temp_ref_product,,"
   __temp_pos_cart_select_ref:=$FAP.ref();
   __temp_pos_cart_select_name:=FAP.NX;
   __temp_pos_cart_select_quantity:=FAP.IL;
::   __temp_pos_cart_select_price:=FAP.CB;
   __temp_pos_cart_select_price:=FAP.CPR;
   __temp_pos_cart_select_discount:={? exec('czyrabp','ceny', @.FAKS.RAB_TYP) || FAP.RAB || FAP.RABK ?};
   __temp_pos_cart_select_type:=1;
   1
   ",null);

   {? _temp_product <> ~~
   ||
      {? _atr_sel_typ <> 0
      ||
      __temp_pos_cart_select_type:=_atr_sel_typ
      ?};
:: Odblokowanie kalkulatora
      exec('calc_show','pos');
:: Ustawienie wartości w formularzu kalkulatora
      exec('set_name','#desktop','','pos1','calc_17@pos1','&Zmiana %1'@[exec('calc_type_change','pos')])
::      exec('set_name','#desktop','','pos1','calc_24@pos1'
::      ,'<html>Zmiana <strong>%1</strong> dla <strong>%2</strong></html>'
::      [exec('calc_type_change','pos'), __temp_pos_cart_select_name]);


::      {? _atr_sel_typ = 0
::      ||
::::        exec('set_value','#desktop','','pos1','calc_19@pos1',$__temp_pos_cart_select_quantity);
::        exec('grab_focus','#desktop','','pos1','calc_19@pos1')
::      ?}
   ?}
?};
~~


\cart_pos_create_header
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Generowanie nagłówka do listy w koszyku
::   WE: -
::   WY: string - treść nagłówka
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_max_char_name:=35;
_max_char_quantity:=10;
_max_char_size:=5;
_max_char_price:=15;
_max_char_discount:=15;
_max_char_value:=15;

_text:='';

_a:='Nazwa'@;
_text:=_text + _a + exec('cart_create_space','pos',(_max_char_name - (+_a)));
_b:='Ilość'@;
_text:=_text + ' |' + exec('cart_create_space','pos',(_max_char_quantity - (+_b))) + _b;
_e:='jm'@;
_text:=_text + ' |' + exec('cart_create_space','pos',(_max_char_size - (+_e))) + _e;
_c:='Cena'@;
_text:=_text + ' |' + exec('cart_create_space','pos',(_max_char_price - (+_c))) + _c;
_d:='Rabat'@;
_text:=_text + ' |' + exec('cart_create_space','pos',(_max_char_discount - (+_d))) + _d;
_e:='Wartość'@;
_text:=_text + ' |' + exec('cart_create_space','pos',(_max_char_value - (+_e))) + _e;

return(_text)


\cart_pos_create
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Generowanie lini tekstu do listy w koszyku
::   WE: _a - nazwa produktu
::       _b - ilość
::       _c - cena
::       [_d] - rabat
::       [_e] - miara
::       _f - wartość brutto
::       _g - waluta
::       _h - typ rabatu
::       [_i] - dokladność ilości
::       [_j] - dokladność ceny
::   WY: string - nazwy towarów wraz z cenami, ilościami itp
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
:: obliczona z automatu wartość = _b * _c - _d
:: maksymalna ilość znaków: 150 | 10 | 20 | 20 | 40

_i:={? _>=9 || {? type_of(_i)=type_of(0) || _i || 2 ?} || 2 ?};
_j:={? _>=10 || {? type_of(_j)=type_of(0) || _j || 2 ?} || 2 ?};

_max_char_name:=35;
_max_char_quantity:=10;
_max_char_size:=5;
_max_char_price:=15;
_max_char_discount:=15;
_max_char_value:=15;
_text:='';
::_wartosc:=0;
{? _ >= 1
||
   {? type_of(_a) = 2 & type_of(_b) = 1 & type_of(_c) = 1
   ||
      {? +_a <= _max_char_name
      ||
         _text:=_text + _a + exec('cart_create_space','pos',(_max_char_name - (+_a)))
      ||
         _text:=_text + ((_max_char_name-3)+_a) + '...'
      ?};
::      _wartosc:=_b * _c;
      _bb:=form(_b,,_i);
      _text:=_text + ' |' + exec('cart_create_space','pos',(_max_char_quantity - (+_bb))) + _bb;
      _ee:='';
      {? _ > 4
      ||
         {? type_of(_e) = 2
         ||
            _ee:=_e
         ?}
      ?};
      _text:=_text + ' |' + _ee + exec('cart_create_space','pos',(_max_char_size - (+_ee)));
      _cc:=form(_c,,_j) + ' ' + _g;
      _text:=_text + ' |' + exec('cart_create_space','pos',(_max_char_price - (+_cc))) + _cc
   ?};
   {? _ > 3
   ||
      {? type_of(_d) = 1
      ||
::         _wartosc:=_wartosc - _d;
         _dd:=form(_d,,2) + ' ' + _h;
         _text:=_text + ' |' + exec('cart_create_space','pos',(_max_char_discount - (+_dd))) + _dd
      ||
         _text:=_text + ' |' + exec('cart_create_space','pos',_max_char_discount - 1) + '0'
      ?}
   ||
      _text:=_text + ' |' + exec('cart_create_space','pos',_max_char_discount - 1) + '0'
   ?};
   _wartoscT:=form(_f,,_j) + ' ' + _g;
   _text:=_text + ' |' + exec('cart_create_space','pos',(_max_char_value - (+_wartoscT))) + _wartoscT
?};
return(_text)


\cart_create_space
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Generowanie spacji do wyrównania tekstu
::   WE: _a - ile spacji należy wygenerować
::   WY: string - spacje
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
:: _a - ilość spacji
_spaces:='';
{! _ii:=1.._a |!
   _spaces:=_spaces + ' '
!};
return(_spaces)


\cart_get_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: m20wloda [18.42]
:: OPIS: Zwraca ref aktualnie zaznaczonej pozycja paragonu
::   WE: -
::   WY: ref aktualnie zaznaczonej pozycja paragonu
::----------------------------------------------------------------------------------------------------------------------
_temp_ref_product:='---';
_tab:=exec('get_selection','#desktop','','pos1','cart_01@pos1');
{? _tab.first()
||
    _temp_ref_product:=_tab.KEY
?};
return(_temp_ref_product)


\calc_save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Zapisanie zmian wprowadzonych z kalkulatora dla danej pozycji w koszyku
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
:: Pobranie nowych wartości

:: aktualnie zaznaczona pozycja paragonu
_temp_ref_product:=exec('cart_get_ref','pos');

:: (m20wloda) wprowadzenie zmiany tylko wtedy, gdy aktualnie zaznaczona pozycja paragonu
:: jest taka sama, jak zapamiętana przez cart_select
::{? _temp_ref_product=__temp_pos_cart_select_ref
::||

  _text:=exec('get_value','#desktop','','pos1','calc_19@pos1');
::  _text2:=(#gsub(_text,',','.'))$2;
  _text2:=#gsub(_text,',','.');

   {? _text2>0 | (__temp_pos_cart_select_type=3 & _text2>=0)
   ||
      __temp_pos_cart_select_new:=_text2;
      __temp_pos_cart_select_value:={? _text2 < 0 || 0 || _text2 ?};

:: Zmiana wartości dla wybranego towaru
      _temp_product:=exec('FindAndGet','#table',FAP,_temp_ref_product,,"
      {? __temp_pos_cart_select_type = 1
      ||
         _il_dokl:=__temp_pos_cart_select_value $ FAP.M().DOKL;
         {? OZ.POSCZYN='T' & FAP.M().RODZ='T'
         ||
            {? _il_dokl>0
            ||
               exec('utw_zk_tymc','zamsiw_wspolne');
               __uzup_z(FAP.M);

               _tab:=exec('f3_il','faktury_poz', _il_dokl,,,{? OZ.POSAUTO='T' || 1 || 0 ?}, 0, FAP.MKODK);
               DISP.ILF:=_tab[2];
               FAP.IL2:=_tab[1];
               obj_del(_tab);
               exec('ae_il','faktury_poz',,,1)
            ||
               FUN.info('Podana ilość towaru jest za mała.'@)
            ?}
         ||
::            FAP.IL:=(__temp_pos_cart_select_value) $ 2
            FAP.IL:=_il_dokl
         ?};

         __temp_pos_cart_select_quantity:=FAP.IL
      |? __temp_pos_cart_select_type = 2
      ||
::         FAP.CPR:=(__temp_pos_cart_select_value) $ 2;
         FAP.CPR:=(__temp_pos_cart_select_value) $ FAP.M().DOKL_S;
         __temp_pos_cart_select_price:=FAP.CPR
      |? __temp_pos_cart_select_type = 3
      ||
         _rab_typ:={? OZ.POSTR = 'P' | OZ.POSTR = 'S' | OZ.POSTR = 'K' | OZ.POSTR = 'W'|| OZ.POSTR || 'P' ?};
::   P - procent suma
::   S - procent kaskadowy
::   K - kwotowy cana
::   W - kwotowy wartość
         _rab:=(__temp_pos_cart_select_value) $ 2;
         {? exec('czyrabp','ceny', @.FAKS.RAB_TYP)
         ||
            _rab:={? _rab > 99.99 || 99.99 || _rab ?};
            FAP.RAB:=_rab;
            __temp_pos_cart_select_discount:=FAP.RAB
         ||
            {? _rab_typ='K'
            ||
               _rab:={? _rab > FAP.CPR || FAP.CPR || _rab ?}
            |? _rab_typ='W'
            ||
               _rab:={? _rab > (FAP.CPR * FAP.IL) || (FAP.CPR * FAP.IL) || _rab ?}
            ?};
            FAP.RABK:=_rab;
            __temp_pos_cart_select_discount:=FAP.RABK
         ?};
         __temp_pos_cart_select_new:=_rab
      ?};
      exec('liczfak','faktury_wspolne');
      FAP.put();
      1
      ",null);


::wyczyszczenie po wprowadzeniu zmiany
      exec('set_value','#desktop','','pos1','calc_19@pos1', '');

      __temp_pos_calc_save:=1;
      __temp_pos_cart_list_refresh:=1;
      exec('set_value','#desktop','','pos1','cart_01@pos1','select_mode=SELECT_RESTORE');
      exec('refresh_element','#desktop','','pos1','cart_01@pos1')
::      exec('grab_focus','#desktop','','pos1','calc_19@pos1')
   ||
      exec('set_value','#desktop','','pos1','calc_19@pos1', '')
   ?};

:: uwzględnienie, że obecnie po odświeżeniu zawsze jest zaznaczony ostatni element listy
:: a nie ten, który został poprawiony
:: to wystarcza
   exec('cart_select','pos', 1, __temp_pos_cart_select_type);

::||

:: (m20wloda) gdy aktualnie zaznaczona pozycja paragonu jest inna niż zapamiętana przez cart_select
::   exec('cart_select','pos', 1, __temp_pos_cart_select_type);
::   FUN.emsg('Nie zmieniono - odświeżona lista pozycji.'@)

::?};
~~


\calc_cancel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Czyszczenie kalkulatora z wcześniej wybranej pozycji
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
VAR_DEL.delete(
'__temp_pos_cart_select_ref'
, '__temp_pos_cart_select_name'
, '__temp_pos_cart_select_quantity'
, '__temp_pos_cart_select_price'
, '__temp_pos_cart_select_discount'
, '__tab_faks_type_list'
, '__temp_pos_type_paragon'
, '__temp_pos_cart_ean'
, '__temp_pos_cart_il'
);
exec('calc_hide','pos');
~~


\calc_hide
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Ukrycie / wyświetlenie lub odblokowanie / zablokowanie elementów kalkulatora
::   WE: [_a] - 0-(domyślnie) ukryj elementy 1-wyświetl elementy
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_atr:=0;
{? _ = 1
||
   {? type_of(_a)=1
   ||
      {? _a=1
      ||
         _atr:=1
      ?}
   ?}
?};
exec('set_visible','#desktop','','pos1','calc_24@pos1',_atr);
{! _ii:=11..17 |!
   _iii:={? _ii < 10 || '0' + $_ii || $_ii ?};
   exec('set_enabled','#desktop','','pos1','calc_%1@pos1'[_iii],_atr)
!};
::{! _ii:=25..28 |!
::   exec('set_enabled','#desktop','','pos1','calc_%1@pos1'[$_ii],_atr)
::!};
   exec('set_enabled','#desktop','','pos1','calc_25@pos1',_atr);

:: Sprawdzenie uprawnień dla zmiany ceny i rabatu
_atr_cena:=0;
B_PERM.cntx_psh();
B_PERM_U.cntx_psh();
B_PERM.index('NAME');
B_PERM.prefix('TARS');
{? B_PERM.first()
||
   B_PERM_U.index('USER');
   B_PERM_U.prefix(FIRMA.ref(), USERS.ref(), B_PERM.ref());
   {? B_PERM_U.first()
   ||
      {? B_PERM_U.ACCESS='T'
      ||
         _atr_cena:=_atr
      ?}
   ?}
?};
B_PERM_U.cntx_pop();
B_PERM.cntx_pop();

:: Wcześniejsze sprawdzanie po uprawnieniach w cennikach
::   TARUP.cntx_psh();
::   TARUP.index('USER');
::   TARUP.prefix(OPERATOR.USER);
::   _atr_cena:=0;
::   {? TARUP.first()
::   ||
::      {! |?
::         {? TARUP.TARGN().SD='S'
::         ||
::            _atr_cena:=_atr
::         ?};
::         TARUP.next()
::      !}
::   ?};
::   TARUP.cntx_pop();

::   {? exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_ZPTM')
::   ||
::      _atr_cena:=_atr
::   ||
::      _atr_cena:=0
::   ?};
   exec('set_enabled','#desktop','','pos1','calc_26@pos1',_atr_cena);
   exec('set_enabled','#desktop','','pos1','calc_27@pos1',_atr_cena);



   exec('set_enabled','#desktop','','pos1','calc_28@pos1',_atr);
{? _atr = 0
||
   exec('set_value','#desktop','','pos1','calc_19@pos1','');
   exec('set_name','#desktop','','pos1','calc_23@pos1','-')
?};
_type:={? _atr = 1 || 0 || 2 ?};
exec('change_color_btn','pos', 'calc_25', _type);
exec('change_color_btn','pos', 'calc_26', _type);
exec('change_color_btn','pos', 'calc_27', _type);
~~


\calc_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wyświetl / odblokuj elementy kalkulatora
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
exec('calc_hide','pos',1);
~~


\pos_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Dodanie nowej pozycji do koszyka
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
:: sprawdza czy jest już dodany nagłówek dokumentu

_czy_dodac:=1;
{? M.RYS<>null & OZ.POSPHOTO='T'
||
   M.win_edit('PHOTO');
  _czy_dodac:=M.edit()
?};

{? _czy_dodac
||
   _new_dok:=exec('nag_add','pos');

   {? _new_dok
   ||
:: dodaje wybraną pozycje

      _ile:={? OZ.POSCZYN='T' & M.RODZ='T' || 0 || __temp_pos_cart_il ?};
      _kk:={? exec('get','#params',300200,2)='T' || exec('m_kk','material',M.ref()) || null() ?};
      exec('ADDPOZF','faktury_poz', M.ref(), _ile, 0, 0, M.VAT,,,,,,,_kk);
      exec('aktfil2','faktury_wspolne');

      {? OZ.POSCZYN='T' & M.RODZ='T'
      ||
         exec('utw_zk_tymc','zamsiw_wspolne');
         __uzup_z(FAP.M);
         _tab:=exec('f3_il','faktury_poz', __temp_pos_cart_il,,,1,0,__temp_pos_cart_ean);
         FAP.MKODK:=__temp_pos_cart_ean;
         __temp_pos_cart_ean:='';
         DISP.ILF:=_tab[2];
         FAP.IL2:=_tab[1];

         obj_del(_tab);
         exec('ae_il','faktury_poz',0)

      ?};

      exec('be_fapcp','faktury_poz', {? OZ.POSCNW='T' || 0 || 1 ?});
      FAP.put();

      {? FAP.IL<=0
      ||
         exec('cart_remove_item','pos',$FAP.ref(),
         'Dodano do koszyka materiał o zerowej ilości. Czy chcesz usunąć tę pozycję z koszyka?'@, 0)
      ?};
      __temp_pos_cart_list_refresh:=1;
      exec('set_value','#desktop','','pos1','cart_01@pos1','select_mode=SELECT_LAST');
      exec('refresh_element','#desktop','','pos1','cart_01@pos1');
      exec('set_value','#desktop','','pos1','calc_19@pos1','');
      exec('grab_focus','#desktop','','pos1','calc_19@pos1')
   ?}
?};
~~


\nag_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Utworzenie nowego dokumentu przy dodawaniu pierwszej pozycji dokumentu
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_pos:=_env.POS;

:: sprawdza czy jest już dodany nagłówek dokumentu, jeśli nie to go dodaje
{? var_pres('__temp_pos_nag_ref')<0
||
:: sprawdza czy okres rozliczeniowy jest poprawnie wybrany
   {? ST.AR=date()~1 & ST.AM=date()~2
   ||

      _type_dok:=null;
      {? OZ.POSTDW='T'
      ||
         _pos_faks_type:=exec('get_value','#desktop','','pos1','pos_faks_type01@pos1');
         _type_dok:=exec('FindAndGet','#table',TYPYSP,_pos_faks_type,,"
            return(TYPYSP.ref())
         ",null)
      ||
         {? OZ.POSTD<>null
         ||
::            _type_dok:=exec('FindInSet','#table','TYPYSP','TYPYKOD', 'PARAGON','PARAGON',,,,null)
::         ||
            _type_dok:=OZ.POSTD().ref()
         ?}
      ?};

      {? _type_dok<>null & type_of(_type_dok)=7
      ||
         _czynnosc:={? OZ.POSCZYN='N' || 'LSP_FAK_DRFP' |? OZ.POSCZYN='T' || 'LSP_FAK_DGDM' || 'LSP_FAK_DRFP' ?};
         _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:=_czynnosc;
         _params.CONTEXT:=obj_new('KH', 'HAN','FAKS', 'TYPYSP');
            _params.CONTEXT.KH:=__temp_pos_kh_ref;
            _params.CONTEXT.HAN:=__temp_pos_han_ref;
            _params.CONTEXT.FAKS:=null;
            _params.CONTEXT.TYPYSP:=_type_dok;
         _params.AKCJA:='POS';
         _params.PROC_START:='T';
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         _params.PORTS_ADJ:=1;

         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TYPYSP',_type_dok);

         exec('mp_run','#b__box',_params);

         {? _params.CONTEXT.FAKS <> null
         ||
            __temp_pos_nag_ref:=_params.CONTEXT.FAKS;

            _pos.prev_faks_ref:=_params.CONTEXT.FAKS;

            _rab_typ:={? OZ.POSTR = 'P' | OZ.POSTR = 'S' | OZ.POSTR = 'K' | OZ.POSTR = 'W'|| OZ.POSTR || 'P' ?};
            FAKS.RAB_TYP:=_rab_typ;

::params_exec('faks_wal_pr','faktury_nag')

::params_exec('po_faksw','faktury_nag')
::params_exec('fz_walh_bd','faktury_nag')
::params_exec('pr_krs','eurusd')

::            {? OZ.POSWAL<>null
::            ||
::               FAKS.WAL:=OZ.POSWAL().ref()
::            ?};

            FAKS.STANUSER:=OZ.FIS;

            FAKS.put();

            OZ.get();
            OZ.LASTFAKS:=$FAKS.ref();
            OZ.put();

            {? var_pres('__temp_pos_nag_r_lock') > 0 || obj_del(__temp_pos_nag_r_lock) ?};
            __temp_pos_nag_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
            exec('nag_add_block','pos',1);

            exec('set_value','#desktop','','pos1','current_document@pos1',
            'Aktualny dokument: %1 (%2)'@[FAKS.SYM, ''])

         ||
            return(0)
         ?}
      ||
         {? OZ.POSTDW='T'
         ||
            FUN.info('Należy podać typ dokumentu.'@)
         ||
            FUN.info('Należy podać typ dokumentu w parametrach POS.'@)
         ?};
         return(0)
      ?}
   ||
FUN.info('Wybrany okres w parametrach pracy jest niepoprawnie skonfigurowany, nie można stworzyć nowego dokumentu.'@);
return(0)
   ?}
?};
1


\nag_add_block
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Odblokowanie elementów dopiero po utworzeniu dokumentu
::   WE: [_a] -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_atr:=0;
{? _ > 0
||
   {? type_of(_a) = 1
   ||
      {? _a = 1
      ||
         _atr:=1
      ?}
   ?}
?};
exec('set_enabled','#desktop','','pos1','summary_01@pos1',_atr);
exec('set_enabled','#desktop','','pos1','summary_02@pos1',_atr);

SLO.cntx_psh();
ZR_SLO.cntx_psh();
exec('czytaj','#stalesys',,XINFO,'SLP');
SLO.index('SL');

_atr_karta:=_atr;
{? OZ.POSFK <> null
||
   SLO.prefix(XINFO.SLP, OZ.POSFK().KOD);
   {? SLO.first() = 1
   ||
      ZR_SLO.index('SLO_NR');
::      ZR_SLO.prefix(SLO.ref(), 4);
      ZR_SLO.prefix(SLO.ref());
::      {? ZR_SLO.first() = 1
      {? ZR_SLO.find_key(4)
      ||
         {? ZR_SLO.WAR = 'T'
         ||
            {? ZR_SLO.find_key(5)
            ||
               _atr_karta:={? exec('spr_typ','faktury_plat',ZR_SLO.WAR)='karta' || _atr_karta || 0 ?}
            ||
               _atr_karta:=0
            ?}
         ||
            _atr_karta:=0
         ?}
      ||
         _atr_karta:=0
      ?}
   ||
      _atr_karta:=0
   ?}
||
   _atr_karta:=0
?};
exec('set_enabled','#desktop','','pos1','summary_07@pos1',_atr_karta);

_atr_got:=_atr;
{? OZ.POSFG <> null
||
   SLO.prefix(XINFO.SLP, OZ.POSFG().KOD);
   {? SLO.first() = 1
   ||
      ZR_SLO.index('SLO_NR');
::      ZR_SLO.prefix(SLO.ref(), 4);
      ZR_SLO.prefix(SLO.ref());
::      {? ZR_SLO.first() = 1
      {? ZR_SLO.find_key(4)
      ||
         {? ZR_SLO.WAR = 'T'
         ||
            {? ZR_SLO.find_key(5)
            ||
               _atr_got:={? exec('spr_typ','faktury_plat',ZR_SLO.WAR)='gotówka' || _atr_got || 0 ?}
            ||
               _atr_got:=0
            ?}
         ||
            _atr_got:=0
         ?}
      ||
         _atr_got:=0
      ?}
   ||
      _atr_got:=0
   ?}
||
   _atr_got:=0
?};
exec('set_enabled','#desktop','','pos1','summary_08@pos1',_atr_got);

ZR_SLO.cntx_pop();
SLO.cntx_pop();

::exec('set_enabled','#desktop','','pos1','basic_05@pos1',{? FAKS.T().PAR<>'T' || _atr || 0 ?} );
::exec('set_enabled','#desktop','','pos1','basic_06@pos1',_atr);
::exec('set_enabled','#desktop','','pos1','basic_08@pos1',_atr);
::exec('set_enabled','#desktop','','pos1','basic_09@pos1',_atr);

::exec('pos_faks_type_par_fak','pos', _atr);


_currency:='PLN';
::_currency:=FAKS.WAL().KOD;
exec('set_name','#desktop','','pos1','summary_04@pos1','%1 %2'[form(0.00,,2), _currency]);
exec('set_name','#desktop','','pos1','summary_06@pos1','0');

{? OZ.POSTDW='T'
||
::   exec('set_visible','#desktop','','pos1','pos_faks_type01@pos1',1);
::   exec('set_visible','#desktop','','pos1','pos_faks_type02@pos1',1);
   _faks_type:={? _atr=1 || 0 || 1 ?};
   exec('set_enabled','#desktop','','pos1','pos_faks_type01@pos1',_faks_type)
||
   exec('set_enabled','#desktop','','pos1','pos_faks_type01@pos1',0)
::   exec('set_visible','#desktop','','pos1','pos_faks_type02@pos1',0)
?};

_suspension:={? OZ.POSSZ<>null || _atr || 0 ?};
exec('set_enabled','#desktop','','pos1','summary_10@pos1',_suspension);

_suspension:={? OZ.POSSZ<>null || 1 || 0 ?};
exec('set_enabled','#desktop','','pos1','summary_11@pos1',_suspension);
::{? OZ.POSSZ=null
::||
::   exec('set_enabled','#desktop','','pos1','summary_11@pos1',_atr)
::?};
~~


\nag_change_nip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Zmiana nipu w dokumencie
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_nip:=exec('get_value','#desktop','','pos1','basic_08@pos1');

_new_dok:=exec('nag_add','pos');
{? _new_dok
||

{? FAKS.NIP<>_nip
||
   {? _wyn:=exec('nip_ok','#id',_nip)
   ||
      FAKS.NIP:=_nip;
      FAKS.put();

      {? FAKS.T().PAR<>'T' & _nip<>''
      ||
         KH.cntx_psh();
         _ndx:=KH.ndx_tmp(,,'NIP',,);
         KH.index(_ndx);
         KH.prefix(_nip);

         {? KH.first() & KH.NIP=_nip
         ||
            {? FUN.ask('Znaleziono kontrahenta "%1" z numerem NIP: "%2". Czy ustawić kontrahenta "%1" w dokumencie?'@
            [KH.NAZ_P,_nip])
            ||
               __temp_pos_kh_naz:=KH.NAZ_P;
               __temp_pos_kh_ref:=KH.ref();
               exec('set_value','#desktop','','pos1','basic_02@pos1',__temp_pos_kh_naz);
               FAKS.KH:=KH.ref();
               exec('duplikuj_dane_kh','faktury_wspolne');
               FAKS.put();
               exec('handlowiec','pos',1)
            ?}
         ?};

         KH.ndx_drop(_ndx);
         KH.cntx_pop()
      ?};

      {? FAKS.NIP<>''
      ||
         FUN.info('Zmieniono numer NIP.'@)
      ||
         FUN.info('Usunięto numer NIP z dokumentu.'@)
      ?}

   ||
      exec('grab_focus','#desktop','','pos1','basic_08@pos1')
   ?}
?}
?};
~~


\pos_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Obliczanie sumy koszyka
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
:: Oblicza sume ilości pozycji i wartości do zapłaty

_summary_04:=0;
_summary_06:=0;

{? var_pres('__temp_pos_nag_ref')>0
||
   FAP.cntx_psh();
   FAP.index('FAP');
   FAP.prefix(FAKS.ref());
   {? FAP.first()
   ||
      {! |?
         _summary_04+=FAP.WB;
::         _summary_04+=FAP.CB;
         _summary_06+=1;
         FAP.next()
      !}
   ?};
   FAP.cntx_pop()

::   FAKS.BRUTTO:=_summary_04 $ 2;
::   FAKS.put()
?};

_currency:='PLN';
::_currency:=FAKS.WAL().KOD;

exec('set_name','#desktop','','pos1','summary_04@pos1','%1 %2'[form(_summary_04,,2), _currency]);
exec('set_name','#desktop','','pos1','summary_06@pos1',$_summary_06);

exec('sumfak','faktury_wspolne');
exec('plat_przel','faktury_plat',FAKS.ref());
~~


\kontrahent
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pobieranie kontrahenta "Kontrahent jednorazowy"
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? OZ.POSDK <> null
||
   __temp_pos_kh_naz:=OZ.POSDK().NAZ_P;
   __temp_pos_kh_ref:=OZ.POSDK().ref()
||
   {? OZ.POSTD<>null
   ||
      {? OZ.POSTD().PAR='T' & OZ.POSTD().KH<>null
      ||
         __temp_pos_kh_naz:=OZ.POSTD().KH().NAZ_P;
         __temp_pos_kh_ref:=OZ.POSTD().KH().ref()
      ||
         KH.cntx_psh();
         KH.index('OSOBA');
         KH.prefix();
         KH.first();
         __temp_pos_kh_naz:=KH.NAZ_P;
         __temp_pos_kh_ref:=KH.ref();
         KH.cntx_pop()
      ?}
   ||
      __temp_pos_kh_naz:='';
      __temp_pos_kh_ref:=null
   ?}
?};
exec('set_value','#desktop','','pos1','basic_02@pos1',__temp_pos_kh_naz);
~~


\handlowiec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Pobranie handlowca przypisanego dla kontrahenta lub pierwszego z lisy
::   WE: [_a] - 1 - zmienić handlowca podczas zminay kontrahenta - 0 (domyślnie) wyłączone
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_zmiana:={? _ >= 1 || {? type_of(_a)=type_of(0) || {? _a=1 || 1 || 0 ?} || 0 ?} || 0 ?};
{? (var_pres('__temp_pos_han_naz')<0 & var_pres('__temp_pos_han_ref')<0) | _zmiana
||
:: pobranie przypisanego handlowca w kontrahencie jeśli jest przypisany
   _han:=0;
   {? var_press('__temp_pos_kh_ref')>0
   ||
      KH_DOD.cntx_psh();
      KH_DOD.index('KH_DOD');
      KH_DOD.prefix(FIRMA.ref(),__temp_pos_kh_ref);
      {? KH_DOD.first()
      ||
         {? KH_DOD.HAN<>null
         ||
            __temp_pos_han_naz:=KH_DOD.HAN().NAZ;
            __temp_pos_han_ref:=KH_DOD.HAN().ref();
            _han:=1
         ?}
      ?};
      KH_DOD.cntx_pop()
   ?};
   {? _han = 0
   ||
      __temp_pos_han_naz:='';
      __temp_pos_han_ref:=null
   ?}
:: w innym przypadku pobiera pierwszego handlowca z bazy
::   {? _han = 0
::   ||
::      HAN.cntx_psh();
::      HAN.index('KOD');
::      HAN.prefix();
::      HAN.first();
::      __temp_pos_han_naz:=HAN.NAZ;
::      __temp_pos_han_ref:=HAN.ref();
::      HAN.cntx_pop()
::   ?}
?};
::exec('set_value','#desktop','','pos1','basic_04@pos1',__temp_pos_han_naz);
::exec('set_enabled','#desktop','','pos1','basic_04@pos1',0);
~~


\kontrahent_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Metoda zmiany kontrahenta
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_return:=exec('kontrahent_win','pos');
::{? _return > 0
::||
::   exec('kontrahent_select','pos')
::?};
~~


\kontrahent_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Metoda wyświetlająca okienko wyboru kontahenta
::   WE: -
::   WY: integer
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_return:=0;
KH.cntx_psh();
_ndx1:=KH.ndx_tmp(,,'KOD',,0);
KH.index(_ndx1);
KH.prefix();

_return:=params_exec('win_kontrahent','pos_win');
{? _return > 0
||
   exec('kontrahent_select','pos')
?};

KH.ndx_drop(_ndx1);
KH.cntx_pop();
_return


\kontrahent_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Zmiana danych no nowego kontrahenta
::   WE: [_a] -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
__temp_pos_kh_naz:=KH.NAZ_P;
__temp_pos_kh_ref:=KH.ref();
exec('set_value','#desktop','','pos1','basic_02@pos1',__temp_pos_kh_naz);
FAKS.KH:=KH.ref();
exec('duplikuj_dane_kh','faktury_wspolne');
{? var_pres('__temp_pos_nag_ref')>0
||
   FAKS.put()
?};
exec('handlowiec','pos',1);
~~


\handlowiec_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Metoda zmiany handlowca
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_return:=exec('handlowiec_win','pos');
::{? _return > 0
::||
::   exec('handlowiec_select','pos')
::?};
~~


\handlowiec_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Metoda wyświetlająca okienko wyboru handlowca
::   WE: -
::   WY: integer
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_return:=0;
HAN.cntx_psh();
_ndx1:=HAN.ndx_tmp(,,'NAZ',,0);
HAN.index(_ndx1);
HAN.prefix();

_return:=params_exec('win_handlowiec','pos_win');
{? _return > 0
||
   exec('handlowiec_select','pos')
?};

HAN.ndx_drop(_ndx1);
HAN.cntx_pop();
_return


\handlowiec_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Zmiana danych z nowego handlowcy
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
__temp_pos_han_naz:=HAN.NAZ;
__temp_pos_han_ref:=HAN.ref();
::exec('set_value','#desktop','','pos1','basic_04@pos1',__temp_pos_han_naz);
FAKS.HAN:=HAN.ref();
{? var_pres('__temp_pos_nag_ref')>0
||
   FAKS.put()
?};
~~


\post_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Wyszukuje produkt po kodzie kreskowym lub nazwie
::   WE: _a - string - kod kreskowy lub nazwa towaru
::   WY: integers - czy wybrano element z list w oknie wyboru
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
M.cntx_psh();

_re:=0;
_show_win:=1;

:: szukaj po kodzie kreskowym MKODK
:: tabela: MKODK
:: pola:
:: M
:: IL - zmiana ilości 1 na tą wartość
:: AKT='T'
:: KODK - szukany kod kreskowy
::MKODK.index('KK');
::MKODK.prefix(_a,);

__temp_pos_cart_ean:='';
__temp_pos_cart_il:=1;

:: Szukaj po kodzie kreskowym (dokładnie)
:: jeśli znajdzie z automatu dodaje pozycje do koszyka towar
{? _a <> '' & _show_win
||
   M.index('AKODK');
   M.prefix('T',_a,);

   {? M.first()
   ||
      _show_win:=0;
      exec('pos_add','pos')
   ?}
?};

{? _a<>'' & _show_win
||
   MKODK.cntx_psh();
   MKODK.index('KK');
   MKODK.prefix(_a,);
   {? MKODK.first()
   ||
      {? MKODK.M().A='T'
      ||
         {? MKODK.KH<>null | (MKODK.IDMOB<>'A' & MKODK.IDMOB<>'D' & MKODK.IDMOB<>'P' & MKODK.IDMOB<>'Z')
         ||
            _a:=''
         ?};
         _show_win:=0;
         __temp_pos_cart_ean:=_a;
         {? MKODK.IDMOB='R'
         ||
            __temp_pos_cart_il:=MKODK.IL
         ?};
         M.seek(MKODK.M().ref())
      ?}
   ?};
   MKODK.cntx_pop();
   {? _show_win=0
   ||
      exec('pos_add','pos')
   ?}
?};


:: Szukaj po nazwie i wyświetla okienko z możliwością wyboru konkretnego towaru z listy
{? _show_win
||

:: gdy napis jest pusty -> wszystkie towary i usługi
:: gdy napis nie jest pusty -> towary i usługi z pasującym kodem kreskowym
:: gdy napis nie jest pusty -> pasujące towary albo pasujące usługi
:: wielkość liter jest istotna!!

   _szuk_jak:='N';
   {? _a=''
   ||
     M.index('ANAZ');
     M.prefix('T',_a)
   ||
     _szuk_jak:='K';
     M.index('AKODK');
     M.prefix('T',_a);
     {? M.first()=0
     ||
       _szuk_jak:='N';
       M.index('AMATNAZ');
       M.prefix('T','T',_a);
       {? M.first()=0
       ||
          M.prefix('T','U',_a)
       ?}
     ?}
   ?};

   {? M.first()
   ||
      {? M.size()>1 | _szuk_jak='K'
      ||
          _re:=params_exec('win_material','pos_win')
      ||
         exec('pos_add','pos')
      ?}
   ||
      {? exec('pos_stock','pos', _a)=1
      ||
         exec('set_value','#desktop','','pos1','calc_19@pos1', '');
         exec('grab_focus','#desktop','','pos1','calc_19@pos1')
      ?}
::      msg('Nie znaleziono towaru o podanych parametrach.'@, 'UWAGA', 1)
::      choice('Nie znaleziono towaru o podanych parametrach.'@, 'UWAGA', 'INFO', 1, , , 'Anuluj', 'OK');
::      FUN.info('Nie znaleziono towaru o podanych parametrach.'@);
::      exec('grab_focus','#desktop','','pos1','calc_19@pos1')
   ?}

?};
::M.cntx_pop();
_re


\calc_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Lista wartości do zminany w kalulatorze
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? var_pres('__tab_calc_list')>0 || obj_del(__tab_calc_list) ?};
__tab_calc_list:=tab_tmp(1
,'LP','INTEGER','Lp.'
,'TEXT','STRING[20]','Text'
);
_ndx1:=__tab_calc_list.ndx_tmp(,,'LP',,);
__tab_calc_list.index(_ndx1);

__tab_calc_list.blank();
__tab_calc_list.LP:=1;
__tab_calc_list.TEXT:='Ilość';
__tab_calc_list.add();

__tab_calc_list.blank();
__tab_calc_list.LP:=2;
__tab_calc_list.TEXT:='Cena';
__tab_calc_list.add();

__tab_calc_list.blank();
__tab_calc_list.LP:=3;
__tab_calc_list.TEXT:='Rabat';
__tab_calc_list.add();

return(__tab_calc_list)


\calc_change
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Akcja wykonywana po zmianie typu zmiany w kalkulatorze
:: oraz doczytanie wcześniejszych danych w inpucie kalkulatora
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
:: (m20wloda) najpierw sprawdzenie, która pozycja paragonu jest zaznaczona
:: 'cart_select' wykonuje się samo tylko przy podwójnym kliknięciu
:: lub wciśnięciu "Enter"
exec('cart_select','pos');
_value:='';
::_type:=#exec('get_value','#desktop','','pos1','calc_21@pos1');
_type:=_a;
{? _type=1
||
   _value:=__temp_pos_cart_select_quantity
|? _type=2
||
   _value:=__temp_pos_cart_select_price
|? _type=3
||
   _value:=__temp_pos_cart_select_discount
?};
__temp_pos_cart_select_type:=_type;
__temp_pos_cart_select_first:=1;
exec('set_value','#desktop','','pos1','calc_19@pos1', $_value);
exec('set_name','#desktop','','pos1','calc_17@pos1','&Zmiana %1'@[exec('calc_type_change','pos')]);
::exec('set_name','#desktop','','pos1','calc_24@pos1'
::,'<html>Zmiana <strong>%1</strong> dla <strong>%2</strong></html>'
::[exec('calc_type_change','pos'), __temp_pos_cart_select_name]);
exec('grab_focus','#desktop','','pos1','calc_19@pos1');
~~


\format_price
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Format cen
::   WE: _a - cena real lub string
::   WY: string
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_wy:='';
{? _=1
||
   {? type_of(_a)=2
   ||
      _a:=#_a
   ?};
   {? type_of(_a)=type_of(0.00)
   ||
      _wy:=form(+_a,,2)
   ?}
?};
return(_wy)


\calc_type_change
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Typy zmiany w kalkulatorze - słownie
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
      _type:='';
      _type1:=0;
      _type2:=0;
      _type3:=0;
      {? __temp_pos_cart_select_type = 1
      ||
         _type:='ilości';
         _type1:=1
      |? __temp_pos_cart_select_type = 2
      ||
         _type:='ceny';
         _type2:=1
      ||
         _type:='rabatu';
         _type3:=1
      ?};
::exec('change_color_btn','pos', 'calc_25', _type1);
::exec('change_color_btn','pos', 'calc_26', _type2);
::exec('change_color_btn','pos', 'calc_27', _type3);
return(_type)


\cart_remove
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Usuwanie wybranej pozycji z koszyka
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_temp_ref_product:=exec('cart_get_ref','pos');

:: (m20wloda) usunięcie tylko wtedy, gdy aktualnie zaznaczona pozycja paragonu
:: jest taka sama, jak zapamiętana przez cart_select
::{? _temp_ref_product=__temp_pos_cart_select_ref
::||

   _temp_product_del:=exec('cart_remove_item','pos',_temp_ref_product,
   'Czy na pewno chcesz usunąć wybraną pozycje w koszyku?'@);

    __temp_pos_cart_list_refresh:=1;
    exec('set_value','#desktop','','pos1','cart_01@pos1','select_mode=SELECT_LAST');
    exec('refresh_element','#desktop','','pos1','cart_01@pos1');
    exec('grab_focus','#desktop','','pos1','calc_19@pos1');

::||

:: (m20wloda) gdy aktualnie zaznaczona pozycja paragonu jest inna niż zapamiętana przez cart_select
::  exec('cart_select','pos', 1, __temp_pos_cart_select_type);
::  FUN.emsg('Nie usunięto - odświeżona lista pozycji.'@)

::?};
~~


\cart_remove_item
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Usuwanie wybranej pozycji z koszyka
::   WE: _a - string - ref pozycji do usunięcia
::       [_b] - string - tekst wyświetlający się przy potwierdzeniu usunięcia pozycji
::       [_c] - 1 (domyślnie) - pokaż komunikat o potwierdzeniu usunięcia, 0 - nie wyświetla komunikatu
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

__temp_product_del_mess:=null;
__temp_product_del_show:=1;

{? _>=2 || {? type_of(_b)=type_of('') || __temp_product_del_mess:=_b ?} ?};
{? _>=3 || {? type_of(_c)=type_of(0) & _c=0 || __temp_product_del_show:=0 ?} ?};

_temp_product_del:=0;
{? _>=1 ||
   {? type_of(_a)=type_of('')
   ||
      _temp_product_del:=exec('FindAndGet','#table',FAP,_a,,"
         FAP.cntx_psh();
         FAP.index('FAP');
         FAP.prefix(FAP.FAKS().ref, FAP.POZ);
            {? var_pres('__temp_product_del_mess')>0 & var_pres('__temp_product_del_show')>0
            ||
               params_exec('fap_del','faktury_poz', __temp_product_del_mess, __temp_product_del_show);
               VAR_DEL.delete('__temp_product_del_mess','__temp_product_del_show')
            ||
               params_exec('fap_del','faktury_poz')
            ?};
         FAP.cntx_pop();
         1
      ",null)
   ?}
?};
return(_temp_product_del)


\pos_pay
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Zapłata, zatwierdzenie dokumentu
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? exec('pos_pay_count_FAP','pos')
||
   __temp_pos_pay_only_cash:=0;
   __pos_cashback:=#exec('get_value','#desktop','','pos1','Cashback_input@pos1');
   exec('pos_grp','pos_pay')
?};
~~


\pos_pay_count_FAP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Sprawdza czy są jakiekolwiek towary dodane do dokumentu
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_summary:=0;
_pos_price:=1;
FAP.cntx_psh();
FAP.index('FAP');
FAP.prefix(FAKS.ref());
_count_pos:=FAP.size();
{? FAP.first()
||
   {! |?
      {? _pos_price
      ||
         {? FAP.WB <= 0
         ||
            _pos_price:=0
         ?}
      ?};
      _summary+=FAP.WB;
      FAP.next()
   !}
?};
FAP.cntx_pop();

:: Sprawdzenie czy jest wypełniony kontrahent
{? var_pres('__temp_pos_kh_ref')<1 | __temp_pos_kh_ref=null
||
   FUN.info('Należy podać kontrahenta.'@);
   return(0)
?};

:: Sprawdzenie czy jest wypełniony handlowiec
::{? var_pres('__temp_pos_han_ref')<1 | __temp_pos_han_ref=null
::||
::   FUN.info('Należy podać handlowca.'@);
::   return(0)
::?};

:: Sprawdzenie czy jest wypełniony kontrahent oraz nip dla dokumentów nie będących paragonem
::{? __temp_pos_type_paragon=0
::||
::   {? var_pres('__temp_pos_kh_ref')<1 | __temp_pos_kh_ref=null
::   ||
::      FUN.info('Należy podać kontrahenta.'@);
::      return(0)
::   ?};
::   _nip:=exec('get_value','#desktop','','pos1','basic_08@pos1');
::   {? _nip=''
::   ||
::      FUN.info('Nie podano numeru NIP dla wystawianego dokumentu.'@);
::      return(0)
::   ?}
::?};

{? _count_pos > 0
||
   {? _summary > 0
   ||
      {? _pos_price
      ||
         return(1)
      ||
         FUN.info('Wartość pozycji nie może być zerowa.'@);
         return(0)
      ?}
   ||
      FUN.info('Wartość dokumentu nie może być zerowa.'@);
      return(0)
   ?}
||
   FUN.info('Koszyk jest pusty.'@);
   return(0)
?};
0


\pay_return
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Akcja wykonana po zapłacie w oknie pos_pay
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

exec('r_unlock_one','#table',FAKS,__temp_pos_nag_ref,__temp_pos_nag_r_lock);

params_exec('faks_zakoncz_wer','faktury_nag');

OZ.get();
OZ.LASTFAKS:='';
OZ.put();

__temp_pos_close:=0;
__temp_pos_cancel:=0;
exec('cancel_doc','pos');
exec('pos_ini','pos');
~~


\change_color_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Zmiana koloru tłą i czcionki dla wybranego elementu pulpitu
::   WE: _a - string - id elementu
::       [_b] - int - 0 (domyślny) dokmyślny kolor, 1 - wyróżniony, 2 - zablokowany
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
:: dokmyślny - czcionka
_t0_c:='255:255:255';
:: dokmyślny - tło
_t0_b:='93:96:100';
:: wyróżniony - czcionka
_t1_c:='0:51:0';
:: wyróżniony - tło
_t1_b:='0:240:40';
:: zablokowany - czcionka
_t2_c:='128:128:128';
:: zablokowany - tło
_t2_b:='200:200:200';

{? _ > 0 & _ < 3
||
   {? type_of(_a) = 2
   ||
      _type:=0;
      {? _ = 2
      ||
         {? type_of(_b) = 1
         ||
            _type:=_b
         ?}
      ?};

      _color:=_t0_c;
      {? _type = 1
      ||
         _color:=_t1_c
      |? _type = 2
      ||
         _color:=_t2_c
      ?};
      _bg:=_t0_b;
      {? _type = 1
      ||
         _bg:=_t1_b
      |? _type = 2
      ||
         _bg:=_t2_b
      ?}
::       Ustawienie koloru tła
::       exec('set_background','#desktop','','pos1','%1@pos1'[_a], _bg);
::       Ustwaienie koloru czcionki
::       exec('set_foreground','#desktop','','pos1','%1@pos1'[_a], _color)
   ?}
?};
~~


\cashback_load
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Podczas wywołania okienka posa wyświetla kontrolki cashback
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? (OZ.TSTYP='polcard' | OZ.TSTYP='paytel') & OZ.TSCASHBA > 0
||
   exec('cashback_hide','pos', 1)
||
   exec('cashback_hide','pos')
?};
~~


\cashback_hide
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: Ukrywa / wyświetla kontrolki cashback
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? _ > 0
||
   {? _a <> 1
   ||
      _a:=0
   ?}
||
   _a:=0
?};

exec('set_name','#desktop','','pos1','Cashback_max@pos1', 'Maksymalna kwota: %1 %2'@[$OZ.TSCASHBA, 'PLN']);
:: FAKS.WAL().KOD

exec('set_value','#desktop','','pos1','Cashback_input@pos1', $__pos_cashback);

exec('set_visible','#desktop','','pos1','Cashback_input@pos1',_a);
exec('set_visible','#desktop','','pos1','Cashback_label@pos1',_a);
exec('set_visible','#desktop','','pos1','Cashback_max@pos1',_a);
~~


\pay_only_one_card
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [18.42]
:: OPIS: przekierowanie do pay_only_one_card z pliku pos_pay
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__pos_cashback:=#exec('get_value','#desktop','','pos1','Cashback_input@pos1');
exec('pay_only_one_card','pos_pay');
~~


\pos_stock
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Wywołanie okienka braku materiału
::   WE: _a - string - nazwa brakującego materiału
::   WY: 1
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? var_pres('__POSSTOCK')>0 || obj_del(__POSSTOCK) ?};
__POSSTOCK:=tab_tmp(1
,'NAME','STRING[250]','Nazwa'
);
_ndx1:=__POSSTOCK.ndx_tmp(,,'NAME',,);
__POSSTOCK.index(_ndx1);

__POSSTOCK.blank();
{? _ > 0
||
   __POSSTOCK.NAME:=_a
||
   __POSSTOCK.NAME:='BRAK PARAMETRU NAZWY TOWARU'@
?};
__POSSTOCK.add();

_ctr_id:=exec('create','#desktop',__POSSTOCK,'pos_stock1',,40);
_win_id:='pos_stock1';
__POSSTOCK.win_ctr(_ctr_id);
_before:="
   exec('load','#desktop','pos_stock1','pos_stock1.dsk',,,,,,0);
   {? __POSSTOCK.first()
   ||
      exec('set_value','#desktop','','pos_stock1','name@pos_stock1', __POSSTOCK.NAME)
   ?};
1";
__POSSTOCK.control(_win_id,'modal=false',_before);
1


\pos_stock_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Wywołanie okienka braku materiału
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
sel_exit();
~~


\pos_faks_type_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Lista typów
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
::VAR_DEL.delete('__tab_faks_type_list');
{? var_pres('__tab_faks_type_list')<0
|| __tab_faks_type_list:=tab_tmp(1
   ,'NAME','STRING[60]','Nazwa'
   ,'REF','STRING[16]','Ref'
   );
   _ndx1:=__tab_faks_type_list.ndx_tmp(,,'NAME',,);
   __tab_faks_type_list.index(_ndx1);

   TYPYSP.cntx_psh();
   TYPYSP.index('HIS');
   TYPYSP.prefix('N','N','N','N',);

   {? TYPYSP.first()
   ||
      {! |?
         {? TYPYSP.FIS='T' & TYPYSP.UE='N'
         ||
            __tab_faks_type_list.blank();
            __tab_faks_type_list.NAME:=TYPYSP.NAZ;
            __tab_faks_type_list.REF:=$TYPYSP.ref();
            __tab_faks_type_list.add()
         ?};
         TYPYSP.next()
      !}
   ?};
   TYPYSP.cntx_pop()
|| {? var_pres('__temp_pos_nag_ref')>0
   || exec('set_value','#desktop','','pos1','pos_faks_type01@pos1',$FAKS.T().ref())
   || {? var_pres('__temp_pos_faks_type')<0
      || __temp_pos_faks_type:=OZ.POSTD
      ?};
      exec('set_value','#desktop','','pos1','pos_faks_type01@pos1',$__temp_pos_faks_type);
      exec('pos_faks_type_par_fak','pos',1,__temp_pos_faks_type)
   ?}
?};

return(__tab_faks_type_list)


\pos_faks_type_change
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Zmiana typu dokumentu w POS
::   WE: -
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
_pos_faks_type:=exec('get_value','#desktop','','pos1','pos_faks_type01@pos1');
exec('FindAndGet','#table',TYPYSP,_pos_faks_type,,"
   exec('pos_faks_type_par_fak','pos',1,TYPYSP.ref())
",null);
1


\pos_faks_type_par_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Odblokowanie / zablokowanie zmiany kontrahenta w zależności od typu dok
::   WE: _a - 1(domyślnie) - odblokować / 0 - zablokować
::       _b - ref typu dokumentu
::   WY: ~~
::  OLD:
::----------------------------------------------------------------------------------------------------------------------
{? _ > 0
||
   _atr:=_a
||
   _atr:=1
?};
__temp_pos_type_paragon:=0;
{? _ > 1
||
::   __temp_pos_type_paragon:=exec('FindAndGet','#table',TYPYSP,_b,,"
::      _re:={? TYPYSP.PAR='T' || 1 || 0 ?};
::      _re
::   ",0)
   TYPYSP.cntx_psh();
   TYPYSP.index('HIS');
   TYPYSP.prefix();
   TYPYSP.seek(_b);
   __temp_pos_type_paragon:={? TYPYSP.PAR='T' || 1 || 0 ?};
   __temp_pos_faks_type:=TYPYSP.ref();
   TYPYSP.cntx_pop()
?};

exec('set_enabled','#desktop','','pos1','basic_05@pos1',{? __temp_pos_type_paragon=0 || _atr || 0 ?});
exec('set_enabled','#desktop','','pos1','basic_06@pos1',_atr);
exec('set_enabled','#desktop','','pos1','basic_08@pos1',_atr);
exec('set_enabled','#desktop','','pos1','basic_09@pos1',_atr);

{? __temp_pos_type_paragon=1
||
   exec('kontrahent','pos')
||
   {? var_pres('__temp_pos_nag_ref')<0
   ||
      exec('set_value','#desktop','','pos1','basic_02@pos1','');
      __temp_pos_kh_naz:='';
      __temp_pos_kh_ref:=null()
   ||
      __temp_pos_kh_naz:=FAKS.KH().NAZ_P;
      __temp_pos_kh_ref:=FAKS.KH().ref()
   ?};
   exec('set_value','#desktop','','pos1','basic_02@pos1',__temp_pos_kh_naz)
?};
   FAKS.KH:=KH.ref();
::   exec('duplikuj_dane_kh','faktury_wspolne');
   exec('handlowiec','pos',1);
{? var_pres('__temp_pos_nag_ref')>0
||
   FAKS.put()
?};
1


\oz_possz_bered
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Okno słowników ograniczone przed redakcją
::----------------------------------------------------------------------------------------------------------------------
:SLU.cntx_psh();
SLU.win_dict('WER');
~~


\oz_possz_afred
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Okno słowników po redakcji
::----------------------------------------------------------------------------------------------------------------------
:SLU.cntx_pop();
~~


\pos_suspension_save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Zawieszenie dokumentu i wyczyszczenie posa
::   WE: _a - null, FAKS.ref - wczytanie zawieszonego dokumentu
::----------------------------------------------------------------------------------------------------------------------
:: Sprawdzenie czy jest nadany słownik zawieszenia dokumentu, jeżeli nie to resetuje posa,
:: jeżeli jest nadany to wyświetla okienko z wyborem / dodanie nowej wartości symbolu zawieszenia dokumentu,
:: dodaje go do dokumentu i resetuje posa
params_set(params_get());
_suspend:=1;
{? OZ.POSSZ<>null & var_pres('__temp_pos_nag_ref')>0
||
::   FAKS.win_edit('POSSZ');
   _faks_sym:=FAKS.SYM;
   SLO.cntx_psh();
   SLO.index('SL_NAZ');
   SLO.prefix(OZ.POSSZ().NAZ,);
   {? FAKS.POSSZ<>null
   ||
      SLO.seek(FAKS.POSSZ)
   ?};
   _faks_slo_temp:=params_exec('win_dokument_slo','pos_win',_faks_sym);
   {? _faks_slo_temp > 0
   ||
      FAKS.POSSZ:=SLO.ref();
      FAKS.put()
   ||
      _suspend:=0
   ?};
::   {? FAKS.edit()
::   ||
::      FAKS.put()
::   ?};
   SLO.cntx_pop()
?};
{? _suspend=1
||
   {? var_pres('__temp_pos_nag_ref')>0
   ||
      exec('r_unlock_one','#table',FAKS,__temp_pos_nag_ref,__temp_pos_nag_r_lock)
   ?};
   exec('cancel_doc','pos');
::   {? _>0
::   ||
::      exec('pos_ini','pos',_a)
::   ||
      exec('pos_ini','pos')
::   ?}
?};
_suspend


\pos_suspension_list
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Wyświetlenie listy zawieszonych dokumentów
::----------------------------------------------------------------------------------------------------------------------
:: (symbol zaw, symbol dok, data, kwota netto i brutto)
params_set(params_get());
_suspend:=1;
   _where:= {? OZ.POSCZYN='N' || '\'P\'' || '\'G\'' ?};
::   FAKS.WHERE='P' &  | FAKS.WHERE='G' & OZ.POSCZYN='T'

   FAKS.cntx_psh();
   FAKS.prefix();
   FAKS.f_clear();
   FAKS.f_set(,,'FAKS.AKC=''N'' and FAKS.POSSZ<>'''' and FAKS.WHERE=:_a', _where);
   _count_faks:=FAKS.f_size();
   {? _count_faks=1 & var_pres('__temp_pos_nag_ref')>0
   ||
      {? FAKS.f_first()
      ||
         {? $__temp_pos_nag_ref=$FAKS.ref()
         ||
            _count_faks:=0
         ?}
      ?}
   ?};
   FAKS.f_clear();
   FAKS.cntx_pop();
   exec('lsp_fak_filtr','lsp');

{? _count_faks=0
||
   _suspend:=0;
   FUN.info('Brak zawieszonych dokumentów'@)
?};

{? var_pres('__temp_pos_nag_ref')>0 & _suspend=1
||
   {? FUN.ask('Czy chcesz zawiesić aktualny dokument?'@)
   ||
      _suspend:=exec('pos_suspension_save','pos',var_pres('__temp_pos_nag_ref'))
   ||
      _suspend:=0
   ?}
?};

{? _suspend=1
||
   _stanuser:=null;
   _oz_stanuser:=OZ.FIS;
   FAKS.cntx_psh();
   FAKS.prefix();
   FAKS.f_clear();
   FAKS.f_set(,,'FAKS.AKC=''N'' and FAKS.POSSZ<>'''' and FAKS.WHERE=:_a', _where);
   _faks_temp:=params_exec('win_dokument','pos_win');
   _faks:={? _faks_temp>0 || FAKS.ref() || null ?};
   {? _faks_temp>0
   ||
      _stanuser:=FAKS.STANUSER
   ?};
   FAKS.f_clear();
   FAKS.cntx_pop();
   exec('lsp_fak_filtr','lsp');
   {? _faks_temp
   ||
      _wczytac:=1;
      {? _oz_stanuser<>null & _stanuser<>_oz_stanuser
      ||
         {? FUN.choice('Dokument był utworzony na innym stanowisku. Czy nadal chcesz go wczytać?'@,1,'Tak'@,'Nie'@,,,2)<>1
         ||
            _wczytac:=0
         ?}
::   exec('pos_suspension_save','pos',_faks)
      ?};
      {? _wczytac
      ||
         exec('pos_ini','pos',_faks)
      ?}
   ?}
?};
~~


\pos_suspension_change
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Wczytanie zawieszonego dokumentu wybranego z listy \pos_suspension_list/pos.fml
::----------------------------------------------------------------------------------------------------------------------
sel_exit();
~~


\cart_nav
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.22]
:: OPIS: Przewijanie listy towarów w koszyku góra / dół
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_ac:='';
{? _>0 ||
   {? _a='up' | _a='down'
   ||
      _ac:=_a
   ?}
?};
{? _ac<>''
||
   _tab:=exec('get_selection','#desktop','','pos1','cart_01@pos1');
   {? _tab.first()
   ||
      _tab_key:=_tab.KEY;
      {? __tab_cart_list.find_key(_tab_key)
      ||
         {? _ac='down'
         ||
            __tab_cart_list.next()
         |? _ac='up'
         ||
            __tab_cart_list.prev()
         ?};
         _tab_key:=__tab_cart_list.REF
      ?};
      exec('set_value','#desktop','','pos1','cart_01@pos1',_tab_key)
::      exec('refresh_element','#desktop','','pos1','cart_01@pos1',,_tab_key)
   ?}
?};
~~

:Sign Version 2.0 jowisz:1045 2024/01/25 12:27:50 9905cde4a5bcb73e0d51d6b8dd829245c97aabefed7baf97672062147f1a0836309b00de093acd5f1ddba0d0dd7f586e75a74c3c16c34fdfc19a51a649d46ec8c5d06ce1bf55715015bfd31af4f0e730b2021eef36d0aee10412c457ceca40734e799b7cccabce1d9e755b758137a8d3bf20563a56dae248c1735ae657b3f672

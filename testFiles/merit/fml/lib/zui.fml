:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zui.fml
:: Utworzony: 09.01.2019
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Formuły oraz wywoływacze obszaru ZUI
::======================================================================================================================


\zui_mwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Główna formuła obszaru ZUI_MWA - MacroWebAPI
::----------------------------------------------------------------------------------------------------------------------
exec('manager','__mwa')


\zui_cli
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Główna formuła obszaru ZUI_CLI - MacroWebApiCLIENT
::----------------------------------------------------------------------------------------------------------------------
exec('define','sync_mwa')


\mwac_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.14]
:: OPIS: Funkcja zapisu do rejestru zadarzeń MWAC
::   WE: _a - nazwa metody
::       _b - url
::       _c - metoda (GET, POST, PUT...)
::       _d - status
::       _e - początek wywołania (utc)
::       _f - koniec wywołania (utc)
::       _g - treść request
::       _h - treść response
::       _i - nagłówek request
::       _j - nagłówek response
::   WY: identyfikator zapisu w logu
::----------------------------------------------------------------------------------------------------------------------
exec('mwac_log','#mwac',_a,_b,_c,_d,_e,_f,_g,_h,_i,_j)


\mwac_log_app
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.14]
:: OPIS: Funkcja dopisania informacji aplikacyjnych do rejestru zdarzeń MWAC
::       UWAGA: domyślnie nadpisuje poprzednie wartości
::   WE: _a - identyfikator zapisu w logu
::       _b - status biznesowy
::       _c - opis dodatkowy
::       [_d] - nadpisywanie [1] / dopisywanie 0
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_d')=type_of(0)
|| _overwrite:=_d
|| _overwrite:=1
?};
exec('mwac_log_app','#mwac',_a,_b,_c,_overwrite)


\mwac_log_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.14]
:: OPIS: Funkcja czyszczenia rejestru zdarzeń MWAC
::----------------------------------------------------------------------------------------------------------------------
exec('mwac_log_clear','#mwac')


\mwac_log_html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Generuje html z informacjami o przetworzonych rekordach
:: WE: _a - Ilość dni (domyślnie 7)
::----------------------------------------------------------------------------------------------------------------------
_days:={? var_pres('_a')=type_of(1) & _a>0 || _a || 7 ?};
_time:=tm_form(tm_stamp((date()-_days)~1,(date()-_days)~2,(date()-_days)~3,time~1,time~2,time~3));
_sql:='select APP_STAT, HTTPSTAT from @S01 where DATETIME>\'%1\' order by APP_STAT'[_time];
_log:=sql(_sql);
_html:='';
_html_td:="
   '<tr [[STYLE_TABLE_TR]]>
   <td [[STYLE_TABLE_TD]]>%1</td>
   <td [[STYLE_TABLE_TD]]>%2</td>
   <td [[STYLE_TABLE_TD]]>%3</td>
   <td [[STYLE_TABLE_TD]]>%4</td>
   </tr>'[_a,_b,_c,_d]
";
_app_stat:='';
{? _log.first()
||
   _html:=
      '<p style="text-align: center;">Informacja o wysłanych komunikatach z %1:</p>
      <table [[STYLE_TABLE]]>
      <thead>
      <tr [[STYLE_TABLE_TR]]>
      <th [[STYLE_TABLE_TH]]>Przeznaczenie komunikat&oacute;w</th>
      <th [[STYLE_TABLE_TH]]>Ilość komunikat&oacute;w</th>
      <th [[STYLE_TABLE_TH]]>Ilość nieprawidłowych komunikat&oacute;w</th>
      <th [[STYLE_TABLE_TH]]>Ilość komunikat&oacute;w z błędem biznesowym</th>
      </tr>
      </thead>
      <tbody>
      '[{? _days=1 || 'ostatniego dnia' ||'ostatnich '+$_days+' dni' ?}];
   {!
   |?
:: nowy wiersz tabeli
      {? _app_stat='' | ~(_log.APP_STAT*_app_stat)
      ||
         {? _app_stat<>'' || _html+=_html_td(_app_stat,$_ik,$_ink,$_inpk) ?};
         _app_stat:=(((1-_log.APP_STAT)*'|')-1)+(1-_log.APP_STAT);
         _ik:=1;
         {? _log.HTTPSTAT<>200 & _log.HTTPSTAT<>201   || _ink:=1; _inpk:=0
         |? _log.APP_STAT*'|R:0|'                     || _ink:=0; _inpk:=1
                                                      || _ink:=0; _inpk:=0
         ?}
      ||
         _ik+=1;
         {? _log.HTTPSTAT<>200 & _log.HTTPSTAT<>201   || _ink+=1
         |? _log.APP_STAT*'|R:0|'                     || _inpk+=1
         ?}
      ?};
      _log.next()
   !};
   {? _app_stat<>'' || _html+=_html_td(_app_stat,$_ik,$_ink,$_inpk) ?};
   _html+=
      '</tbody>
      </table>'
?};

_html


\mwac_log_title
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Tytuł maila raportu
::----------------------------------------------------------------------------------------------------------------------
'Informacja o wysłanych komunikatach firmy '@ +(exec('czytaj','#stalesys',,XINFO); XINFO.NAZ)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 8ef7c0d22400b1c6d075f4601fe3b21b593209aba96f54060645ca3eb1b57c727e8fbd7508b258ff025c0e752ff80cec2b65e2d01b39dd17acd423ecf81f80cfb85b4bdcc2296e086cf84aca388f6bb9567504b4ff8d6751c0235b31169949547b254cb8fe8dafb39f8e30c6653ac933b9ba92603af4a75cea450f572280c02b

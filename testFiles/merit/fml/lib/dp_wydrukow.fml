:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: Nowy1.fml
:: Utworzony: 05.11.2021
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Domyślne parametry wydruków
::======================================================================================================================


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: domyślne parametry wydruków
::   WE: _a - 'KH'/'TD'/'TZ' miejsce wywołania
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_set('dpw_view',_a);
{? _a='TD'
||
   WYDRUKIL.index('DP_TD');
   WYDRUKIL.prefix('T',TYPYSP.uidref())
|?_a='TZ'
||
   WYDRUKIL.index('DP_TD');
   WYDRUKIL.prefix('T',TYPYZAM.uidref())
||
   WYDRUKIL.index('DP_KH');
   WYDRUKIL.prefix('T',KH.ref())
?};
_wer:=WYDRUKIL.mk_sel('Domyślne parametry wydruków'@,,,'dpw_%1'[-_a],,,,,'U');
{? _a='KH' || WYDRUKIL.win_fld(_wer,,'DP_TD2S',,,15,,,'Typ dokumentu'@) ?};
WYDRUKIL.win_fld(_wer,,'DP_TWYDR',,,31,,,'Tytuł'@);
WYDRUKIL.win_fld(_wer,,'DP_NWYDR',,,31,,,'Nazwa'@);
WYDRUKIL.win_fld(_wer,,'WYDRUK',,,8,,,'Wzorzec'@);
WYDRUKIL.win_fld(_wer,,'DP_WHERE',,,1,,,'Skąd'@);
{? _a='TD' | _a='TZ' || WYDRUKIL.win_fld(_wer,,'DP_KH','NAZ',,50,,,'Kontrahent'@) ?};
::WYDRUKIL.win_fld(_wer,,'DP_TYP',,,10,,,'Typ'@);
{? _a='TD' | _a='TZ'
||
   _fb:="params_exec('dolacz','dp_wydrukow',0)";
   WYDRUKIL.win_act(_wer,1,'Formuła','Dołącz'@@,,,_fb,,1);
   WYDRUKIL.win_act(_wer,,'Formuła','Dołącz'@@,,,_fb);
   _fb:="params_exec('dolacz','dp_wydrukow',1)";
   WYDRUKIL.win_act(_wer,1,'Formuła','D&ołącz dla kontrahenta'@@,,,_fb);
   WYDRUKIL.win_act(_wer,,'Formuła','D&ołącz dla kontrahenta'@@,,,_fb)
||
   _fb:="params_exec('dolacz','dp_wydrukow',1)";
   WYDRUKIL.win_act(_wer,1,'Formuła','Dołącz'@@,,,_fb,,1);
   WYDRUKIL.win_act(_wer,,'Formuła','Dołącz'@@,,,_fb)
?};
_fb:="exec('popraw','dp_wydrukow')";
WYDRUKIL.win_act(_wer,,'Formuła','Popraw'@@,,,_fb,,1);
WYDRUKIL.win_act(_wer,,'Usuń');
WYDRUKIL.win_act(_wer,,'Kolejność');
_fb:="exec('wyswietl','dp_wydrukow')";
WYDRUKIL.win_act(_wer,,'Wyświetl',,,,_fb);
WYDRUKIL.win_sel(_wer);
WYDRUKIL.select()


\dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Domyślne parametry wydruków - Dołącz
::   WE: _a - 0/1 - kontrahenta
::       [_b] - KH.ref
::       [_c] - [0]/1 - grupowo
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_czy_kh:=_a;
_kh:={? var_pres('_b')=type_of(null()) || _b || null() ?};
_grupa:={? var_pres('_c')=type_of(0) || _c || 0 ?};
_dpw_view:=params_get().dpw_view;
{? _dpw_view='TD' | _dpw_view='TZ'
|| _tp_jak:=_dpw_view
|| _tp_jak:='';
   _grupa:=0
?};

_continue:=1;

{? (_dpw_view='TD' | _dpw_view='TZ') & _czy_kh
||
   {? _kh=null()
   ||
      KH.cntx_psh();
      KH.win_sel('SEL');
      KH.index('KOD');
      KH.prefix(2);
      {? KH.select()
      ||
         _kh:=KH.ref()
      ?};
      KH.cntx_pop()
   ?};
   {? _kh=null()
   ||
      FUN.info('Nie wybrano kontrahenta.'@);
      return()
   ?}
?};

{? _dpw_view='TD'
|| {? _grupa
   || _tb_sel:=TYPYSP.sel_aget()
   || _tb_sel:=tab_tmp(1,
            'REF','INTEGER','#REF');
      _tb_sel.REF:=#TYPYSP.ref();
      _tb_sel.add()
   ?}
|? _dpw_view='TZ'
|| {? _grupa
   || _tb_sel:=TYPYZAM.sel_aget()
   || _tb_sel:=tab_tmp(1,
            'REF','INTEGER','#REF');
      _tb_sel.REF:=#TYPYZAM.ref();
      _tb_sel.add()
   ?}
|| _tb_sel:=tab_tmp(1,
          'REF','INTEGER','#REF')
?};

TYPYSP.cntx_psh();
TYPYSP.prefix();
TYPYZAM.cntx_psh();
TYPYZAM.prefix();

{? _dpw_view='KH'
||
   _kh:=KH.ref();
   _choice:=FUN.choice('Typ dokumentu:'@,,'Dokument &sprzedaży'@,'Zamówienie &dostawy'@);
   {? _choice=1
   || _tp_jak:='TD'
   |? _choice=2
   || _tp_jak:='TZ'
   || _continue:=0
   ?};
   {? _continue
   || {? _tp_jak='TZ'
      || TYPYZAM.index('TYP');
         TYPYZAM.prefix('D');
         TYPYZAM.win_sel('SEL');
         {? TYPYZAM.select()
         || _tb_sel.REF:=#TYPYZAM.ref();
            _tb_sel.add()
         || _continue:=0
         ?}
      || TYPYSP.index('TYPYSP');
         TYPYSP.prefix('N');
         TYPYSP.win_sel('WYST');
         {? TYPYSP.select()
         || _tb_sel.REF:=#TYPYSP.ref();
            _tb_sel.add()
         || _continue:=0
         ?}
      ?}
   ?}
?};

_typysp_kor:='';
_tb_sel.first();
{? _tp_jak='TD'
|| TYPYSP.seek(_tb_sel.REF);
   _typysp_kor:=TYPYSP.KOR
|? _tp_jak='TZ'
|| TYPYZAM.seek(_tb_sel.REF)
?};

{? _continue>0
||
   _Wydruki:=tab_tmp(1
      ,'DP_TWYDR','STRING[50]','Tytuł'
      ,'DP_NWYDR','STRING[31]','Nazwa'
      ,'WYDRUK','STRING[8]','Wzorzec'
      ,'DP_TYP','STRING[10]','Typ'
      ,'WHERE','STRING[1]','Where'
   );
   _add:="_a.DP_NWYDR:=_b; _a.WYDRUK:=_c; _a.DP_TWYDR:=_d; _a.DP_TYP:=_e; _a.WHERE:=_f; _a.add()";
   _title:="{? _a.find_key(_b,) || _a.TITLE || _c ?}";
   {? _tp_jak='TZ'
   || _Rep_info:=rep_info('lzk_*',1)
   || _Rep_info:=rep_info('lsp_*',1)
   ?};
   _ndx:=_Rep_info.ndx_tmp(,,'FILENAME',,);
   _Rep_info.index(_ndx);
   _typ_wydr:=exec('typ_wydruk','dp_wydrukow');
   _typ_pdf_:=exec('typ_pdf','dp_wydrukow');
   _typ_edok:=exec('typ_edokument','dp_wydrukow');
   {? _tp_jak='TD' & _typysp_kor='N'
   ||
::   _fname:='lsp_fak_new'; _add(_Wydruki,_fname,'WYDR_FAK',_title(_Rep_info,_fname,'Faktura'),_typ_wydr,'');
::   _fname:='lsp_fakpnew'; _add(_Wydruki,_fname,'WYDR_FAK',_title(_Rep_info,_fname,'Faktura'),_typ_pdf_,'');
      _fname:='lsp_fakenew'; _add(_Wydruki,_fname,'WYDR_FAK',_title(_Rep_info,_fname,'Faktura'),_typ_edok,'');
      _fname:='lsp_fakenew'; _add(_Wydruki,_fname,'WYDR_FAK',_title(_Rep_info,_fname,'Faktura'),_typ_edok,'G');
      _fname:='lsp_fakenew'; _add(_Wydruki,_fname,'WYDR_FAK',_title(_Rep_info,_fname,'Faktura'),_typ_edok,'L');
      _fname:='lsp_fakenew'; _add(_Wydruki,_fname,'WYDR_FAK',_title(_Rep_info,_fname,'Faktura'),_typ_edok,'M');
      _fname:='lsp_fakenew'; _add(_Wydruki,_fname,'WYDR_FAK',_title(_Rep_info,_fname,'Faktura'),_typ_edok,'P');
      ~~

   |? _tp_jak='TD' & _typysp_kor='T'
   ||
::   _fname:='lsp_kors_new'; _add(_Wydruki,_fname,'WYDR_KOR',_title(_Rep_info,_fname,'Korekta'),_typ_wydr,'');
::   _fname:='lsp_korspnew'; _add(_Wydruki,_fname,'WYDR_KOR',_title(_Rep_info,_fname,'Korekta'),_typ_pdf_,'');
      _fname:='lsp_korsenew'; _add(_Wydruki,_fname,'WYDR_KOR',_title(_Rep_info,_fname,'Korekta'),_typ_edok,'');
      _fname:='lsp_korsenew'; _add(_Wydruki,_fname,'WYDR_KOR',_title(_Rep_info,_fname,'Korekta'),_typ_edok,'G');
      _fname:='lsp_korsenew'; _add(_Wydruki,_fname,'WYDR_KOR',_title(_Rep_info,_fname,'Korekta'),_typ_edok,'L');
      _fname:='lsp_korsenew'; _add(_Wydruki,_fname,'WYDR_KOR',_title(_Rep_info,_fname,'Korekta'),_typ_edok,'M');
      _fname:='lsp_korsenew'; _add(_Wydruki,_fname,'WYDR_KOR',_title(_Rep_info,_fname,'Korekta'),_typ_edok,'P');
::   _fname:='lsp_kornag'; _add(_Wydruki,_fname,'WYDR_KZ',_title(_Rep_info,_fname,'Korekta danych nagłówkowych'),_typ_wydr,'');
::   _fname:='lsp_kornap'; _add(_Wydruki,_fname,'WYDR_KZ',_title(_Rep_info,_fname,'Korekta danych nagłówkowych'),_typ_pdf_,'');
      _fname:='lsp_kornae'; _add(_Wydruki,_fname,'WYDR_KZ',_title(_Rep_info,_fname,'Korekta danych nagłówkowych'),_typ_edok,'');
      ~~

   |? _tp_jak='TD' & _typysp_kor='Z'
   ||
::   _fname:='lsp_korzb'; _add(_Wydruki,_fname,'WYDR_KZ',_title(_Rep_info,_fname,'Korekta zbiorcza faktur'),_typ_wydr,'');
::   _fname:='lsp_korzp'; _add(_Wydruki,_fname,'WYDR_KZ',_title(_Rep_info,_fname,'Korekta zbiorcza faktur'),_typ_pdf_,'');
      _fname:='lsp_korze'; _add(_Wydruki,_fname,'WYDR_KZ',_title(_Rep_info,_fname,'Korekta zbiorcza faktur'),_typ_edok,'');
::   _fname:='lsp_korzb_poz'; _add(_Wydruki,_fname,'WYDR_KZ',_title(_Rep_info,_fname,'Korekta zbiorcza faktur - pozycje'),_typ_wydr,'');
::   _fname:='lsp_korzp_poz'; _add(_Wydruki,_fname,'WYDR_KZ',_title(_Rep_info,_fname,'Korekta zbiorcza faktur - pozycje'),_typ_pdf_,'');
::   _fname:='lsp_korze_poz'; _add(_Wydruki,_fname,'WYDR_KZ',_title(_Rep_info,_fname,'Korekta zbiorcza faktur - pozycje'),_typ_edok,'');
      ~~

   |? _tp_jak='TZ'
   || _fname:='lzk_zdzam_01'; _add(_Wydruki,_fname,'WYDR_ZD',_title(_Rep_info,_fname,'Zamówienie dostawy'),_typ_edok,'');
      ~~

   ?};
   _Rep_info.ndx_drop(_ndx);
   _wer:=_Wydruki.mk_sel('Lista wydruków'@,,,'dpw_wydruki',,,,,'U');
   _Wydruki.win_fld(_wer,,'DP_TWYDR',,,50,,,'Tytuł'@);
   _Wydruki.win_fld(_wer,,'DP_NWYDR',,,31,,,'Nazwa'@);
   _Wydruki.win_fld(_wer,,'WYDRUK',,,8,,,'Wzorzec'@);
   _Wydruki.win_fld(_wer,,'DP_TYP',,,10,,,'Typ'@);
   _Wydruki.win_fld(_wer,,'WHERE',,,2,,,'Skąd'@);
   _Wydruki.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
   _Wydruki.win_sel(_wer)
?};

{? _continue<=0
||
   ~~

|? ~_Wydruki.first()
||
   {? _tp_jak='TZ'
   || FUN.info('Definiowanie domyślnych parametrów wydruku dla typu dokumentu %1 jest niedostępne.'@[TYPYZAM.T])
   || FUN.info('Definiowanie domyślnych parametrów wydruku dla typu dokumentu %1 jest niedostępne.'@[TYPYSP.T])
   ?}

|? ~_Wydruki.next() | _Wydruki.select()
||
   _continue:=1;
   _nwydr:=_Wydruki.DP_NWYDR;
   _twydr:=_Wydruki.DP_TWYDR;
   _typ:=_Wydruki.DP_TYP;
   _where:=_Wydruki.WHERE;
   _wydruk:=_Wydruki.WYDRUK;
   _tb_sel.first();

   {? _grupa

   || _tb_sel1:=tab_tmp(1,
            'REF','INTEGER','#REF',
            'UIDREF','STRING[48]','UIDREF',
            'T','STRING[8]','Typ',
            'NAZ','STRING[60]','Nazwa',
            'OK','INTEGER','OK',
            'JEST','INTEGER','Jest');
      _ist:=0;
      _nadpis:=0;
      {!
      |? {? _tp_jak='TD'
         || TYPYSP.seek(_tb_sel.REF);
            _tt:=TYPYSP.T;
            _tn:=TYPYSP.NAZ;
            _td:=TYPYSP.uidref();
            _ok:=(TYPYSP.KOR=_typysp_kor)
         |? _tp_jak='TZ'
         || TYPYZAM.seek(_tb_sel.REF);
            _tt:=TYPYZAM.T;
            _tn:=TYPYZAM.NAZ;
            _td:=TYPYZAM.uidref();
            _ok:=1
         ?};
         {? _ok
         || WYDRUKIL.cntx_psh();
            WYDRUKIL.index('DP');
            WYDRUKIL.prefix('T',_td,_typ,_nwydr,_kh,_where,);
            _jest:=WYDRUKIL.first();
            {? _jest & ~_ist
            || _ist:=1
            ?};
            WYDRUKIL.cntx_pop()
         || _jest:=0
         ?};
         _tb_sel1.REF:=_tb_sel.REF;
         _tb_sel1.UIDREF:=_td;
         _tb_sel1.T:=_tt;
         _tb_sel1.NAZ:=_tn;
         _tb_sel1.OK:=_ok;
         _tb_sel1.JEST:=_jest;
         _tb_sel1.add(0);
         _tb_sel.next()
      !};

      {? _ist & FUN.ask('Czy nadpisać już istniejące domyślne parametry wydruków?'@)
      || _nadpis:=1
      ?};

      WYDRUKIL.index('DP');
      WYDRUKIL.prefix('T');

      _tb_sel1.first();

      _par_get_obj:=exec('par_get','dp_wydrukow',1,_wydruk,_nwydr,_twydr,_kh,_typ,_tb_sel1.UIDREF,_where,,1);
      {? _par_get_obj.RESULT
      || _tb_sel1.JEST:=~_par_get_obj.NEW;
         exec('param_w_set_efld_opt','dp_wydrukow',_wydruk,0);
         PARAM_W.win_edit(_wydruk);
         exec('param_w_hdr_edit','dp_wydrukow',_twydr,_kh,0);
         _valid:=$('exec(\'valid_%1\',\'dp_wydrukow\')'[~_wydruk]);
         {? ~PARAM_W.edit(_valid)
         || _continue:=0;
            {? ~_tb_sel1.JEST
            || WYDRUKIL.del()
            ?}
         ?};
         exec('param_w_set_efld_opt','dp_wydrukow',_wydruk,1);
         exec('param_w_hdr_edit','dp_wydrukow',_twydr,_kh,1)
      || _continue:=0
      ?};

      {? _continue

      || exec('ini_kom','#message','Domyślne parametry wydruków'@);

         _pocz:=1;
         {!
         |? {? _tb_sel1.OK
            || {? ~_pocz
               || &_par_get_obj;
                  _par_get_obj:=exec('par_get','dp_wydrukow',1,_wydruk,_nwydr,_twydr,_kh,_typ,_tb_sel1.UIDREF,_where,
                     1,1);
                  _tb_sel1.JEST:=~_par_get_obj.NEW
               ?};
               {? _par_get_obj.RESULT
               || {? _tb_sel1.JEST
                  || {? _nadpis
                     || exec('par_set','dp_wydrukow');
                        _txt:='Poprawiono domyślne parametry wydruku'@;
                        _ikona:=1
                     || _txt:='Nie zapisano - już są domyślne parametry wydruku'@;
                        _ikona:=4
                     ?}
                  || exec('par_set','dp_wydrukow');
                     _txt:='Zapisano domyślne parametry wydruku'@;
                     _ikona:=1
                  ?}
               || _txt:='Nie zapisano - błąd'@;
                  _ikona:=4
               ?}
            || _txt:='Nie zapisano - niewłaściwy typ dokumentu'@;
               _ikona:=4
            ?};
            exec('add_kom','#message',_tb_sel1.T + ' - ' + _tb_sel1.NAZ,_ikona,_txt);
            _pocz:=0;
            _tb_sel1.next()
         !};

         exec('end_kom','#message')

      ?}

   || {? _tp_jak='TZ'
      || _td:=exec('FindAndGet','#table',TYPYZAM,_tb_sel.REF,TYPYZAM.name(),"uidref()",'')
      || _td:=exec('FindAndGet','#table',TYPYSP,_tb_sel.REF,TYPYSP.name(),"uidref()",'')
      ?};
      WYDRUKIL.cntx_psh();
      WYDRUKIL.index('DP');
      WYDRUKIL.prefix('T',_td,_typ,_nwydr,_kh,_where,);
      {? WYDRUKIL.first()
      ||
         {? _where=''
         ||
            {? _kh
            ||
               FUN.info('Dodano już parametry domyślne dla wydruku %1 kontrahenta %2.'@[_nwydr,
                  exec('FindAndGet','#table',KH,_kh,,"KH.NAZ",'')])
            ||
               FUN.info('Dodano już parametry domyślne dla wydruku %1.'@[_nwydr])
            ?}
         ||
            {? _kh
            ||
               FUN.info('Dodano już parametry domyślne dla wydruku %1 (Skąd %2) kontrahenta %3.'@[_nwydr,_where,
                  exec('FindAndGet','#table',KH,_kh,,"KH.NAZ",'')])
            ||
               FUN.info('Dodano już parametry domyślne dla wydruku %1 (Skąd %2).'@[_nwydr,_where])
            ?}
         ?};
         _continue:=0
      ?};
      WYDRUKIL.cntx_pop();

      {? _continue>0
      ||
         {? exec('par_get','dp_wydrukow',1,_wydruk,_nwydr,_twydr,_kh,_typ,_td,_where)
         ||
            exec('param_w_set_efld_opt','dp_wydrukow',_wydruk,0);
            PARAM_W.win_edit(_wydruk);
            exec('param_w_hdr_edit','dp_wydrukow',_twydr,_kh,0);
            _valid:=$('exec(\'valid_%1\',\'dp_wydrukow\')'[~_wydruk]);
            {? PARAM_W.edit(_valid)
            ||
               exec('par_set','dp_wydrukow')
            ||
               WYDRUKIL.del()
            ?};
            exec('param_w_set_efld_opt','dp_wydrukow',_wydruk,1);
            exec('param_w_hdr_edit','dp_wydrukow',_twydr,_kh,1)
         ?}
      ?}

   ?}

?};

TYPYSP.cntx_pop();
TYPYZAM.cntx_pop();

~~


\popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Domyślne parametry wydruków - Popraw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wydruk:=WYDRUKIL.WYDRUK;
_nwydr:=WYDRUKIL.DP_NWYDR;
_twydr:=WYDRUKIL.DP_TWYDR;
_kh:=WYDRUKIL.DP_KH;
_typ:=WYDRUKIL.DP_TYP;
_td:=exec('FindAndGet','#table',ref_tab(WYDRUKIL.DP_TD),WYDRUKIL.DP_TD,,"uidref()",'');
_where:=WYDRUKIL.DP_WHERE;
{? exec('par_get','dp_wydrukow',1,_wydruk,_nwydr,_twydr,_kh,_typ,_td,_where)
||
   exec('param_w_set_efld_opt','dp_wydrukow',_wydruk,0);
   PARAM_W.win_edit(_wydruk);
   exec('param_w_hdr_edit','dp_wydrukow',_twydr,_kh,0);
   _valid:=$('exec(\'valid_%1\',\'dp_wydrukow\')'[~_wydruk]);
   {? PARAM_W.edit(_valid)
   ||
      exec('par_set','dp_wydrukow')
   ?};
   exec('param_w_set_efld_opt','dp_wydrukow',_wydruk,1);
   exec('param_w_hdr_edit','dp_wydrukow',_twydr,_kh,1)
?}


\param_w_set_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Ustawia opcje pól w okienku redakcji zmiennej PARAM_W
::   WE: _a - akronim okienka redakcji
::       _b - 0/1 - clear
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_red:=_a;
{? _b
||
   PARAM_W.efld_opt(_win_red,'enable=1',,'KOPIA');
   PARAM_W.efld_opt(_win_red,'enable=1',,'DR_DUPL');
   PARAM_W.efld_opt(_win_red,'enable=1',,'DUPLIKAT');
   PARAM_W.efld_opt(_win_red,'enable=1',,'WALH')
||
   PARAM_W.efld_opt(_win_red,'enable=0',,'KOPIA');
   PARAM_W.efld_opt(_win_red,'enable=0',,'DR_DUPL');
   PARAM_W.efld_opt(_win_red,'enable=0',,'DUPLIKAT');
   PARAM_W.efld_opt(_win_red,'enable=0',,'WALH')
?}


\param_w_hdr_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: hdr_edit zmiennej PARAM_W
::   WE: _a - tytuł
::       _b - kontrahent
::       _c - 0/1 - clear
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_twydr:=_a;
_kh:=_b;
PARAM_W.hdr_edit();
{? ~_c
||
   _tail:=' dla "%1"'@[_twydr];
   {? _kh || _tail+=' dla kontrahenta %1'@[exec('FindAndGet','#table',KH,_kh,,"KH.NAZ",'')] ?};
   PARAM_W.hdr_edit(_tail)
?}


\wyswietl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Domyślne parametry wydruków - Wyświetl
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wydruk:=WYDRUKIL.WYDRUK;
_nwydr:=WYDRUKIL.DP_NWYDR;
_twydr:=WYDRUKIL.DP_TWYDR;
_kh:=WYDRUKIL.DP_KH;
_typ:=WYDRUKIL.DP_TYP;
_td:=exec('FindAndGet','#table',ref_tab(WYDRUKIL.DP_TD),WYDRUKIL.DP_TD,,"uidref()",'');
_where:=WYDRUKIL.DP_WHERE;
{? exec('par_get','dp_wydrukow',1,_wydruk,_nwydr,_twydr,_kh,_typ,_td,_where)
||
   exec('param_w_set_efld_opt','dp_wydrukow',_wydruk,0);
   PARAM_W.win_edit(_wydruk);
   exec('param_w_hdr_edit','dp_wydrukow',_twydr,_kh,0);
   PARAM_W.display();
   exec('param_w_set_efld_opt','dp_wydrukow',_wydruk,1);
   exec('param_w_hdr_edit','dp_wydrukow',_twydr,_kh,1)
?}


\par_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Pobranie parametrów wydruku
::   WE: _a - 0/1 domyślne parametry wydruku
::       _b - WYDRUKIL.WYDRUK - wzorzec
::       [_c] - WYDRUKIL.DP_NWYDR - nazwa wydruku
::       [_d] - WYDRUKIL.DP.TWYDR - tytuł wydruku
::       [_e] - WYDRUKIL.DP_KH - kontrahent
::       [_f] - WYDRUKIL.TYP - typ parametrów domyślnych Wydruk/PDF/E-dokument
::       [_g] - WYDRUKIL.TD - typ dokumentu
::       [_h] - WYDRUKIL.DP_WHERE - typ dokumentu
::       [_i] - [0]/1 - czy tylko znaleźć / dopisać WYDRUKIL bez ustawiania PARAM_W.*
::       [_j] - [0]/1 - czy zwracać obj('RESULT','NEW')
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
:: Jak obsługujemy zależne od FAKS lub sposobu drukowania?:
:: - PARAM_W.DR_RAB
_dpw:=_a;
_wydruk:=_b;
_nwydr:={? var_pres('_c')=type_of('') || _c || '' ?};
_twydr:={? var_pres('_d')=type_of('') || _d || '' ?};
_kh:={? var_pres('_e')=type_of(null()) || _e || null() ?};
_typ:={? var_pres('_f')=type_of('') || _f || '' ?};
_td:={? var_pres('_g')=type_of('') || _g || '' ?};
_where:={? var_pres('_h')=type_of('') || _h || '' ?};
_bez_paramw:={? var_pres('_i')=type_of(0) || _i || 0 ?};
_res_obj_czy:={? var_pres('_j')=type_of(0) || _j || 0 ?};
_res_obj:=obj_new('RESULT','NEW');
_res_obj.RESULT:=1;
_res_obj.NEW:=0;
_first:=0;
WYDRUKIL.cntx_psh();
{? _dpw
||
   WYDRUKIL.index('DP');
   WYDRUKIL.prefix('T',_td,_typ,_nwydr,_kh,_where,);
   _first:=WYDRUKIL.first()
||
   WYDRUKIL.index('WYDRUKI');
   WYDRUKIL.prefix(OPERATOR.USER().KOD,_wydruk);
   _first:=WYDRUKIL.first()
?};
_ref:={? _first || WYDRUKIL.ref() || null() ?};
WYDRUKIL.cntx_pop();
{? _ref=null()
||
   {? ~_bez_paramw
   || PARAM_W.KOPIA:=2;
      PARAM_W.ZAM:='N';
      PARAM_W.WZ:='N';
::
      PARAM_W.DR_DUPL:='N';
      PARAM_W.DUPLIKAT:=date(0,0,0);
::
      PARAM_W.SLOWNIE:='T';
      PARAM_W.JEZYK:=null;
      PARAM_W.DR_RAB:='T';
      PAR_WYDR.SHAD:=3;
::
      PARAM_W.INDEKS:='T';
      PARAM_W.NAZWA:='T';
      PARAM_W.MC:='N';
      PARAM_W.UWAGI:='N';
      PARAM_W.KODOBCY:='N';
      PARAM_W.DK_C:='N';
::
      PARAM_W.DTPREAL:='N';
      PARAM_W.ZDSWD:='N';
      PARAM_W.PROJEKTY:='N';
::
      PARAM_W.WALH:='N'
   ?};
   {? _dpw
   ||
      ST.AR:=date()~1;
      ST.AM:=date()~2
   ?};
   WYDRUKIL.blank();
   WYDRUKIL.WYDRUK:=_wydruk;
   {? _dpw
   ||
      {? ~_bez_paramw
      || PARAM_W.KOPIA:=0
      ?};
      WYDRUKIL.DP:='T';
      WYDRUKIL.DP_TD:=_td;
      WYDRUKIL.DP_TD2S:=exec('record','#to_string',WYDRUKIL.DP_TD);
      WYDRUKIL.DP_TYP:=_typ;
      WYDRUKIL.DP_TWYDR:=_twydr;
      WYDRUKIL.DP_NWYDR:=_nwydr;
      WYDRUKIL.DP_KH:=_kh;
      WYDRUKIL.DP_WHERE:=_where
   ||
      WYDRUKIL.USERS:=OPERATOR.USER().KOD
   ?};
   WYDRUKIL.FAKS_OD:='IN';
   _res_obj.RESULT:=WYDRUKIL.add();
   _res_obj.NEW:=1

|? WYDRUKIL.seek(_ref) & ~_bez_paramw
||
   {? WYDRUKIL.WYDRUK='WYDR_FAK'
   ||
      PARAM_W.KOPIA:=WYDRUKIL.TYP_ZEST;
      PARAM_W.ZAM:=WYDRUKIL.STR1;
      PARAM_W.WZ:=WYDRUKIL.STR2;
      _par:=WYDRUKIL.FAKS_OD;
      PARAM_W.SLOWNIE:={? WYDRUKIL.CHKBOX01 || 'T' || 'N' ?};
      PARAM_W.DR_RAB:={? WYDRUKIL.CHKBOX02 | FAKS.SPOBLRAB='W'  || 'T' || 'N' ?}

   |? WYDRUKIL.WYDRUK='WYDR_KOR'
   ||
      PARAM_W.KOPIA:=WYDRUKIL.TYP_ZEST;
      PARAM_W.POZY:=WYDRUKIL.STR1;
      PARAM_W.DR_TERM:=WYDRUKIL.STR2;
      PARAM_W.ZAM:={? WYDRUKIL.CHKBOX04 || 'T' || 'N' ?};
      PARAM_W.WZ:={? WYDRUKIL.CHKBOX05 || 'T' || 'N' ?};
      _par:=WYDRUKIL.FAKS_OD;
      PARAM_W.SLOWNIE:={? WYDRUKIL.CHKBOX01 || 'T' || 'N' ?};
      PARAM_W.PODNALDR:={? WYDRUKIL.CHKBOX02 || 'T' || 'N' ?};
      PARAM_W.DR_RAB:={? WYDRUKIL.CHKBOX03 | FAKS.SPOBLRAB='W' || 'T' || 'N' ?}

   |? WYDRUKIL.WYDRUK='WYDR_KZ'
   ||
      PARAM_W.KOPIA:=WYDRUKIL.TYP_ZEST;
      PARAM_W.SLOWNIE:={? WYDRUKIL.CHKBOX01 || 'T' || 'N' ?}
   ?};
   PARAM_W.JEZYK:=WYDRUKIL.JEZYK
?};
{? ~_bez_paramw
|| _par:=WYDRUKIL.FAKS_OD;
   PARAM_W.INDEKS:={? _par*'I' || 'T' || 'N' ?};
   PARAM_W.NAZWA:={? _par*'N' || 'T' || 'N' ?};
   PARAM_W.MC:={? _par*'M' || 'T' || 'N' ?};
   PARAM_W.UWAGI:={? _par*'U' || 'T' || 'N' ?};
   PARAM_W.KODOBCY:={? _par*'K' || 'T' || 'N' ?};
   PARAM_W.DK_C:={? _par*'C' || 'T' || 'N' ?};
   {? WYDRUKIL.WYDRUK='WYDR_ZD'
   || PARAM_W.DTPREAL:={? _par*'D' || 'T' || 'N' ?};
      PARAM_W.ZDSWD:={? _par*'W' || 'T' || 'N' ?};
      PARAM_W.PROJEKTY:={? _par*'P' || 'T' || 'N' ?}
   || PARAM_W.KP:={? _par*'P' || 'T' || 'N' ?}
   ?}
?};

{? _res_obj_czy
|| _res_obj
|| _res_obj.RESULT
?}


\par_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Zapamiętanie parametrów wydruków
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? WYDRUKIL.WYDRUK='WYDR_FAK'
||
   WYDRUKIL.TYP_ZEST:=PARAM_W.KOPIA;
   WYDRUKIL.STR1:=PARAM_W.ZAM;
   WYDRUKIL.STR2:=PARAM_W.WZ;
   WYDRUKIL.STR3:=PARAM_W.DR_DUPL;
   WYDRUKIL.OD_DATY:=PARAM_W.DUPLIKAT;
   WYDRUKIL.CHKBOX01:=PARAM_W.SLOWNIE='T';
   WYDRUKIL.CHKBOX02:=PARAM_W.DR_RAB='T'

|? WYDRUKIL.WYDRUK='WYDR_KOR'
||
   WYDRUKIL.TYP_ZEST:=PARAM_W.KOPIA;
   WYDRUKIL.STR1:=PARAM_W.POZY;
   WYDRUKIL.STR2:=PARAM_W.DR_TERM;
   WYDRUKIL.STR3:=PARAM_W.DR_DUPL;
   WYDRUKIL.CHKBOX04:=PARAM_W.ZAM='T';
   WYDRUKIL.CHKBOX05:=PARAM_W.WZ='T';
   WYDRUKIL.OD_DATY:=PARAM_W.DUPLIKAT;
   WYDRUKIL.CHKBOX01:=PARAM_W.SLOWNIE='T';
   WYDRUKIL.CHKBOX03:=PARAM_W.DR_RAB='T'

|? WYDRUKIL.WYDRUK='WYDR_KZ'
||
   WYDRUKIL.TYP_ZEST:=PARAM_W.KOPIA;
   WYDRUKIL.STR3:=PARAM_W.DR_DUPL;
   WYDRUKIL.OD_DATY:=PARAM_W.DUPLIKAT;
   WYDRUKIL.CHKBOX01:=PARAM_W.SLOWNIE='T'
?};
WYDRUKIL.FAKS_OD:='';
{? PARAM_W.MC='T'      || WYDRUKIL.FAKS_OD+='M' ?};
{? WYDRUKIL.WYDRUK<>'WYDR_KZ'
||
   {? PARAM_W.INDEKS='T'  || WYDRUKIL.FAKS_OD+='I' ?};
   {? PARAM_W.NAZWA='T'   || WYDRUKIL.FAKS_OD+='N' ?};
   {? PARAM_W.UWAGI='T'   || WYDRUKIL.FAKS_OD+='U' ?};
   {? PARAM_W.KODOBCY='T' || WYDRUKIL.FAKS_OD+='K' ?};
   {? PARAM_W.DK_C='T'    || WYDRUKIL.FAKS_OD+='C' ?};
   {? WYDRUKIL.WYDRUK='WYDR_ZD'
   || {? PARAM_W.DTPREAL='T' || WYDRUKIL.FAKS_OD+='D' ?};
      {? PARAM_W.ZDSWD='T' || WYDRUKIL.FAKS_OD+='W' ?};
      {? PARAM_W.PROJEKTY='T' || WYDRUKIL.FAKS_OD+='P' ?}
   || {? PARAM_W.KP='T'      || WYDRUKIL.FAKS_OD+='P' ?}
   ?}
?};
::            WYDRUKIL.JEZYK:={? _djezyk & PARAM_W.JEZYK=_djezyk || _jezyk_ || PARAM_W.JEZYK ?};
::            {? exec('czy_grp_dr','faktury_wydruk')
::            || __GRP_DR.JEZYK:=PARAM_W.JEZYK;
::               __GRP_DR.JEZYK_KH:=PARAM_W.JEZYK_KH
::            ?};
::            {? PARAM_W.JEZYK_KH='T' & _djezyk || PARAM_W.JEZYK:=_djezyk ?};
WYDRUKIL.JEZYK:=PARAM_W.JEZYK;
WYDRUKIL.put()


\valid_wydr_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Sprawdza poprawność wypełnienia domyślnych parametrów wydruków dla wzorca WYDR_FAK
::   WE:
::   WY: '' lub akronime niepopranie wypełnionego pola
::----------------------------------------------------------------------------------------------------------------------
''


\valid_wydr_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
''


\valid_wydr_kz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
''


\valid_wydr_zd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
''


\msk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wyznacza nazwę dyskowę edokumentu
::   WE: _a - FAKS.ref()
::       _b - Wydruk/PDF/Edokument
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_faks:=_a;
_typ:=_b;
_return:='';
FAKS.cntx_psh();
FAKS.use(ref_name(_faks));
FAKS.cntx_psh();
FAKS.prefix();
{? FAKS.seek(_faks)
||
   _td:=exec('FindAndGet','#table',TYPYSP,FAKS.T,,"TYPYSP.uidref()",'');
   WYDRUKIL.cntx_psh();
   WYDRUKIL.index('DP');
   WYDRUKIL.prefix('T',);
   TYPYSP.cntx_psh();
   _nwydr:='';
   {? FAKS.T().WZ<>''
   ||
      _return:=FAKS.T().WZ

   |? FAKS.T().KOR='N'
   ||
      _msk:='lsp_fakenew';
      {? WYDRUKIL.find_key(_td,_typ,_msk,FAKS.KH,FAKS.WHERE,) || _return:=_msk
      |? WYDRUKIL.find_key(_td,_typ,_msk,FAKS.KH,'',) || _return:=_msk
      |? WYDRUKIL.find_key(_td,_typ,_msk,null(),FAKS.WHERE,) || _return:=_msk
      |? WYDRUKIL.find_key(_td,_typ,_msk,null(),'',) || _return:=_msk
      ?}

   |? TYPYSP.KOR='T'
   ||
      _msk:={? FAKS.WHERE<>'N' || 'lsp_korsenew' || 'lsp_kornae' ?};
      {? WYDRUKIL.find_key(_td,_typ,_msk,FAKS.KH,FAKS.WHERE,) || _return:=_msk
      |? WYDRUKIL.find_key(_td,_typ,_msk,FAKS.KH,'',) || _return:=_msk
      |? WYDRUKIL.find_key(_td,_typ,_msk,null(),FAKS.WHERE,) || _return:=_msk
      |? WYDRUKIL.find_key(_td,_typ,_msk,null(),'') || _return:=_msk
      ?}

   |? TYPYSP.KOR='Z'
   ||
      _msk:='lsp_korze';
      {? WYDRUKIL.find_key(_td,_typ,_msk,FAKS.KH,FAKS.WHERE,) || _return:=_msk
      |? WYDRUKIL.find_key(_td,_typ,_msk,FAKS.KH,'',) || _return:=_msk
      |? WYDRUKIL.find_key(_td,_typ,_msk,null(),FAKS.WHERE,) || _return:=_msk
      |? WYDRUKIL.find_key(_td,_typ,_msk,null(),'') || _return:=_msk
      ?};
      {? _return=''
      ||
         _msk:='lsp_korze_poz';
         {? WYDRUKIL.find_key(_td,_typ,_msk,FAKS.KH,FAKS.WHERE,) || _return:=_msk
         |? WYDRUKIL.find_key(_td,_typ,_msk,FAKS.KH,'',) || _return:=_msk
         |? WYDRUKIL.find_key(_td,_typ,_msk,null(),FAKS.WHERE,) || _return:=_msk
         |? WYDRUKIL.find_key(_td,_typ,_msk,null(),'',) || _return:=_msk
         ?}
      ?}
   ?};
   TYPYSP.cntx_pop();
   WYDRUKIL.cntx_pop()
?};
FAKS.cntx_pop();
FAKS.cntx_pop();
_return


\msk_zd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Wyznacza nazwę dyskowę edokumentu dla zamówienia dostawy
::   WE: _a - ZD_NAG.ref()
::       _b - Wydruk/PDF/Edokument
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_zdnag:=_a;
_typ:=_b;
_return:='';
ZD_NAG.cntx_psh();
ZD_NAG.use(ref_name(_zdnag));
ZD_NAG.cntx_psh();
ZD_NAG.prefix();
{? ZD_NAG.seek(_zdnag)
|| _tz:=exec('FindAndGet','#table',TYPYZAM,ZD_NAG.T,,"TYPYZAM.uidref()",'');
   WYDRUKIL.cntx_psh();
   WYDRUKIL.index('DP');
   WYDRUKIL.prefix('T',);
   TYPYZAM.cntx_psh();
   _nwydr:='';
   _msk:='lzk_zdzam_01';
   {? WYDRUKIL.find_key(_tz,_typ,_msk,ZD_NAG.KH,'',)
   || _return:=_msk
   |? WYDRUKIL.find_key(_tz,_typ,_msk,null(),'',)
   || _return:=_msk
   ?};
   TYPYZAM.cntx_pop();
   WYDRUKIL.cntx_pop()
?};
ZD_NAG.cntx_pop();
ZD_NAG.cntx_pop();
_return


\typ_wydruk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Domyślne paramtry wydruków - Typ Wydruk
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wydruk'


\typ_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Domyślne paramtry wydruków - Typ PDF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'PDF'


\typ_edokument
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Domyślne paramtry wydruków - Typ E-dokument
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'E-dokument'


\dpw_grupa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Domyślne paramtry wydruków - jako akcja grupowa
::   WE: _a - 'TD'/'TZ' miejsce wywołania
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dpw_view:=_a;
{? _dpw_view='TD'
|| _tab:=TYPYSP
|? _dpw_view='TZ'
|| _tab:=TYPYZAM
|| return(0)
?};
{? _tab.sel_size()=1
|| _tb_sel:=_tab.sel_aget();
   _tab.seek(_tb_sel.REF);
   _tab.sel_del();
   exec('select','dp_wydrukow',_dpw_view)
|| params_set('dpw_view',_dpw_view);
   exec('dolacz','dp_wydrukow',0,,1);
   _tab.sel_adel()
?};
0

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:39 234c346b0fa299f091fe731094305475bf06bed48c703b1c76db9f88bdd257b94ccb2acbc58bbffe4558f09f848fa23803776c31500b745e20e1fac5ab2e708ecafde3da7dccc6c24a3912cdbde8b6c2d1e0ae54c8fc0bad7bcf5a52f22726d197d875810f3835502147629a22bfda933ca47a91d31065b5f69efd0e231fa8c5

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: gsheet.fml [18.02]
:: Utworzony: 09.10.2017
:: Autor: MB
::======================================================================================================================
:: Zawartosc: Klasy wykorzystywane do exportu i importu danych do/z arkuszy google (Google Sheet)
::======================================================================================================================


\gsheet_decl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Definicja klasy i powolanie obiektu GSheet do exportu i importu danych do/z arkuszy google
:: ~OST: INEXEDIR
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('GSHCLASS',@.CLASS)<0
|| obj_decl('GSHCLASS',
      obj_fld('FILE', ''),
      obj_fld('F', 0),
      obj_fld('Google', 1),
      obj_fld('Cell', ''),
      obj_fld('Value', 0),
      obj_fld('ServiceID', '!@#Service#@!'),
      obj_fld('LIB', 0),
      obj_fld('ID', ''),
      obj_fld('F1', 0),
      obj_fld('F2', 0),
      obj_fld('F3', 0),
      obj_fld('F4', 0),
      obj_fld('F5', 0),
      obj_fld('F6', 0),
      obj_fld('F7', 0),
      obj_fld('F8', 0),
      obj_fld('F9', 0),
      obj_fld('F10', 0),
      obj_fld('F11', 0),
      obj_fld('F12', 0),
      obj_fld('F13', 0),
      obj_fld('F14', 0),
      obj_fld('F15', 0),
      obj_fld('F16', 0),
      obj_fld('F17', 0),
      obj_fld('F18', 0),
      obj_fld('F19', 0),
      obj_fld('F20', 0),
      obj_fld('F21', 0),
      obj_fld('F22', 0),
      obj_fld('F23', 0),
      obj_fld('F24', 0),
      obj_fld('F25', 0),
      obj_fld('F26', 0),
      obj_fld('F27', 0),
      obj_fld('F28', 0),
      obj_fld('F29', 0),
      obj_fld('F30', 0),
      obj_fld('F31', 0),
      obj_fld('F32', 0),
      obj_meth('__init',"
      "),
      obj_meth('__done',"
         {? .LIB
         || lib_free(.LIB)
         ?}
      "),
      obj_meth('init',"
         .init(.ServiceID)
      "),
      obj_meth('init',"
         _dir:=exe_dir()+'/lib/google/';
         {? .Google=1
         || {? ~.LIB
            || .LIB:=lib_load('gsheet.dll',1)
            ?};
            {? .LIB
            || {? ~.F32 || .F32:=lib_decl(.LIB,,'void','setJsonPth','char *') ?};
               {? .F32 || lib_call(.F32,_dir) ?};
               {? ~.F1 || .F1:=lib_decl(.LIB,,'int','init','char *') ?};
               {? .F1 || lib_call(.F1,_a) ?}
            || 0
            ?}
         |? .Google=0
         || .FILE:='google'+$SYSLOG.tm_stamp()+'.txt';
            .F:=fopen(.FILE,'w',1);
            {? .F
            || fwrite(.F,'PATH:'+_dir);
               fwrite(.F,'INIT:'+_a)
            ?};
            .F
         |? .Google=2
         || .FILE:=.FILE+'.txt';
            .F:=fopen(.FILE,'r',1)
         ?}
      ",type_of('')),
      obj_meth('close',"
         {? .F
         || fclose(.F);
            .F:=0
         ?}
      "),
      obj_meth('create',"
         {? .LIB
         || {? ~.F2 || .F2:=lib_decl(.LIB,,'char *','create','char *','char *') ?};
            {? .F2
            || _str:=lib_call(.F2,_a,_b);
               {? _str<>''
               || _tab:=spli_str(_str,'|');
                  _obj:=obj_new('ID','LINK');
                  _obj.ID:=.ID:=_tab[1];
                  _obj.LINK:=_tab[2];
                  _obj
               || ~~
               ?}
            || ~~
            ?}
         || ~~
         ?}
      ",type_of('')),
      obj_meth('addSheet',"
         {? .LIB
         || {? ~.F3 || .F3:=lib_decl(.LIB,,'int','addSheet','char *') ?};
            {? .F3 || lib_call(.F3,_a) || 0 ?}
         || 0
         ?}
      ",type_of('')),
      obj_meth('createRange',"
         {? .Google
         || {? .LIB
            || {? ~.F4 || .F4:=lib_decl(.LIB,,'int','createRange','int','int','int') ?};
               {? .F4 || lib_call(.F4,_a+1,_b+1,_c) ?}
            || 0
            ?}
         |? .F
         || fwrite(.F,'CREATERANGE:'+$(_a+1)+','+$(_b+1)+','+$_c)
         ?}
      ",type_of(1),type_of(1),type_of(1)),
      obj_meth('addRow',"
         {? .Google
         || {? .LIB
            || {? ~.F5 || .F5:=lib_decl(.LIB,,'void','addRow') ?};
               {? .F5 || lib_call(.F5) ?}
            || 0
            ?}
         |? .F
         || {? .Cell<>''
            || fwrite(.F,'CELLS:'+(.Cell-1));
               .Cell:=''
            ?}
         ?}
      "),
      obj_meth('addCell',"
         {? .Google
         || {? .LIB
            || {? ~.F6 || .F6:=lib_decl(.LIB,,'void','addCell','char *') ?};
               {? .F6 || lib_call(.F6,_a) || -1 ?}
            || -1
            ?}
         |? .F
         || .Cell+=_a+'|'
         ?}
      ",type_of('')),
      obj_meth('saveRange',"
         {? .Google
         || {? .LIB
            || {? ~.F7 || .F7:=lib_decl(.LIB,,'int','saveRange') ?};
               {? .F7 || lib_call(.F7) ?}
            || 0
            ?}
         |? .F
         || {? .Cell<>''
            || fwrite(.F,'CELLS:'+(.Cell-1));
               .Cell:=''
            ?};
            fwrite(.F,'SAVERANGE')
         ?}
      "),
      obj_meth('merge',"
         {? .LIB
         || {? ~.F8 || .F8:=lib_decl(.LIB,,'void','merge','int','int','int','int') ?};
            {? .F8 || lib_call(.F8,_a,_b,_c,_d) ?}
         ?}
      ",type_of(1),type_of(1),type_of(1),type_of(1)),
      obj_meth('execute',"
         {? .Google
         || {? .LIB
            || {? ~.F9 || .F9:=lib_decl(.LIB,,'int','execute') ?};
               {? .F9 || lib_call(.F9) ?}
            ?}
         |? .F
         || fwrite(.F,'EXECUTE')
         ?}
      "),
      obj_meth('setStyle',"
         {? .LIB
         || {? ~.F10
            || .F10:=lib_decl(.LIB,,'void','setStyle','int','int','int','int','char *','int','int','char *','char *')
            ?};
            {? .F10 || lib_call(.F10,_a,_b,_c,_d,_e.COLOR,_e.BOLD,_e.FONTSIZE,_e.HALIGN,_e.VALIGN) ?}
         ?}
      ",type_of(1),type_of(1),type_of(1),type_of(1)),
      obj_meth('newStyle',"
         _styl:=obj_new('COLOR','BOLD','FONTSIZE','HALIGN','VALIGN');
         _styl.COLOR:=_styl.HALIGN:=_styl.VALIGN:='';
         _styl.BOLD:=_styl.FONTSIZE:=0;
         _styl

      "),
      obj_meth('cellName',"
         {? .Google
         || {? .LIB
            || {? ~.F11 || .F11:=lib_decl(.LIB,,'char *','cellName','int','int') ?};
               {? .F11 || lib_call(.F11,_a,_b) || '' ?}
            || ''
            ?}
         || _cr:='ABCDEFGHIJKLMNOPQRSTUVWXYZ';
            _b-=1;
            _str:='';
            _jest:=0;
            {! _i:=3//-1..0
            |! _m:=pow(26,_i);
               _nr:=floor(_b/_m);
               {? _nr
               || _str+=1+((_nr-{? _i=0 || 0 || 1 ?})-_cr);
                  _b-=_nr*_m;
                  _jest:=1
               |? _jest | _i=0
               || _str+=1+(_nr-_cr)
               ?}
            !};
            _str+$_a
         ?}
      ",type_of(1),type_of(1)),
      obj_meth('repeat',"
         {? .Google
         || {? .LIB
            || {? ~.F12 || .F12:=lib_decl(.LIB,,'void','repeat','int','int','int','int','char *','int') ?};
               {? .F12 || lib_call(.F12,_a,_b,_c,_d,_e,_f) ?}
            ?}
         |? .F
         || fwrite(.F,'REPEAT:%1,%2,%3,%4,%5,%6'[$_a,$_b,$_c,$_d,_e,$_f])
         ?}
      ",type_of(1),type_of(1),type_of(1),type_of(1),type_of(''),type_of(1)),
      obj_meth('duplicate',"
         {? .LIB
         || {? ~.F13 || .F13:=lib_decl(.LIB,,'void','duplicate','int','int','char *') ?};
            {? .F13 || lib_call(.F13,_a,_b,_c) ?}
         ?}
      ",type_of(1),type_of(1),type_of('')),
      obj_meth('freeze',"
         {? .LIB
         || {? ~.F14 || .F14:=lib_decl(.LIB,,'void','freeze','int','int') ?};
            {? .F14 || lib_call(.F14,_a,_b) ?}
         ?}
      ",type_of(1),type_of(1)),
      obj_meth('autoResize',"
         {? .LIB
         || {? ~.F15 || .F15:=lib_decl(.LIB,,'void','autoResize','int','int','int') ?};
            {? .F15 || lib_call(.F15,_a,_b,_c) ?}
         ?}
      ",type_of(1),type_of(1),type_of(1)),
      obj_meth('setBorder',"
         {? .LIB
         || {? ~.F16 || .F16:=lib_decl(.LIB,,'void','setBorder','int','int','int','int','char *','char *','int','char *') ?};
            {? .F16
            || _typ:='';
               {? _e.LEFT || _typ+='L' ?};
               {? _e.TOP || _typ+='T' ?};
               {? _e.RIGHT || _typ+='R' ?};
               {? _e.BOTTOM || _typ+='B' ?};
               {? _e.HORIZONTAL || _typ+='H' ?};
               {? _e.VERTICAL || _typ+='V' ?};
               lib_call(.F16,_a,_b,_c,_d,_e.COLOR,_e.STYLE,_e.WIDTH,_typ)
            ?}
         || -1
         ?}
      ",type_of(1),type_of(1),type_of(1),type_of(1)),
      obj_meth('newBorder',"
         _border:=obj_new('COLOR','STYLE','WIDTH','LEFT','TOP','RIGHT','BOTTOM','HORIZONTAL','VERTICAL');
         _border.COLOR:='0:0:0';
         _border.STYLE:='SOLID';
         _border.WIDTH:=1;
         {? var_press('_a')=type_of('')
         || _border.LEFT:=_a*'L'>0;
            _border.TOP:=_a*'T'>0;
            _border.RIGHT:=_a*'R'>0;
            _border.BOTTOM:=_a*'B'>0;
            _border.HORIZONTAL:=_a*'H'>0;
            _border.VERTICAL:=_a*'V'>0
         ?};
         _border
      "),
      obj_meth('setSheetProperties',"
         {? .LIB
         || {? ~.F17 || .F17:=lib_decl(.LIB,,'void','setSheetProperties','char *','int') ?};
            {? .F17 || lib_call(.F17,_a,_b) ?}
         ?}
      ",type_of(''),type_of(1)),
      obj_meth('open',"
         {? .Google=1
         || {? .LIB
            || {? ~.F18 || .F18:=lib_decl(.LIB,,'int','open','char *') ?};
               {? .F18 || lib_call(.F18,_a) ?}
            ?}
         |? .Google=0 & .F
         || fwrite(.F,'OPEN:'+_a); 1
         |? .Google=2
         || .F
         ?}
      ",type_of('')),
      obj_meth('openSheet',"
         {? .Google=1
         || {? .LIB
            || {? ~.F19 || .F19:=lib_decl(.LIB,,'int','openSheet','int') ?};
               {? .F19 || lib_call(.F19,_a) ?}
            ?}
         |? .Google=0 & .F
         || fwrite(.F,'OPENSHEET:'+$_a); 1
         |? .Google=2
         || .F
         ?}
      ",type_of(0)),
      obj_meth('setPermission',"
         {? .LIB
         || {? ~.F20 || .F20:=lib_decl(.LIB,,'int','setPermission','char *','char *','char *','char *') ?};
            {? .F20 || lib_call(.F20,_a,_b,_c,_d) ?}
         ?}
      ",type_of(''),type_of(''),type_of(''),type_of('')),
      obj_meth('getRange',"
         {? .Google=1
         || {? .LIB
            || {? ~.F21 || .F21:=lib_decl(.LIB,,'int','getRange','int','int','int','int') ?};
               {? .F21 || lib_call(.F21,_a,_b,_c,_d) ?}
            ?}
         |? .Google=0 & .F
         || fwrite(.F,'GETRANGE:%1,%2,%3,%4'[$_a,$_b,$_c,$_d]);
            0
         |? .Google=2 & .F
         || {? .Cell='GETRANGE'
            || 1
            || {!
               |? _line:=fread(.F);
                  _line<>'\n' & _line<>'GETRANGE'
               !};
               _line='GETRANGE'
            ?}
         ?}
      ",type_of(1),type_of(1),type_of(1),type_of(1)),
      obj_meth('firstRow',"
         {? .Google=1
         || {? .LIB
            || {? ~.F22 || .F22:=lib_decl(.LIB,,'int','firstRow') ?};
               {? .F22 || lib_call(.F22) ?}
            ?}
         |? .Google=2 & .F
         || .Cell:=fread(.F);
            .Cell<>'\n'
         ?}
      "),
      obj_meth('nextRow',"
         {? .Google=1
         || {? .LIB
            || {? ~.F23 || .F23:=lib_decl(.LIB,,'int','nextRow') ?};
               {? .F23 || lib_call(.F23) ?}
            ?}
         |? .Google=2 & .F
         || .Cell:=fread(.F);
            .Cell<>'\n'
         ?}
      "),
      obj_meth('firstColumn',"
         {? .Google=1
         || {? .LIB
            || {? ~.F24 || .F24:=lib_decl(.LIB,,'int','firstColumn') ?};
               {? .F24 || lib_call(.F24) ?}
            ?}
         |? .Google=2 & .F
         || _p:=.Cell*'|';
            {? _p
            || .Value:=_p+.Cell-1;
               .Cell:=_p-.Cell;
               1
            ?}
         ?}
      "),
      obj_meth('nextColumn',"
         {? .Google=1
         || {? .LIB
            || {? ~.F25 || .F25:=lib_decl(.LIB,,'int','nextColumn') ?};
               {? .F25 || lib_call(.F25) ?}
            ?}
         || _p:=.Cell*'|';
            {? _p
            || .Value:=_p+.Cell-1;
               .Cell:=_p-.Cell;
               1
            ?}
         ?}
      "),
      obj_meth('getDouble',"
         {? .Google=1
         || {? .LIB
            || {? ~.F26 || .F26:=lib_decl(.LIB,,'double','getDouble') ?};
               {? .F26 || lib_call(.F26) ?}
            ?}
         |? .Google=2
         || #.Value
         ?}
      "),
      obj_meth('getString',"
         {? .LIB
         || {? ~.F27 || .F27:=lib_decl(.LIB,,'char *','getString') ?};
            {? .F27 || lib_call(.F27) || '' ?}
         || ''
         ?}
      "),
      obj_meth('addProtectedRange',"
         {? .LIB
         || {? ~.F28 || .F28:=lib_decl(.LIB,,'void','addProtectedRange','int','int','int','int','int','char *') ?};
            {? .F28 || lib_call(.F28,_a,_b,_c,_d,_e,_f) ?}
         ?}
      ",type_of(1),type_of(1),type_of(1),type_of(1),type_of(1),type_of('')),
      obj_meth('serviceUser',"
         {? .LIB
         || {? ~.F29 || .F29:=lib_decl(.LIB,,'char *','serviceUser') ?};
            {? .F29 || lib_call(.F29) || '' ?}
         || 'service@curious-mender-169811.iam.gserviceaccount.com'
         ?}
      "),
      obj_meth('lastError',"
         {? .LIB
         || {? ~.F30 || .F30:=lib_decl(.LIB,,'String','lastError') ?};
            {? .F30 || lib_call(.F30) || '' ?}
         || ''
         ?}
      "),
      obj_meth('setGrid',"
         {? .LIB
         || {? ~.F31 || .F31:=lib_decl(.LIB,,'void','setGrid','int','int') ?};
            {? .F31 || lib_call(.F31,_a,_b) || -1 ?}
         || -1
         ?}
      ",type_of(1),type_of(1)),
   )
?}


\config_error
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Zgłasza błąd konfiguracji środowiska
:: ~OST: INEXEDIR
::----------------------------------------------------------------------------------------------------------------------
echo('Brak plików *.json w katalogu: %1.'@[exe_dir()+'/lib/google/']);
FUN.info('Błąd konfiguracji. Nie znaleziono plików\nclient_secret.json lub service_secret.json.'@);
echo()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:18 ccb7c11cc674a2636383ea63aecf814a1e612b365d145e01f51530687dc6e727d584b0aee2f7eaa9226461d26ec35844cc08c5257b0d7504d4939e25592b2763fae600372281c4efde2291599635764569495753c20436fe4c78683b82f757cbe5080d9f3269036fbcc335024f1575ae0123dc40e1aefd65c966fa459d76a194

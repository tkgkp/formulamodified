:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: edi_fo_ksef2.fml
:: Utworzony: 04.01.2023
:: Autor: MW
:: Systemy:
::======================================================================================================================
:: Zawartość:
::======================================================================================================================

\podmiot_dane_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Tworzy obiekt __PODMIOT_DANE
::   WE:
::   WY: obiekt __PODMIOT_ADRES
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__PODMIOT_DANE');
__PODMIOT_DANE:=obj_new('PREFIKS','EORI','NIP','NAZWA','EMAIL','TEL','STATUS_P','KOD_UE','NRVAT_UE','KRAJ','NR_ID',
    'BRAK_ID','ID_NABYWCY','ID_WEW');
{! _ii:= 1.. obj_len(__PODMIOT_DANE)
|! __PODMIOT_DANE[_ii]:=''
!};
~~


\podmiot_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot - dane
::   WE: [a] - nazwa pola
::   WY: 1 dla _a='', wpp. odpowiedni tekst
::----------------------------------------------------------------------------------------------------------------------
_pole:={? var_pres('_a')=type_of('') || ~(-_a) || '' ?};
_wyn:='';
{? _pole<> '' & var_pres('__PODMIOT_DANE')>0 & var_pres(_pole,__PODMIOT_DANE)>0
|| _ff:='__PODMIOT_DANE.'+_pole;
   _ff:=$_ff;
   _wyn:=_ff()
?};
_wyn


\podmiot_adres_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Tworzy obiekt __PODMIOT_ADRES
::   WE:
::   WY: obiekt __PODMIOT_ADRES
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__PODMIOT_ADRES');
__PODMIOT_ADRES:=obj_new('KRAJ','ADRES1','ADRES2','GLN');
{! _ii:= 1.. obj_len(__PODMIOT_ADRES)
|! __PODMIOT_ADRES[_ii]:=''
!};
~~


\podmiot_adres_osadres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot - przygotowanie adresu z tabeli OS_ADRES
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
__PODMIOT_ADRES.KRAJ:=OS_ADRES.KRAJ().SYM;
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',OS_ADRES.ULICA,OS_ADRES.DOM,OS_ADRES.LOKAL,OS_ADRES.MIASTO,
   OS_ADRES.POCZTA);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',OS_ADRES.KOD,OS_ADRES.POCZTA,OS_ADRES.MIASTO,OS_ADRES.ULICA);
__PODMIOT_ADRES.GLN:=OS_ADRES.GLN;
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
~~


\podmiot_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot - dane adresowe
::   WE: [a] - nazwa pola
::   WY: 1 dla _a='', wpp. odpowiedni tekst
::----------------------------------------------------------------------------------------------------------------------
_pole:={? var_pres('_a')=type_of('') || ~(-_a) || '' ?};
_wyn:='';
{? _pole<> '' & var_pres('__PODMIOT_ADRES')>0 & var_pres(_pole,__PODMIOT_ADRES)>0
|| _ff:='__PODMIOT_ADRES.'+_pole;
   _ff:=$_ff;
   _wyn:=_ff()
?};
_wyn


\podmiot_dane_wg_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 / Podmiot 2 /Podmiot 3 jako firma (licencjobiorca) - przygotowanie danych
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',FAKS.DW,XINFO,'NAZ','NIP');
{? exec('upgrade2325_jst01','zbl')
|| exec('czytaj','#stalesys',FAKS.DW,KST,'PODFIZ','PODNAZ','PODIME','EORI','EMAIL','TEL','STATUS_P','JST','ID_WEW')
|| exec('czytaj','#stalesys',FAKS.DW,KST,'PODFIZ','PODNAZ','PODIME','EORI','EMAIL','TEL','STATUS_P','JST')
?};

::_pref:='';
::NIPY.index('SNIP_F');
::NIPY.prefix(REF.S_FIRMA);
::{? NIPY.first()
::|| _pref:={? NIPY.KRAJ().KOD='GR' || 'EL' || NIPY.KRAJ().KOD ?}
::?};
::__PODMIOT_DANE.PREFIKS:=_pref;

_naz:='';
{? KST.PODFIZ
|| {? KST.PODNAZ<>''
   || _naz:=form(KST.PODIME+' '+KST.PODNAZ)
   ?}
|| _naz:=XINFO.NAZ
?};
__PODMIOT_DANE.NAZWA:=_naz;

__PODMIOT_DANE.EORI:=KST.EORI;
{? KST.JST<>'T'
|| __PODMIOT_DANE.NIP:=exec('niptostr','#string',XINFO.NIP)
?};
{? __PODMIOT_DANE.NIP='' & exec('upgrade2325_jst01','zbl')
|| __PODMIOT_DANE.ID_WEW:=KST.ID_WEW
?};
{? __PODMIOT_DANE.NIP<>''
|| __PODMIOT_DANE.PREFIKS:='PL'
?};
__PODMIOT_DANE.EMAIL:=KST.EMAIL;
__PODMIOT_DANE.TEL:=KST.TEL;
__PODMIOT_DANE.STATUS_P:=exec('kst_status_p_ksef','edi_fo_ksef',KST.STATUS_P);

exec('czytaj','#stalesys',,XINFO,'NAZ','NIP');
{? exec('upgrade2325_jst01','zbl')
|| exec('czytaj','#stalesys',FAKS.DW,KST,'PODFIZ','PODNAZ','PODIME','EORI','EMAIL','TEL','STATUS_P','JST','ID_WEW')
|| exec('czytaj','#stalesys',,KST,'PODFIZ','PODNAZ','PODIME','EORI','EMAIL','TEL','STATUS_P','JST')
?};

~~


\podmiot_dane_wg_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 / Podmiot 2 jako kontrahent - przygotowanie danych
::----------------------------------------------------------------------------------------------------------------------
__PODMIOT_DANE.EORI:=FAKS.KH().EORI;
__PODMIOT_DANE.NIP:=exec('niptostr','#string',FAKS.NIP);
{? __PODMIOT_DANE.NIP='' & FAKS.NIP_UE<>''
|| __PODMIOT_DANE.KOD_UE:=2+FAKS.NIP_UE;
   __PODMIOT_DANE.NRVAT_UE:=exec('delspace','#string',2-FAKS.NIP_UE)
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.NRVAT_UE=''
|| __PODMIOT_DANE.BRAK_ID:='1'
?};
::__PODMIOT_DANE.ID_NABYWCY:=FAKS.KH().IDADD;

_naz:='';
{? exec('podmiot_os_fiz','edi_fo_ksef',FAKS.KH)
|| {? exec('podmiot_im_naz','edi_fo_ksef',FAKS.KH)<>''
   || _naz:=form(exec('podmiot_im_naz','edi_fo_ksef',FAKS.KH,'I')+' '+exec('podmiot_im_naz','edi_fo_ksef',FAKS.KH))
   ?}
|| _naz:=FAKS.NAZ
?};
__PODMIOT_DANE.NAZWA:=_naz;

OS_ADRES.cntx_psh();
OS_ADRES.index('RODZAJ1');
OS_ADRES.prefix(FAKS.KH,'H');
_os_adres:=OS_ADRES.last();
__PODMIOT_DANE.EMAIL:={? _os_adres & OS_ADRES.EMAIL<>'' || OS_ADRES.EMAIL || FAKS.KH().EM ?};
__PODMIOT_DANE.TEL:={? _os_adres & OS_ADRES.TEL<>'' || OS_ADRES.TEL || FAKS.KH().TEL ?};
OS_ADRES.cntx_pop();

~~


\podmiot_dane_wg_fakskh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 jako dodatkowy kontrahent (grupa VAT) - przygotowanie danych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__P3');
__P3:=obj_new('JAK','KH');
__P3.JAK:=2;
__P3.KH:=FAKSKH.KH;

_wyn:=exec('podmiot3_dane','edi_fo_ksef2');

VAR_DEL.delete('__P3');

_wyn


\podmiot_dane_wg_p3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 / Podmiot 3 jako odbiorca / dodatkowy kontrahent (__P3) - przygotowanie danych
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;

{? __P3.KH<>null()
|| __PODMIOT_DANE.EORI:=exec('kh_pole','edi_fo_ksef',__P3.KH,'EORI')
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.ID_WEW=''
|| __PODMIOT_DANE.ID_WEW:=exec('podmiot3_idwew','edi_fo_ksef')
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.ID_WEW=''
|| __PODMIOT_DANE.NIP:=exec('podmiot3_nip','edi_fo_ksef')
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.ID_WEW=''
|| _nip_ue:=exec('podmiot3_nipue','edi_fo_ksef');
   {? _nip_ue<>''
   || __PODMIOT_DANE.KOD_UE:=2+_nip_ue;
      __PODMIOT_DANE.NRVAT_UE:=exec('delspace','#string',2-nip_ue)
   ?}
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.ID_WEW='' & __PODMIOT_DANE.NRVAT_UE=''
|| __PODMIOT_DANE.BRAK_ID:='1'
?};
::__PODMIOT_DANE.ID_NABYWCY:=exec('kh_pole','edi_fo_ksef',__P3.KH,'IDADD');
{? __P3.KH<>null()
|| __PODMIOT_DANE.EMAIL:=exec('kh_pole','edi_fo_ksef',__P3.KH, 'EM');
   __PODMIOT_DANE.TEL:=exec('kh_pole','edi_fo_ksef',__P3.KH, 'TEL')
|? __P3.JAK=1
|| __PODMIOT_DANE.EMAIL:=FAKS.KH_ODB().EM
?};

{? __P3.KH<>null() | __P3.JAK=1
|| _naz:='';
   {? exec('podmiot3_os_fiz','edi_fo_ksef')
   || {? exec('podmiot3_nazwisko','edi_fo_ksef')<>''
      || _naz:=form(exec('podmiot3_imie','edi_fo_ksef')+' '+exec('podmiot3_nazwisko','edi_fo_ksef'))
      ?}
   || _naz:=exec('podmiot3_naz','edi_fo_ksef')
   ?};
   __PODMIOT_DANE.NAZWA:=_naz
?};

_wyn


\podmiot_adres_wg_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 / Podmiot 2 jako firma (licencjobiorca) - przygotowanie adresu
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',FAKS.DW,XINFO,'KOD_P','KRAJ','MIA','NAZ','NIP','NR_D','NR_L','POCZ','TEL','UL');
::exec('czytaj','#stalesys',FAKS.DW,KST);
__PODMIOT_ADRES.KRAJ:=exec('licenc_gln','edi_form','KRAJ');
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',XINFO.UL,XINFO.NR_D,XINFO.NR_L,XINFO.MIA,XINFO.POCZ);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',XINFO.KOD_P,XINFO.POCZ,XINFO.MIA,XINFO.UL);
__PODMIOT_ADRES.GLN:=exec('licenc_gln','edi_form','GLN',0);
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
exec('czytaj','#stalesys',,XINFO,'KOD_P','KRAJ','MIA','NAZ','NIP','NR_D','NR_L','POCZ','TEL','UL');
::exec('czytaj','#stalesys',,KST);
~~


\podmiot_adres_wg_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 / Podmiot 2 jako kontrahent - przygotowanie adresu
::----------------------------------------------------------------------------------------------------------------------
_kdp:=exec('KodPoczta','edi_fo_ksef',FAKS.POCZ,FAKS.KPOCZ);
_poczta:=exec('KodPoczta','edi_fo_ksef',FAKS.POCZ,FAKS.KPOCZ,'P');
__PODMIOT_ADRES.KRAJ:=FAKS.KH().KRAJISO().SYM;
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',FAKS.UL,FAKS.DOM,FAKS.LOKAL,FAKS.MIASTO,_poczta);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',_kdp,_poczta,FAKS.MIASTO,FAKS.UL);
__PODMIOT_ADRES.GLN:=FAKS.KH().GLN;
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
~~


\podmiot_adres_wg_fakskh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 jako dodatkowy kontrahent (grupa VAT) - przygotowanie adresu
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__P3');
__P3:=obj_new('JAK','KH');
__P3.JAK:=2;
__P3.KH:=FAKSKH.KH;

_wyn:=exec('podmiot3_adres','edi_fo_ksef2');

VAR_DEL.delete('__P3');

_wyn


\podmiot_adres_wg_p3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 / Podmiot 3 jako odbiorca / dodatkowy kontrahent (__P3) - przygotowanie adresu
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_kdp:=exec('podmiot3_kdp','edi_fo_ksef');
_poczta:=exec('podmiot3_poczta','edi_fo_ksef');
_miasto:=exec('podmiot3_miasto','edi_fo_ksef');
_ulica:=exec('podmiot3_ulica','edi_fo_ksef');
_dom:=exec('podmiot3_dom','edi_fo_ksef');
_lokal:=exec('podmiot3_lokal','edi_fo_ksef');
__PODMIOT_ADRES.KRAJ:=exec('podmiot3_krajiso','edi_fo_ksef');
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',_ulica,_dom,_lokal,_miasto,_poczta);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',_kdp,_poczta,_miasto,_ulica);
{? __P3.KH<>null()
|| __PODMIOT_ADRES.GLN:=exec('kh_pole','edi_fo_ksef',__P3.KH, 'GLN')
|? __P3.JAK=1
|| __PODMIOT_ADRES.GLN:=FAKS.KH_ODB().GLN
?};
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
_wyn


\podmiot_adresk_wg_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 / Podmiot 2 / Podmiot 3 jako firma (licencjobiorca) - przygotowanie adresu korespondencyjnego
::----------------------------------------------------------------------------------------------------------------------
::exec('czytaj','#stalesys',FAKS.DW,XINFO);
exec('czytaj','#stalesys',FAKS.DW,KST,'KULICA','KDOM','KLOKAL','KMIASTO','KPOCZTA','KKOD');
__PODMIOT_ADRES.KRAJ:=KST.KKRAJ().SYM;
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',KST.KULICA,KST.KDOM,KST.KLOKAL,KST.KMIASTO,KST.KPOCZTA);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',KST.KKOD,KST.KPOCZTA,KST.KMIASTO,KST.KULICA);
__PODMIOT_ADRES.GLN:=KST.KGLN;
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
::exec('czytaj','#stalesys',,XINFO);
exec('czytaj','#stalesys',,KST,'KULICA','KDOM','KLOKAL','KMIASTO','KPOCZTA','KKOD');
~~


\podmiot_adresk_wg_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 / Podmiot 2 jako kontrahent - przygotowanie adresu korespondencyjnego
::----------------------------------------------------------------------------------------------------------------------
OS_ADRES.cntx_psh();
OS_ADRES.index('RODZAJ1');
OS_ADRES.prefix(FAKS.KH,'H');
{? OS_ADRES.last()
|| exec('podmiot_adres_osadres','edi_fo_ksef2')
?};
OS_ADRES.cntx_pop();
~~


\podmiot_adresk_wg_fakskh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 jako dodatkowy kontrahent (grupa VAT) - przygotowanie adresu korespondencyjnego
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__P3');
__P3:=obj_new('JAK','KH');
__P3.JAK:=2;
__P3.KH:=FAKSKH.KH;

_wyn:=exec('podmiot3_adresk','edi_fo_ksef2');

VAR_DEL.delete('__P3');

_wyn


\podmiot_adresk_wg_p3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 / Podmiot 3 jako odbiorca / dodatkowy kontrahent (__P3) - przygotowanie adresu korespondencyjnego
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? __P3.KH<>null()
|| OS_ADRES.cntx_psh();
   OS_ADRES.index('RODZAJ1');
   OS_ADRES.prefix(__P3.KH,'H');
   {? OS_ADRES.last()
   || exec('podmiot_adres_osadres','edi_fo_ksef2')
   ?};
   OS_ADRES.cntx_pop()
?};
_wyn


\podmiot1_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');
{? FAKS.T().ZAK='T'
|| exec('podmiot_dane_wg_kh','edi_fo_ksef2')
|| FAKSKH.cntx_psh();
   FAKSKH.index('UNIK');
   FAKSKH.prefix(FAKS.ref(),exec('rola_grupa_vat','edi_fo_ksef'));
   {? FAKSKH.first()
   || exec('podmiot_dane_wg_fakskh','edi_fo_ksef2')
   || exec('podmiot_dane_wg_firma','edi_fo_ksef2')
   ?};
   FAKSKH.cntx_pop()
?};
_wyn


\podmiot1_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
{? FAKS.T().ZAK='T'
|| exec('podmiot_adres_wg_kh','edi_fo_ksef2')
|| FAKSKH.cntx_psh();
   FAKSKH.index('UNIK');
   FAKSKH.prefix(FAKS.ref(),exec('rola_grupa_vat','edi_fo_ksef'));
   {? FAKSKH.first()
   || exec('podmiot_adres_wg_fakskh','edi_fo_ksef2')
   || exec('podmiot_adres_wg_firma','edi_fo_ksef2')
   ?};
   FAKSKH.cntx_pop()
?};
_wyn


\podmiot1_adresk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 1 - przygotowanie adresu korespondencyjnego
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
{? FAKS.T().ZAK='T'
|| exec('podmiot_adresk_wg_kh','edi_fo_ksef2')
|| FAKSKH.cntx_psh();
   FAKSKH.index('UNIK');
   FAKSKH.prefix(FAKS.ref(),exec('rola_grupa_vat','edi_fo_ksef'));
   {? FAKSKH.first()
   || exec('podmiot_adresk_wg_fakskh','edi_fo_ksef2')
   || exec('podmiot_adresk_wg_firma','edi_fo_ksef2')
   ?};
   FAKSKH.cntx_pop()
?};
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\podmiot2_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 2 - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');

{? FAKS.T().ZAK='T'
|| exec('podmiot_dane_wg_firma','edi_fo_ksef2')
|| exec('podmiot_dane_wg_kh','edi_fo_ksef2')
?};

_wyn


\podmiot2_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 2 - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
{? FAKS.T().ZAK='T'
|| exec('podmiot_adres_wg_firma','edi_fo_ksef2')
|| exec('podmiot_adres_wg_kh','edi_fo_ksef2')
?};
_wyn


\podmiot2_adresk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 2 - przygotowanie adresu korespondencyjnego
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
{? FAKS.T().ZAK='T'
|| exec('podmiot_adresk_wg_firma','edi_fo_ksef2')
|| exec('podmiot_adresk_wg_kh','edi_fo_ksef2')
?};
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\podmiot3_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 3 - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');
{? __P3.KH=null() & __P3.JAK=2
:: dane licencjobiorcy - członek grupy VAT albo jednostka samorządu terytorialnego
|| exec('podmiot_dane_wg_firma','edi_fo_ksef2')
?};
exec('podmiot_dane_wg_p3','edi_fo_ksef2');
_wyn


\podmiot3_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 3 - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
exec('podmiot_adres_wg_p3','edi_fo_ksef2');
_wyn


\podmiot3_adresk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 3 - przygotowanie adresu korespondencyjnego
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
{? __P3.KH=null() & __P3.JAK=2
:: dane licencjobiorcy - członek grupy VAT albo jednostka samorządu terytorialnego
|| exec('podmiot_adresk_wg_firma','edi_fo_ksef2')
|| exec('podmiot_adresk_wg_p3','edi_fo_ksef2')
?};
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\podmiotU_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot upoważniony - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');

__PODMIOT_DANE.EORI:=exec('kh_pole','edi_fo_ksef',__PU.KH,'EORI');
__PODMIOT_DANE.NIP:=exec('niptostr','#string',exec('kh_pole','edi_fo_ksef',__PU.KH,'NIP'));
{? __PODMIOT_DANE.NIP=''
|| __PODMIOT_DANE.BRAK_ID:='1'
?};
__PODMIOT_DANE.EMAIL:=exec('kh_pole','edi_fo_ksef',__PU.KH,'EM');
__PODMIOT_DANE.TEL:=exec('kh_pole','edi_fo_ksef',__PU.KH,'TEL');

_naz:='';
{? exec('podmiotU_os_fiz','edi_fo_ksef')
|| {? exec('podmiotU_nazwisko','edi_fo_ksef')<>''
   || _naz:=form(exec('podmiotU_imie','edi_fo_ksef')+' '+exec('podmiotU_nazwisko','edi_fo_ksef'))
   ?}
|| _naz:=exec('kh_pole','edi_fo_ksef',__PU.KH,'NAZ_P')
?};
__PODMIOT_DANE.NAZWA:=_naz;

_wyn


\podmiotU_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot upoważniony - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
_kdp:=exec('kh_pole','edi_fo_ksef',__PU.KH,'KPOCZ');
_poczta:=exec('kh_pole','edi_fo_ksef',__PU.KH,'POCZ');
_miasto:=exec('kh_pole','edi_fo_ksef',__PU.KH,'MIASTO');
_ulica:=exec('kh_pole','edi_fo_ksef',__PU.KH,'UL');
_dom:=exec('kh_pole','edi_fo_ksef',__PU.KH,'DOM');
_lokal:=exec('kh_pole','edi_fo_ksef',__PU.KH,'LOKAL');
__PODMIOT_ADRES.KRAJ:=exec('kh_pole','edi_fo_ksef',__PU.KH,'KRAJISO().SYM');
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',_ulica,_dom,_lokal,_miasto,_poczta);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',_kdp,_poczta,_miasto,_ulica);
__PODMIOT_ADRES.GLN:=exec('kh_pole','edi_fo_ksef',__PU.KH,'GLN');
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
_wyn


\podmiotU_adresk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot upoważniony - przygotowanie adresu korespondencyjnego
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
OS_ADRES.cntx_psh();
OS_ADRES.index('RODZAJ1');
OS_ADRES.prefix(__PU.KH,'H');
{? OS_ADRES.last()
|| exec('podmiot_adres_osadres','edi_fo_ksef2')
?};
OS_ADRES.cntx_pop();
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\podmiot2k_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 2K - przygotowanie danych
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_dane_obj','edi_fo_ksef2');

__PODMIOT_DANE.PREFIKS:=exec('podmiot2k_pref','edi_fo_ksef');
__PODMIOT_DANE.NIP:=exec('podmiot2k_nip','edi_fo_ksef');
{? __PODMIOT_DANE.NIP=''
|| _nip_ue:=exec('podmiot2k_nip','edi_fo_ksef');
   {? _nip_ue<>''
   || __PODMIOT_DANE.KOD_UE:=2+_nip_ue;
      __PODMIOT_DANE.NRVAT_UE:=exec('delspace','#string',2-nip_ue)
   ?}
?};
{? __PODMIOT_DANE.NIP='' & __PODMIOT_DANE.NRVAT_UE=''
|| __PODMIOT_DANE.BRAK_ID:='1'
?};
::__PODMIOT_DANE.ID_NABYWCY:=exec('kh_pole','edi_fo_ksef',__P2K.KH, 'IDADD');

_naz:='';
{? exec('podmiot2k_os_fiz','edi_fo_ksef')
|| {? exec('podmiot2k_nazwisko','edi_fo_ksef')<>''
   || _naz:=form(exec('podmiot2k_imie','edi_fo_ksef')+' '+exec('podmiot2k_nazwisko','edi_fo_ksef'))
   ?}
|| _naz:=exec('podmiot2k_naz','edi_fo_ksef')
?};
__PODMIOT_DANE.NAZWA:=_naz;

_wyn


\podmiot2k_adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podmiot 2K - przygotowanie adresu
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('podmiot_adres_obj','edi_fo_ksef2');
_kdp:=exec('podmiot2k_kdp','edi_fo_ksef');
_poczta:=exec('podmiot2k_poczta','edi_fo_ksef');
_miasto:=exec('podmiot2k_miasto','edi_fo_ksef');
_ulica:=exec('podmiot2k_ulica','edi_fo_ksef');
_dom:=exec('podmiot2k_dom','edi_fo_ksef');
_lokal:=exec('podmiot2k_lokal','edi_fo_ksef');
__PODMIOT_ADRES.KRAJ:=exec('podmiot2k_krajiso','edi_fo_ksef');
__PODMIOT_ADRES.ADRES1:=exec('adr','faktury_wydruk',_ulica,_dom,_lokal,_miasto,_poczta);
__PODMIOT_ADRES.ADRES2:=exec('kpocz','faktury_wydruk',_kdp,_poczta,_miasto,_ulica);
{? __P2K.KH<>null()
|| __PODMIOT_ADRES.GLN:=exec('kh_pole','edi_fo_ksef',__P2K.KH, 'GLN')
|? __P2K.JAK=2
|| __PODMIOT_ADRES.GLN:=FAKS.KH_ODB().GLN
?};
{? __PODMIOT_ADRES.ADRES1=''
|| __PODMIOT_ADRES.ADRES1:=__PODMIOT_ADRES.ADRES2;
   __PODMIOT_ADRES.ADRES2:=''
?};
{? __PODMIOT_ADRES.KRAJ='' | __PODMIOT_ADRES.ADRES1=''
|| _wyn:=0
?};
_wyn


\RachBank_Opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Rachunek bankowy - opis
::   WE: [_a] - 0 - dla FAKS.SKID_RBK (domyslne), 1 - dla FAKS.VSKIDRBK
::   WY: nazwa
::----------------------------------------------------------------------------------------------------------------------
_jak:=0;
{? var_pres('_a')=type_of(0)
|| _jak:=_a
?};
_opi:='';
{? _jak
|| _opi:='Rachunek VAT'
|| {? FAKS.SKID_RBK().WAL().KOD='PLN'
   || _opi:='Rachunek PLN'
   || _opi:='Rachunek walutowy'
   ?}
?};
_opi


\p_13_5_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Warunek dla P_13_5
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=(FAKS.PROC().KOD='WSTO_EE');
_wyn


\p_13_8_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Warunek dla P_13_8
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=((FAKS.T().UE='T' | FAKS.T().UE='T') & ~exec('p_13_5_czy','edi_fo_ksef2') & ~exec('p_13_9_czy','edi_fo_ksef2'));
_wyn


\p_13_9_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Warunek dla P_13_9
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_wyn:=(FAKS.T().UE='T' & FAKS.T().UE_WSU='T' & FAKS.NDVAT='O' & ~exec('p_13_5_czy','edi_fo_ksef2'));
_wyn


\podmiot1k_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: "Podmiot1K" - formuła start (tylko dla korekty RR)
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;

{? FAKS.SZ='Z'
|| VAR_DEL.delete('__P2K');
   __P2K:=obj_new('JAK','KH');
   __P2K.JAK:=0;
   __P2K.KH:=null();

   FAKS.cntx_psh();

   {? FAKS.LKSQL<>'' & FAKS.WHERE='N'
   || _lksql:=FAKS.LKSQL;
      FAKS.use(form(8+_lksql));
      FAKS.prefix();
     {? FAKS.seek(_lksql)
     || __P2K.JAK:=1;
        __P2K.KH:=FAKS.KH
     ?}
   ?};

   {? __P2K.JAK>0
   || _ret:=1
   || FAKS.cntx_pop();
      VAR_DEL.delete('__P2K')
   ?}
?};

_ret


\podmiot1k_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: "Podmiot1K" - formuła end (tylko dla korekty RR)
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
FAKS.cntx_pop();
VAR_DEL.delete('__P2K');
_ret


\podmiot3_rola
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: "Podmiot3" - rola
::   WY: rola
::----------------------------------------------------------------------------------------------------------------------
exec('podmiot3_rola','edi_fo_ksef',2)


\podmiot3_rola_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: "Podmiot3" - opis roli
::   WY: opis roli
::----------------------------------------------------------------------------------------------------------------------
exec('podmiot3_rola_op','edi_fo_ksef',2)


\podmiot3_rola_inna
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: "Podmiot3" - czy rola inna
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_inna:=0;
_rola:=exec('podmiot3_rola','edi_fo_ksef2');
{? #_rola<1 | #_rola>10
|| _inna:=1
?};
_inna


\podmiot3_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: "Podmiot3" - formuła start
::   WE: [_a] - rola
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || _a:='' ?};
exec('podmiot3_start','edi_fo_ksef',2,_a)


\podmiot3_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: "Podmiot3" - formuła end
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
exec('podmiot3_end','edi_fo_ksef',2)


\podmiot2k_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: "Podmiot2K" - formuła end
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
exec('podmiot2k_end','edi_fo_ksef',2)


\rola_podmiotu_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Rola podmiotu - KSeF
::   WE: _a - rola
::   WY: obj_new('ROLA','ROLAINNA','OPISROLI')
::----------------------------------------------------------------------------------------------------------------------
exec('rola_podmiotu_ksef','edi_fo_ksef',_a,2)


\rola_pomin_lsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Czy dana rola podmiotu ma być pomijana w pliku KSeF i na wydruku
::   WE: _a - rola
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
exec('rola_pomin_lsp','edi_fo_ksef',_a,2)


\p7_wart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Wartość pola "P_7" lub "P_7Z" (nazwa towaru lub usługi) w pliku KSeF
::   WE: _a - [P_7]/P_7Z
::   WY: nazwa towaru lub usługi
::----------------------------------------------------------------------------------------------------------------------
_pole:={? var_pres('_a')=type_of('') & (_a='P_7' | _a='P_7Z')  || _a || 'P_7' ?};
_naz:=Plugin.run('KSEF_P7',_pole);
{? _naz=''
|| _naz:=FAP.NX
?};
_naz


\DodatkowyOpis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Utoworzenie tabeli zawierającej wartości dla sekcji "DodatkowyOpis" w pliku KSeF
::   WE:
::   WY: Tabela zawierająca wartości dla sekcji "DodatkowyOpis" w pliku KSeF
::----------------------------------------------------------------------------------------------------------------------
_tdod:=tab_tmp(2,
              'NRWIERSZ','INTEGER','Numer wiersza'@,
              'KLUCZ','STRING[255]','Klucz'@,
              'WARTOSC','STRING[255]','Wartość'@);

FAKSO.cntx_psh();

FAKSO.index('FAKS');
FAKSO.prefix(FAKS.ref());
{? FAKSO.first()
|| {!
   |? {? (FAKSO.PRN='T' | FAKSO.PRN='A') & FAKSO.T<>'' & FAKSO.O<>''
      || _tdod.blank();
         _tdod.NRWIERSZ:=0;
         _tdod.KLUCZ:=FAKSO.T;
         _tdod.WARTOSC:=FAKSO.O;
         _tdod.add()
      ?};
      FAKSO.next()
   !}
?};

_uwagi:=exec('delspace','#string',gsub(FAKS.memo_txt(0,1,'UWAGI'),'\n',' '));
{? _uwagi<>''
|| _tdod.blank();
   _tdod.NRWIERSZ:=0;
   _tdod.KLUCZ:='Uwagi';
   _tdod.WARTOSC:=_uwagi;
   _tdod.add()
?};

FAP.cntx_psh();
FAP.index('FAP');
FAP.prefix(FAKS.ref());
{? FAP.first()
|| FAKSO.index('FAP');
   {!
   |? _nrwiersz:={? FAKS.T().KOR='T' || 2*FAP.POZ || FAP.POZ ?};
      FAKSO.prefix(FAP.ref());
      {? FAKSO.first()
      || {!
         |? {? (FAKSO.PRN='T' | FAKSO.PRN='A') & FAKSO.T<>'' & FAKSO.O<>''
            || _tdod.blank();
               _tdod.NRWIERSZ:=_nrwiersz;
               _tdod.KLUCZ:=FAKSO.T;
               _tdod.WARTOSC:=FAKSO.O;
               _tdod.add()
            ?};
            FAKSO.next()
         !}
      ?};
      {? PARAM_W.UWAGI='T'
      || _uwagi:=exec('delspace','#string',gsub(FAP.memo_txt(0,1,'UWAGI'),'\n',' '));
         {? _uwagi<>''
         || _tdod.blank();
            _tdod.NRWIERSZ:=_nrwiersz;
            _tdod.KLUCZ:='Uwagi';
            _tdod.WARTOSC:=_uwagi;
            _tdod.add()
         ?}
      ?};
      {? PARAM_W.KODOBCY='T'
      || _kod_obcy:=exec('kod_obcy','kody_kresk',FAP.M,FAKS.KH);
         {? _kod_obcy<>''
         || _tdod.blank();
            _tdod.NRWIERSZ:=_nrwiersz;
            _tdod.KLUCZ:='Kod obcy';
            _tdod.WARTOSC:=_kod_obcy;
            _tdod.add()
         ?}
      ?};
      {? PARAM_W.DK_C='T' & FAP.DK_C<>null
      || _dk_c:=form(exec('sym_dk_c','mat_atr',FAP.DK_C));
         {? _dk_c<>''
         || _tdod.blank();
            _tdod.NRWIERSZ:=_nrwiersz;
            _tdod.KLUCZ:='Atrybuty';
            _tdod.WARTOSC:=_dk_c;
            _tdod.add()
         ?}
      ?};
      {? PARAM_W.KP='T'
      || _kp:=FAP.KP;
         {? _kp<>''
         || _tdod.blank();
            _tdod.NRWIERSZ:=_nrwiersz;
            _tdod.KLUCZ:='Kraj pochodzenia';
            _tdod.WARTOSC:=_kp;
            _tdod.add()
         ?}
      ?};
      FAP.next()
   !}
?};
FAP.cntx_pop();

FAKSO.cntx_pop();

_tdod


\r_faks_ds
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: read - dokument sprzedaży - start document
::   WE: _a - [F]aktura, [K]orekta
::   WY: -1/0/1
::----------------------------------------------------------------------------------------------------------------------
_typ:={? var_pres('_a')=type_of('') || _a || '' ?};
{? _typ='' | 'FK'*_typ=0
||
   FUN.info('Nieprawidłowy parametr funkcji r_faks_ds.'@);
   return(-1)
?};
_result:=1;
VAR_DEL.delete('__N','__V','__VK','__P','__OL','__OtherParties','__Faktura','__FAKSV');
{? _result
||
   exec('edi_buf','edi_fo_ufd');
   EDI_Z.POMIN:='N';
   BEER.WYSTFAKS:=null();
   BEER.SZ:='Z';
:: Tabela do zbierania danych z sekcji Document\Order\OrderLine
   __OL:=tab_tmp(
      ,'LN','INTEGER','LineNumber'
      ,'GLN','INTEGER','GlobalLineNumber'
      ,'ID','STRING[100]','ItemDescription'
      ,'IC','STRING[50]','ItemCodeExt'
      ,'IU','STRING[8]','ItemUnit'
      ,'Q','REAL','Quantity'
      ,'UNP','REAL','UnitNetPrice'
      ,'NA','REAL','NetAmount'
      ,'TA','REAL','TaxAmount'
      ,'TR','REAL','TaxRate'
      ,'SP','STRING[1]','SplitPayment'
      ,'PQ','REAL','Quantity'
      ,'PUNP','REAL','UnitNetPrice'
      ,'PNA','REAL','NetAmount'
      ,'PTA','REAL','TaxAmount'
      ,'PTR','REAL','TaxRate'
   );
:: Tabela do zbierania danych z sekcji Document\Parties\OtherParties
   __OtherParties:=tab_tmp(2
      ,'ILN','STRING[13]','ILN'
      ,'TAXID','STRING[20]','TaxID'
::      ,'NAME','STRING[]','Name'
::      ,'ADDRESS1','STRING[255]','Address1'
::      ,'ADDRESS2','STRING[255]','Address2'
::      ,'AUTOADDR','STRING[1]','AutoAddress'
::      ,'ST_AND_N','STRING[255]','StreetAndNumber'
::      ,'CITYNAME','STRING[30]','CityName'
::      ,'POSTALC','STRING[9]','PostalCode'
::      ,'COUNTRY','STRING[2]','Country'
      ,'INTERID','STRING[255]','InternalId'
      ,'ROLE','STRING[2]','Role'
      ,'ROLEDESC','STRING[255]','RoleDescription'
      ,'OTR_ROLE','STRING[1]','OtherRole'
      ,'SHARE','REAL','Share'
      ,'EORI','STRING[255]','EORINumber'
   );
   __VK:=obj_new(
      'P_13_1','P_14_1','P_14_1W',
      'P_13_2','P_14_2','P_14_2W',
      'P_13_3','P_14_3','P_14_3W',
      'P_13_4','P_14_4','P_14_4W',
      'P_13_5','P_14_5',
      'P_13_6_1',
      'P_13_6_2',
      'P_13_6_3',
      'P_13_7',
      'P_13_8',
      'P_13_9',
      'P_13_10',
      'P_13_11',
      'P_15'
   );
   __Faktura:=json_parse(__XML.T.xml_tconvert());
   __FAKSV:=exec('xml2tmp_faksv', 'edi_fo_ksef2',__Faktura)
?};
_result


\faksv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Uzupełnia FAKSV
::   WE: _a - 0/1 monitorowanie odczytu wartości
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_chk_read:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_verify:=exec('verify','edi_xml');
VAR_DEL.delete('__V');
__V:=obj_new(
   'PREV_RWN','PREV_RWV','PREV_RWB','PREV_WN','PREV_WV','PREV_WB',
   'PREV_WAL_WN','PREV_WAL_WV','PREV_WAL_WB',
   'RWN','RWV','RWB','WN','WV','WB',
   'WAL','WAL_WN','WAL_WV','WAL_WB',
   'SV_ID','SV_P','SV_TSID'
);
_faksv_end:="
   _wsk:=_a;
   {? var_pres('N_WN',_wsk)=type_of(0) | var_pres('P_WN',_wsk)=type_of(0) | var_pres('P_WB',_wsk)=type_of(0)
   ||
      _chk_read:=_e;
      _verify:=exec('verify','edi_xml');
      __V.PREV_RWN:=__V.PREV_RWV:=__V.PREV_RWB:=__V.PREV_WN:=__V.PREV_WV:=__V.PREV_WB:=0;
      __V.PREV_WAL_WN:=__V.PREV_WAL_WV:=__V.PREV_WAL_WB:=0;
      __V.RWN:=__V.RWV:=__V.RWB:=__V.WN:=__V.WV:=__V.WB:=0;
      __V.WAL:=''; __V.WAL_WN:=__V.WAL_WV:=__V.WAL_WB:=0;
      __V.SV_ID:=''; __V.SV_P:='bd'; __V.SV_TSID:='';
      {? ~_chk_read
      ||
         __V.PREV_RWN:=__V.PREV_RWV:=1; __V.PREV_RWB:=0;
         __V.RWN:=__V.RWV:=1; __V.RWB:=0
      ?};
      {? _verify
      ||
         ~~
      ||
         FAKSV.index('FAKS_SV');
         FAKSV.prefix()
      ?};
      FAKSV.blank();
      __V.WAL:=__ic;
      FAKSV.WAL:=exec('wal','edi_fo01',__V.WAL);
:: włączenie kontroli zgodności wartości dokumentu w stawkach z wartościami pozycji dokumentu
:: dla dokumentów niewalutowych
      _wal:=FAKSV.WAL<>exec('bl_wal','ustawienia');
      FAKSV.R:=_wal=0;
:: dopisanie pozycji FAKSV
      __V.SV_ID:={? _c='zw' || 'E' |? _c='oo' || 'AE' |? _c='np' || 'NA' || 'S' ?};
      __V.SV_P:=_c;
      __V.SV_TSID:=_d;
      {? var_pres('N_WN',_wsk)=type_of(0)
      ||
         {? _wal
         ||
            __V.WAL_WN:=_wsk.N_WN;
            {? var_pres('N_WV',_wsk)=type_of(0) || __V.WAL_WV:=_wsk.N_WV ?};
            {? var_pres('N_WV_POKRS',_wsk)=type_of(0) || __V.RWV:=1; __V.WV:=_wsk.N_WV_POKRS ?};
            __V.WAL_WB:=__V.WAL_WN+{? var_pres('N_WV',_wsk)=type_of(0) || _wsk.N_WV || 0 ?}
         ||
            __V.RWN:=1; __V.WN:=_wsk.N_WN;
            {? var_pres('N_WV',_wsk)=type_of(0) || __V.RWV:=1; __V.WV:=_wsk.N_WV ?};
            __V.RWB:=1; __V.WB:=__V.WN+{? __V.RWV || __V.WV || 0 ?}
         ?}

      |? var_pres('P_WN',_wsk)=type_of(0)
      ||
         {? _wal
         ||
            __V.WAL_WN:=_wsk.P_WN;
            {? var_pres('P_WV',_wsk)=type_of(0) || __V.WAL_WV:=_wsk.P_WV ?};
            {? var_pres('P_WV_POKRS',_wsk)=type_of(0) || __V.RWV:=1; __V.WV:=_wsk.P_WV_POKRS ?};
            __V.WAL_WB:=__V.WAL_WN+{? var_pres('P_WV',_wsk)=type_of(0) || _wsk.P_WV || 0 ?}
         ||
            __V.RWN:=1; __V.WN:=_wsk.P_WN;
            {? var_pres('P_WV',_wsk)=type_of(0) || __V.RWV:=1; __V.WV:=_wsk.P_WV ?};
            __V.RWB:=1; __V.WB:=__V.WN+{? __V.RWV || __V.WV || 0 ?}
         ?}

      |? var_pres('P_WB',_wsk)=type_of(0)
      ||
         {? _wal
         ||
            __V.WAL_WB:=_wsk.P_WB;
            {? var_pres('P_WV',_wsk)=type_of(0) || __V.WAL_WV:=_wsk.P_WV ?};
            {? var_pres('P_WV_POKRS',_wsk)=type_of(0) || __V.RWV:=1; __V.WV:=_wsk.P_WV_POKRS ?};
            __V.WAL_WN:=__V.WAL_WB-{? var_pres('P_WV',_wsk)=type_of(0) || _wsk.P_WV || 0 ?}
         ||
            __V.RWB:=1; __V.WB:=_wsk.P_WB;
            {? var_pres('N_WV',_wsk)=type_of(0) || __V.RWV:=1; __V.WV:=_wsk.P_WV ?};
            __V.RWN:=1; __V.WB:=__V.WN-{? __V.RWV || __V.WV || 0 ?}
         ?}
      ?};
      exec('faksv_end','edi_fo_ufd',0)
   ?}
";
_FAKSV:=__FAKSV;
_wsk:=_FAKSV.s23; _faksv_end(_wsk,'','23','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.s22; _faksv_end(_wsk,'','22','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.s8; _faksv_end(_wsk,'','8','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.s7; _faksv_end(_wsk,'','7','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.s5; _faksv_end(_wsk,'','5','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.s4; _faksv_end(_wsk,'','4','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.s3; _faksv_end(_wsk,'','3','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.s0; _faksv_end(_wsk,'','0','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.szw; _faksv_end(_wsk,'','zw','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.soo; _faksv_end(_wsk,'','oo','VAT',_chk_read); obj_del(_wsk);
_wsk:=_FAKSV.snp; _faksv_end(_wsk,'','np','VAT',_chk_read); obj_del(_wsk);
1


\fap_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: pozycja dokumentu zakupu - start
::   WE: _a - 0/1 - odczytanie wartości
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_verify:=exec('verify','edi_xml');
_kor:=POM.TYPYSP().KOR='T';
VAR_DEL.delete('__P','__FKOR','__P1');
__P:=obj_new(
   'SV_ID','SV_P','SV_TSID',
   'PRICEA','GPRICE',
   'MAPINFO',
   'JM',
   'MKODK','MKODK_KODK','MKODK_KTM','MKODK_N',
   'WN','WV','WB',
   'KRS',
   'PREV_WWAL','PREV_BWAL',
   'MEMO_PUT');
__P.SV_ID:=''; __P.SV_P:='bd'; __P.SV_TSID:='';
__P.PRICEA:=__P.GPRICE:=0;
__P.MAPINFO:='N';
__P.JM:='';
__P.MKODK:=null(); __P.MKODK_KODK:=''; __P.MKODK_KTM:=''; __P.MKODK_N:='';
::__P.WN:=__P.WV:=__P.WB:=0;
__P.KRS:=0;
__P.PREV_WWAL:=__P.PREV_BWAL:=0;
__P.MEMO_PUT:=0;
_first:=__N.FIRST; __N.FIRST:=0;
__FKOR:=obj_new('WWAL','BWAL');
__FKOR.WWAL:=__FKOR.BWAL:=0;

__P1:=obj_new(
   'POZ','UU_ID',
   'D',
   'N','KTM','EAN',
   'PKWIU_CN',
   'JM','IL',
   'CN','CB',
   'WN','WB','WV',
   'SV',
   'SP_WYM','GTU','PROC',
   'KRS',
   'ST_PRZED'
);

FIND_M:='';
{? _first=1 & _kor & exec('faks_update','edi_fo_ufd')=0
||
   _result:=0

|? EDI_Z.POMIN='T'
||
   _result:=0
||
   {? _kor=0
   ||
      FAP.prefix();
      ATR.MJS:='FAP';
      TAZ.RAB_TYP:=FAKS.RAB_TYP;
      BEER.MW:='K';
      exec('ustaw_ww','eurusd',BEER.MW); BEER.LW:={? BEER.WW || 'W' || '' ?};
      exec('ustafprz','faktury_wspolne');
      BEER.TAB:='FAP';
      FAP.blank();
      FAP.POZ:=-1;
      FAP.WAL:={? FAKS.WAL<>null || FAKS.WAL || INFO.NAROD ?};
      FAP.WAL_H:=FAP.WAL;
      exec('istatr_faks2fap','faktury_nag')
   ||
      ~~
   ?};
   {? _verify=1
   ||
      {? _kor=0
      ||
         ~~
      ||
         xx_ref:=null();
         _result:=exec('bl_faks_zk','edi_fo_ufd',POM.TYPYSP,__N1.KH,__N1.FAK_ID,__N1.FAK_DW,__N1.DW,__N1.ID,
            __N1.FAK_KSEF,__N1.KH_ODB,1);
         {? _result
         ||
            BEER.WYSTFAKS:={? xx_ref || exec('FindAndGet','#table',FAKS,xx_ref,,,null()) || BEER.WYSTFAKS ?}
         ?}
      ?}
   ||
      {? _kor=0
      ||
         {? _a=0 || _result:=FAP.add() ?}
      ||
         FAP.index('FAP');
::       w BEER.WYSTFAKS jest ref dodawanej korekty
         FAP.prefix(BEER.WYSTFAKS);
         ~~
      ?}
   ?}
?};
_result


\fap_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: pozycja dokumentu zakupu - end
::   WE: _a - 'poz' / 'zam_poz'
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a='poz' & __N.RODZ_FAK<>'ZAL' & __N.RODZ_FAK<>'KOR_ZAL'
      |
   _a='zam_poz' & (__N.RODZ_FAK='ZAL' | __N.RODZ_FAK='KOR_ZAL')
||
   __FaWiersz_len+=1;
   __FaWiersz[__FaWiersz_len]:=__P1
?};
1


\faks_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: nagłówek dokumentu zakupu - end
::   WE: _a - domyślny typ dokumentu
::       _b - identyfikator dokumentu
::       _c - oddział
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__Faktura')<=100 || return(1) ?};
_typysp:=_a;
_id:=_b;
_oddz:=_c;
_verify:=exec('verify','edi_xml');
_result:=1;
_Faktura:=__Faktura;
{? var_pres('Fa',_Faktura)>100
||
   _wsk:=_Faktura.Fa;
   {? _verify
   ||
:: typ dokumentu
      {? var_pres('RodzajFaktury',_wsk)<>2
      ||
         FUN.info('Brak rodzaju faktury.'@)
      ||
:: _Faktura.RodzajFaktury={VAT, KOR, ZAL, ROZ, UPR, KOR_ZAL, KOR-ZAL}
         {? _wsk.RodzajFaktury*'KOR'
         || __N1.DTYP0:='INV_CORR'; __N1.DFUNC0:='C'
         || __N1.DTYP0:='INV'; __N1.DFUNC0:='O'
         ?};
         exec('InvoiceType','edi_fo_ufd',
            {? var_pres('P_13_6_2',_wsk)=2 || 'ICD'
            |? _wsk.RodzajFaktury*'ZAL' || 'AI'
            || 'VAT'
            ?}
         )
      ?};
      _result:=FAKS.T<>null();
      {? _result=0 || exec('add_kom','#message','Nie wybrano typu dokumentu.'@,4,_id) ?}
   ?};
:: wyliczenie ilości wierszy
   VAR_DEL.delete('__FaWiersz','__FaWiersz_len');
   _len:=0;
   {? var_pres('Zamowienie',_wsk)>100
   || {? var_pres('ZamowienieWiersz',_wsk.Zamowienie)>100
      || _wsk:=(obj_del(_wsk); _Faktura.Fa.Zamowienie.ZamowienieWiersz);
         _len:={? type_of(_wsk[1])=2 || 1 || obj_len(_wsk) ?}
      ?}
   |? var_pres('FaWiersz',_Faktura.Fa)>100
   || _wsk:=(obj_del(_wsk); _Faktura.Fa.FaWiersz);
      _len:={? type_of(_wsk[1])=2 || 1 || obj_len(_wsk) ?}
   ?};
   {? _len>0
   ||
      __FaWiersz:=obj_new(_len);
      __FaWiersz_len:=0
   ?}
?};
_result


\faks_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: nagłówek dokumentu zakupu - end
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

_done:="VAR_DEL.delete('__FaWiersz','__FaWiersz_len')";
{? var_pres('__FaWiersz_len')<>type_of(0) | __FaWiersz_len=0 || _done(); return() ?};
{? var_pres('RODZ_FAK',__N)<>type_of('') | __N.RODZ_FAK='' || _done(); return() ?};

_verify:=exec('verify','edi_xml');
_rodz_fak:=__N.RODZ_FAK;
_kor:=(3+__N.RODZ_FAK)='KOR';

exec('platnosc','edi_fo_ksef2');
{? _kor=0 & exec('faks_update','edi_fo_ufd')=0 || _result:=0 ?};

{? _kor=0
||
:: faktura
   _ii:=0;
   _lp:=0;
   {!
   |? _result & _ii<__FaWiersz_len
   |!
      _ii+=1; _row:=__FaWiersz[_ii];
      _lp+=1;
      exec('fap_start','edi_fo_ksef2',0);
      FAP.POZ:={? type_of(_row.POZ)=type_of(0) || _row.POZ || _lp ?};
      {? type_of(_row.UU_ID)=type_of('') || FAP.UU_ID:=_row.UU_ID ?};
      {? type_of(_row.D)=type_of(date(0,0,0)) || FAP.D:=_row.D ?};
      _xml_val:=__XML.VAL;
      __P.MKODK_KODK:=__P.MKODK_KTM:=__P.MKODK_N:='';
      {? type_of(_row.EAN)=type_of('') || __XML.VAL:=_row.EAN; exec('EAN','edi_fo_ufd') ?};
      {? type_of(_row.KTM)=type_of('') || __XML.VAL:=_row.KTM; exec('SupplierItemCode','edi_fo_ufd') ?};
      {? type_of(_row.N)=type_of('') || __XML.VAL:=_row.N; exec('ItemDescription','edi_fo_ufd') ?};
      __XML.VAL:=_xml_val;
      {? type_of(_row.PKWIU_CN)=type_of('') || FAP.PKWIU_CN:=_row.PKWIU_CN ?};
      {? type_of(_row.JM)=type_of('') || __P.JM:=_row.JM ?};
      {? type_of(_row.IL)=type_of(0) || FAP.IL:=_row.IL ?};
      {? type_of(_row.CN)=type_of(0) || __P.PRICEA:=_row.CN ?};
      {? type_of(_row.CB)=type_of(0) || __P.GPRICE:=_row.CB ?};
      {? type_of(_row.SV)=type_of('') || FAP.SV:=exec('ufd2sv','edi_fo_ufd','',_row.SV,'VAT') ?};
      {? type_of(_row.WN)=type_of(0) || __P.WN:=_row.WN ?};
      {? type_of(_row.WB)=type_of(0) || __P.WB:=_row.WB ?};
      {? type_of(_row.WV)=type_of(0) || __P.WV:=_row.WV ?};

      {? type_of(_row.SP_WYM)=type_of('') & _row.SP_WYM='1' || FAP.SP_WYM:='T' ?};
::      exec('xmlval_start','edi_fo_ksef',FAP.JPK_GTU().KOD);
::      exec('xmlval_start','edi_fo_ksef',exec('ef_proc','edi_fo_ksef'));
      {? type_of(_row.KRS)=type_of(0) || FAP.KRS:=_row.KRS ?};
      _result:=exec('fap_end','edi_fo_ufd');
      obj_del(_row)
   !}

|? BEER.WYSTFAKS
||
:: korekta
   FAKS.cntx_psh();
   FAP.cntx_psh();
   {? FAKS.name()<>ref_name(BEER.WYSTFAKS)
   ||
      FAKS.use(ref_name(BEER.WYSTFAKS));
      FAP.use('fakpo'+(5-FAKS.name()))
   ?};
   _ii:=0;
   _lp:=0;
   {!
   |? _result & _ii<__FaWiersz_len
   |!
      _ii+=1; _row:=__FaWiersz[_ii];
      _lp+=1;
      _poz:=0;
      _stan_przed:={? type_of(_row.ST_PRZED)=type_of(~~) || '' || _row.ST_PRZED ?};
:: UU_ID
      {? type_of(_row.UU_ID)=type_of('') & _row.UU_ID<>''
      ||
         FAP.index('UU_ID');
         FAP.prefix(_row.UU_ID,BEER.WYSTFAKS);
         {? FAP.first() || _poz:=FAP.POZ ?}
      ?};
:: KTM, IL, CENA - ewentualnie do implementacji
:: POZ
      {? _poz=0 || _poz:=_lp ?};
      {? _poz
      ||
:: przed korektą
         exec('kor_start','edi_fo_ufd',_poz);
         _mat:=FAP.M;
         _ktm:=FAP.M().KTM;
         FAP.M:=null();
         _xml_val:=__XML.VAL;
         {? type_of(_row.EAN)=type_of('') || __XML.VAL:=_row.EAN; exec('EAN','edi_fo_ufd') ?};
         {? type_of(_row.KTM)=type_of('') || __XML.VAL:=_row.KTM; exec('SupplierItemCode','edi_fo_ufd') ?};
         {? type_of(_row.N)=type_of('') || __XML.VAL:=_row.N; exec('ItemDescription','edi_fo_ufd') ?};
         __XML.VAL:=_xml_val;
:: po korekcie
         {? FAP.M<>_mat
         ||
            _msg:='Pozycja %1: niezgodność indeksu (%2;%3).'@[$FAP.POZ,FAP.M().KTM,_ktm];
            {? FAKS.ID<>'' | FAKS.SYM<>''
            || exec('add_kom','#message',_msg,2,'%1 (%2)'[__XML.ID,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?}])
            || exec('add_kom','#message',_msg,2,__XML.ID)
            ?}
         ||
            __P.PREV_WWAL:=__P.PREV_BWAL:=~~;
            {? _stan_przed='1'
            ||
               {? ~_verify
               ||
                  {? type_of(_row.WN)=type_of(0) || __P.PREV_WWAL:=_row.WN ?};
                  {? type_of(_row.WB)=type_of(0) || __P.PREV_BWAL:=_row.WB ?}
               ?};
               _ii+=1; obj_del(_row); _row:=__FaWiersz[_ii]
            ?};
            {? type_of(_row.IL)=type_of(0) || FAP.IL:=_row.IL ?};
            {? type_of(_row.SP_WYM)=type_of('') & _row.SP_WYM='1' || FAP.SP_WYM:='T' ?};
            {? type_of(_row.SV)=type_of('') || FAP.SV:=exec('ufd2sv','edi_fo_ufd','',_row.SV,'VAT') ?};
            __P.GPRICE:=0;
            {? type_of(_row.CB)=type_of(0) || __P.GPRICE:=_row.CB ?};
            {? type_of(_row.WB)=type_of(0) || __P.WB:=_row.WB ?};
            __P.PRICEA:=0;
            {? type_of(_row.CN)=type_of(0) || __P.PRICEA:=_row.CN ?};
            {? type_of(_row.WN)=type_of(0) || __P.WN:=_row.WN ?};
            _result:=exec('fap_end','edi_fo_ufd')
         ?}
      ?};
      obj_del(_row)
   !};
   FAP.cntx_pop();
   FAKS.cntx_pop()
?};
{? _result || exec('faksv','edi_fo_ksef2',1) ?};
_done();
_result


\zaplacono_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Czy wypełniać pozycję "Zaplacono"
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_czy:=0;
_rf:=exec('RodzajFaktury','edi_fo_ksef');
{? (_rf='ROZ' & FAKS.T().ZAL_ROZ='S') | (_rf<>'ZAL' & _rf<>'KOR_ZAL' & FAKS.KW_ZAL>0 & FAKS.KW_ZAL>=FAKS.BRTW & FAKS.BRTW>=0)
|| _czy:=1
?};
_czy


\zaplczesc_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Czy wypełniać pozycje "ZaplataCzesciowa"
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_czy:=0;
_zaplacono:=exec('zaplacono_czy','edi_fo_ksef2');
{? ~_zaplacono
|| FAKSPL.cntx_psh();
   FAKSPL.index('TERM');
   FAKSPL.prefix(FAKS.ref());
   {? FAKSPL.first()
   || {!
      |? {? FAKSPL.TERMPLAT<FAKS.DW
         || _czy:=1
         ?};
         ~_czy & FAKSPL.next()
      !}
   ?};
   FAKSPL.cntx_pop()
?};
_czy


\termplat_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Czy wypełniać pozycje "TerminPlatnosci"
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_czy:=0;
_zaplczesc:=exec('zaplczesc_czy','edi_fo_ksef2');
FAKSPL.cntx_psh();
FAKSPL.index('TERM');
FAKSPL.prefix(FAKS.ref());
{? FAKSPL.first()
|| {!
   |? {? ~_zaplczesc | FAKSPL.TERMPLAT>=FAKS.DW
      || _czy:=1
      ?};
      ~_czy & FAKSPL.next()
  !}
?};
FAKSPL.cntx_pop();
_czy


\xml2tmp_faksv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Tworzy tymczasową tabelę podsumowania vat z pliku ksef
::   WE: _a - objekt z danymi faktury
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_buf:="obj_new('N_WN','N_WV','N_WV_POKRS','P_WN','P_WB','P_WV')";
_FAKSV:=obj_new(
   'KW_NAL_OG',
   's23','s22','s8','s7','s5','s4','s3','s0','szw','soo','snp'
);
{! _ii:=2..obj_len(_FAKSV)  |! _FAKSV[_ii]:=_buf() !};
_Faktura:=_a;
{? var_pres('Fa',_Faktura)>100
||
   {? var_pres('Zamowienie',_Faktura.Fa)>100 & var_pres('ZamowienieWiersz',_Faktura.Fa.Zamowienie)>100
   ||
      _wsk:=_Faktura.Fa.Zamowienie.ZamowienieWiersz;
      _len:={? type_of(_wsk[1])=2 || 1 || obj_len(_wsk) ?};
      {! _ii:=1.._len
      |!
         _wskW:={? _len=1 || _wsk || _wsk[_ii] ?};
         {? var_pres('P_12Z',_wskW)=2
         ||
            _sv:=_wskW.P_12Z;
            _fld:='_a.s%1.P_WN'[_sv]; _add:=type_of(($_fld)(_FAKSV))=type_of(0);
            {? var_pres('P_11NettoZ',_wskW)=2
            || {? _add || ($_fld)(_FAKSV)+=#_wskW.P_11NettoZ || ($_fld)(_FAKSV):=#_wskW.P_11NettoZ ?}
            ?};
            _fld:='_a.s%1.P_WV'[_sv]; _add:=type_of(($_fld)(_FAKSV))=type_of(0);
            {? var_pres('P_11VatZ',_wskW)=2
            || {? _add || ($_fld)(_FAKSV)+=#_wskW.P_11VatZ || ($_fld)(_FAKSV):=#_wskW.P_11VatZ ?}
            ?}
         ?};
         obj_del(_wskW)
      !};
      obj_del(_wsk)

   |? var_pres('FaWiersz',_Faktura.Fa)>100
   ||
      _wsk:=_Faktura.Fa.FaWiersz;
      _len:={? type_of(_wsk[1])=2 || 1 || obj_len(_wsk) ?};
      {! _ii:=1.._len
      |!
         _wskW:={? _len=1 || _wsk || _wsk[_ii] ?};
         {? var_pres('P_12',_wskW)=2
         ||
            _sv:=_wskW.P_12;
            _fld:='_a.s%1.P_WN'[_sv]; _add:=type_of(($_fld)(_FAKSV))=type_of(0);
            {? var_pres('P_11',_wskW)=2
            || {? _add || ($_fld)(_FAKSV)+=#_wskW.P_11 || ($_fld)(_FAKSV):=#_wskW.P_11 ?}
            ?};
            _fld:='_a.s%1.P_WB'[_sv]; _add:=type_of(($_fld)(_FAKSV))=type_of(0);
            {? var_pres('P_11A',_wskW)=2
            || {? _add || ($_fld)(_FAKSV)+=#_wskW.P_11A || ($_fld)(_FAKSV):=#_wskW.P_11A ?}
            ?};
            _fld:='_a.s%1.P_WV'[_sv]; _add:=type_of(($_fld)(_FAKSV))=type_of(0);
            {? var_pres('P_11Vat',_wskW)=2
            || {? _add || ($_fld)(_FAKSV)+=#_wskW.P_11Vat || ($_fld)(_FAKSV):=#_wskW.P_11Vat ?}
            ?}
         ?};
         obj_del(_wskW)
      !};
      obj_del(_wsk)
   ?};
   _wsk:=_Faktura.Fa;
:: 23%, 22% - P_13_1, P_14_1, P_14_1W
   {? var_pres('P_13_1',_wsk)=2 || _FAKSV.s23.N_WN:=#_wsk.P_13_1 ?};
   {? var_pres('P_14_1',_wsk)=2 || _FAKSV.s23.N_WV:=#_wsk.P_14_1 ?};
   {? var_pres('P_14_1W',_wsk)=2 || _FAKSV.s23.N_WV_POKRS:=#_wsk.P_14_1W ?};
:: 8%, 7% - P_13_2, P_14_2, P_14_2W
   {? var_pres('P_13_2',_wsk)=2 || _FAKSV.s8.N_WN:=#_wsk.P_13_2 ?};
   {? var_pres('P_14_2',_wsk)=2 || _FAKSV.s8.N_WV:=#_wsk.P_14_2 ?};
   {? var_pres('P_14_2W',_wsk)=2 || _FAKSV.s8.N_WV_POKRS:=#_wsk.P_14_2W ?};
:: 5% - P_13_3, P_14_3, P_14_3W
   {? var_pres('P_13_3',_wsk)=2 || _FAKSV.s5.N_WN:=#_wsk.P_13_3 ?};
   {? var_pres('P_14_3',_wsk)=2 || _FAKSV.s5.N_WV:=#_wsk.P_14_3 ?};
   {? var_pres('P_14_3W',_wsk)=2 || _FAKSV.s5.N_WV_POKRS:=#_wsk.P_14_3W ?};
:: 4%, 3% - P_13_4, P_14_4, P_14_4W
   {? var_pres('P_13_4',_wsk)=2 || _FAKSV.s4.N_WN:=#_wsk.P_13_3 ?};
   {? var_pres('P_14_4',_wsk)=2 || _FAKSV.s4.N_WV:=#_wsk.P_14_3 ?};
   {? var_pres('P_14_4W',_wsk)=2 || _FAKSV.s4.N_WV_POKRS:=#_wsk.P_14_3W ?};
:: ??? - P_13_5, P_14_5 - dział XII w rozdział 6a ustawy (FAKS.PROC().KOD='WSTO_EE')
:: zakładam, że nie otrzymamy takiej faktury z KSEeF
:: 0% - P_13_6_1
   {? var_pres('P_13_6_1',_wsk)=2 || _FAKSV.s0.N_WN:=#_wsk.P_13_6_1 ?};
:: 0% - P_13_6_2 - WDT
   {? var_pres('P_13_6_2',_wsk)=2 || _FAKSV.s0.N_WN:=#_wsk.P_13_6_2 ?};
:: 0% - P_13_6_3  - eksport
   {? var_pres('P_13_6_3',_wsk)=2 || _FAKSV.s0.N_WN:=#_wsk.P_13_6_3 ?};
:: Zwol. - P_13_7
   {? var_pres('P_13_7',_wsk)=2 || _FAKSV.szw.N_WN:=#_wsk.P_13_7 ?};
:: ??? P_13_8 - poza terytorium kraju minus P_13_5 minus P_13_9
:: zakładam, że nie otrzymamy takiej faktury z KSEeF
:: ??? P_13_9 -  art. 100 ust. 1 pkt 4 ustawy
:: zakładam, że nie otrzymamy takiej faktury z KSEeF
:: -o - P_13_10 - odwrotne obciążenie
   {? var_pres('P_13_10',_wsk)=2 || _FAKSV.soo.N_WN:=#_wsk.P_13_10 ?};
:: P_13_11 - procedura marży
   {? var_pres('P_13_11',_wsk)=2 || _FAKSV.snp.N_WN:=#_wsk.P_13_11 ?};
:: P_15 - kwota należności ogółem
   {? var_pres('P_15',_wsk)=2 || _FAKSV.KW_NAL_OG:=#_wsk.P_15 ?}
?};
_FAKSV


\platnosc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Płatność
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Faktura:=__Faktura;
{? var_pres('Fa',_Faktura)>100 &
   var_pres('Platnosc',_Faktura.Fa)>100
||
   _wsk:=_Faktura.Fa.Platnosc;
   _jest_datazaplaty:=0;
   __N.PAYMENT.clear();
   __N.PAYMENT.blank();
   {? var_pres('FormaPlatnosci',_wsk)=2
   ||
      __N.PAYMENT.DESC:=_wsk.FormaPlatnosci;
      __N.PAYMENT.MEANS:=
         {? -_wsk.FormaPlatnosci='1' || __N.PAYMENT.DESC:='gotówka'; '10'
         |? -_wsk.FormaPlatnosci='2' || __N.PAYMENT.DESC:='karta'; '48'
         |? -_wsk.FormaPlatnosci='3' || __N.PAYMENT.DESC:='bon'; ''
         |? -_wsk.FormaPlatnosci='4' || __N.PAYMENT.DESC:='czek'; '20'
         |? -_wsk.FormaPlatnosci='5' || __N.PAYMENT.DESC:='kredyt'; ''
         |? -_wsk.FormaPlatnosci='6' || __N.PAYMENT.DESC:='przelew'; '31'
         |? -_wsk.FormaPlatnosci='7' || __N.PAYMENT.DESC:='mobilna'; ''
         || ''
         ?}
   ?};
   {? var_pres('DataZaplaty',_wsk)=2
   ||
      _date:=_wsk.DataZaplaty;
      __N.PAYMENT.DATE:=date(#(4+_date),#(7+_date+2),#(_date+2));
      _jest_datazaplaty:=__N.PAYMENT.DATE<>date(0,0,0)
   ?};
   {? _jest_datazaplaty=0 & var_pres('TerminPlatnosci',_wsk)>100
   ||
      _len:={? type_of(_wsk.TerminPlatnosci[1])=2 || 1 || obj_len(_wsk.TerminPlatnosci[1]) ?};
      {! _ii:=1.._len
      |!
         _wskW:={? _len=1 || _wsk.TerminPlatnosci || __wsk.TerminPlatnosci[_ii] ?};
         {? var_pres('Termin',_wskW)=2
         ||
            _date:=_wskW.Termin;
            _date:=date(#(4+_date),#(7+_date+2),#(_date+2));
            {? __N.PAYMENT.DATE=date(0,0,0) | _date<__N.PAYMENT.DATE
            ||
               __N.PAYMENT.DATE:=_date;
               {? __N.PAYMENT.DESC='' & var_pres('TerminOpis',_wskW)=2 || __N.PAYMENT.DESC:=_wskW.TerminOpis ?}
            ?}
         ?};
         obj_del(_wskW)
      !}
   ?};
   {? __N.PAYMENT.DESC='' &
      var_pres('PlatnoscInna',_wsk)=2 &
      var_pres('OpisPlatnosci',_wsk)=2
   ||
      __N.PAYMENT.DESC:=_wsk.OpisPlatnosci
   ?};
   __N.PAYMENT.GROSSA:=__FAKSV.KW_NAL_OG;
   __N.PAYMENT.add();
   exec('formplat','edi_fo_ufd','I');
   {? var_pres('RachunekBankowy',_wsk)>100
   ||
      _wskW:={? type_of(_wsk.RachunekBankowy[1])=2 || _wsk.RachunekBankowy || _wsk.RachunekBankowy[1] ?};
      {? var_pres('NrRB',_wskW)=2 || __N.SELLER.RB:=_wskW.NrRB ?};
      obj_del(_wskW)

   |? var_pres('RachunekBankowyFaktora',_wsk)>100
   ||
      _wskW:=
         {? type_of(_wsk.RachunekBankowyFaktora[1])=2
         || _wsk.RachunekBankowyFaktora
         || _wsk.RachunekBankowyFaktora[1]
         ?};
      {? var_pres('NrRB',_wskW)=2 || __N.SELLER.RB:=_wskW.NrRB ?};
      obj_del(_wskW)
   ?}
::   __N.SELLER.VRB
?}


\data_sprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Data sprzedaży = jedna z poniższych możliwości. Teoretycznie w pliku KSeF wystąpienie jednego wariantu,
::       wyklucza pozostałe.
::       a) /Faktura/Fa/P_6
::       b) /Faktura/Fa/OkresFa/P_6_Do
::       c) Wypełnione są pola /Faktura/Fa/FaWiersz/P_6A (przy każdym wierszu) - wtedy proponuję wziąć najpóźniejszą
::          datę z pozycji.
::       d) Nie istnieje a) ani b) ani c), wtedy DOKUM.KSEFSDAT
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=date(0,0,0);
_Faktura:=__Faktura;
{? var_pres('Fa',_Faktura)>100
||
   _wsk:=_Faktura.Fa;
   {? var_pres('P_6',_wsk)=2
   ||
      _date:=_wsk.P_6;
      _result:=date(#(4+_date),#(2+(5-_date)),#(_date+2))

   |? var_pres('P_6_Do',_wsk)=2
   ||
      _date:=_wsk.P_6_Do;
      _result:=date(#(4+_date),#(2+(5-_date)),#(_date+2))

   |? var_pres('FaWiersz',_wsk)>100
   ||
      obj_del(_wsk);
      _wsk:=_Faktura.Fa.FaWiersz;
      _len:={? type_of(_wsk[1])=2 || 1 || obj_len(_wsk) ?};
      {! _ii:=1.._len
      |!
         _wskW:={? _len=1 || _wsk || _wsk[_ii] ?};
         {? var_pres('P_6A',_wskW)=2
         ||
            _date:=_wskW.P_6A;
            _date:=date(#(4+_date),#(2+(5-_date)),#(_date+2));
            {? _date>_result || _result:=_date ?}
         ?};
         obj_del(_wskW)
      !}
   ?}
?};
_result

:Sign Version 2.0 jowisz:1045 2024/01/30 07:46:31 93672ee8dd650a9ccf604cbfeae1f214ccfa01f10ea0b1d4b4d0f826f5f6ad2fd8111cdde620c251b83a61e15fa16cb45e5eac7b2b6109c5467b467f0329682463fc12c803c98fbbf27a105eff9784c948250d437e00c28dd24a074d3cb9cf1c64289ac19d55322deea944d638c36a24751efd4bac1390bccf84d107859d6da2

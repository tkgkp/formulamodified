:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: typ_ob.fml
:: Utworzony: 27.01.2015
:: Autor: TMR
::======================================================================================================================
:: Zawartosc: Formuly do obslugi typow obiegu
::======================================================================================================================

\exist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja sprawdza czy istnieje typ obiegu
::   WE: _a - [STRING] - nazwa typu obiegu
::   WY: null lub wskazanie na TYPOBIEG
::----------------------------------------------------------------------------------------------------------------------
_nazwa:={? var_pres('_a')=type_of('') || _a || return(null) ?};

_res:=0;
TYPOBIEG.cntx_psh();
TYPOBIEG.index('UNIK');
TYPOBIEG.prefix(_nazwa,_nazwa);
{? TYPOBIEG.first()
|| _res:=TYPOBIEG.ref()
?};
TYPOBIEG.cntx_pop();
_res


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.41 - Emisja]
:: OPIS: Funkcja dodaje typ obiegu
::   WE: _a    - [STRING]  - nazwa typu obiegu
::       [_b]  - [OBJECT]  - obiekt komunikatow
::   WY: null lub wskazanie na TYPOBIEG
::----------------------------------------------------------------------------------------------------------------------
_nazwa:={? var_pres('_a')=type_of('') || _a || return(null()) ?};
 _KOMM:={? var_pres('_b')>100         || _b || ~~             ?};

_res:=exec('exist','typ_ob',_nazwa);

{? _res=null()
|| TYPOBIEG.cntx_psh();
   TYPOBIEG.prefix();
   TYPOBIEG.blank(1);
   TYPOBIEG.NAZWA:=_nazwa;
   {? TYPOBIEG.add()
   || _res:=TYPOBIEG.ref()
   || {? var_pres('_KOMM')>100
      || _KOMM.add('Nie udało się dodać typu obiegu: '+_nazwa,3,,1)
      ?}
   ?};
   TYPOBIEG.cntx_pop()
?};
_res

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:02 8da83508dc7e9129cc4fc5d11f291c706c4b88029f44e4dec61486b4f01c4b13290b643fae85a653da1239c12e05311b97972595228dfd188754b8fcba9e9ca2cd51d2267e6b2474a5e5b384df532f85eb7c127e9d9406891b443396acaf0d627e017f77217b9759c2d40caa9870fec10c39a38e06d7a914ad7fcde3e6fdcc19

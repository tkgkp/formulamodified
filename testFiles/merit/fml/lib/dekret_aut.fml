:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: dekret_aut.fml
:: Utworzony: 04.05.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Metody do automatycznej dekretacji
::======================================================================================================================


\sd_decl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Metody wykorzystywane w automatycznej dekretacji
::  OLD: \sd_decl/skid_dek.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('SD_CLASS',@.CLASS)>0 || return ?};
obj_decl('SD_CLASS',
        obj_fld('Ref',0),

        obj_fld('SYSTEM','KASA'),
        obj_fld('RODZAJ','R'),

        obj_fld('REJ',0),
        obj_fld('DOK_REJ',0),
        obj_fld('VAT_REJ',0),
        obj_fld('GR_VAT',0),
        obj_fld('F_REJ',0),
        obj_fld('F_DOK_RJ',0),
        obj_fld('F_RVAT',0),
        obj_fld('F_GR_VAT',0),

        obj_fld('Error',0),
        obj_fld('ErTxt',' '),

        obj_fld('Wn','W'),
        obj_fld('Ma','M'),
        obj_fld('Str',''),


        obj_fld('SumaWn',0),
        obj_fld('SumaMa',0),

        obj_fld('Lp',0),

        obj_fld('Konto',35*' '), 'czasami potrzebne będzie tylko konto do jakichs celów';

        obj_fld('DEK_OBJ',0),

        obj_fld('TAB',''),

        obj_fld('GR_DOK',''),

        obj_fld('SLO',0),

        obj_meth('__init',"
                _a.opentmp();
:: TODO
::              _a.SYSTEM:=exec('blsystem','skid_for');
                _a.SYSTEM:='';
                _a.RODZAJ:=_b;
                _a.DEK_OBJ:=obj_new(@.CLASS.DEK_CLASS)
        ",2),

        obj_meth('__init',"
:: TODO
::              _a.SYSTEM:=exec('blsystem','skid_for');
                _a.SYSTEM:='';
                _a.DEK_OBJ:=obj_new(@.CLASS.DEK_CLASS)
        ",-1,-1),

        obj_meth('__done',"
                .kas_tmp()
        "),

        'podpowiedź na f3 rodzaju dokumentów';
        obj_meth('rodz_f3',"
:: TODO
::                exec('rodz_sel','skid_dek',.SYSTEM)
        1
        "),

   'zmiana wartoŁci w polu RODZAJ';
        obj_meth('rodzaj',"
                   {? _a=1||
                        .RODZAJ:='M'
                       ||
                        .RODZAJ:='Z'
                   ?}
                "),


        'ustawia odpowiedni kontekst aby udała się formuła sd_key dla ksiegowanego dok';
        obj_virt('sd_cntx'," '' "),

        ' podaje wyróżnik dla typu dokumentu - podpowiedz na f3';
        obj_virt('sd_key'," '' "),

        ' podaje opis wyróżnika dla typu dokumentu';
        obj_virt('sd_opis'," '' "),

:  BL [7.60]
:  do ustawienia kontekstu tabeli typow dokumentow
        obj_virt('td_cntx'," '' "),

:  BL [7.60]
:  po redakcji pola WYROZNIK
        obj_virt('td_chk'," '' "),

        'podpowiedź f3 na wyróżnik ';
        obj_meth('sd_f3'," 1 "),

        ' pętla po pierwszym parametrze - dodanie pustych pozycji ';
        ' dodane sa takie których wyrózników nie ma';
        obj_meth('add_sd'," 1 ",2),

        'sprawdzenie pola rodzaj ';
:: TODO
::      obj_meth('rodz_spr',"exec('rodz_chk','skid_dek',.SYSTEM)"),

        'petla po tabeli przekazanej przez parametr';
        'wykonywane sa dekrety i przesylane do tabeli TMPPOZ';
   obj_virt('loop',"
                {? ($(_a+'.first()'))()
                        ||  {!
                             |?
                                ' petle po wszystkich dekretach pasujacych do dokumentu';
                                .sd_loop();

                                ($(_a+'.next()'))()
                             !};
                             {? .SumaWn$2 <> .SumaMa$2
                                || .er_rout(2)
                             ?}
                 ?}
        "),

        obj_virt('sd_loop',"
                  .sd_loop(0);
                  WAR.clr_fld();
                  {? .fnd_1st()
                  || {! |?
                       {? .for_1() || .fnd_nxt() ?}
                     !}
                  ?}
        ",-1,-1),

        obj_virt('sd_loop',"
                ' '
        ",1),

        obj_meth('kas_tmp',"
                {? do_state<>1
                || {? .system() & var_pres('TMPWYR')>0
                   || obj_del(TMPWYR); &ind_wyr1;
                     {? var_pres('ndx')>0 || &ndx ?}
                   ?};
                   {? .is_kontr() || VAR_DEL.delete('TMP_XD') ?};
                   {? var_pres('TMPPOZ')>0 || obj_del(TMPPOZ); &ind_poz1; &ind_poz2; &ind_poz3; &ind_poz4 ?}
                ?}
        "),

        obj_meth('opentmp',"
                {? var_pres('TMPPOZ')>0
                || TMPPOZ.prefix(); {? TMPPOZ.first || {! |? TMPPOZ.del() !} ?}
                || exec('ini_tpoz','dekret_aut')
                ?};
                {? .is_kontr()
                || {? var_pres('TMP_XD')>0
                   || TMP_XD.prefix(); {? TMP_XD.first || {! |? TMP_XD.del() !} ?}
                   || TMP_XD:=tab_tmp(1,'REF','INTEGER',)
                   ?}
                ?};
                {? .system()
                || {? var_pres('TMPWYR')>0
                   || TMPWYR.prefix(); {? TMPWYR.first || {! |? TMPWYR.del() !} ?}
                   || TMPWYR:=tab_tmp(4,'TMPPOZ','INTEGER','Tmppoz',
                                        'LP','INTEGER','Lp',
                                        'SLU','INTEGER','Slownik wyroznika',
                                        'KOD','INTEGER','Kod wyroznika',
                                        'KW','REAL','Kwota'
                                      );
                      ind_wyr1:=TMPWYR.index('?');
                      {? var_pres('ndx')<0
                      || ndx:=TMPWYR.ndx_tmp('',1,'TMPPOZ',,,'LP',,,'SLU',,,'KOD',,)
                     ?}
                   ?}
               ?}
        "),
::      metody do kontrolingu - wypelnianie tabeli SKIDXD
        obj_meth('add_xd',"
               {? .is_kontr()
               || TMP_XD.REF:=($('#'+_a+'.ref()'))();
                  TMP_XD.add()
               ?}
        "),
        obj_meth('add_sxd',"
               {? .is_kontr() & TMP_XD.first()
                  || {? ROZNE.OKRKON<>null || ROZNE.OKRKON().ROK() ?};
                     SKIDXD.cntx_psh();
                     SKIDXD.use('skxd'+OKRO_F.ROK().KOD+form(OKRO_F.NR, -2));
                     SKIDXD.index('SKIDXD'); SKIDXD.prefix(DOK.ref());
                     {! |?
                        {? .set_xd(TMP_XD.REF) || .xd_loop() ?};
                        TMP_XD.del()
                     !};
                     SKIDXD.cntx_pop()
               ?}
        "),
        obj_virt('set_xd',"1",1),
        obj_virt('xd_loop',"1"),
        obj_meth('to_sxd',"
               SKIDXD.blank();
               SKIDXD.DOK:=DOK.ref();
               SKIDXD.SKID_MB:=_a;
               SKIDXD.PBUD:=_b;
               SKIDXD.JORG:=_c;
               SKIDXD.OKOSZ:=_d;
               SKIDXD.WYM4:=_e;
               SKIDXD.WYM5:=_f;
               SKIDXD.JM:=_g;
               SKIDXD.WAL:=_h;
               SKIDXD.WART:=SKIDXD.WARTW:=0;
               SKIDXD.GEN_AUT:='N';
               {? _g<>null | _h=null
               || _put:=SKIDXD.find_key(_a,_b,_c,_d,_e,_f,_g);
                  SKIDXD.WART+=_j
               || SKIDXD.KURS:=_i;
                  _put:=SKIDXD.find_key(_a,_b,_c,_d,_e,_f,_g,_h,_i);
                  SKIDXD.WART+=_j;
                  SKIDXD.WARTW+=_k
               ?};
               {? _put || SKIDXD.put() || SKIDXD.add() ?}
        ",7,7,7,7,7,7,7,7,1,1,1),

        obj_meth('zap_tmp',"
                exec('ini_tpoz_win','dekret_aut');
                {? TMPPOZ.select()
                || TMPPOZ.erase();
                   1
                ?}
        "),

::      główna metoda księgująca - księguje do DOK, POZ i SKIDXD
        obj_virt('ksieguj',"
                do();
                {? .add_dok()
                || .add_sxd();
                   .add_poz()
                || undo(); .er_rout(3)
                    ?};
                end()
        "),


        obj_virt('add_dok',"
                1
        "),


        obj_virt('add_poz',"
                POZ.index('DOK');
                POZ.prefix(DOK.ref());
                .Lp:={? POZ.last() || POZ.POZ ?} + 1;
                TMPPOZ.index(ind_poz3);
                {? var_pres('optym')=type_of(0) & optym
                || TMPPOZ.index(ind_poz4)
                ?};
                TMPPOZ.prefix();
                {? TMPPOZ.first()
                        || {! |? .one2poz() !}
                ?};
                .er_lev()=0
        "),

        obj_virt('one2poz',"
         {? TMPPOZ.SUM$2<>0
         || POZ.blank();
            {? _=0 || _a:=TMPPOZ.SUM ?};
            .pozfill(_a);
            _ok:=POZ.add();
            {? _ok & TMPPOZ.PR='T'
            || _v:=exec('wartPlat','raty','WAR',,DOK,POZ.DOK);
               {? (POZ.WAL & POZ.SUMW=_v) | (~POZ.WAL & POZ.SUM=_v)
               || exec('copy_drb','raty',DOK,POZ.DOK);
                  POZ.TP:=exec('drb_tpfp','raty','POZ',1); POZ.put()
               ?}
            ?};
            {? _ok & .system() || _ok:=.one2wyr() ?};
            {? _ok & .vat() || .polacz() ?};
            {? _ok
            || .Lp+=1; TMPPOZ.next()
            || undo(); .er_rout(4); 0
            ?}
         || TMPPOZ.next()
         ?}
        "),

        obj_virt('pozfill',"
            POZ.POZ:=.Lp;
            POZ.KON:=TMPPOZ.KON;
            _i:=exec('koment','konto',POZ.KON);
            {? _i>0 || POZ.KOM:=_i ?};
            POZ.SUM:=_a$2;
            POZ.STR:={? TMPPOZ.STR=.Ma || 'Ma' || 'Wn' ?};
            POZ.WAL:=.fnd_wal(TMPPOZ.WAL);
            POZ.KURS:=TMPPOZ.KURS;
            POZ.SUMW:={? POZ.WAL=null || 0 || TMPPOZ.SUMW$2 ?};
            POZ.OP:=TMPPOZ.OP;
            POZ.ID:=TMPPOZ.ID;
            POZ.TP:=TMPPOZ.TP;
            POZ.DATA_R:=TMPPOZ.DATA_R;
            POZ.ODD:=.odd(TMPPOZ.ODD);
            {? POZ.ODD=null & |POZ.ID<>'' || POZ.ODD:=DOK.ODD ?};
            POZ.DO:=TMPPOZ.DO;
            POZ.SYM_ROK:=exec('op_unik_sym','rozrach',POZ.ID,POZ.DO);
            POZ.TID:=TMPPOZ.TID;
            POZ.SLO_PROJ:=.kod_slo(XINFO.SLU_PROJ,TMPPOZ.PROJEKT);
            POZ.SP_WYM:={? TMPPOZ.SP_WYM='' || 'N' || TMPPOZ.SP_WYM ?};
            POZ.RK:=TMPPOZ.RK
        "),

        obj_virt('ref_slo',"
                SLO.prefix();
                {? SLO.seek(_a,SLO.name()) || SLO.ref || null ?}
        ",1),

        obj_virt('kod_slo',"
               {? _a
               || SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(_a);
                  _ref:={? SLO.find_key(_b) & SLO.KOD=_b || SLO.ref || null ?};
                  SLO.cntx_pop();
                  _ref
               || null
               ?}
        ",7,2),

        obj_virt('one2wyr',"
                _ok:=1;
                TMPWYR.index(ind_wyr1);
                TMPWYR.prefix(#TMPPOZ.ref());
                {? TMPWYR.first()
                || {!|? POW.prefix();
                        POW.blank();
                        POW.POZ:=POZ.ref();
                        POW.LP:=TMPWYR.LP;
                        POW.KW:={? TMPWYR.KW$2=0 || POZ.SUM$2 || TMPWYR.KW$2 ?};
                        SLUAPPL.cntx_psh(); SLUAPPL.prefix();
                        {? SLUAPPL.seek(TMPWYR.SLU,) ||  POW.SLU:=SLUAPPL.ref() ?};
                        SLUAPPL.cntx_pop();
                        SLO.cntx_psh(); SLO.prefix();
                        {? SLO.seek(TMPWYR.KOD,) || POW.SLO:=SLO.ref() ?};
                        SLO.cntx_pop();
                        _ok:=POW.add();
                        _ok & TMPWYR.next()
                   !}
                ?}; _ok
        "),

        obj_virt('polacz',"
                VPOZ.cntx_psh(); _t:=#TMPPOZ.ref();
                P_V.index('T_V'); P_V.prefix(_t);
                {? P_V.first()
                || {! |?
                      {? P_V.VPOZ().DOK=DOK.ref()
                      || P_V.prefix();
                         P_V.POZ:=POZ.ref();
                         P_V.TMPPOZ:=0;
                         P_V.put();
                         P_V.prefix(_t); _next:=P_V.first()
                      || _next:=P_V.next()
                      ?};
                      _next
                   !}
                ?};
                VPOZ.cntx_pop()
        "),


        obj_meth('fnd_wal',"
                {? _a<>FINFO.NAROD().KOD & _a<>''
                || SLU.index('NAZ');
                   SLU.prefix();
                   {? SLU.find_key('WALUTY')
                   || SLO.index('SL');SLO.prefix();
                      {? SLO.find_key(SLU.ref(),_a)
                      || SLO.ref
                      || 0
                      ?}
                   || 0
                   ?}
                || 0
                ?}
             "),


        ' metoda szukajaca pierwszego zgodnego wyróżnika dla';
        ' księgowanej pozycji dokumentu - uwaga - fnd_nxt jest wołana rekurencyjnie';
        obj_virt('fnd_1st'," 0 "),

        ' rekurencyjna metoda wołana do wyszukania kolejnego pasującego wzorca dekretacji';
        ' warunek porównania aktualnego rekordu podaje metoda wirtualna sd_equ';
        obj_virt('fnd_nxt'," 0 "),

        obj_virt('for_1',"
                {? .czy_zap(.Wn)
                || {? WAR.W_WN=0 || WAR.W_WN:=1; .wyp_zap(.Wn) ?}; 1
                || {? .for_1(.Wn) || .Str:=.Wn; .dek2tmp(.Wn) ?}; 1
                ?};
                {? .czy_zap(.Ma)
                || {? WAR.W_MA=0 || WAR.W_MA:=1; .wyp_zap(.Ma) ?}; 1
                || {? .for_1(.Ma) || .Str:=.Ma; .dek2tmp(.Ma) ?}; 1
                ?}
        ",-1,-1),

        obj_virt('czy_zap',"
                0
        "),

        obj_virt('wyp_zap',"
                ''
        "),

        obj_virt('czy_wyr',"
                0
        "),

        obj_virt('wyr2zap',"
                ''
        "),

        obj_virt('for_1'," .DEK_OBJ.clr_flds(); 0 ",2),

        ' wedlug wartosci zwroconej przez te funkcje odbywa sie optymalizacja';
        ' ilosci dekretow ';
        obj_virt('opt_wyr'," '' "),

        obj_virt('opt_is'," 1 "),

        obj_meth('system'," 1 "),

        obj_meth('is_kontr',"
                PAR_SKID.get(80)='T'
        "),

        obj_virt('vat',"
                0
        "),


        obj_meth('dek2tmp',"
                TMPPOZ.index(ind_poz1);
                TMPPOZ.prefix();
                {? .DEK_OBJ.KW_PLN$2<>0 | .DEK_OBJ.KW_WAL$2<>0
                || {? .opt_is()
                   || {? ~TMPPOZ.find_key(.opt_wyr(.DEK_OBJ.WALUTA),.DEK_OBJ.KONTO,
                       50+.DEK_OBJ.IDENT,.DEK_OBJ.WALUTA,.DEK_OBJ.PROJEKT,.DEK_OBJ.SP_WYM,_a) | (TMPPOZ.GR_DOK<>.GR_DOK | TMPPOZ.SLO<>.SLO)
                      || .tmp_new(_a)
                      || {? .system()
                         || {? .wyrfill(_a,null) | .is_wyr()
                            || {? .wyr_dup() || TMPPOZ.SUM:=TMPPOZ.SUMW:=0;{? TMPPOZ.add() || .tmp_pol() ?}|| .wyr_del(null) ?}
                            ?}
                         ?}
                      ?};
                      .tmp_add();
                      TMPPOZ.put()
                   || .tmp_new(_a);
                      .tmp_add();
                      TMPPOZ.put()
                   ?};
                   {? var_pres('TMPBRKAS')>0
                   || TMPBRKAS.TMPPOZ:=#TMPPOZ.ref();
                      TMPBRKAS.POZDOK:=$POZDOK.ref();
                      TMPBRKAS.cntx_psh();
                      TMPBRKAS.prefix(); TMPBRKAS.add();
                      TMPBRKAS.cntx_pop()
                   ?};
                   {? .vat() || .utw_pol() ?}
                ?}
        ",2),

        obj_virt('tmp_new',"
                TMPPOZ.last();
                TMPPOZ.blank();
                .tmpfill(_a);
                TMPPOZ.add();
                {? .system() || .wyrfill(_a,TMPPOZ.ref())  ?}
        "),

        obj_virt('odd',"
                ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
                {? _a<>'' & ODD.find_key(_a,_a) || ODD.ref() || null ?}
        "),


        obj_virt('tmpfill',"
                TMPPOZ.WYROZNIK:=.opt_wyr();
                TMPPOZ.STR:=_a;
                .Lp+=1;
                TMPPOZ.LP:=.Lp;
                TMPPOZ.KON:=.DEK_OBJ.KONTO;
                TMPPOZ.ID:=.DEK_OBJ.IDENT;
                TMPPOZ.TP:=.DEK_OBJ.IDATA;
                TMPPOZ.DATA_R:=.DEK_OBJ.RDATA;
                TMPPOZ.ODD:={?.DEK_OBJ.IDENT<>'' || .DEK_OBJ.ODD || '' ?};
                TMPPOZ.DO:=.DEK_OBJ.ODATA;
                TMPPOZ.TID:=.DEK_OBJ.ITYP;
                TMPPOZ.PR:=.DEK_OBJ.PR;
                TMPPOZ.WAL:=.DEK_OBJ.WALUTA;
                TMPPOZ.KURS:=.DEK_OBJ.KURS;
                TMPPOZ.OP:=.DEK_OBJ.OPIS;
                TMPPOZ.SUM:=0;
                TMPPOZ.SUMW:=0;
                TMPPOZ.GR_DOK:=.GR_DOK;
                TMPPOZ.SLO:=.SLO;
                TMPPOZ.PROJEKT:={? TMPPOZ.ID<>'' &
                                   (-TMPPOZ.TID='zob' & -(1+TMPPOZ.STR)='m' | -TMPPOZ.TID='nal' & -(1+TMPPOZ.STR)='w')
                                || .DEK_OBJ.PROJEKT
                                || ''
                                ?};
                TMPPOZ.SP_WYM:={? TMPPOZ.ID<>'' || .DEK_OBJ.SP_WYM || 'N' ?};
                TMPPOZ.RK:={? TMPPOZ.ID<>'' || .DEK_OBJ.RK || '' ?}
        "),

        obj_virt('utw_pol',"
                 {? TMPPOZ.SUM$2<>0
                 || P_V.clear();
                    P_V.index('T_V');
                    _t:=#TMPPOZ.ref();
                    P_V.prefix(_t);
                    {? ~P_V.find_key(VPOZ.ref())
                    || P_V.blank();
                       P_V.TMPPOZ:=_t;
                       P_V.VPOZ:=VPOZ.ref();
                       P_V.add()
                    ?}
                 ?}
        "),


        obj_virt('tmp_add',"
                TMPPOZ.SUM+=.DEK_OBJ.KW_PLN$2;
                {? TMPPOZ.STR=.Wn
                || .SumaWn+=.DEK_OBJ.KW_PLN$2
                || .SumaMa+=.DEK_OBJ.KW_PLN$2
                ?};
                TMPPOZ.SUMW+=.DEK_OBJ.KW_WAL$2
        "),

        obj_virt('wyrfill',"
                {? .konto_be(ROK_F.ref(),ROK_F.SYNT+TMPPOZ.KON)
                || {? .ksw_be()
                   || {? PAR_SKID.get(35)='T'
                      || {? .czy_wyr(_a) || .wyr2zap(_b) || .ksw_loop({?.Str=''||_a||.Str?},_b) ?}
                      ?}
                   ?}
                ?}
        "),

        obj_virt('is_wyr',"
                TMPWYR.index(ind_wyr1);
                TMPWYR.prefix(#TMPPOZ.ref);
                TMPWYR.first()
        "),

        obj_virt('wyr_be',"
                SKID_WYR.index('SLU');
                SKID_WYR.prefix(REF.FIRMA,.TAB,($(.TAB+'.name()'))(),($('#'+.TAB+'.ref'))(),_a)
        "),

        obj_virt('konto_be',"
                KS.index('SYM');
                KS.prefix(_a,_b);
                KS.first() & KS.SYM=_b
        "),


        obj_virt('ksw_be',"
                KS_W.index('LP');
                KS_W.prefix(KS.ref());
                KS_W.first()
        "),

        obj_virt('ksw_loop',"
                .wyr_be(_a);
                _ok:=0;
                {!|? _jest:=0; _dalej:=1;
                     {? var_pres('vroz')>0 & vroz & TMPPOZ.KON=VROZ.KONTO
                     || VPOW.index('VPOW'); VPOW.prefix(VROZ.ref(),KS_W.LP);
                        _dalej:=~{? .vpow2pow(_b) || _jest:=_ok:=1 ?}
                     ?};
                     {? _dalej & SKID_WYR.find_key(KS_W.SLU)
                     || {!|? {? SKID_WYR.SLU=KS_W.SLU & .ustal_wyr(_b)
                             || _jest:=1;_ok:=1;0
                             || SKID_WYR.next()
                             ?}
                        !}
                     ?};
                     {? ~_jest & KS_W.ST<>null
                     || _ok:=.wyr_new(_b,KS_W.LP,KS_W.SLU,KS_W.ST,.DEK_OBJ.KW_PLN$2)
                     ?};
                     KS_W.next()
                !};
                _ok
        "),

        obj_virt('ustal_wyr',"
                _ok:=0;
                {? SKID_WYR.KWOTA<>null & SKID_WYR.KOD=null & SKID_WYR.FORMULA=null
                || SLO.index('SL'); SLO.prefix(KS_W.SLU().SLU);
                   {? SLO.first()  || {!|? _ok:=.wyr_ok(_a); SLO.next() !} ?}
                || {? .ust_kod()<>null
                   || _ok:={? SKID_WYR.PROC$2=0 | SKID_WYR.PROC$2=100
                           || .wyr_ok(_a)
                           || .ust_proc(_a)
                           ?}
                   ?}
                ?}; _ok
        "),

        obj_virt('wyr_ok',"
                _ok:=0;
                {? {? SKID_WYR.KWOTA<>null
                   || _kw:=($SKID_WYR.KWOTA().FORMULA)(); type_of(_kw)=1 & _kw$2<>0
                   |? SKID_WYR.PROC$2<>0
                   || _kw:={? _>1 || _b || ((SKID_WYR.PROC*.DEK_OBJ.KW_PLN)/100)$2 ?}; 1
                   || _kw:=.DEK_OBJ.KW_PLN$2; 1
                   ?}
                || _ok:=.wyr_new(_a,KS_W.LP,SKID_WYR.SLU,SLO.ref(),_kw)
                ?}; _ok
        "),

        obj_virt('ust_kod',"
                 _ref:=null;
                 {? SKID_WYR.KOD<>null
                 || SKID_WYR.KOD(); _ref:=SLO.ref
                 |? SKID_WYR.FORMULA<>null
                 || _kod:=($SKID_WYR.FORMULA().FORMULA)();
                   {? type_of(_kod)=2 & _kod<>''
                   || SLO.index('SL'); SLO.prefix(KS_W.SLU().SLU,_kod);
                      {? SLO.first() & SLO.KOD=_kod || _ref:=SLO.ref ?}
                   ?}
                 ?}; _ref
       "),

        obj_virt('ust_proc',"
                _kw:=_sum:=_ok:=0;
                {!|? {? .ust_kod()<>null & SKID_WYR.PROC<>0
                     || _sum+=SKID_WYR.PROC;
                        {? _sum>=100 || _ok:=.wyr_ok(_a,.DEK_OBJ.KW_PLN-_kw) || _ok:=.wyr_ok(_a) ?};
                        _kw+=((SKID_WYR.PROC*.DEK_OBJ.KW_PLN)/100)$2
                     ?};
                     _ok & SKID_WYR.next()
                !};
                _ok
        "),


        obj_virt('vpow2pow',"
                _ok:=0;
                {? VPOW.first()
                || _ok:=1;
                   {!|? .wyr_new(_a,KS_W.LP,KS_W.SLU,VPOW.KOD,VPOW.KW); VPOW.next() !}
                ?}; _ok
        "),

        obj_virt('wyr_new',"
                TMPWYR.blank();
                TMPWYR.TMPPOZ:=#_a;
                TMPWYR.LP:=_b;
                TMPWYR.SLU:=#_c;
                TMPWYR.KOD:=#_d;
                TMPWYR.KW:=_e;
                TMPWYR.clear();
                TMPWYR.add()
        "),

        obj_virt('wyr_dup',"
                _ref:=null; TMPPOZ.cntx_psh();  TMPPOZ.index(ind_poz1);
                TMPPOZ.prefix(TMPPOZ.WYROZNIK,TMPPOZ.KON,TMPPOZ.ID,TMPPOZ.WAL,TMPPOZ.PROJEKT,TMPPOZ.SP_WYM,TMPPOZ.STR); _ok:=1;
                {? TMPPOZ.first
                || {! |?
                      _ok:=1; TMPWYR.index(ind_wyr1); TMPWYR.prefix(0); _lp:=0;
                      {? TMPWYR.first()
                      || {! |?
                            {? _lp<>TMPWYR.LP
                            || _lp:=TMPWYR.LP; TMPPOZ.cntx_psh(); TMPWYR.cntx_psh(); TMPWYR.index(ndx);
                               TMPWYR.prefix(#TMPPOZ.ref()); _ile:=TMPWYR.size();
                               {? TMPWYR.first() || _ok:=.spr_tmp(_ile) ?};
                               TMPPOZ.cntx_pop();  TMPWYR.cntx_pop()
                            ?};
                            _ok=0 & TMPWYR.next()
                         !};
                         {? _ok=0 || _ok:=-1; _ref:=TMPPOZ.ref() ?}
                     ?};
                     _ok<>-1 & TMPPOZ.next()
                   !}
                 ?};
                 TMPPOZ.cntx_pop();
                 {? _ok=-1 || _ok:=0; TMPPOZ.seek(_ref) ?};_ok
        "),



        obj_virt('spr_tmp',"
                _ok:=1;
                {!|? _ref:=TMPWYR.TMPPOZ;
                     TMPWYR.cntx_psh();
                     TMPWYR.index(ind_wyr1);TMPWYR.prefix(_ref);
                     {? TMPWYR.first()
                     || _jest:=0;
                        {!|? TMPWYR.cntx_psh();
                             TMPWYR.index(ind_wyr1); TMPWYR.prefix(0,TMPWYR.LP,TMPWYR.SLU,TMPWYR.KOD);
                             _jest:={? TMPWYR.first() || _jest+1 || 0 ?};
                             TMPWYR.cntx_pop();
                             _jest>0 & TMPWYR.next()
                         !}
                     ?};
                     TMPWYR.cntx_pop();
                     {? _a=_jest || _ok:=0; TMPPOZ.seek(TMPWYR.TMPPOZ,) ?};
                     _ok & TMPWYR.next()
                !};_ok
        "),

        obj_virt('tmp_pol',"
                 TMPWYR.index(ind_wyr1);
                 TMPWYR.prefix(0);
                 {? TMPWYR.first()
                 || {!|? TMPWYR.TMPPOZ:=#TMPPOZ.ref();
                         TMPWYR.cntx_psh();
                         TMPWYR.prefix();
                         TMPWYR.put();
                         TMPWYR.cntx_pop();
                         TMPWYR.first()
                    !}
                 ?}
        "),


        obj_virt('wyr_del',"
                 TMPWYR.index(ind_wyr1);
                 TMPWYR.prefix(#_a);
                 {? TMPWYR.first()
                 || {!|? TMPWYR.cntx_psh(); _kw:=TMPWYR.KW$2;
                         TMPWYR.prefix(#TMPPOZ.ref,TMPWYR.LP,TMPWYR.SLU,TMPWYR.KOD);
                         {? TMPWYR.first() || TMPWYR.KW+=_kw; TMPWYR.put() ?};
                         TMPWYR.cntx_pop();
                         TMPWYR.del()
                    !}
                 ?}
        "),

        ' metoda ustawiająca index na tabeli';
        obj_virt('sd_ind',"
        "),

        ' metoda ustawiająca prefix na tabeli';
        obj_virt('sd_pref',"
                .sd_cntx()
        "),

        ' metoda sprawdzająca zgodność ksiegowanego dok. z wyróznikiem';
        obj_virt('sd_equ'," 1 "),

:      metoda ustawiajaca pola REJ, DOK_REJ, VAT_REJ, GR_VAT
        obj_meth('sd_rej'," '' "),

        obj_meth('skip_e',"
                .Error<>0
        "),

        ' metoda obsługujaca błędy';
        obj_meth('er_rout'," {? var_pres('ks_dial')<=0 || .Error:=_a ?};
                             {? .skip_e()
                             || {? _a=1
                                || .ErTxt:='Brak definicji automatu dla parametrów dokumentu: ' +.sd_key()
                                |? _a=2
                                || .ErTxt:='Błędna suma kontrolna: Suma Wn ' +form(.SumaWn,,2)+', Suma Ma '+form(.SumaMa,,2)
                                |? _a=3
                                || .ErTxt:='Nie można dodać nagłówka dokumentu\nTabela DOK'
                                |? _a=4
                                || .ErTxt:='Nie można dodać pozycji dokumentu\nTabela POZ'
                                || '??'
                                ?}
                             ?}
                           "),


        ' zwraca kod pierwszego napotkanego błędu';
        obj_meth('er_lev',"
                .Error
        "),

        ' zwraca opis pierwszego napotkanego błędu';
        obj_meth('er_txt',"
                {? .Error=0
                        || ''
                        || .ErTxt
                ?}
        "),

        obj_meth('msg_err',"
         FUN.info('W trakcie księgowania nastąpił błąd.\n'+
          '\nKod błędu: '+form(SD_OB.er_lev(),-3)+
          '\nOpis: '+SD_OB.er_txt())
        "),

: Tworzy nowy rekord (kopie wybranego dekretu)
: dekret dla typu dokumentu przekazanego parametrami.
  obj_meth('fnd_add'," '' "),

        obj_virt('copy'," '' "),
        obj_meth('set_gr',"
               .GR_DOK:=_a;
               .SLO:={? type_of(_b)=1 || _b || #_b?}
        "),
        obj_meth('clr_gr',"
               .set_gr('',0)
        ")
)


\ini_tpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [7.62]
:: OPIS: Stworzenie tabeli tymczasowej TMPPOZ i TMPKAS
::  OLD: \ini_tpoz/skid_dek.fml
::----------------------------------------------------------------------------------------------------------------------
TMPPOZ:=tab_tmp(7,
                  'WYROZNIK','STRING[20]','Wyroznik',
                  'KON','STRING[35]','Konto',
                  'ID','STRING[50]','Ident',
                  'WAL','STRING[3]','Waluta',
                  'PROJEKT','STRING[8]','Projekt',
                  'SP_WYM','STRING[1]','Wymagany split payment',
                  'STR','STRING[1]','St',
                  'SUM','REAL','Kwota w PLN',
                  'TP','DATE','Data rozr.',
                  'TID','STRING[3]','Typ rozrach.',
                  'OP','STRING[100]','Tresc',
                  'SUMW','REAL','Kwota w walucie',
                  'VPOZ','INTEGER','Vpoz',
                  'DO','DATE','Data otwarcia',
                  'KURS','REAL','Kurs',
                  'DATA_R','DATE','Data rozl.',
                  'ODD','STRING[8]','Jednostka  księgowa',
                  'LP','REAL','Lp',
                  'PR','STRING[1]','Platnosc rat.',
                  'GR_DOK','STRING[20]','Grupa dokumentów',
                  'SLO','INTEGER','',
                  'RK','STRING[1]','Różnica kursowa');
ind_poz1:=TMPPOZ.index('?');
ind_poz2:=TMPPOZ.ndx_tmp('',1,'KON',,0,'ID',,0,'OP',,0);
ind_poz3:=TMPPOZ.ndx_tmp('',1,'WYROZNIK',,0,'LP',,0);
ind_poz4:=TMPPOZ.ndx_tmp('',1,'KON',,0,'LP',,0)


\ini_tpoz_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK  [7.62]
:: OPIS: Stworzenie tabeli tymczasowej TMPPOZ i TMPKAS
::----------------------------------------------------------------------------------------------------------------------

:---------okienko wertowania--------------------------
_wer:=TMPPOZ.mk_sel('Tymczasowy dekret'@,,,'tmppoz_wer');
TMPPOZ.win_fld(_wer,,'WYROZNIK',,,14);
TMPPOZ.win_fld(_wer,,'KON',,,12);
TMPPOZ.win_fld(_wer,,'ID',,,15);
TMPPOZ.win_fld(_wer,,'STR',,,2);
TMPPOZ.win_fld(_wer,,'SUM',,,12);
TMPPOZ.win_fld(_wer,,'SUMW',,,12);
TMPPOZ.win_fld(_wer,,'GR_DOK',,,20);
TMPPOZ.win_act(_wer,0,'Formuła','Wybierz'@@,,,,"sel_exit()",1,,,,'W');
TMPPOZ.win_act(_wer,0,'Szukaj');
TMPPOZ.win_act(_wer,0,'Dołącz');
TMPPOZ.win_act(_wer,0,'Usuń');
TMPPOZ.win_act(_wer,0,'Popraw');
TMPPOZ.win_act(_wer,0,'Kolejność');
TMPPOZ.win_act(_wer,1,'Dołącz',,,,,,1);
TMPPOZ.win_sel(_wer);
:---------okienko redakcyjne--------------------------
_red:=TMPPOZ.mk_edit('Pozycja dokumentu'@,0,'tmppoz_red');
TMPPOZ.win_efld(_red,,'WYROZNIK',,,20);
TMPPOZ.win_efld(_red,,'KON',,,35);
TMPPOZ.win_efld(_red,,'ID',,,20);
TMPPOZ.win_efld(_red,,'TP',,,10);
TMPPOZ.win_efld(_red,,'TID',,,3);
TMPPOZ.win_efld(_red,,'OP',,,30);
TMPPOZ.win_efld(_red,,'STR',,,2);
TMPPOZ.win_efld(_red,,'SUM',,,19);
TMPPOZ.win_efld(_red,,'WAL',,,3);
TMPPOZ.win_efld(_red,,'SUMW',,,19);
TMPPOZ.win_edit(_red)


\dek_decl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Deklaracja obiektu DEK_CLASS
::  OLD: \dek_decl/skid_ret.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('DEK_CLASS',@.CLASS)>0 || return ?};
obj_decl('DEK_CLASS',
   obj_fld('KONTO',35*' '),
   obj_fld('KW_PLN', 0.00),
   obj_fld('KW_KAUC', 0.00),
   obj_fld('KW_WAL', 0.00),
   obj_fld('OPIS', 30*' '),
   obj_fld('WALUTA', 3*' '),
   obj_fld('KURS', 0.0000),
   obj_fld('IDENT', 50*' '),
   obj_fld('SYM_ROK', 55*' '),
   obj_fld('IDATA', date(0,0,0)),
   obj_fld('ODATA', date(0,0,0)),
   obj_fld('RDATA', date(0,0,0)),
   obj_fld('ODD', ''),
   obj_fld('RKWOT', 0),
   obj_fld('STVAT', 0),
   obj_fld('GRVAT', 0),
   obj_fld('STR', ''),
   obj_fld('ITYP', ' '),
   obj_fld('FLA',0),
   obj_fld('PR','N'),
   obj_fld('KW_ZAL',0),
   obj_fld('PROJEKT',''),
   obj_fld('SP_WYM','N'),
   obj_fld('RK',''),

   obj_meth('__init',"_a.FLA:=obj_new(@.CLASS.FL_CLASS)",-1,-1),

   obj_meth('__done',"obj_del(.FLA)"),

   obj_meth('clr_flds',"
   .KONTO:='';
   .KW_WAL:=0;
   .WALUTA:='';
   .KURS:=0;
   .KW_PLN:=0;
   .KW_KAUC:=0;
   .IDENT:='';
   .SYM_ROK:='';
   .OPIS:='';
   .IDATA:=date(0,0,0);
   .ODATA:=date(0,0,0);
   .RDATA:=date(0,0,0);
   .ITYP:='';
   .ODD:='';
   .RKWOT:=0;
   .STVAT:=0;
   .GRVAT:=0;
   .PR:='';
   .KW_ZAL:=0;
   .PROJEKT:='';
   .SP_WYM:='N';
   .RK:='N';
   ~~
   "),

   obj_meth('prolog',"
        WAR.set_fld();
        {? DEKRET.PROLOG<>0
   || DEKRET.PROLOG();
      .FLA.run()
   ?}
   "),

        obj_virt('epilog',"
   {? DEKRET.EPILOG<>0
   || DEKRET.EPILOG();
      .FLA.run()
   ?}
   "),

   obj_meth('run',"
        .prolog();
        .clr_flds();
        .STR:=_a;
   {? DEKRET.KONTO<>null || .KONTO:=(DEKRET.KONTO();.FLA.run()) ?};
   {? DEKRET.KW_WAL<>null || .KW_WAL:=(DEKRET.KW_WAL();.FLA.run())?};
   {? DEKRET.WALUTA<>null || .WALUTA:=(DEKRET.WALUTA();.FLA.run())?};
   {? DEKRET.KURS_WAL<>null || .KURS:=(DEKRET.KURS_WAL();.FLA.run())?};
   {? DEKRET.KW_PLN<>null || .KW_PLN:=(DEKRET.KW_PLN();.FLA.run())?};
   {? DEKRET.TRESC<>null || .OPIS:=(DEKRET.TRESC();.FLA.run())?};
        .operacja();
   .epilog()
        "),

        obj_meth('operacja',"
        {? DEKRET.OPERACJA<>0
        || .IDENT:=(DEKRET.OPERACJA();.FLA.run());
           {? .IDENT<>''
           || .ODATA:=WAR.data_o(.KONTO);.RDATA:=WAR.data_r();
              .IDATA:={? DEKRET.DATA<>null || DEKRET.DATA(); .FLA.run() || date(0,0,0) ?};
              {? .IDATA=date(0,0,0)
              || .IDATA:=WAR.data_t(.KONTO);
                 {? .IDATA=date(0,0,0) & .ODATA<>date(0,0,0) || .IDATA:=.ODATA ?};
                 {? .IDATA<>date(0,0,0) & .ODATA=date(0,0,0) || .ODATA:=.IDATA ?}
              ?};
              .ODD:=POZDOK.ODD().OD;
              {? DEKRET.N_Z<>0 || .ITYP:=(DEKRET.N_Z();.FLA.run())?}
           ?}
        ?}
        "),

       obj_meth('run2',"
       .clr_flds();.konto(); .KW_WAL:=0; .KURS:=0; .WALUTA:='';
       _k:=exec('czy2tab','dok_fks') & exec('ile_wal','dok_fks')=1;
       .KW_PLN:=.kwota(_k);
       .ODD:={?+form(DEK.ODD_ROZR)>0 ||($DEK.ODD_ROZR)() ||''?};
       .OPIS:={?+form(DEK.OP)>0 ||($DEK.OP)() ||''?};
       _rodzkKOD:=DEK.RODZK().KOD;
       {? -form(_rodzkKOD)='warfak' & FAK.get()
       || {? (type_of(.KONTO)<>2 | .KONTO='') ||.KONTO:=FAK.KONTO ?};
          {? FAK.OPIS<>'' ||.OPIS:=FAK.OPIS ?};
          {? .czy_wal(.KONTO) & FAK.WAL<>FINFO.NAROD
          || .KW_WAL:={?.KW_PLN$2<>0||FAK.WARW?}*DEK.WSP$2;.KURS:=FAK.KRS;.WALUTA:=FAK.WAL().KOD
          ?}
       |? -form(_rodzkKOD)='pkontrol' & SKIDXD.get() & .czy_wal(.KONTO) & SKIDXD.WAL<>FINFO.NAROD
       || .KW_WAL:=SKIDXD.WARTW;.KURS:=SKIDXD.KURS;.WALUTA:=SKIDXD.WAL().KOD
       |? -form(_rodzkKOD)='nettocit' & .czy_wal(.KONTO)
       || .KURS:=exec('kurs_cit','dok_fks',DOK.DTW,DOK.WAL); .KW_WAL:=VPOZ.WARW; .WALUTA:=DOK.WAL().KOD
       |? -form(_rodzkKOD)='warcit' & fak & FAK.get() & .czy_wal(.KONTO)
       || .KURS:=exec('kurs_cit','dok_fks',DOK.DTW,FAK.WAL); .KW_WAL:=FAK.WARW; .WALUTA:=FAK.WAL().KOD
       |? .KW_PLN$2<>0 & .czy_wal(.KONTO)
       || .kwota_w(_k)
       ?}; .operac2()
       "),

        obj_meth('operac2',"
        {? .czy_roz(.KONTO) & +form(DEK.ID)>0
        || {? -form(DEK.RODZK().KOD)='warfak' & FAK.get()
           || .IDENT:={? ($DEK.ID)()<>'' || .IDENT:=($DEK.ID)() || FAK.SYM ?};
              {?.IDENT<>''
              ||
                .IDATA:=FAK.TZ; .ODATA:=FAK.DTW;
                .ITYP:={?+form(DEK.TID)>0||($DEK.TID)()||''?};
                .PR:=DEK.PR;
                .PROJEKT:={? DEK.PROJEKT<>'' || ($DEK.PROJEKT)() || '' ?}
              ?}
           || .IDENT:=($DEK.ID)();
              {?.IDENT<>''
              ||.ODATA:={?+form(DEK.DO)>0 ||($DEK.DO)() ||date(0,0,0)?};
                .IDATA:={?+form(DEK.TP)>0 ||($DEK.TP)() ||date(0,0,0)?};
                .ITYP:={?+form(DEK.TID)>0||($DEK.TID)()||''?};
                .PR:=DEK.PR;
                .PROJEKT:={? DEK.PROJEKT<>'' || ($DEK.PROJEKT)() || '' ?}
              ?}
           ?}
        || .SP_WYM:='N'
        ?}
        "),


        obj_meth('czy_wal',"
        KS.cntx_psh();
        KS.index('SYM');
        KS.prefix(SSTALE.AR,SSTALE.AR().SYNT+_a);
        _ok:=KS.first() & KS.WIELO='T';
        KS.cntx_pop();
        _ok
        "),

        obj_meth('czy_roz',"
        KS.cntx_psh();
        KS.index('SYM');
        KS.prefix(SSTALE.AR,SSTALE.AR().SYNT+_a);
        _ok:=KS.first() & KS.ROZR<>'Z';
        KS.cntx_pop();
        _ok
        "),

        obj_meth('set_rok',"
        _kod:=4-DOK.name()-2;
        ROK_F.index('KOD'); ROK_F.prefix(REF.FIRMA); ROK_F.find_key(_kod)
        "),

        obj_meth('konto',"
        _rodzkKOD:=DEK.RODZK().KOD;
        .KONTO:={? (5+form(_rodzkKOD)='Konto' | _rodzkKOD='VatKonto') & DEK.KONTO=''
                || VROZ.KONTO
                || ($DEK.KONTO)()
                ?};
        {? type_of(.KONTO)<>2 | .KONTO='<- F3 ->' || .KONTO:='' ?}
        "),

        obj_meth('kwota',"
        _r:=-form(DEK.RODZK().KOD);
        .SP_WYM:='N';
        _w:={? _r='netto'   || .SP_WYM:=VPOZ.SP_WYM; {? DOK.KRAJ().KOD<>'PL' || VPOZ.WCLO || {? _a || VPOZ.NETTO_D || VPOZ.NETTO ?}-VPOZ.AKCYZA ?}
        |? _r='nettocit' || (exec('kurs_cit','dok_fks',DOK.DTW,DOK.WAL)*VPOZ.WARW/exec('jed_wal','waluty',DOK.WAL().KOD))$2
        |? _r='warcit' & fak & FAK.get() || (exec('kurs_cit','dok_fks',DOK.DTW,FAK.WAL)*FAK.WARW/exec('jed_wal','waluty',FAK.WAL().KOD))$2
        |? _r='brutto'  || .SP_WYM:=VPOZ.SP_WYM; {? _a || VPOZ.BRUTTO_D || VPOZ.BRUTTO ?}
        |? _r='podatek' || {? DOK.KRAJ().KOD<>'PL' || VPOZ.CLO || {? _a || VPOZ.VAT_D || VPOZ.VAT ?} ?}
        |? _r='podatrej' || VPOZ.VAT
        |? _r='akcyza'  || VPOZ.AKCYZA
        |? _r='cło'     || VPOZ.CLO
        |? _r='wartcel' || VPOZ.WCLO
        |? _r='vat_odl' || VPOZ.VAT_ODL
        |? _r='vatkoszt' || VPOZ.VATKOSZT
        |? _r='manipula' | _r='wardod' || VPOZ.KOS
        |? _r='warfak' & fak || {? FAK.get() || FAK.WAR || 0 ?}
        |? 5+_r='konto' & vroz || {? VROZ.get() || VROZ.KW || 0 ?}
        |? _r='vatkonto' & vroz
        || GR_VAT.cntx_psh();
           SLO.cntx_psh();
           _kwota:={? exec('gr_pod_typ','dok_fks',VPOZ.GRVAT().GRVAT().KOD)=0
                   || VPOZ.BRUTTO
                   || VPOZ.NETTO
                   ?};
           SLO.cntx_pop();
           GR_VAT.cntx_pop();
           {? VROZ.get() & _kwota<>0 || ((VPOZ.VATKOSZT*VROZ.KW)/_kwota)$2 || 0 ?}
        |? _r='pkontrol' & fak
        || {? SKIDXD.get || SKIDXD.WART || 0 ?}
        |? _r='vatpkont' & fak
        || {? SKIDXD.get
           || {? var_pres('VatPKont')<=0
              || VatPKont:=exec('VatPKont','dekret_aut')
              ?};
              _obj:=VatPKont;
              (SKIDXD.WART*_obj.wart())$2
           || 0
           ?}
        |? 2+_r='pf' & fak
        || _czypkor:=exec('czy_pozf_kor','dok_fks1');
           {? _czypkor
           || {? _r='pfnetto' || -POZF.WN |? _r='pfbrutto' || -POZF.WB || -POZF.WV ?}
           || {? _r='pfnetto' || POZF.WN |? _r='pfbrutto' || POZF.WB || POZF.WV ?}
           ?}
        || 0
        ?}*DEK.WSP$2;
        _w
        "),

        obj_meth('kwota_w',"
        _r:=-form(DEK.RODZK().KOD);
        {? DOK.KRAJ().KOD<>'PL'
        || .KURS:=VPOZ.KOS;.WALUTA:=DOK.JORG().KOD;
           {? _r='podatek' || .KW_WAL:=VPOZ.VAT
           |? _r='netto' || .KW_WAL:=VPOZ.NETTO
           |? _r='brutto' | (_r='warfak' & fak & ~FAK.get) || .KW_WAL:=VPOZ.BRUTTO
           ?}
        || {? KA.WALUTA
           || {? _r='netto'
              || .KW_WAL:=VPOZ.NETTO_W; .WALUTA:=DOK.WAL().KOD;
                 {? _a || .KURS:=VPOZ.KURS_D || .KURS:=VPOZ.KURS ?}
              |? _r='brutto'
              || .KW_WAL:=VPOZ.BRUTTO_W; .WALUTA:=DOK.WAL().KOD;
                 {? _a || .KURS:=VPOZ.KURS_D || .KURS:=VPOZ.KURS ?}
              |? _r='podatek'
              || .KW_WAL:=VPOZ.VAT_W; .WALUTA:=DOK.WAL().KOD;
                 {? _a || .KURS:=VPOZ.KURS_D || .KURS:=VPOZ.KURS ?}
              |? _r='netto' | (VPOZ.NETTO$2=VPOZ.BRUTTO$2 & _r='brutto') | exec('czy_brutto','dok_fks')
              || .KW_WAL:=VPOZ.WARW; .WALUTA:=DOK.WAL().KOD;
                 {? _a || .KURS:=VPOZ.KURS_D || .KURS:=VPOZ.KURS ?}
              ?}
           ?}
        ?}
        "),


        obj_meth('run3',"
        .clr_flds();
     .KONTO:=_b;
   .KW_PLN:=_a$2;
   .OPIS:=DOK.TR;
   {? _>2 || .IDENT:=_c ?};
   {? _>3 || .ITYP:=_d ?};
   {? _>4 || .ODATA:=_e ?};
   {? _>5 || .IDATA:=_f ?};
   {? _>6 & _g<>'' || .OPIS:=_g ?};
   {? _>7 || .WALUTA:=_h ?};
   {? _>8 || .KW_WAL:=_i ?};
   {? _>9 || .KURS:=_j ?};
   {? _>10|| .RDATA:=_k ?};
   {? _>11|| .ODD:=_l ?};
   {? _>12|| .RK:=_m ?}
        "),

)


\fl_decl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Deklaracja klas FL_CLASS i VAR_CLASS oraz powolanie obiektow FL i WAR
::  OLD: \fl_decl/skid_for.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('FL_CLASS',@.CLASS)>0 || return ?};
exec('var_decl','dekret_aut');

obj_decl('FL_CLASS',
 obj_fld('TYP_WYN','#'),
:: TODO
:: obj_fld('SYSTEM',exec('system','skid_for')),
:: obj_fld('NSYSTEM',exec('nsystem','skid_for')),
:: Wygląda na nieużywane:
::   obj_fld('SYSTEM',''),
::   obj_fld('NSYSTEM',''),
:: obj_fld('PAKIET','Merit'),
:: obj_fld('WYTWORCA','Macrologic'),
:: obj_fld('WERSJA','XXXX'),
 obj_meth('__init'," '#' "),
 'dopuszczalne rodzaje formuł ze wzgl. na typ wyniku';
 'N - numer (zwraca integer)';
 'S - string (zwraca string)';
 'K - konto (zwraca string max 15)';
 'I - identyfikator (max 15 string)';
 'D - data';
 'M - czas';
 'T - kurs';
 'R - kwota (real)';
 '# - kwota (real) na potrzeby IFa';
 'W - WALUTA (string 3)';
 'B - boolean (1 lub 0)' ;
 '^ - automatyczne formuły w mag.paletowym';
 '> - formuła dla kodu cechy dostawy';
 'X - formula opisu kodu ID';
 'Y - formula kodu ID';
 obj_meth('set_tw'," _a:=~-_a;
                    {? 'NSKIDTRWBVA#MZ%^>XY'*_a>0
                    || .TYP_WYN:=_a
                    || exec('msg_wart','dekret_aut','FORMULA.RODZAJ','Błąd techniczny.')
                    ?}"),
 'formuła jest wykonywana w konstrukcji wiążącej aby można było w jej treści';
 'odwoływac się do pól obiektu WAR typu VAR_CLASS bez prefiksowania nazwą obiektu';
 obj_meth('run',".set_tw(FORMULA.RODZAJ);
                 ($('{|WAR|!'+FORMULA.FORMULA+'|}'))()")
);
FL:=obj_new(@.CLASS.FL_CLASS);
WAR:=obj_new(@.CLASS.VAR_CLASS)


\msg_wart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Funkcja zwracajaca komunikat
::   WE: _a - string do komunikatu
::       _b - string do komunikatu
::  OLD: \msg_wart/skid_pom.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.emsg('\nNiedozwolona wartość pola >>%1<<\n\n%2'@[_a,_b])


\var_decl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Dekretacja - obiekt VAR_CLASS
::  OLD: \var_decl/skid_var.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('VAR_CLASS',@.CLASS)>100 || return ?};

obj_decl('VAR_CLASS',
        obj_fld('KW_PLN',0.00),
        obj_fld('KW_KAUC', 0.00),
        obj_fld('ANALIT1', 8*' '),
        obj_fld('ANALIT2', 8*' '),
        obj_fld('KONTOTXT', 35*' '),
        obj_fld('KONTO1', 35*' '),
        obj_fld('KONTO2', 35*' '),
        obj_fld('KW_WAL', 0.00),
        obj_fld('WAR_WAL', 0.00),
        obj_fld('KW_CLO', 0.00),
        obj_fld('KW_AKCYZ', 0.00),
        obj_fld('KW_KOSZT', 0.00),
        obj_fld('KW_WCELN', 0.00),
        obj_fld('KW_W1', 0.00),
        obj_fld('KW_W2', 0.00),
        obj_fld('KW_W3', 0.00),
        obj_fld('KW_W4', 0.00),
        obj_fld('WALUTA', 3*' '),
        obj_fld('KURS', 0),
        obj_fld('IDENT', 50*' '),
        obj_fld('IDENT2', 50*' '),
        obj_fld('SYM_ROK', 55*' '),
        obj_fld('DATA', date(0,0,0)),
        obj_fld('KLIENT', 10*' '),
        obj_fld('OPIS',30* ' '),
        obj_fld('SEP','-'),
        obj_fld('ZN','D'),
        obj_fld('W_WN',0),
        obj_fld('W_MA',0),
        obj_fld('KW_ZAL', 0.00),
        obj_fld('LOOPPO',''),
        obj_fld('POWIAZ', 20*''),
        obj_fld('DATA_POW', date(0,0,0)),
        obj_virt('set_fld',".clr_fld()",-1,-1),
        obj_virt('data_o',"date(0,0,0)"),
        obj_virt('data_t',"date(0,0,0)"),
        obj_virt('data_r',"date(0,0,0)"),
        obj_meth('clr_fld',"
                .ANALIT1:='';
                .ANALIT2:='';
                .KONTOTXT:='';
                .KONTO1:='';
                .KONTO2:='';
                .IDENT:='';
                .SYM_ROK:='';
                .DATA:=date(0,0,0);
                .KLIENT:='';
                .KW_PLN:=0;
                .KW_KAUC:=0;
                .KW_WAL:=0;
                .WAR_WAL:=0;
                .KW_W1:=0;
                .KW_W2:=0;
                .KW_W3:=0;
                .KW_W4:=0;
                .WALUTA:='';
                .KURS:=0;
                .KW_CLO:=0;
                .KW_AKCYZ:=0;
                .KW_KOSZT:=0;
                .KW_WCELN:=0;
                .W_WN:=.W_MA:=0
        ")
)


\poz_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Formula dodaje rekord do tabeli POZ. Wykorzystywana przez formuly rejestrujace.
::   WE: _a - kwota
::       _b - strona ksiegowa
::       _c - konto
::       _d - identyfikator rozrachunku
::       _e - typ rozrachunku
::       _f - data otwarcia rozrachunku
::       _g - termin platnosci rozrachunku
::       _h - opis
::       [_i] - kod waluty (nieobowiazkowy)
::       [_j] - kwota w walucie (nieobowiazkowy)
::       [_k] - kurs
::       [_l] - tabela do uzupelnienia wyroznikow
::       [_m] - data rozliczenia rozrachunku
::       [_n] - jednostka ksiegowa rozrachunku
::       [_o] - jednostka ksiegowa dekretu
::       [_p] - VAT z podzielnej płatności
::       [_q] - Róznica kursowa: T/N/W
::       [_r] - Czy na wyjściu POZ.ref(), lub null w przypadku niepowodzenia POZ.add():
::              różne od 0 - tak, 0 lub puste - nie (nieobowiązkowy)
::  OLD: \poz_add/automat.fml
::----------------------------------------------------------------------------------------------------------------------
_dodano:=0;
_ref:=null();
KA.CZY_WYR:='N';
{? +_b & (_a$2<>0 | (var_pres('_i')>0 & var_pres('_j')>0 & _j$2<>0 & var_pres('_k')>0))
|| POZ.cntx_psh(); POZ.index('DOK'); POZ.prefix(DOK.ref);
   {? POZ.last() || _nr:=POZ.POZ+1 || _nr:=1 ?};
   POZ.blank();
   POZ.POZ:=_nr; POZ.SUM:=_a$2;
   POZ.STR:={? -_b='wn' || 'Wn' || 'Ma' ?};
   POZ.KON:=_c; POZ.KOM:=exec('koment','konto',_c);
   _roz:=_c='' | POZ.KOM & POZ.KOM().KS().ROZR<>'Z';
   {? var_press('_q')>0 || POZ.RK:=_q ?};
   {? _d<>'' & _roz
   || {? var_press('_n')>0
      || {? type_of(_n)=2
         || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
            _n:={? ODD.find_key(_n,_n) || ODD.ref() || null ?}
         |? type_of(_n)=1
         || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
            _n:={? _n<>0 & ODD.seek(_n,) || ODD.ref || null ?}
         ?};
         POZ.ODD:=_n
      || POZ.ODD:=DOK.ODD
      ?};
      {? var_press('_o')>0
      || {? type_of(_o)=2
         || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
            _o:={? ODD.find_key(_o,_o) || ODD.ref() || null ?}
         |? type_of(_o)=1
         || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
            _o:={? _o<>0 & ODD.seek(_o,) || ODD.ref || null ?}
         ?};
         POZ.ODD_KS:=_o
      || POZ.ODD_KS:=DOK.ODD
      ?};
      POZ.ID:=_d;
      POZ.TID:=~-_e;
      POZ.DO:=_f;
      POZ.SYM_ROK:=exec('op_unik_sym','rozrach',POZ.ID,POZ.DO);
      {? POZ.ID<>'' & var_pres('sym_zew')>0 & sym_zew<>'' || POZ.SYM_ZEW:=sym_zew ?};
      POZ.TP:=_g;
      {? _>12 & type_of(_m)=4 & _m<>date(0,0,0) || POZ.DATA_R:=_m ?}
   || POZ.ODD:=null
   ?};
   {? var_press('_p')>0 & type_of(_p)=type_of(1) & DOK.DOK_REJ().SP_PRZEL='T'
   || POZ.SP_V:={? POZ.WAL=null | POZ.WAL=FINFO.NAROD
                || _p
                || 0
                ?}
   ?};
   POZ.OP:=_h;
   POZ.SUMW:=0;
   POZ.WAL:=POZ.VAT:=null;
   {? var_pres('_i')>0 & var_pres('_j')>0 & +_i & (_j | (var_pres('_k')>0 & _k))
      & (POZ.KON='' | POZ.KOM().KS().WIELO='T')
   || SLO.cntx_psh();
      SLO.index('SL'); SLO.prefix();
      {? SLO.find_key(FINFO.SLWAL().SLU, _i) & SLO.KOD=_i
      || POZ.WAL:=SLO.ref();
         WAL.index('WAL_SYM'); WAL.prefix();
         POZ.SUMW:=_j;
         {? var_pres('_k')>0 & _k$4<>0
         || POZ.KURS:=_k;
            {? _a$2=0 & WAL.find_key(SLO.KOD)
            || POZ.SUM:=(POZ.SUMW*POZ.KURS/WAL.J)$2
            ?}
         || {? WAL.find_key(SLO.KOD)|| POZ.KURS:=((POZ.SUM*WAL.J)/POZ.SUMW)$6 ?}
         ?}
      ?};
      SLO.cntx_pop()
   ?};
   {? ~POZ.add()
   || FUN.emsg('Błąd zapisu tabeli POZ.'@)
   || _dodano+=1;
      _ref:=POZ.ref();
      {? _>=12 &  type_of(_l)=2 & _l<>''
      || exec('po_kon','dok_fks',_l)
      || exec('po_kon','dok_fks')
      ?};
      exec('dod_wyr','wyrozniki')
    ?};
   POZ.cntx_pop()
?};
{? (var_pres('_r')=1 & _r=0) | var_pres('_r')<0
|| _dodano
|| _ref
?}


\VatPKont
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obiekt do wyznaczana kwoty: Vat w koszty wg podziałów dla controllingu
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'DOK','WART',
   'wart'
);
_obj.DOK:=null;
_obj.WART:=1;
_obj.wart:="
   _obj:=.;
   {? _obj.DOK=null | _obj.DOK<>DOK.ref()
   || _obj.DOK:=DOK.ref();
      _vl:=0;
      _vm:=0;
      VPOZ.cntx_psh();
      VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
      {? VPOZ.first()
      || {!
         |? _vl+=VPOZ.VATKOSZT;
            _vm+=VPOZ.NETTO+VPOZ.VATKOSZT;
            VPOZ.next()
         !}
      ?};
      VPOZ.cntx_pop();
      _obj.WART:={? _vm || _vl/_vm || 1 ?}
   ?};
   _obj.WART
";
_obj

:Sign Version 2.0 jowisz:1045 2024/01/10 10:33:31 1a926dd2970f69419dd193bfd20cde4f0be9f47ba72091485ebeeb9a16aab8f44f0907d16ab75c2e80cdb0a1e51834000a2f6a57005df198e9a5343a39806b931b1171db510589d63f5cee6200d7cced667eff8c59357ad88974a39fda39d1d543b9848dccb5cdcadd28a7fb1f0874f858c38b46f805932804a77284731c77c6

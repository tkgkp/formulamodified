:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: pfron.fml [8.70]
:: Utworzony: 2005/04/28
:: Autor: [casper] Grzegorz Lubos
::======================================================================================================================
:: Zawartosc: Funkcje wykorzystywane do tworzenia deklaracji dla PFRON
::            (Panstwowy Fundusz Rehabilitacji Osob Niepelnosprawnych)
::======================================================================================================================


\zatrudnienie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GL [8.70]
:: OPIS: obliczenie sredniego zatrudnienia wg etatow i osob z uwzlednieniem
::       osob niepelnosprawnych
::   WE: _a - data poczatku okresu
::       _b - data konca okresu
::      [_c]- silent ( 0 - pelne; 1 - uproszczone)
::      [_d] - ustalenie zaokraglenia do 3 miejsc [3 lub niepodany] Tylko dla 1-6 pozycji tablicy _tmp do SODIR wynik
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>4 || return(0) ?};
{? var_pres('_b')<>4 || return(0) ?};
{? var_pres('_d')=type_of(1) & _d=3 || _d || _d:=2 ?};
{? _>2 & var_pres('_c')=type_of(0)
|| {? _c=0
   || _rodzaj:=_c
   |? _c=1
   || _rodzaj:=_c
   || return(0)
   ?}
?};
{? _<3
|| _rodzaj:=FUN.choice('Wybierz sposób obliczania średniego zatrudnienia'@,0,'&Pełne'@,,,,'&Uproszczone'@)
?};
_zm2011:={? _a~1>=2011 || 1 || 0 ?};
H.cntx_psh;
N.cntx_psh;
P_INFO.cntx_psh;
P_INFO.index('OD');
N.index('NIEOBECN');
N.prefix();
H.index('_HISTKOD');
H.prefix();
P.cntx_psh;
P.index('PRACOIP');
P.prefix(exec('ref_firma','ustawienia'),__F_ZATR.P);

_ile_dni:=_b-_a+1;
_tmp:=obj_new(13);
{! _i:=1 .. 13 |!
   _tmp[_i]:=obj_new(_ile_dni)
!};
PROGRESS.set(P.size,'Trwa wyliczanie średniego zatrudnienia...'@,FUN.TYT);
VAR_DEL.delete('__U11119');
__U11119:=tab_tmp(2,'O','INTEGER','Osoba'
               ,'NR','INTEGER','Numer dnia'
               ,'P1','REAL','jeden'
               ,'P2','REAL','dwa'
               ,'P3','REAL','trzy'
               ,'P4','REAL','cztery'
               ,'P5','REAL','pięć'
               ,'P6','REAL','sześć'
               ,'P7','REAL','siedem'
               ,'P8','REAL','osiem'
               ,'P9','REAL','dziewięć'
               ,'P10','REAL','dziesięć'
               ,'P11','REAL','jedenaście'
               ,'P12','REAL','dwanaście');
_rodzp:=__RUB.sys_rub(11542,(_a+_i));
{? P.first
|| {!
   |? PROGRESS.next();
      {? ((P.DZ=date(0,0,0) | P.DZ>=_a) & P.DZA<=_b)
      || {? _rodzaj || _ile:=2 || {? P.DZ<>date(0,0,0) & P.DZ<_b || _ile:=P.DZ-_a+1 || _ile:=_ile_dni ?} ?};
         {! _i:=1..13
         |! {! _j:=1.._ile_dni
            |! _tmp[_i][_j]:=0
            !}
         !};

         {! _i:=0.._ile-1
         |! H.prefix(P.ref,'Z');
            P_INFO.prefix(exec('ref_firma','ustawienia'),P.OSOBA);
            {? H.first
            || {? _rodzaj & _i=1 || _i:=_ile_dni-1 ?};
               {? H.find_le(_a+_i) & {? _rodzaj || (H.DO=date(0,0,0) | H.DO>=_a+_i) || 1 ?}
               || _tmp[1][_i+1]+=H.RWY;
                  _tmp[7][_i+1]+=1;
                  _tmp[13][_i+1]:=H.RWY;
                  {? P_INFO.find_le(_a+_i) & P_INFO.STNP().KOD<>'0'
                  || {? S_ZUS.KOD='3' & P_INFO.STNP_OD<=_a+_i & (P_INFO.STNP_DO=date(0,0,0) | P_INFO.STNP_DO>=_a+_i)
                     || {? P_INFO.SZCZNIEP='T'
                        || _tmp[2][_i+1]+=H.RWY;
                           _tmp[8][_i+1]+=1
                        || _tmp[3][_i+1]+=H.RWY;
                           _tmp[9][_i+1]+=1
                        ?}
                     |? S_ZUS.KOD='2' & P_INFO.STNP_OD<=_a+_i & (P_INFO.STNP_DO=date(0,0,0) | P_INFO.STNP_DO>=_a+_i)
                     || {? P_INFO.SZCZNIEP='T'
                        || _tmp[4][_i+1]+=H.RWY;
                           _tmp[10][_i+1]+=1
                        || _tmp[5][_i+1]+=H.RWY;
                           _tmp[11][_i+1]+=1
                        ?}
                     |? S_ZUS.KOD='1' & P_INFO.STNP_OD<=_a+_i & (P_INFO.STNP_DO=date(0,0,0) | P_INFO.STNP_DO>=_a+_i)
                     || _tmp[6][_i+1]+=H.RWY;
                        _tmp[12][_i+1]+=1
                     ?}
                  ?}
               ?};
               {? P_INFO.find_le(_a+_i) & P_INFO.STNP().KOD='0'
               || N.prefix('N',P.ref);
                  {? N.find_le(_a+_i) & N.DO>=_a+_i
                  || _nieob:=N.NB;
                     {? __RUB.sys_attr(_nieob,1222) |  __RUB.sys_attr(_nieob,11541) | __RUB.sys_attr(_nieob,1122)
                        | __RUB.sys_attr(_nieob,114) | __RUB.sys_attr(_nieob,1323)
                     || _tmp[1][_i+1]-=_tmp[13][_i+1];
                        _tmp[7][_i+1]-=1
                     ?}
                  ?};
                  {? _rodzp.first()
                  || N.cntx_psh();
                     N.index('NIPRACNB');
                     N.prefix('N',P.ref(),_rodzp.RN);
                     {!
                     |? {? N.find_le(_a+_i) & N.DO>=_a+_i
                        || H.prefix(P.ref(),'Z');
                           {? H.find_le(_a+_i) & (H.DO=#0 | H.DO>=_a+_i) || _tmp[1][_i+1]-=(H.RWY-H.WY)$2 ?}
                        ?};
                        _rodzp.next()
                     !};
                     N.cntx_pop()
                  ?}
               ?};
               {? _zm2011
               || {? P_INFO.find_le(_a+_i) & P_INFO.STNP().KOD<>'0'
                     & P_INFO.STNP_OD<=_a+_i
                     & (P_INFO.STNP_DO=date(0,0,0) | P_INFO.STNP_DO>=_a+_i)
                  || _daneH:=_daneN:=0;
                     H.prefix(P.ref,'B');
                     {? H.find_le(_a+_i) & H.DO>=_a+_i
                     || _daneH:=1
                     ?};
                     {? ~_daneH
                     || N.prefix('N',P.ref);
                       {? N.find_le(_a+_i) & N.DO>=_a+_i
                       || {? __RUB.sys_attr(N.NB,112,N.OD)
                          || _daneN:=1
                          ?}
                       ?}
                     ?};
                     {? _daneH | _daneN
                     || _tmp[1][_i+1]-={? _daneH || H.RWY || _tmp[13][_i+1] ?};
                        _tmp[7][_i+1]-=1;
                        {? S_ZUS.KOD='3'
                        || {? P_INFO.SZCZNIEP='T'
                           || _tmp[2][_i+1]-={? _daneH || H.RWY || _tmp[13][_i+1] ?};
                              _tmp[8][_i+1]-=1
                           || _tmp[3][_i+1]-={? _daneH || H.RWY || _tmp[13][_i+1] ?};
                              _tmp[9][_i+1]-=1
                           ?}
                        |? S_ZUS.KOD='2'
                        || {? P_INFO.SZCZNIEP='T'
                           || _tmp[4][_i+1]-={? _daneH || H.RWY || _tmp[13][_i+1] ?};
                              _tmp[10][_i+1]-=1
                           || _tmp[5][_i+1]-={? _daneH || H.RWY || _tmp[13][_i+1] ?};
                              _tmp[11][_i+1]-=1
                           ?}
                        |? S_ZUS.KOD='1'
                        || _tmp[6][_i+1]-={? _daneH || H.RWY || _tmp[13][_i+1] ?};
                           _tmp[12][_i+1]-=1
                        ?}
                     ?}
                  ?}
               ?};
               __U11119.blank(1);
               __U11119.O:=#P.OSOBA;
               __U11119.NR:=_i+1;
               {! _poz:=1..12
               |! ($('__U11119.P'+$_poz))():=_tmp[_poz][_i+1]
               !};
               __U11119.add(1)
            ?}
         !}
      ?};
      P.next
   !}
?};
VAR_DEL.delete('__U_suma');
__U_suma:=sql('
   select NR, O, sum(P1) P1, sum(P2) P2, sum(P3) P3, sum(P4) P4, sum(P5) P5, sum(P6) P6,
         max(P7) P7, max(P8) P8, max(P9) P9, max(P10) P10, max(P11) P11, max(P12) P12
   from  :_a
   group by NR, O
   order by 1
   ',__U11119
);
{? _rodzaj
|| VAR_DEL.delete('__sum_upr');
   __sum_upr:=sql('
      select sum(P1) P1, sum(P2) P2, sum(P3) P3, sum(P4) P4, sum(P5) P5, sum(P6) P6, sum(P7) P7, sum(P8) P8,
         sum(P9) P9, sum(P10) P10, sum(P11) P11, sum(P12) P12
      from :_a
      where NR=1 or NR=:_b
      ',__U_suma,_ile_dni
   );
   {! _poz:=1..6
   |! __PFRONZAT[_poz]:=(($('__sum_upr.P'+$_poz))()/2)${? _d=3 || 3 || 2 ?}
   !};
   {! _poz:=7..12
   |! __PFRONZAT[_poz]:=(($('__sum_upr.P'+$_poz))()/2)$2
   !};
   VAR_DEL.delete('__sum_upr')
||  VAR_DEL.delete('__sum_upr');
   __sum_upr:=sql('
      select NR, sum(P1) P1, sum(P2) P2, sum(P3) P3, sum(P4) P4, sum(P5) P5, sum(P6) P6, sum(P7) P7, sum(P8) P8,
         sum(P9) P9, sum(P10) P10, sum(P11) P11, sum(P12) P12
      from :_a
      group by NR
      order by 1
      ',__U_suma
   );
   {? __sum_upr.first()
   || {!
      |? {! _poz:=1..6
         |! __PFRONZAT[_poz]+=($('__sum_upr.P'+$_poz))()
         !};
         {! _poz:=7..12
         |! __PFRONZAT[_poz]+=($('__sum_upr.P'+$_poz))()
         !};
         __sum_upr.next()
      !};
      {! _poz:=1..6
      |! __PFRONZAT[_poz]:=(__PFRONZAT[_poz]/_ile_dni)${? _d=3 || 3 || 2 ?}
      !};
      {! _poz:=7..12
      |! __PFRONZAT[_poz]:=(__PFRONZAT[_poz]/_ile_dni)$2
      !}
   ?};
   VAR_DEL.delete('__sum_upr')
?};
VAR_DEL.delete('__U_suma');
VAR_DEL.delete('__U11119');
PROGRESS.close();
obj_del(_tmp);
P.cntx_pop;
H.cntx_pop;
N.cntx_pop;
P_INFO.cntx_pop


\open_file
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GL [8.70]
:: OPIS: sprawdzenie istnienia pliku deklaracji
::   WE: _a - nazwa pliku
::       _b - nazwa rodzaju sciezki
::   WY: sciezka dostepu do pliku lub zero w przypadku rezygnacji
::----------------------------------------------------------------------------------------------------------------------
_user:={? _psep:=username*'~' || _psep-1
       |? _psep:=username*'.' || _psep-1
       || +username
       ?}+username;
_path:=exec('get','#profile',,_user,_b);

undefine;
define('HD1',~~);
define('HD2',~~,'Nazwa pliku: '+_a);
define('PTH',_path,'Folder lokalny',,60,20);

{? def_edit(,'Podaj dane...')
|| _div:={? -sys_name='windows' || '\\' || '/' ?};
   {? (_path:={? +DEFINE.PTH || DEFINE.PTH+_div || '' ?})<>''
   || _path:=STR.gsub(_path,_div*2,_div)
   ?};
   exec('set','#profile',,_user,_b,_path);
   _a:=|('@'+_path+_a);
   {? _test:=fopen(_a,'r')
   || fclose(_test);
      {? FUN.choice('Plik '+(1-_a)+' już istnieje.'+
                    '\n(dane zawarte w pliku zostaną bezpowrotnie utracone)',1,'Kontynuuj')
      || _a
      ?}
   || _a
   ?}
?}

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:56 a508cc173f800ff96b511b6c72d92a2e7b6691bd0f6a41bb67754b306bef86a7c1b4416f211cea21e09e6d54de6e55812ba04b84352ea017704f3420925dd8227eaa46631236d022fb2a6f1dee7005d50af35fc5dec724659f5edbc8db92d112c30dfb46d358ab65c80e7843354dd2ebac2c4d394e2aa40fe449dbfe119622b2

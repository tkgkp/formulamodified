:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: genius_zws.fml
:: Utworzony: 25.01.2022
:: Autor: bryQ
:: Systemy:
::======================================================================================================================
:: Zawartość: Obsługa intencji ChatBot dla ZWS
::======================================================================================================================

\kh_kontakt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: bryQ [22.26]
:: OPIS: obsługuje informacje zwrotne nt kontaktu dla kontrahenta.
::   WE:
::   WY: Obiekt 'erp'
::----------------------------------------------------------------------------------------------------------------------
exec('ustaw_firma_user','genius_zws');
_erp:=exec('erp_obj','genius');
{? ~exec('chk_role','#b__box',OPERATOR.USER,'ZWS_PAR_KKHP') &
   ~exec('chk_role','#b__box',OPERATOR.USER,'ZWS_PAR_KKHR')
|| _erp.interact.status:=0;
   _erp.display_message:='Niestety nie masz uprawnień do przeglądania kartoteki kontrahentów.'@;
   return(_erp)
?};

_kontakt := AI.getPar('daneKontaktowe');
_kontakt := {? _kontakt = '' || AI.getPar('daneOsobaKontaktowa') || _kontakt ?};
_kh := AI.getPar('nazwaKh');

{? _kontakt = 'telefon' | _kontakt = '' | _kontakt = 'adres' | _kontakt = 'informacje' | _kontakt = 'wszystkie dane' | _kontakt = 'kontakt'  | _kontakt = 'pokaz'  | _kontakt = 'pokaż'
    || _sql:=sql('select KOD, NAZ, UL, POCZ, MIASTO, TEL, KRAJ from kh where kod=\':_a\' or upper(naz) like \'%:_a%\' or upper(skr) like \'%:_a%\'  ',~-_kh);
       {? _sql.first()
          || _erp.display_message := _sql.KOD+' '+_sql.NAZ+'<br />'+ _sql.UL +'<br />'+ _sql.POCZ +' '+_sql.MIASTO+'<br />'+ _sql.KRAJ +'<br />telefon: '+ _sql.TEL
         || _erp.display_message := 'Nie znaleziono kontrahenta: '+_kh
      ?}
|? _kontakt * 'osob' | _kontakt * 'email'
    || _sql:=sql('select kh_osob.IMIE, kh_osob.NAZWISKO, kh_osob.TEL, kh_osob.GSM, kh_osob.EMAIL, kh_osob.STANOW from kh join kh_osob where kod=\':_a\' or upper(naz) like \'%:_a%\' or upper(skr) like \'%:_a%\'  ',~-_kh);
         {? _sql.first()
          ||  _erp.display_message:='';
            {!|?
                _stan := {? _sql.STANOW = '' || 'Osoba' || _sql.STANOW ?};
               _erp.display_message := _erp.display_message + '<li>'+_stan+': '+_sql.IMIE+' '+_sql.NAZWISKO+' '+_sql.TEL+' '+_sql.GSM+' <a href="mailto:'+_sql.EMAIL+'">'+_sql.EMAIL+'</a>';
               _sql.next()
            !}
         || _erp.display_message := 'Nie znaleziono osób kontaktowych dla: '+_kh
      ?}

|| _erp.display_message := 'Nie wiem jeszcze nic na temat: '+_kontakt
?};
_erp


\test1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: bryQ [22.26]
:: OPIS: przykładowe tworzenie listy linków do dokumentów
::   WE:
::   WY: Obiekt 'erp'
::----------------------------------------------------------------------------------------------------------------------
_a := AI.getPar('rodzajFaktury');
_b := AI.getPar('kodKh');
_c := AI.getPar('numerFaktury');
_d := AI.getPar('nazwaKh');

_erp:=exec('erp_obj','genius',2,2,1);
_erp.data[1].type := 'link';
_erp.data[2].type := 'link';

_erp.data[1].values[1].icon := 'bi bi-bicycle icon-size-lg';
_erp.data[1].values[1].display_name := 'FK/0002/00011';
_erp.data[1].values[1].desc := '<li>przykładowy opis1 <li>przykładowy opis2<br />';
_erp.data[1].values[1].link := 'mbase://test123/test123/t?actiton=fv&kh=Asseco';

_erp.data[1].values[2].icon := 'bi bi-cash-stack icon-size-lg';
_erp.data[1].values[2].display_name := 'FK/0002/00011';
_erp.data[1].values[2].desc := 'test 123';
_erp.data[1].values[2].link := 'mbase://test123/test123/t';

_erp.data[2].values[1].display_name := 'FK/0002/00011';
_erp.data[2].values[1].desc := '<li>przykładowy opis1 <li>przykładowy opis2<br />';
_erp.data[2].values[1].link := 'mbase://test123/test123/t?actiton=fv&kh=Asseco';


_erp





\test2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: bryQ [22.26]
:: OPIS: przykładowa interakcja.
::   WE:
::   WY: Obiekt 'erp'
::----------------------------------------------------------------------------------------------------------------------

_a := AI.getPar('phone');
_erp:=exec('erp_obj','genius');

{? AI.INTERACT.status = 0 ||
   _erp.display_message:='Podaj treść smsa';
   _erp.interact.status:=1;
   _erp.interact.par[1].id:='tresc';
   _erp.interact.par[1].for:=_erp.display_message
|? AI.INTERACT.status = 1 ||
   _erp.display_message:='Czy wysłać sms o treści: <b>'+AI.INTERACT.par[1].value+'</b> na numer: <b>'+_a+'</b> ?';
   _erp.interact.status:=2;
   _erp.interact.par[1].id:='potwiedz';
   _erp.interact.par[1].for:=_erp.display_message
|? AI.INTERACT.status = 2  ||
    {? ~-AI.INTERACT.par[2].value = 'TAK'
      || _erp.display_message:='Ok, wysłano smsa'
          || _erp.display_message:='Zrezygnowano z wysłania sms'
   ?};
   _erp.interact.status:=0
?};
_erp


\test3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: bryQ [22.26]
:: OPIS: przykładowa interakcja - choice.
::   WE:
::   WY: Obiekt 'erp'
::----------------------------------------------------------------------------------------------------------------------

_a := AI.getPar('phone');
_erp:=exec('erp_obj','genius',1,3);
_erp.display_message:='Dokonaj wyboru';
_erp.data[1].type := 'choice';
_erp.data[1].values[1].icon := 'bi bi-bookmark-heart icon-size-lg';
_erp.data[1].values[1].display_name := 'Wybierz serduszko';
_erp.data[1].values[1].desc := 'Wybierz mnie';
_erp.data[1].values[1].link := 'exec(\'test_choice1\',\'genius_zws\',\'serce\')';

_erp.data[1].values[2].icon := 'bi bi-bookmark-star icon-size-lg';
_erp.data[1].values[2].display_name := 'Wybierz gwiazdkę';
_erp.data[1].values[2].desc := 'Albo mnie';
_erp.data[1].values[2].link := 'exec(\'test_choice1\',\'genius_zws\',\'gwiazdka\')';

_erp.data[1].values[3].icon := 'bi bi-bookmark-plus icon-size-lg';
_erp.data[1].values[3].display_name := 'Wybierz krzyżyk';
_erp.data[1].values[3].desc := 'A może mnie';
_erp.data[1].values[3].link := 'exec(\'test_choice1\',\'genius_zws\',\'krzyżyk\')';

_erp

\test_choice1
_erp:=exec('erp_obj','genius');
_erp.display_message:='Dokonałeś wyboru, wybrano: '+_a;
_erp

\test4
_a:=AI.getParCount('endDate');
_b:=AI.getPar('endDate',1);
_c:=AI.getPar('endDate',2);
_d:=AI.getPar('endDate',3);

AI.setErpPar('xxxxx','testowa wartość parametru');

_e:= AI.getErpPar('xxxxx');

_erp:=exec('erp_obj','genius');
_erp.display_message:='ilość parametrów endDate: '+ $_a + '<li> 1:'+_b+'<li> 2:'+_c+'<li> 3:'+_d+'<li> nowy parametr: '+_e;
_erp


\send_sms
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Wysyłanie wiadomości SMS
::----------------------------------------------------------------------------------------------------------------------
_erp:=exec('erp_obj','genius');

{? type_of(AI.INTERACT.par[10].value)<>type_of(~~)
|| _erp.display_message:='Nie udało się wysłać SMSa.'@;_erp.interact.status:=0; return(_erp)
?};

{? exec('get','#params',700020)=''
|| _erp.display_message:='Brak wypełnionego parametru 700020.'@;_erp.interact.status:=0; return(_erp)
?};

{? exec('get','#params',700021)=''
|| _erp.display_message:='Brak wypełnionego parametru 700021.'@;_erp.interact.status:=0; return(_erp)
?};

exec('ustaw_firma_user','genius_zws');
{? ~exec('chk_role','!zws_sms_send')
|| _erp.display_message:='Brak uprawnień do wysyłania wiadomości SMS.'@;_erp.interact.status:=0; return(_erp)
?};

{? var_press('_a')=type_of('') || OK_GSM:=_a ?};
_phone:={? var_press('_a')=type_of('') || _a ||  AI.getPar('phone') ?};
{? (_last_par_val:=exec('last_par_val','genius',AI.INTERACT.par,'phone'))<>'' || _phone:=_last_par_val ?};
_phone:=gsub(_phone,'-','');
_osoba:= AI.getPar('osoba');
_tresc:= AI.getPar('tresc');
{? (_last_par_val:=exec('last_par_val','genius',AI.INTERACT.par,'tresc'))<>'' || _tresc:=_last_par_val ?};

{? AI.INTERACT.status = 0 | AI.INTERACT.status = 1
||
::podana jakaś osoba, próbujemy wyszukać poprzez SQL
   VAR_DEL.delete('OK_GSM');
   OK_GSM:='';
   {? _osoba<>'' & _phone=''
   ||
      _imie_naz:=spli_str(_osoba,' ');
      _sql:='select IMIE, NAZWISKO, SKR, GSM from KH_OSOB join KH using(KH_OSOB.KH, KH.REFERENCE)
            where GSM<>\'\'
            and ((UPPER(IMIE) like UPPER(\'%1\%\') and UPPER(NAZWISKO) like UPPER(\'%2\%\'))
            or (UPPER(IMIE) like UPPER(\'%2\%\') and UPPER(NAZWISKO) like UPPER(\'%1\%\')))'
            [{? obj_len(_imie_naz)>0 || _imie_naz[1] || '' ?},
             {? obj_len(_imie_naz)>1 || _imie_naz[2] || '' ?}];
      _tab_full:=sql(_sql);
      {? _tab_full.size()=1 & _tab_full.first()
      || OK_GSM:=_tab_full.GSM; _phone:=OK_GSM:=gsub(OK_GSM,'-','')
      |? _tab_full.size()>1
      || return(exec('kh_osob_choice','genius_zws',_tab_full))
      ||
         _sql:='select IMIE, NAZWISKO, SKR, GSM from KH_OSOB join KH using(KH_OSOB.KH, KH.REFERENCE)
               where GSM<>\'\'
               and ((UPPER(IMIE) like UPPER(\'%1\%\') and UPPER(NAZWISKO) like UPPER(\'%2\%\'))
               or (UPPER(IMIE) like UPPER(\'%2\%\') and UPPER(NAZWISKO) like UPPER(\'%1\%\')))'
               [{? obj_len(_imie_naz)>0 || 3+_imie_naz[1] || '' ?},
                {? obj_len(_imie_naz)>1 || 3+_imie_naz[2] || '' ?}];
         _tab:=sql(_sql);
         {? _tab.size()=1 & _tab.first()
         || OK_GSM:=_tab.GSM; _phone:=OK_GSM:=gsub(OK_GSM,'-','')
         |? _tab.size()>1
         || return(exec('kh_osob_choice','genius_zws',_tab))
         ?}
      ?}
   ?};
::brak podanej osoby oraz numeru, doptujemy tylko o numer
   {? _osoba<>'' & _phone=''
   ||
      _erp.display_message:='Nie udało się jednoznacznie określić osoby kontaktowej lub brakuje jej numeru.\nPodaj numer telefonu';
      _erp.interact.status:=1;
      _erp.interact.par[1].id:='phone';
      _erp.interact.par[1].for:=_erp.display_message
   |? _phone=''
   ||
      _erp.display_message:='Podaj numer telefonu.'@;
      _erp.interact.status:=1;
      _erp.interact.par[1].id:='phone';
      _erp.interact.par[1].for:=_erp.display_message
   |? (#_phone=0 | +_phone<9 )
   ||
      {? ~-exec('last_par_val','genius',AI.INTERACT.par,'potwierdz') = 'TAK'
      ||
         _erp.display_message:='Zrezygnowano z wysłania SMSa.'@;
         _erp.interact.status:=0
      |? ~-exec('last_par_val','genius',AI.INTERACT.par,'potwierdz') <> ''
         & exec('last_par_val','genius',AI.INTERACT.par,'potwierdz') = exec('last_par_val','genius',AI.INTERACT.par)
      ||
         _erp.display_message:='Podaj numer telefonu.'@;
         _erp.interact.status:=1;
         _erp.interact.par[1].id:='phone';
         _erp.interact.par[1].for:=_erp.display_message
      ||
         _erp.display_message:='Niepoprawny numer telefonu. Czy chcesz przerwać? (Tak/Nie)';
         _erp.interact.status:=1;
         _erp.interact.par[1].id:='potwierdz';
         _erp.interact.par[1].for:=_erp.display_message
      ?}
   || AI.INTERACT.status:=2
   ?}
?};

{? AI.INTERACT.status = 2
||
   {? _tresc=''
   ||
      _erp.display_message:='Podaj treść wiadomości.'@;
      _erp.interact.status:=3;
      _erp.interact.par[1].id:='tresc';
      _erp.interact.par[1].for:=_erp.display_message
   || AI.INTERACT.status:=3
   ?}
?};
{? AI.INTERACT.status = 3
||
   {? var_press('OK_GSM')=type_of('') & OK_GSM<>'' || _phone:=OK_GSM ?};
   _erp.display_message:='Czy wysłać SMS o treści: <b>'+_tresc+'</b> na numer: <b>'+_phone+'</b> ? (Tak/Nie)';
   _erp.interact.status:=4;
   _erp.interact.par[1].id:='potwierdz';
   _erp.interact.par[1].for:=_erp.display_message
|? AI.INTERACT.status = 4
||
   {? ~-exec('last_par_val','genius',AI.INTERACT.par,'potwierdz') = 'TAK'
   ||
         {? var_press('OK_GSM')=type_of('') & OK_GSM<>'' || _phone:=OK_GSM ?};
         _to:=_phone;
         _message:=_tresc;
         _from:=exec('get','#params',700021);
         _flash:='0';
         _fast:='0';
         _date:=date();
         _time:=time();
      _wyn:=exec('send_sms','#sms_send',
         _to,
         _message,
         _from,
         _flash,
         _fast,
         _date,
         _time
         );
      {? type_of(json_parse(_wyn))=117
      & var_pres('list', json_parse(_wyn))>0
      & obj_len(json_parse(_wyn).list)>0
      & var_pres('error', json_parse(_wyn).list[1])=0
      || _erp.display_message:='Wiadomość została wysłana.'@
      || _erp.display_message:='Wystąpiły błędy przy wysyłce wiadomości.\nWięcej informacji w logach.'@
      ?}
   || _erp.display_message:='Zrezygnowano z wysłania SMSa.'@
   ?};
   _erp.interact.status:=0
?};
_erp


\kh_osob_choice
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Wybór osoby kontaktowej
::   WE: _a - tymczasowa tabela
::----------------------------------------------------------------------------------------------------------------------
_kh_osob_tab:=_a;
_kh_osob_il:=_kh_osob_tab.size();
{? _kh_osob_tab.first()
|| _erp:=exec('erp_obj','genius',1,_kh_osob_il);
   _erp.display_message:='Proszę wskazać właściwą osobę kontaktową:'@;
   _erp.data[1].type:='choice';
   {! _ii:=1.._kh_osob_il
   |! _erp.data[1].values[_ii].icon:='bi bi-bookmark-check icon-size-lg';
      _erp.data[1].values[_ii].display_name:=_kh_osob_tab.IMIE+' '+_kh_osob_tab.NAZWISKO;
      _erp.data[1].values[_ii].desc:='Numer telefonu: '@+_kh_osob_tab.GSM;
      _erp.data[1].values[_ii].link:='exec(\'send_sms\',\'genius_zws\',\''+_kh_osob_tab.GSM+'\')';
      _kh_osob_tab.next()
   !}
|| _erp:=exec('erp_obj','genius')
?};
_erp.display_message:='Nie udało się jednoznacznie określić osoby kontaktowej lub brakuje jej numeru.\nWybierz z listy:'@;
_erp.interact.status:=2;
_erp


\start_process
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Start procesu
::   WE: _a - UID_EXE
::       _b - Nazwa procesu
::----------------------------------------------------------------------------------------------------------------------
_erp:=exec('erp_obj','genius');
{? var_press('__PARSES')<100 || exec('start_proces','b_proces',0) ?};

_proces:= {? var_press('_b')=type_of('') || _b || AI.getPar('name') ?};
_uid_exe:={? var_press('_a')=type_of('') || _a || '' ?};

{? AI.INTERACT.status=2 & _uid_exe=''
|| _erp.display_message:='Zakończono wyszukiwanie procesu.'@;
   _erp.interact.status:=0;
   return(_erp)
?};

_obj_event:=obj_new('TAB','WER');
exec('usractTab','#b_desktop',_obj_event,'',2);
_event:=_obj_event.TAB;

_sql:='select UID_EXE, NAME, "UID" from :_a EVENT
      where UPPER(NAME) like UPPER(\'%1\%\')
      or UPPER("UID") like UPPER(\'%1\%\')
      and UID_EXE is not null'
      [_proces];
_tab_full:=sql(_sql, _event);

_potwierdz:=-exec('last_par_val','genius',AI.INTERACT.par,'potwierdz');

{? (_tab_full.size()=1 & _tab_full.first()) & (_proces<>_tab_full.NAME & _proces<>_tab_full.UID)
& _uid_exe='' &_potwierdz=''
|| _erp.display_message:='Czy uruchomić proces \'%1\'? (Tak/Nie)'[_tab_full.NAME];
   _erp.interact.status:=1;
   _erp.interact.par[1].id:='potwierdz';
   _erp.interact.par[1].for:=_erp.display_message;
   return(_erp)
|? _potwierdz<>'' & _potwierdz<>'tak'
|| _erp.display_message:='Zrezygnowano z uruchomienia procesu.'@;
   _erp.interact.status:=0;
   return(_erp)
|? (_tab_full.size()=1 & _tab_full.first()) | _uid_exe<>''
|| {? _uid_exe='' || _uid_exe:=_tab_full.UID_EXE ?};
   {? exec('is_okres','pulpit',__PARSES.getVal('OkresRok').OKRO,_uid_exe,,1)
   || exec('proc_exec_mod','#b_desktop',_uid_exe)
   ?}
|? _tab_full.size()>1
|| return(exec('b_proc_choice','genius_zws',_tab_full))
|? _tab_full.size()=0
||
   _proces_name:=spli_str(_proces,' ');
   _size:=obj_len(_proces_name);
   _ii:=1;
   _proces:='%';
   {? _size>0
   ||
      {!|?
         _proces+=(5+_proces_name[_ii])+'%';
         _ii+=1;
         _ii<=_size
      !}
   ?};
   _sql:='select UID_EXE, NAME, "UID" from :_a EVENT
         where UPPER(NAME) like UPPER(\'%1\%\')
         or UPPER("UID") like UPPER(\'%1\%\')
         and UID_EXE is not null'
         [_proces];
   _tab:=sql(_sql, _event);
   {? (_tab.size()=1 & _tab.first()) & (_proces<>_tab.NAME & _proces<>_tab.UID) & _potwierdz=''
   || _erp.display_message:='Czy uruchomić proces \'%1\'? (Tak/Nie)'[_tab.NAME];
      _erp.interact.status:=1;
      _erp.interact.par[1].id:='potwierdz';
      _erp.interact.par[1].for:=_erp.display_message;
      return(_erp)
   |? (_tab.size()=1 & _tab.first())
   || _uid_exe:=_tab.UID_EXE;
      {? exec('is_okres','pulpit',__PARSES.getVal('OkresRok').OKRO,_uid_exe,,1)
      || exec('proc_exec_mod','#b_desktop',_uid_exe)
      ?}
   |? _tab.size()>1
   || return(exec('b_proc_choice','genius_zws',_tab))
   |? _tab.size()=0
   || _erp.display_message:='Nie znaleziono procesu.'@;
      _erp.interact.status:=0;
      return(_erp)
   ?};
   {? _tab.size()=1 & _tab.first() || _proces:=_tab.NAME ?}
?};
title('Genius',,1);
{? _tab_full.size()=1 & _tab_full.first() || _proces:=_tab_full.NAME ?};
_erp.display_message:='Proces \'%1\' został uruchomiony.'@[_proces];
_erp.interact.status:=0;
_erp


\b_proc_choice
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Wybór procesu
::   WE: _a - tymczasowa tabela
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_il:=_tab.size();
{? _tab.first()
|| _erp:=exec('erp_obj','genius',1,_il);
   _erp.display_message:='Proszę wskazać właściwy proces:'@;
   _erp.data[1].type:='choice';
   {! _ii:=1.._il
   |! _erp.data[1].values[_ii].icon:='bi bi-bookmark-check icon-size-lg';
      _erp.data[1].values[_ii].display_name:=_tab.NAME;
      _erp.data[1].values[_ii].desc:='Symbol: '@+_tab.UID;
      _erp.data[1].values[_ii].link:='exec(\'start_process\',\'genius_zws\',\''+_tab.UID_EXE+'\',\''+_tab.NAME+'\')';
      _tab.next()
   !}
|| _erp:=exec('erp_obj','genius')
?};
_erp.display_message:='Nie udało się jednoznacznie określić procesu.\nWybierz z listy:'@;
_erp.interact.status:=2;
_erp


\ustaw_firma_user
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: ustawienie REF.FIRMA i OPERATOR.USER
::----------------------------------------------------------------------------------------------------------------------
{? REF.FIRMA=null()
|| REF.FIRMA:=exec('firma_ref','#firma',app_info('app_ident'))
?};
{? OPERATOR.USER=null()
|| OPERATOR.USER:=exec('users','#users',exec('operatorKod','#users'))
?};
~~


\start_report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Start raportu
::----------------------------------------------------------------------------------------------------------------------
_erp:=exec('erp_obj','genius');
{? var_press('__PARSES')<100 || exec('start_proces','b_proces',0) ?};

_report:=AI.getPar('nazwa');
_uid_exe:={? var_pres('_a')=type_of('') || _a || '' ?};
    _uid:={? var_pres('_b')=type_of('') || _b || '' ?};
  _b_can:={? var_pres('_c')=type_of('') || _c || '' ?};

_co_dalej:=AI.getErpPar('co_dalej');
{? _co_dalej=~~ || _co_dalej:='' ?};

{? exec('co_dalej_akcja','genius_zws',_co_dalej)='koniec'
|| AI.setErpPar('co_dalej',~~);
   _erp_end:=exec('erp_obj','genius_pkd',1);
   _erp_end.display_message:='Przerwano uruchamianie raportu.'@;
   _erp_end.data[1].values[1].icon:='';
   _erp_end.interact.status:=0;
   _erp_end;
   return(_erp_end)
?};

_obj_report:=obj_new('TAB','WER');
exec('tab_report','#b_report',_obj_report);
_TAB_REP:=_obj_report.TAB;

_sql:='select UID_EXE, NAME, B_CAN, "UID" from :_a EVENT
      where UPPER(NAME) like UPPER(\'%1\%\')
      or UPPER("UID") like UPPER(\'%1\%\')
      and UID_EXE is not null'
      [_report];
_tab_full:=sql(_sql, _TAB_REP);
{? (_tab_full.size()=1 & _tab_full.first()) | _uid_exe<>''
|| {? _uid_exe='' || _uid_exe:=_tab_full.UID_EXE ?};
   {? _uid='' || _uid:=_tab_full.UID ?};
   {? _b_can='' || _b_can:=_tab_full.B_CAN ?};
   {? exec('is_okres','pulpit',__PARSES.getVal('OkresRok').OKRO,_uid_exe,,1)
   || {? _b_can='Q'
      || exec('invoke','analizy_bi',_uid)
      || exec('proc_exec_mod','#b_desktop',_uid_exe)
      ?}
   ?}
|? _tab_full.size()>1
|| return(exec('report_choice','genius_zws',_tab_full))
|? _tab_full.size()=0
|| _report_name:=spli_str(_report,' ');
   _size:=obj_len(_report_name);
   _ii:=1;
   _report:='';
   {? _size>0
   || {!
      |? _report+=(5+_report_name[_ii])+'%';
         _ii+=1;
         _ii<=_size
      !}
   ?};
   _sql:='select UID_EXE, NAME, B_CAN, "UID" from :_a EVENT
      where UPPER(NAME) like UPPER(\'%1\%\')
      or UPPER("UID") like UPPER(\'%1\%\')
      and UID_EXE is not null'
         [_report];
   _tab:=sql(_sql, _TAB_REP);
   {? (_tab.size()=1 & _tab.first())
   || _uid_exe:=_tab.UID_EXE;
      {? exec('is_okres','pulpit',__PARSES.getVal('OkresRok').OKRO,_uid_exe,,1)
      || {? _b_can='Q'
         || exec('invoke','analizy_bi',_uid)
         || exec('proc_exec_mod','#b_desktop',_uid_exe)
         ?}
      ?}
   |? _tab.size()>1
   || return(exec('report_choice','genius_zws',_tab))
   |? _tab.size()=0
   || _erp.display_message:='Nie znaleziono raportu.'@;
      _erp.interact.status:=0;
      return(_erp)
   |? _tab.size()=0
   || _erp.display_message:='Nie znaleziono raportu.'@;
      _erp.interact.status:=0;
      return(_erp)
   ?}
?};
title('Genius',,1);
{? +_uid_exe
|| _erp.display_message:='Zakończono raport.'@;
   _erp.interact.status:=0;
   _erp
|| AI.setErpPar('co_dalej',~~);
   _erp.interact.status:=1;
   exec('erp_obj_add_question','genius_zws',_erp);
   _erp
?}


\report_choice
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Wybór raportu
::   WE: _a - tymczasowa tabela
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_il:=_tab.size();
{? _tab.first()
|| _erp:=exec('erp_obj','genius',1,_il);
   _erp.display_message:='Proszę wskazać właściwy raport:'@;
   _erp.data[1].type:='choice';
   {! _ii:=1.._il
   |! _erp.data[1].values[_ii].icon:='bi bi-bookmark-check icon-size-lg';
      _erp.data[1].values[_ii].display_name:=_tab.NAME;
      _erp.data[1].values[_ii].desc:='Symbol: '@+_tab.UID;
      _erp.data[1].values[_ii].link:=''+"exec('start_report','genius_zws'"+
                                     ',\'%1\',\'%2\',\'%3\''[_tab.UID_EXE,_tab.UID,_tab.B_CAN]+")";
      _tab.next()
   !}
|| _erp:=exec('erp_obj','genius')
?};
_erp.display_message:='%1\n%2'@['Nie udało się jednoznacznie określić raportu.','Wybierz z listy:'];
_erp.interact.status:=2;
AI.setErpPar('co_dalej',~~);
exec('erp_obj_add_question','genius_zws',_erp,1);
_erp


\erp_obj_add_question
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Dodaje rekord do elementu "data" obiektu "erp" z pytaniem co dalej
::   WE: _a   [DICT]    - obiekt _erp
::       [_b] [INTEGER] - czy dodanie pytania do listy znalezionych raportów?
::   WY:
::----------------------------------------------------------------------------------------------------------------------
 _erp:={? var_pres('_a')=117        || _a || return() ?};
_list:={? var_pres('_b')=type_of(0) || _b || 0        ?};


_txt:={? _list
      || 'Czy chcesz kontynuować uruchamianie raportu?'
      || 'Czy chcesz uruchomić jeszcze jakieś inne raporty?'@
      ?};

_size_d:=obj_len(_erp.data)+1;
_data:=obj_new(_size_d);
{! _ind:=1..(_size_d-1)
|! _data[_ind]:=_erp.data[_ind]
!};
_data[_size_d]:=obj_new('type','values');
_data[_size_d].values:=exec('erp_values','genius',1);
_data[_size_d].type:='label';
_data[_size_d].values[1].icon:='';
_data[_size_d].values[1].display_name:='</br>%1'[_txt];
_data[_size_d].values[1].desc:='';
obj_del(_erp.data);
_erp.data:=_data;
_erp


\co_dalej_akcja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Zwraca informację o akcji jaka ma być podjęta na podstawie tego co wpisał użytkownik
::   WE: _a [STRING] - tekst wpisany przez użytkownika
::   WY: '' lub nazwa akcji
::----------------------------------------------------------------------------------------------------------------------
_akcja:='';
_co_dalej:={? var_pres('_a')=type_of('') || _a || return(_akcja) ?};

_co_dalej:=~-_co_dalej;
{? _co_dalej='NIE' | _co_dalej*'KONIEC' | _co_dalej*'ZAKOŃCZ' | _co_dalej*'POMIŃ' | _co_dalej*'ANULUJ'
|| _akcja:='koniec'
?};

_akcja

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 9828800fec332aee6ee632bcb8a0ba914e2e2b21bbb21037ef05386a839cbd268aac0f083149d321f098f451360cb7b5d829271941d5395134f017d14d7cfb7f46c279780b4b4e72cc5a15eb129c10685e9db1b6524acc768dca1189a423d2d0a8598a68d58360d4e73ff18c91e21f89df7c5cac027600aa7c07208e03bf55d7

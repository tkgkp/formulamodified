:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: fks_viudo.fml
:: Utworzony: 22.09.2021 [21.14]
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi deklaracji VIU-DO
::======================================================================================================================


\set_viudo_fml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Ustawia formuły do obsługi VIU-DO
::----------------------------------------------------------------------------------------------------------------------
VAT_DEK.fld_fml('D1','BEFORE_EDIT',"exec('before_edit','fks_viudo')");
VAT_DEK.fld_fml('BANK','BEFORE_EDIT',"exec('before_edit','fks_viudo')");
VAT_DEK.fld_fml('BANK','AFTER_EDIT',"exec('after_edit','fks_viudo')");
VAT_DEK.fld_fml('TKRS','BEFORE_EDIT',"exec('before_edit','fks_viudo')");
HELPJPK.fld_fml('JPK','BEFORE_DISPLAY',"exec('before_display','fks_viudo')");
~~


\before_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Przed redakcją pól związanych z deklaracją VIU-DO
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
{? _fld='D1'
|| (var_pres('Add')<=0 | Add=0) | ~exec('is_vat_poz','fks_viudo')
|? _fld='BANK'
|| {? (var_pres('Add')<=0 | Add=0) | ~exec('is_vat_poz','fks_viudo')
   || XINFO.SLBANK();
      1
   ?}
|? _fld='TKRS'
|| {? VAT_DEK.BANK<>null & ((var_pres('Add')<=0 | Add=0) | ~exec('is_vat_poz','fks_viudo'))
   || TKRS.win_dict('WERGEB');
      SLO.cntx_psh();
      POMOC.BANKEB:=exec('FindInSet','#table','B','BANKOD',VAT_DEK.BANK().KOD);
      SLO.cntx_pop();
      1
   ?}
?}


\after_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Po redakcji pól związanych z deklaracją VIU-DO
::   WE: [_a] - pole
::----------------------------------------------------------------------------------------------------------------------
_fld:={? var_pres('_a')>0 & type_of(_a)=type_of('') || _a || cur_afld() ?};
{? _fld='BANK'
|| _disp:=0;
   POMOC.BANK:=VAT_DEK.BANK;
   {? VAT_DEK.TKRS().BANK<>VAT_DEK.BANK
   || VAT_DEK.TKRS:=null;
      _disp:=1
   ?};
   {? VAT_DEK.TKRS=null
   || exec('tkrs_eb','fks_viudo',1);
      TKRS.index('TKRS_DT');
      TKRS.prefix(VAT_DEK.BANK,POMOC.WAL_TKRS);
      {? TKRS.find_key(VAT_DEK.D1)
      || VAT_DEK.TKRS:=TKRS.ref();
         _disp:=1
      ?};
      exec('tkrs_eb','fks_viudo',2)
   ?};
   {? _disp & var_pres('_a')<=0
   || win_disp()
   ?};
   1
?}


\before_display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Przed wyświetleiem pól związanych z deklaracją VIU-DO
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
{? _fld='JPK'
|| HELPJPK.JPK:={? exec('is_edek','xml','VIUDO') || 'T' || 'N' ?}
?}


\akc_details
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Akcja szczegóły dla deklaracji VIU-DO
::----------------------------------------------------------------------------------------------------------------------
 VAT.NETTO:=VAT.SVAT:=VAT.BRUTTO:=0;
VAT_PS.cntx_psh();
VAT_PS.use('vat_ps'+VAT_DEK.ROK().KOD);
VAT_PS.index('VAT_PS');
VAT_PS.prefix(VAT_DEK.ref(),VAT_POZ.ref());
exec('vat_ps_upd','fks_ved');
VAT_PS.win_sel('VIUDO');
VAT_PS.hdr_sel(' związane z pozycją deklaracji: %1 - %2'@[VAT_POZ.LP,VAT_POZ.OP]);
VAT_PS.select();
VAT_PS.cntx_pop();
~~


\is_vat_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Czy dostepne są pozycje deklaracji
::   WE: [_a] - prefiks kodu pozycje deklaracji
::       [_b] - niezerowy VAT
::----------------------------------------------------------------------------------------------------------------------
_kod:={? var_pres('_a')>0 || _a || '' ?};
_no0vat:={? var_pres('_b')>0 || _b || 0 ?};
VAT_POZ.cntx_psh();
VAT_POZ.index('VAT_POZ');
VAT_POZ.prefix(VAT_DEK.ref(),_kod);
_jest:=VAT_POZ.first();
{? _jest & _no0vat
|| {!
   |? _jest:=VAT_POZ.VAT<>0;
      _jest=0 & VAT_POZ.next()
   !}
?};
VAT_POZ.cntx_pop();
_jest


\tkrs_eb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Ustawia maski dla tabel kursów dla Europejskiego Banku Centralnego
::   WE:  _a  - 1- ustawienie, 2-przywrócenie
::       [_b] - ustawiana maska - domyślnie 'eb'
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| _maska:={? var_pres('_b')>0 & type_of(_b)=type_of('') & +_b=2 || _b || 'eb' ?};
   TKRS.cntx_psh();
   TKRS.use('tabkrs'+_maska);
   KRS.cntx_psh();
   KRS.use('kursy_'+_maska)
|? _a=2
|| KRS.cntx_pop();
   TKRS.cntx_pop()
?};
~~


\edek_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Zwraca wartość pozycji e-Deklaracji VIU-DO
:    WE: [_a] - dodatkowy parametr
::----------------------------------------------------------------------------------------------------------------------
_tag:=ISTDEFS.OPIS;
{? (_pd:=_tag*':')
|| _tag:=_pd-_tag
?};
_par:={? var_pres('_a')>0 || _a ?};
{? _tag='VATReturnMSCON'
|| VAT_POZ.cntx_psh();
   exec('is_vat_poz','fks_viudo','C.2.',1) | exec('is_vat_poz','fks_viudo','C.5.')
|? _tag='Supplies'
|| VAR_DEL.delete('val');
   val:=obj_new(
      'K','T','S','P','LP','TG','TS',
      'set'
   );
   val.LP:=0;
   val.TG:=val.TS:=0;
   val.set:="
      _str:=spli_str(VAT_POZ.OP,',');
      .K:=_str[1];
      .T:={? _str[2]*'towarów' || 'GOODS' || 'SERVICES' ?};
      .S:={? _str[3]*'Podstawowa' || 'STANDARD' || 'REDUCED' ?};
      .P:=|(1-_str[4]-2);
      {? .T='GOODS'
      || .TG+=VAT_POZ.VAT
      || .TS+=VAT_POZ.VAT
      ?}
   ";
   exec('is_vat_poz','fks_viudo','C.2.',1)
|? _tag='MSIDSupplies'
|| {? _par=1
   || VAT_POZ.cntx_psh();
      VAR_DEL.delete('VatNext');
      VatNext:="
         _dalej:=0;
         val.LP+=1;
         VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),'C.2.'+$val.LP+'.');
         {? VAT_POZ.first()
         || {!
            |? {? VAT_POZ.VAT<>0
               || val.set();
                  _dalej:=1;
                  0
               || val.LP+=1;
                  VAT_POZ.prefix(VAT_DEK.ref(),'C.2.'+$val.LP+'.');
                  VAT_POZ.first()
               ?}
            !}
         ?};
         _dalej
      ";
      VatNext()
   |? _par=0
   || VAT_POZ.cntx_pop();
      VAR_DEL.delete('VatNext')
   || VatNext()
   ?}
|? _tag='OSSVATReturnDetail'
|| {? _par=1
   || 1
   |? _par=2
   || {? VAT_POZ.next()
      || val.set(); 1
      ?}
   ?}
|? _tag='MSCONCountryCode'
|| {? var_pres('val')>0 || val.K || VAT_POZ.OP ?}
|? _tag='SupplyType'
|| val.T
|? _tag='VATRate'
|| val.P
|? _tag='TaxableAmount'
|| exec('edek_val','fks_viudo',VAT_POZ.NETTO)
|? _tag='VATAmount'
|| exec('edek_val','fks_viudo',VAT_POZ.VAT)
|? _tag='GrandTotalMSIDServices'
|| {? val.TS || exec('edek_val','fks_viudo',val.TS) || '' ?}
|? _tag='GrandTotalMSIDGoods'
|| {? val.TG || exec('edek_val','fks_viudo',val.TG) || '' ?}
|? _tag='GrandTotal'
|| exec('edek_val','fks_viudo',(val.TS+val.TG))
|? _tag='Corrections'
|| {? _par=1
   || VAR_DEL.delete('val');
      val:=obj_new(
         'K','R','Q','LP',
         'set'
      );
      val.LP:=1;
      val.set:="
         _str:=spli_str(VAT_POZ.OP,',');
         .K:=_str[1];
         _okres:=|_str[2];
         .R:=4+_okres;
         .Q:=_okres+1
      ";
      VAT_POZ.cntx_psh();
      VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),'C.5.1.');
      {? VAT_POZ.first()
      || val.set();
         1
      ?}
   |? _par=0
   || VAT_POZ.cntx_pop();
      VAR_DEL.delete('val')
   |? _par=2
   || val.LP+=1;
      VAT_POZ.prefix(VAT_DEK.ref(),'C.5.'+$val.LP+'.');
      {? VAT_POZ.first()
      || val.set();
         1
      ?}
   ?}
|? _tag='Correction'
|| {? _par=1
   || 1
   |? _par=2
   || {? VAT_POZ.next()
      || vat.set(); 1
      ?}
   ?}
|? _tag='Year'
|| val.R
|? _tag='Quarter'
|| val.Q
|? _tag='TotalVATAmountCorrection'
||  exec('edek_val','fks_viudo',VAT_POZ.VAT)
|? _tag='MSCONBalance'
|| {? _par=1
   || VAT_POZ.cntx_psh();
      VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),'C.6.');
      VAT_POZ.first()
   |? _par=0
   || VAT_POZ.cntx_pop()
   |? _par=2
   || VAT_POZ.next()
   ?}
|? _tag='BalanceOfVATDue'
|| exec('edek_val','fks_viudo',VAT_POZ.VAT)
|? _tag='TotalAmountOfVATDue'
|| _vat:=0;
   VAT_POZ.cntx_pop();
   VAT_POZ.cntx_psh();
   VAT_POZ.index('VAT_POZ'); VAT_POZ.prefix(VAT_DEK.ref(),'C.7.',);
   {? VAT_POZ.first()
   || _vat:=VAT_POZ.VAT
   ?};
   VAT_POZ.cntx_pop();
   {? _vat<0 || _vat:=0 ?};
   exec('edek_val','fks_viudo',_vat)
|? _tag='Pouczenie1'
|| 'Za podanie nieprawdy lub zatajenie prawdy i przez to narażenie podatku na uszczuplenie grozi odpowiedzialność'+
   ' przewidziana w Kodeksie karnym skarbowym.'
|? _tag='Pouczenie2'
|| 'W przypadku niewpłacenia w obowiązującym terminie kwoty podatku VAT należnej Rzeczpospolitej Polskiej lub '+
   'wpłacenia jej w niepełnej wysokości, niniejsza deklaracja stanowi podstawę do wystawienia tytułu wykonawczego, '+
   'zgodnie z art. 3a § 1 pkt 1 ustawy z dnia 17 czerwca 1966 r. o postępowaniu egzekucyjnym w administracji '+
   '(Dz. U. z 2020 r. poz. 1427, z późn. zm.).'
?}


\edek_poz_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Zwraca wartość atrybutu pozycji e-Deklaracji VIU-DO
::----------------------------------------------------------------------------------------------------------------------
_tag:=ISTDEFS.OPIS;
{? (_pd:=_tag*':')
|| _tag:=_pd-_tag
?};
_atr:=ISTDEFI.NAZ;
{? _tag='VATRate'
|| {? _atr='type'
   || val.S
   ?}
|? _tag='TaxableAmount' | _tag='VATAmount' | _tag='GrandTotalMSIDServices' | _tag='GrandTotalMSIDGoods' |
   _tag='GrandTotal' | _tag='TotalVATAmountCorrection' | _tag='BalanceOfVATDue' | _tag='TotalAmountOfVATDue'
|| {? _atr='currency'
   || 'EUR'
   ?}
?}


\edek_val
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Konwersja kwoty do napisu na potrzeby e-Deklaracji VIU-DO
::   WE: _a - kwota
::----------------------------------------------------------------------------------------------------------------------
_kw:=_a;
_skw:=$_a;
_dokl:=2;
{? (_poz:=(_skw*','))>0
|| _dokl:=+(_poz-_skw);
   {? _dokl<2 || _dokl:=2 ?}
?};
form(_kw,,_dokl,'9.')


\print
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Drukuje e-Deklaracje VIU-DO
:: ~OST: INBLGET,INCLIEXEC,INFCOPY,INFERASE,INFEXISTS,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
_interm:=exec('interm','#system');
{? _interm
|| _tmpdir:=fmk_tmp_dir(0);
{? type_of(_tmpdir) <> type_of(~~)
|| _fdir:=_tmpdir.get_path
|| _fdir:=''
?};
   _fc:="{? ~fexists(_b+'/'+_a) || fcopy(_a,_b+'/'+_a,1,0,1) || 1 ?}"
|| _fc:="{? ~fexists('@'+tmp_dir()+'/'+_a) || fcopy(_a,'@'+tmp_dir()+'/'+_a,1,0,1) || 1 ?}";
   _fdir:=tmp_dir()
?};

_fxsl:=-E_DEK.ZRODLO().NR().NAZWA+'_'+$VAT_VER.NR+'.xsl';
_fin:=_fdir+'/edek.xml';
_fout:=_fdir+'/edek.html';

{? fexists(_fxsl,1)
|| _fc(_fxsl,_fdir);
   E_DEK.bl_get('PLIK_GEN',{? _interm || '' || '@' ?}+_fin,0);
   {? _interm
   || exec('xml2html','xml',_fin,_fdir+'/'+_fxsl,_fout,_fdir)
   || exec('xml2html','xml',_fin,_fdir+'/'+_fxsl,_fout)
   ?};
   exec('replace','xml',_fout,32*'#',E_DEK.REF_ID,5800);
::   exec('replace','xml',_fout,50*'Y',XINFO.URZAD().NU);
   ferase({? _interm || '' || '@' ?}+_fdir+'/'+_fxsl,0);
   ferase({? _interm || '' || '@' ?}+_fin,0);
   {? _interm || dlg_save(_fout) || cli_exec(_fout) ?}
|| echo('Brak na serwerze pliku '+_fxsl+'.');
   FUN.info('Wydruk niedostępny.');
   echo()
?}


\be_b_viudo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Przed redagowaniem pola FINFO.B_VIUDO
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SLBANK
|| XINFO.SLBANK();
   1
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:39 105b424084482945cecedded0d012963d7e4ea7e48a4d57490c120d05dc3bffa0eee2bc9d38e92daed5795c9c1a7169ef4fb46c7dc78edfef8e1c6599f3d615ee35e6fa10fac62aa68720744c0495e72971c5447f870bf472608baf9cb2fd25730cbe5c40ba2ea6ce51b1a42a4002a976eab4e61ccf2e54f7b9ad561eb34a3e5

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: szukaj.fml
:: Utworzony: 01.01.2009
:: Autor: jaws
::======================================================================================================================
:: Zawartosc: Procedury do obsługi wyszukiwania współpracowników.
::======================================================================================================================


\p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyszukiwanie rekordu w widoku współpracowników.
::   WE: _a - rodzaj szukania
::   WY: wynik metody find_tab
::  OLD: \sz_widok_p/szukaj.fml
::----------------------------------------------------------------------------------------------------------------------
SZUKAJ.P_RSZUK:=_a;
P.find_tab(0,
   'FIRMA',,           '=',exec('ref_firma','ustawienia'),
   'OSOBA','NAZWISKO',  _a,SZUKAJ.NAZWISKO,
   'OSOBA','PIERWSZE',  _a,SZUKAJ.IMIE,
   'OSOBA','PESEL',     _a,SZUKAJ.PESEL,
   'OSOBA','PLEC',      _a,SZUKAJ.PLEC,
   'OSOBA','UR_DATA',   _a,SZUKAJ.UR_DATA,
   'OSOBA','UR_MIEJ',   _a,SZUKAJ.UR_MIEJ,
   'OSOBA','OBCY',      _a,SZUKAJ.OBCY,
   'OSOBA','NIP',       _a,SZUKAJ.NIP,
   'OSOBA','DOWOD',     _a,SZUKAJ.DOWOD,
   'OSOBA','PASZPORT',  _a,SZUKAJ.PASZPORT,
   'T',,                _a,SZUKAJ.T,
   'IP',,               _a,SZUKAJ.IP,
   'ZA',,               _a,SZUKAJ.ZA,
   'DZA',,              _a,SZUKAJ.DZA,
   'CO',,               _a,SZUKAJ.CO,
   'WY',,               _a,SZUKAJ.WY,
   'FNA','NAZWA',       _a,SZUKAJ.FNA,
   'ZAW','ZD',          _a,SZUKAJ.ZAW,
   'CP','CP',           _a,SZUKAJ.CP,
   'WYDZIAL','SYMBOL',  _a,SZUKAJ.WYDZIAL,
   'ST','ST',           _a,SZUKAJ.ST,
   'KK','SYM',          _a,SZUKAJ.KK,
   'DZ',,               _a,SZUKAJ.DZ,
   'USPKOD','KOD',      _a,SZUKAJ.USPKOD
)


\p_
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyszukiwanie współpracownika.
::   WE: _a - rodzaj szukania: dokładne (':*') / kontekstowe (':-')
::       _b - wyłączenie komunikatów (dla powtórnego szukania)
::   WY: wynik wyszukiwania rekordu
::  OLD: \szukaj_p/szukaj.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_b')<>type_of(0) | ~_b
|| SZUKAJ.blank();
   _buf:=obj_new(@.CLASS.BUFFER,'SZUKAJ');
   {!
   |? _b:=SZUKAJ.edit();
      {? _buf.is_equal() & _b
      || FUN.info('Niewypełniony wzorzec.'@);
         1
      ?}
   !};
   obj_del(_buf)
?};

{? var_press('_b')=type_of(0) & _b
|| _flt:=P.f_active();
   _ret:=exec('p','szukaj',_a);

   {? _ret & (((_flt=1 | _flt=3) & ~P.f_seek(P.ref())) | (_flt>1 & ~P.f_test()))
   || _ref:=P.ref();
      _ret:=0;
      {!
      |? {? exec('p','szukaj',_a)
         || _ret:=(((_flt=1 | _flt=3) & P.f_seek(P.ref())) | (_flt>1 & P.f_test()))
         ?};
         P.ref()<>_ref & ~_ret
      !}
   ?};

   {? ~_ret
   || SZUKAJ.P_RSZUK:='';
      FUN.info('Nie znaleziono zapisu zgodnego ze wzorcem.'@)
   |? _flt
   || P.f_seek(P.ref())
   ?};

   _ret
?}


\p_f3_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Powtórzenie ostatniego wyszukiwania.
::   WY: wynik wyszukiwania
::  OLD: \f3_p/szukaj.fml
::----------------------------------------------------------------------------------------------------------------------
{? SZUKAJ.P_RSZUK=''
|| exec('p_d','szukaj')
|| exec('p_','szukaj',SZUKAJ.P_RSZUK,1)
?}


\p_f3_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Wyświetla listę wartości występujących w kolummnie
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();

_P:=tab_tmp(1,'REF','STRING[16]','REFERENCE');
_ref:=P.ref();
_loop:=P.f_first();
{!
|? _loop
|! _P.REF:=$P.ref();
   _P.add();
   _loop:=P.f_next()
!};
P.f_seek(_ref);

_data:='select distinct ';
{? _fld='DZA'     || _data+='P.DZA as VALUE from P'
|? _fld='CO'      || _data+='P.CO as VALUE from P'
|? _fld='FNA'     || _data+='SLO_NAZ.NAZWA as VALUE from P join SLO_NAZ using (P.FNA,SLO_NAZ.REFERENCE)'
|? _fld='KK'      || _data+='KK.SYM as VALUE from P join KK using (P.KK,KK.REFERENCE)'
|? _fld='WY'      || _data+='P.WY as VALUE from P'
|? _fld='CP'      || _data+='CP.CP as VALUE from P join CP using (P.CP,CP.REFERENCE)'
|? _fld='WYDZIAL' || _data+='UD_SKL.SYMBOL as VALUE from P join UD_SKL using (P.WYDZIAL,UD_SKL.REFERENCE)'
|? _fld='ST'      || _data+='STN.ST as VALUE from P join STN using (P.ST,STN.REFERENCE)'
|? _fld='DZ'      || _data+='P.DZ as VALUE from P'
|? _fld='USPKOD'  || _data+='S_ZUS.KOD as VALUE from P join S_ZUS using (P.USPKOD,S_ZUS.REFERENCE)'
|| return()
?};
_data+=' join :_a using (P.REFERENCE,:_a.REF) order by VALUE';

_rs:=sql(_data,_P);
{? type_of(_rs)<>type_of(SYSLOG)
|| return()
?};

_wnd:=_rs.mk_sel('Wartości','P',,'p_f3_fld',,,20,,'U');
_len:=40;
{? _fld='WY'
|| _rs.win_fld(_wnd,,'VALUE',,,_len,3,,cur_nfld())
|| _rs.win_fld(_wnd,,'VALUE',,,_len,,,cur_nfld())
?};
_rs.win_act(_wnd,,'Formuła','Wybierz'@@,,'Wybór bieżącego zapisu'@,"sel_exit()",,1);
_rs.win_sel(_wnd);

_rs.VALUE:=fld();
_rs.find_rec();

{? _rs.select()
|| fld(_rs.VALUE)
?}


\p_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Dokładne wyszukiwanie współpracownika.
::  OLD: \szu_d_p/szukaj.fml
::----------------------------------------------------------------------------------------------------------------------
exec('p_','szukaj',':*');
~~


\p_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Kontekstowe wyszukiwanie współpracownika.
::  OLD: \szu_k_p/szukaj.fml
::----------------------------------------------------------------------------------------------------------------------
exec('p_','szukaj',':-');
~~


\zs_def_
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Wyszukiwanie współpracownika w zależnościach służbowych.
::   WE: _a - rodzaj szukania: dokładne (':*') / kontekstowe (':-')
::       _b - wyłączenie komunikatów (dla powtórnego szukania)
::   WY: wynik wyszukiwania rekordu
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_b')<>type_of(0) | _b=0<>0
|| SZUKAJ.blank();
   _buf:=obj_new(@.CLASS.BUFFER,'SZUKAJ');
   {!
   |? _b:=SZUKAJ.edit();
      {? _buf.is_equal() & _b
      || FUN.info('Niewypełniony wzorzec.'@);
         1
      ?}
   !};
   obj_del(_buf)
?};
{? ~(var_press('_b')=type_of(0) & _b)
:: porzucono wyszukiwanie
|| return(0)
?};

:: wyszukiwanie współpracownika z pominięciem ograniczeń dziedziny
_ref:=null;
P.cntx_psh();
P.prefix();
{? exec('p','szukaj',_a)<>0
|| _ref:=P.ref()
?};
P.cntx_pop();

{? _ref=null | ~ZS_DEF.find_tab(,
      'ZS_TYP',,'=',ZS_TYP.ref(),
      'P',,'=',_ref
   )
|| SZUKAJ.ZS_RSZUK:='';
   FUN.info('Nie znaleziono zapisu zgodnego ze wzorcem.'@);
   return(0)
?};
1


\zs_def_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Dokładne wyszukiwanie współpracownika w zależnościach służbowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zs_def_','szukaj',':*');
~~


\zs_def_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Kontekstowe wyszukiwanie współpracownika w zależnościach służbowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('zs_def_','szukaj',':-');
~~


\zs_def_wnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Powtórzenie ostatniego wyszukiwania w zależnościach służbowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? SZUKAJ.ZS_RSZUK=''
|| exec('zs_def_d','szukaj')
|| exec('zs_def_','szukaj',SZUKAJ.ZS_RSZUK,1)
?}


\p_ustaw_ezk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ustawia okna wyszukiwania dla obszaru EZK
::----------------------------------------------------------------------------------------------------------------------
exec('__WND','object');

_acr:='EZK';
{? (_wnd:=__WND.EDIT.get(SZUKAJ,_acr))=''
|| _wnd:=SZUKAJ.mk_edit('Wyszukiwanie'@,,,,,'html_maximized');
   SZUKAJ.win_ewin(_wnd,SZUKAJ,'OSOBA');
   SZUKAJ.win_ecol(_wnd);
   SZUKAJ.win_ewin(_wnd,SZUKAJ,'PRAC');
   SZUKAJ.win_ewin(_wnd,SZUKAJ,'ZWOL');
   exec('ok_esc','#window',SZUKAJ,_wnd,,,,,,,exec('text_red_szukaj','#window'));
   __WND.EDIT.put(SZUKAJ,_acr,_wnd)
?};
SZUKAJ.win_edit(_wnd)


\p_ustaw_zat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Ustawia okna wyszukiwania dla obszaru ZAT
::----------------------------------------------------------------------------------------------------------------------
exec('__WND','object');

_acr:='ZAT_P';
{? (_wnd:=__WND.EDIT.get(SZUKAJ,_acr))=''
|| _wnd:=SZUKAJ.mk_edit('Wyszukiwanie'@,,,,,'html_maximized');
   SZUKAJ.win_ewin(_wnd,SZUKAJ,'OSOBA');
   SZUKAJ.win_ecol(_wnd);
   SZUKAJ.win_ewin(_wnd,SZUKAJ,'PRAC');
   exec('ok_esc','#window',SZUKAJ,_wnd,,,,,,,exec('text_red_szukaj','#window'));
   __WND.EDIT.put(SZUKAJ,_acr,_wnd)
?};
SZUKAJ.win_edit(_wnd)


\zs_def_ustaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Ustawia okna wyszukiwania dla czynności ZWS_PAR_PZSL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__WND','object');

_acr:='ZS_DEF';
{? (_wnd:=__WND.EDIT.get(SZUKAJ,_acr))='';1
|| _wnd:=SZUKAJ.mk_edit('Wyszukiwanie'@,,,,,'html_maximized');
   SZUKAJ.win_ewin(_wnd,SZUKAJ,'OSOBA');
   SZUKAJ.win_ecol(_wnd);
   SZUKAJ.win_ewin(_wnd,SZUKAJ,'PRAC');
   SZUKAJ.win_ewin(_wnd,SZUKAJ,'ZS_DEF');
   exec('ok_esc','#window',SZUKAJ,_wnd,,,,,,,exec('text_red_szukaj','#window'));
   __WND.EDIT.put(SZUKAJ,_acr,_wnd)
?};
SZUKAJ.win_edit(_wnd)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 537be44905a5411a14983d9e089773b75edc68dac01a4f771eaa3062c7677868111d615f94a89b5eff8604a61ce584767e2a43c68b1d915683dbf4b9f912885240f299d3c5f057a82f43b5defb6a9ab416a0b0f983b47fdc3f697eaed98a53d646c2dc0867ae01e358dfd710f2180c0dbde66c9b6c523b06042be66b8c1074ab

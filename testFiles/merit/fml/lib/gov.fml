:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: gov.fml
:: Utworzony: 09.10.2016
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Pobieranie danych z portalu danepubliczne.gov.pl
::======================================================================================================================


\get_us_xls
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [19.02]
:: OPIS: Pobranie danych urzędów skarbowych w formacje xls.
::   WE: _a - obiekt z polem FN ze ścieżka do pliku lokalnego z xls
::   WY: '[OK]' lub opos błędu
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_licznik:=1;
{!|?
   _adres:='https://api.dane.gov.pl/datasets?page='+$_licznik+'&per_page=100&sort=-verified';
   _inet:=exec('inet','gov',_adres);
   _ret:=_inet.http_get();
   {? _ret<>200
   || return('Kod odpowiedzi: %1' [$_ret])
   ?};
   _odp:=json_parse(_inet.get_data());
   {? type_of(_odp)>100
   || {? var_pres('data',_odp)>0
      || {! _ii:= 1..obj_len(_odp.data)|!
            {? _odp.data[_ii].attributes.title = 'Dane teleadresowe jednostek Krajowej Administracji Skarbowej w Polsce'
            || _f_url2:=_odp.data[_ii].links.self;
               _inet2:=exec('inet','gov',_f_url2);
               _ret2:=_inet2.http_get();
               {? _ret2<>200
               || return('Kod odpowiedzi: %1' [$_ret2])
               ?};
               _odp2:=json_parse(_inet2.get_data());
               {? type_of(_odp2)>100
               || {? var_pres('data',_odp2)>0
                  || _f_url3:=_odp2.data.relationships.resources.links.related;
                     _ext:={? var_pres('formats',_odp2.data.attributes)>0 & obj_len(_odp2.data.attributes.formats)
                           || _odp2.data.attributes.formats[1]
                           || 'xls'
                           ?};
                     _obj.FN+='.'+_ext;
                     _inet3:=exec('inet','gov',_f_url3);
                     _ret3:=_inet3.http_get();
                    {? _ret3<>200
                    || return('Kod odpowiedzi: %1' [$_ret3])
                    ?};
                    _odp3:=json_parse(_inet3.get_data());
                    {? type_of(_odp3)>100
                    || {? var_pres('data',_odp3)>0
                       || _f_url3:=_odp3.data[1].attributes.download_url
                       || return('Nieprawidłowa struktura.')
                       ?}
                    ?};
                    obj_del(_odp3,_inet3);
                    obj_del(_odp2,_inet2);
                    obj_del(_odp,_inet);
                    _fname:=_obj.FN;
                    _file:= fopen(_fname,'bw',,,1);
                    {? ~_file.is_open
                    || return('Błąd otwarcia pliku: dane_us.xls.')
                    || _i_xls:=exec('inet','gov',_f_url3);
                       _ret4:=_i_xls.http_get(_file);
                       {? _ret4<>200
                       || return('Kod odpowiedzi: %1' [$_ret4])
                       ?};
                       _file.fclose();
                       &_file;
                       return('[OK]')
                    ?}
                  || return('Nieprawidłowa struktura.')
                  ?}
               ?}
            ?}
         !}
      || return('Nieprawidłowa struktura.')
      ?}
   ?};
   _licznik:=_licznik+1;
   VAR_DEL.delete('_inet','_odp');
   &_odp;
   obj_del(_inet);
   _ret=200
!};
return('Nie znaleziono zbioru')

\us
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Dane urzędów skarbowych
:: ~OST: INTMKTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
{? ~FUN.ask('Rozpocząć aktualizację danych adresowych Urzędów Skarbowych?\n\n'
            'Uwaga - dane pobierane są z serwisu Otwarte dane (dane.gov.pl)\nprowadzonego przez Ministerstwo Cyfryzacji,\n'
            'z bazy: Dane teleadresowe jednostek Krajowej Administracji Skarbowej.'@)
|| return(0)
?};

echo('Pobieranie danych z serwisu Otwarte dane: dane.gov.pl...');
_tmpdir:=fmk_tmp_dir(1);
{? type_of(_tmpdir) <> type_of(~~)
|| _fdir:=_tmpdir.get_path
|| FUN.emsg('Nie udało się utworzyć katalogu tymczasowego po stronie serwera.'@);
   return()
?};
_sep:=exec('sep','#file',0);
_fname:='@'+_fdir+_sep+'dane_us';
_obj:=obj_new('FN');
_obj.FN:=_fname;
_ref:=exec('get_us_xls','gov',_obj);
_fname:=_obj.FN;
echo();

_ref:='[OK]';

US.cntx_psh(); US.prefix();

{? _ref='[OK]'
|| VAR_DEL.delete('__US');
   _US:=tab_tmp(1,'KOD_US','STRING[10]','',
                  'POMOC','STRING[1]','',
                  'NAZWA','STRING[100]','',
                  'FAX','STRING[50]','',
                  'WOJ','STRING[30]','',
                  'KOD_P','STRING[10]','',
                  'MIASTO','STRING[30]','',
                  'E_MAIL','STRING[50]','',
                  'TELEFON','STRING[50]','',
                  'ID','STRING[10]','',
                  'TYP','STRING[5]','',
                  'ULICA','STRING[150]','',
                  'NR_BL','STRING[20]','');

:: czytanie danych z pliku xls
   exec('Xls','object');
   {? Xls.open(1-_fname)
   ||
      echo('Wczytywanie danych z pliku xls:');
      Xls.getSheet(0);
      {! _rr:=3..Xls.lastRow()
      |! {? Xls.getRow(_rr)>0
         ||
            _US.blank();
::          (0) województwo
            _US.WOJ:=Xls.getValue(0);
::          (1) typ
            _US.TYP:=Xls.getValue(1);
::          (2) nazwa urzędu
            _US.NAZWA:=gsub(Xls.getValue(2),'\r\n',' ');
::          (3) kod
            _US.KOD_P:=Xls.getValue(3);
::          (4) miasto
            _US.MIASTO:=Xls.getValue(4);
::          (5) ulica
            _US.ULICA:=Xls.getValue(5);
::          (6) nr budynku/lokalu
            _nr_bl:=Xls.getValue(6);
            {? _nr_bl+2='.0' || _nr_bl-=2 ?};
            _US.NR_BL:=_nr_bl;
::          (7) poczta
            _poczta:=Xls.getValue(7);
::          (8) nr telefonu
            _US.TELEFON:=Xls.getValue(8);
::          (9) nr faxu
            _US.FAX:=Xls.getValue(9);
::          (10) adres e-mail urzędu
            _US.E_MAIL:=Xls.getValue(10);
::          (11) kod jednostki
            _kod:=Xls.getValue(11);
            {? _kod+2='.0' || _kod-=2 ?};
            _US.KOD_US:=form(_kod);
::          (12) adres stron BIP
            _www:=Xls.getValue(12);
::          ręczna modyfikacja do czasu wyjaśnienia podwojonego kodu jednostki (3001) w danych na stronie dane.gov.pl
            {? _US.NAZWA='Biuro Wymiany Informacji Podatkowych w Koninie' & _US.KOD_US='3001'
            || _US.KOD_US:='3001B'
            ?};
            _US.add();
            echo('Wczytywanie danych z pliku xls: '+_US.NAZWA)
         ?}
      !};
      echo()
   ?};

:: aktualizacja tabeli US
   {? _US.first()
   || _tmp:=tab_tmp(1,'KOD_US','STRING[10]','Kod US'@,
                      'NAZWA','STRING[50]','Nazwa US'@,
                      'ZMIANA','STRING[100]','Zmiana'@);
      _tmp.win_sel(_tmp.mk_sel(,,1));
      US.cntx_psh();
      US.prefix();
      {!
      |?
::       jezeli znaleziono kod US
         {? US.find_tab(1,'EDEK_SYM',,'=',_US.KOD_US)
         || _zmiana:='';
            {? US.MU<>_US.MIASTO || US.MU:=_US.MIASTO; _zmiana+='Miasto ' ?};
            {? US.NU<>50+_US.NAZWA || US.NU:=50+_US.NAZWA; _zmiana+='Nazwa ' ?};
            {? US.UU<>_US.ULICA & US.UU<>_US.ULICA+' '+form(_US.NR_BL) || US.UU:=_US.ULICA+' '+form(_US.NR_BL); _zmiana+='Ulica ' ?};
            {? US.UK<>_US.KOD_P || US.UK:=_US.KOD_P; _zmiana+='Poczta ' ?};
            {? _zmiana<>''
            || {? US.put(1)
               || _tmp.KOD_US:=_US.KOD_US;
                  _tmp.NAZWA:=50+_US.NAZWA;
                  _tmp.ZMIANA:='Modyfikacja: '+_zmiana;
                  _tmp.add()
               ?}
            ?}
         ||
::          jezeli nie znaleziono kodu US ale znaleziono nazwe US
            {? US.find_tab(1,'NU',,'=',50+_US.NAZWA)
            || _zmiana:='';
               {? US.EDEK_SYM='' | US.EDEK_SYM<>_US.KOD_US
               || US.EDEK_SYM:=_US.KOD_US; _zmiana:='Symbol dla e-deklaracji '
               ?};
               {? US.MU<>_US.MIASTO || US.MU:=_US.MIASTO; _zmiana+='Miasto ' ?};
               {? US.UU<>_US.ULICA & US.UU<>_US.ULICA+' '+form(_US.NR_BL) || US.UU:=_US.ULICA+' '+form(_US.NR_BL); _zmiana+='Ulica ' ?};
               {? US.UK<>_US.KOD_P || US.UK:=_US.KOD_P; _zmiana+='Poczta ' ?};
               {? _zmiana<>''
               || US.put();
                  _tmp.KOD_US:=_US.KOD_US;
                  _tmp.NAZWA:=50+_US.NAZWA;
                  _tmp.ZMIANA:='Modyfikacja: '+_zmiana;
                  _tmp.add()
               ?}
            ||
::             jezeli nie znaleziono ani kodu US ani nazwy US to nowy wpis
               US.blank();
               US.EDEK_SYM:=_US.KOD_US;
               US.NU:=50+_US.NAZWA;
               US.MU:=_US.MIASTO;
               _dlg:=+(' '+form(_US.NR_BL));
               {? _US.ULICA+_dlg=(' '+form(_US.NR_BL))
               || US.UU:=_US.ULICA
               || US.UU:=_US.ULICA+' '+form(_US.NR_BL)
               ?};
               US.UK:=_US.KOD_P;
               {? US.add()
               || _tmp.KOD_US:=_US.KOD_US;
                  _tmp.NAZWA:=_US.NAZWA;
                  _tmp.ZMIANA:='Nowy Urząd Skarbowy';
                  _tmp.add()
               ?}
            ?}

         ?};
         _US.next()
      !};
      US.cntx_pop();

:: weryfikacja pustych pól z symbolem US - do współpracy ze słownikami użytkownika
      exec('us_sym','gov');

      {? _tmp.first()
      || _tmp.hdr_sel('Lista zmodyfikowanych lub dodanych Urzędów Skarbowych.'@);
         _tmp.select()
      || FUN.info('Nie znaleziono Urzędów Skarbowych wymagających modyfikacji.'@)
      ?}
   ?}
|| FUN.emsg('Podczas pobierania danych wystąpił błąd: '+_ret)
?};

US.cntx_pop()


\us_sym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [19.02]
:: OPIS: weryfikacja pustych pól z symbolem US - do współpracy ze słownikami użytkownika
::----------------------------------------------------------------------------------------------------------------------
US.cntx_psh(); SLU.cntx_psh();
SLU.index('NAZ'); SLU.prefix('URZĘDY SKARBOWE',);
{? SLU.first() || __chars:=SLU.DL || __chars:=3 ?};
US.prefix();
lp:=1;
US.for_each("{? US.SYM=''
             || US.SYM:=form(lp,-__chars);
                US.put();
                lp+=1
             ?}");
US.cntx_pop(); SLU.cntx_pop();
VAR_DEL.delete('lp','__chars')


\inet
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42]
:: OPIS: Zwraca obiekt tworzony przez inet_get
::   WE: _a - adres
::----------------------------------------------------------------------------------------------------------------------
_inet:=inet_get(_a);
{? sys_name(1)='U_LINUX'
|| _pem:='ssl_stat_gov_pl.pem';
   _inet.set_cert(pth_dir(_pem)+'/'+_pem)
?};
_inet

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 47c98ef4bf34a813e9bef891af2164fb8ac967cbd5184094e7211998ddf7c7a9d2b3da7e6028152174cc286a4f6a0d47ee0a38735c7ffc6c1a20365ba5d151354f11ed79a5f11e209d391a812ac75e256f47e707ff364bb35486921982546ae5e29908f0dde8b61cd63e88a7e8d2f02422e7a83a77c7a6b0cb5b484ec75c537e

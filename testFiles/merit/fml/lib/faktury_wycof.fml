:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: faktury_wycof.fml
:: Utworzony: 11.09.2019
:: Autor: MW
::======================================================================================================================
:: Zawartość: Obsługa dokumentów sprzedaży i zakupu - wycofanie akceptacji lub zakończenia
::======================================================================================================================


\faks_wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: Dokument sprzedaży lub zakupu - Wycofaj
::  WE: [_a] - czy wyswietlac drzewko z informacjami (T/N) - domyslnie T
::   WY: (0/1) czy wycofano akceptacje
::----------------------------------------------------------------------------------------------------------------------
{? _>=1
|| {? type_of(_a)<>2
   || _a:='T'
   ?}
|| _a:='T'
?};

{? _a='T'
|| _kom:=1
|| _kom:=0
?};

_gru:=(FAKS.sel_size()>0 & var_pres('__askwyc')>0);

_p2200:=exec('get','#params',2200,type_of(''),OPERATOR.USER)='T';
{? _gru
|| _choice:=__askwyc;
   _fak_wyc1:={? _choice=2 || 'akc' || 'all' ?};
   _kom:=1
|| _blkprn:=
      FAKS.SZ='S' & FAKS.STAT_REJ='T'
      & _p2200
      & exec('prn2mail','faktury_nag',$FAKS.ref());
   _eoa:=exec('chk_EndOrAcc','#b__box'
      ,exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF)
      ,{? FAKS.SZ='S' || 'LSP_FAK_EASP' || 'LZK_ZAK_EZAK' ?});
   _fak_wyc1:=
      {? FAKS.T().VALREC<>null() & ~exec('validate','wzorce',FAKS.T().VALREC,FAKS,FAKS.ref())
      || 'anuluj'
      |? _blkprn
      || 'blk'
      |? FAKS.OBI_POW<>'' & FAKS.SZ='S'
      || 'obg'
      |? FAKS.STAT_REJ='A'
      || ''
      |? _eoa=1 & FAKS.STAT_REJ='T'
      || _choice:=FUN.choice('Wybierz jedną z opcji wycofania akceptacji lub zakończenia dokumentu %1'@[FAKS.SYM]
                   ,,'Akceptację i &zakończenie'@,'&Akceptację'@);
         {? _choice=1 || 'all' |? _choice=2 || 'akc' || 'anuluj' ?}
      |? (_eoa=1 | _eoa=2) & FAKS.STAT_REJ='Z'
      || {? FUN.ask('Wycofać zakończenie redakcji dokumentu %1?'@[FAKS.SYM]) || 'end' || 'anuluj' ?}
      |? _eoa=4 & FAKS.STAT_REJ='T'
      || {? FUN.ask('Wycofać akceptację dokumentu %1?'@[FAKS.SYM]) || 'akc' || 'amuluj' ?}
      || ''
      ?}
?};

{? _fak_wyc1='anuluj'
|| return(0)
?};

{? _kom
|| {? ~_gru
   || exec('ini_kom','#message','Informacje o wycofaniu dokumentów'@)
   ?};
   __kom_gr:='Faktura ' + FAKS.SYM
?};

_wyn:=0;
_stat_rej:=FAKS.STAT_REJ;
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);

{? _r_lock
|| _recplug:=0;
   _blkprn:=
       FAKS.SZ='S' & FAKS.STAT_REJ='T'
       & _p2200
       & exec('prn2mail','faktury_nag',$FAKS.ref());
   _eoa:=exec('chk_EndOrAcc','#b__box'
        ,exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF)
        ,{? FAKS.SZ='S' || 'LSP_FAK_EASP' || 'LZK_ZAK_EZAK' ?});
   _fak_wyc:=
       {? _blkprn
       || 'blk'
       |? FAKS.OBI_POW<>'' & FAKS.SZ='S'
       || 'obg'
       |? FAKS.STAT_REJ='A'
       || 'anulowany'
       |? FAKS.STAT_REJ='N'
       || 'nie zakonczony'
       |? FAKS.STAT_REJ='Z' & _fak_wyc1='akc'
       || 'nie zaakceptowany'
       |? (_rek:=exec('dokWITHrek','reklamacje',FAKS.uidref()); _rek<>'')
       || 'reklamacja %1'[_rek]
       |? _eoa=1 & FAKS.STAT_REJ='T' & (_fak_wyc1='all' | _fak_wyc1='akc')
       || _fak_wyc1
       |? (_eoa=1 | _eoa=2) & FAKS.STAT_REJ='Z' & (_fak_wyc1='all' | _fak_wyc1='end')
       || 'end'
       |? _eoa=4 & FAKS.STAT_REJ='T' & (_fak_wyc1='all' | _fak_wyc1='akc')
       || 'akc'
       || ''
       ?};
   {? _fak_wyc='anulowany'
   || _txt:='Dokument anulowany. Wycofanie akceptacji i zakończenia dokumentu niemożliwe.'@;
      {? _kom
      || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
      || FUN.info(_txt)
      ?}
   |? _fak_wyc='nie zakonczony'
   || _txt:='Nie zakończono rejestracji dokumentu. Wycofanie akceptacji i zakończenia dokumentu niemożliwe.'@;
      {? _kom
      || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
      || FUN.info(_txt)
      ?}
   |? _fak_wyc='nie zaakceptowany'
   || _txt:='Nie zaakceptowano dokumentu. Wycofanie akceptacji niemożliwe.'@;
      {? _kom
      || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
      || FUN.info(_txt)
      ?}
   |? (10+_fak_wyc)='reklamacja'
   || _txt:={? (2+_rek)='SN'
            || 'Do dokumentu wystawiono reklamację klienta %1. Wycofanie akceptacji niemożliwe.'@[13-_fak_wyc]
            |? (2+_rek)='SD'
            || 'Dokument powiązano z reklamacją klienta %1. Wycofanie akceptacji niemożliwe.'@[13-_fak_wyc]
            |? (2+_rek)='ZN'
            || 'Do dokumentu wystawiono reklamację dostaw %1. Wycofanie akceptacji niemożliwe.'@[13-_fak_wyc]
            |? (2+_rek)='ZD'
            || 'Dokument powiązano z reklamacją dostaw %1. Wycofanie akceptacji niemożliwe.'@[13-_fak_wyc]
            || 'Dokument powiązany z reklamacją. Wycofanie akceptacji niemożliwe.'@
            ?};
      {? _kom
      || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
      || FUN.info(_txt)
      ?}
   |? _fak_wyc='all' | _fak_wyc='end'
   || {? FAKS.STAT_REJ<>'N' || _recplug:=2 ?};
      exec('fak_wyc','faktury_nag',_gru,,0);
      {? FAKS.STAT_REJ='N'
      || _recplug:=1;
         _wyn:=1
      ?}
   |? _fak_wyc='akc'
   || {? _gru & FAKS.T().VALREC<>null() & ~exec('validate','wzorce',FAKS.T().VALREC,FAKS,FAKS.ref())
      || 1
      |? ~exec('czy_z_ok','okresy',{? FAKS.SZ='S' || -2 || -3 ?},~_kom,FAKS.AR,FAKS.AM)
      || 1
      |? FAKS.ZAK='T'
      || _txt:='Dokument jest zaksięgowany. Wycofanie akceptacji niemożliwe.'@;
         {? _kom
         || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
         || FUN.info(_txt)
         ?}
      |? exec('doc_in_ksef','faktury_nag1')
      || _txt:='Dokument ma status KSeF: %1. Wycofanie akceptacji niemożliwe.'@[FAKS.STATKSEF];
         {? _kom
         || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
         || FUN.info(_txt)
         ?}
      |? (FAKS.EDI_W='T' | FAKS.EDI_W='B') & FAKS.T().KSEF='N'
      || _txt:='Na podstawie dokumentu utworzono komunikat EDI. Wycofanie akceptacji niemożliwe.'@;
         {? _kom
         || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
         || FUN.info(_txt)
         ?}
      |? FAKS.KORYG='T'
      || _txt:='Wystawiono korektę do dokumentu. Wycofanie akceptacji niemożliwe.'@;
         {? _kom
         || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
         || FUN.info(_txt)
         ?}
      || _recplug:=2;
         _del_autoadd:=exec('del_autoadd','dokum',FAKS.ref());
         FAKS.get();
         FAKS.CZY_DRUK:={? _del_autoadd=1 || 'X' || 'N' ?};
         FAKS.STAT_REJ:='Z';
         {? FAKS.put()
         || {? _recplug=2 || _recplug:=1 ?};
            _wyn:=1;
            {? FAKS.T().KOR='T' & FAKS.T().HIS='T'
            || exec('hist_akc','faktury_nag',0,'Z')
            ?}
         ?}
      ?}
   |? _fak_wyc='blk'
   || _txt:='Dokument został przesłany. Wycofanie akceptacji niemożliwe.'@;
      {? _kom
      || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
      || FUN.info(_txt)
      ?}
   |? _fak_wyc='obg'
   || _txt:='Dokument został przesłany do obiegu. Wycofanie akceptacji niemożliwe.'@;
      {? _kom
      || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
      || FUN.info(_txt)
      ?}
   || _txt:='Brak uprawnień do wycofania akceptacji i zakończenia dokumentu.'@;
      {? _kom
      || exec('add_kom','#message',gsub(_txt,'\n',' '),1)
      || FUN.info(_txt)
      ?}
   ?};
   {? _recplug=1 || Plugin.run('AFTER_RECALL_001',{? FAKS.SZ='S' || 'DS' || 'DZ' ?}) ?};
   exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
|| exec('who_rlock_faks','faktury_nag')
?};

:: komunikaty
{? _kom
|| {? _wyn=1
   || {? _stat_rej='T' & _fak_wyc1='akc'
      || exec('add_kom','#message','Akceptacja została wycofana.'@,38)
      ?}
   ?};
   {? ~_gru
   || __kom_on:=1;
      exec('end_kom','#message')
   ?}
?};

:: przywrócenie zmiennej __askwyc, jeśli usunięte zostaną też dokumenty magazynowe
{? _gru || __askwyc:=_choice ?};

_wyn


\wycbdok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: wycofanie akceptacji dokumentow (przed funkcja grupowa)
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__askwyc');

exec('ini_kom','#message','Informacje o wycofaniu dokumentów'@);
_ask:=0;

_ask:=FUN.choice('Wybierz jedną z opcji wycofania akceptacji lub zakończenia zaznaczonych dokumentów'@
      ,,'Akceptację i &zakończenie'@,'&Akceptację'@);

{? ~_ask
|| FAKS.sel_adel();
   VAR_DEL.delete('__askwyc')
|| __askwyc:=_ask
?};
sel_nchk();
''


\wycadok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [19.42]
:: OPIS: wycofanie akceptacji dokumentow (po funkcji grupowej)
::   WE: _a - Czy wyswietlac komunkat na koniec 1-Tak/0-Nie
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?}    || _a:=1 ?};
{? _a
|| __kom_on:=1;
   exec('end_kom','#message')
?};
VAR_DEL.delete('__askwyc');
1


:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 cd1593acc5f3ec43b1f161bf003be163bd70a026cc654be0afbbdf7d14d3b9cbfc2ce5c19c40ba58601e288225f905583c31f7067dc9a0f5292fde93b3d8dba56455550bbb76d93e5a935fa2adab8dc84bc5acf7c329da0f3cdf2d1eec02c187c9558d791e6ac8ae2a857f37549eac4f2af2463887f98ec6dc6d33a4a29cccf7

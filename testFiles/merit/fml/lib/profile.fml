:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: profile.fml [2000]
:: Utworzony: 2000/11/10
:: Autor: GS
::======================================================================================================================
:: Zawartość: Definicje funkcji umożliwących zachowywanie "ustawień".
::======================================================================================================================


\code_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Obsługa zestawów rubryk.
::   WE: [_a] [STRING] - Nazwa pliku [domyślnie: profile.upf].
::        _b  [STRING] - Nazwa sekcji.
::       [_c] [STRING] - Klasa rubryk, jeżeli brak, to wszystkie klasy.
::       [_d] [NUMBER] - Maksymalna liczba kodów, jeśli pominięty, to brak ograniczenia.
::       [_e] [STRING] - Lista kodów rubryk dołączanych dodatkowo do wyboru (rozdzielonych przecinkami) [domuślnie:''].
::       [_f] [STRING] - Tryb pracy:
::                         'SEL'    - Obsługa rzeczywistego wyboru zestawu składników [domyślnie].
::                         'DICT'   - Przygotowanie tabeli tymczasowej.
::   WY: Rezultat określa parametr _f:
::          Lista składników.
::          Tabela tymczasowa.
::----------------------------------------------------------------------------------------------------------------------
_fn:={? var_pres('_a')=type_of('') || _a || 'profile.upf' ?};
_klasa:={? var_pres('_c')=type_of('') || _c || '' ?};
_max:={? var_pres('_d')=type_of(0) || _d || 0 ?};
_lista:={? var_pres('_e')=type_of('') || _e || '' ?};
_tryb:={? var_pres('_f')=type_of('') & (_f='SEL' | _f= 'DICT') || _f || 'SEL' ?};

_ret:='';
{? var_pres('_b')=type_of('') & +form(_b)
|| _sekcja:=_b;
   _PROFILE:=exec('load','#profile',_fn);
   _PROFILE.fld_attr(,2);
   _wnd:=_PROFILE.mk_sel('Wybór ustawień'@,'N',0,'profile2',,,25,,'U');
   _PROFILE.win_fld(_wnd,,'NAME',,,64,,,,,'Zestaw składników listy płac uwzględnionych w wydruku.'@);
   _PROFILE.win_act(_wnd,0,'Formuła','Wybierz'@@,,,
      "  _PROFILE:=cur_tab(1,1);
         {? _PROFILE.memo_txt(,1,'VALUE')=''
         || FUN.emsg(
               'Ustawienia ""%1"" nie zawierają żadnych składników.\n'
               'Należy wybrać akcję ""Szczegóły"" i wskazać składniki do zestawienia.'@[_PROFILE.NAME]
            );
            0
         || sel_exit()
         ?}
      ",,1,,,,'W'
   );
   _PROFILE.win_act(_wnd,1,'Dołącz',,,,,"0",1);
   _PROFILE.win_act(_wnd,0,'Dołącz',,,,,"0");
   _PROFILE.win_act(_wnd,0,'Popraw');
   _PROFILE.win_act(_wnd,0,'Usuń');
   _PROFILE.win_act(_wnd,0,'Formuła','Szczegóły'@@,,,
      $("_PROFILE:=cur_tab(1,1);
         _ret:=exec('sel_code','profile','"+_klasa+"',_PROFILE.memo_txt(,1,'VALUE'),"+$_max+",'"+_lista+"');
         _PROFILE.memo_set(_ret,'VALUE');
         _PROFILE.memo_put(,'VALUE')
      "),,,,,,
      'S'
   );
   {! _empty:=0 .. 1
   |! _PROFILE.win_act(_wnd,_empty,'Rekord',,,,,
         $("_PROFILE:=cur_tab(1,1);
            _PROFILE.GROUP:='"+_sekcja+"';
            __CHK.record(_PROFILE,,'NAME')
         ")
      )
   !};
   _PROFILE.win_btn(_wnd,'text=%1,align=begin'['Dołącz'@],'menu:D');
   _PROFILE.win_btn(_wnd,'text=%1,panel=right,align=begin'['Szczegóły'@],'menu:S');
   _PROFILE.win_sopt(_wnd,'select_record_checkbox=0');
   _PROFILE.win_sel(_wnd);

   _we:=_PROFILE.mk_edit('Ustawienie'@,,'profile2');
   _PROFILE.win_esep(_we,'Dane podstawowe'@);
   _PROFILE.win_efld(_we,,'NAME',,,64,,,,,'Nazwa zestawu składników'@);
   _PROFILE.efld_opt(_we,'mark=1',,'NAME');
   exec('ok_esc','#window',_PROFILE,_we,,,,,,,exec('text_red_ok','#window'));
   _PROFILE.win_edit(_we);

   _PROFILE.prefix(_sekcja);

   {? _tryb='SEL'
   || {!
      |? {? _PROFILE.select()
         || _ret:=_PROFILE.memo_txt(,1,'VALUE');
            0
         || FUN.ask(
               'Żadne ustawienia nie zostały wybrane.\n'
               'Czy na pewno chcesz kontynuować (bez wskazania rubryk)?'@
            )=0
         ?}
      !}
   ?};

:  Zmiana kolejności.
   {? _PROFILE.first()
   || _R:=sql('select LP, RN, 0 as WYB from R order by LP');
      {!
      |? _value:=_PROFILE.memo_txt(,1,'VALUE');
         {? _value<>'' & _R.last()
         || {!
            |? _wyb:=_value*',%1,' [$_R.RN];
               {? _R.WYB<>_wyb
               || _R.WYB:=_wyb;
                  _R.put()
               ?};
               _R.prev()
            !};
            _codes:='';
            {!
            |? {? _R.WYB
               || _codes+=','+$_R.RN
               ?};
               _R.next()
            !};
            {? +_codes
            || _PROFILE.memo_set(_codes+',','VALUE');
               _PROFILE.memo_put()
            ?}
         ?};
         _PROFILE.next()
      !}
   ?};

   {? _tryb='SEL'
   || _PROFILE.clear();
      exec('save','#profile',_PROFILE,_fn);
      exec('max_code','profile',_ret,_max)
   |? _tryb='DICT'
   || _PROFILE
   ?}
|| ~~
?}


\max_code
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Ogranicza liczbę wybranych składników
::   WE: _a [STRING] - Lista kodów.
::       _b [NUMBER] - Maksymalna liczba składników.
::       _c [NUMBER] - Nie obcinaj wyniku.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _b<=0 | _a='' || return(_a) ?};
_c:=(var_pres('_c')=type_of(0) & _c);

_codes:=',';
_skip:='';

_a:=1-_a-1;
STR.split(_a,',');
_ii:=0;
{!
|? (_word:=STR.get_word())<>''
|! {? _b<(_ii+=1)
   || _skip+=_word+', ';
      {? _c || _codes+=_word+',' ?}
   || _codes+=_word+','
   ?}
!};
{? +_skip
|| _skip:=_skip-2;
   FUN.emsg(
      {? _skip*','=0
      || 'Przekroczono dopuszczalną liczbę składników.\n(składnik %1 będzie pominięty)'@ [_skip]
      || 'Przekroczono dopuszczalną liczbę składników.\n(składniki: %1 będą pominięte)'@ [_skip]
      ?}
   )
?};
_codes


\sel_code
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO [17.00]
:: OPIS: Funkcja wyboru rubryk.
::   WE: [_a] [STRING]  - Klasa rubryk, jeśli pominięty, to do wyboru udostępniona będzie cała zawartość słownika R.
::       [_b] [STRING]  - Lista domyślnie wybranych kodów.
::       [_c] [INTEGER] - Maksymalna liczba kodów, jeśli pominięty, to brak ograniczenia.
::       [_d] [STRING] - Lista kodów rubryk dołączanych dodatkowo do wyboru (rozdzielonych przecinkami) [domuślnie:''].
::   WY: Lista wybranych składników oddzielona przecinkami.
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || _a:='' ?};
_b:={? var_pres('_b')=type_of('') || ','+_b+',' || '' ?};
{? var_pres('_c')<>type_of(0) || _c:=0 ?};

: utwórz bufor do wyboru rubryk
: kod, nazwa, klasyfikacja (zaznaczanie), kolejność obliczeń
_TAB:=tab_tmp(1,
   'RN','INTEGER',MS.name(R,'RN'),
   'RT','STRING['+$MS.fld_len(R,'RT')+']',MS.name(R,'RT'),
   'KL','STRING[1]','Klasyfikacja',
   'LP','INTEGER',MS.name(R,'LP')
);
_ndx:=_TAB.ndx_tmp('Kolejność obliczeń'@,,'LP',,);
:  interfejs bufora
_TAB.fld_attr(,2);
_ws:=_TAB.mk_sel('Rubryki'@,'P',0,'profile1',,,29,,'U');
_TAB.win_fld(_ws,,'KL',,,-3,,,,,'Kwalifikacja (T/N)'@,2,,"\'T\'","\'N\'");
_TAB.win_fld(_ws,,'LP',,,-5,,,,,MS.comment(R,'LP'));
_TAB.win_fld(_ws,,'RN',,,-5,,,,,MS.comment(R,'RN'));
_TAB.win_fld(_ws,,'RT',,,,,,,,MS.comment(R,'RT'));
_TAB.win_act(_ws,0,'Formuła','Zaznacz'@@,,'Zaznacza bieżącą rubrykę'@,
   "_TAB:=cur_tab(1,1); _TAB.KL:='T'; _TAB.put()",,1,1,,,'Z');
_TAB.win_act(_ws,0,'Formuła','Pomiń'@@,,'Pomiń bieżącą rubrykę'@,
   "_TAB:=cur_tab(1,1); _TAB.KL:='N'; _TAB.put()",,,1,,,'P');
_TAB.win_act(_ws,0,'Formuła','Odwróć'@@,,'Odwróć zaznaczenie'@,
   "_TAB:=cur_tab(1,1); _TAB.KL:={? _TAB.KL='N' || 'T' || 'N' ?}; _TAB.put()",,,1,,,'O');
_TAB.win_act(_ws,0,'Formuła','Dalej'@@,,'Przechodzi do następnego kroku'@,"sel_exit()",,,,,,'D',,'target=window');
_TAB.win_act(_ws,0,'Rekord',,,,
   "  _par:=params_get();
      _TAB:=_par.TAB;
      _ws:=_par.ws;
      {? _TAB.sel_size()
      || _ag:='';
         _ad:=':'
      |? _TAB.KL=\'T\'
      || _ag:='Z';
         _ad:='P'
      || _ag:='P';
         _ad:='Z'
      ?};
      _TAB.actions_grayed(_ws,_ag);
      _TAB.actions(_ws,,_ad,1);
      ~~
   ");
_TAB.win_act(_ws,0,'Szukaj');
_TAB.win_act(_ws,0,'Kolejność');
_TAB.win_btn(_ws,'text='+'Zaznacz'@+',panel=right,align=begin','menu:Z');
_TAB.win_btn(_ws,'text='+'Pomiń'@+',panel=right,align=begin','menu:P');
_TAB.win_btn(_ws,'text='+'Odwróć'@+',panel=right,align=begin','menu:O');
_TAB.win_btn(_ws,'text='+'Dalej'@+',icon=xwin16.png:22,panel=bottom,align=end','menu:D');

_grp:=_TAB.grp_make('Rubryki'@,,'profile1a',,,
   "  _TAB:=cur_tab(1,1);
      _zazn:=0;
      _TAB.cntx_psh();
      {? _TAB.first()
      || {!
         |? _zazn+=_TAB.KL='T';
            _TAB.next()
         !}
      ?};
      _TAB.cntx_pop();
      _zazn>0 | FUN.ask('Żadna z rubryk nie została zaznaczona.\nCzy na pewno rezygnujesz?'@)
   ",,'normal');
_TAB.grp_sel(_grp,,_ws,,,,,29,,,,,'maximized');
_TAB.win_sel(_grp);

: przepisz wymagane informacje ze słownika rubryk
: (uwzględnienie klasy rubryk lub wszystkie zapisy)
R.cntx_psh();
R.index({? +_a || 'RUBKLKOD' || 'RUBKOD' ?});
{? +_a || R.prefix(_a) || R.prefix() ?};
{? R.first()
|| {!
   |? _TAB.blank();
      _TAB.RN:=R.RN;
      _TAB.RT:=R.RT;
      _TAB.KL:={? _b*(','+$R.RN+',') || 'T' || 'N' ?};
      _TAB.LP:=R.LP;
      _TAB.add();
      R.next()
   !}
?};
R.cntx_pop();

: dołączenie dodatkowych rubryk (wskazanych bezpośrednio parametrami)
{? var_pres('_d')=type_of('') & +_d
|| _ra:=spli_str(_d,',');
   R.cntx_psh();
   R.index('RUBKOD');
   R.prefix();
   {! _tmp:=1 .. obj_len(_ra)
   |! {? R.find_key(#_ra[_tmp])
      || _TAB.blank();
         _TAB.RN:=R.RN;
         _TAB.RT:=R.RT;
         _TAB.KL:={? _b*(','+$R.RN+',') || 'T' || 'N' ?};
         _TAB.LP:=R.LP;
         _TAB.add()
      ?}
   !};
   R.cntx_pop()
?};

params_set(
   'TAB',_TAB,
   'ws',_ws
);

_TAB.select();
_TAB.index(_ndx);
_codes:='';
{? _TAB.first()
|| {!
   |? {? _TAB.KL='T'
      || _codes+=','+$_TAB.RN
      ?};
      _TAB.next()
   !}
?};
{? +_codes
|| _codes+=','
?};

exec('max_code','profile',_codes,_c,1)


\zasw_pob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [12.30]
:: OPIS: Wczytuje ustawienia dla zaświadczeń: sąd pracy i komisja pojednawcza.
::   WY: wskazanie na tablicę zawierającą informacje o sądzie pracy i siedzibie komisji pojednawczej.
:: UWAGA! Poprawność argumentów wywołania nie jest weryfikowana.
::----------------------------------------------------------------------------------------------------------------------
_buf:=obj_new('c','sad','komisja','nipregon');
_buf.c:=obj_new('plik','sekcja','sad','komisja','nipregon');
_buf.c.plik:='staledod.imp';
_buf.c.sekcja:='Zaświadczenia';
_buf.c.sad:='Sąd Pracy';
_buf.c.komisja:='Zaświadczenia';
_buf.c.nipregon:='NIP czy REGON';

_buf.sad:=exec('get','#profile',_buf.c.plik,_buf.c.sekcja,_buf.c.sad);
_buf.komisja:=exec('get','#profile',_buf.c.plik,_buf.c.sekcja,_buf.c.komisja);
_buf.nipregon:=exec('get','#profile',_buf.c.plik,_buf.c.sekcja,_buf.c.nipregon);

_buf


\zasw_zap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GS [12.30]
:: OPIS: Zapisuje ustawienia dla zaświadczeń: sąd pracy i komisja pojednawcza.
::   WE: _a - wskazanie na tablicę zawierającą informacje o sądzie pracy i siedzibie komisji pojednawczej.
:: UWAGA! Poprawność argumentów wywołania nie jest weryfikowana.
::----------------------------------------------------------------------------------------------------------------------
exec('set','#profile',_a.c.plik,_a.c.sekcja,_a.c.sad,_a.sad);
exec('set','#profile',_a.c.plik,_a.c.sekcja,_a.c.komisja,_a.komisja);
exec('set','#profile',_a.c.plik,_a.c.sekcja,_a.c.nipregon,_a.nipregon)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 bc6d5ccb417951f0411bd741f0da89d4b9c3b21b4c3795cdea20e28302a07f5e98e9ea7f09ba63a76e9066012ea343c17223c6825582a41fa33c1e5a67f4c27c5062d9c7e8b1584f169ef864fb0cbcdb1b409903df3a66cd209fb782eb1abed4d8caa4f576ee9fc3c94fe62fc1834b96fada455d34f3387b3a99e7501b1c1a7b

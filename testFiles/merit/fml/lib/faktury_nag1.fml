:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: faktury_nag1.fml
:: Utworzony: 30.07.2020
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa dokumentów sprzedaży i zakupu - nagłówki dokumentów
::======================================================================================================================


\faks_kzf_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: generuje liste faktur do korekty
::  OLD: \faks_kzf_gen/faktury1.fml
::  OLD: \faks_kzf_gen/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('czy_z_ok','okresy',{? BEER.SZ='S' || 2 || 3 ?},,FAKS.AR,FAKS.AM) || return ?};

_kz:=__faks_kzf_env.KZ;
_kz_uid:=__faks_kzf_env.KZ_UID;

:: sprawdzenie czy podano procent lub kwotę rabatu
{? FAKS.RAB=0 & FAKS.RABK=0
||
   FUN.info('Nie podano procentu lub kwoty rabatu.\nGenerowanie listy niedostępne.'@);
   return()
?};

FAKS_KZF.cntx_psh; OKR.cntx_psh;
FAKS_KZF.index('KZ_UID');
FAKS_KZF.prefix(_kz_uid);
_jest:=FAKS_KZF.first;

{? {? _jest
   || ~FUN.ask('Uzupełnić listę faktur do skorygowania?'@)
   || ~FUN.ask('Utworzyć  listę faktur do skorygowania?'@)
   ?}
|| FAKS_KZF.cntx_pop; OKR.cntx_pop();
   return
?};

_licz:=0;

OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first
||
   _kh:=FAKS.KH;

   _ref:=FAKS.ref;
   _wal:=FAKS.WAL;
   _nwal:=FAKS.NWAL;
   _od:=FAKS.KZ_OD;
   _do:=FAKS.KZ_DO;
   _fis:=FAKS.T().FIS;
   _spp:=FAKS.T().SPP;
   _vatzpoz:=FAKS.T().VATZPOZ;
   _ue:=FAKS.T().UE;
   _ndvat:=FAKS.NDVAT;
   _kraj_vat:=FAKS.KRAJ_VAT;
   _proc:=FAKS.PROC;

   FAKS.cntx_psh;
   {!
   |?
      FAKS.use('faktu'+ST.ODDZ+($OKR.ROK+2));
      FAKS.index('FAK_KH'); FAKS.prefix(BEER.SZ,FAKS.KH().KOD,OKR.ROK);
      {? FAKS.first
      ||
         {!
         |?
            {? FAKS.STAT_REJ='T'
               & FAKS.KOR=''
               & FAKS.WHERE<>'L' & FAKS.WHERE<>'K'
               & FAKS.WAL=_wal
               & FAKS.NWAL=_nwal
               & _od<=FAKS.DW & FAKS.DW<=_do
               & FAKS.T().FIS=_fis
               & FAKS.T().SPP=_spp
               & FAKS.T().VATZPOZ=_vatzpoz
               & FAKS.T().UE=_ue
               & FAKS.NDVAT=_ndvat
               & FAKS.KRAJ_VAT=_kraj_vat
               & FAKS.PROC=_proc
            ||
               {? ~FAKS_KZF.find_tab(,'FAKS',,'=',_ref,'FAK_DW',,'=',FAKS.DW,'FAK_SYM',,'=',FAKS.SYM)
                     &
                  exec('czy_fak2kz','faktury_nag1',_kz_uid,FAKS.ref())
               ||
                  FAKS_KZF.blank;
                  FAKS_KZF.FAKS:=_ref;
                  FAKS_KZF.FAK:=$FAKS.ref;
                  FAKS_KZF.FAKS_UID:=_kz_uid;
                  FAKS_KZF.FAK_DW:=FAKS.DW;
                  FAKS_KZF.FAK_SYM:=FAKS.SYM;
                  FAKS_KZF.FAK_TYP:=FAKS.T;
                  {? FAKS_KZF.add || _licz+=1 ?}
               ?}
            ?};
            FAKS.next
         !}
      ?};
      OKR.next
   !};
:: uwzględnienie korekt nagłówkowych
   _Kor_nag:=sql('
      select
         FAKS.REFERENCE REF,
         FAKS.WAL WAL,
         FAKS.NWAL NWAL,
         FAKS.NETTO NETTO,
         FAKS.NETW NETW,
         FAKS.FKSQL FKSQL,
         FAKS.ONUM ONUM
      from
         @FAKS
         join @FAKS as KOR_NAG using(FAKS.KZ,KOR_NAG.REFERENCE)
         join TYPYSP FAKS_T using (FAKS.T,FAKS_T.REFERENCE)
      where
         KOR_NAG.WHERE=''N''
         and FAKS.KH=:_a
         and FAKS.WAL=:_b
         and FAKS.NWAL=:_c
         and FAKS.DW between to_date(:_d) and to_date(:_e)
         and FAKS_T.FIS='':_f''
         and FAKS_T.SPP='':_g''
         and FAKS_T.VATZPOZ='':_h''
         and FAKS_T.UE='':_i''
         and FAKS_T.NDVAT='':_j''
      order by
         FKSQL, ONUM
   ',
      _kh,_wal,_nwal,_od,_do,_fis,_spp,_vatzpoz,_ue,_ndvat
   );
   _loop:=_Kor_nag.first();
   {!
   |? _loop
   |!
      {? {? _wal<>_nwal || _Kor_nag.NETW<0 || _Kor_nag.NETTO<0 ?}
      ||
::       usuwamy fakturę
         FAKS_KZF.cntx_psh();
         FAKS_KZF.index('FAK');
         FAKS_KZF.prefix(_Kor_nag.FKSQL,_Kor_nag.FKSQL);
         _loop:=FAKS_KZF.first();
         {!
         |? _loop
         |!
            {? FAKS_KZF.FAKS_UID=_kz_uid || FAKS_KZF.del; _licz-=1 ?};
            _loop:=FAKS_KZF.next()
         !};
         FAKS_KZF.cntx_pop()
      ||
::       dopisujemy fakturę
         _dw1:=exec('FindAndGet','#table',FAKS,_Kor_nag.FKSQL,,"DW",date(0,0,0));
         _sym1:=exec('FindAndGet','#table',FAKS,_Kor_nag.FKSQL,,"SYM",'');
         _t1:=exec('FindAndGet','#table',FAKS,_Kor_nag.FKSQL,,"T",null());
         _ref1:=exec('FindAndGet','#table',FAKS,_Kor_nag.FKSQL,,,null());
         {? ~FAKS_KZF.find_tab(,'FAKS',,'=',_ref,'FAK_DW',,'=',FAKS.DW,'FAK_SYM',,'=',FAKS.SYM)
               &
            exec('czy_fak2kz','faktury_nag1',_kz_uid,_ref1)
         ||
            FAKS_KZF.blank;
            FAKS_KZF.FAKS:=_ref;
            FAKS_KZF.FAK:=$_ref1;
            FAKS_KZF.FAKS_UID:=_kz_uid;
            FAKS_KZF.FAK_DW:=_dw1;
            FAKS_KZF.FAK_SYM:=_sym1;
            FAKS_KZF.FAK_TYP:=_t1;
            {? FAKS_KZF.add || _licz+=1 ?}
         ?}
      ?};
      _loop:=FAKS.next()
   !};
   FAKS.cntx_pop;
:: aktualizacja wartości do rabatu
   exec('faks_kzr_war2rab_licz','faktury_nag1');
:: tworzenie tymczasowej definicji rabatu
   exec('faks_kzr_copy','faktury_nag1',FAKS.ref(),'N',0)
?};

FAKS_KZF.cntx_pop; OKR.cntx_pop();
FUN.info('Ilość dopisanych pozycji: %1.'@[form(_licz,,,'99')])


\faks_kzf_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: korygowanie faktur
::   WE: _a - FAKS.ref() korekty zbiorczej
::  OLD: \faks_kzf_kor/faktury1.fml
::  OLD: \faks_kzf_kor/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__kz_buf','__kz_t');
_env:=__faks_kzf_env;
_kz:=_env.KZ;
_kz_uid:=_env.KZ_UID;
_kz_buf:=_env.KZ_BUF;
:: __kz_buf - zmienna wykorzystywana w numeracji
__kz_buf:=_kz_buf;
_kz_buf.save();
_kz_w2r:=_env.KZ_W2R;

exec('ini_kom','#message','Wynik tworzenia korekt'@);
ROKMC.AR:=ST.AR;

:: korekta danych nagłówkowych
_kor_nag:=FAKS.WHERE='N';

_continue:=1;

:: sprawdzenie czy podano procent lub kwotę rabatu
{? FAKS.WHERE<>'N' & FAKS.RAB=0 & FAKS.RABK=0
||
   FUN.info('Nie podano procentu lub kwoty rabatu.\nKorygowanie niedostępne.'@);
   _continue:=0
?};
:: sprawdzenie czy kwota rabatu nie jest równa lub większa od wartości rabatowanej
{? _continue>0
||
   FAKS_KZR.cntx_psh();
   FAKS_KZR.index('UNIK');
   FAKS_KZR.prefix(_kz_uid,'N');
   _loop:=FAKS_KZR.first();
:: _kor_rabk=1 - korekta od kwoty rabatu
   _kor_rabk:=_loop;
   {!
   |? _loop
   |!
      _rabk:=form(FAKS_KZR.RABK,,2);
      _war2rab:=form(FAKS_KZR.WAR2RAB,,2);
      _opis:=FAKS_KZR.OPIS;
      {? FAKS_KZR.RABK=0
      ||
         _continue:=0;
         FUN.info('Nie podano kwoty rabatu dla %1.\nKorygowanie niedostępne.'@[_opis])
      ?};
      {? _continue>0 & FAKS_KZR.RABK>=FAKS_KZR.WAR2RAB
      ||
         _continue:=0;
         {? FAKS_KZR.RABK=FAKS_KZR.WAR2RAB
         || FUN.info('Kwota rabatu (%1) jest równa wartości do rabatu (%2) dla %3.\nKorygowanie niedostępne.'@[
               _rabk,_war2rab,_opis])
         || FUN.info('Kwota rabatu (%1) jest większa od wartości do rabatu (%2) dla %3.\nKorygowanie niedostępne.'@[
               _rabk,_war2rab,_opis])
         ?}
      ?};
      _loop:=_continue & FAKS_KZR.next()
   !};
   FAKS_KZR.cntx_pop()
?};
{? ~_continue || return() ?};

:: wypełnienie _kz_w2r
_loop:=_kz_w2r.first();
{!
|? _loop
|!
   _loop:=_kz_w2r.del()
!};
:: aktualizacja wartości do rabatu
exec('faks_kzr_war2rab_licz','faktury_nag1');

__kz_t:=null;
FAKS.cntx_psh;
FAKS_KZF.cntx_psh;
FAKS_KZF.index('KZ_UID');
FAKS_KZF.prefix(_kz_uid);
{? FAKS_KZF.first
||
   {!
   |?
      {? FAKS_KZF.KOR<>''
      ||
         FAKS.cntx_psh;
         FAKS.use(8+FAKS_KZF.KOR);
         __kz_t:=exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"FAKS.T",null());
         FAKS.cntx_pop
      ?};
      __kz_t=null & FAKS_KZF.next
   !};
   {? __kz_t=null
   ||
      _where:=
         'UE=\''+FAKS.T().UE+'\' and KOR=\'T\' and ZAL=\'N\' and HIS=\'N\' and FIS=\''+FAKS.T().FIS+'\'
         and ZAK=\''+{? BEER.SZ='Z' || 'T' || 'N' ?}+'\''+
         ' and SPP=\''+FAKS.T().SPP+'\' and SPPK=\''+FAKS.T().SPPK+'\' and VATZPOZ=\''+FAKS.T().VATZPOZ+'\'';
      __kz_t:=exec('tsp_upr','typysp',_where,,,,2)
   ?}
?};
_m2kor:=~~;
{? ~_kor_nag & __kz_t & FAKS_KZF.first()
      &
   ~_kor_rabk & _kz_buf.get('RABA')<>'T' & FUN.ask('Czy chcesz wybrać indeksy do korekty?'@)
||
   _Mat:=sql($"
      select distinct
         M.REFERENCE REF
      from
         @FAP
         join @FAKS using (FAP.FAKS,FAKS.REFERENCE)
         join FAKS_KZF using (FAKS_KZF.FAK,FAKS.REFERENCE)
         join M using (FAP.M,M.REFERENCE)
         join MGR using (M.MGR,MGR.REFERENCE)
      where
         FAKS_KZF.FAKS=:_a
         and FAKS_KZF.KOR=''
      ",FAKS_KZF.FAKS);

   _Tab:=tab_tmp(2
      ,'RODZ',    'STRING[1]',   'Materiał/Usługa'
      ,'KTM',     'STRING[50]',  'Indeks'
      ,'N',       'STRING[100]', 'Nazwa'
      ,'MGR_KOD', 'STRING[8]',   'Kod grupy'
      ,'MGR_NAZ', 'STRING[30]',  'Nazwa podgrupy'
      ,'SEL',     'STRING[1]',   'Wybrano'
      ,'REF',     'STRING[16]',  '$M.ref()');

   _ff:="
      _mat:=obj_new('RODZ','KTM','N','MGR_KOD','MGR_NAZ');
      _mat.RODZ:=M.RODZ;
      _mat.KTM:=M.KTM;
      _mat.N:=M.N;
      @.MGR.cntx_psh();
      _mat.MGR_KOD:=M.MGR().KOD;
      _mat.MGR_NAZ:=@.MGR.NAZ;
      @.MGR.cntx_pop();
      _mat
   ";
   _loop:=_Mat.first();
   {!
   |? _loop
   |!
      _mat:=exec('FindAndGet','#table',M,_Mat.REF,,_ff,~~);
      {? type_of(_mat)<>type_of(~~)
      ||
         _Tab.blank();
         _Tab.REF:=_Mat.REF;
         _Tab.SEL:='N';
         _Tab.KTM:=_mat.KTM;
         _Tab.N:=_mat.N;
         _Tab.MGR_KOD:=_mat.MGR_KOD;
         _Tab.MGR_NAZ:=_mat.MGR_NAZ;
         _Tab.add()
      ?};
      obj_del(_mat);
      _loop:=_Mat.next()
   !};

   _wer:=_Tab.mk_sel('Indeksy do korekty'@,,,'m2kor',,,,,'U');
   _Tab.win_fld(_wer,,'SEL',,,,,,'Wybrano'@,,'Czy wybrano [T/N]?'@,2,,"'T'","'N'");
   _Tab.win_fld(_wer,,'RODZ',,,,,,'Materiał/Usługa'@,,'Materiał/Usługa [T/U]'@);
   _Tab.win_fld(_wer,,'KTM',,,20,,,'Indeks'@);
   _Tab.win_fld(_wer,,'N',,,30,,,'Nazwa'@);


   _ff:="_Tab:=cur_tab(); _Tab.SEL:={? _Tab.SEL='T' || 'N' || 'T' ?}; _Tab.put()";
   _Tab.win_act(_wer,,'Formuła','&Wybierz'@@,,,_ff,,1,1,,,'W');
   _Tab.win_btn(_wer,'text='+'&Wybierz'@+',panel=right,align=begin','menu:W',,,,,,'noempty');

   _ff:="sel_exit()";
   _Tab.win_act(_wer,,'Formuła','&Akceptuj'@@,,,_ff,,,,,,'A');
   _Tab.win_btn(_wer,'text='+'&Akceptuj'@+',btn_label_align=center,panel=bottom,align=end','menu:A',,,,,,'noempty');

   _Tab.win_btn(_wer,'text='+'A&nuluj'@+',btn_label_align=center,panel=bottom,align=end','key:Esc');

   _Tab.win_sel(_wer);
   {? _Tab.select()
   || _Tab.index(_Tab.ndx_tmp(,,'SEL',,,'REF',,));
      _Tab.prefix('N');
      {? _Tab.first()
      || _Tab.prefix('T');
         {? _Tab.first()
         || _m2kor:=_Tab
         || __kz_t:=0
         ?}
      ?}
   || __kz_t:=0
   ?}
?};
{? __kz_t & FAKS_KZF.first()
||
   FAKS.cntx_psh();
   _st_ar:=ST.AR;
   _st_am:=ST.AM;
   ST.AR:=FAKS.AR;
   ST.AM:=FAKS.AM;
   _oddz:=FAKS.ODDZ;
   _kor_iter:=_kor_nag;
   _memo:={? _kor_nag || FAKS_KZF.FAKS().SYM; FAKS.memo_get('r','UWAGI') || '' ?};
   {!
   |?
      {? FAKS_KZF.KOR=''
      ||
         exec('open','open_tab',_oddz,8+FAKS_KZF.FAK+2);
         FAKS.prefix;
         {? FAKS.seek(FAKS_KZF.FAK)
         ||
            ROKMC.AR:=FAKS.AR;
            FKOR.WHAT:={? (FAKS.name()+2)='hs' || 'H' || 'F' ?};
            exec('koryguj','faktury_nag',,0,'',,,_m2kor,_kor_iter,0);
            {? _kor_iter=2 & FAKS_KZF.KOR<>''
            || FAKS.cntx_psh();
               FAKS.use(8+FAKS_KZF.KOR);
               FAKS.prefix();
               {? FAKS.seek(FAKS_KZF.KOR)
               || FAKS.memo_set(_memo,'UWAGI');
                  FAKS.memo_put()
               ?};
               FAKS.cntx_pop()
            ?};
            {? _kor_iter || _kor_iter+=1 ?};
            {? exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"FAKS.STAT_REJ",'')='N'
            || _fakskzf:=FAKS_KZF.ref();
               exec('faks_kzf_kus','faktury_nag',0,0,_kz);
               FAKS_KZF.seek(_fakskzf)
            ?}
         ?}
      ||
         {? _kor_iter
         ||
            exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"
               FAKS.AKC:='T';
               FAKS.KZ_AKC:='N';
               exec('zest','faktury_nag');
               FAKS.put()
            ",'')
         ?};
         _kgr:=FAKS_KZF.FAK_SYM;
         __kom.set_root(_kgr);
         exec('add_kom','#message','Korekta %1 jest już wystawiona.'@ [FAKS_KZF.KOR_SYM],7,_kgr)
      ?};
      FAKS_KZF.next
   !};
   {? _kor_iter & FAKS_KZF.last()
   ||
      exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"
         FAKS.AKC:='N';
         FAKS.KZ_AKC:='N';
         exec('zest','faktury_nag');
         FAKS.put()
      ",'')
   ?};
   ST.AR:=_st_ar;
   ST.AM:=_st_am;
   FAKS.cntx_pop();
   {? _kor_nag
:: korekta danych nagłówkowych
   ||
      _powkor:=FAKS.POWKOR;
      FAP.cntx_psh();
::    ustalanie wartości korekt
      _warpoz2kor:=tab_tmp(1
         ,'POZ'      ,'INTEGER'  ,'POZ'
         ,'IL'       ,'REAL'     ,'IL'
         ,'WN'       ,'REAL'     ,'WN'
         ,'WV'       ,'REAL'     ,'WV'
         ,'WB'       ,'REAL'     ,'WB'
         ,'WWAL_P'   ,'REAL'     ,'WWAL_P'
         ,'VWAL_P'   ,'REAL'     ,'VWAL_P'
         ,'BWAL_P'   ,'REAL'     ,'BWAL_P'
         ,'WWAL'     ,'REAL'     ,'WWAL'
         ,'VWAL'     ,'REAL'     ,'VWAL'
         ,'BWAL'     ,'REAL'     ,'BWAL'
         ,'IL2'      ,'REAL'     ,'IL2'
         ,'MASAN'    ,'REAL'     ,'MASAN'
         ,'ILUJM'    ,'REAL'     ,'ILUJM'
         ,'WF'       ,'REAL'     ,'WF'
         ,'WS'       ,'REAL'     ,'WS');
      _loop:=FAKS_KZF.first();
      {!
      |? _loop
      |!
         {? FAKS_KZF.KOR<>''
         ||
            _kor_cz:=exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR);
            exec('open','open_tab',_oddz,8+$(_kor_cz)+2);
            FAKS.prefix();
            {? FAKS.seek(_kor_cz)
            ||
               FAP.index('FAP');
               FAP.prefix(_kor_cz);
               exec('kor_zwra','faktury_poz',_warpoz2kor,_powkor);
:: wymagany split payment
               {? exec('sp_active','faktury_wspolne',FAKS.DW)
               ||
                  _group:=0;
                  _c:=0;
                  _komroot:=
                  _komid:=FAKS_KZF.FAK_SYM;
                  _komroot:="
                     {? ~__sect_beg || __kom.sect_beg(_a); __sect_beg:=1 ?};
                     {? _b | __kom.find_msg(_a) || __kom.set_root(_a) || __kom.sect_beg(_a) ?}
                  ";

::       wtyczka wdrożeniowa - Formuła na podzieloną płatność
                  _sp:=Plugin.run('FAKS_SP_001',2,_group,_c,_komroot,_komid);
                  {? _sp=-1
                  ||
::          działanie domyślne
::          nie zmieniamy wyniku bo
::          w tym miejscu nie można już zablokować zakończenia rejestracji dokumentu
                     exec('sp_chk','faktury_wspolne',2,_group,_c,_komroot,_komid)

                  |? _sp=0
                  ||
::          wtyczka wdrożeniowa powinna zablokować zakończenie rejestracji dokumentu ale
::          w tym miejscu nie można już zablokować zakończenia rejestracji dokumentu
                     ~~
                  ?}
               ?}
            ?}
         ?};
         _loop:=FAKS_KZF.next()
      !};
      FAP.cntx_pop()
   ?}
?};
FAKS.cntx_pop;
FAKS_KZF.cntx_pop;

exec('open','open_tab',ST.ODDZ,$ST.AR+2);

:: aktualizacja:
:: - FAKS, FAKSV korekty zbiorczej
:: - FAKS_KZF na podstawie korekt częściowych
exec('sumfakkzf','faktury_wspolne',$_kz);
:: tworzenie tymczasowej definicji rabatu
exec('faks_kzr_copy','faktury_nag1',FAKS.ref(),'N',0);

exec('end_kom','#message');

VAR_DEL.delete('__kz_buf','__kz_t')


\faks_kzr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekta zbiorcza - rabat kwotowy
::   WE: [_a] - 0/1 - akcje edycyjne
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: DRO_TODO_AWI: (KZ):   : Zapamiętywać wybrane indeksy do korekty zbiorczej w przypadku rabatu procentowego
:: DRO_TODO_AWI: (KZ):   : Dodać wtyczkę wdrożeniową pozwalającą:
:: DRO_TODO_AWI: (KZ):   : - dopisywać automatycznie pozycje do rabatu kwotowego
:: DRO_TODO_AWI: (KZ):   : - wyliczać wartość do rabatu
:: DRO_TODO_AWI: (KZ):   : - wyznaczać uidref kwalifikującego rabat kwotowy
_edit:=0; {? var_pres('_a')=type_of(0) || _edit:=_a ?};
_wg_cb:=exec('FindAndGet','#table',TYPYSP,FAKS.T,,"TYPYSP.FIS='T'",0);
_wer:=FAKS_KZR.mk_sel('Definicja kwoty rabatu'@,,,{? _wg_cb || 'faks_kzr_cb' || 'faks_kzr_cn' ?},,,,,'U');
FAKS_KZR.win_fld(_wer,,'RODZ',,,,,1,'Rodzaj'@);
FAKS_KZR.win_fld(_wer,,'OPIS',,,30,,1,'Opis'@);
FAKS_KZR.win_fld(_wer,FAKS,'WAL','KOD',,,,1,'Waluta'@);
{? _wg_cb
||
   FAKS_KZR.win_fld(_wer,,'RABK',,,,,,'Kwota rabatu brutto'@);
   FAKS_KZR.win_fld(_wer,,'WAR_RABK',,,,,1,'Naliczona kwota rabatu brutto'@);
   FAKS_KZR.win_fld(_wer,,'WAR_RAB',,,,,1,'Naliczony \% rabatu'@);
   FAKS_KZR.win_fld(_wer,,'WAR2RAB',,,,,1,'Wartość do rabatu brutto'@)
||
   FAKS_KZR.win_fld(_wer,,'RABK',,,,,,'Kwota rabatu netto'@);
   FAKS_KZR.win_fld(_wer,,'WAR_RABK',,,,,1,'Naliczona kwota rabatu netto'@);
   FAKS_KZR.win_fld(_wer,,'WAR_RAB',,,,,1,'Naliczony \% rabatu'@);
   FAKS_KZR.win_fld(_wer,,'WAR2RAB',,,,,1,'Wartość do rabatu netto'@)
?};
{? _edit & exec('kznf2_czy_ed','faktury_nag')
||
   _fb:="exec('faks_kzr_dolacz','faktury_nag1')";
   FAKS_KZR.win_act(_wer,1,'Formuła','Dołącz'@@,,,_fb,,1);
   FAKS_KZR.win_act(_wer,,'Formuła','Dołącz'@@,,,_fb)
?};
{? FAKS.STAT_REJ='N'
||
   _fb:="{? FAKS_KZR.WAR_RABK=0 || 1 || FUN.info('Naliczono kwotę rabatu.\nPoprawa pozycji niedostępna.'@); 0 ?}";
   FAKS_KZR.win_act(_wer,,'Popraw',,,,_fb,,1);
   _fb:="params_exec('faks_kzr_usun','faktury_nag1')";
   FAKS_KZR.win_act(_wer,,'Formuła','Usuń'@@,,,_fb,,,1,_fb)
?};
_fb:="exec('faks_kzr_rekord','faktury_nag1',_a)";
_fa:="exec('faks_kzr_rekchk','faktury_nag1')";
FAKS_KZR.win_act(_wer,,'Rekord',,,,_fb,_fa);
_cfg:=',btn_label_align=center,panel=bottom,align=end';
FAKS_KZR.win_btn(_wer,'text='+'Zamknij&'@+_cfg,'key:Esc');

FAKS_KZR.win_sel(_wer);
::
_kz_uid:=exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",'');
_faks_kzr_tmp:=
   {? _edit=0 || 'N' || exec('FindAndGet','#table',FAKS,_kz_uid,,"{? FAKS.STAT_REJ='N' || 'T' || 'N' ?}",'T') ?};
FAKS_KZR.index('VIEW');
FAKS_KZR.prefix(_kz_uid,_faks_kzr_tmp);
params_set('edit',_edit);
FAKS_KZR.select()


\faks_rabk_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekta zbiorcza - rabat kwotowy - przycisk
::   WE: _a - 0/1 wołane z przycisku
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_btn_press:={? var_pres('_a')=type_of(0) || _a || 1 ?};
{? _btn_press || __RABK_BTN:=1 ?};
exec('faks_kzr','faktury_nag1',1);
_faks_kzr_suma:=exec('faks_kzr_suma','faktury_nag1',FAKS.ref(),'T');
_il_poz:=_faks_kzr_suma.IL_POZ;
_faks_kzr_rabk:=_faks_kzr_suma.WAR;
FAKS.RABK:=_faks_kzr_rabk


\faks_kzr_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekta zbiorcza - rabat kwotowy - dołącz
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wyn:=obj_new('TAB','NDX1','NDX2','EXIT');
_wyn.EXIT:=1;
_wyn.TAB:=tab_tmp(2
   ,'RODZ','STRING[1]','Rodzaj'@
   ,'KOD','STRING[8]','Kod'@
   ,'NAZ','STRING[30]','Nazwa'@
   ,'MARK','STRING[1]','Wybrano'@
   ,'UIDREF','STRING[48]','UID'@);
_Tab:=_wyn.TAB;
_wyn.NDX1:=_Tab.index('?');
_wyn.NDX2:=_Tab.ndx_tmp(,,'UIDREF',,);
_kz_uid:=exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",'');
FAKS_KZR.cntx_psh();
FAKS_KZR.index('UNIK');
MGR.cntx_psh();
MGR.index('KOD');
MGR.prefix();
_loop:=MGR.first();
{!
|? _loop
|!
   _Tab.blank();
   _Tab.RODZ:=MGR.RODZ;
   _Tab.KOD:=MGR.KOD;
   _Tab.NAZ:=MGR.NAZ;
   _uidref:=exec('FindAndGet','#table',MGR,MGR.ref(),,"MGR.uidref()",'');
   FAKS_KZR.prefix(_kz_uid,'T',_uidref,);
   _Tab.MARK:={? FAKS_KZR.first() || 'T' || 'N' ?};
   _Tab.UIDREF:=_uidref;
   _loop:=_Tab.add() & MGR.next()
!};
MGR.cntx_pop();
FAKS_KZR.cntx_pop();
{? ~_Tab.first()
||
   FUN.info('Brak grup materiałowych i usługowych.'@)
||
   _tit:='Grupy'@;
   _wer:=_Tab.mk_sel(_tit,,,'faks_kzr_mgr',,,,,'U');
   _Tab.win_fld(_wer,,'MARK',,,10,,,'Wybrano'@,,,2,,"'T'","'N'");
   _Tab.win_fld(_wer,,'RODZ',,,1,,,'Rodzaj'@);
   _Tab.win_fld(_wer,,'KOD',,,8,,,'Kod'@);
   _Tab.win_fld(_wer,,'NAZ',,,60,,,'Nazwa'@);
   _fb:="
      _Tab:=cur_tab();
      _Tab.MARK:='T';
      _Tab.put()
   ";
   _Tab.win_act(_wer,,'Formuła','Wybierz'@@,,,_fb,,1,1);
   _Tab.win_btn(_wer,'text=%1, panel=right'['Wybierz'@@],'menu:W');
   _fb:="
      _Tab:=cur_tab();
      _Tab.MARK:='N';
      _Tab.put()
   ";
   _Tab.win_act(_wer,,'Formuła','Odbierz'@@,,,_fb,,,1);
   _Tab.win_btn(_wer,'text=%1, panel=right'['Odbierz'@@],'menu:O');

   _fb:="
      {? FUN.ask('Czy zatwierdzić wybór?'@)
      ||
         sel_exit()
      ?}
   ";
   _Tab.win_act(_wer,,'Formuła','Akceptuj'@@,,,_fb);
   _fb:="
      {? _a
      ||
         _Tab:=cur_tab();
         _win_sel:=cur_win();
         _default:={? _Tab.sel_size()=0 & _Tab.MARK='T' || 'O' || 'W' ?};
         _Tab.actions(_win_sel,,_default,1)
      ?}
   ";
   _Tab.win_act(_wer,,'Rekord',,,,_fb);

   _Tab.win_btn(_wer,'text=%1, panel=bottom'['&Akceptuj'@],'menu:A');
   _Tab.win_btn(_wer,'text=%1, panel=bottom'['A&nuluj'@@],'key:Esc');
   _Tab.win_sel(_wer);
   {? _Tab.select()
   ||
      _Tab.index(_wyn.NDX2);
      _Tab.prefix();
      FAKS_KZR.cntx_psh();
      FAKS_KZR.index('UNIK');
      FAKS_KZR.prefix(_kz_uid,'T');
      _loop:=FAKS_KZR.first();
      {!
      |? _loop
      |!
         _loop:={? _Tab.find_key(FAKS_KZR.UIDREF,) & _Tab.MARK='T' || FAKS_KZR.next() || FAKS_KZR.del(1,1)=2 ?}
      !};
      _loop:=_Tab.first();
      {!
      |? _loop
      |!
         {? _Tab.MARK='T' & ~FAKS_KZR.find_key(_Tab.UIDREF,)
         ||
            FAKS_KZR.blank();
            FAKS_KZR.FAKS_UID:=_kz_uid;
            FAKS_KZR.RODZ:='Grupa';
            FAKS_KZR.UIDREF:=_Tab.UIDREF;
            FAKS_KZR.OPIS:='%1 - %2'[ _Tab.KOD, _Tab.NAZ];
            FAKS_KZR.add()
         ?};
         _loop:=_Tab.next()
      !};
      FAKS_KZR.cntx_pop();
      _wyn.EXIT:=0
   ?}
?};
MG.f_clear();
_wyn


\faks_kzr_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekta zbiorcza - rabat kwotowy - usuń
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_edit:=params_get().edit;
_Sel:=FAKS_KZR.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#FAKS_KZR.ref(); _Sel.add() ?};

{? _Sel.first() & _Sel.next()
||
   {? ~FUN.ask('Czy usunąć zaznaczone wiersze?'@) || return(0) ?}
||
   {? ~FUN.ask('Czy usunąć bieżący wiersz?'@) || return(0) ?}
?};
exec('ini_kom','#message','Usuń definicję kwoty rabatu');
do();
_loop:=_Sel.first();
{!
|? _loop
|!
   {? FAKS_KZR.seek(_Sel.REF,)
   ||
      {? FAKS_KZR.WAR2RAB=0
      ||
         FAKS_KZR.del()
      ||
         exec('add_kom','#message','Definicja ma naliczoną wartość do rabatu. Nie można usunąć defincji.'@,8,
            FAKS_KZR.OPIS)
      ?}
   ?};
   _loop:=_Sel.next()
!};
{? _edit=0
||
   FAKS.RABK:=exec('faks_kzr_suma','faktury_nag1',FAKS.ref,'N').WAR;
   FAKS.put()
?};
end();
exec('end_kom','#message');
FAKS_KZR.sel_adel();
0


\faks_kzr_rekord
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekta zbiorcza - rabat kwotowy - rekord
::   WE: _a - 0/1 - odświeżany ostatni rekord w dziedzinie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_act:={? _a || _act:={? FAKS_KZR.RODZ='~' || 'D' || '' ?};
FAKS_KZR.actions_grayed(FAKS_KZR.win_sel('?'),_act) ?}


\faks_kzr_rekchk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekta zbiorcza - rabat kwotowy - rekord po
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FAKS_KZR.RABK>0
||
   ''
||
   _wg_cb:=exec('FindAndGet','#table',TYPYSP,FAKS.T,,"TYPYSP.FIS='T'",0);
   {? _wg_cb
   ||
      FUN.info('W polu "Kwota rabatu brutto" należy podać wartość większą od zera.'@)
   ||
      FUN.info('W polu "Kwota rabatu netto" należy podać wartość większą od zera.'@)
   ?};
   'RABK'
?}


\faks_kzr_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekta zbiorcza - rabat kwotowy - usuń
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kz_uid:=exec('FindAndGet','#table',FAKS,_a,,"FAKS.uidref()",'');
FAKS_KZR.cntx_psh();
FAKS_KZR.index('UNIK');
FAKS_KZR.prefix(_kz_uid,);
_loop:=FAKS_KZR.first();
{!
|? _loop
|!
   _loop:=FAKS_KZR.del()
!};
FAKS_KZR.cntx_pop()


\faks_kzr_suma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekt zbiorcza - rabat kwotowy - suma
::   WE: _a - FAKS.ref() korekty zbiorczej
::       _b - [T/N] zapis tymczasowy
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_return:=obj_new('IL_POZ','WAR');
_return.IL_POZ:=0;
_return.WAR:=0;
_kz_uid:=exec('FindAndGet','#table',FAKS,_a,,"FAKS.uidref()",'');
FAKS_KZR.cntx_psh();
FAKS_KZR.index('UNIK');
FAKS_KZR.prefix(_kz_uid,_b);
_loop:=FAKS_KZR.first();
{!
|? _loop
|!
   _return.IL_POZ+=1;
   _return.WAR+=FAKS_KZR.RABK;
   _loop:=FAKS_KZR.next()
!};
FAKS_KZR.cntx_pop();
_return


\faks_rabk_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Po redagowaniu FAKS.RABK
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_faks_rabk:=FAKS.RABK;
_faks_kzr_suma:=exec('faks_kzr_suma','faktury_nag1',FAKS.ref(),'T');
_il_poz:=_faks_kzr_suma.IL_POZ;
_faks_kzr_rabk:=_faks_kzr_suma.WAR;
{? var_pres('__RABK_BTN')=type_of(0) & __RABK_BTN=1
||
   &__RABK_BTN
::    wołane po zakończeniu formuły przycisku

|? _il_poz=0
||
   {? FAKS.RABK<0
   ||
      FUN.info('W polu "Kwota rabatu" wartość nie może być mniejsza od zera.'@);
      _result:=0

   |? FAKS.RABK
   ||
::    wołane po redakcji pola
::    wartość rabatu kwotowowego brany z nagłówka korekty zbiorczej
      {? ~FUN.ask('Czy rabat dotyczy wszystkich pozycji?'@)
      ||
         FAKS.RABK:=0;
         {? FUN.ask('Czy chcesz zdefiniować rabat kwotowy?'@)
         ||
            exec('faks_rabk_btn','faktury_nag1',0)
         ?}
      ||
         _kz_uid:=exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",'');
         FAKS_KZR.cntx_psh();
         FAKS_KZR.prefix();
         FAKS_KZR.blank();
         FAKS_KZR.FAKS_UID:=_kz_uid;
         FAKS_KZR.RODZ:='~';
         FAKS_KZR.UIDREF:=_kz_uid;
         FAKS_KZR.OPIS:='Wszystkie pozycje';
         FAKS_KZR.RABK:=_faks_rabk;
         FAKS_KZR.add();
         FAKS_KZR.cntx_pop()
      ?}
   ?}

|? _faks_rabk<>_faks_kzr_rabk
||
:: wartość rabatu kwotowego wynika z definicji
   FAKS.RABK:=_faks_kzr_rabk;
   FUN.info('Kwota rabatu wynika z definicji i została ustalona na wartość %1.'@[form(FAKS.RABK,,2)]);
   _result:=0
?};
{? FAKS.RABK>0 || FAKS.RAB:=0 ?};
exec('set_efld_opt','faktury_nag');
_result


\faks_raba_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Po redagowaniu FAKS.RABA
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.RABA='T'
||
   _kz_uid:=exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",'');
   FAKS_KZR.cntx_psh();
   FAKS_KZR.index('UNIK');
   FAKS_KZR.prefix(_kz_uid,'T');
   _loop:=FAKS_KZR.first();
   {? _loop
   ||
      _loop:=FUN.ask('Zdefiniowano rabat kwotowy.\n'
         'Zaznacznie pola "Anulowanie rabatu" usunie rabat kwotowy.\n\n'
         'Czy usunąć rabat kwotowy?'@);
      {? _loop || FAKS.RAB:=FAKS.RABK:=0 || FAKS.RABA:='N' ?}
   ?};
   {!
   |? _loop
   |!
      _loop:=FAKS_KZR.del()
   !};
   FAKS_KZR.cntx_pop()
?};
exec('set_efld_opt','faktury_nag');
1


\faks_rab_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Po redagowaniu FAKS.RAB
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? exec('spr_rab','ceny') || exec('set_efld_opt','faktury_nag'); 1 ?}


\faks_kzr_rab_uid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: identyfikator kfalifikujący rabat
::   WE: _a - M.ref()
::   WY: '' lub MGR.uidref()
::----------------------------------------------------------------------------------------------------------------------
_result:='';
FAKS_KZR.cntx_psh();
FAKS_KZR.index('UNIK');
FAKS_KZR.prefix(_a,'N',_a,);
{? FAKS_KZR.first() || _result:=_a ?};
FAKS_KZR.cntx_pop();
{? _result=''
||
   _mat:=_b;
:: DRO_TODO_AWI: (KZ):   : Czy dodać tutaj wtyczkę wdrożeniową? gdzie jeszcze?
   _mgr:=exec('FindAndGet','#table',M,_mat,,"M.MGR",null());
   _result:=exec('FindAndGet','#table',MGR,_mgr,,"MGR.uidref()",'')
?};
_result


\faks_kzr_war2rab_licz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekt zbiorcza - rabat kwotowy - wylicza wartości do rabatu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=__faks_kzf_env;
_kz_uid:=_env.KZ_UID;
FAKS_KZR.cntx_psh();
FAKS_KZR.index('UNIK');
FAKS_KZR.prefix(_kz_uid,'N');
_loop:=FAKS_KZR.first();
{!
|? _loop
|!
:: zerowanie
   FAKS_KZR.WAR2RAB:=0;
   _loop:=FAKS_KZR.put() & FAKS_KZR.next()
!};
{? FAKS_KZR.first()
||
   _loop:=FAKS_KZF.first();
   {!
   |? _loop
   |!
::    faktura
      _faks:=FAKS_KZF.FAK;
      _oddz:=6+_faks+1;
      _rok:=exec('faks_kzr_war2rab_liczp','faktury_nag1',_env,_faks);
      FAKS.cntx_psh();
      OKR.cntx_psh();
      OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      _loop:=OKR.find_ge(_rok);
      {!
      |? _loop
      |!
         FAKS.use((FAKS.name()-3)+_oddz+(form(OKR.ROK,,,'99')+2));
         FAKS.cntx_psh();
         FAKS.index('OREFK');
         FAKS.prefix('S',_faks,);
         {? FAKS.first()
         ||
            {!
            |?
::             korekta
               exec('faks_kzr_war2rab_liczp','faktury_nag1',_env,$FAKS.ref());
               FAKS.next()
            !}
         ?};
         FAKS.cntx_pop();
         _loop:=OKR.next()
      !};
      OKR.cntx_pop();
      FAKS.cntx_pop();
      _loop:=FAKS_KZF.next()
   !}
?};
FAKS_KZR.cntx_pop()


\faks_kzr_war2rab_liczp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekt zbiorcza - rabat kwotowy - wylicza wartości do rabatu - pozycje
::   WE: _a - środowisko korekty zbiorczej
::       _b - $FAKS.ref() - faktury lub korekty
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_kz_uid:=_env.KZ_UID;
_faks:=_b;
exec('FindAndGet','#table',FAKS,_faks,,"
   _env:=_b;
   _kz:=_env.KZ;
   _kz_uid:=_env.KZ_UID;
   _kz_w2r:=_env.KZ_W2R;
:: korekta zbiorcza według cen netto/brutto
   _wg_cb:=exec('FindAndGet','#table',TYPYSP,FAKS.T,,\"TYPYSP.FIS='T'\",0);
   FAP.cntx_psh();
   FAP.use((FAP.name()-3)+(8+$FAKS.ref()+3));
   FAP.index('FAP');
   FAP.prefix(FAKS.ref());
   _loop:=FAP.first();
   {!
   |? _loop
   |!
::    _rab_uid - identyfikator kwalifikujący rabat
      _rab_uid:=exec('faks_kzr_rab_uid','faktury_nag1',_kz_uid,FAP.M);
      {? FAKS_KZR.find_key(_rab_uid,)
      ||
         _war2rab:={? _wg_cb || FAP.BWAL || FAP.WWAL ?};
         {? FAKS.KZ=$_kz
         ||
            exec('wysw_kor','faktury_poz');
            _war2rab:=-{? _wg_cb || FKOR.BWAL || FKOR.WWAL ?}
         ||
            FAKS_KZR.WAR2RAB+=_war2rab;
            FAKS_KZR.put()
         ?};
         {? _kz_w2r.find_key(_rab_uid)
         ||
            _kz_w2r.WAR2RAB+=_war2rab;
            _kz_w2r.put()
         ||
            _kz_w2r.blank();
            _kz_w2r.UIDREF:=_rab_uid;
            _kz_w2r.WAR2RAB:=_war2rab;
            _kz_w2r.add()
         ?}
      ?};
      _loop:=FAP.next()
   !};
   FAP.cntx_pop();
   FAKS.AR
",date()~1,_env)


\czy_fak2kz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Czy faktura kwalifikuje się do korekty zbiorczej
::   WE: _a - uid korekty zbiorczej
::       _b - FAKS.ref() sprawdzanej faktury
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_kz_uid:=_a;
_faks:=_b;
_result:=exec('FindAndGet','#table',FAKS,_faks,,"
   FAKS.SPOBLRAB='W' & (FAKS.RAB_TYP='P' | FAKS.RAB_TYP='S'  | FAKS.RAB_TYP='K' | FAKS.RAB_TYP='W')
      |
   FAKS.SPOBLRAB='C' & (FAKS.RAB_TYP='P' | FAKS.RAB_TYP='S'  | FAKS.RAB_TYP='K')
",0);
{? _result=0
||
:: korekta wykluczona bo w pozycji nieobsługiwany typ rabatu
   ~~
||
   FAKS_KZR.cntx_psh();
   FAKS_KZR.index('UNIK');
   FAKS_KZR.prefix(_kz_uid,'N');
   {? FAKS_KZR.find_key(_kz_uid,)
   ||
::    wszystkie pozycje podlegają korekcie
      ~~

   |? FAKS_KZR.first()
   ||
::    sprawdzenie pozycji
      _result:=exec('FindAndGet','#table',FAKS,_faks,,"
         _kz_uid:=_b;
         _result:=1;
         FAP.cntx_psh();
         FAP.use((FAP.name()-3)+(ref_name(FAKS.ref())+3));
         FAP.cntx_psh();
         FAP.index('FAP');
         FAP.prefix(FAKS.ref());
         _loop:=FAP.first();
         {!
         |? _loop
         |!
::          _rab_uid - identyfikator kwalifikujący rabat
            _rab_uid:=exec('faks_kzr_rab_uid','faktury_nag1',_kz_uid,FAP.M);
            _result:=FAKS_KZR.find_key(_rab_uid,);
            _loop:=_result=0 & FAP.next()
         !};
         FAP.cntx_pop();
         FAP.cntx_pop();
         _result
      ",,_kz_uid)
   ?};
   FAKS_KZR.cntx_pop()
?};
_result


\faks_kzr_war_rabk_licz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Wartość naliczonego rabatu w korekcie zbiorczej z rabatem kwotowym
::   WE:
::   WY: Wartość naliczonego rabatu
::----------------------------------------------------------------------------------------------------------------------
_env:=__faks_kzf_env;
_kz_uid:=_env.KZ_UID;
FAKS_KZR.cntx_psh();
FAKS_KZR.index('UNIK');
FAKS_KZR.prefix(_kz_uid,'N');
_loop:=FAKS_KZR.first();
{!
|? _loop
|!
   FAKS_KZR.WAR_RABK:=0;
   FAKS_KZR.WAR_RAB:=0;
   _loop:=FAKS_KZR.put() & FAKS_KZR.next()
!};
FAKS_KZF.cntx_psh();
FAKS_KZF.index('KZ_UID');
FAKS_KZF.prefix(_kz_uid);
_loop:=FAKS_KZF.first();
{!
|? _loop
|!
   exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"
      _env:=__faks_kzf_env;
      _kz_uid:=_env.KZ_UID;
      _wg_cb:=exec('FindAndGet','#table',TYPYSP,FAKS.T,,\"TYPYSP.FIS='T'\",0);
      FAP.cntx_psh();
      FAP.index('FAP');
      FAP.prefix(FAKS.ref());
      _loop:=FAP.first();
      {!
      |? _loop
      |!
::       _rab_uid - identyfikator kwalifikujący rabat
         _rab_uid:=exec('faks_kzr_rab_uid','faktury_nag1',_kz_uid,FAP.M);
         {? FAKS_KZR.find_key(_rab_uid,)
         ||
            FAKS_KZR.WAR_RABK-={? _wg_cb || FAP.BWAL || FAP.WWAL ?};
            {? FAKS_KZR.WAR2RAB>0 || FAKS_KZR.WAR_RAB:=(FAKS_KZR.WAR_RABK/FAKS_KZR.WAR2RAB*100)$2 ?};
            FAKS_KZR.put()
         ?};
         _loop:=FAP.next()
      !};
      FAP.cntx_pop()
   ");
   _loop:=FAKS_KZF.next()
!};
FAKS_KZF.cntx_pop();
FAKS_KZR.cntx_pop()


\faks_kzr_copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Kopiuje definicję rabatu kwotowego
::   WE: _a - FAKS.ref() korekty zbiorczej
::       _b - rodzaj kopiowanych pozycji
::       _c - 0/1 - usunąć kopiowane
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kz_uid:=exec('FindAndGet','#table',FAKS,_a,,"FAKS.uidref()",'');
_from:=_b;
_to:={? _from='T' || 'N' || 'T' ?};
_del:=_c;
FAKS_KZR.cntx_psh();
FAKS_KZR.index('UNIK');
FAKS_KZR.prefix(_kz_uid,_to);
_loop:=FAKS_KZR.first();
{!
|? _loop
|!
   _loop:=FAKS_KZR.del()
!};
FAKS_KZR.prefix(_kz_uid,_from);
_loop:=FAKS_KZR.first();
{!
|? _loop
|!
   FAKS_KZR.cntx_psh();
   FAKS_KZR.prefix();
   FAKS_KZR.TMP:=_to;
   FAKS_KZR.add();
   FAKS_KZR.cntx_pop();
   _loop:={? _del || FAKS_KZR.del() || FAKS_KZR.next() ?}
!};
FAKS_KZR.cntx_pop()


\faks_kzr_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Sprawdzenie naliczonego rabatu kwotowego
::   WE: _a - FAKS.ref() - korekty zbiorczej
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kz_uid:=exec('FindAndGet','#table',FAKS,_a,,"FAKS.uidref()",'');
FAKS_KZR.cntx_psh();
FAKS_KZR.index('UNIK');
FAKS_KZR.prefix(_kz_uid,'N');
_rabk:=0;
_war_rabk:=0;
_loop:=FAKS_KZR.first();
{!
|? _loop
|!
   _rabk+=FAKS_KZR.RABK;
   _war_rabk+=FAKS_KZR.WAR_RABK;
   _loop:=FAKS_KZR.next()
!} ;
FAKS_KZR.cntx_pop();
_zaokr:=_rabk-fabs(_war_rabk);
{? _zaokr<>0
||
   FUN.ask('Różnica między wartością rabtu i wartością rabatu naliczonego wynosi %1.\n'
      'Czy kontynuować zakończenie rejestracji?'@[form(_zaokr,,2)])
||
   1
?}


\faks_rabk_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: przed edycją FAKS.RABK
::   WY: czy mozliwa edycja FAKS.RABK
::----------------------------------------------------------------------------------------------------------------------
exec('faks_fld_czy_ed','faktury_nag')


\faks_raba_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: przed edycją FAKS.RABA
::   WY: czy mozliwa edycja FAKS.RABA
::----------------------------------------------------------------------------------------------------------------------
exec('faks_fld_czy_ed','faktury_nag')


\faks_kzf_env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Korekta zbiorcza - środowisko
::   WE: _a - $FAKS.ref() korekty zbiorczej
::   WY:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__faks_kzf_env');
_env:=__faks_kzf_env:=obj_new('KZ','KZ_UID','KZ_BUF','KZ_W2R','BTN');
_env.KZ:=exec('FindAndGet','#table',FAKS,_a,,,null());
_env.KZ_UID:=exec('FindAndGet','#table',FAKS,_a,,"FAKS.uidref()",'');
_env.KZ_BUF:=obj_new(@.CLASS.BUFFER, 'FAKS');
_env.KZ_W2R:=tab_tmp(1
   ,'UIDREF','STRING[48]','uidref'
   ,'WAR2RAB','REAL','Wartość rabatowana');
_env


\faks_kzn_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: akceptacja korekty zbiorczej
::   WE: _a - grupowa akceptacja
::  OLD: \faks_kzn_akc/faktury1.fml
::  OLD: \faks_kzn_akc/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
_group:=_a;

{? ~exec('czy_z_ok','okresy',{? BEER.SZ='S' || 2 || 3 ?},,FAKS.AR,FAKS.AM) || return ?};

{? FAKS.STAT_REJ='Z' || FUN.info('Zakończono już rejestrację dokumentu.'@); return ?};

{? FAKS.STAT_REJ='T' || FUN.info('Dokument jest już zaakceptowany.'@); return ?};

{? FAKS.STAT_REJ='A' || FUN.info('Dokument został anulowany.'@); return ?};

_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref());
{? ~_r_lock || exec('who_rlock_faks','faktury_nag'); return ?};

{? exec('FindInSet','#table','FAKS_KZF','POZ',FAKS.ref())=null
|| FUN.info('Brak dokumentów do korekty.'@);
   exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
   return
?};

_ask:=1;
{? exec('faks_kzr_chk','faktury_nag1',FAKS.ref()) || _ask:=0 || return() ?};

{? _ask & ~FUN.ask('Zakończyć rejestrację dokumentu?'@)
|| exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
   return
?};

KH_DOD.KLIM:=__War_f('KH_DOD','KLIM',FAKS.KH);
_klim:={? KH_DOD.KLIM<>null || KH_DOD.KLIM || FAKS.KH().GRKH().KLIM ?};
{? ~exec('lim_kred','limit_kredyt',FAKS.KH,date(),INFO.NAROD,6,_klim,FAKS.BRUTTO,,'faktu') || return() ?};

exec('ini_kom','#message','Wynik zakończenia rejestracji dokumentu'@);

_ok:=1;

_blp:=exec('blp','faktury_nag',_group);
_ok:=_blp.WYN;
_txt:=_blp.TXT;
{? _txt<>'' || exec('add_kom','#message',_txt,7,FAKS.SYM) ?};

do();
FAKS.AKC:='T';
{? FAKS.T().UE='T' || FAKS.INTRAKC:='T' ?};
FAKS.STAT_REJ:='Z';
exec('zest','faktury_nag');
{? FAKS.WHERE='N' || FAKS.DZ:=FAKS.DW ?};
_faks_sym:=FAKS.SYM;
_kz_uid:=exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",'');

{? _ok & FAKS.put()
||
   exec('faks_kzr_copy','faktury_nag1',FAKS.ref(),'N',0);
   exec('faks_kzr_copy','faktury_nag1',FAKS.ref(),'T',1);
   FAKS_KZF.cntx_psh();
   FAKS_KZF.index('KZ_UID');
   FAKS_KZF.prefix(_kz_uid);
   {? FAKS_KZF.first
   ||
      {!
      |?
         {? FAKS_KZF.KOR=''
         || exec('add_kom','#message','Nie wystawiono korekty do dokumentu.'@,7,FAKS_KZF.FAK_SYM);
            _ok:=0
         |? exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"FAKS.STAT_REJ",'')<>'Z'
         || exec('add_kom','#message','Korekta nr %1 - %2 jest niezaakceptowana.'@ [$FAKS_KZF.KOR_NR, FAKS_KZF.KOR_SYM]
               ,7,FAKS_KZF.FAK_SYM);
            _ok:=0
         ?};
:: wymagany split payment
         {? exec('sp_active','faktury_wspolne',FAKS.DW)
         ||
            _group:=0;
            _c:=1;
            _komid:=_faks_sym;
            _komroot:="{? _b | __kom.find_msg(_a) || __kom.set_root(_a,,,1) || __kom.sect_beg(_a) ?}";
::          wtyczka wdrożeniowa - Formuła na podzieloną płatność
            _sp:=Plugin.run('FAKS_SP_001',3,_group,_c,_komroot,_komid);
            {? _sp=-1
            ||
::             działanie domyślne
               _ok:=0;
               FAKS.cntx_psh();
               FAKS.prefix();
               {? FAKS.seek(FAKS_KZF.KOR)
               ||
                  _ok:=exec('sp_chk','faktury_wspolne',3,_group,_c,_komroot,_komid)
               ?};
               FAKS.cntx_pop()

            |? _sp=0
            ||
::             wtyczka wdrożeniowa blokuje zakończenie rejestracji dokumentu
               _ok:=0
            ?}
         ?};
         FAKS_KZF.next
      !}
   ?};
   {? _ok & FAKS_KZF.first
   ||
      __auto_ask:=-1;
      FAKS.cntx_psh;
      {!
      |?
         exec('open','open_tab',FAKS.ODDZ,8+FAKS_KZF.KOR+2);
         FAKS.prefix;
         {? FAKS.seek(FAKS_KZF.KOR)
         ||
            FAKS.AKC:='T';
            FAKS.KZ_AKC:='T';
            exec('zest','faktury_nag');
            {? FAKS.put() & __auto_ask=1
            ||
               __kom.sect_beg(form(FAKS.NR,,,'99')+' - '+FAKS.SYM);
               __kom.set_root(form(FAKS.NR,,,'99')+' - '+FAKS.SYM);
               __kom.sect_end()
            ?}
         ?};
         FAKS_KZF.next
      !};
      exec('open','open_tab',ST.ODDZ_REF().KOD,$ST.AR+2);
      FAKS.cntx_pop;
:: aktualizacja daty i czasu akceptacji na nagłówku korekty zbiorczej lub nagłókowej
      FAKS.DA:=date();
      FAKS.CA:=time();
      FAKS.put();
      VAR_DEL.delete('__auto_ask')
   ?};
:: zamiana kontrahenta na zaliczce
   _kh1:=_kh2:=null();
   _loop:=_ok & FAKS.WHERE='N' & FAKS_KZF.first();
   {!
   |? _loop
   |!
      {? _kh1
      || _kh2:=exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"FAKS.KH",null());
         {? _kh1=_kh2 || _kh2:=null() ?}
      || _kh1:=exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"FAKS.KH",null())
      ?};
      _loop:=FAKS_KZF.next()
   !};
   FAKS_KZF.cntx_pop();
   {? _kh2
   ||
      FAPOW.cntx_psh();
      FAPOW.index('FAKS_K');
      FAPOW.prefix(FAKS.FKSQL,'N');
      _loop:=FAPOW.first();
      {!
      |? _loop
      |!
         FAPOW.KH:=_kh2;
         _loop:=FAPOW.put() & FAPOW.next()
      !};
      FAPOW.cntx_pop()
   ?};
:: wtycznka wdrożeniowa - dodanie danych dodatkowych, które są umieszczane w wychodzącym dokumencie Businesslink
   {? FAKS.EDOKUMEN='T' || Plugin.run('BL_DOKUMXML_SET_DSP',FAKS.ref()) ?};
   {? ~_ok
   || undo();
      exec('add_kom','#message','Nie zakończono rejestracji dokumentu.'@,7,FAKS.SYM)
   ?}
|| exec('add_kom','#message','Nie zakończono rejestracji dokumentu.'@,7,FAKS.SYM)
?};
end();

exec('end_kom','#message');

exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)


\faks_kzn_wyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: wycofanie akceptacji korekty zbiroczej
::       [_a] - 0-bez pytania; 1-pytać czy wycofać
::  OLD: \faks_kzn_wyc/faktury1.fml
::  OLD: \faks_kzn_wyc/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};

{? ~exec('czy_z_ok','okresy',{? BEER.SZ='S' || 2 || 3 ?},,FAKS.AR,FAKS.AM) || return ?};

{? FAKS.AKC='N' || FUN.info('Korekta zbiorcza jest niezaakceptowana.\nWycofanie akceptacji niemożliwe.'@); return ?};

{? FAKS.ZAK='T' || FUN.info('Korekta zbiorcza jest zaksięgowana.\nWycofanie akceptacji niemożliwe.'@); return ?};

{? FAKS.STAT_REJ='A' || FUN.info('Korekta zbiorcza jest anulowana.\nWycofanie akceptacji niemożliwe.'@); return ?};

_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref());
{? ~_r_lock || exec('who_rlock_faks','faktury_nag'); return ?};

_continue:=1;
{? FAKS.WHERE='N' & exec('f_mdok','faktury_wspolne',2)=1
||
   _continue:=
      FUN.ask('Faktura jest powiązana z dokumentami magazynowymi.'
         '\nWycofanie akceptacji faktury spowoduje usunięcie dokumentów magazynowych.'
         '\nCzy usunąć dokumenty magazynowe?'@);
   {? _continue || _continue:=exec('wycdokmg','magdok_wspolne',1,0) ?}

|? FAKS.WHERE='K' & exec('f_mdok','faktury_wspolne',2)=3
||
   FUN.info('Faktura jest powiązana z dokumentami magazynowymi.'
      '\nNależy usunąć powiązane dokumenty magazynowe przed wycofaniem akceptacji.'@);
   _continue:=0

|? FAKS.INTRA='T'
||
   FUN.info('Dokument uwzględniony w deklaracji Intrastat.'
      '\nNależy usunąć dokument z deklaracji przed wycofaniem akceptacji.'@);
   _continue:=0

|? _a
||
   _continue:=FUN.ask('Wycofać akceptację korekty zbiorczej?'@)
?};

{? _continue=0
||
   exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
   return()
?};

exec('ini_kom','#message','Wynik wycofania akceptacji korekty zbiorczej'@);

:: sprawdzenie czy można wycofać akceptację

{? _continue
||
   do();

   _stat_rej:=FAKS.STAT_REJ;
   _where:=FAKS.WHERE;
   _kz_uid:=exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",'');

   _da:=FAKS.DA;
   _ca:=FAKS.CA;

   FAKS.AKC:='N';
   FAKS.INTRAKC:='N';
   FAKS.STAT_REJ:='N';
   FAKS.DA:=date(0,0,0);
   FAKS.CA:=time(0,0,0);
   exec('zest','faktury_nag');
   {? FAKS.WHERE='N' || FAKS.DZ:=date(0,0,0) ?};
   FAKS.STATKSEF:='';
   {? _continue:=FAKS.put()
   || 1
   || {? _stat_rej='T'
      || exec('add_kom','#message','Nie powiodło się wycofanie akceptacji i zakończenia korekty zbiorczej.'@,7,FAKS.SYM)
      || exec('add_kom','#message','Nie powiodło się wycofanie zakończenia korekty zbiorczej.'@,7,FAKS.SYM)
      ?}
   ?};

   {? _continue
   ||
      _ii:=0;
      _del_autoadd:=exec('del_autoadd','dokum',FAKS.ref(),_a);
      FAKS.get();
      FAKS.CZY_DRUK:={? _del_autoadd=1 || 'X' || 'N' ?};
      FAKS.put();
      FAKS_KZF.cntx_psh();
      FAKS_KZF.index('KZ_UID');
      FAKS_KZF.prefix(_kz_uid);
      {? FAKS_KZF.first()
      ||
         FAKS.cntx_psh();
         {!
         |?
            exec('open','open_tab',FAKS.ODDZ,8+FAKS_KZF.KOR+2);
            FAKS.prefix();
            {? FAKS.seek(FAKS_KZF.KOR)
            ||
               {? FAKS.KORYG='T'
               ||
                  _ii+=1;
                  {? _where='K' | _ii=2
                  || exec('open','open_tab',ST.ODDZ_REF().KOD,$ST.AR+2);
                     undo();
                     FUN.info('Wystawiono dokument korygujący.\nWycofanie akceptacji niemożliwe.'@);
                     _continue:=0
                  ?}
               ||
                  FAKS.AKC:='N';
                  FAKS.KZ_AKC:='N';
                  exec('zest','faktury_nag');
                  FAKS.put()
               ?}
            ?};
            _continue & FAKS_KZF.next()
         !};
         {? _continue || exec('open','open_tab',ST.ODDZ_REF().KOD,$ST.AR+2) ?};
         FAKS.cntx_pop()
      ?};
:: zamiana kontrahenta na zaliczce
      _kh1:=_kh2:=null();
      _loop:=_continue & FAKS.WHERE='N' & FAKS_KZF.first();
      {!
      |? _loop
      |!
         {? _kh1
         || _kh2:=exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"FAKS.KH",null());
            {? _kh1=_kh2 || _kh1:=null() ?}
         || _kh1:=exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"FAKS.KH",null())
         ?};
         _loop:=FAKS_KZF.next()
      !};
      FAKS_KZF.cntx_pop();
      {? _kh1
      ||
         FAPOW.cntx_psh();
         FAPOW.index('FAKS_K');
         FAPOW.prefix(FAKS.FKSQL,'N');
         _loop:=FAPOW.first();
         {!
         |? _loop
         |!
            {? FAPOW.KH=_kh2 & FAPOW.FAKS_END<>''
            ||
               _da1:=exec('FindAndGet','#table',FAKS,FAPOW.FAKS_END,,"FAKS.DA",date(0,0,0));
               _ca1:=exec('FindAndGet','#table',FAKS,FAPOW.FAKS_END,,"FAKS.CA",date(0,0,0));
               {? _da<_da1 | _da=_da1 & _ca<_ca1
               ||
                  exec('add_kom','#message','Rozliczno zaliczki po wystawieniu korekty. '
                     'Nie powiodło się wycofanie zakończenia korekty nagłówkowej.'@,7,FAKS.SYM);
                  undo()
               ?}
            ?};
            FAPOW.KH:=_kh1;
            _loop:=FAPOW.put() & FAPOW.next()
         !};
         FAPOW.cntx_pop()
      ?}
   ?};

   end()

?};

exec('end_kom','#message');

exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)


\faks_kzn_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: dodaje naglowek korekty zbiorczej
::   WE: [_a] - =1-edycja listy faktur
::       [_b] - typ dokumentu
::  OLD: \faks_kzn_dod/faktury1.fml
::  OLD: \faks_kzn_dod/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=0 ?};
{? var_pres('_b')<>type_of('') || _b:='' ?};

{? ~exec('czy_z_ok','okresy',{? BEER.SZ='S' || 2 || 3 ?}) || return ?};
HELP.WHERE:='K';
POMOC.LOCK:='';

_where:="ZAK='"+{? BEER.SZ='Z' || 'T' || 'N' ?}+"' and KOR='Z'"+{? _b='' || "" || " and TYPYSP.T='"+_b+"'" ?};
_typ:=exec('tsp_upr','typysp',_where,,,,2);

{? _typ || FAKS.T:=_typ || return ?};

FAKS.cntx_psh;

POM.TAB:='FAKS';
POM.TYPDOK:=FAKS.T().KOD;
POM.NRT:=0;
_del:=0;

FAKS.prefix();
FAKS.blank(); FAKS.memo_set(,'UWAGI');
FAKS.T:=_typ;
exec('zest','faktury_nag');
{? FAKS.add
|| _ref:=FAKS.ref();
   _r_lock:=exec('r_lock_one','#table',FAKS,_ref);
   {? _r_lock
   || {? FAKS.HIDDEN='N'
         & FAKS.name()+2<>'hs'
         & (FAKS.f_active()=1 | (FAKS.f_active()=3 & FAKS.f_test()))
      || FAKS.f_add()
      ?};
      FAKS.DW:=date;
      {? FAKS.DW~1<>ST.AR | FAKS.DW~2<>ST.AM || FAKS.DW:=date(ST.AR,ST.AM,0) ?};

      _var_fakpop:=exec('var_fakpop','faktury_nag');
      _var_fakpop.RAB:=FAKS.RAB;
      _var_fakpop.KRS:=FAKS.KRS;
      _var_fakpop.NKRS:=FAKS.NKRS;
      _var_fakpop.R_LOCK:=_r_lock;
      _var_fakpop.PO_FIRST:=1;

::    Parmater 'context' wykorzystywany w:
::    - exec('faks_pozycje_red','faktury_nag')
::    - exec('faks_zakoncz_red','faktury_nag')
::    Parametr 'var_fakpop' wykorzystywany w:
::    - exec('faks_pozycje_red','faktury_nag')
::    - exec('faks_zakoncz_red','faktury_nag')
::    - exec('fak_pop_po','faktury_nag',_dod_nag)
      params_set('context',params_get(),'var_fakpop',_var_fakpop);

      exec('faks_win_edit_set','faktury_nag');
      exec('polafaks_get','jpk_log',FAKS.ref(),FAKS);
      _dod_nag:=FAKS.edit("exec('faks_kzn_val','faktury_nag1')");
      {? FAKS.STAT_REJ='N'
      || _dod_nag:=exec('fak_pop_po','faktury_nag',{? _dod_nag || 1 || -1 ?},1);
         {? _dod_nag & _var_fakpop.PO_FIRST=1
         || {? FAKS.f_active() || FAKS.f_rfresh() ?};
            exec('faks_kzf','faktury_nag')
         ?}
      ?};
      exec('r_unlock_one','#table',FAKS,_ref,_r_lock)
   || exec('who_rlock_faks','faktury_nag')
   ?}
?};

FAKS.cntx_pop


\faks_kzn_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: poprawia naglowek korekty zbiorczej
::  OLD: \faks_kzn_pop/faktury1.fml
::  OLD: \faks_kzn_pop/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('czy_z_ok','okresy',{? BEER.SZ='S' || 2 || 3 ?},,FAKS.AR,FAKS.AM) || return ?};

{? FAKS.STAT_REJ='Z' || FUN.info('Zakończono rejestrację dokumentu.\nPoprawienie nagłówka korekty zbiorczej niemożliwe.'@); return ?};

{? FAKS.STAT_REJ='A' || FUN.info('Dokument został anulowany.\nPoprawienie nagłówka korekty zbiorczej niemożliwe.'@); return ?};

{? FAKS.AKC='T' || FUN.info('Korekta zaakceptowana.\nPoprawienie nagłówka korekty zbiorczej niemożliwe.'@); return ?};

_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref());
{? ~_r_lock || exec('who_rlock_faks','faktury_nag'); return ?};

exec('polafaks_get','jpk_log',FAKS.ref(),FAKS);
:: tworzenie tymczasowej definicji rabatu
exec('faks_kzr_copy','faktury_nag1',FAKS.ref(),'N',0);

_var_fakpop:=exec('var_fakpop','faktury_nag');
_var_fakpop.RAB:=FAKS.RAB;
_var_fakpop.KRS:=FAKS.KRS;
_var_fakpop.NKRS:=FAKS.NKRS;
_var_fakpop.R_LOCK:=_r_lock;
_var_fakpop.PO_FIRST:=1;

:: Parmater 'context' wykorzystywany w:
:: - exec('faks_pozycje_red','faktury_nag')
:: - exec('faks_zakoncz_red','faktury_nag')
:: Parametr 'var_fakpop' wykorzystywany w:
:: - exec('faks_pozycje_red','faktury_nag')
:: - exec('faks_zakoncz_red','faktury_nag')
:: - exec('fak_pop_po','faktury_nag',_dod_nag)
params_set('context',params_get(),'var_fakpop',_var_fakpop);

exec('faks_win_edit_set','faktury_nag');
_wyn:=FAKS.edit("exec('faks_kzn_val','faktury_nag1')");
{? FAKS.STAT_REJ='N'
|| exec('fak_pop_po','faktury_nag',_wyn,0)
?};

exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)


\faks_kzn_pop_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Obsługa popraw po zakończeniu edycji (FAKS.edit)
::  OLD: \faks_kzn_pop_po/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.TZ:=FAKS.DW;
:: przepisanie tymczasowej definicji rabatu i jej usunięcie
exec('faks_kzr_copy','faktury_nag1',FAKS.ref(),'T',0);
{? FAKS.put()
||
   exec('polafaks_update','jpk_log',1);
   FAKS.memo_put(,'UWAGI')
?}


\faks_kzn_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: usuwa korekty częściowe
::       funkcja działa na FAKS ustawionym na nagłówku korekty zbiorczej
::   WE:  _a - 1 - komunikaty
::             0 - bez komunikatow
::  OLD: \faks_kzn_usu/faktury1.fml
::  OLD: \faks_kzn_usu/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_done:="VAR_DEL.delete('__wer1','__faks_kzf_env')";
_done();
_kz_uid:=exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",'');
:: środowisko korekty zbiorczej
exec('faks_kzf_env','faktury_nag1',$FAKS.ref());

{? ~exec('czy_z_ok','okresy',{? BEER.SZ='S' || 2 || 3 ?},,FAKS.AR,FAKS.AM) || return(_wyn) ?};

{? FAKS.AKC='T'
|| {? _a=1
   || FUN.info('Korekta zaakceptowana.\nUsunięcie korekty niemożliwe.'@)
   |? _a=2
   || exec('add_kom','#message','Korekta %1 zaakceptowana. Usunięcie dokumentu niemożliwe.'@[FAKS.SYM],2)
   ?};
   return(_wyn)
?};

_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref());
{? _r_lock
||
   {? (_a%*2)=0 | FUN.ask('Usunąć dokument %1?'@ [FAKS.SYM])
   ||
      {? exec('deladok','dokum','FAKS')=0
      ||
         FUN.info('Nie udało się usunąć dokumentu %1,\nponieważ nie powiodło się usunięcie załączników.'@[FAKS.SYM]);
         return(_wyn)
      ||
         FAKS_KZF.cntx_psh();
         FAKS_KZF.index('KZ_UID');
         FAKS_KZF.prefix(_kz_uid);
         exec('faks_kzf_usu','faktury_nag',FAKS.ref(),1);
         _wyn:=~FAKS_KZF.first();
         FAKS_KZF.cntx_pop()
      ?}
   ?};
   exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
||
   {? _a || exec('who_rlock_faks','faktury_nag') ?}
?};
_done();
_wyn


\faks_kzn_val
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: walidacja naglowka korekty zbiorczej
::   WY: ''-jesli ok, 'akronim pola'-wpp
::  OLD: \faks_kzn_val/faktury1.fml
::  OLD: \faks_kzn_val/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.STAT_REJ<>'N' || return(~~) ?};

_fld:=__CHK.record(FAKS,,'DW','KH','WAL');

KH_DOD.KLIM:=__War_f('KH_DOD','KLIM',FAKS.KH);
_klim:={? KH_DOD.KLIM<>null || KH_DOD.KLIM || FAKS.KH().GRKH().KLIM ?};

_czy_rabk:=exec('FindInSet','#table',
   'FAKS_KZR','UNIK','T',exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",''),,1,,null());
_ctrln:=exec('get','#params',{? BEER.SZ='S' || 300209 || 200209 ?},2)='T';

_wyn:=
   {? _fld<>'' || _fld
   |? FAKS.SZ='Z' & FAKS.T().UE='N' & FAKS.WHERE<>'E' & FAKS.ID=''
   || FUN.info('Należy podać numer zewnętrzny.'@); 'ID'
   |? FAKS.DW~1=ST.AR & (FAKS.DW<date(ST.AR,ST.AM,1) | FAKS.DW>date(ST.AR,ST.AM,0))
   || FUN.info('Data wystawienia musi pochodzić z bieżącego miesiąca obrachunkowego.\n'
        'Zmieniono ją na ostatnią datę z bieżącego okresu.'@);
      FAKS.DW:=date(ST.AR,ST.AM,0);
      win_disp();
      _wyn:='DW'
   |? FAKS.DW<FAKS.KZ_DO || FUN.info('Data wystawienia nie może być wcześniejsza od daty dostawy do.'@); 'DW'
   |? FAKS.KZ_OD>FAKS.KZ_DO || FUN.info('Data dostawy od nie może być późniejsza od daty dostawy do.'@); 'KZ_OD'
   |? FAKS.KZ_OD=date(0,0,0) || FUN.info('Data Dostawy od musi być wypełniona.'@); 'KZ_OD'
   |? FAKS.KZ_DO=date(0,0,0) || FUN.info('Data Dostawy do musi być wypełniona.'@); 'KZ_DO'
   |? FAKS.TZP='' & _ctrln || FUN.info('Należy podać powód korekty w polu Powód nagłówek.'@); 'TZP'
   |? FAKS.POWKOR=null()
   || FUN.info('Należy podać powód korekty w pozycji.'@+
               {? INFO.POWKOR
               || ''
               || '\n\nPodanie powodu korekty niemożlwie ze względu na '
                  'niewypełniony słownik powodów korekt w stałych systemu.'@
               ?});
      {? exec('faks_powkor_be','faktury_nag') || 'POWKOR' || 'TZP' ?}
   |? FAKS.T().UE='T' & FAKS.NIP_UE='' || FUN.info('Należy podać NIP UE.'@); 'NIP'
   |? FAKS.RABA<>'T' & _czy_rabk & FAKS.RABK=0
   || FUN.info('Pole kwota rabatu musi być wypełnione.'@); 'RABK'
   |? FAKS.RABA<>'T' & _czy_rabk=0 & FAKS.RAB=0
   || FUN.info('Pole procent rabatu musi być wypełnione.'@); 'RAB'
   |? FAKS.RABA<>'T' & FAKS.RAB<>0 & FAKS.RABK<>0
   || FUN.info('Można wypełnić tylko jedno z pól procent rabatu lub kwota rabatu.'@); 'RAB'
   |? ~exec('lim_kred','limit_kredyt',FAKS.KH,date(),INFO.NAROD,5,_klim,FAKS.BRUTTO,,'faktu')
   || {? exec('kznf2_czy_ed','faktury_nag') || 'KH' || '0' ?}
   || ''
   ?};
:: kurs, data kursu
{? _wyn='' & FAKS.T().SPPK='T' & (exec('spr_ww','eurusd',0) | FAKS.WAL<>INFO.NAROD)
||
   {? FAKS.WAL<>FAKS.NWAL
   ||
      {? FAKS.KRS=0
      ||
         FUN.info('Niewypełniony kurs.'@);
         _wyn:='KRS'

      |? FAKS.DTK=date(0,0,0)
      ||
         FUN.info('Należy podać datę kursu.'@);
         _wyn:='DTK'
      ?}
   ?};
   {? _wyn='' & FAKS.WAL<>INFO.NAROD
   ||
      {? FAKS.NKRS=0
      ||
         FUN.info('Niewypełniony kurs.'@);
         _wyn:='NKRS'

      |? FAKS.NDTK=date(0,0,0)
      ||
         FUN.info('Należy podać datę kursu.'@);
         _wyn:='NDTK'
      ?}
   ?}
?};
{? _wyn='' & FAKS.KRS & FAKS.DTK=date(0,0,0)
||
   FUN.info('Należy podać datę kursu.'@);
   _wyn:='DTK'
?};
{? _wyn='' & FAKS.NKRS & FAKS.NDTK=date(0,0,0)
||
   FUN.info('Należy podać datę kursu.'@);
   _wyn:='NDTK'
?};
{? _wyn=''
||
   _wyn:=exec('polafaks_chk','jpk_log',FAKSPOLA)
?};
{? _wyn='' & FAKS.NR=0 & FAKS.SYM=''
:: Wyznaczenie numeru jeśli nie określony w trakcie redakcji nagłówka
:: !!! Wyznaczenie numeru ma być na końcu tej funkcji !!!
|| {? exec('wol_nr','numery','FAKS')
   || _wyn:='NR'
   ?}
?};

{? _wyn='0' || 0 || _wyn ?}


\faks_kzn_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: podgląd korygowanych pozycji
::   WE: [_a] - FAP.uidref(), na którym ma stanąć selektor
::       [_b] - 0/1 - zakładka w obszarze
::  OLD: \faks_kzn_view/faktury_nag.fml
::----------------------------------------------------------------------------------------------------------------------
_uidref:={? var_pres('_a')=type_of('') || _a || '' ?};
_tab_in_area:={? var_pres('_b')=type_of(0) || _b || 0 ?};

FAKSZKZN.REF:=FAKS.ref;
FAKSZKZN.ODDZ:=FAKS.ODDZ;
FAKSZKZN.AR:=FAKS.AR;
FAKSZKZN.AM:=FAKS.AM;
FAKSZKZN.NR:=FAKS.NR;
FAKSZKZN.SYM:=FAKS.SYM;
FAKSZKZN.DW:=FAKS.DW;
FAKSZKZN.OD:=FAKS.KZ_OD;
FAKSZKZN.DO:=FAKS.KZ_DO;
FAKSZKZN.RAB:=FAKS.RAB;
FAKSZKZN.KH:=FAKS.KH;
FAKSZKZN.NETTO:=FAKS.NETTO;
FAKSZKZN.VAT:=FAKS.VAT;
FAKSZKZN.BRUTTO:=FAKS.BRUTTO;
FAKSZKZN.NETW:=FAKS.NETW;
FAKSZKZN.BRTW:=FAKS.BRTW;

FAKS.cntx_psh;
::exec('open','open_tab',FAKSZKZN.ODDZ,8+$FAKSZKZN.AR+2);
FAP.index('KZ');
FAP.prefix($FAKSZKZN.REF);
exec('ust_lw','eurusd',1);
FAP.win_sel('WERKZB');
{? _tab_in_area
|| ~~
|| params_set(params_get());
   {? _uidref=''
   || FAP.select()
   || FAP.seek(_uidref);
      FAP.select(,1,5)
   ?}
?};
::exec('open','open_tab',ST.ODDZ_REF().KOD,$ST.AR+2);
FAKS.cntx_pop;

FAKSZKZN.blank(1)


\faks_fld_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Przed redakcją pola tabeli FAKS - funkcja ogólna
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_afld:='|'+cur_afld()+'|';
{? FAKS.T().KOR='T' | FAKS.WHERE='N'
|| {? exec('kornag_fld2kor_khodb','faktury_nag')*_afld & FAKS.KH_ODB=null()
:: pola dotyczące odbiorcy nieredagowalne jeśli nie podano odbiorcy
   || 0
   || params_exec('faks_fld_czy_ed','faktury_nag')
   ?}
|| 0
?}


\faks_fld_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Po redakcji pola tabeli FAKS - funkcja ogólna
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
{? params_exec('faks_fld_fill','faktury_nag1',cur_afld())
||
   exec('faks_win_edit_btn_opt','faktury_nag');
   exec('set_efld_opt','faktury_nag');
   1
?}


\faks_fld_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: wypełnia pola tabeli FAKS wartościami przed korektą
::   WE: [_a] - akronim pola
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_afld:={? var_pres('_a')=type_of('') || _a || '' ?};

{? FAKS.T().HIS='T' || return(1) ?};

{? exec('get','#params',300230,2)='T'
||
   {? FAKS.WHERE='N'
         |
      FAKS.SZ='S' & FAKS.T().KOR='T'
   ||
      _lksql_buf:=exec('FindAndGet','#table',FAKS,FAKS.LKSQL,,"
         _lksql_buf:=obj_new('NIP','NIP_UE','O_NIP');
         _lksql_buf.NIP:=FAKS.NIP;
         _lksql_buf.NIP_UE:=FAKS.NIP_UE;
         _lksql_buf.O_NIP:=FAKS.O_NIP;
         _lksql_buf
      ",'');
      KH.cntx_psh();
      KH_ODB.cntx_psh();
      _result:=1;
      {? _afld<>'KH' & _lksql_buf.NIP<>FAKS.NIP | _afld='KH' & _lksql_buf.NIP<>FAKS.KH().NIP
      ||
         FUN.info('Niedostępna zmiana NIP kontrahenta.'@);
         _result:=0

      |? _afld<>'NIP_UE' & _lksql_buf.NIP_UE<>FAKS.NIP_UE | _afld='NIP_UE' & _lksql_buf.NIP_UE<>INTRAST.NIP
      ||
         FUN.info('Niedostępna zmiana NIP_UE kontrahenta.'@);
         _result:=0

      |? _lksql_buf.O_NIP<>''
            &
         (_afld<>'KH_ODB' & _lksql_buf.O_NIP<>FAKS.O_NIP | _afld='KH_ODB' & _lksql_buf.O_NIP<>FAKS.KH_ODB().NIP)
      ||
         FUN.info('Niedostępna zmiana NIP odbiorcy.'@);
         _result:=0
      ?};
      KH_ODB.cntx_pop();
      KH.cntx_pop();
      return(_result)
   ?}
?};

:: !!! dalsza część funkcji wykorzystywana była do korekt nagłowkowych wystawianych w okienku zwykłej korekty
:: !!! obecnie niewykorzystywana
return(1);

{? type_of(params_get())=type_of(~~) | var_pres('faks',params_get())<0 | FAKS.SZ='Z' || return(1) ?};

:: params_get.faks powołane w \koryguj/faktury_nag.fml, \fak_pop/faktury_nag.fml
_faks:=params_get.faks;

_fld2kor:=exec('kornag_fld2kor','faktury_nag');

{? FAKS.SZ='S' & FAKS.T().KOR='T' & params_exec('kornag','faktury_nag')
:: korekta danych nagłówkowych
||
:: znacznik korekty nagłówkowej
   FAKS.WHERE:='N';
:: kontrahent, nip ue, odbiorca
   {? (_fld2kor*'|KH|')=0
   ||
      FAKS.KH:=_faks.KH;
      FAKS.NIP_UE:=_faks.NIP_UE;
      FAKS.KH_ODB:=_faks.KH_ODB;
      exec('duplikuj_dane','faktury_wspolne','KH','KH_ODB')
   ?};
:: handlowiec
   {? (_fld2kor*'|HAN|')=0
   ||
      FAKS.HAN:=_faks.HAN
   ?};
:: data sprzedaży, odbioru, zwrotu
   {? (_fld2kor*'|D|')=0
   ||
      FAKS.D:=_faks.D;
      FAKS.DO:=_faks.DO
   ?};
   FAKS.DZ:=date(0,0,0);
:: płatność
   FAKS.PL:=null();
   FAKS.TZ:=date(0,0,0);
   exec('del_plat','faktury_plat',FAKS.ref());
:: rachunek bankowy
   {? (_fld2kor*'|SKID_RBK|')=0
   ||
      FAKS.SKID_RBK:=_faks.SKID_RBK;
      FAKS.NB:=_faks.NB;
      FAKS.RBK:=_faks.RBK
   ?};
:: rabat
   FAKS.RAB:=_faks.RAB;
:: waluta handlowa
   FAKS.WAL:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"WAL",null());
   FAKS.RTK:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"RTK",0);
   FAKS.NTK:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"NTK",'');
   FAKS.BTK:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"BTK",'');
   FAKS.DTK:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"DTK",date(0,0,0));
   FAKS.SWAL:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"SWAL",0);
   FAKS.BKRS:=_faks.BKRS;
   FAKS.KRS:=_faks.KRS;
:: waluta opodatkowania
   FAKS.NWAL:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"NWAL",null());
   FAKS.NRTK:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"NRTK",0);
   FAKS.NNTK:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"NNTK",'');
   FAKS.NBTK:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"NBTK",'');
   FAKS.NDTK:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"NDTK",date(0,0,0));
   FAKS.NSWAL:=exec('FindAndGet','#table',FAKS,_faks.KRS_REF,,"NSWAL",0);
   FAKS.BNKRS:=_faks.BNKRS;
   FAKS.NKRS:=_faks.NKRS;
:: parametry wyliczeń
   FAKS.CB:=_faks.CB;
   FAKS.SPOBLRAB:=_faks.SPOBLRAB;
   FAKS.RAB_TYP:=_faks.RAB_TYP;
   FAKS.GRATISY:=_faks.GRATISY;
   FAKS.NDVAT:=_faks.NDVAT;
   FAKS.DO_UE:=_faks.DO_UE

:: przywrócenie FAKS.WHERE z faktury korygowanej
|? FAKS.WHERE='N' & exec('kznf2_czy_ed','faktury_nag')
||
   FAKS.WHERE:=_faks.WHERE
?};
1


\czy_par_nip_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Czy można wystawić fakturę do paragonu - jest NIP albo wolno bez NIP
::   WE: _a - NIP, (jeżeli nie podany to FAKS.NIP)
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=0
|| _nip:=FAKS.NIP
|| _nip:=_a
?};

_result:=1;
{? _nip ='' & exec('get','#params',300804,2)<>'T'
|| _result:=0
?};

_result


\fakszkn_fill_bfld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Korekta nagłówkowa - wypełnia wartość pól przed korektą
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FAKSZKN.blank(1);
exec('FindAndGet','#table',FAKS,FAKS.LKSQL,,"
:: dane
   FAKSZKN.D:=FAKS.D;
   FAKSZKN.DO:=FAKS.DO;
:: kontrahent
   FAKSZKN.KH:=FAKS.KH;
   FAKSZKN.NAZ:=FAKS.NAZ;
   FAKSZKN.KH_KRISO:=FAKS.KH_KRISO;
   FAKSZKN.KH_KRAJ:=FAKS.KH_KRAJ;
   FAKSZKN.MIASTO:=FAKS.MIASTO;
   FAKSZKN.UL:=FAKS.UL;
   FAKSZKN.DOM:=FAKS.DOM;
   FAKSZKN.LOKAL:=FAKS.LOKAL;
   FAKSZKN.KPOCZ:=FAKS.KPOCZ;
   FAKSZKN.POCZ:=FAKS.POCZ;
   FAKSZKN.NIP:=exec('niptostr','#string',FAKS.NIP);
   FAKSZKN.NIP_UE:=FAKS.NIP_UE;
   FAKSZKN.HAN:=FAKS.HAN;
   FAKSZKN.KH_ODB:=FAKS.KH_ODB;
   FAKSZKN.O_NAZ:=FAKS.O_NAZ;
   FAKSZKN.O_KRISO:=FAKS.O_KRISO;
   FAKSZKN.O_KRAJ:=FAKS.O_KRAJ;
   FAKSZKN.O_MIASTO:=FAKS.O_MIASTO;
   FAKSZKN.O_UL:=FAKS.O_UL;
   FAKSZKN.O_DOM:=FAKS.O_DOM;
   FAKSZKN.O_LOKAL:=FAKS.O_LOKAL;
   FAKSZKN.O_KPOCZ:=FAKS.O_KPOCZ;
   FAKSZKN.O_POCZ:=FAKS.O_POCZ;
   FAKSZKN.O_NIP:=exec('niptostr','#string',FAKS.O_NIP);
:: inne
   FAKSZKN.SKID_RBK:=FAKS.SKID_RBK;
   FAKSZKN.VSKIDRBK:=FAKS.VSKIDRBK;
   FAKSZKN.SP_WYM:=FAKS.SP_WYM
")


\fakszkn_fld_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Przed wyświetleniem pola zmiennej FAKSZKN - funkcja ogólna
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_afld:='|'+cur_afld()+'|';
_Tab:=cur_tab(1,1);
{? _Tab=FAKS   & _afld='|D|'  || exec('fakszkn_fill_bfld','faktury_nag1')
|? _Tab=FAKSKH & _afld='|KH|' || exec('fakskh_fill_bfld','faktury_wspolne')
?};
1


\kraj_vat_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14_08]
:: OPIS: Przed redakcją FAKS.KRAJ_VAT
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tfld();
DPOZ.WPR_S:=$fld();
DPOZ.WPR_R:=1;
{? _Tab<>ZK_N || exec('rab_typ_bd','ceny') ?};
DPOZ.WPR_R & (KST.OSS<>'' | KST.IOSS<>'') & (_Tab<>FAKS | params_exec('kznf2_czy_ed','faktury_nag'))


\kraj_vat_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14_08]
:: OPIS: Po redakcji FAKS.KRAJ_VAT
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);
{? DPOZ.WPR_S<>$fld()
||
   {? _Tab.KRAJ_VAT=null() || _Tab.PROC:=null()
   |? _Tab.PROC=null() || _Tab.PROC:=exec('oss_default_proc','jpk_log')
   ?}
?};
1


\proc_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14_08]
:: OPIS: Przed redakcją FAKS.PROC
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tfld();
{? _Tab.KRAJ_VAT
||
   exec('jpk_slo_proc_faks','edi_fo_ksef')
?}
& (_Tab<>FAKS | params_exec('kznf2_czy_ed','faktury_nag') & (FAKS.T().KOR='N' | exec('nip_ue_required','intrastat')))


\proc_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14_08]
:: OPIS: Po redakcji FAKS.PROC
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tfld();
{? _Tab=FAKS || exec('set_efld_opt','faktury_nag') ?};
1


\fill_kraj_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Wypełnia FAKS.KRAJ_VAT, FAKS.PROC wg _a lub _b
::   WE: _a - FAKS.ref
::       _b - ZK_N.ref
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_faks:=_a;
_zk_n:=_b;
{? _faks
||
   FAKS.KRAJ_VAT:=exec('FindAndGet','#table',FAKS,_faks,,"FAKS.KRAJ_VAT",null());
   FAKS.PROC:=exec('FindAndGet','#table',FAKS,_faks,,"FAKS.PROC",null())

|? _zk_n
||
   FAKS.KRAJ_VAT:=exec('FindAndGet','#table',ZK_N,_zk_n,,"ZK_N.KRAJ_VAT",null());
   FAKS.PROC:=exec('FindAndGet','#table',ZK_N,_zk_n,,"ZK_N.PROC",null())
?}


\par_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: wystawia dokument sprzedazy na podstawie paragonu
::   WE: [_a] - ref typu dokumentu sprzedaży
::       [_b] - ref paragonu
::       [_c] - 1-wywołanie z obszaru 0(domyślnie)-nie
::  OLD: \par_fak/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>7 || _a:=null() ?} || _a:=null() ?};
{? _>=2 || {? type_of(_b)<>7 || _b:=null() ?} || _b:=null() ?};
{? _>=3 || {? type_of(_c)<>1 || _c:=0 ?} || _c:=0 ?};

BEER.SZ:='S';
{? exec('czy_z_ok','okresy',-2)
||
   _par:=params_get();
   params_set('out',_par.out,'mp',_par.mp,'akcja',_par.akcja,'in',_par.in,'auto',_par.auto,'context',_par.context);
   _rlock:=0;

   {? _b<>null() & FAKS.get & FAKS.T().PAR='T' & FAKS.T().KOR='N' & FAKS.KORYG='N' & FAKS.STAT_REJ='T'
      & FAKS.SYMF='' & exec('czy_par_nip_ok','faktury_nag1',FAKS.NIP)
   || _ask:={? _c
            || FUN.choice('Wystaw fakturę do paragonu.'@,,'Bieżący paragon'@,'Lista niezafakturowanych'@)
            || 1
            ?}
   |? FAKS.get() & FAKS.T().PAR='T' & FAKS.T().KOR='N' & FAKS.KORYG='T'
   || FUN.info('Wystawiono dokument korygujący.\nUtworzenie faktury do paragonu niemożliwe.'@);
      _ask:=0
   |? FAKS.get & FAKS.T().PAR='T' & FAKS.T().KOR='N' & ~exec('czy_par_nip_ok','faktury_nag1',FAKS.NIP)
   || FUN.info('Nie podano NIP na paragonie.\nUtworzenie faktury do paragonu niemożliwe.'@);
      _ask:=0
   || _ask:=2
   ?};
   {? _ask=1
   || exec('brakNipNaPar','faktury_nag',$FAKS.ref(),1);
      {? FAKS.get & ~(_rlock:=FAKS.r_lock(1,1,1))
      ||
         {? FUN.ask('Dokument %1 obsługuje inny użytkownik.\nCzy chcesz zobaczyć kto?'@[FAKS.SYM])
          & (_rlock:=FAKS.r_lock(1,,1))
         || FAKS.r_unlock()
         ?};
         return
      ?};
      _tab:=tab_tmp(1,'FAKS','STRING[16]','','LOCK','STRING[1]','');
      _tab.blank();
      _tab.FAKS:=$FAKS.ref();
      _tab.LOCK:='N';
      _tab.add(1);
      _par_ref:=FAKS.ref();
      exec('par2fak','faktury_nag',_tab);
      obj_del(_tab);
      {? _rlock || FAKS.r_unlock(_par_ref) ?}
   |? _ask=2
   ||
      {? var_pres('__par_tab')>100 || obj_del(__par_tab) ?};
      __par_tab:=tab_tmp(4
            ,'ROK','INTEGER','Rok'
            ,'STS','STRING[8]','Stanowisko'
            ,'TYP','STRING[8]','Typ'
            ,'SYMBOL','STRING[20]','Symbol paragonu'
            ,'DW','DATE','Data wystawienia'
            ,'KH','STRING[10]','Kontrahent'
            ,'BRUTTO','REAL','Wartość brutto'
            ,'NIP','STRING[16]','NIP'
            ,'REF_PAR','STRING[16]','REF_PAR'
            ,'ACCESS','STRING[1]','Wybrano'
            ,'KRAJ_VAT','STRING[2]','Kraj'
         );
      __par_tab_ndx:=__par_tab.ndx_tmp(,,'ACCESS',,);

      _par_disp:="
         FAKS.cntx_psh();
         FAKS.use(8+__par_tab.REF_PAR);
         FAKS.clear();
         {? FAKS.seek(__par_tab.REF_PAR,8+__par_tab.REF_PAR) || exec('fall_rek','faktury_nag') ?};
         FAKS.cntx_pop()
      ";

      _par_fakt:="
        _tab:=tab_tmp(1,'FAKS','STRING[16]','','LOCK','STRING[1]','');
        _jest:=0;
        __par_tab.cntx_psh();
        {? __par_tab.first()
        || _info:=1;
           {!
           |? {? __par_tab.ACCESS='T'
              || _info:=exec('brakNipNaPar','faktury_nag',__par_tab.REF_PAR,_info);
                 _tab.blank();
                 _tab.FAKS:=__par_tab.REF_PAR;
                 _tab.LOCK:='N';
                 _tab.add(1);
                 _jest:=1
              ?};
              __par_tab.next()
           !}
        ?};
        __par_tab.cntx_pop();
        {? ~_jest
        || _tab.blank();
           _tab.FAKS:=__par_tab.REF_PAR;
           _tab.add(1)
        ?};
        _par:=params_get();
        params_set('out',_par.out,'mp',_par.mp,'akcja',_par.akcja,'in',_par.in,'auto',_par.auto,'context',_par.context);
        _res:=exec('par2fak','faktury_nag',_tab);
        _tab.clear();
        {? _tab.first()
        || {!
           |? {? _tab.LOCK='T'
              || FAKS.cntx_psh();
                 FAKS.use(form(8+_tab.FAKS));
                 FAKS.clear();
                 {? FAKS.seek(_tab.FAKS) || FAKS.r_unlock() ?};
                 FAKS.cntx_pop();
                 {? _res || __par_tab.del() ?}
              ?};
              _tab.next()
           !}
        ?};
        obj_del(_tab);
        {? _res || sel_exit() ?}
      ";

      {? var_pres('__par_sel')>100 || obj_del(__par_sel) ?};
      __par_sel:=__par_tab.mk_sel('Niezafakturowane paragony'@,,0,'_par_fak_',,,,,'U');
      __par_tab.win_fld(__par_sel,,'ACCESS',,,3,,,'Wybrano'@,,'Czy podlega zafakturowaniu [T/N]'@,2,,"'T'","'N'");
      __par_tab.win_fld(__par_sel,,'ROK',,,4,,,'Rok'@);
      __par_tab.win_fld(__par_sel,,'STS',,,8,,,'Stanowisko'@);
      __par_tab.win_fld(__par_sel,,'TYP',,,8,,,'Typ'@);
      __par_tab.win_fld(__par_sel,,'SYMBOL',,,20,,,'Symbol paragonu'@);
      __par_tab.win_fld(__par_sel,,'DW',,,,,,'Data wystawienia'@);
      __par_tab.win_fld(__par_sel,,'KH',,,,,,'Kontrahent'@);
      __par_tab.win_fld(__par_sel,,'BRUTTO',,,12,2,,'Wartość brutto'@);
      __par_tab.win_fld(__par_sel,,'NIP',,,,,,'NIP'@);
      __par_tab.win_fld(__par_sel,,'KRAJ_VAT',,,2,,,'Kraj'@);
      __par_tab.win_act(__par_sel,,'Formuła','&Fakturuj'@@,,'Wystawienie faktury do paragonu'@,,_par_fakt,1,,,,'F');
      __chk_kraj:="
         _msg:=_a;
         _result:=1;
         {? __par_tab.KRAJ_VAT<>''
         ||
            _kraj_vat:=__par_tab.KRAJ_VAT;
            __par_tab.cntx_psh();
            __par_tab.index(__par_tab_ndx);
            __par_tab.prefix('T');
            {? __par_tab.first() & __par_tab.KRAJ_VAT<>_kraj_vat
            ||
               {? _msg || FUN.info('Nie można wybrać paragonów z różnym krajem.'@) ?};
               _result:=0
            ?};
            __par_tab.cntx_pop()
         ?};
         _result
      ";
      _formula:="
         _tab:=__par_tab.sel_aget();
         __par_tab.sel_adel();
         __par_tab.cntx_psh();
         {? _tab.first()
         ||
            _msg:=1;
            {!
            |?
               {? (__par_tab.clear(); __par_tab.seek(_tab.REF,)) & __par_tab.ACCESS='N'
               ||
                  {? __chk_kraj(_msg)
                  ||
                     __par_tab.ACCESS:='T';
                     __par_tab.put(1)
                  ||
                     _msg:=0
                  ?}
               ?};
               _tab.next()
            !}

         |? __par_tab.ACCESS='N'
         ||
            {? __chk_kraj(1)
            ||
               __par_tab.ACCESS:='T';
               __par_tab.put(1)
            ?}
         ?};
         __par_tab.cntx_pop();
         obj_del(_tab)
      ";
      __par_tab.win_act(__par_sel,,'Formuła','&Zaznacz'@@,,,_formula,,,1,_formula,,'Z');
      __par_tab.win_btn(__par_sel,'text=%1,panel=right,align=begin'['Zaznacz'@],'menu:Z',,,,,,'noempty');
      _formula:="_tab:=__par_tab.sel_aget();
                 __par_tab.sel_adel();
                 __par_tab.cntx_psh();
                 {? _tab.first()
                 || {!
                    |? {? (__par_tab.clear(); __par_tab.seek(_tab.REF,)) & __par_tab.ACCESS='T'
                       || __par_tab.ACCESS:='N';
                          __par_tab.put(1)
                       ?};
                       _tab.next()
                    !}
                 |? __par_tab.ACCESS='T'
                 || __par_tab.ACCESS:='N';
                    __par_tab.put(1)
                 ?};
                 __par_tab.cntx_pop();
                 obj_del(_tab)";
      __par_tab.win_act(__par_sel,,'Formuła','&Odznacz'@@,,,_formula,,,1,_formula,,'O');
      __par_tab.win_btn(__par_sel,'text=%1,panel=right,align=begin'['Odznacz'@],'menu:O',,,,,,'noempty');
      __par_tab.win_act(__par_sel,,'Wyświetl',,,,,_par_disp);
      __par_tab.win_act(__par_sel,,'Szukaj');
      __par_tab.win_act(__par_sel,,'Kolejność');

      exec('ptab_add','faktury_nag',ST.AR-1);
      exec('ptab_add','faktury_nag',ST.AR);

      {? __par_tab.first()
      ||
         __par_tab.win_sel(__par_sel);
         __par_tab.select()
      ||
         FUN.info('Brak niezafakturowanych paragonów.'@)
      ?};
::    odblokowanie paragonow
      FAKS.cntx_psh();
      __par_tab.clear();
      {? __par_tab.first()
      || {!
         |? {? __par_tab.REF_PAR<>''
            || FAKS.use(form(8+__par_tab.REF_PAR));
               FAKS.clear();
               {? FAKS.seek(__par_tab.REF_PAR) || FAKS.r_unlock() ?}
            ?};
            __par_tab.next()
         !}
      ?};
      FAKS.cntx_pop()
   ?}
?};
''


\ctrlCOS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.14]
:: OPIS: Kontroal magazynów konsygnacyjnych dla pozycji dokumentu
::   WE: _a - ref FAKS
::       _b - ref KH
::       _c - obsługa COS
::   WY: 1-jest OK 0-nie jest dobrze
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_faks:=_a;
_kh:=_b;
_cos:=_c;

FAP.cntx_psh();
REZ.cntx_psh();
FAP.index('FAP');
FAP.prefix(_faks);
{? FAP.first()
|| {!
   |? REZ.index('FAP');
      REZ.prefix(FAP.ref());
      {? REZ.first()
      || {!
         |? {? (REZ.MG().COS='T' & ~_cos) | (REZ.MG().DLAKH<>null() & REZ.MG().DLAKH<>_kh) || _res:=0 ?};
            _res & REZ.next()
         !}
      ?};
      _res & FAP.next()
   !}
?};
FAP.cntx_pop();
REZ.cntx_pop();
_res


\faks_statksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Ustala wartość FAKS.STATKSEF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_return:='';
{? FAKS.SZ='S'
||
   _return:=exec('ksef_donot_applay','zbl_stat');
   {? FAKS.KZ<>''
   ||
      _return:=''

   |? FAKS.KH
   ||
      _continue:=1;
      {? FAKS.FKSQL<>'' & exec('FindAndGet','#table',FAKS,FAKS.FKSQL,,"FAKS.STATKSEF",_return)=_return
      ||
:: korekta nie podlega KSeF jeśli korygowana faktura poza KSeF
:: korekta nagłówkowa nie podlega KSeF jeśli korygowana faktura poza KSeF
         _continue:=0

      |? FAKS.WHERE='K'
      ||
:: korekta zbiorcza nie pdlega KSeF jeśli którakolwiek faktura cząstkowa poza KSeF
         FAKS_KZF.cntx_psh();
         FAKS_KZF.index('KZ_UID');
         FAKS.cntx_psh();
         FAKS_KZF.prefix(FAKS.uidref());
         FAKS.cntx_pop();
         _loop:=FAKS_KZF.first();
         {!
         |? _loop
         |!
            _continue:=exec('FindAndGet','#table',FAKS,FAKS_KZF.FAK,,"FAKS.STATKSEF",_return)<>_return;
            _loop:=_continue & FAKS_KZF.next()
         !};
         FAKS_KZF.cntx_pop()
      ?};
      {? _continue
      ||
         _kh:=exec('kh_dokum','zbl_dok',FAKS.ref());
         _typysp:=FAKS.T;
         _dw:=FAKS.DW;
         _where:=FAKS.WHERE;
         _mansksef:=FAKS.MANSKSEF;
         {? exec('czy_edokument_bl_ksef','zbl_dok',_kh,_typysp,_dw,_where,_mansksef)
         ||
            _return:=exec('before_send','zbl_stat')
         ?}
      ?}
   ?}
?};
_return


\faks_statksef_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wartość początkowa FAKS.STATKSEF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('faks_statksef','faktury_nag1')


\doc_in_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Sprawdza czy dokument jest w KSeF
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
FAKS.STATKSEF=exec('ksef_send_unconfirmed','zbl_stat') |
FAKS.STATKSEF=exec('ksef_send_confirmed','zbl_stat') |
FAKS.STATKSEF=exec('ksef_send_signature_required','zbl_stat') |
FAKS.STATKSEF=exec('ksef_send_signed','zbl_stat')


\faks_wal_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Ustawienie waluty dokumentu wg typu, kontrahenta lub odbiorcy
::   WE: _a - czy wykonać "faks_wal_set_po"
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0)
|| _a:=0
?};
{? FAKS.SZ='S' & FAKS.WHERE='P' & FAKS.T<>null() & FAKS.T().PAR='N' & exec('czy_poz','eurusd')
|| _wal:=null();
   KH.cntx_psh();
   KH_ODB.cntx_psh();
   {? FAKS.KH_ODB().KH_LNK<>null()
   || exec('kh_dod_ini','kontrahent',FAKS.KH_ODB().KH_LNK);
      {? KH_DOD.WAL<>null()
      || _wal:=KH_DOD.WAL
      ?}
   ?};
   {? _wal=null() & FAKS.KH<>null()
   || exec('kh_dod_ini','kontrahent',FAKS.KH);
      {? KH_DOD.WAL<>null()
      || _wal:=KH_DOD.WAL
      ?}
   ?};
   {? _wal=null() & FAKS.T().WAL<>null()
   || _wal:=FAKS.T().WAL
   ?};
   KH.cntx_pop();
   KH_ODB.cntx_pop();
   {? _wal=null()
   || _wal:=exec('bl_wal','ustawienia')
   ?};
   {? _wal<>null() & FAKS.WAL<>_wal
   || FAKS.WAL:=_wal;
      FAKS.KRS:=0;
      FAKS.NKRS:=0;
      {? FAKS.WAL<>exec('bl_wal','ustawienia')
      || _bankrs:={? KSTE.BANKRS<>null() || KSTE.BANKRS || INFO.BANKRS ?};
         _typkrs:={? KSTE.TYPKRS<>null() || KSTE.TYPKRS || INFO.TYPKRS ?};
         {? _bankrs<>null()
         || exec('dkurs','eurusd',_bankrs,_typkrs,,'FAKS.KRS');
            exec('po_krs','eurusd','FAKS.KRS');
            {? _a
            || win_disp()
            ?}
         ?}
      ?};
      {? _a
      || exec('faks_wal_set_po','faktury_nag1')
      ?}
   ?}
?};
~~


\faks_wal_set_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Po ustawieniu waluty dokumentu wg typu, kontrahenta lub odbiorcy
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.WAL=exec('bl_nwal','ustawienia')
|| FAKS.SWAL:=FAKS.NSWAL:=2;
   {? KSTE.WAL=INFO.NAROD
   || FAKS.KRS:=FAKS.NKRS:=0;
      FAKS.DTK:=FAKS.NDTK:=date(0,0,0)
   ?};
   FAKS.BTK:=FAKS.NBTK:='';
   FAKS.RTK:=FAKS.NRTK:=0;
   FAKS.NTK:=FAKS.NNTK:=''
?};
exec('set_efld_opt','faktury_nag');
~~


\kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Właściwe jądro kopiujące pojedynczą fakturę
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::       context - [obj_new] obiekt służący do przekazywania kontekstu wywołania czynności
::----------------------------------------------------------------------------------------------------------------------
_out:=params_get().out;
_mp:=params_get().mp;
_context:=params_get().context;
_src_FAKS:=_context.IN_FAKS;
_dest_DATE:=_context.DATE;
_dest_DATE_S:=_context.DATE_S;

do();

:: Zachowujemy konteksty
exec('fak_psh','open_tab');

:: Maska aktualna, źródłowa i docelowa
_akt_MSK:=FAKS.name()+3;
_src_MSK:=ref_name(_src_FAKS)+3;
_oddzial:=1+_src_MSK;
_src_ROK:=1-_src_MSK;
_dest_ROK:=($(_dest_DATE~1))+2;
_dest_MSK:=_oddzial+_dest_ROK;

:: o ile konieczne, następuje przełączanie na maskę źródłową
{? _src_MSK<>_akt_MSK || exec('fak_open','open_tab',_oddzial,_src_ROK) ?};
:: Czytamy nagłówek
FAKS.prefix();
{? FAKS.seek(_src_FAKS)
||
   _sz:=FAKS.SZ;

:: Buforowanie nagłówka
   _FAKS:=exec('buffer','faktury_nag');
   _FAKS.get(1);
:: Buforowanie płatności
   FAKSPL.index('FAKS');
   FAKSPL.prefix(FAKS.ref());
   _size_FAKSPL:=FAKSPL.size();
   {? _size_FAKSPL>0
   || _FAKSPL:=obj_new(_size_FAKSPL);
      {? FAKSPL.first()
      || _pos:=0;
         {!
         |? _pos+=1;
            _FAKSPL[_pos]:=exec('buffer','faktury_plat');
            _FAKSPL[_pos].get(1);
            FAKSPL.next()
         !}
      ?}
   ?};
:: Buforowanie pozycji
   FAP.index('FAP');
   FAP.prefix(FAKS.ref());
   _size_FAP:=FAP.size();
   {? _size_FAP>0
   || _FAP:=obj_new(_size_FAP);
      {? FAP.first()
      || _pos:=0;
         {!
         |? _pos+=1;
            _FAP[_pos]:=exec('buffer','faktury_poz');
            _FAP[_pos].get(1);
            FAP.memo_get(,'UWAGI');
            _FAP[_pos].memo_put(,'UWAGI');
            FAP.next()
         !}
      ?}
   ?};

:: Modyfikacja wybranych pól nagłówka
   _FAKS.AR:=_dest_DATE~1;
   _FAKS.AM:=_dest_DATE~2;
   _FAKS.NR:=0;
   _FAKS.SYM:='';
   _FAKS.D:=_dest_DATE_S;
   _FAKS.DW:=_dest_DATE;
   _FAKS.DR:=_dest_DATE_S;
   _FAKS.DO:=_dest_DATE_S;
   _FAKS.STAT_REJ:='N';
   _FAKS.AKC:='N';
   _FAKS.ZAK:='N';
   _FAKS.KORYG:='N';
   _FAKS.EDI_R:='N';
   _FAKS.EDI_W:='N';
   _FAKS.INTRA:='N';
   _FAKS.INTRAKC:='N';
   _FAKS.PLATKAS:=0;
   _FAKS.PAR:='';
   _FAKS.NRUNIKAT:='';
   _FAKS.CZY_DRUK:='N';
   _FAKS.NRKSEF:='';
   _FAKS.DO_NA_DR:=_dest_DATE_S;
   _FAKS.INFO:='Brak załączników'@;
   _FAKS.SYMF:='';
   _FAKS.SYMF_T:=null();
   _FAKS.KSEFCDAT:=date(0,0,0);
   _FAKS.KSEFAUTH:=null();
   _FAKS.MANSKSEF:='N';
   _FAKS.KSEF_ERR:='N';
   _FAKS.USER:=exec('user_n','blank');
   {? exec('upgrade2325_blbc1','zbl')
   || _FAKS.STATBC:=''
   ?};

:: O ile konieczne, następuje przełączenie na maskę docelową
   {? _dest_MSK<>_src_MSK || exec('fak_open','open_tab',_oddzial,_dest_ROK) ?};

:: Dołączamy nagłówek
   FAKS.prefix();
   FAKS.blank(1); _FAKS.setf();
   {? FAKS.add()
   || {? FAKS.memo_put(,'UWAGI')
      || FAKS.STATKSEF:='';
         POM.TAB:='FAKS';
         POM.TYPDOK:={? FAKS.T<>null() || FAKS.T().KOD ?};
         POM.NRT:={? FAKS.T<>null() || FAKS.T().NRT ?};
         FAKS.NR:=exec('numer_new','numery','PACZKA');
         exec('znak','numery','FAKS');

::       Dołączamy płatności
         {? _size_FAKSPL>0
         || FAKSPL.prefix();
            {! _pos:=1..obj_len(_FAKSPL)
            |! FAKSPL.blank(1); _FAKSPL[_pos].setf();
               FAKSPL.FAKS:=FAKS.ref();
               FZ.FORMAPLA:=FAKSPL.PL().KOD;
               FZ.PL:=FAKSPL.PL;
               FAKSPL.TERMPLAT:=exec('term_plat','faktury_plat','X','N',FAKS.ref());
               FAKSPL.add();
               {?_pos=1 | FAKSPL.TERMPLAT>FAKS.TZ
               || FAKS.TZ:=FAKSPL.TERMPLAT
               ?}
            !};
            FAKS.put()
         ?};
::       Dołączamy pozycje
         {? _size_FAP>0
         || FAP.prefix();
            {! _pos:=1..obj_len(_FAP)
            |! FAP.blank(1); _FAP[_pos].setf();
               FAP.FAKS:=FAKS.ref();
               {? var_pres('SP_WYM',MPKW)=27
                     &
                  ~exec('sp_active','faktury_wspolne',FAKS.DW)
               ||
                  FAP.SP_WYM:='N'
               ?};
::             jpk
               {? var_pres('JPK_GTU',MGR)>0
               ||
                  {? FAP.M().JPK_GTU=null & exec('get','#params', 300120, 2)='T'
                  || FAP.JPK_GTU:=FAP.M().MGR().JPK_GTU
                  || FAP.JPK_GTU:=FAP.M().JPK_GTU
                  ?}
               ?};
               FAP.OPAK_GEN:='N';
               exec('fill_fld2prn','faktury_poz');
               {? FAP.add()
               || FAP.memo_put(,'UWAGI');
::                Kopiowanie podziałów controllingowych pozycji
                  {? exec('get','#params',300808)='T'
                  ||
                     SCH_XD.cntx_psh();
                     SCH_XD.index('FAP');
                     SCH_XD.prefix(_FAP[_pos].Ref);
                     {? SCH_XD.first()
                     ||
                        {!|?
                           SCH_XD.FAKS:=FAKS.ref();
                           SCH_XD.FAP:=FAP.ref();
                           SCH_XD.cntx_psh();
                           SCH_XD.prefix();
                           SCH_XD.add();
                           SCH_XD.cntx_pop();
                           SCH_XD.next()
                        !}
                     ?};
                     SCH_XD.cntx_pop()
                  ?}
               ?}
            !}
         ?};
::       Aktualizacja wartość dokumentu i rozpiski vat
         exec('sumfak','faktury_wspolne');
::       Kopiowanie podziałów controllingowych
         {? exec('get','#params',300808)='T'
         ||
            SCH_XD.cntx_psh();
            SCH_XD.index('FAKS_T');
            SCH_XD.prefix(_src_FAKS,'N',);
            {? SCH_XD.first()
            ||
               {!|?
                  SCH_XD.FAKS:=FAKS.ref();
                  SCH_XD.cntx_psh();
                  SCH_XD.prefix();
                  SCH_XD.add();
                  SCH_XD.cntx_pop();
                  SCH_XD.next()
               !}
            ?};
            SCH_XD.cntx_pop()
         ?};

         _context.OUT_FAKS:=FAKS.ref();
         {? _sz='S' || _out.FAKS:=FAKS.ref() || _out.ZAK:=FAKS.ref() ?};
         _mp.save(,_out)
      ?}
   ?}
?};

:: Odtwarzamy konteksty
exec('fak_pop','open_tab');
end();
~~


\f_nip_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Po edycji INTRAST.F_NIP (pole NIP w oknie dokumentu sprzedaży)
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? app_info('web_sesid')=''
|| _cur_tab:=cur_tab()
|? var_pres('__CURTAB')>0
|| _cur_tab:=__CURTAB
|| return(_wyn)
?};
{? _cur_tab=FAKS & FAKS.WHERE<>'N'
|| {? FAKS.T().PAR='T'
   || _wyn:=exec('nip_ok','#id',INTRAST.F_NIP)
   ?};
   {? _wyn
   || _kh0:=FAKS.KH;
      _snip:=exec('niptostr','#string',INTRAST.F_NIP);
      {? _snip=''
      || {? FAKS.KH<>null() & (FAKS.KH().SNIP='' | FAKS.KH().SNIP=FAKS.KH().PESEL)
         || _kh:=FAKS.KH
         |? FAKS.T().PAR='T'
         || _kh:=FAKS.T().KH
         || _kh:=null()
         ?}
      || {? FAKS.KH<>null() & FAKS.KH().SNIP=_snip
         || _kh:=FAKS.KH
         || _kh:=exec('FindInSet','#table','KH','SNIP',_snip,2,,,,null());
            {? _kh=null()
            || {? FAKS.T().PAR='T'
               || _kh:=FAKS.T().KH
               || _win:=KH.win_sel('?');
                  KH.win_sel('SEL');
                  KH.cntx_psh();
                  {? KH.select(,1,-1)
                  || _kh:=KH.ref()
                  ?};
                  KH.cntx_pop();
                  KH.win_sel(_win)
               ?}
            ?}
         ?}
      ?};
      {? _kh<>_kh0
      || FAKS.KH:=_kh;
         _wyn:=exec('ae_fa_kh','faktury_nag');
         {? ~_wyn
         || FAKS.KH:=_kh0
         ?}
      ?};
      {? FAKS.KH=null()
      || {? FAKS.T().PAR='T'
         || FAKS.NIP:=INTRAST.F_NIP
         || FAKS.NIP:=INTRAST.F_NIP:=''
         ?}
      || {? FAKS.T().PAR='T' & FAKS.KH().SNIP=''
         || FAKS.NIP:=INTRAST.F_NIP
         |? FAKS.KH().SNIP<>'' & FAKS.KH().SNIP=FAKS.KH().PESEL
         || FAKS.NIP:=FAKS.KH().NIP;
            INTRAST.F_NIP:=''
         || FAKS.NIP:=FAKS.KH().NIP;
            INTRAST.F_NIP:=FAKS.KH().SNIP
         ?}
      ?};
      {? FAKS.KH<>_kh0
      || win_disp()
      ?}
   ?}
|? _cur_tab=FAKS & FAKS.WHERE='N'
|| FAKS.NIP:=INTRAST.F_NIP
|? _cur_tab=ND | _cur_tab=ZD_NAG | _cur_tab=ZDP_NAG | _cur_tab=ZK_N
|| _kh0:=_cur_tab.KH;
   _snip:=exec('niptostr','#string',INTRAST.F_NIP);
   {? _snip=''
   || {? _cur_tab.KH<>null() & (_cur_tab.KH().SNIP='' | _cur_tab.KH().SNIP=_cur_tab.KH().PESEL)
      || _kh:=_cur_tab.KH
      || _kh:=null()
      ?}
   || {? _cur_tab.KH<>null() & _cur_tab.KH().SNIP=_snip
      || _kh:=_cur_tab.KH
      || _kh:=exec('FindInSet','#table','KH','SNIP',_snip,2,,,,null());
         {? _kh=null()
         || _win:=KH.win_sel('?');
            KH.win_sel('SEL');
            KH.cntx_psh();
            {? KH.select(,1,-1)
            || _kh:=KH.ref()
            ?};
            KH.cntx_pop();
            KH.win_sel(_win)
         ?}
      ?}
   ?};
   {? _kh<>_kh0
   || _cur_tab.KH:=_kh;
      {? _cur_tab=ND
      || _wyn:=exec('ae_n_kh','magdok_nag')
      |? _cur_tab=ZD_NAG
      || exec('ae_zd_kh','zamdst_nag');
         _wyn:=1
      |? _cur_tab=ZDP_NAG
      || _wyn:=exec('ae_zdpkh','zamdst_ptw')
      |? _cur_tab=ZK_N
      || _wyn:=exec('poz_kh','zamsiw_nag')
      || _wyn:=1
      ?};
      {? ~_wyn
      || _cur_tab.KH:=_kh0
      ?}
   ?};
   {? cur_tab().KH=null()
   || INTRAST.F_NIP:=''
   || {? _cur_tab.KH().SNIP<>'' & _cur_tab.KH().SNIP=_cur_tab.KH().PESEL
      || INTRAST.F_NIP:=''
      || INTRAST.F_NIP:=_cur_tab.KH().SNIP
      ?}
   ?};
   {? _cur_tab.KH<>_kh0
   || win_disp()
   ?}
?};
_wyn


\f_nip_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Przed edycją INTRAST.F_NIP (pole NIP w oknie dokumentu sprzedaży)
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? app_info('web_sesid')=''
|| _cur_tab:=cur_tab()
|? var_pres('__CURTAB')>0
|| _cur_tab:=__CURTAB
|| return(_wyn)
?};
{? _cur_tab=FAKS
|| _wyn:=(FAKS.WHERE='N' | exec('be_fa_kh','faktury_nag'));
   {? FAKS.T().KOR='T' & FAKS.T().HIS='N' & exec('faks_fld_czy_ed','faktury_nag','NIP')=0
   || _wyn:=0
   ?}
|? _cur_tab=ND
|| _wyn:=exec('be_khn','magdok_nag')
|? _cur_tab=ZD_NAG
|| _wyn:=exec('be_zd_kh','zamdst_nag')
|? _cur_tab=ZDP_NAG
|| _wyn:=exec('be_zdpkh','zamdst_ptw')
|? _cur_tab=ZK_N
|| _wyn:=exec('prz_kh','zamsiw_nag')
?};
_wyn


\f_nip_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: F3 dla INTRAST.F_NIP (pole NIP w oknie dokumentu sprzedaży) - wybór z listy kontrahentów
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| _cur_tab:=cur_tab()
|? var_pres('__CURTAB')>0
|| _cur_tab:=__CURTAB
|| return(~~)
?};
{? _cur_tab=FAKS & FAKS.WHERE<>'N'
|| {? FAKS.T().PAR='T'
   || FUN.info('Paragon nie ma ustawionych podpowiedzi dla pola NIP.'@)
   || _kh0:=FAKS.KH;
      _win:=KH.win_sel('?');
      KH.win_sel('SEL');
      KH.actions('SEL');
      KH.cntx_psh();
      KH.index('KOD');
      KH.prefix(2);
      {? _kh0=null()
      || KH.first()
      || KH.seek(_kh0)
      ?};
      {? KH.select(,1,-1)
      || _kh:=KH.ref()
      || _kh:=null()
      ?};
      KH.cntx_pop();
      KH.win_sel(_win);
      {? _kh<>null() & _kh<>_kh0
      || FAKS.KH:=_kh;
         _wyn:=exec('ae_fa_kh','faktury_nag');
         {? ~_wyn
         || FAKS.KH:=_kh0
         ?}
      ?};
      {? FAKS.KH=null()
      || FAKS.NIP:=INTRAST.F_NIP:=''
      |? FAKS.KH().SNIP=FAKS.KH().PESEL
      || FAKS.NIP:=FAKS.KH().NIP;
         INTRAST.F_NIP:=''
      || FAKS.NIP:=FAKS.KH().NIP;
         INTRAST.F_NIP:=FAKS.KH().SNIP
      ?};
      {? FAKS.KH<>_kh0
      || win_disp()
      ?}
   ?}
|? _cur_tab=ND | _cur_tab=ZD_NAG | _cur_tab=ZDP_NAG | _cur_tab=ZK_N
|| _kh0:=_cur_tab.KH;
   _win:=KH.win_sel('?');
   KH.win_sel('SEL');
   KH.actions('SEL');
   KH.cntx_psh();
   KH.index('KOD');
   KH.prefix(2);
   {? _kh0=null()
   || KH.first()
   || KH.seek(_kh0)
   ?};
   {? KH.select(,1,-1)
   || _kh:=KH.ref()
   || _kh:=null()
   ?};
   KH.cntx_pop();
   KH.win_sel(_win);
   {? _kh<>null() & _kh<>_kh0
   || _cur_tab.KH:=_kh;
      {? _cur_tab=ND
      || _wyn:=exec('ae_n_kh','magdok_nag')
      |? _cur_tab=ZD_NAG
      || exec('ae_zd_kh','zamdst_nag');
         _wyn:=1
      |? _cur_tab=ZDP_NAG
      || _wyn:=exec('ae_zdpkh','zamdst_ptw')
      |? _cur_tab=ZK_N
      || _wyn:=exec('poz_kh','zamsiw_nag')
      || _wyn:=1
      ?};
      {? ~_wyn
      || _cur_tab.KH:=_kh0
      ?}
   ?};
   {? _cur_tab.KH=null()
   || INTRAST.F_NIP:=''
   |? _cur_tab.KH().SNIP=_cur_tab.KH().PESEL
   || INTRAST.F_NIP:=''
   || INTRAST.F_NIP:=cur_tab().KH().SNIP
   ?};
   {? _cur_tab.KH<>_kh0
   || win_disp()
   ?}
?};
~~


\f_nip_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Przed wyświetleniem INTRAST.F_NIP (pole NIP w oknie dokumentu sprzedaży)
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| _cur_tab:=cur_tab()
|? var_pres('__CURTAB')>0
|| _cur_tab:=__CURTAB
|| return('')
?};
{? _cur_tab=FAKS
|| INTRAST.F_NIP:=exec('niptostr','#string',FAKS.NIP)
|? _cur_tab=ND | _cur_tab=ZD_NAG | _cur_tab=ZDP_NAG | _cur_tab=ZK_N
|| {? _cur_tab.KH().SNIP=_cur_tab.KH().PESEL
   || INTRAST.F_NIP:=''
   || INTRAST.F_NIP:=_cur_tab.KH().SNIP
   ?}
?};
''


\f_onip_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Po edycji INTRAST.F_ONIP (pole NIP odbiorcy w oknie dokumentu sprzedaży)
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_cur_tab:=cur_tab();
{? _cur_tab=FAKS & FAKS.WHERE='N'
|| FAKS.O_NIP:=INTRAST.F_ONIP
?};
_wyn


\f_onip_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Przed edycją INTRAST.F_ONIP (pole NIP odbiorcy w oknie dokumentu sprzedaży)
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_cur_tab:=cur_tab();
{? _cur_tab=FAKS
|| _wyn:=(FAKS.WHERE='N');
   {? FAKS.T().KOR='T' & FAKS.T().HIS='N' & exec('faks_fld_czy_ed','faktury_nag','NIP')=0
   || _wyn:=0
   ?}
?};
_wyn


\f_onip_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Przed wyświetleniem INTRAST.F_ONIP (pole NIP odbiorcy w oknie dokumentu sprzedaży)
::----------------------------------------------------------------------------------------------------------------------
_cur_tab:=cur_tab();
{? _cur_tab=FAKS
|| INTRAST.F_ONIP:=exec('niptostr','#string',FAKS.O_NIP)
?};
''


\f_kh_gv_sprawdz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Czy dopuszczalny FAKS.KH, sprawdzenie wg grupy VAT
::   WE: [_a] - tabela (FAKS)
::       [_b] - [0]/1 - bez komunikatu
::       [_c] - [0]/1 - możliwość zmiany kontrahenta i odborcy
::   WY: obj(WYNIK,MSG) WYNIK: 0 - nie / 1 - tak / -1 - zmieniono kontrahenta i odbiorcę
::----------------------------------------------------------------------------------------------------------------------
_tab:={? var_pres('_a')=type_of('')|| _a || 'FAKS' ?};
_msg_bez:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_zm_moz:={? var_pres('_c')=type_of(0) || _c || 0 ?};
_wyn:=obj_new('WYNIK','MSG');
_wyn.WYNIK:=1;
_wyn.MSG:='';

{? _tab='ND'
|| _f_kh:="ND.KH";
   _f_kh_naz:="ND.KH().NAZ";
   _f_dt:="ND.D"
|? _tab='ZK_N'
|| _f_kh:="ZK_N.KH";
   _f_kh_naz:="ZK_N.KH().NAZ";
   _f_dt:="ZK_N.DP"
|| _f_kh:="FAKS.KH";
   _f_kh_naz:="FAKS.KH().NAZ";
   _f_dt:="FAKS.DW"
?};
_kh:=_f_kh();
_kh_naz:=_f_kh_naz();
_dt:=_f_dt();

_gv:=exec('grvat_typ_kh','kontrahent',_kh,_dt);
{? _gv='CZ'
|| _wyn.WYNIK:=0;
   _khgzv_naz:='';
   {? var_pres('__KH_GZV')>0 & __KH_GZV<>null()
   || _khgzv_naz:=exec('kh_pole','edi_fo_ksef',__KH_GZV,'NAZ')
   ?};
   _wyn.MSG:='Kontrahent %1\njest członkiem grupy VAT %2,\ndata %3.'@
         [_kh_naz,_khgzv_naz,$_dt]
|? _gv='GW'
|| _wyn.WYNIK:=0;
   _wyn.MSG:='Kontrahent %1 jest grupą VAT licencjobiorcy, data %2.'@[_kh_naz,$_dt]
?};

{? _tab='FAKS'
|| {? _gv='CW'
   || FAKS.GV_WEW:='T'
   || FAKS.GV_WEW:='N'
   ?}
?};

{? ~_wyn.WYNIK & ~_msg_bez
|| {? _gv='CZ' & _zm_moz
   || _msg:=_wyn.MSG+'\nCzy zmienić kontrahenta?'@;
      {? FUN.ask(_msg)
      || {? _tab='ND'
         || ND.KH:=__KH_GZV;
            ND.KH_ODB:=__KHODB_CZV
         |? _tab='ZK_N'
         || ZK_N.KH:=__KH_GZV;
            ZK_N.ODB:=__KHODB_CZV
         || FAKS.KH:=__KH_GZV;
            FAKS.KH_ODB:=__KHODB_CZV
         ?};
         _wyn.WYNIK:=-1;
         _wyn.MSG:=''
      ?}
   || FUN.info(_wyn.MSG)
   ?}
?};
_wyn


\faks_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument sprzedaży - Usuń
::----------------------------------------------------------------------------------------------------------------------
_tab:=FAKS.sel_aget();
FAKS.sel_adel();

{? _tab.size()
|| _ok:=FUN.ask('Ilość dokumentów do usunięcia: %1.\n'
               'Operacja może być czasochłonna.\n\nCzy kontynuować?'@[$_tab.size()])
|| _ok:=2
?};
{? _ok=2
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:=exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF);
   _params.UIDREF:=FAKS.uidref();
   _params.AKCJA:='Usuń';

   exec('mp_run','#b__box',_params)
|? _ok=1
|| {? FAKS.SZ='S'
   || exec('ini_kom','#message','Usunięcie dokumentów sprzedaży'@);
      KOMM.init(250,,'Usunięcie dokumentów sprzedaży - menadżer procesów')
   || exec('ini_kom','#message','Usunięcie dokumentów zakupu'@);
      KOMM.init(250,,'Usunięcie dokumentów zakupu - menadżer procesów')
   ?};
   __kom_on:=1;
   FAKS.cntx_psh();
   _tab.clear();
   {? _tab.first()
   ||
      _size:=_tab.size();
      _licz:=0;
      _prgs:=100*_licz/_size;
      progress(_prgs,'\nTrwa usuwanie (%1\%)\n'@[$int(_prgs)],'Usuń'@);
      {!
      |?
         _licz+=1;
         _prgs:=100*_licz/_size;
         progress(_prgs,'\nTrwa usuwanie dokumentu %1 (%2\%)\n'@[
            exec('record','#to_string',FAKS.ref()),$int(_prgs)],'Usuń'@);
         {? (FAKS.clear(); FAKS.seek(_tab.REF,))
         || _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:=exec('faks_dolacz_act_uid','faktury_nag'
                              ,FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF);
            _params.UIDREF:=FAKS.uidref();
            _params.GRUPA:='T';
            _params.AKCJA:='Usuń';

            exec('mp_run','#b__box',_params);
            obj_del(_params)
         ?};
         _tab.next()
      !};
      prgs_clr()
   ?};
   FAKS.cntx_pop();
   exec('end_kom','#message');
   KOMM.select()
?};
obj_del(_tab)


\faks_zakoncz_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument sprzedaży - Zakocz - okno wertowania
::       Obsługa w formule main
::----------------------------------------------------------------------------------------------------------------------
_grupa:='T';
_Sel:=FAKS.sel_aget();
{? ~_Sel.first()
||
   _grupa:='N';
   _Sel.REF:=#FAKS.ref();
   _Sel.add()
?};

__pomoc_mg_opak:='';

_params:=exec('mp_run_a','#b__box');
_params.AKCJA:='Zakończ_wer';
{? _grupa='T'
||
   _params.GRUPA:='T';
   {? ~FUN.ask('Zakończyć rejestrację zaznaczonych dokumentów?'@) || return(0) ?};
   exec('ini_kom','#message','Grupowe zakończ'@)
?};

_loop:=_Sel.first();
{? _loop
||
   _size:=_Sel.size();
   _licz:=0;
   _prgs:=100*_licz/_size;
   progress(_prgs,'\nTrwa zakończenie (%1\%)\n'@[$int(_prgs)],'Zakończ'@)
?};
{!
|? _loop
|!
   _licz+=1;
   _prgs:=100*_licz/_size;
   progress(_prgs,'\nTrwa zakończenie dokumentu %1 (%2\%)\n'@[
      exec('record','#to_string',FAKS.ref()),$int(_prgs)],'Zakończ'@);
   {? FAKS.seek(_Sel.REF,)
   ||
      _params.ACT_UID:=exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF);
      _params.UIDREF:=FAKS.uidref();

      exec('mp_run','#b__box',_params)
   ?};
   _loop:=_Sel.next()
!};
prgs_clr();

{? _params.GRUPA='T'
||
   __kom_on:=1;
   exec('end_kom','#message');
   exec('grp_dr_done','param_wydr');
   FAKS.sel_adel()
?};

VAR_DEL.delete('__pomoc_mg_opak');
0


\faks_wycofaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dokument sprzedaży - Wycofaj
::----------------------------------------------------------------------------------------------------------------------
_Sel:=FAKS.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#FAKS.ref(); _Sel.add() ?};
_group:=_Sel.first() & _Sel.next();

_p2200:=exec('get','#params',2200,type_of(''),OPERATOR.USER)='T';
{? _group
||
   exec('ini_kom','#message','Grupowe akceptuj'@);
   _eoa:=1;
   _choice:=FUN.choice('Wybierz jedną z opcji wycofania akceptacji lub zakończenia dokumentu %1'@[FAKS.SYM]
      ,,'Akceptację i &zakończenie'@,'&Akceptację'@,'Za&kończenie');
   _fak_wyc1:={? _choice=1 || 'all' |? _choice=2 || 'akc' |? _choice=3 || 'end' || 'anuluj' ?}
||
   _blkprn:=
      FAKS.SZ='S' & FAKS.STAT_REJ='T'
      & _p2200
      & exec('prn2mail','faktury_nag',$FAKS.ref());
   _eoa:=exec('chk_EndOrAcc','#b__box'
      ,exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF)
      ,{? FAKS.SZ='S' || 'LSP_FAK_EASP' || 'LZK_ZAK_EZAK' ?});
   _fak_wyc1:=
      {? _blkprn
      || 'blk'
      |? FAKS.OBI_POW<>'' & FAKS.SZ='S'
      || 'obg'
      |? FAKS.STAT_REJ='A'
      || ''
      |? _eoa=1 & FAKS.STAT_REJ='T'
      || _choice:=FUN.choice('Wybierz jedną z opcji wycofania akceptacji lub zakończenia dokumentu %1'@[FAKS.SYM]
                   ,,'Akceptację i &zakończenie'@,'&Akceptację'@);
         {? _choice=1 || 'all' |? _choice=2 || 'akc' || 'anuluj' ?}
      |? (_eoa=1 | _eoa=2) & FAKS.STAT_REJ='Z'
      || {? FUN.ask('Wycofać zakończenie redakcji dokumentu %1?'@[FAKS.SYM]) || 'end' || 'anuluj' ?}
      |? _eoa=4 & FAKS.STAT_REJ='T'
      || {? FUN.ask('Wycofać akceptację dokumentu %1?'@[FAKS.SYM]) || 'akc' || 'amuluj' ?}
      || ''
      ?}
?};

{? _fak_wyc1='anuluj' || return(0) ?};

_loop:=_Sel.first();
{? _loop
||
   _size:=_Sel.size();
   _licz:=0;
   _prgs:=100*_licz/_size;
   progress(_prgs,'\nTrwa wycofanie akceptacji (%1\%)\n'@[$int(_prgs)],'Wycofaj'@)
?};
{!
|? _loop
|!
   _licz+=1;
   _prgs:=100*_licz/_size;
   progress(_prgs,'\nTrwa wycofanie akceptacji dokumentu %1 (%2\%)\n'@[
      exec('record','#to_string',FAKS.ref()),$int(_prgs)],'Wycofaj'@);
   {? FAKS.seek(_Sel.REF,)
   ||
      _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
      {? _r_lock
      ||
         _blkprn:=
            FAKS.SZ='S' & FAKS.STAT_REJ='T'
            & _p2200
            & exec('prn2mail','faktury_nag',$FAKS.ref());
         _eoa:=exec('chk_EndOrAcc','#b__box'
            ,exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF)
            ,{? FAKS.SZ='S' || 'LSP_FAK_EASP' || 'LZK_ZAK_EZAK' ?});
         _fak_wyc:=
            {? _blkprn
            || 'blk'
            |? FAKS.OBI_POW<>'' & FAKS.SZ='S'
            || 'obg'
            |? FAKS.STAT_REJ='A'
            || 'anulowany'
            |? FAKS.STAT_REJ='N'
            || 'nie zakonczony'
            |? _eoa=1 & FAKS.STAT_REJ='T' & (_fak_wyc1='all' | _fak_wyc1='akc')
            || _fak_wyc1
            |? (_eoa=1 | _eoa=2) & FAKS.STAT_REJ='Z' & (_fak_wyc1='all' | _fak_wyc1='end')
            || 'end'
            |? _eoa=4 & FAKS.STAT_REJ='T' & _fak_wyc1='all' | _fak_wyc1='akc'
            || 'akc'
            || ''
            ?};

         {? _fak_wyc='anulowany'
         ||
            _txt:='Dokument anulowany. Wycofanie akceptacji i zakończniea dokumentu niemożliwe.'@;
            {? _group || exec('add_kom','#message',FAKS.SYM,1,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?}

         |? _fak_wyc='nie zakonczony'
         ||
            _txt:='Nie zakończono rejestracji dokumentu. Wycofanie akceptacji i zakończniea dokumentu niemożliwe.'@;
            {? _group || exec('add_kom','#message',FAKS.SYM,1,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?}

         |? _fak_wyc='all' | _fak_wyc='end'
         ||
            exec('fak_wyc','faktury_nag',_group,,0)

         |? _fak_wyc='akc'
         ||
            {? ~exec('czy_z_ok','okresy',{? FAKS.SZ='S' || -2 || -3 ?},,FAKS.AR,FAKS.AM)
            ||
               1

            |? FAKS.KORYG='T'
            ||
               _txt:='Wystawiono korektę do dokumentu. Wycofanie akceptacji niemożliwe.'@;
               {? _group || exec('add_kom','#message',FAKS.SYM,1,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?}
            ||
               _del_autoadd:=exec('del_autoadd','dokum',FAKS.ref());
               FAKS.get();
               FAKS.CZY_DRUK:={? _del_autoadd=1 || 'X' || 'N' ?};
               FAKS.STAT_REJ:='Z';
               {? FAKS.put()
               || {? FAKS.T().KOR='T' & FAKS.T().HIS='T' || exec('hist_akc','faktury_nag',0,'Z') ?}
               ?}
            ?}

         |? _fak_wyc='blk'
         ||
            _txt:='Dokument został przesłany. Wycofanie akceptacji niemożliwe.'@;
            {? _group || exec('add_kom','#message',FAKS.SYM,1,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?}

         |? _fak_wyc='obg'
         ||
            _txt:='Dokument został przesłany do obiegu. Wycofanie akceptacji niemożliwe.'@;
            {? _group || exec('add_kom','#message',FAKS.SYM,1,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?}
         ||
            _txt:='Brak uprawnień do wycofania akceptacji i zakończenia dokumentu.'@;
            {? _group || exec('add_kom','#message',FAKS.SYM,1,gsub(_txt,'\n',' ')) || FUN.info(_txt) ?}
         ?};
         exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)

      |? ~_group
      ||
         exec('who_rlock_faks','faktury_nag')
      ?}
   ?};
   _loop:=_Sel.next()
!};
prgs_clr();
{? _group
||
   exec('end_kom','#message');
   FAKS.sel_adel()
?};
0


\usu_refk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: usuwa informacje o realizacjach dla danej faktury
::  OLD: \usu_refk/zkd.fml
::   WY: 0 lub najmniejsza rok
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
_put:=0;
_faks:=$FAKS.ref();
FAKS.cntx_psh();
ZK_RN.cntx_psh();

:: w biezacej masce
ZK_RN.use('zkhin'+ST.ODDZ+'__');
ZK_RN.clear();
ZK_RN.index('FK');
ZK_RN.prefix(_faks);
{? ZK_RN.first()
||
   {!
   |?
      _ref:=ZK_RN.ref();
      _ok:=ZK_RN.next();
      ZK_RN.cntx_psh();
      ZK_RN.clear();
      {? ZK_RN.seek(_ref)
      || {? ~_res | _res>(ZK_RN.DND~1) || _res:=ZK_RN.DND~1 ?};
         ZK_RN.FAKS:='';
         ZK_RN.SFK:='';
         ZK_RN.DFK:=date(0,0,0);
         {? ~ZK_RN.put(1)
         || undo()
         ?}
      ?};
      ZK_RN.cntx_pop();
      _ok
   !}
?};

:: w archiwum
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.last()
||
   {!
   |?
      ZK_RN.use('zkhin'+ST.ODDZ+($OKR.ROK+2));
      ZK_RN.clear();
      ZK_RN.index('FK');
      ZK_RN.prefix(_faks);
      {? ZK_RN.first()
      ||
         {!
         |?
            _ref:=ZK_RN.ref();
            _ok:=ZK_RN.next();
            ZK_RN.cntx_psh();
            ZK_RN.clear();
            {? ZK_RN.seek(_ref)
            || {? ~_res | _res>(ZK_RN.DND~1) || _res:=ZK_RN.DND~1 ?};
               ZK_RN.FAKS:='';
               ZK_RN.SFK:='';
               ZK_RN.DFK:=date(0,0,0);
               {? ~ZK_RN.put(1)
               || undo()
               ?}
            ?};
            ZK_RN.cntx_pop();
            _ok
         !}
      ?};

      OKR.prev()
   !}
?};
OKR.cntx_pop();

ZK_RN.cntx_pop();
FAKS.cntx_pop();
_res


\rozphist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: rozpisuje dana pozycje na kilka
::  OLD: \rozphist/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
FKOR.win_edit('ILZ');
FKOR.ILZ:=X_Xa.IL_KOR;
{? FUN.ask('Czy rozpisać daną pozycję?'@)
 & FKOR.edit("{? FKOR.ILZ>=0
              || FUN.info('Należy podać ilość ujemną.'@); 0
              |? (|FKOR.ILZ)>(|X_Xa.IL_KOR)
              || FUN.info('Nieprawidłowa ilość do zwrotu.'@); 0
              || 1
              ?}")
 & (|FKOR.ILZ)<>(|X_Xa.IL_KOR)
|| _ilp:=X_Xa.IL_KOR-FKOR.ILZ;
   X_Xa.IL_KOR:=_ilp;
   X_Xa.IL:=|X_Xa.IL_KOR;
   X_Xa.put(1);
   X_Xa.IL_KOR:=FKOR.ILZ;
   X_Xa.IL:=|X_Xa.IL_KOR;
   X_Xa.add(1)
?}


\delphist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: usuwa pozycje z rozpisania
::  OLD: \delphist/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? X_Xa.size>1
|| {? FUN.ask('Czy usunąć daną pozycję?\n\n'
         'UWAGA. Ilość z pozycji usuwanej zostanie uwzględniona\nw pierwszej pozycji na liście.'@)
   || _bylo:=X_Xa.IL_KOR;
      X_Xa.del();
      {? X_Xa.first || X_Xa.IL_KOR+=_bylo; X_Xa.put(1) ?}
   || 0
   ?}
|| FUN.info('Ponieważ jest tylko jedna pozycja\nnie można jej usunąć.'@)
?}


\zz2hist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po popraw kontrola pozycji na ZZ-tce - dla korekty historycznej
::   WY: ''-jest OK lub pole niewypelnione
::  OLD: \zz2hist/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('spr_magh','faktury_nag');
{? _wyn=''
||
   {? FKOR.DK_CZZ<>'' & (1+exec('FindInSet','#table','MG','MAGAZYNY',FKOR.MAGZZ,FKOR.MAGZZ,"MG.TYP",,,''))='E'
   || FUN.info('W magazynie ewidencyjnym obsługa cech jest niedostępna.'@); _wyn:='DK_CZZ'
   |? X_Xa.C<=0
   || FUN.info('Należy podać cenę dostawy.'@); _wyn:='C'
   |? X_Xa.CZY='T' & X_Xa.TW=date(0,0,0)
   || FUN.info('Należy uzupełnić termin ważności.'@); _wyn:='TW'
   ?}
?};
{? _wyn='' || _wyn:=exec('zz_kk','faktury_nag') ?};
_wyn


\spr_magh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: sprawdza czy podano prawidlowy magazyn i czy mamy do niego uprawnienia
::  OLD: \spr_magh/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? X_Xa.MG<>''
||
   {? (_ref_mg:=exec('FindInSet','#table','MG','MAGAZYNY',X_Xa.MG,X_Xa.MG))<>null
   ||
      USERS_UP.cntx_psh();
      USERS_UP.index('MG');
      USERS_UP.prefix(OPERATOR.USER,'MG',ST.ODDZ_KOD);
      _wyn:={? USERS_UP.find_key(_ref_mg)
            || {? USERS_UP.MG().SKL='T'
               || FUN.info('Magazyn typu skład celny jest niedostępny.'@); 'MAGZZ'
               || ''
               ?}
            || FUN.info('W oddziale %1 brak uprawnień do podanego magazynu.'@[ST.ODDZ_KOD]); 'MAGZZ'
            ?};
      USERS_UP.cntx_pop()
   ||
      FUN.info('Nieprawidłowy symbol magazynu.'@); X_Xa.MG:=ST.MAG().SYM; _wyn:='MAGZZ'
   ?}
||
   FUN.info('Należy podać magazyn.'@); _wyn:='MAGZZ'
?};
_wyn


\adzz_pal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: przypisuje palete do zaznaczonych pozycji
::  OLD: \adzz_pal/mws.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=X_Xa.sel_aget();
X_Xa.sel_adel();
X_Xa.cntx_psh();
_wgtab:=_tab.size;
{? ~_wgtab & exec('FindInSet','#table','MG','MAGAZYNY',X_Xa.MG,X_Xa.MG,"MG.PAL",,,'')='N'
|| FUN.info('Magazyn bez obsługi palet.\nOperacja niedostępna.'@)
|? {? _wgtab
   || FUN.ask('Czy przypisać paletę do zaznaczonych pozycji?'@)
   || FUN.ask('Czy przypisać paletę do pozycji?'@)
   ?}
|| PAL.f_clear();
   PAL.index('PAL');
   PAL.prefix();
   PAL.f_set('KODK',,'\"PAL\".STAN=\'N\' and \"PAL\".AKT=\'T\' and \"PAL\".RSQL=\'\' and \"PAL\".ODD=\':_a\'',ST.ODDZ);
   EANX.WHERE:='X';
   PAL.win_sel('SEL');
   PAL.win_dict('SEL');
   PAL.actions('SEL','WACTYMGZ:E');
   PAL.find_key(X_Xa.PAL);
   _ilen:=0;
   _pal:=0;
   {? PAL.select(,1)
   || {? _wgtab
      || _tab.clear;
         {? _tab.first()
         || {!
            |? _pal:=1;
               {? (X_Xa.clear(); X_Xa.seek(_tab.REF,))
                & X_Xa.MG<>'' & (_pal:=exec('FindInSet','#table','MG','MAGAZYNY',X_Xa.MG,X_Xa.MG,"MG.PAL",,,'')='T')
               || X_Xa.PAL:=PAL.KODK;
                  X_Xa.put(1)
               |? X_Xa.MG<>'' & ~_pal
               || _ilen+=1
               ?};
               _tab.next()
            !}
         ?};
         {? _ilen>0
         || FUN.info('Nie przypisano palet dla: %1 pozycji.\nMagazyn(y) bez obsługi palet.'@[form(_ilen,,0,'99')])
         ?}
      |? X_Xa.MG<>'' & (_pal:=exec('FindInSet','#table','MG','MAGAZYNY',X_Xa.MG,X_Xa.MG,"MG.PAL",,,'')='T')
      || X_Xa.PAL:=PAL.KODK;
         X_Xa.put(1)
      |? X_Xa.MG<>'' & ~_pal
      || FUN.info('Magazyn %1 bez obsługi palet.\nPrzypisanie palety niemożliwe.'@[X_Xa.MG])
      ?}
   ?};
   PAL.f_clear()
?};
X_Xa.cntx_pop();
~~


\f3zz_pal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: klawisz F3 pola palety dla dok.zwrotu
::  OLD: \f3zz_pal/mws.fml
::----------------------------------------------------------------------------------------------------------------------
PAL.f_clear();
PAL.index('PAL');
PAL.prefix();
PAL.f_set('KODK',,'\"PAL\".STAN=\'N\' and \"PAL\".AKT=\'T\' and \"PAL\".RSQL=\'\' and \"PAL\".ODD=\':_a\'',ST.ODDZ);
EANX.WHERE:='X';
PAL.win_sel('SEL');
PAL.win_dict('SEL');
PAL.actions('SEL','WACETYMGZ:E');
PAL.find_key(X_Xa.PAL);
{? PAL.select(,1) || X_Xa.PAL:=PAL.KODK ?};
PAL.f_clear();
~~


\pozz_pal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: po redakcji pola palety dla dok.zwrotu
::  OLD: \pozz_pal/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? X_Xa.PAL<>''
|| PAL.cntx_psh();
   PAL.index('PAL');
   PAL.prefix(X_Xa.PAL,X_Xa.PAL);
   {? PAL.first()
   || {? PAL.STAN='N' & PAL.AKT='T' & PAL.RSQL=''
      || X_Xa.PAL:=PAL.KODK
      || FUN.info('Brak palety o podanym kodzie kreskowym lub paleta jest już wykorzystana.'@);
         X_Xa.PAL:=''
      ?}
   || PAL.prefix(X_Xa.PAL);
      {? PAL.first()
      || X_Xa.PAL:='';
         {!
         |? {? PAL.STAN='N' & PAL.AKT='T' & PAL.RSQL=''
            || X_Xa.PAL:=PAL.KODK;
               0
            || PAL.next()
            ?}
         !};
         {? X_Xa.PAL=''
         || FUN.info('Brak palety o podanym kodzie kreskowym lub paleta jest już wykorzystana.'@);
            X_Xa.PAL:=''
         ?}
      || FUN.info('Brak palety o podanym kodzie kreskowym lub paleta jest już wykorzystana.'@);
         X_Xa.PAL:=''
      ?}
   ?};
   PAL.cntx_pop()
|| 1
?}


\f3_magzz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: F3 dla magazynu dla zwrotu - korekta faktury
::  OLD: \f3_magzz/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_mg_ref:=null;
MG.cntx_psh();
MG.index('MAGAZYNY');
MG.prefix(X_Xa.MG,X_Xa.MG);
{? MG.first() || _mg_ref:=MG.ref() ?};
MG.cntx_pop();
{? exec('upr_wys','users','MG',,'MGSEL',1,_mg_ref,1)
|| FKOR.MAGZZ:=USERS_UP.MG().SYM;
   X_Xa.MG:=FKOR.MAGZZ
?};
1


\po_magzz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji magazynu dla zwrotu - korekta faktury
::  OLD: \po_magzz/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_mg:=exec('FindInSet','#table','MG','MAGAZYNY',fld(),fld());

{? fld()<>''
   & ~(exec('canByDoneZz2Mg','magdok_wspolne',fld(),FKOR.TYPYDOK) & exec('czy_z_ok','okresy',1,,,,_mg))
|| return(0)
?};

X_Xa.MG:=FKOR.MAGZZ;
_beermg:=BEER.MG;
BEER.MG:=_mg;

{? (1+BEER.MG().TYP)<>'D'
|| X_Xa.DK_C:='';
   FKOR.DK_CZZ:=''
?};
{? exec('FindInSet','#table','MG','MAGAZYNY',X_Xa.MG,X_Xa.MG,"MG.PAL",,,'')<>'T'
|| X_Xa.PAL:=''
?};
X_Xa.put(1);
BEER.MG:=_beermg;
1


\dz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Uzupełnienie daty zwrotu
::   WE:
::   WY: 0 - nie uzupełniono datę zwrotu, 1 - uzupełniono datę zwrotu
::----------------------------------------------------------------------------------------------------------------------
{? FAP.FAKS().WHERE='L' | FAKS.SZ='Z' || return(1) ?};
{? FAKS.DZ<>date(0,0,0) || return(1) ?};
undefine();
define('DZ',date(0,0,0),'Data zwrotu');
def_btn('text='+'&Zapisz'@+', btn_label_align=center',"'key:F2'");
def_btn('text='+'&Anuluj'@+', btn_label_align=center',"'key:Esc'");
{? def_edit("
   {? DEFINE.DZ=date(0,0,0)
   || FUN.info('Należy podać datę zwrotu.'@);
      'DZ'
   |? DEFINE.DZ<FAKS.D
   || FUN.info('Data zwrotu powinna być większa lub równa dacie sprzedaży %1.'@[$FAKS.D]);
      'DZ'
   || ''
   ?}",'Korekta sprzedaży'@)
|| FAKS.DZ:=DEFINE.DZ;
   FAKS.put()
|| 0
?}


\kh_faks_colr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Marcin Makselon [12.30]
:: OPIS: Ustala procent wykonania dla pola z informacją o stanie wysyłki załączników
::  OLD: \kh_faks_colr/fak_fun.fml
::----------------------------------------------------------------------------------------------------------------------
exec('kh_dod_ini','kontrahent');
_ile:=_ile_m:=_ile_w:=0;
_ile_b:=_ile_u:=_ile_o:=0;

DOKUM.cntx_psh();
_Names:=DOKUM.names();
_loop:=_Names.first();
{!
|? _loop
|!
   DOKUM.use(_Names.NAME);
   _msk:=DOKUM.name()+2; {? _msk='um' || _msk:='og' ?};
   DOKUMLOG.use('dol'+_msk);
   DOKUM.cntx_psh();
   DOKUMLOG.cntx_psh();
   DOKUM.index('STAN');
:: dokumentów ogółem (żeby w ogóle wyświetlać progress)
   DOKUM.prefix(REF.FIRMA,$FAKS.ref());
   _ile+=DOKUM.size();
:: dokumentów wystawionych do wysłania e-mailem
::   DOKUM.prefix(REF.FIRMA,$FAKS.ref(),'T');
::   _ile_m+=DOKUM.size();
:: dokumentów wysłanych e-mailem
   DOKUM.prefix(REF.FIRMA,$FAKS.ref(),'T','T');
   _ile_w+=DOKUM.size();
   {? exec('is_active','businesslink3')
   || DOKUM.index('STANB');
:: dokumentów wysłanych do Businesslinka
      DOKUM.prefix(REF.FIRMA,$FAKS.ref(),'T');
      _ile_b+=DOKUM.size();
:: dokumentów unieważnionych w Businesslink
      DOKUM.prefix(REF.FIRMA,$FAKS.ref(),'X');
      _ile_u+=DOKUM.size();
:: dokumentów odebranych w Businesslinku
      DOKUM.prefix(REF.FIRMA,$FAKS.ref(),'T','T');
      _ile_o+=DOKUM.size()
   ?};
   DOKUM.cntx_pop();
   DOKUMLOG.cntx_pop();
   _loop:=_Names.next()
!};
DOKUM.cntx_pop();

{? _ile
|| {? exec('is_active','businesslink3')
   || FZ.SW:=$floor(((_ile_w+_ile_b+_ile_u)/_ile)*100)+' % / '+$floor((_ile_o/_ile)*100)+' %'
   || FZ.SW:=$floor((_ile_w/_ile)*100)+' %'
   ?}
|| FZ.SW:=''
?};
''


\faks_dazal_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Po redakcji FAKS.DA_ZAL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? params_get().var_fakpop.FLD<>fld()
||
   _fakz:=params_get().var_fakpop.FAKZ;
   {? exec('FindAndGet','#table',FAKZ,_fakz,,"FAKZ.DA_ZAL",date(0,0,0))<>date(0,0,0)
   ||
::    Zwolnie zlecenia fakturowania jeśli na zleceniu podano inną datę otrzymania zaliczki niż na fakturze
      _fakz:=params_get().var_fakpop.FAKZ;
      _r_lock:=params_get().var_fakpop.FAKZ_RLOCK;
      {? _fakz
      ||
         FAKZ.cntx_psh();
         FAKZ.use(ref_name(_fakz));
         FAKZ.prefix();
         {? FAKZ.seek(_fakz)
         ||
            exec('r_unlock_one','#table',FAKZ,FAKZ.ref(),_r_lock)
         ?};
         FAKZ.cntx_pop()
      ?};
      params_get().var_fakpop.FAKZ:=null();
      params_get().var_fakpop.FAKZ_RLOCK:=0
   ?};
   exec('set_efld_opt','faktury_nag')
?};
1


\zlf_slo_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Słownik zleceń fakturowania typu zaliczka
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_return:=fld();
FAKZ.cntx_psh();
FAKZ.prefix();
FAKZ.f_clear();
_sort:='DF';
_from:='';
_where:=$"
   FAKZ.AR=:_a
   and FAKZ.AM=:_b
   and FAKZ.FIRMA=:_c
   and FAKZ.ODDZ=':_d'
   and FAKZ.STS=':_e'
   and FAKZ.KH=:_f
   and FAKZ.STAT_REJ='T'
   and FAKZ.CZY_FAK='N'
   and FAKZ.ZAL='T'
   and FAKZ.KOREKTA='N'
";
FAKZ.f_set(_sort,_from,_where,ST.AR,ST.AM,REF.FIRMA,ST.ODDZ,$ST.STS,FAKS.KH);
FAKZ.win_sel('SEL_ONE');
{? FAKZ.select()
||
   {? _r_lock:=exec('r_lock_one','#table',FAKZ,FAKZ.ref())
   ||
::    Zwolnie zlecenia fakturowania jeśli na zleceniu podano inną datę otrzymania zaliczki niż na fakturze
      _fakz_b:=params_get().var_fakpop.FAKZ;
      _r_lock_b:=params_get().var_fakpop.FAKZ_RLOCK;
      {? _fakz_b
      ||
         FAKZ.cntx_psh();
         FAKZ.use(ref_name(_fakz_b));
         FAKZ.prefix();
         {? FAKZ.seek(_fakz_b)
         ||
            exec('r_unlock_one','#table',FAKZ,FAKZ.ref(),_r_lock_b)
         ?};
         FAKZ.cntx_pop()
      ?};
      exec('FindAndGet','#table',FAKZ,params_get().var_fakpop.FAKZ,,"FAKZ.r_unlock()");
::    Zablokowanie zlecenia fakturowania do czasu zakończenia edycji nagłówka faktury
      params_get().var_fakpop.FAKZ:=FAKZ.ref();
      params_get().var_fakpop.FAKZ_RLOCK:=_r_lock;
      params_get().var_fakpop.FLD_F3:=1;
      {? FAKS.WHERE='L'
      || FAKS.D:=FAKZ.DA_ZAL
      || FAKS.DA_ZAL:=FAKZ.DA_ZAL
      ?};
      {? FAKS.UWAGI=null()
      || {? FAKZ.UWAGI || FAKS.memo_set(FAKZ.memo_get('r','UWAGI'),'UWAGI') ?}
      ?};
      _return:=FAKS.KW_ZAL:=FAKZ.WB
   ||
      exec('who_rlock_fakz','faktury_wspolne')
   ?}
?};
FAKZ.f_clear();
FAKZ.cntx_pop();
_return


\faks_kopiuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Dokument sprzedaży/zakupu - Kopiuj
::----------------------------------------------------------------------------------------------------------------------
_act_uid:=exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF);
_sz:=FAKS.SZ;

{? _sz='S' & _act_uid<>'LSP_FAK_DRFP'
|| {? FAKS.sel_size()=0
   || FUN.info('Kopiować można tylko dokumenty sprzedaży pozostałe.'@)
   || KOMM.add('Dokument %1 nie jest dokumentem sprzedaży pozostałym.'@[FAKS.SYM])
   ?}

|? _sz='Z' & _act_uid='LZK_ZAK_DZAL'
|| {? FAKS.sel_size()=0
   || FUN.info('Nie można kopiować zaliczkowego dokumentu zakupu.'@)
   || KOMM.add('Dokument %1 jest zaliczkowym dokumentem zakupu. Kopiowanie dokumentu jest niedostępne.'@[FAKS.SYM])
   ?}

|? _sz='Z' & ~(_act_uid='LZK_ZAK_DZAK' | _act_uid='LZK_ZAK_DWEW')
|| {? FAKS.sel_size()=0
:   || FUN.info('Kopiować można tylko zwykłe dokumenty zakupu lub dokumenty wewnętrzne.'@)
:   || KOMM.add('Dokument %1 nie jest zwykłym dokumentem zakupu lub dokumentem wewnętrznym.'@[FAKS.SYM])
   || FUN.info('Nie można kopiować dokumentów zakupu i wewnętrznych utworzonych z dokumentu magazynowego.'@)
   || KOMM.add('Dokument %1 jest dokumentem zakupu lub dokumentem wewnętrznym utworzonym z dokumentu magazynowego.'@[FAKS.SYM])
   ?}

|? FAKS.T().ZAL_ROZ='S'
|| {? FAKS.sel_size()=0
   || FUN.info('Nie można kopiować dokumentu w 100\% rozliczającego zaliczkę.'@)
   || KOMM.add('Dokument %1 jest dokumentem w 100\% rozliczającym zaliczkę.'@[FAKS.SYM])
   ?}
|| _can_continue:=0;
   {? FAKS.sel_size()=0
   || _valid:="exec('kopiuj_valid','faktury_nag',1)";
      _red:=HELP.mk_edit(
         '|--|'+{? _sz='S' || 'Kopiowanie dokumentu sprzedaży'@ || 'Kopiowanie dokumentu zakupu'@ ?},,'copyfaks_di'
      );
      {? _sz='S'
      || HELP.win_efld(_red,,'DT',,,,,,'Data wystawienia'@);
         HELP.win_efld(_red,,'D',,,,,,'Data sprzedaży'@);
         HELP.win_efld(_red,,'LK',,,,,,'Liczba kopii'@)
      || HELP.win_efld(_red,,'D',,,,,,'Data zakupu'@)
      ?};
      exec('ok_esc','#window',HELP,_red);
      HELP.win_edit(_red);
      HELP.DT:=exec('thismont','faktury_wspolne');
      HELP.D:=date();
      HELP.LK:=1;
      {? HELP.edit(_valid)
      || _date:={? _sz='S'|| HELP.DT || HELP.D ?};
         _date_s:=HELP.D;
         _copy:=HELP.LK;
         _b_prel:=null();
         _can_continue:=1
      ?}
   || _date:=params_get().env.DATE;
      _date_s:=params_get().env.DATE_S;
      _copy:=1;
      _b_prel:=params_get().env.B_PREL;
      _can_continue:=1
   ?};

   {? _can_continue>0
   ||
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:={? _sz='S' || 'LSP' || 'LZK' ?};

      _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:=_act_uid;
      _params.CONTEXT:=obj_new('IN_FAKS','DATE','DATE_S','OUT_FAKS');
         _params.CONTEXT.IN_FAKS:=FAKS.ref();
         _params.CONTEXT.DATE:=_date;
         _params.CONTEXT.DATE_S:=_date_s;
         _params.CONTEXT.OUT_FAKS:=null();
      _params.AKCJA:='Kopiuj';
      _params.PROC_START:='T';
      _params.GRUPA:={? FAKS.sel_size()>0 || 'T' || 'N' ?};
      _params.B_PREL:=_b_prel;

      _old_AR:=ST.AR;
      _old_AM:=ST.AM;
      _args.AR:=_date~1;
      _args.AM:=_date~2;
      FAKS.cntx_psh();
      __PARSES.setVal('OkresRok',_args);

      {? FAKS.SZ='S' & exec('kh_blok','faktury_nag','faks_kopiuj')
      ||
         FAKS.cntx_pop();~~
      ||
         _wyn:='';
         {! _it:=1.._copy
         |!
:: limit kredytowy
            {? _wyn='' & FAKS.SZ='S' & FAKS.SYMF=''
            ||
               _klim:=null;
               _dok_mag:='N';
               KH_DOD.KLIM:=__War_f('KH_DOD','KLIM',FAKS.KH);
               {? KH_DOD.KLIM<>null
               ||
                  _klim:=KH_DOD.KLIM;
                  _dok_mag:=KH_DOD.KLIM().DOK_SUM
               ||
                  _klim:=FAKS.KH().GRKH().KLIM;
                  _dok_mag:=FAKS.KH().GRKH().KLIM().DOK_SUM
               ?};
               _cur_brutto:={? FAKS.WHERE='M' & _dok_mag='T' || 0 || _cur_brutto:=FAKS.BRUTTO ?};
               {? ~exec('lim_kred','limit_kredyt',FAKS.KH,date(),INFO.NAROD,5,_klim,_cur_brutto,FAKS.sel_size()=0,'faktu')
               || _wyn:='przekroczono limit'
               ?}
            ?};
            {? _wyn=''
            ||
               FAKS.cntx_psh();
               exec('mp_run','#b__box',_params);
               FAKS.cntx_pop();
               params_get().env.B_PREL:=_params.B_PREL;
               {? FAKS.sel_size()=0
               || {? _old_AR=_args.AR & (FILTER.STATUS='R' | _old_AM=_args.AM)
                  || {? FAKS.seek(_params.CONTEXT.OUT_FAKS)
                      & (FAKS.f_active()=1 | (FAKS.f_active()>1 & FAKS.f_test()))
                     || FAKS.f_add()
                     ?}
                  ?}
               ?}
            ?};
            {? _it=_copy & FAKS.T().ZAK='T' & FAKS.KH & FAKS.NRKSEF=''
            || exec('czy_zak_powinien_ksef','zbl_dok',FAKS.KH,FAKS.T,FAKS.DW,1)
            ?}
         !};
         FAKS.cntx_pop();
         {? FAKS.sel_size()=0
         || {? _old_AR=_args.AR & (FILTER.STATUS='R' | _old_AM=_args.AM)
            || {? FAKS.f_active()>1
               || FAKS.f_rfresh();
                  FAKS.f_seek(_params.CONTEXT.OUT_FAKS)
               || FAKS.seek(_params.CONTEXT.OUT_FAKS)
               ?}
            || FUN.info(
                  'Dokument %1 został utworzony w okresie %2/%3.'@
                  [exec('record','#to_string',_params.CONTEXT.OUT_FAKS),$_args.AR,form(_args.AM,-2)]
               )
            ?}
         || params_get().env.OUT_FAKS:=_params.CONTEXT.OUT_FAKS
         ?}
      ?};
      _args.AR:=_old_AR;
      _args.AM:=_old_AM;
      __PARSES.setVal('OkresRok',_args);
      AreaTitle.setTitle()
   ?}
?};
~~


\faks_kopiuj_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Dokumenty sprzedaży/zakupu - Kopiuj (przed grupą rekordów)
::----------------------------------------------------------------------------------------------------------------------
_sz:=FAKS.SZ;
_valid:="exec('kopiuj_valid','faktury_nag',0)";
_red:=HELP.mk_edit(
   '|--|'+{? _sz='S' || 'Kopiowanie dokumentu sprzedaży'@ || 'Kopiowanie dokumentu zakupu'@ ?},,'copyfaks_d'
);
{? _sz='S' || HELP.win_efld(_red,,'DT',,,,,,'Data wystawienia'@) ?};
HELP.win_efld(_red,,'D',,,,,,{? _sz='S' || 'Data sprzedaży'@ || 'Data zakupu'@ ?});

exec('ok_esc','#window',HELP,_red);
HELP.win_edit(_red);
HELP.D:=date();
HELP.DT:=exec('thismont','faktury_wspolne');
{? HELP.edit(_valid)
|| params_get().env.DATE_S:=HELP.D;
   params_get().env.DATE:={? _sz='S' || HELP.DT || HELP.D ?};
   params_get().env.B_PREL:=null();
   KOMM.init(,,{? _sz='S' || 'Kopiowanie dokumentów sprzedaży'@ || 'Kopiowanie dokumentów zakupu'@ ?});
   1
|| 0
?}


\faks_kopiuj_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Dokumenty sprzedaży/zakupu - Kopiuj (po grupie rekordów)
::----------------------------------------------------------------------------------------------------------------------
{? params_get().env.OUT_FAKS=~~
|| KOMM.select()
|| {? params_get().env.OUT_FAKS=null()
   || _ar:=0; _am:=0
   || _ar:=exec('FindAndGet','#table',FAKS,params_get().env.OUT_FAKS,,"AR",0);
      _am:=exec('FindAndGet','#table',FAKS,params_get().env.OUT_FAKS,,"AM",0)
   ?};
   {? ref_name(params_get().env.OUT_FAKS)=FAKS.name() & (FILTER.STATUS='R' | _am=ST.AM)
   || KOMM.select();
      FAKS.f_rfresh();
      {? FAKS.f_test() || FAKS.f_seek(params_get().env.OUT_FAKS) || FAKS.seek(params_get().env.OUT_FAKS)?}
   || {? params_get().env.OUT_FAKS<>null()
      || KOMM.add('Dokumenty zostały utworzone w okresie %1/%2.'@[$_ar,form(_am,-2)])
      ?};
      KOMM.select()
   ?}
?};
~~


\faks_dane_do_intrastat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Dane do intrastat - nagłówek dokumentu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? ~FAKS.get() || return(0) ?};
{? exec('nie_dotyczy','intrastat','FAKS') || return(0) ?};
FAKS.cntx_psh();
FAKS.win_edit('INTRA');
exec('set_efld_opt','faktury_nag');
{? FAKS.INTRAKC='T' | FAKS.INTRA='T'
||
   FAKS.display()
||
:: można zmieniać dane do intrastat czasu uwzględnienia dokumentu w deklaracji
   _wyn:=0;
   _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref());
   {? ~_r_lock
   ||
      exec('who_rlock_faks','faktury_nag')
   ||
      _ist_typ:=FAKS.IST_TYP;
      {? FAKS.edit("exec('chk_f','faktury_nag',0,FAKS.AKC)")
      ||
         _result:=FAKS.put();
         {? _result
         ||
            FAP.cntx_psh();
            FAP.index('FAP');
            FAP.prefix(FAKS.ref);
            {? FAP.first()
            ||
               {!
               |?
                  {? _ist_typ<>FAKS.IST_TYP
                  ||
:: ustawienie danych intrastat rejestrowanych w pozycji
                     {? FAKS.IST_TYP=''
                     ||
:: zerowanie
                        _mat:=FAP.M;
                        FAP.M:=null();
                        exec('ist_wart_fap','faktury_wspolne',0,0);
                        FAP.M:=_mat

                     |? _ist_typ=''
                     ||
:: uzupełnienie
                         exec('liczfak_ist','faktury_wspolne',0,1)
                     ?}
                  ?};
:: przepisanie danych intrastat z nagłówka dokumentu
                  exec('istatr_faks2fap','faktury_nag');
                  _result:=FAP.put();
                  _result & FAP.next
               !}
            ?};
            FAP.cntx_pop()
         ?}
      ?};
      exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
   ?}
?};
FAKS.cntx_pop();
_result


\faks_dane_do_intrastat_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Dane do intrastat - akceptacja
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~FAKS.get() || return(0) ?};
{? FAKS.IST_TYP=''
||
   FUN.info('Dokument nie dotyczy Intrastat.'@)

|? FAKS.INTRAKC='T'
||
   FUN.info('Dane do Intrastat są zaakceptowane.'@)
||
   _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref());
   {? ~_r_lock
   ||
      exec('who_rlock_faks','faktury_nag')
   ||
      {? FUN.ask('Czy zaakceptować dane do Intrastat?'@)
      ||
         exec('ini_kom','#message','Akceptacja danych do Intrastat');
         _err:=0;
         {? exec('chk_f','faktury_nag',1,'T')<>''
         ||
            _err:=1

         |? exec('chk_fakp_akc','faktury_poz')
         ||
            FAKS.INTRAKC:='T';
            FAKS.put()
         ||
            _err:=1
         ?};
         {? _err
         ||
            exec('end_kom','#message')
         ?}
      ?};
      exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
   ?}
?}


\faks_dane_do_intrastat_wyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Dane do intrastat - wycofanie akceptacji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~FAKS.get() || return(0) ?};
{? FAKS.INTRAKC<>'T'
||
   FUN.info('Dane do intrastat są niezaakceptowane.'@)
||
   _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref());
   {? ~_r_lock
   ||
      exec('who_rlock_faks','faktury_nag')
   ||
      {? FUN.ask('Czy wycofać akceptację danych do intrastat?'@)
      ||
         FAKS.INTRAKC:='N';
         FAKS.put()
      ?};
      exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
   ?}
?}


\intrast_pops
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Ustawia zmienną INTRAST.POPS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
INTRAST.POPS:='N';
{? FAKS.T().UE='N'
||
   FUN.info('Dokument nieunijny.'@
      +'\nPoprawa danych do Intrastat niedostępna.'@)

|? FAKS.WHERE='L'
||
   FUN.info('Dokument zaliczkowy.'@
      +'\nPoprawa danych do Intrastat niedostępna.'@)
||
   _cur_dok:=
      {? FAKS.IST_OKR='' || '' || exec('prog','intrastat',date(#(4+FAKS.IST_OKR),#(FAKS.IST_OKR+2),1),FAKS.IST_TYP) ?};
:: _nextdok=
::    '  '-brak kolejnych dokumentow powiązanych lub dokumenty te nie dotyczą intrastat
::    'P '-na intrastat w progu P
::    'PP'-na intrastat w progu P i poza w porgu P
::    'PS'-na intrastat w progu P i poza w progu S
::    'S '-na intrastat w progu S
::    'SP'-na intrastat w progu S i poza w progu P
::    'SS'-na intrastat w progu S i poza w progu S
::    ' P'-poza intrastat w progu P
::    ' S'-poza intrastat w progu S
   _nextdok:=
      {? FAKS.AKC='N'
      || ' '+_cur_dok
      || exec('chk_nextdok','faktury_nag',FAKS.ref)
      ?};
   {? FAKS.INTRA='T' & _cur_dok='S'
   ||
:: poprawiany dokument jest na intrastat w progu S
      FUN.info('Dokument uwzględniony w deklaracji Intrastat w progu szczegółowym.'@
         +'\nPoprawa danych do Intrastat niedostępna.'@)

   |? (1+_nextdok)=' ' & (_nextdok+1)<>' '
   ||
:: kolejne dokumenty nie są w deklaracji intrastat i dotyczą intrastat
:: dlatego uzupełniamy dane aby kolejne dokumenty bazujące na dokumencie poprawianym zostały właściwie uwzględnione
:: w deklaracji intrastat
::    INTRAST.POPS - zmienn wykorzystywana przy rejestracji danych intrastat:
::                   ='N' dokument przed zakończeniem redkacji
::                   ='P' dokument po zakończeniu redakcji - redakcja jak dla progu P
::                   ='S' dokument po zakończeniu redakcji - redkacja jak dla progu S
::                   ='T' dokument po zakończeniu redakcji - redkacja jak dla progu S z blokadą wartości jak dla progu P
      {? FAKS.INTRA='T'
      ||
::       dokument jest w deklaracji intrastat więc można tylko uzupełnić wartość statystyczną
         {? (_nextdok+1)='S'
         ||
            INTRAST.POPS:='T'
         ||
            FUN.info('Dokument uwzględniony w deklaracji Intrastat i kolejne dokumenty nie dotyczą progu szczegółowego.'
               +'\nPoprawa danych do Intrastat niedostępna.'@)
         ?}
      ||
::       dokument jest poza deklaracją więc można redagować dane w zakresie określonym dla progu P lub S
         INTRAST.POPS:=_nextdok+1
      ?}

   |? (1+_nextdok)<>' '
   ||
      FUN.info('Kolejne dokumenty powiązane uwzględnione w deklaracji Intrastat.\n'@+
         +'\nPoprawa danych do Intrastat niedostępna.'@)
   ||
      FUN.info('Brak kolejnych dokumentów powiązanych dotyczących Intrastat.'
         '\nPoprawa danych do Intrastat niedostępna.'@)
   ?}
?}


\faks_kzf_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: faktury korekty zbiorczej - okno wertowania
::   WE: [_a] - 0/1 - zakładka w obszarze
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab_in_area:={? var_pres('_a')=type_of(0) || _a || 0 ?};

_wer:=FAKS_KZF.mk_sel('Korygowane dokumenty'@,,,'cslkkejccbdlaet',,,,,'U','N');
FAKS_KZF.win_fld(_wer,,'FAK_DW',,,10,,,'Data wystawienia faktury'@);
FAKS_KZF.win_fld(_wer,,'FAK_TYP','T',,10,,,'Typ faktury'@);
FAKS_KZF.win_fld(_wer,,'FAK_SYM',,,20,,,'Symbol faktury'@);
FAKS_KZF.win_fld(_wer,,'KOR_NR',,,10,,,'Nr korekty'@);
FAKS_KZF.win_fld(_wer,,'KOR_SYM',,,20,,,'Symbol korekty'@);
FAKS_KZF.win_fld(_wer,,'NETTO',,,12,2,,'Netto'@);
FAKS_KZF.win_fld(_wer,,'BRUTTO',,,12,2,,'Brutto'@);

{? _tab_in_area | FAKS.AKC='N'
||
   {? _tab_in_area | FAKS.WHERE<>'N'
   ||
      _fb:="exec('faks_kzf_gen','faktury_nag1')";
      FAKS_KZF.win_act(_wer,1,'Formuła','&Generuj listę'@@,,'Generuje listę faktur do skorygowania'@,_fb,,1,,,,'G');
      FAKS_KZF.win_act(_wer,,'Formuła','&Generuj listę'@@,,'Generuje listę faktur do skorygowania'@,_fb,,,,,,'G');
      _fb:="exec('faks_kzf_usu','faktury_nag',__faks_kzf_env.KZ,0)";
      FAKS_KZF.win_act(_wer,,'Formuła','&Usuń'@@,,'Usuwa pozycje'@,_fb,,,1,_fb,,'U')
   ?};
   _fb:="exec('faks_kzf_kor','faktury_nag1',__faks_kzf_env.KZ)";
   FAKS_KZF.win_act(_wer,,'Formuła','&Koryguj listę'@@,,'Koryguje listę faktur'@,_fb,,,,,,'K');
   _fb:="exec('faks_kzf_kus','faktury_nag',1,1,__faks_kzf_env.KZ)";
   FAKS_KZF.win_act(_wer,,'Formuła','Usuń k&orektę'@@,,'Usuwa korektę do faktury'@,_fb,,,1,_fb,,'O')
?};
_fb:="
   FAKS.cntx_psh;
   exec('czytaj','#stalesys',,FINFO);
   exec('open','open_tab',6+FAKS_KZF.FAK+1,8+FAKS_KZF.FAK+2);
   FAKS.prefix;
   {? FAKS.seek(FAKS_KZF.FAK)
   ||
      exec('pozycje_fak','faktury_poz',1)
   ?};
   exec('open','open_tab',ST.ODDZ,$ST.AR+2);
   FAKS.cntx_pop
";
FAKS_KZF.win_act(_wer,,'Formuła','Pozycje &faktury'@@,,'Podgląd pozycji faktury'@,_fb,,,,,,'F');
_fb:="
   {? FAKS_KZF.KOR='' || FUN.info('Nie wystawiono korekty.'@); return ?};
   _par1:=FAKS.WHERE='N';
   FAKS.cntx_psh;
   exec('czytaj','#stalesys',,FINFO);
   exec('open','open_tab',6+FAKS_KZF.KOR+1,8+FAKS_KZF.KOR+2);
   FAKS.prefix;
   {? FAKS.seek(FAKS_KZF.KOR)
   ||
      _par1:=_par1 | exec('FindAndGet','#table',FAKS,FAKS_KZF.FAKS,,\"STAT_REJ\",'')<>'N';
      exec('pozycje_fak','faktury_poz',_par1)
   ?};
   exec('open','open_tab',ST.ODDZ,$ST.AR+2);
   FAKS.cntx_pop
";
FAKS_KZF.win_act(_wer,,'Formuła','Pozycje ko&rekty'@@,,'Podgląd pozycji korekty'@,_fb,,,,,,'R');
{? ~_tab_in_area
||
   _fb:="
      {? FAKS.seek(__faks_kzf_env.KZ)
      || {? {? type_of(params_get())=type_of(obj_new('obj')) & var_pres('var_fakpop',params_get())>0
            || _mp:=params_get().context.mp;
               _autoakc:={? FAKS.SZ='S'
                         || exec('autoAkc','#b__box',_mp,300080,'LSP_FAK_EASP')
                         || exec('autoAkc','#b__box',_mp,200080,'LZK_ZAK_EZAK')
                         ?};
               {? exec('faks_zakoncz','faktury_nag',,_autoakc) || _mp.done(); 1 ?}
            || exec('faks_zakoncz_wer','faktury_nag');
               {? FAKS.seek(__faks_kzf_env.KZ) || FAKS.STAT_REJ='Z' ?}
            ?}
         || sel_exit()
         ?}
      ?}
   ";
   FAKS_KZF.win_act(_wer,,'Formuła','Zakończ'@@,,'Podgląd pozycji korekty'@,_fb);
   __faks_kzf_env.BTN:=obj_new('ZAKONCZ','ZAMKNIJ');
   _cfg:=',btn_label_align=center,panel=bottom,align=end';
   __faks_kzf_env.BTN.ZAKONCZ:=FAKS_KZF.win_btn(_wer,'text='+'&Zakończ'@+_cfg,'menu:Z');
   __faks_kzf_env.BTN.ZAMKNIJ:=FAKS_KZF.win_btn(_wer,'text='+'Zamknij&'@+_cfg,'key:Esc');
   FAKS_KZF.btn_opt(__faks_kzf_env.BTN.ZAKONCZ,{? FAKS.STAT_REJ='N' || 'state=normal' || 'state=grayed' ?})
?};
_fb:="
   _dok:=FAKS_KZF.FAK;
   {? FAKS_KZF.KOR<>''
   ||
      _choice:=FUN.choice('Który dokument wybierasz do podglądu?'@,,'Faktura'@,'Korekta'@);
      {? ~_choice || return ?};
      _dok:={? FAKS_KZF.KOR<>'' & _choice=2 || FAKS_KZF.KOR || FAKS_KZF.FAK ?}
   ?};
   FAKSPL.cntx_psh();
   FAKS.cntx_psh();
   FAKSPL.use('fakpl'+((8+_dok)+3));
   FAKS.use(form(8+_dok));
   FAKS.prefix();
   {? FAKS.seek(_dok)
   || exec('fall_rek','faktury_nag')
   ?};
   FAKS.cntx_pop();
   FAKSPL.cntx_pop();
   1
";
FAKS_KZF.win_act(_wer,,'Wyświetl',,,,_fb);
_fb:="
   _wer:=FAKS_KZF.win_sel('?');
:: jest pozycja do korekty
   FAKS_KZF.cntx_psh();
   FAKS_KZF.prefix();
   _poz2kor:=FAKS_KZF.find_tab(,'FAKS',,'=',FAKS.ref(),'KOR',,'=','');
   FAKS_KZF.cntx_pop();
   _act:=':';
   {? ~_poz2kor || _act:='K'+_act ?};
:: Wyszarzanie akcji 'Koryguj', gdy korekta była wystawiona, 'Usuń korektę' i 'Pozycje korekty' - gdy nie była
   _sel_size:=FAKS_KZF.sel_size();
   {? _sel_size=0
   || {? FAKS_KZF.KOR_NR=0
      || _act:='OR'+_act
      ?}
   ?};
:: Wyszarza zakończ, gdy żaden dokument nie został skorygowany
   {? FAKS.STAT_REJ<>'N' | _poz2kor || _act:='Z'+_act ?};
   {? FAKS.AKC='T' || _act:='GUKO'+_act+'G'
   |? FAKS.WHERE='N' || _act:='GU'+_act+'G'
   ?};
   FAKS_KZF.actions_grayed(_wer,_act);
:: domyślne akcje
   {? _poz2kor
   || FAKS_KZF.actions(_wer,,'K:G',1)
   || FAKS_KZF.actions(_wer,,'G:G',1)
   ?};
   ~~
";
FAKS_KZF.win_act(_wer,,'Rekord',,,,_fb);
_wer


\statorej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Status rejestracji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.STAT_REJ='N' || 'Rejestrowany'
|? FAKS.STAT_REJ='Z' || 'Zakończony'
|? FAKS.STAT_REJ='T' || 'Zaakceptowany'
|? FAKS.STAT_REJ='A' || 'Anulowany'
|| ''
?}


\faks_nrksef_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: FAKS.NRKSEF dla podanego FAKS.ref() albo $FAKS.ref()
::   WE: _a - FAKS.ref() albo $FAKS.ref()
::   WY: obj(WYNIK,MSG) WYNIK: 0 - nie / 1 - tak / -1 - zmieniono kontrahenta i odbiorcę
::----------------------------------------------------------------------------------------------------------------------
FAKS.cntx_psh();
FAKS.use(ref_name(_a));
FAKS.prefix();
_nrksef:=exec('FindAndGet','#table',FAKS,_a,,"FAKS.NRKSEF",'');
FAKS.cntx_pop();
_nrksef


\faks_statksef_ib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Formuła na ikonę dla pola FAKS.STATKSEF
::----------------------------------------------------------------------------------------------------------------------
"
   exec('pusta','#icon');
   ''
"


\faks_statbc_ib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25_BLBC1]
:: OPIS: Formuła na ikonę dla pola FAKS.STATBC
::----------------------------------------------------------------------------------------------------------------------
"
   exec('pusta','#icon');
   ''
"


\faks_bc_pokaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25_BLBC1]
:: OPIS: Pokazuje wynik kontroli BusinessCheck dla bieżącego dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__DOKUMZ_BC')<0 | __DOKUMZ_BC.REFSQL<>$FAKS.ref()
|| exec('faks_dokumzbc_seek','zbl')
?};
{? var_pres('__DOKUMZ_BC')>0 & __DOKUMZ_BC.REFSQL=$FAKS.ref() & __DOKUMZ_BC.DOKUMZ<>null()
|| DOKUMZ.cntx_psh();
   DOKUMZ.prefix();
   {? DOKUMZ.seek(__DOKUMZ_BC.DOKUMZ)
   || exec('blc_analyze','businesslink3',1)
   ?};
   DOKUMZ.cntx_pop()
|| FUN.info('Brak informacji z kontroli Businesscheck.'@)
?};
~~

:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 011580396d154fb2f63e26e867fa394ec676b0b1175204a287bff75a4c2f5bce5b282457566f65ee0ddadb1d36072735d8fb5511c9c8972a17dc7eaaf1f37982709b87c450452295f2494d3f1d4e6aa6d9b79e668a6267fcef1237f1e77dff25c52468255cac34c2d1d7d8310e1473b2dd86f5bca9a5163b2f77de618d96695d

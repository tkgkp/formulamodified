:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: prc_por.fml
:: Utworzony: 07.09.2017
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Formuły wspólne dla czynności dziedziny PRC dla webterma.
::======================================================================================================================

:: ------ Obszar roboczy --------

\prc_por
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.42]
:: OPIS: Główna formuła obszaru roboczego PRC_POR - Harmonogramy - webterm.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
{? type_of(_args)>0 & var_pres('ext',_args)>0
|| _p_uidref:=_args.ext[1];
:: sprawdzenie, czy firma pracownika jest zgodna z firmą wywołania
   _firma:={? +_p_uidref=48 || exec('FindAndGet','#table',P,_p_uidref,,"FIRMA",null()) || null() ?};
   {? var_pres('_firma')>0 & _firma & _firma<>exec('firma','ustawienia')
   || _url:=exec('getRCPLink','portal_engine',_firma,0);
      {? +_url
      || web_ses_close();
         web_open_link(_url);
         return()
      ?}
   ?}
?};

{? exec('env_wt','b_proces') & exec('init','pkw')
|| __PERSONEL:=1;
   web_params_set(
      exec('obj_ntab_set','#array',,'CUR_PRAC_SETID','JA')
   );
   {? exec('load_cur_prac','p_web')
   || AreaTitle.setTitle();

:: wyświetlamy okno grupowe z wizytówką zapamiętanego pracownika oraz z czynnościami podpiętymi pod przyciski
      P.web_select('WEB_GR_H')
   ?}
?}

:: ------ Akcje przycisków --------

\p_web_prc_plan_czp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.42]
:: OPIS: Akcja przycisku "Planowanie czasu pracy"
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_formula:=''+"exec('pkw_run','pkw','PRC_POR_RCZP','PRC_POR_RCZP')";
_UID_EXE:=exec('bdexec_check','#b_desktop',_formula,'PRC');
_tabid:=app_info('web_tabid');

::powołanie obiektu inet w celu wykorzystania metody encodującej napis
_inet:=inet_get ('https://');
_encode_uid:=_inet.url_encode(_UID_EXE);
_url:='#action=AREA&p0='+_encode_uid+'&ds=a954a5&keepParams=1';
web_open_link(_url,'Planowanie_%1'[_tabid])


\p_web_prc_graf_rcgw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.42]
:: OPIS: Akcja przycisku "Grafiki pracownicze"
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_formula:=''+"exec('pkw_run','pkw','PRC_POR_RCGW','PRC_POR_RCGW')";
_UID_EXE:=exec('bdexec_check','#b_desktop',_formula,'PRC');
_tabid:=app_info('web_tabid');

::powołanie obiektu inet w celu wykorzystania metody encodującej napis
_inet:=inet_get ('https://');
_encode_uid:=_inet.url_encode(_UID_EXE);
_url:='#action=AREA&p0='+_encode_uid+'&ds=a954a5&keepParams=1';
web_open_link(_url,'Grafiki_%1'[_tabid])


\linkToRCP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.14]
:: OPIS: Formuła zwraca część linku niezbędną do połączenia się do portalu RCP.
::       Link ten będzie wykorzystywany w HR Portal.
::   WE:
::   WY: napis będący linkiem do portalu
::----------------------------------------------------------------------------------------------------------------------
::powołanie obiektu inet w celu wykorzystania metody encodującej napis
_inet:=inet_get ('https://');
_url:='';

:: sprawdzamy czy mamy dziedzinę PRC
_lic_prc:=exec('lic','#b_domain','PRC');
{? _lic_prc
||
:: formuła do obszaru roboczego Harmonogramy
   _formula:=''+"params_exec('wybierz','#b_area','PRC_POR','PRC_POR')";
   _UID_EXE:=exec('bdexec_check','#b_desktop',_formula,'PRC');
   _encode_uid:=_inet.url_encode(_UID_EXE);
   _url:='#action=AREA&ds=a954a5&keepParams=1&p0='+_encode_uid
?};
_url


\p_web_prc_grupy_prac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.02]
:: OPIS: Akcja przycisku "Grupy podwładnych"
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: wyświetl okno z grupami
exec('select','prc_gpdwl',0)


\before_service
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.02]
:: OPIS: Wspólna formuła przed obsługą dla okienek z kontrolką (planowanie i grafiki)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('env_wt','b_proces');
exec('load_cur_prac','p_web');
exec('czytaj','#stalesys',,KST_PAR);
exec('czytaj','#stalesys',,KST,'R_PNOD','R_PNDO');
exec('open_prc','open_tab');
exec('__KAL','object');
exec('__RUB','object');
exec('__HARM','object');
exec('__F_ZATR','object');
exec('__STALE','object');
VAR.JESTLIST:=0;
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:22 9940636ef18d0b83a91e92a1fed1133c8fe7ca8df597695eeb125c7aaeceaac4677fd478462fcc37544e937b12436f0756e217a1f6eea085c0cc363d7e8b0abdfbeffbd6307605122a34f7f9e01d6f0cec583b8e9a9ebaaccdb2b7cc23c33a4f17293211260fd2419252f28d8d01a281709242ab9b74088133e16ef7a2e5e478

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: efaktury.fml
:: Utworzony: 31.03.2006
:: Autor: AMA
::======================================================================================================================
:: Zawartosc: Formuly do obslugi e-faktur
::======================================================================================================================


\edokpbop
::--------------------------------------------------------------------------------------------------
::  UTW: [JH] [12.10]
:: OPIS: Wyswietlanie e-dokumentow
:: WE: _a: miejsce uruchomienia - dopuszczalne wartosci 'PB oraz 'OPTMP'
::--------------------------------------------------------------------------------------------------
VAR_DEL.delete('X_Xa');
X_Xa:=tab_tmp(1,'SYM','STRING[20]','Symbol rozrachunku'@
                  ,'DATA','DATE','Data otwarcia'@
                  ,'AN','STRING[35]','Konto'@
                  ,'KW','REAL','Kwota'@
                  ,'REFSQL','STRING[16]','Ref sql'@
              );
_tt:=sql('select  ROK_F.NAZ from ROK_F join FIRMA using(ROK_F.FIRMA, FIRMA.REFERENCE) '+
         'where substr(to_string(POCZ_ROK),6,5) not like (\'01/01\') '+
         'and FIRMA.REFERENCE=:_a',REF.FIRMA);
_data:=0;
{? _a='PB' & ~_tt.size
|| {? PB_OP.SYM_ROK<> '' || _data:=#(PB_OP.SYM_ROK+4) ?};
   {? _data<>0
   || OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA,date(_data,1,1))
   || OKRO_F.index('ROK'); OKRO_F.prefix(SSTALE.AR)
   ?}
?};
OP.cntx_psh;
ZAP_OP.cntx_psh;
_jest_op:=0;
{? _a='PB'
|| ROK_F.index('KOD');
   ROK_F.prefix(REF.FIRMA);
   {? ~_tt.size & OKRO_F.first
   || _rok:=OKRO_F.ROK
   || ROK_F.first();
      _rok:=ROK_F.ref
   ?};
   {? ROK_F.seek(_rok)
   || {!|?
        _msk:=ROK_F.KOD;
        OP.use('operac' + _msk);
        ZAP_OP.use('rozzap'+ _msk);
        OP.clear;
        OP.index('KON_O');
        OP.prefix(PB_OP.PB().WAL,PB_OP.ODD,PB_OP.AN,PB_OP.SYM,PB_OP.SYM,PB_OP.SYM_ROK);
        {? OP.first()  ||  _jest_op:=1; 0 || ROK_F.next() ?}
      !}
   ?}
|? _a='OPTMP'
|| OP.clear;
   OP.index('KON_O');
   OP.prefix(exec('find_wal','efaktury',OPTMP.WAL().SYM),OPTMP.ODD,OPTMP.KONTO,OPTMP.SYM,OPTMP.SYM,OPTMP.SYM_ROK);
   _jest_op:=OP.first()
?};
{? _jest_op
|| ZAP_OP.cntx_psh();
   ZAP_OP.clear;
   ZAP_OP.index('OP');
   ZAP_OP.prefix(OP.ref);
   {? ZAP_OP.first() & ZAP_OP.size=1
   || {!
      |? {? ZAP_OP.MA<>0
         ||  exec('efakview','dok_fks',0,1)
         ?};
         ZAP_OP.MA=0 & ZAP_OP.next()
      !}
   |? ZAP_OP.size>=1
   || {!
      |? {? ZAP_OP.MA<>0
         || X_Xa.blank;
            X_Xa.SYM:=ZAP_OP.OP().SYM;
            X_Xa.DATA:=ZAP_OP.OP().DO;
            X_Xa.AN:=ZAP_OP.OP().AN;
            X_Xa.KW:=ZAP_OP.MA;
            X_Xa.REFSQL:=$ZAP_OP.ref;
            X_Xa.add(1)
         ?};
         ZAP_OP.next()
      !}
   ?};
   ZAP_OP.cntx_pop()
|| FUN.info('Nie znaleziono zapisu na rozrachunku z e-dokumentem'@)
?};
{? X_Xa.size
|| _mkacr:=X_Xa.mk_sel('Zapisy dla rozrachunku'@,,0);
   X_Xa.win_sel(_mkacr);
   _menu:='Dane źródłowe'@;
   X_Xa.win_act(_mkacr,,'Menu',_menu,,'');
   X_Xa.win_act(_mkacr,0,'Formuła','Dokumenty z innej dziedziny'@@,_menu,,"exec('find_edok','efaktury',X_Xa.REFSQL,1)",,,,,,'D');
   X_Xa.win_act(_mkacr,0,'Formuła','E-dokument'@@,_menu,,"exec('find_edok','efaktury',X_Xa.REFSQL,2)",,,,,,'E');
   X_Xa.win_act(_mkacr,0,'Formuła','Załączniki'@@,_menu,,"exec('find_edok','efaktury',X_Xa.REFSQL,3)",,,,,,'Z');
   X_Xa.win_act(_mkacr,,'Rekord',,,,
                  " ZAP_OP.cntx_psh();_hid:='D(DEZ)';
                    ZAP_OP.use(8+X_Xa.REFSQL);
                     {? ZAP_OP.seek(BB.sqlint(X_Xa.REFSQL),(8+X_Xa.REFSQL))
                     ||  _poz:=$ZAP_OP.POZDOK;
                          DOK.cntx_psh();POZ.cntx_psh();
                          DOK.use('doku'+(4+(4-_poz)));
                          POZ.use(8+_poz);
                          DOK.prefix();POZ.prefix();
                           _hid_d:=''; _hid:='';
                          {? POZ.seek(BB.sqlint(_poz),(8+_poz))
                          ||  {? POZ.DOK().get()
                              ||  {? PAR_SKID.get(82)='N'
                                   ||     DOKUM.index('DOKUM');
                                           DOKUM.prefix(REF.FIRMA,$DOK.ref);
                                           {? DOKUM.first()
                                           ||  ''
                                           || _hid_d+='Z'
                                           ?}
                                     ?};'załącznik';
                                    {? DOK.DOKZRODL='' | DOK.REJ().KOD='~BO'  || _hid_d+='D' ?};'dokument źródłowy';
                                    {? ~DOK.E_DOC || _hid_d+='E' ?};'skan ';
                                    {? _hid_d<>'' || _hid:='D('+_hid_d+')' || _hid:='' ?}
                               ?}
                           ?};
                            DOK.cntx_pop();POZ.cntx_pop()
                     ?};
                     ZAP_OP.cntx_pop();
                    X_Xa.actions_grayed(X_Xa.win_sel('?'),_hid)
                 ");
   X_Xa.win_fld(_mkacr,,'SYM');
   X_Xa.win_fld(_mkacr,,'DATA');
   X_Xa.win_fld(_mkacr,,'AN');
   X_Xa.win_fld(_mkacr,,'KW');
   X_Xa.select
?};
OP.cntx_pop();
ZAP_OP.cntx_pop()


\find_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JH [12.10]
:: OPIS: Szukanie rekordu w tabeli SLO dla kodu waluty
:: _a - Symbol waluty
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=null;
SLUAPPL.cntx_psh();
SLUAPPL.index('NAZ'); SLUAPPL.prefix('F');
{? SLUAPPL.find_key('WALUTY')
|| SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(SLUAPPL.SLU);
   {? SLO.find_key(_a) || _zwrot:=SLO.ref() ?};
   SLO.cntx_pop()
?};
SLUAPPL.cntx_pop();
_zwrot


\find_edok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JH [12.10]
:: OPIS: Szukanie rekordu w tabeli SLO dla kodu waluty
:: _a - ref sql
::----------------------------------------------------------------------------------------------------------------------
ZAP_OP.cntx_psh();
ZAP_OP.clear;
{? ZAP_OP.seek(BB.sqlint(_a),8+_a)
||  {? _b=1
     || exec('zap_op_dok','dok_fks')
     |? _b=2
     || exec('efakview','dok_fks',0,1)
     |? _b=3
     || exec('dok_zal','dokum_zal')
     ?}
?};
ZAP_OP.cntx_pop()


\get_dokum_stat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [12.51]
:: OPIS: Zwraca status załącznika z DOKUM.BL_STAT
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','zbl') | var_press('BL',DOKUM)<=0 || return('') ?};
exec('kh_dod_ini','kontrahent');
_result:='';
_faks:=exec('find_faks','dok_fks1');
_ref:={? _faks<>null() || $_faks || $DOK.ref() ?};
DOKUM.cntx_psh();
DOKUM.index('BL_2');
DOKUM.prefix(REF.FIRMA,'T',_ref);
{? DOKUM.first()
|| _result:=DOKUM.BL_STAT
?};
DOKUM.cntx_pop();
_result


\bl_set_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Ustawia status dla dokumentów BL wysyłanych podczas księgowania
::----------------------------------------------------------------------------------------------------------------------
_result:=0; _dalej:=1;
_czyKsef:=exec('is_dokum_ksef','zbl_dok',DOKUM.ref());
_czyUfd:=exec('czy_ufd','edi_fo_ufd',DOKUM.ref());
:: blokada zmiany statusu jeżeli dokument jest wysyłany do KSeF, a nie ma załącznika KSeF
{? DOK.KSEF='T' & (DOK.A='N' | _czyKsef=0)
|| _dalej:=0
?};
{? _dalej & DOKUM.BL_STAT=exec('before_send','zbl_stat') & (_czyKsef | _czyUfd)
|| _bl_stat:=exec('ready_to_send','zbl_stat');
   {? _czyKsef
   || exec('FindAndGet','#table',USERS,OPERATOR.USER,,"
         {? exec('get_usersf','users') & USERSF.KSEF_UL='T' || DOKUM.KSEFAUTH:=OPERATOR.USER ?}
      ");
      {? DOKUM.KSEFAUTH=null() || _bl_stat:=exec('waiting_auth','zbl_stat') ?}
   ?};
   DOKUM.BL_STAT:=_bl_stat;
   DOKUM.BL_TERM:=exec('bl_term','zbl_dok');
   _result:=DOKUM.put()
?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 7740d951b4181817fb5f9512366d0c762e1544aec9105ab9ac7f5c81ef0243b88d1c081f688c8cd0142037bcc28a909b146b6d9f163f0088e59cdfd8944cfdbd914ed7dce4480c2118bbcc17955fd5c19865a5601a143b6d5c70626564e3b4aca1b96469aff704ac9e997bdf46cf1d2164d5e0dac5be18bfa637d96c0baf3b54

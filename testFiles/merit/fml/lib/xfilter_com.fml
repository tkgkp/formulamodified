:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: xfilter_com.fml [21.14]
:: Utworzony: 10.02.2021
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły obsługujące rozszerzone filtry.
::======================================================================================================================


\r_rn_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Sprawdza, czy w polu podano poprawny numer rubryki.
::   WE: _a STRING - klasa rubryki, jeśli pominięty lub inny niż napis, to przyjęta będzie dowolna klasa
::   WY: 0/1 - numer rubryki błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_cls:='';

{? fld()=0
|| return(1)
?};

{? var_pres('_a')=type_of('') & _a<>''
|| _cls:=_a
?};

_TAB:=cur_tab(1,1);
_fld:=cur_afld();
_max:=__RUB.max();

_chk:=__CHK.validate(_TAB,
   $('{? _b.%1<0 | %2<_b.%1'
     '|| _a.err_fld(_b,\'%1\',1,\'%3\');'
     '   return(\'%1\')'
     '?};'
     '1'[_fld,$_max,'Należy podać liczbę z zakresu od %1 do %2.'@['1',$_max]]),
   $({? _cls<>''
     || '{? __RUB.ref(_b.%1,\'%2\')=null'
        '|| _a.err_fld(_b,\'%1\',1,\'%3\');'
        '   return(\'%1\')'
        '?};'
        '1'[_fld,_cls,'Podana liczba nie jest kodem rubryki klasy "%1".'@[_cls]]
     || '1'
     ?})
);

{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & ~_chk)
|| return(0)
?};

1


\r_rn_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Wybór numeru rubryki.
::   WE: _a STRING - klasa rubryki, jeśli pominięty lub inny niż napis, to przyjęta będzie dowolna klasa
::   WY: numer rubryki
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_cls:='';

{? var_pres('_a')=type_of('') & _a<>''
|| _cls:=_a
?};

_num:=fld();
R.cntx_psh();
{? _cls<>''
|| R.index('RUBKLKOD');
   R.prefix(_cls)
|| R.index('RUBKOD');
   R.prefix()
?};
R.find_key(_num);
R.win_sel('WYB');
{? R.select()
|| _num:=R.RN
?};
R.cntx_pop();

_num


\o_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Zwraca wskazanie listy płac określonej w parametrze sesji.
::   WE:
::   WY: tekstowa reprezentacja wskazania
::----------------------------------------------------------------------------------------------------------------------
__PARSES.getVal('ListaPłac').REF


\o_ref_storno
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Zwraca wskazanie storno listy płac określonej w parametrze sesji.
::   WE:
::   WY: wskazanie wiersza
::----------------------------------------------------------------------------------------------------------------------
_ref:=__PARSES.getVal('ListaPłac').REF;
exec('storno_ustal_o','lista_pow',_ref).VALUE

:Sign Version 2.0 jowisz:1048 2021/04/09 15:26:34 90ddf2d07173672e9c52121a0a29311a9700f2a199c7efd7ae7521c3943d02ed6a02fe8cb9ee3ef98abe5c2341b2cc23c8e1e2a1bf276a3decba3414e219b67521f8ec38c693ae47f592de204a64d475680bcab6ce20196af43ccf60c42d8112e46122a81dd5661ae56b2b197e5c229613014c5410ad73a1fbeda018fe86365e

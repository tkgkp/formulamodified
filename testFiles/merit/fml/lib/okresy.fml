:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: okro.fml
:: Utworzony: 24.02.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa okresów
::======================================================================================================================

\first_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzenie, czy przekazany ROK_F jest pierwszym dla obszaru FKS
::   WE: _a - ROK_F.ref()
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
_o_fks:=exec('szuk_b_dom','parses','FKS');
{? _o_fks
|| OKRO_F.cntx_psh(); ROK_F.cntx_psh();
   OKR_OBSZ.cntx_psh(); OKR_OBSZ.index('OKRO_F'); OKR_OBSZ.prefix(REF.FIRMA,_o_fks);
   _zwrot:=(OKR_OBSZ.first() & OKR_OBSZ.OKRO_F().ROK=_a);
   OKR_OBSZ.cntx_pop(); OKRO_F.cntx_pop(); ROK_F.cntx_pop()
?};
_zwrot


\pocz_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Ref bieżącego roku
::  OLD: \pocz_rok/wspol
::----------------------------------------------------------------------------------------------------------------------
ROK_F.ref()


\czy_z_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: sprawdza czy okres jest zamkniety
::   WE:  _a  - rodzaj kontroli
::               <0 - ogolnie
::                      -1 - magazynowych
::                      -2 - sprzedazy
::                      -3 - zakupu
::                      -4 - umów cyklicznych
::               >0 - dla magazynu wg parametru _e - sprawdza mozliwosc edycji dokumentow
::                      1 - magazynowych
::                      2 - sprzedazy
::                      3 - zakupu
::                      4 - umów
::       [_b] - czy wyswietlac komunikat (1 - tak 0 - nie) 2-tak i wyświetla FUN.info
::       [_c] - rok      domyslnie ST.AR
::       [_d] - mc       domyslnie ST.AM
::       [_e] - magazyn  domyslnie ST.MAG
::   WY: zwraca czy okres jest zamkniety
::       0 - jest zamkniety
::       1 - jest otwarty
::       -1 - brak okresu
::  OLD: \czy_z_ok/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2  || {? type_of(_b)<>1 || _b:=1 ?}  || _b:=1  ?};
{? _>=3  || {? type_of(_c)<>1 || _c:=ST.AR ?}  || _c:=ST.AR  ?};{? _c=0 || _c:=ST.AR  ?};
{? _>=4  || {? type_of(_d)<>1 || _d:=ST.AM ?}  || _d:=ST.AM  ?};{? _d=0 || _d:=ST.AM  ?};
{? _>=5  || {? type_of(_e)<>7 || _e:=ST.MAG ?} || _e:=ST.MAG ?};

BEER.MG:=_e;
_wyn:=1;
_msg:='';
OKR.cntx_psh();
OKR.index('OKR');
OKR.prefix(REF.FIRMA,_c,_d);
{? OKR.first()
||
   _zam_mag:=1+OKR.ZAM;
   _zam_spr:=1+(1-OKR.ZAM);
   _zam_zak:=1+(2-OKR.ZAM);
   _zam_umo:=OKR.ZAM+1;
   {? {? _a=-1 | _a=1 || _zam_mag='N'
      |? _a=-2 | _a=2 || _zam_spr='N'
      |? _a=-3 | _a=3 || _zam_zak='N'
      |? _a=-4 | _a=4 || _zam_umo='N'
      ?}
   ||
      {? _a>0
      ||
         OKRD.index('ODDZ');
         OKRD.prefix(_c,_d,ST.ODDZ,_e);
         {? OKRD.first()
         ||
            {? _a=1 & OKRD.M='T'
            || _msg:='W magazynie: %1 okres obrachunkowy %2 jest zamknięty dla dokumentów magazynowych.'@
                     [BEER.MG().SYM,OKRD.OKR().NAZ];
               _wyn:=0
            |? _a=2 & OKRD.D='T'
            || _msg:='W magazynie: %1 okres obrachunkowy %2 jest zamknięty dla dokumentów sprzedaży.'@
                     [BEER.MG().SYM,OKRD.OKR().NAZ];
               _wyn:=0
            |? _a=3 & OKRD.Z='T'
            || _msg:='W magazynie: %1 okres obrachunkowy %2 jest zamknięty dla dokumentów zakupowych.'@
               [BEER.MG().SYM,OKRD.OKR().NAZ];
               _wyn:=0
            |? _a=4 & OKRD.U='T'
            || _msg:='W magazynie: %1 okres obrachunkowy %2 jest zamknięty dla umów cyklicznych i zgłoszeń.'@
               [BEER.MG().SYM,OKRD.OKR().NAZ];
               _wyn:=0
            ?}
         ?}
      ?}
   |? _a<>0
   ||
      _msg:='Okres obrachunkowy jest zamknięty całkowicie.'@;
      _wyn:=0
   ||
      _msg:='Nieoczekiwana wartość parametru %1.'@['_a'];
      _wyn:=0
   ?}
|? _b=3
|| _msg:='Brak podanego okresu obrachunkowego w systemie'@;
   _wyn:=-1
?};
OKR.cntx_pop();
{? (_b=1 | _b=3) & +_msg
||
   {? var_pres('__kom')>100
   || exec('add_kom','#message',_msg,2,,__lp+=1)
   |? var_pres('__kom')>100
   || __kom.add(_msg,7)
   || FUN.info(_msg+'\n'+'Operacja niemożliwa.'@)
   ?}
|? _b=2 & +_msg
|| FUN.info(_msg+'\n'+'Operacja niemożliwa.'@)
?};
_wyn


\szuk_dat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Szukanie daty poczatku lub konca okresu, jesli wybrano okres BO lub BZ
::   WE: _a - ref okresu
::       _b - 0 - poczatek okresu, 1 - koniec
::   WY: znaleziona data
::  OLD: \szuk_dat/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=date(0,0,0);
ROK_F.cntx_psh(); OKRO_F.cntx_psh();
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA); SSTALE.AR();
OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
{? OKRO_F.seek(_a)
|| OKRO_F.cntx_psh();
   {? ~OKRO_F.prev() & OKRO_F.next() || _zwrot:={? _b=0 || OKRO_F.POCZ || OKRO_F.KON ?} ?};
   OKRO_F.cntx_pop();
   {? _zwrot=date(0,0,0)
   || {? ~OKRO_F.next() & OKRO_F.prev() || _zwrot:={? _b=0 || OKRO_F.POCZ || OKRO_F.KON ?} ?}
   ?}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
_zwrot


\get_date
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.60]
:: OPIS: Zwraca date poczatku lub konca roku (okresu) obrachunkowego
::   WE: _a: ref roku; _b: wskazanie na date: 0 - data poczatku, 1 - data konca; _c: ref okresu
::   WY: data
::  OLD: \get_date/form_sql.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); OKRO_F.cntx_psh(); ROK_F.clear(); OKRO_F.index('ROK'); _ret:=date(0,0,0);
{? ROK_F.seek(_a)
|| OKRO_F.prefix(ROK_F.ref());
   {? _=2 & ((~_b & OKRO_F.first()) | (_b & OKRO_F.last()))
   || {? _b
      || {! |? OKRO_F.KON=date(0,0,0) & OKRO_F.prev() !}; _ret:=OKRO_F.KON
      || {! |? OKRO_F.POCZ=date(0,0,0) & OKRO_F.next() !}; _ret:=OKRO_F.POCZ
      ?}
   |? _=3 & OKRO_F.seek(_c)
   || {? _b
      || {! |? OKRO_F.KON=date(0,0,0) & OKRO_F.next() !};
         {? OKRO_F.KON=date(0,0,0) || {! |? OKRO_F.KON=date(0,0,0) & OKRO_F.prev() !} ?};
         _ret:=OKRO_F.KON
      || {! |? OKRO_F.POCZ=date(0,0,0) & OKRO_F.next() !};
         {? OKRO_F.POCZ=date(0,0,0) || {! |? OKRO_F.POCZ=date(0,0,0) & OKRO_F.prev() !} ?};
         _ret:=OKRO_F.POCZ
      ?}
   ?}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop(); _ret


\okr_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: kolorowanie okresow obrachunkowych, rekord przed i przed wyswietl
::  OLD: \okr_rek/defin.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rekprzed','color','EOKR#01')


\okr_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: kolorowanie okresow obrachunkowych, ustawianie statusu
::  OLD: \okr_kol/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='EOKR#01#01';
{? cur_tab.name()='okresylo'
||
   OKRD.index('OKR');
   OKRD.prefix(OKR.ref());
   {? OKRD.first() || _wyn:='EOKR#01#02' ?};
   {? 1+OKR.ZAM='T' || _wyn:='EOKR#01#03' ?}
|? cur_tab.name()='okruzy'
||
   ST.STAT:='OTWARTY';
   _mg_ref:=null;
   {? ST.MAG_SYM<>''
   ||
      MG.cntx_psh();
      MG.index('MAG');
      MG.prefix(ST.ODDZ_KOD,ST.MAG_SYM);
      {? MG.first()
      || _mg_ref:=MG.ref()
      ?};
      MG.cntx_pop()
   ?};

   OKRD.index('OKR');
   {? _mg_ref<>null
   || OKRD.prefix(OZ.OKR,ST.ODDZ_REF,_mg_ref)
   || OKRD.prefix(OZ.OKR)
   ?};
   {? OKRD.first() || ST.STAT:='ZAMKNIĘTY CZĘŚCIOWO';_wyn:='EOKR#01#02' ?};
   {? 1+OZ.OKR().ZAM='T' || ST.STAT:='ZAMKNIĘTY';_wyn:='EOKR#01#03' ?}
?};
_wyn


\szuk_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RL] [8.70]
:: OPIS: Szuka okresu obrachunkowego na date przekazana parametrem i ustawia go w polu ROZNE.UT_OKROD
::   WE: _a - data
::       _b - ktory okres wstecz od daty _a
::   WY: ref znalezionego okresu lub null gdy nie znaleziono
::  OLD: \szuk_okr/windyk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2  || {? type_of(_b)<>1 || _b:=0 ?}  || _b:=0  ?};
_wyn:=null;
ROZNE.UT_OKROD:=null;
OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
{? OKRO_F.first()
|| {!
   |?
      {? (_a>=OKRO_F.POCZ & _a<=OKRO_F.KON)
      || ROZNE.UT_OKROD:=OKRO_F.ref();
         _wyn:=OKRO_F.ref()
      ?};
      ROZNE.UT_OKROD=null & OKRO_F.next()
   !};
   {? _wyn<>null & _b
   || {! _i:=1.._b |! _wyn:=OKRO_F.prev() !};
      {? _wyn || _wyn:=ROZNE.UT_OKROD:=OKRO_F.ref() ?}
   ?}
?};
_wyn


\szuk_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Szuka okresu na podaną datę w bieżącej firmie
::   WE: _a - data
::   WY: ref OKRO
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=null;
OKRO.cntx_psh();
OKRO.index('POCZ'); OKRO.prefix(REF.FIRMA);
{? type_of(_a)=type_of(date()) & _a<>date(0,0,0)
|| {? OKRO.first()
   || {!
      |? {? (_a>=OKRO.DATA_OD & _a<=OKRO.DATA_DO)
         || _zwrot:=OKRO.ref()
         ?};
         _zwrot=null() & OKRO.next()
      !}
   ?}
?};
OKRO.cntx_pop();
_zwrot


\zwrrok_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: zwraca ROK_F - rok obrotowy w programie finansowym na podstawie podanej daty
::   WE: _a - data, w ktorej ma miescic
::       [_b] - 0/1 czy jeśi nie znajdzie roku z podaną datą, czy ma zwrócić ostatni
::   WY: ref do tabeli ROK_F lub null jezeli nie znaleziono
::  OLD: \zwrrok_f/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
ROK_F.cntx_psh();
OKRO_F.cntx_psh();
OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
{? OKRO_F.find_ge(_a)
|| {!
   |? {? _a<=OKRO_F.KON & _a>=OKRO_F.POCZ
      || _wyn:=OKRO_F.ROK; 0
      || OKRO_F.prev()
      ?}
   !}
?};
{? _wyn=null() & var_pres('_b')=type_of(1) & _b=1 & OKRO_F.last() || _wyn:=OKRO_F.ROK ?};
ROK_F.cntx_pop();
OKRO_F.cntx_pop();
_wyn


\okro_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.40]
:: OPIS: Sprawdzenie, czy okres jest zamknięty
::  OLD: \okro_zam/zam_okr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('bv_lang','lang');
{? OKRO_F.ZAM='T' || 'OKRO_F#02#01' || '' ?}


\dod_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL
:: OPIS: Dodanie okresu raportowania
::   WE: _a - rok (ref)
::       _b - numer okresu od
::       _c - numer okresu do
::       _d - nazwa okresu
::       _e - typ okresu
::  OLD: \dod_ospr/skid_akt.fml
::  OLD: \dod_ospr/sprfin.fml
::----------------------------------------------------------------------------------------------------------------------
_wynik:=1;
OKRO_F.cntx_psh();
OSPR.index('OKRES2'); OSPR.prefix(REF.FIRMA,_e,_a,_b,_c);
{? OSPR.first()
|| OKRESY.OKR_RAP:=OSPR.ref;1
|| OSPR.blank();
   OSPR.ROK:=_a;
   OKRO_F.index('ROK');
   OKRO_F.prefix();
   {? OKRO_F.find_key(_a,_b)
   || OSPR.OKRES_OD:=OKRO_F.ref
   || _wynik:=0
   ?};
   {? OKRO_F.find_key(_a,_c)
   || OSPR.OKRES_DO:=OKRO_F.ref
   || _wynik:=0
   ?};
   OSPR.TYP:=_e;
   OSPR.NAZWA:=_d;
   {? _wynik=1
   || {? OSPR.add(1)
      || exec('okr_ospr','okresy');
         OSPR.put();
         OKRESY.OKR_RAP:=OSPR.ref
      || _wynik:=0
      ?}
   ?}
?};
OKRO_F.cntx_pop();
_wynik


\okr_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Ustawia pole OKRES i SKROT tabeli OSPR na podstawie pozostałych pól rekordu
::  OLD: \okr_ospr/skid_akt.fml
::----------------------------------------------------------------------------------------------------------------------
SLU.index('WZORZEC'); SLU.prefix('prosty','~TYPY OKRESÓW');
{? SLU.first()
|| SLO.index('SL'); SLO.prefix(SLU.ref());
   _w:=OSPR.TYP;
   _w+={? _w='M'
       || {? OSPR.OKRES_OD().POCZ=date(0,0,0) & OKRO_F.NR
          || '99'
          || ('0'+$OSPR.OKRES_OD().NR)+2
          ?}
       |? _w='K'
       || {? OSPR.OKRES_OD().NR>9 || '04'
          |? OKRO_F.NR>6 || '03'
          |? OKRO_F.NR>3 || '02'
          || '01'
          ?}
       |? _w='P'
       || {? OSPR.OKRES_OD().NR>6 || '02'
          || '01'
          ?}
       || '00'
       ?};
   OSPR.OKRES:={? SLO.find_key(_w) || SLO.ref() || null ?}
?};
exec('skr_ospr','okresy',1)


\skr_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Ustawia pole SKROT tabeli OSPR na podstawie pozostałych pól rekordu
::   WE: _a - czy zamazać pole? 1-tak 0-nie
::  OLD: \skr_ospr/skid_akt.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a | OSPR.SKROT=''
|| OSPR.SKROT:={? OSPR.TYP='M' || 3+OSPR.OKRES_OD().NAZ
               |? OSPR.TYP='K' || _f:="{? _a<4 || 'I'*_a || 'IV' ?}"; 'kw '+_f(OSPR.OKRES_OD().KWARTAL)
               |? OSPR.TYP='P' || _f:="{? _a<4 || 'I'*_a || 'IV' ?}"; 'pół '+_f(OSPR.OKRES_OD().POLROCZE)
               |? OSPR.TYP='R' || 'rok'
               || 'inny'
               ?};
   {? OSPR.SKROT='' || OSPR.SKROT:='skrót' ?}
?}; 1


\ospr_rok
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: przed Rekord w okienkach tabeli OSPR; ustawienie roku
::  OLD: \ospr_rok/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
OKRESY.ROK1:=OSPR.OKRES_OD().ROK;
exec('bv_lang','lang');
exec('rekprzed','color','OSPR#01#')


\bv_l_skr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Przed wyswietleniem pola UNPAR.P10
::  OLD: \bv_l_skr/skid_lng.fml
::----------------------------------------------------------------------------------------------------------------------
UNPAR.P10:=exec('get_lang','lang',,,0)


\bgd_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przed akcja grupowa Usun tabeli OSPR
::  OLD: \bgd_ospr/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Podczas operacji usuwania okresu raportowania\n'
           'zostaną również usunięte wartości wierszy.\n\n'
           'Kontynuować usuwanie zaznaczonych okresów raportowania?'@)
|| ospr_sel:=OSPR.sel_size(); ospr_ok:=0; 1
?}


\agd_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Po akcji grupowej Usun tabeli OSPR
::  OLD: \agd_ospr/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info(
   'Liczba zaznaczonych okresów raportowania: %1.\n'
   'Liczba usuniętych okresów raportowania: %2.'@[$ospr_sel,$ospr_ok]
);
&ospr_sel; &ospr_ok


\ospr_por
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: po akcji Rekord w okienkach tabeli OSPR; sprawdzenie poprawnosci numerow
::  OLD: \ospr_por/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
{? OSPR.ROK<>null
|| {? OSPR.OKRES_OD<>null & OSPR.OKRES_DO<>null
   || {? OSPR.OKRES_OD().NR>OSPR.OKRES_DO().NR
      || FUN.info('Nieprawidłowo zdefiniowany okres'@);'OKRES_OD'
      |? OSPR.OKRES_OD().NR<OSPR.OKRES_DO().NR & OSPR.TYP='M'
      || FUN.info('Nieprawidłowo zdefiniowany okres'@);'OKRES_OD'
      || {? OSPR.TYP='I'
         || OSPR.OKRES:=null; __CHK.record(OSPR,,'NAZWA','SKROT')
         |? OSPR.OKRES_OD=OSPR.OKRES_DO & OSPR.OKRES_OD().POCZ=date(0,0,0)
         || OSPR.OKRES:=null; __CHK.record(OSPR,,'NAZWA','SKROT')
         || __CHK.record(OSPR,,'NAZWA','OKRES','SKROT')
         ?}
      ?}
   || 'OKRES_OD'
   ?}
|| 'ROK'
?}


\addokr
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Funkcja tworzaca okres sprawozdawczy (dodanie rekordu do OSPR)
::   WE: _a - typ okresu
::  OLD: \addokr/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
OSPR.blank();
OSPR.OKRES_OD:=OKRESY.OKRES_OD;
OSPR.OKRES_DO:=OKRESY.OKRES_DO;
OSPR.TYP:=_a;
OSPR.NAZWA:={? _a='R'
            || 'ROK: '+form(OKRESY.ROK1().NAZ,,,'1')
            |? _a='P'
            || OKRESY.ROK1().NAZ+' '+form(OKRESY.OKRES_OD().POLROCZE)+' PÓŁROCZE '
            |? _a='K'
            || OKRESY.ROK1().NAZ+' '+form(OKRESY.OKRES_OD().KWARTAL)+' KWARTAŁ '
            |? _a='M'
            || OKRESY.ROK1().NAZ+'-'+form(OKRESY.OKRES_OD().NR,2,0)+' ('+OKRESY.OKRES_OD().NAZ+')'
            ?};
exec('okr_ospr','okresy');
OSPR.add(1);
OKRO_F.cntx_pop()


\dod_rok
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Dolaczanie automatyczne roku
::  OLD: \dod_rok/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.index('ROK');
OKRO_F.prefix(OKRESY.ROK1);
{? OKRO_F.first()
|| OKRESY.OKRES_OD:=OKRO_F.ref
|| OKRESY.OKRES_OD:=null
?};
{? OKRO_F.last()
|| OKRESY.OKRES_DO:=OKRO_F.ref
|| OKRESY.OKRES_DO:=null
?};
{? OKRESY.OKRES_OD<>null & OKRESY.OKRES_DO<>null
|| exec('addokr','okresy','R')
?}


\dod_pol
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Dolaczanie automatyczne polrocza
::  OLD: \dod_pol/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
_indtym:=OKRO_F.ndx_tmp('',1,'ROK',,0,'POLROCZE',,0,'NR',,1);
OKRO_F.index(_indtym);
OKRO_F.prefix(OKRESY.ROK1);
_q:=0;
{? OKRO_F.first()
|| {! |? OKRESY.OKRES_DO:=OKRO_F.ref;
        _nr:=OKRO_F.POLROCZE;
        {! |? {? OKRO_F.POLROCZE=_nr
              || OKRESY.OKRES_OD:=OKRO_F.ref;
                _q:=OKRO_F.next()
              || _q:=1; 0
              ?}
        !};
        {? OKRESY.OKRES_OD<>null & OKRESY.OKRES_DO<>null
        || exec('addokr','okresy','P')
        ?};
        {? _q=1 || 1 || 0 ?}
    !}
|| 0
?};
OKRO_F.cntx_pop()


\dod_kwa
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Dolaczanie automatyczne kwartalu
::  OLD: \dod_kwa/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
_indtym:=OKRO_F.ndx_tmp('',1,'ROK',,0,'KWARTAL',,0,'NR',,1);
OKRO_F.index(_indtym);
OKRO_F.prefix(OKRESY.ROK1);
_q:=0;
{? OKRO_F.first()
|| {! |? OKRESY.OKRES_DO:=OKRO_F.ref;
        _nr:=OKRO_F.KWARTAL;
        {! |? {? OKRO_F.KWARTAL=_nr
              || OKRESY.OKRES_OD:=OKRO_F.ref;
                _q:=OKRO_F.next()
              || _q:=1; 0
              ?}
        !};
        {? OKRESY.OKRES_OD<>null & OKRESY.OKRES_DO<>null
        || exec('addokr','okresy','K')
        ?};
        {? _q=1 || 1 || 0 ?}
    !}
|| 0
?};
OKRO_F.cntx_pop()


\dod_mies
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Dolaczanie automatyczne miesiaca
::  OLD: \dod_mies/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.index('ROK');
OKRO_F.prefix(OKRESY.ROK1);
{? OKRO_F.first()
|| {! |?  OKRESY.OKRES_OD:=OKRO_F.ref;
          OKRESY.OKRES_DO:=OKRO_F.ref;
          exec('addokr','okresy','M');
          OKRO_F.next()
   !}
?}


\bg_zospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formula przed na akcje grupowa Zakonczenie okna wertowania OSPR.WER
::  OLD: \bg_zospr/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
{? _wy:=FUN.choice('Okresy raportowania:'@,,'Zakończ'@,'Otwórz'@); _wy>0
|| ospr_sel:=OSPR.sel_size(); ospr_ok:=0; ospr_wb:=_wy;1
?}


\ag_zospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MM [2006]
:: OPIS: Formula po na akcje grupowa Zakonczenie okna wertowania OSPR.WER
::  OLD: \ag_zospr/definicj.fml
::----------------------------------------------------------------------------------------------------------------------
{? ospr_wb=1
|| FUN.info( 'Liczba zaznaczonych okresów raportowania: %1.\n'
   'Liczba zakończonych okresów raportowania: %2.'@[$ospr_sel,$ospr_ok])
|| FUN.info( 'Liczba zaznaczonych okresów raportowania: %1.\n'
   'Liczba otworzonych okresów raportowania: %2.'@[$ospr_sel,$ospr_ok])
?};
&ospr_sel; &ospr_ok; &ospr_wb


\wydrokr
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Menu glowne - definicje - obsluga akcji Wydruk w tabeli OSPR
::  OLD: \wydrokr/wydruki.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rep_exec','#b_report','','fks_def_okr2',,,,,,,,'W')


\bw_okro_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Okienko przed okna SLO tabeli OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? var_press('OkroFInd')>0
|| OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref())
?};
1


\pp_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMA [7.20]
:: OPIS: Funkcja poprawiania roku bilansowego.
::  OLD: \pp_rok/skid_rok.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROK_F.lock(1,1,1)
|| {? REF.S_FIRMA=REF.GRUPA
   || ROK_F.win_edit('POP_GR')
   || ROK_F.win_edit('POP1')
   ?};
   plan_gr:=ROK_F.PLAN_GR;
   aktrozb:=ROK_F.AKT_ROZ;
   {? ROK_F.edit("exec('rokf_spr','okresy')")
   || {? ROK_F.put() & plan_gr<>'T' & ROK_F.PLAN_GR='T'
      || exec('am_rok','konsolidacja')
      ?};
      {? ROK_F.AKT_ROZ='T'
      || exec('uzup_akt_roz','okresy',ROK_F.ref)
      ?};
      ROK_F.cntx_psh(); OKRO_F.cntx_psh();
      {? SSTALE.AR=ROK_F.ref()
      || BILANSP.ASYNT:=ROK_F.SYNT;
         BILANSP.ASEP:=ROK_F.SEP
      ?};
      ROK_F.cntx_pop(); OKRO_F.cntx_pop()
   ?};
   VAR_DEL.delete('plan_gr','aktrozb');
   ROK_F.unlock();
   ROK_F.win_edit('POP1')
|| FUN.info('Inny użytkownik obsługuje tabelę lat bilansowych.'@)
?}


\akc_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zamykanie okresu w obszarze controlling - Zamknij okres
::   WE: _a - 1 - z poziomu tabeli OKRO
::            2 - z poziomu tabeli OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| _b_dom:=exec('szuk_b_dom','parses','CTR');
   {? ~_b_dom
   || FUN.info('W systemie nie znaleziono zdefiniowanego obszaru Controlling.'@);
      return(0)
   ?};
   OKRO_F.prefix();
   OKR_OBSZ.index('UNIK');
   OKR_OBSZ.prefix(OKRO.ref(),_b_dom);
   {? _b_dom & OKR_OBSZ.first()
   || {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
      OKR_OBSZ.OKRO_F()
   || FUN.info('Nie znaleziono okresu obszaru Controlling.'@);
      return(0)
   ?}
?};
{? OKRO_F.ZAM_CON='T'
|| FUN.info('Okres %1 jest już zamknięty.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
   return(0)
?};
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='CTR_PCO_MOKR';
_params.AKCJA:='zamknij';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.UIDREF:=OKRO_F.uidref();
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',OKRO_F.ref());
_params.PROC_START:='N';
exec('mp_run','#b__box',_params)


\akc_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Otwieranie okresu w obszarze controlling
::   WE: _a - 1 - z poziomu tabeli OKRO
::            2 - z poziomu tabeli OKRO_F
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<0 | _a=1
|| _b_dom:=exec('szuk_b_dom','parses','CTR');
   {? ~_b_dom
   || FUN.info('W systemie nie znaleziono zdefiniowanego obszaru Controlling.'@);
      return(0)
   ?};
   OKRO_F.prefix();
   OKR_OBSZ.index('UNIK');
   OKR_OBSZ.prefix(OKRO.ref(),_b_dom);
   {? _b_dom & OKR_OBSZ.first()
   || {? OKR_OBSZ.size()=2 & OKR_OBSZ.OKRO_F().POCZ=date(0,0,0) || OKR_OBSZ.next() ?};
      OKR_OBSZ.OKRO_F()
   || FUN.info('Nie znaleziono dla wybranego wspólnego okresu odpowiedniego okresu obszaru Controlling.'@);
      return(0)
   ?}
?};
{? OKRO_F.ZAM_CON<>'T'
|| FUN.info('Wskazany okres nie jest zamknięty.'@);
   return(0)
?};
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='CTR_PCO_MOKZ';
_params.AKCJA:='otwórz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.UIDREF:=OKRO_F.uidref();
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',OKRO_F.ref());
_params.PROC_START:='N';
exec('mp_run','#b__box',_params)


\status
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.22]
:: OPIS: Status okresu
::   WE: _a - okres (OKRO.ref)
::       _b - dziedzina (napis)
::   WY: -1-brak dziedziny 0-niedostępny, 1-otwarty, 2-zamknięty
::----------------------------------------------------------------------------------------------------------------------
_wyn:=-1;
_okro:=_a;
_domain:={? var_press('_b')=type_of('') || _b || '' ?};
B_DOMAIN.cntx_psh();
B_DOMAIN.index('SYMBOL'); B_DOMAIN.prefix();
{? _domain<>'' & B_DOMAIN.find_key(_domain,)
|| OKR_OBSZ.cntx_psh();
   OKR_OBSZ.index('UNIK'); OKR_OBSZ.prefix(_okro,B_DOMAIN.ref());
   {? OKR_OBSZ.first()
   || {? 'FKS,HBN,OBG,OBE,CTR'*_domain>0 | ('FST'*_domain>0 & OKR_OBSZ.OKRO_F().NR=0)
      || OKR_OBSZ.next()
      ?};
      OKRO_F.cntx_psh();
      OKR.cntx_psh();
      _zam:=
      {? 'FKS,HBN,OBG,OBE'*_domain>0
      || OKR_OBSZ.OKRO_F().ZAM='T'
      |? _domain='CTR'
      || OKR_OBSZ.OKRO_F().ZAM_CON='T'
      |? _domain='FST'
      || OKR_OBSZ.OKRO_F().AMOR='T'
      |? 'LMG,TTE,WYP'*_domain>0
      || 1+OKR_OBSZ.OKR().ZAM='T'
      |? _domain='LSP'
      || 1-OKR_OBSZ.OKR().ZAM-2='T'
      |? _domain='LZK'
      || OKR_OBSZ.OKR().ZAM+1='T'
      ?};
      OKR.cntx_pop();
      OKRO_F.cntx_pop();
      _wyn:=_zam+1
   || _wyn:=0
   ?};
   OKR_OBSZ.cntx_pop()
?};
B_DOMAIN.cntx_pop();
_wyn


\ini_rokf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Uzupełnia ROK_F_K.ROK_F
::  OLD: \ini_rokf/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F_K.index('LP'); ROK_F_K.prefix();
{? ~ROK_F_K.first()
|| exec('ini_rokf1','okresy');
   _i:=ROK_F_K.ndx_tmp('',1,'KOD',,);
   ROK_F_K.index(_i); ROK_F_K.prefix();
   ROK_F.for_each("
      {? ROK_F_K.find_key(ROK_F.KOD)
      || ROK_F_K.ROK_F:=ROK_F.ref();
         ROK_F_K.put()
      ?}
   ")
?}


\ini_rokf1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Uzupełnia ROK_F_K
::----------------------------------------------------------------------------------------------------------------------
_t:='0123456789abcdefghijklmnopqrstuvwxyz';
_n:=_lp:=0;
ROK_F_K.blank();
{!
|? _n+=1;
   {? _n<100
   || _lp+=1;
      ROK_F_K.LP:=_lp;
      ROK_F_K.KOD:=('0'+$_n)+2;
      ROK_F_K.add()
   || _p:=_n-100;
      _nz:=_p%*+_t+1;
      _kod:=(_nz+_t)+1;
      _nz:=_p%+_t+1;
      _kod2:=((_nz+_t)+1);
      {? '0123456789'*_kod=0 | '0123456789'*_kod2=0
      || _lp+=1;
         ROK_F_K.LP:=_lp;
         ROK_F_K.KOD:=_kod2+_kod;
         ROK_F_K.add()
      ?}
   ?};
   ROK_F_K.KOD<>'zz'
!}


\rokf_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Formuła na popraw sprawdzająca poprawność zredagowanych danych dla tabeli ROK_F
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? aktrozb='T' & ROK_F.AKT_ROZ='N'
|| {? ~FUN.ask('Wyłączenie parametru nie spowoduje wycofania wykonanej aktualizacji rozrachunków.\n'
               'Czy na pewno wyłączyć aktualizację rozrachunków w roku %1?'[ROK_F.NAZ]) || _wyn:='AKT_ROZ' ?}
?};
_wyn


\uzup_akt_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.14]
:: OPIS: Uzupełnia znacznik Aktualizacji rozrachnunków w poprzednich latach biasowych
::   WE: _a - ROK_F.ref
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
ROK_F.index('ROKPOCZ');
ROK_F.prefix(REF.FIRMA);
{? ROK_F.seek(_a)
|| {!
   |? {? ROK_F.prev()
      || {? ROK_F.AKT_ROZ<>'T'
         || ROK_F.AKT_ROZ:='T';
            ROK_F.put()
         ?};
         1
      ?}
   !}
?};
ROK_F.cntx_pop()


\sel_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Wyświetla okno wyboru okresu obrachukowego lub ustawia z bieżacej daty. Zmienia stałe systemu AO i AR.
::   WE: _a - data
::       _b - 0 (bez okna), 1 (okienko wyboru)
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_press('_a')>0 || _a || date() ?};
_b:={? var_press('_b')>0 || _b || 1 ?};
_result:=exec('szuk_okr','okresy',_a)<>null;
{? _b
|| OKRO_F.index('FIRMA_NR');
   OKRO_F.prefix(REF.FIRMA);
   OKRO_F.win_sel('WER2');
   {? OKRO_F.select(,1,25)
   || ROZNE.UT_OKROD:=OKRO_F.ref();
      _result:=1
   || ROZNE.UT_OKROD:=null();
      _result:=0
   ?}
?};
{? ~_result
|| {? _b || FUN.info('Nie wybrano okresu.'@) || FUN.info('Nie znaleziono okresu dla bieżącej daty kalendarzowej.'@) ?}
|? OKRO_F.ZAM='T'
|| FUN.info('Okres %1 jest zamknięty.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
   _result:=0
|? OKRO_F.ZAM_CON='T'
|| FUN.info('Okres %1 jest zamknięty controllingowo.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
   _result:=0
|| SSTALE.AO:=ROZNE.UT_OKROD;
   SSTALE.AR:=ROZNE.UT_OKROD().ROK
?};
_result


\npo_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formula po redakcji pola OKRO_F.NAZ
::----------------------------------------------------------------------------------------------------------------------
_a:=0;
{? ~chk_fld(1)
|| FUN.info('Nie wypełniona nazwa okresu.'@)
|| {? OKRO_F.NAZ='Bilans otwarcia' | OKRO_F.NAZ='Bilans zamknięcia'
   || FUN.info('Nazwa okresu zastrzeżona.\n Zmień nazwę okresu.'@)
   || _a:=1
   ?}
?};
_a


:Sign Version 2.0 jowisz:1045 2024/01/30 11:58:58 2a85e3deb64bc9ecb236712f3d723c3eccaefa134f35f0dfee0ed211442a70a8a79184cfc8812095c1542155e0cd7bd72b6207f2b69d6f690585c6d319e7d91b9695ee9c9aff4e3b99c04c5db3c869d91f0942cf671e1fd0373c62e04db9b7ef1f8e866f92f43282ade56f7fb4c59c47755ce974d29c7d7b482edd328723fd06

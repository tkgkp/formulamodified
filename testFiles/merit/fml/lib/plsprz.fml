:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: plsprz.fml
:: Utworzony: 02.11.2022
:: Autor: PD
::======================================================================================================================
:: Zawartość: Formuły do obslugi planu sprzedazy
::======================================================================================================================


\plps_pla
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przegląd planów sprzedaży
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PS_Z.F:='T';
BEER.MW:='';
exec('ustaw_ww','eurusd','F');
PS.index('A');
PS.prefix(PS_Z.F);
PS.win_sel('GRP');
PS.win_edit('RED');
AreaTitle.setTabWin(PS,~~);
AreaTitle.setTitle();
PS.actions('WER','N','Y:D',1);
PS.select();
PS.actions('WER');
~~


\lsp_psp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przegląd planów sprzedaży
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','lsp');
exec('plps_pla','plsprz')


\ps_s_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: po redakcji pola PS.S
::   WY: 1 - pole prawidlowo wypelnione, 0 -wpp
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_sym:=PS.S;
{? _sym<>''
||
   PS.cntx_psh();
   PS.index('S');
   PS.prefix(PS.S);
   _ps:=PS.ref();
   _sym:=PS.S;
   {? PS.first()
   ||
      {? -menu_txt='popraw'
      || {! |? {? PS.S=_sym & PS.ref()<>_ps || _wyn:=0 ?}; _wyn=1 & PS.next() !}
      || {! |? {? PS.S=_sym || _wyn:=0 ?}; _wyn=1 & PS.next() !}
      ?};
      {? _wyn=0
      || FUN.info('Plan sprzedaży o symbolu %1 już istnieje.'@[_sym])
      ?}
   ?};
   PS.cntx_pop()
||
   FUN.info('Nie wypełnione pole.'@);
   _wyn:=0
?};
_wyn


\ps_r_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: przed redakcja PS.R
::   WY: 1 - redakcja mozliwa, 0 - wpp
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt='dołącz' | -menu_txt='kopiuj'
||
   1
||
   exec('psp_open','plsprz');

   PSP.index('PS');
   PSP.prefix(PS.ref());
   {? PSP.first() || 0 || 1 ?}
?}


\ps_r_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: F3 PS.R
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
OKR.cntx_psh(); OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
_acr_sel:=OKR.mk_sel('Lata obrachunkowe'@,'T',0,'fir_02_rok',,,10);
OKR.win_fld(_acr_sel,,'ROK',,,10,,0,'Rok'@);
OKR.win_act(_acr_sel,0,'Formuła','Wybierz'@@,,'Zatwierdzenie wybranej pozycji'@,"sel_exit()",,1,,,,'W');
OKR.win_act(_acr_sel,0,'Kolejność');
OKR.win_sel(_acr_sel);
OKR.find_key(ST.AR);
{? OKR.select(,1,2) || PS.R:=OKR.ROK ?};
OKR.cntx_pop();
~~


\ps_r_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: po redakcja PS.R
::   WY: 1 - wlasciwa wartosc pola, 0 - wpp
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
OKR.cntx_psh();
OKR.index('MC'); OKR.prefix(REF.FIRMA,1,PS.R);
{? ~OKR.first() || FUN.emsg('Niezdefiniowany rok w systemie.'@); _wyn:=0 ?};
OKR.cntx_pop();
_wyn


\ps_filtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: przed filtr okna WER tabeli PS
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_psz_f:=PS_Z.F;
PS_Z.win_edit('FILTR');
{? PS_Z.edit()
||
   {? _psz_f<>PS_Z.F
   ||
      PS.prefix(PS_Z.F);
      AreaTitle.setTitle()
   ?}
||
   PS_Z.F:=_psz_f
?};
~~


\ps_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: Usuwa plan sprzedazy
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? exec('ps_chk','plsprz')
||
   {? FUN.ask('Czy usunąć plan sprzedaży o symbolu "%1"?'[PS.S])
   ||
      exec('psp_open','plsprz');
      PSP.index('PS');
      PSP.prefix(PS.ref());
      {? PSP.first() || {! |? PSP.del() !} ?};
      PSDEF.index('P');
      PSDEF.prefix(PS.ref());
      {? PSDEF.first() || {! |? PSDEF.del() !} ?};
      PS.del()
   ?}
?};
~~


\ps_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: sprawdzenie czy plan jest dostepny
::   WY: 1-plan dostepny, 0-wpp
::----------------------------------------------------------------------------------------------------------------------
{? PS.OK='T'
|| FUN.info('Plan zaakceptowany.\nModyfikacje niemożliwe.'@);0
|| {? PS.r_lock(1,1,1)
   || PS.r_unlock();1
   || FUN.info('Pozycja obsługiwana przez inego użytkownika.\nModyfikacje niemożliwe.'@);0
   ?}
?}


\psp_open
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: otwarcie tabeli PPP wg maski - otwieramy wg roku z naglowka
::   WE: _a - rok (rrrr)
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _<1 || _a:=PS.R ?};

_maska:=$_a+2;
PSP.use('psp__'+ST.ODDZ+_maska);
~~


\ps_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: przed pozycje okna WER tabeli PS
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('psp_tree','plsprz',0,1); ~~


\psp_tree
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: Redakcja i podglad planu sprzedazy w strukturze drzewiastej
::       Kontekst wywołania - rekord tabeli PS (nagłówek planu)
::   WE:  _a  - =0-pozycje planu, =1-wykonanie, =2-powolywanie zlecen w Produkcji, =3- powolywanie pozycji planu
::        _b  - =0-zwiniete drzewo, =1-rozwiniete
::       [_c] - czy wyswietlac select (T/N - domyslnie T)
::   WY: 1 - wyjscie przez sel_exit, 0 - pozostale przypadki
::----------------------------------------------------------------------------------------------------------------------
_variant:=_a;
__expand:=_b;
_display:={? var_pres('_c')<>type_of('') || 'T' || _c ?};

__ps:=PS.ref();

:: opis: 0-kod, 1-nazwa
__psopis:=1;

:: sprawdzenie czy zdefiniowano strukture planu
PSDEF.index('P');
PSDEF.prefix(__ps);
{? ~PSDEF.first()
||
   {? FUN.ask('Brak struktury planu.\nCzy chcesz zdefiniować strukturę planu?'@)
   || PSDEF.cntx_psh;
      exec('ps_str','plsprz');
      PSDEF.cntx_pop
   ?};
   {? ~PSDEF.first() || return(0) ?}
?};

:: struktura wykorzystywana do redakcji i podgladu planu
VAR_DEL.delete('__plps');

__plps:=sql(
   'select '
      'CAST (NULL AS TREE_REF_TYPE) PARENT, '
      'space(210) KOL, '
      'space(200) O, '
      'CAST (0 AS DATE_TYPE) OD, '
      'CAST (0 AS DATE_TYPE) DO, '
      'CAST (0 AS REAL_TYPE) IL, '
      'CAST (0 AS REAL_TYPE) W, '
      'CAST (0 AS REAL_TYPE) ILW, '
      'CAST (0 AS REAL_TYPE) WW, '
      'CAST (0 AS REAL_TYPE) ILWP, '
      'CAST (0 AS REAL_TYPE) WWP, '
      'CAST (0 AS REAL_TYPE) ILZL, '

      'CAST (0 AS DATE_TYPE) OD_S, '
      'CAST (0 AS DATE_TYPE) DO_S, '
      'CAST (0 AS REAL_TYPE) IL_S, '
      'CAST (0 AS REAL_TYPE) W_S, '
      'CAST (0 AS REAL_TYPE) ILW_S, '
      'CAST (0 AS REAL_TYPE) WW_S, '
      'CAST (0 AS REAL_TYPE) ILWP_S, '
      'CAST (0 AS REAL_TYPE) WWP_S, '
      'CAST (0 AS REAL_TYPE) ILZL_S, '
      '0 as P, '
      'REFERENCE as REF, '

      'space(1) as ZLGEN, '
      'space(1) as ZLALL, '

      'space(210) P1, '
      'space(210) P2, '
      'space(210) P3, '
      'space(210) P4, '
      'space(210) P5, '
      'space(210) P6, '
      'space(210) P7, '
      'space(210) P8, '
      '\'\' RAZEM '

   'from '
      'SYSLOG where 1=0 '
   'order by 1,2'
);

_formula:="
   __plps.ZLGEN:={? __plps.ILZL>0 || 'T' || 'N' ?};
   __plps.ZLALL:={? __plps.ILZL>=__plps.IL || 'T' || 'N' ?};
   1
";
__plps.trig_b('add',_formula);
__plps.trig_b('put',_formula);

{? __Logupr.CS<>0
||
   __plps.fld_fml('W','BEFORE_DISPLAY',"0");
   __plps.fld_fml('WW','BEFORE_DISPLAY',"0");
   __plps.fld_fml('W','BEFORE_EDIT',"0")
?};

:: struktura asocjacyjna dla wezlow drzewa
VAR_DEL.delete('__asoc');

__asoc:=tab_tmp(0,
   'P1','STRING[16]',,
   'P2','STRING[16]',,
   'P3','STRING[16]',,
   'P4','STRING[16]',,
   'P5','STRING[16]',,
   'P6','STRING[16]',,
   'P7','STRING[16]',,
   'P8','STRING[16]',,
   'PLPS','STRING[16]',
);
__asi1:=__asoc.ndx_tmp(,1,'P1',,,'P2',,,'P3',,,'P4',,,'P5',,,'P6',,,'P7',,,'P8',,);
__asi2:=__asoc.ndx_tmp(,1,'PLPS',,);
__asoc.index(__asi1); __asoc.prefix();

:: ilosc poziomow w strukturze
__ps_ilp:={? PSDEF.last() || PSDEF.P ?};

:: zmienna przechowujaca akronim tabel wg definicji struktury
VAR_DEL.delete('__ps_akr','__ps_o','__ps_war');

__ps_akr:=obj_new(__ps_ilp);
__ps_o:=obj_new(__ps_ilp);
__ps_war:=obj_new(__ps_ilp);
{? PSDEF.first()
||
   {! _ii:=1..__ps_ilp
   |!
      __ps_akr[_ii]:=PSDEF.A;
      __ps_o[_ii]:=PSDEF.O;
      PSDEF.next()
   !}
?};

:: zmienna przechowujaca aktualna sciezke struktury
VAR_DEL.delete('__ps_pth');

_null:=BB.refsql(null);
__ps_pth:=obj_new(__ps_ilp); {! _ii:=1..__ps_ilp |! __ps_pth[_ii]:=_null !};

:: budowa indeksu wg definicji struktury
_ind:=''; {! _ii:=1..__ps_ilp |! _ind+=',,,\''+__ps_akr[_ii]+'\'' !};
__pspndx:=($('PSP.ndx_tmp(,1,\'PS\''+_ind+',,)'))();

:: przepisanie pozycji planu do struktury drzewiastej
exec('psp_open','plsprz');
PSP.index(__pspndx);
PSP.prefix(__ps);
:: dodanie root drzewa
__plps.blank();
__plps.PARENT:=null;
__plps.O:='Razem';
__plps.P:=0;
__plps.KOL:='001'+ (207*'0');
__plps.RAZEM:='T';
__plps.add();
_root:=__plps.ref();
{? PSP.first()
||
   _parent:=_root;
   {!
   |? _aktasoc:=0;
      _pspref:=$PSP.ref();
      {! _ii:=1..__ps_ilp-1
      |! _pspfldr:=BB.refsql(($('PSP.'+__ps_akr[_ii]))());
         {? _pspfldr<>_null & __ps_pth[_ii]<>_pspfldr
         ||
::          dodanie zapisow na posrednich poziomach drzewa
            _parent:=
               {? _ii=1
               ||
                  _parent:=_root
               ||
                  _key:='';
                  {! _jj:=1..__ps_ilp
                  |! _prfx:={? _jj=1 || '' || ',' ?};
                     {? _jj>_ii-1
                     ||
                        _key+=_prfx+'\''+_null+'\''
                     ||
                        _key+=_prfx+'\''+BB.refsql(($('PSP.'+__ps_akr[_jj]))())+'\''
                     ?}
                  !};
                  {? ($('__asoc.find_key('+_key+')'))()=1 || BB.sqlint(__asoc.PLPS) ?}
               ?};
            __plps.blank();
            __plps.PARENT:=_parent;
            __plps.O:=exec('opis','plsprz',__ps_akr[_ii],__psopis);
            __plps.P:=_ii;
            __plps.OD:=PSP.PSOKR().OD;
            __plps.DO:=PSP.PSOKR().DO;
            __plps.KOL:=exec('plps_kol','plsprz');
            __plps.RAZEM:='N';
            ($('__plps.P'+$_ii))():=__ps_war[_ii]:=__plps.O;
            {? _ii>1
            ||
               _kk:=_ii-1;
               {!|?
                  ($('__plps.P'+$_kk))():=__ps_war[_kk];
                  _kk-=1;
                  _kk>0
               !}
            ?};
            __plps.add();

            _parent:=__plps.ref();
::          ustalenie aktualnej sciezki struktury i uzupelnienie tabeli asocjacji
            __ps_pth[_ii]:=_pspfldr;
            {! _jj:=1..__ps_ilp
            |!
               {? _jj>_ii
               ||
                  ($('__asoc.P'+$_jj))():=__ps_pth[_jj]:=_null
               ||
                  ($('__asoc.P'+$_jj))():=BB.refsql(($('PSP.'+__ps_akr[_jj]))())
               ?}
            !};
            __asoc.PLPS:=BB.refsql(_parent);
            __asoc.add()
         ?}
      !};
      {? BB.refsql(($('PSP.'+__ps_akr[__ps_ilp]))())<>_null
      ||
::       dodanie zapisu na ostatnim pozimie drzewa
         __plps.blank();
         __plps.PARENT:=_parent;
         __plps.O:=exec('opis','plsprz',__ps_akr[__ps_ilp],__psopis);
         __plps.OD:=__plps.OD_S:=PSP.PSOKR().OD;
         __plps.DO:=__plps.DO_S:=PSP.PSOKR().DO;
         __plps.IL:=__plps.IL_S:=PSP.IL;
         __plps.W:=__plps.W_S:=PSP.W;
         __plps.ILW:=__plps.ILW_S:=PSP.ILW;
         __plps.WW:=__plps.WW_S:=PSP.WW;
         __plps.ILWP:=__plps.ILWP_S:={? __plps.IL=0 || 0 || ((100*__plps.ILW)/__plps.IL)$1 ?};
         __plps.WWP:=__plps.WWP_S:={? __plps.W=0 || 0 || ((100*__plps.WW)/__plps.W)$1 ?};
         __plps.ILZL:=PSP.ILZL;
         __plps.P:=__ps_ilp;
         __plps.REF:={? __plps.P=__ps_ilp || $PSP.ref() || '' ?};
         __plps.KOL:=exec('plps_kol','plsprz');
         __plps.RAZEM:='N';
         ($('__plps.P'+$__ps_ilp))():=__plps.O;
         {? __ps_ilp>1
         ||
            _kk:=__ps_ilp-1;
            {!
            |?
               ($('__plps.P'+$_kk))():=__ps_war[_kk];
               _kk-=1;
               _kk>0
            !}
         ?};
         __plps.add();
::       agregowanie wartosci na posrednich poziomach drzewa
         exec('agreguj','plsprz',__plps.IL,__plps.W,__plps.ILW,__plps.WW,__plps.ILZL)
      ?};
      PSP.next()
   !}
?};

:: definiowanie okna wertowania planu sprzedazy
_title:={? _a=1 || 'Wykonanie planu sprzedaży'@ || 'Plan sprzedaży'@ ?};
_win_grp:=__plps.grp_make(_title,,'plsprz_grp',,,,,'maximized');
__win_acr:=__plps.mk_sel(,'N',,'plsprzplps',,,,,'U',,,,,,,'on');
__win_acr_t:=__plps.mk_sel(,'P',,'plsprzplps_tree',,,,1);

__plps.win_sel(_win_grp);
{? REF.MOBILE='T'
||
   __plps.win_fld(__win_acr,,'P'+$__ps_ilp,,,15,,,__ps_o[__ps_ilp]);
   {? __ps_ilp>1
   ||
      {! _ii:=1..__ps_ilp
      |! __plps.win_fld(__win_acr,,'P'+$_ii,,,15,,,__ps_o[_ii])
      !}
   ?}
||
   {! _ii:=1..__ps_ilp
   |! __plps.win_fld(__win_acr,,'P'+$_ii,,,15,,,__ps_o[_ii])
   !}
?};
__plps.win_fld(__win_acr,,'OD_S',,,10,,,'Od'@);
__plps.win_fld(__win_acr,,'DO_S',,,10,,,'Do'@);
__plps.win_fld(__win_acr,,'IL_S',,,14,4,,'Ilość'@);

__plps.win_fld(__win_acr_t,,'O',,,33,,,'Opis'@);
__plps.win_fld(__win_acr_t,,'OD',,,10,,,'Od'@);
__plps.win_fld(__win_acr_t,,'DO',,,10,,,'Do'@);
__plps.win_fld(__win_acr_t,,'IL',,,14,4,,'Ilość'@);

{? _variant=1
|| __plps.win_fld(__win_acr,,'ILW_S',,,9,4,,'Wykonanie'@);
   __plps.win_fld(__win_acr,,'ILWP_S',,,5,1,,'%');
   __plps.win_fld(__win_acr_t,,'ILW',,,9,4,,'Wykonanie'@);
   __plps.win_fld(__win_acr_t,,'ILWP',,,5,1,,'%')
?};
__plps.win_fld(__win_acr,,'W_S',,,16,2,,'Wartość'@);
__plps.win_fld(__win_acr_t,,'W',,,16,2,,'Wartość'@);
{? _variant=1
|| __plps.win_fld(__win_acr,,'WW_S',,,11,2,,'Wykonanie'@);
   __plps.win_fld(__win_acr,,'WWP_S',,,5,1,,'%');
   __plps.win_fld(__win_acr_t,,'WW',,,11,2,,'Wykonanie'@);
   __plps.win_fld(__win_acr_t,,'WWP',,,5,1,,'%')
?};

{? _variant=2
|| __plps.win_fld(__win_acr,,'ILZL',,,14,4,,'Ilość zlecona'@);
   __plps.win_fld(__win_acr_t,,'ILZL',,,14,4,,'Ilość zlecona'@);
   __plps.fld_fml('ILZL','DISPLAY_FORMAT',"'empty=%1'[{? __plps.REF='' || '1' || '0' ?}]")
?};

::formuły określające kolory okienek
VAR_DEL.delete('__color');
__color:="
   {? __plps.P=1 || Color.fnd_kol('PSP#01#01')
   |? __plps.P=2 || Color.fnd_kol('PSP#01#02')
   |? __plps.P=3 || Color.fnd_kol('PSP#01#03')
   |? __plps.P=4 || Color.fnd_kol('PSP#01#04')
   |? __plps.P=5 || Color.fnd_kol('PSP#01#05')
   |? __plps.P=6 || Color.fnd_kol('PSP#01#06')
   |? __plps.P=7 || Color.fnd_kol('PSP#01#07')
   |? __plps.P=8 || Color.fnd_kol('PSP#01#08')
   || '0:0:0,255:255:255'
   ?}
";
_empty:="
   {? cur_win_id()='PS1' & (((cur_afld()+1)='S' & __plps.P<>__ps_ilp)
      | ((1+cur_afld())='P' & ($__plps.P)<>(cur_afld()+1))) & REF.MOBILE<>'T'
   || 'empty=1'
   || 'empty=0'
   ?}
";
__plps.fld_fml('P1','DISPLAY_FORMAT',_empty);
__plps.fld_fml('P2','DISPLAY_FORMAT',_empty);
__plps.fld_fml('P3','DISPLAY_FORMAT',_empty);
__plps.fld_fml('P4','DISPLAY_FORMAT',_empty);
__plps.fld_fml('P5','DISPLAY_FORMAT',_empty);
__plps.fld_fml('P6','DISPLAY_FORMAT',_empty);
__plps.fld_fml('P7','DISPLAY_FORMAT',_empty);
__plps.fld_fml('P8','DISPLAY_FORMAT',_empty);
__plps.fld_fml('OD_S','DISPLAY_FORMAT',_empty);
__plps.fld_fml('DO_S','DISPLAY_FORMAT',_empty);
__plps.fld_fml('IL_S','DISPLAY_FORMAT',_empty);
__plps.fld_fml('W_S','DISPLAY_FORMAT',_empty);
__plps.fld_fml('ILW_S','DISPLAY_FORMAT',_empty);
__plps.fld_fml('WW_S','DISPLAY_FORMAT',_empty);
__plps.fld_fml('ILWP_S','DISPLAY_FORMAT',_empty);
__plps.fld_fml('WWP_S','DISPLAY_FORMAT',_empty);

:: Plany aktywne - niezatwierdzone
{? _variant=0 & PS.A='T' & PS.OK='N'
||
:: Zmienna z uprawnieniami do rejestracji i modyfikacji
   VAR_DEL.delete('__usr_upr');
   __usr_upr:=exec('chk_role','#b__box',OPERATOR.USER,'LSP_PSP_DPSP');
   __plps.win_act(__win_acr,0,'Formuła','Dołącz'@@,,,,"exec('psp_dol','plsprz')",1,,,,'D');
   __plps.win_act(__win_acr,1,'Formuła','Dołącz'@@,,,,"exec('psp_dol','plsprz')",1,,,,'D');
   __plps.win_act(__win_acr,0,'Formuła','Usuń'@@,,,,"exec('psp_usu','plsprz',1)",,1,
      "sel_nchk(); {? FUN.ask('Usunąć zaznaczone elementy?'@) || __pl_gr:=1; 1 || 0 ?}",,'U');
   __plps.win_act(__win_acr,0,'Formuła','Popraw'@@,,,,"exec('psp_pop','plsprz')",,,,,'P');
   __plps.win_act(__win_acr,0,'Rekord',,,,"
      __plps.cntx_psh();
      __plps.prefix();
      _war:=__plps.first() & __plps.next();
      __plps.cntx_pop();
      _act:={? _war=1 || '' || 'AZ' ?};
      {? __plps.P=0 || _act+='UP' ?};
      {? __usr_upr=0 || _act+='DUP' ?};
      _default:={? __plps.P=__ps_ilp || 'P' || 'D' ?};
      __plps.actions(__win_acr,_act,_default,1);
      __color()
   ");
   __plps.win_act(__win_acr_t,0,'Formuła','Dołącz'@@,,,,"exec('psp_dol','plsprz')",1,,,,'D');
   __plps.win_act(__win_acr_t,0,'Formuła','Usuń'@@,,,,"exec('psp_usu','plsprz',1)",,1,
      "sel_nchk(); {? FUN.ask('Usunąć zaznaczone elementy?'@) || __pl_gr:=1; 1 || 0 ?}",,'U');
   __plps.win_act(__win_acr_t,0,'Formuła','Popraw'@@,,,,"exec('psp_pop','plsprz')",,,,,'P');
   __plps.win_act(__win_acr_t,0,'Rekord',,,,"
      __plps.cntx_psh();
      _war:=__plps.first() & __plps.next();
      __plps.cntx_pop();
      _act:={? _war=1 || '' || 'AZ' ?};
      {? __plps.P=0 || _act+='UP' ?};
      {? __usr_upr=0 || _act+='DUP' ?};
      _default:={? __plps.P=__ps_ilp || 'P' || 'D' ?};
      __plps.actions(__win_acr_t,_act,_default,1);
      __color()
   ")

:: Wykonanie
|? _variant=1
||
   __plps.win_act(__win_acr,0,'Formuła','Aktualizuj'@@,,,,
      "{? exec('ps_wyk_akt','plsprz')=1 || __ident:='PS1'; sel_exit() ?}",,,,,'A',,'target=window');
   __plps.win_act(__win_acr,0,'Formuła','Dokumenty'@@,,,,"exec('ps_wyk_dok','plsprz')",1,,,,'D');
   __plps.win_act(__win_acr,0,'Rekord',,,,"
      __plps.cntx_psh();
      __plps.prefix();
      _war:=__plps.first() & __plps.next();
      __plps.cntx_pop();
      _act:={? _war=1 || {? PS.A='N' || 'A' || '' ?} || 'AZ' ?};
      {? __plps.P<__ps_ilp || _act+='D' ?};
      _default:={? __plps.P=__ps_ilp || 'D' || 'Z' ?};
      __plps.actions(__win_acr,_act,_default,1);
      __color()
   ");
   __plps.win_act(__win_acr_t,0,'Formuła','Aktualizuj'@@,,,,
      "{? exec('ps_wyk_akt','plsprz')=1 || __ident:='PS2'; sel_exit() ?}",,,,,'A',,'target=window');
   __plps.win_act(__win_acr_t,0,'Formuła','Dokumenty'@,,,,"exec('ps_wyk_dok','plsprz')",1,,,,'D');
   __plps.win_act(__win_acr_t,0,'Rekord',,,,"
      __plps.cntx_psh();
      _war:=__plps.first() & __plps.next();
      __plps.cntx_pop();
      _act:={? _war=1 || {? PS.A='N' || 'A' || '' ?} || 'AZ' ?};
      {? __plps.P<__ps_ilp || _act+='D' ?};
      _default:={? __plps.P=__ps_ilp || 'D' || 'Z' ?};
      __plps.actions(__win_acr_t,_act,_default,1);
      __color()
   ")

:: Plany aktywne - zatwierdzone
|? (_variant=0 | _variant=2) & PS.A='T' & PS.OK='T'
||
:: Środowisko generatora z pozycji planu sprzedaży
   _env_zlisty:=exec('env_zlisty','zl_gen',2,__plps);
   params_set('env_zlisty',_env_zlisty);

   __plps.win_act(__win_acr,0,'Formuła','Generuj zlecenie'@@,,,,
      "params_exec('action_psp_generuj','zl_gen')",1,1,
      "params_exec('action_psp_generuj_bg','zl_gen')","params_exec('action_psp_generuj_ag','zl_gen')",'G');
   task_attach('TTE_PZL_DZPS');
   __plps.win_act(__win_acr,,'Formuła','Zlec&enia'@@,,,"exec('psp_zl','zl_gen')",,,,,,'E');
   __plps.win_act(__win_acr,0,'Rekord',,,,"
      _rr:=__plps.REF='';
      _grayed:='';
      {? _rr || {? __plps.sel_size()>0 || _grayed:='E'+_grayed ||  _grayed:='GE'+_grayed ?} ?};
      __plps.actions_grayed(__win_acr,_grayed);
      __color()
   ");
   __plps.win_act(__win_acr_t,0,'Formuła','Generuj zlecenie'@@,,,,
      "params_exec('action_psp_generuj','zl_gen')",1,1,
      "params_exec('action_psp_generuj_bg','zl_gen')","params_exec('action_psp_generuj_ag','zl_gen')",'G');
   task_attach('TTE_PZL_DZPS');
   __plps.win_act(__win_acr_t,,'Formuła','Zlec&enia'@@,,,"exec('psp_zl','zl_gen')",,,,,,'E');
   __plps.win_act(__win_acr_t,0,'Rekord',,,,"
      _rr:=__plps.REF='';
      _grayed:='';
      {? _rr || {? __plps.sel_size()>0 || _grayed:='E'+_grayed ||  _grayed:='GE'+_grayed ?} ?};
      __plps.actions_grayed(__win_acr_t,_grayed);
      __color()
   ")

:: Brak specjalnych akcji - tylko kolorowanie
||
   __plps.win_act(__win_acr,0,'Rekord',,,,"__color()");
   __plps.win_act(__win_acr_t,0,'Rekord',,,,"__color()")
?};

__plps.win_act(__win_acr,0,'Szukaj',,,,,);
__plps.win_act(__win_acr,0,'Formuła','Legenda'@@,,,"exec('legenda','color','PSP#01')",,,,,,'L',,'target=window');
__plps.win_act(__win_acr_t,0,'Formuła','Zwiń/rozwiń'@@,,,,"exec('show_tree','plsprz','__plps')",,,,,'Z');
__plps.win_act(__win_acr_t,0,'Szukaj',,,,,);
__plps.win_act(__win_acr_t,0,'Formuła','Legenda'@@,,,"exec('legenda','color','PSP#01')",,,,,,'L',,'target=window');

__plps.tr_fml(__win_acr_t,,
   "{? _a=-1 || __expand || _a ?}",
   "__plps.cntx_psh(); _rr:=__plps.find_key(__plps.ref()); __plps.cntx_pop(); _rr");
__pl_gr:=0;
__plps.hdr_sel(); __plps.hdr_sel(' — '+PS.S);

__plps.fld_attr('P',2);
__plps.fld_attr('REF',2);

{? REF.MOBILE='T'
|| __ndx_plps:=__plps.ndx_tmp(,,'P',,,'KOL',,)
|| __ndx_plps:=__plps.ndx_tmp(,,'RAZEM',,,'KOL',,)
?};

_plps_before:="
   __plps.cntx_psh();
   __plps.index(__ndx_plps);
   {? REF.MOBILE='T'
   || __plps.prefix(__ps_ilp,)
   || __plps.prefix('N',)
   ?};
   1
";
_plps_after:="
   __plps.cntx_pop();
   1
";

__plps.grp_sel(_win_grp,,__win_acr,_title,,,,,_plps_before,_plps_after,,,'maximized','PS1');
__plps.grp_sel(_win_grp,,__win_acr_t,_title+' struktura'@,,,,,,,,,'maximized','PS2');

_ident:={? var_pres('__ident')=type_of('') || __ident || 'PS1' ?};
{? _display='T'
|| _return:=__plps.select(,,,,_ident)
|| _return:=__plps.size()
?};

PSP.ndx_drop(__pspndx);
__plps.ndx_drop(__ndx_plps);

VAR_DEL.delete('__pl_ps','__asoc','__ps_akr','__ps_o','__ps_war','__ps_pth','__color','__usr_upr');
VAR_DEL.delete('__expand','__ps','__psopis','__ps_ilp','__pspndx','__win_acr','__asi1','__asi2','__pl_gr','__ndx_plps');

{? _a=2
|| VAR.fld_fml('REAL','DISPLAY_FORMAT',"*")
?};

_return


\ps_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: definiowanie struktury planu
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('psp_open','plsprz');
PSP.index('PS');
PSP.prefix(PS.ref());

_wer:=PSDEF.mk_sel('Struktura planu sprzedaży'@,,0,,5,5,8,,'U');
PSDEF.win_sel(_wer);

PSDEF.win_fld(_wer,,'O',,,40);

{? ~PSP.first() & PS.A<>'N'
||
   PSDEF.win_act(_wer,1,'Formuła','Dołącz'@@,,,"exec('ps_str_dol','plsprz')",,1,,,,'D');
   PSDEF.win_act(_wer,0,'Formuła','Dołącz'@@,,,"exec('ps_str_dol','plsprz')",,1,,,,'D');
   PSDEF.win_act(_wer,0,'Formuła','Usuń'@@,,,
   "
      {? FUN.ask('Usunąć pozycję?'@)
      || _lp:=PSDEF.P; PSDEF.del(); {? _lp<PSDEF.P || exec('reNrDel','#table','PSDEF','P') ?}
      ?}
   ",,,,,,'U')
?};

PSDEF.index('P');
PSDEF.prefix(PS.ref());

PSDEF.select();
~~


\ps_str_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: dodanie elementu struktury
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,
   'LP','INTEGER','Liczba porządkowa',
   'O','STRING[100]','Opis',
   'A','STRING[8]','Akronim tabeli');

PSDEF.cntx_psh();
_str:='|'; {? PSDEF.first() || {! |? _str+=PSDEF.A+'|'; PSDEF.next() !} ?};
PSDEF.cntx_pop();

_lp:=0;
{? _str*'|PSOKR|'=0
||
   _lp+=1; _tab.LP:=_lp; _tab.O:='Kwartały'; _tab.A:='PSOKR'; _tab.add();
   _lp+=1; _tab.LP:=_lp; _tab.O:='Miesiące'; _tab.A:='PSOKR'; _tab.add();
   _lp+=1; _tab.LP:=_lp; _tab.O:='Tygodnie'; _tab.A:='PSOKR'; _tab.add();
   _lp+=1; _tab.LP:=_lp; _tab.O:='Dni'; _tab.A:='PSOKR'; _tab.add()
?};
{? _str*'|MGR|'=0 & _str*'|MGRP|'=0 & _str*'|M|'=0
||
   _lp+=1; _tab.LP:=_lp; _tab.O:='Grupy materiałów'; _tab.A:='MGR'; _tab.add()
?};
{? _str*'|MGRP|'=0 & _str*'|M|'=0
||
   _lp+=1; _tab.LP:=_lp; _tab.O:='Podgrupy materiałów'; _tab.A:='MGRP'; _tab.add()
?};
{? _str*'|M|'=0
||
   _lp+=1; _tab.LP:=_lp; _tab.O:='Materiały'; _tab.A:='M'; _tab.add()
?};
{? _str*'|GRKH|'=0 & _str*'|KH_ODB|'=0 & _str*'|KH|'=0
||
   _lp+=1; _tab.LP:=_lp; _tab.O:='Grupy kontrahentów'; _tab.A:='GRKH'; _tab.add()
?};
{? _str*'|KH|'=0 & _str*'|KH_ODB|'=0
||
   _lp+=1; _tab.LP:=_lp; _tab.O:='Kontrahenci'; _tab.A:='KH'; _tab.add()
?};
{? _str*'|KH_ODB|'=0
||
   _lp+=1; _tab.LP:=_lp; _tab.O:='Odbiorcy'; _tab.A:='KH_ODB'; _tab.add()
?};
{? _str*'|HAN|'=0
||
   _lp+=1; _tab.LP:=_lp; _tab.O:='Handlowcy'; _tab.A:='HAN'; _tab.add()
?};

{? ~_tab.first()
||
   FUN.info('Nie można dodać więcej elementów struktury.'@)
||
   _wer:=_tab.mk_sel('Elementy struktury planu sprzedaży'@,,0,,7,16,11,,'U');
   _tab.win_sel(_wer);

   _tab.win_fld(_wer,,'O',,,40);

   _tab.win_act(_wer,0,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');

   {? _tab.select()
   ||
      PSDEF.last();
      PSDEF.PS:=PS.ref();
      PSDEF.P+=1;
      PSDEF.O:=_tab.O;
      PSDEF.A:=_tab.A;
      PSDEF.add()
   ?}
?};
~~


\psp_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: dolacz pozycje planu sprzedazy
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
::jeżeli plan jest przeznaczony do planów dostawy do wyłączamy możliwość dodawania nowych pozycji
{? PS.PD='T'
||
   PSDEF.cntx_psh();
   PSDEF.index('P');
   PSDEF.prefix(PS.ref());
   {? PSDEF.last() & PSDEF.A<>'M'
   || FUN.info('Plan sprzedaży jest uwzględniany w planach dostaw.
      \nOstatni poziom struktury musi odnosić się do materiałów.'@);
      PSDEF.cntx_pop();
      return(~~)
   ?};
   PSDEF.cntx_pop()
?};
:: ustalenie rodzica i poziomu dla ktorego dodawane beda elementy
__plps.cntx_psh();
__plps.prefix();
_choice:=1;
{? __plps.P=1 & cur_win_id()='PS1' & obj_len(__ps_o)>1
|| _choice:=FUN.choice('Z jakiego poziomu chcesz dodać pozycję?'@,1,__ps_o[1],__ps_o[2]);
   {? _choice=1 & ~__plps.seek(__plps.PARENT,)
   ||
      FUN.info('Nie udało się dodać pozycji'@);
      __plps.cntx_pop();
      return(~~)
   ?}
?};
{? __ps_ilp=__plps.P
||
   __plps.cntx_psh(); __plps.seek(__plps.PARENT,); _parent:=__plps.ref(); __plps.cntx_pop();
   _poziom:=__plps.P
||
   _parent:=__plps.ref();
   _poziom:=__plps.P+1
?};

PSDEF.cntx_psh();
PSDEF.index('P');
PSDEF.prefix(PS.ref(),_poziom);
{? ~PSDEF.first()
||
   FUN.info('Nie zdefiniowano poziomu %1.'[$_poziom])
|? _choice
||
   VAR_DEL.delete('__tab');

   __acr:=PSDEF.A;
   __tab:=($__acr)();
   _hdrsel:=_opis:='';

   _wer:=__tab.mk_sel(,'P',0,'__tabplsprz',,,,,'U');
   __tab.win_sel(_wer);

   __tab.win_act(_wer,0,'Formuła','Zatwierdź'@@,,'Zatwierdzenie wybranych pozycji'@,
      "{? __mgr_sel=0 || sel_exit() ?}",,1,1,"__mgr_sel:=__tab.sel_size();__slosel:=__tab.sel_aget(); sel_exit()",,'Z');
   __tab.win_act(_wer,0,'Kolejność');

   {? PSDEF.A='MGR'
   ||
      __tab.win_fld(_wer,,'KOD',,,8);
      __tab.win_fld(_wer,,'NAZ',,,30);
      __tab.hdr_sel('Grupy materiałowe'@);
      __tab.index('MGR');
      __tab.prefix();
      _opis:={? __psopis=1 || '__tab.NAZ' || '__tab.KOD' ?}
   |? PSDEF.A='MGRP'
   ||
      __tab.win_fld(_wer,,'MGR','KOD',,8);
      __tab.win_fld(_wer,,'MGR','NAZ',,30);
      __tab.win_fld(_wer,,'KOD',,,8);
      __tab.win_fld(_wer,,'NAZ',,,30);
      __tab.hdr_sel('Podgrupy materiałowe'@);
      __tab.index('MGR');
      {? (_zal_od:=exec('zalezy_od','plsprz','MGR'))<>null
      || __tab.prefix(_zal_od)
      || __tab.prefix()
      ?};
      _opis:={? __psopis=1 || '__tab.NAZ' || '__tab.KOD' ?}
   |? PSDEF.A='M'
   ||
      {? (_zal_od:=exec('zalezy_od','plsprz','MGRP'))<>null
      || __tab.index('AMGRPM');
         MGR.cntx_psh();
         MGRP.cntx_psh();
         MGRP.prefix();
         _tow_usl:={? MGRP.seek(_zal_od,) || MGRP.MGR().RODZ || '' ?};
         MGR.cntx_pop(); MGRP.cntx_pop();
         __tab.prefix('T',_tow_usl,_zal_od)
      |? (_zal_od:=exec('zalezy_od','plsprz','MGR'))<>null
      || MGR.cntx_psh();
         _tow_usl:={? MGR.seek(_zal_od,) || MGR.RODZ || '' ?};
         MGR.cntx_pop();
         __tab.index('AMGM');
         __tab.prefix('T',_tow_usl,_zal_od)
      || {? (_choice:=FUN.choice('Wybierz kartotekę'@,,'Materiały'@,'Usługi'@))=0
         ||
            __acr:=''
         ||
            _tow_usl:={? _choice=1 || 'T' || 'U' ?};
            __tab.index('ARODZ');
            __tab.prefix('T',_tow_usl)
         ?}
      ?};
      {? __acr<>''
      ||
         __tab.win_fld(_wer,,'KTM',,,20);
         __tab.win_fld(_wer,,'N',,,60);
         __tab.hdr_sel('Materiały'@);
         _opis:={? __psopis=1 || '__tab.KTM+'' - ''+__tab.N' || '__tab.KTM' ?};
         __tab.win_act(_wer,0,'Rekord',,,,"exec('rekprzed','color','M#01')");
         __tab.win_fml(_wer,,'KTM',,'ICON_BEFORE',"exec('m_tktl_icon','tech_common')",1);
         __tab.win_act(_wer,,'Formuła','Legenda'@@,,,"exec('legenda','color','M#01','KTM#')",,,,,,'L')
      ?}
   |? PSDEF.A='GRKH'
   ||
      __tab.win_fld(_wer,,'KOD',,,20);
      __tab.win_fld(_wer,,'NAZ',,,40);
      __tab.hdr_sel('Grupy kontrahentów'@);
      __tab.index('KOD');
      __tab.prefix();
      _opis:={? __psopis=1 || '__tab.NAZ' || '__tab.KOD' ?}
   |? PSDEF.A='KH'
   ||
      __tab.win_fld(_wer,,'KOD',,,8);
      __tab.win_fld(_wer,,'NAZ',,,60);
      __tab.hdr_sel('Kontrahenci'@);
      {? (_zal_od:=exec('zalezy_od','plsprz','GRKH'))<>null
      || __tab.index('GRKH');
         __tab.prefix(2,_zal_od)
      || __tab.index('KOD');
         __tab.prefix(2)
      ?};
      _opis:={? __psopis=1 || '__tab.NAZ' || '__tab.KOD' ?}
   |? PSDEF.A='KH_ODB'
   ||
      __tab.win_fld(_wer,,'KH','KOD',,8);
      __tab.win_fld(_wer,,'KH','NAZ',,50);
      __tab.win_fld(_wer,,'KOD',,,8);
      __tab.win_fld(_wer,,'NAZ',,,50);
      __tab.hdr_sel('Odbiorcy'@);
      __tab.index('KH_ODB');
      {? (_zal_od:=exec('zalezy_od','plsprz','KH'))<>null
      || __tab.prefix(_zal_od)
      || __tab.prefix()
      ?};
      _opis:={? __psopis=1 || '__tab.NAZ' || '__tab.KOD' ?}
   |? PSDEF.A='HAN'
   ||
      __tab.win_fld(_wer,,'KOD',,,5);
      __tab.win_fld(_wer,,'NAZ',,,50);
      __tab.hdr_sel('Handlowcy'@);
      __tab.index('KOD');
      __tab.prefix();
      _opis:={? __psopis=1 || '__tab.NAZ' || '__tab.KOD' ?}
   |? PSDEF.A='PSOKR'
   ||
      exec('psokr_mk','plsprz',PS.R,1+PSDEF.O);

      __tab.win_fld(_wer,,'N',,,60);
      __tab.win_fld(_wer,,'OD',,,10);
      __tab.win_fld(_wer,,'DO',,,10);
      __tab.hdr_sel('Okresy'@);
      _hdrsel:='Okresy';
      _opis:='__tab.N'
   ||
      __acr:='';
      FUN.info('Brak obsługi dla tabeli %1.'@[PSDEF.A])
   ?};

   {? __acr<>'' || exec('dol_z_sl','plsprz',_parent,_poziom,_opis) ?};

   &__acr;
   VAR_DEL.delete('__tab')
?};
PSDEF.cntx_pop();
_plps_ref:=__plps.ref();
__plps.cntx_pop();
__plps.seek(_plps_ref);
~~


\zalezy_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: sprawdzanie zaleznosci w definicji struktury
::   WE: _a - akronim tabeli od ktorej zaleza elementy na kolejnym poziomie
::   WY: ref elementu od ktorego zaleza elementy na kolejnym poziomie, null jesli brak zaleznosci
::----------------------------------------------------------------------------------------------------------------------
_return:=null;

_next:=#__plps.ref();

__plps.cntx_psh();
PSDEF.cntx_psh();
PSDEF.index('P');
PSDEF.prefix(PS.ref());
{!
|? _next<>null
|! {? __plps.seek(_next,)
   ||
      {? __plps.P=0
      ||
         _next:=null
      |? PSDEF.find_key(__plps.P)
      ||
         {? PSDEF.A=_a
         ||
            __asoc.index(__asi2);
            {? __asoc.find_key(BB.refsql(__plps.ref))
            ||
               _return:=BB.sqlint(($('__asoc.P'+$__plps.P))())
            ?};
            _next:=null
         ||
            _next:=__plps.PARENT
         ?}
      ?}
   ||
      _next:=null
   ?}
!};
PSDEF.cntx_pop();
__plps.cntx_pop();

_return


\psp_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: usun pozycje planu sprzedazy
::   WE: _a - =1-pytanie czy usunac, =0-pominac pytanie
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? __plps.P=0 || FUN.info('Funkcja niedostępna dla elementu Razem.'@); return(~~) ?};

{? _a=1 || {? __pl_gr=0 || {? ~FUN.ask('Usunąć element?'@) || return(~~) ?} ?} ?};

_pspl_r:=__plps.ref();

:: usuwanie dzieci elementu
__plps.cntx_psh();
{? __plps.index('?')=__ndx_plps
|| __plps.prefix('N',((__plps.P+1)*3)+__plps.KOL)
|| __plps.prefix(_pspl_r)
?};
{!
|? __plps.last() & __plps.ref()<>_pspl_r
|! exec('psp_usu','plsprz',0)
!};
__plps.cntx_pop();

:: aktualizacja wartosci na elementach posrednich
__plps.cntx_psh();
{? __plps.P=__ps_ilp
|| __plps.prefix();
   exec('agreguj','plsprz',-__plps.IL,-__plps.W,-__plps.ILW,-__plps.WW,-__plps.ILZL)
?};
__plps.cntx_pop();

:: usuwanie elementu
PSP.cntx_psh();
{? __plps.REF<>''
||
:: elementow na nijnizszym poziomie nie ma w tabeli asocjacji dlatego usuwane z pominieciem tabeli asocjacji
   {? PSP.seek(__plps.REF,)
   ||
      PSP.cntx_psh();
      _put:=0;
      _prfx:=''; {! _ii:=1..__plps.P-1 |! _prfx+={? _ii=1 || '' || ',' ?}+'PSP.'+($('__ps_akr['+$_ii+']'))() !};
      ($('PSP.prefix(__ps,'+_prfx+')'))();
      {? PSP.first()
      || _put:={? PSP.next() || 0 || {? __plps.P>1 || 1 || 0 ?} ?}
      ?};
      PSP.cntx_pop();
      {? _put=1
      || ($('PSP.'+__ps_akr[__plps.P]))():=null; PSP.put()
      || PSP.del()
      ?}
   ?}
||
:: usuwanie zapisow z tabeli asocjacji i aktualizacja PSP
   __asoc.cntx_psh();
   __asoc.index(__asi2);
   {? __asoc.find_key(BB.refsql(_pspl_r))
   ||
      _prfx:=''; {! _ii:=1..__plps.P-1 |! _prfx+={? _ii=1 || '' || ',' ?}+'BB.sqlint(__asoc.P'+$_ii+')' !};
      ($('PSP.prefix(__ps,'+_prfx+')'))();
      {? PSP.first()
      || _put:={? PSP.next() || 0 || {? __plps.P>1 || 1 || 0 ?} ?}
      ?};
      {? _put=1
      || ($('PSP.'+__ps_akr[__plps.P]))():=null; PSP.put()
      || _prfx:=''; {! _ii:=1..__plps.P |! _prfx+={? _ii=1 || '' || ',' ?}+'BB.sqlint(__asoc.P'+$_ii+')' !};
         ($('PSP.prefix(__ps,'+_prfx+')'))();
         {? PSP.first() || PSP.del() ?}
      ?};
      __asoc.del()
   ?};
   __asoc.cntx_pop()
?};
PSP.cntx_pop();
__plps.del();
~~


\agreguj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: nalicza wartosci na posrednich poziomach drzewa
::   WE: _a - ilosc
::       _b - wartosc
::       _c - ilosc z wykonania
::       _d - wartosc z wykonania
::       _e - ilość zlecona
::----------------------------------------------------------------------------------------------------------------------
_next:=__plps.PARENT;
{!
|? _next<>null
|! {? __plps.seek(_next,)
   ||
      __plps.IL+=_a;
      __plps.W+=_b;
      __plps.ILW+=_c;
      __plps.WW+=_d;
      __plps.ILWP:={? __plps.IL=0 || 0 || ((100*__plps.ILW)/__plps.IL)$1 ?};
      __plps.WWP:={? __plps.W=0 || 0 || ((100*__plps.WW)/__plps.W)$1 ?};
      __plps.ILZL+=_e;
      __plps.put();
      _next:=__plps.PARENT
   ||
      _next:=null
   ?}
!}


\psp_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: popraw pozycje planu sprzedazy
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? __plps.P<>__ps_ilp
||
   FUN.info('Funkcja niedostępna dla tego poziomu.'@)
||
   _il:=__plps.IL;
   _w:=__plps.W;
   __plps.cntx_psh();
   _red:=__plps.mk_edit('Wartości do planu'@,0);
   __plps.win_edit(_red);
   {? (  _psdef:=PSDEF.ref();
         _mat:=0;
         PSDEF.cntx_psh();
         PSDEF.index('P');
         PSDEF.prefix(PS.ref);
         {? PSDEF.first()
         ||
            {!|?
               {? PSDEF.A='M' || _mat:=1 ?};
               ~_mat & PSDEF.next()
            !}
         ?};
         PSDEF.cntx_pop();
         _mat
      )
   || __plps.win_efld(_red,,'IL',,,17,4,,'Ilość'@)
   ?};
   __plps.win_efld(_red,,'W',,,17,2,,'Wartość'@);
   exec('ok_esc','#window',__plps,_red);
   {? __plps.edit("
         {? __plps.IL<0 || FUN.info('Ilość nie może być ujemna.'@); 'IL'
         |? __plps.W<0 || FUN.info('Wartość nie może być ujemna.'@); 'W'
         |? __plps.ILZL>0 & __plps.ILZL>=__plps.IL || FUN.info('Ilość nie może być mniejsza niż zlecona.'@); 'IL'
         || ''
         ?}
      ")
   ||
      {? PSP.seek(__plps.REF,)
      ||
         __plps.IL_S:=__plps.IL;
         __plps.W_S:=__plps.W;
         __plps.put();
         PSP.IL:=__plps.IL;
         PSP.W:=__plps.W;
         PSP.put();
         exec('agreguj','plsprz',__plps.IL-_il,__plps.W-_w,0,0,0)
      ?}
   ?};
   __plps.cntx_pop()
?};
~~


\ps_wyk_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: wylicza wykonanie planu
::   WE: [_a] - PS.ref-bez pytania o aktualizacje; brak-pytanie czy aktualizowac
::       [_b] - ODDZ.KOD
::   WY: 0 - pominieto aktualizacje, 1 - wykonano aktualizacje
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(null) || _a:=0 ?};
{? var_pres('_b')<>type_of('c') || _b:='' ?};

_czysc:="VAR_DEL.delete('__PSOKR','__MGR','__MGRP','__M','__GRKH','__KH','__KH_ODB','__HAN')";
_czysc();

{? ~(_a | FUN.ask('Aktualizować wykonanie?'@)) || return(0) ?};

{? ~_a
||
   _ps:=__ps;
   _ps_ilp:=__ps_ilp;
   _ps_akr:=__ps_akr;
   _oddz:=ST.ODDZ
||
   _ps:=_a;
   _ps_ilp:=0;
   _ps_akr:=null;
   _oddz:=_b;
   PSDEF.cntx_psh;
   PSDEF.index('P');
   PSDEF.prefix(_ps);
   {? PSDEF.first()
   ||
::    ilosc poziomow w strukturze
      _ps_ilp:={? PSDEF.last() || PSDEF.P ?};
::    zmienna przechowujaca akronim tabel wg definicji struktury
      _ps_akr:=obj_new(_ps_ilp);
      {? PSDEF.first()
      ||
         {! _ii:=1.._ps_ilp
         |!
            _ps_akr[_ii]:=PSDEF.A;
            PSDEF.next()
         !}
      ?}
   ?};
   PSDEF.cntx_pop
?};
:: brak definicji struktury
{? ~_ps_ilp || return(0) ?};
:: wyzerowanie obecnego wykonania
PSP.cntx_psh();
PSP.index('PS');
PSP.prefix(_ps);
{? PSP.first() || {! |? PSP.ILW:=PSP.WW:=0; PSP.put(); PSP.next() !} ?};
PSP.cntx_pop();

PSDEF.cntx_psh;
PSDEF.index('P'); PSDEF.prefix(_ps);
_typ:=''; {? PSDEF.first || {! |? {? PSDEF.A='PSOKR' || _typ:=1+PSDEF.O ?}; _typ='' & PSDEF.next() !} ?};
PSDEF.cntx_pop;

PSOKR.cntx_psh;
PSOKR.index('RTNN'); PSOKR.prefix(PS.R,_typ);

exec('fak_open','open_tab',_oddz,form($PS.R+2,-2));
FAP.index('FAP');
FAP.prefix();
{? FAP.first()
||
   __PSOKR:=
      '_wyn:=null;'+
      '{? PSOKR.first()'+
      '|| {! |? {? PSOKR.OD<=FAKS.D & FAKS.D<=PSOKR.DO || _wyn:=PSOKR.ref() ?}; _wyn=null & PSOKR.next() !}'+
      '?};'+
      '_wyn';
   __MGR:='FAP.M().MGR';
   __MGRP:='FAP.M().MGRP';
   __M:='FAP.M';
   __GRKH:='FAKS.KH().GRKH';
   __KH:='FAKS.KH';
   __KH_ODB:='FAKS.KH_ODB';
   __HAN:='FAKS.HAN';
   _faks:=null;
   _key:='';
   _wal:=PS.WAL<>exec('bl_wal','ustawienia');
   {! _ii:=1.._ps_ilp
   |! _key+={? _ii=1 || '($__' || ',($__' ?}+_ps_akr[_ii]+')()'
   !};
   PSP.cntx_psh;
   _ind:=''; {! _ii:=1.._ps_ilp |! _ind+=',,,\''+_ps_akr[_ii]+'\'' !};
   _pspndx:=($('PSP.ndx_tmp(,1,\'PS\''+_ind+',,)'))();
   PSP.index(_pspndx); PSP.prefix(_ps);
   {!
   |? {? FAP.FAKS<>_faks || FAP.FAKS(); _faks:=FAP.FAKS ?};
      {? FAKS.WAL=PS.WAL & FAKS.AKC='T' & FAKS.WHERE<>'L' & FAKS.SZ='S'
      ||
:: sprawdzenie czy nie powstał dokument z paragonu
         _fak_symf:=0;
         {? FAKS.SYMF<>''
         ||
            TYPYSP.cntx_psh();
            {? FAKS.T().PAR='T'
            ||
               FAKS.cntx_psh();
               FAKS.use(ref_name(FAKS.SYMF));
               FAKS.prefix();
               {? FAKS.seek(FAKS.SYMF) & FAKS.WAL=PS.WAL & FAKS.AKC='T' & FAKS.WHERE<>'L' & FAKS.SZ='S'
               || _fak_symf:=1
               ?};
               FAKS.cntx_pop()
            ?};
            TYPYSP.cntx_pop()
         ?};
         {? ~_fak_symf & ($('PSP.find_key('+_key+')'))()
         ||
            PSP.ILW+=FAP.IL;
            PSP.WW+={? _wal=1 || FAP.WWAL || FAP.WN ?};
            PSP.put()
         ?}
      |? PS.WALH='N' & FAKS.AKC='T' & FAKS.WHERE<>'L' & FAKS.SZ='S'
      ||
:: sprawdzenie czy nie powstał dokument z paragonu
         _fak_symf:=0;
         {? FAKS.SYMF<>''
         ||
            TYPYSP.cntx_psh();
            {? FAKS.T().PAR='T'
            ||
               FAKS.cntx_psh();
               FAKS.use(ref_name(FAKS.SYMF));
               FAKS.prefix();
               {? FAKS.seek(FAKS.SYMF) & PS.WALH='N' & FAKS.AKC='T' & FAKS.WHERE<>'L' & FAKS.SZ='S'
               || _fak_symf:=1
               ?};
               FAKS.cntx_pop()
            ?};
            TYPYSP.cntx_pop()
         ?};

         {? ~_fak_symf & ($('PSP.find_key('+_key+')'))()
         ||
            PSP.ILW+=FAP.IL;
            PSP.WW+=FAP.WN;
            PSP.put()
         ?}
      ?};
      FAP.next()
   !};
   PSP.ndx_drop(_pspndx);
   PSP.cntx_pop
?};
PSOKR.cntx_pop;
exec('fak_open','open_tab',ST.ODDZ,ST.ROK);
1


\ps_wyk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: podglad dokumentow dla wykonania na najnizszym poiomie
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? __plps.P<>__ps_ilp || FUN.info('Funkcja niedostępna dla tego poziomu.'@,ST.MSG); return(~~) ?};

PSP.seek(__plps.REF,);

exec('fak_open','open_tab',ST.ODDZ,form($PS.R+2,-2));
FAP.clear();

_sort:='FAKS,POZ';
_from:=
   'join FAKS using("FAP".FAKS,FAKS.reference) '+
   'join M using("FAP".M,M.reference) '+
   'join KH using(FAKS.KH,KH.reference)';
_where:=
   'FAKS.AKC=\'T\' and FAKS.WHERE<>\'E\' and FAKS.WHERE<>\'L\' and FAKS.SZ=\'S\''+
   {? PSP.PS().WALH='T' || ' and FAKS.WAL=:_j' || ' and FAKS.NWAL=:_j' ?}+
   {? PSP.PSOKR=null || '' || ' and FAKS.D between to_date(:_a) and to_date(:_b) ' ?}+
   {? PSP.MGR=null   || '' || ' and M.MGR=:_c ' ?}+
   {? PSP.MGRP=null  || '' || ' and M.MGRP=:_d ' ?}+
   {? PSP.M=null     || '' || ' and "FAP".M=:_e ' ?}+
   {? PSP.GRKH=null  || '' || ' and KH.GRKH=:_f ' ?}+
   {? PSP.KH=null    || '' || ' and FAKS.KH=:_g ' ?}+
   {? PSP.KH_ODB=null|| '' || ' and FAKS.KH_ODB=:_h ' ?}+
   {? PSP.HAN=null   || '' || ' and FAKS.HAN=:_i ' ?};
FAP.f_set(_sort,_from,_where,PSP.PSOKR().OD,PSOKR.DO,PSP.MGR,PSP.MGRP,PSP.M,PSP.GRKH,PSP.KH,PSP.KH_ODB,PSP.HAN,PS.WAL);
:: usuwam paragony z których powstały faktury
{? FAP.f_first()
||
   {!|?
      {? FAP.FAKS().SYMF<>'' & FAP.FAKS().T().PAR='N'
      || _faks_ref:=null();
         FAKS.cntx_psh();
         FAKS.use(ref_name(FAP.FAKS().SYMF));
         {? FAKS.seek(FAP.FAKS().SYMF) || _faks_ref:=FAKS.ref() ?};
         FAKS.cntx_pop();
         _fap_ref:=FAP.ref();
         {? _faks_ref & FAP.f_find(_faks_ref) || FAP.f_del(); FAP.f_seek(_fap_ref) ?}
      ?};
      FAP.f_next()
   !}
?};
{? ~FAP.f_first()
||
   FUN.info('Brak dokumentów.',ST.MSG)
||
   _win:=FAP.mk_sel('Dokumenty'@,,0,'plsprzdok',5,10,15,,'U');
   FAP.win_sel(_win);
   FAP.win_fld(_win,,'FAKS','SYM',,20,,1,'Symbol'@);
   FAP.win_fld(_win,,'FAKS','DW',,10,,1,'Data'@);
   FAP.win_fld(_win,,'POZ',,,5,,1,'Poz.'@);
   FAP.win_fld(_win,,'M','N',,35,,1,'Nazwa materiału'@);
   FAP.win_fld(_win,,'IL',,,12,4,1,'Ilość'@);
   _wal:=PS.WAL<>exec('bl_wal','ustawienia');
   _facr:={? _wal || 'WWAL' || 'WN' ?};
   FAP.win_fld(_win,,_facr,,,12,2,1,'Wartość'@);
   FAP.win_act(_win,0,'Wyświetl',,,,"exec('fall_rek','faktury_nag',2)",,0);

   FAP.select()
?};
exec('fak_open','open_tab',ST.ODDZ,ST.ROK);
~~


\ps_wyk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: przed wykonanie okna WER tabeli PS
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__ident'); __ident:='PS1'; {! |? exec('psp_tree','plsprz',1,1) !}; VAR_DEL.delete('__ident'); ~~


\ps_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: akceptacja planu sprzedazy
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? PS.OK='T'
||
   FUN.info('Plan jest już zaakceptowany.'@)
||
   exec('psp_open','plsprz');
   PSP.cntx_psh();
   PSP.index('PS');
   PSP.prefix(PS.ref());
   {? ~PSP.first()
   ||
      FUN.info('Plan sprzedaży nie zawiera pozycji.\nAkceptacja niedostępna.'@)
   ||
      {? FUN.ask('Zaakceptować plan sprzedaży?'@)
      ||
         PS.OK:='T';
         PS.DT:=date();
         PS.put()
      ?}
  ?};
  PSP.cntx_pop()
?};
~~


\ps_wyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: wycofanie akceptacji planu produkcji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? PS.OK<>'T' || FUN.info('Plan jest niezaakceptowany.'@); return(~~) ?};

{? FUN.ask('Wycofać akceptację planu?'@)
||
   PS.OK:='N';
   PS.DT:=date(0,0,0);
   PS.put()
?};
~~


\ps_kop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: przed kopiuj okna WER tabeli PS
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
PS.win_edit('RED');
PS.A:='T';
PS.S:='Kopia: '+(14+PS.S);
{!
|? _iter:=0;
   _rok_old:=PS.R;
   {? PS.edit("exec('ps_pored','plsprz')")
   ||
      _proc:=0;
      _wykonanie:=0;
      __tab_wyk:=tab_tmp(2,'PROC','REAL','Procent','WYK','STRING[1]','Wykonanie');
      _red:=__tab_wyk.mk_edit('Kopiowanie planu sprzedaży'@,0);
      __tab_wyk.win_esep(_red,'Dane podstawowe'@);
      __tab_wyk.win_efld(_red,,'PROC',,,20,4,,'Procent'@,,'Procent o jaki powiększyć/pomniejszyć wartości.'@);
      __tab_wyk.win_efld(_red,,'WYK',,,,,,'Na podstawie wykonania'@,1,,'check-box',
      'left_label=1,check_label=%1'['Kopiowanie na podstawie wykonania'@],"'T'","'N'");
      exec('ok_esc','#window',__tab_wyk,_red,1,,,,,exec('help_red_ok','#window'),,exec('help_red_esc','#window','A'));
      __tab_wyk.win_edit(_red);
      _valid:="{? __tab_wyk.PROC<-100 || FUN.info('Wartość nie może być mniejsza od -100.'@);0 || 1 ?}";
      {? __tab_wyk.edit(_valid)
      || _proc:=__tab_wyk.PROC;
         {? _wykonanie:=__tab_wyk.WYK='T'
         ||  PS.cntx_psh();
             exec('ps_wyk_akt','plsprz',PS.ref(),ST.ODDZ);
             PS.cntx_pop()
         ?}
      ?};
      {? var_pres('__tab_wyk')>100 || obj_del(__tab_wyk) ?};
      _proc_wyk:=(100+_proc)/100;
      OKR.index('OKR');
      OKR.prefix(REF.FIRMA,PS.R);
      {? ~OKR.first()
      ||
         FUN.info('Niezdefiniowany rok %1.\nKopiowanie niemożliwe.'@[$PS.R])
      ||
::       _pss - plan zrodlowy
         _pss:=PS.ref();
::       kopiowanie naglowka
         PS.cntx_psh();
         PS.prefix();
         PS.OK:='N';
         PS.DT:=date(0,0,0);
         {? ~PS.add()
         ||
            FUN.info('Dodanie nagłówka planu sprzedaży nie powiodło się.'@);
            _iter:=1
         ||
::          _psd - plan docelowy
            _psd:=PS.ref();
            _rok:=PS.R;
::          kopiowanie struktury
            PSDEF.index('P'); PSDEF.prefix(_pss);
::          _typ - typ okresu
            _typ:='';
            {? PSDEF.first()
            ||
               {!
               |? {? PSDEF.A='PSOKR' || _typ:=1+PSDEF.O ?};
                  PSDEF.cntx_psh();
                  PSDEF.clear();
                  PSDEF.PS:=_psd;
                  PSDEF.add();
                  PSDEF.cntx_pop();
                  PSDEF.next()
               !}
            ?};
::          kopiowanie pozycji
            exec('psp_open','plsprz',_rok_old);
            PSP.index('PS'); PSP.prefix(_pss);
            PSOKR.index('RTNN');
            {? PSP.first()
            ||
::             tworzenie okresow planow sprzedazy typu _typ w roku PS.R
               {? _typ<>'' || exec('psokr_mk','plsprz',PS.R,_typ) ?};
               {!
               |? PSP.cntx_psh();
                  exec('psp_open','plsprz',_rok);
                  PSP.clear();
                  PSP.PS:=_psd;
                  {? _wykonanie
                  || PSP.IL:=_proc_wyk*PSP.ILW; PSP.W:=_proc_wyk*PSP.WW
                  || PSP.IL:=_proc_wyk*PSP.IL; PSP.W:=_proc_wyk*PSP.W
                  ?};
                  PSP.ILW:=PSP.WW:=PSP.ILZL:=0;
                  {? PSP.PSOKR=null()
                  ||
                     PSP.add()
                  ||
                     PSOKR.prefix(PS.R,PSP.PSOKR().T,PSOKR.N,PSOKR.N);
                     {? PSOKR.first()
                     ||
                        PSP.PSOKR:=PSOKR.ref();
                        PSP.add()
                     ||
                        FUN.info(
                           'Nie znaleziono okresu %1 w roku %2.\n'
                           'Pominięto pozycję podczas kopiowania.'@
                           [PSOKR.N,form(PS.R,,,'99')]
                        )
                     ?}
                  ?};
                  PSP.cntx_pop();
                  PSP.next()
               !}
            ?};
            _iter:=0
         ?};
         PS.cntx_pop()
      ?}
   ?};
   _iter
!};
~~


\ps_pored
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: po redakcji nagłowka planu produkcji
::   WY: akronim niewlasciwie wypelnionego pola, ''-wpp
::----------------------------------------------------------------------------------------------------------------------
_return:=__CHK.record(PS,,'S','OP','R','WAL');
{? _return=''
||
   {? PS.R<2000 | PS.R>2099
   ||
      FUN.info('Dopuszczalny zakres lat: 2000 - 2099.'@);
      _return:='R'
   ?}
?};
_return


\psokr_mk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: tworzy okresy planu sprzedazy typu _b dla roku _a
::   WE: _a - rok
::       _b - typ okresu
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
PSOKR.index('RTNN');
PSOKR.prefix(_a,_b);
{? _b='K'
||
   {! _ii:=0..3
   |! _od:=date(_a,1+3*_ii,1);
      _do:=date(_a,3+3*_ii,0);
      _naz:='Kwartał '+$(_ii+1);
      {? ~PSOKR.find_key(_naz,_naz)
      || PSOKR.R:=_a;
         PSOKR.T:=_b;
         PSOKR.N:=_naz;
         PSOKR.OD:=_od;
         PSOKR.DO:=_do;
         PSOKR.add()
      ?}
   !}
|? _b='M'
||
   {! _ii:=1..12
   |! _od:=date(_a,_ii,1);
      _do:=date(_a,_ii,0);
      _naz:='Miesiąc '+form(_ii,-2)+' - '+(form(_od,,8)-5);
      {? ~PSOKR.find_key(_naz,_naz)
      || PSOKR.R:=_a;
         PSOKR.T:=_b;
         PSOKR.N:=_naz;
         PSOKR.OD:=_od;
         PSOKR.DO:=_do;
         PSOKR.add()
      ?}
   !}
|? _b='T'
||
   _il:=ceil((date(_a,12,0)-(date(_a,1,1)-1))/7);
   {! _ii:=1.._il
   |! _szift:={? _ii=1 || date(_a,1,1)~4-1 || 0 ?};
      _szift1:={? _ii>1 || date(_a,1,1)~4-1 || 0 ?};
      _od:=date(_a,1,1)-_szift1+(_ii-1)*7;
      _do:={? _od~1<(_od+6)~1 || date(_a,12,0) || _od+6-_szift ?};
      _naz:='Tydzień '+form(_ii,-2);
      {? ~PSOKR.find_key(_naz,_naz)
      || PSOKR.R:=_a;
         PSOKR.T:=_b;
         PSOKR.N:=_naz;
         PSOKR.OD:=_od;
         PSOKR.DO:=_do;
         PSOKR.add()
      ?}
   !}
|? _b='D'
||
   _il:=date(_a,12,0)-date(_a,1,1);
   {! _ii:=0.._il
   |! _od:=_do:=date(_a,1,1)+_ii;
      _naz:='Dzień '+form(_ii+1,-3);
      {? ~PSOKR.find_key(_naz,_naz)
      || PSOKR.R:=_a;
         PSOKR.T:=_b;
         PSOKR.N:=_naz;
         PSOKR.OD:=_od;
         PSOKR.DO:=_do;
         PSOKR.add()
      ?}
   !}
?};
~~


\ps_arch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: przed do archiwum okna WER tabeli PS
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Przenieść plan sprzedaży do archiwum?'@)
||
   PS.cntx_psh();
   PS.prefix();
   PS.A:='N';
   PS.put();
   PS.cntx_pop()
?};
~~


\ps_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: przed do aktywnych okna WER tabeli PS
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Przenieść plan do aktywnych planów sprzedaży?'@)
||
   PS.cntx_psh();
   PS.prefix();
   PS.A:='T';
   PS.put();
   PS.cntx_pop()
?};
~~


\ps_prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: wydruk bieżącego planu produkcji - PP/PPP
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: sprawdzenie czy zdefiniowano strukture planu
PSDEF.index('P');
PSDEF.prefix(PS.ref);
{? ~PSDEF.first()
|| FUN.info('Brak struktury planu.\nFunkcja niedostępna.'@)
|| rep_exec('ps__*')
?};
~~


\dol_z_sl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: selekcja pozycji z kartotek
::   WE: _a - rodzic
::       _b - poziom
::       _c - formula na opis elementu w drzewie
::       _d - planowana ilosc
::       _e - planowana wartosc
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: reference elementu w slowniku
__ref:=null;

:: __mgr_sel - ilosc zaznaczonych pozycji w slowniku
__mgr_sel:=0;
VAR_DEL.delete('__slosel');
:: wybor elementow ze slownika
{? __tab.select()
||
:: określenie ilosći i wartości
   {? PSDEF.P<>__ps_ilp
   ||
      _psp_il:=_psp_w:=0
   ||
      __plps.cntx_psh();
      _red:=__plps.mk_edit('Wartości do planu'@,0);
      __plps.IL:=__plps.W:=0;
      __plps.win_edit(_red);
      {? (  _psdef:=PSDEF.ref();
            _mat:=0;
            PSDEF.cntx_psh();
            PSDEF.index('P');
            PSDEF.prefix(PS.ref);
            {? PSDEF.first()
            ||
               {!|?
                  {? PSDEF.A='M' || _mat:=1 ?};
                  ~_mat & PSDEF.next()
               !}
            ?};
            PSDEF.cntx_pop();
            _mat
         )
      || __plps.win_efld(_red,,'IL',,,17,4,,'Ilość'@)
      ?};
      __plps.win_efld(_red,,'W',,,17,2,,'Wartość'@);
      exec('ok_esc','#window',__plps,_red);
      {? __plps.edit("{? __plps.IL<0 || 'IL' |? __plps.W<0 || 'W' || '' ?}")
      ||
         _psp_il:=__plps.IL; _psp_w:=__plps.W
      ||
         __acr:=''
      ?};
      __plps.cntx_pop()
   ?};

   {? __acr<>'' & __mgr_sel=0
   ||
      __slosel:=tab_tmp(1,'REF','INTEGER',);
      __slosel.REF:=#__tab.ref();
      __slosel.add()
   ?};
   {? __acr<>'' & __slosel.first()
   ||
      __asoc.cntx_psh();
      __asoc.index(__asi2); __asoc.prefix();
      {? _b=1 | __asoc.find_key(BB.refsql(_a))
      ||
         __asoc.index(__asi1);
         {!
         |?
::          dodanie zapisu w tabeli PSP
::          wyznaczenie asocjacji i dodanie rekordu jesli taki nie istnieje lub poprawa jesli istnieje jako rodzic
::          poprawa tylko dla pierwszego rekordu, pozostale zawsze dodawane, poniewaz tylko jeden rodzic
::          dla pierwszego poziomu wyjatek poniewaz nie ma asocjacji
            __ref:={? __tab.seek(__slosel.REF,) || __tab.ref() || null ?};
            _key:=''; {! _ii:=1.._b-1 |! _key+={? _ii=1 || '' || ',' ?}+'BB.sqlint(__asoc.P'+$_ii+')' !};
            _add:=1;
            {? {? _b=1 || 0 || {? ($('PSP.find_key('+_key+')'))() || ($('PSP.'+__acr))()=null || 0 ?} ?}
            ||
               ($('PSP.'+__acr))():=__ref;
               PSP.IL:=_psp_il;
               PSP.W:=_psp_w;
               PSP.put()
            ||
               {? {? _b=1 || ~PSP.find_key(__ref) || ~($('PSP.find_key('+_key+',__ref)'))() ?}
               ||
                  {? _b=1 || PSP.blank() ?};
                  PSP.PS:=__ps;
                  PSP.IL:=PSP.W:=0;
                  {! _jj:=1..__ps_ilp
                  |!
                     {? _jj>_b
                     ||
                        ($('PSP.'+__ps_akr[_jj]))():=null
                     ?}
                  !};
                  ($('PSP.'+__acr))():=__ref;
                  PSP.IL:=_psp_il;
                  PSP.W:=_psp_w;
                  PSP.add()
               ||
                  __plps.find_tab(1,'REF',,'=',$PSP.ref());
                  _add:=0
               ?}
            ?};
::          dodanie zapisu w drzewie
            {? _add=1
            ||
               __plps.blank();
               __plps.PARENT:=_a;
               __plps.O:=($_c)();
               __plps.P:=_b;
               __plps.REF:={? __plps.P=__ps_ilp || $PSP.ref() || '' ?};
               __plps.OD:=__plps.OD_S:=PSP.PSOKR().OD;
               __plps.DO:=__plps.DO_S:=PSP.PSOKR().DO;
               __plps.IL:=__plps.IL_S:=PSP.IL;
               __plps.W:=__plps.W_S:=PSP.W;
               __plps.ILZL:=__plps.ILZL_S:=PSP.ILZL;
               __plps.KOL:=exec('plps_kol','plsprz');
               __plps.RAZEM:='N';
               ($('__plps.P'+$_b))():=__plps.O;
               {? _b>1
               ||
:: aktualizacja __ps_war zgodnie z gałęzią na której dodajemy pozycję a następnie dodanie wartości do opisów poziomów
                  __plps.cntx_psh();
                  __plps.get();
                  _kk:=_b-1;
                  {!|?
                     __ps_war[_kk]:=($('__plps.P'+$_kk))();
                     _kk-=1;
                     _kk>0
                  !};
                  __plps.cntx_pop();

                  _kk:=_b-1;
                  {!|?
                     ($('__plps.P'+$_kk))():=__ps_war[_kk];
                     _kk-=1;
                     _kk>0
                  !}
               ?};
               __plps.add();
               _parent:=__plps.ref();
               __plps.cntx_psh();
               exec('agreguj','plsprz',__plps.IL,__plps.W,0,0,0);
               __plps.cntx_pop();
::             uzupelnienie tabeli asocjacji
               {? _b<>__ps_ilp
               ||
                  {! _jj:=1..__ps_ilp
                  |!
                     {? _jj>_b
                     ||
                        ($('__asoc.P'+$_jj))():=BB.refsql(null)
                     ||
                        ($('__asoc.P'+$_jj))():=BB.refsql(($('PSP.'+__ps_akr[_jj]))())
                     ?}
                  !};
                  __asoc.PLPS:=BB.refsql(_parent);
                  __asoc.add()
               ?}
            ?};
            __slosel.next()
         !}
      ?};
      __asoc.cntx_pop()
   ?}
?};

VAR_DEL.delete('__slosel');
&__mgr_sel; &__ref;
~~


\opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: zwraca wartosc pola zlaczniowego dla pola _a tabeli PSP
::   WE: _a - akronim pola
::       _b - =0-kod, =1-nazwa
::   WY: wartosc pola
::----------------------------------------------------------------------------------------------------------------------
{? _a='MGR'    || {? _b=1 || PSP.MGR().NAZ               || PSP.MGR().KOD ?}
|? _a='MGRP'   || {? _b=1 || PSP.MGRP().NAZ              || PSP.MGRP().KOD ?}
|? _a='M'      || {? _b=1 || PSP.M().KTM+' - '+PSP.M().N || PSP.M().KTM ?}
|? _a='GRKH'   || {? _b=1 || PSP.GRKH().NAZ              || PSP.GRKH().KOD ?}
|? _a='KH'     || {? _b=1 || PSP.KH().NAZ                || PSP.KH().KOD ?}
|? _a='KH_ODB' || {? _b=1 || PSP.KH_ODB().NAZ            || PSP.KH_ODB().KOD ?}
|? _a='HAN'    || {? _b=1 || PSP.HAN().NAZ               || PSP.HAN().KOD ?}
|? _a='PSOKR'  || PSP.PSOKR().N
               || ''
?}


\psp_ps_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: wartosc poczatkowa PSP.PS
::   WY: PS.reference
::----------------------------------------------------------------------------------------------------------------------
PS.ref()


\psdef_ps_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [23.25]
:: OPIS: wartosc poczatkowa PSDEF.PS
::   WY: PS.reference
::----------------------------------------------------------------------------------------------------------------------
PS.ref()


\show_tree
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [23.25]
:: OPIS: Akcja Zwin/Rozwin
::----------------------------------------------------------------------------------------------------------------------
{? __plps.tr_state || __plps.tr_set(0,cur_win(1),,1) || __plps.tr_set(1,cur_win(1),,1) ?};
~~


\wal_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [23.25]
:: OPIS: Waluta po redakcji
::----------------------------------------------------------------------------------------------------------------------
{? PS.WAL<>exec('bl_wal','ustawienia') || PS.WALH:='T' ?}; 1


\walh_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [23.25]
:: OPIS: Waluta handlowa przed redakcją
::----------------------------------------------------------------------------------------------------------------------
{? PS.WAL=exec('bl_wal','ustawienia') || 1 || 0 ?}


\plps_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [23.25]
:: OPIS: Wyliczanie kolejności
::----------------------------------------------------------------------------------------------------------------------
_kol:='001'+ (207*'0');
_poz:=__plps.P;
_ref:=__plps.PARENT;
__plps.cntx_psh();
__plps.prefix();
{? ~__plps.seek(_ref) || __plps.cntx_pop(); return(_kol) ?};
_kol_par:=((__plps.P+1)*3)+__plps.KOL;
_ndx:=__plps.ndx_tmp(,1,'P',,,'KOL',,);
__plps.index(_ndx);
__plps.prefix(_poz,_kol_par);
_numer:=__plps.size()+1;
__plps.cntx_pop();
__plps.ndx_drop(_ndx);
_kol:=((_poz*3)+_kol_par) + (('00'+$_numer)+3)+((210-_poz*3-3)*'0')


\deleted_in_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Obsługa sytuacji że jest czynność na liście todo ale pozycja planu sprzedaży usunięta
::   WE: _a - obj_new - obiekt Menadżera Procesów
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_mp:=_a;

_msg:='Pozycja planu sprzedaży nie została odnaleziona, prawdopodobnie została usunięta.'@;
{? _mp.isService()=0 & _mp.CLEANER=0
|| {? _mp.isGroup()
   || KOMM.add(_msg,2,,1)
   || FUN.emsg(_msg)
   ?}
?};
_mp.error(_msg);
~~


\ps_pd_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Pole Plan dostaw
::----------------------------------------------------------------------------------------------------------------------
{? PS.PD='T' || return(1) ?};
_wyn:=0;
PSP.cntx_psh();
PSP.index('PS');
PSP.prefix(PS.ref());
{? PSP.first()
||
:: jęzeli już znajdują się jakieś pozycje to możliwość zaznaczenia pola tylko jeśli w definicji ostatni jest materiał
   PSDEF.cntx_psh();
   PSDEF.index('P');
   PSDEF.prefix(PS.ref());
   {? PSDEF.last() & PSDEF.A='M'
   || _wyn:=1
   || FUN.info('Plan zawiera pozycje niezgodne z dopuszczalną strukturą.
      \nOstatnia pozycja musi odnosić się do materiałów.'@)
   ?};
   PSDEF.cntx_pop()
|| _wyn:=1
?};
PSP.cntx_pop();
_wyn


\ps_rp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Rekord przed tabeli PS
::----------------------------------------------------------------------------------------------------------------------
_col:='';
{? PS_Z.F=''
|| {? PS.A='T'
   || _col:=Color.fnd_kol('PS#01#02')
   || _col:=Color.fnd_kol('PS#01#01')
   ?}
?};
{? _a
||
   {? PS_Z.F=''
   ||
      {? PS.A='T'
      || PS.actions('WER',,'Y:D',1);
         PS.actions_grayed('WER','N')
      || PS.actions('WER',,'Y:Z',1);
         PS.actions_grayed('WER','UPAWR:')
      ?}
   ||
      PS.actions_grayed('WER');
      {? PS.A='T'
      || PS.actions('WER','NL','Y:D',1)
      || PS.actions('WER','DUPAWRL:d','Y:Z',1)
      ?}
   ?}
?};
_col


\ps_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Akcja dołącz planu sprzedaży
::----------------------------------------------------------------------------------------------------------------------
PS.cntx_psh();
PS.prefix();
PS.win_edit('RED');
PS.blank();
_ref:=null();
{? PS.edit("exec('ps_pored','plsprz')") || {? PS.add() || _ref:=PS.ref() ?} ?};
PS.cntx_pop();
{? _ref || PS.seek(_ref) ?}


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Parametry pracy
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
{? __PARSES.editDom('LSP')
|| {? _oddz<>ST.ODDZ || exec('init','lsp') ?};
   AreaTitle.setTitle()
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:39 58de5f71c3db2895cdb28f253da06fccc2fb6d98bc19430973b8d42bff90df5c7a8ec0b254deab6a850ab307bf0d0d04544b8c40af562075ecedf50b5a352327c247e9d0ef5a9cd52e90d1b0747d77bb1f9880ad6d33bd86aa6443fd068647ffcca5915a45e9f086c1b2edb20cf8421518ead3124a7d04174eeaef9e465df2a5

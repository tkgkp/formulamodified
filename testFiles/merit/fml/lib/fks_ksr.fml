:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: fks_krs.fml
:: Utworzony: 21.05.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły obszaru FKS (FKS_KRS - księgi rachunkowe)
::======================================================================================================================


\obroty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Przegladanie obrotow na kontach w tabelce tymczasowej
::  OLD: \obroty/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? zm_wal=0
|| {? dwiewalo
   || UNPAR.PSLO_BE:='SLO.win_dict(\'ONE\');SLO.win_sel(\'ONE\');SLU.index(\'NAZ\'); SLU.prefix(\'WALUTY\'); {?SLU.first()||pSloWal:=fld();1 ?}'
   || UNPAR.PSLO_BE:='{? PAR_SKID.get(33)<>\'N\' || SLO.win_dict(\'ONE\');SLO.win_sel(\'ONE\');SLU.index(\'NAZ\'); SLU.prefix(\'WALUTY\'); {?SLU.first()||pSloWal:=fld();1 ?}?}'
   ?};
   {? ROZNE.PAR4<>2 & ROZNE.PAR4<>0 & ROZNE.PAR4<>4 & ROZNE.PAR4<>5 || ROZNE.PAR4:=2 ?}
?};
{? ~(var_pres('MASK_KON')>0)
|| MASK_KON:=tab_tmp(1,'SYM_KON','STRING[35]','Konto','NAGLOWEK','STRING[35]','')
?};
{? dwiewalo || hdr_slo:='Zmiana waluty obrotów'@ || hdr_slo:='Zmiana waluty przeliczeń'@ ?};
ROZNE.win_edit({? dwiewalo || 'OBROTY_W' || 'OBROTY' ?});
{? zm_wal | (ROZNE.edit("
                {? ROZNE.UT_OKROD=null
                || FUN.info('Niewypełniony okres początkowy analizy.'@); 'UT_OKROD'
                |? ROZNE.UT_OKRDO=null
                || FUN.info('Niewypełniony okres końcowy analizy.'@); 'UT_OKRDO'
                |? ROZNE.UT_OKROD().NR>ROZNE.UT_OKRDO().NR
                || FUN.info('Błędna kolejność okresów analizy.'@); 'UT_OKROD'
                |? dwiewalo & UNPAR.PSLO=null
                || FUN.info('Nie wybrano waluty obcej.'@); 'PSLO'
                |? dwiewalo & UNPAR.PSLO=FINFO.NAROD
                || FUN.info('Wybrana waluta nie jest walutą obcą.'@); 'PSLO'
                |? ROZNE.PAR4=5 & ROZNE.KON_WYDR=null
                || FUN.info('Pole \"Zestaw kont\" musi być wypełnione.\n Należy uzupełnić brakujące informacje.'@); 'KON_WYDR'
                |? ROZNE.PAR4=4 & (ROZNE.KS_OD=null | ROZNE.KS_DO=null)
                || FUN.info('Pole \"Zakres kont\" musi być wypełnione.\n Należy uzupełnić brakujące informacje.'@); {? ROZNE.KS_OD=null || 'KS_OD' || 'KS_DO' ?}
                |? ROZNE.PAR4=4 & (#ROZNE.KS_DO().SYM<#ROZNE.KS_OD().SYM)
                || FUN.info('Błędny zakres kont.'@); 'KS_OD'
                || ''
                ?}
               ") & MASK_KON.erase())
|| {? zm_wal=0 & ROZNE.PAR4=2
   || _prfx:=exec('maska2prefix','konto',ROZNE.MASKA);
      {? _prfx<>'?'
      || ROZNE.PAR4:=0;
         ROZNE.PREFIX:=_prfx
      ?}
   ?};
   exec('obroc','fks_ksr')
?}


\obroc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła pomocniczna dla \obroty/fks_krs.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.cntx_psh();
UNPAR.cntx_psh();
lp_gen:=0;
{? dwiewalo
|| __KURS:=1.0
|| __KURS:=exec('kurs','konto',UNPAR.PSLO().KOD,UNPAR.P25);
   {? __KURS=0 || __KURS:=1.0 ?}
?};
fpoz:='';
OKRO_F.cntx_psh();
OKRO_F.index('ROK');
OKRO_F.prefix(SSTALE.AR);
SSTALE.AR();
{? OKRO_F.seek(ROZNE.UT_OKROD)
|| {! |?
      fpoz+='\'pozy'+ROK_F.KOD+form(OKRO_F.NR,-2)+'\',';
      {? OKRO_F.ref()=ROZNE.UT_OKRDO || 0 || OKRO_F.next() ?}
   !};
   fpoz:=fpoz-1
?};
OKRO_F.cntx_pop();
exec('tab_fill','fks_ksr');
UNPAR.cntx_pop();
ROZNE.cntx_pop()


\add_wid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Dodaje rekordy do tabeli z widocznymi pozycjami
::  OLD: \add_wid/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
TAB_WID.SYM_KON:=_OBROT.SYM_KON;
TAB_WID.WINIEN:=_OBROT.WINIEN;
TAB_WID.MA:=_OBROT.MA;
TAB_WID.SWINIEN:=_OBROT.SWINIEN;
TAB_WID.SMA:=_OBROT.SMA;
TAB_WID.POZIOM:=_OBROT.POZIOM;
TAB_WID.ANALIT:=_OBROT.ANALIT;
TAB_WID.LP_DRU:=lp_dru; lp_dru+=1;
TAB_WID.add()


\widoczne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Wrzuca do tabelki tymczasowej rozwiniete elementy i drukuje je
::  OLD: \widoczne/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
TAB_WID:=tab_tmp(1,'LP_DRU','INTEGER',''
                  ,'SYM_KON','STRING[35]','Konto'
                  ,'WINIEN','REAL','Obroty Wn'
                  ,'MA','REAL','Obroty Ma'
                  ,'SWINIEN','REAL','Saldo Wn'
                  ,'SMA','REAL','Saldo Ma'
                  ,'POZIOM','INTEGER','Pozycja'
                  ,'ANALIT','INTEGER',''
                );
_OBROT.cntx_psh();
lp_dru:=1;
{! _i:=1..MASK_KON.size()
|! {? dwiewalo || _OBROT.index(ind_wbr4) || _OBROT.index(ind_obr4) ?};
   _OBROT.prefix(_i,null);
   {? _OBROT.first()
   || {! |?
         exec('add_wid','fks_ksr');
         {? _OBROT.tr_state()=1 || exec('rekuren','fks_ksr',_OBROT.ref()) ?};
         _OBROT.next()
      !}
   ?}
!};
_OBROT.cntx_pop();
exec('rep_exec','#b_report','','fks_obroty',,,,,,,,'W');
VAR_DEL.delete('TAB_WID','lp_dru');
1


\tab_create
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Tworzenie tabelki tymczasowej
::  OLD: \tab_create/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
TAB_OBR:=tab_tmp(2,'TREE','TREE_REF',''
                  ,'SYM_KON','STRING[35]','Konto'@
                  ,'POZIOM','INTEGER','Poziom'@
                  ,'WINIEN','REAL','Obroty Winien'@
                  ,'MA','REAL','Obroty Ma'@
                  ,'SWINIEN','REAL','Saldo Winien'@
                  ,'SMA','REAL','Saldo Ma'@
                  ,'ANALIT','INTEGER','Analityczne'@
                  ,'LP_GEN','INTEGER','Lp generowania'@
                  ,'KON_OPIS','STRING[255]','Opis analityczny konta'@
                );
ind_obr1:=TAB_OBR.index('?');
ind_obr2:=TAB_OBR.ndx_tmp('',1,'LP_GEN',,0,'SYM_KON',,0);
ind_obr3:=TAB_OBR.ndx_tmp('',1,'LP_GEN',,0,'ANALIT',,0,'SYM_KON',,0);
ind_obr4:=TAB_OBR.ndx_tmp('',1,'LP_GEN',,0,'TREE',,0);
ind_obr5:=TAB_OBR.ndx_tmp('',1,'SYM_KON',,0);
_sel:=TAB_OBR.mk_sel('Obroty na kontach'@,'N',0,'tab_obr_wer1',,,,1);
TAB_OBR.win_fld(_sel,,'SYM_KON',,,40);
TAB_OBR.win_act(_sel,,'Formuła','Zwiń/&rozwiń całość'@@,,
                'Zwijanie/rozwijanie całości'@,"exec('zwrw_all','#tree','TAB_OBR','TREE')",,,,,,'R');
TAB_OBR.win_act(_sel,,'Formuła','Z&mień zakres'@@,,
                'Obroty kont'@,"exec('obroty','fks_ksr')",,1,,,,'M');
TAB_OBR.win_act(_sel,1,'Formuła','Z&mień zakres'@@,,
                'Obroty kont'@,"exec('obroty','fks_ksr')",,1,,,,'M');
TAB_OBR.win_act(_sel,,'Formuła','Odświ&eż'@@,,
                'Odświeżenie okna'@,"exec('odswiez','fks_ksr')",,,,,,'E');
{? dwiewalo | PAR_SKID.get(33)<>'N'
|| TAB_OBR.win_act(_sel,,'Formuła','Zmień &walutę przeliczeń'@@,,'Zmiana parametrów analizy obrotów'@,"exec('zm_wal','fks_ksr')",,,,,,'W');
   TAB_OBR.win_act(_sel,1,'Formuła','Zmień &walutę przeliczeń'@@,,'Zmiana parametrów analizy obrotów'@,"exec('zm_wal','fks_ksr')",,,,,,'W')
?};
TAB_OBR.win_act(_sel,,'Formuła','Dodaj &konta'@@,,'Dodawanie zakresu kont do analizy'@,"exec('dod_kont','fks_ksr')",,,,,,'K');
TAB_OBR.win_act(_sel,,'Formuła','Zap&isy'@@,,'Zapisy księgowe dla danego poziomu konta'@,
                "exec('zapisy','dok_fks_ks',TAB_OBR.SYM_KON,,{? TAB_OBR.TREE || 'P' || 'M' ?})",,,,,,'I');
TAB_OBR.win_act(_sel,,'Formuła','Opis kon&ta'@@,,'Opis poziomów konta'@,,"exec('op_konta','konto',TAB_OBR.SYM_KON)",,,,,'T');
TAB_OBR.win_act(_sel,,'Formuła','Oblicz obroty'@@,,'Obroty i salda zaznaczonych pozycji'@,,
                "exec('obl_obr','dok_fks',TAB_OBR,TAB_OBR.WINIEN,TAB_OBR.MA,TAB_OBR.SWINIEN,TAB_OBR.SMA)",,
                1,"exec('be_gobr','dok_fks')","exec('ae_gobr','dok_fks')",'O' );
TAB_OBR.win_act(_sel,,'Formuła','Druku&j'@@,,'Drukowanie obrotów'@,"exec('widoczne','fks_ksr'); 0",,,1,"exec('bgp_poz','dok_fks','fks_obroty',TAB_OBR)",,'J',,'icon=print');
_menu:='Raport&y'@;
{! _mi:=0..1
|! TAB_OBR.win_act(_sel,_mi,'Menu',_menu,,'',,,,,,,'Y');
   TAB_OBR.win_act(_sel,_mi,'Formuła','&Wydruk kartoteki księgowań'@@,_menu,,"exec('main','!fks_ksr_zakk',0)",,,,,,'W');
   task_attach('FKS_KSR_ZAKK');
   TAB_OBR.win_act(_sel,_mi,'Formuła','&Sprawozdania według analityk'@@,_menu,,"exec('main','!fks_ksr_zbwa',0)",,,,,,'S');
   task_attach('FKS_KSR_ZBWA');
   TAB_OBR.win_act(_sel,_mi,'Formuła','&Zestawienie obrotów i sald'@@,_menu,,"exec('main','!fks_ksr_zbos',0)",,,,,,'Z');
   task_attach('FKS_KSR_ZBOS');
   TAB_OBR.win_act(_sel,_mi,'Formuła','Zapytania S&QL'@@,_menu,,"exec('main','!fks_ksr_zsql')",,,,,,'Q');
   task_attach('FKS_KSR_ZSQL')
!};
TAB_OBR.win_act(_sel,,'Formuła','P&arametry pracy'@@,,'Zmiana parametrów pracy'@,
                "_ar:=SSTALE.AR;
                 _ao:=SSTALE.AO;
                 _wl:=SSTALE.WAL;
                 _odd:=OPERATOR.DEPT;
                 {? exec('fks_ksr_params','dok_fks_ks')
                    & (_ao<>SSTALE.AO | _ar<>SSTALE.AR | _wl<>SSTALE.WAL | _odd<>OPERATOR.DEPT)
                 || ROZNE.UT_OKROD:=__OBR.UT_OKROD:=SSTALE.AO;
                    ROZNE.UT_OKRDO:=__OBR.UT_OKRDO:=SSTALE.AO;
                    ROZNE.UT_ROKOD:=__OBR.UT_ROKOD:=SSTALE.AR;
                    ROZNE.UT_ROKDO:=__OBR.UT_ROKDO:=SSTALE.AR;
                    exec('rfresh','fks_ksr')
                 ?}
                ",,,,,,'C');
TAB_OBR.win_act(_sel,1,'Formuła','P&arametry pracy'@@,,'Zmiana parametrów pracy'@,
                "_ar:=SSTALE.AR;
                 _ao:=SSTALE.AO;
                 _wl:=SSTALE.WAL;
                 _odd:=OPERATOR.DEPT;
                 {? exec('fks_ksr_params','dok_fks_ks')
                    & (_ao<>SSTALE.AO | _ar<>SSTALE.AR | _wl<>SSTALE.WAL | _odd<>OPERATOR.DEPT)
                 || ROZNE.UT_OKROD:=__OBR.UT_OKROD:=SSTALE.AO;
                    ROZNE.UT_OKRDO:=__OBR.UT_OKRDO:=SSTALE.AO;
                    ROZNE.UT_ROKOD:=__OBR.UT_ROKOD:=SSTALE.AR;
                    ROZNE.UT_ROKDO:=__OBR.UT_ROKDO:=SSTALE.AR;
                    exec('rfresh','fks_ksr')
                 ?}
                ",,,,,,'C');
TAB_OBR.win_act(_sel,,'Szukaj');
TAB_OBR.win_act(_sel,,'Rekord',,,,"{? TAB_OBR.ANALIT=-1
                                   || TAB_OBR.actions(TAB_OBR.win_sel('?'),'S',,1); echo()
                                   || TAB_OBR.actions(TAB_OBR.win_sel('?'),,,1);
                                      KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR,SSTALE.AR().SYNT+TAB_OBR.SYM_KON);
                                      {? KS.first()
                                      || opis_poz:='';odd_ksn:=0;
                                         exec('opis','konto',TAB_OBR.SYM_KON);
                                         exec('ka_opis','konto');
                                         TAB_OBR.KON_OPIS:=opis_poz;
                                         &opis_poz; &odd_ksn
                                      ?}; KS.cntx_pop()
                                   ?}; 0");
TAB_OBR.win_fml(_sel,,'SYM_KON',,'ICON_BEFORE',
                "TAB_OBR.cntx_psh(); TAB_OBR.index(ind_obr1); TAB_OBR.prefix(TAB_OBR.ref());
                 _zwrot:={? TAB_OBR.first()
                         || TAB_OBR.cntx_pop();
                            {? TAB_OBR.tr_state()=1
                            || 'xwin16.png:75'
                            || 'xwin16.png:74'
                            ?}
                         || TAB_OBR.cntx_pop(); 'xwin16.png:76'
                         ?}; _zwrot");
{? dwiewalo
|| k_1:='Obroty Wn (%1)'@[UNPAR.PSLO().KOD];
   k_2:='Obroty Ma (%1)'@[UNPAR.PSLO().KOD];
   k_3:='Obroty Wn (%1)'@[FINFO.NAROD().KOD];
   k_4:='Obroty Ma (%1)'@[FINFO.NAROD().KOD]
|| k_1:='Obroty Wn'@;
   k_2:='Obroty Ma'@;
   k_3:='Saldo Wn'@;
   k_4:='Saldo Ma'@
?};
:: przyciski
TAB_OBR.win_btn(_sel,'text=%1,panel=right'['Z&mień zakres'@],'menu:M',"","",,"","",'');
TAB_OBR.win_btn(_sel,'text=%1,panel=right'['Dodaj &konta'@],'menu:K',"","",,"","",'');
TAB_OBR.win_btn(_sel,'text=%1,panel=right'['Zap&isy'@],'menu:I',"","",,"","",'');
TAB_OBR.win_btn(_sel,'text=%1,panel=right'['Odświ&eż'@],'menu:E',"","",,"","",'');
::
TAB_OBR.win_fld(_sel,,'WINIEN',,,18,2,,k_1);
TAB_OBR.win_fld(_sel,,'MA',,,18,2,,k_2);
TAB_OBR.win_fld(_sel,,'SWINIEN',,,18,2,,k_3);
TAB_OBR.win_fld(_sel,,'SMA',,,18,2,,k_4);
TAB_OBR.win_sel(_sel);
_sel


\tab_create_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Tworzenie tabelki tymczasowej (wersja walutowa)
::  OLD: \tab_create/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
TAB_OBRW:=tab_tmp(2,'TREE','TREE_REF',''
                  ,'SYM_KON','STRING[35]','Konto'@
                  ,'POZIOM','INTEGER','Poziom'@
                  ,'WINIEN','REAL','Obroty Winien'@
                  ,'MA','REAL','Obroty Ma'@
                  ,'SWINIEN','REAL','Saldo Winien'@
                  ,'SMA','REAL','Saldo Ma'@
                  ,'ANALIT','INTEGER','Analityczne'@
                  ,'LP_GEN','INTEGER','Lp generowania'@
                  ,'KON_OPIS','STRING[255]','Opis analityczny konta'@
                );
ind_wbr1:=TAB_OBRW.index('?');
ind_wbr2:=TAB_OBRW.ndx_tmp('',1,'LP_GEN',,0,'SYM_KON',,0);
ind_wbr3:=TAB_OBRW.ndx_tmp('',1,'LP_GEN',,0,'ANALIT',,0,'SYM_KON',,0);
ind_wbr4:=TAB_OBRW.ndx_tmp('',1,'LP_GEN',,0,'TREE',,0);
ind_wbr5:=TAB_OBRW.ndx_tmp('',1,'SYM_KON',,0);
_sel:=TAB_OBRW.mk_sel('Obroty na kontach'@,'N',0,'tab_obr_wer1',,,,1);
TAB_OBRW.win_fld(_sel,,'SYM_KON',,,40);
TAB_OBRW.win_act(_sel,,'Formuła','Zwiń/&rozwiń całość'@@,,
                'Zwijanie/rozwijanie całości',"exec('zwrw_all','#tree','TAB_OBRW','TREE')",,,,,,'R');
TAB_OBRW.win_act(_sel,,'Formuła','Z&mień zakres'@@,,
                'Obroty kont'@,"exec('obroty','fks_ksr')",,1,,,,'M');
TAB_OBRW.win_act(_sel,1,'Formuła','Z&mień zakres'@@,,
                'Obroty kont'@,"exec('obroty','fks_ksr')",,1,,,,'M');
TAB_OBRW.win_act(_sel,,'Formuła','Odświ&eż'@@,,
                'Odświeżenie okna'@,"exec('odswiez','fks_ksr')",,,,,,'E');
{? dwiewalo | PAR_SKID.get(33)<>'N'
|| TAB_OBRW.win_act(_sel,,'Formuła','Zmień &walutę obrotów'@@,,'Zmiana parametrów analizy obrotów'@,"exec('zm_wal','fks_ksr')",,,,,,'W');
   TAB_OBRW.win_act(_sel,1,'Formuła','Zmień &walutę obrotów'@@,,'Zmiana parametrów analizy obrotów'@,"exec('zm_wal','fks_ksr')",,,,,,'W')
?};
TAB_OBRW.win_act(_sel,,'Formuła','Dodaj &konta'@@,,'Dodawanie zakresu kont do analizy'@,"exec('dod_kont','fks_ksr')",,,,,,'D');
TAB_OBRW.win_act(_sel,,'Formuła','Zap&isy'@@,,'Zapisy księgowe dla danego poziomu konta'@,"exec('zapisy','dok_fks_ks',TAB_OBRW.SYM_KON,UNPAR.PSLO)",,,,,,'I');
TAB_OBRW.win_act(_sel,,'Formuła','Opis kon&ta'@@,,'Opis poziomów konta'@,,"exec('op_konta','konto',TAB_OBRW.SYM_KON)",,,,,'T');
TAB_OBRW.win_act(_sel,,'Formuła','Oblicz obroty'@@,,'Obroty i salda zaznaczonych pozycji'@,,
                "exec('obl_obr','dok_fks',TAB_OBRW,TAB_OBRW.WINIEN,TAB_OBRW.MA,TAB_OBRW.SWINIEN,TAB_OBRW.SMA)",,
                1,"exec('be_gobr','dok_fks')","exec('ae_gobr','dok_fks')" ,'O');
TAB_OBRW.win_act(_sel,,'Formuła','Druku&j'@@,,'Drukowanie obrotów'@,"exec('widoczne','fks_ksr'); 0",,,1,"exec('bgp_poz','dok_fks','fks_obroty',TAB_OBRW)",,'J');
_menu:='Raport&y'@;
{! _mi:=0..1
|! TAB_OBRW.win_act(_sel,_mi,'Menu',_menu,,'');
   TAB_OBRW.win_act(_sel,_mi,'Formuła','&Wydruk kartoteki księgowań'@@,_menu,,"exec('main','!fks_ksr_zakk',0)",,,,,,'W');
   task_attach('FKS_KSR_ZAKK');
   TAB_OBRW.win_act(_sel,_mi,'Formuła','&Sprawozdania według analityk'@@,_menu,,"exec('main','!fks_ksr_zbwa',0)",,,,,,'S');
   task_attach('FKS_KSR_ZBWA');
   TAB_OBRW.win_act(_sel,_mi,'Formuła','&Zestawienie obrotów i sald'@@,_menu,,"exec('main','!fks_ksr_zbos',0)",,,,,,'Z');
   task_attach('FKS_KSR_ZBOS');
   TAB_OBRW.win_act(_sel,_mi,'Formuła','Zapytania S&QL'@@,_menu,,"exec('main','!fks_ksr_zsql')",,,,,,'Q');
   task_attach('FKS_KSR_ZSQL')
!};
TAB_OBRW.win_act(_sel,,'Formuła','P&arametry pracy'@@,,'Zmiana parametrów pracy'@,
                "_ar:=SSTALE.AR;
                 _ao:=SSTALE.AO;
                 _wl:=SSTALE.WAL;
                 _odd:=OPERATOR.DEPT;
                 {? exec('fks_ksr_params','dok_fks_ks') & (_ao<>SSTALE.AO | _ar<>SSTALE.AR | _wl<>SSTALE.WAL | _odd<>OPERATOR.DEPT)
                 || ROZNE.UT_OKROD:=__OBR.UT_OKROD:=SSTALE.AO;
                    ROZNE.UT_OKRDO:=__OBR.UT_OKRDO:=SSTALE.AO;
                    ROZNE.UT_ROKOD:=__OBR.UT_ROKOD:=SSTALE.AR;
                    ROZNE.UT_ROKDO:=__OBR.UT_ROKDO:=SSTALE.AR;
                    exec('rfresh','fks_ksr')
                 ?}
                ",,,,,,'C');
TAB_OBRW.win_act(_sel,1,'Formuła','P&arametry pracy'@@,,'Zmiana parametrów pracy'@,
                "_ar:=SSTALE.AR;
                 _ao:=SSTALE.AO;
                 _wl:=SSTALE.WAL;
                 _odd:=OPERATOR.DEPT;
                 {? exec('fks_ksr_params','dok_fks_ks') & (_ao<>SSTALE.AO | _ar<>SSTALE.AR | _wl<>SSTALE.WAL | _odd<>OPERATOR.DEPT)
                 || ROZNE.UT_OKROD:=__OBR.UT_OKROD:=SSTALE.AO;
                    ROZNE.UT_OKRDO:=__OBR.UT_OKRDO:=SSTALE.AO;
                    ROZNE.UT_ROKOD:=__OBR.UT_ROKOD:=SSTALE.AR;
                    ROZNE.UT_ROKDO:=__OBR.UT_ROKDO:=SSTALE.AR;
                    exec('rfresh','fks_ksr')
                 ?}
                ",,,,,,'C');
TAB_OBRW.win_act(_sel,,'Szukaj');
TAB_OBRW.win_act(_sel,,'Rekord',,,,"{? TAB_OBRW.ANALIT=-1
                                   || TAB_OBRW.actions(TAB_OBRW.win_sel('?'),'S',,1); echo()
                                   || TAB_OBRW.actions(TAB_OBRW.win_sel('?'),,,1);
                                      KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR,SSTALE.AR().SYNT+TAB_OBRW.SYM_KON);
                                      {? KS.first()
                                      || opis_poz:='';odd_ksn:=0;
                                         exec('opis','konto',TAB_OBRW.SYM_KON);
                                         exec('ka_opis','konto');
                                         TAB_OBRW.KON_OPIS:=opis_poz;
                                         &opis_poz; &odd_ksn
                                      ?}; KS.cntx_pop()
                                   ?}; 0");
TAB_OBRW.win_fml(_sel,,'SYM_KON',,'ICON_BEFORE',
                "TAB_OBRW.cntx_psh(); TAB_OBRW.index(ind_wbr1); TAB_OBRW.prefix(TAB_OBRW.ref());
                 _zwrot:={? TAB_OBRW.first()
                         || TAB_OBRW.cntx_pop();
                            {? TAB_OBRW.tr_state()=1
                            || 'xwin16.png:75'
                            || 'xwin16.png:74'
                            ?}
                         || TAB_OBRW.cntx_pop(); 'xwin16.png:76'
                         ?}; _zwrot");
{? dwiewalo
|| k_1:='Obroty Wn (Waluta)';
   k_2:='Obroty Ma (Waluta)';
   k_3:='Obroty Wn ('+FINFO.NAROD().KOD+')';
   k_4:='Obroty Ma ('+FINFO.NAROD().KOD+')'
|| k_1:='Obroty Wn';
   k_2:='Obroty Ma';
   k_3:='Saldo Wn';
   k_4:='Saldo Ma'
?};
:: przyciski
TAB_OBRW.win_btn(_sel,'text=%1,panel=right'['Z&mień zakres'@],'menu:M',"","",,"","",'');
TAB_OBRW.win_btn(_sel,'text=%1,panel=right'['Dodaj &konta'@],'menu:K',"","",,"","",'');
TAB_OBRW.win_btn(_sel,'text=%1,panel=right'['Odświ&eż'@],'menu:E',"","",,"","",'');
::
TAB_OBRW.win_fld(_sel,,'WINIEN',,,18,2,,k_1);
TAB_OBRW.win_fld(_sel,,'MA',,,18,2,,k_2);
TAB_OBRW.win_fld(_sel,,'SWINIEN',,,18,2,,k_3);
TAB_OBRW.win_fld(_sel,,'SMA',,,18,2,,k_4);
TAB_OBRW.win_sel(_sel);
_sel


\tab_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Wypełnianie tabelki tymczasowej
::  OLD: \tab_create/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
{? zm_wal
|| {? MASK_KON.first()
   || {! |?
         exec('first_fill','fks_ksr',0);
         MASK_KON.next()
      !}
   ?};
   zm_wal:=0
|| exec('first_fill','fks_ksr',0)
?};
_OBROT.hdr_sel();
_opis:=', okresy: %1-%2'@[$ROZNE.UT_OKROD().NR,$ROZNE.UT_OKRDO().NR];
{? ~dwiewalo & UNPAR.PSLO & UNPAR.PSLO<>SSTALE.WAL
|| _opis+=', waluta: %1'@[UNPAR.PSLO().KOD];
    {? UNPAR.P25<>1 | (UNPAR.PSLO<>null & UNPAR.PSLO<>SSTALE.WAL)
    || _opis+=', kurs: %1'@[form(UNPAR.P25,,6,'9,')]
    ?}
|| _opis+=', waluta: %1'@[UNPAR.PSLO().KOD]
?};
_OBROT.hdr_sel(_opis);
{? dwiewalo || _OBROT.index(ind_wbr1) || _OBROT.index(ind_obr1) ?};
_OBROT.prefix();
_OBROT.first()


\tab_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Dwie waluty - zapytanie sql z pozycjami dokumentow ksiegowych
::   WE: _a - symbol konta
::       _b - skrocona nazwa jednostki ksiegowej
::       _c - maski tabeli POZ
::       _d - kod waluty
::  OLD: \tab_poz/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? ROZNE.PAR4=4
|| TAB_POZ:=sql('select '+
                '/*+MASK_FILTER(POZ,:_c) '+
                'INDEX (POZ,KON) */ '+
                'POZ.KON as KONTO, '+
                'KS.SYM as SYNT, '+
                'sum(case POZ.STR when ''Wn'' then POZ.SUM end) as WN, '+
                'sum(case POZ.STR when ''Ma'' then POZ.SUM end) as MA, '+
                'sum(case POZ.STR when ''Wn'' then POZ.SUMW end) as WWN, '+
                'sum(case POZ.STR when ''Ma'' then POZ.SUMW end) as WMA '+
                'from @POZ '+
                'join @DOK using (POZ.DOK,DOK.REFERENCE) '+
                'join ODD using (DOK.ODD,ODD.REFERENCE) '+
                'left join SLO using (POZ.WAL,SLO.REFERENCE) '+
                'left join KOM using (POZ.KOM,KOM.REFERENCE) '+
                'left join KS using (KOM.KS,KS.REFERENCE) '+
                'where '+
                'POZ.KON between \''+ROZNE.KS_OD().SYM+'\' and \''+ROZNE.KS_DO().SYM+%255+'\''+
                'AND POZ.ZP = ''T'' AND '+
                'ODD.OD like \':_b\''+
                {? _d<>FINFO.NAROD().KOD | dwiewalo
                || ' AND SLO.KOD = \':_d\' '
                || ' '
                ?}+
                'group by POZ.KON, KS.SYM '+
                'order by KONTO',_a,_b,_c,_d)
|? ROZNE.PAR4=5
|| KON_SCH.cntx_psh();
   KON_SCH.index('IND_LP');
   KON_SCH.prefix(ROZNE.KON_WYDR);
   _sql:= 'select '+
          '/*+MASK_FILTER(POZ,:_c) '+
          'INDEX (POZ,KON) */ '+
          'POZ.KON as KONTO, '+
          'KS.SYM as SYNT, '+
          'sum(case POZ.STR when ''Wn'' then POZ.SUM end) as WN, '+
          'sum(case POZ.STR when ''Ma'' then POZ.SUM end) as MA, '+
          'sum(case POZ.STR when ''Wn'' then POZ.SUMW end) as WWN, '+
          'sum(case POZ.STR when ''Ma'' then POZ.SUMW end) as WMA '+
          'from @POZ '+
          'join @DOK using (POZ.DOK,DOK.REFERENCE) '+
          'join ODD using (DOK.ODD,ODD.REFERENCE) '+
          'left join SLO using (POZ.WAL,SLO.REFERENCE) '+
          'left join KOM using (POZ.KOM,KOM.REFERENCE) '+
          'left join KS using (KOM.KS,KS.REFERENCE) '+
          'where '+
          'POZ.ZP = ''T'' AND ODD.OD like \':_b\'';
   {? KON_SCH.first()
   || {? KON_SCH.MZ = 'M'
      || {! |?
            {? KON_SCH.MASKA+1='?' || KON_SCH.MASKA:=KON_SCH.MASKA-1 ?};
            KON_SCH.MASKA+1='?'
         !};
         KON_SCH.MASKA:=KON_SCH.MASKA+'%';
         KON_SCH.MASKA:=STR.gsub( KON_SCH.MASKA,'?','_');
         _sql+='and (POZ.KON like \''+KON_SCH.MASKA+'\''
      || _sql+='and (POZ.KON between \''+KON_SCH.ZOD+'\' and \''+KON_SCH.ZDO+%255+'\''
      ?};
      {!|? KON_SCH.next()
      |! {? KON_SCH.MZ = 'M'
         || {! |?
               {? KON_SCH.MASKA+1='?' || KON_SCH.MASKA:=KON_SCH.MASKA-1 ?};
               KON_SCH.MASKA+1='?'
            !};
            KON_SCH.MASKA:=KON_SCH.MASKA+'%';
            KON_SCH.MASKA:=STR.gsub( KON_SCH.MASKA,'?','_');
            _sql+=' or POZ.KON like \''+KON_SCH.MASKA+'\''
         || _sql+=' or POZ.KON between \''+KON_SCH.ZOD+'\' and \''+KON_SCH.ZDO+%255+'\''
         ?}
      !};
      _sql+=') '
   ?};
   {? _d<>FINFO.NAROD().KOD | dwiewalo
   || _sql+=' AND SLO.KOD = \':_d\' '
   || _sql+=' '
   ?};
   _sql+='group by POZ.KON, KS.SYM order by KONTO';
   TAB_POZ:=sql(_sql,_a,_b,_c,_d);
   KON_SCH.cntx_pop()

|| TAB_POZ:=sql('select '+
                '/*+MASK_FILTER(POZ,:_c) '+
                'INDEX (POZ,KON) */ '+
                'POZ.KON as KONTO, '+
                'KS.SYM as SYNT, '+
                'sum(case POZ.STR when ''Wn'' then POZ.SUM end) as WN, '+
                'sum(case POZ.STR when ''Ma'' then POZ.SUM end) as MA, '+
                'sum(case POZ.STR when ''Wn'' then POZ.SUMW end) as WWN, '+
                'sum(case POZ.STR when ''Ma'' then POZ.SUMW end) as WMA '+
                'from @POZ '+
                'join @DOK using (POZ.DOK,DOK.REFERENCE) '+
                'join ODD using (DOK.ODD,ODD.REFERENCE) '+
                'left join SLO using (POZ.WAL,SLO.REFERENCE) '+
                'left join KOM using (POZ.KOM,KOM.REFERENCE) '+
                'left join KS using (KOM.KS,KS.REFERENCE) '+
                'where '+
                'POZ.KON like \':_a\' AND POZ.ZP = ''T'' AND '+
                'ODD.OD like \':_b\''+
                {? _d<>FINFO.NAROD().KOD | dwiewalo
                || ' AND SLO.KOD = \':_d\' '
                || ' '
                ?}+
                'group by POZ.KON, KS.SYM '+
                'order by KONTO',_a,_b,_c,_d)
?}
\first_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Wypelnienie tabeli kont i ich obrotow
::   WE: _a - 0 - pierwsze uruchomienie
::            1 - kolejne
::  OLD: \first_fill/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
echo('Proszę czekać. Trwa wykonywanie obliczeń...'@);
lp_gen+=1; ref_zer:=0;
{? dwiewalo || _OBROT.index(ind_wbr2) || _OBROT.index(ind_obr2) ?};
_OBROT.prefix(lp_gen);
{? _a=0 & zm_wal=0 || _OBROT.erase() ?};
{? zm_wal=0
|| _konto:={? ROZNE.PAR4=0 || ROZNE.PREFIX || ROZNE.MASKA ?};
   _maska:=35+exec('str_sql','#sql',_konto);
   _nagl:=35+({? ROZNE.PAR4=0 || ROZNE.PREFIX || ROZNE.MASKA ?}+35*'?');
   MASK_KON.SYM_KON:=_maska; MASK_KON.NAGLOWEK:=_nagl; MASK_KON.add()
|| _maska:=MASK_KON.SYM_KON; _nagl:=MASK_KON.NAGLOWEK
?};
_odd:={? OPERATOR.DEPT=null || '%' || OPERATOR.DEPT().OD ?};
VAR_DEL.delete('TAB_POZ');
exec('tab_poz','fks_ksr',_maska,_odd,fpoz,{? dwiewalo || UNPAR.PSLO().KOD || SSTALE.WAL().KOD ?});
{? TAB_POZ.first()
|| _OBROT.cntx_psh();
   _OBROT.prefix();
   _OBROT.blank();
   _OBROT.ANALIT:=-1;
   _OBROT.LP_GEN:=lp_gen;
   _OBROT.SYM_KON:=_nagl;
   {? _OBROT.add() || ref_zer:=#_OBROT.ref() ?};
   _OBROT.cntx_pop();
   KS.cntx_psh();
   KS.index('SYM');
   KS.prefix(SSTALE.AR);
   BUD.cntx_psh();
   BUD.index('KS');
   {! |?
      _OBROT.blank();
      _OBROT.SYM_KON:=TAB_POZ.KONTO;
      _OBROT.LP_GEN:=lp_gen;
      _OBROT.POZIOM:=0;
      {? KS.find_key(TAB_POZ.SYNT)
      || BUD.prefix(KS.ref());
         _OBROT.POZIOM:=BUD.size()
      ?};
      _OBROT.TREE:=null;
      _OBROT.ANALIT:=1;
      {? dwiewalo | (UNPAR.PSLO=SSTALE.WAL & UNPAR.PSLO<>FINFO.NAROD)
      || _OBROT.WINIEN:=TAB_POZ.WWN;
         _OBROT.MA:=TAB_POZ.WMA
      |? UNPAR.PSLO<>SSTALE.WAL & SSTALE.WAL<>FINFO.NAROD
      || _OBROT.WINIEN:=(TAB_POZ.WWN/__KURS)$2;
         _OBROT.MA:=(TAB_POZ.WMA/__KURS)$2
      || _OBROT.WINIEN:=(TAB_POZ.WN/__KURS)$2;
         _OBROT.MA:=(TAB_POZ.MA/__KURS)$2
      ?};
      {? ~dwiewalo
      || _OBROT.SWINIEN:=F.SWn(_OBROT.WINIEN,_OBROT.MA);
         _OBROT.SMA:=F.SMa(_OBROT.WINIEN,_OBROT.MA)
      || _OBROT.SWINIEN:=TAB_POZ.WN;
         _OBROT.SMA:=TAB_POZ.MA
      ?};
      {? _OBROT.add()
      || _wn:=_OBROT.WINIEN;
         _ma:=_OBROT.MA;
         _swn:=_OBROT.SWINIEN;
         _sma:=_OBROT.SMA;
         {? TAB_POZ.SYNT<>TAB_POZ.KONTO
         || _OBROT.cntx_psh();
            {? dwiewalo || _OBROT.index(ind_wbr3) || _OBROT.index(ind_obr3) ?};
            _OBROT.prefix(lp_gen,0,TAB_POZ.SYNT);
            {? _OBROT.first()
            || _OBROT.WINIEN+=_wn;
               _OBROT.MA+=_ma;
               {? ~dwiewalo
               || _OBROT.SWINIEN:=F.SWn(_OBROT.WINIEN,_OBROT.MA);
                  _OBROT.SMA:=F.SMa(_OBROT.WINIEN,_OBROT.MA)
               || _OBROT.SWINIEN+=_swn;
                  _OBROT.SMA+=_sma
               ?};
               _OBROT.put()
            || _OBROT.blank();
               _OBROT.SYM_KON:=TAB_POZ.SYNT;
               _OBROT.TREE:=ref_zer;
               _OBROT.LP_GEN:=lp_gen;
               _OBROT.WINIEN:=_wn;
               _OBROT.MA:=_ma;
               {? ~dwiewalo
               || _OBROT.SWINIEN:=F.SWn(_OBROT.WINIEN,_OBROT.MA);
                  _OBROT.SMA:=F.SMa(_OBROT.WINIEN,_OBROT.MA)
               || _OBROT.SWINIEN:=_swn;
                  _OBROT.SMA:=_sma
               ?};
               _OBROT.add()
            ?};
            _nad_ref:=_OBROT.ref();
            _OBROT.prefix(lp_gen,-1);
            {? _OBROT.first()
            || _OBROT.WINIEN+=_wn;
               _OBROT.MA+=_ma;
               {? ~dwiewalo
               || _OBROT.SWINIEN:=F.SWn(_OBROT.WINIEN,_OBROT.MA);
                  _OBROT.SMA:=F.SMa(_OBROT.WINIEN,_OBROT.MA)
               || _OBROT.SWINIEN+=_swn;
                  _OBROT.SMA+=_sma
               ?};
               _OBROT.put()
            ?};
            _OBROT.cntx_pop();
            _OBROT.TREE:=_nad_ref;
            _OBROT.put()
         || _OBROT.TREE:=ref_zer;
            {? _OBROT.put()
            || _OBROT.cntx_psh();
               _OBROT.prefix();
               {? _OBROT.seek(ref_zer,_OBROT.name())
               || _OBROT.WINIEN+=_wn;
                  _OBROT.MA+=_ma;
                  {? ~dwiewalo
                  || _OBROT.SWINIEN:=F.SWn(_OBROT.WINIEN,_OBROT.MA);
                     _OBROT.SMA:=F.SMa(_OBROT.WINIEN,_OBROT.MA)
                  || _OBROT.SWINIEN+=_swn;
                     _OBROT.SMA+=_sma
                  ?};
                  _OBROT.put()
               ?};
               _OBROT.cntx_pop()
            ?}
         ?}
      ?};
      TAB_POZ.next()
   !};
   KS.cntx_pop();
   BUD.cntx_pop();
   {? dwiewalo || _OBROT.index(ind_wbr3) || _OBROT.index(ind_obr3) ?};
   _OBROT.prefix(lp_gen,0);
   {? _OBROT.first()
   || {! |?
         _OBROT.cntx_psh();
         exec('rozwin','fks_ksr');
         _OBROT.cntx_pop();
         _OBROT.next()
      !}
   ?}
?};
echo()


\rozwin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.60]
:: OPIS: Tworzenie nizszych poziomow
::  OLD: \rozwin/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
_prefiks:=form(ROZNE.PREFIX);
KS.cntx_psh(); KS.index('SYM');
BUD.cntx_psh(); BUD.index('KS');
SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
SLO.cntx_psh(); SLO.index('SL');
_konto:=_OBROT.SYM_KON;
_ref_nad:=#_OBROT.ref();
KS.prefix(SSTALE.AR,BILANSP.ASYNT+_konto);
{? KS.first()
|| BUD.prefix(KS.ref(),_OBROT.POZIOM+1);
   {? BUD.first()
   || BUD.SLU().SLU();
      SLO.prefix(SLU.ref());
      {? SLO.first()
      || {! |?
            _OBROT.blank();
            _OBROT.SYM_KON:=_spr_kon:=_konto+{? BILANSP.ASEP<>',' || BILANSP.ASEP || '' ?}+SLO.KOD;
            _OBROT.cntx_psh();
            {? dwiewalo || _OBROT.index(ind_wbr3) || _OBROT.index(ind_obr3) ?};
            _OBROT.prefix(lp_gen,1,_spr_kon);
            _dalej:={? _OBROT.first()
                    || {? _OBROT.SYM_KON=_spr_kon || 2 || 1 ?}
                    || 0
                    ?};
            {? ~_dalej
            || _OBROT.prefix(lp_gen,0,_spr_kon);
               {? _OBROT.first() || _dalej:=1 ?}
            ?};
            {? _dalej=1
            || _OBROT.cntx_pop();
               _OBROT.POZIOM:=BUD.POZ;
               _OBROT.TREE:=_ref_nad;
               _OBROT.LP_GEN:=lp_gen;
               _wn:=_ma:=_swn:=_sma:=0;
               _OBROT.cntx_psh();
               {? dwiewalo || _OBROT.index(ind_wbr3) || _OBROT.index(ind_obr3) ?};
               _OBROT.prefix(lp_gen,1,_OBROT.SYM_KON);
               {? _OBROT.first()
               || {! |?
                     _wn+=_OBROT.WINIEN;
                     _ma+=_OBROT.MA;
                     {? ~dwiewalo
                     || _OBROT.SWINIEN:=F.SWn(_OBROT.WINIEN,_OBROT.MA);
                        _OBROT.SMA:=F.SMa(_OBROT.WINIEN,_OBROT.MA)
                     || _swn+=_OBROT.SWINIEN;
                        _sma+=_OBROT.SMA
                     ?};
                     _OBROT.next()
                  !}
               ?};
               _OBROT.cntx_pop();
               _OBROT.WINIEN:=_wn;
               _OBROT.MA:=_ma;
               {? ~dwiewalo
               || _OBROT.SWINIEN:=F.SWn(_OBROT.WINIEN,_OBROT.MA);
                  _OBROT.SMA:=F.SMa(_OBROT.WINIEN,_OBROT.MA)
               || _OBROT.SWINIEN:=_swn;
                  _OBROT.SMA:=_sma
               ?};
               _OBROT.add()
            |? _dalej=2
            || _OBROT.TREE:=_ref_nad;
               _OBROT.put();
               _OBROT.cntx_pop()
            || _OBROT.cntx_pop()
            ?};
            SLO.next()
         !}
      ?}
   ?}
?};
KS.cntx_pop(); BUD.cntx_pop(); SLU.cntx_pop(); SLO.cntx_pop()


\zm_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Zmiana waluty analizy
::  OLD: \zm_wal/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
zm_wal:=0;
{? dwiewalo
|| FINFO.SLWAL().SLU();
   SLO.index('SL');
   SLO.prefix(FINFO.SLWAL().SLU);
   SLO.win_sel('ONE_SEL');
   UNPAR.PSLO();
   SLO.hdr_sel('Zmiana waluty obrotów'@);
   {? SLO.select(,1)
   || {? SLO.ref()=FINFO.NAROD
      || FUN.info('Wybrana waluta nie jest walutą obcą.'@)
      |? UNPAR.PSLO<>SLO.ref()
      || UNPAR.PSLO:=SLO.ref();
         zm_wal:=1
      ?}
   ?}
|| ROZNE.win_edit('OBROTYZW');
   hdr_slo:='Zmiana waluty przeliczeń'@;
   {? ROZNE.edit() || zm_wal:=1?}
?};
{? zm_wal
|| _OBROT.erase();
   lp_gen:=0;
   exec('obroty','fks_ksr')
?}


\dod_kont
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009+]
:: OPIS: Dodawanie kont do analizy
::  OLD: \dod_kont/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
ROZNE.win_edit('OBROTY_M');
ROZNE.PAR4:=2;
ROZNE.PREFIX:=ROZNE.MASKA:='';
{? ROZNE.edit()
|| _prfx:=exec('maska2prefix','konto',ROZNE.MASKA);
   {? _prfx<>'?'
   || ROZNE.PAR4:=0;
      ROZNE.PREFIX:=_prfx
   ?};
   _OBROT.cntx_psh();
   exec('first_fill','fks_ksr',1);
   _OBROT.cntx_pop();
   {? ref_zer<>0 || _OBROT.seek(ref_zer,_OBROT.name()) ?}
?}


\rekuren
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Rekurencyjnie wrzuca do tabelki wynikowej widoczne pozycje
::   WE: _a - ref rekordu nadrzednego
::  OLD: \rekuren/obroty.fml
::----------------------------------------------------------------------------------------------------------------------
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
_zwrot:=1;
_OBROT.cntx_psh();
{? dwiewalo || _OBROT.index(ind_wbr1) || _OBROT.index(ind_obr1) ?};
_OBROT.prefix(_a);
{? _OBROT.first()
|| {! |?
      exec('add_wid','fks_ksr');
      {? _OBROT.tr_state()=1 || exec('rekuren','fks_ksr',_OBROT.ref()) ?};
      _OBROT.next()
   !}
|| _zwrot:=0
?};
_OBROT.cntx_pop();
_zwrot


\rfresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Przeliczanie obrotów jeśli zmieniono finansowe parametry sesji
::----------------------------------------------------------------------------------------------------------------------
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
_OBROT.erase();
exec('obroty','fks_ksr')


\odswiez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Katarzyna Niziolek [19.42]
:: OPIS: Odświeżanie tabeli w zakładce obroty kont
::----------------------------------------------------------------------------------------------------------------------
ROZNE.win_edit({? dwiewalo || {? UNPAR.PSLO=null || FUN.info('Nie wybrano waluty obcej.'@) ?}; 'OBROTY_W' || 'OBROTY' ?});
{? dwiewalo || _OBROT:=TAB_OBRW || _OBROT:=TAB_OBR ?};
_symbol:=_OBROT.SYM_KON;
_OBROT.erase();
{? zm_wal=0
|| {? dwiewalo
   || UNPAR.PSLO_BE:='SLO.win_dict(\'ONE\');SLO.win_sel(\'ONE\');SLU.index(\'NAZ\'); SLU.prefix(\'WALUTY\'); {?SLU.first()||pSloWal:=fld();1 ?}'
   || UNPAR.PSLO_BE:='{? PAR_SKID.get(33)<>\'N\' || SLO.win_dict(\'ONE\');SLO.win_sel(\'ONE\');SLU.index(\'NAZ\'); SLU.prefix(\'WALUTY\'); {?SLU.first()||pSloWal:=fld();1 ?}?}'
   ?};
   {? ROZNE.PAR4<>2 & ROZNE.PAR4<>0 & ROZNE.PAR4<>4 & ROZNE.PAR4<>5 || ROZNE.PAR4:=2 ?}
?};
{? ~(var_pres('MASK_KON')>0)
|| MASK_KON:=tab_tmp(1,'SYM_KON','STRING[35]','Konto','NAGLOWEK','STRING[35]','')
?};
{? dwiewalo || hdr_slo:='Zmiana waluty obrotów'@ || hdr_slo:='Zmiana waluty przeliczeń'@ ?};
ROZNE.win_edit({? dwiewalo || 'OBROTY_W' || 'OBROTY' ?});
{? zm_wal | MASK_KON.erase()
|| {? zm_wal=0 & ROZNE.PAR4=2
   || _prfx:=exec('maska2prefix','konto',ROZNE.MASKA);
      {? _prfx<>'?'
      || ROZNE.PAR4:=0;
         ROZNE.PREFIX:=_prfx
      ?}
   ?};
   exec('obroc','fks_ksr')
?};
{? _symbol<>''
|| _OBROT.cntx_psh();
   {? dwiewalo || _OBROT.index(ind_wbr5) || _OBROT.index(ind_obr5) ?};
   _OBROT.prefix(_symbol);
   _OBROT.first();
   _ref:=_OBROT.ref();
   _OBROT.cntx_pop();
   _OBROT.seek(_ref)
?}


\poppozan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Przesuwanie poziomu w gore lub w dol
::   WE: _a - 1 - w gore
::            2 - w dol
::       _b - alias do tabeli
::  OLD: \poppozan/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_b.cntx_psh(); _ref:=null;
_przesun:={? (_a=1 & _b.prev()) | (_a=2 & _b.next())
          || _lp:=_b.LP; _ref:=_b.ref(); 1
          || 0
          ?};
_b.cntx_pop();
_b.cntx_psh();
do();
{? _przesun
|| _b.LP:=_lp;
   {? _b.put() & _b.seek(_ref)
   || _b.LP:={? _a=1 || _lp+1 || _lp-1 ?}; _b.put()
   ?}
?};
end();
_b.cntx_pop()


\zap_spac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [???] [????]
:: OPIS: Pokazuje pozycje dokumentu na spacje
::  OLD: \zap_spac/ksiega.fml
::----------------------------------------------------------------------------------------------------------------------
_spraw:=(var_pres('spr_grp')>0);
DOK.cntx_psh(); POZ.cntx_psh(); POZ.DOK();
KOMPEN.WN:=KOMPEN.MA:=0;
SIK.WALUTA:=null;
{? POZ.WAL<>null
|| wys_sum:=1;
   SIK.WALUTA:=FINFO.NAROD;
   {? -POZ.STR='wn'
   || KOMPEN.WN:=POZ.SUM; KOMPEN.MA:=0
   || KOMPEN.WN:=0; KOMPEN.MA:=POZ.SUM
   ?}
|| wys_sum:=0
?};
exec('gr_bufor','dok_fks','POZ');
POZ.win_edit({? (_spraw & _a) | (~_spraw & SSTALE.AO().NR=0)
             || 'POZ_BO'
             || 'C_WYS'
             ?});
POZ.display();
DOK.cntx_pop(); POZ.cntx_pop();
wys_sum:=(FINFO.NAROD<>SSTALE.WAL);
{? _spraw || VAR_DEL.delete('wys_sum') ?};
1


\chk_spr_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Weryfikacja poprawności rekordu dla tabeli SPR_NAG
::   WY: akronim pola lub ~~
::----------------------------------------------------------------------------------------------------------------------
{? __CHK.record(SPR_NAG,,'NAZWA')=''
|| {? __CHK.index(SPR_NAG,{? -menu_txt()*'dołącz' || 0 || 1 ?})<>'' || 0 || ~~ ?}
?}


\obj_hlp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Pomocniczy obiekt dla wydruku ksiegowan
::   WE: [_a] - tabela z zapisami
::----------------------------------------------------------------------------------------------------------------------
_hlp:=obj_new('first','next','seek','aget','okres','tab');
_hlp.aget:={? var_press('_a')>0
           || _hlp.tab:=_a;
              3
           |? var_pres('TT_AGET')>100
           || 1
           |? var_press('ZapPoz')>100 & ZapPoz.TAB<>POZ
           || 2
           || 0
           ?};
_hlp.first:="
   {? .aget=1 || TT_AGET.first()
   |? .aget=2 || ZapPoz.TAB.first()
   |? .aget=3 || .tab.first()
   || POZ.first()
   ?}
";
_hlp.next:="
   {? .aget=1 || TT_AGET.next()
   |? .aget=2 || ZapPoz.TAB.next()
   |? .aget=3 || .tab.next()
   || POZ.next()
   ?}
";
_hlp.seek:="
   {? .aget=1 & (4+cur_tab(1,1).name())='pozy'
   || _maska:=(cur_tab(1,1).name())+4;
      DOK.use('doku'+_maska);
      POZ.prefix();
      POZ.seek(TT_AGET.REF,)
   |? .aget=1
   || cur_tab(1,1).seek(TT_AGET.REF,);
      _maska:=(cur_tab(1,1).REF-8)+4;
      POZ.use('pozy'+_maska);
      DOK.use('doku'+_maska);
      POZ.prefix();
      POZ.seek(BIT.sqlint(cur_tab(1,1).REF),)
   |? .aget=2
   || POZ.use(8+ZapPoz.TAB.REF);
      DOK.use('doku'+((8+ZapPoz.TAB.REF)+4));
      POZ.prefix();
      POZ.seek(BIT.sqlint(ZapPoz.TAB.REF),)
   |? .aget=3
   || POZ.use(8+.tab.REF);
      DOK.use('doku'+((8+.tab.REF)+4));
      POZ.prefix();
      POZ.seek(BIT.sqlint(.tab.REF),)
   || 1
   ?}
";
_hlp.okres:="
   {? .aget=2 | .aget=3 | (.aget=1 & (4+cur_tab(1,1).name())<>'pozy')
   || ROZNE.DATA_OD$4+' - '+ROZNE.DATA_DO$4
   || SSTALE.AO().NAZ+' '+OKRO_F.ROK().NAZ
   ?}
";
_hlp


\tab_create_zois
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Tworzy tabelę tymczasową dla zest. obrotów i sald
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('TAB_ZOIS')<=0
|| TAB_ZOIS:=tab_tmp(0,'TREE','TREE_REF',''
                     ,'SYM_KON','STRING[35]','Konto'@
                     ,'OPIS','STRING[80]','Opis'@
                     ,'BOWN','REAL','BO Wn'@
                     ,'BOMA','REAL','BO Ma'@
                     ,'WINIEN','REAL','Obroty Wn'@
                     ,'MA','REAL','Obroty Ma'@
                     ,'NWN','REAL','Narastająco Wn'@
                     ,'NMA','REAL','Narastająco Ma'@
                     ,'SWINIEN','REAL','Saldo Wn'@
                     ,'SMA','REAL','Saldo Ma'@
                     ,'ANALIT','STRING[1]','Analityka'@
                     ,'REK','STRING[1]','Rekord'@
                     ,'LP','INTEGER','Lp generowania'@
                     ,'KON_OPIS','STRING[255]','Opis analityczny konta'@
                    )
?};
{? ROZNE.T_TREE='T'
|| _ndx:=TAB_ZOIS.ndx_tmp(,1,'TREE',,);
   TAB_ZOIS.index(_ndx); TAB_ZOIS.prefix();
   {? var_pres('skonref')<=0 | skonref=null
   || TAB_ZOIS.first()
   || TAB_ZOIS.seek(skonref)
   ?};
   _sel:=TAB_ZOIS.mk_sel('Zestawienie obrotów i sald'@,'N',0,'tab_obr_sal1',,,,1)
|? ROZNE.T_TREE='O'
|| _ndx:=TAB_ZOIS.ndx_tmp(,1,'SYM_KON',,);
   TAB_ZOIS.index(_ndx); TAB_ZOIS.prefix();
   {? var_pres('skonref')<=0 | skonref=null
   || TAB_ZOIS.first()
   || TAB_ZOIS.seek(skonref)
   ?};
   _sel:=TAB_ZOIS.mk_sel('Zestawienie obrotów i sald'@,'T',0,'tab_obr_sal2',,,,,'U',,,,,,,'on')
|| _ndx:=TAB_ZOIS.ndx_tmp(,1,'SYM_KON',,);
   TAB_ZOIS.index(_ndx); TAB_ZOIS.prefix();
   {? var_pres('skonref')<=0 | skonref=null
   || TAB_ZOIS.first()
   || TAB_ZOIS.seek(skonref)
   ?};
   _sel:=TAB_ZOIS.mk_sel('Zestawienie obrotów i sald'@,'T',0,'tab_obr_sal3',,,,,'U')
?};
ind_zois1:=TAB_ZOIS.index('?');
TAB_ZOIS.win_fld(_sel,,'SYM_KON',,,20);
TAB_ZOIS.win_act(_sel,0,'Formuła','Z&mień zakres'@@,,
                'Zestawienie obrotów i sald'@,"exec('zois','fks_ksr')",,1,,,,'M');
TAB_ZOIS.win_act(_sel,1,'Formuła','Z&mień zakres'@@,,
                'Zestawienie obrotów i sald'@,"exec('zois','fks_ksr')",,1,,,,'M');
{? ROZNE.T_TREE='T'
|| TAB_ZOIS.win_act(_sel,,'Formuła','Zwiń/&rozwiń całość'@@,,
                'Zwijanie/rozwijanie całości'@,"exec('zwrw_all','#tree','TAB_ZOIS','TREE')",,,,,,'R')
?};
TAB_ZOIS.win_act(_sel,,'Formuła','Opis kon&ta'@@,,'Opis poziomów konta'@,,
                "exec('op_konta','konto',TAB_ZOIS.SYM_KON)",,,,,'T');
TAB_ZOIS.win_act(_sel,,'Formuła','Zap&isy'@@,,'Zapisy księgowe dla danego poziomu konta'@,
                "exec('zapisy','dok_fks_ks',
                      {? TAB_ZOIS.REK<>'G'
                      || TAB_ZOIS.SYM_KON
                      || 35+({? ROZNE.PAR4=0 || ROZNE.PREFIX || ROZNE.MASKA ?}+35*'?')
                      ?},,{? TAB_ZOIS.TREE || 'P' || 'M' ?})",,,,,,'I');
TAB_ZOIS.win_act(_sel,,'Formuła','Oblicz obroty'@@,,'Obroty i salda zaznaczonych pozycji'@,,
                "exec('obl_obr','dok_fks',TAB_ZOIS,TAB_ZOIS.WINIEN,TAB_ZOIS.MA,TAB_ZOIS.SWINIEN,TAB_ZOIS.SMA)",,
                1,"exec('be_gobr','dok_fks')","exec('ae_gobr','dok_fks')",'O' );
_menu:='Raport&y'@;
{! _mi:=0..1
|! TAB_ZOIS.win_act(_sel,_mi,'Menu',_menu,,'',,,,,,,'Y');
   TAB_ZOIS.win_act(_sel,_mi,'Formuła','&Wydruk kartoteki księgowań'@@,_menu,,"exec('main','!fks_ksr_zakk',0)",,,,,,'W');
   task_attach('FKS_KSR_ZAKK');
   TAB_ZOIS.win_act(_sel,_mi,'Formuła','&Sprawozdania według analityk'@@,_menu,,"exec('main','!fks_ksr_zbwa',0)",,,,,,'S');
   task_attach('FKS_KSR_ZBWA');
   TAB_ZOIS.win_act(_sel,_mi,'Formuła','&Zestawienie obrotów i sald'@@,_menu,,"exec('main','!fks_ksr_zbos',0)",,,,,,'Z');
   task_attach('FKS_KSR_ZBOS');
   TAB_ZOIS.win_act(_sel,_mi,'Formuła','Zapytania S&QL'@@,_menu,,"exec('main','!fks_ksr_zsql')",,,,,,'Q');
   task_attach('FKS_KSR_ZSQL')
!};
TAB_ZOIS.win_act(_sel,1,'Formuła','P&arametry pracy'@@,,'Zmiana parametrów pracy'@,
                "_ar:=SSTALE.AR;
                 _ao:=SSTALE.AO;
                 _wl:=SSTALE.WAL;
                 _odd:=OPERATOR.DEPT;
                 {? exec('fks_ksr_params','dok_fks_ks')
                    & (_ao<>SSTALE.AO | _ar<>SSTALE.AR | _wl<>SSTALE.WAL | _odd<>OPERATOR.DEPT)
                 || ROZNE.UT_OKROD:=SSTALE.AO;
                    ROZNE.UT_OKRDO:=SSTALE.AO;
                    ROZNE.UT_ROKOD:=SSTALE.AR;
                    ROZNE.UT_ROKDO:=SSTALE.AR;
                    ROZNE.ROKKON:=SSTALE.AR;
                    {? var_pres('__ZOIS')>0
                    || __ZOIS.DATA_OD:=ROZNE.DATA_OD;
                       __ZOIS.DATA_DO:=ROZNE.DATA_DO
                    ?};
                    {? ~exec('zois','fks_ksr') || notfirst:=0; TAB_ZOIS.hdr_sel(); TAB_ZOIS.erase() ?}
                 ?}
                ",,,,,,'C');
TAB_ZOIS.win_act(_sel,,'Formuła','P&arametry pracy'@@,,'Zmiana parametrów pracy'@,
                "_ar:=SSTALE.AR;
                 _ao:=SSTALE.AO;
                 _wl:=SSTALE.WAL;
                 _odd:=OPERATOR.DEPT;
                 {? exec('fks_ksr_params','dok_fks_ks')
                    & (_ao<>SSTALE.AO | _ar<>SSTALE.AR | _wl<>SSTALE.WAL | _odd<>OPERATOR.DEPT)
                 || ROZNE.UT_OKROD:=SSTALE.AO;
                    ROZNE.UT_OKRDO:=SSTALE.AO;
                    ROZNE.UT_ROKOD:=SSTALE.AR;
                    ROZNE.UT_ROKDO:=SSTALE.AR;
                    ROZNE.ROKKON:=SSTALE.AR;
                    {? var_pres('__ZOIS')>0
                    || __ZOIS.DATA_OD:=ROZNE.DATA_OD;
                       __ZOIS.DATA_DO:=ROZNE.DATA_DO
                    ?};
                    {? ~exec('zois','fks_ksr') || notfirst:=0; TAB_ZOIS.hdr_sel(); TAB_ZOIS.erase() ?}
                 ?}
                ",,,,,,'C');
{? ROZNE.T_TREE<>'T'
|| TAB_ZOIS.win_act(_sel,,'Kolejność')
?};
TAB_ZOIS.win_act(_sel,,'Szukaj');
{? ROZNE.T_TREE<>'O'
|| TAB_ZOIS.win_act(_sel,0,'Formuła','&Legenda',,,"exec('legenda','color','ZOIS#01#')")
?};
TAB_ZOIS.win_act(_sel,,'Rekord',,,,"{? TAB_ZOIS.ANALIT='O'
                                   || TAB_ZOIS.actions(TAB_ZOIS.win_sel('?'),'S',,1); echo()
                                   || TAB_ZOIS.actions(TAB_ZOIS.win_sel('?'),,,1);
                                      KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR,SSTALE.AR().SYNT+TAB_ZOIS.SYM_KON);
                                      {? KS.first()
                                      || opis_poz:='';odd_ksn:=0;
                                         exec('opis','konto',TAB_ZOIS.SYM_KON);
                                         exec('ka_opis','konto');
                                         TAB_ZOIS.KON_OPIS:=opis_poz;
                                         &opis_poz; &odd_ksn
                                      ?}; KS.cntx_pop()
                                   ?}; 0");
{? ROZNE.T_TREE='T'
|| TAB_ZOIS.win_fml(_sel,,'SYM_KON',,'ICON_BEFORE',
                    "TAB_ZOIS.cntx_psh();
                    _ndxtmp:=TAB_ZOIS.ndx_tmp(,1,'TREE',,);
                    TAB_ZOIS.index(_ndxtmp); TAB_ZOIS.prefix(TAB_ZOIS.ref());
                    _zwrot:={? TAB_ZOIS.first()
                            || TAB_ZOIS.ndx_drop(_ndxtmp); TAB_ZOIS.cntx_pop();
                               {? TAB_ZOIS.tr_state()=1
                               || 'xwin16.png:75'
                               || 'xwin16.png:74'
                               ?}
                            || TAB_ZOIS.ndx_drop(_ndxtmp); TAB_ZOIS.cntx_pop(); 'xwin16.png:76'
                            ?}; _zwrot")
|? ROZNE.T_TREE='N'
|| TAB_ZOIS.win_fml(_sel,,'SYM_KON',,'ICON_BEFORE',
                    "TAB_ZOIS.cntx_psh();
                    _ndxtmp:=TAB_ZOIS.ndx_tmp(,1,'TREE',,);
                    TAB_ZOIS.index(_ndxtmp); TAB_ZOIS.prefix(TAB_ZOIS.ref());
                    _zwrot:={? TAB_ZOIS.first()
                            || TAB_ZOIS.ndx_drop(_ndxtmp); TAB_ZOIS.cntx_pop();'xwin16.png:74'
                            || TAB_ZOIS.ndx_drop(_ndxtmp); TAB_ZOIS.cntx_pop(); 'xwin16.png:76'
                            ?}; _zwrot")
?};
::Przyciski
TAB_ZOIS.win_btn(_sel,'text=%1,panel=right'['Z&mień zakres'@],'menu:M',"","",,"","",'');
TAB_ZOIS.win_btn(_sel,'text=%1,panel=right'['Zap&isy'@],'menu:I',"","",,"","",'');
TAB_ZOIS.win_fld(_sel,,'OPIS',,,30);
{? ROZNE.T_TREE='N'
|| TAB_ZOIS.win_fld(_sel,,'ANALIT',,,,,,,,,2,,"'T'","'N'")
?};
TAB_ZOIS.win_fld(_sel,,'BOWN',,,14,2);
TAB_ZOIS.win_fld(_sel,,'BOMA',,,14,2);
TAB_ZOIS.win_fld(_sel,,'WINIEN',,,14,2);
TAB_ZOIS.win_fld(_sel,,'MA',,,14,2);
TAB_ZOIS.win_fld(_sel,,'NWN',,,14,2);
TAB_ZOIS.win_fld(_sel,,'NMA',,,14,2);
TAB_ZOIS.win_fld(_sel,,'SWINIEN',,,14,2);
TAB_ZOIS.win_fld(_sel,,'SMA',,,14,2);

TAB_ZOIS.win_sel(_sel);
1


\zois
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wypełnia tabelę tymczasową
::----------------------------------------------------------------------------------------------------------------------
ROZNE.win_edit('ZOIS');
exec('ini_wal','konto');
SA:='A';
{? var_pres('w')>100 || obj_del(w) ?};
{? var_pres('ww')>100 || obj_del(ww) ?};
_wystmp:=ROZNE.TYP_WYS;
ROZNE.blank(1);
{? var_pres('__ZOIS')<=0
|| ROZNE.BZ:='N'; ROZNE.UT_DOK:=1; ROZNE.UT_ROZR:=1; ROZNE.PAR1:=1; ROZNE.PAR3:=0; ROZNE.PAR4:=0; ROZNE.PAR2:=3;
   ROZNE.DATA_OD:=SSTALE.AO().POCZ; ROZNE.DATA_DO:=SSTALE.AO().KON; ROZNE.T_TREE:='O'; ROZNE.ANSLU:=null;
   ROZNE.ANSLO:=null
|| exec('zois_par_get','fks_ksr')
?};
ROZNE.UT_KS:=1; ROZNE.UT_SCAL:=0;
ROZNE.efld_opt(ROZNE.win_edit('?'),'enable=1, editable=1',,'UT_SCAL');
w:=obj_new(10); ww:=obj_new(10);
{!_i:=3..10|! ww[_i]:=w[_i]:=0!};
VAR_DEL.delete('TABW','TABK','TABK_IA','TABK_IS'); SSTALE.cntx_psh();
{? ROZNE.ANSLU=null
|| ROZNE.efld_opt('ZOIS','enable=0, editable=0',,'ANSLO')
|| ROZNE.efld_opt('ZOIS','enable=1, editable=1',,'ANSLO')
?};
{? ~ROZNE.edit(" _a:='';
                     {? ROZNE.PAR4=4
                     ||  _a:=__CHK.record(ROZNE,,'KS_OD','KS_DO');
                        {? _a=''
                        || {? ROZNE.KS_OD().SYM>ROZNE.KS_DO().SYM
                           || FUN.info('Błędny zakres kont.'@); _a:='KS_OD'
                           ?}
                        ?}
                     |? ROZNE.PAR4=5
                     || _a:=__CHK.record(ROZNE,,'KON_WYDR');
                        {? _a=''
                        || KON_SCH.prefix(KON_WYDR.ref);
                           _a:={? KON_SCH.first()
                               || ''
                               || FUN.info('Brak definicji kont w podanym zestawie.'@); 'KON_WYDR'
                               ?}
                        ?}
                     ?};
                     {? _a=''
                     || {? ROZNE.DATA_OD>ROZNE.DATA_DO
                        || FUN.info('Błędny zakres dat.'@); _a:='DATA_OD'
                        ?}
                     ?}; _a")
|| VAR_DEL.delete('TABW','TABK','TABK_IA','TABK_IS','k','r','SA');
   obj_del(w); obj_del(ww);
   SSTALE.cntx_pop(); ROZNE.TYP_WYS:=_wystmp; return(0)
?};
tuser:=tm_form(TMPVAT.tm_stamp);
{? ROZNE.PAR4=3
|| _okn:=KS.win_sel('?'); exec('initmpks','dok_fks_zest'); KS.select; KS.win_sel(_okn);
   {? ~TMPSIK.first()
   || &tuser; VAR_DEL.delete('TABW','TABK','TABK_IA','TABK_IS','k','r','SA');
      obj_del(w); obj_del(ww);
      SSTALE.cntx_pop(); ROZNE.TYP_WYS:=_wystmp; return(0)
   ?}
|? ROZNE.PAR4=6
|| _okn:=AN.win_sel('?'); exec('initmpan','dok_fks_zest');
   _r:=ROZNE.SZUK; ROZNE.SZUK:=1;
   AN.select();
   ROZNE.SZUK:=_r; AN.win_sel(_okn);
   {? ~TMPSIK.first()
   || &tuser; VAR_DEL.delete('TABW','TABK','TABK_IA','TABK_IS','k','r','SA');
      obj_del(w); obj_del(ww);
      SSTALE.cntx_pop(); ROZNE.TYP_WYS:=_wystmp; return(0)
   ?}
?};
_symkon:='';
{? var_pres('TAB_ZOIS')>0
|| {? TAB_ZOIS.get()
   || _symkon:=TAB_ZOIS.SYM_KON
   ?}
?};
exec('zois_par_create','fks_ksr');
TAB_ZOIS.erase();
notfirst:=1;
VAR_DEL.delete('rokr');
rokr:='';
OKRO_F.index('ROK');
OKRO_F.prefix(SSTALE.AR);
_ilok:=0;
_zn:='';
_zmz:=0;
{? OKRO_F.first()
|| {!
   |? {? OKRO_F.POCZ<>date(0,0,0)
         & ROZNE.DATA_OD<=OKRO_F.KON & ROZNE.DATA_DO>=OKRO_F.POCZ
      || _ilok+=1;
         {? _zn= ''
         || _zn:=OKRO_F.ZAM
         || {? _zn<>OKRO_F.ZAM || _zmz:=1 ?}
         ?}
      ?};
      ~_zmz & OKRO_F.next()
   !};
   {? ~_zmz || rokr:=_zn ?}
?};
VAR_DEL.delete('TABW','TABK','TABK_IA','TABK_IS');
TABW:=proc_exec('ks_anal@ks_anal',SSTALE.AO().ROK().KOD,SSTALE.AO().ROK().NAZ,SSTALE.WAL().KOD,0,WYDRUKI.ODD().OD,
                                   {? ROZNE.UT_KS || 'T' || 'N' ?}, {? ROZNE.UT_ROZR||'S'||'O'?},ROZNE.PAR2,
                                   ROZNE.PAR4,ROZNE.PREFIX, ROZNE.MASKA,ROZNE.KS_OD().SYM,
                                   ROZNE.KS_DO().SYM,ROZNE.KON_WYDR().KOD,{?ROZNE.UT_DOK||'T'||'N'?},
                                   BILANSP.ASEP,BILANSP.ASYNT,rokr,#ROZNE.KON_WYDR,tuser,ROZNE.DATA_OD,ROZNE.DATA_DO,ROZNE.PAR1,ROZNE.PAR3,ROZNE.BZ,
                                   form(exec('kurs','konto',UNPAR.PSLO().KOD,UNPAR.P25),,,'9.'),1);
_ndx:=TABW.ndx_tmp('',0,'KONTO',,0);
TABW.index(_ndx); TABW.prefix();
_tmp:=1;
_arsynt:=SSTALE.AR().SYNT;
{? TABW.first()
|| k:=TABW.KS;r:=TABW.REK;
   {! |?
      w[1]:={? TABW.REK='S' || (TABW.KONTO-1) || TABW.KONTO ?};
      w[2]:=TABW.OPIS;
      w[5]:=TABW.OBWN; w[6]:=TABW.OBMA; w[7]:=TABW.ONWN; w[8]:=TABW.ONMA;
      {? ROZNE.PAR3 & (TABW.REK='S' | TABW.REK='P') & ((TABW.BOWN<>0 & TABW.BOMA<>0) | (TABW.SAWN<>0 & TABW.SAMA<>0)) & TABW.KSSALDO='D'
      || w[3]:=TABW.BOWN; w[4]:=TABW.BOMA;
         w[9]:=TABW.SAWN; w[10]:=TABW.SAMA
      || {? ROZNE.UT_ROZR
         || w[3]:=F.SWn(TABW.BOWN,TABW.BOMA); w[4]:=F.SMa(TABW.BOWN,TABW.BOMA)
         || w[3]:=TABW.BOWN; w[4]:=TABW.BOMA
         ?};
         w[9]:=F.SWn(TABW.SAWN,TABW.SAMA); w[10]:=F.SMa(TABW.SAWN,TABW.SAMA)
      ?};
      TAB_ZOIS.blank();
      TAB_ZOIS.SYM_KON:=w[1];
      TAB_ZOIS.ANALIT:={? TABW.REK='S' || 'N' || 'T' ?};
      TAB_ZOIS.OPIS:=w[2];
      TAB_ZOIS.BOWN:=w[3];
      TAB_ZOIS.BOMA:=w[4];
      TAB_ZOIS.WINIEN:=w[5];
      TAB_ZOIS.MA:=w[6];
      TAB_ZOIS.NWN:=w[7];
      TAB_ZOIS.NMA:=w[8];
      TAB_ZOIS.SWINIEN:=w[9];
      TAB_ZOIS.SMA:=w[10];
      TAB_ZOIS.REK:=TABW.REK;
      TAB_ZOIS.LP:=_tmp;
      _tmp+=1;
      TAB_ZOIS.add();
      {? +w[1]=_arsynt
      || {! _i:=3..10
         |! ww[_i]+=w[_i]
         !}
      ?};
      TABW.next()
   !}
?};
_nagl:=35+({? ROZNE.PAR4=0 || ROZNE.PREFIX || ROZNE.MASKA ?}+35*'?');
{? ROZNE.PAR4=4
|| _nagl:='Zakres od %1 do %2'@[ROZNE.KS_OD().SYM,ROZNE.KS_DO().SYM]
|? ROZNE.PAR4=5
|| _nagl:='Zestaw kont %1'@[ROZNE.KON_WYDR().KOD]
|? ROZNE.PAR4=3
|| _nagl:='Wybrane konta syntetyczne'
|? ROZNE.PAR4=6
|| _nagl:='Wybrane konta analityczne'
?};
TAB_ZOIS.blank();
TAB_ZOIS.SYM_KON:=_nagl;
TAB_ZOIS.OPIS:='Okres: '+$ROZNE.DATA_OD+' - '+$ROZNE.DATA_DO+
               {? UNPAR.PSLO<>null & UNPAR.PSLO().KOD<>'PLN' || ' waluta: ' + UNPAR.PSLO().KOD || '' ?};
TAB_ZOIS.BOWN:=ww[3];
TAB_ZOIS.BOMA:=ww[4];
TAB_ZOIS.WINIEN:=ww[5];
TAB_ZOIS.MA:=ww[6];
TAB_ZOIS.NWN:=ww[7];
TAB_ZOIS.NMA:=ww[8];
TAB_ZOIS.SWINIEN:=ww[9];
TAB_ZOIS.SMA:=ww[10];
TAB_ZOIS.REK:='G';
TAB_ZOIS.LP:=0;
TAB_ZOIS.ANALIT:='O';
{? ROZNE.T_TREE='T'
|| TAB_ZOIS.add();
   _mroot:=TAB_ZOIS.ref()
|| _mroot:=0
?};
TAB_ZOIS.cntx_psh();
_ndx_zois:=TAB_ZOIS.ndx_tmp(,1,'LP',,1);
TAB_ZOIS.index(_ndx_zois); TAB_ZOIS.prefix();
_opis:='';
_tref:=tab_tmp(,'REFER','INTEGER','','LP','INTEGER','','DL','INTEGER','');
_tref.index(_tref.ndx_tmp(,,'LP',,)); _tref.prefix();
_lp:=0;
_long:=0;
_br:='';

{? ROZNE.T_TREE='T' & TAB_ZOIS.first()
|| {! |?
      {? TAB_ZOIS.REK='S'
      || {? +TAB_ZOIS.SYM_KON=_arsynt
         || _tref.erase();
            _lp:=1;
            _long:=0;
            _tref.blank();
            TAB_ZOIS.TREE:=_mroot;
            _tref.REFER:=#TAB_ZOIS.ref();
            _tref.LP:=_lp;
            _tref.DL:=+TAB_ZOIS.SYM_KON;
            _tref.add()
         |? +TAB_ZOIS.SYM_KON>_tref.DL
         || TAB_ZOIS.TREE:=_tref.REFER;
            _lp+=1;
            _tref.blank();
            _tref.REFER:=#TAB_ZOIS.ref();
            _tref.LP:=_lp;
            _tref.DL:=+TAB_ZOIS.SYM_KON;
            _tref.add()
         |? +TAB_ZOIS.SYM_KON<=_tref.DL
         || {! |?
               {? +TAB_ZOIS.SYM_KON<=_tref.DL
               || _tref.prev()
               || TAB_ZOIS.TREE:=_tref.REFER;
                  _lp+=1;
                  _tref.blank();
                  _tref.REFER:=#TAB_ZOIS.ref();
                  _tref.LP:=_lp;
                  _tref.DL:=+TAB_ZOIS.SYM_KON;
                  _tref.add();
                  0
               ?}
            !}
         ?};
         TAB_ZOIS.put();
         TAB_ZOIS.next()
      |? TAB_ZOIS.REK='P'
      || {? +TAB_ZOIS.SYM_KON=_arsynt
         || TAB_ZOIS.TREE:=_mroot
         || _tref.cntx_psh();
            _tref.last();
            TAB_ZOIS.TREE:=_tref.REFER;
            _tref.cntx_pop()
         ?};
         TAB_ZOIS.put();
         TAB_ZOIS.next()
      |? TAB_ZOIS.REK='G'
      || _zzz:=TAB_ZOIS.next();
         _zzz
      ?}
   !}
?};
{? TAB_ZOIS.first() & TAB_ZOIS.REK='G'
|| {? TAB_ZOIS.size()=1
   || TAB_ZOIS.erase()
   ?}
?};
TAB_ZOIS.cntx_pop();
TAB_ZOIS.ndx_drop(_ndx_zois);

VAR_DEL.delete('skonref');
skonref:=null;
TAB_ZOIS.cntx_psh();
_skndx:=TAB_ZOIS.ndx_tmp(,1,'SYM_KON',,);
TAB_ZOIS.index(_skndx); TAB_ZOIS.prefix(_symkon,);
{? TAB_ZOIS.first()
|| skonref:=TAB_ZOIS.ref()
?};
TAB_ZOIS.ndx_drop(_skndx);
TAB_ZOIS.cntx_pop();

&rokr; &tuser; VAR_DEL.delete('TABW','TABK','TABK_IA','TABK_IS','k','r','SA');
obj_del(w); obj_del(ww);
SSTALE.cntx_pop();
{? 6+ZapPoz.WIN='C_WYS1'
|| ZapPoz.TRYB:=7;
   {? SSTALE.WAL<>FINFO.NAROD || ZapPoz.tryb(3) || ZapPoz.tryb(1) ?}
|| ZapPoz.TRYB:=2
?};
ZapPoz.REPAINT:=1;
ROZNE.TYP_WYS:=_wystmp;
sel_exit();
1


\zois_par_create
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Zmienne do parametrów przeglądania
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__ZOIS')<=0
|| __ZOIS:=obj_new('DATA_OD','DATA_DO','BZ','PAR2','PAR4','PREFIX','MASKA','KS_OD','KS_DO','KON_WYDR','PSLO','P25',
                   'PAR3','PAR1','UT_ROZR','UT_DOK','T_TREE','ANSLU','ANSLO')
?};
__ZOIS.DATA_OD:=ROZNE.DATA_OD;
__ZOIS.DATA_DO:=ROZNE.DATA_DO;
__ZOIS.BZ:=ROZNE.BZ;
__ZOIS.PAR2:=ROZNE.PAR2;
__ZOIS.PAR4:=ROZNE.PAR4;
__ZOIS.PREFIX:=ROZNE.PREFIX;
__ZOIS.MASKA:=ROZNE.MASKA;
__ZOIS.KS_OD:=ROZNE.KS_OD;
__ZOIS.KS_DO:=ROZNE.KS_DO;
__ZOIS.KON_WYDR:=ROZNE.KON_WYDR;
__ZOIS.PSLO:=UNPAR.PSLO;
__ZOIS.P25:=UNPAR.P25;
__ZOIS.PAR3:=ROZNE.PAR3;
__ZOIS.PAR1:=ROZNE.PAR1;
__ZOIS.UT_ROZR:=ROZNE.UT_ROZR;
__ZOIS.UT_DOK:=ROZNE.UT_DOK;
__ZOIS.T_TREE:=ROZNE.T_TREE;
__ZOIS.ANSLU:=ROZNE.ANSLU;
__ZOIS.ANSLO:=ROZNE.ANSLO;
1


\zois_par_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Ustawia zmienne w parametrach przeglądania
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__ZOIS')>0
|| ROZNE.DATA_OD:=__ZOIS.DATA_OD;
   ROZNE.DATA_DO:=__ZOIS.DATA_DO;
   ROZNE.BZ:=__ZOIS.BZ;
   ROZNE.PAR2:=__ZOIS.PAR2;
   ROZNE.PAR4:=__ZOIS.PAR4;
   ROZNE.PREFIX:=__ZOIS.PREFIX;
   ROZNE.MASKA:=__ZOIS.MASKA;
   ROZNE.KS_OD:=__ZOIS.KS_OD;
   ROZNE.KS_DO:=__ZOIS.KS_DO;
   ROZNE.KON_WYDR:=__ZOIS.KON_WYDR;
   UNPAR.PSLO:=__ZOIS.PSLO;
   UNPAR.P25:=__ZOIS.P25;
   ROZNE.PAR3:=__ZOIS.PAR3;
   ROZNE.PAR1:=__ZOIS.PAR1;
   ROZNE.UT_ROZR:=__ZOIS.UT_ROZR;
   ROZNE.UT_DOK:=__ZOIS.UT_DOK;
   ROZNE.T_TREE:=__ZOIS.T_TREE;
   ROZNE.ANSLU:=__ZOIS.ANSLU;
   ROZNE.ANSLO:=__ZOIS.ANSLO
?}


\af_roz_anslu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła po redagowaniu ANSLU
::----------------------------------------------------------------------------------------------------------------------
{? fld()=null
|| ROZNE.ANSLO:=null;
   ROZNE.efld_opt(ROZNE.win_edit('?'),'enable=0, editable=0',,'ANSLO')
|| ROZNE.efld_opt(ROZNE.win_edit('?'),'enable=1, editable=1',,'ANSLO')
?};
ROZNE.ANSLO:=null;
win_disp; 1


\anslu_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wybór kont analitycznych opartych na danym słowniku
::----------------------------------------------------------------------------------------------------------------------
AN_SLU.cntx_psh(); AN.cntx_psh();
_mas:=SSTALE.AR().KOD;
AN.use('koan__'+_mas); AN.index('SYM'); AN.prefix();
AN_SLU.use('koansl'+_mas);
_ndx:=AN_SLU.ndx_tmp(,1,'SLU',,,'SLO',,);
AN_SLU.index(_ndx);
{? ROZNE.ANSLO=null
|| AN_SLU.prefix(ROZNE.ANSLU)
|| AN_SLU.prefix(ROZNE.ANSLU,ROZNE.ANSLO)
?};
VAR_DEL.delete('TMPANKON');
TMPANKON:=tab_tmp(1,'SYM_KON','STRING[35]','Konto');
{? AN_SLU.first()
|| {! |?
      TMPANKON.SYM_KON:=AN_SLU.AN().SYM;
      TMPANKON.add();
      AN_SLU.next()
   !}
?};
::_fml:="TMPANKON.prefix(TAB_ZOIS.SYM_KON);
::       {? ~TMPANKON.first() & TAB_ZOIS.REK<>'G' & TAB_ZOIS.REK<>'O'
::       || TAB_ZOIS.del()
::       ?}";
::TAB_ZOIS.for_each(_fml);
::
::VAR_DEL.delete('TMPANKON');
AN_SLU.ndx_drop(_ndx);
AN_SLU.cntx_pop(); AN.cntx_pop()


\aput_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Trigger po tabeli SLO
::----------------------------------------------------------------------------------------------------------------------
_sloref:=SLO.ref();
AN_SLU.cntx_psh(); AN.cntx_psh();
{? SSTALE.AR<>null
|| _mas:=SSTALE.AR().KOD;
   AN_SLU.use('koansl'+_mas); AN_SLU.index('SLO_AN'); AN_SLU.prefix();
   AN.use('koan__'+_mas); AN.index('SYM'); AN.prefix();
   _tabtmp:=sql('select distinct AN_SLU.AN as REFAN from AN_SLU join SLO where SLO.REFERENCE=\':_a\'',$_sloref );

   AN_SLU.index('DISP');
   {? _tabtmp.first()
   || {! |?
         {? AN.seek(_tabtmp.REFAN)
         || AN_SLU.prefix(AN.ref());
            {? AN_SLU.last() & AN_SLU.SLO=_sloref
            ||  AN.OPIS:=SLO.TR;
                AN.put()
            ?}
         ?};
         _tabtmp.next()
      !}
   ?}
?};
AN_SLU.cntx_pop(); AN.cntx_pop();
~~


:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 8b3b0d0af9fd36819e84518ddfb55d36ab0f31db576594dfd0197c0432a70d5aac06666c3446865ed20f328379eb567ff9d3d1c4636b0f99707b3d0ebd62cfecee20905d2bc874f14ae0a632d19cd9cbc31873095c56e0c736334960cf5ceaaf105105668a5a3806640e928134617f77dd67aa1ed0f58ce014a3ca895e1ed4ab

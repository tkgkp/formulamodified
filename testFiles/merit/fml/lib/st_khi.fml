:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_khi.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa instalacji kontrahenta dla systemu Statystyki
::======================================================================================================================


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Czyści powiązania do rekordu tabeli ST_KHI
::   WE: _a - ST_KHI.ref()
::   WY: >0  -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PRYWATNA><CLEAN>
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---
ST_KHI.cntx_psh(); ST_KHI.clear();
{? ST_KHI.seek(_ref)
||
   {? _can_continue>0
   ||
::    1. Usuwam ST_SESJA które wskazują na ten element
      ST_SESJA.cntx_psh();
      ST_SESJA.index('ST_KHI');
      ST_SESJA.prefix(ST_KHI.ref());
      {? ST_SESJA.first()
      || {!
         |? _can_continue:=exec('delete','st_sesja',ST_SESJA.ref());
            ST_SESJA.first() & _can_continue>0
         !}
      ?};
      ST_SESJA.cntx_pop();

      ST_SESJ2.cntx_psh();
      ST_SESJ2.index('ST_KHI');
      ST_SESJ2.prefix(ST_KHI.ref());
      {? ST_SESJ2.first()
      || {!
         |? _can_continue:=exec('delete','st_sesj2',ST_SESJ2.ref());
            ST_SESJ2.first() & _can_continue>0
         !}
      ?};
      ST_SESJ2.cntx_pop()
   ?};
   ~~
?};
ST_KHI.cntx_pop();
:: --- wszystkie powiazania usuniete? ---

{? _can_continue>0
|| _result:=1
|| undo()
?};

{? _mydo || end() ?};

_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Kasuje podany rekord tabeli ST_KHI (wykonywane w transakcji!!!)
::   WE: _a - ST_KHI.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_KHI.cntx_psh(); ST_KHI.clear();
{? ST_KHI.seek(_ref)
|| {? exec('clean','st_khi',_ref)>0
   || {? ST_KHI.del(,1)>0
      || _result:=1
      || undo();
         _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};

{? _result<0
|| undo()
?};

ST_KHI.cntx_pop();
{? _mydo || end() ?};
_result


\st_khi_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Dołącz dla ST_KHI
::----------------------------------------------------------------------------------------------------------------------
ST_KHI.win_edit('RED');
ST_KHI.blank();
{? VAR.A_ST_KH<>null()
|| ST_KHI.ST_KH:=VAR.A_ST_KH
?};
{? ST_KHI.edit()
|| ST_KHI.add()
?};
~~


\st_khi_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń dla ST_KHI
::----------------------------------------------------------------------------------------------------------------------
_dialog:=1;

_can_continue:=1;

{? VAR.GRUPA='T'
|| _dialog:=0
?};

_what:=exec('to_string','st_khi');
{? _dialog>0
||

   _delete_src:=0;
   _option:='text=%1;selected=%2'['Usunąć pliki i zapisy źródłowe?'@,'N'];
   _msg:='Usunąć instalację kontrahenta: %1?\n'
         'Po wykonaniu tej operacji należy zasilić od nowa dane w QLIK'@[_what];
   _choice:=FUN.multichoice(_msg,,,80,7,_option);

   {? _choice[1]=''
   || _can_continue:=0
   ?};
   {? _can_continue>0
   || {? _choice[2]<>''
      || _delete_src:=1
      ?}
   ?};

   {? _can_continue>0 & _delete_src>0
   || _msg:='Wybrano opcję usunięcia plików źródłowych.\n'
            'Tej operacji nie da się odwrócić.\n\n'
            'Kontynuować?'@;
      _can_continue:=FUN.ask(_msg);
      {? _can_continue>0
      || VAR.GRUPA:='X'
      ?}
   ?}
?};
{? _can_continue>0
|| KOMM.init(250,,'Usuwanie instalacji klienta');
   exec('delete','st_khi',ST_KHI.ref())
?};
~~


\st_khi_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Popraw dla ST_KHI
::----------------------------------------------------------------------------------------------------------------------
_standard:=ST_KHI.STANDARD;
_valid:=$('
   _result:='''';
   {? ST_KHI.STANDARD<>''%1''
   || {? ST_KHI.STANDARD=''N''
      || {? ~FUN.ask(''Oznaczyć instalację klienta jako niestandardową.''@) || _result:=''STANDARD'' ?}
      || {? ~FUN.ask(''Oznaczyć instalację klienta jako standardową.''@) || _result:=''STANDARD'' ?}
      ?}
   ?};
   _result
'[ST_KHI.STANDARD]);
ST_KHI.win_edit('RED');
{? ST_KHI.edit(_valid)
|| {? ST_KHI.put() & ST_KHI.STANDARD<>'' & ST_KHI.STANDARD<>_standard
   ||
      ST_LOG.cntx_psh();
      _Names:=ST_LOG.names();
      _loop:=_Names.first();
      {!
      |? _loop
      |!
         ST_LOG.use(_Names.NAME);
         ST_LOG.index('ST_KHI');
         ST_LOG.prefix(ST_KHI.ref());
         _loop:=ST_LOG.first();
         {!
         |? _loop
         |!
            ST_LOG.STANDARD:=ST_KHI.STANDARD;
            _loop:=ST_LOG.put() & ST_LOG.next()
         !};
         _loop:=_Names.next()
      !};
      ST_LOG.cntx_pop()
   ?}
?};
~~


\st_khi_del_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń przed dla grupy rekordów (tabela ST_KHI)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?'@)
|| VAR.GRUPA:='T';
   1
|| 0
?}


\st_khi_del_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń po dla grupy rekordów (tabela ST_KHI)
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~


\to_string
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.42]
:: OPIS: Zwraca tekstową reprezentację bieżącego rekordu
::   WY: STRING
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
ST_KHI.ST_KH().DANE+' ('+ST_KHI.AS_ADDRE+':'+ST_KHI.AS_PORT+')'

:Sign Version 2.0 jowisz:1048 2021/04/09 15:26:32 40187b5c3b16fe982d3036cc545df419dddb441c0ce6e7ae0eff4db151a1ef80632a8b142cbb059288d8ab7968d1777d94a36276f97b1e27740a55203fdbacf8aeec439eed014faece9a672b989f7691dd116695e7d872c6ba9e111b660c3f0f0667c972cec8680e754f9a5be94dc2c4d593ac6cdda576b41ed1ce3760ed408e

:!UTF-8
::(c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: px_rules.fml [12.10]
:: Utworzony: 2011/09/05
:: Autor: jerry
::======================================================================================================================
:: Zawartosc: Obsluga tabeli PX_RULES i jej zawartosci
::======================================================================================================================


\px_rules_init
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [12.10]
:: OPIS: Inicjuje tabele z formulami dla wielowymiarowego planu pojemnosciowego
::----------------------------------------------------------------------------------------------------------------------
PX_RULES.cntx_psh();
PX_RULES.index('TYP');
PX_RULES.clear();

_add_args:=obj_new('TYP','SYMBOL','NAZWA','RULE');
_add_rule:="_args:=_a;
   {? ~PX_RULES.find_key(_a.TYP,_a.SYMBOL,)
   || PX_RULES.blank();
      PX_RULES.TYP:=_args.TYP;
      PX_RULES.SYMBOL:=_args.SYMBOL;
      PX_RULES.NAZWA:=_args.NAZWA;
      PX_RULES.RULE:=_args.RULE;
      PX_RULES.add()
   ?}
";
::formuly na pojemnik
_add_args.TYP:='CUP';
_add_args.SYMBOL:='Pojemniki wg rozdzielczości planu';
_add_args.NAZWA:='Pojemniki generowane wg rozdzielczości planu (parametr 500351)';
_add_args.RULE:=''+"exec('gen_4resolution','px_gen',_kont,_begin,_end,_rozdz)";
_add_rule(_add_args);

_add_args.TYP:='CUP';
_add_args.SYMBOL:='2 zmiany';
_add_args.NAZWA:='Zmiana 1 (godz. rozp.-godz. rozp.+8h), Zmiana 2 (godz. rozp.+8h-godz. rozp.+16h)';
_add_args.RULE:=''+"exec('gen_2zmiany','px_gen',_kont,_begin,_end,_rozdz)";
_add_rule(_add_args);

_add_args.TYP:='CUP';
_add_args.SYMBOL:='3 zmiany';
_add_args.NAZWA:='Zmiana 1 (godz. rozp.-godz. rozp.+8h), Zmiana 2 (godz. rozp.+8h-godz. rozp.+16h), Zmiana 3 (godz. rozp.+16h-godz. rozp.+24h)';
_add_args.RULE:=''+"exec('gen_3zmiany','px_gen',_kont,_begin,_end,_rozdz)";
_add_rule(_add_args);

_add_args.TYP:='CUP';
_add_args.SYMBOL:='2 zmiany (dni robocze)';
_add_args.NAZWA:='Zmiana 1 (godz. rozp.-godz. rozp.+8h), Zmiana 2 (godz. rozp.+8h-godz. rozp.+16h)';
_add_args.RULE:=''+"exec('gen_2zmiany_wrk','px_gen',_kont,_begin,_end,_rozdz)";
_add_rule(_add_args);

_add_args.TYP:='CUP';
_add_args.SYMBOL:='3 zmiany (dni robocze)';
_add_args.NAZWA:='Zmiana 1 (godz. rozp.-godz. rozp.+8h), Zmiana 2 (godz. rozp.+8h-godz. rozp.+16h), Zmiana 3 (godz. rozp.+16h-godz. rozp.+24h)';
_add_args.RULE:=''+"exec('gen_3zmiany_wrk','px_gen',_kont,_begin,_end,_rozdz)";
_add_rule(_add_args);

_add_args.TYP:='CUP';
_add_args.SYMBOL:='Dni (wszystkie)';
_add_args.NAZWA:='Pn,Wt,Śr,Czw,Pt,Sb,Nd + dni świąteczne';
_add_args.RULE:=''+"exec('gen_dni_all','px_gen',_kont,_begin,_end,_rozdz)";
_add_rule(_add_args);

_add_args.TYP:='CUP';
_add_args.SYMBOL:='Dni (robocze)';
_add_args.NAZWA:='Pn,Wt,Śr,Czw,Pt - dni świąteczne';
_add_args.RULE:=''+"exec('gen_dni_work','px_gen',_kont,_begin,_end,_rozdz)";
_add_rule(_add_args);

_add_args.TYP:='CUP';
_add_args.SYMBOL:='Tygodnie';
_add_args.NAZWA:='Pojemniki tygodniowe od poniedziałku do następnego poniedziałku';
_add_args.RULE:=''+"exec('gen_tygodnie','px_gen',_kont,_begin,_end,_rozdz)";
_add_rule(_add_args);

_add_args.TYP:='CUP';
_add_args.SYMBOL:='Miesiące';
_add_args.NAZWA:='Pojemniki miesięczne od pierwszego do pierwszego dnia miesiąca';
_add_args.RULE:=''+"exec('gen_miesiace','px_gen',_kont,_begin,_end,_rozdz)";
_add_rule(_add_args);

:: typ dla formul w px_tex
_rul_tex:=exec('px_rule_tex','px_param');
_add_args.TYP:=_rul_tex;
_add_args.SYMBOL:=exec('px_rule_tex_tex','px_param');
_add_args.NAZWA:='Materiał do planowania na podstawie technologii';
_add_args.RULE:=''+"exec('pck_technology','px_whisk',_eggs)";
_add_rule(_add_args);

_add_args.TYP:=_rul_tex;
_add_args.SYMBOL:=exec('px_rule_tex_own','px_param');
_add_args.NAZWA:='Materiał do planowania na podstawie przepisu własnego';
_add_args.RULE:=''+"exec('pck_px_tex','px_whisk',_eggs)";
_add_rule(_add_args);

:: jeszcze wywolanie formul z wdrozenia
exec('px_rules_init','px_prepq', _add_rule, _add_args);
PX_RULES.cntx_pop();
~~


\PX_RULES_blank
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: Wartosc poczatkowa PX_RULES
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
{? _fld='SYMBOL' || ''
|? _fld='NAZWA'  || ''
|? _fld='TYP'    || PX_RULES.TYP:=PX_VAR.RULE_TYP
|? _fld='RULE'   || ''
                 || ~~
?}


\PX_RULES_bFE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: przed redakcja pol w PX_RULES
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
{? _fld='SYMBOL' || 1+PX_RULES.SYMBOL<>'~'
|? _fld='NAZWA'  || 1
|? _fld='TYP'    || PX_VAR.RULE_TYP=''
|? _fld='RULE'   || 1
                 || 0
?}


\PX_RULES_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: przed f3 w polach PX_RULES
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
~~


\PX_RULES_aFE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jerry [12.10]
:: OPIS: po redakcji pol w PX_RULES
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
{? _fld='SYMBOL' || fld(form(fld()));
                    {? 1+fld()='~' || FUN.info('Symbol nie może rozpoczynać się od znaku tyldy (~).'@);0
                    |? fld()=''    || FUN.info('Proszę podać symbol formuły.'@);0
                                   || 1
                    ?}
|? _fld='NAZWA'  || fld(form(fld()));
                    1
|? _fld='TYP'    || {? '#CUP#IN#OUT#CAP#TEX#'*('#'+fld()+'#') || 1 || FUN.info('Proszę wybrać prawidłowy typ.'@); 0 ?}
|? _fld='RULE'   || fld(form(fld()));
                    {? fld()='' || FUN.info('Proszę podać treść/wywołanie formuły.'@);0 || 1 ?}
                 || 0
?}


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Redagowanie formul planu strategicznego
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,
   'OPIS','STRING[15]','Opis',
   'TYP','STRING[3]','Typ'
);
_tab.OPIS:='1. na pojemnik';  _tab.TYP:='CUP'; _tab.add();
_tab.OPIS:='2. po wlaniu';    _tab.TYP:='IN';  _tab.add();
_tab.OPIS:='3. po wylaniu';   _tab.TYP:='OUT'; _tab.add();
_tab.OPIS:='4. na pojemność'; _tab.TYP:='CAP'; _tab.add();
_tab.OPIS:='5. na przepis';   _tab.TYP:='TEX'; _tab.add();

_wer:=_tab.mk_sel('Typ formuł'@,,,'typyformul');
_tab.win_fld(_wer,,'OPIS',,,,,,'Opis'@);
_tab.win_act(_wer,,'Rekord',,,,"
   PX_VAR.RULE_TYP:=cur_tab(1,1).TYP;
   PX_RULES.index('TYP');
   PX_RULES.prefix(PX_VAR.RULE_TYP);
   grp_disp(PX_RULES,'WER');
   ~~
");

_grp:=_tab.grp_make('Formuły planu strategicznego'@);
_tab.grp_sel(_grp,,_wer,,,,,,,,,,'maximized_with_title');
_tab.grp_splt(_grp,,'vertical','px_rules');
_tab.grp_sel(_grp,PX_RULES,'WER',,,,,,,,,,'maximized');
_tab.win_sel(_grp);

_tab.select();

PX_VAR.RULE_TYP:='';
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:00 12723acb25867d038c2d80d35c07cf9280ebe826e14a8c13deb6d2782d011b9923049239e6164eb07da2f3ef12ccbc47f2e400650634f8490541ecc4549e0f26cd4b94e42d02d4b873943787253e94ffc27fb795aa2bf430bb0703e087c25fde94499baa62c34cfdb9b05b082c4134ad0e89ac69cab0d06f9da3f6411e0aa6cc

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obiegi_dekr.fml
:: Utworzony: 21.04.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa obiegów dokumentów - dekretacja
::======================================================================================================================


\dob_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Dekretacja dokumentu w obiegu
::   WE: _a - opcja - dekretacja z TODO
::  OLD: \dob_dek/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_size:=EDOKUM.sel_size();
{? ~_size
|| {? _
   || exec('dob_dek1','obiegi_dekr',_a)
   || exec('dob_dek1','obiegi_dekr')
   ?};
   return(0)
?};
_ksdot:=0;
{? exec('czy_okr_zam_con','konsolidacja',_size=0)
|| return(0)
|? _size | EDOKUM.f_get()
|| {? EDOKUM.r_lock(1,1,1)
   || _zwrot:=1; EDOKUM.TYP();
      DOK.cntx_psh(); VPOZ.cntx_psh();
      {? EDOKUM.TYP().DEKR='N' || _zwrot:=0 ?};
      {? _zwrot & EDOKUM.STDEKRD || _zwrot:=0  ?};
      {? _zwrot
      || EDOKOS.cntx_psh(); EDOKOS.index('SZUK5'); EDOKOS.prefix(EDOKUM.ref(),'D','T');
         _zwrot:=EDOKOS.first();
         EDOKOS.cntx_pop()
      ?};
      {? _zwrot
      || ETYPYFIR.index('UNIK'); ETYPYFIR.prefix(ETYPY.ref(),REF.FIRMA,SSTALE.AR,EDOKUM.ODD);
         _zwrot:=ETYPYFIR.first()
      ?};
      {? _zwrot & SSTALE.AO().POCZ=date(0,0,0) || _zwrot:=0 ?};
      {? _zwrot & SSTALE.AO().ZAM='T' || _zwrot:=0 ?};
      {? _zwrot & exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER) || _zwrot:=0 ?};
      {? _zwrot & (typobi=2 | (typobi=1 & _size)) & (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON) || _zwrot:=0 ?};
      {? _zwrot & typobi=1 & _size &
        (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON) & (EDOKUM.DO<OKRO_F.POCZ | EDOKUM.DO>OKRO_F.KON)
      || _zwrot:=0
      ?};
      {? _zwrot & EDOKUM.KH=null & (typobi=1 | (typobi=2 & ETYPY.CZY_KH)) || _zwrot:=0 ?};
      {? _zwrot || _zwrot:=exec('spr_podz_dek','obiegi_dekr') ?};
      {? _zwrot & typobi=1
      || brutdob:=netdob:=0;
         _zwrot:=exec('spr_pvat','obiegi2',0,_size);
         VAR_DEL.delete('brutdob','netdob')
      ?};
      {? _zwrot &
         ETYPYFIR.DOM_REJ<>null &
         {? typobi=1 & ~ETYPY.TYPVATPR
         || ETYPYFIR.DOM_RVAT<>null &
            {? EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS || ETYPYFIR.DOMGRVAT<>null || 1 ?}
         || 1
         ?} &
         {? EDOKUM.KOREKTA='N' || ETYPYFIR.DOM_RDOK<>null || ETYPYFIR.DOM_RKOR<>null ?}
      || POMOC.AKC:='E'; _addvpoz:=0;
         ETYPYFIR.DOM_RDOK();
         DOK.index('REJ'); DOK.prefix(); DOK.blank();
         DOK.ODD:=EDOKUM.ODD;
         DOK.REJ:=ETYPYFIR.DOM_REJ;
         {? ~ETYPY.TYPVATPR & typobi=1
         || {? EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS || DOK.SUMWAL:=EDOKUM.WART ?};
            DOK.RVAT:=ETYPYFIR.DOM_RVAT;
            {? EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS || VPOZ.GRVAT:=ETYPYFIR.DOMGRVAT ?}
         ?};
         {? EDOKUM.KOREKTA='T'
         || DOK.DOK_REJ:=ETYPYFIR.DOM_RKOR;
            DOK.KOR:=DOK.KOR_ZEW:=EDOKUM.SYM_KOR;
            DOK.DWKOR:=EDOKUM.DWKOR
         || DOK.DOK_REJ:=ETYPYFIR.DOM_RDOK
         ?};
         _edi:=exec('read_edi','obiegi_dekr') & EDI_Z.DOK<>'';
         {? _edi || {? ~DOK.seek(EDI_Z.DOK) || _edi:=0 || EDI_Z.DOK:=DOK.DOKZRODL:='' ?} ?};
         _dodane:=0;
         DOK.EDI:=EDOKUM.EDI;
         {? var_pres('dod_slu')>0
         || DOK.DODSLU:=dod_slu; DOK.DODSLO:=dod_slo
         ?};
         DOK.WYS:=EDOKUM.KH;
         DOK.TR:=EDOKUM.TR;
         {? var_pres('RB',EDOKUM)>0
         || DOK.RB:=EDOKUM.RB
         ?};
         exec('edokum_kh','obiegi_dekr');
         DOK.WAL:={? EDOKUM.WAL=FINFO.NAROD || null || EDOKUM.WAL ?};
         {? typobi=1
         || DOK.NK:=DOK.SYM_ZEW:=EDOKUM.SYM;
            DOK.D4:=EDOKUM.DO;
            DOK.D3:=EDOKUM.TP;
            DOK.DTO:=EDOKUM.DW;
            DOK.NRKSEF:=EDOKUM.NRKSEF
         || DOK.DTO:=EDOKUM.DOP;
            DOK.NK:=DOK.SYM_ZEW:=EDOKUM.ID
         ?};
         DOK.DTW:={? _ksdot=1 || EDOKUM.DO
                  |? _ksdot=2 || WYDRUKIN.DATAOD
                  || EDOKUM.DOP
                  ?};
         DOK.DOP:=EDOKUM.DOP;
         DOK.E_DOC:=EDOKUM.EDOKUM;
         DOK.E_DOCP:=EDOKUM.EPODPIS;
         VAT7.KK:=VAT7.K2:='N';
         {? typobi=1 || DOK.SP_PL:=EDOKUM.PLATNOSC ?};
         {? ~ETYPY.TYPVATPR & typobi=1
         || {? EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS || DOK.SUMWAL:=EDOKUM.WART ?};
            DOK.HAN:=EDOKUM.HAN;
            DOK.KH_FULL:=EDOKUM.KH_FULL;
            DOK.UL:=EDOKUM.UL;
            DOK.MIASTO:=EDOKUM.MIASTO;
            DOK.POCZ:=EDOKUM.POCZ;
            DOK.DOM:=EDOKUM.DOM;
            DOK.LOKAL:=EDOKUM.LOKAL;
            DOK.KPOCZ:=EDOKUM.KPOCZ;
            exec('be_okrvt','dok_fks')
         ?};
         {? typobi=1 & ~ETYPY.TYPVATPR
         || _pargrp:=exec('get','#params',300914,type_of('a'));
            {? 1+_pargrp='T'
            || exec('tagger_tip_nag_of','tagger',EDOKUM.KH,EDOKUM.KOREKTA); exec('be_okrvt','dok_fks')
            |? 1+_pargrp='N'
            || 1
            |? 1+_pargrp='P'
            || _odp:=FUN.ask('Czy ustawić parametry dekretacji na podstawie danych podpowiedzi?'@);
               {? _odp
               || exec('tagger_tip_nag_of','tagger',EDOKUM.KH,EDOKUM.KOREKTA); exec('be_okrvt','dok_fks')
               || 1
               ?}
            ?}
         ?};
         DOK.SP_WYM:=EDOKUM.SP_WYM;
         {? DOK.SP_WYM='T' & 'VAT,SAD'*DOK.DOK_REJ().SLO().KOD=0
         || DOK.SP_WYM:='N'
         ?};
         DOK.BANK:=z_bank; DOK.TYPKRS:=z_typkrs; DOK.TKRS:=z_tkrs; DOK.TRKS_V:=z_tkrs_v;
         {? exec('sprdokks_dob','obiegi_dekr')=''
         || {? ~ETYPY.TYPVATPR
            || DOK.RVAT().RVAT();
               DOK.KRAJ:=exec('kod_pl','slo_slu');
               DOK.JORG:=exec('ust_vwal','kraje',DOK.KRAJ().KOD)
            || DOK.RVAT:=null
            ?};
            DOK.EDOKUM:=$EDOKUM.ref();
            _grvat:=VPOZ.GRVAT; _r:=VPOZ.R; _kk:=VPOZ.KK; _k2:=VPOZ.K2;
            {? DOK.REJ().WAL='N' || DOK.BANK:=DOK.TKRS:=DOK.TRKS_V:=null; DOK.TYPKRS:='' ?};
            DOK.cntx_psh;
            DOK.index('REJ'); DOK.prefix(DOK.REJ);
            _numer:={? DOK.last() || DOK.NR+1 || 1 ?};
            DOK.cntx_pop();
            DOK.JPK_V_T:=exec('typ_dok','obiegi2');
            {!
            |? do();
               {? ~_edi
               || DOK.NR:=_numer
               ?};
               {? {? ~_edi || DOK.add(1) || DOK.put(1) ?}
               || DOK.memo_set(EDOKUM.NRKSEFK,'NRKSEFDK');
                  DOK.memo_put(,'NRKSEFDK');
                  _refdok:=dok_ref:=DOK.ref();
                  POZ.cntx_psh(); POZ.index('DOK'); POZ.prefix(DOK.ref());
                  exec('pop_poz','dok_fks','DOK',0);
                  EDOKUM.REJ:=DOK.REJ;
                  EDOKUM.DOK_KS:=$DOK.ref();
                  EDOKUM.NREJ:=DOK.NR;
                  EDOKUM.DATDOK:=DOK.DTW;
                  {? OBIEGI.EDOKOS
                  || OBIEGI.EDOKOS();
                     EDOKOS.STATUS:='T';
                     EDOKOS.DATA:=date();
                     EDOKOS.TIME:=time();
                     EDOKOS.USERS:=OPERATOR.USER;
                     EDOKOS.cntx_psh(); EDOKOS.prefix();
                     EDOKOS.put();
                     EDOKOS.cntx_pop();
                     {? typobi=1
                     || exec('add_elog','obiegi_akc','Dekretacja faktury',0)
                     || exec('add_elog','obiegi_akc','Dekretacja wniosku',0)
                     ?}
                  ?};
                  EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
                  EDOKUM.cntx_psh();
                  EDOKUM.prefix();
                  {? EDOKUM.put()
                  || EDOKUM.STDEKRD:=exec('zn_deleg','obiegi_dekr',EDOKUM.ref());
                     EDOKUM.put()
                  ?};
                  EDOKUM.cntx_pop();
                  {? (~_edi | (var_pres('edipozfv')>0 & ~edipozfv)) || exec('gen_pozf','obiegi_dekr',_grvat) ?};
                  exec('gen_podz','obiegi_dekr',_refdok);
                  _addvpoz:={? (~_edi | (var_pres('edipozfv')>0 & ~edipozfv)) & (~ETYPY.TYPVATPR &
                               (EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS) &
                               typobi=1)
                            || {? DOK.EDI='N'
                               || exec('gen_vpoz','obiegi_dekr',_refdok,_grvat,_r,_kk,_k2)
                               || POZF.index('DOK'); POZF.prefix(DOK.ref);
                                  exec('pozf2vpoz','dok_fks1')
                               ?}
                            || 0
                            ?};
                  exec('copy_zalacz','obiegi_dekr',EDOKUM.ref(),DOK.ref());
                  {? (typobi=1 | typobi=2) & ETYPYFIR.AUTOKSIE<>null & POMOC.AUT_SCH
                  || exec('oblicz_f','dok_fks',ETYPYFIR.AUTOKSIE().NAZ)
                  ?};
                  {? (ETYPY.TYPVATPR | typobi=2) & (EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS)
                  || EDOKUM.cntx_psh(); EDOKUM.prefix();
                     {? EDOKUM.put()
                     || EDOKUM.NETTONAR:=EDOKUM.NETTO;
                        EDOKUM.put()
                     ?};
                     EDOKUM.cntx_pop()
                  ?};
                  {? var_pres('ile_dek')>0 || ile_dek+=1 ?};
                  _dodane:=1;
                  POZ.cntx_pop(); _dalej:=0
               || _numer+=1; _dalej:=1
               ?};
               _end:=end(); _dalej
            !};
            {? _edi || VAR_DEL.delete('obdekredi','ileautow'); obdekredi:=ileautow:=1 ?};
            {? _edi | _end & _addvpoz || exec('dek_vpoz','obiegi_dekr',_refdok) ?};
            VAR_DEL.delete('obdekredi','ileautow');
            {? ~_dodane & _edi || exec('del_edi_dok','obiegi_dekr',DOK.ref()) ?}
         ?}
      ?};
      EDOKUM.r_unlock();
      DOK.cntx_pop(); VPOZ.cntx_pop()
   ?}
?};
VAR_DEL.delete('SD_OB','edipozfv')


\dob_dek1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Dekretacja dokumentu w obiegu
::   WE: _a - opcja - dekretacja z TODO
::  OLD: \dob_dek/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
_open:=0; _dalej:=0;
rok:=__PARSES.getVal('OkresRok').NAZWA;
VAR_DEL.delete('g_dokkh1','g_dokkh2','g_dokkh3');
g_dokkh1:=obj_new(2); g_dokkh1[1]:=g_dokkh1[2]:=0;
g_dokkh2:=obj_new(2); g_dokkh2[1]:=g_dokkh2[2]:='';
g_dokkh3:=obj_new(2); g_dokkh3[1]:=g_dokkh3[2]:='';
{? _
|| exec('szuk_okro','okresy',EDOKUM.DOP);
   {? ~_a
   || ROK_F.cntx_psh(); OKRO_F.cntx_psh(); OKRO_F.win_sel('WER');
      {? EDOKUM.ODD = null
      || OKRO_F.actions_grayed('WER','W')
      || OKRO_F.actions_grayed('WER','')
      ?};
      OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
      ROZNE.UT_OKROD();
      {? OKRO_F.select(,1)
      || {? OKRO_F.POCZ=date(0,0,0)
         || exec('blad_okr','obiegi_dekr')
         || _ar:=SSTALE.AR; _ao:=SSTALE.AO; _open:=1; _dalej:=1;
            SSTALE.AR:=OKRO_F.ROK; SSTALE.AO:=OKRO_F.ref()
         ?}
      ?};
      ROK_F.cntx_pop(); OKRO_F.cntx_pop()
   || _ar:=SSTALE.AR; _ao:=SSTALE.AO; _open:=1; _dalej:=1;
      SSTALE.AR:=ROZNE.UT_OKROD().ROK; SSTALE.AO:=ROZNE.UT_OKROD
   ?}
|| _dalej:=1
?};
_grvat:=null; _r:=_kk:=_k2:=''; POMOC.AUT_DEK:=0;
_ksdot:=0;
{? ~_dalej
|| return(0)
|? exec('czy_okr_zam_con','konsolidacja',1)
|| return(0)
|? (EDOKUM.f_active() & EDOKUM.f_get()) |
   (~EDOKUM.f_active() & EDOKUM.get())
|| {? EDOKUM.r_lock(1,1,1)
   || {? EDOKUM.ODD=null & ((var_press('zawin')>0 & zawin=8) | var_press('_a')=-1)
      || _tmpTab:=tab_tmp(1,
            'KOD','STRING[8]','Skrót'@,
            'OPIS','STRING[40]','Pełna nazwa'@,
            'REF','INTEGER',
         );
         ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
         {? ODD.first()
         || {!
            |? _tmpTab.KOD:=ODD.OD;
               _tmpTab.OPIS:=ODD.N;
               _tmpTab.REF:=#ODD.ref();
               _tmpTab.add();
               ODD.next()
            !}
         ?};
         _win:=_tmpTab.mk_sel('Jednostki księgowe'@,'P',,'#oddusr',,,,,'U');
         _tmpTab.win_fld(_win,,'KOD');
         _tmpTab.win_fld(_win,,'OPIS');
         _tmpTab.win_act(_win,0,'Formuła','Wybierz'@@,,'Wybór bieżącej pozycji'@,"sel_exit()",,1,,,,'W');
         _tmpTab.win_sel(_win);
         {? _tmpTab.select
         || ODD.seek(_tmpTab.REF,);
            EDOKUM.ODD:=ODD.ref();
            EDOKUM.put()
         ?}
      ?};
      EDOKUM.cntx_psh();
      _zwrot:=1; EDOKUM.TYP();
      dob_dek:=1;
      DOK.cntx_psh(); VPOZ.cntx_psh();
      {? EDOKUM.TYP().DEKR='N'
      || {? typobi=1
         || FUN.info('Faktura nie podlega dekretacji.'@)
         || FUN.info('Wniosek nie podlega dekretacji.'@)
         ?};
         _zwrot:=0
      ?};
      {? _zwrot & EDOKUM.STDEKRD
      || {? typobi=1
         || FUN.info('Faktura jest już po dekretacji.'@)
         || FUN.info('Wniosek jest już po dekretacji.'@)
         ?}; _zwrot:=0
      ?};
      {? _zwrot
      || EDOKOS.cntx_psh(); EDOKOS.index('SZUK5'); EDOKOS.prefix(EDOKUM.ref(),'D','T');
         _zwrot:=EDOKOS.first();
         {? ~_zwrot
         || {? typobi=1
            || FUN.info('Faktura nie jest jeszcze w czynności dekretacji.'@)
            || FUN.info('Wniosek nie jest jeszcze w czynności dekretacji.'@)
            ?}
         ?};
         EDOKOS.cntx_pop()
      ?};
      {? _zwrot
      || ETYPYFIR.index('UNIK'); ETYPYFIR.prefix(ETYPY.ref(),REF.FIRMA,SSTALE.AR,EDOKUM.ODD);
         {? ~ETYPYFIR.first()
         || _zwrot:=0; _rok:=SSTALE.AR().NAZ; _odd:=EDOKUM.ODD().OD;
            FUN.info('Nie znaleziono parametrów dekretacji dla typu %1 roku %2'
                     ' i jednostki księgowej %3.'@[ETYPY.NAZWA,_rok,_odd])
         ?}
      ?};
      {? _zwrot & SSTALE.AO().POCZ=date(0,0,0)
      || exec('blad_okr','obiegi_dekr'); _zwrot:=0
      ?};
      {? _zwrot & SSTALE.AO().ZAM='T'
      || FUN.info('Bieżący okres jest zamknięty.'@); _zwrot:=0
      ?};
      {? _zwrot & exec('is_okr_blck','!zws_par_aokr',SSTALE.AO,OPERATOR.USER) || _zwrot:=0 ?};
      {? _zwrot & typobi=2 & (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON)
      || FUN.info('Data operacji wniosku pochodzi z innego okresu obrachunkowego.'@); _zwrot:=0
      ?};
      {? _zwrot & EDOKUM.KH=null & (typobi=1 | (typobi=2 & ETYPY.CZY_KH))
      || FUN.info('Nie wybrano kontrahenta.'@); _zwrot:=0
      ?};
      {? _zwrot || _zwrot:=exec('spr_podz_dek','obiegi_dekr') ?};
      {? _zwrot & typobi=1
      || brutdob:=netdob:=0;
         _zwrot:=exec('spr_pvat','obiegi2',0,0);
         VAR_DEL.delete('brutdob','netdob')
      ?};
      SSTALE.AO();
      {? _
      || {? EDOKUM.DOP>=OKRO_F.POCZ & EDOKUM.DOP<=OKRO_F.KON
         || _ksdot:=0
         |? EDOKUM.DO>=OKRO_F.POCZ & EDOKUM.DO<=OKRO_F.KON
         || _ksdot:=1
         || _ksdot:=2; WYDRUKIN.DATAOD:=OKRO_F.KON
         ?}
      || {? _zwrot & typobi=1 &
            (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON) &
            (EDOKUM.DO>=OKRO_F.POCZ & EDOKUM.DO<=OKRO_F.KON)
         || _zwrot:=FUN.ask('Data sprzedaży faktury (%2) jest spoza bieżącego okresu (%1).\n'
                            'Data otrzymania faktury (%3) zawiera się w bieżącym okresie.\n'
                            'Zadekretować dokument w okresie zgodnie z datą '
                            'otrzymania faktury?'@[SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ,$EDOKUM.DOP,$EDOKUM.DO]);
            {? _zwrot || _ksdot:=1 ?}
         ?};
         {? _zwrot & typobi=1 & (EDOKUM.DO<OKRO_F.POCZ | EDOKUM.DO>OKRO_F.KON)
         || _zwrot:=FUN.ask('Data otrzymania faktury (%1) jest spoza bieżącego okresu (%2).\n'
                            'Zadekretować dokument w bieżącym okresie?'@[$EDOKUM.DO,SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ])
         ?};
         {? _zwrot & ~_ksdot & typobi=1 &
            (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON) & (EDOKUM.DO<OKRO_F.POCZ | EDOKUM.DO>OKRO_F.KON)
         || WYDRUKIN.DATAOD:=OKRO_F.KON;
            WYDRUKIN.win_edit('DATA'); WYDRUKIN.hdr_edit(); WYDRUKIN.hdr_edit('Data zapisu'@);
            {? WYDRUKIN.edit("{? WYDRUKIN.DATAOD<OKRO_F.POCZ | WYDRUKIN.DATAOD>OKRO_F.KON
                              || FUN.info('Wybrana data nie zawiera się w bieżącym okresie obrachunkowym.'@); 0
                              || 1
                              ?}"
                            )
            || _ksdot:=2
            || _zwrot:=0
            ?};
            WYDRUKIN.hdr_edit()
         ?}
      ?};
      {? _zwrot
      || VAT7.KK:=VAT7.K2:='N';
         POMOC.AKC:='E'; _addvpoz:=0; VPOZ.blank(1);
         DOK.use('doku'+SSTALE.AR().KOD+form(OKRO_F.NR,-2));
         DOK.index('REJ'); DOK.prefix(); DOK.blank();
         DOK.ODD:=EDOKUM.ODD;
         DOK.REJ:=ETYPYFIR.DOM_REJ;
         {? ~ETYPY.TYPVATPR & typobi=1
         || {? EDOKUM.WAL=FINFO.NAROD || DOK.SUMWAL:=EDOKUM.WART ?};
            DOK.RVAT:=ETYPYFIR.DOM_RVAT;
            VPOZ.GRVAT:=ETYPYFIR.DOMGRVAT
         ?};
         {? EDOKUM.KOREKTA='T'
         || DOK.DOK_REJ:=ETYPYFIR.DOM_RKOR;
            DOK.KOR:=DOK.KOR_ZEW:=EDOKUM.SYM_KOR;
            DOK.DWKOR:=EDOKUM.DWKOR
         || DOK.DOK_REJ:=ETYPYFIR.DOM_RDOK
         ?};
         POMOC.DISP_POZ:=EDOKUM.WAL<>FINFO.NAROD & (var_pres('KURS',EDOKUM)<=0 | EDOKUM.KURS=0);
         _edi:=exec('read_edi','obiegi_dekr') & EDI_Z.DOK<>'';
         {? _edi || {? ~DOK.seek(EDI_Z.DOK) || _edi:=0 || EDI_Z.DOK:=''; DOK.r_lock(1,1,1) ?} ?};
         _dodane:=0;
         DOK.EDI:=EDOKUM.EDI;
         {? ~ETYPY.TYPVATPR & typobi=1
         || {? EDOKUM.WAL=FINFO.NAROD || DOK.SUMWAL:=EDOKUM.WART ?};
            DOK.HAN:=EDOKUM.HAN;
            DOK.KH_FULL:=EDOKUM.KH_FULL;
            DOK.UL:=EDOKUM.UL;
            DOK.MIASTO:=EDOKUM.MIASTO;
            DOK.POCZ:=EDOKUM.POCZ;
            DOK.DOM:=EDOKUM.DOM;
            DOK.LOKAL:=EDOKUM.LOKAL;
            DOK.KPOCZ:=EDOKUM.KPOCZ
         ?};
         {? typobi=1 & ~ETYPY.TYPVATPR || exec('tagger_tip_nag_of','tagger',EDOKUM.KH,EDOKUM.KOREKTA) ?};
         DOK.SP_WYM:=EDOKUM.SP_WYM;
         DOK.NRKSEF:=EDOKUM.NRKSEF;
         {? DOK.SP_WYM='T' & 'VAT,SAD'*DOK.DOK_REJ().SLO().KOD=0
         || DOK.SP_WYM:='N'
         ?};
         {? typobi=1 & ~ETYPY.TYPVATPR
         || DOK.win_edit('KSDKOB02');
            POMOC.AUT_DEK:=EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS;
            POMOC.AUT_SCH:=0;
            DOK.efld_opt('KSDKOB02','enable='+$(ETYPYFIR.AUTOKSIE<>null),POMOC,'AUT_SCH')
         || DOK.win_edit('KSDKOB01');
            POMOC.AUT_SCH:=1
         ?};
         {? _edi |
            (var_pres('zoknzedi')<=0 & DOK.edit("exec('sprdokks_do1','obiegi_dekr')"))
         || {? typobi=1 & ~ETYPY.TYPVATPR
            || _grvat:=VPOZ.GRVAT; _r:=VPOZ.R; _kk:=VPOZ.KK; _k2:=VPOZ.K2
            ?};
            {? DOK.DOK_REJ().KOR='T'
            || DOK.KOR_PRZY:=EDOKUM.KOR_PRZY;
               DOK.KOR_OKR:=EDOKUM.KOR_OKR;
               DOK.DWKOR:=EDOKUM.DWKOR
            ?};
            DOK.NRKSEF:=EDOKUM.NRKSEF;
            DOK.memo_set(EDOKUM.NRKSEFK,'NRKSEFDK');
            DOK.WYS:=EDOKUM.KH;
            DOK.TR:=EDOKUM.TR;
            {? var_pres('RB',EDOKUM)>0
            || DOK.RB:=EDOKUM.RB
            ?};
            exec('edokum_kh','obiegi_dekr');
            DOK.WAL:={? EDOKUM.WAL=FINFO.NAROD || null || EDOKUM.WAL ?};
            {? typobi=1
            || DOK.NK:=DOK.SYM_ZEW:=EDOKUM.SYM;
               DOK.D4:=EDOKUM.DO;
               DOK.DTO:=EDOKUM.DW;
               _tp:=EDOKUM.TP;
               {? PAR_SKID.get(38)='N' & _tp<DOK.DTO || _tp:=DOK.DTO ?};
               DOK.D3:=_tp
            || DOK.DTO:=EDOKUM.DOP;
               DOK.NK:=DOK.SYM_ZEW:=EDOKUM.ID
            ?};
            DOK.DTW:={? _ksdot=1 || EDOKUM.DO
                     |? _ksdot=2 || WYDRUKIN.DATAOD
                     || EDOKUM.DOP
                     ?};
            DOK.DOP:=EDOKUM.DOP;
            DOK.E_DOC:=EDOKUM.EDOKUM;
            DOK.E_DOCP:=EDOKUM.EPODPIS;
            {? typobi=1 || DOK.SP_PL:=EDOKUM.PLATNOSC ?};
            DOK.EDOKUM:=$EDOKUM.ref();
            {? DOK.REJ().WAL='N' || DOK.BANK:=DOK.TKRS:=DOK.TRKS_V:=null; DOK.TYPKRS:='' ?};
            DOK.cntx_psh; DOK.index('REJ'); DOK.prefix(DOK.REJ);
            _numer:={? DOK.last() || DOK.NR+1 || 1 ?};
            DOK.cntx_pop();
            {? ~_edi
            || DOK.NR:=_numer
            ?};
            POMOC.V:=DOK.RVAT;
            exec('dok_edit','obiegi_dekr',exec('okna_r','dok_fks',DOK.DOK_REJ().SLO().KOD));
            {? ~ETYPY.TYPVATPR & typobi=1
            || DOK.RVAT().RVAT();
               DOK.KRAJ:={? RVAT.KRAJ=null || exec('kod_pl','slo_slu') || RVAT.KRAJ ?};
               DOK.JORG:=exec('ust_vwal','kraje',DOK.KRAJ().KOD);
               exec('be_okrvt','dok_fks')
            ?};
            DOK.JPK_V_T:=exec('typ_dok','obiegi2');
            {? ~_edi || exec('edit','dok_ksef',0) || exec('edit','dok_ksef',1); DOK.DOKZRODL:='' ?};
            {? _edi || VAR_DEL.delete('obdekredi'); obdekredi:=1 ?};
            {? DOK.edit("exec('dok_spr','dok_fks')")
            || {! |?
                  {? ~_edi
                  || _numer:=DOK.NR; _i:=exec('numer','dok_fks');
                     {? _i=1
                     || FUN.info('Dokument o numerze '+$_numer+' już istnieje. Proszę zmienić numer dokumentu.'@);
                        DOK.edit("exec('dok_spr','dok_fks')")
                     || 0
                     ?}
                  || 0
                  ?}
               !};
               {? _edi || DOK.r_unlock() ?};
               {? DOK.NR<>0
               || {! |?
                     {? {? ~_edi || DOK.add(1) || DOK.put(1) ?}
                     || {? DOK.r_lock(1,1)
                        || DOK.memo_put(,'NRKSEFDK');
                           exec('edit','dok_ksef',2);
                           {? var_pres('zak_dek')>0 || zak_dek:=1 ?};
                           exec('putbloby','dok_fks');
                           _refdok:=dok_ref:=DOK.ref();
                           POZ.cntx_psh(); POZ.index('DOK'); POZ.prefix(DOK.ref());
                           exec('pop_poz','dok_fks','DOK',0);
                           ROZRACH.TABELA:='DOK';
                           {? DOK.DOK_REJ().PR='T' || exec('dokpradd','dok_fks') ?};
                           EDOKUM.REJ:=DOK.REJ;
                           EDOKUM.DOK_KS:=$DOK.ref();
                           EDOKUM.NREJ:=DOK.NR;
                           EDOKUM.DATDOK:=DOK.DTW;
                           {? OBIEGI.EDOKOS
                           || OBIEGI.EDOKOS();
                              EDOKOS.STATUS:='T';
                              EDOKOS.DATA:=date();
                              EDOKOS.TIME:=time();
                              EDOKOS.USERS:=OPERATOR.USER;
                              EDOKOS.cntx_psh(); EDOKOS.prefix();
                              EDOKOS.put();
                              EDOKOS.cntx_pop();
                              {? typobi=1
                              || exec('add_elog','obiegi_akc','Dekretacja faktury',0)
                              || exec('add_elog','obiegi_akc','Dekretacja wniosku',0)
                              ?}
                           ?};
                           EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
                           EDOKUM.prefix();
                           {? EDOKUM.put()
                           || EDOKUM.STDEKRD:=exec('zn_deleg','obiegi_dekr',EDOKUM.ref());
                              EDOKUM.put()
                           ?};
                           {? (~_edi | (var_pres('edipozfv')>0 & ~edipozfv))
                           || exec('gen_pozf','obiegi_dekr',_grvat)
                           ?};
                           exec('gen_podz','obiegi_dekr',_refdok);
                           _addvpoz:={? (~_edi | (var_pres('edipozfv')>0 & ~edipozfv)) & (~ETYPY.TYPVATPR &
                                        (EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS) &
                                        typobi=1)
                                     || {? DOK.EDI='N'
                                        || exec('gen_vpoz','obiegi_dekr',_refdok,_grvat,_r,_kk,_k2)
                                        || POZF.index('DOK'); POZF.prefix(DOK.ref);
                                           exec('pozf2vpoz','dok_fks1')
                                        ?}
                                     || 0
                                     ?};
                           exec('copy_zalacz','obiegi_dekr',EDOKUM.ref(),DOK.ref());
                           {? (typobi=1 | typobi=2) & ETYPYFIR.AUTOKSIE<>null & POMOC.AUT_SCH
                           || exec('oblicz_f','dok_fks',ETYPYFIR.AUTOKSIE().NAZ)
                           ?};
                           {? (ETYPY.TYPVATPR | typobi=2) & EDOKUM.WAL=FINFO.NAROD
                           || EDOKUM.prefix();
                              {? EDOKUM.put()
                              || EDOKUM.NETTONAR:=EDOKUM.NETTO;
                                 EDOKUM.put()
                              ?}
                           ?};
                           POZ.cntx_pop();
                           _dodane:=1;
                           DOK.r_unlock()
                        || FUN.info('Dokument obsługuje inny operator.'@)
                        ?};
                        _dalej:=0
                     || _numer:=DOK.NR; _i:=exec('numer','dok_fks');
                        {? _i=1
                        || FUN.info('Dokument o numerze %1 już istnieje.\nProszę zmienić numer dokumentu.'@[$_numer]);
                           DOK.edit("exec('dok_spr','dok_fks')")
                        ?};
                        _dalej:=1
                     ?};
                     _dalej
                  !}
               ?};
               {? DOK.r_lock(1,1)
               || {? _edi & POMOC.AUT_DEK | _addvpoz & POMOC.AUT_DEK
                  || exec('dek_vpoz','obiegi_dekr',_refdok)
                  ?}
               ?};
               DOK.r_unlock();
               {? POMOC.DISP_POZ || exec('poz_dok','dok_fks') ?}
            || {? _edi || DOK.r_unlock() ?}
            ?};
            VAR_DEL.delete('obdekredi')
         ?};
         {? ~_dodane & _edi || exec('del_edi_dok','obiegi_dekr',DOK.ref()) ?}
      ?};
      DOK.cntx_pop(); VPOZ.cntx_pop();
      VAR_DEL.delete('dob_dek','zoknzedi','edipozfv');
      VAR_DEL.delete('__tagvpztip');
      EDOKUM.cntx_pop(); EDOKUM.f_rfresh();
      EDOKUM.r_unlock()
   || exec('err_kom','obiegi')
   ?}
|| exec('errnodok','obiegi')
?};
{? _open
|| SSTALE.AR:=_ar; SSTALE.AO:=_ao;
   exec('open_tabele','open_tab')
?}


\dek_vpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Dekretacja pozycji VAT
::   WE: _a - ref dokumentu ksiegowego
::  OLD: \dek_vpoz/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
_mask:=ref_name(_a)+4;
VPOZ.cntx_psh(); VPOZ.use('pozv'+_mask); VPOZ.index('VDOK'); VPOZ.prefix(_a);
POZ.cntx_psh(); POZ.use('pozy'+_mask); POZ.index('DOK'); POZ.prefix(_a);
P_V.cntx_psh(); P_V.use('p_v_'+_mask);
P_W.cntx_psh(); P_W.use('p_w_'+_mask);
{? VPOZ.first()
|| DOK.cntx_psh();
   VPOZ.DOK();
   exec('poz_dekv','dok_fks');
   DOK.cntx_pop()
?};
P_W.cntx_pop(); P_V.cntx_pop();POZ.cntx_pop(); VPOZ.cntx_pop()


\sprdokks_do1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Sprawdzanie parametrow przy dekretacji dokumentu w obiegu
::  OLD: \sprdokks/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? DOK.REJ=null
|| FUN.info('Nie wybrano rejestru księgowego.'@); _zwrot:='REJ'
|? DOK.DOK_REJ=null
|| FUN.info('Nie wybrano rodzaju dokumentu.'@); _zwrot:='DOK_REJ'
?};
{? _zwrot=''
|| {? ~EDOKUM.TYP().TYPVATPR & typobi=1
   || {? DOK.RVAT=null || FUN.info('Nie wybrano rejestru VAT.'@); _zwrot:='RVAT' ?};
      {? _zwrot='' & VPOZ.GRVAT=null || FUN.info('Nie wybrano grupy podatkowej.'@); _zwrot:='GRVAT' ?}
   || DOK.RVAT:=null
   ?}
?};
{? _zwrot=''
|| {? EDOKUM.WAL<>FINFO.NAROD & DOK.REJ().WAL='N'
   || {? ~FUN.ask('Wybrany rejestr księgowy nie jest walutowy. Kontynuować dekretację?'@)
      || _zwrot:='REJ'
      ?}
   ?}
?};
_zwrot


\copy_zalacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Funkcja kopiuje zalaczniki podczas dekretowania z EDOKUM do tworzonego DOK
::   WE: _a - wskazanie na ref EDOKUM
::       _b - wskazanie na ref DOK
::  OLD: \copy_zalacz/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_ref_dok:=_b;
EDOKUMZ.cntx_psh(); EDOKUMZ.index('DISP'); EDOKUMZ.prefix(_a);
DOKUM.cntx_psh(); DOKUM.index('DOKUM'); DOKUM.prefix(REF.FIRMA,$_ref_dok);
{? EDOKUMZ.first()
|| POM.TAB:='DOKUM';
   POM.TYPDOK:='SYS';
   exec('add_grnr','numery','SYS');
   {!
   |? DOKUM.blank();
      DOKUM.TYP:='D';
      DOKUM.REFSQL:=DOKUM.DOKR:=$_ref_dok;
      {? DOKUM.add()
      || DOKUM.NR:=exec('numer_new','numery','PACZKA',0,0);
         exec('znak','numery','DOKUM');
         DOKUM.bl_file('DOKUM',1);
         DOKUM.DATA:=EDOKUMZ.DATE;
         DOKUM.KR_OP:=EDOKUMZ.KR_OP;
         DOKUM.CZAS:=EDOKUMZ.TIME;
         DOKUM.OPER:=EDOKUMZ.USER;
         DOKUM.DOKUM:=EDOKUMZ.EDOKUM;
         DOKUM.NAZWA:=EDOKUMZ.bl_info('EDOKUM','NAME');
         {? DOKUM.put() & DOKUM.DOKUM<>null()
         || DOKUMZ.cntx_psh();
            DOKUMZ.index('DOK');
            DOKUMZ.prefix();
            DOKUMZ.blank();
            DOKUMZ.DOK:=DOKUM.ref();
            {? exec('upgrade2325_blbc1','zbl')
            || DOKUMZ.REFSQL:=DOKUM.REFSQL
            ?};
            DOKUMZ.DOKUM:=DOKUM.DOKUM;
            DOKUMZ.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
            DOKUMZ.add();
            DOKUMZ.cntx_pop()
         ?}
      ?};
      EDOKUMZ.next()
   !}
?};
DOKUM.cntx_pop();
EDOKUMZ.cntx_pop()


\gen_vpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Tworzenie pozycji VAT w dokumencie ksiegowym
::   WE: _a - ref dokumentu ksiegowego
::       _b - ref grupy podatkowej
::       _c - symbol konta rodzaj
::       _d - symbol konta 1
::       _e - symbol konta 2
::   WY: 1/0 - czy dodano pozycje VAT do dokumentu
::  OLD: \gen_vpoz/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
VPOZ.cntx_psh(); VPOZ.use('pozv'+(ref_name(_a)+4)); VPOZ.index('VDOK'); VPOZ.prefix();
EVAT.cntx_psh(); EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
{? EVAT.first()
|| {!
   |? VPOZ.blank(1);
      VPOZ.DOK:=_a;
      VPOZ.PR:=EVAT.PR;
      VPOZ.GRVAT:=_b;
      VPOZ.R:=_c;
      VPOZ.KK:=_d;
      VPOZ.K2:=_e;
      VPOZ.OKRVAT:=DOK.OKRVAT;
      VPOZ.A_VAT:=DOK.A_VAT;
      VPOZ.RVAT:=DOK.RVAT;
      {? EDOKUM.WAL<>FINFO.NAROD & var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS
      || VPOZ.KURS:=EDOKUM.KURS;
         _czy_brutto:=exec('czy_brutto','dok_fks');
         {? _czy_brutto
         || VPOZ.WARW:=EVAT.BRUTTO
         || VPOZ.WARW:=EVAT.NETTO
         ?};
         {? var_pres('NETTO_W',VPOZ)>0
         || VPOZ.NETTO_W:=EVAT.NETTO;
            VPOZ.VAT_W:=EVAT.VAT;
            VPOZ.BRUTTO_W:=EVAT.BRUTTO
         ?};
         exec('po_vwarw','dok_fks1',0);
         {? _czy_brutto
         || VPOZ.NETTO:=(VPOZ.BRUTTO/(1+0.01*exec('vat','dok_fks',VPOZ.PR().KOD)))$2;
            VPOZ.VAT:=(VPOZ.BRUTTO-VPOZ.NETTO)$2
         || VPOZ.VAT:=(0.01*VPOZ.NETTO*exec('vat','dok_fks',VPOZ.PR().KOD))$2;
            VPOZ.BRUTTO:=(VPOZ.NETTO+VPOZ.VAT)$2
         ?}
      || VPOZ.NETTO:=EVAT.NETTO;
         VPOZ.BRUTTO:=EVAT.BRUTTO;
         VPOZ.VAT:=EVAT.VAT
      ?};
      exec('setvatodl','dok_fks');
      VPOZ.SP_WYM:={? EDOKUM.SP_WYM='T' || 'W' || 'T' ?};
      VPOZ.add();
      EVAT.next()
   !}
?};
VPOZ.index('VDOK'); VPOZ.prefix(_a);
_zwrot:=VPOZ.first();
VPOZ.cntx_pop(); EVAT.cntx_pop();
_zwrot


\gen_podz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Tworzenie podzialow w dokumencie ksiegowym
::   WE: _a - ref dokumentu ksiegowego
::  OLD: \gen_podz/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUM.TYP().ED_PODZ='T'
|| SKIDXD.cntx_psh(); SKIDXD.use('skxd'+(ref_name(_a)+4)); SKIDXD.index('DOK'); SKIDXD.prefix();
   EPODZ.cntx_psh(); EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
   {? EPODZ.first()
   || SKIDXDUD.cntx_psh(); SKIDXDUD.index('POZ');
      {! |?
        SKIDXDUD.prefix(EPODZ.PBUD);
        _il:={? SKIDXDUD.first()
             || SKIDXDUD.WART_IL='I'
             || 0
             ?};
        {? EPODZ.SKID_MB<>null &
           ((~_il &
             (EPODZ.WAL=FINFO.NAROD |
              (EPODZ.WAL<>FINFO.NAROD & (ROZNE.KRS:=exec('ust_krs','dok_fks',EPODZ.WAL); ROZNE.KRS$6<>0)
             )
            )
           ) |
           _il)
        || SKIDXD.blank(1);
           SKIDXD.SKID_MB:=EPODZ.SKID_MB;
           SKIDXD.DOK:=_a;
           SKIDXD.JORG:=EPODZ.JORG;
           SKIDXD.OKOSZ:=EPODZ.OKOSZ;
           SKIDXD.PBUD:=EPODZ.PBUD;
           SKIDXD.WYM4:=EPODZ.WYM4;
           SKIDXD.WYM5:=EPODZ.WYM5;
           SKIDXD.WAL:=EPODZ.WAL;
           SKIDXD.JM:=EPODZ.JM;
           {? EPODZ.WAL=FINFO.NAROD | _il
           || SKIDXD.WART:=EPODZ.WART
           || SKIDXD.WARTW:=EPODZ.WART;
              _jedn:=exec('jed_wal','waluty',EPODZ.WAL().KOD);
              SKIDXD.WART:=(SKIDXD.WARTW*ROZNE.KRS/_jedn)$2;
              SKIDXD.KURS:=ROZNE.KRS
           ?};
           SKIDXD.GEN_AUT:='N';
           SKIDXD.add()
        ?};
        EPODZ.next()
     !};
     SKIDXDUD.cntx_pop()
   ?};
   SKIDXD.cntx_pop(); EPODZ.cntx_pop()
?}


\zn_deleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Ustawia znacznik zadekretowania dla delegacji
::   WE: _a - ref delegacji
::   WY: znacznik zadekretowania delegacji
::  OLD: \zn_deleg/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_dekret:=0;
_tmprej:=EDOKUM.REJ;
EDOKUM.cntx_psh(); EDOKUM.index('ID'); EDOKUM.prefix();
{? _a<>null & EDOKUM.seek(_a) & _tmprej
|| _dekret:=1
?};
EDOKUM.cntx_pop();
_dekret


\edokum_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Uzupelnianie pol dotyczacych kontrahenta
::  OLD: \edokum_kh/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
RS.cntx_psh(); RS.index('RS'); RS.prefix();
{? EDOKUM.KH<>null & RS.find_key(DOK.WYS().SLU().WZ) & RS.TAB='KH'
|| KH.cntx_psh(); KH.index('KOD'); KH.prefix(2);
   {? KH.find_key(SLO.KOD)
   || DOK.KH:=KH.SKR;
      DOK.NIP:={? KH.TYP='R'
               || {? KH.NIP<>'' || KH.NIP || KH.PESEL ?}
               || {? 1+DOK.RVAT().RVAT().KVAT().SYM<>'_'
                  || KH.NIP
                  || exec('jak_nue','kontrahent')
                  ?}
               ?}
   ?};
   KH.cntx_pop()
?};
RS.cntx_pop()


\spr_podz_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [$1210]
:: OPIS: Sprawdza wprowadzone podzialy dla dokumentu podczas dekretacji
::  OLD: \spr_podz_dek/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(80)='T' & (typobi=1 | (typobi=2 & EDOKUM.TYP().CZY_WAR>=2))
|| {? EDOKUM.TYP().ED_PODZ='T'
   || exec('spr_podz1','obiegi2',~EDOKUM.sel_size())
   || 1
   ?}
|| 1
?}


\blad_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Bledny okres do dekretacji - komunikat
::  OLD: \blad_okr/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? typobi=1
|| FUN.info('Faktury w obiegu nie są dekretowane w okresach\n'+
            '"Bilans otwarcia" i "Bilans zamknięcia".'@)
|? typobi=2
|| FUN.info('Wnioski nie są dekretowane w okresach\n'+
            '"Bilans otwarcia" i "Bilans zamknięcia".\n'@)
?}


\sprdokks_dob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Sprawdzanie parametrow przy dekretacji dokumentu w obiegu
::  OLD: \sprdokks/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=''; _size:=EDOKUM.sel_size();
{? DOK.REJ=null
|| {? ~_size || FUN.info('Nie wybrano rejestru księgowego.'@) ?};
   _zwrot:='REJ'
?};
{? _zwrot='' & EDOKUM.WAL<>FINFO.NAROD & DOK.REJ().WAL='N'
|| {? ~_size
   || {? ~FUN.ask('Wybrany rejestr księgowy nie jest walutowy. Kontynuować dekretację?'@)
      || _zwrot:='REJ'
      ?}
   || _zwrot:='REJ'
   ?}
?};
{? _zwrot='' & DOK.DOK_REJ=null
|| {? ~_size || FUN.info('Nie wybrano rodzaju dokumentu.'@) ?}; _zwrot:='DOK_REJ'
?};
{? _zwrot=''
|| {? ~EDOKUM.TYP().TYPVATPR & typobi=1
   || {? -DOK.DOK_REJ().SLO().KOD<>'vat' & -DOK.DOK_REJ().SLO().KOD<>'sad'
      || {? ~_size || FUN.info('Wybrany rodzaj dokumentu musi być typu "Dokument VAT" lub "Dokument SAD".'@) ?};
         _zwrot:='DOK_REJ'
      |? -EDOKUM.KOREKTA='t' & -DOK_REJ.KOR<>'t'
      || {? ~_size
         || FUN.info('Wybrany rodzaj dokumentu nie umożliwia\nrejestracji dokumentów korygujących.'@)
         ?};
         _zwrot:='DOK_REJ'
      ?}
   || {? -DOK.DOK_REJ().SLO().KOD<>'prosty'
      || {? ~_size || FUN.info('Wybrany rodzaj dokumentu musi być typu "Dokument prosty".'@) ?};
          _zwrot:='DOK_REJ'
      ?}
   ?}
?};
{? _zwrot='' & DOK_REJ.PR='T'
|| {? ~_size
   || FUN.info('Wybrany rodzaj dokumentu wykorzystywany jest do rejestracji dokumentów ratalnych.'@)
   ?}; _zwrot:='DOK_REJ'
?};
{? _zwrot='' & ~EDOKUM.TYP().TYPVATPR & typobi=1
|| {? DOK.RVAT=null
   || {? ~_size || FUN.info('Nie wybrano rejestru VAT.'@) ?}; _zwrot:='RVAT'
   ?};
   {? _zwrot='' & EDOKUM.WAL=FINFO.NAROD & VPOZ.GRVAT=null
   || {? ~_size || FUN.info('Nie wybrano grupy VAT.'@) ?}; _zwrot:='GRVAT'
   ?}
?};
{? EDOKUM.WAL<>FINFO.NAROD & EDOKUM.WAL<>null
|| {? _zwrot='' & DOK.BANK=null
   || {? ~_size || FUN.info('Nie wybrano banku.'@) ?}; _zwrot:='BANK'
   ?};
   {? _zwrot='' & DOK.TYPKRS=''
   || {? ~_size || FUN.info('Nie wybrano typu kursu.'@) ?}; _zwrot:='TYPKRS'
   ?};
   {? _zwrot='' & DOK.TKRS=null
   || {? ~_size || FUN.info('Nie wybrano tabeli kursów dla dekretów.'@) ?}; _zwrot:='TKRS'
   ?};
   {? _zwrot=''
   || ROZNE.KRS:=exec('ust_krs','dok_fks',EDOKUM.WAL);
      {? ROZNE.KRS$6=0
      || {? ~_size
         || FUN.info('W wybranej dla dekretów tabeli kursów nie ma kursu dla waluty %1.'@[EDOKUM.WAL().KOD])
         ?}; _zwrot:='TKRS'
      ?}
   ?}
?};
_zwrot


\bedobdek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Grupa przed - dekretacja dokumentow w obiegu
::  OLD: \bedobdek/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
_co:={? typobi=1 || 'faktury' || 'wnioski' ?};
{? SSTALE.AO().POCZ=date(0,0,0)
|| exec('blad_okr','obiegi_dekr'); 0
|| {? (typobi=1 & FUN.ask('Zadekretować zaznaczone faktury?'@)) |
      (typobi=2 & FUN.ask('Zadekretować zaznaczone wnioski?'@))
   || DOK.win_edit('KSDOKOBG');
      DOK.blank(1); VPOZ.blank(1);
      {? DOK.edit()
      || ile_zazn:=EDOKUM.sel_size();
         z_bank:=DOK.BANK; z_typkrs:=DOK.TYPKRS; z_tkrs:=DOK.TKRS; z_tkrs_v:=DOK.TRKS_V;
         ile_dek:=0; dob_dek:=1; dod_slu:=DOK.DODSLU; dod_slo:=DOK.DODSLO; _zwrot:=1
      ?};
      VAR_DEL.delete('__tagvpztip')
   ?}
?}; _zwrot


\aedobdek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Grupa po - dekretacja dokumentow w obiegu
::  OLD: \aedobdek/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_dl:=(+$ile_zazn);
{? typobi=1
|| FUN.info(form(form('Faktur zaznaczonych: ',28)+$ile_zazn,28+_dl)+'\n'+
            form(form('Faktur zadekretowanych: ',28)+$ile_dek,28+_dl))
|? typobi=2
|| FUN.info(form(form('Wniosków zaznaczonych: ',26)+$ile_zazn,26+_dl)+'\n'+
            form(form('Wniosków zadekretowanych: ',26)+$ile_dek,26+_dl))
?};
EDOKUM.f_rfresh();
VAR_DEL.delete('ile_zazn','ile_dek','z_bank','z_typkrs','z_tkrs','z_tkrs_v','dob_dek','dod_slu','dod_slo')


\aut_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.41]
:: OPIS: Przed redakcja i na wyswietl dla pola POMOC.AUT_DEK
::  OLD: \aut_dek/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS


\dok_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tworzy okno redagowania dokumentu księgowego
::   WE: _a - bazowe okno redagowania
::----------------------------------------------------------------------------------------------------------------------
{? _a<>''
|| _okn:=DOK.mk_edit('Dokument księgowy'@);
   DOK.win_etab(_okn,'Dane podstawowe'@);
   DOK.win_ewin(_okn,,_a);
   {? ~DOK.RVAT || DOK.RVAT:=exec('new_vat','!fks_dks_dark') ?};
   {? DOK.RVAT
   || VAR_DEL.delete('TAB2POLA','TAB2FAKSPOLA');
      TAB2POLA:=DOKPOLA;
      TAB2FAKSPOLA:=DOK;
      DOK.win_etab(_okn,'Dane dodatkowe'@);
      DOK.win_ewin(_okn,DOKPOLA,'RED_DOD')
   ?};
   {? _a<>'DOK_PRO' & _a<>'DOK_STAT'
   || DOK.win_etab(_okn,'Kontrahent'@);
      {? DOK.RVAT().RVAT().GRVAT().KOD='Sprzedaż'
      || {? POMOC.D().KOR_NAG='T' | DOK.DOK_REJ().KOR_NAG='T'
         || DOK.win_ewin(_okn,,'KH_KORN')
         || DOK.win_ewin(_okn,,'KH_ODB')
         ?}
      || DOK.win_ewin(_okn,,'KH_WYST')
      ?}
   ?};
   {? DOK.RVAT
   || DOK.win_etab(_okn,'Dane e-dokumentu'@);
      DOK.win_ewin(_okn,DOKUM,'EDOKUMEN')
   ?};
   DOK.win_etab(_okn,'Status'@);
   DOK.win_ewin(_okn,,'DOK_STAT');
   btn_idp[4]:=DOK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Nr dok. &mag.'@],"exec('dokpola_memo','dok_ksef',DOKPOLA,'WZ')");
   DOK.btn_eopt(_okn,btn_idp[4],'tooltip='+'Numery dokumentów magazynowych'@);
   btn_idp[5]:=DOK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Nr fak. za&l.'@],"exec('dokpola_memo','dok_ksef',DOKPOLA,'NRFAZAL')");
   DOK.btn_eopt(_okn,btn_idp[5],'tooltip='+'Numery faktur zaliczkowych'@);
   btn_idp[6]:=DOK.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Nr fak. &kor.'@],"exec('dokpola_memo','dok_ksef',DOK,'NRKSEFDK')");
   DOK.btn_eopt(_okn,btn_idp[6],'tooltip='+'Numery KSeF faktur korygowanych'@);
   DOK.win_ebtn(_okn,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Dekretuj'@],"'key:F2'");
   _anuluj:=DOK.win_ebtn(_okn,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
   DOK.btn_eopt(_okn,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   DOK.win_edit(_okn);
   exec('wal_edi_dok','dok_fks',_okn)
?};
1


\ustaw_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów wyjściowych
::   WE: _a - obiekt wyjsciowy
::       _b - 'D' - dekretacja
::            'O' - odrzucenie
::       _c - ref dokumentu księgowego
::       _d - ref dokumentu w obiegu
::----------------------------------------------------------------------------------------------------------------------
_a.STATUS:=_b;
_a.DOK:=_c;
_a.EDOKUM:=_d;
{? OBIEGI.EDOKOS
|| OBIEGI.EDOKOS();
   {? exec('szuk_edokpar','obiegi')
   || _a.B_PREL:=EDOKPAR.B_PREL;
      _a.B_PRELS:=EDOKPAR.B_PRELS
   ?}
?};
exec('null_bprel','obiegi_akc')


\dok_dob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Wyswietlanie / wydruk dokumentu w obiegu z poziomu dokumentu ksiegowego
::  OLD: \dok_dob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK.EDOKUM<>''
|| _ref:=BB.sqlint(DOK.EDOKUM); _nam:=form(DOK.EDOKUM-8);
   {? _ref<>0 & _nam<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.index('ID'); EDOKUM.prefix();
      TYPOBIEG.cntx_psh(); TYPOBIEG.prefix();
      {? EDOKUM.seek(_ref,_nam)
      || EDOKUM.TYPOBIEG();
         {? TYPOBIEG.NAZWA='Obieg faktur'
         || _okn:=EDOKUM.mk_edit('Faktura w obiegu'@);
            EDOKUM.win_ewin(_okn,,'WPR'); typobi:=1
         |? TYPOBIEG.NAZWA='Obieg wniosków'
         || _okn:=EDOKUM.mk_edit('Wniosek w obiegu'@);
            EDOKUM.win_ewin(_okn,,'KSW'); typobi:=2
         || _okn:=EDOKUM.mk_edit('Delegacja w obiegu'@);
            EDOKUM.win_ewin(_okn,,'KSD'); typobi:=3
         ?};
         EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Historia obiegu'@],
                      "exec('okn_opis','obiegi'); ''"
                      );
         EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje VAT'@],
                         "exec('pozvdob','obiegi_log'); ''"
                         );
         EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['P&odziały'@],
                         "exec('podz_dob','obiegi_log'); ''"
                         );
         EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Pozycje &faktury'@],
                         "exec('pozf_edokum','obiegi_log'); ''"
                         );
         _anuluj:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],'key:Esc');
         EDOKUM.btn_eopt(_okn,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
         EDOKUM.win_edit(_okn);
         EDOKUM.display()
      || FUN.info('Nie znaleziono powiązanego dokumentu.'@)
      ?};
      EDOKUM.cntx_pop(); TYPOBIEG.cntx_pop()
   || FUN.info('Nie znaleziono powiązanego dokumentu.'@)
   ?}
|| FUN.info('Wybrany dokument księgowy nie jest powiązany z dokumentem w obiegu.'@)
?}


\odrzuc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Odrzucanie dokumentu w obiegu przy dekretacji
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
_we:=_args.in;
_wy:=_args.out;
{? EDOKUM.sel_size() || return(0) ?};
EDOKUM.cntx_psh();
EDOKOS.cntx_psh(); EDOKOS.prefix();
_refb:=EDOKUM.ref();
_refn:=null;
{? EDOKUM.f_active()
|| EDOKUM.f_rfresh();
   {? EDOKUM.f_next()
   || _refn:=EDOKUM.ref()
   || {? EDOKUM.f_prev()
      || _refn:=EDOKUM.ref()
      ?}
   ?};
   EDOKUM.f_seek(_refb)
?};
_ask:={? typobi=1
      || 'Odrzucić fakturę w obiegu?'
      |? typobi=2
      || 'Odrzucić wniosek?'
      |? typobi=3
      || 'Odrzucić delegację?'
      ?};
{? {? EDOKUM.f_active() || EDOKUM.f_get() || EDOKUM.get() ?}
|| {? EDOKUM.r_lock(1,1,1)
   || {? _mp.pathTodo() | FUN.ask(_ask)
      || _zwrot:=1;
         OBIEGI.EDOKOS();
         {? _zwrot & EDOKUM.ZAM='T'
         || {? typobi=1
            || FUN.info('Faktura zamknięta.'@)
            |? typobi=2
            || FUN.info('Wniosek zamknięty.'@)
            |? typobi=3
            || FUN.info('Delegacja zamknięta.'@)
            ?};
            _zwrot:=0
         ?};
         {? var_pres('B_PRELS',_we)>0 & _we.B_PRELS<>~~
         || OBIEGI.POPETAP:=_we.B_PRELS
         ?};
         EDOKOS.win_edit('AKCO');
         {? _zwrot & exec('szuk_edokpar','obiegi') & EDOKOS.edit() & EDOKOS.put() & EDOKOS.memo_put(,'UW_DL')
         || EDOKUM.cntx_psh(); EDOKUM.prefix(); _reftag:=null;
            do();
            exec('generuj_od_dok','obiegi_dekr');
            {? EDOKUM.put()
            || {? var_pres('zak_dek')>0 || zak_dek:=2 ?}
            || undo()
            ?};
            _reftag:=EDOKUM.ref();
:: dodawanie zapisu w tabeli logowan
            _log:={? typobi=1
                  || 'Odrzucenie faktury'
                  |? typobi=2
                  || 'Odrzucenie wniosku'
                  |? typobi=3
                  || 'Odrzucenie delegacji'
                  || ''
                  ?};
            EDOKOS.prefix();
            exec('add_elog','obiegi_akc',_log,1);
            end();
            {? _reftag<>null & typobi=1
            || exec('tagagr_csh_del','tagger',_reftag)
            ?};
            EDOKUM.cntx_pop();
            {? EDOKUM.f_active()
            || EDOKUM.f_rfresh();
               {? ~EDOKUM.f_seek(_refb)
               || {? _refn<>null || EDOKUM.f_seek(_refn) ?}
               ?}
            ?}
         ?}
      ?};
      unlock_r()
   || exec('err_kom','obiegi')
   ?}
|| exec('errnodok','obiegi')
?};
EDOKOS.cntx_pop(); EDOKUM.cntx_pop()


\generuj_od_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamykanie zapisu na bieżącym poziomie przy odrzucaniu dokumentu w procesie odrzucania dokumentu
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.EDOKOS();
EDOKOS.STATUS:='O';
EDOKOS.WID:='N';
EDOKOS.DATA:=date(0,0,0);
EDOKOS.TIME:=time(0,0,0);
EDOKOS.put();
exec('szuk_edokpar','obiegi')


\bedobdekdel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Grupa przed - dekretacja delegacji
::  OLD: \bedobdekdel/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? FUN.ask('Zadekretować zaznaczone delegacje?'@)
|| _zwrot:=exec('okienko_deleg','obiegi_dekr');
   {? _zwrot
   || exec('kom_del_ini','obiegi_dekr',1);
      ile_zazn:=EDOKUM.sel_size(); ile_dek:=0
   ?}
?};
_zwrot


\okienko_deleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Okienko do wprowadzania parametrow dekretacji delegacji
::  OLD: \okienko_deleg/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
DOK.blank(1); VPOZ.blank(1);
POMOC.AKC:='E';
VAT7.KK:=VAT7.K2:='N';
DOK.BANK:=XINFO.BDEKRDEL;
DOK.TYPKRS:=XINFO.KURSTYPD;
{? OPERATOR.DEPT<>null() & EDOKUM.ODD=null() || EDOKUM.ODD:=OPERATOR.DEPT ?};
{? EDOKUM.sel_size()
|| DOK.win_edit('KSDELEG1')
|| DOK.DTO:=EDOKUM.DOP;
   DOK.DOP:=EDOKUM.DOP;
   exec('dok_kurs','dok_fks');
   exec('be_d_tkv','dok_fks');
   DOK.win_edit('KSDELEG')
?};
{? DOK.edit("
   _ret:=__CHK.record(EDOKUM,,'ODD');
   {? _ret='' & ~EDOKUM.sel_size()
   || ETYPYFIR.index('UNIK'); ETYPYFIR.prefix(EDOKUM.TYP,REF.FIRMA,SSTALE.AR,EDOKUM.ODD);
      _zwrot:=ETYPYFIR.first();
      {? ~_zwrot
      || FUN.info('Nie zdefiniowano parametrów dekretacji dla typu %1'
                  '\nroku %2 i jednostki księgowej %3'@[ETYPY.NAZWA,SSTALE.AR().NAZ,EDOKUM.ODD().OD]);

         _ret:='ODD'
      ?}
   ?};
   _ret
")
||
   ddodslu:=DOK.DODSLU;
   ddodslo:=DOK.DODSLO;
   dbank:=DOK.BANK;
   dtypkrs:=DOK.TYPKRS;
   dtkrs:=DOK.TKRS;
   dtkrs_v:=DOK.TRKS_V;
   dr:=VPOZ.R;
   dkk:=VPOZ.KK;
   dk2:=VPOZ.K2;
   dodd:=EDOKUM.ODD;
   {? ~EDOKUM.sel_size()
   || EDOKUM.cntx_psh();
      EDOKUM.use(8+$edokum_ref);
      EDOKUM.prefix();
      EDOKUM.seek(edokum_ref);
      EDOKUM.ODD:=dodd;
      EDOKUM.put();
      EDOKUM.cntx_pop()
   ?};
   _zwrot:=1
?};
_zwrot


\deleg_zm_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Kasowanie zmiennych - dekretacja delegacji
::  OLD: \deleg_zm_del/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('KOM_DEL','komdind1','komdind2');
VAR_DEL.delete('ddodslu','ddodslo','dbank','dtypkrs','dtkrs','dtkrs_v','dr','dkk','dk2','edokum_ref')


\aedobdekdel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Grupa po - dekretacja delegacji
::  OLD: \aedobdekdel/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_dl:=(+$ile_zazn);
FUN.info(form(form('Delegacji zaznaczonych: ',28)+$ile_zazn,28+_dl)+'\n'+
         form(form('Dokumentów utworzonych: ',28)+$ile_dek,28+_dl));
KOM_DEL.index(komdind1); KOM_DEL.prefix();
{? KOM_DEL.first() || KOM_DEL.select() ?};
exec('deleg_zm_del','obiegi_dekr');
VAR_DEL.delete('ile_zazn','ile_dek');
EDOKUM.f_rfresh()


\dob_dek_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Dekretacja dokumentu w obiegu - delegacje
::   WE: _a - 0/1 - dekretacja z TODO
::  OLD: \dob_dek_del/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_size:=EDOKUM.sel_size(); POMOC.DISP_POZ:=0; _open:=0;
{? _size |
   ((EDOKUM.f_active() & EDOKUM.f_get()) |
    (~EDOKUM.f_active() & EDOKUM.get()))
|| {? EDOKUM.r_lock(1,1,1)
   || _zwrot:=1; EDOKUM.TYP();
      {? ~_size || dob_dek:=1 ?};
      DOK.cntx_psh(); VPOZ.cntx_psh();
      {? EDOKUM.TYP().DEKR='N'
      || {? ~_size || FUN.info('Delegacja nie podlega dekretacji.'@) ?}; _zwrot:=0
      ?};
      {? _zwrot & EDOKUM.STDEKRD
      || {? ~_size || FUN.info('Delegacja jest zadekretowana.'@) ?}; _zwrot:=0
      ?};
      {? _zwrot
      || EDOKOS.cntx_psh(); EDOKOS.index('SZUK5'); EDOKOS.prefix(EDOKUM.ref(),'D','T');
         {? ~EDOKOS.first() || FUN.info('Delegacja nie jest jeszcze w czynności dekretacji.'@); _zwrot:=0 ?};
         EDOKOS.cntx_pop()
      ?};
      {? _zwrot & _size
      || ETYPYFIR.index('UNIK'); ETYPYFIR.prefix(EDOKUM.TYP,REF.FIRMA,SSTALE.AR,{? var_pres('dodd')>0 || dodd || EDOKUM.ODD ?});
         _zwrot:=ETYPYFIR.first()
      ?};
:: data rozliczenia, jezeli spoza biezacego okresu to decyzja uzytkownika
      {? _zwrot
      || {? ~_a
         || SSTALE.AO();
            {? (EDOKUM.DOP<OKRO_F.POCZ | EDOKUM.DOP>OKRO_F.KON) & (EDOKUM.DO<OKRO_F.POCZ | EDOKUM.DO>OKRO_F.KON)
            || {? ~_size
               || {? FUN.ask('Data rozliczenia delegacji (%1) jest spoza bieżącego  okresu rozliczeniowego'
                            '\n(%2 - %3). Kontynuować?'@[EDOKUM.DOP$3,OKRO_F.POCZ$3,OKRO_F.KON$3])
                  || WYDRUKIN.DATAOD:=OKRO_F.KON;
                     WYDRUKIN.win_edit('DATA'); WYDRUKIN.hdr_edit(); WYDRUKIN.hdr_edit('Data rozliczenia do dekretu'@);
                     {? ~WYDRUKIN.edit("{? WYDRUKIN.DATAOD<OKRO_F.POCZ | WYDRUKIN.DATAOD>OKRO_F.KON
                                        || FUN.info('Wybrana data nie zawiera się w bieżącym okresie obrachunkowym.'@); 0
                                        || 1
                                        ?}"
                                       )
                     || _zwrot:=0
                     ?};
                     WYDRUKIN.hdr_edit()
                  || _zwrot:=0
                  ?}
               || _zwrot:=0
               ?}
            || WYDRUKIN.DATAOD:=EDOKUM.DOP
            ?}
         || exec('szuk_okr','okresy',EDOKUM.DOP);
            {? ROZNE.UT_OKROD=null
            || FUN.info('Nie znaleziono w systemie okresu obrachunkowego dla wprowadzonej w dokumencie '
                        'daty rozliczenia delegacji.'@); _zwrot:=0
            || _ar:=SSTALE.AR; _ao:=SSTALE.AO; _open:=1;
               SSTALE.AR:=ROZNE.UT_OKROD().ROK; SSTALE.AO:=ROZNE.UT_OKROD;
               WYDRUKIN.DATAOD:=EDOKUM.DOP;
               exec('open_tabele','open_tab')
            ?}
         ?}
      ?};
:: data
      {? _zwrot
      || {? ~_size || exec('kom_del_ini','obiegi_dekr',_size) ?};
         exec('spr_deleg','obiegi_dekr',WYDRUKIN.DATAOD);
         KOM_DEL.index(komdind2); KOM_DEL.prefix(EDOKUM.ID,EDOKUM.ID);
         {? KOM_DEL.first()
         || _zwrot:=0;
            {? ~_size || KOM_DEL.select() ?}
         ?}
      ?};
      {? _zwrot
      || {? _size
         || DOK.BANK:=dbank;
            DOK.TYPKRS:=dtypkrs;
            DOK.TKRS:=DOK.TRKS_V:=null;
            DOK.DTO:=EDOKUM.DOP;
            DOK.DOP:=EDOKUM.DOP;
            exec('dok_kurs','dok_fks');
            exec('be_d_tkv','dok_fks');
            dtkrs:=DOK.TKRS;
            dtkrs_v:=DOK.TRKS_V
         || _zwrot:=exec('okienko_deleg','obiegi_dekr')
         ?};
         {? _zwrot
         || op_del:='Delegacja: '+EDOKUM.ID+' miejsce: '+EDOKUM.DEL_MIE;
            exec('dob_dek_del1','obiegi_dekr',WYDRUKIN.DATAOD);
            EDOKUM.STDEKRD:=exec('zn_deleg','obiegi_dekr',EDOKUM.ref());
            {? OBIEGI.EDOKOS & EDOKUM.STDEKRD
            || OBIEGI.EDOKOS();
               EDOKOS.STATUS:='T';
               EDOKOS.DATA:=date();
               EDOKOS.TIME:=time();
               EDOKOS.USERS:=OPERATOR.USER;
               EDOKOS.cntx_psh(); EDOKOS.prefix();
               EDOKOS.put();
               EDOKOS.cntx_pop()
            ?};
            {? EDOKUM.STDEKRD || exec('add_elog','obiegi_akc','Dekretacja delegacji',0) ?};
            EDOKUM.put();
            VAR_DEL.delete('op_del')
         ?}
      ?};
      EDOKUM.r_unlock();
      DOK.cntx_pop(); VPOZ.cntx_pop();
      {? ~_size
      || VAR_DEL.delete('dob_dek');
         {? EDOKUM.f_active() || EDOKUM.f_rfresh() ?}
      ?}
   |? ~_size
   || exec('err_kom','obiegi')
   ?}
|? ~_size
|| exec('errnodok','obiegi')
?};
{? ~_size
|| exec('deleg_zm_del','obiegi_dekr');
   {? EDOKUM.f_active() || EDOKUM.f_rfresh() ?}
?};
{? _open
|| SSTALE.AR:=_ar; SSTALE.AO:=_ao;
   exec('open_tabele','open_tab')
?}


\dob_dek_del1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Dekretacja dokumentu w obiegu - delegacja bez wydatkowych faktur rozszerzonych
::   WE: [_a] - opcjonalna data rozliczenia
::  OLD: \dob_dek_del1/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
POMOC.V:=POMOC.D:=null;
{? EDOKUM.REJ=null & ETYPYFIR.DOM_REJ<>null & ETYPYFIR.DOM_RDOK<>null & ETYPYFIR.AUTOKSIE<>null
|| exec('szuk_okr','okresy',{? _>0 || _a || EDOKUM.DOP ?});
   {? ROZNE.UT_OKROD<>null
   || _ar:=SSTALE.AR; _ao:=SSTALE.AO;
      SSTALE.AR:=ROZNE.UT_OKROD().ROK; SSTALE.AO:=OKRO_F.ref();
      exec('open_tabele','open_tab');
      DOK.index('REJ'); DOK.prefix(); DOK.blank();
      DOK.DODSLU:=ddodslu; DOK.DODSLO:=ddodslo;
      DOK.ODD:=EDOKUM.ODD;
      DOK.WYS:=EDOKUM.KH;
      DOK.TR:=EDOKUM.TR;
      {? |DOK.TR='' || DOK.TR:=op_del || DOK.TR:=DOK.TR+' '+op_del ?};
      RS.cntx_psh(); RS.index('RS'); RS.prefix();
      {? EDOKUM.KH<>null & RS.find_key(DOK.WYS().SLU().WZ) & RS.TAB='KH'
      || KH.cntx_psh(); KH.index('KOD'); KH.prefix(2);
         {? KH.find_key(SLO.KOD)
         || DOK.KH:=KH.SKR;
            DOK.NIP:={? KH.TYP='R'
                     || {? KH.NIP<>'' || KH.NIP || KH.PESEL ?}
                     || {? 1+DOK.RVAT().RVAT().KVAT().SYM<>'_'
                        || KH.NIP
                        || exec('jak_nue','kontrahent')
                        ?}
                     ?}
         ?};
         KH.cntx_pop()
      ?};
      RS.cntx_pop();
      DOK.REJ:=ETYPYFIR.DOM_REJ;
      DOK.DOK_REJ:=ETYPYFIR.DOM_RDOK;
      DOK.JPK_V_T:=exec('typ_dok','obiegi2');
      DOK.DTO:={? _>0 || _a || EDOKUM.DOP ?};
      DOK.DTW:={? _>0 || _a || EDOKUM.DOP ?};
      DOK.DOP:={? _>0 || _a || EDOKUM.DOP ?};
      {? DOK.REJ().WAL<>'N'
      || DOK.BANK:=dbank;
         DOK.TYPKRS:=dtypkrs;
         DOK.TKRS:=dtkrs;
         DOK.TRKS_V:=dtkrs_v
      ?};
      DOK.EDOKUM:=$EDOKUM.ref();
      {! |?
         DOK.cntx_psh;
         DOK.index('REJ'); DOK.prefix(DOK.REJ);
         _numer:={? DOK.last() || DOK.NR+1 || 1 ?};
         DOK.cntx_pop();
         do();
         DOK.NR:=_numer;
         DOK_REJ.cntx_psh(); DOK_REJ.prefix();
         {? |DOK.DOK_REJ().FORM_SYM<>''
         || _sym:=($DOK_REJ.FORM_SYM)();
            DOK.NK:={? type_of(_sym)=2 || _sym || '' ?}
         ?};
         DOK_REJ.cntx_pop();
         {? DOK.NK='' || DOK.NK:=EDOKUM.ID ?};
         {? DOK.add(1)
         || _refdok:=dok_ref:=DOK.ref();
            {? var_pres('zak_dek')>0 || zak_dek:=1 ?};
            POZ.cntx_psh(); POZ.index('DOK'); POZ.prefix(DOK.ref());
            exec('pop_poz','dok_fks','DOK',0);
            EDOKUM.REJ:=DOK.REJ;
            EDOKUM.DOK_KS:=$DOK.ref();
            EDOKUM.NREJ:=DOK.NR;
            EDOKUM.DATDOK:=DOK.DTW;
            EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
            podz_del:=(EDOKUM.TYP().ED_PODZ='T');
            EDOKUM.put();
            {? ETYPYFIR.AUTOKSIE<>null
            || {? podz_del
               || PODZDEL:=tab_tmp(2,'REK','STRING[16]','Rekord',
                                     'POLE','STRING[8]','Pole',
                                     'KWOTA','REAL','Kwota')
               ?};
               exec('oblicz_f','dok_fks',ETYPYFIR.AUTOKSIE().NAZ);
               {? podz_del
               || exec('podz_deleg_add','obiegi_dekr',DOK.ref(),EDOKUM.ref());
                  VAR_DEL.delete('PODZDEL')
               ?}
            ?};
            {? var_pres('ile_dek')>0 || ile_dek+=1 ?};
            POZ.cntx_pop(); _dalej:=0
         || _dalej:=1
         ?};
         _end:=end();
         _dalej
      !};
      {? POMOC.DISP_POZ || exec('poz_dok','dok_fks') ?};
      SSTALE.AR:=_ar; SSTALE.AO:=_ao;
      exec('open_tabele','open_tab')
   ?}
?};
VAR_DEL.delete('podz_del')


\podz_deleg_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Dodaje podzialy do dokumentu delegacji
::   WE: _a - ref dokumentu (DOK)
::       _b - ref delegacji (EDOKUM)
::  OLD: \podz_deleg_add/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
PODZDEL.prefix();
{? _a & _b & PODZDEL.first()
|| _kwota:=0;
   {! |?
      _kwota+=PODZDEL.KWOTA; PODZDEL.next()
   !};
   {? _kwota$2<>0
   || SKIDXD.cntx_psh(); SKIDXD.index('DOK'); SKIDXD.prefix();
      EPODZ.cntx_psh(); EPODZ.index('EDOKUM'); EPODZ.prefix(_b);
      {? EPODZ.first()
      || {! |?
            {? EPODZ.SKID_MB<>null & EPODZ.SKID_MB().EWID='T'
            || SKIDXD.blank(1);
               SKIDXD.SKID_MB:=EPODZ.SKID_MB;
               SKIDXD.DOK:=_a;
               SKIDXD.JORG:=EPODZ.JORG;
               SKIDXD.OKOSZ:=EPODZ.OKOSZ;
               SKIDXD.PBUD:=EPODZ.PBUD;
               SKIDXD.WYM4:=EPODZ.WYM4;
               SKIDXD.WYM5:=EPODZ.WYM5;
               SKIDXD.WAL:=FINFO.NAROD;
               SKIDXD.WART:=(EPODZ.WART*_kwota/100)$2;
               SKIDXD.GEN_AUT:='N';
               {? ~SKIDXD.add() || undo() ?}
            ?};
            EPODZ.next()
         !}
      ?};
      SKIDXD.cntx_pop(); EPODZ.cntx_pop()
   ?}
?}


\spr_deleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Sprawdzenie czy mozna dekretowac delegacje - poszczegolne dokumenty - lista komunikatow
::  OLD: \spr_deleg/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOK_ZAL.cntx_psh();
id_del:=EDOKUM.ID;
{? EDOKUM.REJ=null
|| exec('szuk_okr','okresy',{? _>0 || _a || EDOKUM.DOP ?});
   {? ROZNE.UT_OKROD=null
   || exec('add_kom_del','obiegi','Nie znaleziono w systemie okresu obrachunkowego dla podanej daty rozliczenia delegacji.')
   |? ROZNE.UT_OKROD().ZAM='T'
   || exec('add_kom_del','obiegi','Okres obrachunkowy dla podanej daty rozliczenia delegacji jest zamknięty.')
   ?}
?};
EDOK_ZAL.index('WAL'); EDOK_ZAL.prefix(EDOKUM.ref(),'W');
{? EDOK_ZAL.first()
|| {! |?
      {? EDOK_ZAL.REJ=null & EDOK_ZAL.ETYPWYD().ED_FULL=4 & EDOK_ZAL.EDOKUMF
      || EDOKUM.cntx_psh();
         EDOK_ZAL.EDOKUMF();
         exec('szuk_okr','okresy',EDOKUM.DOP);
         {? ROZNE.UT_OKROD=null
         || exec('add_kom_del','obiegi','Nie znaleziono w systemie okresu obrachunkowego dla podanej daty operacji wydatku '+EDOKUM.SYM)
         |? ROZNE.UT_OKROD().ZAM='T'
         || exec('add_kom_del','obiegi','Okres obrachunkowy dla podanej daty operacji wydatku '+EDOKUM.SYM+' jest zamknięty.')
         ?};
         brutdob:=netdob:=0;
         exec('spr_pvat','obiegi2',0,EDOKUM.sel_size());
         EDOKUM.cntx_pop()
      ?};
      EDOK_ZAL.next()
   !}
?};
EDOKUM.cntx_pop(); EDOK_ZAL.cntx_pop();
VAR_DEL.delete('id_del','brutdob','netdob')


\kom_del_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Utworzenie tabelki z komunikatami
::   WE: _a - czy w okienku ma byc identyfikator delegacji
::  OLD: \kom_del_ini/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
KOM_DEL:=tab_tmp(2,'LP','INTEGER','Lp.',
                   'ID','STRING[30]','Identyfikator',
                   'OPIS','STRING[200]','Opis'
                 );
komdind1:=KOM_DEL.index('?');
komdind2:=KOM_DEL.ndx_tmp('',1,'ID',,0,'ID',,0);
_wer:=KOM_DEL.mk_sel('Komunikaty o błędach'@,'P',0,'kom_del_wer');
KOM_DEL.win_fld(_wer,,'LP',,,3);
{? _a || KOM_DEL.win_fld(_wer,,'ID',,,20) ?};
KOM_DEL.win_fld(_wer,,'OPIS',,,100);
KOM_DEL.win_sel(_wer)


\gen_pozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzy pozycje faktury na podstawie pozycji faktury w obiegu
::   WE: _a - dromyślna grupa podatkowa
::  OLD: \gen_pozf/skid_do1.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('JPK_FA_P',DOK_REJ)>0 & DOK.DOK_REJ().JPK_FA_P='T' & DOK_REJ.TYP_CENY=EDOKUM.TYP().TYP_CENY
|| _kh:=null;
   {? DOK.EDI<>'N'
   || KH.cntx_psh();
      KH.index('SKR');
      KH.prefix(2,DOK.KH,);
      {? KH.first() || _kh:=KH.ref() ?};
      KH.cntx_pop()
   ?};
   POZF.cntx_psh();
   POZF.use('pozf__'+(EDOKUM.name()+2));
   POZF.index('EDOKUM'); POZF.prefix(EDOKUM.ref());
   {? POZF.first()
   || {!
      |? POZF.cntx_psh();
         POZF.use('pozf'+(DOK.name()+4));
         POZF.prefix();
         POZF.DOK:=DOK.ref();
         POZF.EDOKUM:=null;
         {? DOK.EDI<>'N' & _kh<>null & POZF.KODP<>''
         || POZF.GRVAT:=exec('find_grvat','dok_fks',_kh,POZF.KODP,DOK.REJ,DOK.EDI,0);
            {? POZF.GRVAT=null | POZF.GRVAT().REJ<>DOK.REJ
            || POZF.GRVAT:=_a
            ?}
         ?};
         POZF.add();
         POZF.cntx_pop();
         POZF.next()
      !}
   ?};
   POZF.cntx_pop()
?}


\jedn_ks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AF [19.42]
:: OPIS: Wybór jednostki księgowej do dekretacji wniosku
::----------------------------------------------------------------------------------------------------------------------
_tmpTab:=tab_tmp(1,
      'KOD','STRING[8]','Skrót'@,
      'OPIS','STRING[40]','Pełna nazwa'@,
      'REF','INTEGER',
   );
ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
{? ODD.first()
|| {!
   |? _tmpTab.KOD:=ODD.OD;
      _tmpTab.OPIS:=ODD.N;
      _tmpTab.REF:=#ODD.ref();
      _tmpTab.add();
      ODD.next()
   !}
?};
_win:=_tmpTab.mk_sel('Jednostki księgowe'@,'P',,'#oddusr',,,,,'U');
_tmpTab.win_fld(_win,,'KOD');
_tmpTab.win_fld(_win,,'OPIS');
_tmpTab.win_act(_win,0,'Formuła','Wybierz'@@,,'Wybór bieżącej pozycji'@,"sel_exit()",,1,,,,'W');
_tmpTab.win_sel(_win);
{? _tmpTab.select
|| ODD.seek(_tmpTab.REF,);
   EDOKUM.ODD:=ODD.ref();
   EDOKUM.put();
   OKRO_F.actions_grayed('WER','')
?}


\czy_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Sprawdza, czy faktura w obiegu została zarejestrowana z Businesslink
::   WY: DOKUM.ref()/null
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null();
DOKUM.cntx_psh(); DOKUM.use('dokum'); DOKUM.index('TYP'); DOKUM.prefix($EDOKUM.ref());
{? DOKUM.first()
|| {! |? {? DOKUM.BL='T' || _wyn:=DOKUM.ref(); 0 || DOKUM.next() ?} !}
?};
DOKUM.cntx_pop();
_wyn


\bl_odczyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Ponowny odczyt komunikatu
::   WE: _a - DOKUM.ref()
::----------------------------------------------------------------------------------------------------------------------
{? _a=null || return(0) ?};
DOKUM.cntx_psh();
DOKUM.use(ref_name(_a)); DOKUM.prefix();
{? DOKUM.seek(_a)
|| _edi:=exec('env','edi_wspolne');
   {? ~_edi.init()
   ||
      DOKUM.cntx_pop(); return(0)
   ||
      _edi.context:=exec('context_obj_bl','edi_wspolne');
      _edi.context.DOKUM:=_a;
      _edi.sza:=1;
      _istdef:=null();
      _dokumz:=exec('dokumz','edi_fo_ufd',_a,'ksef');
      {? _dokumz || _istdef:=exec('bl_ufd_read','dokum',DOKUM.ref(),_dokumz,_edi.context,'FKS') ?};
      {? _istdef
      || exec('dokum2pilki','edi_wspolne',_a,'ksef')
      || _dokumz:=exec('dokumz','edi_fo_ufd',_a,'ufd');
         {? _dokumz || _istdef:=exec('bl_ufd_read','dokum',DOKUM.ref(),_dokumz,_edi.context,'FKS') ?};
         {? _istdef || exec('dokum2pilki','edi_wspolne',_a,'ufd') ?}
      ?};
      {? _istdef=null()
      ||
         DOKUM.cntx_pop(); return(0)
      ||
            DOKUM.cntx_psh();
            VAR_DEL.delete('obdekredi','ileautow');
            obdekredi:=ileautow:=1;
            exec('edi_read','edi_wspolne',_istdef,0);
            VAR_DEL.delete('obdekredi');
            DOKUM.cntx_pop()
      ?}
   ?}
?};
DOKUM.cntx_pop();
1


\read_edi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wywołuje odczytanie komunikatu EDI DKS
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('RB',EDOKUM)>0 & PAR_SKID.get(485)='T' & ~ETYPY.TYPVATPR & typobi=1 & EDOKUM.AUTOMAT
|| _dokum:=exec('czy_bl','obiegi_dekr');
   {? _dokum=null || return(0) ?};
   VAR_DEL.delete('edipozfv');
   edipozfv:=PAR_SKID.get(486)='T';
   DOK.win_edit('KSDKOB02');
   POMOC.AUT_DEK:=EDOKUM.WAL=FINFO.NAROD | var_pres('KURS',EDOKUM)>0 & EDOKUM.KURS;
   POMOC.AUT_SCH:=0;
   DOK.efld_opt('KSDKOB02','enable='+$(ETYPYFIR.AUTOKSIE<>null),POMOC,'AUT_SCH');
   VAR_DEL.delete('zoknzedi');
   {? EDOKUM.sel_size() | (zoknzedi:=DOK.edit("exec('sprdokks_do1','obiegi_dekr')");zoknzedi)
   || exec('bl_odczyt','obiegi_dekr',_dokum)
   || return(0)
   ?}
|| return(0)
?};
1


\del_edi_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Usuwa dokument dodany automatycznie podczas dekretacji
::   WE: _a - DOK.ref
::----------------------------------------------------------------------------------------------------------------------
DOK.cntx_psh();
{? DOK.seek(_a)
|| VAR_DEL.delete('DokWyk','licz');
   DokWyk:=licz:=1;
   exec('dok_usun','dok_fks1');
   VAR_DEL.delete('DokWyk','licz')
?};
DOK.cntx_pop()



:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 c6de8260ef24dd0e71d6ce0b30431b69a77d06e034f83adbf197e3b284dc87e2e3a6d8ed0b88e1b47e77cefb1c0c1753b9cfc684e93f35a247f38a19e9d26d9080d04b1b3580c844897c90dea784cc9bd5d70c05f5fb1923fe210e5cf19965857e5861138852bf612dc55c92290c5644893648550202eebeb7be1f97c22a2809

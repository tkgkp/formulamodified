:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: areatitle.fml
:: Utworzony: 09.02.2015 [17.00]
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi tutułu obszaru roboczego
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawienie tytułów obszarów roboczych
::----------------------------------------------------------------------------------------------------------------------
exec('AreaTitle','#object');
:: Inicjalizacja najpierw czyści wszystko
AreaTitle.clear().

:: Parametry
addParam('PULPIT_OKRO','okres'@@,$"exec('pulpit_okro','areatitle')").
addParam('FKS_KRAJ','kraj'@@,$"SLO.cntx_psh();SLO.prefix();_t:={? SLO.seek(kraj) || SLO.KOD || '' ?};SLO.cntx_pop();_t").
addParam('FKS_ODD','jednostka księgowa'@@,$"_ret:=__PARSES.getVal('JednostkaKsięgowa');{? _ret.REF<>null() || _ret.KOD || 'Wszystkie jednostki'@ ?}").
addParam('OBE_GRP','grupa dokumentów'@@,$"_ret:=__PARSES.getVal('GrupaObiegu');{? _ret.REF<>null() || _ret.KOD || 'Wszystkie grupy'@ ?}").
addParam('FKS_OKRO','okres'@@,$"exec('fks_okro','areatitle')").
addParam('WAL','waluta'@@,$"SLO.cntx_psh();_t:=SSTALE.WAL().KOD;SLO.cntx_pop();_t").
addParam('FKS_ROK','rok'@@,$"ROK_F.cntx_psh();_rok:=SSTALE.AR().NAZ;ROK_F.cntx_pop();_rok").
addParam('FKS_KSF_ZAKR','zakres'@@,$"exec('ksf_scope','dok_ksef')").
addParam('OBG_ZAKR','zakres'@@,$"{? var_pres('hdr_obg')>0 || hdr_obg || '' ?}").
addParam('OBE_ZAKR','zakres'@@,$"exec('title','zaliczki')").
addParam('VAT_ROK','rok'@@,$"ROK_F.cntx_psh();_rok:=SIK.ROK1().NAZ;ROK_F.cntx_pop();_rok").
addParam('LANG','język'@@,$"{? SKID.LANG_SLO || SKID.LANG_SLO().TR || 'Polski'@ ?}").
addParam('O','lista płac'@@,$"exec('o','areatitle')").
addParam('ODD','oddział'@@,$"exec('odd','areatitle')").
addParam('LMG_OKRO','okres'@@,$"exec('lmg_okro','areatitle')").
addParam('MAG','magazyn'@@,$"exec('mag','areatitle')").
addParam('STS','stanowisko sprzedaży'@@,$"exec('sts','areatitle')").
addParam('STZ','stanowisko zakupu'@@,$"exec('stz','areatitle')").
addParam('WYDZ','wydział'@@,$"_ret:=__PARSES.getVal('WydzialProd');{? _ret.SYMBOL<>'' || _ret.SYMBOL || 'brak'@ ?}").
addParam('VIEW_VER','wersja'@@,$"PX_VAR.VIE_VER().SYMBOL").
addParam('TAR','zakres'@@,$"{? TAZ.SD='G' || 'Gratisy'@ || 'Cenniki'@ ?}").
addParam('ZL_ZAKR','zakres'@@,$"exec('zl_zakres','areatitle')").
addParam('STANKAS','stanowisko'@@,$"_ret:=__PARSES.getVal('StKasowe');{? _ret.REF<>null() || _ret.NAZWA || 'brak'@ ?}").
addParam('MULTIKASA','multikasa'@@,$"_ret:=__PARSES.getVal('Mkasa');{? _ret.REF<>null() || _ret.NAZ || 'brak'@ ?}").
addParam('KAS_OKRO','okres'@@,$"exec('kas_okro','areatitle')").
addParam('ZDR_ROK','rok'@@,$"exec('zdr_rok','areatitle')").
addParam('PRACOWNIK',''@@,$"exec('pracownik','areatitle')").
addParam('A_OKR','Okres rozliczeniowy'@@,$"exec('a_okr','areatitle')").
addParam('A_OKRM','Miesiąc rozliczeniowy'@@,$"exec('a_okrm','areatitle')").
addParam('F_ZATR','Forma współpracy'@@,
   $"_ret:=__PARSES.getVal('F_ZATR');{? _ret.REF<>null() || _ret.KOD || 'brak'@ ?}").
addParam('JO','Jednostka organizacyjna'@@,
   $"_ret:=__PARSES.getVal('JednostkaOrganizacyjna');{? _ret.REF<>null() || _ret.SYMBOL || 'brak'@ ?}").
addParam('PPK_UMZA','zarządzanie'@@,$"exec('ppk','areatitle','umza')").
addParam('PPK_UMPR','prowadzenie'@@,$"exec('ppk','areatitle','umpr')").
addParam('POC_OFO','program'@@,$"exec('poc','areatitle')").
addParam('LOG_ZAKR','zakres'@@,$"exec('log_zakr','areatitle')").
addParam('TRA_ZAKR','zakres'@@,$"exec('tra_zakr','areatitle')").
addParam('PSP_ZAKR','zakres'@@,$"exec('psp_zakr','areatitle')").

:: Parametry tytułów dla obszarów
:: FKS
addDomain4Param('PULPIT_OKRO','FKS').
addDomain4Param('PULPIT_OKRO','FST').
addTitleParams('FKS_DKS','FKS_OKRO','FKS_ODD','WAL').
addTitleParams('FKS_HUB','FKS_OKRO','FKS_ODD').
addTitleParams('FKS_RUC','FKS_OKRO','FKS_ODD','WAL').
addTitleParams('FKS_RRK','FKS_OKRO','FKS_ODD','WAL').
addTitleParams('FKS_RWP','FKS_OKRO','FKS_ODD','WAL').
addTitleParams('FKS_KSR','FKS_OKRO','FKS_ODD','WAL').
addTitleParams('FST_EWI','FKS_OKRO','FKS_ODD').
addTitleParams('FST_EWZ','FKS_OKRO','FKS_ODD').
addTitleParams('FST_INP','FKS_OKRO','FKS_ODD').
addTitleParams('FKS_VAT','FKS_OKRO','FKS_ODD','FKS_KRAJ').
addTitleParams('FKS_VED','VAT_ROK').
addTitleParams('FKS_KSP','LANG').
addTitleParams('FKS2KSP','FKS_OKRO','FKS_ODD','LANG').
addTitleParams('FKS_ROZ','FKS_ROK','FKS_ODD','WAL').
addTitleParams('HBN_WBA','FKS_OKRO','FKS_ODD','WAL').
addTitleParams('FKS_KSF','FKS_OKRO','FKS_ODD','WAL','FKS_KSF_ZAKR').

:: HBN
addDomain4Param('PULPIT_OKRO','HBN').

:: KON
addDomain4Param('PULPIT_OKRO','KON').
addTitleParams('KON_SPR','FKS_OKRO','FKS_ODD','LANG').

:: CTR
addDomain4Param('PULPIT_OKRO','CTR').
addTitleParams('CTR_PDM','FKS_OKRO','FKS_ODD').

:: OBG
addDomain4Param('PULPIT_OKRO','OBG').
addTitleParams('OBG_FZO','FKS_OKRO','FKS_ODD','OBG_ZAKR').

:: OBE
addDomain4Param('PULPIT_OKRO','OBE').
addTitleParams('OBE_ZAL','OBE_ZAKR').
addTitleParams('OBE_FAD','FKS_OKRO','FKS_ODD','OBG_ZAKR').
addTitleParams('OBE_FDD','FKS_OKRO','FKS_ODD','OBG_ZAKR').

:: KAS
addDomain4Param('PULPIT_OKRO','KAS').
addTitleParams('KAS_DOK','KAS_OKRO','STANKAS','MULTIKASA').

:: LMG
addDomain4Param('PULPIT_OKRO','LMG').
addTitleParams('LMG_DSM','ODD','MAG','STS','LMG_OKRO').
addTitleParams('LMG_INT','ODD','STS','LMG_OKRO').
addTitleParams('LMG_MAG','ODD','MAG','STS','LMG_OKRO','LOG_ZAKR').
addTitleParams('LMG_OPK','ODD','MAG','STS','LMG_OKRO').
addTitleParams('LMG_BAD','ODD','MAG','STS','LMG_OKRO').
addTitleParams('LMG_ZAM','ODD','MAG','STS','LMG_OKRO').

:: LMO
addTitleParams('LMO_MOB','ODD','MAG','LMG_OKRO').
addTitleParams('LMO_PAL','ODD','MAG','LMG_OKRO').

:: LSP
addDomain4Param('PULPIT_OKRO','LSP').
addTitleParams('LSP_CEN','TAR','ODD','STS','LMG_OKRO').
addTitleParams('LSP_INT','ODD','STS','LMG_OKRO').
addTitleParams('LSP_FAK','ODD','STS','LMG_OKRO','LOG_ZAKR').
addTitleParams('LSP_OFE','ODD','STS','LMG_OKRO').
addTitleParams('LSP_ZKN','ODD','STS','LMG_OKRO').
addTitleParams('LSP_PSP','ODD','PSP_ZAKR').

:: LSS
addDomain4Param('PULPIT_OKRO','LSS').
addTitleParams('LSS_POS','ODD','STS','LMG_OKRO','LOG_ZAKR').

:: LTR
addDomain4Param('PULPIT_OKRO','LTR').
addTitleParams('LTR_KAR','LMG_OKRO').
addTitleParams('LTR_ZLE','LMG_OKRO','TRA_ZAKR').
addTitleParams('LTR_MOB','LMG_OKRO','TRA_ZAKR').

:: LUM
addDomain4Param('PULPIT_OKRO','LUM').
addTitleParams('LUM_UMO','ODD','LMG_OKRO').
addTitleParams('LUM_UMM','ODD','LMG_OKRO').
addTitleParams('LUM_HAW','ODD').
addTitleParams('LUM_FAK','ODD','STS','LMG_OKRO').
addTitleParams('LUM_ZGL','ODD','LMG_OKRO').

:: LUO
addDomain4Param('PULPIT_OKRO','LUO').
addTitleParams('LUO_KPO','ODD','LMG_OKRO').
addTitleParams('LUO_KEO','ODD','LMG_OKRO').

:: LZK
addDomain4Param('PULPIT_OKRO','LZK').
addTitleParams('LZK_DST','ODD','STZ','LMG_OKRO').
addTitleParams('LZK_PLD','ODD').
addTitleParams('LZK_ZAK','ODD','STZ','LMG_OKRO','LOG_ZAKR').
addTitleParams('LZK_ZDS','ODD','STZ','LMG_OKRO').

:: PKD
addDomain4Param('F_ZATR','PKD').
addTitleParams('PKD_EZK','O','JO').

:: PPL
addDomain4Param('F_ZATR','PPL').
addTitleParams('PPL_PLL','O','JO').
addTitleParams('PPL_ZDR','ZDR_ROK').

:: PKW
addTitleParams('PKW_POR','PRACOWNIK').

:: POC
addTitleParams('POC_OCW','PRACOWNIK').
addTitleParams('POC_OFO','POC_OFO').

:: PSZ
addTitleParams('PSZ_SDW','PRACOWNIK').

:: PPK
addTitleParams('PPK_EWI','PPK_UMZA','PPK_UMPR').
addTitleParams('PPK_WMD','PPK_UMZA','PPK_UMPR').

:: TTE
addDomain4Param('PULPIT_OKRO','TTE').
addTitleParams('TTE_BAD','ODD','LMG_OKRO').
addTitleParams('TTE_KOO','ODD','MAG','LMG_OKRO').
addTitleParams('TTE_PZL','ZL_ZAKR','ODD','MAG','WYDZ','LMG_OKRO').
addTitleParams('TTE_TEC','ODD','LMG_OKRO').
addTitleParams('TTE_WTE','ODD').
addTitleParams('TTE_WYK','ODD','WYDZ','LMG_OKRO').
addTitleParams('TTE_WKJ','ODD','LMG_OKRO').
addTitleParams('TTE_WZL','ZL_ZAKR','ODD','MAG','WYDZ','LMG_OKRO').

:: TPP
addDomain4Param('PULPIT_OKRO','TPP').
addTitleParams('TPP_PPS','ODD','VIEW_VER').
addTitleParams('TPP_PPO','WYDZ').
addTitleParams('TPP_PRZ','').
addTitleParams('TPP_GOP','ODD','MAG','LMG_OKRO').

:: TRE
addTitleParams('TRE_ZGL','ODD','WYDZ','LMG_OKRO').
addTitleParams('TRE_REK','ODD').
addTitleParams('TRE_RDO','ODD').

:: WYP
addDomain4Param('PULPIT_OKRO','WYP').
addTitleParams('WYP_LWW','ODD','MAG','LMG_OKRO').
addTitleParams('WYP_LWP','ODD','LMG_OKRO').
addTitleParams('WYP_LWZ','ODD','MAG','LMG_OKRO').
addTitleParams('WYP_LWN','ODD','LMG_OKRO').

:: ZPR
addTitleParams('ZPR_PRO','').
addTitleParams('ZPR_ZAS','').

:: ZWS
addTitleParams('ZWS_ADM','').
addTitleParams('ZWS_EDI','').
addTitleParams('ZWS_KHR','FKS_OKRO','FKS_ODD','ODD').
addTitleParams('ZWS_MAT','').
addTitleParams('ZWS_USR','').
addTitleParams('ZWS_PRJ','ODD','LMG_OKRO','STS','STZ','MAG').
addTitleParams('ZWS_ZAS','FKS_OKRO','ODD').
addTitleParams('ZWS_W01','').
addTitleParams('ZWS_W02','').
addTitleParams('ZWS_W03','').
addTitleParams('ZWS_W04','').
addTitleParams('ZWS_W05','').

:: ZUI
addTitleParams('ZUI_CLI','').

:: PULPIT
:addDesktopParams('PULPIT_OKRO','ODD','MAG','WYDZ','STANKAS','MULTIKASA','F_ZATR','O','A_OKR','FKS_ODD').

:: PRC
addDomain4Param('F_ZATR','PRC').
addTitleParams('PRC_HAR','A_OKR').
addTitleParams('PRC_MCR','A_OKRM')
;
~~


\fks_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zwraca tytuł okresu dla dziedziny finanse
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
OKRO_F.cntx_psh();
_t:=SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ;
_zam:={? 'FKS,HBN,OBG,OBE,KON'*(3+AreaTitle.area)>0
      || OKRO_F.ZAM='T'
      |? 3+AreaTitle.area='FST'
      || OKRO_F.AMOR='T'
      |? 3+AreaTitle.area='CTR'
      || OKRO_F.ZAM_CON='T'
      || -1
      ?};
_t+={? _zam<>-1 || ' '+{? _zam=1 || 'zamknięty' || 'otwarty'?} || '' ?};
OKRO_F.cntx_pop();
ROK_F.cntx_pop();
_t


\kas_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zwraca tytuł okres obszaru roboczego
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
_t:=USRCONST.ROK().NAZ;
ROK_F.cntx_pop();
_t


\o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Lista płac.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=__PARSES.getVal('ListaPłac');
{? _ret.REF
|| '%1 - %2 %3 (%4)'
   [~-_ret.LT,_ret.T,date(_ret.R,_ret.M,0)$8,{? _ret.Z='T' || 'zamknięta'@ || 'otwarta'@ ?}]
|| 'brak'@
?}


\zdr_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Zwraca rok deklaracji podatkowych
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__Rok')>0 || $__Rok || 'brak'@ ?}


\zl_zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zwraca zakres widoku zleceń
::----------------------------------------------------------------------------------------------------------------------
_title:='';
{? ZAKR.VZL='N'
|| _title+=' '+'nadrzędne'@
?};
{? ZAKR.ZL_STAN='W'
|| _title+=' '+'w przygotowaniu'@
|? ZAKR.ZL_STAN='O'
|| _title+=' '+'otwarte'@
|? ZAKR.ZL_STAN='Z'
|| _title+=' '+'zamknięte'@
|? ZAKR.ZL_STAN='N'
|| _title+=' '+'niezamknięte'@
|| _title:=' '+'wszystkie'@
?};
_title


\web_title_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Ustawia podstawowy tytuł w belce tytułowej okna dla webTerma
::   WE: [_a][INTEGER] - 0*/1 Czy doklejać informacje o pracowniku? Domyślnie 0 - nie doklejać
::----------------------------------------------------------------------------------------------------------------------
_addPrac:={? var_pres('_a')=type_of(0) || _a || 0 ?};
exec('firma','#firma',exec('firma_symbol','#firma'));
web_title('%1, %2%3'[exec('nazwa','#system'), 'Firma '+REF.FIRMA().SYMBOL,
                     {? _addPrac || ' %1'[exec('pracownik','areatitle')] || '' ?}],
          'all'
         )


\pracownik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.00]
:: OPIS: Opis tabeli P na potrzeby areatitle w webtermie.
::   WE:
::   WY: Tekstowa reprezentacja rekordu tabeli P.
::----------------------------------------------------------------------------------------------------------------------
_result:='';
OSOBA.cntx_psh();
P.OSOBA();
_result:=OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE+': '+
'Numer teczki'@+' - '+P.T+', '+
'Identyfikator'@+' - '+$P.IP;
OSOBA.cntx_pop();
_result


\odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Zwraca kod oddziału lub 'brak'
::   WY: STRING
::----------------------------------------------------------------------------------------------------------------------
_result:='brak'@;
_ret:=__PARSES.getVal('OddzialLogProd');
{? _ret.REF<>null()
|| _result:=_ret.KOD
?};
_result


\lmg_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Zwraca nazwę okresu obrachunkowego logistycznego
::   WY: STRING
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='brak'@;
{? ST.OKR<>''
|| _result:=ST.OKR
?};
{? ST.OKR_REF
|| OKR.cntx_psh();
   _zam:={? 'LMG,TTE,WYP,TRE'*(3+AreaTitle.area)>0
         || 1+ST.OKR_REF().ZAM='T'
         |? 3+AreaTitle.area='LSP'
         || 1-ST.OKR_REF().ZAM-1='T'
         |? 3+AreaTitle.area='LZK'
         || ST.OKR_REF().ZAM+1='T'
         ?};
   OKR.cntx_pop();
   _result+=' '+{? _zam || 'zamknięty'@ || 'otwarty'@ ?}
?};
_result


\mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Zwraca nazwę magazynu
::   WY: STRING
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='brak'@;
_ret:=__PARSES.getVal('Magazyn');
{? _ret.REF<>null()
|| _result:=_ret.SYM
?};
_result


\sts
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Zwraca kod stanowiska sprzedaży
::   WY: STRING
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='brak'@;
{? ST.STS<>null()
|| _result:=ST.STS().KOD
?};
_result


\stz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.28]
:: OPIS: Zwraca kod stanowiska zakupu
::   WY: STRING
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='brak'@;
{? ST.STZ<>null()
|| _result:=ST.STZ().KOD
?};
_result


\a_okr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [17.28]
:: OPIS: Okres rozliczeniowy - harmonogramy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:={? var_pres('__HARM')>100 & __HARM.OKR_REF<>null()
      || __HARM.OKR_REF
      || __PARSES.getVal('OkresRozliczeniowy_Harmonogramy').REF
      ?};
{? _ret=null()
|| 'brak'@
|| exec('record','#to_string',_ret)+
   {? VAR.P
   || {? P.seek(VAR.P,,1)
      || ', '+P.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE
      || ''
      ?}
   || ''
   ?}
?}


\a_okrm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Informacja o wybranym miesiącu rozliczeniowym.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
' '+date(A_OKRM.R,A_OKRM.M,0)$8


\pulpit_okro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [18.22]
:: OPIS: Zwraca nazwę okresu dla pulpitu
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_obj:=__PARSES.getVal('OkresRok');
{? _obj.OKRO_R='O'
|| _result:='Bilans otwarcia %1'@[_obj.ROK_BO]
|? _obj.OKRO_R='Z'
|| _result:='Bilans zamknięcia %1'@[_obj.ROK_BO]
|| _result:=__PARSES.getVal('OkresRok').NAZWA
?};
_result


\ppk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Tytuły związane z obsługą PPK.
::   WE: _a [STRING] - Kod zwracanej informacji:
::                     umza - umowa o zarządzanie
::                     umpr - umowa o prowadzenie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='umza' | _a='umpr')
|| _kod:=_a
|| return('???')
?};

_ret:='[brak wybranej umowy]';
{? KST_PPK.PPK_UMO
|| PPK_UMO.cntx_psh();
   PPK_UMO.prefix();
   PPK_UZA.cntx_psh();
   PPK_UZA.prefix();
   ADRES.cntx_psh();
   ADRES.prefix();
   KST_PPK.PPK_UMO().PPK_UZA().ADRES();
   {? _kod='umza'
   || _ret:=ADRES.SYMBOL+'/'+PPK_UZA.NUMER
   |? _kod='umpr'
   || _ret:=PPK_UMO.NUMER+':'+$PPK_UMO.OD+'-'+$PPK_UMO.DO
   ?};
   ADRES.cntx_pop();
   PPK_UZA.cntx_pop();
   PPK_UMO.cntx_pop()
?};
_ret


\poc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Tytuły związane z obsługą POC.
::   WE: _a [STRING] - Kod zwracanej informacji:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_str1:= {? ZO_PROC.A='T' || 'Otwarta' || 'Nieotwarta'?};
_str2:={? ZO_PROC.Z='T' || 'Zamknięta' || 'Niezamknięta' ?};
_ret:= '%1, sesja: %2'[ZO_PROG.NAZWA, $(ZO_PROC.OKRES_OD)+' - '+$(ZO_PROC.OKRES_DO)+' '+_str1+', '+_str2];
_ret


\log_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: zakres dokumentów - logistyka
::----------------------------------------------------------------------------------------------------------------------
_res:={? FILTER.STATUS='M' || 'miesięczny'@
      |? FILTER.STATUS='R' || 'roczny'@
      || ''
      ?};
_res



\tra_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: zakres transportów - logistyka
::----------------------------------------------------------------------------------------------------------------------
_res:={? (3+TR_Z.ARCHNAZ)='Nie' || TR_Z.ARCHNAZ || '%1 (archiwum)'@[TR_Z.ARCHNAZ] ?};
_res


\psp_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [23.25]
:: OPIS: zakres planów sprzedaży - logistyka
::----------------------------------------------------------------------------------------------------------------------
_res:={? PS_Z.F='T' || ' aktywne'@ |? PS_Z.F='N' || ' archiwalne'@ || ' wszystkie' ?};
_res

:Sign Version 2.0 jowisz:1045 2023/12/21 13:22:35 db61d7c3cbe1fe597a22f35310d67d1999a900fdb3ee422eeb72f722d7bd7d310cb0837834eb135ed9dd1437ec6022f8fafc46ae5881b927040580dfe8c9c7ee5188cf0d360c5ffca3e29b4adf100a7eaeaf63b8b8f85de235c5b82e44aa95927f7b376ae3ce3c15218b49c397ee238b7a9b24f97d240bfe5f87fc723dc989c2

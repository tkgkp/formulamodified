:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: pkd_grp.fml
:: Utworzony: 06.04.2021
:: Autor: MicKoc [21.14]
::======================================================================================================================
:: Zawartość: Funkcje wspólne dla operacji grupowych dziedziny PKD
::======================================================================================================================


\jos_create
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła tworzy strukturę i okno wertowania tabeli tymczasowej do prezentacji struktury organizacyjnej
::       i związanych z nią stanowisk.
::   WE:
::   WY: Tablica z elementami nazwanymi:
::       TAB - Tabela tymczasowa.
::       NDX - Tablica indeksów.
::       WS  - Okno wertowania.
::----------------------------------------------------------------------------------------------------------------------
_TAB:=tab_tmp(2,
   'RODZIC','TREE_REF','Rodzic',
:  Nazwa jednostki organizacyjnej  (SYMBOL) (16) lub
:  nazwa stanowiska (ST) (80).
   'NAZWA','STRING[80]','Nazwa'@,
   'UD_DEF','STRING[16]','$UD_DEF.ref()',
   'STN','STRING[16]','$STN.ref()',
:  Typ listka:
:     J - Jednostka organizacyjna;
:     S - Stanowisko.
   'TYP','STRING[1]','Typ zasobu'
);

_wnd:=_TAB.mk_sel('Struktura'@,'N',,'#struorgstn',,,,1);
_TAB.win_fld(_wnd,,'NAZWA',,,30);

_TAB.win_act(_wnd,,'Formuła','Schemat'@,,'Wybór schematu danych'@,
   "params_exec('schemat_b','pkd_grp')","params_exec('schemat_a','pkd_grp')"
);
_TAB.win_act(_wnd,,'Formuła','Zwiń/&rozwiń'@,,'Zwijanie / rozwijanie drzewa'@,
   "exec('zwin_rozwin','#tree')",,,,,,'R',,'target=window'
);
_TAB.win_act(_wnd,,'Okienko',,,,"
   _par:=params_get();
   {? type_of(_par)<100 | var_pres('JOS',_par)<100
   || cur_tab(1,1).actions(cur_win(1,1),'S',,1)
   ?}
");
_TAB.win_sopt(_wnd,'select_record_checkbox=0');

_JOS:=obj_new('TAB','NDX','WS');
_JOS.NDX:=obj_new('TREE','NAZWA');

_JOS.TAB:=_TAB;

_JOS.NDX.TREE:=_TAB.index('?');
_JOS.NDX.NAZWA:=_TAB.ndx_tmp(,,'NAZWA',,,'TYP',,);
_JOS.WS:=_wnd;

_JOS


\schemat_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Przed akcję "Schemat" - wybór schematu danych.
::   WE:
::   WY: 0/1 wykonanie zasadniczej akcji
::----------------------------------------------------------------------------------------------------------------------
_sch:=exec('ud_sch_wybierz','schemat','PODZORG');
{? _sch.REF<>null
|| P_FILTER.UD_SCH:=_sch.REF
?}


\schemat_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.37]
:: OPIS: Zmiana schematu danych
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('jos_fill','pkd_grp',params_get().JOS,P_FILTER.UD_SCH,__PARSES.getVal('JednostkaOrganizacyjna').SYMBOL);
~~


\pkd_grp_azat_dsk_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła przygotowuje tłumaczenia dla kontrolki !pkd_grp_azat.dsk.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_TAB:=exec('elements_table','#desktop');
_add:="_a.blank(); _a.ID_SYS:=_b; _a.NAME:=_c; _a.add()";

_add(_TAB,'schTile@panel','Schemat'@);

_TAB


\jos_fill
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła tworzy rekordy tabeli tymczasowej JOS.
::   WE: _a - Tablica JOS z elementami nazwanymi.
::       _b - Wskazanie schematu.
::       _c - Element odcięcia.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_JOS:=_a;
_ud_sch:=_b;
_ud_skl:=_c;

_copy:=
   "  _copy:=_a;
      _JOS:=_b;
      _ud_sch:=_c;
      _rodzic:=_d;
      _ud_defp:=_e;
      UD_DEF.cntx_psh();
      UD_DEF.prefix(_ud_sch,_ud_defp);
      _dalej:=UD_DEF.first();
      {!
      |? _dalej
      |! _ud_def:=UD_DEF.ref();
         _JOS.TAB.blank();
         _JOS.TAB.RODZIC:=_rodzic;
         _JOS.TAB.NAZWA:=UD_DEF.SYMBOL;
         _JOS.TAB.UD_DEF:=$UD_DEF.ref();
         _JOS.TAB.TYP:='J';
         {? _JOS.TAB.add()
         || _tr:=#_JOS.TAB.ref();
            _copy(_copy,_JOS,_ud_sch,_tr,_ud_def);
            STO.prefix(UD_DEF.UD_SKL);
            {? STO.first()
            || {!
               |? _JOS.TAB.blank();
                  _JOS.TAB.RODZIC:=_tr;
                  _JOS.TAB.NAZWA:=STO.ST;
                  _JOS.TAB.UD_DEF:=$UD_DEF.ref();
                  _JOS.TAB.STN:=$STO.STN;
                  _JOS.TAB.TYP:='S';
                  _JOS.TAB.add();
                  STO.next()
               !}
            ?};
            STO.prefix()
         ?};
         _dalej:=UD_DEF.next()
      !};
      UD_DEF.prefix();
      UD_DEF.cntx_pop()
   ";

_JOS.TAB.erase();
STO.cntx_psh();
STO.index('ST');
UD_DEF.cntx_psh();
UD_DEF.index('OPIS');
_copy(_copy,_JOS,_ud_sch,0,0);
UD_DEF.cntx_pop();
STO.cntx_pop();

: Ewentualne odcięcie gałęzi.
_JOS.TAB.cntx_psh();
_JOS.TAB.index(_JOS.NDX.NAZWA);
_JOS.TAB.prefix(_ud_skl,'J');
{? _JOS.TAB.first()
|| _jos:=_JOS.TAB.RODZIC;
   _ref:=_JOS.TAB.ref()
|| _jos:=0;
   _ref:=null()
?};
_JOS.TAB.cntx_pop();
_JOS.TAB.tr_root(_JOS.WS,1,_jos,#_ref);

_JOS.TAB.first();
~~


:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:21 47faed733ca19fcba86fb22dbf60e32b8e04717af474ddee94b0ec8287de3aed558c1d04a2d3d415ef6236863770e45a2e598ac790a076251a93b7290c7c4247fac0a3f73c6245252045817dcdc89b5ab98344119c7fe5a625149b77f55b4fe8dbb273155f3e03839dbc4558494c4e57d39b3a4ea6ca822d757f2e1349910c56

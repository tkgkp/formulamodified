:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: dok_proj.fml
:: Utworzony: 27.03.2015 [17.00]
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi projektów w dokumencie
::======================================================================================================================


\op_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Operacje na tabeli OP_PROJ na podstawie rozrachunku i pozycji
::   WE: _a - 1-tworzenie 0-usuwanie
::       _b - 0-zapis w walucie rozrachunku 1-zapis w walucie narodowej
::       [_c] - wskazanie na rozliczenie rozrachunku
::  OLD: \op_proj/skid_prj.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<=0 || _c:=null ?};
_ma:=-POZ.STR='ma';
{? _a
|| {? _b || _kw:=POZ.SUMW; _kw2:=POZ.SUM || _kw:=POZ.SUM; _kw2:=POZ.SUMW ?};
   {? POZ.SLO_PROJ & (_ma & POZ.TID='ZOB' | ~_ma & POZ.TID='NAL')
   || OP_PROJ.index('OP'); OP_PROJ.prefix(OP.ref(),POZ.SLO_PROJ);
      {? {? OP_PROJ.first()
         || {? _ma || OP_PROJ.MA+=_kw || OP_PROJ.WN+=_kw ?};
            OP_PROJ.put()
         || OP_PROJ.blank(1);
            OP_PROJ.OP:=OP.ref();
            OP_PROJ.SLO_PROJ:=POZ.SLO_PROJ;
            {? _ma || OP_PROJ.MA:=_kw || OP_PROJ.WN:=_kw ?};
            OP_PROJ.WAL:=OP.WAL;
            OP_PROJ.add()
         ?}
      || ZAP_PROJ.prefix();
         ZAP_PROJ.blank(1);
         ZAP_PROJ.OP_PROJ:=OP_PROJ.ref();
         ZAP_PROJ.POZ:=POZ.ref();
         ZAP_PROJ.WAL:=OP_PROJ.WAL;
         {? _ma
         || ZAP_PROJ.MA:=_kw;
            ZAP_PROJ.MA2:=_kw2;
            ZAP_PROJ.SP_V_MA:=POZ.SP_V
         || ZAP_PROJ.WN:=_kw;
            ZAP_PROJ.WN2:=_kw2;
            ZAP_PROJ.SP_V_WN:=POZ.SP_V
         ?};
         ZAP_PROJ.KURS:=POZ.KURS;
         ZAP_PROJ.WAL2:={? OP_PROJ.WAL=FINFO.NAROD || POZ.WAL || FINFO.NAROD ?};
         ZAP_PROJ.REJ:=DOK.REJ;
         ZAP_PROJ.NR:=DOK.NR;
         ZAP_PROJ.NR_POZ:=POZ.POZ;
         ZAP_PROJ.DATA:={? POZ.DATA_R<>date(0,0,0)
                        || POZ.DATA_R
                        || exec('dat_otw','dok_fks')
                        ?};
         ZAP_PROJ.DKS:=DOK.DKS;
         ZAP_PROJ.NK:=DOK.NK;
         ZAP_PROJ.PAR_POZ:=_c;
         {? ZAP_PROJ.add()
         || exec('upd_op_proj','dok_proj',~_ma)
         ?}
      ?}
   |? POZ.SLO_PROJ=null & (~_ma & POZ.TID='ZOB' | _ma & POZ.TID='NAL')
   || exec('upd_op_proj','dok_proj',_ma)
   || 1
   ?}
|? POZ.TID='NAL' | POZ.TID='ZOB'
|| ZAP_PROJ.index('OP'); ZAP_PROJ.prefix(OP.ref(),POZ.ref(),_c);
   {? ZAP_PROJ.first()
   || OP_PROJ.prefix();
      {!
      |? ZAP_PROJ.OP_PROJ();
         OP_PROJ.WN-=ZAP_PROJ.WN;
         OP_PROJ.MA-=ZAP_PROJ.MA;
         {? OP_PROJ.WN=0 & OP_PROJ.MA=0
         || _del:=1
         || _del:=0;
            OP_PROJ.put()
         ?};
         _nxt:=ZAP_PROJ.del();
         {? _del & OP_PROJ.count()=0 || OP_PROJ.del() ?};
         _nxt
      !};
      exec('upd_op_proj','dok_proj',POZ.TID='NAL')
   ?}
?}


\upd_op_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Aktualizacja zapisow po 'drugiej' stronie
::   WE: _a - 1-ma 0-wn
::  OLD: \upd_op_proj/skid_prj.fml
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh();
_op_suma:=0;
ZAP_OP.cntx_psh(); ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
{? ZAP_OP.first()
|| {!
   |? {? _a & ZAP_OP.WN
      || _op_suma+=|ZAP_OP.WN
      |? ~_a & ZAP_OP.MA
      || _op_suma+=|ZAP_OP.MA
      ?};
      ZAP_OP.next()
   !}
?};
ZAP_OP.cntx_pop();
{? _op_suma
|| {? _a || ZAP_PROJ.index('MA') || ZAP_PROJ.index('WN') ?};
   ZAP_PROJ.prefix(OP.ref(),0);
   {? ZAP_PROJ.first()
   || ZAP_PROJ.prefix(OP.ref());
      {? ZAP_PROJ.first()
      || {!
         |? {? _a & (ZAP_PROJ.MA<>0 | ZAP_PROJ.WN=0) | ~_a & (ZAP_PROJ.WN<>0 | ZAP_PROJ.MA=0)
            || OP_PROJ.cntx_psh(); OP_PROJ.prefix();
               ZAP_PROJ.OP_PROJ();
               {? _a || OP_PROJ.MA-=ZAP_PROJ.MA || OP_PROJ.WN-=ZAP_PROJ.WN ?};
               OP_PROJ.put();
               _next:=ZAP_PROJ.del();
               {? OP_PROJ.WN=0 & OP_PROJ.MA=0 & OP_PROJ.count()=0 || OP_PROJ.del() ?};
               OP_PROJ.cntx_pop();
               _next
            || ZAP_PROJ.next()
            ?}
         !}
      ?};
      ZAP_OP.cntx_psh();
      ZAP_OP.index('OP'); ZAP_OP.prefix(OP.ref());
      _op:=OP.ref();
      {? ZAP_OP.first() & ZAP_PROJ.first()
      || _sum_str:=_sum:=0; _ost:=null;
         {!
         |? ZAP_PROJ.prefix(_op,0);
            {? (_a & ZAP_OP.MA | ~_a & ZAP_OP.WN) & ZAP_PROJ.first()
            || {!
               |? ZAP_PROJ.cntx_psh(); ZAP_PROJ.index('POZ'); ZAP_PROJ.prefix();
                  _str:={? _a || ZAP_PROJ.WN || ZAP_PROJ.MA ?};
                  _op_proj:=ZAP_PROJ.OP_PROJ;
                  _sum_str+=_str;
                  ZAP_PROJ.OP_PROJ();
                  {? ZAP_PROJ.find_key(_op_proj,ZAP_OP.POZDOK,ZAP_OP.PAR_POZ)
                  || _kw:=( {? _a || ZAP_OP.MA || ZAP_OP.WN ?}*_str/_op_suma)$2;
                     {? _a || ZAP_PROJ.MA+=_kw || ZAP_PROJ.WN+=_kw ?};
                     {? _a
                        || ZAP_PROJ.MA2+=(_kw*ZAP_PROJ.KURS)$2
                        || ZAP_PROJ.WN2+=(_kw*ZAP_PROJ.KURS)$2
                     ?};
                     OP_PROJ.cntx_psh(); OP_PROJ.prefix();
                     {? _a || OP_PROJ.MA+=_kw || OP_PROJ.WN+=_kw ?};
                     OP_PROJ.put();
                     OP_PROJ.cntx_pop();
                     _sum+=_kw;
                     {? ZAP_PROJ.put() || _ost:=ZAP_PROJ.ref() ?}
                  || ZAP_PROJ.blank();
                     ZAP_PROJ.WAL:=ZAP_OP.OP().WAL;
                     ZAP_PROJ.REJ:=ZAP_OP.REJ;
                     ZAP_PROJ.NR:=ZAP_OP.NR;
                     ZAP_PROJ.NR_POZ:=ZAP_OP.POZ;
                     ZAP_PROJ.DATA:=ZAP_OP.DATA;
                     ZAP_PROJ.OP_PROJ:=_op_proj;
                     ZAP_PROJ.POZ:=ZAP_OP.POZDOK;
                     ZAP_PROJ.PAR_POZ:=ZAP_OP.PAR_POZ;
                     {? _a
                     || _kw:=(ZAP_OP.MA*_str/_op_suma)$2;
                        ZAP_PROJ.MA:=_kw;
                        ZAP_PROJ.KURS:=ZAP_OP.KURS;
                        ZAP_PROJ.MA2:={? ZAP_OP.WAL2<>FINFO.NAROD & ZAP_PROJ.KURS || (_kw/ZAP_PROJ.KURS)$2 || (_kw*ZAP_PROJ.KURS)$2 ?};
                        ZAP_PROJ.WAL2:=ZAP_OP.WAL2
                     || _kw:=(ZAP_OP.WN*_str/_op_suma)$2;
                        ZAP_PROJ.WN:=_kw;
                        ZAP_PROJ.KURS:=ZAP_OP.KURS;
                        ZAP_PROJ.WN2:={? ZAP_OP.WAL2<>FINFO.NAROD & ZAP_PROJ.KURS || (_kw/ZAP_PROJ.KURS)$2 || (_kw*ZAP_PROJ.KURS)$2 ?};
                        ZAP_PROJ.WAL2:=ZAP_OP.WAL2
                     ?};
                     ZAP_PROJ.DKS:=ZAP_OP.DKS;
                     ZAP_PROJ.NK:=ZAP_OP.NK;
                     OP_PROJ.cntx_psh(); OP_PROJ.prefix();
                     {? _a
                     || OP_PROJ.MA+=_kw
                     || OP_PROJ.WN+=_kw
                     ?};
                     OP_PROJ.put();
                     OP_PROJ.cntx_pop();
                     _sum+=_kw;
                     {? ZAP_PROJ.add() || _ost:=ZAP_PROJ.ref() ?}
                  ?};
                  ZAP_PROJ.cntx_pop();
                  ZAP_PROJ.next()
               !};
               ZAP_PROJ.prefix();
               {? _sum_str=_op_suma & _sum<>{? _a || ZAP_OP.MA || ZAP_OP.WN ?} & _ost<>null & ZAP_PROJ.seek(_ost)
               || _kw:={? _a || ZAP_OP.MA || ZAP_OP.WN ?}-_sum;
                  {? _a
                  || ZAP_PROJ.WN+=_kw;
                     ZAP_PROJ.WN2+=(_kw*ZAP_PROJ.KURS)$2
                  || ZAP_PROJ.MA+=_kw;
                     ZAP_PROJ.MA2+=(_kw*ZAP_PROJ.KURS)$2
                  ?};
                  ZAP_PROJ.put()
               ?}
            ?};
            ZAP_OP.next()
         !}
      ?};
      ZAP_OP.cntx_pop()
   ?}
?};
OP.cntx_pop();
1


\trig_op_proj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Triger przed add i put tabeli OP_PROJ
::   WE: _a - 1-przed add, 2-przed put
::----------------------------------------------------------------------------------------------------------------------
SLU.cntx_psh();
SLO.cntx_psh();
_wz:=OP_PROJ.SLO_PROJ().SLU().WZ;
_kod:=OP_PROJ.SLO_PROJ().KOD;
SLO.cntx_pop();
SLU.cntx_pop();
{? _wz='Projekty_symbol' | _wz='Projekty_id'
|| _firma:=null;
   ROK_F.cntx_psh(); ROK_F.index('KODG'); ROK_F.prefix(OP_PROJ.name()+2);
   {? ROK_F.first() || _firma:=ROK_F.FIRMA ?};
   ROK_F.cntx_pop();
   {? _wz='Projekty_symbol'
   || PROJEKTY.index('SYM'); PROJEKTY.prefix(_kod,)
   || PROJEKTY.index('ID_KSG'); PROJEKTY.prefix(#_kod)
   ?};
   {? PROJEKTY.first()
   || {! |?
         {? PROJEKTY.FIRMA=_firma || OP_PROJ.PROJEKTY:=PROJEKTY.ref() ?};
         PROJEKTY.next()
      !}
   ?}
?};
1

:Sign Version 2.0 jowisz:1048 2020/10/16 15:22:52 aa32f0cadfffd01c3975af9adf49ff6e57944c437d04451a812431ccc7249a3e13f13c1f0f02d8dd2674888c639c52a22fd4794b3755954c3108ecab31a6b6f81d18dc07758bc23cd9946a300ef5348b437340e1098b55aa7645561815a81579e589a4581afa71dfe03f438f63e1c640fa9d04555a4dac96531c15bcde5c5bfb

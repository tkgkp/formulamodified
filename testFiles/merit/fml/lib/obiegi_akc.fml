:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obiegi.fml
:: Utworzony: 10.03.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa obiegów dokumentów - przekazywanie i akceptacja
::======================================================================================================================

\przekaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przekazanie dokumentu w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| _args:=params_get();
   _mp:=_args.mp;
   _we:=_args.in;
   params_exec('ustaw_bprel','obiegi');
   exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref(),OBIEGI.B_PREL,OBIEGI.B_PRELS);
   {? exec('ust_b_role','obiegi_akc',_we.EDOKUM,OBIEGI.B_PREL)=0 || return(~~) ?}
?};
{? app_info('web_sesid')='' & EDOKUM.sel_size() || return(0) ?};
VAR_DEL.delete('__mark'); __mark:=0;
_refb:=EDOKUM.ref();
_refn:=null;
{? app_info('web_sesid')='' & EDOKUM.f_active() & ~_mp.isAutoRun()
|| EDOKUM.f_rfresh();
   {? EDOKUM.f_next()
   || _refn:=EDOKUM.ref()
   || {? EDOKUM.f_prev()
      || _refn:=EDOKUM.ref()
      ?}
   ?};
   EDOKUM.f_seek(_refb)
?};
{? {? app_info('web_sesid')='' & EDOKUM.f_active() & ~_mp.isAutoRun() || EDOKUM.f_get() || EDOKUM.get() ?}
|| {? EDOKUM.r_lock(1,1,1)
   || _zwrot:=1;
      {? app_info('web_sesid')='' || params_exec('ustaw_bprel','obiegi') ?};
      {? exec('spr_edokrzap','obiegi',1) || _zwrot:=0 ?};
      {? _zwrot & EDOKUM.ZAM='T'
      || {? typobi=1
         || FUN.info('Faktura zamknięta.'@)
         |? typobi=2
         || FUN.info('Wniosek zamknięty.'@)
         |? typobi=3
         || FUN.info('Delegacja zamknięta.'@)
         ?};
         _zwrot:=0
      ?};
      {? _zwrot
      || {? app_info('web_sesid')<>''
         || B_USRROL.cntx_psh(); B_USRROL.index('B_ROLE'); B_USRROL.prefix(OBIEGI.B_ROLE);
            {? EDOKPAR.BLZMUSR='T'
            || exec('ustal_users','obiegi_akc',2);
               params_exec('przekaz1','obiegi_akc')
            || {? EDOKPAR.BLZMROL='T'
               || B_USRROL.web_win_init('WER_OBIW',,'grayed=R:R')
               || B_USRROL.web_win_init('WER_OBIW',,'grayed=')
               ?};
               {? EDOKPAR.JEDEN_OP='T'
               || B_USRROL.web_win_init('WER_OBIW',,'title=Wskaż użytkownika do akceptacji'@)
               || B_USRROL.web_win_init('WER_OBIW',,'title=Wskaż użytkowników do akceptacji'@)
               ?};
               exec('ustal_users','obiegi_akc',1);
               web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),
                                     'B_ROLE',OBIEGI.B_ROLE().NAME
                                     ));
               B_USRROL.web_select('WER_OBIW')
            ?};
            B_USRROL.cntx_pop()
         || _zwrot:=0;
            B_USRROL.cntx_psh(); B_USRROL.win_sel('WER_OBI');
            B_USRROL.index('B_ROLE'); B_USRROL.prefix(OBIEGI.B_ROLE);
            {? EDOKPAR.BLZMUSR='T'
            || exec('ustal_users','obiegi_akc',2); _zwrot:=1
            || _zmrol:={? EDOKPAR.BLZMROL='T' || 'R:R' || '' ?};
               B_USRROL.actions(B_USRROL.win_sel('?'),_zmrol,,1);
               {? EDOKPAR.JEDEN_OP='T'
               || B_USRROL.hdr_sel('Wskaż użytkownika do akceptacji'@)
               || B_USRROL.hdr_sel('Wskaż użytkowników do akceptacji'@)
               ?};
               {? B_USRROL.select(,1) || _zwrot:=1 || _mp.cancel() ?}
            ?};
            {? _zwrot || params_exec('przekaz1','obiegi_akc') ?};
            B_USRROL.cntx_pop()
         ?}
      ?};
      unlock_r()
   || exec('err_kom','obiegi')
   ?}
|| exec('errnodok','obiegi')
?};
exec('EDOKUM_to_send','portal_interm');
{? app_info('web_sesid')='' & EDOKUM.f_active() & ~_mp.isAutoRun()
|| EDOKUM.f_rfresh();
   {? ~EDOKUM.f_seek(_refb)
   || {? _refn<>null || EDOKUM.f_seek(_refn) ?}
   ?}
?};
VAR_DEL.delete('__mark')


\przekaz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przekazanie dokumentu w obiegu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| _args:=params_get();
   _mp:=_args.mp
?};
EDOKUSER.cntx_psh(); EDOKUSER.index('UNIK'); EDOKUSER.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
{? ~EDOKUSER.first()
|| FUN.info('Nie wybrano użytkowników.'@)
|? EDOKPAR.BLZMUSR<>'T' | (app_info('web_sesid')='' & _mp.isAutoRun()) |
   (app_info('web_sesid')='' & EDOKPAR.BLZMUSR='T' &
    FUN.ask('Zakończyć przekazanie dokumentu w obiegu użytkownikom?'@)
   )
|| params_exec('przekaz2','obiegi_akc')
|? app_info('web_sesid')<>'' & EDOKPAR.BLZMUSR='T'
|| params_exec('przekaz3','obiegi_akc')
?};
EDOKUSER.cntx_pop()


\przekaz2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przekazanie dokumentu w obiegu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
do();
EDOKUSER.index('UNIK'); EDOKUSER.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
{? EDOKUSER.first()
|| {! |?
      EDOKUSER.PRZEK:=0; EDOKUSER.WYB_AKC:=0; EDOKUSER.put(); EDOKUSER.next()
   !}
?};
EDOKOS.cntx_psh(); EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
{? EDOKOS.first()
|| EDOKOS.STATUS:='T';
   EDOKOS.DATA:=date();
   EDOKOS.TIME:=time();
   {? EDOKOS.put()
   || _log:={? typobi=1
            || 'Przekazanie faktury do akceptacji'@
            |? typobi=2
            || 'Przekazanie wniosku do akceptacji'@
            || 'Przekazanie delegacji do akceptacji'@
            ?};
      EDOKOS.prefix();
      params_exec('add_elog','obiegi_akc',_log,1);
      params_exec('par_out','obiegi_akc',form(EDOKUM.name(),8)+form(EDOKUM.UNIK_ID,15)+OBIEGI.B_PREL)
   ?}
?};
EDOKOS.cntx_pop();
end()


\przekaz3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przekazanie dokumentu w obiegu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
EDOKUSER.web_cntx_save(0);
_us:="{? _a
      || exec('env_edokum_get','obiegi');
         EDOKUSER.web_cntx_load(0);
         co_dalej:=''; _edokum:=EDOKUM.ref();
         _context:=obj_new('EDOKUM','AKCJA');
         _context.EDOKUM:=EDOKUM.ref();
         _context.AKCJA:='Przekaż2';
         co_dalej:=''; _edokum:=EDOKUM.ref();
         _refs:=exec('obj_ntab_set','#array',,'refs',EDOKUM.uidref());
         _biprels:=exec('getInstPrel4Ref','#b_keyref',_refs,{? typobi=1 || 'OBG_FZO_CPRZ'
                                                            |? typobi=2 || 'OBE_FAW_CPRZ'
                                                            || 'OBE_FDL_CPRZ' ?});
         exec('web_run','#b__box',_biprels[1],'Todo',,'web_Todo',,_context,,,,'T');
         _glo:=web_global_params_get();
         {? type_of(_glo)>100 & var_pres('todo_run',_glo)>0 & _glo.todo_run>0
         || {? exec('get_wt_par','obiegi2','dynamic')=1
            || web_def_close(web_top_win())
            |? typobi=1
            || EDOKUM.web_eclose('WF01')
            |? typobi=2
            || EDOKUM.web_eclose('WW03')
            || EDOKUM.web_eclose('WD03')
            ?};
            exec('bitodo_zakres_www','#bi_todo'); BI_TODO.first();
            _wer_akr:={? exec('lang','#bi_todo') || 'WER_WEBL' || 'WER_WEB' ?};
            BI_TODO.web_refresh(_wer_akr)
         ||
:           web_top_refresh(0);
            {? typobi=1
            || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
               || exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
                  exec('zakres_dob1','obiegi','P','F',_glo.zakrdp2,_glo.zakrdp1,1);
                  exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
                  exec('zakres_dob1','obiegi','A','F',_glo.zakrda2,_glo.zakrda1,1)
               ?};
               exec('activate','obiegi_akc',_edokum);
               _okno:={? PAR_SKID.get(80)='T' || 'WPRGF' || 'WPRGF1' ?};
               EDOKUMW.web_refresh(_okno,'PRZ');
               EDOKUMW.web_refresh(_okno,'AKC')
            |? typobi=2
            || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
               || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
                  exec('zakres_dob1','obiegi','R','W',_glo.zakrdr2,_glo.zakrdr1,1);
                  exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
                  exec('zakres_dob1','obiegi','P','W',_glo.zakrdp2,_glo.zakrdp1,1);
                  exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
                  exec('zakres_dob1','obiegi','A','W',_glo.zakrda2,_glo.zakrda1,1)
               ?};
               exec('activate','obiegi_akc',_edokum);
               EDOKUMW.web_refresh('WPRGW','WPR');
               EDOKUMW.web_refresh('WPRGW','PRZ');
               EDOKUMW.web_refresh('WPRGW','AKC')
            || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
               || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
                  exec('zakres_dob1','obiegi','R','D',_glo.zakrdr2,_glo.zakrdr1,1);
                  exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
                  exec('zakres_dob1','obiegi','P','D',_glo.zakrdp2,_glo.zakrdp1,1);
                  exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
                  exec('zakres_dob1','obiegi','A','D',_glo.zakrda2,_glo.zakrda1,1)
               ?};
               exec('activate','obiegi_akc',_edokum);
               EDOKUMW.web_refresh('WPRGD','WPR');
               EDOKUMW.web_refresh('WPRGD','PRZ');
               EDOKUMW.web_refresh('WPRGD','AKC')
            ?}
         ?};
         VAR_DEL.delete('co_dalej')
      ?}";
web_ask(_us,'Zakończyć przekazanie dokumentu w obiegu użytkownikom?'@,FUN.TYT)


\przekaz_todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przekazanie,akceptacja i dekretacja dokumentu w obiegu z listy czynności do wykonania
::   WE: _a - 'P' - przekaz
::            'A' - akceptacja
::            'D' - dekretacja
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
_we:=_args.in;
_wy:=_args.out;
edokum_ref:=EDOKUM.ref();
EDOKUM.cntx_psh();
exec('dok_edit','obiegi_akc',_a);
zawin:=0;
{? EDOKUM.EDOKUM || OBIEGI.PREVIEW:=EDOKUM.EDOKUM ?};
{! |?
   {? zawin || EDOKUM.get() ?};
   EDOKUM.display();
   {? zawin=2
   || params_exec('pozvdob','obiegi')
   |? zawin=3
   || params_exec('podz_dob','obiegi')
   |? zawin=4
   || exec('zalaczniki','obiegi')
   |? zawin=5
   || exec('inne_dane','obiegi')
   |? zawin=6
   || {? _a='P'
      || params_exec('przekaz','obiegi_akc')
      |? _a='A'
      || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
         params_exec('akceptuj','obiegi_akc','A',0)
      ?};
      zawin:=0
   |? zawin=7 & _a='A'
   || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      params_exec('odrzuc','obiegi_akc');
      zawin:=0
   |? zawin=7 & _a='D'
   || params_exec('odrzuc','obiegi_dekr');
      zawin:=0
   |? zawin=8 | zawin=9
   || {? typobi=1 | typobi=2
      || params_exec('dob_dek','obiegi_dekr',zawin=8)
      || params_exec('dob_dek_del','obiegi_dekr',1)
      ?};
      {? var_pres('dok_ref')>0 & dok_ref || zawin:=0 ?}
   |? zawin=10
   || params_exec('poz_del','obiegi')
   |? zawin=11
   || exec('okn_opis','obiegi')
   |? zawin=12
   || exec('pozf_edokum','obiegi')
   |? zawin=13
   || params_exec('zapprzeg','obiegi_zap')
   |? zawin=14
   || exec('dokumzbc_select','dok_fks1')
   ?};
   {? zawin || zawin:=0; 1 ?}
!};
EDOKUM.cntx_pop();
1


\dok_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Tworzy okno redagowania faktury w obiegu
::   WE: _a - 'P' - przekaz
::            'A' - akceptacja
::            'D' - dekretacja
::            'PDG' - podglad
::----------------------------------------------------------------------------------------------------------------------
{? typobi=1
|| _okn:=EDOKUM.mk_edit('Faktura w obiegu'@);
   {? EDOKUM.EDOKUM
   || EDOKUM.win_ewin(_okn,,'WPR_TEST');
      {? 'pdf;bmp;png;tiff;tif;jpeg;jpg'*(-EDOKUM.bl_info('EDOKUM','EXTENSION'))>0
      || OBIEGI.PREVIEW:=EDOKUM.EDOKUM
      || OBIEGI.bl_file('PREVIEW',1,'')
      ?}
   || EDOKUM.win_ewin(_okn,,'WPR')
   ?}
|? typobi=2
|| _okn:=EDOKUM.mk_edit('Wniosek w obiegu'@);
   EDOKUM.win_ewin(_okn,,'KSW')
|| _okn:=EDOKUM.mk_edit('Delegacja w obiegu'@);
   EDOKUM.win_ewin(_okn,,'KSD')
?};
{? var_press('typobi')<0 | typobi=1
||    _btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Poz. &faktury'@],
                   "zawin:=12; 'key:F2'");
   EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Pozycje faktury'@);
   _btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Poz. VAT'@],
                   "zawin:=2; 'key:F2'");
   EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Pozycje VAT dokumentu w obiegu'@)
|? typobi=3
|| EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Pozycje'@],
                   "zawin:=10; 'key:F2'")
?};
_btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Podział&y'@],
                "zawin:=3; 'key:F2'");
EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Podziały controllingowe dokumentu w obiegu'@);
{? _a<>'D'
|| _btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Informacje'@],
                         "zawin:=5; 'key:F2'");
   EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Informacje dodatkowe dokumentu w obiegu'@);
   _btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Załączniki'@],
                   "zawin:=4; 'key:F2'");
   EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Załączniki dokumentu w obiegu'@)
?};
{? typobi=1
|| _btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&E-dokument'@],
                   "exec('dspedob','obiegi'); ''");
   EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Wyświetlanie dokumentu źródłowego'@)
?};
{? typobi=1 & _a='A' & EDOKUM.TYP().ZAP_REAL='T'
|| _btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Zapotrzebowanie'@],
                   "zawin:=13; 'key:F2'");
   EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Etapy obiegu - szczegóły'@)
?};

_btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Historia'@],
                   "zawin:=11; 'key:F2'");
EDOKUM.btn_eopt(_okn,_btn,'tooltip='+'Etapy obiegu - szczegóły'@);
{? exec('upgrade2325_blbc1','zbl') & typobi=1 & exec('fin_dokumzbc_seek','zbl',EDOKUM)
|| btn:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['&Businesscheck'@],
                       "zawin:=14; 'key:F2'"
                       )
?};
{? _a='P'
|| EDOKUM.win_ebtn(_okn,
                   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Przekaż'@],
                   "zawin:=6; 'key:F2'")
|? _a='A'
|| EDOKUM.win_ebtn(_okn,
                   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Akceptuj'@],
                   "zawin:=6; 'key:F2'");
   EDOKUM.win_ebtn(_okn,
                   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Od&rzuć'@],
                   "zawin:=7; 'key:F2'")
|? _a<>'PDG'
|| EDOKUM.win_ebtn(_okn,
                   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Dekretuj'@],
                   "zawin:=9; 'key:F2'");
   exec('szuk_okr','okresy',EDOKUM.DOP);
   {? ROZNE.UT_OKROD<>null & typobi<>3
   || _okres:='Dekr. w %1'@[$ROZNE.UT_OKROD().NR+'\\'+ROZNE.UT_OKROD().ROK().NAZ];
      EDOKUM.win_ebtn(_okn,
                     'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'[_okres],
                     "zawin:=8; 'key:F2'")
   ?};

   EDOKUM.win_ebtn(_okn,
                   'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Od&rzuć'@],
                   "zawin:=7; 'key:F2'")
?};
{? _a<>'D' & _a<>'A' & _a<>'PDG'
|| _zapisz:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Zapisz'@],
   "'key:F2'");
   EDOKUM.btn_eopt(_okn,_zapisz,'tooltip='+exec('help_red_ok','#window','Z'))
?};
_anuluj:=EDOKUM.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],'key:Esc');
EDOKUM.btn_eopt(_okn,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
EDOKUM.win_edit(_okn);
1


\edokum_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamykanie okienka EDOKUM
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.web_eclose()


\par_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów wyjściowych czynności
::   WE: _a - pełny identyfikator (maska EDOKUM, UNIK_ID, B_PREL)
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
_we:=_args.in;
_wy:=_args.out;
{? _a<>''
|| _nam:=(8+_a);
   _id:=(15+(8-_a));
   _bprel:=(23-_a);
   {? _nam<>'' & _id<>'' & _bprel<>''
   || EDOKUM.cntx_psh(); EDOKUM.use(_nam); EDOKUM.index('UNIK_ID'); EDOKUM.prefix(_id,);
      {? EDOKUM.first()
      || EDOKUSER.cntx_psh(); EDOKUSER.use('skidk_'+(EDOKUM.name()+2));
         EDOKUSER.index('PRZEK'); EDOKUSER.prefix(EDOKUM.ref(),0,_bprel);
         {? EDOKUSER.first()
         || _wy.EDOKUM:=EDOKUM.ref();
            _wy.UNIK_ID:={? EDOKUSER.size()>1 || _a || '' ?};
            _wy.USERS:=EDOKUSER.USERS;
            {? var_pres('NETTO',_wy)>=0 || _wy.NETTO:=EDOKUM.NETTO ?};
            {? var_pres('BRUTTO',_wy)>=0 || _wy.BRUTTO:=EDOKUM.WART ?};
            {? var_pres('WAL',_wy)>=0 || _wy.WAL:=EDOKUM.WAL ?};
            _wy.B_PREL:=EDOKUSER.B_PREL;
            _wy.B_PRELS:=EDOKUSER.B_PRELS;
            _wy.DEKR:=EDOKUM.TYP().DEKR;
            _wy.STATUS:='A';
            _mp.save(,_wy);
            {? _wy.UNIK_ID<>'' || _mp.loop_continue() ?};
            EDOKUSER.PRZEK:=1;
            EDOKUSER.cntx_psh(); EDOKUSER.prefix(); EDOKUSER.put(); EDOKUSER.cntx_pop()
         || _wy.UNIK_ID:='';
            _wy.EDOKUM:=null;
            {? var_pres('NETTO',_wy)>=0 || _wy.NETTO:=EDOKUM.NETTO ?};
            {? var_pres('BRUTTO',_wy)>=0 || _wy.BRUTTO:=EDOKUM.WART ?};
            {? var_pres('WAL',_wy)>=0 || _wy.WAL:=EDOKUM.WAL ?};
            _wy.DEKR:=EDOKUM.TYP().DEKR;
            _wy.STATUS:='A';
            _mp.save(,_wy)
         ?};
         EDOKUSER.cntx_pop();
         _mp.done()
      ?};
      EDOKUM.cntx_pop()
   ?}
?}


\null_bprel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wyzerowanie danych dotyczących czynności w dokumencie
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOKUM.prefix(); EDOKUM.B_PREL:=EDOKUM.B_PRELS:=''; EDOKUM.put(); EDOKUM.cntx_pop()


\no_bprel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzenie, czy jest to ostatnia osoba akceptująca
::   WE: _a - B_PREL
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? type_of(_a)=type_of('') & _a<>''
|| EDOKOS.cntx_psh(); EDOKOS.use('skid_y'+(EDOKUM.name()+2));
   EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref(),_a);
   {? EDOKOS.first()
   || _zwrot:=1;
      {! |?
         {? EDOKOS.STATUS='N' || _zwrot:=0 ?};
         _zwrot & EDOKOS.next()
      !}
   ?};
   EDOKOS.cntx_pop()
?};
_zwrot


\zazn_us_obi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zaznaczanie użytkownika
::----------------------------------------------------------------------------------------------------------------------
{? ~B_USRROL.sel_size() & ~B_USRROL.sel_mark() || sel_add() ?};
1


\zazn_us_obi1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Odznaczanie użytkownika - grupa przed
::----------------------------------------------------------------------------------------------------------------------
{? B_USRROL.sel_mark() || B_USRROL.sel_del() ?};
0


\zazn_us_obi2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zaznaczanie użytkownika - grupa przed
::----------------------------------------------------------------------------------------------------------------------
{? B_USRROL.sel_size() & ~B_USRROL.sel_mark()
|| {? EDOKPAR.JEDEN_OP='T' ||  exec('czy_zazn_usrrol','obiegi2') ?};
   sel_add()
?};
0


\rkprz_obi_usrrol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Rekord przed - użytkownicy roli
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| _zmrol:={? EDOKPAR.BLZMROL='T' || 'R' || '' ?};
   _gray:='';
   {? B_USRROL.sel_mark()
   || B_USRROL.actions(B_USRROL.win_sel('?'),_zmrol,'O',1); _gray+='Z'
   || B_USRROL.actions(B_USRROL.win_sel('?'),_zmrol,'Z',1); _gray+='O';
      _gray+={? EDOKPAR.JEDEN_OP='T' & B_USRROL.sel_size()>1 || 'Z' || '' ?}
   ?};
   {? var_pres('typobi')>0 & typobi<>1 || _gray+='H' ?};
   B_USRROL.actions_grayed(B_USRROL.win_sel('?'),_gray);
   {? B_USRROL.sel_mark()
   || {? EDOKPAR.JEDEN_OP='T'
      || {? B_USRROL.sel_size()>1
         || OBIEGI.SEL_MARK:='N'
         || OBIEGI.SEL_MARK:='T'
         ?}
      || OBIEGI.SEL_MARK:='T'
      ?}
   || OBIEGI.SEL_MARK:='N'
   ?};
   {? var_pres('__lgtblbobg')>0 & __lgtblbobg.find_key(B_USRROL.USERS().KOD)
   || OBIEGI.LGHTBULB:='T'
   || OBIEGI.LGHTBULB:='N'
   ?}
?};
0


\zmrolus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zmiana roli
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| exec('env_edokum_get','obiegi');
   exec('tab_form','obiegi','B_ROLE','wyb_b_role','obiegi_akc','env_edokum_get1','obiegi');
   B_ROLE.index('UNIK'); B_ROLE.prefix(REF.FIRMA);
   _par:=web_global_params_get();
   {? ~(type_of(_par)>0 & var_pres('B_ROLE',_par)>0 & _par.B_ROLE<>'' & B_ROLE.find_key(_par.B_ROLE))
   || {? ~B_ROLE.seek(OBIEGI.B_ROLE) || B_ROLE.first() ?}
   ?};
   B_ROLE.web_select('WER_WT',,1)
|| B_ROLE.cntx_psh(); B_ROLE.index('UNIK'); B_ROLE.prefix(REF.FIRMA);
   _wer:=B_ROLE.mk_sel('Role'@);
   B_ROLE.win_fld(_wer,,'NAME',,,,,,,1);
   B_ROLE.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
   B_ROLE.win_sel(_wer);
   {? B_ROLE.select()
   || OBIEGI.B_ROLE:=B_ROLE.ref();
      B_USRROL.index('B_ROLE'); B_USRROL.prefix(OBIEGI.B_ROLE)
   ?};
   B_ROLE.cntx_pop()
?};
1


\wyb_b_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Wybór roli dla użytkownika
::----------------------------------------------------------------------------------------------------------------------
B_USRROL.index('B_ROLE'); B_USRROL.prefix(B_ROLE.ref());
web_global_params_set(exec('obj_ntab_set','#array',web_params_get(),
                      'B_ROLE',B_ROLE.NAME
                     ));
web_top_tab().web_close();
web_top_refresh(1,,,1);
1


\bgzmrolus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zmiana roli - grupa przed
::----------------------------------------------------------------------------------------------------------------------
exec('zmrolus','obiegi_akc'); 0


\op_usrrol_ob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Okienko przed ustalanie użytkowników
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lgtblbobg');
__lgtblbobg:=tab_tmp(,'USER','STRING[10]','USER');
B_USRROL.win_fml('WER_OBI',B_USRROL,'USERS','KOD','ICON_BEFORE',
                 "{? OBIEGI.SEL_MARK='T' || 'xwin16.png:13' || 'xwin16.png:14' ?}");
{? typobi=1 & exec('get','#params',300911,type_of('a'))='T' || exec('obg_akc_aut_zazn','tagger') ?};
B_USRROL.win_fml('WER_OBI',B_USRROL,'USERS','DANE','ICON_BEFORE',
                 "{? OBIEGI.LGHTBULB='T' || 'xwin16.png:57' || '' ?}");
exec('ustal_users','obiegi_akc',1)


\ustal_users
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.10]
:: OPIS: Ustalenie użytkowników - formułowo
::   WE: _a - 1 - okienko przed
:;            2 - bez wyboru użytkowników
::----------------------------------------------------------------------------------------------------------------------
B_USRROL.cntx_psh();
{? EDOKPAR.USR_FORM & B_USRROL.first()
|| B_USRROL.index('B_ROLE'); B_USRROL.prefix(OBIEGI.B_ROLE);
   no_msg(1); on_error(3); _ok_us:=1;
   _ref:=($(EDOKPAR.USR_FORM().TRESC))();
   {? in_error()
   || FUN.emsg('Formuła ustalająca użytkowników zawiera błąd.'@); _ok_us:=0
   ?};
   on_error(); no_msg(0);
   {? _ok_us & type_of(_ref)>100 & var_pres('REF_US',_ref)>0 & var_pres('KOD_US',_ref)>0 & _ref.first()
   || USERS.cntx_psh(); USERS.index('USR_KKOD'); USERS.prefix();
      EDOKUSER.cntx_psh(); EDOKUSER.index('UNIK');
      EDOKUSER.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
      {? EDOKUSER.first() || {! |? EDOKUSER.del() !} ?};
      {! |?
         {? ((_ref.REF_US & USERS.seek(_ref.REF_US,)) |
             (_ref.KOD_US<>'' & USERS.find_key(_ref.KOD_US,))) &
            B_USRROL.find_key(USERS.KOD,)
         || {? _a=1
            || {? app_info('web_sesid')<>''
               || B_USRROL.web_sel_init_add('WER_OBIW',,B_USRROL.ref())
               || sel_add()
               ?}
            || EDOKUSER.prefix(EDOKUM.ref(),OBIEGI.B_PREL,USERS.ref());
               {? ~EDOKUSER.first()
               || EDOKUSER.blank(1);
                  EDOKUSER.EDOKUM:=EDOKUM.ref();
                  EDOKUSER.B_PREL:=OBIEGI.B_PREL;
                  EDOKUSER.B_PRELS:=OBIEGI.B_PRELS;
                  EDOKUSER.USERS:=USERS.ref();
                  EDOKUSER.add(1)
               ?}
            ?}
         ?};
         _ref.next()
      !};
      USERS.cntx_pop(); EDOKUSER.cntx_pop()
   ?}
?};
B_USRROL.cntx_pop();
1


\zak_wyb_user
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zakończenie wyboru użytkowników
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| FUN.info('Nie wybrano użytkowników.'@)
|| {? ~B_USRROL.sel_size() || FUN.info('Nie wybrano użytkowników.'@) ?}
?};
1


\gb_zak_wyb_user
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zakończenie wyboru użytkowników - grupa przed
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? app_info('web_sesid')<>''
|| exec('env_edokum_get','obiegi');
   _tab:=web_sel_aget();
   {? _tab.size>1
   || EDOKPAR.index('UNIK'); EDOKPAR.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
      {? EDOKPAR.first()
      || {? EDOKPAR.JEDEN_OP='T'
         || FUN.info('Wybierz tylko jednego użytkownika do akceptacji.'@); _dalej:=0
         ?}
      ?}
   ?};
   {? _dalej & ~_tab.first()
   || FUN.info('Nie wybrano użytkowników.'@); _dalej:=0
   ?}
|| {? ~B_USRROL.sel_size()
   || FUN.info('Nie wybrano użytkowników.'@); _dalej:=0
   |? EDOKPAR.JEDEN_OP='T' & B_USRROL.sel_size()>1
   || FUN.info('Wybierz tylko jednego użytkownika do akceptacji.'@); _dalej:=0
   || _tab:=B_USRROL.sel_aget()
   ?}
?};
{? _dalej & _tab.first()
|| B_USRROL.cntx_psh(); B_USRROL.prefix();
   {! |?
      {? B_USRROL.seek(_tab.REF,)
      || {? typobi=1 & EDOKUM.ODD & ~exec('usr_fjks','b_perm',EDOKUM.ODD().OD,B_USRROL.USERS)
         || FUN.info('Użytkownik '@+B_USRROL.USERS().DANE+
                     '\nnie ma uprawnień do jednostki księgowej dokumentu ('@+EDOKUM.ODD().OD+').');
            _dalej:=0
         ?};
         {? _dalej & var_pres('typobi')>0 & typobi>0
         || {? typobi=1
            || {? ~exec('hasAction','users','OBG_FZO_EAKC',B_USRROL.USERS)
               || FUN.info('Użytkownik '@+B_USRROL.USERS().DANE+' nie ma uprawnień do akceptacji faktur w obiegu'@);
                  _dalej:=0
               ?}
            |? typobi=2
            || {? ~exec('hasAction','users','OBE_FAW_EAKP',B_USRROL.USERS)
               || FUN.info('Użytkownik '@+B_USRROL.USERS().DANE+' nie ma uprawnień do akceptacji wniosków w obiegu'@);
                  _dalej:=0
               ?}
            |? typobi=3
            || {? ~exec('hasAction','users','OBE_FDL_EBKP',B_USRROL.USERS)
               || FUN.info('Użytkownik '@+B_USRROL.USERS().DANE+' nie ma uprawnień do akceptacji delegacji w obiegu'@);
                  _dalej:=0
               ?}
            ?}
         ?}
      ?};
      _dalej & _tab.next()
   !};
   B_USRROL.cntx_pop()
?};
{? _dalej & _tab.first()
|| EDOKUSER.cntx_psh(); EDOKUSER.index('UNIK');
   EDOKUSER.prefix(EDOKUM.ref(),OBIEGI.B_PREL);
   {? EDOKUSER.first() || {! |? EDOKUSER.del() !} ?};
   B_USRROL.cntx_psh(); B_USRROL.prefix();
   {! |?
      {? B_USRROL.seek(_tab.REF,)
      || EDOKUSER.prefix(EDOKUM.ref(),OBIEGI.B_PREL,B_USRROL.USERS);
         {? ~EDOKUSER.first()
         || EDOKUSER.blank(1);
            EDOKUSER.EDOKUM:=EDOKUM.ref();
            EDOKUSER.B_PREL:=OBIEGI.B_PREL;
            EDOKUSER.B_PRELS:=OBIEGI.B_PRELS;
            EDOKUSER.USERS:=B_USRROL.USERS;
            EDOKUSER.add(1)
         ?}
      ?};
      _tab.next()
   !};
   EDOKUSER.cntx_pop(); B_USRROL.cntx_pop();

   EDOKPAR.cntx_psh();
   EDOKPAR.index('UNIK'); EDOKPAR.prefix(EDOKUM.ref(),OBIEGI.B_PREL,);
   {? EDOKPAR.first()
   || EDOKPAR.B_ROLE:=OBIEGI.B_ROLE;
      EDOKPAR.put()
   ?};
   EDOKPAR.cntx_pop();

   {? app_info('web_sesid')<>''
   || co_dalej:=''; _edokum:=EDOKUM.ref();
      _context:=obj_new('EDOKUM','AKCJA');
      _context.EDOKUM:=EDOKUM.ref();
      _context.AKCJA:='Przekaż2';
      _refs:=exec('obj_ntab_set','#array',,'refs',EDOKUM.uidref());
      _biprels:=exec('getInstPrel4Ref','#b_keyref',_refs,{? typobi=1 || 'OBG_FZO_CPRZ'
                                                         |? typobi=2 || 'OBE_FAW_CPRZ'
                                                         || 'OBE_FDL_CPRZ' ?});
      exec('web_run','#b__box',_biprels[1],'Todo',,'web_Todo',,_context,,,,'T');
      web_top_tab().web_close();
      _glo:=web_global_params_get();
      {? type_of(_glo)>100 & var_pres('todo_run',_glo)>0 & _glo.todo_run>0
      || {? exec('get_wt_par','obiegi2','dynamic')=1
         || web_def_close(web_top_win(1))
         |? typobi=1
         || EDOKUM.web_eclose('WF01')
         |? typobi=2
         || EDOKUM.web_eclose('WW03')
         || EDOKUM.web_eclose('WD03')
         ?};
         _wer_akr:={? exec('lang','#bi_todo') || 'WER_WEBL' || 'WER_WEB' ?};
         BI_TODO.web_refresh(_wer_akr)
      || {? typobi=1
         || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
            || exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
               exec('zakres_dob1','obiegi','P','F',_glo.zakrdp2,_glo.zakrdp1,1);
               exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
               exec('zakres_dob1','obiegi','A','F',_glo.zakrda2,_glo.zakrda1,1)
            ?};
            exec('activate','obiegi_akc',_edokum);
            _okno:={? PAR_SKID.get(80)='T' || 'WPRGF' || 'WPRGF1' ?};
            EDOKUMW.web_refresh(_okno,'PRZ');
            EDOKUMW.web_refresh(_okno,'AKC')
         |? typobi=2
         || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
            || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
               exec('zakres_dob1','obiegi','R','W',_glo.zakrdr2,_glo.zakrdr1,1);
               exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
               exec('zakres_dob1','obiegi','P','W',_glo.zakrdp2,_glo.zakrdp1,1);
               exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
               exec('zakres_dob1','obiegi','A','W',_glo.zakrda2,_glo.zakrda1,1)
            ?};
            exec('activate','obiegi_akc',_edokum);
            EDOKUMW.web_refresh('WPRGW','WPR');
            EDOKUMW.web_refresh('WPRGW','PRZ');
            EDOKUMW.web_refresh('WPRGW','AKC')
         || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
            || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
               exec('zakres_dob1','obiegi','R','D',_glo.zakrdr2,_glo.zakrdr1,1);
               exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
               exec('zakres_dob1','obiegi','P','D',_glo.zakrdp2,_glo.zakrdp1,1);
               exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
               exec('zakres_dob1','obiegi','A','D',_glo.zakrda2,_glo.zakrda1,1)
            ?};
            exec('activate','obiegi_akc',_edokum);
            EDOKUMW.web_refresh('WPRGD','WPR');
            EDOKUMW.web_refresh('WPRGD','PRZ');
            EDOKUMW.web_refresh('WPRGD','AKC')
         ?}
      ?};
      VAR_DEL.delete('co_dalej')
   ?}
?};
{? app_info('web_sesid')=''
|| {? _dalej || sel_exit() ?}; _dalej
|| 0
?}


\add_elog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [$1210]
:: OPIS: Dodawanie zapisow w tabeli logowan
::   WE:  _a  - opis operacji
::        _b  - 1/0 - czy przepisywac dlugie i krotkie uwagi
::       [_c] - użytkownik
::       [_d] - podów odrzucenia
::  OLD: \add_elog/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_usr:={? var_pres('_c')>0 || _c || OPERATOR.USER ?};
EDOKLOG.cntx_psh();
{? EDOKLOG.name()+2<>EDOKUM.name()+2
|| EDOKLOG.use('skid_d'+(EDOKUM.name()+2))
?};
EDOKLOG.prefix();
EDOKLOG.blank(1);
EDOKLOG.EDOKUM:=EDOKUM.ref();
EDOKLOG.OPIS:=_a;
{? _b || EDOKLOG.UW_KR:=EDOKOS.UW_KR ?};
EDOKLOG.DATA:=date();
EDOKLOG.TIME:=time();
EDOKLOG.USERS:=_usr;
EDOKLOG.B_PREL:=EDOKOS.B_PREL;
EDOKLOG.B_PRELS:=EDOKOS.B_PRELS;
{? EDOKLOG.add()
|| {? var_pres('_d')=type_of('')
   || EDOKLOG.memo_set(_d,'UW_DL');
      EDOKLOG.memo_put(,'UW_DL');
      EDOKLOG.put()
   |? _b
   || _f:=EDOKOS.memo_get('r','UW_DL');
      {? _f.is_open()
      || EDOKLOG.memo_put(_f,'UW_DL');
         _f.fclose()
      ?}
   ?}
?};
EDOKLOG.cntx_pop()


\be_i_usgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.10]
:: OPIS: Przed redakcja pola OBIEGI.B_ROLE
::  OLD: \be_i_usgr/skid_obi.fml
::----------------------------------------------------------------------------------------------------------------------
exec('szuk_edokpar','obiegi') & EDOKPAR.BLZMROL<>'T'


\ae_i_usgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.10]
:: OPIS: Po redakcji pola OBIEGI.B_ROLE
::----------------------------------------------------------------------------------------------------------------------
1


\aeomdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Po redakcji pola OBIEGI.OPER_AKC
::  OLD: \aeomdob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
1


\dspomdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [1210]
:: OPIS: Na wyswietl dla pola OBIEGI.OPER_AKC
::  OLD: \dspomdob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
1


\f3omdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Na klawisz F3 dla pola OBIEGI.OPER_AKC
::  OLD: \f3omdob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
B_USRROL.select();
1


\beomdob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed redakcją OBIEGI.OPER_AKC
::  OLD: \beomdob/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKPAR.BLZMUSR<>'T'


\upr_oper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności akceptacji
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_user_r:=params_get().user_r;
_mp:=params_get().mp;
_result:=1;
_wew:=_mp.load(exec('kind_internal','#b_port'));
{? var_pres('prz_act')>0 & var_pres('USERSN',_wew)>=0 & prz_act=2
|| _wew.USERSN:=_user; _mp.save(_wew)
?};
_release:={? Plugin.runnable('OB_AKC_PERM') &
             (var_pres('EDOKUM',_in)>0 & _in.EDOKUM | var_pres('EDOKUM',_out)>0 & _out.EDOKUM)
          || Plugin.run('OB_AKC_PERM',params_get())
          || 0
          ?};
::_co:='';
::USERS.cntx_psh(); USERS.prefix();
::{? _user<>~~ & _user & USERS.seek(_user) || _co+='User 1: '+USERS.DANE ?};
::{? _user_r<>~~ & _user_r & USERS.seek(_user_r) || _co+=', User 2: '+USERS.DANE ?};
::USERS.cntx_pop();
::msg(_co);
EDOKUM.cntx_psh();
{? var_pres('FORM_USR',_in)>0
|| _fml:=_in.FORM_USR;
   {? _fml<>'' & var_pres('EDOKUM',_in)>0
   || EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_in.EDOKUM));
      EDOKUM.prefix();
      {? EDOKUM.seek(_in.EDOKUM)
      || _ret:=($_fml)(_user);
         {? var_pres('_ret')>0
         || {? type_of(_ret)=type_of(1)
            || _result:=_ret
            |? type_of(_ret)=type_of(SYSLOG) & var_pres('REF_US',_ret) & type_of(_ret.REF_US)=type_of(1)
            || _result:=_ret.find_key(#_user)
            ?}
         ?}
      ?};
      EDOKUM.cntx_pop()
   ?}
|? (var_pres('zw_act')<=0 & var_pres('prz_act')<=0) | (var_pres('prz_act')>0 & prz_act>0)
|| {? var_pres('USERS',_in)>0 & _in.USERS
   || {? var_pres('pob_act')<=0 | (var_pres('pob_act')>0 & pob_act<>1)
      || _result:=(_in.USERS=_user);
         {? ~_result & _user_r<>~~
         || _result:=(_in.USERS=_user_r)
         ?};
         {? ~_result & var_pres('USERSN',_wew)>0 & _wew.USERSN
         || _result:=(_wew.USERSN=_user);
            {? ~_result & _user_r<>~~ || _wew.USERSN=_user_r ?}
         ?}
      ?};
      {? var_pres('B_PREL',_in)>0 & _in.B_PREL<>'' &
         var_pres('EDOKUM',_in)>0 & _in.EDOKUM & EDOKUM.seek(_in.EDOKUM,ref_name(_in.EDOKUM),1)
      || EDOKUSER.cntx_psh(); EDOKUSER.use('skidk_'+(EDOKUM.name()+2));
         EDOKUSER.index('UNIK'); EDOKUSER.prefix(EDOKUM.ref(),_in.B_PREL);
         {? EDOKUSER.first()
         || EDOKUSER.index('AKCEPT');
            EDOKUSER.prefix(EDOKUM.ref(),1,0,_in.B_PREL,_user);
            {? EDOKUSER.first() || EDOKUSER.prefix(); EDOKUSER.WYB_AKC:=1; EDOKUSER.put() ?}
         ?};
         EDOKUSER.cntx_pop()
      ?}
   |? var_pres('B_PREL',_in)>0 & _in.B_PREL<>'' &
      var_pres('EDOKUM',_in)>0 & _in.EDOKUM & EDOKUM.seek(_in.EDOKUM,ref_name(_in.EDOKUM),1)
   || EDOKUSER.cntx_psh(); EDOKUSER.use('skidk_'+(EDOKUM.name()+2));
      EDOKUSER.index('UNIK'); EDOKUSER.prefix(EDOKUM.ref(),_in.B_PREL);
      {? EDOKUSER.first()
      || EDOKUSER.prefix(EDOKUM.ref(),_in.B_PREL,_user);
         _result:=EDOKUSER.first();
         {? ~_result
         || {? _user_r<>~~
            || EDOKUSER.prefix(EDOKUM.ref(),_in.B_PREL,_user_r);
               _result:=EDOKUSER.first()
            || EDOKUSER.prefix(EDOKUM.ref(),_in.B_PREL);
               {? EDOKUSER.first()
               || _b_act:=exec('FindInSet','#table','B_ACTION','B_ELE',BI_PREL.B_PREL().B_ELE);
                  _b_role:=BI_PREL.B_PREL().B_ROLE;
                  {!
                  |? _result:=exec('zastepujacy','#users',,EDOKUSER.USERS,_b_act,,_b_role).USER=_user;
                     _result=0 & EDOKUSER.next()
                  !}
               ?}
            ?}
         ?}
      ?};
      EDOKUSER.cntx_pop()
   ?}
|? var_pres('zw_act')>0
|| {? _release
   || {? var_pres('B_PREL',_in)>0 & _in.B_PREL<>'' &
         var_pres('EDOKUM',_in)>0 & _in.EDOKUM & EDOKUM.seek(_in.EDOKUM,ref_name(_in.EDOKUM),1)
      || EDOKUSER.cntx_psh(); EDOKUSER.use('skidk_'+(EDOKUM.name()+2));
         EDOKUSER.index('UNIK'); EDOKUSER.prefix(EDOKUM.ref(),_in.B_PREL);
         {? EDOKUSER.first()
         || EDOKUSER.index('UNIK'); EDOKUSER.prefix(EDOKUM.ref(),_in.B_PREL,_user);
            {? EDOKUSER.first()
            || {? EDOKUSER.WYB_AKC=1
               || {? OPERATOR.USER=_user
                  || EDOKUSER.prefix(); EDOKUSER.WYB_AKC:=0; EDOKUSER.put()
                  || _result:=0
                  ?}
               ?}
            || _result:=0
            ?}
         ?};
         EDOKUSER.cntx_pop()
      ?}
   || {? var_pres('EDOKUM',_in)>0 & _in.EDOKUM & EDOKUM.seek(_in.EDOKUM,ref_name(_in.EDOKUM),1)
      || _result:=(_user & zw_act_usr=_user);
         {? ~_result & _user_r<>~~ || _result:=(zw_act_usr=_user_r) ?}
      |? var_pres('EDOKUM',_out)>0 & _out.EDOKUM & EDOKUM.seek(_out.EDOKUM,ref_name(_out.EDOKUM),1)
      || _result:=(_user & zw_act_usr=_user);
         {? ~_result & _user_r<>~~ || _result:=(zw_act_usr=_user_r) ?}
      ?}
   ?}
?};
{? _result & var_pres('EDOKUM',_in)>0 & _in.EDOKUM<>null
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(_in.EDOKUM));
   EDOKUM.prefix();
   {? EDOKUM.seek(_in.EDOKUM)
   || USERS.cntx_psh();
      USERS.prefix();
      {? USERS.seek(_user)
      || {? EDOKUM.TYP().W_PORTAL<>'N'
         || _result:=exec('wp_email','obiegi2')<>''
         ?};
         {? _result & exec('EDOKUM_typ','portal_seod','W') || _result:=exec('upr_p','obiegi_akc',_in,,,_mp) ?}
      ?};
      USERS.cntx_pop()
   ?};
   EDOKUM.cntx_pop()
?};
{? _result || _result:=params_exec('uprawnienia','obiegi',0) ?};
{? _result || _result:=exec('akt_rola_us','obiegi_akc',_user,_mp.b_prel) ?};
{? _result & _user_r<>~~ || _result:=exec('akt_rola_us','obiegi_akc',_user_r,_mp.b_prel) ?};
EDOKUM.cntx_pop();
_result


\akt_rola_us
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Sprawdza czy użytkownik _a ma aktywną rolę z B_PREL podanego parametrem _b
::   WE: _a - USERS.ref()
::       _b - B_PREL.ref()
::       _c - identyfikator czynności
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
B_PREL.cntx_psh(); B_PREL.index('UID'); B_PREL.prefix();
{? B_PREL.seek(_b)
|| B_ROLE.cntx_psh(); B_ROLE.index('UNIK'); B_ROLE.prefix();
   B_PREL.B_ROLE();
   USERS.cntx_psh(); USERS.index('USR_GUID'); USERS.prefix();
   {? USERS.seek(_a)
   || B_USRROL.cntx_psh(); B_USRROL.index('UNIK'); B_USRROL.prefix(REF.FIRMA,B_ROLE.ref(),USERS.ref());
      {? B_USRROL.first()
      || B_ELE.cntx_psh(); B_ELE.index('SYMBOL'); B_ELE.prefix();
         B_PREL.B_ELE();
         B_ACTION.cntx_psh(); B_ACTION.index('B_ELE'); B_ACTION.prefix(B_ELE.ref());
         _action:={? B_ACTION.first() || B_ACTION.ref() || null ?};
         B_ACTION.cntx_pop(); B_ELE.cntx_pop();
         ZAST_POZ.cntx_psh(); ZAST_POZ.index('USERS3');
         ZAST_NAG.cntx_psh(); ZAST_NAG.index('DATA_DO'); ZAST_NAG.prefix();
         {! |?
            {? ~B_USRROL.ZAST_NAG
            || _zwrot:=1
            |? _action
            || ZAST_POZ.prefix(REF.FIRMA,_a,_action,B_ROLE.ref());
               {? ZAST_POZ.last()
               || {! |?
                     ZAST_POZ.ZAST_NAG();
                     _zwrot:=(date()>=ZAST_NAG.DATA_OD & date()<=ZAST_NAG.DATA_DO);
                     ~_zwrot & ZAST_POZ.prev()
                  !}
               ?}
            ?};
            ~_zwrot & B_USRROL.next()
         !};
         ZAST_POZ.cntx_pop(); ZAST_NAG.cntx_pop()
      ?};
      B_USRROL.cntx_pop()
   ?};
   USERS.cntx_pop(); B_ROLE.cntx_pop()
?};
B_PREL.cntx_pop();
_zwrot


\akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja dokumentu w obiegu
::   WE: _a - 'A' - akceptacja
::            'W' - zakończenie po wprowadzaniu
::       _b - 1/0 - czy wyszukiwać EDOKUM
::----------------------------------------------------------------------------------------------------------------------
{? _=1 || _b:=1 ?};
{? app_info('web_sesid')=''
|| _args:=params_get();
   _mp:=_args.mp;
   _we:=_args.in;
   _wy:=_args.out;
   {? EDOKUM.sel_size() || return(0) ?}
?};
EDOKOS.cntx_psh(); EDOKOS.prefix();
{? ~_b |
   _b & {? app_info('web_sesid')='' & EDOKUM.f_active() || EDOKUM.f_get() || EDOKUM.get() ?}
|| {? EDOKUM.r_lock(1,1,1)
   || _zwrot:=1;
      {? app_info('web_sesid')<>'' || exec('rkprz1','obiegi',{? _a='W' || 1 || 2 ?}) ?};
      {? OBIEGI.EDOKOS=null
      || FUN.info('Dokument nie jest na etapie akceptacji.'@); _zwrot:=0
      || OBIEGI.EDOKOS()
      ?};
:: sprawdzanie statusu
      {? _zwrot & exec('spr_edokrzap','obiegi',1) || _zwrot:=0 ?};
      {? _zwrot & EDOKUM.ZAM='T'
      || {? typobi=1
         || FUN.info('Faktura zamknięta.'@)
         |? typobi=2
         || FUN.info('Wniosek zamknięty.'@)
         |? typobi=3
         || FUN.info('Delegacja zamknięta.'@)
         ?};
         _zwrot:=0
      ?};
      {? _zwrot
      ||
:: obiekt do sprawdzania i sprawdzenie etapu
         exec('spr_etap','obiegi_akc',1);
:: sprawdzanie projektów
         exec('spr_proj','obiegi_proj');
:: sprawdzanie podzialow
         exec('spr_podz','obiegi2',1);
:: sprawdzenie informacji dodatkowych
         exec('spr_inn_dan','obiegi2');
:: Sprawdzanie danych delegacyjnych
         {? typobi=3 || exec('spr_dan_del','obiegi') ?};
:: Sprawdzanie wymaganych załączników
         {? typobi=2 & EDOKUM.PAPERLES
         || exec('chk_zal','portal_paperless')
         ?};
         __OBISPR.prefix(1);
:: Błędy blokujące, akceptacja nie może być dokonana
         {? __OBISPR.first() || _zwrot:=0 ?};
         __OBISPR.prefix();
         {? app_info('web_sesid')<>''
         || EDOKOS.web_cntx_save(0)
         ?};
         {? __OBISPR.first()
         || _zwrot:=exec('okn_obispr','obiegi_akc',_a)
         |? app_info('web_sesid')<>''
         || {? _a='A'
            || params_exec('akceptuj1w','obiegi_akc',1)
            || params_exec('akceptuj1ww','obiegi_akc',0)
            ?}
         ?};
         {? _zwrot
         || {? app_info('web_sesid')<>''
            || _zwrot:=0
            ||  __OBISPR.prefix(3);
               {? __OBISPR.first() || _zwrot:=FUN.ask('Czy zaakceptować dokument pomimo uwag?'@) ?}
            ?}
         ?};
         {? _zwrot=0 & BPMN.END=1 || BPMN.END:=0 ?};
         exec('tab_con_del','obe_budz');
         VAR_DEL.delete('__OBISPR')
      ?};
      {? _zwrot & app_info('web_sesid')=''
      || params_exec('akceptuj1j','obiegi_akc',_a);
         {? EDOKUM.f_active() || EDOKUM.f_rfresh() ?}
      ?};
      unlock_r()
   || exec('err_kom','obiegi')
   ?}
|| exec('errnodok','obiegi')
?};
EDOKOS.cntx_pop();
VAR_DEL.delete('BUFEDS')


\akceptuj1j
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja dokumentu w obiegu - wewnętrzna - JTERM
::   WE: _a - 'A' - akceptacja
::            'W' - zakończenie po wprowadzaniu
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
_we:=_args.in;
OBIEGI.POPETAP:='';
{? var_pres('B_PRELS',_we)>0 & _we.B_PRELS<>~~
|| OBIEGI.POPETAP:=_we.B_PRELS
?};
{? exec('szuk_edokpar','obiegi') &
   (
      _a='W' & exec('edokuser_set','obiegi_akc',EDOKPAR.B_ROLE) |
      _a='A' & exec('kom_edit','obiegi_akc','A')
   ) &
   EDOKOS.put() & EDOKOS.memo_put(,'UW_DL') &
   {? typobi=1 || EDOKUM.put() || 1 ?}
|| params_exec('akceptuj2','obiegi_akc',_a)
?}


\spr_edokos
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdzanie parametrów EDOKOS
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? PAR_SKID.get(456)='T' & EDOKOS.UW_KR=''
|| FUN.info('Nie wypełniono pola "Uwagi".'@); _zwrot:='UW_KR'
?};
_zwrot


\akceptuj1w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja dokumentu w obiegu - wewnętrzna - WEBTERM
::   WE: _a - 0 - inna obsługa
::            1 - ta sama obsługa
::----------------------------------------------------------------------------------------------------------------------
_par:=web_global_params_get();
OBIEGI.POPETAP:='';
{? type_of(_par)>0 & var_pres('popetap',_par)>0
|| OBIEGI.POPETAP:=_par.popetap
?};
EDOKOS.get();
EDOKUM.use('skid_v'+(EDOKOS.name()+2)); EDOKUM.prefix(); EDOKOS.EDOKUM();
EDOKOS.prefix(); EDOKOS.memo_get(,'UW_DL');
EDOKUM.web_cntx_save(2);
TYPOBIEG.cntx_psh(); TYPOBIEG.prefix();
{? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur'
|| OBIEGI.TR:=EDOKUM.TR;
   _okno:='AKC_WTF';
   {? exec('szuk_edokpar','obiegi') & EDOKPAR.ED_OPOP='T'
   || EDOKOS.web_efld_init(_okno,,'mark=0, enable=1, editable=1',OBIEGI,'TR')
   || EDOKOS.web_efld_init(_okno,,'mark=0, enable=1, editable=0',OBIEGI,'TR')
   ?}
|| _okno:='AKC_WT'
?};
TYPOBIEG.cntx_pop();
{? PAR_SKID.get(456)='T'
|| EDOKOS.web_efld_init(_okno,,'mark=1',EDOKOS,'UW_KR')
|| EDOKOS.web_efld_init(_okno,,'mark=0',EDOKOS,'UW_KR')
?};
EDOKOS.web_edit(_okno,,,,"{? _a='OK'
                          || _chk:=exec('spr_edokos','obiegi_akc');
                             {? _chk<>''
                             || web_top_update(0,,,_chk)
                             || co_dalej:='';
                                EDOKUM.use('skid_v'+(EDOKOS.name()+2)); EDOKUM.prefix(); EDOKOS.EDOKUM();
                                _edokum:=EDOKUM.ref();
                                TYPOBIEG.cntx_psh(); TYPOBIEG.prefix();
                                {? EDOKUM.TYPOBIEG().NAZWA='Obieg faktur'
                                || EDOKUM.TR:=OBIEGI.TR; EDOKUM.put()
                                ?};
                                TYPOBIEG.cntx_pop();
                                {? EDOKOS.put() & EDOKOS.memo_put(,'UW_DL')
                                || exec('env_edokum_get','obiegi');
                                   _params:=exec('mp_run_a','#b__box');
                                   _params.ACT_UID:={? typobi=1
                                                    || 'OBG_FZO_EAKC'
                                                    |? typobi=2
                                                    || 'OBE_FAW_EAKP'
                                                    || 'OBE_FDL_EBKP'
                                                    ?};
                                   _params.AKCJA:='Akceptuj2';
                                   _params.UIDREF:=EDOKUM.uidref();
                                   _params.WT_SKIP:=1;
                                   _params.CONTEXT:=obj_new('EDOKUM');
                                   _params.CONTEXT.EDOKUM:=EDOKUM.ref();
                                   exec('mp_run','#b__box',_params)
                                ?};
                                EDOKOS.web_eclose();
                                _glo:=web_global_params_get();
                                {? type_of(_glo)>100 & var_pres('todo_run',_glo)>0 & _glo.todo_run>0
                                || {? exec('get_wt_par','obiegi2','dynamic')=1
                                   || web_def_close(web_top_win(1))
                                   |? typobi=1
                                   || EDOKUM.web_eclose('WF02')
                                   |? typobi=2
                                   || EDOKUM.web_eclose('WW04')
                                   || EDOKUM.web_eclose('WD04')
                                   ?};
                                   exec('bitodo_zakres_www','#bi_todo'); BI_TODO.first();
                                   _wer_akr:={? exec('lang','#bi_todo') || 'WER_WEBL' || 'WER_WEB' ?};
                                   BI_TODO.web_refresh(_wer_akr)
                                || {? typobi=1
                                   || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
                                      || exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
                                         exec('zakres_dob1','obiegi','P','F',_glo.zakrdp2,_glo.zakrdp1,1);
                                         exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
                                         exec('zakres_dob1','obiegi','A','F',_glo.zakrda2,_glo.zakrda1,1)
                                      ?};
                                      exec('activate','obiegi_akc',_edokum);
                                      _okno:={? PAR_SKID.get(80)='T' || 'WPRGF' || 'WPRGF1' ?};
                                      EDOKUMW.web_refresh(_okno,'PRZ');
                                      EDOKUMW.web_refresh(_okno,'AKC')
                                   |? typobi=2
                                   || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
                                      || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
                                         exec('zakres_dob1','obiegi','R','W',_glo.zakrdr2,_glo.zakrdr1,1);
                                         exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
                                         exec('zakres_dob1','obiegi','P','W',_glo.zakrdp2,_glo.zakrdp1,1);
                                         exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
                                         exec('zakres_dob1','obiegi','A','W',_glo.zakrda2,_glo.zakrda1,1)
                                      ?};
                                      exec('activate','obiegi_akc',_edokum);
                                      EDOKUMW.web_refresh('WPRGW','WPR');
                                      EDOKUMW.web_refresh('WPRGW','PRZ');
                                      EDOKUMW.web_refresh('WPRGW','AKC')
                                   || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
                                      || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
                                         exec('zakres_dob1','obiegi','R','D',_glo.zakrdr2,_glo.zakrdr1,1);
                                         exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
                                         exec('zakres_dob1','obiegi','P','D',_glo.zakrdp2,_glo.zakrdp1,1);
                                         exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
                                         exec('zakres_dob1','obiegi','A','D',_glo.zakrda2,_glo.zakrda1,1)
                                      ?};
                                      exec('activate','obiegi_akc',_edokum);
                                      EDOKUMW.web_refresh('WPRGD','WPR');
                                      EDOKUMW.web_refresh('WPRGD','PRZ');
                                      EDOKUMW.web_refresh('WPRGD','AKC')
                                   ?}
                                ?};
                                VAR_DEL.delete('co_dalej')
                             ?}
                          || EDOKOS.web_eclose()
                          ?}")


\akceptuj1ww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja dokumentu w obiegu przy kończeniu rejestracji - wewnętrzna - WEBTERM
::   WE: _a - 1/0 - które okienko na stosie
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
{? typobi=2
|| _params.ACT_UID:='OBE_FAW_DARP'
|| _params.ACT_UID:='OBE_FDL_DBRP'
?};
_params.AKCJA:='Zakończ';
_params.UIDREF:=EDOKUM.uidref();
_params.WT_SKIP:=1;
_params.CONTEXT:=obj_new('EDOKUM');
_params.CONTEXT.EDOKUM:=EDOKUM.ref();
co_dalej:=''; _edokum:=EDOKUM.ref();
exec('mp_run','#b__box',_params);
_par:=web_params_get();
_glo:=web_global_params_get();
{? type_of(_glo)>100 & var_pres('todo_run',_glo)>0 & _glo.todo_run>0 &
   ~(type_of(_par)>100 & var_pres('B_PREL',_par)>0 & _par.B_PREL<>'')
|| web_top_close(_a);
   exec('bitodo_zakres_www','#bi_todo'); BI_TODO.first();
   _wer_akr:={? exec('lang','#bi_todo') || 'WER_WEBL' || 'WER_WEB' ?};
   BI_TODO.web_refresh(_wer_akr)
|? type_of(_glo)>100 & var_pres('todo_run',_glo)>0 & _glo.todo_run<0
|| web_top_close(_a)
|| {? type_of(_par)>100 & var_pres('akcja',_par)>0 & (_par.akcja='dołącz' | _par.akcja='popraw')
   || web_top_close(_a)
   ?};
   {? typobi=2
   || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
      || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
         exec('zakres_dob1','obiegi','R','W',_glo.zakrdr2,_glo.zakrdr1,1);
         exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
         exec('zakres_dob1','obiegi','P','W',_glo.zakrdp2,_glo.zakrdp1,1);
         exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
         exec('zakres_dob1','obiegi','A','W',_glo.zakrda2,_glo.zakrda1,1)
      ?};
      {? ~(type_of(_glo)>100 & ((var_pres('path_run',_glo)>0 & _glo.path_run='Proc')
         | (var_pres('todo_run',_glo)>0 & _glo.todo_run>0)))
      || exec('activate','obiegi_akc',_edokum);
         EDOKUMW.web_refresh('WPRGW','WPR');
         EDOKUMW.web_refresh('WPRGW','PRZ');
         EDOKUMW.web_refresh('WPRGW','AKC')
      ?}
   |? typobi=3
   || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
      || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
         exec('zakres_dob1','obiegi','R','D',_glo.zakrdr2,_glo.zakrdr1,1);
         exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
         exec('zakres_dob1','obiegi','P','D',_glo.zakrdp2,_glo.zakrdp1,1);
         exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
         exec('zakres_dob1','obiegi','A','D',_glo.zakrda2,_glo.zakrda1,1)
      ?};
      exec('activate','obiegi_akc',_edokum);
      EDOKUMW.web_refresh('WPRGD','WPR');
      EDOKUMW.web_refresh('WPRGD','PRZ');
      EDOKUMW.web_refresh('WPRGD','AKC')
   ?}
?};
VAR_DEL.delete('co_dalej')


\akceptuj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja dokumentu w obiegu - wewnętrzna
::   WE: _a - 'A' - akceptacja
::            'W' - zakończenie po wprowadzaniu
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOKUM.prefix();
do();
:: zamykanie zapisow w tabeli EDOKOS
EDOKOS.prefix();
exec('generuj_zak_dok','obiegi_akc');
:: zamykanie dokumentow
{? _a='W' & EDOKUM.TYP().ZAPOT=2
|| EDOKUM.B_PRELS:=''
?};
{? EDOKUM.put()
|| {? var_pres('zak_akc_ed')>0 || zak_akc_ed:=1 ?}
|| undo()
?};
:: dodawanie zapisu w tabeli logowan
{? _a='A'
|| _log:={? typobi=1
         || 'Akceptacja faktury - akceptacja'@
         |? typobi=2
         || 'Akceptacja wniosku - akceptacja'@
         |? typobi=3
         || 'Akceptacja delegacji - akceptacja'@
         || ''
         ?}
|| _log:={? typobi=1
         || 'Rejestracja faktury - zakończenie'@
         |? typobi=2
         || 'Rejestracja wniosku - zakończenie'@
         |? typobi=3
         || 'Rejestracja delegacji - zakończenie'@
         || ''
         ?}
?};
EDOKOS.prefix();
exec('add_elog','obiegi_akc',_log,1);
end();
exec('EDOKUM_to_send','portal_interm');
EDOKUM.cntx_pop()


\activate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Aktywacja zakładki z przekazaniem bądź akceptacją
::   WE: _a - ref EDOKUM
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('co_dalej')>0 & _a &
  ((4+web_top_win(0,0))='WPRG' | (4+web_top_win(1,0))='WPRG' | (4+web_top_win(2,0))='WPRG')
|| _poziom:={? (4+web_top_win(0,0))='WPRG'
            || 0
            |? (4+web_top_win(1,0))='WPRG'
            || 1
            || 2
            ?};
   _tcid:=app_info('web_tcid');
   _tabid:=app_info('web_tabid');
   {? co_dalej='R'
   || EDOKUMW.index('DISP'); EDOKUMW.prefix(_tcid,_tabid,'R');
      {? exec('find_edokum','obiegi',_a) || web_top_activate(_poziom,'WPR') ?}
   |? co_dalej='P'
   || EDOKUMW.index('DISP'); EDOKUMW.prefix(_tcid,_tabid,'P');
      {? exec('find_edokum','obiegi',_a) || web_top_activate(_poziom,'PRZ') ?}
   |? co_dalej='A'
   || EDOKUMW.index('DISP'); EDOKUMW.prefix(_tcid,_tabid,'A');
      {? exec('find_edokum','obiegi',_a) || web_top_activate(_poziom,'AKC') ?}
   ?}
?}


\obiegi_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawia zmienną OBIEGI.TYP na podstawie refa SQL tabeli ETYPY
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.TYP:=null; OBIEGI.CZY_TYP:='W'; OBIEGI.CZY_ZAM:='N';
ETYPY.cntx_psh(); ETYPY.prefix();
{? _a<>'' & ETYPY.seek(_a) || OBIEGI.TYP:=ETYPY.ref() ?};
ETYPY.cntx_pop()


\okn_obispr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Okienko z komunikatami
::   WE: _a - 'A' - akceptacja
::            'O' - odrzucanie
::            'W' - zakończenie po wprowadzaniu
::  OLD: \okn_obispr/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
__OBISPR.prefix();
{? __OBISPR.first()
|| {? app_info('web_sesid')=''
   || _win:=__OBISPR.mk_sel('Komunikaty'@,,,'kom_spr_etap');
      __OBISPR.win_fld(_win,,'OPIS',,,80);
      __OBISPR.win_fld(_win,,'BLOKAKC',,,10,,,,,,2,,"1","0");
      __OBISPR.win_act(_win,,'Formuła','Szczegóły'@@,,,"exec('kom_details','obiegi_akc',__OBISPR.DETAILS)",,1,,,,'S');
      __OBISPR.win_act(_win,,'Formuła','Kontynuuj'@@,,,"sel_exit()",,1,,,,'K');
      __OBISPR.win_act(_win,,'Rekord',,,,"
         __OBISPR.cntx_psh();
         __OBISPR.prefix(1);
         _gray:={? __OBISPR.first() || 'K' || '' ?};
         __OBISPR.cntx_pop();
         {? __OBISPR.DETAILS=0
         || _gray+='S'
         ?};
         __OBISPR.actions_grayed(__OBISPR.win_sel('?'),_gray);
         0
      ");
      __OBISPR.win_btn(_win,'text='+'Szczegóły'@+',btn_label_align=center,panel=right,align=begin','menu:S');
      __OBISPR.win_btn(_win,'text='+'Kontynuuj'@+',btn_label_align=center,panel=right,align=begin','menu:K');
      __OBISPR.win_sel(_win);
      _ok:=__OBISPR.select();
      __OBISPR.win_sel();
      __OBISPR.win_del(_win);
      &_win;
      _ok
   || {? _a='A'
      || exec('tab_form','obiegi','TAB_WT','wyb_tab_wt1','obiegi_akc','bobs_tab_wt','obiegi_akc',1)
      |? _a='W'
      || exec('tab_form','obiegi','TAB_WT','wyb_tab_wt3','obiegi_akc','bobs_tab_wt','obiegi_akc',1)
      || exec('tab_form','obiegi','TAB_WT','wyb_tab_wt2','obiegi_akc','bobs_tab_wt','obiegi_akc',1)
      ?};
      _tcid:=app_info('web_tcid');
      _mbid:=app_info('web_mbid');
      _sesid:=app_info('web_sesid');
      _tabid:=app_info('web_tabid');
      TAB_WT.index('INDEX04'); TAB_WT.prefix(_tcid,_tabid,2);
      do();
      {? TAB_WT.first()
      || {! |?
            _delr:=TAB_WT.del(,1);
            {? _delr=1 || 0 |? _delr=0 || undo(); 0 || 1 ?}
         !}
      ?};
      {? __OBISPR.first()
      || {! |?
            TAB_WT.blank(1);
            TAB_WT.SESID:=_sesid;
            TAB_WT.MBID:=_mbid;
            TAB_WT.TCID:=_tcid;
            TAB_WT.TABID:=_tabid;
            TAB_WT.TYP:=2;
            TAB_WT.KOD:={? __OBISPR.BLOKAKC=1 || 'tak'@ || 'nie'@ ?};
            TAB_WT.BLOKAKC:=__OBISPR.BLOKAKC;
            TAB_WT.OPIS:=__OBISPR.OPIS;
            TAB_WT.DETAILS:=__OBISPR.DETAILS;
            TAB_WT.add();
            __OBISPR.next()
         !}
      ?};
      end();
      __OBISPR.cntx_psh();
      __OBISPR.prefix(1);
      {? __OBISPR.first()
      || TAB_WT.web_win_init('WER02',,'grayed=K')
      || TAB_WT.web_win_init('WER02',,'grayed=')
      ?};
      __OBISPR.cntx_pop();
      EDOKUM.web_cntx_save();
      TAB_WT.web_select('WER02')
   ?}
?};
_ok


\bobs_tab_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przed obsługą okienka wertowania tabeli TAB_WT - komunikaty o błędach
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.web_cntx_load();
exec('env_edokum_get1','obiegi');
1


\tab_wt02_arfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Po odświeżeniu okienka wertowania WER02 tabeli TAB_WT
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
_tcid:=app_info('web_tcid');
_tabid:=app_info('web_tabid');
TAB_WT.cntx_psh(); TAB_WT.index('INDEX04'); TAB_WT.prefix(_tcid,_tabid,2,1);
{? TAB_WT.first() || _gray+='K' ?};
TAB_WT.cntx_pop();
{? TAB_WT.DETAILS<>1 || _gray+='S' ?};
TAB_WT.web_win_opt(web_top_win(),,'grayed='+_gray);
1


\wyb_tab_wt1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na wybór rekordu w TAB_WT - akceptacja webterm
::----------------------------------------------------------------------------------------------------------------------
exec('env_edokum_get','obiegi');
EDOKOS.web_cntx_load(0);
TAB_WT.web_close();
params_exec('akceptuj1w','obiegi_akc',0);
1


\wyb_tab_wt2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na wybór rekordu w TAB_WT - odrzucanie webterm
::----------------------------------------------------------------------------------------------------------------------
exec('env_edokum_get','obiegi');
EDOKOS.web_cntx_load(0);
TAB_WT.web_close();
params_exec('odrzuc1w','obiegi_akc',0);
1


\wyb_tab_wt3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na wybór rekordu w TAB_WT - zakończenie rejestracji webterm
::----------------------------------------------------------------------------------------------------------------------
exec('env_edokum_get','obiegi');
EDOKOS.web_cntx_load(0);
TAB_WT.web_close();
params_exec('akceptuj1ww','obiegi_akc',1)


\spr_etap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Formula sprawdzajaca dane wypelnienia etapow.
::       Pole wynikowe BLOKAKC moze przyjmowac wartosc 1 jesli nie ma blokady dalszej akceptacji
::       lub 2 jesli jest blokada
::       jesli BLOKAKC=3 to mozliwa akceptacja po dodatkowym potwierdzeniu
::   WE: _a - 1 - uruchamianie formuły walidacyjnej akceptacji
::            2 - uruchamianie formuły walidacyjnej odrzucania
::            0 - bez uruchamiania formuły
::  OLD: \spr_etap/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
VAR_DEL.delete('__OBISPR');
exec('create_kom_tab','obiegi');
{? _a & exec('szuk_edokpar','obiegi')
|| {? _a=1 & EDOKPAR.FORM_WAL
   || _tab:=($EDOKPAR.FORM_WAL().TRESC)()
   |? _a=2 & EDOKPAR.FORMWODR
   || _tab:=($EDOKPAR.FORMWODR().TRESC)()
   ?};
   {? var_pres('_tab')=type_of(SYSLOG)
   || _tab.prefix();
      {? _tab.first()
      || {!
         |? exec('add_kom','obiegi',1,_tab.BLOKAKC,_tab.OPIS);
            _tab.next()
         !}
      ?}
   ?}
?};
EDOKUM.cntx_pop()


\generuj_zak_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamykanie zapisu na bieżącym poziomie przy akceptacji dokumentu
::----------------------------------------------------------------------------------------------------------------------
EDOKOS.STATUS:='T';
EDOKOS.DATA:=date();
EDOKOS.TIME:=time();
{? EDOKOS.put()
|| {? exec('szuk_edokpar','obiegi') & EDOKPAR.FORM_PRZ || ($EDOKPAR.FORM_PRZ().TRESC)() ?}
?}


\generuj_od_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamykanie zapisu na bieżącym poziomie przy odrzucaniu dokumentu w procesie akceptacji dokumentu
::----------------------------------------------------------------------------------------------------------------------
EDOKOS.STATUS:='O';
EDOKOS.WID:='N';
EDOKOS.DATA:=date(0,0,0);
EDOKOS.TIME:=time(0,0,0);
{? EDOKOS.put() & exec('szuk_edokpar','obiegi') & EDOKPAR.FORM_ODR
|| ($EDOKPAR.FORM_ODR().TRESC)()
?}


\ustaw_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów wyjściowych
::   WE: _a - obiekt wyjsciowy
::       _b - 'A' - akceptacja
::            'O' - odrzucenie
::            'Z' - zamknięcie
::----------------------------------------------------------------------------------------------------------------------
_a.EDOKUM:=EDOKUM.ref();
_a.USERS:=OPERATOR.USER;
_a.STATUS:=_b;
_a.DEKR:=EDOKUM.TYP().DEKR;
{? var_press('BUDZ_COND',_a)>0 & var_press('budz_cond')>0
|| _a.BUDZ_COND:=budz_cond
?};
{? var_pres('NETTO',_a)>=0 || _a.NETTO:=EDOKUM.NETTO ?};
{? var_pres('BRUTTO',_a)>=0 || _a.BRUTTO:=EDOKUM.WART ?};
{? var_pres('WAL',_a)>=0 || _a.WAL:=EDOKUM.WAL ?};
{? var_pres('EM_NAD',_a)>=0 & var_pres('EM_ODB',_a)>=0 & var_pres('EM_NAG',_a)>=0 & var_pres('EM_TR',_a)>=0
|| USERS.cntx_psh(); USERS.prefix();
   _a.EM_NAD:=_a.EM_ODB:=OPERATOR.USER().EMAIL;
   _a.EM_NAG:={? typobi=1
              || {? _b='A'
                 || 'Zaakceptowano fakturę: '
                 |? _b='O'
                 || 'Odrzucono fakturę: '
                 || 'Zamknięto fakturę: '
                 ?}+
                 EDOKUM.SYM+', data sprzedaży '+$EDOKUM.DOP+', otrzymano '+$EDOKUM.DO+
                 ', typ '+EDOKUM.TYP().NAZWA
              |? typobi=2
              || {? _b='A'
                 || 'Zaakceptowano wniosek: '
                 |? _b='O'
                 || 'Odrzucono wniosek: '
                 || 'Zamknięto wniosek: '
                 ?}+
                 EDOKUM.ID+', data operacji '+$EDOKUM.DOP+', typ '+EDOKUM.TYP().NAZWA
              || {? _b='A'
                 || 'Zaakceptowano delegację: '
                 |? _b='O'
                 || 'Odrzucono delegację: '
                 || 'Zamknięto delegację: '
                 ?}+EDOKUM.ID+', data polecenia '+$EDOKUM.DATAW+
                 ', delegowany '+EDOKUM.DOSTAWCA().NAZWISKO+' '+OSOBA.PIERWSZE+
                 ', typ '+EDOKUM.TYP().NAZWA
              ?};
   USERS.cntx_pop();
   _a.EM_TR:='';
   EDOKOS.use('skid_y'+(EDOKUM.name()+2));
   {? OBIEGI.EDOKOS
   || OBIEGI.EDOKOS();
      {? exec('szuk_edokpar','obiegi')
      || _a.B_PREL:=EDOKPAR.B_PREL;
         _a.B_PRELS:=EDOKPAR.B_PRELS
      ?};
       _a.EM_TR+=EDOKOS.UW_KR+'\n\n';
       EDOKOS.memo_get(,'UW_DL');
       _a.EM_TR+=EDOKOS.memo_txt(0,0,'UW_DL')
   ?}
?};
{? _b='A' & exec('no_bprel','obiegi_akc',EDOKUM.B_PREL) & typobi=1
|| exec('tagagr_csh_add','tagger',EDOKUM.uidref())
?};
{? (_b='A' & exec('no_bprel','obiegi_akc',EDOKUM.B_PREL) ) | _b='Z' || exec('null_bprel','obiegi_akc') ?}


\ustaw_outw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów wyjściowych przy zakończeniu rejestracji - wniosek
::   WE: _a - wskazanie na obiekt z parametrami wyjściowymi
::       _b - 'A' - akceptacja
::            'Z' - zamknięcie
::----------------------------------------------------------------------------------------------------------------------
_a.EDOKUM:=EDOKUM.ref();
_a.WAL:=EDOKUM.WAL;
_a.NETTO:=EDOKUM.NETTO;
_a.BRUTTO:=EDOKUM.WART;
_a.DEKR:=EDOKUM.TYP().DEKR;
_a.STATUS:=_b;
USERS.cntx_psh(); USERS.prefix();
_a.EM_NAD:=_a.EM_ODB:=OPERATOR.USER().EMAIL;
_a.EM_NAG:='Zarejestrowano wniosek: '+EDOKUM.ID+', data operacji '+$EDOKUM.DOP;
USERS.cntx_pop();
EDOKOS.use('skid_y'+(EDOKUM.name()+2));
exec('rkprz1','obiegi',1);
{? OBIEGI.EDOKOS
|| OBIEGI.EDOKOS();
   {? exec('szuk_edokpar','obiegi')
   || _a.B_PREL:=EDOKPAR.B_PREL;
      _a.B_PRELS:=EDOKPAR.B_PRELS
   ?}
?}


\ustaw_outd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametrów wyjściowych przy zakończeniu rejestracji - delegacja
::   WE: _a - wskazanie na obiekt z parametrami wyjściowymi
::       _b - 'A' - akceptacja
::            'Z' - zamknięcie
::----------------------------------------------------------------------------------------------------------------------
_a.EDOKUM:=EDOKUM.ref();
_a.DEKR:=EDOKUM.TYP().DEKR;
_a.STATUS:=_b;
USERS.cntx_psh(); USERS.prefix();
_a.EM_NAD:=_a.EM_ODB:=OPERATOR.USER().EMAIL;
_a.EM_NAG:='Zarejestrowano delegację: '+
           EDOKUM.ID+', data polecenia '+$EDOKUM.DATAW+', delegowany '+EDOKUM.DOSTAWCA().NAZWISKO+' '+OSOBA.PIERWSZE;
{? var_press('BUDZ_COND',_a)>0 & var_press('budz_cond')>0
|| _a.BUDZ_COND:=budz_cond
?};
USERS.cntx_pop();
EDOKOS.use('skid_y'+(EDOKUM.name()+2));
exec('rkprz1','obiegi',1);
{? OBIEGI.EDOKOS
|| OBIEGI.EDOKOS();
   {? exec('szuk_edokpar','obiegi')
   || _a.B_PREL:=EDOKPAR.B_PREL;
      _a.B_PRELS:=EDOKPAR.B_PRELS
   ?}
?}


\odrzuc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Odrzucanie dokumentu w obiegu przy akceptacji
::       _a - 1/0 - czy wyszukiwać EDOKUM
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=1 ?};
{? app_info('web_sesid')=''
|| _args:=params_get();
   _mp:=_args.mp;
   _we:=_args.in;
   _wy:=_args.out;
   {? EDOKUM.sel_size() || return(0) ?};
   EDOKUM.cntx_psh();
   EDOKOS.cntx_psh(); EDOKOS.prefix();
   _refb:=EDOKUM.ref();
   _refn:=null;
   {? EDOKUM.f_active()
   || EDOKUM.f_rfresh();
      {? EDOKUM.f_next()
      || _refn:=EDOKUM.ref()
      || {? EDOKUM.f_prev()
         || _refn:=EDOKUM.ref()
         ?}
      ?};
      EDOKUM.f_seek(_refb)
   ?}
?};
{? ~_a | _a & {? app_info('web_sesid')='' & EDOKUM.f_active() || EDOKUM.f_get() || EDOKUM.get() ?}
|| {? EDOKUM.r_lock(1,1,1)
   || _zwrot:=1;
      {? app_info('web_sesid')<>'' || exec('rkprz1','obiegi',zakres) ?};
      OBIEGI.EDOKOS();
:: sprawdzanie statusu
      {? exec('spr_edokrzap','obiegi',1) || _zwrot:=0 ?};
      {? _zwrot & EDOKUM.ZAM='T'
      || {? typobi=1
         || FUN.info('Faktura zamknięta.'@)
         |? typobi=2
         || FUN.info('Wniosek zamknięty.'@)
         |? typobi=3
         || FUN.info('Delegacja zamknięta.'@)
         ?};
         _zwrot:=0
      ?};
      {? _zwrot
      || exec('spr_etap','obiegi_akc',2);
         __OBISPR.prefix(1);
:: Błędy blokujące, odrzucenie nie może być dokonane
         {? __OBISPR.first() || _zwrot:=0 ?};
         __OBISPR.prefix();
         {? app_info('web_sesid')<>'' || EDOKOS.web_cntx_save(0) ?};
         {? __OBISPR.first()
         || _zwrot:=exec('okn_obispr','obiegi_akc','O')
         |? app_info('web_sesid')<>''
         || params_exec('odrzuc1w','obiegi_akc',1)
         ?};
         {? _zwrot
         || {? app_info('web_sesid')<>''
            || _zwrot:=0
            ||  __OBISPR.prefix(3);
               {? __OBISPR.first() || _zwrot:=FUN.ask('Czy odrzucić dokument pomimo uwag?'@) ?}
            ?}
         ?};
         VAR_DEL.delete('__OBISPR')
      ?};
      {? _zwrot=0 & BPMN.END=1 || BPMN.END:=0 ?};
      {? _zwrot
      || params_exec('odrzuc1j','obiegi_akc');
         {? EDOKUM.f_active()
         || EDOKUM.f_rfresh();
            {? ~EDOKUM.f_seek(_refb)
            || {? _refn<>null || EDOKUM.f_seek(_refn) ?}
            ?}
         ?}
      ?};
      unlock_r()
   || exec('err_kom','obiegi')
   ?}
|| exec('errnodok','obiegi')
?};
{? app_info('web_sesid')=''
|| EDOKOS.cntx_pop(); EDOKUM.cntx_pop()
?};
VAR_DEL.delete('BUFEDS')


\odrzuc1j
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Odrzucanie dokumentu w obiegu przy akceptacji - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
_args:=params_get();
_mp:=_args.mp;
_we:=_args.in;
OBIEGI.POPETAP:='';
{? var_pres('B_PRELS',_we)>0 & _we.B_PRELS<>~~
|| OBIEGI.POPETAP:=_we.B_PRELS
?};
BPMN.END:=0;
{? exec('szuk_edokpar','obiegi') & exec('kom_edit','obiegi_akc','O') &
   EDOKOS.put() & EDOKOS.memo_put(,'UW_DL')
|| params_exec('odrzuc2','obiegi_akc');
   BPMN.END:=1
?}


\odrzuc1w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Odrzucanie dokumentu w obiegu - wewnętrzna - WEBTERM
::   WE: _a - 0 - inna obsługa
::            1 - ta sama obsługa
::  OLD: \odrzuc1w/obiegi_akc.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=web_global_params_get();
OBIEGI.POPETAP:='';
{? type_of(_par)>0 & var_pres('popetap',_par)>0
|| OBIEGI.POPETAP:=_par.popetap
?};
: _l:=EDOKOS.web_cntx_load(0,_a);
EDOKUM.use('skid_v'+(EDOKOS.name()+2)); EDOKUM.prefix(); EDOKOS.EDOKUM();
EDOKOS.prefix(); EDOKOS.get(); EDOKOS.memo_get(,'UW_DL');
EDOKUM.web_cntx_save(2);
{? exec('szuk_edokpar','obiegi') & EDOKPAR.UWODRWYM='T'
|| EDOKOS.web_efld_init('AKC_WTO',,'mark=1',EDOKOS,'UW_KR'); _uw_wym:='T'
|| EDOKOS.web_efld_init('AKC_WTO',,'mark=0',EDOKOS,'UW_KR'); _uw_wym:='N'
?};
web_params_set(exec('obj_ntab_set','#array',web_params_get(),'uwodrwym',_uw_wym));
EDOKOS.web_edit('AKC_WTO',,,,"{? _a='OK'
                              || _chk:=''; _get:=web_params_get(); _glo:=web_global_params_get();
                                 {? type_of(_get)>100 & var_pres('uwodrwym',_get)>0 &
                                    _get.uwodrwym='T' & EDOKOS.UW_KR=''
                                 || FUN.info('Nie wypełniono pola \"Uwagi\".'@); _chk:='UW_KR'
                                 ?};
                                 {? _chk<>''
                                 || web_top_update(0,,,_chk)
                                 || co_dalej:='';
                                    EDOKUM.use('skid_v'+(EDOKOS.name()+2)); EDOKUM.prefix(); EDOKOS.EDOKUM();
                                    _edokum:=EDOKUM.ref(); EDOKOS.prefix();
                                    {? EDOKOS.put() & EDOKOS.memo_put(,'UW_DL')
                                    || exec('env_edokum_get1','obiegi');
                                       _params:=exec('mp_run_a','#b__box');
                                       _params.ACT_UID:={? typobi=1
                                                        || {? _glo.zakres=5
                                                           || 'OBG_FZO_CPRZ'
                                                           || 'OBG_FZO_EAKC'
                                                           ?}
                                                        |? typobi=2
                                                        || {? _glo.zakres=5
                                                           || 'OBE_FAW_CPRZ'
                                                           || 'OBE_FAW_EAKP'
                                                           ?}
                                                        || {? _glo.zakres=5
                                                           || 'OBE_FDL_CPRZ'
                                                           || 'OBE_FDL_EBKP'
                                                           ?}
                                                        ?};
                                       _params.AKCJA:='Odrzuc2';
                                       _params.UIDREF:=EDOKUM.uidref();
                                       _params.WT_SKIP:=1;
                                       _params.CONTEXT:=obj_new('EDOKUM');
                                       _params.CONTEXT.EDOKUM:=EDOKUM.ref();
                                       exec('mp_run','#b__box',_params)
                                    ?};
                                    {? var_pres('todo_run',_glo)>0 & _glo.todo_run>0
                                    || web_top_close(1)
                                    ?};
                                    web_top_close(0);
                                    {? var_pres('todo_run',_glo)>0 & _glo.todo_run>0
                                    || exec('bitodo_zakres_www','#bi_todo'); BI_TODO.first();
                                       _wer_akr:={? exec('lang','#bi_todo') || 'WER_WEBL' || 'WER_WEB' ?};
                                       BI_TODO.web_refresh(_wer_akr)
                                    || {? typobi=1
                                       || _okno:={? PAR_SKID.get(80)='T' || 'WPRGF' || 'WPRGF1' ?};
                                          {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
                                          || exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
                                             exec('zakres_dob1','obiegi','P','F',_glo.zakrdp2,_glo.zakrdp1,1);
                                             exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
                                             exec('zakres_dob1','obiegi','A','F',_glo.zakrda2,_glo.zakrda1,1)
                                          ?};
                                          exec('activate','obiegi_akc',_edokum);
                                          EDOKUMW.web_refresh(_okno,'PRZ');
                                          EDOKUMW.web_refresh(_okno,'AKC')
                                       |? typobi=2
                                       || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
                                          || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
                                             exec('zakres_dob1','obiegi','R','W',_glo.zakrdr2,_glo.zakrdr1,1);
                                             exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
                                             exec('zakres_dob1','obiegi','P','W',_glo.zakrdp2,_glo.zakrdp1,1);
                                             exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
                                             exec('zakres_dob1','obiegi','A','W',_glo.zakrda2,_glo.zakrda1,1)
                                          ?};
                                          exec('activate','obiegi_akc',_edokum);
                                          EDOKUMW.web_refresh('WPRGW','WPR');
                                          EDOKUMW.web_refresh('WPRGW','PRZ');
                                          EDOKUMW.web_refresh('WPRGW','AKC')
                                       || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
                                          || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
                                             exec('zakres_dob1','obiegi','R','D',_glo.zakrdr2,_glo.zakrdr1,1);
                                             exec('obiegi_typ','obiegi_akc',_glo.zakrdp3);
                                             exec('zakres_dob1','obiegi','P','D',_glo.zakrdp2,_glo.zakrdp1,1);
                                             exec('obiegi_typ','obiegi_akc',_glo.zakrda3);
                                             exec('zakres_dob1','obiegi','A','D',_glo.zakrda2,_glo.zakrda1,1)
                                          ?};
                                          exec('activate','obiegi_akc',_edokum);
                                          EDOKUMW.web_refresh('WPRGD','WPR');
                                          EDOKUMW.web_refresh('WPRGD','PRZ');
                                          EDOKUMW.web_refresh('WPRGD','AKC')
                                       ?}
                                    ?};
                                    VAR_DEL.delete('co_dalej')
                                 ?}
                              || EDOKOS.web_eclose()
                              ?}")


\odrzuc2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Odrzucanie dokumentu w obiegu przy akceptacji - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOKUM.prefix(); _ref:=null;
do();
exec('generuj_od_dok','obiegi_akc');
{? EDOKUM.put()
|| {? var_pres('zak_akc_ed')>0 || zak_akc_ed:=2 ?}
|| undo()
?};
_ref:=EDOKUM.ref();
:: dodawanie zapisu w tabeli logowan
_log:={? typobi=1
      || 'Odrzucenie faktury'
      |? typobi=2
      || 'Odrzucenie wniosku'
      |? typobi=3
      || 'Odrzucenie delegacji'
      || ''
      ?};
EDOKOS.prefix();
exec('add_elog','obiegi_akc',_log,1);
end();
{? _ref<>null & typobi=1
|| exec('tagagr_csh_del','tagger',_ref)
?};
exec('EDOKUM_to_send','portal_interm');
EDOKUM.cntx_pop()


\rek_obi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JSz [12.30]
:: OPIS: Rekord przed - dla tabeli tymczasowej komunikatów __OBISPR
::  OLD: \rek_obi/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? __OBISPR.BLOKAKC=1
|| 'OBISPR#01#01'
|| 'OBISPR#01#02'
?}


\kom_details
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Wyświetla informacje o szczegółach informacji/błędu
::   WE: _a - nr typu komunikatu
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
||
:: Kontrola budżetu
   exec('show_budzet2','obe_budz',app_info('web_sesid')<>'','',1)
?}


\br_tab_wt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed tabeli TAB_WT
::----------------------------------------------------------------------------------------------------------------------
TAB_WT.cntx_psh();
TAB_WT.index('INDEX04'); TAB_WT.prefix(TAB_WT.TCID,TAB_WT.TABID,TAB_WT.TYP,1);
_gray:={? TAB_WT.first() || 'K' || '' ?};
TAB_WT.cntx_pop();
{? TAB_WT.DETAILS=0
|| _gray+='S'
?};
TAB_WT.web_win_opt('WER02',,'grayed='+_gray);
~~


\edok_edokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Załącznik elektroniczny dokumentu w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUM.bl_file('EDOKUM')<>'' | EDOKUM.EDOKUM<>null
|| EDOKUM.web_bl_download('EDOKUM')
|| FUN.info('W dokumencie nie wybrano E-dokumentu.'@)
?}


\zamknij
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamykanie dokumentu w obiegu przy akceptacji
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')=''
|| {? EDOKUM.sel_size() || return(0) ?};
   EDOKUM.cntx_psh(); EDOKOS.cntx_psh(); EDOKOS.prefix();
   _refb:=EDOKUM.ref();
   _refn:=null;
   {? EDOKUM.f_active()
   || EDOKUM.f_rfresh();
      {? EDOKUM.f_next()
      || _refn:=EDOKUM.ref()
      || {? EDOKUM.f_prev()
         || _refn:=EDOKUM.ref()
         ?}
      ?};
      EDOKUM.f_seek(_refb)
   ?}
?};
{? {? app_info('web_sesid')='' & EDOKUM.f_active() || EDOKUM.f_get() || EDOKUM.get() ?}
|| {? EDOKUM.r_lock(1,1,1)
   || _zwrot:=1;
      OBIEGI.EDOKOS();
      {? _zwrot & EDOKUM.ZAM='T'
      || {? typobi=1
         || FUN.info('Faktura zamknięta.'@)
         |? typobi=2
         || FUN.info('Wniosek zamknięty.'@)
         |? typobi=3
         || FUN.info('Delegacja zamknięta.'@)
         ?};
         _zwrot:=0
      ?};
      {? _zwrot
      || {? app_info('web_sesid')<>''
         || {? zakres=1 || params_exec('zamknij1ww','obiegi_akc') ?}
         || params_exec('zamknij1j','obiegi_akc');
            {? EDOKUM.f_active()
            || EDOKUM.f_rfresh();
               {? ~EDOKUM.f_seek(_refb)
               || {? _refn<>null || EDOKUM.f_seek(_refn) ?}
               ?}
            ?}
         ?}
      ?};
      unlock_r()
   || exec('err_kom','obiegi')
   ?}
|| exec('errnodok','obiegi')
?};
{? app_info('web_sesid')=''
|| EDOKOS.cntx_pop(); EDOKUM.cntx_pop()
?}


\zamknij1j
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Kończenie dokumentu w obiegu przy akceptacji - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Operacja nieodwracalna.\nZamknąć obieg dokumentu?'@)
|| exec('zamknij2','obiegi_akc')
?}


\zamknij2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamykanie dokumentu w obiegu - wewnętrzna
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh(); EDOKUM.prefix();
do();
EDOKOS.prefix();
OBIEGI.EDOKOS();
EDOKOS.STATUS:='T'; EDOKOS.WID:='T';
EDOKOS.DATA:=date(0,0,0);
EDOKOS.TIME:=time(0,0,0);
EDOKUM.ZAM:='T';
{? EDOKOS.put() & EDOKUM.put()
|| {? var_pres('zak_akc_ed')>0 || zak_akc_ed:=3 ?};
   {? var_pres('zak_rej_ed')>0 || zak_rej_ed:=1 ?}
|| undo()
?};
_log:={? typobi=1
      || 'Zamknięcie obiegu faktury'
      |? typobi=2
      || 'Zamknięcie obiegu wniosku'
      |? typobi=3
      || 'Zamknięcie obiegu delegacji'
      || ''
      ?};
EDOKOS.prefix();
exec('add_elog','obiegi_akc',_log,1);
end();
EDOKUM.cntx_pop();
{? EDOKUM.f_active() || EDOKUM.f_rfresh() ?}


\zamknij1ww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Kończenie dokumentu w obiegu - wewnętrzna - WEBTERM - rejestracja
::----------------------------------------------------------------------------------------------------------------------
_par:=web_global_params_get();
OBIEGI.POPETAP:='';
{? type_of(_par)>0 & var_pres('popetap',_par)>0
|| OBIEGI.POPETAP:=_par.popetap
?};
EDOKOS.web_cntx_save();
EDOKUM.web_cntx_save();
EDOKUMW.web_cntx_save();
_zam:="{? _a
       || exec('env_edokum_get','obiegi');
          EDOKOS.web_cntx_load();
          EDOKUM.web_cntx_load();
          EDOKUMW.web_cntx_load();
          _params:=exec('mp_run_a','#b__box');
          _params.ACT_UID:={? typobi=2
                           || 'OBE_FAW_DARP'
                           || 'OBE_FDL_DBRP'
                           ?};
          _params.AKCJA:='Zamknij2';
          _params.UIDREF:=EDOKUM.uidref();
          _params.WT_SKIP:=1;
          _params.CONTEXT:=obj_new('EDOKUM');
          _params.CONTEXT.EDOKUM:=EDOKUM.ref();
          exec('mp_run','#b__box',_params);
         _glo:=web_global_params_get();
         {? typobi=2
         || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
            || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
               exec('zakres_dob1','obiegi','R','W',_glo.zakrdr2,_glo.zakrdr1,1)
            ?};
            EDOKUMW.web_refresh('WPRGW','WPR')
         || {? type_of(_glo)>100 & var_pres('zakrdr1',_glo)>0
            || exec('obiegi_typ','obiegi_akc',_glo.zakrdr3);
               exec('zakres_dob1','obiegi','R','D',_glo.zakrdr2,_glo.zakrdr1,1)
            ?};
            EDOKUMW.web_refresh('WPRGD','WPR')
         ?}
       ?}";
web_ask(_zam,'Zamknąć obieg dokumentu?'@,FUN.TYT)


\gatestate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Sprawdza, czy podczas akceptacji ma być puszczony dalej proces
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? type_of(params_get().in.p01)=2
|| _status:=params_get().in.p01;
   {? _status='O' | _status='Z'
   || _zwrot:=1
   || _tab:=params_get().mp.gate_state();
      _count:={? _tab.first() || _tab.COUNT ?};
      _zwrot:={? _tab.next() || _count=_tab.COUNT ?}
   ?}
?};
_zwrot


\web_top_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Zamykanie okienek wertowania
::----------------------------------------------------------------------------------------------------------------------
web_top_close()


\czy_wymoduw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Ustawianie parametru czynności - czy wymagane uzupełnienie uwag przy odrzuceniu
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Wymagane uzupełnienie uwag przy zakończeniu?'@,1,'Tak'@,'Nie'@);
{? _choice
|| {? _choice=1
   || 'Tak'
   |? _choice=2
   || 'Nie'
   ?}
|| ~~
?}


\czy_wympozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Ustawianie parametru czynności - czy wymagane uzupełnienie pozycji faktury
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Wymagane uzupełnienie pozycji faktury podczas akceptacji?'@,2,'Tak'@,'Nie'@);
{? _choice
|| {? _choice=1
   || 'Tak'
   |? _choice=2
   || 'Nie'
   ?}
|| ~~
?}


\czy_wympozv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Ustawianie parametru czynności - czy wymagane uzupełnienie pozycji VAT
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Wymagane uzupełnienie pozycji VAT podczas akceptacji?'@,1,'Tak'@,'Nie'@);
{? _choice
|| {? _choice=1
   || 'Tak'
   |? _choice=2
   || 'Nie'
   ?}
|| ~~
?}


\czy_ed_pozf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Ustawianie parametru czynności - czy edycja pozycji faktury podczas akceptacji
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Edycja pozycji faktury podczas akceptacji?'@,2,'Tak'@,'Nie'@);
{? _choice
|| {? _choice=1
   || 'Tak'
   |? _choice=2
   || 'Nie'
   ?}
|| ~~
?}


\czy_ed_pozv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [20.42]
:: OPIS: Ustawianie parametru czynności - czy edycja pozycji VAT podczas akceptacji
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice('Edycja pozycji VAT podczas akceptacji?'@,2,'Tak'@,'Nie'@);
{? _choice
|| {? _choice=1
   || 'Tak'
   |? _choice=2
   || 'Nie'
   ?}
|| ~~
?}


\wn_ob_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Akceptacja wniosków w obiegu
::   WE: _a - zadeklarowane i równe 1 - odpalanie z TODO na WEBTERM
::       _b - z okienka
::----------------------------------------------------------------------------------------------------------------------
_todo:={? var_pres('_a')>0 || _a || 0 ?};
_button:={? var_pres('_b')>0 || _b || 0 ?};
{? _todo|| exec('todo_run','obiegi',1); todo_run:=1;
   _dalej:=exec('env_edokum','obiegi','AW',0)
|| exec('todo_run','obiegi',0);
   _dalej:=exec('env_edokum','obiegi','AW',1);
   EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
   EDOKUM.prefix(); EDOKUMW.EDOKUM()
?};
VAR_DEL.delete('todo_run');
{? _dalej
|| _dalej:=exec('is_act_user','#b__box','OBE_FAW_EAKP',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
   {? ~_dalej
   || FUN.info('Akceptacja wniosku nie jest na liście zadań użytkownika.'@)
   ?}
?};
{? _dalej
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FAW_EAKP';
   _params.AKCJA:='chkWT';
   _params.UIDREF:=EDOKUM.uidref();
   _params.WT_SKIP:=1;
   _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
   _params.CONTEXT.EDOKUM:=EDOKUM.ref();
   _params.CONTEXT.SPR_WT:=0;
   exec('maski_w','obiegi',1);
   exec('mp_run','#b__box',_params);
   {? _params.CONTEXT.SPR_WT
   || BPMN.END:=1;
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      exec('rkprz1','obiegi',2); OBIEGI.EDOKOS();
      {? _button | ~_todo
      || params_exec('akceptuj','obiegi_akc','A')
      || EDOKOS.use('skid_y'+(EDOKUM.name()+2));
         exec('rkprz1','obiegi',1); OBIEGI.EDOKOS();
         params_exec('pop_dob_wt','obiegi',2,_todo,'A')
      ?}
   ?}
?}


\wn_ob_prz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Przekazanie wniosków w obiegu - przycisk w oknie
::   WE: _a - zadeklarowane i równe 1 - odpalanie z TODO na WEBTERM
::       _b - z okienka
::----------------------------------------------------------------------------------------------------------------------
_todo:={? var_pres('_a')>0 || _a || 0 ?};
_button:={? var_pres('_b')>0 || _b || 0 ?};
{? _todo|| exec('todo_run','obiegi',1); todo_run:=1;
   EDOKUM.web_cntx_save(0);
   _dalej:=exec('env_edokum','obiegi','PW',0)
|| exec('todo_run','obiegi',0);
   EDOKUM.web_cntx_save(0);
   EDOKOS.web_cntx_save(0);
   _dalej:=exec('env_edokum','obiegi','PW',1);
   EDOKUMW.EDOKUM()
?};
VAR_DEL.delete('todo_run');
{? _dalej
|| _dalej:=exec('is_act_user','#b__box','OBE_FAW_CPRZ',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
   {? ~_dalej
   || FUN.info('Przekazanie wniosku nie jest na liście zadań użytkownika.'@)
   ?}
?};
{? _dalej
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FAW_CPRZ';
   _params.AKCJA:='chkWT';
   _params.UIDREF:=EDOKUM.uidref();
   _params.WT_SKIP:=1;
   _params.CONTEXT:=obj_new('EDOKUM','SPR_WT');
   _params.CONTEXT.EDOKUM:=EDOKUM.ref();
   _params.CONTEXT.SPR_WT:=0;
   exec('maski_w','obiegi',1);
   exec('mp_run','#b__box',_params);
   {? _params.CONTEXT.SPR_WT
   || {? _button | ~_todo
      || params_exec('przekaz','obiegi_akc')
      || _red:=exec('win_red_wnioski','obiegi2','P',,0);
         web_def_fld_opt(_red, 'editable=0',);
         EDOKUM.web_cntx_save();
         web_def_disp(_red,"
            EDOKUM.web_cntx_load();
            exec('inne_akcje','obiegi2',_a,0)
         ",'Wniosek w obiegu'@)
      ?}
   ?}
?}


\kom_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Edycja komentarza przy akceptac, odrzuceniu lub zamknięciu
::   WE: _a - tryb
::----------------------------------------------------------------------------------------------------------------------
_tryb:=_a;
VAR_DEL.delete('KomWym');
KomWym:=obj_new('WYM','WP','TRYB','ED_SEOD','NIP','KH');
KomWym.WP:='TXS'*EDOKUM.TYP().W_PORTAL;
KomWym.TRYB:=_tryb;
KomWym.ED_SEOD:=EDOKUM.TYP().W_PORTAL='S' & EDOKPAR.ED_OPOP='T';

:: 1-pytanie, 2-dialog, 3-dialog z wymaganym komentarzem
KomWym.WYM:={? KomWym.WP
            || _par:={? EDOKUM.TYP().W_PORTAL='S' || 465 || 390 ?};
               _obj:=exec('obj_par','portal_wnioski',PAR_SKID.get(_par));
               {? _tryb='A'
               || _obj.PA
               |? _tryb='O'
               || _obj.PO
               |? _tryb='Z'
               || _obj.PZ
               ?}
            || {? _tryb='A'
               || 2+(PAR_SKID.get(456)='T')
               |? _tryb='O'
               || 2+(EDOKPAR.UWODRWYM='T')
               || 1
               ?}
            ?};
{? KomWym.WYM=1
|| _txt:={? _tryb='A'
         || {? typobi=1 || 'Czy zaakceptować fakturę?'@
            |? typobi=2 || 'Czy zaakceptować wniosek?'@
            || 'Czy zaakceptować dokument?'@
            ?}
         |? _tryb='O'
         || {? typobi=1 || 'Czy odrzucić fakturę?'@
            |? typobi=2 || 'Czy odrzucić wniosek?'@
            || 'Czy odrzucić dokument?'@
            ?}
         |? _tryb='Z'
         || 'Operacja nieodwracalna.\nCzy zamknąć obieg dokumentu?'@
         || ''
         ?};
   _edit:=FUN.ask(_txt);
   {? _edit
   || EDOKOS.memo_get(,'UW_DL')
   ?}
|| _tyt:={? _tryb='A' || 'Akceptacja' |? _tryb='O' || 'Odrzucenie' || 'Zamykanie obiegu' ?};
   _win:=EDOKOS.mk_edit(_tyt,,'obeakckom');
   EDOKOS.win_esep(_win,'Czynność'@);
   {? KomWym.WP=0
   || EDOKOS.win_efld(_win,OBIEGI,'POPETAP',,,96,,1,'Poprzednia'@)
   ?};
   EDOKOS.win_efld(_win,EDOKUM,'B_PRELS',,,96,,1,'Bieżąca'@);
   {? typobi=1 & _tryb='A'
   || {? KomWym.ED_SEOD
      || EDOKOS.win_esep(_win,'Kontrahent'@);
         SKID.SL_KH:=EDOKUM.TYP().DOM_SL().SLU().NAZ;
         EDOKOS.win_efld(_win,SKID,'NIP',,,93,,,'NIP'@,,,,'F3_button=1');
         SKID.fld_fml('NIP','F3',"exec('aenipdob','obiegi',1)");
         EDOKOS.win_efld(_win,EDOKUM,'KH','KOD','SL',93,,,'Kod'@);
         EDOKOS.efld_opt(_win,'mark=1',EDOKUM,'KH','KOD');
         EDOKOS.win_efld(_win,EDOKUM,'KH_FULL',,,96,,,'Pełna nazwa'@)
      ?};
      EDOKOS.win_esep(_win,'Dokument'@);
      EDOKOS.win_efld(_win,EDOKUM,'TR',,,96,,,'Opis operacji'@);
      {? KomWym.ED_SEOD
      || EDOKOS.win_efld(_win,EDOKUM,'DOP',,,,,1,'Data sprzedaży'@);
         EDOKOS.win_efld(_win,EDOKUM,'PLATNOSC','KOD','SL',93,,,'Sposób płatności'@);
         EDOKOS.efld_opt(_win,'mark=1',EDOKUM,'PLATNOSC','KOD');
         EDOKOS.win_efld(_win,EDOKUM,'PLATNOSC','TR',,93,,,,1);
         EDOKOS.win_efld(_win,EDOKUM,'TP',,,,,,'Termin płatności'@);
         EDOKOS.efld_opt(_win,'mark=1',EDOKUM,'TP')
      ?}
   ?};
   EDOKOS.win_esep(_win,'Dane etapu'@);
   {? KomWym.WP=0
   || EDOKOS.win_efld(_win,,'UW_KR',,,96,,,'Uwagi'@)
   ?};
   EDOKOS.win_efld(_win,,'UW_DL',,,96,-7,,'Szczegóły'@);
   _btn:={? _tryb='A' || 'Wyślij'@ |? _tryb='O' || 'Odrzuć'@ || 'Zamknij'@ ?};
   EDOKOS.win_ebtn(_win,'text=%1'[_btn],"'key:F2'");
   _anuluj:=EDOKOS.win_ebtn(_win,'text=%1'['Anuluj'@],"'key:Esc'");
   EDOKOS.btn_eopt(_win,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   EDOKOS.win_edit(_win);
   {? KomWym.WP=0
   || EDOKOS.efld_opt(_win,'mark='+$(KomWym.WYM=3),EDOKOS,'UW_KR')
   || EDOKOS.efld_opt(_win,'mark='+$(KomWym.WYM=3),EDOKOS,'UW_DL')
   ?};
   EDOKOS.memo_get(,'UW_DL');
   OBIEGI.B_ROLE:=null;
   {? typobi=1 | typobi=2
   || {? EDOKPAR.B_ROLE
      || OBIEGI.B_ROLE:=EDOKPAR.B_ROLE
      ?}
   ?};
   {? typobi=1 & OBIEGI.B_ROLE & 0
   || B_USRROL.win_sel('WER_OBI');
      B_USRROL.index('B_ROLE'); B_USRROL.prefix(OBIEGI.B_ROLE);
      B_USRROL.first();
      B_USRROL.hdr_sel(); B_USRROL.hdr_sel('Przekazanie do akceptacji'@);
      _grp:=EDOKOS.grp_make('Akceptacja'@,"
         grp_disp(B_USRROL,B_USRROL.win_sel('?'))
      ",'obgakckom',,,,,'normal');
      EDOKOS.grp_edit(_grp,,_win);
      EDOKOS.win_sel(_grp);
      EDOKOS.grp_splt(_grp,,'horizontal','bottom');
      EDOKOS.grp_sel(_grp,B_USRROL,'WER_OBI',,,,,,,,,,'maximized_with_title');
      EDOKOS.select()

   || KomWym.NIP:=SKID.NIP;
      KomWym.KH:=EDOKUM.KH;
      _edit:=EDOKOS.edit("
         _ret:='';
         {? KomWym.ED_SEOD & KomWym.TRYB='A'
         || {? ~EDOKUM.KH
            || FUN.info('Nie wypełniono pola: \"%1\".'@['Kod']);
               _ret:='KH'
            ?};
            {? _ret='' & KomWym.NIP<>SKID.NIP
            || SKID.NIP:=KomWym.NIP;
               EDOKUM.KH:=KomWym.KH;
               win_disp();
               FUN.info('Nie można zmieniać NIP-u kontrahenta.'@);
               _ret:='NIP'
            ?};
            {? _ret='' & ~EDOKUM.PLATNOSC
            || FUN.info('Nie wypełniono pola: \"%1\".'@['Sposób płatności']);
               _ret:='PLATNOSC'
            ?};
            {? _ret='' & EDOKUM.TP=date(0,0,0)
            || FUN.info('Nie wypełniono pola: \"%1\".'@['Termin płatności']);
               _ret:='TP'
            ?}
         ?};
         {? _ret='' & KomWym.WYM=3
         || {? KomWym.WP=0
            || {? EDOKOS.UW_KR=''
               || FUN.info('Nie wypełniono pola: \"%1\".'@['Uwagi']);
                  _ret:='UW_KR'
               ?}
            || {? EDOKOS.memo_txt(,0,'UW_DL')=''
               || FUN.info('Nie wypełniono pola: \"%1\".'@['Szczegóły']);
                  _ret:='UW_DL'
               ?}
            ?}
         ?};
         _ret
      ");
      {? _edit & _tryb='A'
      || _edit:=exec('edokuser_set','obiegi_akc',OBIEGI.B_ROLE)
      ?};
      {? _edit || _edit:=2 ?}
   ?}
?};
VAR_DEL.delete('KomWym');
_edit


\ust_b_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Ustawia rolę dla przekazania
::   WE: _a - EDOKUM.ref
::       _b - B_PREL
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.B_ROLE:=null;
EDOKPAR.index('UNIK'); EDOKPAR.prefix(_a,_b);
{? EDOKPAR.first()
|| {? EDOKPAR.B_ROLEF
   || OBIEGI.B_ROLE:=($EDOKPAR.B_ROLEF().TRESC)()
   |? EDOKPAR.B_ROLE
   || OBIEGI.B_ROLE:=EDOKPAR.B_ROLE
   ?}
?};
{? ~OBIEGI.B_ROLE
|| FUN.info('Nie ustalono roli użytkowników.'@);
   0
|| 1
?}


\upr_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Formuła na uprawnienia do współpracowników dla czynności akceptacji wniosków.
::       Dla domyślnych wartości argumentów zakłada się, że konteksty tabeli EDOKUM oraz tabeli USERS są ustawione.
::   WE:  _a  [DICT]      - tablica nazwana z parametrami wejściowymi czynności, dla której sprawdzenie
::       [_b] [REFERENCE] - złączenie do rekordu tabeli EDOKUM (wniosek, dla którego sprawdzenie uprawnień)
::       [_c] [REFERENCE] - złączenie do rekordu tabeli USERS (użytkownik, dla którego sprawdzenie uprawnień)
::       [_d] [DICT]      - tablica nazwana z instancją menadżera procesów
::   WY: 0 - użytkownik nie posiada uprawnień, wpp. 1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_cntx:=0;

    _in:={? var_pres('_a')>100                                                        || _a || return(_result) ?};
_edokum:={? var_pres('_b')=type_of(null) & _b<>null & ref_tab(_b)=EDOKUM & (_cntx:=1) || _b || EDOKUM.ref      ?};
  _user:={? var_pres('_c')=type_of(null) & _b<>null & ref_tab(_c)=USERS               || _c || USERS.ref       ?};
    _mp:={? var_pres('_d')>100                                                        || _d || return(_result) ?};

:: czy w danym procesie mamy sprawdzać uprawnienia do danych?
_proc_symbol:=exec('FindAndGet','#table',B_PROC,_mp.b_proc,,"B_PROC.SYMBOL",'');
_role_symbol:=exec('FindAndGet','#table',B_ROLE,_mp.buf_prel.B_ROLE,,"B_ROLE.NAME",'');
_act_symbol:=_mp.buf_act.UID;
_chk_upr:={? Plugin.runnable('OB_P_UPR')
          || Plugin.run('OB_P_UPR',_proc_symbol,_role_symbol,_act_symbol)
          || 0
?};

_result:={? _chk_upr
         || {? Plugin.runnable('OB_P_UPR_4USER')
            || Plugin.run('OB_P_UPR_4USER',_in,_edokum,_user,_mp)
            || 1
            ?}
         || 1
         ?};
_result


\upr_p_4user
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: Formuła do wykorzystania we wtyczce na uprawnienia do współpracowników dla czynności akceptacji wniosków.
::       Dla domyślnych wartości argumentów zakłada się, że konteksty tabeli EDOKUM oraz tabeli USERS są ustawione.
::   WE:  _a  [DICT]      - tablica nazwana z parametrami wejściowymi czynności, dla której sprawdzenie
::       [_b] [REFERENCE] - złączenie do rekordu tabeli EDOKUM (wniosek, dla którego sprawdzenie uprawnień)
::       [_c] [REFERENCE] - złączenie do rekordu tabeli USERS (użytkownik, dla którego sprawdzenie uprawnień)
::       _d [DICT]      - tablica nazwana z instancją menadżera procesów
::   WY: 0 - użytkownik nie posiada uprawnień, wpp. 1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_cntx:=0;

    _in:={? var_pres('_a')>100                                                        || _a || return(_result) ?};
_edokum:={? var_pres('_b')=type_of(null) & _b<>null & ref_tab(_b)=EDOKUM & (_cntx:=1) || _b || EDOKUM.ref      ?};
  _user:={? var_pres('_c')=type_of(null) & _b<>null & ref_tab(_c)=USERS               || _c || USERS.ref       ?};
    _mp:={? var_pres('_d')>100                                                        || _d || return(_result) ?};

   {? var_pres('__F_ZATR')<=0 || exec('__F_ZATR','object') ?};

   _types:=exec('types','portal_wnioski');

   {? _cntx
   || EDOKUM.cntx_psh();
      EDOKUM.use(ref_name(_edokum));
      EDOKUM.seek(_edokum,,1)
   ?};

   ETYPY.cntx_psh();
   ETYPY.prefix();
   _type:=EDOKUM.TYP().W_PORTAL;
   ETYPY.cntx_pop();

   _domain:={? _type=_types.portal | _type=_types.absence || 'POR'
            |? _type=_types.paperless || 'PEP'
            |? _type='s' || 'SEO'
            || 'OBE'
            ?};
   _employment_codes:='P,Z';

:: jeżeli to czynność rejestracji to tylko osoba wprowadzające dokument może mieć uprawnienie
{? _mp.buf_act.UID='OBE_FAW_DARP'
|| _os_user:=exec('FindAndGet','#table',USERS,_user,,"USERS.OSOBA",null());
   _result:=EDOKUM.DOSTAWCA=_os_user

|? exec('lic','#b_domain',_domain)
|| _p_ref_s:='';
   {? _domain='OBE'
::    wnioski webTermowe
   || EDOK_ATR.cntx_psh();
      {? exec('open_edk_atr', 'obiegi', _edokum)
      || _p_ref_s:=exec('atr_get','form_ob',_edokum,'ET_PRAC','REF_SQL')
      ?};
      EDOK_ATR.cntx_pop()
   ||
::    wnioski Portal HR
      {? (+EDOKUM.OSOBAWWW=16) & (type_of(ref_tab(EDOKUM.OSOBAWWW))>0) & (ref_tab(EDOKUM.OSOBAWWW)=P)
      || _p_ref_s:=EDOKUM.OSOBAWWW
      ?}
   ?};
   _result:=1;

   {? exec('lic_or','#b_domain','PKD','PPL')
   || {? +_p_ref_s
      || _P_UPR:=exec('dostepne_p','schemat',_domain,_employment_codes,,,_user);
         {? ~_P_UPR.find_key(_p_ref_s)
         || _result:=0
         ?}
      |? _domain='OBE' & EDOKUM.DOSTAWCA<>null()
      ||
::       dla wniosków webTermowych, jeżeli nie mogliśmy ustalić pracownika, którego wniosek dotyczy,
::       to sprawdzamy dostęp dla wszystkich P, osoby która rejestrowała wniosek
         OSOBA.cntx_psh();
         OSOBA.index('OSOBA');
         OSOBA.prefix();
         {? OSOBA.seek(EDOKUM.DOSTAWCA)
         || _firma:=exec('firma','#firma',exec('firma_symbol','#firma'),cli_ver());
            P.cntx_psh();
            P.index('OSOZATR');
            P.prefix(_firma,OSOBA.ref(),'T');
            {? P.first()
            || _size:=P.size();
               _upr:=0;
               {!
               |? _P_UPR:=exec('dostepne_p','schemat',_domain,_employment_codes,,,_user);
                  _upr+=_P_UPR.find_key($P.ref());
                  P.next()
               !};
               _result:=_upr=_size
   ?};
            P.cntx_pop()
         ?};
         OSOBA.cntx_pop()
      ?}
   ?}
?};

{? _cntx || EDOKUM.cntx_pop() ?};
_result


\edokuser_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Ustawia użytkowników roli
::   WE: _a - wskazanie na rolę
::----------------------------------------------------------------------------------------------------------------------
_rola:=OBIEGI.B_ROLE;
OBIEGI.B_ROLE:=_a;
_edit:=1;
{? OBIEGI.B_ROLE
|| B_USRROL.win_sel('WER_OBI');
   B_USRROL.index('B_ROLE'); B_USRROL.prefix(OBIEGI.B_ROLE);
   B_USRROL.first();
   B_USRROL.hdr_sel(); B_USRROL.hdr_sel('Przekazanie do akceptacji'@);
   _edit:=B_USRROL.select()
?};
OBIEGI.B_ROLE:=_rola;
_edit

:Sign Version 2.0 jowisz:1045 2024/02/08 14:55:30 33ec5ac0978ac1fc257726b18c08f80075262959001a6885ab71121837c28fdb7b7c14f6f4eca123933b693f1fef65a4d0c687c8d5f6cc92c58b87f856935be05d62d90acf29c54b2688bc16f298174ab2c268d4208c9705ea7c3ad3a407fe27847a18da4b5bf21b4db065da8e84f4300dbe730a8a453d0c7bdff50a7447fba8

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: edi_fo02.fml
:: Utworzony: 1.04.2019
:: Autor: JK
:: Systemy: FIKS, OBG, ZWS
::======================================================================================================================
:: Zawartość: edi - formuły wykorzystywane w definicji komunikatu EDI dla e-faktur
::           (dokumenty w obiegu i dokumenty księgowe)
::======================================================================================================================


\r_sg_edok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: read - dokument w obiegu - start grupa
::   WE: [_a] - formuła na ustawienie parametrów domyślnych w okienku.
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_edi:=exec('edi','edi_wspolne');
{? _edi.sza || return(1) ?};
VAR_DEL.delete('__PAR');
__PAR:=obj_new('ODD','TYP','SL','PAY','KOR');
__PAR.ODD:=__PAR.TYP:=__PAR.SL:=''; __PAR.PAY:=null;
EDOKUM.cntx_psh(); ETYPY.cntx_psh();
EDOKUM.index('DISP'); EDOKUM.prefix();
ETYPY.index('UNIK'); ETYPY.prefix();
OBIEGI.ODD:=null; OBIEGI.TYP:=null; OBIEGI.SL_KH:=''; EDOKUM.PLATNOSC:=null;
exec('tabkhini','kontrahent');
EDOKUM.win_edit('PAR');
ODD.f_clear();
OBIEGI.fld_fml('ODD','BEFORE_EDIT',"{? OPERATOR.DEPT<>null || 0 || 1 ?}");
OBIEGI.fld_fml('TYP','AFTER_EDIT',"{? OBIEGI.TYP=null
                                   || FUN.info('Wybierz typ faktury.'@);
                                      0
                                   |? OBIEGI.TYP().POZF='N'
                                   || FUN.info('Wybierz typ faktury zawierający pozycje.'@);
                                      0
                                   || OBIEGI.SL_KH:=OBIEGI.TYP().DOM_SL().SLU().NAZ;
                                      EDOKUM.PLATNOSC:=OBIEGI.TYP().PLATNOSC;
                                      1
                                   ?}");
OBIEGI.fld_fml('ODD','AFTER_EDIT',"{? OBIEGI.ODD=null
                                   || FUN.info('Wybierz jednostkę księgową.'@);
                                      0
                                   || 1
                                   ?}");
OBIEGI.fld_fml('SL_KH','AFTER_EDIT',"{? OBIEGI.SL_KH=''
                                     || FUN.info('Wybierz źródło pochodzenia.'@);
                                        0
                                     || 1
                                     ?}");
OBIEGI.fld_fml('ODD','BEFORE_DISPLAY',"{? OPERATOR.DEPT<>null || OBIEGI.ODD:=OPERATOR.DEPT || 1 ?}");
{? _>0 || ($_a)() ?};
{? EDOKUM.edit()
||
   __PAR.ODD:=OBIEGI.ODD().OD;
   __PAR.TYP:=OBIEGI.TYP().NAZWA;
   __PAR.SL:=OBIEGI.SL_KH;
   __PAR.PAY:=EDOKUM.PLATNOSC().ref();
   1
||
   exec('tabkhdel','kontrahent');
   VAR_DEL.delete('__PAR');
   return(0)
?};
1


\r_eg_edok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: read - dokument - end grupa
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_edi:=exec('edi','edi_wspolne');
{? _edi.sza || return(1) ?};
OBIEGI.fld_fml('ODD','BEFORE_EDIT',"*");
OBIEGI.fld_fml('ODD','BEFORE_DISPLAY',"*");
OBIEGI.fld_fml('ODD','AFTER_EDIT',"*");
OBIEGI.fld_fml('TYP','AFTER_EDIT',"*");
OBIEGI.fld_fml('SL_KH','AFTER_EDIT',"*");
exec('tabkhdel','kontrahent');
VAR_DEL.delete('__PAR');
ETYPY.cntx_pop(); EDOKUM.cntx_pop();
1


\r_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: read - dokument - start document
::   WE: _a - F - faktura, K - korekta
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__N','__V','__P','typ_ust');
_typ:={? var_pres('_a')=type_of('') || _a || '' ?};
{? _typ='' | 'FK'*_typ=0
||
   FUN.info('Nieprawidłowy parametr funkcji r_dok_ds.'@);
   return(-1)
?};
_result:=1;
:: _def_pid - identyfikator pozycji definicji: cbc:CustomizationID - weryfikacja wersji komunikatu
_def_pid:={? _typ='F' || '62a13f2232bf29aea545199e90dfa24ee9b3e19a'  || '294d17b632a359fc1cd1ded9996059d8f4ac26a4' ?};
_valID:=0;
_val:=exec('id_val','edi_xml',_def_pid);
{? var_pres('_val')<>type_of(~~)
      &
   var_pres('[1]',_val)<>type_of(~~)
||
   _valID:=
      {? _typ='F'
      ||
         _val[1]='urn:cen.eu:en16931:2017#compliant#urn:fdc:peppol.eu:2017:poacc:billing:3.0'
      ||
         _val[1]='urn:cen.eu:en16931:2017#compliant#urn:fdc:peppol.eu:2017:poacc:billing:3.0#extended#urn:fdc:www.efaktura.gov.pl:ver1.0'
      ?}
?};
{? _valID=0
||
   exec('add_kom','#message','Niepoprawna wersja komunikatu.',4,__XML.ID);
   return(-1)
?};
__N:=obj_new('EP_ID','EP_SID','KOR','RB');
__N.EP_ID:=''; __N.EP_SID:=''; {? _typ='F'|| __N.KOR:='N' || __N.KOR:='T' ?};
__N.RB:='';
_result


\r_ed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: read - dokument - end document
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__N','__V','__P','typ_ust');
1


\edok_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: dodawanie dokumentu w obiegu - start
::   WE: [_a] - formuła na ustawienie parametrów domyślnych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_edi:=exec('edi','edi_wspolne');
{? _=0 & _edi.sza
|| _edi.log('Brak przekazanej w parametrze formuły na domyślne parametry dokumentu.'@);
   return(0)
?};
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
{? _edi.sza
|| {? exec('szuk_okr','okresy',EDOKUM.DOP)=null
   || _edi.log('Nie znaleziono okresu dla daty %1.'@[EDOKUM.DOP]);
      return(0)
   |? OKRO_F.ZAM='T'
   || _edi.log('Okres %1 jest zamknięty.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
      return(0)
   |? OKRO_F.ZAM_CON='T'
   || _edi.log('Okres %1 jest zamknięty controllingowo.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
      return(0)
   || SSTALE.AO:=ROZNE.UT_OKROD;
      SSTALE.AR:=ROZNE.UT_OKROD().ROK;
      exec('open_tabele','open_tab','F')
   ?}
?};
EDOKUM.clear();
EDOKUM.blank();
EDOKUM.FIRMA:=REF.FIRMA;
EDOKUM.ROK_F:=SSTALE.AR;
{? var_press('__PAR')>0
|| EDOKUM.ODD:=exec('find_odd','fst',__PAR.ODD);
   EDOKUM.TYP:=exec('typfak','edi_wspolne',__PAR.TYP,1);
   SKID.SL_KH:=__PAR.SL;
   EDOKUM.PLATNOSC:=__PAR.PAY
?};
EDOKUM.DATAW:=date();
EDOKUM.ALERTY:='T';
EDOKUM.AUTOMAT:=2;
OBIEGI.DEL_ETAT:='';
EDOKUM.DOSTAWCA:=OPERATOR.USER().OSOBA;
EDOKUM.USERS:=OPERATOR.USER;
EDOKUM.UNIK_ID:=tm_stamp();
EDOKUM.KOREKTA:=__N.KOR;
:: PARAMETRYZACJA
{? _>0
|| ($_a)()
|? _edi.sza
|| _edi.log('Brak przekazanej w parametrze formuły na domyślne parametry dokumentu.'@);
   return(0)
?};
:: PARAMETRYZACJA
{? exec('chk_par','edi_fo02','OBG') || return(0) ?};
EDOKUM.TYP();
EDOKUM.TYPOBIEG:=EDOKUM.TYP().TYPOBIEG;
{? EDOKUM.add()
|| EDOKUM.bl_put('EDOKUM',__XML.ID,0);
   {? ETYPY.AUT_ID='T' & EDOKUM.ID=''
   || VAR_DEL.delete('id_edok');
      exec('ustal_numer','obiegi',1,SSTALE.AR,1,0);
      EDOKUM.ID:={? var_pres('id_edok')>0 || id_edok || '' ?};
      EDOKUM.put()
   ?};
   zakres:=1;
   exec('dol_edokos','obiegi');
   {? EDOKUM.TYP().ATR_G1R>0
   || exec('edk_atr_dolacz','obiegi',0)
   ?}
|| exec('add_kom','#message','Błąd podczas dodawania faktury w obiegu'@,38,'UWAGA'@);
   return(0)
?};
{? __XML.ID<>''
|| _pth:=__XML.ID;
   {? 1+_pth<>'@' || _pth:='@'+_pth ?};
   {? exec('interm','#system') & 1+_pth='@'
   || _pth:=1-_pth
   ?};
   EDOKUM.bl_file('EDOKUM',1,_pth)
?};
1


\edok_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: dodawanie dokumentu w obiegu - koniec
::   WE: _a - 0/1 - Czy dokument businesslink
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_press('_a')>0 || _a || 0 ?};
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
{? _a=0
|| EDOKUM.DO:=EDOKUM.DW:=EDOKUM.DOP
|| _edi:=exec('edi','edi_wspolne');
   _context:=_edi.context;
   {? EDOKUM.NRKSEF='' || EDOKUM.NRKSEF:=exec('FindAndGet','#table',DOKUM,_context.DOKUM,,"@.DOKUM.NRKSEF",'') ?};
   _ksefsdat:=exec('FindAndGet','#table',DOKUM,_context.DOKUM,,"@.DOKUM.KSEFSDAT",date(0,0,0));
   _ksefcdat:=exec('FindAndGet','#table',DOKUM,_context.DOKUM,,"@.DOKUM.KSEFCDAT",date(0,0,0));
   {? EDOKUM.DO=date(0,0,0) || EDOKUM.DO:={? _ksefsdat<>date(0,0,0) || _ksefsdat || _ksefcdat ?} ?};
   EDOKUM.DW:={? _ksefsdat<>date(0,0,0) || _ksefsdat || _ksefcdat ?}
?};
{? EDOKUM.TP=date(0,0,0)
|| EDOKUM.TP:=EDOKUM.DOP+#exec('get_par','slo_slu',EDOKUM.PLATNOSC,2)
?};
EVAT.cntx_psh(); EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
{? EVAT.first()
|| _netto:=_brutto:=0;
   {!
   |?
      _netto+=EVAT.NETTO;
      _brutto+=EVAT.BRUTTO;
      EVAT.next()
   !};
   EDOKUM.WART:=_brutto;
   EDOKUM.NETTO:=_netto
|| EDOKUM.WART:=EDOKUM.NETTO:=0
?};
EVAT.cntx_pop();
EDI_Z.DOK:=$EDOKUM.ref(); EDI_Z.SYM:=EDOKUM.SYM; EDI_Z.KH:=EDOKUM.KHKH;
EDOKUM.put();
1


\evat_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: dodawanie pozycji VAT - start
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__V');
__V:=obj_new('SV_ID','SV_P','SV_TSID');
__V.SV_ID:=''; __V.SV_P:='bd'; __V.SV_TSID:='';
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
EVAT.cntx_psh();
EVAT.index('EDOKUM'); EVAT.prefix();
EVAT.blank(1);
EVAT.EDOKUM:=EDOKUM.ref();
1


\evat_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: dodawanie pozycji VAT - end
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
{? EDOKUM.WAL<>FINFO.NAROD || return(1) ?};
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
EVAT.BRUTTO:=(EVAT.NETTO+EVAT.VAT);
{? ~EVAT.add()
|| exec('add_kom','#message','Błąd podczas dodawania pozycji VAT.'@,2,__XML.ID)
?};
EVAT.cntx_pop();
1


\zal_se
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Formuła początkowa na odczyt informacji o załącznikach.
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__VAR');
__VAR:=obj_new('FN','VAL','MC');
__VAR.FN:=''; __VAR.VAL:=''; __VAR.MC:='';
1


\zal_ee
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Formuła kończąca odczyt i dekodowanie z xml informacji o załącznikach.
::   WE: _a - 'FKS' - Dokument księgowy
::            'OBG' - Dokument w obiegu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
{? ~_verify & __VAR.FN<>'' & var_press('_a')>0
|| {? _a='OBG'
   || EDOKUMZ.cntx_psh();
      EDOKUMZ.index('DISP'); EDOKUMZ.prefix();
      EDOKUMZ.blank(1);
      EDOKUMZ.DOKUM:=EDOKUM.ref();
      EDOKUMZ.DATE:=date();
      EDOKUMZ.TIME:=time();
      EDOKUMZ.USER:=exec('name','users');
      EDOKUMZ.NAZWA:=EDOKUMZ.KR_OP:=__VAR.FN;
      EDOKUMZ.bl_file('EDOKUM',1);
      _dest:=fopen(EDOKUMZ.EDOKUM,'bw',,,1);
      {? _dest.is_open() & EDOKUMZ.add()
      || {? __XML.T.VTRUNC='T'
         || _src:=fopen(__XML.T.BVAL,'br',,,1);
            {? _src.is_open()
            || base64('decode',_src,_dest);
               fclose(_src)
            ?}
         || base64('decode',__VAR.VAL,_dest)
         ?};
         EDOKUMZ.bl_put('EDOKUM',_dest,,,__VAR.FN);
         fclose(_dest)
      ?};
      EDOKUMZ.cntx_pop()
   |? _a='FKS'
   || DOKUM.cntx_psh();
      DOKUM.index('DOKUM'); DOKUM.prefix();
      _tab:=POM.TAB;
      _typdok:=POM.TYPDOK;
      POM.TAB:='DOKUM';
      POM.TYPDOK:='SYS';
      exec('add_grnr','numery','SYS');
      DOKUM.blank();
      DOKUM.TYP:='I';
      DOKUM.REFSQL:=$DOK.ref;
      DOKUM.NR:=exec('numer_new','numery','PACZKA');
      DOKUM.NAZWA:=DOKUM.KR_OP:=__VAR.FN;
      DOKUM.bl_file('DOKUM',1);
      _dest:=fopen(DOKUM.DOKUM,'bw',,,1);
      {? _dest.is_open() & DOKUM.add()
      || exec('znak','numery','DOKUM');
         {? __XML.T.VTRUNC='T'
         || _src:=fopen(__XML.T.BVAL,'br',,,1);
            {? _src.is_open()
            || base64('decode',_src,_dest);
               fclose(_src)
            ?}
         || base64('decode',__VAR.VAL,_dest)
         ?};
         DOKUM.bl_put('DOKUM',_dest,,,__VAR.FN);
         fclose(_dest)
      || numer:=DOKUM.NR;
         oldnumer:=1;
         exec('nr_old','numery')
      ?};
      DOKUM.cntx_pop();
      POM.TAB:=_tab;
      POM.TYPDOK:=_typdok
   ?}
?};
VAR_DEL.delete('__VAR');
1


\id_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Sprawdzenie rodzaju adresu PEF.
::----------------------------------------------------------------------------------------------------------------------
:: gln
{? _a='0088'
|| return('GLN')
:: nip
|? _a='9945'
|| return('NIP')
|| return('INNY')
?}


\check_jm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Sprawdzenie oraz wpisanie jednostki miary dla pozycji faktury do tabeli tymczasowej
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
{? ~_verify || return(1) ?};
_jm:=exec('jm','edi_fo01',__XML.VAL);
{? _jm<>''
|| JM.cntx_psh();
   JM.index('KOD'); JM.prefix(_jm,_jm,);
   {? JM.first()
   || POZF.JM:=JM.ref()
   || exec('add_kom','#message','Pozycja %1: (%2) - Brak jednostki miary w słowniku.'[$POZF.LP,_jm],2,__XML.ID)
   ?};
   JM.cntx_pop()
?};
{? _jm='' || POZF.JM:=exec('jm','edi_form','I',__XML.VAL) ?};
{? POZF.JM<>null()
|| _ok:=1
|| {? __XML.VAL<>''
   || exec('add_kom','#message','Pozycja %1: Nieznana jednostka miary (%2).'[$POZF.LP,__XML.VAL],2,__XML.ID)
   ?}
?};
1


\chk_dezezw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: Sprawdzenie zezwoleń dla kontrahenta.
::   WE: _a - 'FKS' - Dokument księgowy
::            'OBG' - Dokument w obiegu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_ep_sid:=exec('id_typ','edi_fo02',__N.EP_SID);
KH.cntx_psh();
_ok:=1;
{? _ep_sid='NIP'
|| KH.index('SNIP'); KH.prefix(2,exec('niptostr','#string',__N.EP_ID),);
   {? ~KH.first
   || exec('add_kom','#message','Brak w bazie kontrahenta o identyfikatorze NIP: %1'@[__N.EP_ID],2,__XML.ID); _ok:=0
   ?}
|? _ep_sid='GLN'
|| KH.index('GLN'); KH.prefix(2,__N.EP_ID,__N.EP_ID);
   {? ~KH.first
   || exec('add_kom','#message','Brak w bazie kontrahenta o identyfikatorze GLN: %1'@[__N.EP_ID],2,__XML.ID); _ok:=0
   ?}
|| exec('add_kom','#message','Nieobsługiwany typ identyfikatora.'@,2,__XML.ID); _ok:=0
?};
{? _ok
|| _ok:=0;
   DEZEZW.cntx_psh();
   DEZEZW.index('WR');
   DEZEZW.prefix('ZWS',ISTDEF.ref,'R',KH.ref);
   {? DEZEZW.first()
   || {!
      |? {? (DEZEZW.OD<=date() & DEZEZW.DO>=date()) | (DEZEZW.OD<=date() & DEZEZW.DO=date(0,0,0))  || _ok:=1 ?};
         ~_ok & DEZEZW.next()
      !}
   ?};
   {? ~_ok
   || exec('add_kom','#message','Brak zezwolenia na odczyt komunikatu dla kontrahenta: %1'@[KH.NAZ],2,__XML.ID); _ok:=0
   ?};
   DEZEZW.cntx_pop()
?};
_verify:=exec('verify','edi_xml');
{? _verify
|| KH.cntx_pop();
   {? _ok || return(1) || return(0) ?}
?};
{? _ok
|| SLU.index('NAZ'); SLU.prefix();
   {? var_press('__PAR')>0
   || SLU.find_key(__PAR.SL)
   || {? _a='OBG' || SLU.find_key(SKID.SL_KH)
      |? _a='FKS' || SLU.find_key(SLOSLU.SLSLU)
      ?}
   ?};
   RS.cntx_psh(); RS.index('RS'); RS.prefix();
   {? RS.find_key(SLU.WZ) & RS.TAB='KH'
   || _kod:=($('SLO.KOD:='+RS.TAB+'.'+RS.KOD))();
      SLO.cntx_psh();
      SLO.index('SL');
      {? SLO.prefix(SLU.ref()); ~(SLO.find_key(_kod) & _kod=SLO.KOD)
      || SLO.blank(); SLO.SLU:=SLU.ref();SLO.KOD:=_kod;SLO.TR:=KH.NAZ; SLO.add()
      ?};
      {? _a='OBG'
      || EDOKUM.KH:=SLO.ref(); SKID.NIP:=KH.NIP;
         EDOKUM.KH_FULL:=KH.NAZ_P;
         EDOKUM.UL:=KH.UL;
         EDOKUM.MIASTO:=KH.MIASTO;
         EDOKUM.DOM:=KH.DOM;
         EDOKUM.LOKAL:=KH.LOKAL;
         EDOKUM.POCZ:=KH.POCZ;
         EDOKUM.KPOCZ:=KH.KPOCZ
      |? _a='FKS'
      || DOK.WYS:=SLO.ref();
         DOK.KH:=KH.SKR;
         DOK.KH_FULL:=KH.NAZ_P;
         DOK.KH_KRAJ:=KH.KRAJ; DOK.KH_KRISO:=KH.KRAJISO;
         DOK.MIASTO:=KH.MIASTO;
         DOK.UL:=KH.UL;
         DOK.DOM:=KH.DOM;
         DOK.LOKAL:=KH.LOKAL;
         DOK.POCZ:=KH.POCZ;
         DOK.KPOCZ:=KH.KPOCZ;
         DOK.NIP:=KH.NIP
      ?};
      SLO.cntx_pop()
   ?};
   RS.cntx_pop()
?};
KH.cntx_pop();
_ok


\pozf_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: dodawnie pozycji faktury - start
::   WE: _a - 'FKS' - Dokument księgowy
::            'OBG' - Dokument w obiegu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__P','__IDPOZF','__REJ_POZF');
__P:=obj_new('SV_ID','SV_P','SV_TSID');
__P.SV_ID:=''; __P.SV_P:='bd'; __P.SV_TSID:='';
__IDPOZF:=0;
{? _=0
|| exec('add_kom','#message','Brak parametru _a w formule pozf_start '+
   'określającego rodzaj dokumentu (FKS/OBG).'@,2,__XML.ID);
   return(0)
?};
__REJ_POZF:={? _a='OBG'
            || EDOKUM.TYP().POZF='T'
            |? _a='FKS'
            || DOK.DOK_REJ().JPK_FA_P='T'
            ?};
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
POZF.cntx_psh();
POZF.clear();
{? _a='FKS'
|| POZF.use('pozf'+(DOK.name()+4));
   POZF.index('DOK'); POZF.prefix(DOK.ref);
   POZF.cntx_psh();
   __IDPOZF:={? POZF.last() || POZF.LP+1 || 1 ?};
   POZF.cntx_pop();
   POZF.blank();
   POZF.DOK:=DOK.ref();
   POZF.EDOKUM:=null()
|? _a='OBG'
|| POZF.use('pozf__'+(EDOKUM.name()+2));
   POZF.index('EDOKUM'); POZF.prefix(EDOKUM.ref);
   POZF.cntx_psh();
   __IDPOZF:={? POZF.last() || POZF.LP+1 || 1 ?};
   POZF.cntx_pop();
   POZF.blank();
   POZF.DOK:=null();
   POZF.EDOKUM:=EDOKUM.ref()
?};
1


\pozf_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: dodawnie pozycji faktury - end
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__P','__IDPOZF');
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
{? POZF.SV
|| _pr:=exec('vat','dok_fks',POZF.SV().KOD);
   {? POZF.IL || POZF.WN:=(POZF.C*POZF.IL)$2-POZF.RABAT ?};
   POZF.WV:=(POZF.WN*_pr*0.01)$2;
   POZF.WB:=POZF.WN+POZF.WV
?};
POZF.add();
POZF.cntx_pop();
1


\r_sg_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: read - dokument księgowy - start grupa
::   WE: _a - F - faktura, K - korekta
::      [_b] - formuła na ustawienie parametrów domyślnych w okienku
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__PAR','edi_dok');
DOK.cntx_psh(); VPOZ.cntx_psh(); REJ.cntx_psh();
_typ:={? var_pres('_a')=type_of('') || _a || '' ?};
{? _typ='' | 'FK'*_typ=0
||
   FUN.info('Nieprawidłowy parametr funkcji r_sg_dok.'@);
   return(-1)
?};
_edi:=exec('edi','edi_wspolne');
{? _edi.sza || return(1) ?};
__PAR:=obj_new('ODD','REJ','DZ','DOK_REJ','RVAT','GRVAT','R','KK','K2','SL','SP_PL','OKRVAT');
__PAR.R:=__PAR.KK:=__PAR.K2:='';
__PAR.OKRVAT:=__PAR.SP_PL:=__PAR.SL:=__PAR.ODD:=__PAR.REJ:=__PAR.DZ:=__PAR.DOK_REJ:=__PAR.RVAT:=__PAR.GRVAT:=null;
DOK.index('REJ'); DOK.prefix();
VPOZ.index('VDOK'); VPOZ.prefix();
VPOZ.blank; DOK.blank;
DOK.REJ:=null; DOK.DZ:=null; DOK.WYS:=null; DOK.DOK_REJ:=null;
VPOZ.GRVAT:=null; VPOZ.RVAT:=null; VPOZ.R:='';
SLOSLU.SLSLU:='';
ODD.f_clear();
DOK.ODD:=OPERATOR.DEPT;
edi_dok:=1;
exec('tabkhini','kontrahent');
DOK.win_edit('KSDKOB03');
DOK.fld_fml('REJ','BEFORE_EDIT',"OPERATOR.DEPT:=DOK.ODD; 1");
{? _>1 || ($_b)() ?};
{? DOK.edit({? _a='K' || "exec('chk_dok','edi_fo02','K')" || "exec('chk_dok','edi_fo02','F')" ?})
|| __PAR.ODD:=DOK.ODD;
   __PAR.SL:=SLOSLU.SLSLU;
   __PAR.R:=VPOZ.R;
   __PAR.KK:=VPOZ.KK;
   __PAR.K2:=VPOZ.K2;
   __PAR.REJ:=DOK.REJ;
   __PAR.DZ:=DOK.DZ;
   __PAR.DOK_REJ:=DOK.DOK_REJ;
   __PAR.RVAT:=DOK.RVAT;
   __PAR.GRVAT:=VPOZ.GRVAT;
   __PAR.SP_PL:=DOK.SP_PL;
   __PAR.OKRVAT:=OKRO_F.ref;
   1
||
   exec('tabkhdel','kontrahent');
   VAR_DEL.delete('__PAR');
   return(0)
?};
1


\r_eg_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: read - dokument księgowy - end grupa
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
REJ.cntx_pop(); VPOZ.cntx_pop(); DOK.cntx_pop();
VAR_DEL.delete('__PAR','edi_dok');
1


\dok_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: dodawanie dokumentu w obiegu - start
::   WE: [_a] - formuła na ustawienie parametrów domyślnych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_edi:=exec('edi','edi_wspolne');
{? _=0 & _edi.sza
|| _edi.log('Brak przkazanej w parametrze formuły na domyślne parametry dokumentu.'@);
   return(0)
?};
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
VAR_DEL.delete('__REFDOK');
{? _edi.sza
|| {? exec('szuk_okr','okresy',DOK.DOP)=null
   || _edi.log('Nie znaleziono okresu dla daty %1.'@[DOK.DOP]);
      return(0)
   |? OKRO_F.ZAM='T'
   || _edi.log('Okres %1 jest zamknięty.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
      return(0)
   |? OKRO_F.ZAM_CON='T'
   || _edi.log('Okres %1 jest zamknięty controllingowo.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
      return(0)
   || SSTALE.AO:=ROZNE.UT_OKROD;
      SSTALE.AR:=ROZNE.UT_OKROD().ROK;
      exec('open_tabele','open_tab','F')
   ?}
?};
DOK.clear();
DOK.blank(1);
{? var_press('__PAR')>0
|| DOK.ODD:=__PAR.ODD;
   DOK.REJ:=__PAR.REJ;
   DOK.RVAT:=__PAR.RVAT;
   DOK.DOK_REJ:=__PAR.DOK_REJ;
   DOK.SP_PL:=__PAR.SP_PL;
   DOK.OKRVAT:=__PAR.OKRVAT
|| DOK.OKRVAT:=SSTALE.AO
?};
DOK.DR:=exec('data','#blank');
DOK.KRAJ:=exec('kod_pl','slo_slu');
DOK.JORG:=FINFO.NAROD;
DOK.TR:='';
DOK.DR:=date();
DOK.ZAR:=exec('usr_zar','dok_fks');
DOK.A:=DOK.ZK:=DOK.ZP:=DOK.WSK_XD:='N';
DOK.DOKZRODL:='';
exec('fl_decl','dekret_aut');
exec('dek_decl','dekret_aut');
exec('sd_decl','dekret_aut');
:: Wywołanie formuły na domyślne parametry
{? _>0 || ($_a)() ?};
{? exec('chk_par','edi_fo02','FKS') || return(0) ?};
DOK.cntx_psh();
DOK.index('REJ');
DOK.prefix(DOK.REJ);
{? DOK.last() || _vnr:=DOK.NR+1 || _vnr:=1 ?};
DOK.cntx_pop();
DOK.NR:=_vnr;
DOK.JPK_V_T:=exec('typ_dok','obiegi2');
{? DOK.add()
|| DOK.bl_put('E_DOC',__XML.ID,0)
|| exec('add_kom','#message','Błąd podczas dodawania dokumentu księgowego'@,2,__XML.ID);
   return(0)
?};
1


\dok_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: dodawanie dokumentu w obiegu - koniec
::   WE: a - ksef
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_ksef:={? var_pres('_a')>0 || _a='ksef' || 0 ?};
{? _ksef || exec('other_parties_ee','edi_fo_ufd') ?};
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
{? _ksef
|| _edi:=exec('edi','edi_wspolne');
   _context:=_edi.context;
   {? DOK.NRKSEF='' || DOK.NRKSEF:=exec('FindAndGet','#table',DOKUM,_context.DOKUM,,"@.DOKUM.NRKSEF",'') ?};
   _ksefsdat:=exec('FindAndGet','#table',DOKUM,_context.DOKUM,,"@.DOKUM.KSEFSDAT",date(0,0,0));
   _ksefcdat:=exec('FindAndGet','#table',DOKUM,_context.DOKUM,,"@.DOKUM.KSEFCDAT",date(0,0,0));
   DOK.DTO:={? _ksefsdat<>date(0,0,0) || _ksefsdat || _ksefcdat ?};
   {? DOK.DTW=date(0,0,0) || DOK.DTW:={? _ksefsdat<>date(0,0,0) || _ksefsdat || _ksefcdat ?} ?}
|| DOK.DTO:=DOK.DTW:=DOK.DOP
?};
{? DOK.D3=date(0,0,0) || DOK.D3:=DOK.DTO+#exec('get_par','slo_slu',DOK.SP_PL,2) ?};
{? DOK.D4=date(0,0,0) || DOK.D4:=DOK.DOP ?};
KH.cntx_psh();
KH.index('NAZ');
KH.prefix(2,DOK.KH,DOK.MIASTO,DOK.UL);
:: Dodawnie rachunku bankowego
{? __N.RB<>''
|| SKID_RBK.cntx_psh();
   SKID_RBK.index('TAB'); SKID_RBK.prefix(REF.FIRMA,'KH','KH',#KH.ref());
   _rbk:=1;
   {? SKID_RBK.first()
   || {!
      |? {? {? +__N.RB=28 || 2-__N.RB=SKID_RBK.N || __N.RB=SKID_RBK.N ?}
         || DOK.RB:=SKID_RBK.ref; _rbk:=0
         ?};
         _rbk & SKID_RBK.next()
      !}
   ?};
   SKID_RBK.cntx_pop()
?};
KH.cntx_pop();
EDI_Z.DOK:=$DOK.ref(); EDI_Z.SYM:=DOK.NK;
{? DOK.put()
|| POZ.cntx_psh(); POZ.prefix();
   POZ.use('pozy'+(DOK.name+4));
   {? ~(var_pres('obdekredi')>0 & obdekredi=1)
   || exec('poz_dekv','dok_fks')
   ?};
   POZ.cntx_pop()
|| exec('add_kom','#message','Błąd podczas dodawania dokumentu.'@,2,__XML.ID);
   return(0)
?};
VAR_DEL.delete('__POZFDATE');
1


\vpoz_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: dodawanie pozycji VAT - start
::   WE: [_a] - formuła na ustawienie parametrów domyślnych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__V');
__V:=obj_new('SV_ID','SV_P','SV_TSID');
__V.SV_ID:=''; __V.SV_P:='bd'; __V.SV_TSID:='';
_edi:=exec('edi','edi_wspolne');
{? _=0 & _edi.sza
|| _edi.log('Brak przkazanej w parametrze formuły na domyślne parametry pozycji VAT.'@);
   return(0)
?};
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
{? DOK.WAL<>FINFO.NAROD || return(1) ?};
VPOZ.cntx_psh();
VPOZ.index('VDOK'); VPOZ.prefix();
VPOZ.blank(1);
VPOZ.DOK:=DOK.ref();
:: PARAMETRYZACJA
{? var_press('__PAR')>0
|| VPOZ.R:=__PAR.R;
   VPOZ.GRVAT:=__PAR.GRVAT;
   VPOZ.RVAT:=__PAR.RVAT;
   VPOZ.KK:=__PAR.KK;
   VPOZ.K2:=__PAR.K2;
   VPOZ.OKRVAT:=__PAR.OKRVAT
|| VPOZ.RVAT:=DOK.RVAT;
   VPOZ.OKRVAT:=DOK.OKRVAT
?};
{? _>0 || ($_a)() ?};
1


\vpoz_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: dodawanie pozycji VAT - end
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_verify:=exec('verify','edi_xml');
{? _verify || return(1) ?};
{? DOK.WAL<>FINFO.NAROD || return(1) ?};
VPOZ.BRUTTO:=VPOZ.NETTO+VPOZ.VAT;
exec('setvatodl','dok_fks');
{? ~VPOZ.add()
|| exec('add_kom','#message','Błąd podczas dodawania pozycji VAT.'@,2,__XML.ID)
?};
VPOZ.cntx_pop();
1


\chk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [2006]
:: OPIS: Sprawdzenie poprawnosci parametrow dekretowania e-faktury
::   WE: _a - F - faktura, K - korekta
::----------------------------------------------------------------------------------------------------------------------
{? ~DOK.REJ
|| FUN.info('Nie wybrano rejestru księgowego!'@); 'REJ'
|? DOK.REJ().ODD<>DOK.ODD
|| FUN.info('Wybrany rejest nie występuje we wskazanej jednostce księgowej!'@); 'REJ'
|? ~DOK.DOK_REJ
|| FUN.info('Nie wybrano rodzaju dokumentu!'@); 'DOK_REJ'
|? ~+SLOSLU.SLSLU
|| FUN.info('Nie wybrano źródła pochodzenia!'@); 'SLSLU'
|? ~DOK.RVAT & (DOK.DOK_REJ().SLO().KOD='VAT' | SLO.KOD='SAD')
||  FUN.info('Nie wybrano lub brak rejestru VAT powiązanego z rejestrem księgowym!'@); 'RVAT'
|? ~VPOZ.GRVAT & (DOK.DOK_REJ().SLO().KOD='VAT' | SLO.KOD='SAD')
|| FUN.info('Nie wybrano grupy podatkowej!'@); 'GRVAT'
::|? ~DOK.SP_PL
::|| FUN.info('Nie wybrano sposobu płatności!'@);
::   {? DOK.DOK_REJ<>null & (DOK.DOK_REJ().SLO().KOD='VAT' | SLO.KOD='SAD') || 'SP_PL' || 'REJ' ?}
|| _rej:=DOK.REJ; _dok_rej:=DOK.DOK_REJ; _rvat:=DOK.RVAT; _grvat:=VPOZ.GRVAT;
   {? _rej<>DOK.DOK_REJ().REJ
   || FUN.info('Niezgodność rodzaju dokumentu z rejestrem księgowym!'@); DOK.DOK_REJ:=null;
      'DOK_REJ'
   |? _a='K' & DOK.DOK_REJ().KOR='N'
   || FUN.info('Nieprawidłowy rodzaj dokumentu dla korekty faktury!'@); DOK.DOK_REJ:=null;
      'DOK_REJ'
   |? _a='F' & DOK.DOK_REJ().KOR='T'
   || FUN.info('Nieprawidłowy rodzaj dokumentu dla faktury!'@); DOK.DOK_REJ:=null;
      'DOK_REJ'
   |? (DOK.DOK_REJ().SLO().KOD='VAT' | SLO.KOD='SAD')
   || {? DOK.RVAT().REJ<>_rej
      || FUN.info('Niezgodność rejestru VAT z rejestrem księgowym!'@); DOK.RVAT:=null; 'REJ_EDI'
      || {? VPOZ.GRVAT().REJ<>_rej
         || FUN.info('Niezgodność grupy podatkowej z rejestrem księgowym!'@); VPOZ.GRVAT:=null;
            'GRVAT'
         || 1
         ?}
      ?}
   || 1
   ?}
?}


\chk_dtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: kontrola poprawności dat w dokumencie
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_edi:=exec('edi','edi_wspolne');
_verify:=exec('verify','edi_xml');
{? ~_edi.sza & _verify
|| {? SSTALE.AO().POCZ=date(0,0,0) & ~exec('dat_not','dok_fks',DOK.DOP)
   || exec('add_kom','#message','Data sprzedaży faktury (%1) spoza bieżącego roku bilansowego.'@[$DOK.DOP],2,__XML.ID)
   |? (DOK.DOP<SSTALE.AO().POCZ | DOK.DOP>SSTALE.AO().KON) & SSTALE.AO().KON<>date(0,0,0)
   || exec('add_kom','#message','Data sprzedaży faktury (%1) spoza aktywnego okresu.'@[$DOK.DOP],2,__XML.ID)
   ?}
?};
_ok


\chk_do
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: kontrola poprawności daty otrzymania w dokumencie w obiegu.
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_verify:=exec('verify','edi_xml');
_edi:=exec('edi','edi_wspolne');
{? ~_edi.sza & _verify & ~exec('dat_not','dok_fks',EDOKUM.DOP)
|| exec('add_kom','#message','Data dostawy towarów lub wykonania usługi (%1) spoza bieżącego roku bilansowego.'@[$EDOKUM.DOP],2,__XML.ID);
   _ok:=1
?};
_ok


\chk_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.22]
:: OPIS: sprawdzenie, czy ustawiono podstawowe parametry dokumentu.
::   WE: [_a]: FKS - dokument księgowy, OBG - dokument w obiegu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_err:=0;
_text:='';
{? _a='FKS'
|| {? ~DOK.ODD || _text+='DOK.ODD, ' ?};
   {? ~DOK.REJ || _text+='DOK.REJ, ' ?};
   {? ~DOK.RVAT || _text+='DOK.RVAT, ' ?};
   {? ~DOK.DOK_REJ || _text+='DOK.DOK_REJ, ' ?}
|? _a='OBG'
|| {? typobi=1 & ~EDOKUM.TYP || _text+='EDOKUM.TYP, ' ?};
   {? ~EDOKUM.ODD || _text+='EDOKUM.ODD, ' ?}
?};
{? _text<>''
|| exec('add_kom','#message','Nie ustawiono podstawowych parametrów dokumentu (%1).'@[(_text-2)],2,__XML.ID);
   _err:=1
?};
_err

:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 c2f2733762ff1b2ae495f93418d179891c11a25d50ce03fe9115fa56015a494345a235d2001e8827c04da923d3c2bd5f46498cdf95d61c8b2170187dbec78c2c0ed58ee0fefb3d90850b0c2081330192581a82a097f4c86ee6621f4a3d93e659ee1f065ca076b36c77eafe1ae52fc30551580a61bb1f9851aec9cda74cd0e0ed

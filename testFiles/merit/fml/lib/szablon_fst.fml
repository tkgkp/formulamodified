:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: szablon_fst.fml
:: Utworzony:
:: Autor: AFI
::======================================================================================================================
:: Zawartość: Formuły wykorzystywane w szablonach dokumentów WORD w obaszarach środków trwałych
::======================================================================================================================


\sr_dok_rej_prolog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Prolog funkcji pobrania dokumentów rejestrów środków trwałych
::   WE:
::----------------------------------------------------------------------------------------------------------------------
SRDO.cntx_psh();
SRDO.prefix()


\sr_dok_rej_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Funkcja pobrania dokumentów rejestrów środków trwałych
::   WE: _a - obiekt z parametrami wywołania
::----------------------------------------------------------------------------------------------------------------------
{? SRDO.seek(_a.SRDO,,1)
|| SRDO
|| ~~
?}


\sr_dok_rej_epilog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Epilog funkcji pobrania dokumentów rejestrów środków trwałych
::   WE:
::----------------------------------------------------------------------------------------------------------------------
SRDO.cntx_pop()


\sr_nag_dostawca
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Formuła zwracająca dostawcę środka trwałego
::   WE: _a - SRDO.ref()
::   WY: sformatowana wartość
::----------------------------------------------------------------------------------------------------------------------
_dostawca:='';
SRDZ.index('SRDZ');
SRDZ.prefix();
SRDZ.cntx_psh();
SRDZ.prefix(SRDO.ref());
{? SRDZ.first()
|| {! |?
      {? SRDZ.DOK_REF<>''
      || DOK.cntx_psh();
         DOK.use(ref_name(SRDZ.DOK_REF));
         DOK.index('CON');
         DOK.prefix();
         {? DOK.seek(SRDZ.DOK_REF)
         || {? DOK.KH_FULL<>''
            || _dostawca+=DOK.KH_FULL+', '
            || _dostawca+=DOK.WYS().TR+', '
            ?}
         ?};
         DOK.cntx_pop()
      ?};
      SRDZ.next()
   !}
?};
{? _dostawca<>'' || _dostawca:=_dostawca-2 ?};
SRDZ.cntx_pop();
_dostawca


\sr_nag_pk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Formuła zwracająca wartości sekcji polecenie księgowania dla wydruku dokumentu OT LT
::   WE: _a - SRDO.ref()
::   WY: tabela nazwana
::----------------------------------------------------------------------------------------------------------------------
_resTab:=obj_new('NR','DDOK','KWN','KMA','WN','MA','DKS');
_resTab.NR:='Brak danych';
_resTab.DDOK:='Brak danych';
_resTab.WN:=0;
_resTab.MA:=0;
_resTab.KWN:='Brak danych';
_resTab.KMA:='Brak danych';
_resTab.DKS:='Brak danych';

{? _a.DOKKSIEG<>''
|| DOK.cntx_psh();
   POZ.cntx_psh();
   DOK.use('doku'+{? _a.TYP().WARP='T' || 4+(_a.DOKKSIEG+12) || (4+_a.DOKKSIEG) ?});
   POZ.use('pozy'+{? _a.TYP().WARP='T' || 4+(_a.DOKKSIEG+12) || (4+_a.DOKKSIEG) ?});
   DOK.prefix();
   {? DOK.seek(#{? _a.TYP().WARP='T' || 4-(_a.DOKKSIEG+12) || (4-_a.DOKKSIEG) ?},DOK.name())
   || _resTab.NR:=DOK.NR;
      _resTab.DDOK:=DOK.DTO;
      _resTab.DKS:=DOK.DKS;
      POZ.index('DOK');
      POZ.prefix(DOK.ref());
      {? POZ.first
      || {! |? {? 1+POZ.STR='W' || _resTab.WN+=POZ.SUM || _resTab.MA+=POZ.SUM ?};
               {? 1+POZ.STR='W' || _resTab.KWN:=POZ.KON || _resTab.KMA:=POZ.KON ?};
               POZ.next
         !};
         POZ.first
      ?}
   ?};
   DOK.cntx_pop();
   POZ.cntx_pop()
?};
_resTab


\sr_nag_ward
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Formuła zwracająca wartość dodatkową
::   WE: _a - SRDO.ref()
::   WY: sformatowana wartość
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.TOR_D='T' & _a.TYP().WARP='T'
|| form(_a.WARD,,2)
|| {? _a.SRSR_E<>null
   || form(_a.SRSR_E().WARD,,2,' ,')
   || form(_a.SRSR().WARD,,2,' ,')
   ?}
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 4f4ae2153f6657d23172551bc790cf9f930d89452c96d8a10c43d00075fd713aaf4d6dfa8b5a26055bd5d15d2562f0c7af41553aef3f288855e20cac706204c7e971d294c1394c414c4d2aa2b79b7a5b0bc3b8ca01f3e0616a10f5acc965326f58f126770af16157d59aacda90fd933b63e05da56378b0487bebf9551eb670fe

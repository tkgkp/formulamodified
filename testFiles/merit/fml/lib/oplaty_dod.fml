:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: oplaty_dod.fml
:: Utworzony: 20.12.2021
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa opłat dodatkowych - pozycje / nagłówki
::======================================================================================================================

\sel_opl_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wyświetlenie informacji o naliconych opłatach
::   WE: _a - uidref() nagłówka lub pozycji
::----------------------------------------------------------------------------------------------------------------------
_uid:={? var_pres('_a')=type_of('') & (+_a)=48 || _a || '' ?};
{? _uid<>''
|| _Tab:=ref_tab(_uid);
   {? _Tab=ND | _Tab=FAKS
   || TAXXYZ.UIN:=_uid;
      TAXXYZ.UIP:='N';
::    widok wg nagłówków opłat
      FAKS_K.index('TAXS');
      FAKS_K.prefix(_uid);
      {? FAKS_K.first()
      || FAKS_K.win_sel('OPL');
         FAKS_K.select()
      || FUN.info('Brak naliczonych opłat dodatkowych.'@)
      ?}
   |? _Tab=DK | _Tab=FAP
   || TAXXYZ.UIN:={? _Tab=DK || _Tab.N().uidref() || _Tab.FAKS().uidref() ?};
      TAXXYZ.UIP:=_uid;
::    widok pozycji opłat
      FAP_K.index('TAXS');
      FAP_K.prefix(_uid);
      {? FAP_K.first()
      || FAP_K.win_sel('GRP');
         FAP_K.select()
      || FUN.info('Brak naliczonych opłat dodatkowych.'@)
      ?}
   ?}
?};
~~


\init_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Inicjuje tabelę atrybutów
::   WE: _a - wskazanie na podatek
::       [_b] - wskazanie na wersję
::       [_c] - rodzaj atrybutu
::----------------------------------------------------------------------------------------------------------------------
_taxo:={? var_pres('_b')=type_of(null()) || _b || null() ?};
_rodz:={? var_pres('_c')=type_of('') || _c || '' ?};


VAR_DEL.delete('__aod');
_tab:='tab_tmp(1';
:: pobieram bez wersji
TAXT.index('TAXS');
TAXT.prefix(_a);
{? TAXT.first()
|| {!
   |? {? ~(_tab*(',\'%1\',\''[TAXT.KOD])) & (TAXT.TAXO=null() | _taxo=null()) & (_rodz='' | _rodz=TAXT.R)
      || _txt:={? TAXT.RV='R' || 'REAL'
               |? TAXT.RV='D' || 'DATE'
               |? TAXT.RV='T' || 'TIME'
               |? TAXT.RV='L' || 'STRING[1]'
               |? TAXT.RV='S' || 'STRING[%1]'[{? TAXT.RP>0 || $TAXT.RP || '100' ?}]
               || ''
               ?}
      || _txt:=''
      ?};
      {? _txt<>'' || _tab+=',\'%1\',\'%2\',\'\''[TAXT.KOD,_txt] ?};
      TAXT.next()
   !}
?};
:: pobieram z wersji
{? _taxo<>null()
|| TAXT.index('TAXO');
   TAXT.prefix(_taxo);
   {? TAXT.first()
   || {!
      |? {? ~(_tab*(',\'%1\',\''[TAXT.KOD])) & (_rodz='' | _rodz=TAXT.R)
         || _txt:={? TAXT.RV='R' || 'REAL'
                  |? TAXT.RV='D' || 'DATE'
                  |? TAXT.RV='T' || 'TIME'
                  |? TAXT.RV='L' || 'STRING[1]'
                  |? TAXT.RV='S' || 'STRING[%1]'[{? TAXT.RP>0 || $TAXT.RP || '100' ?}]
                  || ''
                  ?}
         || _txt:=''
         ?};
         {? _txt<>'' || _tab+=',\'%1\',\'%2\',\'\''[TAXT.KOD,_txt] ?};
         TAXT.next()
      !}
   ?}
?};
_tab+=')';
{? (+_tab)>10
|| __aod:=($_tab)()
?};
~~


\fill_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Wypełnia tabelę atrybutów
::   WE: _a - wskazanie na podatek
::       _b - wskazanie na wersję podatku
::       _c - indeks
::----------------------------------------------------------------------------------------------------------------------
_taxs:={? var_pres('_a')=type_of(null()) || _a || null() ?};
_taxo:={? var_pres('_b')=type_of(null()) || _b || null() ?};
 _mat:={? var_pres('_c')=type_of(null()) || _c || null() ?};

:: usunięcie poprzedniego wypełnienia
__aod.clear(); {? __aod.first() || {! |? __aod.del() !} ?};
TAXT.index('TAXS');
TAXT.prefix(_taxs);
{? TAXT.first()
|| __aod.blank();
   {!
   |? {? TAXT.TAXO=null() & TAXT.R='C' & (';RSDTL'*TAXT.RV)>1
      || ($('__aod.%1'[TAXT.KOD]))():=($('TAXT.VAL%1'[TAXT.RV]))()
      |? (TAXT.TAXO=null() | TAXT.TAXO=_taxo) & TAXT.R='M' & (';RSDTL'*TAXT.RV)>1
      || ($('__aod.%1'[TAXT.KOD]))():={? TAXT.RV='R'
                                      || exec('FindInSet','#table','TAXI','M',TAXT.KOD,_mat,"@.TAXI.VALR",,TAXT.TAXS)
                                      |? TAXT.RV='S'
                                      || exec('FindInSet','#table','TAXI','M',TAXT.KOD,_mat,"@.TAXI.VALS",,TAXT.TAXS)
                                      |? TAXT.RV='D'
                                      || exec('FindInSet','#table','TAXI','M',TAXT.KOD,_mat,"@.TAXI.VALD",,TAXT.TAXS)
                                      |? TAXT.RV='T'
                                      || exec('FindInSet','#table','TAXI','M',TAXT.KOD,_mat,"@.TAXI.VALT",,TAXT.TAXS)
                                      |? TAXT.RV='L'
                                      || exec('FindInSet','#table','TAXI','M',TAXT.KOD,_mat,"@.TAXI.VALL",,TAXT.TAXS)
                                      || ''
                                      ?}
      ?};
      TAXT.next()
   !};
   __aod.add(1)
?};
{? _taxo<>null() & __aod.first()
|| TAXT.index('TAXO');
   TAXT.prefix(_taxo);
   {? TAXT.first()
   || _put:=0;
      {!
      |? {? TAXT.R='C' & (';RSDTL'*TAXT.RV)>1
         || _put:=1;
            ($('__aod.%1'[TAXT.KOD]))():=($('TAXT.VAL%1'[TAXT.RV]))()
         ?};
         TAXT.next()
      !};
      {? _put || __aod.put(1) ?}
   ?}
?};
~~


\done_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Usuwa tabelę atrybutów
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__aod');
~~


\value
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: zwraca wartość w postaci stringa
::   WE: _a - tabela
::       _b - rodzaj
::       _c - precyzja
::----------------------------------------------------------------------------------------------------------------------
_Tab:={? var_pres('_a')=type_of(FIRMA) || _a || return('') ?};
_rodz:={? var_pres('_b')=type_of('') || _b || '' ?};
_prec:={? var_pres('_c')=type_of(0) || _c || 0 ?};

_Tab.VALUE:={? _rodz='R' || form(_Tab.VALR,,_prec,'99')
            |? _rodz='S' || {? ~_prec || form(_Tab.VALS) || form(_Tab.VALS,_prec) ?}
            |? _rodz='D' || form(_Tab.VALD)
            |? _rodz='T' || form(_Tab.VALT,,3)
            |? _rodz='L' || {? _Tab.VALL='N' || 'Nie' || 'Tak' ?}
            || ''
            ?};
~~


\valid_TAXI
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Główna formuła walidacji dla danych indeksu/opłaty
::   WE: [_a] - 1-komunikaty(domyślnie) 0-nie
::   WY: ''-jest OK, jakieś pole - nie jest dobrze
::----------------------------------------------------------------------------------------------------------------------
_res:='';
_tryb:={? var_pres('_a')=type_of(0) || _a || 1 ?};

_res:=exec('validATR','oplaty_dod',TAXI,_tryb);
_res


\ctrlatrm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: sprawdzenie wypełnienia atrybutów opłaty dla indeksu
::   WE: _a - indeks
::       _b - opłata
::   WY: 1-jest ok 0-niestety
::----------------------------------------------------------------------------------------------------------------------
 _res:=1;
 _mat:={? var_pres('_a')=type_of(null) || _a || null() ?};
_taxs:={? var_pres('_b')=type_of(null) || _b || null() ?};

{? _mat<>null() & _taxs<>null()
|| TAXI.cntx_psh();
   TAXI.index('M');
   TAXI.prefix(_mat,_taxs);
   {? TAXI.first()
   || {!
      |? _res:=exec('valid_TAXI','oplaty_dod',0)='';
         _res & TAXI.next()
      !}
   ?};
   TAXI.cntx_pop()
?};
_res


\calctaxx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Funkcja przelicza opłatę na wszystkich dokumentach dla danego indeksu
::   WE: [_a] - TAXS
::       [_b] - M
::----------------------------------------------------------------------------------------------------------------------
_nopar:=0;
_taxs:={? var_pres('_a')=type_of(null()) || _a || _nopar:=1; TAXX.TAXS ?};
 _mat:={? var_pres('_b')=type_of(null()) || _b || TAXX.M ?};
_taxo:=null();

_form:=exec('FindInSet','#table','TAXT','R_RV','F',_taxs,"@.TAXT.FORM",1,'C','');
_nopl:=exec('FindInSet','#table','TAXX','UNIK',_taxs,_mat,,,,null())=null();

{? ~exec('FindAndGet','#table',TAXS,_taxs,,"AKT='T'",0)
|| FUN.info('Opłata dodatkowa %1 nie jest aktywna do naliczania.\n'
            'Nalezy ją aktywować ażeby przeliczyć dokumenty.'@[exec('FindAndGet','#table',TAXS,_taxs,,"TAX",'')])
|? ~_nopl & _form=''
|| FUN.info('Brak formuły na obliczenie opłaty dodatkowej.'@)
|| _fval:='%1'[_form];
   exec('init_atr','oplaty_dod',_taxs);
   _msk:=DK.names();
   _mfk:=FAP.names();
   _ll:=_msk.size()+_mfk.size();
   _ii:=0;
   _msk.prefix();
   {? _msk.first()
   || exec('mag_psh','open_tab');
      {!
      |? _ii+=1;
         progress((_ii/_ll)*100,'Trwa obliczenie...','Przeliczenie opłat dodatkowych'@,0);
         exec('mag_open','open_tab',1+(_msk.NAME+3),(_msk.NAME+3)+2);
         DK.index('MAT');
         DK.prefix(_mat);
         {? DK.first()
         || {!
            |? {? ~_nopl & exec('sprwarTAXN','oplaty_dod',_taxs,DK,DK.N().uidref(),DK.uidref())
               || {? DK.N().WAL<>INFO.NAROD
                  || _wal:=DK.N().WAL;
                     _krs:=DK.N().KRS
                  || _wal:=INFO.NAROD;
                     _krs:=0
                  ?};
                  _baxo:=exec('verTAX','oplaty_dod',_taxs,DK,DK.N().uidref(),DK.uidref());
                  _oki:={? var_pres('_baxo')=type_of(null()) || _taxo:=_baxo; 1 || _taxo:=null(); _baxo ?};
                  _form:=exec('formTAX','oplaty_dod',_taxs,_taxo);
                  {? _oki>0 & _form<>''
                  || exec('fill_atr','oplaty_dod',_taxs,_taxo,_mat);
                     _fval:='%1'[_form];
                     _tax:=($_fval)(DK);
                     {? _tax>0
                     || exec('addFAP_K','oplaty_dod',DK.N().uidref(),DK.uidref(),_taxs,_tax,_wal,_krs)
                     || exec('delFAP_K','oplaty_dod',DK.N().uidref(),DK.uidref(),_taxs)
                     ?}
                  |? _oki<>-1
                  || exec('delFAP_K','oplaty_dod',DK.N().uidref(),DK.uidref(),_taxs)
                  ?}
               || exec('delFAP_K','oplaty_dod',DK.N().uidref(),DK.uidref(),_taxs)
               ?};
               DK.next()
            !}
         ?};
         _msk.next()
      !};
      exec('mag_pop','open_tab')
   ?};
   _mfk.prefix();
   {? _mfk.first()
   || exec('fak_psh','open_tab');
      {!
      |? _ii+=1;
         progress((_ii/_ll)*100,'Trwa obliczenie...','Przeliczenie opłat dodatkowych'@,0);
         exec('fak_open','open_tab',1+(_mfk.NAME+3),(_mfk.NAME+3)+2);
         FAP.index('M');
         FAP.prefix(_mat);
         {? FAP.first()
         || {!
            |? {? ~_nopl & exec('sprwarTAXN','oplaty_dod',_taxs,FAP,FAP.FAKS().uidref(),FAP.uidref())
               || {? FAP.FAKS().WAL<>INFO.NAROD
                  || _wal:=FAP.FAKS().WAL;
                     _krs:=FAP.FAKS().KRS
                  || _wal:=INFO.NAROD;
                     _krs:=0
                  ?};
                  _baxo:=exec('verTAX','oplaty_dod',_taxs,FAP,FAP.FAKS().uidref(),FAP.uidref());
                  _oki:={? var_pres('_baxo')=type_of(null()) || _taxo:=_baxo; 1 || _taxo:=null(); _baxo ?};
                  _form:=exec('formTAX','oplaty_dod',_taxs,_taxo);
                  {? _oki>0 & _form<>''
                  || exec('fill_atr','oplaty_dod',_taxs,_taxo,_mat);
                     _fval:='%1'[_form];
                     _tax:=($_fval)(FAP);
                     {? _tax>0
                     || exec('addFAP_K','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),_taxs,_tax,_wal,_krs)
                     || exec('delFAP_K','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),_taxs)
                     ?}
                  |? _oki<>-1
                  || exec('delFAP_K','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),_taxs)
                  ?}
               || exec('delFAP_K','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),_taxs)
               ?};
               FAP.next()
            !}
         ?};
         _mfk.next()
      !};
      exec('fak_pop','open_tab')
   ?};
   obj_del(_msk);
   obj_del(_mfk);
   prgs_clr();
   exec('done_atr','oplaty_dod',_taxs);
   {? _nopar || exec('actTAXX','oplaty_dod',TAXX.ref()) ?}
?};
~~


\actTAXS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Naliczenie opłat dodatkowych
::   WE: _a - uidref nagłówka
::       _b - uidref pozycji
::       _c - indeks materiałowy
::       _d - poprzedni indeks materiałowy
::----------------------------------------------------------------------------------------------------------------------
_uin:={? var_pres('_a')=type_of('') & (+_a)=48 || _a || '' ?};
_uip:={? var_pres('_b')=type_of('') & (+_b)=48 || _b || '' ?};
_mat:={? var_pres('_c')=type_of(null) || _c || null() ?};
_old:={? var_pres('_d')=type_of(null) || _d || null() ?};

{? _uin<>'' & _uip<>'' & _old<>null() || exec('delFAP_K','oplaty_dod',_uin,_uip,null(),0) ?};
{? _uin<>'' & _uip<>'' & _mat<>null()
|| _Tap:=ref_tab(_uip);
   _kor:=ref_tab(_uin)=FAKS & exec('FindAndGet','#table',FAKS,_uin,,"T().KOR='T'",0);
   {? ref_tab(_uin)=FAKS
   || _wal:=exec('FindAndGet','#table',FAKS,_uin,,"WAL",INFO.NAROD);
      _krs:={? _wal=INFO.NAROD || 0 || exec('FindAndGet','#table',FAKS,_uin,,"KRS",0) ?}
   |? ref_tab(_uin)=ND
   || _wal:=exec('FindAndGet','#table',ND,_uin,,"WAL",INFO.NAROD);
      _krs:={? _wal=INFO.NAROD || 0 || exec('FindAndGet','#table',ND,_uin,,"KRS",0) ?}
   || _wal:=INFO.NAROD;
      _krs:=0
   ?};
   TAXX.index('M');
   TAXX.prefix(_mat);
   {? TAXX.first()
   || {!
      |? _taxs:=TAXX.TAXS;
         _oki:=exec('FindAndGet','#table',TAXS,_taxs,,"AKT='T'",0);
         {? _oki
         || _baxo:=exec('verTAX','oplaty_dod',_taxs,_Tap,_uin,_uip);
            _oki:={? var_pres('_baxo')=type_of(null()) || _taxo:=_baxo; 1 || _taxo:=null(); _baxo ?}
         ?};
         _mat:=TAXX.M;
         {? _oki>0 & exec('sprwarTAXN','oplaty_dod',_taxs,_Tap,_uin,_uip)
         || _form:=exec('formTAX','oplaty_dod',_taxs,_taxo);
            {? _form<>''
            || exec('init_atr','oplaty_dod',_taxs,_taxo);
               exec('fill_atr','oplaty_dod',_taxs,_taxo,_mat);
               _fval:='%1'[_form];
               _tax:=($_fval)(_Tap);
               {? _tax>0 | (_kor & _tax<0)
               || exec('addFAP_K','oplaty_dod',_uin,_uip,_taxs,_tax,_wal,_krs)
               || exec('delFAP_K','oplaty_dod',_uin,_uip,_taxs)
               ?};
               exec('done_atr','oplaty_dod')
            || exec('delFAP_K','oplaty_dod',_uin,_uip,_taxs)
            ?}
         |? _oki<>-1
         || exec('delFAP_K','oplaty_dod',_uin,_uip,_taxs)
         ?};
         TAXX.next()
      !}
   || exec('delFAP_K','oplaty_dod',_uin,_uip,null())
   ?}
?};
~~


\addFAP_K
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dodanie lub aktualizacja opłaty dodatkowej dla pozycji
::   WE: _a - uidref nagłówka
::       _b - uidref pozycji
::       _c - wskazanie na opłatę
::       _d - wartość opłaty
::       _e - waluta
::       _f - kurs
::----------------------------------------------------------------------------------------------------------------------
_uin:={? var_pres('_a')=type_of('') & (+_a)=48 || _a || '' ?};
_uip:={? var_pres('_b')=type_of('') & (+_b)=48 || _b || '' ?};
_taxs:={? var_pres('_c')=type_of(null()) || _c || null() ?};
_tax:={? var_pres('_d')=type_of(0) || _d || 0 ?};
_wal:={? var_pres('_e')=type_of(null()) || _e || INFO.NAROD ?};
_krs:={? var_pres('_f')=type_of(_f) & _f>0 || _f || 0 ?};
_res:=0;

_kor:=_uin<>'' & ref_tab(_uin)=FAKS & exec('FindAndGet','#table',FAKS,_uin,,"T().KOR='T'",0);

{? _uin<>'' & _uip<>'' & (_tax>0 | (_kor & _tax<0)) & _taxs<>null()
|| FAKS_K.cntx_psh();
   FAP_K.cntx_psh();
   TAXJ.cntx_psh();
   _faks_k:=null();
   FAKS_K.index('TAXS');
   FAKS_K.prefix(_uin,_taxs);
   {? FAKS_K.first()
   || {? FAKS_K.WAL=null()
      || FAKS_K.WAL:=_wal;
         FAKS_K.KRS:=_krs;
         FAKS_K.put(1)
      ?};
      _faks_k:=FAKS_K.ref()
   || FAKS_K.blank();
      FAKS_K.OPLD:='T';
      FAKS_K.TAXS:=_taxs;
      FAKS_K.UID:=_uin;
      FAKS_K.WAL:=_wal;
      FAKS_K.KRS:=_krs;
      {? FAKS_K.add(1) || _faks_k:=FAKS_K.ref() ?}
   ?};
   {? _faks_k<>null()
   || FAP_K.index('TAXS');
      FAP_K.prefix(_uip,_taxs);
      {? FAP_K.first()
      || FAP_K.FAKS_K:=_faks_k;
         FAP_K.NOPL:=_tax;
         {? _wal<>INFO.NAROD & _krs>0
         || FAP_K.WAL:=_wal;
            FAP_K.WOPL:=FAP_K.NOPL/_krs $2
         ?};
         {? FAP_K.put(1) || _res:=1 ?}
      || FAP_K.blank();
         FAP_K.FAKS_K:=_faks_k;
         FAP_K.TAXS:=_taxs;
         FAP_K.UID:=_uip;
         FAP_K.UIN:=_uin;
         FAP_K.NOPL:=_tax;
         {? _wal<>INFO.NAROD & _krs>0
         || FAP_K.WAL:=_wal;
            FAP_K.WOPL:=FAP_K.NOPL/_krs $2
         ?};
         FAP_K.M:=exec('FindAndGet','#table',ref_tab(_uip),_uip,,"M",null());
         {? FAP_K.add(1) || _res:=1 ?}
      ?};
      {? _res || exec('sumFAP_K','oplaty_dod',_faks_k) ?};
      {? _res || exec('addTAXJ','oplaty_dod',_taxs,_uin,_uip) ?}
   ?};
   FAKS_K.cntx_pop();
   FAP_K.cntx_pop();
   TAXJ.cntx_pop()
?};
_res


\sumFAP_K
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Obliczenie sumarycznej wartości opłat dodatkowych na pozycjach
::   WE: _a - wskazanie na FAKS_K
::       [_b] - 0-bez usuwania atrybutów informacyjnych 1(domyślnie)-tak
::   WY: 1-ok 0-nie
::----------------------------------------------------------------------------------------------------------------------
_faks_k:={? var_pres('_a')=type_of(null()) || _a || null() ?};
_delj:={? var_pres('_b')=type_of(0) || _b || 1 ?};
_res:=0;

FAKS_K.cntx_psh();
FAP_K.cntx_psh();
FAKS_K.prefix();
{? FAKS_K.seek(_faks_k)
|| _sumN:=0;
   _sumW:=0;
   _count:=FAKS_K.count();
   _taxs:=FAKS_K.TAXS;
   _uin:=FAKS_K.UID;
   FAKS_K.cntx_psh();
   FAP_K.index('TAX_N');
   FAP_K.prefix(FAKS_K.TAXS,FAKS_K.UID);
   {? FAP_K.first()
   || {!
      |? _sumN+=FAP_K.NOPL;
         _sumW+=FAP_K.WOPL;
         FAP_K.next()
      !}
   ?};
   FAKS_K.cntx_pop();
   FAKS_K.NOPL:=_sumN;
   FAKS_K.WOPL:=_sumW;
   {? ~_count & FAKS_K.NOPL=0 & FAKS_K.WOPL=0
   || _res:=FAKS_K.del(,1)>0;
      {? _res & _delj
      || TAXJ.index('UIN');
         TAXJ.prefix(_uin,'N',_taxs);
         {? TAXJ.first() || {! |? _del:=TAXJ.del(,1); _res:=_del>0; _del>1 !} ?}
      ?}
   || {? FAKS_K.put(1) || _res:=1 ?}
   ?}
?};
FAKS_K.cntx_pop();
FAP_K.cntx_pop();
_res


\delFAP_K
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Usunięciea opłaty dodatkowej dla pozycji
::   WE: _a - uidref nagłówka
::       _b - uidref pozycji
::       _c - wskazanie na opłatę
::       [_d] - 0-bez usuwania atrybutów informacyjnych 1(domyślnie)-tak
::   WY: 1-usunięto 0-coś nie tak
::----------------------------------------------------------------------------------------------------------------------
_uin:={? var_pres('_a')=type_of('') & (+_a)=48 || _a || '' ?};
_uip:={? var_pres('_b')=type_of('') & (+_b)=48 || _b || '' ?};
_taxs:={? var_pres('_c')=type_of(null()) || _c || null() ?};
_delj:={? var_pres('_d')=type_of(0) || _d || 1 ?};
_res:=1;
_faks_k:=null();

{? _taxs<>null()
|| FAP_K.index('TAXS');
   FAP_K.prefix(_uip,_taxs);
   {? FAP_K.first() || _faks_k:=FAP_K.FAKS_K; _del:=FAP_K.del(,1); _res:=_del>0 ?};
   {? _faks_k<>null() || exec('sumFAP_K','oplaty_dod',_faks_k) ?};
   {? _res & _delj || exec('delTAXJ','oplaty_dod',_taxs,_uin,_uip) ?}
|| FAP_K.index('TAXS');
   FAP_K.prefix(_uip);
   {? FAP_K.first()
   || {!
      |? _taxs:=FAP_K.TAXS;
         _uin:=FAP_K.UIN;
         _faks_k:=FAP_K.FAKS_K;
         _del:=FAP_K.del(,1);
         {? _del
         || exec('sumFAP_K','oplaty_dod',_faks_k,_delj);
            {? _delj || exec('delTAXJ','oplaty_dod',_taxs,_uin,_uip) ?}
         || _res:=0
         ?};
         _del>1
      !}
   ?}
?};
_res


\po_fap_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Odświeżenie okienka w grupie
::----------------------------------------------------------------------------------------------------------------------
TAXI.index('M');
TAXI.prefix(FAP_K.M,FAP_K.TAXS);
TAXI.first();
grp_disp(TAXI,'OPL_TAX');
{? TAXXYZ.UIP<>'N'
|| TAXJ.index('UIP');
   TAXJ.prefix(TAXXYZ.UIP,FAP_K.TAXS);
   TAXJ.first();
   grp_disp(TAXJ,'WER')
?};
~~


\po_faks_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Odświeżenie okienka w grupie
::----------------------------------------------------------------------------------------------------------------------
FAP_K.index('TAX_N');
FAP_K.prefix(FAKS_K.TAXS,FAKS_K.UID);
FAP_K.first();
grp_disp(FAP_K,'OPL_POZ');
exec('po_fap_k','oplaty_dod');
~~


\sprwarTAXN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Sprawdzenie warunków naliczenia opłaty
::   WE: _a - wskazanie na opłatę
::       _b - tabela
::       _c - uidref nagłówka
::       _d - uidref pozycji
::   WY: 1-naliczać opłatę 0-nie naliczaj
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_taxs:={? var_pres('_a')=type_of(null()) || _a || return(0) ?};
 _Tap:={? var_pres('_b')=type_of(FIRMA) || _b || return(0) ?};
 _uin:={? var_pres('_c')=type_of('') & (+_c)=48 || _c || '' ?};
 _uip:={? var_pres('_d')=type_of('') & (+_d)=48 || _d || '' ?};

_data:={? _Tap=DK  || _Tab:=ND; exec('FindAndGet','#table',_Tab,_uin,,"D",date(0,0,0))
       |? _Tap=FAP || _Tab:=FAKS; exec('FindAndGet','#table',_Tab,_uin,,"D",date(0,0,0))
       || return(0); date(0,0,0)
       ?};

TAXN.cntx_psh();
TAXN.index('TAXS');
TAXN.prefix(_taxs);
:: są warunki a jak nie ma to naliczamy opłatę
{? TAXN.first()
|| _res:=0;
   _taxn:=sql('select distinct'
              ' GRP, OD '
              'from TAXN '
              'where TAXN.TAXS=:_a '
              ' and TAXN.OD<=to_date(:_b) '
              ' and ((TAXN.DO is null) or TAXN.DO>=to_date(:_b)) '
              'order by GRP,OD',_taxs,_data);
   _taxn.clear();
   {? _taxn.first()
   || {!
      |? TAXN.index('GRP');
         TAXN.prefix(_taxs,_taxn.GRP,_taxn.GRP,_taxn.OD);
         {? TAXN.first()
         || _first:=1;
            _formres:='';
            {!
            |? _yes:=TAXN.YESNO='=';
               _war:={? TAXN.ROP='S'  || {? _yes || _Tab=FAKS & _Tab.SZ='S' || _Tab<>FAKS | _Tab.SZ<>'S' ?}
                     |? TAXN.ROP='Z'  || {? _yes || _Tab=FAKS & _Tab.SZ='Z' || _Tab<>FAKS | _Tab.SZ<>'Z' ?}
                     |? TAXN.ROP='M'  || {? _yes || _Tab=ND || _Tab<>ND ?}
                     |? TAXN.ROP='TN' || {? _yes || _Tab=ND & _Tab.TYP().P='N' || _Tab<>ND | _Tab.TYP().P<>'N' ?}
                     |? TAXN.ROP='TT' || {? _yes || _Tab=ND & _Tab.TYP().P='T' || _Tab<>ND | _Tab.TYP().P<>'T' ?}
                     |? TAXN.R='K'    || {? _yes || _Tab.KH=TAXN.KH || _Tab.KH<>TAXN.KH ?}
                     |? TAXN.R='O'    || {? _yes || _Tab.KH_ODB=TAXN.KH_ODB || _Tab.KH_ODB<>TAXN.KH_ODB ?}
                     |? TAXN.R='M'
                      & _Tab=ND       || {? _yes || _Tab.MAG=TAXN.MG || _Tab.MAG<>TAXN.MG ?}
                     |? TAXN.R='S'
                      & _Tab=FAKS     || {? _yes || _Tab.STS=TAXN.SP || _Tab.STS<>TAXN.SP ?}
                     |? TAXN.R='Z'
                      & _Tab=FAKS     || {? _yes || _Tab.STS=TAXN.SP || _Tab.STS<>TAXN.SP ?}
                     |? TAXN.R='T'
                      & _Tab=ND       || {? _yes || _Tab.TYP=TAXN.TM || _Tab.TYP<>TAXN.TM ?}
                     |? TAXN.R='X'
                      & _Tab=FAKS     || {? _yes || _Tab.T=TAXN.TSZ || _Tab.T<>TAXN.TSP ?}
                     |? TAXN.R='Y'
                      & _Tab=FAKS     || {? _yes || _Tab.T=TAXN.TSZ || _Tab.T<>TAXN.TSP ?}
                     |? TAXN.R='F'    || ($TAXN.FORM)(_uin,_uip)
                     || 0
                     ?};
               _formres+={? _first || '%1'[$_war] || ' %1 %2'[TAXN.ANDOR,$_war] ?};
               _first:=0;
               TAXN.next()
            !};
            _res:=($_formres)()>0
         ?};
         ~_res & _taxn.next()
      !}
   ?};
   obj_del(_taxn)
?};
TAXN.cntx_pop();
_res


\taxi_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kolorowanie dla atrybutów opłaty indeksu
::   WY: schemat kolorów
::----------------------------------------------------------------------------------------------------------------------
_rodz:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.RV",1,,'');
_disp:='RED%1'[_rodz];
TAXI.win_edit(_disp);
{? exec('valid_TAXI','oplaty_dod',0)<>''
|| 'TAXI#01#01'
|| ''
?}


\taxx_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Kolorowanie dla opłaty indeksu
::   WY: schemat kolorów
::----------------------------------------------------------------------------------------------------------------------
{? exec('FindAndGet','#table',TAXX,TAXX.ref(),,"MOD",'')<>''
|| 'TAXX#01#01'
|| ''
?}


\actTAXX
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: aktualizuje znacznik dla TAXX
::   WE: _a - TAXI.ref() lub TAXX.ref()
::       _b - wartość przed
::       _c - wartość po
::----------------------------------------------------------------------------------------------------------------------
_res:='';
TAXI.cntx_psh();
TAXX.cntx_psh();
{? ref_tab(_a)=TAXI
|| TAXI.prefix();
   {? TAXI.seek(_a)
   || _taxs:=TAXI.TAXS;
      _mat:=TAXI.M;
      {? _b<>_c || _res+='W' ?};
      {? ~exec('ctrlatrm','oplaty_dod',_mat,_taxs) || _res+='K' ?};
      TAXX.index('UNIK');
      TAXX.prefix(_mat,_taxs);
      {? TAXX.first()
      || TAXX.MOD:=_res;
         {? TAXX.put(1)
         || TAXX.prefix();
            {? TAXX.win_sel('?')='GRP'
            || grp_disp(TAXX,'WER')
            |? TAXX.win_sel('?')='TAX'
            || grp_disp(TAXX,'MAT')
            ?}
         ?}
      ?}
   ?}
|? ref_tab(_a)=TAXX
|| TAXX.prefix();
   {? TAXX.seek(_a)
   || _taxs:=TAXX.TAXS;
      _mat:=TAXX.M;
      {? ~exec('ctrlatrm','oplaty_dod',_mat,_taxs) || _res+='K' ?};
      TAXX.index('UNIK');
      TAXX.prefix(_mat,_taxs);
      {? TAXX.first()
      || TAXX.MOD:=_res;
         TAXX.put(1)
      ?}
   ?}
?};
TAXI.cntx_pop();
TAXX.cntx_pop();
~~


\mod_taxj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Modyfikacja atrybutu nagłówka/pozycji
::----------------------------------------------------------------------------------------------------------------------
_rodz:=exec('FindInSet','#table','TAXT','TAXS',TAXJ.TAXT,TAXJ.TAXS,"@.TAXT.RV",1,,'');
_prec:=exec('FindInSet','#table','TAXT','TAXS',TAXJ.TAXT,TAXJ.TAXS,"@.TAXT.RP",,,0);
_form:=exec('FindInSet','#table','TAXT','R_RV','F',TAXJ.TAXS,"@.TAXT.FORM",1,'N','');
{? _form='' || _form:='exec(\'validATR\',\'oplaty_dod\',TAXJ)' ?};

_valid:='\"%1\"'[_form];
_edit:='RED'+_rodz;
_fldval:={? (';RSDTL'*_rodz)>1 || 'TAXJ.VAL%1'[_rodz] || '' ?};

_prevval:=($_fldval)();
TAXJ.win_edit(_edit);
{? TAXJ.edit(($_valid)())
|| exec('value','oplaty_dod',TAXJ,_rodz,_prec);
   {? TAXJ.put(1) || exec('actTAXX','oplaty_dod',TAXJ.ref(),_prevval,($_fldval)()) ?}
?};
TAXJ.win_edit('REDL');
~~


\addTAXJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Dodanie atrybutów nagłówka/pozycji opłaty dodatkowej
::   WE: _a - wskazanie na opłatę
::       _b - uidref nagłówka
::       _c - uidref pozycji
::   WY: 1-usunięto 0-coś nie tak
::----------------------------------------------------------------------------------------------------------------------
_taxs:={? var_pres('_a')=type_of(null()) || _a || null() ?};
_uin:={? var_pres('_b')=type_of('') & (+_b)=48 || _b || '' ?};
_uip:={? var_pres('_c')=type_of('') & (+_c)=48 || _c || '' ?};
_res:=1;

TAXT.index('MNAG');
TAXT.prefix(_taxs,'N');
{? TAXT.first()
|| {!
   |? _rodz:=TAXT.RV;
      {? (';RSDTL'*_rodz)>1
      || _uid:={? TAXT.RNP='N' || 'N' || _uip ?};
         TAXJ.index('UIN');
         TAXJ.prefix(_uin,_uid,_taxs,TAXT.KOD,);
         {? ~TAXJ.first()
         || TAXJ.blank();
            TAXJ.TAXS:=_taxs;
            TAXJ.UIN:=_uin;
            TAXJ.UIP:=_uid;
            TAXJ.TAXT:=TAXT.KOD;
            TAXJ.DESC:=TAXT.DESC;
            ($('TAXJ.VAL%1:=TAXT.VAL%1'[_rodz]))();
            exec('value','oplaty_dod',TAXJ,_rodz,TAXT.RP);
            {? ~TAXJ.add(1) || _res:=0; undo() ?}
         ?}
      ?};
      _res & TAXT.next()
   !}
?};
_res


\delTAXJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Usunięcie atrybutów nagłówka/pozycji opłaty dodatkowej
::   WE: _a - wskazanie na opłatę
::       _b - uidref nagłówka
::       _c - uidref pozycji
::   WY: 1-usunięto 0-coś nie tak
::----------------------------------------------------------------------------------------------------------------------
_taxs:={? var_pres('_a')=type_of(null()) || _a || null() ?};
_uin:={? var_pres('_b')=type_of('') & (+_b)=48 || _b || '' ?};
_uip:={? var_pres('_c')=type_of('') & (+_c)=48 || _c || '' ?};

_res:=1;
_faks_k:=null();

{? _taxs<>null()
|| TAXJ.index('UIP');
   TAXJ.prefix(_uip,_taxs);
   {? TAXJ.first() || {! |? _del:=TAXJ.del(,1); _res:=_del>0; _del>1 !} ?}
|| TAXJ.index('UIP');
   TAXJ.prefix(_uip);
   {? TAXJ.first() || {! |? _del:=TAXJ.del(,1); _res:=_del>0; _del>1 !} ?}
?};
_res


\sel_uin_taxj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Atrybuty nagłówka dla opłaty
::----------------------------------------------------------------------------------------------------------------------
TAXJ.index('UIN');
TAXJ.prefix(TAXXYZ.UIN,'N',FAKS_K.TAXS);
{? TAXJ.first()
|| TAXJ.win_sel('WER');
   TAXJ.select()
|| FUN.info('Brak atrybutów dotyczących nagłówka dokumentu dla opłaty dodatkowej %1.'@[FAKS_K.TAXS().TAX])
?}


\disp_uin_taxj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Atrybuty nagłówka dla opłaty
::----------------------------------------------------------------------------------------------------------------------
TAXJ.index('UIN');
TAXJ.prefix(TAXXYZ.UIN,TAXXYZ.UIP,FAKS_K.TAXS);
{? TAXJ.first()
|| TAXJ.win_sel('OPL_TAX');
   TAXJ.select()
|| FUN.info('Brak atrybutów dotyczących nagłówka dokumentu dla opłaty dodatkowej %1.'@[FAKS_K.TAXS().TAX])
?}


\verTAX
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Pobiera wersję opłaty dodatkowej
::   WE: _a - wskazanie na opłatę
::       _b - tabela
::       _c - uidref nagłówka
::       _d - uidref pozycji
::   WY: ref TAXO lub null jak brak
::        lub -2-jak są wersje bez daty, która pasuje
::        lub -1-jak wersja jest nieaktywna
::----------------------------------------------------------------------------------------------------------------------
_res:=null();
_taxs:={? var_pres('_a')=type_of(null()) || _a || return(0) ?};
 _Tap:={? var_pres('_b')=type_of(FIRMA) || _b || return(0) ?};
 _uin:={? var_pres('_c')=type_of('') & (+_c)=48 || _c || '' ?};
 _uip:={? var_pres('_d')=type_of('') & (+_d)=48 || _d || '' ?};

_data:={? _Tap=DK  || _Tab:=ND; exec('FindAndGet','#table',_Tab,_uin,,"D",date(0,0,0))
       |? _Tap=FAP || _Tab:=FAKS; exec('FindAndGet','#table',_Tab,_uin,,"D",date(0,0,0))
       || return(0); date(0,0,0)
       ?};

TAXO.cntx_psh();
TAXO.index('TAXS');
TAXO.prefix(_taxs);
:: szukamy wersji opłaty
{? TAXO.first()
|| _oki:=0;
   {!
   |? {? TAXO.OD<=_data & (TAXO.DO=date(0,0,0) | TAXO.DO>=_data)
      || {? TAXO.AKT='T'
         || _res:=TAXO.ref();
            _oki:=1
         || _res:=-1;
            _oki:=-1
         ?}
      ?};
      ~_oki & TAXO.next()
   !};
   {? ~_oki || _res:=-2 ?}
?};
TAXO.cntx_pop();
_res


\formTAX
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Pobiera formułę naliczenia opłaty dodatkowej
::   WE: _a - wskazanie na opłatę
::       _b - wskazanie na wersję
::   WY: formuła lub pusty string
::----------------------------------------------------------------------------------------------------------------------
_res:='';
_taxs:={? var_pres('_a')=type_of(null()) || _a || return(0) ?};
_taxo:={? var_pres('_b')=type_of(null()) || _b || null() ?};

_res:={? _taxo<>null()
      || exec('FindInSet','#table','TAXT','RORV','F',_taxo,"@.TAXT.FORM",1,'C','')
      || ''
      ?};
{? _res='' || _res:=exec('FindInSet','#table','TAXT','R_RV','F',_taxs,"@.TAXT.FORM",1,'C','') ?};
_res


\act_taxi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja atrybutów opłaty dodatkowej dla indeksu wg aktualnego TAXX
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_taxs:=TAXX.TAXS;
_taxx:=TAXX.ref();
 _mat:=TAXX.M;

{? _taxs<>null()
|| do();
   TAXT.index('MNAG');
   TAXT.prefix(_taxs,'M');
   {? TAXT.first()
   || {!
      |? _rodz:=TAXT.RV;
         {? (';RSDTL'*_rodz)>1
         || TAXI.index('M');
            TAXI.prefix(_mat,_taxs,TAXT.KOD,);
            {? ~TAXI.first()
            || TAXI.blank();
               TAXI.TAXS:=_taxs;
               TAXI.M:=_mat;
               TAXI.TAXT:=TAXT.KOD;
               TAXI.DESC:=TAXT.DESC;
               ($('TAXI.VAL%1:=TAXT.VAL%1'[_rodz]))();
               exec('value','oplaty_dod',TAXI,_rodz,TAXT.RP);
               {? ~TAXI.add(1) || _res:=0; undo() ?}
            ?}
         ?};
         _res & TAXT.next()
      !}
   ?};
:: usunięcie kodów, których nie ma w definicji
   TAXI.index('M');
   TAXI.prefix(_mat,_taxs);
   {? TAXI.first()
   || {!
      |? {? ~exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,_taxs,"@.TAXT.R='M'",1,,0)
         || TAXI.del()
         || TAXI.next()
         ?}
      !}
   ?};
   {? _res
   || {? ~exec('ctrlatrm','oplaty_dod',_mat,_taxs)
      || TAXX.MOD:='K';
         TAXX.put(1)
      || TAXX.MOD:='';
         TAXX.put(1)
      ?}
   ?};
   end()
?};
_res


\validATR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Podstawowa formuła walidacji dla atrybutów
::   WE: _a - tabela
::       [_b] - 1-komunikaty(domyślnie) 0-nie 2-zapis komunikatów do KOMM-a
::----------------------------------------------------------------------------------------------------------------------
_res:='';
 _Tab:={? var_pres('_a')=type_of(FIRMA) || _a || cur_tab(1,1) ?};
_tryb:={? var_pres('_b')=type_of(0) || _b || 1 ?};

_rodz:=exec('FindInSet','#table','TAXT','TAXS',_Tab.TAXT,_Tab.TAXS,"@.TAXT.RV",'L');
_prec:=exec('FindInSet','#table','TAXT','TAXS',_Tab.TAXT,_Tab.TAXS,"@.TAXT.RP",0);
_ctrl:=exec('FindInSet','#table','TAXT','TAXS',_Tab.TAXT,_Tab.TAXS,"@.TAXT.CTRL",'');

{? _rodz='R' & _ctrl='A' & _Tab.VALR<=0
|| {? _tryb=1 || FUN.info('Należy podać wartość większą od zera.'@) ?};
   _res:='VALR'
|? _rodz='R' & _ctrl='B' & _Tab.VALR<0
|| {? _tryb=1 || FUN.info('Należy podać wartość większą lub równą zero.'@) ?};
   _res:='VALR'
|? _rodz='S' & _ctrl='T' & _Tab.VALS=''
|| {? _tryb=1 || FUN.info('Należy podać wartość atrybutu.'@) ?};
   _res:='VALS'
|? _rodz='D' & _ctrl='T' & _Tab.VALD=date(0,0,0)
|| {? _tryb=1 || FUN.info('Należy podać niezerową datę.'@) ?};
   _res:='VALD'
|? _rodz='T' & _ctrl='T' & _Tab.VALT=''
|| {? _tryb=1 || FUN.info('Należy podać niezerowy czas.'@) ?};
   _res:='VALT'
|? _rodz='T' & ~((';TN'*_Tab.VALL)>1)
|| {? _tryb=1 || FUN.info('Należy wybrać wartość logiczną.'@) ?};
   _res:='VALL'
?};
{? _res<>'' & _tryb=2
|| exec('add_kom','#message','Niepoprawna wartość atrybutu %1 dla opłaty %2.'@[_Tab.TAXT,_Tab.TAXS().TAX],2)
?};
_res


\ctrlTAXJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: sprawdzenie wypełnienia atrybutów dla dokumentu
::   WE: _a - uidref nagłówka
::       _b - uidref pozycji
::   WY: 1-jest ok 0-niestety
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_uin:={? var_pres('_a')=type_of('') & (+_a)=48 || _a || '' ?};
_uip:={? var_pres('_b')=type_of('') & (+_b)=48 || _b || '' ?};

_buf:=1;
{? _uin<>''
|| TAXJ.cntx_psh();
   TAXJ.index('UIN');
   TAXJ.prefix(_uin);
   {? TAXJ.first()
   || {!
      |? _buf:=exec('validATR','oplaty_dod',TAXJ,2)='';
         {? ~_buf || _res:=0 ?};
         TAXJ.next()
      !}
   ?};
   TAXJ.cntx_pop()
?};
_res

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:20 37ddb48476adb1e1290a40b05d879b1d3d0fe96c59a7be2e302139a98fb58a3c6985fb8ce5c42bdee286ff5e0e768414720efcda2369939577434d42ca19d1fba737eab2a29e65eb23266e2b17494b0775e51db067612d9e6715be2ee1be4e37cf131377b2cbf862780aeaca46fcb0c65ba9c64270885f5c1a0c897a88041dde

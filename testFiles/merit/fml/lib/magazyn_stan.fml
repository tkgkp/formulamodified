:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: magazyn_stan.fml
:: Utworzony: 25.06.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły obliczające stan magazynowy, stan dostaw
::======================================================================================================================


\obl_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: oblicza ilosci na stanach magazynowych dla materialu wg uprawnien
::   WE:  _a  ref indeksu materialowego
::        _b  1 - wg danego magazynu
::            2 - wg operatora zamowien sprzedazy
::            3 - wg wszystkich magazynow dla uzytkownika
::            4 - wg magazynow do realizacji zamowien sprzedazy
::            5 - wg wszystkich magazynow oddzialu
::            6 - wg magazynow do wyliczenia dostepnosci surowcow na produkcje
::            7 - wg wszystkich magazynow dla uzytkownika bez skladow celnych i paletowych
::            8 - wg przekazanej tablicy magazynow - parametr _h
::            9 - wg operatora zamowien wewnetrznych
::            10 - wg magazynow do realizacji zamowien wewnetrznych
::            11 - wg wszystkich magazynów użytkownika (sprawdzenie uprawnień do oddziału i magazynu)
::       [_c] wg magazynu tylko dla _b=1 (gdy nie wypelnione to biezacy magazyn)
::       [_d] czy uzupelniac tabele tymczasowa  __smmag  (0 - nie, 1 - tak)
::       [_e] czy uzupelniac tabele tymczasowa - ze stanami rezerwacji
::            (0-nie, 1-tak, 2-tak i pomija juz istniejace magazyny)
::       [_f] stan na dzien - obliczany na podstawie dokumentow zaakceptowanych domyslnie date(0,0,0) wg pola SM.S
::            nie uwzglednia dokumentow rozchodowych wystwionych po dacie _f
::       [_g] wersja uproszczona obliczenia stanu dostepnego 0-nie(domyslnie) 1-tak
::       [_h] tablica magazynow
::       [_i] czy blokowac tabele ZD_NAG 0-nie 1-tak(domyslnie)
::       [_j] czy pomijac wydania na czytnik 0-nie (domyslnie) 1-tak
::       [_k] ref DK_C - w tym przypadku sprawdza zawieranie sie cechy
::       [_l] czy wyswietlac szczegoly 0-nie (domyslnie) 1-tak
::       [_m] czy uwzględniać tylko rezerwacje dostawy 0-nie (domyślnie) 1-tak
::  OLD: \obl_stan/funkcje.fml
::  OLD: \obl_stan/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>7 || _c:=null ?} || _c:=null ?};
{? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};
{? _>=6 || {? type_of(_f)<>4 || _f:=date(0,0,0) ?} || _f:=date(0,0,0) ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=0 ?} || _g:=0 ?};
{? _>=8 || {? type_of(_h)<>118 || _h:=0 ?} || _h:=0 ?};
{? _>=9 || {? type_of(_i)<>1 || _i:=1 ?} || _i:=1 ?};
{? _>=10 || {? type_of(_j)<>1 || _j:=0 ?} || _j:=0 ?};
{? _>=11 || {? type_of(_k)<>7 || _k:=null ?} || _k:=null ?};
{? _>=12 || {? type_of(_l)<>1 || _l:=0 ?} || _l:=0 ?};
{? _>=13 || {? type_of(_m)<>1 || _m:=0 ?} || _m:=0 ?};

ZMIENNE.P8:={? var_pres('__withj2')>0 ||  'T'+{? __withj2 || 'T' || 'N' ?} || 'NN' ?};

VAR_DEL.delete('__wydean','__ile','__matwmg','__wmg','__prod','__il_wyd','__il_rez','__il_zam','__il_poz'
 ,'__matwdr','__matzlm','__matzls','__ma2wdr','__ma2zlm','__ma2zls','__wy2ean','__withj2'
 ,'__il2wyd','__il2rez','__il2zam','__ma2wmg');

__wmg:={? _b=1 & _c=null || __wmg:=ST.MAG || _c ?};
__prod:=exec('tte_lic','tte');
:: uwzględniamy drugą jednostkę ewidencyjną
__withj2:=exec('FindAndGet','#table',M,_a,,"{? J2<>null() & WS2>0 || WS2 || 0 ?}",0);
{? __withj2<>0 || __withj2:=1/__withj2 ?};
_dokl_m:=3;
_jednm:='';

{? _l
|| _beerm:=BEER.M;
   BEER.M:=_a;
   _dokl_m:=BEER.M().DOKL;
   _jednm:=BEER.M().J().KOD;

   exec('add_infz','magazyn_stan',0,1,'dokładność (zaokrąglenie podczas redakcji dokumentów)'@
    ,exec('jaka_dok_m','jm',BEER.M),0);
   {? _b=1
   || exec('add_infz','magazyn_stan',1,0,'STAN W MAGAZYNIE'@,0,0,,'xwin16.png:91');
      exec('add_infz','magazyn_stan',12,0,'STAN DOSTĘPNY W MAGAZYNIE'@,0,0,,'xwin16.png:56')
   |? _b=2 | _b=9
   || exec('add_infz','magazyn_stan',1,0
           ,'STAN W MAGAZYNACH (wg uprawnionych magazynów do zamówień)'@,0,0,,'xwin16.png:91');
      exec('add_infz','magazyn_stan',12,0
           ,'STAN DOSTĘPNY W MAGAZYNACH (wg uprawnionych magazynów do zamówień)'@,0,0,,'xwin16.png:56')
   |? _b=3 | _b=5 | _b=7 | _b=11
   || exec('add_infz','magazyn_stan',1,0,'STAN W MAGAZYNACH (wg uprawnionych magazynów)'@,0,0,,'xwin16.png:91');
      exec('add_infz','magazyn_stan',12,0,'STAN DOSTĘPNY W MAGAZYNACH (wg uprawnionych magazynów)'@,0,0,,'xwin16.png:56')
   ?};
   exec('add_infz','magazyn_stan',2,0,'ILOŚĆ W WYDANIU'@,0,0,,'xwin16.png:185');
   exec('add_infz','magazyn_stan',3,0,'REZERWACJE TYMCZASOWE'@,0,0,,'xwin16.png:134');
   exec('add_infz','magazyn_stan',4.1,0,'REZERWACJE Z ZAMÓWIEŃ SPRZEDAŻY'@,0,0,,'xwin16.png:61');
   exec('add_infz','magazyn_stan',4.2,0,'REZERWACJE Z ZAMÓWIEŃ WEWNĘTRZNYCH'@,0,0,,'xwin16.png:61');
   exec('add_infz','magazyn_stan',4.3,0,'REZERWACJE Z DYSPOZYCJI ZAŁADUNKU'@,0,0,,'xwin16.png:61');
   exec('add_infz','magazyn_stan',5.1,0,'ILOŚĆ ZAMAWIANA Z ZAMÓWIEŃ SPRZEDAŻY'@,0,0,,'xwin16.png:92');
   exec('add_infz','magazyn_stan',5.2,0,'ILOŚĆ ZAMAWIANA Z ZAMÓWIEŃ WEWNĘTRZNYCH'@,0,0,,'xwin16.png:92');
   exec('add_infz','magazyn_stan',6,0,'ILOŚĆ DO REZERWACJI'@,0,0,,'xwin16.png:83');
   exec('add_infz','magazyn_stan',7,0,'ILOŚĆ W PRODUKCJI'@,0,0,,'xwin16.png:158');
   exec('add_infz','magazyn_stan',9.1,0,'ILOŚĆ W DRODZE (BEZ POTWIERDZENIA)'@,0,0,,'xwin16.png:162');
   exec('add_infz','magazyn_stan',9.2,0,'ILOŚĆ W DRODZE (POTWIERDZONA)'@,0,0,,'xwin16.png:162');
   exec('add_szcz','magazyn_stan',$BEER.M);
   exec('add_infz','magazyn_stan',0,0,BEER.M().KTM+' - '+BEER.M().N,0,0);
   MST.index('MG');
   MST.prefix(_a,null);
   {? MST.first
   || exec('add_infz','magazyn_stan',0,1,'stan minimalny domyślny'@,MST.MIN,1,,,,MST.MIN*__withj2);
      exec('add_infz','magazyn_stan',0,1,'stan maksymalny domyślny'@,MST.MAX,2,,,,MST.MAX*__withj2)
   ?};
   _oldwmg:=__wmg;
   exec('add_zami','magazyn_stan',_a,_b,__wmg);
   __wmg:=_oldwmg;
   BEER.M:=_beerm
?};

{? _e
|| {? var_pres('__strez')>100
     || __strez.prefix(); {? __strez.first() || {! |? __strez.del() !} ?}
     || __strez:=tab_tmp(6,'KTM','STRING[50]','Ktm'@
          ,'KT2','STRING[50]','Ktm'@
          ,'MAG','STRING[10]','Magazyn'@
          ,'TYP','STRING[15]','Typ rezerwacji'@
          ,'SYM','STRING[20]','Symbol dokumentu'@
          ,'POZ','INTEGER','Pozycja na dokumencie'@
          ,'ILE','REAL','Ilość zarezerwowana'@
          ,'SQL','STRING[18]','Ref SQL-owy pozycji'@)
     ?}
?};

BEER.S:=0;BEER.SD:=0;BEER.SRT:=0;BEER.SRR:=0;BEER.SSZ:=0;BEER.WDR:=0;BEER.SP:=0;
BEER.S2:=0;BEER.SD2:=0;BEER.SRT2:=0;BEER.SRR2:=0;BEER.SSZ2:=0;BEER.WDR2:=0;BEER.SP2:=0;

:: suma rezerwacji tymczasowych na potrzeby rozpisywania pozycji zamowien
BEER.SW2:=0;
:: wydania dla kompletacji wysylki
__wydean:=0;
__wy2ean:=0;

M.cntx_psh();
SM.cntx_psh();
ND.cntx_psh();
DK.cntx_psh();
REZ.cntx_psh();
ZK_N.cntx_psh();
ZK_P.cntx_psh();
ZD_NAG.cntx_psh();
ZD_POZ.cntx_psh();
ZDP_NAG.cntx_psh();
ZDP_POZ.cntx_psh();
USERS_UP.cntx_psh();
BUFMG.cntx_psh();
ZL.cntx_psh();
ZLIM.cntx_psh();

:: uprawnione magazyny
{? _b=11
|| _filtr:=MG.f_active();
   {? ~_filtr || exec('zakrMag','lmg') ?};
   __ile:=MG.f_size();
   _ref:=MG.ref();
   BUFMG.index('US');
   BUFMG.prefix(OPERATOR.USER,11);
   {? BUFMG.first() || {! |? BUFMG.del() !} ?};
   {? MG.f_first()
   || {!
      |? BUFMG.blank();
         BUFMG.US:=OPERATOR.USER;
         BUFMG.MG:=MG.ref();
         BUFMG.TYP:=11;
         BUFMG.add(1);
         MG.f_next()
      !}
   ?};
   MG.f_seek(_ref);
   {? ~_filtr || MG.f_clear() ?}
|? _b=8
|| __ile:=obj_len(_h);
   BUFMG.index('US');
   BUFMG.prefix(OPERATOR.USER,8);
   {? BUFMG.first() || {! |? BUFMG.del() !} ?};
   {? _h.first()
   || {!
      |? BUFMG.blank();
         BUFMG.US:=OPERATOR.USER;
         BUFMG.MG:=_h[1];
         BUFMG.TYP:=8;
         BUFMG.add(1);
         _h.next()
      !}
   ?}
|? _b=1
|| BUFMG.index('US');
   BUFMG.prefix(OPERATOR.USER,5,__wmg);
   {? BUFMG.first()
   || _time:=exec('spr_dttm','magazyn_inw',__wmg,_f,1);
      {? (*_time)>(*time(23,59,0)) || _time:=time(0,0,0) ?};
      {? ~_d | _f=date(0,0,0) |
       (_d & ~exec('czyinwds','magdok_wspolne',__wmg,_f,_a,,1)
        & exec('czy_z_ok','okresy',1,0,_f~1,_f~2,__wmg)
        & exec('sprprzec','magazyn_przec',__wmg,_f,_a,_time)<3)
      || __ile:=1
      || __ile:=0
      ?}
   || __ile:=0
   ?}
|? _b>1
|| BUFMG.index('US');
   BUFMG.prefix(OPERATOR.USER,_b);
   __ile:=BUFMG.size()
|| __ile:=0
?};

SM.use('stm__'+ST.ODDZ+'zb');
SM.index('SM');
{? __ile & BUFMG.first()
||
:: obliczamy stan wg dokumentow na dana chwile
   _first:=1;
   _dp:=obj_new(8);    {! _ii..8    |! _dp[_ii]:=0 !};
   _sd:=obj_new(2*__ile); {! _ii..2*__ile |! _sd[_ii]:=0 !};

:: _stats[1] _stats[3] - niezgodne, _stats[2] _stats[4] - w badaniu
   _stats:=obj_new(4); {! _il..4 |! _stats[_il]:=0 !};
   _buf:=obj_new(2);  _buf[1]:=0; _buf[2]:=0;

   _dr:=obj_new(10); {! _ii..10 |! _dr[_ii]:=0 !};
   _poz_rez:=0;
   _pozrez1:=0;
   _zam_mag:=obj_new(2*__ile);

   __matwdr:=0;  __ma2wdr:=0;
   __matzls:=0;  __ma2zls:=0;

:: obliczamy ilosci rezerwacji wg zamowien na dana chwile
   _bufzk:=obj_new(2); _bufzk[1]:=_bufzk[2]:=0;

   _ii:=0;
   {!
   |?

   {? BUFMG.MG().ODDZ=ST.ODDZ_KOD &
      {? ~(_b=3 | (_b=7 & BUFMG.MG().SKL='N' & BUFMG.MG().PAL='N'))
      || 1
      || _time:=exec('spr_dttm','magazyn_inw',BUFMG.MG,_f,1);
         {? (*_time)>(*time(23,59,0)) || _time:=time(0,0,0) ?};
         {? ~_d | _f=date(0,0,0) | (_d & ~exec('czyinwds','magdok_wspolne',BUFMG.MG,_f,_a,,1)
               & exec('czy_z_ok','okresy',1,0,_f~1,_f~2,BUFMG.MG)
               & exec('sprprzec','magazyn_przec',BUFMG.MG,_f,_a,_time)<3)
         || 1
         || 0
         ?}
      ?}
   || _ii+=1;
      SM.prefix(BUFMG.MG,_a);
      {? SM.first()
      || {? ~__withj2
         || _btb:=obj_new(2);
            _btb[1]:={? _k<>null || exec('stnwgdkc','magazyn_stan',BUFMG.MG,_a,_k) || SM.S ?};
            _btb[2]:=0;
            _bil:=_btb[1];
            _dp[2]+=_btb[1];
            _dp[6]+=_btb[2]
         || {? _k<>null
            || _btb:=exec('stnwgdkc','magazyn_stan',BUFMG.MG,_a,_k,,,1);
               _bil:=_btb[1]
            || _btb:=obj_new(2);
               _btb[1]:=SM.S;
               _btb[2]:=SM.S2;
               _bil:=SM.S
            ?};
            _bil:=_btb[1];
            _dp[2]+=_btb[1];
            _dp[6]+=_btb[2]
         ?};
         {? _l
         || _korzen:=exec('add_infz','magazyn_stan',1,1,'Magazyn: %1'@[BUFMG.MG().SYM],_bil,_ii,,,,_btb[2]);
            exec('add_infz','magazyn_stan',12,1,'Magazyn: %1'@[BUFMG.MG().SYM],SM.SD,_ii,,,,SM.SD2);
            {? 1+BUFMG.MG().TYP='D'
            || exec('add_infz','magazyn_stan',1,1,'Nieokreślone'@,SM.S_O,_ii+1,_korzen
                ,exec('get','#params',540011,2),,SM.S_O2);
               exec('add_infz','magazyn_stan',1,1,'Zgodne'@,SM.S-SM.S_O-SM.S_N,_ii+2,_korzen
                ,exec('get','#params',540012,2),,SM.S2-SM.S_O2-SM.S_N2);
               exec('add_infz','magazyn_stan',1,1,'Niezgodne'@,SM.S_N,_ii+3,_korzen
                ,exec('get','#params',540013,2),,SM.S_N2)
            ?}
         ?};
         obj_del(_btb);
::       statusy dostaw
         {? ~__withj2
         || _stats[1]+={? _k=null() || SM.S_N || exec('stnwgdkc','magazyn_stan',BUFMG.MG,_a,_k,,'N') ?};
            _stats[2]+={? _k=null() || SM.S_O || exec('stnwgdkc','magazyn_stan',BUFMG.MG,_a,_k,,'O') ?}
         || {? _k=null()
            || _stats[1]+=SM.S_N; _stats[2]+=SM.S_O; _stats[3]+=SM.S_N2; _stats[4]+=SM.S_O2
            || _btb:=exec('stnwgdkc','magazyn_stan',BUFMG.MG,_a,_k,,'N',1);
               _stats[1]+=_btb[1]; _stats[3]+=_btb[2];
               obj_del(_btb);
               _btb:=exec('stnwgdkc','magazyn_stan',BUFMG.MG,_a,_k,,'O',1);
               _stats[2]+=_btb[1]; _stats[4]+=_btb[2];
               obj_del(_btb)
            ?}
         ?}
      ?};
      __il_wyd:=__il2wyd:=0;
      ND.cntx_psh();
      DK.cntx_psh();
      DK_L.cntx_psh();
      SC.cntx_psh();
      OKR.cntx_psh;
      OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      ||
         {!
         |?
            DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
            ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));
            DK_L.use('doklk'+ST.ODDZ+($OKR.ROK+2));

            {? _g<=0 & _f<>date(0,0,0)
            || DK.index('DOKMAT');
               DK.prefix('T',_a,BUFMG.MG);
               {? DK.last()
               ||
                  {!
                  |?
                     {? DK.N().D>_f
                     || {? DK.PLUS='T'
                        || _czybo:=DK.N().TYP().T='BO';
                           SC.use('stc__'+(DK.NDK+3));
                           SC.index('SN');
                           SC.prefix(BUFMG.MG,_a,DK.RDK,DK.NDK);
                           {? SC.first()
                           ||
                             {? _czybo
                              | (SC.D<_f & (form(DK.RDK)+DK.NDK)<>(form(#DK.ref())+DK.name()))
                              | (SC.D>_f & _g=-2)
                             || _scst:={? _czybo | SC.S>DK.IL || DK.IL || SC.S ?};
                                _dp[2]-=_scst;
                                {? SC.STATS().KIND='O' || _stats[2]-=_scst ?};
                                {? __withj2
                                || _scst:={? _czybo | SC.S2>DK.IL2 || DK.IL2 || SC.S2 ?};
                                   _dp[6]-=_scst;
                                   {? SC.STATS().KIND='O' || _stats[4]-=_scst ?}
                                ?}
                             |? ~(BUFMG.MG().TYP*'DOST')
                             || _dp[2]-=DK.IL;
                                {? __withj2 || _dp[6]-=DK.IL2 ?}
                             ?}
                           || _dp[2]-=DK.IL;
                              {? __withj2 || _dp[6]-=DK.IL2 ?}
                           ?}
                        || {? ~_g
                           || SC.use('stc__'+(DK.NDK+3));
                              SC.index('SN');
                              SC.prefix(BUFMG.MG,_a,DK.RDK,DK.NDK);
                              {? SC.first() & SC.D<=_f
                              || _dp[2]+=DK.IL;
                                 {? __withj2 || _dp[6]+=DK.IL2 ?}
                              ?}
                           ?}
                        ?};
                        DK.prev()
                     || 0
                     ?}
                  !}
               ?}
            ?};

            OKR.next()
         !}
      ?};
      OKR.cntx_pop;
      DK.cntx_pop();
      ND.cntx_pop();
      DK_L.cntx_pop();
      SC.cntx_pop();

::    rozchodowe niezaakceptowane

      _bil:=0;
      REZ.index('MG_M');
      REZ.prefix(BUFMG.MG,_a,'B','W');
      {? REZ.first()
      || {!
         |? {? _k=null | (_k<>null & exec('czyzawch','mat_atr',_k,REZ.DK_C))
            || BEER.SW+=REZ.ILR;
               BEER.SW2+=REZ.IL2;
               {? _f=date(0,0,0) || _dp[3]+=REZ.ILR; _dp[7]+=REZ.IL2
               |? REZ.ODDT<=_f   || _dp[3]+=REZ.ILR; _dp[7]+=REZ.IL2
               ?};
::             ilosc w wydaniu moze byc wieksza od stanu
               {? _dp[3]>_dp[2] || _dp[3]:=_dp[2] ?};
               {? _dp[7]>_dp[6] || _dp[7]:=_dp[6] ?};
               __il_wyd+=REZ.ILR;
               __il2wyd+=REZ.IL2;
               {? ~_g & _e
               || exec('addstrez','magazyn_stan',BUFMG.MG().SYM,'WYDANIE'
                       ,REZ.ILR,REZ.SYMDOK,REZ.NRPOZ,$REZ.DK,REZ.M().KTM)
               ?};
               {? _l
               || exec('add_infz','magazyn_stan',2,2,
                   {? REZ.KH<>null || 'Dokument: %1, %2, dla: %3, z dnia: %4'@[REZ.TYPDOK,REZ.SYMDOK,REZ.KH().SKR,form(REZ.ODDT)]
                                   || 'Dokument: %1, %2, z dnia: %3'@[REZ.TYPDOK,REZ.SYMDOK,form(REZ.ODDT)] ?}
                   ,REZ.ILR,_ii,,,$REZ.DK,REZ.IL2)
               ?};
               _bil+=REZ.ILR
            ?};
            REZ.next()
         !}
      ?};
      {? _l
      || _bufz:=tab_tmp(2,'EANN','STRING[16]','','RSQL','STRING[16]','','MIN','REAL','');
         _kmpl:=tab_tmp(1,'EANN','STRING[16]','','NRC','INTEGER','','DATA','DATE','','ILE','REAL','','IL2','REAL','');
         EANN.cntx_psh();
         EANP.cntx_psh();
         EANP.index('TYP');
         EANP.prefix('Z',_a,BUFMG.MG);
         {? EANP.first()
         || {!
            |? {? EANP.EANN<>null & EANP.EANN().AKT='T'
               || {? EANP.IL>0
                  || {? EANP.RSQL<>'' & ((5+EANP.RSQL)='zkpoz')
                      & (_bufz.clear(); ~_bufz.find_key($EANP.EANN,EANP.RSQL))
                     || _bufz.blank();
                        _bufz.EANN:=$EANP.EANN;
                        _bufz.RSQL:=EANP.RSQL;
                        _bufz.MIN:=exec('FindAndGet','#table',ZK_P,EANP.RSQL,,"ILRB",0);
                        _bufz.add(1)
                     ?};
                     _kmpl.clear();
                     {? ~_kmpl.find_key($EANP.EANN)
                     || _kmpl.blank();
                        _kmpl.EANN:=$EANP.EANN;
                        _kmpl.NRC:=EANP.EANN().NRC;
                        _kmpl.DATA:=EANP.EANN().DATA;
                        _kmpl.ILE:={? EANP.EANN().STAN<>'Z' || EANP.IL || EANP.ILS ?};
                        {? __withj2 || _kmpl.IL2:={? EANP.EANN().STAN<>'Z' || EANP.IL || EANP.ILS ?}*EANP.M().WS2 ?};
                        _kmpl.add(1)
                     || _kmpl.ILE+={? EANP.EANN().STAN<>'Z' || EANP.IL || EANP.ILS ?};
                        {? __withj2 || _kmpl.IL2+={? EANP.EANN().STAN<>'Z' || EANP.IL || EANP.ILS ?}*EANP.M().WS2 ?};
                        _kmpl.put(1)
                     ?}
                  ?}
               ?};
               EANP.next()
            !}
         ?};
::       dodatkowo sprawdzenie indeksu na paletach zakładamy, ze na EANP nie ma indeksu materiałowego
         PAL.cntx_psh();
         PAL_POZ.cntx_psh();
         EANP.index('TYP');
         EANP.prefix('Z',null,BUFMG.MG);
         {? EANP.first()
         || _beerm:=BEER.M; BEER.M:=_a;
            {!
            |? {? EANP.EANN<>null & EANP.EANN().AKT='T' & EANP.PAL<>null & EANP.IL>0
               || {? EANP.PAL().AKT='N'
                  || PAL_POZ.use('palet'+form(EANP.PAL().AR-2000,-2,0,'99'))
                  || PAL_POZ.use('paletyp')
                  ?};
                  PAL_POZ.index('PAL');
                  PAL_POZ.prefix(EANP.PAL,BEER.M().KTM,BEER.M().KTM);
                  {? PAL_POZ.first()
                  || {!
                     |? {? PAL_POZ.ILP>0
                        || {? EANP.RSQL<>'' & ((5+EANP.RSQL)='zkpoz')
                            & (_bufz.clear(); ~_bufz.find_key($EANP.EANN,EANP.RSQL))
                           || _bufz.blank();
                              _bufz.EANN:=$EANP.EANN;
                              _bufz.RSQL:=EANP.RSQL;
                              _bufz.MIN:=exec('FindAndGet','#table',ZK_P,EANP.RSQL,,"ILRB",0);
                              _bufz.add(1)
                           ?};
                           _kmpl.clear();
                           {? ~_kmpl.find_key($EANP.EANN)
                           || _kmpl.blank();
                              _kmpl.EANN:=$EANP.EANN;
                              _kmpl.NRC:=EANP.EANN().NRC;
                              _kmpl.DATA:=EANP.EANN().DATA;
                              _kmpl.ILE:=PAL_POZ.ILP;
                              {? __withj2 || _kmpl.IL2:=PAL_POZ.ILP*PAL_POZ.M().WS2 ?};
                              _kmpl.add(1)
                           || _kmpl.ILE+=PAL_POZ.ILP;
                              {? __withj2 || _kmpl.IL2+=PAL_POZ.ILP*PAL_POZ.M().WS2 ?};
                              _kmpl.put(1)
                           ?}
                        ?};
                        PAL_POZ.next()
                     !}
                  ?}
               ?};
               EANP.next()
            !};
            BEER.M:=_beerm
         ?};
         PAL.cntx_pop();
         PAL_POZ.cntx_pop();

         _kmpl.clear();
         {? _kmpl.first()
         || _wys:=obj_new(2);
            {!
            |? _min:=0;

               _bufz.clear();
               _bufz.prefix(_kmpl.EANN);
               {? _bufz.first() || {! |? _min+=_bufz.MIN; _bufz.next() !} ?};
               _wys[1]:={? _min<=_kmpl.ILE || _kmpl.ILE-_min || 0 ?};
               _wys[2]:=_kmpl.ILE;
               {? _wys[1]>0
               || exec('add_infz','magazyn_stan',2,2,'Kompletacja wysyłki: Czytnik %1,'
                   ' z dnia: %2'@[form(_kmpl.NRC),form(_kmpl.DATA)],_wys[1],_ii,,,'eanp',_wys[1]*__withj2)
               ?};
               {? _min>0
               || exec('add_infz','magazyn_stan',2,3,'Zarezerwowana kompletacja wysyłki: Czytnik %1,'
                  ' z dnia: %2'@[form(_kmpl.NRC),form(_kmpl.DATA)],_wys[2],_ii,,,'eanp',_wys[2]*__withj2)
               ?};
               _kmpl.next()
            !};
            obj_del(_wys)
         ?};
         obj_del(_kmpl);
         obj_del(_bufz);

         EANP.index('TYP');
         EANP.prefix('W',_a,BUFMG.MG);
         {? EANP.first()
         || {!
            |? {? EANP.EANN<>null & EANP.EANN().AKT='T'
               || {? EANP.ILS>0
                  || exec('add_infz','magazyn_stan',2,2,'Wydanie magazynowe: Czytnik %1'
                      ', z dnia: %2'@[form(EANP.EANN().NRC),form(EANP.EANN().DATA)],EANP.ILS,_ii,,
                      ,$EANP.ref(),EANP.ILS*EANP.M().WS2)
                  ?}
               ?};
               EANP.next()
            !}
         ?};
         EANN.cntx_pop();
         EANP.cntx_pop()
      ?};

      __wydean:={? ~_j || exec('oblmeanp','magazyn_stan',BUFMG.MG,_a,,,,_k) || 0 ?};
      __il_wyd+=__wydean;
      __il2wyd+=__wydean*__withj2;
      _dp[3]+=__wydean;
      _dp[7]+=__wydean*__withj2;
      {? ~_g
      || SM.prefix(BUFMG.MG,_a);
         {? SM.first()
         || SM.W:=__il_wyd;
            SM.W2:=__il2wyd;
            SM.put()
         ?}
      ?};
      __il_wyd:=0;
      __il2wyd:=0;

      _sd[_ii]:=_dp[2]-_dp[3]-_buf[1]-_stats[1]-_stats[2];
      _sd[_ii+__ile]:=_dp[6]-_dp[7]-_buf[2]-_stats[3]-_stats[4];
      {? _sd[_ii]<>0 || _buf[1]+=_sd[_ii] ?};
      {? _sd[_ii+__ile]<>0 || _buf[2]+=_sd[_ii+__ile] ?};

::    obliczamy ilosci rezerwacji wg zamowien na dana chwile
      __il_rez:=0;  __il2rez:=0;
      __il_zam:=0;  __il2zam:=0;
      _zam_mag[_ii]:=0; _zam_mag[_ii+__ile]:=0;

      REZ.index('MG_M');
      REZ.prefix(BUFMG.MG,_a,'B');
      {? REZ.first()
      || __il_wyd:=0;
         __il2wyd:=0;
         {!
         |? _tt:=REZ.TYP;
            _mskzkp:=ref_name(REZ.ZK_P);
            ZK_N.cntx_psh();
            ZK_P.cntx_psh();
            {? _mskzkp<>'' & _mskzkp<>ZK_P.name()
            || ZK_N.use('zknag'+(_mskzkp+3));
               ZK_P.use(_mskzkp)
            ?};
            _lim:=(_tt='Z' & ref_name(REZ.ZK_P().ZLIM)<>'') | (_tt='S' & ref_name(REZ.ZLIM)<>'');
            {? _tt<>'W'
            || {? _tt='Z' & _g>=0 & _m & _f<>date(0,0,0) & REZ.SC<>''
               || 0
               |? _tt<>'F'
               || {? _k=null | (_k<>null & exec('czyzawch','mat_atr',_k,REZ.DK_C))
                  || _dr[{? ( _tt='T' | _tt='M' ) || 2 |? _tt='P' || 4 || 1 ?}]+=REZ.ILR;
                     _dr[5+{? ( _tt='T' | _tt='M' ) || 2 |? _tt='P' || 4 || 1 ?}]+=REZ.IL2
                  ?}
               || {? REZ.SC='' | _k=null()
                   | (_k<>null & exec('czyzawch','mat_atr',_k,exec('FindAndGet','#table',DK,REZ.SC,,"DK_C",null())))
                  || {? _f=date(0,0,0) | REZ.ODDT<=_f
                     || _dp[3]+=REZ.ILR; __il_wyd+=REZ.ILR;
                        _dp[7]+=REZ.IL2; __il2wyd+=REZ.IL2;
                        _bufzk[1]-=REZ.ILR; _bufzk[2]-=REZ.IL2
                     ?}
                  ?}
               ?};
               {? ~_g
               || {? _tt='T' || _sd[_ii]-=REZ.ILR; _sd[_ii+__ile]-=REZ.IL2
                  |? _tt='P' || 0
                             || _sd[_ii]-=REZ.ILR; _sd[_ii+__ile]-=REZ.IL2
                  ?};
                  {? _e
                  || _opis:={? _tt='T' || 'REZ.TYMCZ.'
                            |? _tt='Z' & ~_lim || 'ZAMÓWIENIE'
                            |? _tt='F' || 'FAKTURA'
                            |? (_tt='Z' | _tt='S') & _lim || 'ZLECENIE'
                            || ''
                            ?};
                     {? ~_lim
                     || exec('addstrez','magazyn_stan',REZ.MG().SYM,_opis,REZ.ILR,REZ.KH().SKR,REZ.NRPOZ,REZ.REFSQL
                         ,REZ.M().KTM)
                     || exec('addstrez','magazyn_stan',REZ.MG().SYM,_opis,REZ.ILR,REZ.ZL().SYM,REZ.NRPOZ,REZ.REFSQL
                         ,REZ.M().KTM)
                     ?}
                  ?}
               ?};
               {? _l
               || {? _tt='Z' & ~_lim
                  || _zspr:={? REZ.ZK_P().RODZ='Z'
                            || {? REZ.TR_ZL='' || 1 || 2 ?}
                            || {? REZ.TR_ZL='' || 0 || 2 ?}
                            ?};
                     _zlec:={? REZ.ZL<>null() || 1 |? REZ.ZK_P().ZL<>null() || 2 || 0 ?};
                     _txtt:=exec('opis2Zam','magazyn_stan',_zspr,REZ.SYMDOK,REZ.US().KOD
                                ,{? REZ.KH<>null() || REZ.KH().SKR || '' ?}
                                ,{? REZ.MG<>null() || REZ.MG().SYM || '' ?}
                                ,{? _zlec=1 || REZ.ZL().SYM |? _zlec || REZ.ZK_P().ZL().SYM || '' ?}
                                ,{? _zlec=1 || REZ.ZL().JORG().KOD |? _zlec || REZ.ZK_P().ZL().JORG().KOD || '' ?});
                     exec('add_infz','magazyn_stan',{? _zspr=1 || 4.1
                                                    |? _zspr=0 || 4.2
                                                    || 4.3
                                                    ?},1,_txtt,REZ.ILR,_ii,,,$REZ.ZK_P,REZ.IL2)
                  |? _tt='F'
                  || exec('add_infz','magazyn_stan',2,2,'Faktura: %1, wystawił: %2, dla: %3, '
                     'w dniu: %4'@[REZ.SYMDOK,REZ.US().KOD,REZ.KH().SKR,form(REZ.ODDT)],REZ.ILR,_ii,,,$REZ.FAP,REZ.IL2)
                  |? _tt='T'
                  || {? REZ.PROJEKT<>''
                     ||
                        exec('projekt2projm','projekty',REZ.PROJEKT);
                        exec('add_infz','magazyn_stan',3,2,'Użytkownik: %1, ważna do: %2, dla projektu: %3'
                               '>%4>%5>%6>%7>%8'@[REZ.US().KOD,form(REZ.DODT),PROJM.PROJN,PROJM.ETAPO,PROJM.ZLO,PROJM.RMSS,form(PROJM.ZDNR,,,'99'),form(PROJM.ZDLP,,,'99')]
                              ,REZ.ILR,_ii,,,,,REZ.IL2)
                     || exec('add_infz','magazyn_stan',3,2,'Użytkownik: %1, dla: %2, ważna do: %3'@[REZ.US().KOD,REZ.KH().SKR,form(REZ.DODT)]
                           ,REZ.ILR,_ii,,,$REZ.ref(),REZ.IL2)
                     ?}
                  |? (_tt='Z' | _tt='S') & _lim
                  || {? REZ.ZL<>null()
                     || _symzl:=REZ.ZL().SYM;
                        _wydzial:=REZ.ZL().JORG().KOD
                     || _symzl:=REZ.ZK_P().ZL().SYM;
                        _wydzial:=REZ.ZK_P().ZL().JORG().KOD
                     ?};
                     _txtwyd:={? _wydzial<>'' || ', na wydziale: %1'@[_wydzial] || '' ?};
                     exec('add_infz','magazyn_stan',6,1,'Zlecenie: %1%2'@[_symzl,_txtwyd]
                      ,REZ.ILR,_ii,,,$REZ.ZK_P,REZ.IL2)
                  ?}
               ?}
            ?};
            ZK_N.cntx_pop();
            ZK_P.cntx_pop();
            REZ.next()
         !}
      || __il_wyd:=0;
         __il2wyd:=0
      ?};

::    aktualizacja ilosci w wydaniu o faktury sprzedazy
      {? ~_g & (__il_wyd | __il2wyd)
      || SM.prefix(BUFMG.MG,_a); {? SM.first() || SM.W+=__il_wyd; SM.W2+=__il2wyd; SM.put() ?} ?};
      _bufzk[1]+=-_dr[1]-_dr[2];
      _bufzk[2]+=-_dr[6]-_dr[7];

::    obliczamy ilosc zamawiane bez magazynu dla produkcji i dla zamowien
      {? 1
      || REZ.index('MG_M');
         REZ.prefix(BUFMG.MG,_a,'W');
         {? REZ.first()
         || {!
            |? {? REZ.TYP='Z'
               || _mskzkp:=ref_name(REZ.ZK_P);
                  ZK_N.cntx_psh();
                  ZK_P.cntx_psh();
                  {? _mskzkp<>'' & _mskzkp<>ZK_P.name()
                  || ZK_N.use('zknag'+(_mskzkp+3));
                     ZK_P.use(_mskzkp)
                  ?};
                  _lim:=ref_name(REZ.ZK_P().ZLIM)<>'';
                  {? _l & ~_lim
                  || _zspr:=REZ.ZK_P().RODZ='Z';
                     _zlec:={? REZ.ZL<>null() || 1 |? REZ.ZK_P().ZL<>null() || 2 || 0 ?};
                     _txtt:=exec('opis2Zam','magazyn_stan',_zspr,REZ.SYMDOK,REZ.US().KOD
                                ,{? REZ.KH<>null() || REZ.KH().SKR || '' ?}
                                ,{? REZ.MG<>null() || REZ.MG().SYM || '' ?}
                                ,{? _zlec=1 || REZ.ZL().SYM |? _zlec || REZ.ZK_P().ZL().SYM || '' ?}
                                ,{? _zlec=1 || REZ.ZL().JORG().KOD |? _zlec || REZ.ZK_P().ZL().JORG().KOD || '' ?});
                     exec('add_infz','magazyn_stan',6,1,_txtt,REZ.ILR,_ii,,,$REZ.ZK_P,REZ.IL2)
                  |? _l & _lim
                  || {? REZ.ZL<>null()
                     || _symzl:=REZ.ZL().SYM;
                        _wydzial:=REZ.ZL().JORG().KOD
                     || _symzl:=REZ.ZK_P().ZL().SYM;
                        _wydzial:=REZ.ZK_P().ZL().JORG().KOD
                     ?};
                     _txtwyd:={? _wydzial<>'' || ', na wydziale: %1'@[_wydzial] || '' ?};
                     _txtt:={? REZ.RP_REZ='T' || ' (produkcja dedykowana)'@ || '' ?};
                     exec('add_infz','magazyn_stan',6,1,'Zlecenie%3: %1%2'@[_symzl,_txtwyd,_txtt]
                      ,REZ.ILR,_ii,,,$REZ.ZK_P,REZ.IL2)
                  ?};
                  ZK_N.cntx_pop();
                  ZK_P.cntx_pop();
                  _dr[3]+=REZ.ILR
               ?};
               REZ.next()
            !}
         ?};
         {? _first
          & (_b<>1 | exec('uprmgpow','magazyn_stan',BUFMG.MG,2,OPERATOR.USER)
                   | exec('uprmgpow','magazyn_stan',BUFMG.MG,9,OPERATOR.USER))
         || _first:=0;
            REZ.index('MG_M');
            REZ.prefix(null,_a,'W');
            {? REZ.first()
            || {!
               |? {? REZ.TYP='Z'
                  || _mskzkp:=ref_name(REZ.ZK_P);
                     ZK_N.cntx_psh();
                     ZK_P.cntx_psh();
                     {? _mskzkp<>'' & _mskzkp<>ZK_P.name()
                     || ZK_N.use('zknag'+(_mskzkp+3));
                        ZK_P.use(_mskzkp)
                     ?};
                     _lim:=ref_name(REZ.ZK_P().ZLIM)<>'';
                     {? _l & ~_lim
                     || _zspr:=REZ.ZK_P().RODZ='Z';
                        _zlec:={? REZ.ZL<>null() || 1 |? REZ.ZK_P().ZL<>null() || 2 || 0 ?};
                        _txtt:=exec('opis2Zam','magazyn_stan',_zspr,REZ.SYMDOK,REZ.US().KOD
                                   ,{? REZ.KH<>null() || REZ.KH().SKR || '' ?}
                                   ,{? REZ.MG<>null() || REZ.MG().SYM || '' ?}
                                   ,{? _zlec=1 || REZ.ZL().SYM |? _zlec || REZ.ZK_P().ZL().SYM || '' ?}
                                   ,{? _zlec=1 || REZ.ZL().JORG().KOD |? _zlec || REZ.ZK_P().ZL().JORG().KOD || '' ?});
                        exec('add_infz','magazyn_stan',6,1,_txtt,REZ.ILR,_ii,,,$REZ.ZK_P,REZ.IL2)
                     |? _l & _lim
                     || {? REZ.ZL<>null()
                        || _symzl:=REZ.ZL().SYM;
                           _wydzial:=REZ.ZL().JORG().KOD
                        || _symzl:=REZ.ZK_P().ZL().SYM;
                           _wydzial:=REZ.ZK_P().ZL().JORG().KOD
                        ?};
                        _txtwyd:={? _wydzial<>'' || ', na wydziale: %1'@[_wydzial] || '' ?};
                        _txtt:={? REZ.RP_REZ='T' || ' (produkcja dedykowana)'@ || '' ?};
                        exec('add_infz','magazyn_stan',6,1,'Zlecenie%3: %1%2'@[_symzl,_txtwyd,_txtt]
                         ,REZ.ILR,_ii,,,$REZ.ZK_P,REZ.IL2)
                     ?};
                     ZK_N.cntx_pop();
                     ZK_P.cntx_pop();
                     _dr[3]+=REZ.ILR;
                     _dr[8]+=REZ.IL2
                  ?};
                  REZ.next()
               !}
            ?}
         ?};
         ZK_P.index('CZYBEZ');
         ZK_P.prefix('A','T',1,_a);
         {? ZK_P.first()
         || {!
            |? _licz:='ZA'+{? ZK_P.RODZ='Z' || 'M' || 'W' ?};
               _bmg:={? _b=5 | _b=3 || null() || BUFMG.MG ?};
               {? ZK_P.RMAG=BUFMG.MG | (_ii=1 & ZK_P.RMAG=null & exec('uprmgzam','magazyn_stan',_bmg,_licz))
               || {? _l
                  || _zspr:=_licz='ZAM';
                     _zlec:={? ZK_P.ZL<>null() || 1 || 0 ?};
                     _txtt:=exec('opis2Zam','magazyn_stan',_zspr,ZK_P.N().SYM,ZK_P.N().US().KOD
                                ,{? ZK_P.N().KH<>null() || ZK_P.N().KH().SKR || '' ?}
                                ,{? ZK_P.RMAG<>null() || ZK_P.RMAG().SYM || '' ?}
                                ,{? _zlec=1 || ZK_P.ZL().SYM || '' ?}
                                ,{? _zlec=1 || ZK_P.ZL().JORG().KOD || '' ?});
                     exec('add_infz','magazyn_stan',{? _zspr || 5.1 || 5.2 ?},1,_txtt,ZK_P.ILBEZ,_ii,,
                      ,$ZK_P.ref(),ZK_P.ILBEZ*__withj2)
                  ?};
                  _dr[4]+=ZK_P.ILBEZ;
                  _dr[8]+=ZK_P.ILBEZ*__withj2
               ?};
               ZK_P.next()
            !}
         ?}
      ?};

::    obliczamy ilosci w drodze
      {? ~_g
      || _tt:='N';
         __matwmg:=0;
         __ma2wmg:=0;
         BEER.MG:=BUFMG.MG;
         ZD_POZ.cntx_psh();
         ZD_POZ.index('ZRE');
         ZD_POZ.prefix(_a,BUFMG.MG,'N');
         {? ZD_POZ.first()
         || {!
            |? {? ZD_POZ.IL_POZ>0 & {? _i=1 & ~_l || ZD_NAG.r_lock(0,1,1,#ZD_POZ.ZD_NAG,) || 1 ?}
               || {? 'ACN'*ZD_POZ.ZD_NAG().STAN>0
                  || _ilpoz:=ZD_POZ.IL_POZ;
::                   szukamy potwierdzen tylko aktywnych - w biezacej masce
                     ZDP_POZ.index('ZD_POZ');
                     ZDP_POZ.prefix($ZD_POZ.ref(),$ZD_POZ.ref());
                     {? ZDP_POZ.first()
                     || {!
                        |? {? ZDP_POZ.ZDP_NAG().AKC='T'
                           || __il_poz:=ZDP_POZ.IL-exec('obl_rea','zamdst_rea',$ZDP_POZ.ref());
                              {? __il_poz>0
                              || __matwmg+=__il_poz;
                                 __ma2wmg+=__il_poz*__withj2;
                                 {? _l
                                 || exec('add_infz','magazyn_stan',9.2,2
                                    ,'Potwierdzenie: %1 (Zamówienia: %5), wystawił: %2, dla: %3, '
                                    'wysyłka: %4'@[ZDP_POZ.ZDP_NAG().SYM,ZDP_POZ.ZDP_NAG().US().KOD
                                    ,ZDP_POZ.ZDP_NAG().KH().SKR
                                    ,{? ZDP_POZ.D_WYS<>date(0,0,0) || $ZDP_POZ.D_WYS || $ZDP_POZ.ZDP_NAG().D_WYS ?}
                                    ,ZDP_POZ.ZD_N_SYM]
                                    +{? ZDP_POZ.ZDP_NAG().D_REA<>date(0,0,0)
                                     || ', dostawa: %1'@[$ZDP_POZ.ZDP_NAG().D_REA]
                                     || ''
                                     ?}
                                    ,__il_poz,_ii,,,$ZDP_POZ.ref(),__il_poz*__withj2)
                                 ?};
                                 {? __il_poz<=_ilpoz || _ilpoz-=__il_poz || _ilpoz:=0 ?}
                              ?}
                           ?};
                           ZDP_POZ.next()
                        !}
                     ?};
                     __matwmg+=_ilpoz;
                     __ma2wmg+=_ilpoz*__withj2;
                     {? _ilpoz>0 & _l
                     || exec('add_infz','magazyn_stan',9.1,2,'Zamówienie: %1, wystawił: %2'
                         ', dla: %3'@[ZD_POZ.ZD_NAG().SYM,ZD_POZ.ZD_NAG().US().KOD,ZD_POZ.ZD_NAG().KH().SKR]
                         +{? ZD_POZ.D_POTW<>date(0,0,0)
                          || ', najbliższa data dostawy: %1'@[$ZD_POZ.D_POTW]
                          |? ZD_POZ.ZD_NAG().DTPREAL<>date(0,0,0)
                          || ', planowana realizacja: %1'@[$ZD_POZ.ZD_NAG().DTPREAL]
                          || ''
                          ?}
                         ,_ilpoz,_ii,,,$ZD_POZ.ref(),_ilpoz*__withj2)
                     ?}
                  ?};
                  {? _i=1 || ZD_NAG.r_unlock() ?}
               ?};
               ZD_POZ.next()
            !}
         ?};
         ZD_POZ.cntx_pop();
         {? __matwmg | __ma2wmg || __matwdr+=__matwmg; __ma2wdr+=__ma2wmg ?}
      ?};

::    obliczamy ilości w produkcji
      {? ~_g
      || __matzlm:=exec('obl_prod','magazyn_stan',_a,BUFMG.MG,_l,_b,_ii);
         __ma2zlm:=__matzlm*__withj2;
         {? __matzlm | __ma2zlm || __matzls+=__matzlm; __ma2zls+=__ma2zlm ?}
      ?}
   ?};

      {? _l
      || MST.index('MG');
         MST.prefix(_a,BUFMG.MG);
         {? MST.first
         || {? MST.MIN>0
            || exec('add_infz','magazyn_stan',0,2,'stan minimalny dla magazynu: %1'@[BUFMG.MG().SYM],MST.MIN,1
                ,,,,MST.MIN*__withj2)
            ?};
            {? MST.MAX>0
            || exec('add_infz','magazyn_stan',0,2,'stan maksymalny dla magazynu: %1'@[BUFMG.MG().SYM],MST.MAX,2
                ,,,,MST.MAX*__withj2)
            ?}
         ?}
      ?};

      _ii<__ile & BUFMG.next()
   !};

:: przypisanie do zmiennych
   BEER.S   :=_dp[2];
   {? _f<>date(0,0,0) & BEER.S<0 || BEER.S:=0 ?};
   BEER.SD:=_dp[2]-_dp[3]-_dr[1]-_dr[2]-_stats[1]-_stats[2];
   BEER.S_N:=_stats[1];
   BEER.S_O:=_stats[2];
   {? BEER.SD>_dr[5] || BEER.SD-=_dr[5] || BEER.SD:=0 ?};
   {? _f<>date(0,0,0) & BEER.SD<0 || BEER.SD:=0 ?};

   BEER.SRT:=_dr[2]+_dr[5];
   BEER.SRR:=_dr[1];
   BEER.SRA:=_dr[1]+_dr[2];
   BEER.SSZ:=_dr[4];
   BEER.SRP:=_dr[3];
   BEER.WDR:=__matwdr;
   BEER.SP:=__matzls;
:: przypisanie do zmiennych druga jednostka ewidencyjna
   {? __withj2
   || BEER.S2   :=_dp[6];
      {? _f<>date(0,0,0) & BEER.S2<0 || BEER.S2:=0 ?};
      BEER.SD2:=_dp[6]-_dp[7]-_dr[6]-_dr[7]-_stats[3]-_stats[4];
      BEER.S_N2:=_stats[3];
      BEER.S_O2:=_stats[4];
      {? BEER.SD2>_dr[10] || BEER.SD2-=_dr[10] || BEER.SD2:=0 ?};
      {? _f<>date(0,0,0) & BEER.SD2<0 || BEER.SD2:=0 ?};

      BEER.SRT2:=_dr[7]+_dr[10];
      BEER.SRR2:=_dr[6];
      BEER.SRA2:=_dr[6]+_dr[7];
      BEER.SSZ2:=_dr[9];
      BEER.SRP2:=_dr[8];
      BEER.WDR2:=__ma2wdr;
      BEER.SP2:=__ma2zls
   ?};

:: gdy edycja dokumentow magazynowych
:: F3 dla pola D_HELP.M
:: {? HELP.POP=1 & DK.PLUS='N' & DK.RDK<>0 & DK.NDK<>'' & DK.RDK=HELP.RDK & DK.NDK=HELP.NDK
:: || BEER.SD+=HELP.IL
:: ?};
   {? ~_g & HELP.POP=1 & HELP.F3=1 & $_a=form(16+HELP.MRSQL)
   || exec('FindAndGet','#table',DK,form(16-HELP.MRSQL),,"BEER.SD+=DK.IL; BEER.SD2+=DK.IL2",0)
   ?};
   {? BEER.SD>BEER.S || BEER.SD:=BEER.S ?};
   {? BEER.SD2>BEER.S2 || BEER.SD2:=BEER.S2 ?};

:: aktualizacja pol na stanach magazynowych
   {? ~_g & _b=1 & _f=date(0,0,0) & _k=null()
   || {? BUFMG.MG().ODDZ=ST.ODDZ_KOD
      || SM.prefix(BUFMG.MG,_a);
         {? SM.first()
         ||
::       SM.Z:=_dr[3]-(_dr[1]+_dr[2]+_dr[5]);
            SM.RT:=_dr[2]+_dr[5];
            SM.RZ:=_dr[1];
            SM.Z:=_dr[4];
            SM.P:=_dr[3];
            SM.D:=__matwmg;
            SM.SP:=__matzlm;
            SM.R:=_dr[1]+_dr[2];
            {? ~HELP.F3 || SM.SD:=BEER.SD ?};
            {? __withj2
            || SM.RT2:=_dr[7]+_dr[10];
               SM.RZ2:=_dr[6];
               SM.Z2:=_dr[9];
               SM.P2:=_dr[8];
               SM.D2:=__ma2wmg;
               SM.SP2:=__ma2zlm;
               SM.R2:=_dr[6]+_dr[7];
               {? ~HELP.F3 || SM.SD2:=BEER.SD2 ?}
            ?};
            SM.J2:={? __withj2 || SM.M().J2 || SM.M().J ?};
            SM.SM2:={? __withj2 || 'T' || 'N' ?};
            SM.put()
         |? __matwmg>0 | _zam_mag[_ii]>0 | (_dr[3]-(_dr[1]+_poz_rez)>0) | _poz_rez>0 | _dr[5]>0
          | _pozrez1>0 | __matzlm>0
         ||
            M.cntx_psh();
            M.clear();
            {? M.seek(_a) & M.RODZ<>'U'
            ||
               SM.blank();
               SM.MAG:=BUFMG.MG;
               SM.M:=_a;
               SM.KTM:=SM.M().KTM;
               SM.NKTM:=SM.M().N;
               SM.M_ATR:=SM.M().M_ATR_B;
               {! _wsk:=1..10 |! ($('SM.WAR'+form(_wsk,-2,0,'99')))():=($('SM.M().WAR'+form(_wsk,-2,0,'99')))() !};
               SM.MGR:=M.MGR;
               SM.MGRP:=M.MGRP;
               SM.Z:=_dr[4];
               SM.RT:=_dr[2]+_dr[5];
               SM.RZ:=_dr[1];
               SM.P:=_dr[3];
               SM.D:=__matwmg;
               SM.R:=_dr[1]+_dr[2];
               SM.SP:=__matzlm;
               {? ~HELP.F3 || SM.SD:=BEER.SD ?};
               {? __withj2
               || SM.Z2:=_dr[9];
                  SM.RT2:=_dr[7]+_dr[10];
                  SM.RZ2:=_dr[6];
                  SM.P2:=_dr[8];
                  SM.D2:=__ma2wmg;
                  SM.SP2:=__ma2zlm;
                  SM.R2:=_dr[6]+_dr[7];
                  {? ~HELP.F3 || SM.SD2:=BEER.SD2 ?}
               ?};
               SM.J2:={? __withj2 || SM.M().J2 || SM.M().J ?};
               SM.SM2:={? __withj2 || 'T' || 'N' ?};
               SM.add(1)
            ?};
            M.cntx_pop()
         ?}
      ?}
   ?};

   HELP.WYD  :=BEER.SW;

:: wypelnianie tabeli ze stanami wg magazynow
   {? ~_g & _d=1
   ||
      {? var_pres('__smmag')>100
      ||
         __smmag.prefix();
         {? __smmag.first() || {! |? __smmag.del() !} ?};
         _ii:=0;
         {? BUFMG.first()
         || {!
            |? {? BUFMG.MG().ODDZ=ST.ODDZ_KOD
               || _ii+=1;
                  __smmag.MAG:=BUFMG.MG().SYM;
                  __smmag.STAN:={? _sd[_ii]<0 || 0 || _sd[_ii] ?};
                  __smmag.RSQL:=$BUFMG.MG;
                  __smmag.STA2:={? _sd[_ii+__ile]<0 || 0 || _sd[_ii+__ile] ?};
                  __smmag.J2:={? __withj2
                              || exec('FindAndGet','#table',M,_a,,"{? J2<>null() || J2().KOD || '' ?}",'')
                              || ''
                              ?};
                  __smmag.add()
               ?};
               _ii<__ile & BUFMG.next()
            !}
         ?};
         __smmag.first()
      ?}
   ?};

   obj_del(_sd);
   obj_del(_dp);
   obj_del(_dr);
   obj_del(_buf);
   obj_del(_bufzk)
|| __ile:=0
?};

SM.cntx_pop();
M.cntx_pop();
ND.cntx_pop();
DK.cntx_pop();
REZ.cntx_pop();
ZK_N.cntx_pop();
ZK_P.cntx_pop();
ZD_NAG.cntx_pop();
ZD_POZ.cntx_pop();
ZDP_NAG.cntx_pop();
ZDP_POZ.cntx_pop();
USERS_UP.cntx_pop();
BUFMG.cntx_pop();
ZL.cntx_pop();
ZLIM.cntx_pop();

{? _l || exec('sum_infz','magazyn_stan') ?};
VAR_DEL.delete('__wydean','__ile','__matwmg','__wmg','__prod','__il_wyd','__il_rez','__il_zam','__il_poz'
 ,'__matwdr','__matzlm','__matzls','__ma2wdr','__ma2zlm','__ma2zls','__wy2ean','__withj2');
{? (1+ZMIENNE.P8)='T' || __withj2:=(ZMIENNE.P8+1)='T' ?};
1


\minimax
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: sprawdzenie stanow minimalnym i maksymalnych
::   WE: _a - 1-przychow 0-rozchod
::       _b - magazyn
::       _c - indeks materialowy
::       _d - ilosc do kontroli - przychow
::       _e - ilosc do kontroli - rozchod
::       [_f] - uproszczony komunikat
::   WY: komunikat lub pusty string
::  OLD: \minimax/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=6 || {? type_of(_f)<>1 || _f:=0 ?} || _f:=0 ?};

_wyn:='';
_minmax:=0;

MST.index('MG');
MST.prefix(_c,_b);
{? MST.first()
||
   {? _a & MST.MAX>0
   || {? _d>MST.MAX
      || _wyn+={? ~_f
               || '\nPrzyjęcie tej ilości materiału spowoduje \nprzekroczenie stanu maksymalnego,'
                  '\nktóry wynosi: %1 %2.'@[$MST.MAX,MST.M().J().KOD]
               || '\n %1 st.max.: %2 %3.'@[MST.M().KTM,$MST.MAX,MST.M().J().KOD]
               ?};
         _minmax:=1
      ?}
   ?};
   {? ~_a & MST.MIN>0
   || {? _e<MST.MIN
      || _wyn+={? ~_f
               || '\nWydanie tej ilości materiału spowoduje \nprzekroczenie stanu minimalnego,'
                  '\nktóry wynosi: %1 %2.'@[$MST.MIN,MST.M().J().KOD]
               || '\n %1 st.min.: %2 %3.'@[MST.M().KTM,$MST.MIN,MST.M().J().KOD]
               ?};
         _minmax:=1
      ?}
   ?}
||
:: sprawdzenie ogolne
   MST.prefix(_c,null);
   {? MST.first
   ||
      {? _a & MST.MAX>0
      || {? _d>MST.MAX
         || _wyn+={? ~_f
                  || '\nPrzyjęcie tej ilości materiału spowoduje \nprzekroczenie stanu maksymalnego,'
                     '\nktóry wynosi: %1 %2.'@[$MST.MAX,MST.M().J().KOD]
                  || '\n %1 st.max.: %2 %3.'@[MST.M().KTM,$MST.MAX,MST.M().J().KOD]
                  ?};
            _minmax:=1
         ?}
      ?};
      {? ~_a & MST.MIN>0
      || {? _e<MST.MIN
         || _wyn+={? ~_f
                  || '\nWydanie tej ilości materiału spowoduje \nprzekroczenie stanu minimalnego,'
                     '\nktóry wynosi: %1 %2.'@[$MST.MIN,MST.M().J().KOD]
                  || '\n %1 st.min.: %2 %3.'@[MST.M().KTM,$MST.MIN,MST.M().J().KOD]
                  ?};
            _minmax:=1
         ?}
      ?}
   ?}
?};
_wyn


\obl_dost
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RR, MZ [2008]
:: OPIS: sprawdza czy ilosc jest na dostawie
::   WE:  _a  - magazyn
::        _b  - indeks materialowy
::       [_c] - ref konkretna dostawa
::       [_d] - name() konkretna dostawa
::       [_e] - dla danej daty, domyslnie data zerowa czyli pomija kontrole dokumentow wg daty
::   WY: zwraca najwieksza ilosc dostepna dla dostawy
::  OLD: \obl_dost/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>1 || _c:=0 ?} || _c:=0 ?};
{? _>=4 || {? type_of(_d)<>2 || _d:='' ?} || _d:='' ?};
{? _>=5 || {? type_of(_e)<>4 || _e:=date(0,0,0) ?} || _e:=date(0,0,0) ?};
_stskind:={? _>5 & type_of(_f)=2 || ~-_f || 'NOZ' ?};

_jmg:=exec('FindAndGet','#table',M,_b,,"J2<>null()",0);
_stan:=0; _stan2:=0;
{? ~_c
|| exec('sc_tymczas','magazyn_stan',_a,_b,'T',,_stskind);
   {? __sc.first()
   ||
      {!
      |? HELP.DIL2:=0;
         _sta:=exec('obl_stsc','magazyn_stan',__sc.RDK,__sc.NDK,_a,_b);
         _sta2:=HELP.DIL2;
         {? _sta>_stan
         || _stan:=_sta;
            _stan2:=_sta2
         ?};
         __sc.next()
      !}
   ?}
||
:: oblicza stan dostawy - rezerwacje dla dostawy
   _stan:=exec('obl_stsc','magazyn_stan',_c,_d,_a,_b,_e);
   _stan2:=HELP.DIL2
?};
:: korekta gdy POPRAW
HELP.DIL2:=0;
{? HELP.POP & HELP.RDK=_c & HELP.NDK=_d || _stan+=HELP.IL; _stan2+=HELP.IL2 ?};
HELP.DIL2+=_stan2;
_stan


\sc_tymczas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: tworzy tymczasowa tabele SC
::   WE:  _a  - magazyn (ref)
::        _b  - material (ref)
::       [_c] - '' wszystkie rekordy / 'T' tylko aktywne / 'N' tylko nieaktywne
::       [_d] - porzadek wg lokalizacji-1 domyslnie-0
::       [_e] - dopuszczalne rodzaje statusow
::       [_f] - 1(domyslnie)-powolywac okna 2-bez win_del 0-nie powolywac
::       [_g] - zbijanie rekordow wg kodu identyfikujacego
::       [_h] - dostawy tylko z podanym kodem identyfikacyjnym
::       [_i] - ZL.ref() - ogranicza zapisy do dostaw produktów zlecenia (parametr rozwojowy - obecnie wyłączony)
::       [_j] - string - kod kreskowy dostawy - do ograniczenia stanów magazynu tylko do wybranych dostaw
::       [_k] - akcja badania
::       [_l] - domyślnie brak wskazanie na SRDK
::  OLD: \sc_tymczas/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
material:=_b;
{? _>=3 || {? type_of(_c)<>2 || _c:='' ?} || _c:='' ?};
{? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
{? _>=5 || {? type_of(_e)<>2 || _e:='NOZ'?} || _e:='NOZ' ?};
{? _>=6 || {? type_of(_f)<>1 || _f:=1 ?} || _f:=1 ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=0 ?} || _g:=0 ?};
{? _>=8 || {? type_of(_h)<>2 || _h:='' ?} || _h:='' ?};
{? _>=10 || {? type_of(_j)<>type_of('') || _j:='' ?} || _j:='' ?};
::{? _>=9 || {? type_of(_i)<>type_of(null()) || _i:=null() ?} || _i:=null() ?};
_i:=null();
_bad:={? var_pres('_k')=type_of(0) || _k || 0 ?};
_scsql:={? var_pres('_l')=type_of('') & (+_l)=16 || _l || '' ?};

{? var_pres('__sc')<100 | _d
|| _d:=0;
   _tmg:=1+exec('FindAndGet','#table',MG,$_a,,"MG.TYP",'');
   __sc:=tab_tmp(3,
      'D','DATE','Data'@,
      'DA','DATE','Data akcept.'@,
      'TA','TIME','Czas akcept.'@,
      'S','REAL','Stan'@,
      'CP','REAL','Cena przed korektą'@,
      'C','REAL','Cena'@,
      'SCC','REAL','Cena w walucie'@,
      'WAL','STRING[4]','Waluta'@,
      'WAR01','STRING[25]','Wartość 01'@,
      'WAR02','STRING[25]','Wartość 02'@,
      'WAR03','STRING[25]','Wartość 03'@,
      'WAR04','STRING[25]','Wartość 04'@,
      'WAR05','STRING[25]','Wartość 05'@,
      'WAR06','STRING[25]','Wartość 06'@,
      'WAR07','STRING[25]','Wartość 07'@,
      'WAR08','STRING[25]','Wartość 08'@,
      'WAR09','STRING[25]','Wartość 09'@,
      'WAR10','STRING[25]','Wartość 10'@,
      'RDK','REAL','rdk',
      'NDK','STRING[16]','ndk',
      'SRDK','STRING[16]','srdk',
      'PRDK','STRING[16]','prdk',
      'REF','STRING[16]','ref sql',
      'ZD_POZ','STRING[16]','$ZD_POZ.ref()',
      'DK_C','STRING[16]','$DK_C.ref()',
      'ICON','STRING[16]','Ikona'@,
      'JM','STRING[10]','jm'@,
      'DOKL','REAL','Dokładność'@,
      'DOKL_C','REAL','Dokładność ceny'@,
      'STATS','STRING[16]','$STATS.ref()',
      'MAT','STRING[16]','$M.ref()',
      'MAG','STRING[16]','$MG.ref()',
      'SCEAN','STRING[128]','Identyfikator dostawy'@,
      'TW','DATE','Termin ważności'@,
      'CN','REAL','Cena netto'@,
      'CB','REAL','Cena brutto'@,
      'RAB','REAL','Rabat'@,
      'S2','REAL','Stan w 2 jm'@,
      'ATEST','STRING[1]','Jest atest'@,
      'DK_AT','STRING[16]','Atest'@
   );

   __sc.fld_fml('S','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   __sc.fld_fml('S','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   DISP.fld_fml('SD','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   DISP.fld_fml('SD','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   DISP.fld_fml('ILWW','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   DISP.fld_fml('ILWW','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   DISP.fld_fml('ILR','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   DISP.fld_fml('ILR','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   DISP.fld_fml('ILD','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   DISP.fld_fml('ILD','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   EANX.fld_fml('ILW','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' & var_pres('__sc')>0 || $__sc.DOKL || $ST.DOKL ?}");
   EANX.fld_fml('ILW','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' & var_pres('__sc')>0 || $__sc.DOKL || $ST.DOKL ?}");
   __sc.fld_fml('C','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL_C || $ST.DOKL_C ?}");
   __sc.fld_fml('C','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL_C || $ST.DOKL_C ?}");
   __sc.fld_fml('SCC','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL_C || $ST.DOKL_C ?}");
   __sc.fld_fml('SCC','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL_C || $ST.DOKL_C ?}");
   exec('war_hid','ceny',__sc,'CP','C','SCC');

:: podsumowanie stanow dla dostaw
   __sumsc:=tab_tmp(1,'KTM','STRING[50]','Indeks'@
             ,'ST','REAL','Stan'@
             ,'SD','REAL','Stan dost.'@
             ,'SW','REAL','W wydaniu'@
             ,'SR','REAL','Zarezer.'@
             ,'RD','REAL','Zar.na dost.'@
             ,'KW','REAL','Kompl.wysyłki'@
             ,'JM','STRING[10]','jm'@
             ,'DOKL','REAL','dokladnosc'@);

   __sumsc.fld_fml('ST','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('ST','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('SD','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('SD','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('SW','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('SW','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('SR','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('SR','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('RD','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('RD','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('KW','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");
   __sumsc.fld_fml('KW','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $__sumsc.DOKL || $ST.DOKL ?}");

   {? _f || exec('sc_tymczas_okna','magazyn_stan',_tmg,_f=2) ?}
|| {? do_state()=0 || __sc.erase() || {? __sc.first() || {! |? __sc.del() !} ?} ?};
   _tmg:=1+exec('FindAndGet','#table',MG,$_a,,"MG.TYP",'');
   {? _f || exec('sc_tymczas_okna','magazyn_stan',_tmg,_f=2) ?}
?};

:: odswiezenie okienek dla atrybutow materialow
{? SC.M().M_ATR<>null()
|| _tmg:=1+exec('FindAndGet','#table',MG,$_a,,"MG.TYP",'');
   {? _f || exec('sc_tymczas_okna','magazyn_stan',_tmg,_f=2) ?}
?};

{? _f
|| {? SC.MAG().SKL='T'
   || __sc.win_sel(__sc_sew);
      __sc.win_sel(__sc_grp)
   || __sc.win_sel(__sc_sel);
      __sc.win_sel(__sc_grp)
   ?}
?};

_wgident:=_h<>'';
_wgiddst:=_scsql<>'';
_idmob:=tab_tmp(1,'SCEAN','STRING[128]','','REF','STRING[16]','');

SC.cntx_psh();
DK.cntx_psh();
DK_C.cntx_psh();
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |?
      SC.use('stc__'+ST.ODDZ+($OKR.ROK+2));
      {? _a<>null()
      || SC.index('SB');
         {? _c<>''
         || SC.prefix(_a,_b,_c)
         || SC.prefix(_a,_b)
         ?}
      || SC.index('MA');
         {? _c<>''
         || SC.prefix(_b,_c)
         || SC.prefix(_b)
         ?}
      ?};
      {? {? _wgiddst || SC.find_tab('first','SRDK',,'=',_scsql) | SC.find_tab('first','PRDK',,'=',_scsql)
         |? _wgident || SC.find_tab('first','SCEAN',,'=',_h)
         || SC.first()
         ?}
      || DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
         {!
         |?
            _dk_scean:=exec('FindAndGet','#table',DK,SC.SRDK,,"DK.SCEAN",'')=_j | _j='';
::            _dk_scean:=SC.SRDK=_j | _j='';
            {? {? SC.STATS().KIND<>'' || _e*SC.STATS().KIND || 1 ?} & _dk_scean
            || {? _i=null()
               || _go:=1
               || _go:=(exec('FindAndGet','#table',DK,SC.SRDK,,"ZL",null())=_i)
               ?};
               {? _go
               || _add:=1;
                  _scean:={? SC.SCEAN<>'' || SC.SCEAN || 'xxxx' ?};
                  {? _g
                  || _idmob.clear();
                     {? _idmob.find_key(_scean,) & (__sc.clear(); __sc.seek(_idmob.REF))
                     || __sc.S+=SC.S;
                        {? __sc.put(1) || _add:=0 ?}
                     ?}
                  ?};
                  {? _add
                  || __sc.blank();
                     __sc.D:=SC.D;
                     __sc.S:=SC.S;
                     __sc.CP:=SC.C;
                     __sc.C:=exec('cena_mag','ceny',SC.M,SC.RDK,SC.NDK,SC.MAG,SC.C,3);
                     __sc.SCC:=SC.SCC;
                     __sc.WAL:=SC.SCWAL().KOD;
                     __sc.RDK:=SC.RDK;
                     __sc.NDK:=SC.NDK;
                     __sc.SRDK:=SC.SRDK;
                     _nd:=exec('FindAndGet','#table',DK,SC.SRDK,,"$N",'');
                     _old:={? _nd<>'' || exec('FindAndGet','#table',ND,_nd,,"NDSQL<>'' | TYP().DN()='T'",0) || 1 ?};
                     __sc.PRDK:={? _old || SC.PRDK || SC.SRDK ?};
                     __sc.REF:=$SC.ref();
                     __sc.ZD_POZ:=exec('dk_dla_zdp','magazyn_stan',SC.RDK,SC.NDK);
                     __sc.DK_C:=$SC.DK_C;
                     __sc.DA:=SC.DA;
                     __sc.TA:=SC.TA;
                     __sc.ICON:=SC.STATS().ICON;
                     __sc.JM:=SC.M().J().KOD;
                     __sc.DOKL:=SC.M().DOKL;
                     __sc.DOKL_C:=SC.M().DOKL_C;
                     __sc.STATS:=$SC.STATS;
                     __sc.MAT:=$SC.M;
                     __sc.MAG:=$SC.MAG;
                     __sc.SCEAN:=SC.SCEAN;
                     __sc.TW:=SC.TW;
                     __sc.S2:=SC.S2;
                     __sc.DK_AT:=exec('FindAndGet','#table',DK,SC.PRDK,,"$DK_AT",'');
                     __sc.ATEST:={? __sc.DK_AT<>'' || 'T' || 'N' ?};
                     {? SC.MAG().COS='T'
                     || exec('FindAndGet','#table',DK,SC.SRDK,,"__sc.CN:=CN; __sc.CB:=CB; __sc.RAB:=RAB")
                     ?};
                     {? SC.DK_C().M_ATR<>null()
                     ||
                        {! _ii:=1..10
                        |!
                           ($('__sc.WAR'+form(_ii,-2,,'99')))():=($('DK_C.WAR'+form(_ii,-2,,'99')))()
                        !}
                     ?};
                     {? __sc.add() & _g
                     || _idmob.clear();
                        _idmob.blank();
                        _idmob.SCEAN:=_scean;
                        _idmob.REF:=$__sc.ref();
                        _idmob.add(1)
                     ?}
                  ?}
               ?}
            ?};
            {? _wgiddst || SC.find_tab('next','SRDK',,'=',_scsql) | SC.find_tab('next','PRDK',,'=',_scsql)
            |? _wgident || SC.find_tab('next','SCEAN',,'=',_h)
            || SC.next()
            ?}
         !}
      ?};
      OKR.next()
   !}
?};

OKR.cntx_pop();
DK.cntx_pop();
DK_C.cntx_pop();
SC.cntx_pop();

obj_del(_idmob);
''


\sc_tymczas_okna
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [12.10]
:: OPIS: tworzy okna tymczasowej tabeli SC
::   WE: [_a] - typ magazynu _tmg
::       [_b] - 1-bez win_del 0(domyslnie) z
::  OLD: \sc_tymczas_okna/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:=1+ST.MAG().TYP ?} || _a:=1+ST.MAG().TYP ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};

_grp:={? var_pres('__sc_grp')>0 || __sc_grp || '' ?};
_sel:={? var_pres('__sc_sel')>0 || __sc_sel || '' ?};
_sum:={? var_pres('__sc_sum')>0 || __sc_sum || '' ?};
_sew:={? var_pres('__sc_sew')>0 || __sc_sew || '' ?};

{? ~_b
|| __sc.win_sel();
   __sumsc.win_sel();
   {? _grp<>'' || __sc.win_del(_grp) ?};
   {? _sel<>'' || __sc.win_del(_sel) ?};
   {? _sum<>'' || __sumsc.win_del(_sum) ?};
   {? _sew<>'' || __sc.win_del(_sew) ?}
?};
VAR_DEL.delete('__sc_sel','__sc_grp','__sc_sum');
:: okienko sumy stanow
__sc_sum:=__sumsc.mk_sel('Razem'@,,0,'podsumsc');
__sumsc.win_fld(__sc_sum,,'KTM',,,30);
__sumsc.win_fld(__sc_sum,,'ST',,,15,3,,'Stan'@);
__sumsc.win_fld(__sc_sum,,'SD',,,15,3,,'Stan dostępny'@);
__sumsc.win_fld(__sc_sum,,'SW',,,15,3,,'W wydaniu'@);
__sumsc.win_fld(__sc_sum,,'SR',,,15,3,,'Zarezerwowano'@);
__sumsc.win_fld(__sc_sum,,'RD',,,-15,3,,'Zarezerwowano na dostawie'@);
__sumsc.win_fld(__sc_sum,,'KW',,,-15,3,,'Kompletacja wysyłki'@);
__sumsc.win_fld(__sc_sum,,'JM',,,5,0,,'jm'@);

__sc_sel:=__sc.mk_sel('Stany wg cen'@,,0,'msyafagsfakrdso',,,,,'U','T');
__sc.win_fld(__sc_sel,,'D',,,10);
{? SC.fld_read('C') || __sc.win_fld(__sc_sel,,'C',,,15,2,,'Cena'@) ?};
__sc.win_fld(__sc_sel,,'S',,,-15,3);
__sc.win_fld(__sc_sel,DISP,'SD',,,-15,3,,'Stan dostępny'@);
__sc.win_fld(__sc_sel,DISP,'ILWW' ,,,-15,3,,'W wydaniu'@);
{? _a='D'
|| __sc.win_fld(__sc_sel,DISP,'ILD',,,-15,3,,'Zarezerwowano na dostawie'@);
   __sc.win_fld(__sc_sel,EANX,'ILW',,,-15,3,,'Kompletacja wysyłki'@);
   ~~
?};
__sc.win_fld(__sc_sel,,'JM',,,5,0,,'jm'@);
__sc.win_fld(__sc_sel,DISP,'DOST',,,10,,,'Dostawa'@);

:: dodanie kolumn zwiazanych z atrybutami
{? SC.M().M_ATR<>null()
||
   SC.M().M_ATR().SYM;
   {! _i:=1..10
   |!
      {? ($('M_ATR.SL_'+form(_i,-2,,'99')))()<>null()
      || ($('__sc.win_fld(_a,,''WAR'+form(_i,-2,,'99')+''',,,-3,,,
            '''+($('M_ATR.SL_'+form(_i,-2,,'99')+'().NA'))()+''',,,,1)'))(__sc_sel)
      ?}
   !}
|| __sc.win_fld(__sc_sel,DISP,'CECHA',,,8,,,'Cecha'@)
?};
__sc.win_fld(__sc_sel,,'ATEST',,,5,,,'Atest'@,,'Czy posiada atest [T/N]?'@,2,,"'T'","'N'");

__sc.win_act(__sc_sel,,'Formuła','&Wybierz'@@,,'Wybór bieżącego zapisu'@,,"exec('ten_sc','magazyn_stan')",1,,,,'W');
__sc.win_act(__sc_sel,,'Formuła','Opis &cechy'@@,,,"exec('dk_cechy_sc','magazyn_stan')",,,,,,'C');
__sc.win_act(__sc_sel,,'Formuła','&Atest'@@,,,"exec('atest_sc','magazyn_stan')",,,,,,'A');
__sc.win_act(__sc_sel,,'Formuła','&Badania'@@,,,"exec('badania_sc','magazyn_stan')",,,,,,'B');
__sc.win_act(__sc_sel,,'Kolejność');
__sc.win_act(__sc_sel,,'Formuła','&Legenda'@@,,'Opis znaczenia kolorów wierszy i ikon'@,
   "exec('legenda','color','SC#01')",,,,,,'L');
__sc.win_act(__sc_sel,,'Rekord',,,,"exec('sc_tmp_info','magazyn_stan',material)");
__sc.win_fml(__sc_sel,,'S' ,,'ICON_BEFORE',"{? __sc.ICON<>'' || __sc.ICON || '' ?}",2);

:: okienko stanow walutowych
VAR_DEL.delete('__sc_sew');
__sc_sew:=__sc.mk_sel('Stany wg cen'@,,0,'msyafaglasrefrt',,,,,'U','T');
__sc.win_fld(__sc_sew,,'D',,,10);
__sc.win_fld(__sc_sew,,'S',,,12,3);
{? SC.fld_read('C') || __sc.win_fld(__sc_sew,,'C',,,12,2,,'Cena'@) ?};
__sc.win_fld(__sc_sew,,'SCC',,,-12,4,,'Cena walutowa'@);
__sc.win_fld(__sc_sew,,'WAL',,,6,,,'Waluta'@);
__sc.win_fld(__sc_sew,DISP,'SD',,,-12,3,,'Stan dostępny'@);
__sc.win_fld(__sc_sew,DISP,'ILWW' ,,,-12,3,,'W wydaniu'@);
{? _a='D'
|| __sc.win_fld(__sc_sew,DISP,'ILD',,,-12,3,,'Zarezerwowano na dostawie'@);
   __sc.win_fld(__sc_sew,EANX,'ILW',,,-12,3,,'Kompletacja wysyłki'@);
   ~~
?};
__sc.win_fld(__sc_sew,,'JM',,,5,0,,'jm'@);
__sc.win_fld(__sc_sew,DISP,'DOST',,,10,,,'Dostawa'@);
__sc.win_fld(__sc_sew,DISP,'SC_S',,,10,2,,'Suma kosztów'@);

:: dodanie kolumn zwiazanych z atrybutami
{? SC.M().M_ATR<>null()
||
   SC.M().M_ATR().SYM;
   {! _i:=1..10
   |!
      {? ($('M_ATR.SL_'+form(_i,-2,,'99')))()<>null()
      || ($('__sc.win_fld(_a,,''WAR'+form(_i,-2,,'99')+''',,,-3,,,
            '''+($('M_ATR.SL_'+form(_i,-2,,'99')+'().NA'))()+''',,,,1)'))(__sc_sew)
      ?}
   !}
|| __sc.win_fld(__sc_sew,DISP,'CECHA',,,8,,,'Cecha'@)
?};
__sc.win_fld(__sc_sew,,'ATEST',,,5,,,'Atest'@,,'Czy posiada atest [T/N]?'@,2,,"'T'","'N'");

__sc.win_act(__sc_sew,,'Formuła','Wybierz'@@,,'Wybór bieżącego zapisu'@,,"sel_exit()",1);
__sc.win_act(__sc_sew,,'Formuła','Opis &cechy'@@,,,"exec('dk_cechy_sc','magazyn_stan')");
__sc.win_act(__sc_sew,,'Formuła','&Atest'@@,,,"exec('atest_sc','magazyn_stan')");
__sc.win_act(__sc_sew,,'Kolejność');
{? ATR.MJS<>'REZ' & var_pres('__zkpdkc')>0 & __zkpdkc<>null()
|| __sc.win_act(__sc_sew,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy i ikon'@,
    "exec('legenda','color','SC#01#')")
|| __sc.win_act(__sc_sew,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy i ikon'@,
    "exec('legenda','color','SC#01')")
?};
__sc.win_act(__sc_sew,,'Rekord',,,,"exec('sc_tmp_info','magazyn_stan')");
__sc.win_fml(__sc_sew,,'S' ,,'ICON_BEFORE',"{? cur_tab().ICON<>'' || cur_tab().ICON || '' ?}",2);

__sc_grp:=__sc.grp_make('Stan dostaw'@,,'standostawgrp',,,,,'normal');
__sc.grp_sel(__sc_grp,__sc,{? ST.MAG().SKL='T' || __sc_sew || __sc_sel ?}
      ,,"exec('odsw_sum','magazyn_stan')",,,,,,,,'maximized');
__sc.grp_splt(__sc_grp,,'horizontal','tab1','28,65%');
__sc.grp_sel(__sc_grp,__sumsc,__sc_sum,,"",,,1,"",,,,'maximized_with_title');
''


\dk_cechy_sc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: wyswietlanie cech dla stanow magazynowych (__sc)
::  OLD: \dk_cechy/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? __sc.DK_C<>''
||
   DK_C.prefix();
   {? DK_C.seek(__sc.DK_C)
   || SC.DK_C().NAZ;
      DK_C.memo_get(,'O');
      DK_C.memo_view(,'O')
   ?}
|| FUN.info('Brak cechy dla dostawy.'@)
?};
''


\sc_tmp_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: uzupelnianie pola z info. dla tymczasowej tabeli tworzonej na podstawie SC
::  OLD: \sc_tmp_info/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
_refmat:=exec('FindAndGet','#table',M,__sc.MAT,,,null());
_refmag:=exec('FindAndGet','#table',MG,__sc.MAG,,,null());
_niedst:=(1+exec('FindAndGet','#table','MG',__sc.MAG,,"MG.TYP",''))<>'D';

{? _>=1 || {? type_of(_a)<>7 || _mat:=_refmat || _mat:=_a ?} || _mat:=_refmat ?};

ND.cntx_psh();
DK.cntx_psh();

DISP.DOST:='';
DISP.SC_P:=DISP.SC_R:=DISP.SC_S:=0;

{? __sc.RDK<>0
||
   ND.cntx_psh();
   DK.cntx_psh();
   ND.use('nagdo'+(__sc.NDK+3));
   DK.use(__sc.NDK);
   DK.index('DOST3');
   DK.prefix(_refmag,_mat,__sc.RDK,__sc.NDK,'T');
   {? DK.first()
   ||
      DISP.DOST:=DK.N().KH().KOD+' '+DK.N().TYP().T;
      DISP.DOST+=' - '+$DK.N().NR+' poz.'+$DK.P;
      {? DK.ZL<>null() || DISP.DOST+='; '+'zlecenie: %1'@[DK.ZL().SYM] ?};

      {!
      |?
         DISP.SC_P+=exec('koszt','magdok_poz');
         DK.next()
      !}
   ?};
   DK.cntx_pop();
   ND.cntx_pop();

   DISP.ILWW:=exec('obl_ssc','magazyn_stan',__sc.RDK,__sc.NDK,_refmag,_mat,'S');

   DISP.ILR:=exec('obl_rsc','magazyn_stan',__sc.RDK,__sc.NDK,_refmag,_mat,_niedst);

   DISP.SC_R:=exec('oblkoszr','magazyn_stan',__sc.RDK,__sc.NDK,_refmag,_mat);

   _stan:={? 0 & (2+BEER.MENU_PTH)='MR'
          || exec('obl_dost','magazyn_stan',_refmag,_mat,__sc.RDK,__sc.NDK,DK.N().D)
              +{? HELP.POP & HELP.RDK=__sc.RDK & HELP.NDK=__sc.NDK || DISP.ILWW-HELP.IL || DISP.ILWW ?}
          || __sc.S
          ?};
   _stad:=exec('FindInSet','#table','SM','SM',_mat,_refmag,"SM.SD",,,0);
   {? HELP.POP & ATR.MJS='REZ' & REZ.SC='' & var_pres('__ilprz')>0 || _stad+=__ilprz ?};
   {? DISP.REZREA>0 & _stad<_stan || {? DISP.REZREA+_stad>_stan || _stad:=_stan || _stad+=DISP.REZREA ?} ?};
   _akt_rez:=0;
   {? HELP.POP & ATR.MJS='REZ'
   || _refrez:=REZ.ref();
      REZ.cntx_psh();
      REZ.clear();
      {? REZ.seek(_refrez) & {? DISP.ILR || REZ.SC<>'' || REZ.SC='' ?} || _akt_rez:=REZ.ILR ?};
      REZ.cntx_pop()
   ?};
   _czydst:=HELP.POP & HELP.RDK=__sc.RDK & HELP.NDK=__sc.NDK;
   DISP.SD:=_stan-DISP.ILR-DISP.ILWW+{? _czydst || HELP.IL || 0 ?};
   {? _akt_rez>0
   || {? (DISP.SD+_akt_rez)<=__sc.S
      || DISP.SD+=_akt_rez
      || DISP.SD:=__sc.S
      ?}
   ?};

   {? DISP.SD<0
   || DISP.SD:=0
   ?};
   {? DISP.SD>(_stad+_akt_rez) & ~_czydst || DISP.SD:=(_stad+_akt_rez) ?};
   DISP.SC_S:=DISP.SC_P+DISP.SC_R;
   DISP.CECHA:='';
   DK_C.prefix();
   {? DK_C.seek(__sc.DK_C) || DISP.CECHA:=DK_C.SYM ?}
?};

DK.cntx_pop();
ND.cntx_pop();
{? __sc.DK_C<>'' & ATR.MJS='REZ' & var_pres('__zkpdkc')>0 & __zkpdkc<>null
  & ~exec('czyzawch','mat_atr',__zkpdkc,exec('FindAndGet','#table','DK_C',__sc.DK_C,,,null()))
|| Color.fnd_kol('SC#01x#03')
|? __sc.CP<>__sc.C
|| Color.fnd_kol('SC#01#01')
|| DISP.SD>0;
   Color.fnd_kol('SC#01#02')
?}


\obl_ssc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: stan danej dostawy - w zaleznosci od parametru uwzglednia niezaakceptowane
::       (jezeli _a = 0 _b = '' to dziala dla wszystkich dokumentow magazynowych dla podanej _c i _d)
::   WE: [_a] - ref dostawy DK
::       [_b] - name dostawy DK
::       [_c] - ref magazynu
::       [_d] - ref materialu
::       [_e] - (N\T) - zwraca stan dostawy (N-nie uwzglednia ilosci w wydaniu\T-stan pomniejszony o ilosc w wydaniu)
::              (S) zwraca tylko ilosc w wydaniu
::       [_f] - dla danej daty, domyslnie data zerowa czyli pomija kontrole dokumentow wg daty
::       [_g] - kod identyfikacyjny (domyślnie brak), jeśli podano to sprawdzenie wg niego a nie _a,_b
::   WY: zwraca obliczona ilosc, dodatkowo:
::       - uzupelnia zmienna __ward - obliczona wartosc dokumentow
::       - uzupelnia zmienna __wpod - ilosc w wydaniu po dacie jesli parametr _f<>date(0,0,0)
::  OLD: \obl_ssc/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=SC.RDK ?}  || _a:=SC.RDK  ?};
{? _>=2 || {? type_of(_b)<>2 || _b:=SC.NDK ?} || _b:=SC.NDK ?};
{? _>=3 || {? type_of(_c)<>7 || _c:=SC.MAG ?} || _c:=SC.MAG ?};
{? _>=4 || {? type_of(_d)<>7 || _d:=SC.M ?} || _d:=SC.M ?};
{? _>=5 || {? type_of(_e)<>2 || _e:='N' ?} || _e:='N' ?};
{? _>=6 || {? type_of(_f)<>4 || _f:=date(0,0,0) ?} || _f:=date(0,0,0) ?};
{? _>=7 || {? type_of(_g)<>2 || _g:='' ?} || _g:='' ?};

VAR_DEL.delete('__ward','__wpod');
__ward:=__wpod:=0;

_rep:=_a;
_namp:=_b;
_mag:=_c;
_mat:=_d;
_sc:=exec('MaskInt2RefSql','#convert',_namp,_rep);
_scean:=_g;

_ild:=0;
HELP.DIL2:=0;

ND.cntx_psh();
DK.cntx_psh();
OKR.cntx_psh();

OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |?
      ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));
      DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
      {? _scean=''
      || DK.index('DOST3');
         {? _a=0 & _b=''
         || DK.prefix(_mag,_mat)
         || DK.prefix(_mag,_mat,_rep,_namp)
         ?}
      || DK.index('SCEAN');
         DK.prefix(_scean,_scean)
      ?};
      {? DK.first()
      || {!
         |? {? _scean='' | (DK.N().MAG=_mag & DK.M=_mat)
            ||
               {? {? _f=date(0,0,0) | (DK.REF_KOR<>'') | (DK.Z='T' & DK.PLUS='N') || 1 || DK.N().D<=_f ?}
               || _czyakc:=DK.Z='T';
                  {? _czyakc & _e<>'S'
                  || {? DK.PLUS='T'
                     || _ild+=DK.IL;HELP.DIL2+=DK.IL2;__ward+=DK.WAR
                     || _ild-=DK.IL;HELP.DIL2-=DK.IL2;__ward-=DK.WAR
                     ?}
                  |? ~_czyakc & _e='T'
                  || {? DK.PLUS<>'T' || _ild-=DK.IL;HELP.DIL2-=DK.IL2;__ward-=DK.WAR ?}
                  |? ~_czyakc & _e='S'
                  || {? DK.PLUS<>'T' || _ild+=DK.IL;HELP.DIL2-=DK.IL2;__ward+=DK.WAR ?}
                  ?}
               |? DK.N().D>_f & DK.PLUS='N'
               ||
::                gdy stan na dzien - wszystkie wydania po dacie
                  __wpod+=DK.IL
               ?}
            ?};
            DK.next()
         !}
      ?};
      OKR.next()
   !}
?};

OKR.cntx_pop();
ND.cntx_pop();
DK.cntx_pop();

:: ilosci w wydaniu wynikajace z niezaakceptowanych faktur
REZ.cntx_psh();
REZ.index('SC');
REZ.prefix(_mat,_sc,_sc,'F','B');
{? REZ.first() & 'TS'*_e
|| {!
   |? _ild+={? _e='T' || -1 || 1 ?}*REZ.ILR;
      HELP.DIL2+={? _e='T' || -1 || 1 ?}*REZ.IL2;
      REZ.next()
   !}
?};
REZ.cntx_pop();
:: ilości w wydaniu wynikające z wyposażenia
REZ.cntx_psh();
REZ.index('SC');
REZ.prefix(_mat,_sc,_sc,'W','B');
{? REZ.first() & 'TS'*_e
|| {!
   |? {? form(8+REZ.REFSQL)='karo'
      || _ild+={? _e='T' || -1 || 1 ?}*REZ.ILR;
         HELP.DIL2+={? _e='T' || -1 || 1 ?}*REZ.IL2
      ?};
      REZ.next()
   !}
?};
REZ.cntx_pop();
:: ilosci rezerwowane na dostawach z daną identyfikacją
{? _scean<>''
|| REZ.cntx_psh();
   REZ.index('MG_M');
   REZ.prefix(_mag,_mat,'B');
   {? REZ.first()
   || {!
      |? {? ~((';WF'*REZ.TYP)>1) & REZ.SC<>'' & exec('FindAndGet','#table',DK,REZ.SC,,"SCEAN",'')=_scean
         || _ild+={? _e='T' || -1 || 1 ?}*REZ.ILR;
            HELP.DIL2+={? _e='T' || -1 || 1 ?}*REZ.IL2
         ?};
         REZ.next()
      !}
   ?};
   REZ.cntx_pop()
?};
_ild


\obl_rsc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: rezerwacje danej dostawy
::   WE:  _a  - ref dostawy DK
::        _b  - name dostawy DK
::        _c  - ref magazynu
::        _d  - ref materialu
::       [_e] - czy obliczac rezerwacje z faktur 0-nie(domyslnie) 1-tak
::       [_f] - ref SQL ZK_N do pominiecia
::   WY: ilosc zarezerwowana
::  OLD: \obl_rsc/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};

{? _=0
|| _rep:=SC.RDK;  _namp:=SC.NDK;
   _mag:=SC.MAG;  _mat:=SC.M
|| _rep:=_a;      _namp:=_b;
   _mag:=_c;      _mat:=_d
?};
_ilr:=0; _ild:=0;
_ilr2:=0; _ild2:=0;
_jmg:=exec('FindAndGet','#table',M,_mat,,"J2<>null()",0);

HELP.DIL2:=0;
_scsql:=exec('MaskInt2RefSql','#convert',_namp,_rep);
_scean:={? _scsql<>'' || exec('FindInSet','#table','SC','SRDK',_scsql,_scsql,"SC.SCEAN",,,'') || '' ?};
REZ.cntx_psh();
REZ.index('SC');
REZ.prefix(_mat,_scsql,_scsql);
{? REZ.first()
|| {!
   |? {? REZ.TYP<>'W'
      || {? _e & REZ.TYP='F'
         || _ilr+=REZ.ILR;
            {? _jmg || _ilr2+=REZ.IL2 ?}
         |? REZ.TYP<>'F' & (_f='' | _f<>($REZ.ZK_N))
         || _ilr+=REZ.ILR;
            _ild+=REZ.ILR;
            {? _jmg
            || _ilr2+=REZ.IL2;
               _ild2+=REZ.IL2
            ?}
         ?}
      ?};
      REZ.next()
   !}
?};
REZ.cntx_pop();
_kpl:=0;
{? _scean<>''
|| EANN.cntx_psh();
   EANP.cntx_psh();
   EANP.index('SCEAN');
   EANP.prefix(_scean,_scean,'Z');
   {? EANP.first()
   || {!
      |? {? EANP.EANN<>null & EANP.EANN().AKT='T' & EANP.EANN().STAN<>'!' & EANP.LOKZ().MG=_mag
         || _ilrr:=0;
            _ilrr2:=0;
            {? EANP.M().IDMOB='D' & EANP.RSQL<>''
            || ZK_P.cntx_psh();
               REZ.cntx_psh();
               _zkp:=exec('FindAndGet','#table',ZK_P,EANP.RSQL,,,null());
               REZ.index('ZK_P');
               REZ.prefix(_zkp,'B');
               {? REZ.first()
               || {!
                  |? {? REZ.SC=_scsql || _ilrr+=REZ.ILR; {? _jmg || _ilrr2+=REZ.IL2 ?} ?};
                     REZ.next()
                  !}
               ?};
               ZK_P.cntx_pop();
               REZ.cntx_pop()
            ?};
            _ile:={? EANP.EANN().STAN<>'Z' || EANP.IL || EANP.ILS ?};
            _kpl+={? _ile>=_ilrr || _ile-_ilrr || 0 ?}
         ?};
         EANP.next()
      !}
   ?};
   EANN.cntx_pop();
   EANP.cntx_pop()
?};
DISP.ILD:=_ild;
EANX.ILW:=_kpl;
HELP.DIL2:=_ilr2;
_ilr


\oblkoszr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: oblicza koszt pozycji rozchodowych dostawy
::   WE:  _a  - ref dostawy DK
::        _b  - name dostawy DK
::        _c  - ref magazynu
::        _d  - ref materialu
::   WY:  ilosc w wydaniu
::  OLD: \oblkoszr/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0
|| _rep:=SC.RDK;  _namp:=SC.NDK;
   _mag:=SC.MAG;  _mat:=SC.M
|| _rep:=_a;      _namp:=_b;
   _mag:=_c;      _mat:=_d
?};
_koszt_r:=0;
ND.cntx_psh();
DK.cntx_psh();
DK.clear();
DK.index('DOST3');
DK.prefix(_mag,_mat,_rep,_namp,'N');
{? DK.first()
|| {!
   |? _koszt_r+=exec('koszt','magdok_poz');
      DK.next()
   !}
?};
DK.cntx_pop();
ND.cntx_pop();
_koszt_r


\obl_stsc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: oblicza stan na danej dostawie (ilosc dostepna pomniejszona o rezerwacje)
::   WE: [_a]  - ref dostawy DK
::       [_b]  - name dostawy DK
::       [_c]  - ref magazynu
::       [_d]  - ref materialu
::       [_e] - dla danej daty, domyslnie data zerowa czyli pomija kontrole dokumentow wg daty
::       [_f] - ref SQL ZK_N do pominiecia
::       [_g] - 1-odejmuje ilosci realizowane z dostawa 0(domyslnie)-nie
::       [_h] - ilość rezerwowana do pominięcia, 0(domyślnie)
::   WY: ilosc
::  OLD: \obl_stsc/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=5 || {? type_of(_e)<>4 || _e:=date(0,0,0) ?} || _e:=date(0,0,0) ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=0 ?} || _g:=0 ?};
{? _>=8 || {? type_of(_h)<>1 || _h:=0 ?} || _h:=0 ?};

{? _=0
|| _rep:=SC.RDK;  _namp:=SC.NDK;
   _mag:=SC.MAG;  _mat:=SC.M;
   _dat:=date(0,0,0)
|| _rep:=_a;      _namp:=_b;
   _mag:=_c;      _mat:=_d;
   _dat:=_e
?};

:: stan dostawy
HELP.DIL2:=0;
_ild:=exec('obl_ssc','magazyn_stan',_rep,_namp,_mag,_mat,'T',_dat);
_ild2:=HELP.DIL2;

:: rezerwacje danej dostawy
HELP.DIL2:=0;
_ilr:=exec('obl_rsc','magazyn_stan',_rep,_namp,_mag,_mat,,_f);
_ilr2:=HELP.DIL2;

SM.cntx_psh();
SM.index('SM');
SM.prefix(_mag);
{? SM.find_key(_mat)
|| _stan:=SM.S-SM.W;
   _stan2:=SM.S2-SM.W2;
   _reze:=SM.R-_h;
   _reze2:=SM.R2;
   {? _stan<=_ilr & _reze>0 & _reze>_ilr || _ilr:=_reze ?};
   {? _stan2<=_ilr2 & _reze2>0 & _reze2>_ilr2 || _ilr2:=_reze2 ?}
?};
SM.cntx_pop();
{? _g || _ilr+=exec('obl_zkrp','magazyn_stan',exec('MaskInt2RefSql','#convert',_namp,_rep)) ?};

HELP.DIL2:={? _ild2-_ilr2<0 || 0 || _ild2-_ilr2 ?};
{? _ild-_ilr<0 || 0 || _ild-_ilr ?}


\obl_zkrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: Oblicza ilosci w realizacji danej dostawy
::   WE: _a - ref SQL dostawy
::   WY: ilosc
::  OLD: \obl_zkrp/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
ZK_RN.cntx_psh();
ZK_RP.cntx_psh();
{? ZK_RP.find_tab('first','N','ND','=','','SC',,'=',_a)
|| {!
   |? _wyn+=ZK_RP.ILR;
      ZK_RP.find_tab('next','N','ND','=','','SC',,'=',_a)
   !}
?};
ZK_RP.cntx_pop();
ZK_RN.cntx_pop();
_wyn


\ten_sc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: wybor konkretnej dostawy
::   WY: 1/0
::  OLD: \ten_sc/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? __sc.DK_C<>'' & ATR.MJS='REZ' & var_pres('__zkpdkc')>0 & __zkpdkc<>null
|| {? exec('czyzawch','mat_atr',__zkpdkc,exec('FindAndGet','#table','DK_C',__sc.DK_C,,,null()))
   || sel_exit()
   || FUN.info('Niezgodność cechy danej dostawy z cechą wprowadzoną na pozycji zamówienia.\n'
       'Wybór dostawy niemożliwy.'@);
      0
   ?}
|| sel_exit()
?}


\odsw_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: odswieża rekord podsumowania dostaw
::  OLD: \odsw_sum/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
exec('oblscsum','magazyn_stan');
grp_disp(__sc,{? SC.MAG().SKL='T' || __sc_sew || __sc_sel ?});
grp_disp(__sumsc,__sc_sum);
~~


\oblscsum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: oblicza podsumowania dostaw
::  OLD: \oblscsum/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
_nddk:=(2+BEER.MENU_PTH)='MR';
_dkln:=(2+BEER.MENU_PTH)='MO';
{? _dkln
|| _mag:=DK_L.DK_LN().MG;
   _mat:=DK_L.M;
   _ktm:=DK_L.M().KTM;
   _rez:=0
|? ATR.MJS='KARO'
|| _mag:=KARO.MG;
   _mat:=KARO.M;
   _ktm:=KARO.M().KTM;
   _rez:=0
|? ~_nddk
|| _mag:=REZ.MG;
   _mat:=REZ.M;
   _ktm:=REZ.M().KTM;
   _rez:=1
|| _mag:=exec('FindAndGet','#table',ND,$DK.N,,"MAG",null());
   {? _mag=null() || _mag:=ST.MAG ?};
   _mat:=DK.M;
   _ktm:=DK.M().KTM;
   _rez:=0
?};
SM.cntx_psh();
_rezbez:=exec('oblrwgd','magazyn_stan',_mag,_mat);
SM.index('SM');
SM.prefix(_mag,_mat);
_zwrot:=~_rez & exec('FindAndGet','#table',ND,$DK.N,,"TYP().P='T' & TYP().DN='T' & TYP().DK='N'",0);
_tmg:=1+ST.MAG().TYP;
{? var_pres('__sumsc')<100 & ('D'*_tmg | _zwrot) & ~_rez
||
   _stskind:={? _zwrot
             || DK.N().MAG().DEFSTATS().KIND
             || exec('Create_ZON4typ','statexam',DK.N().TYP();TYPYDOK)
             ?};
   exec('sc_tymczas','magazyn_stan',
      _mag,
      _mat,
      'T',,
      _stskind,
      {? DK.N().MAG().KOOP='T' || 2 || 1 ?},
      {? DK.N().TYP().WYR='T' & TYPYDOK.P='N' || DK.ZL || null() ?}
   )
?};
_akt_rez:=0;
{? HELP.POP & ATR.MJS='REZ'
|| _refrez:=REZ.ref();
   REZ.cntx_psh();
   REZ.clear();
   {? REZ.seek(_refrez) || _akt_rez:=REZ.ILR ?};
   REZ.cntx_pop()
|? HELP.POP & ATR.MJS='DK'
|| _refrez:=DK.ref();
   DK.cntx_psh();
   DK.clear();
   {? DK.seek(_refrez) || _akt_rez:=DK.IL ?};
   DK.cntx_pop()
|? HELP.POP & ATR.MJS='KARO'
|| _refrez:=KARO.ref();
   KARO.cntx_psh();
   KARO.clear();
   {? KARO.seek(_refrez) || _akt_rez:=KARO.IL ?};
   KARO.cntx_pop()
?};
_kpl:=0;
EANN.cntx_psh();
EANP.cntx_psh();
EANP.index('TYP');
EANP.prefix('Z',_mat,_mag);
{? EANP.first()
|| {!
   |? {? EANP.SCEAN<>'' & EANP.EANN<>null & EANP.EANN().AKT='T' & EANP.EANN().STAN<>'!'
      || _kpl+={? EANP.EANN().STAN<>'Z' || EANP.IL || EANP.ILS ?}
      ?};
      EANP.next()
   !}
?};
EANN.cntx_pop();
EANP.cntx_pop();

{? var_pres('__sumsc')>117
|| __sumsc.clear();
   {? __sumsc.first() || {! |? __sumsc.del() !} ?};
   __sumsc.blank();
   __sumsc.KTM:=_ktm;
   {? SM.first()
   || __sumsc.ST:=SM.S;
      __sumsc.SD:={? (SM.SD+_akt_rez)<=SM.S || (SM.SD+_akt_rez) || SM.S ?};
      __sumsc.SW:=SM.W;
      __sumsc.SR:=SM.R;
      __sumsc.RD:=SM.R-_rezbez;
      __sumsc.KW:=_kpl;
      __sumsc.JM:=SM.M().J().KOD;
      __sumsc.DOKL:=SM.M().DOKL
   ?};
   __sumsc.add(1)
?};
SM.cntx_pop()


\oblrwgd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: oblicza roznice rezerwacji i rezerwacji wg dostaw dla danego materialu na danym magazynie
::   WE: _a - ref magazynu
::       _b - ref materialu
::   WY: roznica rezerwacji
::  OLD: \oblrwgd/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_rez:=0;
_dst:=0;
REZ.cntx_psh();
REZ.index('MG_M');
REZ.prefix(_a,_b,'B');
{? REZ.first()
|| {!
   |? {? REZ.ILR>0 & REZ.TYP<>'F' & REZ.TYP<>'W'
      || _rez+=REZ.ILR;
         _dst+={? REZ.SC<>'' || REZ.ILR || 0 ?}
      ?};
      REZ.next()
   !}
?};
REZ.cntx_pop();
_wyn:=_rez-_dst;
_wyn


\dk_dla_zdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: zwraca $ZD_POZ.ref jesli dostawa przeznaczona dla pozycji zamowienia
::   WE: _a - RDK
::       _b - NDK
::   WY: '' lub $ZD_POZ.ref
::  OLD: \dk_dla_zdp/zkd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
DK.cntx_psh;
DK.use(_b);
DK.prefix;
{? DK.seek(_a,) & DK.ZAM_RP<>'' & ref_tab(DK.ZAM_RP)=ZD_RP
||
   ZD_RP.cntx_psh;
   ZD_RP.use(8+DK.ZAM_RP);
   ZD_RP.prefix;
   {? ZD_RP.seek(DK.ZAM_RP)
   ||
      _wyn:=$ZD_RP.ZD_POZ
   ?};
   ZD_RP.cntx_pop
?};
DK.cntx_pop;
_wyn


\ilwydsce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: oblicza ilosc w wydaniu dla danego identyfikatora dostaw
::   WE: _a - ref magazynu
::       _b - ref materialu
::       _c - identyfikator dostawy
::       [_d] - lokalizacja
::       [_e] - termin ważności
::       [_f] - ref DK_L-a
::       [_g] - 1-uwzgledniaj rowniez reorganizacje 0(domyslnie)-nie
::       [_h] - wymuszenie sprawdzenia terminu dla dla daty zerowej, [0] - nie, 1 -tak
::  OLD: \ilwydsce/ean2.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=4 || {? type_of(_d)<>7 || _d:=null ?} || _d:=null ?};
{? _>=5 || {? type_of(_e)<>4 || _e:=date(0,0,0) ?} || _e:=date(0,0,0) ?};
{? _>=6 || {? type_of(_f)<>7 || _f:=null ?} || _f:=null ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=0 ?} || _g:=0 ?};
{? _>=8 || {? type_of(_h)<>1 || _h:=0 ?} || _h:=0 ?};

_wyn:=0;
_bez:=_d=null & _e=date(0,0,0);
DK_L.cntx_psh();
DK_L.index('SCEAN');
DK_L.prefix('N',_a,_b,_c,_c);
{? DK_L.first()
|| {!
   |? {? (_g | $DK_L.DK<>'') & {? _f<>null || DK_L.ref()<>_f || 1 ?}
       & (_bez | ({? _d<>null || DK_L.LOK=_d || 1 ?} & {? _e<>date(0,0,0) | _h || DK_L.TW=_e || 1 ?}))
      || _wyn+=DK_L.IL
      ?};
      DK_L.next()
   !}
?};
DK_L.cntx_pop();
_wyn


\procent
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: oblicza procent zajetosci dokladnie danej lokalizacji
::   WY: zwraca tabele danych (procent,zajete,calosc)
::  OLD: \procent/mws.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=obj_new(3);
{! _i..3 |! _wyn[_i]:=0 !};
_pal:=EANL.MG().PAL='T';
_zajete:=exec('eanlzobj','magazyn_stan',EANL.MG,EANL.ref());
_calosc:={? _pal || EANL.ILPAL || EANL.OBJ ?};
_zostalo:=_calosc-_zajete;

{? _zostalo<0
|| _wyn[1]:=100;
   _wyn[2]:=_wyn[3]:=_calosc
|? ~_calosc
|| _wyn[1]:={? _zajete>0 || 100 || 0 ?};
   _wyn[2]:={? _zajete>0 || _calosc || 0 ?};
   _wyn[3]:=_calosc
|| _wyn[1]:=(1-(_zostalo/_calosc))*100 $2;
   _wyn[2]:=_zajete;
   _wyn[3]:=_calosc
?};
_wyn


\eanlzobj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: oblicza zajetość danej lokalizacji
::   WE: _a - magazyn
::       _b - lokalizacja
::       [_c] - ref DK_LN-a do odjęcia lub ref ND do odjęcia
::   WY: obliczona objetość w przypadku nie stosowania palet lub ilość palet na lokalizacji w przypadku stosowania
:: UWAGA. w przypadku gdy na danej lokalizacji towar nie ma podanej objetości wówczas przyjmujemy że zajęta jest cała
::        lokalizacja
::  OLD: \eanlzobj/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>7 || _c:=null() ?} || _c:=null() ?};

_wyn:=0;
_buf:=tab_tmp(1,'SQL','STRING[16]','');
SL.cntx_psh();
M.cntx_psh();
SL.index('EANL');
SL.prefix(_a,_b);
{? SL.first()
|| _pal:=SL.MG().PAL='T';
   {!
   |? {? SL.IL<=0
      || SL.next()
      |? _pal
      || _buf.clear();
         {? SL.PAL<>null & ~_buf.find_key($SL.PAL)
         || _buf.blank();
            _buf.SQL:=$SL.PAL;
            _buf.add(1);
            _wyn+=1
         ?};
         SL.next()
      || {? ~SL.M().OBJ
         || _wyn:=SL.EANL().OBJ;
            0
         || _wyn+=(SL.IL/({? SL.M().ILOBJ>0 || SL.M().ILOBJ || 1 ?}))*SL.M().OBJ;
            SL.next()
         ?}
      ?}
   !}
?};
SL.cntx_pop();
M.cntx_pop();
obj_del(_buf);
{? _c<>null()
|| {? ref_tab(_c)=DK_LN
   || _wyn+=exec('eanzdkln','magdok_wymiary',_c,_b)
   |? ref_tab(_c)=ND
   || _wyn+=exec('eanznd','magdok_wymiary',_c,_b)
   |? ref_tab(_c)=EANN
   || _wyn+=exec('eanzeann','magdok_wymiary',_c,_b)
   ?}
?};
_wyn


\obl_stad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: oblicza stan dostepny podanej dostawy
::   WE: _a - ref materialu
::       _b - ref SQL-owy danej dostawy
::       [_c] - pomijaj ZK_P.ref
::       [_d] - pomijaj ZK_N.ref
::       [_e] - numer pozycji
::       [_f] - 1-wynik tablicowy z ilością wg drugiej jednostki, 0(domyślnie)-wynik prosty
::  OLD: \obl_stad/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>7 || _c:=null ?} || _c:=null ?};
{? _>=4 || {? type_of(_d)<>7 || _d:=null ?} || _d:=null ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};

_withj2:={? var_pres('_f')=type_of(0) || _f || 0 ?};

{? (+_b)<>16
|| _wyn:=0;
   _wyn2:=0
|| SC.cntx_psh();
   _akt:=SC.name()+3;
   _msk:=form(8+_b)+3;
   {? _akt<>_msk || SC.use((SC.name()-3)+_msk) ?};
   _wyn:=exec('FindInSet','#table','SC','SRDK',_b,_b,"SC.S",,,0);
   _wy2:=exec('FindInSet','#table','SC','SRDK',_b,_b,"SC.S2",,,0);
   _scean:=exec('FindInSet','#table','SC','SRDK',_b,_b,"SC.SCEAN",,,'');
   _mag:=exec('FindInSet','#table','SC','SRDK',_b,_b,"SC.MAG",,,null());
   SC.cntx_pop();
   ZK_N.cntx_psh();
   ZK_P.cntx_psh();
   REZ.cntx_psh();
   REZ.index('SC');
   REZ.prefix(_a,_b,_b);
   {? REZ.first()
   || {!
      |? {? REZ.RODZ='B' & (_c=null | _c<>REZ.ZK_P) & (_d=null | _d<>REZ.ZK_N)
         || _wyn-=REZ.ILR;
            _wy2-=REZ.IL2
         ?};
         REZ.next()
      !}
   ?};
   REZ.cntx_pop();
   ZK_N.cntx_pop();
   ZK_P.cntx_pop();
:: pomniejszenie dostawy na kompletacji wysyłki
   _beerm:=BEER.M;
   BEER.M:=_a;
   {? _scean<>'' & BEER.M().IDMOB='D'
   || EANN.cntx_psh();
      EANP.cntx_psh();
      EANP.index('SCEAN');
      EANP.prefix(_scean,_scean,'Z');
      {? EANP.first()
      || {!
         |? {? EANP.EANN<>null & EANP.EANN().AKT='T' & EANP.EANN().STAN<>'!' & EANP.LOKZ().MG=_mag
            || {? EANP.EANN().STAN<>'Z'
               || _wyn-=EANP.IL;
                  _wy2-=EANP.IL*EANP.M().WS2
               || _wyn-=EANP.ILS;
                  _wy2-=EANP.ILS*EANP.M().WS2
               ?}
            ?};
            EANP.next()
         !}
      ?};
      EANN.cntx_pop();
      EANP.cntx_pop()
   ?};
   BEER.M:=_beerm
?};

{? _wyn<0 || _wyn:=0 ?};
{? ~_withj2
|| _wyn
|| _res:=obj_new(2); _res[1]:=_wyn; _res[2]:=_wy2;
   _res
?}


\zamstany
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: aktualizacja stanow na uprawnionych magazynach dla danego materialu
::   WE: _a - ref materialu
::       [_b] - 0(domyslnie)-zamowienia sprzedazy 1-zamowienia wewnetrzne
::  OLD: \zamstany/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};

_byl:=0;
USERS_UP.cntx_psh();
USERS_UP.index('MG');
USERS_UP.prefix(OPERATOR.USER,'ZA'+{? ~_b || 'M' || 'W' ?},ST.ODDZ_KOD);
{? USERS_UP.first()
||
   _byl:=1;
   {!
   |? _mag:=USERS_UP.MG;
      USERS_UP.cntx_psh();
      exec('obl_stan','magazyn_stan',_a,1,_mag);
      USERS_UP.cntx_pop();
      USERS_UP.next()
   !}
?};
{? ~_byl
||
   USERS_UP.index('MG');
   USERS_UP.prefix(null,'ZA'+{? ~_b || 'M' || 'W' ?},ST.ODDZ_KOD);
   {? USERS_UP.first()
   || {!
      |? _mag:=USERS_UP.MG;
         USERS_UP.cntx_psh();
         exec('obl_stan','magazyn_stan',_a,1,_mag);
         USERS_UP.cntx_pop();
         USERS_UP.next()
      !}
   ?}
?};
USERS_UP.cntx_pop();
1


\akt_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: aktualizuje stany magazynowe po przeprowadzonych operacjach
::   WE: _a - tabelka stanow
::       _b - ref M
::       _c - magazyn lub  null
::  OLD: \akt_stan/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
_dod:=_c<>null;
_mag:=tab_tmp(1,'MAG','STRING[16]','');
SM.cntx_psh();
{? _c=null()
|| BUFMG.index('US');
   BUFMG.prefix(OPERATOR.USER,4);
   {? BUFMG.first()
   || {!
      |? {? BUFMG.MG().ODDZ=ST.ODDZ_KOD
         || _mag.blank();
            _mag.MAG:=$BUFMG.MG;
            _mag.add(1);
            exec('obl_stan','magazyn_stan',_b,1,BUFMG.MG)
         ?};
         BUFMG.next()
      !}
   ?};
   BUFMG.index('US');
   BUFMG.prefix(OPERATOR.USER,10);
   {? BUFMG.first()
   || {!
      |? {? (_mag.clear(); ~_mag.find_key($BUFMG.MG)) & BUFMG.MG().ODDZ=ST.ODDZ_KOD
         || exec('obl_stan','magazyn_stan',_b,1,BUFMG.MG)
         ?};
         BUFMG.next()
      !}
   ?}
|| exec('obl_stan','magazyn_stan',_b,1,_c)
?};
obj_del(_mag);
SM.cntx_pop();
~~


\stnwgdkc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: oblicza stan dostaw wg podanej cechy dostawy
::   WE: _a - ref magazynu
::       _b - ref materialu
::       _c - DK_C
::       [_d] - 1-minus rezerwacje 0(domyslnie)-nie
::       [_e] - status dostawy
::       [_f] - 1-wynik tablicowy z ilością wg drugiej jednostki, 0(domyślnie)-wynik prosty
::   WY: stan
::  OLD: \stnwgdkc/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
{? _>=5 || {? type_of(_e)<>2 || _e:='' ?} || _e:='' ?};

_wgdkc:={? _c<>null()
        || ~(exec('FindAndGet','#table',DK_C,$_c,,"{? M_ATR().MOD='T' || UMOD || UZUP ?}",'')*'0')
        || 0
        ?};
_withj2:={? var_pres('_f')=type_of(0) || _f || 0 ?};

_wyn:=_wy2:=0;
{? _withj2 || _stan:=obj_new(2) ?};
SC.cntx_psh();
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
|| {!
   |? SC.use('stc__'+ST.ODDZ+(form(OKR.ROK,,,'99')+2));
      {? ~_wgdkc
      || SC.index('SN');
         SC.prefix(_a,_b);
         {? {? _e='' || SC.find_tab('first','S',,'>',0) || SC.find_tab('first','S',,'>',0,'STATS','KIND','=',_e) ?}
         || {!
            |? {? exec('czyzawch','mat_atr',_c,SC.DK_C)
               || {? ~_withj2
                  || _stan:={? _d || exec('obl_stad','magazyn_stan',SC.M,SC.SRDK) || SC.S ?};
                     _wyn+={? _stan>0 || _stan || 0 ?}
                  || {? _d
                     || obj_del(_stan); _stan:=exec('obl_stad','magazyn_stan',SC.M,SC.SRDK,,,,1)
                     || _stan[1]:=SC.S; _stan[2]:=SC.S2
                     ?};
                     _wyn+={? _stan[1]>0 || _stan[1] || 0 ?};
                     _wy2+={? _stan[2]>0 || _stan[2] || 0 ?}
                  ?}
               ?};
               {? _e='' || SC.find_tab('next','S',,'>',0) || SC.find_tab('next','S',,'>',0,'STATS','KIND','=',_e) ?}
            !}
         ?}
      || SC.index('MG_DKC');
         SC.prefix(_a,_c);
         {? {? _e=''
            || SC.find_tab('first','S',,'>',0,'M',,'=',_b)
            || SC.find_tab('first','S',,'>',0,'M',,'=',_b,'STATS','KIND','=',_e)
            ?}
         || {!
            |? {? ~_withj2
               || _stan:={? _d || exec('obl_stad','magazyn_stan',SC.M,SC.SRDK) || SC.S ?};
                  _wyn+={? _stan>0 || _stan || 0 ?}
               || {? _d
                  || obj_del(_stan); _stan:=exec('obl_stad','magazyn_stan',SC.M,SC.SRDK,,,,1)
                  || _stan[1]:=SC.S; _stan[2]:=SC.S2
                  ?};
                  _wyn+={? _stan[1]>0 || _stan[1] || 0 ?};
                  _wy2+={? _stan[2]>0 || _stan[2] || 0 ?}
               ?};
               {? _e=''
               || SC.find_tab('next','S',,'>',0,'M',,'=',_b)
               || SC.find_tab('next','S',,'>',0,'M',,'=',_b,'STATS','KIND','=',_e)
               ?}
            !}
         ?}
      ?};
      OKR.next()
   !}
?};
SC.cntx_pop();
OKR.cntx_pop();
{? _withj2 || obj_del(_stan) ?};
{? ~_withj2
|| _wyn
|| _res:=obj_new(2); _res[1]:=_wyn; _res[2]:=_wy2;
   _res
?}


\sm_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: poprawia pole SM.ST_WAR
::   WE: _a - biezaca maska dokumentow
::       _b - M.ref
::       _c - wartosc
::  OLD: \sm_upd/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
SM.cntx_psh();
SM.use('stm__'+_a);
SM.clear();
SM.index('SM');
SM.prefix(ST.MAG,_b);
{? SM.first() || SM.ST_WAR+=_c; SM.put(1) ?};
SM.use('stm__'+ST.ODDZ+'zb');
SM.clear();
SM.index('SM');
SM.prefix(ST.MAG,_b);
{? SM.first() || SM.ST_WAR+=_c; SM.put(1) ?};
SM.cntx_pop();
''


\adddelsl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: dodaje lub wycofuję zapis dla tabeli SL
::       UWAGA. zapisy zerowe są usuwane z tabeli SL
::   WE: _a - magazyn
::       _b - materiał
::       _c - lokalizacja
::       _d - termin ważności
::       _e - ilość
::       _f - współczynnik 1 lub -1
::       _g - paleta
::       [_h] - identyfikator dostawy - domyślnie brak
::       [_i] - ilość w drugiej jednostce ewidencyjnej domyślnie 0
::   WY: różny od zera jest ok 0 niestety nie dodano
::  OLD: \adddelsl/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
_scean:={? var_pres('_h')=type_of('') & _h<>'brak' || _h || '' ?};
_il2:={? var_pres('_i')=type_of(0) & _i<>0 || _i || 0 ?};

_wyn:=0;
_ref:=null;
SL.index('MG');
SL.prefix(_a,_b);
{? SL.find_key(_c,_d,_g)
|| SL.IL+=_e*_f;
   SL.IL2+=_il2*_f;
   exec('uzupIDkod','magdok_palety',SL);
   _wyn:=SL.put(1);
   _ref:=SL.ref();
   _mag:=SL.MG;
   _pal:=SL.MG().PAL='T'
|| SL.blank();
   SL.MG:=_a;
   SL.M:=_b;
   SL.EANL:=_c;
   SL.TW:=_d;
   SL.IL:=_e*_f;
   SL.JM:=SL.M().J;
   SL.PAL:=_g;
   SL.J2:={? SL.M().J2<>null() || SL.M().J2 || null() ?};
   SL.IL2:=_il2*_f;
   exec('uzupIDkod','magdok_palety',SL);
   _wyn:=SL.add(1);
   _ref:=SL.ref();
   _mag:=SL.MG;
   _pal:=SL.MG().PAL='T'
?};
{? _wyn & ~_pal & _scean<>''
|| SLD.index('SCEAN');
   SLD.prefix(_scean,_scean,_mag,_ref);
   {? SLD.first()
   || SLD.IL+=_e*_f;
      _wyn:=SLD.put(1);
      {? ~SLD.IL || SLD.del() ?}
   || SLD.clear();
      SLD.blank();
      SLD.SCEAN:=_scean;
      SLD.SL:=_ref;
      SLD.IL:=_e*_f;
      _wyn:=SLD.add(1);
      {? ~SLD.IL || SLD.del() ?}
   ?};
   {? ~SL.IL & ~SL.count() || SL.del() ?}
|? _wyn & ~SL.IL & ~SL.count()
|| SL.del()
?};
_wyn


\stan_zb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: aktualizacja zbiorczej tabeli stanow magazynowych
::   WE:  _a - magazyn
::        _b - material
::        _c - zmiana ilosci (na poczatek roku)
::        _d - zmiana wartosci (na poczatek roku)
::        _e - od ktorego roku nastepuje zmiana
::        _f - ilość w drugiej jednostce ewidencyjnej
::  OLD: \stan_zb/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
_sm_s:=_sm_w:=0;
_sm_s2:=_sm_w2:=0;
_il:=_war:=_il2:=0;
_st_il:=_st_war:=_st_il2:=0;
_sm_so:=_sm_sn:=0;
_sm_so2:=_sm_sn2:=0;
SM.cntx_psh();
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |? SM.use('stm__'+ST.ODDZ+($OKR.ROK+2));
      SM.index('SM');
      SM.prefix(_a,_b);
      {? SM.first()
      || _sm_s+=SM.S;
         _sm_w+=SM.W;
         _il:=SM.ST_IL;
         _il2:=SM.ST_IL2;
         _war:=SM.ST_WAR;
         _sm_so+=SM.S_O;
         _sm_sn+=SM.S_N;
         _sm_s2+=SM.S2;
         _sm_w2+=SM.W2;
         _sm_so2+=SM.S_O2;
         _sm_sn2+=SM.S_N2;
         {? OKR.ROK>=_e
         ||
            SM.ST_IL+=_c;
            SM.ST_IL2+=_f;
            SM.ST_WAR+=_d;
            SM.put()
         ?};
         _st_il:=SM.ST_IL;
         _st_il2:=SM.ST_IL2;
         _st_war:=SM.ST_WAR
      ||
         {? OKR.ROK>=_e
         ||
            SM.blank();
            SM.MAG:=_a;
            SM.M:=_b;
            SM.KTM:=SM.M().KTM;
            SM.NKTM:=SM.M().N;
            SM.M_ATR:=SM.M().M_ATR_B;
            {! _wsk:=1..10 |! ($('SM.WAR'+form(_wsk,-2,0,'99')))():=($('SM.M().WAR'+form(_wsk,-2,0,'99')))() !};
            SM.MGR:=M.MGR;
            SM.MGRP:=M.MGRP;
            SM.ST_IL:=_c+_il;
            SM.ST_IL2:=_f+_il2;
            SM.ST_WAR:=_d+_war;
            SM.add(1);
            _st_il:=SM.ST_IL;
            _st_il2:=SM.ST_IL2;
            _st_war:=SM.ST_WAR
         ?}
      ?};
      OKR.next()
   !}
?};
SM.use('stm__'+ST.ODDZ+'zb');
SM.index('SM');
SM.prefix(_a,_b);
{? SM.first()
||
   SM.S:=_sm_s;
   SM.W:=_sm_w;
   SM.ST_IL+=_c;
   SM.ST_IL2+=_f;
   SM.ST_WAR+=_d;
   SM.S_O:=_sm_so;
   SM.S_N:=_sm_sn;
   SM.S2:=_sm_s2;
   SM.W2:=_sm_w2;
   SM.S_O2:=_sm_so2;
   SM.S_N2:=_sm_sn2;
   SM.put()
||
   SM.blank();
   SM.MAG:=_a;
   SM.M:=_b;
   SM.S:=_sm_s;
   SM.W:=_sm_w;
   SM.ST_IL:=_st_il;
   SM.ST_IL2:=_st_il2;
   SM.ST_WAR:=_st_war;
   SM.S_O:=_sm_so;
   SM.S_N:=_sm_sn;
   SM.KTM:=SM.M().KTM;
   SM.NKTM:=SM.M().N;
   SM.M_ATR:=SM.M().M_ATR_B;
   {! _wsk:=1..10 |! ($('SM.WAR'+form(_wsk,-2,0,'99')))():=($('SM.M().WAR'+form(_wsk,-2,0,'99')))() !};
   SM.MGR:=M.MGR;
   SM.MGRP:=M.MGRP;
   SM.S2:=_sm_s2;
   SM.W2:=_sm_w2;
   SM.S_O2:=_sm_so2;
   SM.S_N2:=_sm_sn2;
   SM.add(1)
?};
OKR.cntx_pop();
SM.cntx_pop();
1


\stnilwym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: oblicza stan do danej daty wg podanych wymiarow
::   WE: _a - data
::       _b - wskazanie na magazyn
::       _c - wskazanie na material
::       _d - wskazanie na lokalizacje
::       _e - termin waznosci
::       _f - kod identyfikacji
::       [_g] - ref sql dostawy
::       [_h] - wskazanie na paletę
::   WY: ilosc
::  OLD: \stnilwym/mws2.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_a:={? var_pres('_a')=type_of(date()) & _a>date() || _a || date() ?};
_scsql:={? var_pres('_g')=type_of('') & (+_g)=16 || _g || '' ?};
_pal:={? var_pres('_h')=type_of(null()) || _h || null() ?};

SL.cntx_psh();
SL.index('MG');
SL.prefix(_b,_c);
{? SL.first()
|| _onewym:=SL.MG().SP_REANL='B' | _d<>null() | _e<>date(0,0,0);
   {!
   |? {? {? _onewym & SL.M().SETW<>'P'
         || SL.EANL=_d & (_pal<>null() | _e=SL.TW) & (_pal=null() | SL.PAL=_pal)
         || (_d=null() | SL.EANL=_d) & (_e=date(0,0,0) | _e=SL.TW) & (_pal=null() | SL.PAL=_pal)
         ?}
      || {? _f=''
         || _wyn+=SL.IL-exec('oblsldat','magazyn_stan',_c,SL.EANL,SL.TW,_a,SL.PAL,'',,_scsql,SL.IL)
         || SLD.index('SL');
            SLD.prefix(SL.ref());
            {? SLD.first()
            || {!
               |? {? _f=SLD.SCEAN
                  || _wyn+=SLD.IL-exec('oblsldat','magazyn_stan',_c,SL.EANL,SL.TW,_a,SL.PAL,SLD.SCEAN,,_scsql,SLD.IL)
                  ?};
                  SLD.next()
               !}
            ?}
         ?}
      ?};
      SL.next()
   !}
?};
SL.cntx_pop();
_wyn


\oblsldat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: Oblicza stan wg DK_L-zaakceptowane dokumenty i reorganizacje - powyzej danej daty
::   WE: _a - material
::       _b - lokalizacja
::       _c - termin waznosci
::       _d - data do porownania
::       [_e] - paleta
::       [_f] - kod identyfikacyjny SCEAN
::       [_g] - 1-kontrola tylko terminu ważności - pomijamy lokalizację 0-nie (domyślnie)
::       [_h] - wskazanie na dostawę lub pusta wartość (domyślnie)
::       [_i] - aktualna ilość na stanie - wymagane wypełnienie parametru _h
::   WY: stan powyzej danej daty
::  OLD: \oblsldat/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=5 || {? type_of(_e)<>7 || _e:=null() ?} || _e:=null() ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};
_onlytw:={? var_pres('_g')=type_of(0) || _g || 0 ?};
_scsql:={? var_pres('_h')=type_of('') & +(_h)=16 || _h || '' ?};

_wyn:={? var_pres('_i')=type_of(0) & _scsql<>'' || _i || 0 ?};
_dokl_m:=exec('jaka_dok_m','jm',_a);
_rok:={? _scsql<>'' || 2+(6-_scsql) || form(_d~1,,0,'99')+2 ?};
_akt:=ND.name()+3;
_maski:=ND.names();
_mg:=exec('FindAndGet','#table',EANL,_b,,"MG",null());
_ispal:=exec('FindAndGet','#table',MG,_mg,,"PAL='T'",0);

ND.cntx_psh();
DK.cntx_psh();
DK_L.cntx_psh();
SC.cntx_psh();
_maski.clear();
{? _maski.last()
|| {!
   |? {? (_maski.NAME+2)>=_rok
      || exec('mag_open','open_tab',1+(_maski.NAME+3),_maski.NAME+2);
         {? ~_onlytw
         || DK_L.index('MLOKTW');
            DK_L.prefix(_a,_b,_e,_c)
         || DK_L.index('MTW');
            DK_L.prefix(_a,_e,_c)
         ?};
         {? _f=''
         || {? DK_L.first() & DK_L.find_tab('first','DK',,'<>',null(),'Z',,'=','T','MG',,'=',_mg)
            || {!
               |? _dksql:=DK_L.SCSQL;
                  _ppsql:={? _ispal & DK_L.DK<>null() || DK_L.DK().PRDK || '' ?};
                  {? DK_L.DK().NDK<>''
                  || _msksc:='stc__'+(DK_L.DK().NDK+3);
                     SC.cntx_psh();
                     {? _msksc<>SC.name() || SC.use(_msksc) ?};
                     SC.index('SN');
                     SC.prefix(_mg,DK.M,DK.RDK,DK.NDK);
                     {? SC.first() & SC.SRDK<>'' & SC.SRDK<>_dksql || _dksql:=SC.SRDK ?};
                     SC.cntx_pop()
                  ?};
                  _wsp:={? _scsql<>'' & (_dksql=_scsql | _ppsql=_scsql) || -1 || 0 ?}+
                        {? DK_L.DK().N().D>_d || 1 || 0 ?};
                  _wyn+=_wsp*DK_L.IL $ _dokl_m;
                  DK_L.find_tab('next','DK',,'<>',null(),'Z',,'=','T','MG',,'=',_mg)
               !}
            ?};
            {? DK_L.first() & DK_L.find_tab('first','DK_LN',,'<>',null(),'DK_LN','AKC','=','T','MG',,'=',_mg)
            || {!
               |? _wsp:={? _scsql<>'' & DK_L.SCSQL=_scsql || -1 || 0 ?}+
                        {? {? DK_L.DK_LN().DT>date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,0)
                           || date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,0)>_d
                           |? DK_L.DK_LN().DT<date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,1)
                           || date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,1)>_d
                           || DK_L.DK_LN().DT>_d
                           ?}
                        || 1
                        || 0
                        ?};
                  _wyn+=_wsp*DK_L.IL $ _dokl_m;
                  DK_L.find_tab('next','DK_LN',,'<>',null(),'DK_LN','AKC','=','T')
               !}
            ?}
         |? _f<>''
         || {? DK_L.first() & DK_L.find_tab('first','DK',,'<>',null(),'DK','SCEAN','=',_f,'Z',,'=','T','MG',,'=',_mg)
            || {!
               |? _dksql:=DK_L.SCSQL;
                  _ppsql:={? _ispal & DK_L.DK<>null() || DK_L.DK().PRDK || '' ?};
                  {? DK_L.DK().NDK<>''
                  || _msksc:='stc__'+(DK_L.DK().NDK+3);
                     SC.cntx_psh();
                     {? _msksc<>SC.name() || SC.use(_msksc) ?};
                     SC.index('SN');
                     SC.prefix(_mg,DK.M,DK.RDK,DK.NDK);
                     {? SC.first() & SC.SRDK<>'' & SC.SRDK<>_dksql & SC.SCEAN=_f || _dksql:=SC.SRDK ?};
                     SC.cntx_pop()
                  ?};
                  _wsp:={? _scsql<>'' & (_dksql=_scsql | _ppsql=_scsql) || -1 || 0 ?}+
                        {? DK_L.DK().N().D>_d || 1 || 0 ?};
                  _wyn+=_wsp*DK_L.IL $ _dokl_m;
                  DK_L.find_tab('next','DK',,'<>',null(),'DK','SCEAN','=',_f,'Z',,'=','T','MG',,'=',_mg)
               !}
            ?};
            {? DK_L.first()
             & DK_L.find_tab('first','DK_LN',,'<>',null(),'DK_LN','AKC','=','T','SCEAN',,'=',_f,'MG',,'=',_mg)
            || {!
               |? _wsp:={? _scsql<>'' & DK_L.SCSQL=_scsql || -1 || 0 ?}+
                        {? {? DK_L.DK_LN().DT>date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,0)
                           || date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,0)>_d
                           |? DK_L.DK_LN().DT<date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,1)
                           || date(DK_L.DK_LN().AR,DK_L.DK_LN().AM,1)>_d
                           || DK_L.DK_LN().DT>_d
                           ?}
                        || 1
                        || 0
                        ?};
                  _wyn+=_wsp*DK_L.IL $ _dokl_m;
                  DK_L.find_tab('next','DK_LN',,'<>',null(),'DK_LN','AKC','=','T','SCEAN',,'=',_f,'MG',,'=',_mg)
               !}
            ?}
         ?}
      ?};
      _maski.prev()
   !}
?};
exec('mag_open','open_tab',1+_akt,_akt+2);
ND.cntx_pop();
DK.cntx_pop();
DK_L.cntx_pop();
SC.cntx_pop();
_wyn


\m_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: oblicza wartosci zmiennej stan w okienku materialow
::   WE: [_a] - Rekord bieżący: 1 / 0
::       [_b] - czy obliczac stany 0 - tak, 1 -nie
::   WY: kolor
::  OLD: \m_stan/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=1 ?}  || _a:=1  ?};
_oblst:={? var_pres('_b')=type_of(0) || _b || 0 ?};

SUM.SUM:=0;
SUM.S:=0;

GRSL.END:=0;
BEER.BADSEH:=exec('FindInSet','#table','M_DOD','M_DOD',M.ref(),exec('firma','ustawienia'),"M_DOD.BADSEH",,,null());
_acrwin:=M.win_sel('?');
{? _acrwin='GRPWYB' || _acrwin:='MPAL' ?};

{? M.count()=0 || D_HELP.POPM:=1 || D_HELP.POPM:=0 ?};

_actf:='';

{? _oblst=0
||
   {? (2+BEER.MENU_PTH)='ZW' & ZD_POZ.MG<>null
   || exec('zwr_stan','magazyn_stan',M.ref,1,ZD_POZ.MG,0)
   |? (2+BEER.MENU_PTH)='ZW' & ZD_NAG.MG<>null
   ||
::    akcja dolacz grupowo dla zamowienia dostaw moze nie miec pozycji
      exec('zwr_stan','magazyn_stan',M.ref,1,ZD_NAG.MG,0)
   |? (2+BEER.MENU_PTH)='ZW' & ZD_NAG.MG=null
   || exec('zwr_stan','magazyn_stan',M.ref,3,null,0)
   |? HELP.WHERE='G' & FAP.FAKS & FAP.FAKS().MAG=null
   || exec('obl_stan','magazyn_stan',M.ref,7,,1,,FAP.FAKS().D)
   |? HELP.WMAG=3
   || exec('zwr_stan','magazyn_stan',M.ref,4,null,0)
   || exec('obl_stan','magazyn_stan',M.ref,1,ST.MAG)
   ?};
   SUM.S:=BEER.S
?};

{? type_of(params_get())<>type_of(~~) & var_pres('act_f3_m_dk',params_get())=type_of(obj_new('obj'))
:: Ustawienie akcji w kontekście exec('f3_m_dk','magdok_poz')
|| _act_f3_m_dk:=params_get().act_f3_m_dk;
   M.actions(_act_f3_m_dk.WIN_ACR,_act_f3_m_dk.HID_ACTIONS,,1)
?};

:: akcja 'koszt Wytworzenia' tylko dla produktow
{? _acrwin='NL_WER'
||
   _prod:=exec('tte_lic','tte');
   {? (M.R='W' | M.R='P') & _prod='T'
   || _act:='Z:Z'
   || _act:='ZW:Z'
   ?};
   M.actions(M.win_sel('NL_WER'),_act,,1)
?};

POM.SKLAD:=exec('m_sklad_','material');
{? ~(var_pres('__MkTmp')>0) & 'M'*(1+menu_pth())>0
||
   _act:={? POM.SKLAD=0 || 'AW' || '' ?};
   M.actions(cur_win(1,1),_act,,1)
?};

:: wylaczenie lub wlaczenie ten dla dokumentu magazynowego

{? (2+BEER.MENU_PTH)='MR' & _acrwin<>'NL_WERST'
|| {? (_tab:=M.sel_aget(); _tab.size())
   || _act:=M.actions(cur_win(1,1));
      _nr:=_act*'t';
      {? ~_nr || M.actions(cur_win(1,1),'t'+_act,,1) ?}
   || _act:=M.actions(cur_win(1,1));
      _nr:=_act*'t';
      {? _nr || M.actions(cur_win(1,1),((_nr-1)+_act)+(_nr-_act),,1) ?}
   ?}
?};

HELP.MGR:=M.MGR;
HELP.ADD:='';
HELP.REFMAT:=M.ref();
ZMIENNE.MGAT:=M.MGAT().KOD;
ZMIENNE.MODM:=M.MODM().KOD;
ZRODZO.WYPDOM:={? M.MGRP<>null() & M.MGRP().M=(#M.ref()) || 'T' || 'N' ?};
VAR.DEFSTATS:=exec('statsM','statexam',M.ref());
exec('ustawJMwym','jm');
:: uzueplnienie etykiet słownika budowy strukturalnej
{? M.M_ATR_B<>null
   || {! _i..10 |! ($('POM.SYM'+form(_i,-2,0,'99')))():=($('M.M_ATR_B().SL_'+form(_i,-2,0,'99')+'().NA'))() !}
   || {! _i..10 |! ($('POM.SYM'+form(_i,-2,0,'99')))():='' !}
?};
exec('rekprzed','color','M#01')


\zwr_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: uproszczona funkcja zamiast obl_stan = bazuje na polach tabeli SM
::       UWAGA!!! dzialanie tej funkcji w znaczny sposob wplywa na szybkosc dzialania systemu
::   WE:  _a  kod
::        _b  1 - wg danego magazynu
::            2 - wg operatora zamowien
::            3 - wg wszystkich magazynow dla uzytkownika
::            4 - wg magazynow do realizacji zamowien sprzedazy
::            5 - wg wszystkich magazynow oddzialu
::            6 - wg magazynow do wyliczenia dostepnosci surowcow na produkcje
::            7 - wg wszystkich magazynow dla uzytkownika bez skladow celnych i paletowych
::            9 - wg operatora zamowien wewnetrznych
::            10 - wg magazynow do realizacji zamowien wewnetrznych
::        _c  wg magazynu tylko dla _b=1 (gdy nie wypelnione to biezacy magazyn)
::        _d  czy uzupelniac tabele tymczasowa  __smmag  (0 - nie, 1 - tak)
::        [_e] czy dodatkowo pomijac kompletacje wysylki 1-tak 0-nie (domyslnie)
::        [_f] czy dodatkowo uwzgledniac magazyny paletowe 1-tak 0-nie (domyslnie)
::        [_g] ref DK_C do sprawdzenia
::        [_h] dodatkowo oblicza pola Do zamówienia i Zamówienia 1-tak 0-nie (domyślnie)
::  OLD: \zwr_stan/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};
{? _>=6 || {? type_of(_f)<>1 || _f:=0 ?} || _f:=0 ?};
{? _>=7 || {? type_of(_g)<>7 || _g:=null ?} || _g:=null ?};
{? _>=8 || {? type_of(_h)<>1 || _h:=0 ?} || _h:=0 ?};

_wmg:={? _b=1 & _c=null || _wmg:=ST.MAG || _c ?};

BEER.S:=0;BEER.SD:=0;BEER.SRT:=0;BEER.SRR:=0;BEER.SSZ:=0;BEER.WDR:=0;BEER.SP:=0;
BEER.S2:=0;BEER.SD2:=0;BEER.SRT2:=0;BEER.SRR2:=0;BEER.SSZ2:=0;BEER.WDR2:=0;BEER.SP2:=0;

:: suma rezerwacji tymczasowych na potrzeby rozpisywania pozycji zamowien
BEER.SW:=0;BEER.SW2:=0;

:: uwzględniamy drugą jednostkę ewidencyjną
_withj2:=exec('FindAndGet','#table',M,_a,,"{? J2<>null() & WS2>0 || WS2 || 0 ?}",0);
{? _withj2<>0 || _withj2:=1/_withj2 ?};

:: uprawnione magazyny
{? _b=1
|| _ile:=1; _mag:=obj_new(_ile); _mag[1]:=_wmg
|? _b=2 | _b=9
||
   USERS_UP.index('MG');
   USERS_UP.prefix(OPERATOR.USER,'ZA'+{? _b=2 || 'M' || 'W' ?},ST.ODDZ_KOD);
   {? USERS_UP.first()
   || _ile:=USERS_UP.size();
      _mag:=obj_new(_ile);
      _i  :=0;
      {!
      |? {? _f | USERS_UP.MG().PAL<>'T'
         || _i+=1;
            _mag[_i]:=USERS_UP.MG
         ?};
         USERS_UP.next()
      !};
      _ile:=_i
   || USERS_UP.index('MG');
      USERS_UP.prefix(null,'ZA'+{? _b=2 || 'M' || 'W' ?},ST.ODDZ_KOD);
      {? USERS_UP.first()
      || _ile:=USERS_UP.size();
         _mag:=obj_new(_ile);
         _i:=0;
         {!
         |? {? _f | USERS_UP.MG().PAL<>'T'
            || _i+=1;
               _mag[_i]:=USERS_UP.MG
            ?};
            USERS_UP.next()
         !};
         _ile:=_i
      || _ile:=0
      ?}
   ?}
|? _b=3 | _b=7
||
   USERS_UP.index('MG');
   USERS_UP.prefix(OPERATOR.USER,'MG',ST.ODDZ_KOD);
   {? USERS_UP.first()
   || _ile:=USERS_UP.size();
      _mag:=obj_new(_ile);
      _i  :=0;
      {!
      |? {? {? _b=7 || USERS_UP.MG().PAL<>'T' & USERS_UP.MG().SKL<>'T' || _f | (USERS_UP.MG().PAL<>'T') ?}
         || _i+=1;
            _mag[_i]:=USERS_UP.MG
         ?};
         USERS_UP.next()
      !};
      _ile:=_i
   || _ile:=0
   ?}
|? _b=4 | _b=10
||
   USERS_UP.index('MG');
   USERS_UP.prefix(null,'ZA'+{? _b=4 || 'M' || 'W' ?},ST.ODDZ_KOD);
   {? USERS_UP.first()
   || _ile:=USERS_UP.size();
      _mag:=obj_new(_ile);
      _i:=0;
      {!
      |? _i+=1;
         _mag[_i]:=USERS_UP.MG;
         USERS_UP.next()
      !}
   || _ile:=0
   ?}
|? _b=5
|| MG.cntx_psh();
   MG.index('MAG');
   MG.prefix(ST.ODDZ_KOD);
   {? MG.first()
   || _ile:=MG.size();
      _mag:=obj_new(_ile);
      _i:=0;
      {!
      |? _i+=1;
         _mag[_i]:=MG.ref();
         MG.next()
      !}
   || _ile:=0
   ?};
   MG.cntx_pop()
|? _b=11
|| _filtr:=MG.f_active();
   {? ~_filtr || exec('zakrMag','lmg') ?};
   _ref:=MG.ref();
   {? MG.f_first()
   || _ile:=MG.f_size();
      _mag:=obj_new(_ile);
      _i:=0;
      {!
      |? _i+=1;
         _mag[_i]:=MG.ref();
         MG.f_next()
      !}
   || _ile:=0
   ?};
   MG.f_seek(_ref);
   {? ~_filtr || MG.f_clear() ?}
|? _b=6
|| BUFMG.cntx_psh();
   BUFMG.index('US');
   BUFMG.prefix(OPERATOR.USER,_b);
   {? BUFMG.first()
   || _ile:=BUFMG.size();
      _mag:=obj_new(_ile);
      _i:=0;
      {!
      |?
         _i+=1;
         _mag[_i]:=BUFMG.MG;
         BUFMG.next()
      !};
      _ile:=_i
   || _ile:=0
   ?};
   BUFMG.cntx_pop()
|| _ile:=0
?};
SM.index('SM');
{? _d=1 & var_pres('__smmag')>100
|| __smmag.prefix();
   __smmag.erase()
?};
{? _ile
||
   {! _i:=1.._ile
   |!
      SM.prefix(_mag[_i],_a);
      {? SM.first()
      || _wysylka:={? _e || exec('oblmeanp','magazyn_stan',_mag[_i],_a) || 0 ?};
         {? _withj2>0
         || {? _g<>null
            || _buf:=exec('stnwgdkc','magazyn_stan',_mag[_i],_a,_g,,,1);
               _stan_s:=_buf[1];
               _stan_s2:=_buf[2];
               obj_del(_buf)
            || _stan_s:=SM.S;
               _stan_s2:=SM.S2
            ?}
         || _stan_s:={? _g<>null || exec('stnwgdkc','magazyn_stan',_mag[_i],_a,_g) || SM.S ?};
            _stan_s2:=0
         ?};
         BEER.S+=_stan_s;
         BEER.S2+=_stan_s2;
         _stan_d:=_stan_s-(SM.S-SM.SD);
         _stan_d2:={? _withj2>0 || _stan_s2-(SM.S2-SM.SD2) || 0 ?};
         {? _stan_d<0 || _stan_d:=0 ?};
         BEER.SD+=(_stan_d+_wysylka);
         BEER.WDR+=SM.D;
         BEER.SP+=SM.SP;
         BEER.SW+=(SM.W-_wysylka);
         BEER.SRT+=SM.RT;
         BEER.SRR+=SM.RZ;
         {? _withj2
         || BEER.SD2+=(_stan_d2+_wysylka*_withj2);
            BEER.WDR2+=SM.D2;
            BEER.SP2+=SM.SP2;
            BEER.SW2+=(SM.W2-_wysylka*_withj2);
            BEER.SRT2+=SM.RT2;
            BEER.SRR2+=SM.RZ2
         ?};
::       wypelnianie tabeli ze stanami wg magazynow
         {? _d=1
         || MG.cntx_psh();
            MG.prefix();
            {? MG.seek(_mag[_i])
            ||
               __smmag.MAG:=MG.SYM;
               __smmag.STAN:={? _stan_d<0 || 0 || _stan_d ?};
               __smmag.STA2:=_stan_d2;
               __smmag.RSQL:=$MG.ref();
               __smmag.J2:={? _withj2
                           || exec('FindAndGet','#table',M,_a,,"{? J2<>null() || J2().KOD || '' ?}",'')
                           || ''
                           ?};
               __smmag.add();
               __smmag.first()
            ?};
            MG.cntx_pop()
         ?}
      || {? _d=1
         || MG.cntx_psh();
            MG.prefix();
            {? MG.seek(_mag[_i])
            ||
               __smmag.MAG:=MG.SYM;
               __smmag.STAN:=0;
               __smmag.STA2:=0;
               __smmag.RSQL:=$MG.ref();
               __smmag.J2:={? _withj2
                           || exec('FindAndGet','#table',M,_a,,"{? J2<>null() || J2().KOD || '' ?}",'')
                           || ''
                           ?};
               __smmag.add();
               __smmag.first()
            ?};
            MG.cntx_pop()
         ?}
      ?}
   !};
   HELP.WYD:=BEER.SW;
   BEER.SRA:=BEER.SRT+BEER.SRR;
   BEER.SSZ:=0;
   BEER.SRP:=0;
   BEER.SRA2:=BEER.SRT2+BEER.SRR2;
   BEER.SSZ2:=0;
   BEER.SRP2:=0;
   {? _h || exec('obl_dozam','magazyn_stan',_a,_mag,_ile) ?}
|| _ile:=0
?};
1


\oblmeanp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: oblicza ilosc materialu na EANP wg magazynu - ilosc z kompletacji wysylki (ilosc wyslana)
::   WE: _a - magazyn
::       _b - material
::       [_c] - lokalizacja
::       [_d] - termin waznosci
::       [_e] - rodzaj
::       [_f] - cecha do sprawdzenia
::       [_g] - 1-bez kontoli RSQL 0-tak(domyslnie)
::       [_h] - kod identyfikujacy dostawe
::       [_i] - 1-dodatkowo ilosc z reorganizacji 0-nie(domyślnie)
::       [_j] - EANN.IDADD do pominięcia
::   WY: ilosc na EANP (czytniku)
::  OLD: \oblmeanp/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>7 || _c:=null ?} || _c:=null ?};
{? _>=4 || {? type_of(_d)<>4 || _d:=date(0,0,0) ?} || _d:=date(0,0,0) ?};
{? _>=5 || {? type_of(_e)<>2 || _e:='Z' ?} || _e:='Z' ?};
{? _>=6 || {? type_of(_f)<>7 || _f:=null ?} || _f:=null ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=0 ?} || _g:=0 ?};
{? _>=8 || {? type_of(_h)<>2 || _h:='' ?} || _h:='' ?};
{? _>=9 || {? type_of(_i)<>1 || _i:=0 ?} || _i:=0 ?};
{? _>=10 || {? type_of(_j)<>2 || _j:='' ?} || _j:='' ?};
_nbl:=var_pres('__isope')>0 & __isope=1;

_wyn:=0;
_buf:=tab_tmp(1,'RSQL','STRING[16]','');
_min:=0;
_isp:=1+_i;
_spr:=obj_new(_isp); _spr[1]:='Z'; {? _i || _spr[2]:='R' ?};
EANN.cntx_psh();
EANP.cntx_psh();
{! _ii:=1.._isp
|! EANP.index('TYP');
   {? _c=null || EANP.prefix(_spr[_ii],_b,_a) || EANP.prefix(_spr[_ii],_b,_a,_c,_d) ?};
   {? EANP.first()
   || {!
      |? {? ~_nbl
         || _reann:=#EANP.EANN; _meann:=ref_name(EANP.EANN);
            EANN.cntx_psh(); EANN.prefix(); _blk:=EANN.r_lock(1,1,1,_reann,_meann); EANN.cntx_pop()
         || _blk:=0
         ?};
         {? EANP.EANN<>null & (~_blk | (EANP.EANN().AKT='T' & EANP.EANN().STAN<>'!'))
          & {? _h<>'' & EANP.PAL=null || EANP.SCEAN=_h || 1 ?} & (_j='' | ~_blk | EANP.EANN().IDADD<>_j)
         || _add:=1;
            {? _ii<2 & ~_g & EANP.RSQL<>'' & ((5+EANP.RSQL)='zkpoz') & (_buf.clear(); ~_buf.find_key(EANP.RSQL))
            || {? _f<>null
               || _dkczkp:=exec('FindAndGet','#table',ZK_P,EANP.RSQL,,"DK_C",null());
                  _add:=exec('czyzawch','mat_atr',_f,_dkczkp)
               ?};
               _min+={? _add || exec('FindAndGet','#table',ZK_P,EANP.RSQL,,"ILRB",0) ?};
               _buf.blank();
               _buf.RSQL:=EANP.RSQL;
               _buf.add(1)
            |? _f<>null() & _ii<2 & ~_g & EANP.RSQL<>'' & ((5+EANP.RSQL)='zkpoz')
            || _add:=0
            ?};
            _wyn+={? _add || {? ~_blk | EANP.EANN().STAN='Z' || EANP.ILS || EANP.IL ?} || 0 ?}
         ?};
         {? _blk || EANN.r_unlock() ?};
         EANP.next()
      !};
      {? _min<=_wyn || _wyn-=_min || _wyn:=0 ?}
   ?};
:: dodatkowo sprawdzenie indeksu na paletach zakładamy, ze na EANP nie ma indeksu materiałowego
   PAL.cntx_psh();
   PAL_POZ.cntx_psh();
   EANP.index('TYP');
   {? _c=null || EANP.prefix(_spr[_ii],null,_a) || EANP.prefix(_spr[_ii],null,_a,_c,_d) ?};
   {? EANP.first()
   || _beerm:=BEER.M; BEER.M:=_b;
      {!
      |? {? ~_nbl
         || _reann:=#EANP.EANN; _meann:=ref_name(EANP.EANN);
            EANN.cntx_psh(); EANN.prefix(); _blk:=EANN.r_lock(1,1,1,_reann,_meann); EANN.cntx_pop()
         || _blk:=0
         ?};
         {? EANP.EANN<>null & (~_blk | (EANP.EANN().AKT='T' & EANP.EANN().STAN<>'Z' & EANP.EANN().STAN<>'!'))
          & EANP.PAL<>null & (_j='' | ~_blk | EANP.EANN().IDADD<>_j)
         || _add:=1;
            {? EANP.PAL().AKT='N'
            || PAL_POZ.use('palet'+form(EANP.PAL().AR-2000,-2,0,'99'))
            || PAL_POZ.use('paletyp')
            ?};
            PAL_POZ.index('PAL');
            PAL_POZ.prefix(EANP.PAL,BEER.M().KTM,BEER.M().KTM);
            {? PAL_POZ.first()
            || {!
               |? {? _ii<2 & ~_g & EANP.RSQL<>'' & ((5+EANP.RSQL)='zkpoz') & (_buf.clear(); ~_buf.find_key(EANP.RSQL))
                  || {? _f<>null
                     || _dkczkp:=exec('FindAndGet','#table',ZK_P,EANP.RSQL,,"DK_C",null());
                        _add:=exec('czyzawch','mat_atr',_f,_dkczkp)
                     ?};
                     _min+={? _add || exec('FindAndGet','#table',ZK_P,EANP.RSQL,,"ILRB",0) ?};
                     _buf.blank();
                     _buf.RSQL:=EANP.RSQL;
                     _buf.add(1)
                  ?};
                  _wyn+={? _add || PAL_POZ.ILP || 0 ?};
                  PAL_POZ.next()
               !}
            ?}
         ?};
         {? _blk || EANN.r_unlock() ?};
         EANP.next()
      !};
      {? _min<=_wyn || _wyn-=_min || _wyn:=0 ?};
      BEER.M:=_beerm
   ?};
   PAL.cntx_pop();
   PAL_POZ.cntx_pop()
!};
:: dodalem odczytywanie z ilosci w wydaniu dla WZ-tek z czytnika
{? _c=null || EANP.prefix('W',_b,_a) || EANP.prefix('W',_b,_a,_c,_d) ?};
{? EANP.first()
|| {!
   |? {? ~_nbl
      || _reann:=#EANP.EANN; _meann:=ref_name(EANP.EANN);
         EANN.cntx_psh(); EANN.prefix(); _blk:=EANN.r_lock(1,1,1,_reann,_meann); EANN.cntx_pop()
      || _blk:=0
      ?};
      {? EANP.EANN<>null & (~_blk | EANP.EANN().AKT='T')
       & {? _h<>'' & EANP.PAL=null || EANP.SCEAN=_h || 1 ?}
       & (_j='' | ~_blk | EANP.EANN().IDADD<>_j)
      || _wyn+=EANP.ILS
      ?};
      {? _blk || EANN.r_unlock() ?};
      EANP.next()
   !}
?};
EANN.cntx_pop();
EANP.cntx_pop();
obj_del(_buf);
_wyn


\add_infz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: dodaje jeden rekord do tabeli szczegolow
::   WE: _a - liczba porzadkowa
::       _b - numer porzadkowy
::       _c - opis operacji
::       _d - ilosc
::       _e - trzeci poziom - sczegolowosci
::      [_f] - numer nadrzednego (domyslnie 0)
::      [_g] - ikona
::      [_h] - ref SQL dokumentu
::      [_i] - ilość w drugiej jednostce ewidencyjnej
::   WY: numer dodanego rekordu
::  OLD: \add_infz/rezerw.fml
::       \add_infz/zkd.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=6 || {? type_of(_f)<>1 || _f:=0 ?} || _f:=0 ?};
{? _>=7 || {? type_of(_g)<>2 || _g:='' ?} || _g:='' ?};
{? _>=8 || {? type_of(_h)<>2 || _h:='' ?} || _h:='' ?};
_il2:={? var_pres('_i')=type_of(0) || _i || 0 ?};

INFZAM.clear;
{? _f<>0
|| _tree:=_f
|? _b<>0 & INFZAM.find_key(0,_a,0)
|| _tree:=INFZAM.ref()
|| _tree:=0
?};

INFZAM.blank;
INFZAM.TREE:=_tree;
INFZAM.LP:=_a;
INFZAM.NR:=_b;
INFZAM.SZ:=_e;
INFZAM.OP:=_c;
INFZAM.IL:=_d;
INFZAM.ICO:=_g;
INFZAM.REF:=_h;
INFZAM.IL2:=_il2;
_korzen:={? INFZAM.add(1) || #INFZAM.ref || 0 ?};
{? INFZAM.NR=0 & _tree=0
|| INFZAM.cntx_psh;
   INFZAM.clear;
   INFZAM.prefix(0,_a);
   {? INFZAM.first
   || {!
      |? {? INFZAM.NR=1
         || _ref:=INFZAM.ref; _ok:=INFZAM.next;
            INFZAM.cntx_psh;
            INFZAM.clear;
            {? INFZAM.seek(_ref) || INFZAM.TREE:=_korzen; INFZAM.put(1) ?};
            INFZAM.cntx_pop
         || _ok:=INFZAM.next
         ?};
         _ok
      !}
   ?};
   INFZAM.cntx_pop
|? INFZAM.NR=1 & _tree=0
|| _sz:=INFZAM.SZ;
   INFZAM.cntx_psh;
   INFZAM.clear;
   INFZAM.prefix(0,_a);
   {? INFZAM.first
   || {!
      |? {? INFZAM.NR=2 & {? _a=0 || INFZAM.SZ=_sz || 1 ?}
         || _ref:=INFZAM.ref; _ok:=INFZAM.next;
            INFZAM.cntx_psh;
            INFZAM.clear;
            {? INFZAM.seek(_ref) || INFZAM.TREE:=_korzen; INFZAM.put(1) ?};
            INFZAM.cntx_pop
         || _ok:=INFZAM.next
         ?};
         _ok
      !}
   ?};
   INFZAM.cntx_pop
?};
_korzen


\add_szcz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: dodanie informacji o budowie indeksu do szczegolow
::   WE: _a - ref sql tabeli M
::  OLD: \add_szcz/wzorsl.fml
::----------------------------------------------------------------------------------------------------------------------
_nr:=3;
_i:=0;
M.cntx_psh();
M.clear();
{? M.seek(_a)
|| {!
   |? _i+=1;
      {? ($('M.M_ATR_B().SL_'+form(_i,-2,0,'99')))()<>null
      || _naz:=($('M.M_ATR_B().SL_'+form(_i,-2,0,'99')+'().NA'))()+': '+($('M.WAR'+form(_i,-2,0,'99')))();
         exec('add_infz','magazyn_stan',0,1,' '+_naz,0,_nr+_i);
         _i<10
      || 0
      ?}
   !}
?};
M.cntx_pop();
~~


\add_zami
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: informacja o zamiennikach
::  OLD: \add_zami/zkd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
M.cntx_psh();
MZ.index('MZ');
MZ.prefix(_a);
{? MZ.first
|| {!
   |? exec('obl_stan','magazyn_stan',MZ.MZ,_b,_c);
      {? BEER.SD>0
      || _wyn+=BEER.SD;
         exec('add_infz','magazyn_stan',10,1,' - '+MZ.MZ().KTM+' - '+MZ.MZ().N,BEER.SD,0,,,,BEER.SD2)
      ?};
      MZ.next
   !};
   exec('add_infz','magazyn_stan',10,0,'STAN WG ZAMIENNIKÓW'@,_wyn,0)
?};
M.cntx_pop();
_wyn


\addstrez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: dodaje lub aktualizuje jeden rekord do tabeli informujacej o rezerwacjach
::   WE: _a - symbol magazynu
::       _b - typ rezerwacji
::       _c - ilosc
::       _d - symbol dokumentu
::       _e - pozycja
::       _f - ref SQL-owy
::       _g - indeks materialowy
::   WY: 1-dodano/poprawion 0-nie
::  OLD: \addstrez/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
__strez.cntx_psh();
__strez.clear();
__strez.prefix(_g,_g,_a,_b,_d,_e);
{? __strez.first()
|| __strez.ILE+=_c;
   _wyn:=__strez.put(1)
|| __strez.blank();
   __strez.MAG:=_a;
   __strez.TYP:=_b;
   __strez.ILE:=_c;
   __strez.SYM:=_d;
   __strez.POZ:=_e;
   __strez.SQL:=_f;
   __strez.KTM:=__strez.KT2:=_g;
   _wyn:=__strez.add(1)
?};
__strez.cntx_pop();
_wyn


\uprmgpow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: sprawdza czy dany magazyn lezy w zakresie uprawnien
::   WE: _a - ref magazynu
::       _b - zakres liczbowy
::       _c - uzytkownik
::   WY: 1-ok 0-nie
::  OLD: \uprmgpow/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
BUFMG.cntx_psh();
BUFMG.index('US');
BUFMG.prefix(_c,_b,_a);
_wyn:=BUFMG.first();
BUFMG.cntx_pop();
_wyn


\sum_infz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: oblicza sumy dla poszczegolnych galezi
::  OLD: \sum_infz/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
INFZAM.clear();
INFZAM.prefix(0);
{? INFZAM.first()
|| {!
   |? {? INFZAM.LP>0
      || _korzen:=INFZAM.ref();
         _sum:=0; _su2:=0;
         INFZAM.cntx_psh();
         INFZAM.clear();
         INFZAM.prefix(_korzen);
         {? INFZAM.first() || {! |? _sum+=INFZAM.IL; _su2+=INFZAM.IL2; INFZAM.next() !} ?};
         INFZAM.cntx_pop();
         INFZAM.IL:=_sum;
         INFZAM.IL2:=_su2;
         INFZAM.put(1)
      ?};
      INFZAM.next()
   !}
?};
INFZAM.clear();
INFZAM.first();
~~


\uprmgzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: Sprawdza czy dany magazyn jest na liscie uprawnionych do realizacji zamowien
::   WE: _a - ref magazynu lub null (jesli choc jeden spelnia kryteria)
::       _b - typ sprawdzenia ZAM-sprzedazy ZAW-wewnetrzne
::   WY: 1-tak 0-nie
::  OLD: \uprmgzam/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
USERS_UP.cntx_psh();
USERS_UP.index('MG');
{? _a=null() || USERS_UP.prefix(null,_b,ST.ODDZ) || USERS_UP.prefix(null,_b,ST.ODDZ,_a) ?};
_wyn:=USERS_UP.first();
USERS_UP.cntx_pop();
_wyn


\refwg_zp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.41]
:: OPIS: oblicza ilosc w wydaniu dla podanego identyfikatora
::   WE: _a - naglowek
::       _b - pole ZP
::   WY: ref SQL DK
::  OLD: \refwg_zp/pommag.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
ND.cntx_psh();
DK.cntx_psh();
DK.clear();
{? DK.seek(_b,) & DK.N=_a || _wyn:=$DK.ref() ?};
ND.cntx_pop();
DK.cntx_pop();
_wyn


\sort_sl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: buduje tabelke stanow wg wymiarow na podstawie roznych parametrow
::   WE: _a - typ analizy
::       _b - czy wg stref magazynowania
::       _c - ref magazynu
::       _d - ref materialu
::       _e - kod kontrahenta lub pusty ciag
::       [_f] - ref palety
::       [_g] - czy uwzgledniac niezakceptowane DK_L-e - domyslnie-0 czyli nie
::       [_h] - kod identyfikujacy dostawe
::       [_i] - czy tylko z Dokow-1 domyslnie 0 czyli nie
::       [_j] - data - termin ważności do kontroli
::              0 - nie kontrolować (wartość domyślna)
::       [_k] - dotyczy statusu dostawy
::   WY: tabelka danych z SL-a
::  OLD: \sort_sl/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=6 || {? type_of(_f)<>7 || _f:=null ?} || _f:=null ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=0 ?} || _g:=0 ?};
{? _>=8 || {? type_of(_h)<>2 || _h:='' ?} || _h:='' ?};
{? _>=9 || {? type_of(_i)<>1 || _i:=0 ?} || _i:=0 ?};
_twctrl:=date(0,0,0);
_ctrltw:={? var_pres('_j')=type_of(date(0,0,0)) || _twctrl:=_j; 1 || 0 ?};
_stats:={? var_pres('_k')=type_of(null()) || _k || null() ?};

{? exec('FindAndGet','#table',MG,_c,,"TYP*'DOST'",0) & _stats<>null()
|| _loop:=2
|| _stats:=null();
   _loop:=1
?};

_wyn:=tab_tmp(1,'LP','INTEGER',''
       ,'SQL','STRING[16]',''
       ,'BYL','INTEGER',''
       ,'EAN','STRING[16]',''
       ,'SCEAN','STRING[128]','');

_wg_dok:=_i;
_i:=0;
SL.index('AUT'+_a);
SL.prefix(_c,_d);
{? SL.first()
|| {!
   |? {? SL.EANL().AKT='T' & (~_b | (_b & SL.EANL().BLOK<>'T')) & {? _f=null || 1 || SL.PAL=_f ?}
       & (~_ctrltw | _twctrl=SL.TW)
      || {? _h=''
         || _i+=1;
            _wyn.clear();
            _wyn.blank();
            _wyn.LP:=_i;
            _wyn.SQL:=$SL.ref();
            _wyn.BYL:=0;
            _wyn.EAN:=$exec('zwrocdok','wymiary_mag',SL.EANL,_c,_wg_dok);
            _wyn.SCEAN:='';
            _wyn.add(1)
         || SLD.index('SL');
            SLD.prefix(SL.ref());
            {? SLD.first()
            || {!
               |? _i+=1;
                  _wyn.clear();
                  _wyn.blank();
                  _wyn.LP:=_i;
                  _wyn.SQL:=$SL.ref();
                  _wyn.BYL:=0;
                  _wyn.EAN:=$exec('zwrocdok','wymiary_mag',SL.EANL,_c,_wg_dok);
                  _wyn.SCEAN:=SLD.SCEAN;
                  _wyn.add(1);
                  SLD.next()
               !}
            ?}
         ?}
      ?};
      SL.next()
   !}
?};
:: niezaakceptowane DK_L-e z reorganizacji
{? _g
|| DK_L.cntx_psh();
   DK_L.index('MMG');
   DK_L.prefix(_d,_c);
   {? DK_L.first()
   || {!
      |? {? DK_L.DK=null & DK_L.DK_LN().AKC='N' & DK_L.LOKDO<>null
          & DK_L.LOKDO().AKT='T' & (~_b | (_b & DK_L.LOKDO().BLOK<>'T'))
          & (~_ctrltw | _twctrl=SL.TW)
         || _i+=1;
            _wyn.clear();
            _wyn.blank();
            _wyn.LP:=_i;
            _wyn.SQL:=$DK_L.ref();
            _wyn.BYL:=0;
            _wyn.EAN:=$exec('zwrocdok','wymiary_mag',DK_L.LOKDO,_c,_wg_dok);
            _wyn.SCEAN:={? _h='' || '' || DK_L.SCEAN ?};
            _wyn.add(1)
         ?};
         DK_L.next()
      !}
   ?};
   DK_L.cntx_pop()
?};

{? _b
||
   _kh:={? _e<>'' || exec('FindInSet','#table','KH','KOD',_e,2) || null ?};

:: renumeracja wg stref magazynowania

   _ndx:=_wyn.ndx_tmp('',0,'EAN',,0,'EAN',,0,'LP',,0);

   _i:=0;
   _ll:=_loop-1;
   {!
   |?
:: dla kontrahenta i indeksu
      {? _kh<>null
      || M_MG_GR.index('M_KH');
         M_MG_GR.prefix(_d,_kh);
         {? M_MG_GR.first()
         || {!
            |? MG_GRP.index('MG_GR');
               MG_GRP.prefix(M_MG_GR.MG_GR,_c);
               {? MG_GRP.first() & (~_ll | (_ll & MG_GRP.find_tab('first','MG_GR','DEFSTATS','=',_stats)))
               || {!
                  |? _lok:=$exec('zwrocdok','wymiary_mag',MG_GRP.EANL,_c,_wg_dok);
                     _wyn.clear();
                     _wyn.index(_ndx);
                     _wyn.prefix(_lok,_lok);
                     {? _wyn.first()
                     || {!
                        |? {? ~_wyn.BYL
                           || _i+=1;
                              _wyn.BYL:=_i;
                              _wyn.put(1)
                           ?};
                           _wyn.next()
                        !}
                     ?};
                     MG_GRP.next() & (~_ll | (_ll & MG_GRP.find_tab('next','MG_GR','DEFSTATS','=',_stats)))
                  !}
               ?};
               M_MG_GR.next()
            !}
         ?}
      ?};

:: dla kontrahenta
      {? _kh<>null
      || MG_GR.index('KH');
         MG_GR.prefix(_kh);
         {? MG_GR.first()
         || {!
            |? MG_GRP.index('MG_GR');
               MG_GRP.prefix(MG_GR.ref(),_c);
               {? MG_GRP.first() & (~_ll | (_ll & MG_GRP.find_tab('first','MG_GR','DEFSTATS','=',_stats)))
               || {!
                  |? _lok:=$exec('zwrocdok','wymiary_mag',MG_GRP.EANL,_c,_wg_dok);
                     _wyn.clear();
                     _wyn.index(_ndx);
                     _wyn.prefix(_lok,_lok);
                     {? _wyn.first()
                     || {!
                        |? {? ~_wyn.BYL
                           || _i+=1;
                              _wyn.BYL:=_i;
                              _wyn.put(1)
                           ?};
                           _wyn.next()
                        !}
                     ?};
                     MG_GRP.next() & (~_ll | (_ll & MG_GRP.find_tab('next','MG_GR','DEFSTATS','=',_stats)))
                  !}
               ?};
               M_MG_GR.next()
            !}
         ?}
      ?};

:: strefy dla materialu
      M_MG_GR.index('M');
      M_MG_GR.prefix(_d);
      {? M_MG_GR.first()
      || {!
         |? {? M_MG_GR.MG_GR().KH=null
            || MG_GRP.index('MG_GR');
               MG_GRP.prefix(M_MG_GR.MG_GR,_c);
               {? MG_GRP.first() & (~_ll | (_ll & MG_GRP.find_tab('first','MG_GR','DEFSTATS','=',_stats)))
               || {!
                  |? _lok:=$exec('zwrocdok','wymiary_mag',MG_GRP.EANL,_c,_wg_dok);
                     _wyn.clear();
                     _wyn.index(_ndx);
                     _wyn.prefix(_lok,_lok);
                     {? _wyn.first()
                     || {!
                        |? {? ~_wyn.BYL
                           || _i+=1;
                              _wyn.BYL:=_i;
                              _wyn.put(1)
                           ?};
                           _wyn.next()
                        !}
                     ?};
                     MG_GRP.next() & (~_ll | (_ll & MG_GRP.find_tab('next','MG_GR','DEFSTATS','=',_stats)))
                  !}
               ?}
            ?};
            M_MG_GR.next()
         !}
      ?};
:: poszukiwania wg statusu dostawy
      {? _loop=2
      || MG_GR.index('KODST');
         MG_GR.prefix(_stats);
         {!
         |? {? MG_GR.first()
            || {!
               |? MG_GRP.index('MG_GR');
                  MG_GRP.prefix(MG_GR.ref(),_a);
                  {? MG_GRP.first()
                  || {!
                     |? _lok:=$exec('zwrocdok','wymiary_mag',MG_GRP.EANL,_c,_wg_dok);
                        _wyn.clear();
                        _wyn.index(_ndx);
                        _wyn.prefix(_lok,_lok);
                        {? _wyn.first()
                        || {!
                           |? {? ~_wyn.BYL
                              || _i+=1;
                                 _wyn.BYL:=_i;
                                 _wyn.put(1)
                              ?};
                              _wyn.next()
                           !}
                        ?};
                        MG_GRP.next()
                     !}
                  ?};
                  MG_GR.next()
               !}
            ?};
            MG_GR.next()
         !}
      ?};
      _ll-=1;
      _ll>=0
   !};
   _wyn.ndx_drop(_ndx);

:: ustawienie nowego porzadku
   _wyn.clear();
   {? _wyn.first()
   || {!
      |? {? ~_wyn.BYL
         || _i+=1;
            _wyn.BYL:=_i;
            _wyn.put(1)
         ?};
         _ref:=_wyn.ref();
         _nxt:=_wyn.next();
         _wyn.cntx_psh();
         _wyn.clear();
         {? _wyn.seek(_ref)
         || _wyn.LP:=_wyn.BYL;
            _wyn.put(1)
         ?};
         _wyn.cntx_pop();
         _nxt
      !}
   ?}
?};

{? _h<>''
||
:: renumeracja wg identyfikacji dostaw
   _i:=0;
   _ndx:=_wyn.ndx_tmp('',0,'SCEAN',,0,'SCEAN',,0,'LP',,0);
   _wyn.index(_ndx);
   _wyn.prefix(_h,_h);
   {? _wyn.first()
   || {!
      |? _i+=1;
         _wyn.BYL:=_i;
         _wyn.put(1);
         _wyn.next()
      !}
   ?};
   _wyn.ndx_drop(_ndx);
   _ndx:=_wyn.ndx_tmp('',0,'LP',,0);
   _wyn.index(_ndx);
   _wyn.prefix();
   {? _wyn.first()
   || {!
      |? {? _wyn.SCEAN<>_h
         || _i+=1;
            _wyn.BYL:=_i;
            _wyn.put(1)
         ?};
         _wyn.next()
      !}
   ?};
   _wyn.ndx_drop(_ndx);
   _ndx:=_wyn.ndx_tmp('',0,'BYL',,0,'EAN',,0);
   _wyn.index(_ndx);
   {? _wyn.first()
   || {!
      |? _wyn.LP:=_wyn.BYL;
         _wyn.put(1);
         _wyn.next()
      !}
   ?};
   _wyn.ndx_drop(_ndx)
?};

_wyn


\ilenasld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: oblicza ilosc wg identyfikacji dostawy
::   WE: _a - ref SL
::       _b - ref MG
::       _c - SCEAN
::       _d - ref SQL pozycji do pominiecia lub ''
::  OLD: \ilenasld/ean2.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
SL.cntx_psh();
SLD.cntx_psh();
SLD.index('SCEAN');
SLD.prefix(_c,_c,_b,_a);
_wyn:={? SLD.first() || SLD.IL-exec('wyda_sce','magazyn_stan',SLD.SL().M,SLD.SCEAN,_b,_d) || 0 ?};
SL.cntx_pop();
SLD.cntx_pop();
_wyn


\cenas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: ustalenie ceny dla magazynu srednie
::   WE: _a-M.ref
::       _b-Ś/E/F/L/D-typ magazynu
::       _c-maska roku 'rr'
::  OLD: \cenas/pommag.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<>type_of('str') || _c:=DK.name()+2 ?};

_cenas:=0;
_ktm:=_a;
_tmg:=_b;
{? 'Ś'=_tmg
||
   _msk:=_c;
   OKR.cntx_psh(); M.cntx_psh();
   OKR.index('MC'); OKR.prefix(REF.FIRMA,1,#('20'+_msk));
   {? OKR.first()
   ||
      SM.cntx_psh();
      SM.use('stm__'+ST.ODDZ+form(#_msk,-2,,'99'));
      SM.index('SM');
      SM.prefix(ND.MAG,_ktm);
      _cenas:=0;
      {? SM.first() & SM.ST_IL>0
      || _cenas:=(SM.ST_WAR/SM.ST_IL)$SM.M().DOKL_C
      || _cenas:=exec('cenas','magazyn_stan',_a,_b,form(#_msk-1,-2))
      ?};
      SM.cntx_pop()
   ||
      exec('add_kom','#message','Zerowa cena magazynowa %1.'@[DK.M().KTM],2);
      undo()
   ?};
   OKR.cntx_pop(); M.cntx_pop()
?};
_cenas


\OBLSTAN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: oblicza stan magazynowy - zmienia zapisy na stanach
::   WE:  _a  - magazyn
::        _b  - indeks materialowy
::        _c  - data dostawy
::        _d  - cena
::        _e  - dokument przychodowy
::        _f  - miejsce wywolania 0 biezaca praca 1 odtwarzanie stanow
::
::        _g  - ref dokumentu przychodowego
::        _h  - name dokumentu przychodowego
::        _i  - ref sql dokumentu przychodowego
::        _j  - ref sql pierwotnego dokumentu przychodowego
::       [_k] - cena magazynowa walutowa
::       [_l] - waluta ceny magazynowej walutowej
::       [_m] - status dostawy
::       [_n] - identyfiakcja dostawy (kod kreskowy)
::       [_o] - termin ważności
::   WY: zwraca 1 jesli udalo sie zaktualizowac stany, w przeciwnym przypadku zwraca 0
::  OLD: \FD.OBLSTAN/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=11 || {? type_of(_k)<>1 || _k:=0 ?}    || _k:=0 ?};
{? _>=12 || {? type_of(_l)<>7 || _l:=null ?} || _l:=null ?};
_stats:={? _>=13 & type_of(_m)=type_of(STATS.ref()) & _e='T' || _m || null() ?};
{? _>=14 || {? type_of(_n)<>2 || _n:='' ?} || _n:='' ?};
{? _>=15 || {? type_of(_o)<>4 || _o:=date(0,0,0) ?} || _o:=date(0,0,0) ?};

SM.cntx_psh();
SC.cntx_psh();
{? _h<>''
||
   SM.use('stm__'+(5-_h));
   SC.use('stc__'+(5-_h))
?};
_wynik:=1;
_tmg:=1+DK.N().MAG().TYP;
_my_dat:=DK.N().D;
_msk:=DK.name()+2;
_kor:=DK.N().TYP().DK='T';

DK.Z:='T';
{? DK.RDK=0 || DK.RDK:=#DK.ref ?};
DK.put();

{? (DK.N().TYP().DN='T' | (DK.N().INN='T' & DK.N().TYP().INW='I' & DK.N().TYP().P='T')) & (_g<>#DK.ref | _h<>DK.name)
||
   _rdk:=DK.RDK;_ndk:=DK.NDK;
   {? _rdk<>0 & _ndk<>''
   ||
      ND.cntx_psh();
      DK.cntx_psh();
      DK.use(_ndk);
      ND.use('nagdo'+(5-_ndk));
      DK.clear();
      {? DK.seek(_rdk,_ndk)
      || _c:=DK.N().D
      ?};
      DK.cntx_pop();
      ND.cntx_pop()
   ?}
?};

{? exec('czy_bo','magdok_wspolne',DK.N().TYP) || _c:=DK.DOST ?};
_jestsc:=0;
_jmg:=DK.M().J2<>null();
_dklsrdk:='';

SC.index('SN');
SC.prefix(_a,_b,_g,_h);
{? SC.first()
||
   {? _e='N' & SC.S < DK.IL &  _f=0
   ||
      {? var_pres('__kom')>100
      || exec('add_kom','#message','Materiału %1 nie ma na stanie. Zakończenie redakcji dokumentu niemożliwe.'@[DK.M().KTM]
          ,2)
      || FUN.info('Materiału %1 nie ma na stanie. Zakończenie redakcji dokumentu niemożliwe.'@[DK.M().KTM])
      ?};
      _wynik:=0
   ||
      {? _e='T'
      || SC.S+=DK.IL;
         {? _jmg || SC.S2+=DK.IL2 ?}
      || SC.S-=DK.IL;
         {? _jmg || SC.S2-=DK.IL2 ?}
      ?};
      {? SC.S=0 || SC.A:='N' || SC.A:='T' ?};
      {? SC.STATS=null()
      || SC.STATS:={? _stats<>null() || _stats || exec('FindAndGet','#table',DK,SC.SRDK,,"STATS",null()) ?}
      ?};
      {? SC.put()
      || _jestsc:=2
      || _wynik:=0
      ?};
      {? _e<>'T'
      || DK.DOST:=SC.D;
         DK.RDK:=SC.RDK;
         DK.NDK:=SC.NDK;
         DK.SRDK:=SC.SRDK;
         DK.PRDK:=SC.PRDK;
         DK.DK_C:=SC.DK_C;
         DK.SCEAN:=SC.SCEAN;
         {? DK.STATS=null || DK.STATS:=SC.STATS ?}
      ?};
      DK.put();
      {? DK.SRDK<>SC.SRDK & DK.PLUS='T' || _dklsrdk:=SC.SRDK ?}
   ?}
||
   {? _e='T'  | _f=1
   ||
      SC.blank();
      SC.MAG:=_a;
      SC.M:=_b;
      SC.D:=_c;
      SC.C:=_d;
      SC.RDK:=_g;
      SC.NDK:=_h;
      SC.SRDK:=_i;
      SC.PRDK:=_j;
      SC.SCC:=_k;
      SC.SCWAL:=_l;
      SC.DK_C:=DK.DK_C;
      {? SC.DK_C().M_ATR<>null
      || {! _i:=1..10
         |! ($('SC.WAR'+form(_i,-2,,'99')))():=($('SC.DK_C().WAR'+form(_i,-2,,'99')))()
         !}
      ?};
      {? _e='T' || SC.S+=DK.IL || SC.S-=DK.IL ?};
      {? SC.S=0 || SC.A:='N' || SC.A:='T' ?};
      SC.STATS:={? _stats<>null() || _stats || exec('FindAndGet','#table',DK,SC.SRDK,,"STATS",null()) ?};
      SC.SCEAN:=_n;
      SC.TW:=_o;
      {? _jmg
      || SC.J2:=DK.M().J2;
         SC.S2:=DK.IL2
      ?};
      _wynik:=SC.add();
      _jestsc:=1;
      {? _wynik=0 || _jestsc:=0 ?};
      DK.DOST:=SC.D;
      DK.RDK:=SC.RDK;
      DK.NDK:=SC.NDK;
      DK.SRDK:=SC.SRDK;
      DK.PRDK:=SC.PRDK;
      DK.DK_C:=SC.DK_C;
      DK.STATS:=SC.STATS;
      DK.SCEAN:=SC.SCEAN;
      DK.put()
   || _wynik:=0;
      {? var_pres('__kom')>100
      || exec('add_kom','#message','Materiału %1 nie ma na stanie. Zakończenie redakcji dokumentu niemożliwe.'@[DK.M().KTM]
          ,2)
      || FUN.info('Materiału %1 nie ma na stanie. Zakończenie redakcji dokumentu niemożliwe.'@[DK.M().KTM])
      ?}
   ?}
?};
{? _jestsc
|| {? DK.NDK<>''
   || _da:=DK.N().DA;
      _ta:=DK.N().TA;
      {? _da=date(0,0,0) & DK.NDK<>'' & DK.RDK<>0 & ~(DK.RDK=(#DK.ref()) & DK.NDK=ref_name(DK.ref()))
      || _msk:=DK.NDK+3;
         _rdk:=DK.RDK;
         SC.cntx_psh();
         ND.cntx_psh();
         DK.cntx_psh();
         ND.use('nagdo'+_msk);
         DK.use('dokma'+_msk);
         DK.clear();
         {? DK.seek(_rdk,)
         || _da:=DK.N().DA;
            _ta:=DK.N().TA
         ?};
         ND.cntx_pop();
         DK.cntx_pop();
         SC.cntx_pop()
      ?};
      SC.DA:=_da;
      SC.TA:=_ta;
      {? ~SC.put() || _wynik=0 ?}
   ?};
   {? _jestsc=2 & _dklsrdk<>''
   || DK_L.cntx_psh();
      DK_L.index('DK');
      DK_L.prefix(DK.ref(),null());
      {? DK_L.first()
      || {!
         |? DK_L.SCSQL:=_dklsrdk;
            DK_L.put(1);
            DK_L.next()
         !}
      ?};
      DK_L.cntx_pop()
   ?}
?};
{? _wynik=1
||
:: sprawdzenie dostepnosci stanow w masce zbiorczej
   SM.cntx_psh();
   SM.use('stm__'+ST.ODDZ+'zb');
   SM.index('SM');
   SM.prefix(_a,_b);
   {? SM.first()
   || {? _e='N' & SM.S<DK.IL & _f=0
      ||
         {? var_pres('__kom')>100
         || exec('add_kom','#message','Materiału %1 nie ma na stanie. Zakończenie redakcji dokumentu niemożliwe.'@[DK.M().KTM]
             ,2)
         || FUN.info('Materiału %1 nie ma na stanie. Zakończenie redakcji dokumentu niemożliwe.'@[DK.M().KTM])
         ?};
         _wynik:=0
      ?}
   ||
      {? _e='T' | _f=1
      ||
         _wynik:=1
      ||
         {? var_pres('__kom')>100
         || exec('add_kom','#message','Materiału %1 nie ma na stanie. Zakończenie redakcji dokumentu niemożliwe.'@[DK.M().KTM]
             ,2)
         || FUN.info('Materiału %1 nie ma na stanie. Zakończenie redakcji dokumentu niemożliwe.'@[DK.M().KTM])
         ?};
         _wynik:=0
      ?}
   ?};
   SM.cntx_pop();
:: aktualizacja stanu w masce rocznej
   {? _wynik=1
   ||
      SM.index('SM');
      SM.prefix(_a,_b);
      {? SM.first()
      ||
         {? _e='T'
         || SM.S+=DK.IL;
            {? _jmg || SM.S2+=DK.IL2 ?};
            {? ~_kor
            || {? DK.STATS().KIND='O' | STATS.KIND='' & SM.MAG().DEFSTATS().KIND='O'
               || SM.S_O+=DK.IL;
                  {? _jmg || SM.S_O2+=DK.IL2 ?}
               |? DK.STATS().KIND='N' | STATS.KIND='' & SM.MAG().DEFSTATS().KIND='N'
               || SM.S_N+=DK.IL;
                  {? _jmg || SM.S_N2+=DK.IL2 ?}
               ?}
            ?}
         || SM.S-=DK.IL;
            {? _jmg || SM.S2-=DK.IL2 ?};
            {? ~_kor
            || {? DK.STATS().KIND='O' | STATS.KIND='' & SM.MAG().DEFSTATS().KIND='O'
               || SM.S_O-=DK.IL;
                  {? _jmg || SM.S_O2-=DK.IL2 ?}
               |? DK.STATS().KIND='N' | STATS.KIND='' & SM.MAG().DEFSTATS().KIND='N'
               || SM.S_N-=DK.IL;
                  {? _jmg || SM.S_N2-=DK.IL2 ?}
               ?}
            ?}
         ?};
         SM.J2:={? _jmg || SM.M().J2 || SM.M().J ?};
         SM.SM2:={? _jmg || 'T' || 'N' ?};
         {? ~SM.put() || _wynik:=0 ?}
      ||
::       zalozenie brakujacego SM
         _st_il:=_st_war:=_st_il2:=0;
         SM.cntx_psh(); OKR.cntx_psh();
         OKR.index('MC'); OKR.prefix(REF.FIRMA,1);
         _loop:=OKR.find_key(2000+(#_msk-1));
         {!
         |? _loop
         |!
            SM.use('stm__'+ST.ODDZ+($OKR.ROK+2));
            SM.index('SM');
            SM.prefix(_a,_b);
            {? SM.first()
            ||
               _st_il:=SM.ST_IL;
               _st_il2:=SM.ST_IL2;
               _st_war:=SM.ST_WAR;
               _loop:=0
            ||
               _loop:=OKR.prev()
            ?}
         !};
         SM.cntx_pop(); OKR.cntx_pop();
         SM.blank();
         SM.MAG:=_a;
         SM.M:=_b;
         SM.KTM:=SM.M().KTM;
         SM.NKTM:=SM.M().N;
         SM.M_ATR:=SM.M().M_ATR_B;
         {! _wsk:=1..10 |! ($('SM.WAR'+form(_wsk,-2,0,'99')))():=($('SM.M().WAR'+form(_wsk,-2,0,'99')))() !};
         SM.MGR:=M.MGR;
         SM.MGRP:=M.MGRP;
         SM.ST_IL:=_st_il;
         SM.ST_IL2:=_st_il2;
         SM.ST_WAR:=_st_war;
         SM.J2:={? _jmg || SM.M().J2 || SM.M().J ?};
         SM.SM2:={? _jmg || 'T' || 'N' ?};
         {? _e='T'
         || SM.S+=DK.IL;
            {? _jmg || SM.S2+=DK.IL2 ?};
            {? ~_kor
            || {? DK.STATS().KIND='O' | STATS.KIND='' & SM.MAG().DEFSTATS().KIND='O'
               || SM.S_O+=DK.IL;
                  {? _jmg || SM.S_O2+=DK.IL2 ?}
               |? DK.STATS().KIND='N' | STATS.KIND='' & SM.MAG().DEFSTATS().KIND='N'
               || SM.S_N+=DK.IL;
                  {? _jmg || SM.S_N2+=DK.IL2 ?}
               ?}
            ?}
         || SM.S-=DK.IL;
            {? _jmg || SM.S2-=DK.IL2 ?};
            {? ~_kor
            || {? DK.STATS().KIND='O' | STATS.KIND='' & SM.MAG().DEFSTATS().KIND='O'
               || SM.S_O-=DK.IL;
                  {? _jmg || SM.S_O2+=DK.IL2 ?}
               |? DK.STATS().KIND='N' | STATS.KIND='' & SM.MAG().DEFSTATS().KIND='N'
               || SM.S_N-=DK.IL;
                  {? _jmg || SM.S_N2+=DK.IL2 ?}
               ?}
            ?}
         ?};

         SM.add(1)
      ?}
   ?}
?};

:: uzupelnianie zbiorczej tabeli stanow
{? _wynik=1
||
   exec('stan_zb','magazyn_stan',_a,_b,{? _e='T' || DK.IL || DK.IL* -1 ?}
    ,{? _e='T' || DK.WAR || DK.WAR*-1 ?},DK.N().AR,{? _jmg || {? _e='T' || DK.IL2 || DK.IL2*-1 ?} || 0 ?})
?};
SC.cntx_pop();
SM.cntx_pop();
_wynik


\dk_l2sl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: aktualizacja SL-a wg DK i DK_L
::   WE: _a - ref DK
::       [_b] - wspolczynnik
::       [_c] - przy odtworzeniu stanow
::       [_d] - tabela palet (domyslnie 0) 1-__bufpal
::       [_e] - czy na magazynie paletowym (domyslnie 0) 1-tak
::       [_f] - czy uzgodnienie palety wg oddzialow
::  OLD: \dk_l2sl/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};
{? _>=3 || {? type_of(_c)<>1 || _c:=0 ?} || _c:=0 ?};
{? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};
{? _>=6 || {? type_of(_f)<>1 || _f:=0 ?} || _f:=0 ?};

:: rozpakowanie palety dodatkowe zapisy dla wymiarow
{? ~_c & DK.PLUS='N' & DK.Z='T' & DK.N().MAG().PAL='T' || exec('pal2dkdk','magdok_palety',_a) ?};

DK_L.index('DK');
DK_L.prefix(_a,null);
{? (_first:=DK_L.first())>0
|| _wsp:={? ~_b || {? DK_L.DK().PLUS='N' & ~_c || -1 || 1 ?} || _b ?};
   _mmmp:={? DK.N().MD<>null & DK.N().MD().PAL='T' || 1 |? DK.N().NDSQL<>'' || 2 || 0 ?};
   {!
   |? _czyzakc:=DK_L.DK().N().Z='T' | (';ZT'*DK_L.DK().N().STAT_REJ)>1;
      DK_L.IL:=_wsp*DK_L.IL;
      DK_L.MG:=DK_L.DK().N().MAG;
      DK_L.M:=DK_L.DK().M;
      DK_L.Z:={? _czyzakc || 'T' || 'N' ?};
      {? DK_L.Z='T'
      || DK_L.DT:=DK_L.DK().N().DA;
         DK_L.TM:=DK_L.DK().N().TA
      ?};
      exec('reoIL2','magdok_wymiary');
      exec('uzupIDkod','magdok_palety',DK_L);
      {? ~_c || DK_L.put(1) ?};
      exec('adddelsl','magazyn_stan',DK_L.MG,DK_L.M,DK_L.LOK,DK_L.TW,DK_L.IL
       ,{? DK_L.DK().PLUS<>'N' & DK_L.Z='N' || -1 || 1 ?},DK_L.PAL
       ,{? DK_L.SCEAN<>'' || DK_L.SCEAN |? DK_L.DK().SCEAN<>'' || DK_L.DK().SCEAN || DK_L.SCEAN ?},DK_L.IL2);
::    znacznik dla palety Przychod/ Wydanie
      {? DK_L.PAL<>null
      || {? _f
         || _odd:=(1+(ref_name(DK_L.DK)+3));
            {? _odd<>DK_L.PAL().ODD || exec('zmienodd','magdok_palety',DK_L.PAL,_odd) ?}
         ?};
         PAL.cntx_psh();
         PAL.clear();
         {? PAL.seek(DK_L.PAL)
         || exec('znacznik','magdok_palety',PAL.ref());
            {? _wsp<0
            || {? _mmmp=1
               || PAL.W:={? DK_L.Z='T' || 'N' || 'W' ?};
                  PAL.P:={? DK_L.Z='T' || 'W' || 'N' ?};
                  PAL.put(1);
                  {? PAL.W='T' & (';TN'*PAL.R)>1 & PAL.AKT='T'
                   & ((~DK_L.AUTO & DK_L.ZPALET<2) | ~exec('czyilpoz','magdok_palety',PAL.ref()))
                  || exec('aktdepal','magdok_palety',0,1)
                  |? PAL.W='W' & PAL.AKT='N'
                   & ((~DK_L.AUTO & DK_L.ZPALET<2) | ~exec('czyilpoz','magdok_palety',PAL.ref()))
                  || exec('aktdepal','magdok_palety',1,1)
                  ?}
               || PAL.W:={? DK_L.Z='T' || 'T' || 'W' ?};
                  PAL.put(1);
                  {? PAL.W='T' & (';TN'*PAL.R)>1 & PAL.AKT='T'
                   & ((~DK_L.AUTO & DK_L.ZPALET<2) | ~exec('czyilpoz','magdok_palety',PAL.ref()))
                  || exec('aktdepal','magdok_palety',0,1)
                  |? PAL.W='W' & PAL.AKT='N'
                   & ((~DK_L.AUTO & DK_L.ZPALET<2) | ~exec('czyilpoz','magdok_palety',PAL.ref()))
                  || exec('aktdepal','magdok_palety',1,1)
                  ?}
               ?}
            ?}
         ?};
         {? DK_L.ZPALET=2 & DK_L.PALDO<>null
         || PAL.clear();
            {? PAL.seek(DK_L.PALDO)
            || exec('znacznik','magdok_palety',PAL.ref());
               {? _wsp<0
               || PAL.W:={? DK_L.Z='T' || 'T' || 'W' ?};
                  PAL.put(1);
                  {? PAL.W='T' & (';TN'*PAL.R)>1 & PAL.AKT='T'
                  || exec('aktdepal','magdok_palety',0,1)
                  |? PAL.W='W' & PAL.AKT='N'
                  || exec('aktdepal','magdok_palety',1,1)
                  ?}
               ?}
            ?}
         ?};
         PAL.cntx_pop()
      ?};
      DK_L.next()
   !};
:: usuniecie automatycznych pozycji
   _czyzakc:=DK_L.DK().N().Z='T' | (';ZT'*DK_L.DK().N().STAT_REJ)>1;
   {? ~_c & ~_czyzakc
   || {? ~_d
      || _pal:=tab_tmp(1,'PAL','STRING[16]','')
      || __bufpal.clear(); {? __bufpal.first() || {! |? __bufpal.del() !} ?};
         _pal:=__bufpal
      ?};
      DK_L.index('DKAUTO');
      DK_L.prefix(_a,null,1);
      {? DK_L.first()
      || {!
         |? {? DK_L.PAL<>null
            || _pal.clear();
               {? ~_pal.find_key($DK_L.PAL)
               || _pal.blank();
                  _pal.PAL:=$DK_L.PAL;
                  _pal.add(1)
               ?}
            ?};
            DK_L.del()
         !}
      ?};
::    aktualizacja znacznikow na paletach
      {? _pal.size() & _pal.first()
      || {!
         |? exec('znacznik','magdok_palety',exec('FindAndGet','#table','PAL',_pal.PAL,,,null()));
            _pal.next()
         !}
      ?};
      {? ~_d || obj_del(_pal) ?}
   ?}
?};
:: ewentualna aktualizacja SL do stanu dla danego materialu
{? ~_c | (_e & ~_first)
|| _stan:=exec('FindInSet','#table','SM','SM',DK.M,DK.N().MAG,"SM.S",,,0);
   _stan2:=exec('FindInSet','#table','SM','SM',DK.M,DK.N().MAG,"SM.S2",,,0);
   _stsl:=exec('obl_stsl','magazyn_stan',DK.N().MAG,DK.M);
   {? _stan<>_stsl[1]
   || exec('adddelsl','magazyn_stan',DK.N().MAG,DK.M,DK.N().MAG().EANL,date(0,0,0),_stan-_stsl[1],1,null,''
       ,_stan2-_stsl[2])
   ?};
   obj_del(_stsl)
?}


\obl_stsl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: sumaryczny stan wg SL-a
::   WE: _a - ref magazynu
::       _b - ref materialu
::   WY: stan na SL-u - tablica
::  OLD: \obl_stsl/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=obj_new(2); _wyn[1]:=0; _wyn[2]:=0;
SL.cntx_psh();
SL.index('MG');
SL.prefix(_a,_b);
{? SL.first() || {! |? _wyn[1]+=SL.IL; _wyn[2]+=SL.IL2; SL.next() !} ?};
SL.cntx_pop();
_wyn


\bl_ktm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: przypisanie wartosci zmiennej
::  OLD: \bl_ktm/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
HELP.KTM


\find_ma_st
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: szuka materialu w okienku SM oraz okresla rezerwacje dla okienka WER
::  OLD: \find_ma_st/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELP.WHERE='G' & var_pres('__faks_r')>0 & __faks_r<>null
|| FAKS.cntx_psh();
   FAKS.clear();
   {? FAKS.seek(__faks_r) & FAKS.MAG<>null
   || exec('obl_stan','magazyn_stan',SM.M,1,FAKS.MAG,1,,FAP.FAKS().D,-2)
   || exec('obl_stan','magazyn_stan',SM.M,1,ST.MAG)
   ?};
   FAKS.cntx_pop()
|? HELP.WHERE='G' & FAKS.MAG<>null
|| exec('obl_stan','magazyn_stan',SM.M,1,FAKS.MAG,1,,FAP.FAKS().D,-2)
||
   exec('obl_stan','magazyn_stan',SM.M,1,ST.MAG);
   {? HELP.WHERE='M' & DK.PLUS='N'
   ||
      {? DK.N().TYP().STATS_N='T' || BEER.SD+=SM.S_N ?};
      {? DK.N().TYP().STATS_O='T' || BEER.SD+=SM.S_O ?}
   ?}
?};
DISP.JMN:=SM.M().J;
{? _a
|| SM.get()
?};
exec('rekprzed','color','SM#01')


\all_kars
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja wszystkie indeksy materialowe
::  OLD: \all_kars/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
SCRP.TMG:=0;
sel_exit();
:: wg kartoteki M
{? POMOC.MGR<>null
|| M.index('MGM');
   M.prefix(POMOC.RODZ,POMOC.MGR)
|| M.index('RODZ');
   M.prefix(POMOC.RODZ)
?};
M.find_key(D_HELP.M);
exec('slo_m_ok','material',POMOC.RODZ,0);
{? M.select(,1,10)
|| POMOC.MGR:=M.MGR; D_HELP.M:=M.KTM; _wyn:=M.KTM
|| _wyn:=''
?};
_wyn


\obl_stm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: oblicza stan w magazynie
::  OLD: \obl_stm/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=exec('FindAndGet','#table',FUX.TFUX,FUX.FUX,,,null());
exec('FindInSet','#table','SM','SM',_ref,ST.MAG,"SM.SD",,,0)


\pweanx_s
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: przed wyswietleniem pola EANX.S
::  OLD: \pweanx_s/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? BEER.NAWIG='S' | BEER.NAWIG='M'
|| 1
|? ST.MAG().PAL='T'
|| exec('findfnv','#color')
|| 1
?}


\DELSTAN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: usuwa / modyfikuje zapisy w tabeli stanow
::   WE:  _a - magazyn
::        _b - indeks materialowy
::        _c - data dostawy
::        _d - cena
::        _e - dokument przychodowy
::        _f - RDK
::        _g - NDK
::   WY: funkcja zwraca 1 jesli udalo sie zaktualizowac stany w przeciwnym przypadku zwraca 0
::  OLD: \FD.DELSTAN/funkcje.fml
::       \delstan/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
{?_>=7 || {? type_of(_g)<>2 || _g:=DK.name ?} || _g:=DK.name ?};

_wynik:=1;
SM.cntx_psh();
SC.cntx_psh();
{? _g<>''
|| SM.use('stm__'+(5-_g));
   SC.use('stc__'+(5-_g))
?};

_oddn:=1+(SM.name()+3);
_odda:=ST.ODDZ;
_rozn:=_oddn<>_odda;

_tmg:=1+DK.N().MAG().TYP;
_kor:=DK.N().TYP().DK='T';

{? (DK.N().TYP().DN='T' | (DK.N().INN='T' & DK.N().TYP().INW='I' & DK.N().TYP().P='T'))
& (_f <> #DK.ref() | _g <> DK.name())
||
   _rdk:=DK.RDK;_ndk:=DK.NDK;
   {? _rdk<>0 & _ndk<>''
   ||
      ND.cntx_psh();
      DK.cntx_psh();
      DK.use(_ndk);
      ND.use('nagdo'+(5-_ndk));
      DK.prefix();
      ND.prefix();
      {? DK.seek(_rdk,_ndk)
      || ND.seek(#DK.N,'nagdo'+(5-_ndk));
         _c:=DK.N().D
      ?};
      DK.cntx_pop();
      ND.cntx_pop()
   ?}
?};
{? DK.N().Z<>'R' || DK.Z:='N' || DK.Z:='R' ?};DK.put();

_jmg:=DK.M().J2<>null();

SC.index('SN');
SC.prefix(_a,_b,_f,_g);

{? SC.first()
||
   {? _e='T' & SC.S<DK.IL
   ||
      _msg:='Materiału %1 nie ma na stanie.'@[DK.M().KTM];
      {? var_pres('__kom')>100
      || exec('add_kom','#message',_msg,4,,__lp+=1)
      || FUN.info(_msg+'\nWycofanie akceptacji niemożliwe.'@)
      ?};
      _wynik:=0
   || {? _e='N'
      || SC.S+=DK.IL;
         {? _jmg || SC.S2+=DK.IL2 ?}
      || SC.S-=DK.IL;
         {? _jmg || SC.S2-=DK.IL2 ?}
      ?};
      {? SC.S=0 & SC.S2=0 || SC.A:='N' || SC.A:='T' ?};
      _wynik:=SC.put();
      {? SC.S=0 & SC.S2=0 & _wynik
      || DK.cntx_psh();
         DK.index('DOST3');
         DK.prefix(SC.MAG,SC.M,SC.RDK, SC.NDK ,'T');
         {? SC.count()=0
          & (~DK.first
           | (SC.RDK=#DK.ref() & SC.NDK=DK.name() & DK.size()=1)
           | (SC.RDK=DK.RDK & SC.NDK=DK.NDK & DK.size()=1))
         ||
            SC.del()
         ?};
         DK.cntx_pop()
      ?}
   ?}
||
   _msg:='Błąd w danych: nie ma dostawy dla materiału %1.'@[DK.M().KTM];
   {? var_pres('__kom')>100
   || exec('add_kom','#message',_msg,4,,__lp+=1)
   || FUN.info(_msg+'\nWycofanie akceptacji niemożliwe.'@)
   ?};
   _wynik:=0
?};

{? _wynik=1
||
   SM.index('SM');
   SM.prefix(_a,_b);
   {? SM.first()
   || {? _e='T' & SM.S<DK.IL
      ||
         _msg:='Materiału %1 nie ma na stanie.'@[DK.M().KTM];
         {? var_pres('__kom')>100
         || exec('add_kom','#message',_msg,4,,__lp+=1)
         || FUN.info(_msg+'\nWycofanie akceptacji niemożliwe.'@)
         ?};
         _wynik:=0
      || {? _e='N'
         || SM.S+=DK.IL;
            {? _jmg || SM.S2+=DK.IL2 ?};
            {? ~_kor
            || {? DK.STATS().KIND='O'
               || SM.S_O+=DK.IL;
                  {? _jmg || SM.S_O2+=DK.IL2 ?}
               |? DK.STATS().KIND='N'
               || SM.S_N+=DK.IL;
                  {? _jmg || SM.S_N2+=DK.IL2 ?}
               ?}
            ?}
         || SM.S-=DK.IL;
            {? _jmg || SM.S2-=DK.IL2 ?};
            {? ~_kor
            || {? DK.STATS().KIND='O'
               || SM.S_O-=DK.IL;
                  {? _jmg || SM.S_O2-=DK.IL2 ?}
               |? DK.STATS().KIND='N'
               || SM.S_N-=DK.IL;
                  {? _jmg || SM.S_N2-=DK.IL2 ?}
               ?}
            ?}
         ?}
      ?};
      SM.J2:={? _jmg || SM.M().J2 || SM.M().J ?};
      SM.SM2:={? _jmg || 'T' || 'N' ?};
      _wynik:=SM.put()
   ||
      _msg:='Błąd w danych: nie ma rekordu stanu dla materiału %1.'@[DK.M().KTM];
      {? var_pres('__kom')>100
      || exec('add_kom','#message',_msg,4,,__lp+=1)
      || FUN.info(_msg+'\nWycofanie akceptacji niemożliwe.'@)
      ?};
      _wynik:=0
   ?}
?};

:: uzupelnianie zbiorczej tabeli stanow
{? _wynik=1
|| ST.ODDZ:=ST.ODDZ_KOD:=_oddn;
   exec('stan_zb','magazyn_stan',_a,_b,{? _e='T' || DK.IL*-1 || DK.IL ?},{? _e='T' || DK.WAR*-1 || DK.WAR ?}
     ,DK.N().AR,{? _jmg || {? _e='T' || DK.IL2*-1 || DK.IL2 ?} || 0 ?});
   {? _rozn || exec('obl_stan','magazyn_stan',_b,1,_a) ?};
   ST.ODDZ:=ST.ODDZ_KOD:=_odda
?};
SC.cntx_pop();
SM.cntx_pop();
_wynik


\wyda_sce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.41]
:: OPIS: oblicza ilosc w wydaniu dla podanego identyfikatora
::   WE: _a - material
::       _b - kod identyfikatora
::       _c - magazyn
::       _d - ref SQL pozycji do pominiecia lub '' (uwaga - pomijamy albo pozycję albo pozycje wg nagłówka dokumentu)
::   WY: ilosc w wydaniu
::  OLD: \wyda_sce/pommag.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
ND.cntx_psh();
DK.cntx_psh();
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
|| {!
   |? ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));
      DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
      DK.index('WYDSCEAN');
      DK.prefix(_a,'N','N',_b,_b,_c);
      {? DK.first()
      || {!
         |? _wyn+={? _d='' | {? (5+_d)='nagdo' || $DK.N<>_d || $DK.ref<>_d  ?} || DK.IL || 0 ?};
            DK.next()
         !}
      ?};
      OKR.next()
   !}
?};
ND.cntx_pop();
DK.cntx_pop();
OKR.cntx_pop();
_wyn


\info_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: szczegoly dotyczace indeksu materialowego
::   WE: _a - typ przedstawienia informacji
::       [_b] - indeks materialowy
::  OLD: \info_zam/zkd.fml
::----------------------------------------------------------------------------------------------------------------------
{? ST.ODDZ=''
|| FUN.info('Należy wskazać oddział w parametrach pracy.'@);
   return()
?};

{? _>=2 || {? type_of(_b)<>7 || _b:=M.ref() ?} || _b:=M.ref() ?};

VAR_DEL.delete('INFZAM');

INFZAM:=tab_tmp(4,'TREE','TREE_REF',''
         ,'LP','REAL',''
         ,'NR','INTEGER',''
         ,'SZ','INTEGER',''
         ,'OP','STRING[255]',''
         ,'IL','REAL',''
         ,'REF','STRING[20]',''
         ,'ICO','STRING[16]',''
         ,'IL2','REAL','');
exec('pob_dane','magazyn_stan',_b,_a);

_dokl:=exec('FindAndGet','#table',M,$_b,,"DOKL",0);
_jm:=exec('FindAndGet','#table',M,$_b,,"J().KOD",'');
_jmg:=exec('FindAndGet','#table',M,$_b,,"{? J2<>null() || J2().KOD || '' ?}",'');
_dok2:={? _jmg<>''
       || exec('jaka_dok_mjm','jm',_b
           ,exec('FindAndGet','#table',M,$_b,,"J2",'')
           ,exec('FindAndGet','#table',M,$_b,,"J",''))
       || _dokl
       ?};

_win_sel:=INFZAM.mk_sel('Szczegóły: '@,'P',,'info_zam_acr_se',,,{? INFZAM.size()>33 || 33 || INFZAM.size() ?},1);
INFZAM.win_fld(_win_sel,,'OP',,,80,,1,'Informacje'@,,,,,,,,'mobile_visible=1,mobile_header=1');
INFZAM.win_fld(_win_sel,,'IL',,,15,_dokl,1,'Ilość [%1]'@[_jm],,,,,,,,'mobile_visible=1');
{? _jmg<>'' || INFZAM.win_fld(_win_sel,,'IL2',,,15,_dok2,1,'Ilość [%1]'@[_jmg]) ?};
INFZAM.win_act(_win_sel,,'Formuła','&Zwiń/rozwiń'@@,,,"exec('zwin_rozwin','#tree')",,1,,,,'Z');
INFZAM.win_act(_win_sel,0,'Rekord',,,,"exec('info_rek','magazyn_stan')",,1);
INFZAM.win_act(_win_sel,0,'Wyświetl',,,,"exec('disp_rek','magazyn_stan')",,1);
INFZAM.tr_fml(_win_sel,,"_a");
INFZAM.win_fml(_win_sel,,'OP',,'ICON_BEFORE',"
         {? INFZAM.ICO<>''                 || INFZAM.ICO
         |? INFZAM.TREE=0                  || 'xwin16.png:7'
         |? INFZAM.TREE<>0 & INFZAM.NR<2   || 'xwin16.png:76'
                                           || ''
         ?}
     ");
INFZAM.win_sel(_win_sel);
INFZAM.select()


\pob_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr], MZ [2008]
:: OPIS: podobna formula do oblicz stan
::   WE: _a - indeks materialowy
::       _b - wg danego magazynu 1 wg operatora zam.sprz.  2 wg wszystkich uprawnionych magazynow 3 wg mag.oddzialu 5
::            wg operatora zamowien wewnetrznych 9
::       _c - wg magazynu
::  OLD: \pob_dane/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
_wmg:={? _=2 || _wmg:=ST.MAG || _c ?};

MG.cntx_psh();
{? _b=1
|| exec('obl_stan','magazyn_stan',_a,1,_wmg,,,,,,,,,1)
|| exec('obl_stan','magazyn_stan',_a,_b,,,,,,,,,,1)
?};
MG.cntx_pop();
~~


\info_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: podswietlenie rekordow dla szczegolow
::   WY: kolor w zależności od opcji
::  OLD: \info_rek/zkd.fml
::----------------------------------------------------------------------------------------------------------------------
''


\disp_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: wyswietla podglad dla pozycji dokumentu
::  OLD: \disp_rek/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
{? INFZAM.TREE & INFZAM.LP=2 & INFZAM.REF<>''
|| {? (1+INFZAM.REF)='d'
   || ND.cntx_psh();
      DK.cntx_psh();
      ND.use('nagdo'+(form(8+INFZAM.REF)+3));
      DK.use(form(8+INFZAM.REF));
      DK.clear();
      {? DK.seek(INFZAM.REF) || exec('disp_dkm','magdok_poz') ?};
      ND.cntx_pop();
      DK.cntx_pop()
   |? (1+INFZAM.REF)='f'
   || FAKS.cntx_psh();
      FAP.cntx_psh();
      FAKS.use('faktu'+(form(8+INFZAM.REF)+3));
      FAP.use(form(8+INFZAM.REF));
      FAP.clear();
      {? FAP.seek(INFZAM.REF) || exec('disp_fap','faktury_poz') ?};
      FAKS.cntx_pop();
      FAP.cntx_pop()
   ?}
|? INFZAM.TREE & INFZAM.LP=3 & INFZAM.REF<>''
|| REZ.cntx_psh();
   REZ.clear();
   {? REZ.seek(INFZAM.REF)
   || typ:=2;
      REZ.win_patt('TYM');
      REZ.win_edit('TYM');
      REZ.display()
   ?};
   REZ.cntx_pop()
|? INFZAM.TREE
 & (INFZAM.LP=4.1 | INFZAM.LP=4.2 | INFZAM.LP=4.3 | INFZAM.LP=5.1 | INFZAM.LP=5.2 | INFZAM.LP=6 | INFZAM.LP=8)
 & INFZAM.REF<>''
|| ZK_N.cntx_psh();
   ZK_P.cntx_psh();
   ZK_N.use('zknag'+(form(8+INFZAM.REF)+3));
   ZK_P.use(form(8+INFZAM.REF));
   ZK_P.clear();
   {? ZK_P.seek(INFZAM.REF) || exec('wys_pozy','zamsiw_poz') ?};
   ZK_N.cntx_pop();
   ZK_P.cntx_pop()
|? INFZAM.TREE & (INFZAM.LP=9.1 | INFZAM.LP=9.2) & INFZAM.REF<>''
|| _ms:=(form(8+INFZAM.REF)+3);
   _ptw:='';
   _sql:=INFZAM.REF;
   {? (5+INFZAM.REF)='zdptp'
   || _ptw:=INFZAM.REF;
      _sql:=exec('FindAndGet','#table',ZDP_POZ,_sql,,"ZD_POZ",'')
   ?};
   {? _sql<>''
   || ZD_NAG.cntx_psh();
      ZD_POZ.cntx_psh();
      ZDP_NAG.cntx_psh();
      ZDP_POZ.cntx_psh();
      ZD_NAG.use('zdnag'+_ms);
      ZD_POZ.use('zdpoz'+_ms);
      ZDP_NAG.use('zdptn'+_ms);
      ZDP_POZ.use('zdptp'+_ms);
      {? _ptw<>''
      || ZDP_POZ.prefix();
         {? ZDP_POZ.seek(_ptw) || params_exec('zdpp_rek','zamdst_ptw') ?}
      ?};
      ZD_POZ.clear();
      {? ZD_POZ.seek(_sql)
      || {? _ptw<>''
         || exec('zdpo_wys','zamdst_poz','INFPOTW')
         || exec('zdpo_wys','zamdst_poz')
         ?}
      ?};
      ZD_NAG.cntx_pop();
      ZD_POZ.cntx_pop();
      ZDP_NAG.cntx_pop();
      ZDP_POZ.cntx_pop()
   || ''
   ?}
|| ''
?}


\sm_dokar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wyswietla dokumenty z okienka stanow - dokumenty z biezacego miesiaca
::       MZ nowa kolejnosc dla korekt (pole DK.POW)
::  OLD: \sm_dokar/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
_atrmjs:=ATR.MJS;
ATR.MJS:='DK';

DISP.SMIL:=DISP.SMW:=0;
DISP.SPIL:=DISP.SPW:=0;
DISP.SRIL:=DISP.SRW:=0;
ND.cntx_psh();
DK.cntx_psh();
DK_L.cntx_psh();

ND.cntx_psh();
DK.cntx_psh();
DK_L.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |?
      DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
      ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));
      DK_L.use('doklk'+ST.ODDZ+($OKR.ROK+2));
      DK.index('DK_SM');
      DK.prefix(ST.MAG,SM.M,'T');
      {? DK.first()
      || {!
         |? {? DK.N().D<date(ST.AR,ST.AM,1)
            ||
               {? DK.PLUS='T'
               ||
                  DISP.SMIL+=DK.IL;
                  DISP.SMW+=DK.WAR
               |? DK.PLUS='N'
               ||
                  DISP.SMIL-=DK.IL;
                  DISP.SMW-=DK.WAR
               ?};
               DK.next()
            ?}
         !}
      ?};
      OKR.next()
   !}
?};
DK.cntx_pop();
ND.cntx_pop();
DK_L.cntx_pop();

DISP.MG_SYM:=ST.MAG().SYM;
DK.index('DK_SM');
DK.prefix(ST.MAG,SM.M,'T',ST.AR,ST.AM);
_war:=0;_il:=0;
{? DK.first()
|| {!
   |?
      {? DK.PLUS='T' || _war+=DK.WAR; _il+=DK.IL; DISP.SPIL+=DK.IL; DISP.SPW+=DK.WAR
      |? DK.PLUS='N' || _war-=DK.WAR; _il-=DK.IL; DISP.SRIL+=DK.IL; DISP.SRW+=DK.WAR
      ?};
      DK.next()
   !}
?};

DISP.SKIL:=DISP.SMIL+_il;
DISP.SKW:=DISP.SMW+_war;

DK.win_sel('WER_SMM');
DK.win_fml('WER_SMM',ND,'SYM',,'ICON_BEFORE',exec('nd_sym_ib','magdok_nag'));
exec('dk_p_ib','magdok_poz','WER_SMM');
DK.fld_fml('C','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $DK.M().DOKL_C || $ST.DOKL_C ?}+','+{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
DK.fld_fml('WAR','DISPLAY_FORMAT',"{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
DISP.fld_fml('SMW','DISPLAY_FORMAT',"{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
DISP.fld_fml('SKW','DISPLAY_FORMAT',"{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
DISP.fld_fml('SPW','DISPLAY_FORMAT',"{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
DISP.fld_fml('SRW','DISPLAY_FORMAT',"{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
DK.win_edit('WREDZ');
DK.first();
DK.select();
DK.fld_fml('C','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $DK.M().DOKL_C || $ST.DOKL_C ?}+','+'empty=0'");
DK.fld_fml('WAR','DISPLAY_FORMAT',"'empty=0'");
DISP.fld_fml('SMW','DISPLAY_FORMAT',"'empty=0'");
DISP.fld_fml('SKW','DISPLAY_FORMAT',"'empty=0'");
DISP.fld_fml('SPW','DISPLAY_FORMAT',"'empty=0'");
DISP.fld_fml('SRW','DISPLAY_FORMAT',"'empty=0'");
DK.cntx_pop();
ND.cntx_pop();
DK_L.cntx_pop();
ATR.MJS:=_atrmjs;
''


\sm_dost
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JF [2006]
:: OPIS: akcja dostawy z okienka stanow
::   WE: [_a] - ref magazynu, domyślnie ST.MAG
::       [_b] - ref indeksu materiałowego, domyślnie SM.M
::  OLD: \sm_dost/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
_mag:={? var_pres('_a')=type_of(null()) & _a<>null() || _a || ST.MAG ?};
_mat:={? var_pres('_b')=type_of(null()) & _b<>null() || _b || SM.M ?};

_beermg:=BEER.MG;
_beerm:=BEER.M;
BEER.MG:=_mag;
BEER.M:=_mat;

_actdkc:=DK_C.actions('WER','DuP:d');

_sel:=SC.grp_make('Dostawy w podziale na lata'@,,'jkigakischyvydl');

_atrmjs:=ATR.MJS;
ATR.MJS:='SC';
_panel:=0;
BO.fld_fml('CEN','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $SC.M().DOKL_C || $ST.DOKL_C ?}+','+{? BEER.MG().IL='T' || 'empty=1' || 'empty=0' ?}");

SC.cntx_psh();

VAR_DEL.delete('czy_dsp','bie_r','__nrz');
__nrz:=0;
_il:=0;
czy_dsp:=0;
OKR.cntx_psh();
{? OKR.last()
||
   {!
   |?
      {? OKR.FIRMA=REF.FIRMA & OKR.MC=1
      ||
         SC.use('stc__'+ST.ODDZ+($OKR.ROK+2));
         SC.index('SC');
         SC.prefix(BEER.MG,BEER.M);
         {? SC.first()
         ||
            _il+=1;
            _form:=$('__nrz:=(__nrz+1)%*2;czy_dsp:=1;bie_r:='+$OKR.ROK+';SC.use(\'stc__\'+ST.ODDZ+($bie_r+2));SC.index(\'SCDT\');
            SC.prefix(BEER.MG,BEER.M);SC.first()');
            {? (1+BEER.MG().TYP)='D' & ATR.CZY_ATR & exec('get','#params',540101,2,null())='T'
            || _badp:=$('czy_dsp:=1;bie_r:='+$OKR.ROK+';DK_HS.use(\'dokhs\'+ST.ODDZ+($bie_r+2))')
            ?};
            {? SC.MAG().SKL='T'
            ||
               _far:="
                  params_set(params_get());
                  {? czy_dsp=1 || grp_disp(SC,'WER',0,1) ?};czy_dsp:=0
               ";
               SC.grp_sel(_sel,SC,'WER',$OKR.ROK,_far,,,,_form)
            |? (1+BEER.MG().TYP)='D' & ATR.CZY_ATR
            || _panel:=1;
               _fb:="
                  params_set(params_get());
                  {? czy_dsp=1 || grp_disp(SC,'WER',0,1) ?};czy_dsp:=0;ATR.MJS:='SC';
                  exec('odsw_sc','magazyn_stan',1)
               ";
               SC.grp_sel(_sel,SC,'WER',$OKR.ROK,_fb,,,,_form,,,,'maximized_with_title')
            ||
               SC.grp_sel(_sel,SC,'WER',$OKR.ROK,"{? czy_dsp=1 || grp_disp(SC,'WER',0,1) ?};czy_dsp:=0",,,,_form)
            ?}
         ?}
      ?};
      OKR.prev() & _il<16
   !}
?};
OKR.cntx_pop();
{? _il=0
||
   _form:=$('czy_dsp:=1;bie_r:='+$ST.AR+';SC.use(\'stc__\'+ST.ODDZ+($bie_r+2));SC.index(\'SCDT\');
   SC.prefix(BEER.MG,BEER.M);SC.first()');
   {? SC.MAG().SKL='T'
   ||
      SC.grp_sel(_sel,SC,'WER',$OKR.ROK,"{? czy_dsp=1 || grp_disp(SC,'WER',0,1) ?};czy_dsp:=0",,,,_form)
   |? (1+BEER.MG().TYP)='D' & ATR.CZY_ATR
   || _panel:=1;
      SC.grp_sel(_sel,SC,'WER',$OKR.ROK,"{? czy_dsp=1 || grp_disp(SC,'WER',0,1) ?};czy_dsp:=0",,,,_form)
   ||
      SC.grp_sel(_sel,SC,'WER',$OKR.ROK,"{? czy_dsp=1 || grp_disp(SC,'WER',0,1) ?};czy_dsp:=0",,,,_form)
   ?}
?};

{? _panel
|| SC.grp_splt(_sel,'','vertical','tab1','0, 67%');
   SC.grp_edit(_sel,DK_C,'DISPR',,"ATR.MJS:='SC';exec('odsw_sc','magazyn_stan',1)",,,,,'maximized_with_title');

   {? exec('get','#params',540101,2,null())='T'
   || SC.grp_splt(_sel,'tab1','horizontal','tab2');
      SC.grp_sel(_sel,BADP,'WERDKR',,"{? czy_dsp=1 || grp_disp(BADP,'WERDKR',0,1) ?};czy_dsp:=0"
       ,,,1,,,,,'maximized_with_title')
   ?}
?};

_SCact:='F(';
{? ~exec('chk_role','#b__box',OPERATOR.USER,'LMG_BAD_PBAD')
 | ~exec('chk_role','#b__box',OPERATOR.USER,'LMG_BAD_DWYK') || _SCact+='B' ?};
{? ~exec('chk_role','#b__box',OPERATOR.USER,'LMG_BAD_EAKC') || _SCact+='M' ?};
_SCact+=')';

_tmg:=1+BEER.MG().TYP;
_only:=BEER.MG().IL='T';
SC.actions('WER',{? _tmg<>'D' || 'F' || '' ?}+{? _only || 'Y' || '' ?}+'C:C','D');
{? 'D'*_tmg=1 || SC.actions('WER',_SCact+{? _only || 'Y' || '' ?}+'O:O','D',1) ?};

params_set('PANEL',_panel);
exec('disp_dost_ib','magazyn_stan','WER');
SC.win_sel(_sel);

SC.select();
SC.actions('WER',,'');
SC.cntx_pop();
VAR_DEL.delete('czy_dsp','bie_r','__nrz');
DK_C.actions('WER',_actdkc);
ATR.MJS:=_atrmjs;
BO.fld_fml('CEN','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $SC.M().DOKL_C || $ST.DOKL_C ?}+','+'empty=0'");
BEER.MG:=_beermg;
BEER.M:=_beerm;
''


\odsw_sc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JSz [12.30]
:: OPIS: Odświeżaenie okien w oknie grupowym dla okna tabeli __sc
::   WE: [_a] 0(domyslnie) dla __sc 1-dla SC
::  OLD: \odsw_sc/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};

_czybad:=exec('chk_role','#b__box',OPERATOR.USER,'LMG_BAD_PBAD') & exec('get','#params',540101,2,null())='T';
DK_C.clear();
{? {? ~_a || DK_C.seek(__sc.DK_C) || DK_C.seek(SC.DK_C) ?}
|| DK_C.M_ATR().SYM;
   {? DK_C.M_ATR<>null() || M_ATR.get() ?};
   _symbol:=DK_C.SYM;
   DK_C.index('SYM');
   DK_C.prefix(_symbol,_symbol)
|| DK_C.index('M_ATR');
   DK_C.prefix(null,'','','');
   {! _i..10 |! ($('M_ATR.SL_'+form(_i,-2,0,'99')))():=null !}
?};
{? ATR.CZY_ATR & _a<>2 || grp_edisp(DK_C,'DISPR',0,1) ?};
{? _czybad
|| {? ~_a & __winbad
   || exec('badp_wmag','magazyn_stan',FAKS.DW,BB.sqlint(__sc.REF),8+__sc.REF)
   || exec('badp_wmag','magazyn_stan',date(),#SC.ref(),SC.name(),_a)
   ?}
?}


\badp_wmag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JSz [12.30]
:: OPIS: Odświeża okienko z listą parametrow badania dla tabeli
::   WE: _a - data dokumentu
::       _b - ref SC
::       _c - name SC
::       [_d] 0(domyslnie) dla __sc 1-dla SC -1-select
::  OLD: \badp_wmag/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>4 || _a:=date() ?} || _a:=date() ?};
{? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};

_czybadp:=(4+cur_tab(1,1).name())='badp';

{? (_d | exec('get_par','#parametr',141)='T' | ATR.CZY_ATR) & _b & 3+_c='stc'
|| _nul:=0;
   _rdk:=0;
   _ndk:='';
   _badp:=BADP.ref();
   SC.cntx_psh();
   SC.use(_c);
   SC.clear();
   {? SC.seek(_b,)
   || _rdk:=SC.RDK;
      _ndk:=SC.NDK
   ?};
   SC.cntx_pop();
   {? _rdk<>0 & _ndk<>''
   || _dkhs:=exec('zwrDK_HS','statexam',_ndk,_rdk,_a);
      {? _dkhs<>null
      || DK_HS.cntx_psh();
         {? DK_HS.name()+3<>_ndk+3
         || DK_HS.use((DK_HS.name()-3)+(_ndk+3))
         ?};

         DK_HS.clear();
         {? DK_HS.seek(_dkhs)
         || {? DK_HS.BADH<>null
            || BADP.prefix(DK_HS.BADH);
               {? ~_czybadp || BADP.seek(_badp) ?}
            || _nul:=1
            ?}
         ?};
         DK_HS.cntx_pop()
      || _nul:=1
      ?}
   || _nul:=1
   ?};
   {? _nul
   || BADP.index('BADHBPAR');
      BADP.prefix(null)
   ?}
|| BADP.index('BADHBPAR');
   BADP.prefix(null)
?};
{? ~_czybadp
|| {? _d=-1
   || _win:=BADP.win_sel('?');
      _act:=BADP.actions('WER','dupAM:dA');
      BADP.win_sel('WER');
      BADP.select();
      BADP.win_sel(_win);
      BADP.actions('WER',_act)
   |? ~_d
   || grp_disp(BADP,'WER_SS')
   |? var_pres('__czywyb')>0
   || grp_disp(BADP,'WERDKP')
   || grp_disp(BADP,'WERDKR')
   ?}
?}


\stan_sl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: stany wg wymiarow
::  OLD: \stan_sl/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
exec('stanyl','magazyn_stan',SM.M,SM.MAG)


\stanyl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: stany wg lokalizacji na magazynie
::   WE: _a - M.ref
::       _b - MG.ref
::  OLD: \stanyl/defin.fml
::----------------------------------------------------------------------------------------------------------------------
EANX.SLSUM:=0;
VAR_DEL.delete('__onemg');

{? var_pres('_a')<>type_of(null) || _a:=null ?};
{? var_pres('_b')<>type_of(null) || _b:=null ?};

__onemg:={? _b=null || 0 || 1 ?};

{? ST.MAG<>null & {? _b=null || 1 || ST.MAG().SL='T' ?}
||
   _czy_pal:={? ST.MAG().PAL='T' || -1 || 0 ?};
   _zm:="
      VAR_DEL.delete('__wer1','__wer2','__wer3','__wer4','__werg','__ktm','__eanl','__prfx','__prfxr','__dokl');
      VAR_DEL.delete('__prmgr','__slr','__pal')
   "; _zm();
   __prfxr:=_a; __prfx:='';
   __prmgr:=_b;
   {? __prfxr<>null
   || M.cntx_psh; M.prefix; {? M.seek(__prfxr) || __prfx:=M.KTM ?}; M.cntx_pop
   ?};
:: dokladnosc ilosci
   __dokl:=ST.DOKL;
:: stany SL
   __wer1:=SL.mk_sel('Stany'@,'P',,'vkkeopladkowerj',,,,,'U'); _szer:=2;
   {? __prmgr=null
   || _width:=8; _szer+=_width+1; SL.win_fld(__wer1,,'MG','SYM',,_width,,,'Magazyn'@)
   ?};
   _width:=20; _szer+=_width+1; SL.win_fld(__wer1,,'M','KTM',,_width,,,'Indeks'@);
   _width:=20; _szer+=_width+1; SL.win_fld(__wer1,,'M','N',,_width,,,'Nazwa'@);
   _width:=20; _szer+=_width+1; SL.win_fld(__wer1,,'EANL','KOD',,_width,,,'Lokalizacja'@);
   _width:=10; _szer+=_width+1; SL.win_fld(__wer1,,'TW',,,_width,,,'Termin ważności'@);
   {? _czy_pal<0
   || _width:=20; _szer+=_width+1; SL.win_fld(__wer1,,'PAL','KODK',,_width,,,'Paleta'@)
   ?};
   _width:=10; _szer+=_width+1; SL.win_fld(__wer1,,'IL',,,_width,__dokl,,'Ilość'@);
   _width:=5; _szer+=_width+1; SL.win_fld(__wer1,,'JM','KOD',,_width,__dokl,,'jm'@);
   {? (2+menu_pth())<>'MO'
   || SL.win_act(__wer1,,'Formuła','&Operacje'@@,,,"exec('oper','magdok_wspolne')",,1,,,,'O');
      {? ~__onemg | (__onemg & ST.MAG().PAL<>'T' & (1+ST.MAG().TYP)='D')
      || SL.win_act(__wer1,,'Formuła','&Identyfikacja dostaw'@@,,,"exec('infiddst','magazyn_stan')",,,,,,'I')
      ?};
      {? _czy_pal<0
      || SL.win_act(__wer1,,'Formuła','&Paleta'@@,,,"exec('infstpal','magazyn_stan')",,,,,,'P')
      ?}
   ?};
   SL.win_act(__wer1,,'Kolejność');

:: stany na lokalizacji
   __wer2:=SL.mk_sel('Stan lokalizacji: '@,,,'#dkakggckxcmmgv',,,,,'U');
   SL.win_fld(__wer2,,'M','KTM',,16,,,'Indeks'@,,'Indeks materiałowy'@);
   SL.win_fld(__wer2,,'IL',,,10,__dokl,,'Ilość'@,,'Ilość'@);
   SL.win_fld(__wer2,,'JM','KOD',,3,,,'jm'@,,'Jednostka miary'@);
   SL.win_fld(__wer2,,'TW',,,-14,,,'Termin ważności'@,,'Termin ważności'@);

:: stany materialu
   __wer3:='STN';

:: stany na palecie
   __wer4:=SL.mk_sel('Stan palety: '@,,,'#dkakggckxcmmvv',,,,,'U');
   SL.win_fld(__wer4,,'M','KTM',,16,,,'Indeks'@,,'Indeks materiałowy'@);
   SL.win_fld(__wer4,,'IL',,,10,__dokl,,'Ilość'@,,'Ilość'@);
   SL.win_fld(__wer4,,'JM','KOD',,3,,,'jm'@,,'Jednostka miary'@);
   SL.win_fld(__wer4,,'TW',,,-10,,,'Termin ważności'@,,'Termin ważności'@);

:: stany magazynowe wg wymiarow
   _fb:="
      __ktm:=__eanl:=__pal:='';
      __slr:=null
   ";
   __werg:=SL.grp_make('Stany magazynowe wg wymiarów'@,_fb,'doodpdpcvflkadg');
:: stany SL
   {? _czy_pal>0
   || _fr:="
         __f_act:={? SL.f_active() || {? SL.sel_size() || 1 || ~SL.f_get() ?} || 0 ?};
         __ktm:=' '+SL.M().KTM;
         __eanl:=SL.EANL().KOD;
         __pal:=SL.PAL().KODK;
         SL.cntx_psh; grp_disp(SL,__wer2,0); SL.win_sel(__wer2); SL.hdr_sel; SL.hdr_sel(__eanl); SL.cntx_pop;
         SL.cntx_psh; grp_disp(SL,__wer3,0); SL.win_sel(__wer3); SL.hdr_sel; SL.hdr_sel(__ktm); SL.cntx_pop;
         SL.cntx_psh; grp_disp(SL,__wer4,0); SL.win_sel(__wer4); SL.hdr_sel; SL.hdr_sel(__pal); SL.cntx_pop;
         {? cur_win(1)<>__wer1
         || SL.index('AUTS');
            {? __prfxr=null & __prmgr=null
            || SL.prefix
            |? __prfxr=null & __prmgr<>null
            || SL.prefix(__prmgr)
            |? __prfxr<>null & __prmgr=null
            || SL.index('M'); SL.prefix(__prfxr)
            || SL.prefix(__prmgr,__prfxr)
            ?}
         ?}
      "
   || _fr:="
         __f_act:={? SL.f_active() || {? SL.sel_size() || 1 || ~SL.f_get() ?} || 0 ?};
         __ktm:=' '+SL.M().KTM;
         __eanl:=SL.EANL().KOD;
         SL.cntx_psh; grp_disp(SL,__wer2,0); SL.win_sel(__wer2); SL.hdr_sel; SL.hdr_sel(__eanl); SL.cntx_pop;
         SL.cntx_psh; grp_disp(SL,__wer3,0); SL.win_sel(__wer3); SL.hdr_sel; SL.hdr_sel(__ktm); SL.cntx_pop;
         {? cur_win(1)<>__wer1
         || SL.index('AUTS');
            {? __prfxr=null & __prmgr=null
            || SL.prefix
            |? __prfxr=null & __prmgr<>null
            || SL.prefix(__prmgr)
            |? __prfxr<>null & __prmgr=null
            || SL.index('M'); SL.prefix(__prfxr)
            || SL.prefix(__prmgr,__prfxr)
            ?}
         ?}
      "
   ?};
   _fb:="
      __f_act:={? SL.f_active()
               || SL.f_rfresh();
                  SL.f_seek(__slr); {? SL.sel_size() || 1 || ~SL.f_get() ?}
               || 0
               ?};
      SL.index('AUTS');
      {? __prfxr=null & __prmgr=null
      || SL.prefix
      |? __prfxr=null & __prmgr<>null
      || SL.prefix(__prmgr)
      |? __prfxr<>null & __prmgr=null
      || SL.index('M'); SL.prefix(__prfxr)
      || SL.prefix(__prmgr,__prfxr)
      ?}
   ";
   _fa:="";
   SL.grp_sel(__werg,,__wer1,,_fr,0,0,22,_fb,_fa,,,'maximized_with_title');
   SL.grp_splt(__werg,,'vertical','tab1','0, 67%');

:: stany na lokalizacji
   _fr:="
      __ktm:=' '+SL.M().KTM;
      __slr:=SL.ref;
      SL.cntx_psh; grp_disp(SL,__wer1,0); SL.cntx_pop;
      SL.cntx_psh; grp_disp(SL,__wer3,0); SL.win_sel(__wer3); SL.hdr_sel; SL.hdr_sel(__ktm); SL.cntx_pop
   ";
   _fb:="
      SL.cntx_psh();
      SL.index('EANL');
      {? __f_act
      ||
         SL.prefix(null)
      ||
         {? __prfx=''
         || SL.prefix(SL.MG,SL.EANL)
         || SL.prefix(SL.MG,SL.EANL,__prfx)
         ?}
      ?}
   ";
   _fa:="SL.cntx_pop()";

   SL.grp_sel(__werg,,__wer2,,_fr,_szer,0,10,_fb,_fa,,,'maximized_with_title');
   SL.grp_splt(__werg,'tab1','horizontal','tab2');

:: stan na palecie
   {? _czy_pal>0
   || _fr:="
         __ktm:=' '+SL.M().KTM;
         __slr:=SL.ref;
         __eanl:=SL.EANL().KOD;
         __pal:=SL.PAL().KODK;
         SL.cntx_psh; grp_disp(SL,__wer1,0); SL.cntx_pop;
         SL.cntx_psh; grp_disp(SL,__wer2,0); SL.win_sel(__wer2); SL.hdr_sel; SL.hdr_sel(__eanl); SL.cntx_pop;
         SL.cntx_psh; grp_disp(SL,__wer3,0); SL.win_sel(__wer3); SL.hdr_sel; SL.hdr_sel(__ktm); SL.cntx_pop
      ";
      _fb:="
         SL.cntx_psh();
         SL.index('PAL');
         {? __f_act
         ||
            SL.prefix(null)
         ||
            {? __prfx=''
            || SL.prefix(SL.MG,SL.PAL)
            || SL.prefix(SL.MG,SL.PAL,__prfx)
            ?}
         ?}
      ";
      _fa:="SL.cntx_pop()";

      SL.grp_sel(__werg,,__wer4,,_fr,_szer,8,6,_fb,_fa,,,'maximized_with_title');
      SL.grp_splt(__werg,'tab2','horizontal','tab3')

   ?};
:: stany materialu
   _fr:="
      __eanl:=SL.EANL().KOD;
      __slr:=SL.ref;
      SL.cntx_psh; grp_disp(SL,__wer1,0); SL.cntx_pop;
      SL.cntx_psh; grp_disp(SL,__wer2,0); SL.win_sel(__wer2); SL.hdr_sel; SL.hdr_sel(__eanl); SL.cntx_pop
   ";
   _fb:="
      SL.cntx_psh();
      SL.index('M');
      {? __f_act
      ||
         SL.prefix(null)
      ||
         {? __onemg
         || SL.prefix(SL.M,SL.MG)
         || SL.prefix(SL.M)
         ?}
      ?}
   ";
   _fa:="SL.cntx_pop()";

   SL.grp_sel(__werg,,__wer3,,_fr,_szer,12,10,_fb,_fa,,,'maximized_with_title');

   SL.win_sel(__werg);
   SL.select;

   _zm()
|? _b<>null & ST.MAG<>null & ST.MAG().SL<>'T'
|| FUN.info('Magazyn bez zastosowania wymiarów (lokalizacja, termin ważności).'@)
|| FUN.info('Nie wybrano magazynu w parametrach pracy programu.'@)
?};
POMOC.MGR:=null;
VAR_DEL.delete('__onemg');
''


\sm_dokal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wyswietla wszystkie dokumenty z okienka stanow
::  OLD: \sm_dokal/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
_sel:=DK.grp_make('Pozycje dokumentów magazynowych'@,,'msaewdkvusopoly');
_atrmjs:=ATR.MJS;
ATR.MJS:='DK';

DK.fld_fml('C','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $DK.M().DOKL_C || $ST.DOKL_C ?}+','+{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
DK.fld_fml('WAR','DISPLAY_FORMAT',"{? ST.MAG().IL='T' || 'empty=1' || 'empty=0' ?}");
ND.cntx_psh();
DK.cntx_psh();
DK_L.cntx_psh();
{? var_pres('bie_r')>100 || obj_del(bie_r) ?};
_il:=0;
czy_dsp:=0;
OKR.cntx_psh();
{? OKR.last()
||
   {!
   |?
      {? OKR.FIRMA=REF.FIRMA & OKR.MC=1
      ||
         DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
         ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));
         DK_L.use('doklk'+ST.ODDZ+($OKR.ROK+2));
         DK.index('DK_SM');
         DK.prefix(ST.MAG,SM.M);
         {? DK.first()
         ||
            _il+=1;
            _form:=$('czy_dsp:=1;bie_r:='+$OKR.ROK+';
            ND.use(\'nagdo\'+ST.ODDZ+($bie_r+2));
            DK.use(\'dokma\'+ST.ODDZ+($bie_r+2));
            DK_L.use(\'doklk\'+ST.ODDZ+($bie_r+2));
            DK.index(\'DK_SM\');
            DK.prefix(ST.MAG,SM.M);DK.first()');
            DK.grp_sel(_sel,DK,'WER_SM',$OKR.ROK,"{? czy_dsp=1 || grp_disp(DK,'WER_SM',0,1) ?};czy_dsp:=0",,,,_form)
         ?}
      ?};
      OKR.prev() & _il<16
   !}
?};
OKR.cntx_pop();
{? _il=0
||
   _form:=$('czy_dsp:=1;bie_r:='+$ST.AR+';
   ND.use(\'nagdo\'+ST.ODDZ+($bie_r+2));
   DK.use(\'dokma\'+ST.ODDZ+($bie_r+2));
   DK_L.use(\'doklk\'+ST.ODDZ+($bie_r+2));
   DK.index(\'DK_SM\');
   DK.prefix(ST.MAG,SM.M);DK.first()');
   DK.grp_sel(_sel,DK,'WER_SM',$ST.AR,"{? czy_dsp=1 || grp_disp(DK,'WER_SM',0,1) ?};czy_dsp:=0",,,,_form)
?};

exec('dk_p_ib','magdok_poz','WER_SM');
DK.win_sel(_sel);
DK.win_fml('WER_SM',ND,'SYM',,'ICON_BEFORE',exec('nd_sym_ib','magdok_nag'));
{? _il>0 || DK.select() || FUN.info('Brak dokumentów magazynowych dla indeksu %1.'@[SM.M().KTM]) ?};
DK.cntx_pop();
ND.cntx_pop();
DK_L.cntx_pop();
VAR_DEL.delete('czy_dsp','bie_r');
ATR.MJS:=_atrmjs;
DK.fld_fml('C','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $DK.M().DOKL_C || $ST.DOKL_C ?}+','+'empty=0'");
DK.fld_fml('WAR','DISPLAY_FORMAT',"'empty=0'");
''


\dk_c_stan_mat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: Stany magazynowe materialu w magazynie
::  OLD: \dk_c_stan_mat/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
_m_atr:=M.M_ATR;
{? SM.S<=0
|| FUN.info('Brak stanu dla indeksu: %1'@[SM.M().KTM])
|? _m_atr<>null
||
:: parametry kolumn
   VAR_DEL.delete('__atrdst','__sumdkc','__idbtn','__wski','__zabtn','__max','_acrsum','__acrdkc','__wtrdst');
   __atrdst:=tab_tmp(10,'W01','STRING[25]',''
              ,'W02','STRING[25]',''
              ,'W03','STRING[25]',''
              ,'W04','STRING[25]',''
              ,'W05','STRING[25]',''
              ,'W06','STRING[25]',''
              ,'W07','STRING[25]',''
              ,'W08','STRING[25]',''
              ,'W09','STRING[25]',''
              ,'W10','STRING[25]',''
              ,'ILE','REAL','');
   __sumdkc:=tab_tmp(10,'W01','STRING[25]',''
              ,'W02','STRING[25]',''
              ,'W03','STRING[25]',''
              ,'W04','STRING[25]',''
              ,'W05','STRING[25]',''
              ,'W06','STRING[25]',''
              ,'W07','STRING[25]',''
              ,'W08','STRING[25]',''
              ,'W09','STRING[25]',''
              ,'W10','STRING[25]',''
              ,'ILE','REAL','');
   __idbtn:=obj_new(10);
   __zabtn:=obj_new(10);
   {! _i..10 |! __zabtn[_i]:=1 !};
   __max:=0;
   _rozm:=form(($_m_atr)+8);
   _iddkc:='#sdd'+_rozm;
   _idsum:='#sds'+_rozm;
   _idgrp:='#sdg'+_rozm;

:: definicja okienek
   _acr:=__acrdkc:=__atrdst.mk_sel('Atrybuty cechy'@,'P',0,_iddkc,,,,,'U');
   _acs:=__acrsum:=__sumdkc.mk_sel('Suma wg aktywnych atrybutów'@,'P',0,_idsum,,,,,'U');

   M_ATR.cntx_psh();
   M_ATR.clear();
   {? M_ATR.seek(_m_atr)
   || _i:=0;
      {!
      |? _i+=1;
         {? ($('M_ATR.SL_'+form(_i,-2,,'99')))()<>null
         || _nr:=form(_i,-2,0,'99');
            __atrdst.win_fld(_acr,,'W'+_nr,,,-7,,,($('M_ATR.SL_'+_nr+'().NA'))());
            __sumdkc.win_fld(_acs,,'W'+_nr,,,-7,,,($('M_ATR.SL_'+_nr+'().NA'))());
            _form:=$('exec(\'wyl_btn\',\'magazyn_stan\','+$_i+')');
            _menu:=%(64+(2*_i));
            __atrdst.win_act(_acr,,'Formuła','%1&'[($('M_ATR.SL_'+_nr+'().NA'))()],,,_form,,,,,,_menu);
            __idbtn[_i]:=__atrdst.win_btn(_acr,'text=&'+$_i+'. '+($('M_ATR.SL_'+_nr+'().NA'))(),'menu:%1'[_menu],,,,,,'noempty');
            __max:=_i;
            _i<10
         || 0
         ?}
      !}
   ?};
   __atrdst.win_fld(_acr,,'ILE',,,6,,,'Stan'@);
   __sumdkc.win_fld(_acs,,'ILE',,,6,,,'Suma'@);
   __atrdst.win_act(_acr,,'Formuła','&Aktywuj klawisze'@@,,,"exec('akt_btn','magazyn_stan')",,,1,,,'A');
   __atrdst.win_act(_acr,,'Szukaj');
   __atrdst.win_act(_acr,,'Kolejność');
   __sumdkc.win_act(_acs,,'Szukaj');
   __sumdkc.win_act(_acs,,'Kolejność');
   __atrdst.win_btn(_acr,'text=%1'['&Aktywuj klawisze'@],'menu:A',,,,,,'noempty');
   __wtrdst:=_acr;

   M_ATR.cntx_pop();

:: wypelnienie tabelki atrybutow
   SC.cntx_psh();
   OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   {? OKR.first()
   || {!
      |? SC.use('stc__'+ST.ODDZ+($OKR.ROK+2));
         SC.index('SC');
         SC.prefix(ST.MAG,SM.M);
         {? SC.first()
         || {!
            |? {? SC.S>0
               || {? SC.DK_C<>null
                  || __atrdst.clear();
                     _fnd:='__atrdst.find_key(';
                     {! _i..__max
                     |! _fnd+='\''+($('SC.DK_C().WAR'+form(_i,-2,0,'99')))()+'\','
                     !};
                     _fnd:=(_fnd)+')';
                     {? ($_fnd)()
                     || __atrdst.ILE+=SC.S;
                        __atrdst.put(1)
                     || __atrdst.blank();
                        {! _i..__max
                        |! __atrdst[_i]:=($('SC.DK_C().WAR'+form(_i,-2,0,'99')))()
                        !};
                        __atrdst.ILE:=SC.S;
                        __atrdst.add(1)
                     ?}
                  || __atrdst.clear();
                     {? __atrdst.find_key('brak atrybutów',)
                     || __atrdst.ILE+=SC.S;
                        __atrdst.put(1)
                     || __atrdst.blank();
                        __atrdst.W01:='brak atrybutów';
                        __atrdst.ILE:=SC.S;
                        __atrdst.add(1)
                     ?}
                  ?}
               ?};
               SC.next()
            !}
         ?};
         OKR.next()
      !}
   ?};
   OKR.cntx_pop();
   SC.cntx_pop();

   _sel:=__atrdst.grp_make('Atrybuty cech'@,,_idgrp,,,,,'normal');
   __atrdst.grp_sel(_sel,__atrdst,_acr,,"exec('oblstzaz','magazyn_stan')",,,20);
   __atrdst.grp_splt(_sel,,'horizontal','new_stan_maga_x');
   __atrdst.grp_sel(_sel,__sumdkc,_acs,,,,,5);
   __atrdst.win_sel(_sel);
   __atrdst.clear();
   {? __atrdst.first() |1
   || __atrdst.select()
   || FUN.info('Brak stanów wg atrybutów dostaw.'@)
   ?};
   VAR_DEL.delete('__atrdst','__sumdkc','__idbtn','__wski','__zabtn','__max','__acrsum','__acrdkc','__wtrdst')
|| FUN.info('Materiał nie posiada zdefiniowanych atrybutów. Funkcja niedostępna.'@)
?};
1


\wyl_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: wylaczenie klawisza
::   WE: _a - numer klawisza
::  OLD: \wyl_btn/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
__atrdst.btn_sopt(__acrdkc,__idbtn[_a],'state=grayed');
_act:=__atrdst.actions_grayed(__wtrdst,'');
_menu:=%(64+2*_a);
__atrdst.actions_grayed(__wtrdst,_act+_menu);
__zabtn[_a]:=0;
exec('oblstzaz','magazyn_stan');
'activate:browse'


\akt_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: aktywacja wszystkich klawiszy
::  OLD: \akt_btn/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
{! _i..__max
|! __atrdst.btn_sopt(__acrdkc,__idbtn[_i],'state=normal');
   __zabtn[_i]:=1
!};
exec('oblstzaz','magazyn_stan');
'activate:browse'


\oblstzaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: obliczenia wg zaznaczonych rekordow
::  OLD: \oblstzaz/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
__sumdkc.erase();
__atrdst.cntx_psh();
__atrdst.clear();
{? __atrdst.first()
|| _warunek:='';
   {! _i..__max
   |! {? __zabtn[_i]
      || _warunek+={? +_warunek || '& ' || '' ?}+'__atrdst['+$_i+']=__sumdkc['+$_i+']'
      ?}
   !};
   {!
   |? _new:=1;
      __sumdkc.clear();
      {? __sumdkc.first()
      || {!
         |? {? _warunek='' | ($_warunek)()
            || _new:=0;
               __sumdkc.ILE+=__atrdst.ILE;
               __sumdkc.put(1);
               0
            || __sumdkc.next()
            ?}
         !}
      ?};
      {? _new
      || __sumdkc.blank();
         {! _i..__max
         |! {? __zabtn[_i]
            || __sumdkc[_i]:=__atrdst[_i]
            ?}
         !};
         __sumdkc.ILE:=__atrdst.ILE;
         __sumdkc.add(1)
      ?};
      __atrdst.next()
   !}
?};
__atrdst.cntx_pop();

__sumdkc.first();
grp_disp(__sumdkc,__acrsum);
~~


\find_mat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: szuka materialu w okienku SM oraz okresla rezerwacje
::  OLD: \find_mat/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
_prod:=exec('tte_lic','tte');

_act:={? _prod='N' || 'N(P)C(Z)' || '' ?}
     +{? ST.MAG().SKL='T'
      || 'ECN(P)'
      |? ST.MAG().PAL='T'
      || 'EC'
      |? exec('spr_uprr','users',1)
      || ''
      || 'E'
      ?}
     +{? ST.MAG().SL<>'T' || 'G' || '' ?};

DISP.JMN:=SM.M().J;
SM.actions('STANY_MG',_act,,1);
SM.actions('STANY2MG',_act,,1);
SM.actions('STANY_ML',_act,,1);
SM.actions('STANY2ML',_act,,1);
_gray:={? (1+SM.MAG().TYP)='D'
       || {? SM.M().M_ATR<>null || '' || 'U' ?}
       || 'N(BY)U'
       ?}
      +{? (';SWP'*SM.M().R)>1 || '' || 'P' ?};
SM.actions_grayed('STANY_MG',{? SM.MAG().SL<>'T' || 'MO' || '' ?}+_gray);
SM.actions_grayed('STANY2MG',{? SM.MAG().SL<>'T' || 'MO' || '' ?}+_gray);
SM.actions_grayed('STANY_ML',{? SM.MAG().SL<>'T' || 'MO' || '' ?}+_gray);
SM.actions_grayed('STANY2ML',{? SM.MAG().SL<>'T' || 'MO' || '' ?}+_gray);
{? _a
|| SM.get()
?};
exec('rekprzed','color','SM#01')


\btnsumsm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: suma wg jednostek zaznaczonych rekordow
::  OLD: \btnsumsm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__sum_sm','__sumwin');

__sum_sm:=tab_tmp(1,'JM','STRING[10]','jm'@
           ,'S','REAL',''
           ,'W','REAL',''
           ,'R','REAL',''
           ,'P','REAL',''
           ,'Z','REAL',''
           ,'D','REAL',''
           ,'SP','REAL',''
           ,'SD','REAL','');
:: okienko podsumy
__sumwin:=__sum_sm.mk_sel('Razem'@,,0,'podsumsm',,,,,'U');
__sum_sm.win_fld(__sumwin,,'JM',,,-8,,,'jm'@);
__sum_sm.win_fld(__sumwin,,'S',,,-12,3,,'Stan'@);
__sum_sm.win_fld(__sumwin,,'W',,,-12,3,,'W wydaniu'@);
__sum_sm.win_fld(__sumwin,,'R',,,-12,3,,'Zarezerwowano'@);
__sum_sm.win_fld(__sumwin,,'P',,,-12,3,,'Do rezerwacji'@);
__sum_sm.win_fld(__sumwin,,'Z',,,-12,3,,'Zamówienia'@);
__sum_sm.win_fld(__sumwin,,'SP',,,-12,3,,'W produkcji'@,,'Stan według zleceń produkcyjnych'@);
__sum_sm.win_fld(__sumwin,,'D',,,-12,3,,'W drodze'@);
__sum_sm.win_fld(__sumwin,,'SD',,,-12,3,,'Dostępne'@);
__sum_sm.win_sel(__sumwin);

exec('odsw_sum_jm','magazyn_stan',1);
SM.sel_adel();
__sum_sm.select();

VAR_DEL.delete('__sum_sm','__sumwin');
'activate:browse'


\btninfsm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: szczegoly danego indeksu materialowego
::  OLD: \btninfsm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('info_zam','magazyn_stan',1,SM.M);
'activate:browse'


\btnatrsm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: stan wg atrybutow dostaw
::   WE: [_a] - 0(domyslnie) wg SM-a 1-wg M
::  OLD: \btnatrsm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};

VAR_DEL.delete('__infatr','__winatr','__aktref');
__infatr:=tab_tmp(3,'TREE','TREE_REF','galazka'@
           ,'LAB','STRING[100]',''
           ,'M_ATR','STRING[16]',''
           ,'WAR01','STRING[25]',''
           ,'WAR02','STRING[25]',''
           ,'WAR03','STRING[25]',''
           ,'WAR04','STRING[25]',''
           ,'WAR05','STRING[25]',''
           ,'WAR06','STRING[25]',''
           ,'WAR07','STRING[25]',''
           ,'WAR08','STRING[25]',''
           ,'WAR09','STRING[25]',''
           ,'WAR10','STRING[25]',''
           ,'SD','REAL',''
           ,'JM','STRING[10]',''
           ,'SQL','STRING[16]',''
           ,'SCEAN','STRING[128]','Kod identyfikujący dostawę'@
           ,'SD2','REAL',''
           ,'J2','STRING[10]','');

_max:=0;
M.cntx_psh();
SM.cntx_psh();
MG.cntx_psh();

{? ~_a
|| __aktref:=SM.ref();
   _tab:=SM.sel_aget();
   {? ~_tab.size() || _tab.blank(); _tab.REF:=#SM.ref(); _tab.add(1) ?};
   SM.sel_adel()
|| __aktref:=M.ref();
   _tab:=M.sel_aget();
   {? ~_tab.size() || _tab.blank(); _tab.REF:=#M.ref(); _tab.add(1) ?};
   M.sel_adel()
?};

{? _tab.first()
|| {!
   |? {? {? ~_a
         || (SM.clear(); SM.seek(_tab.REF,)) & SM.S>0
         || (M.clear(); M.seek(_tab.REF,))
         ?}
      || _mat:={? ~_a || SM.M || M.ref() ?};
         {? ~_a
         || VAR_DEL.delete('__smmag');
            __smmag:=tab_tmp(1,'RSQL','STRING[16]','');
            __smmag.blank();
            __smmag.RSQL:=$SM.MAG;
            __smmag.add(1)
         ?};
         __smmag.cntx_psh();
         __smmag.clear();
         {? __smmag.first()
         || _refk:=null;
            {!
            |? _mag:=exec('FindAndGet','#table','MG',__smmag.RSQL,,,null());
               SM.index('SM');
               SM.prefix(_mag);
               {? SM.find_key(_mat) & SM.MAG().TYP*'DOST' & SM.S>0
               || _smg:=SM.MAG().SYM;
                  _rmg:=$SM.MAG;
                  {? _refk=null
                  || __infatr.clear();
                     __infatr.blank();
                     __infatr.TREE:=0;
                     _czy:=SM.M().M_ATR<>null;
                     __infatr.LAB:=SM.M().KTM+' ['+{? _czy || SM.M().M_ATR().SYM || 'brak atrybutów dostawy' ?}+']';
                     __infatr.M_ATR:={? _czy || $ SM.M().M_ATR || 'xxx' ?};
                     __infatr.SD:=0;
                     __infatr.JM:=SM.M().J().KOD;
                     __infatr.SQL:=$SM.M;
                     __infatr.SD2:=0;
                     __infatr.J2:={? SM.M().J2<>null() || SM.M().J2().KOD || '' ?};
                     {? _czy
                     || _i:=0;
                        {!
                        |? _i+=1;
                           {? ($('SM.M().M_ATR().SL_'+form(_i,-2,0,'99')))()<>null
                           || __infatr[_i+3]:=($('SM.M().M_ATR().SL_'+form(_i,-2,0,'99')+'().NA'))();
                              {? _max<_i || _max:=_i ?};
                              _i<10
                           || 0
                           ?}
                        !}
                     ?};
                     _refk:={? __infatr.add(1) || __infatr.ref() || null ?}
                  ?};
                  {? _refk<>null
                  || exec('sc_tymczas','magazyn_stan',SM.MAG,SM.M,'T');
                     {? __sc.first()
                     || _refm:=null;
                        {!
                        |? {? __sc.S>0
                           || _dk_c:=exec('FindAndGet','#table','DK_C',__sc.DK_C,,"DK_C.SYM",'');
                              {? type_of(_dk_c)<>type_of('') | _dk_c='' || _dk_c:='brak cechy dostawy' ?};
                              _rdkc:={? __sc.DK_C='' || 'xxx' || __sc.DK_C ?};
                              __infatr.clear();
                              {? __infatr.find_key(_refk,_dk_c,_rdkc)
                              || __infatr.SD+=__sc.S;
                                 __infatr.SD2+=__sc.S2;
                                 __infatr.put(1);
                                 _refm:=__infatr.ref()
                              || __infatr.blank();
                                 __infatr.TREE:=_refk;
                                 __infatr.LAB:=_dk_c;
                                 __infatr.M_ATR:=_rdkc;
                                 __infatr.SD:=__sc.S;
                                 __infatr.SCEAN:=__sc.SCEAN;
                                 {! _i..10 |! __infatr[_i+3]:=($('__sc.WAR'+form(_i,-2,0,'99')))() !};
                                 __infatr.JM:=SM.M().J().KOD;
                                 __infatr.SQL:=$SM.M;
                                 __infatr.SD2:=__sc.S2;
                                 __infatr.J2:={? SM.M().J2<>null()|| SM.M().J2().KOD || '' ?};
                                 _refm:={? __infatr.add(1) || __infatr.ref() || null ?}
                              ?};
                              {? _a & _refm<>null
                              || {? __infatr.find_key(_refm,_smg,_rmg)
                                 || __infatr.SD+=__sc.S;
                                    __infatr.SD2+=__sc.S2;
                                    __infatr.put(1);
                                    _refm:=__infatr.ref()
                                 || __infatr.blank();
                                    __infatr.TREE:=_refm;
                                    __infatr.LAB:=_smg;
                                    __infatr.M_ATR:=_rmg;
                                    __infatr.SD:=__sc.S;
                                    __infatr.SCEAN:=__sc.SCEAN;
                                    {! _i..10 |! __infatr[_i+3]:=($('__sc.WAR'+form(_i,-2,0,'99')))() !};
                                    __infatr.JM:=SM.M().J().KOD;
                                    __infatr.SQL:=$SM.M;
                                    __infatr.SD2:=__sc.S2;
                                    __infatr.J2:={? SM.M().J2<>null()|| SM.M().J2().KOD || '' ?};
                                    __infatr.add(1)
                                 ?}
                              ?};
                              __infatr.clear();
                              {? __infatr.seek(_refk)
                              || __infatr.SD+=__sc.S;
                                 __infatr.SD2+=__sc.S2;
                                 __infatr.put(1)
                              ?}
                           ?};
                           __sc.next()
                        !}
                     ?}
                  ?}
               ?};
               __smmag.next()
            !}
         ?};
         __smmag.cntx_pop();
         {? ~_a || VAR_DEL.delete('__smmag') ?}
      ?};
      _tab.next()
   !}
?};
M.cntx_pop();
SM.cntx_pop();
MG.cntx_pop();

_ident:='#infatrsmsel';
{? _tab.first() & ~_tab.next()
||
   _mat:={? ~_a || SM.M || M.ref() ?};
   _m_atr:=exec('FindAndGet','#table',M,#M.ref(),,"$M.M_ATR+8",'');
   {? _m_atr<>'' || _ident:='#infatr'+_m_atr ?}
?};
_win_sel:=__infatr.mk_sel('Stan wg cech (atrybutów dostaw)'@,'P',,_ident,,,30,1);
__winatr:=_win_sel;

__infatr.win_fld(_win_sel,,'LAB',,,24,,1,'Indeks/Cecha [atrybuty]'@);
{? _max>0 || {! _i:=1.._max |! __infatr.win_fld(_win_sel,,'WAR'+form(_i,-2,0,'99'),,,7,,1,'') !} ?};
__infatr.win_fld(_win_sel,,'SD',,,-12,3,1,'Stan'@);
__infatr.win_fld(_win_sel,,'JM',,,3,,1,'jm'@);
__infatr.win_fld(_win_sel,,'SD2',,,-12,3,1,'Stan 2'@);
__infatr.win_fld(_win_sel,,'J2',,,3,,1,'jm'@);
__infatr.win_act(_win_sel,0,'Rekord',,,,"exec('rekprzed','color','__infatr#01')");
__infatr.win_act(_win_sel,,'Formuła','&Zwiń/rozwiń'@@,,,"exec('zwin_rozwin','#tree')",,1,,,,'Z');
__infatr.win_act(_win_sel,0,'Szukaj');
__infatr.win_act(_win_sel,0,'Kolejność');
__infatr.win_act(_win_sel,0,'Formuła','&Legenda'@@,,,"exec('legenda','color','__infatr#01')",,,,,,'L');

__infatr.tr_fml(_win_sel,,"{? _a || {? __infatr.TREE=0 || 1 || _a ?} || _a ?}");
__infatr.win_fml(_win_sel,,'LAB',,'ICON_BEFORE',"
         {? ~__infatr.TREE & __infatr.M_ATR<>'xxx'
         || 'xwin16.png:57'
         |? ~__infatr.TREE & __infatr.M_ATR='xxx'
         || 'xwin16.png:81'
         |? __infatr.TREE & __infatr.M_ATR<>'xxx'
         || 'xwin16.png:7'
         |? __infatr.TREE & __infatr.M_ATR='xxx'
         || 'xwin16.png:8'
         || ''
         ?}
     ");
__infatr.fld_fml('SD2','DISPLAY_FORMAT',"{? __infatr.J2='' || 'empty=1' || 'empty=0' ?}");
__infatr.win_sel(_win_sel);
__infatr.clear();
{? __infatr.first()
|| __infatr.select()
|| FUN.info('Brak stanu.\nWyświetlenie informacji niemożliwe.'@)
?};

VAR_DEL.delete('__infatr','__winatr','__aktref');
'activate:browse'


\odsw_sum_jm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: oblicza sumy zaznaczonych rekordow
::   WE: [_a] - 0(domyslnie) M, 1-SM
::  OLD: \odsw_sum/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};

{? __sum_sm.first() || {! |? __sum_sm.del() !} ?};
{? _a
|| _tab:=SM.sel_aget();
   SM.cntx_psh();
   {? _tab.first()
   || {!
      |? {? (SM.clear(); SM.seek(_tab.REF,))
         || __sum_sm.clear();
            {? __sum_sm.find_key(SM.M().J().KOD,)
            || __sum_sm.S+=SM.S;
               __sum_sm.W+=SM.W;
               __sum_sm.R+=SM.RT+SM.RZ;
               __sum_sm.P+=SM.P;
               __sum_sm.Z+=SM.Z;
               __sum_sm.D+=SM.D;
               __sum_sm.SP+=SM.SP;
               __sum_sm.SD+=SM.SD;
               __sum_sm.put(1)
            || __sum_sm.blank();
               __sum_sm.JM:=SM.M().J().KOD;
               __sum_sm.S:=SM.S;
               __sum_sm.W:=SM.W;
               __sum_sm.R:=SM.RT+SM.RZ;
               __sum_sm.P:=SM.P;
               __sum_sm.Z:=SM.Z;
               __sum_sm.D:=SM.D;
               __sum_sm.SP:=SM.SP;
               __sum_sm.SD:=SM.SD;
               __sum_sm.add(1)
            ?}
         ?};
         _tab.next()
      !}
   || __sum_sm.blank();
      __sum_sm.JM:=SM.M().J().KOD;
      __sum_sm.S:=SM.S;
      __sum_sm.W:=SM.W;
      __sum_sm.R:=SM.RT+SM.RZ;
      __sum_sm.P:=SM.P;
      __sum_sm.Z:=SM.Z;
      __sum_sm.D:=SM.D;
      __sum_sm.SP:=SM.SP;
      __sum_sm.SD:=SM.SD;
      __sum_sm.add(1)
   ?};
   SM.cntx_pop()
|| _tab:=M.sel_aget();
   M.cntx_psh();
   {? _tab.first()
   || {!
      |? {? (M.clear(); M.seek(_tab.REF,))
         || exec('rek_mate','magazyn_stan');
            __sum_sm.clear();
            {? __sum_sm.find_key(M.J().KOD,)
            || __sum_sm.S+=BEER.S;
               __sum_sm.W+=BEER.SW;
               __sum_sm.R+=BEER.SRT+BEER.SRR;
               __sum_sm.D+=BEER.WDR;
               __sum_sm.SP+=BEER.SP;
               __sum_sm.SD+=BEER.SD;
               __sum_sm.put(1)
            || __sum_sm.blank();
               __sum_sm.JM:=M.J().KOD;
               __sum_sm.S:=BEER.S;
               __sum_sm.W:=BEER.SW;
               __sum_sm.R:=BEER.SRT+BEER.SRR;
               __sum_sm.D:=BEER.WDR;
               __sum_sm.SP:=BEER.SP;
               __sum_sm.SD:=BEER.SD;
               __sum_sm.add(1)
            ?}
         ?};
         _tab.next()
      !}
   || __sum_sm.blank();
      __sum_sm.JM:=M.J().KOD;
      __sum_sm.S:=BEER.S;
      __sum_sm.W:=BEER.SW;
      __sum_sm.R:=BEER.SRT+BEER.SRR;
      __sum_sm.D:=BEER.WDR;
      __sum_sm.SP:=BEER.SP;
      __sum_sm.SD:=BEER.SD;
      __sum_sm.add(1)
   ?};
   M.cntx_pop()
?};
__sum_sm.first();
~~


\btnfndsm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: wyszukiwanie wg atrybutow dostaw
::   WE: [_a] - 0(domyslnie) wg SM-a 1-wg M
::  OLD: \btnfndsm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};

VAR_DEL.delete('__fndatr','__infatr','__aktref');
__fndatr:=tab_tmp(10,'F01','STRING[25]',''
           ,'F02','STRING[25]',''
           ,'F03','STRING[25]',''
           ,'F04','STRING[25]',''
           ,'F05','STRING[25]',''
           ,'F06','STRING[25]',''
           ,'F07','STRING[25]',''
           ,'F08','STRING[25]',''
           ,'F09','STRING[25]',''
           ,'F10','STRING[25]','');

_max:=10;
M.cntx_psh();
SM.cntx_psh();
SC.cntx_psh();
MG.cntx_psh();
OKR.cntx_psh();

{? ~_a
|| __aktref:=SM.ref()
|| __aktref:=M.ref()
?};

_win_edit:=__fndatr.mk_edit('Wartości atrybutów'@,0);
{! _i:=1..10 |! __fndatr.win_efld(_win_edit,,'F'+form(_i,-2,0,'99'),,,20,,,'Wartość %1'@[form(_i,2,0,'99')]) !};
__fndatr.win_edit(_win_edit);
{? __fndatr.edit("exec('sprfndsm','magazyn_stan')")
|| _maxfnd:=0;
   {! _i:=1..10 |! {? ($('__fndatr.F'+form(_i,-2,0,'99')))()<>'' || _maxfnd:=_i ?} !};
   __infatr:=tab_tmp(3,'TREE','TREE_REF','galazka'@
              ,'LAB','STRING[100]',''
              ,'M_ATR','STRING[16]',''
              ,'WAR01','STRING[25]',''
              ,'WAR02','STRING[25]',''
              ,'WAR03','STRING[25]',''
              ,'WAR04','STRING[25]',''
              ,'WAR05','STRING[25]',''
              ,'WAR06','STRING[25]',''
              ,'WAR07','STRING[25]',''
              ,'WAR08','STRING[25]',''
              ,'WAR09','STRING[25]',''
              ,'WAR10','STRING[25]',''
              ,'SD','REAL',''
              ,'JM','STRING[10]',''
              ,'SQL','STRING[16]',''
              ,'SCEAN','STRING[128]','Kod identyfikujący dostawę'@
              ,'SD2','REAL',''
              ,'J2','STRING[10]','');

   _mat:={? ~_a || SM.M || M.ref() ?};
   {? ~_a
   || VAR_DEL.delete('__smmag');
      __smmag:=tab_tmp(1,'RSQL','STRING[16]','');
      __smmag.blank();
      __smmag.RSQL:=$SM.MAG;
      __smmag.add(1)
   ?};
   __smmag.cntx_psh();
   __smmag.clear();
   {? __smmag.first()
   || _refg:=null;
      _refd:=null;
      {!
      |? _mag:=exec('FindAndGet','#table','MG',__smmag.RSQL,,,null());
         OKR.index('MC');
         OKR.prefix(REF.FIRMA,1);
         SM.index('SM');
         SM.prefix(_mag);
         {? SM.first() & SM.MAG().TYP*'DOST' & OKR.first()
         || _smg:=SM.MAG().SYM;
            _rmg:=$SM.MAG;

            {? _refg=null
            || __infatr.clear();
               __infatr.blank();
               __infatr.TREE:=0;
               __infatr.LAB:='Wyszukiwanie główne (zgodność położenia)';
               __infatr.M_ATR:='+';
               _refg:={? __infatr.add(1) || __infatr.ref() || null ?}
            ?};

            {? _refd=null
            || __infatr.clear();
               __infatr.blank();
               __infatr.TREE:=0;
               __infatr.LAB:='Wyszukiwanie szczegółowe (nieistotne położenie)';
               __infatr.M_ATR:='-';
               _refd:={? __infatr.add(1) || __infatr.ref() || null ?}
            ?};

            {!
            |? SC.use('stc__'+ST.ODDZ+($OKR.ROK+2));
               SC.index('SA');
               SC.prefix(_mag);
               {? SC.find_tab('first','M','M_ATR','<>',null(),'DK_C',,'<>',null(),'S',,'>=',0)
               || {!
                  |? _dk_c:=exec('FindAndGet','#table','DK_C',$SC.DK_C,,"DK_C.SYM",'');
                     _rdkc:=$SC.DK_C;
::                   sprawdzenie dokladne i szczegolowe
                     _sprd:=-1;
                     _sprs:=-1;
                     {! _i:=1.._maxfnd
                     |! _fnd:=form(($('__fndatr.F'+form(_i,-2,0,'99')))());
                        {? _fnd<>''
                        || {? _sprd<=0 & ($('SC.WAR'+form(_i,-2,0,'99')))()*_fnd
                           || _sprd:=0
                           || _sprd:=1
                           ?};
                           {? _sprs<=0
                           || _j:=0;
                              {!
                              |? _j+=1;
                                 _scwar:=($('SC.WAR'+form(_j,-2,0,'99')))();
                                 {? _scwar=''
                                 || _sprs:=1; 0
                                 |? _scwar*_fnd
                                 || _sprs:=0;
                                    0
                                 || {? _j<10 || 1 || _sprs:=1; 0 ?}
                                 ?}
                              !}
                           ?}
                        ?}
                     !};
                     {? ~_sprd
                     ||  __infatr.clear();
                        {? __infatr.find_key(_refg,_dk_c,_rdkc)
                        || __infatr.SD+=SC.S;
                           __infatr.SD2+=SC.S2;
                           __infatr.put(1)
                        || __infatr.blank();
                           __infatr.TREE:=_refg;
                           __infatr.LAB:=SC.M().KTM+' ['+_dk_c+']';
                           __infatr.M_ATR:=_rdkc;
                           __infatr.SD:=SC.S;
                           __infatr.SCEAN:=SC.SCEAN;
                           {! _i:=1..10 |! __infatr[_i+3]:=($('SC.WAR'+form(_i,-2,0,'99')))() !};
                           __infatr.JM:=SC.M().J().KOD;
                           __infatr.SQL:=$SC.M;
                           __infatr.SD2:=SC.S2;
                           __infatr.J2:={? SC.M().J2<>null() || SC.M().J2().KOD || '' ?};
                           __infatr.add(1)
                        ?}
                     ?};
                     {? ~_sprs & _sprd<>0
                     ||  __infatr.clear();
                        {? __infatr.find_key(_refd,_dk_c,_rdkc)
                        || __infatr.SD+=SC.S;
                           __infatr.put(1)
                        || __infatr.blank();
                           __infatr.TREE:=_refd;
                           __infatr.LAB:=SC.M().KTM+' ['+_dk_c+']';
                           __infatr.M_ATR:=_rdkc;
                           __infatr.SD:=SC.S;
                           __infatr.SCEAN:=SC.SCEAN;
                           {! _i..10 |! __infatr[_i+3]:=($('SC.WAR'+form(_i,-2,0,'99')))() !};
                           __infatr.JM:=SC.M().J().KOD;
                           __infatr.SQL:=$SC.M;
                           __infatr.SD2:=SC.S2;
                           __infatr.J2:={? SC.M().J2<>null() || SC.M().J2().KOD || '' ?};
                           __infatr.add(1)
                        ?}
                     ?};
                     SC.find_tab('next','M','M_ATR','<>',null(),'DK_C',,'<>',null(),'S',,'>=',0)
                  !}
               ?};
               OKR.next()
            !}
         ?};
         __smmag.next()
      !}
   ?};
   __smmag.cntx_pop();

   _win_sel:=__infatr.mk_sel('Rezultat wyszukiwania wg cech (atrybutów dostaw)'@,'P',,'#fndatrsmsel',,,30,1);
   __winatr:=_win_sel;

   __infatr.win_fld(_win_sel,,'LAB',,,24,,1,'Indeks/Cecha [atrybuty]'@);
   {! _i:=1.._max
   |! __infatr.win_fld(_win_sel,,'WAR'+form(_i,-2,0,'99'),,,7,,1,'')
   !};
   __infatr.win_fld(_win_sel,,'SD',,,-12,3,1,'Stan'@);
   __infatr.win_fld(_win_sel,,'JM',,,3,,1,'jm'@);
   __infatr.win_fld(_win_sel,,'SD2',,,-12,3,1,'Stan 2'@);
   __infatr.win_fld(_win_sel,,'J2',,,3,,1,'jm'@);
   __infatr.win_act(_win_sel,0,'Formuła','&Idź do indeksu'@@,,,"exec('goto_mat','magazyn_stan')",,1,,,,'I');
   __infatr.win_act(_win_sel,0,'Formuła','&Historia dostaw'@@,,
       ,{? ~_a
        || "exec('his_dk_c','magdok_wspolne',__infatr.M_ATR)"
        || "exec('his_dk_c','magdok_wspolne',__infatr.M_ATR,0)"
        ?},,,,,,'H');
   __infatr.win_act(_win_sel,0,'Formuła','&Legenda'@@,,,"exec('legenda','color','__infatr#02')",,,,,,'L');
   __infatr.win_act(_win_sel,0,'Rekord',,,,"exec('rekprzed','color','__infatr#02')");

   __infatr.win_act(_win_sel,0,'Wyświetl',,,,"exec('dspatrsm','magazyn_stan')");
   __infatr.win_act(_win_sel,0,'Szukaj');
   __infatr.win_act(_win_sel,0,'Kolejność');

   __infatr.tr_fml(_win_sel,,"{? _a || {? __infatr.TREE=0 || 1 || _a ?} || _a ?}");
   __infatr.win_fml(_win_sel,,'LAB',,'ICON_BEFORE',"
            {? ~__infatr.TREE & __infatr.M_ATR='+'
            || 'xwin16.png:38'
            |? ~__infatr.TREE & __infatr.M_ATR='-'
            || 'xwin16.png:111'
            || ''
            ?}
        ");
   __infatr.fld_fml('SD2','DISPLAY_FORMAT',"{? __infatr.J2='' || 'empty=1' || 'empty=0' ?}");
   __infatr.win_sel(_win_sel);
   __infatr.clear();
   {? __infatr.first()
   || __infatr.select()
   || FUN.info('Brak stanu.\nWyświetlenie informacji niemożliwe.'@)
   ?}
?};

M.cntx_pop();
SM.cntx_pop();
SC.cntx_pop();
MG.cntx_pop();
OKR.cntx_pop();

{? ~_a || SM.seek(__aktref) || M.seek(__aktref) ?};
VAR_DEL.delete('__fndatr','__infatr','__aktref');
'activate:browse'


\rek_mate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja rekord na materiale dla okienek WER_ZAM?
::   WY: kod koloru
::  OLD: \rek_mate/zk.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.ZK_N().SYM;
_menu:=2+menu_pth();
{? BEER.RS=11
|| exec('zwr_stan','magazyn_stan',M.ref,BEER.RS,null,1)
|? (';RM;FM;'*_menu)>0
|| BEER.RS:=5;
   exec('zwr_stan','magazyn_stan',M.ref,BEER.RS,null,1)
|? ('|ZAM|SPR|'*BEER.ST_ZAKR)>1
|| BEER.RS:=3;
   exec('zwr_stan','magazyn_stan',M.ref,BEER.RS,null,1)
|? BEER.ZK_N().MG=null
|| BEER.RS:={? BEER.ZK_N=null
            || {? ZAKR.RODZ_ZAM='S' || 2 || 9 ?}
            || {? BEER.ZK_N().T().R='Z' || 2 || 9 ?}
            ?};
   exec('zwr_stan','magazyn_stan',M.ref,BEER.RS,null,1,,1)
|| BEER.RS:=1;
   exec('zwr_stan','magazyn_stan',M.ref,BEER.RS,ZK_N.MG,1)
?};
ZMIENNE.MGAT:=M.MGAT().KOD;
ZMIENNE.MODM:=M.MODM().KOD;
BEER.BADSEH:=exec('FindInSet','#table','M_DOD','M_DOD',M.ref(),exec('firma','ustawienia'),"M_DOD.BADSEH",,,null());
VAR.DEFSTATS:=exec('statsM','statexam',M.ref());
exec('ustawJMwym','jm');
exec('rekprzed','color','M#02')


\sprfndsm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: sprawdza czy cokolwiek podano
::   WY: ''-podano 'F01'-nie podano
::  OLD: \sprfndsm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='F01';
_i:=0;
{!
|? _i+=1;
   {? form(($('__fndatr.F'+form(_i,-2,0,'99')))())<>''
   || _wyn:='';
      0
   || _i<10
   ?}
!};
{? _wyn='F01' || FUN.info('Nie podano żadnych wartości atrybutów\nWyszukiwanie niemożliwe.'@) ?};
_wyn


\goto_mat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: ustawienie sie na odpowiednim rekordzie
::  OLD: \goto_mat/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
_refm:=exec('FindAndGet','#table','M',__infatr.SQL,,,null());
{? _refm<>null
|| {? (3+($__aktref))='stm'
   || SM.cntx_psh();
      SM.index('SM');
      SM.prefix(ST.MAG);
      {? SM.find_key(_refm) || __aktref:=SM.ref() ?};
      SM.cntx_pop()
   || __aktref:=_refm
   ?}
?};
sel_exit()


\dspatrsm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: wyswietla atrybuty
::  OLD: \dspatrsm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
DK_C.cntx_psh();
DK_C.clear();
{? (5+__infatr.M_ATR)='dokce' & DK_C.seek(__infatr.M_ATR)
|| _atrmjs:=ATR.MJS;
   ATR.MJS:='M_ATR';
   DK_C.M_ATR().SYM;
   {! _i..10 |! ($('ATR.WAR'+form(_i,-2,0,'99')))():=__infatr[_i+3] !};
   DK_C.win_edit('RED_ATR');
   DK_C.display();
   ATR.MJS:=_atrmjs
?};
DK_C.cntx_pop();
~~


\btninfm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: szczegoly danego indeksu materialowego
::  OLD: \btninfm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('info_zam','magazyn_stan',BEER.RS);
'activate:browse'


\btnsumm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: suma wg jednostek zaznaczonych rekordow
::  OLD: \btnsumm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__sum_sm','__sumwin');

__sum_sm:=tab_tmp(1,'JM','STRING[10]','jm'@
           ,'S','REAL',''
           ,'W','REAL',''
           ,'R','REAL',''
           ,'P','REAL',''
           ,'Z','REAL',''
           ,'SP','REAL',''
           ,'D','REAL',''
           ,'SD','REAL','');
:: okienko podsumy
__sumwin:=__sum_sm.mk_sel('Razem'@,,0,'podsumsm',,,,,'U');
__sum_sm.win_fld(__sumwin,,'JM',,,-8,,,'jm'@);
__sum_sm.win_fld(__sumwin,,'S',,,-12,3,,'Stan'@);
__sum_sm.win_fld(__sumwin,,'W',,,-12,3,,'W wydaniu'@);
__sum_sm.win_fld(__sumwin,,'R',,,-12,3,,'Zarezerwowano'@);
__sum_sm.win_fld(__sumwin,,'SP',,,-12,3,,'W produkcji'@,,'Stan według zleceń produkcyjnych'@);
__sum_sm.win_fld(__sumwin,,'D',,,-12,3,,'W drodze'@);
__sum_sm.win_fld(__sumwin,,'SD',,,-12,3,,'Dostępne'@);
__sum_sm.win_sel(__sumwin);

exec('odsw_sum_jm','magazyn_stan');
M.sel_adel();
__sum_sm.select();

VAR_DEL.delete('__sum_sm','__sumwin');
'activate:browse'


\btnatrm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: stan wg atrybutow dostaw
::  OLD: \btnatrm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('btnatrsm','magazyn_stan',1)


\btnfndm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: wyszukiwanie wg atrybutow dostaw
::  OLD: \btnfndm/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
exec('btnfndsm','magazyn_stan',1)


\zam_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: stany magazynowe wg zamowien / wg magazynow / wg zamowienia
::       w zaleznosci od akcji rekord na wywolanym okienku
::   WE: [_a] czy maja byc wyswietlane pozycje zamowienia
::           (redakacja pozycji zamowienia oraz wlaczona funkcja wybor)
::            0 - bez pozycji zamowienia (domyslnie)
::            1 - redakcja pozycji
::       [_b] czy pokazywac okienko z rezerwacjami tymczasowymi 1-tak(domyslnie) 0-nie
::            jeżeli _a=1 i_b=1, to okno rezerwacji tymczasowych jest dostępne tylko jako akcja okna M.WER_ZAM
::       [_c] miejsce wywoławania:
::              0-ALL-wsz (domyślnie)
::              1-SPR-sprzedaż
::              2-ZAM-zamówienia
::       [_d] 1-przy dołączeniu pozycji zamówienia, 0-nie (domyślnie)
::  OLD: \zam_stan/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};
{? _>=2  || {? type_of(_b)<>1 || _b:=1 ?}  || _b:=1  ?};
{? _>=3  || {? type_of(_c)<>1 || _c:=0 ?}  || _c:=0  ?};

_addpoz:={? var_pres('_d')=type_of(0) || _d || 0 ?};

_hdr:=_c=2;
_czypal:=ZK_N.MG().PAL='T';
POMOC.RODZ:='T';
M.win_edit('RED');
ZK_N.MG:=null;
DPOZ.DT:=date();
POMOC.MGR:=null();
_beersz:=BEER.SZ;
BEER.SZ:='S';

_par:={? _c=0 || 'ALL'
      |? _c=1 || 'SPR'
      |? _c=2 || 'ZAM'
      || ''
      ?};
{? ~exec('samgdost','mat_atr',_par)
|| M.btn_sopt('STANY','ATRYB','state=grayed');
   M.btn_sopt('STANY','FIND','state=grayed')
|| M.btn_sopt('STANY','ATRYB','state=normal');
   M.btn_sopt('STANY','FIND','state=normal')
?};

{? _a=0 || BEER.TYP:='T' ?};
M.index('ARODZ');
M.prefix('T','T');
{? _par<>'' | _a=1
|| M.actions('STANY',{? KSTE.CT<>'T'|| 'C' || '' ?}
    +{? ~(exec('FindInSet','#table','M_ATR','TYP','B')<>null()) || 'B' || '' ?}
    +{? ~_hdr || 'P' || '' ?})
?};
{? _a=0
|| BEER.ZK_N:=null;
   ZAKR.RODZ_ZAM:='S';
   BEER.WG:='';
   M.actions('STANY',{? KSTE.CT<>'T'|| 'C' || '' ?}
      +{? ~(exec('FindInSet','#table','M_ATR','TYP','B')<>null()) || 'B' || '' ?}+{? ~_hdr || 'P' || '' ?}+'W:W')
?};

{? _a=1
|| MZ.actions('WERZ','WDUPdup:Dd','Z')
|| MZ.actions('WERZ',,'?:?')
?};

{? _a=1 || ZK_P.actions('ZAM_PRZ'+{? BEER.ZK_N().T().R='W' || 'W' || '' ?},{? _czypal || 'CEW' || '' ?}) ?};

_sel:=M.grp_make('Stany magazynowe'@,,'addzkp_'+{? BEER.ZK_N().T().R='Z' || 'spr' || 'wew' ?});

__okn:=exec('addsmmag','magazyn_stan');
__okt:=exec('addzk_pt','magazyn_stan',1);

{? _a=0
||
   _grp:={? _b
         || "grp_disp(__smmag,__okn);
             exec('tymc_prf','magazyn_stan',M.ref
              ,{? 2+menu_pth()='RM' & (';FM'*menu_pth())>0 || '' |? 'FS|ZK'*(2+menu_pth()) || 'MG' || 'ZAM'?})"
         || "grp_disp(__smmag,__okn)"
         ?}
||
   _grp:= {? _czypal
          || "grp_disp(__smmag,__okn);
              exec('tymc_pr2','magazyn_stan');
              exec('odswstsl','magazyn_stan')"
          || "grp_disp(__smmag,__okn);
               exec('tymc_pr2','magazyn_stan')"
          ?}
?};

_bez:='';
{? _addpoz
|| _bez+='EJ'
?};
{? ~_b | ~_a
|| _bez+='T'
?};
M.actions('WER_ZAM',_bez);

M.grp_sel(_sel,M,'WER_ZAM',,_grp,,,,,,,,'maximized_with_title');

{? _a=1
|| M.grp_splt(_sel,'panel0','horizontal','tab5',20);
   M.grp_sel(_sel,ZK_P,'ZAM_PRZ'+{? BEER.ZK_N().T().R='W' || 'W' || '' ?},,,,,
    ,"M.cntx_psh();ZK_P.index('TYPN');ZK_P.prefix('A','Z',BEER.ZK_N,1);
    ZK_P.last();_poz:=ZK_P.POZ;ZK_P.first();ZK_P.find_key(_poz-5)","M.cntx_pop()",,,'maximized_with_title')
|| {? _b
   || M.grp_splt(_sel,,'horizontal','tab3',28);
      M.grp_sel(_sel,__zk_pt,__okt,,,,,,,,,,'maximized_with_title')
   ?}
?};

M.grp_splt(_sel,'panel0','vertical','tab2',',80%');
{? _a=1 & _czypal
|| M.grp_sel(_sel,__smmag,__okn,'Stany wg magazynów'@,,,,,,,,,'maximized');
   M.grp_sel(_sel,SL,'STP','Stany wg wymiarów'@,,,,,"exec('odswsts2','magazyn_stan')",,,,'maximized')
|| M.grp_sel(_sel,__smmag,__okn,,,,,,,,,,'maximized_with_title')
?};

M.win_sel(_sel);
{? _hdr
|| M.hdr_sel();
   M.hdr_sel(' (WIDOK WG UPRAWNIEŃ ZAMÓWIEŃ SPRZEDAŻY)'@)
?};
_wyn:=M.select(,{? _a=1 || M.seek(BEER.M) || M.seek(M.ref()) ?},30);
{? BPMN.END=2 || sel_exit(); _wyn:=0 ?};
M.actions('WER_ZAM','');
{? _a=1 || ZK_P.actions('ZAM_PRZ'+{? BEER.ZK_N().T().R='W' || 'W' || '' ?},'') ?};
BEER.SZ:=_beersz;
_wyn


\addsmmag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: tworzenie tabeli przechowujacej stan dostepny w podziale na magazyny
::  OLD: \addsmmag/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
_nodefine:=var_pres('__smmag')>117 & __smmag.fld_acr(1)='MAG';

{? ~_nodefine
|| VAR_DEL.delete('__smmag');
   __smmag:=tab_tmp(1,'MAG','STRING[10]','Magazyn'@
             ,'STAN','REAL','Stan'@
             ,'RSQL','STRING[16]',''
             ,'STA2','REAL','Dostępne 2'@
             ,'J2','STRING[10]','jm 2'@
             ,'AKT','STRING[1]','');

   _win_sel:=__smmag.mk_sel('Stany wg magazynów'@,'P',,'jsyajdufhfteoka',,,,,'U');

   __smmag.win_fld(_win_sel,,'MAG',,,12,,,'Magazyn'@);
   __smmag.win_fld(_win_sel,,'STAN',,,16,3,,'Dostępne'@);
   __smmag.win_act(_win_sel,0,'Kolejność');
   __smmag.fld_fml('STAN','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $M.DOKL || $ST.DOKL ?}");
   __smmag.fld_fml('STAN','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $M.DOKL || $ST.DOKL ?}");
   __smmag.win_sel(_win_sel)
|| _win_sel:=__smmag.win_sel('?')
?};
_win_sel


\sc_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: uzupelnianie pola  z info. dla tabeli SC
::   WE: [_a] - magazyn (domyslnie z tabeli SC)
::       [_b] - material (domyslnie z tabeli SC)
::       [_c] - rdk (domyslnie z tabeli SC)
::       [_d] - ndk (domyslnie z tabeli SC)
::       [_e] - cena (domyslnie z tabeli SC)
::       [_f] - stan dostawy (domyslnie z tabeli SC)
::       [_g] - czy dodac do informacji date dostawy 0/1 (domyslnie 0-nie)
::  OLD: \sc_info/magazyn2.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>7 || _a:=SC.MAG ?} || _a:=SC.MAG ?};
{? _>=2 || {? type_of(_b)<>7 || _b:=SC.M ?} || _b:=SC.M ?};
{? _>=3 || {? type_of(_c)<>1 || _c:=SC.RDK ?} || _c:=SC.RDK ?};
{? _>=4 || {? type_of(_d)<>2 || _d:=SC.NDK ?} || _d:=SC.NDK ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=SC.C ?} || _e:=SC.C ?};
{? _>=6 || {? type_of(_f)<>1 || _f:=SC.S ?} || _f:=SC.S ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=0 ?} || _g:=0 ?};

_mag:=_a;
{? _=0
 & (_nd:=exec('FindAndGet','#table',DK,SC.PRDK,,"$N",'');
    _nd<>'' & (_mag:=exec('FindAndGet','#table',ND,_nd,,"MAG",_a);_mag)<>_a)
|| _c:=exec('FindAndGet','#table',DK,SC.PRDK,,"RDK",'');
   _d:=exec('FindAndGet','#table',DK,SC.PRDK,,"NDK",'');
   _przes:=1
?};

_stdst:=var_pres('__nrz')>0;
{? _stdst & __nrz=1 || return(0) ?};
ND.cntx_psh();
DK.cntx_psh();
DK_C.cntx_psh();

_czydst:=0;
DISP.DOST:='';
DISP.SC_P:=DISP.SC_R:=DISP.SC_S:=0;
DISP.DK_AT:=null();
{? _c<>0
||
   _msk:=DK.name;
   ND.cntx_psh;
   DK.cntx_psh;
   ND.use('nagdo'+(_d+3));
   DK.use(_d);
   DK.index('DOST3');
   DK.prefix(_mag,_b,_c,_d,'T');
   {? DK.first()
   ||
      DISP.DK_AT:=DK.DK_AT;
:: aktualne parametry badań
      {? ATR.CZY_ATR & exec('get_par','#parametr',141)='T'
       & exec('get','#params',540101,2,null())='T'
       & type_of(cur_tab())<>type_of(~~) & cur_tab=SC
       & (1+SC.MAG().TYP)='D' & SC.MAG().SKL<>'T'
      || exec('sc_badp_sel','magazyn_stan',DK.name,#DK.ref)
      ?};
      {!
      |?
         {? DK.RDK=#DK.ref() & DK.NDK=DK.name()
         ||
            DISP.DOST:={? _g || 'data dost. '+form(DK.DOST)+' dok. ' || '' ?}+DK.N().KH().KOD+' '+DK.N().TYP().T;
            DISP.DOST+=' - '+$DK.N().NR+' poz.'+$DK.P
         ?};

         DISP.SC_P+=exec('koszt','magdok_poz');
         DK.next
      !}
   ?};
   ND.use('nagdo'+(_msk+3));
   DK.use(_msk);
   DK.cntx_pop;
   ND.cntx_pop;

   BO.CEN:=exec('cena_mag','ceny',_b,_c,_d,,_e,3);
   {? BO.CEN<>_e
   || BO.NAZ:='Cena po korekcie'@
   || BO.NAZ:='Cena'@
   ?};

   DISP.SC_R:=exec('oblkoszr','magazyn_stan');
   DISP.SC_S:=DISP.SC_P+DISP.SC_R;
   {? ~_stdst
   || DISP.ILWW:=exec('obl_ssc','magazyn_stan',,,,,'S');
      DISP.ILR:=exec('obl_rsc','magazyn_stan');

      _stad:=exec('FindInSet','#table','SM','SM',_b,_a,"SM.SD",,,0);

      DISP.SD:=_f-DISP.ILR-DISP.ILWW+{? HELP.POP & HELP.RDK=_c & HELP.NDK=_d || HELP.IL || 0 ?};

      {? DISP.SD<0 || DISP.SD:=0 ?};
      {? DISP.SD>_stad || DISP.SD:=_stad ?}
   ?}
?};

DK.cntx_pop();
ND.cntx_pop();
SC.DK_C().M_ATR().SYM;
{? SC.DK_C().M_ATR<>null & SC.WAR01=''
|| {! _i:=1..10
   |! ($('SC.WAR'+form(_i,-2,,'99')))():=($('SC.DK_C().WAR'+form(_i,-2,,'99')))()
   !}
?};
:: atrybuty dostawy
{? ~_stdst & ATR.CZY_ATR
   & type_of(cur_tab())<>type_of(~~) & cur_tab=SC
   & (1+SC.MAG().TYP)='D'
   & params_get().PANEL
|| exec('matr_sc','magazyn_stan',$SC.DK_C)
?};
DK_C.cntx_pop();
DISP.JM:=SC.M().J;
_no_et:={? ~exec('isDEFetyk','magazyn_mobi','SC') || 'I' || '' ?};
SC.actions_grayed('WER',_no_et);
exec('rekprzed','color','SC#01')


\sc_badp_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [12.30]
:: OPIS: Odszukuje aktualne parametry badania do dostawy
::   WE: _a - DK.name
::       _b - #DK.ref
::  OLD: \sc_badp_sel/statexam.fml
::----------------------------------------------------------------------------------------------------------------------
_nam:={?_>0 & type_of(_a)=2 || _a || return(-1) ?};
_ref:={?_>1 & type_of(_b)=1 || _b || return(-2) ?};

{? _nam='' | _ref=0
|| _ref:=#DK.ref();
   _nam:=DK.name()
?};

{? DK.size()
|| _dkhs:=exec('lastDK_HS','statexam',_nam,_ref);
   {? _dkhs<>null
   || DK_HS.cntx_psh();
      {? DK_HS.name()+3<>_nam+3
      || DK_HS.use((DK_HS.name()-3)+(_nam+3))
      ?};
      DK_HS.clear();
      {? DK_HS.seek(_dkhs)
      || {? DK_HS.BADH<>null
         || BADP.index('BADHBPAR');
            BADP.prefix(DK_HS.BADH);
            BADP.first()
         || BADP.index('BADHBPAR');
            BADP.prefix(null)
         ?}
      ?};
      DK_HS.cntx_pop()
   || BADP.index('BADHBPAR');
      BADP.prefix(null)
   ?}
|| BADP.index('BADHBPAR');
   BADP.prefix(null)
?};
grp_disp(BADP,'WERDKR')


\matr_sc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [12.30]
:: OPIS: Odświeża okno z atrybutami na SC
::  OLD: \matr_sc/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('get_par','#parametr',141)='T' & _a<>''
|| DK_C.use(8+_a);
   DK_C.clear();
   DK_C.seek(_a);
   DK_C.index('SYM');
   DK_C.prefix(DK_C.SYM,DK_C.SYM);
   DK_C.M_ATR().SYM
|| DK_C.clear();
   DK_C.index('M_ATR');
   DK_C.prefix(null,'','',);
   {! _i..10 |! ($('M_ATR.SL_'+form(_i,-2,0,'99')))():=null !}
?};
grp_edisp(DK_C,'DISPR')


\dk_cechy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wyswietlanie cech dla stanow magazynowych
::  OLD: \dk_cechy/magazyn2.fml
::----------------------------------------------------------------------------------------------------------------------
{? SC.DK_C<>null
|| {? SC.M().M_ATR=null
   || SC.DK_C().NAZ;
      DK_C.memo_get(,'O');
      DK_C.memo_view(,'O')
   || exec('matr_sc','mat_atr')
   ?}
||
   FUN.info('Brak cechy dla dostawy.'@)
?};
''


\sc_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: wyswietla dokumenty magazynowe dla SC
::  OLD: \sc_dok/magazyn2.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__dok_mag')>100 || obj_del(__dok_mag) ?};
__dok_mag:=tab_tmp(3,
   'TYP','STRING[10]','Typ'@,
   'NR','INTEGER','Numer'@,
   'SYM','STRING[20]','Symbol'@,
   'D','DATE','Data'@,
   'POZ','INTEGER','Pozycja'@,
   'IL','REAL','Ilość'@,
   'C','REAL','Cena'@,
   'WAR','REAL','Wartość'@,
   'DOST','DATE','Data dostawy'@,
   'PLUS','STRING[1]','Przychodowy'@,
   'POW','STRING[1]','Powiązany'@,
   'DA','DATE','Data akceptacji'@,
   'TA','TIME','Czas akceptacji'@,
   'DKREF','INTEGER','ref'@,
   'DKNAME','STRING[8]','name'@,
   'NDREF','INTEGER','ref'@,
   'NDNAME','STRING[8]','name'@);
exec('war_hid','ceny',__dok_mag,'C','WAR');

__dok_mag.fld_attr('DKREF',2);
__dok_mag.fld_attr('DKNAME',2);
__dok_mag.fld_attr('NDREF',2);
__dok_mag.fld_attr('NDNAME',2);

_only:=SC.MAG().IL='T';
_etykieta:={? _only || 'msyafksfowakrdi' || 'msyafksfowakrds' ?};
_win_sel:=__dok_mag.mk_sel('Dokumenty magazynowe wg stanu'@,,0,_etykieta,,,,,'U','T');
__dok_mag.win_fld(_win_sel,,'TYP',,,10);
__dok_mag.win_fld(_win_sel,,'NR',,,8);
__dok_mag.win_fld(_win_sel,,'SYM',,,22);
__dok_mag.win_fld(_win_sel,,'D',,,10);
__dok_mag.win_fld(_win_sel,,'POZ',,,4);
__dok_mag.win_fld(_win_sel,,'IL',,,12,3);
{? SC.MAG().IL<>'T'
|| __dok_mag.win_fld(_win_sel,,'C',,,12,2);
   __dok_mag.win_fld(_win_sel,,'WAR',,,12,2)
?};

__dok_mag.fld_fml('C','DISPLAY_FORMAT',"'out_prec='+$ST.DOKL_C");
__dok_mag.fld_fml('IL','DISPLAY_FORMAT',"'out_prec='+$ST.DOKL");

__dok_mag.win_act(_win_sel,,'Formuła','Pokaż &dokument'@@,,,"
   ND.cntx_psh();DK.cntx_psh();DK_L.cntx_psh();
   DK.use(__dok_mag.DKNAME);ND.use(__dok_mag.NDNAME);DK_L.use('doklk'+(__dok_mag.DKNAME+3));
   DK.clear();
   {? DK.seek(__dok_mag.DKREF,)
   || DK.N().SYM;
      exec('disp_dk_pom','magdok_wspolne',1)
   ?};
   DK.cntx_pop();ND.cntx_pop();DK_L.cntx_pop()",,1);
__dok_mag.win_act(_win_sel,,'Szukaj');
__dok_mag.win_act(_win_sel,,'Kolejność');
__dok_mag.win_act(_win_sel,,'Rekord',,,,"
   _wyn:='';
   ND.cntx_psh();DK.cntx_psh();DK_L.cntx_psh();
   DK.use(__dok_mag.DKNAME);ND.use(__dok_mag.NDNAME);DK_L.use('doklk'+(__dok_mag.DKNAME+3));
   DK.clear();
   {? DK.seek(__dok_mag.DKREF,)
   || _wyn:=exec('szuk_n','magdok_wspolne')
   ?};
   DK.cntx_pop();ND.cntx_pop();DK_L.cntx_pop();
   _wyn");
__dok_mag.win_act(_win_sel,,'Formuła','&Legenda'@@,,,"exec('legenda','color','DK#01')",,,,,,'L');
__dok_mag.win_sel(_win_sel);

ND.cntx_psh();
DK.cntx_psh();
DK_L.cntx_psh();

OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first
||
   {!
   |?
      ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));
      DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
      DK_L.use('doklk'+ST.ODDZ+($OKR.ROK+2));
      DK.index('DK_SC');
      DK.prefix(SC.MAG,SC.M,SC.RDK,SC.NDK);
      {? DK.first()
      ||
         {!
         |?
            __dok_mag.TYP:=DK.N().TYP().T;
            __dok_mag.NR:=DK.N().NR;
            __dok_mag.SYM:=DK.N().SYM;
            __dok_mag.D:=DK.N().D;
            __dok_mag.POZ:=DK.P;
            __dok_mag.IL:=DK.IL;
            __dok_mag.C:=DK.C;
            __dok_mag.WAR:=DK.WAR;
            __dok_mag.DOST:=DK.DOST;
            __dok_mag.PLUS:=DK.PLUS;
            __dok_mag.POW:=DK.POW;
            __dok_mag.DA:=DK.N().DA;
            __dok_mag.TA:=DK.N().TA;
            __dok_mag.DKREF:=#DK.ref;
            __dok_mag.DKNAME:=DK.name;
            __dok_mag.NDREF:=#DK.N;
            __dok_mag.NDNAME:=DK.N().name;
            __dok_mag.add();
            DK.next
         !}
      ?};
      OKR.next
   !}
?};
OKR.cntx_pop();
_ndx:=__dok_mag.ndx_tmp(,,'D',,,'DA',,,'TA',,,'POW',,1,'PLUS',,1);
__dok_mag.index(_ndx);
__dok_mag.select();
__dok_mag.ndx_drop(_ndx);
ND.cntx_pop();
DK.cntx_pop();
DK_L.cntx_pop();
''


\sc_koszt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: podglad kosztow dostawy
::  OLD: \sc_koszt/koszty.fml
::----------------------------------------------------------------------------------------------------------------------
ND.cntx_psh(); DK.cntx_psh(); FAKS_K.cntx_psh(); FAP_K.cntx_psh();
FAKS_K.use('fakkk'+(SC.NDK+3));
FAP_K.use('fapkk'+(SC.NDK+3));
ND.use('nagdo'+(SC.NDK+3));
DK.use(SC.NDK); DK.prefix();
FAP_K.index('DKSQL'); FAP_K.prefix(SC.SRDK); FAP_K.first();

DISP.DOK_MAG:={? DK.seek(SC.RDK,) || '%1: %2, z dnia %3 poz.%4'@[DK.N().TYP().T,DK.N().SYM,$DK.N().D,$DK.P] || '' ?};

FAP_K.win_sel('WER');
_act:=FAP_K.actions('WER','dpu:d');
{? SC.SCWAL<>null || FAP_K.hdr_sel(' (waluta %1)'@[SC.SCWAL().KOD]) ?};
FAP_K.select();
FAP_K.actions('WER',_act);
ND.cntx_pop(); DK.cntx_pop(); FAKS_K.cntx_pop(); FAP_K.cntx_pop();
1


\rek_mx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja rekord dla tabeli MX
::  OLD: \rek_mx/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
exec('obl_stan','magazyn_stan',MX.M,1,ST.MAG);
exec('rekprzed','color','M#01')


\sm_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: Rekord przed dla okienek wertowania tabeli SM
::  OLD: \sm_kol/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
_min:=0;
_max:=0;
MST.index('MG');
MST.prefix(SM.M,ST.MAG);
{? MST.first()
||
   _min:=MST.MIN;
   _max:=MST.MAX
||
   MST.prefix(SM.M,null);
   {? MST.first
   ||
      _min:=MST.MIN;
      _max:=MST.MAX
   ?}
?};
{? SM.S=0                 || 'SM#01#01'
|?  SM.S<_min & _min<>0   || 'SM#01#02'
|?  SM.S>_max & _max<>0   || 'SM#01#03'
|| ''
?}


\sc_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: kolorowanie dostaw
::  OLD: \sc_kol/magazyn2.fml
::----------------------------------------------------------------------------------------------------------------------
{? BO.CEN<>SC.C
|| 'SC#01#01'
|| 'SC#01#02'
?}


\obl_std
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2009]
:: OPIS: oblicza stan w magazynach - dla zamowien z wypelnionym magazynem
::  OLD: \obl_std/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=exec('FindAndGet','#table',FUX.TFUX,FUX.FUX,,,null());
{? HELP.WMAG=2 & ZD_POZ.MG=null
|| exec('obl_stan','magazyn_stan',_ref,2);
   BEER.SD
|? HELP.WMAG=2 & ZD_POZ.MG<>null
|| exec('obl_stan','magazyn_stan',_ref,1,ZD_POZ.MG);
   BEER.SD
|| 0
?}


\suma_sl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: podsumowanie stanu na SL-u
::  OLD: \suma_sl/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_dokl:=exec('jaka_dok_m','jm',SL.M);
SL.cntx_psh();
{? __onemg
|| _wyn:=exec('FindInSet','#table','SM','SM',SL.M,SL.MG,"SM.S",,,0)
|| _wyn:=0;
   MG.cntx_psh();
   MG.index('SL');
   MG.prefix(ST.ODDZ,'T');
   {? MG.first() || {! |? _wyn+=exec('FindInSet','#table','SM','SM',SL.M,MG.ref(),"SM.S",,,0); MG.next() !} ?};
   MG.cntx_pop()
?};
SL.cntx_pop();
EANX.SLSUM:=_wyn $ _dokl;
~~


\kol_tow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: kolorowanie w słowniku materiałów
::  OLD: \kol_tow/magazyn1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
UD_SKL.cntx_psh();
{? cur_tab(1)=M
|| UNPAR.P10:=M.CPV().SYMBOL;
   UNPAR.P11:=M.POZ_BUD().SYMBOL
?};
UD_SKL.cntx_pop();
_mref:={? cur_tab(1)=M || M.ref() |? cur_tab(1)=MX || MX.M || null() ?};
{? _mref
||
   POM.SKLAD:=exec('m_sklad_','material');
   {? POM.SKLAD<>0
   ||
      _wyn:='M#01#03'
   |?
      MDOST.cntx_psh();
      MDOST.index('M');
      MDOST.prefix('D',_mref);
      _log:=MDOST.first();
      MDOST.cntx_pop();
      _log
   ||
      _wyn:='M#01#01'
   ||
      DOKUM.cntx_psh();
      DOKUM.index('DOKUM');
      POLA.REFSQL:=$_mref;
      DOKUM.prefix(REF.FIRMA,POLA.REFSQL);
      {? DOKUM.first()
      || _wyn:={? M.RODZ='T' || 'M#01#02' || 'M#05#01' ?}
      ?};
      DOKUM.cntx_pop()
   ?}
?};
_wyn


\kol_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: kolorowanie zbiorczego przegladu stanow magazynowych
::  OLD: \kol_m/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? BEER.SD=0
|| _wyn:='M#02#01'
|| _wyn:='M#02#02'
?};
_wyn


\czymdost
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: sprawdza czy dla danej pozycji jest stan w magazynie dostaw
::   WY: 1-jest 0-brak
::  OLD: \czymdost/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

{? ~(var_pres('__smmag')>100) || exec('addsmmag','magazyn_stan') ?};
{? ZK_N.MG=null
|| BEER.RS:={? ZK_N.T().R='Z' || 2 || 9 ?};
   exec('obl_stan','magazyn_stan',ZK_P.M,BEER.RS,,1)
|| BEER.RS:=1;
   exec('obl_stan','magazyn_stan',ZK_P.M,BEER.RS,ZK_N.MG,1)
?};

__smmag.cntx_psh();
__smmag.prefix();
{? __smmag.first()
|| {!
   |? MG.cntx_psh();
      MG.prefix();
      {? MG.seek(__smmag.RSQL) || _wyn:=(1+MG.TYP)='D' ?};
      MG.cntx_pop();
      ~_wyn & __smmag.next()
   !}
?};
__smmag.cntx_pop();
_wyn


\aktu_zam_sm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: aktualizuje tabele stanow magazynowych dla na podstawie uprawnien operatora zamowien
::       dla indeksu materialowego w biezacej pozycji zamowienia
::   WE:  _a  - indeks materialowy
::        [_b]  - MG.ref
::  OLD: \aktu_zam_sm/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_b:={? _>=2 || {? var_pres('_b')=7 || _b || null ?} || null ?};

{? _b<>null
||
   _mag:=obj_new(1); _mag[1]:=_b; _i:=_ile:=1
||
   _i:=0;
   _ile:=0;

   _magik:=tab_tmp(1,'MG','STRING[16]','');

   USERS_UP.index('MG');
   USERS_UP.prefix(null,'ZAM',ST.ODDZ);
   {? USERS_UP.first()
   || {!
      |? _magik.clear();
         {? ~_magik.find_key($USERS_UP.MG)
         || _magik.blank();
            _magik.MG:=$USERS_UP.MG;
            _magik.add(1)
         ?};
         USERS_UP.next()
      !}
   ?};

   USERS_UP.index('MG');
   USERS_UP.prefix(null,'ZAW',ST.ODDZ);
   {? USERS_UP.first()
   || {!
      |? _magik.clear();
         {? ~_magik.find_key($USERS_UP.MG)
         || _magik.blank();
            _magik.MG:=$USERS_UP.MG;
            _magik.add(1)
         ?};
         USERS_UP.next()
      !}
   ?};
   _magik.clear();
   {? _magik.size() & _magik.first()
   || _ile:=_magik.size();
      _mag:=obj_new(_ile);
      {!
      |? _i+=1;
         _mag[_i]:=exec('FindAndGet','#table','MG',_magik.MG,,,null());
         _magik.next()
      !}
   ?};
   obj_del(_magik)

?};

{? _i>0
||
   _buf:=0;
   {! _i.._ile
   |! {? exec('FindInSet','#table','SM','SM',_a,_mag[_i])=null()
      || SM.cntx_psh();
         SM.prefix();
         SM.blank();
         SM.MAG:=_mag[_i];
         SM.M:=_a;
         SM.KTM:=SM.M().KTM;
         SM.NKTM:=SM.M().N;
         SM.M_ATR:=SM.M().M_ATR_B;
         {! _wsk:=1..10 |! ($('SM.WAR'+form(_wsk,-2,0,'99')))():=($('SM.M().WAR'+form(_wsk,-2,0,'99')))() !};
         SM.MGR:=SM.M().MGR;
         SM.MGRP:=SM.M().MGRP;
         SM.add(1);
         SM.cntx_pop()
      ?};
      exec('obl_stan','magazyn_stan',_a,1,_mag[_i])
   !}
?};
''


\dolmgzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: dolacz po - magazyn do realizacji zamowien sprzedazy
::  OLD: \dolmgzam/defin.fml
::----------------------------------------------------------------------------------------------------------------------
SM.cntx_psh(); USERS_UP.cntx_psh();
SM.index('SA'); SM.prefix(USERS_UP.MG);
{? SM.first() || {! |? exec('aktu_zam_sm','magazyn_stan',SM.M,USERS_UP.MG); SM.next() !} ?};
SM.cntx_pop(); USERS_UP.cntx_pop();
exec('usr_upr_uzupełnij_pelne','users_upraw','ZAM');
exec('usr_upr_uzupełnij_pelne','users_upraw','ZAW');
1


\delmgzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: usuniecie rekordu z uprawieniami dla zamowien - powoduje automatyczne usuniecie uprawnien dla uzytkownikow
::  OLD: \delmgzam/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_mg:=USERS_UP.MG;
_wew:=USERS_UP.AKR='ZAW';
{? FUN.ask('Czy usunąć bieżący rekord?'@)
|| USERS_UP.cntx_psh();
   exec('deluszam','users',_mg,_wew);
   _mg:=USERS_UP.MG;
   _ok:=USERS_UP.del(,1);
   _ref:=USERS_UP.ref();
   {? _ok>0
   ||
::    kasuje rezerwacje z zamowien
      exec('mgrez','zamsiw_wspolne',_mg);
      SM.cntx_psh();
      SM.index('SA'); SM.prefix(_mg);
      {? SM.first() || {! |? exec('aktu_zam_sm','magazyn_stan',SM.M,_mg); SM.next() !} ?};
      SM.cntx_pop();
::    przenumerowuje priorytety
      {? USERS_UP.first()
      || _nr:=0;
         {!
         |? _nr+=1;
            USERS_UP.NR:=_nr;
            USERS_UP.put(1);
            USERS_UP.next()
         !}
      ?}
   ?};
   USERS_UP.cntx_pop();
   USERS_UP.seek(_ref)
?}


\addzk_pt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: tworzenie tabeli przechowujacej zamowienia tymczasowe
::   WE: _a - czy pokazywac kontrahenta
::  OLD: \addzk_pt/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__zk_pt')>100 || obj_del(__zk_pt) ?};
__zk_pt:=tab_tmp(1,'DAT','DATE','Data'@
          ,'KHK','STRING[10]','Kontrahent-Kod'@
          ,'KHS','STRING[10]','Kontrahent-Skrót'@
          ,'KHN','STRING[150]','Kontrahent-Nazwa'@
          ,'KTM','STRING[50]','Indeks-KTM'@
          ,'NAZ','STRING[100]','Indeks-Nazwa'@
          ,'DNI','INTEGER','Dni'@
          ,'ILE','REAL','Ilość'@
          ,'MAG','STRING[10]','Magazyn'@
          ,'USR','STRING[20]','Kto rez.'@
          ,'REZ','STRING[16]','Ref SQL REZ'@
          ,'DST','STRING[100]','Dostawa'@);

_win_sel:=__zk_pt.mk_sel('Rezerwacje tymczasowe'@,'P',,'jsyajdufhfteqpa');

__zk_pt.win_fld(_win_sel,,'DAT',,,8,,,'Data'@);
{? _a
|| __zk_pt.win_fld(_win_sel,,'KHK',,,-8,,,'Kod kontrahenta'@);
   __zk_pt.win_fld(_win_sel,,'KHS',,,-13,,,'Skrót kontrahenta'@);
   __zk_pt.win_fld(_win_sel,,'KHN',,,-34,,,'Nazwa kontrahenta'@);
   __zk_pt.win_fld(_win_sel,,'DNI',,,7,,,'Dni'@);
   __zk_pt.win_fld(_win_sel,,'ILE',,,10,3,,'Ilość'@);
   __zk_pt.win_fld(_win_sel,,'MAG',,,10,,,'Magazyn'@);
   __zk_pt.win_fld(_win_sel,,'USR',,,15,,,'Kto zarezerwował'@);
   __zk_pt.win_fld(_win_sel,,'DST',,,12,,,'Dostawa'@)
|| __zk_pt.win_fld(_win_sel,,'DNI',,,2,,,'Dni'@);
   __zk_pt.win_fld(_win_sel,,'ILE',,,7,3,,'Ilość'@);
   __zk_pt.win_fld(_win_sel,,'MAG',,,8,,,'Magazyn'@)
?};
__zk_pt.win_act(_win_sel,,'Kolejność');
__zk_pt.win_act(_win_sel,,'Wyświetl',,,,"exec('disprezt','rezerwacje')");
__zk_pt.win_sel(_win_sel)


\tymc_prf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: ustawia zakres na okienku rezerwacji tymczasowych - wypelnia tabelke wg uprawnien
::   WE: _a - ref materialu
::       _b - zakres wg ZAM, MG
::       [_c] - kontrahent, domyslnie null
::  OLD: \tymc_prf/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=2 || _c:=null ?};
__zk_pt.erase();
M.cntx_psh();
exec('tymc_prf','rezerwacje',_a,_b,_c);
M.cntx_pop();
__zk_pt.first();
grp_disp(__zk_pt,__okt);
''


\tymc_pr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: ustawia zakres na okienku rezerwacji tymczasowych
::  OLD: \tymc_pr2/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
M.cntx_psh();
ZK_P.cntx_psh();
ZK_P.index('TYPN');
ZK_P.prefix('A','Z',BEER.ZK_N,1);
ZK_P.first();
grp_disp(ZK_P,'ZAM_PRZ'+{? BEER.ZK_N().T().R='W' || 'W' || '' ?});
ZK_P.cntx_pop();
M.cntx_pop();
''


\odswstsl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: odswieza SL-a
::  OLD: \odswstsl/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
M.cntx_psh();
SL.cntx_psh();
SL.index('M');
SL.prefix(M.ref(),ZK_N.MG);
SL.first();
grp_disp(SL,'STP');
SL.cntx_pop();
M.cntx_pop();
''


\odswsts2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: odswieza SL-a
::  OLD: \odswsts2/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
SL.index('M');
SL.prefix(M.ref(),ZK_N.MG);
~~


\obl_stz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [12.30]
:: OPIS: oblicza stan w magazynach - zamowienia sprzedazy i zamowienia wewnetrzne
::  OLD: \obl_stz/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=exec('FindAndGet','#table',FUX.TFUX,FUX.FUX,,,null());
{? ZK_P.RMAG=null
|| exec('obl_stan','magazyn_stan',_ref,{? ZK_P.RODZ='Z' || 4 || 10 ?});
   BEER.SD
|? ZK_P.RMAG<>null
|| exec('obl_stan','magazyn_stan',_ref,1,ZK_P.RMAG);
   BEER.SD
|| 0
?}


\sc_aktyw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akualizuje znacznik aktywny w bazie SC
::   WE:  _a - magazyn   np. ST.MAG
::        _b - material  np. DK.M
::  OLD: \sc_aktyw/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
SC.index('SC');
SC.prefix(_a,_b);
{? SC.first()
|| {!
   |? {? SC.S>0 || SC.A:='T' || SC.A:='N' ?};
      SC.put();
      SC.next()
   !}
?};
''


\stan_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.53]
:: OPIS: stan konkretnego materiału z M lub TMAT lub TCHMAT
::   WE: _a - z jakiej tabeli wywołano formułę (akronim - string)
::  OLD: \stan/stanymag.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='M'
||  VAR.A_T:=M.ref();
    VAR.REAL:=VAR.REAL2:=0;
    exec('stan_x','magazyn_stan',_a)
|? _a='TMAT' | _a='TCHMAT'
|| _tmat:=($_a)();
   {? _tmat.GRKTM='G'
   || VAR.A_T:=_tmat.TGDFLT().PT
   || VAR.A_T:=_tmat.PT
   ?};
   {? VAR.ACTION='Z'
   || _dokl:=exec('jaka_dok_m','jm',VAR.A_T);
      _wsp:=VAR.A_ZLEC().IL/_tmat.NRK().XJM;
      {? _dokl>0
      || {? _tmat.FORMB<>''
         || VAR.REAL:=(_wsp*tpar.calc(_tmat.FORMB))$_dokl
         || VAR.REAL:=(_wsp*_tmat.WARB)$_dokl
         ?}
      || {? _tmat.FORMB<>''
         || VAR.REAL:=ceil(_wsp*tpar.calc(_tmat.FORMB))
         || VAR.REAL:=ceil(_wsp*_tmat.WARB)
         ?}
      ?}
   || VAR.REAL:=VAR.REAL2:=0
   ?};
:: TODO:
::   exec('lim_rez','zl_zmat',VAR.A_T);
   {? _tmat.DK_C<>null()
   ||
      M.cntx_psh();
      VAR.A_T();
      VAR_DEL.delete('__smmag');
      __smmag:=tab_tmp(1,'RSQL','STRING[16]','');

      SM.cntx_psh();
      SM.index('SMM');
      SM.prefix(VAR.A_T);
      {? SM.first()
      || {!
         |? _add:=1;
            __smmag.cntx_psh();
            __smmag.prefix($SM.MAG);
            {? __smmag.first()
            || _add:=0
            ?};
            __smmag.cntx_pop();

            {? _add>0
            ||
               __smmag.blank();
               __smmag.RSQL:=$SM.MAG;
               __smmag.add(1)
            ?};
            SM.next()
         !}
      ?};
      SM.cntx_pop();
      exec('btnatrsm','magazyn_stan',1);
      M.cntx_pop()
   || exec('stan_x','magazyn_stan',_a)
   ?}
?};
~~


\stan_surx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.53]
:: OPIS: Wołane ze 'stan' i 'stan_d'
::       Kontekst wywołania: ustawione VAR.A_T i VAR.REAL
::   WY: VAR.SR_ILD oraz ustawione VAR.SR_IL, VAR.SR_ILW, VAR.SR_ILR, VAR.SR_ILG, VAR.SR_ILD
::  OLD: \stan_surx/stanymag.fml
::----------------------------------------------------------------------------------------------------------------------
SM.cntx_psh();
SM.index('SMM');
SM.prefix(VAR.A_T);
_par:=exec('get','#params',500002,2);
VAR.SR_IL:=VAR.SR_ILD:=VAR.SR_ILW:=VAR.SR_ILR:=VAR.SR_ILG:=VAR.SR_ILPR:=0;
{? SM.first()
|| {!
   |?
::    stan
      VAR.SR_IL+=SM.S;
::    w wydaniu
      VAR.SR_ILW+=SM.W;
::    rezerwacje
      VAR.SR_ILR+=SM.R;
::    w drodze
      VAR.SR_ILG+=SM.D;
::    w produkcji
      VAR.SR_ILPR+=SM.SP;
      {? _par*SM.MAG().SYM>0
      ||
::       dostępne
         VAR.SR_ILD+=SM.SD
      ?};
      SM.next()
   !}
?};
SM.cntx_pop();
VAR.SR_ILD


\stan_x
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.53]
:: OPIS: Wołane ze 'stan_p' i 'stan_d'
::       Kontekst wywołania: ustawione VAR.A_T i VAR.REAL
::   WE: _a - z jakiej tabeli wywołano formułę
::            jeżeli (_a='TMAT' lub _a='TCHMAT') i VAR.ACTION='Z' lub _a='ANAL' to okno z zapotrzebowaniem
::  OLD: \stan_x/stanymag.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=exec('get','#params',500002,2);
SM.cntx_psh();
SM.hdr_sel(': '+form(ST.AM,-2)+'/'+$ST.AR);
SM.index('SMM');
SM.prefix(VAR.A_T);
VAR.SR_IL:=VAR.SR_ILD:=VAR.SR_ILW:=VAR.SR_ILR:=VAR.SR_ILG:=VAR.SR_ILPR:=0;
{? SM.first()
|| {!
   |?
::    stan
      VAR.SR_IL+=SM.S;
::    w wydaniu
      VAR.SR_ILW+=SM.W;
::    rezerwacje
      VAR.SR_ILR+=SM.R;
::    w drodze
      VAR.SR_ILG+=SM.D;
::    w produkcji
      VAR.SR_ILPR+=SM.SP;
      {? _par*SM.MAG().SYM>0
      ||
::       dostępne
         VAR.SR_ILD+=SM.SD
      ?};
      SM.next()
   !}
?};
{? _a='ANAL'
|| exec('lim_rez','zl_limit',VAR.A_T)
?};
VAR.SR_D_DT:=exec('termin_o','zamdst_wspolne',VAR.A_T,'');
{? ((_a='TMAT' | _a='TCHMAT') & VAR.ACTION='Z') | _a='ANAL'
|| SM.win_sel('STANYP')
|| SM.win_sel('STANY')
?};
SM.win_edit();
SM.win_patt();
SM.select();
SM.cntx_pop();
~~


\stan_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.53]
:: OPIS: Stan konkretnego materiału w tabeli dynamicznej
::   WE: _a --- #M.ref() --- zawsze na konkretny towar
::       _b - zapotrzebowanie
::  OLD: \stan_d/stanymag.fml
::----------------------------------------------------------------------------------------------------------------------
M.clear();
{? M.seek(_a,) || VAR.A_T:=M.ref() || VAR.A_T:=null() ?};
VAR.REAL:=_b;
exec('lim_rez','zl_limit',VAR.A_T);
exec('stan_x','magazyn_stan','ANAL');
~~


\sm_pred
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2008]
:: OPIS: 'przed rekord' w oknie stanów SM
::  OLD: \sm_pred/stanymag.fml
::----------------------------------------------------------------------------------------------------------------------
_par:=exec('get','#params',500002,2);
{? _par*SM.MAG().SYM>0
|| VAR.ILD:=SM.SD;
   exec('rekprzed','color','SM#01#04')
|| VAR.ILD:=0;
   ''
?}


\popdkltw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: poprawa terminow waznosci w przypadku terminow zerowych
::   WE: _a - ref DK
::       _b - ref DK_LN
::  OLD: \popdkltw/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
{? _b=null
|| DK_L.index('DK');
   DK_L.prefix(_a,_b)
|| DK_L.index('DK_LN');
   DK_L.prefix('Z',_b)
?};

_tab:=tab_tmp(1,'SL','STRING[16]','','ILR','REAL','');

{? DK_L.last()
|| _spis:=DK_L.DK_LN().RODZ='I';
   {!
   |? {? {? _spis || DK_L.TWDO || DK_L.TW ?}=date(0,0,0)
      || _ild:=DK_L.IL;
         SL.index('MG');
         SL.prefix(DK_L.MG,DK_L.M,{? _spis || DK_L.LOKDO || DK_L.LOK ?});
         {? SL.first()
         || {!
            |? {? SL.TW<>date(0,0,0)
               || _tab.clear();
                  _ile:=SL.IL-{? _tab.find_key($SL.ref()) || _tab.ILR || 0 ?};
                  {? _ile>0
                  || {? _ild<=_ile
                     || {? _spis
                        || DK_L.TWDO:=SL.TW
                        || DK_L.TW:=DK_L.TWDO:=SL.TW
                        ?};
                        {? ~DK_L.put() || undo() ?};
                        _tab.clear();
                        {? _tab.find_key($SL.ref())
                        || _tab.ILR+=_ild;
                           _tab.put(1)
                        || _tab.blank();
                           _tab.SL:=$SL.ref();
                           _tab.ILR:=_ild;
                           _tab.add(1)
                        ?};
                        _ild:=0
                     || _jest:=DK_L.IL;
                        {? _spis
                        || DK_L.TWDO:=SL.TW
                        || DK_L.TW:=DK_L.TWDO:=SL.TW
                        ?};
                        DK_L.IL:=_ile;
                        exec('reoIL2','magdok_wymiary');
                        exec('uzupIDkod','magdok_palety',DK_L);
                        {? ~DK_L.put(1) || undo() ?};
                        DK_L.IL:=_jest-_ile;
                        exec('reoIL2','magdok_wymiary');
                        exec('uzupIDkod','magdok_palety',DK_L);
                        {? ~DK_L.add(1) || undo() ?};
                        _tab.clear();
                        {? _tab.find_key($SL.ref())
                        || _tab.ILR+=_ile;
                           _tab.put(1)
                        || _tab.blank();
                           _tab.SL:=$SL.ref();
                           _tab.ILR:=_ile;
                           _tab.add(1)
                        ?};
                        _ild:=_jest-_ile
                     ?}
                  ?}
               ?};
               _ild>0 & SL.next()
            !}
         ?}
      ||
::       jak termin zerowy i SL istnieje na lokalizacje i date
         SL.index('MG');
         SL.prefix(DK_L.MG,DK_L.M,{? _spis || DK_L.LOKDO || DK_L.LOK ?});
         {? SL.find_key({? _spis || DK_L.TWDO || DK_L.TW ?})
         || _tab.clear();
            {? _tab.find_key($SL.ref())
            || _tab.ILR+=DK_L.IL;
               _tab.put(1)
            || _tab.blank();
               _tab.SL:=$SL.ref();
               _tab.ILR:=DK_L.IL;
               _tab.add(1)
            ?}
         ?}
      ?};
      DK_L.prev()
   !}
?}


\wgmatmag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: aktualizuje stan wg tabeli __mat_mag
::  OLD: \wgmatmag/zkd.fml
::----------------------------------------------------------------------------------------------------------------------
__mat_mag.clear();
{? __mat_mag.first()
|| {!
   |? MG.cntx_psh();
      M.cntx_psh();
      _mag:=exec('FindAndGet','#table','MG',__mat_mag.MG,,,null());
      _mat:=exec('FindAndGet','#table','M',__mat_mag.M,,,null());
      {? _mag<>null & _mat<>null || exec('obl_stan','magazyn_stan',_mat,1,_mag) ?};
      MG.cntx_pop();
      M.cntx_pop();
      __mat_mag.next()
   !}
?};
VAR_DEL.delete('__mat_mag');
''


\lokaldok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.41]
:: OPIS: zwraca lokalizacje typu dok lub zwraca ref lokalizacji po podaniu kodu
::   WE: _a - kod lokalizacji do sprawdzenia
::       _b - ref lokalizacji domyslnej lub null()
::   WY: ref lokalizacji
::  OLD: \lokaldok/mws2.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=_b;
{? _wyn=null() | exec('FindInSet','#table','EANL','KOD',_a,_a,"EANL.DOK",,,'')<>''
|| _wyn:=exec('FindInSet','#table','EANL','KOD',_a,_a)
?};
_wyn


\odtstan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: odtwarzanie stanow oraz stanow wg lokalizacji dla wszystkich masek
::  OLD: \odtstan/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
SM.cntx_psh();
SC.cntx_psh();
SM.use('stm__'+ST.ODDZ+($ST.AR+2));
SC.use('stc__'+ST.ODDZ+($ST.AR+2));
{? FUN.ask('Odtworzyć stany magazynowe?'@)
||
   VAR_DEL.delete('LKTM');
   exec('odtstwyp','magazyn_stan');
   exec('odtstany','magazyn_stan',0);
   VAR_DEL.delete('LKTM');
   echo();prgs_clr();
   {? ST.MAG().SL='T' || exec('odtw_sl','magazyn_stan',ST.MAG) ?};
   echo();prgs_clr();
   FUN.info('Odtwarzanie stanów zakończone.'@)
?};
SC.cntx_pop();
SM.cntx_pop();
''


\odtstany
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: odtwarzanie stanow dla wszystkich dokumentow
::   WE: [_a] - czy wyswietlac pasek postepu 0 - tak 1 - nie
::  OLD: \odtstany/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('LKTM')<100 || exec('list_ktm','magazyn_stan',2) ?};

LKTM.clear;
{? LKTM.first
||
   _mi:=LKTM.size; _tp:=time(); _li:=0;
   {!
   |?
      _li+=1;
      {? _a=0
      || progress(_li/_mi*100,'Proszę czekać.'@,'Odtwarzanie stanów'@)
      ?};
      do();
      _obl:=exec('odtst_m','magazyn_stan',LKTM.KTM,ST.MAG);
      end();
      {? _obl
      ||
         {? _a=0
         || progress(_li/_mi*100,'Proszę czekać.'@,'Odtwarzanie stanów'@)
         ?};
         exec('obl_stan','magazyn_stan',exec('FindInSet','#table','M','MATKTM',LKTM.KTM,LKTM.KTM),1,ST.MAG);
         LKTM.del
      || LKTM.next
      ?}
   !}
?};

:: aktualizacja stanow wg zamowien
{? LKTM.first
||
   _mi:=LKTM.size; _li:=0;
   {!
   |?
      _li+=1;
      {? _a=0
      || progress(_li/_mi*100,'Aktualizacja stanów wg zamówień.\nProszę czekać.'@,'Odtwarzanie stanów'@)
      ?};
      exec('obl_stan','magazyn_stan',exec('FindInSet','#table','M','MATKTM',LKTM.KTM,LKTM.KTM),1,ST.MAG);
      LKTM.next
   !}
?};

:: aktualizacja otwartej inwetaryzacji ciaglej jesli istnieje
INN.cntx_psh();
INN.index('INDP');
INN.prefix(ST.MAG,date(0,0,0));
{? INN.first() || {! |? exec('aktualse','magazyn_stan'); INN.next() !} ?};
INN.cntx_pop();
''


\list_ktm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: tworzy liste KTM-ow
::   WE: [_a] - czy tworzyc na podstawie uzywanych indeksow w magazynie czy wszystkie KTM (1 - mag, 2 wszystko)
::   WY: tabela z lista KTM (akronim LKTM)
::  OLD: \list_ktm/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};

{? var_pres('LKTM')>100 || obj_del(LKTM) ?};
{? _a=1
||
   {? var_pres('LKTM2')>100 || obj_del(LKTM2) ?};
   _zap:='select distinct M.KTM, JM.KOD JM, J2.KOD J2 from @DK '+
         'join M using(DK.M,M.REFERENCE) join @ND using(DK.N,ND.REFERENCE) join MG using(ND.MAG,MG.REFERENCE) '+
         'left join JM using (M.J,JM.REFERENCE) left join JM J2 using (M.J2,J2.REFERENCE) '+
         'where ND.MAG=:_a and DK.PLUS=\'T\' and M.RODZ=\'T\' and ND.D<=to_date(:_b) '+
         'union all '+
         'select distinct M.KTM, JM.KOD JM, J2.KOD J2 from ZD_POZ '+
         'join M using(ZD_POZ.M,M.REFERENCE) join MG using(ZD_POZ.MG,MG.REFERENCE) '+
         'left join JM using (M.J,JM.REFERENCE) left join JM J2 using (M.J2,J2.REFERENCE) '+
         'where ZD_POZ.MG=:_a and ZD_POZ.ZRE=\'N\' and ZD_POZ.IL_POZ>0 and M.RODZ=\'T\' '+
         'union all '+
         'select distinct M.KTM, JM.KOD JM, J2.KOD J2 from ZK_P '+
         'join M using(ZK_P.M,M.REFERENCE) join MG using(ZK_P.MG,MG.REFERENCE) '+
         'left join JM using (M.J,JM.REFERENCE) left join JM J2 using (M.J2,J2.REFERENCE) '+
         'where ZK_P.MG=:_a and ZK_P.ILP>0 and ZK_P.A=\'A\' and M.RODZ=\'T\' '+
         {? _a
         || 'union all '+
            'select distinct M.KTM, JM.KOD JM, J2.KOD J2 from @SM '+
            'join M using(SM.M,M.REFERENCE) join MG using(SM.MAG,MG.REFERENCE) '+
            'left join JM using (M.J,JM.REFERENCE) left join JM J2 using (M.J2,J2.REFERENCE) '+
            'where SM.MAG=:_a '
         || ' '
         ?}+
         'order by 1';
   LKTM2:=sql(_zap,ST.MAG,date(ST.AR,ST.AM,0));
   LKTM:=sql('select distinct KTM, JM, J2 from :_a order by KTM',LKTM2)
|| LKTM:=sql('select distinct KTM, JM.KOD JM, J2.KOD J2 from M '+
             'left join JM using (M.J,JM.REFERENCE) left join JM J2 using (M.J2,J2.REFERENCE) '+
             'order by KTM')
?};
''


\odtw_sl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: odtworzenie tabeli SL
::   WE: _a - magazyn na ktorym odtwarzamy SL-a
::  OLD: \odtw_sl/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
_msk:=ND.name()+3;

:: usuniecie danych w SL-u na danym magazynie
SL.index('MG');
SL.prefix(_a);
{? SL.first()
|| {!
   |? SLD.index('SL');
      SLD.prefix(SL.ref());
      {? SLD.first() || {! |? SLD.del() !} ?};
      SL.del()
   !}
?};

_maski:=tab_tmp(1,'NAME','STRING[3]','');

OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first
|| {!
   |? _maski.blank();
      _maski.NAME:=(1+_msk)+($OKR.ROK+2);
      _maski.add(1);
      OKR.next()
   !}
?};
OKR.cntx_pop();

_maski.clear();
{? _maski.first()
|| {!
   |?
      exec('mag_open','open_tab',1+(_maski.NAME+3),_maski.NAME+2);
::    dogenerowanie danych do DK_L dla wszystkich zaakceptowanych dokumentow
      echo('Odtwarzanie wymiarów rok: '@+'20'+(_maski.NAME+2));

::    ustalanie danych do progressu
      DK_LN.index('MG');
      DK_LN.prefix(_a);
      ND.index('NAGDT');
      ND.prefix(_a,'T');
      _mi:=DK_LN.size()+ND.size; _tp:=time(); _li:=0;
      {? ND.first()
      || _czypal:=ND.MAG().PAL='T';
         {!
         |?
::          aktualizacja tabeli SL
            _li+=1;
            progress(_li/_mi*100,'Rok: 20%1\nProszę czekać.'@[(_maski.NAME+2)],'Odtwarzanie wymiarów'@);
            DK.index('DOKMAG');
            DK.prefix(ND.ref());
            {? DK.first() & ~(ND.TYP().DK='T' & ND.TYP().Z='T')
            || {!
               |?
                  DK_L.index('DK');
                  DK_L.prefix(DK.ref);
                  _warunek:={? DK.N().TYP().P='T' & ~DK_L.first()
                            || 1
                            |? DK.N().TYP().P='N' & DK.ZP=0 & ~DK_L.first()
                            || 1
                            || 0
                            ?};
                  _il_dk:={? DK.N().TYP().P='T'
                          || DK.IL
                          || _w:=DK.IL; _nd:=DK.N; _zpo:=#DK.ref;
                             DK.cntx_psh();
                             DK.index('DOKZP');
                             DK.prefix(_nd,_zpo);
                             {? DK.first() || {!|? _w+=DK.IL; DK.next !} ?};
                             DK.cntx_pop();
                             _w
                          ?};
                  {? _warunek & DK.M().RODZ='T' || exec('rebuild_dle','magazyn_stan',DK.ref,_il_dk) ?};
                  DK_L.prefix();
                  exec('dk_l2sl','magazyn_stan',DK.ref(),,1,,_czypal);
                  DK.next()
               !}
            ?};
            ND.next()
         !}
      ?};

::    tabela SL na podstawie reorganizacji
      DK_LN.index('MG');
      DK_LN.prefix(_a);
      {? DK_LN.first()
      || {!
         |?
            _li+=1;
            progress(_li/_mi*100,'Rok: 20%1\nProszę czekać.'@[(_maski.NAME+2)],'Odtwarzanie wymiarów'@);
            {? DK_LN.AKC='T'
            || DK_L.index('DK_LN');
               DK_L.prefix('Z',DK_LN.ref());
               {? DK_L.first()
               || {!
                  |? exec('adddelsl','magazyn_stan'
                          ,DK_L.MG,DK_L.M,DK_L.LOK,DK_L.TW,DK_L.IL,1,DK_L.PAL,DK_L.SCEAN,DK_L.IL2);
                     exec('adddelsl','magazyn_stan'
                          ,DK_L.MG,DK_L.M,DK_L.LOKDO,DK_L.TWDO,DK_L.IL,-1
                          ,{? DK_L.PALDO().STAN='I' || null() || DK_L.PALDO ?},DK_L.SCEAN,DK_L.IL2);
                     DK_L.next()
                  !}
               ?}
            ?};
            DK_LN.next()
         !}
      ?};
      _maski.next()
   !}
?};
exec('mag_open','open_tab',1+(_msk+3),_msk+2);

echo();prgs_clr();
''


\rebuild_dle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MaX] [12.10 spis]
:: OPIS: Odbudowuje lokalizacje dla pozycji magazynowych ktore nie mialy ich uzupelnionych
::            bo  np. magazyn zostal pozniej przestawiony na lokalizacje.
::   WE: _a - DK.ref
::           _b - Ilosc do
::   WY: 1-jest OK 0-nie jest dobrze
::  OLD: \rebuild_dle/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
DK.cntx_psh(); DK_L.cntx_psh();
DK.prefix();
{? DK.seek(_a)
||
    DK_L.prefix();
    DK_L.blank();
    DK_L.DK:=DK.ref();
    DK_L.LOK:=DK.N().MAG().EANL;
    DK_L.TW:=date(0,0,0);
    DK_L.IL:={? DK.N().TYP().P='T' || _b || -_b ?};
    DK_L.DT:={? DK.N().Z='T' || DK.N().DA || DK.N().D ?};
    DK_L.TM:={? DK.N().Z='T' || DK.N().TA || time() ?};
    DK_L.US:=DK.N().US;
    DK_L.Z:=DK.N().Z;
    DK_L.Z_DO:='Z';
    DK_L.MG:=DK.N().MAG;
    DK_L.M:=DK.M;
    DK_L.JM:=DK.M().J;
    DK_L.AUTO:=1;
    DK_L.SCSQL:=DK.SRDK;
    exec('reoIL2','magdok_wymiary');
    exec('uzupIDkod','magdok_palety',DK_L);
    _wyn:=DK_L.add(1)
|| _wyn:=0
?};
DK.cntx_pop(); DK_L.cntx_pop();
_wyn


\odtst_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: odtwarzanie stanow dla podanego materialu w podanym magazynie w biezacej masce
::       UWAGA .... transakcja musi byc wywolywana zewnetrznie
::   WE:  _a  - kod materialu - M.KTM
::        _b  - magazyn (ref)
::   WY: czy sa dokumenty magazynowe (0 - nie ma, 1 - sa)
::  OLD: \odtst_m/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
:: kasowanie baz stanow i stanow cen
_wyn:=0;
M.cntx_psh();
M.index('MATKTM');
M.prefix(_a,_a);
{? M.first
||
   ND.cntx_psh();
   DK.cntx_psh();
   SC.cntx_psh();
   SM.cntx_psh();

   OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   {? OKR.first
   ||
      {!
      |?
         DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
         ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));
         SM.use('stm__'+ST.ODDZ+($OKR.ROK+2));
         SC.use('stc__'+ST.ODDZ+($OKR.ROK+2));
         SM.index('SM');
         SM.prefix(_b,M.ref);
         {? SM.first || {! |? SM.del !} ?};

         SC.index('SN');
         SC.prefix(_b,M.ref);
         {? SC.first
         || {!
            |? {? SC.count=0
               || SC.del
               || SC.S:=0;
                  SC.S2:=0;
                  {? SC.S=0 || SC.A:='N' || SC.A:='T' ?};
                  SC.put;
                  SC.next
               ?}
            !}
         ?};
         OKR.next
      !}
   ?};
   OKR.cntx_pop();

:: usuwanie stanow zbiorczych

   SM.cntx_psh();
   SC.cntx_psh();
   SM.use('stm__'+ST.ODDZ+'zb');
   SC.use('stc__'+ST.ODDZ+'zb');

   SM.index('SM');
   SM.prefix(_b,M.ref);
   {? SM.first || {! |? SM.del !} ?};

   SC.index('SN');
   SC.prefix(_b,M.ref);
   {? SC.first
   || {!
      |? {? SC.count=0
         || SC.del
         || SC.S:=0;
            SC.S2:=0;
            {? SC.S=0 || SC.A:='N' || SC.A:='T' ?};
            SC.put;
            SC.next
         ?}
      !}
   ?};

   SC.cntx_pop();
   SM.cntx_pop();

   OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   {? OKR.first
   ||
      {!
      |?
         OKR.cntx_psh();
         DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
         ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));
         SM.use('stm__'+ST.ODDZ+($OKR.ROK+2));
         SC.use('stc__'+ST.ODDZ+($OKR.ROK+2));

         DK.index('DK_SM');
         DK.prefix(_b,M.ref);
         {? DK.first
         ||
            _wyn:=1;
            echo('trwa odtwarzanie stanów ... %1 w roku %2'@[DK.M().KTM,$OKR.ROK]);
            {!
            |?
               {? DK.M().RODZ='T' & ~(DK.N().TYP().DK='T' & DK.N().TYP().Z='T')
               ||
                  DK.PLUS:=DK.N().TYP().P;
                  {? DK.STATS=null() & DK.PRDK<>''
                  || DK.STATS:=exec('FindAndGet','#table',DK,DK.SRDK,,"STATS",null())
                  ?};
                  DK.put;
                  {? DK.Z='T'
                  ||
                     SC.clear;
                     SM.clear;
                     {? exec('OBLSTAN','magazyn_stan',ND.MAG,DK.M,{? DK.PLUS='T' || ND.D || DK.DOST ?}
                           ,DK.C,DK.PLUS,1,DK.RDK,DK.NDK,DK.SRDK,DK.PRDK,DK.SCC,DK.N().SCWAL,DK.STATS,DK.SCEAN,DK.TW)=0
                     || undo;
                        FUN.info('Stanu nie udało sie zaktualizować.'@)
                     ?}
                  ?}
               ?};
               DK.next
            !};
            echo()
         ?};
         OKR.cntx_pop();
         OKR.next
      !}
   ?};
   OKR.cntx_pop();
   SM.cntx_pop();
   SC.cntx_pop();
   DK.cntx_pop();
   ND.cntx_pop()
||
   undo;
   FUN.info('Nie znaleziono indeksu materiałowego %1.'@[_a])
?};
M.cntx_pop();
_wyn


\aktualse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: aktualizuje stan wg ewidencji
::   WE: _a - na dzień
::       [_b] - na godzinę
::  OLD: \aktualse/inweko.fml
::----------------------------------------------------------------------------------------------------------------------
_date:={? var_pres('_a')=type_of(date()) || _a || INN.DI ?};
_dtim:={? var_pres('_b')=type_of(time()) || _b || INN.TI ?};

SM.index('SA');
SM.prefix(ST.MAG);
{? SM.first
|| _size:=SM.size;
   _licz:=0;
   _time:=time();
   {!
   |? _licz+=1;
      progress(_licz/_size*100,'Materiał: %1 - %2'@[SM.M().KTM,SM.M().N],'Uzgodnienie stanów'@);
      {? exec('FindInSet','#table','INP','KOD',SM.M,INN.ref())<>null
      || exec('add_onei','magazyn_inw',INN.ref,INN.MG,_date,SM.M,,,,,,_dtim)
      ?};
      SM.next
   !}
?};
echo();
prgs_clr();
1


\odtwstsl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: odtworzenie tabeli SL - z pytaniem
::  OLD: \odtwstsl/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy odtworzyć stany wymiarów na podstawie dokumentów i reorganizacji magazynu?'@)
|| exec('odtw_sl','magazyn_stan',ST.MAG);
   FUN.info('Odtwarzanie zakończone.'@)
?}


\uzgodn_s
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: uzgodnienie stanow
::  OLD: \uzgodn_s/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('czy_kor','magazyn_stan') & FUN.ask('Przygotować listę niezgodności?'@)
||
   SM.cntx_psh();
   SC.cntx_psh();
   SM.use('stm__'+ST.ODDZ+($ST.AR+2));
   SC.use('stc__'+ST.ODDZ+($ST.AR+2));
   exec('list_ktm','magazyn_stan',2);

   {? var_pres('UZGST')>100 || obj_del(UZGST) ?};

   UZGST:=tab_tmp(2,'JMJ2','STRING[2]',''
      ,'KTM','STRING[50]',''
      ,'REF','INTEGER',''
      ,'DKI','REAL',''
      ,'DKW','REAL',''
      ,'SCI','REAL',''
      ,'SCW','REAL',''
      ,'SMI','REAL',''
      ,'S_SM','REAL','Stan wg SM'@
      ,'S_IL','REAL',''
      ,'S_WAR','REAL',''
      ,'KOL','INTEGER',''
      ,'BL' ,'STRING[10]',''
      ,'DK2','REAL',''
      ,'SC2','REAL',''
      ,'SM2','REAL',''
      ,'S2SM','REAL',''
      ,'S2IL','REAL',''
      ,'JM','STRING[10]',''
      ,'J2','STRING[10]',''
      ,'WAL','STRING[3]','');

:: uzgodnienie stanow

   LKTM.clear;
   {? LKTM.first
   ||
      _mi:=LKTM.size; _li:=0;
      {!
      |?
         _li+=1;
         progress(_li/_mi*100,'Materiał: %1'@[LKTM.KTM],'Uzgodnienie stanów'@);
         _ref:=exec('FindInSet','#table','M','RODZ',LKTM.KTM,'T');
         exec('uzup_uzg','magazyn_stan',_ref,0);
         LKTM.next
      !}
   ?};

   UZGST.clear;
   {? UZGST.first
   || _acr_sel:=UZGST.mk_sel('Stany ilościowe'@,'T',0,'start_uz1acrsel');
      UZGST.win_fld(_acr_sel,,'KTM',,,30,,0,'Indeks'@);
      UZGST.win_fld(_acr_sel,,'DKI',,,15,3,0,'Wg dokumentów'@,,'Ilość wg dokumentów'@);
      UZGST.win_fld(_acr_sel,,'SCI',,,15,3,0,'Wg dostaw'@,,'Ilość wg dostaw'@);
      UZGST.win_fld(_acr_sel,,'SMI',,,15,3,0,'Stan zbiorczy'@,,'Ilość wg stanów zbiorczych'@);
      UZGST.win_fld(_acr_sel,,'S_SM',,,15,3,0,'Stan roczny'@,,'Ilość wg stanów rocznych'@);
      UZGST.win_fld(_acr_sel,,'S_IL',,,15,3,0,'Na koniec okresu'@,,'Ilość na koniec okresu'@);
      UZGST.win_fld(_acr_sel,,'JM',,,10,,0,'jm'@);
      UZGST.win_act(_acr_sel,,'Formuła','&Uzgodnij stan'@@,,,"exec('uzg_ktm','magazyn_stan')",,1,,,,'U');
      UZGST.win_act(_acr_sel,,'Formuła','&Legenda'@@,,,"exec('legenda','color','UZGST#01')",,,,,,'L');
      UZGST.win_act(_acr_sel,,'Rekord',,,,"exec('rekprzed','color','UZGST#01')",,0);
      _acr_se1:=_acr_sel;
      _acr_sel:=UZGST.mk_sel('Stany wartościowe'@,'T',0,'start_uz2acrsel');
      UZGST.win_fld(_acr_sel,,'KTM',,,48,,0,'Indeks'@);
      UZGST.win_fld(_acr_sel,,'DKW',,,20,2,0,'Wg dokumentów'@,,'Wartość wg dokumentów'@);
      UZGST.win_fld(_acr_sel,,'SCW',,,20,2,0,'Wg dostaw'@,,'Wartość wg dostaw'@);
      UZGST.win_fld(_acr_sel,,'S_WAR',,,20,2,0,'Na koniec okresu'@,,'Wartość na koniec okresu'@);
      UZGST.win_fld(_acr_sel,,'WAL',,,10,,0,'Waluta'@);
      UZGST.win_act(_acr_sel,,'Formuła','&Uzgodnij stan'@@,,,"exec('uzg_ktm','magazyn_stan')",,1,,,,'U');
      UZGST.win_act(_acr_sel,,'Formuła','&Legenda'@@,,,"exec('legenda','color','UZGST#01')",,,,,,'L');
      UZGST.win_act(_acr_sel,,'Rekord',,,,"exec('rekprzed','color','UZGST#01')",,0);
      _acr_se2:=_acr_sel;
      _acr_sel:=UZGST.mk_sel('Stany ilościowe druga jednostka ewidencyjna'@,'T',0,'start_uz3acrsel');
      UZGST.win_fld(_acr_sel,,'KTM',,,30,,0,'Indeks'@);
      UZGST.win_fld(_acr_sel,,'DK2',,,15,3,0,'Wg dokumentów'@,,'Ilość wg dokumentów'@);
      UZGST.win_fld(_acr_sel,,'SC2',,,15,3,0,'Wg dostaw'@,,'Ilość wg dostaw'@);
      UZGST.win_fld(_acr_sel,,'SM2',,,15,3,0,'Stan zbiorczy'@,,'Ilość wg stanów zbiorczych'@);
      UZGST.win_fld(_acr_sel,,'S2SM',,,15,3,0,'Stan roczny'@,,'Ilość wg stanów rocznych'@);
      UZGST.win_fld(_acr_sel,,'S2IL',,,15,3,0,'Na koniec okresu'@,,'Ilośc na koniec okresu'@);
      UZGST.win_fld(_acr_sel,,'J2',,,10,,0,'jm'@);
      UZGST.win_act(_acr_sel,,'Formuła','&Uzgodnij stan'@@,,,"exec('uzg_ktm','magazyn_stan')",,1,,,,'U');
      UZGST.win_act(_acr_sel,,'Formuła','&Legenda'@@,,,"exec('legenda','color','UZGST#01')",,,,,,'L');
      UZGST.win_act(_acr_sel,,'Rekord',,,,"exec('rekprzed','color','UZGST#01')",,0);
      _acr_se3:=_acr_sel;

      _fb:="";
      _acr_sel:=UZGST.grp_make('Różnice'@,_fb,'start_uz_acrgrp',,,);
      _fb:="UZGST.clear(); UZGST.prefix('T')";
      _fa:="";
      _fr:="";
      UZGST.grp_sel(_acr_sel,,_acr_se1,'Stany ilościowe'@,_fr,,,16,_fb,_fa,,,'maximized');
      _fb:="UZGST.clear(); UZGST.prefix('T')";
      _fa:="";
      _fr:="";
      UZGST.grp_sel(_acr_sel,,_acr_se2,'Stany wartościowe'@,_fr,,,16,_fb,_fa,,,'maximized');
      _fb:="UZGST.clear(); UZGST.prefix('TT')";
      _fa:="";
      _fr:="";
      UZGST.grp_sel(_acr_sel,,_acr_se3,'Stany ilościowe druga jednostka ewidencyjna'@,_fr,,,16,_fb,_fa,,,'maximized');
      UZGST.win_sel(_acr_sel);
      _ru:=0; _rw:=0; _rs:=0; _rk:=0;  _ri:=0;
      {!
      |? _rs+=~(UZGST.DKI=UZGST.SCI & UZGST.DKI=UZGST.SMI);
         _rw+=(UZGST.DKW<>UZGST.SCW);
         _ru+=(UZGST.DKI<0);
         _rk+=(UZGST.KOL);
         _ri:=(UZGST.DKI=0 & UZGST.DK2<>0);
         UZGST.next
      !};
      echo();
      prgs_clr();
      UZGST.select

   || FUN.info('Nie stwierdzono żadnych niezgodności na stanach.'@)
   ?};

   echo();
   prgs_clr();
   SC.cntx_pop();
   SM.cntx_pop();

   VAR_DEL.delete('UZGST')
?};
''


\czy_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: czy w magazynie ST.MAG byly wystawiane korekty dokumentow przychodowych
::   WY: 0/1
::  OLD: \czy_kor/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

_t_ndx:=TYPYDOK.ndx_tmp(,,'DK',,);
TYPYDOK.clear();
TYPYDOK.index(_t_ndx);
TYPYDOK.prefix('T');
{? TYPYDOK.first()
||
   _qq:=$"
      select distinct
         TYPYDOK.T
      from
         @ND
         join MG using (ND.MAG,MG.reference)
         join TYPYDOK using (ND.TYP,TYPYDOK.reference)
      where
         MG.SYM=':_a'
         and TYPYDOK.T=':_b'
   ";
   {? sql(_qq,ST.MAG().SYM,TYPYDOK.T).first() || _wyn:=1 ?}
?};
TYPYDOK.ndx_drop(_t_ndx);

{? _wyn
|| FUN.info('Funkcja zablokowana.\nW magazynie %1 wystawiono korekty dokumentów przychodowych.'@[ST.MAG().SYM])
?};
_wyn


\uzup_uzg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: uzupelnia tabele UZGST
::   WE:  _a - ref materialu
::        _b - czy usuwac rekord w UZGST (czy jest wywolywana dla biezacego rekordu)
::  OLD: \uzup_uzg/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;

_bl:='';

_ild:=exec('obl_ssc','magazyn_stan',0,'',ST.MAG,_ref,'N');
_il2:=HELP.DIL2;
_ward:=__ward;

_sc:=exec('obl_smsc','magazyn_stan',ST.MAG,_ref);
_sc2:=HELP.DIL2;
_swar:=__ward;&__ward;
_smil:=__s_sm;&__s_sm;
_smi2:=HELP.SIL2;

_sm:=0;
_s_il:=0;
_s_war:=0;
_sm2:=0;
_s2il:=0;
SM.cntx_psh();
SM.use('stm__'+ST.ODDZ+'zb');
SM.clear();
SM.index('SM');
SM.prefix(ST.MAG,_ref);
{? SM.first()
|| _sm:=SM.S;
   _sm2:=SM.S2;
   _s_il:=SM.ST_IL;
   _s2il:=SM.ST_IL2;
   _s_war:=SM.ST_WAR
?};
SM.cntx_pop();

_bl+={? _ild<>_sc || 'd' || '' ?};
_bl+={? _ild<>_sm  | _sc<>_sm | _smil<>_sm | _s_il<>_sm || 's' || '' ?};
_bl+={? _ward<>_swar | _s_war<>_swar || 'D' || '' ?};
{? LKTM.J2<>''
|| _bl+={? _ild=0 & (_il2<>0 | _sc2<>0 | _smi2<>0 | _s2il<>0) || 'p' || '' ?};
   _bl+={? _il2<>_sm2  | _sc2<>_sm2 | _smi2<>_sm2 | _s2il<>_sm2 || '2' || '' ?}
?};

{? _b=1
||
   {? _bl<>''
   ||
      UZGST.DKI:=_ild;
      UZGST.SCI:=_sc;
      UZGST.SMI:=_sm;
      UZGST.S_SM:=_smil;

      UZGST.DKW:=_ward;
      UZGST.SCW:=_swar;

      UZGST.BL:=_bl;
      UZGST.S_IL:=_s_il;
      UZGST.S_WAR:=_s_war;

      UZGST.DK2:=_il2;
      UZGST.SC2:=_sc2;
      UZGST.SM2:=_sm2;
      UZGST.S2SM:=_smi2;
      UZGST.S2IL:=_s2il;
      UZGST.put(1)
   || UZGST.del
   ?}
||
   {? _bl<>''
   || UZGST.clear;
      UZGST.KTM:=LKTM.KTM;
      UZGST.REF:=#_ref;

      UZGST.DKI:=_ild;
      UZGST.SCI:=_sc;
      UZGST.SMI:=_sm;
      UZGST.S_SM:=_smil;

      UZGST.DKW:=_ward;
      UZGST.SCW:=_swar;

      UZGST.BL:=_bl;
      UZGST.S_IL:=_s_il;
      UZGST.S_WAR:=_s_war;

      UZGST.DK2:=_il2;
      UZGST.SC2:=_sc2;
      UZGST.SM2:=_sm2;
      UZGST.S2SM:=_smi2;
      UZGST.S2IL:=_s2il;
      UZGST.JM:=LKTM.JM;
      UZGST.J2:=LKTM.J2;
      UZGST.JMJ2:='T'+{? LKTM.J2<>'' || 'T' || 'N' ?};
      UZGST.WAL:='PLN';
      UZGST.add(1)
   ?}
?};
''


\obl_smsc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: stan danej tabel maskowalnych SC dla magazynu i materialu
::   WE:  _a - ref magazynu
::        _b - ref materialu
::   WY: zwraca obliczona ilosc wg tabeli SC
::       dodatkowo uzupelnia zmienna __ward - obliczona wartosc dokumentow
::       dodatkowo uzupelnia zmienna __s_sm - obliczona ilosc stanow wg tabeli SM
::  OLD: \obl_smsc/mag_fun.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__ward')>100 || obj_del(__ward) ?};
{? var_pres('__s_sm')>100 || obj_del(__s_sm) ?};
__ward:=0;
__s_sm:=0;
HELP.DIL2:=HELP.SIL2:=0;

_ild:=0;

SC.cntx_psh();
SM.cntx_psh();
OKR.cntx_psh();

OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |?
      SC.use('stc__'+ST.ODDZ+($OKR.ROK+2));
      SM.use('stm__'+ST.ODDZ+($OKR.ROK+2));
      SC.index('SC');
      SC.prefix(_a,_b);
      {? SC.first()
      || _ewi:=SC.MAG().TYP*'EWI';
         {!
         |? _ild+=SC.S;
            _cena:={?_ewi || exec('biez_cen','ceny_dok',SC.M,SC.MAG,date()+1) || SC.C ?};
            __ward+=SC.S*_cena $2;
            HELP.DIL2+=SC.S2;
            SC.next()
         !}
      ?};
      SM.index('SM');
      SM.prefix(_a,_b);
      {? SM.first() || {! |? __s_sm+=SM.S; HELP.SIL2+=SM.S2; SM.next() !} ?};
      OKR.next()
   !}
?};

OKR.cntx_pop();
SM.cntx_pop();
SC.cntx_pop();
_ild


\uzg_ktm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: odtwarza stan dla biezacego KTM w tabeli UZGST i ja aktualizuje
::  OLD: \uzg_ktm/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_txt:='';
_wgkr:=0;
{? UZGST.DKI=UZGST.SCI & UZGST.DKI=UZGST.SMI & UZGST.DKW<>UZGST.SCW
|| _txt+='Należy uruchomić korektę niezgodności (obszar: Dokumenty magazynowe)\n'
         'rozliczającą zaokrąglenia magazynowe.\n'@; _wgkr:=1; _ok:=0
?};
{? UZGST.DKI=0 & UZGST.DK2<>0
|| _txt+='Należy uruchomić korektę niezgodności (obszar: Dokumenty magazynowe)\n'
         'rozliczającą ilości w drugiej jednostce magazynowej.\n'@; _wgkr:=2; _ok:=0
?};
_zgil:=UZGST.DKI=UZGST.SCI & UZGST.DKI=UZGST.SMI;
_zgi2:={? UZGST.J2<>'' || UZGST.DK2=UZGST.SC2 & UZGST.DK2=UZGST.SM2 & UZGST.S2IL=UZGST.SM2 || 1 ?};
{? _wgkr=2
|| _ok:=0
|? _zgil & _zgi2
|| _txt+='Brak różnic ilościowych.'@; _ok:=0
|| _ok:=1
?};
{? _txt<>'' || FUN.info(_txt) ?};
{? _ok=1
||
   do();
   _ref:=exec('FindInSet','#table','M','RODZ',UZGST.KTM,'T');
:: poprawia pole SM.ST_WAR
   {? UZGST.S_WAR<>UZGST.DKW
   || SM.cntx_psh();
      SM.use('stm__'+ST.ODDZ+'zb');
      SM.clear();
      SM.index('SM');
      SM.prefix(ST.MAG,_ref);
      {? SM.first() || SM.ST_WAR:=UZGST.DKW; SM.put(1) ?};
      SM.cntx_pop()
   ?};
:: odtwarzanie stanu danego indeksu materialowego
   exec('odtst_m','magazyn_stan',UZGST.KTM,ST.MAG);
   end();
   exec('obl_stan','magazyn_stan',_ref,1,ST.MAG);
   exec('popsc','magazyn_stan');
   exec('uzup_uzg','magazyn_stan',_ref,1)
?};
echo();
''


\popsc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: poprawia pozycje na SC
::  OLD: \popsc/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_acr:=SC.ndx_tmp('',0,'MAG',,0,'M',,0,'C',,0,'RDK',,0);
SC.clear;
SC.index(_acr);
SC.prefix(ST.MAG);
{? SC.first
|| {!
   |? {? SC.S<0
      || _ile:=-SC.S;
         SC.cntx_psh;
         SC.clear;
         SC.index(_acr);
         SC.prefix(SC.MAG,SC.M,SC.C);
         {? SC.first
         || {!
            |? {? SC.S>0
               || {? SC.S>=_ile
                  || SC.S-=_ile;
                     {? SC.S=0 || SC.A:='N' || SC.A:='T' ?};
                     SC.put;
                     _ile:=0;
                     0
                  || _ile-=SC.S; SC.S:=0;
                     {? SC.S=0 || SC.A:='N' || SC.A:='T' ?};
                     SC.put;
                     SC.next
                  ?}
               || SC.next
               ?}
            !}
         ?};
         SC.cntx_pop;
         SC.S:=-_ile; {? SC.S=0 || SC.A:='N' || SC.A:='T' ?};SC.put; SC.next
      || SC.next
      ?}
   !}
?};
SC.ndx_drop(_acr);
''


\uzg_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: kolorowanie rekordow w tabeli uzgodnien
::   WY: kod koloru
::  OLD: \uzg_kol/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? UZGST.BL * 's'
||
:: niezgodnosc ilosciowa
   _wyn:='UZGST#01#01'
|? UZGST.DKW<>UZGST.SCW
:: niezgodnosc wartosciowa
|| _wyn:='UZGST#01#02'
|? UZGST.BL*'p' | UZGST.BL*'2'
:: niezgodność ilościowa w drugiej jednostce ewidencyjnej
|| _wyn:='UZGST#01#03'
?};
_wyn


\stan_zest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: oblicza stan materialu na podany dzien
::   WE: _a - magazyn
::       _b - index
::       _c - data
::       _d=1 - liczyc na podstawie dokumentow, =0 - pobierac stan poczatkowy z SM
::       _e - zmienna do ktorej przypisany bedzie stan ilosciowy
::       _f - zmienna do ktorej przypisany bedzie stan wartosciowy
::       [_g] - 1-oblicza wylacznie rozchod 0-przychod 2(domyslnie)-wszystko
::       [_h] - dodatkowa data = od daty
::   WY: ~~
::  OLD: \stan/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=7 || {? type_of(_g)<>1 || _g:=2 ?} || _g:=2 ?};
{? _>=8 || {? type_of(_h)<>4 || _h:=date(0,0,0) ?} || _h:=date(0,0,0) ?};
HELP.SIL2:=0;

_data:={? _c~1=0 || _data:=date() || _data:=_c ?};
_rok:=_data~1;

{? _d=0
|| ND.cntx_psh(); DK.cntx_psh(); DK_L.cntx_psh();
   ND.use('nagdo'+ST.ODDZ+(form(form(_rok,,,'99'),-4)+2));
   DK.use('dokma'+ST.ODDZ+(form(form(_rok,,,'99'),-4)+2));
   DK_L.use('doklk'+ST.ODDZ+(form(form(_rok,,,'99'),-4)+2));
   DK.index('DOKMAT');
   DK.prefix('T',_b,_a,_rok);
   _sm:={? DK.last() || DK.N().D<=_data || 0 ?};
   ND.cntx_pop(); DK.cntx_pop(); DK_L.cntx_pop()
|| _sm:=0
?};

{? _sm=1
|| SM.cntx_psh();
   SM.use('stm__'+ST.ODDZ+(form(form(_rok,,,'99'),-4)+2));
   SM.index('SM');
   SM.prefix(_a,_b);
   {? SM.first()
   || _e():=SM.ST_IL;
      _f():=SM.ST_WAR;
      {? SM.M().J2<>null() || HELP.SIL2:=SM.ST_IL2 ?}
   ?};
   SM.cntx_pop()
|| OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   {? OKR.last() & OKR.ROK<_rok || _rok:=OKR.ROK ?};

   {? OKR.first()
   ||
      {? _d=0
      ||
::       wyznaczenie stanu na poczatek roku
         {? OKR.find_key(_rok) & OKR.prev
         ||
            SM.cntx_psh();
            {!
            |?
               _rok_sm:=form(form(OKR.ROK,,,'99'),-4)+2;
               SM.use('stm__'+ST.ODDZ+_rok_sm);
               SM.index('SM');
               SM.prefix(_a,_b);
               {? SM.first()
               || _e():=SM.ST_IL; _f():=SM.ST_WAR;
                  {? SM.M().J2<>null() || HELP.SIL2:=SM.ST_IL2 ?};
                  OKR.next;
                  0
               || OKR.prev
               ?}
            !};
            SM.cntx_pop()
         ?}
      ?};

      _cont:=1;
      ND.cntx_psh();
      DK.cntx_psh();
      DK_L.cntx_psh();
      {!
      |? ND.use('nagdo'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
         DK.use('dokma'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
         DK_L.use('doklk'+ST.ODDZ+(form(form(OKR.ROK,,,'99'),-4)+2));
         DK.index('DOKMAT');
         DK.prefix('T',_b,_a,OKR.ROK);
         {? DK.first()
         || {!
            |? {? DK.M().RODZ='T' & DK.N().D<=_data & DK.N().D>=_h
               || _e()+={? DK.PLUS='T' || {? ~(_g%*2) || DK.IL || 0 ?}  || {? _g || -1*DK.IL || 0 ?}  ?};
                  _f()+={? DK.PLUS='T' || {? ~(_g%*2) || DK.WAR || 0 ?} || {? _g || -1*DK.WAR || 0 ?} ?};
                  {? DK.M().J2<>null()
                  || HELP.SIL2+={? DK.PLUS='T' || {? ~(_g%*2) || DK.IL2 || 0 ?}  || {? _g || -1*DK.IL2 || 0 ?}  ?}
                  ?}
               |? DK.N().D>_data
               || _cont:=0
               ?};
               _cont=1 & DK.next()
            !}
         ?};
         _cont=1 & OKR.next()
      !};
      DK.cntx_pop();
      ND.cntx_pop();
      DK_L.cntx_pop()
   ?};
   OKR.cntx_pop()
?};
~~


\ilpal2sl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: sprawdza ilosc wystapien palet na lokalizacjach w danej chwili
::   WE: _a - ref palety
::   WY: 0-brak 1-raz 2-wiecej niz jeden
::  OLD: \ilpal2sl/mws.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_pal:='';
SL.cntx_psh();
SL.index('PALALL');
SL.prefix(PAL.ref());
{? SL.first()
|| _eanl:=$SL.EANL;
   _wyn:=1;
   {? SL.next()
   || {!
      |? {? _eanl<>$SL.EANL || _wyn:=2 ?};
         _wyn=1 & SL.next()
      !}
   ?}
?};
SL.cntx_pop();
_wyn


\dk_l0grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: wyswietlenie informacji o stanach i lokalizacjach magazynu - przed obsluga
::  OLD: \dk_l0grp/cechy.fml
::----------------------------------------------------------------------------------------------------------------------

EANL.index('MG');
EANL.prefix('T',ST.MAG);
grp_disp(EANL,'WERDK_L');

1


\dk_l_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: wyswietlenie informacji o stanach i lokalizacjach magazynu
::  OLD: \dk_l_grp/cechy.fml
::----------------------------------------------------------------------------------------------------------------------

{? DK_LN.MG().PAL='N'
|| SM.index('ASA');
   SM.prefix(ST.MAG,'T');
   grp_disp(SM,'WERDK_L')
|| PAL.index('AKT');
   PAL.prefix(ST.ODDZ,'N','T');
   grp_disp(PAL,'WERDK_L')
?};
1


\stnscean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: oblicza stan na danym identyfikatorze dostawy
::   WE: _a - ref magazynu
::       _b - ref materialu
::       _c - identyfikator dostawy
::       _d - ref DK_L-a
::       [_e] - ref lokalizacji domyslnie(null)
::       [_f] - termin waznosci
::       [_g] - 1-uwzgledniaj rowniez reorganizacje 0(domyslnie)-nie
::       [_h] - kontrola z uwzglednieniem terminów zerowych 0-nie(domyślnie) 1-tak
::  OLD: \dk_l_grp/ean2.fml
::----------------------------------------------------------------------------------------------------------------------

{? _>=5 || {? type_of(_e)<>7 || _e:=null ?} || _e:=null ?};
{? _>=6 || {? type_of(_f)<>4 || _f:=date(0,0,0) ?} || _f:=date(0,0,0) ?};
{? _>=7 || {? type_of(_g)<>1 || _g:=0 ?} || _g:=0 ?};
{? _>=8 || {? type_of(_h)<>1 || _h:=0 ?} || _h:=0 ?};

_wyn:=0;
_bez:=_e=null & _f=date(0,0,0);
DK_L.cntx_psh();
SLD.cntx_psh();
SL.cntx_psh();
SLD.index('MG_M');
SLD.prefix(_a,_b,_c,_c);
{? SLD.first()
|| {!
   |? {? (_bez | ({? _e<>null || SLD.SL().EANL=_e || 1 ?} & {? _h | _f<>date(0,0,0) || SLD.SL().TW=_f || 1 ?}))
      || _wyn+=SLD.IL
      ?};
      SLD.next()
   !}
?};
_wyn-=exec('ilwydsce','magazyn_stan',_a,_b,_c,_e,_f,_d,_g);
DK_L.cntx_pop();
SLD.cntx_pop();
SL.cntx_pop();
_wyn


\eanl_mws
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: informacja o lokalizacjach magazynu
::   WE: _a - ref magazynu
::       _b - czy MWS
::       _c - czy paletowy
::  OLD: \eanl_mws/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>7 || _a:=ST.MAG ?} || _a:=ST.MAG ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=ST.MAG().MWS='T' ?} || _b:=ST.MAG().MWS='T' ?};
{? _>=3 || {? type_of(_c)<>1 || _c:=ST.MAG().PAL='T' ?} || _c:=ST.MAG().PAL='T' ?};

VAR_DEL.delete('__infean','__infesl','__infegr','__winroz','__magmws');
__magmws:=_b;

__infesl:=tab_tmp(2,'KTM','STRING[50]',''
           ,'KT2','STRING[50]',''
           ,'ILE','REAL',''
           ,'PRO','REAL',''
           ,'JM','STRING[10]',''
           ,'ILZ','REAL',''
           ,'SQL','STRING[16]','');
_win_ssl:=__infesl.mk_sel('Zajętość wg materiału'@,'P',,'#inf_easlsel');
__infesl.win_fld(_win_ssl,,'KTM',,,40,,1,'Indeks'@);
__infesl.win_fld(_win_ssl,,'ILE',,,10,ST.DOKL,1,'Ilość'@);
__infesl.win_fld(_win_ssl,,'JM',,,3,,1,'jm'@);
__infesl.win_fld(_win_ssl,,'PRO',,,8,2,1,'Procent'@);
__infesl.win_act(_win_ssl,0,'Wyświetl',,,,"exec('dispemat','magazyn_stan')");
__infesl.win_sel(_win_ssl);

__infegr:=tab_tmp(3,'KOD','STRING[20]',''
           ,'KO2','STRING[20]',''
           ,'POZ','INTEGER',''
           ,'OPI','STRING[120]','');
_win_sgr:=__infegr.mk_sel('Strefy magazynowania'@,'P',,'#inf_eagrsel');
__infegr.win_fld(_win_sgr,,'KOD',,,20,,1,'Strefa'@);
__infegr.win_fld(_win_sgr,,'OPI',,,38,,1,'Opis'@);
__infegr.win_fld(_win_sgr,,'POZ',,,-4,,1,'Pozycja'@);
__infegr.win_sel(_win_sgr);

__infean:=tab_tmp(3,'TREE','TREE_REF','galazka'@
           ,'LAB','STRING[100]','Etykieta'@
           ,'KOD','STRING[2]','kod'@
           ,'P10','STRING[4]',''
           ,'P20','STRING[4]',''
           ,'P30','STRING[4]',''
           ,'P40','STRING[4]',''
           ,'P50','STRING[4]',''
           ,'P60','STRING[4]',''
           ,'P70','STRING[4]',''
           ,'P80','STRING[4]',''
           ,'P90','STRING[4]',''
           ,'P00','STRING[4]',''
           ,'PRO','REAL','procent zajetosci'@
           ,'ILE','REAL','ile materialu'@
           ,'EAN','STRING[30]','Kod'@
           ,'SQL','STRING[16]','ref sql'@
           ,'ILZ','REAL',''
           ,'ILC','REAL',''
           ,'BLK','INTEGER','');

_win_sel:=__infean.mk_sel('Zajętość lokalizacji w magazynie'@,'P',,'#inf_eanlsel',,,__infean.size(),1);
__winroz:=_win_sel;

__infean.win_fld(_win_sel,,'LAB',,,28,,1,'');
__infean.win_fld(_win_sel,,'EAN',,,11,,1,'');
__infean.win_fld(_win_sel,,'P10',,,2,,1,' 10%');
__infean.win_fld(_win_sel,,'P20',,,2,,1,' 20%');
__infean.win_fld(_win_sel,,'P30',,,2,,1,' 30%');
__infean.win_fld(_win_sel,,'P40',,,2,,1,' 40%');
__infean.win_fld(_win_sel,,'P50',,,2,,1,' 50%');
__infean.win_fld(_win_sel,,'P60',,,2,,1,' 60%');
__infean.win_fld(_win_sel,,'P70',,,2,,1,' 70%');
__infean.win_fld(_win_sel,,'P80',,,2,,1,' 80%');
__infean.win_fld(_win_sel,,'P90',,,2,,1,' 90%');
__infean.win_fld(_win_sel,,'P00',,,2,,1,'100%');
{? ~_c
|| __infean.win_fld(_win_sel,,'ILC',,,-9,3,1,'Objętość[m3]'@);
   __infean.win_fld(_win_sel,,'ILZ',,,-9,3,1,'Zajętość[m3]'@)
|| __infean.win_fld(_win_sel,,'ILC',,,-10,0,1,'Objętość[pal]'@);
   __infean.win_fld(_win_sel,,'ILZ',,,-10,0,1,'Zajętość[pal]'@)
?};
__infean.win_fld(_win_sel,,'PRO',,,6,2,1,'Procent'@);
{? _b & exec('get','#params',1300,2,OPERATOR.USER)='T'
|| __infean.win_act(_win_sel,0,'Menu','Blokada'@);
   __infean.win_act(_win_sel,0,'Formuła','&Zablokuj'@@,'Blokada'@,,"exec('blokinfe','magazyn_stan')"
     ,,0,1,"exec('blokinfe','magazyn_stan')",,'Z');
   __infean.win_act(_win_sel,0,'Formuła','&Odblokuj'@@,'Blokada'@,,"exec('blokinfe','magazyn_stan')"
     ,,0,1,"exec('blokinfe','magazyn_stan')",,'O')
?};
__infean.win_act(_win_sel,,'Formuła','&Zwiń/rozwiń'@@,,,"exec('zwin_rozwin','#tree')",,1,,,,'Z');
__infean.win_act(_win_sel,0,'Formuła','&Legenda'@@,,,"exec('legenda','color','__infean#01','@__infean#PRO')",,,,,,'L');
__infean.win_act(_win_sel,0,'Formuła','&Odśwież'@@,,,"exec('odsweanl','magazyn_stan')",,,,,,'O');
__infean.win_act(_win_sel,0,'Szukaj');
__infean.win_act(_win_sel,0,'Kolejność');
__infean.win_act(_win_sel,0,'Wyświetl',,,,"exec('dispeanl','magazyn_stan')");
__infean.win_act(_win_sel,0,'Rekord',,,,"exec('rekprzed','color','__infean#01')");

__infean.tr_fml(_win_sel,,"{? _a || {? __infean.TREE=0 || 1 || _a ?} || _a ?}");
__infean.win_fml(_win_sel,,'LAB',,'ICON_BEFORE',"
         {? __magmws
         || {? (1+__infean.LAB)='M'
            || 'xwin16.png:91'
            |? (1+__infean.KOD)='D'
            || 'xwin16.png:163'
            |? (+__infean.EAN)=2
            || 'xwin16.png:76'
            |? (+__infean.EAN)=5
            || 'xwin16.png:73'
            |? (+__infean.EAN)=8
            || 'xwin16.png:98'
            |? (+__infean.EAN)=11 & __infean.BLK
            || 'xwin16.png:18'
            |? (+__infean.EAN)=11 & (1+__infean.EAN)='D'
            || 'xwin16.png:162'
            |? (+__infean.EAN)=11 & ~__infean.BLK
            || 'xwin16.png:97'
            || ''
            ?}
         || {? (1+__infean.LAB)='M'
            || 'xwin16.png:91'
            |? __infean.BLK
            || 'xwin16.png:18'
            |? ~__infean.BLK
            || 'xwin16.png:97'
            || ''
            ?}
         ?}
     ");
__infean.win_fml(_win_sel,,'P10',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.win_fml(_win_sel,,'P20',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.win_fml(_win_sel,,'P30',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.win_fml(_win_sel,,'P40',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.win_fml(_win_sel,,'P50',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.win_fml(_win_sel,,'P60',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.win_fml(_win_sel,,'P70',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.win_fml(_win_sel,,'P80',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.win_fml(_win_sel,,'P90',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.win_fml(_win_sel,,'P00',,'ICON_BEFORE',"exec('col_proc','magazyn_stan')");
__infean.fld_fml('PRO','BEFORE_DISPLAY',"exec('pro_proc','magazyn_stan')");

__infean.win_sel(_win_sel);

exec('odsweanl','magazyn_stan',1);

_grp_sel:=__infean.grp_make('Zajętość na lokalizacjach magazynu %1'@[ST.MAG().SYM],,'#grp_seleanl');

__infean.grp_sel(_grp_sel,__infean,_win_sel,,"exec('akt_slgr','magazyn_stan')",,,,,,,,'maximized_with_title');
__infean.grp_splt(_grp_sel,,'vertical','tab1','0, 67%');
__infean.grp_sel(_grp_sel,__infesl,_win_ssl,,"",124,,20,"",,,,'maximized_with_title');
{? _b
|| __infean.grp_splt(_grp_sel,'tab1','horizontal','tab2');
   __infean.grp_sel(_grp_sel,__infegr,_win_sgr,,"",124,22,11,"",,,,'maximized_with_title')
?};
__infean.win_sel(_grp_sel);

__infean.clear();
__infean.select();

VAR_DEL.delete('__infean','__infesl','__infegr','__winroz','__magmws')


\dispemat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: wyswietla informacje o materiale
::  OLD: \dispemat/mws.fml
::----------------------------------------------------------------------------------------------------------------------
M.cntx_psh();
M.clear();
M.seek(__infesl.SQL);
M.win_edit('RED');
M.display();
M.cntx_pop()


\blokinfe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: blokada odblowanie lokalizacji/ wg zaznaczonych rekordow
::  OLD: \blokinfe/mws.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=__infean.sel_aget();
__infean.sel_adel();
__infean.cntx_psh();
EANL.cntx_psh();
_blok:=(1+menu_txt())='Z';
{? _tab.size()
|| {? _tab.first() & FUN.ask({? _blok || 'Zablokować zaznaczone lokalizacje?'@ || 'Odblokować zaznaczone lokalizacje?'@ ?})
   || _ndx:=__infean.ndx_tmp('',0,'EAN',,0,'EAN',,0);
      {!
      |? {? (__infean.clear; __infean.seek(_tab.REF,))
         || EANL.index('MG');
            EANL.prefix('T',ST.MAG,__infean.EAN);
            {? EANL.first()
            || {!
               |? {? EANL.ref()<>EANL.MG().EANL
                  || EANL.BLOK:={? _blok || 'T' || 'N' ?};
                     EANL.put(1)
                  ?};
                  EANL.next()
               !}
            ?};
            _kod:=__infean.EAN;
            __infean.index(_ndx);
            __infean.prefix(_kod);
            {? __infean.first()
            || {!
               |? {? (+__infean.EAN)=11
                   & exec('FindAndGet','#table',EANL,__infean.SQL,,,null())
                     <>exec('FindAndGet','#table',EANL,__infean.SQL,,"MG().EANL",null())
                  || __infean.BLK:=_blok;
                     __infean.put(1)
                  ?};
                  __infean.next()
               !}
            ?}
         ?};
         _tab.next()
      !};
      __infean.ndx_drop(_ndx)
   ?}
|? (2+__infean.EAN)='D0' & (11+ST.MAG().EANL().KOD)=__infean.EAN
|| FUN.info('Domyślna lokalizacja magazynu.\nBlokada niemożliwa.'@)
|? FUN.ask({? _blok || 'Zablokować daną lokalizację?'@ || 'Odblokować daną lokalizację?'@ ?})
|| EANL.cntx_psh();
   EANL.index('MG');
   EANL.prefix('T',ST.MAG,__infean.EAN);
   {? EANL.first()
   || {!
      |? {? EANL.ref()<>EANL.MG().EANL
         || EANL.BLOK:={? _blok || 'T' || 'N' ?};
            EANL.put(1)
         ?};
         EANL.next()
      !}
   ?};
   EANL.cntx_pop();
   _kod:=__infean.EAN;
   _ndx:=__infean.ndx_tmp('',0,'EAN',,0,'EAN',,0);
   __infean.index(_ndx);
   __infean.prefix(_kod);
   {? __infean.first()
   || {!
      |? {? (+__infean.EAN)=11
          & exec('FindAndGet','#table',EANL,__infean.SQL,,,null())
            <>exec('FindAndGet','#table',EANL,__infean.SQL,,"MG().EANL",null())
         || __infean.BLK:=_blok;
            __infean.put(1)
         ?};
         __infean.next()
      !}
   ?};
   __infean.ndx_drop(_ndx)
?};
__infean.cntx_pop();
EANL.cntx_pop();
~~


\odsweanl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: odswieza stany lokalizacji
::   WE: [_a] - magazyn
::       [_b] - 0(domyslnie)-odswiezenie 1-utworzenie
::  OLD: \odsweanl/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>7 || _a:=ST.MAG ?} || _a:=ST.MAG ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};

_czymws:=ST.MAG().MWS='T';

{? ~_b
|| _lab:=__infean.LAB;
   _kod:=__infean.EAN;
   __infean.erase()
?};

{? _czymws
|| _pol:=sql('select distinct
               substr(EANL.KOD,1,2) KOR,
               substr(EANL.KOD,4,2) REG,
               substr(EANL.KOD,7,2) POL,
               0 tree
             from
               EANL
             where EANL.MG=:_a
             order by 1,2,3 ',_a);
   _reg:=sql('select distinct :_a.KOR, :_a.REG, :_a.TREE from :_a order by 1,2 ',_pol);
   _kor:=sql('select distinct :_a.KOR, :_a.TREE from :_a order by 1 ',_pol);

   EANL.cntx_psh();
   EANL.index('MG');
   EANL.prefix('T',_a);
   {? EANL.first()
   || {!
      |?
::       korytarz
         _kkor:=2+EANL.KOD;
         _kor.clear();
         {? _kor.find_key(_kkor) & _kor.TREE
         || _tkor:=_kor.TREE
         || __infean.clear();
            __infean.blank();
            __infean.TREE:=0;
            __infean.LAB:={? (1+_kkor='D')
                          || 'Doki załadunkowe/wyładunkowe'
                          || 'Korytarz: '+form(_kkor)
                          ?};
            __infean.KOD:=_kkor;
            __infean.EAN:=_kkor;
            _tkor:={? __infean.add(1) || __infean.ref() || null ?};
            _kor.TREE:=_tkor;
            _kor.put(1)
         ?};
::       regal
         _kreg:=2+(3-EANL.KOD);
         _reg.clear();
         {? _reg.find_key(_kkor,_kreg) & _reg.TREE
         || _treg:=_reg.TREE
         |? (1+_kkor)<>'D'
         || __infean.clear();
            __infean.blank();
            __infean.TREE:=_tkor;
            __infean.LAB:='Regał: '+form(_kreg);
            __infean.KOD:=_kreg;
            __infean.EAN:=_kkor+'-'+_kreg;
            _treg:={? __infean.add(1) || __infean.ref() || null ?};
            _reg.TREE:=_treg;
            _reg.put(1)
         ?};
::       polka
         _kpol:=2+(6-EANL.KOD);
         _pol.clear();
         {? _pol.find_key(_kkor,_kreg,_kpol) & _pol.TREE
         || _tpol:=_pol.TREE
         |? (1+_kkor)<>'D'
         || __infean.clear();
            __infean.blank();
            __infean.TREE:=_treg;
            __infean.LAB:='Półka: '+form(_kpol);
            __infean.KOD:=_kpol;
            __infean.EAN:=_kkor+'-'+_kreg+'-'+_kpol;
            _tpol:={? __infean.add(1) || __infean.ref() || null ?};
            _pol.TREE:=_tpol;
            _pol.put(1)
         ?};
::       pojemnik
         _kpoj:=2+(9-EANL.KOD);
         __infean.clear();
         __infean.blank();
         __infean.TREE:={? (1+_kkor)='D' || _tkor || _tpol ?};
         __infean.LAB:={? (1+_kkor)='D' || 'Numer: ' || 'Pojemnik: ' ?}+form(_kpoj);
         __infean.KOD:=_kpoj;
         __infean.EAN:=_kkor+'-'+_kreg+'-'+_kpol+'-'+_kpoj;
         __infean.BLK:=EANL.BLOK='T';
         _dan:=exec('procent','magazyn_stan');
         __infean.PRO:=_dan[1];
         __infean.ILZ:=_dan[2];
         __infean.ILC:=_dan[3];
         __infean.SQL:=$EANL.ref();
         obj_del(_dan);
         __infean.add(1);
         EANL.next()
      !}
   ?};
   EANL.cntx_pop();

:: podsumowania procentow
   _pol.clear();
   {? _pol.first()
   || {!
      |? exec('sum_proc','magazyn_stan',_pol.TREE);
         _pol.next()
      !}
   ?};
   _reg.clear();
   {? _reg.first()
   || {!
      |? exec('sum_proc','magazyn_stan',_reg.TREE);
         _reg.next()
      !}
   ?};
   _kor.clear();
   {? _kor.first()
   || {!
      |? exec('sum_proc','magazyn_stan',_kor.TREE);
         _kor.next()
      !}
   ?}
|| EANL.cntx_psh();
   EANL.index('MG');
   EANL.prefix('T',_a);
   {? EANL.first()
   ||
::    magazyn
      __infean.clear();
      __infean.blank();
      __infean.TREE:=0;
      __infean.LAB:='Magazyn '+EANL.MG().SYM;
      __infean.KOD:=EANL.MG().SYM;
      __infean.EAN:='';
      _tkor:={? __infean.add(1) || __infean.ref() || null ?};
      {!
      |? __infean.clear();
         __infean.blank();
         __infean.TREE:=_tkor;
         __infean.LAB:='Lokalizacja (kod kreskowy): '+EANL.EAN;
         __infean.KOD:=EANL.KOD;
         __infean.EAN:=EANL.KOD;
         __infean.BLK:=0;
         _dan:=exec('procent','magazyn_stan');
         __infean.PRO:=_dan[1];
         __infean.ILZ:=_dan[2];
         __infean.ILC:=_dan[3];
         __infean.SQL:=$EANL.ref();
         obj_del(_dan);
         __infean.add(1);
         EANL.next()
      !}
   ?};
   EANL.cntx_pop()
?};

{? ~_b
|| __infean.clear();
   {? __infean.last()
   || {!
      |? {? __infean.LAB=_lab & __infean.EAN=_kod
         || 0
         || __infean.prev()
         ?}
      !}
   ?}
?};
~~


\sum_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: oblicza sumaryczny procent zajetosci dla wyzszego poziomu i wypelnia pola ILZ, ILC, PRO
::   WE: _a - TREE ref
::  OLD: \sum_proc/mws.fml
::----------------------------------------------------------------------------------------------------------------------
_silz:=0;
_silc:=0;

__infean.cntx_psh();
__infean.clear();
__infean.prefix(_a);
{? __infean.first()
|| {!
   |? _silz+=__infean.ILZ;
      _silc+=__infean.ILC;
      __infean.next()
   !}
?};
_procent:={? _silc || (_silz/_silc)*100 || 0 ?}$2;
__infean.clear();
{? __infean.seek(_a,)
|| __infean.ILZ:=_silz;
   __infean.ILC:=_silc;
   __infean.PRO:=_procent;
   __infean.put(1)
?};
__infean.cntx_pop();
~~


\dispeanl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: wyswietla informacje o lokalizacji
::  OLD: \dispeanl/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? +__infean.EAN=11 | (1+__infean.LAB)='L'
|| EANL.cntx_psh();
   EANL.clear();
   EANL.seek(__infean.SQL);
   exec('pw_segm','magazyn_stan',1);
   EANL.win_edit('RED'+{? (1+__infean.LAB)='L' || '' || 'MWS' ?});
   EANL.win_patt('RED');
   EANL.display();
   EANL.cntx_pop()
?}


\pw_segm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: przed wyświetleniem segmentu
::   WE: [_a] - wypełnij: domyślnie 0 (nie) 1-tak
::  OLD: \pw_segm/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};

{? _a | (1+menu_txt())='P'
|| EANX.KEANL:=2+EANL.KOD;
   EANX.REANL:=2+(3-EANL.KOD);
   EANX.PEANL:=2+(6-EANL.KOD);
   EANX.OEANL:=2+(9-EANL.KOD)
?};
''


\col_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: koloruje badz nie w zaleznosci od pola PRO
::----------------------------------------------------------------------------------------------------------------------
_kod:=cur_afld()+2;
_proc:=#(_kod);

{? ~_proc || _proc:=100 ?};
_aprc:=(__infean.PRO%10+{? __infean.PRO%*10 || 1 || 0 ?})*10;
_koda:={? _aprc=100 || '00' || form(_aprc) ?};
_icon:=form(191+{? _kod='00' || 10 || #_kod %10 ?},,0,'99');

{? _proc<=_aprc
|| {? (__infean.PRO-(_proc-10))>0 & (__infean.PRO-(_proc-10))<10
   || 'xwin16.png:162'
   || 'xwin16.png:165'
   ?}
|| ''
?}


\odcien
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: tworzy odcien kolorystyczny
::   WE: _a - kod rgb w formacie '::,::'
::       _b - procent
::   WY: kod rgb pocieniowany
::  OLD: \odcien/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=''
|| ''
|| _wsk:=_a*',';
   _buf:=_wsk-_a;

   _wsk:=_buf*':';
   _rr:=#((_wsk-1)+_buf); _buf:=_wsk-_buf;
   _gg:=#((_wsk-1)+_buf); _buf:=_wsk-_buf;
   _bb:=#_buf;

   _wsp:=_b*0.1;
   _wsp:={? _wsp<1 || 0 || _wsp-1 ?};
   _rr-=(_rr*0.06)*_wsp $0;
   _gg-=(_gg*0.06)*_wsp $0;
   _bb-=(_bb*0.06)*_wsp $0;
   form(_rr)+':'+form(_gg)+':'+form(_bb)+','+form(_rr)+':'+form(_gg)+':'+form(_bb)
?}


\akt_slgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: aktualizuje okienka informacyjne
::  OLD: \akt_slgr/mws.fml
::----------------------------------------------------------------------------------------------------------------------
__infesl.clear();
{? __infesl.first() || {! |? __infesl.del() !} ?};

_czypal:=ST.MAG().PAL='T';
_pal:=tab_tmp(2,'RMAT','STRING[16]',''
       ,'RPAL','STRING[16]','');

{? ST.MAG().MWS='T'
|| SL.index('MGLOK');
   SL.prefix(ST.MAG,__infean.EAN)
|| SL.index('EANL');
   SL.prefix(ST.MAG,exec('FindInSet','#table','EANL','KOD',__infean.EAN,__infean.EAN))
?};
{? SL.first()
|| {!
   |? {? SL.IL>0
      || __infesl.clear;
         {? __infesl.find_key(SL.M().KTM,SL.M().KTM)
         || __infesl.ILE+=SL.IL;
            {? _czypal
            || _pal.clear();
               {? ~_pal.find_key($SL.M,{? SL.PAL<>null || $SL.PAL || 'brak' ?})
               || __infesl.ILZ+=1;
                  _pal.blank();
                  _pal.RMAT:=$SL.M;
                  _pal.RPAL:={? SL.PAL<>null || $SL.PAL || 'brak' ?};
                  _pal.add(1)
               ?}
            || __infesl.ILZ+=(SL.IL/({? SL.M().ILOBJ>0 || SL.M().ILOBJ || 1 ?}))*SL.M().OBJ
            ?};
            __infesl.PRO:=exec('procwg','magazyn_stan',__infesl.ILZ,__infean.ILC);
            __infesl.put(1)
         || __infesl.blank();
            __infesl.KTM:=__infesl.KT2:=SL.M().KTM;
            __infesl.ILE:=SL.IL;
            {? _czypal
            || _pal.clear();
               {? ~_pal.find_key($SL.M,{? SL.PAL<>null || $SL.PAL || 'brak' ?})
               || __infesl.ILZ:=1;
                  _pal.blank();
                  _pal.RMAT:=$SL.M;
                  _pal.RPAL:={? SL.PAL<>null || $SL.PAL || 'brak' ?};
                  _pal.add(1)
               ?}
            || __infesl.ILZ:=(SL.IL/({? SL.M().ILOBJ>0 || SL.M().ILOBJ || 1 ?}))*SL.M().OBJ
            ?};
            __infesl.PRO:=exec('procwg','magazyn_stan',__infesl.ILZ,__infean.ILC);
            __infesl.JM:=SL.M().J().KOD;
            __infesl.SQL:=$SL.M;
            __infesl.add(1)
         ?}
      ?};
      SL.next()
   !}
?};

obj_del(_pal);

__infegr.clear();
{? __infegr.first() || {! |? __infegr.del() !} ?};

{? ST.MAG().MWS='T'
|| MG_GRP.index('EANLKOD');
   MG_GRP.prefix(ST.MAG,__infean.EAN);
   {? MG_GRP.first()
   || {!
      |? __infegr.clear;
         {? ~__infegr.find_key(MG_GRP.MG_GR().KOD,MG_GRP.MG_GR().KOD,MG_GRP.LP)
         || __infegr.blank();
            __infegr.KOD:=__infegr.KO2:=MG_GRP.MG_GR().KOD;
            __infegr.OPI:=MG_GRP.MG_GR().OPIS;
            __infegr.POZ:=MG_GRP.LP;
            __infegr.add(1)
         ?};
         MG_GRP.next()
      !}
   ?}
?};

__infesl.clear();
__infesl.first();
grp_disp(__infesl,__infesl.win_sel('?'));
{? ST.MAG().MWS='T'
|| __infegr.clear();
   __infegr.first();
   grp_disp(__infegr,__infegr.win_sel('?'))
?};
1


\procwg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: oblicza procent zajetosci w stosunku do lokalizacji
::   WE: _a - ilosc
::       _b - ilosc calosciowa
::  OLD: \procwg/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? _b || (_a/_b)*100 || 0 ?}$2


\pro_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: pole __infean.PRO - kolorowanie
::  OLD: \pro_proc/mws.fml
::----------------------------------------------------------------------------------------------------------------------
exec('rekprzed','color','__infean#PRO#01')


\col_pfld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: kolor na rekordzie __infean.PRO
::  OLD: \col_pfld/mws.fml
::----------------------------------------------------------------------------------------------------------------------
'__infean#PRO#01#01'


\stan_mg_up
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: stany magazynowe wg uprawnień użytkownika
::   WE: _a - miejsce wywołania:
::            S - dokumenty sprzedaży
::            Z - zamówienia sprzedaży
::            W - zamówienia wewnętrzne
::            K - dokumenty zakupu
::            D - zamówienia dostaw
::----------------------------------------------------------------------------------------------------------------------
POMOC.RODZ:='T';
M.win_edit('RED');
DPOZ.DT:=date();

_par:={? 'KD'*_a || 'ALL'
      |? _a='S'  || 'SPR'
      |? 'ZW'*_a || 'ZAM'
      || ''
      ?};
{? _a='S' || BEER.RS:=7
|? _a='Z' || BEER.RS:=2
|? _a='W' || BEER.RS:=9
|? _a='K' || BEER.RS:=3
|? _a='D' || BEER.RS:=3
?};
{? ~exec('samgdost','mat_atr',_par)
|| M.btn_sopt('STANY_UP','ATRYB','state=grayed');
   M.btn_sopt('STANY_UP','FIND','state=grayed')
|| M.btn_sopt('STANY_UP','ATRYB','state=normal');
   M.btn_sopt('STANY_UP','FIND','state=normal')
?};

exec('addsmmag','magazyn_stan');
M.index('ARODZ');
M.prefix('T','T');
~~


\stn_upr_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: wyświetla stany dla upranionych magazynów
::----------------------------------------------------------------------------------------------------------------------
__smmag.select()


\rek_mate_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: akcja rekord na materiale dla okienek STANY_UP
::   WY: kod koloru
::----------------------------------------------------------------------------------------------------------------------
exec('zwr_stan','magazyn_stan',M.ref,BEER.RS,null,1,,,,1);
ZMIENNE.MGAT:=M.MGAT().KOD;
ZMIENNE.MODM:=M.MODM().KOD;
BEER.BADSEH:=exec('FindInSet','#table','M_DOD','M_DOD',M.ref(),exec('firma','ustawienia'),"M_DOD.BADSEH",,,null());
VAR.DEFSTATS:=exec('statsM','statexam',M.ref());
exec('ustawJMwym','jm');
{? var_pres('__mz_gr')<1 & var_pres('__smmag')>117 & __smmag.fld_acr(1)='MAG'
|| {? __smmag.f_active() || __smmag.f_rfresh(); __smmag.f_first() || __smmag.first() ?};
   grp_disp(__smmag,__smmag.win_sel('?'))
?};
exec('rekprzed','color','M#02')


\obl_dozam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: obliczenie zmiennych Do zamówienia oraz zamówienia ustawia BEER.
::   WE: _a - indeks materiałowy
::       _b - tabela magazynów do sprawdzenia
::       _c - ilość wypełnionych rekordów w tabeli
::----------------------------------------------------------------------------------------------------------------------
REZ.cntx_psh();
ZK_P.cntx_psh();

_lstmag:=';';
_uprzam:=0;
_uprzaw:=0;

_mag:=_b;
_len:=_c;
BEER.SSZ:=0;
BEER.SRP:=0;
{! _i:=1.._len
|! REZ.index('MG_M');
   REZ.prefix(_mag[_i],_a,'W','Z');
   {? REZ.first()
   || {!
      |? BEER.SRP+=REZ.ILR;
         REZ.next()
      !}
   ?};
   _lstmag+=(8-($_mag[_i]))+';';
   {? ~_uprzam || _uprzam:=exec('uprmgpow','magazyn_stan',_mag[_i],2,OPERATOR.USER) ?};
   {? ~_uprzaw || _uprzaw:=exec('uprmgpow','magazyn_stan',_mag[_i],9,OPERATOR.USER) ?}
!};
REZ.index('MG_M');
REZ.prefix(null(),_a,'W','Z');
{? REZ.first()
|| {!
   |? BEER.SRP+=REZ.ILR;
      REZ.next()
   !}
?};

ZK_P.index('CZYBEZ');
ZK_P.prefix('A','T',1,_a);
{? ZK_P.first()
|| {!
   |? {? {? ZK_P.RMAG<>null()
         || (_lstmag*(8-($ZK_P.RMAG)))>1
         || (ZK_P.RODZ='Z' & _uprzam) | (ZK_P.RODZ='W' & _uprzaw)
         ?}
      || BEER.SSZ+=ZK_P.ILBEZ
      ?};
      ZK_P.next()
   !}
?};
REZ.cntx_pop();
ZK_P.cntx_pop();
obj_del(_mag);
~~


\bds_zams
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JF [2006]
:: OPIS: przed wyswietleniem pola stan w zamiennikach
::  OLD: \bds_zams/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
SM.cntx_psh();
{? SM.index('SM'); SM.prefix(ST.MAG); SM.find_key(MZ.MZ)
|| SUM.S:=SM.S
|| SUM.S:=0
?};
SM.cntx_pop();
exec('rekprzed','color','MZ#01')


\kol_mz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: kolorowanie MZ
::  OLD: \kol_mz/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? BEER.SD=0
|| _wyn:='MZ#01#01'
|| _wyn:='MZ#01#02'
?};
_wyn


\infozzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009+]
:: OPIS: szczegoly dla zamiennikow
::  OLD: \infozzam/zkd.fml
::----------------------------------------------------------------------------------------------------------------------
M.cntx_psh();
MZ.cntx_psh();
M.clear();
{? M.seek(MZ.MZ) || exec('info_zam','magazyn_stan',BEER.RS) ?};
MZ.cntx_pop();
M.cntx_pop();
''


\infiddst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: informacja o kodach identyfikujących dostawę
::  OLD: \infiddst/ean2.fml
::----------------------------------------------------------------------------------------------------------------------
SLD.index('SL');
SLD.prefix(SL.ref());
{? SLD.first()
|| SLD.win_sel('WER');
   SLD.select()
|? ~((';DAPZ'*SL.M().IDMOB)>1)
|| FUN.info('Dla indeksu: %1 brak identyfikacji wg dostawy.'@[SL.M().KTM])
|| FUN.info('Brak kodów identyfikujących.'@)
?}


\dispEANL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: wyswietla informacje o lokalizacji
::  OLD: \dispeanl/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? EANL.MG().MWS='T'
|| exec('pw_segm','magazyn_stan',1)
?};
EANL.display()


\rek_zzdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: rekord dla zamiennika
::  OLD: \rek_zzdk/pommag.fml
::----------------------------------------------------------------------------------------------------------------------
{? (2+BEER.MENU_PTH)='ZW' & ZD_POZ.MG<>null
|| exec('zwr_stan','magazyn_stan',MZ.MZ,1,ZD_POZ.MG,0)
|? (2+BEER.MENU_PTH)='ZW' & ZD_NAG.MG<>null
||
:: akcja dolacz grupowo dla zamowienia dostaw moze nie miec pozycji
   exec('zwr_stan','magazyn_stan',MZ.MZ,1,ZD_NAG.MG,0)
|? (2+BEER.MENU_PTH)='ZW' & ZD_NAG.MG=null
|| exec('zwr_stan','magazyn_stan',MZ.MZ,3,null,0)
|? HELP.WHERE='G' & FAP.FAKS & FAP.FAKS().MAG=null
|| exec('obl_stan','magazyn_stan',MZ.MZ,7,,1,,FAP.FAKS().D)
|? HELP.WMAG=3
|| exec('zwr_stan','magazyn_stan',MZ.MZ,4,null,0)
|| exec('obl_stan','magazyn_stan',MZ.MZ,1,ST.MAG)
?};
BEER.SD>0;
exec('rekprzed','color','MZ#01')


\akt_cstm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: wypelnia dane dla danej tabeli
::   WE: _a - ref magazynu
::       _b - ref materialu
::       _c - ref SQL cechy lub 'brak'
::       _d - do rozpisania
::       _e - ilosc juz rozpisana
::       _f - stan na dostawie
::  OLD: \akt_cstm/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------

{? _>=6 || {? type_of(_f)<>1 || _f:=0 ?} || _f:=0 ?};

_wyn:=obj_new(2);
_wyn[1]:=_d;
_wyn[2]:=_e;
_ile:=0;

__stm.clear();
{? ~__stm.find_key($_a,$_b,_c)
|| {? _f<>0
   || _stan:=_f
   || exec('obl_stan','magazyn_stan',_b,1,_a);
      _stan:=BEER.SD
   ?};
   __stm.blank();
   __stm.MAG:=$_a;
   __stm.MAT:=$_b;
   __stm.DKC:=_c;
   __stm.SD:=_stan;
   __stm.SR:=0;
   __stm.add(1);
   {? __stm.SD>0
   || {? __stm.SD>=_wyn[1]
      || _ile:=_wyn[1];
         _wyn[2]+=_wyn[1];
         __stm.SR+=_wyn[1];
         __stm.put(1);
         _wyn[1]:=0
      || _ile:=__stm.SD;
         _wyn[2]+=__stm.SD;
         __stm.SR+=__stm.SD;
         __stm.put(1);
         _wyn[1]-=__stm.SD
      ?}
   ?}
|| {? _f<>0
   || __stm.SD+=_f;
      __stm.add(1)
   ?};
   _stn:=__stm.SD-__stm.SR;
   {? _stn>0
   || {? _stn>=_wyn[1]
      || _ile:=_wyn[1];
         _wyn[2]+=_wyn[1];
         __stm.SR+=_wyn[1];
         __stm.put(1);
         _wyn[1]:=0
      || _ile:=_stn;
         _wyn[2]+=_stn;
         __stm.SR+=_stn;
         __stm.put(1);
         _wyn[1]-=_stn
      ?}
   ?}
?};
:: pomniejszenie stanu dostepnego w calym magazynie
{? _c<>'brak'
|| __stm.clear();
   {? ~__stm.find_key($_a,$_b,'brak')
   || exec('obl_stan','magazyn_stan',_b,1,_a);
      _stan:=BEER.SD;
      __stm.blank();
      __stm.MAG:=$_a;
      __stm.MAT:=$_b;
      __stm.DKC:=_c;
      __stm.SD:=_stan;
      __stm.SR:=_ile;
      __stm.add(1)
   || __stm.SR+=_ile;
      __stm.put(1)
   ?}
?};
_wyn


\pob_dane2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr], MZ [2008]
:: OPIS: podobna formula do oblicz stan
::   WE: _a - indeks materialowy
::       _b - wg danego magazynu 1 wg operatora zam.sprz.  2 wg wszystkich uprawnionych magazynow 3 wg mag.oddzialu 5
::            wg operatora zamowien wewnetrznych 9
::       _c - wg magazynu
::  OLD: \pob_dane/zkd.fml
::----------------------------------------------------------------------------------------------------------------------
_wmg:={? _=2 || _wmg:=ST.MAG || _c ?};
M.cntx_psh();
MZ.cntx_psh();
SM.cntx_psh();
ZK_N.cntx_psh();
ZK_P.cntx_psh();
ZL.cntx_psh();

:: uprawnione magazyny
{? _b=1 || _ile:=1; _mag:=obj_new(_ile); _mag[1]:=_wmg
|? _b=2 | _b=9
        || USERS_UP.index('MG');
           USERS_UP.prefix(OPERATOR.USER,'ZA'+{? _b=2 || 'M' || 'W' ?},ST.ODDZ_KOD);
           {? USERS_UP.first
           || _ile:=USERS_UP.size;
              _mag:=obj_new(_ile);
              _i:=0;
              {!
              |? _i+=1;
                 _mag[_i]:=USERS_UP.MG;
                 USERS_UP.next
              !}
           ||
::            parametry globalne
              USERS_UP.index('MG');
              USERS_UP.prefix(null,'ZA'+{? _b=2 || 'M' || 'W' ?},ST.ODDZ_KOD);
              {? USERS_UP.first
              || _ile:=USERS_UP.size;
                 _mag:=obj_new(_ile);
                 _i:=0;
                 {!
                 |? _i+=1;
                    _mag[_i]:=USERS_UP.MG;
                    USERS_UP.next
                 !}
              || _ile:=0
              ?}
           ?}
|? _b=3 || USERS_UP.index('MG');
           USERS_UP.prefix(OPERATOR.USER,'MG',ST.ODDZ_KOD);
           {? USERS_UP.first
           || _ile:=USERS_UP.size;
              _mag:=obj_new(_ile);
              _i:=0;
              {!
              |? _i+=1;
                 _mag[_i]:=USERS_UP.MG;
                 USERS_UP.next
              !}
           || _ile:=0
           ?}
|? _b=5 || MG.cntx_psh();
           MG.index('MAG');
           MG.prefix(ST.ODDZ_KOD);
           {? MG.first()
           || _ile:=MG.size();
              _mag:=obj_new(_ile);
              _i:=0;
              {!
              |? _i+=1;
                 _mag[_i]:=MG.ref();
                 MG.next()
              !}
           || _ile:=0
           ?};
           MG.cntx_pop()
|| _ile:=0
?};

{? _ile
||
::
:: aktualizacja stanow na magazynach
   _sd:=obj_new(_ile);
   {! _i.. _ile
   |! exec('obl_stan','magazyn_stan',_a,1,_mag[_i],,,,,,0);
      _sd[_i]:=BEER.SD;
      ~~
   !};
:: obliczamy stan wg dokumentow na dana chwile
:: _dp[1] - ?
:: _dp[2] - SM.S
:: _dp[3] - suma DK.IL w wydaniu
:: _dp[4] - zmniejszenia dostepnego
   _dp:=obj_new(4);    {! _i..4    |! _dp[_i]:=0 !};
   _buf:=obj_new(4); {! _i..4 |! _buf[_i]:=0 !};
   {! _i.._ile
   |!
      _sto:=exec('FindInSet','#table','SM','SM',_a,_mag[_i],"SM.S_O",,,0);
      _stn:=exec('FindInSet','#table','SM','SM',_a,_mag[_i],"SM.S_N",,,0);
      _stz:=exec('FindInSet','#table','SM','SM',_a,_mag[_i],"SM.S",,,0);

      _dp[2]+=_stz;
      _dp[4]+=_sto+_stn;
      _stz-=_dp[4];

      BEER.MG:=_mag[_i];
      _korzen:=exec('add_infz','magazyn_stan',1,1,'Magazyn: %1'@[BEER.MG().SYM],_stz+_sto+_stn,_i);
      MG.cntx_psh();
      {? 1+BEER.MG().TYP='D'
      || exec('add_infz','magazyn_stan',1,1,'Nieokreślone'@,_sto,_i+1,_korzen,exec('get','#params',540011,2));
         exec('add_infz','magazyn_stan',1,1,'Zgodne'@      ,_stz,_i+2,_korzen,exec('get','#params',540012,2));
         exec('add_infz','magazyn_stan',1,1,'Niezgodne'@   ,_stn,_i+3,_korzen,exec('get','#params',540013,2));
         ~~
      ?};
      MG.cntx_pop();

      ND.cntx_psh();
      DK.cntx_psh();

      OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      ||
         {!
         |?
            DK.use('dokma'+ST.ODDZ+($OKR.ROK+2));
            ND.use('nagdo'+ST.ODDZ+($OKR.ROK+2));

            DK.index('DOKPLUS');
            DK.prefix(_mag[_i],_a,'N','N');
            {? DK.first()
            || {!
               |?
                  _dp[3]+=DK.IL;
                  {? DK.IL>0
                  || exec('add_infz','magazyn_stan',2,2,'Dokument: %1, %2'@[DK.N().TYP().T,DK.N().SYM]+
                      {? DK.N().KH<>null || ', dla: %1'@[DK.N().KH().SKR] || '' ?}+', z dnia: %1'@[form(DK.N().D)],DK.IL,_i)
                  ?};
                  DK.next()
               !}
            ?};

            OKR.next()
         !}
      ?};

      ND.cntx_pop();
      DK.cntx_pop();

      EANP.cntx_psh();
      EANP.index('TYP');
      EANP.prefix('Z',_a,_mag[_i]);
      {? EANP.first()
      || {!
         |? {? EANP.EANN<>null & EANP.EANN().AKT='T'
            || _dp[3]+=EANP.IL;
               {? EANP.IL>0
               || exec('add_infz','magazyn_stan',2,2,'Kompletacja wysyłki: Czytnik %1'
                   ', z dnia: %2'@[form(EANP.EANN().NRC),form(EANP.EANN().DATA)],EANP.IL,_i)
               ?}
            ?};
            EANP.next()
         !}
      ?};
      EANP.index('TYP');
      EANP.prefix('W',_a,_mag[_i]);
      {? EANP.first()
      || {!
         |? {? EANP.EANN<>null & EANP.EANN().AKT='T'
            || _dp[3]+=EANP.ILS;
               {? EANP.ILS>0
               || exec('add_infz','magazyn_stan',2,2,'Wydanie magazynowe: Czytnik %1'
                   ', z dnia: %2'@[form(EANP.EANN().NRC),form(EANP.EANN().DATA)],EANP.ILS,_i)
               ?}
            ?};
            EANP.next()
         !}
      ?};
      EANP.cntx_pop();

      _ilw:=_dp[3]-_buf[2];
      _buf[1]:=_dp[2]-_dp[3];
      _buf[2]:=_dp[3];

      {? _ilw>0
      || BEER.MG:=_mag[_i];
         exec('add_infz','magazyn_stan',2,1,'Rozchody w magazynie: %1'@[BEER.MG().SYM],_ilw,_i)
      ?}
   !};
:: zamowienia
:: _dr[1]   ZK_P.T<>'TPLM
:: _dr[2] - ZK_P.T='T'
:: _dr[3] - ZK_P.T='F','Z'
:: _dr[4] - ZK_P.T='P'
:: _dr[5] - ZK_P.T='L'
:: _dr[6] - ZK_P.T='M'
:: _dr[7] - zadania dostaw (Projekty)
:: _dr[8] - ZK_P.T='Z'
   _dr:=obj_new(11); {! _i..11 |! _dr[_i]:=0 !};

:: obliczamy ilosci rezerwacji wg zamowien na dana chwile
   _buf[1]:=0; _buf[2]:=0;  _buf[3]:=0;  _buf[4]:=0;
   {! _i.._ile
   |! _rt:=0;
      _rw:=0;
      _dr[8]:=0; _dr[11]:=0;
      ZK_P.cntx_psh;
      ZK_P.index('AKT');
      ZK_P.prefix('A',_mag[_i],_a);
      {? ZK_P.first
      ||
         _bufilp:=tab_tmp(1,'SQL','STRING[24]','','ILP','REAL','');
         {!
         |? _t:=1+ZK_P.T;
            _zspr:=ZK_P.RODZ='Z';
            _refsql:=$ZK_P.N+form(ZK_P.POZ,-8,0,'99');
            {? _t<>'F'
            || _dr[{? _t='T' || 2
                   |? _t='P' || 4
                   |? _t='L' || 5
                   |? _t='M' || 6
                   |? _zspr  || 1
                   || 9 ?}]+=ZK_P.ILR
            || _dp[3]+=ZK_P.ILR
            ?};
            {? _t='Z'
            ||
               _bufilp.clear;
               {? _bufilp.find_key(_refsql)
               || _bufilp.ILP:=0;
                  _bufilp.put(1)
               || _bufilp.SQL:=_refsql;
                  _bufilp.ILP:=exec('ilp2sr','zamsiw_rea',ZK_P.N,ZK_P.POZ,ZK_P.MG);
                  _bufilp.add(1)
               ?};
               {? ZK_P.REZ=0 & _bufilp.ILP<>0
               ||
                  _dr[{? _zspr || 3 || 10 ?}]+=_bufilp.ILP;
                  exec('add_infz','magazyn_stan',{? _zspr || 5.1 || 5.2 ?},1,
                   {? _zspr || 'Zamówienie sprzedaży: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD]
                            || 'Zamówienie wewnętrzne: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD] ?}
                   +{? ZK_P.N().KH<>null
                    || ', '+'dla: %1'@[ZK_P.N().KH().SKR]
                    |? ZK_P.ZL<>null
                    || ', '+'zlec.: %1, wydz.: %2'@[ZK_P.ZL().SYM,ZK_P.ZL().JORG().KOD]
                    || ''
                    ?}
                   +{? ZK_P.RMAG<>null || ', magazyn: %1'@[ZK_P.RMAG().SYM] || '' ?}
                   ,_bufilp.ILP,_i)
               ?}
            ?};
            {? (ZK_P.T='T') & ZK_P.ILR
            || _rt+=ZK_P.ILR;
               {? ZK_P.PROJEKT<>''
               || exec('projekt2projm','projekty',ZK_P.PROJEKT);
                  exec('add_infz','magazyn_stan',3,2,'Użytkownik: %1, ważna do: %2, dla projektu: %3>%4>%5>%6>%7'
                  '>%8'@[ZK_P.US().KOD,$(ZK_P.DP+ZK_P.OKR),PROJM.PROJN,PROJM.ETAPO,PROJM.ZLO,PROJM.RMSS,
                  form(PROJM.ZDNR,,,'99'),+form(PROJM.ZDLP,,,'99')]
                        ,ZK_P.ILR,_i)
               || exec('add_infz','magazyn_stan',3,2,'Użytkownik: %1, dla: %2'
                     ', ważna do: %3'@[ZK_P.US().KOD,ZK_P.KH().SKR,$(ZK_P.DP+ZK_P.OKR)],ZK_P.ILR,_i)
               ?}
            |? (ZK_P.T='F') & ZK_P.ILR
            || _rw+=ZK_P.ILR;
               FAKS.cntx_psh(); FAP.cntx_psh();
               {? (_msk:=ref_name(ZK_P.FAP))<>''
               || FAKS.use('faktu'+(_msk+3));
                  FAP.use(_msk)
               ?};
               exec('add_infz','magazyn_stan',2,2,'Faktura: %1, wystawił: %2, dla: %3'
               ', w dniu: %4'@[ZK_P.FAP().FAKS().SYM,ZK_P.FAP().FAKS().USER,ZK_P.FAP().FAKS().KH().SKR,form(ZK_P.FAP().FAKS().DW)]
                ,ZK_P.ILR,_i);
               FAKS.cntx_pop(); FAP.cntx_pop()
            |? (ZK_P.T='Z') & ZK_P.ILR
            || _dr[{? _zspr || 8 || 11 ?}]+=ZK_P.ILR;
               exec('add_infz','magazyn_stan',{? _zspr || 4.1 || 4.2 ?},2,
                {? _zspr || 'Zamówienie sprzedaży: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD]
                         || 'Zamówienie wewnętrzne: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD] ?}
                +{? ZK_P.N().KH<>null
                 || ', '+'dla: %1'@[ZK_P.N().KH().SKR]
                 |? ZK_P.ZL<>null
                 || ', '+'zlec.: %1, wydz.: %2'@[ZK_P.ZL().SYM,ZK_P.ZL().JORG().KOD]
                 || ''
                 ?}
                ,ZK_P.ILR,_i)
            ?};
            {? ZK_P.REZ=1 & ZK_P.ILP>ZK_P.SR & ZK_P.TOP=1
            || {? ZK_P.T='Z'
               || _dr[7]+=ZK_P.ILP-ZK_P.SR;
                  exec('add_infz','magazyn_stan',6,1,
                   {? ZK_P.RODZ='Z' || 'Zamówienie sprzedaży: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD]
                                    || 'Zamówienie wewnętrzne: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD] ?}
                   +{? ZK_P.N().KH<>null
                    || ', '+'dla: %1'@[ZK_P.N().KH().SKR]
                    |? ZK_P.ZL<>null
                    || ', '+'zlec.: %1, wydz.: %2'@[ZK_P.ZL().SYM,ZK_P.ZL().JORG().KOD]
                    || ''
                    ?}
                   ,ZK_P.ILP-ZK_P.SR,_i)
               |? ZK_P.T='L'
               || _dr[7]+=ZK_P.ILP-ZK_P.SR;
                  _txtwyd:={? ZK_P.ZL().JORG().KOD<>'' || ', na wydziale: %1'@[ZK_P.ZL().JORG().KOD] || '' ?};
                  exec('add_infz','magazyn_stan',6,1,'Zlecenie: %1%2'@[ZK_P.ZL().SYM,_txtwyd]
                      ,ZK_P.ILP-ZK_P.SR,_i)
               ?}
            ?};
            ZK_P.next
         !};
         obj_del(_bufilp)
      ?};
      ZK_P.cntx_pop;


      ZK_P.cntx_psh();
      ZK_P.index('RMAG');
      ZK_P.prefix('A',null,_a,'L',_mag[_i]);
      {? ZK_P.first() || {! |? _dr[5]+=ZK_P.ILR; ZK_P.next() !} ?};
      ZK_P.index('RMAG');
      ZK_P.prefix('A',null,_a,'M',_mag[_i]);
      {? ZK_P.first() || {! |? _dr[6]+=ZK_P.ILR; ZK_P.next() !} ?};
      ZK_P.cntx_pop();

      _buf[1]+=-(_dr[1]+_dr[9])-_dr[2];
      _buf[3]+=_dr[2];
      _rr:=(_dr[1]+_dr[9])-_buf[2]; _buf[2]+=(_dr[1]+_dr[9]);
      _zz:=(_dr[3]+_dr[10])-_buf[4]; _buf[4]+=(_dr[3]+_dr[10]);
      {? _rt>0 || BEER.MG:=_mag[_i]; exec('add_infz','magazyn_stan',3,1,'Magazyn: %1'@[BEER.MG().SYM],_rt,_i) ?};
      {? _dr[8]>0 || BEER.MG:=_mag[_i]; exec('add_infz','magazyn_stan',4.1,1,'Magazyn: %1'@[BEER.MG().SYM],_dr[8],_i) ?};
      {? _dr[11]>0 || BEER.MG:=_mag[_i]; exec('add_infz','magazyn_stan',4.2,1,'Magazyn: %1'@[BEER.MG().SYM],_dr[11],_i) ?};
      {? _rw>0 || BEER.MG:=_mag[_i]; exec('add_infz','magazyn_stan',2,1,'Faktury do magazynu: %1'@[BEER.MG().SYM],_rw,_i) ?}
   !};
:: obliczamy ilosci zamawiane bez magazynu
   ZK_P.index('AKT');
   ZK_P.prefix('A',null,_a);
   {? ZK_P.first
   || {!
      |?
         _t:=1+ZK_P.T;
         {? _t<>'F'
         || _dr[{? _t='T' || 2 |? _t='P' || 4 |? (_t='L' | _t='M') & ZK_P.RMAG=null() || 5 || 4 ?}]+=ZK_P.ILR
         || _dp[3]+=ZK_P.ILR
         ?};
         {? ZK_P.TOP & ~('PLM'*_t) & ZK_P.ILR=0
         || _zspr:=ZK_P.RODZ='Z';
            {? ZK_P.ILP>0 & (ZK_P.T<>'T')
                  &
               {? _b=1
               || USERS_UP.index('MG');
                  USERS_UP.prefix(null,'ZA'+{? ZK_P.RODZ='W' || 'W' || 'M' ?},ST.ODDZ,_mag[_i]);
                  USERS_UP.first
               || 1
               ?}
            ||
               {? ZK_P.REZ=0
               || _dr[{? _zspr || 3 || 10 ?}]+=ZK_P.ILP-ZK_P.SR;
                  exec('add_infz','magazyn_stan',{? _zspr || 5.1 || 5.2 ?},1,
                   {? _zspr || 'Zamówienie sprzedaży: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD]
                            || 'Zamówienie wewnętrzne: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD] ?}
                   +{? ZK_P.N().KH<>null
                    || ', '+'dla: %1'@[ZK_P.N().KH().SKR]
                    |? ZK_P.ZL<>null
                    || ', '+'zlec.: %1, wydz.: %2'@[ZK_P.ZL().SYM,ZK_P.ZL().JORG().KOD]
                    || ''
                    ?}
                   ,ZK_P.ILP-ZK_P.SR,0)
               ?}
            ?};
            {? ZK_P.REZ=1 & ZK_P.T='Z' & ZK_P.ILP>ZK_P.SR & ZK_P.TOP=1
            || _bufdo:=ZK_P.ILP-ZK_P.SR;
               _dr[7]+=ZK_P.ILP-ZK_P.SR;
               exec('add_infz','magazyn_stan',6,1,
               {? _zspr || 'Zamówienie sprzedaży: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD]
                        || 'Zamówienie wewnętrzne: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD] ?}
                +{? ZK_P.N().KH<>null
                 || ', '+'dla: %1'@[ZK_P.N().KH().SKR]
                 |? ZK_P.ZL<>null
                 || ', '+'zlec.: %1, wydz.: %2'@[ZK_P.ZL().SYM,ZK_P.ZL().JORG().KOD]
                 || ''
                 ?}
                ,ZK_P.ILP-ZK_P.SR,_i)
            ?}
         ?};
         {? (ZK_P.T='T') & ZK_P.ILR
         || exec('add_infz','magazyn_stan',3,2,'Użytkownik: %1, dla: %2'@[ZK_P.US().KOD,ZK_P.KH().SKR],ZK_P.ILR,0)
         |? (ZK_P.T='Z') & ZK_P.ILR
         || _zspr:=ZK_P.RODZ='Z';
            exec('add_infz','magazyn_stan',{? _zspr || 4.1 || 4.2 ?},2,
             {? _zspr || 'Zamówienie sprzedaży: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD]
                      || 'Zamówienie wewnętrzne: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD] ?}
             +{? ZK_P.N().KH<>null
              || ', '+'dla: %1'@[ZK_P.N().KH().SKR]
              |? ZK_P.ZL<>null
              || ', '+'zlec.: %1, wydz.: %2'@[ZK_P.ZL().SYM,ZK_P.ZL().JORG().KOD]
              || ''
              ?}
             ,ZK_P.ILR,0)
         ?};
         ZK_P.next
      !}
   ||
::    sprawdzenie czy uprawnienia ogolne czy na uzytkowniku
::    dodatkowo obliczane ilosci do rezerwacji i do zamowienia
      USERS_UP.index('MG');
      USERS_UP.prefix(OPERATOR.USER,'ZAM',ST.ODDZ);
      {? ~USERS_UP.first()
      || USERS_UP.index('MG');
         USERS_UP.prefix(null,'ZAM',ST.ODDZ);
         {? USERS_UP.first()
         || {!
            |? {? USERS_UP.MG<>_mag[_i]
               || _ildo:=exec('FindInSet','#table','SM','SM',_a,{? _b=1 || _mag[_i] || ST.MAG ?},"SM.P",,,0);
                  ZK_P.index('AKT');
                  ZK_P.prefix('A',USERS_UP.MG,_a,'Z',1);
                  {? _ildo & ZK_P.first()
                  || {!
                     |? {? ZK_P.RMAG=null()
                        || _bufdo:=ZK_P.ILP-ZK_P.SR;
                           {? _bufdo>0 & _bufdo<=_ildo
                           || _dr[7]+=_bufdo;
                              _ildo-=_bufdo;
                              exec('add_infz','magazyn_stan',6,1,
                               {? ZK_P.RODZ='Z' || 'Zamówienie sprzedaży: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD]
                                                || 'Zamówienie wewnętrzne: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD] ?}
                               +{? ZK_P.N().KH<>null
                                || ', '+'dla: %1'@[ZK_P.N().KH().SKR]
                                |? ZK_P.ZL<>null
                                || ', '+'zlec.: %1, wydz.: %2'@[ZK_P.ZL().SYM,ZK_P.ZL().JORG().KOD]
                                || ''
                                ?}
                               ,_bufdo,0)
                           |? _bufdo>0 & _bufdo>_ildo
                           || _dr[7]+=_ildo;
                              _ildo:=0;
                              exec('add_infz','magazyn_stan',6,1,
                               {? ZK_P.RODZ='Z' || 'Zamówienie sprzedaży: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD]
                                                || 'Zamówienie wewnętrzne: %1, wystawił: %2'@[ZK_P.N().SYM,ZK_P.N().US().KOD] ?}
                               +{? ZK_P.N().KH<>null
                                || ', '+'dla: %1'@[ZK_P.N().KH().SKR]
                                |? ZK_P.ZL<>null
                                || ', '+'zlec.: %1, wydz.: %2'@[ZK_P.ZL().SYM,ZK_P.ZL().JORG().KOD]
                                || ''
                                ?}
                               ,_ildo,0)
                           ?}
                        ?};
                        ZK_P.next()
                     !}
                  ?}
               ?};
               USERS_UP.next()
            !}
         ?}
      ?}
   ?};
   _sdsum:=0;
   {! _i.._ile
   |! {? _sd[_i]>0
      || BEER.MG:=_mag[_i];
         _sdsum+=_sd[_i];
         exec('add_infz','magazyn_stan',12,1,'Magazyn: %1'@[BEER.MG().SYM],_sd[_i],_i)
      ?}
   !};

:: [RL] ilosc materialow w drodze
   _mat_w_dr:=0;
   {! _i:=1.._ile
   |!
      _mat_w_mg:=0;
      BEER.MG:=_mag[_i];
      ZD_POZ.cntx_psh();
      ZD_POZ.index('ZRE');
      ZD_POZ.prefix(_a,_mag[_i],'N');
      {? ZD_POZ.first()
      ||
         {!
         |? {? ZD_POZ.IL_POZ>0 & 'ACN'*ZD_POZ.ZD_NAG().STAN>0
            || _ilpoz:=ZD_POZ.IL_POZ;
::             szukamy potwierdzen tylko aktywnych - w biezacej masce
               ZDP_POZ.index('ZD_POZ');
               ZDP_POZ.prefix($ZD_POZ.ref(),$ZD_POZ.ref());
               {? ZDP_POZ.first()
               || ''
               || ''
               ?};
               {? _ilpoz>0
               || exec('add_infz','magazyn_stan',9,2,'Dostawa: %1, wystawił: %2'
                  ', dla: %3'@[ZD_POZ.ZD_NAG().SYM,ZD_POZ.ZD_NAG().US().KOD,ZD_POZ.ZD_NAG().KH().SKR]+
                   {? ZD_POZ.ZD_NAG().DTPREAL<>date(0,0,0) || ', planowana dostawa: %1'@[$ZD_POZ.ZD_NAG().DTPREAL] || '' ?}
                   ,_ilpoz,_i);
                  _mat_w_mg+=_ilpoz
               ?}
            ?};
            ZD_POZ.next()
         !}
      ?};
      ZD_POZ.cntx_pop();

      {? _mat_w_mg
      ||
         exec('add_infz','magazyn_stan',9,1,'Magazyn %1'@[BEER.MG().SYM],_mat_w_mg,_i);
         _mat_w_dr+=_mat_w_mg
      ?}
   !};
:: [rr] ilosc materialow w produkcji
   _mat_w_pr:=0;
   {! _i:=1.._ile
   |!
      BEER.MG:=_mag[_i];
      _mat_w_mgp:=exec('obl_prod','magazyn_stan',_a,_mag[_i],1,1,_i);
      {? _mat_w_mgp
      || exec('add_infz','magazyn_stan',7,1,'Magazyn %1'@[BEER.MG().SYM],_mat_w_mg,_i);
         _mat_w_pr+=_mat_w_mgp
      ?}
   !};
:: [mz] sprawdza stany min/max
   {! _i:=1.._ile
   |!
      BEER.MG:=_mag[_i];

      MST.index('MG');
      MST.prefix(M.ref,BEER.MG);
      {? MST.first
      ||
         {? MST.MIN>0 || exec('add_infz','magazyn_stan',0,2,'stan minimalny dla magazynu: %1'@[BEER.MG().SYM],MST.MIN,1) ?};
         {? MST.MAX>0 || exec('add_infz','magazyn_stan',0,2,'stan maksymalny dla magazynu: %1'@[BEER.MG().SYM],MST.MAX,2) ?}
      ?}
   !};
   BEER.M:=_a;
   MST.index('MG');
   MST.prefix(M.ref,null);
   {? MST.first
   ||
      exec('add_infz','magazyn_stan',0,1,'stan minimalny domyślny'@,MST.MIN,1);
      exec('add_infz','magazyn_stan',0,1,'stan maksymalny domyślny'@,MST.MAX,2)
   ?};

   _standwmg:={? _dr[5]>=_dp[2]-_dp[1]-_dp[3]-_dp[4]-_dr[1]-_dr[2]-_dr[9]
              || 0
              || _dp[2]-_dp[1]-_dp[3]-_dp[4]-_dr[1]-_dr[2]-_dr[5]-_dr[9]
              ?};
:: przypisanie do zmiennych
   BEER.M:=_a;
   exec('add_infz','magazyn_stan',0,1,'dokładność (zaokrąglenie podczas redakcji dokumentów)'@
    ,exec('jaka_dok_m','jm',BEER.M),0);
   {? _b=1
   || exec('add_infz','magazyn_stan',1,0,'STAN W MAGAZYNIE'@,_dp[2],0);

      exec('add_infz','magazyn_stan',12,0,'STAN DOSTĘPNY W MAGAZYNIE'@,_standwmg,0)
   |? _b=2 | _b=9
   || exec('add_infz','magazyn_stan',1,0,'STAN W MAGAZYNACH (wg uprawnionych magazynów do zamówień)'@,_dp[2],0);
      exec('add_infz','magazyn_stan',12,0,'STAN DOSTĘPNY W MAGAZYNACH (wg uprawnionych magazynów do zamówień)'@
           ,_standwmg,0)
   |? _b=3 | _b=5
   || exec('add_infz','magazyn_stan',1,0,'STAN W MAGAZYNACH (wg uprawnionych magazynów)'@,_dp[2],0);
      exec('add_infz','magazyn_stan',12,0,'STAN DOSTĘPNY W MAGAZYNACH (wg uprawnionych magazynów)'@,_standwmg,0)
   ?};
   exec('add_infz','magazyn_stan',2,0,'ILOŚĆ W WYDANIU'@,_dp[3],0);
   exec('add_infz','magazyn_stan',3,0,'REZERWACJE TYMCZASOWE'@,_dr[2],0);
   exec('add_infz','magazyn_stan',4.1,0,'REZERWACJE Z ZAMÓWIEŃ SPRZEDAŻY'@,_dr[1],0);
   exec('add_infz','magazyn_stan',4.2,0,'REZERWACJE Z ZAMÓWIEŃ WEWNĘTRZNYCH'@,_dr[9],0);
   exec('add_infz','magazyn_stan',5.1,0,'ILOŚĆ ZAMAWIANA Z ZAMÓWIEŃ SPRZEDAŻY'@,_dr[3],0);
   exec('add_infz','magazyn_stan',5.2,0,'ILOŚĆ ZAMAWIANA Z ZAMÓWIEŃ WEWNĘTRZNYCH'@,_dr[10],0);
   exec('add_infz','magazyn_stan',6,0,'ILOŚĆ DO REZERWACJI'@,_dr[7],0);
   _prod:=exec('tte_lic','tte');
   {? _prod='T'
   || exec('add_infz','magazyn_stan',8,0,'REZERWACJE Z HARMONOGRAMU MRP'@,_dr[6],0)
   ?};
   exec('add_infz','magazyn_stan',9,0,'ILOŚĆ W DRODZE'@,_mat_w_dr,0);
   exec('add_szcz','magazyn_stan',$BEER.M);
   exec('add_infz','magazyn_stan',0,0,BEER.M().KTM+' - '+BEER.M().N,0,0);
   exec('add_zami','magazyn_stan',_a,_b,_wmg);
   obj_del(_sd);
   obj_del(_dp);
   obj_del(_dr);
   obj_del(_mag);
   obj_del(_buf)
|| _ile:=0
?};
M.cntx_pop;
MZ.cntx_pop;
SM.cntx_pop;
ZK_N.cntx_pop();
ZK_P.cntx_pop();
ZL.cntx_pop();
1


\aktstmag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.41]
:: OPIS: aktualizacja stanow magazynowych dla wydania i kompletacji
::   WE: [_a] - material
::       [_b] - magazyn
::       [_c] - paleta
::       [_d] - maska
::       [_e] - ilosc zmieniana
::  OLD: \aktstmag/ean2.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>7 || _a:=null() ?} || _a:=null() ?};
{? _>=2 || {? type_of(_b)<>7 || _b:=null() ?} || _b:=null() ?};
{? _>=3 || {? type_of(_c)<>7 || _c:=null() ?} || _c:=null() ?};
{? _>=4
|| {? type_of(_d)<>2 || _d:=form((EANP.EANN().DATA~1)-2000,-2,0,'99') ?}
|| _d:=form((EANP.EANN().DATA~1)-2000,-2,0,'99')
?};
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0 ?};

_nopar:=_a=null() & _b=null() & _c=null();
_msk:=_d;
{? _nopar
|| _mat:=EANP.M;
   _mag:=EANP.LOKZ().MG;
   _pal:=EANP.PAL
|| _mat:=_a;
   _mag:=_b;
   _pal:=_c
?};

{? (~_nopar  | (EANP.LOKZ<>null() & (';WZ'*EANP.EANN().TYP)>1)) & _e<>0
|| {? _mat<>null()
   || exec('open','open_tab',ST.ODDZ,_msk);
      exec('obl_stan','magazyn_stan',_mat,1,_mag)
   |? _pal<>null()
   || exec('open','open_tab',ST.ODDZ,_msk);
      PAL_POZ.use('paletyp');
      PAL_POZ.index('PAL');
      PAL_POZ.prefix(_pal);
      {? PAL_POZ.first()
      || {!
         |? exec('obl_stan','magazyn_stan',PAL_POZ.M,1,_mag);
            PAL_POZ.next()
         !}
      ?}
   ?}
?};
~~


\aktustan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: aktualizacja stanu na SM-ie
::   WE: _a - ref ND
::  OLD: \aktustan/ean.fml
::----------------------------------------------------------------------------------------------------------------------
ND.cntx_psh();
DK.cntx_psh();
DK.index('DOKMAG');
DK.prefix(_a);
{? DK.first()
|| _mag:=DK.N().MAG;
   {!
   |? exec('obl_stan','magazyn_stan',DK.M,1,_mag);
      DK.next()
   !}
?};
ND.cntx_pop();
DK.cntx_pop();
~~


\odtwstop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: odtwarzanie tabeli OPAK_SM dla biezacego oddzialu
::       odtwarzane sa trzy zapisy najnizszego poziomu stanow:
::          -Nasze-  bez kaucji dla magazynu    (MAG<>'' & KH=null)
::          -All-    bez kaucji dla kontrahenta (MAG='' & KH<>null)
::          Kaucje   dla magazynu i kontrahenta (MAG<>'' & KH<>null)
::   WE: [_a] - czy komunikaty
::  OLD: \sm_odtw/opakow.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};
{? (_a=1 & FUN.ask('Odtworzyć stan magazynów opakowań dla aktywnego oddziału?'@)) | _a=0
||
   exec('rebuild','opakow_stany');

   {? _a=1 || FUN.info('Odtwarzanie stanów magazynów opakowań zakończone.'@) ?}
?};
''


\rek_wer_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PS] [17.00]
:: OPIS: Akcja rekord na materiale dla okienek WER_SEL
::----------------------------------------------------------------------------------------------------------------------
exec('actions_grayed','material');
exec('m_stan','magazyn_stan')


\blk_lokal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: koloruje badz nie w zaleznosci od pola PRO
::----------------------------------------------------------------------------------------------------------------------
{? (+__infean.EAN)=11 & __infean.BLK
|| '__infean#01#10'
|| ''
?}


\opis2Zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: tworzy opis do szczegółow pozycje z zamówieniem
::   WE: _a - 1-zamówienie sprzedaży 0-wewnętrzne 2-dyspozycja załadunku
::       _b - symbol zamówienia
::       _c - kod operatora
::       _d - kontrahent - skrót
::       _e - magazyn - symbol
::       _f - zlecenie - symbol
::       _g - zlecenie -wydział
::----------------------------------------------------------------------------------------------------------------------
{? _a=2
|| 'Dyspozycja w magazynie: %1, wystawił: %2, dla: %3, magazyn: %4'@[_b,_c,_d,_e]
|? _a=1 & _d<>''
|| {? _e<>''
   || 'Zamówienie sprzedaży: %1, wystawił: %2, dla: %3, magazyn: %4'@[_b,_c,_d,_e]
   || 'Zamówienie sprzedaży: %1, wystawił: %2, dla: %3'@[_b,_c,_d]
   ?}
|? _a=1
|| {? _e<>''
   || 'Zamówienie sprzedaży: %1, wystawił: %2, magazyn: %3'@[_b,_c,_e]
   || 'Zamówienie sprzedaży: %1, wystawił: %2'@[_b,_c]
   ?}
|? ~_a & _f<>''
|| {? _e<>''
   || 'Zamówienie wewnętrzne: %1, wystawił: %2, zlec.: %3, wydz.: %4, magazyn: %5'@[_b,_c,_f,_g,_e]
   || 'Zamówienie wewnętrzne: %1, wystawił: %2, zlec.: %3, wydz.: %4'@[_b,_c,_f,_g]
   ?}
|? ~_a
|| {? _e<>''
   || 'Zamówienie wewnętrzne: %1, wystawił: %2, magazyn: %3'@[_b,_c,_e]
   || 'Zamówienie wewnętrzne: %1, wystawił: %2'@[_b,_c]
   ?}
|| ''
?}


\regulat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: regulacja magazynu w danym okresie (dla magazynow typu FIFO, LIFO, SREDNIE)
::  OLD: \regulat/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('LKTM');
_tmg:=(1+ST.MAG().TYP);
{? 'FLŚ'*_tmg
||
   _width:=60;
   _txt:=form('UWAGA'@,_width)+'\n';
   _txt+=form('Operacja modyfikuje pozycje dokumentów rozchodowych.'@,_width)+'\n';
   _txt+=form('Zalecane jest wykonanie kopii danych.'@,_width)+'\n';
   _txt+='\n'+'Czy przeprowadzić regulację magazynu?\nMagazyn: %1 %2'@[ST.MAG().SYM,ST.MAG().NAZ];
   {? FUN.ask(_txt)
   || {? exec('reg_fifo','magazyn_stan',1,ST.AR,ST.AM)
      || FUN.info('Regulacja magazynu zakończona.\nNależy uruchomić funkcję sprawdzającą ceny\n'
                  'na przesunięciach międzymagazynowych.'@)
      ?}
   ?}
?};
VAR_DEL.delete('LKTM');
''


\reg_fifo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: regulacja magazynu typu FIFO, SREDNIE, LIFO
::       uwaga  -> wywolywane rowniez z regulacji
::       uwaga  -> funkcja powinna byc wywolywana w transakcji
::   WE: _a - czy transakcja 1- tak, 0 - nie
::       _b - Rok od ktorego zaczynamy regulacje
::       _c - Miesiac od ktorego zaczynamy regulacje
::   WY: czy dzialala funkcja regulacji
::  OLD: \reg_fifo/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
exec('mag_open','open_tab',ST.ODDZ,($_b+2));

exec('ini_kom','#message','Informacje o regulacji magazynu %1'@[ST.MAG().SYM]);
__lp:=0;
{? _a=1 || do() ?};

:: scalanie pozycji rozchodowych dokumentow magazynowych
_wyn:=exec('scalfifo','magazyn_stan','1/1 Scalenie pozycji w magazynie %1 w okresie:'@[ST.MAG().SYM],_b,_c);

:: ponowne rozpisywanie lub wycena dokumentow
_tmg:=(1+ST.MAG().TYP);
{? 'FL'*_tmg & _wyn
||
   _wyn:=exec('wycen_fi','magazyn_stan','2/3 Regulacja dokumentów w magazynie %1 w okresie:'@[ST.MAG().SYM],_b,_c)
|? 'Ś'*_tmg & _wyn
||
:: przeliczenie dokumentow rozchodowych wg kolejki
   {? _wyn || exec('wycen_sr','magazyn_stan','2/3 Wycena dokumentów w magazynie %1 w okresie:'@[ST.MAG().SYM],_b,_c) ?}
?};

:: obliczanie wartosci dokumentow
{? _wyn || exec('obl_all','magazyn_stan','3/3 Obliczanie wartości w magazynie %1 w okresie:'@[ST.MAG().SYM],_b,_c) ?};

{? _a=1 || end ?};
exec('end_kom','#message');

exec('mag_open','open_tab',ST.ODDZ,($ST.AR+2));
echo();
_wyn


\scalfifo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: scala pozycje dokumentow rozchodowych typu FIFO/LIFO
::   WE: _a - komunikat dla paska postepu
::       _b - rok
::       _c - miesiac
::   WY: czy uruchomiono scalanie dokumentow 1 - tak 0 - nie
::  OLD: \scalfifo/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_rok:=_b;
_mc:=_c;
_acr:=ND.ndx_tmp('',0,'MAG',,0,'Z',,0,'AR',,0,'AM',,0,'TYP','P',0,'D',,0,'DA',,0,'TA',,0);
OKR.index('OKR');
{? OKR.find_key(REF.FIRMA,_rok,_mc)
||
   _bie_rok:=_rok;
   {!
   |?
      {? OKR.ROK<>_bie_rok
      || exec('mag_open','open_tab',ST.ODDZ,($OKR.ROK+2));
         _bie_rok:=OKR.ROK
      ?};
      ND.clear;
      ND.index('REG');
      ND.prefix(OKR.ROK,OKR.MC,'T','N',ST.MAG);
      {? ND.first
      || _li:=0; _mi:=ND.size; _tp:=time;
         {!
         |?
            {? ND.TYP().DK='T' & ND.TYP().Z='T'
            || _li+=1
            |? ND.ZAK='N'
            ||
               _li+=1;
               echo('Przetwarzanie ... '@+$_li+'/'+$_mi,_a+' '+OKR.NAZ);
::             sprawdza czy to nie przesuniecie i czy nie bylo inwentaryzacji na drugim magazynie
               {? ND.TYP().T<>'INW-' & form(ND.TYP().TD)<>'' & ND.TYP().DK<>'T'
               ||
                  __kom_gr:='Przesunięcie %1 do magazynu %2'@[ND.SYM,ND.MD().SYM];
                  exec('sprd_inw','magdok_wspolne',ND.D,ND.MD,ND.TYP().P='T',,,ND.T);
::                sprawdza pozycje dla inwentaryzacji ciaglej
                  ND.cntx_psh();
                  DK.cntx_psh();
                  _n_utw:=sql('select /*+INDEX(ND,MM)*/ ND.REFERENCE ref, ND.NR, ND.SYM from @ND where ND.NDSQL=\':_a\''
                     ,$ND.ref());
                  {? _n_utw.first()
                  ||
                     _rwz:=_n_utw.REF;
                     _nwz:=8+_n_utw.REF;

                     _mask:=0;
                     {? ND.name()<>_nwz || _mask:=1;exec('open','open_tab',5-_nwz-2,6-_nwz) ?};

                     ND.prefix();
                     {? ND.seek(_rwz,_nwz)
                     ||
                        DK.index('DOKMAG');
                        DK.prefix(ND.ref());
                        {? DK.first()
                        ||
                           {!
                           |?
                              exec('sprd_inp','magazyn_inw',DK.M,DK.N().D,,,DK.N().T);
                              DK.next()
                           !}
                        ?}
                     ?};

                     {? _mask || exec('open','open_tab',ST.ODDZ,2-$(ST.AR)) ?}
                  ?};
                  &_n_utw;
                  DK.cntx_pop();
                  ND.cntx_pop()
               ?};
               exec('akt_fifo','magazyn_stan',ND.ref)
            ||
               undo;
               _wyn:=0;
               FUN.info('Dokument %1 jest zaksięgowany.\nRegulacja anulowana.'@[ND.SYM])
            ?};
            ND.next & _wyn=1
         !}
      ?};
      OKR.next & _wyn=1
   !};
   exec('mag_open','open_tab',ST.ODDZ,($_rok+2))
?};
{? _wyn=1 & 1+ST.MAG().TYP='Ś' & OKR.find_key(_rok,_mc)
||
   _bie_rok:=_rok;
   {!
   |?
      {? OKR.ROK<>_bie_rok
      || exec('mag_open','open_tab',ST.ODDZ,($OKR.ROK+2));
         _bie_rok:=OKR.ROK
      ?};
      ND.index(_acr);
      ND.prefix(ST.MAG,'T',OKR.ROK,OKR.MC,'T');
      {? ND.first()
      || _li:=0; _mi:=ND.size; _tp:=time;
         {!
         |?
            {? ND.TYP().DK='T' & ND.TYP().Z='T'
            || _li+=1
            |? ND.ZAK='N'
            ||
               _li+=1;
               echo('Przetwarzanie ... '@+$_li+'/'+$_mi,_a+' '+OKR.NAZ);
               DK.index('DOKMAG');
               DK.prefix(ND.ref);
               {? DK.last()
               ||
                  DK.first();
                  {!
                  |?
                     {? DK.M().RODZ='T'
                     ||
                        exec('DELSTAN','magazyn_stan',DK.N().MAG,DK.M,{? DK.PLUS='T' || ND.D || DK.DOST ?}
                         ,DK.C,DK.PLUS,DK.RDK,DK.NDK);
                        DK.next()
                     || DK.next()
                     ?}
                  !}
               ?}
            ||
               undo();
               _wyn:=0;
               FUN.info('Dokument %1 jest zaksięgowany.\nRegulacja anulowana.'@[ND.SYM])
            ?};
            ND.next & _wyn=1
         !}
      ?};
      OKR.next & _wyn=1
   !};
   exec('mag_open','open_tab',ST.ODDZ,($_rok+2))
?};
ND.ndx_drop(_acr);
_wyn


\akt_fifo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: aktualizuje dokument wg FIFO/LIFO
::   WE:  _a - dokument magazynowy
::  OLD: \akt_fifo/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
DK.index('DOKMAG');
DK.prefix(_a);
{? DK.last()
||
   DK.first();
   {!
   |? {? DK.M().RODZ='T' & DK.N().TYP().DK<>'T'
      || exec('DELSTAN','magazyn_stan',DK.N().MAG,DK.M,{? DK.PLUS='T' || ND.D || DK.DOST ?},DK.C,DK.PLUS,DK.RDK,DK.NDK);
         DK.next()
      || DK.next()
      ?}
   !};

   {? DK.N().TYP().T<>'INW-' & TYPYDOK.DK<>'T'
   || {!
      |? {? DK.ZP<>0
         || _ile:=DK.IL;
            _ok:=0;
            DK.cntx_psh();
            {? DK.seek(DK.ZP,DK.name) || _ok:=1;DK.IL+=_ile; DK.put(1) || _ok:=0 ?};
            DK.cntx_pop();
            {? ~_ok || DK.ZP:=0; DK.put(1) ?};
            DK.prev()
         || 0
         ?}
      !};
      {? DK.last()
      || _pal:=tab_tmp(1,'PAL','STRING[16]','');
         {!
         |? {? DK.ZP<>0
            || exec('delFAP_K','oplaty_dod',DK.N().uidref(),DK.uidref(),null());
               FAP_K.index('DKSQL');
               FAP_K.prefix($DK.ref());
               {? FAP_K.first() || {! |? exec('dk_k_del','magdok_poz') !}?};
               {? DK.count()>0
               || DK_L.index('DK');
                  DK_L.prefix(DK.ref(),null);
                  {? DK_L.first()
                  || {!
                     |? {? DK_L.PAL<>null
                        || _pal.clear();
                           {? ~_pal.find_key($DK_L.PAL)
                           || _pal.blank();
                              _pal.PAL:=$DK_L.PAL;
                              _pal.add(1)
                           ?}
                        ?};
                        DK_L.del()
                     !}
                  ?}
               ?};
               {? DK.PLUS='N' || exec('update','rezerwacje','DK',DK.ref(),ND.MAG,DK.M,0,DK.SRDK) ?};
::             Dla dokumentu RW sprawdzane jest, czy usuwana pozycja jest ostanią pozycją dokumentu RW danego limitu.
::             Jeżeli tak to odblokowuję możliwość rejestracji nielimitu/odpadu na zleceniu montażowym/podzleceniu.
               _chk_lim:=(exec('nd_is_rw','magdok_wspolne',DK.N) | exec('nd_is_zw','magdok_wspolne',DK.N)) & DK.ZLIM<>null()
                         & exec('FindAndGet','#table',ZLIM,DK.ZLIM,,
                            "ZLIM.PODZL<>'' & (ZLIM.SO='O' | ZLIM.SO='S' & ZLIM.LIMIT='N')",0);
               _zlim:=DK.ZLIM;
               _del:=DK.del(,1);
               {? _del & _chk_lim || exec('zlim_chk_podzl','zl_limit',_zlim) ?};
               _del>1
            || DK.RDK:=0; DK.NDK:=''; DK.C:=0; DK.WAR:=0; DK.DOST:=date(0,0,0); DK.put(1); DK.prev()
            ?}
         !};
::       aktualizacja znacznikow na paletach
         {? _pal.size() & _pal.first()
         || {!
            |? exec('znacznik','magdok_palety',exec('FindAndGet','#table','PAL',_pal.PAL,,,null()));
               _pal.next()
            !}
         ?};
         obj_del(_pal)
      ?}
   ?}
?};
1


\wycen_fi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: wycena fifo/lifo
::   WE: _a - komunikat dla paska postepu
::       _b - rok
::       _c - miesiac
::   WY: wynik dzialania
::  OLD: \wycen_fi/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_rok:=_b;
_mc:=_c;
OKR.index('OKR');
{? OKR.find_key(REF.FIRMA,_rok,_mc)
||
   _bie_rok:=_rok;
   {!
   |?
      {? OKR.ROK<>_bie_rok
      || exec('mag_open','open_tab',ST.ODDZ,($OKR.ROK+2));
         _bie_rok:=OKR.ROK
      ?};
      ND.clear;
      ND.index('REG');
      ND.prefix(OKR.ROK,OKR.MC,'T','N',ST.MAG);
      {? ND.first
      ||
         _li:=0; _mi:=ND.size; _tp:=time;
         _last:=(1+ND.MAG().TYP)='L';
         {? _last || ND.last ?};
         {!
         |?
            _li+=1;
            echo('Przetwarzenie ... '@+$_li+'/'+$_mi,_a+' '+OKR.NAZ);
            {? ND.TYP().T<>'INW-' & TYPYDOK.DK<>'T'
            || ND.cntx_psh();
               _wyn:=exec('gen_fifo','magazyn_stan',ND.ref);
               exec('gen_dost','magazyn_stan',ND.ref);
               ND.cntx_pop()
            ?};
            {? _last || ND.prev || ND.next ?}
         !}
      ?};
      OKR.next
   !}
?};
exec('mag_open','open_tab',ST.ODDZ,($_rok+2));
_wyn


\gen_fifo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: generacja nowej rozpiski fifo
::   WE: _a - ND.ref
::  OLD: \gen_fifo/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
DK.cntx_psh();
ND.cntx_psh();
DK.clear;
DK.index('DOKMAG');
DK.prefix(_a);
{? DK.first()
||
   __kom_gr:=DK.N().SYM;
   _tmg:=(1+DK.N().MAG().TYP);
   _czy__all:=exec('gen_fifo','magdok_poz',_tmg,,1);
   {? _czy__all<>0
   ||
      _wyn:=0;
      undo()
   ?}
?};
ND.cntx_pop();
DK.cntx_pop();
_wyn


\gen_dost
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: uaktualnia stany dla podanego dokumentu
::   WE: _a - ref dokumentu (ND.ref)
::  OLD: \gen_dost/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
DK.clear;
DK.index('DOKMAG');
DK.prefix(_a);
{? DK.first
|| {!
   |?
      {? DK.M().RODZ='T'
      ||
         exec('OBLSTAN','magazyn_stan',DK.N().MAG,DK.M,{? DK.PLUS<>'N' || DK.N().D || DK.DOST ?},DK.C,DK.PLUS,1
          ,DK.RDK,DK.NDK,DK.SRDK,DK.PRDK,,,,DK.SCEAN,DK.TW);
         DK.next
      || DK.next
      ?}
   !}
?};
''


\wycen_sr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: wycena fifo/lifo
::   WE: _a - komunikat dla paska postepu
::       _b - rok
::       _c - miesiac
::   WY: ~~
::  OLD: \wycen_sr/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_rok:=_b;
_mc:=_c;
OKR.index('OKR');
{? OKR.find_key(REF.FIRMA,_rok,_mc)
||
   _bie_rok:=_rok;
   {!
   |?
      {? OKR.ROK<>_bie_rok
      || exec('mag_open','open_tab',ST.ODDZ,($OKR.ROK+2));
         _bie_rok:=OKR.ROK
      ?};
      _mi:=0; _li:=0; _tp:=time();

      _acr:=ND.ndx_tmp('',0,'MAG',,0,'Z',,0,'AR',,0,'AM',,0,'D',,0,'DA',,0,'TA',,0);

      ND.clear();
      ND.index(_acr);
      ND.prefix(ST.MAG,'T',OKR.ROK,OKR.MC);
      {? ND.first()
      || _mi:=ND.size();
         {!
         |? _li+=1;
            {? ~(ND.TYP().DK='T' & ND.TYP().Z='T')
            || echo('Przetwarzanie ... '@+$_li+'/'+$_mi,_a);
               ND.cntx_psh();
               {? ND.TYP().P='N' & ND.TYP().T<>'INW-'
               || exec('gen_fifo','magazyn_stan',ND.ref());
                  exec('dk_sum','magdok_wspolne',ND.ref(),1)
               ?};
               exec('gen_dost','magazyn_stan',ND.ref());
               ND.cntx_pop()
            ?};
            ND.next()
         !}
      ?};
      ND.ndx_drop(_acr);
      OKR.next
   !};
   SM.index('SM'); SM.prefix(ST.MAG);
   {? SM.first
   ||
      {!
      |?
         exec('obl_stan','magazyn_stan',SM.M,1,ST.MAG);
         SM.next
   !}
   ?}
?};
exec('mag_open','open_tab',ST.ODDZ,($_rok+2));
~~


\obl_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: oblicza wartosc wszystkich dokumentow w magazynie od biezacego miesiaca do konca zalozonych okresow w systemie
::   WE: _a - komunikat dla paska postepu
::       _b - rok
::       _c - miesiac
::  OLD: \obl_all/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_rok:=_b;
_mc:=_c;
_korn:=tab_tmp(1
        ,'FAKS','STRING[16]',''
        ,'STAT_REJ','STRING[1]',''
        ,'ND','STRING[16]',''
        ,'TYP','STRING[8]','');
OKR.index('OKR');
{? OKR.find_key(REF.FIRMA,_rok,_mc)
||
   _bie_rok:=_rok;
   {!
   |?
      {? OKR.ROK<>_bie_rok
      || exec('mag_open','open_tab',ST.ODDZ,($OKR.ROK+2));
         _bie_rok:=OKR.ROK
      ?};
      ND.clear;
      ND.index('NAGDOK');
      ND.prefix(OKR.ROK,OKR.MC,ST.MAG);
      {? ND.first
      || _mi:=ND.size; _li:=0; _tp:=time;
         {!
         |? _li+=1;
            echo('Przetwarzanie ... '@+$_li+'/'+$_mi,_a+' '+OKR.NAZ);
            exec('dk_sum','magdok_wspolne',ND.ref,1);
            {? ND.TYP().DK='T' & ND.TYP().Z='T' & ND.FAKS_REF<>''
            || _korn.blank();
               _korn.FAKS:=ND.FAKS_REF;
               _korn.STAT_REJ:=ND.STAT_REJ;
               _korn.ND:=$ND.ref();
               _korn.TYP:=ND.TYP().T;
               _korn.add(1)
            ?};
            ND.next
         !}
      ?};
      OKR.next
   !}
?};
_korn.clear();
{? _korn.first()
|| _tran:=do_state();
   _oki:=1;
   _faks:=sql('select distinct :_a.FAKS FAKS, 0 GEN from :_a order by 1',_korn);
   _faks.clear();
   {? _faks.first()
   || exec('fak_psh','open_tab');
      {? ~_tran || do() ?};
::    generacja nowych dokumentów
      {!
      |? _oddz:=1+(form(8+_faks.FAKS)+3);
         _rok:=form(8+_faks.FAKS)+2;
         {? _oddz=ST.ODDZ
         || exec('fak_open','open_tab',_oddz,_rok);
            FAKS.prefix();
            {? FAKS.seek(_faks.FAKS)
            || _korn.clear();
               _korn.prefix(_faks.FAKS,);
               {? _korn.first()
               || {!
                  |? exec('mag_open','open_tab',ST.ODDZ,form(8+_korn.ND)+2);
                     ND.prefix();
                     {? ND.seek(_korn.ND)
                     || ND.Z:='N';
                        ND.STAT_REJ='N';
                        ND.put(1);
                        {? exec('n_usun','magdok_nag',0,0)=0 || _oki:=0; undo() ?}
                     ?};
                     _korn.next()
                  !}
               ?};
               {? _oki & exec('gen_zz','faktury_nag',,1)>0
               || _faks.GEN:=1;
                  _faks.put(1)
               || _oki:=0;
                  undo()
               ?}
            ?}
         ?};
         _oki & _faks.next()
      !};
::    aktualizacja statusów dokumentów
      {? _oki & _korn.first()
      || {!
         |? _faks.clear();
            _faks.prefix(_korn.FAKS,);
            {? _faks.first() & _faks.GEN
            || exec('mag_open','open_tab',ST.ODDZ,form(8+_korn.ND)+2);
               ND.index('FAKS_REF');
               ND.prefix(_faks.FAKS,);
               {? ND.first()
               || {!
                  |? {? ND.TYP().T=_korn.TYP
                     || {? (';ZT'*_korn.STAT_REJ)>1
                        || ND.Z:='T';
                           ND.STAT_REJ:=_korn.STAT_REJ;
                           {? ND.put(1)
                           || DK.index('DOKMAG');
                              DK.prefix(ND.ref());
                              {? DK.first()
                              || {!
                                 |? DK.Z:='T';
                                    DK.put(1);
                                    DK.next()
                                 !}
                              ?}
                           ?}
                        ?};
                        0
                     || ND.next()
                     ?}
                  !}
               ?}
            ?};
            _korn.next()
         !}
      ?};
      {? ~_tran || end() ?};
      exec('fak_pop','open_tab')
   ?};
   obj_del(_faks)
?};
obj_del(_korn);
exec('mag_open','open_tab',ST.ODDZ,($_rok+2));
''


\uzgod_mm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: wartosciowe uzgodnienie dokumentow magazynowych MM - MP (na wszystkich magazynach)
::  OLD: \uzgod_mm/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy przeprowadzić uzgodnienie wartości magazynu?\nMagazyn: %1'@[ST.MAG().SYM+' '+ST.MAG().NAZ])
||
   VAR_DEL.delete('X_Xu','maga');

   X_Xu:=tab_tmp(4,'DATA','DATE','Data'
                 ,'SYM','STRING[20]',''
                 ,'ZAK','STRING[1]',''
                 ,'WAR','REAL',''
                 ,'WAR_N','REAL',''
                 ,'OREF','INTEGER',''
                 ,'ONAM','STRING[8]',''
                 ,'NSQL','STRING[16]',''
                 ,'SYM_N','STRING[20]',''
                 ,'ZAK_N','STRING[1]',''
                 ,'MAG','STRING[10]',''
                 ,'MAG_T','STRING[10]',''
                 ,'UWAGI','STRING[255]',''
                 ,'BL','INTEGER',''
                 ,'MAG1','STRING[10]',''
                 ,'MAG1_T','STRING[10]','');

   _acr_sel:=X_Xu.mk_sel('Różnice'@,'T',0,'start_mm_acrsel',,,,,'U','T');
   X_Xu.win_fld(_acr_sel,,'DATA',,,10,,0,'Data'@);
   X_Xu.win_fld(_acr_sel,,'SYM',,,10,,0,'Symbol (z)'@);
   X_Xu.win_fld(_acr_sel,,'ZAK',,,1,,0,'Z'@);
   X_Xu.win_fld(_acr_sel,,'WAR',,,12,2,0,'Wartość (z) magazynu'@);
   X_Xu.win_fld(_acr_sel,,'MAG1',,,7,,,'(Z) magazynu'@);
   X_Xu.win_fld(_acr_sel,,'MAG1_T',,,10,,,'Typ magazynu (z)'@);
   X_Xu.win_fld(_acr_sel,,'SYM_N',,,10,,,'Symbol (do)'@);
   X_Xu.win_fld(_acr_sel,,'ZAK_N',,,1,,0,'Z'@);
   X_Xu.win_fld(_acr_sel,,'WAR_N',,,12,2,0,'Wartość (do) magazynu'@);
   X_Xu.win_fld(_acr_sel,,'MAG',,,7,,,'(Do) magazynu'@);
   X_Xu.win_fld(_acr_sel,,'MAG_T',,,10,,,'Typ magazynu (do)'@);
   X_Xu.win_fld(_acr_sel,,'UWAGI',,,10,,,'Informacja'@);
   X_Xu.win_act(_acr_sel,,'Rekord',,,,"exec('rekprzed','color','WYC_MM#01')",,0);
   X_Xu.win_act(_acr_sel,,'Formuła','Wyceń &dokument'@@,,,"exec('wycendok','magazyn_stan',1)",,1);
   X_Xu.win_act(_acr_sel,,'Formuła','Wyceń &wszystko'@@,,,"exec('wycen_all','magazyn_stan')");
   X_Xu.win_act(_acr_sel,,'Formuła','&Legenda'@@,,,"exec('legenda','color','WYC_MM#01')");
   X_Xu.win_sel(_acr_sel);

   maga:=null;
   exec('start_mm','magazyn_stan');
   X_Xu.clear();
   {? X_Xu.first
   || X_Xu.select()
   || FUN.info('Nie stwierdzono żadnych niezgodności wartościowych.'@)
   ?};
   VAR_DEL.delete('X_Xu','maga')
?};
1


\wycendok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: wycena dokument magazynowy
::   WE: _a - czy wyswietlac komunikaty (1 - tak, 0 - nie)
::   WY: czy wykonano wycene 0 - nie, 1 - tak 2 - tak (dodatkowo wykonano regulacje na innym magazynie)
::  OLD: \wycendok/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? X_Xu.SYM_N=''
|| {? _a=1 || FUN.info('Nie znaleziono powiązanego dokumentu.\nWycena niemożliwa.'@) ?}; return('')
?};

{? (X_Xu.MAG_T*'EWI')
||
   {? _a=1 || FUN.info('Dla magazynu z cenami ewidencyjnymi operacja niemożliwa.'@) ?}
|?
   {? X_Xu.BL=2
   ||
      {? _a=1 || FUN.ask('Wycenić dokumenty powiązane na innych magazynach?'@) || 1 ?}
   |? X_Xu.BL=3
   ||
      {? _a=1 || FUN.ask('Dokument powiązany mógł być redagowany ręcznie przez operatora.\n'
                         'Wycenić dokumenty powiązane na innych magazynach?'@) || 1 ?}
   |? X_Xu.BL=4
   ||
      {? _a=1 || FUN.ask('Dokument powiązany ma różną ilość pozycji.\n'
                         'Odtworzyć dokument powiązany na innym magazynie?'@) || 1 ?}
   |? X_Xu.BL=5
   ||
      {? _a=1 || FUN.info('Na magazynie powiązanego dokumentu przeprowadzona została inwentaryzacja.\n'
                          'Wycena niemożliwa.'@) ?};
      0
   ?}
||
   {? _a=1
   ||
      exec('ini_kom','#message','Informacje o wycenie dokumentów dla magazynu %1'@[ST.MAG().SYM]);
      __lp:=0
   ?};

   do();
   _ref:=X_Xu.OREF;
   _name:=X_Xu.ONAM;
   exec('mag_open','open_tab',ST.ODDZ,(X_Xu.ONAM+2));
   ND.clear();
   {? ND.seek(_ref,_name)
   ||
      _wyn:=exec('wycendo2','magazyn_stan',ND.ref,_a);
      X_Xu.del()
   ?};
   ND.clear();
   {? ND.seek(_ref,_name) || exec('addmmone','magazyn_stan') ?};
   exec('mag_open','open_tab',ST.ODDZ,($ST.AR+2));
   end();

   {? _a=1 || exec('end_kom','#message') ?}
?};
_wyn


\wycendo2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: sprawdza dla ND wygenerowany dokument przychodowy
::   WE:  _a  - ref dokumentu tworzacego
::       [_b] - czy wyswietlac komunitaty 0 - nie 1 -tak
::  OLD: \wycendo2/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2  || {? type_of(_b)<>1 || _b:=0 ?}  || _b:=0  ?};
VAR_DEL.delete('__skl','__cena','LKTM','__ilo','__poz','__dkprdk');
_ok:=0;
ND.cntx_psh();
DK.cntx_psh();
ND.clear();
{? ND.seek(_a)
||
   __kom_gr:='Przesunięcie %1 do magazynu %2'@[ND.SYM,ND.MD().SYM];
   __skl:=_skl:=ND.MAG().SKL='T';
   _poz_dok:=tab_tmp(1,
      'KTM','STRING[50]','KTM',
      'C','REAL','Cena');

:: aktualizacja pozycji w tabeli pozycji dostaw magazynowych
   exec('dkdost_trig_on','magdok_poz','put');

:: przygotowywanie sredniej ceny dla poszczegolnych materialow na dokumencie
   DK.prefix(_a);
   {? DK.first
   || {! |?
         _poz_dok.KTM:=DK.M().KTM;
         _poz_dok.C:=DK.C;
         _poz_dok.add();
         DK.next
      !}
   ?};
   _lktm:=LKTM:=sql('select KTM, KTM as KTM2, avg(C) C from :_a group by KTM order by 1,2',_poz_dok);

   _n_utw:=sql('select /*+INDEX(ND,MM)*/ ND.REFERENCE REF from @ND where ND.NDSQL=\':_a\'',$ND.ref());
   {? _n_utw.first()
   ||
      _rmp:=_n_utw.REF;
      _nmp:=8+_n_utw.REF;

      ND.cntx_psh();
      MG.cntx_psh();

      _mask:=0;
      {? ND.name()<>_nmp || _mask:=1; exec('open','open_tab',5-_nmp-2,6-_nmp) ?};

      ND.prefix();
      {? ND.seek(_rmp,_nmp)
      ||
::       MP
         _n_new:=ND.ref();

         {? ND.Z='T' & ND.ZAK<>'T'
         ||
            _tmg:=(1+ND.MAG().TYP);
            {? 'FLDŚ'*_tmg
            ||
::             sprawdza naglowek dla inwentaryzacji pelnej
               _ok:=exec('sprd_inw','magdok_wspolne',ND.D,ND.MAG,ND.TYP().P='T',,,ND.T);

               _spr:=0;
               _il_new:=0;_il_old:=0;_war_new:=0;_war_old:=0;_poz_old:=0;_poz_new:=0;

               {? _ok=1
               ||
                  DK.index('DOKMAG');
                  DK.prefix(_n_new);
                  {? DK.first()
                  ||
                     {!
                     |?
                        _il_new+=DK.IL;
                        _war_new+=DK.WAR;
                        _poz_new+=1;
::                      sprawdza pozycje dla inwentaryzacji ciaglej
                        _ok:=exec('sprd_inp','magazyn_inw',DK.M,DK.N().D,,,DK.N().T);
                        DK.next()
                     !}
                  ?}
               ?};

               {? _ok=1
               ||
                  _ok:=0;

                  DK.cntx_psh();
                  {? _mask || DK.use('dokma'+(ref_name(_a)+3)) ?};
                  DK.prefix(_a);
                  {? DK.first()
                  || {! |? _il_old+=DK.IL;_war_old+=DK.WAR;_poz_old+=1;DK.next() !}
                  ?};
                  DK.cntx_pop();

                  {? _il_new<>_il_old
                  || undo();
                     FUN.info('Niezgodności ilościowe pomiędzy'
                              '\ndokumentem rozchodowym i przychodowym.'
                              '\nWycena niemożliwa.'@)
                  ?}
               ||
                  undo()
               ?};

::             1 odtworzenie w przypadku gdy pozycje sa zgodne ze soba - najlatwiejszy przypadek
               _spr_poz:=1;
               {? _war_new<>_war_old & _poz_new=_poz_old
               ||
                  {! _il:=1.._poz_old
                  |!
                     {? _mask || DK.use('dokma'+(ref_name(_a)+3)) ?};
                     DK.index('DOKMAG');
                     DK.prefix(_a,_il);
                     {? DK.first()
                     ||
                        _mat:=#DK.M;
                        __ilo:=DK.IL;
                        {? _mask || DK.use('dokma'+(ref_name(_n_new)+3)) ?};
                        DK.index('DOKMAG');
                        DK.prefix(_n_new,_il);
                        {? DK.first()
                        ||
                           {? _mat=#DK.M & __ilo=DK.IL
                           || 1
                           || _spr_poz:=0
                           ?}
                        ||
                           _spr_poz:=0
                        ?}
                     ||
                        _spr_poz:=0
                     ?}
                  !}
               ?};

               {? _spr_poz=1
               ||
                  {! _il:=1.._poz_old
                  |!
                     {? _mask || exec('open','open_tab',ST.ODDZ,2-$(ST.AR)) ?};
                     DK.index('DOKMAG');
                     DK.prefix(_a,_il);
                     {? DK.first()
                     ||
                        {? _skl=1
                        ||
                           ND.cntx_psh();
                           __cena:=exec('cena','ceny_dok',DK.ref(),DK.N().SCKRS,DK.WAR,DK.IL);
                           ND.cntx_pop()
                        ||
                           __cena:=DK.C
                        ?};

                        __dkprdk:={? DK.N().TYP().P='N' & DK.N().TYP().TD<>'' || DK.PRDK || '' ?};

                        {? _mask || exec('open','open_tab',5-_nmp-2,6-_nmp) ?};
                        DK.index('DOKMAG');
                        DK.prefix(_n_new,_il);
                        {? DK.first()
                        ||
                           {? __cena<>DK.C
                           ||
                              _ok:=exec('mod_cend','magazyn_stan',DK.RDK,DK.NDK,__cena);
                              {? ~_ok || undo(); FUN.info('Błąd. Dokumentu nie wyceniono.'@) ?}
                           ?};
                           {? __dkprdk<>'' & __dkprdk<>DK.PRDK
                           ||
                              DK.PRDK:=__dkprdk;
                              DK.put()
                           ?}
                        ?}
                     ?}
                  !}
               ?};

::             2 przypadek -> rozna liczba pozycji ale te same ceny na pozycjach rozchowych
               {? _spr_poz=0
               ||
                  {? _mask || exec('open','open_tab',ST.ODDZ,2-$(ST.AR)) ?};
                  DK.index('DOKMAG');
                  DK.prefix(_a);
                  {? DK.first()
                  ||
                     {!
                     |?
                        _lktm.prefix(DK.M().KTM,DK.M().KTM);
                        {? _lktm.first()
                        ||
                           {? DK.C=_lktm.C
                           ||
                              _cena:={? _skl=1 || exec('cena','ceny_dok',DK.ref(),DK.N().SCKRS,DK.WAR,DK.IL) || DK.C ?};
                              _mat:=DK.M;
                              DK.cntx_psh();
                              {? _mask || exec('open','open_tab',5-_nmp-2,6-_nmp) ?};
                              _iind:=DK.ndx_tmp('',,'N',,,'M',,,'P',,);
                              DK.index(_iind);
                              DK.prefix(_n_new,_mat);
                              {? DK.first()
                              ||
                                 {!
                                 |?
                                    _ok:=exec('mod_cend','magazyn_stan',DK.RDK,DK.NDK,_cena);
                                    {? ~_ok || undo(); FUN.info('Błąd. Dokumentu nie wyceniono.'@) ?};
                                    DK.next()
                                 !}
                              ||
                                 undo();
                                 FUN.info('Nie odnaleziono prawidłowych pozycji w powiązanym dokumencie.'@)
                              ?};
                              DK.ndx_drop(_iind);
                              DK.cntx_pop()
                           ||
                              _ok:=0
                           ?}
                        ?};
                        _ok=1 & DK.next()
                     !}
                  ?}
               ?};

::             3 przypadek rozne ceny na poszczegolnych pozycjach - wariant pesymistyczny
               {? _ok=0 & _mask=0
               ||
                  {? 'FLDŚ'*_tmg
                  ||
                     DK.index('DOKMAG');
                     DK.prefix(_a);
                     {? DK.first()
                     ||
                        _odts:=1;
                        _mag:=null;

                        DK.prefix(_n_new);
                        {? DK.first()
                        ||
                           _mag:=DK.N().MAG;
                           _rok:=DK.N().AR;
                           _mc:=DK.N().AM;

::                         jesli drugi magazyn z obsługą dostaw sprawdzy czy nie bylo rozchodow dla dok. przychodowych
                           {? 1+DK.N().MAG().TYP='D' & DK.N().TYP().P='T'
                           ||
                              {!
                              |?
                                 DK.cntx_psh();
                                 DK.index('DOKPLUS');
                                 DK.prefix(_mag,DK.M,'N','T',DK.RDK,DK.NDK);
                                 {? DK.first()
                                 ||
                                    DK.cntx_pop();

                                    _odts:=0;
                                    exec('add_kom','#message','%1 w magazynie %2'
                                      ' - dostawa została wydana'@[DK.N().SYM,DK.N().MAG().SYM],4,,__lp+=1);
                                    exec('add_kom','#message','- pozycja %1'
                                      ', indeks materiałowy %2'@[$DK.P,DK.M().KTM],110,,__lp+=1)
                                 ||
                                    DK.cntx_pop()
                                 ?};

                                 DK.next()
                              !}
                           ?};

                           {? _odts=1 & DK.first()
                           || _pal:=tab_tmp(1,'PAL','STRING[16]','');
                              {!
                              |?
                                 exec('delprzyp','fakso','FAKSO','DK',DK.ref);
                                 exec('delFAP_K','oplaty_dod',DK.N().uidref(),DK.uidref(),null());
                                 FAP_K.index('DKSQL');
                                 FAP_K.prefix($DK.ref());
                                 {? FAP_K.first() || {! |? exec('dk_k_del','magdok_poz') !} ?};
                                 {? DK.count()>0
                                 || DK_L.index('DK');
                                    DK_L.prefix(DK.ref(),null);
                                    {? DK_L.first()
                                    || {!
                                       |? {? DK_L.PAL<>null
                                          || _pal.clear();
                                             {? ~_pal.find_key($DK_L.PAL)
                                             || _pal.blank();
                                                _pal.PAL:=$DK_L.PAL;
                                                _pal.add(1)
                                             ?}
                                          ?};
                                          DK_L.del()
                                       !}
                                    ?}
                                 ?};
                                 {? DK.PLUS='N' || exec('update','rezerwacje','DK',DK.ref(),ND.MAG,DK.M,0,DK.SRDK) ?};
::                               Dla dokumentu RW sprawdzane jest, czy usuwana pozycja jest ostanią pozycją dokumentu RW danego limitu.
::                               Jeżeli tak to odblokowuję możliwość rejestracji nielimitu/odpadu na zleceniu montażowym/podzleceniu.
                                 _chk_lim:=(exec('nd_is_rw','magdok_wspolne',DK.N) | exec('nd_is_zw','magdok_wspolne',DK.N)) & DK.ZLIM<>null()
                                           & exec('FindAndGet','#table',ZLIM,DK.ZLIM,,
                                              "ZLIM.PODZL<>'' & (ZLIM.SO='O' | ZLIM.SO='S' & ZLIM.LIMIT='N')",0);
                                 _zlim:=DK.ZLIM;
                                 {? DK.del(,1) & _chk_lim
                                 || exec('zlim_chk_podzl','zl_limit',_zlim)
                                 ?}
                              !};
::                            aktualizacja znacznikow na paletach
                              {? _pal.size() & _pal.first()
                              || {!
                                 |? exec('znacznik','magdok_palety',exec('FindAndGet','#table',PAL,_pal.PAL,,,null()));
                                    _pal.next()
                                 !}
                              ?};
                              obj_del(_pal)
                           ?}
                        ?};

                        {? _odts=1
                        ||
                           _odts:=0;

                           DK.index('DOKMAG');
                           DK.prefix(_a);
                           DK.first();
                           {!
                           |?
                              _mat:=#DK.M;
                              _mat2:=DK.M;
                              __ilo:=DK.IL;
                              _cen:={? _skl=1 || exec('cena','ceny_dok',DK.ref(),DK.N().SCKRS,DK.WAR,DK.IL) || DK.C ?};
                              _war:=DK.WAR;
                              __poz:=DK.P;
                              __dkprdk:={? DK.N().TYP().P='N' & DK.N().TYP().TD<>'' || DK.PRDK || '' ?};
                              DK.cntx_psh();
                              ND.cntx_psh();
                              DK.prefix(_n_new,__poz);
                              {? DK.first()
                              ||
                                 {? _mat=#DK.M & __ilo=DK.IL
                                 ||
                                    DK.IL:=__ilo;
                                    DK.C:=_cen;
                                   DK.WAR:=_war;
                                    _ok:=DK.put()
                                 ?}
                              ||
                                 _ok:=exec('adddk','magdok_poz',_n_new,_mat2,__ilo,_cen,,,,,,,,,,,,,,__dkprdk)
                              ?};
                              _odts:=exec('odtst_m','magazyn_stan',DK.M().KTM,DK.N().MD);
                              exec('dk_sum','magdok_wspolne',_n_new,1);
                              exec('obl_stan','magazyn_stan',DK.M,1,DK.N().MD);
                              ND.cntx_pop();
                              DK.cntx_pop();

                              DK.next()
                           !}
                        ?};

                        {? _mag<>null & _odts<>0
                        ||
                           mm_m:=ST.MAG;
                           ST.MAG:=_mag;
                           _ok:=exec('reg_fifo','magazyn_stan',0,_rok,_mc);
                           {? _ok=1 || _ok:=2 ?};
                           ST.MAG:=mm_m;
                           &mm_m
                        ?}
                     ?}
                  ?}
               |? _ok=0 & _mask=1
               ||
                  FUN.info('Przesunięcie między oddziałami.\n'
                           'Nie odnaleziono prawidłowych pozycji w powiązanym dokumencie.'@)
               ?}
            ?}
         ||
            {? _b=1
            || {? ND.ZAK='T'
               || undo();
                  FUN.info('Dokument zaksięgowany.'@)
               || undo();
                  FUN.info('Dokument niezaakceptowany.'@)
               ?}
            ?}
         ?}
      ?};

      {? _mask || exec('open','open_tab',ST.ODDZ,2-$(ST.AR)) ?};
      ND.cntx_pop();
      MG.cntx_pop()

   ?};
   exec('dkdost_trig_off','magdok_poz');
   VAR_DEL.delete('__skl','__cena','LKTM','__ilo','__poz','__dkprdk')
?};
DK.cntx_pop();
ND.cntx_pop();
{? _ok=0
|| undo();
   FUN.info('Wycena dokumentu nie powiodła się.'@)
?};
_ok


\mod_cend
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: modyfikuje pozycje dokumentow magazynowych - cene dostawy. Dziala dla magazynu typu FIFO LIFO i DOSTAWY
::   WE: _a - RDK
::       _b - NDK
::       _c - cena
::   WY: czy poprawiono
::  OLD: \mod_cend/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
ND.cntx_psh();
DK.cntx_psh();
SC.cntx_psh();

_ind:=DK.ndx_tmp('',,'RDK',,,'NDK',,);
DK.index(_ind);
DK.prefix(_a,_b);
_ok:=1;

:: sprawdzanie i blokowanie
{? DK.first
|| {!
   |?
      {? DK.N().ZAK='T' || _ok:=0 ?};
      {? ~DK.r_lock(1,1,1) || _ok:=0 ?};
      DK.next & _ok=1
   !}
?};

_dkprdk:={? var_pres('__dkprdk')=2 || __dkprdk || ~~ ?};
:: poprawianie
{? _ok=1
|| DK.first();
   {!
   |?
      DK.C:=_c;
      DK.WAR:=(DK.IL*DK.C)$2;
      _wyn:=DK.put;
      exec('dk_sum','magdok_wspolne',DK.N,1);
      {? DK.N().TYP().TD<>''
      ||
         exec('wycendo2','magazyn_stan',ND.ref())
      ?};
      DK.next()
   !}
?};
{? _dkprdk<>~~ || __dkprdk:=_dkprdk ?};

_ind2:=SC.ndx_tmp('',,'RDK',,,'NDK',,);
SC.index(_ind2);
SC.prefix(_a,_b);
{? SC.first
|| {!
   |?
      SC.C:=_c;
      {? SC.S=0 || SC.A:='N' || SC.A:='T' ?};
      SC.put();
      SC.next
   !}
?};
SC.ndx_drop(_ind2);

:: odblokowywanie
{? DK.first
|| {!
   |?
      DK.unlock();
      DK.next
   !}
?};

DK.ndx_drop(_ind);

SC.cntx_pop();
DK.cntx_pop();
ND.cntx_pop();
_wyn


\wycen_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: wycenia dokumeny magazynowe
::  OLD: \wycen_all/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? FUN.ask('Wycenić wszystkie dokumenty przesunięć magazynowych?'@)
||
   exec('ini_kom','#message','Informacje o wycenie dokumentów dla magazynu %1'@[ST.MAG().SYM]);
   __lp:=0;

   {!
   |?
      _ok:=exec('wycendok','magazyn_stan',0);
      {? _ok=2
      || X_Xu.erase();
         exec('start_mm','magazyn_stan')
      ?};
      {? _ok>0
      ||
         X_Xu.clear();
         _ok:=X_Xu.first
      ?};
      win_disp;
      _ok
   !};

   exec('end_kom','#message')
?};
''


\start_mm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: uzgodnienie dokumentow magazynowych z MM - MP
::  OLD: \start_mm/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_rok:=ST.AR;
_mc:=ST.AM;
OKR.index('OKR');
{? OKR.find_key(REF.FIRMA,_rok,_mc)
||
   _bie_rok:=_rok;
   {!
   |?
      {? OKR.ROK<>_bie_rok
      || exec('mag_open','open_tab',ST.ODDZ,($OKR.ROK+2));
         _bie_rok:=OKR.ROK
      ?};
      ND.index('REG');
      ND.prefix(OKR.ROK,OKR.MC,'T','N');
      {? ND.first
      ||
         _mi:=ND.size; _li:=0;
         {!
         |?
            _li+=1;
            echo('Przetwarzanie ... '@+$_li+'/'+$_mi,'Uzgodnienie przesunięć w okresie: %1'@[OKR.NAZ]);
            {? ND.TYP().TD<>'' & ND.MAG().SKL<>'T' & ND.TYP().SYS<>'L'
            ||
               exec('addmmone','magazyn_stan')
            ?};
            ND.next
         !}
      ?};
      OKR.next
   !}
?};
exec('mag_open','open_tab',ST.ODDZ,($_rok+2));
echo();
1


\wycmm_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: kolorowanie pozycji wyceny przesuniec miedzymagazynowych
::  OLD: \wycmm_kol/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? X_Xu.BL=1
|| _wyn:='WYC_MM#01#01'
|? X_Xu.BL=2
|| _wyn:='WYC_MM#01#02'
|? X_Xu.BL=3
|| _wyn:='WYC_MM#01#03'
?};
_wyn


\addmmone
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: sprawdza poszczegolne pozycje podczas sprawdzania wartosci przesuniec miedzymagazynowych
::  OLD: \addmmone/regulat.fml
::----------------------------------------------------------------------------------------------------------------------
_skl:=ND.MAG().SKL='T';
X_Xu.blank();
X_Xu.DATA:=ND.D;
X_Xu.SYM:=ND.SYM;
X_Xu.WAR:={? _skl=1 || exec('war_wyc','magazyn_stan') || exec('dk_sum','magdok_wspolne',ND.ref()) ?};
X_Xu.OREF:=#ND.ref;
X_Xu.ONAM:=ND.name;
X_Xu.ZAK:=ND.ZAK;
X_Xu.MAG1:=ND.MAG().SYM;
X_Xu.MAG1_T:=ND.MAG().TYP;
_akc:='N';
_ok:=1;

_n_utw:=sql('select /*+INDEX(ND,MM)*/ ND.REFERENCE REF from @ND where ND.NDSQL=\':_a\'',$ND.ref());
{? _n_utw.first()
||
   _rmp:=_n_utw.REF;
   _nmp:=8+_n_utw.REF;

   ND.cntx_psh();
   MG.cntx_psh();

   _mask:=0;
   {? ND.name()<>_nmp || _mask:=1; exec('open','open_tab',5-_nmp-2,6-_nmp) ?};

   ND.prefix();
   {? ND.seek(_rmp,_nmp)
   ||
::    MP
      X_Xu.SYM_N:=ND.SYM;
      X_Xu.WAR_N:=exec('dk_sum','magdok_wspolne',ND.ref());
      X_Xu.NSQL:=$ND.ref();
      X_Xu.MAG:=ND.MAG().SYM;
      X_Xu.ZAK_N:=ND.ZAK;
      X_Xu.MAG_T:=ND.MAG().TYP;
      {? X_Xu.WAR_N<>X_Xu.WAR & _akc='N'
      ||
         X_Xu.BL:=3;
         _ok:=0
      |? X_Xu.WAR_N<>X_Xu.WAR
      ||
         X_Xu.BL:=2;
         _ok:=0
      ||
::       porownuje pozycje
         DK.cntx_psh();
         ND.cntx_psh();

         DK.index('DOKMAG');
         DK.prefix(ND.ref());
         {? DK.first()
         ||
            {!
            |?
               _size:=DK.size();

               _poz:=DK.P;
               _dk_war:=DK.WAR;

               ND.cntx_psh();
               DK.cntx_psh();
               ND.prefix();
               {? ND.seek(X_Xu.OREF,X_Xu.ONAM)
               ||
                  DK.index('DOKMAG');
                  DK.prefix(ND.ref());
                  {? DK.first()
                  ||
                     {? DK.size()<>_size
                     ||
                        X_Xu.BL:=4;
                        _ok:=0
                     ||
                        {!
                        |?
                           {? DK.P=_poz & DK.WAR<>_dk_war || X_Xu.BL:=2; _ok:=0 ?};
                           _ok=1 & DK.next()
                        !}
                     ?}
                  ?}
               ?};
               DK.cntx_pop();
               ND.cntx_pop();

               _ok=1 & DK.next()
            !}
         ?};

         DK.cntx_pop();
         ND.cntx_pop()
      ?}
   ?};

   {? _mask || exec('open','open_tab',ST.ODDZ,2-$(ST.AR)) ?};

   MG.cntx_pop();
   ND.cntx_pop()
||
   X_Xu.BL:=1;
   _ok:=0
?};

{? _ok=0
||
   X_Xu.UWAGI:='';
   X_Xu.add();

   DK.cntx_psh();
   ND.cntx_psh();
   DK.index('DOKMAG');
   DK.prefix(ND.ref);
   {? DK.first
   || {!
      |?
         _dok:='';
         _rdk:=DK.RDK; _ndk:=DK.NDK;_mag:=ND.MAG;_mat:=DK.M;
         ND.cntx_psh();
         DK.cntx_psh();

         DISP.DOST:='';
         {? _rdk<>0
         ||
            _msk:=DK.name;
            ND.cntx_psh;
            DK.cntx_psh;
            ND.use('nagdo'+(_ndk+3));
            DK.use(_ndk);
            DK.index('DOST3');
            DK.prefix(_mag,_mat,_rdk,_ndk,'T');
            _dok:='';
            {? DK.first() & DK.N().NDSQL<>''
            || _dok:=$DK.N().D+' '+DK.N().SYM+' poz.'+$DK.P+'; '
            ?};
            ND.use('nagdo'+(_msk+3));
            DK.use(_msk);
            DK.cntx_pop;
            ND.cntx_pop
         ?};

         DK.cntx_pop();
         ND.cntx_pop();
         X_Xu.UWAGI+=_dok;X_Xu.put;
         DK.next
      !}
   ?};
   ND.cntx_pop();
   DK.cntx_pop()
?};
''


\war_wyc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: wartosc dokumentu z uwzglednieniem kosztow celnych
::   WY: patrz opis
::  OLD: \war/koszty.fml
::----------------------------------------------------------------------------------------------------------------------
_war:=0;
DK.cntx_psh();
DK.index('DOKMAG'); DK.prefix(ND.ref());
{? DK.first()
||
   {!
   |? _war+=(DK.IL*exec('cena','ceny_dok',DK.ref(),ND.SCKRS,DK.WAR,DK.IL)) $2;
      DK.next()
   !}
?};
DK.cntx_pop();
_war


\disp_dost_ib
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Ikona pola DISP.DOST
::   WE: _a - okno wertowania
::   WY:
::----------------------------------------------------------------------------------------------------------------------
SC.win_fml(_a,DISP,'DOST',,'ICON_BEFORE',"
   _icon:=exec('pusta','#icon');
   DK.cntx_psh();
   DK.use(8+SC.SRDK);
   DK.prefix();
   {? DK.seek(SC.SRDK)
   || DOKUML.cntx_psh();
      DOKUML.use('atta_'+(DK.name()+3));
      DOKUML.index('GRP2');
      DOKUML.prefix(DK.uidref(),);
      {? DOKUML.first() || _icon:=exec('zalacznik','icon') ?};
      DOKUML.cntx_pop()
   ?};
   DK.cntx_pop();
   _icon
")


\rezScWgAtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.41]
:: OPIS: oblicza stan podanej dostawy z uwzględnieniem rezerwacji z atrybutami
::   WE: _a - ref magazynu
::       _b - ref materialu
::       _c - SC.RDK
::       _d - SC.NDK
::       _e - SC.DK_C
::       [_f] - tabela wydań, domyślnie brak
::       [_g] - wskazanie na ref SQL ZK_P - do pominięcia
::       [_h] - kod identyfikacyjny: domyślnie brak, jeśli jest to pomijane są dostawy z nim a nie wg RDK i NDK
::   WY: ilość rezerwowana dla dostawy
::  OLD: \rezScWgAtr/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_isrez:=0;
_rdk:=_c;
_ndk:=_d;

_czywyd:=0;

{? var_pres('_f')=type_of(FIRMA) || _wyddkc:=_f; _czywyd:=1 ?};
_zkp:={? var_pres('_g')=type_of('') || _g || '' ?};
_zkn:={? _zkp<>'' || exec('FindAndGet','#table',ZK_P,_zkp,,"$N",'') || '' ?};
_scean:={? var_pres('_h')=type_of('') || _h || '' ?};

_tab:=tab_tmp(1,'DKC','STRING[16]','','ILR','REAL','');

:: pobiera dane o rezerwacja na zamówieniach bez wskazania dostawy
REZ.cntx_psh();
REZ.index('MG_M');
REZ.prefix(_a,_b,'B','Z');
{? REZ.first()
|| {!
   |? {? REZ.SC='' & REZ.DK_C<>null()
       & (_zkp='' |  (_zkp<>$REZ.ZK_P & {? REZ.REFREA<>'' || REZ.REFREA<>__refrea || _zkn<>$REZ.ZK_P().N ?}))
      || _tab.clear();
         {? _tab.find_key($REZ.DK_C,)
         || _tab.ILR+=REZ.ILR;
            _tab.put(1)
         || _tab.blank();
            _tab.DKC:=$REZ.DK_C;
            _tab.ILR:=REZ.ILR;
            _tab.add(1);
            _isrez+=1
         ?}
      ?};
      REZ.next()
   !}
?};
REZ.cntx_pop();
:: dodatkowe dane zbierane na podstawie aktywnych pozycji mobilnych z identyfikacją wg atrybutów
_beerm:=BEER.M;
BEER.M:=_b;
{? BEER.M().M_ATR<>null() & BEER.M().IDMOB='A'
|| EANN.cntx_psh();
   EANP.cntx_psh();
   EANP.index('TYP');
   EANP.prefix('Z',_b,_a);
   {? EANP.first()
   || {!
      |? {? EANP.EANN<>null & EANP.EANN().AKT='T' & EANP.EANN().STAN<>'!' & EANP.SCEAN<>''
         || _ile:={? EANP.EANN().STAN<>'Z' || EANP.IL || EANP.ILS ?};
            {? EANP.RSQL<>'' & ((5+EANP.RSQL)='zkpoz') & exec('FindAndGet','#table',ZK_P,EANP.RSQL,,"REZ",0)
            || _dkczkp:=$exec('FindAndGet','#table',ZK_P,EANP.RSQL,,"DK_C",null())
            || _dkczkp:=''
            ?};
            _rsql:=exec('FindInSet','#table','MKODK','KK',EANP.SCEAN,EANP.SCEAN,"MKODK.RSQL",,,'');
            _dkcsc:=$exec('FindAndGet','#table',DK_C,_rsql,,,null());
            {? _dkcsc<>'' & _dkcsc<>_dkczkp
            || _tab.clear();
               {? _tab.find_key(_dkcsc,)
               || _tab.ILR+=_ile;
                  _tab.put(1)
               || _tab.blank();
                  _tab.DKC:=_dkcsc;
                  _tab.ILR:=_ile;
                  _tab.add(1);
                  _isrez+=1
               ?};
               {? _dkczkp<>''
               || _tab.clear();
                  {? _tab.find_key(_dkczkp,)
                  || _tab.ILR-=_ile;
                     {? _tab.ILR>0
                     || _tab.put(1)
                     || _tab.del();
                        _isrez-=1
                     ?}
                  ?}
               ?}
            ?}
         ?};
         EANP.next()
      !}
   ?};
   EANN.cntx_pop();
   EANP.cntx_pop()
?};
BEER.M:=_beerm;
:: rozpisuje rezerwacje po dostawach z pominięciem wskazanej
{? _isrez
|| SC.cntx_psh();
   OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   {? OKR.first()
   || {!
      |? SC.use('stc__'+ST.ODDZ+(form(OKR.ROK,,,'99')+2));
         SC.index('SN');
         SC.prefix(_a,_b);
         {? SC.find_tab('first','S',,'>',0)
         || {!
            |? {? {? _scean='' || ~(SC.RDK=_rdk & SC.NDK=_ndk) || SC.SCEAN<>_scean ?}
               || _stan:=exec('obl_stad','magazyn_stan',SC.M,SC.SRDK);
                  {? _czywyd & (_wyddkc.clear(); _wyddkc.prefix(SC.RDK,SC.NDK,); _wyddkc.first())
                  || _stan:={? _wyddkc.ILW>=_stan || 0 || _stan-_wyddkc.ILW ?}
                  ?};
                  {? _stan>0
                  || _tab.clear();
                     {? _tab.first()
                     || {!
                        |? {? _tab.ILR>0
                           || _dkc:=exec('FindAndGet','#table',DK_C,_tab.DKC,,,null());
                              {? exec('czyzawch','mat_atr',_dkc,SC.DK_C)
                              || {? _tab.ILR<=_stan
                                 || _stan-=_tab.ILR;
                                    _tab.ILR:=0;
                                    _tab.put(1)
                                 || _tab.ILR-=_stan;
                                    _tab.put(1);
                                    _stan:=0
                                 ?}
                              ?}
                           ?};
                           _stan>0 & _tab.next()
                        !}
                     ?}
                  ?}
               ?};
               SC.find_tab('next','S',,'>',0)
            !}
         ?};
         OKR.next()
      !}
   ?};
   SC.cntx_pop();
   OKR.cntx_pop()
?};
_tab.clear();
{? _tab.first()
|| {!
   |? {? _tab.ILR>0
      || _dkc:=exec('FindAndGet','#table',DK_C,_tab.DKC,,,null());
         {? exec('czyzawch','mat_atr',_dkc,_e) || _wyn+=_tab.ILR ?}
      ?};
      _tab.next()
   !}
?};
obj_del(_tab);
_wyn


\ctrlREZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.41]
:: OPIS: oblicza stan podanej dostawy z uwzględnieniem rezerwacji z atrybutami
::   WE: _a - 0-F3 1-kontrola rezerwacji
::       [_b] - ilość rezerwowana do pomniejszenia
::   WY: 1-gdy REZ.ILR>0 0-gdy nie ma ilości
::  OLD: \ctrlREZ/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_minrez:={? var_pres('_b')<>type_of(0) || 0 || _b ?};

_dk_c:={? REZ.ZK_P<>null()
       || REZ.ZK_P().DK_C
       |? var_pres('__sqlzkp')>0 & __sqlzkp<>''
       || exec('FindAndGet','#table',ZK_P,__sqlzkp,,"DK_C",null())
       || REZ.DK_C
       ?};
{? REZ.SC<>'' & _dk_c<>null()
|| _rez_pr:=REZ.ILR;
   _rdk:=BB.sqlint(REZ.SC);
   _ndk:=form(8+REZ.SC);
   _sc_rez:=exec('rezScWgAtr','magazyn_stan',REZ.MG,REZ.M,_rdk,_ndk,_dk_c)-_minrez;
   {? _sc_rez<0 || _sc_rez:=0 ?};
   _dorez:={? _sc_rez>=DISP.SD || fabs(_minrez) || DISP.SD-_sc_rez ?};
   {? _rez_pr>_dorez || REZ.ILR:=_dorez ?};
   {? _a & _rez_pr<>REZ.ILR
   || FUN.info('Aktualna ilość dostępna dla dostawy to: %1.\nZmieniono ilość rezerwowaną.'@[$REZ.ILR])
   ?};
   {? REZ.ILR<=0 || _wyn:=0 ?}
?};
_wyn


\odtstwyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: odtworzenie stanów wg zapisów dla wyposażenia
::----------------------------------------------------------------------------------------------------------------------
:: usunięcie aktualnych rezerwacji
REZ.cntx_psh();
REZ.index('MGSQL');
REZ.prefix(ST.MAG,'karo');
{? REZ.first() || {! |? REZ.del() !} ?};
REZ.cntx_pop();
:: wygenerowanie rezerwacji
KARO.cntx_psh();
KARO.index('MG');
KARO.prefix(ST.MAG,'W','N');
{? KARO.first()
|| {!
   |? {? KARO.GENDK=''
      || _zwr:=exec('obl_zwr','wyp_head');
         _ile:=KARO.IL-_zwr;
         {? _ile>0
         || _nrk:=exec('update','rezerwacje','KARO',KARO.ref(),KARO.MG,KARO.M,_ile,KARO.SRDK,,,3)
         ?}
      ?};
      KARO.next()
   !}
?};
KARO.cntx_pop();
~~


\obl_prod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: obliczenie ilości w produkcji
::   WE: _a - indeks materiałowy
::       _b - magazyn
::       [_c] - 1-czy dodawać do informacji, 0-nie (domyślnie), 2-nie, ale aktualizować stany
::       [_d] - 1-dotyczy tylko jdnego magazynu, 0-nie (domyślnie)
::       [_e] - numer porządkowy informacji, 1-domyślnie
::   WY: ilość w produkcji
::----------------------------------------------------------------------------------------------------------------------
_info:={? var_pres('_c')=type_of(0) || _c || 0 ?};
_onemg:={? var_pres('_d')=type_of(0) || _d || 0 ?};
_nrinf:={? var_pres('_e')=type_of(0) || _e || 0 ?};

_res:=0;
ZL.cntx_psh();
ZL.index('SMPROD');
ZL.prefix('O',ST.ODDZ_KOD,'Z','P','Z',_a,_b);
{? ZL.first()
|| {!
   |? {? ZL.ILPDOK>0
      || _res+=ZL.ILPDOK;
         {? _info=1
         || _txtwyd:={? ZL.JORG().KOD<>'' || ', na wydziale: %1'@[ZL.JORG().KOD] || '' ?};
            exec('add_infz','magazyn_stan',7,2,
               {? _onemg=1
               || 'Zlecenie złożone: %1%2'@[ZL.SYM,_txtwyd]
               || 'Zlecenie złożone: %1%2, do magazynu: %3'@[ZL.SYM,_txtwyd,ZL.MG().SYM]
               ?}
            ,ZL.ILPDOK,_nrinf,,,$ZL.ref())
         ?}
      ?};
      ZL.next()
   !}
?};
ZL.prefix('O',ST.ODDZ_KOD,'Z','P','P',_a,_b);
{? ZL.first()
|| {!
   |? {? ZL.ILPDOK>0
      || _res+=ZL.ILPDOK;
         {? _info=1
         || _txtwyd:={? ZL.JORG().KOD<>'' || ', na wydziale: %1'@[ZL.JORG().KOD] || '' ?};
            exec('add_infz','magazyn_stan',7,2,
               {? _onemg=1
               || 'Zlecenie złożone: %1%2'@[ZL.SYM,_txtwyd]
               || 'Zlecenie złożone: %1%2, do magazynu: %3'@[ZL.SYM,_txtwyd,ZL.MG().SYM]
               ?}
            ,ZL.ILPDOK,_nrinf,,,$ZL.ref())
         ?}
      ?};
      ZL.next()
   !}
?};
ZL.cntx_pop();
{? _info=2 & _res>0
|| SM.cntx_psh();
   SM.index('SM');
   SM.prefix(_b,_a);
   {? SM.first()
   || SM.SP:=_res;
      SM.put(1)
   || M.cntx_psh();
      M.clear();
      {? M.seek(_a) & M.RODZ<>'U'
      || SM.blank();
         SM.MAG:=_b;
         SM.M:=_a;
         SM.KTM:=SM.M().KTM;
         SM.NKTM:=SM.M().N;
         SM.M_ATR:=SM.M().M_ATR_B;
         {! _wsk:=1..10 |! ($('SM.WAR'+form(_wsk,-2,0,'99')))():=($('SM.M().WAR'+form(_wsk,-2,0,'99')))() !};
         SM.MGR:=M.MGR;
         SM.MGRP:=M.MGRP;
         SM.SP:=_res;
         SM.add(1)
      ?};
      M.cntx_pop()
   ?};
   SM.cntx_pop()
?};
_res


\oblmeanpReo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: oblicza ilosc materialu na EANP wg magazynu - ilość wynikająca z reorganizacji (lokalizacja docelowa)
::   WE: _a - magazyn
::       _b - material
::       [_c] - lokalizacja
::       [_d] - termin waznosci
::       [_e] - kod identyfikujacy dostawę
::       [_f] - IDADD EANN do pominięcia
::   WY: ilosc na EANP (czytniku)
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>7 || _c:=null ?} || _c:=null ?};
{? _>=4 || {? type_of(_d)<>4 || _d:=date(0,0,0) ?} || _d:=date(0,0,0) ?};
{? _>=5 || {? type_of(_e)<>2 || _e:='' ?} || _e:='' ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};

_wyn:=0;
_min:=0;
EANN.cntx_psh();
EANP.cntx_psh();
EANP.index('TYPDO');
EANP.prefix('R',_b,_a,_c,_d);
{? EANP.first()
|| {!
   |? {? EANP.EANN<>null & EANP.EANN().AKT='T' & EANP.EANN().STAN<>'!'
       & {? _e<>'' & EANP.PAL=null || EANP.SCEAN=_e || 1 ?}
       & (_f='' | EANP.EANN().IDADD<>_f)
      || _wyn+=EANP.ILS
      ?};
      EANP.next()
   !};
   {? _min<=_wyn || _wyn-=_min || _wyn:=0 ?}
?};
:: dodatkowo sprawdzenie indeksu na paletach zakładamy, ze na EANP nie ma indeksu materiałowego
PAL.cntx_psh();
PAL_POZ.cntx_psh();
EANP.index('TYPDO');
EANP.prefix('R',null,_a,_c,_d);
{? EANP.first()
|| _beerm:=BEER.M; BEER.M:=_b;
   {!
   |? {? EANP.EANN<>null & EANP.EANN().AKT='T' & EANP.EANN().STAN<>'Z' & EANP.EANN().STAN<>'!'
       & EANP.PAL<>null & (_f='' | EANP.EANN().IDADD<>_f)
      || {? EANP.PAL().AKT='N'
         || PAL_POZ.use('palet'+form(EANP.PAL().AR-2000,-2,0,'99'))
         || PAL_POZ.use('paletyp')
         ?};
         PAL_POZ.index('PAL');
         PAL_POZ.prefix(EANP.PAL,BEER.M().KTM,BEER.M().KTM);
         {? PAL_POZ.first()
         || {!
            |? _wyn+=PAL_POZ.ILP;
               PAL_POZ.next()
            !}
         ?}
      ?};
      EANP.next()
   !};
   {? _min<=_wyn || _wyn-=_min || _wyn:=0 ?};
   BEER.M:=_beerm
?};
PAL.cntx_pop();
PAL_POZ.cntx_pop();
EANN.cntx_pop();
EANP.cntx_pop();
_wyn


\stnilwgtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: oblicza stan do danej daty dla danego terminu ważności
::   WE: _a - data
::       _b - wskazanie na magazyn
::       _c - wskazanie na material
::       _d - termin waznosci
::       _e - kod identyfikacji
::   WY: ilosc
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_a:=date();
_pal:=tab_tmp(1,'PAL','STRING[16]','');
SL.cntx_psh();
SL.index('AUTT');
SL.prefix(_b,_c,_d);
{? SL.first()
|| {!
   |? {? _e=''
      || _wyn+=SL.IL;
         {? (_pal.clear(); ~_pal.find_key({? SL.PAL<>null() || $SL.PAL || 'brak' ?}))
         || _pal.blank();
            _pal.PAL:={? SL.PAL<>null() || $SL.PAL || 'brak' ?};
            _pal.add(1)
         ?}
      || SLD.index('SL');
         SLD.prefix(SL.ref());
         {? SLD.first()
         || {!
            |? {? _e=SLD.SCEAN
               || _wyn+=SLD.IL;
                  {? (_pal.clear(); ~_pal.find_key({? SL.PAL<>null() || $SL.PAL || 'brak' ?}))
                  || _pal.blank();
                     _pal.PAL:={? SL.PAL<>null() || $SL.PAL || 'brak' ?};
                     _pal.add(1)
                  ?}
               ?};
               SLD.next()
            !}
         ?}
      ?};
      SL.next()
   !}
?};
_pal.clear();
{? _pal.first()
|| {!
   |? _rpal:={? _pal.PAL='brak' || null() || exec('FindAndGet','#table',PAL,_pal.PAL,,,null()) ?};
      _wyn-=exec('oblsldat','magazyn_stan',_c,null(),_d,_a,_rpal,_e,1);
      _pal.next()
   !}
|| _wyn-=exec('oblsldat','magazyn_stan',_c,null(),_d,_a,null(),_e,1)
?};
SL.cntx_pop();
obj_del(_pal);
_wyn


\mod_atr_sc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: Zmiana atrybutów cechy dostawy
::----------------------------------------------------------------------------------------------------------------------
{? SC.PRDK<>''
|| _scean:=SC.SCEAN;
   _prdk:={? _scean='' | exec('FindAndGet','#table',DK,SC.PRDK,,"SCEAN",'')=_scean || SC.PRDK || SC.SRDK ?};
   _msk:=form(8+_prdk)+3;
   SC.cntx_psh();
   exec('mag_psh','open_tab');
   exec('mag_open','open_tab',1+_msk,_msk+2);
   DK.prefix();
   {? DK.seek(_prdk)
   || ATR.MOD:=(DK.N().MAG().TYP*'DOST') & exec('isMOD','mat_atr',DK.M().M_ATR);
      {? ATR.MOD=1
      || {? FUN.ask('Czy zmienić atrybuty dostawy?'@) || exec('mod_atrs','magdok_wspolne',1) ?}
      || FUN.info('Funkcja zmiany atrybutów dostawy niedostępna.'@)
      ?};
      ATR.MOD:=0
   ?};
   exec('mag_pop','open_tab');
   SC.cntx_pop()
?};
~~


\sc_tmp_scean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Wypełnia tabelę tymczasową z dostawami o kodzie identyfikacyjnym podanym w parametrze
::   WE: _a - ref magazynu (MG.ref) / [null] - jak null to sprawdza dla wszystkich magazynów
::      [_b] - kod identyfikacyjny dostawy
::      [_c] - tabela tymczasowa do uzupełnienia (nie tworzy na nowo)
::      [_d] - dopuszczalne rodzaje statusow (domyślnie tylko zgodny) NOZ
::      [_e] - Ref dok. dostawy w postaci SQL - gdy podany, to zostanie dodany wyłącznie ta dostawa do tabeli
::   WY: tabela tymczasowa z listą dostaw
::----------------------------------------------------------------------------------------------------------------------
_mg:={? var_pres('_a')=type_of(null()) || _a || null() ?};
_scean:={? var_pres('_b') =type_of('') || _b || '' ?};

{? var_pres('_c')>100
|| _sc:=_c
|| _sc:=tab_tmp(3,
      'SCEAN','STRING[128]','Identyfikator dostawy'@,
      'MAG','STRING[16]','$MG.ref()',
      'M','STRING[16]','$M.ref()',
      'RDK','REAL','rdk',
      'NDK','STRING[16]','ndk',
      'SRDK','STRING[16]','srdk',
      'PRDK','STRING[16]','prdk',
      'REF','STRING[16]','ref sql',
      'DK_C','STRING[16]','$DK_C.ref()',
      'STATS','STRING[16]','$STATS.ref()'
   )
?};
_stats:={? var_pres('_d')=type_of('') || _d || 'Z' ?};
_srdk:={? var_pres('_e')=type_of('') || _e || '' ?};
SC.cntx_psh();
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |?
      SC.use('stc__'+ST.ODDZ+($OKR.ROK+2));
      {? _srdk=''
      || SC.index('SC_MG');
         {? _mg=null()
         || SC.prefix(_scean,)
         || SC.prefix(_scean,_mg)
         ?}
      || SC.index('SRDK');
         SC.prefix(_srdk,)
      ?};
      {? SC.first()
      || {!
         |?
            _add:={? SC.STATS().KIND<>'' || _stats*SC.STATS().KIND || 1 ?};
            {? _add
            || _sc.blank();
               _sc.RDK:=SC.RDK;
               _sc.NDK:=SC.NDK;
               _sc.SRDK:=SC.SRDK;
               _sc.PRDK:=SC.PRDK;
               _sc.REF:=$SC.ref();
               _sc.DK_C:=$SC.DK_C;
               _sc.M:=$SC.M;
               _sc.MAG:=$SC.MAG;
               _sc.SCEAN:=SC.SCEAN;
               _sc.STATS:=$SC.STATS;
               _sc.add()
            ?};
            SC.next()
         !}
      ?};
      OKR.next()
   !}
?};
OKR.cntx_pop();
SC.cntx_pop();

_sc


\tymc_prf_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: pokazuje okienko rezerwacji tymczasowych towaru - wypelnia tabelke wg uprawnien
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__okt')<0 | var_pres('__zk_pt')<0
|| VAR_DEL.delete('__okt');
   VAR_DEL.delete('__zk_pt');
   __okt:=exec('addzk_pt','magazyn_stan',1)
?};
M.cntx_psh();
exec('tymc_prf','rezerwacje',M.ref(),'ZAM',,0);
M.cntx_pop();
__zk_pt.first();
__zk_pt.select();
~~


\infstpal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: informacja o stanie na palecie
::----------------------------------------------------------------------------------------------------------------------
SL.cntx_psh();
SL.index('PAL');
SL.prefix(SL.MG,SL.PAL);
_win:=SL.win_sel('?');
SL.win_sel(__wer4);
SL.hdr_sel(SL.PAL().KODK);
{? SL.first() || SL.select() ?};
SL.win_sel(_win);
SL.cntx_pop();
~~


\badania_sc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: badania dla okienka z dostawami
::----------------------------------------------------------------------------------------------------------------------
exec('badp_wmag','magazyn_stan',ND.D,__sc.REF,8+__sc.REF,-1);
1


\atest_sc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: wyswietlanie atestu dla stanow magazynowych (__sc)
::----------------------------------------------------------------------------------------------------------------------
{? __sc.DK_AT<>''
|| DK_AT.cntx_psh();
   DK_AT.prefix();
   {? DK_AT.seek(__sc.DK_AT) || DK_AT.display() ?};
   DK_AT.cntx_pop()
|| FUN.info('Brak atestu dla dostawy.'@)
?};
''


\stan_lok_scean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [23.25]
:: OPIS: stan dla magazynu w kontretnej lokalizacji dla materiału z dostawy
::   WE: _a - ref magazynu
::       _b - ref materialu
::       _c - identyfikator dostawy
::       _d - lokalizacja
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
DK_L.cntx_psh();
DK_L.index('SCEAN');
DK_L.prefix('T',_a,_b,_c,_c,_d);
{? DK_L.first()
|| {!
   |? _wyn+=DK_L.IL;
      DK_L.next()
   !}
?};
DK_L.cntx_pop();
_wyn

:Sign Version 2.0 jowisz:1045 2024/02/13 12:01:07 4ceca943450f23e8271e1e8604e8cd2f61d1c6ee48d61c75c72f20b2a07c8bc6ff77799c0bb08936d2fbf92938b1e7ff1e16f4bcdce134606bdd89f1c3fe95a587e6a1ac6e1897d5ee406293e130717002a89c85d75127317721313b42674e10a7264fba3dcdc5e7b8106eafe34f5b2ca32d398bb57e2dcfd85abefb385c3181

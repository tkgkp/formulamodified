:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: zus_iwa.fml
:: Utworzony: 02.02.2021
:: Autor: j9szafra [21.14]
::======================================================================================================================
:: Zawartosc: Funkcja wspomagające tworzenie raportu pomocniczego do złożenia formularza ZUS IWA.
::======================================================================================================================


\generuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: j9szafra [21.14]
:: OPIS: Funkcja tworząca listę zestawienie osób do formularza ZUS IWA.
::   WE: _a [INTEGER] - rok wygenerowania raportu
::      [_b][INTEGER] - rodzaj raportu: 0-uproszczony, 1-pełny. Domyślnie 1-pełny
::   WY: alias do tabeli tymczasowej zawierającej listę osób uwzględnianych w raporcie w danym miesiącu
::----------------------------------------------------------------------------------------------------------------------
_ub_tmp:=
   tab_tmp(3,'REF','INTEGER','Ubezpieczony',
             'MC','INTEGER','Miesiąc',
             'ZC','INTEGER','Zleceniobiorca',
             'WYM','REAL','Wymiar zatr.',
             'OD','DATE','Data od',
             'DO','DATE','Data do',
             'IMIE','STRING[40]','Imię',
             'NAZWISKO','STRING[40]','Nazwisko',
             'ID','STRING[40]','Identyfikator',
             'ID_TYP','STRING[14]','Typ identyfikatora'
          );
{? var_pres('_a')<>type_of(0)
|| FUN.error('Błąd parametru wejściowego'@);
   return(_ub_tmp)
?};
_rok:=_a;
_pelny:={? var_pres('_b')=type_of(0) || _b || 1 ?};
ZC_INFO.cntx_psh();
P.cntx_psh();
H.cntx_psh();
OS_N.cntx_psh();
OSOBA.cntx_psh();
exec('__RUB','object');
:: parametry raportu
_zdate:=date(0,0,0);
_start:=date(_rok,1,1);
_stop:=date(_rok,12,0);

_ndx1:=_ub_tmp.ndx_tmp(,,'REF',,,'MC',,,'ZC',,);
_ndx2:=_ub_tmp.ndx_tmp(,,'ZC',,,'MC',,);
_ndx3:=_ub_tmp.ndx_tmp(,,'MC',,,'NAZWISKO',,);
_oddo:=exec('buf_oddo','rap_zus');
_zc_ndx:=ZC_INFO.ndx_tmp(,1,'FIRMA',,,'DW',,);
:: osoby zatrudnione na podstawie umowy o pracę.
P.index('PRACOSOB');
H.index('_HISTKOD');
{! _ind:=1..12
|! _start1:=date(_rok,_ind,1);
   _stop1:=date(_rok,_ind,0);
   _ub_tmp.index(_ndx1);
   P.prefix(exec('ref_firma','ustawienia'),'P');
   {? P.first
   ||
      {!
      |? {? P.DZA<=_stop1 & (P.DZ=_zdate | _start1<=P.DZ) & ~(_ub_tmp.find_key(#P.OSOBA,_ind) & _ub_tmp.WYM)
         || _data:={? P.DZ=_zdate  | _stop1<P.DZ
                   || _stop1
                   || P.DZ
                   ?};
            _ub_tmp.blank();
            _ub_tmp.REF:=#P.OSOBA;
            _ub_tmp.MC:=_ind;
            _ub_tmp.IMIE:=P.OSOBA().PIERWSZE;
            _ub_tmp.NAZWISKO:=P.OSOBA().NAZWISKO;

            {? P.OSOBA().PESEL<>''
            || _ub_tmp.ID:=P.OSOBA().PESEL;
               _ub_tmp.ID_TYP:='PESEL'
            |? P.OSOBA().PASZPORT<>''
            || _ub_tmp.ID:=P.OSOBA().PASZPORT;
               _ub_tmp.ID_TYP:='Paszport'
            || _ub_tmp.ID:=$P.OSOBA().UR_DATA;
               _ub_tmp.ID_TYP:='Data urodzenia'
            ?};
            P.cntx_psh();
            _ub_tmp.WYM:=exec('wym_zat','rap_zus',P.OSOBA,_data,2,1,'19431,112,1151,11521,1153,11541',1);
            P.cntx_pop();
            {? _ub_tmp.WYM & ~exec('okr_odd_bzus','oddelegowanie',P.ref(),_start1,_stop1) || _ub_tmp.add() ?}
         ?};
         P.next()
      !}
   ?};
:  Aktualizacja listy ubezpieczonych o zleceniobiorcow,
:  bez wzgledu na to czy maja naliczone wynagrodzenie czy nie.
ZC_INFO.index(_zc_ndx);
ZC_INFO.prefix(exec('ref_firma','ustawienia'));
{? ZC_INFO.last()
|| {!
   |? {? ZC_INFO.DW>=_start1
      || {? ZC_INFO.DU<=_stop1 & ZC_INFO.FW='T' & 2+ZC_INFO.TTUB().KOD<>'01' &
            ~(_ub_tmp.find_key(#ZC_INFO.OSOBA,_ind,0) & _ub_tmp.WYM)
         || {? _ub_tmp.find_key(#ZC_INFO.OSOBA,_ind,1)
            || {? ZC_INFO.DU<_start1
               || _ub_tmp.OD:=_start1
               || {? ZC_INFO.DU<_ub_tmp.OD || _ub_tmp.OD:=ZC_INFO.DU ?}
               ?};
               {? ZC_INFO.DW>_stop1
               || _ub_tmp.DO:=_stop1
               || {? ZC_INFO.DW>_ub_tmp.DO || _ub_tmp.DO:=ZC_INFO.DW ?}
               ?};
               _ub_tmp.put()
            || _ub_tmp.blank();
               _ub_tmp.REF:=#ZC_INFO.OSOBA;
               _ub_tmp.MC:=_ind;
               _ub_tmp.WYM:=1;
               _ub_tmp.ZC:=1;
               _ub_tmp.IMIE:=ZC_INFO.OSOBA().PIERWSZE;
               _ub_tmp.NAZWISKO:=ZC_INFO.OSOBA().NAZWISKO;
               {? ZC_INFO.OSOBA().PESEL<>''
               || _ub_tmp.ID:=ZC_INFO.OSOBA().PESEL;
                  _ub_tmp.ID_TYP:='PESEL'
               |? ZC_INFO.OSOBA().PASZPORT<>''
               || _ub_tmp.ID:=ZC_INFO.OSOBA().PASZPORT;
                  _ub_tmp.ID_TYP:='Paszport'
               || _ub_tmp.ID:=$ZC_INFO.OSOBA().UR_DATA;
                  _ub_tmp.ID_TYP:='Data urodzenia'
               ?};
               {? ZC_INFO.DU<_start1
               || _ub_tmp.OD:=_start1
               || _ub_tmp.OD:=ZC_INFO.DU
               ?};
               {? ZC_INFO.DW>_stop1
               || _ub_tmp.DO:=_stop1
               || _ub_tmp.DO:=ZC_INFO.DW
               ?};
               _ub_tmp.add()
            ?}
         ?};
         ZC_INFO.prev()
      ?}
   !}
?};
:: uwzglednienie nieobecnosci zleceniobiorcow
_ub_tmp.index(_ndx2);
_ub_tmp.prefix(1,_ind);
OSOBA.clear;
OS_N.index('ONN');
{? _ub_tmp.first()
|| {? var_pres('_NB')>100 || &_NB ?};
   _NB:=exec('buf_oddo','rap_zus');
   _NB.index(_NB.ndx_tmp(,,'OD',,));
   {!
   |? _NB.erase;
      {? OSOBA.seek(_ub_tmp.REF,)
      || _kod_str:='1151,11521,1153,11541,';
         {!
         |? _str:=_kod_str*',';
            _num:=#((_str-1)+_kod_str); _kod_str:=_str-_kod_str;
            {? _num
            || {? var_pres('_kod')>100 || &_kod ?};
               _kod:=__RUB.sys_rub(_num);
               {? _kod.first
               || {!
                  |? OS_N.prefix(exec('ref_firma','ustawienia'),OSOBA.ref,_kod.RN);
                     {? OS_N.find_le(_ub_tmp.DO) & _ub_tmp.OD<=OS_N.DO
                     || {!
                        |?
                           {? OS_N.OD>_start1 || _NB.OD:=OS_N.OD || _NB.OD:=_start1 ?};
                           {? OS_N.DO>_stop1 || _NB.DO:=_stop1 || _NB.DO:=OS_N.DO ?};
                           _NB.add;
                           OS_N.prev & OS_N.OD<=_ub_tmp.DO & _ub_tmp.OD<=OS_N.DO
                        !}
                     ?};
                     _kod.next
                  !}
               ?};
               {? _NB.first()
               || _do:=_NB.DO;
                  {!
                  |? {? _NB.OD-1=_do
                     || _do:=_NB.DO; _NB.prev(); _NB.DO:=_do; _NB.put(); _NB.next; _NB.del(,1)=2
                     || _do:=_NB.DO; _NB.next()
                     ?}
                  !};
                  {? _NB.first()
                  || {!
                     |?
                        {? _NB.OD=_ub_tmp.OD & _NB.DO=_ub_tmp.DO
                        || _ub_tmp.WYM:=0; _ub_tmp.put()
                        |? _NB.OD=_ub_tmp.OD
                        || _ub_tmp.OD:=_NB.DO+1; _ub_tmp.put()
                        ?};
                        _NB.next()
                     !}
                  ?}
               ?}
            ?};
            +_kod_str
         !}
      ?};
      _ub_tmp.next()
   !}
?}
!};
ZC_INFO.ndx_drop(_zc_ndx);
OSOBA.cntx_pop();
OS_N.cntx_pop();
H.cntx_pop();
P.cntx_pop();
ZC_INFO.cntx_pop();
_ub_tmp.index(_ndx3);
_ub_tmp.prefix();

::Sprawdzenie rodzaju raportu: uproszczony/pełny
{? ~_pelny || return(_ub_tmp) ?};

_ub_tmp_wyd:=tab_tmp(3,
                 'REF','INTEGER','Ubezpieczony',
                 'NAZIM','STRING[81]','Nazwisko i imię',
                 'ID','STRING[40]','Identyfikator',
                 'ID_TYP','STRING[14]','Typ identyfikatora',
                 'MC1','INTEGER','Miesiąc1',
                 'MC2','INTEGER','Miesiąc2',
                 'MC3','INTEGER','Miesiąc3',
                 'MC4','INTEGER','Miesiąc4',
                 'MC5','INTEGER','Miesiąc5',
                 'MC6','INTEGER','Miesiąc6',
                 'MC7','INTEGER','Miesiąc7',
                 'MC8','INTEGER','Miesiąc8',
                 'MC9','INTEGER','Miesiąc9',
                 'MC10','INTEGER','Miesiąc10',
                 'MC11','INTEGER','Miesiąc11',
                 'MC12','INTEGER','Miesiąc12'
                );
_ndx_wyd_1:=_ub_tmp_wyd.ndx_tmp(,,'NAZIM',,);
_ub_tmp_wyd.index(_ndx_wyd_1);
_ub_tmp_wyd.prefix();

_ndx4:=_ub_tmp.ndx_tmp(,,'NAZWISKO',,,'ID',,,'MC',,);
_ub_tmp.index(_ndx4);
_ub_tmp.prefix();

{? _ub_tmp.first()
||  _ub_tmp_wyd.NAZIM:=_ub_tmp.NAZWISKO + ' ' + _ub_tmp.IMIE;
    _ub_tmp_wyd.ID:=_ub_tmp.ID;
    _ub_tmp_wyd.ID_TYP:=_ub_tmp.ID_TYP;
    _ub_tmp_wyd.REF:=_ub_tmp.REF;
    _ub_tmp_wyd.MC1:=_ub_tmp_wyd.MC2:=_ub_tmp_wyd.MC3:=0;
    _ub_tmp_wyd.MC4:=_ub_tmp_wyd.MC5:=_ub_tmp_wyd.MC6:=0;
    _ub_tmp_wyd.MC7:=_ub_tmp_wyd.MC8:=_ub_tmp_wyd.MC9:=0;
    _ub_tmp_wyd.MC10:=_ub_tmp_wyd.MC11:=_ub_tmp_wyd.MC12:=0;
    _ub_tmp_wyd.add();
    {! |?
       {? _ub_tmp.ID<>_ub_tmp_wyd.ID
       || _ub_tmp_wyd.put();
          _ub_tmp_wyd.NAZIM:=_ub_tmp.NAZWISKO + ' ' + _ub_tmp.IMIE;
          _ub_tmp_wyd.ID:=_ub_tmp.ID;
          _ub_tmp_wyd.ID_TYP:=_ub_tmp.ID_TYP;
          _ub_tmp_wyd.REF:=_ub_tmp.REF;
          _ub_tmp_wyd.MC1:=_ub_tmp_wyd.MC2:=_ub_tmp_wyd.MC3:=0;
          _ub_tmp_wyd.MC4:=_ub_tmp_wyd.MC5:=_ub_tmp_wyd.MC6:=0;
          _ub_tmp_wyd.MC7:=_ub_tmp_wyd.MC8:=_ub_tmp_wyd.MC9:=0;
          _ub_tmp_wyd.MC10:=_ub_tmp_wyd.MC11:=_ub_tmp_wyd.MC12:=0;
          _ub_tmp_wyd.add()
       ?};
       {? _ub_tmp.MC=1 || _ub_tmp_wyd.MC1:=1
       |? _ub_tmp.MC=2 || _ub_tmp_wyd.MC2:=1
       |? _ub_tmp.MC=3 || _ub_tmp_wyd.MC3:=1
       |? _ub_tmp.MC=4 || _ub_tmp_wyd.MC4:=1
       |? _ub_tmp.MC=5 || _ub_tmp_wyd.MC5:=1
       |? _ub_tmp.MC=6 || _ub_tmp_wyd.MC6:=1
       |? _ub_tmp.MC=7 || _ub_tmp_wyd.MC7:=1
       |? _ub_tmp.MC=8 || _ub_tmp_wyd.MC8:=1
       |? _ub_tmp.MC=9 || _ub_tmp_wyd.MC9:=1
       |? _ub_tmp.MC=10 || _ub_tmp_wyd.MC10:=1
       |? _ub_tmp.MC=11 || _ub_tmp_wyd.MC11:=1
       |? _ub_tmp.MC=12 || _ub_tmp_wyd.MC12:=1
       ?};
       _ub_tmp.next()
    !};
    _ub_tmp_wyd.put()
?};
_ub_tmp_wyd


\params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Parametry wydruku ZUS_IWA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,
   'ROK','INTEGER','Okres raportu',
   'RR','INTEGER','Rodzaj raportu'
);
_tab.ROK:=date()~1;
_tab.RR:=0;
_tab.add();

_ed:=_tab.mk_edit('Wygenerowanie raportu za rok'@,0,'pkd_zus_iwa');
_tab.win_esep(_ed,'Parametry wydruku'@);
_tab.win_efld(_ed,,'ROK',,,5,,,'Rok zestawienia:'@,,'Rok za jaki wykonywane jest zestawienie'@);
_tab.win_efld(_ed,,'RR',,,,,,
   'Rodzaj raportu:'@,,
   'Możliwość wyboru między comiesięcznym zestawieniem (Pełny raport) a całorocznym (Uproszczony raport)'@,
   'radio-buttons',,
   'Pełny'@,"0",
   'Uproszczony'@,"1");
_tab.fld_fml('ROK','AFTER_EDIT',
   "  {? cur_tab().ROK=0
      || FUN.emsg('Błędny rok zestawienia.'@); 0
      || 1
      ?}
   "
);
exec('ok_esc','#window',_tab,_ed);
_tab.efld_opt(_ed,'enable=1, mark=1',,'ROK');
_tab.win_edit(_ed);

params_set('ed',_ed);

{? ~_tab.edit()
|| _tab.del()
|| _tab.put()
?};
_tab


\wym_zat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Uwaga: Wywołanie pozostawione dla zgodności wstecznej. Właściwa formuła znajduje się w pliku rap_zus.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_parStr:=_par:='';
{? _
|| _par:=obj_new(_);
   {! _ii:=1.._
   |! _par[_ii]:=_[_ii];
      _parStr+=',_a[%1]'[$_ii]
   !}
?};
($('exec(\'wym_zat\',\'rap_zus\'%1)'[_parStr]))(_par)


\buf_oddo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Uwaga: Wywołanie pozostawione dla zgodności wstecznej. Właściwa formuła znajduje się w pliku rap_zus.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_parStr:=_par:='';
{? _
|| _par:=obj_new(_);
   {! _ii:=1.._
   |! _par[_ii]:=_[_ii];
      _parStr+=',_a[%1]'[$_ii]
   !}
?};
($('exec(\'buf_oddo\',\'rap_zus\'%1)'[_parStr]))(_par)


\pom_nieo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Uwaga: Wywołanie pozostawione dla zgodności wstecznej. Właściwa formuła znajduje się w pliku rap_zus.fml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_parStr:=_par:='';
{? _
|| _par:=obj_new(_);
   {! _ii:=1.._
   |! _par[_ii]:=_[_ii];
      _parStr+=',_a[%1]'[$_ii]
   !}
?};
($('exec(\'pom_nieo\',\'rap_zus\'%1)'[_parStr]))(_par)

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 0c3e13f1ae38ff6e7b73b1bbc05896b6a03f8989f8865e93ab01e42094200199b541011fba5241d8b07c7c4ce91dfaf35e8f3af45d0af15c937d9ff8f16968ac656feda5df9cd353d66e0ee61aed448b2d34d90d616667a7cd31fe00d9d96990f22640ec2a33e3dafd933acbb1fa8ce6f7e46d04e50369e60653fcaad615a4c2

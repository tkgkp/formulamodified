:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: umo.fml
:: Utworzony: 08.01.2019
:: Autor: WH
::======================================================================================================================
:: Zawartość: Formuły oraz wywoływacze obszaru LUM
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Formuła inicjująca dla obszaru LUM
::----------------------------------------------------------------------------------------------------------------------
:: Podczytanie INFO.KRAJ - kraj waluty opodatkowania
exec('czytaj','#stalesys',,INFO);
exec('czytaj','#stalesys',,FINFO);
exec('czytaj','#stalesys',,XINFO);
exec('czytaj','#stalesys',,KST,'BDO');

BEER.SZ:='S';
:: Walutowość dla sprzedaży
BEER.MW:='F';
:: Ustawia BEER.WW
exec('ustaw_ww','eurusd',BEER.MW);

:: RB - objekt obsługujący rachunki bankowe
exec('RB','object');

:: Podczytanie parametrów oddziału
KSTE.prefix; KSTE.first();

:: Ustawienie prezycji pól w oknach
exec('fld_prec','win_ust');

:: Ustawia zmienne informujące o dostępie do wartości sprzedaży, zakupu, magazynowych
exec('upr_set','ceny');

:: Dogenerowywanie zapisów w tabeli DATY
exec('nowedane_DATY','daty');

:: Formuła podczytująca dane specyficzne dla firmy
__War_f:="exec('FindInSet','#table',_a,_a,_c,REF.FIRMA,$(_a+'.'+_b))";

:: Deklaracje obiektów
exec('FZL','object');

:: Wyłączenie aktualnie nieobsługiwanych akcji
UPMZAD.actions('WER','JA');
UPMZAD.actions('WERN','J');
~~


\lum_haw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Uruchamia obszar roboczy LUM_HAW
::   WE: [_a] - INTEGER - rok który domyślnie ustawić
::       [_b] - STRING - kod oddziału
::  TAG: <MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_ar:=0;
{? var_pres('_a')=type_of(0)
|| _ar:=_a
?};
_oddz:='';
{? var_pres('_b')=type_of('')
|| _oddz:=_b
?};

_env_harm:=exec('env','umowy_harm');
params_set('env_harm',_env_harm);
{? _ar>0
|| _env_harm.AR:=_ar;
   _env_harm.AR_LOCK:=1
?};

{? _oddz<>''
|| __PARSES.setVal('OddzialLogProd',_oddz);
   __PARSES.setEnv('LUM_HAW')
?};
_env_harm.tit_main:='';
AreaTitle.setTabWin(HN,_env_harm.WIN_MAIN);
AreaTitle.setTitle();
_env_harm.select();
~~


\lum_umo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Uruchamia obszar roboczy LUM_UMO
::----------------------------------------------------------------------------------------------------------------------
_far:="exec('before_allum','lum')";

_umc:=exec('authorized','#b__box','LUM_UMO','LUM','LUM_UMO_');
_umm:=exec('authorized','#b__box','LUM_UMO','LUM','LUM_UMM_');
_agr:=exec('chk_role','#b__box',OPERATOR.USER,'LUM_UMO_DAGR')
    | exec('chk_role','#b__box',OPERATOR.USER,'LUM_UMO_PAGR');

_win_grp:=UM.grp_make(,_far,'umowycykl_grp',,,"exec('exit','zws',_a)");

{? _umc
||
:: UMOWY CYKLICZNE - WSZYSTKIE
   _far:="exec('aftref_allum','lum')"; _fb:="exec('before_allum','lum')"; _fa:="exec('after_allum','lum')";
   UM.grp_sel(_win_grp,UM,'WER_KH','Umowy cykliczne'@,_far,,,22,_fb,_fa,,,'maximized');
   UM.tab_splt(_win_grp,,'horizontal','info');
   _fb:="exec('set_efld_opt','umowy','INFO')";
   UM.grp_edit(_win_grp,UM,'INFO',,,_fb,,,'maximized');

:: UMOWY CYKLICZNE - WG POSESJI
   _far:="exec('aftref_pos','lum')"; _fb:="exec('before_pos','lum')"; _fa:="exec('after_pos','lum')";
   UM.grp_sel(_win_grp,POS,'WER3','Umowy wg posesji'@,_far,,,22,_fb,_fa,,1,'maximized');
   UM.tab_splt(_win_grp,'tab0','vertical','umowy');
   _far:="exec('aftref_posum','lum')"; _fb:="exec('after_posum','lum')"; _fa:="exec('before_posum','lum')";
   UM.grp_sel(_win_grp,UM,'WER_KHP',,_far,,,22,_fb,_fa,,,'maximized');
   UM.tab_splt(_win_grp,,'horizontal','info');
   _fb:="exec('set_efld_opt','umowy','INFO')";
   UM.grp_edit(_win_grp,UM,'INFO',,,_fb,,,'maximized');
   {? _umm
   ||
::    UMOWY MIEJSKIE
      _far:="exec('aftref_umm','lum')"; _fb:="exec('before_umm','lum')"; _fa:="exec('after_umm','lum')";
      UM.grp_sel(_win_grp,UMM,'WER','Umowy miejskie'@,_far,,,22,_fb,_fa,,,'maximized');
      UM.tab_splt(_win_grp,,'horizontal','info');
      _fb:="exec('set_efld_opt','umowy_miejskie','INFO')";
      UM.grp_edit(_win_grp,UMM,'INFO',,,_fb,,,'maximized')
   ?};

:: ANEKSY UMÓW
   {? _agr
   || _far:="exec('aftref_angr','lum')"; _fb:="exec('before_angr','lum')"; _fa:="exec('after_angr','lum')";
      UM.grp_sel(_win_grp,UMANGR,'WER','Aneksy grupowe'@,_far,,,22,_fb,_fa,,1,'maximized');
      UM.tab_splt(_win_grp,'tab0','vertical','aneksy','0, 33%');
      _far:="exec('aftref_uman','lum')"; _fb:="exec('before_uman','lum')"; _fa:="exec('after_uman','lum')";
      UM.grp_sel(_win_grp,UMAN,'WER2',,_far,,,22,_fb,_fa,,,'maximized');
      UM.tab_splt(_win_grp,,'horizontal','info');
      _fb:="exec('set_efld_opt','umowy','INFO',UMANGR)";
      UM.grp_edit(_win_grp,UM,'INFO',,,_fb,,,'maximized')
   ?};

:: OKRESY GENERACJI ZGŁOSZEŃ (HGEN)
   _far:="exec('aftref_hgen','lum')"; _fb:="exec('before_hgen','lum')"; _fa:="exec('after_hgen','lum')";
   UM.grp_sel(_win_grp,HGEN,'WER','Generowanie zgłoszeń'@,_far,,,22,_fb,_fa,,,'maximized');
   task_attach('LUM_UMO_HGEN');

   UM.win_sel(_win_grp);
   UM.win_fml('WER_KH',,'SYM',,'ICON_BEFORE',exec('um_sym_ib','umowy'));
   UM.win_fml('WER_KHP',,'SYM',,'ICON_BEFORE',exec('um_sym_ib','umowy'));

   exec('init','lum');

:: Ustalenie treści linku
   _params:=params_get();
   {? type_of(_params)>0 & var_pres('LINK',_params)=type_of('')
   || _link:=_params.LINK
   || _link:=''
   ?};
   _oddz:='';
   _aneksowana:='';

:: Obsługa linku
   {? _link<>''
::    Ustalenie parametrów na podstawie przekazanego rekordu
   || _dobrze:=1;
     {? ref_tab(_link)=UM
     || _oddz:=exec('FindAndGet','#table',UM,_link,,"ODDZ",'');
        _aneksowana:=exec('FindAndGet','#table',UM,_link,,"A",'')
     |? ref_tab(_link)=UP
     || _oddz:=exec('FindAndGet','#table',UP,_link,,"exec('FindAndGet','#table',@.UM,UM,,\"ODDZ\",'')",'');
        _aneksowana:=exec('FindAndGet','#table',UP,_link,,"exec('FindAndGet','#table',@.UM,UM,,\"A\",'')",'')
     ?};
     {? _oddz=''
     || _dobrze:=0;
        {? ref_tab(_link)=UP
        || FUN.info('Nie odnaleziono pozycji umowy cyklicznej: %1'@[exec('record','#to_string',_link)])
        || FUN.info('Nie odnaleziono umowy cyklicznej: %1'@[exec('record','#to_string',_link)])
        ?}
     ?};
     {? _dobrze
     || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
        {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
        || _dobrze:=0
        ?}
     ?};
:: Pokazują się tylko umowy nieaneksowane
     {? _dobrze & _aneksowana<>'N'
     || _dobrze:=0;
        {? ref_tab(_link)=UP
        || _um:=exec('FindAndGet','#table',UP,_link,,"UM",null());
           FUN.info('Umowy cykliczna %1 jest aneksowana.'@[exec('record','#to_string',_um)])
        || FUN.info('Umowy cykliczna %1 jest aneksowana.'@[exec('record','#to_string',_link)])
        ?}
     ?};

     {? _dobrze
::       Ustalenie parametrów sesji
     || __PARSES.setVal('OddzialLogProd',_oddz)
     || return(0)
     ?}

   ?};

   AreaTitle.setTabWin(UM,~~);
   AreaTitle.setTitle();
   {? _link <> ''
   || {? ref_tab(_link)=UM & UM.seek(_link)
      || UM.select(,1,5,,,,1)
      |? ref_tab(_link)=UP & UP.seek(_link) & UM.seek(UP.UM)
      || _f:=$('{? UM.ref() || exec(\'umowa_pozycje\',\'umowy\',\'%1\')?}'[_link]);
         UM.select(,1,5,,,_f,1)
      || return(0)
      ?}
   || UM.select()
   ?}

|| _far:="exec('before_umm','lum')";
   _win_grp:=UMM.grp_make(,_far,'umowymiejs_grp');

:: UMOWY MIEJSKIE
   _far:="exec('aftref_umm','lum')"; _fb:="exec('before_umm','lum')"; _fa:="exec('after_umm','lum')";
   UMM.grp_sel(_win_grp,UMM,'WER','Umowy miejskie'@,_far,,,22,_fb,_fa,,,'maximized');
   UMM.tab_splt(_win_grp,,'horizontal','info');
   _fb:="exec('set_efld_opt','umowy_miejskie','INFO')";
   UMM.grp_edit(_win_grp,UMM,'INFO',,,_fb,,,'maximized');
:: OKRESY GENERACJI ZGŁOSZEŃ (HGEN)
   _far:="exec('aftref_hgen','lum')"; _fb:="exec('before_hgen','lum')"; _fa:="exec('after_hgen','lum')";
   UMM.grp_sel(_win_grp,HGEN,'WER','Generowanie zgłoszeń'@,_far,,,22,_fb,_fa,,,'maximized');
   task_attach('LUM_UMO_HGEN');

   exec('init','lum');
   AreaTitle.setTabWin(UMM,~~);
   AreaTitle.setTitle();
   UMM.win_sel(_win_grp);
   UMM.select()
?};
~~


\aftref_allum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Umowy wszystkie
::----------------------------------------------------------------------------------------------------------------------
grp_edisp(UM,'INFO',,1);
~~


\after_allum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Umowy wszystkie
::----------------------------------------------------------------------------------------------------------------------
{? ~UM.sel_size() || UM.f_clear() ?};
~~


\before_allum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Umowy wszystkie
::----------------------------------------------------------------------------------------------------------------------
TAZ.SD:=BEER.SZ:='S';
UMO.MJS:='UM';
UMO.RKH:='';
UM.win_edit('RED');
UMO.KH:=null;
UMO.KHN:='';
UMO.KHNAZ:='';
UM.win_sel('WER_KH');
{? UM.sel_size() || UM.sel_adel() ?};
UM.f_clear();
UM.index('NRU');
UM.prefix(ST.ODDZ,'N');
~~


\aftref_posum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Umowy wg posesji
::----------------------------------------------------------------------------------------------------------------------
grp_edisp(UM,'INFO',,1);
~~


\after_posum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Umowy wg posesji
::----------------------------------------------------------------------------------------------------------------------
~~


\before_posum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Umowy wg posesji
::----------------------------------------------------------------------------------------------------------------------
~~


\aftref_pos
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Umowy wg posesji
::   WE: [_a] - 1-odśwież 0-nie (domyślnie)
::----------------------------------------------------------------------------------------------------------------------
_refresh:={? var_pres('_a')=type_of(0)
          || _a
          || {? UM.f_active() || ~UM.f_size() || 1 ?} | exec('FindInSet','#table','UP','UMP',POS.ref(),UM.ref())=null()
          ?};

{? _refresh
|| UMO.T_POS:=POS.ref();
   _pos:=POS.ref();
   _kh:=POS.KH;
   {? KH.seek(_kh)
   ||
      UMO.KH:=KH.ref();
      STR.split(UMO.KH().NAZ_P);
      UMO.KHN:=STR.line(57);
      UMO.KHNAZ:=STR.line(100)
   ||
      UMO.KH:=null
   ?};

   UM.index('KH');
   UM.prefix();
   UM.f_clear();
   UM.f_set(,'join UP using(UP.UM,UM.reference)','UM.ODDZ=\':_a\' and UM.KH=:_b and UM.A=\':_c\' and UP.POS=:_d'
      ,ST.ODDZ,_kh,'N',POS.ref());
   UM.f_first();
   {? FILTER.UM<>null() || UM.f_seek(FILTER.UM) ?};
   grp_disp(UM,'WER_KHP',,1)
?};
~~


\after_pos
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Formuła po dla umów wg posesji
::----------------------------------------------------------------------------------------------------------------------
UMO.MJS:='POS';
UMO.RKH:='N';
POM.TYPDOK:='UMO';
POM.TAB:='UM';
POS.win_sel('WER3');
POS.actions('WER3','',,1);
{? ~(POS.ref()=UMO.T_POS)
|| _b_pos:=POS.ref();
   POS.index('MIA');
   POS.prefix();
   POS.seek(_b_pos);
   UMO.MIA:=null;
   UMO.T_POS:=POS.ref();
   exec('aftref_pos','lum',1)
?};
~~


\before_pos
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Formuła po wyjściu z zakładki umów wg posesji
::----------------------------------------------------------------------------------------------------------------------
POS.prefix();
POS.actions('WER3','PH','W',1);
{? FILTER.UM<>null()
|| _pos:=exec('FindInSet','#table','UP','POZ',FILTER.UM,,"@.UP.POS",,,null());
   {? _pos<>null() || POS.seek(_pos) ?}
?};
~~


\aftref_umm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Formuła odśwież umowy z miastem
::----------------------------------------------------------------------------------------------------------------------
grp_edisp(UMM,'INFO',,1);
~~


\after_umm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Formuła po umowy z miastem
::----------------------------------------------------------------------------------------------------------------------
~~


\before_umm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Marcin Makselon
:: OPIS: Umowa miejska glowny select
::  OLD: \umm_sel/umo_mia.fml
::----------------------------------------------------------------------------------------------------------------------
UMM.index('KH');
UMM.prefix(ST.ODDZ,);
UMM.win_sel('WER');
~~


\aftref_hgen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Formuła odśwież hgen
::----------------------------------------------------------------------------------------------------------------------
~~


\after_hgen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Formuła po hgen
::----------------------------------------------------------------------------------------------------------------------
~~


\before_hgen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.22]
:: OPIS: Okienko przed HGEN
::----------------------------------------------------------------------------------------------------------------------
HGEN.index('ORM');
HGEN.prefix(ST.ODDZ);
~~


\aftref_angr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Formuła odśwież umowy z miastem
::----------------------------------------------------------------------------------------------------------------------
{? {? UMANGR.f_active() || UMANGR.f_size() || UMANGR.size() ?}
|| UMAN.index('ANG');
   UMAN.prefix(UMANGR.ref);
   UMAN.win_sel('WER2');
   UMAN.win_edit('RED');
   _il:=UMAN.size();
   {? _il<>UMANGR.IL
   || UMANGR.IL:=UMAN.size();
      UMANGR.put(1)
   ?}
|| UMAN.win_sel('WER2');
   UMAN.win_edit('RED');
   UMAN.index('ANG');
   UMAN.prefix(null(),'xxx')
?};
grp_disp(UMAN,'WER2',,1);
exec('aftref_uman','lum');
~~


\after_angr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Formuła po umowy z miastem
::----------------------------------------------------------------------------------------------------------------------
~~


\before_angr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Marcin Makselon
:: OPIS: Umowa miejska glowny select
::  OLD: \umm_sel/umo_mia.fml
::----------------------------------------------------------------------------------------------------------------------
UMANGR.index('ROK');
UMANGR.prefix(ST.ODDZ,ST.AR);
UMANGR.win_sel('WER');
UMANGR.win_edit('RED');
jaka_tp:='UMANGR.SLU';
UMANGR.actions('WER','F(XJ)');
~~


\aftref_uman
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Formuła odśwież umowy z miastem
::----------------------------------------------------------------------------------------------------------------------
UMAN.BIE().SYM;
grp_edisp(UM,'INFO',,1);
~~


\after_uman
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Formuła po umowy z miastem
::----------------------------------------------------------------------------------------------------------------------
~~


\before_uman
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Marcin Makselon
:: OPIS: Umowa miejska glowny select
::  OLD: \umm_sel/umo_mia.fml
::----------------------------------------------------------------------------------------------------------------------
UMANGR.index('ROK');
UMANGR.prefix(ST.ODDZ,ST.AR);
UMANGR.win_sel('WER');
UMANGR.win_edit('RED');
jaka_tp:='UMANGR.SLU';
UMANGR.actions('WER','F(XJ)');
~~


\lum_umo_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Zmiana parametrów pracy obszaru LUM_UMO
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
_okr_ref:=ST.OKR_REF;
{? AreaTitle.area='LUM_ZGL'
||
   _env:=params_get().env_zgl;
   {? __PARSES.editPar('OkresRok','OddzialLogProd')
   ||
      {? _env.TAB='jednorazowe'
      ||
         ZLP.index('R');
         ZLP.prefix('N');
         exec('zlp_actions','umowy_zlecenia','WER_ZGL')

      |? _env.TAB='wszystkie'
      ||
         ZLP.index('UZUP');
         ZLP.prefix();
         exec('zlp_actions','umowy_zlecenia','UZUP')

      |? _env.TAB='z_umow'
      ||
         ZLP.index('TFAK');
         ZLP.prefix('T','T','N');
         exec('zlp_actions','umowy_zlecenia','UZUP')
      ?};
      AreaTitle.setTitle()
   ?}

|? AreaTitle.area='LUM_FAK'
||
   _param:=$ST.AR+$ST.AM+ST.ODDZ;
   {? __PARSES.editPar('OkresRok','OddzialLogProd')
   ||
      {? _param<>$ST.AR+$ST.AM+ST.ODDZ
      ||
         _env:=params_get().env_fak;
         _czyFaks:=cur_tab(1,1)=FAKS;
         {? _czyFaks || _env.REF_U:=_env.REF_Z:=null() ?};
         {? _czyFaks
         ||
            FAKS.index('FPACZKA');
            _ref:={? _env.RODZ='U' || _env.REF_U || _env.REF_Z ?};
            {? _ref=null() || _ref:=-1 ?};
            FAKS.prefix(_ref)

         ||
            FPACZ.index('ROK');
            FPACZ.prefix(ST.ODDZ,ST.AR,_env.RODZ);
            _ref:={? _env.RODZ='U' || _env.REF_U || _env.REF_Z ?};
            FPACZ.seek(_ref)
         ?};
         AreaTitle.setTitle()
      ?}
   ?}
|? AreaTitle.area='LUO_KPO'
|| ZLP.f_clear();
   ZLP.prefix();
   __PARSES.editPar('OkresRok','OddzialLogProd');
   {? VAR.A_TABKPO='ZKPO'
   || exec('before_luokpo_zgl','luo')
   |? VAR.A_TABKPO='ZKPOK'
   || exec('before_luokpok_zgl','luo')
   ?};
   AreaTitle.setTitle()
|? __PARSES.editPar('OkresRok','OddzialLogProd')
||
   {? _oddz<>ST.ODDZ || exec('init','lum') ?};
   exec('before_allum','lum');
   AreaTitle.setTitle()
?}


\lum_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Uruchamia obszar roboczy LUM_FAK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env_fak:=exec('env','umowy_faktury');
params_set('env_fak',_env_fak);
_env_fak.select();
~~


\lum_umm_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Zmiana parametrów pracy obszaru LUM_UMO
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
_okr_ref:=ST.OKR_REF;
{? __PARSES.editPar('OddzialLogProd')
|| {? _oddz<>ST.ODDZ || exec('init','lum') ?};
   exec('before_umm','lum');
   AreaTitle.setTitle()
?}


\lum_zgl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Uruchamia obszar roboczy LUM_ZGL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env_zgl:=exec('env','umowy_zlecenia');

:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)=type_of('')
|| _link:=_params.LINK
|| _link:=''
?};

params_set('env_zgl',_env_zgl,'LINK',_link);
_env_zgl.select();
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:19 3b28939fb0ca1692f139a80f679b4a5d25ac1815f2aad7b410d16139542421ec0e348ae47057b45de7a00eb358bb977759160a9d506dfff38f726beb8cb1f30b6152fd2589b9d08d7344a80386376a99f5267696e58fd3b8e06684ce0f72c7753a99a4e1a48f5315089abf028fefe88577da9662871fa57f24a5ad7a9be9bc5a

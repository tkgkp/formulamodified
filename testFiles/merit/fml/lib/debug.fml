:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: debug.fml []
:: Utworzony: 2012 []
:: Autor: jerry
::======================================================================================================================
:: Zawartość: Uzupełnienie _debug.fml, @.CLASS.DEBUG wymagany do obsługi profilera
:: uruchomienie exec(,'debug') - zastępuje DEBUG20 @.CLASS.jDebug i wpina w niego @.CLASS.DEBUG
::======================================================================================================================

\decl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [jerry] [20.14]
:: OPIS: deklaracja klasy debug
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('jDebug',@.CLASS)<0
||
   obj_decl('jDebug'
   ,obj_fld('tab',~~)
   ,obj_fld('fml','')
   ,obj_fld('sel','')
   ,obj_fld('edit','')
   ,obj_fld('Debug20',~~)
   ,obj_fld('Config',~~)

   ,obj_meth('__init',"
::    tabela
      _a.tab:=tab_tmp(1
                     ,'LP'      ,'INTEGER'    ,'Numer'
                     ,'FORMULA' ,'SYS_MEMO'   ,'Formuła:'
                     ,'F_LINE'  ,'STRING[255]','Formuła:'
                     ,'RES_TYPE','STRING[30]' ,'Typ:'
                     ,'RESULT'  ,'SYS_MEMO'   ,'Wynik:'
                     ,'PROFILE' ,'STRING[1]'  ,''
                     ,'KIND'    ,'STRING[1]'  ,''
                     ,'PARAMS'  ,'STRING[255]','Parametry:'
                     );
::    ładuje konfiguracje
      _a.ConfigLoad();
::    okno selekcji
      _a.sel:=_a.tab.mk_sel('Historia'@,,,'#jdebugselect',,,,,'U');
      _a.tab.win_sel(_a.sel);
      _a.tab.win_fld(_a.sel,,'KIND'   ,,,            1,,,                       ,1);
      _a.tab.win_fld(_a.sel,,'F_LINE',,,#_a.Config.memowidth,,,'Formuła/Zapytanie SQL'@,);
      _a.tab.win_act(_a.sel,,'Formuła','Wybierz'@@,,,\"sel_exit()\",,1);
      _a.tab.win_act(_a.sel,,'Usuń',,,,,,,1);
      _a.tab.win_act(_a.sel,,'Kolejność');
::    okno edycji
      _a.edit:=_a.tab.mk_edit('Uruchom %1'@['(Ctrl+Enter)'],0,'#jdebugedit');
      _a.tab.win_edit(_a.edit);
      _a.tab.win_efld(_a.edit,,'KIND',,,,,1,,1,,'radio-buttons',,
         'Formuła',\"'F'\",'Zapytanie SQL'@,\"'Q'\",'Json'@,\"'J'\");
      _a.tab.win_efld(_a.edit,,'FORMULA',,,#_a.Config.memowidth,-#_a.Config.formulaheight);
      _a.tab.win_efld(_a.edit,,'PARAMS',,,#_a.Config.memowidth,,,,,'Parametry doklejane do sql(<zapytanie>,<zawartość pola>) lub <Formuła>(<zawartość pola>)'@);
      {? var_pres('AH',@)=type_of(SYSLOG) & var_pres('H',@.AH)>0 || _a.tab.win_efld(_a.edit,AH,'H') ?};
      _a.tab.win_efld(_a.edit,,'RES_TYPE');
      _a.tab.win_efld(_a.edit,,'RESULT',,,#_a.Config.memowidth,-#_a.Config.resultheight);
      1
   ")

   ,obj_meth('ConfigInit',"
      .Config:=obj_new('haschosen'
                      ,'filename'
                      ,'separator'
                      ,'sepconfig'
                      ,'memowidth'
                      ,'formulaheight'
                      ,'resultheight'
                      );
      .Config.haschosen:={? sec_superuser()>0
                         || 'New'
                         || 'Org'
                         ?};
      .Config.filename:='jdebug.hst';
      .Config.separator:=60*'☺☻';
      .Config.sepconfig:=10*'☻☺';
      .Config.memowidth:='110';
      {? exec('interm','#system')
      || .Config.formulaheight:='5';
         .Config.resultheight:='5'
      ||
         .Config.formulaheight:='10';
         .Config.resultheight:='15'
      ?};
      ~~
   ")
   ,obj_meth('prepare',"
      {? var_pres('DEBUG',@.CLASS)>100
      || .Debug20:=obj_new(@.CLASS.DEBUG,20)
      ?};
      _isDebug20:=type_of(.Debug20)>100;

      _load_rule:=\"_tab.memo_get(,'FORMULA'); _tab.memo_get(,'RESULT');\";

::-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -  bottom
      _rule:=\"_tab:=cur_tab(1,1);
               _ref:=null();

               _mfml:=_tab.memo_txt(,,'FORMULA');
               _mres:=_tab.memo_txt(,,'RESULT');

               _tab.cntx_psh();
               {? _tab.select(,1,20) || _ref:=_tab.ref() ?};
               _tab.cntx_pop();
               _tab.memo_set(_mfml,'FORMULA');
               _tab.memo_set(_mres,'RESULT');

               {? _ref<>null() & _tab.seek(_ref) || \"+_load_rule+\" ~~ ?};
               'edit:FORMULA'
             \";
      .tab.win_ebtn(.edit,'text=%1,panel=bottom,align=begin'['Historia'@],$(''+_rule));

      {? exec('interm','#system')=0
      ||
         _rule:=\"_tab:=cur_tab(1,1); {? _tab.prev() || \"+_load_rule+\" ~~ ?}; 'edit:FORMULA' \";
         .tab.win_ebtn(.edit,'text=%1,panel=bottom,align=begin'['Poprzednia'@],$(''+_rule));

         _rule:=\"_tab:=cur_tab(1,1); {? _tab.next() || \"+_load_rule+\" ~~ ?}; 'edit:FORMULA' \";
         .tab.win_ebtn(.edit,'text=%1,panel=bottom,align=begin'['Następna'@],$(''+_rule))
      ?};

      {? _isDebug20
      ||
         _rule:=\"_Debug20:=\"+!.Debug20+\"; _Debug20.prof_sel();'edit:FORMULA'\";
         .tab.win_ebtn(.edit,'text=%1,panel=bottom,align=begin'['Analizy'@],$(''+_rule));

         _rule:=\"cur_tab(1,1).PROFILE:='T';'key:F2'\";
         .tab.win_ebtn(.edit,'text=%1,panel=bottom,align=begin'['Pr&ofiluj'@],$(''+_rule));

         ~~
      ?};
::-     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -
      _rule:=\"cur_tab(1,1).PROFILE:='N';'key:F2'\";
      _btn:=.tab.win_ebtn(.edit,'text=%1,panel=bottom,align=end'['Wykonaj'@],$(''+_rule));
      .tab.btn_opt(_btn,'default=1');

      _rule:=\"{? FUN.ask('Czy na pewno wyczyścić okienko?')
               || _tab:=cur_tab(1,1);
                  _tab.memo_set('','FORMULA');
                  _tab.RES_TYPE:='';
                  _tab.memo_set('','RESULT');
                  _tab.KIND:='F';
                  _tab.PARAMS:='';
                  ~~
               ?};
               'edit:FORMULA'\";
      .tab.win_ebtn(.edit,'text=%1,panel=bottom,align=end'['Wy&czyść'@],$(''+_rule));

::      _rule:=\"'key:Esc'\";
::      .tab.win_ebtn(.edit,'text=Koniec',$(''+_rule));
::-   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -   -  right

      _rule:=\"cur_tab(1,1).KIND:='F';'key:F2'\";
      _btn:=.tab.win_ebtn(.edit,'text=%1,panel=right,align=begin'['Wykonaj &Formułę'@],$(''+_rule));

      _rule:=\"cur_tab(1,1).KIND:='Q';'key:F2'\";
      _btn:=.tab.win_ebtn(.edit,'text=%1,panel=right,align=begin'['Wykonaj S&QL'@],$(''+_rule));

      _rule:=\"cur_tab(1,1).KIND:='Q'; cur_tab(1,1).PROFILE:='T';'key:F2'\";
      _btn:=.tab.win_ebtn(.edit,'text=%1,panel=right,align=begin'['Profil&uj SQL'@],$(''+_rule));

      _rule:=\"cur_tab(1,1).KIND:='J';'key:F2'\";
      _btn:=.tab.win_ebtn(.edit,'text=%1,panel=right,align=begin'['Wykonaj &Json'@],$(''+_rule));
::-     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -     -
      {? exec('interm','#system')=0
      ||
         _rule:=\"exec('mbTables','libfml');'edit:FORMULA'\";
         .tab.win_ebtn(.edit,'text=%1,panel=right,align=end'['Struktury'@],_rule);

         _rule:=\"_fml:=exec('sel_fml','libfml'); {? _fml<>'' || cur_tab(1,1).memo_set(_fml,'FORMULA') ?};'edit:FORMULA'\";
         .tab.win_ebtn(.edit,'text=%1,panel=right,align=end'['Fo&rmuły'@],_rule);

         _rule:=\"exec('ico_selall_ask','#icon');'edit:FORMULA'\";
         .tab.win_ebtn(.edit,'text=%1,panel=right,align=end'['Ikony'@],_rule);

         _rule:=\"
            _t:=form_chk();
            {? var_pres('_t')>100
            || _t.win_sel(_t.mk_sel('Błędy składniowe'@,'P',1,,,,,,'U'));
               _t.select()
            ?};
            'edit:FORMULA'
         \";
         .tab.win_ebtn(.edit,'text=%1,panel=right,align=end'['S&kładnia'@],_rule)
      ?};
      1
   ")

   ,obj_meth('load',"
      _pth:=~( .Config.filename*'@'
             | .Config.filename*'\\\\'
             | .Config.filename*'/'
             );
      _file:=fopen(.Config.filename,'ur',_pth,0,1);
      {? _file.is_open()
      ||
         _consize:=+.Config.sepconfig;
         _cfg:=obj_new(1);
         .tab.erase();
         _lp:=0;
         _kind:='F';
         _params:='';
         _fml:='';
         {!
         |? _line:=_file.fread();
            _line<>'\n'
         |!
            {? _line=.Config.separator
            || {? _fml<>''
               || .tab.blank();
                  .tab.LP:=(_lp+=1);
                  .tab.KIND:=_kind;
                  .tab.PARAMS:=_params;
                  .tab.add();
                  .tab.memo_set(form(_fml),'FORMULA');
                  .tab.memo_put(,'FORMULA');
                  .tab.F_LINE:=.tab.memo_lin('FORMULA');
                  .tab.put()
               ?};
               _kind:='F';
               _params:='';
               _fml:=''

            |? _consize+_line=.Config.sepconfig
            || obj_del(_cfg);
               _cfg:=spli_str(_consize-_line,.Config.sepconfig);
               {? obj_len(_cfg)>1 & _cfg[1]='KIND'   || _kind:=_cfg[2]
               |? obj_len(_cfg)>1 & _cfg[1]='PARAMS' || _params:=_cfg[2]
               ?}

            || _fml+=_line+'\n'
            ?}
         !};
         _file.fclose()
      ?};
      ~~
   ")

   ,obj_meth('save',"
      _pth:=~( .Config.filename*'@'
             | .Config.filename*'\\\\'
             | .Config.filename*'/'
             );
      _file:=fopen(.Config.filename,'uw',_pth,0,1);
      .tab.cntx_psh();
      {? .tab.first()
       & _file.is_open()
      || {!
         |?
            _fml:=.tab.memo_txt(,1,'FORMULA');
            _file.fwrite(.Config.separator);
            _file.fwrite(.Config.sepconfig+'KIND'+.Config.sepconfig+.tab.KIND);
            {? form(.tab.PARAMS)<>'' || _file.fwrite(.Config.sepconfig+'PARAMS'+.Config.sepconfig+.tab.PARAMS) ?};
            _file.fwrite(form(_fml)+'\n'+.Config.separator);
            .tab.next()
         !};
         _file.fclose()
      ?};
      .tab.cntx_pop();
      ~~
   ")

   ,obj_meth('type_decode',"
      {? _a=0 || 'VOID (pusty)'
      |? _a=1 || 'NUMBER (liczba)'
      |? _a=2 || 'STRING (ciąg znaków)'
      |? _a=3 || 'FORMULA (formuła) '
      |? _a=4 || 'DATE (data)'
      |? _a=5 || 'TIME (odstęp czasowy)'
      |? _a=6 || 'BŁĄD !!! '
      |? _a=7 || 'SŁOWNIKOWY'
      |? _a=type_of(SYSLOG) || 'ALIAS TABELI'
      || 'OBIEKT KLASY <'+form(_a)+'>'
      ?}
   ")

   ,obj_meth('result_decode',"
      {? _a=0
      || '~~'
      |? _a=1
      || form(_b)+{? _b>0 & _b$0=_b || '\n tm_stamp - '+tm_form(_b) || '' ?}
      |? _a = 2
      || _b
      |? _a=3
      || '\"'+_b+'\"'
      |? _a=4
      || _b$1+'\n'+_b$7+'\n'+form(#_b,,,'99')
      |? _a=5
      || _b$3+'\n'+form(*_b,,,'99')
      |? _a=6 || '????'
      |? _a=7
      || '$ref: '+$_b+'\nref_name(ref): '+ref_name(_b)+'\n#ref: '+form(#_b,,,'99')
      |? _a=type_of(SYSLOG)
      ||      'Aktualna maska:        '+_b.name()
          +{? _b.name()<>''
           || '\nRekordów w masce:      '+form(_b.cntx_psh(); _b.prefix(); _size:=_b.size(); _b.cntx_pop(); _size,,,' ,')
              +'\nRekordów w dziedzinie: '+form(_b.size(),,,' ,')
           || ''
           ?}
          +'\nAktualny bufor:\n\t'+exec('to_string','#convert',_b,'\n\t','','')

      || 'alias typu '+$_a+'\nPo zdekodowaniu - wariant prosty:\n\t'+exec('to_string','#convert',_b,'\n\t','','')
::                          +'\n\nPo zdekodowaniu - wariant rekurencyjny:\n\t'+exec('to_string','#convert',_b,'\n\t','','','\t')
      ?}
   ")

   ,obj_meth('run',"
      {? type_of(.Debug20)=0 || .prepare() ?};
      {? .Config.haschosen=''
      || .Config.haschosen:={? choice('Który debug?','Pytanie','ASK',,0,1,'Standard','Tuningowany')|| 'New' || 'Org' ?}
      ?};
      {? .Config.haschosen='Org' || .Debug20.run(); return() ?};
      .load();
      {!
      |?
         _lformula:=~~;
         _llp:=0;
         _lkind:=~~;
         _lpars:=~~;

         {? .tab.last()
         || _lformula:=form(.tab.memo_txt(,1,'FORMULA'));
            _llp:=.tab.LP;
            _lkind:=.tab.KIND;
            _lpars:=.tab.PARAMS;
            ~~
         ?};
         _size:=.tab.size();
         {? .tab.edit()
         ||
::          żeby było ładniej, to od razu ociosam formułkę ze zbędnych enteruf i spacyi
            _fml:=form(.tab.memo_txt(,0,'FORMULA'));
            _kind:=.tab.KIND;
            _pars:=.tab.PARAMS;

            {? +_fml>0
            ||
               .tab.memo_set(_fml,'FORMULA');
               _profile:=.tab.PROFILE='T';
::             jeżeli są różnice to zapisujemy do bazy i do pliku
               {? _fml<>_lformula
                | _lkind<>_kind
                | _lpars<>_pars
                | .tab.get()=0
               || .tab.blank();
                  .tab.LP:=_llp+1;
                  .tab.RES_TYPE:='';
                  .tab.KIND:=_kind;
                  .tab.PARAMS:=_pars;
                  .tab.add();
                  .tab.memo_set(_fml,'FORMULA');
                  .tab.memo_put(,'FORMULA');
                  .tab.F_LINE:=.tab.memo_lin('FORMULA');
                  .tab.put();
                  _size:=.tab.size();
                  .save()
               ?};
               {? .tab.KIND='Q' || .sql_exe(_fml,_pars,_profile)
               |? .tab.KIND='F' || .execute(_fml,_profile,_pars)
               |? .tab.KIND='J' || .json_exe(_fml)
               ?}
            || ~~
            ?};
            1
         || {? _size<>.tab.size()
             & ask('Zmieniono historię formuł. Zapisać zmiany do pliku?')
            || .save()
            ?};
            0
         ?}
      !}
   ")
   ,obj_meth('execute',"
      _fml:=_a;
      _profile:=_b;
      _pars:=form(_c);
      _can_continue:=1;
::    przygotowuję formułę (wycinam komentarze)
      _str:=spli_str(_fml,'\n');
      _fml:='';
      {! _ll:=1..obj_len(_str)
      |! {? 1+_str[_ll]<>':'
         || _fml+=_str[_ll]+'\n'
         ?}
      !};
      _fml:=_fml-1;
      {? var_pres('form_chk')=var_pres('exec')
      ||
::         _form_chk:=tab_tmp(1,'ERR_LINE','INTEGER','Linia');
         _form_chk:=form_chk($_fml);
         {? _form_chk.first()
         || _can_continue:=0;
            .tab.RES_TYPE:=.type_decode(6);
            .tab.put();
            _form_txt:='[L:'+form(_form_chk.ERR_LINE,,,'9.')+',C:'+form(_form_chk.ERR_COL,,,'9.')+'] '+_form_chk.ERR_DESC;
            .tab.memo_set(_form_txt,'RESULT');
            .tab.memo_put(,'RESULT');
            ~~
         ?}
      ?};

      {? _can_continue>0
      ||
::       wykonujemy formułe
         on_error(2);

         _fml:=gsub(_fml,'\"','\"\"');
         _fml:=gsub(_fml,%92,2*(%92));
         _ofml:=$('\"'+_fml+'\"('+{? _pars<>'' || _pars || '' ?}+')');
         _result:={? _profile
                  || profile(,_ofml)
                  || (_ofml)()
                  ?};
         _res_type:={? in_error() || 6 || type_of(_result) ?};

         .tab.RES_TYPE:=.type_decode(_res_type);
         .tab.put();
         .tab.memo_set(.result_decode(_res_type,_result),'RESULT');
         .tab.memo_put(,'RESULT');
         on_error(0);
         {? _profile
         || .Debug20.apopup()
         ?}
      ?};
      1
   ")

   ,obj_meth('sql_exe',"
      _sql:=_a;
      _params:=form(_b);
      _profile:=_c;
::    przygotowuję zapytanie (wycinam komentarze)
      _str:=spli_str(_sql,'\n');
      _sql:='';
      {! _ll:=1..obj_len(_str)
      |! {? 1+_str[_ll]<>':'
         || _sql+=_str[_ll]+'\n'
         ?}
      !};
      _sql:=_sql-1;
::    wykonujemy zapytanie
      on_error(2);
      _formula:=$('sql(_a'+{? _params<>'' || ','+_params || '' ?}+')');
      _log:=(form(SYSLOG.tm_stamp(),,,'99'))+'.log';

      {? _profile || sql_log(_log,1) ?};
      _result:=_formula(_sql);
      {? _profile || sql_log() ?};

      _res_type:={? in_error()
                 || 6
                 || 1
                 ?};
      .tab.RES_TYPE:=.type_decode(_res_type);
      .tab.put();
      .tab.memo_set('','RESULT');
      .tab.memo_put(,'RESULT');
      on_error(0);
      {? _res_type=1
      || exec('select','#table',_result);
         {? _profile
         || txt_view(_log,1)
         ?}
      ?};
      1
   ")

   ,obj_meth('json_exe',"
      _fml:=_a;
      _rs:=sql('select /* + EMPTY_TAB_OUT_OF_TRANSACTION */ SPACE(255)'
               ' \"TXT\", 0 \"SORT\" from \"SYSLOG\" where 0=1 order by 2');
      _wyn:=FUN.choice('Json request...',2,'Uruchom','Debuguj');
      {? _wyn=0 || return(0)
      |? _wyn=2 || debug()
      ?};
      json_request(_fml,_rs);
      _show:=sql('select TXT from :_a',_rs);
      exec('select','#table',_show,,,'Response'@);
      1
   ")

   ,obj_meth('ConfigLoad',"
      {? type_of(.Config)>100 || obj_del(.Config) ?};
      .ConfigInit();
      _file:='';
      {? _file<>''
       & ~fexists(_file,0)
      || _ff:=fopen(_file,'uw',0,0,1);
         {? _ff.is_open()
         ||
            _ff.fwrite('#Plik konfiguracyjny dla debug.fml');
            _ff.fclose()
         ?}
      ?};
      {? _file<>''
       & fexists(_file,0)
      || _file:=fopen(_file,'ur',0,0,1,1);
         {? _file.is_open()
         ||
            _chk:=$(''+\"
               _Config:=_a;
               _parts:=spli_str(_b,'=');
               _parts[1]:=-_parts[1];
               {? var_pres(form(8+_parts[1]),_Config)>=0
               || _value:={? obj_len(_parts)=2
                          || form(_parts[2])
                          || 'T'
                          ?};
                  ($('_a.'+form(8+_parts[1])+':=_b'))(_Config,_value)
               ?}
            \");
            {!
            |? _line:=_file.fread(); _line<>'\n'
            |! _chk(.Config,_line)
            !};
            _file.fclose()
         ?}
      ?};
      ~~

   ")
:: koniec deklaracji
   )
?};

_var_name:='DEBUG20';
{? var_pres(_var_name)<100
|| ($(_var_name+":=obj_new(@.CLASS.jDebug)"))()
|| ($(
      "{? type_of("+_var_name+"[1])<100
       || obj_del("+_var_name+");
          "+_var_name+":=obj_new(@.CLASS.jDebug)
       ?}
    ")
   )()
?}


\debug
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: deklaracja klasy debug
::----------------------------------------------------------------------------------------------------------------------
exec('decl','debug');
DEBUG20.run();
''

:Sign Version 2.0 jowisz:1045 2021/09/17 15:17:05 c8ccd1fa64ecd18b8034ca3806d3d27363d929065ae12a46fd1b83c18cd9f5954299bd9c6c27619611ccf6df5c073a30084885c61de9578b69032661cf82cc932678b018512c97f97085b9d63957b4e07c4c7eb737ee668d9adc60d2b582b8ca9f8157fe1a0bace30dfe8c22158ad4d13ba30a145239da1e70b6faa61dfe03d8

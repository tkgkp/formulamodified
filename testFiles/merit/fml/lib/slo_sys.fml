:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: slo_sys.fml [17.00]
:: Utworzony: 2015.02.02
:: Autor: TS
::======================================================================================================================
:: Zawartosc: Funkcje obsługi słowników systemowych:
::            - redagowanie słowników systemowych z poziomu parametryzatora;
::            - formuły przed wyświetlaniem pól, po wyświetlaniu, przed radakcją, po redakcji, walidujące,
::              ustawiające znaczniki wymagalności (gwiazdki), itp.
::======================================================================================================================


\def_slo_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Definiowanie słowników systemowych
::   WY: 1 - edycja powiodła się, 0 - wpp
::  TAG: <SLOSLU><REDAKCJA>
::----------------------------------------------------------------------------------------------------------------------
_data:=exec('od_daty','#stalesys');
{? _data.OK<>1
|| return(0)
?};

_dt:=_data.OD;
_wyn:=0;
exec('init_wer','#stalesys');

:: Czytanie zestawów XINFO i INFO
exec('czytaj','#stalesys',_dt,XINFO,INFO,FINFO);

exec('required_fields','slo_sys');
def_slo:=1;
XINFO.win_edit('RED_SLO');
{? XINFO.edit("exec('valid_slo_sys','slo_sys')")
||
   exec('zapisz','#stalesys',_dt,XINFO);

:: INFO.POWKOR
   exec('zapisz','#stalesys',_dt,INFO,'POWKOR');
:: INFO.NAROD, FINFO.NAROD
   exec('zapisz','#stalesys',_dt,INFO,'NAROD');
   FINFO.NAROD:=INFO.NAROD;
   exec('zapisz','#stalesys',_dt,FINFO,'NAROD');

   exec('zapisz','#stalesys',_dt,KST_PAR,'SKID_MB');

:: Dla zgodności replikacja SLU na SLUAPPL do innych zestawów

:: INFO.SLKH____
   INFO.SLKH____:=exec('sluappl','slo_sys',XINFO.SLKH____);
   exec('zapisz','#stalesys',_dt,INFO,'SLKH____');
:: INFO.SLBANK
   INFO.SLBANK:=exec('sluappl','slo_sys',XINFO.SLBANK);
   exec('zapisz','#stalesys',_dt,INFO,'SLBANK');
:: KINFO.SLBANK
   KINFO.SLBANK:=exec('sluappl','slo_sys',XINFO.SLBANK);
   exec('zapisz','#stalesys',_dt,KINFO,'SLBANK');
:: FINFO.SLBANK
   FINFO.SLBANK:=exec('sluappl','slo_sys',XINFO.SLBANK);
   exec('zapisz','#stalesys',_dt,FINFO,'SLBANK');

:: INFO.SLWAL
   INFO.SLWAL:=exec('sluappl','slo_sys',XINFO.SLWAL);
   exec('zapisz','#stalesys',_dt,INFO,'SLWAL');
:: FINFO.SLWAL
   FINFO.SLWAL:=exec('sluappl','slo_sys',XINFO.SLWAL);
   exec('zapisz','#stalesys',_dt,FINFO,'SLWAL');

:: INFO.SLKH____
   INFO.SLKH____:=exec('sluappl','slo_sys',XINFO.SLKH____);
   exec('zapisz','#stalesys',_dt,INFO,'SLKH____');

:: INFO.SLKHD___
   INFO.SLKHD___:=exec('sluappl','slo_sys',XINFO.SLKHD___);
   exec('zapisz','#stalesys',_dt,INFO,'SLKHD___');

:: INFO.KRAJ
   exec('zapisz','#stalesys',_dt,INFO,'KRAJ');

:: FINFO
   exec('zapisz','#stalesys',_dt,FINFO,'GRUPYRMK');
   exec('zapisz','#stalesys',_dt,FINFO,'GRUPYRMP');
   exec('zapisz','#stalesys',_dt,FINFO,'SL_ODS');
   exec('zapisz','#stalesys',_dt,FINFO,'TODS_UST');
   exec('zapisz','#stalesys',_dt,FINFO,'TODS_DOM');
   exec('zapisz','#stalesys',_dt,FINFO,'B_VAT');
   exec('zapisz','#stalesys',_dt,FINFO,'B_WYDR');
   exec('zapisz','#stalesys',_dt,FINFO,'KURS_TYP');
   exec('zapisz','#stalesys',_dt,FINFO,'OKRROZ');
   _wyn:=1
?};
VAR_DEL.delete('def_slo');
_wyn


\valid_slo_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: sprawdzenie słowników systemowych
::   WE: [_a] - 0-dla okienka edycji (domyślnie) 1-do sprawdzenia parametrów
::   WY: _a=0-akronim niewypełnionego pola; _a=1-1-pola wypełnione poprawnie, 0-wpp
::  TAG: <SLOSLU><CHK>
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};
{? ~_a
|| _wyn:=__CHK.record(XINFO,,'SLWAL');
   {? _wyn=''
   || _wyn:=__CHK.record(INFO,,'NAROD')
   ?};
   {? _wyn=''
   || {? exec('tte_lic','tte')='N'
      || _wyn:=__CHK.record(XINFO,,'SLP');
         {? _wyn='' || _wyn:=__CHK.record(INFO,,'KRAJ') ?}
      || _wyn:=__CHK.record(XINFO,,'SLP','SLWYDZIA');
         {? _wyn='' || _wyn:=__CHK.record(INFO,,'KRAJ') ?}
      ?}
   ?};
   {? _wyn='' & XINFO.SLTYPPL & exec('chk_pl_us','slo_sys')=0
   || FUN.info('Wskazany słownik płatności US jest nieprawidłowy.\nNie zawiera pozycji o treści CIT, VAT lub PIT.'@);
      _wyn:='SLTYPPL'
   ?}
|| {? exec('tte_lic','tte')='N'
   || _wyn:=XINFO.SLP<>null() & INFO.KRAJ<>null()
   || _wyn:=XINFO.SLP<>null() & XINFO.SLWYDZIA<>null() & INFO.KRAJ<>null()
   ?}
?};

_wyn


\required_fields
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Oznacza pola wymagane w oknie redagowania słowników systemowych
::  TAG: <SLOSLU><REDAKCJA>
::----------------------------------------------------------------------------------------------------------------------
_red:='RED_SLO';
{? exec('tte_lic','tte')='N'
|| XINFO.efld_opt(_red,'mark=1',,'SLP')
|| XINFO.efld_opt(_red,'mark=1',,'SLP');
   XINFO.efld_opt(_red,'mark=1',,'SLWYDZIA')
?};
XINFO.efld_opt(_red,'mark=1',,'SLWAL');
XINFO.efld_opt(_red,'mark=1',INFO,'NAROD');
~~


\be_sljezyk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Przed redakcja pola XINFO.SLJEZYK
::  OLD: \be_sllng/skid_lng.fml
::  TAG: <SLOSLU><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
1


\be_slwydzia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Przed redakcja pola XINFO.SLWYDZIA
::  TAG: <SLOSLU><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
exec('tte_lic','tte')='T' | exec('tre_lic','tre')='T' | exec('wyp_lic','wyp')='T'


\sluappl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zwraca rekord SLUAPPL dla SLU
::   WE: _a - SLU.ref()
::   WY: SLUAPPL.ref()
::  TAG: <SLOSLU>
::----------------------------------------------------------------------------------------------------------------------
_sluappl:=null();
SLUAPPL.cntx_psh();
SLUAPPL.index('REF');
SLUAPPL.prefix(_a);
{? SLUAPPL.first() || _sluappl:=SLUAPPL.ref() ?};
SLUAPPL.cntx_pop();
_sluappl


\pr_narod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RL] [8.70]
:: OPIS: Formuła przed redakcją pola FINFO.NAROD, INFO.NAROD
::  OLD: \pr_narod/dok_zrd4.fml
::  TAG: <SLOSLU><MBUILDER><BE>
::----------------------------------------------------------------------------------------------------------------------
_SPR:=sql('select count(*) from @DOK join REJ join ROK_F using(REJ.ROK,ROK_F.REFERENCE) where ROK_F.FIRMA=:_a',REF.FIRMA);
{? _SPR[1]=0 | fld()=null() || exec('wz_wal','wzorce',0) ?}


\beb_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Przed redakcja pola FINFO.B_VAT
::  OLD: \beb_vat/skid_pw.fml
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.SLBANK<>null
|| FINFO.SLBANK().SLU();
   1
|| 0
?}


\be_tkurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2008]
:: OPIS: Przed redakcja pola FINFO.KURS_TYP
::  OLD: \be_tkurs/param.fml
::----------------------------------------------------------------------------------------------------------------------
{? FINFO.B_WYDR=null || FINFO.KURS_TYP:=''; win_disp(); 0 || 1 ?}


\ae_tkurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2008]
:: OPIS: Po redakcji pola FINFO.KURS_TYP
::  OLD: \ae_tkurs/param.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld=''
|| FUN.info('Niewypełnione pole Typ kursu.'@); 0
|? 'KSR'*fld()=0
|| FUN.info('Dozwolone wartości typu kursu walut:\nK-kupna, S-sprzedaży lub R-średni.'@); 0
|| win_disp();
   1
?}


\con_34
::----------------------------------------------------------------------------------------------------------------------
::  UTW: darokr [2010]
:: OPIS: Formula przed redagowaniem pola KST_PAR.SKID_MB
::  OLD: \con_34/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBN.actions('WER','T');1


\con_30
::----------------------------------------------------------------------------------------------------------------------
::  UTW: darokr [2010]
:: OPIS: Formula po redagowaniu pola KST_PAR.SKID_MB
::  OLD: \con_30/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
UD_POM.SKID_MB:=KST_PAR.SKID_MB; SKID_MBN.actions('WER','');1


\def_slo_fks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Definiowanie słowników obszaru FKS
::   WY: 1 - edycja powiodła się, 0 - wpp
::----------------------------------------------------------------------------------------------------------------------
_data:=exec('od_daty','#stalesys');
{? _data.OK<>1
|| return(0)
?};

_dt:=_data.OD;
_wyn:=0;
XINFO.win_edit('RED_FKS');
:: Czytanie zestawów XINFO i INFO
exec('czytaj','#stalesys',_dt,XINFO,FINFO);
{? XINFO.edit()
||
   exec('zapisz','#stalesys',_dt,XINFO);

:: INFO.SLAVAT
   INFO.SLAVAT:=exec('sluappl','slo_sys',XINFO.SLAVAT);
   exec('zapisz','#stalesys',_dt,INFO,'SLAVAT');
:: FINFO
   exec('zapisz','#stalesys',_dt,FINFO,'GRUPYRMK');
   exec('zapisz','#stalesys',_dt,FINFO,'GRUPYRMP');
   exec('zapisz','#stalesys',_dt,FINFO,'SL_ODS');
   exec('zapisz','#stalesys',_dt,FINFO,'TODS_UST');
   exec('zapisz','#stalesys',_dt,FINFO,'TODS_DOM');
   exec('zapisz','#stalesys',_dt,FINFO,'B_VAT');
   exec('zapisz','#stalesys',_dt,FINFO,'B_WYDR');
   exec('zapisz','#stalesys',_dt,FINFO,'KURS_TYP');
   exec('zapisz','#stalesys',_dt,FINFO,'OKRROZ');
   {? var_pres('B_VIUDO',FINFO)>0
   || exec('zapisz','#stalesys',_dt,FINFO,'B_VIUDO')
   ?};
   _wyn:=1
?};
_wyn


\bv_tkrsopis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed wyświetleniem SLOSLU.TKRSOPIS
::----------------------------------------------------------------------------------------------------------------------
SLOSLU.TKRSOPIS:={? FINFO.KURS_TYP='K' || 'Kupna'
                 |? FINFO.KURS_TYP='S' || 'Sprzedaży'
                 || 'Średni'
                 ?};
~~


\chk_pl_us
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Sprawdza czy wybrano poprawny słownik płatności dla US/UC
::----------------------------------------------------------------------------------------------------------------------
SLO.cntx_psh();
SLO.index('SL_TR'); SLO.prefix(XINFO.SLTYPPL);
_ok:=SLO.find_key('CIT',) & SLO.find_key('VAT',) & SLO.find_key('PIT',);
SLO.cntx_pop();
_ok

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:23 97bc8792522b8826ba265d31b6020bfaa38906db471d55f7d3235d51316f1b471e0d93abaf43628b5193ef23561b96fd9699f766f616881ca7a7c61992248e9ccdda7200e4e1be42a37381f5e86af4b61a77d3a817dc99bee2bdeed687fc68684f7dd84f1fbee26bfb521eb00d1836b6308a3a29fc8f284b2a04bba0fc53988a

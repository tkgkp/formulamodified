:!UTF-8
:: (c) Asseco Business Solutions S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_ucz.fml [12.51]
:: Utworzony: 10.05.2019
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Funkcje do obsługi uwzględnionych w raportów.
::======================================================================================================================


\_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed dołączeniem lub poprawieniem wiersza tabeli PPK_RTP.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
{? PPK_RTP.PPK_XRA
|| PPK_XRA.cntx_psh();
   PPK_RTP.PPK_XEL:=PPK_RTP.PPK_XRA().PPK_XEL;
   PPK_XRA.cntx_pop()
|| PPK_RTP.PPK_XEL:=null()
?};
1


\_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli PPK_RTP.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_rtp') & exec('_chk','ppk_rtp',0)


\_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli PPK_RTP.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_rtp') & exec('_chk','ppk_rtp',1)


\_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed usunięciem wiersza tabeli PPK_RTP.
::   WE:
::   WY: 0/1 - wiersz może/nie może być usunięty
::----------------------------------------------------------------------------------------------------------------------
PPK_RPL.cntx_psh();
_ok:=(PPK_RTP.PPK_RPL().WYS_STAT<>'T');
PPK_RPL.cntx_pop();

_ok & exec('del_ndx','#table',PPK_RAP,'OSOBA',PPK_RTP.ref())


\_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli PPK_RTP.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
__CHK.validate(PPK_RTP,
   $("_a.table(_b,"+$_put+",,'PPK_XRA')")
)


\akt_ir
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Aktualizuje liczbę utworzonych raportów.
::   WE: _a _PPK_RTP - uwzględniany typ raportu
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_ref:=_a;

PPK_RTP.cntx_psh();
PPK_RTP.prefix();
{? PPK_RTP.seek(_ref)
|| PPK_RAP.cntx_psh();
   PPK_RAP.index('OSOBA');
   PPK_RAP.prefix(PPK_RTP.ref());
   PPK_RTP.ILOSC:=PPK_RAP.size();
   PPK_RTP.put();
   PPK_RAP.cntx_pop()
?};
PPK_RTP.cntx_pop();
~~


\dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Obsługa akcji "Dołącz - przed" w okienkach tabeli PPK_RTP.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
PPK_XRA.cntx_psh();
PPK_XRA.prefix();
PPK_XRA.f_set('OPIS',,
   '"PPK_XRA"."PPK_XWE"=:_a and '
   '"PPK_XRA".REFERENCE not in '
      '(select PPK_RTP.PPK_XRA from PPK_RTP where PPK_RTP.PPK_RPL=:_b)',
   PPK_RPL.PPK_XWE,PPK_RPL.ref()
);

_dh:=PPK_XRA.f_size()+1;
{? _dh<5
|| _dh:=5
|? _dh>30
|| _dh:=30
?};

_ws:=PPK_XRA.mk_sel('Raporty'@,'N',,'#xrawyb',,,_dh,,'U',,,,,'normal');
PPK_XRA.win_fld(_ws,,'OPIS',,,,,,,,MS.comment(PPK_XRA,'OPIS'));
PPK_XRA.win_act(_ws,,'Formuła','Wybierz'@@,,,
   "  PPK_RTP.blank();
      PPK_RTP.PPK_RPL:=PPK_RPL.ref();
      PPK_RTP.PPK_XRA:=PPK_XRA.ref();
      PPK_RTP.add()
   ",
   "{? ~PPK_XRA.sel_size() || sel_exit() ?}",1,1,
   "FUN.ask('Czy dołączyć zaznaczone typy raportów do pliku wymiany danych?'@)",
   "sel_exit()",
   'W'
);
PPK_XRA.win_sel(_ws);
PPK_XRA.select();
PPK_XRA.f_clear();
PPK_XRA.cntx_pop();
~~


\rek_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Po edycji wiersza tabeli PPK_RTP.
::   WE:
::   WY: 1 w przypadku gdy rekord jest poprawny, akronim wymaganego pola lub 0 gdy doszło do powielenia klucza.
::----------------------------------------------------------------------------------------------------------------------
exec('_chk','ppk_rtp',-menu_txt()='popraw')

:Sign Version 2.0 jowisz:1045 2020/04/03 17:16:44 4318d96b40a4ddf86f0aad51bd6879a1d8a2e896e1cf98402875bc0f4f4dcaaa6bbe3ddff70f385015927795053c137b6f0c0356d4968fc37a8d27f45708dfec885897d70d85aa488e4e92fb8107c90aabdad7f88af071dacb3abea08208d4a09e1a2414ef137cdf9a5986cbc36768baee08dfccfe06fc88b11555d4217e9aa9

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: mwa_abs.fml
:: Utworzony: 20.11.2018
:: Autor: Markus
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsługi abstore przez MacroWebAPI
::======================================================================================================================


\conv_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Odczytuje conversation-id z konfiguracji lub go generuje
::   WE: _a - mode: B2B lub B2C
::       _b - id account _accId
::       [_c] - id conversation conversation_id
::   WY: _convId
::----------------------------------------------------------------------------------------------------------------------
_re:=obj_new('CONV_ID', 'BLAD');
_re.BLAD:='';
_re.CONV_ID:='';
_mode:=_a;

_accId:=exec('get','#params', #(_mode + '01'));
_convId:=exec('get','#params', #(_mode + '02'));

_accId_get:={? _ > 1 || {? type_of(_b)=type_of('') || _b || '' ?} || '' ?};
_convId_get:={? _ > 2 || {? type_of(_c)=type_of('') || _c || '' ?} || '' ?};

{? _accId=_accId_get & _accId<>''
||
   {? (_convId<>_convId_get & _convId_get<>'') | _convId=''
   ||
::      generowanie guida
::      _convId:=tm_stamp();
      _convId:=exec('gen_guid', 'mwa_abs');
      exec('set','#params', #(_mode + '02'), _convId);
      _re.CONV_ID:=_convId
   ||
      _re.CONV_ID:=_convId
   ?}
||
   _re.BLAD:='Nieprawidłowy identyfikator %1 [%2].'@['"accId"',_accId_get]
?};
_re


\serialize
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Formuła pomocnicza do serializacji objektu do XML
::   WE: _a - obiekt???
::       _b - object data
::----------------------------------------------------------------------------------------------------------------------
_resp:=fopen(null(),'Uw',,1,1);

_tab:=tab_tmp(
   ,'STATUS','STRING[20]',''
   ,'ACCID','STRING[100]',''
   ,'CONVID','STRING[100]',''
   ,'VER','STRING[100]',''
);

_tab.STATUS:=_b.STATUS;
_tab.ACCID:=_b.ACCID;
_tab.CONVID:=_b.CONVID;
_tab.VER:=_b.VER;
_tab.add();

_tab.xml_records(_resp,_a.METHOD+'Response',,'p',_a.OUTNSPC,'norecord=1'
   ,'STATUS:status',
   ,'ACCID:accId',
   ,'CONVID:convId',
   ,'VER:ver',
);

_resp


\Conversation_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Odesłanie ip połączenia dla danej konfiguracji
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_accMode:=-exec('get_option','mwapi',_par,'accMode').value;
{? _accMode='' || _accMode:='B2B' ?};
_accId:=exec('get_option','mwapi',_par,'accId').value;
_accType:=exec('get_option','mwapi',_par,'accType').value;
{? _accType='' || _accType:='MACROLOGIC_B2B' ?};
_ver:=exec('get_option','mwapi',_par,'ver').value;
{? _ver='' || _ver:='DEFAULT' ?};

_wsenv:=exec('wsenv','#mwapi');
_wsenv.erase();

_er:=0;

{? _accMode='b2b' | _accMode='b2c'
||
   _mode:={? _accMode='b2b' || '8001' || '8002' ?};
   _conv_id_obj:=exec('conv_id','mwa_abs', _mode, _accId);
   {? _conv_id_obj.BLAD=''
   ||
      _tab:=obj_new('STATUS','ACCID','CONVID','VER');
      _tab.STATUS:='OK';
      _tab.VER:=_ver;
      _tab.ACCID:=_accId;
      _tab.CONVID:=_conv_id_obj.CONV_ID;
      _result:=exec('serialize','mwa_abs',_par,_tab)
   ||
      _er:=1;
      _wsenv.add_error(_conv_id_obj.BLAD)
   ?}
||
   _er:=1;
   _wsenv.add_error('Nieprawidłowa wartość parametru %1 [%2].'@['"accMode"',_accMode])
?};

{? _er=1
||
   _resp:=_wsenv.to_json();
   _result:=exec('serialize_res','mwapi',_par,_resp)
?};

_result


\gen_guid_rand
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Generowanie guida
::----------------------------------------------------------------------------------------------------------------------
_tab:='0123456789abcdef';
_lp:=int(rand() * 16);
1 + (_lp - _tab)


\gen_guid_rand_4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Generowanie guida
::----------------------------------------------------------------------------------------------------------------------
exec('gen_guid_rand', 'mwa_abs') + '' + exec('gen_guid_rand', 'mwa_abs') + '' + exec('gen_guid_rand', 'mwa_abs')
 + '' + exec('gen_guid_rand', 'mwa_abs')


\gen_guid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Markus [19.02]
:: OPIS: Generowanie guida
::----------------------------------------------------------------------------------------------------------------------
::xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx
exec('gen_guid_rand_4', 'mwa_abs') + '' + exec('gen_guid_rand_4', 'mwa_abs') + '-' + exec('gen_guid_rand_4', 'mwa_abs')
 + '-' + exec('gen_guid_rand_4', 'mwa_abs') + '-' + exec('gen_guid_rand_4', 'mwa_abs')
 + '-' + exec('gen_guid_rand_4', 'mwa_abs') + '' + exec('gen_guid_rand_4', 'mwa_abs')
 + '' + exec('gen_guid_rand_4', 'mwa_abs')


\gen_guid_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: trocek [12.10]
:: OPIS: zamienia podany string na ciag hex
::   WE: _a - string z ktorego zostanie utworzony guid
::----------------------------------------------------------------------------------------------------------------------
_tab:='0123456789abcdef';
_hex:='';
{!  _idx:=1 .. +_a |!
::      debug;
    _znak:=%(1+(_idx-_a ));
    _h1:=_znak % 16;
    _h2:=_znak %* 16;
    _hex+=1+(_h1-_tab);
    _hex+=1+(_h2-_tab)
!};
{! |?
   _hex+='00';
   +_hex<40
!};
_guid:=8+_hex + '-' + (4+(8-_hex)) + '-' + (4+(12-_hex)) + '-' + (4+(16-_hex))+'-'+(4+(24-_hex))+'-'+(8+(32-_hex));
_guid

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:55 a3af0cebe36b2b50736119c1e43512cb9e94400cc73b822dad88c67be930708728e80baa6ba8f1002a31625d69e9c19e1e928d399139ad9e6222d71d329f5d66034640324a6aad734e5d74adb973a3bf1c81e94e9d18c9cfe2cfca190ac49340856cf392602e6ce71c0eec260dc2e970ff021a316f08cc167088459bd515ab57

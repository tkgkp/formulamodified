:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: genius_lsp.fml
:: Utworzony: 11.03.2022
:: Autor: MW
:: Systemy:
::======================================================================================================================
:: Zawartość: Obsługa intencji ChatBot dla LSP
::======================================================================================================================

\sprz_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: obsługuje informacje zwrotne dla kontrahenta.
::   WE:
::   WY: Obiekt 'erp'
::----------------------------------------------------------------------------------------------------------------------
_erp:=exec('erp_obj','genius');
_rodz:=AI.getPar('rodzajFaktury');
{? _rodz='zakupowe'
|| _rodz:='Z'
|| _rodz:='S'
?};
_kh_kod:=AI.getPar('kodKh');
_kh_naz:=AI.getPar('nazwaKh');
_kh_sql:=exec('kh_sql','genius_lsp',_kh_kod,_kh_naz);
{? _kh_sql=''
|| _erp.display_message:='Trzeba podać kontrahenta.'@
|| _kh_tab:=sql(_kh_sql);
   _kh_il:=_kh_tab.size();
   {? _kh_il=0
   || _erp.display_message:='Nie ma takiego aktywnego kontrahenta.'@
   |? _kh_il=1
   || _kh_tab.first();
      obj_del(_erp);
      _erp:=exec('faks_erp','genius_lsp',_kh_tab.REFERENCE,_rodz)
   |? _kh_il<=4
   || obj_del(_erp);
      _erp:=exec('kh_faks_erp','genius_lsp',_kh_tab,_rodz)
   || _erp.display_message:='Liczba znalezionych aktywnych kontrahentów: %1.'@[$_kh_il]+
      ' Trzeba podać dokładniejsze dane kontrahenta.'@
   ?}
?};
_erp


\kh_sql
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: zwraca zapytanie SQL wyszukujące kontrahentów wg parametrów AI
::   WE: _a - kod kontrahenta
::       _b - nazwa kontrahenta
::       _c - NIP kontrahenta
::       _d - treść SQL 0 - więcej danych (domyślnie), 1 - tylko reference
::   WY: treść SQL
::----------------------------------------------------------------------------------------------------------------------
_kh_kod:='';
_kh_naz:='';
_kh_nip:='';
_tylko_ref:=0;
{? var_pres('_a')=type_of('')
|| _kh_kod:=_a
?};
{? var_pres('_b')=type_of('')
|| _kh_naz:=_b
?};
{? var_pres('_c')=type_of('')
|| _kh_nip:=_c
?};
{? var_pres('_d')=type_of(0)
|| _tylko_ref:=_d
?};
_sql:='';
_where:='';
{? _kh_kod<>''
|| _where:='kh.kod = \''+_kh_kod+'\' or kh.snip = \''+exec('niptostr','#string',_kh_kod)+'\''
?};
{? _kh_naz<>''
|| _kh_naz:=-_kh_naz;
   {? _where<>''
   || _where+=' or '
   ?};
   _where+='lower(kh.skr) like \''+_kh_naz+'%\' or lower(kh.naz) like \''+_kh_naz+
           '%\' or lower(kh.naz_p) like \''+_kh_naz+'%\''
?};
{? _kh_nip<>''
|| {? _where<>''
   || _where+=' or '
   ?};
   _where+='kh.snip = \''+exec('niptostr','#string',_kh_nip)+'\''
?};
{? _where<>''
|| _where:='('+_where+') and kh.p=2';
   _sql:='select kh.reference';
   {? ~_tylko_ref
   || _sql+=', kh.kod, kh.skr, kh.naz, kh.naz_p, kh.miasto, kh.nip'
   ?};
   _sql+=' from kh where '+_where;
   {? ~_tylko_ref
   || _sql+=' order by kod'
   ?}
?};
_sql


\kh_opi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: zwraca opis kontrahenta
::   WE: _a - ref kontrahenta (jako napis)
::   WY: opis kontrahenta (kod + nazwa)
::----------------------------------------------------------------------------------------------------------------------
_kh:=_a;
_opi:='';
KH.cntx_psh();
KH.prefix();
{? KH.seek(_kh)
|| _opi:=KH.KOD;
   {? KH.NIP<>''
   || _opi+=', '+KH.NIP
   ?};
   {? KH.NAZ<>''
   || _opi+=', <b>'+KH.NAZ+'</b>'
   ?};
   {? KH.MIASTO<>''
   || _opi+=', '+KH.MIASTO
   ?}
?};
KH.cntx_pop();
_opi


\faks_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: zwraca tabelę dokumentów sprzedaży albo zakupu kontrahenta
::   WE: _a - ref kontrahenta (jako napis)
::       _b - rodzaj S - sprzedaż (domyślnie), Z - zakup
::   WY: tabela
::----------------------------------------------------------------------------------------------------------------------
_kh:=_a;
_rodz:='S';
{? var_pres('_b')=type_of('') & (_b='S' | _b='Z')
|| _rodz:=_b
?};
_sql:='select top 20 faks.dw, faks.sym, typysp.t, typysp.naz, faks.oddz, faks.reference'+
      ' from @faks join typysp using (faks.t, typysp.reference)'+
      ' where faks.kh = \''+_kh+'\' and faks.sz = \''+_rodz+'\' order by dw desc, sym desc';
_faks_tab:=sql(_sql);
_faks_tab


\faks_erp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: zwraca obiekt ERP z listą dokumentów sprzedaży albo zakupu kontrahenta
::   WE: _a - ref kontrahenta (jako napis)
::       _b - rodzaj S - sprzedaż (domyślnie), Z - zakup
::   WY: obiekt ERP
::----------------------------------------------------------------------------------------------------------------------
_kh:=_a;
_rodz:='S';
{? var_pres('_b')=type_of('') & (_b='S' | _b='Z')
|| _rodz:=_b
?};
_pokaz_max:=4;
_kh_opi:=exec('kh_opi','genius_lsp',_kh);
_faks_tab:=exec('faks_tab','genius_lsp',_kh,_rodz);
_faks_il:=_faks_tab.size();
{? _faks_tab.first()
|| {? _faks_il<=_pokaz_max
   || _erp:=exec('erp_obj','genius',1,_faks_il)
   || _erp:=exec('erp_obj','genius',1,_pokaz_max+1)
   ?};
   _erp.data[1].type:='link';
   {? _rodz='Z'
   || _erp.display_message:='Dokumenty zakupu - %1'@[_kh_opi]
   || _erp.display_message:='Dokumenty sprzedaży - %1'@[_kh_opi]
   ?};
   {! _ii:=1..exec('min','#math',_faks_il,_pokaz_max)
   |! _faks_uid:=exec('FindAndGet','#table',FAKS,_faks_tab.REFERENCE,,"FAKS.uidref()");
      _erp.data[1].values[_ii].icon:='bi bi-file-text icon-size-lg';
      _erp.data[1].values[_ii].display_name:=_faks_tab.T+' '+_faks_tab.SYM;
      _erp.data[1].values[_ii].desc:=$_faks_tab.DW+', '+_faks_tab.ODDZ+', '+_faks_tab.NAZ;
      _erp.data[1].values[_ii].link:=exec('link_uri','#system',exec('obj_ntab_set','#array',,'LINK',_faks_uid));
      _faks_tab.next()
   !};
   {? _faks_il>_pokaz_max
   || _ii:=_pokaz_max+1;
      _faks_uid:=exec('FindAndGet','#table',FAKS,_faks_tab.REFERENCE,,"FAKS.uidref()");
      _erp.data[1].values[_ii].icon:='bi bi-bookmark-plus icon-size-lg';
      _erp.data[1].values[_ii].display_name:='Więcej dokumentów'@;
::      _erp.data[1].values[_ii].desc:=$_faks_tab.DW+', '+_faks_tab.ODDZ+', '+_faks_tab.NAZ;
      _erp.data[1].values[_ii].link:=exec('link_uri','#system',exec('obj_ntab_set','#array',,'LINK',_faks_uid))
   ?}
|| _erp:=exec('erp_obj','genius');
   {? _rodz='Z'
   || _erp.display_message:='Nie ma dokumentów zakupu kontrahenta %1'@[_kh_opi]
   || _erp.display_message:='Nie ma dokumentów sprzedaży kontrahenta %1'@[_kh_opi]
   ?}
?};
_erp


\kh_faks_erp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: wybór kontrahenta, zwraca obiekt ERP z listą wyboru kontrahentów i pokazaniem dokumentów sprzedaży albo zakupu
::   WE: _a - tymczasowa tabela kontrahentów do wyboru
::       _b - rodzaj S - sprzedaż (domyślnie), Z - zakup
::   WY: obiekt ERP
::----------------------------------------------------------------------------------------------------------------------
_kh_tab:=_a;
_rodz:='S';
{? var_pres('_b')=type_of('') & (_b='S' | _b='Z')
|| _rodz:=_b
?};
_kh_il:=_kh_tab.size();
{? _kh_tab.first()
|| _erp:=exec('erp_obj','genius',1,_kh_il);
   _erp.display_message:='Proszę wskazać właściwego kontrahenta:'@;
   _erp.data[1].type:='choice';
   {! _ii:=1.._kh_il
   |! _erp.data[1].values[_ii].icon:='bi bi-bookmark-check icon-size-lg';
      _erp.data[1].values[_ii].display_name:=_kh_tab.KOD+', '+_kh_tab.SKR;
      _erp.data[1].values[_ii].desc:={? _kh_tab.NIP<>'' || _kh_tab.NIP + ', ' || '' ?} + _kh_tab.NAZ+', '+
           _kh_tab.MIASTO;
      _erp.data[1].values[_ii].link:='exec(\'faks_erp\',\'genius_lsp\',\''+_kh_tab.REFERENCE+'\',\''+_rodz+'\')';
      _kh_tab.next()
   !}
|| _erp:=exec('erp_obj','genius')
?};
_erp


\zam_sprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: obsługuje informacje zwrotne dla kontrahenta - zamówienia sprzedaży.
::   WE:
::   WY: Obiekt 'erp'
::----------------------------------------------------------------------------------------------------------------------
exec('ustaw_firma_user','genius_zws');
_erp:=exec('erp_obj','genius');
{? ~exec('chk_role','#b__box',OPERATOR.USER,'LSP_ZKN_PSPR') &
   ~exec('chk_role','#b__box',OPERATOR.USER,'LSP_ZKN_DRZK')
|| _erp.interact.status:=0;
   _erp.display_message:='Niestety nie masz uprawnień do przeglądania zamówień sprzedaży.'@;
   return(_erp)
?};
{? AI.INTERACT.status=1
|| _kh_naz:=AI.INTERACT.par[1].value;
   {? _kh_naz<>'' & exec('isDigit','#string',1+_kh_naz)
   || _kh_kod:=_kh_naz;
      _kh_nip:=_kh_naz
   || _kh_kod:='';
      _kh_nip:=''
   ?}
|| _kh_kod:=AI.getPar('kodKh');
   _kh_naz:=AI.getPar('nazwaKh');
   _kh_nip:=AI.getPar('nip')
?};
_kh_sql:=exec('kh_sql','genius_lsp',_kh_kod,_kh_naz,_kh_nip);
{? _kh_sql=''
|| {? AI.INTERACT.status=0
   || _erp.interact.status:=1;
      _erp.display_message:='Proszę podać kontrahenta - kod, nazwę, początek nazwy albo numer NIP.'@;
      _erp.interact.par[1].id:='nazwaKh';
      _erp.interact.par[1].for:=_erp.display_message
   || _erp.interact.status:=0;
      _erp.display_message:=''
   ?}
|| _erp.interact.status:=0;
   _kh_tab:=sql(_kh_sql);
   _kh_il:=_kh_tab.size();
   {? _kh_il=0
   || _msg_kh:='';
      {? AI.INTERACT.status=1
      || {? _kh_naz<>''
         || _msg_kh:='dane "%1"'@[_kh_naz]
         ?}
      || {? _kh_naz<>''
         || _msg_kh:='nazwa "%1"'@[_kh_naz]
         ?};
         {? _kh_kod<>''
         || {? _msg_kh<>''
            || _msg_kh+=', '
            ?};
            _msg_kh+='kod "%1"'@[_kh_kod]
         ?};
         {? _kh_nip<>''
         || {? _msg_kh<>''
            || _msg_kh+=', '
            ?};
            _msg_kh+='NIP "%1"'@[_kh_nip]
         ?}
      ?};
      {? _msg_kh=''
      || _msg_kh:='?'
      ?};
      _erp.display_message:='Nie ma takiego aktywnego kontrahenta (%1).'@[_msg_kh]
   |? _kh_il=1
   || _kh_tab.first();
      obj_del(_erp);
      _erp:=exec('zam_sprz_erp','genius_lsp',_kh_tab.REFERENCE)
   |? _kh_il<=4
   || obj_del(_erp);
      _erp:=exec('kh_zam_sprz_erp','genius_lsp',_kh_tab)
   || _erp.display_message:='Liczba znalezionych aktywnych kontrahentów: %1.'@[$_kh_il]+
      ' Trzeba podać dokładniejsze dane kontrahenta.'@
   ?}
?};
_erp


\zam_sprz_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: zwraca tabelę niezrealizowanych zamówień sprzedaży kontrahenta
::   WE: _a - ref kontrahenta (jako napis)
::   WY: tabela
::----------------------------------------------------------------------------------------------------------------------
_kh:=_a;
_sql:='select top 10 zk_n.dp, zk_n.dt, zk_n.sym, typyzam.t, typyzam.naz, zk_n.oddz, zk_n.net, zk_n.brt,'+
      ' zk_n.reference'+
      ' from @zk_n join typyzam using (zk_n.t, typyzam.reference)'+
      ' where zk_n.kh = \''+_kh+'\' and zk_n.stat_rej = \'T\' and zk_n.stan<>\'ZRE\''+
      ' and zk_n.stan<>\'ARCHIWALNE\' order by zk_n.dp, zk_n.sym';
_zam_tab:=sql(_sql);
_zam_tab


\zam_sprz_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: zwraca tabelęz podsumowaniem niezrealizowanych zamówień sprzedaży kontrahenta
::   WE: _a - ref kontrahenta (jako napis)
::       _b - 0 - wszystkie (domyślnie), 1 - przeterminowane
::   WY: tabela
::----------------------------------------------------------------------------------------------------------------------
_kh:=_a;
_przetermin:=0;
{? var_pres('_b')=type_of(0)
|| _przetermin:=_b
?};
_sql:='select sum(zk_n.net) as net, sum(zk_n.brt) as brt, count(zk_n.reference) as licz'+
      ' from @zk_n'+
      ' where zk_n.kh = \''+_kh+'\' and zk_n.stat_rej = \'T\' and zk_n.stan<>\'ZRE\''+
      ' and zk_n.stan<>\'ARCHIWALNE\'';
{? _przetermin
|| _sql+=' and zk_n.dt < TO_DATE(\''+$date()+'\')'
?};
_zam_sum_tab:=sql(_sql);
_zam_sum_tab


\zam_sprz_erp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: zwraca obiekt ERP z listą niezrealizowanych zamówień kontrahenta
::   WE: _a - ref kontrahenta (jako napis)
::   WY: obiekt ERP
::----------------------------------------------------------------------------------------------------------------------
_kh:=_a;
_pokaz_max:=4;
_kh_opi:=exec('kh_opi','genius_lsp',_kh);
_zam_tab:=exec('zam_sprz_tab','genius_lsp',_kh);
_zam_il:=_zam_tab.size();
_pokaz_il:=exec('min','#math',_zam_il,_pokaz_max);
{? _pokaz_il>0
|| _zam_sum:=exec('zam_sprz_sum','genius_lsp',_kh);
   _zam_sum1:=exec('zam_sprz_sum','genius_lsp',_kh,1);
   _erp:=exec('erp_obj','genius',2,2,_pokaz_il);
   _erp.display_message:='Niezrealizowane zamówienia sprzedaży - %1'@[_kh_opi];
   _erp.data[1].type:='label';
   _erp.data[1].values[1].display_name:='Razem:'@+' <b>'+form(_zam_sum.LICZ)+'</b>';
   _erp.data[1].values[1].desc:='Wartość netto:'@+' <b>'+form(_zam_sum.NET,,2)+'</b><br>'+
               'Wartość brutto:'@+' <b>'+form(_zam_sum.BRT,,2)+'</b>';
   _nap:='W tym przeterminowane:'@;
   {? _zam_sum1.LICZ=0
   || _erp.data[1].values[2].desc:='<b>'+'Brak'@+'</b>'
   |? _zam_sum1.LICZ=_zam_sum.LICZ
   || _erp.data[1].values[2].desc:='<b>'+'Wszystkie'+'</b>'@
   || _nap+=' <b>'+form(_zam_sum1.LICZ)+'</b>';
      _erp.data[1].values[2].desc:='Wartość netto:'@+' <b>'+form(_zam_sum1.NET,,2)+'</b><br>'+
                  'Wartość brutto:'@+' <b>'+form(_zam_sum1.BRT,,2)+'</b>'
   ?};
   _erp.data[1].values[2].display_name:=_nap;
   _erp.data[2].type:='link';
   _zam_tab.first();
   {! _ii:=1.._pokaz_il
   |! _zam_uid:=exec('FindAndGet','#table',ZK_N,_zam_tab.REFERENCE,,"ZK_N.uidref()");
      _erp.data[2].values[_ii].icon:='bi bi-basket icon-size-lg';
      _erp.data[2].values[_ii].display_name:='<b>'+_zam_tab.T+' '+_zam_tab.SYM+'</b>, '+'netto'@+' <b>'+
                   form(_zam_tab.NET,,2)+'</b>, '+'brutto'@+' <b>'+form(_zam_tab.BRT,,2)+'</b>';
      _nap:='<b>'+$_zam_tab.DP+'</b>, '+ 'termin'@+' <b>'+$_zam_tab.DT+'</b>, ';
      {? _zam_tab.ODDZ<>''
      || _nap+='<b>'+_zam_tab.ODDZ+'</b>, '
      ?};
      _nap+=_zam_tab.NAZ;
      _erp.data[2].values[_ii].desc:=_nap;
      _erp.data[2].values[_ii].link:=exec('link_uri','#system',exec('obj_ntab_set','#array',,'LINK',_zam_uid));
      _zam_tab.next()
   !}
|| _erp:=exec('erp_obj','genius');
   _erp.display_message:='Nie ma niezrealizowanych zamówień kontrahenta %1'@[_kh_opi]
?};
_erp


\kh_zam_sprz_erp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: wybór kontrahenta, zwraca obiekt ERP z listą wyboru kontrahentów i pokazaniem niezrealizowanych zamówień
::   WE: _a - tymczasowa tabela kontrahentów do wyboru
::   WY: obiekt ERP
::----------------------------------------------------------------------------------------------------------------------
_kh_tab:=_a;
_kh_il:=_kh_tab.size();
{? _kh_tab.first()
|| _erp:=exec('erp_obj','genius',1,_kh_il);
   _erp.display_message:='Proszę wskazać właściwego kontrahenta:'@;
   _erp.data[1].type:='choice';
   {! _ii:=1.._kh_il
   |! _erp.data[1].values[_ii].icon:='bi bi-bookmark-check icon-size-lg';
      _erp.data[1].values[_ii].display_name:=_kh_tab.KOD+', <b>'+_kh_tab.SKR+'</b>';
      _erp.data[1].values[_ii].desc:={? _kh_tab.NIP<>'' || _kh_tab.NIP + ', ' || '' ?}+
           '<b>'+_kh_tab.NAZ+'</b>, '+_kh_tab.MIASTO;
      _erp.data[1].values[_ii].link:='exec(\'zam_sprz_erp\',\'genius_lsp\',\''+_kh_tab.REFERENCE+'\')';
      _kh_tab.next()
   !}
|| _erp:=exec('erp_obj','genius')
?};
_erp

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:18 646f3dad4b0076d3bd55aab6be7d6483065347003db77a217553697a547f4992ab6cf26157af92b963119c337130fd6e4ff951c6a6136d24e91ef838533ad2d7692625cbafbe3210e70dd3dc7a7d6c811cfa4a499595b3eb8a85294ac0b0c21365455275f0eae5ef65449a7a93d140fc1fb23a44082c6270d98a0456ec8e91ce

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tech_tool.fml
:: Utworzony: 27.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa N-P-U (narzędzia, przyrządy, urządzenia) w kartach technologicznych
::            Plik biblioteczny - wspólna obsługa dla TTE_TEC, TTE_WTE, TTE_PZL
::            Obsługa tebeli TACTTLS - N-P-U procesu technologicznego
::======================================================================================================================


\buffer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zwraca obiekt nazwany - bufor tabeli TACTTLS
::   WY: obj_new()
::----------------------------------------------------------------------------------------------------------------------
exec('TACTTLS','buffer')


\tools_main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Podstawowa formuła wyświetlająca N-P-U
::   WE: _a - TKTL.ref()
::       _b - TOPER.ref()
::       _c - 'N', 'P', 'U', ''
::       _d - inicjowanie środowiska (1), brak inicjowania (0)
::       _e - karta używana na wyższym poziomie wywołania (np. operacje)
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;
VAR.A_OP:=_b;
_npu:=_c;
_init:=_d;
_used:=_e;

TKTL.cntx_psh();
TKTL.prefix();
{? TKTL.seek(_tktl)
|| {? TKTL.TYP().UTIL='N'
   || FUN.emsg('Typ karty wyklucza redagowanie N-P-U.'@)
   |? VAR.A_OP<>null() & VAR.A_OP().PZ='Z'
   || FUN.emsg('Do operacji złożonej nie można przypisać N-P-U.'@)
   |? VAR.A_OP<>null() & VAR.A_OP().WEW='N'
   || FUN.emsg('Do operacji zewnętrznej nie można przypisać N-P-U.'@)
   || {? _init || exec('menu_start','tech_head') ?};
      exec('narzedz','tech_tool',_npu,_used);
      {? _init || exec('menu_stop','tech_head') ?}
   ?}
?};
TKTL.cntx_pop();
''


\env_tool
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zwraca zmienną środowiskową do obsługi listy NPU
::----------------------------------------------------------------------------------------------------------------------
_env:=obj_new('used','buffer','buffer_grp','fields');
_env.used:=0;
_env.buffer:=exec('buffer','tech_tool');
_env.buffer_grp:=exec('buffer','tech_tool');
_env.fields:=obj_new('NROP');
_env


\narzedz
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Narzędzia do karty technologicznej/operacji (w zależności od ustawienia zmiennej VAR.A_OP)
::   WE: _a - 'N', 'P', 'U', ''
::       _b - karta używana na wyższym poziomie wywołania (np. operacje)
::  OLD: \narzedz/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
_npu:=_a;
_used_oper:=_b;

{? exec('get','#params',500750,2)='' |
   exec('get','#params',500751,2)='' |
   exec('get','#params',500752,2)=''
|| FUN.emsg(
      'Należy uzupełnić grupy materiałowe dla narzędzi w parametrach systemu\n(parametry: %1, %2, %3).'@
      ['500750','500751','500752']
   );
   return()
?};

_locked:=exec('tktl_lock','tech_common',,'P');
_used:=~_locked | _used_oper;
_interm:=exec('interm','#system');
_disp_all:={? VAR.A_OP=null() || 1 || 0 ?};

:: Ustawienie zmiennych środowiska w oknie wertowania
_env_tool:=exec('env_tool','tech_tool');
_env_tool.used:=_used;
params_set('env_tool',_env_tool);

M.cntx_psh();
MGR.cntx_psh();
M.f_clear();
M.clear();

VAR.A_NPU:=_npu;
_act:={? VAR.A_OP=null() || 1 || VAR.A_OP().ACT='T' ?};
_wer:='WER_'+TKTL.TORW;
_wert:={? _npu='' || 'WERA_' || 'WER_' ?}+TKTL.TORW;
_mgr:=null();
{? _npu<>'' || POMOC.MGR:=exec('rodz2mgr','tech_tool',_npu) || POMOC.MGR:=null() ?};
TKTL.cntx_psh();

_hidden:=':';
:: Ukrycie akcji wynikające z kontekstu programu
{? TKTL.ARCH='T' | _used | TKTL.STAT_N='N' & TKTL.TORW<>'Z'
|| _hidden:='DPU'+_hidden+'D'; _default:='M'
|? TKTL.STAT_P='T' & TKTL.STAN<>'T'
|| _hidden:='DPU'+_hidden+'D'; _default:=''
|? TKTL.STAT_P='T' & TKTL.STAN='T'
|| _hidden:=''+_hidden+''; _default:='D:D'
|| _hidden:=''+_hidden+''; _default:='D:D'
?};
{? ~_act || _hidden:='DPUF(T)'+_hidden+'DF(T)' ?};
{? ~(TKTL.STAN='T' & TKTL.ARCH='N' & ~_used) || _hidden:='Ó'+_hidden ?};
{? TKTL.STAN='T' & (TKTL.TORW='W' | TKTL.TORW='Z' | ~exec('can_modify','tech_common')) || _hidden:='DPUÓ:D' ?};
{? (TKTL.STAT_P='T' | TKTL.TORW='W' | _used | TKTL.ARCH='T' | TKTL.STAT_N='N' & TKTL.TORW<>'Z')
|| _hidden:='F(T)'+_hidden+'F(T)'
?};

{? ((TKTL.TORW='T' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRDP')) |
    (TKTL.TORW='W' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRDP')) |
    (TKTL.TORW='Z' & exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC'))
   )
      &
   ~_used & TKTL.ARCH='N' & TKTL.STAT_P='N' & (TKTL.STAT_N='T' | TKTL.TORW='Z')
||
   _id:='#tacttls_grp'+{? _npu='N' || 'n' || '_' ?}+{? _npu='P' || 'p' || '_' ?}+{? _npu='U' || 'u' || '_' ?};
   _grp:=TACTTLS.grp_make('',,_id,,10,,,'html_maximized');

   _after_refresh:="";
   {? _interm=0
   || _after_refresh:=$(
         {? _npu='N' | _npu='' || 'grp_disp(M,__wern); ' || '' ?}+
         {? _npu='P' | _npu='' || 'grp_disp(M,__werp); ' || '' ?}+
         {? _npu='U' | _npu='' || 'grp_disp(M,__weru); ' || '' ?}+
         '~~')
   ?};
   TACTTLS.grp_sel(_grp,,_wer,,_after_refresh,,,,,,,,'maximized');

   {? _interm=0
   ||
      TACTTLS.grp_splt(_grp,,'vertical','slowniki',',67%');

      {? _npu='N' | _npu=''
      || __wern:=M.mk_sel('Narzędzia'@,'P',0,'npu_n_wer');
         M.win_fld(__wern,,'KTM',,,10,,,'Symbol'@);
         M.win_fld(__wern,,'N',,,20,,,'Nazwa'@);
         MGR.index('MGR');
         MGR.prefix(exec('get','#params',500750,2),);
         MGR.first();
         _mgr:=MGR.ref();
         TACTTLS.grp_sel(_grp,M,__wern,'Narzędzia'@,,,,,"
            MGR.index('MGR');
            MGR.prefix(exec('get','#params',500750,2),);
              {? MGR.first()
              || M.index('MGR_A');
                 M.prefix(MGR.ref(),'T')
              || M.index('MGR_A');
                 M.prefix(null(),'T')
              ?}
         ",,,,'maximized')
      ?};

      {? _npu='P' | _npu=''
      || __werp:=M.mk_sel('Przyrządy'@,'P',0,'npu_p_wer');
         M.win_fld(__werp,,'KTM',,,10,,,'Symbol'@);
         M.win_fld(__werp,,'N',,,20,,,'Nazwa'@);
         MGR.index('MGR');
         MGR.prefix(exec('get','#params',500751,2),);
         MGR.first();
         _mgr:=MGR.ref();
         TACTTLS.grp_sel(_grp,M,__werp,'Przyrządy'@,,,,,"
            MGR.index('MGR');
            MGR.prefix(exec('get','#params',500751,2),);
              {? MGR.first()
              || M.index('MGR_A');
                 M.prefix(MGR.ref(),'T')
              || M.index('MGR_A');
                 M.prefix(null(),'T')
              ?}
         ",,,,'maximized')
      ?};

      {? _npu='U' | _npu=''
      || __weru:=M.mk_sel('Urządzenia'@,'P',0,'npu_u_wer');
         M.win_fld(__weru,,'KTM',,,10,,,'Symbol'@);
         M.win_fld(__weru,,'N',,,20,,,'Nazwa'@);
         MGR.index('MGR');
         MGR.prefix(exec('get','#params',500752,2),);
         MGR.first();
         _mgr:=MGR.ref();
         TACTTLS.grp_sel(_grp,M,__weru,'Urządzenia'@,,,,,"
            MGR.index('MGR');
            MGR.prefix(exec('get','#params',500752,2),);
              {? MGR.first()
              || M.index('MGR_A');
                 M.prefix(MGR.ref(),'T')
              || M.index('MGR_A');
                 M.prefix(null(),'T')
              ?}
         ",,,,'maximized')
      ?}
   ?};

   TACTTLS.win_sel(_grp)
|| MGR.index('MGR');
   {? _npu='N'
   || MGR.prefix(exec('get','#params',500750,2),)
   |? _npu='P'
   || MGR.prefix(exec('get','#params',500751,2),)
   |? _npu='U'
   || MGR.prefix(exec('get','#params',500752,2),)
   ?};
   {? _npu<>'' & MGR.first()
   || _mgr:=MGR.ref()
   ?};
   TACTTLS.win_sel(_wert)
?};

TACTTLS.win_edit('RED'+{? _npu='' || 'N' || '_' ?}+TKTL.TYP().PAR);
_hdr:=
   {? _npu='N'
   || 'Narzędzia do procesu technologicznego: %1 wersja: %2'@[TKTL.NRK,TKTL.WER]
   |? _npu='P'
   || 'Przyrządy do procesu technologicznego: %1 wersja: %2'@[TKTL.NRK,TKTL.WER]
   |? _npu='U'
   || 'Urządzenia do procesu technologicznego: %1 wersja: %2'@[TKTL.NRK,TKTL.WER]
   || 'N-P-U do procesu technologicznego: %1 wersja: %2'@[TKTL.NRK,TKTL.WER]
   ?};

TACTTLS.hdr_sel();
{? _used
|| TACTTLS.hdr_sel(_hdr+' — '+'PODGLĄD'@)
|? TKTL.ARCH='T'
|| TACTTLS.hdr_sel(_hdr+' — '+'ARCHIWALNE'@)
|? TKTL.STAT_P='T'
|| TACTTLS.hdr_sel(_hdr+' — '+'ZATWIERDZONE'@)
|| TACTTLS.hdr_sel(_hdr)
?};

:: Jeżeli jest aktywny filtr programowy, to wyświetlane narzędzia wg filtra
{? TACTTLS.f_active()=1 | TACTTLS.f_active()=3
|| TACTTLS.f_first()
|| TACTTLS.index({? _npu='' || 'KNROP' || {? _disp_all>0 || 'KNROPR2' || 'KNROPR' ?} ?});
   {? _npu=''
   || {? _disp_all>0
      || TACTTLS.prefix(VAR.A_KTL)
      || TACTTLS.prefix(VAR.A_KTL,VAR.A_OP)
      ?}
   || {? _disp_all>0
      || TACTTLS.prefix(VAR.A_KTL,_mgr)
      || TACTTLS.prefix(VAR.A_KTL,VAR.A_OP,_mgr)
      ?}
   ?};
   TACTTLS.first()
?};

:: Dołączanie rekordów za pomocą d'n'd
_dnd:=1;
{? ~(exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRDP') & TKTL.TORW='T' |
     exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRDP') & TKTL.TORW='W' |
     exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC') & TKTL.TORW='Z')
|| _dnd:=0
?};

{? ~_used & TKTL.ARCH='N' & TKTL.STAT_P='N' & (TKTL.STAT_N='T' | TKTL.TORW='Z') & _dnd>0
||
   TACTTLS.dnd_sel(_wer,,'records.M',"
      params_set(params_get());
      _records:=dnd_info('dropped_records');
      {? _records.size()>1
      || FUN.info('Dołączać można tylko pojedynczą pozycję.'@)
      || TACTTLS.blank();
         M.clear();
         {? _records.first() & M.seek(_records.REF,)
         ||
            TACTTLS.M:=M.ref();
            exec('ust_npu','tech_tool');
            exec('ust_hdr_tacttls','tech_tool');
            {? TACTTLS.edit(\"exec('TACTWERr','tech_tool')\")
            || TACTTLS.add()
            ?}
         ?}
      ?}
   ")
||
   TACTTLS.dnd_sel(_wer,,'records.M',"")
?};

TACTTLS.actions(_wert,_hidden,_default);
TACTTLS.actions(_wer,_hidden,_default);
exec('slo_m_ok','material','T',1,,'EWN(SZ)');
FILTER.T_WPM:='';
ZAKR.R_PROD:='';
ZAKR.R_KOMP:='';
POMOC.RODZ:='T';
ZAKR.MATU:='A';
TACTTLS.select();
TKTL.cntx_pop();
TACTTLS.hdr_edit();
TACTTLS.actions(_wert);

M.cntx_pop();
MGR.cntx_pop();
{? _locked || exec('tktl_unlock','tech_common',,'P') ?};
''


\npu_rodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [12.10]
:: OPIS: Ustawia nagłówek dla okna słownika M wołanego przy redagowaniu TACTTLS
::  OLD: \npu_rodz/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRP_MOD='N'
|| MGR.cntx_psh();
   {? M.MGR().KOD=exec('get','#params',500750,2)
   || VAR1.NPU:='N'
   |? M.MGR().KOD=exec('get','#params',500751,2)
   || VAR1.NPU:='P'
   |? M.MGR().KOD=exec('get','#params',500752,2)
   || VAR1.NPU:='U'
   || VAR1.NPU:='I'
   ?};
   MGR.cntx_pop()
|| 1
?}


\npu_rodz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Po redagowaniu zmiennej VAR1.NPU - ustawia zakres słownika M wołanego przy redagowaniu TACTTLS
::  OLD: \npu_rodz/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
POMOC.MGR:=exec('rodz2mgr','tech_tool',VAR1.NPU);
M.cntx_psh();
{? TACTTLS.M<>null() & TACTTLS.M().MGR<>POMOC.MGR
|| TACTTLS.M:=null()
?};
M.cntx_pop()


\ust_npu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [12.10]
:: OPIS: Ustawia zmienną VAR.A_NPU dla M w kontekście
::  OLD: \ust_npu/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
MGR.cntx_psh();
{? M.MGR().KOD=exec('get','#params',500750,2)
|| VAR.A_NPU:='N'
|? M.MGR().KOD=exec('get','#params',500751,2)
|| VAR.A_NPU:='P'
|? M.MGR().KOD=exec('get','#params',500752,2)
|| VAR.A_NPU:='U'
|| VAR.A_NPU:='I'
?};
MGR.cntx_pop()


\rodz2mgr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [12.10]
:: OPIS: Zwraca MGR.ref() w zależnosci od parametru ('N','P','U','I')
::   WE: _a - ('N','P','U','I')
::  OLD: \rodz2mgr/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
_kod:='';
{? _a='N'
|| _kod:=(exec('get','#params',500750,2))
|? _a='P'
|| _kod:=(exec('get','#params',500751,2))
|? _a='U'
|| _kod:=(exec('get','#params',500752,2))
|| return(null())
?};
MGR.index('MGR');
MGR.prefix(_kod,);
{? MGR.first()
|| MGR.ref()
|| null()
?}


\tm_uhead
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: przed redakcją pola TACTTLS.EXIST
::   WY: 0 / 1
::  OLD: \tm_uhead/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
TKTL.STAT_P='N' | VAR.KOR


\be_file
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: przed redakcją pola TACTTLS.FILE
::  OLD: \be_file/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
TKTL.STAT_P='N' | VAR.KOR


\file
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: po redakcji TACTTLS.FILE
::  OLD: \file/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()=''
|| 1
|| {? tpar.chk(fld())
   || {? TKTL.STAT_P='N' | VAR.KOR || TACTTLS.ILE:=tpar.RESULT ?};
      {? TACTTLS.ILE>=0
      || 1
      || exec('err_form_ltzero','tech_common');
         0
      ?}
   || 0
  ?}
?}


\be_ileac
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Przed redakcją pola TACTTLS.ILE
::  OLD: \be_ileac/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
{? (TACTTLS.FILE='') & (TKTL.STAT_P='N' | VAR.KOR)
|| 1
|| {? (TACTTLS.FILE<>'') & (TKTL.STAT_P='N' | VAR.KOR)
   || fld(tpar.calc(TACTTLS.FILE))
   ?};
   0
?}


\tm_zuz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: przed redakcją pól TACTTLS.FZXJM i TACTTLS.FZH
::   WY: 0 / 1
::  OLD: \tm_zuz/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
TKTL.STAT_P='N' | VAR.KOR


\ae_fzxjm
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: po redakcji formuły na zużycie na xjm
::  OLD: \ae_fzxjm/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()=''
|| 1
|| {? tpar.chk(fld())
   || {? TKTL.STAT_P='N' | VAR.KOR || TACTTLS.ZXJM:=tpar.RESULT ?};
      {? TACTTLS.ZXJM>=0
      || 1
      || exec('err_form_ltzero','tech_common');
         0
      ?}
   || 0
   ?}
?}


\be_zxjm
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Przed redakcją pola TACTTLS.ZXJM
::  OLD: \be_zxjm/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
{? (TACTTLS.FZXJM='') & (TKTL.STAT_P='N' | VAR.KOR)
|| 1
|| {? (TACTTLS.FZXJM<>'') & (TKTL.STAT_P='N' | VAR.KOR)
   || fld(tpar.calc(TACTTLS.FZXJM))
   ?};
   0
?}


\ae_fzh
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: po redakcji formuły na zużycie na godzinę
::  OLD: \ae_fzh/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()=''
|| 1
|| {? tpar.chk(fld())
   || {? TKTL.STAT_P='N' | VAR.KOR || TACTTLS.ZH:=tpar.RESULT ?};
      {? TACTTLS.ZH>=0
      || 1
      || exec('err_form_ltzero','tech_common');
         0
      ?}
   || 0
   ?}
?}


\be_zh
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Przed redakcją pola TACTTLS.ZH
::  OLD: \be_zh/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
{? (TACTTLS.FZH='') & (TKTL.STAT_P='N' | VAR.KOR)
|| 1
|| {? (TACTTLS.FZH<>'') & (TKTL.STAT_P='N' | VAR.KOR)
   || fld(tpar.calc(TACTTLS.FZH))
   ?};
   0
?}


\sprnarz
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Przed redakcją pola TACTTLS.PFAZ
::   WY: 0 / 1
::  OLD: \sprnarz/tex_tmat.fml
::----------------------------------------------------------------------------------------------------------------------
{? TACTTLS.NROP<>null()
|| 0
|| PFAZ.f_set('KOD', '',
              'PFAZ.WYD='':_a'' or '':_a''=''''',
              $VAR.A_KTL().JORG);
   1
?}


\ae_pfaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: po redakcji pola TACTTLS.PFAZ
::   WY: 1
::  OLD: \ae_pfaz/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.A_PFAZ:=fld();
VAR.A_WYD:={? VAR.A_PFAZ<>null() || VAR.A_PFAZ().WYD || TKTL.JORG ?};
PFAZ.f_clear();
1


\pfaz_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Przed wyświetleniem pola TACTTLS.PFAZ
::----------------------------------------------------------------------------------------------------------------------
VAR.A_WYD:={? TACTTLS.PFAZ<>null() || TACTTLS.PFAZ().WYD || TKTL.JORG ?};
1


\ust_hdr_tacttls
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [10.11]
:: OPIS: Ustawia nagłówek okna redagowania NPU
::   WY: 1
::  OLD: \ust_hdr_tacttls/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
TACTTLS.hdr_edit();
TACTTLS.hdr_edit(
   {? VAR.A_NPU='N' || 'Dane narzędzia'@
   |? VAR.A_NPU='P' || 'Dane przyrządu'@
   |? VAR.A_NPU='U' || 'Dane urządzenia'@
                    || 'Dane N-P-U'@
   ?}+
   {? VAR.KOR || ' — '+'MODYFIKACJA ZATWIERDZONEJ KARTY'@ || '' ?}
);
1


\zm_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Dołączenie nowego NPU na technologii
::  OLD: \zm_dolacz/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? VAR.GRP_MOD='' ||  VAR.GRP_MOD:='N' ?};
{? TKTL.STAT_P='N' || VAR.KOR:=0 || VAR.KOR:=1 ?};
VAR.A_WYD:=TKTL.JORG;
exec('ust_hdr_tacttls','tech_tool');
TTXCAUSE.win_sel('WER');
TTXCAUSE.actions('WER','T','d:d');

{? VAR.A_OP<>null()
|| _unrop:=exec('FindAndGet','#table',TOPER,VAR.A_OP,,"UNROP",0);
   _nrop:=exec('get_oper_nr','tech_oper',_unrop,'\\','n');
   VAR.STRING:=VAR1.TOPER:=_nrop
|| VAR.STRING:=VAR1.TOPER:=''
?};

POMOC.RODZ:='T';
:: Pytam się co dołączam żeby zaprefiksować odpowiednio dziedzinę M
_msg:='Typ dołączanego rekordu'@;
_choice:=FUN.choice(_msg,,'Narzędzie'@,'Przyrząd'@,'Urządzenie'@);
{? _choice=1
||
:: Narzędzie
   MGR.cntx_psh();
   MGR.index('MGR');
   MGR.prefix(exec('get','#params',500750,2),);
   {? MGR.first()
   || POMOC.MGR:=MGR.ref()
   || POMOC.MGR:=null()
   ?};
   MGR.cntx_pop()
|? _choice=2
||
:: Przyrząd
   MGR.cntx_psh();
   MGR.index('MGR');
   MGR.prefix(exec('get','#params',500751,2),);
   {? MGR.first()
   || POMOC.MGR:=MGR.ref()
   || POMOC.MGR:=null()
   ?};
   MGR.cntx_pop()
|? _choice=3
||
:: Urządzenie
   MGR.cntx_psh();
   MGR.index('MGR');
   MGR.prefix(exec('get','#params',500752,2),);
   {? MGR.first()
   || POMOC.MGR:=MGR.ref()
   || POMOC.MGR:=null()
   ?};
   MGR.cntx_pop()
?};

TACTTLS.blank();
{? VAR.KOR
|| TACTTLS.OD:=date();
   TACTTLS.CRE_TIME:=TACTTLS.tm_stamp();
   TACTTLS.USER:=OPERATOR.USER;
   TACTTLS.CAUSE:=exec('ttxcause_default','tech_common')
?};
exec('efld_opt','tech_tool');

{? _choice>0
|| {? TACTTLS.edit("exec('TACTWERr','tech_tool')")
   || TACTTLS.add()
   ?}
?};
VAR.KOR:=0;
~~


\zm_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Poprawienie NPU na technologii
::  OLD: \zm_popraw/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
{? VAR.GRP_MOD='' ||  VAR.GRP_MOD:='N' ?};
{? TACTTLS.ACT='N'
|| _msg:='Element nieaktywny — nie można modyfikować.'@;
   {? VAR.GRUPA='T'
   || KOMM.add(exec('grp_mod_msg','tech_tool')+_msg)
   || FUN.info(_msg)
   ?};
   return()
?};

{? TKTL.STAT_P='N' || VAR.KOR:=0 || VAR.KOR:=1 ?};
{? VAR.GRP_MOD='N'
|| {? TACTTLS.PFAZ<>null() || VAR.A_WYD:=TACTTLS.PFAZ().WYD || VAR.A_WYD:=TKTL.JORG ?};
   _mgr:=TACTTLS.M().MGR().KOD;
   {? _mgr=exec('get','#params',500750,2) |
      _mgr=exec('get','#params',500751,2) |
      _mgr=exec('get','#params',500752,2)
   || POMOC.MGR:=TACTTLS.M().MGR
   || POMOC.MGR:=null()
   ?};
   exec('ust_hdr_tacttls','tech_tool');
   TTXCAUSE.win_sel('WER');
   TTXCAUSE.actions('WER','T','d:d');
   {? VAR.KOR
   || params_get().env_tool.buffer.get();
      TACTTLS.OD:=date();
      TACTTLS.MOD_TIME:=TACTTLS.tm_stamp();
      TACTTLS.USER:=OPERATOR.USER;
      TACTTLS.CAUSE:=exec('ttxcause_default','tech_common')
   ?};
   exec('efld_opt','tech_tool')
?};

{? VAR.GRP_MOD='T' | TACTTLS.edit("
      _buffer:=exec('buffer','tech_tool');
      _buffer.get();
      {? VAR.KOR & exec('compare','#table',_buffer,params_get().env_tool.buffer,1,'OD','USER','CAUSE','MOD_TIME')
      || FUN.emsg('Nic nie zostało zmienione.\nNie można zatwierdzić zmian.'@);
         0
      || exec('TACTWERr','tech_tool')
      ?}")
|| _ok:=1;
   {? VAR.GRP_MOD='T'
   || _ok:=exec('tact_modify_frombfr','tech_tool')
   ?};
   {? _ok>0
   || TACTTLS.put()
   ?}
?};
VAR.KOR:=0;
~~


\usun_tacttls
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: 'Usuń' w oknie wertowania TACTTLS
::  OLD: \usun_tacttls/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_P='N'
|| {? VAR.GRUPA='T' | FUN.ask('Czy usunąć bieżący wiersz?'@)
   || DocLib.del('TACTTLS',TACTTLS.ref());
      TACTTLS.del()
   ?}
|| exec('zm_usun','tech_tool',1)
?};
~~


\usun_tacttls_gpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: 'Usuń' w oknie TACTTLS - przed grupą rekordów
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAN='T'
|| FUN.info('Na zaakceptowanej karcie usuwać (dezaktywować) można tylko pojedyncze pozycje.'@);
   0
|? FUN.ask('Ilość zaznaczonych N-P-U: %1. Czy usuwać?'@[form(TACTTLS.sel_size())])
|| VAR.GRUPA:='T';
   KOMM.init();
   1
|| 0
?}


\usun_tacttls_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: 'Usuń' w oknie TACTTLS - po grupie rekordów
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
KOMM.select();
~~


\przywr_tacttls
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: 'Przywróć' w oknie wertowania TACTTLS
::----------------------------------------------------------------------------------------------------------------------
{? TKTL.STAT_P='T'
|| exec('zm_usun','tech_tool',3)
?};
~~


\zm_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Zmiana - usunięcie (dezaktualizacja)/przywrócenie NPU na zatwierdzonej technologii
::   WE: _a - usuwanie-czy z dialogiem (1/0), przywracanie-czy z dialogiem (3/2)
::      [_b] - data
::      [_c] - przyczyna TTXCAUSE.ref()
::      [_d] - ustawiany znacznik czasowy
::      [_e] - znacznik czasowy przywracanych elementów
::   WY: ~~
::  OLD: \zm_usun/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
:: czy tryb przywracania
_przywr:= _a>1;
:: czy z oknem dialogowym
_bDialog:=_a-2*_przywr;

{? var_pres('_b')=type_of(date()) || _dData:=_b || _dData:=date() ?};
{? var_pres('_c')=type_of(null()) || _rCause:=_c || _rCause:=null() ?};
{? var_pres('_d')=type_of(0) || _tmstamp:=_d || _tmstamp:=TACTTLS.tm_stamp() ?};
{? var_pres('_e')=type_of(0) || _oldstamp:=_e || _oldstamp:=TACTTLS.MOD_TIME ?};

{? _przywr
|| {? TACTTLS.ACT='T'
   || {? _bDialog || FUN.emsg('Element aktywny — nie można go przywracać.'@) ?};
      return()
   ?}
|| {? TACTTLS.ACT='N'
   || {? _bDialog || FUN.emsg('Element usunięty — nie można modyfikować.'@) ?};
      return()
   ?}
?};
VAR.KOR:=1;

_win_edit:=TACTTLS.win_edit('?');
exec('npu_rodz','tech_tool');
_npu:={? VAR1.NPU='N' || 'Narzędzie' |? VAR.A_NPU='P' || 'Przyrząd' |? VAR.A_NPU='U' || 'Urządzenie' || 'N-P-U' ?};
{? _przywr
|| TACTTLS.win_edit('PRZYWR');
   TACTTLS.hdr_edit(_npu+' — '+'przywrócenie'@)
|| TACTTLS.win_edit('USUN');
   TACTTLS.hdr_edit(_npu+' — '+'usunięcie'@)
?};

{? ~_bDialog
      |
   FUN.ask(
      'Modyfikacja zatwierdzonej karty technologicznej.'@+'\n\n'+
      {? _przywr || 'Czy na pewno przywrócić element?'@ || 'Czy na pewno usunąć (dezaktywować) element?'@ ?}
   )
|| TTXCAUSE.win_sel('WER');
   TTXCAUSE.actions('WER','','T:d');
   TACTTLS.OD:=_dData;
   {? _rCause<>null()
   || TACTTLS.CAUSE:=_rCause
   || TACTTLS.CAUSE:=exec('ttxcause_default','tech_common')
   ?};
   {? ~_bDialog | TACTTLS.edit("
         {? TACTTLS.CAUSE=null()
         || FUN.info('Podaj przyczynę zmiany.'@);
            'CAUSE'
         || ''
         ?}
         ")
   ||
      {? _bDialog | ~_przywr | TACTTLS.MOD_TIME=_oldstamp
      || TACTTLS.ACT:={? _przywr || 'T' || 'N' ?};
         TACTTLS.MOD_TIME:=_tmstamp;
         TACTTLS.USER:=OPERATOR.USER;
         TACTTLS.put()
      ?}
   ?}
?};
TACTTLS.win_edit(_win_edit);
VAR.KOR:=0;
~~


\TACTWERr
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: 'Po rekord' w oknie wertowania TACTTLS
::  OLD: \TACTWERr/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
_chk:='';
_chk_rec:=0;
{? VAR.GRP_MOD<>'P'
|| {? VAR.KOR
   || {? VAR.GRUPA='T'
      || _chk:=__CHK.record(TACTTLS,0,'M','CAUSE');
         {? _chk<>'' || _chk_rec:=1 ?}
      || _chk:=__CHK.record2(TACTTLS,'M','Symbol','CAUSE','Przyczyna')
      ?}
   || {? VAR.GRUPA='T'
      || _chk:=__CHK.record(TACTTLS,0,'M');
         {? _chk<>'' || _chk_rec:=1 ?}
      || _chk:=__CHK.record2(TACTTLS,'M','Symbol')
      ?}
   ?}
?};
{? _chk_rec>0 & _chk<>''
|| _fld:={? _chk='M' || 'Symbol'@ |? _chk='CAUSE' || 'Przyczyna'@ ?};
   _msg:='Pole \'%1\' musi być wypełnione. Należy uzupełnić brakujące informacje.'@[_fld];
   KOMM.add(exec('grp_mod_msg','tech_tool')+_msg)
?};
{? _chk=''
|| {? VAR.GRP_MOD='N'
   || {? TACTTLS.FILE<>'' || TACTTLS.ILE:=tpar.calc(TACTTLS.FILE) ?};
      {? TACTTLS.FZXJM<>'' || TACTTLS.ZXJM:=tpar.calc(TACTTLS.FZXJM) ?};
      {? TACTTLS.FZH<>'' || TACTTLS.ZH:=tpar.calc(TACTTLS.FZH) ?};
      {? TACTTLS.ILE<=0
      || FUN.info('Ilość musi być większa od zera.'@);
         _chk:={? TACTTLS.FILE<>'' || 'FILE' || 'ILE' ?}
      |? TACTTLS.ZXJM<0
      || FUN.info('Zużycie nie może być ujemne.'@);
         _chk:={? TACTTLS.FZXJM<>'' || 'FZXJM' || 'ZXJM' ?}
      |? TACTTLS.ZH<0
      || FUN.info('Zużycie nie może być ujemne.'@);
         _chk:={? TACTTLS.FZH<>'' || 'FZH' || 'ZH' ?}
      ?}
   ?}
?};
{? _chk=''
|| {? -menu_txt()='popraw' || _ref:=TACTTLS.ref() || _ref:=null() ?};
   TACTTLS.cntx_psh();
   TACTTLS.index('UNIQ');
   TACTTLS.prefix(TACTTLS.NRK,TACTTLS.NROP,TACTTLS.M,TACTTLS.PFAZ);
   {? TACTTLS.first() & TACTTLS.ref()<>_ref
   || {? VAR.GRUPA='T'
      || _msg:='Istnieje już N-P-U o danym materiale przypisane do danej operacji i fazy produkcji.'@;
         KOMM.add(exec('grp_mod_msg','tech_tool')+_msg)
      || FUN.info('Należy wpisać inny symbol albo fazę.'@)
      ?};
      _chk:='M'
   ?};
   TACTTLS.cntx_pop()
?};
_chk


\ttls_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: 'Przed rekord' (m.in. kolorowanie rekordu) w oknie TACTTLS
::   WY: Schemat kolorowania
::  OLD: \ttls_rec/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
_unrop:=exec('FindAndGet','#table',TOPER,TACTTLS.NROP,,"UNROP",0);
_nrop:=exec('get_oper_nr','tech_oper',_unrop,'\\','n');
VAR.STRING:=VAR1.TOPER:=_nrop;
TACTTLS.actions_grayed(cur_win(1,1),{? TACTTLS.ACT='T' || 'Ó' || {? TACTTLS.sel_size()>0 || 'U' || 'UP' ?} ?});
{? TACTTLS.ACT='N'
|| 'TACTTLS#01#01'
|| ~~
?}


\display_tacttls
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: 'Wyświetl' w oknie wertowania TACTTLS
::  OLD: \display_tacttls/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ust_hdr_tacttls','tech_tool');
_unrop:=exec('FindAndGet','#table',TOPER,TACTTLS.NROP,,"UNROP",0);
_nrop:=exec('get_oper_nr','tech_oper',_unrop,'\\','n');
VAR.STRING:=VAR1.TOPER:=_nrop;
TACTTLS.display();
~~


\TACTTLS_kasuj
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: kasuje wszystkie narzędzia do karty
::   WE: TKTL.ref()
::  OLD: \TACTTLS/tkasuj.fml
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;
TACTTLS.clear();
TACTTLS.index('KNROP');
TACTTLS.prefix(_tktl);
{? TACTTLS.first()
|| {!
   |? DocLib.del('TACTTLS',TACTTLS.ref());
      TACTTLS.del()
   !}
?};
1


\get_util_way
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: zwraca ścieżkę do narzędzia (użyta przy usuwaniu parametrów)
::   WE: TACTTLS.ref()
::  OLD: \get_util_way/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
_way:='';
TACTTLS.cntx_psh(); TOPER.cntx_psh();
TACTTLS.prefix();
{? TACTTLS.seek(_a)
|| _way:=_way+{? TACTTLS.NROP<>null() || 'Operacja: '+exec('get_oper_nr','tech_oper',TACTTLS.NROP().UNROP)+'-> (Parametry) ' ?};
   _way:=_way+'Narzędzie: '+TACTTLS.M().KTM
?};
TACTTLS.cntx_pop(); TOPER.cntx_pop();
_way


\rodz_decode
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [12.10]
:: OPIS: Zwraca 'N','P','U' w zależności od kodu grupy materiałowej
::  OLD: \rodz_decode/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=exec('get','#params',500750,2)
|| return('N')
|? _a=exec('get','#params',500751,2)
|| return('P')
|? _a=exec('get','#params',500752,2)
|| return('U')
|| return('I')
?}


\efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Opcje pól w oknach redagowania tabeli TACTTLS
::----------------------------------------------------------------------------------------------------------------------
TACTTLS.efld_opt(TACTTLS.win_edit('?'),{? VAR.KOR || 'mark=1' || 'mark=0' ?},,'CAUSE');
~~


\action_tools
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rejestrowanie pozostałych danych dla wzorca/karty technologicznej - akcja N-P-U z menu okna wertowania
::   WE: [_a] - 1 (do karty - inicjować środowisko), 0 (do operacji - nie inicjować środowiska)
::       [_b] - 'N', 'P', 'U', ''
::       [_c] - karta używana na wyższym poziomie wywołania (operacje)
::   WY: tekst sterujący menu użytkownika / ~~
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0) || _tool2card:=_a || _tool2card:=1 ?};
{? var_pres('_b')=type_of('') || _npu:=_b || _npu:='' ?};
{? var_pres('_c')=type_of(0) || _used:=_c || _used:=0 ?};

_action:={? TKTL.TORW='Z' || 'TTE_PZL_DTEC' |? TKTL.TORW='W' || 'TTE_WTE_DRDP' || 'TTE_TEC_DRDP' ?};

{? TKTL.STAT_N='N' & TKTL.TORW<>'Z'
|| exec('tools_main','tech_tool',TKTL.ref(),{? _tool2card || null() || TOPER.ref() ?},_npu,_tool2card,1)

|? TKTL.TORW<>'Z' & TKTL.ARCH='N' & TKTL.STAT_P='N' & exec('chk_role','#b__box',OPERATOR.USER,_action)
|| _args:=exec('mp_run_a','#b__box');
   _args.ACT_UID:=_action;
   _args.UIDREF:=TKTL.uidref();
   _args.AKCJA:='NPU';
   _args.CONTEXT:=obj_new('oper','npu','init','used');
   _args.CONTEXT.oper:={? _tool2card || null() || TOPER.ref() ?};
   _args.CONTEXT.npu:=_npu;
   _args.CONTEXT.init:=_tool2card;
   _args.CONTEXT.used:=_used;

   _args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);
   exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'TKTL',TKTL.ref());

   exec('mp_run','#b__box',_args)

|| exec('tools_main','tech_tool',TKTL.ref(),{? _tool2card || null() || TOPER.ref() ?},_npu,_tool2card,_used)
?}


\action_tools_button
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Podgląd N-P-U dla wzorca technologii - obsługa przycisku w oknie redagowania TKTL w czynnościach
::----------------------------------------------------------------------------------------------------------------------
:: Wyświetlana zawsze pełna lista N-P-U
{? 1
::   TKTL.TYP().UTIL='K'
|| _res:=exec('tools_main','tech_tool',TKTL.ref(),null(),'',1,1)
|| exec('menu_start','tech_head');
   exec('tree','tech_oper',5,0);
   exec('menu_stop','tech_head')
?};
''


\tactlls_filtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Ustawia filtr programowy na tabelę TACTTLS (N-P-U). Filtr ustawiany na podsatwie aktualnych wartości zmiennych
::       VAR.A_KTL oraz VAR.A_OP
::   WE: _a - TKTL.ref()
::       [_b] - TOPER.ref()
::       [_c] - dodatkowo dołączyć do listy N-P-U bez przypisanej operacji: [0] - nie , 1 - tak
::             (ma znaczenie, gdy _b<>null()
::----------------------------------------------------------------------------------------------------------------------
_tktl:={? var_pres('_a')=type_of(null()) || _a || null() ?};
_toper:={? var_pres('_b')=type_of(null()) || _b || null() ?};
_add_null:={? var_pres('_c')=type_of(0) || _c || 0 ?};
TACTTLS.clear();
_sort:='NROP,M(MGR),M(KTM)';
_from:='';
_where:='NRK=:_a ';
{? _toper<>null() & _add_null>0
|| _where+='and (NROP=:_b or NROP is null)'
|| _where+='and NROP=:_b '
?};
TACTTLS.f_set(_sort,_from,_where,_tktl,_toper);
~~


\zm_popraw_gpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Grupa przed dla akcji Popraw w oknach wertowania dla N-P-U
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_continue:=1;
TACTTLS.cntx_psh();
{? TACTTLS.sel_size()>1
|| _continue:=FUN.ask('Ilość zaznaczonych N-P-U: %1. Czy poprawić wybrane N-P-U?'@
      [form(TACTTLS.sel_size())]);
   {? _continue>0
   ||
      TACTTLS.seek(null(),,,1);
      TACTTLS.blank(1);
::    Sprawdzenie czy są zaznaczone zatwierdzone N-P-U
      _tab_sel:=TACTTLS.sel_aget();
      _kor:=0;
      {? _tab_sel.first()
      || TACTTLS.cntx_psh();
         TKTL.cntx_psh();
         TACTTLS.prefix();
         {!
         |?
            {? TACTTLS.seek(_tab_sel.REF)
            ||
::             Sprawdzenie, czy modyfikowana zatwierdzona operacja
               {? _kor=0 & TACTTLS.NRK().STAT_P='T'
               || _kor:=1
               ?}
            ?};
            _kor=0 & _tab_sel.next()
         !};
         TACTTLS.cntx_pop();
         TKTL.cntx_pop()
      ?};
      _kor_bf:=VAR.KOR;
      VAR.KOR:=_kor;
      VAR1.NPU:='N';
      VAR.GRP_MOD:='P';
      _buffer:=exec('buffer','tech_tool');
      _buffer.get();
      params_set('env_tool',params_get().env_tool,'buffer',_buffer);
      TACTTLS.win_edit('GRP_POP');
      _valid:="
         _res:=1;
         params_set(params_get());
         _buffer:=exec('buffer','tech_tool');
         _buffer.get();
         {? exec('compare','#table',_buffer,params_get().buffer,0,'M','PFAZ')
         || FUN.info('Przynajmniej jedno z pól musi zostać określone.'@);
            _res:=0
         ?};
         {? _res>0
         || {? VAR.KOR
            || {? TACTTLS.CAUSE=null()
               || FUN.emsg('Podaj przyczynę zmiany.'@);
                  _res:='CAUSE'
               ?}
            ?}
         ?};
         _res
      ";
      exec('efld_opt','tech_tool');
      _continue:=TACTTLS.edit(_valid);
      {? _continue>0
      || params_get().env_tool.buffer_grp.get();
         sel_nchk();
         KOMM.init(255,,'Grupowa modyfikacja N-P-U'@);
         VAR.GRP_MOD:='T';
         VAR.GRUPA:='T'
      || VAR.GRP_MOD:='N';
         VAR.GRUPA:='N'
      ?};
      VAR.KOR:=_kor_bf
   ?}
?};
TACTTLS.cntx_pop();
_continue


\zm_popraw_gpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Grupa po dla akcji Popraw w oknach wertowania dla N-P-U
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRP_MOD='T'
|| VAR.GRUPA:='N';
   VAR.GRP_MOD:='N';
   KOMM.select()
?};
~~


\grp_mod_msg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Zwraca informacje o operacji, materiale, którego będzie dotyczył komunikat błędu.
::       Działa na aktualnym buforze tabeli TACTTLS
::   WY: STRING z informacją o operacji, materiale
::----------------------------------------------------------------------------------------------------------------------
_grp_msg:='';
_grp_msg:=
   {? TACTTLS.NROP<>null()
   || 'Operacja: %1, N-P-U: %2'@[exec('get_oper_nr','tech_oper',TACTTLS.NROP().UNROP),TACTTLS.M().KTM]
   || 'N-P-U: %1'@[TACTTLS.M().KTM]
   ?}+' - ';
_grp_msg


\tact_modify_frombfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Modyfikuje aktualny bufor tabeli TACTTLS na podstawie innego bufora (podczas grupowej modyfikacji danych)
::   WY: 0 - wystąpił błędy, rekord nie zostanie zmodfikowany / 1 - ok
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_ok:=1;
::_fields:=params_get().env_tool.fields;
_buffer_grp:=params_get().env_tool.buffer_grp;
:: Zmiana indeksu materiałowego N-P-U (grupa materiałowa musi się zgadzać z dotychczasową)
{? _buffer_grp.M<>null() & TACTTLS.M<>_buffer_grp.M
|| _mgr:=exec('FindAndGet','#table',M,_buffer_grp.M,,"M.MGR",null());
   {? _mgr<>TACTTLS.M().MGR
   || _ok:=0;
      _msg:='Nie można przypisać indeksu materiałowego o innej grupie materiałowej niż dotychczas.'@;
      {? VAR.GRUPA='T'
      || KOMM.add(exec('grp_mod_msg','tech_tool')+_msg)
      || FUN.emsg(_msg)
      ?}
   || TACTTLS.M:=_buffer_grp.M
   ?}
?};
:: Faza produkcji
{? _ok>0
|| {? _buffer_grp.PFAZ<>null() & _buffer_grp.PFAZ<>TACTTLS.PFAZ
   ||
      {? TACTTLS.NROP<>null()
      || _ok:=0;
         _msg:='Nie można modyfikować fazy produkcji N-P-U przypisanego do operacji.'@;
         {? VAR.GRUPA='T'
         || KOMM.add(exec('grp_mod_msg','tech_tool')+_msg)
         || FUN.emsg(_msg)
         ?}
      || TACTTLS.PFAZ:=_buffer_grp.PFAZ
::         exec('ae_pfaz','tech_tool')
      ?}
   ?}
?};

:: Dla zatwierdzonych kart przypisanie przyczyny zmiany
_tktl_stan:=exec('FindAndGet','#table',TKTL,TACTTLS.NRK,,"TKTL.STAT_P",'');
{? _ok>0 & _tktl_stan='T'
|| TACTTLS.OD:=date();
   TACTTLS.MOD_TIME:=TACTTLS.tm_stamp();
   TACTTLS.USER:=OPERATOR.USER;
   TACTTLS.CAUSE:=_buffer_grp.CAUSE
?};
{? _ok>0
||
:: Końcowa walidacja TACTTLS
   _res:=exec('TACTWERr','tech_tool');
   {? (type_of(_res)=type_of(0) & _res=0) | (type_of(_res)=type_of('') & _res<>'')
   || _ok:=0
   ?}
?};
_ok

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 48c42a88718bb62e6340b87565c55d03a1efbb87a7c06b9a266964fb5246b53d5ee8d9ad652995be159e920b6234b4b45250b616e6195c4b5bc871c976180e4a69b8c82f257a1451dfee7c46f3294940d93bae66fd01bf55eaf8cdc416f9582edebd106689855409c9829b2680cde1e9145b725a00bdb9cac397f0f79b60001c

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zbl_dok.fml
:: Utworzony: 22.09.2020
:: Autor: AWI
:: Systemy: ZBL
::======================================================================================================================
:: Zawartość: Obsługa dokumentów Businesslink
::======================================================================================================================


\env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: e-invoicing - środowisko
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablice i komentować poszczególne jej elementy
::       powiedzmy, że to bedzie pole
         _fld:="31+form(_a)";
::       powiedzmy, że to bedzie metoda
         _mth:="31+form(_a)";

_env:=obj_new(
::             pola
                _fld('MERIT',                'przy kopiowaniu - data sprzedaży nowych dokumentów')
               ,_fld('TAB',                  'zakładka')
::             identyfikatory okienek
               ,_fld('wid_win_main',         'ID okna glownego')

::             tytuly okienek
               ,_fld('tit_main',             'Tytul okienka glownego')

::             tabele tymczasowe
               ,_fld('TAB_DOKUM',            'Robocza tabela $DOKUM.ref(), np. do akcji grupowych')

::             indeksy tymczasowych tabel

::             uchwyty do okien
               ,_fld('WIN_MAIN',             'Główne okno grupowe')
::               ,_fld('WIN_IN',               'Dokumenty przychodzące')
               ,_fld('WIN_OUT',              'Dokumenty wychodzące')
               ,_fld('WIN_OUT_ALL',          'Wszystkie dokumenty wychodzące')
               ,_fld('WIN_KH',               'Kontrahenci')
               ,_fld('WIN_TD',               'Typy dokumentów sprzedażu')
               ,_fld('RED_VIEW',              'Okno podglądu dokumentu')

::             identyfikatory przycisków

::             METODY
               ,_mth('initObszar',           'Init obszaru')
               ,_mth('doneObszar',           'Done obszaru')
               ,_mth('winMain',              'Ustawienie głównego okna')
               );

_env.MERIT:='T';
_env.TAB:='';

_env.wid_win_main:='bl_winmain';

:: Tytuł okna ustawiany w obiekcie AreaTitle
_env.tit_main:='';

_env.TAB_DOKUM:=tab_tmp(1,'REF','STRING[16]','$DOKUM.ref()');

_env.WIN_MAIN:='';
_env.RED_VIEW:='';

_env.initObszar:="exec('initObszar','zbl_dok',.)";

_env.doneObszar:="exec('doneObszar','zbl_dok',.)";

_env.winMain:="exec('winMain','zbl_dok',.)";

_env


\initObszar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: init
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR');
1


\doneObszar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: done
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR');
~~


\winMain
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Okno główne
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_merit:=_env.MERIT='T';
BPMN.B_DOMAIN:=exec('FindInSet','#table','B_DOMAIN','SYMBOL','ZBL',,,1,,null());
::
:: Przychodzące - okno wertowania
::
_wer_in:=exec('wer_in','zbl_dok','ZBL_DOK');
_wer_in_all:=exec('wer_in','zbl_dok','ZBL_DOK_ALL');
::
:: Wychodzące - okno wertowania
::
_wer_out:=_env.WIN_OUT:=exec('wer_out','zbl_dok','ZBL_DOK');
_wer_out_all:=_env.WIN_OUT_ALL:=exec('wer_out','zbl_dok','ZBL_DOK_ALL');
::
:: załączniki
::
exec('ctr_zal_init','zbl_dok','DOKUMZ');
::
:: log
::
exec('ctr_log_init','zbl_dok');
::
:: Kontrahenci - okno wertowania
::
:_wer_kh:=_env.WIN_KH:=exec('wer_kh','zbl_dok');
::
:: Podgląd dokumentu - okno wertowania
::
exec('web_view','zbl_dok');
::
:: Dokumenty Businesslink - okno grupowe
::
_win_grp:=_env.WIN_MAIN:=DOKUM.grp_make(_env.tit_main,,_env.wid_win_main,,,"exec('exit','zws',_a)");
DOKUM.win_sel(_win_grp);
::
:: przychodzące
::
_far_in:=$('
   _env:=params_get().env;
   {? DOKUM.sel_size()=0
         &
      {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
   ||
      tab_show(0,''atta'');
      tab_show(1,''view'');
      exec(''dokum_rfr'',''bloby'','':%1'',''%2'');
      exec(''bl_log_html'',''zbl_dok'');
      exec(''web_view_load'',''zbl_dok'');
      {? exec(''upgrade2325_blbc1'',''zbl'')
      || exec(''dokumzbc_load'',''zbl_dok'');
         grp_disp(DOKUMZBC,''WER2'')
      ?}
   ||
      tab_hide(0,1,''atta'');
      exec(''web_view_load'',''zbl_dok'',1);
      tab_hide(1,,''view'')
   ?}
   '[__CTR.WIN_ID,__CTR.WIN_ACR]
);
_fb:=$('
   {? var_press(''WIN'',__CTR)<0 || exec(''ctr_zal_init'',''zbl_dok'',''DOKUMZ'') ?};
   _env:=params_get().env;
   _env.TAB:=''IN'';
   __CTR.WIN:=''%1'';
   DOKUM.index(''BL'');
   DOKUM.prefix(''T'',REF.FIRMA,''P'',''T'')
   '[_wer_in]
);
DOKUM.grp_sel(_win_grp,,_wer_in,'Przychodzące'@,_far_in,,,,_fb,,,,'maximized','bl_wer_in');
::
:: wychodzące
::
_far_out:=$('
   _env:=params_get().env;
   {? DOKUM.sel_size()=0
         &
      {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
   ||
      tab_show(0,''atta'');
      tab_show(1,''view'');
      exec(''dokum_rfr'',''bloby'','':%1'',''%2'',,0);
      exec(''bl_log_html'',''zbl_dok'');
      exec(''web_view_load'',''zbl_dok'');
      {? exec(''upgrade2325_blbc1'',''zbl'')
      || exec(''dokumzbc_load'',''zbl_dok'');
         grp_disp(DOKUMZBC,''WER2'')
      ?}
   ||
      tab_hide(0,1,''atta'');
      exec(''web_view_load'',''zbl_dok'',1);
      tab_hide(1,,''view'')
   ?}
   '[__CTR.WIN_ID,__CTR.WIN_ACR]
);
_fb:="
   {? var_press('WIN',__CTR)<0 || exec('ctr_zal_init','zbl_dok','DOKUMZ') ?};
   _env:=params_get().env;
   _env.TAB:='OUT';
   __CTR.WIN:=_env.WIN_OUT;
   DOKUM.index('BL');
   DOKUM.prefix('T',REF.FIRMA,'W','T')
";
DOKUM.grp_sel(_win_grp,,_wer_out,'Wychodzące'@,_far_out,,,,_fb,,,,'maximized','bl_wer_out',1);
::
:: wszystkie przychodzące
::
_fb:=$('
   {? var_press(''WIN'',__CTR)<0 || exec(''ctr_zal_init'',''zbl_dok'',''DOKUMZ'') ?};
   _env:=params_get().env;
   _env.TAB:=''IN_ALL'';
   __CTR.WIN:=''%1'';
   DOKUM.index(''BL'');
   DOKUM.prefix(''T'',REF.FIRMA,''P'')
   '[_wer_in_all]
);
DOKUM.grp_sel(_win_grp,,_wer_in_all,'Wszystkie przychodzące'@,_far_in,,,,_fb,,,,'maximized','bl_wer_in_all');
::
:: wszystkie wychodzące
::
_fb:="
   {? var_press('WIN',__CTR)<0 || exec('ctr_zal_init','zbl_dok','DOKUMZ') ?};
   _env:=params_get().env;
   _env.TAB:='OUT_ALL';
   __CTR.WIN:=_env.WIN_OUT_ALL;
   DOKUM.index('BL');
   DOKUM.prefix('T',REF.FIRMA,'W')
";
DOKUM.grp_sel(_win_grp,,_wer_out_all,'Wszystkie wychodzące'@,_far_out,,,,_fb,,,,'maximized','bl_wer_out_all');
::
:: kontrahenci
::
_fb:="
   KH_DOD.index('BL1');
   KH_DOD.prefix(REF.FIRMA,'T');
   tab_hide(0,1,'atta');
   exec('web_view_load','zbl_dok',1);
   tab_hide(1,,'view')
";
:DOKUM.grp_sel(_win_grp,KH_DOD,_wer_kh,'Kontrahenci'@,,,,,_fb,,,,'maximized');
:task_attach('ZWS_PAR_KKHP');
:task_attach('ZWS_PAR_KKHR');
::
:: podgląd załącznika
::
DOKUM.grp_splt(_win_grp,'panel0','vertical','view',',60%');
DOKUM.grp_ctr(_win_grp,,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,'Podgląd'@,,,,100,50,"","",,'maximized');
::
:: załączniki
::
DOKUM.grp_splt(_win_grp,'panel0','horizontal','atta',20);
_fb:="
   __CTR.PODGLAD:=-1;
   {? DOKUM.sel_size()=0
         &
      {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
         &
      DOKUM.RODZAJ_K='W'
   || __CTR.PODGLAD:=0
   ?};
   exec('bobs_dokumz','bloby')
";
_fa:="";
DOKUM.grp_ctr(_win_grp,DOKUMZ,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_fb,_fa,,'maximized');
::
:: log
::
_fb:="exec('bl_log_html','zbl_dok')";
_fa:="";
DOKUM.grp_ctr(_win_grp,,__CTH.WIN_ACR,,'Log'@,,,,80,,_fb,_fa,,'maximized');
::
:: Rezultat kontroli Businesscheck
::
{? exec('upgrade2325_blbc1','zbl')
|| _fb:="exec('dokumzbc_load','zbl_dok')";
   _fa:="";
   DOKUM.grp_sel(_win_grp,DOKUMZBC,'WER2','Rezultat kontroli Businesscheck'@,,,,,_fb,_fa,,,'maximized')
?};

{? _merit
|| DOKUM.win_edit('REDK')
|| DOKUM.win_edit('RED')
?};

:: definicje linków
DOKUM.win_link(_wer_in,"
   _a:=obj_new('LINK','text');
   _a.LINK:=DOKUM.uidref();
   _a.text:='Odsyłacz do dokumentu: '+DOKUM.SYM;
   _a
");
DOKUM.win_link(_wer_in_all,"
   _a:=obj_new('LINK','text');
   _a.LINK:=DOKUM.uidref();
   _a.text:='Odsyłacz do dokumentu: '+DOKUM.SYM;
   _a
");
DOKUM.win_link(_wer_out,"
   _a:=obj_new('LINK','text');
   _a.LINK:=DOKUM.uidref();
   _a.text:='Odsyłacz do dokumentu: '+DOKUM.SYM;
   _a
");
DOKUM.win_link(_wer_out_all,"
   _a:=obj_new('LINK','text');
   _a.LINK:=DOKUM.uidref();
   _a.text:='Odsyłacz do dokumentu: '+DOKUM.SYM;
   _a
");

AreaTitle.setTabWin(DOKUM,~~);
AreaTitle.setTitle()


\winLzk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Zakładka dokumenty Businesslink w obszarze Dokumenty zakupu i wewnętrzne
::   WE: _a - akronim okna grupowego
::       [_b] - bez dolnego panelu [0]/1
::       [_c] - formuła "odśwież" zamiast domyślnej
::       [_d] - formuła "przed" zamiast domyślnej
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_grp:=_a;
_bez_dol:={? var_pres('_b')=type_of(0) || _b || 0 ?};
{? exec('get','#params',6006,2,OPERATOR.USER)='N' || return() ?};
BPMN.B_DOMAIN:=exec('FindInSet','#table','B_DOMAIN','SYMBOL','LZK',,,1,,null());
::
:: Przychodzące - okno wertowania
::
_wer_in:=exec('wer_in','zbl_dok','LZK_DOK');
:: załączniki
::
{? _bez_dol
|| exec('ctr_zal_init','zbl_dok','DOKUMZ')
?};
::
:: log
::
exec('ctr_log_init','zbl_dok');
::
:: Podgląd dokumentu - okno wertowania
::
exec('web_view','zbl_dok');
::
:: Zakładka Przychodzące Businesslink
::
:: przychodzące
::
{? var_pres('_c')=type_of("")
|| _far_in:=_c
|| _far_in:=$('
      _fget:=DOKUM.get();
      {? _fget & DOKUM.f_active()>=2
      || _fget:=DOKUM.f_test()
      ?};
      {? ~_fget || DOKUM.DOKUMI:=null() ?};
      exec(''bl_log_html'',''zbl_dok'');
      exec(''web_view_load'',''zbl_dok'',~_fget)'
   )
?};
{? var_pres('_d')=type_of("")
|| _fb:=_d
|| _fb:="
      FAKS.f_clear();
      DOKUM.index('BL_DZK');
      DOKUM.prefix('T',REF.FIRMA,'P','T')
   "
?};
FAKS.grp_sel(_win_grp,DOKUM,_wer_in,'Przychodzące Businesslink'@,_far_in,,,,_fb,,,,'maximized','bl_wer_in');
task_attach('ZBL_DOK_PBUF');
task_attach('ZBL_DOK_RBUF');
{? ~_bez_dol
::
:: log
::
|| FAKS.tab_splt(_win_grp,'','horizontal','log',16);
   _fb:="exec('bl_log_html','zbl_dok')";
   _fa:="";
   FAKS.grp_ctr(_win_grp,DOKUM,__CTH.WIN_ACR,,,,,,80,,_fb,_fa,,'maximized_with_title')
?};
::
:: podgląd załącznika
::
FAKS.tab_splt(_win_grp,,'vertical','view');
FAKS.grp_ctr(_win_grp,DOKUM,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,,,,,100,50,"","",,'maximized');
::

DOKUM.win_edit('REDK')


\winDks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Zakładka dokumenty Businesslink w obszarze Dokumenty ksiegowe
::   WE: _a - akronim okna grupowego
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_grp:=_a;
_merit:=1;
BPMN.B_DOMAIN:=exec('FindInSet','#table','B_DOMAIN','SYMBOL','FKS',,,1,,null());
BPMN.SYM_DOM:=BPMN.B_DOMAIN().SYMBOL;
::
:: Przychodzące - okno wertowania
::
_wer_in:=exec('wer_in','zbl_dok','FKS_DOK');
::
:: log
::
::exec('ctr_log_init','zbl_dok');
::
:: Podgląd dokumentu - okno wertowania
::
::_redview:=exec('red_view','zbl_dok',55,21);
::
:: Web podgląd dokumentu - okno wertowania
::
exec('web_view','zbl_dok',65,16);
::
:: Zakładka Przychodzące Businesslink
::
:: przychodzące
::
_far_in:=$('
   _fget:=DOKUM.get();
   {? _fget & DOKUM.f_active()>=2
   || _fget:=DOKUM.f_test()
   ?};
   {? ~_fget || DOKUM.DOKUMI:=null() ?};
   {? exec(''upgrade2325_blbc1'',''zbl'')
   || grp_disp(DOKUMZBC,''WER_DOK1'')
   || _show:=objZakl.showZakl(''DOKUM'',''LOG'')+objZakl.showZakl(''DOK'',''PREVIEW'');
      exec(''tab_show_hide'',''!fks_dks_zdok'',_show,objZakl.qtyZakl(),''bottom'')
   ?};
   exec(''bl_log_html'',''zbl_dok'');
   {? PAR_SKID.get(442)=''T'' || exec(''web_view_load'',''zbl_dok'',~_fget) ?}
   '
);
_fb:="
   {? var_pres('dok_zbl')<=0 | dok_zbl=0
   || _show:=objZakl.showZakl('DOKUM','LOG')+objZakl.showZakl('DOK','PREVIEW')+objZakl.showZakl('DOKUMZBC','WER_DOK1');
      exec('tab_show_hide','!fks_dks_zdok',_show,objZakl.qtyZakl(),'bottom')
   ?};
   dok_zbl:=1;
   DOKUM.index('BL_DZK');
   DOKUM.prefix('T',REF.FIRMA,'P','T')
";
TREE_REJ.grp_sel(_win_grp,DOKUM,'WER_FKS','Przychodzące Businesslink'@,_far_in,,,,_fb,,,,'maximized','bl_wer_in');
task_attach('ZBL_DOK_PBUF');
task_attach('ZBL_DOK_RBUF');
::
:: podgląd załącznika - split wyłączony bo podzielone w tree_rej_dok
::
::TREE_REJ.tab_splt(_win_grp,,'vertical','view');

::TREE_REJ.grp_edit(_win_grp,DOKUM,_redview,,,,,,'maximized_with_title');
::
:: web podgląd załącznika - wyłączony bo obsłużony w tree_rej_dok
::
::TREE_REJ.grp_ctr(_win_grp,DOKUM,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,,,,,1,1,"","",,'maximized');
::
{? _merit
|| DOKUM.win_edit('REDK')
|| DOKUM.win_edit('RED')
?}


\winObg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Zakładka dokumenty Businesslink w obszarze faktury w obiegu
::   WE: _a - akronim okna grupowego
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_grp:=_a;
_merit:=1;
BPMN.B_DOMAIN:=exec('FindInSet','#table','B_DOMAIN','SYMBOL','OBG',,,1,,null());
BPMN.SYM_DOM:=BPMN.B_DOMAIN().SYMBOL;
::
:: Przychodzące - okno wertowania
::
_wer_in:=exec('wer_in','zbl_dok','DOB_DOK');
::
:: log
::
exec('ctr_log_init','zbl_dok');
::
:: Podgląd dokumentu - okno wertowania
::
exec('web_view','zbl_dok',65,16);
::
:: Zakładka Przychodzące Businesslink
::
:: przychodzące
::
_far_in:=$('
   _fget:=DOKUM.get();
   {? _fget & DOKUM.f_active()>=2
   || _fget:=DOKUM.f_test()
   ?};
   {? ~_fget || DOKUM.DOKUMI:=null() ?};
   {? exec(''upgrade2325_blbc1'',''zbl'')
   || grp_disp(DOKUMZBC,''WER_EDOK'')
   ?};
   exec(''bl_log_html'',''zbl_dok'');
   exec(''web_view_load'',''zbl_dok'',~_fget)'
);
_fb:="
   DOKUM.index('BL_DZK');
   DOKUM.prefix('T',REF.FIRMA,'P','T');
   dok_zbl:=1;
   tab_hide(1,,'tab2'); tab_hide(2,,'tab2'); tab_hide(3,,'tab2'); tab_show(4,'tab2');
   {? exec('upgrade2325_blbc1','zbl') ||  tab_show(5,'tab2') ?}
";
EDOKUM.grp_sel(_win_grp,DOKUM,_wer_in,'Przychodzące Businesslink'@,_far_in,,,,_fb,,,,'maximized','bl_wer_in');
task_attach('ZBL_DOK_PBUF');
task_attach('ZBL_DOK_RBUF');
:: podgląd załącznika
::
EDOKUM.tab_splt(_win_grp,'','vertical','view');
EDOKUM.grp_ctr(_win_grp,DOKUM,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,,,,,65,16,"","",,'maximized');
::
{? _merit
|| DOKUM.win_edit('REDK')
|| DOKUM.win_edit('RED')
?}


\winObe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Zakładka dokumenty Businesslink w obszarze faktury w obiegu
::   WE: _a - akronim okna grupowego
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_grp:=_a;
_merit:=1;
BPMN.B_DOMAIN:=exec('FindInSet','#table','B_DOMAIN','SYMBOL','OBE',,,1,,null());
BPMN.SYM_DOM:=BPMN.B_DOMAIN().SYMBOL;
::
:: Przychodzące - okno wertowania
::
_wer_in:=exec('wer_in','zbl_dok','WOB_DOK');
::
:: log
::
exec('ctr_log_init','zbl_dok');
::
:: Podgląd dokumentu - okno wertowania
::
exec('web_view','zbl_dok',65,16);
::
:: Zakładka Przychodzące Businesslink
::
:: przychodzące
::
_far_in:=$('
   _fget:=DOKUM.get();
   {? _fget & DOKUM.f_active()>=2
   || _fget:=DOKUM.f_test()
   ?};
   {? ~_fget || DOKUM.DOKUMI:=null() ?};
   {? exec(''upgrade2325_blbc1'',''zbl'') || grp_disp(DOKUMZBC,''WER_EDOK'') ?};
   exec(''bl_log_html'',''zbl_dok'');
   exec(''web_view_load'',''zbl_dok'',~_fget)'
);
_fb:="
   DOKUM.index('BL');
   DOKUM.prefix('T',REF.FIRMA,'P','T');
   tab_hide(1,,'tab2'); tab_hide(2,,'tab2'); tab_show(3,'tab2');
   {? exec('upgrade2325_blbc1','zbl') ||  tab_show(4,'tab2') ?}
";
EDOKUM.grp_sel(_win_grp,DOKUM,_wer_in,'Przychodzące Businesslink'@,_far_in,,,,_fb,,,,'maximized','bl_wer_in');
task_attach('ZBL_DOK_PBUF');
task_attach('ZBL_DOK_RBUF');
:: podgląd załącznika
::
EDOKUM.tab_splt(_win_grp,'','vertical','view',',60%');
EDOKUM.grp_ctr(_win_grp,DOKUM,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,,,,,65,16,"","",,'maximized');
::
{? _merit
|| DOKUM.win_edit('REDK')
|| DOKUM.win_edit('RED')
?}


\wer_in
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Przychodzące - okno wertowania
::   WE: _a - obszar
::       [_b] - [0]/1/2 (1 - tylko akcja "Wybierz", 2 - dokumenty powiązane)
::   WY: akronim okna
::----------------------------------------------------------------------------------------------------------------------
_area:=_a;
_wyb_tylko:=0;
_doku_powi:=0;
{? var_pres('_b')=type_of(0)
|| {? _b=1
   || _wyb_tylko:=1
   |? _b=2
   || _doku_powi:=1
   ?}
?};
_wer_in:=DOKUM.mk_sel('Przychodzące'@,'P',,'bl_wer_in',,,,,'U');
DOKUM.win_fld(_wer_in,,'DATA',,,10,,,'Data'@,,,,,,,,'mobile_visible=1');
DOKUM.win_fld(_wer_in,,'RECEIVED',,,'-7,12,12',,,'Potwierdzono'@,,,2,,"'T'","'N'");
DOKUM.win_fld(_wer_in,,'ESEND',,,'-7,13,13',,,'Wysłano e-mail'@,,,2,,"'T'","'N'");
DOKUM.win_fld(_wer_in,,'KH','SKR',,'10,11,11',,,'Kontrahent'@,,,,,,,,'mobile_visible=1');
DOKUM.win_fld(_wer_in,,'BL_TYPE',,,10,,,'Typ'@);
DOKUM.win_fld(_wer_in,,'SYM_ZEW',,,20,,,'Symbol zewnętrzny'@,,,,,,,,'mobile_visible=1');
{? _area='FKS_DOK'
|| DOKUM.win_fld(_wer_in,,'KR_OP',,,47,,,'Opis'@)
|? _area='DOB_DOK'
|| DOKUM.win_fld(_wer_in,,'KR_OP',,,62,,,'Opis'@)
|? _area='WOB_DOK'
|| DOKUM.win_fld(_wer_in,,'KR_OP',,,67,,,'Opis'@)
|| DOKUM.win_fld(_wer_in,,'KR_OP',,,30,,,'Opis'@)
?};
DOKUM.win_fld(_wer_in,,'BL_STAT',,,20,,,'Status'@,,,,,,,,'mobile_visible=1');
_fml:="
   {? DOKUM.DG_UID=''
   || ''
   || 'xwin16.png:11'
   ?}";
DOKUM.win_fml(_wer_in,,'BL_STAT',,'ICON_BEFORE',_fml);
{? _wyb_tylko
||
:: akcja: Wybierz
   _fb:="
     {? FUN.ask('Czy powiązać z tym dokumentem?'@)
     || sel_exit()
     ?}";
   DOKUM.win_act(_wer_in,,'Formuła','&Wybierz'@@,,,_fb,,1,,,,'W')
|? _doku_powi
||
:: akcja: Przeglądaj archiwum
   _fb:="params_exec('archive_view','dokum','BUID')";
   DOKUM.win_act(_wer_in,,'Formuła','Przeglądaj archiwum'@@,,,_fb,,,,,,'P');
   DOKUM.win_act(_wer_in,1,'Formuła','Przeglądaj archiwum'@@,,,_fb,,,,,,'P')
||
   {? _area='LZK_DOK'
   ||
:: akcja: Rejestruj dokument zakupu
      _fb:="params_exec('bl_act','zbl_dok','DZK',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','Rejestruj dokument &zakupu'@@,,,_fb,,,,,,'Z');
      task_attach('ZBL_DOK_RBUF')

   |? _area='FKS_DOK'
   ||
:: akcja: Rejestruj dokument księgowy
      _fb:="params_exec('bl_act','zbl_dok','DKS',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','Rejestruj dokument k&sięgowy'@@,,,_fb,,,,,,'S');
      task_attach('ZBL_DOK_RBUF')

   |? _area='DOB_DOK'
   ||
:: akcja: Rejestruj dokument w obiegu
      _fb:="params_exec('ob_start','dokum',1,0,'N')";
      DOKUM.win_act(_wer_in,,'Formuła','Rejestruj &fakturę w obiegu'@@,,,_fb,,,,,,'D');
      task_attach('ZBL_DOK_RBUF')
   |? _area='WOB_DOK'
   ||
:: akcja: Rejestruj wniosek w obiegu
      _fb:="params_exec('ob_start','dokum',2,0,'N')";
      DOKUM.win_act(_wer_in,,'Formuła','Rejestruj &wniosek w obiegu'@@,,,_fb,,,,,,'W');
      task_attach('ZBL_DOK_RBUF')
   |? _area='LSP_ZKN'
   ||
:: akcja: Rejestruj zamówienie sprzedaży
      _fb:="params_exec('bl_act','zbl_dok','ORD',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','Rejestruj zamów&ienie sprzedaży'@@,,,_fb,,,,,,'I');
      task_attach('ZBL_DOK_RBUF')
   |? _area='ZWS_KHR'
:: akcja: Rejestruj dokument w kontaktach
   || _fb:="params_exec('bl_act','zbl_dok','DKH',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','Rejestruj dokument w ko&ntaktach'@@,,,_fb,,,,,,'N');
      task_attach('ZBL_DOK_RBUF')
   ||
:: akcja: Rejestruj automatycznie
      _fb:="params_exec('bl_act','zbl_dok','AUT',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','Rejestruj automatyczni&e'@@,,,_fb,,1,,,,'E');
      task_attach('ZBL_DOK_RBUF');
::DOKUM.win_btn(_wer_in,'text=%1, panel=right'['Rejestruj automatyczni&e'@@],'menu:E');
:: menu: Rejestruj - start
      _menu:='Rejestruj'@@;
      DOKUM.win_act(_wer_in,,'Menu',_menu,,,,,,,,,'R');
:: akcja: Rejestruj - Dokument zakupu
      _fb:="params_exec('bl_act','zbl_dok','DZK',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','Dokument &zakupu'@@,'#R',,_fb,,,,,,'Z');
      task_attach('ZBL_DOK_RBUF');
::DOKUM.win_btn(_wer_in,'text=%1, panel=right'['Rejestruj dokument &zakupu'@@],'menu:RZ');
:: akcja: Rejestruj - Dokument księgowy
      _fb:="params_exec('bl_act','zbl_dok','DKS',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','Dokument k&sięgowy'@@,'#R',,_fb,,,,,,'S');
      task_attach('ZBL_DOK_RBUF');
::DOKUM.win_btn(_wer_in,'text=%1, panel=right'['Rejestruj dokument k&sięgowy'@@],'menu:RS');
:: akcja: Rejestruj - Dokument w obiegu
      _fb:="params_exec('ob_start','dokum',1,0,'N')";
      DOKUM.win_act(_wer_in,,'Formuła','&Faktura w obiegu'@@,'#R',,_fb,,,,,,'D');
      task_attach('ZBL_DOK_RBUF');
:: akcja: Rejestruj - Dokument w obiegu
      _fb:="params_exec('ob_start','dokum',2,0,'N')";
      DOKUM.win_act(_wer_in,,'Formuła','&Wniosek w obiegu'@@,'#R',,_fb,,,,,,'W');
      task_attach('ZBL_DOK_RBUF');
::DOKUM.win_btn(_wer_in,'text=%1, panel=right'['Rejestruj dokument &obiegu'@@],'menu:RO');
:: akcja: Rejestruj - Dokument w kontaktach
      _fb:="params_exec('bl_act','zbl_dok','DKH',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','Dokument w ko&ntaktach'@@,'#R',,_fb,,,,,,'N');
      task_attach('ZBL_DOK_RBUF');
:: akcja: Rejestruj - Zamówienie sprzedaży
      _fb:="params_exec('bl_act','zbl_dok','ORD',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','Zamów&ienie sprzedaży'@@,'#R',,_fb,,,,,,'I');
      task_attach('ZBL_DOK_RBUF');
:: ---
      DOKUM.win_act(_wer_in,,'Formuła','--','#R',,,,,,,,'Ź');
:: akcja: Powiąż z dokumentem nadrzędnym
      _fb:="params_exec('bl_act','zbl_dok','POW',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','&Powiąż z dokumentem nadrzędnym'@@,'#R',,_fb,,,,,,'P');
      task_attach('ZBL_DOK_RBUF')
:: menu: Rejestruj - koniec
   ?};
:: akcja: Popraw
   _fb:="params_exec('popraw','zbl_dok')";
   DOKUM.win_act(_wer_in,,'Formuła','Popraw'@@,,,_fb,,,1,_fb,,'P'); task_attach('ZBL_DOK_RBUF');
:: akcja: Mapowanie indeksów
   _fb:="params_exec('map_ind','zbl_dok')";
   DOKUM.win_act(_wer_in,,'Formuła','Mapowanie indeksów'@@,,,_fb,,,,,,'M');
::--- Do ćwiczeń z pobieraniem dokumentów (odblokować w razie potrzeby)
::_fb:="
::   {? DOKUM.sel_size()>0 | FUN.ask('Czy na pewno usunąć dokument?'@)
::   || DOKUMZ.index('DISP');
::      DOKUMZ.prefix(DOKUM.ref());
::      {? DOKUMZ.first()
::      || {!
::         |? DOKUMZ.del()
::         !}
::      ?};
::      DOKUM.del()
::   ?}
::";
::_fbg:="FUN.ask('Czy na pewno usunąć zaznaczone dokumenty?'@)";
::_fag:="~~";
::DOKUM.win_act(_wer_in,,'Formuła','Usuń (tymczasowe)'@@,,,_fb,,,1,_fbg,_fag,'U');
::---
:: akcja: Idź do dokumentu
   _fb:="params_exec('go_to_doc','zbl_dok')";
   DOKUM.win_act(_wer_in,,'Formuła','Idź do dokumen&tu'@@,,,_fb,,,,,,'T');
:: akcja: Załączniki
   {? (_area='LZK_DOK' & ~exec('upgrade2325_blbc1','zbl')) | _area='ZWS_KHR' | _area='DOB_DOK'
      | _area='WOB_DOK' | _area='FKS_DOK' | _area='LSP_ZKN'
   ||
      _fb:=" exec('winAtta','zbl_dok',DOKUMZ)";
      DOKUM.win_act(_wer_in,,'Formuła','Załą&czniki'@@,,,_fb,,,,,,'C')
   ?};
:: menu: Funkcje - start
   _menu:='Funkcje'@@;
   DOKUM.win_act(_wer_in,0,'Menu',_menu,,,,,,,,,'F');
   DOKUM.win_act(_wer_in,1,'Menu',_menu,,,,,,,,,'F');
:: akcja: Anuluj w systemie
   _fb:="params_exec('bl_act','zbl_dok','ANU',DOKUM.ref())";
   DOKUM.win_act(_wer_in,,'Formuła','A&nuluj w systemie'@@,'#F',,_fb,,,,,,'N');
:: akcja: Nie obsługuj
   _fb:="params_exec('bl_act','zbl_dok','NIE',DOKUM.ref())";
   DOKUM.win_act(_wer_in,,'Formuła','Nie obsługu&j'@@,'#F',,_fb,,,,,,'J');
   task_attach('ZBL_DOK_RBUF');
:: akcja: Obsługuj
   {? _area='ZBL_DOK_ALL'
   ||
      _fb:="params_exec('bl_act','zbl_dok','TAK',DOKUM.ref())";
      DOKUM.win_act(_wer_in,,'Formuła','&Obsługuj'@@,'#F',,_fb,,,,,,'O')
   ?};
:: ---
   DOKUM.win_act(_wer_in,,'Formuła','--','#F',,,,,,,,'Y');
:: akcja: Aktualizuj z Businesslink
::_fb:="params_exec('document_get','businesslink3')";
::_fbg:="params_exec('document_get_bg','businesslink3')";
::_fag:="params_exec('document_get_ag','businesslink3')";
::DOKUM.win_act(_wer_in,,'Formuła','Ak&tualizuj z Businesslink'@@,'#F',,_fb,,,1,_fbg,_fag,'T');
::task_attach('ZBL_BIL_DOKG');
:: akcja: Potwierdź w Businesslink
::_fb:="params_exec('document_get_confirm','businesslink3')";
::_fbg:="params_exec('document_get_confirm_bg','businesslink3')";
::_fag:="params_exec('document_get_confirm_ag','businesslink3')";
::DOKUM.win_act(_wer_in,,'Formuła','Potwierdź w &Businesslink'@@,'#F',,_fb,,,1,_fbg,_fag,'B');
::task_attach('ZBL_BIL_DOKC');
:: akcja: Synchronizuj z Businesslink
   {? _area<>'DOB_DOK' & _area<>'WOB_DOK' &
      exec('chk_role','#b__box',OPERATOR.USER,'ZBL_BIL_DOKG') & exec('chk_role','#b__box',OPERATOR.USER,'ZBL_BIL_DOKC')
   || _fb:="params_exec('document_get_and_confirm','businesslink3')";
      _fbg:="params_exec('document_get_and_confirm_bg','businesslink3')";
      _fag:="params_exec('document_get_and_confirm_ag','businesslink3')";
      DOKUM.win_act(_wer_in,,'Formuła','Synchronizuj z Businesslink'@@,'#F',,_fb,,,1,_fbg,_fag,'S')
   ?};
:: ---
   DOKUM.win_act(_wer_in,,'Formuła','--','#F',,,,,,,,'X');
:: akcja: Podgląd log
   _fb:="{? DOKUM.BL_LOG = null || FUN.info('Brak zapisu w log.'@) || exec('bl_view','#blob',DOKUM,'BL_LOG') ?}";
   DOKUM.win_act(_wer_in,,'Formuła','Podgląd lo&g'@@,'#F',,_fb,,,,,,'G');
   _fb:="exec('dokumlog_his','zbl_dok',DOKUM.uidref())";
   DOKUM.win_act(_wer_in,,'Formuła','&Historia zmian'@@,'#F',,_fb,,,,,,'H');
:: akcja: Status usług
   _fb:="exec('api_status_action','businesslink3')";
   DOKUM.win_act(_wer_in,,'Formuła','S&tatus usług'@@,'#F',,_fb,,,,,,'T');
   DOKUM.win_act(_wer_in,1,'Formuła','S&tatus usług'@@,'#F',,_fb,,,,,,'T');
:: ---
   DOKUM.win_act(_wer_in,,'Formuła','--','#F',,,,,,,,'Ź');
   DOKUM.win_act(_wer_in,1,'Formuła','--','#F',,,,,,,,'Ź');
:: akcja: pokaż powiązane
   _fb:="exec('powi_pokaz','zbl_dok')";
   DOKUM.win_act(_wer_in,,'Formuła','Po&każ powiązane'@@,'#F',,_fb,,,,,,'K');
:: akcja: dane xml
   _fb:="exec('dokumxml_sel','edi_xml')";
   DOKUM.win_act(_wer_in,,'Formuła','Dane XML'@@,'#F',,_fb,,,,,,'D');
:: ---
   DOKUM.win_act(_wer_in,,'Formuła','--','#F',,,,,,,,'L');
:: akcja: Archiwizuj
   {? _area='ZBL_DOK_ALL'
   ||
      _fb:="params_exec('archive','dokum','BL')";
      DOKUM.win_act(_wer_in,,'Formuła','Archiwizuj'@@,'#F',,_fb,,,1,_fb,,'A'); task_attach('ZWS_DOK_ARCH')
   ?};
:: akcja: Przeglądaj archiwum
   _fb:="params_exec('archive_view','dokum','BL')";
   DOKUM.win_act(_wer_in,,'Formuła','Przeglądaj archiwum'@@,'#F',,_fb,,,,,,'P');
   DOKUM.win_act(_wer_in,1,'Formuła','Przeglądaj archiwum'@@,'#F',,_fb,,,,,,'P');
:: menu: Funkcje - end
:: akcja: Raporty
   {! _ii:=0..1
   |! DOKUM.win_act(_wer_in,_ii,'Menu','Raport&y'@@,,,,,,,,,'Y');
      _fb:="params_exec('np_run','#b__box','ZBL_DOK_ZWYD')";
      DOKUM.win_act(_wer_in,_ii,'Formuła','&Zestawienia'@@,'#Y',,_fb,,,,,,'Z'); task_attach('ZBL_DOK_ZWYD');
      _fb:="params_exec('np_run','#b__box','ZBL_DOK_ZSQL')";
      DOKUM.win_act(_wer_in,_ii,'Formuła','Zapytania &SQL'@@,'#Y',,_fb,,,,,,'S'); task_attach('ZBL_DOK_ZSQL')
   !}
?};
:: akcja: Kolejność
DOKUM.win_act(_wer_in,,'Kolejność');
:: akcja: Legenda
_fb:="exec('legenda','zbl_dok')";
DOKUM.win_act(_wer_in,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,_fb,,,,,,'L');
:: akcja: Rekord
_fb:=$('
   {? _a
   ||
      _act:='''';

      _Tab:=ref_tab(DOKUM.REFSQL);
      {? DOKUM.BL_STAT<>exec(''ready_to_registration'',''zbl_stat'')
         & DOKUM.BL_STAT<>exec(''registration_failed'',''zbl_stat'')
         & DOKUM.BL_STAT<>exec(''during_registration'',''zbl_stat'')
      || {? %1
         || _act+=''ER''
         || _act+=''ZSDWINP''
         ?}
      |? type_of(_Tab)=type_of(~~)
      || {? %1
         || _contact_rej_auto:=0;
            {? DOKUM.KH
            ||
               exec(''kh_dod_ini'',''kontrahent'',DOKUM.KH,0);
               _contact_rej_auto:=
                  DOKUM.BL_STAT<>exec(''contact'',''zbl_stat'') & exec(''bl_act_kod'',''zbl_dok'',KH_DOD.BL_ACT)=''DKH''
            ?};
            {? exec(''czy_ufd'',''edi_fo_ufd'',DOKUM.ref())=0 & ~_contact_rej_auto
                  |
               DOKUM.BL_STAT=exec(''contact'',''zbl_stat'')
            ||
               _act+=''E''
            ?}
         ?};
         _act_r:='''';
         {? DOKUM.BL_TYPE=''DOC''
         || _act_r:=''S''
         |? 3+DOKUM.BL_TYPE=''INV''
         || _act_r:=''I''
         |? 3+DOKUM.BL_TYPE=''ORD''
         || _act_r:=''ZSDW''
         ?};
         {? _act_r<>''''
         || {? %1
            || _act+=''R(''+_act_r+'')''
            || _act+=_act_r
            ?}
         ?}
      ||
         {? %1
         || _act+=''E''
         ?};
         _act_r:=''NP'';
         {? _Tab<>FAKS || _act_r+=''Z'' ?};
         {? _Tab<>DOK  || _act_r+=''S'' ?};
         {? _Tab<>ZK_N  || _act_r+=''I'' ?};
         {? _Tab<>EDOKUM
         || _act_r+=''DW''
         || _typobieg:=exec(''FindAndGet'',''#table'',EDOKUM,DOKUM.REFSQL,,"@.EDOKUM.TYP().TYPOBIEG().NAZWA",'''');
            {? _typobieg<>''Obieg faktur''
            || _act_r+=''D''
            |? _typobieg<>''Obieg wniosków''
            || _act_r+=''W''
            ?}
         ?};
         {? _act_r<>''''
         || {? %1
            || _act+=''R(''+_act_r+'')''
            || _act+=_act_r
            ?}
         ?}
      ?};
      _act+={? DOKUM.sel_size()=0 & exec(''archive_ready'',''zbl_dok'')=0 || ''F(A)'' || '''' ?};
      DOKUM.actions_grayed(''%2'',_act);
      DOKUM.win_edit(exec(''dokum_okno_red'',''dokum''))
   ?};
   params_exec(''rekprzed'',''color'',''DOKUM#03'')
'[
   $(_area='ZBL_DOK' | _area='ZBL_DOK_ALL'),_wer_in]);
DOKUM.win_act(_wer_in,,'Rekord',,,,_fb);
_wer_in


\wer_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Wychodzące - okno wertowania
::   WE: _a - obszar
::   WY: akronim okna
::----------------------------------------------------------------------------------------------------------------------
_area:=_a;
_wer_out:=DOKUM.mk_sel('Wychodzące'@,'P',,'bl_wer_out',,,,,'U');
DOKUM.win_fld(_wer_out,,'DATA',,,10,,,'Data'@,,,,,,,,'mobile_visible=1');
DOKUM.win_fld(_wer_out,,'CZAS',,,5,,,'Czas'@);
DOKUM.win_fld(_wer_out,,'BSEND',,,,,,'Wysłano'@,,,2,,"'T'","'N'","'X'",'mobile_visible=1');
DOKUM.win_fld(_wer_out,,'RECEIVED',,,,,,'Odebrano'@,,,2,,"'T'","'N'");
DOKUM.win_fld(_wer_out,,'ESEND',,,,,,'Wysłano e-mail'@,,,2,,"'T'","'N'");
DOKUM.win_fld(_wer_out,,'KH','SKR',,10,,,'Kontrahent'@,,,,,,,,'mobile_visible=1');
DOKUM.win_fld(_wer_out,,'KR_OP',,,30,,,'Opis'@);
DOKUM.win_fld(_wer_out,,'BL_STAT',,,20,,,'Status'@,,,,,,,,'mobile_visible=1');
:: akcja: Przygotuj do wysłania
_fb:="params_exec('bl_act','zbl_dok','PDW',DOKUM.ref())";
DOKUM.win_act(_wer_out,,'Formuła','Przygotuj do wysłania'@@,,,_fb,,1,,,,'P');
task_attach('ZBL_DOK_RBUF');
::DOKUM.win_btn(_wer_out,'text=%1, panel=right'['Przygotuj do wysłania'@@],'menu:P');
:: akcja: Wyślij do Businesslink
_fb:="params_exec('action_send','businesslink3')";
_fbg:="params_exec('action_send_bg','businesslink3')";
_fag:="params_exec('action_send_ag','businesslink3')";
DOKUM.win_act(_wer_out,,'Formuła','Wyślij do Businesslink'@@,,,_fb,,,1,_fbg,_fag,'W'); task_attach('ZBL_BIL_DOKS');
:: akcja: Doślij załączniki - aktualnie wyłączona
_fb:="params_exec('DocumentAttachmentsAdd','businesslink3',DOKUM.ref(),1)";
DOKUM.win_act(_wer_out,,'Formuła','Doślij załączniki'@@,,,_fb,,,,,,'D'); task_attach('ZBL_BIL_DOKS');
task_attach('ZBL_DOK_RBUF');
:: akcja: Idź do dokumentu
_fb:="params_exec('go_to_doc','zbl_dok')";
DOKUM.win_act(_wer_out,,'Formuła','Idź do dokumen&tu'@@,,,_fb,,,,,,'T');
:: menu: Funkcje - start
_menu:='Funkcje'@@;
DOKUM.win_act(_wer_out,0,'Menu',_menu,,,,,,,,,'F');
DOKUM.win_act(_wer_out,1,'Menu',_menu,,,,,,,,,'F');
:: menu: KSeF - start
_menu_ksef:='KSeF'@@;
DOKUM.win_act(_wer_out,,'Menu',_menu_ksef,'#F',,,,,,,,'K');
:: akcja: Autoryzuj
_fb:="params_exec('ksefauth','zbl_dok')";
DOKUM.win_act(_wer_out,,'Formuła','Autoryzuj'@@,'#FK',,_fb,,,1,_fb,,'A');
:: akcja: Wycofaj autoryzcję
_fb:="params_exec('ksefauth_revoke','zbl_dok')";
DOKUM.win_act(_wer_out,,'Formuła','Wycofaj a&utoryzację'@@,'#FK',,_fb,,,1,_fb,,'U');
:: akcja: Podpisz dokument wysyłany do KSeF
_fb:="params_exec('ksef_sign','zbl_dok')";
DOKUM.win_act(_wer_out,,'Formuła','Podpi&sz dokument wysyłany do KSeF'@@,'#FK',,_fb,,,,,,'S');
:: ---
DOKUM.win_act(_wer_out,,'Formuła','--','#FK',,,,,,,,'Ą');
:: akcja: Urzędowe potwierdzenie odbioru (UPO)
_fb:="params_exec('upo_get','businesslink3')";
DOKUM.win_act(_wer_out,,'Formuła','Urzędowe &potwierdzenie odbioru (UPO)'@@,'#FK',,_fb,,,,,,'P');
:: menu: KSeF - end
:: akcja: Anuluj w systemie
_fb:="params_exec('bl_act','zbl_dok','ANU',DOKUM.ref())";
DOKUM.win_act(_wer_out,,'Formuła','A&nuluj w systemie'@@,'#F',,_fb,,,,,,'N');
:: akcja: Nie obsługuj
_fb:="params_exec('bl_act','zbl_dok','NIE',DOKUM.ref())";
DOKUM.win_act(_wer_out,,'Formuła','Nie obsługu&j'@@,'#F',,_fb,,,,,,'J');
:: ---
DOKUM.win_act(_wer_out,,'Formuła','--','#F',,,,,,,,'X');
:: akcja: Informacja z Businesslink
_fb:="params_exec('action_verify','businesslink3')";
DOKUM.win_act(_wer_out,,'Formuła','Informacja z Businesslink'@@,'#F',,_fb,,,,,,'I'); task_attach('ZBL_BIL_DOKS');
:: akcja: Unieważnij w Businesslink
_fb:="params_exec('action_invalidate','businesslink3')";
DOKUM.win_act(_wer_out,,'Formuła','Unieważnij w Businesslink'@@,'#F',,_fb,,,,,,'U'); task_attach('ZBL_BIL_DOKS');
:: akcja: Funkcje techniczne Businesslink
{? sec_superuser()
|| _fb:="params_exec('dokum_fun_tech','businesslink3')";
   _fbg:="params_exec('dokum_fun_tech_bg','businesslink3')";
   DOKUM.win_act(_wer_out,,'Formuła','Funkcje techniczne Businesslink'@@,'#F',,_fb,,,1,_fbg,,'F')
?};
:: ---
DOKUM.win_act(_wer_out,,'Formuła','--','#F',,,,,,,,'Y');
:: akcja: Podgląd log
_fb:="{? DOKUM.BL_LOG = null || FUN.info('Brak zapisu w log.'@) || exec('bl_view','#blob',DOKUM,'BL_LOG') ?}";
DOKUM.win_act(_wer_out,,'Formuła','Podgląd lo&g'@@,'#F',,_fb,,,,,,'G');
_fb:="exec('dokumlog_his','zbl_dok',DOKUM.uidref())";
DOKUM.win_act(_wer_out,,'Formuła','&Historia zmian'@@,'#F',,_fb,,,,,,'H');
:: akcja: Status usług
_fb:="exec('api_status_action','businesslink3')";
DOKUM.win_act(_wer_out,,'Formuła','S&tatus usług'@@,'#F',,_fb,,,,,,'T');
DOKUM.win_act(_wer_out,1,'Formuła','S&tatus usług'@@,'#F',,_fb,,,,,,'T');
:: ---
DOKUM.win_act(_wer_out,,'Formuła','--','#F',,,,,,,,'L');
DOKUM.win_act(_wer_out,1,'Formuła','--','#F',,,,,,,,'L');
:: akcja: Archiwizuj
{? _area='ZBL_DOK_ALL'
||
   _fb:="params_exec('archive','dokum','BL')";
   DOKUM.win_act(_wer_out,,'Formuła','Archiwizuj'@@,'#F',,_fb,,,1,_fb,,'A'); task_attach('ZWS_DOK_ARCH')
?};
:: akcja: Przeglądaj archiwum
_fb:="params_exec('archive_view','dokum','BL')";
DOKUM.win_act(_wer_out,,'Formuła','Przeglądaj archiwum'@@,'#F',,_fb,,,,,,'P');
DOKUM.win_act(_wer_out,1,'Formuła','Przeglądaj archiwum'@@,'#F',,_fb,,,,,,'P');
:: menu: Funkcje - end
:: akcja: Raporty
{! _ii:=0..1
|! DOKUM.win_act(_wer_out,_ii,'Menu','Raport&y'@@,,,,,,,,,'Y');
   _fb:="params_exec('np_run','#b__box','ZBL_DOK_ZWYD')";
   DOKUM.win_act(_wer_out,_ii,'Formuła','&Zestawienia'@@,'#Y',,_fb,,,,,,'Z'); task_attach('ZBL_DOK_ZWYD');
   _fb:="params_exec('np_run','#b__box','ZBL_DOK_ZSQL')";
   DOKUM.win_act(_wer_out,_ii,'Formuła','Zapytania &SQL'@@,'#Y',,_fb,,,,,,'S'); task_attach('ZBL_DOK_ZSQL')
!};
:: akcja: Kolejność
DOKUM.win_act(_wer_out,,'Kolejność');
:: akcja: Legenda
_fb:="exec('legenda','zbl_dok')";
DOKUM.win_act(_wer_out,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,_fb,,,,,,'L');
:: akcja: Rekord
_fb:=$('
   {? _a
   ||
      _env:=params_get().env;
      _act:={? DOKUM.sel_size()=0 & exec(''archive_ready'',''zbl_dok'')=0 || ''F(A)'' || '''' ?};
      DOKUM.actions_grayed(''%1'',_act);
      DOKUM.win_edit(exec(''dokum_okno_red'',''dokum''))
   ?};
   params_exec(''rekprzed'',''color'',''DOKUM#03'')
'[
   _wer_out]);
DOKUM.win_act(_wer_out,,'Rekord',,,,_fb);
_wer_out


\wer_archive
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Archiwalne - okno wertowania
::   WE: [_a] - podgląd dokumentów powiązane [0]/1
::   WY: akronim okna
::----------------------------------------------------------------------------------------------------------------------
_doku_powi:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_wer:=DOKUM.mk_sel('Archiwum'@,'P',,'bl_wer_archive',,,,,'U');
DOKUM.win_fld(_wer,,'RODZAJ_K',,,2,,,'Rodzaj'@);
DOKUM.win_fld(_wer,,'DATA',,,10,,,'Data'@);
DOKUM.win_fld(_wer,,'CZAS',,,5,,,'Czas'@);
DOKUM.win_fld(_wer,,'BSEND',,,,,,'Wysłano'@,,,2,,"'T'","'N'","'X'");
DOKUM.win_fld(_wer,,'RECEIVED',,,,,,'Odebrano/Potwierdzono'@,,,2,,"'T'","'N'");
DOKUM.win_fld(_wer,,'ESEND',,,,,,'E-mail'@,,,2,,"'T'","'N'");
DOKUM.win_fld(_wer,,'KH','SKR',,10,,,'Kontrahent'@);
DOKUM.win_fld(_wer,,'KR_OP',,,20,,,'Opis'@);
DOKUM.win_fld(_wer,,'BL_STAT',,,20,,,'Status'@);
_fml:="
   {? DOKUM.DG_UID=''
   || ''
   || 'xwin16.png:11'
   ?}";
DOKUM.win_fml(_wer,,'BL_STAT',,'ICON_BEFORE',_fml);
{? ~_doku_powi
||
:: akcja: Załączniki
  _fb:=" exec('winAtta','zbl_dok',DOKUMZA)";
  DOKUM.win_act(_wer,,'Formuła','Załą&czniki'@@,,,_fb,,,,,,'C');
:: akcja: Podgląd log
  _fb:="{? DOKUM.BL_LOG = null || FUN.info('Brak zapisu w log.'@) || exec('bl_view','#blob',DOKUM,'BL_LOG') ?}";
  DOKUM.win_act(_wer,,'Formuła','Podgląd lo&g'@@,,,_fb,,,,,,'G');
:: akcja: Historia zmian
  _fb:="exec('dokumlog_his','zbl_dok',DOKUM.uidref())";
  DOKUM.win_act(_wer,,'Formuła','H&istoria zmian'@@,,,_fb,,,,,,'I');
:: akcja: Idź do dokumentu
  _fb:="params_exec('go_to_doc','zbl_dok')";
  DOKUM.win_act(_wer,,'Formuła','Idź do dokumen&tu'@@,,,_fb,,,,,,'T');
:: akcja: pokaż powiązane
   _fb:="exec('powi_pokaz','zbl_dok')";
   DOKUM.win_act(_wer,,'Formuła','Pokaż po&wiązane'@@,,,_fb,,,,,,'W');
:: akcja: Przywróć z archiwum
  _fb:="exec('archive_przywr','dokum','BL')";
  DOKUM.win_act(_wer,,'Formuła','Przywróć z arc&hiwum'@@,,,_fb,,,1,_fb,,'H');
  task_attach('ZWS_DOK_ARCH')
?};
DOKUM.win_act(_wer,,'Kolejność');
:: akcja: Legenda
_fb:="exec('legenda','zbl_dok')";
DOKUM.win_act(_wer,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy'@,_fb,,,,,,'L');
:: akcja: Rekord
_fb:='
   {? _a
   || {? DOKUM.RODZAJ_K=\'W\'
      || _act:=\'W\'
      || _act:=\'\'
      ?};
      DOKUM.actions_grayed(\''+_wer+'\',_act)
   ?};
   params_exec(\'rekprzed\',\'color\',\'DOKUM#03\')';
_fb:=$_fb;
DOKUM.win_act(_wer,,'Rekord',,,,_fb);
_wer


\ctr_zal_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Kontrolka Załączniki - deklaracja
::   WE: _a - akronim tabeli z załącznikami
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab_acr:=_a;

VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB', 'BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','BLOKUJ','PODGLAD','WIN');
__CTR.BTAB:=_tab_acr;
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','bloby','bl_dokz_ctr',':',75);
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('sep','#file',1);
__CTR.DND:=1;
__CTR.BLOKUJ:=0;
__CTR.PODGLAD:=0;
__CTR.WIN:=''


\ctr_log_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Kontrolka Log - deklaracja
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTH');
__CTH:=obj_new('ctr_id','FILEPATH','FML_SAVE','FML_CONFIG','WIN_ACR','GRP','RESULT','CLOSE','ACTIVE');
_sep:=exec('sep','#file');
_filelocal:=exec('tmp_filepath','#file',$SYSLOG.tm_stamp()+'.html');
_env_html:=exec('html_core','#edit',DOKUM,_filelocal,'bl_log','Log',,0,0,0,0,0,0);
__CTH.ctr_id:=_env_html.ctr_id;
__CTH.FILEPATH:=_env_html.FILEPATH;
__CTH.FML_SAVE:=_env_html.FML_SAVE;
__CTH.FML_CONFIG:=_env_html.FML_CONFIG;
__CTH.WIN_ACR:=_env_html.WIN_ACR;
__CTH.GRP:=_env_html.GRP;
__CTH.RESULT:=_env_html.RESULT;
__CTH.CLOSE:=_env_html.CLOSE;
__CTH.ACTIVE:=0


\wer_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Kontrahenci - okno wertowania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wer_kh:=KH_DOD.mk_sel('Kontrahenci'@,'P',,'bl_wer_kh',,,,,'U');
::KH_DOD.win_fld(_wer_kh,,'FIRMA','SYMBOL',,10,,,'Firma'@);
KH_DOD.win_fld(_wer_kh,,'KH','SKR',,10);
KH_DOD.win_fld(_wer_kh,,'KH','NAZ',,35);
KH_DOD.win_fld(_wer_kh,,'KH','NIP',,13,,,'NIP'@);
KH_DOD.win_fld(_wer_kh,,'BL_STAT',,,20,,,'Status relacji'@);
KH_DOD.win_fld(_wer_kh,,'BL_ACT',,,30,,,'Domyślna obsługa dokumentu Businesslink'@);
_fb:="exec('kh2rel','zbl_dok')";
KH_DOD.win_act(_wer_kh,0,'Formuła','Nawiąż relację'@@,,,_fb,,1,,,,'N'); task_attach('ZBL_BIL_DOKK');
KH_DOD.win_act(_wer_kh,1,'Formuła','Nawiąż relację'@@,,,_fb,,1,,,,'N'); task_attach('ZBL_BIL_DOKK');
_fb:="
   {? KH_DOD.sel_size()=0
   || _act:=exec('bl_act_slo','zbl_dok',KH_DOD.BL_ACT,'INV');
      {? type_of(_act)=type_of('')
      || KH_DOD.BL_ACT:=_act;
         KH_DOD.put()
      ?}
   ?}
";
_fbg:="
   sel_nchk();
   _act:=exec('bl_act_slo','zbl_dok','','INV');
   {? type_of(_act)=type_of('')
   || {? _act=''
      || _ask:='Czy usunąć domyślną obsługę dokumentów Businesslink dla zaznaczonych kontrahentów?'@
      || _ask:='Czy ustawić domyślną obsługę dokumentów Businesslink dla zaznaczonych kontrahentów\n'
               'na wartość: %1?'@[_act]
      ?};
      {? FUN.ask(_ask)
      || _sel:=KH_DOD.sel_aget();
         {? _sel.first()
         || {!
            |? _formula:=\"KH_DOD.BL_ACT:=_b.act; KH_DOD.put()\";
               exec('FindAndGet','#table',KH_DOD,_sel.REF,,_formula,~~,exec('obj_ntab_set','#array',,'act',_act));
               _sel.next()
            !}
         ?};
         1
      || 0
      ?}
   || 0
   ?}
";
KH_DOD.win_act(_wer_kh,,'Formuła','Ustaw domyślną &obsługę'@@,,,_fb,,,1,_fbg,,'O');
_fb:="
   {? KH_DOD.sel_size()=0
   || _act:=exec('bl_act_slo','zbl_dok',KH_DOD.BL_ACT1,'ORD');
      {? type_of(_act)=type_of('')
      || KH_DOD.BL_ACT1:=_act;
         KH_DOD.put()
      ?}
   ?}
";
_fbg:="
   sel_nchk();
   _act:=exec('bl_act_slo','zbl_dok','','ORD');
   {? type_of(_act)=type_of('')
   || {? _act=''
      || _ask:='Czy usunąć domyślną obsługę zamówień Businesslink dla zaznaczonych kontrahentów?'@
      || _ask:='Czy ustawić domyślną obsługę zamówień Businesslink dla zaznaczonych kontrahentów\n'
               'na wartość: %1?'@[_act]
      ?};
      {? FUN.ask(_ask)
      || _sel:=KH_DOD.sel_aget();
         {? _sel.first()
         || {!
            |? _formula:=\"KH_DOD.BL_ACT1:=_b.act; KH_DOD.put()\";
               exec('FindAndGet','#table',KH_DOD,_sel.REF,,_formula,~~,exec('obj_ntab_set','#array',,'act',_act));
               _sel.next()
            !}
         ?};
         1
      || 0
      ?}
   || 0
   ?}
";
KH_DOD.win_act(_wer_kh,,'Formuła','Ustaw domyślną obsługę zamówień'@@,,,_fb,,,1,_fbg,,'Z');
_fb:="exec('czytaj','#stalesys',,XINFO,'BL_TNNT','BL_TP'); exec('action_partners_find','businesslink3')";
KH_DOD.win_act(_wer_kh,0,'Formuła','Aktualizuj kontrahentów'@@,,,_fb,,,,,,'A'); task_attach('ZBL_BIL_DOKK');
KH_DOD.win_act(_wer_kh,1,'Formuła','Aktualizuj kontrahentów'@@,,,_fb,,,,,,'A'); task_attach('ZBL_BIL_DOKK');
KH_DOD.win_act(_wer_kh,,'Kolejność');
_fb:="
   KH_DOD.cntx_psh();
   exec('kh_wys1','kontrahent');
   KH_DOD.cntx_pop();
   ~~
";
KH_DOD.win_act(_wer_kh,,'Wyświetl',,,,_fb);
_wer_kh


\red_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Podgląd dokumentu - okno redakcji
::   WE: _a - szerokość
::       _b - wysokość
::----------------------------------------------------------------------------------------------------------------------
_width:=100; _hight:=50;
{? _>0 || _width:=_a ?};
{? _>1 || _hight:=_b ?};
_redview:=DOKUM.mk_edit('Podgląd'@,,'bl_redview');
DOKUM.win_efld(_redview,,'DOKUMI',,,_width,_hight,,,1);
_redview


\typysp_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [20.42]
:: OPIS: Wybór typów dokumentów sprzedaży lub zakupu z listy wszystkich typów
::   WE: [_a] S - dokumenty sprzedaży (domyślnie), Z - dokumenty zakupu
::----------------------------------------------------------------------------------------------------------------------
_rodz:='S';
{? var_pres('_a')=type_of('') & (_a='S' | _a='Z')
|| _rodz:=_a
?};

_par0:='';
TYPYSP.cntx_psh();
TYPYSP.index('SPRZAK');
{? _rodz='Z'
|| TYPYSP.prefix('T')
|| TYPYSP.prefix('N')
?};
{? TYPYSP.first()
|| {!
   |? {? TYPYSP.BL='T'
      || _par0+=TYPYSP.T+' '
      ?};
      TYPYSP.next()
   !}
?};
TYPYSP.cntx_pop();

{? _rodz='Z'
|| _war:="TYPYSP.ZAK='T' & TYPYSP.KOR<>'Z'"
|| _war:="TYPYSP.ZAK='N' & TYPYSP.KOR<>'Z'"
?};
_par:=exec('typysp_wyb','params',_par0,_war);
{? _par<>_par0
|| TYPYSP.cntx_psh();
   TYPYSP.index('SPRZAK');
   {? _rodz='Z'
   || TYPYSP.prefix('T')
   || TYPYSP.prefix('N')
   ?};
   {? TYPYSP.first()
   || {!
      |? {? (' '+_par+' ')*(' '+TYPYSP.T+' ')>0 & TYPYSP.BL<>'T'
         || TYPYSP.BL:='T';
            TYPYSP.put()
         |? (' '+_par+' ')*(' '+TYPYSP.T+' ')=0 & TYPYSP.BL='T'
         || TYPYSP.BL:='N';
            TYPYSP.put()
         ?};
         TYPYSP.next()
      !}
   ?};
   TYPYSP.cntx_pop()
?};
~~


\bl_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Przetwarza zapis w rejestrze komunikacji
::   WE: _a - akcja:
::            - przychodzące: patrz opis parametru _c exec('plugins_bl_in','zbl_dok')
::            - wychodzące: patrz opis parametru _c exec('plugins_bl_out','zbl_dok')
::       _b - DOKUM.ref() dokumentu Businesslink
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_act_uid:='ZBL_DOK_RBUF';
{? exec('chk_role','#b__box',OPERATOR.USER,_act_uid)=0 || return() ?};
_act:=_a;
_dokum:=_b;
_dokum_uidref:=exec('FindAndGet','#table',DOKUM,_dokum,,"@.DOKUM.uidref()",'');
_dokum_refsql:=exec('FindAndGet','#table',DOKUM,_dokum,,"@.DOKUM.REFSQL",'');
_dokum_refsql_tab:=ref_tab(_dokum_refsql);
{? type_of(_dokum_refsql_tab)>0 & _dokum_refsql_tab=ZD_NAG
|| {? 'PDW/ANU/NIE'*_act=0
   || return()
   ?}
|? type_of(_dokum_refsql_tab)>0 & _dokum_refsql_tab=ZK_N
|| {? 'ANU/NIE/TAK/MAP/ORD/DKH/POW'*_act=0
   || return()
   ?}
|| _sz:=exec('FindAndGet','#table',FAKS,_dokum_refsql,,"FAKS.SZ",'');
   {? _sz='' || _sz:=exec('FindAndGet','#table',DOKUM,_dokum,,"{? @.DOKUM.RODZAJ_K='W' || 'S' || 'Z' ?}",'') ?};
   {? _act=''
         |
      _sz='S' & 'PDW/ANU/NIE/POW'*_act=0
         |
      _sz<>'S' & 'AUT/DZK/DKS/DOB/WOB/DKH/ANU/NIE/TAK/MAP/ORD/POW'*_act=0
   || return()
   ?}
?};
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:=_act_uid;
_params.UIDREF:=_dokum_uidref;
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ACT',_act);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOKUM',_dokum);
exec('mp_run','#b__box',_params)


\plugins_bl_out
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Uruchamia wtyczkę BL_OUT
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::       _c - sposób obsługi:
::            - 'PDW' - przygotuj do wysyłki
::            - 'ANU' - anuluj dokument w systemie
::            - 'NIE' - nie obsługuj
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=Plugin.run('BL_OUT',_a,_b,_c);
{? _result=-1
||
:: domyślne działanie wtyczki
   _result:=exec('plugins_bl_out_default','zbl_dok',_a,_b,_c)
?};
_result


\plugins_bl_out_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Dokument wychodzący Businesslink - domyślne działanie
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::       _c - patrz opis w exec('plugins_bl_out','zbl_dok')
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b=1;
_act:=_c;

_result:=0;

{? ref_name(_dokum)<>DOKUM.name() || return(_result) ?};

_continue:=1;

{? _act='ANU'
||
:: Anuluj dokumentu w systemie
   _continue:=exec('bl_act_anu','zbl_dok',_dokum)

|? _act='NIE'
||
:: Nie obsługuj
   _continue:=exec('bl_act_nie','zbl_dok',_dokum)
|? _act='PDW'
||
:: Przygotuj do wysłania
   _continue:=exec('bl_act_pdw','zbl_dok',_dokum,_sza)
?};

{? _continue>0
||
   _result:=1
?};
_result


\plugins_bl_in
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Uruchamia wtyczkę BL_IN
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::       _c - sposób obsługi:
::            - 'AUT' - rejestruj automatycznie
::            - 'DZK' - rejestruj dokument zakupu
::            - 'DKS' - rejestruj dokument księgowy
::            - 'DOB' - rejestruj dokument w obiegu
::            - 'DKH' - rejestruj dokument w kontaktach
::            - 'ANU' - anuluj dokument w systemie
::            - 'NIE' - nie obsługuj
::            - 'TAK' - obsługuj
::            - 'MAP' - mapowanie indeksów
::            - 'ORD' - rejestruj zamówienie sprzedaży
::            - 'POW' - powiąż z innym dokumentem Businesslink
::   WY: -1/
::----------------------------------------------------------------------------------------------------------------------
_result:=Plugin.run('BL_IN',_a,_b,_c);
{? _result=-1
||
:: domyślne działanie wtyczki
   _result:=exec('plugins_bl_in_default','zbl_dok',_a,_b,_c)
?};
_result


\plugins_bl_in_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Dokument przychodzący Businesslink - domyślne działanie
::   WE: _a - DOKUM.ref()
::       _b - 0/1 - bez komunikatów
::       _c - patrz opis exec('plugins_bl_in','zbl_dok')
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b;
_act:=_c;

_result:=0;

_continue:=1;

{? ref_name(_dokum)=DOKUM.name()
||
   DOKUM.cntx_psh();
   DOKUM.prefix();
   {? ~DOKUM.seek(_dokum)
   ||
      FUN.info('Nie znaleziono dokumentu Businesslink.'@);
      _continue:=0
   ||
      exec('kh_dod_ini','kontrahent',DOKUM.KH,0);
:: domyślna akcja
      {? _act='AUT'
      ||
         _domain_lic:=app_info('domain_lic');
         _act:='SKIP';
         {? exec('czy_ufd','edi_fo_ufd',DOKUM.ref())
         ||
            {? DOKUM.BL_TYPE='ORD'
            ||
               _def_act_kh:=exec('get','#params',100367);
               {? KH_DOD.BL_ACT1<>''
               || _act_kh:=exec('bl_act_kod','zbl_dok',KH_DOD.BL_ACT1);
                  {? _act_kh='ORD' & _domain_lic.find_key('LSP') || _act:='ORD'
                  |? _act_kh='DKH' & _domain_lic.find_key('KKH') || _act:='DKH'
                  ?}
               |? _def_act_kh='ORD' & _domain_lic.find_key('LSP') || _act:='ORD'
               |? _def_act_kh='DKH' & _domain_lic.find_key('KKH') || _act:='DKH'
               ?}
            ||
                _def_act_kh:=exec('get','#params',100366);
               {? KH_DOD.BL_ACT<>''
               || _act_kh:=exec('bl_act_kod','zbl_dok',KH_DOD.BL_ACT);
                  {? _act_kh='DZK' & _domain_lic.find_key('LZK') || _act:='DZK'
                  |? _act_kh='DKS' & _domain_lic.find_key('FKS') || _act:='DKS'
                  |? _act_kh='DOB' & _domain_lic.find_key('OBG') || _act:='DOB'
                  |? _act_kh='WOB' & _domain_lic.find_key('OBE') || _act:='WOB'
                  |? _act_kh='DKH' & _domain_lic.find_key('KKH') || _act:='DKH'
                  ?}
               |? _def_act_kh='DOB' & _domain_lic.find_key('OBG') || _act:='DOB'
               |? _def_act_kh='DZK' & _domain_lic.find_key('LZK') || _act:='DZK'
               |? _def_act_kh='DKS' & _domain_lic.find_key('FKS') || _act:='DKS'
               |? _def_act_kh='WOB' & _domain_lic.find_key('OBE') || _act:='WOB'
               |? _def_act_kh='DKH' & _domain_lic.find_key('KKH') || _act:='DKH'
               ?}
            ?};
            {? ~_sza & _act='SKIP'
            || FUN.info('Nie zdefiniowano domyślnej obsługi.'@)
            ?}

         |? exec('bl_act_kod','zbl_dok',KH_DOD.BL_ACT)='DKH'  & _domain_lic.find_key('KKH')
         ||
            _act:='DKH'
         ?}
      ?};
      {? _act='SKIP'
      ||
:: Pominięcie obsługi
         _continue:=0

      |? _act='ANU'
      ||
:: Anuluj dokument w systemie
         _continue:=exec('bl_act_anu','zbl_dok',_dokum)

      |? _act='NIE'
      ||
:: Nie obsługuj
         _continue:=exec('bl_act_nie','zbl_dok',_dokum)

      |? _act='TAK'
      ||
:: Obsługuj
         _continue:=exec('bl_act_tak','zbl_dok',_dokum)
      |? _act='POW'
      ||
:: Powiąż
         _continue:=exec('bl_act_pow','zbl_dok',_dokum)
      ||
:: Rejestruj
         {? _act='DZK'
         ||
:: Rejestruj Dokument zakupu
            _continue:=exec('bl_act_dzk','zbl_dok',_dokum,_sza)

         |? _act='DKS'
         ||
:: Rejestruj dokument księgowy
            _continue:=exec('bl_act_dks','zbl_dok',_dokum,_sza,{? _c='AUT' || 1 || 0 ?})

         |? _act='DOB'
         ||
:: Rejestruj dokument w obiegu
            _continue:=exec('bl_act_dob','zbl_dok',_dokum,_sza,{? _c='AUT' || 1 || 0 ?})

         |? _act='WOB'
         ||
:: Rejestruj dokument w obiegu
            _continue:=exec('bl_act_wob','zbl_dok',_dokum,_sza,{? _c='AUT' || 1 || 0 ?})

         |? _act='DKH'
         ||
:: Rejestruj dokument w kontaktach
            _continue:=exec('bl_act_dkh','zbl_dok',_dokum,_sza)

         |? _act='MAP'
         ||
:: Mapowanie indeksów
            exec('bl_act_map','zbl_dok',_dokum,_sza);
            _continue:=0
         |? _act='ORD'
         ||
:: Rejestruj zamówienie sprzedaży
            _continue:=exec('bl_act_ord','zbl_dok',_dokum,_sza)

         ?};

         {? _continue>0
         ||
            {? DOKUM.get()=0
                  |
               DOKUM.BL_STAT<>exec('during_registration','zbl_stat')
                  &
               DOKUM.BL_STAT<>exec('registered','zbl_stat')
                  &
               DOKUM.BL_STAT<>exec('contact','zbl_stat')
            ||
               {? ~_sza || FUN.info('Rejestracja nie powiodła się.'@) ?};
               _continue:=0
            ?}
         ?}
      ?}
   ?};
   DOKUM.cntx_pop()
?};

{? _continue>0
||
   _result:=1
?};
_result


\kh2rel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.42]
:: OPIS: Lista kontrahentów, z którymi można nawiązać relację
::----------------------------------------------------------------------------------------------------------------------
KH_DOD.cntx_psh();
KH_DOD.index('KH_KOD');
KH_DOD.prefix(REF.FIRMA);
_wer:=KH_DOD.mk_sel('Kontrahenci, z którymi można nawiązać relację'@,'P',0,'khdodrel',,,,,'U');
KH_DOD.win_fld(_wer,,'KH','KOD');
KH_DOD.win_fld(_wer,,'KH','SKR');
KH_DOD.win_fld(_wer,,'KH','NAZ');
KH_DOD.win_fld(_wer,,'KH','NIP',,13);
KH_DOD.win_fld(_wer,,'KH','BL_STAT',,20,,,'Status kontrahenta'@);
KH_DOD.win_fld(_wer,,'BL_STAT',,,20,,,'Status relacji'@);
_fb:="
:: Sprawdzenie, czy inny kontrahent o tym samym NIP jest już zarejestrowany w usłudze
   {? ~exec('kh_nip_registered','businesslink3',KH.ref(),1)
   || exec('PartnerFind','businesslink3',KH_DOD.ref(),1)
   ?}
";
KH_DOD.win_act(_wer,,'Formuła','Sprawdzenie stanu'@@,,,_fb,,1,,,,'S');
_fb:="exec('invite_kh','businesslink3')";
KH_DOD.win_act(_wer,,'Formuła','Nawiązanie relacji'@@,,,_fb,,,,,,'N');
_fb:="exec('invite_op','businesslink3')";
KH_DOD.win_act(_wer,,'Formuła','Ponowienie zaproszenia'@@,,,_fb,,,,,,'P');
KH_DOD.win_act(_wer,,'Kolejność');
_fb:="
   {? _a
   || _grayed:=':';
      {? KH_DOD.BL_STAT<>'' || _grayed:='N'+_grayed ?};
      {? KH_DOD.KH().BL_STAT<>exec('relation_state_awaiting','businesslink3') || _grayed:='P'+_grayed ?};
      KH_DOD.actions_grayed(cur_win(1,1),_grayed)
   ?};
   ~~
";
KH_DOD.win_act(_wer,,'Rekord',,,,_fb);
_fb:="KH_DOD.cntx_psh();exec('kh_wys1','kontrahent');KH_DOD.cntx_pop();~~";
KH_DOD.win_act(_wer,,'Wyświetl',,,,_fb);
KH_DOD.win_sel(_wer);
KH_DOD.select();
KH_DOD.cntx_pop();
~~


\bl_log_html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: przygotowanie danych dla DOKUM.OPIS pokazywanym w edytorze HTML
::   WE: [_a] - wyczyszczenie okna z edytorem, domyślnie 0
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_czysc:=0;
{? var_pres('_a')=type_of(0)
|| _czysc:=_a
?};

{? ~_czysc
|| _fget:=DOKUM.get();
   {? _fget & DOKUM.f_active()>=2
   || _fget:=DOKUM.f_test()
    ?};
   _czysc:=~_fget
?};

_filelocal:=__CTH.FILEPATH;
_can_continue:=2;
{? ~_czysc
|| _sep:=exec('sep','#file',1);

:: Tworzę katalog tymczasowy na serwerze i tam ściągam wszystkie zaznaczone pliki
   _tmp_dir:=fmk_tmp_dir(0);
   {? type_of(_tmp_dir)<>type_of(~~)
   || _pth:=_tmp_dir.get_path();
      _file0_naz:=_pth+_sep+$SYSLOG.tm_stamp()+'.txt';
      {? DOKUM.BL_LOG<>null() & DOKUM.bl_get('BL_LOG',_file0_naz)
      ||
         _fileobj:=fopen(_file0_naz,'Ur',0,,1);
         {? _fileobj.is_open()
         ||
            _tab:=tab_tmp(1,
                  'ID','STRING[1]','aaa',
                  'OPIS','SYS_MEMO','Nazwa pola 1',
                  'BLOB','BLOBRAW','Blob');
            _tab.blank();
            {? _tab.add()
            ||
               _tab.memo_put(_fileobj,'OPIS');

               _memo:=_tab.memo_txt(,1,'OPIS');
               {? _memo*'<html>'=0 & _memo*'<!DOCTYPE html>'=0
               || _memo:=gsub(_memo,'\n','<br>')
               ?};
               _tab.memo_set(_memo,'OPIS');
               _tab.memo_put(,'OPIS');
               _file_memo:=_tab.memo_get('r','OPIS');
               {? var_pres('_file_memo')>100 & _file_memo.is_open()
               || _tab.bl_put('BLOB',_file_memo,0,,'info.txt');
                  _can_continue:=_tab.bl_get('BLOB',_filelocal)
               || _can_continue:=0
               ?}
            ?};
            _fileobj.fclose()
         || _can_continue:=0
         ?};
::         _file0:=fopen(_file0_naz,'ur',0,,1);
::         {? _file0.is_open()
::         || _html:=0;
::            {!
::            |? _line:=_file0.fread();
::               _continue:=_line<>'\n';
::               {? _line<>'\n'
::               || {? _html=0 & _line*'<html>'>0
::                  || _html:=1
::                  ?};
::                  {? _html=0 & _line*'<!DOCTYPE html>'>0
::                  || _html:=1
::                  ?};
::                  {? _html=0
::                  || _line+='<br>'
::                  ?};
::                  _line+='\n';
::                  _file_copy.fwrite(_line)
::               ?};
::               _continue
::            !};
::            _file0.fclose()
::         || _can_continue:=1
::         ?};
         ~~
      || _can_continue:=0
      ?}
   || _can_continue:=0
   ?}
?};

{? __CTH.ACTIVE
|| {? _can_continue & ~_czysc
   || ctr_call(,__CTH.ctr_id,'readFile',gsub(_filelocal,'\\','\\\\'))
   || ctr_call(,__CTH.ctr_id,'resetContent','')
   ?}
|| {? _can_continue
   || {? _czysc
      || fopen(_filelocal,'Uw',0,,1)
      ?};
      __CTH.FML_CONFIG();
      __CTH.ACTIVE:=1
   ?}
?};

~~


\legenda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Dokumenty Businesslink - Legenda
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.RODZAJ_K='W'
|| exec('legenda','color','DOKUM#03')
|| exec('legenda','color','DOKUM#04')
?}


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Dokumenty Businesslink - kolory
::   WE:
::   WY: kod koloru
::----------------------------------------------------------------------------------------------------------------------
_stat:=DOKUM.BL_STAT;
{? DOKUM.RODZAJ_K='W'
||
   {? _stat=exec('before_send','zbl_stat')            || 'DOKUM#03#01'
   |? _stat=exec('ready_to_send','zbl_stat')          || 'DOKUM#03#02'
   |? _stat=exec('send','zbl_stat')                   || 'DOKUM#03#03'
   |? _stat=exec('cancelled_portal','zbl_stat')       || 'DOKUM#03#04'
   |? _stat=exec('cancelled_erp','zbl_stat')          || 'DOKUM#03#05'
   |? _stat=exec('abandoned','zbl_stat')              || 'DOKUM#03#06'
   || ''
   ?}
||
   {? _stat=exec('ready_to_download','zbl_stat')      || 'DOKUM#04#01'
   |? _stat=exec('ready_to_registration','zbl_stat')  || 'DOKUM#04#02'
   |? _stat=exec('registration_failed','zbl_stat')    || 'DOKUM#04#03'
   |? _stat=exec('registered','zbl_stat')             || 'DOKUM#04#04'
   |? _stat=exec('cancelled_portal','zbl_stat')       || 'DOKUM#04#05'
   |? _stat=exec('cancelled_erp','zbl_stat')          || 'DOKUM#04#06'
   |? _stat=exec('abandoned','zbl_stat')              || 'DOKUM#04#07'
   |? _stat=exec('during_registration','zbl_stat')    || 'DOKUM#04#08'
   |? _stat=exec('contact','zbl_stat')                || 'DOKUM#04#09'
   || ''
   ?}
?}


\typysp_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [20.42]
:: OPIS: Typy dokumentów sprzedaży lub zakupu związanych z Businesslink
::   WE: [_a] S - dokumenty sprzedaży (domyślnie), Z - dokumenty zakupu
::----------------------------------------------------------------------------------------------------------------------
_rodz:='S';
{? var_pres('_a')=type_of('') & (_a='S' | _a='Z')
|| _rodz:=_a
?};

{? _rodz='Z'
|| _tyt:='Typy dokumentów zakupu'@
|| _tyt:='Typy dokumentów sprzedaży'@
?};
_wer_td:=TYPYSP.mk_sel(_tyt,,,,,,,,'U');
TYPYSP.win_fld(_wer_td,,'T',,,10,,,'Typ'@);
TYPYSP.win_fld(_wer_td,,'NAZ',,,30,,,'Nazwa'@);
_fb:=$('exec(\'typysp_wyb\',\'zbl_dok\',\''+_rodz+'\')');
TYPYSP.win_act(_wer_td,0,'Formuła','&Ustaw'@@,,,_fb,,1,,,,'U');
TYPYSP.win_act(_wer_td,1,'Formuła','&Ustaw'@@,,,_fb,,1,,,,'U');
{? _rodz='S'
||
   _fb:="params_exec('select','dp_wydrukow','TD')";
   _fbg:="params_exec('dpw_grupa','dp_wydrukow','TD')";
   TYPYSP.win_act(_wer_td,,'Formuła','Domyślne parametry &wydruków'@@,,,_fb,,,1,_fbg,,'W')
?};

_win0:=TYPYSP.win_sel('?');
TYPYSP.cntx_psh();

TYPYSP.index('BL1');
{? _rodz='Z'
|| TYPYSP.prefix('T','T')
|| TYPYSP.prefix('T','N')
?};
TYPYSP.win_sel(_wer_td);
TYPYSP.select();

TYPYSP.cntx_pop();
TYPYSP.win_sel(_win0);

~~


\map_ind
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Mapowanie indeksów
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_exec('bl_act','zbl_dok','MAP',DOKUM.ref());
DOKUM.get();
{? DOKUM.KH=null()
||
   FUN.info('W dokumncie nie podano kontrahenta. Mapowanie indeksów jest niedostępne.'@)
||
   {? var_pres('__Map')<>type_of(FIRMA) ||  __Map:=exec('map_tab','edi_wspolne') ?};
   exec('FindAndGet','#table',KH,DOKUM.KH,,"
:: okno mapowania
      exec('khvz_kh','kontrahent');
      exec('kh_dod_ini','kontrahent',KH.ref(),1);
      _act:=MKODK.actions('WERKHALL',{? KH_DOD.EFAKTURA='B' || '' || 'O' ?},'P');
      HELP.KODK:='bl';
      POMOC.RODZ:='';
      MKODK.win_sel('WERKHALL');
      MKODK.win_patt('REDKHALL');
      MKODK.hdr_sel();
      MKODK.hdr_sel({? KHVZ.KH || ': %1'[KHVZ.KH().SKR] || '' ?});
      MKODK.f_clear();
      exec('zakres','kody_kresk',0);
:: DRO_TODO_AWI: (bl):   : -  Jest jakiś problem z dodawaniem materiału w słowniku. Nie wiedzieć czemu jest transakcja
:: DRO_TODO_AWI: (bl):   :    w trakcie redagowania indeksów kontrahenta.
      MKODK.select();
      MKODK.actions('WERKHALL',,_act);
:: sprawdzenie
      MKODK.cntx_psh();
      MKODK.prefix();
      MKODK.f_set(,'join :_a using(MKODK.KTM,:_a.KTM)','MKODK.KH=:_b and MKODK.M is null',__Map,KH.ref());
      {? MKODK.f_first()
      ||
         FUN.info('Pozostały kody towarowe kontrahenta z nieprzypisanym indeksem.'@)
      ?};
      MKODK.f_clear();
      MKODK.cntx_pop()
   ");
   VAR_DEL.delete('__Map')
?}


\bl_act_anu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Dokument Businesslink - Anuluj w systemie
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;

exec('FindAndGet','#table',DOKUM,_dokum,,"
   {? @.DOKUM.BL_STAT<>exec('cancelled_portal','zbl_stat')
   ||
      FUN.info('Dokument Businesslink nie jest anulowany.\nAnulowanie dokumentu w systemie niedostępne.'@);
      0
   ||
      {? FUN.ask('Czy dokument w systemie został anulowany?'@)
      ||
         @.DOKUM.BL_STAT:=exec('cancelled_erp','zbl_stat');
         @.DOKUM.put()
      ?}
   ?}
")


\bl_act_pdw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Dokument Businesslink - Przygotuj do wysyłki
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b=1;

_result:=0;

_continue:=1;


DOKUM.cntx_psh();
DOKUM.prefix();
{? ~DOKUM.seek(_dokum)
||
   FUN.info('Nie znaleziono dokumentu Businesslink.'@)
?};

:: _z_dok - 1 - utworzony z DOK, 2 - utworzony w DOK i dodany do FAKS
:: _z_faks - utworzony z FAKS
:: _z_zdnag - utworzony z ZD_NAG
_z_faks:=_z_dok:=_z_zdnag:=0;
{? DOKUM.REFSQL<>'' & ref_tab(DOKUM.REFSQL)=FAKS
|| _z_faks:=1;
   {? var_pres('DOKR',DOKUM)>0 & DOKUM.DOKR<>''
   || _z_dok:=2; _z_faks:=0
   ?}
|? DOKUM.REFSQL<>'' & ref_tab(DOKUM.REFSQL)=DOK
|| _z_dok:=1
|? DOKUM.REFSQL<>'' & ref_tab(DOKUM.REFSQL)=ZD_NAG
|| _z_zdnag:=1
?};

{? ~_z_faks & ~_z_dok & ~_z_zdnag || _continue:=0 ?};

{? _continue>0
||
   _make_pdf:=_make_ufd:=_make_ksef:=0;
   _kh:=@.DOKUM.KH;
   _sym:='';
   {? _z_faks
   ||
      {? var_pres('__kom')>0
      ||
         __sect_beg:=1;
         _komroot:="
            {? ~__sect_beg || __kom.sect_beg(_a); __sect_beg:=1 ?};
            {? _b | __kom.find_msg(_a) || __kom.set_root(_a) || __kom.sect_beg(_a) ?}
         ";
         _komroot(FAKS.SYM,1)
      ?};
      _info:="{? var_pres('__kom')>0 || __kom.add(_a,7) || FUN.info(_a) ?}";
      _kh:=exec('kh_dokum','zbl_dok',@.DOKUM.REFSQL);
      _typysp:=null();
      _dw:=date(0,0,0);
      _where:='';
      _mansksef:='';
      _faks_buf:=exec('FindAndGet','#table',FAKS,@.DOKUM.REFSQL,,"
         _faks_buf:=obj_new('DW','T','SYM','WHERE','MANSKSEF');
         _faks_buf.DW:=FAKS.DW;
         _faks_buf.T:=FAKS.T;
         _faks_buf.SYM:=FAKS.SYM;
         _faks_buf.WHERE:=FAKS.WHERE;
         _faks_buf.MANSKSEF:=FAKS.MANSKSEF;
         _faks_buf
      ",~~);
      {? type_of(_faks_buf)<>type_of(~~)
      ||
         _dw:=_faks_buf.DW;
         _typysp:=_faks_buf.T;
         _sym:=_faks_buf.SYM;
         _where:=_faks_buf.WHERE;
         _mansksef:=_faks_buf.MANSKSEF
      ?};
      {? _kh=null()
      ||
         _info('Nie podano kontrahenta w dokumencie.'@);
         _continue:=0

      |? _typysp=null()
      ||
         _info('Nie określono typu dokumentu.'@);
         _continue:=0
      ||
         _make_pdf:=exec('czy_edokument_bl_pdf','zbl_dok',_kh,_typysp,_dw,_where,_mansksef);
         _make_ufd:=exec('czy_edokument_bl_ufd','zbl_dok',_kh,_typysp,_dw,_where);
         _make_ksef:=exec('czy_edokument_bl_ksef','zbl_dok',_kh,_typysp,_dw,_where,_mansksef)
      ?}
   |? _z_dok
   || {? var_pres('__kom')>0
      ||
         __sect_beg:=1;
         _komroot:="
            {? ~__sect_beg || __kom.sect_beg(_a); __sect_beg:=1 ?};
            {? _b | __kom.find_msg(_a) || __kom.set_root(_a) || __kom.sect_beg(_a) ?}
         ";
         _komroot(DOK.NK,1)
      ?};
      _info:="{? var_pres('__kom')>0 || __kom.add(_a,7) || FUN.info(_a) ?}";
      _kh:=exec('kh_dokum_dks','zbl_dok',{? _z_dok=1 || @.DOKUM.REFSQL || @.DOKUM.DOKR ?});
      _dw:=date(0,0,0);
      _dok_buf:=exec('FindAndGet','#table',DOK,{? _z_dok=1 || @.DOKUM.REFSQL || @.DOKUM.DOKR ?},,"
         _dok_buf:=obj_new('DW','SYM');
         _dok_buf.DW:=DOK.DTO;
         _dok_buf.SYM:=DOK.NK;
         _dok_buf
      ",~~);
      {? type_of(_dok_buf)<>type_of(~~)
      ||
         _dw:=_dok_buf.DW;
         _sym:=_dok_buf.SYM
      ?};
      {? _kh=null()
      ||
         _info('Nie podano kontrahenta w dokumencie.');
         _continue:=0
      ||
         _make_pdf:=0;
         _make_ufd:=0;
         _make_ksef:=exec('czy_edokument_bl_ksef_dks','zbl_dok',_kh,_dw)
      ?}
   |? _z_zdnag
   || {? var_pres('__kom')>0
      || __sect_beg:=1;
         _komroot:="
            {? ~__sect_beg || __kom.sect_beg(_a); __sect_beg:=1 ?};
            {? _b | __kom.find_msg(_a) || __kom.set_root(_a) || __kom.sect_beg(_a) ?}
         ";
         _komroot(FAKS.SYM,1)
      ?};
      _info:="{? var_pres('__kom')>0 || __kom.add(_a,7) || FUN.info(_a) ?}";
      _kh:=exec('kh_dokum_zam','zbl_dok',@.DOKUM.REFSQL);
      _typyzam:=null();
      _dw:=date(0,0,0);
      _zdnag_buf:=exec('FindAndGet','#table',ZD_NAG,@.DOKUM.REFSQL,,"
         _zdnag_buf:=obj_new('DATA','T','SYM');
         _zdnag_buf.DATA:=ZD_NAG.DATA;
         _zdnag_buf.T:=ZD_NAG.T;
         _zdnag_buf.SYM:=ZD_NAG.SYM;
         _zdnag_buf
      ",~~);
      {? type_of(_zdnag_buf)<>type_of(~~)
      ||
         _dw:=_zdnag_buf.DATA;
         _typyzam:=_zdnag_buf.T;
         _sym:=_zdnag_buf.SYM
      ?};
      {? _kh=null()
      || _info('Nie podano kontrahenta w dokumencie.'@);
         _continue:=0
      |? _typyzam=null()
      || _info('Nie określono typu dokumentu.'@);
         _continue:=0
      || _make_pdf:=exec('czy_zam_edokument_bl_pdf','zbl_dok',_kh,_typyzam,_dw);
         _make_ufd:=exec('czy_zam_edokument_bl_ufd','zbl_dok',_kh,_typyzam,_dw)
      ?}

   ?};
   {? _continue>0 & ~_make_pdf & ~_make_ufd & ~_make_ksef
   ||
      _info('Dokument %1 nie jest Edokumentem. Opcja jest niedostępna.'@[
         {? _z_faks
         || exec('FindAndGet','#table',FAKS,@.DOKUM.REFSQL,,"FAKS.SYM",null())
         |? _z_zdnag
         || exec('FindAndGet','#table',ZD_NAG,@.DOKUM.REFSQL,,"ZD_NAG.SYM",null())
         || exec('FindAndGet','#table',DOK,{? _z_dok=2 || @.DOKUM.DOKR || @.DOKUM.REFSQL ?},,"DOK.NK",'')
         ?}]);
      _continue:=0
   ?}
?};
{? _continue>0
||
   _set_ready_to_send:=0;
   {? _z_zdnag
   || _stat_rej:=exec('FindAndGet','#table',ZD_NAG,DOKUM.REFSQL,,"ZD_NAG.STAT_REJ",'')
   || _stat_rej:=exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"FAKS.STAT_REJ",'')
   ?};
   {? _z_faks
   || _p100360:=exec('get','#params',100360);
      {? _p100360='Po utworzeniu PDF'
      ||
         _set_ready_to_send:=_stat_rej='T'

      |? _p100360='Po księgowaniu'
      ||
         _set_ready_to_send:=exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"FAKS.ZAK='T'",0)
      ?}
   || _set_ready_to_send:=1
   ?};

   _gen_dok:=~exec('is_redy_to_send','zbl_dok',DOKUM.ref());

   {? DOKUM.BL_STAT<>'' & DOKUM.BL_STAT=exec('validation_failed','zbl_stat')
   ||
      {? _z_zdnag
      || _info('Dokument ma status KSeF "%1". Należy poprawić dokument i ponownie utworzyć e-dokument.'@[
            exec('validation_failed','zbl_stat')])
      || _info('Dokument ma status KSeF "%1". Należy poprawić dokument sprzedaży i ponownie utworzyć e-dokument.'@[
            exec('validation_failed','zbl_stat')])
      ?};
      _continue:=0

   |? DOKUM.BL_STAT<>'' & DOKUM.BL_STAT=exec('waiting_auth','zbl_stat')
   ||
      _info('Należy autoryzować e-dokument "%1".'@[_sym],);
      _continue:=0

   |? DOKUM.BL_STAT<>'' & DOKUM.BL_STAT<>exec('before_send','zbl_stat')
   ||
      _info('Opcja dostępna dla e-dokumentu o statusie "%1". '
         'Obecny status to "%2".'@[exec('before_send','zbl_stat'),DOKUM.BL_STAT]);
      _continue:=0

   |? _gen_dok=0
   ||
      {? _set_ready_to_send
      ||
         {? _sza | FUN.ask('Czy zmienić status e-dokumentu na "%1"?'@[exec('ready_to_send','zbl_stat')])
         ||
            DOKUM.BL_STAT:=exec('ready_to_send','zbl_stat');
            DOKUM.BL_TERM:=exec('bl_term','zbl_dok');
            DOKUM.put()
         ?}
      ||
         _continue:=0;
         {? _stat_rej='T' || _info('Dokument oczekuje na zaksięgowanie.'@)
         |? _stat_rej='A' || _info('Dokument został anulowany. Należy zaniechać obsługi dokumentu.'@)
         || _info('Dokument oczekuje na akceptację'@)
         ?}
      ?};
      ~~
   ||
      _record:={? _z_faks
               || exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,,null())
               |? _z_zdnag
               || exec('FindAndGet','#table',ZD_NAG,DOKUM.REFSQL,,,null())
               || exec('FindAndGet','#table',DOK,{? _z_dok=1 || DOKUM.REFSQL || DOKUM.DOKR ?},,,null())
               ?};
      _edi_result_ufd:='';
      _edi_result_ksef:='';
      _table:={? _z_faks || FAKS |? _z_zdnag || ZD_NAG || DOK ?};
      _table.cntx_psh();
      _table.use(ref_name(_record));
      _table.prefix();
      {? ~_table.seek(_record)
      ||
         _info('Nie znaleziono dokumentu.'@);
         _continue:=0

      |? (_z_faks & FAKS.STAT_REJ<>'T') | (_z_zdnag & ZD_NAG.STAT_REJ<>'T')
      ||
:: Dokument niezaakceptowany jest pomijany
         _continue:=0
      ||
         _kh_odb:=exec('kh_odb_dokum','zbl_dok',{? _z_dok=2 || DOKUM.DOKR || DOKUM.REFSQL ?});
         {? _make_pdf
         ||
:: Edokument - tylko pdf
            {? DOKUM.BL='N'
            ||
               DOKUM.BL:='T';
               DOKUM.BL_TYPE:=exec('bl_type','zbl_dok',_table.ref());
               DOKUM.BL_STAT:=
                  {? _set_ready_to_send || exec('ready_to_send','zbl_stat') || exec('before_send','zbl_stat') ?};
               DOKUM.BL_LOG:=null();
               DOKUM.KH:=_kh;
               DOKUM.KH_ODB:=_kh_odb;
               {? DOKUM.put()
               ||
                  exec('FindInSet','#table','DOKUMZ','KIND','',DOKUM.ref(),"DOKUMZ.BL_TYPE:='PDF'; DOKUMZ.put()",1)
               ?}
            ?}
         ||
:: Edokument - pdf, ufd, ksef
            {? DOKUM.BL='N'
            ||
               DOKUM.BL:='T';
               DOKUM.BL_TYPE:=exec('bl_type','zbl_dok',_table.ref());
               DOKUM.BL_STAT:=exec('before_send','zbl_stat');
               DOKUM.BL_LOG:=null();
               DOKUM.KH:=_kh;
               DOKUM.KH_ODB:=_kh_odb;
               DOKUM.put()
            ?};
            _edi:=exec('env','edi_wspolne');
            _edi.sza:=1;
            _edi.REF:=_record;
            {? ~_edi.init()
            ||
               _info('Nie powiodło się zainicjowanie środowiska.'@);
               _continue:=0
            ||
               _edi.context:=exec('context_obj_bl','edi_wspolne');
               _bl_type_ufd:=DOKUM.BL_TYPE;
               _bl_type_ksef:={? _z_dok=2
                              || 'KSeF_'+_bl_type_ufd+'_DKS'
                              || 'KSeF_'+_bl_type_ufd
                              ?};
               ISTDEF.cntx_psh();
               ISTDEF.index('BL_DATA');
               ISTDEF.prefix(__Firma,'ZWS','E',_bl_type_ufd,'N','T',);
               _istdef_ufd:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?};
               ISTDEF.prefix(__Firma,'ZWS','E',_bl_type_ksef,'N','T',);
               _istdef_ksef:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?};
               ISTDEF.cntx_pop();
               {? _make_ufd & _istdef_ufd=null()
               ||
                  _info('Nie ustalono definicji komunikatu typu %1.'@[_bl_type_ufd]);
                  _continue:=0

               |? _make_ksef=1 & _istdef_ksef=null()
               ||
                  _info('Nie ustalono definicji komunikatu typu %1.'@[_bl_type_ksef]);
                  _continue:=0
               ||
                  {? var_pres('__kom')>0 || _kom:=__kom ?};
                  {? var_pres('__kom_gr')>0 || _kom_gr:=__kom_gr ?};
                  {? var_pres('__kom_on')>0 || _kom_on:=__kom_on ?};
                  _edi.context.DOK2REJKOM:='A';
                  _edi.context.DOKUM:=_dokum;
                  {? _z_faks
                  || _edi.context.FAKS:=_record
                  |? _z_zdnag
                  || _edi.context.ZD_NAG:=_record
                  || _edi.context.DOK:=_record
                  ?};
                  _edi.context.SET_READY_TO_SEND:=_set_ready_to_send;
:: załącznik UFD
::                   dodatkowy zapis w dzienniku inforumujący o starcie odczytu lub zapisu edi
                  _log_ufd:=null();
                  {? _make_ufd
                  ||
                     _dzien_a:=exec('dzien_add','edi_wspolne',_istdef_ufd,'W',date(),time(),OPERATOR.USER,'LOG',''
                        ,null(),'Start','',,,'B');
                     DOKUM.cntx_psh();
                     _edi.context.BL_TYPE:=_bl_type_ufd;
                     _edi.context.SCHEMA:=exec('FindAndGet','#table',ISTDEF,_istdef_ufd,,"ISTDEF.XMLNS",'');
                     exec('edi_write','edi_wspolne',_istdef_ufd,_record);
                     DOKUM.cntx_pop();
::                      (_out.LOG) - zapisanie na wyjściu komunikatów wygenerowanych podczas działania czynności
                     _log_ufd:=_edi.logSave(_dzien_a)
                  ?};
:: załącznik KSeF
                  _log_ksef:=null();
                  {? _make_ksef=1 & DOKUM.get()
                  ||
                     _Pliki:=_edi.PLIKI;
                     _loop:=_Pliki.first();
                     {!
                     |? _loop
                     |!
                        _loop:=_Pliki.del()
                     !};
::                      dodatkowy zapis w dzienniku inforumujący o starcie odczytu lub zapisu edi
                     _dzien_a:=exec('dzien_add','edi_wspolne',_istdef_ksef,'W',date(),time(),OPERATOR.USER,'LOG',''
                        ,null(),'Start','',,,'B');
                     DOKUM.cntx_psh();
                     _edi.context.BL_TYPE:=_bl_type_ksef;
                     _edi.context.SCHEMA:=exec('FindAndGet','#table',ISTDEF,_istdef_ksef,,"ISTDEF.XMLNS",'');
                     exec('edi_write','edi_wspolne',_istdef_ksef,_record);
                     DOKUM.cntx_pop();
::                      (_out.LOG) - zapisanie na wyjściu komunikatów wygenerowanych podczas działania czynności
                     _log_ksef:=_edi.logSave(_dzien_a)
                  ?};
:: Log dokumentu Businesslink
                  {? DOKUM.get()
                  ||
                     _dokum_log:=fopen(DOKUM.BL_LOG,'ua',,,1);
                     {? _dokum_log.is_open()
                     ||
                        {? _log_ufd
                        ||
                           _log:=fopen(_log_ufd,'ur',,,1);
                           {? _log.is_open()
                           ||
                              fwrite(_dokum_log,'\nStart: %1\n'[_bl_type_ufd]);
                              {!
                              |? (_txt:=fread(_log))<>'\n'
                              |!
                                 fwrite(_dokum_log,_txt)
                              !}
                           ?};
                           obj_del(_log)
                        ?};
                        {? _log_ksef
                        ||
                           _log:=fopen(_log_ksef,'ur',,,1);
                           {? _log.is_open()
                           ||
                              fwrite(_dokum_log,'\nStart: %1\n'[_bl_type_ksef]);
                              {!
                              |? (_txt:=fread(_log))<>'\n'
                              |!
                                 fwrite(_dokum_log,_txt)
                              !}
                           ?}
                        ?};
                        {? _log_ufd | _log_ksef ||  DOKUM.bl_put('BL_LOG',_dokum_log,,,'log.txt') ?}
                     ?}
                  ?};
                  VAR_DEL.delete('__kom','__kom_gr','__kom_on');
                  {? var_pres('_kom')>0 || __kom:=_kom ?};
                  {? var_pres('_kom_gr')>0 || __kom_gr:=_kom_gr ?};
                  {? var_pres('_kom_on')>0 || __kom_on:=_kom_on ?}
               ?}
            ?}
         ?}
      ?};
      _table.cntx_pop();

      {? _continue>0
      ||
         {? DOKUM.get()=0 | DOKUM.BL_STAT<>exec('ready_to_send','zbl_stat')
         ||
            {? (_make_pdf | _edi.result='OK') & _set_ready_to_send=0
            ||
               _info('E-dokument oczekuje na zaksięgowanie.'@);
               _continue:=0

            |? exec('is_redy_to_send','zbl_dok',DOKUM.ref())
            ||
               _info('Przygotowanie do wysłania e-dokumentu nie powiodło się.'@);
               _continue:=0

            |? _make_ksef=2
            ||
               _info('Należy dokończyć wystawienie e-dokumentu z dokumentu księgowego.'@);
               _continue:=0
            ?}
         ?}
      ?}
   ?}
?};
DOKUM.cntx_pop();

{? _continue>0 || _result:=1 || __kom_on:=1 ?};
_result


\bl_act_dzk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Dokument Businesslink - Rejestruj dokument zakupu
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b;

_result:=0;

_continue:=1;

DOKUM.cntx_psh();
DOKUM.prefix();
{? ~DOKUM.seek(_dokum)
||
   ~~
|? 3+DOKUM.BL_TYPE<>'INV' & DOKUM.BL_TYPE<>'DOC'
||
   FUN.info('Nie można zarejestrować dokumentu typu "%1" jako dokument zakupu.'@[DOKUM.BL_TYPE]);
   _continue:=0

|? DOKUM.REFSQL='' & ~_sza & ~FUN.ask('Czy zarejestrować dokument zakupu?'@)
||
   _continue:=0

||
   {? DOKUM.BL_STAT=exec('during_registration','zbl_stat')
   ||
      _res:=exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"
         {? FAKS.STAT_REJ='Z' | FAKS.STAT_REJ='T'
         ||
            DOKUM.BL_STAT:=exec('registered','zbl_stat');
            DOKUM.put()
         ||
            -1
         ?}
      ");
      {? _res=0
      ||
::       nie udało się zmienić statusu na registred
         _continue:=0

      |? _res=-1
      ||
::       Rejestracja dokumentu
         {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
         ||
            exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"
               _kor:=FAKS.T().KOR='T';
               _zal:=TYPYSP.ZAL='T';
               _wew:=TYPYSP.UE='T';
               _params:=exec('mp_run_a','#b__box');
               _params.ACT_UID:=
                  {? _kor || 'LZK_ZAK_DKOR' |? _zal || 'LZK_ZAK_DZAL' |? _wew || 'LZK_ZAK_DWEW' || 'LZK_ZAK_DZAK' ?};
               _params.AKCJA:='Popraw';
               _params.UIDREF:=FAKS.uidref();
               __PARSES.setVal('OddzialLogProd',FAKS.ODDZ);
               _args:=__PARSES.args('OkresRok');
               _args.OBSZAR:='LZK';
               _args.AR:=FAKS.DW~1;
               _args.AM:=FAKS.DW~2;
               __PARSES.setVal('OkresRok',_args);
:: dokument zakupu
               {? __PARSES.setEnv(_params.ACT_UID) || exec('mp_run','#b__box',_params) ?}
            ",'')
         ?}
      ?}

   |? DOKUM.BL_STAT=exec('registered','zbl_stat')
   ||
      _continue:=exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"
         {? FAKS.STAT_REJ='N'
         ||
            @.DOKUM.BL_STAT:=exec('during_registration','zbl_stat');
            @.DOKUM.put()
         ||
            FUN.info('Dokument jest zarejestrowany.'@)
         ?}
      ");
      _continue:=0

   |? DOKUM.BL_STAT<>exec('ready_to_registration','zbl_stat')
         &
      DOKUM.BL_STAT<>exec('registration_failed','zbl_stat')
   ||
      FUN.info('Nie można zarejestrować dokumentu o statusie "%1".'@[DOKUM.BL_STAT]);
      _continue:=0

   |? exec('czy_ksef','edi_fo_ufd',DOKUM.ref())=0
      & exec('czy_ufd','edi_fo_ufd',DOKUM.ref())=0
   ||
      {? exec('czy_pdf','edi_fo_ufd',DOKUM.ref())=0
      ||
         FUN.info('Nie dostarczono dokumentu przychodzącego Businesslink w formacie UFD lub PDF.'@@);
         _continue:=0
      ||
:: Ścieżka PDF
         _context:=obj_new('BL','DOKUM','ODDZ','TYPYSP','DW','KH','KH_ODB');
         _context.BL:='T';
         _context.DOKUM:=DOKUM.ref();
         _continue:=exec('dzk_manual','zbl_dok',_context);
         {? _continue>0
         ||
            _typsp:=_context.TYPYSP;
            _kor:=exec('FindAndGet','#table',TYPYSP,_typsp,,"TYPYSP.KOR='T'",0);
            _zal:=exec('FindAndGet','#table',TYPYSP,_typsp,,"TYPYSP.ZAL='T'",0);
            _wew:=exec('FindAndGet','#table',TYPYSP,_typsp,,"TYPYSP.UE='T'",0);
            _act_uid:=
               {? _kor || 'LZK_ZAK_DKOR' |? _zal || 'LZK_ZAK_DZAL' |? _wew || 'LZK_ZAK_DWEW' || 'LZK_ZAK_DZAK' ?};
            __PARSES.setVal('OddzialLogProd',_context.ODDZ);
            _args:=__PARSES.args('OkresRok');
            _args.OBSZAR:='LZK';
            _args.AR:=_context.DW~1;
            _args.AM:=_context.DW~2;
            __PARSES.setVal('OkresRok',_args);
            {? __PARSES.setEnv(_act_uid)
            ||
               _params:=exec('mp_run_a','#b__box');
               _params.ACT_UID:=_act_uid;
               _params.AKCJA:='Dołącz';
               _params.PROC_START:='T';
               _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
               exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TYPYSP',_typsp);
               _params.CONTEXT:=_context;
               exec('mp_run','#b__box',_params);
               {? DOKUM.KH=null()
               ||
                  DOKUM.KH:=exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"FAKS.KH",null());
                  DOKUM.put()
               ?};
               exec('dokum_pow_zapisz_refsql','zbl_dok',@.DOKUM.ref(),1)
            ?}
         ?}
      ?}
   ||
:: Ścieżka KSeF, UFD
::    Usnięcie wcześniejszego log'a dokumentu Businesslink
      DOKUM.BL_LOG:=null();
      DOKUM.put();
      _edi:=exec('env','edi_wspolne');
      _edi.sza:={? _sza || 1 || 2 ?};
      {? ~_edi.init()
      ||
         FUN.info('Nie powiodło się zainicjowanie środowiska.'@)
      ||
         _edi.context:=exec('context_obj_bl','edi_wspolne');
         _edi.context.DOKUM:=_dokum;
         _edi.context.OCR:=exec('czy_ufd_z_ocr','edi_fo_ufd',DOKUM.ref());
         _istdef:=exec('istdef','zbl_dok',_edi.context);
         {? _istdef=null()
         ||
            _edi.context.TYPE:='';
            FUN.info('Nie ustalono definicji komunikatu.'@);
            _continue:=0
         ||
::          dodatkowy zapis w dzienniku inforumujący o starcie odczytu lub zapisu edi
            _dzien_a:=exec('dzien_add','edi_wspolne',_istdef,'R',date(),time(),OPERATOR.USER,'LOG',''
               ,null(),'Start','',,,'B');
            DOKUM.cntx_psh();
            exec('edi_read','edi_wspolne',_istdef);
            DOKUM.cntx_pop();
::          wtyczka wdożeniowa - pobranie danych dodatkowych z przychodzącego dokumentu Businesslink
            {? DOKUM.get() || Plugin.run('BL_DOKUMXML_GET_DZK',_edi) ?};
::          (_out.LOG) - zapisanie na wyjściu komunikatów wygenerowanych podczas działania czynności
            _edi.logSave(_dzien_a);
::          aktualizacja log
            {? DOKUM.get() || DOKUM.bl_put('BL_LOG',_edi.LOG,,,'log.txt') ?};
            {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
            ||
               _faks:=exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,,null());
               _typsp:=exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"FAKS.T",null());
               _kor:=exec('FindAndGet','#table',TYPYSP,_typsp,,"TYPYSP.KOR='T'",0);
               _zal:=exec('FindAndGet','#table',TYPYSP,_typsp,,"TYPYSP.ZAL='T'",0);
               _wew:=exec('FindAndGet','#table',TYPYSP,_typsp,,"TYPYSP.UE='T'",0);
               _params:=exec('mp_run_a','#b__box');
               _params.ACT_UID:=
                  {? _kor || 'LZK_ZAK_DKOR' |? _zal || 'LZK_ZAK_DZAL' |? _wew || 'LZK_ZAK_DWEW' || 'LZK_ZAK_DZAK' ?};
               _params.AKCJA:='Businesslink';
               _params.PROC_START:='T';
               _params.QUIET:='T';
               _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
               _akr:={? _kor || 'KORZ' |? _zal || 'FAKS' |? _wew || 'ZAK' || 'ZAK' ?};
               exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,_akr,_faks);
               exec('mp_run','#b__box',_params)
            ?}
         ?}
      ?}
   ?}
?};
DOKUM.cntx_pop();

{? _continue>0
||
   _result:=1
?};
_result


\dzk_manual
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Dokument Businesslink - Rejestruj dokument zakupu - ręcznie
::   WE: _a - context
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_context:=_a;
_result:=1;
:: wartości początkowe
FAKS.ODDZ:=ST.ODDZ;
{? ST.ODDZ='' & BPMN.B_DOMAIN().SYMBOL='ZBL'
||
   _oddz:=exec('get','#params',100362);
   FAKS.ODDZ:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,_oddz,"ODDZ.KOD",1,,'')
?};
ODDZ.NAZ:={? FAKS.ODDZ='' || '' || ODDZ.NAZ:=exec('FindInSet','#table','ODDZ','KOD2',FAKS.ODDZ,,"ODDZ.NAZ",1,,'') ?};
FAKS.DW:=DOKUM.DATA;
FAKS.T:=null();
FAKS.KH:=DOKUM.KH;
FAKS.ID:='';
FKOR.DF:=date(0,0,0);
:: typy dokumentów
TYPYSP.cntx_psh();
TYPYSP.fld_fml('ZAK','BLANK',"'T'");
TYPYSP.fld_fml('BL','BLANK',"'T'");
TYPYSP.win_dict('WYST');
TYPYSP.actions('WYST','W');
_ff:="";
FAKS.cntx_psh();
:: oddział
FAKS.fld_fml('ODDZ','BEFORE_EDIT',{? BPMN.B_DOMAIN().SYMBOL='LZK' || "0" || "1" ?});
_ff:="
   _result:=FAKS.ODDZ;
   ODDZ.prefix();
   ODDZ.f_clear();
   ODDZ.f_set('KOD,NAZ'
      ,'join USERS_UP'
      ,'USERS_UP.USERS=:_a and USERS_UP.AKR=''ODDZ'''
      ,OPERATOR.USER);

   ODDZ.win_sel('SEL');
   {? ODDZ.select()
   || _result:=ODDZ.KOD
   ?};
   ODDZ.f_clear();
   _result
";
FAKS.fld_fml('ODDZ','F3',_ff);
_ff:="
   FAKS.ODDZ:=exec('FindInSet','#table','ODDZ','KOD2',FAKS.ODDZ,,\"ODDZ.KOD\",1,,'');
   ODDZ.NAZ:=exec('FindInSet','#table','ODDZ','KOD2',FAKS.ODDZ,,\"ODDZ.NAZ\",1,,'')
";
FAKS.fld_fml('ODDZ','AFTER_EDIT',_ff);
:: data wystawienia
_ff:="";
FAKS.fld_fml('DW','BEFORE_EDIT',_ff);
FAKS.fld_fml('DW','AFTER_EDIT',_ff);
:: kontrahent
FAKS.fld_fml('KH','BEFORE_EDIT',_ff);
_ff:="{? FAKS.KH = null() || FAKS.KH_ODB:=null() ?}; 1";
FAKS.fld_fml('KH','AFTER_EDIT',_ff);
:: wystawca
_ff:="";
FAKS.fld_fml('KH_ODB','PATTERN',_ff);
_ff:="{? FAKS.KH || 1 || 0 ?}";
FAKS.fld_fml('KH_ODB','BEFORE_EDIT',_ff);
_ff:="";
FAKS.fld_fml('KH_ODB','AFTER_EDIT',_ff);
KH_ODB.fld_fml('KH','BLANK',"FAKS.KH");
::
FAKS.win_edit('BL');
{? ~FAKS.edit("__CHK.record(FAKS,,'ODDZ','T','DW','KH')")
||
   _result:=0
||
   _context.ODDZ:=FAKS.ODDZ;
   _context.TYPYSP:=FAKS.T;
   _context.DW:=FAKS.DW;
   _context.KH:=FAKS.KH;
   _context.KH_ODB:=FAKS.KH_ODB
?};
TYPYSP.fld_fml('ZAK','BLANK',"*");
TYPYSP.fld_fml('BL','BLANK',"*");
TYPYSP.actions('WYST','');
FAKS.fld_fml('ODDZ','BEFORE_EDIT',"*");
FAKS.fld_fml('ODDZ','AFTER_EDIT',"*");
FAKS.fld_fml('DW','BEFORE_EDIT',"*");
FAKS.fld_fml('DW','AFTER_EDIT',"*");
FAKS.fld_fml('KH','BEFORE_EDIT',"*");
FAKS.fld_fml('KH','AFTER_EDIT',"*");
FAKS.fld_fml('KH_ODB','PATTERN',"*");
FAKS.fld_fml('KH_ODB','BEFORE_EDIT',"*");
FAKS.fld_fml('KH_ODB','AFTER_EDIT',"*");
KH_ODB.fld_fml('KH','BLANK',"*");
FAKS.cntx_pop();
TYPYSP.cntx_pop();
_result


\bl_act_dks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Dokument Businesslink - Rejestruj dokument księgowy
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::       _c - 0/1 - automatyczny
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b;
_aut:=_c;
_result:=0;
_continue:=1;
_OkresRok:=__PARSES.getVal('OkresRok');
VAR_DEL.delete('__ProcParam');
DOKUM.cntx_psh();
DOKUM.prefix();
{? ~DOKUM.seek(_dokum)
|| ~~
|? ~exec('chk_role','#b__box',OPERATOR.USER,'FKS_DKS_DARK')
|| {? ~_sza || FUN.info('Brak uprawnień do rejestracji dokumentu księgowego.'@) ?};
   _continue:=0
|| {? DOKUM.BL_STAT=exec('during_registration','zbl_stat')
   || _res:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"
         {? @.DOK.A='T'
         ||
            @.DOKUM.BL_STAT:=exec('registered','zbl_stat');
            @.DOKUM.put()
         ||
            -1
         ?}
      ");
      {? _res=0
      ||
::       nie udało się zmienić statusu na registred
         _continue:=0

      |? _res=-1
      ||
::       Rejestracja dokumentu
         {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
         || _dok:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,,null());
            _dok_uidref:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"@.DOK.uidref()",'');
            _ar:=SSTALE.AR; _ao:=SSTALE.AO;
            exec('mask2stale','dok_fks',4+(4-$_dok));
            exec('open_tabele','open_tab','F');
            _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:='FKS_DKS_DARK';
            _params.AKCJA:='Businesslink2';
            _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
            _params.UIDREF:=_dok_uidref;
            _res:=exec('isActiveProcOrProblems','#b__box',_dok_uidref);
            {? ~_res.isActive || _params.PROC_START:='T' ?};
            exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOK',_dok);
            exec('mp_run','#b__box',_params);
            {? exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"@.DOK.A",'')='T'
            || DOKUM.BL_STAT:=exec('registered','zbl_stat');
               DOKUM.put()
            ?};
            SSTALE.AR:=_ar; SSTALE.AO:=_ao;
            exec('open_tabele','open_tab','F')
         ?}
      |? type_of(_res)=0
      || DOKUM.BL_STAT:=exec('registration_failed','zbl_stat');
         DOKUM.put()
      ?}

   |? DOKUM.BL_STAT=exec('registered','zbl_stat')
   ||
      _continue:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"
         {? @.DOK.A='N'
         ||
            @.DOKUM.BL_STAT:=exec('during_registration','zbl_stat');
            @.DOKUM.put()
         ||
            FUN.info('Dokument księgowy jest już zarejestrowany.'@)
         ?}
      ");
      _continue:=0

   |? DOKUM.BL_STAT<>exec('ready_to_registration','zbl_stat')
         &
      DOKUM.BL_STAT<>exec('registration_failed','zbl_stat')
   ||
      FUN.info('Nie można zarejestrować dokumentu o statusie "%1".'@[DOKUM.BL_STAT]);
      _continue:=0

   |? exec('dokum_pow_pod_czy','zbl_dok')
   || FUN.info('Dokument ma już powiązane podrzędne dokumenty.\n'
               'Rejestracja jest dostępna dla dokumentów bez powiązań.'@)

   |?  exec('czy_ksef','edi_fo_ufd',DOKUM.ref())=0 & exec('czy_ufd','edi_fo_ufd',DOKUM.ref())=0
   ||
      {? exec('czy_pdf','edi_fo_ufd',DOKUM.ref())=0
      ||
         FUN.info('Nie dostarczono dokumentu przychodzącego Businesslink w formacie UFD lub PDF.'@@);
         _continue:=0
      ||
:: Ścieżka PDF
         _continue:=exec('dks_manual','zbl_dok',{? DOKUM.BL_TYPE='INV_CORR' || 'K' || 'F' ?});
         {? _continue>0
         || {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
            || _dok:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,,null());
               _dok_uidref:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"@.DOK.uidref()",'');
               _ar:=SSTALE.AR; _ao:=SSTALE.AO;
               exec('mask2stale','dok_fks',4+(4-$_dok));
               exec('open_tabele','open_tab','F');
               _params:=exec('mp_run_a','#b__box');
               _params.ACT_UID:='FKS_DKS_DARK';
               _params.AKCJA:='Businesslink2';
               _params.PROC_START:='T';
               _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
               _params.UIDREF:=_dok_uidref;
               exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOK',_dok);
               exec('mp_run','#b__box',_params);
               {? DOKUM.get() & exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"@.DOK.A",'')='T'
               || DOKUM.BL_STAT:=exec('registered','zbl_stat');
                  DOKUM.put()
               ?};
               SSTALE.AR:=_ar; SSTALE.AO:=_ao;
               exec('open_tabele','open_tab','F')
            ?}
         ?}
      ?}
   ||
:: Ścieżka KSeF, UFD
::    Usnięcie wcześniejszego log'a dokumentu Businesslink
      DOKUM.BL_LOG:=null();
      {? DOKUM.BL_TYPE='' || DOKUM.BL_TYPE:='INV' ?};
      DOKUM.put();
      _edi:=exec('env','edi_wspolne');
      {? ~_edi.init()
      ||
         FUN.info('Nie powiodło się zainicjowanie środowiska.'@)
      ||
         _edi.context:=exec('context_obj_bl','edi_wspolne');
         _edi.context.DOKUM:=_dokum;
         _istdef:=exec('istdef','zbl_dok',_edi.context,'FKS');
         {? _istdef=null()
         ||
            _edi.context.TYPE:='';
            FUN.info('Nie ustalono definicji komunikatu.'@);
            _continue:=0
         ||
::          dodatkowy zapis w dzienniku inforumujący o starcie odczytu lub zapisu edi
            _dzien_a:=exec('dzien_add','edi_wspolne',ISTDEF.ref(),'W',date(),time(),OPERATOR.USER,'LOG',''
               ,null(),'Start','',,,'B');
            {? ~_aut
            || _edi.sza:=0;
               DOKUM.cntx_psh();
               exec('edi_read','edi_wspolne',_istdef);
               DOKUM.cntx_pop();
               {? DOKUM.get() || DOKUM.bl_put('BL_LOG',_edi.LOG,,,'log.txt') ?};
               _edi.logSave(_dzien_a);
               {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
               || _dok:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,,null());
                  _dok_uidref:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"@.DOK.uidref()",'');
                  _ar:=SSTALE.AR; _ao:=SSTALE.AO;
                  exec('mask2stale','dok_fks',4+(4-$_dok));
                  exec('open_tabele','open_tab','F');
                  _params:=exec('mp_run_a','#b__box');
                  _params.ACT_UID:='FKS_DKS_DARK';
                  _params.AKCJA:='Businesslink2';
                  _params.PROC_START:='T';
                  _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
                  _params.UIDREF:=_dok_uidref;
                  exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOK',_dok);
                  exec('mp_run','#b__box',_params);
                  {? DOKUM.get() & exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"@.DOK.A",'')='T'
                  || DOKUM.BL_STAT:=exec('registered','zbl_stat');
                     DOKUM.put()
                  ?};
                  SSTALE.AR:=_ar; SSTALE.AO:=_ao;
                  exec('open_tabele','open_tab','F')
               ?}
            || _edi.sza:=1;
               DOKUM.cntx_psh();
               exec('edi_read','edi_wspolne',_istdef);
               DOKUM.cntx_pop();
               {? DOKUM.get() || Plugin.run('BL_DOKUMXML_GET_DKS',_edi) ?};
               {? DOKUM.get() || DOKUM.bl_put('BL_LOG',_edi.LOG,,,'log.txt') ?};
               _edi.logSave(_dzien_a);
               {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
               || _dok:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,,null());
                  _dok_uidref:=exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"@.DOK.uidref()",'');
                  _obj:=obj_new(1); _obj[1]:=obj_new('PARAMETR','VALUE');
                  _obj[1].PARAMETR:='DOK'; _obj[1].VALUE:=_dok;
                  exec('force_signal','#b__box',exec('get','#params',100372),,_obj);
                  exec('ntc_ksef_auto_dok','dok_fks1',_dok)
               ?}
            ?}
         ?}
      ?}
   ?}
?};
DOKUM.cntx_pop();
VAR_DEL.delete('__ProcParam');
__PARSES.setVal('OkresRok',_OkresRok);
{? _continue>0
||
   _result:=1
?};
_result


\bl_act_dob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Dokument Businesslink - Rejestruj fakturę w obiegu
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::       _c - 0/1 - automatyczny
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b;
_aut:=_c;
_result:=0;
_continue:=1;
DOKUM.cntx_psh();
DOKUM.prefix();
{? ~DOKUM.seek(_dokum)
|| ~~
|? ~exec('chk_role','#b__box',OPERATOR.USER,'OBG_FZO_DARK')
|| {? ~_sza || FUN.info('Brak uprawnień do rejestracji faktury w obiegu.'@) ?};
   _continue:=0
|| {? DOKUM.BL_STAT=exec('during_registration','zbl_stat')
   ||
      _res:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"
         _stat:=1+@.EDOKUM.B_PRELS;
         {? _stat<>'' & _stat<>'R'
         ||
            @.DOKUM.BL_STAT:=exec('registered','zbl_stat');
            @.DOKUM.put()
         ||
            -1
         ?}
      ");
      {? _res=0
      ||
::       nie udało się zmienić statusu na registred
         _continue:=0

      |? _res=-1
      ||
::       Rejestracja dokumentu

         {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
         || exec('ob_edit','dokum',1,'N')
         ?}
      |? type_of(_res)=0
      || DOKUM.BL_STAT:=exec('registration_failed','zbl_stat');
         DOKUM.put()
      ?}

   |? DOKUM.BL_STAT=exec('registered','zbl_stat')
   ||
      _continue:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"
         _stat:=1+@.EDOKUM.B_PRELS;
         {? _stat='R'
         ||
            @.DOKUM.BL_STAT:=exec('during_registration','zbl_stat');
            @.DOKUM.put()
         ||
            FUN.info('Faktura w obiegu jest już zarejestrowana.'@)
         ?}
      ");
      _continue:=0

   |? DOKUM.BL_STAT<>exec('ready_to_registration','zbl_stat')
         &
      DOKUM.BL_STAT<>exec('registration_failed','zbl_stat')
   ||
      FUN.info('Nie można zarejestrować dokumentu o statusie "%1".'@[DOKUM.BL_STAT]);
      _continue:=0

   |? exec('dokum_pow_pod_czy','zbl_dok')
   || FUN.info('Dokument ma już powiązane podrzędne dokumenty.\n'
               'Rejestracja jest dostępna dla dokumentów bez powiązań.'@)

   |? exec('czy_ksef','edi_fo_ufd',DOKUM.ref())=0 & exec('czy_ufd','edi_fo_ufd',DOKUM.ref())=0
   ||
      {? exec('czy_pdf','edi_fo_ufd',DOKUM.ref())=0 & DOKUM.BL_TYPE<>'DOC'
      ||
         FUN.info('Nie dostarczono dokumentu przychodzącego Businesslink w formacie UFD lub PDF.'@@);
         _continue:=0
      ||
:: Ścieżka PDF
         _continue:=exec('ob_edit','dokum',1,'N')
      ?}
   ||
:: Ścieżka KSEF, UFD
      _continue:=exec('ob_edit','dokum',1,{? _aut || 'T' || 'N' ?})
   ?}
?};
DOKUM.cntx_pop();
{? _continue>0
||
   _result:=1
?};
_result


\bl_act_wob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Dokument Businesslink - Rejestruj wniosek w obiegu
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::       _c - 0/1 - automatyczny
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b;
_aut:=_c;
_result:=0;
_continue:=1;
DOKUM.cntx_psh();
DOKUM.prefix();
{? ~DOKUM.seek(_dokum)
|| ~~
|? ~exec('chk_role','#b__box',OPERATOR.USER,'OBE_FAW_DARP')
|| {? ~_sza || FUN.info('Brak uprawnień do rejestracji wniosku w obiegu.'@) ?};
   _continue:=0
|| {? DOKUM.BL_STAT=exec('during_registration','zbl_stat')
   ||
      _res:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"
         _stat:=1+@.EDOKUM.B_PRELS;
         {? _stat<>'' & _stat<>'R'
         ||
            @.DOKUM.BL_STAT:=exec('registered','zbl_stat');
            @.DOKUM.put()
         ||
            -1
         ?}
      ");
      {? _res=0
      ||
::       nie udało się zmienić statusu na registred
         _continue:=0

      |? _res=-1
      ||
::       Rejestracja dokumentu

         {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
         || exec('ob_edit','dokum',2,'N')
         ?}
      |? type_of(_res)=0
      || DOKUM.BL_STAT:=exec('registration_failed','zbl_stat');
         DOKUM.put()
      ?}

   |? DOKUM.BL_STAT=exec('registered','zbl_stat')
   ||
      _continue:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"
         _stat:=1+@.EDOKUM.B_PRELS;
         {? _stat='R'
         ||
            @.DOKUM.BL_STAT:=exec('during_registration','zbl_stat');
            @.DOKUM.put()
         ||
            FUN.info('Wniosek w obiegu jest już zarejestrowana.'@)
         ?}
      ");
      _continue:=0

   |? DOKUM.BL_STAT<>exec('ready_to_registration','zbl_stat')
         &
      DOKUM.BL_STAT<>exec('registration_failed','zbl_stat')
   ||
      FUN.info('Nie można zarejestrować dokumentu o statusie "%1".'@[DOKUM.BL_STAT]);
      _continue:=0

   |? exec('dokum_pow_pod_czy','zbl_dok')
   || FUN.info('Dokument ma już powiązane podrzędne dokumenty.\n'
               'Rejestracja jest dostępna dla dokumentów bez powiązań.'@)

   |? exec('czy_ufd','edi_fo_ufd',DOKUM.ref())=0
   ||
      {? exec('czy_pdf','edi_fo_ufd',DOKUM.ref())=0 & DOKUM.BL_TYPE<>'DOC'
      ||
         FUN.info('Nie dostarczono dokumentu przychodzącego Businesslink w formacie UFD lub PDF.'@@);
         _continue:=0
      ||
:: Ścieżka PDF
         _continue:=exec('ob_edit','dokum',2,'N')
      ?}
   ||
:: Ścieżka UFD
      _continue:=exec('ob_edit','dokum',2,{? _aut || 'T' || 'N' ?})
   ?}
?};
DOKUM.cntx_pop();
{? _continue>0
||
   _result:=1
?};
_result


\bl_type
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Typ dokumentu Businesslink
::   WE: _a - reference dokumentu w merit
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_result:='';
{? ref_tab(_ref)=FAKS
||
   _result:=exec('FindAndGet','#table',FAKS,_ref,,"
      {? FAKS.T().KOR='N' || exec('bl_type_inv','zbl_dok')
      |? FAKS.T().KOR='T' | FAKS.T().KOR='Z' || exec('bl_type_inv_corr','zbl_dok')
      || ''
      ?}
   ",'')
|? ref_tab(_ref)=DOK
||
   _result:=exec('FindAndGet','#table',DOK,_ref,,"
      {? DOK.DOK_REJ().KOR='T'
      || exec('bl_type_inv_corr_dks','zbl_dok')
      || exec('bl_type_inv_dks','zbl_dok')
      ?}
    ",'')
|? ref_tab(_ref)=ZD_NAG
|| _result:=exec('FindAndGet','#table',ZD_NAG,_ref,,"
      exec('bl_type_ord_dzk','zbl_dok')
    ",'')
?};
_result


\bl_type_inv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Typ dokumentu Businesslink - INV
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'INV'


\bl_type_inv_corr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Typ dokumentu Businesslink - INV_CORR
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'INV_CORR'


\bl_type_inv_dks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Typ dokumentu Businesslink - INV_DKS
::----------------------------------------------------------------------------------------------------------------------
'INV_DKS'


\bl_type_inv_corr_dks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Typ dokumentu Businesslink - INV_CORR_DKS
::----------------------------------------------------------------------------------------------------------------------
'INV_CORR_DKS'


\go_to_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Dokumenty Businesslink - Idź do dokumentu
::   WE:
::   WY:
:: ~OST: INFORK
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.REFSQL='' || FUN.info('Dokument Businesslink nie ma odpowiednika w systemie.'@); return() ?};

_uidref:={? 3+DOKUM.REFSQL='fak'
         || exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"FAKS.uidref()",'')
         |? 3+DOKUM.REFSQL='ski'
         || exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"@.EDOKUM.uidref()",'')
         |? 3+DOKUM.REFSQL='dok'
         || exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"@.DOK.uidref()",'')
         |? 3+DOKUM.REFSQL='zdn'
         || exec('FindAndGet','#table',ZD_NAG,DOKUM.REFSQL,,"@.ZD_NAG.uidref()",'')
         |? 3+DOKUM.REFSQL='zkn'
         || exec('FindAndGet','#table',ZK_N,DOKUM.REFSQL,,"@.ZK_N.uidref()",'')
         || ''
         ?};

{? _uidref='' || FUN.info('Nie znaleziono odpowiednika dokumentu Businesslink w systemie.'@); return() ?};

_refname:=ref_name(_uidref);
{? BEER.NAWIG='Z' & 3+_refname='fak'
|| _fml:="FUN.info('Dokument wystawiono w okresie %1 w oddziale %2.\nNależy zmienić okres obrachunkowy, oddział, zakres lub filtr.'@
            [FAKS.DW$8,1+(5-FAKS.name())])";
   _jest:=0;
   {? FAKS.name()=_refname
   || FAKS.f_rfresh();
      _jest:=FAKS.f_seek(_uidref)
   ?};
   {? _jest
   || win_activate('WER')
   || exec('FindAndGet','#table',FAKS,_uidref,,_fml)
   ?}
|? BEER.NAWIG='ZS' & 3+_refname='zkn'
|| _oddz:=1+(5-_refname);
   {? ZK_N.name()=_refname
   || {? ~ZK_N.seek(_uidref)
      || ZK_N.seek(_uidref,,1)
      ?};
      FILTER.ZK_N:=ZK_N.ref();
      win_activate('zamspr_zk_n')
   |? 1+(5-ZK_N.name())<>_oddz
   || FUN.info('Zamówienie wystawiono w oddziale %1.\nNależy zmienić oddział.'@[_oddz])
   |? _refname+2='__'
   || FUN.info('Zamówienie nie jest w archiwum.\nNależy zmienić zakres.'@)
   || exec('FindAndGet','#table',ZK_N,_uidref,,"
         FUN.info('Zamówienie jest w archiwum %1.\nNależy zmienić zakres.'@[$(ZK_N.DP~1)])
      ")
   ?}
|| _domain:={? DOKUM.RODZAJ_K='P'
            || {? 3+DOKUM.REFSQL='fak'
               || 'LZK'
               |? 3+DOKUM.REFSQL='ski'
               || {? exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"@.EDOKUM.TYP().TYPOBIEG().NAZWA",'')='Obieg faktur'
                  || 'OBG'
                  || 'OBE'
                  ?}
               |? 3+DOKUM.REFSQL='dok'
               || 'FKS'
               |? 3+DOKUM.REFSQL='zkn'
               || 'LSP'
               || ''
               ?}
            || {? 3+DOKUM.REFSQL='zdn'
               || 'LZK'
               || 'LSP'
               ?}
            ?};
   {? _domain='OBE'
   || FUN.info('Funkcja jest niedostępna dla dokumentu powiązanego z wnioskiem w obiegu.'@)
   |? _domain<>''
   || {? exec('interm','#system')
      || _params:=exec('obj_ntab_set','#array',,'LINK',_uidref);
         _link_interm:=link_uri(_params);
         cli_open_link(_link_interm)
      || _color:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_domain,,"B_DOMAIN.COLOR",1,,'');
         mb_fork(,'START_PROCES'
            ,'color='+exec('color_conv','#convert',_color)
            ,'LINK',_uidref)
      ?}
   || FUN.info('Funkcja jest niedostępna dla wybranego dokumentu.'@)
   ?}
?}


\bl_act_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Zwraca kod odpowidający nazwie akcji lub jeśli przekazano kod zwraca ten kod
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a='Rejestruj dokument zakupu' || 'DZK'
|? _a='Rejestruj dokument księgowy' || 'DKS'
|? _a='Rejestruj fakturę w obiegu' || 'DOB'
|? _a='Rejestruj wniosek w obiegu' || 'WOB'
|? _a='Rejestruj dokument w kontaktach' || 'DKH'
|? _a='Rejestruj zamówienie sprzedaży' || 'ORD'
|| _a
?}


\bl_act_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Formuła na wartość parametru ACT
::   WE: _a - aktualna domyślna akcja
::       _b - dla typu dokumentu Businesslink: '' - dowolny (domyślnie), INV, ORD
::   WY: tekst (jeśli ustawiana akcja) / ~~ (jeśli rezygnacja)
::----------------------------------------------------------------------------------------------------------------------
_act:={? var_pres('_a')=type_of('') || _a || '' ?};
_bltyp:={? var_pres('_b')=type_of('') || _b || '' ?};
_Tab:=tab_tmp(1,'ACT','STRING[50]','Nazwa akcji'@);
_Tab.ACT:=''; _Tab.add();
{? _bltyp='' | _bltyp='INV'
|| _Tab.ACT:='Rejestruj dokument zakupu'; _Tab.add();
   _Tab.ACT:='Rejestruj dokument księgowy'; _Tab.add();
   _Tab.ACT:='Rejestruj fakturę w obiegu'; _Tab.add();
   _Tab.ACT:='Rejestruj wniosek w obiegu'; _Tab.add()
?};
{? _bltyp='' | _bltyp='ORD'
|| _Tab.ACT:='Rejestruj zamówienie sprzedaży'; _Tab.add()
?};
 _Tab.ACT:='Rejestruj dokument w kontaktach'; _Tab.add();
_Tab.first();
_Tab.find_key(_act);
_wer:=_Tab.mk_sel('Akcje dla dokumentu Businesslink'@,,1,'act_bl_slo',,,,,'U');
_Tab.win_act(_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
_Tab.win_sel(_wer);
{? _Tab.select(,1,-1)
|| _Tab.ACT
|| ~~
?}


\czy_ocr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Czy jest plik do OCR?
::   WE: _a - DOKUM.ref()
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('FindInSet','#table','DOKUMZ','KOCR','N',_a,"1",1,'T',0)


\bl_dokum_link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Wiąże dokument Businesslink z dokumentem w systemie
::   WE: _a - wskazanie na dokument
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;

_edi:=exec('edi','edi_wspolne');
_context:=_edi.context;
{? _context.TYPE<>exec('context_type_bl','edi_wspolne') || return() ?};

_edi_ref:=_edi.REF;
_edi.REF:=_ref;

exec('FindAndGet','#table',DOKUM,_context.DOKUM,,"
   _edi:=_b;
   @.DOKUM.REFSQL:=_edi.REF;
   @.DOKUM.BL_STAT:=
      {? @.DOKUM.REFSQL<>'' & _edi.result='OK'
      || exec('during_registration','zbl_stat')
      || exec('registration_failed','zbl_stat')
      ?};
   @.DOKUM.DOKR:=
          {? @.DOKUM.REFSQL<>'' & _edi.result='OK' & (4+(@.DOKUM.REFSQL))='doku'
          || @.DOKUM.REFSQL
          || ''
          ?};
   @.DOKUM.put();
   @.DOKUM.bl_put('BL_LOG',_edi.LOG,,,'log.txt');
   exec('dokum_pow_zapisz_refsql','zbl_dok',@.DOKUM.ref(),1)
",,_edi);
_edi.REF:=_edi_ref


\bl_dokum_unlink
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Usuwa powiązanie dokumentu Businesslink z dokumentem w systemie
::   WE: _a - wskazanie na dokument
::       _b - [0/1] zmiana statusu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_b:={? var_pres('_b')=type_of(0) || _b || 1 ?};
exec('FindAndGet','#table',DOKUM,_a,,"
   @.DOKUM.prefix();
   @.DOKUM.REFSQL:='';
   @.DOKUM.DOKR:='';
   {? _b
   || @.DOKUM.BL_STAT:=exec('ready_to_registration','zbl_stat');
      @.DOKUM.OBKONETP:='';
      @.DOKUM.DG_UID:=''
   ?};
   _wyn:=@.DOKUM.put();
   exec('dokum_pow_zapisz_refsql','zbl_dok',@.DOKUM.ref(),1);
   _wyn
",,_b)


\bl_act_nie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Dokument Businesslink - Nie obsługuj
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;

exec('FindAndGet','#table',DOKUM,_dokum,,"
   _kier:=@.DOKUM.RODZAJ_K;
   _stan:=@.DOKUM.BL_STAT;
   _czy_ksef:=0;
   {? _kier='W' & @.DOKUM.REFSQL<>'' & ref_tab(@.DOKUM.REFSQL)=FAKS
   ||
      _czy_ksef:=exec('FindAndGet','#table',ref_tab(@.DOKUM.REFSQL),@.DOKUM.REFSQL,,\"
         STATKSEF<>'' & STATKSEF<>exec('ksef_donot_applay','zbl_stat')
      \",0)
   ?};
   {? _stan=exec('abandoned','zbl_stat')
   ||
      FUN.info('Już zrezygnowano z obsługi dokumentu.'@);
      0
   |? _czy_ksef
      | (_kier='P' & _stan<>exec('ready_to_download','zbl_stat') & _stan<>exec('ready_to_registration','zbl_stat')
         & _stan<>exec('registration_failed','zbl_stat'))
      | (_kier='W' & _stan<>exec('before_send','zbl_stat') & _stan<>exec('ready_to_send','zbl_stat'))
   ||
      FUN.info('Nie można już zrezygnować z obsługi dokumentu.'@);
      0
   ||
      {? FUN.ask('Czy zrezygnować z obsługi dokumentu?'@)
      ||
         @.DOKUM.BL_STAT:=exec('abandoned','zbl_stat');
         @.DOKUM.put()
      ?}
   ?}
")


\bl_act_tak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Dokument Businesslink - Obsługuj
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;

exec('FindAndGet','#table',DOKUM,_dokum,,"
   _kier:=@.DOKUM.RODZAJ_K;
   _stan:=@.DOKUM.BL_STAT;
   {? _kier='P' & _stan<>exec('abandoned','zbl_stat') & _stan<>exec('contact','zbl_stat')
   ||
      FUN.info('Dokument jest obsługiwany.'@);
      0
   || {? @.DOKUM.DG_UID=''
      || _pyt:='Czy przywrócić obsługę dokumentu?'@
      || {? 5+@.DOKUM.REFSQL='faktu'
         || _pyt:='Dokument jest załącznikiem dokumentu zakupu i zostanie usunięty z listy jego załączników.\n'
         |? 5+@.DOKUM.REFSQL='zknag'
         || _pyt:='Dokument jest załącznikiem zamówienia sprzedaży i zostanie usunięty z listy jego załączników.\n'
         || _pyt:=''
         ?};
         _pyt+='Czy usunąć powiązanie z dokumentem nadrzędnym i przywrócić obsługę dokumentu?'@
      ?};
      {? FUN.ask(_pyt)
      ||
         @.DOKUM.BL_STAT:=exec('ready_to_registration','zbl_stat');
         {? @.DOKUM.DG_UID<>''
         || @.DOKUM.DG_UID:='';
            @.DOKUM.REFSQL:=''
         ?};
         @.DOKUM.put()
      ?}
   ?}
")


\winAtta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Wyświetla załączniki DOKUM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_tab_acr:=2-!_tab;

VAR_DEL.delete('__CTR');
exec('ctr_zal_init','zbl_dok',_tab_acr);
DOKUM.cntx_psh();
_fb:=$('
   exec(''dokum_rfr'',''bloby'','':%1'',''%2'',''%3'',,0)
   '[
      __CTR.WIN_ID,__CTR.WIN_ACR,_tab_acr]
);
_win_grp:=DOKUM.grp_make('Załączniki'@,_fb,'bl_win_atta');
DOKUM.win_sel(_win_grp);
_fb:="
   __CTR.PODGLAD:=1;
   {? DOKUM.sel_size()=0
         &
      {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
         &
      DOKUM.RODZAJ_K='W'
   || __CTR.PODGLAD:=0
   ?};
   exec('bobs_dokumz','bloby')
";
_fa:="";
DOKUM.grp_ctr(_win_grp,_tab,__CTR.WIN_ACR,__CTR.WIN_ID,,,,,,5,_fb,_fa,,'maximized');
DOKUM.select();
DOKUM.cntx_pop()


\obg_manual
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Dokument Businesslink - Rejestruj dokument w obiegu - ręcznie
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys');
_dalej:=1; typobi:=1; _param:=Plugin.runnable('BL_OBG_001');
ROK_F.cntx_psh(); OKRO_F.cntx_psh();
_ar:=SSTALE.AR; _ao:=SSTALE.AO;
_ok:=exec('szuk_okr','okresy',DOKUM.DATA)<>null;
OKRO_F.index('FIRMA_NR');
OKRO_F.prefix(REF.FIRMA);
OKRO_F.win_sel('WER2');
{? OKRO_F.select(,1,25)
|| ROZNE.UT_OKROD:=OKRO_F.ref()
|| ROZNE.UT_OKROD:=null();
   _ok:=0
?};
{? ~_ok
|| FUN.info('Nie wybrano okresu.'@)
|? OKRO_F.ZAM='T'
|| FUN.info('Okres %1 jest zamknięty.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
   _ok:=0
|? OKRO_F.ZAM_CON='T'
|| FUN.info('Okres %1 jest zamknięty controllingowo.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
   _ok:=0
|| SSTALE.AO:=ROZNE.UT_OKROD;
   SSTALE.AR:=ROZNE.UT_OKROD().ROK
?};
{? ~_ok || ROK_F.cntx_pop(); OKRO_F.cntx_pop(); return(0) ?};
exec('open_tabele','open_tab','F');
EDOKUM.cntx_psh(); ETYPY.cntx_psh();
EDOKUM.index('DISP'); EDOKUM.prefix();
EDOKUM.clear();
EDOKUM.blank();
ETYPY.index('UNIK'); ETYPY.prefix();
OBIEGI.TYPOBIEG:=exec('bl_typ','obiegi',typobi);
OBIEGI.ODD:=null; OBIEGI.TYP:=null; OBIEGI.SL_KH:=''; EDOKUM.PLATNOSC:=null;
exec('tabkhini','kontrahent');
EDOKUM.win_edit('PAR');
ODD.f_clear();
OBIEGI.fld_fml('ODD','BEFORE_EDIT',"{? OPERATOR.DEPT<>null || 0 || 1 ?}");
OBIEGI.fld_fml('TYP','AFTER_EDIT',"{? OBIEGI.TYP=null
                                   || FUN.info('Wybierz typ faktury.'@);
                                      0
                                   |? OBIEGI.TYP().POZF='N'
                                   || FUN.info('Wybierz typ faktury zawierający pozycje.'@);
                                      0
                                   || OBIEGI.SL_KH:=OBIEGI.TYP().DOM_SL().SLU().NAZ;
                                      EDOKUM.PLATNOSC:=OBIEGI.TYP().PLATNOSC;
                                      1
                                   ?}");
OBIEGI.fld_fml('ODD','AFTER_EDIT',"{? OBIEGI.ODD=null
                                   || FUN.info('Wybierz jednostkę księgową.'@);
                                      0
                                   || 1
                                   ?}");
OBIEGI.fld_fml('SL_KH','AFTER_EDIT',"{? OBIEGI.SL_KH=''
                                     || FUN.info('Wybierz źródło pochodzenia.'@);
                                        0
                                     || 1
                                     ?}");
OBIEGI.fld_fml('ODD','BEFORE_DISPLAY',"{? OPERATOR.DEPT<>null || OBIEGI.ODD:=OPERATOR.DEPT || 1 ?}");
{? _param | EDOKUM.edit()
|| EDOKUM.FIRMA:=REF.FIRMA;
   EDOKUM.ROK_F:=SSTALE.AR;
   EDOKUM.TYP:=OBIEGI.TYP;
   EDOKUM.ODD:=OBIEGI.ODD;
   EDOKUM.DATAW:=date();
   EDOKUM.ALERTY:='T';
   OBIEGI.DEL_ETAT:='';
   EDOKUM.DOSTAWCA:=OPERATOR.USER().OSOBA;
   EDOKUM.USERS:=OPERATOR.USER;
   EDOKUM.UNIK_ID:=tm_stamp();
   EDOKUM.TYP();
   EDOKUM.TYPOBIEG:=EDOKUM.TYP().TYPOBIEG;
   EDOKUM.EDOKUM:=DOKUM.DOKUMI;
   {? _param || _dalej:=Plugin.run('BL_OBG_001',1) ?};
   {? _dalej=1 & EDOKUM.add()
   || exec('edokumz','dokum',EDOKUM.ref);
      {? ETYPY.AUT_ID='T' & EDOKUM.ID=''
      || VAR_DEL.delete('id_edok');
         exec('ustal_numer','obiegi',1,SSTALE.AR,1,0);
         EDOKUM.ID:={? var_pres('id_edok')>0 || id_edok || '' ?};
         _dalej:=EDOKUM.put()
      ?};
      zakres:=1;
      exec('dol_edokos','obiegi');
      {? EDOKUM.TYP().ATR_G1R>0
      || exec('edk_atr_dolacz','obiegi',0)
      ?}
   || FUN.info('Błąd podczas dodawania faktury w obiegu.'@);
      _dalej:=0
   ?}
|| _dalej:=0
?};
{? _dalej
|| DOKUM.BL_STAT:=exec('during_registration','zbl_stat');
   DOKUM.REFSQL:=$EDOKUM.ref();
   DOKUM.put()
|| DOKUM.BL_STAT:=exec('registration_failed','zbl_stat');
   DOKUM.put()
?};
OBIEGI.fld_fml('ODD','BEFORE_EDIT',"*");
OBIEGI.fld_fml('ODD','BEFORE_DISPLAY',"*");
OBIEGI.fld_fml('ODD','AFTER_EDIT',"*");
OBIEGI.fld_fml('TYP','AFTER_EDIT',"*");
OBIEGI.fld_fml('SL_KH','AFTER_EDIT',"*");
exec('tabkhdel','kontrahent');
VAR_DEL.delete('__PAR');
SSTALE.AR:=_ar; SSTALE.AO:=_ao;
exec('open_tabele','open_tab');
ETYPY.cntx_pop(); EDOKUM.cntx_pop();
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
_dalej


\dks_manual
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Dokument Businesslink - Rejestruj dokument w obiegu - ręcznie
::   WE: _a - F - faktura, K - korekta
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys');
VAR_DEL.delete('edi_dok');
_typ:={? var_pres('_a')=type_of('') || _a || '' ?};
_dalej:=1;
_param:=Plugin.runnable('BL_DKS_001');
{? _typ='' | 'FK'*_typ=0
||
   FUN.info('Nieprawidłowy parametr funkcji r_sg_dok.'@);
   return(-1)
?};

ROK_F.cntx_psh(); OKRO_F.cntx_psh();
_ar:=SSTALE.AR; _ao:=SSTALE.AO;
_ok:=exec('szuk_okr','okresy',DOKUM.DATA)<>null;
OKRO_F.index('FIRMA_NR');
OKRO_F.prefix(REF.FIRMA);
OKRO_F.win_sel('WER2');
{? OKRO_F.select(,1,25)
|| ROZNE.UT_OKROD:=OKRO_F.ref()
|| ROZNE.UT_OKROD:=null();
   _ok:=0
?};
{? ~_ok
|| FUN.info('Nie wybrano okresu.'@)
|? OKRO_F.ZAM='T'
|| FUN.info('Okres %1 jest zamknięty.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
   _ok:=0
|? OKRO_F.ZAM_CON='T'
|| FUN.info('Okres %1 jest zamknięty controllingowo.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
   _ok:=0
|| SSTALE.AO:=ROZNE.UT_OKROD;
   SSTALE.AR:=ROZNE.UT_OKROD().ROK;
   ROZNE.ROKKON:=SSTALE.AR
?};
{? ~_ok || ROK_F.cntx_pop(); OKRO_F.cntx_pop(); return(0) ?};
exec('open_tabele','open_tab','F');
DOK.cntx_psh(); VPOZ.cntx_psh(); REJ.cntx_psh();
DOK.index('REJ'); DOK.prefix();
VPOZ.index('VDOK'); VPOZ.prefix();
VPOZ.blank;
DOK.clear(); DOK.blank(1);
DOK.REJ:=null; DOK.DZ:=null; DOK.WYS:=null; DOK.DOK_REJ:=null;
VPOZ.GRVAT:=null; VPOZ.RVAT:=null; VPOZ.R:='';
DOK.D4:={? DOKUM.DATA_ODB<>date(0,0,0) || DOKUM.DATA_ODB || DOKUM.DATAKONT ?};
SLOSLU.SLSLU:='';
ODD.f_clear();
_odd:=DOK.ODD:=OPERATOR.DEPT;
edi_dok:=1;
{? BPMN.SYM_DOM='FKS' || exec('rodz_dok','edi_fo_ufd') ?};
exec('tabkhini','kontrahent');
DOK.win_edit('KSDKOB03');
DOK.fld_fml('REJ','BEFORE_EDIT',"OPERATOR.DEPT:=DOK.ODD; 1");
{? _param | DOK.edit({? _a='K' || "exec('chk_dok','edi_fo02','K')" || "exec('chk_dok','edi_fo02','F')" ?})
|| DOK.DR:=exec('data','#blank');
   DOK.KRAJ:=exec('kod_pl','slo_slu');
   DOK.JORG:=FINFO.NAROD;
   DOK.TR:='';
   DOK.ZAR:=exec('usr_zar','dok_fks');
   DOK.A:=DOK.ZK:=DOK.ZP:=DOK.WSK_XD:='N';
   DOK.DOKZRODL:='';
   exec('fl_decl','dekret_aut');
   exec('dek_decl','dekret_aut');
   exec('sd_decl','dekret_aut');
:: Wywołanie formuły na domyślne parametry
   {? _param || _dalej:=Plugin.run('BL_DKS_001',1) ?};
   exec('tagger_tip_nag','tagger','DK',DOKUM.KH,DOKUM.FIRMA,_typ);
   {? _dalej
   || DOK.use('doku'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
      DOK.cntx_psh();
      DOK.index('REJ');
      DOK.prefix(DOK.REJ);
      {? DOK.last() || _vnr:=DOK.NR+1 || _vnr:=1 ?};
      DOK.cntx_pop();
      DOK.NR:=_vnr;
      DOK.E_DOC:=DOKUM.DOKUMI;
      {? ~DOK.add()
      || FUN.info('Błąd podczas dodawania dokumentu księgowego'@);
        _dalej:=0
      ?}
   || FUN.info('Błąd podczas dodawania dokumentu księgowego'@)
   ?}
|| _dalej:=0
?};
{? _dalej
|| DOKUM.BL_STAT:=exec('during_registration','zbl_stat');
   DOKUM.REFSQL:=$DOK.ref();
   DOKUM.put()
|| DOKUM.BL_STAT:=exec('registration_failed','zbl_stat');
   DOKUM.put()
?};
exec('tabkhdel','kontrahent');
DOK.cntx_pop(); VPOZ.cntx_pop(); REJ.cntx_pop();
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
SSTALE.AR:=_ar; SSTALE.AO:=_ao;
OPERATOR.DEPT:=_odd;
exec('open_tabele','open_tab');
_dalej


\bl_term
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.51_112]
:: OPIS: Termin ustawienia załącznikowi statusu 'do wysłania'
::----------------------------------------------------------------------------------------------------------------------
time_ident(date(),time())


\bl_dokum_reg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Zmienia status dokumentu Businesslink po rejestracji faktury w systemie
::   WE: _a - wskazanie na dokument
::----------------------------------------------------------------------------------------------------------------------
exec('FindAndGet','#table',DOKUM,_a,,"
   @.DOKUM.prefix();
   @.DOKUM.BL_STAT:=exec('registered','zbl_stat');
   _wyn:=@.DOKUM.put()
")


\archive_ready
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Sprawdza czy status dokumentu uprawnia do jego archiwizacji
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
DOKUM.BL_STAT=exec('send','zbl_stat')
         |
DOKUM.BL_STAT=exec('cancelled_erp','zbl_stat')
         |
DOKUM.BL_STAT=exec('registered','zbl_stat')
         |
DOKUM.BL_STAT=exec('abandoned','zbl_stat')


\bl_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Dokument Businesslink - edycja brakujących danych wymaganych do rejestracji dokumentu zakupu na podstawie
::       pliku xml w formacie UFD
::   WE: _a - context
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_context:=_a;
_result:=1;
:: wartości początkowe
FAKS.ODDZ:=ST.ODDZ;
{? ST.ODDZ='' & BPMN.B_DOMAIN().SYMBOL='ZBL'
||
   _oddz:=exec('get','#params',100362);
   FAKS.ODDZ:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,_oddz,"ODDZ.KOD",1,,'')
?};
ODDZ.NAZ:={? FAKS.ODDZ='' || '' || ODDZ.NAZ:=exec('FindInSet','#table','ODDZ','KOD2',FAKS.ODDZ,,"ODDZ.NAZ",1,,'') ?};
:: typy dokumentów
_where:="TYPYSP.ZAK='T' and TYPYSP.BL='T'";
_where+={? _context.KOREKTA='' || "" |? _context.KOREKTA='T' || " and TYPYSP.KOR='T'" || " and TYPYSP.KOR='N'" ?};
TYPYSP.cntx_psh();
TYPYSP.prefix();
TYPYSP.f_set('T',,_where);
TYPYSP.win_dict('WYST');
TYPYSP.actions('WYST','W');
_ff:="";
FAKS.cntx_psh();
:: oddział
FAKS.fld_fml('ODDZ','BEFORE_EDIT',{? BPMN.B_DOMAIN().SYMBOL='LZK' || "0" || "1" ?});
_ff:="
   _result:=FAKS.ODDZ;
   ODDZ.prefix();
   ODDZ.f_clear();
   ODDZ.f_set('KOD,NAZ'
      ,'join USERS_UP'
      ,'USERS_UP.USERS=:_a and USERS_UP.AKR=''ODDZ'''
      ,OPERATOR.USER);

   ODDZ.win_sel('SEL');
   {? ODDZ.select()
   || _result:=ODDZ.KOD
   ?};
   ODDZ.f_clear();
   _result
";
FAKS.fld_fml('ODDZ','F3',_ff);
_ff:="
   FAKS.ODDZ:=exec('FindInSet','#table','ODDZ','KOD2',FAKS.ODDZ,,\"ODDZ.KOD\",1,,'');
   ODDZ.NAZ:=exec('FindInSet','#table','ODDZ','KOD2',FAKS.ODDZ,,\"ODDZ.NAZ\",1,,'')
";
FAKS.fld_fml('ODDZ','AFTER_EDIT',_ff);
:: typ dokumentu
_ff:="exec('bl_edit_efld_opt','zbl_dok'); 1";
FAKS.fld_fml('T','AFTER_EDIT',_ff);
:: data wystawienia
_ff:="";
FAKS.fld_fml('DW','BEFORE_EDIT',_ff);
FAKS.fld_fml('DW','AFTER_EDIT',_ff);
:: kontrahent
FAKS.fld_fml('KH','BEFORE_EDIT',_ff);
_ff:="{? FAKS.KH = null() || FAKS.KH_ODB:=null() ?}; 1";
FAKS.fld_fml('KH','AFTER_EDIT',_ff);
:: wystawca
_ff:="";
FAKS.fld_fml('KH_ODB','PATTERN',_ff);
_ff:="{? FAKS.KH || 1 || 0 ?}";
FAKS.fld_fml('KH_ODB','BEFORE_EDIT',_ff);
_ff:="";
FAKS.fld_fml('KH_ODB','AFTER_EDIT',_ff);
KH_ODB.fld_fml('KH','BLANK',"FAKS.KH");
:: data wystawienia dokumentu do korekty
FKOR.fld_fml('DF','BEFORE_EDIT',_ff);
FKOR.fld_fml('DF','AFTER_EDIT',_ff);

_win_akr:='BL_FIXML';
_win_red:=FAKS.mk_edit('Dane dokumentu'@,,'bl_fixml');
FAKS.win_etab(_win_red,'Dane podstawowe'@);
FAKS.win_ewin(_win_red,,_win_akr);
exec('ok_esc','#window',FAKS,_win_red,1);
FAKS.win_edit(_win_red);
{? DOKUM.DOKUMI
||
   FAKS.win_etab(_win_red,'Podgląd'@);
   FAKS.win_ewin(_win_red,DOKUM,'DOKUMI')
?};
_valid:="
   {? FAKS.T().KOR='N'
   ||
      __CHK.record(FAKS,,'ODDZ','T','DW','ID','KH')
   ||
      _chk:=__CHK.record(FAKS,,'ODDZ','T','DW','ID','KH','KOR');
      {? _chk='' || __CHK.record(FKOR,,'DF') || _chk ?}
   ?}
";
::FAKS.ODDZ:=_context.ODDZ;
FAKS.T:=_context.TYPYSP;
FAKS.ID:=_context.ID;
FAKS.DW:=_context.DW;
FAKS.KH:=_context.KH;
FAKS.KH_ODB:=_context.KH_ODB;
FAKS.CB:=_context.CB;
FAKS.KOR:=_context.KOR;
FKOR.DF:=_context.DF;
exec('bl_edit_efld_opt','zbl_dok');
params_set('bl_fixml','T');
{? ~FAKS.edit(_valid)
||
   _result:=0
||
   _context.ODDZ:=FAKS.ODDZ;
   _context.TYPYSP:=FAKS.T;
   _context.ID:=FAKS.ID;
   _context.DW:=FAKS.DW;
   _context.KH:=FAKS.KH;
   _context.KH_ODB:=FAKS.KH_ODB;
   _context.KOR:=FAKS.KOR;
   _context.DF:=FKOR.DF;
   _context.CB:=FAKS.CB
?};
TYPYSP.f_clear();
TYPYSP.fld_fml('ZAK','BLANK',"*");
TYPYSP.fld_fml('BL','BLANK',"*");
TYPYSP.actions('WYST','');
FAKS.fld_fml('ODDZ','BEFORE_EDIT',"*");
FAKS.fld_fml('T','AFTER_EDIT',"*");
FAKS.fld_fml('ODDZ','AFTER_EDIT',"*");
FAKS.fld_fml('DW','BEFORE_EDIT',"*");
FAKS.fld_fml('DW','AFTER_EDIT',"*");
FAKS.fld_fml('KH','BEFORE_EDIT',"*");
FAKS.fld_fml('KH','AFTER_EDIT',"*");
FAKS.fld_fml('KH_ODB','PATTERN',"*");
FAKS.fld_fml('KH_ODB','BEFORE_EDIT',"*");
FAKS.fld_fml('KH_ODB','AFTER_EDIT',"*");
KH_ODB.fld_fml('KH','BLANK',"*");
FKOR.fld_fml('DF','AFTER_EDIT',"*");
FAKS.cntx_pop();
TYPYSP.cntx_pop();
_result


\bl_edit_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Dokument Businesslink - edycja brakujących danych wymaganych do rejestracji dokumentu zakupu na podstawie
::       pliku xml w formacie UFD - ustawienie opcji pól
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_red:=FAKS.win_edit('?');
{? FAKS.T().KOR='N'
|| FAKS.efld_opt(_win_red,'enable=0',,'KOR');
   FAKS.efld_opt(_win_red,'enable=0',FKOR,'DF');
   FAKS.efld_opt(_win_red,'enable=1',,'CB')
||
   FAKS.efld_opt(_win_red,'enable=1',,'KOR');
   FAKS.efld_opt(_win_red,'enable=1',FKOR,'DF');
   FAKS.efld_opt(_win_red,'enable=0',,'CB')
?};
1


\bl_act_dkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Dokument Businesslink - Rejestruj dokument w kontaktach
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b;

_kkh_tp:=exec('kkh_typ','zbl_dok');

_result:=0;

_continue:=1;

DOKUM.cntx_psh();
DOKUM.prefix();
{? ~DOKUM.seek(_dokum)
||
   ~~

|? DOKUM.BL_STAT<>exec('ready_to_registration','zbl_stat') & DOKUM.BL_STAT<>exec('registration_failed','zbl_stat')
||
   FUN.info('Rejestracja w kontaktach dostępna dla dokumentów o statusach:'
      ' %1, %2.'@[exec('ready_to_registration','zbl_stat'),exec('registration_failed','zbl_stat')])
|? exec('dokum_pow_pod_czy','zbl_dok')
|| FUN.info('Dokument ma już powiązane podrzędne dokumenty.\n'
            'Rejestracja w kontaktach jest dostępna dla dokumentów bez powiązań.'@)
|? DOKUM.DOT=null() & _kkh_tp=null()
|| FUN.info(
            'Brak zdefiniowanego typu kontaktu dla dokumentu Businesslink.\n'
            'Należy uzupełnić parametr aplikacyjny %1.'@['100801']
         )
|? ~_sza & ~FUN.ask('Czy zarejestrować dokument w kontaktach?'@)
||
   ~~
||
   _continue:=0;
   _kkh_tp_zm:=0;
   {? DOKUM.DOT=null()
   ||
      _kkh_tp_zm:=1;
      DOKUM.DOT:=_kkh_tp;
      DOKUM.put()
   ?};
   {? DOKUM.KH=null() || exec('popraw','zbl_dok') ?};
   {? DOKUM.get() & DOKUM.KH & DOKUM.DOT
   ||
      DOKUM.BL_STAT:=exec('contact','zbl_stat');
      DOKUM.OPER:=exec('name','users');
      {? DOKUM.KR_OP='' || DOKUM.KR_OP:='Dokument businesslink %1'@[DOKUM.SYM_ZEW] ?};
      _continue:=DOKUM.put()
   ?};
   {? ~_continue & _kkh_tp_zm
   || DOKUM.DOT:=null();
      DOKUM.put()
   ?}
?};

{? _continue>0
||
   _result:=1
?};
DOKUM.cntx_pop();
_result


\popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Dokument Businesslink - popraw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KHVZ.KONTAKTY:='T';
KHVZ.KHVP:='dokum';
params_exec('doku_pop','dokum');
KHVZ.KONTAKTY:='';
KHVZ.KHVP:=''


\bl_kh_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Zmienia kontrahenta dokumentu Businesslink po rejestracji faktury w systemie
::   WE: _a - wskazanie na dokument
::       _b - KH.ref
::----------------------------------------------------------------------------------------------------------------------
{? _<2 || return(0) ?};
exec('FindAndGet','#table',DOKUM,_a,,"
   {? _b<>@.DOKUM.KH
   || @.DOKUM.prefix();
      @.DOKUM.KH:=_b;
      _wyn:=@.DOKUM.put()
   || _wyn:=0
   ?}
",null(),_b)


\is_redy_to_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Czy dokument jest gotowy do wysłania? Sprawdzamy czy ma wszystkie wymagane załączniki
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('FindAndGet','#table',DOKUM,_a,,"
   _z_faks:=_z_dok:=_z_zdnag:=0;
   {? @.DOKUM.REFSQL<>'' & ref_tab(@.DOKUM.REFSQL)=FAKS
   || _z_faks:=1;
      {? var_pres('DOKR',@.DOKUM)>0 & @.DOKUM.DOKR<>''
      || _z_dok:=2; _z_faks:=0
      ?}
   |? @.DOKUM.REFSQL<>'' & ref_tab(@.DOKUM.REFSQL)=DOK
   || _z_dok:=1
   |? @.DOKUM.REFSQL<>'' & ref_tab(@.DOKUM.REFSQL)=ZD_NAG
   || _z_zdnag:=1
   ?};

   _kh:=@.DOKUM.KH;
   {? _z_faks || _kh:=exec('kh_dokum','zbl_dok',@.DOKUM.REFSQL)
   |? _z_dok || _kh:=exec('kh_dokum_dks','zbl_dok',{? _z_dok=1 || @.DOKUM.REFSQL || @.DOKUM.DOKR ?})
   |? _z_zdnag || _kh:=exec('kh_dokum_zam','zbl_dok',@.DOKUM.REFSQL)
   ?};
   _tab:=null();
   _typysp:=null();
   _dw:=date(0,0,0);
   _where:='';
   _mansksef:='N';
   _typyzam:=null();
   {? _z_zdnag
   || _tab_buf:=exec('FindAndGet','#table',ZD_NAG,@.DOKUM.REFSQL,,\"
         _tab_buf:=obj_new('DATA','T','SYM');
         _tab_buf.DATA:=ZD_NAG.DATA;
         _tab_buf.T:=ZD_NAG.T;
         _tab_buf.SYM:=ZD_NAG.SYM;
         _tab_buf
      \",~~);
      {? type_of(_tab_buf)<>type_of(~~)
      || _dw:=_tab_buf.DATA;
         _typyzam:=_tab_buf.T;
         _sym:=_tab_buf.SYM
      ?}
   || _tab_buf:=exec('FindAndGet','#table',{? _z_faks || FAKS || DOK ?},{? _z_dok=2 || @.DOKUM.DOKR || @.DOKUM.REFSQL ?},,\"
         _tab_buf:=obj_new('DW','T','SYM','WHERE','MANSKSEF');
         {? _a=FAKS
         || _tab_buf.DW:=FAKS.DW;
            _tab_buf.T:=FAKS.T;
            _tab_buf.SYM:=FAKS.SYM;
            _tab_buf.WHERE:=FAKS.WHERE;
            _tab_buf.MANSKSEF:=FAKS.MANSKSEF
         |? _a=DOK
         || _tab_buf.DW:=DOK.DTO;
            _tab_buf.T:=null();
            _tab_buf.SYM:=DOK.NK;
            _tab_buf.WHERE:='';
            _tab_buf.MANSKSEF:='N'
         ?};
         _tab_buf
      \",~~);
      {? type_of(_tab_buf)<>type_of(~~)
      || _dw:=_tab_buf.DW;
         _typysp:=_tab_buf.T;
         _sym:=_tab_buf.SYM;
         _where:=_tab_buf.WHERE;
         _mansksef:=_tab_buf.MANSKSEF
      ?}
   ?};
   {? _z_faks & (_kh=null() | _typysp=null() | _dw=date(0,0,0) | _where='') |
      _z_dok & (_kh=null() | _dw=date(0,0,0)) |
      _z_zdnag & (_kh=null() | _typyzam=null() | _dw=date(0,0,0))
   ||
       0
   ||
      _make_pdf:=_z_faks & exec('czy_edokument_bl_pdf','zbl_dok',_kh,_typysp,_dw,_where,_mansksef) |
                 _z_zdnag & exec('czy_zam_edokument_bl_pdf','zbl_dok',_kh,_typyzam,_dw);
      _make_ufd:=_z_faks & exec('czy_edokument_bl_ufd','zbl_dok',_kh,_typysp,_dw,_where) |
                 _z_zdnag & exec('czy_zam_edokument_bl_ufd','zbl_dok',_kh,_typyzam,_dw);
      _make_ksef:=_z_faks & exec('czy_edokument_bl_ksef','zbl_dok',_kh,_typysp,_dw,_where,_mansksef) |
                  _z_dok & exec('czy_edokument_bl_ksef_dks','zbl_dok',_kh,_dw);
      DOKUMZ.cntx_psh();
      DOKUMZ.index('KIND');
      DOKUMZ.prefix(@.DOKUM.ref());
      _return:=
         {? _make_pdf & ~DOKUMZ.find_key('PDF',) || 0
         |? _make_ufd & _z_faks & ~DOKUMZ.find_key('INV',) & ~DOKUMZ.find_key('INV_CORR',) || 0
         |? _make_ufd & _z_zdnag & ~DOKUMZ.find_key('ORD_DZK',) || 0
         |? _make_ksef &
            ~DOKUMZ.find_key(exec('bl_type_ksef_inv','zbl_dok'),) &
            ~DOKUMZ.find_key(exec('bl_type_ksef_inv_corr','zbl_dok'),) &
            ~DOKUMZ.find_key(exec('bl_type_ksef_dks_inv','zbl_dok'),) &
            ~DOKUMZ.find_key(exec('bl_type_ksef_dks_inv_corr','zbl_dok'),)
         || 0
         || 1
         ?};
      DOKUMZ.cntx_pop();
      _return
   ?}
",0)


\kh_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [21.37]
:: OPIS: Ustala kontrahenta dla DOKUM na podstawie faktury i zbudowanych relacji dla kontrahenta i odbiorcy faktury
::   WE: _a - FAKS.ref() / $FAKS.ref()
::       _b - 0/1 - dane pobierać z bufora tabeli FAKS
::   WY: KH.ref()
::----------------------------------------------------------------------------------------------------------------------
_faks:=_a;
_buf:={? var_pres('_b')=type_of(0) || _b || 0 ?};

_result:=null();

KH_ODB.cntx_psh();
{? exec('is_active','businesslink3',0)
||
   KH_DOD.cntx_psh();
   _kh:=
      {? _buf
      || {? FAKS.KH_ODB<>null() || FAKS.KH_ODB().KH_LNK || null() ?}
      || exec('FindAndGet','#table',FAKS,_faks,,"{? FAKS.KH_ODB<>null() || FAKS.KH_ODB().KH_LNK || null() ?}",null())
      ?};
   {? _kh
   || exec('kh_dod_ini','kontrahent',_kh,0);
      {? (KH_DOD.EFAKTURA='B' | KH_DOD.EFAKTURA='U') & KH_ODB.FV_EM='T'
      || _result:=_kh
      ?}
   ?};
   KH_DOD.cntx_pop();
   {? _result=null() || _result:={? _buf || FAKS.KH || exec('FindAndGet','#table',FAKS,_faks,,"FAKS.KH",null()) ?} ?}

|| _formula:="
      {? FAKS.KH_ODB<>null() & FAKS.KH_ODB().KH_LNK<>null() & @.KH_ODB.FV_EM='T'
      || @.KH_ODB.KH_LNK
      || FAKS.KH
      ?}
   ";
   _result:={? _buf || _formula() || exec('FindAndGet','#table',FAKS,_faks,,_formula,null()) ?}

?};
KH_ODB.cntx_pop();

_result


\kh_dokum_dks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Ustala kontrahenta dla DOKUM na podstawie dokumentu księgowego i zbudowanych relacji dla kontrahenta
::       i odbiorcy dokumentu księgowego
::   WE: _a - DOK.ref() / $DOK.ref()
::       _b - 0/1 - dane pobierać z bufora tabeli DOK
::   WY: KH.ref()
::----------------------------------------------------------------------------------------------------------------------
_dok:=_a;
_buf:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_result:=null();


{? exec('is_active','businesslink3',0)
||
   KH_DOD.cntx_psh();
   _kh:={? _buf
        || {? DOK.KH_ODB<>null() || DOK.KH_ODB().KH_LNK || null() ?}
        || exec('FindAndGet','#table',DOK,_dok,,"{? DOK.KH_ODB<>null() || DOK.KH_ODB().KH_LNK || null() ?}",null())
        ?};
   {? _kh
   || exec('kh_dod_ini','kontrahent',_kh,0);
      {? (KH_DOD.EFAKTURA='B' | KH_DOD.EFAKTURA='U') & KH_ODB.FV_EM='T'
      || _result:=_kh
      ?}
   ?};
   KH_DOD.cntx_pop();
   {? _result=null()
   || _formula:="
         @.KH.cntx_psh();
         @.KH.index('SKR'); @.KH.prefix(2,DOK.KH);
         _result:={? @.KH.first() || @.KH.ref || null() ?};
         @.KH.cntx_pop();
         _result";
      {? _buf
      || _result:=_formula()
      || _result:=exec('FindAndGet','#table',DOK,_dok,,_formula,null())
      ?}
   ?}

|| _formula:="
      {? DOK.KH_ODB<>null() & DOK.KH_ODB().KH_LNK<>null() & KH_ODB.FV_EM='T'
      || KH_ODB.KH_LNK
      || @.KH.cntx_psh();
         @.KH.index('SKR'); @.KH.prefix(2,DOK.KH);
         _kh:={? @.KH.first() || @.KH.ref || null() ?};
         @.KH.cntx_pop();
         _kh
      ?}
   ";
   {? _buf
   || _result:=_formula()
   || _result:=exec('FindAndGet','#table',DOK,_dok,,_formula,null())
   ?}
?};
_result


\kkh_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Typ kontaktu Businesslink - parametr 100801
::   WY: ZDARZT.ref()
::----------------------------------------------------------------------------------------------------------------------
_kkh_tp:=null();
ZDARZT.cntx_psh();
ZDARZT.index('ZDARZT');
ZDARZT.prefix('N',exec('get','#params',100801),);
{? ZDARZT.first()
|| _kkh_tp:=ZDARZT.ref()
?};
ZDARZT.cntx_pop();
_kkh_tp


\pgl_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Podgląd log - akcja dla okna Businesslink dla FKS_DOK
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.BL_LOG = null || FUN.info('Brak zapisu w log.'@) || exec('bl_view','#blob',DOKUM,'BL_LOG') ?}


\br_bus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Rekord przed - akcja dla okna Businesslink dla FKS_DOK
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| _act:='';
   _Tab:=ref_tab(DOKUM.REFSQL);
   {? DOKUM.BL_STAT<>exec('ready_to_registration','zbl_stat')
      & DOKUM.BL_STAT<>exec('registration_failed','zbl_stat')
      & DOKUM.BL_STAT<>exec('during_registration','zbl_stat')
   || _act+='D'
   |? type_of(_Tab)=type_of(~~)
   || {? DOKUM.BL_TYPE='DOC'
      || _act+='D'
      |? 3+DOKUM.BL_TYPE='ORD'
      || _act+='D'
      ?}
   || {? _Tab<>DOK
      || _act+='D'
      ?}
   ?};
   DOKUM.actions_grayed(cur_win(1),_act)
?};
params_exec('rekprzed','color','DOKUM#03')


\dokumlog_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Dodanie wpisu w logu zdarzeń tabeli DOKUM
::   WE: _a - DOKUML.uidref()
::       [_b] - 1-możliwe skorzystanie z bfld, 0(domyślnie)-nie
::   WY: 1-dodano zapis, 0-nie dodano
::----------------------------------------------------------------------------------------------------------------------
_dokum:={? var_pres('_a')=type_of('') & (+_a)=48 || _a || '' ?};
 _bfld:={? var_pres('_b')=type_of(0) || _b || 0 ?};
  _mod:=1;

{? _dokum='' || return(0) ?};

_opdok:=exec('record','#to_string',_dokum);

{? _dokum='' || _return(0) ?};

{? _bfld & bfld('BL_STAT')<>DOKUM.BL_STAT || _mod:=2 ?};
{? _bfld & bfld('ONEID')<>DOKUM.ONEID || _mod:=0 ?};

_opis:={? _mod=0 || 'Dodano dokument Businesslink: %1.'@[_opdok]
       |? _mod=1 || 'Zmodyfikowano dokument Businesslink: %1.'@[_opdok]
       |? _mod=2 || 'Zmieniono stan dokumentu Businesslink: %1.'@[_opdok]
       || ''
       ?};

_changes:=tab_tmp(1,'LP','INTEGER','','ACR','STRING[8]','','NAME','STRING[60]','');
:: analiza dokonanych zmian dla DOKUM-a
{? _mod=1 & _bfld
||
:: pola wyłączone z porównania
   _nocomp:=';IDADD;IDPUT;ONEIDDOKUM;E_DOKUM;BL_LOG;DOKUMI;OPIS;UPRAW;SL_KH;WAL;JEZYK;BLC_URL;BLC_ANS;';
   _num:=DOKUM.fld_num();
   {! _ii.._num
   |! _acr:=DOKUM.fld_acr(_ii);
      _name:=DOKUM.fld_name(_ii);
      {? ~(_nocomp*(';%1;'[_acr])) & bfld(_acr)<>DOKUM[_ii]
      || _changes.blank();
         _changes.LP:=_ii;
         _changes.ACR:=_acr;
         _changes.NAME:=_name;
         _changes.add(1)
      ?}
   !}
|? _mod=2 & _bfld
|| _changes.blank();
   _changes.LP:=64;
   _changes.ACR:='BL_STAT';
   _changes.NAME:='Status dokumentu Businesslink';
   _changes.add(1)
?};

DOKUMLOG.cntx_psh();
DOKUMLOG.prefix();
DOKUMLOG.blank();
DOKUMLOG.OPIS:=_opis;
DOKUMLOG.DOKUM:=_dokum;
DOKUMLOG.BL_STAT:=exec('FindAndGet','#table',DOKUM,_dokum,,"BL_STAT",'');
_refsql:=exec('FindAndGet','#table',DOKUM,_dokum,,"REFSQL",'');
{? _refsql<>''
|| DOKUMLOG.DOK_REF:=_refsql;
   _tab:=ref_tab(_refsql);
   {? _tab=FAKS
   || DOKUMLOG.DOK_SYM:=exec('FindAndGet','#table',_tab,_refsql,,"SYM",'');
      DOKUMLOG.DOK_DT:=exec('FindAndGet','#table',_tab,_refsql,,"DW",date(0,0,0));
      DOKUMLOG.DOK_OP:=exec('record','#to_string',_refsql)
   |? _tab=DOK
   || DOKUMLOG.DOK_SYM:=exec('FindAndGet','#table',_tab,_refsql,,"NK",'');
      DOKUMLOG.DOK_DT:=exec('FindAndGet','#table',_tab,_refsql,,"DTO",date(0,0,0));
      DOKUMLOG.DOK_OP:=exec('record','#to_string',_refsql)
   |? _tab=EDOKUM
   || DOKUMLOG.DOK_SYM:=exec('FindAndGet','#table',_tab,_refsql,,"SYM",'');
      DOKUMLOG.DOK_DT:=exec('FindAndGet','#table',_tab,_refsql,,"DW",date(0,0,0));
      DOKUMLOG.DOK_OP:=exec('record','#to_string',_refsql)
   ?}
?};
_change:='';
_changes.clear();
{? _changes.first()
|| DOKUM.cntx_psh();
   DOKUM.use(ref_name(_dokum));
   DOKUM.prefix();
   {? DOKUM.seek(_dokum)
   || {!
      |? _type:=type_of(DOKUM[_changes.LP]);
         {? _type=2
         || _change+='POLE: %1\n - BYŁO: %2\n + JEST: %3\n\n'@[_changes.NAME,bfld(_changes.ACR),DOKUM[_changes.LP]]
         |? _type=1 | _type=4 | _type=5
         || _change+='POLE: %1\n - BYŁO: %2\n + JEST: %3\n\n'@[_changes.NAME,$bfld(_changes.ACR),$DOKUM[_changes.LP]]
         |? _type=7
         || _be_war:=exec('record','#to_string',bfld(_changes.ACR));
            _ae_war:=exec('record','#to_string',DOKUM[_changes.LP]);
            _change+='POLE: %1\n - BYŁO: %2\n + JEST: %3\n\n'@[_changes.NAME,_be_war,_ae_war]
         ?};
         _changes.next()
      !}
   ?};
   DOKUM.cntx_pop()
?};
_res:={? _mod & _bfld & _change=''
      || -1
      || DOKUMLOG.add()
      ?};
:: czy zapisywać do memo można zablokować ustawiając 1
_nochange:=0;
{? _res>0 & ~_nochange & _change<>''
|| DOKUMLOG.memo_set(_change);
   DOKUMLOG.memo_put()
?};
DOKUMLOG.cntx_pop();
obj_del(_changes);
_res


\dokumlog_his
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Wyświetla okienko z historią zmian
::----------------------------------------------------------------------------------------------------------------------
_dokum:={? var_pres('_a')=type_of('') || _a || DOKUM.uidref() ?};

{? _dokum<>''
|| DOKUMLOG.index('DOKUM');
   DOKUMLOG.prefix(_dokum,);
   {? DOKUMLOG.first()
   || DOKUMLOG.win_sel('WER');
      DOKUMLOG.select()
   || FUN.info('Brak histori dla dokumentu %1.'@[exec('record','#to_string',_dokum)])
   ?}
?};
~~


\dokumlog_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Usunięcie wpisów w logu zdarzeń tabeli DOKUM
::   WE: _a - DOKUML.uidref()
::   WY: 1-usunięto wpisy  0-nie do końca się powiodło
::----------------------------------------------------------------------------------------------------------------------
_dokum:={? var_pres('_a')=type_of('') || _a || '' ?};
_res:=1;

{? _dokum='' || return(0) ?};

DOKUMLOG.cntx_psh();
DOKUMLOG.index('DOKUM');
DOKUMLOG.prefix(_dokum,);
{? DOKUMLOG.first()
|| {!
   |? _del:=DOKUMLOG.del(,1);
      _del>1
   !};
   {? ~_del || _res:=0 ?}
?};
DOKUMLOG.cntx_pop();
_res


\dokumlog_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: Zmienia informacje o powiązanym dokumencie DOKUM.REFSQL
::   WE: _a - poprzednie wskazanie DOKUM.REFSQL
::       _b - nowe wskazanie DOKUM.REFSQL
:: UWAGA. Kontekstk aktualnego DOKUMLOG-a
::----------------------------------------------------------------------------------------------------------------------
_refsql:={? var_pres('_a')=type_of('') || _a || '' ?};
_newsql:={? var_pres('_b')=type_of('') || _b || '' ?};

{? _refsql<>'' & DOKUMLOG.DOK_REF=_refsql
|| _put:=0;
   DOKUMLOG.DOK_REF:=_newsql;
   _tab:=ref_tab(_newsql);
   {? _tab=FAKS
   || DOKUMLOG.DOK_SYM:=exec('FindAndGet','#table',_tab,_newsql,,"SYM",'');
      DOKUMLOG.DOK_DT:=exec('FindAndGet','#table',_tab,_newsql,,"DW",date(0,0,0));
      DOKUMLOG.DOK_OP:=exec('record','#to_string',_newsql);
      _put:=1
   |? _tab=DOK
   || DOKUMLOG.DOK_SYM:=exec('FindAndGet','#table',_tab,_refsql,,"NK",'');
      DOKUMLOG.DOK_DT:=exec('FindAndGet','#table',_tab,_refsql,,"DTO",date(0,0,0));
      DOKUMLOG.DOK_OP:=exec('record','#to_string',_refsql);
      _put:=1
   |? _tab=EDOKUM
   || DOKUMLOG.DOK_SYM:=exec('FindAndGet','#table',_tab,_refsql,,"SYM",'');
      DOKUMLOG.DOK_DT:=exec('FindAndGet','#table',_tab,_refsql,,"DW",date(0,0,0));
      DOKUMLOG.DOK_OP:=exec('record','#to_string',_refsql);
      _put:=1
   ?};
   {? _put || DOKUMLOG.put(1) ?}
?};
~~


\web_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Okno Podgląd w zakładce Przychodzące Businesslink
::   WE: _a - szerokość
::       _b - wysokość
:: ~OST: INWEBBROWSER
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR_WB');
__CTR_WB:=obj_new('WIN_ACR','WIN_ID','DOKUMI');
_width:=100; _hight:=50;
{? _>0 || _width:=_a ?};
{? _>1 || _hight:=_b ?};
_wb_acr:=DOKUM.mk_ctr('Podgląd'@,'obg_bl_podglad',,,_width,_hight);
DOKUM.win_ctr(_wb_acr);
{? exec('runtime_r10','#system')
|| DOKUM.win_cctr(_wb_acr,'ctr_id','@webframe')
|| DOKUM.win_cctr(_wb_acr,'ctr_id','@webbrowser2',1)
?};
__CTR_WB.WIN_ACR:=_wb_acr;
__CTR_WB.WIN_ID:=-(1-_wb_acr);
__CTR_WB.DOKUMI:=0;
~~


\web_view_load
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Ładuje zawartość okna Podgląd w zakładce Przychodzące Businesslink
::   WE: _a - 0/1 - ukryj zawartość
::----------------------------------------------------------------------------------------------------------------------
_ret:=~~;
_hide:={? var_pres('_a')>0 || _a || 0 ?};
{? _hide=0
|| _put:=0;
   {? DOKUM.RODZAJ_K='W' & DOKUM.name()='dokum' & DOKUM.DOKUMI=null()
   || DOKUMZ.index('DISP');
      DOKUMZ.prefix(DOKUM.ref());
      {? DOKUMZ.first()
      || {!
         |? {? DOKUMZ.DOKUM<>null() & DOKUMZ.bl_info('DOKUM','EXTENSION')='pdf'
            || DOKUM.DOKUMI:=DOKUMZ.DOKUM;
               _put:=1
            ?};
            _put=0 & DOKUMZ.next()
         !}
      ?}
   ?};
   {? _put || DOKUM.put() ?}
?};
{? _hide
|| __CTR_WB.DOKUMI:=0;
   ctr_call(':'+__CTR_WB.WIN_ID,'ctr_id','setHTMLContent', '<html></html>');
   _ret:='#disable'
|? #DOKUM.DOKUMI<>__CTR_WB.DOKUMI
|| _filepath:='';
   _loaded:=0;
   {? DOKUM.DOKUMI<>null()
   || _filename:=exec('get_unique_file_name','#file',DOKUM.bl_info('DOKUMI','NAME'),'#!Tmp/');
      _filepath:=exec('tmp_filepath','#file',_filename);
      _loaded:=exec('bl_get','#blob',DOKUM.DOKUMI,,_filename)
   ?};
   {? _loaded>0
   || __CTR_WB.DOKUMI:=#DOKUM.DOKUMI;
      _filepath:=gsub(_filepath,'\\','\\\\');
      ctr_call(':'+__CTR_WB.WIN_ID,'ctr_id','navigate',_filepath)
   || __CTR_WB.DOKUMI:=0;
      ctr_call(':'+__CTR_WB.WIN_ID,'ctr_id','setHTMLContent', '<html></html>');
      _ret:='#disable'
   ?}
?};
_ret


\typysp_ksef_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Typy dokumentów sprzedaży dla KSeF
::   WE: [_a] S - dokumenty sprzedaży (domyślnie), Z - dokumenty zakupu
::----------------------------------------------------------------------------------------------------------------------
_rodz:='S';
{? var_pres('_a')=type_of('') & (_a='S' | _a='Z')
|| _rodz:=_a
?};

{? _rodz='Z'
|| _tyt:='Typy dokumentów zakupu'@
|| _tyt:='Typy dokumentów sprzedaży'@
?};
_wer_td:=TYPYSP.mk_sel(_tyt,,,,,,,,'U');
TYPYSP.win_fld(_wer_td,,'T',,,10,,,'Typ'@);
TYPYSP.win_fld(_wer_td,,'NAZ',,,30,,,'Nazwa'@);
TYPYSP.win_fld(_wer_td,,'KSEFZDOK',,,3,,,'E-dokument tworzony z dokumentu księgowego'@,,,2,,"'T'","'N'");
{? exec('upgrade2325_blbc1','zbl')
|| TYPYSP.win_fld(_wer_td,POLA_GRP,'TXT_1',,,16,,,'Zakres kontroli Businesscheck'@)
?};
_fb:=$('exec(\'typysp_ksef_wyb\',\'zbl_dok\',\''+_rodz+'\')');
TYPYSP.win_act(_wer_td,0,'Formuła','&Ustaw'@@,,,_fb,,1,,,,'U');
TYPYSP.win_act(_wer_td,1,'Formuła','&Ustaw'@@,,,_fb,,1,,,,'U');
_fb:="TYPYSP.KSEFZDOK:={? TYPYSP.KSEFZDOK='N' || 'T' || 'N' ?}; TYPYSP.put()";
TYPYSP.win_act(_wer_td,,'Formuła','E-dokument z dokumentu księgowego'@@,,,_fb,,,1,,,'E');
_fb:="params_exec('select','dp_wydrukow','TD')";
_fbg:="params_exec('dpw_grupa','dp_wydrukow','TD')";
TYPYSP.win_act(_wer_td,,'Formuła','Domyślne parametry &wydruków'@@,,,_fb,,,1,_fbg,,'W');
_fb:="exec('typd_inf','typysp')";
TYPYSP.win_act(_wer_td,,'Formuła','&Informacje dodatkowe'@@,,,_fb,,,,,,'I');
{? exec('upgrade2325_blbc1','zbl')
|| _fb:="exec('typysp_ksef_bc','zbl_dok')";
   TYPYSP.win_act(_wer_td,,'Formuła','&Zakres kontroli Businesscheck'@@,,,_fb,,,1,_fb,,'Z');
   {? exec('is_businesscheck','businesslink3')
   || _fb:="POLA_GRP.TXT_1:=exec('ksef_bc_naz','zbl_dok',TYPYSP.KSEF_BC)"
   || _fb:="POLA_GRP.TXT_1:=''"
   ?};
   TYPYSP.win_act(_wer_td,,'Rekord',,,,_fb);
   {? ~exec('is_businesscheck','businesslink3')
   || TYPYSP.actions_grayed(_wer_td,'Z')
   ?}
?};

_win0:=TYPYSP.win_sel('?');
TYPYSP.cntx_psh();

TYPYSP.index('BL_KSEF1');
{? _rodz='Z'
|| TYPYSP.prefix('T','T')
|| TYPYSP.prefix('T','N')
?};
TYPYSP.win_sel(_wer_td);
TYPYSP.select();

TYPYSP.cntx_pop();
TYPYSP.win_sel(_win0);

~~


\typysp_ksef_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wybór typów dokumentów sprzedaży dla KSeF z listy wszystkich typów
::   WE: [_a] S - dokumenty sprzedaży (domyślnie), Z - dokumenty zakupu
::----------------------------------------------------------------------------------------------------------------------
_rodz:='S';
{? var_pres('_a')=type_of('') & (_a='S' | _a='Z')
|| _rodz:=_a
?};

_par0:='';
TYPYSP.cntx_psh();
TYPYSP.index('BL_KSEF1');
{? _rodz='Z'
|| TYPYSP.prefix('T','T')
|| TYPYSP.prefix('T','N')
?};
{? TYPYSP.first()
||
   {!
   |?
      _par0+=TYPYSP.T+' ';
      TYPYSP.next()
   !}
?};
TYPYSP.cntx_pop();

{? _rodz='Z'
|| _war:="TYPYSP.ZAK='T' & TYPYSP.KOR<>'Z'"
|| _war:="TYPYSP.ZAK='N' & TYPYSP.PAR='N'"
?};
_par:=exec('typysp_wyb','params',_par0,_war);
{? _par<>_par0
|| TYPYSP.cntx_psh();
   TYPYSP.index('SPRZAK');
   {? _rodz='Z'
   || TYPYSP.prefix('T')
   || TYPYSP.prefix('N')
   ?};
   {? TYPYSP.first()
   || {!
      |? {? (' '+_par+' ')*(' '+TYPYSP.T+' ')>0 & TYPYSP.KSEF<>'T'
         || TYPYSP.KSEF:='T';
            TYPYSP.put()
         |? (' '+_par+' ')*(' '+TYPYSP.T+' ')=0 & TYPYSP.KSEF='T'
         || TYPYSP.KSEF:='N';
            TYPYSP.put()
         ?};
         TYPYSP.next()
      !}
   ?};
   TYPYSP.cntx_pop()
?};
~~


\typysp_ksef_bc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Ustawienie TYPYSP.KSEF_BC - zakres kontroli Businesscheck
::----------------------------------------------------------------------------------------------------------------------
_grp:=(TYPYSP.sel_size()>0);
_win:=TYPYSP.win_edit('?');
TYPYSP.win_edit('KSEF_BC');
{? TYPYSP.edit()
|| {? _grp
   || _ref:=TYPYSP.ref();
      _ksef_bc:=TYPYSP.KSEF_BC;
      _tab:=TYPYSP.sel_aget();
      TYPYSP.sel_adel();
      {? _tab.first()
      || {!
         |? {? TYPYSP.seek(_tab.REF,)
            || TYPYSP.KSEF_BC:=_ksef_bc;
               TYPYSP.put()
            ?};
            _tab.next()
         !}
      ?};
      TYPYSP.seek(_ref)
   || TYPYSP.put()
   ?}
?};
{? _grp || 0 || ~~ ?}


\ksef_bc_naz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Dla podanego zakresu kontroli Businesscheck zwraca jego opis
::   WE: _a - zakres kontroli Businesscheck - 0, 1, 2, 3, 4
::   WY: zakres kontroli Businesscheck - napis
::----------------------------------------------------------------------------------------------------------------------
_nap:='Brak'@;
{? var_pres('_a')=type_of(0)
|| {? _a=1
   || _nap:='Kontrola, przy braku błędów wysyłka do KSeF'@
   |? _a=2
   || _nap:='Kontrola, bez wysyłki do KSeF'@
   |? _a=3
   || _nap:='Kontrola, przy braku błędów/uwag wysyłka do KSeF'@
   |? _a=4
   || _nap:='Kontrola, wysyłka do KSeF niezależnie od wyniku'@
   ?}
?};
_nap


\rodzaje_edokumentu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Zwraca rodzaje edokumentu dla kontrahenta _a, typu dokumentu _b, od daty wystawienia _c
::   WE: _a - KH.ref()
::       _b - TYPYSP.ref()
::       _c - data wystawienia
::       _d - FAKS.WHERE
::       _e - FAKS.MANSKSEF
::   WY: |pdf|ufd|ksef|e-mail|
::----------------------------------------------------------------------------------------------------------------------
_kh:=_a;
_typysp:=_b;
_dw:=_c;
_where:=_d;
_mansksef:=_e;
_result:='';
{? _kh & _typysp
||
   {? exec('czy_edokument_bl_pdf','zbl_dok',_kh,_typysp,_dw,_where,_mansksef) || _result+='pdf|' ?};
   {? exec('czy_edokument_bl_ufd','zbl_dok',_kh,_typysp,_dw,_where) || _result+='ufd|' ?};
   {? exec('czy_edokument_bl_ksef','zbl_dok',_kh,_typysp,_dw,_where,_mansksef) || _result+='ksef|' ?};
   {? exec('czy_edokument_email','zbl_dok',_kh) || _result+='e-mail|' ?}
?};
{? +_result || '|'+_result || _result ?}


\czy_edokument_email
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Czy e-dokument przez e-mail?
::   WE: _a - KH.ref()
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
KH_DOD.cntx_psh();
{? exec('kh_dod_ini','kontrahent',_a,0)
||
   _return:=KH_DOD.EFAKTURA='T'
?};
KH_DOD.cntx_pop();
_return


\czy_edokument_bl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Czy wystawiać e-dokument UFD?
::   WE: _a - rodzaje edokumentu
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_a*'|pdf|' | _a*'|ufd|' | _a*'|ksef|'


\czy_edokument_bl_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Czy wystawiać e-dokument PDF?
::   WE: _a - KH.ref()
::       _b - TYPYSP.ref()
::       _c - data wystawienia
::       _d - FAKS.WHERE
::       _e - FAKS.MANSKSEF
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
KH_DOD.cntx_psh();
{? exec('kh_dod_ini','kontrahent',_a,0)
||
   _return:=KH_DOD.EFAKTURA='U' & KH_DOD.BL_DATE<=_c
?};
KH_DOD.cntx_pop();
{? _return
||
   _return:=~exec('czy_edokument_bl_ufd','zbl_dok',_a,_b,_c,_d) & ~exec('czy_edokument_bl_ksef','zbl_dok',_a,_b,_c,_d,_e)
?};
_return


\czy_edokument_bl_ufd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Czy wystawiać e-dokument UFD?
::   WE: _a - KH.ref()
::       _b - TYPYSP.ref()
::       _c - data wystawienia
::       _d - FAKS.WHERE
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
KH_DOD.cntx_psh();
{? var_pres('__MANSKSEF')=type_of('') & __MANSKSEF='BC'
||
   _return:=0

|? exec('kh_dod_ini','kontrahent',_a,0)
||
   _return:=KH_DOD.EFAKTURA='B' & KH_DOD.BL_DATE<=_c
?};
KH_DOD.cntx_pop();
{? _return || _return:=_d<>'N' & exec('FindAndGet','#table',TYPYSP,_b,,"TYPYSP.BL='T'",0) ?};
_return


\czy_edokument_bl_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Czy wystawiać e-dokument KSeF?
::   WE: _a - KH.ref()
::       _b - TYPYSP.ref()
::       _c - data wystawienia
::       _d - FAKS.WHERE
::   WY: 0 - nie / 1 - z FAKS / 2 - z DOK
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
KH_DOD.cntx_psh();
{? var_pres('__MANSKSEF')=type_of('') & __MANSKSEF='BC'
||
   _return:=1

|? FAKS.MANSKSEF='T'
||
   _return:=0

|? exec('kh_dod_ini','kontrahent',_a,0)
||
   _return:=KH_DOD.KSEFKH='T' & KH_DOD.KSEFKHOD<=_c
?};
KH_DOD.cntx_pop();
{? _return || _return:=exec('FindAndGet','#table',TYPYSP,_b,,"(TYPYSP.KSEF='T')+(TYPYSP.KSEFZDOK='T')",0) ?};
_return


\bl_type_ksef_inv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Typ dokumentu Businesslink - KSeF_INV
::----------------------------------------------------------------------------------------------------------------------
'KSeF_INV'


\bl_type_ksef_inv_corr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Typ dokumentu Businesslink - KSeF_INV_CORR
::----------------------------------------------------------------------------------------------------------------------
'KSeF_INV_CORR'


\is_dokum_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Sprawdza, czy w DOKUM jest DOKUMZ typu "KSeF"
::   WE: _a - DOKUM.ref()
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;

_res:=0;

DOKUMZ.cntx_psh();
DOKUMZ.index('KIND');
DOKUMZ.prefix(_dokum);
_res:=DOKUMZ.find_key(exec('bl_type_ksef_inv','zbl_dok'),)
     | DOKUMZ.find_key(exec('bl_type_ksef_inv_corr','zbl_dok'),)
     | DOKUMZ.find_key(exec('bl_type_ksef_dks_inv','zbl_dok'),)
     | DOKUMZ.find_key(exec('bl_type_ksef_dks_inv_corr','zbl_dok'),);
DOKUMZ.cntx_pop();

_res


\ksefauth
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Autoryzuj dokumentu KSeF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);
_continue:=1;
{? 'T'<>exec('FindAndGet','#table',USERS,OPERATOR.USER,,"{? exec('get_usersf','users') || USERSF.KSEF_UL || 'N' ?}",'N')
||
   FUN.info('Brak uprawnień do autoryzacji.'@);
   _continue:=0
?};
{? _continue>0
||
   _Sel:=_Tab.sel_aget();
   {? ~_Sel.first() || _Sel.REF:=#_Tab.ref(); _Sel.add() ?};
   _continue:=
      {? _Sel.first() & _Sel.next()
      || FUN.ask('Czy autoryzować dokumenty?'@)
      || _msg:='';
         {? _Tab=DOKUM
         || _refsql_tab:=ref_tab(DOKUM.REFSQL);
            {? type_of(_refsql_tab)>0 & _refsql_tab=ZD_NAG
            || _msg:='Opcja niedostępna dla zamówienia dostawy.'@
            |? type_of(_refsql_tab)>0 & _refsql_tab<>FAKS & _refsql_tab<>DOK
            || _msg:='Opcja niedostępna dla tego dokumentu.'@
            ?};
            &_refsql_tab
         ?};
         {? _msg<>''
         || FUN.info(_msg);
            0
         || FUN.ask('Czy autoryzować dokument?'@)
         ?}
      ?}
?};
{? _continue>0
||
   exec('ini_kom','#message','Wynik autoryzacji dokumentu');
   DOKUM.cntx_psh();
   _Tab.cntx_psh();
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      {? _Tab.seek(_Sel.REF,)
      ||
         _continue:=1;
         {? _Tab<>DOKUM
         ||
            _sym:={? _Tab=FAKS || FAKS.SYM || DOK.NK ?};
            {? ~exec('bl_dokum_seek','zbl',_Tab)
            ||
               exec('add_kom','#message','Nie znaleziono e-dokumenutu.'@,7,_sym);
               _continue:=0
            ?}
         ?};
         {? _continue>0
         ||
            _msg:='';
            _refsql_tab:=ref_tab(DOKUM.REFSQL);
            {? type_of(_refsql_tab)>0 & (_refsql_tab=FAKS | _refsql_tab=DOK)
            || _statksef:=exec('FindAndGet','#table',ref_tab(DOKUM.REFSQL),DOKUM.REFSQL,,"STATKSEF",'');
               {? _statksef<>exec('waiting_auth','zbl_stat')
                     &
                  _statksef<>exec('ready_to_send','zbl_stat')
                     &
                  _statksef<>exec('ksef_send_rejected','zbl_stat')
               ||
                  _msg:='Opcja dostępna dla dokumentu o statusie KSeF "%1" lub "%2" lub "%3". '
                  'Obecny status to "%4".'@[exec('waiting_auth','zbl_stat'),exec('ready_to_send','zbl_stat'),
                  exec('ksef_send_rejected','zbl_stat'),_statksef]
               || DOKUM.KSEFAUTH:=OPERATOR.USER;
                  DOKUM.BL_STAT:=exec('ready_to_send','zbl_stat');
                  DOKUM.put()
               ?}
            |? type_of(_refsql_tab)>0 & _refsql_tab=ZD_NAG
            || _msg:='Opcja niedostępna dla zamówienia dostawy.'@
            || _msg:='Opcja niedostępna dla tego dokumentu.'@
            ?};
            &_refsql_tab;
            {? _msg<>''
            || exec('add_kom','#message',_msg,7,DOKUM.KR_OP)
            ?}
         ?}
      ?};
      _loop:=_Sel.next()
   !};
   _Tab.cntx_pop();
   _Tab.sel_adel();
   DOKUM.cntx_pop();
   exec('end_kom','#message')
?};
0


\ksefauth_revoke
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wycofaj autoryzację dokumentu KSeF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);
_continue:=1;
{? 'T'<>exec('FindAndGet','#table',USERS,OPERATOR.USER,,"{? exec('get_usersf','users') || USERSF.KSEF_UL || 'N' ?}",'N')
||
   FUN.info('Brak uprawnień do anulowania autoryzacji.'@);
   _continue:=0
?};
{? _continue>0
||
   _Sel:=_Tab.sel_aget();
   {? ~_Sel.first() || _Sel.REF:=#_Tab.ref(); _Sel.add() ?};
   _continue:=
      {? _Sel.first() & _Sel.next()
      || FUN.ask('Czy anulować autoryzację dokumentów?'@)
      || _msg:='';
         {? _Tab=DOKUM
         || _refsql_tab:=ref_tab(DOKUM.REFSQL);
            {? type_of(_refsql_tab)>0 & _refsql_tab=ZD_NAG
            || _msg:='Opcja niedostępna dla zamówienia dostawy.'@
            |? type_of(_refsql_tab)>0 & _refsql_tab<>FAKS & _refsql_tab<>DOK
            || _msg:='Opcja niedostępna dla tego dokumentu.'@
            ?};
            &_refsql_tab
         ?};
         {? _msg<>''
         || FUN.info(_msg);
            0
         || FUN.ask('Czy anulować autoryzację dokumentu?'@)
         ?}
      ?}
?};
{? _continue>0
||
   exec('ini_kom','#message','Wynik anulowania autoryzacji dokumentu');
   DOKUM.cntx_psh();
   _Tab.cntx_psh();
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      {? _Tab.seek(_Sel.REF,)
      ||
         _continue:=1;
         {? _Tab<>DOKUM
         ||
            _sym:={? _Tab=FAKS || FAKS.SYM || DOK.NK ?};
            {? ~exec('bl_dokum_seek','zbl',_Tab)
            ||
               exec('add_kom','#message','Nie znaleziono e-dokumentu.'@,7,_sym);
               _continue:=0
            ?}
         ?};
         {? _continue>0 & DOKUM.KSEFAUTH<>null() & DOKUM.KSEFAUTH<>OPERATOR.USER
         ||
            exec('add_kom','#message','Brak uprawnień do anulowania autoryzacji e-dokumentu.'@,7,DOKUM.KR_OP);
            _continue:=0
         ?};
         {? _continue>0
         ||
            _msg:='';
            _refsql_tab:=ref_tab(DOKUM.REFSQL);
            {? type_of(_refsql_tab)>0 & (_refsql_tab=FAKS | _refsql_tab=DOK)
            || _statksef:=exec('FindAndGet','#table',ref_tab(DOKUM.REFSQL),DOKUM.REFSQL,,"STATKSEF",'');
               {? _statksef<>exec('ready_to_send','zbl_stat')
                     &
                  _statksef<>exec('ksef_send_rejected','zbl_stat')
               || _msg:='Opcja dostępna dla dokumentu o statusie KSeF "%1" lub "%2". '
                  'Obecny status to "%3".'@[exec('ready_to_send','zbl_stat'),exec('ksef_send_rejected','zbl_stat'),
                  _statksef]
               ||
                  DOKUM.KSEFAUTH:=null();
                  DOKUM.BL_STAT:=exec('waiting_auth','zbl_stat');
                  DOKUM.put()
               ?}
            |? type_of(_refsql_tab)>0 & _refsql_tab=ZD_NAG
            || _msg:='Opcja niedostępna dla zamówienia dostawy.'@
            || _msg:='Opcja niedostępna dla tego dokumentu.'@
            ?};
            &_refsql_tab;
            {? _msg<>''
            || exec('add_kom','#message',_msg,7,DOKUM.KR_OP)
            ?}
         ?}
      ?};
      _loop:=_Sel.next()
   !};
   _Tab.cntx_pop();
   _Tab.sel_adel();
   DOKUM.cntx_pop();
   exec('end_kom','#message')
?};
0


\ksef_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Wysyłanie dokumentu do KSeF
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);
_selected:=(_Tab.sel_size()>0);
_symbol:={? _Tab=FAKS || FAKS.SYM |? _Tab=DOK || DOK.NK || DOKUM.SYM ?};

:: Dodatkowe sprawdzenia, gdy wysyłka z poziomu faktur albo dokumentów księgowych
{? _Tab=DOK | _Tab=FAKS
|| {? exec('upgrade2325_blbc1','zbl') & _Tab.STATBC=exec('check_pending','zbl_stat')
   || KOMM.msg('Dokument %1 jest w trakcie kontroli.'@[_symbol],~_selected);
      return()
   |? _Tab.STATKSEF<>exec('ready_to_send','zbl_stat') & _Tab.STATKSEF<>exec('verification_required','zbl_stat')
   || KOMM.msg('Dokument %1 nie jest gotowy do wysłania.'@[_symbol],~_selected);
      return()
   ?};
   {? ~_Tab.r_lock(1,1,1)
   || KOMM.msg('Dokument %1 jest zablokowany przez innego użytkownika.'@[_symbol],~_selected);
      _Tab.r_unlock();
      return()
   || _Tab.r_unlock()
   ?}
?};

DOKUM.cntx_psh();

{? _Tab=DOKUM | exec('bl_dokum_seek','zbl',_Tab)
|| {? DOKUM.RODZAJ_K<>'W'
   || KOMM.msg('E-dokument do %1 nie jest \'wychodzący\'.'@[_symbol],~_selected)
   || exec('document_send','businesslink3',_selected)
   ?}
|| KOMM.msg('E-dokument do %1 nie został wygenerowany.'@[_symbol],~_selected)
?};

DOKUM.cntx_pop();
~~


\ksef_send_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Wysyłanie dokumentu do KSeF - przed grupą zaznaczonych rekordów
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy wysłać zaznaczone dokumenty do KSeF?'@)
|| KOMM.init(,,'Wysyłanie dokumentów do KSeF'@);
   sel_nchk();
   1
|| 0
?}


\ksef_send_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Wysyłanie dokumentu do KSeF - po grupie zaznaczonych rekordów
::----------------------------------------------------------------------------------------------------------------------
KOMM.select();
~~


\bl_log_view4faks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Podgląd log dokumentu Busineslink powiązanego z dokumentem sprzedaży znajdującym się w buforze tabeli FAKS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_rodzaje_edokumentu:=exec('rodzaje_edokumentu','zbl_dok',FAKS.KH,FAKS.T,FAKS.DW,FAKS.WHERE,FAKS.MANSKSEF);
_bl_bez:=(_rodzaje_edokumentu='' | _rodzaje_edokumentu='e-mail');
DOKUM.cntx_psh();
{? exec('bl_dokum_seek','zbl',FAKS,0,_bl_bez)
||
   {? DOKUM.BL_LOG=null()
   ||
      FUN.info('Brak zapisu w log.'@)
   ||
      exec('edit_memo','#edit',exec('blob_txt_dl','#edit',DOKUM.BL_LOG),'Log'@,'E-dokument'@,180,25,,,0)
   ?}
||
   FUN.info('Log dla dokumentu nie jest dostępny.'@)
?};
DOKUM.cntx_pop()


\kh_odb_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Ustala kontrahenta odbiorcę dla DOKUM na podstawie faktury
::   WE: _a - FAKS.ref() / $FAKS.ref()
::   WY: KH.ref()
::----------------------------------------------------------------------------------------------------------------------
_record:=_a;
_result:={? ref_tab(_record)=FAKS
         || exec('FindAndGet','#table',FAKS,_record,,"{? FAKS.KH_ODB<>null() || FAKS.KH_ODB().KH_LNK || null() ?}",null())
         |? ref_tab(_record)=DOK
         || exec('FindAndGet','#table',DOK,_record,,"{? DOK.KH_ODB<>null() || DOK.KH_ODB().KH_LNK || null() ?}",null())
         || null()
         ?};
_result


\czy_edokument_bl_ksef_dks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Czy wystawiać e-dokument KSeF?
::   WE: _a - KH.ref()
::       _b - data wystawienia
::   WY: 0 - nie / 1 - z FAKS / 2 - z DOK
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
KH_DOD.cntx_psh();
{? var_pres('__MANSKSEF')=type_of('') & __MANSKSEF='BC'
||
   _return:=1
|? exec('kh_dod_ini','kontrahent',_a,0)
||
   _return:=KH_DOD.KSEFKH='T' & KH_DOD.KSEFKHOD<=_b
?};
KH_DOD.cntx_pop();
_return


\bl_type_ksef_dks_inv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Typ dokumentu Businesslink - KSeF_INV_DKS
::----------------------------------------------------------------------------------------------------------------------
'KSeF_INV_DKS'


\bl_type_ksef_dks_inv_corr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Typ dokumentu Businesslink - KSeF_INV_CORR_DKS
::----------------------------------------------------------------------------------------------------------------------
'KSeF_INV_CORR_DKS'


\bl_log_view4dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Podgląd log dokumentu Busineslink powiązanego z dokumentem księgowym znajdującym się w buforze tabeli DOK
::----------------------------------------------------------------------------------------------------------------------
{? 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'
|| FAKS.cntx_psh(); FAKS.prefix();
   {? FAKS.seek(#(4-DOK.DOKZRODL),'faktu'+(3+(1-DOK.DOKZRODL)))
   || exec('bl_log_view4faks','zbl_dok')
   ?};
   FAKS.cntx_pop()
||
   DOKUM.cntx_psh();
   {? exec('bl_dokum_seek','zbl',DOK)
   ||
      {? DOKUM.BL_LOG=null()
      ||
         FUN.info('Brak zapisu w log.'@)
      ||
         exec('edit_memo','#edit',exec('blob_txt_dl','#edit',DOKUM.BL_LOG),'Log'@,'E-dokument'@,180,25,,,0)
      ?}
   ||
      FUN.info('Log dla dokumentu nie jest dostępny.'@)
   ?};
   DOKUM.cntx_pop()
?}


\bl_dok_unlink
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Usuwa powiązanie dokumentu księgowy z kontaktem
::   WE: _a - DOKUM.ref

::----------------------------------------------------------------------------------------------------------------------
exec('FindAndGet','#table',DOKUM,_a,,"
   @.DOKUM.DOKR:='';
   @.DOKUM.SYM_SYS:='';
   @.DOKUM.put()
")


\winWebPrvDok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37_14]
:: OPIS: Okno podglądu web browser dla dokumentów księgowych
::   WE: _a - akronim okna grupowego
::       _b - tabela
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_grp:=_a;
_tab:={? var_pres('_b')>0 || _b || TREE_REJ ?};
:: Podgląd dokumentu - okno wertowania
::
exec('web_view_dok','zbl_dok',65,16);

:: podgląd załącznika
::
{? _tab=DOK
|| DOK.grp_ctr(_win_grp,DOK,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,'Podgląd',,,,,,$('zakl_dok:=%1'[$(objZakl.addZakl('DOK','PREVIEW'))]),"",,'maximized')
|| TREE_REJ.grp_ctr(_win_grp,DOK,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,'Podgląd',,,,,,$('zakl_dok:=%1'[$(objZakl.addZakl('DOK','PREVIEW'))]),"",,'maximized')
?}


\web_view_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37_14]
:: OPIS: Okno Web Podgląd w dokumentach księgowych
::   WE: _a - szerokość
::       _b - wysokość
:: ~OST: INWEBBROWSER
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR_WB');
__CTR_WB:=obj_new('WIN_ACR','WIN_ID','DOKUMI');
{? exec('upgrade2325_blbc1','zbl')
|| _wb_acr:=DOK.mk_ctr('Podgląd'@)
|| _width:=100; _hight:=50;
   {? _>0 || _width:=_a ?};
   {? _>1 || _hight:=_b ?};
   _wb_acr:=DOK.mk_ctr('Podgląd'@,,,,_width,_hight)
?};
DOK.win_ctr(_wb_acr);
{? exec('runtime_r10','#system')
|| DOK.win_cctr(_wb_acr,'ctr_id','@webframe')
|| DOK.win_cctr(_wb_acr,'ctr_id','@webbrowser2',1)
?};
__CTR_WB.WIN_ACR:=_wb_acr;
__CTR_WB.WIN_ID:=-(1-_wb_acr);
__CTR_WB.DOKUMI:=0;
~~


\web_view_load_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Ładuje zawartość okna Web Podgląd w dokumentach księgowych
::   WE: _a - 0/1 - ukryj zawartość
::----------------------------------------------------------------------------------------------------------------------
_ret:=~~;
_hide:={? var_press('_a')>0 || _a || 0 ?};
{? _hide
|| __CTR_WB.DOKUMI:=0;
   ctr_call(':'+__CTR_WB.WIN_ID,'ctr_id','setHTMLContent', '<html></html>');
   _ret:='#disable'
|? #DOK.E_DOC<>__CTR_WB.DOKUMI
|| _filepath:='';
   _loaded:=0;
   {? DOK.E_DOC<>null()
   || _filename:=exec('get_unique_file_name','#file',DOK.bl_info('E_DOC','NAME'),'#!Tmp/');
      _filepath:=exec('tmp_filepath','#file',_filename);
      _loaded:=exec('bl_get','#blob', DOK.E_DOC,,_filename)
   ?};
   {? _loaded>0
   || __CTR_WB.DOKUMI:=#DOK.E_DOC;
      _filepath:=gsub(_filepath,'\\','\\\\');
      ctr_call(':'+__CTR_WB.WIN_ID,'ctr_id','navigate',_filepath)
   || __CTR_WB.DOKUMI:=0;
      ctr_call(':'+__CTR_WB.WIN_ID,'ctr_id','setHTMLContent', '<html></html>');
      _ret:='#disable'
   ?}
?};
_ret


\web_view_edokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Okno Web Podgląd w dokumentach w obiegu
::   WE: _a - szerokość
::       _b - wysokość
:: ~OST: INWEBBROWSER
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR_WB2');
__CTR_WB2:=obj_new('WIN_ACR','WIN_ID','DOKUMI');
_width:=100; _hight:=50;
{? _>0 || _width:=_a ?};
{? _>1 || _hight:=_b ?};
_wb_acr:=EDOKUM.mk_ctr('Podgląd'@,,,,_width,_hight);
EDOKUM.win_ctr(_wb_acr);
{? exec('runtime_r10','#system')
|| EDOKUM.win_cctr(_wb_acr,'ctr_id','@webframe')
|| EDOKUM.win_cctr(_wb_acr,'ctr_id','@webbrowser2',1)
?};
__CTR_WB2.WIN_ACR:=_wb_acr;
__CTR_WB2.WIN_ID:=-(1-_wb_acr);
__CTR_WB2.DOKUMI:=0;
~~


\web_view_load_edokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Ładuje zawartość okna Web Podgląd w dokumentach w obiegu
::   WE: _a - 0/1 - ukryj zawartość
::----------------------------------------------------------------------------------------------------------------------
_ret:=~~;
_hide:={? var_press('_a')>0 || _a || 0 ?};
{? _hide
|| __CTR_WB2.DOKUMI:=0;
   ctr_call(':'+__CTR_WB2.WIN_ID,'ctr_id','setHTMLContent', '<html></html>');
   _ret:='#disable'
|? #EDOKUM.EDOKUM<>__CTR_WB2.DOKUMI
|| _filepath:='';
   _loaded:=0;
   {? EDOKUM.EDOKUM<>null()
   || _filename:=exec('get_unique_file_name','#file',EDOKUM.bl_info('EDOKUM','NAME'),'#!Tmp/');
      _filepath:=exec('tmp_filepath','#file',_filename);
      _loaded:=exec('bl_get','#blob',EDOKUM.EDOKUM,,_filename)
   ?};
   {? _loaded>0
   || __CTR_WB2.DOKUMI:=#EDOKUM.EDOKUM;
      _filepath:=gsub(_filepath,'\\','\\\\');
      ctr_call(':'+__CTR_WB2.WIN_ID,'ctr_id','navigate',_filepath)
   || __CTR_WB2.DOKUMI:=0;
      ctr_call(':'+__CTR_WB2.WIN_ID,'ctr_id','setHTMLContent', '<html></html>');
      _ret:='#disable'
   ?}
?};
_ret


\ksef_donot_apply
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Nie wysyłaj do KSeF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);
{? _Tab=DOKUM || return(0) ?};

_continue:=1;

_Sel:=_Tab.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#_Tab.ref(); _Sel.add() ?};
_continue:=
   {? _Sel.first() & _Sel.next()
   || FUN.ask('Czy nie wysyłać dokumentów do KSeF?'@)
   || FUN.ask('Czy nie wysyłać dokumentu do KSeF?'@)
   ?};
{? _continue>0
||
   _Tab1:=
      {? exec('bl_dokum_seek','zbl',_Tab) || DOKUM
      || _Tab
      ?};
   exec('ini_kom','#message','Wynik zmiany status dokumentów KSeF');
   DOKUM.cntx_psh();
   _Tab.cntx_psh();
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      {? _Tab.seek(_Sel.REF,)
      ||
         _bl_dokum_seek:=0;
         _txt:=
            {? _Tab=DOKUM || DOKUM.KR_OP
            |? exec('bl_dokum_seek','zbl',_Tab) || _bl_dokum_seek:=1; DOKUM.KR_OP
            |? _Tab=FAKS || FAKS.SYM
            || ''
            ?};
         {? _Tab=DOK || _txt:=DOK.ODD().OD+'|'+DOK.REJ().KOD+'|'+$DOK.NR+'|'+DOK.NK ?};
         _statksef:=
            {? _bl_dokum_seek
            || exec('FindAndGet','#table',ref_tab(DOKUM.REFSQL),DOKUM.REFSQL,,"STATKSEF",'')
            || _Tab.STATKSEF
            ?};
         {? _statksef<>exec('before_send','zbl_stat')
               &
            _statksef<>exec('validation_failed','zbl_stat')
               &
            _statksef<>exec('waiting_auth','zbl_stat')
               &
            _statksef<>exec('ready_to_send','zbl_stat')
               &
            _statksef<>exec('ksef_send_rejected','zbl_stat')
         ||
            exec('add_kom','#message','Opcja dostępna dla dokumentu o statusie KSeF:',7,_txt);
            exec('add_kom','#message','- %1'[exec('before_send','zbl_stat')],7,_txt);
            exec('add_kom','#message','- %1'[exec('validation_failed','zbl_stat')],7,_txt);
            exec('add_kom','#message','- %1'[exec('waiting_auth','zbl_stat')],7,_txt);
            exec('add_kom','#message','- %1'[exec('ready_to_send','zbl_stat')],7,_txt);
            exec('add_kom','#message','- %1'[exec('ksef_send_rejected','zbl_stat')],7,_txt)
         |? _Tab=DOK & (DOK.ZP='T' | DOK.ZK='T')
         ||  exec('add_kom','#message','Opcja niedostępna dla zaksięgowanego dokumentu.',7,_txt)
         ||
            _Tab.KSEFAUTH:=null();
            _Tab.MANSKSEF:='T';
            {? _Tab=DOK
            || _record:=DOK.ref;
               {? 1+DOK.DOKZRODL='D'
               || FAKS.cntx_psh();
                  FAKS.clear();
                  FAKS.use('faktu'+(3+(1-DOK.DOKZRODL)));
                  {? FAKS.seek(#(4-DOK.DOKZRODL),FAKS.name)
                  || _record:=FAKS.ref();
                     FAKS.KSEFAUTH:=null();
                     FAKS.MANSKSEF:='T';
                     FAKS.put()
                  ?};
                  FAKS.cntx_pop()
               ?};
               exec('set_dok_ksef','dok_ksef',1)
            ?};
            _Tab.put();
            {? _Tab=FAKS || exec('set_dok_ksef_donot_apply','zbl_dok') ?};
            {? _Tab1=DOKUM
            || {? _Tab=FAKS
               || exec('del_autoadd','dokum',FAKS.ref()); FAKS.get()
               |? _Tab=DOK
               || exec('del_autoadd','dokum',_record); DOK.get()
               ?}
            ?}
         ?}
      ?};
      _loop:=_Sel.next()
   !};
   _Tab.cntx_pop();
   _Tab.sel_adel();
   DOKUM.cntx_pop();
   exec('end_kom','#message')
?};
0


\ksef_apply
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Przywróć do KSeF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);
{? _Tab=DOKUM || return(0) ?};

_continue:=1;

_Sel:=_Tab.sel_aget();
{? ~_Sel.first() || _Sel.REF:=#_Tab.ref(); _Sel.add() ?};
_continue:=
   {? _Sel.first() & _Sel.next()
   || FUN.ask('Czy przywrócić dokumenty do KSeF?'@)
   || FUN.ask('Czy przywrócić dokument do KSeF?'@)
   ?};
{? _continue>0
||
   exec('ini_kom','#message','Wynik zmiany status dokumentów KSeF');
   DOKUM.cntx_psh();
   _Tab.cntx_psh();
   _loop:=_Sel.first();
   {!
   |? _loop
   |!
      {? _Tab.seek(_Sel.REF,)
      ||
         _bl_dokum_seek:=0;
         _txt:=
            {? _Tab=DOKUM || DOKUM.KR_OP
            |? exec('bl_dokum_seek','zbl',_Tab) || _bl_dokum_seek:=1; DOKUM.KR_OP
            |? _Tab=FAKS || FAKS.SYM
            || ''
            ?};
         {? _Tab=DOK || _txt:=DOK.ODD().OD+'|'+DOK.REJ().KOD+'|'+$DOK.NR+'|'+DOK.NK ?};
         _statksef:=
            {? _bl_dokum_seek
            || exec('FindAndGet','#table',ref_tab(DOKUM.REFSQL),DOKUM.REFSQL,,"STATKSEF",'')
            || _Tab.STATKSEF
            ?};
         {? _statksef<>exec('ksef_donot_applay','zbl_stat')
         ||
            exec('add_kom','#message','Opcja dostępna dla dokumentu o statusie KSeF:',7,_txt);
            exec('add_kom','#message','- %1'[exec('ksef_donot_applay','zbl_stat')],7,_txt)
         |? _Tab=DOK & (DOK.ZP='T' | DOK.ZK='T')
         ||  exec('add_kom','#message','Opcja niedostępna dla zaksięgowanego dokumentu.',7,_txt)
         ||
            _Tab.MANSKSEF:='N';
            {? _Tab=DOK
            || {? 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'
               || FAKS.cntx_psh();
                  FAKS.clear();
                  FAKS.use('faktu'+(3+(1-DOK.DOKZRODL)));
                  {? FAKS.seek(#(4-DOK.DOKZRODL),FAKS.name)
                  || FAKS.MANSKSEF:='N';
                     FAKS.put()
                  ?};
                  FAKS.cntx_pop()
               ?};
               exec('set_dok_ksef','dok_ksef',1)
            ?};
            {? _Tab.put()
            || {? _Tab.STATKSEF=exec('ksef_donot_applay','zbl_stat')
               || exec('add_kom','#message','Przywrócenie do KSeF niedostępne ponieważ dokument nie podlega KSeF.',7,_txt)
               |? _Tab=FAKS
               || exec('set_dok_ksef_apply','zbl_dok')
               ?}
            ?}
         ?}
      ?};
      _loop:=_Sel.next()
   !};
   _Tab.cntx_pop();
   _Tab.sel_adel();
   DOKUM.cntx_pop();
   exec('end_kom','#message')
?};
0


\bl_act_map
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Dokument Businesslink - Mapowanie indeksu
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b;

_result:=0;

_continue:=1;

DOKUM.cntx_psh();
DOKUM.prefix();
{? ~DOKUM.seek(_dokum)
||
   ~~

||
   {? exec('czy_ksef','edi_fo_ufd',DOKUM.ref())
      & exec('czy_ufd','edi_fo_ufd',DOKUM.ref())
   ||
:: Ścieżka UFD
      _edi:=exec('env','edi_wspolne');
      _edi.sza:={? _sza || 1 || 2 ?};
      {? ~_edi.init()
      ||
         FUN.info('Nie powiodło się zainicjowanie środowiska.'@)
      ||
         _edi.context:=exec('context_obj_bl','edi_wspolne');
         _edi.context.DOKUM:=_dokum;
         _edi.context.OCR:=exec('czy_ufd_z_ocr','edi_fo_ufd',DOKUM.ref());
         _edi.context.VERIFY_ONLY:=1;
         _istdef:=exec('istdef','zbl_dok',_edi.context);
         {? _istdef=null()
         ||
            FUN.info('Nie ustalono definicji komunikatu.'@);
            _continue:=0
         ||
            DOKUM.cntx_psh();
            exec('edi_read','edi_wspolne',_istdef,0);
            DOKUM.cntx_pop();
            VAR_DEL.delete('__Map');
            __Map:=_edi.context.MAP
         ?}
      ?}
   ?}
?};
DOKUM.cntx_pop();

{? _continue>0
||
   _result:=1
?};
_result


\set_dok_ksef_apply
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Przywraca do ksef dokument księgowy podczas przywracania faktury
::----------------------------------------------------------------------------------------------------------------------
_maska:=exec('maska','dok_fks_aut_dok',{? FAKS.T().KOR<>'Z' || 'F' || 'K' ?})
             +{? FAKS.T().KOR='Z' || form(#FAKS.ref,,,'99') || '' ?};
{? var_pres('X_Xa')>0 || obj_del(X_Xa) ?};
X_Xa:=sql('select DOK.REFERENCE as REF from @DOK join DOK_REJ
           where DOK.DOKZRODL = \':_a\' ',_maska);
{? X_Xa.first()
||
   _mask:=4+(4-X_Xa.REF);
   DOK.cntx_psh();
   DOK.use('doku'+_mask);
   DOK.clear();
   {? DOK.seek(X_Xa.REF,DOK.name)
   || DOK.MANSKSEF:='N';
      exec('set_dok_ksef','dok_ksef',1);
      DOK.put()
   ?};
   DOK.cntx_pop()
?};
obj_del(X_Xa);
1


\set_dok_ksef_donot_apply
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS:  Ustawienie statusu nie wysyłaj do ksef dokument księgowy podczas przywracania faktury
::----------------------------------------------------------------------------------------------------------------------
_maska:=exec('maska','dok_fks_aut_dok',{? FAKS.T().KOR<>'Z' || 'F' || 'K' ?})
             +{? FAKS.T().KOR='Z' || form(#FAKS.ref,,,'99') || '' ?};
{? var_pres('X_Xa')>0 || obj_del(X_Xa) ?};
X_Xa:=sql('select DOK.REFERENCE as REF from @DOK join DOK_REJ
           where DOK.DOKZRODL = \':_a\' ',_maska);
{? X_Xa.first()
||
   _mask:=4+(4-X_Xa.REF);
   DOK.cntx_psh();
   DOK.use('doku'+_mask);
   DOK.clear();
   {? DOK.seek(X_Xa.REF,DOK.name)
   || DOK.KSEFAUTH:=null();
      DOK.MANSKSEF:='T';
      exec('set_dok_ksef','dok_ksef',1);
      DOK.put()
   ?};
   DOK.cntx_pop()
?};
obj_del(X_Xa);
1


\typyzam_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [23.25]
:: OPIS: Wybór typów dokumentów sprzedaży lub zakupu z listy wszystkich typów
::   WE: [_a] D - zamówienie dostawy (domyślnie), Z - zamówienie sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_rodz:='D';
{? var_pres('_a')=type_of('') & (_a='D' | _a='Z')
|| _rodz:=_a
?};

_par0:='';
TYPYZAM.cntx_psh();
TYPYZAM.index('TYP');
TYPYZAM.prefix(_rodz);
{? TYPYZAM.first()
|| {!
   |? {? TYPYZAM.BL='T'
      || _par0+=TYPYZAM.T+' '
      ?};
      TYPYZAM.next()
   !}
?};
TYPYZAM.cntx_pop();

_war:=$('TYPYZAM.R=\''+_rodz+'\'');
_par:=exec('typyzam_wyb','params',_par0,_war);
{? _par<>_par0
|| TYPYZAM.cntx_psh();
   TYPYZAM.index('TYP');
   TYPYZAM.prefix(_rodz);
   {? TYPYZAM.first()
   || {!
      |? {? (' '+_par+' ')*(' '+TYPYZAM.T+' ')>0 & TYPYZAM.BL<>'T'
         || TYPYZAM.BL:='T';
            TYPYZAM.put()
         |? (' '+_par+' ')*(' '+TYPYZAM.T+' ')=0 & TYPYZAM.BL='T'
         || TYPYZAM.BL:='N';
            TYPYZAM.put()
         ?};
         TYPYZAM.next()
      !}
   ?};
   TYPYZAM.cntx_pop()
?};
~~


\typyzam_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [20.42]
:: OPIS: Typy zamówień klienta lub dostawy związanych z Businesslink
::   WE: [_a] D - zamówienie dostawy (domyślnie), Z - zamówienie sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_rodz:='D';
{? var_pres('_a')=type_of('') & (_a='D' | _a='Z')
|| _rodz:=_a
?};

{? _rodz='Z'
|| _tyt:='Typy zamówień sprzedaży'@
|| _tyt:='Typy zamówień dostaw'@
?};
_wer_td:=TYPYZAM.mk_sel(_tyt,,,,,,,,'U');
TYPYZAM.win_fld(_wer_td,,'T',,,10,,,'Typ'@);
TYPYZAM.win_fld(_wer_td,,'NAZ',,,30,,,'Nazwa'@);
_fb:=$('exec(\'typyzam_wyb\',\'zbl_dok\',\''+_rodz+'\')');
TYPYZAM.win_act(_wer_td,0,'Formuła','&Ustaw'@@,,,_fb,,1,,,,'U');
TYPYZAM.win_act(_wer_td,1,'Formuła','&Ustaw'@@,,,_fb,,1,,,,'U');
{? _rodz='D'
|| _fb:="params_exec('select','dp_wydrukow','TZ')";
   _fbg:="params_exec('dpw_grupa','dp_wydrukow','TZ')";
   TYPYZAM.win_act(_wer_td,,'Formuła','Domyślne parametry &wydruków'@@,,,_fb,,,1,_fbg,,'W')
?};

_win0:=TYPYZAM.win_sel('?');
TYPYZAM.cntx_psh();

TYPYZAM.index('BL1');
TYPYZAM.prefix('T',_rodz);
TYPYZAM.win_sel(_wer_td);
TYPYZAM.select();

TYPYZAM.cntx_pop();
TYPYZAM.win_sel(_win0);

~~


\czy_zam_edokument_bl_ufd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Czy wystawiać e-dokument UFD dla zamówienia dostawy?
::   WE: _a - KH.ref()
::       _b - TYPYZAM.ref()
::       _c - data wystawienia
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
KH_DOD.cntx_psh();
{? exec('kh_dod_ini','kontrahent',_a,0)
|| _return:=KH_DOD.EFAKTURA='B' & KH_DOD.BL_DATE<=_c
?};
KH_DOD.cntx_pop();
{? _return
|| _return:=exec('FindAndGet','#table',TYPYZAM,_b,,"TYPYZAM.BL='T'",0)
?};
_return


\czy_zam_edokument_bl_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Czy wystawiać e-dokument PDF dla zamówienia dostawy?
::   WE: _a - KH.ref()
::       _b - TYPYZAM.ref()
::       _c - data wystawienia
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
KH_DOD.cntx_psh();
{? exec('kh_dod_ini','kontrahent',_a,0)
|| _return:=KH_DOD.EFAKTURA='U' & KH_DOD.BL_DATE<=_c
?};
KH_DOD.cntx_pop();
{? _return
|| _return:=~exec('czy_zam_edokument_bl_ufd','zbl_dok',_a,_b,_c)
?};
_return


\rodzaje_zam_edokumentu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Zwraca rodzaje edokumentu zamówienia dostawy dla kontrahenta _a, typu zamówienia _b, od daty wystawienia _c
::   WE: _a - KH.ref()
::       _b - TYPYZAM.ref()
::       _c - data wystawienia
::   WY: |pdf|ufd|
::----------------------------------------------------------------------------------------------------------------------
_kh:=_a;
_typyzam:=_b;
_dw:=_c;
_result:='';
{? _kh & _typyzam
|| {? exec('czy_zam_edokument_bl_pdf','zbl_dok',_kh,_typyzam,_dw)
   || _result+='pdf|'
   ?};
   {? exec('czy_zam_edokument_bl_ufd','zbl_dok',_kh,_typyzam,_dw)
   || _result+='ufd|'
   ?};
   {? exec('czy_edokument_email','zbl_dok',_kh)
   || _result+='e-mail|'
   ?}
?};
{? +_result || '|'+_result || _result ?}


\kh_dokum_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Ustala kontrahenta dla DOKUM na podstawie zamówienia dostawy i zbudowanych relacji dla kontrahenta zamówienia
::   WE: _a - ZD_NAG.ref() / $ZD_NAG.ref()
::       _b - 0/1 - dane pobierać z bufora tabeli ZD_NAG
::   WY: KH.ref()
::----------------------------------------------------------------------------------------------------------------------
_zd_nag:=_a;
_buf:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_result:={? _buf || ZD_NAG.KH || exec('FindAndGet','#table',ZD_NAG,_zd_nag,,"ZD_NAG.KH",null()) ?};
_result


\bl_type_ord_dzk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Typ dokumentu Businesslink - ORD_DZK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'ORD_DZK'


\bl_log_view4zdnag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Podgląd log dokumentu Busineslink powiązanego z zamówieniem dostawy znajdującym się w buforze tabeli ZD_NAG
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_rodzaje_edokumentu:=exec('rodzaje_zam_edokumentu','zbl_dok',ZD_NAG.KH,ZD_NAG.T,ZD_NAG.DATA);
{? _rodzaje_edokumentu='' | _rodzaje_edokumentu='e-mail'
||
   FUN.info('Log dla dokumentu nie jest dostępny.'@)
||
   DOKUM.cntx_psh();
   {? exec('bl_dokum_seek','zbl',ZD_NAG)
   ||
      {? DOKUM.BL_LOG=null()
      ||
         FUN.info('Brak zapisu w log.'@)
      ||
         exec('edit_memo','#edit',exec('blob_txt_dl','#edit',DOKUM.BL_LOG),'Log'@,'E-dokument'@,180,25,,,0)
      ?}
   ||
      FUN.info('Log dla dokumentu nie jest dostępny.'@)
   ?};
   DOKUM.cntx_pop()
?}


\bl_act_ord
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Dokument Businesslink - Rejestruj zamówienie sprzedaży
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::       _b - 0/1 - bez komunikatów
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_sza:=_b;

_result:=0;

_continue:=1;

DOKUM.cntx_psh();
DOKUM.prefix();
{? ~DOKUM.seek(_dokum)
||
   ~~

|? DOKUM.BL_TYPE<>'ORD' & DOKUM.BL_TYPE<>'DOC'
||
   FUN.info('Nie można zarejestrować dokumentu typu "%1" jako zamówienie sprzedaży.'@[DOKUM.BL_TYPE]);
   _continue:=0

|? DOKUM.REFSQL='' & ~_sza & ~FUN.ask('Czy zarejestrować zamówienie sprzedaży?'@)
||
   _continue:=0

||
   {? DOKUM.BL_STAT=exec('during_registration','zbl_stat')
   ||
      _res:=exec('FindAndGet','#table',ZK_N,DOKUM.REFSQL,,"
         {? ZK_N.STAT_REJ='Z' | ZK_N.STAT_REJ='T'
         ||
            DOKUM.BL_STAT:=exec('registered','zbl_stat');
            DOKUM.put()
         ||
            -1
         ?}
      ");
      {? _res=0
      ||
::       nie udało się zmienić statusu na registred
         _continue:=0

      |? _res=-1
      ||
::       Rejestracja zamówienia
         {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
         ||
            exec('FindAndGet','#table',ZK_N,DOKUM.REFSQL,,"
               _params:=exec('mp_run_a','#b__box');
               _params.ACT_UID:='LSP_ZKN_DRZK';
               _params.AKCJA:='Popraw';
               _params.UIDREF:=ZK_N.uidref();
               __PARSES.setVal('OddzialLogProd',ZK_N.ODDZ);
               _args:=__PARSES.args('OkresRok');
               _args.OBSZAR:='LSP';
               _args.AR:=ZK_N.DP~1;
               _args.AM:=ZK_N.DP~2;
               __PARSES.setVal('OkresRok',_args);
:: zamówienie sprzedaży
               {? __PARSES.setEnv(_params.ACT_UID) || exec('mp_run','#b__box',_params) ?}
            ",'')
         ?}
      ?}

   |? DOKUM.BL_STAT=exec('registered','zbl_stat')
   ||
      _continue:=exec('FindAndGet','#table',ZK_N,DOKUM.REFSQL,,"
         {? ZK_N.STAT_REJ='N'
         ||
            @.DOKUM.BL_STAT:=exec('during_registration','zbl_stat');
            @.DOKUM.put()
         ||
            FUN.info('Zamówienie jest zarejestrowane.'@)
         ?}
      ");
      _continue:=0

   |? DOKUM.BL_STAT<>exec('ready_to_registration','zbl_stat')
         &
      DOKUM.BL_STAT<>exec('registration_failed','zbl_stat')
   ||
      FUN.info('Nie można zarejestrować dokumentu o statusie "%1".'@[DOKUM.BL_STAT]);
      _continue:=0

   |? exec('czy_ufd','edi_fo_ufd',DOKUM.ref())=0
   ||
      {? exec('czy_pdf','edi_fo_ufd',DOKUM.ref())=0
      ||
         FUN.info('Nie dostarczono dokumentu przychodzącego Businesslink w formacie UFD lub PDF.'@@);
         _continue:=0
      ||
:: Ścieżka PDF
         _context:=obj_new('BL','DOKUM','ODDZ','TYPYZAM','DP','KH');
         _context.BL:='T';
         _context.DOKUM:=DOKUM.ref();
         _continue:=exec('ord_manual','zbl_dok',_context);
         {? _continue>0
         ||
            _typzam:=_context.TYPYZAM;
            _act_uid:='LSP_ZKN_DRZK';
            __PARSES.setVal('OddzialLogProd',_context.ODDZ);
            _args:=__PARSES.args('OkresRok');
            _args.OBSZAR:='LSP';
            _args.AR:=_context.DP~1;
            _args.AM:=_context.DP~2;
            __PARSES.setVal('OkresRok',_args);
            {? __PARSES.setEnv(_act_uid)
            ||
               _params:=exec('mp_run_a','#b__box');
               _params.ACT_UID:=_act_uid;
               _params.AKCJA:='Dołącz';
               _params.PROC_START:='T';
               _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
               exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TYPYZAM',_typzam);
               _params.CONTEXT:=_context;
               exec('mp_run','#b__box',_params);
               {? DOKUM.KH=null()
               ||
                  DOKUM.KH:=exec('FindAndGet','#table',ZK_N,DOKUM.REFSQL,,"ZK_N.KH",null());
                  DOKUM.put()
               ?};
               exec('dokum_pow_zapisz_refsql','zbl_dok',@.DOKUM.ref(),1)
            ?}
         ?}
      ?}
   ||
:: Ścieżka UFD
::    Usnięcie wcześniejszego log'a dokumentu Businesslink
      DOKUM.BL_LOG:=null();
      DOKUM.put();
      _edi:=exec('env','edi_wspolne');
      _edi.sza:={? _sza || 1 || 2 ?};
      {? ~_edi.init()
      ||
         FUN.info('Nie powiodło się zainicjowanie środowiska.'@)
      ||
         ISTDEF.cntx_psh();
         ISTDEF.index('BL_DATA');
         ISTDEF.prefix(__Firma,'ZWS','E',DOKUM.BL_TYPE,'T','N',);
         _istdef:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?};
         ISTDEF.cntx_pop();
         {? _istdef=null()
         ||
            FUN.info('Nie ustalono definicji komunikatu.'@);
            _continue:=0
         ||
            _edi.context:=exec('context_obj_bl','edi_wspolne');
            _edi.context.DOKUM:=_dokum;
            _edi.context.OCR:=exec('czy_ufd_z_ocr','edi_fo_ufd',DOKUM.ref());
            exec('dokum2pilki','edi_wspolne',_dokum);
::          dodatkowy zapis w dzienniku inforumujący o starcie odczytu lub zapisu edi
            _dzien_a:=exec('dzien_add','edi_wspolne',_istdef,'R',date(),time(),OPERATOR.USER,'LOG',''
               ,null(),'Start','',,,'B');
            DOKUM.cntx_psh();
            exec('edi_read','edi_wspolne',_istdef);
            DOKUM.cntx_pop();
::          (_out.LOG) - zapisanie na wyjściu komunikatów wygenerowanych podczas działania czynności
            _edi.logSave(_dzien_a);
::          aktualizacja log
            {? DOKUM.get() || DOKUM.bl_put('BL_LOG',_edi.LOG,,,'log.txt') ?};
            {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
            ||
               _zkn:=exec('FindAndGet','#table',ZK_N,DOKUM.REFSQL,,,null());
               _typzam:=exec('FindAndGet','#table',ZK_N,DOKUM.REFSQL,,"ZK_N.T",null());
               _params:=exec('mp_run_a','#b__box');
               _params.ACT_UID:='LSP_ZKN_DRZK';
               _params.AKCJA:='Businesslink';
               _params.PROC_START:='T';
::             _params.QUIET:='T';
               _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
               _akr:='ZK_N';
               exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,_akr,_zkn);
               exec('mp_run','#b__box',_params)
            ?}
         ?}
      ?}
   ?}
?};
DOKUM.cntx_pop();

{? _continue>0
||
   _result:=1
?};
_result


\ord_manual
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Dokument Businesslink - Rejestruj zamówienie sprzedaży - ręcznie
::   WE: _a - context
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_context:=_a;
_result:=1;
:: wartości początkowe
ZK_N.ODDZ:=ST.ODDZ;
{? ST.ODDZ='' & BPMN.B_DOMAIN().SYMBOL='ZBL'
||
   _oddz:=exec('get','#params',100362);
   ZK_N.ODDZ:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,_oddz,"ODDZ.KOD",1,,'')
?};
ODDZ.NAZ:={? ZK_N.ODDZ='' || '' || ODDZ.NAZ:=exec('FindInSet','#table','ODDZ','KOD2',ZK_N.ODDZ,,"ODDZ.NAZ",1,,'') ?};
ZK_N.DP:=DOKUM.DATA;
ZK_N.T:=null();
ZK_N.KH:=DOKUM.KH;
ZK_N.ZAM_KL:='';
:: typy dokumentów
TYPYZAM.cntx_psh();
TYPYZAM.fld_fml('R','BLANK',"'Z'");
TYPYZAM.fld_fml('BL','BLANK',"'T'");
TYPYZAM.win_dict('SEL');
TYPYZAM.actions('SEL','W');
_ff:="";
ZK_N.cntx_psh();
:: oddział
ZK_N.fld_fml('ODDZ','BEFORE_EDIT',{? BPMN.B_DOMAIN().SYMBOL='LSP' || "0" || "1" ?});
_ff:="
   _result:=ZK_N.ODDZ;
   ODDZ.prefix();
   ODDZ.f_clear();
   ODDZ.f_set('KOD,NAZ'
      ,'join USERS_UP'
      ,'USERS_UP.USERS=:_a and USERS_UP.AKR=''ODDZ'''
      ,OPERATOR.USER);

   ODDZ.win_sel('SEL');
   {? ODDZ.select()
   || _result:=ODDZ.KOD
   ?};
   ODDZ.f_clear();
   _result
";
ZK_N.fld_fml('ODDZ','F3',_ff);
_ff:="
   ZK_N.ODDZ:=exec('FindInSet','#table','ODDZ','KOD2',ZK_N.ODDZ,,\"ODDZ.KOD\",1,,'');
   ODDZ.NAZ:=exec('FindInSet','#table','ODDZ','KOD2',ZK_N.ODDZ,,\"ODDZ.NAZ\",1,,'')
";
ZK_N.fld_fml('ODDZ','AFTER_EDIT',_ff);
:: data wystawienia
_ff:="";
ZK_N.fld_fml('DP','BEFORE_EDIT',_ff);
ZK_N.fld_fml('DP','AFTER_EDIT',_ff);
:: kontrahent
ZK_N.fld_fml('KH','BEFORE_EDIT',_ff);
ZK_N.fld_fml('KH','AFTER_EDIT',_ff);
ZK_N.fld_fml('KH','BEFORE_DISPLAY',_ff);
::
ZK_N.win_edit('BL');
{? ~ZK_N.edit("__CHK.record(ZK_N,,'ODDZ','T','DP','KH')")
||
   _result:=0
||
   _context.ODDZ:=ZK_N.ODDZ;
   _context.TYPYZAM:=ZK_N.T;
   _context.DP:=ZK_N.DP;
   _context.KH:=ZK_N.KH
?};
TYPYZAM.fld_fml('R','BLANK',"*");
TYPYZAM.fld_fml('BL','BLANK',"*");
TYPYZAM.actions('SEL','');
ZK_N.fld_fml('ODDZ','BEFORE_EDIT',"*");
ZK_N.fld_fml('ODDZ','AFTER_EDIT',"*");
ZK_N.fld_fml('DP','BEFORE_EDIT',"*");
ZK_N.fld_fml('DP','AFTER_EDIT',"*");
ZK_N.fld_fml('KH','BEFORE_EDIT',"*");
ZK_N.fld_fml('KH','AFTER_EDIT',"*");
ZK_N.fld_fml('KH','BEFORE_DISPLAY',"*");
ZK_N.cntx_pop();
TYPYZAM.cntx_pop();
_result


\winOrd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Zakładka dokumenty Businesslink w obszarze Zamówienia sprzedaży
::   WE: _a - akronim okna grupowego
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_grp:=_a;
{? exec('get','#params',3017,2,OPERATOR.USER)='N' || return() ?};
BPMN.B_DOMAIN:=exec('FindInSet','#table','B_DOMAIN','SYMBOL','LSP',,,1,,null());
::
:: Przychodzące - okno wertowania
::
_wer_in:=exec('wer_in','zbl_dok','LSP_ZKN');
::
:: log
::
exec('ctr_log_init','zbl_dok');
::
:: Podgląd dokumentu - okno wertowania
::
exec('web_view','zbl_dok');
::
:: Zakładka Przychodzące Businesslink
::
:: przychodzące
::
_far_in:=$('
   _fget:=DOKUM.get();
   {? _fget & DOKUM.f_active()>=2
   || _fget:=DOKUM.f_test()
   ?};
   {? ~_fget || DOKUM.DOKUMI:=null() ?};
   exec(''bl_log_html'',''zbl_dok'');
   exec(''web_view_load'',''zbl_dok'',~_fget)'
);
_fb:="
   DOKUM.index('BL_ORD');
   DOKUM.prefix('T',REF.FIRMA,'P','T')
";
ZK_N.grp_sel(_win_grp,DOKUM,_wer_in,'Przychodzące Businesslink'@,_far_in,,,,_fb,,,,'maximized','bl_wer_in');
task_attach('ZBL_DOK_PBUF');
task_attach('ZBL_DOK_RBUF');
::
:: log
::
ZK_N.tab_splt(_win_grp,'','horizontal','log',16);
_fb:="exec('bl_log_html','zbl_dok')";
_fa:="";
ZK_N.grp_ctr(_win_grp,DOKUM,__CTH.WIN_ACR,,,,,,80,,_fb,_fa,,'maximized_with_title');
::
:: podgląd załącznika
::
ZK_N.tab_splt(_win_grp,,'vertical','view');
ZK_N.grp_ctr(_win_grp,DOKUM,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,,,,,100,50,"","",,'maximized');
::
DOKUM.win_edit('REDK')


\czy_zak_powinien_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Czy faktura zakupu powinna być przyjęta jako e-dokument KSeF?
::   WE: _a - KH.ref()
::       _b - TYPYSP.ref()
::       _c - data wystawienia
::       [_d] - czy pokazać ostrzeżenie [0]/1
::   WY: 0 - nie / 1 - tak
::----------------------------------------------------------------------------------------------------------------------
_d:={? var_pres('_d')=type_of(0) || _d || 0 ?};
_return:=0;
KH_DOD.cntx_psh();
exec('kh_dod_ini','kontrahent',_a,0);
_return:=KH_DOD.KSEFLI='T' & KH_DOD.KSEFLIOD<=_c;
KH_DOD.cntx_pop();
{? _return
|| _return:=exec('FindAndGet','#table',TYPYSP,_b,,"TYPYSP.BL='T' & TYPYSP.ZAK='T' & TYPYSP.KOR<>'Z'",0)
?};
{? _return & _d
|| _tp:=exec('FindAndGet','#table',TYPYSP,_b,,"T",'');
   _kh_naz:=exec('FindAndGet','#table',KH,_a,,"NAZ",'');
   _msg:='Dokument zakupu %1 od kontrahenta %2 powinien być przyjęty z KSeF.'@[_tp,_kh_naz];
   FUN.info(_msg)
?};
_return


\bl_act_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Dokument Businesslink - Powiąż
::   WE: _a - DOKUM.ref() dokumentu Businesslink
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;

_result:=0;
DOKUM.cntx_psh();
DOKUM.prefix();

{? ~DOKUM.seek(_dokum)
|| ~~

|? DOKUM.BL_STAT<>exec('ready_to_registration','zbl_stat') & DOKUM.BL_STAT<>exec('registration_failed','zbl_stat')
|| FUN.info('Powiązanie z innym dokumentem dostępne dla dokumentów o statusach:'
      ' %1, %2.'@[exec('ready_to_registration','zbl_stat'),exec('registration_failed','zbl_stat')])

|? exec('dokum_pow_pod_czy','zbl_dok')
|| FUN.info('Dokument ma już powiązane podrzędne dokumenty.'@)

|? DOKUM.BL_OCR<>''
|| FUN.info('Dokument do OCR.'@)

|| _kh:=exec('FindAndGet','#table',DOKUM,_dokum,,"KH",'');
   exec('psh_ctr','zbl_dok');
   exec('psh_ctrwb','zbl_dok');
   exec('psh_cth','zbl_dok');
   _win_wyb:=exec('win_powi','zbl_dok',1);
   _win0:=DOKUM.win_sel('?');
   DOKUM.win_sel(_win_wyb);
   _where:='KH = \''+$_kh+'\' and FIRMA=\''+$REF.FIRMA+'\' and BL=\'T\' and RODZAJ_K=\'P\' and DG_UID=\'\''+
           ' and REFERENCE<>\''+$_dokum+'\' and (BL_STAT=\''+exec('ready_to_registration','zbl_stat')+
           '\' or BL_STAT=\''+exec('registration_failed','zbl_stat')+'\' or DOKUM.BL_STAT=\''+
           exec('during_registration','zbl_stat')+'\' or DOKUM.BL_STAT=\''+exec('registered','zbl_stat')+
           '\') and (DOKUM.REFSQL=\'\' or DOKUM.REFSQL like \'faktu%\' or DOKUM.REFSQL like \'zknag%\')';
   _order:='DATA^,CZAS^';
   DOKUM.prefix();
   DOKUM.f_set(_order,,_where);
   DOKUM.f_first();
   _uid:='';
   _drefsql:='';
   {? DOKUM.select()
   || _uid:=DOKUM.uidref();
      _drefsql:=DOKUM.REFSQL
   ?};
   DOKUM.f_clear();
   DOKUM.win_sel(_win0);
   exec('pop_ctr','zbl_dok');
   exec('pop_ctrwb','zbl_dok');
   exec('pop_cth','zbl_dok');
   {? _uid<>'' & DOKUM.seek(_dokum)
   || DOKUM.DG_UID:=_uid;
      DOKUM.REFSQL:=_drefsql;
      DOKUM.BL_STAT:=exec('abandoned','zbl_stat');
      _result:=DOKUM.put()
   ?}

?};

DOKUM.cntx_pop();

_result


\powi_pokaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Dokument Businesslink - Pokaż powiązane
::   WY:
::----------------------------------------------------------------------------------------------------------------------

{? DOKUM.DG_UID='' & ~exec('dokum_pow_pod_czy','zbl_dok')
|| FUN.info('Dokument nie jest powiązany z innymi dokumentami.'@);
   return(~~)
?};

{? DOKUM.DG_UID=''
|| _duid:=DOKUM.uidref();
   _uref:=DOKUM.ref()
|| _duid:=DOKUM.DG_UID;
   _uref:=exec('FindAndGet','#table',DOKUM,_duid,,,null())
?};

DOKUM.cntx_psh();
{? DOKUM.name()<>'dokum'
|| DOKUM.use('dokum')
?};
exec('psh_ctr','zbl_dok');
exec('psh_ctrwb','zbl_dok');
exec('psh_cth','zbl_dok');

_win_powi:=exec('win_powi','zbl_dok',0);
_win0:=DOKUM.win_sel('?');
DOKUM.win_sel(_win_powi);
_where:='REFERENCE=\''+$_uref+'\' or DG_UID= \''+_duid+'\'';
_order:='DG_UID,DATA^,CZAS^';
DOKUM.prefix();
DOKUM.f_set(_order,,_where);
DOKUM.select();
DOKUM.f_clear();
DOKUM.win_sel(_win0);

exec('pop_ctr','zbl_dok');
exec('pop_ctrwb','zbl_dok');
exec('pop_cth','zbl_dok');
DOKUM.cntx_pop();

~~


\win_powi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Okno wyboru dokumentu przychodzącego Businesslink
::   WE: [_a] - 0 - podgląd (domyślnie), 1 - wybór
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wybor:={? var_pres('_a')=type_of(0) || _a || 0 ?};

{? _wybor
|| _tyt:='Przychodzące Businesslink niepowiązane'@
|| _tyt:='Przychodzące Businesslink powiązane'@
?};
_win_grp:=DOKUM.grp_make(_tyt);

:: Przychodzące - okno wertowania
_wer_in:=exec('wer_in','zbl_dok','ZBL_DOK',{? _wybor || 1 || 2 ?});

:: załączniki
exec('ctr_zal_init','zbl_dok','DOKUMZ');

:: log
exec('ctr_log_init','zbl_dok');

:: Podgląd dokumentu - okno wertowania
exec('web_view','zbl_dok');

:: przychodzące
_far_in:=$('
   {? DOKUM.sel_size()=0
         &
      {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
   ||
      tab_show(0,''atta'');
      tab_show(1,''view'');
      exec(''dokum_rfr'',''bloby'','':%1'',''%2'');
      exec(''bl_log_html'',''zbl_dok'');
      exec(''web_view_load'',''zbl_dok'')
   ||
      tab_hide(0,1,''atta'');
      exec(''web_view_load'',''zbl_dok'',1);
      tab_hide(1,,''view'')
   ?}
   '[__CTR.WIN_ID,__CTR.WIN_ACR]
);
_fb:="";
{? _wybor
|| _tyt:='Przychodzące Businesslink niepowiązane'@
|| _tyt:='Przychodzące Businesslink powiązane'@
?};
DOKUM.grp_sel(_win_grp,DOKUM,_wer_in,_tyt,_far_in,,,,_fb,,,,'maximized','bl_wer_in');

:: podgląd załącznika
DOKUM.grp_splt(_win_grp,'panel0','vertical','view',',60%');
DOKUM.grp_ctr(_win_grp,,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,'Podgląd'@,,,,100,50,"","",,'maximized');

:: załączniki
DOKUM.grp_splt(_win_grp,'panel0','horizontal','atta',20);
_fb:="
   __CTR.PODGLAD:=-1;
   {? DOKUM.sel_size()=0
         &
      {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
         &
      DOKUM.RODZAJ_K='W'
   || __CTR.PODGLAD:=0
   ?};
   exec('bobs_dokumz','bloby')
";
_fa:="";
DOKUM.grp_ctr(_win_grp,DOKUMZ,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_fb,_fa,,'maximized');

:: log
_fb:="exec('bl_log_html','zbl_dok')";
_fa:="";
DOKUM.grp_ctr(_win_grp,,__CTH.WIN_ACR,,'Log'@,,,,80,,_fb,_fa,,'maximized');

_win_grp


\psh_ctr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Zapamiętanie __CTR
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__CTR')>0
|| {? var_pres('__CTR_N')<0
   || __CTR_N:=0
   ?};
   __CTR_N+=1;
   _ctr_naz:='__CTR'+$__CTR_N;
   _fml:="
     VAR_DEL.delete('"+_ctr_naz+"');
     "+_ctr_naz+":=obj_new('BTAB', 'BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','BLOKUJ','PODGLAD','WIN');
     "+_ctr_naz+".BTAB:=__CTR.BTAB;
     "+_ctr_naz+".BBLOB:=__CTR.BBLOB;
     "+_ctr_naz+".TAB:=__CTR.TAB;
     "+_ctr_naz+".WIN_ID:=__CTR.WIN_ID;
     "+_ctr_naz+".WIN_ACR:=__CTR.WIN_ACR;
     "+_ctr_naz+".DIR_SEP:=__CTR.DIR_SEP;
     "+_ctr_naz+".DND:=__CTR.DND;
     "+_ctr_naz+".BLOKUJ:=__CTR.BLOKUJ;
     "+_ctr_naz+".PODGLAD:=__CTR.PODGLAD;
     "+_ctr_naz+".WIN:=__CTR.WIN";
   _fml:=$_fml;
   _fml();
   VAR_DEL.delete('__CTR')
?};
~~


\pop_ctr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Odczyt zapamiętanego __CTR
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__CTR_N')>0 & __CTR_N>0
|| _ctr_naz:='__CTR'+$__CTR_N;
   {? var_pres(_ctr_naz)>0
   || VAR_DEL.delete('__CTR');
      __CTR:=obj_new('BTAB', 'BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','BLOKUJ','PODGLAD','WIN');
      _fml:="
        __CTR.BTAB:="+_ctr_naz+".BTAB;
        __CTR.BBLOB:="+_ctr_naz+".BBLOB;
        __CTR.TAB:="+_ctr_naz+".TAB;
        __CTR.WIN_ID:="+_ctr_naz+".WIN_ID;
        __CTR.WIN_ACR:="+_ctr_naz+".WIN_ACR;
        __CTR.DIR_SEP:="+_ctr_naz+".DIR_SEP;
        __CTR.DND:="+_ctr_naz+".DND;
        __CTR.BLOKUJ:="+_ctr_naz+".BLOKUJ;
        __CTR.PODGLAD:="+_ctr_naz+".PODGLAD;
        __CTR.WIN:="+_ctr_naz+".WIN;
        VAR_DEL.delete('"+_ctr_naz+"')";
      _fml:=$_fml;
      _fml()
   ?};
   __CTR_N-=1
?};
~~


\psh_ctrwb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Zapamiętanie __CTR_WB
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__CTR_WB')>0
|| {? var_pres('__CTRWB_N')<0
   || __CTRWB_N:=0
   ?};
   __CTRWB_N+=1;
   _ctrwb_naz:='__CTR_WB'+$__CTRWB_N;
   _fml:="
     VAR_DEL.delete('"+_ctrwb_naz+"');
     "+_ctrwb_naz+":=obj_new('WIN_ACR','WIN_ID','DOKUMI');
     "+_ctrwb_naz+".WIN_ACR:=__CTR_WB.WIN_ACR;
     "+_ctrwb_naz+".WIN_ID:=__CTR_WB.WIN_ID;
     "+_ctrwb_naz+".DOKUMI:=__CTR_WB.DOKUMI";
   _fml:=$_fml;
   _fml();
   VAR_DEL.delete('__CTR_WB')
?};
~~


\pop_ctrwb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Odczyt zapamiętanego __CTR_WB
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__CTRWB_N')>0 & __CTRWB_N>0
|| _ctrwb_naz:='__CTR_WB'+$__CTRWB_N;
   {? var_pres(_ctrwb_naz)>0
   || VAR_DEL.delete('__CTR_WB');
      __CTR_WB:=obj_new('WIN_ACR','WIN_ID','DOKUMI');
      _fml:="
        __CTR_WB.WIN_ACR:="+_ctrwb_naz+".WIN_ACR;
        __CTR_WB.WIN_ID:="+_ctrwb_naz+".WIN_ID;
        __CTR_WB.DOKUMI:="+_ctrwb_naz+".DOKUMI;
        VAR_DEL.delete('"+_ctrwb_naz+"')";
      _fml:=$_fml;
      _fml()
   ?};
   __CTRWB_N-=1
?};
~~


\psh_cth
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Zapamiętanie __CTH
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__CTH')>0
|| {? var_pres('__CTH_N')<0
   || __CTH_N:=0
   ?};
   __CTH_N+=1;
   _cth_naz:='__CTH'+$__CTH_N;
   _fml:="
     VAR_DEL.delete('"+_cth_naz+"');
     "+_cth_naz+":=obj_new('ctr_id','FILEPATH','FML_SAVE','FML_CONFIG','WIN_ACR','GRP','RESULT','CLOSE','ACTIVE');
     "+_cth_naz+".ctr_id:=__CTH.ctr_id;
     "+_cth_naz+".FILEPATH:=__CTH.FILEPATH;
     "+_cth_naz+".FML_SAVE:=__CTH.FML_SAVE;
     "+_cth_naz+".FML_CONFIG:=__CTH.FML_CONFIG;
     "+_cth_naz+".WIN_ACR:=__CTH.WIN_ACR;
     "+_cth_naz+".GRP:=__CTH.GRP;
     "+_cth_naz+".RESULT:=__CTH.RESULT;
     "+_cth_naz+".CLOSE:=__CTH.CLOSE;
     "+_cth_naz+".ACTIVE:=__CTH.ACTIVE";
   _fml:=$_fml;
   _fml();
   VAR_DEL.delete('__CTH')
?};
~~


\pop_cth
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Odczyt zapamiętanego __CTH
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__CTH_N')>0 & __CTH_N>0
|| _cth_naz:='__CTH'+$__CTH_N;
   {? var_pres(_cth_naz)>0
   || VAR_DEL.delete('__CTH');
      __CTH:=obj_new('ctr_id','FILEPATH','FML_SAVE','FML_CONFIG','WIN_ACR','GRP','RESULT','CLOSE','ACTIVE');
      _fml:="
        __CTH.ctr_id:="+_cth_naz+".ctr_id;
        __CTH.FILEPATH:="+_cth_naz+".FILEPATH;
        __CTH.FML_SAVE:="+_cth_naz+".FML_SAVE;
        __CTH.FML_CONFIG:="+_cth_naz+".FML_CONFIG;
        __CTH.WIN_ACR:="+_cth_naz+".WIN_ACR;
        __CTH.GRP:="+_cth_naz+".GRP;
        __CTH.RESULT:="+_cth_naz+".RESULT;
        __CTH.CLOSE:="+_cth_naz+".CLOSE;
        __CTH.ACTIVE:="+_cth_naz+".ACTIVE;
        VAR_DEL.delete('"+_cth_naz+"')";
      _fml:=$_fml;
      _fml()
   ?};
   __CTH_N-=1
?};
~~


\dokum_pow_pod_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Sprawdzenie, czy dokument Businesslink ma powiązane dokumenty podrzędne
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
DOKUM.cntx_psh();
_uid:=DOKUM.uidref();
{? exec('FindInSet','#table','DOKUM','DG_UID',_uid,,,1,,null()) <> null()
|| _jest:=1
?};
{? ~_jest
|| _names:=DOKUM.names();
   {? _names.last()
   || {!
      |? {? _names.NAME<>'dokum'
         || DOKUM.use(_names.NAME);
            {? exec('FindInSet','#table','DOKUM','DG_UID',_uid,,,1,,null()) <> null()
            || _jest:=1
            ?}
         ?};
         ~_jest & _names.prev()
      !}
   ?}
?};
DOKUM.cntx_pop();
_jest


\dokum_pow_zapisz_refsql
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Dokument Businesslink - zapis DOKUM.REFSQL w powiązanych dokumentach podrzędnych
::   WE: [_a] - DOKUM.ref() dokumentu Businesslink
::       [_b] - zapis również w archiwum 0 - nie (domyślnie), 1 - tak
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_dokum:={? var_pres('_a')=type_of(null()) || _a || DOKUM.ref() ?};
_arch:={? var_pres('_b')=type_of(0) || _b || 0 ?};

DOKUM.cntx_psh();
DOKUM.prefix();

{? DOKUM.seek(_dokum)

|| _uid:=DOKUM.uidref();
   _drefsql:=DOKUM.REFSQL;
   DOKUM.index('DG_UID');
   DOKUM.prefix(_uid);
   {? DOKUM.first()
   || {!
      |? {? DOKUM.REFSQL<>_drefsql
         || DOKUM.REFSQL:=_drefsql;
            DOKUM.put(1)
         ?};
         DOKUM.next()
      !}
   ?};

   {? _arch
   || _names:=DOKUM.names();
      {? _names.last()
      || {!
         |? {? _names.NAME<>'dokum'
            || DOKUM.use(_names.NAME);
               DOKUM.index('DG_UID');
               DOKUM.prefix(_uid);
               {? DOKUM.first()
               || {!
                  |? {? DOKUM.REFSQL<>_drefsql
                     || DOKUM.REFSQL:=_drefsql;
                        DOKUM.put(1)
                     ?};
                     DOKUM.next()
                  !}
               ?}
            ?};
            _names.prev()
         !}
      ?}
   ?}

?};

DOKUM.cntx_pop();
~~


\ksef_sign
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Podpisanie dokumentu KSeF wymagane dla wysyłki wsadowej
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);

DOKUM.cntx_psh();

_symbol:={? _Tab=FAKS || FAKS.SYM |? _Tab=DOK || DOK.NK || DOKUM.SYM ?};

{? _Tab=DOKUM | exec('bl_dokum_seek','zbl',_Tab)
|| {? DOKUM.RODZAJ_K<>'W'
   || FUN.info('E-dokument do %1 nie jest \'wychodzący\'.'@[_symbol])
   || {? DOKUM.KSEF2SGN='T'
      || {? DOKUM.KSEFXSGN=null()
         || FUN.info('Brak dokumentu inicjującego podpis.'@)
         || exec('send_batch_sign','businesslink3')
         ?}
      |? DOKUM.KSEF2SGN='S'
      || FUN.info('Dokument został już podpisany.'@)
      || FUN.info('Podpisanie dokumentu nie jest wymagane.'@)
      ?}
   ?}
|| FUN.info('E-dokument do %1 nie został wygenerowany.'@[_symbol])
?};

DOKUM.cntx_pop();
~~


\schemat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Dokument Businesslink - ustalenie wersji dokumentu
::   WE: _a - DOKUM.ref() - dokumentu Businesslink
::       [_b] - DOKUMZ.ref() - załącznik Businesslink
::       [_c] - context
::   WY: schemat
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_dokumz:={? var_pres('_b')=type_of(null()) || _b || null() ?};
_context:={? var_pres('_c')>0 || _c || ~~ ?};

_result:='';

DOKUM.cntx_psh();
DOKUM.prefix();
{? _dokumz=null() & ~DOKUM.seek(_dokum)
||
   ~~
||
   {? _dokumz=null() || _dokumz:=exec('dokumz','edi_fo_ufd',DOKUM.ref(),'ufd') ?};
   DOKUMZ.cntx_psh();
   DOKUMZ.prefix();
   {? DOKUMZ.seek(_dokumz)
   ||
      _xml:=fopen(DOKUMZ.DOKUM,'b',0,,1);
      {? _xml.is_open()
      ||
         VAR_DEL.delete('__TP');
         __TP:=obj_new('DEBUG','PTH','TYPE','VER');
         __TP.DEBUG:=0;
         __TP.PTH:='';
         __TP.TYPE:='';
         __TP.VER:='';
         xml_sax_parse(
:: xml
             _xml
:: start document
            ,  "
               {? __TP.DEBUG || FUN.info('start document'[]) ?};
               1
               "
:: end document
            ,  "
               {? __TP.DEBUG || FUN.info('end document'[]) ?};
               1
               "
:: start element
            ,  "
               _continue:=1;
               {? __TP.DEBUG
               ||
                  FUN.info(
                     'start element:'
                     '\n_a - nazwa elementu: %1'
                     '\n_b - prefiks przestrzeni nazw: %2'
                     '\n_c - uri _b: %3'
                     '\n_d - tablica przestrzeni nazw: %4'
                     '\n_e - tablica z atrybutami: %5'[_a,_b,_c,$obj_len(_d),$obj_len(_e)]
                  )
               ?};
               __TP.PTH+='%1/'[_a];
:: KSEF
               {? __TP.PTH='Faktura/Naglowek/KodFormularza/'
               ||
                  _len:=obj_len(_e);
                  {! _ii:=1 // 4.._len-1
                  |!
                     {? _e[_ii]='kodSystemowy' || __TP.TYPE:=_e[_ii+3]
                     |? _e[_ii]='wersjaSchemy' || __TP.VER:=_e[_ii+3]; _continue:=0
                     ?}
                  !}
               ?};
               _continue
               "
:: end element
            ,  "
               {? __TP.DEBUG
               ||
                  FUN.info(
                     'end element:'
                     '\n_a - nazwa elementu: %1'
                     '\n_b - prefiks przestrzeni nazw: %2'
                     '\n_c - uri _b: %3'[_a,_b,_c]
                  )
               ?};
               __TP.PTH-=(+_a+1);
               1
               "
:: text
            ,  "
               _continue:=1;
               {? __TP.DEBUG || FUN.info('text (długość: %1): %2'[$+_a,_a]) ?};
               {? |_a='' || _a:='' ?};
:: UFD
               {? __TP.PTH='Document/DocumentType/' || __TP.TYPE:=_a
               |? __TP.PTH='Document/DocumentVersion/' || __TP.VER:=_a; _continue:=0
               ?};
               _continue
               "
:: comment
            ,  "
               {? __TP.DEBUG || FUN.info('comment: %1'[_a]) ?};
               1
               "
         );
         {? type_of(_context)<>type_of(~~)
         ||
            _context.BL_TYPE:=__TP.TYPE;
            _context.BL_VER:=__TP.VER
         ?};
         {? __TP.TYPE<>'' & __TP.VER<>''
         ||
            {? (3+__TP.TYPE)='INV' & __TP.VER='1.0' || _result:=''
            |? (3+__TP.TYPE)='INV' & __TP.VER='2.0' || _result:='UFD_2_0_Schema.xsd'
            |? __TP.TYPE='FA (1)' & __TP.VER='1-0E' || _result:='Schemat_FA_VAT(1)_v9-0E.xsd'
            |? __TP.TYPE='FA (2)' & __TP.VER='1-0E' || _result:='schemat_FA(2)_v1.xsd'
            ?}
         ?};
         VAR_DEL.delete('__TP')
      ?}
   ?};
   DOKUMZ.cntx_pop()
?};
DOKUM.cntx_pop();

_result


\istdef_ref4read
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Zwraca ISTDEF.ref definicji komunikatu dla odczytu
::   WE: _a - DOKUM.ref() - dokumentu Businesslink
::       [_b] - DOKUMZ.ref() - załącznik Businesslink
::       [_c] - context
::   WY: null/ISTDEF.ref
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_dokumz:={? var_pres('_b')=type_of(null()) || _b || null() ?};
_context:={? var_pres('_c')>0 || _c || ~~ ?};

_result:=null();

_schemat:=params_exec('schemat','zbl_dok',_dokum,_dokumz,_context);

DOKUM.cntx_psh();
DOKUM.prefix();
{? DOKUM.seek(_dokum)
||
   _bl_type:={? DOKUM.BL_TYPE='ORD' || DOKUM.BL_TYPE || DOKUM.BL_TYPE+'_DZK' ?};
   ISTDEF.cntx_psh();
   {? exec('upgrade2325_ksef2','zbl')
   ||
      ISTDEF.index('BL_DATA1');
      ISTDEF.prefix(__Firma,'ZWS','E',_bl_type,_schemat,'T','N',);
      _result:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?}
   ||
      ISTDEF.index('BL_DATA');
      ISTDEF.prefix(__Firma,'ZWS','E',_bl_type,'T','N',);
      _result:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?}
   ?};
   ISTDEF.cntx_pop()
?};
DOKUM.cntx_pop();

_result


\ksef_send_go
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [RR.xx]
:: OPIS: Wysyłanie sprawdzonego dokumentu do KSeF
::       (raczej nie będzie bezpośrednio używane)
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);
_selected:=(_Tab.sel_size()>0);
_symbol:={? _Tab=FAKS || FAKS.SYM |? _Tab=DOK || DOK.NK || DOKUM.SYM ?};

{? _Tab=DOK | _Tab=FAKS
|| {? ~_Tab.r_lock(1,1,1)
   || KOMM.msg('Dokument %1 jest zablokowany przez innego użytkownika.'@[_symbol],~_selected);
      _Tab.r_unlock();
      return()
   || _Tab.r_unlock()
   ?}
?};

DOKUM.cntx_psh();

{? _Tab=DOKUM | exec('bl_dokum_seek','zbl',_Tab)
|| {? DOKUM.RODZAJ_K<>'W'
   || KOMM.msg('E-dokument do %1 nie jest \'wychodzący\'.'@[_symbol],~_selected)
   || exec('DocumentSendGo','businesslink3',DOKUM.ref(),~_selected)
   ?}
|| KOMM.msg('E-dokument do %1 nie został wygenerowany.'@[_symbol],~_selected)
?};

DOKUM.cntx_pop();
~~


\ksef_send_go_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [RR.xx]
:: OPIS: Wysyłanie sprawdzonego dokumentu do KSeF - przed grupą zaznaczonych rekordów
::       (raczej nie będzie bezpośrednio używane)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy wysłać sprawdzone i zaznaczone dokumenty do KSeF?'@)
|| KOMM.init(,,'Wysyłanie sprawdzonych dokumentów do KSeF'@);
   1
|| 0
?}


\ksef_send_go_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [RR.xx]
:: OPIS: Wysyłanie sprawdzonego dokumentu do KSeF - po grupie zaznaczonych rekordów
::       (raczej nie będzie bezpośrednio używane)
::----------------------------------------------------------------------------------------------------------------------
KOMM.select();
~~


\ksef_send_check
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25_BLBC1]
:: OPIS: Wysyłanie dokumentu tylko do Businesscheck
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tab(1,1);
_selected:=(_Tab.sel_size()>0);
_symbol:={? _Tab=FAKS || FAKS.SYM |? _Tab=DOK || DOK.NK || DOKUM.SYM ?};
_is_dokum:=0;

:: Dodatkowe sprawdzenie tylko dla FAKS
{? _Tab=FAKS & FAKS.AKC='N'
|| KOMM.msg('Dokument nie zakończony \'%1\' nie podlega sprawdzaniu.'@[_symbol],~_selected);
   return()
:: Dodatkowe sprawdzenia daty dla DOK lub FAKS
|? _Tab=DOK & DOK.DOP<date(2016,7,1) | _Tab=FAKS & FAKS.D<date(2016,7,1)
|| KOMM.msg('Dokument z datą wcześniejszą niż %1 nie podlega sprawdzaniu.'@[$date(2016,7,1)],~_selected);
   return()
?};

:: Dodatkowe sprawdzenia, gdy wysyłka z poziomu faktur albo dokumentów księgowych
{? _Tab=DOK | _Tab=FAKS
|| {? _Tab.STATKSEF=exec('ksef_send_unconfirmed','zbl_stat') |
      _Tab.STATKSEF=exec('ksef_send_confirmed','zbl_stat') |
      _Tab.STATKSEF=exec('send_to_check','zbl_stat') |
      _Tab.STATKSEF=exec('send_checked','zbl_stat') |
      _Tab.STATKSEF=exec('verification_required','zbl_stat')
   || KOMM.msg('Dokument %1 o statusie \'%2\' nie podlega już sprawdzaniu.'@[_symbol,_Tab.STATKSEF],~_selected);
      return()
   |? FAKS.STATBC=exec('check_pending','zbl_stat')
   || KOMM.msg('Dokument %1 w trakcie kontroli nie podlega sprawdzaniu.'@[_symbol],~_selected);
      return()
   ?};
   {? ~_Tab.r_lock(1,1,1)
   || KOMM.msg('Dokument %1 jest zablokowany przez innego użytkownika.'@[_symbol],~_selected);
      _Tab.r_unlock();
      return()
   || _Tab.r_unlock()
   ?}
?};

DOKUM.cntx_psh();

__MANSKSEF:='BC';
{? _Tab=FAKS
||
   PARAM_W.JEZYK:=null();
   _czy_edokument:=1;
   _rodzaje_edokumentu:='|ksef|';
   _dokum:=
      exec('save_dok','dokum','FAKS',~~,,FAKS.SYM,
         {? FAKS.KH_ODB<>null() & FAKS.KH_ODB().KH_LNK<>null() & FAKS.KH_ODB().FV_EM='T'
         || {? FAKS.KH_ODB().EM<>'' || FAKS.KH_ODB().EM || '' ?}
         || FAKS.KH().EM
         ?},
         'Dokument o symbolu: '+FAKS.SYM+' wystawca: '+XINFO.NAZ,
         PARAM_W.JEZYK,
         FAKS.STAT_REJ,
         _czy_edokument,
         _rodzaje_edokumentu,
         1
      );
   {? _dokum || _is_dokum:=1 ?}
|? _Tab=DOK
||
   _dokum:=null();
   _dokum:=exec('dok_edokument','dok_fks1',0,1);
   {? _dokum=null() & 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'
   || FAKS.cntx_psh();
      FAKS.use('faktu'+(3+(1-DOK.DOKZRODL)));
      {? FAKS.seek(#(4-DOK.DOKZRODL),FAKS.name)
      || PARAM_W.JEZYK:=null();
         _czy_edokument:=1;
         _rodzaje_edokumentu:='|ksef|';
         _dokum:=
            exec('save_dok','dokum','FAKS',~~,,FAKS.SYM,
               {? FAKS.KH_ODB<>null() & FAKS.KH_ODB().KH_LNK<>null() & FAKS.KH_ODB().FV_EM='T'
               || {? FAKS.KH_ODB().EM<>'' || FAKS.KH_ODB().EM || '' ?}
               || FAKS.KH().EM
               ?},
               'Dokument o symbolu: '+FAKS.SYM+' wystawca: '+XINFO.NAZ,
               PARAM_W.JEZYK,
               FAKS.STAT_REJ,
               _czy_edokument,
               _rodzaje_edokumentu,
               1
            )
      ?};
      FAKS.cntx_pop()
   ?};
   _is_dokum:={? _dokum<>null() || DOKUM.clear(); DOKUM.seek(_dokum,ref_name(_dokum),1) || 0 ?}
?};

{? _is_dokum
|| {? DOKUM.RODZAJ_K<>'W'
   || KOMM.msg('E-dokument do %1 nie jest \'wychodzący\'.'@[_symbol],~_selected)
   || exec('document_send_check','businesslink3',_selected)
   ?}
|| KOMM.msg('E-dokument do %1 nie został wygenerowany.'@[_symbol],~_selected)
?};
VAR_DEL.delete('__MANSKSEF');

DOKUM.cntx_pop();
~~


\ksef_send_check_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [RR.xx]
:: OPIS: Wysyłanie dokumentu tylko do Businesscheck - przed grupą zaznaczonych rekordów
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy wysłać zaznaczone dokumenty do sprawdzenia?'@)
|| KOMM.init(,,'Wysyłanie dokumentów do sprawdzenia'@);
   VAR_DEL.delete('__GRP_DR');
:: __GRP_DR - srodowisko grupowego drukowania
   __GRP_DR:=exec('grp_dr_init','param_wydr');
:: __GRP_DR.GR - [0/1] - wydruk grupowy
   __GRP_DR.GR:=1;
   1
|| 0
?}


\ksef_send_check_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [RR.xx]
:: OPIS: Wysyłanie dokumentu tylko do Businesscheck - po grupie zaznaczonych rekordów
::----------------------------------------------------------------------------------------------------------------------
KOMM.select();
VAR_DEL.delete('__GRP_DR');
~~


\sendToBusinessCheck
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [RR.xx]
:: OPIS: Zwraca informację, czy dokument podlega sprawdzeniu
::   WE: _a - DOKUM.ref()
::   WY: 0 - kontrola wyłączona
::       1 - kontrola automatyczna
::       2 - kontrola ręczna - wymaga później popchnięcia komunikatem BLMT_DOCUMENT_SEND_GO
::       3 - kontrola, ERROR/WARNING wymagają GO
::       4 - kontrola, zawsze wysyłka
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
{? exec('is_businesscheck','businesslink3') & exec('is_dokum_ksef','zbl_dok',_a)
|| {? DOKUM.REFSQL<>''
   || _res:={? ref_tab(DOKUM.REFSQL)=FAKS
            || exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"T().KSEF_BC",0)
            |? ref_tab(DOKUM.REFSQL)=DOK
            || exec('FindAndGet','#table',DOK,DOKUM.REFSQL,,"DOK_REJ().KSEF_BC",0)
            || 0
            ?};
      {? _res<>0 & _res<>1 & _res<>2 & _res<>3 & _res<>4
      || _res:=0
      ?}
   ?}
?};
_res


\dokumzbc_load
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Ustawienie danych w zakładce "Rezultat kontroli Businesscheck"
::----------------------------------------------------------------------------------------------------------------------
_res:=~~;
{? ~exec('dokum_dokumzbc_seek','zbl')
|| _res:='#disable'
?};
_res


\bl_log_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.xx]
:: OPIS: Podgląd log dokumentu Busineslink
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.BL='T'
|| {? DOKUM.BL_LOG=null()
   || FUN.info('Brak zapisu w log.'@)
   || exec('edit_memo','#edit',exec('blob_txt_dl','#edit',DOKUM.BL_LOG),'Log'@,'E-dokument'@,180,25,,,0)
   ?}
|| FUN.info('Log dla dokumentu nie jest dostępny.'@)
?};
~~


\dane_xml_faks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Dane XML dla dokumentu sprzedaży lub zakupu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.SZ='S'
|| exec('dokumxml_sel2','edi_xml',$FAKS.ref(),FAKS.IDADD)
|? FAKS.SZ='Z'
|| exec('dokumxml_sel','edi_xml',FAKS)
?}


\istdef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Wyznacza definicję EDI
::   WE: _a - kontekst EDI
::       _b - rodzaj dokumentu (DKS/DZK/OBG/OBE)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_context:=_a;
_dokum:=_context.DOKUM;
_docType:={? var_pres('_b')>0 || _b || '' ?};

_istdef:=null();
_dokumz:=exec('dokumz','edi_fo_ufd',_dokum,'ksef');
{? _dokumz
||
   _context.DOKUMZ:=_dokumz;
   _istdef:={? _docType='FKS' | _docType='OBG' | _docType='OBE'
            || exec('bl_ufd_read','dokum',DOKUM.ref(),_dokumz,_context,_docType)
            || exec('istdef_ref4read','zbl_dok',_dokum,_dokumz,_context)
            ?}
?};
{? _istdef
||
   exec('dokum2pilki','edi_wspolne',_dokum,'ksef')
||
   _dokumz:=exec('dokumz','edi_fo_ufd',_dokum,'ufd');
   {? _dokumz
   || _istdef:={? _docType='FKS' | _docType='OBG' | _docType='OBE'
               || exec('bl_ufd_read','dokum',DOKUM.ref(),_dokumz,_context,_docType)
               || exec('istdef_ref4read','zbl_dok',_dokum,_dokumz,_context)
               ?}
   ?};
   {? _istdef
   ||
      _context.DOKUMZ:=_dokumz;
      exec('dokum2pilki','edi_wspolne',_dokum,'ufd')
   ?}
?};
_istdef

:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 6f7b8cbea5b3bbdc8146bb50b12aedf8ba02f67ee0bd2f6f440ff82df978af26a4493b5188e51b64cd225b3ac2c6e1df4e1abdcc77f5ebc1e558d2883be6e512381c8c6da2623b48b254034ba9c0a70a3b27ebf1baea9af540ea02ea99f4fe8c439c05f4f72c598d3cc3c5c7ab45dc3bdff8f9c6481f731069d18ce1328fb200

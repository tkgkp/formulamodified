:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: oddzialy.fml [17.00]
:: Utworzony: 2015-01-20
:: Autor: Mario
::======================================================================================================================
:: Zawartość: Formuły do obsługi tabeli ODDZ - oddziały
::======================================================================================================================


\wz_low_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [$12.10]
:: OPIS: Wzorzec pola
::       l - dowolny znak. Dodatkowo każda wprowadzona wielka litera jest zamieniana na małą,
::       ! - jesli wystapi na końcu zwróconego tekstu, oznacza powielenie przedostatniego znaku formatu dla wszystkich
::           pozostałych znaków pola,
::  OLD: \wz_low_all/war_tech.fml
::  TAG: <ODDZ><PT><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
'l!'


\sel_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: wyswietlenie oddzialow
::  OLD: \sel_oddz/firma.fml
::  TAG: <ODDZ>
::----------------------------------------------------------------------------------------------------------------------
ODDZ.win_sel('WERM');
ODDZ.win_edit('RED');
ODDZ.index('KOD');
ODDZ.prefix();
ODDZ.select();
''


\be_oddzk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: sprawdzenie - brak możliwości poprawiania symbolu oddziału
::       formuła przed redakcją pola ODDZ.KOD
::  OLD: \be_oddzk/firma.fml
::  TAG: <ODDZ><BE><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? -(1+menu_txt())='p' & ODDZ.KOD<>'' || 0 || 1 ?}


\ae_oddzk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [$12.10]
:: OPIS: sprawdzenie - czy podany znak może wystepować w masce tabeli
::       formuła po redakcji pola ODDZ.KOD
::  OLD: \ae_oddzk/firma.fml
::  TAG: <ODDZ><AE><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_maska:='abcdefghijklmnopqrstuvwxyz_1234567890';
_fld:=fld();
_wyn:=0;
{? _fld=''
|| FUN.info('Wymagane podanie kodu oddziału.'@)
|? (_maska*_fld)=0
|| FUN.info('Dozwolone znaki to: '@+_maska)
|| params_exec('kste_set','oddzialy');
   _wyn:=1
?};
_wyn


\chk_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord po dla tabeli ODDZ - oddziały
::       UWAGA - formuła wołana również podczas importu rekordów
::   WE: [_a] - 'add' / 'put' - dołączenie lub poprawa rekordu
::  OLD: \chk_oddz/defin.fml
::  TAG: <ODDZ><CHK><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};

{? _a='' & -(1+menu_txt)='d'
|| _a:='add'
|| _a:='put'
?};
_akcja:=_a;

_wyn:=__CHK.record(ODDZ,,'KOD','NAZ');

_maska:='abcdefghijklmnopqrstuvwxyz_1234567890';
{? _wyn='' & (_maska* ODDZ.KOD)=0
|| FUN.info('Dozwolone znaki dla kodu oddziału: '@+_maska);
   _wyn='KOD'
?};


{? _wyn='' & ODDZ.WW='*' & ODDZ.TW=''
|| FUN.info('Ponieważ wybrano stosowanie wielowalutowości,\n'
            'należy określić zakres jej stosowania.'@);
   _wyn:='WAL_F'
|? _wyn=''
||
   _kod:=ODDZ.KOD;
   _ref:=ODDZ.ref();
   ODDZ.cntx_psh();
   _ndx:=ODDZ.ndx_tmp(,,'KOD',,,'KOD',,);
   ODDZ.index(_ndx);
   ODDZ.prefix(_kod,_kod);
   {? ODDZ.first()
   ||
      {!
      |?
         {? ODDZ.ref()<>_ref | _akcja='add'
         || FUN.info('Istnieje już oddział z kodem '@+_kod+'.'); _wyn:='KOD'
         ?};
         _wyn='' & ODDZ.next()
      !}
   ?};
   ODDZ.ndx_drop(_ndx);
   ODDZ.cntx_pop()
?};
_wyn


\kste_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po dołącz/popraw ODDZ
::  OLD: \ae__oddz/defin.fml
::  TAG: <ODDZ><ADD><PUT><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,INFO,'WW');
ODDZ.cntx_psh();
{? var_pres('_a')=type_of('')
|| ODDZ.index('KOD2');
   ODDZ.prefix(_a,_a);
   ODDZ.first()
?};
_oddz:=ODDZ.KOD;
_wal:=ODDZ.WW;
{? params_get().var.KSTE_SET
||
   params_get().var.KSTE_SET:=0;
   KSTE.use('permane'+_oddz);
   KSTE.prefix();
   {? ~KSTE.first()
   || KSTE.blank();KSTE.add()
   ?}
?};
{? KSTE.KRAJ=null || KSTE.KRAJ:=INFO.KRAJ; KSTE.put() ?};
{? KSTE.WAL=null || KSTE.WAL:=INFO.NAROD; KSTE.put() ?};
{? INFO.WW<>'A' & _wal<>'*' || KSTE.KRAJ:=INFO.KRAJ; KSTE.WAL:=INFO.NAROD; KSTE.put() ?};
ODDZ.cntx_pop();
''


\oddz_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: usuwa zapis z tabeli ODDZ
::   WY: ~~
::  OLD: \oddz_usun/defin.fml
::  TAG: <ODDZ><DEL><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
{? ODDZ.count()-exec('oddz_ile_upr','oddzialy',ODDZ.ref())>0
 | exec('FindInSet','#table','STS','KOD',ODDZ.KOD,'S')<>null()
 | exec('FindInSet','#table','STS','KOD',ODDZ.KOD,'Z')<>null()
 | exec('FindInSet','#table','MG','MAG',ODDZ.KOD)<>null()
|| FUN.info('Oddział wykorzystywany w definicjach systemu.\nUsunięcie niemożliwe.'@)
|? FUN.ask('Usunąć oddział?'@)
|| KSTE.cntx_psh();
   KSTE.use('permane'+ODDZ.KOD);
   {? KSTE.first() || {! |? KSTE.del() !} ?};
   KSTE.cntx_pop();
   RBK_RD.cntx_psh();
   RBK_RD.index('ODDZ');
   RBK_RD.prefix(ODDZ.KOD);
   _loop:=RBK_RD.first(); {! |? _loop |! _loop:=RBK_RD.del() !};
   RBK_RD.cntx_pop();
   USERS_UP.cntx_psh();
   USERS_UP.index('USM');
   USERS_UP.prefix('ODDZ');
   {? USERS_UP.first()
   || {!
      |? {? USERS_UP.ODDZ=ODDZ.ref()
         || USERS_UP.del()
         || USERS_UP.next()
         ?}
      !}
   ?};
   USERS_UP.cntx_pop();
   ODDZ.del()
?};
~~


\oddz_ile_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [22.26]
:: OPIS: sprawdza liczbę użytkowników uprawnionych do danego oddziału
::   WE: _a - ODDZ.ref
::   WY: ilosc użytkowników
::----------------------------------------------------------------------------------------------------------------------
_sql:='select users from users_up where akr = \'ODDZ\' and oddz = \'' + $_a + '\'';
_tab:=sql(_sql);
_wyn:=_tab.size();
_wyn


\bd_kstww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed wyswietl pola KRAJ, WALUTA dla parametrow oddzialu
::  OLD: \bd_kstww/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? ((';Dołącz;Popraw'*menu_txt())>1 & cur_tab()=ODDZ & (INFO.WW='A' | (INFO.WW='W' & ODDZ.WW='*')))
 | exec('czy_ww','oddzialy')
|| ''
|| exec('findfnrd','color')
?}


\be_kstww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja pola KRAJ, WALUTA dla parametrow oddzialu
::  OLD: \be_kstww/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='WAL' | ~exec('czy_doki','oddzialy',ODDZ.KOD)
|| {? ((';Dołącz;Popraw'*menu_txt())>1 & cur_tab()=ODDZ & (INFO.WW='A' | (INFO.WW='W' & ODDZ.WW='*')))
    | exec('czy_ww','oddzialy')
   || {? cur_afld()='WAL' || exec('wz_wal','wzorce') || exec('jakiskra','oddzialy',1) ?}
   || 0
   ?}
?}


\czy_ww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: czy wielowalutowosc
::  OLD: \czy_ww/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

ODDZ.cntx_psh();

ODDZ.index('KOD');
ODDZ.prefix(KSTE.name()+1);
{? ODDZ.first()
||
   {? INFO.WW='A' | (INFO.WW='W' & ODDZ.WW='*') || _wyn:=1 ?}
?};
ODDZ.cntx_pop();
_wyn


\czy_doki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ
:: OPIS: sprawdzenie czy sa wystawione dokumenty
::  OLD: \czy_doki/defin.fml
::   WE: [_a] - kod oddzialu=tylko dla oddzialu ''-domyslnie dla wszystkich
::   WY: 1-sa dokumenty 0-nie ma
::----------------------------------------------------------------------------------------------------------------------
{? fld<>null
||
   {? _>=1 || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};
   _test:=sql('select count(ND.SYM) as ILE from @ND '+
    {? _a<>'' || 'where substr(ND.REFERENCE,6,1)=\':_a\' ' || '' ?},_a);
   {? _test.ILE=0
   || obj_del(_test);
       _test:=sql('select count(FAKS.SYM) as ILE from @FAKS '+
        {? _a<>'' || 'where substr(FAKS.REFERENCE,6,1)=\':_a\' ' || '' ?},_a)
   ?};
   {? _test.ILE=0
   || obj_del(_test);
       _test:=sql('select count(ZK_N.SYM) as ILE from @ZK_N '+
        {? _a<>'' || 'where substr(ZK_N.REFERENCE,6,1)=\':_a\' ' || '' ?},_a)
   ?};
   {? _test.ILE=0
   || obj_del(_test);
       _test:=sql('select count(ZD_NAG.SYM) as ILE from @ZD_NAG '+
        {? _a<>'' || 'where substr(ZD_NAG.REFERENCE,6,1)=\':_a\' ' || '' ?},_a)
   ?}
|| _test:=sql('select count(ND.SYM) as ILE from ND where 1=0')
?};
_test.ILE>0


\jakiskra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JF
:: OPIS: slownik krajow
::   WE: _a - 0 - sprawdzenia czy wystawiono dokumenty; 1 - bez sprawdzenie czy wystawiono dokumenty
::  OLD: \jakiskra/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0) || _a:=0 ?};

{? _a | ~exec('czy_doki','oddzialy')
||
   SLU.index('NAZ');
   SLO.index('SL');
   SLU.prefix();
   {? SLU.find_key('~KRAJE UE')
   || SLO.prefix(SLU.ref)
   || FUN.info('Brak słownika: ~KRAJE UE.'@)
   ?}
?}


\par_all_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: globalne parametry oddziałów
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,INFO);

exec('wczytaj_pom_wal','eurusd',INFO);
INFO.win_edit('ODDZ');
exec('wyl_or_wl','eurusd',INFO);
{? INFO.edit()
|| exec('zapisz','#stalesys',1,INFO,'KRAJ');

   exec('zapisz','#stalesys',1,INFO,'NAROD');
   exec('zapisz','#stalesys',1,INFO,'BANKRS');
   exec('zapisz','#stalesys',1,INFO,'TYPKRS');
   exec('zapisz','#stalesys',1,INFO,'WW');
   exec('zapisz','#stalesys',1,INFO,'TW');

   {? INFO.WW<>'W'
   ||
      ODDZ.cntx_psh();
      ODDZ.index('KOD2');
      _loop:=ODDZ.first();
      {!
      |? _loop
      |!
         {? INFO.WW='N' | INFO.WW='A' | INFO.WW='P'
         ||
            ODDZ.WW:=ODDZ.TW:='';
            ODDZ.put()
         ?};
         _loop:=ODDZ.next()
      !};
      ODDZ.cntx_pop()
   ?}
?}


\disp_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: podczytuje parametry dla wyświetlania - okienak podglądu
::----------------------------------------------------------------------------------------------------------------------
_var:=obj_new('KSTE_SET');
_var.KSTE_SET:=1;
params_set('var',_var);
exec('kste_set','oddzialy');
exec('wczytaj_pom_wal','eurusd');
ODDZ.win_edit(exec('oddz_win_edit','oddzialy'));
exec('wyl_or_wl','eurusd');
ODDZ.display();
1


\add_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: dodanie nowego oddziału
::----------------------------------------------------------------------------------------------------------------------
{? exec('pr_oddz','eurusd')
||
   _var:=obj_new('KSTE_SET');
   _var.KSTE_SET:=1;
   params_set('var',_var);
   ODDZ.blank();
   ODDZ.win_edit(exec('oddz_win_edit','oddzialy'));
   exec('wyl_or_wl','eurusd',,0);
   {? ODDZ.edit("__CHK.record(ODDZ,,'NAZ')")
   || ODDZ.add();
      KSTE.put();
      exec('usr_upr_uzupełnij_pelne','users_upraw','ODDZ')
   ?}
?}


\mod_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: modyfikacja oddziału
::----------------------------------------------------------------------------------------------------------------------
_red:=exec('oddz_win_edit','oddzialy');
{? exec('pr_oddz','eurusd')
||
   _var:=obj_new('KSTE_SET');
   _var.KSTE_SET:=1;
   params_set('var',_var);
   exec('kste_set','oddzialy');
   exec('wczytaj_pom_wal','eurusd');
   ODDZ.get();
   ODDZ.win_edit(_red);
   exec('wyl_or_wl','eurusd',,0);
   {? ODDZ.edit()
   || ODDZ.put(1);
      KSTE.put(1)
   ?}
?}


\oddz_win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: okienko edycyjne dla oddziału
::----------------------------------------------------------------------------------------------------------------------
_title:='Dane oddziału'@;
_win_red:=ODDZ.mk_edit(_title);
ODDZ.win_etab(_win_red,'Dane podstawowe'@);
ODDZ.win_ewin(_win_red,,'RED');
ODDZ.win_etab(_win_red,'Dane dodatkowe'@);
ODDZ.win_ewin(_win_red,KSTE,'RED_ODDZ');
exec('ok_esc','#window',ODDZ,_win_red,1);
_win_red

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:20 2c13d724d285f112dcb1a28b2c285e22974f26e0b53634ee86c0732d03c997640988781a9f17785c73155fdbaec1f3b51f9b4182f26ece429a37ce0c45748427e24d1504557ec7713071148ad0559fe0aa733088d87418b794e3e97d868a0c834157a9d5fe7b956bc2004275aa511ddffcf40655932b3e6dec1b97151a157304

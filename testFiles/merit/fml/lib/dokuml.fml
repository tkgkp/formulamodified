:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: Nowy1.fml
:: Utworzony: 13.06.2017
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Załącznik logistyczne
::======================================================================================================================


\edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Obsługa redagowania załączników jako parametr czynności
::   WE: [_a] - GRP1 np. ND.uidref()
::       [_b] - GRP2 np. DK.uidref()
::       [_c] - Dostawa
::       [_d] - INTEGER - 0/[1] - czy załączniki edytowalne?
::       [_e] - STRING - tytuł okienka, domyślnie 'Załączniki'
::       [_f] - STRING - tryb widoku: ['icons'], 'table'
::   WY: BI_BLOB.ref
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_grp1:='';
{? var_pres('_a')=type_of('')
|| _grp1:=_a
?};
_grp2:='';
{? var_pres('_b')=type_of('')
|| _grp2:=_b
?};
_dost:='';
{? var_pres('_c')=type_of('')
|| _dost:=_c
?};
_editable:=1;
{? var_pres('_d')=type_of(0)
|| _editable:=_d
?};
_title:='Załączniki';
{? var_pres('_e')=type_of('')
|| _title:=_e
?};
_viewmode:='icons';
{? var_pres('_f')=type_of('')
|| _viewmode:=_f
?};

_result:=0;

{? _grp1='' & _grp2='' || return(_result) ?};

{? _grp2='' || _editable:=0 ?};

_fml_edit:="";
_fml_valid:="
   _args:=_a;
   _buffer:=_b;
   _filename:=_c;
   _filepath:=_d;
   _dialog:=_e;

   _result:=1;
   _env:=params_get().env_blob;
   {? _env.has_file(_filename)>0
   || _result:=0;
      _msg:='Plik o nazwie: %1 jest już dodany jako załącznik i nie może zostać dodany ponownie.'@[_filename];
      {? _dialog=0
      || FUN.emsg(_msg)
      || KOMM.add(_msg,2,,1)
      ?}
   ?};
   _result
";
_fml_file:="
   _args:=_a;
   _buffer:=_b;
   _filename:=_c;
   _filepath:=_d;

   _result:=0;
   _can_continue:=1;

   DOKUML.GRP1:=_args.GRP1;
   DOKUML.GRP2:=_args.GRP2;
   DOKUML.USERS:=OPERATOR.USER;
   DOKUML.ESEND:='N';

   {? _can_continue>0
   || _result:=1
   ?};
   _result
";

_fml_args:=obj_new('GRP1','GRP2','DOST');
_fml_args.GRP1:=_grp1;
_fml_args.GRP2:=_grp2;
_fml_args.DOST:=_dost;

DOKUML.cntx_psh();

{? _grp2<>''
||
   DOKUML.index('GRP2');
   DOKUML.prefix(_grp2,)
|? _grp1<>''
||
   DOKUML.index('GRP1');
   DOKUML.prefix(_grp1,)
?};

_tab_tmp:=tab_tmp(1,
   'NAME','STRING[255]','Nazwa pliku',
   'SIZE','REAL','Rozmiar',
   'MOD_DATE','STRING[16]','Data modyfikacji',
   'ADD_DATE','STRING[19]','Data dodania',
   'INT_DATA','INTEGER','Identyfikator pliku',
   'STR_DATA','STRING[48]','Identyfikator pliku',
   'USER','STRING[30]','Użytkownik',
   'ESEND','STRING[1]','Wysłano'
);
_fml_load:="

   _tab_ctrl:=_a;
   _tab:=_b;

   _tab_ctrl.blank();
   _blob_ref:=($('_tab:=_a;_tab.'+.FIELD))(_tab);
   {? _blob_ref<>null()
   ||
      _tab_ctrl.NAME:=_tab.bl_info(.FIELD,'NAME');
      _tab_ctrl.SIZE:=_tab.bl_info(.FIELD,'SIZE');
      _tab_ctrl.MOD_DATE:=$_tab.bl_info(.FIELD,'MODIFY_DATE')+' '+$_tab.bl_info(.FIELD,'MODIFY_TIME')
   || _tab_ctrl.NAME:='brak pliku'
   ?};
   _tab_ctrl.ADD_DATE:=19+_tab.IDADD;
::   _tab_ctrl.INT_DATA:=#_tab.ref();
   _tab_ctrl.STR_DATA:=_tab.uidref();
   USERS.cntx_psh();
   _tab_ctrl.USER:=_tab.USERS().DANE;
   USERS.cntx_pop();
   _tab_ctrl.ESEND:=_tab.ESEND;
   _tab_ctrl.add()
";

:: Uruchamiam kontrolkę
exec('edit_blob','#edit',DOKUML
                        ,'BLOB'
                        ,
                        ,_fml_edit
                        ,_fml_file
                        ,_fml_args
                        ,_editable
                        ,_title
                        ,_viewmode
                        ,_tab_tmp
                        ,_fml_load
                        ,_fml_valid
                        ,_dost<>'');

DOKUML.cntx_pop();
_result


\nd_zalacznikidk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Załączniki pozycji dokumentu magazynowego - nagłówek dokumentu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('edit','dokuml',ND.uidref())


\dk_zalacznikidk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Załączniki pozycji dokumentu magazynowego - pozycja dokumentu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ND.cntx_psh();
{? DK.N
|| _dost:='';
   {? DK.PLUS='N' || _dost:=exec('FindAndGet','#table',DK,DK.RDK,DK.NDK,"DK.uidref()",'') ?};
   exec('edit','dokuml',DK.N().uidref(),DK.uidref(),_dost)
?};
ND.cntx_pop()


\sc_zalacznikidk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Załączniki pozycji dokumentu magazynowego - stan dostawy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ND.cntx_psh();
DK.cntx_psh();
DOKUML.cntx_psh();
DK.use(8+SC.SRDK);
DK.prefix();
{? DK.seek(SC.SRDK) & DK.N
||
   _ref_name:=ref_name(DK.N);
   DOKUML.use((DOKUML.name()-3)+(_ref_name+3));
   ND.use(_ref_name);
   exec('edit','dokuml',DK.N().uidref(),DK.uidref())
?};
DOKUML.cntx_pop();
DK.cntx_pop();
ND.cntx_pop();
1


\del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Usuwa załączniki dostawy
::   WE: _a - DK.uidref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dk_uid:=_a;
DOKUML.cntx_psh();
DOKUML.use((DOKUML.name()-3)+(ref_name(_dk_uid)+3));
DOKUML.cntx_psh();
DOKUML.index('GRP2');
DOKUML.prefix(_dk_uid);
_loop:=DOKUML.first();
{!
|? _loop
|!
   _loop:=DOKUML.del()
!};
DOKUML.cntx_pop();
DOKUML.cntx_pop()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:16 a13895e602d4eaf819bb48706399a87a3ccd5312b8a905a0e0a38f784cc274ae4d89a23b442bc092af009ae8f2775cf7a40b84f1557e5327146e6f2e79463aa36635cf96c2a52d2178579a3f4898fb8b3a77a0b13133a62278d1c215ce9b9972636e39c5d8d9dc1c91f9a6bdbcc6e524f116a7ea8ebe5b24be4619b55e079f8b

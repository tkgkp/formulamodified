:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_zal.fml [12.51]
:: Utworzony: 2019/03/15
:: Autor: jaws
:: Systemy: PPK
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę kartoteki załączników PPK.
::======================================================================================================================


\_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Aktualizacja kolumny UID.
::   WE: _a REAL - poprzedni identyfikator
::       _a REAL - nowy identyfikator
::   WY: 0/1 - błąd podczas aktualizacji/aktualizacja zakończona powodzeniem
::----------------------------------------------------------------------------------------------------------------------
:: warunek wykonania
{? _a=_b || return(1) ?};

:: mapa argumentów
_old:=_a;
_new:=_b;

:: wartość zwracana
_ret:=1;

PPK_ZAL.cntx_psh();
PPK_ZAL.prefix();
PPK_ZAL.f_set(,,'"UID"=:_a',_old);
_loop:=PPK_ZAL.f_first();
{!
|? _loop & _ret
|! PPK_ZAL.UID:=_new;
   _ret:=PPK_ZAL.put();
   _loop:=PPK_ZAL.f_next()
!};
PPK_ZAL.f_clear();
PPK_ZAL.cntx_pop();

_ret


\_remove
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Usunięcie zapisów właściwych według argumentów wywołania.
::   WE: _a REAL - identyfikator
::   WY: 0/1 - błąd podczas usuwania/usunięcie zakończone powodzeniem
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',PPK_ZAL,'PPK_ZAL',_a)


\setup
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przygotowuje kontekst tabeli załączników.
::   WE: _a TABLE - alias tabeli
::       _b REAL - identyfikator
::       _c [STRING] - rodzaj słownika opisów (domyślnie akronim tabeli danej aliasem _a)
::       _d [STRING] - kod czynności
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_uid:=_b;
_tab:=2-!_a;
_typ:={? var_pres('_c')<>type_of('') | _c='' || _tab || _c ?};
_act:={? var_pres('_d')=type_of('') & _d<>'' || _d || '' ?};

PPK_ZAL.fld_fml('UID','BLANK',$$_uid);
PPK_ZAL.fld_fml('TABELA','BLANK',$('\''+_tab+'\''));
PPK_ZRD.fld_fml('RODZAJ','BLANK',$('\''+_typ+'\''));

{? _act='' | exec('chk_role','#b__box',OPERATOR.USER,_act)=0
|| PPK_ZAL.actions('WER','dpu:d')
?};
~~


\zal_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed akcjami dla pliku okienek tabeli PPK_ZAL.
::   WE:
::   WY: 0/1 - akcja nie może być wykonana/można wykonać zasadniczą akcję
::----------------------------------------------------------------------------------------------------------------------
{? ~PPK_ZAL.PLIK
|| FUN.info('Nie znaleziono pliku załącznika.')
|| 1
?}



\otw_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Akcja "Otwórz" okienek tabeli PPK_ZAL.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('bl_view','#blob',PPK_ZAL,'PLIK')


\zap_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Akcja "Zapisz" okienek tabeli PPK_ZAL.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('bl_save','#blob',PPK_ZAL,'PLIK')


\rek_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Po edycji wiersza tabeli PPK_ZAL.
::   WE:
::   WY: 1 w przypadku gdy rekord jest poprawny, akronim wymaganego pola lub 0 gdy doszło do powielenia klucza.
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(PPK_ZAL,-menu_txt()='popraw',,'DATA','RODZAJ');
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & _chk=0)
|| return(_chk)
?};
{? -menu_txt()='popraw'
|| {? PPK_ZAL.PLIK=null & PPK_ZAL.bl_file('PLIK')=''
   || __CHK.err_fld(PPK_ZAL,'PLIK');
      return('PLIK')
   ?}
|| {? PPK_ZAL.bl_file('PLIK')=''
   || __CHK.err_fld(PPK_ZAL,'PLIK');
      return('PLIK')
   ?}
?};
1


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Udostępnia kartotekę załączników.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_uid:=PPK_ZAL.fld_fml('UID','*BLANK');

PPK_ZAL.cntx_psh();
PPK_ZAL.index('PPK_ZAL');
PPK_ZAL.prefix(_uid());
PPK_ZAL.win_sel('WER');
PPK_ZAL.win_edit('RED');
PPK_ZAL.select();
PPK_ZAL.actions('WER');
PPK_ZAL.cntx_pop();

PPK_ZAL.fld_fml('UID','BLANK',"*");
PPK_ZAL.fld_fml('TABELA','BLANK',"*");
PPK_ZRD.fld_fml('RODZAJ','BLANK',"*");
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:22 2323a351938f68b7122023169c4c49f00872ceac28143bc53d26ecd9a5cf8a8e5e5e9da00d2df3401fec0ab636a040ff107a75384da55296b88aea619655a82635bf53adf76d95c631750df9e3316fc95fee02c951f9a877af9bc4b46508b9d428c3c7133ac0bc2a89c06581533c68c045d3f0300f6f060a631f4a6689ecf348

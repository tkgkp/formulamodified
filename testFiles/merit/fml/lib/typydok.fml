:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: typydok.fml
:: Utworzony: 27.07.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Typy dokumentów magazynowych - formuły wspólne m.in. dla importu eksportu
::======================================================================================================================


\chk_typd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: po redakcji TYPYDOK
::   WE: [_a] - 'put', 'add' lub pusty string (domyślnie)
::  OLD: \chk_typd/firma.fml
::----------------------------------------------------------------------------------------------------------------------
_ctrl:={? var_pres('_a')=type_of('') & (+_a)=3
       || _a
       |? (1+menu_txt())='D'
       || 'add'
       || 'put'
       ?};
{? TYPYDOK.P='T' & TYPYDOK.Z='T' || TYPYDOK.TD:='' ?};
{? ~(TYPYDOK.UPRW='T' & TYPYDOK.P='N' )
|| {? TYPYDOK.UPRW='T'
   || FUN.info('Funkcja: uproszczone wystawianie dokumentu\nnie może być aktywna.\n'
               '\nFunkcja dostępna tylko dla dokumentów rozchodowych.'@);
      TYPYDOK.UPRW:='N'
   ?}
?};
{? TYPYDOK.CS='T'
|| {? TYPYDOK.P='T' | TYPYDOK.Z='N'
   || FUN.info('Funkcja: redakcja cen sprzedaży dostępna tylko dla\n'
               'dokumentów rozchodowych zewnętrznych.'@);
      TYPYDOK.CS:='N'
   ?}
?};
_wyn:=__CHK.record(TYPYDOK,,'T','NAZ','KOD','Z','P');
{? _wyn='' & {? exec('ae_spkod','numery',TYPYDOK.KOD) || _wyn:='';1 || _wyn:='KOD';0 ?}
|| _kod:=TYPYDOK.T;
   _ref:=TYPYDOK.ref();
   TYPYDOK.cntx_psh();
   TYPYDOK.index('TYP');
   TYPYDOK.prefix(_kod,_kod);
   {? TYPYDOK.first()
   || {!
      |? {? TYPYDOK.ref()<>_ref | _ctrl='add'
         || FUN.info('Istnieje już typ %1.'@[_kod]); _wyn:='T'
         ?};
         _wyn='' & TYPYDOK.next()
      !}
   ?};
   TYPYDOK.cntx_pop()
?};
_txt:={? TYPYDOK.DK='T' || 'korygujących' |? TYPYDOK.DK='W' || 'wartościowych' || '' ?};
{? _wyn='' & TYPYDOK.NRT=1 & _txt<>''
|| FUN.info('Numeracja tymczasowa dla dokumentów %1 jest niedostępna.'@[_txt]);
   _wyn:='DK'
?};
{? _wyn='' & TYPYDOK.NRT=1 & exec('FindInSet','#table','NRDOK','NRDOK',TYPYDOK.KOD,,"NRDOK.FT",,,'')=''
|| FUN.info('Definicja numeracji %1 nie ma formuły na znak tymczasowy.\n'
            'Numeracja tymczasowa została wyłączona.'@[TYPYDOK.KOD]);
   TYPYDOK.NRT:=0
?};
{? _wyn='' & TYPYDOK.Z='N' & TYPYDOK.UE='T'
|| FUN.info('Typ dokumentu unijny dostępny wyłącznie dla dokumentów zewnętrznych.\n'
            'Poprawiono znacznik unijny.'@);
   TYPYDOK.UE:='N'
?};
{? _wyn='' & ~exec('td_po','typydok',1)
|| _wyn:='TD'
?};
{? _wyn='' & TYPYDOK.P='N' & TYPYDOK.STATS_Z<>'T' & TYPYDOK.STATS_O<>'T' & TYPYDOK.STATS_N<>'T'
|| FUN.info('Należy określić rodzaj statusu dla wydań.'@);
   _wyn:='STATS_Z'
|? _wyn='' & TYPYDOK.P='T' & (TYPYDOK.STATS_Z='T' | TYPYDOK.STATS_O='T' | TYPYDOK.STATS_N='T')
|| FUN.info('Rodzaje statusów dla wydań nie dotyczą dokumentów przychodowych.'@);
   TYPYDOK.STATS_Z:=TYPYDOK.STATS_O:=TYPYDOK.STATS_N:='N'
|? _wyn='' & TYPYDOK.KOOP='T' & TYPYDOK.DS='T'
|| FUN.info('Dokument powiązany z kooperacją nie może tworzyć dokumentu sprzedaży.'@);
   _wyn:='DS'
|? _wyn='' & TYPYDOK.KOOP='T' & TYPYDOK.CS='T'
|| FUN.info('Dokument powiązany z kooperacją nie umożliwia redakcji cen sprzedaży.'@);
   _wyn:='CS'
|? _wyn='' & TYPYDOK.KOOP='T' & TYPYDOK.AFIFO<>''
|| FUN.info('Dokument powiązany z kooperacją nie może mieć włączonego Auto FIFO/LIFO.'@);
   _wyn:='AFIFO'
?};
{? _wyn='' & TYPYDOK.DK='T' & TYPYDOK.Z='T'
|| _przy:=TYPYDOK.P;
   _ref:={? _ctrl='put' || TYPYDOK.ref() || null() ?};
   _typ:='';
   _par:={? _przy='T' || 'Przychodowy'@ || 'Rozchodowy'@ ?};
   TYPYDOK.cntx_psh();
   TYPYDOK.index('KRZ');
   TYPYDOK.prefix(_przy,'T','T');
   {? TYPYDOK.first() || {? _ref<>TYPYDOK.ref() || _typ:=TYPYDOK.T; _wyn:='T' ?} ?};
   TYPYDOK.cntx_pop();
   {? _typ<>'' & _wyn<>''
   || FUN.info('Istnieje już typ korekty zewnętrznej o symbolu: %1.\n\n'
               'W systemie można zdefiniować tylko jeden typ dokumentu o parametrach:\n'
               '%2, Zewnętrzny, Korygujący.'@[_typ,_par])
   ?}
?};
_wyn


\td_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: formuła po redakcji pola TYPYDOK.TD
::   WE: [_a] - 0(domyślnie) fld 1-wartość pola TD
::  OLD: \td_po/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};

_typ:={? ~_a || fld() || TYPYDOK.TD ?}; _czy_p:=TYPYDOK.P; _czy_z:=TYPYDOK.Z;
{? _typ<>''
 & (_typ=TYPYDOK.T
     | _czy_p=exec('FindInSet','#table','TYPYDOK','TYP',_typ,_typ,"TYPYDOK.P",,,'')
     | _czy_z<>exec('FindInSet','#table','TYPYDOK','TYP',_typ,_typ,"TYPYDOK.Z",,,'')
     | exec('FindInSet','#table','TYPYDOK','TYP',_typ,_typ,"TYPYDOK.DS",,,'')='T'
     | (exec('FindInSet','#table','TYPYDOK','TYP',_typ,_typ,"TYPYDOK.UE",,,'')='T' & _czy_p='N'))
|| FUN.info('Należy podać inny dokument.'@);
   TYPYDOK.TD:='';
   0
|? _typ<>''
|| TYPYDOK.cntx_psh();
   TYPYDOK.index('TYP');
   TYPYDOK.prefix(_typ,_typ);
   {? ~TYPYDOK.first()
   || FUN.info('Brak podanego typu dokumentu: %1.'@[_typ]);
      _typ:='';
      _ok:=0
   || _ok:=1
   ?};
   TYPYDOK.cntx_pop();
   TYPYDOK.TD:=_typ;
   _ok
|? TYPYDOK.DN='D' & _typ=''
|| FUN.info('Dla dokumentu typu dekompletacja wymagane podanie typu tworzonego dokumentu przyjęcia.'@);
   0
|| TYPYDOK.TD:='';
   1
?}


\dk_count
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: czy można edytować pola w TYPYDOK
::   WE: [_a] - 0-wołane poza okienkiem redakcji, 1-wołane w okienku redakcji
::  OLD: \dk_count/firma.fml
::       \dk_count/!zws_par_ltdm.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};

T2MG.cntx_psh;
T2MG.index('TYP');
T2MG.prefix(TYPYDOK.ref());
_wyn:=-T2MG.size;
T2MG.cntx_pop;
TYPYDOK.cntx_psh();
{? 1+menu_txt()='D'
|| _wyn:=1
|| _wyn+=TYPYDOK.count();
   _wyn:=_wyn=0
?};
TYPYDOK.cntx_pop();
{? _a & cur_afld()='P' & (TYPYDOK.TD<>'' | TYPYDOK.UPRW='T' | TYPYDOK.AFIFO<>'')
|| _wyn:=0
|? _a & cur_afld()='TD' & TYPYDOK.P='T' & TYPYDOK.Z='T'
|| _wyn:=0
?};
_wyn

:Sign Version 2.0 jowisz:1048 2020/10/16 15:22:59 68ee92202ce2df831b6da8f49367e252c26f810cbc449218598d56ea4045c3e29a1191df2dfc6fb66747231a9d2eaed5e7d7b972f10f3072f7e7824bbd7ad4d717056daa1a9ce2d149010832f60202c492a0badfc016e7064ef031e9fd144ce7349a0ce0f01fcdcf6a1a271402b6142f2c58556d625fc8519551da054b40c228

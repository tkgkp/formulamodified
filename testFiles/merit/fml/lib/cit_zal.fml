:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: cit_zal_ved.fml
:: Utworzony: 30.11.2023
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi załączników deklaracji CIT-8
::======================================================================================================================


\schemat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [RR.xx]
:: OPIS: Zwraca schemat na podstawie typu załacznika
::   WE: _a - typ załacznika
::       _b - 1-ref 2-typ
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
_rodz:=_b;
{? _rodz=1
|| _ref:=null;
   ISTDEF.cntx_psh();
   ISTDEF.index('DATA');
   ISTDEF.prefix('FKS','D','CIT8',VAT_DEK.NR);
   {? ISTDEF.last() || _ref:=ISTDEF.ref() ?};
   ISTDEF.cntx_pop();
   _ref
|| 'CIT8'
?}


\gen_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Tworzy pozycje deklaracji CIT-8 na podstawie schematu e-deklaracji
::   WE: _a - typ deklaracji: CIT8, CIT8/O
::       _b - nr zalacznika
::----------------------------------------------------------------------------------------------------------------------
_prfx:=_a;
_typ:=form(_a);
_typ_zal:=_b;
_istdef:=exec('schemat','cit_zal',_typ,1);
exec('add_schemat','cit_zal',_typ,_istdef);
ISTDEF.cntx_psh();
ISTDEF.prefix();
{? ISTDEF.seek(_istdef)
|| ISTDEFS.cntx_psh();
   ISTDEFS.index('TYPPOLA');
   ISTDEFS.prefix(ISTDEF.ref(),_typ,);
   {? ISTDEFS.first()
   || _start:=_stop:=null;
      ISTDEFS.index('DRZEWO');
      ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
      {? ISTDEFS.first()
      || _start:=ISTDEFS.ref()
      ?};
      {? ISTDEFS.last()
      || _stop:=ISTDEFS.ref()
      ?};
      {? _start & _stop
      || ISTDEFS.index('LP');
         ISTDEFS.prefix(ISTDEF.ref());
         {? ISTDEFS.seek(_start)
         || VAT_POZ.cntx_psh();
            VAT_POZ.prefix();
            {!
            |? {? ISTDEFS.TYPFLD<>'' & +ISTDEFS.TYPFLD=3
               || VAT_POZ.blank(1);
                  VAT_POZ.VAT_DEK:=VAT_DEK.ref();
                  {? var_pres('TYP_ZAL',VAT_POZ)>0 || VAT_POZ.TYP_ZAL:=_typ_zal ?};
                  _opis:=ISTDEFS.OPIS;
                  {? (_poz:=_opis*':') || _opis:=_poz-_opis ?};
                  {? 2+_opis='P_' || _opis:=2-_opis ?};
                  _opis:=-_opis;
                  _litera:={? (_opis+1)<>'0' & #(_opis+1)=0 || _opis+1 || '' ?};
                  {? _litera<>'' || _opis:=_opis-1 ?};
                  _nr:=#_opis;
                  VAT_POZ.LP:=_prfx+(('00'+$_nr)+3)+_litera;
                  VAT_POZ.TYP:=1+ISTDEFS.TYPFLD;
                  _p1:=#(1-ISTDEFS.TYPFLD-1);
                  _p2:=#(ISTDEFS.TYPFLD+1);
                  {? _p1
                  || VAT_POZ.POZ1:=_nr;
                     _nr+=1
                  ?};
                  {? _p2
                  || VAT_POZ.POZ2:=_nr
                  ?};
                  VAT_POZ.OP:=ISTDEFS.memo_txt(,1,'ANOTACJA');
                  VAT_POZ.add()
               ?};
               ISTDEFS.ref()<>_stop & ISTDEFS.next()
            !};
            VAT_POZ.cntx_pop()
         ?}
      ?}
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop()


\add_schemat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodaje schemat e-deklaracji w deklaracji
::   WE: _a - typ schematu
::       _b - wskazanie na schemat
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
_rodz:=exec('schemat','cit_zal',_typ,2);
_schemat:=_b;
VAT_DEKS.cntx_psh();
VAT_DEKS.index('VAT_DEKS');
VAT_DEKS.prefix(VAT_DEK.ref(),_rodz,);
{? ~VAT_DEKS.first()
|| VAT_DEKS.blank(1);
   VAT_DEKS.VAT_DEK:=VAT_DEK.ref();
   VAT_DEKS.TYP:=_rodz;
   VAT_DEKS.ISTDEF:=_schemat;
   VAT_DEKS.add()
|| VAT_DEKS.ISTDEF:=_schemat;
   VAT_DEKS.put()
?};
VAT_DEKS.cntx_pop()

:Sign Version 2.0 jowisz:1045 2024/02/20 12:44:28 9d7c0762931e8b0280c337e44cde395c5591bda197efae57b1aab79f69d52ce448f556269220b8e7325305948b7f673cdb6ccc66cd71d309542a0ffaede60284b54bee20fe44f7966bf392765aa8cb3f290b2b11ddc72105f5a91782edad61a1734d96011e0c1a29432b65393e55b4b0b9e14ce32846fdc6f125c2aa53a4e07f

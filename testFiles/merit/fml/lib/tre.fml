:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tre.fml
:: Utworzony: 15.01.2019
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły oraz obszary robocze dziedziny TRE
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Formuła inicjująca dla dziedziny TRE
::----------------------------------------------------------------------------------------------------------------------
:: Czytanie stałych z zestawu XINFO
exec('czytaj','#stalesys',,XINFO,'SLWYDZIA','TER_AKT','TER_KNT');
exec('czytaj','#stalesys',,INFO,'NAROD');
exec('czytaj','#stalesys',,FINFO);

:: Ustawia zmienne informujące o dostępie do wartości sprzedaży, zakupu, magazynowych
exec('upr_set','ceny');

:: Formuła podczytująca dane specyficzne dla firmy
__War_f:="exec('FindInSet','#table',_a,_a,_c,REF.FIRMA,$(_a+'.'+_b))";

:: Właściwe dla dziedziny
exec('init_ztp','tre');

:: RB - objekt obsługujący rachunki bankowe
exec('RB','object');

:: Ustawienie prezycji pól w oknach
exec('fld_prec','win_ust');

:: Formy zatrudnienia
exec('__F_ZATR','object');
:: Rubryki (na potrzeby rozszerzonego filtra do wyboru współpracowników)
exec('__RUB','object');
~~


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Ustawienie parametrów sesji w obszarze roboczym dziedziny TRE
::----------------------------------------------------------------------------------------------------------------------
{? {? AreaTitle.area='ZWS_ZAS' | AreaTitle.area='TRE_REK' | AreaTitle.area='TRE_RDO'
   || __PARSES.editPar('OkresRok','OddzialLogProd')
   || __PARSES.editDom('TRE')
   ?}
|| AreaTitle.setTitle()
?};
:: Odświeżenie prefixu dla reklamacji
{? AreaTitle.area='TRE_REK' | AreaTitle.area='TRE_RDO'
|| REK_N.prefix(BEER.SZ)
?};
~~


\init_ztp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Inicjuje tabelę typów zasobów
::----------------------------------------------------------------------------------------------------------------------
REM_ZTP.cntx_psh();
REM_ZTP.index('TYP');
_txt:='Niepowiązany;Środek trwały;Pojazd;Stanowisko produkcyjne;Gniazdo produkcyjne;Materiał;Cecha dostawy;'
      'Wielokrotnie powiązany';
_tab:=spli_str(_txt,';');
{! _it:=1.. obj_len(_tab)
|! REM_ZTP.prefix(_tab[_it],);
   {? ~REM_ZTP.first()
   || REM_ZTP.blank();
      REM_ZTP.TYP:=_tab[_it];
      REM_ZTP.add()
   ?}
!};
REM_ZTP.cntx_pop();
~~


\tre_zgl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Wyświetla obszar roboczy 'Zgłoszenia remontowe'
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
exec('init','tre');
_env:=exec('env','remonty_zgloszenia',1);
_env.select();
~~


\p_select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Wybór pracownika (P) z listy
::   WY: P.ref() / null()
::----------------------------------------------------------------------------------------------------------------------
_p6771:=exec('get','#params',6771,2,OPERATOR.USER);
_ret:=exec('wybierz_pracownika','tre',0,1+_p6771,_p6771+1);
{? _ret.STATUS=''
|| {? _ret.P.first() || _res:=exec('FindAndGet','#table',P,_ret.P.UID,,"ref()",null()) || _res:=null() ?}
|| FUN.emsg(_ret.STATUS);
   _res:=null()
?};
_res


\wybierz_pracownika
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Wybór pracownika dla funkcji remontowych - uwzględnia uprawnienia do danych
::       (formy zatrudnienia, struktura organizacyjna)
::   WE: _a - możliwość wielowyboru (1 / 0)
::       [_b] - zakres pracowników - zatrudnieni (T), niezatrudnieni (N), domyślnie wszyscy (W)
::       [_c] - zakres jednostek organizacyjnych - wybrane (W), domyślnie wszystkie (A)
::----------------------------------------------------------------------------------------------------------------------
_wielu:=_a;
{? var_pres('_b')=type_of('') || _view:=_b || _view:='W' ?};
{? var_pres('_c')=type_of('') || _jorg:=_c || _jorg:='A' ?};

_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='TRE';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
{? _jorg='A'
|| UD_FIR.cntx_psh();
   UD_FIR.index('UD_SKL');
   UD_FIR.prefix(REF.FIRMA);
   {? UD_FIR.first()
   || _args.UD_SKL:=UD_FIR.UD_SKL().SYMBOL
   || _args.UD_SKL:=''
   ?};
   UD_FIR.cntx_pop()
|| {? ST.WYD=null()
   || _ret:=obj_new('STATUS');
      _ret.STATUS:=
         'Należy wybrać wydział w parametrach pracy albo zmienić zakres pracowników na "wszystkie jednostki".'@;
      return(_ret)
   || _args.UD_SKL:=ST.WYD().SYMBOL
   ?}
?};
_args.VIEW:=_view;
_args.HDR_SEL:={? _wielu || 'Wybór pracowników'@ || 'Wybór pracownika'@ ?};
_args.F_ZATR:='*T';
_args.WIELU:=_wielu;
_args.POMIN_KOLUMNY:='IP, PESEL, DOWOD, PASZPORT';
_args.GRP_IDENT:='pracnaz_sel';
_args.GRP_MODE:='normal';
_ret:=exec('wybierz','pracownik',_args);
_ret


\zakres_pracownikow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Redagowanie zakresu widoczności pracowników w module remontowym
::----------------------------------------------------------------------------------------------------------------------
_p6771:=exec('get','#params',6771,2,OPERATOR.USER);
_value:=exec('zakres_pracownikow_core','prod_rej',_p6771);

exec('set','#params',6771,_value,OPERATOR.USER);
~~


\tre_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Formuła sprawdzająca licencję dla obszaru TRE
::   WY: 'T' - jest licencja, 'N' - nie ma licencji
::----------------------------------------------------------------------------------------------------------------------
_result:='N';
_domain:=exec('domain_ref','#b_domain','TRE');
{? _domain<>null()
|| {? exec('lic','#b_domain',_domain)=1
   || _result:='T'
   ?}
?};
_result


\tre_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Wyświetla obszar roboczy 'Reklamacje klientów'
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

:: sprawdzenie czy jest grupa numeracji REK
{? exec('FindInSet','#table','NRDOK','NRDOK','REK','REK')=null()
|| FUN.info('Brak zdefiniowanej numeracji dla reklamacji klienta - typ REK.\n\n'
            'Numeracje należy zdefiniować w obszarze [Ustawienia i parametryzacja]\n'
            'lub zaimportować z danych wzorcowych.'@);
   return(~~)
?};
:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)=type_of('')
|| _link:=_params.LINK
|| _link:=''
?};

exec('init','tre');
params_set('LINK',_link);
exec('rekl_main','reklamacje','S');
~~


\tre_rdo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Wyświetla obszar roboczy 'Reklamacje do dostawców'
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());

:: sprawdzenie czy jest grupa numeracji ZEK
{? exec('FindInSet','#table','NRDOK','NRDOK','ZEK','ZEK')=null()
|| FUN.info('Brak zdefiniowanej numeracji dla reklamacji do dostawcy - typ ZEK.\n\n'
            'Numeracje należy zdefiniować w obszarze [Ustawienia i parametryzacja]\n'
            'lub zaimportować z danych wzorcowych.'@);
   return(~~)
?};

exec('init','tre');
exec('rekl_main','reklamacje','Z');
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 42eaf74af4c4fce06e10c1ee7be8fdcff81269657dd9055a3208e653cfc32a5430750983373a05d91467d217e1e876736855c886028b8e6c35068e35966c0daa34197e9eb51a46c7b1f27d5d4be8eaeee4690f2f78e0dbf8f653c40f5326e37b3e18ed34fa534dfd84fe44f00128106b8f06294ec26254a2167a06f22ca1635f

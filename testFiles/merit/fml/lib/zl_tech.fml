:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zl_tech.fml
:: Utworzony: 25.06.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa technologii zlecenia
::            Plik biblioteczny - wspólna obsługa dla czynności obszaru roboczego TTE_PZL
::======================================================================================================================


\usun_tktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ???
:: OPIS: kasowanie karty technologicznej zlecenia
::   WE: _a - 'no ask' znaczy, że bez pytania
::       _b - 'ext_del' znaczy, że już jest w transakcji
::       _c - 'pl_prod' znaczy, że dotyczy analizy wykonania, wpp zlecenia
::       Kontekst wywolania - VAR.A_ZLEC
::   WY: 0 - porażka
::       1 - sukces
::  OLD: \usun_ktlz/zl_zktl.fml
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_ask:={? _>0
      || {? _a='no ask'
         || 0
         || 1
         ?}
      || 1
      ?};
_ext:={? _>1
      || {? _b='ext_del'
         || 1
         || 0
         ?}
      || 0
      ?};
{? {? _ask || FUN.ask('Czy na pewno?'@) || 1 ?}
|| {? ~_ext || do() ?};
   _can_continue:=0;
   exec('tktl_cntx_psh','tech_common');
   {? _< 3
   || _msk:=(8+(VAR.A_ZLEC().RTKTL))+3;
      exec('tktl_use','tech_common',_msk);
      TKTL.clear();
      {? TKTL.seek(VAR.A_ZLEC().TKTL)
      || _can_continue:=1;
::       usuwanie surowców nielimitowanych tylko dla technologii zlecenia
         {? VAR.A_ZLEC=null() || VAR.A_ZLEC:=ZL.ref() ?};
         ZLIM.clear();
         {? (VAR.A_ZLEC().NRNZL=0 & ZL.GENLIM='N')
             | (ZL.NRNZL<>0 & ZL.GENLIM='P' )
             | (ZL.TYP().WP='W')
         || ZLIM.index('ZN')
         || ZLIM.index('ZPN')
         ?};
         ZLIM.prefix(VAR.A_ZLEC,'N');
         {? ZLIM.first()
         || {! |? ZLIM.del() !}
         ?}
      ?}
   || _msk:=(8+(PLRELWYR.RTKTL))+3;
      exec('tktl_use','tech_common',_msk);
      TKTL.clear();
      {? TKTL.seek(PLRELWYR.RTKTL)
      || _can_continue:=1
      ?}
   ?};
   {? _can_continue>0
   ||
      {? _< 3
      || ZL.TKTL:=null();
         ZL.put()
      || PLRELWYR.TKTL:=null();
         PLRELWYR.put()
      ?};
      _result:=exec('TKTL_kasuj','tech_head',TKTL.ref())
   ?};
   exec('tktl_cntx_pop','tech_common');
   {? ~_ext || end()?}
?};
_result


\zlecenia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Lista zleceń używających wskazanej karty technologicznej
::  OLD: \zlecenia/tech1.fml
::----------------------------------------------------------------------------------------------------------------------
ZL.index('KTL');
ZL.prefix($TKTL.ref());
_wer:=ZL.mk_sel('Zlecenia'@,'P',0,'zleceniatktl',,15,20,,'U');
ZL.win_fld(_wer,,'SYM');
ZL.win_fld(_wer,,'OPIS',,,30,,,'Opis'@);
ZL.win_fld(_wer,,'KTM','KTM',,20,,,'Kod produktu'@);
ZL.win_fld(_wer,,'KTM','N',,30,,,'Nazwa produktu'@);
ZL.win_fld(_wer,,'STAN',,,2,,,'Stan'@);
ZL.win_act(_wer,,'Formuła','Legenda'@@,,,"exec('legenda','color','ZL#03','ZL#05')",,,,,,'L');
ZL.win_fml(_wer,,'STAN',,'ICON_BEFORE',"exec('icon_stan','zl_head')");
ZL.win_sel(_wer);
ZL.select();
~~


\cofaj_tech
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Zmienia znacznik zakończenia technologii na zleceniu
::   WE: _a - ZL.ref()
::   WY: 1 - udało się zmienić znacznik, 0 - nie udało się zmienić znacznika
::----------------------------------------------------------------------------------------------------------------------
_zl:=_a;

:: Rozpoczynam transakcję jeśli jeszcze nie rozpoczęta
_tran_started:=0;
{? do_state()=0
|| do();
   _tran_started:=1
?};

_result:=0;
ZL.cntx_psh();
ZL.prefix();
{? ZL.seek(_zl)
|| {? ZL.RTKTL=''
   || _result:=1
   || _result:=exec('FindAndGet','#table',TKTL,ZL.RTKTL,,"STAT_N:=STAT_O:=STAT_S:=STAT_P:=STAN:='N';TKTL.put()",0)
   ?};
   {? _result>0
   || ZL.STAT_T:='N';
      _result:=ZL.put()
   ?}
?};
ZL.cntx_pop();

{? _result=0
||
:: Wycofuję transakcję
   undo()
?};

:: Kończę transakcję
{? _tran_started>0
|| end()
?};

_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 3753788401d89d2cffe1220e85507e5610d2b0a89f5dd65434d00256841ada42b911e25efd8acd3838d15fc7e14253e0c48b9a37f0e515aab9292c7260fd95d8b76058e8b2f41cb825f5a8725fa35b3092a224030537489926a181b575d8c9cdaad09df2667a479da101f0fb77f1cd4b45f36c01b4de9238145d099f4bc5a416

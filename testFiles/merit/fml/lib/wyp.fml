:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: wyp.fml
:: Utworzony: 14.07.2017
:: Autor: GZ
::======================================================================================================================
:: Zawartość: Formuły oraz obszary robocze dziedziny WYP
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [17.42]
:: OPIS: Formuła inicjująca dla dziedziny WYP
::----------------------------------------------------------------------------------------------------------------------
:: Czytanie stałych z zestawu XINFO
exec('czytaj','#stalesys',,XINFO,'SLWYDZIA','SLWAL','NAZ');

_red:=exec('mk_mgrp_edit','material');
MGRP.win_edit(_red);

:: Formy zatrudnienia
exec('__F_ZATR','object');
~~


\wyp_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [17.42]
:: OPIS: Formuła sprawdzająca licencję dla obszaru WYP
::   WY: 'T' - jest licencja, 'N' - nie ma licencji
::----------------------------------------------------------------------------------------------------------------------
_result:='N';
_domain:=exec('domain_ref','#b_domain','WYP');
{? _domain<>null()
|| {? exec('lic','#b_domain',_domain)=1
   || _result:='T'
   ?}
?};
_result


\mgrp_m_dokl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Zwraca wartosc M.DOKL dla podanego M
::  OLD: \rodzo_m_dokl/wyprodzo.fml
::   WE: #M.ref()
::   WY: M.DOKL lub -1
::----------------------------------------------------------------------------------------------------------------------
{? (_<1) | (_a=~~) | (type_of(_a)<>type_of(0)) | (_a=0) || return(-1) ?};
M.cntx_psh(); M.clear();
_odp:={? M.seek(_a,) || M.DOKL || -1 ?};
M.cntx_pop();
_odp


\filtruj_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [17.42]
:: OPIS: Nakładanie filtru na tabelę pracowników
::   WE: [_a] - parametry zakresu pracowników - aktywność
::       [_b] - parametry zakresu pracowników - jednostka organizacyjna
::       [_c] - dodatkowa sekcja WHERE dla filtra
::   WY: '' / uwaga
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _view:=_a || _view:='W' ?};
{? var_pres('_b')=type_of('') || _jorg:=_b || _jorg:='A' ?};
{? var_pres('_c')=type_of('') || _where:=_c || _where:='' ?};

_result:='';

ST.WYD:=__PARSES.getVal('WydzialProd').REF_UD_SKL;

{? _jorg='A'
|| UD_FIR.cntx_psh();
   UD_FIR.index('UD_SKL');
   UD_FIR.prefix(REF.FIRMA);
   {? UD_FIR.first()
   || _ud_skl:=UD_FIR.UD_SKL
   || _ud_skl:=null()
   ?};
   UD_FIR.cntx_pop()
|| {? ST.WYD=null()
   || _result:='Należy wybrać wydział w parametrach pracy albo zmienić zakres pracowników na "wszystkie jednostki".'@;
      _ud_skl:=null()
   || _ud_skl:=ST.WYD
   ?}
?};
_ud_def:=exec('szukaj_ud_def','schemat',
      exec('domyslny','schemat','PODZORG'),
      _ud_skl
   ).REF;
exec('filtruj_p','schemat','WYP',_ud_def,'','T',_view,,_where);
_result


\p_zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [17.42]
:: OPIS: Redagowanie zakresu widoczności pracowników w module wyposażenia
::----------------------------------------------------------------------------------------------------------------------
_p6520:=exec('get','#params',6520,2,OPERATOR.USER);
_value:=exec('zakres_pracownikow_core','prod_rej',_p6520);
exec('filtruj_p','wyp',1+_value,_value+1);
exec('set','#params',6520,_value,OPERATOR.USER);
~~


\osoba_npd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Zwrócenie stringu składającego się z wartości pól: NAZWISKO PIERWSZE DRUGIE rekordu OSOBA
::  OLD: \osoba_npd/wypdefin.fml
::   WE: OSOBA.ref() domyślnie bieżący
::   WY: OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE
::----------------------------------------------------------------------------------------------------------------------
{? (_<1) | (_a=~~) || _a:=OSOBA.ref() ?};
{? (ref_name(_a)=OSOBA.name())
||
   OSOBA.cntx_psh(); OSOBA.prefix();
   _odp:={? OSOBA.seek(_a)
         || _odp:=OSOBA.NAZWISKO+' '+OSOBA.PIERWSZE+' '+OSOBA.DRUGIE
         || ''
         ?};
:: odrzucenie skrajnych spacji
   _odp:=form(_odp);
   OSOBA.cntx_pop();
   _odp
|| ''
?}


\uzu_p_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Uzupelnia informacje na temat zatrucnienia w pol INFO wszystkich rekordow tabeli podanej parametram
::   WE: _a - tabela z polem INFO i #OS_VIEW.ref() w polu REF
::  OLD: \uzu_ov_info/wypdefin.fml
::----------------------------------------------------------------------------------------------------------------------
{? (_<1) | (type_of(_a)<=100) || return(0) ?};
{? _a.first()
||
   P.cntx_psh(); P.clear();
   {!
   |?
      {? P.seek(BIT.sqlint(_a.REF),)
      || _a.INFO:=exec('set_p_info','wyp')
      || _a.INFO:='Brak danych'
      ?};
      _a.put();
      _a.next()
   !};
   P.cntx_pop()
?}


\set_p_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.10]
:: OPIS: Zwraca informacje na temat zatrudnienia pracownika w postaci stringu
::   WE: _a - P.ref() [domyslnie biezacy]
::   WY: Informacja okreslajaca zatrudnienie
::  OLD: \set_ov_info/wypdefin.fml
::----------------------------------------------------------------------------------------------------------------------
{? (_<1) | (_a=~~) || _a:=P.ref() ?};
_odp:='';
{? ref_name(_a)=P.name()
||
   P.cntx_psh(); P.prefix();
   {? P.seek(_a)
   ||
      _odp:=P.WYDZIAL().SYMBOL;
      {? +_odp || _odp+=': ' ?};
      {? P.ST<>null() || _odp+=P.ST().ST ?}
   ?};
   P.cntx_pop()
?};
_odp


\wybierz_pracownika
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ML [17.42]
:: OPIS: Wybór pracownika dla funkcji wyposażenia - uwzględnia uprawnienia do danych
::       (formy zatrudnienia, struktura organizacyjna)
::   WE: _a - możliwość wielowyboru (1 / 0)
::       [_b] - zakres pracowników - zatrudnieni (T), niezatrudnieni (N), domyślnie wszyscy (W)
::       [_c] - zakres jednostek organizacyjnych - wybrane (W), domyślnie wszystkie (A)
::       [_d]  - dodatkowy warunek na filtr fraza FROM
::       [_e]  - dodatkowy warunek na filtr WHERE
::----------------------------------------------------------------------------------------------------------------------
_wielu:=_a;
{? var_pres('_b')=type_of('') || _view:=_b || _view:='W' ?};
{? var_pres('_c')=type_of('') || _jorg:=_c || _jorg:='A' ?};
{? var_pres('_d')=type_of('') || _from:=_d || _from:='' ?};
{? var_pres('_e')=type_of('') || _where:=_e || _where:='' ?};

_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='WYP';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
{? _jorg='A'
|| UD_FIR.cntx_psh();
   UD_FIR.index('UD_SKL');
   UD_FIR.prefix(REF.FIRMA);
   {? UD_FIR.first()
   || _args.UD_SKL:=UD_FIR.UD_SKL().SYMBOL
   || _args.UD_SKL:=''
   ?};
   UD_FIR.cntx_pop()
|| {? ST.WYD=null()
   || _ret:=obj_new('STATUS');
      _ret.STATUS:='Należy wybrać wydział w parametrach pracy albo zmienić zakres pracowników na "wszystkie jednostki".'@;
      return(_ret)
   || _args.UD_SKL:=ST.WYD().SYMBOL
   ?}
?};
_args.VIEW:=_view;
_args.HDR_SEL:={? _wielu || 'Wybór pracowników'@ || 'Wybór pracownika'@ ?};
_args.F_ZATR:='*T';
_args.WIELU:=_wielu;
_args.POMIN_KOLUMNY:='IP, PESEL, DOWOD, PASZPORT';
_args.GRP_IDENT:='pracnaz_sel';
_args.GRP_MODE:='normal';
_args.SQL_FROM:=_from;
_args.SQL_WHERE:=_where;

_ret:=exec('wybierz','pracownik',_args);
_ret


\wyp_lww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [17.42]
:: OPIS: Obszar roboczy WYP_LWW - stany wyposażenia
::----------------------------------------------------------------------------------------------------------------------
exec('init','wyp');
exec('select_karo','wyp_head');
~~


\wyp_lww_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Zmiana parametrów pracy obszaru WYP_LWW
::----------------------------------------------------------------------------------------------------------------------
BPMN.TYP_MG:='W';

_oddz:=ST.ODDZ;
_mg:=ST.MAG;
_okr_ref:=ST.OKR_REF;
_typydok:=TYPYDOK.ref();
{? {? exec('authorized','#b__box','WYP_LWW','WYP') || __PARSES.editDom('WYP')
   || 0
   ?}
|| {? _mg<>ST.MAG | _okr_ref<>ST.OKR_REF | _oddz<>ST.ODDZ
   || {? _oddz<>ST.ODDZ || exec('init','lmg') ?};
      {? _okr_ref<>ST.OKR_REF || ND.blank(0); DK.blank(0) ?};
      AreaTitle.setTitle()
   ?}
?}


\wyp_lwp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Selekcja POLWO - obszar roboczy WYP_LWP
::----------------------------------------------------------------------------------------------------------------------
exec('init','wyp');
exec('select','wyp_polwo');
~~


\wyp_lwp_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.42]
:: OPIS: Zmiana parametrów pracy obszaru WYP_LWP
::----------------------------------------------------------------------------------------------------------------------
BPMN.TYP_MG:='W';

_oddz:=ST.ODDZ;
_mg:=ST.MAG;
_okr_ref:=ST.OKR_REF;
_typydok:=TYPYDOK.ref();
{? {? exec('authorized','#b__box','WYP_LWP','WYP') || __PARSES.editPar('OkresRok','OddzialLogProd')
   || 0
   ?}
|| {? _mg<>ST.MAG | _okr_ref<>ST.OKR_REF | _oddz<>ST.ODDZ
   || {? _oddz<>ST.ODDZ || exec('init','lmg') ?};
      {? _okr_ref<>ST.OKR_REF || ND.blank(0); DK.blank(0) ?};
      AreaTitle.setTitle()
   ?}
?}


\wyp_lwz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.02]
:: OPIS: Selekcja POLWO - obszar roboczy WYP_LWP
::----------------------------------------------------------------------------------------------------------------------
exec('init','wyp');
exec('select','wyp_zaopatrz');
~~


\wyp_lwz_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.02]
:: OPIS: Zmiana parametrów pracy obszaru WYP_LWZ
::----------------------------------------------------------------------------------------------------------------------
BPMN.TYP_MG:='W';

_oddz:=ST.ODDZ;
_mg:=ST.MAG;
_okr_ref:=ST.OKR_REF;
_typydok:=TYPYDOK.ref();
{? {? exec('authorized','#b__box','WYP_LWZ','WYP') || __PARSES.editDom('WYP')
   || 0
   ?}
|| {? _mg<>ST.MAG | _okr_ref<>ST.OKR_REF | _oddz<>ST.ODDZ
   || {? _oddz<>ST.ODDZ || exec('init','lmg') ?};
      {? _okr_ref<>ST.OKR_REF || ND.blank(0); DK.blank(0) ?};
      AreaTitle.setTitle()
   ?}
?}


\wyp_lwn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: obszar roboczy WYP_LWN - selekcja normatywów i atrybutów osób
::----------------------------------------------------------------------------------------------------------------------
exec('init','wyp');
exec('select','normatyw');
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:04 28d9b62ef1a3ef8293b376bff05c2bbd0d9125741c89098a5cf7ef18fe2d77963b23761226a2fa9fa8cda3e1d4da0f967ff57d604ab5edb4ecb70141b38619d2df7891f8a55cb3b95acc53d25b866e6040fbaa43d7915ed74bead474c37c4ade1dfd3eff100361ef6ac213b993f81fe3718993a283447283817b1b99a23d65de

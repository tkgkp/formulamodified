:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: b_perm.fml
:: Utworzony: 09.02.2015 [17.00]
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi uprawnień do danych
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodanie uprawnień na starcie systemu
::----------------------------------------------------------------------------------------------------------------------
:: WAŻNE - w przypadku dodawania nowych uprawnień należy tę formułę wykonać jako zadanie potransferowe!
Perm.add('FJKS','Jednostki księgowe',"exec('set_fjks','b_perm',_a)","exec('copy_fjks','b_perm')");
Perm.add('FREJ','Uprawnienia do rejestrów',"exec('set_rej_upr','fks',_a)","exec('copy_frej','b_perm')");
Perm.add('HRB','Rachunki bankowe',"exec('set_hrb','b_perm',_a)","exec('copy_hrb','b_perm')");
Perm.add('HRP','Rodzaje przelewów',"exec('set_hrp','b_perm',_a)","exec('copy_hrp','b_perm')");
Perm.add('OGRP','Uprawnienia do grup wniosków i dokumentów w obiegu',"exec('set_ogrp','b_perm',_a)","~~");
Perm.add('ODDZ','Uprawnienia do oddziałów',"exec('set_UserUpr','b_perm','ODDZ',,,_a)","exec('copy_upr','b_perm','ODDZ')");
Perm.add('LMG','Uprawnienia do magazynów',"exec('set_UserUpr','b_perm','MG',,,_a)","exec('copy_upr','b_perm','MG')");
Perm.add('LSP','Uprawnienia do stanowisk sprzedaży',"exec('set_UserUpr','b_perm','STS',,,_a)"
   ,"exec('copy_upr','b_perm','STS')");
Perm.add('LZK','Uprawnienia do stanowisk zakupu',"exec('set_UserUpr','b_perm','STZ',,,_a)"
   ,"exec('copy_upr','b_perm','STZ')");
Perm.add('ZSMG','Uprawnienia do magazynów realizujących zamówienia sprzedaży',"exec('set_UserUpr','b_perm','ZAM',,,_a)"
   ,"exec('copy_upr','b_perm','ZAM')");
Perm.add('ZWMG','Uprawnienia do magazynów realizujących zamówienia wewnętrzne',"exec('set_UserUpr','b_perm','ZAW',,,_a)"
   ,"exec('copy_upr','b_perm','ZAW')");
Perm.add('F_ZATR','Uprawnienia do form współpracy',"exec('set_f_zatr','b_perm',_a)","exec('copy_f_zatr','b_perm')");
Perm.add('UD_SKL','Uprawnienia do schematów danych',"exec('set_ud_skl','b_perm',_a)","exec('copy_ud_skl','b_perm')");
Perm.add('KAS','Uprawnienia do stanowisk kasowych',"exec('set_upr_stankas','b_perm',_a)"
   ,"exec('copy_upr_kasa','b_perm','STANKAS')");
Perm.add('KAS_MUL','Uprawnienia do multikas',"exec('set_upr_mkasa','b_perm',_a)"
   ,"exec('copy_upr_kasa','b_perm','MKASA')");
Perm.add('TARS','Uprawnienia do cenników sprzedaży',"exec('set_upr_tar','b_perm','S',_a)"
   ,"exec('copy_upr_tar','b_perm','S')");
Perm.add('TARD','Uprawnienia do cenników dostawców',"exec('set_upr_tar','b_perm','D',_a)"
   ,"exec('copy_upr_tar','b_perm','D')");
Perm.add('TR_RODZ','Uprawnienia do kategorii transportu',"exec('set_UserUpr','b_perm','TR_RODZ',,,_a)"
   ,"exec('copy_upr','b_perm','TR_RODZ')");
~~


\b_perm_u_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
:: OPIS: Po usunięciu informacji o uprawnieniach do danych. Należy zadbać o usunięcie lub odpowiednią zapisów w tabeli
::       właściwej dla usuniętego uprawnienia
::   WE: wynik usuwania rekordu
::----------------------------------------------------------------------------------------------------------------------
: zakończ, jeśli nie usunięto rekordu
{? ~_a | do_state()<>1 || return() ?};

: ustal kontekst
_firma:=bfld('FIRMA');
_user:=bfld('USER');
_b_perm:=bfld('B_PERM');
{? _firma=null | _user=null | _b_perm=null
|| return()
?};
B_PERM.cntx_psh();
B_PERM.prefix();
_perm:='';
{? B_PERM.seek(_b_perm)
|| _perm:=B_PERM.NAME
|| B_PERM.cntx_pop();
   return()
?};
B_PERM.cntx_pop();

: aktualizacja właściwych tabel
{? _perm='F_ZATR'
|| USERS_FZ.cntx_psh();
   USERS_FZ.index('UNIQUE');
   USERS_FZ.prefix(_firma,_user);
   _loop:=USERS_FZ.first();
   {!
   |? _loop
   |! {? USERS_FZ.DOSTEP<>'?'
      || USERS_FZ.DOSTEP:='?';
         USERS_FZ.put()
      ?};
      _loop:=USERS_FZ.next()
   !};
   USERS_FZ.cntx_pop()

|? _perm='UD_SKL'
|| UDB_UPR.cntx_psh();
   UDB_UPR.prefix();
   UDB_UPR.f_set('USERS',,'"UDB_UPR".USERS=:_a and "UDB_UPR".DOSTEP<>\'?\'',_user);
   _loop:=UDB_UPR.f_first();
   {!
   |? _loop
   |! UDB_UPR.DOSTEP:='?';
      UDB_UPR.put();
      _loop:=UDB_UPR.f_next()
   !};
   UDB_UPR.cntx_pop()

|? _perm='FJKS'
|| USERSDEP.cntx_psh();
   USERSDEP.index('UNIK');
   USERSDEP.prefix(_firma,_user);
   {? USERSDEP.first()
   || {! |? USERSDEP.del() !}
   ?};
   USERSDEP.cntx_pop()

|? _perm='FREJ'
|| USERSREJ.cntx_psh();
   USERSREJ.index('REJ');
   USERSREJ.prefix(_user);
   {? USERSREJ.first()
   || {! |? USERSREJ.del() !}
   ?};
   USERSREJ.cntx_pop()

|? (';ODDZ;LMG;LSP;LZK;ZSMG;ZWMG;TR_RODZ;'*(';%1;'[_perm]))>0
|| _rodz:={? _perm='LMG'  || 'MG'
          |? _perm='LSP'  || 'STS'
          |? _perm='LZK'  || 'STZ'
          |? _perm='ZSMG' || 'ZAM'
          |? _perm='ZWMG' || 'ZAW'
          || _perm
          ?};
   USERS_UP.index('MAG');
   USERS_UP.prefix(_user,_rodz);
   {? USERS_UP.first() || {! |? USERS_UP.del() !} ?}

|? (';TARS;TARD;'*(';%1;'[_perm]))>0
|| _sd:=_perm+1;
   TARUP.index('USER');
   TARUP.prefix(_user);
   {? TARUP.first() & TARUP.find_tab('first','TARGN','SD','=',_sd)
   || {!
      |? _ref:=TARUP.ref(); _next:=TARUP.find_tab('next','TARGN','SD','=',_sd);
         TARUP.cntx_psh();
         TARUP.prefix();
         {? TARUP.seek(_ref) || TARUP.del() ?};
         TARUP.cntx_pop();
         _next
      !}
   ?}
?};
~~


\b_perm_u_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Trigger przed dodaniem rekordu do tabeli B_PERM_U
::----------------------------------------------------------------------------------------------------------------------
{? B_PERM_U.B_PERM().NAME='FREJ'
|| B_PERM_U.ACCESS:='T'
?};
1


\set_fjks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawienie uprawnienia FJKS do jednostek księgowych
::   WE: _a - wskazanie na użytkownika lub tabela z użytkownikami
::   WY: typ dostępu: -1- bez zmian 0-brak 1-częściowy 2-pełny
::----------------------------------------------------------------------------------------------------------------------
_grp:=var_pres('_a')=type_of(SYSLOG);
_tab:=exec('usr_tab','b_perm',_a);
_old:=Perm.getType();
_cur:={? _old=0 || 3 |? _old=1 || 2 |? _old=2 || 1 ?};
_pyt:=FUN.choice(
   'Do których jednostek księgowych nadać uprawnienia?'@,_cur,
   '&Wszystkich'@,'Wy&branych'@,'Ża&dnej'@
);

{? _grp
|| {? (_pyt=1 | _pyt=3)
   || {? FUN.ask('Zmiana spowoduje usunięcie\nwybranych dotychczas jednostek księgowych.\nCzy kontynuować?'@)
      || USERS.cntx_psh();
         USERS.prefix();
         {? _tab.first()
         || {!
            |? {? USERS.seek(_tab.REF,)
               || USERSDEP.cntx_psh();
                  USERSDEP.index('USERSDEP');
                  USERSDEP.prefix(REF.FIRMA,USERS.ref());
                  {? USERSDEP.first() || {! |? USERSDEP.del() !} ?};
                  USERSDEP.cntx_pop()
               ?};
               _tab.next()
            !}
         ?};
         USERS.cntx_pop()
      || _pyt:=0
      ?}
   ?}
|| USERSDEP.index('USERSDEP'); USERSDEP.prefix(REF.FIRMA,USERS.ref());
   {? (_pyt=1 | _pyt=3) & USERSDEP.first()
   || {? FUN.ask('Zmiana spowoduje usunięcie\nwybranych dotychczas jednostek księgowych.\nCzy kontynuować?'@)
      || {? USERSDEP.first() || {! |? USERSDEP.del() !} ?}
      || _pyt:=0
      ?}
   ?}
?};
{? _pyt=1
|| 2
|? _pyt=2
|| exec('init','hbn');
   VAR_DEL.delete('Tmp');
   Tmp:=tab_tmp(1,
      'KOD','STRING[8]','Skrót'@,
      'OPIS','STRING[40]','Pełna nazwa'@,
      'ACCESS','STRING[1]','Uprawnienie'@,
      'REF','INTEGER',
   );
   ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || {!
      |? Tmp.KOD:=ODD.OD;
         Tmp.OPIS:=ODD.N;
         Tmp.REF:=#ODD.ref();
         Tmp.ACCESS:={? _grp || '' |? USERSDEP.find_key(ODD.OD,) || 'T' || 'N' ?};
         Tmp.add();
         ODD.next()
      !}
   ?};
   TmpEnd:=0;
   _o:=Tmp.mk_sel('Jednostki księgowe'@,'P',,'#odduprusr',,,,,'U');
   Tmp.win_fld(_o,,'ACCESS',,,,,,,,'Czy nadano uprawnienie do jednostki księgowej [T/N]?'@,2,,"'T'","'N'","''");
   Tmp.win_fld(_o,,'KOD');
   Tmp.win_fld(_o,,'OPIS');
   _formula:="TmpEnd:=Tmp.ACCESS<>'T'; Tmp.ACCESS:='T'; Tmp.put()";
   Tmp.win_act(_o,,'Formuła','Nadaj'@@,,,_formula,,~_grp,1,,,'N');
   btn_n:=Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Nadaj'@],'menu:N');
   _formula:="TmpEnd:=Tmp.ACCESS<>'N'; Tmp.ACCESS:='N'; Tmp.put()";
   Tmp.win_act(_o,,'Formuła','Odbierz'@@,,,_formula,,,1,,,'O');
   btn_o:=Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O');
   {? _grp
   || _formula:="TmpEnd:=Tmp.ACCESS<>''; Tmp.ACCESS:=''; Tmp.put()";
      Tmp.win_act(_o,,'Formuła','Nie zmieniaj'@@,,,_formula,,,1,,,'M');
      Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Nie zmieniaj'@],'menu:M')
   ?};
   _formula:="TmpEnd:=2; sel_exit()";
   Tmp.win_act(_o,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A');
   Tmp.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A');
   Tmp.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
   Tmp.win_act(_o,,'Kolejność',);
   Tmp.win_act(_o,,'Okienko',,,,,"
      {? TmpEnd=1
      || FUN.ask('Czy zakończyć nadawania uprawnień?\n(Wprowadzone zmiany będą anulowane).'@)
      || 1
      ?}
   ");
   {? ~_grp
   || Tmp.win_act(_o,,'Rekord',,,,"
         _sel:=Tmp.sel_size();
         {? Tmp.ACCESS='T' & ~_sel
         || Tmp.actions(Tmp.win_sel('?'),,'O',1)
         || Tmp.actions(Tmp.win_sel('?'),,'N',1)
         ?};
         _gray:={? _sel || '' |? Tmp.ACCESS='T' || 'N' || 'O' ?};
         Tmp.actions_grayed(Tmp.win_sel('?'),_gray)
      ")
   ?};
   Tmp.win_sel(_o);
   {? Tmp.select()
   || {? Tmp.first()
      || {!
         |? {? Tmp.ACCESS<>''
            || {? ODD.seek(Tmp.REF,)
               || {? _tab.first()
                  || USERS.cntx_psh();
                     USERS.prefix();
                     USERSDEP.cntx_psh();
                     USERSDEP.index('UNIK');
                     USERSDEP.prefix();
                     {!
                     |? {? USERS.seek(_tab.REF,)
                        || _jest:=USERSDEP.find_key(REF.FIRMA,USERS.ref(),ODD.ref,null);
                           {? Tmp.ACCESS='T'
                           || {? ~_jest
                              || USERSDEP.blank(1);
                                 USERSDEP.FIRMA:=REF.FIRMA;
                                 USERSDEP.USERS:=USERS.ref();
                                 USERSDEP.DEPT:=ODD.ref();
                                 USERSDEP.add()
                              ?}
                           |? Tmp.ACCESS='N'
                           || exec('usersrej_del','fks',ODD.OD);
                              {? _jest || USERSDEP.del() ?}
                           |? _jest
                           || USERSDEP.del()
                           ?}
                        ?};
                        _tab.next()
                     !};
                     USERSDEP.cntx_pop();
                     USERS.cntx_pop()
                  ?}
               ?}
            ?};
            Tmp.next()
         !}
      ?};
      _old:=
      {? _grp | USERSDEP.first()
      || 1
      ?}
   ?};
   VAR_DEL.delete('btn_n','btn_o');
   _old
|? _pyt=3
|| 0
|| -1
?}


\ba_usersdep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodawanie uprawnień dla jednostek księgowych
::----------------------------------------------------------------------------------------------------------------------
ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
ODD.first();
ODD.win_sel('WYB');
{? ODD.select()
|| USERSDEP.cntx_psh();
   USERSDEP.index('USERSDEP');
   USERSDEP.prefix(REF.FIRMA);
   {? ~USERSDEP.find_key(OPERATOR.USER,ODD.OD,)
   || USERSDEP.blank();
      USERSDEP.DEPT:=ODD.ref();
      USERSDEP.add()
   ?};
   USERSDEP.cntx_pop()
?}


\set_hrb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawienie uprawnień do rachunków bankowych (HRB)
::   WE: _a - wskazanie na użytkownika lub tabelę z użytkownikami
::   WY: typ dostępu: -1- bez zmian 0-brak 1-częściowy 2-pełny
::----------------------------------------------------------------------------------------------------------------------
_grp:=var_pres('_a')=type_of(SYSLOG);
_tab:=exec('usr_tab','b_perm',_a);
_old:=Perm.getType();
_cur:={? _old=0 || 3 |? _old=1 || 2 |? _old=2 || 1 ?};
_pyt:=FUN.choice(
   'Do których rachunków bankowych nadać uprawnienia?'@,_cur,
   '&Wszystkich'@,'Wy&branych'@,'Ża&dnego'@
);
::SKID_RBK.N:=RB.get_rbtx(1,SKID_RBK.ref())

{? _grp
|| {? (_pyt=1 | _pyt=3)
   || {? FUN.ask('Zmiana spowoduje usunięcie\nwybranych dotychczas rachunków bankowych.\nCzy kontynuować?'@)
      || USERS.cntx_psh();
         USERS.prefix();
         {? _tab.first()
         || {!
            |? {? USERS.seek(_tab.REF,)
               || UPR_RB.cntx_psh();
                  UPR_RB.index('RB');
                  UPR_RB.prefix(USERS.ref());
                  {? UPR_RB.first() || {! |? UPR_RB.del() !} ?};
                  UPR_RB.cntx_pop()
               ?};
               _tab.next()
            !}
         ?};
         USERS.cntx_pop()
      || _pyt:=0
      ?}
   ?}
|| UPR_RB.index('RB'); UPR_RB.prefix(USERS.ref());
   {? (_pyt=1 | _pyt=3) & UPR_RB.first()
   || {? FUN.ask('Zmiana spowoduje usunięcie\nwybranych dotychczas rachunków bankowych.\nCzy kontynuować?'@)
      || {? UPR_RB.first() || {! |? UPR_RB.del() !} ?}
      || _pyt:=0
      ?}
   ?}
?};
{? _pyt=1
|| 2
|? _pyt=2
|| exec('init','hbn');
   VAR_DEL.delete('Tmp');
   Tmp:=tab_tmp(1,
      'RB','STRING[51]','Rachunek bankowy'@,
      'NB','STRING[80]','Nazwa banku'@,
      'ACCESS','STRING[1]','Uprawnienie'@,
      'REF','INTEGER',
   );
   SKID_RBK.index('TRN'); SKID_RBK.prefix(REF.INFO,0,null);
   {? SKID_RBK.first()
   || {!
      |? Tmp.RB:=RB.get_rbtx(1,SKID_RBK.ref());
         Tmp.NB:=SKID_RBK.BANK().NB;
         Tmp.REF:=#SKID_RBK.ref();
         Tmp.ACCESS:={? _grp || '' |? UPR_RB.find_key(SKID_RBK.ref()) || 'T' || 'N' ?};
         Tmp.add();
         SKID_RBK.next()
      !}
   ?};
   TmpEnd:=0;
   _o:=Tmp.mk_sel('Rachunki bankowe'@,'P',,'#rbuprusr',,,,,'U');
   Tmp.win_fld(_o,,'ACCESS',,,,,,,,'Czy nadano uprawnienie do rachunku bankowego [T/N]?'@,2,,"'T'","'N'","''");
   Tmp.win_fld(_o,,'RB');
   Tmp.win_fld(_o,,'NB');
   _formula:="TmpEnd:=Tmp.ACCESS<>'T'; Tmp.ACCESS:='T'; Tmp.put()";
   Tmp.win_act(_o,,'Formuła','&Nadaj'@@,,,_formula,,~_grp,1,,,'N');
   btn_n:=Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Nadaj'@],'menu:N');
   _formula:="TmpEnd:=Tmp.ACCESS<>'N'; Tmp.ACCESS:='N'; Tmp.put()";
   Tmp.win_act(_o,,'Formuła','&Odbierz'@@,,,_formula,,,1,,,'O');
   btn_o:=Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O');
   {? _grp
   || _formula:="TmpEnd:=Tmp.ACCESS<>''; Tmp.ACCESS:=''; Tmp.put()";
      Tmp.win_act(_o,,'Formuła','Nie zmieniaj'@@,,,_formula,,,1,,,'M');
      Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Nie zmieniaj'@],'menu:M')
   ?};
   _formula:="TmpEnd:=2; sel_exit()";
   Tmp.win_act(_o,,'Formuła','&Akceptuj'@@,,,_formula,,,,,,'A');
   Tmp.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A');
   Tmp.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
   Tmp.win_act(_o,,'Kolejność',);
   Tmp.win_act(_o,,'Okienko',,,,,"
      {? TmpEnd=1
      || FUN.ask('Czy zakończyć nadawania uprawnień?\n(Wprowadzone zmiany będą anulowane).'@)
      || 1
      ?}
   ");
   {? ~_grp
   || Tmp.win_act(_o,,'Rekord',,,,"
         _sel:=Tmp.sel_size();
         {? Tmp.ACCESS='T' & ~_sel
         || Tmp.actions(Tmp.win_sel('?'),,'O',1)
         || Tmp.actions(Tmp.win_sel('?'),,'N',1)
         ?};
         _gray:={? _sel || '' |? Tmp.ACCESS='T' || 'N' || 'O' ?};
         Tmp.actions_grayed(Tmp.win_sel('?'),_gray)
      ")
   ?};
   Tmp.win_sel(_o);
   {? Tmp.select()
   || {? Tmp.first()
      || {!
         |? {? Tmp.ACCESS<>'' & SKID_RBK.seek(Tmp.REF,)
            || {? _tab.first()
               || USERS.cntx_psh();
                  USERS.prefix();
                  UPR_RB.cntx_psh();
                  UPR_RB.index('RB');
                  UPR_RB.prefix();
                  {!
                  |? {? USERS.seek(_tab.REF,)
                     || _jest:=UPR_RB.find_key(USERS.ref(),SKID_RBK.ref(),null);
                        {? Tmp.ACCESS='T'
                        || {? ~_jest
                           || UPR_RB.blank(1);
                              UPR_RB.USER:=USERS.ref();
                              UPR_RB.RB:=SKID_RBK.ref();
                              UPR_RB.add()
                           ?}
                        |? _jest
                        || UPR_RB.del()
                        ?}
                     ?};
                     _tab.next()
                  !};
                  UPR_RB.cntx_pop();
                  USERS.cntx_pop()
               ?}
            ?};
            Tmp.next()
         !}
      ?};
      _old:=
      {? _grp | UPR_RB.first()
      || 1
      ?}
   ?};
   VAR_DEL.delete('btn_n','btn_o');
   _old
|? _pyt=3
|| 0
|| -1
?}


\ba_upr_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodawanie uprawnień do rachunków bankowych dla użytkownika
::----------------------------------------------------------------------------------------------------------------------
SKID_RBK.index('TAB'); SKID_RBK.prefix(null,REF.INFO,REF.INFO,0);
{? SKID_RBK.first()
|| _o:=SKID_RBK.mk_sel('Rachunki bankowe'@,'P',,'#skidrbkupr',,,,,'U');
   SKID_RBK.win_fld(_o,,'N',,,,,,'Numer rachunku bankowego'@);
   SKID_RBK.win_fld(_o,,'BANK','NB',,60,,,);
   SKID_RBK.win_act(_o,,'Formuła','&Wybierz'@@,,,"
      UPR_RB.cntx_psh();
      UPR_RB.index('RB'); UPR_RB.prefix(OPERATOR.USER,SKID_RBK.ref());
      {? ~UPR_RB.first()
      || UPR_RB.USER:=OPERATOR.USER;
         UPR_RB.RB:=SKID_RBK.ref();
         UPR_RB.add()
      ?};
      UPR_RB.cntx_pop();
      {? SKID_RBK.sel_size()=0 || sel_exit() ?};
      1
   ",,1,1,,"sel_exit()",'W');
   SKID_RBK.win_sel(_o);
   SKID_RBK.select()
|| FUN.info('Brak rachunków bankowych licencjobiorcy.'@)
?}


\bs_nb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: Przed wyswietleniem ROZNE.NB
::  OLD: \bs_nb/skid_usr.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.NB:=UPR_RB.RB().BANK().NB


\set_hrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawienie uprawnień do rodzajów przelewów (HRP)
::   WE: _a - wskazanie na użytkownika lub tabela z użytkownikami
::   WY: typ dostępu: -1- bez zmian 0-brak 1-częściowy 2-pełny
::----------------------------------------------------------------------------------------------------------------------
_grp:=var_pres('_a')=type_of(SYSLOG);
_tab:=exec('usr_tab','b_perm',_a);
_old:=Perm.getType();
_cur:={? _old=0 || 3 |? _old=1 || 2 |? _old=2 || 1 ?};
_pyt:=FUN.choice(
   'Do których typów przelewów nadać uprawnienia?'@,_cur,
   '&Wszystkich'@,'Wy&branych'@,'Ża&dnego'@);
{? _grp
|| {? (_pyt=1 | _pyt=3)
   || {? FUN.ask('Zmiana spowoduje usunięcie\nwybranych dotychczas typów przelewów.\nCzy kontynuować?'@)
      || USERS.cntx_psh();
         USERS.prefix();
         {? _tab.first()
         || {!
            |? {? USERS.seek(_tab.REF,)
               || UPR.cntx_psh();
                  UPR.index('REFKOD');
                  UPR.prefix(USERS.ref());
                  {? UPR.first() || {! |? UPR.del() !} ?};
                  UPR.cntx_pop()
               ?};
               _tab.next()
            !}
         ?};
         USERS.cntx_pop()
      || _pyt:=0
      ?}
   ?}
|| UPR.index('REFKOD'); UPR.prefix(USERS.ref());
   {? (_pyt=1 | _pyt=3) & UPR.first()
   || {? FUN.ask('Zmiana spowoduje usunięcie\nwybranych dotychczas typów przelewów.\nCzy kontynuować?'@)
      || {? UPR.first() || {! |? UPR.del !} ?}
      || _pyt:=0
      ?}
   ?}
?};
{? _pyt=1
|| 2
|? _pyt=2
|| exec('init','hbn');
   VAR_DEL.delete('Tmp');
   Tmp:=tab_tmp(1,
      'KOD','STRING[20]','Kod'@,
      'OPIS','STRING[50]','Opis'@,
      'ACCESS','INTEGER','Uprawnienie'@,
      'REF','INTEGER',
   );
   TZL.index('KOD'); TZL.prefix();
   {? TZL.first()
   || {!
      |? Tmp.KOD:=TZL.KOD;
         Tmp.OPIS:=TZL.OPIS;
         Tmp.REF:=#TZL.ref();
         Tmp.ACCESS:={? _grp || -1 || UPR.find_key(TZL.KOD,) ?};
         Tmp.add();
         TZL.next()
      !}
   ?};
   TmpEnd:=0;
   _o:=Tmp.mk_sel('Typy przelewów'@,'P',,'#ztluprusr',,,,,'U');
   Tmp.win_fld(_o,,'ACCESS',,,,,,,,'Czy nadano uprawnienie do typu przelewu [1/0]?'@,2,,"1","0","-1");
   Tmp.win_fld(_o,,'KOD',,,,,,,,'Kod typu przelewu'@);
   Tmp.win_fld(_o,,'OPIS',,,,,,,,'Opis typu przelewu'@);
   _formula:="TmpEnd:=Tmp.ACCESS<>1; Tmp.ACCESS:=1; Tmp.put()";
   Tmp.win_act(_o,,'Formuła','Nadaj'@@,,,_formula,,1,1,,,'N');
   btn_n:=Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Nadaj'@],'menu:N');
   _formula:="TmpEnd:=Tmp.ACCESS<>0; Tmp.ACCESS:=0; Tmp.put(); TmpEnd:=1";
   Tmp.win_act(_o,,'Formuła','Odbierz'@@,,,_formula,,,1,,,'O');
   btn_o:=Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O');
   {? _grp
   || _formula:="TmpEnd:=Tmp.ACCESS<>-1; Tmp.ACCESS:=-1; Tmp.put()";
      Tmp.win_act(_o,,'Formuła','Nie zmieniaj'@@,,,_formula,,,1,,,'M');
      Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Nie zmieniaj'@],'menu:M')
   ?};
   _formula:="TmpEnd:=2; sel_exit()";
   Tmp.win_act(_o,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A');
   Tmp.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A');
   Tmp.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
   Tmp.win_act(_o,,'Kolejność',);
   Tmp.win_act(_o,,'Okienko',,,,,"
      {? TmpEnd=1
      || FUN.ask('Czy zakończyć nadawania uprawnień?\n(Wprowadzone zmiany będą anulowane).'@)
      || 1
      ?}
   ");
   {? ~_grp
   || Tmp.win_act(_o,,'Rekord',,,,"
         _sel:=Tmp.sel_size();
         {? Tmp.ACCESS & ~_sel
         || Tmp.actions(Tmp.win_sel('?'),,'O',1)
         || Tmp.actions(Tmp.win_sel('?'),,'N',1)
         ?};
         _gray:={? _sel || '' |? Tmp.ACCESS || 'N' || 'O' ?};
         Tmp.actions_grayed(Tmp.win_sel('?'),_gray)
      ")
   ?};
   Tmp.win_sel(_o);
   {? Tmp.select()
   || {? Tmp.first()
      || {!
         |? {? TZL.seek(Tmp.REF,)
            || {? _tab.first()
               || USERS.cntx_psh();
                  USERS.prefix();
                  UPR.cntx_psh();
                  UPR.index('REFKOD');
                  UPR.prefix();
                  {!
                  |? {? USERS.seek(_tab.REF,)
                     || _jest:=UPR.find_key(USERS.ref(),TZL.KOD,null);
                        {? Tmp.ACCESS
                        || {? ~_jest
                           || UPR.blank(1);
                              UPR.RR:=USERS.ref();
                              UPR.TP:=TZL.ref();
                              UPR.add()
                           ?}
                        |? _jest
                        || UPR.del()
                        ?}
                     ?};
                     _tab.next()
                  !};
                  UPR.cntx_pop();
                  USERS.cntx_pop()
               ?}
            ?};
            Tmp.next()
         !}
      ?};
      _old:=
      {? UPR.first()
      || 1
      ?}
   ?};
   VAR_DEL.delete('btn_n','btn_o');
   _old
|? _pyt=3
|| 0
|| -1
?}


\usr_hrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Czy użytkownik ma uprawnienia do typu przelewu
::   WE:  _a  - kod typu przelewu
::       [_b] - wskazanie na użytkownika - domyślnie zalogowany
::----------------------------------------------------------------------------------------------------------------------
_has:={? _<2 || Perm.hasFull('HRP') || Perm.hasFull('HRP',_b) ?};
{? _has=0
|| UPR.cntx_psh();
   UPR.index('REFKOD');
   UPR.prefix({? var_pres('_b')>0 || _b || OPERATOR.USER ?},_a);
   _has:=UPR.first();
   UPR.cntx_pop()
?};
_has


\usr_hrp_any
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Czy użytkownik ma uprawnienia do jakiegokolwiek typu przelewu
::   WE: [_a] - wskazanie na użytkownika - domyślnie zalogowany
::----------------------------------------------------------------------------------------------------------------------
_has:={? _=0 || Perm.hasFull('HRP') || Perm.hasFull('HRP',_a) ?};
{? _has=0
|| UPR.cntx_psh();
   UPR.index('REFKOD');
   UPR.prefix({? var_pres('_a')>0 || _a || OPERATOR.USER ?});
   _has:=UPR.first();
   UPR.cntx_pop()
?};
_has


\usr_rej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JH [2011]
:: OPIS: Sprawdzanie czy user ma uprawnienia do rejestru
::   WE: _a - Ref rejestru
::   WY: 1 - posiada uprawnienia, 0 - nie posiada uprawnien
::  OLD: \czy_upr_us/dok_zrd4.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('usersrej_check','fks') || return(1) ?};
_wyn:=0;
USERS.cntx_psh();
USERSREJ.cntx_psh();
REJ.cntx_psh();
USERS.index('USR_KKOD');
USERS.prefix(OPERATOR.USER().KOD,OPERATOR.USER().KOD);
{? USERS.first()
||
   USERSREJ.index('REJ_ROK');
   USERSREJ.prefix(OPERATOR.USER,SSTALE.AR);
   {?  USERSREJ.first()
   ||
      USERSREJ.index('REJ');
      USERSREJ.prefix(OPERATOR.USER,_a);
      {? USERSREJ.first || _wyn:=1 ?}

   ?}
?};
USERS.cntx_pop();
USERSREJ.cntx_pop();
REJ.cntx_pop();
_wyn


\usr_fjks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Czy użytkownik ma uprawnienia do jednostki księgowej
::   WE:  _a  - skrót lub ref jednostki księgowej
::       [_b] - użytkownik, jeśli brak to bieżący
::       [_c] - firma
::   WY: 1 - ma, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
_users:={? var_pres('_b')>0 || _b || OPERATOR.USER ?};
{? var_press('_c')>0
|| _firma:=REF.FIRMA;
   REF.FIRMA:=_c
?};
_has:=Perm.hasFull('FJKS',_users);
{? _has=0
|| {? var_pres('_a')=type_of(null())
   || ODD.cntx_psh();
      ODD.prefix();
      {? ODD.seek(_a) || _a:=ODD.OD || _a:='' ?};
      ODD.cntx_pop()
   ?};
   {? _a<>''
   || USERSDEP.cntx_psh();
      USERSDEP.index('USERSDEP');
      USERSDEP.prefix(REF.FIRMA,_users,_a);
      _has:=USERSDEP.first();
      USERSDEP.cntx_pop()
   ?}
?};
{? var_press('_c')>0
|| REF.FIRMA:=_firma
?};
_has


\usr_fjks_any
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Czy użytkownik ma uprawnienia do jakiejkolwiek jednostki księgowej
::   WE: [_a] - użytkownik, jeśli brak to bieżący
::       [_b] - firma, jeśli brak to bieżąca
::   WY: 1 - ma, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_b')>0
|| _firma:=REF.FIRMA;
   REF.FIRMA:=_b
?};
_has:={? _=0 || Perm.hasFull('FJKS') || Perm.hasFull('FJKS',_a) ?};
{? _has=0
|| USERSDEP.cntx_psh();
   USERSDEP.index('USERSDEP');
   USERSDEP.prefix(REF.FIRMA,{? var_pres('_a')>0 || _a || OPERATOR.USER ?});
   _has:=USERSDEP.first();
   USERSDEP.cntx_pop()
?};
{? var_press('_b')>0
|| REF.FIRMA:=_firma
?};
_has


\usr_hrb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Czy użytkownik ma uprawnienia do rachunku bankowego licencjobiorcy
::   WE: _a - nr rachunku bankowego licencjobiorcy, _b - użytkownik, jeśli brak to bieżący
::   WY: 1 - ma, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
_has:={? _<2 || Perm.hasFull('HRB') || Perm.hasFull('HRB',_b) ?};
{? _has=0
|| exec('RB','object');
   _rach:=RB.getrrban(_a,REF.INFO,0,0,1);
   {? _rach<>null
   || UPR_RB.cntx_psh();
      UPR_RB.index('RB');
      UPR_RB.prefix({? var_pres('_b')>0 || _b || OPERATOR.USER ?},_rach);
      _has:=UPR_RB.first();
      UPR_RB.cntx_pop()
   ?}
?};
_has


\usr_hrb_any
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Czy użytkownik ma uprawnienia do jakiegokolwiek rachunku bankowego licencjobiorcy
::   WE: _a - użytkownik, jeśli brak to bieżący
::   WY: 1 - ma, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
_has:={? _=0 || Perm.hasFull('HRB') || Perm.hasFull('HRB',_a) ?};
{? _has=0
|| UPR_RB.cntx_psh();
   UPR_RB.index('RB');
   UPR_RB.prefix({? var_pres('_a')>0 || _a || OPERATOR.USER ?});
   _has:=UPR_RB.first();
   UPR_RB.cntx_pop()
?};
_has


\new_TabUpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Definicja tabeli uprawnień
::   WE: _a - opis okienka
::       _b - etykieta
::       _c - pole z oddziałem 1-tak, 0-nie 2-pole domyślny
::       _d - 0-bez pola Tylko podląd, 1-z polem Tylko podgląd dotyczy uprawnień kasowych
::       _e - 0-'', 1-noempty
::       _f - 0/1 - akcja grupowa
::   WY: uchwyt tabeli
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_d')<>type_of(0) || _d:=0 ?};
{? var_pres('_e')<>type_of(0) || _e:=1 ?};
{? var_pres('_f')<>type_of(0) || _f:=0 ?};
_zam:=fabs(_c);
_wyn:=tab_tmp(2
   ,'ORDER','INTEGER','Lp'@
   ,'KOD','STRING[20]','Kod/symbol'@
   ,'OPIS','STRING[100]','Nazwa'@
   ,'ACCESS','STRING[1]','Uprawnienie'@
   ,'REF','STRING[16]','Ref SQL'
   ,'ODD','STRING[10]','Oddział'@
   ,'DOM','STRING[1]','Domyślny'@
   ,'UPR','STRING[1]','Tylko podgląd'@
);
_sel:=_wyn.mk_sel(_a,'P',,_b,,,,,'U');
_wyn.win_fld(_sel,,'ACCESS',,,,,,,,'Czy nadano uprawnienie [T/N]?'@,2,,"'T'","'N'","''");
{? _c || _wyn.win_fld(_sel,,'ODD') ?};
_wyn.win_fld(_sel,,'KOD');
_wyn.win_fld(_sel,,'OPIS',,,40);
{? _c=2 || _wyn.win_fld(_sel,,'DOM',,,,,,,,'Domyślny dla EDI'@,2,,"'T'","'N'","''") ?};
{? _d=1 || _wyn.win_fld(_sel,,'UPR',,,,,,,,'Tylko podgląd'@,2,,"'N'","'T'","''") ?};
_formula:="_tab:=cur_tab(); TmpEnd:=_tab.ACCESS<>'T'; _tab.ACCESS:='T'; _tab.put()";
_formaft:={? _zam=2
          || "_tab:=cur_tab();
              _order:=0;
              _tab.cntx_psh();
              _tab.clear();
              {? _tab.last()
              || {!
                 |? {? _tab.ACCESS='T' & _tab.ORDER<>0 || _order+=1 ?};
                    _tab.prev()
                 !};
                 {!
                 |? _ref:=_tab.ref(); _oki:=_tab.next();
                    _tab.cntx_psh();
                    {? (_tab.clear(); _tab.seek(_ref)) & _tab.ACCESS='T' & _tab.ORDER=0
                    || _order+=1;
                       _tab.ORDER:=_order;
                       _tab.put(1)
                    ?};
                    _tab.cntx_pop();
                    _oki
                 !}
              ?};
              _tab.cntx_pop()"
          || ""
          ?};
_formagr:={? _zam=2
          || "sel_nchk()"
          || ""
          ?};
_wyn.win_act(_sel,,'Formuła','&Nadaj'@@,,,_formula,_formaft,1,1,_formagr,,'N');
_wyn.win_btn(_sel,'text=%1,panel=right,align=begin'['Nadaj'@],'menu:N',,,,,,'noempty');
_formula:="_tab:=cur_tab(); TmpEnd:=_tab.ACCESS<>'N'; _tab.ACCESS:='N'; _tab.put()";
_formaft:={? _zam=2
          || "_tab:=cur_tab();
              _order:=1;
              _tab.cntx_psh();
              _tab.clear();
              {? _tab.first()
              || {!
                 |? _ref:=_tab.ref(); _oki:=_tab.next();
                    _tab.cntx_psh();
                    {? (_tab.clear(); _tab.seek(_ref))
                    || {? _tab.ACCESS='T' & _tab.ORDER<>0
                       || {? _tab.ORDER>_order
                          || _tab.ORDER:=_order;
                             _tab.put(1)
                          ?};
                          _order+=1
                       |? _tab.ACCESS='N' & _tab.ORDER<>0
                       || _tab.ORDER:=0;
                          _tab.put(1)
                       ?}
                    ?};
                    _tab.cntx_pop();
                    _oki
                 !}
              ?};
              _tab.cntx_pop()"
          || ""
          ?};
_formagr:={? _zam=2
          || "sel_nchk()"
          || ""
          ?};
_wyn.win_act(_sel,,'Formuła','&Odbierz'@@,,,_formula,_formaft,,1,_formagr,,'O');
_wyn.win_btn(_sel,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O',,,,,,'noempty');
{? _f
|| _formula:="_tab:=cur_tab();TmpEnd:=_tab.ACCESS<>''; _tab.ACCESS:=''; _tab.put()";
   _wyn.win_act(_sel,,'Formuła','Nie zmieniaj'@@,,,_formula,,,1,,,'M');
   _wyn.win_btn(_sel,'text=%1,panel=right,align=begin'['Nie zmieniaj'@],'menu:M')
?};
{? _c=2
|| _formula:="_tab:=cur_tab(); _ref:=_tab.ref();
              _tab.cntx_psh();
              {? _tab.first()
              || {!
                 |? _tab.DOM:={? _tab.ref()=_ref || 'T' || 'N' ?};
                    _tab.put();
                    _tab.next()
                 !}
              ?};
              _tab.cntx_pop()";
   _wyn.win_act(_sel,,'Formuła','&Ustaw domyślny'@@,,,_formula,,1,,,,'U');
   _wyn.win_btn(_sel,'text=%1,panel=right,align=begin'['Ustaw domyślny'@],'menu:U',,,,,,'noempty')
?};
{? _d=1
||
   _formula:="
      _Tab:=cur_tab();
      _Tab.UPR:={? _Tab.UPR='N' || 'T' || 'N' ?};
      _Tab.put()
   ";
   _wyn.win_act(_sel,,'Formuła','&Ustaw tylko do podglądu'@@,,,_formula,,,,,,'U')
?};
_cfg:=',btn_label_align=center,panel=bottom,align=end';
_formula:="TmpEnd:=2; sel_exit()";
_wyn.win_act(_sel,,'Formuła','&Akceptuj'@@,,,_formula,,,,,,'A');
_wyn.win_btn(_sel,'text=%1'['Akceptuj'@]+_cfg,'menu:A',,,,,,'noempty');
_wyn.win_btn(_sel,'text=%1'['A&nuluj'@]+_cfg,'key:Esc');
{? _zam=2
|| _wyn.win_act(_sel,,'Formuła','&Przesuń'@@,,,"exec('renumUpr','b_perm',1)",,,1,"exec('renumUpr','b_perm',1)",,'P')
?};
_wyn.win_act(_sel,,'Kolejność',);
_wyn.win_act(_sel,,'Okienko',,,,,"
   {? TmpEnd=1
   || FUN.ask('Czy zakończyć nadawania uprawnień?\nUwaga. Wprowadzone zmiany nie zostaną uwzględnione.'@)
   || 1
   ?}");
_wyn.win_act(_sel,,'Rekord',,,,"
      _tab:=cur_tab();
      {? _tab.ACCESS='T' | _tab.sel_size()
      || _tab.actions(_tab.win_sel('?'),,'O',1)
      || _tab.actions(_tab.win_sel('?'),,'N',1)
      ?}
   ");
_wyn.win_sel(_sel);
{? _zam=2
|| _formula:="exec('renumUpr','b_perm')";
   _wyn.dnd_sel(_sel,,'records.'+_b,_formula)
|| _wyn.dnd_sel(_sel,,'records.'+_b,"")
?};
_wyn


\set_UserUpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Czy użytkownik ma uprawnienia do oddziału, magazynu, stanowisk sprzedaży, stanowisk zakupu
::   WE: _a - rodzaj uprawnień
::       [_b] - 0-nadawanie uprawnień(domyślnie) 1-sprawdzanie uprawnień
::       [_c] - czy sprawdzac wypelnienie pol - brak = T (wart T lub N)
::       [_d] - wskazanie na użytkownika lub tabela z użytkownikami
::   WY: przy nadawaniu: 2 - pełne, 1 - do wybranych, 0 - nie ma, -1 - bez zmian, przy sprawdzaniu: 0 / 1
::  OLD: \spr_upr/firma.fml
::----------------------------------------------------------------------------------------------------------------------
_typ:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_spr:={? var_pres('_c')=type_of('') & _c<>'' || _c || 'T' ?};
_grp:=var_pres('_d')=type_of(SYSLOG);
_tab_users:=exec('usr_tab','b_perm',{? _grp || _d  || USERS.ref() ?});

POM.TYPDOK:='';
USER.AKR:=_a;

_rodz:=_a;
{? _rodz='ODDZ'
|| _opis:='Uprawnienia do oddziałów'@;
   _label:='#userupoddz';
   _pyt:='Do których oddziałów nadać uprawnienia?'@;
   _pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich oddziałów.\nCzy kontynuować?'@;
   _pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich oddziałów.\nCzy kontynuować?'@;
   _zaden:='Ża&dnego'@
|? _rodz='MG'
|| _opis:='Uprawnienia do magazynów'@;
   _label:='#userup__mg';
   _pyt:='Do których magazynów nadać uprawnienia?'@;
   _pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich magazynów.\nCzy kontynuować?'@;
   _pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich magazynów.\nCzy kontynuować?'@;
   _zaden:='Ża&dnego'@
|? _rodz='STS'
|| _opis:='Uprawnienia do stanowisk sprzedaży'@;
   _label:='#userup_sts';
   _pyt:='Do których stanowisk sprzedaży nadać uprawnienia?'@;
   _pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich stanowisk sprzedaży.\nCzy kontynuować?'@;
   _pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich stanowisk sprzedaży.\nCzy kontynuować?'@;
   _zaden:='Ża&dnego'@
|? _rodz='STZ'
|| _opis:='Uprawnienia do stanowisk zakupu'@;
   _label:='#userup_stz';
   _pyt:='Do których stanowisk zakupu nadać uprawnienia?'@;
   _pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich stanowisk zakupu.\nCzy kontynuować?'@;
   _pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich stanowisk zakupu.\nCzy kontynuować?'@;
   _zaden:='Ża&dnego'@
|? _rodz='ZAM'
|| _opis:='Uprawnienia do magazynów realizujących zamówienia sprzedaży'@;
   _label:='#userup__zam';
   _pyt:='Do których magazynów realizujących zamówienia sprzedaży nadać uprawnienia?'@;
   _pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich magazynów realizujących zamówienia sprzedaży.\nCzy kontynuować?'@;
   _pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich magazynów realizujących zamówienia sprzedaży.\nCzy kontynuować?'@;
   _zaden:='Ża&dnego'@
|? _rodz='ZAW'
|| _opis:='Uprawnienia do magazynów realizujących zamówienia wewnętrzne'@;
   _label:='#userup__zaw';
   _pyt:='Do których magazynów realizujących zamówienia wewnętrzne nadać uprawnienia?'@;
   _pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich magazynów realizujących zamówienia wewnętrzne.\nCzy kontynuować?'@;
   _pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich magazynów realizujących zamówienia wewnętrzne.\nCzy kontynuować?'@;
   _zaden:='Ża&dnego'@
|? _rodz='TR_RODZ'
|| _opis:='Uprawnienia do kategorii transportu'@;
   _label:='#userup__trr';
   _pyt:='Do których kategorii transportu nadać uprawnienia?'@;
   _pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich kategorii transportu.\nCzy kontynuować?'@;
   _pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich kategorii transportu.\nCzy kontynuować?'@;
   _zaden:='Ża&dnej'@
?};

{? ~_typ
|| _wyn:=-1;
   _jak0:={? B_PERM_U.FULL='T' || 2 |? B_PERM_U.ACCESS='T' || 1 || 0?};
   VAR_DEL.delete('Tmp','TmpEnd');
   TmpEnd:=0;
   _empty:=(';STS;STZ;TR_RODZ;'*_rodz)>1;
   Tmp:=exec('new_TabUpr','b_perm',_opis,_label,{? _rodz='ZAM' || 2 |? _rodz='ZAW' || -2 || _rodz<>'ODDZ' ?},,~_empty,
   _grp);
   _add:="Tmp.blank();
          Tmp.ORDER:=_f;
          Tmp.KOD:=_a;
          Tmp.OPIS:=_b;
          Tmp.REF:=_c;
          Tmp.ACCESS:=_d;
          Tmp.ODD:=_e;
          Tmp.DOM:=_g;
          Tmp.add(1)
         ";
   USERS_UP.cntx_psh();
   ODDZ.cntx_psh();
   MG.cntx_psh();
   STS.cntx_psh();
   TR_RODZ.cntx_psh();
   USERS.cntx_psh();
   USERS.prefix();
   USERS_UP.index({? 'MG;ZAM;ZAW;'*_rodz || 'MAG' || _rodz ?});
   USERS_UP.prefix(USERS.ref(),_rodz);
   {? _rodz='ODDZ'
   || ODDZ.index('KOD');
      ODDZ.prefix();
      {? ODDZ.first()
      || {!
         |? _access:={? _grp || '' |? _jak0=2 | USERS_UP.find_key(ODDZ.ref()) || 'T' || 'N' ?};
            (_add)(ODDZ.KOD,ODDZ.NAZ,$ODDZ.ref(),_access,ODDZ.KOD,0,'N');
            ODDZ.next()
         !}
      ?}
   |? _rodz='MG'
   || MG.index('MAG');
      MG.prefix();
      {? MG.first()
      || {!
         |? _access:={? _grp || '' |? _jak0=2 | USERS_UP.find_key(MG.ref()) || 'T' || 'N' ?};
            (_add)(MG.SYM,MG.NAZ,$MG.ref(),_access,MG.ODDZ,0,'N');
            MG.next()
         !}
      ?}
   |? _rodz='STS'
   || STS.index('KOD');
      STS.prefix('S');
      {? STS.first()
      || {!
         |? _access:={? _grp || '' |? _jak0=2 | USERS_UP.find_key(STS.ref()) || 'T' || 'N' ?};
            (_add)(STS.KOD,STS.NAZ,$STS.ref(),_access,STS.ODDZ,0,'N');
            STS.next()
         !}
      ?}
   |? _rodz='STZ'
   || STS.index('KOD');
      STS.prefix('Z');
      {? STS.first()
      || {!
         |? _access:={? _grp || '' |? _jak0=2 | USERS_UP.find_key(STS.ref()) || 'T' || 'N' ?};
            (_add)(STS.KOD,STS.NAZ,$STS.ref(),_access,STS.ODDZ,0,'N');
            STS.next()
         !}
      ?}
   |? 'ZAM;ZAW;'*_rodz
   || USERS_UP.cntx_psh();
      USERS_UP.index('MGNR');
      USERS_UP.prefix(null(),_rodz);
      {? USERS_UP.first()
      || {!
         |? _mg:=USERS_UP.MG;
            _num:=0;
            USERS_UP.cntx_psh();
            USERS_UP.index('MAG');
            USERS_UP.prefix(USERS.ref(),_rodz);
            {? USERS_UP.find_key(_mg)
            || _dom:=USERS_UP.DOM;
               _num:=USERS_UP.NR;
               _access:={? _grp || '' || 'T' ?}
            || _dom:='N';
               _access:={? _grp || '' |? _jak0=2 || 'T' || 'N' ?}
            ?};
            USERS_UP.cntx_pop();
            (_add)(USERS_UP.MG().SYM,USERS_UP.MG().NAZ,$USERS_UP.MG,_access,USERS_UP.MG().ODDZ,_num,_dom);
            USERS_UP.next()
         !}
      ?};
      USERS_UP.cntx_pop()
   |? _rodz='TR_RODZ'
   || TR_RODZ.index('KOD');
      TR_RODZ.prefix();
      {? TR_RODZ.first()
      || {!
         |? _access:={? _grp || '' |? _jak0=2 | USERS_UP.find_key(TR_RODZ.ref()) || 'T' || 'N' ?};
            (_add)(TR_RODZ.KOD,TR_RODZ.NAZ,$TR_RODZ.ref(),_access,'',0,'N');
            TR_RODZ.next()
         !}
      ?}
   ?};

   _wyb0:={? _jak0=1 || 2 |? _jak0=2 || 1 || 0 ?};
   _wyb:=FUN.choice(_pyt,_wyb0,'&Wszystkich'@,'Wy&branych'@,_zaden);
   {? _wyb=1
   || _jak:=2
   |? _wyb=2
   || _jak:=1
   |? _wyb=3
   || _jak:=0
   || _jak:=-1
   ?};

   {? (_jak=2 | _jak=0) & _jak<>_jak0
   || {? ~FUN.ask({? _jak=2 || _pyt1 || _pyt2 ?})
      || _jak:=-1
      ?}
   ?};

   {? _jak=1
   || {? ~Tmp.select()
      || _jak:=-1
      ?}
   ?};

:: Przygotowanie do zapisu w USERS_UP
:: ZAM, ZAW, TR_RODZ - przy uprawnieniu "do wszystkich" brak w USERS_UP
   {? _jak=2 | _jak=0
   || {? Tmp.first()
      || {!
         |? {? _jak=2 & 'ZAM;ZAW;TR_RODZ'*_rodz=0
            || Tmp.ACCESS:='T'
            || Tmp.ACCESS:='N'
            ?};
            Tmp.put();
            Tmp.next()
         !};
         Tmp.first()
      ?}
   ?};

   {? _jak<>-1
   || {? Tmp.first()
      || _tab:={? _rodz='ODDZ'    || ODDZ
               |? _rodz='MG'      || MG
               |? _rodz='STS'     || STS
               |? _rodz='STZ'     || STS
               |? _rodz='ZAM'     || MG
               |? _rodz='ZAW'     || MG
               |? _rodz='TR_RODZ' || TR_RODZ
               ?};
         _fld:={? 'ZAM;ZAW;'*_rodz || $('USERS_UP.MG') || $('USERS_UP.'+_rodz) ?};
         {!
         |? _ref:=exec('FindAndGet','#table',_tab,Tmp.REF,,,null());
            {? _tab_users.first()
            || {!|?
                  {? USERS.seek(_tab_users.REF,)
                  ||
                     USERS_UP.prefix(USERS.ref(),_rodz);
                     {? Tmp.ACCESS='T'
                     || {? ~USERS_UP.find_key(_ref)
                        || USERS_UP.blank();
                           USERS_UP.USERS:=USERS.ref();
                           _fld():=_ref;
                           {? 'ZAM;ZAW;'*_rodz
                           || USERS_UP.NR:=Tmp.ORDER;
                              USERS_UP.DOM:=Tmp.DOM
                           ?};
                           USERS_UP.add(1)
                        |? 'ZAM;ZAW;'*_rodz
                        || USERS_UP.NR:=Tmp.ORDER;
                           USERS_UP.DOM:=Tmp.DOM;
                           USERS_UP.put(1)
                        ?}
                     |? Tmp.ACCESS='N'
                     || {? USERS_UP.find_key(_ref)
                        || USERS_UP.del(1)
                        ?}
                     ?}
                  ?};
                  _tab_users.next()
               !}
            ?};
            Tmp.next()
         !}
      ?};
      {? _jak=1 & USERS_UP.size()=0
      || _wyn:=0
      || _wyn:=_jak
      ?}
   ?};
   USERS_UP.cntx_pop();
   ODDZ.cntx_pop();
   MG.cntx_pop();
   STS.cntx_pop();
   TR_RODZ.cntx_pop();
   USERS.cntx_pop();
   VAR_DEL.delete('Tmp','TmpEnd')

|| _wyn:=0;
   {? (_spr='T' & fld()=0) | (_spr='T' & fld()<>0) | (_spr='N' & fld()<>0)
   || {? fld()=0
      || FUN.info('Niewypełnione pole.'@)
      || _wyn:=exec('usr_upr','b_perm',_rodz,fld(),USERS.ref());
         {? ~_wyn
         || FUN.info('Brak uprawnień dla wybranej pozycji.'@)
         ?}
      ?}
   || _wyn:=1
   ?}

?};

_wyn


\set_f_zatr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
::  MOD: DG [23.25]
:: OPIS: Umożliwia nadanie uprawnień do form współpracy
::   WE: _a - wskazanie na użytkownika lub tabela z użytkownikami
::   WY: -1 - uprawnienia bez zmian
::        0 - brak uprawnień
::        1 - częściowe uprawnienia
::        2 - pełne uprawnienia
::----------------------------------------------------------------------------------------------------------------------
{? REF.FIRMA=null
|| FUN.info('Uprawnienia mogą być nadane tylko w kontekście firmy.'@);
   return(-1)
?};

_grp:=var_pres('_a')=type_of(SYSLOG);
_tab:=exec('usr_tab','b_perm',{? _grp || _a  || USERS.ref() ?});
_old:=Perm.getType();
_cur:={? _old=0 || 3 |? _old=1 || 2 |? _old=2 || 1 ?};
_pyt:=FUN.choice(
   'Do których form zatrudnienia nadać uprawnienia?'@,_cur,
   '&Wszystkich'@,'Wy&branych'@,'Ża&dnej'@
);
_res:=-1;

:: Uzupełnienie uprawnień dla wszystkich wybranych użytkowników
{? _tab.first()
|| USERS.cntx_psh();
   USERS.prefix();
   {!
   |? {? USERS.seek(_tab.REF,)
      || exec('users_upr','f_zatr',REF.FIRMA,USERS.ref())
      ?};
      _tab.next()
   !};
   USERS.cntx_pop()
?};

:: Oryginalne ustawienia uprawnień dla pojedynczego użytkownika
{? ~_grp
|| _upq:='select F.REFERENCE as REF, F.DOSTEP as PERM from USERS_FZ as F where F.FIRMA=:_a and F.USERS=:_b';
   _old:=sql(_upq,REF.FIRMA,USERS.ref())
?};

:: Przetwarzanie danych w przypadku nadawania bądź odbierania wszystkich uprawnień
{? _pyt=1 | _pyt=3
|| {? _tab.first()
   || USERS_FZ.cntx_psh();
      USERS_FZ.index('W_ZATR');
      USERS_FZ.prefix();
      USERS.cntx_psh();
      USERS.prefix();
      {!
      |? {? USERS.seek(_tab.REF,)
         || USERS_FZ.prefix(REF.FIRMA,USERS.ref());
            {? USERS_FZ.first()
            || {!
               |? USERS_FZ.DOSTEP:={? _pyt=1 || 'T' || 'N' ?};
                  USERS_FZ.put();
                  USERS_FZ.next()
               !}
            ?}
         ?};
         _tab.next()
      !};
      USERS.cntx_pop();
      USERS_FZ.cntx_pop()
   ?};
   _res:={? _pyt=1 || 2 || 0 ?}

:: Przetwarzanie danych w przypadku nadawania lub odbierania wybranych uprawnień
|? _pyt=2
|| VAR_DEL.delete('__Tmp','__TmpEnd');
   __Tmp:=tab_tmp(1,
      'KOD','STRING[8]','Skrót'@,
      'OPIS','STRING[100]','Pełna nazwa'@,
      'DOSTEP','STRING[1]','Uprawnienie'@,
      'REF','INTEGER','USERS_FZ',
      'F_ZATR','INTEGER','F_ZATR'
   );
   USERS_FZ.cntx_psh();
   F_ZATR.cntx_psh();
   USERS_FZ.index('W_ZATR');
   USERS_FZ.prefix(REF.FIRMA,USERS.ref());
   {? USERS_FZ.first()
   || {!
      |? __Tmp.KOD:=USERS_FZ.F_ZATR().KOD;
         __Tmp.OPIS:=F_ZATR.OPIS;
         __Tmp.REF:=#USERS_FZ.ref();
         __Tmp.F_ZATR:=#F_ZATR.ref();
         __Tmp.DOSTEP:={? _grp || '?' || USERS_FZ.DOSTEP ?};
         __Tmp.add();
         USERS_FZ.next()
      !}
   ?};
   F_ZATR.cntx_pop();
   USERS_FZ.cntx_pop();
   __TmpEnd:=0;

   _win:=__Tmp.mk_sel('Formy współpracy'@,'P',0,'users_fz_upr',1,21,13,0,'U','T',0,0,0,'','','on','lumen');
   __Tmp.win_fld(_win,,'DOSTEP',,,'3,7,7',0,0,'',0,'Uprawnienie (T/N/?)'@,2,,"'T'","'N'","'?'");
   __Tmp.win_fld(_win,,'KOD',,,7,0,1,'Kod'@@,0,'Kod formy współpracy'@,,);
   __Tmp.win_fld(_win,,'OPIS',,,83,0,1,'Opis'@@,0,'Opis formy współpracy'@,,);
   _formula:="__TmpEnd:=__Tmp.DOSTEP<>'T'; __Tmp.DOSTEP:={? __Tmp.DOSTEP<>'T' || 'T' || 'N' ?}; __Tmp.put()";
   __Tmp.win_act(
      _win,0,'Formuła','Zmień'@@,'','Zmiana ustawień bieżącego zapisu'@,_formula,,1,0,"","",'Z',0,
      'mobile=1,target=record'
   );
   _formula:="__TmpEnd:=__Tmp.DOSTEP<>'T'; __Tmp.DOSTEP:='T'; __Tmp.put()";
   __Tmp.win_act(
      _win,0,'Formuła','Nadaj'@@,'','Nadanie uprawnień'@,_formula,,0,1,"","",'N',0,'mobile=1,target=record'
   );
   _formula:="__TmpEnd:=__Tmp.DOSTEP<>'N'; __Tmp.DOSTEP:='N'; __Tmp.put()";
   __Tmp.win_act(
      _win,0,'Formuła','Odbierz'@@,'','Wycofanie uprawnień'@,_formula,,0,1,"","",'O',0,'mobile=1,target=record'
   );

   _btnN:=__Tmp.win_btn(
      _win,'text="%1",btn_label_align=center,panel=right,align=begin'['Nadaj'@@],'menu:N',,,0,,,'noempty'
   );
   __Tmp.btn_sopt(_win,_btnN,'tooltip="%1"'['Nadanie uprawnień'@]);
   _btnO:=__Tmp.win_btn(
      _win,'text="%1",btn_label_align=center,panel=right,align=begin'['Odbierz'@@],'menu:O',,,0,,,'noempty'
   );
   __Tmp.btn_sopt(_win,_btnO,'tooltip="%1"'['Wycofanie uprawnień'@]);

   {? _grp
   || _formula:="__TmpEnd:=__Tmp.DOSTEP<>'?'; __Tmp.DOSTEP:='?'; __Tmp.put()";
      __Tmp.win_act(_win,,'Formuła','N&ie zmieniaj'@@,,,_formula,,,1,,,'M');
      __Tmp.win_btn(_win,'text=%1,panel=right,align=begin'['Nie zmieniaj'@],'menu:M')
   ?};
   _formula:="__TmpEnd:=2; sel_exit()";
   __Tmp.win_act(_win,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A');
   __Tmp.win_btn(_win,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A');
   __Tmp.win_btn(_win,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
   __Tmp.win_act(_win,0,'Szukaj','','','',"","",0,0,"","",'',0,'mobile=1,target=window');
   __Tmp.win_act(_win,0,'Kolejność','','','',"","",0,0,"","",'',0,'mobile=1,target=window');
   __Tmp.win_act(_win,,'Okienko',,,,,"
      {? __TmpEnd=1
      || FUN.ask('Czy zakończyć nadawania uprawnień?\n(Wprowadzone zmiany będą anulowane).'@)
      || 1
      ?}
   ");

   {? ~_grp
   || __Tmp.win_act(_win,,'Rekord',,,,"
         _sel:=__Tmp.sel_size();
         {? __Tmp.DOSTEP='T' & ~_sel
         || __Tmp.actions(__Tmp.win_sel('?'),,'O',1)
         || __Tmp.actions(__Tmp.win_sel('?'),,'N',1)
         ?};
         _gray:={? _sel || '' |? __Tmp.DOSTEP='T' || 'N' || 'O' ?};
         __Tmp.actions_grayed(__Tmp.win_sel('?'),_gray)
      ")
   ?};
   __Tmp.win_sel(_win);
   {? __Tmp.select()
   || {? __Tmp.first()
      || USERS_FZ.cntx_psh();
         USERS_FZ.index('UNIQUE');
         USERS_FZ.prefix(REF.FIRMA);
         F_ZATR.cntx_psh();
         F_ZATR.prefix();
         {!
         |? {? __Tmp.DOSTEP<>'?'
            || {? USERS_FZ.seek(__Tmp.REF,) & F_ZATR.seek(__Tmp.F_ZATR,)
               || {? _tab.first()
                  || USERS.cntx_psh();
                     USERS.prefix();
                     {!
                     |? {? USERS.seek(_tab.REF,)
                        || _jest:=USERS_FZ.find_key(USERS.ref(),F_ZATR.ref());
                           {? _jest
                           || USERS_FZ.DOSTEP:=__Tmp.DOSTEP;
                              USERS_FZ.put()
                           ?}
                        ?};
                        _tab.next()
                     !};
                     USERS.cntx_pop()
                  ?}
               ?}
            ?};
            __Tmp.next()
         !};
         F_ZATR.cntx_pop();
         USERS_FZ.cntx_pop();
         {? _grp
         || _res:=1
         || USERS_FZ.cntx_psh();
            USERS_FZ.index('W_ZATR');
            USERS_FZ.prefix(REF.FIRMA,USERS.ref());
            _all:=USERS_FZ.size();
            USERS_FZ.index('DOSTEP');
            USERS_FZ.prefix('T',REF.FIRMA,USERS.ref());
            _cur:=USERS_FZ.size();
            USERS_FZ.cntx_pop();
            _res:={? _cur=0     || 0
                  |? _cur=_all  || 2
                  |? _cur<>_all || 1
                  ?}
         ?}
      ?}
   ?};
   VAR_DEL.delete('__Tmp','__TmpEnd')
?};

:: Aktualne ustawienia uprawnień dla pojedynczego użytkownika
{? ~_grp
|| _new:=sql(_upq,REF.FIRMA,USERS.ref());
:: Sprawdzenie, czy zmieniły się uprawnienia
   {? ~sql('select :_a.REF from :_a join :_b using (:_a.REF,:_b.REF) where :_a.PERM<>:_b.PERM',_new,_old).first()
   || _res:=-1
   ?}
?};

_res


\copy_f_zatr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła kopiuje uprawnienia do form współpracy. Dokładniej: dba o to, aby użytkownik "TO" miał w tabeli
::       USERS_FZ rekordy odpowiadające wszystkim aktualnie zdefiniowanym formom współpracy. I dla takiego układu
::       kopiuje uprawnienia z użytkownika "FROM". Teoretycznie może się zatem zdarzyć, że "TO" będzie miał więcej
::       rekordów niż "FROM". Te "nowe" formy współpracy otrzymaja uprawnienie "?" - nieokreślone.
::       Formuła działa na zasadzie aktualizacji danych (nawet dla ustawionej flagi "CLEAR" nie ma kasowania rekordów
::       i ponownego ich dodawania).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;

:: Tablica konwersji uprawnień dla "Dodaj do istniejących".
::+---------------+---+---+---+
::| "TO" \ "FROM" |   |   |   |
::|     v \    -> | ? | N | T |
::+---------------+---+---+---+
::|     ?         | ? | N | T |
::+---------------+---+---+---+
::|     N         | N | N | T |
::+---------------+---+---+---+
::|     T         | T | T | T |
::+---------------+---+---+---+
_firma:=exec('ref_firma','ustawienia');

USERS_FZ.cntx_psh();
USERS_FZ.index('UNIQUE');
USERS_FZ.prefix();
F_ZATR.cntx_psh();
F_ZATR.index('UNIQUE');
F_ZATR.prefix();
_again:=F_ZATR.first();
{!
|? _again
|! _f_zatr:=F_ZATR.ref();
   _dn:={? USERS_FZ.find_key(_firma,_from,_f_zatr) || USERS_FZ.DOSTEP || '?' ?};
   {? USERS_FZ.find_key(_firma,_to,_f_zatr)
   || _da:=USERS_FZ.DOSTEP;
      {? (_clear & _da<>_dn) | (_da='?' & _dn<>'?') | (_da='N' & _dn='T')
      || USERS_FZ.DOSTEP:=_dn;
         USERS_FZ.put()
      ?}
   || USERS_FZ.blank(1);
      USERS_FZ.FIRMA:=_firma;
      USERS_FZ.USERS:=_to;
      USERS_FZ.F_ZATR:=_f_zatr;
      USERS_FZ.DOSTEP:=_dn;
      USERS_FZ.add()
   ?};
   _again:=F_ZATR.next()
!};
F_ZATR.cntx_pop();
USERS_FZ.cntx_pop();
~~


\set_ud_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [17.00]
::  MOD: DG [23.25]
:: OPIS: Umożliwia nadanie uprawnień do elementów schematów danych
::   WE: _a - wskazanie użytkownika lub tabela z użytkownikami
::   WY: -1 - uprawnienia bez zmian
::        0 - brak uprawnień
::        1 - częściowe uprawnienia
::        2 - pełne uprawnienia
::----------------------------------------------------------------------------------------------------------------------
: wstępny warunek definiowania uprawnień: użytkownik aktywny, z dostępem przez pulpit
{? USERS.AKT<>'T' | (USERS.JTERM<>'T' & USERS.EKIOSK<>'T')
|| FUN.emsg(
      'Uprawnienia mogą być ustawione wyłącznie dla aktywnych użytkowników,\n'
      'mogących uruchomić aplikację z poziomu pulpitu systemu operacyjnego.'
   );
   return(0)
?};

_grp:=var_pres('_a')=type_of(SYSLOG);
_tab:=exec('usr_tab','b_perm',{? _grp || _a  || USERS.ref() ?});
_old:=Perm.getType();
_cur:={? _old=0 || 3 |? _old=1 || 2 |? _old=2 || 1 ?};
_pyt:=FUN.choice(
   'Do których schematów danych nadać uprawnienia?'@,_cur,
   '&Wszystkich'@,'Wy&branych'@,'Ża&dnych'@
);
_res:=-1;

:: Uzupełnienie zawartość tabeli dla użytkownika
{? _tab.first()
|| USERS.cntx_psh();
   USERS.prefix();
   {!
   |? {? USERS.seek(_tab.REF,)
      || exec('upr_usr_add','schemat',USERS.ref())
      ?};
      _tab.next()
   !};
   USERS.cntx_pop()
?};

:: Przetwarzanie danych w przypadku nadawania bądź odbierania wszystkich uprawnień
{? _pyt=1 | _pyt=3
|| {? _tab.size()
   || UDB_GRP.cntx_psh();
      UDB_GRP.index('SYMBOL');
      UDB_GRP.prefix();
      UDB_SYS.cntx_psh();
      UDB_SYS.index('OCHRONA');
      UDB_SYS.prefix();
      UD_DEF.cntx_psh();
      UD_DEF.index('SYMBOL');
      UD_DEF.prefix();
      UDB_UPR.cntx_psh();
      UDB_UPR.index('UNIQUE');
      UDB_UPR.prefix();
      USERS.cntx_psh();
      USERS.prefix();
      {? UDB_GRP.first()
      || {!
         |? UDB_SYS.prefix(UDB_GRP.ref,'T');
            {? UDB_SYS.first()
            || {!
               |? UD_DEF.prefix(exec('domyslny','schemat',UDB_SYS.UD_TYP));
                  {? UD_DEF.first()
                  || {!
                     |? {? _tab.first()
                        || {!
                           |? {? USERS.seek(_tab.REF,)
                              || UDB_UPR.prefix(UDB_SYS.ref(),USERS.ref,UD_DEF.UD_SKL);
                                 {? UDB_UPR.first()
                                 || UDB_UPR.DOSTEP:={? _pyt=1 || 'T' || 'N' ?};
                                    {? UDB_UPR.put()
                                    || exec('udb_upr_zmien','schemat')
                                    ?}
                                 ?}
                              ?};
                              _tab.next()
                           !}
                        ?};
                        UD_DEF.next()
                     !}
                  ?};
                  UDB_SYS.next()
               !}
            ?};
            UDB_GRP.next()
         !}
      ?};

      USERS.cntx_pop();
      UDB_UPR.cntx_pop();
      UD_DEF.cntx_pop();
      UDB_SYS.cntx_pop();
      UDB_GRP.cntx_pop()
   ?};
   _res:={? _pyt=1 || 2 || 0 ?}

:: Przetwarzanie danych w przypadku nadawania lub odbierania wybranych uprawnień
|? _pyt=2
|| VAR_DEL.delete('__Tmp','__TmpNdx','__TmpNdx2','__TmpWin','__TmpEnd');
   __Tmp:=tab_tmp(2,
      'UDB_SYS','INTEGER',   'UDB_SYS'@,
      'TREE',   'TREE_REF',  'Tree'@,
      'SYMBOL', 'STRING[16]','Symbol'@,
      'SCHEMAT','STRING[16]','Schemat'@,
      'OPIS',   'STRING[60]','Opis'@,
      'DOSTEP', 'STRING[1]', 'Uprawnienie'@,
      'REF',    'INTEGER',   'UDB_UPR'@,
      'UD_TYP', 'INTEGER',   'UD_TYP'@,
      'UD_DEF', 'INTEGER',   'UD_DEF'@,
      'UD_SCH', 'STRING[16]','UD_SCH'@
   );
   __TmpNdx:=__Tmp.index('?');
   __TmpNdx2:=__Tmp.ndx_tmp(,,'UDB_SYS',,,'UD_DEF',,);
   __Tmp.index(__TmpNdx2);

   UDB_GRP.cntx_psh();
   UDB_GRP.index('SYMBOL');
   UDB_GRP.prefix();
   UDB_SYS.cntx_psh();
   UDB_SYS.index('OCHRONA');
   UDB_SYS.prefix();
   UD_DEF.cntx_psh();
   UD_DEF.index('SYMBOL');
   UD_DEF.prefix();
   UDB_UPR.cntx_psh();
   UDB_UPR.index('UNIQUE');
   UDB_UPR.prefix();
   USERS.cntx_psh();
   USERS.prefix();
   {? UDB_GRP.first()
   || {!
      |? UDB_SYS.prefix(UDB_GRP.ref,'T',);
         {? UDB_SYS.first()
         || {!
            |? __Tmp.prefix(#UDB_SYS.ref());
               UD_DEF.prefix(exec('domyslny','schemat',UDB_SYS.UD_TYP));
               {? UD_DEF.first()
               || {!
                  |? UDB_UPR.prefix(UDB_SYS.ref(),USERS.ref(),UD_DEF.UD_SKL);
                     {? UDB_UPR.first()
                     || _tree:=0;
                        {? __Tmp.find_key(UD_DEF.UD_DEF)
                        || _tree:=#__Tmp.ref
                        ?};
                        __Tmp.blank();
                        __Tmp.TREE:=_tree;
                        __Tmp.REF:=#UDB_UPR.ref();
                        __Tmp.SCHEMAT:=$UD_DEF.UD_SCH;
                        __Tmp.SYMBOL:=UD_DEF.SYMBOL;
                        __Tmp.OPIS:=UD_DEF.OPIS;
                        __Tmp.UDB_SYS:=#UDB_SYS.ref();
                        __Tmp.UD_TYP:=#UD_TYP.ref();
                        __Tmp.UD_DEF:=#UD_DEF.ref();
                        __Tmp.UD_SCH:=$UD_DEF.UD_SCH;
                        __Tmp.DOSTEP:={? _grp || '?' || UDB_UPR.DOSTEP ?};
                        __Tmp.add()
                     ?};
                     UD_DEF.next()
                  !}
               ?};
               UDB_SYS.next()
            !}
         ?};
         UDB_GRP.next()
      !}
   ?};
   USERS.cntx_pop();
   UDB_UPR.cntx_pop();
   UD_DEF.cntx_pop();
   UDB_SYS.cntx_pop();
   UDB_GRP.cntx_pop();
   __TmpEnd:=0;

   __TmpWin:=__Tmp.mk_sel('Schematy danych'@,'N',0,'ud_def_upr',1,21,13,1,'N','N',0,0,0,'','','on','lumen');
   __Tmp.win_fld(__TmpWin,,'SYMBOL',,,27,0,1,'Symbol'@@,0,'Identyfikator'@,,);
   __Tmp.win_fld(__TmpWin,,'OPIS',,,63,0,1,'Opis'@@,0,'Krótki opis'@,,);
   __Tmp.win_fld(__TmpWin,,'DOSTEP',,,'3,7,7',0,0,'',0,'Uprawnienie (T/N/?)'@,2,,"'T'","'N'","'?'");
   _formula:="__TmpEnd:=__Tmp.DOSTEP<>'T'; __Tmp.DOSTEP:={? __Tmp.DOSTEP<>'T' || 'T' || 'N' ?}; __Tmp.put()";
   __Tmp.win_act(
      __TmpWin,0,'Formuła','Zmień'@@,'','Zmiana ustawień bieżącego zapisu'@,_formula,,1,0,"","",'Z',0,
      'mobile=1,target=record'
   );
   _formula:="__TmpEnd:=__Tmp.DOSTEP<>'T'; __Tmp.DOSTEP:='T'; __Tmp.put()";
   __Tmp.win_act(
      __TmpWin,0,'Formuła','Nadaj'@@,'','Nadanie uprawnień'@,_formula,,0,1,"","",'N',0,'mobile=1,target=record'
   );
   _formula:="__TmpEnd:=__Tmp.DOSTEP<>'N'; __Tmp.DOSTEP:='N'; __Tmp.put()";
   __Tmp.win_act(
      __TmpWin,0,'Formuła','Odbierz'@@,'','Wycofanie uprawnień'@,_formula,,0,1,"","",'O',0,'mobile=1,target=record'
   );

   _btnN:=__Tmp.win_btn(
      __TmpWin,'text="%1",btn_label_align=center,panel=right,align=begin'['Nadaj'@@],'menu:N',,,0,,,'noempty'
   );
   __Tmp.btn_sopt(__TmpWin,_btnN,'tooltip="%1"'['Nadanie uprawnień'@]);
   _btnO:=__Tmp.win_btn(
      __TmpWin,'text="%1",btn_label_align=center,panel=right,align=begin'['Odbierz'@@],'menu:O',,,0,,,'noempty'
   );
   __Tmp.btn_sopt(__TmpWin,_btnO,'tooltip="%1"'['Wycofanie uprawnień'@]);

   {? _grp
   || _formula:="__TmpEnd:=__Tmp.DOSTEP<>'?'; __Tmp.DOSTEP:='?'; __Tmp.put()";
      __Tmp.win_act(__TmpWin,,'Formuła','N&ie zmieniaj'@@,,,_formula,,,1,,,'M');
      __Tmp.win_btn(__TmpWin,'text=%1,panel=right,align=begin'['Nie zmieniaj'@],'menu:M')
   ?};
   _formula:="__TmpEnd:=2; sel_exit()";
   __Tmp.win_act(__TmpWin,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A');
   __Tmp.win_btn(__TmpWin,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A');
   __Tmp.win_btn(__TmpWin,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
   __Tmp.win_act(__TmpWin,0,'Formuła','Zwiń/&rozwiń'@@,,'Zwijanie / rozwijanie drzewa'@,
      "params_exec('zwin_rozwin','#tree')",,,,,,'R'
   );
   __Tmp.win_act(__TmpWin,0,'Szukaj','','','',"","",0,0,"","",'',0,'mobile=1,target=window');
   __Tmp.win_act(__TmpWin,0,'Kolejność','','','',"","",0,0,"","",'',0,'mobile=1,target=window');
   __Tmp.win_act(__TmpWin,,'Okienko',,,,,"
::      {? __TmpEnd=1
::      || FUN.ask('Czy zakończyć nadawania uprawnień?\n(Wprowadzone zmiany będą anulowane).'@)
::      || 1
::      ?}
   ");

   {? ~_grp
   || __Tmp.win_act(__TmpWin,,'Rekord',,,,"
         _sel:=__Tmp.sel_size();
         {? __Tmp.DOSTEP='T' & ~_sel
         || __Tmp.actions(__Tmp.win_sel('?'),,'O',1)
         || __Tmp.actions(__Tmp.win_sel('?'),,'N',1)
         ?};
         _gray:={? _sel || '' |? __Tmp.DOSTEP='T' || 'N' || 'O' ?};
         __Tmp.actions_grayed(__Tmp.win_sel('?'),_gray)
      ")
   ?};
   __Tmp.win_sel(__TmpWin);

: panel uprawnień użytkownika
   _wnd:=UDB_GRP.grp_make('Uprawnienia'@,
:  przed wyświetleniem
      "  UDB_GRP.cntx_psh();
         UDB_GRP.index('SYMBOL');

         UDB_SYS.cntx_psh();
         UDB_SYS.index('OCHRONA');

         __Tmp.cntx_psh();
         __Tmp.index(__TmpNdx);
         1
      ",
:  identyfikator
      'usr_skl_perm',,,
:  przy zamknięciu
      "  {? __TmpEnd=1
         || FUN.ask('Czy zakończyć nadawania uprawnień?\n(Wprowadzone zmiany będą anulowane).'@)
         || UDB_GRP.cntx_pop();
            UDB_SYS.cntx_pop();
            __Tmp.ndx_drop(__TmpNdx);
            __Tmp.cntx_pop();
            1
         ?}
      "
   );
: lista typów danych
   UDB_GRP.grp_sel(_wnd,,'NAW',,
:  po odświeżeniu
      "grp_disp(UDB_SYS,'NAW',1,1)",
:  położenie, wysokość
      ,,10,
:  przed obsługą
      "",
:  po obsłudze
      "",
:  utrwalenie, aktywacja, tryb
      ,,'maximized_with_title'
   );
: lista grup dziedzin
   UDB_GRP.grp_splt(_wnd,,'horizontal','bottom');
   UDB_GRP.grp_sel(_wnd,UDB_SYS,'NAW',,
:  po odświeżeniu
      "grp_disp(__Tmp,__TmpWin,1,1)",
:  położenie, wysokość
      ,,,
:  przed obsługą
      "UDB_SYS.prefix(UDB_GRP.ref(),'T')",
:  po obsłudze
      "",
:  utrwalenie, aktywacja, tryb
      ,,'maximized_with_title'
   );
: drzewko struktury z uprawnieniami
   UDB_GRP.grp_splt(_wnd,,'vertical','right');
   UDB_GRP.grp_sel(_wnd,__Tmp,__TmpWin,,
:  po odświeżeniu
      "",
:  położenie, wysokość
      ,,,
:  przed obsługą
      "  {? exec('dom_empty','#table',UDB_SYS)>0
         || return('#disable')
         ?};
         {? USERS.AKT<>'T' | (USERS.JTERM<>'T' & USERS.EKIOSK<>'T')
         || return('#disable')
         ?};
         __Tmp.prefix(#UDB_SYS.ref())
      ",
:  po obsłudze
      "",
:  utrwalenie, aktywacja, tryb
      ,,'maximized_with_title'
   );

   UDB_GRP.win_sel(_wnd);
   {? UDB_GRP.select()
   || __Tmp.prefix();
      {? __Tmp.first()
      || UDB_UPR.cntx_psh();
         UDB_UPR.index('UNIQUE');
         UDB_UPR.prefix();
         {!
         |? {? __Tmp.DOSTEP<>'?'
            || {? UDB_UPR.seek(__Tmp.REF,)
               || UDB_UPR.DOSTEP:=__Tmp.DOSTEP;
                  {? UDB_UPR.put()
                  || exec('udb_upr_zmien','schemat')
                  ?}
               ?}
            ?};
            __Tmp.next()
         !};
         UDB_UPR.index('FK_USR');
         UDB_UPR.prefix(_tab.REF,'T',);
         _res:={? _grp | UDB_UPR.first() || 1 || 0 ?};
         UDB_UPR.cntx_pop()
      ?}
   ?};
   VAR_DEL.delete('__Tmp','__TmpNdx','__TmpNdx2','__TmpWin','__TmpEnd')
?};

_res


\copy_ud_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [17.00]
:: OPIS: Formuła kopiuje uprawnienia do elementów schematów danych. Dokładniej: dba o to, aby użytkownik "TO" miał
::       w tabeli UDB_UPR rekordy odpowiadające wszystkim aktualnie zdefiniowanym elementom schematów danych.
::       I dla takiego układu kopiuje uprawnienia z użytkownika "FROM". Teoretycznie może się zatem zdarzyć, że "TO"
::       będzie miał więcej rekordów niż "FROM". Te "nowe" elementy schematów danych otrzymaja uprawnienie
::        "?" - nieokreślone. Formuła działa na zasadzie aktualizacji danych (nawet dla ustawionej flagi "CLEAR" nie ma
::       kasowania rekordów i ponownego ich dodawania).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;

:: Tablica konwersji uprawnień dla "Dodaj do istniejących".
::+---------------+---+---+---+
::| "TO" \ "FROM" |   |   |   |
::|     v \    -> | ? | N | T |
::+---------------+---+---+---+
::|     ?         | ? | N | T |
::+---------------+---+---+---+
::|     N         | N | N | T |
::+---------------+---+---+---+
::|     T         | T | T | T |
::+---------------+---+---+---+

USERS.cntx_psh();
USERS.prefix();
_ok:=(
   USERS.seek(_from) & USERS.JTERM='T' & USERS.AKT='T' &
   USERS.seek(_to) & USERS.JTERM='T' & USERS.AKT='T'
);
_key:=$USERS.ref();
USERS.cntx_pop();
{? _ok=0
|| return()
?};
:: ustaw semafor operacji hurtowej
exec('add','#bulk',USERS,'add',_key);
UDB_UPR.cntx_psh();
UDB_UPR.index('UNIQUE');
UDB_UPR.prefix();
UD_SKL.cntx_psh();
UD_SKL.index('SYMBOL');
UDB_SYS.cntx_psh();
UDB_SYS.prefix();
_dsys:=UDB_SYS.first();
{!
|? _dsys
|! _udb_sys:=UDB_SYS.ref();
   UD_SKL.prefix(UDB_SYS.UD_TYP);
   _dskl:=UD_SKL.first();
   {!
   |? _dskl
   |! _ud_skl:=UD_SKL.ref();
      _dn:={? UDB_UPR.find_key(_udb_sys,_from,_ud_skl) || UDB_UPR.DOSTEP || '?' ?};
      {? UDB_UPR.find_key(_udb_sys,_to,_ud_skl)
      || _da:=UDB_UPR.DOSTEP;
         {? (_clear & _da<>_dn) | (_da='?' & _dn<>'?') | (_da='N' & _dn='T')
         || UDB_UPR.DOSTEP:=_dn;
            UDB_UPR.put()
         ?}
      || UDB_UPR.blank(1);
         UDB_UPR.UDB_SYS:=_udb_sys;
         UDB_UPR.USERS:=_to;
         UDB_UPR.UD_SKL:=_ud_skl;
         UDB_UPR.DOSTEP:=_dn;
         UDB_UPR.add()
      ?};
      _dskl:=UD_SKL.next()
   !};
   _dsys:=UDB_SYS.next()
!};
:: usuń semafor hurtowego przetwarzania
exec('del','#bulk',USERS,'add',_key);
{? UDB_UPR.first()<>0
:: aktualizuj redundantne informacje
|| exec('udb_upr_zmien','schemat')
?};
UDB_SYS.cntx_pop();
UD_SKL.cntx_pop();
UDB_UPR.cntx_pop();
~~


\set_upr_stankas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Czy użytkownik ma uprawnienia do stanowisk kasowych (nadawanie uprawnień)
::   WE: _a - wskazanie na użytkownika lub tabela z użytkownikami
::   WY: 2 - pełne, 1 - do wybranych, 0 - nie ma, -1 - bez zmian
::  OLD: \mf4_upr/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=-1;
_opis:='Uprawnienia do stanowisk kasowych'@;
_label:='#userup__kas';
_pyt:='Do których stanowisk kasowych nadać uprawnienia?'@;
_pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich stanowisk kasowych.\nCzy kontynuować?'@;
_pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich stanowisk kasowych.\nCzy kontynuować?'@;
_jak0:={? B_PERM_U.FULL='T' || 2 |? B_PERM_U.ACCESS='T' || 1 || 0?};
_grp:=var_pres('_a')=type_of(SYSLOG);
_tab:=exec('usr_tab','b_perm',{? _grp || _a  || USERS.ref() ?});
VAR_DEL.delete('Tmp','TmpEnd');
TmpEnd:=0;
Tmp:=exec('new_TabUpr','b_perm',_opis,_label,0,0,,_grp);
_add:="
      Tmp.blank();
      Tmp.KOD:=_a;
      Tmp.OPIS:=_b;
      Tmp.REF:=_c;
      Tmp.ACCESS:=_d;
      Tmp.UPR:=_e;
      Tmp.add(1)";

STANKAS.cntx_psh();
STANKAS.index('KOD');
STANKAS.prefix();
_loop:=STANKAS.first();
{!
|? _loop
|!
   _stankas:=STANKAS.ref();
   _num:=0;
   KUSERUPR.cntx_psh();
   KUSERUPR.index('KU_ST');
   KUSERUPR.prefix(USERS.ref(),STANKAS.ref());
   _access:={? _grp || '' |? _jak0=2 | KUSERUPR.first() || 'T' || 'N' ?};
   _upr:={? KUSERUPR.first() || KUSERUPR.UPR || 'T' ?};
   KUSERUPR.cntx_pop();
   (_add)($STANKAS.KOD,STANKAS.NAZWA,$STANKAS.ref(),_access,_upr);
   _loop:=STANKAS.next()
!};
STANKAS.cntx_pop();

_wyb0:={? _jak0=1 || 2 |? _jak0=2 || 1 || 0 ?};
_wyb:=FUN.choice(_pyt,_wyb0,'&Wszystkich'@,'Wy&branych'@,'Ża&dnego'@);
{? _wyb=1
|| _jak:=2
|? _wyb=2
|| _jak:=1
|? _wyb=3
|| _jak:=0
|| _jak:=-1
?};

{? (_jak=2 | _jak=0) & _jak<>_jak0
|| {? ~FUN.ask({? _jak=2 || _pyt1 || _pyt2 ?})
   || _jak:=-1
   ?}
?};

{? _jak=2 | _jak=0
|| {? Tmp.first()
   || {!
      |? {? _jak=2
         || Tmp.ACCESS:='T';
            Tmp.UPR:='T'
         || Tmp.ACCESS:='N'
         ?};
         Tmp.put();
         Tmp.next()
      !};
      Tmp.first()
   ?}
?};

{? _jak=0 | _jak=2 | (_jak=1 & Tmp.select())
||
   Tmp.first();
   {!
   |?
      {? Tmp.ACCESS<>''
      ||
         {? _tab.first()
         || USERS.cntx_psh();
            USERS.prefix();
            {!
            |?
               {? USERS.seek(_tab.REF,)
               ||
                  KUSERUPR.index('KU_ST');
                  KUSERUPR.prefix(USERS.ref());
                  _stankas:=exec('FindAndGet','#table',STANKAS,Tmp.REF,,,null());
                  {? Tmp.ACCESS='T'
                  ||
                     {? KUSERUPR.find_key(_stankas)
                     || KUSERUPR.UPR:=Tmp.UPR;
                        KUSERUPR.STANKASN:=KUSERUPR.STANKAS().NAZWA;
                        KUSERUPR.put()
                     || KUSERUPR.blank();
                        KUSERUPR.KU:=USERS.ref();
                        KUSERUPR.STANKAS:=_stankas;
                        KUSERUPR.STANKASN:=KUSERUPR.STANKAS().NAZWA;
                        KUSERUPR.UPR:=Tmp.UPR;
                        KUSERUPR.add()
                     ?}
                  || {? KUSERUPR.find_key(_stankas)
                     || KUSERUPR.del()
                     ?}
                  ?}
               ?};
               _tab.next()
            !};
            USERS.cntx_pop()
         ?}
      ?};
      Tmp.next()
   !};
   {? _jak=1 & KUSERUPR.size()=0
   || _wyn:=0
   || _wyn:=_jak
   ?}
?};

VAR_DEL.delete('Tmp','TmpEnd');
_wyn


\set_upr_mkasa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Czy użytkownik ma uprawnienia do multikas (nadawanie uprawnień)
::   WE: _a - wskazanie na użytkownika lub tabela z użytkownikami
::   WY: 2 - pełne, 1 - do wybranych, 0 - nie ma, -1 - bez zmian
::----------------------------------------------------------------------------------------------------------------------
_wyn:=-1;
_opis:='Uprawnienia do multikas'@;
_label:='#userup__kas';
_pyt:='Do których multikas nadać uprawnienia?'@;
_pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich multikas.\nCzy kontynuować?'@;
_pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich multikas.\nCzy kontynuować?'@;
_jak0:={? B_PERM_U.FULL='T' || 2 |? B_PERM_U.ACCESS='T' || 1 || 0?};
_grp:=var_pres('_a')=type_of(SYSLOG);
_tab:=exec('usr_tab','b_perm',{? _grp || _a  || USERS.ref() ?});
VAR_DEL.delete('Tmp','TmpEnd');
TmpEnd:=0;
Tmp:=exec('new_TabUpr','b_perm',_opis,_label,0,,0,_grp);

_add:="
      Tmp.blank();
      Tmp.KOD:=_a;
      Tmp.OPIS:=_b;
      Tmp.REF:=_c;
      Tmp.ACCESS:=_d;
      Tmp.add(1)";

MKASA.cntx_psh();
MKASA.index('NAZ');
MKASA.prefix();
_loop:=MKASA.first();
{!
|? _loop
|!
   _mkasa:=MKASA.ref();
   _num:=0;
   MKASAUPR.cntx_psh();
   MKASAUPR.index('UNIK');
   MKASAUPR.prefix(USERS.ref(),MKASA.ref());
   _access:={? _grp || '' |? _jak0=2 | MKASAUPR.first() || 'T' || 'N' ?};
   MKASAUPR.cntx_pop();
   (_add)($#MKASA.ref(),MKASA.NAZ,$MKASA.ref(),_access);
   _loop:=MKASA.next()
!};
MKASA.cntx_pop();

_wyb0:={? _jak0=1 || 2 |? _jak0=2 || 1 || 0 ?};
_wyb:=FUN.choice(_pyt,_wyb0,'&Wszystkich'@,'Wy&branych'@,'Ża&dnej'@);
{? _wyb=1
|| _jak:=2
|? _wyb=2
|| _jak:=1
|? _wyb=3
|| _jak:=0
|| _jak:=-1
?};

{? (_jak=2 | _jak=0) & _jak<>_jak0
|| {? ~FUN.ask({? _jak=2 || _pyt1 || _pyt2 ?})
   || _jak:=-1
   ?}
?};

{? _jak=2 | _jak=0
|| {? Tmp.first()
   || {!
      |? {? _jak=2
         || Tmp.ACCESS:='T'
         || Tmp.ACCESS:='N'
         ?};
         Tmp.put();
         Tmp.next()
      !};
      Tmp.first()
   ?}
?};
{? _jak=0 | _jak=2 | (_jak=1 & Tmp.select())
||
   Tmp.first();
   {!
   |?
      {? Tmp.ACCESS<>''
      ||
         {? _tab.first()
         ||
            USERS.cntx_psh();
            USERS.prefix();
            {!
            |?
               {? USERS.seek(_tab.REF,)
               ||
                  MKASAUPR.index('UNIK');
                  MKASAUPR.prefix(USERS.ref());
                  _mkasa:=exec('FindAndGet','#table',MKASA,Tmp.REF,,,null());
                  _naz:=exec('FindAndGet','#table',MKASA,Tmp.REF,,"NAZ",'');
                  {? Tmp.ACCESS='T'
                  ||
                     {? ~MKASAUPR.find_key(_mkasa)
                     || MKASAUPR.blank(1);
                        MKASAUPR.USER:=USERS.ref();
                        MKASAUPR.MKASA:=_mkasa;
                        MKASAUPR.MKASAN:=_naz;
                        MKASAUPR.add()
                     ?}
                  || {? MKASAUPR.find_key(_mkasa)
                     || MKASAUPR.del()
                     ?}
                  ?}
               ?};
               _tab.next()
            !};
            USERS.cntx_pop()
         ?}
      ?};
      Tmp.next()
   !};
   {? _jak=1 & MKASAUPR.size()=0
   || _wyn:=0
   || _wyn:=_jak
   ?}
?};

VAR_DEL.delete('Tmp','TmpEnd');
_wyn


\copy_fjks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Kopia uprawnień do jednostek księgowych
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;
USERSDEP.cntx_psh();
USERSDEP.index('UNIK');
{? _clear
|| USERSDEP.prefix(REF.FIRMA,_to);
   {? USERSDEP.first() || {! |? USERSDEP.del !} ?}
?};
USERSDEP.prefix(REF.FIRMA,_from);
{? USERSDEP.first()
|| {!
   |? USERSDEP.cntx_psh();
      USERSDEP.prefix(REF.FIRMA,_to,USERSDEP.DEPT);
      {? ~USERSDEP.first()
      || USERSDEP.USERS:=_to;
         USERSDEP.add()
      ?};
      USERSDEP.cntx_pop();
      USERSDEP.next()
   !}
?};
USERSDEP.cntx_pop();
1


\copy_hrb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Kopia uprawnień do rachunków bankowych
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;
UPR_RB.cntx_psh();
UPR_RB.index('RB');
{? _clear | B_PERM_U.FULL='T'
|| UPR_RB.prefix(_to);
   {? UPR_RB.first() || {! |? UPR_RB.del !} ?}
?};
UPR_RB.prefix(_from);
{? UPR_RB.first()
|| {!
   |? UPR_RB.cntx_psh();
      UPR_RB.prefix(_to,UPR_RB.RB);
      {? ~UPR_RB.first()
      || UPR_RB.USER:=_to;
         UPR_RB.add()
      ?};
      UPR_RB.cntx_pop();
      UPR_RB.next()
   !}
?};
UPR_RB.cntx_pop()


\copy_hrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Kopia uprawnień do rodzajów przelewów
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;
UPR.cntx_psh();
UPR.index('REFKOD');
{? _clear | B_PERM_U.FULL='T'
|| UPR.prefix(_to);
   {? UPR.first() || {! |? UPR.del !} ?}
?};
UPR.prefix(_from);
{? UPR.first()
|| {!
   |? UPR.cntx_psh();
      UPR.prefix(_to,UPR.TP().KOD,);
      {? ~UPR.first()
      || UPR.RR:=_to;
         UPR.add()
      ?};
      UPR.cntx_pop();
      UPR.next()
   !}
?};
UPR.cntx_pop()


\copy_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PS] [17.00]
:: OPIS: Kopiuje uprawnienia do danych z obszaru logistyki pomiędzy użytkownikami.
::       Odczekuje trzech parametrów: FROM  - USERS.ref() - użytkownik, od którego kopiujemy uprawnienia,
::                                    TO    - USERS.ref() - użytkownik, do którego kopiujemy uprawnienia,
::                                    CLEAR - INTEGER     - 1 nadpisuje uprawnienia, 0 jedynie dodaje nowe.
::       UWAGA! Funkcja nie weryfikuje poprawnosci argumentów i przekazywanych parametrów.
::   WE: _a - STRING - rodzaj uprawnień ('ODDZ','MG','STS','STZ','ZAM','ZAW')
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;
_rodz:=_a;
_ind:={? ';ZAM;ZAW;MG;'*_rodz
      || 'MG'
      || _rodz
      ?};

USERS_UP.cntx_psh();
USERS_UP.index(_ind);
{? _clear | B_PERM_U.FULL='T'
|| USERS_UP.prefix(_to,_rodz);
   {? USERS_UP.first() || {! |? USERS_UP.del !} ?}
?};
USERS_UP.prefix(_from,_rodz);
{? USERS_UP.first()
|| {!
   |? USERS_UP.cntx_psh();
      {? _ind='ODDZ'    || USERS_UP.prefix(_to,_rodz,USERS_UP.ODDZ)
      |? _ind='MG'      || USERS_UP.prefix(_to,_rodz,USERS_UP.MG().ODDZ,USERS_UP.MG)
      |? _ind='STS'     || USERS_UP.prefix(_to,_rodz,USERS_UP.STS)
      |? _ind='STZ'     || USERS_UP.prefix(_to,_rodz,USERS_UP.STZ)
      |? _ind='TR_RODZ' || USERS_UP.prefix(_to,_rodz,USERS_UP.TR_RODZ)
      ?};
      {? ~USERS_UP.first()
      || USERS_UP.USERS:=_to;
         USERS_UP.add()
      ?};
      USERS_UP.cntx_pop();
      USERS_UP.next()
   !}
?};
USERS_UP.cntx_pop();
1


\copy_upr_kasa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PS] [17.00]
:: OPIS: Kopia uprawnień do stanowisk kasowych.
::       Odczekuje trzech parametrów: FROM  - USERS.ref() - użytkownik, od którego kopiujemy uprawnienia,
::                                    TO    - USERS.ref() - użytkownik, do którego kopiujemy uprawnienia,
::                                    CLEAR - INTEGER     - 1 nadpisuje uprawnienia, 0 jedynie dodaje nowe.
::       UWAGA! Funkcja nie weryfikuje poprawnosci argumentów i przekazywanych parametrów.
::   WE: _a - STRING - rodzaj uprawnień ('STANKAS','MKASA')
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;
_rodz:=_a;
_tab:={? _rodz='STANKAS' || KUSERUPR
      |? _rodz='MKASA'   || MKASAUPR
      ?};
_ind:={? _rodz='STANKAS' || 'KU_ST'
      |? _rodz='MKASA'   || 'UNIK'
      ?};

_tab.cntx_psh();
_tab.index(_ind);
{? _clear | B_PERM_U.FULL='T'
|| _tab.prefix(_to);
   {? _tab.first() || {! |? _tab.del !} ?}
?};
_tab.prefix(_from);
{? _tab.first()
|| {!
   |? _tab.cntx_psh();
      {? _ind='KU_ST' || _tab.prefix(_to,_tab.STANKAS)
      |? _ind='UNIK'  || _tab.prefix(_to,_tab.MKASA)
      ?};
      {? ~_tab.first()
      || {? _ind='KU_ST' || _tab.KU:=_to
         |? _ind='UNIK'  || _tab.USER:=_to
         ?};
         _tab.add()
      ?};
      _tab.cntx_pop();
      _tab.next()
   !}
?};
_tab.cntx_pop();
1


\set_upr_tar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Czy użytkownik ma uprawnienia do cenników (nadawanie uprawnień)
::   WE: _a - [S/D] Sprzedaży/Dostaw
::       [_b] - wskazanie na użytkownika lub tabela z użytkownikami
::   WY: 2 - pełne, 1 - do wybranych, 0 - nie ma, -1 - bez zmian
::----------------------------------------------------------------------------------------------------------------------
_sd:=_a;
_grp:=var_pres('_b')=type_of(SYSLOG);
_tab_users:=exec('usr_tab','b_perm',{? _grp || _b  || USERS.ref() ?});

_wyn:=-1;

_opis:={? _sd='S' || 'Uprawnienia do cenników sprzedaży'@ || 'Uprawnienia do cenników dostawców'@ ?};
_label:='#userup__tar'+(-_sd);
{? _sd='S'
|| _pyt:='Do których cenników sprzedaży nadać uprawnienia?'@;
   _pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich cenników sprzedaży.\nCzy kontynuować?'@;
   _pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich cenników sprzedaży.\nCzy kontynuować?'@
|| _pyt:='Do których cenników dostawców nadać uprawnienia?'@;
   _pyt1:='Zmiana spowoduje nadanie uprawnień\ndo wszystkich cenników dostawców.\nCzy kontynuować?'@;
   _pyt2:='Zmiana spowoduje odebranie uprawnień\ndo wszystkich cenników dostawców.\nCzy kontynuować?'@
?};
_jak0:={? B_PERM_U.FULL='T' || 2 |? B_PERM_U.ACCESS='T' || 1 || 0?};

VAR_DEL.delete('Tmp','TmpEnd');
TmpEnd:=0;
Tmp:=exec('new_TabUpr','b_perm',_opis,_label,0,,0,_grp);

_add:="
      Tmp.blank();
      Tmp.KOD:=_a;
      Tmp.OPIS:=_b;
      Tmp.REF:=_c;
      Tmp.ACCESS:=_d;
      Tmp.add(1)";

TARGN.cntx_psh();
TARGN.index('S');
TARGN.prefix(_sd);
_loop:=TARGN.first();
{!
|? _loop
|!
   _num:=0;
   TARUP.cntx_psh();
   TARUP.index('USER');
   TARUP.prefix(USERS.ref(),null(),TARGN.ref());
   _access:={? _grp || '' |? _jak0=2 | TARUP.first() || 'T' || 'N' ?};
   TARUP.cntx_pop();
   (_add)(TARGN.KOD,TARGN.OP,$TARGN.ref(),_access);
   _loop:=TARGN.next()
!};
TARGN.cntx_pop();

_wyb0:={? _jak0=1 || 2 |? _jak0=2 || 1 || 0 ?};
_wyb:=FUN.choice(_pyt,_wyb0,'&Wszystkich'@,'Wy&branych'@,'Ża&dnego'@);
{? _wyb=1
|| _jak:=2
|? _wyb=2
|| _jak:=1
|? _wyb=3
|| _jak:=0
|| _jak:=-1
?};

{? (_jak=2 | _jak=0) & _jak<>_jak0
|| {? ~FUN.ask({? _jak=2 || _pyt1 || _pyt2 ?})
   || _jak:=-1
   ?}
?};

{? _jak=2 | _jak=0
|| {? Tmp.first()
   || {!
      |? {? _jak=2
         || Tmp.ACCESS:='T'
         || Tmp.ACCESS:='N'
         ?};
         Tmp.put();
         Tmp.next()
      !};
      Tmp.first()
   ?}
?};

{? _jak=0 | _jak=2 | (_jak=1 & Tmp.select())
|| TARUP.index('USER');
   TARUP.prefix(USERS.ref(),null());
   _loop:=Tmp.first();
   {!
   |? _loop
   |!
      {? _tab_users.first()
      || {!|?
            {? USERS.seek(_tab_users.REF,)
            || TARUP.prefix(USERS.ref(),null());
               _targn:=exec('FindAndGet','#table',TARGN,Tmp.REF,,,null());
               {? Tmp.ACCESS='T'
               ||
                  {? ~TARUP.find_key(_targn)
                  || TARUP.blank(1);
                     TARUP.USER:=USERS.ref();
                     TARUP.TARGN:=_targn;
                     TARUP.add()
                  ?}
               |? Tmp.ACCESS='N'
               || {? TARUP.find_key(_targn)
                  || TARUP.del()
                  ?}
               ?}
            ?};
            _tab_users.next()
         !}
      ?};
      _loop:=Tmp.next()
   !};
   {? _jak=1
   || _jest:=0;
      {? TARUP.first()
      || {!
         |? {? TARUP.TARGN().SD=_sd
            || _jest:=1
            ?};
            ~_jest & TARUP.next()
         !}
      ?};
      {? _jest
      || _wyn:=_jak
      || _wyn:=0
      ?}
   || _wyn:=_jak
   ?}
?};

VAR_DEL.delete('Tmp','TmpEnd');
_wyn


\copy_upr_tar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Kopia uprawnień do stanowisk kasowych.
::       Odczekuje trzech parametrów: FROM  - USERS.ref() - użytkownik, od którego kopiujemy uprawnienia,
::                                    TO    - USERS.ref() - użytkownik, do którego kopiujemy uprawnienia,
::                                    CLEAR - INTEGER     - 1 nadpisuje uprawnienia, 0 jedynie dodaje nowe.
::       UWAGA! Funkcja nie weryfikuje poprawnosci argumentów i przekazywanych parametrów.
::   WE: _a - [S/D] Sprzedaży/Dostaw
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_sd:=_a;

_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;

TARUP.cntx_psh();
TARUP.index('USER');
{? _clear | B_PERM_U.FULL='T'
|| TARUP.prefix(_to,null());
   {? TARUP.first()
   || {!
      |? {? TARUP.TARGN().SD=_sd
         || _nxt:=null();
            TARUP.cntx_psh();
            {? TARUP.next()
            || _nxt:=TARUP.ref()
            ?};
            TARUP.cntx_pop();
            TARUP.del;
            {? _nxt=null()
            || _dalej:=0
            || _dalej:=TARUP.seek(_nxt)
            ?}
         || _dalej:=TARUP.next()
         ?};
         _dalej
      !}
   ?}
?};
TARUP.prefix(_from);
{? TARUP.first()
|| {!
   |? {? TARUP.TARGN().SD=_sd
      ||
         TARUP.cntx_psh();
         TARUP.prefix(_to,null(),TARUP.TARGN);
         {? ~TARUP.first()
         || TARUP.USER:=_to;
            TARUP.add()
         ?};
         TARUP.cntx_pop()
      ?};
      TARUP.next()
   !}
?};
TARUP.cntx_pop();
1


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42]
:: OPIS: Czyście nagłówki uprawnień, które zostały usuniete z formuły \init
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('PermDel');
PermDel:=sql('select NAME, REFERENCE as REF from B_PERM order by 1');
exec('Perm','#object');
Perm.Init:=0;
Perm.FNoInit:="{? PermDel.find_key(_a,) || PermDel.del() ?}";
exec('init','b_perm');
Perm.FNoInit:="";
Perm.Init:=1;
{? PermDel.first()
|| _tab:=sql(
      'select B_PERM_U.B_PERM, REFERENCE as REF from B_PERM_U where B_PERM_U.B_PERM in (select REF from :_a) order by 1,2 '
      'union all '
      'select B_PERM_A.B_PERM, REFERENCE as REF from B_PERM_A where B_PERM_A.B_PERM in (select REF from :_a) ',
      PermDel
   );
   {? _tab.first()
   || B_PERM.cntx_psh(); B_PERM.prefix();
      B_PERM_A.cntx_psh(); B_PERM_A.prefix();
      B_PERM_U.cntx_psh(); B_PERM_U.prefix();
      {!
      |? _tab.prefix(_tab.B_PERM,);
         {? _tab.first() & B_PERM.seek(_tab.B_PERM)
         || {!
            |? _t:=ref_tab(_tab.REF);
               {? _t.seek(_tab.REF) || _t.del() ?};
               &_t;
               _tab.next()
            !};
            {? B_PERM.count()=0
            || B_PERM.del()
            ?}
         ?};
         _tab.prefix();
         _tab.next()
      !};
      B_PERM_U.cntx_pop();
      B_PERM_A.cntx_pop();
      B_PERM.cntx_pop()
   ?}
?};
VAR_DEL.delete('PermDel')


\set_ogrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Ustawienie uprawnienia OGRP do grup dokumentów w obiegu
::   WE: _a - wskazanie na użytkownika lub tabela z użytkownikami
::   WY: uprawnienie: 1 - ma, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
_grp:=var_pres('_a')=type_of(SYSLOG);
_tab:=exec('usr_tab','b_perm',_a);
_old:=Perm.getType();
_cur:={? _old=0 || 3 |? _old=1 || 2 |? _old=2 || 1 ?};
_pyt:=FUN.choice(
   'Do których grup nadać uprawnienia?'@,_cur,
   '&Wszystkich'@,'Wy&branych'@,'Ża&dnej'@
);
{? _grp
|| {? (_pyt=1 | _pyt=3)
   || {? FUN.ask('Zmiana spowoduje usunięcie wybranych dotychczas grup.\nCzy kontynuować?'@)
      || USERS.cntx_psh();
         USERS.prefix();
         {? _tab.first()
         || {!
            |? {? USERS.seek(_tab.REF,)
               || USERSOGR.cntx_psh();
                  USERSOGR.index('UNIK');
                  USERSOGR.prefix(REF.FIRMA,USERS.ref(),);
                  {? USERSOGR.first() || {! |? USERSOGR.del() !} ?};
                  USERSOGR.cntx_pop()
               ?};
               _tab.next()
            !}
         ?};
         USERS.cntx_pop()
      || _pyt:=0
      ?}
   ?}
|| USERSOGR.index('USERSGRP'); USERSOGR.prefix(REF.FIRMA,USERS.ref());
   {? (_pyt=1 | _pyt=3) & USERSOGR.first()
   || {? FUN.ask('Zmiana spowoduje usunięcie wybranych dotychczas grup.\nCzy kontynuować?'@)
      || {? USERSOGR.first() || {! |? USERSOGR.del() !} ?}
      || _pyt:=0
      ?}
   ?}
?};
{? _pyt=1
|| VAR_DEL.delete('Tmp');
   Tmp:=tab_tmp(1,
      'KOD','STRING[8]','Skrót'@,
      'OPIS','STRING[40]','Pełna nazwa'@,
      'ACCESS','STRING[1]','Uprawnienie'@,
      'REF','INTEGER',
   );
   ETYP_GRP.index('N'); ETYP_GRP.prefix(REF.FIRMA);
   {? ETYP_GRP.first()
   || {!
      |? Tmp.KOD:=ETYP_GRP.N;
         Tmp.OPIS:=ETYP_GRP.NAZ;
         Tmp.REF:=#ETYP_GRP.ref();
         Tmp.ACCESS:='T';
         Tmp.add();
         ETYP_GRP.next()
      !}
   ?};
   {? Tmp.first()
   || USERSOGR.cntx_psh();
      USERSOGR.index('UNIK');
      USERSOGR.prefix(REF.FIRMA);
      {!
      |? {? ETYP_GRP.seek(Tmp.REF,)
         || {? _tab.first()
            || USERS.cntx_psh();
               USERS.prefix();
               {!
               |? {? USERS.seek(_tab.REF,)
                  || _jest:=USERSOGR.find_key(USERS.ref(),ETYP_GRP.ref(),null);
                     {? Tmp.ACCESS='T'
                     || {? ~_jest
                        || USERSOGR.blank(1);
                           USERSOGR.FIRMA:=REF.FIRMA;
                           USERSOGR.USERS:=USERS.ref();
                           USERSOGR.OGRP:=ETYP_GRP.ref();
                           USERSOGR.add()
                        ?}
                     |? _jest
                     || USERSOGR.del()
                     ?}
                  ?};
                  _tab.next()
               !};
               USERS.cntx_pop()
            ?}
         ?};
         Tmp.next()
      !};
      USERSOGR.cntx_pop()
   ?};
   2
|? _pyt=2
|| VAR_DEL.delete('Tmp');
   Tmp:=tab_tmp(1,
      'KOD','STRING[8]','Skrót'@,
      'OPIS','STRING[40]','Pełna nazwa'@,
      'ACCESS','STRING[1]','Uprawnienie'@,
      'REF','INTEGER',
   );
   ETYP_GRP.index('N'); ETYP_GRP.prefix(REF.FIRMA);
   {? ETYP_GRP.first()
   || USERSOGR.cntx_psh();
      USERSOGR.index('UNIK');
      USERSOGR.prefix(REF.FIRMA);
      {!
      |? Tmp.KOD:=ETYP_GRP.N;
         Tmp.OPIS:=ETYP_GRP.NAZ;
         Tmp.REF:=#ETYP_GRP.ref();
         Tmp.ACCESS:={? _grp || '' |? USERSOGR.find_key(USERS.ref(),ETYP_GRP.ref(),null) || 'T' || 'N' ?};
         Tmp.add();
         ETYP_GRP.next()
      !};
      USERSOGR.cntx_pop()
   ?};
   TmpEnd:=0;
   _o:=Tmp.mk_sel('Grupy obiegu'@,'P',,'#etypgrpuprusr',,,,,'U');
   Tmp.win_fld(_o,,'ACCESS',,,,,,,,'Czy nadano uprawnienie do grupy obiegu [T/N]?'@,2,,"'T'","'N'","''");
   Tmp.win_fld(_o,,'KOD');
   Tmp.win_fld(_o,,'OPIS');
   _formula:="TmpEnd:=Tmp.ACCESS<>'T'; Tmp.ACCESS:='T'; Tmp.put()";
   Tmp.win_act(_o,,'Formuła','Nadaj'@@,,,_formula,,~_grp,1,,,'N');
   btn_n:=Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Nadaj'@],'menu:N');
   _formula:="TmpEnd:=Tmp.ACCESS<>'N'; Tmp.ACCESS:='N'; Tmp.put()";
   Tmp.win_act(_o,,'Formuła','Odbierz'@@,,,_formula,,,1,,,'O');
   btn_o:=Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Odbierz'@],'menu:O');
   {? _grp
   || _formula:="TmpEnd:=Tmp.ACCESS<>''; Tmp.ACCESS:=''; Tmp.put()";
      Tmp.win_act(_o,,'Formuła','Nie zmieniaj'@@,,,_formula,,,1,,,'M');
      Tmp.win_btn(_o,'text=%1,panel=right,align=begin'['Nie zmieniaj'@],'menu:M')
   ?};
   _formula:="TmpEnd:=2; sel_exit()";
   Tmp.win_act(_o,,'Formuła','Akceptuj'@@,,,_formula,,,,,,'A');
   Tmp.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=end'['Akceptuj'@],'menu:A');
   Tmp.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
   Tmp.win_act(_o,,'Kolejność',);
   Tmp.win_act(_o,,'Okienko',,,,,"
      {? TmpEnd=1
      || FUN.ask('Czy zakończyć nadawania uprawnień?\n(Wprowadzone zmiany będą anulowane).'@)
      || 1
      ?}
   ");
   {? ~_grp
   || Tmp.win_act(_o,,'Rekord',,,,"
         _sel:=Tmp.sel_size();
         {? Tmp.ACCESS='T' & ~_sel
         || Tmp.actions(Tmp.win_sel('?'),,'O',1)
         || Tmp.actions(Tmp.win_sel('?'),,'N',1)
         ?};
         _gray:={? _sel || '' |? Tmp.ACCESS='T' || 'N' || 'O' ?};
         Tmp.actions_grayed(Tmp.win_sel('?'),_gray)
      ")
   ?};
   Tmp.win_sel(_o);
   {? Tmp.select()
   || {? Tmp.first()
      || USERSOGR.cntx_psh();
         USERSOGR.index('UNIK');
         USERSOGR.prefix(REF.FIRMA);
         {!
         |? {? Tmp.ACCESS<>'' & ETYP_GRP.seek(Tmp.REF,)
            || {? _tab.first()
               || USERS.cntx_psh();
                  USERS.prefix();
                  {!
                  |? {? USERS.seek(_tab.REF,)
                     || _jest:=USERSOGR.find_key(USERS.ref(),ETYP_GRP.ref(),null);
                        {? Tmp.ACCESS='T'
                        || {? ~_jest
                           || USERSOGR.FIRMA:=REF.FIRMA;
                              USERSOGR.USERS:=USERS.ref();
                              USERSOGR.OGRP:=ETYP_GRP.ref();
                              USERSOGR.add()
                           ?}
                        |? _jest
                        || USERSOGR.del()
                        ?}
                     ?};
                     _tab.next()
                  !};
                  USERS.cntx_pop()
               ?}
            ?};
            Tmp.next()
         !};
         USERSOGR.cntx_pop()
      ?};
      _old:=
      {? _grp | USERSOGR.first()
      || 1
      ?}
   ?};
   VAR_DEL.delete('btn_n','btn_o');
   _old
|? _pyt=3
|| 0
|| -1
?}


\copy_ogrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Kopia uprawnień do grup dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;
USERSOGR.cntx_psh();
USERSOGR.index('UNIK');
{? _clear
|| USERSOGR.prefix(REF.FIRMA,_to);
   {? USERSOGR.first() || {! |? USERSOGR.del !} ?}
?};
USERSOGR.prefix(REF.FIRMA,_from);
{? USERSOGR.first()
|| {!
   |? USERSOGR.cntx_psh();
      USERSOGR.prefix(REF.FIRMA,_to,USERSOGR.OGRP);
      {? ~USERSOGR.first()
      || USERSOGR.USERS:=_to;
         USERSOGR.add()
      ?};
      USERSOGR.cntx_pop();
      USERSOGR.next()
   !}
?};
USERSOGR.cntx_pop();
1


\rodz_uu2bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Przekształcenie kodu rodzaju uprawnienia USERS_UP -> B_PERM
::   WE: [_a] - rodzaj uprawnienia (ODDZ, MG, STS, STZ, ZAM, ZAW, TR_RODZ, MKAS, KAS, TARD, TARS)
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('')
|| _a:=''
?};
{? _a='MG'
|| _rodz:='LMG'
|? _a='STS'
|| _rodz:='LSP'
|? _a='STZ'
|| _rodz:='LZK'
|? _a='ZAM'
|| _rodz:='ZSMG'
|? _a='ZAW'
|| _rodz:='ZWMG'
|? _a='MKAS'
|| _rodz:='KAS_MUL'
|| _rodz:=_a
?};
_rodz


\usr_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Czy użytkownik ma uprawnienia do podanej danej
::   WE: [_a] - rodzaj (kod) uprawnienia (ODDZ, MG, STS, STZ, ZAM, ZAW, TR_RODZ)
::       [_b] - ref danej
::       [_c] - użytkownik, jeśli brak to bieżący
::   WY: 1 - ma, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | (_a<>'ODDZ' & _a<>'MG' & _a<>'STS' & _a<>'STZ' & _a<>'ZAM' & _a<>'ZAW'
   & _a<>'TR_RODZ')
|| return(0)
?};
{? var_pres('_b')<>type_of(null())
|| return(0)
?};

_wyn:=0;
_rodz:=_a;
_users:={? var_pres('_c')>0 || _c || OPERATOR.USER ?};

{? _rodz='STS'
|| _wyn:=(exec('get','#params',2102,2,_users)<>'T')
|? _rodz='STZ'
|| _wyn:=(exec('get','#params',6102,2,_users)<>'T')
?};

{? ~_wyn
|| _wyn:=Perm.hasFull(exec('rodz_uu2bp','b_perm',_rodz),_users)
?};

{? ~_wyn
|| USERS_UP.cntx_psh();
   USERS_UP.index({? _rodz='MG' | _rodz='ZAM' | _rodz='ZAW' || 'MAG' || _rodz ?});
   USERS_UP.prefix(_users,_rodz,_b);
   {? USERS_UP.first()
   || _wyn:=1
   ?};
   USERS_UP.cntx_pop()
?};

_wyn


\usr_upr_mkasa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Czy użytkownik ma uprawnienia do podanej multikasy
::   WE: [_a] - MKASA.ref()
::       [_b] - użytkownik, jeśli brak to bieżący
::   WY: 1 - ma, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(null())
|| return(0)
?};

_users:={? var_pres('_b')>0 || _b || OPERATOR.USER ?};

_wyn:=Perm.hasFull(exec('rodz_uu2bp','b_perm','MKAS'),_users);
{? ~_wyn
|| MKASAUPR.cntx_psh();
   MKASAUPR.index('UNIK');
   MKASAUPR.prefix(_users,_a);
   {? MKASAUPR.first()
   || _wyn:=1
   ?};
   MKASAUPR.cntx_pop()
?};

_wyn


\usr_upr_stankas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Czy użytkownik ma uprawnienia do podanego stanowiska kasowego
::   WE: [_a] - STANKAS.ref()
::       [_b] - użytkownik, jeśli brak to bieżący
::   WY: 1 - ma, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(null())
|| return(0)
?};

_users:={? var_pres('_b')>0 || _b || OPERATOR.USER ?};

_wyn:=Perm.hasFull(exec('rodz_uu2bp','b_perm','KAS'),_users);
{? ~_wyn
|| KUSERUPR.cntx_psh();
   KUSERUPR.index('KU_ST');
   KUSERUPR.prefix(_users,_a);
   {? KUSERUPR.first()
   || _wyn:=1
   ?};
   KUSERUPR.cntx_pop()
?};

_wyn


\usr_upr_tar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Czy użytkownik ma uprawnienia do podanego cennika
::   WE: [_a] - rodzaj cennika S - sprzedaży, D - dostawcy
::       [_b] - TAR.ref()
::       [_c] - użytkownik, jeśli brak to bieżący
::   WY: 1 - ma, 0 - nie ma
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | (_a<>'S' & _a<>'D')
|| return(0)
?};
{? var_pres('_b')<>type_of(null())
|| return(0)
?};

_rodz:=_a;
_users:={? var_pres('_c')>0 || _c || OPERATOR.USER ?};

TARGN.cntx_psh();
TARGN.prefix();
_rodz_ok:=TARGN.seek(_b) & TARGN.SD=_rodz;
TARGN.cntx_pop();
{? ~_rodz_ok
|| return(0)
?};

_wyn:=Perm.hasFull(exec('rodz_uu2bp','b_perm','TAR'+_rodz),_users);
{? ~_wyn
|| TARUP.cntx_psh();
   TARUP.index('USER');
   TARUP.prefix(_users,null(),_b);
   {? TARUP.first() & TARUP.TARGN().SD=_rodz
   || _wyn:=1
   ?};
   TARUP.cntx_pop()
?};
_wyn


\renumUpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: renumeracja uprawnień
::   WE: [_a] - 0(domyślnie) - drag&drop, 1-akcja do przenumerowania
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of(0) || _a || 0 ?};

_ref:=null();

_tab:=cur_tab();
_tab.cntx_psh();

{? ~_tryb
|| _ref:=dnd_info('dest_record');
   _zaz:=dnd_info('dropped_records')
|| _tab.cntx_psh();
   _sel:=_tab.mk_sel('Przenieś do pozycji','P',,'b_permrenum',,,,,'U');
   _tab.win_fld(_sel,,'KOD');
   _tab.win_fld(_sel,,'OPIS',,,40);
   _tab.win_act(_sel,,'Formuła','&Wybierz'@@,,,"sel_exit",,1,,,,'W');
   _tab.win_sel(_sel);
   {? _tab.select() || _ref:=_tab.ref() ?};
   _tab.cntx_pop();
   _zaz:=tab_tmp(1,'REF','INTEGER','','POS','INTEGER','');
   {? _tab.sel_size()
   || _buf:=_tab.sel_aget();
      _buf.prefix();
      {? _buf.first()
      || _pos:=0;
         {!
         |? _zaz.blank();
            _zaz.REF:=_buf.REF;
            _zaz.POS:=_pos;
            _zaz.add(1);
            _pos+=1;
            _buf.next()
         !}
      ?};
      obj_del(_buf);
      _tab.sel_adel()
   || _zaz.blank();
      _zaz.REF:=#_tab.ref();
      _zaz.POS:=0;
      _zaz.add(1)
   ?}
?};

_nd1:=_zaz.ndx_tmp(,,'REF',,0);
_nd2:=_zaz.ndx_tmp(,,'POS',,0);
{? _ref<>null() & _tab.seek(_ref) & _tab.ACCESS='T'
|| _nr:=_tab.ORDER;
  _ile:=0;
  _pos:=0;
  _zaz.index(_nd2);
  {? _zaz.first()
  || _size:=_zaz.size();
     {!
     |? _tab.clear();
        {? _tab.seek(_zaz.REF,) & _tab.ACCESS='T'
        || {? _tab.ORDER<_nr & ~_pos
           || _ile+=1;
              _pos:=_ile
           ?};
           _ile+=1;
           _zaz.POS:=_ile-_size;
           _zaz.put(1);
           _zaz.next()
        || _zaz.del()
        ?}
     !}
  ?};
  {? _ile
  || {? ~_pos || _ile+=1; _pos:=_ile ?};
     _zaz.blank();
     _zaz.REF:=#_ref;
     _zaz.POS:=_pos-_size;
     _zaz.add(1);
     _first:=0;
     _tab.clear();
     {? _tab.first()
     || _size:=_tab.size;
        {! |? _tab.ORDER-=_size; _tab.put(1); _tab.next() !};
        _nr-=_size;
        _tab.first();
        _lp:=0;
        {!
        |? _ref:=_tab.ref(); _size-=1; _oki:=_tab.next() & _size;
           _tab.cntx_psh();
           {? (_tab.clear(); _tab.seek(_ref))
           || {? _tab.ACCESS='T'
              || {? (_zaz.index(_nd1); _zaz.find_key(#_tab.ref))
                 || _tab.ORDER:=0;
                    _tab.put(1)
                 |? _tab.ORDER<_nr
                 || _lp+=1;
                    _tab.ORDER:=_lp;
                    _tab.put(1)
                 |? _tab.ORDER>_nr
                 || _lp+=1;
                    {? ~_first || _first:=_lp ?};
                    _tab.ORDER:=_lp+_ile;
                    _tab.put(1)
                 ?}
              || _tab.ORDER:=0;
                 _tab.put(1)
              ?}
           ?};
           _tab.cntx_pop();
           _oki
        !};
        {? ~_first || _first:=_lp+1 ?};
        _zaz.index(_nd2);
        {? _zaz.first()
        || _lp:=_first;
           {!
           |? _tab.clear();
              {? _tab.seek(_zaz.REF,)
              || _tab.ORDER:=_lp;
                 _tab.put(1);
                 _lp+=1
              ?};
              _zaz.next()
           !}
        ?}
     ?}
  ?}
?};
_tab.cntx_pop()


\usr_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca tabelę z użytkownikami
::   WE: _a - wskazanie na użytkownika lub tabelę z użytkownikami
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null)
|| _tab:=tab_tmp(1,
      'REF','INTEGER','REF'
   );
   _tab.blank(1);
   _tab.REF:=_a;
   _tab.add();
   _tab
|| _a
?}



\fill_compare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Uzupełnia tabelę na potrzeby porównania użytkowników
::   WE: _a - obiekt do obsługi tabeli
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_usr1:=_obj.USR[1].REF;
_usr2:=_obj.USR[2].REF;

{? B_PERM.NAME='FJKS'
|| ODD.cntx_psh();
   ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || USERSDEP.cntx_psh();
      USERSDEP.index('UNIK');
      USERSDEP.prefix();
      {!
      |? _obj.add(
            B_PERM.DESC,ODD.OD,ODD.N,'U'+B_PERM.uidref(),ODD.uidref(),
            USERSDEP.find_key(REF.FIRMA,_usr1,ODD.ref(),null),
            USERSDEP.find_key(REF.FIRMA,_usr2,ODD.ref(),null)
         );
         ODD.next()
      !};
      USERSDEP.cntx_pop()
   ?};
   ODD.cntx_pop()
|? B_PERM.NAME='HRB'
|| SKID_RBK.index('TRN'); SKID_RBK.prefix(REF.INFO,0,null);
   {? SKID_RBK.first()
   || UPR_RB.cntx_psh();
      UPR_RB.index('RB');
      UPR_RB.prefix();
      exec('RB','object');
      {!
      |? _obj.add(
            B_PERM.DESC,RB.get_rbtx(1,SKID_RBK.ref()),SKID_RBK.BANK().NB,'U'+B_PERM.uidref(),SKID_RBK.uidref(),
            UPR_RB.find_key(_usr1,SKID_RBK.ref(),null),
            UPR_RB.find_key(_usr2,SKID_RBK.ref(),null)
         );
         SKID_RBK.next()
      !};
      UPR_RB.cntx_pop()
   ?}
|? B_PERM.NAME='HRP'
|| TZL.index('KOD'); TZL.prefix();
   {? TZL.first()
   || UPR.cntx_psh();
      UPR.index('REFKOD');
      UPR.prefix();
      {!
      |? _obj.add(
            B_PERM.DESC,TZL.KOD,TZL.OPIS,'U'+B_PERM.uidref(),TZL.uidref(),
            UPR.find_key(_usr1,TZL.KOD,null),
            UPR.find_key(_usr2,TZL.KOD,null)
         );
         TZL.next()
      !};
      UPR.cntx_pop()
   ?}
|? B_PERM.NAME='OGRP'
|| ETYP_GRP.index('N'); ETYP_GRP.prefix(REF.FIRMA);
   {? ETYP_GRP.first()
   || USERSOGR.cntx_psh();
      USERSOGR.index('UNIK');
      USERSOGR.prefix();
      {!
      |? _obj.add(
            B_PERM.DESC,ETYP_GRP.N,ETYP_GRP.NAZ,'U'+B_PERM.uidref(),ETYP_GRP.uidref(),
            USERSOGR.find_key(REF.FIRMA,_usr1,ETYP_GRP.ref(),null),
            USERSOGR.find_key(REF.FIRMA,_usr2,ETYP_GRP.ref(),null)
         );
         ETYP_GRP.next()
      !};
      USERSOGR.cntx_pop()
   ?}
|? B_PERM.NAME='TARS' | B_PERM.NAME='TARD'
|| TARGN.cntx_psh();
   TARGN.index('S');
   TARGN.prefix(B_PERM.NAME+1,);
   {? TARGN.first()
   || TARUP.cntx_psh();
      TARUP.index('TARGN');
      TARUP.prefix();
      {!
      |? _obj.add(
            B_PERM.DESC,TARGN.KOD,TARGN.OP,'U'+B_PERM.uidref(),TARGN.uidref(),
            TARUP.find_key(TARGN.ref(),_usr1),
            TARUP.find_key(TARGN.ref(),_usr2)
         );
         TARGN.next()
      !};
      TARUP.cntx_pop()
   ?};
   TARGN.cntx_pop()
|? B_PERM.NAME='ODDZ' | B_PERM.NAME='LMG' | B_PERM.NAME='LSP' | B_PERM.NAME='LZK'
   | B_PERM.NAME='ZSMG' | B_PERM.NAME='ZWMG' | B_PERM.NAME='TR_RODZ'
|| _real_acr:={? B_PERM.NAME='LMG'     || 'MG'
              |? B_PERM.NAME='LSP'     || 'STS'
              |? B_PERM.NAME='LZK'     || 'STZ'
              |? B_PERM.NAME='ZSMG'    || 'ZAM'
              |? B_PERM.NAME='ZWMG'    || 'ZAW'
              || B_PERM.NAME
              ?};
   _real_name:={? _real_acr='STZ'     || 'STS'
               |? _real_acr='ZAM'     || 'MG'
               |? _real_acr='ZAW'     || 'MG'
               || _real_acr
               ?};
   _real_index:={? _real_acr='ZAM' | _real_acr='ZAW'
                || 'MG'
                || _real_name
                ?};
   ($(_real_name+'.cntx_psh()'))();
   ($(_real_name+'.prefix()'))();
   {? ($(_real_name+'.first()'))()
   || USERS_UP.cntx_psh();
      USERS_UP.index(_real_index);
      USERS_UP.prefix();
      {!
      |? _obj.add(
            B_PERM.DESC,{? _real_name='MG' || MG.SYM || ($(_real_name+'.KOD'))() ?},($(_real_name+'.NAZ'))(),'U'+B_PERM.uidref(),
            ($(_real_name+'.uidref()'))(),
            {? _real_index='MG'
            || USERS_UP.find_key(_usr1,_real_acr,MG.ODDZ,MG.ref())
            || USERS_UP.find_key(_usr1,_real_acr,($(_real_name+'.ref()'))())
            ?},
            {? _real_index='MG'
            || USERS_UP.find_key(_usr2,_real_acr,MG.ODDZ,MG.ref())
            || USERS_UP.find_key(_usr2,_real_acr,($(_real_name+'.ref()'))())
            ?}
         );
         ($(_real_name+'.next()'))()
      !};
      USERS_UP.cntx_pop()
   ?};
   ($(_real_name+'.cntx_pop()'))()
|? B_PERM.NAME='F_ZATR'
|| F_ZATR.cntx_psh();
   F_ZATR.index('UNIQUE');
   F_ZATR.prefix();
   {? F_ZATR.first()
   || USERS_FZ.cntx_psh();
      USERS_FZ.index('UNIQUE');
      USERS_FZ.prefix(REF.FIRMA);
      {!
      |? _obj.add(
            B_PERM.DESC,F_ZATR.KOD,F_ZATR.OPIS,'U'+B_PERM.uidref(),F_ZATR.uidref(),
            {? USERS_FZ.find_key(_usr1,F_ZATR.ref()) || USERS_FZ.DOSTEP || '?' ?},
            {? USERS_FZ.find_key(_usr2,F_ZATR.ref()) || USERS_FZ.DOSTEP || '?' ?}
         );
         F_ZATR.next()
      !};
      USERS_FZ.cntx_pop()
   ?};
   F_ZATR.cntx_pop()
|? B_PERM.NAME='UD_SKL'
|| UDB_GRP.cntx_psh();
   UDB_GRP.index('SYMBOL');
   UDB_GRP.prefix();
   UDB_SYS.cntx_psh();
   UDB_SYS.index('OCHRONA');
   UDB_SYS.prefix();
   UD_TYP.cntx_psh();
   UD_DEF.cntx_psh();
   UD_DEF.index('SYMBOL');
   UD_DEF.prefix();
   UDB_UPR.cntx_psh();
   UDB_UPR.index('UNIQUE');
   UDB_UPR.prefix();
   {? UDB_GRP.first()
   || {!
      |? UDB_SYS.prefix(UDB_GRP.ref,'T',);
         {? UDB_SYS.first()
         || {!
            |? UD_DEF.prefix(exec('domyslny','schemat',UDB_SYS.UD_TYP));
               {? UD_DEF.first()
               || UDB_UPR.prefix(UDB_SYS.ref());
                  {!
                  |? _obj.add(
                        B_PERM.DESC,'%1 > %2 > %3'[UDB_GRP.SYMBOL,UDB_SYS.UD_TYP().SYMBOL,UD_DEF.SYMBOL],
                        UD_DEF.OPIS,'U'+B_PERM.uidref(),UD_DEF.uidref(),
                        {? UDB_UPR.find_key(_usr1,UD_DEF.UD_SKL) || UDB_UPR.DOSTEP || '?' ?},
                        {? UDB_UPR.find_key(_usr2,UD_DEF.UD_SKL) || UDB_UPR.DOSTEP || '?' ?}
                     );
                     UD_DEF.next()
                  !}
               ?};
               UDB_SYS.next()
            !}
         ?};
         UDB_GRP.next()
      !}
   ?};
   UDB_UPR.cntx_pop();
   UD_DEF.cntx_pop();
   UD_TYP.cntx_pop();
   UDB_SYS.cntx_pop();
   UDB_GRP.cntx_pop()
?}


\set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Nadaje lub odbiera uprawnienia do wskazanego typu danych
::   WE:  _a  - wskazanie na uprawnienie do danych (B_PERM)
::        _b  - wskazanie na użytkownika (USERS)
::        _c  - typ operacji: 1-nadanie 0-odebranie
::       [_d] - wskazanie na element danych lub brak gdy chodzi o dostęp do wszystkich
::----------------------------------------------------------------------------------------------------------------------
_b_perm:=_a;
_users:=_b;
_set:=_c;
_ref:={? var_pres('_d')=type_of(null) | var_pres('_d')=type_of('') || _d || ~~ ?};
_access:=-1;
_res:=-1;
_fz:=0;
_real_acr:='';
B_PERM.cntx_psh();
B_PERM.prefix();
{? B_PERM.seek(_b_perm)
|| B_PERM_U.cntx_psh();
   B_PERM_U.index('USER');
   B_PERM_U.prefix(REF.FIRMA,_users,_b_perm);
   {? B_PERM_U.first()
   || {? var_pres('_ref')<=0
      || B_PERM_U.FULL:={? _set || 'T' || 'N' ?};
         {? _set
         || B_PERM_U.ACCESS:='T'
         ?};
         B_PERM_U.put()
      || _tab:=ref_tab(_ref);
         _tab.cntx_psh();
         _tab.prefix();
         {? _tab.seek(_ref)
         || {? B_PERM.NAME='FJKS'
            || {? _tab=ODD
               || USERSDEP.cntx_psh();
                  USERSDEP.index('UNIK');
                  USERSDEP.prefix(REF.FIRMA,_users);
                  _jest:=USERSDEP.find_key(_tab.ref(),null);
                  {? _set & ~_jest
                  || USERSDEP.blank(1);
                     USERSDEP.FIRMA:=REF.FIRMA;
                     USERSDEP.USERS:=_users;
                     USERSDEP.DEPT:=_tab.ref();
                     _res:=USERSDEP.add()
                  |? ~_set & _jest
                  || _res:=USERSDEP.del(,1)
                  ?};
                  _fz:=1;
                  _access:=USERSDEP.first();
                  USERSDEP.cntx_pop()
               ?}
            |? B_PERM.NAME='FREJ'
            || _access:=1;
               _res:=1
            |? B_PERM.NAME='HRB'
            || {? _tab=SKID_RBK
               || UPR_RB.cntx_psh();
                  UPR_RB.index('RB');
                  UPR_RB.prefix(_users);
                  _jest:=UPR_RB.find_key(_tab.ref(),null);
                  {? _set & ~_jest
                  || UPR_RB.blank(1);
                     UPR_RB.USER:=_users;
                     UPR_RB.RB:=_tab.ref();
                     _res:=UPR_RB.add()
                  |? ~_set & _jest
                  || _res:=UPR_RB.del(,1)
                  ?};
                  _fz:=1;
                  _access:=UPR_RB.first();
                  UPR_RB.cntx_pop()
               ?}
            |? B_PERM.NAME='HRP'
            || {? _tab=TZL
               || UPR.cntx_psh();
                  UPR.index('REFKOD');
                  UPR.prefix(_users);
                  _jest:=UPR.find_key(_tab.KOD,null);
                  {? _set & ~_jest
                  || UPR.blank(1);
                     UPR.RR:=_users;
                     UPR.TP:=_tab.ref();
                     _res:=UPR.add()
                  |? ~_set & _jest
                  || _res:=UPR.del(,1)
                  ?};
                  _fz:=1;
                  _access:=UPR.first();
                  UPR.cntx_pop()
               ?}
            |? B_PERM.NAME='KAS_MUL'
            || {? _tab=MKASA
               || MKASAUPR.cntx_psh();
                  MKASAUPR.index('UNIK');
                  MKASAUPR.prefix(_users);
                  _jest:=MKASAUPR.find_key(_tab.ref());
                  {? _set & ~_jest
                  || MKASAUPR.blank(1);
                     MKASAUPR.USER:=_users;
                     MKASAUPR.MKASA:=_tab.ref();
                     MKASAUPR.MKASAN:=_tab.NAZ;
                     _res:=MKASAUPR.add()
                  |? ~_set & _jest
                  || _res:=MKASAUPR.del(,1)
                  ?};
                  _fz:=1;
                  _access:=MKASAUPR.first();
                  MKASAUPR.cntx_pop()
               ?}
            |? B_PERM.NAME='KAS'
            || {? _tab=STANKAS
               || KUSERUPR.cntx_psh();
                  KUSERUPR.index('KU_ST');
                  KUSERUPR.prefix(_users);
                  _jest:=KUSERUPR.find_key(_tab.ref());
                  {? _set & ~_jest
                  || KUSERUPR.blank(1);
                     KUSERUPR.KU:=_users;
                     KUSERUPR.STANKAS:=_tab.ref();
                     KUSERUPR.STANKASN:=KUSERUPR.STANKAS().NAZWA;
                     KUSERUPR.UPR:='T';
                     _res:=KUSERUPR.add()
                  |? ~_set & _jest
                  || _res:=KUSERUPR.del(,1)
                  ?};
                  _fz:=1;
                  _access:=KUSERUPR.first();
                  KUSERUPR.cntx_pop()
               ?}
            |? B_PERM.NAME='OGRP'
            || {? _tab=ETYP_GRP
               || USERSOGR.cntx_psh();
                  USERSOGR.index('UNIK');
                  USERSOGR.prefix(REF.FIRMA,_users);
                  _jest:=USERSOGR.find_key(_tab.ref(),null);
                  {? _set & ~_jest
                  || USERSOGR.blank(1);
                     USERSOGR.FIRMA:=REF.FIRMA;
                     USERSOGR.USERS:=_users;
                     USERSOGR.OGRP:=_tab.ref();
                     _res:=USERSOGR.add()
                  |? ~_set & _jest
                  || _res:=USERSOGR.del(,1)
                  ?};
                  _fz:=1;
                  _access:=USERSOGR.first();
                  USERSOGR.cntx_pop()
               ?}
            |? B_PERM.NAME='TARS' | B_PERM.NAME='TARD'
            || {? _tab=TARGN
               || TARUP.cntx_psh();
                  TARUP.index('USER');
                  TARUP.prefix(_users,null());
                  _jest:=TARUP.find_key(_tab.ref());
                  {? _set & ~_jest
                  || TARUP.blank(1);
                     TARUP.USER:=_users;
                     TARUP.TARGN:=_tab.ref();
                     _res:=TARUP.add()
                  |? ~_set & _jest
                  || _res:=TARUP.del(,1)
                  ?};
                  _access:=TARUP.first();
                  TARUP.cntx_pop()
               ?}
            |? B_PERM.NAME='ODDZ' | B_PERM.NAME='LMG' | B_PERM.NAME='LSP' | B_PERM.NAME='LZK'
               | B_PERM.NAME='ZSMG' | B_PERM.NAME='ZWMG' | B_PERM.NAME='TR_RODZ'
            || _real_acr:={? B_PERM.NAME='LMG'     || 'MG'
                          |? B_PERM.NAME='LSP'     || 'STS'
                          |? B_PERM.NAME='LZK'     || 'STZ'
                          |? B_PERM.NAME='ZSMG'    || 'ZAM'
                          |? B_PERM.NAME='ZWMG'    || 'ZAW'
                          || B_PERM.NAME
                          ?};
               _real_name:={? _real_acr='STZ'     || 'STS'
                           |? _real_acr='ZAM'     || 'MG'
                           |? _real_acr='ZAW'     || 'MG'
                           || _real_acr
                           ?};
               _real_index:={? _real_acr='ZAM' | _real_acr='ZAW'
                            || 'MG'
                            || _real_acr
                            ?};
               {? _tab=($_real_name)()
               || USERS_UP.cntx_psh();
                  USERS_UP.index(_real_index);
                  USERS_UP.prefix(_users,_real_acr);
                  _jest:={? _real_index='MG'
                         || USERS_UP.find_key(_tab.ODDZ,_tab.ref())
                         || USERS_UP.find_key(_tab.ref())
                         ?};
                  {? _set & ~_jest
                  || USERS_UP.blank(1);
                     USERS_UP.AKR:=_real_acr;
                     USERS_UP.USERS:=_users;
                     ($('USERS_UP.'+_real_index))():=_tab.ref();
                     _res:=USERS_UP.add()
                  |? ~_set & _jest
                  || _res:=USERS_UP.del(,1)
                  ?};
                  _access:=USERS_UP.first();
                  USERS_UP.cntx_pop()
               ?}
            |? B_PERM.NAME='F_ZATR'
            || {? _tab=F_ZATR
               || USERS_FZ.cntx_psh();
                  USERS_FZ.index('UNIQUE');
                  USERS_FZ.prefix(REF.FIRMA);
                  _jest:=USERS_FZ.find_key(_users,_tab.ref());
                  {? _set & _jest
                  || USERS_FZ.DOSTEP:='T';
                     _res:=USERS_FZ.put()
                  |? ~_set & _jest
                  || USERS_FZ.DOSTEP:='N';
                     _res:=USERS_FZ.put()
                  ?};
                  USERS_FZ.index('DOSTEP');
                  USERS_FZ.prefix('T',REF.FIRMA,_users);
                  _access:=USERS_FZ.first();
                  USERS_FZ.cntx_pop()
               ?}
            |? B_PERM.NAME='UD_SKL'
            || {? _tab=UD_DEF
               || _udb_sys:=exec('szukaj_udb_sys','schemat','PKD',
                     exec('FindAndGet','#table',UD_SKL,_tab.UD_SKL,,"UD_TYP"));
                  UDB_UPR.cntx_psh();
                  UDB_UPR.index('UNIQUE');
                  UDB_UPR.prefix(_udb_sys);
                  _jest:=UDB_UPR.find_key(_users,_tab.UD_SKL);
                  {? _set & _jest
                  || UDB_UPR.DOSTEP:='T';
                     _res:=UDB_UPR.put()
                  |? ~_set & _jest
                  || UDB_UPR.DOSTEP:='N';
                     _res:=UDB_UPR.put()
                  ?};
                  UDB_UPR.index('FK_USR');
                  UDB_UPR.prefix(_users,'T');
                  _access:=UDB_UPR.first();
                  UDB_UPR.cntx_pop()
               ?}
            ?};
            {? _access<>-1
            || B_PERM_U.ACCESS:={? _access || 'T' || 'N' ?};
               {? B_PERM_U.FULL='T' & (~_access | ~_set | _fz | _real_acr='TR_RODZ' | _real_acr='ZAM' | _real_acr='ZAW')
               || B_PERM_U.FULL:='N'
               ?};
               B_PERM_U.put()
            ?}
         ?};
         _tab.cntx_pop()
      ?}
   || _res:=~_set
   ?};
   B_PERM_U.cntx_pop()
?};
B_PERM.cntx_pop();
_res


\copy_frej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RO [23.25]
:: OPIS: Kopia uprawnień do rejestrów księgowych.
::----------------------------------------------------------------------------------------------------------------------
_par:=params_get();
_from:=_par.FROM;
_to:=_par.TO;
_clear:=_par.CLEAR;
USERSREJ.cntx_psh();
USERSREJ.index('REJ');
{? _clear
|| USERSREJ.prefix(_to);
   {? USERSREJ.first() || {! |? USERSREJ.del !} ?}
?};
USERSREJ.prefix(_from);
{? USERSREJ.first()
|| _rej:=USERSREJ.REJ;
   _odd:=USERSREJ.REJ().ODD().OD;
   {!
   |? USERSREJ.cntx_psh();
      USERSREJ.prefix(_to,_rej);
      {? ~USERSREJ.first()
      || USERSREJ.USER:=_to;
         USERSREJ.REJ:=_rej;
         USERSREJ.ODD:=_odd;
         USERSREJ.add()
      ?};
      USERSREJ.cntx_pop();
      USERSREJ.next()
   !}
?};
USERSREJ.cntx_pop();
1

:Sign Version 2.0 jowisz:1045 2023/12/22 12:28:59 8cf18bc60a9aa7d5f3058466061d37b9fc5a4228ded5ebe9d0fafe1285ab67568715d4e6985cffd7d410b00f9de2b84d2e68f7889cecf35a41e5138c4f739a7a360cd7c97f923ef5d9253ac2d9e18c7e3deb2e4348124dc89bf5ed08b94be14e483233220b1cbe6841a0e6d4d126a78ed307106152d3e6e60405f4f6627e17da

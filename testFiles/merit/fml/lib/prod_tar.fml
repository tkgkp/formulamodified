:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: prod_tar.fml
:: Utworzony: 26.07.2018
:: Autor: TS
::======================================================================================================================
:: Zawartość: Taryfikatory kosztów produkcji - wspólna obługa tabel: TTOPER, TTOUT, TKAT, TKATHIS
::======================================================================================================================


\chk_tkat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Walidacja rekordu tabeli TKAT (kategorie zaszeregowania) - wołana z poziomu okna oraz funkcji importujących
::   WE: _a - specyfikacja testu: 1-poprawianie, 0-dołączanie
::   WY: akronim pola / ''
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.record(TKAT,,'KAT','OPIS','PLNH');
{? _chk=''
|| {? TKAT.PLNH<0
   || FUN.info('Stawka nie może być ujemna.'@);
      'PLNH'
   |? __CHK.index(TKAT,_a)=''
   || ''
   || 'KAT'
   ?}
|| _chk
?}


\chk_ttout
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Walidacja rekordu tabeli TTOUT (taryfikator usług) - wołana z poziomu okna oraz funkcji importujących
::   WE: _a - specyfikacja testu: 1-poprawianie, 0-dołączanie
::   WY: akronim pola / ''
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.record(TTOUT,,'KOD','NA','CENA');
{? _chk=''
|| {? TTOUT.CENA<0
   || FUN.info('W polu "%1" należy podać wartość większą od zera.'@['Cena'@]);
      _chk:='CENA'
   |? __CHK.index(TTOUT,_a)=''
   || ''
   || 'KOD'
   ?}
|| _chk
?}


\chk_ttoper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Walidacja rekordu tabeli TTOPER (taryfikator operacji) - wołana z poziomu okna oraz funkcji importujących
::   WE: _a - specyfikacja testu: 1-poprawianie, 0-dołączanie
::   WY: akronim pola / ''
::----------------------------------------------------------------------------------------------------------------------
{? TTOPER.KAT='N'
|| _chk:=__CHK.record(TTOPER,,'KOD','NA','STAWKA')
|| _chk:=__CHK.record(TTOPER,,'KOD','NA','TKAT')
?};
{? _chk=''
|| {? TTOPER.KAT='N' & TTOPER.STAWKA<0
   || FUN.info('W polu "%1" należy podać wartość większą od zera.'@['Stawka godzinowa'@]);
      _chk:='STAWKA'
   |? __CHK.index(TTOPER,_a)<>''
   || _chk:='KOD'
   ?}
?};
{? _chk='' & TTOPER.PL_GRP='T'
|| {? TTOPER.KONTROLA='T'
   || _chk:='PL_GRP';
      FUN.emsg('Nie można ustawić operacji kontroli jakości jako grupowej.'@)
   |? TTOPER.KJ_BAD<>'N'
   || _chk:='PL_GRP';
      FUN.emsg('Dla operacji grupowej nie można używać kontroli jakości przez moduł badań.'@)
   |? TTOPER.BAD_OP<>null()
   || _chk:='PL_GRP';
      FUN.emsg('Dla operacji grupowej nie można używać badań przeprowadzanych w trakcie wykonywania operacji.'@)
   ?}
?};
{? _chk='' & TTOPER.WYK_FAST='T'
|| {? TTOPER.KJ_BAD<>'N'
   || _chk:='WYK_FAST';
      FUN.emsg('Operacja ze sposobem potwierdzania nie może być rejestrowana w szybki sposób.'@)
   |? TTOPER.BRYG='T'
   || _chk:='WYK_FAST';
      FUN.emsg('Operacja wykonywana przez brygadę nie może być rejestrowana w szybki sposób.'@)
   |? TTOPER.PL_GRP='T'
   || _chk:='WYK_FAST';
      FUN.emsg('Operacja planowana grupowo nie może być rejestrowana w szybki sposób.'@)
   ?}
?};
_chk

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 cae984dcb5583e4e53dc3638fcd8efaf007993f2268db6a37d216d5bb49575b9c2ff673abcf1438c8c8204888490c9b41ee3c85b7ef1aecf8707bbc4f6c3c834043aa3cfb80978489218d48e3cdee11baebb75930fa1729042f9e6baff8cadc852d443b5f935d018b9c817cd28a7155e70abb3f8160e9dfb2708a1205e7c3aa0

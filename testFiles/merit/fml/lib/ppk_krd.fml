:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_krd.fml [12.51]
:: Utworzony: 2019/04/23
:: Autor: jaws
:: Systemy: PPK
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę słownika rodzajów komunikatów.
::======================================================================================================================


\xxx_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed edycją pól tabeli PPK_KRD
::   WE:
::   WY: 0/1 - pole zablokowane/dostępne do edycji
::----------------------------------------------------------------------------------------------------------------------
PPK_KRD.SYSTEM<>'T'


\usu_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed usunięciem wiersza tabeli PPK_KRD.
::   WE:
::   WY: 0/1 - akcja zablokowana/dostępna
::----------------------------------------------------------------------------------------------------------------------
{? PPK_KRD.SYSTEM='T'
|| FUN.emsg('Zapis systemowy nie może być usunięty.'@);
   0
|| 1
?}


\rek_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Po edycji wiersza tabeli PPK_KRD.
::   WE:
::   WY: 1 w przypadku gdy rekord jest poprawny, akronim wymaganego pola lub 0 gdy doszło do powielenia klucza.
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.table(PPK_KRD,-menu_txt()='popraw',,'SYMBOL','OPIS');
{? (type_of(_chk)=type_of('') & _chk<>'') |
   (type_of(_chk)=type_of(0) & _chk=0)
|| return(_chk)
?};
{? PPK_KRD.FORMULA='T'
|| _fml:=PPK_KRD.memo_txt(,0,'TRESC');
   {? _fml=''
   || __CHK.err_fld(PPK_KRD,'TRESC');
      return('TRESC')
   ?};
   _ERR:=form_chk($_fml);
   {? _ERR.first()
   || _wnd:=_ERR.mk_sel('Błędy w treści formuły'@,'P',,'#ppk_krd_err');
      _ERR.win_fld(_wnd,,'ERR_DESC');
      _ERR.win_sel(_wnd);
      _ERR.select();
      return('TRESC')
   ?}
?};
1


\wnd_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: "Okienko po" wszystkich okienek tabeli PPK_KRD.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
1


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Tworzy i wyświetla panel zawierający rodzaje komunikatów.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized';
:: okno panelu obsługi komunikatów
_wnd:=PPK_KRD.grp_make('Rodzaje komunikatów'@,
:: przed otwarciem
   "  PPK_KRD.cntx_psh();
      PPK_KRD.index('SYMBOL');
      1
   ",
:: identyfikator
   '#ppk_krd_grp',,,
:: podczas zamykania
   "  PPK_KRD.fld_fml('RODZAJ','BLANK',$'*');
      PPK_KRD.cntx_pop();
      1
   "
);
PPK_KRD.grp_sel(_wnd,,'WER','Uczestnicy'@,,,,,
:: przed obsługą
   "  PPK_KRD.fld_fml('RODZAJ','BLANK',$('\\'U\\''));
      PPK_KRD.prefix('U')
   ",,,,
:: tryb
   _mode
);
PPK_KRD.grp_sel(_wnd,,'WER','Osoby'@,,,,,
:: po obsłudze
   "  PPK_KRD.fld_fml('RODZAJ','BLANK',$('\\'O\\''));
      PPK_KRD.prefix('O')
   ",,,,
:: tryb
   _mode
);

_org:=PPK_KRD.win_sel('?');
PPK_KRD.win_sel(_wnd);
PPK_KRD.select();
PPK_KRD.win_sel(_org);

:: porządki
PPK_KRD.win_del(_wnd);
~~


\szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyszukuje rodzaj komunikatu o danym kodzie
::   WE: _a STRING - rodzaj: U-uczestnik, O-osoba
::       _b STRING - symbol
::   WY: wskazanie wiersza w tabeli PPK_KRD lub null
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_rodzaj:=_a;
_symbol:=_b;

_ref:=null;
PPK_KRD.cntx_psh();
PPK_KRD.index('SYMBOL');
PPK_KRD.prefix(_rodzaj);
{? PPK_KRD.find_tab(0,'SYMBOL',,'=',_symbol)
|| _ref:=PPK_KRD.ref()
?};
PPK_KRD.cntx_pop();
_ref


\dane_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Uzupełnia listę rodzajów komunikatów.
::   WE:
::   WY: ilość dodanych komunikatów
::----------------------------------------------------------------------------------------------------------------------
_add:=0;
_add+=exec('dodaj','ppk_krd','U','AWW','Automatyczne wznowienie wpłat do PPK',,,'T',
   'exec(\'wzn_wpl\',\'ppk_krd\')'
)<>null();
_add+=exec('dodaj','ppk_krd','O','AWW','Automatyczne wznowienie wpłat do PPK',,,'T',
   'exec(\'wzn_wpl\',\'ppk_krd\')'
)<>null();
_add+=exec('dodaj','ppk_krd','U','IAWW','Informacja o wznowieniu wpłat do PPK',,,'T',
   'exec(\'info_wzn\',\'ppk_krd\')'
)<>null();
_add+=exec('dodaj','ppk_krd','O','IAWW','Informacja o wznowieniu wpłat do PPK',,,'T',
   'exec(\'info_wzn\',\'ppk_krd\')'
)<>null();
_add+=exec('dodaj','ppk_krd','O','DUOS','Powiadomienie o możliwości przystąpienia do PPK',,,'T',
   'exec(\'dekl_ucz\',\'ppk_krd\')'
)<>null();
_add+=exec('dodaj','ppk_krd','U','IUCZ','Informacja dla nowego uczestnika PPK',,,'T',
   'exec(\'info_ucz\',\'ppk_krd\')'
)<>null();
_add+=exec('dodaj','ppk_krd','U','IDWT','Informacja o deklaracji wypłaty transferowej',,,'T',
   'exec(\'idwt\',\'ppk_krd\')'
)<>null();
_add+=exec('dodaj','ppk_krd','U','WYPTRANS','Informacja o wykonaniu wypłaty transferowej','T','T','T',
   'exec(\'wyptrans\',\'ppk_krd\')'
)<>null();
_add+=exec('dodaj','ppk_krd','U','ZWROT','Informacja o wycofaniu środków','T','T','T',
   'exec(\'zwrot\',\'ppk_krd\')'
)<>null();
_add+=exec('dodaj','ppk_krd','U','IDWP','Wypłata transferowa - zmiana zarządzającego',,,'T',
   'exec(\'idwp\',\'ppk_krd\')'
)<>null();

_add


\dodaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Tworzy zapis w tabeli PPK_KRD.
::   WE:  _a  [STRING] - rodzaj: U-uczestnik, O-osoba
::        _b  [STRING] - symbol
::        _c  [STRING] - opis
::       [_d] [STRING] - dostępny przez portal [domyślnie: 'N']
::       [_e] [STRING] - treść dostępna do edycji [domyślnie: 'N']
::       [_f] [STRING] - treść określona formułą [domyślnie: 'N']
::       [_g] [STRING] - treść lub formuła komunikatu [domyślnie: '']
::   WY: wskazanie utworzonego/istniejącego zapisu lub null w przypadku błędu
::UWAGA: poprawność argumentów wywołania nie jest weryfikowana
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_rodzaj:=_a;
_symbol:=_b;
_opis:=_c;
_www:={? var_pres('_d')=type_of('') & _d<>'' || _d || 'N' ?};
_edi:={? var_pres('_e')=type_of('') & _e<>'' || _e || 'N' ?};
_fml:={? var_pres('_f')=type_of('') & _f<>'' || _f || 'N' ?};
_txt:={? var_pres('_g')=type_of('') & _g<>'' || _g || '' ?};

_ref:=null;
PPK_KRD.cntx_psh();
PPK_KRD.index('SYMBOL');
PPK_KRD.prefix(_rodzaj);
{? ~PPK_KRD.find_tab(0,'SYMBOL',,'=',_symbol)
|| PPK_KRD.blank(1);
   PPK_KRD.RODZAJ:=_rodzaj;
   PPK_KRD.SYMBOL:=_symbol;
   PPK_KRD.OPIS:=_opis;
   PPK_KRD.PORTAL:=_www;
   PPK_KRD.EDYCJA:=_edi;
   PPK_KRD.FORMULA:=_fml;
   PPK_KRD.SYSTEM:='T';
   {? PPK_KRD.add()
   || {? _txt<>''
      || PPK_KRD.memo_set(_txt,'TRESC');
         PPK_KRD.memo_put(,'TRESC')
      ?};
      _ref:=PPK_KRD.ref()
   ?}
|| _ref:=PPK_KRD.ref()
?};
PPK_KRD.cntx_pop();
_ref


\wybierz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Umożliwia wybór definicji komunikatu.
::   WE: _a STRING - rodzaj komunikatu: U/O
::       _b [STRING] - symbol komunikatu
::   WY: wskazanie na tablicę nazwaną zawierającą wskazanie i wartości wybranych pól definicji komunikatu
::----------------------------------------------------------------------------------------------------------------------
_val:=obj_new('ref','RODZAJ','SYMBOL','OPIS','PORTAL','EDYCJA','FORMULA','TRESC','SYSTEM');
{! _ii:=1..obj_len(_val)
|! _val[_ii]:=''
!};
_val.ref:=null;

{? var_pres('_a')<>type_of('') | +_a<>1 | 'UO'*_a=0
:: błedne wywołanie
|| return(_val)
?};

PPK_KRD.cntx_psh();
PPK_KRD.win_sel('WYB');
PPK_KRD.index('SYMBOL');
PPK_KRD.prefix(_a);
{? var_pres('_b')=type_of('')
|| PPK_KRD.find_key(_b)
|| PPK_KRD.first()
?};

{? PPK_KRD.select(,1)
|| _val.ref:=PPK_KRD.ref();
   _val.RODZAJ:=PPK_KRD.RODZAJ;
   _val.SYMBOL:=PPK_KRD.SYMBOL;
   _val.OPIS:=PPK_KRD.OPIS;
   _val.PORTAL:=PPK_KRD.PORTAL;
   _val.EDYCJA:=PPK_KRD.EDYCJA;
   _val.FORMULA:=PPK_KRD.FORMULA;
   _val.TRESC:=PPK_KRD.memo_txt(,1,'TRESC');
   _val.SYSTEM:=PPK_KRD.SYSTEM
?};

PPK_KRD.cntx_pop();

_val


\info_wzn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [12.51]
:: OPIS: Treść informacji dla osób, którym należy wznowić przekazywanie wpłat do PPK.
::  AKT: IS [12.51]
:: OPIS: Aktualizacja treści na podstawie [wzór] - Obowiązki informacyjne pracodawcy wobec pracownika (27.12.2022)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',PPK_KOM.DATA,KST_PPK);
PPK_UCZ.cntx_psh();
PPK_UCZ.prefix();
OSOBA.cntx_psh();
OSOBA.prefix();
{? PPK_KOM.PPK_UCZ<>null()
|| PPK_KOM.PPK_UCZ().OSOBA()
|| PPK_KOM.OSOBA()
?};
{? -OSOBA.PLEC='k'
|| _zwrot1:='Panią';
   _zwrot2:=_zwrot3:='Pani'
|| _zwrot1:=_zwrot2:='Pana';
   _zwrot3:='Pan'
?};
OSOBA.cntx_pop();
PPK_UCZ.cntx_pop();
_dataWzn:=date(PPK_KOM.DATA~1,KST_PPK.WZN_MS,KST_PPK.WZN_DN)$6;
'W związku ze złożeniem przez '+_zwrot1+' deklaracji o rezygnacji z dokonywania wpłat do PPK informuję, że '+
'od '+_dataWzn+' roku będą dokonywane wpłaty do PPK na '+_zwrot2+' rachunek PPK, '+
'chyba że ponownie złoży '+_zwrot3+' deklarację o rezygnacji z ich dokonywania. Jeżeli przed '+_dataWzn+
' roku ukończy '+_zwrot3+' 55. rok życia, wpłaty do PPK będą dokonywane na '+_zwrot2+' rachunek PPK od '+_dataWzn+
' wyłącznie na '+_zwrot2+' wniosek złożony do ostatniego dnia lutego.*\n\n'+
exec('data_podpis','ppk_krd')+
'*Co 4 lata (w 2023 r., w 2027 r. itd.), od 1 marca, przestają obowiązywać deklaracje o rezygnacji z dokonywania '+
'wpłat do PPK złożone zarówno przez uczestników PPK, jak i przez osoby zatrudnione, w imieniu których - na skutek '+
'złożenia tej deklaracji – nie została zawarta umowa o prowadzenie PPK. Jeżeli uczestnicy PPK nie ponowią deklaracji '+
'o rezygnacji z dokonywania wpłat do PPK, od wypłacanego im wynagrodzenia będą obliczanie i pobierane wpłaty do PPK. '+
'Wpłaty do PPK obliczone i pobrane z wynagrodzenia wypłaconego w marcu zostaną dokonane od 1 kwietnia. Dotyczy to '+
'także osób zatrudnionych, które do końca lutego nie stały się uczestnikami PPK. W ich przypadku, jeżeli w marcu nie '+
'ponowią deklaracji o rezygnacji z dokonywania wpłat do PPK, obliczanie, pobieranie i dokonywanie wpłat do PPK będzie '+
'poprzedzone zawarciem w ich imieniu umowy o prowadzenie PPK (art. 16 ust. 1 zdanie drugie, art. 23 ust. 6 ustawy o '+
'PPK).\n'+
'Podmiot zatrudniający nie dokona wpłat za uczestnika PPK, jeżeli uczestnik PPK po złożeniu deklaracji o rezygnacji z '+
'dokonywania wpłat do PPK, a przed 1 kwietnia ukończył 70. rok życia (art. 23 ust. 8 ustawy o PPK). Podmiot '+
'zatrudniający będzie dokonywać wpłat za uczestnika PPK, który po złożeniu deklaracji o rezygnacji z dokonywania '+
'wpłat do PPK, a przed 1 kwietnia, ukończył 55. rok życia, wyłącznie na wniosek tego uczestnika PPK złożony do '+
'ostatniego dnia lutego (art. 23 ust. 9 ustawy o PPK). W przypadku złożenia wniosku o dokonywanie wpłat do PPK w '+
'marcu lub później, wniosek ten zostanie uwzględniony na zasadach ogólnych.'


\wzn_wpl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [12.51]
:: OPIS: Treść informacji dla osób, którym wznowiono przekazywanie wpłat do PPK.
::  AKT: IS [12.51]
:: OPIS: Aktualizacja treści na podstawie [wzór] - Obowiązki informacyjne pracodawcy wobec pracownika (27.12.2022)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',PPK_KOM.DATA,KST_PPK);
PPK_UCZ.cntx_psh();
PPK_UCZ.prefix();
OSOBA.cntx_psh();
OSOBA.prefix();
{? PPK_KOM.PPK_UCZ<>null()
|| PPK_KOM.PPK_UCZ().OSOBA()
|| PPK_KOM.OSOBA()
?};
{? -OSOBA.PLEC='k'
|| _zwrot1:='Panią';
   _zwrot2:=_zwrot3:='Pani'
|| _zwrot1:=_zwrot2:='Pana';
   _zwrot3:='Pan'
?};
OSOBA.cntx_pop();
PPK_UCZ.cntx_pop();
_dataWzn:=date(PPK_KOM.DATA~1,KST_PPK.WZN_MS,KST_PPK.WZN_DN)$6;
'W związku ze złożeniem przez '+_zwrot1+' deklaracji o rezygnacji z dokonywania wpłat do PPK informuję, że '+
'od '+_dataWzn+' roku będą dokonywane wpłaty do PPK na '+_zwrot2+' rachunek PPK, '+
'chyba że ponownie złoży '+_zwrot3+' deklarację o rezygnacji z ich dokonywania. Jeżeli przed '+_dataWzn+
' roku ukończy '+_zwrot3+' 55. rok życia, wpłaty do PPK będą dokonywane na '+_zwrot2+' rachunek PPK od '+_dataWzn+
' wyłącznie na '+_zwrot2+' wniosek złożony do ostatniego dnia lutego.*\n\n'+
exec('data_podpis','ppk_krd')+
'*Co 4 lata (w 2023 r., w 2027 r. itd.), od 1 marca, przestają obowiązywać deklaracje o rezygnacji z dokonywania '+
'wpłat do PPK złożone zarówno przez uczestników PPK, jak i przez osoby zatrudnione, w imieniu których - na skutek '+
'złożenia tej deklaracji – nie została zawarta umowa o prowadzenie PPK. Jeżeli uczestnicy PPK nie ponowią deklaracji '+
'o rezygnacji z dokonywania wpłat do PPK, od wypłacanego im wynagrodzenia będą obliczanie i pobierane wpłaty do PPK. '+
'Wpłaty do PPK obliczone i pobrane z wynagrodzenia wypłaconego w marcu zostaną dokonane od 1 kwietnia. Dotyczy to '+
'także osób zatrudnionych, które do końca lutego nie stały się uczestnikami PPK. W ich przypadku, jeżeli w marcu nie '+
'ponowią deklaracji o rezygnacji z dokonywania wpłat do PPK, obliczanie, pobieranie i dokonywanie wpłat do PPK będzie '+
'poprzedzone zawarciem w ich imieniu umowy o prowadzenie PPK (art. 16 ust. 1 zdanie drugie, art. 23 ust. 6 ustawy o '+
'PPK).\n'+
'Podmiot zatrudniający nie dokona wpłat za uczestnika PPK, jeżeli uczestnik PPK po złożeniu deklaracji o rezygnacji z '+
'dokonywania wpłat do PPK, a przed 1 kwietnia ukończył 70. rok życia (art. 23 ust. 8 ustawy o PPK). Podmiot '+
'zatrudniający będzie dokonywać wpłat za uczestnika PPK, który po złożeniu deklaracji o rezygnacji z dokonywania '+
'wpłat do PPK, a przed 1 kwietnia, ukończył 55. rok życia, wyłącznie na wniosek tego uczestnika PPK złożony do '+
'ostatniego dnia lutego (art. 23 ust. 9 ustawy o PPK). W przypadku złożenia wniosku o dokonywanie wpłat do PPK w '+
'marcu lub później, wniosek ten zostanie uwzględniony na zasadach ogólnych.'


\dekl_ucz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [12.51]
:: OPIS: Treść informacji o możliwości przystąpienia do PPK dla osób w wieku pomiędzy 55 a 70 lat.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='Pan'+{? PPK_KOM.OSOBA().PLEC='M' || 'a' || 'ią' ?};

_txt:='Informuję '+_zwrot+' o możliwości złożenia wniosku o przystąpienie do PPK, Art. 15. ust. 2. Ustawy z dnia ';
_txt+='4 października 2018r. o pracowniczych planach  kapitałowych.';
_txt


\info_ucz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [12.51]
:: OPIS: Treść informacji o wypłacie transferowej oraz możliwości zadeklarowania wpłaty dodatkowej i zmiany podstawowej.
::  AKT: IS [12.51]
:: OPIS: Aktualizacja treści na podstawie [wzór] - Obowiązki informacyjne pracodawcy wobec pracownika (27.12.2022)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',PPK_KOM.DATA,KST_PPK);
PPK_UCZ.cntx_psh();
PPK_UCZ.prefix();
OSOBA.cntx_psh();
OSOBA.prefix();
PPK_UMO.cntx_psh();
PPK_UMO.prefix();
PPK_UZA.cntx_psh();
PPK_UZA.prefix();
ADRES.cntx_psh();
ADRES.prefix();
PPK_KOM.PPK_UCZ().OSOBA();
_osoba:=OSOBA.ref();
_kobieta:=(OSOBA.PLEC='K');
_if:=PPK_UCZ.PPK_UMO().PPK_UZA().ADRES().NAZWA;
_uczOd:=PPK_UCZ.OD$1;
ADRES.cntx_pop();
PPK_UZA.cntx_pop();
PPK_UMO.cntx_pop();
OSOBA.cntx_pop();
PPK_UCZ.cntx_pop();

{? _kobieta
|| _zwrot1:='Panią';
   _zwrot2:='Pani'
|| _zwrot1:='Pana';
   _zwrot2:='Pan'
?};

'Informuję '+_zwrot1+' o tym, że wypełniając obowiązki wynikające z ustawy*,  z dniem '+_uczOd+' został'+
{? _kobieta || 'a' || '' ?}+' '+_zwrot2+' zgłoszon'+{? _kobieta || 'a' || 'y' ?}+' do '+_if+
' jako uczestnik PPK.\n\n'+
'Pracownicze Plany Kapitałowe to dobrowolny program długoterminowego oszczędzania, tworzony '+
'i współfinansowany przez pracowników, pracodawców i państwo. Prywatne i imienne rachunki PPK będą zasilane wpłatami '+
'pracownika i podmiotu zatrudniającego oraz wpłatą powitalną i dopłatami rocznymi od państwa. Wpłaty pracownika oraz '+
'podmiotu zatrudniającego będą naliczane procentowo od wysokości wynagrodzenia pracownika. Pracownik może w każdej '+
'chwili zarówno zrezygnować z oszczędzania w tym programie, jak i do niego wrócić.\n\n'+
'Ponadto informuję, że:\n'+
' - osoba zatrudniona, która jest uczestnikiem PPK, powinna - w terminie 7 dni od dnia zawarcia w jej imieniu i na '+
'jej rzecz umowy o prowadzenie PPK - złożyć podmiotowi zatrudniającemu oświadczenie o zawartych w jej imieniu umowach '+
'o prowadzenie PPK. Oświadczenie powinno zawierać oznaczenie instytucji finansowych, z którymi zawarto te umowy;\n'+
' - osoba zatrudniona, która ukończyła 55 lat i nie ukończyła jeszcze 70 lat, aby zostać uczestnikiem PPK, powinna '+
'złożyć podmiotowi zatrudniającemu wniosek o zawarcie - w jej imieniu i na jej rzecz - umowy o prowadzenie PPK;\n'+
' - uczestnik PPK, poza obowiązkową wpłatą podstawową, może zadeklarować wpłatę dodatkową do PPK w wysokości do 2% '+
'jego wynagrodzenia;\n'+
' - uczestnik PPK, którego wynagrodzenie osiągane z różnych źródeł w danym miesiącu nie przekracza kwoty '+
'odpowiadającej 1,2-krotności minimalnego wynagrodzenia, może złożyć podmiotowi zatrudniającemu deklarację o '+
'obniżeniu wpłaty podstawowej do PPK. Obniżona wpłata podstawowa może wynosić mniej niż 2%, ale nie mniej niż 0,5% '+
'jego wynagrodzenia.\n\n'+
exec('data_podpis','ppk_krd')+
'*Ustawa z 4 października 2018r. o pracowniczych planach kapitałowych.'


\wyptrans
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Treść informacji o wypłacie transferowej.
::   WE: _a [DATE]   - Data wykonania wypłaty transferowej.
::       _b [STRING] - Nazwa instytucji finansowej, w której uczestnik posiadał rachunek PPK u poprzedniego pracodawcy.
::       _c [STRING] - Numer wersji otrzymanego pliku
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dt:={? var_pres('_a')=type_of(date()) || _a || PPK_KOM.DATA ?};
_pzif:={? var_pres('_b')=type_of('') || _b || '' ?};
_wer:={? var_pres('_c')=type_of('') || _c || '1.00' ?};

ADRES.cntx_psh();
ADRES.index('ADRES');
ADRES.prefix(exec('slo_typ','ext_slo','PPK_IF'));
_pzif:=
   {? ADRES.find_tab(,'NIP',,'=',gsub(_pzif,spli_str(' ,-',','),'')) |
      ADRES.find_tab(,'NAZWA',,'=',_pzif)
::    znaleziono według NIP lub nazwy
   || ADRES.NAZWA
   || 'instytucję finansową'
   ?};
ADRES.cntx_pop();

'Informuję o wpłynięciu do podmiotu zatrudniającego potwierdzenia dokonanej %1 roku '
'przez %2 wypłaty transferowej dotyczącej środków PPK.'
[_dt$6,_pzif]


\zwrot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Treść informacji o zwrocie (Art. 12.6 / Art. 19.6).
::   WE: _a [DATE]   - Data wycofania środków.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_dt:={? var_pres('_a')=type_of(date()) || _a || PPK_KOM.DATA ?};

'Informuję o wpłynięciu do podmiotu zatrudniającego potwierdzenia dokonanego '+_dt$6+' roku zwrotu (wycofania) '+
'środków zgromadzonych w PPK.'


\idwt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.51]
:: OPIS: Treść informacji o obowiązku złożenia w imieniu uczestnika wniosku o wypłatę transferową.
::       UWAGA!!! - kontekst tabeli PPK_KOM musi być ustalony
::  AKT: IS [12.51]
:: OPIS: Aktualizacja treści na podstawie [wzór] - Obowiązki informacyjne pracodawcy wobec pracownika (27.12.2022)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PPK_UCZ.cntx_psh();
PPK_UCZ.prefix();
OSOBA.cntx_psh();
OSOBA.prefix();
{? PPK_KOM.PPK_UCZ().OSOBA().PLEC='M'
|| _zwrot:=_zwrot2:='Pana'
|| _zwrot:='Panią';
   _zwrot2:='Pani'
?};
OSOBA.cntx_pop();
PPK_UCZ.cntx_pop();

:: Pobranie nazwy instytucji finansowej:
PPK_UMO.cntx_psh();
PPK_UMO.prefix();
PPK_UZA.cntx_psh();
PPK_UZA.prefix();
ADRES.cntx_psh();
ADRES.prefix();
_if:=PPK_KOM.PPK_UMO().PPK_UZA().ADRES().NAZWA;
ADRES.cntx_pop();
PPK_UZA.cntx_pop();
PPK_UMO.cntx_pop();

'Informuję*, że podmiot zatrudniający ma obowiązek złożenia - w '+_zwrot2+' imieniu - wniosku o wypłatę transferową '+
'środków zgromadzonych na wskazanych - w złożonym przez '+_zwrot+' oświadczeniu - rachunkach PPK, na rachunek '+
'PPK prowadzony przez '+_if+' **\n\n'+
exec('data_podpis','ppk_krd')+
'*Przekazanie tej informacji uczestnikowi PPK jest obowiązkiem podmiotu zatrudniającego na podstawie art. 19 ust. 2 '+
'ustawy z dnia 4 października 2018 r. o pracowniczych planach kapitałowych.\n'+
'**W terminie 7 dni od dnia otrzymania tej informacji od podmiotu zatrudniającego uczestnik PPK może poinformować, '+
'w formie pisemnej, podmiot zatrudniający o braku zgody na złożenie przez ten podmiot wniosku o wypłatę transferową.'


\ppk_krd_formula_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.22]
:: OPIS: Przed wyświetleniem pola FORMULA w tabeli PPK_KRD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PPK_KRD.efld_opt('RED','mark=%1'[$(PPK_KRD.FORMULA='T')],,'TRESC')


\ppk_krd_formula_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [19.22]
:: OPIS: Po edycji pola FORMULA w tabeli PPK_KRD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PPK_KRD.efld_opt('RED','mark=%1'[$(PPK_KRD.FORMULA='T')],,'TRESC')


\idwp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Treść informacji o złożeniu w imieniu uczestnika wniosku o wypłatę transferową w związku ze zmianą
::       instytucji zarządzającej na podstawie [wzór] - Obowiązki informacyjne pracodawcy wobec pracownika (27.12.2022)
::       UWAGA!!! - kontekst tabeli PPK_KOM musi być ustalony
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PPK_UCZ.cntx_psh();
PPK_UCZ.prefix();
OSOBA.cntx_psh();
OSOBA.prefix();
{? PPK_KOM.PPK_UCZ().OSOBA().PLEC='M'
|| _zwrot:='Pana'
|| _zwrot:='Pani'
?};
OSOBA.cntx_pop();
PPK_UCZ.cntx_pop();

:: Pobranie nazwy nowej instytucji finansowej:
PPK_UMO.cntx_psh();
PPK_UMO.prefix();
PPK_UZA.cntx_psh();
PPK_UZA.prefix();
ADRES.cntx_psh();
ADRES.prefix();
_ifN:=PPK_KOM.PPK_UMO().PPK_UZA().ADRES().NAZWA;
ADRES.cntx_pop();
PPK_UZA.cntx_pop();
PPK_UMO.cntx_pop();

:: Pobranie nazwy poprzedniej instytucji finansowej:
_popUmo:=exec('pop_umo','ppk_rpl',PPK_KOM.PPK_UMO);
_if:=_popUmo.PZIF_NAZWA;
obj_del(_popUmo);

'Informuję*, że podmiot zatrudniający ma obowiązek złożenia - w '+_zwrot+' imieniu - wniosku o wypłatę transferową '+
'środków zgromadzonych na '+_zwrot+' rachunku PPK, prowadzonym przez '+_if+', której umowa o zarządzanie PPK '+
'została wypowiedziana, na '+_zwrot+' rachunek PPK, prowadzony przez '+_ifN+', z którą zawarto nową umowę '+
'o prowadzenie PPK.**\n\n'+
exec('data_podpis','ppk_krd')+
'*Przekazanie tej informacji uczestnikowi PPK jest obowiązkiem podmiotu zatrudniającego na podstawie art. 12 ust. 2 '+
'ustawy z dnia 4 października 2018 r. o pracowniczych planach kapitałowych.\n'+
'**W terminie 7 dni od dnia otrzymania tej informacji od podmiotu zatrudniającego uczestnik PPK może poinformować, '+
'w formie pisemnej, podmiot zatrudniający o braku zgody na złożenie przez ten podmiot wniosku o wypłatę transferową.'


\data_podpis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Zwraca ciąg z datą i podpisem podmiotu i zatrudnionego
::   WE:
::   WY: [STRING]
::----------------------------------------------------------------------------------------------------------------------
'.............................\n'+
'(data i podpis podmiotu zatrudniającego)\n\n'+
'Potwierdzenie otrzymania:\n'+
'.............................\n'+
'(data i podpis osoby zatrudnionej)\n\n'

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 5296da38aa4a853b93f5244e8d106ba377a98a244ab8a519b7969e82248d63cfcd222af9c0ff17e89a3fbf553534fdc2dc21e0888690f47c2dadefe0d0a359fac74704b4162669660b66260840e8073f7535ae9516709b590029671c62ec3f7cbc884807e6bef26dac5afa6918d7b6257691ad50ac7ee84d06efae5e8fab5a79

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ro_map.fml
:: Utworzony: 01.02.2018
:: Autor: jaws
:: Systemy: RODO
::======================================================================================================================
:: Zawartość: Obsługa aplikacji dla administratora danych osobowych (RODO)
::======================================================================================================================


::======================================================================================================================
:: Formuły obsługi tabeli RO_MAP.
::======================================================================================================================


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Dodaje do mapy informacje dla rekordu źródłowego i docelowego.
::   WE: _a [STRING] - rodzaj zapisu:
::          'U' - dane zanonimizowane
::          'Z' - zapytanie dla danych
::       _b [REFERENCE] - wskazanie wiersza źródłowego
::       _c [REFERENCE] - wskazanie wiersza docelowego
::       _d [_FIRMA/STRING] - wskazanie wiersza w tabeli FIRMA lub '*' jeżeli wpis ma dotyczyć wszystkich firm
::   WY: wynik operacji dołączenia wiersza
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | _a=''
|| return(0)
?};

_typ:=_a;
_org:=null;
_new:=null;

{? var_pres('_b')=type_of(null) & _b<>null || _org:=$_b
|? var_pres('_b')=type_of('') & _b<>'' || _org:=_b
|| return(0)
?};

{? var_pres('_c')=type_of(null) & _c<>null || _new:=$_c
|? var_pres('_c')=type_of('') & _c<>'' || _new:=_c
|| return(0)
?};

{? var_pres('_d')=type_of('') & _d='*'
:  utwórz wpisy dla wszystkich firm
|| FIRMA.cntx_psh();
   FIRMA.index('SYMBOL');
   FIRMA.prefix();
   _size:=FIRMA.size();
   _loop:=FIRMA.first();
   {!
   |? _loop
   |! _size-=exec('add','ro_map',_typ,_org,_new,FIRMA.ref());
      _loop:=FIRMA.next()
   !};
   FIRMA.cntx_pop();
   return(_size=0)
?};

_firma:=
   {? var_pres('_d')<>type_of(null) | _d=null
   || exec('ref_firma','ro_map',_c)
   || _d
   ?};

{? ~exec('find_org','ro_map',_typ,_org,_firma)
|| RO_MAP.blank();
   RO_MAP.TYP:=_typ;
   RO_MAP.FIRMA:=_firma;
   RO_MAP.ORG_REF:=_org;
   RO_MAP.NEW_REF:=_new;
   RO_MAP.add()
|| 1
?}


\del_org
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Usuwa z mapy informacje dla rekordu źródłowego.
::   WE: _a [STRING] - rodzaj zapisu:
::          'U' - dane zanonimizowane
::          'Z' - zapytanie dla danych
::       _b [REFERENCE] - wskazanie wiersza
::       _c [_FIRMA] - wskazanie wiersza w tabeli FIRMA
::   WY: 1 - usunięto dane lub nie znaleziono zapisów, 0 - nie udało się usunąć danych
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | var_pres('_b')<>type_of(null) |
   _a='' | _b=null
|| return(0)
?};

{? exec('find_org','ro_map',_a,_b,{? var_pres('_c')=type_of(null) & _c<>null || _c ?})
|| RO_MAP.del(1,1)
|| 1
?}


\del_org_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Usuwa z mapy informacje dla rekordu źródłowego.
::   WE: _a [REFERENCE] - wskazanie wiersza
::       _b [_FIRMA] - wskazanie wiersza w tabeli FIRMA
::   WY:
::----------------------------------------------------------------------------------------------------------------------
RO_MAP.cntx_psh();
RO_MAP.index('ORG');
{? var_pres('_b')=type_of(null) & _b<>null
|| RO_MAP.prefix($_a,_b)
|| RO_MAP.prefix($_a)
?};
{? RO_MAP.first()
|| {!
   |? RO_MAP.del()
   !}
?};
RO_MAP.cntx_pop();
~~


\find_new
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Wyszukuje w mapie zapis właściwy dla wskazania nowego wiersza.
::   WE: _a [STRING] - rodzaj zapisu:
::          'U' - dane zanonimizowane
::          'Z' - zapytanie dla danych
::       _b [REFERENCE/STRING] - wskazanie wiersza
::       _d [_FIRMA] - wskazanie wiersza w tabeli FIRMA
::   WY: wynik metody find_key tabeli RO_MAP
::----------------------------------------------------------------------------------------------------------------------
_ref:='';
{? var_pres('_a')<>type_of('') | _a=''
|| return(0)
?};
{? var_pres('_b')=type_of(null)
|| {? _b=null
   || return(0)
   || _ref:=$_b
   ?}
|? var_pres('_b')=type_of('')
|| {? _b=''
   || return(0)
   || _ref:=_b
   ?}
|| return(0)
?};

exec('find','ro_map','TYP_NEW',_a,_ref,{? var_pres('_c')=type_of(null) & _c<>null || _c ?})


\find_org
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Wyszukuje w mapie zapis właściwy dla wskazania oryginalnego wiersza.
::   WE: _a [STRING] - rodzaj zapisu:
::          'U' - dane zanonimizowane
::          'Z' - zapytanie dla danych
::       _b [REFERENCE/STRING] - wskazanie wiersza
::       _d [_FIRMA] - wskazanie wiersza w tabeli FIRMA
::   WY: wynik metody find_key tabeli RO_MAP
::----------------------------------------------------------------------------------------------------------------------
_ref:='';
{? var_pres('_a')<>type_of('') | _a=''
|| return(0)
?};
{? var_pres('_b')=type_of(null)
|| {? _b=null
   || return(0)
   || _ref:=$_b
   ?}
|? var_pres('_b')=type_of('')
|| {? _b=''
   || return(0)
   || _ref:=_b
   ?}
|| return(0)
?};

exec('find','ro_map','TYP_ORG',_a,_ref,{? var_pres('_c')=type_of(null) & _c<>null || _c ?})


\find
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Wyszukuje w mapie zapis właściwy dla wskazania.
::   WE: _a [STRING] - klucz wyszukiwania
::       _b [STRING] - rodzaj zapisu:
::          'U' - dane zanonimizowane
::          'Z' - zapytanie dla danych
::       _c [STRING] - wskazanie wiersza
::       _d [_FIRMA] - wskazanie wiersza w tabeli FIRMA
::   WY: wynik metody find_key tabeli RO_MAP
::----------------------------------------------------------------------------------------------------------------------
RO_MAP.index(_a);
RO_MAP.prefix();

_firma:=
   {? var_pres('_d')<>type_of(null) | _d=null
   || exec('ref_firma','ro_map',_c)
   || _d
   ?};

RO_MAP.find_key(_firma,_b,_c,)


\ref_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Ustala firmę właściwą dla wskazania.
::   WE: _a [REFERENCE/STRING] - wskazanie wiersza
::   WY: wskazanie na firmę lub null w przypadku tabeli globalnej
::UWAGA:
::----------------------------------------------------------------------------------------------------------------------
_mask:='';
{? var_pres('_a')=type_of(null) & _a<>null || _mask:=form(8+$_a)
|? var_pres('_a')=type_of('') & _a<>'' || _mask:=form(8+_a)
|| return(REF.FIRMA)
?};

_local:=REF.FIRMA().SYMBOL+".mdb";
_dir:=pth_dir(":"+_mask+".mdb");

{? (_dir++_local)=_local | REF.WFIRM=0
|| REF.FIRMA
|| null
?}


\ro_map_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Przed zapisem wiersza tabeli RO_MAP ze sprawdzeniem poprawności danych.
::   WE:
::   WY: wynik testu: 1 - dane poprawne, 0 - błędne dane
::----------------------------------------------------------------------------------------------------------------------
{? RO_MAP.ORG_REF<>'' & RO_MAP.NEW_REF<>''
|| RO_MAP.ORG_ROW:=BIT.sqlint(RO_MAP.ORG_REF);
   RO_MAP.NEW_ROW:=BIT.sqlint(RO_MAP.NEW_REF);
   {? RO_MAP.ORG_ROW & RO_MAP.NEW_ROW
   || RO_MAP.ORG_MDB:=8+RO_MAP.ORG_REF;
      RO_MAP.NEW_MDB:=8+RO_MAP.NEW_REF;
      1
   ?}
?}


\ro_map_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli RO_MAP.
::   WE:
::   WY: 1 - jeśli można zapisać wiersz, 0 - wpp
::----------------------------------------------------------------------------------------------------------------------
RO_MAP.DATA:=date();
RO_MAP.STATUS:='N';

exec('ro_map_modb','ro_map')


\ro_map_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.22]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli RO_MAP.
::   WE:
::   WY: 1 - jeśli można zapisać wiersz, 0 - wpp
::----------------------------------------------------------------------------------------------------------------------
exec('ro_map_modb','ro_map')

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:01 dffb580b2000be5f9f46c03c6d18c6ea9454b8f2f6766add24ab94d084e4635bcbe19feb1165be8a4529085ce8c4126453f5d044a78e843bc474aab327b1610d828264b478cb5ca55b829207e8950091393c94a4ee098b0417454d7dd2b3f74df72684405c9fa6cd84f626f6d5c92b459002829c9f8b2e28c1939e6f429529d5

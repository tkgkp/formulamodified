:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: eurusd.fml
:: Utworzony: 2008
:: Autor: mario
::======================================================================================================================
:: Zawartość: Formuły do obsługi wielowalutowości dla dokumentów sprzedaży, magazynowych, ofert, zamówien sprzedaży,
::            zamówien dostaw, dokumentów zakupowych
::            zmienna BEER.WW = 0 brak walutowości 1 tylko na pozycjach 2 nagłówki
::            zmienna BEER.MW = F-sprzedaż M-magazyn Z-zamówienia O-oferty
::                              W-zamówienia dostaw inicjowana na polu Numer nagłówka
::                              K-zakupy
::                            Literki zwiaząne z pozycjami dokumentów:
::                              A-Pozycje dokumentów magazynowych
::                              B-Pozycje dokumentów sprzedaży
::                              C-Pozycje zamówień sprzedaży
::                              D-Pozycje ofert
::                              E-Pozycje zamówień dostaw
::                              G-Pozycje dokumentów zakupu
::======================================================================================================================


\ustaw_ww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: ustawia zmienną WW
::   WE: _a - typ dokumentu ZMF
::  TAG: <EURUSD>
::----------------------------------------------------------------------------------------------------------------------
BEER.WW:=0;
{? INFO.WW='A'
|| BEER.WW:=2*{? INFO.TW*_a || 1 || 0 ?}+{? INFO.TW*exec('poz_opc','eurusd',_a) || 1 || 0 ?}
|? INFO.WW='W' & ST.ODDZ_REF().WW='*'
|| BEER.WW:=2*{? ST.ODDZ_REF().TW*_a || 1 || 0 ?}+{? ST.ODDZ_REF().TW*exec('poz_opc','eurusd',_a) || 1 || 0 ?}
|? INFO.WW='P'
|| BEER.WW:={? INFO.TW*exec('poz_opc','eurusd',_a) || 1 || 0 ?}
?};
1


\ust_lw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: ustawienie litery dla okienek redakcyjnych i selekcji
::   WY: 1
::  TAG: <EURUSD>
::----------------------------------------------------------------------------------------------------------------------
BEER.LW:={? exec('spr_ww','eurusd',_a) || 'W' || '' ?};
1


\spr_ww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: sprawdzenie pola BEER.WW
::   WE: _a - 0-nagłówek 1-pozycja
::  TAG: <EURUSD>
::----------------------------------------------------------------------------------------------------------------------
{? _a || BEER.WW%*2 || BEER.WW%2 ?}


\poz_opc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: pozycja dla opcji
::   WE: _a - litera miejsca wywołania - czego dotyczy podobnie jak BEER.MW
::   WY: zwracana jest literka dla pozycji dokumentów podobnie jak INFO.TW:
::       A-magazyn B-sprzedaż C-zamówienia D-oferty E-zamówienia dostaw G-zakupy
::  TAG: <EURUSD>
::----------------------------------------------------------------------------------------------------------------------
_nr:='MFZOWK'*_a;
{? _nr || 1+((_nr-1)-'ABCDEG') || '' ?}


\po_oww
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji pola ODDZ.WW
::  TAG: <EURUSD><AE><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_fld:=fld();
_tab:=cur_tab();
{? _fld='' | _fld='N' | _fld='W'
|| _tab.TW:=''
|? _fld='A' & _tab.TW=''
|| _tab.TW:='MFZOWKABCDEG'
|? _fld='P'
|| _tab.TW:='ABCDEG'
?};
exec('wczytaj_pom_wal','eurusd');
exec('wyl_or_wl','eurusd');
_wyn


\pr_otw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcją pola ODDZ.TW
::  TAG: <EURUSD><BE><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
_tab.WW='*' | _tab.WW='A' | (_tab.WW='P' & 'ABCDEG'*(cur_afld()+1))


\pr_oddz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła odczytu ODDZ.TW dla pól POM.WAL_..
::  TAG: <EURUSD>
::----------------------------------------------------------------------------------------------------------------------
_tab:=cur_tab();
{? (1+menu_txt)='D' || _tab.TW:=''; _tab.WW:='' ?};
1


\wczytaj_pom_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła odczytu ODDZ.TW i INFO.TW dla pól POM.WAL_..
::   WE: [_a] - wskazanie na tabele
::  TAG: <EURUSD>
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>118 || _tab:=cur_tab() || _tab:=_a ?} || _tab:=cur_tab() ?};
_spr:='MFZOWKABCDEG';
_i:=0;
{!
|? _i+=1;
   _ch:=1+((_i-1)-_spr);
   ($('POM.WAL_'+_ch))():={? _tab.TW*_ch || 'T' || 'N' ?};
   _i<(+_spr)
!};
~~


\wyl_or_wl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: formuła odczytu ODDZ.TW dla pól POM.WAL_..
::   WE: [_a] - wskazanie na tabele
::       [_b] - 0(bez odświeżenia) 1-z odświeżeniem
::  TAG: <EURUSD>
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>118 || _tab:=cur_tab() || _tab:=_a ?} || _tab:=cur_tab() ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};

_win:={? _tab=ODDZ || ODDZ.win_edit('?') || 'ODDZ' ?};
_enanag:={? _tab=INFO & INFO.WW='A'
            | _tab=ODDZ & INFO.WW='W' & ODDZ.WW='*'   || 'enable=1' || 'enable=0' ?};
_enapoz:={? _tab=INFO & (INFO.WW='A' | INFO.WW='P')
            | _tab=ODDZ & INFO.WW='W' & ODDZ.WW='*'   || 'enable=1' || 'enable=0' ?};
{? _tab=ODDZ
|| {? INFO.WW='W'
   || _tab.efld_opt(_win,'enable=1',,'WW')
   || _tab.efld_opt(_win,'enable=0',,'WW')
   ?}
?};
_tab.efld_opt(_win,_enapoz,POM,'WAL_A');
_tab.efld_opt(_win,_enapoz,POM,'WAL_A');
_tab.efld_opt(_win,_enapoz,POM,'WAL_B');
_tab.efld_opt(_win,_enapoz,POM,'WAL_C');
_tab.efld_opt(_win,_enapoz,POM,'WAL_D');
_tab.efld_opt(_win,_enapoz,POM,'WAL_E');
_tab.efld_opt(_win,_enanag,POM,'WAL_F');
_tab.efld_opt(_win,_enapoz,POM,'WAL_G');
_tab.efld_opt(_win,_enanag,POM,'WAL_K');
_tab.efld_opt(_win,_enanag,POM,'WAL_M');
_tab.efld_opt(_win,_enanag,POM,'WAL_O');
_tab.efld_opt(_win,_enanag,POM,'WAL_W');
_tab.efld_opt(_win,_enanag,POM,'WAL_Z');
{? _b || win_disp() ?};
1


\po_otw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji pola POM.WAL_..
::  TAG: <EURUSD><AE><MBUILDER>
::----------------------------------------------------------------------------------------------------------------------
_spr:='MFZOWKABCDEG';
_wyn:=''; _i:=0;
_tab:=cur_tab();
{!
|? _i+=1;
   _ch:=1+((_i-1)-_spr);
   {? ($('POM.WAL_'+_ch))()='T' || _wyn+=_ch ?};
   _i<(+_spr)
!};
_tab.TW:=_wyn;
1


\jakiwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: ustalenie slownika wg parametrow
::   WE: _a - akronim pola w parametrach pracy systemu
::       _b - opis slownika
::       _c - sprawdzenie dla pozycji
::       _d - wartosc dla BEER.MW
::       [_e] - 1-podczas redakcji pola (domyślnie) 0-nie
::    [_f] - 1-zmiana wartości waluty tylko dla bieżącej tabeli (obsługa waluty dla tabeli FAKZ), 0 - nie (domyslnie)
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_d')<>type_of('A') || _d:='' ?};
{? var_pres('_e')<>type_of(0) || _e:=1 ?};
{? var_pres('_f')<>type_of(0) || _f:=0 ?};

{? _d='C'
|| _mw:=BEER.MW; BEER.MW:='C';
   _ww:=BEER.WW; exec('ustaw_ww','eurusd','F')
?};

{? _e & type_of($fld)=2 || DPOZ.WPR_S:=$fld ?};
POMOC.WAL:={? BEER.MW='M' || ND.WAL || null ?};
_wyn:=0;
{? _=2 | (_d='C' & INFO.WW<>'N') || _c:=0 ?};
_oki:={? _c
      || {? _f || 1 || exec('czy_rwal','eurusd',BEER.MW)?}
      || {? (cur_tab()=FAKS_K & (FAKS_K.ROZLICZ='T' | FAKS_K.INMG)) | exec('czy_poz','eurusd')
         || 1
         || (';IJO'*POMOC.LOCK)>1
         ?}
      ?};
{? _oki & exec('spr_ww','eurusd',_c)
|| _wyn:=exec('sl_wal','eurusd',_a,_b)
?};
{? _wyn=1 & POMOC.LOCK<>'' & ~((';IJO'*POMOC.LOCK)>1) || _wyn:=0 ?};
{? BEER.MW='M' & ((ND.TYP().INW='I' & ND.INN='T') | ND.TYP().DN='K') || _wyn:=0 ?};
{? BEER.MW='Z' & ZK_N.OFE<>null()  || _wyn:=0 ?};

{? _d='C' || BEER.MW:=_mw; BEER.WW:=_ww ?};

_wyn
&
{? BEER.MW='M' & ND.TYP().P='T' || exec('upr_cm','ceny')
|? BEER.MW='M' & ND.TYP().P='N' | BEER.MW='F' | BEER.MW='Z' | BEER.MW='O' || exec('upr_cs','ceny')
|? BEER.MW='K' || exec('upr_cz','ceny')
|| 1
?}


\czy_rwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: czy pole waluta jest dostepne do redakcji dla pozycji
::   WE: _a  typ MFZOW  M - magazyn F - faktury(pozycje) Z - zamowienia sprzedazy
::                      O - oferty W - zamowienia dostaw K-zakupy P - koszyk planu dostaw
::                      A - faktury(nagłówki)
::   WY: 1 tak 0 nie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? _a='M'
   & (exec('FindAndGet','#table',ND,DK.N,,"$ref()",'')='' | form(8+ref_name(DK.N))<>ND.name() )
|| DK.blank(0)
?};

_pln:={? 'WP'*_a
         | _a='M' & ND.TYP().P='T'
      || exec('bl_nwal_mg','ustawienia')
      || exec('bl_nwal','ustawienia')
      ?};
{? _a='M' || _wyn:={? exec('FindAndGet','#table',ND,DK.N,,,null())=0 || 1 || DK.N().WAL=_pln ?}
|? _a='F' || _wyn:=FAP.FAKS().WAL=_pln
|? _a='A' || _wyn:=FAKS.WAL=_pln
|? _a='Z' || _wyn:=(ZK_P.N().WAL=_pln | ZK_P.N=null)
|? _a='O' || _wyn:=OFP.OFE().WAL=_pln
|? _a='W' || _wyn:=ZD_POZ.ZD_NAG().WAL=_pln
|? _a='K' || _wyn:=FAP.FAKS().WAL=_pln
|? _a='P' || _wyn:=PD_K.WAL=_pln
?};
_wyn


\czy_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: czy dolaczono pozycje do dokumentu - sprawdza czy moze zmienic walute
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? BEER.MW='M'
|| _typ:={? ND.TYP().P='T' || 1 |? ND.TYP().CS='T' || 2 || 0 ?};
::   _war:=exec('get','#params',300501,2);
   _war:='"TYPYDOK".Z=\'T\' and "TYPYDOK".P=\'T\' and "TYPYDOK".DN=\'T\''
         +'and "TYPYDOK".TD=\'\' and "TYPYDOK".DS=\'N\'';
   _zwr:={? _war=''
         || 0
         || (_txt:='select * from TYPYDOK where %1 and "TYPYDOK".T=\'%2\''[_war,ND.TYP().T];
             sql(_txt).size())
         ?};
   {? _typ
   || _wyn:=exec('FindInSet','#table','DK','DOKMAG',ND.ref())=null;
      {? ~_wyn
      || _msk1:=EANP.name();
         _msk2:=form((ND.D~1)-2000,-2,0,'99');
         EANP.cntx_psh();
         EANP.use((_msk1-2)+_msk2);
         EANP.clear();
         _wyn:=exec('FindInSet','#table','EANP','RNAG',$ND.ref(),$ND.ref())<>null;
         EANP.cntx_pop();
         {? _wyn & ~_zwr
         || DK.cntx_psh();
            DK.index('DOKMAG');
            DK.prefix(ND.ref());
            {? DK.first()
            || {!
               |? _wyn:={? _typ=1 || ~DK.C || ~DK.CN & DK.ZAM_RP='' ?};
                  _wyn & DK.next()
               !}
            ?};
            DK.cntx_pop()
         ?}
      ?};
      _wyn
   || 1
   ?}
|? BEER.MW='F'
||
   _Tab:=cur_tab();
   {? type_of(_Tab)=0
   || _Tab:=FAKS
   ?};
   {? _Tab=FPACZ | _Tab=UP | _Tab=ZLE
   || 1
   |? _Tab=FAKS & FAKS.WHERE<>'K' & FAKS.WHERE<>'N'
   || {? FAKS.WHERE='M'
      || {? exec('FindInSet','#table','FAP','FAP',FAKS.ref())=null
         || exec('list_zam','magdok_wspolne',ND.ref()); ~__list_zk.first()
         || 0
         ?}
      || exec('FindInSet','#table','FAP','FAP',FAKS.ref())=null
      ?}
   |? _Tab=FAKS & FAKS.WHERE='K'
   || exec('FindAndGet','#table',FAKS,FAKS.ref(),,"
         exec('FindInSet','#table','FAKS_KZF','KZ_UID',FAKS.uidref())=null
      ",0)
   || 0
   ?}

|? BEER.MW='Z'
|| 1
|? BEER.MW='W'
|| 1
|? BEER.MW='K'
||
:: czy faktura nie ma pozycji i czy nie ma recznie wpisanych stawek VAT
   exec('FindInSet','#table','FAP','FAP',FAKS.ref())=null
   & exec('FindInSet','#table','FAKSV','FAKS_SV','R',$FAKS.ref())=null
|| 1
?}


\spr_wnag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: podpowiada wartosc dla sposobu wyboru kursu
::   WE: [_a] tabela + pole np. 'FAKS.SWAL'
::   WY:  wartosc dla pola 'sposob ustalenia kursu'
::----------------------------------------------------------------------------------------------------------------------
::{? _>=1  || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};

::exec('spr_ww','eurusd',0) & ~exec('czy_nwal','eurusd',BEER.MW);
0


\czy_nwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: czy pole waluta jest dostepne do redakcji dla naglowka
::   WE: _a  typ MFZOW  M - magazyn F - faktury Z - zamowienia sprzedazy
::                      O - oferty W - zamowienia dostaw  K - zakupy
::                      I - ??? T - !!! magazyn poprawienie pozycji do sprawdzenia
::       _b -O-opodatkowania, N-narodowa
::   WY: 1 tak 0 nie
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of('a') || _b:='O' ?};

_wyn:=1;
_pln:={? 'W'*_a
         | _a='M' & ND.TYP().P='T'
      || exec('bl_nwal_mg','ustawienia')
      |? _b='N'
      || exec('bl_nwal_mg','ustawienia')
      || exec('bl_nwal','ustawienia')
      ?};
_plnkod:=exec('FindAndGet','#table',SLO,$_pln,,"SLO.KOD",'');
{? _a='M' || _wyn:=ND.WAL=_pln
|? _a='F' || _wyn:=cur_tab.WAL=_pln
|? _a='Z' || _wyn:=ZK_N.WAL=_pln | ZK_N.OFE<>null()
|? _a='O' || _wyn:=OFE.WAL=_pln
|? _a='W' || _wyn:=ZD_NAG.WAL=_pln
|? _a='K' || _wyn:=FAKS.WAL=_pln
|? _a='I' || _wyn:=cur_tab.WAL=_plnkod
|? _a='T' || _wyn:=DK.KURS=_pln
?};
_wyn


\pr_krs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formula przed redakcja kursu KRS,NKRS
::   WE: [_a] - tabela
::       [_b] - pole
::----------------------------------------------------------------------------------------------------------------------
_tfld:={? var_pres('_a')=type_of(FIRMA) || _a || cur_tfld() ?};
_afld:={? var_pres('_b')=type_of('') || _b || cur_afld() ?};

_wyn:=0;

_mw:={? _tfld=ZMIENNE || 'T' || BEER.MW ?};
_przy:=BEER.MW='M' & (ND.TYP=null() | ND.TYP().P='T');
_zawsze_narod:=_afld='NKRS' | _przy;
_wal_typ:={? _zawsze_narod || 'N' || 'O' ?};

{? (exec('spr_ww','eurusd',0) | _zawsze_narod)
   & ~exec('czy_nwal','eurusd',_mw,_wal_typ)
   & (cur_tfld()<>FAKS | FAKS.T().KOR<>'Z' | FAKS.T().SPPK='T')
||
   _bankrs:={? KSTE.BANKRS<>null() || KSTE.BANKRS || INFO.BANKRS ?};
   _typkrs:={? KSTE.TYPKRS<>null() || KSTE.TYPKRS || INFO.TYPKRS ?};
   {? _bankrs<>null()
   || exec('dkurs','eurusd',_bankrs,_typkrs);
      win_disp()
   ?};
   {? _afld='KURS' | _afld='KRS' | _afld='NKRS'
   || {? var_pres('Krs_be')>0 || &Krs_be ?};
      Krs_be:=fld();
      {? _tfld=FAKS & FAKS.WHERE='K'
      || _wyn:=exec('faks_fld_czy_ed','faktury_nag')
      |? _tfld=FAKS & FAKS.WHERE='N'
      || _wyn:=0
      |? _tfld=ND
         & ND.WAL={? _afld='KRS' & ~_przy || exec('bl_nwal','ustawienia') || exec('bl_nwal_mg','ustawienia') ?}
      || _wyn:=0
      || _wyn:=1
      ?}

   || ($('_a.%1:=1'[_afld]))(_tfld);
      win_disp()
   ?}
?};

_wyn
&
{? BEER.MW='M' & _przy || exec('upr_cm','ceny')
|? BEER.MW='M' & ~_przy & BEER.MW='F' | BEER.MW='Z' | BEER.MW='O' || exec('upr_cs','ceny')
|? BEER.MW='K' || exec('upr_cz','ceny')
|| 1
?}


\dkurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Roman Firla [12.30]
:: OPIS: Procedura zwracajaca wartosc kursu typu _b dla banku _a w dniu _c
::   WE: _a - SLO.ref wskaznie na domyslny bank, _b - typ kursu, _c - data kursu,
::       _d - tabela i pole (np. FAKS.KRS, żeby pominąć wywołanie "cur_")
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>4 || _c:=date(0,0,0) ?} || _c:=date(0,0,0) ?};
{? _>=4 || {? type_of(_d)<>type_of('') || _d:='' ?} || _d:='' ?};
VAR_DEL.delete('cur_tfld_');
VAR_DEL.delete('cur_afld_');
VAR_DEL.delete('cur_tab_');
{? _d='FAKS.KRS'
|| cur_tab_:=FAKS;
   cur_tfld_:=FAKS;
   cur_afld_:='KRS'
|? _d='FAKS.NKRS'
|| cur_tab_:=FAKS;
   cur_tfld_:=FAKS;
   cur_afld_:='NKRS'
|? _d='ZK_N.KRS'
|| cur_tab_:=ZK_N;
   cur_tfld_:=ZK_N;
   cur_afld_:='KRS'
|| cur_tfld_:=cur_tfld();
   cur_afld_:=cur_afld();
   cur_tab_:=cur_tab()
?};
TKRS.cntx_psh();
KRS.cntx_psh();
{? cur_afld_='NKRS'
||
   _wal:=cur_tfld_.WAL;
   _wal_tkrs:=exec('bl_nwal_mg','ustawienia');
   _wyn:=cur_tfld_.NKRS;
   _krs:=$('cur_tfld_.NKRS');   _dtk:=$('cur_tfld_.NDTK');
   _rtk:=$('cur_tfld_.NRTK');   _ntk:=$('cur_tfld_.NNTK');
   _btk:=$('cur_tfld_.NBTK');
   _t_k:=$('cur_tfld_.NSWAL')
|? {? cur_tfld_=DK || DK.KURS=0 || cur_tfld_.KRS=0 ?}
||
   {? exec('get','#params',100352,2)='J'
   || _iledni:=exec('get','#params',100350,1)
   || _iledni:=exec('get','#params',4050,1,OPERATOR.USER)
   ?};
   _wal:=cur_tfld_.WAL;
   {? type_of(_wal)=type_of('') || _wal:=exec('FindInSet','#table','SLO','SL',_wal,XINFO.SLWAL) ?};
   _wal_tkrs:=
      {? 5+cur_tab_.name()='nagdo' & ND.TYP().P='T'
         | 5+cur_tab_.name()=5+ZD_NAG.name()
         | cur_tab_.name()=PD_K.name()
      || exec('bl_nwal_mg','ustawienia')
      |? 5+cur_tab_.name()='faktu'
      || cur_tfld_.NWAL
      || exec('bl_nwal','ustawienia')
      ?};
   _wyn:=cur_tfld_.KRS;
   _krs:=$('cur_tfld_.KRS');   _dtk:=$('cur_tfld_.DTK');
   _rtk:=$('cur_tfld_.RTK');   _ntk:=$('cur_tfld_.NTK');
   _btk:=$('cur_tfld_.BTK');
   _t_k:=$('cur_tfld_.SWAL');
   _tk:={? _c=date(0,0,0)
        ||
           _tab:=exec('tab_acr','#table',cur_tfld_);
           {? _tab='FAKS'
           ||
              {? cur_afld_='KRS'
              ||
                 _data:={? exec('get','#params',300171,2)='T' & FAKS.DO<>date(0,0,0) & FAKS.DO<=FAKS.DW || FAKS.DO || FAKS.DW ?};
                 {? cur_tfld_.T().ZAL='T' & cur_tfld_.KH().TYP<>'U'
                 || {? cur_tfld_.D<>date(0,0,0)
                    || cur_tfld_.D-_iledni
                    || date(0,0,0)
                    ?}
                 |? _data<>date(0,0,0)
                 || _data-_iledni
                 || date(0,0,0)
                 ?}
              |? cur_tfld_.D<>date(0,0,0)
              ||
                 cur_tfld_.D-_iledni
              ||
                 date(0,0,0)
              ?}
           |? _tab='ND'
           ||
              {? cur_tfld_.D<>date(0,0,0)
              || cur_tfld_.D-_iledni
              || date(0,0,0)
              ?}
           |? _tab='ZK_N'
           ||
              {? cur_tfld_.DP<>date(0,0,0)
              || cur_tfld_.DP-_iledni
              || date(0,0,0)
              ?}
           |? _tab='ZD_NAG'
           ||
              {? cur_tfld_.DATA<>date(0,0,0)
              || cur_tfld_.DATA-_iledni
              || date(0,0,0)
              ?}
           |? _tab='OFE'
           ||
              {? cur_tfld_.DU<>date(0,0,0)
              || cur_tfld_.DU-_iledni
              || date(0,0,0)
              ?}
           |? _tab='FPACZ'
           ||
              {? cur_tfld_.DS<>date(0,0,0)
              || cur_tfld_.DS-_iledni
              || date(0,0,0)
              ?}
           |? _tab='ZMIENNE'
           ||
              {? cur_tfld_.DTK<>date(0,0,0)
              || cur_tfld_.DTK-_iledni
              || date(0,0,0)
              ?}
           |? _tab='FAKSZ'
           ||
              date()
           |? _tab='PROJEKTY' | _tab='PR_KOS'
           || {? cur_tfld_.DTK<>date(0,0,0)
              || cur_tfld_.DTK
              || date()
              ?}
           |? cur_tfld_.D<>date(0,0,0)
           ||
              cur_tfld_.D-_iledni
           ||
              date(0,0,0)
           ?}
        ||
           _c
        ?};
   {! |? exec('spr_kal','faktury_plat',_tk,,1)=1 |! _tk-=1 !};
   _acr:=KRS.ndx_tmp('',0,'TKRS','BANK',0,'TKRS','WAL',0,'WAL',,0,'TKRS','DT',0);
   KRS.index(_acr);
   KRS.prefix(_a,_wal_tkrs,_wal,_tk);
   _wyn:=0;
   _sel:=1;
   {? KRS.first()
   || KRS.win_sel('SLO');
      {? ~KRS.next() | (_sel:=KRS.select())
      || _wyn:={? _b=0 || KRS.SP |? _b=1 || KRS.KP |? _b=2 || KRS.SR ?}
      ?}
   ?};
   {? _wyn
   ||
      _dtk():=KRS.TKRS().DT;
      _rtk():=#KRS.TKRS;
      _ntk():=ref_name(TKRS.ref());
      _btk():=KRS.TKRS().BANK().KOD;

      _krs():=_wyn;
      _t_k():=1
   ||
      {? cur_afld_='KRS'
      ||
         _dtk():=date(0,0,0);
         _rtk():=0;
         _ntk():='';
         _btk():='';
         _krs():=0
      ?};
      _t_k():=2;
      {? _sel & _tk<>date(0,0,0)
      || FUN.info('Nie znaleziono kursu waluty dla domyślnego banku\nna dzień %1.'@[_tk$7])
      ?}
   ?};
   KRS.ndx_drop(_acr)
?};
KRS.cntx_pop();
TKRS.cntx_pop();
VAR_DEL.delete('cur_tfld_');
VAR_DEL.delete('cur_afld_');
VAR_DEL.delete('cur_tab_');
~~


\f3_krs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja F3 dla kursu (wybor banku oraz kursu) KRS, NKRS
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
TKRS.cntx_psh();
KRS.cntx_psh();
:: zwraca wybrany kurs lub to co jest w red. polu
{? exec('spr_ww','eurusd',0)
||
   {? cur_afld()='NKRS'
   ||
      _typ:=cur_tfld.NSWAL;
      _wal:=cur_tfld.WAL;
      _wal_tkrs:=INFO.NAROD;
      _wyn:=cur_tfld.NKRS;
      _krs:=$('cur_tfld.NKRS');   _dtk:=$('cur_tfld.NDTK');
      _rtk:=$('cur_tfld.NRTK');   _ntk:=$('cur_tfld.NNTK');
      _btk:=$('cur_tfld.NBTK');
      _t_k:=$('cur_tfld.NSWAL')
   ||
      _typ:=cur_tfld.SWAL;
      _wal:=cur_tfld.WAL;
      {? type_of(_wal)=type_of('') || _wal:=exec('FindInSet','#table','SLO','SL',_wal,XINFO.SLWAL) ?};
      _wal_tkrs:=
         {? 5+cur_tab.name='faktu'
            | 5+cur_tab.name='nagdo' & ND.TYP().P='N'
            | cur_tab().name()<>'' & cur_tab().name()=FAKSZ.name()
         ||
            cur_tfld.NWAL

         |? cur_tab.name='oferty'
            | 5+cur_tab.name='zknag'
         ||
            exec('bl_nwal','ustawienia')
         ||
            INFO.NAROD
         ?};
      _wyn:=cur_tfld.KRS;
      _krs:=$('cur_tfld.KRS');   _dtk:=$('cur_tfld.DTK');
      _rtk:=$('cur_tfld.RTK');   _ntk:=$('cur_tfld.NTK');
      _btk:=$('cur_tfld.BTK');
      _t_k:=$('cur_tfld.SWAL')
   ?};
   DPOZ.WAL:=_wal;
   {? _typ<3
   || SLO.cntx_psh();
      _ok:=1;
      {? #INFO.SLBANK=0
      || FUN.info('Nie zdefiniowano słownika banków.'@);
         _ok:=0
      ||
         INFO.SLBANK().SLU();
         {? #SSTALE.WAL<>0 || SSTALE.WAL() ?};
         _bank:=null;
         SLO.index('SL');
         SLO.prefix(SLU.ref(),_btk());
         {? SLO.first() || _bank:=SLO.ref() ?};
         SLO.prefix(SLU.ref());
         SLO.seek(_bank);
         SLO.win_sel('BANKI');
         exec('slu_okn','slo_slu')
      ?};

      {? _ok=1
      || {? SLO.select(,1)
         || _slo:=SLO.ref();
            {? _slo<>null
            || DPOZ.B:=_slo;
               _acr:=KRS.ndx_tmp('',0,'TKRS','BANK',0,'TKRS','WAL',0,'WAL',,0,'TKRS','DT',0);
               KRS.index(_acr);
               KRS.prefix(_slo,_wal_tkrs,_wal);
               KRS.win_sel('SLO');
               KRS.hdr_sel();
               KRS.hdr_sel(' banku '@+SLO.TR);
               {? KRS.select(,KRS.last,10)
               || _opc:=FUN.choice('Wybierz typ kursu '@,3,'Sprzedaży'@,'Za&kupu'@,'Ś&redni'@);
                  _dtk():=KRS.TKRS().DT;
                  _rtk():=#KRS.TKRS;
                  _ntk():=ref_name(TKRS.ref());
                  _btk():=KRS.TKRS().BANK().KOD;
                  _wyn:={? _opc=1 || KRS.SP |? _opc=2 || KRS.KP |? _opc=3 || KRS.SR ?};
                  _krs():=_wyn;
                  _t_k():=1;
                  {? var_pres('Krs_f3')>0 || &Krs_f3 ?};
                  Krs_f3:=1
               || _wyn:=~~
               ?};
               KRS.ndx_drop(_acr)
            ?}
         || _wyn:=~~
         ?}
      ?};
      SLO.cntx_pop()
   ?}
?};
KRS.cntx_pop();
TKRS.cntx_pop();
_wyn


\po_krs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formula po redakcji kursu KRS,NKRS
::   WE: _a - tabela i pole (np. FAKS.KRS, żeby pominąć wywołanie "cur_")
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>type_of('') || _a:='' ?} || _a:='' ?};
VAR_DEL.delete('cur_tfld_');
VAR_DEL.delete('cur_afld_');
VAR_DEL.delete('cur_tab_');
{? _a='FAKS.KRS'
|| cur_tab_:=FAKS;
   cur_tfld_:=FAKS;
   cur_afld_:='KRS'
|? _a='FAKS.NKRS'
|| cur_tab_:=FAKS;
   cur_tfld_:=FAKS;
   cur_afld_:='NKRS'
|? _a='ZK_N.KRS'
|| cur_tab_:=ZK_N;
   cur_tfld_:=ZK_N;
   cur_afld_:='KRS'
|| cur_tfld_:=cur_tfld();
   cur_afld_:=cur_afld();
   cur_tab_:=cur_tab()
?};
_ok:=0;
_nk_zap:=0;
{? cur_afld_='NKRS'
|| _typ:=cur_tfld_.NSWAL;
   _wal:=cur_tfld_.NWAL;
   cur_tfld_.NKRS:=cur_tfld_.NKRS$4;
   _krs:=cur_tfld_.NKRS;

   _rtk:=$('cur_tfld_.NRTK');   _ntk:=$('cur_tfld_.NNTK');
   _btk:=$('cur_tfld_.NBTK');
   _t_k:=$('cur_tfld_.NSWAL')
|| _typ:=cur_tfld_.SWAL;
   _wal:=cur_tfld_.WAL;
   cur_tfld_.KRS:=cur_tfld_.KRS$4;
   {? 5+cur_tab_.name='faktu' | 5+cur_tab_.name='nagdo' | POMOC.LOCK='O'
   || {? cur_tfld_.NWAL=INFO.NAROD & (cur_tfld_.NKRS=0 | 5+cur_tab_.name='nagdo' & ND.TYP().P='T')
      || cur_tfld_.NKRS:=cur_tfld_.KRS$4 ;
         {? cur_tfld_.SWAL=1
         || cur_tfld_.NSWAL:=1;
            cur_tfld_.NRTK:=cur_tfld_.RTK;
            cur_tfld_.NNTK:=cur_tfld_.NTK;
            cur_tfld_.NDTK:=cur_tfld_.DTK;
            cur_tfld_.NBTK:=cur_tfld_.BTK;
            _nk_zap:=1
         ?}
      ?}
   ?};

   _krs:=cur_tfld_.KRS;
   _rtk:=$('cur_tfld_.RTK');   _ntk:=$('cur_tfld_.NTK');
   _btk:=$('cur_tfld_.BTK');
   _t_k:=$('cur_tfld_.SWAL');
   1
?};
{? _a='' & cur_tfld_=FAKS || exec('set_efld_opt','faktury_nag') ?};
{? cur_tfld_=PROJEKTY || exec('projekty_p_val_ae','!zws_par_kprr') ?};

{? _a=''
|| {? var_pres('Krs_be')<0 || Krs_be:=fld() ?};
   {? var_pres('Krs_f3')<0 || Krs_f3:=0 ?};
   {? Krs_be<>fld() & ~Krs_f3
   || _rtk():=0;
      _ntk():='';
      _btk():='';
      _t_k():=2;
      {? _nk_zap
      || cur_tfld_.NSWAL:=cur_tfld_.SWAL;
         cur_tfld_.NRTK:=cur_tfld_.RTK;
         cur_tfld_.NNTK:=cur_tfld_.NTK;
         cur_tfld_.NDTK:=cur_tfld_.DTK;
         cur_tfld_.NBTK:=cur_tfld_.BTK
      ?}
   ?};
   &Krs_be;
   &Krs_f3
?};

_ok:=1;
:: [mz] komunikat tylko dla typ=2 reczny wybor
{? _typ=2 & _krs=0 & ~(cur_tfld_=OFE | cur_tfld_=ZK_N | cur_tfld_=FAKS & FAKS.WHERE='K')
|| _ok:=1
|? _typ=2 & _krs<0
|| FUN.info('Kurs nie może być ujemny.'@)
?};
{? _krs<=0
||
   _rtk():=0;
   _ntk():='';
   _btk():='';
   _t_k():=2;
   {? _krs<0 || _ok:=0 ?}
|? cur_tfld_=ZMIENNE
|| exec('ae_zm_ck','magdok_poz')
?};
VAR_DEL.delete('cur_tfld_');
VAR_DEL.delete('cur_afld_');
VAR_DEL.delete('cur_tab_');
_ok


\pr_dtk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcja pola DTK, NDTK
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
_tfld:=cur_tfld();
_afld:=cur_afld();
_mw:={? _tfld=ZMIENNE || 'T' || BEER.MW ?};
_przy:=BEER.MW='M' & (ND.TYP=null() | ND.TYP().P='T');
_zawsze_narod:=_afld='NDTK' | _przy;
_wal_typ:={? _zawsze_narod || 'N' || 'O' ?};

{? (exec('spr_ww','eurusd',0) | _zawsze_narod)
   & ~exec('czy_nwal','eurusd',_mw,_wal_typ)
   & (cur_tfld()<>FAKS | FAKS.T().KOR<>'Z' | FAKS.T().SPPK='T')
||
   {? _afld='DTK' | _afld='NDTK'
   || _typ:={? _afld='NDTK' || _tfld.NSWAL || _tfld.SWAL ?};
      {? _typ>1
      || {? _tfld=FAKS & FAKS.WHERE='K'
         || exec('faks_fld_czy_ed','faktury_nag')
         |? _tfld=FAKS & FAKS.WHERE='N'
         || 0
         |? _tfld=ND
            & ND.WAL={? _afld='DTK' & ~_przy || exec('bl_nwal','ustawienia') || exec('bl_nwal_mg','ustawienia') ?}
         || 0
         |? _tfld=FAP & (exec('dsp_wpoz','faktury_wspolne')=0 | params_exec('fap_pow_z_dk','faktury_poz'))
         || 0
         || 1
         ?}
      |? _tfld=FAP & exec('dsp_wpoz','faktury_wspolne')
      || 1
      || 0
      ?}
   || 0
   ?}
?}
&
{? BEER.MW='M' & _przy || exec('upr_cm','ceny')
|? BEER.MW='M' & ~_przy | BEER.MW='F' | BEER.MW='Z' | BEER.MW='O' || exec('upr_cs','ceny')
|? BEER.MW='K' || exec('upr_cz','ceny')
|| 1
?}


\po_dtk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji pola DTK, NDTK
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='NDTK'
|| _typ:=cur_tfld.NSWAL;
   _dtk:=cur_tfld.NDTK;
   _krs:=cur_tfld.NKRS
|| {? 5+cur_tab.name='faktu' | 5+cur_tab.name='nagdo' | POMOC.LOCK='O'
   || {? cur_tfld.NWAL=INFO.NAROD & (cur_tfld.NDTK=date(0,0,0) | 5+cur_tab.name='nagdo' & ND.TYP().P='T')
      || cur_tfld.NDTK:=cur_tfld.DTK
      ?}
   ?};
   _typ:=cur_tfld.SWAL;
   _dtk:=cur_tfld.DTK;
   _krs:=cur_tfld.KRS
?};
::{? _typ>1 & _krs>0 & _dtk=date(0,0,0)
::|| FUN.info('Należy podać datę kursu.'@); 0
::|| 1
::?}
1


\czy_pwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: czy pole waluta do redakcji - dla pozycji
::   WE: _a  typ MFZOW  M - magazyn F - faktury Z - zamówienia sprzedaży
::                      O - oferty W - zamówienia dostaw  K - zakupy P - koszyk planu dostaw
::   WY: 1 tak 0 nie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;

_pln:={? 'WP'*_a
         | _a='M' & ND.TYP().P='T'
      || exec('bl_nwal_mg','ustawienia')
      || exec('bl_nwal','ustawienia')
      ?};
{? _a='M' || _wyn:=DK.WAL=_pln
|? _a='F' || _wyn:=FAP.WAL=_pln
|? _a='Z' || _wyn:=ZK_P.WAL=_pln
|? _a='O' || _wyn:=OFP.WAL=_pln
|? _a='W' || _wyn:=ZD_POZ.WAL=_pln
|? _a='K' || _wyn:=FAP.WAL=_pln
|? _a='P' || _wyn:=PD_K.WALK=_pln
?};
_wyn


\spr_npoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: Formuła przed redakcją pola CWAL
::----------------------------------------------------------------------------------------------------------------------
(exec('spr_ww','eurusd',0) | exec('spr_ww','eurusd',1)) & ~exec('czy_pwal','eurusd',BEER.MW)


\sl_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formuła przed redakcją pola OFP.ZWAL
::   WE:  _a - akronim pola w paramerach pracy systemu
::        _b - opis słownika
::   WY: 1/0 - prawidłowe ustalenie słownika
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? ($('INFO.'+_a))()<>null
|| ($('INFO.'+_a+'().SLU()'))();
   exec('slu_okn','slo_slu');
   SLO.hdr_sel();
   SLO.hdr_sel('Waluty'@);
   _wyn:=1
|| FUN.info('Nie ustalono słownika %1 w parametrach pracy programu.'@[_b])
?};
_wyn


\pkurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [12.30]
:: OPIS: Procedura zwracajaca wartość kursu typu _b dla banku _a w dniu _c
::   WE: _a - SLO.ref wskazanie na domyślny bank, _b - typ kursu, _c - data kursu
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>4 || _c:=date(0,0,0) ?} || _c:=date(0,0,0) ?};
TKRS.cntx_psh();
KRS.cntx_psh();
{? cur_afld()='WAL'
||
   {? exec('get','#params',100352,2)='J'
   || _iledni:=exec('get','#params',100351,1)
   || _iledni:=exec('get','#params',4051,1,OPERATOR.USER)
   ?};
   _wal:=cur_tfld.WAL;
   {? type_of(_wal)=type_of('') || _wal:=exec('FindInSet','#table','SLO','SL',_wal,XINFO.SLWAL) ?};
   _wal_tkrs:=INFO.NAROD;
   _krs:=$('cur_tfld.KRS');
   _tk:=
      {? _c=date(0,0,0)
      ||
         _tab:=exec('tab_acr','#table',cur_tfld.name);
         {? _tab='FAP'
         ||
            {? cur_afld()='KRS'
            ||
               {? FAP.FAKS().T().ZAL='T' & FAKS.KH().TYP<>'U'
               || {? FAKS.D<>date(0,0,0)
                  || FAKS.D-_iledni
                  || date(0,0,0)
                  ?}
               |? FAKS.DW<>date(0,0,0)
               || FAKS.DW-_iledni
               || date(0,0,0)
               ?}
            |? FAP.FAKS().D<>date(0,0,0)
            || FAKS.D-_iledni
            || date(0,0,0)
            ?}
         |? _tab='DK'
         || _krs:=$('cur_tfld.KURS');
            {? DK.N().D<>date(0,0,0)
            || ND.D-_iledni
            || date(0,0,0)
            ?}
         |? _tab='ZK_P'
         ||
            {? ZK_P.N().DP<>date(0,0,0)
            || ZK_N.DP-_iledni
            || date(0,0,0)
            ?}
         |? _tab='ZD_POZ'
         ||
            {? ZD_POZ.ZD_NAG().DATA<>date(0,0,0)
            || ZD_NAG.DATA-_iledni
            || date(0,0,0)
            ?}
         |? _tab='OFP'
         ||
            {? OFP.OFE().DU<>date(0,0,0)
            || OFE.DU-_iledni
            || date(0,0,0)
            ?}
         |? _tab='PD_K'
         ||
            {? PD_K.DZ<>date(0,0,0)
            || PD_K.DZ-_iledni
            || date(0,0,0)
            ?}
         |? cur_tfld.D<>date(0,0,0)
         ||
            cur_tfld.D-_iledni
         ||
            date(0,0,0)
         ?}
      ||
         _c
      ?};
   {! |? exec('spr_kal','faktury_plat',_tk,,1)=1 |! _tk-=1 !};
   _acr:=KRS.ndx_tmp('',0,'TKRS','BANK',0,'TKRS','WAL',0,'WAL',,0,'TKRS','DT',0);
   KRS.index(_acr);
   KRS.prefix(_a,_wal_tkrs,_wal,_tk);
   _wyn:=0;
   _sel:=1;
   {? KRS.first()
   || KRS.win_sel('SLO');
      {? ~KRS.next() | (_sel:=KRS.select())
      || _wyn:={? _b=0 || KRS.SP |? _b=1 || KRS.KP |? _b=2 || KRS.SR ?}
      ?}
   ?};
   {? _wyn
   ||
      {? _krs() & _krs()<>_wyn
      ||  {? FUN.ask('W ustawieniach wybrano opcję podpowiadania kursów z tabel kursowych.\n'
                     'Kurs wynikający z tabeli kursów:%1 jest inny niż wpisany kurs: %2.\n'
                     'Podpowiedzieć nowy kurs?'@[$_wyn,$_krs()])
          || _krs():=_wyn
          ?}
      || _krs():=_wyn
      ?}
   ||
      {? _sel || FUN.info('Nie znaleziono kursu waluty dla domyślnego banku\nna dzień %1.'@[_tk$7]) ?};
      _krs():=_wyn
   ?};
   KRS.ndx_drop(_acr)
?};
KRS.cntx_pop();
TKRS.cntx_pop();
~~


\spr_wpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: sprawdza czy do redakcji. Formuła przed redakcją dla pola Kurs
::   WE: _a-1-zapamietywać wartość pola, 0-wpp
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};

{? _a
||
   DPOZ.WPR_R:=fld;
   exec('pr_krs','eurusd')
?};

_wyn:=
   exec('spr_ww','eurusd',1)
   & {? exec('czy_rwal','eurusd',BEER.MW)
     || ~exec('czy_pwal','eurusd',BEER.MW)
     || {? BEER.MW='F' | BEER.MW='K' || exec('dsp_wpoz','faktury_wspolne') & ~params_exec('fap_pow_z_dk','faktury_poz')
        |? BEER.MW='P' || PD_K.WALK<>INFO.NAROD
        |?  exec('czy_nwal','ustawienia',INFO.NAROD)=0 || 1
        || 0
        ?}
     ?};
{? _wyn=1 & cur_tab.WAL=null || _wyn:=0 ?};

_wyn
&
{? BEER.MW='M' & ND.TYP().P='T' || exec('upr_cm','ceny')
|? BEER.MW='M' & ND.TYP().P='N' | BEER.MW='F' | BEER.MW='Z' | BEER.MW='O' || exec('upr_cs','ceny')
|? BEER.MW='K' || exec('upr_cz','ceny')
|| 1
?}


\f3_pkrs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: akcja F3 dla kursu z tabeli (wybór banku oraz kursu) dla pozycji dokumentu
::   WE: [_a] - działa dla pola OFP.ZKRS (1)
::   WY: zwraca wybrany kurs lub to co jest w red. polu
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0?};

_wyn:=0;
{? exec('spr_ww','eurusd',1) | _a=1
|| {? _a=1
   || _wyn:=OFP.ZKRS;
      _krs:=$('@.OFP.ZKRS');   _wal:=$('OFP.ZWAL')
   |? BEER.MW='M'
   || _wyn:=DK.KURS;
      _krs:=$('@.DK.KURS');   _wal:=$('DK.WAL')
   |? BEER.MW='F'
   || _wyn:=FAP.KRS;
      _krs:=$('@.FAP.KRS');   _wal:=$('FAP.WAL')
   |? BEER.MW='Z'
   || _wyn:=ZK_P.KRS;
      _krs:=$('@.ZK_P.KRS');  _wal:=$('ZK_P.WAL')
   |? BEER.MW='O'
   || _wyn:=OFP.KRS;
      _krs:=$('@.OFP.KRS');  _wal:=$('OFP.WAL')
   |? BEER.MW='K'
   || _wyn:=FAP.KRS;
      _krs:=$('@.FAP.KRS');   _wal:=$('FAP.WAL')
   |? BEER.MW='W'
   || _wyn:=ZD_POZ.KRS;
      _krs:=$('@.ZD_POZ.KRS');  _wal:=$('ZD_POZ.WAL')
   |? BEER.MW='P'
   || _wyn:=PD_K.KRS;
      _krs:=$('@.PD_K.KRS');  _wal:=$('PD_K.WALK')
   ?};
   DPOZ.WAL:=_wal();
   _wal_tkrs:=
      {? BEER.MW='F' | BEER.MW='K' || FAKS.NWAL
      |? BEER.MW='O' | BEER.MW='Z' || exec('bl_nwal','ustawienia')
      |? BEER.MW='M' & ND.TYP().P='N' || ND.NWAL
      || INFO.NAROD
      ?};
   {? DPOZ.WAL<>_wal_tkrs
   || SLO.cntx_psh();
      _ok:=1;
      {? #INFO.SLBANK=0
      || FUN.info('Nie zdefiniowano słownika banków.'@);
         _ok:=0
      || INFO.SLBANK().SLU();
         {? #SSTALE.WAL<>0 || SSTALE.WAL() ?};
         SLO.index('SL');
         SLO.prefix(SLU.ref());
         SLO.win_sel('BANKI');
         exec('slu_okn','slo_slu')
      ?};

      {? _ok=1
      || {? SLO.select(,1)
         || _slo:=SLO.ref();
            {? _slo<>null
            || DPOZ.B:=_slo;

               _acr:=KRS.ndx_tmp('',0,'TKRS','BANK',0,'TKRS','WAL',0,'WAL',,0,'TKRS','DT',0);

               KRS.index(_acr);
               KRS.prefix(_slo,_wal_tkrs,DPOZ.WAL);
               KRS.win_sel('SLO');
               KRS.last();
               {? KRS.select(,1,10)
               || _opc:=FUN.choice('Wybierz typ kursu '@,3,'Sprzedaży'@,'Za&kupu'@,'Ś&redni'@);
                  _wyn:={? _opc=1 || KRS.SP |? _opc=2 || KRS.KP |? _opc=3 || KRS.SR ?};
                  _krs():=_wyn
               || _wyn:=~~
               ?};
               KRS.ndx_drop(_acr)
            ?}
         || _wyn:=~~
         ?}
      ?};
      SLO.cntx_pop()
   ?}
|| ''
?};
_wyn


\bl_krsw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Wartość początkowa ZD_POZ.KRS
::----------------------------------------------------------------------------------------------------------------------
ZD_NAG.KRS


\bl_swal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: formuła na wartość początkową pola ND.SWAL - sposób wyboru kursu
::   WY: 2 - podpowiada ręczny kurs
::----------------------------------------------------------------------------------------------------------------------
2


\krs_rekb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: rekord przed tabeli KRS
::----------------------------------------------------------------------------------------------------------------------
TKRS.cntx_psh();
DPOZ.B:=KRS.TKRS().BANK;
TKRS.cntx_pop();
~~


\zmienne_f3_krs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: F3 ZMIENNE.KRS
::----------------------------------------------------------------------------------------------------------------------
{? ND.WAL=INFO.NAROD
|| exec('f3_pkrs','eurusd')
|| exec('f3_krs','eurusd')
?}


\mw_bez_wal_opod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Miejsce bez obsługi waluty opodatkowania
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
('OZW'*BEER.MW<>0 | BEER.MW='M' & ND.TYP().P='T')
& exec('bl_nwal','ustawienia')<>exec('bl_nwal_mg','ustawienia')

:Sign Version 2.0 jowisz:1045 2024/02/20 14:59:52 08fb5aa46ff5dabd6f877aabd79c43f2d0b11c2d27ebcfb68936e7d22963143e38724a80dc86b59b84418cc1d9359f5af16b7b805a709d509a19b6352d5abcda99a991d3dc80d10c7e38290675822fef6ec485cb2222287f82e4b876c768ea3b115a4ddb6dd4c291e384e6cf7e6086322e0710fe616ae4749ecf8a42493b4fd5

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zamdst_wspolne.fml
:: Utworzony: 06.07.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa zamówień dostaw - funkcje wspołne zamówień
::======================================================================================================================

:: TODO: do czynności zamówień dostaw


\aktu_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: aktualizacja danych w zamowieniu
::       np. po zaakceptowaniu lub usunieciu realizacji
::   WE: _a - ref zamowienia (ZD_NAG.ref)
::       [_b] tryb zmiany ilości na zamówieniu 1, 0-nie(domyślnie)
::  OLD: \aktu_zam/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_pozo:=_zreal:=_ilpoz:=0;
_mianownik:=_licznik:=0;
_modil:={? var_pres('_b')=type_of(0) || _b & exec('get','#params',600803,2)='Z' || 0 ?};
_upilr:=exec('get','#params',100709,2)='T';
:: aktualizacja pozycji zam.
ZD_POZ.cntx_psh();
ZD_POZ.index('POZ');
ZD_POZ.prefix(_a);
{? ZD_POZ.first()
||
   {? ZD_POZ.size()>100 || echo('Trwa aktualizacja stanu zamówienia.'@) ?};
   {!
   |? _il_zre:=0;
      _il_zr2:=0;
      ZD_RP.index('ZAMPOZ');
      ZD_RP.prefix(ZD_POZ.ref);
      {? ZD_RP.first()
      || {! |? _il_zre+=ZD_RP.IL_ZRE; ZD_RP.next() !}
      ?};
      _il_zr2:=_il_zre;
      {? BEER.JMZ & ZD_POZ.WS2<>1 & ZD_POZ.M().J2=null()
      || _il_zre:=_il_zre*ZD_POZ.WS2 $ ZD_POZ.M().DOKL
      ?};

      {? _modil & ZD_POZ.IL<_il_zre
      || _rozn:=_il_zre-ZD_POZ.IL;
         ZD_POZ.IL_POZ+=_rozn;
         ZD_POZ.IL+=_rozn;
         ZD_POZ.IL_KOR+=_rozn
      |? ZD_POZ.IL<_il_zre & ~_upilr
      || _il_zre:=ZD_POZ.IL
      ?};

      ZD_POZ.IL_ZRE:={? BEER.JMZ & ZD_POZ.WS2<>1 & _il_zr2=ZD_POZ.IL2
                     || ZD_POZ.IL
                     || _il_zre
                     ?};
      ZD_POZ.IL_POZ:=ZD_POZ.IL-ZD_POZ.IL_ZRE;
      {? ZD_POZ.IL_POZ<0 || ZD_POZ.IL_POZ:=0 ?};
      _ilpoz+=1;
      {? ZD_POZ.IL_ZRE<>0 | ZD_POZ.END
      || {? ZD_POZ.IL_POZ=0 | ZD_POZ.END || _zreal+=1 || _pozo+=1 ?}
      ?};

      {? (ZD_POZ.IL_POZ<=0 & ZD_POZ.IL>0) | ZD_POZ.ZD_NAG().STAN='M' | ZD_POZ.ZD_NAG().STAN='Q' | ZD_POZ.END
      || ZD_POZ.ZRE:='T'
      || ZD_POZ.ZRE:='N'
      ?};
      ZD_POZ.put();
      _cn:={? ZD_POZ.CENA=0 || 1 || ZD_POZ.CENA ?};
      _mianownik+=(_cn*ZD_POZ.IL)$2;
      _licznik+=(_cn*ZD_POZ.IL_ZRE)$2;
      ZD_POZ.next()
   !}
?};
ZD_POZ.cntx_pop();

:: aktualizacja znacznika zam.
ZD_NAG.cntx_psh();
ZD_NAG.prefix();
{? ZD_NAG.seek(_a) & ZD_NAG.STAN<>'M' & ZD_NAG.STAN<>'Q' & ZD_NAG.STAN<>'U'
||
   {? _pozo>0 | (_zreal<_ilpoz & _zreal>0 )
   || ZD_NAG.STAN:='C'
   |?   _zreal & ~_pozo || ZD_NAG.STAN:='Z'
   || ZD_NAG.STAN:=ZD_NAG.ST
   ?};
   ZD_NAG.PR:={? _mianownik>0 || (_licznik/_mianownik) || 0 ?}*100 $2;
   ZD_NAG.put()
?};
ZD_NAG.cntx_pop();
ZD_NAG.get();
echo();
''


\zdz_pd_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: przed wyswietleniem ZD_Z.PD
::  OLD: \zdz_pd_bd/zd.fml
::----------------------------------------------------------------------------------------------------------------------
ZD_Z.PD:='';
{? cur_tab(1,1)=ZD_NAG
|| exec('zdz_pd_bn_zdn','zamdst_wspolne')
|? cur_tab(1,1)=ZD_POZ & -menu_txt<>'dołącz'
|| PD_K.cntx_psh;
   PD_K.index('ZAM_REF1'); PD_K.prefix($ZD_POZ.ZD_NAG,$ZD_POZ.ref);
   {? PD_K.first
   || ZD_Z.PD:=$PD_K.PD_N().SYM+' - id pozcji koszyka:'+$PD_K.ID
   ?};
   PD_K.cntx_pop
?};
''


\zdz_pd_bn_zdn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.02]
:: OPIS: Na wyżwietl dla pola ZD_Z.PD w tabeli ZD_NAG
::----------------------------------------------------------------------------------------------------------------------
ZD_Z.PD:='';
PD_K.cntx_psh(); PD_K.index('ZAM_REF1'); PD_K.prefix($ZD_NAG.ref(),);
_pd_n:=null();
_comma:='';
_loop:=PD_K.first();
{!
|? _loop
|! {? _pd_n<>PD_K.PD_N
   || _txt:=_comma+PD_K.PD_N().SYM;
      ZD_Z.PD+=
         {? +(ZD_Z.PD+_txt)+3>100
         || '...'
         || _comma+PD_K.PD_N().SYM
         ?};
      _comma:=', ';
      _pd_n:=PD_K.PD_N
   ?};
   _loop:=PD_K.next()
!};
PD_K.cntx_pop()


\war_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: oblicza wartosc zamowienia dostaw
::   WE: _a - ZD_NAG.ref
::       [_b] - 1-z aktualizacją kontrahenta i miejsca dostawy 0(domyślnie)-nie
::       [_c] - 0/1 - wyznaczanie ceny
::   WY: wartosc zamowienia
::  OLD: \war_zam/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_akt:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_akt_cena:={? var_pres('_c')=type_of(0) || _c || 0 ?};

_dodo:=do_state()=0;

_war:=_warw:=0;
ZD_POZ.cntx_psh;
ZD_POZ.index('POZ'); ZD_POZ.prefix(_a);
{? ZD_POZ.first()
||
   {? _akt_cena || exec('st_licz_wz','ceny','ZD_NAG') ?};
   {!
   |? _war+=ZD_POZ.WAR;
      _warw+=ZD_POZ.WARW;
      _put:=0;
      {? _akt
      ||
         ZD_POZ.KH:=ZD_POZ.ZD_NAG().KH;
         ZD_POZ.KH_MSC:=ZD_POZ.ZD_NAG().KH_MSC;
         _put:=1
      ?};
      {? _akt_cena
      ||
         PD_K.M:=ZD_POZ.M;
::       KALK.JM, KALK.J2, KALK.WS2 - zmienne wykorzystywane w \ceny_mat/ceny_mat.fml
         KALK.JM:=KALK.J2:=ZD_POZ.M().J; KALK.WS2:=1;
         exec('stplicz','ceny',ZD_POZ.IL);
         {? ZD_NAG.WAL=INFO.NAROD
         ||
            PD_K.CENA:=0;
            {? _dodo || do() ?};
            exec('ceny_mat','ceny_mat',ZD_POZ.M,ZD_NAG.KH,'PD_K.CENA',0,,'','N',INFO.NAROD,'',,0,,,0,0,0);
            {? _dodo || end() ?};
            _put:=PD_K.CENA & ZD_POZ.CENA<>PD_K.CENA;
            {? _put || ZD_POZ.CENA:=PD_K.CENA ?}
         ||
            PD_K.CWAL:=0;
            {? _dodo || do() ?};
            exec('ceny_mat','ceny_mat',ZD_POZ.M,ZD_NAG.KH,'PD_K.CENA',0,,'','T',ZD_NAG.WAL,'PD_K.CWAL',,0,,,0,0,0);
            {? _dodo || end() ?};
            _put:=PD_K.CWAL & ZD_POZ.CWAL<>PD_K.CWAL;
            {? _put || ZD_POZ.CWAL:=PD_K.CWAL ?}
         ?};
         {? _put || exec('zezdp_ce','zamdst_poz') ?}
      ?};
      {? _put || ZD_POZ.put() || 1 ?}
         &
      ZD_POZ.next()
   !}
?};
ZD_POZ.cntx_pop;
ZD_NAG.WAR:=_war;
ZD_NAG.WARW:=_warw;
ZD_NAG.put


\magazyny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Magazyny do których można użytkownik może rejestrować zamówienia dostaw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
MG.f_clear();
MG.clear();
MG.f_set('SYM,NAZ','join USERS_UP','USERS_UP.USERS=:_a and USERS_UP.AKR=\'MG\' and \"MG\".ODDZ=\':_b\''
   ,OPERATOR.USER,ST.ODDZ)


\zwr_zapo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: zwraca typ zapotrzebowania na dany rok i oddzial
::   WE: _a - typ zamowienia
::       _b - rok_f
::       _c - odd
::   WY: ref zapotrzebowania lub null
::  OLD: \zwr_zapo/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
ETZAPZAM.cntx_psh();
ETZAPZAM.index('TZAM');
ETZAPZAM.prefix(_a,_b,_c);
_wyn:={? ETZAPZAM.first || ETZAPZAM.ref() || null ?};
ETZAPZAM.cntx_pop();
_wyn


\zdnagedd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: usuniecie powiazania z zapotrzebowaniem
::   WE: [_a] - 0(domyslnie)-zam.dostaw 1-zam.wewnetrzne
::   WY: 1-udalo sie usunac 0-nie udalo
::  OLD: \zdnagedd/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
_wyn:=0;
_ezapot:={? ~_a || ZD_NAG.EZAPOT || ZK_N.EZAPOT ?};
_kod:=form(8+_ezapot)+2;
EDOKUM.cntx_psh(); EDOKUM.use('skid_v'+_kod); EDOKUM.index('ID'); EDOKUM.prefix();
EPODZ.cntx_psh(); EPODZ.use('skid_j'+_kod);
EDOKOS.cntx_psh(); EDOKOS.use('skid_y'+_kod);
EDOKPAR.cntx_psh(); EDOKPAR.use('skidh_'+_kod);
EDOKGR.cntx_psh(); EDOKGR.use('skid_c'+_kod);
EDOKLOG.cntx_psh(); EDOKLOG.use('skid_d'+_kod);
EDOKUMZ.cntx_psh(); EDOKUMZ.use('skid_n'+_kod);
{? EDOKUM.seek(_ezapot,form(8+_ezapot))
|| EDOKOS.index('SZUK12'); EDOKOS.prefix(EDOKUM.ref(),'W','T');
   {? EDOKOS.first()
   || FUN.info('Zapotrzebowanie zaakceptowane.'@+'\n'+
               'Niemożliwe usunięcie zapotrzebowania.'@+'\n'+
               'Niemożliwa zmiana statusu zamówienia.'@)
   || do();
      EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
      {? EPODZ.first() || {! |? EPODZ.del() !} ?};
      EDOKOS.index('EDOKUM'); EDOKOS.prefix(EDOKUM.ref());
      {? EDOKOS.first() || {! |? EDOKOS.del() !} ?};
      EDOKGR.index('UNIK'); EDOKGR.prefix(EDOKUM.ref());
      {? EDOKGR.first() || {! |? EDOKGR.del() !} ?};
      EDOKLOG.index('DISP'); EDOKLOG.prefix(EDOKUM.ref());
      {? EDOKLOG.first() || {! |? EDOKLOG.del() !} ?};
      EDOKUMZ.index('DISP'); EDOKUMZ.prefix(EDOKUM.ref());
      {? EDOKUMZ.first() || {! |? EDOKUMZ.del() !} ?};
      EDOKPAR.index('UNIK'); EDOKPAR.prefix(EDOKUM.ref());
      {? EDOKPAR.first() || {! |? EDOKPAR.del !} ?};
      _wyn:=EDOKUM.del(,1);
      {? _wyn
      || {? ~_a
         || ZD_NAG.EZAPOT:=''; _wyn:=ZD_NAG.put
         || ZK_N.EZAPOT:='';
            _wyn:=ZK_N.put
         ?};
         {? ~_wyn || undo() ?}
      || FUN.info('Nie udało się usunąć zapotrzebowania.'@+'\n'+
                  'Niemożliwa zmiana statusu zamówienia.'@);
         undo()
      ?};
      end()
   ?}
|| FUN.info('Nie znaleziono powiązanego zapotrzebowania.'@+'\n'+
            'Niemożliwa zmiana statusu zamówienia.'@)
?};
EDOKUM.cntx_pop(); EPODZ.cntx_pop(); EDOKOS.cntx_pop(); EDOKGR.cntx_pop(); EDOKLOG.cntx_pop(); EDOKUMZ.cntx_pop();
EDOKPAR.cntx_pop();
_wyn


\termin_o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.53]
:: OPIS: Najpóźniejszy termin realizacji zamówień od dostawcy
::   WE: _a --- M.ref() --- zawsze na konkretny materiał
::       _b --- MAG.KOD --- jeżeli pusty tekst, to wszystkie magazyny
::   WY: _wyn - date()
::  OLD: \termin_o/zamowien.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=date(0,0,0);
ZD_POZ.cntx_psh();
MG.cntx_psh();
MG.index('MAG');
MG.prefix(ST.ODDZ,_b);
{? MG.first()
|| {!
   |? ZD_POZ.index('ZRE');
      ZD_POZ.prefix(_a,MG.ref(),'N');
      {? ZD_POZ.first()
      || {!
         |? {? ZD_POZ.ZD_NAG().STAN='A' | ZD_POZ.ZD_NAG().STAN='C'
            || {? _wyn<ZD_POZ.ZD_NAG().DTPREAL || _wyn:=ZD_POZ.ZD_NAG().DTPREAL ?}
            ?};
            ZD_POZ.next()
         !}
      ?};
      MG.next()
   !}
?};
MG.cntx_pop();
ZD_POZ.cntx_pop();
_wyn


\term_dost
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.53]
:: OPIS: Wylicza czas dostawy partii towaru
::   WE: _a --- M.ref() --- zawsze na konkretny towar
::       _b --- ilość potrzebna.
::   WY: _wyn - date()
::  OLD: \term_dost/zamowien.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=date(0,0,0);
M.cntx_psh();
M.clear();
{? M.seek(_a)
|| _tab:=tab_tmp(3,'DATA','DATE',''
          ,'ZRE','STRING[1]',''
          ,'STAN','STRING[1]',''
          ,'ILOSC','REAL','');
   exec('openzd_psh','open_tab');
   ZD_POZ.index('ZRE');
   ZD_POZ.prefix(_a);
   {? ZD_POZ.find_tab('first','ZD_NAG','STAN','<>','Z','ZD_NAG','STAN','<>','M')
   || {!
      |? _ilosc:=ZD_POZ.IL_POZ;
         _zre:=ZD_POZ.ZD_NAG().ZRE;
         _stan:=ZD_POZ.ZD_NAG().STAN;
         _dtrea:=ZD_POZ.ZD_NAG().DTPREAL;
         ZDP_POZ.index('ZD_POZ');
         ZDP_POZ.prefix($ZD_POZ.ref(),);
         {? ZDP_POZ.first()
         || {!
            |? _il_ptw:=ZDP_POZ.IL-exec('obl_rea','zamdst_rea',$ZDP_POZ.ref());
               {? _il_ptw>0
               || _drea:={? ZDP_POZ.ZDP_NAG().D_REA<>date(0,0,0)
                         || ZDP_POZ.ZDP_NAG().D_REA
                         || _dtrea
                         ?};
                  _tab.clear();
                  {? ~_tab.find_key(_drea,_zre,_stan)
                  || _tab.blank();
                     _tab.ZRE:=_zre;
                     _tab.STAN:=_stan;
                     _tab.DATA:=_drea;
                     _tab.ILOSC:=_il_ptw;
                     _tab.add(1)
                  || _tab.ILOSC+=_il_ptw;
                     _tab.put(1)
                  ?};
                  _ilosc-=_il_ptw
               ?};
               ZDP_POZ.next()
            !}
         ?};
         {? _ilosc>0
         || _tab.clear();
            {? ~_tab.find_key(_dtrea,_zre,_stan)
            || _tab.blank();
               _tab.ZRE:=_zre;
               _tab.STAN:=_stan;
               _tab.DATA:=_dtrea;
               _tab.ILOSC:=_ilosc;
               _tab.add(1)
            || _tab.ILOSC+=_ilosc;
               _tab.put(1)
            ?}
         ?};
         ZD_POZ.find_tab('next','ZD_NAG','STAN','<>','Z','ZD_NAG','STAN','<>','M')
      !}
   ?};
   exec('openzd_pop','open_tab');
   _il:=0;
   _ok:=1;
   _tab.prefix();
   _tab.first();
   {!
   |? {? _tab.DATA<>date(0,0,0)
      || _il+=_tab.ILOSC;
         {? _il>=_b || _ok:=0;_wyn:=_tab.DATA ?}
      ?};
      _tab.next() & _ok
   !};
   {? _wyn=date(0,0,0) || _tab.last(); _wyn:=_tab.DATA ?};
   obj_del(_tab)
?};
M.cntx_pop();
_wyn


\zdn_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zamówienie dostaw - Utwórz PDF
::   WE: [_a] - akcja
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || _a:='Area Utwórz PDF' ?};

_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='LZK_ZDS_WPDF';
_params.UIDREF:=ZD_NAG.uidref();
_params.AKCJA:=_a;
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'ZD_NAG',ZD_NAG.ref());

exec('mp_run','#b__box',_params)


\zdn_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [18.22]
:: OPIS: podglad opisu dodatkowego dla zamowien dostaw
::----------------------------------------------------------------------------------------------------------------------
ZD_NAG.memo_get(,'UW');
ZD_NAG.memo_vie(,'UW');
1


\biq_zdn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.14]
:: OPIS: Analiza BI w obszarze "Zamówienia dostaw"
::----------------------------------------------------------------------------------------------------------------------
exec('np_run','#b__box','BIQ_LZK_ZDSA')

:Sign Version 2.0 jowisz:1045 2023/10/10 10:06:21 697e6c091f981a9d09dcdd5227a2bc489f3a81f54fa7eb203a0ad81e94ddead20a1417edd94a243f2f447b85cf018665058abc3b5db2420574cc787f9c72372ceb276c7ff7e5ac26404ffc29006445cca71a6adee3851be8e9c0b6e349e0c1332d58cfb07f5652ed3c4abdb823d2985bc2dfe208258f6a32bb3ffdd2250090a2

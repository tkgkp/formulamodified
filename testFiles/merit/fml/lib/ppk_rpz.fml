:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_rpz.fml [12.51]
:: Utworzony: 2019/05/27
:: Systemy: PPK
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę załączników plików raportów.
::======================================================================================================================


\_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Wyzwalacz "Dołącz - przed" dla tabeli PPK_RPZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_rpz') & exec('_chk','ppk_rpz',0)


\_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Wyzwalacz "Popraw - przed" dla tabeli PPK_RPZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_rpz') & exec('_chk','ppk_rpz',1)


\_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Przed modyfikacją rekordu.
::       Formuła wywoływana z wyzwalaczy "Dołącz - przed" i "Popraw - przed" dla tabeli RPZ.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? PPK_RPZ.TYP='xml'
|| PPK_RPZ.PPK_RTP:=PPK_RPZ.PPK_XEL:=null()

|? PPK_RPZ.PPK_RTP
|| PPK_RTP.cntx_psh();
   PPK_RPZ.PPK_RPL:=PPK_RPZ.PPK_RTP().PPK_RPL;
   PPK_RPZ.PPK_XEL:=PPK_RPZ.PPK_RTP().PPK_XEL;
   PPK_RTP.cntx_pop()

|| PPK_RPZ.PPK_XEL:=null()
?};
1


\_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli PPK_RPZ.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
__CHK.validate(PPK_RPZ,
:: Unikalność indeksu
   $("_a.index(_b,"+$_put+")=''"),
:: Pola nieredagowalne.
   "_a.record(_b,,'PPK_RPL')",
   "_a.in_set(PPK_RPZ,'TYP',,'xml','csv')",
   "  _ret:=0;
      {? PPK_RPZ.TYP='csv' & PPK_RPZ.PPK_RTP=null()
      || _a.err_msg('Dla typu ""csv"" pole ""PPK_RTP"" musi być wypełnione.')
      || _ret:=1
      ?};
      _ret
   "
)


\update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Formuła aktualizuje rekord z załącznikiem.
::   WE: [_a] [REFERENCE] - Wskazanie rekordu nagłówka (PPK_RPL). [domyślnie: PPK_RPL.ref()]
::       [_b] [STRING]    - Typ załącznika (rzeczywiste rozszerzenie pliku). [domyślnie: 'xml']
::       [_c] [REFERENCE] - Wskazanie rodzaj raportu (PPK_RTP) - parametr wymagany dla _b='csv'. [domyślnie: null()].
::       [_d] [STRING]    - Pełna ścieżka dostępu do pliku lokalnego (@), który ma być załączony. Jeżeli nie podano,
::                          to plik nie będzie załączony - rekord załącznika może być usunięty.
::       [_e] [STRING]    - Nazwa pod jaką plik ma zostać załączony. Parametr wymagany jeżeli podano _c.
::   WY: Wynik operacji [0/1].
::----------------------------------------------------------------------------------------------------------------------
_rpl:={? var_pres('_a')=type_of(null()) & ref_tab(_a)=PPK_RPL || _a || PPK_RPL.ref() ?};
_typ:={? var_pres('_b')=type_of('') & (_b='xml' | _b='csv') || _b || 'xml' ?};
{? var_pres('_c')=type_of(null()) & ref_tab(_c)=PPK_RTP
|| _rtp:=_c
|? _typ='xml'
|| _rtp:=null()
|| return(0)
?};
_fn:={? var_pres('_d')=type_of('') & fexists(_d,0) || _d || ~~ ?};
_name:={? var_pres('_e')=type_of('') & +_e || _e || 'plik.'+_typ ?};

_ret:=0;
PPK_RPZ.cntx_psh();
PPK_RPZ.index('RODZAJ');
PPK_RPZ.prefix(_rpl,_rtp);
{? PPK_RPZ.first()
|| _ret:={? _fn=~~ || PPK_RPZ.del(0,1) || PPK_RPZ.bl_put('ZAL',_fn,,,_name) ?}
|? _fn<>~~
|| PPK_RPZ.blank();
   PPK_RPZ.PPK_RPL:=_rpl;
   PPK_RPZ.TYP:=_typ;
   PPK_RPZ.PPK_RTP:=_rtp;
   _ret:=PPK_RPZ.add() & PPK_RPZ.bl_put('ZAL',_fn,,,_name)
?};
PPK_RPZ.cntx_pop();
_ret


\otworz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Otwórz" dla pliku wymiany danych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? PPK_RPZ.ZAL
|| exec('bl_view','#blob',PPK_RPZ,'ZAL')
|| FUN.info('Pliku wymiany danych nie znaleziono.'@)
?}


\zapisz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Obsługa akcji "Zapisz" dla pliku wymiany danych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? PPK_RPZ.ZAL=null()
|| FUN.info('Pliku wymiany danych nie znaleziono.'@);
   return()
?};

exec('bl_save','#blob',PPK_RPZ,'ZAL')


:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:22 18950cca4c32000249f6232cc400941583e2b09083498bbf39abf002d319ecae9ae423d65d6001ed4b4065d70684faaea84b95751cab05a5b83b9dc3f98955c97d8ad00c70495e7e8076963c286419f475fc698c9f3c66ea2bf8997038b0e312a3f456b7ceb2d311556dfd371e6841102a8070051ab43335a37742c3ecad611c

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_rap.fml [12.51]
:: Utworzony: 2019/05/09
:: Autor: jaws
:: Systemy: PPK
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę kartoteki raportów uczestnika.
::======================================================================================================================


\_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed dołączeniem lub poprawieniem wiersza tabeli PPK_RAP.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
PPK_RAP.OSOBA:=PPK_RAP.PPK_RUC().OSOBA;
PPK_RAP.PPK_XEL:=PPK_RAP.PPK_RTP().PPK_XEL;
1


\_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli PPK_RAP.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_rap')


\_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli PPK_RAP.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_rap')


\_adda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz po dołączeniu wiersza tabeli PPK_RAP.
::   WE:
::   WY: zgodny ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
:: podstawowy warunek dalszego działania
{? ~_a | do_state()<>1 || return() ?};

exec('akt_ir','ppk_ruc',PPK_RAP.PPK_RUC);
exec('akt_ir','ppk_rtp',PPK_RAP.PPK_RTP);
~~


\_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [19.22]
:: OPIS: Wyzwalacz "Usuń - przed" dla tabeli PPK_RAP.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PPK_RPL.cntx_psh();
PPK_RUC.cntx_psh();
_ok:=(PPK_RAP.PPK_RUC().PPK_RPL().WYS_STAT<>'T');
PPK_RUC.cntx_pop();
PPK_RPL.cntx_pop();

{? _ok=0
:: wysłano
|| return(0)
?};

:: Usuwamy wszystkie załączniki związane z bieżącym plikiem wymiany danych.
PPK_RDA.cntx_psh();
_ret:=exec('del_ndx','#table',PPK_RPZ,'RODZAJ',PPK_RAP.PPK_RDA().PPK_RPL);
PPK_RDA.cntx_pop();
_ret


\_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz po usunięciu wiersza tabeli PPK_RAP.
::   WE:
::   WY: zgodny ze specyfikacją narzędzi
::----------------------------------------------------------------------------------------------------------------------
:: podstawowy warunek dalszego działania
{? ~_a | do_state()<>1 || return() ?};

exec('usun','ppk_rda',bfld('PPK_RDA'));

exec('akt_ir','ppk_ruc',bfld('PPK_RUC'));
exec('akt_ir','ppk_rtp',bfld('PPK_RTP'));
~~


\zaw_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Obsługa akcji "Zawartość" okienek tabeli PPK_RAP.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('zaw_a','ppk_rda',PPK_RAP.PPK_RUC().PPK_RPL,#PPK_RAP.PPK_RDA);
~~


\szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyszukuje informacje o raporcie.
::   WE: _a _PPK_RUC - uczestnik uwzględniony w pliku
::       _b _PPK_RTP - typ raportu występujący w pliku
::   WY: wskazanie istniejącego zapisu lub null
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_ruc:=_a;
_rtp:=_b;

PPK_RAP.index('UNIQUE');
PPK_RAP.prefix(_ruc);
{? PPK_RAP.find_key(_rtp)
|| PPK_RAP.ref()
|| null
?}


\dodaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Dodaje informacje o raporcie.
::   WE: _a _PPK_RUC - uczestnik uwzględniony w pliku
::       _b _PPK_RTP - typ raportu występujący w pliku
::       _c _PPK_RDA - dane raportu
::   WY: wskazanie dodanego/istniejącego zapisu lub null
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_ruc:=_a;
_rtp:=_b;
_rda:=_c;

{? _ruc=null | _rtp=null | _rda=null
:: zakończ działanie
|| return(null)
?};

:: sprawdź, czy element już istnieje
_ref:=exec('szukaj','ppk_rap',_ruc,_rtp);
{? _ref<>null
:: znaleziono
|| {? PPK_RAP.PPK_RDA<>_rda
   || PPK_RAP.PPK_RDA:=_rda;
      PPK_RAP.put()
   ?};
   return(_ref)
?};

PPK_RAP.blank();
PPK_RAP.PPK_RUC:=_ruc;
PPK_RAP.PPK_RTP:=_rtp;
PPK_RAP.PPK_RDA:=_rda;
{? PPK_RAP.add()
|| _ref:=PPK_RAP.ref()
?};

_ref

:Sign Version 2.0 jowisz:1028 2019/10/14 09:21:32 602a4d5ae63c22f6ecd90aba99e8d0ae32df1c9a1be329f4050715c1bb6e7d6def7c2102dd6153d092851cb7ed01eace7d11dbd64d5aa2038fd084180153b0cb0e12a0c5036c5c1f865cb563b6cf6ec52be179e242ac7e95ff7ead957193c896d91e41247a96140b4911b273d1c36b39a94c787fc81e819dd04b00ba57bff63c

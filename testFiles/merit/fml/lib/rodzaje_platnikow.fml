:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: rodzaje_platnikow.fml
:: Utworzony: 08.12.2022
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły obsługi rodzajów płatników (OS_GRPRZ, OS_GRRU)
::======================================================================================================================

\grupy_przychodow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Redakcja powiązanych z rekordem rodzajów płatników
::   WE: _a [STRING] - akronim tabeli, do której mają być przywiązywane (OS_ZWPOD, OS_ZWZAL)
::      [_b][INTEGER] - 0/1 Czy z pytaniem początkowym? domyślnie: 0 -bez pytania
::      [_c][INTEGER] - 1[domyślnie] - redakcja, 0 - tylko podgląd
::   WY:
::  OLD: \grupy_przychodow/kali.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') | ~(',OS_ZWPOD,OS_ZWZAL,'*_a)
|| FUN.error('Błąd parametru wejściowego'@);
   return()
?};
_pyt:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_redakcja:={? var_pres('_c')=type_of(0) || _c || 1 ?};
{? ~_pyt |
   FUN.ask('Czy określić dodatkowo rodzaj płatnika dla jakiego zapis ma obowiązywać?\n'
           'Jeżeli nie zostanie wprowadzony rodzaj płatnika, dokument obowiązuje\n'
           'dla wszystkich rodzajów płatnika (pracownik i zleceniobiorca).'@)
|| _akr:=_a;
   _TAB:=($_akr)();
   {? (_TAB=OS_ZWPOD & _TAB.REZYGN='T') | (_TAB=OS_ZWZAL & _TAB.WYCOF='T')
   || FUN.info('Funkcja niedostępna dla wycofań.'@);
      return()
   |? _TAB=OS_ZWZAL & _TAB.D_OB<date(2023,1,1) &
      ~FUN.ask('Wybrano redagowanie rodzajów płatników przed wejściem w życie ustawy (%1).\n'
               'Kontynuować?'@[$date(2023,1,1)])
   || return()
   ?};
   OS_GRPRZ.cntx_psh();
   OS_GRPRZ.win_sel('WER');
   {? ~_redakcja
   || OS_GRPRZ.actions('WER','DPU:D')
   ?};
   OS_GRPRZ.index(_akr);
   OS_GRPRZ.prefix(_TAB.ref());
   OS_GRPRZ.select();
   OS_GRPRZ.cntx_pop()
?};
~~


\os_grprz_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed akcją "Dołącz" tabeli OS_GRPRZ
::   WE:
::   WY: 0/1 powodzenie operacji
::  OLD: \os_grprz_dolacz/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_index:=OS_GRPRZ.index('?');
{? ~(',OS_ZWPOD,OS_ZWZAL,'*_index)
|| FUN.error('Błąd wykorzystania funkcji'@);
   return(_ret)
?};
_pole:=_index;
_TAB:=($_index)();
EDIT_VAR.cntx_psh();
SLO_TYP.cntx_psh();
SLO_KOD.cntx_psh();
SLO_KOD.win_sel('WER');
SLO_KOD.hdr_sel(' - Rodzaje płatników'@);
SLO_KOD.prefix();
SLO_KOD.f_set(,,
   'SLO_KOD.SLO_TYP=:_a and SLO_KOD.KOD not in
   (select SLO_KOD.KOD from OS_GRPRZ join SLO_KOD using(OS_GRPRZ.SLO_KOD, SLO_KOD.REFERENCE) where OS_GRPRZ.:_b=:_c)',
   exec('slo_typ','ext_slo','RODZ_PL'),_pole,_TAB.ref());
{? SLO_KOD.select()
|| OS_GRPRZ.cntx_psh();
   OS_GRPRZ.blank();
   OS_GRPRZ.SLO_KOD:=SLO_KOD.ref();
   ($('OS_GRPRZ.%1'[_pole]))():=_TAB.ref();
   _ret:=OS_GRPRZ.add();
   OS_GRPRZ.cntx_pop()
?};
SLO_KOD.f_clear();
SLO_KOD.cntx_pop();
SLO_TYP.cntx_pop();
EDIT_VAR.cntx_pop();
_ret


\os_grprz_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed akcją "Popraw" tabeli OS_GRPRZ
::   WE:
::   WY:
::  OLD: \os_grprz_popraw/kali.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('os_grprz_dolacz','rodzaje_platnikow')
|| OS_GRPRZ.del()
?};
~~


\grupy_umow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Redakcja powiązanych z rekordem rodzajów płatników
::   WE: [_a][INTEGER] - 1[domyślnie] - redakcja, 0 - tylko podgląd
::   WY:
::  OLD: \grupy_umow/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_redakcja:={? var_pres('_a')=type_of(0) || _a || 1 ?};
OS_GRRU.cntx_psh();
OS_GRRU.win_sel('WER');
{? ~_redakcja
|| OS_GRRU.actions('WER','DPU:D')
?};
OS_GRRU.index('RU');
OS_GRRU.prefix(RU.ref());
OS_GRRU.select();
OS_GRRU.cntx_pop();
~~


\os_grru_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed akcją "Dołącz" tabeli OS_GRRU
::   WE: [_a][INTEGER] - 0/1 Czy poprawianie? domyślnie: 0-dołączanie
::   WY:
::  OLD: \os_grru_dolacz/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_popraw:={? var_pres('_a')=type_of(0) || _a || 0 ?};
EDIT_VAR.cntx_psh();
SLO_TYP.cntx_psh();
SLO_KOD.cntx_psh();
SLO_KOD.win_sel('WER');
SLO_KOD.hdr_sel(' - Grupa umów'@);
SLO_KOD.prefix();
SLO_KOD.f_set(,,
   'SLO_KOD.SLO_TYP=:_a and SLO_KOD.KOD not in
   (select SLO_KOD.KOD from OS_GRRU join SLO_KOD using(OS_GRRU.SLO_KOD, SLO_KOD.REFERENCE) where OS_GRRU.RU=:_b)',
   exec('slo_typ','ext_slo','RODZ_PL'),RU.ref());
{? SLO_KOD.select()
|| OS_GRRU.cntx_psh();
   _add:=1;
   {? ~_popraw & SLO_KOD.SYSTEM='T'
   || OS_GRRU.index('RUSYS');
      OS_GRRU.prefix(RU.ref(),'T',);
      {? OS_GRRU.first()
      || FUN.emsg('Dla typu umowy nie może istnieć więcej niż jeden systemowy rodzaj przychodu.'@);
         _add:=0
      ?}
   ?};
   {? _add
   || OS_GRRU.prefix();
      OS_GRRU.blank();
      OS_GRRU.SLO_KOD:=SLO_KOD.ref();
      OS_GRRU.RU:=RU.ref();
      _ret:=OS_GRRU.add()
   ?};
   OS_GRRU.cntx_pop()
?};
SLO_KOD.f_clear();
SLO_KOD.cntx_pop();
SLO_TYP.cntx_pop();
EDIT_VAR.cntx_pop();
_ret


\os_grru_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed akcją "Popraw" tabeli OS_GRRU
::   WE:
::   WY:
::  OLD: \os_grru_popraw/kali.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('os_grru_dolacz','rodzaje_platnikow',1)
|| OS_GRRU.del()
?};
~~


\edit_var_rp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Przed wyświetleniem pola RP tabeli EDIT_VAR (Czy posiada powiązane rodzaje płatników? [T/N])
::   WY: ~~
::  OLD: \edit_var_rp_bd/kali.fml
::----------------------------------------------------------------------------------------------------------------------
_TAB:=cur_tab(1,1);
{? (_TAB=RU & +RU.K) |
   (_TAB=OS_ZWPOD & OS_ZWPOD.OS_ZWSLO & OS_ZWPOD.REZYGN<>'T') |
   (_TAB=OS_ZWZAL & OS_ZWZAL.OS_ZWSLO & OS_ZWZAL.WYCOF<>'T')
|| _TABakr:=exec('tab_acr','#table',_TAB);
   _RPTAB:={? _TAB=RU || OS_GRRU || OS_GRPRZ ?};
   _RPTAB.cntx_psh();
   _RPTAB.index(_TABakr);
   _RPTAB.prefix(_TAB.ref());
   EDIT_VAR.RP:={? _RPTAB.first() || 'T' || 'N' ?};
   _RPTAB.cntx_pop();
   obj_del(_RPTAB)
|| EDIT_VAR.RP:=''
?};
obj_del(_TAB);
~~


\RU_platnik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.51]
:: OPIS: Znajduje płatnika dla RU.
::   WE: _a [REFERENCE] - RU.ref
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OS_GRRU.cntx_psh();
OS_GRRU.index('RU');
OS_GRRU.prefix(_a);
_platnik:='UM_ZLEC';
{? OS_GRRU.first()
|| {!
   |? {? OS_GRRU.SLO_KOD().SYSTEM='T'
      || _platnik:=OS_GRRU.SLO_KOD().KOD
      ?};
      _platnik='' & OS_GRRU.next()
   !}
?};
OS_GRRU.cntx_pop();
_platnik


\GRRPRZ_platnik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.51]
:: OPIS: Znajduje płatnika dla OS_ZWPOD, OS_ZWZAL
::   WE: _a [STRING] - akronim tabeli do wyszukania
::       _b [REFERENCE] - ref dla typu płatnika
::       _c [STRING] - kod płatnika
::   WY: 1 - przypisany kod płatnika lub domyślnie bez przypisania
:        0 - brak przypisanego płatnika
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
_ref:=_b;
_platnik:=c;
_wynik:=1;
OS_GRPRZ.index(_typ);
OS_GRPRZ.prefix(_ref);
{? OS_GRPRZ.first()
|| _wynik:=OS_GRPRZ.find_key(_platnik)
?};
_wynik

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 e1dfc2fa5e90261614cf1260b8aecaef486336a3d42f99d4f7059be50f7474a0026f59fcf711fa30dc3fc82f14a232836d97d58e2d93d15ecc22f469177ed24a9bbd4c890c75a5caf5bf6397ce87828fb25c3e8944922dca095a513d36eda92a820e2414b3dab15a222e4ba258de741a199d471a26c9c19c1dbab618c2edb14c

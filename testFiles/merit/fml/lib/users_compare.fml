:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: users_compare.fml [23.25]
:: Utworzony: 20.01.2023
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi porównania użytkowników
::======================================================================================================================


\compare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Akcja porównaj okna CALL tabeli USERS
::----------------------------------------------------------------------------------------------------------------------
{? USERS.sel_size()<>2
|| FUN.info('Akcja dostępna dla dwóch zaznaczonych użytkowników.');
   return(0)
?}


\bg_compare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Przed akcją grupową porównaj okna CALL tabeli USERS
::----------------------------------------------------------------------------------------------------------------------
{? USERS.sel_size()<>2
|| FUN.info('Akcja dostępna dla dwóch zaznaczonych użytkowników.');
   return(0)
?};
_usr:=obj_new(2);
_sel:=USERS.sel_aget();
_obj:=exec('obj','users_compare',_sel);
_obj.select();
0


\ag_compare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Po akcją grupowej porównaj okna CALL tabeli USERS
::----------------------------------------------------------------------------------------------------------------------


\obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca obiekt używany do porównania danych użytkowników
::   WE: _a - tabela z zaznaczonymi użytkownikami
::----------------------------------------------------------------------------------------------------------------------
_sel:=_a;

_obj:=obj_new(
   'MSG',
   'msg','show_msg',

   'TAB','IND1','IND2','IND3',
   'KAT','TYP',
   'USR','EXIT',
   'create_win','fill_tab','add','fill_upr','set_usr',
   'upd_perm','apply',
   'select'
);
_obj.EXIT:=1;

_obj.MSG:=tab_tmp(1,
   'MSG','STRING[255]','Opis'
);
_obj.msg:="
   _msg:=_a;
   _tab:=.MSG;
   _tab.blank(1);
   _tab.MSG:=_msg;
   _tab.add()
";
_obj.show_msg:="
   _tab:=.MSG;
   {? _tab.first()
   || _wer:=_tab.mk_sel('Komunikaty','P',,'#idcomapreinfo',,,,,'U');
      _tab.win_fld(_wer,,'MSG',,,100);
      _tab.win_act(_wer,,'Kolejność');
      _tab.win_sel(_wer);
      _tab.select()
   ?}
";

_obj.TAB:=tab_tmp(3,
   'P1','STRING[100]','Poziom 1',
   'P2','STRING[100]','Poziom 2',
   'P3','STRING[100]','Poziom 3',
   'P4','STRING[255]','Poziom 4',
   'KOD','STRING[49]','Kod',
   'REF','STRING[48]','UIDREF',
   'SEL1','STRING[255]','SEL1',
   'SEL2','STRING[255]','SEL2',
   'OSTR','STRING[20]','Opis operacji',
   'OPERACJA','STRING[1]','Operacja',
   'ZMIANY','INTEGER','Zmiany',
   'KAT','INTEGER','Kategoria',
   'SEL','STRING[1]','Oznaczenie roli'
);
_obj.IND1:=_obj.TAB.index('?');
_obj.IND2:=_obj.TAB.ndx_tmp(,,'ZMIANY',,0,'KAT',,0);
_obj.IND3:=_obj.TAB.ndx_tmp(,,'KOD',,0);
_obj.TAB.trig_b('add',"
   _tab:=trig_info('table');
   _tab.OPERACJA:={? _tab.SEL1=_tab.SEL2 || '=' || '~' ?};
   1
");
_obj.TAB.trig_b('put',"
   _tab:=trig_info('table');
   {? _tab.OPERACJA='<'
   || _tab.OSTR:='< W lewo'
   |? _tab.OPERACJA='>'
   || _tab.OSTR:='W prawo >'
   || _tab.OSTR:=''
   ?};
   _tab.ZMIANY:='<>'*_tab.OPERACJA<>0;
   1
");
_obj.select:="
   _tab:=.TAB;
   _wer:=exec('create_win','users_compare',.);
   _tab.win_sel(_wer);
   params_set('obj',.);
   {? _tab.select()
   || .apply()
   ?}
";
_obj.add:="
   _tab:=.TAB;
   _p2:={? var_pres('_a')=type_of('') || _a || '' ?};
   _p3:={? var_pres('_b')=type_of('') || _b || '' ?};
   _p4:={? var_pres('_c')=type_of('') || _c || '' ?};
   _kod:=_d;
   _ref:={? var_pres('_e')=type_of('') || _e || '' ?};
   _sel1:={? var_pres('_f')=type_of(1)
          || {? _f || 'T' || 'N' ?}
          |? var_pres('_f')=type_of('')
          || _f
          || ''
          ?};
   _sel2:={? var_pres('_g')=type_of(1)
          || {? _g || 'T' || 'N' ?}
          |? var_pres('_g')=type_of('')
          || _g
          || ''
          ?};
   {? _sel1<>_sel2
   || _tab.blank(1);
      _tab.P1:=.TYP;
      _tab.P2:=_p2;
      _tab.P3:=_p3;
      _tab.P4:=_p4;
      _tab.KOD:=_kod;
      _tab.REF:=_ref;
      _tab.SEL1:=_sel1;
      _tab.SEL2:=_sel2;
      _tab.KAT:=.KAT;
      _tab.add()
   ?}
";
_obj.fill_tab:="
   _tab:=.TAB;
   _usr:=.USR;
   B_ROLE.cntx_psh();
   B_ROLE.index('UNIK');
   B_ROLE.prefix(REF.FIRMA);
   {? B_ROLE.first()
   || B_USRROL.cntx_psh();
      B_USRROL.index('UNIK');
      B_USRROL.prefix(REF.FIRMA);
      .KAT:=1;
      .TYP:='Rola';
      {!
      |? .add(
            B_ROLE.NAME,,,'R'+B_ROLE.uidref(),B_ROLE.uidref(),
            B_USRROL.find_key(B_ROLE.ref(),.USR[1].REF,null),
            B_USRROL.find_key(B_ROLE.ref(),.USR[2].REF,null)
         );
         B_ROLE.next()
      !};
      B_USRROL.cntx_pop()
   ?};
   B_ROLE.cntx_pop();

   B_PERM.cntx_psh();
   B_PERM.index('NAME');
   B_PERM.prefix();
   {? B_PERM.first()
   || B_PERM_U.cntx_psh();
      B_PERM_U.index('USER');
      B_PERM_U.prefix(REF.FIRMA);
      .KAT:=2;
      .TYP:='Uprawnienie do danych';
      {!
      |? .add(
            B_PERM.DESC,'Wszystkie','Wszystkie','U'+B_PERM.uidref(),B_PERM.uidref(),
            B_PERM_U.find_key(.USR[1].REF,B_PERM.ref()) & B_PERM_U.FULL='T',
            B_PERM_U.find_key(.USR[2].REF,B_PERM.ref()) & B_PERM_U.FULL='T'
         );
         exec('fill_compare','b_perm',.);
         B_PERM.next()
      !};
      B_PERM_U.cntx_pop()
   ?};
   B_PERM.cntx_pop();

   _prot:=exec('user_prot_obj','#b_role');
   {! _ii:=1..obj_len(.USR)
   |! _prot.update(.USR[_ii].REF)
   !};
   B_PROT.cntx_psh();
   B_PROT.index('UNIK');
   B_PROT.prefix();
   {? B_PROT.first()
   || _pt:=_prot.TAB;
      _pt.index(_prot.I_B_PROT);
      .KAT:=3;
      .TYP:='Grupa ochron';
      {!
      |? _pt.prefix(B_PROT.ref());
         .add(
            '',B_PROT.PROT_KEY,B_PROT.DESC,'G',B_PROT.uidref(),
            _pt.find_key(.USR[1].REF),
            _pt.find_key(.USR[2].REF)
         );
         B_PROT.next()
      !}
   ?};
   B_PROT.cntx_pop();

   FO_DEF.cntx_psh();
   FO_DEF.index('ALL');
   FO_DEF.prefix('U');
   {? FO_DEF.first()
   || FO_USR.cntx_psh();
      FO_USR.index('TAB');
      FO_USR.prefix();
      .KAT:=4;
      .TYP:='Uniwersalny parametr';
      {!
      |? .add(
            FO_DEF.TAB,$FO_DEF.NR,FO_DEF.NAME,'P',FO_DEF.uidref(),
            {? FO_USR.find_key(.USR[1].REF,FO_DEF.TAB,FO_DEF.NR)
            || FO_USR.VALUE
            || FO_DEF.VALUE
            ?},
            {? FO_USR.find_key(.USR[2].REF,FO_DEF.TAB,FO_DEF.NR)
            || FO_USR.VALUE
            || FO_DEF.VALUE
            ?}
         );
         FO_DEF.next()
      !};
      FO_USR.cntx_pop()
   ?};
   FO_DEF.cntx_pop();

   _tab.first();
   ~~
";
_obj.upd_perm:="
   _tab:=.TAB;
   _tab.cntx_psh();
   {? _tab.first()
   || {!
      |? _tab.SEL:='N';
         _tab.put();
         _tab.next()
      !}
   ?};
   _tab.index(.IND2);
   _tab.prefix(1,1);
   {? _tab.first()
   || B_ROLE.cntx_psh();
      B_ROLE.prefix();
      B_ACTROL.cntx_psh();
      B_ACTROL.index('UNIK');
      B_PERM_A.cntx_psh();
      B_PERM_A.index('B_ACTION');
      B_PERM.cntx_psh();
      B_PERM.prefix();
      B_PERM_U.cntx_psh();
      B_PERM_U.index('USER');
      B_PERM_U.prefix(REF.FIRMA);
      {!
      |? {? _tab.OPERACJA='>'
         || _usr:=.USR[2].REF;
            _add:=_tab.SEL2='N'
         || _usr:=.USR[1].REF;
            _add:=_tab.SEL1='N'
         ?};
         {? _add & B_ROLE.seek(_tab.REF)
         || B_ACTROL.prefix(REF.FIRMA,B_ROLE.ref());
            {? B_ACTROL.first()
            || {!
               |? B_PERM_A.prefix(B_ACTROL.B_ACTION);
                  {? B_PERM_A.first()
                  || {!
                     |? {? B_PERM.seek(B_PERM_A.B_PERM)
                        || {? ~B_PERM_U.find_key(_usr,B_PERM.ref())
                           || _tab.cntx_psh();
                              _tab.index(.IND3);
                              _tab.prefix('U'+B_PERM.uidref());
                              {? _tab.first()
                              || {!
                                 |? _tab.SEL:='T'; _tab.put();
                                    _tab.next()
                                 !}
                              ?};
                              _tab.cntx_pop()
                           ?}
                        ?};
                        B_PERM_A.next()
                     !}
                  ?};
                  B_ACTROL.next()
               !}
            ?}
         ?};
         _tab.next()
      !};
      B_PERM_U.cntx_pop();
      B_PERM.cntx_pop();
      B_PERM_A.cntx_pop();
      B_ACTROL.cntx_pop();
      B_ROLE.cntx_pop()
   ?};
   _tab.prefix(1,2);
   {? _tab.first()
   || B_PERM.cntx_psh();
      B_PERM.prefix();
      B_PERM_U.cntx_psh();
      B_PERM_U.index('USER');
      B_PERM_U.prefix(REF.FIRMA);
      B_PERM_A.cntx_psh();
      B_PERM_A.index('B_PERM');
      B_ACTROL.cntx_psh();
      B_ACTROL.index('ACTION');
      B_ROLE.cntx_psh();
      B_ROLE.prefix();
      {!
      |? {? _tab.OPERACJA='>'
         || _usr:=.USR[2].REF;
            _add:=_tab.SEL2='N'
         || _usr:=.USR[1].REF;
            _add:=_tab.SEL1='N'
         ?};
         {? _add
         || {? B_PERM.seek(1-_tab.KOD) & ~B_PERM_U.find_key(_usr,B_PERM.ref())
            || B_PERM_A.prefix(B_PERM.ref());
               {? B_PERM_A.first()
               || {!
                  |? B_ACTROL.prefix(B_PERM_A.B_ACTION);
                     {? B_ACTROL.first()
                     || {!
                        |? {? B_ROLE.seek(B_ACTROL.B_ROLE)
                           || _tab.cntx_psh();
                              _tab.index(.IND3);
                              _tab.prefix('R'+B_ROLE.uidref());
                              {? _tab.first()
                              || _tab.SEL:='T';
                                 _tab.put()
                              ?};
                              _tab.cntx_pop()
                           ?};
                           B_ACTROL.next()
                        !}
                     ?};
                     B_PERM_A.next()
                  !}
               ?}
            ?}
         ?};
         _tab.next()
      !};
      B_ROLE.cntx_pop();
      B_ACTROL.cntx_pop();
      B_PERM_A.cntx_pop();
      B_PERM_U.cntx_pop();
      B_PERM.cntx_pop()
   ?};
   _tab.cntx_pop()
";
_obj.apply:="
   _tab:=.TAB;
   _tab.cntx_psh();
   _tab.index(.IND2);
   _tab.prefix(1);
   {? _tab.first()
   || PROGRESS.set(_tab.size(),'Kopiowanie uprawnień ...'@,FUN.TYT);
      {!
      |? {? 'RU'*(1+_tab.KOD)
         || {? _tab.OPERACJA='>'
            || _usr:=.USR[2].REF;
               _usr_naz:=.USR[2].NAME;
               _add:=_tab.SEL2='N'
            || _usr:=.USR[1].REF;
               _usr_naz:=.USR[1].NAME;
               _add:=_tab.SEL1='N'
            ?}
         ?};
         {? 1+_tab.KOD='R'
         || {? var_pres('_buffer')<=0
            || _buffer:=exec('buffer','#b_usrrol');
               _buffer.FIRMA:=REF.FIRMA
            ?};
            B_ROLE.cntx_psh();
            B_ROLE.prefix();
            {? B_ROLE.seek(_tab.REF)
            || B_USRROL.cntx_psh();
               B_USRROL.index('UNIK');
               B_USRROL.prefix(REF.FIRMA,B_ROLE.ref(),_usr,null);
               _jest:=B_USRROL.first();
               {? _add
               || {? ~_jest
                  || _buffer.USERS:=_usr;
                     _buffer.B_ROLE:=B_ROLE.ref();
                     exec('add','#b_usrrol',_buffer,,0)
                  ?}
               || {? _jest
                  || exec('delete','#b_usrrol',B_USRROL.ref(),0)
                  ?}
               ?};
               B_USRROL.cntx_pop()
            ?};
            B_ROLE.cntx_pop()
         |? 1+_tab.KOD='U'
         || B_PERM.cntx_psh();
            B_PERM.prefix();
            {? B_PERM.seek(1-_tab.KOD)
            || _pow:=ref_tab(_tab.REF);
               {? ~exec('set','b_perm',B_PERM.ref(),_usr,_add,{? _pow=B_PERM || ~~ || _tab.REF ?})
               || .msg('Nie udało się nadać uprawnień użytkownikowi: %1 do %2 - %3'[_usr_naz,B_PERM.DESC,_tab.P4])
               ?};
               &_pow
            ?};
            B_PERM.cntx_pop()
         |? 1+_tab.KOD='P'
         || {? _tab.OPERACJA='>'
            || _from:=.USR[1].REF;
               _to:=.USR[2].REF
            || _from:=.USR[2].REF;
               _to:=.USR[1].REF
            ?};
            FO_DEF.cntx_psh();
            FO_DEF.index('ALL');
            FO_DEF.prefix();
            {? FO_DEF.find_key('U',#_tab.P3)
            || FO_USR.cntx_psh();
               FO_USR.index('TAB');
               FO_USR.prefix();
               _jest:=FO_USR.find_key(_from,FO_DEF.TAB,FO_DEF.NR);
               FO_USR.cntx_psh();
               {? FO_USR.find_key(_to,FO_DEF.TAB,FO_DEF.NR)
               || FO_USR.del()
               ?};
               FO_USR.cntx_pop();
               {? _jest
               || FO_USR.USERS:=_to;
                  FO_USR.add()
               ?};
               FO_USR.cntx_pop()
            ?};
            FO_DEF.cntx_pop()
         ?};
         PROGRESS.next();
         _tab.next()
      !};
      PROGRESS.close()
   ?};
   _tab.cntx_pop();
   .show_msg()
";
_obj.set_usr:="
   _sel:=_a;
   .USR:=obj_new(2);
   {? _sel.first()
   || USERS.cntx_psh();
      USERS.prefix();
      {! _ii:=1..obj_len(.USR)
      |! .USR[_ii]:=obj_new('REF','NAME');
         {? USERS.seek(_sel.REF)
         || .USR[_ii].REF:=USERS.ref();
            .USR[_ii].NAME:=USERS.KOD
         ?};
         _sel.next()
      !};
      USERS.cntx_pop()
   ?}
";
_obj.set_usr(_sel);
_obj.fill_tab();
_obj


\create_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Tworzy okno na potrzeby prezentacji porównania uprawnień użytkownika
::   WE: _a - obiekt do porównania uprawnień użytkowników
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_tab:=_obj.TAB;
_wer:=_tab.mk_sel('Różnice w uprawnieniach użytkowników','P',,'#usercompare0',,,,,'U');
_tab.win_fld(_wer,,'P1',,,20,,,'Typ'@);
_tab.win_fld(_wer,,'P2',,,30,,,'Nazwa'@);
_tab.win_fld(_wer,,'P3',,,30,,,'Skrót');
_tab.win_fld(_wer,,'P4',,,30,,,'Opis');
_tab.win_fld(_wer,,'SEL1',,,20,,,_obj.USR[1].NAME);
_tab.win_fld(_wer,,'OSTR',,,15,,,'Operacja'@);
_tab.win_fld(_wer,,'SEL2',,,15,,,_obj.USR[2].NAME);
_tab.win_fld(_wer,,'SEL',,,10,,,'Powiązane',,,2,,"'T'","'N'");

_fml_bg:="sel_nchk();1";
_fml_ag:="
   _obj:=params_get().obj;
   _obj.upd_perm()
";
_tab.win_act(_wer,,'Formuła','< Kopiuj w &lewo'@@,,,"
   _tab:=cur_tab();
   {? 'RUP'*(1+_tab.KOD)
   || _tab.OPERACJA:='<';
      _tab.put();
      {? 'RU'*(1+_tab.KOD) & _tab.sel_size()=0
      || _obj:=params_get().obj;
         _obj.upd_perm()
      ?}
   ?}
",,,1,_fml_bg,_fml_ag);
_tab.win_act(_wer,,'Formuła','Kopiuj w &prawo >'@@,,,"
   _tab:=cur_tab();
   {? 'RUP'*(1+_tab.KOD)
   || _tab.OPERACJA:='>';
      _tab.put();
      {? 'RU'*(1+_tab.KOD) & _tab.sel_size()=0
      || _obj:=params_get().obj;
         _obj.upd_perm()
      ?}
   ?}
",,,1,_fml_bg,_fml_ag);
_tab.win_act(_wer,,'Formuła','Nie zmieniaj'@@,,,"
   _tab:=cur_tab();
   {? 'RUP'*(1+_tab.KOD)
   || _tab.OPERACJA:={? _tab.SEL1=_tab.SEL2 || '=' || '~' ?};
      _tab.put();
      {? 'RU'*(1+_tab.KOD) & _tab.sel_size()=0
      || _obj:=params_get().obj;
         _obj.upd_perm()
      ?}
   ?}
",,,1,_fml_bg,_fml_ag);
_tab.win_act(_wer,,'Formuła','Zastosuj'@@,,,"
   _tab:=cur_tab();
   _obj:=params_get().obj;
   _tab.cntx_psh();
   _tab.index(_obj.IND2);
   _tab.prefix(1);
   _sel:=_tab.first();
   _tab.cntx_pop();
   {? _sel | FUN.ask('Nie wprowadzono zmian. Czy kontynuować?'@)
   || _obj.EXIT:=0;
      sel_exit()
   ?}
");
_tab.win_act(_wer,,'Rekord',,,,"
   _tab:=cur_tab();
   _gray:='';
   {? _tab.sel_size()=0
   || {? 'RUP'*(1+_tab.KOD)=0
      || _gray+='LPN'
      ?}
   ?};
   _tab.actions_grayed(_tab.win_sel('?'),_gray);
   _tab.SEL='T'
");
_tab.win_act(_wer,,'Kolejność');
_tab.win_act(_wer,,'Okienko',,,,,"
   _exit:=1;
   _obj:=params_get().obj;
   {? _obj.EXIT
   || _tab:=cur_tab();
      _tab.cntx_psh();
      _tab.index(_obj.IND2);
      _tab.prefix(1);
      _sel:=_tab.first();
      _tab.cntx_pop();
      {? _sel & ~FUN.ask('Wprowadzono zmiany, które NIE zostaną zastosowane.\nCzy kontynuować?'@)
      || _exit:=0
      ?}
   ?};
   _exit
");
_tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['< Kopiuj w lewo'@@],'menu:L');
_tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Kopiuj w prawo >'@@],'menu:P');
_tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['Nie zmieniaj'@@],'menu:N');
_tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['Zastosuj'@@],'menu:Z');
_wer

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 4f12d115a1b6d83a4ab604236da025d5589e830663d361a53d868e81d49f8d1a12c178b36546462ee5c6093fd7ce3109caf75b540701113ab9559135a3b47812622fc23435d33a309fe6fc105a86156ec14f737007b050631dc70a1abe25a94ea53a53165d67e26c254142c504f58149a09e9a8010abe7a0f05926d44bfd02d1

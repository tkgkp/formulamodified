:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: dok_fks?ks.fml
:: Utworzony: 27.03.2015 [17.00]
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi ksigowania dokumentów księgowych
::======================================================================================================================


\ust_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodanie konta i ich obrotów podczas księgowania
::   WE: _a - waluta
::       _b - konto
::       _c - komentarz do konta
::  OLD: \ust_kon/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
_obr:=0;
AN.index('WALSYM');
AN.prefix();
{? AN.find_key(_a, _b)
|| _obr:=1
|| KOM.prefix;
   {? KOM.seek(_c)
   || AN.KS:=KOM.KS;
      AN.SYM:=_b;
      AN.STAN:='N';
      AN.WAL:=_a;
      _bl_an:=0;
      {? AN.add(1)
      || {? exec('addanslu','konto',AN.ref())
         || _obr:=1
         || _bl_an:=1
         ?}
      || _bl_an:=1
      ?};
      {? _bl_an
      || {? KOMT=''|| KOMT:='\nNie powiodło się dodanie analityki konta ' +POZ.KON+'.' ?}
      ?}
   || {? KOMT=''|| KOMT:='\nBrak komentarza konta '+POZ.KON+'.' ?}
   ?}
?};
{? _obr
|| OBR.index('AN_O');
   OBR.prefix();
   _obr:=OBR.find_key(AN.ref,DOK.ODD,SSTALE.AO().KOD);
   {? _obr=0
   || OBR.blank(1);
      OBR.AN:=AN.ref;
      OBR.ODD:={? OPERATOR.DEPT || OPERATOR.DEPT || DOK.ODD ?};
      OBR.OKRO:=SSTALE.AO
   ?};
   {? POZ.STR='Wn'
    || OBR.WN+={? _a=FINFO.NAROD || POZ.SUM || POZ.SUMW ?}
    || OBR.MA+={? _a=FINFO.NAROD || POZ.SUM || POZ.SUMW ?}
   ?};
   _ok:={? _obr || OBR.put(1) || OBR.add(1) ?};
   {? _ok=0
   || {? KOMT=''|| KOMT:='\nNie powiodła się aktualizacja obrotów konta ' +POZ.KON+'.' ?}
   ?};
   _ok
?}


\mod_op
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Tworzy zapisy rozrachunkow
::   WE: _a - 0 zapis w walucie rozrachunku, 1 zapis w walucie narodowej dla wielowalutowego
::  OLD: \mod_op/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
{? form(POZ.ID)<>''
|| ZAP_OP.cntx_psh(); ZAP_OP.prefix();
   {? ~exec('dod_oper','dok_fks_ks',{? _a=1 || POZ.WAL || FINFO.NAROD ?}) || undo() ?};
   ZAP_OP.blank(1);
   ZAP_OP.OP:=OP.ref;
   ZAP_OP.ODD:=OP.ODD;
   ZAP_OP.POZDOK:=POZ.ref;
   ZAP_OP.DATA:={? POZ.DATA_R<>date(0,0,0)
                || POZ.DATA_R
                || exec('dat_otw','dok_fks')
                ?};
   ZAP_OP.OKRO:=SSTALE.AO;
   ZAP_OP.REJ:=DOK.REJ;
   ZAP_OP.NR:=DOK.NR;
   ZAP_OP.DKS:=DOK.DKS;
   ZAP_OP.NK:=DOK.NK;
   ZAP_OP.POZ:=POZ.POZ;
   ZAP_OP.KURS:=POZ.KURS;
   ZAP_OP.WAL2:={? OP.WAL=FINFO.NAROD || POZ.WAL || FINFO.NAROD ?};
   {? _a
   || {? -POZ.STR='wn' || ZAP_OP.WN:=POZ.SUMW; ZAP_OP.WN2:=POZ.SUM   || ZAP_OP.MA:=POZ.SUMW;  ZAP_OP.MA2:=POZ.SUM ?}
   || {? -POZ.STR='wn' || ZAP_OP.WN:=POZ.SUM;  ZAP_OP.WN2:=POZ.SUMW  || ZAP_OP.MA:=POZ.SUM;   ZAP_OP.MA2:=POZ.SUMW ?}
   ?};
   ZAP_OP.TYP:={? DOK.DOK_REJ().ZAPL='T' || 'Z' || '' ?};
   ZAP_OP.RK:=POZ.RK;
   exec('upd_zap_op','fks_sp');
   {? ~ZAP_OP.add()
   || undo(); {? KOMT=''|| KOMT:='\nBłąd zapisu tabeli ZAP_OP.'?}; 0
   ?};
   {? ~exec('rozl_zap','dok_fks_ks', {? _a=1 || POZ.WAL || FINFO.NAROD ?}) || undo() ?};
   {? ~exec('op_proj','dok_proj',1,_a)
   || undo();
      {? KOMT=''|| KOMT:='\nBłąd zapisu tabeli OP_PROJ.'?}
   ?};
   ZAP_OP.cntx_pop()
?}


\dod_oper
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Aktualizacja rozrachunkow na podstawie dekretu.
::   WE: _a - ref waluty
::  OLD: \dod_oper/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
OP.index('KON_O');
OP.prefix();
{? OP.find_key(_a,POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK)
|| DOK.cntx_psh();
   {? var_press('DSP',OP)>0 & OP.DSP=date(0,0,0) & POZ.DOK().RVAT
   || OP.DSP:=POZ.DOK().DTO
   ?};
   DOK.cntx_pop();
   {? -OP.TYP<>'rmk' & -POZ.TID='rmk' | -OP.TYP<>'rmp' & -POZ.TID='rmp' |
      -OP.TYP='rmk' & -POZ.TID<>'rmk' | -OP.TYP='rmp' & -POZ.TID<>'rmp'
   || {? KOMT=''
      || KOMT:='\n\nNa koncie %1 istnieje rozrachunek o symbolu %2 typu %3.\n'
               'Niedozwolone użycie typu rozrachunku %4 na pozycji %5 dokumentu.\n'@[OP.AN,OP.SYM,OP.TYP,POZ.TID,$POZ.POZ]
      ?};
      0
   |? POZ.SLO_PROJ & exec('roz_op_proj','rozrach')
   || {? KOMT='' || KOMT:='\n\nRozrachunek '+OP.SYM+' rozlicza inne rozrachunki.\nNie można powiązać z nim projektów.' ?}; 0
   |? POZ.DO<OP.DO
   || OP.SYM_ZEW:=POZ.SYM_ZEW;
      OP.DO:=POZ.DO;
      OP.TZ:=POZ.TP;
      OP.TYP:=POZ.TID;
      {? ~OP.KH & -POZ.TID<>'rmk' & -POZ.TID<>'rmp' || OP.KH:=exec('kh_op','dok_fks_ks') ?};
      {? (~POZ.WAL | (POZ.WAL & OP.WAL<>FINFO.NAROD)) & exec('czyLista','raty',POZ)
      || D_RB.cntx_psh(); D_RB.index('POZ');
         D_RB.prefix(SSTALE.AO,POZ.ref());
         exec('updt_drb','raty','OP',OP.ref()); D_RB.cntx_pop();
         OP.TZ:=exec('drb_tpfp','raty','OP',1)
      ?};
      {? OP.put()
      || exec('pop_all','rozrach',0); 1
      || {? KOMT=''||KOMT:='\nNieudana próba aktualizacji rozrachunku '+OP.SYM+'.'?};
         0
      ?}
   || {? (~POZ.WAL | (POZ.WAL & OP.WAL<>FINFO.NAROD)) & exec('czyLista','raty',POZ)
      || D_RB.cntx_psh(); D_RB.index('POZ');
         D_RB.prefix(SSTALE.AO,POZ.ref());
         exec('updt_drb','raty','OP',OP.ref()); D_RB.cntx_pop();
         OP.TZ:=exec('drb_tpfp','raty','OP',1); OP.put();
         exec('pop_all','rozrach',0)
      ?}; 1
   ?}
|| OP.blank(1);
   OP.SYM:=POZ.ID;
   OP.SYM_ZEW:=POZ.SYM_ZEW;
   OP.DO:=POZ.DO;
   OP.ODD:={? POZ.ODD<>null || POZ.ODD || DOK.ODD ?};
   OP.A_VAT:=DOK.A_VAT;
   OP.TZ:=POZ.TP;
   OP.OP:={? POZ.OP='' || DOK.TR || POZ.OP ?};
   OP.TYP:=POZ.TID;
   OP.AN:=POZ.KON;
   OP.STAN:='N';
   OP.SP:='N';
   OP.SYM_ROK:=POZ.SYM_ROK;
   {? -POZ.TID<>'rmk' & -POZ.TID<>'rmp'
   || OP.POCH:=exec('wys_op','dok_fks_ks',POZ.KON);
      {? OP.POCH || OP.KH:=exec('kh_op','dok_fks_ks') ?}
   ?};
   OP.WAL:=_a;
   OP.CZY_MON:='T';
   OP.CZY_ODS:={? OP.TYP='NOD' | OP.TYP='ZOD' || 'N' || 'T' ?};
   {? OP.KH
   || {? OP.WAL<>FINFO.NAROD
      || {? DOK.RB & exec('czy_rach_kh','rachunki',DOK.RB,OP.KH) || OP.RB:=DOK.RB ?}
      || {? DOK.RB_V & (POZ.WAL=null | POZ.WAL=FINFO.NAROD) & DOK.WAL & DOK.WAL<>FINFO.NAROD
         || {? exec('czy_rach_kh','rachunki',DOK.RB_V,OP.KH) || OP.RB:=DOK.RB_V ?}
         |? DOK.RB & exec('czy_rach_kh','rachunki',DOK.RB,OP.KH)
         || OP.RB:=DOK.RB
         ?}
      ?}
   ?};
   OP.HAN:=DOK.HAN;
   DOK.cntx_psh();
   {? var_press('DSP',OP)>0 & POZ.DOK().RVAT
   || OP.DSP:=POZ.DOK().DTO
   ?};
   DOK.cntx_pop();
   {? OP.add()
   || {? (~POZ.WAL | (POZ.WAL & OP.WAL<>FINFO.NAROD)) & exec('czyLista','raty',POZ)
      || D_RB.cntx_psh();
         D_RB.index('POZ');
         D_RB.prefix(SSTALE.AO,POZ.ref());
         exec('updt_drb','raty','OP',OP.ref()); D_RB.cntx_pop();
         OP.TZ:=exec('drb_tpfp','raty','OP',1); OP.put();
         exec('pop_all','rozrach',0)
      ?};
      1
   || {? KOMT=''||KOMT:='\nNieudana próba założenia rozrachunku '+OP.SYM+'.'?};0
   ?}
?}


\kh_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Przypisanie kontrahenta do rozrachunku
::   WE: _a - niezadeklarowane - na podstawie OP.POCH, zadeklarowane na podstawie SKID.POCH
::   WY: ref kontrahenta (KH)
::  OLD: \kh_op/skid_dek.fml
::----------------------------------------------------------------------------------------------------------------------
_wys:=0; _a:={? var_pres('_a')>0 || 1 || 0 ?};
{? (~_a & OP.POCH) | (_a & SKID.POCH)
|| {? ~_a
   || OP.POCH().SLU(); _kod:=OP.POCH().KOD
   || SKID.POCH().SLU(); _kod:=SKID.POCH().KOD
   ?};
   RS.index('RS'); RS.prefix(SLU.WZ);
   {? RS.first() & RS.TAB='KH'
   || KH.index('KOD'); KH.prefix(2,_kod);
      {? KH.first() & KH.KOD=_kod  || _wys:=KH.ref() ?};
      KH.prefix()
   ?};
   RS.prefix()
?};
_wys


\wys_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Szuka wystawcy rozrachunku
::   WE: _a - symbol konta, _b - ref roku (ROK_F) - opcja
::   WY: ref wystawcy (SLO)
::  OLD: \wys_op/skid_dek.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<=0 || _b:=ROK_F.ref() ?};
_wys:=_us:=_dlug:=0; _slu:=null;
KS.cntx_psh();  KS.index('SYM');
BUD.cntx_psh(); BUD.index('KS');
SLO.cntx_psh(); SLO.index('SL'); SLO.prefix();
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
{? _b<>null
|| _ok:=(ROK_F.seek(_b) & ((+(ROK_F.SYNT+_a))=ROK_F.SYNT));
   {? _ok
   || KS.prefix(ROK_F.ref(),ROK_F.SYNT+_a);
      _synt:=ROK_F.SYNT; _sep:=ROK_F.SEP
   ?}
|| _ok:=((+(BILANSP.ASYNT+_a))=BILANSP.ASYNT);
   {? _ok
   || KS.prefix(SSTALE.AR,BILANSP.ASYNT+_a);
     _synt:=BILANSP.ASYNT; _sep:=BILANSP.ASEP
   ?}
?};
{? _ok & KS.first()
|| _us:=_synt+{? _sep=',' || 0 || 1 ?};
   BUD.prefix(KS.ref());
   _up:=_uk:=0;
   {? BUD.first()
   || {!|? BUD.SLU().SLU();
          {? SLU.WZ='Kontrahent'
          || _uk:=_us; _slu:=SLU.ref; _dlug:=SLU.DL; 0
          |? _up=0 & (SLU.WZ='Osoba' | SLU.WZ='Pracownik' | SLU.WZ='Kartoteka osób')
          || _up:=_us; _slu:=SLU.ref; _dlug:=SLU.DL; _us+=_dlug+{? _sep=',' || 0 || 1 ?}; BUD.next()
          || _us+=SLU.DL+{? _sep=',' || 0 || 1 ?}; BUD.next()
          ?}
      !}
   ?};
   {? _slu
   || _us:={? _uk || _uk || _up ?};
      SLO.prefix(_slu); _co:=_dlug+(_us-_a);
      {? _co<>'' & SLO.find_key(_co) & SLO.KOD=_co || _wys:=SLO.ref() ?}
   ?}
?};
SLU.cntx_pop(); SLO.cntx_pop(); BUD.cntx_pop(); KS.cntx_pop(); ROK_F.cntx_pop();
_wys


\rozl_zap
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Rozliczenie rozrachunku z zapisu
::   WE: _a - ref waluty rozliczenia
::  OLD: \rozl_zap/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
{? STANY.find_key(OP.ref, SSTALE.AO)
|| STANY.WN+=ZAP_OP.WN;
   STANY.MA+=ZAP_OP.MA;
   _res:={? STANY.put() || 1 || {? KOMT=''||KOMT:='\nBłąd zapisu tabeli STANY.'?}; 0 ?}
|| STANY.blank(1);
   STANY.OP:=OP.ref;
   STANY.OKRO:=SSTALE.AO;
   STANY.WN:=ZAP_OP.WN;
   STANY.MA:=ZAP_OP.MA;
   _res:={? STANY.add() || 1 || {? KOMT=''||KOMT:='\nBłąd zapisu tabeli STANY.'?}; 0 ?}
?};
{? _res
|| OP.WN+=ZAP_OP.WN;
   OP.MA+=ZAP_OP.MA;
   {? _a=OP.WAL & OP.WN$2=OP.MA$2
   || OP.STAN:='R'
   || OP.STAN:='N'
   ?};
   {? OP.put() || 1 || {? KOMT=''|| KOMT:='\nBłąd zapisu tabeli OP.'?}; 0 ?}
?}


\tstwal
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Czy rozrachunek wielowalutowy
::  OLD: \tstwal/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? POZ.ID<>''
|| OP.cntx_psh(); OP.index('KON_O'); OP.prefix();
   _odd:=POZ.ODD; _konto:=POZ.KON; _sym:=POZ.ID; _usym:=POZ.SYM_ROK;
   SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(FINFO.SLWAL().SLU);
   {? SLO.first()
   || {! |?
         {? SLO.ref()<>FINFO.NAROD
         || {? OP.find_key(SLO.ref(),_odd,_konto,_sym,_sym,_usym)
            || _wyn:=(SLO.ref()<>_a & exec('euro','waluty',_a,SLO.ref()))
            ?}
         ?};
         ~_wyn & SLO.next()
      !}
   ?};
   SLO.cntx_pop(); OP.cntx_pop()
?};
_wyn


\vat_ks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RF
:: OPIS: Tworzy zapisy w ewidencji VAT
::   WE: _a - ref okresu z jakiego pochodzi dokument (zwykle SSTALE.AO)
::  OLD: \vat_ks/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref);
{? VPOZ.first
|| {!
   |? _rozlvat:=0;
      {? VPOZ.OKRVAT
      || exec('vat_ks1','dok_fks_ks',_a,0,0,'',VPOZ.D)
      |? VPOZ.OKR_Z | VPOZ.OKR_C
      || {? VPOZ.OKR_Z
         || exec('vat_ks1','dok_fks_ks',_a,,,'Z',VPOZ.D)
         || _rozlvat:=1
         ?};
         {? VPOZ.OKR_C
         || exec('vat_ks1','dok_fks_ks',_a,,,'C',VPOZ.D)
         || _rozlvat:=1
         ?}
      || _rozlvat:=1
      ?};
      {? _rozlvat
      || ROZLVAT.index('DOK');
         {? DOK.DOK_REJ().KOR_NAG='T'
         || ROZLVAT.prefix(_a,DOK.ref,DOK.RVAT().RVAT,VPOZ.D)
         || ROZLVAT.prefix(_a,DOK.ref,VPOZ.RVAT().RVAT,VPOZ.D)
         ?};
         {? ~ROZLVAT.first()
         || ROZLVAT.blank(1);
            ROZLVAT.DOK:=DOK.ref; ROZLVAT.OKRO:=_a;
            ROZLVAT.REJ:=DOK.REJ; ROZLVAT.SYM:=DOK.NK;
            ROZLVAT.DATA:=DOK.DTW;
            ROZLVAT.DOP:=VPOZ.D; ROZLVAT.KH:=DOK.KH;
            ROZLVAT.RVAT:={? DOK.DOK_REJ().KOR_NAG='T' || DOK.RVAT().RVAT || VPOZ.RVAT().RVAT ?};
            ROZLVAT.ODD:=DOK.ODD;
            ROZLVAT.SP_WYM:=DOK.SP_WYM;
            {? ~ROZLVAT.add()
            || undo();
               {? KOMT='' || KOMT:='\nBłąd zapisu tabeli ROZLVAT.'@ ?}
            ?}
         ?}
      ?};
      VPOZ.next()
   !}
?}


\vat_ks1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Tworzy pojedynczy zapis w ewidencji VAT
::   WE: _a - ref okresu z jakiego pochodzi dokument
::       [_b] - czy ustawic znacznik usuniecia z kartoteki VAT
::       [_c] - czy czesciowe rozliczenie
::       [_d] - typ okresu
::       [_e] - data sprzedaży
::  OLD: \vat_ks1/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<=0 || _b:=0 ?};
{? var_pres('_c')<=0 || _c:=0 ?};
{? var_pres('_d')<=0 || _d:='' ?};
{? var_pres('_e')<=0 || _e:=DOK.DOP ?};
{? _c
|| _rok:=VPOZ_ROZ.OKRO_F().ROK;
   _rok_kod:=OKRO_F.ROK().KOD;
   _okrvat:=VPOZ_ROZ.OKRO_F
|? VPOZ.OKRVAT
|| _rok:=VPOZ.OKRVAT().ROK;
   _rok_kod:=OKRO_F.ROK().KOD;
   _okrvat:=VPOZ.OKRVAT
|? _d='Z'
|| _rok:=VPOZ.OKR_Z().ROK;
   _rok_kod:=OKRO_F.ROK().KOD;
   _okrvat:=VPOZ.OKR_Z
|| _rok:=VPOZ.OKR_C().ROK;
   _rok_kod:=OKRO_F.ROK().KOD;
   _okrvat:=VPOZ.OKR_C
?};
{? _rok<>SSTALE.AR
|| {? ~DVAT.use('dvat__'+_rok_kod) || undo() ?};
   {? ~PVAT.use('pvat__'+_rok_kod) || undo() ?};
   DVAT.index('NR');
   DVAT.prefix();
   PVAT.index('NR');
   PVAT.prefix()
?};
exec('dvat','dok_fks_ks',_a,_okrvat,_b, VPOZ.RVAT().RVAT,_e);
PVAT.prefix(DVAT.ref);
_ok:=1;
{? PVAT.first()
|| {! |? {? PVAT.VPOZ_REF=VPOZ.ref()
         || {? PVAT.TYP_VAT<>'' & VPOZ.OKRVAT & ~VPOZ.OKR_Z & ~VPOZ.OKR_C
            || PVAT.TYP_VAT:='';
               PVAT.put();_ok:=0
            ?}
         || PVAT.next()
         ?}
   !}
?};
{? _ok=1
|| _nr:={? PVAT.last() || PVAT.NR+1 || 1 ?};
   PVAT.blank(1);
   PVAT.DVAT:=DVAT.ref;
   PVAT.NR:=_nr;
   PVAT.GRVAT:={? _c || VPOZ_ROZ.GR_VAT().GRVAT ||  VPOZ.GRVAT().GRVAT ?};
   PVAT.STVAT:=VPOZ.PR;
   PVAT.SUM1:={? _c || VPOZ_ROZ.BRUTTO || VPOZ.BRUTTO ?};
   PVAT.SUM2:={? _c || VPOZ_ROZ.NETTO || VPOZ.NETTO ?};
   PVAT.VAT:={? _c || VPOZ_ROZ.VAT || VPOZ.VAT ?};
   PVAT.CLO:=VPOZ.CLO;
   PVAT.AKCYZA:=VPOZ.AKCYZA;
   PVAT.KOS:=VPOZ.KOS;
   PVAT.SUMC:=VPOZ.WCLO;
   PVAT.KURS:=VPOZ.KURS;
   PVAT.WARW:=VPOZ.WARW;
   PVAT.VAT_ODL:={? _c || VPOZ_ROZ.VAT_ODL || VPOZ.VAT_ODL ?};
   PVAT.VATKOSZT:={? _c || VPOZ_ROZ.VATKOSZT || VPOZ.VATKOSZT ?};
   PVAT.TYP_VAT:=_d;
   {? POMOC.KNAG=2
   || PVAT.SUM1  :=PVAT.SUM1*(-1);
      PVAT.SUM2  :=PVAT.SUM2*(-1);
      PVAT.VAT   :=PVAT.VAT*(-1);
      PVAT.CLO   :=PVAT.CLO*(-1);
      PVAT.AKCYZA:=PVAT.AKCYZA*(-1);
      PVAT.KOS   :=PVAT.KOS*(-1);
      PVAT.SUMC  :=PVAT.SUMC*(-1);
      PVAT.KURS  :=PVAT.KURS*(-1);
      PVAT.WARW  :=PVAT.WARW*(-1);
      PVAT.VAT_ODL:=PVAT.VAT_ODL*(-1);
      PVAT.VATKOSZT:=PVAT.VATKOSZT*(-1);
      PVAT.KNAG:=POMOC.KNAG
   ?};
   {? _c
   || PVAT.VPOZ_ROZ:=VPOZ_ROZ.ref()
   || PVAT.VPOZ_REF:=VPOZ.ref()
   ?};
   PVAT.OP:=VPOZ.OP;
   {? ~PVAT.add() || undo(); {? KOMT='' || KOMT:='\nBłąd zapisu tabeli PVAT.'?}; 0 ?}
?};
{? _rok<>SSTALE.AR
|| {? ~DVAT.use('dvat__'+SSTALE.AR().KOD) || undo() ?};
   {? ~PVAT.use('pvat__'+SSTALE.AR().KOD) || undo() ?}
?}


\dvat
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Dodanie naglowka dokumentu VAT
::   WE: _a - ref okresu z jakiego pochodzi dokument (zwykle SSTALE.AO)
::       _b - ref okresu, ktorego dotyczy rozliczenie VAT
::       _c - czy ustawic znacznik usuniecia z kartoteki VAT
::       [_d] - rejestr VAT z pozycji VPOZ
::       [_e] - data sprzedaży
::  OLD: \dvat/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
DVAT.index('DOK');
{? _=4
|| DVAT.prefix(DOK.ref(),_a,_b,_d)
|? _=5
|| DVAT.prefix(DOK.ref(),_a,_b,_d,_e)
|| DVAT.prefix(DOK.ref(),_a,_b)
?};
{? var_pres('_e')<=0 || _e:=DOK.DOP ?};
{? DVAT.lock(2)
|| {? DVAT.first()
   || 1
   || DVAT.index('NR');
      VPOZ.RVAT();
      DVAT.prefix(DOK.KRAJ,_b,VAT_REJ.RVAT);
      _nr:={? DVAT.last || DVAT.NR+1 || 1 ?};
      DVAT.blank(1);
      DVAT.OBR:=_b;
      DVAT.RVAT:=VAT_REJ.RVAT;
      DVAT.NR:=_nr;
      DVAT.WYS:=DOK.WYS;
      DVAT.NIP:=DOK.NIP;
      DVAT.KH:=DOK.KH_FULL;
      DVAT.UL:=DOK.UL;
      DVAT.MIASTO:=DOK.MIASTO;
      DVAT.DOM:=DOK.DOM;
      DVAT.LOKAL:=DOK.LOKAL;
      DVAT.KPOCZ:=DOK.KPOCZ;
      DVAT.POCZ:=DOK.POCZ;
      DVAT.DAT1:=DOK.DTW;
      DVAT.DAT2:={? DOK.KSEFCDAT<>date(0,0,0) || DOK.KSEFCDAT || DOK.DTO ?};
      DVAT.DAT3:=DOK.D3;
      DVAT.DAT4:=DOK.D4;
      DVAT.DAT5:=_e;
      DVAT.SYM1:=DOK.NK;
      DVAT.SYM2:=DOK.KOR;
      DVAT.WAL:=DOK.JORG;
      DVAT.DOK:=DOK.ref();
      DVAT.DOKOKRO:=_a;
      DVAT.REJ:=DOK.REJ;
      DVAT.NR_W_REJ:=DOK.NR;
      DVAT.KRAJ:=DOK.KRAJ;
      DVAT.ODD:=DOK.ODD;
      {? DOK.E_DOC || DVAT.E_DOC:=1 ?};
      {? _c<>0 || DVAT.USUNIETY:=_c ?};
      DVAT.SP_WYM:=DOK.SP_WYM;
      DVAT.POMIN:=exec('dvat_pomin','jpk_v',DOK.JPK_V_T);
      {? ~DVAT.add()
      || undo();
         {? KOMT='' || KOMT:='\nNieudana próba dołączenia rekordu tabeli DVAT'?}
      ?}
   ?};
   DVAT.unlock()
|| {? KOMT='' || KOMT:='\nNieudana próba dołączenia rekordu tabeli DVAT'?}
?}


\ks_dok
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Zasadnicza formuła księgująca
::   WE: _a - 1 ksiegowanie koncowe, 0 ksiegowanie probne
::  OLD: \ks_dok/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
_dok_mod:=exec('spr_mod','dok_fks');
KOMT:=''; POMOC.KNAG:=0;
{? MLEX.FIKSB
|| exec('KOMM','#object');
   KOMM.init(,,'Uwagi obsługi struktury rodzajowej');
   _koml:=1
|| _koml:=0
?};
{? ~_dok_mod || do() ?};
DOK.cntx_psh();
DOK.clear();
{? DOK.get()
|| {? DOK.KSEF='T' & DOK.MANSKSEF='N' & DOK.KSEFCDAT=date(0,0,0) & PAR_SKID.get(439)='N'
   || undo(); {? KOMT='' || KOMT:='\nBrak potwierdzenia wysłania dokumentu do KSeF.' ?}
   ?};
   POZ.index('DOK');
   POZ.prefix(DOK.ref);
   _dat_ks:=date();
   _obj:=exec('obj_op','dok_fks_ks');
   {? POZ.first
   || {!
      |? {? ~exec('ust_kon','dok_fks_ks',FINFO.NAROD,POZ.KON,POZ.KOM) || undo() ?};
         DOK.DKS:=_dat_ks;
         exec('mod_op','dok_fks_ks',0);
         exec('mod_op2','dok_fks_ks',0,_obj);
::Poczatek modyfikacji dla Maclex Fiks 12-10-2009 AK [1120]
           {? MLEX.FIKSB
           ||   {? ~exec('mlbzaddod','ml_zad') || undo() ?};
                {? ~exec('pozbdod','mlxstan') || undo() ?};
                {? ~exec('add_bud_obr','mlxstan',FINFO.NAROD) || undo() ?};
                {? ~exec('add_ml_gr_obr','ml_gr',FINFO.NAROD) || undo() ?}
           ?};
::Koniec modyfikacji dla Maclex
         {? POZ.WAL<>null
         || {? exec('tstwal','dok_fks_ks',POZ.WAL)
            || undo();
               {? KOMT=''
               || KOMT:='\nIstnieje rozrachunek o podanym symbolu otwarty w roku '+$(POZ.DO~1)+'\n'+
                        ' w innej walucie obcej.\n'+
                        ' (pozycja: '+$POZ.POZ+' )\n'+
                        '\nWycofaj akceptację dokumentu i popraw dekret.'
               ?}
            || {? ~exec('ust_kon','dok_fks_ks',POZ.WAL,POZ.KON,POZ.KOM) || undo() ?};
::Poczatek modyfikacji dla Maclex Fiks 14-10-2009 AK [1120]
               {? MLEX.FIKSB
               ||   {? ~exec('add_bud_obr','mlxstan',POZ.WAL) || undo() ?};
                    {? ~exec('add_ml_gr_obr','ml_gr',FINFO.NAROD) || undo() ?}
               ?};
::Koniec modyfikacji dla Maclex
               exec('mod_op','dok_fks_ks',1);
               exec('mod_op2','dok_fks_ks',1,_obj)
            ?}
         ?};
         exec('mod_mw','dok_fks_ks');
         POZ.A:=POZ.ZP:='T';
         {? _a || POZ.ZK:='T' ?};
         POZ.put();
         POZ.next()
      !};
      {? DOK.RVAT || exec('vat_ks','dok_fks_ks',SSTALE.AO) ?};
      {? 4+DOK.KN='doku' & DOK.OKRVAT
      || DOK.cntx_psh(); VPOZ.cntx_psh();
         _ref:=BB.sqlint(DOK.KN);
         _maska:=(8+DOK.KN)+4;
         DOK.use('doku'+_maska); DOK.prefix();
         VPOZ.use('pozv'+_maska);
         _ok:=_ref<>0 & DOK.seek(_ref,) & DOK.ZP='T';
         {? _ok
         || _CzyTezKor:={? DOK.DOK_REJ().KOR='T' || DOK.KOR || '' ?};
            POMOC.KNAG:=2;
            OKRO_F.cntx_psh();
            _okr:=exec('szuk_okr','okresy',DOK.DTW);
            {? _okr
            || exec('vat_ks','dok_fks_ks',_okr)
            || {? KOMT=''|| KOMT:='\nBrak okresu dla daty.' ?}; undo()
            ?};
            OKRO_F.cntx_pop()
         ?};
         DOK.cntx_pop(); VPOZ.cntx_pop();
         {? _ok & _CzyTezKor<>''
         || _ok:=0; _ref:=null;
            DVAT.cntx_psh(); _ind1:=DVAT.ndx_tmp(,1,'SYM1',,0,'SYM1',,0); DVAT.index(_ind1); DVAT.prefix(_CzyTezKor);
            {? DVAT.first()
            || {? exec('FindAndGet','#table','DOK',DVAT.DOK,,"LKN",'')=$DOK.ref
               || _ok:=1; _ref:=DVAT.DOK
               ?}
            ?};
            DVAT.cntx_pop(); DVAT.ndx_drop(_ind1);
            DOK.cntx_psh(); VPOZ.cntx_psh();
            {? _ok & _ref<>null
            || _maska:=4-ref_name(_ref);
               DOK.use('doku'+_maska); DOK.prefix();
               VPOZ.use('pozv'+_maska);
               _ok:=_ref<>0 & DOK.seek(_ref,) & DOK.ZP='T'
            ?};
            {? _ok
            || POMOC.KNAG:=2;
               OKRO_F.cntx_psh();
               _okr:=exec('szuk_okr','okresy',DOK.DTW);
               {? _okr
               || exec('vat_ks','dok_fks_ks',_okr)
               || {? KOMT=''|| KOMT:='\nBrak okresu dla daty.' ?}; undo()
               ?};
               OKRO_F.cntx_pop()
            ?};
            DOK.cntx_pop(); VPOZ.cntx_pop()
         ?}
      ?};
      {? _a
      || exec('nrdz','dok_fks_ks');
         DOK.KSIEGOWY:=exec('usr_zar','dok_fks');
         DOK.DKS_K:=date();
         DOK.ZK:='T'
      ?};
      DOK.ZP:='T';
      DOK.DKS:=_dat_ks;
      DOK.cntx_psh();
      DOK.prefix;
      {? ~DOK.put() || {? KOMT=''|| KOMT:='\nBłąd zapisu w tabeli dokumentów.'?}; undo() ?};
      DOK.cntx_pop()
   |? DOK.DOK_REJ().M_ODD='T'
   || {? DOK.RVAT || exec('vat_ks','dok_fks_ks',SSTALE.AO) ?}
   |? DOK.RVAT
   ||
:: #VATKN - księgowanie korekty nagłówkowej
      {? DOK_REJ.KOR_NAG='T'
      || exec('vat_ks','dok_fks_ks',SSTALE.AO);
         {? 4+DOK.KN='doku' & DOK.OKRVAT<>null
         || DOK.cntx_psh(); VPOZ.cntx_psh();
            _ref:=BB.sqlint(DOK.KN);
            _maska:=(8+DOK.KN)+4;
            DOK.use('doku'+_maska); DOK.prefix();
            VPOZ.use('pozv'+_maska);
            {? _ref<>0 & DOK.seek(_ref,) & DOK.ZP='T'
            || POMOC.KNAG:=2;
               OKRO_F.cntx_psh();
               _okr:=exec('szuk_okr','okresy',DOK.DTW);
               {? _okr
               || exec('vat_ks','dok_fks_ks',_okr)
               || {? KOMT=''|| KOMT:='\nBrak okresu dla daty.' ?}; undo()
               ?};
               OKRO_F.cntx_pop()
            ?};
            DOK.cntx_pop(); VPOZ.cntx_pop()
         ?}
      || exec('vat_ks','dok_fks_ks',SSTALE.AO)
      ?};
      {? _a
      || exec('nrdz','dok_fks_ks');
         DOK.KSIEGOWY:=exec('usr_zar','dok_fks');
         DOK.DKS_K:=date();
         DOK.ZK:='T'
      ?};
      DOK.ZP:='T';
      DOK.DKS:=_dat_ks;
      DOK.cntx_psh();
      DOK.prefix;
      {? ~DOK.put() || {? KOMT=''|| KOMT:='\nBłąd zapisu w tabeli dokumentów.'?}; undo() ?};
      DOK.cntx_pop()
   |? DOK.DOK_REJ().PUDEK='T'
   || {? _a
      || exec('nrdz','dok_fks_ks');
         DOK.KSIEGOWY:=exec('usr_zar','dok_fks');
         DOK.DKS_K:=date();
         DOK.ZK:='T'
      ?};
      DOK.ZP:='T';
      DOK.DKS:=_dat_ks;
      DOK.cntx_psh();
      DOK.prefix;
      {? ~DOK.put() || {? KOMT=''|| KOMT:='\nBłąd zapisu w tabeli dokumentów.'?}; undo() ?};
      DOK.cntx_pop()
   || {? KOMT=''|| KOMT:='\nBrak pozycji dokumentu.' ?};undo()
   ?}
|| {? KOMT=''|| KOMT:='\nDokument niedostępny.' ?}; undo()
?};
DOK.cntx_pop();
_wyn:={? _dok_mod || 1 || end()  ?};
{? _koml=1
|| {? KOMT<>''
   || KOMM.add('Dokument '+POZ.DOK().NK+' - '+KOMT)
   ?};
   {? ~KOMM.empty() || KOMM.select() ?}
?};
_wyn


\nrdz
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Funkcja wylicza kolejny numer w dzienniku.
::       Powinna byc wolana tuz przed ustawieniem znacznika zaksiegowania koncowego.
::  OLD: \nrdz/dziennik.fml
::----------------------------------------------------------------------------------------------------------------------
_rej:=DOK.REJ;
DOK.cntx_psh();
DOK.index('NRDZ2');
DOK.prefix('T','T',_rej);
_nrdz:={? DOK.last() || DOK.NRDZ+1 || 1 ?};
DOK.cntx_pop();
DOK.NRDZ:=_nrdz


\wycofaj
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Wycofanie księgowania próbnego
::  OLD: \wycofaj/dok_ks.fml
::----------------------------------------------------------------------------------------------------------------------
KOMT:=''; POMOC.KNAG:=0;
OP.prefix;
ZAP_OP.index('ZAP');
OBR.index('OKR');
OBR.prefix(SSTALE.AO().NR);
::Poczatek modyfikacji dla Maclex Fiks 14-10-2009 AK [1120]
{? MLEX.FIKSB
||   ML_OBRCZ.index('OOKRCZ'); ML_OBRCZ.prefix(SSTALE.AO().NR);
     ML_OBRDZ.index('OOKRDZ'); ML_OBRDZ.prefix(SSTALE.AO().NR);
     ML_OBRRZ.index('OOKRRZ'); ML_OBRRZ.prefix(SSTALE.AO().NR);
     ML_OBRPG.index('OOKRPG'); ML_OBRPG.prefix(SSTALE.AO().NR);
     ML_OBRPP.index('OOKRPP'); ML_OBRPP.prefix(SSTALE.AO().NR)
?};
::Koniec modyfikacji dla MacLex
STANY.index('STAN');
STANY.prefix;
DVAT.index('DOK');
DVAT.prefix;
PVAT.index('NR');
ROZLVAT.index('DOK');
ROZLVAT.prefix();
POZ.index('DOK');
POZ.prefix(DOK.ref);
AN.index('WALSYM');
AN.prefix();
_do:=do_state()=0;
{? _do || do() ?};
{? DOK.RVAT || exec('wyc_vatg','dok_fks_ks',1) ?};
:: #VATKN wycofywanie z VAT faktury powiązanej z wycofywaną korektą nagłówkową
{? DOK.RVAT & DOK.DOK_REJ().KOR_NAG='T' & 4+DOK.KN='doku'
|| DOK.cntx_psh(); VPOZ.cntx_psh(); DOK.prefix();
   _ref:=BB.sqlint(DOK.KN); _CzyTezKor:='';
   _maska:=(8+DOK.KN)+4;
   DOK.use('doku'+_maska);
   VPOZ.use('pozv'+_maska);
   DOK.prefix();
   {? _ref<>0 & DOK.seek(_ref,)
   || _CzyTezKor:={? DOK.DOK_REJ().KOR='T' || DOK.KOR || '' ?};
      POMOC.KNAG:=2;
      exec('wyc_vatg','dok_fks_ks',1)
   ?};
   DOK.cntx_pop(); VPOZ.cntx_pop();
   {? _CzyTezKor<>''
   || _ok:=0; _ref:=null;
      DVAT.cntx_psh(); _ind1:=DVAT.ndx_tmp(,1,'SYM1',,0,'SYM1',,0); DVAT.index(_ind1); DVAT.prefix(_CzyTezKor);
      {? DVAT.first()
      || {? exec('FindAndGet','#table','DOK',DVAT.DOK,,"LKN",'')=$DOK.ref
         || _ok:=1; _ref:=DVAT.DOK
         ?}
      ?};
      DVAT.cntx_pop(); DVAT.ndx_drop(_ind1);
      DOK.cntx_psh(); VPOZ.cntx_psh();
      {? _ok & _ref<>null
      || _maska:=4-ref_name(_ref);
         DOK.use('doku'+_maska); DOK.prefix();
         VPOZ.use('pozv'+_maska);
         _ok:=_ref<>0 & DOK.seek(_ref,) & DOK.ZP='T'
      ?};
      {? _ok
      || POMOC.KNAG:=2;
         exec('wyc_vatg','dok_fks_ks',1)
      ?};
      DOK.cntx_pop(); VPOZ.cntx_pop()
   ?}
?};
{? POZ.first()
|| {!
   |? {? AN.find_key(FINFO.NAROD,POZ.KON)
      || {? OBR.find_key(AN.ref,POZ.DOK().ODD)
         || {? -POZ.STR='wn' || OBR.WN-=POZ.SUM || OBR.MA-=POZ.SUM ?};
            OBR.put
         || {? KOMT=''|| KOMT:='\nNie znaleziono rekordu w tabeli obrotów'?};
            undo
         ?}
      || {? KOMT=''|| KOMT:='\nNie znaleziono rekordu w tabeli kont analitycznych'?};
         undo
      ?};
      {? POZ.ODD<>null & |POZ.ID<>''
      || exec('wyc_op','rozrach',0,0);
         {? KOMT='' || exec('wyc_op','rozrach',0,1) ?}
      ?};
      {? POZ.KOM().KS().MW='T' || exec('wyc_mw','dok_fks_ks') ?};
::Poczatek modyfikacji dla Maclex Fiks 13-10-2009 AK [1120]
      {? MLEX.FIKSB
      || exec('mlbzadusu','ml_zad',,'A');
         {? ~exec('wyc_bud_obr','mlxstan',FINFO.NAROD) || undo() ?};
         {? ~exec('wyc_ml_gr_obr','ml_gr',FINFO.NAROD) || undo() ?}
      ?};
::Koniec modyfikacji dla MacLex
      {? POZ.WAL<>null
      || {? AN.find_key(POZ.WAL,POZ.KON)
         || {? OBR.find_key(AN.ref,POZ.DOK().ODD)
            || {? -POZ.STR='wn' || OBR.WN-=POZ.SUMW || OBR.MA-=POZ.SUMW ?};
               OBR.put
            || {? KOMT=''|| KOMT:='\nNie znaleziono rekordu w tabeli obrotów'?};
               undo
            ?}
         || {? KOMT=''|| KOMT:='\nNie znaleziono rekordu w tabeli kont analitycznych'?};
            undo
         ?};
         {? POZ.ODD<>null & |POZ.ID<>''
         || exec('wyc_op','rozrach',1,0);
            {? KOMT='' || exec('wyc_op','rozrach',1,1) ?}
         ?};
::Poczatek modyfikacji dla Maclex Fiks 12-10-2009 AK [1120]
         {? MLEX.FIKSB
         ||   {? ~exec('wyc_bud_obr','mlxstan',POZ.WAL)||undo() ?};
              {? ~exec('wyc_ml_gr_obr','ml_gr',FINFO.NAROD) || undo() ?}
         ?}
      ?};
      exec('pozbusu','mlxstan');
::Koniec modyfikacji dla MacLex
      POZ.ZP:=POZ.ZK:='N'; POZ.put();
      POZ.next()
   !}
|? DOK.DOK_REJ().M_ODD<>'T' & DOK_REJ.PUDEK='N'
|| {? KOMT=''|| KOMT:='\nNie znaleziono zapisu księgowego' ?};undo()
?};
DOK.ZP:='N';
{? DOK.CZY_CZ='T'
|| DOK.CZY_CZ:='';
   DOK.OKRVAT:=null
?};
DOK.DKS:=date(0,0,0);
DOK.cntx_psh();
DOK.prefix();
{? ~DOK.put() || undo() ?};
_ok:={? _do || end() || do_state()<>2 ?};
{? _ok & exec('hasAction','users','FKS_DKS_DARK')
|| exec('anuluj','dok_fks',0)
?};
DOK.cntx_pop();
_ok


\wyc_vatg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [2008]
:: OPIS: Wycofanie dokumentow z ewidancji VAT powiazanych z dokumentem zrodlowym
::   WE: [_a] - przy wycowywaniu księgowania dokumentu
::  OLD: \wyc_vatg/dok_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<=0 || _a:=0 ?};
DVAT.index('DOK'); DVAT.prefix();
VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
{? VPOZ.first()
|| VPOZ_ROZ.index('VPOZ_ROZ');
   {! |?
      VPOZ_ROZ.prefix(VPOZ.ref());
      {? VPOZ.OKRVAT<>null & VPOZ_ROZ.first()
      || VPOZ.get(); VPOZ.OKRVAT:=null; VPOZ.trig_off('*','*'); VPOZ.put(); VPOZ.trig_on('*','*')
      ?};
      VPOZ.next()
   !}
?};
{? VPOZ.first()
|| {!
   |? {? VPOZ.OKRVAT
      || exec('wyc_vatg1','dok_fks_ks',VPOZ.OKRVAT().ROK().KOD,ROK.ref(),VPOZ.OKRVAT,_a)
      |? VPOZ.OKR_Z | VPOZ.OKR_C
      || {? VPOZ.OKR_Z
         || exec('wyc_vatg1','dok_fks_ks',VPOZ.OKR_Z().ROK().KOD,ROK.ref(),VPOZ.OKR_Z)
         ?};
         {? VPOZ.OKR_C
         || exec('wyc_vatg1','dok_fks_ks',VPOZ.OKR_C().ROK().KOD,ROK.ref(),VPOZ.OKR_C)
         ?};
         {? ~VPOZ.OKR_C | ~VPOZ.OKR_Z
         || ROZLVAT.prefix(SSTALE.AO,DOK.ref());
            {? ROZLVAT.first() || ROZLVAT.del() ?}
         ?}
      || {? exec('is_rozl_cz','fks_vat')
         || VPOZ_ROZ.index('VPOZ_ROZ'); VPOZ_ROZ.prefix(VPOZ.ref());
            {? VPOZ_ROZ.first()
            || {!
               |? VPOZ_ROZ.cntx_psh();
                  exec('wyc_vatg1','dok_fks_ks',VPOZ_ROZ.OKRO_F().ROK().KOD,ROK.ref(),,_a);
                  VPOZ_ROZ.cntx_pop();
                  VPOZ_ROZ.first()
               !}
            ?}
         ?};
         ROZLVAT.prefix(SSTALE.AO,DOK.ref());
         {? _a & VPOZ.OKRVAT<>null
         || exec('wyc_vatg1','dok_fks_ks',VPOZ.OKRVAT().ROK().KOD,ROK.ref(),VPOZ.OKRVAT,_a)
         ?};
         {? ROZLVAT.first() || {! |? ROZLVAT.del() !} ?}
      ?};
      VPOZ.next()
   !}
?}


\wyc_vatg1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wycofanie dokumentow z ewidencji VAT powiazanych z dokumentem zrodlowym
::   WE:  _a  - kod roku rozliczenia
::        _b  - rok rozliczenia
::       [_c] - okres rozliczenia VAT
::       [_d] - czy wywołanie przy wycofywaniu księgowania dokumentu
::  OLD: \wyc_vatg1/dok_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_d')<=0 || _d:=0 ?};
{? _b<>SSTALE.AR
|| {? ~DVAT.use('dvat__'+_a) || undo() ?};
   {? ~PVAT.use('pvat__'+_a) || undo() ?};
   DVAT.index('DOK'); DVAT.prefix();
   PVAT.index('NR'); PVAT.prefix()
?};
{? POMOC.KNAG | var_pres('TmpKn')>0
|| OKRO_F.cntx_psh(); ROK_F.cntx_psh();
   OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
   _rvat:={? exec('sprvpozv','dok_fks',DOK.ref) || DOK.RVAT().RVAT || VPOZ.RVAT().RVAT ?};
   {? OKRO_F.find_ge(DOK.DTW)
   || _ok:={? var_press('_c')>0
           || DVAT.find_key(DOK.ref(),OKRO_F.ref(),_c,_rvat)
           || DVAT.find_key(DOK.ref(),OKRO_F.ref())
           ?}
   ?};
   OKRO_F.cntx_pop(); ROK_F.cntx_pop()
|| _ok:={? var_press('_c')>0 || DVAT.find_key(DOK.ref(),SSTALE.AO,_c) || DVAT.find_key(DOK.ref(),SSTALE.AO) ?}
?};
{? _ok
|| {? _d & ~exec('sprvpozd','dok_fks',DOK.ref) & ~(POMOC.KNAG | var_pres('TmpKn')>0)
   || DVAT.cntx_psh(); 
      {? var_press('_c')>0
      || DVAT.prefix(DOK.ref(),SSTALE.AO,_c)
      || DVAT.prefix(DOK.ref(),SSTALE.AO)
      ?};
      {! |?
         DVAT.cntx_psh();
         exec('wyc_vat','dok_fks_ks');
         DVAT.cntx_pop();
         DVAT.next()
      !};
      DVAT.cntx_pop()
   || DVAT.cntx_psh();
      exec('wyc_vat','dok_fks_ks');
      DVAT.cntx_pop()
   ?}
?};
{? _b<>SSTALE.AR
|| {? ~DVAT.use('dvat__'+SSTALE.AR().KOD) || undo() ?};
   {? ~PVAT.use('pvat__'+SSTALE.AR().KOD) || undo() ?}
?}


\wyc_vat
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Usuniecie dokumentu z ewidencji VAT
::  OLD: \wyc_vat/dok_ks.fml
::----------------------------------------------------------------------------------------------------------------------
PVAT.prefix(DVAT.ref());
{? PVAT.first()
|| {! |?
      {? ~POMOC.KNAG | PVAT.KNAG
      ||
         _vpoz_roz:=PVAT.VPOZ_ROZ;
         _delr:=PVAT.del(,1);
         {? _delr & _vpoz_roz
         || VPOZ_ROZ.prefix();
            {? VPOZ_ROZ.seek(_vpoz_roz)
            || {? ~VPOZ_ROZ.del(,1)
               || _delr:=0
               ?}
            || _delr:=0
            ?}
         ?};
         {? _delr=1
         || 0
         |? _delr=0
         || undo(); 0
         || 1
         ?}
      || PVAT.next()
      ?}
   !}
|| {? KOMT=''|| KOMT:='\nNie znaleziono pozycji dokumentu VAT ' ?}; undo()
?};
{? PVAT.size() || return() ?};
DVAT.index('NR'); DVAT.prefix(DOK.KRAJ,DVAT.OBR,DVAT.RVAT);

_par461:=PAR_SKID.get(461)='T';
{? _par461
|| _size:=DVAT.size; _ile:=1;
   DVAT.cntx_psh();
   {! _i:=1..+_size
   |? _ile=1
   |! DVAT.prefix(DOK.KRAJ,DVAT.OBR,DVAT.RVAT,_i); _ile:=DVAT.size()
   !};
   DVAT.cntx_pop()
|| _ile:=0
?};
_dvatref:=DVAT.ref();
{? _par461 & DVAT.next() & DVAT.prev() & DVAT.ref()=_dvatref
|| {? DVAT.del(,1)=0 || undo() ?};
   {? _ile=1
   || {! |?
         DVAT.NR-=1;
         {? ~DVAT.put() || undo() ?};
         DVAT.next()
      !}
   ?}
|| {? DVAT.del(,1)=0 || undo() ?}
?}


\fks_ksr_params
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Parametry pracy obszaru roboczego ksiegi rachunkowe
::----------------------------------------------------------------------------------------------------------------------
_rok:=SSTALE.AR;
_okres:=SSTALE.AO;
_odd:=OPERATOR.DEPT;
_wal:=SSTALE.WAL;
{? __PARSES.editDom('FKS')
|| pa:=SSTALE.WAL;
   AN.hdr_sel();
   AN.hdr_sel(' w %1'@[SSTALE.WAL().KOD]);
   AN.index('WALSYM');
   AN.prefix(pa);
   OKRO_F.index('ROK');
   OKRO_F.prefix(SSTALE.AR);
   OKRO_F.first();
   {? _rok<>SSTALE.AR | _odd<>OPERATOR.DEPT
   || exec('context','dok_fks_ks',0)
   ?};
   {? _rok<>SSTALE.AR | _okres<>SSTALE.AO | _odd<>OPERATOR.DEPT | _wal<>SSTALE.WAL
   || exec('set_var','dok_fks_ks');
      exec('set_global','!fks_ksr_zbob');
      exec('set_global','!fks_ksr_zboc');
      _prfx:=exec('maska2prefix','konto',ROZNE.MASKA);
      {? _prfx<>'?' & SSTALE.AO().POCZ=ROZNE.DATA_OD & OKRO_F.KON=ROZNE.DATA_DO
      || ROZNE.TYP_WYS:='P';
         ROZNE.PREFIX:=_prfx
      ?};
      exec('obj_kon','dok_fks_ks');
      SymKon.save();
      POZ.ndx_drop(); VAR_DEL.delete('pozndx');
      {? cur_tab(1,1)=POZ & cur_win(1,1)='WER_T'
      || _maska:=SSTALE.AR().KOD+form(SSTALE.AO().NR,-2);
         {? POZ.use('pozy'+_maska) & DOK.use('doku'+_maska)
         || POZ.f_clear();
            {? OPERATOR.DEPT=null
            || {? zakladka=8
               || POZ.index('KS_GV'); POZ.prefix('T','GL')
               || POZ.index('KS_PR'); POZ.prefix('T')
               ?};
               exec('suma_dok','dok_fks');
               {? POZ.first() || 1 ?}
            || {? zakladka=8
               || POZ.index('KS_GV'); POZ.prefix('T','GL',OPERATOR.DEPT().OD)
               || POZ.index('KS_PR'); POZ.prefix('T',OPERATOR.DEPT().OD)
               ?};
               exec('suma_dok','dok_fks');
               {? POZ.first() || 1 ?}
            ?}
         ?}
      || pozpar:=1
      ?};
      {? zakladka=2
      || exec('context1','dok_fks_ks')
      ?};
      {? zakladka=5 & var_press('WinRej')>0
      || grp_disp(WinRej.REJ,WinRej.WINREJ,1)
      ?}
   ?};
   exec('title','dok_fks',AN);
   1
|| 0
?}


\zapisy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zapisy księgowe
::   WE: _a  - konto
::      [_b] - waluta (SLO)
::      [_c] - Prefix lub Maska
::----------------------------------------------------------------------------------------------------------------------
_rm:=ROZNE.MASKA;
_od:=ROZNE.DATA_OD;
_do:=ROZNE.DATA_DO;
_pref:=ROZNE.PREFIX;
_typ_wys:=ROZNE.TYP_WYS;
ROZNE.TYP_WYS:={? var_pres('_c')>0 || _c || 'M' ?};
ROZNE.MASKA:=ROZNE.PREFIX:=_a;
exec('obj_kon','dok_fks_ks');
SymKon.save();
{? ~(var_pres('TAB_ZOIS')>0 & cur_tab(1,1)=TAB_ZOIS) || ROZNE.DATA_OD:=ROZNE.UT_OKROD().POCZ ?};
{? ROZNE.DATA_OD=date(0,0,0) || ROZNE.DATA_OD:={? OKRO_F.NR=0 || OKRO_F.ROK().POCZ_ROK || OKRO_F.ROK().KON_ROK ?} ?};
{? ~(var_pres('TAB_ZOIS')>0 & cur_tab(1,1)=TAB_ZOIS) || ROZNE.DATA_DO:=ROZNE.UT_OKRDO().KON ?};
{? ROZNE.DATA_DO=date(0,0,0) || ROZNE.DATA_DO:={? OKRO_F.NR=0 || OKRO_F.ROK().POCZ_ROK || OKRO_F.ROK().KON_ROK ?} ?};
_zak:=zakladka; zakladka:=10;
_wal:=SSTALE.WAL;
SSTALE.WAL:={? var_pres('_b')>0 || _b || FINFO.NAROD ?};
ROZRACH.TABELA:='POZ';
ROZRACH.SYMR:=_a;
POMOC.WALUTZAP:={? SSTALE.WAL=FINFO.NAROD || FINFO.NAROD || POMOC.WALUTA ?};
POZ.cntx_psh(); DOK.cntx_psh();
AN.cntx_psh();
_tab:=exec('zap_poz','dok_fks_ks');
_win:=exec('zap_win','dok_fks_ks',_tab,0);
_tab.win_sel(_win);
_tab.select();
AN.cntx_pop();
POZ.cntx_pop(); DOK.cntx_pop();
SSTALE.WAL:=_wal;
ROZRACH.SYMR:='';
VAR_DEL.delete('SymKon');
ROZNE.MASKA:=_rm;
ROZNE.DATA_OD:=_od;
ROZNE.DATA_DO:=_do;
zakladka:=_zak; ROZNE.PREFIX:=_pref;
ROZNE.TYP_WYS:=_typ_wys


\mask
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Formuła do otwierania masek
::  OLD: \mask/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
_c:=_a+form(_b,-2);
{? ~DOK.use('doku'+_c) || undo() ?};
{? ~POZ.use('pozy'+_c) || undo() ?};
::Poczatek modyfikacji dla Maclex Fiks 14-10-2009 AK [1120]
{? ~ML_POZB.use('pozb'+_c) || undo() ?};
::Koniec modyfikacji dla Maclex
{? ~POW.use('pow_'+_c) || undo() ?};
{? ~VPOZ.use('pozv'+_c) || undo() ?};
{? ~VPOZ_ROZ.use('pozr'+_c) || undo() ?};
{? ~AN.use('koan__'+_a) || undo() ?};
{? ~AN_SLU.use('koansl'+_a) || undo() ?}


\cntxmask
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Zapamietuje/przywraca kontekst tabel ustawianych w formuli mask
::   WE: _a - 1-psh 0-pop
::  OLD: \cntxmask/rozlicz.fml
::----------------------------------------------------------------------------------------------------------------------
::Poczatek modyfikacji dla Maclex Fiks 14-10-2009 AK [1120]
{? _a=1
|| DOK.cntx_psh(); POZ.cntx_psh(); POW.cntx_psh(); VPOZ.cntx_psh(); AN.cntx_psh(); AN_SLU.cntx_psh(); ML_POZB.cntx_psh()
|| DOK.cntx_pop(); POZ.cntx_pop(); POW.cntx_pop(); VPOZ.cntx_pop(); AN.cntx_pop(); AN_SLU.cntx_pop(); ML_POZB.cntx_pop()
?}
::Koniec modyfikacji dla Maclex


\poz_zn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Ustawianie znaczników na POZ
::   WE: _a=0 tylko POZ.A, _a=1 POZ.A i POZ.ZP, _a=2 wszystko
::  OLD: \poz_zn/akcept.fml
::----------------------------------------------------------------------------------------------------------------------
POZ.cntx_psh(); POZ.index('DOK'); POZ.prefix(DOK.ref);
{? POZ.first
|| {! |?
      POZ.A:='T';
      {? _a=1 | _a=2 || POZ.ZP:='T' ?};
      {? _a=2 || POZ.ZK:='T' ?};
      POZ.put(); POZ.next()
   !}
?};
POZ.cntx_pop()


\zap_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Tworzy tabele tymczasowa z zapisami
::----------------------------------------------------------------------------------------------------------------------
_mdok:=_mpoz:='';
ROK_F.index('KOD'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.first()
|| _tak:=0;
   {!
   |? _pocz:=ROK_F.POCZ_ROK<=ROZNE.DATA_OD & ROK_F.KON_ROK>=ROZNE.DATA_OD;
      _kon:=ROK_F.POCZ_ROK<=ROZNE.DATA_DO & ROK_F.KON_ROK>=ROZNE.DATA_DO;
      {? _tak=0 & _pocz
      || _tak:=1
      ?};
      {? _tak
      || _mdok+='\'doku'+ROK_F.KOD+'*\',';
         _mpoz+='\'pozy'+ROK_F.KOD+'*\','
      ?};
      {? _tak & _kon
      || _tak:=0
      ?};
      ROK_F.next()
   !}
?};
{? var_pres('TAB_ZOIS')<=0 | TAB_ZOIS.REK<>'G'
   | cur_tab(1,1)=ZapPoz.TAB | cur_tab(1,1)<>TAB_ZOIS | ROZNE.PAR4=0 | ROZNE.PAR4=2
|| {? PAR_SKID.get(209)='T'
   || _kon:=sql('select SYM from @AN where SYM like \':_a\'',exec('str_sql','#sql',ROZNE.MASKA))
   || _kon:=sql('select SYM from AN where SYM like \':_a\'',exec('str_sql','#sql',ROZNE.MASKA))
   ?}
|| {? ROZNE.PAR4=5
   || KON_SCH.cntx_psh();
      KON_SCH.index('IND_LP'); KON_SCH.prefix(ROZNE.KON_WYDR);
      _ilekont:=KON_SCH.size();
      _smaska:=_szakres:='';
      _dlug:=SSTALE.AR().SYNT;
      KS.cntx_psh(); KS.index('SYM'); KS.prefix(SSTALE.AR);
      {? KON_SCH.first()
      || {! |?
            {? KON_SCH.MZ='M'
            || _smaska+='SYM like \'%1\' or '[exec('str_sql','#sql',KON_SCH.MASKA)]
            || _symdo1:=KON_SCH.ZDO;
               _symdo2:='';
               {? KS.find_key(_dlug+_symdo1) & KS.next()
               || _symdo2:=KS.SYM
               ?};
               _szakres+='SYM>=\'%1\' and SYM<=\'%2\' or '[KON_SCH.ZOD,_symdo2]
            ?};
            KON_SCH.next()
         !}
      ?};
      KS.cntx_pop();
      KON_SCH.cntx_pop();
      _schwhere:=(_szakres+_smaska)-3;
      {? PAR_SKID.get(209)='T'
      || _kon:=sql('select SYM from @AN where %1'[_schwhere])
      || _kon:=sql('select SYM from AN where %1'[_schwhere])
      ?}
   || TAB_ZOIS.cntx_psh(); TAB_ZOIS.prefix();
      _kon:=sql('select SYM_KON as SYM from :_a where ANALIT=\'T\'',TAB_ZOIS);
      TAB_ZOIS.cntx_pop()
   ?}
?};
_tab:=sql(
   'select '+
   '/*+MASK_FILTER(DOK,:_e) MASK_FILTER(POZ,:_f) */ '+
   'DOK.ZAR as Zarejestrował, DOK.DR as "Data rejestracji", DOK.ZAA as Zaakceptował, DOK.DAKC as "Data akceptacji", '+
   'DOK.KSIEGOWY as Zaksięgował, DOK.DKS_K as "Data zaksięgowania końcowego", POZ.PKON as Maska, '+
   'POZ.SUM as "Kwota pozycji", POZ.SUMW as "Kwota w walucie",POZ.KON_OPIS as "Opis analityczny konta", '+
   'DOK.NIP, DOK.KH as "Kontrahent zbiorczy", DOK.D4 as "Data otrzymania", DOK.MIASTO as Miasto, '+
   'ODD.OD as Jednostka, REJ.KOD as "Symbol rejestru", DOK.NR as Numer, POZ.POZ as "Nr pozycji w dokumencie", '+
   'DOK.DTW as Zapis, POZ.KON as Konto, '+
   'POZ.OP as Opis, POZ.WN as Wn, POZ.MA as Ma, POZ.WN_WAL as "Wn w walucie", POZ.MA_WAL as "Ma w walucie", '+
   'POZ.STR as Strona, POZ.REFERENCE as REF, '+
   'SLO.REFERENCE as SLO_REF, SLO.KOD as "Nr wystawcy", '+
   'SLO.TR as "Nazwa wystawcy", DOK.KH as "Nazwa kontrahenta", '+
   'POZ.SUM, POZ.SUMW, POZ.WN_WAL, POZ.MA_WAL, POZ.OP as OP, REJ.KOD, DOK.KH, DOK.D4 as DAT_OTRZ, POZ.STR, '+
   'SLO.KOD as WYS, POZ.POZ, DOK.NK as "Symbol dokumentu"'+
   {? Plugin.runnable('FKS_FLD_ZAP') || Plugin.run('FKS_FLD_ZAP') || ' ' ?} +
   'from @POZ '+
   'join @DOK using (POZ.DOK,DOK.REFERENCE) '+
   'join ODD using (POZ.DOK_ODD,ODD.REFERENCE) '+
   'join REJ using (POZ.DOK_REJ,REJ.REFERENCE) '+
   'left join SLO using (DOK.WYS,SLO.REFERENCE) '+
   'where '+
   'POZ.KON between \''+ROZNE.KS_OD().SYM+'\' and \''+ROZNE.KS_DO().SYM+%255+'\'' +
   'and DOK.DTW between to_date(:_b) and to_date(:_c) '+
   'and DOK.ZP=\'T\' '+
   'and KON in (select SYM from :_a) '+
   {? SSTALE.WAL=FINFO.NAROD || '' || 'and POZ.WAL=:_d ' ?}+
   {? ROZNE.ODD || 'and DOK.ODD=\''+$ROZNE.ODD+'\' ' || '' ?}+
   'order by 20,15,19,17'
   ,
   _kon,ROZNE.DATA_OD,ROZNE.DATA_DO,SSTALE.WAL,_mdok-1,_mpoz-1
);
_tab.fld_attr('SUM',2,2);
_tab.fld_attr('WN_WAL',2,2);
_tab.fld_attr('MA_WAL',2,2);
_tab.fld_attr('POZ',2,2);
_tab.fld_attr('WYS',2,2);
_tab.fld_attr('REF',2,2);
_tab.fld_attr('SLO_REF',2,2);
_tab.fld_attr('STR',2,2);
_tab.fld_attr('OP',2,2);
_tab.fld_attr('DAT_OTRZ',2,2);
_tab.fld_attr('KH',2,2);
_tab.fld_attr('SUMW',2,2);
_tab.fld_attr('KOD',2,2);
_tab


\zap_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Okno do zapisów
::   WE: _a - tabela
::       _b - dodać akcje zakresy
::----------------------------------------------------------------------------------------------------------------------
_o:=_a.mk_sel('Księgowania dla %1 od %2 do %3'@[ROZNE.MASKA,$ROZNE.DATA_OD,$ROZNE.DATA_DO],'P',,'#zappzosel',,,,,'U');
_a.win_fld(_o,,'JEDNOSTK',,,,,,'Jednostka księgowa'@);
_a.win_fld(_o,,'KOD',,,,,,'Rejestr'@);
_a.win_fld(_o,,'NUMER',,,5,,,'Numer'@);
_a.win_fld(_o,,_a.fld_acr(18),,,5,,,'Pozycja'@);
_a.win_fld(_o,,'ZAPIS',,,,,,'Data zapisu'@);
_a.win_fld(_o,,'KONTO',,,22,,,'Konto'@);
_a.win_fld(_o,,_a.fld_acr(21),,,20,,,'Opis'@);
_a.win_fld(_o,,_a.fld_acr(29),,,15,,,'Nr wystawcy'@);
_a.win_fld(_o,,_a.fld_acr(30),,,22,,,'Nazwa wystawcy'@);
_a.win_fld(_o,,'NIP',,,12,,,'NIP'@);

{? SSTALE.WAL=FINFO.NAROD
|| _a.win_fld(_o,,'WN',,,16,2,,'Wn'@);
   _a.win_fld(_o,,'MA',,,16,2,,'Ma'@)
|| _a.win_fld(_o,,_a.fld_acr(24),,,16,2,,'Wn'@);
   _a.win_fld(_o,,_a.fld_acr(25),,,16,2,,'Ma'@)
?};
{? _b
|| {! _ii:=0..1
   |! _a.win_act(_o,_ii,'Formuła','&Zmień zakres'@@,,,"exec('fks_ksr_zamk_zakr','dok_fks_ks')",,1,,,,'Z')
   !};
   _a.win_btn(_o,'text='+'&Zmień zakres'@+',btn_label_align=center,panel=right,align=begin','menu:Z')
?};
_a.win_act(_o,,'Formuła','Zapisy &dokumentu'@@,,,"exec('zap_kont','dok_fks')",,,,,,'D');
_a.win_btn(_o,'text='+'Zapisy &dokumentu'@+',btn_label_align=center,panel=right,align=begin','menu:D');
_a.win_act(_o,,'Formuła','Na&główek dokumentu'@@,,,"exec('dok_poz','dok_fks')",,,,,,'G');
_a.win_act(_o,,'Formuła','Wyróż&niki'@@,,,"exec('roz_poz','!fks_dks_dawr')",,,,,,'N');
_a.win_act(_o,,'Formuła','&Opis konta'@@,,,"exec('op_konta','konto',cur_tab(1,1).KONTO)",,,,,,'O');
_a.win_act(_o,,'Formuła','Druku&j'@@,,,"exec('druk_zap2','!fks_ksr_zbka')",,,1,"exec('bgp_poz','dok_fks','fks_w_ksieg',cur_tab(1,1))",,'J',,'icon=print');
_a.win_act(_o,,'Formuła','O&broty dla okresów'@@,,,"exec('obr_okr','!fks_ksr_zbka',cur_tab(1,1))",,,,,,'T');
_a.win_act(_o,,'Formuła','Oblicz obro&ty'@@,,,"
   exec('obl_obr','dok_fks',ZapPoz.TAB,{? var_pres('dwiewalo')>0 & ~dwiewalo || ZapPoz.TAB.WN || {? ZapPoz.TAB.STR='Wn' || ZapPoz.TAB.SUMW || 0 ?}?},
                                       {? var_pres('dwiewalo')>0 & ~dwiewalo || ZapPoz.TAB.MA || {? ZapPoz.TAB.STR='Ma' || ZapPoz.TAB.SUMW || 0 ?}?},
                                       {? var_pres('dwiewalo')>0 & ~dwiewalo || 0 || ZapPoz.TAB.WN ?},
                                       {? var_pres('dwiewalo')>0 & ~dwiewalo || 0 || ZapPoz.TAB.MA ?})
",,,1,"exec('be_gobr','dok_fks')","exec('ae_gobr','dok_fks')",'B');
_menu:='Dan&e źródłowe'@;
_a.win_act(_o,,'Menu',_menu,,,,,,,,,'E');
_a.win_btn(_o,'text='+_menu+',btn_label_align=center,panel=right,align=begin','menu:E');
_a.win_act(_o,,'Formuła','Dokument z innej ewidencji'@@,_menu,,"exec('dok_zrd','dok_fks')",,,,,,'D');
_a.win_act(_o,,'Formuła','--X',_menu,,,,,,,,'X');
_a.win_act(_o,,'Formuła','E-dokument'@@,_menu,,"exec('efakview','dok_fks')",,,,,,'E');
_a.win_act(_o,,'Formuła','--Y',_menu,,,,,,,,'Y');
_a.win_act(_o,,'Formuła','&Załaczniki'@@,_menu,,"exec('dok_zal','dokum_zal',(cur_tab(1,1).REF-8)+4,BB.sqlint(cur_tab(1,1).REF))",,,,,,'Z');
_menu:='Raport&y'@;
{! _ii:=0..1
|! _a.win_act(_o,_ii,'Menu',_menu,,,,,,,,,'Y');
   _a.win_act(_o,_ii,'Formuła','&Wydruki kartoteki księgowań'@@,_menu,,"exec('main','!fks_ksr_zakk')",,,,,,'W');
   _a.win_act(_o,_ii,'Formuła','&Sprawozdania według analityk'@@,_menu,,"exec('main','!fks_ksr_zbwa')",,,,,,'S');
   _a.win_act(_o,_ii,'Formuła','&Zestawienie obrotów i sald'@@,_menu,,"exec('main','!fks_ksr_zbos')",,,,,,'Z');
   _a.win_act(_o,_ii,'Formuła','Zapytania S&QL'@@,_menu,,"exec('main','!fks_ksr_zsql')",,,,,,'Q');
   {? _b
   || _a.win_act(_o,_ii,'Formuła','P&arametry pracy'@@,,,"exec('fks_ksr_params','dok_fks_ks')",,,,,,'P')
   ?}
!};
_a.win_act(_o,,'Szukaj');
_a.win_act(_o,,'Kolejność');
_a.win_act(_o,,'Rekord',,,,"
   _maska:=(cur_tab(1,1).REF-8)+4;
   POZ.use('pozy'+_maska);
   VPOZ.use('pozv'+_maska);
   DOK.use('doku'+_maska);
   POW.use('pow_'+_maska);
   POZ.prefix();
   {? POZ.seek(BIT.sqlint(cur_tab(1,1).REF),)
   || exec('opis_kon','!fks_ksr_zbka')
   ?}
");
_a.win_act(_o,,'Wyświetl',,,,"exec('zap_spac','fks_ksr')");
_o


\zap_win2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Tworzy okno podsumowań dla zapisów
::   WE: _a - tabela
::----------------------------------------------------------------------------------------------------------------------
_o:=_a.mk_sel('Podsumowania dla zapisów'@,'N',,'zappodsum',10,,,,'N',,,,,'normal');
_a.win_fld(_o,,'PUSTE',,,115,,,''@,1);
_a.win_fld(_o,,'NAZWA',,,5,,,''@,1);
_a.win_fld(_o,,'WN',,,16,,,'Wn'@);
_a.win_fld(_o,,'MA',,,16,,,'Ma'@);
_o


\zap_sum2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Tworzy tabele tymczasowa z podsumowaniami dla zapisów
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(,'LP','INTEGER','liczba','PUSTE','STRING[20]','','NAZWA','STRING[20]','','WN','REAL','Winien','MA','REAL','Ma');
SUM.SUMWN:=SUM.SUMMA:=0;
{? ZapPoz.TAB.first()
|| {! |?
      SUM.SUMWN+=ZapPoz.TAB.WN;
      SUM.SUMMA+=ZapPoz.TAB.MA;
      ZapPoz.TAB.next()
   !}
?};
_tab.LP:=1;
_tab.NAZWA:='Suma';
_tab.WN:=SUM.SUMWN;
_tab.MA:=SUM.SUMMA;
_tab.add();
_tab.LP:=2;
_tab.NAZWA:='Saldo';
{? SUM.SUMWN>SUM.SUMMA
|| _tab.WN:=SUM.SUMWN-SUM.SUMMA;
   _tab.MA:=0
|| _tab.WN:=0;
   _tab.MA:=SUM.SUMMA-SUM.SUMWN
?};
_tab.add();
_tab


\zap_sum2_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Aktualizuje tabele tymczasowa z podsumowaniami dla zapisów
::----------------------------------------------------------------------------------------------------------------------
SUM.SUMWN:=SUM.SUMMA:=0;
ZapPoz.TAB.cntx_psh();
_all:=ZapPoz.TAB.size();
{? ZapPoz.TAB.first()
|| {! |?
      {? SSTALE.WAL=FINFO.NAROD
      || SUM.SUMWN+=ZapPoz.TAB.WN;
         SUM.SUMMA+=ZapPoz.TAB.MA
      || SUM.SUMWN+=ZapPoz.TAB.WN_WAL;
         SUM.SUMMA+=ZapPoz.TAB.MA_WAL
      ?};
      ZapPoz.TAB.next()
   !}
?};
{? ZapPoz.TAB2.first()
|| {! |?
      ZapPoz.TAB2.del()
   !}
?};
ZapPoz.TAB2.LP:=1;
ZapPoz.TAB2.NAZWA:='Suma';
ZapPoz.TAB2.WN:=SUM.SUMWN;
ZapPoz.TAB2.MA:=SUM.SUMMA;
ZapPoz.TAB2.add();
ZapPoz.TAB2.LP:=2;
ZapPoz.TAB2.NAZWA:='Saldo';
{? SUM.SUMWN>SUM.SUMMA
|| ZapPoz.TAB2.WN:=SUM.SUMWN-SUM.SUMMA;
   ZapPoz.TAB2.MA:=0
|| ZapPoz.TAB2.WN:=0;
   ZapPoz.TAB2.MA:=SUM.SUMMA-SUM.SUMWN
?};
ZapPoz.TAB2.add();
ZapPoz.TAB2.first();
ZapPoz.TAB.cntx_pop();
ZapPoz.TAB2


\context
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia dziedzinę okien
::   WE: _a - start: 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
{? zakladka<>5 || return() ?};
{? ~_a
|| WinRej.REJ.erase()
?};
exec('fill_rej','dok_fks_ks');
WinRej.REJ.first();
{? ~_a
|| DOK.index('NRDZ');
   DOK.prefix('T','T',REJ.ODD,REJ.ref());
   DOK.first();
   grp_disp(WinRej.REJ,WinRej.WINREJ)
?}


\fill_rej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Uzupełnia tabelę z rejestrami
::  OLD: \fill_rej/!fks_ksr_zddz.fml
::----------------------------------------------------------------------------------------------------------------------
_all:=Perm.hasFull('FJKS');
{? _all & OPERATOR.DEPT=null
|| TREJ.TREE:=0;
   TREJ.NAZ:='Wszystkie';
   TREJ.TYP:='W';
   _nad:={? TREJ.add() || #TREJ.ref() ?}
|| _nad:=0
?};
ODD.cntx_psh();
ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
REJ.cntx_psh();
REJ.index('KOD');
USERSDEP.index('UNIK'); USERSDEP.prefix(REF.FIRMA,OPERATOR.USER);
{? OPERATOR.DEPT | ODD.first()
|| {!
   |? {? _all | USERSDEP.find_key(ODD.ref())
      || TREJ.TREE:=_nad;
         TREJ.TYP:='O';
         TREJ.NAZ:=ODD.OD;
         TREJ.REF:=#ODD.ref();
         {? TREJ.add()
         || _rej:=#TREJ.ref();
            REJ.prefix(SSTALE.AR,ODD.ref());
            {? REJ.first()
            || {!
               |? TREJ.TREE:=_rej;
                  TREJ.TYP:='R';
                  TREJ.NAZ:=REJ.KOD+' - '+REJ.NAZ;
                  TREJ.REF:=#REJ.ref();
                  TREJ.add();
                  REJ.next()
               !}
            ?}
         ?}
      ?};
      OPERATOR.DEPT=null & ODD.next()
   !}
?};
REJ.cntx_pop();
ODD.cntx_pop()


\context1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia dziedzinę wyswietlanych zapisów
::   WE: [_a] - odrysować
::----------------------------------------------------------------------------------------------------------------------
_tryb:=ZapPoz.TRYB;
{? _=0 || _a:=1 ?};
{? ROZNE.TYP_WYS='P'
|| {? POZ.name()+4<>SSTALE.AR().KOD+form(SSTALE.AO().NR,-2)
   || exec('open_tabele','open_tab','F')
   ?};
   POZ.f_clear();
   {? ROZNE.ODD
   || {? SSTALE.WAL<>FINFO.NAROD
      || 'waluta obca'; wys_sum:=1; ZapPoz.TRYB:=3;
         POZ.index('OKWAL_O'); POZ.prefix('T','T',SSTALE.WAL,ROZNE.ODD,ROZNE.PREFIX)
      || 'waluta narodowa'; wys_sum:=0; ZapPoz.TRYB:=1;
         POZ.index('OKWAL_O1'); POZ.prefix('T','T',ROZNE.ODD,ROZNE.PREFIX)
      ?}
   || {? SSTALE.WAL<>FINFO.NAROD
      || 'waluta obca'; wys_sum:=1; ZapPoz.TRYB:=3;
         POZ.index('OKWAL'); POZ.prefix('T','T',SSTALE.WAL,ROZNE.PREFIX)
      || 'waluta narodowa'; wys_sum:=0; ZapPoz.TRYB:=1;
         POZ.index('OKWAL1'); POZ.prefix('T','T',ROZNE.PREFIX)
      ?}
   ?};
   POZ.first();
   VAR_DEL.delete('PozFset')
|? ROZNE.TYP_WYS='M'
|| ZapPoz.TRYB:=2
|| ZapPoz.REPAINT:=0
?};
ZapPoz.REPAINT:=_tryb<>ZapPoz.TRYB | ZapPoz.TRYB=2;
exec('sumy','dok_fks_ks');
exec('title','dok_fks_ks');
{? zakladka<>7
|| {? _a & ~ZapPoz.REPAINT & zakladka<>5 || grp_disp(ZapPoz.TAB,ZapPoz.WIN,1) ?};
   {? _a & ZapPoz.REPAINT & cur_win(1,1)<>'C_REN_DZ' || sel_exit() ?}
?}


\sumy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Oblicza podsumowanie widoku
::----------------------------------------------------------------------------------------------------------------------
:: TODO: [MB] wyszarzenie pola narastająco gdy pojawi się funkcja
SUM.SUMWN:=SUM.POMOCWN:=0;
SUM.SUMMA:=SUM.POMOCMA:=0;
{? ROZNE.TYP_WYS='P'
|| OKRO_F.cntx_psh();
   AN.cntx_psh();
   _nr:=SSTALE.AO().NR;
   AN.index('WALSYM');
   AN.prefix(SSTALE.WAL,ROZNE.PREFIX);
   _allan:=AN.size();
   _aktan:=1;
   {? AN.first()
   || {!
      |? _procan:=(100*_aktan/_allan);
         progress(_procan,'Trwa przetwarzanie danych...'@);
         exec('sumy2','dok_fks_ks',_nr);
         _aktan+=1;
         AN.next()
      !}
   ?};
   prgs_clr();
   OKRO_F.cntx_pop();
   AN.cntx_pop()
|? ROZNE.TYP_WYS='M'
|| POZ.cntx_psh();
   {? POZ.f_active() & POZ.f_first()
   || {!
      |? {? -POZ.STR='wn'
         || SUM.SUMWN+=POZ.SUM
         || SUM.SUMMA+=POZ.SUM
         ?};
         POZ.f_next()
      !};
      POZ.f_first()
   ?};
   POZ.cntx_pop()
?};
{? ROZNE.TYP_WYS='M'
|| SUM.SALWN:=F.SWn(SUM.SUMWN, SUM.SUMMA);
   SUM.SALMA:=F.SMa(SUM.SUMWN, SUM.SUMMA)
|| SUM.SALWN:=F.SWn(SUM.POMOCWN, SUM.POMOCMA);
   SUM.SALMA:=F.SMa(SUM.POMOCWN, SUM.POMOCMA)
?}


\sumy2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Pomocnicza do sumy
::   WE: _a - nr okresu
::----------------------------------------------------------------------------------------------------------------------
OBR.index('AN_O');
{? OPERATOR.DEPT
|| OBR.prefix(AN.ref,OPERATOR.DEPT)
|| OBR.prefix(AN.ref)
?};
{? OBR.first()
|| {!
   |? {? OBR.OKRO().NR<_a
      || SUM.POMOCWN+=OBR.WN; SUM.POMOCMA+=OBR.MA
      |? OBR.OKRO=SSTALE.AO
      || SUM.SUMWN+=OBR.WN; SUM.SUMMA+=OBR.MA;
         SUM.POMOCWN+=OBR.WN;SUM.POMOCMA+=OBR.MA
      ?};
      OBR.next()
   !}
?}


\title
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia tytuł okna
::----------------------------------------------------------------------------------------------------------------------
ZapPoz.TAB.cntx_psh();
ZapPoz.TAB.win_sel(ZapPoz.WIN);
ZapPoz.TAB.hdr_sel();
{? ROZNE.ODD<>null()
|| ODD.cntx_psh();
   ODD.seek(ROZNE.ODD);
   ZapPoz.TAB.hdr_sel(' dla jednostki księgowej %1'[ODD.OD]);
   ODD.cntx_pop()
?};
{? SSTALE.WAL<>FINFO.NAROD
|| SLO.cntx_psh();
   SLO.prefix();
   SLO.seek(SSTALE.WAL);
   ZapPoz.TAB.hdr_sel(' dla waluty %1'[SLO.KOD]);
   SLO.cntx_pop()
?};
{? ZapPoz.TRYB<>2
|| ZapPoz.TAB.hdr_sel({? ROZNE.TYP_WYS='P'
                       || {? ROZNE.PREFIX<>'' || ' dla kont \'' + ROZNE.PREFIX +'\'' || '' ?}
                       || {? ROZNE.MASKA<>'' || ' dla kont \'' + ROZNE.MASKA +'\'' || '' ?}
                       ?})
::   {? ROZNE.TYP_WYS='M' || ' od '+$ROZNE.DATA_OD+' do '+$ROZNE.DATA_DO || '' ?}
?};
ZapPoz.TAB.cntx_pop()


\set_var
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia zmienne po zmianie stałych
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
SSTALE.AO();
{? OKRO_F.POCZ=date(0,0,0) || {? OKRO_F.NR=0 || OKRO_F.next || OKRO_F.prev ?} ?};
ROZNE.ODD:=OPERATOR.DEPT;
ROZNE.DATA_OD:=OKRO_F.POCZ;
ROZNE.DATA_DO:=OKRO_F.KON;
ROZNE.UT_OKROD:=ROZNE.UT_OKRDO:=SSTALE.AO;
ROZNE.UT_ROKOD:= ROZNE.UT_ROKDO:=SSTALE.AR;
OKRO_F.cntx_pop()


\fks_ksr_zamk_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Akcja zakres okna C_WYS1 tabeli POZ
::----------------------------------------------------------------------------------------------------------------------
{? ROZNE.TYP_WYS<>'M' || ROZNE.TYP_WYS:='M' ?};
ROZNE.win_edit('ZAKR_WYS');
{? ROZNE.edit("
      {? ROZNE.TYP_WYS='M'
      || _r:=__CHK.record2(
               ROZNE,
               'DATA_OD','Od daty',
               'DATA_DO','Do daty'
            );
         {? _r=''
         || {? ROZNE.DATA_DO < ROZNE.DATA_OD
            || FUN.info('Nieprawidłowy zakres dat.'@); 'DATA_OD'
            || 1
            ?}
         || _r
         ?}
      || 1
      ?}
   ")
|| _prfx:='?';
   {? _prfx<>'?' & SSTALE.AO().POCZ=ROZNE.DATA_OD & OKRO_F.KON=ROZNE.DATA_DO
   || ROZNE.TYP_WYS:='P';
      ROZNE.PREFIX:=_prfx
   ?};
   exec('obj_kon','dok_fks_ks');
   SymKon.save();
   exec('context1','dok_fks_ks')
?}


\ks_dok1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??
:: OPIS: Księgowanie pojedynczego dokumentu
::   WE: _a - Pytanie o księgowanie
::       _b - Wyśwetlać podsumowanie
::  OLD: \ks_dok1/zaks.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
{? ~DOK.get()
|| {? DOK.sel_size()=0
   || FUN.info('Nie znaleziono dokumentu \'%1\'.'@ [DOK.NK])
   ?};
   _ok:=-1
|? DOK.A<>'T'
|| {? DOK.sel_size()=0
   || FUN.info('Dokument \' %1 \' jest w trakcie rejestracji.'@ [DOK.NK])
   ?}
|? DOK.ZP='T'
|| {? DOK.sel_size()=0 & _b
   || {? DOK.ZK='T'
      || FUN.info('Dokument \'%1\' jest już zaksięgowany  końcowo.'@[DOK.NK])
      || FUN.info('Dokument \'%1\' jest już zaksięgowany  próbnie.'@[DOK.NK])
      ?}
   ?};
   _ok:=1
|| {? DOK.r_lock(1,1)
   || {? DOK.get()
      || KOMT:='';
         {? DOK.ZP='N'
         || _ok:=exec('ks_dok','dok_fks_ks',0);
            {? _ok || exec('akc_par','rozrach') ?}
         ?};
         echo('');
         {? _ok
         || {? DOK.sel_size()<>0
            || DKS_OK+=1
            ?}
         || {? DOK.sel_size()=0 || FUN.info('Księgowanie dokumentu \'%1\' nie powiodło się.%2'@[DOK.NK,KOMT]) ?}
         ?};
         &KOMT
      || {? DOK.sel_size()=0 || FUN.info('Księgowanie dokumentu nie powiodło się.'@) ?}
      ?};
      DOK.r_unlock()
   || {? DOK.sel_size()=0 || FUN.info('Dokument obsługuje inny operator.'@) ?}
   ?}
?};
DOK.cntx_psh();
DOK.clear();
{? _ok & DOK.get() & (DOK.ZP='T' | DOK.ZK ='T')
|| {? var_pres('KSEF',DOK)>0 & PAR_SKID.get(448)='T' & DOK.KSEF='T'
   || exec('dok_edokument','dok_fks1',0)
   ?};
   {? PAR_SKID.get(440)='T' & DOK.KSEF<>'T'
   || exec('bl_send_dok','dok_fks1',1)
   ?}
?};
DOK.cntx_pop();
_ok


\czywal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ
:: OPIS: Czy rozrachunek wielowalutowy - sprawdza czy OP ma braciszka w walucie obcej
::   WE: _a - ref waluty
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0; _odd:=OP.ODD; _konto:=OP.AN; _sym:=OP.SYM; _usym:=OP.SYM_ROK;
{? 1
|| OP.cntx_psh(); OP.index('KON_O'); OP.prefix();
   SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(FINFO.SLWAL().SLU);
   {? SLO.first()
   || {! |?
         {? SLO.ref()<>FINFO.NAROD
         || {? OP.find_key(SLO.ref(),_odd,_konto,_sym,_sym,_usym)
            || _wyn:=(SLO.ref()<>_a & exec('euro','waluty',_a,SLO.ref()))
            ?}
         ?};
         ~_wyn & SLO.next()
      !}
   ?};
   _wyn:={? _wyn || SLO.KOD || '' ?};
   SLO.cntx_pop(); OP.cntx_pop()
?};
_wyn


\obj_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42]
:: OPIS: Obiekt do obsługi rozrachunków w następnych latach
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new('first','next','PKON','KON','getKON','ROK','cntx_psh','cntx_pop','TRYB','LP','PROK','use','getACTYEAR');
_obj.PKON:='';
_obj.LP:=0;
_obj.TRYB:=PAR_SKID.get(76);
_obj.first:="
   .LP:=0;
   .ROK:=SSTALE.AR;
   .next()
";
_obj.next:="
   .LP+=1;
   {? .TRYB<>'N'
   || ROK_F.index('AKT_OP');
      ROK_F.prefix(REF.FIRMA,'T');
      {? ROK_F.seek(.ROK)
      || .PROK:=ROK_F.ref();
         {? ROK_F.next()
         || .ROK:=ROK_F.ref();
            .KON:=.getKON();
            {? .KON<>''
            || .use();
               1
            ?}
         ?}
      ?}
   ?}
";
_obj.getKON:="
   _an:='';
   OKRO_F.cntx_psh();
   OKRO_F.index('ROK');
   OKRO_F.prefix(ROK_F.ref());
   {? OKRO_F.first()
   || POZ.cntx_psh();
      POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2));
      POZ.index('PKON'); POZ.prefix(.PKON,);
      {? POZ.first()
      || {!
         |? {? POZ.KON<>''
            || _an:=POZ.KON;
               0
            || POZ.next()
            ?}
         !}
      ?};
      POZ.cntx_pop()
   ?};
   OKRO_F.cntx_pop();
   {? _an=''
   || _an:=exec('an_bo','zam_okr_rok',.PKON,0,0)
   ?};
   {? _an='' & exec('rowne','konto',.PKON,.PKON,1,.PROK,.ROK,1)
   || _an:=.PKON
   ?};
   _an
";
_obj.cntx_psh:="
   OP.cntx_psh();
   ZAP_OP.cntx_psh();
   STANY.cntx_psh();
   OP_PROJ.cntx_psh();
   ZAP_PROJ.cntx_psh();
   PAR_NAG.cntx_psh();
   PAR_POZ.cntx_psh();
   D_RB.cntx_psh();
   ROK_F.cntx_psh()
";
_obj.cntx_pop:="
   OP.cntx_pop();
   ZAP_OP.cntx_pop();
   STANY.cntx_pop();
   OP_PROJ.cntx_pop();
   ZAP_PROJ.cntx_pop();
   PAR_NAG.cntx_pop();
   PAR_POZ.cntx_pop();
   D_RB.cntx_pop();
   ROK_F.cntx_pop()
";
_obj.use:="
   OP.use('operac'+ROK_F.KOD);
   ZAP_OP.use('rozzap'+ROK_F.KOD);
   STANY.use('stany_'+ROK_F.KOD);
   OP_PROJ.use('rozpro'+ROK_F.KOD);
   ZAP_PROJ.use('rozprz'+ROK_F.KOD);
   PAR_NAG.use('parnag'+ROK_F.KOD);
   PAR_POZ.use('parpoz'+ROK_F.KOD);
   D_RB.use('dr__fk'+ROK_F.KOD)
";
:: OPIS: Zwraca ostatni aktywny rok dla rozrachunków, musi być większy bądź równy podanemu
::   WY: ROK_F.ref()
::   WE: _a - data początku bieżącego roku
_obj.getACTYEAR:="
   _zwrot:=0;
   {? _ & type_of(_a)=type_of(date()) & _a<>date(0,0,0)
   || ROK_F.cntx_psh(); ROK_F.index('AKT_OP'); ROK_F.prefix(REF.FIRMA,'T');
      {? ROK_F.last() & ROK_F.POCZ_ROK>=_a
      || _zwrot:=ROK_F.ref()
      ?};
      ROK_F.cntx_pop()
   ?};
   _zwrot
";
_obj


\mod_op2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42]
:: OPIS: Dodaje zapisy rozrachunków w kolejnych latach
::   WE: _a - 0-zapis w walucie rozrachunku 1-zapis w walucie narodowej dla wielowalitowego
::       _b - objekt
::----------------------------------------------------------------------------------------------------------------------
_obj:=_b;
{? form(POZ.ID)<>'' & PAR_SKID.get(76)<>'N'
|| _fkon:=_obj.PKON:=POZ.KON;
   _rmk:=null;
   RMK.cntx_psh(); RMK.index('NAZ'); RMK.prefix();
   {? RMK.find_key(POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK,POZ.TID,)

   || _rmk:=RMK.ref()
   ?};
   RMK.cntx_pop();
   _obj.cntx_psh();
   {? _obj.first()
   || {!
      |? POZ.KON:=_obj.KON;
         exec('mod_op','dok_fks_ks',_a);
         {? _rmk || exec('mod_rmk','dok_fks_ks',_rmk,_obj.ROK) ?};
         _obj.PKON:=_obj.KON;
         _obj.next()
      !}
   ?};
   _obj.cntx_pop();
   POZ.KON:=_fkon
?}


\mod_rmk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [20.42]
:: OPIS: Kopiowanie zapisów dotyczących RMK w kolejnych latach
::   WE: _a - RMK.ref()
::       _b - rok do którego są kopiowane zapisy (ROK_F.ref())
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('KOD'); ROK_F.prefix();
_mas_new:=_mas_poz:='';
{? ROK_F.seek(_b)
|| _mas_new:=ROK_F.KOD;
   OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
   {? OKRO_F.first() || _mas_poz:=ROK_F.KOD+form(OKRO_F.NR,-2) ?};
   OKRO_F.cntx_pop()
?};
_mas_old:=SSTALE.AR().KOD;
RMK.cntx_psh(); RMK.use('rmkop_'+_mas_old); RMK.prefix();
{? _mas_new<>'' & _mas_old<>'' & _mas_poz<>'' & _a & RMK.seek(_a)
|| RMK.cntx_psh(); _konto:=RMK.KONP; _odd:=RMK.ODD;
   POZ.cntx_psh(); POZ.use('pozy'+_mas_poz);
   POZ.index('O_PKON'); POZ.prefix(_odd,_konto,);
   {? POZ.first || _konto:=POZ.KON ?};
   POZ.cntx_pop();
   _sr:=_nr:=0;
   RMK.KONP:=_konto; _sr:=#RMK.ref();
   RMK.use('rmkop_'+_mas_new); RMK.prefix();
   {? ~RMK.find_key(RMK.ODD,RMK.KONP,RMK.SYM,RMK.SYM,RMK.SYM_ROK)
   || RMK.add(); _nr:=#RMK.ref()
   || _nr:=#RMK.ref()
   ?};
   RRMK.cntx_psh();
   RRMK.use('rozrmk'+_mas_new); RRMK.prefix();
   {? _nr>0 & ~RRMK.find_key(RMK.ref())
   || RRMK.cntx_psh();
      RRMK.use('rozrmk'+_mas_old); RRMK.prefix();
      {? RMK.seek(_sr,'rmkop_'+_mas_old) & (RRMK.prefix(RMK.ref()); RRMK.first())
      || {! |?
            RRMK.cntx_psh();
            RRMK.use('rozrmk'+_mas_new); RRMK.prefix();
            {? RMK.seek(_nr,'rmkop_'+_mas_new)
            || RRMK.RMK:=RMK.ref();
               RRMK.add(1)
            ?};
            RRMK.cntx_pop();
            RRMK.next()
         !}
      ?};
      RRMK.cntx_pop()
   ?};
   RRMK.cntx_pop();
   RMK.cntx_pop()
?};
RMK.cntx_pop(); ROK_F.cntx_pop()


\obj_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Obiekt do zapamiętania kontekstu maski lub prefixu konta
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('SymKon')<=0
|| _obj:=obj_new(
      'KONTO','TYP',
      'save'
   );
   _obj.save:="
      .TYP:=ROZNE.TYP_WYS;
      .KONTO:={? .TYP='M' || ROZNE.MASKA || ROZNE.PREFIX ?}
   ";
   SymKon:=_obj
?}


\mod_mw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Uaktualnia salda Magazyn walut przy księgowaniu
::----------------------------------------------------------------------------------------------------------------------
_AddMWP:=" MAGWALP.prefix();
           MAGWALP.blank();
           MAGWALP.POZ:=POZ.ref(); MAGWALP.KON:=POZ.KON; MAGWALP.ODD:=POZ.DOK().ODD; MAGWALP.OKRO:=OKRO_F.ref();
           MAGWALP.KURS:=POZ.KURS$6; MAGWALP.WAL:=POZ.WAL; MAGWALP.DATA:=DOK.DTW;
           MAGWALP.PRZY_DEB:={? var_pres('_b')=type_of('') & (_b='P' | _b='D') || _b || 'P' ?};
           _kwota:={? MAGWALP.PRZY_DEB='D' || POZ.SUMW || POZ.SUMW ?};
           {? _a='Wn' || MAGWALP.WN:=_kwota || MAGWALP.MA:=_kwota ?};
           MAGWALP.add(0)
         ";
_AddMWR:=" MAGWALR.index('KON_O');
           MAGWALR.prefix();
           MAGWALR.blank();
           MAGWALR.POZ:=$POZ.ref(); MAGWALR.POZR:=null(); MAGWALR.KON:=POZ.KON; MAGWALR.ODD:=POZ.DOK().ODD; MAGWALR.OKRO:=OKRO_F.ref();
           MAGWALR.KURS:=POZ.KURS$6; MAGWALR.WAL:=POZ.WAL; MAGWALR.DATA:=DOK.DTW;
           MAGWALR.ROZ_SPL:={? var_pres('_b')=type_of('') & (_b='R' | _b='S') || _b || 'R' ?};
           _kwota:={? MAGWALR.ROZ_SPL='S' || POZ.SUMW || POZ.SUMW ?};
           {? _a='Ma' || MAGWALR.MA:=_kwota ||  MAGWALR.WN:=_kwota ?};
           MAGWALR.add(1)
         ";
KS.cntx_psh();
KS.index('SYM');
KS.prefix(ROK_F.ref());
{? KS.find_key(ROK_F.SYNT+POZ.KON) & KS.MW='T'
|| P_W.index('MW'); P_W.prefix(POZ.ref(),'mwp');
   {? P_W.first()
   || {! |?
            MAGWALP.prefix();
            {? MAGWALP.seek(P_W.MWUIDREF)
            || _kwota:=P_W.KWOTA;
               MAGWALP.WN-=_kwota;
               {? MAGWALP.put()
               || P_W.cntx_psh();
                  P_W.prefix(POZ.ref(),'mwr');
                  {? P_W.first()
                  || {! |?  {? P_W.X=0 & P_W.KWOTA<=_kwota
                            ||
                               MAGWALR.prefix();
                               {? MAGWALR.seek(P_W.MWUIDREF)
                               || {? MAGWALR.POZR=null()
                                  || {? P_W.KWOTA<MAGWALR.MA
                                     || MAGWALR.WN+=_kwota;
                                        {? MAGWALR.WN=MAGWALR.MA
                                        || MAGWALR.POZR:=POZ.ref();
                                           MAGWALR.MAGWAL:=MAGWALP.ref()
                                        ?};
                                        MAGWALR.put()
                                     || MAGWALR.WN:=_kwota;
                                        MAGWALR.POZR:=POZ.ref();
                                        MAGWALR.MAGWAL:=MAGWALP.ref();
                                        MAGWALR.put()
                                     ?}
                                  || MAGWALR.WN:=_kwota;
                                     MAGWALR.MA:=0;
                                     MAGWALR.POZR:=POZ.ref();
                                     MAGWALR.MAGWAL:=MAGWALP.ref();
                                     MAGWALR.add()
                                  ?}
                                || undo(); {? KOMT=''|| KOMT:='\nDane w magazynie walut uległy zmianie.\nWykonaj ponownie naliczenie RKB dla konta %1.'@[POZ.KON] ?}
                                ?};
                                0
                             || P_W.next()
                             ?}
                      !}
                  ?};
                  P_W.X:=1; P_W.put();
                  P_W.cntx_pop()
               ?}
            || undo(); {? KOMT=''|| KOMT:='\nDane w magazynie walut uległy zmianie.\nWykonaj ponownie naliczenie RKB dla konta %1.'@[POZ.KON] ?}
            ?};
            P_W.X:=1; P_W.put();
            P_W.next()
       !}
   || _saldo:=exec('saldo_mw','dok_fks_ks');
      {? KS.SALDO='A' & 1+POZ.STR='W'
      || {? _saldo<0
         || _AddMWR('Ma','S')
         || _AddMWP('Wn')
         ?}
      |? KS.SALDO='A' & 1+POZ.STR='M'
      || {? _saldo<0 | (_saldo=0 & 1+POZ.STR='M')
         || _AddMWP('Wn','D')
         || _AddMWR('Ma')
         ?}
      |? KS.SALDO='P' & 1+POZ.STR='M'
      || _AddMWP('Ma')
      |? KS.SALDO='P' & 1+POZ.STR='W'
      || _AddMWR('Wn')
      |? KS.SALDO='D'
      || {? _saldo<0 | (_saldo=0 & 1+POZ.STR='M')
         || {? 1+POZ.STR='W'
            || _AddMWR('Ma','S')
            || _AddMWP('Wn','D')
            ?}
         || {? 1+POZ.STR='W'
            || _AddMWP('Wn')
            || _AddMWR('Ma')
            ?}
         ?}
      ?}
   ?}
?};
KS.cntx_pop()


\wyc_mw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Uaktualnia Magazyn walut przy wycofywaniu księgowania
::----------------------------------------------------------------------------------------------------------------------
KS.cntx_psh();
KS.index('SYM');
KS.prefix(ROK_F.ref());
{? KS.find_key(ROK_F.SYNT+POZ.KON) & KS.MW='T'
|| P_W.index('MW'); P_W.prefix(POZ.ref(),'mwp');
   {? P_W.first()
   || {! |? {? MAGWALP.seek(P_W.MWUIDREF)
            || MAGWALP.WN+=P_W.KWOTA;
               {? MAGWALP.put()
               || MAGWALR.index('MW');
                  MAGWALR.prefix(MAGWALP.ref(),POZ.ref());
                  {? MAGWALR.first()
                  || {! |?
                           _maska:=4-(8+MAGWALR.POZ);
                           DOK.cntx_psh(); POZ.cntx_psh();
                           DOK.use('doku'+_maska);
                           POZ.use('pozy'+_maska);
                           _ref:=BB.sqlint(MAGWALR.POZ);
                           POZ.prefix();
                           _kw:={? _ref<>0 & POZ.seek(_ref,) || POZ.SUMW || 0 ?};
                           DOK.cntx_pop(); POZ.cntx_pop();
                           {? _kw & MAGWALR.MA=_kw
                           || MAGWALR.WN:=0;
                              MAGWALR.POZR:=null();
                              MAGWALR.MAGWAL:=null();
                              MAGWALR.cntx_psh();
                              MAGWALR.prefix();
                              MAGWALR.put();
                              MAGWALR.cntx_pop();
                              0
                           |? MAGWALR.WN=P_W.KWOTA
                           || MAGWALR.del()
                           || MAGWALR.next()
                           ?}
                     !}

                  ?}
               ?}
            ?};
            P_W.X:=0; P_W.put();
            P_W.next()
       !};
       P_W.prefix(POZ.ref(),'mwr');
       {? P_W.first() || {! |?  P_W.X:=0; P_W.put(); P_W.next() !} ?}
   || 'przychody';
      MAGWALP.index('POZ');
      MAGWALP.prefix(POZ.ref());
      {? MAGWALP.first() || MAGWALP.del() ?};
::      rozchody
      MAGWALR.index('POZ');
      MAGWALR.prefix($POZ.ref());
      {? MAGWALR.first() || MAGWALR.del() ?};
      MAGWALR.index('POZR');
      MAGWALR.prefix(POZ.ref());
      {? MAGWALR.first()
      || MAGWALR.WN:=0;
         MAGWALR.POZR:=null();
         MAGWALR.cntx_psh();
         MAGWALR.prefix();
         MAGWALR.put();
         MAGWALR.cntx_pop()
      ?}
   ?}
?};
KS.cntx_pop()


\zap_sum2_upd2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Wypełnia tabelę ZapPoz.TAB2 po ustawieniu filtra w ZapPoz.TAB
::----------------------------------------------------------------------------------------------------------------------
SUM.SUMWN:=SUM.SUMMA:=0;
ZapPoz.TAB.cntx_psh();
_all:=ZapPoz.TAB.f_size();
{? ZapPoz.TAB.f_first()
|| {! |?
      {? SSTALE.WAL=FINFO.NAROD
      || SUM.SUMWN+=ZapPoz.TAB.WN;
         SUM.SUMMA+=ZapPoz.TAB.MA
      || SUM.SUMWN+=ZapPoz.TAB.WN_WAL;
         SUM.SUMMA+=ZapPoz.TAB.MA_WAL
      ?};
      ZapPoz.TAB.f_next()
   !}
?};
{? ZapPoz.TAB2.first()
|| {! |?
      ZapPoz.TAB2.del()
   !}
?};
ZapPoz.TAB2.LP:=1;
ZapPoz.TAB2.NAZWA:='Suma';
ZapPoz.TAB2.WN:=SUM.SUMWN;
ZapPoz.TAB2.MA:=SUM.SUMMA;
ZapPoz.TAB2.add();
ZapPoz.TAB2.LP:=2;
ZapPoz.TAB2.NAZWA:='Saldo';
{? SUM.SUMWN>SUM.SUMMA
|| ZapPoz.TAB2.WN:=SUM.SUMWN-SUM.SUMMA;
   ZapPoz.TAB2.MA:=0
|| ZapPoz.TAB2.WN:=0;
   ZapPoz.TAB2.MA:=SUM.SUMMA-SUM.SUMWN
?};
ZapPoz.TAB2.add();
ZapPoz.TAB2.first();
ZapPoz.TAB.cntx_pop();
ZapPoz.TAB2


\magwal_spli_poz0
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [23.25]
:: OPIS: sprawdzenie i jeśli trzeba to rozbicie na dwie pozycje gdy saldo magazynu walut przechodzi na minus lub plus
::       wg kolejki fifo
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
POZ.cntx_psh();
{? POZ.first()
|| _wal:=tab_tmp(1,'KON','STRING[35]','Konto analityczne','SALDO','REAL','Saldo');
   KS.index('SYM');
   {! |?
      KS.prefix(ROK_F.ref(),ROK_F.SYNT+POZ.KON);
      {? KS.first & KS.MW='T'
      || {? ~_wal.find_tab('first','KON',,'=',POZ.KON)
         || _wal.KON:=POZ.KON;
            _wal.SALDO:=exec('saldo_mw','dok_fks_ks');
            _wal.add
         ?};
::         sprawdzam czy saldo przechodzi przez 0 (wchodzimy na - lub wychodzimy na +)
         {? _wal.SALDO<>0 & _wal.SALDO*(_wal.SALDO+POZ.WN_WAL - POZ.MA_WAL )<-1
         ||
            _ask:={? _wal.SALDO>0
                  || FUN.ask('Zaksięgowanie pozycji dokumentu spowoduje przejście salda (waluty '@+POZ.WAL().TR
                     +') w debet.\nZostanie wydzielona część debetowa pozycji.\nKontynuować?'@)
                  || FUN.ask('Zaksięgowanie pozycji dokumentu spowoduje wyjście salda (waluty '@+POZ.WAL().TR
                     +') z debetu.\nZostanie wydzielona część debetowa pozycji.\nKontynuować?'@)
                  ?};
            {? _ask
            || _ref:=POZ.ref;
               _kwota_1:=|_wal.SALDO*(POZ.SUMW/|POZ.SUMW);
               _kwota_2:=|(_kwota_1-POZ.SUMW)*(POZ.SUMW/|POZ.SUMW);
::            zmiana Lp. następnych pozycji
               POZ.last();
               {! |? POZ.ref<>_ref
               |! POZ.POZ:=POZ.POZ+1;
                  POZ.put;
                  POZ.prev
               !};
::            pierwsza część pozycji rozbijanej
               POZ.SUMW:=_kwota_1;
               {? 1+POZ.STR='W'
               || POZ.WN_WAL:=POZ.SUMW
               || POZ.MA_WAL:=POZ.SUMW
               ?};
               POZ.SUM:=POZ.SUMW*POZ.KURS;
               POZ.put;
               _wal.SALDO:=_wal.SALDO+POZ.WN_WAL - POZ.MA_WAL;
               _wal.put;
::            druga część pozycji rozbijanej
               POZ.POZ:=POZ.POZ+1;
               POZ.SUMW:=_kwota_2;
               {? 1+POZ.STR='W'
               || POZ.WN_WAL:=POZ.SUMW
               || POZ.MA_WAL:=POZ.SUMW
               ?};
               POZ.SUM:=POZ.SUMW*POZ.KURS;
               POZ.add;
               _wal.SALDO:=_wal.SALDO+POZ.WN_WAL - POZ.MA_WAL;
               _wal.put
            || POZ.cntx_pop();
               return(0)
            ?}
         || _wal.SALDO:=_wal.SALDO+POZ.WN_WAL - POZ.MA_WAL;
            _wal.put
         ?}
      ?};
      POZ.next
   !}
?};
POZ.cntx_pop();
1


\saldo_mw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [23.25]
:: OPIS: saldo magazynu walut
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wn:=0;
_ma:=0;
MAGWALP.index('KON_O');
MAGWALR.index('KON_O');
MAGWALP.prefix(POZ.DOK().ODD,POZ.KON,POZ.WAL);
MAGWALR.prefix(POZ.DOK().ODD,POZ.KON,POZ.WAL);
{? MAGWALP.first()
|| {! |?
      _wn+={? MAGWALP.PRZY_DEB='D' || -MAGWALP.WN ||  MAGWALP.WN ?};
      MAGWALP.next()
   !}
?};
{? MAGWALR.first()
|| {! |?
      _ma+={? MAGWALR.ROZ_SPL='S' || -MAGWALR.MA || MAGWALR.MA ?};
      MAGWALR.next()
   !}
?};
(_wn-_ma)$2


:Sign Version 2.0 jowisz:1045 2023/12/22 11:36:03 86da3d0e531164686190e77339bfd43706b03421483d5bca5365b737eecfa842e14a932f422579c8b2e347728cffe686f7e050273319ef68ff8f928445f4a475215f0b0c6798e3dc1ebf0f30ec7ffbd98d7fbc1021e39daac05c0f523fe1dcb47f589767dbd9e14ab54084b7a613a148671041788c72572f71aa2b8150ae6767

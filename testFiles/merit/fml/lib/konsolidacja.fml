:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: konsolidacja.fml
:: Utworzony: 2015.01.29
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Funkcje odpowiedzialne za obsługę konsolidacji
::======================================================================================================================



\okro_f_g_automa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Automatycznie przypisywanie okresów firm do okresu grupy kapitałowej
::   WE: [_a] [1/0] - czy zwracać wynik, 0 - domyślnie wyświetlać, 1 - zwracać
::  OLD: \okro_f_g_automa/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(1)
|| _return:=_a
|| _return:=0
?};
FIRMA.index('EWID'); FIRMA.prefix('T');
OKRO_F_G.cntx_psh();
OKRO_F_G.index('OKRO_F_G'); OKRO_F_G.prefix();
ROK_F.cntx_psh(); OKRO_F.cntx_psh();
OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
_dr1:=ROK_F.POCZ_ROK;
_dr2:=ROK_F.KON_ROK;
_lo:=_lpd:=_ld:=0;
{? OKRO_F.first() & FIRMA.first()
|| _first:=OKRO_F.ref();
   OKRO_F.last();
   _last:=OKRO_F.ref();
   OKRO_F.first();
   {!
   |? _okro_f:=OKRO_F.ref();
      _rok_f:=OKRO_F.ROK;
      _d1:=OKRO_F.POCZ;
      _d2:=OKRO_F.KON;
      OKRO_F.cntx_psh();
      OKRO_F.index('KON'); OKRO_F.prefix();
      {? FIRMA.first()
      || {!
         |? _lo+=1;
            {? ~OKRO_F_G.find_key(_okro_f,FIRMA.ref())
            || _ok:=0;
               _lpd+=1;
               {? _okro_f<>_first & _okro_f<>_last
               || OKRO_F.index('KON'); OKRO_F.prefix();
                  _ok:=OKRO_F.find_key(FIRMA.ref(),_d2,_d1);
                  {? _ok
                  || _rr:=OKRO_F.ROK;
                     _ro:=OKRO_F.ref()
                  ?}
               || ROK_F.cntx_psh();
                  ROK_F.index('ROKPOCZ'); ROK_F.prefix(FIRMA.ref(),_dr1,_dr2);
                  {? ROK_F.first()
                  || OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
                     _ok:=_okro_f=_first & OKRO_F.first() | _okro_f=_last & OKRO_F.last();
                     {? _ok
                     || _rr:=OKRO_F.ROK;
                        _ro:=OKRO_F.ref()
                     ?}
                  ?};
                  ROK_F.cntx_pop()
               ?};
               {? _ok
               || OKRO_F_G.ROK_F_G:=_rok_f;
                  OKRO_F_G.OKRO_F_G:=_okro_f;
                  OKRO_F_G.FIRMA:=FIRMA.ref();
                  OKRO_F_G.OKRO_F:=_ro;
                  OKRO_F_G.ROK_F:=_rr;
                  _ld+=OKRO_F_G.add()
               ?}
            ?};
            FIRMA.next()
         !}
      ?};
      OKRO_F.cntx_pop();
      OKRO_F.next()
   !}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
OKRO_F_G.cntx_pop();
_info:='Liczba okresów: %1\n'
       'Liczba wymaganych uzupełnień: %2\n'
       'Liczba wykonanych uzupełnień: %3.'@[$_lo,$_lpd,$_ld];
{? _return
|| return(_ld)
?};
FUN.info(_info)


\add_rok_firmy_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie roku i planu kont do firmy wyłączeniowej
::  OLD: \add_rok_firmy_w/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? REF.S_FIRMA().TYP='W'
|| exec('odd_new','ustawienia',FIRMA.ref())
?};
FIRMA.cntx_psh();
FIRMA.index('SYMBOL2'); FIRMA.prefix('S',3+FIRMA.SYMBOL,);
{? FIRMA.first()
|| ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ'); ROK_F.prefix(FIRMA.ref());
   ROK_F.win_sel('ROK');
   {? ROK_F.select()
   || ROK_F.cntx_psh();
      ROK_F.index('NAZWA'); ROK_F.prefix(REF.S_FIRMA,ROK_F.NAZ,);
      _jest:=ROK_F.first();
      ROK_F.cntx_pop();
      _rok:=null;
      {? do_state()=0 || do() ?};
      {? _jest
      || FUN.info('Istnieje już rok %1.'@[ROK_F.NAZ])
      || ROK_F.cntx_psh(); ROK_F.prefix();
         ROK_F_K.index('LP'); ROK_F_K.prefix(null);
         {? ROK_F_K.first()
         || ROK_F.FIRMA:=REF.S_FIRMA;
            ROK_F.KOD:=ROK_F_K.KOD;
            {? ROK_F.add()
            || _rok:=ROK_F.ref();
               ROK_F_K.prefix();
               ROK_F_K.ROK_F:=ROK_F.ref();
               ROK_F_K.put();
               exec('add_bobz','rejestry',ROK_F.ref());
               exec('add_rej','konsolidacja',REF.S_FIRMA)
            ?}
         ?};
         ROK_F.cntx_pop()
      ?};
      {? _rok<>null
      || OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
         {? OKRO_F.first()
         || {!
            |? OKRO_F.cntx_psh(); OKRO_F.prefix();
               OKRO_F.ROK:=_rok;
               OKRO_F.add();
               OKRO_F.cntx_pop();
               OKRO_F.next()
            !}
         ?};
         KS.index('SYM'); KS.prefix(ROK_F.ref());
         {? KS.first()
         || {!
            |? _ks:=null;
               KS.cntx_psh(); KS.prefix();
               KS.ROK:=_rok;
               {? KS.add()
               || _ks:=KS.ref()
               ?};
               KS.cntx_pop();
               {? _ks<>null
               || BUD.index('KS'); BUD.prefix(KS.ref());
                  {? BUD.first()
                  || {!
                     |? BUD.cntx_psh(); BUD.prefix();
                        BUD.KS:=_ks;
                        BUD.add();
                        BUD.cntx_pop();
                        BUD.next()
                     !}
                  ?};
                  KOM.index('KS'); KOM.prefix(KS.ref());
                  {? KOM.first()
                  || KOM.cntx_psh(); KOM.prefix();
                     KOM.KS:=_ks;
                     KOM.add();
                     KOM.cntx_pop()
                  ?};
                  KS_W.index('LP'); KS_W.prefix(KS.ref());
                  {? KS_W.first()
                  || {!
                     |? KS_W.cntx_psh(); KS_W.prefix();
                        KS_W.S:=_ks;
                        KS_W.add();
                        KS_W.cntx_pop();
                        KS_W.next()
                     !}
                  ?}
               ?};
               KS.next()
            !}
         ?};
         exec('add_alg_par','konsolidacja',ROK_F.ref(),_rok);
         exec('add_ospr','konsolidacja',FIRMA.ref(),ROK_F.ref(),_rok);
         exec('add_okro_f_g','konsolidacja',FIRMA.ref(),ROK_F.ref(),_rok)
      ?};
      end()
   ?};
   ROK_F.cntx_pop()
?};
FIRMA.cntx_pop()


\add_rej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie rejestru do wyłaczeń do firmy wyłaczeniowej
::   WE: _a - firma
::  OLD: \add_rej/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
FIRMA.cntx_psh();
ROK_F.cntx_psh(); ODD.cntx_psh(); REJ.cntx_psh();
ROK_F.index('KOD'); ROK_F.prefix(_a);
ODD.index('ODDZIALY'); ODD.prefix(_a);
REJ.index('KOD');
DOK_REJ.index('NAZ');
{? ROK_F.first()
|| {!
   |? {? ODD.first()
      || {!
         |? REJ.prefix(ROK_F.ref(),ODD.ref());
            {? ODD.OD='FIRMA'+(3+ODD.FIRMA().SYMBOL)
            || {? ~REJ.find_key('~NOTY_WY',)
               || REJ.blank();
                  REJ.KOD:='~NOTY_WY';
                  REJ.NAZ:='Noty do wyłączeń';
                  REJ.ODD:=ODD.ref();
                  REJ.ROK:=ROK_F.ref();
                  REJ.add()
               ?};
               DOK_REJ.prefix(REJ.ref(),'~Dokument do wyłączeń',);
               {? ~DOK_REJ.first()
               || DOK_REJ.blank();
                  DOK_REJ.REJ:=REJ.ref();
                  DOK_REJ.NAZ:='~Dokument do wyłączeń';
                  DOK_REJ.M_ODD:='T';
                  DOK_REJ.ODD_ROZR:='T';
                  SLU.index('NAZ'); SLU.prefix();
                  {? SLU.find_key('~RODZAJE DOKUMENTÓW')
                  || SLO.index('SL'); SLO.prefix(SLU.ref(),'PROSTY',);
                     {? SLO.first()
                     || DOK_REJ.SLO:=SLO.ref();
                        DOK_REJ.add()
                     ?}
                  ?}
               ?}
            || {? ~REJ.find_key('~NOTY_W2',)
               || REJ.blank();
                  REJ.KOD:='~NOTY_W2';
                  REJ.NAZ:='Noty do wyłączeń 2';
                  REJ.ODD:=ODD.ref();
                  REJ.ROK:=ROK_F.ref();
                  REJ.add()
               ?};
               DOK_REJ.prefix(REJ.ref(),'~Dokument do wyłączeń',);
               {? ~DOK_REJ.first()
               || DOK_REJ.blank();
                  DOK_REJ.REJ:=REJ.ref();
                  DOK_REJ.NAZ:='~Dokument do wyłączeń';
                  SLU.index('NAZ'); SLU.prefix();
                  {? SLU.find_key('~RODZAJE DOKUMENTÓW')
                  || SLO.index('SL'); SLO.prefix(SLU.ref(),'PROSTY',);
                     {? SLO.first()
                     || DOK_REJ.SLO:=SLO.ref();
                        DOK_REJ.add()
                     ?}
                  ?}
               ?}
            ?};
            ODD.next()
         !}
      ?};
      ROK_F.next()
   !}
?};
ROK_F.cntx_pop(); ODD.cntx_pop(); REJ.cntx_pop(); FIRMA.cntx_pop()


\add_alg_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie algorytmów sprawozdań do firmy wyłączeniowej
::   WE: _a - rok firmy A
::       _b - rok firmy do wyłaczeń firmy A
::  OLD: \add_alg_par/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
FIRMA.cntx_psh(); ROK_F.cntx_psh();
GR_SIK.index('SKROT'); GR_SIK.prefix(REF.GRUPA);
DEFW.index('GRUPA');
ALG_PAR.index('ALG_PAR4');
{? GR_SIK.first()
|| {!
   |? DEFW.prefix(GR_SIK.ref());
      {? DEFW.first()
      || {!
         |? ALG_PAR.prefix(DEFW.ref(),_a);
            {? ALG_PAR.first()
            || {!
               |? ALG_PAR.cntx_psh(); ALG_PAR.prefix();
                  ALG_PAR.ROK:=_b;
                  ALG_PAR.FIRMA:=ALG_PAR.ROK().FIRMA;
                  ALG_PAR.add();
                  ALG_PAR.cntx_pop();
                  ALG_PAR.next()
               !}
            ?};
            DEFW.next()
         !}
      ?};
      GR_SIK.next()
   !}
?};
FIRMA.cntx_pop(); ROK_F.cntx_pop()


\add_ospr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie okresów raportowania
::   WE: _a - firma A
::       _b - rok firmy A
::       _c - rok firmy do wyłaczeń firmy A
::  OLD: \add_ospr/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
OKRO_F.index('ROK');
OSPR.index('OKRES3'); OSPR.prefix(_a,_b);
{? OSPR.first()
|| {!
   |? OSPR.cntx_psh(); OSPR.prefix();
      OSPR.ROK:=_c;
      OSPR.FIRMA:=OSPR.ROK().FIRMA;
      OKRO_F.prefix(_c);
      {? OKRO_F.find_key(OSPR.OKRES_OD().NR)
      || OSPR.OKRES_OD:=OKRO_F.ref()
      ?};
      {? OKRO_F.find_key(OSPR.OKRES_DO().NR)
      || OSPR.OKRES_DO:=OKRO_F.ref()
      ?};
      OSPR.add();
      OSPR.cntx_pop();
      OSPR.next()
   !}
?};
ROK_F.cntx_pop()


\add_okro_f_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Dodanie mapowania okresów firmy wyłączeniowej na okresy grupy
::   WE: _a - firma A
::       _b - rok firmy A
::       _c - rok firmy do wylaczen firmy A
::  OLD: \add_okro_f_g/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
OKRO_F.index('ROK'); OKRO_F.prefix(_c);
OKRO_F_G.index('ROK_F'); OKRO_F_G.prefix(_b);
{? OKRO_F_G.first()
|| {!
   |? OKRO_F_G.cntx_psh(); OKRO_F_G.prefix();
      OKRO_F_G.ROK_F:=_c;
      OKRO_F_G.FIRMA:=OKRO_F_G.ROK_F().FIRMA;
      {? OKRO_F.find_key(OKRO_F_G.OKRO_F().NR)
      || OKRO_F_G.OKRO_F:=OKRO_F.ref()
      ?};
      OKRO_F_G.add();
      OKRO_F_G.cntx_pop();
      OKRO_F_G.next()
   !}
?};
ROK_F.cntx_pop()


\awr_okrof
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Po odświeżeniu okna OKRO_F_G okna grupowego OKRO_F_G tabeli ROK_F
::  OLD: \awr_okrof/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F_G.index('OKRO_F_G');
{? OKRO_F.get()
|| OKRO_F_G.prefix(OKRO_F.ref())
|| OKRO_F_G.prefix(null)
?};
ROK_F.cntx_psh();
OKRO_F.cntx_psh();
grp_disp(OKRO_F_G,'WER');
OKRO_F.cntx_pop();
ROK_F.cntx_pop()


\bu_okro_f_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Przed obsługą okna WER tabeli OKRO_F_G w oknie grupowym OKRO_F_G tabeli ROK_F
::  OLD: \bu_okro_f_g/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
OKRO_F.cntx_psh();
__RokF:=ROK_F.ref();
__OkroF:=OKRO_F.ref()


\au_okro_f_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Przed obsługą okna WER tabeli OKRO_F_G w oknie grupowym OKRO_F_G tabeli ROK_F
::  OLD: \au_okro_f_g/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_pop();
ROK_F.cntx_pop();
&__RokF;
&__OkroF;
1


\rok_f_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Zwraca rok firmy do wylaczen na podstawie roku firmy X
::   WE: _a - firma X
::       _b - rok firmy X
::  OLD: \rok_f_w/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
_fw:=exec('firma_w','konsolidacja');
{? _fw<>null
|| ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ'); ROK_F.prefix(_fw,ROK_F.POCZ_ROK,ROK_F.KON_ROK);
   _rokw:={? ROK_F.first() || ROK_F.ref() || null ?};
   ROK_F.cntx_pop();
   _rokw
|| null
?}


\firma_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Zwraca firme do wylaczen firmy lub null
::   WE: _a - firma
::  OLD: \firma_w/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
FIRMA.cntx_psh();
{? _
|| FIRMA.prefix();
   FIRMA.seek(_a)
?};
FIRMA.index('SYMBOL2'); FIRMA.prefix('W',FIRMA.SYMBOL+'W');
_fw:={? FIRMA.first() || FIRMA.ref() || null ?};
FIRMA.cntx_pop();
_fw


\wz_firma_symbol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Formuła na wzorzec pola FIRMA.SYMBOL
::  OLD: \wz_firma_symbol/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('Wz_firma')>0
|| Wz_firma
|| ''
?}


\bl_firma_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Wartość początkowa FIRMA.TYP
::  OLD: \bl_firma_typ/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()*'bez merit'>0
|| 'S'
|? -menu_txt()*'konsolid'>0
|| 'K'
|? var_pres('__F_ref')>0
|| __F_ref
|| 'S'
?}


\bl_firma_ewid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [12.10]
:: OPIS: Wartość początkowa FIRMA.EWID
::  OLD: \bl_firma_ewid/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('confuned')>0 | FIRMA.TYP='S' | FIRMA.TYP='W'
|| 'T'
|| 'N'
?}


\bl_firma_z
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Wartość początkowa FIRMA.Z
::  OLD: \bl_firma_z/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()*'bez merit' || 'T' || 'N' ?}


\czy_okr_zam_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Czy okres zamkniety controllingowo
::   WE: [_a] - czy wyswietlac komunikat
::       [_b] - okres lub okres od
::       [_c] - okres do
::  OLD: \czy_okr_zam_con/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
{? _<2
|| _tak:=SSTALE.AO().ZAM_CON='T';
   {? _tak & var_pres('_a')>0 & _a
   || FUN.info('Okres %1 %2 jest zamknięty controllingowo.'@[OKRO_F.NAZ,OKRO_F.ROK().NAZ])
   ?};
   _tak
|? _=2
|| OKRO_F.cntx_psh();
   OKRO_F.prefix();
   _tak:=OKRO_F.seek(_b) & OKRO_F.ZAM_CON='T';
   OKRO_F.cntx_pop();
   {? _tak & var_pres('_a')>0 & _a
   || FUN.info('Okres %1 %2 jest zamknięty controllingowo.'@[OKRO_F.NAZ,OKRO_F.ROK().NAZ])
   ?};
   _tak
|| _tak:=0;
   OKRO_F.cntx_psh();
   OKRO_F.index('FIRMA_NR');
   {? OKRO_F.seek(_b)
   || {!
      |? _tak:=OKRO_F.ZAM_CON='T';
         _tak=0 & OKRO_F.ref()<>_c & OKRO_F.next()
      !}
   ?};
   OKRO_F.cntx_pop();
   {? _tak & var_pres('_a')>0 & _a
   || FUN.info('Podany zakres zawiera okres zamknięty controllingowo.'@)
   ?};
   _tak
?}


\czy_rok_zam_con
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Czy w roku wystepuje okres zamkniety controllingowo
::   WE: _a - wskazanie na rok
::  OLD: \czy_rok_zam_con/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh();
OKRO_F.index('ZAM_CON');
OKRO_F.prefix(_a,'T');
_tak:=OKRO_F.first();
OKRO_F.cntx_pop();
_tak


\am_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Plan kont firmy jak plan kont grupy - zmiana znacznika na roku firmy ROK_F.PLAN_GR
::  OLD: \am_rok/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
_rf:=ROK_F.ref();
_firma:=ROK_F.FIRMA;
ROK_F.cntx_psh();
_rg:=exec('rok_f_g','konsolidacja',_rf);
_res:=0;
{? _rg<>null
|| do();
   KS.cntx_psh();
   KS.index('SYM'); KS.prefix(_rg);
   {? KS.first()
   || {!
      |? KS.cntx_psh(); KS.prefix();
         KS.ROK:=_rf;
         _ks:={? KS.add() || KS.ref() || null ?};
         KS.cntx_pop();
         {? _ks
         || KOM.cntx_psh();
            KOM.index('KS'); KOM.prefix(KS.ref());
            {? KOM.first()
            || {!
               |? KOM.cntx_psh(); KOM.prefix();
                  KOM.KS:=_ks;
                  KOM.add();
                  KOM.cntx_pop();
                  KOM.next()
               !}
            ?};
            KOM.cntx_pop();
            BUD.cntx_psh();
            BUD.index('KS'); BUD.prefix(KS.ref());
            {? BUD.first()
            || {!
               |? BUD.cntx_psh(); BUD.prefix();
                  BUD.KS:=_ks;
                  BUD.add();
                  BUD.cntx_pop();
                  BUD.next()
               !}
            ?};
            BUD.cntx_pop();
            KS_W.cntx_psh();
            KS_W.index('LP'); KS_W.prefix(KS.ref());
            {? KS_W.first()
            || {!
               |? KS_W.cntx_psh(); KS_W.prefix();
                  KS_W.S:=_ks;
                  KS_W.add();
                  KS_W.cntx_pop();
                  KS_W.next()
               !}
            ?};
            KS_W.cntx_pop()
         ?};
         KS.next()
      !}
   ?};
   KS.cntx_pop();
   AUTOKSIE.cntx_psh();
   AUTOKSIE.index('NAZ'); AUTOKSIE.prefix(_rg);
   {? AUTOKSIE.first()
   || {!
      |? AUTOKSIE.cntx_psh(); AUTOKSIE.prefix();
         AUTOKSIE.ROK_F:=_rf;
         _aut:={? AUTOKSIE.add() || AUTOKSIE.ref() || null ?};
         AUTOKSIE.cntx_pop();
         {? _aut
         || FORM.cntx_psh();
            FORM.index('AUTOKSIE'); FORM.prefix(AUTOKSIE.ref());
            {? FORM.first()
            || {!
               |? FORM.memo_get(,'FML');
                  FORM.cntx_psh(); FORM.prefix();
                  FORM.AUTOKSIE:=_aut;
                  {? FORM.add() || FORM.memo_put(,'FML') ?};
                  FORM.cntx_pop();
                  FORM.next()
               !}
            ?};
            FORM.cntx_pop()
         ?};
         AUTOKSIE.next()
      !}
   ?};
   AUTOKSIE.cntx_pop();
   AV.cntx_psh();
   AV.index('AV'); AV.prefix(_rg);
   {? AV.first()
   || {!
      |? AV.cntx_psh(); AV.prefix();
         AV.ROK_F:=_rf;
         _av:={? AV.add()|| AV.ref() || null ?};
         AV.cntx_pop();
         DEK.cntx_psh();
         DEK.index('DEK'); DEK.prefix(AV.ref());
         {? DEK.first()
         || {!
            |? DEK.cntx_psh(); DEK.prefix();
               DEK.AV:=_av;
               _dek:={? DEK.add() || DEK.ref() || null ?};
               DEK.cntx_pop();
               {? _dek
               || SKID_WYR.cntx_psh();
                  SKID_WYR.index('SKID_WYR'); SKID_WYR.prefix(REF.GRUPA,'DEK',DEK.name(),#DEK.ref());
                  {? SKID_WYR.first()
                  || {!
                     |? SKID_WYR.cntx_psh(); SKID_WYR.prefix();
                        SKID_WYR.FIRMA:=_firma;
                        SKID_WYR.REF:=#_dek;
                        SKID_WYR.add();
                        SKID_WYR.cntx_pop();
                        SKID_WYR.next()
                     !}
                  ?};
                  SKID_WYR.cntx_pop()
               ?};
               DEK.next()
            !}
         ?};
         DEK.cntx_pop();
         AV.next()
      !}
   ?};
   AV.cntx_pop();
   ALG_PAR.cntx_psh();
   ALG_PAR.index('ROK'); ALG_PAR.prefix(_rg);
   {? ALG_PAR.first()
   || {!
      |? ALG_PAR.cntx_psh(); ALG_PAR.prefix();
         ALG_PAR.ROK:=_rf;
         ALG_PAR.FIRMA:=_firma;
         ALG_PAR.add();
         ALG_PAR.cntx_pop();
         ALG_PAR.next()
      !}
   ?};
   ALG_PAR.cntx_pop();
   _res:=do_state();
   end()
?};
ROK_F.cntx_pop();
_res


\rok_f_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Rok grupy na podstawie roku firmy
::   WE: _a - rok
::  OLD: \rok_f_g/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=SSTALE.AR ?};
ROK_F.prefix();
{? ROK_F.seek(_a)
|| {? ROK_F.FIRMA=REF.GRUPA
   || _a
   || OKRO_F_G.index('ROK_F');
      OKRO_F_G.prefix(ROK_F.ref());
      {? OKRO_F_G.first()
      || OKRO_F_G.OKRO_F_G().ROK
      || null
      ?}
   ?}
|| _a
?}


\wykon_firmy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Zwraca wersje dla wykonania firmy
::   WE:  _a  - firma (FIRMA.ref())
::       [_b] - schemat grupy kapitalowej - domyslnie bez schematu
::  OLD: \wykon_firmy/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=1 || _b:=null ?};
{? SIK.WYL='' || SIK.WYL:='T' ?};
_firmawyl:={? SIK.WYL='W' || REF.FIRMAWYL || null ?};
WERSJE.index('KOD'); WERSJE.prefix(SIK.WYL,_b,_a,_firmawyl);
{? ~WERSJE.first()
|| WERSJE.blank(1);
   WERSJE.WYKON:=SIK.WYL;
   WERSJE.FIRMA:=_a;
   WERSJE.W_SCH:=_b;
   WERSJE.FIRMAWYL:=_firmawyl;
   WERSJE.add()
?};
WERSJE.ref()


\jest_sprzedana
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Czy w okresie firma jest sprzedana
::   WE: [_a] - wskazanie na firme
::       [_b] - wskazanie na okres sprawozdawczy (OSPR.ref)
::  OLD: \jest_sprzedana/skid_con.fml
::----------------------------------------------------------------------------------------------------------------------
FIRMA.cntx_psh(); OSPR.cntx_psh();
{? _=2
|| OSPR.prefix();
   {? OSPR.seek(_b)
   || OSPR.OKRES_OD();
      OSPR.FIRMA()
   ?}
|? _=0
|| REF.FIRMA()
|| FIRMA.prefix();
   FIRMA.seek(_a)
?};
{? FIRMA.NAR_SPRZ='N' & FIRMA.OKR_SPRZ
|| ROK_F.cntx_psh(); OKRO_F.cntx_psh();
   _okr:=$(OKRO_F.ROK().POCZ_ROK~1)+(('0'+$OKRO_F.NR)+2);
   _okrs:=$(FIRMA.ROK_SPRZ().POCZ_ROK()~1)+(('0'+$FIRMA.OKR_SPRZ().NR)+2);
   _ret:=_okr>=_okrs;
   ROK_F.cntx_pop(); OKRO_F.cntx_pop()
|| _ret:=0
?};
FIRMA.cntx_pop(); OSPR.cntx_pop();
_ret


\okro_f_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Zwraca okres bilansowy firmy na podstawie okresu grupy
::   WE: _a - okres grupy
::       _b - firma
::  OLD: \okro_f_f/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
OKRO_F_G.index('OKRO_F_G'); OKRO_F_G.prefix(_a,_b);
{? OKRO_F_G.first()
|| OKRO_F_G.OKRO_F
|| null
?}


\plan_firmy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Zwraca plan firmy
::   WE: _a - schemat firmy
::       _b - firma
::       _c - kod wariantu
::  OLD: \plan_firmy/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
WERSJE.cntx_psh();
WERSJE.index('KOD'); WERSJE.prefix('N',_a,_b,null,_c,);
_ref:=
   {? WERSJE.first()
   || WERSJE.ref()
   || WERSJE_W.index('KOD'); WERSJE_W.prefix(_c,);
      {? WERSJE_W.first()
      || WERSJE.blank(1);
         WERSJE.WYKON:='N';
         WERSJE.W_SCH:=_a;
         WERSJE.FIRMA:=_b;
         WERSJE.WERSJE_W:=WERSJE_W.ref();
         WERSJE.KOD:=WERSJE_W.KOD;
         WERSJE.OPIS:=WERSJE_W.OPIS;
         {? WERSJE.add()
         || WERSJE.ref()
         || null
         ?}
      || null
      ?}
   ?};
WERSJE.cntx_pop();
_ref


\rok_f_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Rok firmy na podstawie roku
::   WE:  _a  - rok
::       [_b] - firma
::  OLD: \rok_f_f/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? _<1 || _a:=SSTALE.AR ?};
{? _<2 || _b:=REF.FIRMA ?};
ROK_F.prefix();
{? ROK_F.seek(_a)
|| {? ROK_F.FIRMA=_b || return(_a) ?};
   _ok:=0;
   {? ROK_F.FIRMA<>REF.GRUPA
   || OKRO_F_G.index('ROK_F'); OKRO_F_G.prefix(ROK_F.ref());
      {? OKRO_F_G.first()
      || OKRO_F_G.ROK_F_G(); _ok:=1
      ?}
   || _ok:=1
   ?};
   {? _ok
   || FIRMA.cntx_psh(); FIRMA.prefix();
      _z:={? FIRMA.seek(_b) || FIRMA.Z='T' || 0 ?};
      FIRMA.cntx_pop();
      {? _z
      || ROK_F.ref()
      || OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref(),0);
         {? OKRO_F.first()
         || OKRO_F_G.index('OKRO_F_G'); OKRO_F_G.prefix(OKRO_F.ref(),_b);
            {? OKRO_F_G.first()
            || OKRO_F_G.ROK_F
            || null
            ?}
         || null
         ?}
      ?}
   || null
   ?}
|| null
?}


\ospr_fg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Okres sprawozdawczy firmy
::   WE:  _a  - OSPR.ref
::        _b  - 'F'-firmy 'G'-grupy
::       [_c] - firma
::  OLD: \ospr_fg/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
OSPR.cntx_psh();
OSPR.prefix();
_ref:=
{? OSPR.seek(_a)
|| {? _b='F'
   || _rok:=exec('rok_f_f','konsolidacja',OSPR.ROK,{? _=3 || _c || REF.FIRMA ?})
   || _rok:=exec('rok_f_g','konsolidacja',OSPR.ROK,{? _=3 || _c || REF.FIRMA ?})
   ?};
   _o1:=OSPR.OKRES_OD().NR;
   _o2:=OSPR.OKRES_DO().NR;
   OKRO_F.index('ROK'); OKRO_F.prefix(_rok);
   _o1_ref:={? OKRO_F.find_key(_o1) || OKRO_F.ref() || null ?};
   _o2_ref:={? OKRO_F.find_key(_o2) || OKRO_F.ref() || null ?};
   OSPR.index('OKRES3'); OSPR.prefix({? _=3 || _c || REF.FIRMA ?},_rok,_o1_ref,_o2_ref);
   {? OSPR.first()
   || OSPR.ref()
   || null
   ?}
|| null
?};
OSPR.cntx_pop();
_ref


\k_insmbn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Ustawia rekord K_INSMBN na podstawie SKID_MBN
::   WE: [_a] - SKID_MBN.ref()
::  OLD: \k_insmbn/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=SKID_MBN.ref() ?};
K_INSMBN.index('INST2'); K_INSMBN.prefix(REF.FIRMA,_a);
{? ~K_INSMBN.first()
|| K_INSMBN.SKID_MBN:=_a;
   K_INSMBN.FIRMA:=REF.FIRMA;
   K_INSMBN.CZY_INST:='N';
   K_INSMBN.CZY_DOK:='N';
   K_INSMBN.CZY_LIST:='N';
   K_INSMBN.CZY_NAR:='N';
   K_INSMBN.WART_NAR:='N';
   K_INSMBN.add()
|| 1
?}


\spr_widok_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.30]
:: OPIS: Przed redakcja pola ze sprawozdaniem widoku danych
::  OLD: \spr_widok_be/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()='popraw'
|| K_HARM_P.index('K_N_VIEW');
   K_HARM_P.prefix(K_N_VIEW.ref());
   {? ~K_HARM_P.first()
   || K_P_VIEW.index('K_P_VIEW');
      K_P_VIEW.prefix();
      ~K_P_VIEW.first()
   ?}
|| 1
?}


\be_firma_symbol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Przed redakcja FIRMA.SYMBOL
::  OLD: \be_firma_symbol/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
-(6+menu_txt())='szukaj' | FIRMA.TYP='K' | FIRMA.Z='T'


\ustaw_trigger
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Ustawia triggery tabel na potrzeby konsolidacji
::   WE: _a - rok firmy
::  OLD: \ustaw_trigger/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
_naz_trig:='konsolidacja';
KS.trig_a('add',"{? _a || exec('t_ks','konsolidacja',1) || ~~ ?}",_naz_trig);
KS.trig_a('put',"{? _a || exec('t_ks','konsolidacja',2) || ~~ ?}",_naz_trig);
KS.trig_b('del',"exec('t_ks','konsolidacja',3)",_naz_trig);
KOM.trig_a('add',"{? _a || exec('t_kom','konsolidacja',1) || ~~ ?}",_naz_trig);
KOM.trig_a('put',"{? _a || exec('t_kom','konsolidacja',2) || ~~ ?}",_naz_trig);
KOM.trig_b('del',"exec('t_kom','konsolidacja',3)",_naz_trig);
BUD.trig_a('add',"{? _a || exec('t_bud','konsolidacja',1) || ~~ ?}",_naz_trig);
BUD.trig_a('put',"{? _a || exec('t_bud','konsolidacja',2) || ~~ ?}",_naz_trig);
BUD.trig_b('del',"exec('t_bud','konsolidacja',3)",_naz_trig);
AUTOKSIE.trig_a('add',"{? _a || exec('t_autoksie','konsolidacja',1) || ~~ ?}",_naz_trig);
AUTOKSIE.trig_a('put',"{? _a || exec('t_autoksie','konsolidacja',2) || ~~ ?}",_naz_trig);
AUTOKSIE.trig_b('del',"exec('t_autoksie','konsolidacja',3)",_naz_trig);
FORM.trig_a('add',"{? _a || exec('t_form','konsolidacja',1) || ~~ ?}",_naz_trig);
FORM.trig_a('put',"{? _a || exec('t_form','konsolidacja',2) || ~~ ?}",_naz_trig);
FORM.trig_a('memo_put',"{? _a || exec('t_form','konsolidacja',4) || ~~ ?}",_naz_trig);
FORM.trig_b('del',"exec('t_form','konsolidacja',3)",_naz_trig);
AV.trig_a('add',"{? _a || exec('t_av','konsolidacja',1) || ~~ ?}",_naz_trig);
AV.trig_a('put',"{? _a || exec('t_av','konsolidacja',2) || ~~ ?}",_naz_trig);
AV.trig_b('del',"exec('t_av','konsolidacja',3)",_naz_trig);
DEK.trig_a('add',"{? _a || exec('t_dek','konsolidacja',1) || ~~ ?}",_naz_trig);
DEK.trig_a('put',"{? _a || exec('t_dek','konsolidacja',2) || ~~ ?}",_naz_trig);
DEK.trig_b('del',"exec('t_dek','konsolidacja',3)",_naz_trig);
SKID_WYR.trig_a('add',"{? _a || exec('t_skid_wyr','konsolidacja',1) || ~~ ?}",_naz_trig);
SKID_WYR.trig_a('put',"{? _a || exec('t_skid_wyr','konsolidacja',2) || ~~ ?}",_naz_trig);
SKID_WYR.trig_b('del',"exec('t_skid_wyr','konsolidacja',3)",_naz_trig);
KS_W.trig_a('add',"{? _a || exec('t_ks_w','konsolidacja',1) || ~~ ?}",_naz_trig);
KS_W.trig_a('put',"{? _a || exec('t_ks_w','konsolidacja',2) || ~~ ?}",_naz_trig);
KS_W.trig_b('del',"exec('t_ks_w','konsolidacja',3)",_naz_trig);
ALG_PAR.trig_a('add',"{? _a || exec('t_alg_par','konsolidacja',1) || ~~ ?}",_naz_trig);
ALG_PAR.trig_a('put',"{? _a || exec('t_alg_par','konsolidacja',2) || ~~ ?}",_naz_trig);
ALG_PAR.trig_b('del',"exec('t_alg_par','konsolidacja',3)",_naz_trig);
WERSJE_W.trig_a('add',"{? _a || exec('t_wersje_w','konsolidacja',1) || ~~ ?}",_naz_trig);
WERSJE_W.trig_a('put',"{? _a || exec('t_wersje_w','konsolidacja',2) || ~~ ?}",_naz_trig);
FIRMA.trig_a('add',"{? _a || exec('t_firma','konsolidacja',1) || ~~ ?}",_naz_trig);
AN_BO.trig_a('add',"{? _a || exec('t_an_bo','konsolidacja',1) || ~~ ?}",_naz_trig);
AN_BO.trig_a('put',"{? _a || exec('t_an_bo','konsolidacja',2) || ~~ ?}",_naz_trig);
AN_BO.trig_b('del',"exec('t_an_bo','konsolidacja',3)",_naz_trig);
1


\t_ks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add,put i przed del tabeli KS
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_ks/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
FIRMA.cntx_psh();
{? KS.ROK().FIRMA().TYP='C'
|| {? _a=2
   || _bsym:=bfld('SYM')
   || _bsym:=KS.SYM
   ?};
   _rok:=ROK_F.ref();
   {? _a=2
   || _tab:=obj_new(KS.fld_num());
      {! _i:=1..KS.fld_num() |! _tab[_i]:=KS[_i] !}
   || _tab:=null
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
   {? OKRO_F_G.first()
   || {!
      |?
         OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         {? OKRO_F_G.ROK_F().PLAN_GR='T'
            || exec('t_ks2','konsolidacja',_a,ROK_F.ref(),_bsym,_tab)
         ?};
         OKRO_F_G.prefix(_rok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
|? FIRMA.TYP='S'
|| _rw:=exec('rok_f_w','konsolidacja',FIRMA.ref(),ROK_F.ref());
   {? _rw
   || {? _a=2
      || _bsym:=bfld('SYM')
      || _bsym:=KS.SYM
      ?};
      {? _a=2
      || _tab:=obj_new(KS.fld_num());
         {! _i:=1..KS.fld_num() |! _tab[_i]:=KS[_i] !}
      || _tab:=null
      ?};
      exec('t_ks2','konsolidacja',_a,_rw,_bsym,_tab)
   ?}
?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_ks2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Pomocnicza dla triggera po add,put i przed del tabeli KS
::   WE: _a - 1-add 2-put 3-del
::       _b - rok
::       _c - symbol
::       _d - tablica z danymi
::  OLD: \t_ks2/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
KS.cntx_psh();
KS.index('SYM'); KS.prefix(_b,_c);
{? KS.first()
|| {? _a=2
   || {! _i:=1..KS.fld_num() |! KS[_i]:=_d[_i] !};
      KS.prefix();
      KS.ROK:=_b;
      KS.put()
   |? _a=3
   || KS.del()
   ?}
|? _a=1
|| KS.ROK:=_b;
   KS.add()
?};
KS.cntx_pop()


\t_bud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add, put i przed del tabeli BUD
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_bud/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
KS.cntx_psh();
ROK_F.cntx_psh();
FIRMA.cntx_psh();
{? BUD.KS().ROK().FIRMA().TYP='C'
|| {? _a=2
   || _bpoz:=bfld('POZ')
   || _bpoz:=BUD.POZ
   ?};
   _rok:=ROK_F.ref();
   _ks_sym:=KS.SYM;
   {? _a=2
   || _tab:=obj_new(BUD.fld_num());
      {! _i:=1..BUD.fld_num() |! _tab[_i]:=BUD[_i] !}
   || _tab:=null
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
   {? OKRO_F_G.first()
   || {!
      |?
         OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         {? OKRO_F_G.ROK_F().PLAN_GR='T' & ROK_F.FIRMA().TYP='S'
         || exec('t_bud2','konsolidacja',_a,ROK_F.ref(),_ks_sym,_bpoz,_tab)
         ?};
         OKRO_F_G.prefix(_rok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
|? FIRMA.TYP='S'
|| _rw:=exec('rok_f_w','konsolidacja',FIRMA.ref(),ROK_F.ref());
   {? _rw
   || {? _a=2
      || _bpoz:=bfld('POZ')
      || _bpoz:=BUD.POZ
      ?};
      _ks_sym:=KS.SYM;
      {? _a=2
      || _tab:=obj_new(BUD.fld_num());
         {! _i:=1..BUD.fld_num() |! _tab[_i]:=BUD[_i] !}
      || _tab:=null
      ?};
      exec('t_bud2','konsolidacja',_a,_rw,_ks_sym,_bpoz,_tab)
   ?}
?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
KS.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_bud2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Pomocnicza dla triggera po add, put i przed del tabeli BUD
::   WE: _a - 1-add 2-put 3-del
::       _b - rok
::       _c - symbol konta
::       _d - pozycja budowy
::       _e - tablica z danymi
::  OLD: \t_bud2/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
KS.cntx_psh();
KS.index('SYM'); KS.prefix(_b,_c);
{? KS.first()
|| BUD.cntx_psh(); BUD.index('KS'); BUD.prefix(KS.ref(),_d);
   {? BUD.first()
   || {? _a=2
      || {! _i:=1..BUD.fld_num() |! BUD[_i]:=_e[_i] !};
         BUD.prefix();
         BUD.KS:=KS.ref();
         BUD.put()
      |? _a=3
      || BUD.del()
      ?}
   |? _a=1
   || BUD.KS:=KS.ref();
      BUD.add()
   ?};
   BUD.cntx_pop()
?};
KS.cntx_pop()


\t_autoksie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add,put i przed del tabeli AUTOKSIE
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_autoksie/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
FIRMA.cntx_psh();
{? AUTOKSIE.ROK_F().FIRMA().TYP='C'
|| {? _a=2
   || _bnaz:=bfld('NAZ')
   || _bnaz:=AUTOKSIE.NAZ
   ?};
   _rok:=ROK_F.ref();
   {? _a=2
   || _tab:=obj_new(AUTOKSIE.fld_num());
      {! _i:=1..AUTOKSIE.fld_num() |! _tab[_i]:=AUTOKSIE[_i] !}
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
   {? OKRO_F_G.first()
   || {!
      |? {? OKRO_F_G.ROK_F().PLAN_GR='T'
         || AUTOKSIE.cntx_psh();
            AUTOKSIE.index('NAZ'); AUTOKSIE.prefix(ROK_F.ref(),_bnaz,);
            {? AUTOKSIE.first()
            || {? _a=2
               || {! _i:=1..AUTOKSIE.fld_num() |! AUTOKSIE[_i]:=_tab[_i] !};
                  AUTOKSIE.prefix();
                  AUTOKSIE.ROK_F:=ROK_F.ref();
                  AUTOKSIE.put()
               |? _a=3
               || AUTOKSIE.del()
               ?}
            |? _a=1
            || AUTOKSIE.ROK_F:=ROK_F.ref();
               AUTOKSIE.add()
            ?};
            AUTOKSIE.cntx_pop()
         ?};
         OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         OKRO_F_G.prefix(_rok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_form
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add, put i przed del tabeli FORM
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_form/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
AUTOKSIE.cntx_psh();
ROK_F.cntx_psh();
FIRMA.cntx_psh();
{? FORM.AUTOKSIE().ROK_F().FIRMA().TYP='C'
|| {? _a=2
   || _bpoz:=bfld('POZ')
   || _bpoz:=FORM.POZ
   ?};
   _rok:=ROK_F.ref();
   _auto:=AUTOKSIE.NAZ;
   {? _a=2
   || _tab:=obj_new(FORM.fld_num());
      {! _i:=1..FORM.fld_num() |! _tab[_i]:=FORM[_i] !}
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
   {? OKRO_F_G.first()
   || {!
      |? {? OKRO_F_G.ROK_F().PLAN_GR='T'
         || AUTOKSIE.cntx_psh();
            AUTOKSIE.index('NAZ'); AUTOKSIE.prefix(ROK_F.ref(),_auto,);
            {? AUTOKSIE.first()
            || FORM.cntx_psh();
               FORM.index('AUTOKSIE'); FORM.prefix(AUTOKSIE.ref(),_bpoz);
               {? FORM.first()
               || {? _a=2
                  || {! _i:=1..FORM.fld_num() |! {? _i<>6 || FORM[_i]:=_tab[_i] ?} !};
                     FORM.prefix();
                     FORM.AUTOKSIE:=AUTOKSIE.ref();
                     FORM.memo_put(,'FML');
                     FORM.put()
                  |? _a=3
                  || FORM.del()
                  |? _a=4
                  || FORM.memo_put(,'FML');
                     FORM.put()
                  ?}
               |? _a=1
               || FORM.AUTOKSIE:=AUTOKSIE.ref();
                  FORM.add();
                  FORM.memo_put(,'FML')
               ?};
               FORM.cntx_pop()
            ?};
            AUTOKSIE.cntx_pop()
         ?};
         OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         OKRO_F_G.prefix(_rok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
AUTOKSIE.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_av
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add,put i przed del tabeli AV
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_av/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
FIRMA.cntx_psh();
{? AV.ROK_F().FIRMA().TYP='C'
|| {? _a=2
   || _bkod:=bfld('KOD')
   || _bkod:=AV.KOD
   ?};
   _rok:=ROK_F.ref();
   {? _a=2
   || _tab:=obj_new(AV.fld_num());
      {! _i:=1..AV.fld_num() |! _tab[_i]:=AV[_i] !}
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
   {? OKRO_F_G.first()
   || {!
      |? {? OKRO_F_G.ROK_F().PLAN_GR='T'
         || AV.cntx_psh();
            AV.index('AV'); AV.prefix(ROK_F.ref(),_bkod,);
            {? AV.first()
            || {? _a=2
               || {! _i:=1..AV.fld_num() |! AV[_i]:=_tab[_i] !};
                  AV.prefix();
                  AV.ROK_F:=ROK_F.ref();
                  AV.put()
               |? _a=3
               || AV.del()
               ?}
            |? _a=1
            || AV.ROK_F:=ROK_F.ref();
               AV.add()
            ?};
            AV.cntx_pop()
         ?};
         OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         OKRO_F_G.prefix(_rok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_dek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add, put i przed del tabeli DEK
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_dek/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
AV.cntx_psh();
ROK_F.cntx_psh();
FIRMA.cntx_psh();
{? DEK.AV().ROK_F().FIRMA().TYP='C'
|| {? _a=2
   || _bpoz:=bfld('LP')
   || _bpoz:=DEK.LP
   ?};
   _rok:=ROK_F.ref();
   _av_kod:=AV.KOD;
   {? _a=2
   || _tab:=obj_new(DEK.fld_num());
      {! _i:=1..DEK.fld_num() |! _tab[_i]:=DEK[_i] !}
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
   {? OKRO_F_G.first()
   || {!
      |? {? OKRO_F_G.ROK_F().PLAN_GR='T'
         || AV.cntx_psh();
            AV.index('AV'); AV.prefix(ROK_F.ref(),_av_kod,);
            {? AV.first()
            || DEK.cntx_psh(); DEK.index('DEK'); DEK.prefix(AV.ref(),_bpoz);
               {? DEK.first()
               || {? _a=2
                  || {! _i:=1..DEK.fld_num() |! DEK[_i]:=_tab[_i] !};
                     DEK.prefix();
                     DEK.AV:=AV.ref();
                     DEK.put()
                  |? _a=3
                  || DEK.del()
                  ?}
               |? _a=1
               || DEK.AV:=AV.ref();
                  DEK.add()
               ?};
               DEK.cntx_pop()
            ?};
            AV.cntx_pop()
         ?};
         OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         OKRO_F_G.prefix(_rok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
AV.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_skid_wyr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add, put i przed del tabeli SKID_WYR
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_skid_wyr/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? SKID_WYR.TAB='DEK'
|| AV.cntx_psh();
   DEK.cntx_psh();
   ROK_F.cntx_psh();
   FIRMA.cntx_psh();
   DEK.prefix();
   {? DEK.seek(SKID_WYR.REF,) & DEK.AV().ROK_F().FIRMA().TYP='C'
   || {? _a=2
      || _bstr:=bfld('STR');
         _blp:=bfld('LP')
      || _bstr:=SKID_WYR.STR;
         _blp:=SKID_WYR.LP
      ?};
      _rok:=ROK_F.ref();
      _av_kod:=AV.KOD;
      _dek_lp:=DEK.LP;
      {? _a=2
      || _tab:=obj_new(SKID_WYR.fld_num());
         {! _i:=1..SKID_WYR.fld_num() |! _tab[_i]:=SKID_WYR[_i] !}
      ?};
      OKRO_F_G.cntx_psh();
      OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
      {? OKRO_F_G.first()
      || {!
         |? {? OKRO_F_G.ROK_F().PLAN_GR='T'
            || AV.cntx_psh();
               AV.index('AV'); AV.prefix(ROK_F.ref(),_av_kod,);
               {? AV.first()
               || DEK.cntx_psh(); DEK.index('DEK'); DEK.prefix(AV.ref(),_dek_lp);
                  {? DEK.first()
                  || SKID_WYR.cntx_psh();
                     SKID_WYR.index('SKID_WYR'); SKID_WYR.prefix(ROK_F.FIRMA,'DEK',DEK.name(),#DEK.ref(),_bstr,_blp);
                     {? SKID_WYR.first()
                     || {? _a=2
                        || {! _i:=1..SKID_WYR.fld_num() |! SKID_WYR[_i]:=_tab[_i] !};
                           SKID_WYR.prefix();
                           SKID_WYR.FIRMA:=ROK_F.FIRMA;
                           SKID_WYR.REF:=#DEK.ref();
                           SKID_WYR.TABNAME:=DEK.name();
                           SKID_WYR.put()
                        |? _a=3
                        || SKID_WYR.del()
                        ?}
                     |? _a=1
                     || SKID_WYR.prefix();
                        SKID_WYR.FIRMA:=ROK_F.FIRMA;
                        SKID_WYR.REF:=#DEK.ref();
                        SKID_WYR.TABNAME:=DEK.name();
                        SKID_WYR.add()
                     ?};
                     SKID_WYR.cntx_pop()
                  ?};
                  DEK.cntx_pop()
               ?};
               AV.cntx_pop()
            ?};
            OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
            OKRO_F_G.last();
            OKRO_F_G.prefix(_rok);
            OKRO_F_G.next()
         !}
      ?};
      OKRO_F_G.cntx_pop()
   ?};
   FIRMA.cntx_pop();
   ROK_F.cntx_pop();
   AV.cntx_pop();
   DEK.cntx_pop()
|? SKID_WYR.TAB='FORM'
|| AUTOKSIE.cntx_psh();
   ROK_F.cntx_psh();
   FIRMA.cntx_psh();
   FORM.cntx_psh();
   FORM.prefix();
   {? FORM.seek(SKID_WYR.REF,) & FORM.AUTOKSIE().ROK_F().FIRMA().TYP='C'
   || {? _a=2
      || _bstr:=bfld('STR');
         _blp:=bfld('LP')
      || _bstr:=SKID_WYR.STR;
         _blp:=SKID_WYR.LP
      ?};
      _rok:=ROK_F.ref();
      _auto:=AUTOKSIE.NAZ;
      _fpoz:=FORM.POZ;
      {? _a=2
      || _tab:=obj_new(SKID_WYR.fld_num());
         {! _i:=1..SKID_WYR.fld_num() |! _tab[_i]:=SKID_WYR[_i] !}
      ?};
      OKRO_F_G.cntx_psh();
      OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
      {? OKRO_F_G.first()
      || {!
         |? {? OKRO_F_G.ROK_F().PLAN_GR='T'
            || AUTOKSIE.cntx_psh();
               AUTOKSIE.index('NAZ'); AUTOKSIE.prefix(ROK_F.ref(),_auto,);
               {? AUTOKSIE.first()
               || FORM.cntx_psh();
                  FORM.index('AUTOKSIE'); FORM.prefix(AUTOKSIE.ref(),_fpoz);
                  {? FORM.first()
                  || SKID_WYR.cntx_psh();
                     SKID_WYR.index('SKID_WYR'); SKID_WYR.prefix(ROK_F.FIRMA,'FORM',FORM.name(),#FORM.ref(),_bstr,_blp);
                     {? SKID_WYR.first()
                     || {? _a=2
                        || {! _i:=1..SKID_WYR.fld_num() |! SKID_WYR[_i]:=_tab[_i] !};
                           SKID_WYR.prefix();
                           SKID_WYR.FIRMA:=ROK_F.FIRMA;
                           SKID_WYR.REF:=#FORM.ref();
                           SKID_WYR.TABNAME:=FORM.name();
                           SKID_WYR.put()
                        |? _a=3
                        || SKID_WYR.del()
                        ?}
                     |? _a=1
                     || SKID_WYR.prefix();
                        SKID_WYR.FIRMA:=ROK_F.FIRMA;
                        SKID_WYR.REF:=#FORM.ref();
                        SKID_WYR.TABNAME:=FORM.name();
                        SKID_WYR.add()
                     ?};
                     SKID_WYR.cntx_pop()
                  ?};
                  FORM.cntx_pop()
               ?};
               AUTOKSIE.cntx_pop()
            ?};
            OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
            OKRO_F_G.last();
            OKRO_F_G.prefix(_rok);
            OKRO_F_G.next()
         !}
      ?};
      OKRO_F_G.cntx_pop()
   ?};
   FIRMA.cntx_pop();
   ROK_F.cntx_pop();
   AUTOKSIE.cntx_pop();
   FORM.cntx_pop()
?};
{? _a=3 || 1 || ~~ ?}


\t_ks_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add, put i przed del tabeli KS_W
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_ks_w/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
KS.cntx_psh();
ROK_F.cntx_psh();
FIRMA.cntx_psh();
KS_W.cntx_psh();
{? KS_W.S().ROK().FIRMA().TYP='C'
|| {? _a=2
   || _blp:=bfld('LP')
   || _blp:=KS_W.LP
   ?};
   _rok:=ROK_F.ref();
   _ks_sym:=KS.SYM;
   {? _a=2
   || _tab:=obj_new(KS_W.fld_num());
      {! _i:=1..KS_W.fld_num() |! _tab[_i]:=KS_W[_i] !}
   || _tab:=null
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
   {? OKRO_F_G.first()
   || {!
      |? {? OKRO_F_G.ROK_F().PLAN_GR='T' & ROK_F.FIRMA().TYP='S'
         || exec('t_ks_w2','konsolidacja',_a,ROK_F.ref(),_ks_sym,_blp,_tab)
         ?};
         OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         OKRO_F_G.prefix(_rok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
|? FIRMA.TYP='S'
|| {? _a=2
   || _blp:=bfld('LP')
   || _blp:=KS_W.LP
   ?};
   _ks_sym:=KS.SYM;
   {? _a=2
   || _tab:=obj_new(KS_W.fld_num());
      {! _i:=1..KS_W.fld_num() |! _tab[_i]:=KS_W[_i] !}
   || _tab:=null
   ?};
   _rw:=exec('rok_f_w','konsolidacja',FIRMA.ref(),ROK_F.ref());
   exec('t_ks_w2','konsolidacja',_a,_rw,_ks_sym,_blp,_tab)
?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
KS.cntx_pop();
KS_W.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_ks_w2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Pomocnicza dla trigger po add, put i przed del tabeli KS_W
::   WE: _a - 1-add 2-put 3-del
::       _b - rok
::       _c - symbol konta
::       _d - LP wyroznika
::       _e - tablica z danymi
::  OLD: \t_ks_w2/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
KS.cntx_psh();
KS.index('SYM'); KS.prefix(_b,_c);
{? KS.first()
|| KS_W.cntx_psh(); KS_W.index('LP'); KS_W.prefix(KS.ref(),_d);
   {? KS_W.first()
   || {? _a=2
      || {! _i:=1..KS_W.fld_num() |! KS_W[_i]:=_e[_i] !};
         KS_W.prefix();
         KS_W.S:=KS.ref();
         KS_W.put()
      |? _a=3
      || KS_W.del()
      ?}
   |? _a=1
   || KS_W.S:=KS.ref();
      KS_W.add()
   ?};
   KS_W.cntx_pop()
?};
KS.cntx_pop()


\t_kom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add, put i przed del tabeli KOM
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_kom/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
KS.cntx_psh();
ROK_F.cntx_psh();
FIRMA.cntx_psh();
{? KOM.KS().ROK().FIRMA().TYP='C'
|| {? _a=2
   || _bnaz:=bfld('NAZ')
   || _bnaz:=KOM.NAZ
   ?};
   _rok:=ROK_F.ref();
   _ks_sym:=KS.SYM;
   {? _a=2
   || _tab:=obj_new(KOM.fld_num());
      {! _i:=1..KOM.fld_num() |! _tab[_i]:=KOM[_i] !}
   || _tab:=null
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
   {? OKRO_F_G.first()
   || {!
      |? {? OKRO_F_G.ROK_F().PLAN_GR='T' & ROK_F.FIRMA().TYP='S'
         || exec('t_kom2','konsolidacja',_a,ROK_F.ref(),_ks_sym,_bnaz,_tab)
         ?};
         OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         OKRO_F_G.prefix(_rok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
|? FIRMA.TYP='S'
|| _rw:=exec('rok_f_w','konsolidacja',FIRMA.ref(),ROK_F.ref());
   {? _rw
   || {? _a=2
      || _bnaz:=bfld('NAZ')
      || _bnaz:=KOM.NAZ
      ?};
      _ks_sym:=KS.SYM;
      {? _a=2
      || _tab:=obj_new(KOM.fld_num());
         {! _i:=1..KOM.fld_num() |! _tab[_i]:=KOM[_i] !}
      || _tab:=null
      ?};
      exec('t_kom2','konsolidacja',_a,_rw,_ks_sym,_bnaz,_tab)
   ?}
?};
{? _a=2 || VAR_DEL.delete('__tab') ?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
KS.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_kom2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Pomicnicza dla triggera po add, put i przed del tabeli KOM
::   WE: _a - 1-add 2-put 3-del
::       _b - rok
::       _c - symbol konta
::       _d - nazwa konta
::       _e - tablica z danymi
::  OLD: \t_kom2/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
KS.cntx_psh();
KS.index('SYM'); KS.prefix(_b,_c);
{? KS.first()
|| KOM.cntx_psh(); KOM.index('KS'); KOM.prefix(KS.ref(),_d);
   {? KOM.first()
   || {? _a=2
      || {! _i:=1..KOM.fld_num() |! KOM[_i]:=_e[_i] !};
         KOM.prefix();
         KOM.KS:=KS.ref();
         KOM.put()
      |? _a=3
      || KOM.del()
      ?}
   |? _a=1
   || KOM.prefix();
      KOM.KS:=KS.ref();
      KOM.add()
   ?};
   KOM.cntx_pop()
?};
KS.cntx_pop()


\t_alg_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add,put i przed del tabeli ALG_PAR
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_alg_par/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
FIRMA.cntx_psh();
{? ALG_PAR.ROK().FIRMA().TYP='C'
|| {? _a=2
   || _brok:=bfld('ROK');
      _blp:=bfld('LP');
      _bkol:=bfld('GR_KOL')
   || _brok:=ALG_PAR.ROK;
      _blp:=ALG_PAR.LP;
      _bkol:=ALG_PAR.GR_KOL
   ?};
   _rok:=ROK_F.ref();
   {? _a=2
   || _tab:=obj_new(ALG_PAR.fld_num());
      {! _i:=1..ALG_PAR.fld_num() |! _tab[_i]:=ALG_PAR[_i] !}
   || _tab:=null
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(_brok);
   {? OKRO_F_G.first()
   || {!
      |? {? OKRO_F_G.ROK_F().PLAN_GR='T' & ROK_F.FIRMA().TYP='S'
         || {? _a=2 & _brok<>_rok
            || exec('t_alg_par3','konsolidacja',_rok,_blp,_tab,_bkol)
            || exec('t_alg_par2','konsolidacja',_a,_blp,_tab,_bkol)
            ?}
         |? _a=2 & _brok<>_rok & OKRO_F_G.ROK_F().PLAN_GR<>'T'
         || exec('t_alg_par3','konsolidacja',_rok,_blp,_tab,_bkol)
         ?};
         OKRO_F_G.prefix(_brok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         OKRO_F_G.prefix(_brok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
|? FIRMA.TYP='S'
|| _rw:=exec('rok_f_w','konsolidacja',FIRMA.ref(),ROK_F.ref());
   {? _rw<>null
   || {? _a=2
      || _blp:=bfld('LP');
         _bkol:=bfld('GR_KOL')
      || _blp:=ALG_PAR.LP;
         _bkol:=ALG_PAR.GR_KOL
      ?};
      {? _a=2
      || _tab:=obj_new(ALG_PAR.fld_num());
         {! _i:=1..ALG_PAR.fld_num() |! _tab[_i]:=ALG_PAR[_i] !}
      || _tab:=null
      ?};
      ROK_F.prefix(); ROK_F.seek(_rw);
      exec('t_alg_par2','konsolidacja',_a,_blp,_tab,_bkol)
   ?}
?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_alg_par2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Pomocnicza dla triggera po add,put i przed del tabeli ALG_PAR
::   WE: _a - 1-add 2-put 3-del
::       _b - LP algorytmu
::       _c - tablica z danymi
::       _d - GR_KOL algorytmu
::  OLD: \t_alg_par2/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ALG_PAR.cntx_psh();
ALG_PAR.index('ALG_PAR7');
ALG_PAR.prefix(ROK_F.FIRMA,ALG_PAR.DEFW,_d,ALG_PAR.WYL,ROK_F.NAZ,'G',_b);
{? ALG_PAR.first()
|| {? _a=2
   || {! _i:=1..ALG_PAR.fld_num() |! ALG_PAR[_i]:=_c[_i] !};
      ALG_PAR.prefix();
      ALG_PAR.FIRMA:=ROK_F.FIRMA;
      ALG_PAR.ROK:=ROK_F.ref();
      ALG_PAR.MIEJSCE:='G';
      ALG_PAR.put()
   |? _a=3
   || W_ALGPAR.cntx_psh();
      ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(ROK_F.FIRMA);
      {? ROK_F.first()
      || {!
         |? W_ALGPAR.use('walg_'+ROK_F.KOD);
            W_ALGPAR.index('W_ALGPAR'); W_ALGPAR.prefix(ALG_PAR.ref());
            {? W_ALGPAR.first() || {! |? W_ALGPAR.del() !} ?};
            ROK_F.next()
         !}
      ?};
      ROK_F.cntx_pop();
      W_ALGPAR.cntx_pop();
      ALG_PAR.del()
   ?}
|? _a=1
|| ALG_PAR.prefix();
   ALG_PAR.FIRMA:=ROK_F.FIRMA;
   ALG_PAR.ROK:=ROK_F.ref();
   ALG_PAR.MIEJSCE:='G';
   ALG_PAR.add()
?};
ALG_PAR.cntx_pop()


\t_alg_par3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [BZ] [12.41]
:: OPIS: Pomocnicza dla triggera po put tabeli ALG_PAR w przypadku zmiany roku
::   WE: _a - ref nowego roku
::       _b - LP algorytmu
::       _c - tablica z danymi
::       _d - GR_KOL algorytmu
::  OLD: \t_alg_par3/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); OKRO_F_G.cntx_psh(); OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix();
_rok:={? OKRO_F_G.find_key(_a,ROK_F.FIRMA) & OKRO_F_G.ROK_F().PLAN_GR='T'
      || OKRO_F_G.ROK_F
      || null()
      ?};
ROK_F.cntx_pop(); OKRO_F_G.cntx_pop();

ALG_PAR.cntx_psh();
ALG_PAR.index('ALG_PAR7');
ALG_PAR.prefix(ROK_F.FIRMA,ALG_PAR.DEFW,_d,ALG_PAR.WYL,ROK_F.NAZ,'G',_b);
{? ALG_PAR.first()
|| {? _rok=null()
:: nie ma nowego roku w grupie dla aktualizowanej firmy
   || W_ALGPAR.cntx_psh();
      ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(ROK_F.FIRMA);
      {? ROK_F.first()
      || {!
         |? W_ALGPAR.use('walg_'+ROK_F.KOD);
            W_ALGPAR.index('W_ALGPAR'); W_ALGPAR.prefix(ALG_PAR.ref());
            {? W_ALGPAR.first() || {! |? W_ALGPAR.del() !} ?};
            ROK_F.next()
         !}
      ?};
      ROK_F.cntx_pop();
      W_ALGPAR.cntx_pop();
      ALG_PAR.del()
:: jest rok w grupie i jest on znaleziony po zmienna _ref
   || {! _i:=1..ALG_PAR.fld_num() |! ALG_PAR[_i]:=_c[_i] !};
      ALG_PAR.prefix();
      ALG_PAR.FIRMA:=ROK_F.FIRMA;
      ALG_PAR.ROK:=_rok;
      ALG_PAR.MIEJSCE:='G';
      ALG_PAR.put()
   ?}
|? _rok<>null()
|| ALG_PAR.prefix();
   ALG_PAR.FIRMA:=ROK_F.FIRMA;
   ALG_PAR.ROK:=_rok;
   ALG_PAR.MIEJSCE:='G';
   ALG_PAR.add()
?};
ALG_PAR.cntx_pop()


\t_an_bo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add,put i przed del tabeli AN_BO
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_an_bo/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh();
FIRMA.cntx_psh();
{? AN_BO.ROK().FIRMA().TYP='C'
|| {? _a=2
   || _bkon:=bfld('PKON')
   || _bkon:=AN_BO.PKON
   ?};
   _rok:=ROK_F.ref();
   {? _a=2
   || _tab:=obj_new(AN_BO.fld_num());
      {! _i:=1..AN_BO.fld_num() |! _tab[_i]:=AN_BO[_i] !}
   || _tab:=null
   ?};
   OKRO_F_G.cntx_psh();
   OKRO_F_G.index('ROK_F_G'); OKRO_F_G.prefix(ROK_F.ref());
   {? OKRO_F_G.first()
   || {!
      |? {? OKRO_F_G.ROK_F().PLAN_GR='T'
         || _ok:=0;
            ROK_F.cntx_psh();
            ROK_F.index('ROKPOCZ'); ROK_F.prefix(ROK_F.FIRMA);
            {? ROK_F.prev()
            || _ok:=ROK_F.PLAN_GR='T'
            ?};
            ROK_F.cntx_pop();
            {? _ok
            || AN_BO.cntx_psh();
               AN_BO.index('ROK'); AN_BO.prefix(ROK_F.ref(),_bkon);
               {? AN_BO.first()
               || {? _a=2
                  || {! _i:=1..AN_BO.fld_num() |! AN_BO[_i]:=_tab[_i] !};
                     AN_BO.prefix();
                     AN_BO.ROK:=ROK_F.ref();
                     AN_BO.put()
                  |? _a=3
                  || AN_BO.del()
                  ?}
               |? _a=1
               || AN_BO.ROK:=ROK_F.ref();
                  AN_BO.add()
               ?};
               AN_BO.cntx_pop()
            ?}
         ?};
         OKRO_F_G.prefix(_rok,OKRO_F_G.FIRMA);
         OKRO_F_G.last();
         OKRO_F_G.prefix(_rok);
         OKRO_F_G.next()
      !}
   ?};
   OKRO_F_G.cntx_pop()
?};
FIRMA.cntx_pop();
ROK_F.cntx_pop();
{? _a=3 || 1 || ~~ ?}


\t_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add, put i przed del tabeli FIRMA
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_firma/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? FIRMA.TYP='W'
   || 1
   |? FIRMA.TYP='S'
   || 1
   ?};
   ~~
?}


\t_wersje_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Trigger po add,put i przed del tabeli WERSJE_W
::   WE: _a - 1-add 2-put 3-del
::  OLD: \t_wersje_w/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| WERSJE.cntx_psh();
   WERSJE.index('KOD'); WERSJE.prefix();
   FIRMA.for_each("
      {? ~WERSJE.find_key('N',null,FIRMA.ref(),null,WERSJE_W.KOD,)
      || WERSJE.blank(1);
         WERSJE.KOD:=WERSJE_W.KOD;
         WERSJE.OPIS:=WERSJE_W.OPIS;
         WERSJE.WYKON:='N';
         WERSJE.WERSJE_W:=WERSJE_W.ref();
         WERSJE.FIRMA:=FIRMA.ref();
         WERSJE.add()
      ?}
   ");
   WERSJE.cntx_pop()
|? _a=2
|| _bkod:=bfld('KOD');
   WERSJE.cntx_psh();
   WERSJE.index('WERSJE_W'); WERSJE.prefix(WERSJE_W.ref());
   {? WERSJE.first()
   || {!
      |? WERSJE.KOD:=WERSJE_W.KOD;
         WERSJE.OPIS:=WERSJE_W.OPIS;
         WERSJE.put();
         WERSJE.next()
      !}
   ?};
   WERSJE.cntx_pop()
?};
~~


\rok_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Rok firmy/grupy na podstawie roku grupy/firmy
::   WE: _a - rok
::  OLD: \rok_f/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
ROK_F.prefix();
{? ROK_F.seek(_a)
|| {? ROK_F.FIRMA=REF.GRUPA
   || OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref(),1);
      {? OKRO_F.first()
      || OKRO_F_G.index('OKRO_F_G'); OKRO_F_G.prefix(OKRO_F.ref(),REF.FIRMA);
         {? OKRO_F_G.first()
         || OKRO_F_G.ROK_F
         || null
         ?}
      || null
      ?}
   |? ROK_F.PLAN_GR='T'
   || OKRO_F_G.index('ROK_F'); OKRO_F_G.prefix(ROK_F.ref());
      {? OKRO_F_G.first()
      || OKRO_F_G.OKRO_F_G().ROK
      || null
      ?}
   || ROK_F.cntx_psh();
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA,ROK_F.POCZ,ROK_F.KON);
      _ref:={? ROK_F.first() || ROK_F.ref() || _a ?};
      ROK_F.cntx_pop();
      _ref
   ?}
?}


\ae_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Po redakcji pola
::  OLD: \ae_firma/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld
|| FIRMA.cntx_psh();
   {? STALE.AF<>REF.FIRMA
   || {? REF.FIRMA=REF.GRUPA
      || OKRESY.ROK1:=exec('rok_f_g','konsolidacja',OKRESY.ROK1)
      || OKRESY.ROK1:=exec('rok_f_f','konsolidacja',OKRESY.ROK1)
      ?};
      {? PARAM.ROK=null || PARAM.OKROTXT:='' ?};
      STALE.AF:=REF.FIRMA;
      OKRO_F.cntx_psh();
      OKRO_F.index('ROK'); OKRO_F.prefix(REF.FIRMA);
      {? OKRESY.OKRES_OD().ROK<>OKRESY.ROK1
      || OKRESY.OKRES_OD:={? OKRO_F.find_key(OKRESY.OKRES_OD().NR) || OKRO_F.ref() || null ?}
      ?};
      {? OKRESY.OKRES_DO().ROK<>OKRESY.ROK1
      || OKRESY.OKRES_DO:={? OKRO_F.find_key(OKRESY.OKRES_DO().NR) || OKRO_F.ref() || null ?}
      ?};
      OKRO_F.cntx_pop();
      {? OPERATOR.DEPT & OPERATOR.DEPT().FIRMA<>REF.FIRMA || OPERATOR.DEPT_WYB:='' ?}
   ?};
   {? REF.FIRMA().TYP='S' | REF.FIRMA().TYP='W' || REF.W_SCH:=null ?};
   FIRMA.cntx_pop()
?};
_ok:=REF.S_FIRMA=REF.GRUPA & REF.FIRMA & REF.FIRMA().TYP<>'S' & FIRMA.TYP<>'W';
{? cur_tab=OKRESY
|| OKRESY.efld_opt(OKRESY.win_edit('?'),'mark='+$_ok,REF,'W_SCH');
   OKRESY.efld_opt(OKRESY.win_edit('?'),'enable='+$_ok,REF,'W_SCH')
|? cur_tab=ROZNE
|| ROZNE.efld_opt(ROZNE.win_edit('?'),'mark='+$_ok,REF,'W_SCH');
   ROZNE.efld_opt(ROZNE.win_edit('?'),'enable='+$_ok,REF,'W_SCH')
?};
1


\ud_typ4gr_sik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.10]
:: OPIS: Tworzenie typu danych dla sprawozdan
::  OLD: \ud_typ4gr_sik/skid_kns.fml
::----------------------------------------------------------------------------------------------------------------------
UD_TYP.index('SYMBOL'); UD_TYP.prefix();
UD_SCH.index('SYMBOL'); UD_SCH.prefix();
{? ~UD_TYP.find_key('SPR_PB',)
|| UD_TYP.blank(1);
   UD_TYP.SYMBOL:='SPR_PB';
   UD_TYP.OPIS:='Sprawozdanie finansowe';
   UD_TYP.SYSTEM:='T';
   UD_TYP.add()
?};
{? ~UD_TYP.find_key('SPR_KOL',)
|| UD_TYP.blank(1);
   UD_TYP.SYMBOL:='SPR_KOL';
   UD_TYP.OPIS:='Kolumny sprawozdań finansowych';
   UD_TYP.SYSTEM:='T';
   UD_TYP.add()
?};
{? ~UD_TYP.find_key('SPR_FIR',)
|| UD_TYP.blank(1);
   UD_TYP.SYMBOL:='SPR_FIR';
   UD_TYP.OPIS:='Firmy dla sprawozdań finansowych';
   UD_TYP.SYSTEM:='T';
   UD_TYP.add()
?};
{? ~UD_TYP.find_key('SPR_TYP',)
|| UD_TYP.blank(1);
   UD_TYP.SYMBOL:='SPR_TYP';
   UD_TYP.OPIS:='Typ wartości sprawozdań finansowych';
   UD_TYP.SYSTEM:='T';
   UD_TYP.add()
?};
{? ~UD_SCH.find_key(UD_TYP.ref(),'SPR_TYP',)
|| UD_SCH.blank(1);
   UD_SCH.UD_TYP:=UD_TYP.ref();
   UD_SCH.SYMBOL:='SPR_TYP';
   UD_SCH.OPIS:='Typ wartości dla sprawozdań finasowych';
   UD_SCH.SYSTEM:='T';
   UD_SCH.add()
?};
_ud_skl:=exec('dodaj_ud_skl','schemat',UD_TYP.ref(),'Skonsolidowane','Wartości skonsolidowane');
{? _ud_skl || exec('dodaj_ud_def','schemat',UD_SCH.ref(),null,_ud_skl) ?};
_ud_skl:=exec('dodaj_ud_skl','schemat',UD_TYP.ref(),'Jednostkowe','Wartości jednostkowe');
{? _ud_skl || exec('dodaj_ud_def','schemat',UD_SCH.ref(),null,_ud_skl) ?};
_ud_skl:=exec('dodaj_ud_skl','schemat',UD_TYP.ref(),'Wyłączeniowe','Wartości wyłączeń');
{? _ud_skl || exec('dodaj_ud_def','schemat',UD_SCH.ref(),null,_ud_skl) ?};
{? ~UD_TYP.find_key('SPR_FWYL',)
|| UD_TYP.blank(1);
   UD_TYP.SYMBOL:='SPR_FWYL';
   UD_TYP.OPIS:='Firma wyłączeń sprawozdań finansowych';
   UD_TYP.SYSTEM:='T';
   UD_TYP.add()
?};
{? ~UD_SCH.find_key(UD_TYP.ref(),'SPR_FWYL',)
|| UD_SCH.blank(1);
   UD_SCH.UD_TYP:=UD_TYP.ref();
   UD_SCH.SYMBOL:='SPR_FWYL';
   UD_SCH.OPIS:='Firma wyłączeń sprawozdań finasowych';
   UD_SCH.SYSTEM:='T';
   UD_SCH.add()
?};
FIRMA.index('SYMBOL2'); FIRMA.prefix('S');
{? FIRMA.first()
|| {!
   |? _ud_skl:=exec('dodaj_ud_skl','schemat',UD_TYP.ref(),FIRMA.SYMBOL,FIRMA.OPIS);
      {? _ud_skl || exec('dodaj_ud_def','schemat',UD_SCH.ref(),null,_ud_skl) ?};
      FIRMA.next()
   !}
?};
~~


\kon_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Zwraca firmę wyłączeniową z na podstawie konta
::   WE: _a - konto księgowe
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
_firma:=null;
{? SIK.NR_SL=null

|| {? var_press('_a')=type_of('')
   || KS.cntx_psh(); AN.cntx_psh();
      KS.index('SYM'); KS.prefix(SSTALE.AR,SSTALE.AR().SYNT+_a,);
      {? KS.first()
      || AN.index('KS'); AN.prefix(KS.ref(),_a);
         _ok:=AN.first()
      ?}
   || _ok:=1
   ?};
   {? _ok
   || _kon:=AN.SYM;
      _kon:=AN.KS().ROK().SYNT-_kon;
      _sep:={? ROK_F.SEP=',' || 0 || 1 ?};
      RS.cntx_psh();
      RS.index('RS');
      RS.prefix();
      BUD.cntx_psh();
      BUD.index('KS'); BUD.prefix(AN.KS);
      {? BUD.first()
      || {!
         |? _kon:=_sep-_kon;
            _an:=BUD.SLU().SLU().DL+_kon;
            _kon:=SLU.DL-_kon;
            {? RS.find_key(BUD.SLU().SLU().WZ,) & RS.TAB='FIRMA'
            || FIRMA.cntx_psh();
               FIRMA.index('SYMBOL'); FIRMA.prefix(_an,);
               _firma:={? FIRMA.first() || FIRMA.ref() || null ?};
               FIRMA.cntx_pop()
            ?};
            _firma=null & BUD.next()
         !}
      ?};
      BUD.cntx_pop();
      RS.cntx_pop()
   ?};
   {? var_press('_a')=type_of('')
   || KS.cntx_pop(); AN.cntx_pop()
   ?}
?};
_firma


\wyr_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Zwraca firmę wyłączeniową na podstawie wyróżnika i słownika opartego na tabelę FIRMA
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=null;
{? SIK.NR_SL<>null

|| RS.index('RS');
   RS.prefix();
   {? RS.find_key(POW.SLU().SLU().WZ,) & RS.TAB='FIRMA'
   || FIRMA.index('SYMBOL');
      FIRMA.prefix(POW.SLO().KOD,);
      {? FIRMA.first()
      || _zwrot:=FIRMA.ref()
      ?}
   ?}
?};
_zwrot


\be_ref_firmawyl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.42]
:: OPIS: Przed redakcją REF.FIRMAWYL
::----------------------------------------------------------------------------------------------------------------------
__F_ref:='S';
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:39 1aa35d7c99f6341aebb0f09ba681952ed1bd5f5da128f628dbc2a1eee6f01efb309d81a4a5460f1474951365bc9b35c3cd3c79ab3bd9a391b6e0e82ce9b759acdfbc4ed72fd2e1ab877c7c925bdf727eb93cf3aafae171c9b92fbc294723e733d79b9abc7d3385c2d7208b4c276dbcd9c33430e6403ae7f3a95742bc36432eb3

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: kraje.fml
:: Utworzony: 2014.12.08
:: Autor: AMK
::======================================================================================================================
:: Zawartosc:  Procedury do obsługi tabeli KRAJE
::======================================================================================================================


\be_kraj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.70]
:: OPIS: Przed redakcja i usuwaniem rekordu tabeli KRAJE.
::   WE: _a - czy wyświetlać komunikat (0 - nie, 1 - tak)
::  OLD: \be_kraj/skid_ror.fml
::----------------------------------------------------------------------------------------------------------------------
SLU.cntx_psh(); SLO.cntx_psh();
SLU.index('NAZ'); SLU.prefix('~KRAJE UE'); _ret:=1;
{? 1+menu_txt()<>'D' & SLU.first() & SLU.NAZ='~KRAJE UE'
|| KRAJE.cntx_psh(); KRAJE.get(); _sym:=KRAJE.SYM; KRAJE.cntx_pop();
   SLO.index('SL'); SLO.prefix(SLU.ref(),_sym);
   {? SLO.first() || {! |? {? SLO.KOD=_sym || _ret:=0 ?}; _ret & SLO.next() !} ?}
?};
{? ~_ret & (~_ | _a=1)
|| FUN.emsg('Rekord wykorzystywany w słowniku użytkownika.'@)
?};
SLU.cntx_pop(); SLO.cntx_pop();
_ret


\rakraje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMA] [8.70]
:: OPIS: Rekord po dla tabeli KRAJE
::  OLD: \rakraje/skid_ror.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~+KRAJE.NAZ
|| FUN.emsg('Nie podano nazwy kraju.'@); 'NAZ'
|| {? ~+KRAJE.KODISO
   || FUN.emsg('Nie podano kodu ISO kraju.'@); 'KODISO'
   || {? +KRAJE.KODISO=1
      || FUN.emsg('Niepoprawny kod ISO kraju.'@); 'KODISO'
      || {? ~+KRAJE.SYM
         || FUN.emsg('Nie podano kodu UE kraju.'@); 'SYM'
         || {? +KRAJE.SYM=1
            || FUN.emsg('Niepoprawny kod kraju.'@); 'SYM'
            || _vr:=KRAJE.ref();
               _vs:=KRAJE.SYM; _ret:=1;
               KRAJE.cntx_psh(); KRAJE.index('KRAJE'); KRAJE.prefix(_vs);
               {? KRAJE.first() & KRAJE.SYM=_vs || {? -(1+menu_txt())='d' | KRAJE.ref()<>_vr || _ret:=0 ?} ?};
               KRAJE.cntx_pop();
               {? _ret
               || _vk:=KRAJE.KODISO;
                  KRAJE.cntx_psh(); KRAJE.index('KODISO'); KRAJE.prefix(_vk,_vk);
                  {? KRAJE.first() || {? -(1+menu_txt())='d' | KRAJE.ref()<>_vr || _ret:=0 ?} ?};
                  KRAJE.cntx_pop();
                  {? ~_ret
                  || FUN.emsg('Istnieje już kraj o podanym kodzie ISO.'@); 'KODISO'
                  || {? ~+KRAJE.IBAN
                     || FUN.emsg('Nie podano, czy rachunki bankowe są w formacie IBAN.'@); 'IBAN'
                     || ''
                     ?}
                  ?}
               || FUN.emsg('Istnieje już kraj o podanym kodzie UE.'@); 'SYM'
               ?}
            ?}
         ?}
      ?}
   ?}
?}


\ust_vwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL  [8.40]
:: OPIS: Formula ustala walute dla symbolu kraju UE
::   WE: _a - symbol kraju
::   WY: ref SLO
::  OLD: \ust_vwal/skid_kh.fml
::----------------------------------------------------------------------------------------------------------------------
KRAJE.index('KRAJE');
KRAJE.prefix();
{? KRAJE.find_key(_a) || KRAJE.WAL || null ?}

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:53 97e41fe555cef0449c615ab5b488d308015d615813171c90a99332f15b8cdbdb5319b7e0eb8e9ea53eca737cdcc30824aed4dc0ac77be3d7c548dee5a5f379eca562ea11ec83f724038ee2fdc53caaab718c7b9a2601bd008a442801b85b7cc2daeade3dc19322e7ea97fcaa1ff1f681ee5237d1147a94a6738054e8c9171462

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obe_faw.fml
:: Utworzony: 23.06.2020
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły obszaru OBE do obsługi wniosków w obiegu
::======================================================================================================================


\akc_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akcja dodania wniosku w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? exec('chk_okres','obiegi2',1,1)>=0
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FAW_DARP';
   _params.AKCJA:='Dołącz';
   _params.PROC_START:='T';
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'M_WYW','W');
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'R_WYW','JW');
   exec('mp_run','#b__box',_params);
   exec('akc_f_upd','obe_faw')
?}


\akc_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akcja poprawiania wniosku w obiegu
::   WE: [_a] - dodawać kontekst roku
::----------------------------------------------------------------------------------------------------------------------
_cnt_rok:={? var_pres('_a')=type_of(1) || _a ?};
_jest:=exec('is_act_user','#b__box','OBE_FAW_DARP',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
{? _jest
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FAW_DARP';
   _params.AKCJA:='Popraw';
   _params.UIDREF:=EDOKUM.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',EDOKUM.ref());
   _params.PROC_START:='N';
   {? _cnt_rok
   || _params.CONTEXT:=obj_new('ROK');
      _params.CONTEXT.ROK:=EDOKUM.ROK_F
   ?};
   params_set('params',_params);
   exec('mp_run','#b__box',_params);
   exec('akc_f_upd','obe_faw')
|| _txt:='Rejestracja wniosku nie jest na liście zadań użytkownika.'@;
   {? EDOKUM.B_PRELS<>''
   || _txt+='Wniosek jest na etapie: %1\n'@[EDOKUM.B_PRELS]
   ?};
   FUN.info(_txt)
?}


\akc_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akcja usunięcia wniosku w obiegu
::----------------------------------------------------------------------------------------------------------------------
_jest:=exec('is_act_user','#b__box','OBE_FAW_DARP',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
{? _jest
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FAW_DARP';
   _params.AKCJA:='Usuń';
   _params.UIDREF:=EDOKUM.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',EDOKUM.ref());
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params)
|| {? EDOKUM.ZAM='T'
   || {? exec('chk_role','#b__box',OPERATOR.USER,'OBE_FAW_ZAMK')
      || exec('us_dob','obiegi')
      || FUN.emsg('Brak uprawnień do usuwania zamkniętych dokumentów.'@)
      ?}
   || _txt:='Rejestracja wniosku nie jest na liście zadań użytkownika.'@;
      {? EDOKUM.B_PRELS<>''
      || _txt+='Wniosek jest na etapie: %1\n'@[EDOKUM.B_PRELS]
      ?};
      FUN.info(_txt)
   ?}
?}



\akc_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akcja kończenia czynności rejestracji wniosku
::----------------------------------------------------------------------------------------------------------------------
_jest:=exec('is_act_user','#b__box','OBE_FAW_DARP',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
{? _jest
|| {? FUN.ask('Czy zakończyć rejestrację wniosku?'@)
   || _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='OBE_FAW_DARP';
      _params.AKCJA:='Zakończ';
      _params.UIDREF:=EDOKUM.uidref();
      _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',EDOKUM.ref());
      _params.PROC_START:='N';
      exec('mp_run','#b__box',_params);
      exec('akc_f_upd','obe_faw')
   ?}
|| _txt:='Rejestracja wniosku nie jest na liście zadań użytkownika.'@;
   {? EDOKUM.B_PRELS<>''
   || _txt+='Wniosek jest na etapie: %1\n'@[EDOKUM.B_PRELS]
   ?};
   FUN.info(_txt)
?}


\akc_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akcja zamknięcia obiegu
::----------------------------------------------------------------------------------------------------------------------
{? zakres=1
|| _jest:=exec('is_act_user','#b__box','OBE_FAW_DARP',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
   {? _jest
   || {? FUN.ask('Operacja nieodwracalna.\nZamknąć obieg wniosku?'@)
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:='OBE_FAW_DARP';
         _params.AKCJA:='Zamknij';
         _params.UIDREF:=EDOKUM.uidref();
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',EDOKUM.ref());
         _params.PROC_START:='N';
         exec('mp_run','#b__box',_params);
         exec('akc_f_upd','obe_faw')
      ?}
   || {? exec('chk_role','#b__box',OPERATOR.USER,'OBE_FAW_ZAMK')
      || {? FUN.ask('Nie znaleziono wniosku na liście zadań użytkownika.\nOperacja nieodwracalna. Zamknąć obieg wniosku?'@)
         || exec('zamknij2','obiegi_akc')

         ?}
      || FUN.emsg('Rejestracja wniosku nie jest na liście zadań użytkownika.\nBrak uprawnień do zamknięcia obiegu wniosku.'@)
      ?}
   ?}
|| exec('ob_zam','obiegi2','A','W')
?}


\akc_f_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Aktualizacja filtra
::----------------------------------------------------------------------------------------------------------------------
{? BPMN.END & EDOKUM.f_active() & (zakres=1 | zakr_wpr<>4)
|| EDOKUM.f_next() | EDOKUM.f_prev();
   EDOKUM.f_rfresh()
?};
~~


\create_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Tworzy okno redagowania dla wniosku
::   WE:  _a  - obiekt do obsługi tabel
::       [_b] - tylko informacje dodatkowe? 1-tak 0-nie
::       [_c] - typ akcji: 'R'-rejestracja 'P'-przekazania 'A'-akceptacji 'D'-wyświetlenie
::       [_d] - czy dodać przycisk zakończ? [1]-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_tab:=_obj.getTab('REKORD');
_all:=0;
_w_str:=100;
_w_number:=13;
_edok:=var_pres('_b')<=0 | _b=0;
_akcja:={? var_pres('_c')>0 || _c || 'D' ?};
_dokum:=EDOKUM.DOKUM<>'';
_istyp:=EDOKUM.TYP<>null;
EDOKUM.TYP();
_wp:=ETYPY.W_PORTAL<>'N';
_seod:=ETYPY.W_PORTAL='S';
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null);
_atr:=ETYP_ATR.first();
ETYP_ATR.cntx_pop();
_tabs:=_all | ETYPY.CZY_KH | _dokum | ETYPY.CZY_ZALP='T' | ETYPY.DEKR='T' | _atr | _edok & EDOKUM.ODD=null;
_tyt:={? _edok || 'Wniosek' || 'Informacje dodatkowe' ?};
_editable:={? _akcja='R' || 'editable=1'  || 'editable=0' ?};
_red:=_tab.mk_edit(_tyt,,'obe_faw_red');
{? _edok
|| {? _tabs
   || _tab.win_etab(_red,'Dane ogólne'@)
   ?};
   _tab.win_esep(_red,'Dane ogólne'@);
   {? _wp
   || _tab.win_efld(_red,EDOKUM,'DATAW',,,,,,'Data rejestracji'@);
      _tab.efld_opt(_red,_editable,EDOKUM,'DATAW')
   ?};
   {? ETYPY.CZY_DOPE='T' | _all
   || _tab.win_efld(_red,EDOKUM,'DOP',,,,,,'Operacja z dnia'@);
      _tab.efld_opt(_red,_editable+',mark=1',EDOKUM,'DOP')
   ?};
   {? exec('be_id_edokum1','obiegi') | _all
   || _tab.win_efld(_red,EDOKUM,'ID',,,_w_str);
      _tab.efld_opt(_red,_editable,EDOKUM,'ID')
:: mark, edit
   ?};
   _tab.win_efld(_red,EDOKUM,'TYP','NAZWA','DISP6',_w_str-3,,0,'Typ'@);
   {? _istyp
   || _tab.efld_opt(_red,'mark=0, enable=1, editable=0',EDOKUM,'TYP')
   || _tab.efld_opt(_red,'mark=1, enable=1, '+_editable,EDOKUM,'TYP')
   ?};
   {? ETYPY.W_PORTAL<>'P'
   || _tab.win_efld(_red,OBIEGI,'OSOBAOBI',,'OSOBA',_w_str-3,,,'Osoba'@,,,,'F3_button=1');
      {? ETYPY.CZY_ZALP='T' & ETYPY.ETYPZAL
      || {? ETYPY.CZY_PRAC
         || _tab.efld_opt(_red,'mark=1, enable=1, '+_editable,OBIEGI,'OSOBAOBI')
         || _tab.efld_opt(_red,'mark=1, enable=1, editable=0',OBIEGI,'OSOBAOBI')
         ?}
      |? ETYPY.CZY_PRAC
      || _tab.efld_opt(_red,'mark=0, enable=1, '+_editable,OBIEGI,'OSOBAOBI')
      || _tab.efld_opt(_red,'mark=0, enable=1, editable=0',OBIEGI,'OSOBAOBI')
      ?}
   |? ETYPY.CZY_PRAC
   || OBIEGI.fld_fml('PRAC','BEFORE_DISPLAY',"exec('prac_fld','portal_paperless','BD')");
      OBIEGI.fld_fml('PRAC','F3',"exec('prac_fld','portal_paperless','F3')");
      OBIEGI.fld_fml('PRAC','AFTER_EDIT',"exec('prac_fld','portal_paperless','AE')");
      OBIEGI.PRAC:='';
      _tab.win_efld(_red,OBIEGI,'PRAC',,,_w_str-3,,,'Pracownik'@,,,,'F3_button=1');
      {? EDOKUM.PAPERLES
      || _tab.efld_opt(_red,'mark=1, enable=1, '+_editable,OBIEGI,'PRAC')
      || _tab.efld_opt(_red,_editable,OBIEGI,'PRAC')
      ?}
   ?};
   _tab.win_efld(_red,EDOKUM,'ALERTY',,,,,,'Wysłanie emaili'@,,,'check-box',,"'T'","'N'");
   _tab.efld_opt(_red,_editable,EDOKUM,'ALERTY');
   {? ETYPY.CZY_WAR | _all
   || _tab.win_esep(_red,'Wartości'@);
      _tab.win_efld(_red,EDOKUM,'WAL','KOD','SL',_w_str-3,,,'Waluta transakcji'@);
      _tab.efld_opt(_red,_editable,EDOKUM,'WAL');
      {? ETYPY.CZY_WAR=2 | ETYPY.CZY_WAR=3
      || _tab.win_efld(_red,EDOKUM,'NETTO',,,_w_number,2,,'Wartość netto'@);
         _tab.efld_opt(_red,_editable,EDOKUM,'NETTO')
      ?};
      {? ETYPY.CZY_WAR=1 | ETYPY.CZY_WAR=3
      || _tab.win_efld(_red,EDOKUM,'WART',,,_w_number,2,,'Wartość brutto'@);
         _tab.efld_opt(_red,_editable+{? ETYPY.CZY_ZALP='T' || ',mark=1' || '' ?},EDOKUM,'WART')
      ?};
      {? ETYPY.ZAPOT
      || _tab.win_efld(_red,EDOKUM,'TP',,,,,,'Termin'@);
         _tab.efld_opt(_red,_editable,EDOKUM,'TP')
      ?}
   ?};
   {? ETYPY.CZY_KH | _all | _dokum
   || _tab.win_esep(_red,'Kontrahent'@);
      SKID.SL_KH:={? ETYPY.DOM_SL || ETYPY.DOM_SL().SLU().NAZ || '' ?};
      _tab.win_efld(_red,SKID,'SL_KH',,,_w_str-3,,,'Źródło pochodzenia'@,,,,'F3_button=1');
      _tab.efld_opt(_red,_editable,SKID,'SL_KH');
      SKID.fld_fml('SL_KH','BEFORE_EDIT',"exec('besldob','obiegi')");
      SKID.NIP:={? EDOKUM.KHKH || EDOKUM.KHKH().NIP || '' ?};
      _tab.win_efld(_red,SKID,'NIP',,,20,,,'NIP'@);
      _tab.efld_opt(_red,_editable,SKID,'NIP');
      _tab.win_efld(_red,EDOKUM,'KH','KOD','SL',_w_str-3,,,'Kod'@);
      _tab.efld_opt(_red,'mark='+$(ETYPY.DEKR='T' | _dokum)+', '+_editable,EDOKUM,'KH')
   ?};
   {? ETYPY.CZY_DATY | _all
   || _tab.win_esep(_red,'Zakres dat i godzin'@);
      _tab.win_efld(_red,EDOKUM,'DATA_OD',,,,,,'Od dnia'@);
      _tab.efld_opt(_red,_editable,EDOKUM,'DATA_OD');
      {? ETYPY.CZY_DATY=2 | _all
      || _tab.win_efld(_red,EDOKUM,'GODZ_OD',,,13,,,'Od godziny'@);
         _tab.efld_opt(_red,_editable,EDOKUM,'GODZ_OD')
      ?};
      _tab.win_efld(_red,EDOKUM,'DATA_DO',,,,,,'Do dnia'@);
      _tab.efld_opt(_red,_editable,EDOKUM,'DATA_DO');
      {? ETYPY.CZY_DATY=2 | _all
      || _tab.win_efld(_red,EDOKUM,'GODZ_DO',,,13,,,'Do godziny'@);
         _tab.efld_opt(_red,_editable,EDOKUM,'GODZ_DO')
      ?}
   ?};
   {? EDOKUM.TYP().W_PORTAL='N'
   || _tab.win_esep(_red,'Opis'@);
      {? ETYPY.ED_TEMAT='T' | _all
      || _tab.win_efld(_red,EDOKUM,'TR',,,_w_str,,,'Temat'@);
         _tab.efld_opt(_red,'mark=1 '+_editable,EDOKUM,'TR')
      ?};
      _tab.win_efld(_red,EDOKUM,'UW_OPDL',,,_w_str,,,'Treść'@);
      _tab.efld_opt(_red,_editable,EDOKUM,'UW_OPDL');
      EDOKUM.memo_get(,'UW_OPDL')
   ?}
?};
_editInnd:=exec('blok_innd','obiegi',OBIEGI.B_PREL)=0 | _akcja='R' & EDOKUM.TYP().W_PORTAL='N' & ETYPY.IN_POR;
_edit:=_editInnd & _editable+1='1';

{? EDOKUM.TYP().ATR_G1R
|| EDOK_ATR.cntx_psh();
   EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
   EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',);
   {? _edit & ~EDOK_ATR.first()
   || refr_okn:=0;
      OBIEGI.TYP_ID:='D';
      _msg:=exec('edk_atr_dolacz','obiegi',0,0,1);
      VAR_DEL.delete('refr_okn')
   ?};
   EDOK_ATR.cntx_pop()
?};
{? EDOKUM.TYP().IDINHEAD | _wp | _edok=0
|| exec('get_atr','obe_faw',_obj);
   {? _akcja<>'D' || exec('set_atr','obe_faw',_obj) ?};
   ETYP_ATR.cntx_psh();
   {? _wp
   || ETYP_ATR.index('SECWP')
   || ETYP_ATR.index('KOLZ')
   ?};
   ETYP_ATR.prefix(EDOKUM.TYP,null);
   {? ETYP_ATR.first()
   || {? _edok & 0
      || _tab.win_etab(_red,'Informacja dodatkowa'@)
      ?};
      _first:=1;
      {? EDOKUM.TYP().ATR_ONE=0 & ~_wp
      || _tab.win_esep(_red,{? _wp || 'Dane wniosku (HR Portal)' || 'Informacja dodatkowa'@ ?});
         _tab.win_efld(_red,,'REKORD',,,_w_number,,,'Pozycja'@);
         _tab.fld_fml('REKORD','BEFORE_EDIT',"0");
         _first:=0
      ?};
      _wp_sect:='';
      {!
      |? {? ETYP_ATR.TAT & (~_wp | _seod | ETYP_ATR.VISIBLE)
         || _pole:='P'+$ETYP_ATR.KOL;
            _tabela:=_obj.getTab(_pole);
            _naz:=60+ETYP_ATR.TAT().OPIS;
            _typ:=ETYP_ATR.TAT().TYP;
            _opis:=ETYP_ATR.memo_txt(,1,'OPIS');
            {? (_opis*'deklaracja będzie obowiązywała')>0
               | (42+_opis='Deklaruję chęć oszczędzania w pracowniczym')
               | (32+_opis='Oświadczam, że mam zawarte umowy') | (38+_opis='Przesłanie deklaracji będzie oznaczało')
            || _tab.win_esep(_red,'');
               ETYP_ATR.cntx_psh();
               ETYP_ATR.memo_set(_opis,'OPIS');
               _tab.win_efld(_red,ETYP_ATR,'OPIS',,,_w_str-3,-2,1,'',1);
               ETYP_ATR.cntx_pop()
            |? EDOKUM.TYP().NAZWA=exec('nazwa_rachunek','obiegi')
            || {? (_opis*'Dane aktualne')>0
               || _tab.win_esep(_red,'Dane aktualne'@)
               |? (_opis*'Dane dotychczasowe')>0
               || _tab.win_esep(_red,'Dane dotychczasowe'@)
               |? 5+_opis='Konto'
               || _tab.win_esep(_red,_opis)
               ?}
            ?};
            {? _wp

            || {? _wp_sect<>ETYP_ATR.SECTION & ETYP_ATR.SECTION<>''
               || _tab.win_esep(_red,ETYP_ATR.SECTION);
                  _wp_sect:=ETYP_ATR.SECTION;
                  _first:=0
               ?}
            ?};
            {? _first
            || _first:=0;
               {? _typ<>'O' & EDOKUM.TYP().NAZWA<>exec('nazwa_rachunek','obiegi')
               || _tab.win_esep(_red,'Informacja dodatkowa'@)
               ?}
            ?};
            {? _typ='O'
:: Nagłowek
            || _opis:=ETYP_ATR.memo_txt(,1,'OPIS');
               {? +_opis>60 || _opis:=60+_opis ?};
               {? _opis<>''
               || _tab.win_esep(_red,_opis)
               ?}
            ?};
            {? var_press('_tabela')>0
            ||
               {? _typ='L'
:: Liczba
               || _tab.win_efld(_red,_tabela,_pole,,,_w_number,ETYP_ATR.TAT().PREC,,_naz)
               |? _typ='B'
:: Tak/nie
               || _tab.win_efld(_red,_tabela,_pole,,,,,,_naz,,,'check-box',,"'T'","'N'")
               |? _typ='D'
:: Data
               || _tab.win_efld(_red,_tabela,_pole,,,10,,,_naz)
               |? _typ='C'
:: czas
               || _tab.win_efld(_red,_tabela,_pole,,,10,,,_naz)
               |? _typ='S' & ETYP_ATR.TAT().SLU
:: Słownik
               || _tab.win_efld(_red,_tabela,_pole,,,_w_str-3,,,_naz,,,,'F3_button=1');
                  _tabela.fld_fml(_pole,'F3',"exec('akc_f3','obe_faw')");
                  _tabela.fld_fml(_pole,'AFTER_EDIT',"exec('akc_ae','obe_faw')")
               |? _typ='U' & ETYP_ATR.TAT().UD_SCH
:: Schemat struktury
               || _tab.win_efld(_red,_tabela,_pole,,,_w_str-3,,,_naz,,,,'F3_button=1');
                  _tabela.fld_fml(_pole,'F3',"exec('akc_f3','obe_faw')");
                  _tabela.fld_fml(_pole,'AFTER_EDIT',"exec('akc_ae','obe_faw')")
               |? _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
:: TODO - poprawić zależności pomiędzy polami
               || {? ETYP_ATR.SLO_CHG
                  || _tab.win_efld(_red,_tabela,_pole,,,_w_str-3,,,_naz,,,,'F3_button=1');
                     _tabela.fld_fml(_pole,'F3',"exec('akc_f3','obe_faw')")
                  || _tab.win_efld(_red,_tabela,_pole,,,_w_str-3,,,_naz,,,,'F3_button=1');
                     _tabela.fld_fml(_pole,'F3',"exec('akc_f3','obe_faw')");
                     _tabela.fld_fml(_pole,'AFTER_EDIT',"exec('akc_ae','obe_faw')")
                  ?}
               |? _typ='W'
               || _tab.win_efld(_red,_tabela,_pole,,,_w_str,-3,,_naz)
               |? _seod & _typ='T' & ETYP_ATR.TAT().PORSLO
               || _tab.win_efld(_red,_tabela,_pole,,,_w_str-3,,,_naz,,,,'F3_button=1');
                  _tabela.fld_fml(_pole,'F3',"exec('akc_f3','obe_faw')");
                  _tabela.fld_fml(_pole,'AFTER_EDIT',"exec('akc_ae','obe_faw')")
               || _tab.win_efld(_red,_tabela,_pole,,,_w_str,,,_naz)
               ?};
               {? _typ<>'O'
               || _tab.efld_opt(_red,'mark='+$(ETYP_ATR.WYMAG='T')+{? _edit || '' || ',editable=0'?},_tabela,_pole);
                  {? ETYP_ATR.EDIT='T' | ~_edit
                  || _tabela.fld_fml(_pole,'BEFORE_EDIT',"0")
                  ?}
               ?};
:: TODO - poprawić formuły po redakcji pól
               {? ETYP_ATR.AE<>''
               || _tabela.fld_fml(_pole,'AFTER_EDIT',$ETYP_ATR.AE)
               ?}
            ?};
            &_tabela
         ?};
         ETYP_ATR.next()
      !}
   ?};
   ETYP_ATR.cntx_pop()
?};
{? _edok & (ETYPY.CZY_ZALP='T' | _all)
|| _tab.win_etab(_red,'Dane zaliczki'@);
   _tab.win_esep(_red,'Dane zaliczki'@);
   _tab.win_efld(_red,EDOKUM,'KASPRZEL',,,10,,,'Realizacja poprzez'@,,,'radio-buttons',,'Kasę'@,"'K'",'Przelew'@,"'P'");
   _tab.win_efld(_red,EDOKUM,'N',,,36,,,'Rachunek bankowy pracownika'@,,,,'F3_button=1');
   _tab.efld_opt(_red,_editable+{? EDOKUM.KASPRZEL='P' || ',mark=1' || ',mark=0' ?},EDOKUM,'N');
   _tab.win_efld(_red,EDOKUM,'PLACE',,,,,,'Rozliczane w płacach'@,,,'check-box',,"'T'","'N'");
   _tab.win_efld(_red,EDOKUM,'POTRODDN',,,,,,'Od dnia'@);
   _tab.efld_opt(_red,_editable+{? EDOKUM.PLACE='T' || ',mark=1' || ',mark=0' ?},EDOKUM,'POTRODDN')
?};
{? _edok
|| {? ETYPY.DEKR='T' | _all | EDOKUM.ODD=null
   || {? _tabs || _tab.win_etab(_red,'Dekretacja'@) ?};
      _tab.win_esep(_red,'Dekretacja wniosku'@);
      _tab.win_efld(_red,EDOKUM,'ODD','OD','ODDZIALY',,,,'Jednostka księgowa'@);
      {? ETYPY.DEKR='T' | _all
      || _tab.win_efld(_red,EDOKUM,'DATDOK',,,10,,1,'Data dokumentu'@);
         _tab.win_efld(_red,EDOKUM,'REJ','KOD','KOD',10,,1,'Rejestr'@);
         _tab.win_efld(_red,EDOKUM,'NREJ',,,10,,1,'Numer w rejestrze'@)
      ?}
   ?}
?};
{? _edok
|| {? ~_istyp | (~EDOKUM.TYP().IDINHEAD & ETYPY.W_PORTAL='N')
   || _ind_fml:=$('EDOKUM.cntx_psh(); exec(\'inne_dane\',\'obiegi\',,2,1,'+{? (_akcja='D' | ~_editInnd) || '1' || '0' ?}+'); EDOKUM.cntx_pop(); \'\'');
      _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Informacje'@],_ind_fml)
   ?};
   {? _akcja<>'D'
   || {? EDOKUM.TYP().OBS_PROJ
      || _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Projekty'@],"
            EDOKUM.cntx_psh(); exec('proj_dob','obiegi_proj',,2); EDOKUM.cntx_pop(); ''
         ")
      ?};
      {? EDOKUM.TYP().ED_PODZ='T' & PAR_SKID.get(80)='T'
      || _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Podziały'@],"
            EDOKUM.cntx_psh(); exec('podz_dob','obiegi',,2); EDOKUM.cntx_pop(); ''
         ")
      ?};

      {? exec('dok_pow_log','obiegi_log')
      || _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['W logi&styce'@],"
            EDOKUM.cntx_psh(); exec('dob_log','obiegi_log'); EDOKUM.cntx_pop(); ''
         ")
      ?};
      _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Załą&czniki'@],"
         EDOKUM.cntx_psh(); exec('zalaczniki','obiegi'); EDOKUM.cntx_pop(); ''
      ")
   ?}
?};
{? EDOKUM.TYP().IN_POR
|| _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['Ś&cieżka akceptacji'@],"
      exec('btn_akc_path','portal_interm'); ''
   ")
?};
{? _edok & EDOKUM.EDOKUM & 'pdf;bmp;png;tiff;tif;jpeg;jpg'*(-EDOKUM.bl_info('EDOKUM','EXTENSION'))>0
|| _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1'['E-dokument'@],"
         exec('preview','obe_faw')
      ")
?};
{? _edok & EDOKUM.TYP().IDINHEAD
|| {? ~EDOKUM.TYP().ATR_ONE
   || {? _akcja<>'D' & _edit
      || _obj.btnD:=
         _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['<<'@],"
            exec('edok_atk_step','obe_faw',-1,1)
         ");
         _obj.btnU:=
         _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['>>'@],"
            exec('edok_atk_step','obe_faw',1,1)
         ");
         {? _akcja<>'P' & _edit
         || _obj.btnDEL:=
            _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['X'@],"
               exec('edok_atk_del','obe_faw')
            ")
         ?}
      || _obj.btnD:=
         _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['<<'@],"
            exec('edok_atk_step','obe_faw',-1,0)
         ");
         _obj.btnU:=
         _tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['>>'@],"
            exec('edok_atk_step','obe_faw',1,0)
         ")
      ?};
      exec('edok_atk_btn_upd','obe_faw',_red,_akcja<>'D' & _edit)
   ?};
   {? _akcja<>'D' & _akcja<>'R' & _editInnd
   ||  _obj.btnEDIT:=_tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Edycja'@],"
            EDOKUM.cntx_psh(); exec('edit_red','obiegi2'); EDOKUM.cntx_pop(); ''
         ")
   ?}
?};
{? _edok
|| {? _akcja='R' & (var_pres('_d')<=0 | _d>0)
   || _id:=_tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['Wyślij'@],
                         "BPMN.END:=1;'key:F2'");
      _tab.btn_eopt(_red,_id,'tooltip=Zapisanie danych i wysłanie do kolejnego etapu')
   |? _akcja='P'
   || _id:=_tab.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end,display=1'['Przekaż'@],
                         "BPMN.END:=1;'key:Esc'");
      _tab.btn_eopt(_red,_id,'tooltip=Przekazanie dokumentu do dalszego obiegu');
      _id:=_tab.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end,display=1'['Odrzuć'@],
                         "BPMN.END:=2;'key:Esc'");
      _tab.btn_eopt(_red,_id,'tooltip=Odrzucenie dokumentu')
   |? _akcja='A'
   || _id:=_tab.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end,display=1'['Akceptuj'@],
                         "BPMN.END:=1;'key:F2'");
      _tab.btn_eopt(_red,_id,'tooltip=Akceptacja dokumentu');
      _id:=_tab.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end,display=1'['Odrzuć'@],
                         "BPMN.END:=2;'key:Esc'");
      _tab.btn_eopt(_red,_id,'tooltip=Odrzucenie dokumentu')
   ?}
?};
_id:=_tab.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end'['Zapisz'@],"'key:F2'");
_tab.btn_eopt(_red,_id,'tooltip='+exec('help_red_ok','#window','Z'));
_anuluj:=_tab.win_ebtn(_red,'text=%1, btn_label_align=center, panel=bottom, align=end, display=1'
                       [{? _akcja='D' || 'Zamkni&j'@ || 'Anulu&j'@ ?}],'key:Esc');
_tab.btn_eopt(_red,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
_red


\obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Tworzy obiekt do obsługi wniosku z informacjami dodatkowymi
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new(
   'tab','ile','max','lTab','lFld','str','buf_size','btnU','btnD','ADD',
   'add','end','getTab','get','set','blank','buf_add','buf_length','buf_max','btnDEL','btnEDIT'
);
_tab.max:=256;
_tab.buf_max:=11264;
_tab.tab:=obj_new(10);
_tab.lTab:=0;
_tab.lFld:=0;
_tab.btnU:=_tab.btnD:=_tab.btnDEL:=_tab.btnEDIT:=~~;
_tab.buf_length:="
   _type:=_a;
   _size:=0;
   {? _type='INTEGER' | _type='DATE' | _type='TIME'
   || _size:=4
   |? _type='REAL'
   || _size:=12
   |? 6+_type='STRING'
   || _size:=(#(7-_type-1)*3)+1
   ?};
   _size+=4;
   _size
";
_tab.add:="
   _fld:=_a;
   _type:=_b;
   _name:=60+_c;
   .lFld+=1;
   {? .lFld=1
   || .buf_size:=8
   ?};
   _size:=.buf_length(_type);
   {? .lFld>.max | .buf_size+_size>.buf_max
   || .end();
      .lFld:=1
   ?};
   {? .lFld=1
   || .str:='tab_tmp(1,'+($\"'REKORD','INTEGER','Rekord'\")+',\n';
      .buf_size:=8
   ?};
   .buf_size+=_size;
   .str+='\'\''+_fld+'\'\''+',\'\''+_type+'\'\',\'\''+_name+'\'\',\n'
";
_tab.end:="
   {? var_pres('str',.)<=0
   || .str:='tab_tmp(1,'+($\"'REKORD','INTEGER','Rekord'\")+',\n'
   ?};
   .str:=.str-2;
   .str+=')';
   .lTab+=1;
   .tab[.lTab]:=($.str)()
";
_tab.getTab:="
   _fld:=_a;
   _tab:=~~;
   {! _i:=1.. .lTab
   |? var_pres('_tab')<=0
   |! {? var_pres(_fld,.tab[_i])>0
      || _tab:=.tab[_i]
      ?}
   !};
   _tab
";
_tab.set:="
   _fld:=_a;
   _value:=_b;
   _memo:={? var_pres('_c')>0 || _c ?};
   _tab:=.getTab(_fld);
   {? var_pres('_tab')>0
   || {? _memo=0
      || ($('_a.'+_fld+':=_b'))(_tab,_value)
      || _tab.memo_set(_value,_fld)
      ?}
   ?}
";
_tab.get:="
   _fld:=_a;
   _tab:=.getTab(_fld);
   {? var_pres('_tab')>0
   || {? var_pres(_fld,_tab)=36
      || _tab.memo_txt(,0,_fld)
      || ($('_a.'+_fld))(_tab)
      ?}
   || ~~
   ?}
";
_tab.blank:="
   {! _i:=1.. .lTab
   |! {? var_pres('_a')>0
      || .tab[_i].blank(_a)
      || .tab[_i].blank()
      ?}
   !}
";

ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null);
{? ETYP_ATR.first()
|| _first:=1;
   {!
   |? _pole:='P'+$ETYP_ATR.KOL;
      _typ:=ETYP_ATR.TAT().TYP;
      _naz:=gsub(60+ETYP_ATR.TAT().OPIS,'\'','\\''');
      {? _typ='O'
      || 1
      |? _typ='L'
:: Liczba
      || _tab.add(_pole,'REAL',_naz)
      |? _typ='B'
:: Tak/nie
      || _tab.add(_pole,'STRING[1]',_naz)
      |? _typ='D'
:: Data
      || _tab.add(_pole,'DATE',_naz)
      |? _typ='C'
:: czas
      || _tab.add(_pole,'TIME',_naz)
      |? _typ='S' & ETYP_ATR.TAT().SLU
:: Słownik
      || _typ:=ETYP_ATR.SLOPOLE;
         _max:=0;
         SLO.cntx_psh();
         SLO.index('SL');
         {? _typ='K' || SLO.index('SL') || SLO.index('SL_TR') ?};
         SLO.prefix(ETYP_ATR.TAT().SLU);
         {? SLO.first()
         || {!
            |? _value:={? _typ='K' || SLO.KOD || SLO.TR ?};
               _dl:=+_value;
               {? _max<_dl || _max:=_dl ?};
               SLO.next()
            !};
            {? _max>255 || _max:=255 ?};
            _tab.add(_pole,'STRING['+$_max+']',_naz)
         ?};
         SLO.cntx_pop()
      |? _typ='U' & ETYP_ATR.TAT().UD_SCH
:: Schemat struktury
      || _tab.add(_pole,{? ETYP_ATR.SLOPOLE='K' || 'STRING[16]' || 'STRING[60]' ?},_naz)
      |? _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
      || {? ETYP_ATR.SLO_CHG
         || _tab.add(_pole,'STRING[255]',_naz)
         || _tab1:=($ETYP_ATR.TSLO)();
            {? type_of(_tab1)>100 & var_press('KOD',_tab1)>0 & var_press('TR',_tab1)>0 & var_press('REF_SQL',_tab1)>0 &
               _tab1.first()
            || _typ:=ETYP_ATR.SLOPOLE;
               _max:=0;
               {!
               |? _fld:={? _typ='K' || _tab1.KOD+' - '+_tab1.TR || _tab1.TR ?};
                  _dl:=+_fld;
                  {? _max<_dl || _max:=_dl ?};
                  _tab1.next()
               !};
               {? _max=0 | _max>255 || _max:=255 ?};
               _tab.add(_pole,'STRING['+$_max+']',_naz)
            || _tab.add(_pole,'STRING[255]',_naz)
            ?};
            &_tab1
         ?}
      |? _typ='W'
      || _tab.add(_pole,'SYS_MEMO',_naz)
      || _tab.add(_pole,'STRING[255]',_naz)
      ?};
      ETYP_ATR.next()
   !}
?};
ETYP_ATR.cntx_pop();
_tab.end();

::_tab.win_sel(_tab.mk_sel(,,1));
::_tab.select();
_tab


\spr_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Sprawdzenie wartości informacji dodatkowych
::   WE: _a - obiekt do obsługi bufora informacji dodatkowych
::   WY: akronim pola z niepoprawną daną lub '' gdy dane poprawne
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('editWni')<=0 | var_pres('editWni')>0 & editWni=0 || return('') ?};
_ret:='';
_obj:=_a;
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null);
{? ETYP_ATR.first()
|| {!
   |? _err:=0;
      {? ETYP_ATR.TAT().TYP<>'O' & ETYP_ATR.TAT
      || _pole:='P'+$ETYP_ATR.KOL;
         _typ:=TAT.TYP;
         _value:=_obj.get(_pole);
         {? ETYP_ATR.WYMAG='T' & ETYP_ATR.EDIT='N'
         || {? _typ='L'
:: Liczba
            || _err:=_value=0 & ETYP_ATR.CZY_ZERO='P'
            |? _typ='B'
:: Tak/nie
            || _err:=_value=''
            |? _typ='D'
:: Data
            || _err:=_value=date(0,0,0)
            |? _typ='C'
:: Czas
            || _err:=0
            |? _typ='S' & ETYP_ATR.TAT().SLU
:: Słownik
            || _err:=_value=''
            |? _typ='U' & ETYP_ATR.TAT().UD_SCH
:: Schemat struktury
            || _err:=_value=''
            |? _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
            || _err:=_value=''
            || _err:=_value=''
            ?};
            {? _err
            || _ret:=_pole;
               {? _err=1 || FUN.info(__CHK.empty_msg(60+ETYP_ATR.TAT().OPIS)) ?}
            ?}
         ?};
         {? _err=0
         || {? _typ='S' & ETYP_ATR.TAT().SLU
:: Słownik
            || {? _value<>''
               || SLO.cntx_psh();
                  SLO.index('SL');
                  {? ETYP_ATR.SLOPOLE='K' || SLO.index('SL') || SLO.index('SL_TR') ?};
                  SLO.prefix(ETYP_ATR.TAT().SLU,_value,);
                  {? ~SLO.first()
                  || _err:=2;
                     FUN.info('Nie znaleziono wartości pola "%1" w słowniku użytkownika.'@[ETYP_ATR.TAT().OPIS])
                  ?};
                  SLO.cntx_pop()
               ?}
            |? _typ='U' & ETYP_ATR.TAT().UD_SCH
:: Schemat struktury
            || {? _value<>'' & exec('szukaj_ud_skl','schemat',TAT.UD_SCH().UD_TYP,_value)=null
               || _err:=2;
                  FUN.info('Nie znaleziono wartości pola "%1" w słowniku.'@[ETYP_ATR.TAT().OPIS])
               ?}
            |? _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
            || {? _value<>''
               || _tab:=($ETYP_ATR.TSLO)();
                  {? type_of(_tab)>100 & var_press('KOD',_tab)>0 & var_press('TR',_tab)>0 & var_press('REF_SQL',_tab)>0
                  || _tab.blank(1);
                     {? ETYP_ATR.SLOPOLE='K' || _tab.KOD:=_value || _tab.TR:=_value ?};
                     {? ~_tab.find_rec()
                     || _err:=2;
                        FUN.info('Nie znaleziono wartości pola "%1" w słowniku.'@[ETYP_ATR.TAT().OPIS])
                     ?}
                  ?};
                  &_tab
               ?}
            |? _typ='T' & ETYP_ATR.TAT().PORSLO
            || {? _value<>''
               || _firma:={? ETYP_ATR.TAT().PORSLO().GLOBAL<>'T' || REF.FIRMA || null ?};
                  PORSLOIT.cntx_psh();
                  PORSLOIT.index('SLOVAL');
                  PORSLOIT.prefix();
                  {? ~PORSLOIT.find_key(TAT.PORSLO,_firma,_value,)
                  || _err:=2;
                     FUN.info('Nie znaleziono wartości pola "%1" w słowniku.'@[ETYP_ATR.TAT().OPIS])
                  ?};
                  PORSLOIT.cntx_pop()
               ?}
            ?};
            {? _err
            || _ret:=_pole
            ?}
         ?}
      ?};
      _err=0 & ETYP_ATR.next()
   !}
?};
ETYP_ATR.cntx_pop();
_ret


\get_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Zapisuje atrybuty z tabeli do bufora
::   WE: _a - obiekt do obsługi bufora informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_rec:=_obj.get('REKORD');
_obj.blank(1);
_obj.set('REKORD',_rec);
EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_rec);
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null);
{? ETYP_ATR.first()
|| {!
   |? _err:=0;
      {? ETYP_ATR.TAT & ETYP_ATR.TAT().TYP<>'O' & EDOK_ATR.find_key(ETYP_ATR.KOL)
      || _pole:='P'+$ETYP_ATR.KOL;
         _typ:=TAT.TYP;
         _value:=~~;
         {? _typ='L'
:: Liczba
         || _value:=#gsub(EDOK_ATR.WAR,',','.')
         |? _typ='B'
:: Tak/nie
         || _value:=1+EDOK_ATR.WAR
         |? _typ='D'
:: Data
         || _value:=exec('str2date','#convert',EDOK_ATR.WAR)
         |? _typ='C'
:: Czas
         || _value:=exec('str2time','#convert',EDOK_ATR.WAR)
         |? _typ='S' & ETYP_ATR.TAT().SLU
:: Słownik
         || _value:=EDOK_ATR.WAR
         |? _typ='U' & ETYP_ATR.TAT().UD_SCH
:: Schemat struktury
         || _value:=EDOK_ATR.WAR
         |? _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
         || _value:=EDOK_ATR.WAR
         |? _typ='W'
         || _value:=EDOK_ATR.memo_txt(,1,'WAR_W')
         || _value:=EDOK_ATR.WAR
         ?};
         {? var_pres('_value')>0
         || _obj.set(_pole,_value,_typ='W')
         ?}
      ?};
      ETYP_ATR.next()
   !}
?};
ETYP_ATR.cntx_pop();
EDOK_ATR.cntx_pop()


\set_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Zapisuje atrybuty z bufora do tabeli
::   WE: _a - obiekt do obsługi bufora informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_rekord:=_obj.get('REKORD');
EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_rekord);
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null);
{? ETYP_ATR.first()
|| {!
   |? _err:=0;
      _add:=0;
      {? ETYP_ATR.TAT & ETYP_ATR.TAT().TYP<>'O'
      || {? ~EDOK_ATR.find_key(ETYP_ATR.KOL)
         || EDOK_ATR.blank(1);
            EDOK_ATR.EDOKUM:=EDOKUM.ref();
            EDOK_ATR.REKORD:=_rekord;
            EDOK_ATR.KOL:=ETYP_ATR.KOL;
            EDOK_ATR.TAT:=ETYP_ATR.TAT;
            _add:=EDOK_ATR.add()
         ?};
         _pole:='P'+$ETYP_ATR.KOL;
         _typ:=ETYP_ATR.TAT().TYP;
         {? var_pres('_value')>0 || &_value ?};
         {? var_pres('_default')>0 || &_default ?};
         _value:={? _add
                 || _default:=exec('etp_get_default','obiegi',ETYP_ATR.ref());
                    {? type_of(_default)=0 || _obj.get(_pole) || _default ?}
                 || _obj.get(_pole)
                 ?};
         {? _typ='L'
:: Liczba
         || EDOK_ATR.WAR:=$_value
         |? _typ='B'
:: Tak/nie
         || EDOK_ATR.WAR:={? 1+_value='T' || 'Tak' || 'Nie' ?}
         |? _typ='D'
:: Data
         || EDOK_ATR.WAR:=$_value
         |? _typ='C'
:: Czas
         || EDOK_ATR.WAR:=_value$3
         |? _typ='W'
:: Wielolinijkowe
         || EDOK_ATR.memo_set(_value,'WAR_W');
            EDOK_ATR.memo_put(,'WAR_W')
         |? _typ='S' & ETYP_ATR.TAT().SLU
:: Słownik
         || EDOK_ATR.WAR:=_value;
            {? _value=''
            || EDOK_ATR.SLO:=null
            || SLO.cntx_psh();
               SLO.index('SL');
               {? ETYP_ATR.SLOPOLE='K' || SLO.index('SL') || SLO.index('SL_TR') ?};
               SLO.prefix(ETYP_ATR.TAT().SLU,_value,);
               {? SLO.first()
               || EDOK_ATR.SLO:=SLO.ref()
               ?};
               SLO.cntx_pop()
            ?}
         |? _typ='U' & ETYP_ATR.TAT().UD_SCH
:: Schemat struktury
         || EDOK_ATR.WAR:=_value;
            EDOK_ATR.UD_SKL:=exec('szukaj_ud_skl','schemat',TAT.UD_SCH().UD_TYP,_value)
         |? _typ='X' & ETYP_ATR.TSLO<>''
:: Dowolny słownik
         || {? type_of(_value)>100
            ||  EDOK_ATR.WAR:=_value[1];
                EDOK_ATR.REF_SQL:=_value[2]
            ||  EDOK_ATR.WAR:=_value;
               _tab:=($ETYP_ATR.TSLO)();
               {? type_of(_tab)>100 & var_press('KOD',_tab)>0 & var_press('TR',_tab)>0 &
                  var_press('REF_SQL',_tab)>0 & _tab.find_tab(,{? ETYP_ATR.SLOPOLE='K' || 'KOD' || 'TR' ?},,'=',_value)
               || EDOK_ATR.REF_SQL:=_tab.REF_SQL
               ?};
               &_tab
            ?}
         || EDOK_ATR.WAR:={? var_pres('_value')>0 || _value || '' ?}
         ?};
         EDOK_ATR.put()
      ?};
      ETYP_ATR.next()
   !}
?};
ETYP_ATR.cntx_pop();
EDOK_ATR.cntx_pop()


\akc_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Obsługa klawisza F3 dla innych danych
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
_fld:=cur_afld();
_kol:=#(1-_fld);
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null,_kol);
{? ETYP_ATR.first()
|| _typ:=ETYP_ATR.TAT().TYP;
   {? _typ='X' & ETYP_ATR.TSLO<>''
   || _tab:=($ETYP_ATR.TSLO)();
      {? var_pres('_tab')>0
      || {? var_pres('KOD',_tab)>0 & var_pres('TR',_tab)>0
         || _red:=_tab.mk_sel(,'P',,,,,,,'U');
            {? ETYP_ATR.SLOPOLE='K'
            || _tab.win_fld(_red,,'KOD',,,,,,'Kod')
            ?};
            _tab.win_fld(_red,,'TR',,,,,,'Treść');
            _tab.win_act(_red,,'Formuła','Wybierz',,,"sel_exit()",,1)
         || _tab
         ?};
         _tab.win_sel(_red);
         _tab.hdr_sel();
         _tab.hdr_sel(ETYP_ATR.TAT().OPIS);
         {? _tab.select()
         || {? ETYP_ATR.SLOPOLE='K' & var_pres('KOD',_tab)>0
            || _ret:=_tab.KOD
            |? var_pres('TR',_tab)>0
            || _ret:=_tab.TR
            ?}
         ?}
      ?}
   |? _typ='U' & ETYP_ATR.TAT().UD_SCH
   ||
      _tablica:=exec('ud_def_tablica_f3','schemat',TAT.UD_SCH().UD_TYP,TAT.UD_SCH);
      _ret:={? ETYP_ATR.SLOPOLE='K' || _tablica.SYMBOL || _tablica.OPIS ?}
   |? _typ='S' & ETYP_ATR.TAT().SLU
   || _pole:=ETYP_ATR.SLOPOLE;
      SLO.cntx_psh();
      {? _pole='K' || SLO.index('SL') || SLO.index('SL_TR') ?}; SLO.prefix(TAT.SLU);
      SLO.find_key(fld()) | SLO.first();
      SLO.win_sel('ONE_SEL');
      {? SLO.select(,1)
      || _ret:={? _pole='K' || SLO.KOD || SLO.TR ?}
      ?};
      SLO.cntx_pop()
   |? _typ='T' & ETYP_ATR.TAT().PORSLO
   || PORSLOIT.cntx_psh();
      PORSLOIT.index('SLOVAL');
      PORSLOIT.prefix(ETYP_ATR.TAT().PORSLO);
      PORSLOIT.win_sel('SLO');
      PORSLOIT.hdr_sel();
      PORSLOIT.hdr_sel(' - '+TAT.PORSLO().OPIS);
      _ha:={? PORSLO.SYSTEM='T' || ':D' || ':' ?};
      {? PORSLOIT.select(,,,_ha)
      || _ret:=PORSLOIT.VALUE
      ?};
      PORSLOIT.cntx_pop()
   ?}
?};
ETYP_ATR.cntx_pop();
_ret


\akc_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Po redakcji pól słownikowych
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_fld:=cur_afld();
_kol:=#(1-_fld);
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ'); ETYP_ATR.prefix(EDOKUM.TYP,null,_kol);
{? ETYP_ATR.first() & fld()<>''
|| _typ:=ETYP_ATR.TAT().TYP;
   {? _typ='X' & ETYP_ATR.TSLO<>''
   || _tab:=($ETYP_ATR.TSLO)();
      _tab.blank(1);
      _pole:=ETYP_ATR.SLOPOLE;
      {? _pole='K' & var_pres('_tab')>0 & var_pres('KOD',_tab)>0
      || _tab.KOD:=fld()
      |? var_pres('_tab')>0 & var_pres('TR',_tab)>0
      || _tab.TR:=fld()
      ?};
      {? _tab.find_rec()
      || fld({? _pole='K' || _tab.KOD || _tab.TR ?})
      ?}
   |? _typ='S' & ETYP_ATR.TAT().SLU
   || _pole:=ETYP_ATR.SLOPOLE;
      SLO.cntx_psh();
      {? _pole='K' || SLO.index('SL') || SLO.index('SL_TR') ?};
      SLO.prefix(TAT.SLU,fld());
      {? SLO.first()
      || fld({? _pole='K' || SLO.KOD || SLO.TR ?})
      ?};
      SLO.cntx_pop()
   |? _typ='U' & ETYP_ATR.TAT().UD_SCH
   || UD_SKL.cntx_psh();
      UD_SKL.index('SYMBOL'); UD_SKL.prefix(TAT.UD_SCH().UD_TYP);
      {? UD_SKL.find_key(fld)
      || fld(UD_SKL.SYMBOL)
      ?};
      UD_SKL.cntx_pop()
   ?}
?};
ETYP_ATR.cntx_pop();
_ret


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Dodanie wniosku w obiegu
::   WE: _a - parametry wejściowe czynności
::       _b - 0/1 - dołączanie z DOKUM
::----------------------------------------------------------------------------------------------------------------------
_we:=_a;
_dokum:={? var_pres('_b')>0 || _b || 0 ?};
_ref:=null;
utw_dob:=1;
typobi:=2; exec('typobi','obiegi');
OBIEGI.EDOKOS:=null();
{? var_pres('TYP_DOK',_we)>0
|| _objT:=exec('obj_typ_dok','obiegi2');
   _objT.set(_we.TYP_DOK);
   _typ:=OBIEG.ETYPY:=exec('FindInSet','#table','ETYPY','UNIK_WP',_objT.NAZWA,OBIEGI.TYPOBIEG,,1,_objT.W_PORTAL,null);
   {? ~_typ
   || FUN.info('Nie znaleziono typu wniosku w obiegu:\n %1.'@[_objT.NAZWA]);
      return(null)
   |? 'TX'*OBIEG.ETYPY().W_PORTAL | ETYPY.W_PORTAL='P' & ETYPY.IN_ERP=0
   || FUN.info('Typ wniosku w obiegu\n "%1"\ndostępny tylko z portalu HR.'@[_objT.NAZWA]);
      return(null)
::   |? OBIEG.ETYPY().GRP_DOC<>null & ~exec('has_grp_perm','obiegi2',OBIEG.ETYPY().GRP_DOC().N,{? typobi=1 || 'OBG_FZO_DARK' |? typobi=2 || 'OBE_FAW_DARP' |? typobi=3 || 'OBE_FDL_DBRP' ?}); 0
::   || FUN.info('Brak uprawnienia do grupy '+OBIEG.ETYPY().GRP_DOC().N+' wniosków i dokumentów w obiegu\ndla typu wniosku "%1".'@[_objT.NAZWA]);
::      return(null)
   ?}
|| _typ:=null
?};
EDOKUM.cntx_psh();
EDOKUM.use('skid_v'+($(SSTALE.AR().POCZ_ROK~1)+2));
EDOKUM.prefix();
EDOKUM.blank();
EDOKUM.USERS:=OPERATOR.USER;
EDOKUM.TYP:=_typ;
{? EDOKUM.TYP().ED_TEMAT='N'
|| EDOKUM.TR:=EDOKUM.TYP().NAZWA
?};
{? EDOKUM.TYP
|| OSOBA.cntx_psh();
   OPERATOR.USER().OSOBA();
   {? EDOKUM.TYP().CZY_PRAC & ~exec('prac_firm_af_www1','obiegi2',OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO,OSOBA.IMEX)
   || EDOKUM.DOSTAWCA:=null()
   || EDOKUM.DOSTAWCA:=OSOBA.ref()
   ?};
   OSOBA.cntx_pop()
?};
EDOKUM.FIRMA:=REF.FIRMA;
EDOKUM.ROK_F:=SSTALE.AR;
EDOKUM.UNIK_ID:=tm_stamp();
EDOKUM.ODD:=OPERATOR.DEPT;
{? EDOKUM.ODD=null || EDOKUM.ODD:=exec('edokum_odd','obiegi2') ?};
exec('ae_typ_zal','obiegi');
SKID.SL_KH:=SKID.NIP:='';
EDOKUM.memo_set('','UW_OPDL');
EDOKUM.DOP:=date();
{? _dokum & DOKUM.get()
|| kh_ask:=0;
   OBIEGI.OBIEGKON:=1;
   EDOKUM.DOKUM:=$DOKUM.ref;
   EDOKUM.EDOKUM:=DOKUM.DOKUMI;
   EDOKUM.DO:={? DOKUM.DATA_ODB<>date(0,0,0)
              || DOKUM.DATA_ODB
              |? DOKUM.DATAKONT<>date(0,0,0)
              || DOKUM.DATAKONT
              || date()
              ?};
   KH.cntx_psh();
   DOKUM.KH();
   EDOKUM.KHKH:=KH.ref();
   EDOKUM.KH_FULL:=KH.NAZ_P;
   EDOKUM.MIASTO:=KH.MIASTO;
   EDOKUM.UL:=KH.UL;
   EDOKUM.DOM:=KH.DOM;
   EDOKUM.LOKAL:=KH.LOKAL;
   EDOKUM.POCZ:=KH.POCZ;
   EDOKUM.KPOCZ:=KH.KPOCZ;
   SKID.NIP:=KH.NIP;
   {? EDOKUM.TYP().DOM_SL<>null
   || RS.cntx_psh(); RS.index('RS'); RS.prefix();
      {? RS.find_key(EDOKUM.TYP().DOM_SL().SLU().WZ,) & RS.TAB='KH'
      || _kod:=($('KH.'+RS.KOD))();
         SLO.cntx_psh();
         SLO.index('SL'); SLO.prefix(SLU.ref());
         {? ~SLO.find_key(_kod) | _kod<>SLO.KOD
         || SLO.blank();
            SLO.SLU:=SLU.ref();
            SLO.KOD:=_kod;
            SLO.TR:=KH.NAZ;
            SLO.add()
         ?};
         EDOKUM.KH:=SLO.ref();
         SLO.cntx_pop()
      ?};
      RS.cntx_pop()
   ?};
   KH.cntx_pop()
?};
EDOKUM.B_PREL:=OBIEGI.B_PREL;
EDOKUM.B_PRELS:=OBIEGI.B_PRELS;
_in_por:=EDOKUM.TYP().IN_POR;
{? _in_por
|| EDOKUM.STATUS:='R';
   {? EDOKUM.DOSTAWCA
   || P.index('OSOZATR');
      P.prefix();
      {? P.find_key(REF.FIRMA,EDOKUM.DOSTAWCA,'T')
      || EDOKUM.OSOBAWWW:=$P.ref()
      ?}
   ?}
?};
EDOKUM.add();
{? EDOKUM.TYP & (EDOKUM.TYP().AUT_ID='T' | EDOKUM.PAPERLES) & EDOKUM.ID=''
|| exec('ustal_numer','obiegi',typobi,SSTALE.AR,1);
   EDOKUM.ID:={? var_pres('id_edok')>0 || id_edok || '' ?};
   EDOKUM.TYP:=_typ;
   EDOKUM.put()
?};
VAR_DEL.delete('objID');
objID:=_obj:=exec('obj','obe_faw');
_obj.ADD:=0;
_obj.set('REKORD',1);
_b_prel:=OBIEGI.B_PREL; _b_prels:=OBIEGI.B_PRELS;
exec('ustaw_edokpar','obiegi',_we,EDOKUM.ref,OBIEGI.B_PREL:=_b_prel,OBIEGI.B_PRELS:=_b_prels);
_red:=exec('create_red','obe_faw',_obj,,'R');
_tab:=_obj.getTab('REKORD');
_tab.win_edit(_red);
{? EDOKUM.TYP().IDINHEAD
|| exec('get_atr','obe_faw',_obj)
?};
{? _tab.edit("exec('after_record','obe_faw')")
|| OBIEGI.B_PREL:=_b_prel; OBIEGI.B_PRELS:=_b_prels;
   {? EDOKUM.PAPERLES & EDOKUM.REFPRAC<>''
   || _p_uidref:=exec('FindAndGet','#table',P,EDOKUM.REFPRAC,,"uidref()",'');
      _p_id:=exec('getPid','portal_core',_p_uidref);
::    uzupełnienie wymaganych pól wniosku
      exec('atr_put','portal_walidacja','PracownikIdERP',EDOKUM.REFPRAC);
      exec('atr_put','portal_walidacja','PersonFromRequestId',$_p_id)
   ?};
   EDOKUM.memo_put(,'UW_OPDL');
:: Dołączanie wniosku z DOKUM
   EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
   {? _dokum & DOKUM.get()
   || exec('edokumz','dokum',EDOKUM.ref());
      exec('dokum_put','obiegi2')
   ?};
   {? _in_por & BPMN.END
   || EDOKUM.STATUS:='s';
      EDOKOS.cntx_psh();
      EDOKOS.index('PORTAL');
      EDOKOS.prefix(EDOKOS.EDOKUM,'S',OBIEGI.B_PREL,OPERATOR.USER);
      {? EDOKOS.first()
      || {? EDOKOS.STATUS<>'T'
         || EDOKOS.STATUS:='T';
            EDOKOS.B_PREL:='R'+$EDOKOS.OPER_NUM;
            EDOKOS.prefix();
            EDOKOS.DATA:=date();
            EDOKOS.TIME:=time();
            EDOKOS.put()
         ?}
      ?};
      EDOKOS.cntx_pop()
   ?};
   EDOKUM.put();
   {? EDOKUM.TYP().IDINHEAD
   || exec('set_atr','obe_faw',_obj)
   ?};
   exec('dol_edokos','obiegi',_we);
   OBIEGI.EDOKOS();
   {? exec('szuk_edokpar','obiegi') & EDOKPAR.FORM_ADD
   || ($EDOKPAR.FORM_ADD().TRESC)()
   ?};
   {? _in_por
   || exec('EDOKUM_to_send','portal_interm')
   ?};
   {? ~_dokum & EDOKUM.f_active()
   || EDOKUM.f_rfresh();
      EDOKUM.f_seek(EDOKUM.ref())
   ?};
   _ref:=EDOKUM.ref()
|| exec('del_edok','obiegi')
?};
EDOKUM.cntx_pop();
VAR_DEL.delete('objID','kh_ask','utw_dob');
_ref


\put_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Poprawianie delegacji w dekretacji
::   WE: _a - parametry wejściowe czynności
::       _b - parametry wyjściowe czynności
::       _c - uruchomienie automatyczne
::       _d - 0 - uruchamiane z obszaru roboczego
::            1 - uruchamiane z TODO
::            2 - uruchamiane z pulpitu
::----------------------------------------------------------------------------------------------------------------------
_we:=_a;
_wy:=_b;
_auto:=_c;
_ref:=null;
{? var_pres('_wy')>0 & var_pres('EDOKUM',_wy)>0 & _wy.EDOKUM
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(_wy.EDOKUM));
   {? EDOKUM.seek(_wy.EDOKUM)
   || typobi:=3; exec('typobi','obiegi');
      _dalej:=exec('pop_dob_spr','obiegi2');
      {? _dalej
      || exec('tabkhini','kontrahent');
         _okno:={? _d=1 || 'WD021' |? _d=2 || 'WD022J' || 'WD020' ?};
         EDOKUM.efld_opt(_okno,'enable=1,editable=0',EDOKUM,'TYP');
         exec('win_del_ini','obiegi2',_okno);
         {? EDOKUM.TYP
         || {? ETYPY.ED_PODZ='T'
            || EDOKUM.btn_eopt(_okno,'btnPODZ','state=normal')
            || EDOKUM.btn_eopt(_okno,'btnPODZ','state=grayed')
            ?};
            {? ETYPY.OBS_PROJ
            || EDOKUM.btn_eopt(_okno,'btnPROJ','state=normal')
            || EDOKUM.btn_eopt(_okno,'btnPROJ','state=grayed')
            ?}
         ?};
         EDOKUM.memo_get(,'UW_OPDL');
         OBIEGI.DEL_ETAT:=''; SKID.SL_KH:=SKID.NIP:=''; SKID.ZAPOT:=0;
         {? _d=1
         || ''
         |? _d=2
         || _grey:=Plugin.run('ACT_GREY_EDOKUM_001',OPERATOR.USER,'EDOKUM',_okno);
            {? type_of(_grey)=type_of('') & _grey<>''
            || STR.split(_grey,',');
               STR.get_word();
               _word:=STR.get_word();
               {? _word<>''
               || {!
                  |? EDOKUM.btn_eopt(_okno,_word,'state=grayed');
                     _opt+=_word:=STR.get_word();
                     _word<>''
                  !}
               ?}
            ?};
            EDOKUM.win_edit(_okno);
            {? EDOKUM.edit("exec('after_record_del','obe_faw')")
            || EDOKUM.memo_put(,'UW_OPDL');
               EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
               EDOKUM.USERS:=OPERATOR.USER;
               EDOKUM.put()
            ?};
            _ref:=EDOKUM.ref()
         || ''
         ?}
      ?}
   ?};
   EDOKUM.cntx_pop()
?};
_ref


\put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Poprawianie wniosku w obiegu
::   WE: _a - parametry wejściowe czynności
::       _b - parametry wyjściowe czynności
::       _c - uruchomienie automatyczne
::----------------------------------------------------------------------------------------------------------------------
_we:=_a;
_wy:=_b;
_auto:=_c;
_ref:=null;
{? var_pres('_wy')>0 & var_pres('EDOKUM',_wy)>0 & _wy.EDOKUM
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(_wy.EDOKUM));
   EDOKUM.prefix();
   {? EDOKUM.seek(_wy.EDOKUM)
   || typobi:=2; exec('typobi','obiegi');
      {? EDOKUM.DOKUM<>''
      || kh_ask:=0;
         {? EDOKUM.TYP=null()
         || exec('set_typ','obe_faw',_we)
         ?}
      ?};
      VAR_DEL.delete('objID');
      objID:=_obj:=exec('obj','obe_faw');
      _obj.ADD:=1;
      _obj.set('REKORD',1);
      _red:=exec('create_red','obe_faw',_obj,,'R',var_pres('_we'));
      _tab:=_obj.getTab('REKORD');
      _tab.win_edit(_red);
      {? EDOKUM.TYP().IDINHEAD || exec('get_atr','obe_faw',_obj) ?};
      EDOKUM.memo_get(,'UW_OPDL');
      {? _auto
      || _fld:=exec('after_record','obe_faw');
         _edit:=_fld<>'';
         {? _edit=0
         || BPMN.END:=1
         ?}
      || _edit:=1;
         BPMN.END:=0
      ?};
      {? _edit & (_auto | _tab.edit("exec('after_record','obe_faw')"))
      || EDOKUM.memo_put(,'UW_OPDL');
         EDOKUM.KHKH:=exec('edokum_khkh','obiegi');
         {? EDOKUM.put(,1)
         || {? EDOKUM.DOKUM<>'' || exec('dokum_put','obiegi2') ?}
         ?};
         {? EDOKUM.TYP().IDINHEAD || exec('set_atr','obe_faw',_obj) ?}
      ?};
      VAR_DEL.delete('objID','kh_ask');
      _ref:=EDOKUM.ref()
   ?};
   EDOKUM.cntx_pop()
?};
_ref


\del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Usunięcie wniosku w obiegu
::   WE:
::----------------------------------------------------------------------------------------------------------------------
exec('us_dob','obiegi')


\after_record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Rekord po dla wniosku w obiegu
::----------------------------------------------------------------------------------------------------------------------
_fld:=exec('spr_dok','obiegi');
{? _fld='' & EDOKUM.TYP().IDINHEAD & exec('blok_innd','obiegi')=0
|| _fld:=exec('spr_atr','obe_faw',objID)
?};
_fld


\after_record_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37]
:: OPIS: Rekord po dla delegacji w obiegu
::----------------------------------------------------------------------------------------------------------------------
_fld:=exec('spr_dok','obiegi');
_fld


\edok_atk_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akcje okna WER2 tabeli EDOK_ATK
::----------------------------------------------------------------------------------------------------------------------
{? -menu_txt()='dołącz' & Plugin.runnable('EDOK_ATK_F_ADD_001') & Plugin.run('EDOK_ATK_F_ADD_001',EDOKUM.ref(),0)<>-1
|| 1
|? -menu_txt()='dołącz' | -menu_txt()='popraw'
|| {? -menu_txt()='popraw'
   || _rec:=EDOK_ATK.REKORD
   || EDOK_ATK.cntx_psh();
      EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOKUM.ref());
      _rec:={? EDOK_ATK.last() || EDOK_ATK.REKORD+1 || 1 ?};
      EDOK_ATK.cntx_pop()
   ?};
   {? var_pres('objID')>0
   || _tmp:=objID
   ?};
   VAR_DEL.delete('objID');
   objID:=_obj:=exec('obj','obe_faw');
   _obj.set('REKORD',_rec);
   _tab:=_obj.getTab('REKORD');
   _red:=exec('create_red','obe_faw',_obj,1,'R');
   exec('get_atr','obe_faw',_obj);
   _tab.win_edit(_red);
   {? _tab.edit("exec('spr_atr','obe_faw',objID)")
   || exec('set_atr','obe_faw',_obj)
   || {? -menu_txt()='dołącz' || exec('edk_atk_del','obe_faw',_obj) ?}
   ?};
   VAR_DEL.delete('objID');
   {? var_pres('_tmp')>0
   || objID:=_tmp
   ?}
|? -menu_txt()='usuń'
|| {? FUN.ask('Czy usunąć informację dodatkową?'@)
   || exec('edk_atk_del','obe_faw')
   ?}
|? -menu_txt()='w górę'
|| exec('edok_atk_move','obe_faw',-1)
|? -menu_txt()='w dół'
|| exec('edok_atk_move','obe_faw',1)
|| _obj:=exec('obj','obe_faw');
   _obj.set('REKORD',EDOK_ATK.REKORD);
   _tab:=_obj.getTab('REKORD');
   _red:=exec('create_red','obe_faw',_obj,1,'D');
   exec('get_atr','obe_faw',_obj);
   _tab.win_edit(_red);
   _tab.display()
?}


\edok_atk_move
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Przeniesienie kompletu
::   WE: _a - -1 w górę, 1 w dół
::----------------------------------------------------------------------------------------------------------------------
_r1:=EDOK_ATK.REKORD;
_ref:=EDOK_ATK.ref();
{? _a=-1
|| _ok:=EDOK_ATK.prev()
|| _ok:=EDOK_ATK.next()
?};
{? _ok
|| _r2:=EDOK_ATK.REKORD;
   {? EDOKUM.r_lock(1,1,1)
   || exec('edok_atk_renum','obe_faw',_r1,0);
      exec('edok_atk_renum','obe_faw',_r2,_r1);
      exec('edok_atk_renum','obe_faw',0,_r2);
      EDOKUM.r_unlock()
   || FUN.info('Inny użytkownik obsługuje wniosek.'@)
   ?}
?};
EDOK_ATK.seek(_ref)


\edok_atk_renum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Przeniesienie kompletu
::   WE: _a - z rekordu
::       _b - na rekord
::----------------------------------------------------------------------------------------------------------------------
EDOK_ATR.cntx_psh();
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_a);
{? EDOK_ATR.first()
|| {!
   |? EDOK_ATR.cntx_psh();
      EDOK_ATR.prefix();
      EDOK_ATR.REKORD:=_b;
      EDOK_ATR.put();
      EDOK_ATR.cntx_pop();
      EDOK_ATR.first()
   !}
?};
EDOK_ATR.cntx_pop()


\edk_atk_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Usuwanie kompletu.
::   WE: [_a] - obiekt do obsługi bufora informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')>0 || _obj:=_a ?};
EDOK_ATK.cntx_psh();
_next:={? EDOK_ATK.next() | EDOK_ATK.prev() || EDOK_ATK.ref() || null ?};
EDOK_ATK.cntx_pop();
_nr:={? _=0 || EDOK_ATK.REKORD || _obj.get('REKORD') ?};
{? EDOKUM.r_lock(1,1,1)
|| EDOK_ATR.cntx_psh();
   EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_nr);
   {? EDOK_ATR.first() || {! |? EDOK_ATR.del !} ?};
   EDOK_ATR.cntx_pop();
   {!
   |? _nr+=1;
      EDOK_ATK.cntx_psh();
      EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOKUM.ref(),_nr);
      _jest:=EDOK_ATK.first();
      EDOK_ATK.cntx_pop();
      {? _jest
      || exec('edok_atk_renum','obe_faw',_nr,_nr-1);
         1
      ?}
   !};
   EDOKUM.r_unlock()
|| {? -menu_txt()='usuń' || FUN.info('Inny użytkownik obsługuje wniosek.'@) ?}
?}


\edok_atk_btn_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Aktualizacja przycisków << >> okna z informacją dodatkową
::   WE: [_a] - okno redagowania
::       [_b] - edycja (możliwość dołączania)
::----------------------------------------------------------------------------------------------------------------------
EDOK_ATK.cntx_psh();
EDOK_ATK.index('EDOKUM'); EDOK_ATK.prefix(EDOKUM.ref());
_max:={? EDOK_ATK.last() || EDOK_ATK.REKORD || 0 ?};
EDOK_ATK.cntx_pop();
_tab:=objID.getTab('REKORD');
_cur:=objID.get('REKORD');
_red:={? var_pres('_a')>0 || _a || _tab.win_edit('?') ?};
_edit:={? var_pres('_b')>0 || _b || 1 ?};
_tab.btn_eopt(_red,objID.btnD,'state='+ {? _cur<=1 || 'grayed' || 'normal' ?});
_tab.btn_eopt(_red,objID.btnU,'state='+ {? ((exec('blok_innd','obiegi') | ~_edit) & _cur=_max) || 'grayed' || 'normal' ?});
{? _edit || _tab.btn_eopt(_red,objID.btnDEL,'state='+ {? (_cur<=1 | ~_edit | _max<2) || 'grayed' || 'normal' ?}) ?};
~~


\edok_atk_step
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Przejście między informacjami dodatkowymi w oknie redagowania
::   WE: _a - -1 poprzedmi, 1 następny
::       _b - 1-edycja 0-wyswietlenie
::----------------------------------------------------------------------------------------------------------------------
_step:=_a;
_edit:={? var_pres('_b')>0 || _b ?};
_cur:=objID.get('REKORD');
_fld:={? _edit || exec('spr_dok','obiegi') || '' ?};
_max:=exec('edk_atr_lastpoz','obiegi',EDOKUM.ref());
_dalej:={? _cur+_step>_max || FUN.ask('Czy dodać nowy komplet informacji dodatkowych?'@) || 1 ?};
{? _fld='' & _dalej
|| {? _edit & exec('blok_innd','obiegi')=0
   || _fld:=exec('spr_atr','obe_faw',objID)
   ?};
   {? _fld=''
   || {? _edit || exec('set_atr','obe_faw',objID) ?};
      _cur+=_step;
      objID.set('REKORD',_cur);
      exec('get_atr','obe_faw',objID);
      {? _edit
      || exec('set_atr','obe_faw',objID);
         exec('get_atr','obe_faw',objID)
      ?};
      exec('edok_atk_btn_upd','obe_faw',,_edit)
   ?}
?};
{? _fld=''
|| ''
|| 'edit:'+_fld
?}


\akc_zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akcja zmiany zakresu
::----------------------------------------------------------------------------------------------------------------------
typobi:=2;
_tab:=OBIEGI;
_red:=_tab.mk_edit('Zakres',,'obeakczak');
_tab.win_efld(_red,,'ZAKRES',,,,,,'Zakres',,,'radio-buttons',,
   {? zakres=1 || 'Niezakończone'
   |? zakres=5 || 'Nieprzekazane'
   |? zakres=2 || 'Niezaakceptowane'
   |? zakres=3 || 'Niezadekretowane'
   ?},"1",
   {? zakres=1 || 'Zakończone'
   |? zakres=5 || 'Przekazane'
   |? zakres=2 || 'Zaakceptowane'
   |? zakres=3 || 'Zadekretowane'
   ?},"2",
   {? zakres=3
   || 'Wszystkie dekretowane'
   || 'Odrzucone'
   ?},"3",
   {? zakres=3
   || 'Zarejestrowane'
   || 'Wszystkie'
   ?},"4"
);
_tab.win_esep(_red,'');
_tab.win_efld(_red,,'CZY_TYP',,,,,,'Wybór typu',,,'radio-buttons',,
   'Wszystkie',"'W'",
   'Wybrany',"'B'"
);
_tab.win_efld(_red,,'TYP','NAZWA','UNIK',,,,'Typ');
_tab.win_esep(_red,'');
_tab.win_efld(_red,,'CZY_ZAM',,,,,,'Wnioski',,,'radio-buttons',,
   'Niezamknięte',"'N'",
   'Zamknięte',"'Z'",
   'Wszystkie',"'W'"
);
_tab.win_esep(_red,'');
_tab.win_efld(_red,,'ALL_USR',,,,,,'Użytkownik',,,'radio-buttons',,
   'Bieżący',"0",
   'Wszyscy',"1"
);
_ok:=_tab.win_ebtn(_red,'text=%1'['OK'@],"'key:F2'");
_anuluj:=_tab.win_ebtn(_red,'text=%1'['Anuluj'@],"'key:Esc'");
_tab.btn_eopt(_red,_ok,'tooltip='+exec('help_red_ok','#window','P'));
_tab.btn_eopt(_red,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
_all:=exec('hasAction','users','OBE_FAW_ZBRP');
_tab.efld_opt(_red,'enable='+$_all,_tab,'ALL_USR');
_tab.win_edit(_red);
exec('ae_czy_typ','obiegi');
OBIEGI.ZAKRES:=OBEWin.zakres[zakres];
_zakres:=OBIEGI.ZAKRES;
_czy_typ:=OBIEGI.CZY_TYP;
_czy_zam:=OBIEGI.CZY_ZAM;
_all_usr:=OBIEGI.ALL_USR;
{? _tab.edit()
|| OBEWin.zakres[zakres]:=OBIEGI.ZAKRES;
   wybortyp:=0;
   wyb_typ:=OBIEGI.CZY_TYP='B';
   exec('zakres','obiegi',zakres,OBIEGI.ZAKRES,OBIEGI.ALL_USR,0)
|| OBIEGI.ZAKRES:=_zakres;
   OBIEGI.CZY_TYP:=_czy_typ;
   OBIEGI.CZY_ZAM:=_czy_zam;
   OBIEGI.ALL_USR:=_all_usr
?}


\akc_przekaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akcja przekazania wniosku do akceptacji
::----------------------------------------------------------------------------------------------------------------------
_jest:=exec('is_act_user','#b__box','OBE_FAW_CPRZ',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
{? _jest
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FAW_CPRZ';
   _params.AKCJA:='Przekaż';
   _params.UIDREF:=EDOKUM.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',EDOKUM.ref());
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params)
|| FUN.info('Przekazanie wniosku nie jest na liście zadań użytkownika.'@)
?}


\akc_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Prezentuje okno na potrzeby czynności przekazania lub akceptacji
::   WE: _a - typ akcji: 'P'-przekazanie 'A'-akceptacja
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
typobi:=2;
_edit:=0;
{? _typ='P'
|| zakres:=5
|| zakres:=2
?};
exec('rkprz1','obiegi',zakres);
VAR_DEL.delete('objID');
objID:=_obj:=exec('obj','obe_faw');
_obj.ADD:=1;
_obj.set('REKORD',1);
_red:=exec('create_red','obe_faw',_obj,,_typ);
_tab:=_obj.getTab('REKORD');
_tab.win_edit(_red);
{? EDOKUM.TYP().IDINHEAD
|| _edit:=_typ<>'P' & exec('blok_innd','obiegi')=0;
   exec('get_atr','obe_faw',_obj)
?};
BPMN.END:=0;
{? _edit
|| {? _tab.edit("exec('spr_atr','obe_faw',objID)")
   || {? EDOKUM.TYP().IDINHEAD || exec('set_atr','obe_faw',_obj) ?}
   ?}
|| _tab.display()
?};
VAR_DEL.delete('objID');
{? _typ='P'
|| {? BPMN.END=1
   || 'Przekaż'
   |? BPMN.END=2
   || 'Odrzuc1'
   || ''
   ?}
|| {? BPMN.END=1
   || 'Akceptuj'
   |? BPMN.END=2
   || 'Odrzuć'
   || ''
   ?}
?}


\akc_odrzuc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akcja odrzucenia przekazania wniosku do akceptacji
::----------------------------------------------------------------------------------------------------------------------
{? zakres=5
|| _action:='OBE_FAW_CPRZ'
|| _action:='OBE_FAW_EAKP'
?};
_jest:=exec('is_act_user','#b__box',_action,OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);

{? _jest
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:=_action;
   {? zakres=5
   || _params.AKCJA:='Odrzuc1'
   || _params.AKCJA:='Odrzuć'
   ?};
   _params.UIDREF:=EDOKUM.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',EDOKUM.ref());
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params)
|| {? zakres=5
   || FUN.info('Przekazanie wniosku nie jest na liście zadań użytkownika.'@)

   || FUN.info('Akceptacja wniosku nie jest na liście zadań użytkownika.'@)

   ?}
?}


\win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Zwraca okno wertowania dla wniosków w obiegu
::   WE: _a - typ okna: 'R'-rejestracja, 'P'-przekazanie, 'A'-akceptacja
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;

_sel:=EDOKUM.mk_sel('','P',,'wniosel'+-_typ,,,,,'U');
EDOKUM.win_fld(_sel,,'DATAW',,,10,,,'Rejestrowano');
EDOKUM.win_fld(_sel,,'USERS','DANE',,12,,,'Rejestrujący');
EDOKUM.win_fld(_sel,,'ID',,,20,,,'Identyfikator');
EDOKUM.win_fld(_sel,,'DOSTAWCA','NAZWISKO',,20,,,'Osoba');
EDOKUM.win_fld(_sel,,'TR',,,{? 'RA'*_typ || 26 || 30 ?},,,'Temat');
EDOKUM.win_fld(_sel,,'NETTO',,,14,,,'Kwota netto');
EDOKUM.win_fld(_sel,,'WART',,,14,,,'Kwota brutto');
EDOKUM.win_fld(_sel,,'B_PRELS',,,18,,,'Bieżąca czynność');
{? 'RA'*_typ
|| EDOKUM.win_fld(_sel,,'PAPERLES',,,-4,,,'Paperless',,,2,,"1","0")
?};
EDOKUM.win_fld(_sel,,'ZAM',,,-9,,,'Zamknięty',,,2,,"'T'","'N'");
{? _typ='R'
|| EDOKUM.win_act(_sel,,'Formuła','Dołącz',,,"exec('akc_add','obe_faw')",);
   task_attach('OBE_FAW_DARP');
   EDOKUM.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['Dołącz'@@],'menu:D');
   EDOKUM.win_act(_sel,,'Formuła','Popraw',,,"exec('akc_mod','obe_faw')",);
   task_attach('OBE_FAW_DARP');
   EDOKUM.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['Popraw'@@],'menu:P');
   EDOKUM.win_act(_sel,,'Formuła','Usuń',,,"exec('akc_del','obe_faw')",);
   task_attach('OBE_FAW_DARP');
   EDOKUM.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['Usuń'@@],'menu:U');
   EDOKUM.win_act(_sel,,'Formuła','Wyśl&ij',,'Zapisanie danych i wysłanie do kolejnego etapu',"exec('akc_end','obe_faw')",);
   task_attach('OBE_FAW_DARP');
   EDOKUM.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['Wyślij'@@],'menu:I');
   EDOKUM.win_act(_sel,,'Formuła','Wycofaj',,,,"exec('ob_wycofaj','obiegi2','W','W')");
   task_attach('OBE_FAW_WYCO');
   task_attach('POR_KIE_WYCO')
|? _typ='P'
|| EDOKUM.win_act(_sel,,'Formuła','Prz&ekaż do akceptacji',,,"exec('akc_przekaz','obe_faw')",);
   task_attach('OBE_FAW_CPRZ');
   EDOKUM.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['Prz&ekaż'@@],'menu:E');
   EDOKUM.win_act(_sel,,'Formuła','O&drzuć',,,"exec('akc_odrzuc','obe_faw')",);
   task_attach('OBE_FAW_CPRO');
   EDOKUM.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['O&drzuć'@@],'menu:D');
   EDOKUM.win_act(_sel,,'Formuła','Wycofaj',,,,"exec('ob_wycofaj','obiegi2','P','W')");
   task_attach('OBE_FAW_WYCO');
   task_attach('POR_KIE_WYCO')
|? _typ='A'
|| EDOKUM.win_act(_sel,,'Formuła','Ak&ceptuj',,,"exec('akc_akceptuj','obe_faw')",);
   task_attach('OBE_FAW_EAKP');
   EDOKUM.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['Ak&ceptuj'@@],'menu:C');
   EDOKUM.win_act(_sel,,'Formuła','O&drzuć',,,"exec('akc_odrzuc','obe_faw')",);
   task_attach('OBE_FAW_EAKP');
   EDOKUM.win_btn(_sel,'text=%1,btn_label_align=center,panel=right,align=begin'['O&drzuć'@@],'menu:D');
   EDOKUM.win_act(_sel,,'Formuła','Wycofaj',,,,"exec('ob_wycofaj','obiegi2','A','W')");
   task_attach('OBE_FAW_WYCO');
   task_attach('POR_KIE_WYCO')
?};
EDOKUM.win_act(_sel,,'Formuła','&Zamknij obieg',,,"exec('akc_close','obe_faw')",);
task_attach('OBE_FAW_ZAMK');
task_attach('POR_KIE_ZAMK');
EDOKUM.win_act(_sel,,'Formuła','Pro&jekty',,,"exec('proj_dob','obiegi_proj','WW',2)",);
_podz_dob:=$('exec(\'podz_dob\',\'obiegi\',\''+_typ+'W'+'\')');
EDOKUM.win_act(_sel,,'Formuła','Podziały dla co&ntrollingu',,,_podz_dob,);
EDOKUM.win_act(_sel,,'Formuła','Historia obie&gu',,,"exec('okn_opis','obiegi')",);
EDOKUM.win_act(_sel,,'Formuła','Zadan&ia',,,"exec('todo_select','obiegi')",,,,,,'A');
EDOKUM.win_act(_sel,,'Menu','Funkcje',,,,);
EDOKUM.win_act(_sel,,'Formuła','Pokaż &budżet','Funkcje',,"exec('main','!ctr_oba_pbkp')",);
::EDOKUM.win_act(_sel,,'Formuła','Z&mień zakres',,,"exec('akc_zakres','obe_faw')",);
EDOKUM.win_act(_sel,0,'Formuła','Pokaż statystykę','Funkcje','Statystyka dla wniosków w obiegu',
               "exec('ob_stat_act','obiegi2',2)",);
task_attach('OBE_FAW_KOBI');
EDOKUM.win_act(_sel,,'Menu','Z&mień zakres',,,,);
EDOKUM.win_act(_sel,,'Menu','Wnioski &użytkownika','Z&mień zakres',,,,);
EDOKUM.win_act(_sel,,'Menu','Wszystkie wnioski wg &grup','Z&mień zakres',,,,);
task_attach('OBE_FAW_ZBGR');
EDOKUM.win_act(_sel,,'Menu','W&szystkie wnioski','Z&mień zakres',,,);
task_attach('OBE_FAW_ZBRP');
EDOKUM.win_act(_sel,,'Formuła',
               {? _typ='R' || 'Niezakończone'
               |? _typ='P' || 'Nieprzekazane'
               |? _typ='A' || 'Niezaakceptowane'
               ?},
               'Z&mień zakres,Wnioski &użytkownika',,"exec('zakres','obiegi',zakres,1,0,0)",);
EDOKUM.win_act(_sel,,'Formuła',
               {? _typ='R' || 'Zakończone'
               |? _typ='P' || 'Przekazane'
               |? _typ='A' || 'Zaakceptowane'
               ?},
               'Z&mień zakres,Wnioski &użytkownika',,"exec('zakres','obiegi',zakres,2,0,0)",);
EDOKUM.win_act(_sel,,'Formuła','Odrzucone','Z&mień zakres,Wnioski &użytkownika',,"exec('zakres','obiegi',zakres,3,0,0)",);
EDOKUM.win_act(_sel,,'Formuła','Wszystkie','Z&mień zakres,Wnioski &użytkownika',,"exec('zakres','obiegi',zakres,4,0,0)",);
EDOKUM.win_act(_sel,,'Formuła',
               {? _typ='R' || 'Niezakończone'
               |? _typ='P' || 'Nieprzekazane'
               |? _typ='A' || 'Niezaakceptowane'
               ?},
               'Z&mień zakres,Wszystkie wnioski wg &grup',,"exec('zakres','obiegi',zakres,1,2,0)",);
EDOKUM.win_act(_sel,,'Formuła',
               {? _typ='R' || 'Zakończone'
               |? _typ='P' || 'Przekazane'
               |? _typ='A' || 'Zaakceptowane'
               ?},
               'Z&mień zakres,Wszystkie wnioski wg &grup',,"exec('zakres','obiegi',zakres,2,2,0)",);
EDOKUM.win_act(_sel,,'Formuła','Odrzucone','Z&mień zakres,Wszystkie wnioski wg &grup',,"exec('zakres','obiegi',zakres,3,2,0)",);
EDOKUM.win_act(_sel,,'Formuła','Wszystkie','Z&mień zakres,Wszystkie wnioski wg &grup',,"exec('zakres','obiegi',zakres,4,2,0)",);
EDOKUM.win_act(_sel,,'Formuła',
               {? _typ='R' || 'Niezakończone'
               |? _typ='P' || 'Nieprzekazane'
               |? _typ='A' || 'Niezaakceptowane'
               ?},
               'Z&mień zakres,W&szystkie wnioski',,"exec('zakres','obiegi',zakres,1,1,0)",);
EDOKUM.win_act(_sel,,'Formuła',
               {? _typ='R' || 'Zakończone'
               |? _typ='P' || 'Przekazane'
               |? _typ='A' || 'Zaakceptowane'
               ?},
               'Z&mień zakres,W&szystkie wnioski',,"exec('zakres','obiegi',zakres,2,1,0)",);
EDOKUM.win_act(_sel,,'Formuła','Odrzucone','Z&mień zakres,W&szystkie wnioski',,"exec('zakres','obiegi',zakres,3,1,0)",);
EDOKUM.win_act(_sel,,'Formuła','Wszystkie','Z&mień zakres,W&szystkie wnioski',,"exec('zakres','obiegi',zakres,4,1,0)",);
EDOKUM.win_act(_sel,,'Menu','D&rukuj',,,,,,,,,,,'icon=print');
EDOKUM.win_act(_sel,,'Formuła','D&rukuj bieżący dokument','D&rukuj',,"exec('wnio_print','obe','A')",);
EDOKUM.win_act(_sel,,'Formuła','--A','D&rukuj',,,);
EDOKUM.win_act(_sel,,'Formuła','&Zestawienia dokumentów','D&rukuj',,"exec('obe_zest','obe')",);
EDOKUM.win_act(_sel,,'Formuła','Parametr&y pracy',,,"exec('obg_params','obiegi')",);
{? exec('lic_or','#b_domain','LZK',' LSP')
|| EDOKUM.win_act(_sel,,'Formuła','Dokument w logi&styce',,,"exec('dob_log','obiegi_log')",)
?};
EDOKUM.win_act(_sel,,'Kolejność',,,,,);
EDOKUM.win_act(_sel,,'Formuła','Legenda',,,"exec('legenda','color','EDOKUM#02')",);
EDOKUM.win_act(_sel,,'Rekord',,,,"exec('rekprzed','color','EDOKUM#01')",);
EDOKUM.win_act(_sel,,'Wyświetl',,,,"exec('akc_display','obe_faw')",);
{? _typ='R'
|| EDOKUM.win_act(_sel,1,'Formuła','Dołącz',,,"exec('akc_add','obe_faw')",);
   task_attach('OBE_FAW_DARP')
?};
EDOKUM.win_act(_sel,1,'Menu','Funkcje',,,,);
EDOKUM.win_act(_sel,1,'Formuła','Pokaż &budżet','Funkcje',,"exec('main','!ctr_oba_pbkp')",);
EDOKUM.win_act(_sel,1,'Formuła','Pokaż statystykę','Funkcje','Statystyka dla wniosków w obiegu',
               "exec('ob_stat_act','obiegi2',2)",);
task_attach('OBE_FAW_KOBI');
EDOKUM.win_act(_sel,1,'Menu','Z&mień zakres',,,,);
EDOKUM.win_act(_sel,1,'Menu','Wnioski &użytkownika','Z&mień zakres',,,,);
EDOKUM.win_act(_sel,1,'Menu','Wszystkie wnioski wg &grup','Z&mień zakres',,,);
task_attach('OBE_FAW_ZBGR');
EDOKUM.win_act(_sel,1,'Menu','W&szystkie wnioski','Z&mień zakres',,,);
task_attach('OBE_FAW_ZBRP');
EDOKUM.win_act(_sel,1,'Formuła',
               {? _typ='R' || 'Niezakończone'
               |? _typ='P' || 'Nieprzekazane'
               |? _typ='A' || 'Niezaakceptowane'
               ?},
               'Z&mień zakres,Wnioski &użytkownika',,"exec('zakres','obiegi',zakres,1,0,0)",);
EDOKUM.win_act(_sel,1,'Formuła',
               {? _typ='R' || 'Zakończone'
               |? _typ='P' || 'Przekazane'
               |? _typ='A' || 'Zaakceptowane'
               ?},
               'Z&mień zakres,Wnioski &użytkownika',,"exec('zakres','obiegi',zakres,2,0,0)",);
EDOKUM.win_act(_sel,1,'Formuła','Odrzucone','Z&mień zakres,Wnioski &użytkownika',,"exec('zakres','obiegi',zakres,3,0,0)",);
EDOKUM.win_act(_sel,1,'Formuła','Wszystkie','Z&mień zakres,Wnioski &użytkownika',,"exec('zakres','obiegi',zakres,4,0,0)",);
EDOKUM.win_act(_sel,1,'Formuła',
               {? _typ='R' || 'Niezakończone'
               |? _typ='P' || 'Nieprzekazane'
               |? _typ='A' || 'Niezaakceptowane'
               ?},
               'Z&mień zakres,Wszystkie wnioski wg &grup',,"exec('zakres','obiegi',zakres,1,2,0)",);
EDOKUM.win_act(_sel,1,'Formuła',
               {? _typ='R' || 'Zakończone'
               |? _typ='P' || 'Przekazane'
               |? _typ='A' || 'Zaakceptowane'
               ?},
               'Z&mień zakres,Wszystkie wnioski wg &grup',,"exec('zakres','obiegi',zakres,2,2,0)",);
EDOKUM.win_act(_sel,1,'Formuła','Odrzucone','Z&mień zakres,Wszystkie wnioski wg &grup',,"exec('zakres','obiegi',zakres,3,2,0)",);
EDOKUM.win_act(_sel,1,'Formuła','Wszystkie','Z&mień zakres,Wszystkie wnioski wg &grup',,"exec('zakres','obiegi',zakres,4,2,0)",);
EDOKUM.win_act(_sel,1,'Formuła',
               {? _typ='R' || 'Niezakończone'
               |? _typ='P' || 'Nieprzekazane'
               |? _typ='A' || 'Niezaakceptowane'
               ?},
               'Z&mień zakres,W&szystkie wnioski',,"exec('zakres','obiegi',zakres,1,1,0)",);
EDOKUM.win_act(_sel,1,'Formuła',
               {? _typ='R' || 'Zakończone'
               |? _typ='P' || 'Przekazane'
               |? _typ='A' || 'Zaakceptowane'
               ?},
               'Z&mień zakres,W&szystkie wnioski',,"exec('zakres','obiegi',zakres,2,1,0)",);
EDOKUM.win_act(_sel,1,'Formuła','Odrzucone','Z&mień zakres,W&szystkie wnioski',,"exec('zakres','obiegi',zakres,3,1,0)",);
EDOKUM.win_act(_sel,1,'Formuła','Wszystkie','Z&mień zakres,W&szystkie wnioski',,"exec('zakres','obiegi',zakres,4,1,0)",);
EDOKUM.win_act(_sel,1,'Formuła','Parametr&y pracy',,,"exec('obg_params','obiegi')",);
EDOKUM.win_fml(_sel,,'TR',,'ICON_BEFORE',"{? EDOKUM.EDOKUM || 'xwin16.png:119' || '' ?}");
_sel


\akc_akceptuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Akceptacja wniosku w obiegu z obszaru - jTerm
::----------------------------------------------------------------------------------------------------------------------
_jest:=exec('is_act_user','#b__box','OBE_FAW_EAKP',OPERATOR.USER,EDOKUM.uidref(),REF.FIRMA);
{? _jest
|| _params:=exec('mp_run_a','#b__box');
   _params.ACT_UID:='OBE_FAW_EAKP';
   _params.AKCJA:='Akceptuj';
   _params.UIDREF:=EDOKUM.uidref();
   _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',EDOKUM.ref());
   _params.PROC_START:='N';
   exec('mp_run','#b__box',_params)
|| FUN.info('Akceptacja wniosku nie jest na liście zadań użytkownika.'@)
?}


\akc_display
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Wyświetlenie wniosku w obiegu
::----------------------------------------------------------------------------------------------------------------------
_zakres:={? var_pres('zakres')>0 || zakres || ~~ ?};
zakres:=1;
_obj:=objID:=exec('obj','obe_faw');
_obj.ADD:=0;
_obj.set('REKORD',1);
_red:=exec('create_red','obe_faw',_obj,,'D');
_tab:=_obj.getTab('REKORD');
_tab.win_edit(_red);
{? EDOKUM.TYP().IDINHEAD || exec('get_atr','obe_faw',_obj) ?};
_tab.display();
VAR_DEL.delete('objID');
{? var_pres('_zakres')>0
|| zakres:=_zakres
?}


\set_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Ustawia typ dla wniosków na podstawie parametru procesu
::   WE: _a - parametry wejściowe czynności
::       _b - komunikaty
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_we:=_a;
_result:=1;
{? var_pres('TYP_DOK',_we)>0
|| _obj:=exec('obj_typ_dok','obiegi2');
   _obj.set(_we.TYP_DOK);
   _typ:=OBIEG.ETYPY:=exec('FindInSet','#table','ETYPY','UNIK_WP',_obj.NAZWA,OBIEGI.TYPOBIEG,,1,_obj.W_PORTAL,null);
   {? ~_typ
   || return(0)
   |? 'TX'*OBIEG.ETYPY().W_PORTAL | ETYPY.W_PORTAL='P' & ETYPY.IN_ERP=0
   || return(0)
::   |? OBIEG.ETYPY().GRP_DOC<>null &
::      ~exec('has_grp_perm','obiegi2',OBIEG.ETYPY().GRP_DOC().N,{? typobi=1 || 'OBG_FZO_DARK'
::                                                               |? typobi=2 || 'OBE_FAW_DARP'
::                                                               |? typobi=3 || 'OBE_FDL_DBRP'
::                                                               ?})
::   || return(0)
   ?}
|| _typ:=null
?};
EDOKUM.TYP:=_typ;
{? EDOKUM.TYP
|| EDOKUM.TYPOBIEG:=EDOKUM.TYP().TYPOBIEG;
   {? EDOKUM.TYP().ED_TEMAT='N'
   || EDOKUM.TR:=EDOKUM.TYP().NAZWA
   ?};
   OSOBA.cntx_psh();
   OPERATOR.USER().OSOBA();
   {? EDOKUM.TYP().CZY_PRAC & ~exec('prac_firm_af_www1','obiegi2',OSOBA.PIERWSZE+' '+OSOBA.NAZWISKO)
   || EDOKUM.DOSTAWCA:=null()
   || EDOKUM.DOSTAWCA:=OSOBA.ref()
   ?};
   OSOBA.cntx_pop();
   {? EDOKUM.TYP().AUT_ID='T' & EDOKUM.ID=''
   || exec('ustal_numer','obiegi',typobi,SSTALE.AR,1);
      EDOKUM.ID:={? var_pres('id_edok')>0 || id_edok || '' ?};
      EDOKUM.TYP:=_typ
   ?};
   EDOKUM.put()
|| _result:=0
?};
_result


\preview
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Podgląd e-dokumentu
:: ~OST: INWEBBROWSER
::----------------------------------------------------------------------------------------------------------------------
_filepath:='';
_loaded:=0;
_wb_acr:=EDOKUM.mk_ctr('Podgląd e-dokumentu'@,,,,100,35);
EDOKUM.win_ctr(_wb_acr);
{? exec('runtime_r10','#system')
|| EDOKUM.win_cctr(_wb_acr,'ctr_id','@webframe')
|| EDOKUM.win_cctr(_wb_acr,'ctr_id','@webbrowser2',1)
?};
{? EDOKUM.EDOKUM<>null() & 'pdf;bmp;png;tiff;tif;jpeg;jpg'*(-EDOKUM.bl_info('EDOKUM','EXTENSION'))>0
|| _filepath:=exec('tmp_filepath','#file',EDOKUM.bl_info('EDOKUM','NAME'));
   _loaded:=EDOKUM.bl_get('EDOKUM',_filepath)
?};
{? _loaded>0
|| _filepath:=gsub(_filepath,'\\','\\\\');
   EDOKUM.control(,,$("ctr_set(,'ctr_id','navigate','"+_filepath+"');~~"))
|| EDOKUM.control(,,"ctr_set(,'ctr_id','setHTMLContent','<html></html>');~~")
?};
''


\szuk_edokpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Szuka EDOKPAR dla bieżącego EDOKUM i B_PREL
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || return(0) ?};
_a:={? var_pres('_a')>0 || _a || EDOKUM.ref() ?};
_b:={? var_pres('_b')>0 || _b || '' ?};
_zwrot:=0;
{? ref_name(_a)+2<>EDOKPAR.name()+2
|| EDOKPAR.use('skidh_'+(ref_name(_a)+2))
?};
{? _a<>null() & _b<>''
|| EDOKPAR.index('UNIK'); EDOKPAR.prefix(_a,_b);
   _zwrot:=EDOKPAR.first()
?};
_zwrot


\edok_atk_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Usunięcie informacji dodatkowych w oknie redagowania
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć bieżący komplet informacji dodatkowych?'@)
||  _cur:=objID.get('REKORD');
    EDOK_ATR.cntx_psh();
    EDOK_ATR.use('edokat'+(EDOKUM.name()+2));
    EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(EDOKUM.ref(),null,'',_cur);
    {? EDOK_ATR.first()
    || exec('edk_atr_usun1','obiegi',0,EDOKUM)
    ?};
    EDOK_ATR.cntx_pop();
    _cur:=exec('edk_atr_lastpoz','obiegi',EDOKUM.ref());
    objID.set('REKORD',_cur);
    exec('get_atr','obe_faw',objID);
    exec('edok_atk_btn_upd','obe_faw',,1);
    ''
|| ''
?}

:Sign Version 2.0 jowisz:1045 2023/12/11 09:59:25 0b75800198e0d426f422fe6c7357dfb9cb8e7e20ac5e3d75643735fd05c0f51b9153a6d6907f4f0e8506361342ca1969850fa16ec748c98d1005c636d4b944a863316c9160b6958120981e490ddbb0e534f11b28237b4fbc8c304cd9dbcbd1625b688bb4f4c85d2a0dc2ae929510875fbdb049941af2e50087ec41d6e92d7a5a

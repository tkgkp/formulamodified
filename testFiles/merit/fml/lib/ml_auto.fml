:!UTF-8
::(c) Macrologic SA odzial Wroclaw Wszelkie prawa zastrzeżone
::========================================================================================================================
::Nazwa pliku: ml_auto.fml
::Utworzony: 2009-04-30
::Autor: EP
::Systemy: FIKS,FIKS_B
::========================================================================================================================
::Zawartosc: Formuly automatycznej rejestracji w wersji budzetowej
::========================================================================================================================


\poz_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?.??]
:: OPIS: Formuła dodaje rekord do tabeli POZ. Wykorzystywana przez formuły przez
::       formuly rejestrujace
::   WE: _a - kwota
::       _b - strona księgowa
::       _c - konto
::       _d - identyfikator rozrachunku
::       _e - typ rozrachunku
::       _f - data otwarcia rozrachunku
::       _g - termin płatności rozrachunku
::       _h - opis
::      [ _i - kod waluty (nieobowiązkowy)
::       _j - kwota w walucie (nieobowiązkowy)
::       _k - kurs
::       _l - tabela do uzupelnienia wyroznikow
::       _m - data rozliczenia rozrachunku]
::       _n - oddział
::   WY: objekt[czy_dodano[0,1],POZ.ref]
:: HIST: AK[8.50] 14.04.2005 dla potrzeb formul rejestrujacy MacLex dodano na
::       wyjsciu POZ.ref
::       WS[1020] 18.08.2008 - dodano wariant zmiennej _n - oddzial dla wartoŁci liczbowej
::----------------------------------------------------------------------------------------------------------------------
_dodano:=obj_new(2);
_dodano[1]:=0; _dodano[2]:=null();
KA.CZY_WYR:='N';
  {? +_b & (_a$2<>0 | (var_pres('_i')>0 & var_pres('_j')>0 & _j$2<>0 & var_pres('_k')>0))
  || POZ.cntx_psh();
     POZ.index('DOK');
     POZ.prefix(DOK.ref);
     {? POZ.last() || _nr:=POZ.POZ+1 || _nr:=1 ?};
     POZ.blank();
     POZ.POZ:=_nr;
     POZ.SUM:=_a$2;
     {? -_b='wn' || POZ.STR:='Wn' |? -_b='ma' || POZ.STR:='Ma' ?};
     POZ.KON:=_c;
     POZ.KOM:=exec('koment','konto',_c);
     {? _d<>''
     || {? _=14
        || {? type_of(_n)=2
           || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
              _n:={? ODD.find_key(_n,_n) || ODD.ref() || null ?}
           |? type_of(_n)=1
           || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
              _n:={? _n<>0 & ODD.seek(_n,) || ODD.ref || null ?}
           ?};
           POZ.ODD:=_n
        || POZ.ODD:=DOK.ODD
        ?}
     || POZ.ODD:=null()
     ?};
     POZ.ID:=_d;
     POZ.TID:=~-_e;
     POZ.DO:=_f;
     POZ.TP:=_g;
     POZ.OP:=_h;
     POZ.SUMW:=0;
     POZ.WAL:=POZ.VAT:=null;
     {? var_pres('_i')>0 & var_pres('_j')>0 & +_i & (_j | (var_pres('_k')>0 & _k))
     || SLO.cntx_psh();
        SLO.index('SL'); SLO.prefix();
        {? SLO.find_key(FINFO.SLWAL().SLU, _i) & SLO.KOD=_i
        || POZ.WAL:=SLO.ref();
           WAL.index('WAL_SYM');
           WAL.prefix();
           POZ.SUMW:=_j;
           {? var_pres('_k')>0 & _k$4<>0
           || POZ.KURS:=_k;
              {? _a$2=0 & WAL.find_key(SLO.KOD)
              || POZ.SUM:=(POZ.SUMW*POZ.KURS/WAL.J)$2
              ?}
           || {? WAL.find_key(SLO.KOD)|| POZ.KURS:=((POZ.SUM*WAL.J)/POZ.SUMW)$6 ?}
           ?}
        ?};
        SLO.cntx_pop()
     ?};
     {? _>12 & _m<>date(0,0,0) || POZ.DATA_R:=_m ?};
     {? POZ.ID<>'' || exec('ust_roz','dok_fks') ?};
     {? ~POZ.add(1)
     || FUN.emsg('Błąd zapisu tabeli POZ.')
     || _dodano[1]+=1;
        {? _>=12 &  _l<>''|| exec('po_kon','dok_fks',_l)
                         || exec('po_kon','dok_fks')
        ?};
        exec('dod_wyr','wyrozniki')
     ?};
     _dodano[2]:=POZ.ref;
     POZ.cntx_pop()
  ?};
_dodano

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:54 a24643b3fd16d89bab9e494534433f9d86b08f77785f1be186f725168c811d5bfd663fc2b0dc46ee4b58d875a5f8390d0c182117fef646caa0fc15c1a57cdd7380ec205fee7198582c333a68c16cc2281183d7ab1c05c1edf54598d65ad492660aa58c13237ac05984f674c64005cde9edb30d6113825dd4fb07cb5d209befcb

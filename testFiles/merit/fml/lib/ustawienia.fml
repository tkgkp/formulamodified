:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ustawienia.fml
:: Utworzony: 19.01.2015
:: Autor: mario
::======================================================================================================================
:: Zawartość: Formuły do obsługi globalnych ustawień systemu: słowników, walut itp.
::======================================================================================================================


\bl_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wartość początkową pola waluta
::  OLD: \bl_wal/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
{? KSTE.WAL<>null
|| KSTE.WAL
|| INFO.NAROD
?}


\bl_nwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: podpowiadanie waluty narodowej
::   WY: zwraca walutę narodową
::  OLD: \bl_nwal/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
_kraj:='';
{? KSTE.KRAJ<>null
||
   _kraj:=KSTE.KRAJ().KOD
?};
{? _kraj<>INFO.KRAJ().KOD & _kraj<>''
|| _wyn:=exec('findkraj','ustawienia',_kraj,0)
|| _wyn:=INFO.NAROD
?};
_wyn


\bl_nwal_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: podpowiadanie waluty narodowej dla dokumentów magazynowych, zamówień dostaw, planów dostaw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
INFO.NAROD


\czy_narod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Sprawdza czy waluta _a jest narodowa i jest walutą opodatkowania
::   WE: _a - waluta handlowa
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('czy_nwal','ustawienia',_a) & _a=INFO.NAROD


\czy_nwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Sprawdza czy waluta _a jest walutą opodatkowania
::   WE: _a - waluta handlowa
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_a=exec('bl_nwal','ustawienia')


\findkraj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: szukanie waluty podanego kraju
::   WE: _a - kod kraju np. 'GB'
::       [_b - 0,1,2]
::   WY: w zalezności od parametru _b jest to wskazanie na
::       kraj (_b nie przekazane) KRAJE.ref
::       walute (_b=0) SLO.ref
::       slownik stawek VAT (_b=1) SLU.ref
::       kraj (_b=2) w słowniku krajów SLO.ref
::  OLD: \findkraj/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
SLO.cntx_psh();
SLU.cntx_psh();
KRAJE.cntx_psh();
{? _
|| KRAJE.index('KRAJE');
   KRAJE.prefix();
   _wy:={? KRAJE.find_key(_a) || KRAJE.ref || null ?}
?};
{? _=2
|| _wy:={? _b=0
        || KRAJE.WAL
        |? _b=1
        || SLU.index('NAZ');
           SLU.prefix();
           {? SLU.find_key('~STAWKI VAT '+_a)
           || SLU.ref
           || null
           ?}
        |? _b=2
        || SLU.index('NAZ');
           SLU.prefix();
           {? SLU.find_key('~KRAJE UE')
           || SLO.index('SL');
              SLO.prefix(SLU.ref);
              {? SLO.find_key(_a)
              || SLO.ref
              || null
              ?}
           || null
           ?}
        || null
        ?}
?};
KRAJE.cntx_pop();
SLU.cntx_pop();
SLO.cntx_pop();
_wy


\wz_sv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: wzorzec dla slownika stawek vat
::   WE: czy brac pod uwage tylko zapis w tabeli info T/N - domyslnie N
::  OLD: \wz_sv/tr_slown.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 || _a:='N' ?} || _a:='N' ?};
{? _a='N' & 5+cur_tab.name()='fakpo' & KSTE.KRAJ<>null
||
:: slownik wspolnoty
   {? (_a:=exec('findkraj','ustawienia',KSTE.KRAJ().KOD,1))
   || SLU.prefix();
      {? SLU.seek(_a)
      || SLU.get()
      ?}
   ?}
|? _a='N' & KSTE.KRAJ<>null
||
   SLU.index('NAZ');
   SLO.index('SL');
   SLU.prefix();
   {? SLU.find_key('~STAWKI VAT '+KSTE.KRAJ().KOD)
   || SLU.get()
   ?}
|| SLU.index('NAZ');
   SLO.index('SL');
   SLU.prefix();
   {? SLU.find_key('~STAWKI VAT '+INFO.KRAJ().KOD)
   || SLU.get()
   ?}
?};
''


\jakislsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: slownik stawek vat
::   WE: [_a]- 1 - nie wywoluje cur_tab  2 - dziala dla KSTE.KRAJ
::   WY: '' - czy znaleziono slownik stawek VAT 'brak'-nie znaleziono
::  OLD: \jakislsv/defin.fml
::       \wz_sv/tr_slown.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_get:=0;
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};

exec('czytaj','#stalesys',,INFO,'KRAJ');

_kraj_vat:={? BEER.KRAJ_VAT || BEER.KRAJ_VAT().SYM || KSTE.KRAJ().KOD ?};

_poz:=_a=0 & ('fakpo zkpoz'*(5+cur_tab.name()) | 'dokma'*(5+cur_tab.name()) & DK.PLUS='N' & ND.TYP().Z='T');

{? _a=0 & _poz & _kraj_vat<>''
||
:: slownik wspolnoty
   {? (_a:=exec('findkraj','ustawienia',_kraj_vat,1))
   || SLU.prefix();
      _wyn:={? SLU.seek(_a) || _get:=1; '' || 'brak' ?};
      SLU.get()
   ?}
|? _a=2
||
   {? _kraj_vat<>''
   || SLU.index('NAZ');
      SLO.index('SL');
      SLU.prefix();
      {? SLU.find_key('~STAWKI VAT '+_kraj_vat)
      || SLU.get();
         SLO.prefix(SLU.ref);
         _get:=1;
         _wyn:=''
      || FUN.info('Brak słownika: ~STAWKI VAT %1.'@[_kraj_vat])
      ?}
   || FUN.info('Niewypełniony kraj w stałych oddziału.\nNie można określić słownika dla Stawek VAT.'@)
   ?}
|| {? INFO.KRAJ<>null()
      & (exec('on','#mwapi') | (_a=0 & cur_tab(1)=M) | (INFO.KRAJ=KSTE.KRAJ) | (_a=0 & KSTE.KRAJ=null))
   || SLU.index('NAZ');
      SLO.index('SL');
      SLU.prefix();
      {? SLU.find_key('~STAWKI VAT '+INFO.KRAJ().KOD)
      || SLU.get();
         SLO.prefix(SLU.ref);
         _get:=1;
         _wyn:=''
      || FUN.info('Brak słownika: ~STAWKI VAT %1.'@[INFO.KRAJ().KOD])
      ?}
   |? INFO.KRAJ<>KSTE.KRAJ & KSTE.KRAJ<>null
   || SLU.index('NAZ');
      SLO.index('SL');
      SLU.prefix();
      {? SLU.find_key('~STAWKI VAT '+KSTE.KRAJ().KOD)
      || SLU.get();
         SLO.prefix(SLU.ref);
         _get:=1;
         _wyn:=''
      || FUN.info('Brak słownika: ~STAWKI VAT %1.'@[KSTE.KRAJ().KOD])
      ?}
   || FUN.info('Niewypełniony kraj w stałych systemu.\nNie można określić słownika dla Stawek VAT.'@)
   ?}
?};
{? _get || SLO.hdr_sel(''); SLO.hdr_sel(SLU.OP) ?};
''


\bl_wal_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK
:: OPIS: Wartosc poczatkowa FINFO.SLWAL
::  OLD: \bl_wal/wspol.fml
::----------------------------------------------------------------------------------------------------------------------
SLUAPPL.index('NAZ'); SLUAPPL.prefix('F');
{? SLUAPPL.find_key('WALUTY') || SLUAPPL.ref() || 0 ?}


\bl_wnarod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [11.22]
:: OPIS: Wartosc poczatkowa pola FINFO.NAROD
::  OLD: \bl_wnarod/wspol.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=null;
SLUAPPL.cntx_psh(); SLUAPPL.index('NAZ'); SLUAPPL.prefix('F');
{? SLUAPPL.find_key('WALUTY')
|| SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(SLUAPPL.SLU);
   {? SLO.find_key('PLN') || _zwrot:=SLO.ref() ?};
   SLO.cntx_pop()
?};
SLUAPPL.cntx_pop();
_zwrot


\bl_banki_http
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KGS [2011]
:: OPIS: Funkcja na wartosc poczatkowa pola HTTPBANK w tabeli XINFO.
::  OLD: \bl_banki_http/skid_upd.fml
::----------------------------------------------------------------------------------------------------------------------
'https://ewib.nbp.pl/faces/PlainDok?dokNazwa='


\bl_kraj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2010]
:: OPIS: Formula na blank dla pola XINFO.KRAJ
::  OLD: \bl_kraj/skid_xin.fml
::----------------------------------------------------------------------------------------------------------------------
KRAJE.cntx_psh(); KRAJE.index('KRAJE'); KRAJE.prefix('PL');
{? KRAJE.first() || _wy:=KRAJE.ref() || _wy:=null ?};
KRAJE.cntx_pop();
_wy


\bl_http
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [2008]
:: OPIS: Funkcja na wartosc poczatkowa pola HTTPPATH w tabeli XINFO
::  OLD: \bl_http/skid_upd.fml
::----------------------------------------------------------------------------------------------------------------------
'https://www.macrologic.pl/update'


\bl_kolor_et
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JSz [12.30]
:: OPIS: Wartośc początkowa dla pola XINFO.KOLOR_ET
::   WY: rgb koloru tła
::   OLD: \bl_kolor_et/skid_obi.fml
::----------------------------------------------------------------------------------------------------------------------
'255:0:0'


\jakiswal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JF [2008]
:: OPIS: slownik walut
::  OLD: \jakiswal/defin.fml
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,INFO,'SLWAL');

{? INFO.SLWAL<>null
||
   INFO.SLWAL().SLU();
   exec('slu_okn','slo_slu');
   1
||
   FUN.info('Nie wskazano słownika walut w parametrach pracy programu.'@);
   0
?}


\bl_fakkr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Zwraca KSTE.KRAJ
::   WY: KSTE.KRAJ
::  OLD: \bl_fakkr/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
KSTE.KRAJ


\czy_sts
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: sprawdza czy nalezy uzupelnic stanowisko sprzedazy
::  OLD: \czy_sts/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? BEER.SZ='S' & exec('get','#params',2102,2,OPERATOR.USER)='T' & ST.STS=null
|| FUN.info('Należy wskazać stanowisko sprzedaży\nw parametrach pracy użytkownika.'@);
   _wyn:=0
?};
_wyn


\czy_stz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: sprawdza czy nalezy uzupelnic stanowisko zakupow
::  OLD: \czy_stz/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? BEER.SZ='Z' & exec('get','#params',6102,2,OPERATOR.USER)='T' & ST.STZ=null
|| FUN.info('Należy wskazać stanowisko zakupów\nw parametrach pracy użytkownika.'@);
   _wyn:=0
?};
_wyn


\bl_narod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: podpowiada walutę z INFO
::  OLD: \bl_narod/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
INFO.NAROD


\bl_krsf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: blank - kurs
::  OLD: \bl_krsf/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.KRS


\crsystem_msg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Komunikat o niemożliwości uruchomienia raportu CR
::  OLD: \crsystem_msg/skid.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.emsg('Środowisko nie jest właściwe dla przeglądarki Crystal Reports.'@)


\be_msc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja miejsca dostawy
::  OLD: \be_msc/firma.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=cur_tfld();
{? cur_tfld()=ZD_NAG | (cur_tfld()=TR_NZL & TR_NZL.WHERE='ZD_NAG')
|| {? _Tab.TR_ZEWN<>null() & _Tab.TR_ZEWN().WYB_MSC
   || KH_MSC.prefix();
      KH_MSC.f_set('NAZ',,'KH_MSC.TR_ZEWN = :_a',_Tab.TR_ZEWN);
      KH_MSC_Z.TR_ZEWN:=_Tab.TR_ZEWN;
      KH_MSC_Z.KH:=null();
      KH_MSC.actions('WER','k');
      1
   || KH_MSC.prefix();
      KH_MSC.f_set('NAZ',,'KH_MSC.KH is null and KH_MSC.TR_ZEWN is null');
      KH_MSC_Z.KH:=null();
      KH_MSC.actions('WER','k');
      1
   ?}
|? cur_tfld()=ND & ND.TYP().P='T'
|| KH_MSC.prefix();
   KH_MSC.f_set('NAZ',,'KH_MSC.KH is null and KH_MSC.TR_ZEWN is null');
   KH_MSC_Z.KH:=null();
   KH_MSC.actions('WER','k');
   1
|? cur_tfld()=ND & ND.TYP().P='N'
|| KH_MSC.prefix();
   KH_MSC.f_set('NAZ',,'KH_MSC.KH = :_a',ND.KH);
   KH_MSC_Z.KH:=cur_tfld().KH;
   KH_MSC_Z.KH().KOD;
   KH_MSC.actions('WER','k');
   ND.KH<>null
|? _Tab=ZK_N | _Tab=TR_NZL
|| {? _Tab.TR_ZEWN<>null() & _Tab.TR_ZEWN().WYB_MSC
   || KH_MSC.prefix();
      KH_MSC.f_set('NAZ',,'KH_MSC.TR_ZEWN = :_a',_Tab.TR_ZEWN);
      KH_MSC_Z.TR_ZEWN:=_Tab.TR_ZEWN;
      KH_MSC_Z.KH:=null();
      KH_MSC.actions('WER','k');
      1
   || KH_MSC.prefix();
      KH_MSC.f_set('NAZ',,'KH_MSC.KH = :_a',_Tab.KH);
      KH_MSC_Z.TR_ZEWN:=null();
      KH_MSC_Z.KH:=_Tab.KH;
      KH_MSC_Z.KH().KOD;
      KH_MSC.actions('WER','k');
      _Tab.KH<>null()
   ?}
|| KH_MSC.f_clear();
   KH_MSC_Z.KH:=cur_tfld().KH;
   KH_MSC_Z.KH().KOD;
   KH_MSC.actions('WER','');
   cur_tfld.KH<>null
?}


\ref_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.10]
:: OPIS: Wskazanie na firme
::  OLD: \ref_firma/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ref_firma','#firma')


\firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.10]
:: OPIS: Wskazanie na bieżącą firme - do blankowania pól tabel powiązanych z FIRMA
::  OLD: \firma/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__F_ref')>0
|| FIRMA.ref()
|| {? app_info('web_sesid')<>''
   || exec('firma','#firma',exec('firma_symbol','#firma'))
   ?};
   REF.FIRMA
?}


\sel_win_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Okienko na F3 dla kontrahenta
::   WE: [_a] - STRING - akronim okienka, który ustawić, domyślnie brane okno WER
::----------------------------------------------------------------------------------------------------------------------
_win:={? var_pres('_a')=type_of('') & _a<>'' || _a || 'WER' ?};
KH.win_sel(_win);
KH.win_dict(_win);
KH.actions(_win,'WM:M');
~~


\tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: zwraca katalog roboczy
::  OLD: \tmp/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
'@!Tmp/'


\sel_win_typysp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Okienko na F3 dla kontrhenta
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
TYPYSP.win_sel('WER1');
~~


\rek_typydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: przed dołączeniem typydok
::----------------------------------------------------------------------------------------------------------------------
HELP.DN:={? TYPYDOK.DN='D' || 'D' || 'N' ?};
HELP.RRU:={? TYPYDOK.WYR='U' || 'T' || 'N' ?};
{? HELP.RRU='T' || TYPYDOK.WYR:='T' ?};
MG.cntx_psh();
{? ~(ST.MAG().TYP*'EWI')
|| TYPYDOK.actions_grayed('TYP_MAG','N(P)')
|| TYPYDOK.actions_grayed('TYP_MAG','')
?};
MG.cntx_pop();
~~


\kh_msc_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Miejsca dostaw licencjobiorcy
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KH_MSC.prefix();
KH_MSC.f_clear();
KH_MSC.f_set('NAZ',,'KH_MSC.KH is null and KH_MSC.TR_ZEWN is null');
KH_MSC_Z.KH:=null();
KH_MSC.actions('WER','kM');
KH_MSC.win_sel('WER');
KH_MSC.select();
KH_MSC.f_clear();
KH_MSC.actions('WER','');
''


\odd_par_1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Zwraca jednostkę księgową z pierwszego parametru systemu lub CENTRALĘ
::   WE: _a - opcja - ref rekordu w tabeli FIRMA
::   WY: ODD.ref()
::----------------------------------------------------------------------------------------------------------------------
_firma:={? _ & type_of(_a)=type_of(null) & _a
        || _a
        || exec('ref_firma','ustawienia')
        ?};
exec('odd_new','ustawienia',_firma);
_zwrot:=null;
_odd:=PAR_SKID.get(1);
{? _odd='' || _odd:='CENTRALA' ?};
ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(_firma,_odd,);
{? ODD.first() || _zwrot:=ODD.ref() ?};
ODD.cntx_pop();
{? ~_zwrot
|| ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(_firma,'CENTRALA',);
   {? ODD.first() || _zwrot:=ODD.ref() ?};
   ODD.cntx_pop()
?};
_zwrot


\odd_new
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.28]
:: OPIS: Zakłada jednostke księgową CENTRALA jeśli w systemie nie ma żadnej jednostki księgowej
::   WE: _a - opcja - ref rekordu w tabeli FIRMA
::----------------------------------------------------------------------------------------------------------------------
_firma:={? _ & type_of(_a)=type_of(null) & _a
        || _a
        || exec('ref_firma','ustawienia')
        ?};
ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(_firma);
{? ~ODD.first()
|| ODD.blank();
   ODD.FIRMA:=_firma;
   ODD.OD:='CENTRALA';
   ODD.N:='Główna jednostka księgowa zakładu';
   ODD.add()
?};
ODD.cntx_pop()


\pr_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: Formula przed redakcja pola WYDRUKIL._a
::  OLD: \pr_od/tr_slown.fml
::----------------------------------------------------------------------------------------------------------------------
_pod:=$('WYDRUKIL.'+_a+'_OD');
_pdo:=$('WYDRUKIL.'+_a+'_DO');
{? #_pod()<>0 & #_pdo()=0
|| _pdo():=_pod()
|| _pdo()
?}


\pr_do
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: Formula po redakcji pola WYDRUKIL._a
::  OLD: \pr_do/tr_slown.fml
::----------------------------------------------------------------------------------------------------------------------
_pod:=$('WYDRUKIL.'+_a+'_OD');
_pdo:=$('WYDRUKIL.'+_a+'_DO');
{? #_pod()=0 || _pod():=_pdo() ?};
1


\poobs_od
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: po redakcji obszaru
::  OLD: \poobs_od/wydruki.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld<>null
|| {? WYDRUKIL.OBS_DO=null
   || WYDRUKIL.OBS_DO:=WYDRUKIL.OBS_OD; 1
   || 1
   ?}
|| 1
?}


\rodzfak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: rodzaj faktury
::  OLD: \rodzfak/wydruki.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld<>'S' & fld<>'M' & fld<>'L' & fld<>'K' & fld<>'P'
||
   FUN.info('Dozwolone wartości: S,M,L,K i P.'@);0
|| 1
?}


\po_wyb_ul_wydr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Po wyborze ulicy
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld()+3;
{? (';_OD;_DO'*_fld)>1 || ($('WYDRUKIL.MI'+_fld))():=($('WYDRUKIL.UL'+_fld+'().MIA'))() ?};
1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 b1c5d43b0b8dd17f912aaf2d25dbc8aa130cec01c528e63f5501e1616f79937425b42e96014ac16f0b853d759a57f67db1c552e2454dc3aa117a9d5c94ca5519dbeab2e164ace5dc93b72d454c9eb9724042d3590412418468f02abfffdf4e2eb63c37dc75598769d0c502211f860d88abe245a7c69f8b205b8d5eb7e3db809f

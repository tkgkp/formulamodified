:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku : hb_getin.fml
:: Utworzony   : 2018
:: Autor       : PJ
:: Systemy     : HOMEBANK
::======================================================================================================================
:: Zawartosc  : Specjalna wersja formuły searchln dla banku Getin Noble
::======================================================================================================================


\searchln
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.51]
:: OPIS: Wyszukuje w pliku _a sekcje rozpoczynajaca sie znakami _b (po pominieciu wiodacych spacji)
::       i zwraca zawartosc sekcji do wiersza rozpoczynajacego sie znakami _c. Jesli nie podano _c,
::       zwraca jeden wiersz. Opcjonalny parametr d wskazuje znaki poczatkowe wiersza, ktory
::       stanowi naglowek wyciagu, co pozwala rozpoznac wiele wyciagow w jednym pliku.
::       Wersja dla banku Getin Noble - wiersze sekcji :86: są oddzielone znakiem |
::----------------------------------------------------------------------------------------------------------------------
_v:=1; _vline:=_vline2:='';
:: jezeli biezaca linia pliku odpowiada kryteriom _b, jest zapamietywana
{? var_pres('hbfline')>=0 & ((+_b)+hbfline=_b)
|| {? _>2 & +_c || _vline2:=hbfline+'|'; _v:=2 || _vline:=hbfline+'|'; _v:=0 ?}
?};
:: odczyt kolejnych linii
{! |? _v & (hbfline:=fread(_a))<>'\n' |!
   hbfline:=form(hbfline)+'|';
   {? _v=2 & (+_c)+hbfline<>_c || hbfline:=_vline2+hbfline; _v:=1 ?};
   {? (+_b)+hbfline=_b & _v<>2
   || _vline:=hbfline;
      {! |? _>2 & +_c & (hbfline:=fread(_a))<>'\n' & (+_c)+hbfline<>_c  & (_<4 | (_=4 & (+_d)+hbfline<>_d))
         |! _vline+=hbfline+'|'
      !}; _v:=0
   |? _v=2 || _vline:=_vline2; _v:=0
   |? _=4 & (+_d)+hbfline=_d || _v:=0
   ?}
!};
{? _=4 & (+_d)+hbfline=_d || HBP.first() ?};
{? hbfline='\n' || hbfeof:=1 ?};
{? _vline+1='|' || _vline:=_vline-1 ?};
_vline


\str2real
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.51]
:: OPIS: zamienia STRING na REAL
::   WE: _a - STRING do zamiany
::----------------------------------------------------------------------------------------------------------------------
{? _a*',' & _a*'.'
|| _a:=STR.gsub(_a,' ','');
   _a:=STR.gsub(_a,'.','');
   _a:=STR.gsub(_a,',','.')
|| _a:=STR.gsub(_a,' ','');
   _a:=STR.gsub(_a,',','.')
?};
#_a

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:52 e21b9e8001a2d07456e1ef18da8648371cd80f270897355b8dd0e1e23ab0e3e0e6f0b997d5477a88652d4832c4549619eb9e76386a5456570b5164d945b021fc1cf55fcb54e23399ad5d9eb72a93bd1aa3354cdf8472046dbed22533d28d0248f4552518fc6366daab56856bb2c8b79043a6e5723c955f2d67f58f92d2f026a0

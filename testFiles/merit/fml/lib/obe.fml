:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obe.fml
:: Utworzony: 02.06.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły obszaru OBE
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła inicjująca dla obszaru OBE
::----------------------------------------------------------------------------------------------------------------------
exec('RB','object');
exec('KOMM','#object');
exec('czytaj','#stalesys',,XINFO);
exec('add_grnr','numery','DOB');
exec('fl_decl','dekret_aut');
__PAR188:=PAR_SKID.get(188)='T';
~~


\obe_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Główna formuła obszaru roboczego OBE_ZAL - Zaliczki
::  OLD: \zaliczki/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init','obg',1);
zakr_zal:=1;
exec('typy_zal_noob','zaliczki');
EZAL.cntx_psh(); EZAL.win_edit('WPR'); EZAL.win_sel('WER_GR');
AreaTitle.setTabWin(EZAL,'WER_GR');
exec('zakr_zal','zaliczki',1);
EZAL.select();
EZAL.cntx_pop();
VAR_DEL.delete('zakr_zal');
~~


\obe_faw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Główna formuła obszaru roboczego OBE_FAW - Wnioski w obiegu - webterm
::----------------------------------------------------------------------------------------------------------------------
exec('env_wt','b_proces');
exec('FUN','#object');
exec('init_www','obg');
_auto_okr:=0;
_nowdate:=date();
OKRO.cntx_psh();
OKRO.index('NAZ');
OKRO.prefix(REF.FIRMA,);
{? OKRO.first()
|| {!|?
   {? (OKRO.ROK = (_nowdate~1)) & (_nowdate >= OKRO.DATA_OD & _nowdate <=OKRO.DATA_DO)
   || _auto_okr:=1
   ?};
   OKRO.next()
   !}
?};
OKRO.cntx_pop();
{? _auto_okr
|| exec('www_parses_auto','zws','rest_params_set_faw','obe','','',0,'W')
|| {? ~SSTALE.AR | ~SSTALE.AO
   || exec('www_parses','zws','rest_params_set_faw','obe','','',0)
   || exec('rest_params_set_faw','obe')
   ?}
?};
~~


\obe_fdl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Główna formuła obszaru roboczego OBE_FDL - Delegacje w obiegu
::----------------------------------------------------------------------------------------------------------------------
exec('env_wt','b_proces');
exec('FUN','#object');
exec('init_www_del','obe');
_auto_okr:=0;
_nowdate:=date();
OKRO.cntx_psh();
{? OKRO.first()
   || {!|?
      {? (OKRO.ROK = (_nowdate~1)) & (_nowdate >= OKRO.DATA_OD & _nowdate <=OKRO.DATA_DO)
      || _auto_okr:=1
      ?};
   OKRO.next()
   !}
?};
OKRO.cntx_pop();
{? _auto_okr
|| exec('www_parses_auto','zws','rest_params_set_fdl','obe','','',0,'D')
|| {? ~SSTALE.AR | ~SSTALE.AO
   || exec('www_parses','zws','rest_params_set_fdl','obe','','',0)
   || exec('rest_params_set_fdl','obe')
   ?}
?};
~~


\rest_params_set_faw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AB [19.42]
:: OPIS: Formuła uzupełniająca obszar roboczy - Wnioski w obiegu
::----------------------------------------------------------------------------------------------------------------------
_mas:={? SSTALE.AR
      || ($(SSTALE.AR().POCZ_ROK~1)+2)
      || '__'
      ?};
EDOKUMW.use('skidv1'+_mas);
exec('AreaTitle','#object');
AreaTitle.setArea('OBE_FAW');
_jedn:={? OPERATOR.DEPT
       || ', jednostka księgowa: '@+OPERATOR.DEPT().OD
       || ', wszystkie jednostki księgowe'@
       ?};
web_title('%1, %2: %3'[exec('nazwa','#system'),'Firma '@+REF.FIRMA().SYMBOL,AreaTitle.getTitle()+' rok: '@+SSTALE.AR().NAZ+_jedn],'all');
exec('maski_w','obiegi',0);
EDOKUMW.index('DISP'); EDOKUMW.prefix();
OBIEGI.CZY_TYP:='W'; OBIEGI.TYP:=null;
OBIEGI.CZY_ZAM:='N';
EDOKUM.prefix();
web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'typobi',2));
exec('zakres_dob1','obiegi','R','W','U','N',0);
exec('zakres_dob1','obiegi','P','W','U','N',0);
exec('zakres_dob1','obiegi','A','W','U','N',0);
EDOKUMW.web_select('WPRGW',,1);
web_ses_close()


\rest_params_set_fdl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AB [19.42]
:: OPIS: Formuła uzupełniająca obszar roboczy - Delegacje w obiegu
::----------------------------------------------------------------------------------------------------------------------
_mas:={? SSTALE.AR
      || ($(SSTALE.AR().POCZ_ROK~1)+2)
      || '__'
      ?};
EDOKUMW.use('skidv1'+_mas);
exec('AreaTitle','#object');
AreaTitle.setArea('OBE_FDL');
_jedn:={? OPERATOR.DEPT
       || ', jednostka księgowa: '@+OPERATOR.DEPT().OD
       || ', wszystkie jednostki księgowe'@
       ?};
web_title('%1, %2: %3'[exec('nazwa','#system'),'Firma '@+REF.FIRMA().SYMBOL,AreaTitle.getTitle()+' rok: '@+SSTALE.AR().NAZ+_jedn],'all');
exec('maski_w','obiegi',0);
EDOKUMW.index('DISP'); EDOKUMW.prefix();
typobi:=2;
zakr_wpr:=1;
zakr_pak:=1;
zakr_mer:=1;
wla_dok:=wla_dok1:=wla_dok2:=1;
OBIEGI.CZY_TYP:='W'; OBIEGI.TYP:=null;
OBIEGI.CZY_ZAM:='N';
web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'typobi',3));
exec('zakres_dob1','obiegi','R','D','U','N',0);
exec('zakres_dob1','obiegi','P','D','U','N',0);
exec('zakres_dob1','obiegi','A','D','U','N',0);
_grey:=Plugin.run('ACT_GREY_EDOKUM_001',OPERATOR.USER,'EDOKUMW','WPRGD');
{? type_of(_grey)=type_of('') & _grey<>''
|| STR.split(_grey,',');
   EDOKUMW.web_win_init('WPRGD',,'grayed='+STR.get_word())
?};
EDOKUMW.web_select('WPRGD',,1);
web_ses_close()


\init_www
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła inicjująca dla obszaru OBG - WWW
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.TYP_ID:='T';
TYPOBIEG.cntx_psh(); TYPOBIEG.index('UNIK');
TYPOBIEG.prefix('Obieg wniosków','Obieg wniosków');
OBIEGI.TYPOBIEG:={? TYPOBIEG.first() || TYPOBIEG.ref() || null ?};
TYPOBIEG.cntx_pop();
OPERATOR.USER:={? exec('getUser','users',app_info('web_user')) || USERS.ref() || null ?};
~~


\init_www_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła inicjująca dla obszaru OBE - WWW (Delegacje)
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.TYP_ID:='T';
TYPOBIEG.cntx_psh(); TYPOBIEG.index('UNIK');
TYPOBIEG.prefix('Obieg delegacji','Obieg delegacji');
OBIEGI.TYPOBIEG:={? TYPOBIEG.first() || TYPOBIEG.ref() || null ?};
TYPOBIEG.cntx_pop();
typobi:=3;
{? var_pres('zakr_wpr')<=0 || zakr_wpr:=1 ?};
{? var_pres('zakr_pak')<=0 || zakr_pak:=1 ?};
{? var_pres('zakr_mer')<=0 || zakr_mer:=1 ?};
{? var_pres('wla_dok')<=0 || wla_dok:=1 ?};
{? var_pres('wla_dok1')<=0 || wla_dok1:=1 ?};
{? var_pres('wla_dok2')<=0 || wla_dok2:=1 ?};
OPERATOR.USER:={? exec('getUser','users',app_info('web_user')) || USERS.ref() || null ?};
~~


\chk_deleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Kontrola poprawnosci stalych w oknie RED_DEL zmiennej XINFO
::  OLD: \chk_deleg/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
{? XINFO.DIETA<0
|| FUN.emsg('Stawka diety nie może być mniejsza od zera.'@);
   _wy:='DIETA'
?};
{? _wy='' & XINFO.RYCZ_NOC<0
|| FUN.emsg('Stawka diety nie może być mniejsza od zera.'@);
   _wy:='RYCZ_NOC'
?};
{? _wy='' & XINFO.RYCZ_KOM<0
|| FUN.emsg('Stawka diety nie może być mniejsza od zera.'@);
   _wy:='DOD_KOM'
?};
{? _wy='' & (XINFO.SNIAD<0 | XINFO.SNIAD>100)
|| FUN.emsg('Procent zmniejszenia diety powinien zawierać się w przedziale 0-100.'@);
   _wy:='SNIAD'
?};
{? _wy='' & (XINFO.OBIAD<0 | XINFO.OBIAD>100)
|| FUN.emsg('Procent zmniejszenia diety powinien zawierać się w przedziale 0-100.'@);
   _wy:='OBIAD'
?};
{? _wy='' & (XINFO.KOLACJA<0 | XINFO.KOLACJA>100)
|| FUN.emsg('Procent zmniejszenia diety powinien zawierać się w przedziale 0-100.'@);
   _wy:='KOLACJA'
?};
{? _wy='' & (XINFO.SNIAD+XINFO.OBIAD+XINFO.KOLACJA)>100
|| FUN.emsg('Procent zmniejszenia za śniadanie, obiad,\n kolację nie może być w sumie wiekszy od 100.'@);
   _wy:='SNIAD'
?};
_wy


\doltwyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Dolaczanie typow wydatkow w delegacjach
::  OLD: \doltwyd/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPWYD.blank();
{? ETYPWYD.edit("exec('spr_etypwyd','obe')") || ETYPWYD.add() ?}


\spr_etypwyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja pola ETYPWYD.ED_FULL
::  OLD: \spr_etypwyd/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? ETYPWYD.NAZWA=''
|| FUN.info('Nie wprowadzono nazwy typu wydatku.'@); _zwrot:='NAZWA'
|| {? -menu_txt='popraw'@ || _ref:=ETYPWYD.ref() ?};
   ETYPWYD.cntx_psh();
   ETYPWYD.index('UNIK'); ETYPWYD.prefix(ETYPWYD.NAZWA,);
   {? ETYPWYD.first() & {? -menu_txt='popraw'@ || ETYPWYD.ref()<>_ref || 1 ?}
   || FUN.info('Istnieje już typ wydatku o podanej nazwie.'@); _zwrot:='NAZWA'
   ?};
   ETYPWYD.cntx_pop()
?};
_zwrot


\poptwyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Poprawianie typow wydatkow w delegacjach
::  OLD: \poptwyd/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPWYD.edit("exec('spr_etypwyd','obe')") || ETYPWYD.put() ?}


\usutwyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Usuwanie typow wydatkow w delegacjach
::  OLD: \usutwyd/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? ETYPWYD.count()>0
|| FUN.info('Typ wydatku wykorzystywany w danych.'@)
|? FUN.ask('Usunąć typ wydatku?'@)
|| ETYPWYD.del()
?}


\bed_ed_full
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja pola ETYPWYD.ED_FULL
::  OLD: \bed_ed_full/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? -menu_txt()='popraw'@
|| ETYPWYD.cntx_psh();
   _zwrot:=(ETYPWYD.count()=0);
   ETYPWYD.cntx_pop()
?};
_zwrot


\edk_zal_rwyd_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.30]
:: OPIS: Przed redakcja EDOK_ZAL.RWYD
::   WY: 1/0 - mozna/nie mozna edytowac
::  OLD: \edk_zal_rwyd_be/delegdan.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('get_rwyd_slu','obe')<>null() || 1 || 0 ?}


\get_rwyd_slu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.30]
:: OPIS: Ustawienie SLU na slownik rodzajow wydatkow
::   WY: SLU.ref()
::  OLD: \get_rwyd_slu/delegdan.fml
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SLU_WYD<>null() || SLU.prefix(); SLU.seek(XINFO.SLU_WYD) ?};
XINFO.SLU_WYD


\rpo_rkwotdel_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Rekord po dla tabeli SKL_PLD
::  OLD: \rpo_rkwotdel_r/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
{? SKL_PLD.RKWOT=null
|| FUN.info('Nie wybrano rodzaju kwoty.'@); _zwrot:='RKWOT'
?};
{? _zwrot=''
|| {? -menu_txt='popraw'@ || _ref:=SKL_PLD.ref() ?};
   SKL_PLD.cntx_psh();
   SKL_PLD.index('UNIK'); SKL_PLD.prefix(SKL_PLD.RKWOT);
   {? SKL_PLD.first() & {? -menu_txt='popraw'@ || SKL_PLD.ref()<>_ref || 1 ?}
   || FUN.info('Istnieje już definicja dla wybranego rodzaju kwoty.'@); _zwrot:='RKWOT'
   ?};
   SKL_PLD.cntx_pop()
?};
{? _zwrot='' & SKL_PLD.R=null
|| FUN.info('Nie wybrano rubryki.'@); _zwrot:='R'
?};
_zwrot


\berkwotdel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja pola FORM.RKWOTDEL i SKL_PLD.RKWOT
::  OLD: \berkwotdel/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
SLO.win_dict('ONE');SLO.win_sel('ONE');
SLU.index('NAZ'); SLU.prefix('~KWOTY W DELEGACJACH'); SLU.first();
1


\aerkwotdel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Po redakcji pola FORM.RKWOTDEL
::  OLD: \aerkwotdel/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? FORM.RKWOTDEL=null
|| FUN.info('Nie wybrano rodzaju kwoty.'@); 0
|| FORM.RKWOTDEL();
   {? ~(SLO.KOD='WDN' | SLO.KOD='WDB') || FORM.ETYPWYD:=null ?};
   win_disp(); 1
?}


\bdstypwyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed wyswietl dla pola FORM.ETYPWYD
::  OLD: \bdstypwyd/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
{? FORM.RKWOTDEL=null | ~(FORM.RKWOTDEL().KOD='WDN' | SLO.KOD='WDB')
|| exec('findfnv','#color')
|| 1
?}


\beetypwyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja pola FORM.ETYPWYD
::  OLD: \beetypwyd/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
ETYPWYD.win_dict('SLO');
FORM.RKWOTDEL<>null & (FORM.RKWOTDEL().KOD='WDN' | SLO.KOD='WDB')


\be_rkwotdel_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Przed redakcja pola SKL_PLD.R
::  OLD: \be_rkwotdel_r/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.KLASA:='D'; 1


\edokum_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WW [12.30]
:: OPIS: Ref biezacego rekordu EDOKUM
::   WY: EDOKUM.ref()
::  OLD: \edokum_ref/deleg_at.fml
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.ref()


\pw_del_etat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JSz [12.30]
:: OPIS: Przed wyświetleniem zmiennej OBIEGI.DEL_ETAT
::  OLD: \pw_del_etat/skid_dob.fml
::----------------------------------------------------------------------------------------------------------------------
_data:=EDOKUM.DW; OBIEGI.DEL_ETAT:='';
{? EDOKUM.REFPRAC<>'' & _data<>date(0,0,0)
|| P.cntx_psh(); P.use('pracowni'); P.prefix();
   H.cntx_psh(); H.use('_hist'); H.index('_HISTKOD');
   {? P.seek(BB.sqlint(EDOKUM.REFPRAC),'pracowni')
   || _ok:=0;
      H.prefix(P.ref(),'Z');
      {? H.first()
      || {! |?
            {? _data>=H.OD & (_data<=H.DO | H.DO=date(0,0,0))
            || _ok:=1;
               OBIEGI.DEL_ETAT:=form(P.T+
                                 {? H.WYDZIAL || ' - '+H.WYDZIAL().SYMBOL || ''?}+
                                 {? H.ST ||  ' - '+H.ST().ST || ''?})
            ?};
            ~_ok & H.next()
         !}
      ?}
   ?};
   P.cntx_pop();
   H.cntx_pop()
?};
OBIEGI.DEL_ETAT


\obe_fad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Obszar roboczy OBE_FAD - Wnioski w obiegu - jTerm
::----------------------------------------------------------------------------------------------------------------------
BPMN.AREA:='OBE_FAD';
TYPOBIEG.cntx_psh(); TYPOBIEG.index('UNIK');
TYPOBIEG.prefix('Obieg wniosków','Obieg wniosków');
OBIEGI.TYPOBIEG:={? TYPOBIEG.first() || TYPOBIEG.ref() || null ?};
TYPOBIEG.cntx_pop();
VAR_DEL.delete('OBEWin');
OBEWin:=obj_new('rwin','pwin','awin','zakres');
OBEWin.rwin:=exec('win','obe_faw','R');
OBEWin.pwin:=exec('win','obe_faw','P');
OBEWin.awin:=exec('win','obe_faw','A');
OBEWin.zakres:=obj_new(5);
{! _ii:=1..obj_len(OBEWin.zakres) |! OBEWin.zakres[_ii]:=1 !};
::OBEWin.rwin:='WWPR';
::OBEWin.pwin:='WPRZ';
::OBEWin.awin:='WAKC';
typobi:=2;
zakr_wpr:=4;
zakr_pak:=1;
zakr_mer:=1;
zakr_ks:=1;
zakres:=1;
wla_dok:=wla_dok1:=wla_dok2:=1;
dla_kh:=0;
dek_fak:=_;
:: Ustawienie prezycji pól w oknach
exec('fld_prec','win_ust');

_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)
|| _link:=_params.LINK
|| _link:=''
?};

exec('faktury_sel','obiegi',_,_link)


\obe_fdd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Obszar roboczy OBE_FDD - Delegacje w obiegu - dekretowanie
::----------------------------------------------------------------------------------------------------------------------
TYPOBIEG.cntx_psh(); TYPOBIEG.index('UNIK');
TYPOBIEG.prefix('Obieg delegacji','Obieg delegacji');
OBIEGI.TYPOBIEG:={? TYPOBIEG.first() || TYPOBIEG.ref() || null ?};
TYPOBIEG.cntx_pop();
typobi:=3;
zakr_wpr:=1;
zakr_pak:=1;
zakr_mer:=1;
zakr_ks:=1;
zakres:=1;
wla_dok:=wla_dok1:=wla_dok2:=1;
dla_kh:=0;
dek_fak:=_;
exec('faktury_sel','obiegi',_)


\del_print
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Wydruki delegacji
::   WE: 'W' - wprowadzanie, 'P' - przekazanie, 'A' - Akceptacja
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
||
{? _=0 || _a:='W' ?};
{? EDOKUMW.get()
|| EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
   EDOKUM.prefix(); EDOKUMW.EDOKUM();
   exec('set_parw','param_wydr');
   exec('czytaj','#stalesys',,FINFO);
   web_params_set('edokum_ref',$EDOKUM.ref());
   EDOKUM.web_cntx_save();
   web_rep_exec('obe_del*','Wydruki delegacji'@)
|| FUN.info('Nie znaleziono delegacji.'@);
   exec('dob_refresh','obiegi',_a,'D')
?}
|| exec('rep_exec','#b_report','','obe_del*','Wydruki delegacji'@)
?}

:: START: obsługa pól check-box w oknie wertowania EDOKUMW dla delegacji - na razie nie działa ze względu
:: na brak obsługoi akcji po okienku wertowania - nie ma jak odświeżyć okna pod spodem - pola zostały usunięte,
:: ale wrócą w przyszłości


\wnio_print
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [17.00]
:: OPIS: Wydruki wniosku
::   WE: 'W' - wprowadzanie, 'P' - przekazanie, 'A' - Akceptacja
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
||
   {? _=0 || _a:='W' ?};
   {? EDOKUMW.get()
   || EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
      EDOKUM.prefix(); EDOKUMW.EDOKUM();
      exec('set_parw','param_wydr');
      exec('czytaj','#stalesys',,FINFO);
      web_params_set('edokum_ref',$EDOKUM.ref());
      EDOKUM.web_cntx_save();
      web_rep_exec('obe_dok*','Wydruki wniosków'@)
   || FUN.info('Nie znaleziono wniosku.'@);
      exec('dob_refresh','obiegi',_a,'W')
   ?}
|| exec('rep_exec','#b_report','','obe_dok*','Wydruki wniosków'@,,,,,,,'W')
?}


\fakt_print
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [17.00]
:: OPIS: Wydruki faktury
::   WE: 'W' - wprowadzanie, 'P' - przekazanie, 'A' - Akceptacja
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
||
{? _=0 || _a:='W' ?};
{? EDOKUMW.get()
|| EDOKUM.use('skid_v'+(EDOKUMW.name()+2));
   EDOKUM.prefix(); EDOKUMW.EDOKUM();
   exec('set_parw','param_wydr');
   exec('czytaj','#stalesys',,FINFO);
   web_params_set('edokum_ref',$EDOKUM.ref());
   EDOKUM.web_cntx_save();
   web_rep_exec('obe_dok*','Wydruki faktury'@)
|| FUN.info('Nie znaleziono faktury.'@);
   exec('dob_refresh','obiegi',_a,'F')
?}
|| exec('rep_exec','#b_report','','obe_dok*','Wydruki faktury'@)
?}


\obe_zest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [17.00]
:: OPIS: Zestawienia dokumentow w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? app_info('web_sesid')<>''
|| exec('set_parw','param_wydr');
   exec('czytaj','#stalesys',,FINFO);
   EDOKUMW.web_cntx_save();
   PAR_WYDR.DATA_DO:=PAR_WYDR.DATA_OD:=date();
   PAR_WYDR.web_edit('WYDR_DAT',,,,"{? _a='OK'
                                    || web_params_set('data_od',PAR_WYDR.DATA_OD,'data_do',PAR_WYDR.DATA_DO);
                                       EDOKUMW.web_cntx_load(); EDOKUMW.web_cntx_save();
                                       web_rep_exec('obe_zest*','Zestawienie dokumentów'@)
                                    || PAR_WYDR.web_eclose()
                                    ?}")
|| exec('rep_exec','#b_report','OBX_XXX_XXXX','obe_zesj*','Zestawienie dokumentów'@,0,,,,,,'W')
?}


\edokum_atr_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Czy są informacje dodatkowe dla delegacji
::   WE: [_a] - EDOKUM.ref, jeśli brak to bieżący
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
{? _>0 & type_of(_a)=type_of(null) || EDOKUM.seek(_a) ?};
EDOK_ATR.cntx_psh();
{? (EDOK_ATR.name(0)='') | (EDOK_ATR.name(0)+3)<>('t'+(EDOKUM.name(0)+2))
|| EDOK_ATR.use((EDOK_ATR.name(1)-3)+'t'+(EDOKUM.name(0)+2))
?};
EDOK_ATR.index('REKKOLED');
EDOK_ATR.prefix(EDOKUM.ref());
{? EDOK_ATR.first() || _wy:=1 ?};
EDOK_ATR.cntx_pop();
_wy


\edokum_zal_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Czy są załączniki dla delegacji
::   WE: [_a] - EDOKUM.ref, jeśli brak to bieżący
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
EDOKUM.cntx_psh(); EDOKUM.index('ID'); EDOKUM.prefix();
{? _>0 & type_of(_a)=type_of(null) || EDOKUM.seek(_a) ?};
EDOKUMZ.cntx_psh();
{? EDOKUMZ.name()<>'skid_n'+(EDOKUM.name()+2)
|| EDOKUMZ.use('skid_n'+(EDOKUMW.name()+2))
?};
EDOKUMZ.index('DISP');
EDOKUMZ.prefix(EDOKUM.ref());
{? EDOKUMZ.first() || _wy:=1 ?};
EDOKUMZ.cntx_pop();
EDOKUM.cntx_pop();
_wy


\edokum_pod_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Czy są podziały dla delegacji
::   WE: [_a] - EDOKUM.ref, jeśli brak to bieżący
::----------------------------------------------------------------------------------------------------------------------
_wy:=0;
EDOKUM.cntx_psh(); EDOKUM.index('ID'); EDOKUM.prefix();
{? _>0 & type_of(_a)=type_of(null) || EDOKUM.seek(_a) ?};
EPODZ.cntx_psh();
EPODZ.use('skid_j'+(EDOKUM.name()+2));
EPODZ.index('EDOKUMDS');
EPODZ.prefix(EDOKUM.ref());
{? EPODZ.first() || _wy:=1 ?};
EPODZ.cntx_pop();
EDOKUM.cntx_pop();
_wy

:: STOP: obsługa pól check-box w oknie wertowania EDOKUMW dla delegacji


\etypy_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Wskazanie na typ dokumentu w obiegu
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.TYP


\obe_fap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Obszar roboczy OBE_FAP - Wnioski w obiegu - techniczne - HR Portal
::----------------------------------------------------------------------------------------------------------------------
exec('EDOKUM_sel','portal_wnioski')


\dok_spac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Funkcja do wyświetlania wniosku z tabeli EDOKUM
::   WE: [_a] - wskazanie na wniosek w obiegu (EDOKUM.ref)
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? var_press('_a')>=0
|| _ar:=SSTALE.AR;
   _ao:=SSTALE.AO;
   {? type_of(_a)=7
   || EDOKUM.cntx_psh();
      _ref:=($_a)+16;
      EDOKUM.use(8+_ref);
      EDOKUM.prefix();
      _ok:=EDOKUM.seek(_a);
      {? _ok
      || OKRO_F.cntx_psh();
         OKRO_F.index('ROK'); OKRO_F.prefix(EDOKUM.ROK_F().ref());
         {? OKRO_F.first()
         || SSTALE.AR:=EDOKUM.ROK_F;
            SSTALE.AO:=OKRO_F.ref()
         || _ok:=0
         ?};
         OKRO_F.cntx_pop()
      ?}
   || _ok:=0
   ?}
?};
{? _ok
|| exec('open_tabele','open_tab','F');
   exec('akc_display','obe_faw');
   SSTALE.AR:=_ar;
   SSTALE.AO:=_ao;
   exec('open_tabele','open_tab','F')
|| FUN.info('Nie znaleziono wniosku w obiegu.'@)
?};
{? var_press('_a')>0 & type_of(_a)=7
|| EDOKUM.cntx_pop()
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 54cec2183fa7fb2d11681e1501cec9f9006f02d25b750479925e8716326148d5dd60cebbea8ccb2a5e767bc50507716707090e6c3930b5548afd2684c98d54ee592e9284acf3e247c9f55047babd13b2b4416c6fab955e757b70b21bb496ae45283f615928b9a22af7653cfdb2605c7f044e08e7dec14f18f54e90d7df2be5f1

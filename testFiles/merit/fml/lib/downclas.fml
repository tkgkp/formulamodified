:!UTF-8
::(c) Macrologic S.A. Wszelkie prawa zastrzezone
::====================================================================================================
:: Nazwa pliku: downclas.fml [12.30]
:: Utworzony: 28.11.2012
:: Autor: TN
:: ====================================================================================================
:: Zawartosc: Klasa obslugujaca pobierania tabeli kursowej NBP w ALERT-cie (aplikacja download.jar)
::====================================================================================================

\download_class
{? var_pres('DOWNLOAD', @.CLASS) >= 0 || return ?};

obj_decl('DOWNLOAD',

obj_fld('JarPath', ''),
obj_fld('ProxyAddress', ''),
obj_fld('ProxyPort', 0),
obj_fld('ProxyUser', ''),
obj_fld('ProxyPassword', ''),

obj_meth('__init',
"
  choice('Inicjalizacja obiektu wymaga podania katalogu z biblioteką download.jar', 'Download.jar', 'ERROR');
  return

", -1),

obj_meth('__init',
"
  {? fexists(_b + '/download.jar') <> 1 ||
    choice('Nie można znaleźć biblioteki download.jar w lokalizacji ' + _b, 'Download.jar', 'ERROR');
    return
  ?};

  .JarPath := _b + '/download.jar';
  return

", type_of(''), -1),

obj_meth('SetProxy', ".SetProxy(_a, _b, '', '')", type_of(''), type_of(0), -1),
obj_meth('SetProxy',
"
  .ProxyAddress := _a;
  .ProxyPort := _b;
  .ProxyUser := _c;
  .ProxyPassword := _d;

  return

", type_of(''), type_of(0), type_of(''), type_of(''), -1),

:: _a - adres pliku do pobrania protokół http lub https
:: _b - ścieżka do zapisania pliku
obj_meth('DownToFile',
"
  _ret:='';
  {? _=0 | type_of(_a)<>type_of('') | _a=''
  || _ret:='[ERR] Nie podano adresu pliku do pobrania'
  ?};

  {? _ret=''
  || _ht:= inet_get(_a);
     _ht.set_opt('SSL_VERIFYPEER',0);
     _status:=_ht.http_get();
     {? _status=200
     || _data:=_ht.get_data();
        _f:=fopen(_b,'bw',1);
        {? _f
        || fwrite(_f,_data);
           fclose(_f)
        || _ret:='[ERR] Błąd przy zapisywaniu pliku.'
        ?}
     || _ret:='[ERR] ' + $_status
     ?}
  ?};

  return(_ret)

", type_of(''), type_of(''), -1),

:: _a - adres pliku do pobrania protokół http lub https
obj_meth('DownToString',
"
  _ret:='';

  _ret:='';
  {? _=0 | type_of(_a)<>type_of('') | _a=''
  || _ret:='[ERR] Nie podano adresu pliku do pobrania'
  ?};

  {? _ret=''
  || _ht:= inet_get(_a);
     _ht.set_opt('SSL_VERIFYPEER',0);
     _status:=_ht.http_get();
     {? _status=200
     || _ret:=_ht.get_data()
     || _ret:='[ERR] ' + $_status
     ?}
  ?};

  return(_ret)

", type_of(''), -1),


::private
obj_meth('__cmdLine',
" {? sys_name(1)='U_LINUX'
  || _cmd:=exe_dir(1)+'/MacroJRUN -wait -java -Djava.net.preferIPv4Stack=true -jar '+.JarPath+' '+_a+' ""'+_b+'""'
  || _cmd:=exe_dir(1)+'/mjrunw.exe -wait -java -Djava.net.preferIPv4Stack=true -jar '+.JarPath+' '+_a+' ""'+_b+'""'
  ?};
  {? .ProxyAddress<>''
  || _cmd+=' '+.ProxyAddress+' '+$.ProxyPort;
    {? .ProxyUser<>''
    || _cmd+=' ""'+.ProxyUser+'"" ""'+.ProxyPassword+'""'
    ?}
  ?};
  return(_cmd)
"),

obj_meth('__ret2string',
"
  _ret := '';
  {? _a = 1 ||  _ret := '[ERR] REQUIRED MacroLang.jar IS MISSING'
  |? _a = 11 || _ret := '[ERR] Nie podano adresu pliku do pobrania'
  |? _a = 12 || _ret := '[ERR] Podano niepoprawny adres!'
  |? _a = 21 || _ret := '[ERR] Nie podano scieżki docelowej do zapisania pobranego pliku!'
  |? _a = 22 || _ret := '[ERR] Błąd utworzenia pliku: '
  |? _a = 31 || _ret := '[ERR] JAVA_DOWNLOAD_BAD_PROXY_SETTINGS'
  |? _a = 41 || _ret := '[ERR] Proces pobierania nieudany!'
  |? _a <> 0 || _ret := '[ERR] Nieznany błąd: ' + $_a
  ?};

  return(_ret)
"),

)

:Sign Version 2.0 jowisz:1045 2020/04/03 17:16:35 15da265bf3fb30e521b57352c0a9d45a93065b6a74b4aff49b91b157a1597f5064cadfc2afafdd3fc5efe2ed87b873fbc1dbe70b68d5f38ea76e9e9a190bf89690ef89a1e4ccf1b3dec445470202090bf3755ca24b93b2bf61119e30c33c4a3f4939f043335e73551737cebf1240efd6996f0d45a6e79bcadb84e86af8ed7359

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: rt_lib.fml [20.42]
:: Utworzony: 15.05.2015
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły do obsługi rejestru tłumaczeń.
::======================================================================================================================


\tlumacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Zamienia tekst oryginalny na tekst w podanym języku.
::   WE: _a STRING - tekst oryginalny
::       _b STRING - kod języka, na który ma być przetłumaczony tekst
::       _c TABLE - alias tabeli, z której pochodzi tekst
::       _d STRING - akronim kolumny, z której pochodzi tekst
::       _e [STRING] - wartość dyskryminatora
::   WY: przetłumaczony tekst lub tekst oryginalny gdy nie znaleziono odpowiednika lub wywołano nieprawidłowo
::----------------------------------------------------------------------------------------------------------------------
:: weryfikacja wywołania
_err:="FUN.emsg('Błędny argument %1 wywołania formuły \"tlumacz\" z pliku \"rt_lib.fml\".\n%2'@[_a,_b]); _c";

{? var_pres('_a')<>type_of('')
|| return(_err('_a','Oczekiwany tekst do tłumaczenia.',''))

|? var_pres('_b')<>type_of('') | form(_b)=''
|| return(_err('_b','Oczekiwany kod języka obcego.',_a))

|? var_pres('_c')<>type_of(SYSLOG)
|| return(_err('_c','Oczekiwany alias tabeli.',_a))

|? var_pres('_d')<>type_of('') | form(_d)=''
|| return(_err('_d','Oczekiwany akronim kolumny.',_a))

|? var_pres('_e')>type_of(~~) & var_pres('_e')<>type_of('')
|| return(_err('_e','Oczekiwana wartość dyskryminatora.',_a))
?};

:: mapa argumentów
_org:=_a;
_lng:=_b;
_tab:=2-!_c;
_fld:=_d;
_pfx:={? var_pres('_e')=type_of('') & form(_e)<>'' || _e || '' ?};

_ret:=_org;
RT_TEKST.cntx_psh();
RT_TEKST.index('DEF');
RT_TEKST.prefix(_tab,_fld,_pfx,_lng,);
{? RT_TEKST.find_key(_org,) & RT_TEKST.OBCY<>''
|| _ret:=RT_TEKST.OBCY
?};
RT_TEKST.cntx_pop();
_ret


\aktualizuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Aktualizuje zawartość rejestru tłumaczeń.
::   WE: _a [RT_KONF] - wskazanie wiersza w tabeli RT_KONF (ustawienie)
::       _b [SLO_KOD] - wskazanie wiersza w tabeli SLO_KOD (język obcy)
::       _c [INTEGER] - wyświetlaj postęp operacji jeśli liczba różna od 0
::   WY: wskazanie tablicy nazwanej o strukturze:
::       STATUS - wynik operacji (0/1 - błąd/sukces)
::       INFO - opis wyniku operacji
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_cfg:=null;
_lng:=null;
_prc:=(var_pres('_c')=type_of(0) & _c<>0);

:: wartość zwracana (domyślnie "sukces")
_val:=obj_new('STATUS','INFO','message','error');
_val.STATUS:=1;
_val.INFO:='';
_val.message:="
   {? var_pres('_b')<>type_of(0) | _b<>0
   || RT_TEKST.cntx_pop();
      RT_JEZYK.cntx_pop();
      RT_JEZYK.f_clear();
      RT_KONF.cntx_pop();
      RT_KONF.f_clear();
      ~~
   ?};
   .INFO:=_a;
   return(.)
";
_val.error:="
   .STATUS:=0;
   .message(_a,{? var_pres('_b')=type_of(0) || _b || ~~ ?})
";

:: weryfikuj wskazanie ustawień
{? var_pres('_a')=type_of(null)
|| {? _a<>null & ref_tab(_a)<>RT_KONF
   || return(_val.error('Błędne wskazanie ustawienia rejestru.'@,0))
   ?};
   _cfg:=_a
?};

:: weryfikuj wskazanie języka
{? var_pres('_b')=type_of(null)
|| {? _b<>null & ref_tab(_b)<>SLO_KOD
   || return(_val.error('Błędne wskazanie języka obcego.'@,0))
   ?};
   _lng:=_b
?};

RT_KONF.cntx_psh();
RT_KONF.f_clear();
RT_KONF.clear();
:: ustal zakres przetwarzania ustawień
RT_KONF.f_set('TABELA,POLE,WARTOSC',,{? _cfg<>null || 'REFERENCE=:_a' || '' ?},_cfg);

RT_JEZYK.cntx_psh();
RT_JEZYK.f_clear();
RT_JEZYK.clear();
:: ustal zakres przetwarzania języków
RT_JEZYK.f_set('SLO_KOD(KOD)',,{? _lng<>null || 'SLO_KOD=:_a' || '' ?},_lng);

RT_TEKST.cntx_psh();
RT_TEKST.index('UNIQUE');

{? RT_KONF.f_size()=0  || return(_val.message('Nie znaleziono ustawień rejestru tłumaczeń.'@))
|? RT_JEZYK.f_size()=0 || return(_val.message('Nie znaleziono języka tłumaczeń.'@))
?};

_size:=RT_KONF.f_size()*RT_JEZYK.f_size();
_count:=0;

:: przeglądaj ustawienia
_loop:=RT_KONF.f_first();
_stop:=0;
{!
|? _loop & ~_stop
|! _get:=$('%1.%2'[RT_KONF.TABELA,RT_KONF.POLE]);
   _cnd:="1";
   {? (_chk:=exec('rt_konf_pole_chk','rt_lib'))<>''
::    kolumna nie istnieje lub jest błędnego typu
   || return(_val.error(_chk))
   ?};
   {? form(RT_KONF.WARTOSC)<>''
::    testuj formułę dyskryminatora
   || {? (_chk:=exec('rt_konf_formula_chk','rt_lib',1))<>''
::       wykryto błąd składniowy formuły
      || return(_val.error(_chk))
      ?};
      _cnd:=$('(%1)=_a'[RT_KONF.memo_txt(,1,'FORMULA')])
   ?};
   _TAB:=($RT_KONF.TABELA)();
   _TAB.cntx_psh();
   _TAB.prefix();
:: przeglądaj języki
   _loop:=RT_JEZYK.f_first();
   {!
   |? _loop & ~_stop
   |! _loop:=_TAB.first();
      {!
      |? _loop
      |! {? _cnd(RT_KONF.WARTOSC)
         || _org:=_get();
            {? _org<>''
            || RT_TEKST.prefix(RT_KONF.ref(),RT_JEZYK.SLO_KOD);
               {? RT_TEKST.find_tab(0,'ORYGINAL',,'=',_org)
               || {? RT_TEKST.REKORD=''
                  || RT_TEKST.REKORD:=$_TAB.ref();
                     RT_TEKST.put()
                  ?}
               || RT_TEKST.blank();
                  RT_TEKST.RT_KONF:=RT_KONF.ref();
                  RT_TEKST.SLO_KOD:=RT_JEZYK.SLO_KOD;
                  RT_TEKST.ORYGINAL:=_org;
                  RT_TEKST.REKORD:=$_TAB.ref();
                  RT_TEKST.add()
               ?}
            ?}
         ?};
         _loop:=_TAB.next()
      !};
      {? _prc<>0
      || _count+=1;
         {? progress(100*_count/_size,
               'Proszę czekać. Trwa aktualizacja rejestru tłumaczeń...'@,FUN.TYT,,,,,
               'Anuluj'@
            )=0
         || _stop:=1
         ?}
      ?};
      _loop:=RT_JEZYK.f_next()
   !};
   _TAB.cntx_pop();
   obj_del(_TAB);
   _loop:=RT_KONF.f_next()
!};
prgs_clr();

_val.message('Zaktualizowano zawartość rejestru tłumaczeń.'@)


\rt_konf_tabela_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Sprawdza, czy wartość RT_KONF.TABELA jest akronimem tabeli.
::   WE:
::   WY: treść komunikatu w przypadku błędu, wpp tekst pusty
::----------------------------------------------------------------------------------------------------------------------
{? var_pres(RT_KONF.TABELA)<>type_of(SYSLOG) | ($('%1.name(1)'[RT_KONF.TABELA]))()=''
|| return('"%1" nie jest akronimem tabeli.'@[RT_KONF.TABELA])
?};

''


\rt_konf_pole_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Sprawdza, czy wartość RT_KONF.POLE jest akronimem pola tabeli.
::   WE:
::   WY: treść komunikatu w przypadku błędu, wpp tekst pusty
::----------------------------------------------------------------------------------------------------------------------
{? (_chk:=exec('rt_konf_tabela_chk','rt_lib'))<>''
:: tabela nie istnieje
|| return(_chk)

|? var_pres(RT_KONF.POLE,($RT_KONF.TABELA)())<0
:: kolumna o podanym akronimie nie występuje w tabeli
|| return('"%1" nie występuje w tabeli "%2".'@[RT_KONF.POLE,RT_KONF.TABELA])

|? var_pres(RT_KONF.POLE,($RT_KONF.TABELA)())<>27
:: podany akronim nie identyfikuje kolumny typu napisowego
|| return('"%1" nie jest typu napisowego.'@[RT_KONF.POLE])
?};

''


\rt_konf_formula_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.42]
:: OPIS: Sprawdza składnię formuły w RT_KONF.FORMULA.
::   WE: _a INTEGER - 0/1 pobierz treść formuły z bufora/tabeli
::   WY: treść komunikatu w przypadku błędu, wpp tekst pusty
::----------------------------------------------------------------------------------------------------------------------
_fml:=RT_KONF.memo_txt(,_a,'FORMULA');
{? form_chk($_fml).first()=0
|| return('')
?};

'Formuła:\n%1\nzawiera błędy składniowe.'@[_fml]

:Sign Version 2.0 jowisz:1048 2020/10/16 15:22:57 013abe59f2cb2199009da4122d37efe02309168b6e4290e62c5ec9afa0da9f62eb37522366c3824151908301040e2f3cef5d78f16f609ee30b1df4581174eea0df62ba7a5e5fd6a05e69ec4085bf7b3fec0b019f3e58ed2948d1cabae045315d13b02666ece9bea53bfab123d003dec4c281f5350044590c15a8e8f42744ee7f

:!UTF-8
::(c) Macrologic S.A. Wszelkie prawa zastrzezone
::====================================================================================================
:: Nazwa pliku: proxy.fml
:: Utworzony: 13.11.2012
:: Autor: PJ
:: Systemy: HOMEBANK,ALERT
:: ===================================================================================================
:: Zawartosc: Procedury zwiazane z obsluga proxy podczas wykorzystywania biblioteki download.jar
::            do pobierania plikow
::====================================================================================================


\be_host
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed redagowaniem pola PROXYSET.HOST
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.CZY='T' || 1 || 0 ?}


\bs_host
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed wyswietleniem pola PROXYSET.HOST
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.CZY='N' | (PROXYSET.win_edit('?')='USER' & PROXYSET.CZY='G')
|| exec('findfnv','#color')
|| ''
?}


\be_port
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed redagowaniem pola PROXYSET.PORT
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.CZY='T' || 1 || 0 ?}


\bs_port
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed wyswietleniem pola PROXYSET.PORT
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.CZY='N' | (PROXYSET.win_edit('?')='USER' & PROXYSET.CZY='G')
|| exec('findfnv','#color')
|| ''
?}


\ae_proxy_czy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola PROXYSET.CZY
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.CZY='N' | PROXYSET.CZY='G'
|| PROXYSET.HOST:='';
   PROXYSET.PORT:=0;
   PROXYSET.AUTO:='N';
   PROXYSET.USER:='';
   PROXYSET.HASLO:='';
   PROXY.KONTROLA:=''
?};
_enabled:=PROXYSET.CZY='T';
PROXYSET.efld_opt(PROXYSET.win_edit('?'),'enable='+$_enabled,PROXYSET,'HOST');
PROXYSET.efld_opt(PROXYSET.win_edit('?'),'enable='+$_enabled,PROXYSET,'PORT');
PROXYSET.efld_opt(PROXYSET.win_edit('?'),'enable='+$_enabled,PROXYSET,'AUTO');
_enabled:=_enabled & PROXYSET.AUTO='T';
PROXYSET.efld_opt(PROXYSET.win_edit('?'),'enable='+$_enabled,PROXYSET,'USER');
PROXYSET.efld_opt(PROXYSET.win_edit('?'),'enable='+$_enabled,PROXYSET,'HASLO');
PROXYSET.efld_opt(PROXYSET.win_edit('?'),'enable='+$_enabled,PROXY,'KONTROLA');
win_disp();
1


\be_proxy_auto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed redakcja pola PROXYSET.AUTO
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.CZY='T' || 1 || 0 ?}


\ae_proxy_auto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola PROXYSET.AUTO
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.AUTO='N'
|| PROXYSET.USER:='';
   PROXYSET.HASLO:='';
   PROXY.KONTROLA:=''
?};
_enabled:=PROXYSET.AUTO='T';
PROXYSET.efld_opt(PROXYSET.win_edit('?'),'enable='+$_enabled,PROXYSET,'USER');
PROXYSET.efld_opt(PROXYSET.win_edit('?'),'enable='+$_enabled,PROXYSET,'HASLO');
PROXYSET.efld_opt(PROXYSET.win_edit('?'),'enable='+$_enabled,PROXY,'KONTROLA');

win_disp();
1


\bs_user
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed wyswietleniem pola PROXYSET.USER
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.AUTO='N'
|| exec('findfnv','#color')
|| ''
?}


\be_user
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed redakcja pola PROXYSET.USER
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.CZY='T' & PROXYSET.AUTO='T' || 1 || 0 ?}


\bs_haslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed wyswietleniem pola PROXYSET.HASLO
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.AUTO='N'
|| exec('findfnv','#color')
|| ''
?}


\be_haslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Przed redakcja pola PROXYSET.HASLO
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.CZY='T' & PROXYSET.AUTO='T' || 1 || 0 ?}


\edit_alert
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Redakcja ustawien proxy dla systemu Alert
::       (przetwarzanie zadan wymagajacych pobierania plikow z internetu)
::----------------------------------------------------------------------------------------------------------------------
PROXYSET.index('TYP');
PROXYSET.prefix('A');
{? ~PROXYSET.first()
|| PROXYSET.blank();
   PROXYSET.TYP:='A';
   PROXYSET.add()
?};
PROXYSET.win_edit('ALERT');
exec('ae_proxy_czy','proxy');
{? PROXYSET.edit("exec('chk_proxy','proxy')")
|| PROXYSET.put()
?}


\edit_glob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Redakcja globalnych ustawien proxy dla systemu Xpertis
::----------------------------------------------------------------------------------------------------------------------
PROXYSET.index('TYP');
PROXYSET.prefix('G');
{? ~PROXYSET.first()
|| PROXYSET.blank();
   PROXYSET.TYP:='G';
   PROXYSET.add()
?};
PROXYSET.win_edit('GLOB');
exec('ae_proxy_czy','proxy');
{? PROXYSET.edit("exec('chk_proxy','proxy')")
|| PROXYSET.put()
?}


\edit_user
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Redakcja ustawien proxy biezacego uzytkownika
::----------------------------------------------------------------------------------------------------------------------
PROXYSET.index('TYP');
PROXYSET.prefix('U', OPERATOR.USER);
{? ~PROXYSET.first()
|| PROXYSET.blank();
   PROXYSET.TYP:='U';
   PROXYSET.CZY:='G';
   PROXYSET.USERS:=OPERATOR.USER;
   PROXYSET.add()
?};
PROXYSET.win_edit('USER');
exec('ae_proxy_czy','proxy');
PROXYSET.hdr_edit(': '+PROXYSET.USERS().KOD);
{? PROXYSET.edit("exec('chk_proxy','proxy')")
|| PROXYSET.put()
?}


\chk_proxy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Formula weryfikujaca poprawnosc wypelnienie okna redakcyjnego tabeli PROXYSET
::----------------------------------------------------------------------------------------------------------------------
{? PROXYSET.win_edit('?')='ALERT' || _a:='alert' || _a:=''  ?};
_wy:='';
{? PROXYSET.CZY='T' & PROXYSET.HOST=''
|| FUN.emsg('Nie wypełniono pola: Host.'@);
   _wy:='HOST'
?};
{? _wy='' & PROXYSET.CZY='T' & PROXYSET.PORT=0
|| FUN.emsg('Nie wypełniono pola: Port.'@);
   _wy:='PORT'
?};
{? _wy='' & PROXYSET.CZY='T' & PROXYSET.PORT<0
|| FUN.emsg('Pole PORT musi zawierać wartość dodatnią.'@);
   _wy:='PORT'
?};
{? _wy='' & _a='alert'
|| {? PROXYSET.AUTO='T' & PROXYSET.USER=''
   || FUN.emsg('W przypadku ustawień proxy dla serwera\n'
               'należy podać nazwę użytkownika.'@);
      _wy:='USER'
   ?};
   {? _wy='' & PROXYSET.AUTO='T' & PROXYSET.HASLO=''
   || FUN.emsg('W przypadku ustawień proxy dla serwera\n'
               'należy podać hasło użytkownika.'@);
      _wy:='HASLO'
   ?};
   {? _wy='' & PROXYSET.AUTO='T' & PROXY.KONTROLA=''
   || FUN.emsg('W przypadku ustawień proxy dla serwera\n'
               'należy ponownie wprowadzić hasło użytkownika aby skontrolować jego poprawność.'@);
      _wy:='KONTROLA'
   ?};
   {? _wy='' & PROXYSET.AUTO='T'
   || {? PROXY.KONTROLA<>PROXYSET.HASLO
      || FUN.emsg('Niepoprawnie wprowadzone hasło: pola Hasło\n'
                  'i Powtórzenie nie są ze sobą zgodne.'@);
         _wy:='HASLO'
      ?}
   ?}
?};
_wy


\get_user
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Zwraca ustawienia proxy dla biezacego uzytkownika lub globalne (jesli ustawien dla biezacego uzytkownika brak)
::----------------------------------------------------------------------------------------------------------------------
_proxy:='U';
PROXYSET.index('TYP');
PROXYSET.prefix('U',OPERATOR.USER);
{? PROXYSET.first()
|| {? PROXYSET.CZY='G'
   || _proxy:='G'
   || exec('czytaj_proxy','proxy')
   ?}
|| _proxy:='G'
?};
{? _proxy='G'
|| PROXYSET.prefix('G');
   {? PROXYSET.first()
   || exec('czytaj_proxy','proxy')
   || PROXY.blank()
   ?}
?}


\get_glob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Zwraca globalne ustawienia proxy
::----------------------------------------------------------------------------------------------------------------------
PROXYSET.index('TYP');
PROXYSET.prefix('G');
{? PROXYSET.first()
|| exec('czytaj_proxy','proxy')
|| PROXY.blank()
?}


\get_alert
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Zwraca ustawienia proxy dla serwera
::----------------------------------------------------------------------------------------------------------------------
PROXYSET.index('TYP');
PROXYSET.prefix('A');
{? PROXYSET.first()
|| exec('czytaj_proxy','proxy')
|| PROXY.blank()
?}


\czytaj_proxy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Funkcja wczytuje ustawienia proxy z biezacego rekordu tabeli PROXYSET do zmiennej PROXY
::----------------------------------------------------------------------------------------------------------------------
PROXY.CZY:=PROXYSET.CZY;
PROXY.AUTO:=PROXYSET.AUTO;
_zmiana:=0;
{? PROXY.HOST<>PROXYSET.HOST | PROXY.PORT<>PROXYSET.PORT || _zmiana:=1 ?};
PROXY.HOST:=PROXYSET.HOST;
PROXY.PORT:=PROXYSET.PORT;
{? _zmiana
|| PROXY.USER:='';
   PROXY.HASLO:=''
?};
{? PROXYSET.TYP='A'
|| PROXY.USER:=PROXYSET.USER;
   PROXY.HASLO:=PROXYSET.HASLO
|? PROXYSET.TYP='U'
|| PROXY.USER:=PROXYSET.USER
?}


\login
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Procedura wyswietla prosbe o nazwe uzytkownika i haslo do serwera proxy,
::       jesli te dane nie byly podawane w biezacej sesji systemu
::----------------------------------------------------------------------------------------------------------------------
_wy:=1;
{? PROXY.USER='' | PROXY.HASLO=''
|| PROXY.win_edit('RED');
   {? ~PROXY.edit("exec('chk_login','proxy')")
   || _wy:=0
   ?}
?};
_wy


\chk_login
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Procedura weryfikujaca poprawnosc podanych danych: nazwy uzytkownika i hasla
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
{? PROXY.USER=''
|| FUN.emsg('Nie wypełnione pole: Użytkownik.'@);
   _wy:='USER'
?};
{? _wy='' & PROXY.HASLO=''
|| FUN.emsg('Nie wypełnione pole: Hasło.'@);
   _wy:='HASLO'
?};
_wy


\proxy_lista
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Lista uzytkownikow z ustawieniami indywidualnymi proxy
::----------------------------------------------------------------------------------------------------------------------
PROXYSET.index('TYP');
PROXYSET.prefix('U');
PROXYSET.win_sel('WER');
PROXYSET.win_edit('USER');
PROXYSET.select(,1)


\proxy_user_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Dodaje ustawienia indywidualne proxy dla uzytkownika
::----------------------------------------------------------------------------------------------------------------------
PROXYSET.blank();
PROXYSET.TYP:='U';
PROXYSET.win_edit('NUSER');
{? PROXYSET.edit("exec('chk_proxy','proxy')")
|| PROXYSET.add()
?};
PROXYSET.win_edit('USER')


\proxy_user_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Modyfikuje ustawienia indywidualne proxy dla uzytkownika
::----------------------------------------------------------------------------------------------------------------------
PROXYSET.win_edit('USER');
{? PROXYSET.edit("exec('chk_proxy','proxy')")
|| PROXYSET.put()
?}


\proxy_user_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Usuwa ustawienia indywidualne proxy dla uzytkownika
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć indywidualne ustawienia proxy dla użytkownika:\n\n%1'@[PROXYSET.USERS().KOD+' ('+PROXYSET.USERS().DANE+')'])
|| PROXYSET.del()
?}


\inet_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42]
:: OPIS: Zwraca wynika działania funkcji inet_get po uwzględnieniu ustawień proxy
::   WE: _a - adres internetowy
::       [_b][INTEGER] - okno z postępem wywołania zapytania (0 - tak / 1 - nie)
::----------------------------------------------------------------------------------------------------------------------
_no_prog:={? var_pres('_b')=type_of(0) || _b || 0 ?};
{? 1+_a='@'
|| exec('get_user','proxy')
|| exec('get_alert','proxy')
?};
{? PROXY.CZY='T'
|| _proxy:='';
   {? PROXY.AUTO='T'
   || _proxy+=PROXY.USER+':'+PROXY.HASLO+'@'
   ?};
   {? PROXY.HOST<>''
   || _proxy+=PROXY.HOST
   ?};
   {? PROXY.PORT
   || _proxy+=':'+$PROXY.PORT
   ?};
   {? _proxy<>''
   || _proxy:='http://'+_proxy
   ?};
   _http:=inet_get(_a,_proxy,_no_prog)
|| _http:=inet_get(_a,,_no_prog)
?};
_http


\triger
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42]
:: OPIS: Triger dla tabeli PROXYSET
::----------------------------------------------------------------------------------------------------------------------
PROXYSET.CZY_OPIS:={? PROXYSET.CZY='G' || 'Globalne' |? PROXYSET.CZY='T' || 'Indywiduwalne' || 'Brak' ?};
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:22 9d168c02e37cd0bc03fa0b065c2ecf41279d521c4f81df45a1d9c7d2f6c2c6fbb2d268a3b0874e5a698470d487ff13f356e389a5f52e582c082208389da9109f7aa42002dbd06e39b374efacfc5e1b158f1b1fa24816928393f86c031c5b8648f3e545fe5b6586cf6b77d8281e5f86286e955f30916187586b6233f9ecfc4b1c

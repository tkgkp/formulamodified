:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: px_test.fml
:: Utworzony: 2011-08-31
:: Autor: TS
::======================================================================================================================
:: Zawartosc: Formuly do testowania px_*
::======================================================================================================================


\time_check_test
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PRIVATE
::  UTW: WH [12.10]
:: OPIS: Testuje funkcje cup_time_check/px_plan
::----------------------------------------------------------------------------------------------------------------------
PX_KONT.cntx_psh();
PX_CUP.cntx_psh();
PX_KONT.clear();
PX_KONT.first();
_t1:=time(13,0,0);
_t2:=time(18,0,0);
_data:=date(2011,7,15);
_start:=tm_stamp(_data~1,_data~2,_data~3,_t1~1,_t1~2,_t1~3,0);
_end:=tm_stamp((_data+1)~1,(_data+1)~2,(_data+1)~3,_t2~1,_t2~2,_t2~3,0);
_result:=exec('cup_time_check','px_plan',PX_KONT.ref(),_start,_end,-1);
msg(PX_CUP.SYMBOL+' wynik: '+$_result);
PX_KONT.cntx_pop();
PX_CUP.cntx_pop();
~~


\test
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [12.10]
:: OPIS: Testuje funkcje select/px_plan
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{! _it:=1..10
|! exec('select','px_plan',exec('get_mainversion','px_ver'),1)
!};
~~


\px_obj_offset
::---------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [12.10]
:: OPIS: Przesuwa wszystkim PX_OBJ termin realizacji o zadana liczbe dni - przydatne, poniewaz
::       obiektow z pozniejszym terminem niz dzisiaj nie da sie zaplanowac
::   WE: _a - INTEGER - liczba dni o jaka przesunac terminy
::---------------------------------------------------------------------------------------------------------------------
_offset:=0;
{? var_pres('_a')=type_of(0)
|| _offset:=_a
?};

PX_OBJ.cntx_psh();
PX_OBJ.index('MAT');
PX_OBJ.clear();
{? PX_OBJ.first()
|| {!
   |? _data:=PX_OBJ.ENDD().DATA;
      _data:=_data+_offset;
      DATY.cntx_psh();
      DATY.index('DATA');
      DATY.prefix(_data);
      {? DATY.first()
      || PX_OBJ.ENDD:=DATY.ref();
         PX_OBJ.put()
      ?};
      DATY.cntx_pop();
      PX_OBJ.next()
   !}
?};
PX_OBJ.cntx_pop();
~~


\move_pxpoz
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PRIVATE
::  UTW: WH [12.30]
:: OPIS: Testuje przesuwanie, scalanie PX_POZow
::   WE: _a - INTEGER - #PX_POZ.ref()
::----------------------------------------------------------------------------------------------------------------------
_px_poz:=_a;
PX_POZ.cntx_psh();
PX_WYK.cntx_psh();
PX_POZ.clear();
{? PX_POZ.seek(_px_poz,)
|| PX_WYK.index('POZ');
   _px_wyk:=null();
   PX_WYK.prefix(PX_POZ.ref());
   {? PX_WYK.first()
   || _px_wyk:=PX_WYK.ref()
   ?};
   exec('move','px_poz',PX_POZ.ref(),PX_CUP.ref(),_px_wyk)
?};
PX_POZ.cntx_pop();
PX_WYK.cntx_pop();
~~


\split_pxpoz
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PRIVATE
::  UTW: WH [12.30]
:: OPIS: Testuje dzielenie PX_POZow
::   WE: _a - INTEGER - #PX_POZ.ref()
::----------------------------------------------------------------------------------------------------------------------
_px_poz:=_a;
_ilosc:=0;
undefine();
define('ILOSC',0,'Podaj ilosc');
{? def_edit(,ST.MSG)
|| _ilosc:=DEFINE.ILOSC;
   {? _ilosc>0;1
   ||
      PX_POZ.cntx_psh();
      PX_WYK.cntx_psh();
      PX_POZ.clear();
      {? PX_POZ.seek(_px_poz,)
      || _argsplit:=exec('split_a','px_poz');
         _argsplit.SRC_POZ:=PX_POZ.ref();
         _argsplit.DST_CUP:=PX_CUP.ref();
         _argsplit.SPLITWYK:=1;

         _ilosci:=obj_new(2);
         _ilosci[1]:=_ilosc;
         _ilosci[2]:=0;

         exec('split','px_poz',_argsplit,_ilosci)
      ?};
      PX_POZ.cntx_pop();
      PX_WYK.cntx_pop()
   ?}
?};
undefine();
~~

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:00 1579dd4a9005acca6372657828111d91fea441d9fcbc9d920b015d4ae73f42bad696519028abd291f9e7cf928caccab229bc84e93580b0a128921199259de3aaf92a8272bad7e8e32ebc0b7246d358ebe89c8b54b4dc36c58a52ecef67da4dca1936c08e5846e33c84aa204794350a1345a2b74c71abf899af8d18c2439b3d9e

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: portal_seod.fml
:: Utworzony: 01.02.2022
:: Autor: MB
::======================================================================================================================
:: Zawartość: Plik zawiera formuły wykorzystywane do obsługi faktur na portalu.
::======================================================================================================================


\EDOKUM_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Wyświetla faktury w obiegu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_kod:={? var_pres('_a')=type_of('') & +_a=2 || _a || $(date~1)+2 ?};
_w_portal:={? var_pres('_b')=type_of('') & +_b=1 & 'S'*_b || _b || 'S' ?};

EDOKUM.cntx_psh();
exec('EDOKUM_win_sel','portal_seod');
EDOKUM.use('skid_v'+_kod);
EDOKUM.index('W_PORTAL');
EDOKUM.prefix(REF.FIRMA,exec('bl_typ','obiegi',1),_w_portal);
POZF.cntx_psh();
EVAT.cntx_psh();
EPODZ.cntx_psh();
EDOKOS.cntx_psh();
EDOKUMZ.cntx_psh();
::EDOKUM.win_sel(_grp);
EPODZ.fld_fml('SKID_MB','BEFORE_DISPLAY',"");
typobi:=1;
EDOKUM.select();
EPODZ.fld_fml('SKID_MB','BEFORE_DISPLAY',"*");
EDOKUMZ.cntx_pop();
EDOKOS.cntx_pop();
EPODZ.cntx_pop();
EVAT.cntx_pop();
POZF.cntx_pop();
EDOKUM.cntx_pop()


\EDOKUM_win_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca okno wertowania dla faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
:: Okno EDOKUM
_wedok:=_o:=EDOKUM.mk_sel('Faktury - SEOD','P',,'edokumtmp10',,,,,'U');
EDOKUM.win_fld(_o,,'DATAW',,,-12,,,'Data rejestracji');
EDOKUM.win_fld(_o,,'USERS','KOD',,10,,,'Użytkownik');
EDOKUM.win_fld(_o,,'TP',,,-15,,,'Termin płatności');
EDOKUM.win_fld(_o,,'TYP','NAZWA',,20,,,'Typ');
EDOKUM.win_fld(_o,,'KHKH','SKR',,,,,'Kontrahent');
EDOKUM.win_fld(_o,,'KH_FULL',,,20,,,,1);
EDOKUM.win_fld(_o,,'SYM',,,,,,'Symbol');
EDOKUM.win_fld(_o,,'NETTO',,,16,2,,'Netto');
EDOKUM.win_fld(_o,,'WART',,,16,2,,'Brutto');
EDOKUM.win_fld(_o,,'ID',,,26,,,'Identyfikator');
EDOKUM.win_fld(_o,,'STATUS');
EDOKUM.win_sel(_o);
EDOKUM.win_act(_o,,'Formuła','Usuń',,,"
   _akc_grp:=EDOKUM.sel_size();
   {? _akc_grp | FUN.ask('Czy usunąć fakturę?')
   || _next:=null;
      EDOKUM.cntx_psh();
      {? EDOKUM.next() | EDOKUM.prev()
      || _next:=EDOKUM.ref()
      ?};
      EDOKUM.cntx_pop();
      {? exec('EDOKUM_del','portal_wnioski') & _next
      || EDOKUM.seek(_next)
      ?}
   ?}
",,,1,"
   FUN.ask('Czy usunąć zaznaczone faktury?')
");
_fml:="
   EDOKUM.cntx_psh();
   exec('process_receive','sync_mwa','PORTAL_SEOD',,,'EDOKUM');
   EDOKUM.cntx_pop()
";
EDOKUM.win_act(_o,,'Formuła','Testuj od&bieranie',,,_fml);
EDOKUM.win_act(_o,1,'Formuła','Testuj od&bieranie',,,_fml);
EDOKUM.win_act(_o,,'Formuła','Testuj wy&syłanie',,,"
   EDOKUM.cntx_psh();
   exec('process_send','sync_mwa','PORTAL_SEOD',,,'EDOKUM');
   EDOKUM.cntx_pop()
",,);
EDOKUM.win_act(_o,,'Formuła','Zadania',,,"exec('todo_select','#b__box',EDOKUM.uidref())",,);

_menu:='Json';
EDOKUM.win_act(_o,,'Menu',_menu);
EDOKUM.win_act(_o,,'Formuła','Faktura',_menu,,"
   _par:=obj_new('env');
   _par.env:=obj_new('Adds','temp','MethodName');
   _par.env.MethodName:='cseod_PurchaseDocumentModify';
   _par.env.temp:='{\"Arraycseod_PurchaseDocumentModifyData\":{\"cseod_PurchaseDocumentModifyData\":[{\"__id_cloud\":<long|null>,\"DokType\":<string>,\"DokNd\":<int|null>,\"DokNumber\":<string|null>,\"InvoiceNumber\":<string|null>,\"CostCategory\":<string|null>,\"BusinessContactId\":<long|null>,\"BCAddresHistoryId\":<long|null>,\"CurrencySymbol\":<string>,\"RegistryDate\":<dateTime>,\"ReceivedDate\":<dateTime|null>,\"InvoiceDate\":<dateTime|null>,\"SaleDate\":<dateTime|null>,\"GrossValue\":<decimal|null>,\"NetValue\":<decimal|null>,\"VATValue\":<decimal|null>,\"PaymentType\":<string|null>,\"DueDate\":<dateTime|null>,\"payedFromAdvance\":<unsignedByte|null>,\"SplitPayment\":<int|null>,\"Status\":<int|null>,\"Description\":<string|null>,\"TenantUserId\":<long|null>,\"__ModifiedDate_cloud\":<dateTime|null>,\"__ModifiedDate_erp\":<dateTime|null>,\"__id_erp\":<string|null>,\"__JSON\":<string|null>,\"__exf__cseod_PurchaseDocument_d_01\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_d_02\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_d_03\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_d_04\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_d_05\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_d_06\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_d_07\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_d_08\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_d_09\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_d_10\":<dateTime|null>,\"__exf__cseod_PurchaseDocument_n_01\":<decimal|null>,\"__exf__cseod_PurchaseDocument_n_02\":<decimal|null>,\"__exf__cseod_PurchaseDocument_n_03\":<decimal|null>,\"__exf__cseod_PurchaseDocument_n_04\":<decimal|null>,\"__exf__cseod_PurchaseDocument_n_05\":<decimal|null>,\"__exf__cseod_PurchaseDocument_n_06\":<decimal|null>,\"__exf__cseod_PurchaseDocument_n_07\":<decimal|null>,\"__exf__cseod_PurchaseDocument_n_08\":<decimal|null>,\"__exf__cseod_PurchaseDocument_n_09\":<decimal|null>,\"__exf__cseod_PurchaseDocument_n_10\":<decimal|null>,\"__exf__cseod_PurchaseDocument_s_01\":<string|null>,\"__exf__cseod_PurchaseDocument_s_02\":<string|null>,\"__exf__cseod_PurchaseDocument_s_03\":<string|null>,\"__exf__cseod_PurchaseDocument_s_04\":<string|null>,\"__exf__cseod_PurchaseDocument_s_05\":<string|null>,\"__exf__cseod_PurchaseDocument_s_06\":<string|null>,\"__exf__cseod_PurchaseDocument_s_07\":<string|null>,\"__exf__cseod_PurchaseDocument_s_08\":<string|null>,\"__exf__cseod_PurchaseDocument_s_09\":<string|null>,\"__exf__cseod_PurchaseDocument_s_10\":<string|null>,\"TenantCompanyId\":<long|null>,\"SubType\":<string|null>,\"__BinAttachment\":<string|null>,\"__Notes\":<string|null>,\"__SessionId\":<int|null>,\"BCAddresHistoryId__id_erp\":<string|null>,\"NumberPath\":<string|null>,\"TenantCompanyId__id_erp\":<string|null>,\"TenantUserId__id_erp\":<string|null>}]}}';
   _par.env.Adds:=obj_new('obj');
   exec('portaljson_decl','portal_engine');
   _par.env.Adds.obj:=obj_new(@.CLASS.PortalJSON);
   _par.env.Adds.obj.init(_par.env.temp,_par.env.MethodName,KOMM);
   params_set('env',_par.env);
   exec('cseod_PurchaseDocModify_json','portal_method_seod',0);
   _json:=_par.env.Adds.obj.json();
   KH.memo_set(_json,'OPIS');
   KH.memo_view(,'OPIS')
");
EDOKUM.win_act(_o,,'Formuła','Scieżka',_menu,,"
   _json:=exec('EDOKUM_akc_json','portal_wnioski');
   KH.memo_set(_json,'OPIS');
   KH.memo_view(,'OPIS')
",,);
EDOKUM.win_act(_o,,'Formuła','Dodatkowe',_menu,,"
   _json:=exec('EDOKUM_json','portal_seod');
   KH.memo_set(_json,'OPIS');
   KH.memo_view(,'OPIS')
",,);
EDOKUM.win_act(_o,,'Formuła','Do &wysyłki',,,"EDOKUM.put(,1)",,,1);
EDOKUM.win_act(_o,,'Wyświetl',,,,"
   EDOKUM.cntx_psh();
   EDOKUM.win_edit('WPR');
   EDOKUM.display();
   EDOKUM.cntx_pop()
");

EDOKUM.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=begin'['Testuj od&bieranie'],'menu:B');
EDOKUM.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=begin'['Testuj wy&syłanie'],'menu:S');

:: Okno POZF
_wpoz:=POZF.mk_sel('Pozycje towarowe','P',,'#pozfseod',,,,,'U');
POZF.win_fld(_wpoz,,'LP',,,5,,,'Lp');
POZF.win_fld(_wpoz,,'NAZ',,,52,,,'Nazwa');
POZF.win_fld(_wpoz,,'IL',,,8,6,,'Ilość');
POZF.win_fld(_wpoz,,'JM','KOD',,5,,,'Miara');
POZF.win_fld(_wpoz,,'C',,,25,2,,'Cena');
POZF.win_fld(_wpoz,,'RABAT',,,16,2,,'Rabat');
POZF.win_fld(_wpoz,,'WN',,,25,2,,'Netto');
POZF.win_fld(_wpoz,,'SV','KOD',,8,,,'Stawka');
POZF.win_fld(_wpoz,,'WV',,,16,2,,'VAT');
POZF.win_fld(_wpoz,,'WB',,,25,2,,'Brutto');
::POZF.win_act(_wpoz,,'Formuła','Nazwa_akcji',,,"przed","po",1);
POZF.win_sel(_wpoz);

:: Okno EVAT
_wvat:=EVAT.mk_sel('Pozycje VAT','P',,'#evatseod',,,,,'U');
EVAT.win_fld(_wvat,UNPAR,'P10',,,116,,,,1);
EVAT.win_fld(_wvat,,'NETTO',,,25,2,,'Netto');
EVAT.win_fld(_wvat,,'PR','KOD',,8,,,'Stawka');
EVAT.win_fld(_wvat,,'VAT',,,16,2,,'VAT');
EVAT.win_fld(_wvat,,'BRUTTO',,,25,2,,'Brutto');
::EVAT.win_act(_wvat,,'Formuła','Nazwa_akcji',,,"przed","po",1);
EVAT.win_sel(_wvat);

:: Okno EPODZ
_wpodz:=EPODZ.mk_sel('Podziały','P',,'#epodzseod',,,,,'U');
EPODZ.win_fld(_wpodz,,'SKID_MB','KOD',,,,,'Model');
EPODZ.win_fld(_wpodz,,'PBUD','SYMBOL',,13,,,'Wymiar 1');
EPODZ.win_fld(_wpodz,,'PBUD','OPIS',,13,,,,1);
EPODZ.win_fld(_wpodz,,'JORG','SYMBOL',,13,,,'Wymiar 2');
EPODZ.win_fld(_wpodz,,'JORG','OPIS',,13,,,,1);
EPODZ.win_fld(_wpodz,,'OKOSZ','SYMBOL',,13,,,'Wymiar 3');
EPODZ.win_fld(_wpodz,,'OKOSZ','OPIS',,13,,,,1);
EPODZ.win_fld(_wpodz,,'WYM4','SYMBOL',,13,,,'Wymiar 4');
EPODZ.win_fld(_wpodz,,'WYM4','OPIS',,13,,,,1);
EPODZ.win_fld(_wpodz,,'WYM5','SYMBOL',,13,,,'Wymiar 5');
EPODZ.win_fld(_wpodz,,'WYM5','OPIS',,13,,,,1);
EPODZ.win_fld(_wpodz,,'WART',,,25,2,,'Wartość');
EPODZ.win_fld(_wpodz,,'WAL','KOD',,,,,'Waluta');
EPODZ.win_fld(_wpodz,,'JM','KOD',,,,,'Jednostka');
::EPODZ.win_act(_wpodz,,'Formuła','Nazwa_akcji',,,"przed","po",1);
EPODZ.win_sel(_wpodz);

:: Inne dane
_watr:=_o:=EDOK_ATR.mk_sel('Inne dane','P',,'#edokaseod',,,,,'U');
EDOK_ATR.win_fld(_o,,'KOL');
EDOK_ATR.win_fld(_o,,'TAT','OPIS',,80);
EDOK_ATR.win_fld(_o,,'WAR',,,50);
EDOK_ATR.win_fld(_o,,'WAR_W',,,50);
EDOK_ATR.win_act(_o,,'Kolejność');
EDOK_ATR.win_sel(_o);

:: Okno EDOKOS
_wos:=_o:=EDOKOS.mk_sel('Obieg','P',,'edokumymp03',,,,,'U');
EDOKOS.win_fld(_o,,'DATA',,,-10);
EDOKOS.win_fld(_o,,'TIME',,,-8);
EDOKOS.win_fld(_o,,'USERS','KOD',,,,,'Użytkownik');
EDOKOS.win_fld(_o,,'OPERACJA');
EDOKOS.win_fld(_o,,'UW_DL',,,50);
EDOKOS.win_fld(_o,,'WID');
EDOKOS.win_fld(_o,,'B_PREL',,,20);
EDOKOS.win_fld(_o,,'B_PRELS',,,20);
EDOKOS.win_fld(_o,,'STATUS',,,20);
EDOKOS.win_fld(_o,,'OPER_NUM');
EDOKOS.win_act(_o,,'Usuń');
EDOKOS.win_act(_o,,'Wyświetl',,,,"FUN.info(EDOKOS.uidref())",,);
EDOKOS.win_act(_o,,'Kolejność');
EDOKOS.win_sel(_o);

:: Okno EDOKUMZ
_wz:=_o:=EDOKUMZ.mk_sel('Załaczniki','P',,'edokumymp04',,,,,'U');
EDOKUMZ.win_fld(_o,,'DATE');
EDOKUMZ.win_fld(_o,,'TIME');
EDOKUMZ.win_fld(_o,,'USER',,,,,,'Użytkownik');
EDOKUMZ.win_fld(_o,,'TYP_ZAL','NAZWA',,30,,,'Typ');
EDOKUMZ.win_fld(_o,,'EDOKUM',,,133);
EDOKUMZ.win_act(_o,,'Wyświetl',,,,"FUN.info(EDOKUMZ.uidref())",,);
EDOKUMZ.win_act(_o,,'Kolejność');
EDOKUMZ.win_sel(_o);


:: Okno grupowe EDOKUM
_grp:=EDOKUM.grp_make('Faktury w obiegu - SEOD',"",'edokumseod');
EDOKUM.grp_sel(_grp,,_wedok,,"
   _kod:=EDOKUM.name()+2;
   POZF.use('pozf__'+_kod);
   POZF.index('EDOKUM');
   POZF.prefix(EDOKUM.ref()); POZF.first();
   grp_disp(POZF,POZF.win_sel('?'));

   EVAT.use('skid_a'+_kod);
   EVAT.index('EDOKUM');
   EVAT.prefix(EDOKUM.ref()); EVAT.first();
   grp_disp(EVAT,EVAT.win_sel('?'));

   EPODZ.use('skid_j'+_kod);
   EPODZ.index('EDOKUMDS');
   EPODZ.prefix(EDOKUM.ref()); EPODZ.first();
   grp_disp(EPODZ,EPODZ.win_sel('?'));

   EDOK_ATR.use('edokat'+_kod);
   EDOK_ATR.index('REKKOLED');
   EDOK_ATR.prefix(EDOKUM.ref(),null,'',); EDOK_ATR.first();
   grp_disp(EDOK_ATR,EDOK_ATR.win_sel('?'));

   EDOKOS.use('skid_y'+_kod);
   EDOKOS.index('DATA');
   EDOKOS.prefix(EDOKUM.ref()); EDOKOS.first();
   grp_disp(EDOKOS,EDOKOS.win_sel('?'));

   EDOKUMZ.use('skid_n'+_kod);
   EDOKUMZ.index('DISP');
   EDOKUMZ.prefix(EDOKUM.ref()); EDOKUMZ.first();
   grp_disp(EDOKUMZ,EDOKUMZ.win_sel('?'),1)
");
EDOKUM.grp_splt(_grp,,'horizontal','bottom',15);
EDOKUM.grp_sel(_grp,POZF,_wpoz,'Pozycje');
EDOKUM.tab_splt(_grp,,'horizontal','bottom',10);
EDOKUM.grp_sel(_grp,EVAT,_wvat);
EDOKUM.grp_sel(_grp,EPODZ,_wpodz,'Podziały');
EDOKUM.grp_sel(_grp,EDOK_ATR,_watr,'Inne dane');
EDOKUM.grp_sel(_grp,EDOKOS,_wos,'Obieg');
EDOKUM.grp_sel(_grp,EDOKUMZ,_wz,'Załączniki');


EDOKUM.win_sel(_grp);
~~


\EDOKUM_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca json do komunikatu z fakturą w obiegu/delegacją
::----------------------------------------------------------------------------------------------------------------------
_jman:=exec('json_man','__jsonm','{}');
_maska:=ref_name(EDOKUM.ref())+2;

_wp:=EDOKUM.TYP().W_PORTAL;

{? _wp='S'
||
:: Pozycje faktury
   _poz:=1;
   {? EDOKUM.TYP().POZF='T'
   || POZF.cntx_psh();
      POZF.use('pozf__'+_maska);
      POZF.index('EDOKUM'); POZF.prefix(EDOKUM.ref());
      {? POZF.first()
      || _poz:=0;
         _jman.setObject(_path:=_jman.addChildElement('','Lines'),'[]');
         {!
         |? _jman.setObject(_path2:=_jman.addChildElement(_path,''),'{}');
            _jman.setValue(_jman.addChildElement(_path2,'LP'),POZF.LP);
            _jman.setValue(_jman.addChildElement(_path2,'Description'),POZF.NAZ);
            _jman.setValue(_jman.addChildElement(_path2,'SymbolVR'),exec('st_vat_map','portal_seod',POZF.SV().KOD));
            _jman.setValue(_jman.addChildElement(_path2,'Quantity'),POZF.IL);
            _jman.setValue(_jman.addChildElement(_path2,'Price'),POZF.C);
            _jman.setValue(_jman.addChildElement(_path2,'NetValue'),POZF.WN);
            _jman.setValue(_jman.addChildElement(_path2,'VATValue'),POZF.WV);
            _jman.setValue(_jman.addChildElement(_path2,'JM'),POZF.JM().KOD);
            POZF.next()
         !}
      ?};
      POZF.cntx_pop()
   ?};

:: Pozycje VAT
   {? _poz
   || EVAT.cntx_psh();
      EVAT.use('skid_a'+_maska);
      EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
      {? EVAT.first()
      || _jman.setObject(_path:=_jman.addChildElement('','Lines'),'[]');
         _lp:=0;
         {!
         |? _jman.setObject(_path2:=_jman.addChildElement(_path,''),'{}');
            _jman.setValue(_jman.addChildElement(_path2,'LP'),(_lp+=1));
            _jman.setValue(_jman.addChildElement(_path2,'Description'),'Suma: '+EVAT.PR().KOD);
            _jman.setValue(_jman.addChildElement(_path2,'SymbolVR'),exec('st_vat_map','portal_seod',EVAT.PR().KOD));
            _jman.setValue(_jman.addChildElement(_path2,'Quantity'),1);
            _jman.setValue(_jman.addChildElement(_path2,'Price'),EVAT.NETTO);
            _jman.setValue(_jman.addChildElement(_path2,'NetValue'),EVAT.NETTO);
            _jman.setValue(_jman.addChildElement(_path2,'VATValue'),EVAT.VAT);
            _jman.setValue(_jman.addChildElement(_path2,'JM'),'szt');
            EVAT.next()
         !}
      ?};
      EVAT.cntx_pop()
   ?};

:: Podziały dla controllingu
   exec('EPODZ_json','portal_seod',_jman);

:: Zapotrzebowania
   {? 1 | EDOKUM.TYP().ZAP_REAL='T'
   || EDOKRZAP.cntx_psh();
      EDOKRZAP.use('skid_q'+_maska);
      EDOKRZAP.index('EDOKUM');
      EDOKRZAP.prefix(EDOKUM.ref());
      {? EDOKRZAP.first()
      || _jman.setObject(_path:=_jman.addChildElement('','LinkDocuments'),'[]');
         {!
         |? _uid:='';
            EDOKUM.cntx_psh();
            EDOKUM.use(ref_name(EDOKRZAP.EZAPOT));
            EDOKUM.prefix();
            {? EDOKUM.seek(EDOKRZAP.EZAPOT)
            || _uid:=EDOKUM.uidref()
            ?};
            EDOKUM.cntx_pop();
            {? _uid<>''
            || _sid:=exec('get_id_cloud','portal_core','PORTAL_EDOKUM_ID',_uid);
               {? _sid
               || _jman.setObject(_path2:=_jman.addChildElement(_path,''),'{}');
                  _jman.setValue(_jman.addChildElement(_path2,'SourceId'),_sid);
                  _jman.setValue(_jman.addChildElement(_path2,'LinkType'),'PurchaseDocument-ZapMat');
                  _jman.setValue(_jman.addChildElement(_path2,'SettValue'),EDOKRZAP.NETTO)
               ?}
            ?};
            EDOKRZAP.next()
         !}
      ?};
      EDOKRZAP.cntx_pop()
   ?}

|? _wp='D'
||
:: Diety
   EDOKUMD.cntx_psh();
   EDOKUMD.use('bdiety'+_maska);
   EDOKUMD.index('DELEG');
   EDOKUMD.prefix(EDOKUM.ref());
   {? EDOKUMD.first()
   || _jman.setObject(_path:=_jman.addChildElement('','BTTimeTable'),'[]');
      {!
      |? _jman.setObject(_path2:=_jman.addChildElement(_path,''),'{}');
         _jman.setValue(_jman.addChildElement(_path2,'StartTime'),
            exec('date2iso8601','#convert',EDOKUMD.DATA_OD,EDOKUMD.GODZ_OD)
         );
         _jman.setValue(_jman.addChildElement(_path2,'EndTime'),
            exec('date2iso8601','#convert',EDOKUMD.DATA_DO,EDOKUMD.GODZ_DO)
         );
         _jman.setValue(_jman.addChildElement(_path2,'Localization'),EDOKUMD.MIEJSCE);
         _jman.setValue(_jman.addChildElement(_path2,'Country'),EDOKUMD.KRAJ().KODISO);
         _jman.setValue(_jman.addChildElement(_path2,'BreakfastProvidedQty'),EDOKUMD.LICZ_SNI);
         _jman.setValue(_jman.addChildElement(_path2,'LunchProvidedQty'),EDOKUMD.LICZ_OBI);
         _jman.setValue(_jman.addChildElement(_path2,'DinnerProvidedQty'),EDOKUMD.LICZ_KOL);
         _jman.setValue(_jman.addChildElement(_path2,'AccommodationAllowance'),EDOKUMD.LICZ_NOC);
         _jman.setValue(_jman.addChildElement(_path2,'PublicTransportAllowance'),EDOKUMD.LICZ_DOJ);
         EDOKUMD.next()
      !}
   ?};
   EDOKUMD.cntx_pop();

:: Przejazdy
   EDOKUMP.cntx_psh();
   EDOKUMP.use('skidpu'+_maska);
   EDOKUMP.index('CHRONO');
   EDOKUMP.prefix(EDOKUM.ref());
   {? EDOKUMP.first()
   || _jman.setObject(_path:=_jman.addChildElement('','BTWay'),'[]');
      _kw:=0;
      {!
      |? _jman.setObject(_path2:=_jman.addChildElement(_path,''),'{}');
         _jman.setValue(_jman.addChildElement(_path2,'DepartureTime'),
            exec('date2iso8601','#convert',EDOKUMP.DATA_WYJ,EDOKUMP.GODZ_WYJ)
         );
         _jman.setValue(_jman.addChildElement(_path2,'ArrivalTime'),
            exec('date2iso8601','#convert',EDOKUMP.DATA_PRZ,EDOKUMP.GODZ_PRZ)
         );
         _jman.setValue(_jman.addChildElement(_path2,'DepartureLocation'),EDOKUMP.MIE_WYJ);
         _jman.setValue(_jman.addChildElement(_path2,'ArrivalLocation'),EDOKUMP.MIE_PRZ);
         _jman.setValue(_jman.addChildElement(_path2,'DepartureCountry'),EDOKUMP.KRAJ_WYJ().KODISO);
         _jman.setValue(_jman.addChildElement(_path2,'ArrivalCountry'),EDOKUMP.KRAJ_PRZ().KODISO);
         _jman.setValue(_jman.addChildElement(_path2,'Distance'),EDOKUMP.LICZ_KM);
         _st:={? EDOKUMP.SRODTRAN='I' || EDOKUMP.INNESRTR().NAZWA
              |? EDOKUMP.SRODTRAN='P' || 'Samochód prywatny'
              || 'Samochód służbowy'
              ?};
         _jman.setValue(_jman.addChildElement(_path2,'VehicleKind'),_st);
         {? EDOKUMP.SAMREF<>''
         || _nr:=exec('FindAndGet','#table','POJAZDY',EDOKUMP.SAMREF,,$"POJAZDY.NRREJ");
            {? var_pres('_nr')=type_of('')
            || _jman.setValue(_jman.addChildElement(_path2,'PlateNumber'),_nr)
            ?}
         ?};
         {? EDOKUMP.DATA_GR<>date(0,0,0)
         || _jman.setValue(_jman.addChildElement(_path2,'BorderCrossTime'),
               exec('date2iso8601','#convert',EDOKUMP.DATA_GR,EDOKUMP.GODZ_GR)
            )
         ?};
         EDOKUMP.next()
      !}
   ?};
   EDOKUMP.cntx_pop();

:: Podsumowanie
   _lp:=_suma:=0;
   _sum:=exec('obj_sum_deleg','portal_seod');
   _tsum:=_sum.TAB;
   _jman.setObject(_pathS:=_jman.addChildElement('','BTSettlement'),'[]');

:: Podsumowanie diet
   EDOKUMD.cntx_psh();
   EDOKUMD.use('bdiety'+_maska);
   EDOKUMD.index('DELEG');
   EDOKUMD.prefix(EDOKUM.ref());
   {? EDOKUMD.first()
   || {!
      |? _kw:=exec('kwota_refresh','diety');
         {? _kw
         || _sum.add(EDOKUMD.WAL().SYM,'diety','Dieta - ryczałt',_kw)
         ?};
         EDOKUMD.next()
      !}
   ?};
   EDOKUMD.cntx_pop();

:: Podsumowanie przejazdów
   EDOKUMP.cntx_psh();
   EDOKUMP.use('skidpu'+_maska);
   EDOKUMP.index('CHRONO');
   EDOKUMP.prefix(EDOKUM.ref());
   {? EDOKUMP.first()
   || _kw:=0;
      {!
      |? _kw+=EDOKUMP.WARTOSC;
         EDOKUMP.next()
      !};
      {? _kw
      || _sum.add('PLN','wydatki','Przejazd - ryczałt',_kw)
      ?}
   ?};
   EDOKUMP.cntx_pop();

:: Podsumowanie wydatków
   EDOK_ZAL.cntx_psh();
   EDOK_ZAL.use('edokzl'+_maska);
   EDOK_ZAL.index('WYDATKI');
   EDOK_ZAL.prefix(EDOKUM.ref());
   {? EDOK_ZAL.first()
   || _kw:=0;
      {!
      |? _kw+=EDOK_ZAL.BRUTTO;
         EDOK_ZAL.next()
      !};
      {? _kw
      || _sum.add('PLN','wydatki','Wydatki',_kw)
      ?}
   ?};
   EDOK_ZAL.cntx_pop();

:: Podsumowanie
   _tsum.prefix();
   {? _tsum.first()
   || {!
      |? _tsum.prefix(_tsum.WAL,);
         {? _tsum.first()
         || _suma:=0;
            {!
            |? _jman.setObject(_pathS2:=_jman.addChildElement(_pathS,''),'{}');
               _jman.setValue(_jman.addChildElement(_pathS2,'Currency'),_tsum.WAL);
               _jman.setValue(_jman.addChildElement(_pathS2,'Lp'),$(_lp+=1));
               _jman.setValue(_jman.addChildElement(_pathS2,'Description'),_tsum.DESC);
               _jman.setValue(_jman.addChildElement(_pathS2,'Amount'),_tsum.KW);
               _jman.setValue(_jman.addChildElement(_pathS2,'SettlementKind'),_tsum.TYPE);
               _suma+=_tsum.KW;
               _tsum.next()
            !}
         ?};
         {? _suma
         || _jman.setObject(_pathS2:=_jman.addChildElement(_pathS,''),'{}');
            _jman.setValue(_jman.addChildElement(_pathS2,'Currency'),_tsum.WAL);
            _jman.setValue(_jman.addChildElement(_pathS2,'Lp'),$(_lp+=1));
            _jman.setValue(_jman.addChildElement(_pathS2,'Description'),'Do wypłaty');
            _jman.setValue(_jman.addChildElement(_pathS2,'Amount'),_suma);
            _jman.setValue(_jman.addChildElement(_pathS2,'SettlementKind'),'sum')
         ?};
         _tsum.prefix();
         _tsum.next()
      !}
   ?}
?};

:: Kontrahent
{? EDOKUM.KHKH
|| _jman.setObject(_pathKH:=_jman.addChildElement('','BCAddresHistory'),'{}');
   _jman.setValue(_jman.addChildElement(_pathKH,'Name'),EDOKUM.KHKH().NAZ);
   _jman.setValue(_jman.addChildElement(_pathKH,'FullName'),KH.NAZ_P);
   _jman.setValue(_jman.addChildElement(_pathKH,'NIP'),KH.NIP);
   _jman.setValue(_jman.addChildElement(_pathKH,'Regon'),KH.REG);
   _jman.setValue(_jman.addChildElement(_pathKH,'KRS'),KH.KRS);
   _jman.setValue(_jman.addChildElement(_pathKH,'Address'),
      KH.UL+{? KH.DOM<>''
            || ' '+KH.DOM+{? KH.LOKAL<>'' || '/'+KH.LOKAL || '' ?}
            || ''
            ?}
   );
   _jman.setValue(_jman.addChildElement(_pathKH,'ZipCode'),KH.KPOCZ);
   _jman.setValue(_jman.addChildElement(_pathKH,'City'),KH.MIASTO);
   _jman.setValue(_jman.addChildElement(_pathKH,'Country'),KH.KRAJISO().KODISO);
   _jman.setValue(_jman.addChildElement(_pathKH,'PostOffice'),KH.POCZ);
   _jman.setValue(_jman.addChildElement(_pathKH,'__id_erp'),KH.uidref())
?};

:: Użytkownik
_jman.setObject(_pathU:=_jman.addChildElement('','TenantUser'),'{}');
_jman.setValue(_jman.addChildElement(_pathU,'LoginName'),EDOKUM.USERS().WEBLOGIN);
_jman.setValue(_jman.addChildElement(_pathU,'TenantUserDesc'),USERS.DANE);

{? _wp='D'
|| _jman.setObject(_pathBT:=_jman.addChildElement('','SourceBusinessTrip'),'{}');
   _jman.setValue(_jman.addChildElement(_pathBT,'SourceId_id_erp'),EDOKUM.uidref());
   _id_del:=exec('get_id_cloud','portal_core','PORTAL_SEOD_EDOK_DEL_ID',EDOKUM.uidref());
   _jman.setValue(_jman.addChildElement(_pathBT,'SourceId'),_id_del);

   _jman.setObject(_pathBT:=_jman.addChildElement('','DUMMY'),'{}');
   _jman.setValue(_jman.addChildElement(_pathBT,'DUMMY'),1)
?};

_json:=_jman.getJSON();
_json


\EPODZ_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: _a - wskazania na obiekt JMAN
::----------------------------------------------------------------------------------------------------------------------
_jman:=_a;

EPODZ.cntx_psh();
EPODZ.use('skid_j'+(ref_name(EDOKUM.ref())+2));
EPODZ.index('EDOKUM');
EPODZ.prefix(EDOKUM.ref(),exec('SKID_MBN','portal_seod'));
{? EPODZ.first()
|| _lp:=0;
   _portal:=EDOKUM.TYP().W_PORTAL;
   _suma:=0;
   {? _portal='S'
   || {!
      |? _suma+=EPODZ.WART;
         EPODZ.next()
      !}
   ?};
   EPODZ.first();
   _jman.setObject(_pathP1:=_jman.addChildElement('','CostShare'),'[]');
   {!
   |? _jman.setObject(_pathP2:=_jman.addChildElement(_pathP1,''),'{}');
      _jman.setValue(_jman.addChildElement(_pathP2,'Lp'),(_lp+=1));
      _proc:={? _suma || (EPODZ.WART/_suma*100)$2 || 100 ?};
      _wart:=EPODZ.WART;
      _jman.setValue(_jman.addChildElement(_pathP2,'Prc'),_proc);
      _jman.setValue(_jman.addChildElement(_pathP2,'Amount'),_wart);
      SKID_MBP.cntx_psh();
      SKID_MBP.index('LP'); SKID_MBP.prefix(EPODZ.SKID_MB);
      {? SKID_MBP.first()
      || {!
         |? _field_no:=exec('UD_SEOD_lp','portal_seod',SKID_MBP.UD_SCH,0);
            _jman.setValue(
               _jman.addChildElement(_pathP2,exec('SKID_MBP_map','portal_seod',_field_no,'field')),
               exec('EPODZ_get','portal_seod',SKID_MBP.LP)
            );
            SKID_MBP.next()
         !}
      ?};
      SKID_MBP.cntx_pop();
::    _jman.setValue(_jman.addChildElement(_pathP2,'__id_erp'),EPODZ.uidref());
      EPODZ.next()
   !}
?};
EPODZ.cntx_pop()


\EDOKUM_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Czy EDOKUM jest odpowiedniego typu obiegu
::   WE:  _a  - typ obiegu
::       [_b] - typ portalowy
::       [_c] - typ obiegu, domyślnie EDOKUM.TYPOBIEG
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
_wp:={? var_pres('_b')=type_of('') || _b || '' ?};
_typobieg:={? var_pres('_c')=type_of(null) || _c || EDOKUM.TYPOBIEG ?};
{? _typ='F'
|| {? app_info('web_sesid')<>'' | REF.TFAKTURA=null
   || REF.TFAKTURA:=exec('bl_typ','obiegi',1)
   ?};
   _typobieg=REF.TFAKTURA & (_wp='' | (ETYPY.cntx_psh(); _ok:=EDOKUM.TYP().W_PORTAL=_wp; ETYPY.cntx_pop(); _ok))
|? _typ='W'
|| {? app_info('web_sesid')<>'' | REF.TWNIOSEK=null
   || REF.TWNIOSEK:=exec('bl_typ','obiegi',2)
   ?};
   _typobieg=REF.TWNIOSEK & (_wp='' | (ETYPY.cntx_psh(); _ok:=_wp*EDOKUM.TYP().W_PORTAL; ETYPY.cntx_pop(); _ok))
|? _typ='D'
|| {? app_info('web_sesid')<>'' | REF.TDELEG=null
   || REF.TDELEG:=exec('bl_typ','obiegi',3)
   ?};
   _typobieg=REF.TDELEG & (_wp='' | (ETYPY.cntx_psh(); _ok:=_wp*EDOKUM.TYP().W_PORTAL; ETYPY.cntx_pop(); _ok))
|| 1
?}


\EDOKUM_seod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Czy EDOKUM jest odpowiedniego typu do wysyłki do SEOD
::----------------------------------------------------------------------------------------------------------------------
exec('EDOKUM_typ','portal_seod','F','S') | exec('EDOKUM_typ','portal_seod','D','D')


\SKID_MBN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca wskazanie na model danych dla SEOD
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
_ref:=EDOKUM.TYP().SKID_MB;
ETYPY.cntx_pop();
_ref


\SKID_MBN_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Czy model danych ma być wysyłany do SEOD
::----------------------------------------------------------------------------------------------------------------------
SKID_MBN.CZY_OFZ='T'


\UD_SEOD_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Czy wymiar ma być wysyłany do SEOD
::----------------------------------------------------------------------------------------------------------------------
UD_SEOD.ETYPY<>null


\SKID_MBP_map
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca identyfikator wymiaru
::   WE: _a - wartość
::       _b - typ mapowania: type, field
::----------------------------------------------------------------------------------------------------------------------
_val:=_a;
_typ:=_b;
{? _typ='type'
|| {? _val=1 || 'ST'
   |? _val=2 || 'PR'
   |? _val=3 || 'UM'
   |? _val=4 || 'UR'
   || 'Ad'+$(_val-4)
   ?}
|? _typ='field'
|| {? _val=1 || 'cc_Department'
   |? _val=2 || 'cc_Project'
   |? _val=3 || 'cc_Agreement'
   |? _val=4 || 'cc_Device'
   || 'cc_Additional'+$(_val-4)
   ?}
?}


\UD_DEF_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Czy UD_DEF ma być wysyłany do SEOD
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
UD_SEOD.cntx_psh();
UD_SEOD.index('UD_SCH');
UD_SEOD.prefix();
UD_SEOD.blank(1);
UD_SEOD.UD_SCH:=UD_DEF.UD_SCH;
_ok:=UD_SEOD.find_rec();
UD_SEOD.cntx_pop();
_ok


\UD_DEF_type
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Typ UD_DEF
::----------------------------------------------------------------------------------------------------------------------
_lp:=0;
UD_SEOD.cntx_psh();
UD_SEOD.index('UD_SCH');
UD_SEOD.prefix();
{? UD_SEOD.find_key(UD_DEF.UD_SCH)
|| _lp:=UD_SEOD.LP
?};
UD_SEOD.cntx_pop();
{? _lp
|| exec('SKID_MBP_map','portal_seod',_lp,'type')
|| 'brak'
?}


\EDOKUM_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Dodaje dokument z SEOD do bieżącej maski tabeli EDOKUM
::   WE: [_a] - typ dokumentu: [1]-faktura, 3-delegacja
::   WY: EDOKUM.uidref() lub ~~ gdy nie udało się utworzyć
::----------------------------------------------------------------------------------------------------------------------
_typ:={? var_pres('_a')=type_of(1) || _a || 1 ?};
_ref:=~~;
{? ~EDOKUM.FIRMA || EDOKUM.FIRMA:=REF.FIRMA ?};
{? EDOKUM.DATAW=date(0,0,0) || EDOKUM.DATAW:=date() ?};
{? EDOKUM.DOP=date(0,0,0) || EDOKUM.DOP:=EDOKUM.DATAW ?};
{? ~EDOKUM.USERS || EDOKUM.USERS:=OPERATOR.USER ?};
{? ~EDOKUM.DOSTAWCA || EDOKUM.DOSTAWCA:=EDOKUM.USERS().OSOBA ?};
{? ~EDOKUM.ROK_F
|| ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ'); ROK_F.prefix(EDOKUM.FIRMA);
   {? ROK_F.find_le(date(EDOKUM.DATAW~1,1,1))
   || EDOKUM.ROK_F:=ROK_F.ref()
   ?};
   ROK_F.cntx_pop()
?};
EDOKUM.TYPOBIEG:=EDOKUM.TYP().TYPOBIEG;
{? EDOKUM.TR=''
|| EDOKUM.TR:=EDOKUM.TYP().NAZWA
?};
VAR_DEL.delete('no_us'); no_us:=1;
{? EDOKUM.TYP & EDOKUM.add()
|| _ref:=EDOKUM.uidref();
   {? EDOKUM.ID=''
   || exec('ustal_numer','obiegi',_typ,EDOKUM.ROK_F,1,0);
      {? var_pres('id_edok')>0
      || EDOKUM.ID:=id_edok;
         EDOKUM.put()
      ?}
   ?}
?};
VAR_DEL.delete('no_us');
_ref


\EDOKUM_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie dodatkowych danych faktury/delegacji w obiegu
::   WE:  _a  - tablica JSON
::       [_b] - typ dokumentu: [1]-faktura, 3-delegacja
::----------------------------------------------------------------------------------------------------------------------
_json:=_a;
_typ:={? var_pres('_b')=type_of(1) || _b || 1 ?};
_maska:=ref_name(EDOKUM.ref())+2;

:: Pozycje faktury i Pozycje VAT (linijki)
{? _typ=1 & var_pres('Lines',_json)>0
|| _line1:=_json.Lines[1];
   _pozf:=0;
   {? var_pres('_line1')>0 & var_pres('Description',_line1)=type_of('') & 4+_line1.Description<>'Suma'
   || _pozf:=1
   ?};
   {? _pozf
   || POZF.cntx_psh();
      POZF.use('pozf__'+_maska);
      POZF.index('EDOKUM'); POZF.prefix(EDOKUM.ref());
      _max:=0;
      {! _ii:=1..obj_len(_json.Lines)
      |! _line:=_json.Lines[_ii];
         {? var_pres('_line')>0
         || _lp:=_line.LP;
            {? _lp>_max || _max:=_lp ?};
            _add:=1;
            {? POZF.find_key(_lp)
            || _add:=0
            || POZF.blank(1);
               POZF.EDOKUM:=EDOKUM.ref();
               POZF.LP:=_lp
            ?};
            {? var_pres('Description',_line)=type_of('')
            || POZF.NAZ:=_line.Description
            ?};
            {? var_pres('SymbolVR',_line)=type_of('')
            || _st:=_line.SymbolVR;
               POZF.SV:=exec('st_vat','%seod',_st)
            ?};
            {? var_pres('Quantity',_line)=type_of(1)
            || POZF.IL:=_line.Quantity
            ?};
            {? var_pres('Price',_line)=type_of(1)
            || POZF.C:=_line.Price
            ?};
            {? var_pres('NetValue',_line)=type_of(1)
            || POZF.WN:=_line.NetValue
            ?};
            {? var_pres('VATValue',_line)=type_of(1)
            || POZF.WV:=_line.VATValue
            ?};
            POZF.WB:=POZF.WN+POZF.WV;
            {? var_pres('JM',_line)=type_of('')
            || POZF.JM:=exec('FindInSet','#table','JM','KOD',_line.JM,,,1)
            ?};
            {? _add || POZF.add() || POZF.put() ?}
         ?};
         &_line
      !};
      _max+=1;
      {? POZF.find_key(_max)
      || {!
         |! POZF.LP>=_max & POZF.del()
         !}
      ?};

::    Uzupełnienie pozycji VAT na podstawie pozycji faktury
      EVAT.cntx_psh();
      EVAT.use('skid_a'+_maska);
      EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
      {? EVAT.first() || {! |? EVAT.del !} ?};
      POZF.index('ESV'); POZF.prefix(EDOKUM.ref());
      {? POZF.first()
      || {!
         |? _wn:=_wv:=_wb:=0;
            POZF.prefix(EDOKUM.ref(),POZF.SV);
            {? POZF.first()
            || {!
               |? _wn+=POZF.WN;
                  _wv+=POZF.WV;
                  _wb+=POZF.WB;
                  POZF.next()
               !}
            ?};
            EVAT.blank(1);
            EVAT.EDOKUM:=EDOKUM.ref();
            EVAT.PR:=POZF.SV;
            EVAT.NETTO:=_wn;
            EVAT.VAT:=_wv;
            EVAT.BRUTTO:=_wb;
            EVAT.add();
            POZF.prefix(EDOKUM.ref());
            POZF.next()
         !}
      ?};
      EVAT.cntx_pop();
      POZF.cntx_pop()
   || EVAT.cntx_psh();
      EVAT.use('skid_a'+_maska);
      EVAT.index('EDOKUM'); EVAT.prefix(EDOKUM.ref());
      {? EVAT.first() || {! |? EVAT.del !} ?};
      {! _ii:=1..obj_len(_json.Lines)
      |! _line:=_json.Lines[_ii];
         {? var_pres('_line')>0
         || EVAT.blank(1);
            EVAT.EDOKUM:=EDOKUM.ref();
            {? var_pres('SymbolVR',_line)=type_of('')
            || _st:=_line.SymbolVR;
               EVAT.PR:=exec('st_vat','%seod',_st)
            ?};
            {? var_pres('NetValue',_line)=type_of(1)
            || EVAT.NETTO:=_line.NetValue
            ?};
            {? var_pres('VATValue',_line)=type_of(1)
            || EVAT.VAT:=_line.VATValue
            ?};
            EVAT.BRUTTO:=EVAT.NETTO+EVAT.VAT;
            EVAT.add()
         ?};
         &_line
      !};
      EVAT.cntx_pop()
   ?}
?};

:: Podziały controllingowe
exec('EPODZ_parse','portal_seod',_json);

:: Kontrahent - dene adresowe
{? var_pres('BCAddresHistory',_json)>0
|| _kh:=_json.BCAddresHistory[1];
   _put:=0;
   {? var_pres('FullName',_kh)=type_of('')
   || EDOKUM.KH_FULL:=_kh.FullName;
      _put:=1
   ?};
   {? var_pres('Address',_kh)=type_of('')
   || EDOKUM.UL:=_kh.Address;
      _put:=1
   ?};
   {? var_pres('PostOffice',_kh)=type_of('')
   || EDOKUM.POCZ:=_kh.PostOffice;
      _put:=1
   ?};
   {? var_pres('ZipCode',_kh)=type_of('')
   || EDOKUM.KPOCZ:=_kh.ZipCode;
      _put:=1
   ?};
   {? var_pres('City',_kh)=type_of('')
   || EDOKUM.MIASTO:=_kh.City;
      _put:=1
   ?};
   {? _typ=3 & var_pres('__id_erp',_kh)=type_of('')
   || EDOKUM.KHKH:=exec('FindAndGet','#table','KH',_kh.__id_erp,,,null);
      EDOKUM.KH:=exec('EDOKUM_kh2slo','%seod');
      _put:=1
   ?};
   {? _put || EDOKUM.put() ?}
?};

:: Przejazdy
{? _typ=3 & var_pres('Way',_json)>0
|| EDOKUMP.cntx_psh();
   EDOKUMP.use('skidpu'+_maska);
   EDOKUMP.index('CHRONO');
   EDOKUMP.prefix(EDOKUM.ref());
   {? EDOKUMP.first() || {! |? EDOKUMP.del !} ?};
   EDOKUMP.prefix();
   _ways:=_json.Way;
   {! _ii:=1..obj_len(_ways)
   |! _way:=_ways[_ii];
      EDOKUMP.blank();
      EDOKUMP.EDOKUM:=EDOKUM.ref();
      {? var_pres('DepartureTime',_way)=type_of('')
      || EDOKUMP.DATA_WYJ:=exec('str2date','#convert',10+_way.DepartureTime);
         EDOKUMP.GODZ_WYJ:=exec('str2time','#convert',8+(11-_way.DepartureTime))
      ?};
      {? var_pres('DepartureLocation',_way)=type_of('')
      || EDOKUMP.MIE_WYJ:=_way.DepartureLocation
      ?};
      {? var_pres('DepartureCountry',_way)=type_of('')
      || EDOKUMP.KRAJ_WYJ:=exec('FindInSet','#table','KRAJE','KRAJE',_way.DepartureCountry,,,1)
      ?};
      {? var_pres('ArrivalTime',_way)=type_of('')
      || EDOKUMP.DATA_PRZ:=exec('str2date','#convert',10+_way.ArrivalTime);
         EDOKUMP.GODZ_PRZ:=exec('str2time','#convert',8+(11-_way.ArrivalTime))
      ?};
      {? var_pres('ArrivalLocation')=type_of('')
      || EDOKUMP.MIE_PRZ:=_way.ArrivalLocation
      ?};
      {? var_pres('ArrivalCountry',_way)=type_of('')
      || EDOKUMP.KRAJ_PRZ:=exec('FindInSet','#table','KRAJE','KRAJE',_way.ArrivalCountry,,,1)
      ?};
      {? ~EDOKUMP.KRAJ_WYJ & EDOKUMP.KRAJ_PRZ
      || EDOKUMP.KRAJ_WYJ:=EDOKUMP.KRAJ_PRZ
      ?};
      {? var_pres('Distance',_way)=type_of(1)
      || EDOKUMP.LICZ_KM:=_way.Distance
      ?};
      {? var_pres('VehicleKind',_way)=type_of('')
      || INNESRTR.cntx_psh();
         INNESRTR.index('NAZWA');
         {? INNESRTR.find_key(_way.VehicleKind,)
         || {? INNESRTR.DODAT<>'T'
            || EDOKUMP.SRODTRAN:='I';
               EDOKUMP.INNESRTR:=INNESRTR.ref()
            |? INNESRTR.NAZWA*'prywatny'
            || EDOKUMP.SRODTRAN:='P'
            || EDOKUMP.SRODTRAN:='S'
            ?}
         ?};
         INNESRTR.cntx_pop()
      ?};
      {? EDOKUMP.SRODTRAN<>'I'
      || {? var_pres('PlateNumber',_way)=type_of('')
         || EDOKUMP.SAMREF:=$exec('pojazd','%seod',_way.PlateNumber)
         ?}
      ?};
      {? var_pres('BorderCrossTime',_way)=type_of('')
      || EDOKUMP.DATA_GR:=exec('str2date','#convert',10+_way.BorderCrossTime);
         EDOKUMP.GODZ_GR:=exec('str2time','#convert',8+(11-_way.BorderCrossTime))
      ?};
      exec('edkp_dwyj_ae','przejazdy');
      &_way;
      EDOKUMP.add()
   !};
   EDOKUMP.cntx_pop()
?};

:: Diety
{? _typ=3 & var_pres('TimeTable',_json)>0
|| EDOKUMD.cntx_psh();
   EDOKUMD.use('bdiety'+_maska);
   EDOKUMD.index('DELEG');
   EDOKUMD.prefix(EDOKUM.ref());
   {? EDOKUMD.first() || {! |? EDOKUMD.del() !} ?};
   EDOKUMD.prefix();
   _diety:=_json.TimeTable;
   {! _ii:=1..obj_len(_diety)
   |! _dieta:=_diety[_ii];
      EDOKUMD.blank();
      EDOKUMD.EDOKUM:=EDOKUM.ref();
      EDOKUMD.Z_CZY_G:='Z';
      {? var_pres('StartTime',_dieta)=type_of('')
      || EDOKUMD.DATA_OD:=exec('str2date','#convert',10+_dieta.StartTime);
         EDOKUMD.GODZ_OD:=exec('str2time','#convert',8+(11-_dieta.StartTime))
      ?};
      {? var_pres('EndTime',_dieta)=type_of('')
      || EDOKUMD.DATA_DO:=exec('str2date','#convert',10+_dieta.EndTime);
         EDOKUMD.GODZ_DO:=exec('str2time','#convert',8+(11-_dieta.EndTime))
      ?};
      {? var_pres('Country',_dieta)=type_of('')
      || EDOKUMD.KRAJ:=exec('FindInSet','#table','KRAJE','KRAJE',_dieta.Country,,,1)
      ?};
      {? EDOKUMD.KRAJ().SYM='PL'
      || EDOKUMD.WAL:=exec('FindInSet','#table','WAL','WAL_SYM','PLN',,,1,,null)
      || EDOKUMD.WAL:=exec('stawki_zagr','diety',EDOKUMD.KRAJ().SYM,EDOKUM.DATA_OD,'W')
      ?};
      {? var_pres('Localization',_dieta)=type_of('')
      || EDOKUMD.MIEJSCE:=_dieta.Localization
      ?};
      _ld:=EDOKUMD.DATA_DO-EDOKUMD.DATA_OD;
      {? var_pres('IsMealProvided',_dieta)=type_of(1)
      || EDOKUMD.CZY_DIET:=~_dieta.IsMealProvided
      ?};
      EDOKUMD.CZY_DIET:=1;
      {? var_pres('BreakfastProvidedQty',_dieta)=type_of(1)
      || EDOKUMD.LICZ_SNI:=_dieta.BreakfastProvidedQty;
         EDOKUMD.SNIAD:=EDOKUMD.LICZ_SNI>0;
         EDOKUMD.W_SNI:=EDOKUMD.LICZ_SNI>0 & EDOKUMD.LICZ_SNI=_ld
      ?};
      {? var_pres('DinnerProvidedQty',_dieta)=type_of(1)
      || EDOKUMD.LICZ_OBI:=_dieta.LunchProvidedQty;
         EDOKUMD.OBIAD:=EDOKUMD.LICZ_OBI>0;
         EDOKUMD.W_OBI:=EDOKUMD.LICZ_OBI>0 & EDOKUMD.LICZ_OBI=_ld
      ?};
      {? var_pres('DinnerProvidedQty',_dieta)=type_of(1)
      || EDOKUMD.LICZ_KOL:=_dieta.DinnerProvidedQty;
         EDOKUMD.KOLACJA:=EDOKUMD.LICZ_KOL>0;
         EDOKUMD.W_KOL:=EDOKUMD.LICZ_KOL>0 & EDOKUMD.LICZ_KOL=_ld
      ?};
      {? var_pres('AccommodationAllowance',_dieta)=type_of(1)
      || EDOKUMD.LICZ_NOC:=_dieta.AccommodationAllowance;
         EDOKUMD.CZY_NOC:=EDOKUMD.LICZ_NOC>0
      ?};
      {? var_pres('PublicTransportAllowance',_dieta)=type_of(1)
      || EDOKUMD.LICZ_DOJ:=_dieta.PublicTransportAllowance;
         EDOKUMD.CZY_DOJ:=EDOKUMD.LICZ_DOJ>0
      ?};
      exec('przelicz_diet','diety');
::    EDOKUMD.LICZ_NOC:=exec('wylicz_nocleg','diety');
      EDOKUMD.RYCZ_NOC:=exec('rycz_noc','diety');
::    EDOKUMD.LICZ_DOJ:=exec('wylicz_nocleg','diety',1);
      EDOKUMD.RYCZ_DOJ:=exec('rycz_doj','diety');
      exec('kwota_refresh','diety');
      EDOKUMD.add();
      &_dieta
   !};
   EDOKUMD.cntx_pop()
?};

:: Wydatki i zapotrzebowania
{? var_pres('LinkDocuments',_json)>0
|| _first:=1;
   _linki:=_json.LinkDocuments;
   {! _ii:=1..obj_len(_linki)
   |! _link:=_linki[_ii];
      {? var_pres('LinkType',_link)>0
      || {? _typ=3 & _link.LinkType='PurchaseDocument-BusinessTrip'
         || _id:=$_link.DestId;
            EDOK_ZAL.cntx_psh();
            EDOK_ZAL.use('edokzl_1');
            EDOK_ZAL.index('ID_CLOUD');
            EDOK_ZAL.prefix();
            {? EDOK_ZAL.find_key(_id,)
            || _ref_uid:=EDOK_ZAL.uidref();
               EDOK_ZAL.cntx_psh();
               EDOK_ZAL.use('edokzl'+_maska);
               EDOK_ZAL.prefix();
               EDOK_ZAL.EDOKUM:=EDOKUM.ref();
               _add:=EDOK_ZAL.add();
               _nref_uid:=EDOK_ZAL.uidref();
               EDOK_ZAL.cntx_pop();
               {? _add
               || exec('change_rek','#sync_id','PORTAL_SEOD_EDOK_ZAL_ID',_ref_uid,_nref_uid);
                  EDOK_ZAL.del()
               ?}
            ?};
            EDOK_ZAL.cntx_pop()
         |? _typ=1 & _link.LinkType='PurchaseDocument-ZapMat'
         || {? _first
            || _first:=0;
               EDOKRZAP.cntx_psh();
               EDOKRZAP.use('skid_q'+_maska);
               EDOKRZAP.index('EDOKUM');
               EDOKRZAP.prefix(EDOKUM.ref());
               {? EDOKRZAP.first() || {! |? EDOKRZAP.del() !} ?};
               EDOKRZAP.cntx_pop()
            ?};
            _sid:=_link.SourceId;
            _zapot:=exec('get_id_erp','portal_core','PORTAL_EDOKUM_ID',_sid);
            {? _zapot
            || _ref:=null;
               EDOKUM.cntx_psh();
               EDOKUM.use(ref_name(_zapot));
               EDOKUM.prefix();
               {? EDOKUM.seek(_zapot)
               || _ref:=EDOKUM.ref();
                  _rok:=EDOKUM.ROK_F;
                  _rok_naz:=EDOKUM.ROK_F().NAZ;
                  _id:=EDOKUM.ID;
                  _tr:=EDOKUM.TR
               ?};
               EDOKUM.cntx_pop();
               {? _ref
               || EDOKRZAP.cntx_psh();
                  EDOKRZAP.use('skid_q'+_maska);
                  EDOKRZAP.index('EDOKUM');
                  EDOKRZAP.prefix(EDOKUM.ref(),_rok_naz,_id,);
                  {? EDOKRZAP.first()
                  || EDOKRZAP.NETTO:=_link.SettValue;
                     EDOKRZAP.put()
                  || EDOKRZAP.blank(1);
                     EDOKRZAP.EDOKUM:=EDOKUM.ref();
                     EDOKRZAP.EZAPOT:=_ref;
                     EDOKRZAP.ROK_F:=_rok;
                     EDOKRZAP.NETTO:=_link.SettValue;
                     EDOKRZAP.ID:=_id;
                     EDOKRZAP.TR:=_tr;
                     EDOKRZAP.add()
                  ?};
                  EDOKRZAP.cntx_pop()
               ?}
            ?}
         ?}
      ?};
      &_link
   !}
?}


\EPODZ_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: _a - wskazanie na tablicę z JSON-em
::----------------------------------------------------------------------------------------------------------------------
_json:=_a;

{? var_pres('CostShare',_json)>0
|| _portal:=EDOKUM.TYP().W_PORTAL;
   EPODZ.cntx_psh();
   EPODZ.use('skid_j'+(ref_name(EDOKUM.ref())+2));
   EPODZ.index('EDOKUM'); EPODZ.prefix(EDOKUM.ref());
   {? EPODZ.first() || {! |? EPODZ.del !} ?};
   {! _ii:=1..obj_len(_json.CostShare)
   |! _cs:=_json.CostShare[_ii];
      {? var_pres('_cs')>0
      || EPODZ.blank(1);
         EPODZ.EDOKUM:=EDOKUM.ref();
         EPODZ.SKID_MB:=exec('SKID_MBN','portal_seod');
         {? var_pres('Amount',_cs)=type_of(1)
         || EPODZ.WART:=_cs.Amount
         ?};
         SKID_MBP.cntx_psh();
         SKID_MBP.index('LP'); SKID_MBP.prefix(EPODZ.SKID_MB);
         {? SKID_MBP.first()
         || UD_DEF.cntx_psh();
            UD_DEF.index('SCHSYM');
            {!
            |? _field_no:=exec('UD_SEOD_lp','portal_seod',SKID_MBP.UD_SCH,0);
               _fld:=exec('SKID_MBP_map','portal_seod',_field_no,'field');
               {? var_pres(_fld,_cs)>0
               || _sym:=($('_a.'+_fld))(_cs);
                  {? UD_DEF.find_key(SKID_MBP.UD_SCH,_sym,)
                  || exec('EPODZ_set','portal_seod',SKID_MBP.LP,UD_DEF.UD_SKL)
                  ?}
               ?};
               SKID_MBP.next()
            !};
            UD_DEF.cntx_pop()
         ?};
         SKID_MBP.cntx_pop();
         {? EPODZ.PBUD
         || SKIDXDUD.cntx_psh();
            SKIDXDUD.index('POZ'); SKIDXDUD.prefix(EPODZ.PBUD);
            {? SKIDXDUD.first()
            || {? SKIDXDUD.WART_IL='W'
               || EPODZ.WAL:={? SKIDXDUD.WSK_WAL='T'
                             || EDOKUM.WAL
                             || exec('wal_nar','dok_fks')
                             ?}
               ?}
            ?};
            SKIDXDUD.cntx_pop()
         ?};
         EPODZ.add();
         &_cs
      ?}
   !};
   EPODZ.cntx_pop()
?}


\EPODZ_fld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca pole z elementem wymiaru
::   WE: _a - nr wymiaru
::----------------------------------------------------------------------------------------------------------------------
_lp:=_a;
{? _lp=1 || 'PBUD'
|? _lp=2 || 'JORG'
|? _lp=3 || 'OKOSZ'
|| 'WYM'+$_lp
?}


\EPODZ_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Ustawia element wymiaru
::   WE: _a - numer wymiaru
::       _b - wskazanie na element wymiaru
::----------------------------------------------------------------------------------------------------------------------
_lp:=_a;
_ud_skl:=_b;
_fld:=exec('EPODZ_fld','portal_seod',_lp);
($('EPODZ.'+_fld+':=_a'))(_ud_skl)


\EPODZ_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca symbol elementu wymiaru
::   WE: _a - numer wymiaru
::----------------------------------------------------------------------------------------------------------------------
_lp:=_a;
_fld:=exec('EPODZ_fld','portal_seod',_lp);
($('EPODZ.'+_fld+'().SYMBOL'))()


\ETYPY_type
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Czy typ dotyczy odpowiedniego typu obiegu
::   WE: _a - F-obieg faktur, W-obieg wniosków
::----------------------------------------------------------------------------------------------------------------------
{? _a='F'
|| {? REF.TFAKTURA=null
   || REF.TFAKTURA:=exec('bl_typ','obiegi',1)
   ?};
   ETYPY.TYPOBIEG=REF.TFAKTURA
|? _a='W'
|| {? REF.TWNIOSEK=null
   || REF.TWNIOSEK:=exec('bl_typ','obiegi',2)
   ?};
   ETYPY.TYPOBIEG=REF.TWNIOSEK
|| 1
?}


\UD_SEOD_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Inicjuje tabelę UD_SEOD dla modelu powiązanego z typem faktury
::   WE: _a - tryb: 1-inicjuje schematy, 2-inicjuje schematy typów faktur, 3-inicjuje schamty typu faktury
::----------------------------------------------------------------------------------------------------------------------
_tryb:=_a;
{? _tryb=1
|| UD_SEOD.cntx_psh();
   UD_SEOD.index('UD_SCH');
   UD_SEOD.prefix();
   {? UD_SEOD.ETYPY().SKID_MB
   || SKID_MBP.cntx_psh();
      SKID_MBP.index('LP');
      SKID_MBP.prefix(ETYPY.SKID_MB);
      {? SKID_MBP.first()
      || {!
         |? {? ~UD_SEOD.find_key(null,SKID_MBP.UD_SCH)
            || UD_SEOD.cntx_psh();
               UD_SEOD.index('LP');
               UD_SEOD.prefix(null);
               _lp:={? UD_SEOD.last() || UD_SEOD.LP+1 || 1 ?};
               UD_SEOD.cntx_pop();
               UD_SEOD.blank(1);
               UD_SEOD.LP:=_lp;
               UD_SEOD.UD_SCH:=SKID_MBP.UD_SCH;
               UD_SEOD.add()
            ?};
            SKID_MBP.next()
         !}
      ?};
      SKID_MBP.cntx_pop()
   ?};
   UD_SEOD.cntx_pop()
|? _tryb=2
|| ETYPY.cntx_psh();
   ETYPY.index('UNIK_WP');
   ETYPY.prefix(exec('bl_typ','obiegi',1),'S',);
   {? ETYPY.first()
   || {!
      |? {? ETYPY.SKID_MB
         || exec('UD_SEOD_init','portal_seod',3)
         ?};
         ETYPY.next()
      !}
   ?};
   ETYPY.cntx_pop()
|? _tryb=3
|| SKID_MBP.cntx_psh();
   SKID_MBP.index('LP');
   SKID_MBP.prefix(ETYPY.SKID_MB);
   {? SKID_MBP.first()
   || UD_SEOD.cntx_psh();
      UD_SEOD.index('UD_SCH');
      UD_SEOD.prefix();
      {!
      |? {? UD_SEOD.find_key(ETYPY.ref(),SKID_MBP.UD_SCH)
         || UD_SEOD.NAZ:=SKID_MBP.NAZ;
            UD_SEOD.LP:=exec('UD_SEOD_lp','portal_seod',UD_SEOD.UD_SCH);
            UD_SEOD.put()
         || UD_SEOD.blank(1);
            UD_SEOD.ETYPY:=ETYPY.ref();
            UD_SEOD.UD_SCH:=SKID_MBP.UD_SCH;
            UD_SEOD.LP:=exec('UD_SEOD_lp','portal_seod',UD_SEOD.UD_SCH);
            UD_SEOD.NAZ:=SKID_MBP.NAZ;
            UD_SEOD.add()
         ?};
         SKID_MBP.next()
      !};
      UD_SEOD.cntx_pop()
   ?};
   SKID_MBP.cntx_pop()
?}


\UD_SEOD_lp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca lp dla schematu danych
::   WE: _a  - wskazanie na schemat danych (_UD_SCH)
::      [_b] - dodawać gdy brakuje? [1]-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_sch:=_a;
_add:={? var_pres('_b')=type_of(1) || _b || 1 ?};
UD_SEOD.cntx_psh();
UD_SEOD.index('UD_SCH');
UD_SEOD.prefix(null,_sch);
_lp:={? UD_SEOD.first() || UD_SEOD.LP ?};
{? _lp=0 & _add
|| UD_SEOD.index('LP');
   UD_SEOD.prefix(null);
   _lp:={? UD_SEOD.last() || UD_SEOD.LP+1 || 1 ?};
   UD_SEOD.blank(1);
   UD_SEOD.LP:=_lp;
   UD_SEOD.UD_SCH:=_sch;
   UD_SEOD.add()
?};
UD_SEOD.cntx_pop();
_lp


\SKID_MBP_type
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca typ wymiaru w SEOD
::   WE: _a - wskazanie na schemat danych (_UD_SCH)
::----------------------------------------------------------------------------------------------------------------------
_sch:=_a;
_lp:= exec('UD_SEOD_lp','portal_seod',_sch);
exec('SKID_MBP_map','portal_seod',_lp,'type')


\EDOKUM_details
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Ustawia informacje szczegółowe dotyczące faktury
::   WE: _a - tablica etykiet
::       _b - tablica wartości
::       _c - typ dokumentu: 1-faktura, 3-delegacja
::----------------------------------------------------------------------------------------------------------------------
_caption:=_a;
_value:=_b;
_type:=_c;
{? _type=1
|| _caption[1][1]:='Kontrahent';
   _value[1][1]:=EDOKUM.KH_FULL;
   _caption[1][2]:='NIP';
   _value[1][2]:=EDOKUM.KHKH().NIP;

   _caption[2][1]:='Brutto';
   _value[2][1]:=form(EDOKUM.WART,,2,' ,')+' '+EDOKUM.WAL().KOD;
   _caption[2][2]:='Termin płatności';
   _value[2][2]:=gsub($EDOKUM.TP,'/','-')
|? _type=3
|| _caption[1][1]:='Data';
   _value[1][1]:=gsub($EDOKUM.DATA_OD,'/','-')+' - '+gsub($EDOKUM.DATA_DO,'/','-');
   _caption[1][2]:='Miejsce';
   _value[1][2]:=EDOKUM.DEL_MIE;

   _caption[2][1]:='Cel';
   _value[2][1]:=EDOKUM.DEL_CEL().TR;
   _caption[2][2]:='Opis';
   _value[2][2]:=EDOKUM.TR
?};
~~


\akc_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Ustawia element AcceptanceCondition
::   WE: _a - obiekt JMAN
::       _b - element nadrzędny w json
::       _c - element AcceptanceCondition
::----------------------------------------------------------------------------------------------------------------------
_jman:=_a;
_path:=_b;
_pathAC:={? var_pres('_c')=type_of('') || _c || '' ?};

EDOKPAR.cntx_psh();
EDOKPAR.use('skidh_'+(ref_name(EDOKUM.ref())+2));
EDOKPAR.index('UNIK');
EDOKPAR.prefix(EDOKUM.ref(),EDOKOS.B_PREL,);
{? EDOKPAR.first()
|| {? _pathAC=''
   || _pathAC:=_jman.addChildElement(_path,'AcceptanceCondition');
      _jman.setObject(_pathAC,'[]')
   ?};

:: Dodatkowe parametry dla faktury
   {? EDOKUM.TYP().W_PORTAL='S'
   || _jman.setObject(_pathACE:=_jman.addChildElement(_pathAC,''),'{}');
      _jman.setValue(_jman.addChildElement(_pathACE,'DataGroup'), 'PurchaseDocument');
:: _jman.setValue(_jman.addChildElement(_pathACE,'Required'), 1);
      _jman.setValue(_jman.addChildElement(_pathACE,'CanEdit'), EDOKPAR.ED_OPOP='T');
      _jman.setObject(_pathACEFs:=_jman.addChildElement(_pathACE,'FieldRules'),'[]');
      _jman.setObject(_pathACEF:=_jman.addChildElement(_pathACEFs,''),'{}');
      _jman.setValue(_jman.addChildElement(_pathACEF,'FieldName'), 'Description');
:: _jman.setValue(_jman.addChildElement(_pathACEF,'Required'), 1);
      _jman.setValue(_jman.addChildElement(_pathACEF,'Editable'), 1);
      _jman.setObject(_pathACEF:=_jman.addChildElement(_pathACEFs,''),'{}');
      _jman.setValue(_jman.addChildElement(_pathACEF,'FieldName'), 'PaymentType');
      _jman.setValue(_jman.addChildElement(_pathACEF,'Required'), 1);
      _jman.setValue(_jman.addChildElement(_pathACEF,'Editable'), 1);
      _jman.setObject(_pathACEF:=_jman.addChildElement(_pathACEFs,''),'{}');
      _jman.setValue(_jman.addChildElement(_pathACEF,'FieldName'), 'DueDate');
      _jman.setValue(_jman.addChildElement(_pathACEF,'Required'), 1);
      _jman.setValue(_jman.addChildElement(_pathACEF,'Editable'), 1);

      ETYP_ATR.cntx_psh();
      ETYP_ATR.index('KOLZ');
      ETYP_ATR.prefix(EDOKUM.TYP,null);
      {? ETYP_ATR.first()
      || {!
         |? _jman.setObject(_pathACEF:=_jman.addChildElement(_pathACEFs,''),'{}');
            _jman.setValue(_jman.addChildElement(_pathACEF,'FieldName'),ETYP_ATR.TAT().ID_WP);
            _jman.setValue(_jman.addChildElement(_pathACEF,'Required'), ETYP_ATR.WYMAG='T');
            _jman.setValue(_jman.addChildElement(_pathACEF,'Editable'), 1);
            ETYP_ATR.next()
         !}
      ?};
      ETYP_ATR.cntx_pop();

      _jman.setObject(_pathACE:=_jman.addChildElement(_pathAC,''),'{}');
      _jman.setValue(_jman.addChildElement(_pathACE,'DataGroup'), 'CostShare');
      _jman.setValue(_jman.addChildElement(_pathACE,'Required'), EDOKPAR.ED_PODZ='W');
      _jman.setValue(_jman.addChildElement(_pathACE,'CanEdit'), EDOKPAR.ED_PODZ<>'P');

      _jman.setObject(_pathACE:=_jman.addChildElement(_pathAC,''),'{}');
      _jman.setValue(_jman.addChildElement(_pathACE,'DataGroup'), 'Attachment');
:: _jman.setValue(_jman.addChildElement(_pathACE,'Required'), 1);
      _jman.setValue(_jman.addChildElement(_pathACE,'CanEdit'), 1);

      _jman.setObject(_pathACE:=_jman.addChildElement(_pathAC,''),'{}');
      _jman.setValue(_jman.addChildElement(_pathACE,'DataGroup'), 'PurchaseDocLines');
      _jman.setValue(_jman.addChildElement(_pathACE,'Required'), EDOKUM.TYP().TYPVATPR=0);
      _jman.setValue(_jman.addChildElement(_pathACE,'CanEdit'), EDOKPAR.ED_POZV='T' | EDOKPAR.ED_POZF='T')
   ?};

:: Przekazanie użytkowników następnej akceptacji
   OBIEGI.B_ROLE:=null;
   {? EDOKPAR.B_ROLE
   || OBIEGI.B_ROLE:=EDOKPAR.B_ROLE
   |? EDOKPAR.B_ROLEF
   || OBIEGI.B_ROLE:=($EDOKPAR.B_ROLEF().TRESC)()
   ?};
   {? OBIEGI.B_ROLE
   || exec('B_ROLE_usr_json','portal_seod',_jman,_pathAC,OBIEGI.B_ROLE,EDOKPAR.JEDEN_OP='T')
   ?}
?};
EDOKPAR.cntx_pop();
~~


\B_ROLE_usr_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Uzupełnia json-a o listę użytkowników następnego etapu
::   WE: _a - obiekt JMAN
::       _b - element nadrzędny
::       _c - rola
::       _d - wybór jednego użytkownika
::----------------------------------------------------------------------------------------------------------------------
_jman:=_a;
_pathAC:=_b;
_b_role:=_c;
_jeden:=_d;
B_USRROL.cntx_psh();
B_USRROL.index('B_ROLE'); B_USRROL.prefix(_b_role);
{? B_USRROL.first()
|| _jman.setObject(_pathACE:=_jman.addChildElement(_pathAC,''),'{}');
   _jman.setValue(_jman.addChildElement(_pathACE,'DataGroup'), 'SelectOperator');
   _jman.setValue(_jman.addChildElement(_pathACE,'Required'), 1);
   _jman.setValue(_jman.addChildElement(_pathACE,'CanEdit'), 1);
   _jman.setValue(_jman.addChildElement(_pathACE,'SelectOne'), _jeden);
   _jman.setObject(_pathACEL:=_jman.addChildElement(_pathACE,'OperatorList'),'[]');
   {!
   |? _jman.setObject(_pathACELE:=_jman.addChildElement(_pathACEL,''),'{}');
      _jman.setValue(_jman.addChildElement(_pathACELE,'TenantUserId__id_erp'), B_USRROL.USERS().WEBLOGIN);
      _jman.setValue(_jman.addChildElement(_pathACELE,'TenantUserDesc'), B_USRROL.USERS().DANE);

      B_USRROL.next()
   !}
?};
B_USRROL.cntx_pop();
~~


\EDOKUSER_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Pobranie użytkowników przekazanych z akceptacji
::   WE: _a - B_PREL
::       _b - B_PRELS
::       _c - tablica z elementami JSON
::----------------------------------------------------------------------------------------------------------------------
_b_prel:=_a;
_b_prels:=_b;
_json:=_c;

:: Z akceptacji
{? var_pres('Acceptance',_json)>0
|| {? obj_len(_json.Acceptance)>0
   || _el:=_json.Acceptance[1]
   ?}
:: z rejesracji
|| _el:=_json
?};

{? var_pres('_el')>0 & var_pres('SelectedOperators',_el)>0
|| _str_sos:=_el.SelectedOperators;
   {? var_pres('_str_sos')=type_of('')
   || _sos:=json_parse(_str_sos)
   || _sos:=_str_sos
   ?};
   {? obj_len(_sos)>0
   || EDOKUSER.cntx_psh();
      EDOKUSER.use('skidk_'+(ref_name(EDOKUM.ref())+2));
      EDOKUSER.index('UNIK');
      EDOKUSER.prefix(EDOKUM.ref(),_b_prel,);
      {? EDOKUSER.first() || {! |? EDOKUSER.del() !} ?};
      USERS.cntx_psh();
      USERS.index('WEBLOGIN');
      USERS.prefix();
      {! _ii:=1..obj_len(_sos)
      |! _so:=_sos[_ii];
         {? var_pres('TenantUserId__id_erp',_so)>0
         || _email:=_so.TenantUserId__id_erp;
            {? USERS.find_key(_email,)
            || EDOKUSER.blank(1);
               EDOKUSER.EDOKUM:=EDOKUM.ref();
               EDOKUSER.USERS:=USERS.ref();
               EDOKUSER.B_PREL:=_b_prel;
               EDOKUSER.B_PRELS:=_b_prels;
               EDOKUSER.add(1)
            ?}
         ?};
         &_so
      !};
      USERS.cntx_pop();
      EDOKUSER.cntx_pop()
   ?};
   &_sos
?};
~~


\st_vat_map
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Mapuje kod stawki VAT na wartość wysyłaną do SEOD
::   WE: _a - kod stawki VAT
::----------------------------------------------------------------------------------------------------------------------
_st:=_a;
{? _st='Zwol.'
|| _st:='ZW'
|? _st=' -zw0'
|| _st:='-zw0'
?};
_st


\TAT_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Aktualizuje listę typów atrybutów innych danych
::----------------------------------------------------------------------------------------------------------------------
_str:='s,d,n';
TAT.cntx_psh();
TAT.index('ID_WP2');
TAT.prefix('S');
{? ~TAT.first()
|| _ile:=10;
   _arr:=spli_str(_str,',');
   {! _jj:=1..obj_len(_arr)
   |! {! _ii:=1.._ile
      |! exec('TAT_add','portal_seod',_arr[_jj],_ii)
      !}
   !}
?};
TAT.cntx_pop()


\TAT_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Dodaje typy atrybutów innych danych
::   WE: _a - typ: s,d,n
::       _b - numer pola
::----------------------------------------------------------------------------------------------------------------------
::__exf__cseod_PurchaseDocument_d_01
_typ:=_a;
_lp:=_b;

TAT.cntx_psh();
TAT.prefix();
TAT.blank(1);
_lp_str:=('00'+$_lp)+2;
TAT.ID_WP:='__exf__cseod_PurchaseDocument_%1_%2'[_typ,_lp_str];
TAT.OPIS:=TAT.ID_WP;
TAT.NA:='seod_Document_%1_%2'[_typ,_lp_str];
TAT.W_PORTAL:='S';
TAT.TYP:={? _typ='s' || 'T' |? _typ='d' || 'D' |? _typ='n' || 'L' || '?' ?};
TAT.MIEJSCE:='W';
TAT.add(1);
TAT.cntx_pop()


\EDOKUM_custom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca wartość pól kastomizowalnych faktury
::   WE: _a - akronim pola
::----------------------------------------------------------------------------------------------------------------------
_fld:=_a;
_ret:=~~;
TAT.cntx_psh();
TAT.index('ID_WP2');
TAT.prefix('S',_fld,);
{? TAT.first()
|| EDOK_ATR.cntx_psh();
   EDOK_ATR.use('edokat'+(ref_name(EDOKUM.ref())+2));
   EDOK_ATR.index('REKTAT');
   EDOK_ATR.prefix(EDOKUM.ref(),null,'',TAT.ref(),1);
   {? EDOK_ATR.first()
   || {? EDOK_ATR.TAT().TYP='L'
      || _ret:=#gsub(EDOK_ATR.WAR,',','.')
      |? TAT.TYP='D'
      || _ret:=exec('str2date','#convert',EDOK_ATR.WAR)
      || _ret:=EDOK_ATR.WAR
      ?}
   ?};
   EDOK_ATR.cntx_pop()
?};
TAT.cntx_pop();
_ret


\t_etyp_atr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Triger po add tabeli EDOK_ATR
::----------------------------------------------------------------------------------------------------------------------
TAT.cntx_psh();
{? ETYP_ATR.TAT().W_PORTAL='S'
|| _entity:='cseod_PurchaseDocument';
   PORTALK.cntx_psh();
   PORTALK.index('FLD');
   PORTALK.prefix(ETYP_ATR.TAT().ID_WP,);
   {? PORTALK.first()
   || PORTALK.ENABLED:='T';
      PORTALK.put()
   ?};
   PORTALK.cntx_pop();
   exec('PORTALAH_update','portal_seod',1);
   {? var_pres('NoUpdPK')<=0
   || exec('PORTALK_upd_add','portal_seod')
   ?}
?};
TAT.cntx_pop();
~~


\t_tat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Triger po add, put tabeli TAT
::   WE: _a - wynik operacji
::       _b - typ operaji: 0-add 1-put
::----------------------------------------------------------------------------------------------------------------------
_ok:=_a;
_typ:=_b;
{? _ok & TAT.W_PORTAL='S'
|| _entity:='cseod_PurchaseDocument';
   PORTALK.cntx_psh();
   PORTALK.index('FLD');
   PORTALK.prefix();
   {? ~PORTALK.find_key(TAT.ID_WP,)
   || PORTALK.blank();
      PORTALK.ENTITY:=_entity;
      PORTALK.FLD:=TAT.ID_WP;
      PORTALK.add()
   ?};
   PORTALK.DESC:=TAT.OPIS;
   PORTALK.GRIDDESC:=TAT.OPIS;
   PORTALK.HINT:=TAT.OPIS;
   PORTALK.FLDTYPE:={? TAT.TYP='T' || 'TSLDescription'
                    |? TAT.TYP='L' || 'TSLInt'
                    |? TAT.TYP='D' || 'TSLDate'
                    || ''
                    ?};
   PORTALK.GRIDWDTH:=100;
   {? TAT.PORSLO
   || PORTALK.LKUPTYPE:='DESC_CHECK';
      PORTALK.LKUPFLD:=TAT.PORSLO().FIELD;
      PORTALK.LKUPCODE:=TAT.PORSLO().CODE
   || PORTALK.LKUPTYPE:='';
      PORTALK.LKUPFLD:='';
      PORTALK.LKUPCODE:=''
   ?};
   PORTALK.FORMULA:='exec(\'EDOKUM_custom\',\'portal_seod\',\'%1\')'[TAT.ID_WP];
   PORTALK.put();
   PORTALK.cntx_pop()
?};
{? 'NS'*TAT.W_PORTAL=0
|| ETYP_ATR.cntx_psh();
   ETYP_ATR.index('TAT'); ETYP_ATR.prefix(TAT.ref());
   {? ETYP_ATR.first()
   || ETYPY.cntx_psh();
      ETYPY.prefix();
      {!
      |? ETYP_ATR.put(,1);
         {? ETYPY.seek(ETYP_ATR.ETYPY)
         || ETYPY.put(,1)
         ?};
         ETYP_ATR.next()
      !};
      ETYPY.cntx_pop()
   ?};
   ETYP_ATR.cntx_pop()
?};
~~


\EDOK_ATR_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Pobranie informacji z pól kastomizowalnych faktury
::   WE: _a - obiekt do operacji na JSON
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
ETYP_ATR.cntx_psh();
ETYP_ATR.index('KOLZ');
ETYP_ATR.prefix(EDOKUM.TYP,null);
{? ETYP_ATR.first()
|| EDOK_ATR.cntx_psh();
   EDOK_ATR.use('edokat'+(ref_name(EDOKUM.ref())+2));
   EDOK_ATR.index('REKKOLED');
   EDOK_ATR.prefix(EDOKUM.ref(),null,'',1);
   {!
   |? _val:=_obj.getValue(ETYP_ATR.TAT().ID_WP);
      {? var_pres('_val')>0
      || _str:=~~;
         {? TAT.TYP='T'
         || _str:=_val
         |? TAT.TYP='L'
         || _str:=$_val
         |? TAT.TYP='D'
         || _str:=gsub(10+_val,'-','/')
         ?};
         {? var_pres('_str')=type_of('')
         || {? ~EDOK_ATR.find_key(ETYP_ATR.KOL)
            || EDOK_ATR.blank(1);
               EDOK_ATR.EDOKUM:=EDOKUM.ref();
               EDOK_ATR.TAT:=ETYP_ATR.TAT;
               EDOK_ATR.REKORD:=1;
               EDOK_ATR.KOL:=ETYP_ATR.KOL;
               EDOK_ATR.add()
            ?};
            EDOK_ATR.WAR:=_str;
            EDOK_ATR.put()
         ?}
      ?};
      ETYP_ATR.next()
   !};
   EDOK_ATR.cntx_pop()
?};
ETYP_ATR.cntx_pop()


\FIRMA_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Czy firma ma być wysyłana na portal HR/SEOD
::   WE: _a - akcja put/del
::       _b - symbol firmy
::       _c - wskazanie na firmę
::----------------------------------------------------------------------------------------------------------------------
_akc:={? var_pres('_a')=type_of('') || _a || '' ?};
{? _akc='del' || return(1) ?};

REF.S_FIRMA=FIRMA.ref() & FIRMA.EWID='T'


\USERSF_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Czy użytkownik firmy może być wysyłany na portal HR/SEOD
::   WE: _a - akcja put/del
::       _b - symbol firmy
::       _c - wskazanie na firmę
::----------------------------------------------------------------------------------------------------------------------
_akc:={? var_pres('_a')=type_of('') || _a || '' ?};
{? _akc='del' || return(1) ?};

FIRMA.cntx_psh();
_tak:=USERSF.FIRMA=REF.S_FIRMA & USERSF.FIRMA().EWID='T';
FIRMA.cntx_pop();
_tak


\am_etypy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Po dodaniu, poprawieniu typu faktury
::----------------------------------------------------------------------------------------------------------------------
{? ETYPY.W_PORTAL='S'
|| exec('UD_SEOD_init','portal_seod',3)
?};
~~


\UD_SEOD_new
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca liczbę nowych drzew używanych w SEOD w związku z użyciem modelu danych typu faktury
::----------------------------------------------------------------------------------------------------------------------
_new:=0;
SKID_MBP.cntx_psh();
SKID_MBP.index('LP');
SKID_MBP.prefix(ETYPY.SKID_MB);
{? SKID_MBP.first()
|| UD_SEOD.cntx_psh();
   UD_SEOD.index('UD_SCH');
   UD_SEOD.prefix();
   {!
   |? {? ~UD_SEOD.find_key(null,SKID_MBP.UD_SCH) || _new+=1 ?};
      SKID_MBP.next()
   !};
   UD_SEOD.cntx_pop()
?};
SKID_MBP.cntx_pop();
_new


\UD_SEOD_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Usunięcie drzew portalu SEOD związanych z bieżącym typem faktury
::----------------------------------------------------------------------------------------------------------------------
UD_SEOD.cntx_psh();
UD_SEOD.index('LP');
UD_SEOD.prefix(ETYPY.ref());
{? UD_SEOD.first() || {! |? UD_SEOD.del() !} ?};
UD_SEOD.cntx_pop();
~~


\t_etypy_seod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Wyzwalacz po add i put oraz przed del dla tabeli ETYPY
::   WE:  _a  - typ operacji: 1-po add, 2-po put, 3-przed del
::       [_b] - status operacji: 1-sukces 0-porażka
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
_ok:={? var_pres('_b')=type_of(1) || _b || 1 ?};
{? _ok & 'SsD'*ETYPY.W_PORTAL
|| {? _typ=2 | _typ=3
   || exec('UD_SEOD_del','portal_seod')
   ?};
   {? _typ=1 | _typ=2
   || exec('UD_SEOD_init','portal_seod',3)
   ?}
?};
{? _typ=3 || 1 || ~~ ?}


\PORTALAH_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Aktualizacja w tabeli uprawnień do pól
::   WE: _a - typ operaji: 1-add, 2-put, 0-del
::----------------------------------------------------------------------------------------------------------------------
_entity:='cseod_PurchaseDocument';
_field:='SubType';
_name:=ETYP_ATR.TAT().ID_WP;
ETYPY.cntx_psh();
_value:=$ETYP_ATR.ETYPY;
ETYPY.cntx_pop();
PORTALAH.cntx_psh();
PORTALAH.index('ENTITY');
PORTALAH.prefix(_entity,_field,_name,);
{? ~PORTALAH.first()
|| PORTALAH.blank(1);
   PORTALAH.ENTITY:=_entity;
   PORTALAH.FIELD:=_field;
   PORTALAH.NAME:=_name;
   PORTALAH.prefix();
   PORTALAH.add()
?};
PORTALAV.cntx_psh();
PORTALAV.index('VALUE');
PORTALAV.prefix(PORTALAH.ref());
{? ~PORTALAV.find_key(_value,)
|| PORTALAV.blank(1);
   PORTALAV.PORTALAH:=PORTALAH.ref();
   PORTALAV.VALUE:=_value;
   PORTALAV.prefix();
   PORTALAV.add();
   PORTALAH.put(,1)
?};
PORTALAV.cntx_pop();
PORTALAH.cntx_pop();
PORTALK.cntx_psh();
PORTALK.index('FLD');
PORTALK.prefix(_name);
{? PORTALK.first()
|| PORTALK.CTRFIELD:=_field;
   PORTALK.CTRSET:=_name;
   PORTALK.put()
?};
PORTALK.cntx_pop()


\PORTALAH_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca zawartość pola __JSON komunikatu sl_TenantExfAvailCtrlValSetModify
::----------------------------------------------------------------------------------------------------------------------
_jman:=exec('json_man','__jsonm','{}');
PORTALAV.cntx_psh();
PORTALAV.index('VALUE'); PORTALAV.prefix(PORTALAH.ref());
{? PORTALAV.first()
|| _jman.setObject(_path:=_jman.addChildElement('','Values'),'[]');
   {!
   |? _jman.setObject(_path2:=_jman.addChildElement(_path,''),'{}');
      _jman.setValue(_jman.addChildElement(_path2,'Value'),PORTALAV.VALUE);
      PORTALAV.next()
   !}
?};
PORTALAV.cntx_pop();
_json:=_jman.getJSON();
_json


\PORTALAH_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie tabel PORTALAH iPORTALAV
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
ETYPY.index('GRUPA_WP'); ETYPY.prefix('S');
{? ETYPY.first()
|| ETYP_ATR.cntx_psh();
   ETYP_ATR.index('ID_WP');
   {!
   |? ETYP_ATR.prefix(ETYPY.ref());
      {? ETYP_ATR.first()
      || {!
         |? exec('PORTALAH_update','portal_seod');
            ETYP_ATR.next()
         !}
      ?};
      ETYPY.next()
   !};
   ETYP_ATR.cntx_pop()
?};
ETYPY.cntx_pop()


\EDOKUM_netto
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca wartość netto wniosku pozostałego (SEOD)
::----------------------------------------------------------------------------------------------------------------------
_netto:=0;
_arr:=obj_new(2);
_arr[1]:='Amount';
_arr[2]:='NetValue';
ETYP_ATR.cntx_psh();
ETYP_ATR.index('ID_WP');
ETYP_ATR.prefix(EDOKUM.TYP);
EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokat'+(ref_name(EDOKUM.ref())+2));
EDOK_ATR.index('REKKOLED');
EDOK_ATR.prefix(EDOKUM.ref(),null,'',1);
_dalej:=1;
{! _ii:=1..obj_len(_arr)
|? _dalej
|! {? ETYP_ATR.find_key(_arr[_ii],) & ETYP_ATR.TAT().TYP='L'
   || {? var_pres('objID')>0
      || _obj:=objID;
         _netto:=_obj.get('P'+$ETYP_ATR.KOL);
         _dalej:=0
      || {? EDOK_ATR.find_key(ETYP_ATR.KOL)
         || _netto:=#EDOK_ATR.WAR;
            _dalej:=0
         ?}
      ?}
   ?}
!};
EDOK_ATR.cntx_pop();
ETYP_ATR.cntx_pop();
PAR_WYDR.RPAR3:=_netto


\EDOKUM_zal_trig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Ustawia wyzwalacze synchronizujące załaczniki dla faktur SEOD
::   WE: [_a] - typ operacji
::       [_b] - akronim tabeli
::----------------------------------------------------------------------------------------------------------------------
_typ:={? var_pres('_a')=type_of('') || _a || '' ?};
_tab:={? var_pres('_b')=type_of('') || _b || '' ?};

{? _typ=''
|| EDOKUM.trig_a('add',"exec('EDOKUM_zal_trig','portal_seod','add','EDOKUM')",'seod');
   EDOKUM.trig_a('put',"exec('EDOKUM_zal_trig','portal_seod','put','EDOKUM')",'seod')
|? _tab='EDOKUM'
|| {? exec('EDOKUM_typ','portal_seod','F','S')
   || _b_prel:='#EDOK#';
      _kr_op:='Kopia e-dokumentu';
      {? _typ='add' & EDOKUM.EDOKUM | _typ='put' & EDOKUM.EDOKUM<>bfld('EDOKUM') & EDOKUM.EDOKUM
      || _naz:=EDOKUM.bl_info('EDOKUM','NAME');
         EDOKUMZ.cntx_psh();
         EDOKUMZ.use('skid_n'+(ref_name(EDOKUM.ref())+2));
         EDOKUMZ.index('NAZWA'); EDOKUMZ.prefix();
         EDOKUMZ.blank(1);
         EDOKUMZ.DOKUM:=EDOKUM.ref();
         EDOKUMZ.KR_OP:=_kr_op;
         EDOKUMZ.B_PREL:=_b_prel;
         {? ~EDOKUMZ.find_rec()
         || EDOKUMZ.NAZWA:=_naz;
            EDOKUMZ.EDOKUM:=EDOKUM.EDOKUM;
            EDOKUMZ.add()
         || EDOKUMZ.NAZWA:=_naz;
            EDOKUMZ.EDOKUM:=EDOKUM.EDOKUM;
            EDOKUMZ.put()
         ?};
         EDOKUMZ.cntx_pop()
      |? _typ='put' & EDOKUM.EDOKUM<>bfld('EDOKUM') & ~EDOKUM.EDOKUM
      || EDOKUMZ.cntx_psh();
         EDOKUMZ.use('skid_n'+(ref_name(EDOKUM.ref())+2));
         EDOKUMZ.index('NAZWA'); EDOKUMZ.prefix();
         EDOKUMZ.blank(1);
         EDOKUMZ.DOKUM:=EDOKUM.ref();
         EDOKUMZ.B_PREL:=_b_prel;
         {? EDOKUMZ.find_rec()
         || EDOKUMZ.del()
         ?};
         EDOKUMZ.cntx_pop()
      ?}
   ?}
?};
~~


\ETYPY_deleg_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodaje typy delegacji SEOD
::   WE: _a - typ delegacji: 'K'-krajowa, 'Z'-zagraniczna
::   WY: status operacji: 1-ok, wpp-nie ok
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
_id:='SEODREQUEST#DEL'+_typ;
ETYPY.cntx_psh();
ETYPY.index('ID_WP');
ETYPY.prefix();
_jest:=ETYPY.find_key(_id,);
{? ~_jest
|| typobi:=3;
   ETYPY.blank();
   &typobi
?};
ETYPY.TYPOBIEG:=exec('bl_typ','obiegi',3);
ETYPY.ID_WP:=_id;
ETYPY.W_PORTAL:='D';
ETYPY.NAZWA:='Delegacja '+{? _typ='K' || 'krajowa' || 'zagraniczna' ?};
ETYPY.DEL_ZAGR:=_typ='Z';
ETYPY.CZY_DATY:=1;
{? _jest
|| _ok:=ETYPY.put()
|| _ok:=ETYPY.add()
?};
ETYPY.cntx_pop();
_ok


\SLO_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Czy dane słownika pozycji użytkownika mają być wysyłane na portal
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SLU_CEL=null
|| exec('czytaj','#stalesys',,XINFO,'SLU_CEL')
?};
{? XINFO.SLP=null
|| exec('czytaj','#stalesys',,XINFO,'SLP')
?};
SLO.SLU=XINFO.SLU_CEL | SLO.SLU=XINFO.SLP


\EDOK_ZAL_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Czy rekord dotyczy wydatków dla delegacji
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
{? EDOK_ZAL.ZW='W' & EDOK_ZAL.EDOKUM
|| EDOKUM.cntx_psh();
   EDOKUM.use(ref_name(EDOK_ZAL.EDOKUM));
   EDOKUM.prefix();
   {? EDOKUM.seek(EDOK_ZAL.EDOKUM) & EDOKUM.FIRMA=REF.FIRMA
   || ETYPY.cntx_psh();
      _ok:=EDOKUM.TYP().W_PORTAL='D';
      ETYPY.cntx_pop()
   ?};
   EDOKUM.cntx_pop()
?};
_ok


\obj_sum_deleg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca obiekt do obsługi podsumowania kwot delegacji
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('objSumD')<=0
|| _obj:=obj_new(
      'TAB',
      'add','clear'
   );
   _obj.TAB:=tab_tmp(3,
      'WAL','STRING[3]','Symbol waluty',
      'TYPE','STRING[20]','Typ',
      'DESC','STRING[100]','Opis',
      'KW','REAL','Kwota'
   );
   _obj.add:="
      _wal:=_a;
      _type:=_b;
      _desc:=_c;
      _kw:=_d;
      _tab:=.TAB;
      {? _tab.find_key(_wal,_type,_desc,)
      || _tab.KW+=_kw;
         _tab.put()
      || _tab.blank(1);
         _tab.WAL:=_wal;
         _tab.TYPE:=_type;
         _tab.DESC:=_desc;
         _tab.KW:=_kw;
         _tab.add()
      ?}
   ";
   _obj.clear:="
      _tab:=.TAB;
      _tab.erase()
   ";
   objSumD:=_obj
?};
objSumD.clear();
objSumD


\par_wart
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Obsługa parametru 466 - SEOD: Typ wartości w Centrum akceptacji
::   WE: _a - typ formuły: 'F3' lub 'AE'
::----------------------------------------------------------------------------------------------------------------------
_par_a:=_a;
{? _par_a='F3'
|| _wart:=~-PARAMETR.TRESC;
   {? _wart='' | +_wart<>2 || _wart:='BN' ?};
   _tab:=tab_tmp(1,
      'F','STRING[1]','Faktury',
      'W','STRING[1]','Wnioski'
   );
   _tab.blank(1);
   _tab.F:=1+_wart;
   _tab.W:=_wart+1;
   _tab.add();
   _win:=_tab.mk_edit('Typ wartości w Centrum akceptacji',,'par466');
   _tab.win_esep(_win,'Faktura'@);
   _tab.win_efld(_win,,'F',,,,,,'Wartość',,,'radio-buttons',,'Brutto',"'B'",'Netto',"'N'");
   _tab.win_esep(_win,'Wniosek'@);
   _tab.win_efld(_win,,'W',,,,,,'Wartość',,,'radio-buttons',,'Brutto',"'B'",'Netto',"'N'");
   exec('ok_esc','#window',_tab,_win);
   _tab.win_edit(_win);
   {? _tab.edit()
   || _wart:=_tab.F+_tab.W
   ?};
   _wart
|? _par_a='AE'
|| _wart:=~-(2+PARAMETR.TRESC);
   _wart_f:=1+_wart;
   {? 'BN'*_wart_f=0 || _wart_f:='B' ?};
   _wart_w:=_wart+1;
   {? 'BN'*_wart_w=0 || _wart_w:='N' ?};
   _wart_f+_wart_w
?}


\PORTALK_upd_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodanie informacji o konieczności aktualizacji tabeli PORTAK w innych firmach
::----------------------------------------------------------------------------------------------------------------------
UD_SEOD.cntx_psh();
UD_SEOD.index('LP');
UD_SEOD.prefix();
{? ~UD_SEOD.find_key(ETYPY.ref(),0)
|| UD_SEOD.blank(1);
   UD_SEOD.LP:=0;
   UD_SEOD.ETYPY:=ETYPY.ref();
   UD_SEOD.NAZ:='PORTALK';
   UD_SEOD.add()
|| UD_SEOD.put(,1)
?};
UD_SEOD.cntx_pop()


\PORTALK_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja tabeli PORTALK na podstawie pól w ETYP_ATR i zapisie w UD_SEOD
::   WE: _a - typ dokumentu (ETYPY)
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
ETYP_ATR.cntx_psh();
ETYP_ATR.index('ID_WP');
ETYP_ATR.prefix(_typ);
{? ETYP_ATR.first()
|| TAT.cntx_psh();
   TAT.prefix();
   VAR_DEL.delete('NoUpdPK');
   NoUpdPK:=1;
   {!
   |? {? ETYP_ATR.TAT & TAT.seek(ETYP_ATR.TAT)
      || exec('t_tat','portal_seod',1,1)
      ?};
      exec('t_etyp_atr','portal_seod');
      ETYP_ATR.next()
   !};
   TAT.cntx_pop();
   VAR_DEL.delete('NoUpdPK')
?};
ETYP_ATR.cntx_pop()


\ETP_REDDEF_SEOD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca okno edycji RED_SEOD tabeli ETYP_ATR - edycja innych danych faktur SEOD
::----------------------------------------------------------------------------------------------------------------------
_red:=ETYP_ATR.mk_edit('Zmiana właściwości domyślnych atrybutu'@,0,'etyp_atrqreddef',1,1,'html_maximized','','lumen');
ETYP_ATR.win_esep(_red,'Wartość domyślna'@);
ETYP_ATR.win_efld(_red,,'DEF_KIND',,,21,2,0,'',1,'Nadawanie wg.: W - podana wartość, F - wyliczana formułą'@,'radio-buttons','left_label=1','Stała wartość'@,"exec('str_w_','#blank')",'Obliczana formułą'@,"exec('str_f_','#blank')");
ETYP_ATR.win_efld(_red,,'DEFAULT',,,65,0,1,'Wartość',,'Wartość domyślna'@,,'');
ETYP_ATR.win_efld(_red,,'DEF_FORM',,,65,0,1,'Formuła',,'Formuła wyliczająca wartośc poczatkową'@,,'');
_btn1:=ETYP_ATR.win_ebtn(_red,'text="%1",panel=bottom,align=end,edit=1'['Zmiana'@],"exec('etp_set_def_for','obiegi')");
ETYP_ATR.btn_eopt(_red,_btn1,'tooltip="%1"'['Zmiana wartości lub formuły domyślnej'@]);
_btn2:=ETYP_ATR.win_ebtn(_red,'text="%1",panel=bottom,align=end,edit=1'['Usunięcie'@],"exec('etp_cle_def_for','obiegi')");
ETYP_ATR.btn_eopt(_red,_btn2,'tooltip="%1"'['Usunięcie wartości domyślnej lub formuły na artość domyślną'@]);
_red

:Sign Version 2.0 jowisz:1045 2023/12/22 09:54:19 06ba5347a5629d81c4129956e6078b0f9e09007da7e8ddf56b95b92e09c87f34a6fb11b1ee37d0ff58c5a5884b4e7b51f00a9deb5a46d1ba4307e04ad70ec6424756a55ea50e7ac1afaec2325eb2123acd7d8bf14a60d45e28dd79499fe3625b459fe93187b22305261bc16d938e6f61c0e8cd3ff72b7c4916bbcf21d0dd331a

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_xwe.fml [12.51]
:: Utworzony: 2019/05/08
:: Autor: jaws
:: Systemy: PPK
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę wersji plików raportów PPK.
::======================================================================================================================


\_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Sprawdza wypełnienie wymaganych pól. Wykorzystywana w wyzwalaczach przed dołącz i popraw.
::   WE: _a [INTEGER] - Tryb modyfikacji danych
::          0 - dołączanie (domyślnie)
::          1 - poprawianie
::   WY: wynik testu poprawności danych zależny od kontekstu wywołania:
::       - jako formuła "rekord po"
::          - akronim niewypełnionego pola
::          - akronim pola o błędnej wartości
::          - 0 w przypadku powielenia unikalnego klucza
::       - jako formuła sprawdzająca dla wyzwalaczy "dołącz przed" i "popraw przed"
::          - 1 wiersz poprawny
::          - 0 niewypełnione wymagane pole, błędna wartość pola, powielony unikalny klucz
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_mode:={? var_pres('_a')=type_of(0) || _a<>0 ?};

__CHK.validate(PPK_XWE,
   $("_a.table(_b,"+$_mode+",,'NUMER','DATA')")
)


\_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli PPK_XWE.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_chk','ppk_xwe',0)


\_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli PPK_XWE.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_chk','ppk_xwe',1)


\_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed usunięciem wiersza tabeli PPK_XWE.
::   WE:
::   WY: 0/1 - wiersz może/nie może być usunięty
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','#table',PPK_XRA,'UNIQUE',PPK_XWE.ref())


\ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wskazanie aktualnej wersji raportów.
::   WE:
::   WY: Wskazanie wiersza tabeli PPK_XWE
::----------------------------------------------------------------------------------------------------------------------
PPK_XWE.ref()


\select
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Udostępnia zawartość kartoteki PPK_XWE.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_mode:='maximized_with_title';
:: panel zawierający informacje o strukturze plików
_grp:=PPK_XWE.grp_make('Wersje plików PPK'@,
:: przed otwarciem
   "  PPK_XWE.cntx_psh();
      PPK_XWE.index('NUMER');
      PPK_XWE.clear();
      PPK_XRA.cntx_psh();
      PPK_XRA.index('OPIS');
      1
   ",
:: identyfikator
   '#ppk_xwe_grp',,,
:: podczas zamykania
   "  PPK_XWE.cntx_pop();
      PPK_XRA.cntx_pop();
      1
   "
);
:: lista wersji plików
PPK_XWE.grp_sel(_grp,PPK_XWE,'WER',,
:: po odświeżeniu
   "grp_disp(PPK_XRA,'WER')"
   ,,,
:: wysokość
   8,,,,,
:: tryb
   _mode
);
:: struktura pliku
PPK_XWE.grp_splt(_grp,,'horizontal','bottom');
PPK_XWE.grp_sel(_grp,PPK_XRA,'WER',,,,,,
:: przed obsługą
   "PPK_XRA.prefix(PPK_XWE.ref())"
   ,,,,
:: tryb
   _mode
);

_typ:='BEFORE_EDIT';
_fml:=exec('save_fml_type','#field',PPK_XWE,_typ);
{? exec('jest_a11','ppk')
|| PPK_XWE.fld_fml('NUMER',_typ,"0")
|| PPK_XWE.actions('WER','P')
?};

:: udostępnij dane
_org:=PPK_XWE.win_sel('?');
PPK_XWE.win_sel(_grp);
PPK_XWE.select();

:: porządki
PPK_XWE.win_sel(_org);
PPK_XWE.win_del(_grp);
PPK_XWE.actions('WER');
exec('restore_fml_type','#field',PPK_XWE,_typ,_fml);
~~


\szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyszukuje informacje o wersji.
::   WE: _a STRING - numer wersji
::   WY: wskazanie istniejącego zapisu lub null
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_numer:=_a;

PPK_XWE.index('NUMER');
PPK_XWE.prefix();
{? PPK_XWE.find_key(_numer,)
|| PPK_XWE.ref()
|| null
?}


\dodaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Dodaje informacje o wersji.
::   WE: _a STRING - numer wersji
::       _b DATA - data obowiązywania
::       _c [STRING] - treść komentarza
::   WY: wskazanie dodanego/istniejącego zapisu lub null
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_numer:=_a;
_data:=_b;
_uwagi:={? var_pres('_c')=type_of('') || _c || '' ?};

:: sprawdź, czy wersja już istnieje
_ref:=exec('szukaj','ppk_xwe',_numer);
{? _ref<>null
:: znaleziono
|| return(_ref)
?};

PPK_XWE.blank();
PPK_XWE.NUMER:=_numer;
PPK_XWE.DATA:=_data;
{? PPK_XWE.add()
|| {? _uwagi<>''
   || PPK_XWE.memo_set(_uwagi,'UWAGI');
      PPK_XWE.memo_put(,'UWAGI')
   ?};
   _ref:=PPK_XWE.ref()
?};

_ref


\popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Zmienia informacje o wersji.
::   WE: _a STRING - numer wersji
::       _b [STRING] - nowy numer wersji
::       _c [DATA] - data obowiązywania
::       _d [STRING] - treść komentarza
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_numer:=_a;
_ret:=1;

{? exec('szukaj','ppk_xwe',_numer)=null
|| {? var_pres('_b')=type_of('') & _b<>''
   || {? var_pres('szukaj','ppk_xwe',_b)=null
      || _ret:=0
      ?}
   ?};
   return(_ret)
?};

_mod:=0;
{? var_pres('_b')=type_of('') & _b<>''
|| PPK_XWE.NUMER:=_b;
   _mod+=1
?};
{? var_pres('_c')=type_of(date)
|| PPK_XWE.DATA:=_c;
   _mod+=1
?};
{? var_pres('_d')=type_of('')
|| PPK_XWE.memo_set(_d,'UWAGI');
   PPK_XWE.memo_put(,'UWAGI');
   _mod+=1
?};

{? _mod<>0
|| _ret:=PPK_XWE.put()
?};

_ret


\usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Usuwa dane wersji plików PPK.
::   WE: _a STRING - numer wersji
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_numer:=_a;

{? exec('szukaj','ppk_xwe',_numer)=null
|| return(0)
?};

PPK_XWE.del(,1)<>0


\dane_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Uzupełnia dane wersji plików PPK.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: uzupełnij listę elementów
exec('dane_sys','ppk_xel');

exec('w_1_00','ppk_xwe',exec('dodaj','ppk_xwe','1.00',date(2019,7,1)));
exec('w_1_02','ppk_xwe',exec('dodaj','ppk_xwe','1.02',date(2020,6,22)));
exec('w_2_00','ppk_xwe',exec('dodaj','ppk_xwe','2.00',date(2020,8,3)));

~~


\w_1_00
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Uzupełnia dane wersji 1.00
::   WE: _a _PPK_XWE - opis wersji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_wer:=_a;
PPK_XRA.cntx_psh();
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','REJESTRACJA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','SKLADKA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','DANE_KOREKT'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','DANE_DEKLARACJI'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','UCZESTNIK_ZMIANA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','DANE_ZMIANA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','ZWOLNIENIE'));
PPK_XRA.cntx_pop();
~~


\w_1_02
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Uzupełnia dane wersji 1.02
::   WE: _a _PPK_XWE - opis wersji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_wer:=_a;
PPK_XRA.cntx_psh();
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','REJESTRACJA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','SKLADKA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','DANE_KOREKT'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','DANE_DEKLARACJI'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','UCZESTNIK_ZMIANA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','DANE_ZMIANA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','ZWOLNIENIE'));
PPK_XRA.cntx_pop();
~~


\w_2_00
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [12.51]
:: OPIS: Uzupełnia dane wersji 2.00
::   WE: _a _PPK_XWE - opis wersji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_wer:=_a;
PPK_XRA.cntx_psh();
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','REJESTRACJA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','SKLADKA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','DANE_KOREKT'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','DANE_DEKLARACJI'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','UCZESTNIK_ZMIANA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','DANE_ZMIANA'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','ZWOLNIENIE'));
exec('dodaj','ppk_xra',_wer,exec('szukaj','ppk_xel','TRANSFER'));
PPK_XRA.cntx_pop();
~~


\rek_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [20.14]
:: OPIS: Po edycji wiersza tabeli PPK_XWE.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('_chk','ppk_xwe',-menu_txt()='popraw')


:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:22 5e22f71c0eb123d722bd19bdcbcdda11ddbddf43cf64e162875e6e0dab967317dc717efb35d09577b14a7d50b82af223987b9cd2effe5360487906ae66a935c40a24d9ab1aa2cd5a7f6758ceece5a6f99fc616be2084ba95e868bf09547358cc20db33367f94250aaa702dddff6789a7bfa408d83dbf96fe83ce0a09c01820c8

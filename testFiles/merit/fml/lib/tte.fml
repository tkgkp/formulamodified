:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tte.fml
:: Utworzony: 26.01.2015
:: Autor: maglak
::======================================================================================================================
:: Zawartość: Formuły oraz obszary robocze dziedziny TTE
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: maglak [17.00]
:: OPIS: Formuła inicjująca dla dziedziny TTE
::----------------------------------------------------------------------------------------------------------------------
:: Czytanie stałych z zestawu XINFO
exec('czytaj','#stalesys',,XINFO,'SLWYDZIA','SLWAL','NAZ');
:: Czytanie stałych z zestawu INFO
exec('czytaj','#stalesys',,INFO,FINFO);

:: Ustawia zmienne informujące o dostępie do wartości sprzedaży, zakupu, magazynowych
exec('upr_set','ceny');

:: Formuła podczytująca dane specyficzne dla firmy
__War_f:="exec('FindInSet','#table',_a,_a,_c,REF.FIRMA,$(_a+'.'+_b))";

:: Właściwe dla dziedziny
:: - obiekt do obsługi parametrów
exec('Tpar_decl','tech_param');
:: - obiekt do obsługi drzewa operacji
exec('declare','tech_tree');
:: - obiekt do obsługi dokumentacji elektronicznej + domyślna instancja
exec('declare','tech_doc');
:: - obiekt kalkulacyjny
exec('declare','kalkulator');
exec('ANK_MAT_declare','kalkulator');
:: - obiekt do obsługi przewodników
exec('cubedekl','object');
:: RB - objekt obsługujący rachunki bankowe
exec('RB','object');

:: Ustawienie prezycji pól w oknach
exec('fld_prec','win_ust');
VAR1.fld_fml('SD','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $SM.M().DOKL || $ST.DOKL_P ?}");
_fml:="
   _empty:='empty=0';
   {? VAR1.SD=0
   || _empty:='empty=1'
   ?};
   _prec:=',out_prec='+{? (2+cur_kwin())='e_' || $SM.M().DOKL || $ST.DOKL_P ?};
   _empty+_prec
";
VAR1.fld_fml('SD','DISPLAY_FORMAT',_fml);

:: Formy zatrudnienia
exec('__F_ZATR','object');
:: Rubryki (wykorzystanie na potrzeby mechanizmu rozszerzonego filtra - wybierałka pracowników)
exec('__RUB','object');

:: Salda i obroty kont i rozrachunków
exec('F','object');

:: drukarki kodów kreskowych
exec('myDPRINT','object');

:: Badania - ikonki pól
exec('ikonki','statexam');

:: Materiały - ikonki dla pól
exec('m_icon','tech_common');

:: konta kosztowe
BPMN.KK_DOM:='LMG';
~~


\tte_tec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obszar roboczy TTE_TEC - karty technologiczne
::----------------------------------------------------------------------------------------------------------------------
:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)=type_of('')
|| _link:=_params.LINK
|| _link:=''
?};

exec('init','tte');
params_set('LINK',_link);
exec('tech_red','tech_head');
~~


\tte_wte
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obszar roboczy TTE_WTE - wzorce technologii
::----------------------------------------------------------------------------------------------------------------------
:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)=type_of('')
|| _link:=_params.LINK
|| _link:=''
?};

exec('init','tte');
params_set('LINK',_link);
exec('tbox_red','tech_head');
~~


\tte_pzl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obszar roboczy TTE_PZL - zlecenia produkcyjne
::----------------------------------------------------------------------------------------------------------------------
exec('init','tte');

exec('first_prac','tte');

:: zainicjowanie środowiska selektora zleceń
_env:=exec('env','zl_view');

:: zasilenie selektora zleceń
ZLV.blank();
ZLV.ID:='TECH_SRC';
ZLV.NAME:='Technologia źródłowa'@;
ZLV.P:='T';
ZLV.W:='N';
ZLV.CONFIG:="params_exec('cfg_technologie','zl_view',_a,_b,'T')"+'';
ZLV.REFRESH:="{? params_get().env.CUR_TAB.ID='TECH_SRC' || params_exec('technologia_after_refresh','zl_view','T') ?}"+'';
ZLV.BEFORE:="params_exec('technologia_before','zl_view',_a,'T')"+'';
ZLV.AFTER:="params_exec('technologia_after','zl_view',_a,'T')"+'';
_env.tabs.TECH_SRC.get();

ZLV.blank();
ZLV.ID:='TECH_ZL';
ZLV.NAME:='Technologia zlecenia'@;
ZLV.P:='T';
ZLV.W:='N';
ZLV.CONFIG:="params_exec('cfg_technologie','zl_view',_a,_b,'Z')"+'';
ZLV.REFRESH:="{? params_get().env.CUR_TAB.ID='TECH_ZL' || params_exec('technologia_after_refresh','zl_view','Z') ?}"+'';
ZLV.BEFORE:="params_exec('technologia_before','zl_view',_a,'Z')"+'';
ZLV.AFTER:="params_exec('technologia_after','zl_view',_a,'Z')"+'';
_env.tabs.TECH_ZL.get();

ZLV.blank();
ZLV.ID:='PRZEWODNIKI';
ZLV.NAME:='Przewodniki'@;
ZLV.P:='T';
ZLV.W:='T';
ZLV.CONFIG:="params_exec('cfg_przewodniki','zl_view',_a,_b)"+'';
ZLV.REFRESH:="params_exec('przewodniki_after_refresh','zl_view')"+'';
ZLV.BEFORE:="params_exec('przewodniki_before','zl_view',_a)"+'';
ZLV.AFTER:="params_exec('przewodniki_after','zl_view',_a)"+'';
_env.tabs.PRZEWODNIKI.get();

ZLV.blank();
ZLV.ID:='LIMITY';
ZLV.NAME:='Zapotrzebowania'@;
ZLV.P:='T';
ZLV.W:='T';
ZLV.CONFIG:="params_exec('cfg_limity','zl_view',_a,_b)"+'';
ZLV.REFRESH:="{? params_get().env.CUR_TAB.ID='LIMITY' || params_exec('limity_after_refresh','zl_view') ?}"+'';
ZLV.BEFORE:="params_exec('limity_before','zl_view',_a)"+'';
ZLV.AFTER:="params_exec('limity_after','zl_view',_a)"+'';
_env.tabs.LIMITY.get();

ZLV.blank();
ZLV.ID:='DODATKOWE';
ZLV.NAME:='Dane dodatkowe'@;
ZLV.P:='T';
ZLV.W:='T';
ZLV.CONFIG:="params_exec('cfg_dodatkowe','zl_view',_a,_b)"+'';
ZLV.REFRESH:="params_exec('dodatkowe_after_refresh','zl_view')"+'';
ZLV.BEFORE:="params_exec('dodatkowe_before','zl_view',_a)"+'';
ZLV.AFTER:="params_exec('dodatkowe_after','zl_view',_a)"+'';
_env.tabs.DODATKOWE.get();

:: Załadowanie selektora zleceń
exec('load','zl_view',_env);
_env.WP:='P';

params_set(params_get());
_env.select();
~~


\tte_wzl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obszar roboczy TTE_WZL - zlecenia warsztatowe
::----------------------------------------------------------------------------------------------------------------------
exec('init','tte');

exec('first_prac','tte');

:: zainicjowanie środowiska selektora zleceń
_env:=exec('env','zl_view');

:: zasilenie selektora zleceń
ZLV.blank();
ZLV.ID:='PRZEWODNIKI';
ZLV.NAME:='Przewodniki'@;
ZLV.P:='T';
ZLV.W:='T';
ZLV.CONFIG:="params_exec('cfg_przewodniki','zl_view',_a,_b)"+'';
ZLV.REFRESH:="params_exec('przewodniki_after_refresh','zl_view')"+'';
ZLV.BEFORE:="params_exec('przewodniki_before','zl_view',_a)"+'';
ZLV.AFTER:="params_exec('przewodniki_after','zl_view',_a)"+'';
_env.tabs.PRZEWODNIKI.get();

ZLV.blank();
ZLV.ID:='LIMITY';
ZLV.NAME:='Zapotrzebowania'@;
ZLV.P:='T';
ZLV.W:='T';
ZLV.CONFIG:="params_exec('cfg_limity','zl_view',_a,_b)"+'';
ZLV.REFRESH:="{? params_get().env.CUR_TAB.ID='LIMITY' || params_exec('limity_after_refresh','zl_view') ?}"+'';
ZLV.BEFORE:="params_exec('limity_before','zl_view',_a)"+'';
ZLV.AFTER:="params_exec('limity_after','zl_view',_a)"+'';
_env.tabs.LIMITY.get();

ZLV.blank();
ZLV.ID:='DODATKOWE';
ZLV.NAME:='Dane dodatkowe'@;
ZLV.P:='T';
ZLV.W:='T';
ZLV.CONFIG:="params_exec('cfg_dodatkowe','zl_view',_a,_b)"+'';
ZLV.REFRESH:="params_exec('dodatkowe_after_refresh','zl_view')"+'';
ZLV.BEFORE:="params_exec('dodatkowe_before','zl_view',_a)"+'';
ZLV.AFTER:="params_exec('dodatkowe_after','zl_view',_a)"+'';
_env.tabs.DODATKOWE.get();

:: Załadowanie selektora zleceń
exec('load','zl_view',_env);
_env.WP:='W';

params_set(params_get());
_env.select();
~~


\tte_wyk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obszar roboczy TTE_WYK - wykonania
::   WE: [_a] - czy praca w trybie terminala (e-kiosk)= T, standardowo ['N']
::       [_b] - symbol oddziału (ma znaczenie przy pracy w trybie terminala e-kiosk)
::       [_c] - symbol wydziału (ma znaczenie przy pracy w trybie terminala e-kiosk)
::       [_d] - symbol stanowiska/gniazda (ma znaczenie przy pracy w trybie terminala e-kiosk)
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _terminal:=_a || _terminal:='N' ?};
{? var_pres('_b')=type_of('') || _oddzial:=_b || _oddzial:='' ?};
{? var_pres('_c')=type_of('') || _wydzial:=_c || _wydzial:='' ?};
{? var_pres('_d')=type_of('')
|| {? 2+_d='G:'
   || _twrkplc:='';
      _twrkzbr:=2-_d
   || _twrkplc:=_d;
      _twrkzbr:=''
   ?}
|| _twrkplc:='';
   _twrkzbr:=''
?};

exec('init','tte');

{? _terminal='T'
|| exec('startEkiosk','ekioski',_oddzial,_wydzial,_twrkplc,_twrkzbr)
|| exec('first_prac','tte');
   FILTER.PR_A:='T';
   AreaTitle.setTabWin(PROD_REJ,~~);
   {? exec('get','#params',8712,2,OPERATOR.USER)='T'
   || exec('rej_all_grp','prod_rej',0,null(),null(),"exec('exit','zws',_a)")
   || exec('rej_all_select','prod_rej',0,null(),null(),"exec('exit','zws',_a)")
   ?}
?};
~~


\tte_wbr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [17.00]
:: OPIS: Obszar roboczy TTE_WBR - rejestracja braków
::----------------------------------------------------------------------------------------------------------------------
exec('init','tte');
exec('first_prac','tte');
exec('select','braki_k');
~~


\tte_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: maglak [17.00]
:: OPIS: Formuła sprawdzająca licencję dla obszaru TTE
::   WY: 'T' - jest licencja, 'N' - nie ma licencji
::----------------------------------------------------------------------------------------------------------------------
_result:='N';
_domain:=exec('domain_ref','#b_domain','TTE');
{? _domain<>null()
|| {? exec('lic','#b_domain',_domain)=1
   || _result:='T'
   ?}
?};
_result


\st_a_wyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [2010]
:: OPIS: Zwraca aktualny wydział
::   WY: ST.A_WYD
::  OLD: \st_a_wyd/war_tech.fml
::----------------------------------------------------------------------------------------------------------------------
ST.A_WYD


\set_a_wyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [2010]
:: OPIS: Ustawia pole VAR.A_WYD
::   WE: _a - Ref na wydział
::  OLD: \set_a_wyd/varget.fml
::  OLD: \varwydz/tex_ope1.fml
::----------------------------------------------------------------------------------------------------------------------
VAR.A_WYD:=_a;
~~


\parses
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Ustawienie parametrów sesji w obszarze roboczym dziedziny TTE
::----------------------------------------------------------------------------------------------------------------------
{? __PARSES.editDom('TTE')
|| AreaTitle.setTitle()
?};
~~


\parses_OkresRok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Ustawienie parametru sesji w obszarze roboczym - tylko OkresRok
::----------------------------------------------------------------------------------------------------------------------
{? __PARSES.editPar('OkresRok')
|| AreaTitle.setTitle()
?};
~~


\tktl2todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Ustala treść na ToDo dla czynności ręcznej, której parametr jest typu _TKTL (karta technologiczna)
::       Przykładowe użycie w formule głównej:
::        FUN.ask('Kontynuować proces dotyczący karty: '+params_exec('tktl2todo','tte','p01')+'?')
::       Przykładowe użycie na ToDo:
::        'Kontynuacja procesu dotyczącego karty: '+params_exec('tktl2todo','tte','p01')
::   WE: _a - symbol parametru, np. 'p01'
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_par:=_a;

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest parametr wejściowy to ustawiam odpowiedniego TKTL
{? var_pres(_par,_in)
|| _desc:=exec('record','#to_string',($('_a.'+_par))(_in))
?};
_desc


\zl2todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Ustala treść na ToDo dla czynności ręcznej, której parametr jest typu _ZL (zlecenie)
::       Przykładowe użycie w formule głównej:
::        FUN.ask('Kontynuować proces dotyczący zlecenia: '+params_exec('zl2todo','tte','p01')+'?')
::       Przykładowe użycie na ToDo:
::        'Kontynuacja procesu dotyczącego zlecenia: '+params_exec('zl2todo','tte','p01')
::   WE: _a - symbol parametru, np. 'p01'
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_par:=_a;

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest parametr wejściowy to ustawiam odpowiedniego ZL
{? var_pres(_par,_in)
|| _desc:=exec('record','#to_string',($('_a.'+_par))(_in))
?};
_desc


\zgh2todo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Ustala treść na ToDo dla czynności ręcznej, której parametr jest typu _ZGH (przewodnik zlecenia)
::       Przykładowe użycie w formule głównej:
::        FUN.ask('Kontynuować proces dotyczący przewodnika: '+params_exec('zgh2todo','tte','p01')+'?')
::       Przykładowe użycie na ToDo:
::        'Kontynuacja procesu dotyczącego przewodnika: '+params_exec('zgh2todo','tte','p01')
::   WE: _a - symbol parametru, np. 'p01'
::----------------------------------------------------------------------------------------------------------------------
_mp:=params_get().mp;
_par:=_a;

_desc:='';
_in:=_mp.load(exec('kind_in','#b_port'));

:: jest parametr wejściowy to ustawiam odpowiedniego ZGH
{? var_pres(_par,_in)
|| _desc:=exec('record','#to_string',($('_a.'+_par))(_in))
?};
_desc


\tte_wkj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MAGLAK [17.00]
:: OPIS: Obszar roboczy TTE_WKJ - Kontrola jakości
::----------------------------------------------------------------------------------------------------------------------
exec('init','tte');
exec('first_prac','tte');

exec('select','zl_wkj');
~~


\tte_koo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obszar roboczy TTE_KOO - Kooperacja
::----------------------------------------------------------------------------------------------------------------------
exec('init','tte');
exec('init','lmg');

_koopmag:=exec('FindInSet','#table','MG','KOOP','T',ST.ODDZ);
{? _koopmag<>null()
|| __PARSES.setVal('Magazyn',_koopmag)
?};

exec('select','zl_koop');
~~


\tte_bad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obszar roboczy TTE_BAD - Badania do operacji zleceń
::----------------------------------------------------------------------------------------------------------------------
exec('init','tte');
exec('init','lmg');

AreaTitle.setTabWin(BADH);

exec('Badania_sel_','statexam','O');
~~


\wybierz_pracownika
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Wybór pracownika dla funkcji produkcyjnych - uwzględnia uprawnienia do danych
::       (formy zatrudnienia, struktura organizacyjna)
::   WE: _a - możliwość wielowyboru (1 / 0)
::       [_b] - zakres pracowników - zatrudnieni (T), niezatrudnieni (N), domyślnie wszyscy (W)
::       [_c] - zakres jednostek organizacyjnych - wybrane (W), domyślnie wszystkie (A)
::       [_d] - OSOBA.ref() - zawęź dziedzinę do pracowników powiązanych z tą osobą
::----------------------------------------------------------------------------------------------------------------------
_wielu:=_a;
{? var_pres('_b')=type_of('') || _view:=_b || _view:='W' ?};
{? var_pres('_c')=type_of('') || _jorg:=_c || _jorg:='A' ?};
_osoba:=null();
{? var_pres('_d')=type_of(OSOBA.ref())
|| _osoba:=_d
?};
_args:=exec('wybierz_args','pracownik');
_args.DOMAIN:='TTE';
_args.UD_SCH:=exec('domyslny','schemat','PODZORG');
{? _jorg='A'
|| UD_FIR.cntx_psh();
   UD_FIR.index('UD_SKL');
   UD_FIR.prefix(REF.FIRMA);
   {? UD_FIR.first()
   || _args.UD_SKL:=UD_FIR.UD_SKL().SYMBOL
   || _args.UD_SKL:=''
   ?};
   UD_FIR.cntx_pop()
|| {? ST.WYD=null()
   || _ret:=obj_new('STATUS');
      _ret.STATUS:='Należy wybrać wydział w parametrach pracy albo zmienić zakres pracowników na "wszystkie jednostki".'@;
      return(_ret)
   || _args.UD_SKL:=ST.WYD().SYMBOL
   ?}
?};
_args.VIEW:=_view;
_args.HDR_SEL:={? _wielu || 'Wybór pracowników'@ || 'Wybór pracownika'@ ?};
_args.F_ZATR:='*T';
_args.WIELU:=_wielu;
_args.POMIN_KOLUMNY:='IP, PESEL, DOWOD, PASZPORT';
_args.GRP_IDENT:='pracnaz_sel';
_args.GRP_MODE:='html_maximized';

{? _osoba<>null()
|| _args.SQL_FROM:=''+"JOIN OSOBA using(P.OSOBA, OSOBA.REFERENCE) JOIN USERS using(USERS.OSOBA, OSOBA.REFERENCE)";
   _args.SQL_WHERE+=''+"P.OSOBA='"+$_osoba+"'"
?};
_ret:=exec('wybierz','pracownik',_args);
_ret


\first_prac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Ustawia się na pierwszym pracowniku przy wejściu w dany obszar roboczy
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
P.index('PRACONAZ');
P.find_key(REF.FIRMA,'P');
P.prefix();
~~


\tte_prz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Obszar roboczy TTE_PRZ - przewodniki zleceń
::----------------------------------------------------------------------------------------------------------------------
:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)=type_of('')
|| _link:=_params.LINK
|| _link:=''
?};

exec('init','tte');
params_set('LINK',_link);
AreaTitle.setTabWin(TKTL,~~);
AreaTitle.setTitle();
exec('zgh_all_sel','zl_guide');
~~

:Sign Version 2.0 jowisz:1045 2023/11/21 14:46:44 1fa23eb33fe11387b2d0010f71f4a8ac4e85fb6a98371d83ff2aaf0854ca8ac00e9cb4ee9d9b6e057cdd0dbe92c89610e1d13e212ff5562094dcd0d83418b72f178b545b273167ee903a4bae5c47f8829c1b2ab8e3d3c9b6c43febe5b3bce9ca8fc4877cd6bec9678dec8e967e9d9fd0749efe24817d45ef246f414d6a4d74cc

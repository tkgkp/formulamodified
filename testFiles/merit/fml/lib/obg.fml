:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: obg.fml
:: Utworzony: 22.02.2016
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Formuły obszaru OBG
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła inicjująca dla obszaru OBG
::----------------------------------------------------------------------------------------------------------------------
{? _ || exec('del_zm','obg',1) || exec('del_zm','obg') ?};
exec('RB','object');
exec('__F_ZATR','object');
exec('KOMM','#object');
exec('czytaj','#stalesys',,XINFO);
exec('czytaj','#stalesys',,FINFO);
exec('add_grnr','numery','DOB');
exec('fl_decl','dekret_aut');
exec('dek_decl','dekret_aut');
exec('sd_decl','dekret_aut');
__PAR188:=PAR_SKID.get(188)='T';
__War_f:="exec('FindInSet','#table',_a,_a,_c,REF.FIRMA,$(_a+'.'+_b))";
dla_kh:=0;
OBIEGI.TYP_ID:='T';
OBIEGI.CZY_ZAM:='N';
TYPOBIEG.cntx_psh(); TYPOBIEG.index('UNIK');
TYPOBIEG.prefix('Obieg faktur','Obieg faktur');
OBIEGI.TYPOBIEG:={? TYPOBIEG.first() || TYPOBIEG.ref() || null ?};
TYPOBIEG.cntx_pop();
typobi:=1;
EDOKUM.win_patt('SZUK'); ETYPY.win_edit('RED');
EDOKUMZ.win_edit('RED'); EDOKUMZ.win_patt('SZUK');
OSOBA.win_dict('SLO');
ODD.win_dict('SLO'); ETYPY.win_dict('SLO'); EDOKUM.win_edit('WPR');
btn_idpf:=obj_new(10); {! _i:=1..10 |! btn_idpf[_i]:='' !};
{? _
|| wla_dok:=wla_dok1:=wla_dok2:=1;
   zakr_wpr:=4;
   zakr_mer:=zakr_ks:=zakr_pak:=1; zakres:=1
?};
{? var_pres('zakres')<=0 || zakres:=1 ?};
exec('EDOKUM_zal_trig','portal_seod');
~~


\del_zm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Kasowanie zmiennych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('ikon_dob','dla_kh','typobi','btn_idpf');
{? _ || VAR_DEL.delete('wla_dok','wla_dok1','wla_dok2','zakr_wpr','zakr_ks','zakr_mer','zakr_pak','zakres') ?};
{? UD_POM.UDDEFUPR<>''
|| exec('ud_def_upr_del','control')
?};
undefine()


\obg_fzo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Główna formuła obszaru roboczego OBG_FZO - Faktury w obiegu
::   WE: _a - zadeklarowany - dekretacja z poziomu dokumentów księgowych (DOK)
::----------------------------------------------------------------------------------------------------------------------
exec('init','obg',1);
EDOKUM.win_fml('FWPR',,'SYM',,'ICON_BEFORE',"{? EDOKUM.EDOKUM || 'xwin16.png:119' || '' ?}");
EDOKUM.win_fml('FPRZ',,'SYM',,'ICON_BEFORE',"{? EDOKUM.EDOKUM || 'xwin16.png:119' || '' ?}");
EDOKUM.win_fml('FAKC',,'SYM',,'ICON_BEFORE',"{? EDOKUM.EDOKUM || 'xwin16.png:119' || '' ?}");
EDOKUM.win_fml('FDEK',,'SYM',,'ICON_BEFORE',"{? EDOKUM.EDOKUM || 'xwin16.png:119' || '' ?}");
SKID.fld_fml('NIP','F3',"exec('aenipdob','obiegi',1)");

:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)
|| _link:=_params.LINK
|| _link:=''
?};

exec('faktury_sel','obiegi',_,_link);
SKID.fld_fml('NIP','F3',"*");
{? _ || exec('del_zm','obg',1) ?};
~~


\uprawnienia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła na uprawnienia do danych dla czynności
::   WE: params_get().in - parametry wejściowe czynności
::           params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::           params_get().mp - Menadżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do danych dla czynności
::       1 - użytkownik ma uprawnienia do danych czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_result:=0;
USERS.cntx_psh(); USERS.prefix();
{? USERS.seek(_user)
|| {? var_pres('EDOKUM',_in) & var_pres('EDOKUM',_in)=type_of(null()) & _in.EDOKUM<>null()
   || EDOKUM.cntx_psh();
      {? EDOKUM.seek(_in.EDOKUM,form(($_in.DOK)-8),1)
      || {? exec('usr_fjks','b_perm',EDOKUM.ODD().OD,USERS.ref())
         || _result:=1
         ?}
      ?};
      EDOKUM.cntx_pop()
   ||
:: czy w ogóle user ma uprawnienia do jakiejkolwiek jednostki księgowej
      {? exec('usr_fjks_any','b_perm',USERS.ref())
      || _result:=1
      ?}
   ?}
?};
USERS.cntx_pop();
_result


\obg_fzp_www
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Główna formuła obszaru roboczego OBG_FZO - Faktury w obiegu w portalu
::----------------------------------------------------------------------------------------------------------------------
exec('env_wt','b_proces');
exec('FUN','#object');
exec('init_www','obg');
{? ~SSTALE.AR | ~SSTALE.AO
|| exec('www_parses','zws','obg_fzp_www','obg','','',0)
|| _mas:={? SSTALE.AR
         || ($(SSTALE.AR().POCZ_ROK~1)+2)
         || '__'
         ?};
   EDOKUMW.use('skidv1'+_mas);
   exec('AreaTitle','#object');
   AreaTitle.setArea('OBG_FZP');
   _jedn:={? OPERATOR.DEPT
          || ', jednostka księgowa: '+OPERATOR.DEPT().OD
          || ', wszystkie jednostki księgowe'
          ?};
   web_title('%1, %2: %3'[exec('nazwa','#system'),'Firma '+REF.FIRMA().SYMBOL,AreaTitle.getTitle()+' rok: '+SSTALE.AR().NAZ+_jedn],'all');
   exec('maski_w','obiegi',0);
   EDOKUMW.index('DISP'); EDOKUMW.prefix();
   typobi:=2;
   zakr_pak:=1;
   zakr_mer:=1;
   wla_dok:=wla_dok1:=wla_dok2:=1;
   OBIEGI.CZY_TYP:='W'; OBIEGI.TYP:=null; OBIEGI.CZY_ZAM:='N';
   EDOKUM.prefix();
   web_global_params_set(exec('obj_ntab_set','#array',web_global_params_get(),'typobi',1));
   exec('zakres_dob1','obiegi','P','F','U','N',0);
   exec('zakres_dob1','obiegi','A','F','U','N',0);
   _okno:={? PAR_SKID.get(80)='T' || 'WPRGF' || 'WPRGF1' ?};
   EDOKUMW.web_select(_okno);
   web_ses_close()
?};
~~


\init_www
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [17.00]
:: OPIS: Formuła inicjująca dla obszaru OBG - WWW
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.TYP_ID:='T';
TYPOBIEG.cntx_psh(); TYPOBIEG.index('UNIK');
TYPOBIEG.prefix('Obieg faktur','Obieg faktur');
OBIEGI.TYPOBIEG:={? TYPOBIEG.first() || TYPOBIEG.ref() || null ?};
TYPOBIEG.cntx_pop();
typobi:=1;
{? var_pres('zakr_wpr')<=0 || zakr_wpr:=4 ?};
{? var_pres('zakr_pak')<=0 || zakr_pak:=1 ?};
{? var_pres('zakr_mer')<=0 || zakr_mer:=1 ?};
{? var_pres('wla_dok')<=0 || wla_dok:=1 ?};
{? var_pres('wla_dok1')<=0 || wla_dok1:=1 ?};
{? var_pres('wla_dok2')<=0 || wla_dok2:=1 ?};
OPERATOR.USER:={? exec('getUser','users',app_info('web_user')) || USERS.ref() || null ?};
~~


\dok_spac
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Funkcja do wyświetlania nagłówka dokumentu źródłowego z tabeli EDOKUM
::   WE: [_a] - wskazanie na dokument obiegu
::----------------------------------------------------------------------------------------------------------------------
exec('init','obg');
_ok:=1;
{? var_press('_a')>=0
|| _ar:=SSTALE.AR;
   _ao:=SSTALE.AO;
   {? type_of(_a)=7
   || EDOKUM.cntx_psh();
      _ref:=($_a)+16;
      EDOKUM.use(8+_ref);
      EDOKUM.prefix();
      _ok:=EDOKUM.seek(_a);
      OKRO_F.cntx_psh();
      OKRO_F.index('ROK'); OKRO_F.prefix(EDOKUM.ROK_F().ref());
      {? OKRO_F.first()
      || SSTALE.AR:=EDOKUM.ROK_F().ref();
         SSTALE.AO:=OKRO_F.ref()
      || _ok:=0
      ?};
      OKRO_F.cntx_pop()
   || _ok:=0
   ?}
?};
{? _ok
|| exec('open_tabele','open_tab','F');
   exec('dok_edit','obiegi_akc','PDG');
   zawin:=0;
   {! |?
      EDOKUM.display();
      {? zawin=2
      || params_exec('pozvdob','obiegi','PDG')
      |? zawin=3
      || params_exec('podz_dob','obiegi','PDG')
      |? zawin=4
      || exec('zalaczniki','obiegi')
      |? zawin=5
      || exec('inne_dane','obiegi','PDG')
      |? zawin=6
      || exec('zak_rej_dok','obiegi');
         {? zak_rej_ed || zawin:=0 ?}
      |? zawin=12
      || exec('pozf_edokum','obiegi','PDG')
      |? zawin=8
      || exec('proj_dob','obiegi_proj')
      |? zawin=11
      || exec('okn_opis','obiegi')
      |? zawin=14
      || exec('dokumzbc_select','dok_fks1')
      ?};
      {? zawin
      || zawin:=0; 1
      || 0
      ?}
   !}
|| FUN.info('Nie znaleziono dokumentu obiegu.'@)
?};
{? var_press('_a')>0
|| SSTALE.AR:=_ar;
   SSTALE.AO:=_ao;
   {? type_of(_a)=7 || EDOKUM.cntx_pop() ?};
   {? _ok || exec('open_tabele','open_tab','F') ?}
?}


\okna_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Ustawianie okienek tabeli EDOKUM
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('btn_idp');
btn_idp:=obj_new(3); {! _i:=1..3 |! btn_idp[_i]:='' !};
okno_dok:='';
{? (_a='VAT' | _a='SAD') & POMOC.V
|| POMOC.V(); _v:=VAT_REJ.RVAT().RT; _nd:=VAT_REJ.RVAT().KVAT().SYM;
   {? _v='I'
   || okno_dok:='DOK_IMP'; exec('set_rbd','rachunki',1)
   |? _v='W'
   || {? 6+_nd='_WWspD'
      || okno_dok:='DOK_WWD'; exec('set_rbd','rachunki',2)
      |? 6+_nd='_WWspN'
      || okno_dok:='DOK_WWN'; exec('set_rbd','rachunki',1)
      || okno_dok:='DOK_WW'
      ?}
   |? _v='Z'
   || okno_dok:='DOK_VAT'; exec('set_rbd','rachunki',1)
   |? _v='S'
   || okno_dok:='DOK_SPR'; exec('set_rbd','rachunki',2)
   |? _v='E'
   || okno_dok:='DOK_EXP'; exec('set_rbd','rachunki',2)
   ?};
   PARAM.OKROTXT:=DOK.OKRVAT().NAZ;
   PARAM.OKROVAT:={? +DOK.OKRVAT().NAZ || DOK.OKRVAT().NAZ+'/'+DOK.OKRVAT().ROK().NAZ || '' ?}
|| DOK_REJ.cntx_psh();
   okno_dok:='DOK_PRO';
   MLEX_OKN.CZY_RBAN:={? DOK.DOK_REJ().CZY_RRB<>'' || DOK.DOK_REJ().CZY_RRB || 'N' ?};
   exec('set_rbd','rachunki',1);
   DOK_REJ.cntx_pop()
?};
okno_dok


:Sign Version 2.0 jowisz:1045 2023/10/30 11:52:05 7be90201d05804343b115fbc7c5af076f5124b393d9a24541f98f0a9b683e0198fe5f3abe8dadba2a28951a173c47333d79c1a1df8ae8067c6449fc7f629d74fde22d2fec7a08faf4f7f3611e3aadd3e77046c2339793a14923117ed01be90997bb77ef19e0ab06a55f6144b2579744f68291afa3e57e227265a9958354a2ae2

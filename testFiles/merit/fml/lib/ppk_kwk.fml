:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_kwk.fml [12.51]
:: Utworzony: 2019/05/22
:: Systemy: PPK
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę kwot korekt / zwrotów.
::======================================================================================================================


\add_attr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [12.51]
:: OPIS: Formuła dodaje/modyfikuje rekord do tabeli PPK_KWK.
::   WE: _a [REFERENCE] - Wskazanie rekordu nagłówka (PPK_KWN).
::       _b [NUMBER]    - Numer atrybutu, dla którego dodawany jest rekord.
::       _c [NUMBER]    - Kwota.
::   WY: Wynik operacji [0/1].
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(null()) & ref_tab(_a)=PPK_KWN
|| _kwn:=_a
|| return(0)
?};
{? var_pres('_b')<>type_of(0)
|| return(0)
|? (_rn:=__RUB.sys_kod(_b))=0
|| return(0)
|? (_rref:=__RUB.ref(_rn))=null()
|| return(0)
?};
{? var_pres('_c')=type_of(0)
|| _kw:=_c
|| return(0)
?};

PPK_KWK.cntx_psh();
PPK_KWK.index('UNIQUE');
PPK_KWK.prefix(_kwn);
{? PPK_KWK.find_key(_rref)
|| {? PPK_KWK.KW<>_kw
   || PPK_KWK.KW:=_kw;
      _ret:=PPK_KWK.put()
   || _ret:=1
   ?}
|| PPK_KWK.blank();
   PPK_KWK.PPK_KWN:=_kwn;
   PPK_KWK.R:=_rref;
   PPK_KWK.KW:=_kw;
   _ret:=PPK_KWK.add()
?};
PPK_KWK.cntx_pop();
_ret


\mod_kwk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.51]
:: OPIS: Obsługa akcji "Popraw - przed" w tabeli PPK_KWK.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('spr_kwn','ppk_kwn',menu_txt)


\zlicz_skl_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [12.51]
:: OPIS: Zliczenie wartości z korekt. Bufor nagłówka korekt musi być ustalony.
::   WE: _a - INTEGER - atrybut ze składnikami płacowymi
::       [_b]- DATE - data dla pobrania atrybutu (jeśli nie podana to bieżąca)
::       [_c] - STRING - Symbol typu korekty ( jeśli nie podany to wszystkie typy)
::   WY: wartość
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0) || return(0) || _attr:=_a ?};
_data:={? var_pres('_b')<>type_of(date()) || date() || _b ?};
_typ:={? var_pres('_c')<>type_of('') || '' || _c ?};

PPK_KWK.cntx_psh();
PPK_KWK.index('RN');
_wynik:=0;

_tab_rub:=__RUB.sys_rub(_attr,_data);
{? _tab_rub.first
|| {!
   |? PPK_KWK.prefix(PPK_KWN.ref,_tab_rub.RN);
     {? PPK_KWK.first()
     || {!
        |? _wynik+=PPK_KWK.KW;
           PPK_KWK.next()
        !}
     ?};
     _tab_rub.next()
   !}
?};
PPK_KWK.cntx_pop();
_wynik

:Sign Version 2.0 jowisz:1028 2019/10/14 09:21:32 305c353c79d2feb49ab909a649d23bab674cfecc789308fde1035ea8793da35d9ef07ff6d5a201ccf1061de71b85198f8f2ca1cd95c6d157633f4aa3341e6b59a8e79045ced2664fe7cd202dca690a7de75e7fd358082b2e0a9611bbd6039c3c8e6f2560e81c9e0a1fc41e013b5ca8de1b0b88383d352f10931af8c83ff585a1

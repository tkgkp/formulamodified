:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: jpk_sf.fml [12.51]
:: Utworzony: 15.11.2018
:: Autor: MB
:: Systemy: FIKS
::======================================================================================================================
:: Zawartosc: Formuly odpowiedzialne za obsługę e-sprawozdań (JPK_SF)
::======================================================================================================================


\generuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tworzy e-sprawozdanie
::----------------------------------------------------------------------------------------------------------------------
_jpk:=null;
JPK.cntx_psh();
JPK.prefix();
OKRO_F.cntx_psh();
OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
OKRO_F.last(); OKRO_F.prev();
{? HELPJPK.E_SPR_A='T'
|| ROK_F.cntx_psh();
   exec('akt_spr','jpk_sf',HELPJPK.E_SPR().BILANS);
   exec('akt_spr','jpk_sf',HELPJPK.E_SPR().RZIS,1);
   exec('akt_spr','jpk_sf',HELPJPK.E_SPR().ZMWKAP,1);
   exec('akt_spr','jpk_sf',HELPJPK.E_SPR().RACH,1);
   exec('akt_spr','jpk_sf',HELPJPK.E_SPR().I_PODAT,1);
   ROK_F.cntx_pop();
   exec('okres','jpk_sf','B');
   prgs_clr()
?};
REF.WYKON:=exec('wykon_firmy','konsolidacja',REF.FIRMA);
{? exec('jpk_nag','jpk',HELPJPK.OKR_DO,ROK_F.POCZ_ROK,HELPJPK.OKR_DO().KON,'e-Sprawozdania '+OKRO_F.NAZ+' '+ROK_F.NAZ,$HELPJPK.E_SPR,,,HELPJPK.OKR_DO().KON)
|| _jpk:=JPK.ref();
   VAR_DEL.delete('TList');
   _file:=exec('gen_jpk','jpk');
   VAR_DEL.delete('TList');
   {? _file<>''
   || exec('upd_jpk','jpk');
      JPK.bl_put('PLIK',_file,1);
      {? JPK.put()
      || jpkOK+=1
      ?};
      ferase(_file,1)
   ?}
?};
OKRO_F.cntx_pop();
JPK.cntx_pop();
{? _jpk || JPK.seek(_jpk) ?};
1


\pkd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Kody pkd dla e-sprawozdania
::   WE: _a - 1-first, 2-wartośc 3-next
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? _b=1
   || Pkd:=obj_new('values','value','tab','first','next');
      E_SPR.cntx_psh(); E_SPR.prefix();
      Pkd.values:={? E_SPR.seek(JPK.E_SPR)
                  || E_SPR.memo_txt(,1,'PKD')
                  || ''
                  ?};
      Pkd.values:=STR.gsub(Pkd.values,' ','');
      E_SPR.cntx_pop();
      Pkd.tab:=tab_tmp(1,'PKD','STRING[5]','PKD');
      Pkd.first:="
         _t:=spli_str(.values,',');
         {! _i:=1..obj_len(_t)
         |! {? _t[_i]<>''
            || .tab.PKD:=_t[_i];
               .tab.add()
            ?}
         !};
         .tab.first()
      ";
      Pkd.next:="
         .tab.next()
      ";
      Pkd.value:=".tab.PKD";
      Pkd.first()
   || VAR_DEL.delete('Pkd')
   ?}
|? _a=2
|| {? var_press('Pkd')>0 || Pkd.value() || 1 ?}
|? _a=3
|| Pkd.next()
?}


\ba_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed dołącz E_SPR
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_JPK_DSPR';
_params.AKCJA:='Dołącz';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',SSTALE.AO);
_params.PROC_START:='T';
exec('mp_run','#b__box',_params)


\bm_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Akcja poprawk dla e-Sprawozdania
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='FKS_JPK_DSPR';
_params.AKCJA:='Popraw';
_params.UIDREF:=E_SPR.uidref();
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'OKRO_F',SSTALE.AO);
_params.PROC_START:='N';
exec('mp_run','#b__box',_params)


\chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Sprawdza pola wymagane przez e-Sprawozdanie
::----------------------------------------------------------------------------------------------------------------------
_ret:='';
{? HELPJPK.OKR_DO
|| OSPR.cntx_psh();
   OSPR.index('OKRES1');
   OSPR.prefix(REF.FIRMA,'M',HELPJPK.OKR_DO().ROK,OKRO_F.NR,OKRO_F.NR);
   _jest:=OSPR.first();
   OSPR.cntx_pop();
   {? _jest=0
   || FUN.info('Nie znaleziono okresu sprawozdawczego: %1.'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
      _ret:='OKR_DO'
   ?}
?};
{? _ret=''
|| _ret:=__CHK.record2(HELPJPK,'E_SPR','e-Sprawozdania')
?};
{? _ret='' & HELPJPK.E_SPR().AKC<>'T'
|| FUN.info('Definicja e-Sprawozdania nie jest zakończona.'@);
   _ret:='E_SPR'
?};
_ret


\bd_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed usuń okna E_SPR
::----------------------------------------------------------------------------------------------------------------------
JPK.index('E_SPR'); JPK.prefix(E_SPR.ref());
{? JPK.first()
|| FUN.info('Istnieją pliki JPK utworzone na podstawie tej definicji.'@)
|? FUN.ask('Czy usunąć definicję e-Sprawozdania z powiązaniami?'@)
|| E_SPR_O.index('KOD'); E_SPR_O.prefix(E_SPR.ref());
   {? E_SPR_O.first() || {! |? E_SPR_O.del !} ?};
   E_SPR_F.index('E_SPR'); E_SPR_F.prefix(E_SPR.ref());
   {? E_SPR_F.first() || {! |? E_SPR_F.del() !} ?};
   E_SPR.del()
?}


\bv_jpk_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed wyświetl HELPJPK.E_SPR
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.efld_opt('RED2','enable='+$(HELPJPK.E_SPR<>null),HELPJPK,'E_SPR_A');
{? HELPJPK.RODZAJ<>'SF' | HELPJPK.ISTDEF=null
|| exec('findfnrd','color')
|| ''
?}


\be_jpk_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redagowaniem HELPJPK.E_SPR
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.RODZAJ='SF' & HELPJPK.ISTDEF
|| exec('espr_win','jpk_sf');
   1
?}


\win_e_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tworzy okno grupowe dla e-Sprawozdań
::   WE: _a - pełne okno
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
_win:=E_SPR.grp_make('e-Sprawozdania - definicja'@,,'#esprwin');
E_SPR.grp_sel(_win,,'WER',,"JpkSF.prefix()",,,,,,,,'maximized');
E_SPR.grp_splt(_win,,'horizontal','bottom',10);
E_SPR.grp_sel(_win,E_SPR_O,'WER_O','Wprowadzenie'@,,,,,"JpkSF.prefix('M')",,,,'maximized');
E_SPR.grp_sel(_win,E_SPR_O,'WER_P','Dodatkowe informacje'@,,,,,"JpkSF.prefix('B')",,,,'maximized');
E_SPR.grp_sel(_win,E_SPR_O,'WER_P','Zestawienie lokat (Załącznik 1)'@,,,,,"JpkSF.prefix('L')",,,,'maximized');
E_SPR.grp_sel(_win,E_SPR_O,'WER_P','Dodatkowe informacje (Załącznik 2)'@,,,,,"JpkSF.prefix('Z')",,,,'maximized');
E_SPR.grp_sel(_win,E_SPR_F,'WER','Firmy',,,,,"JpkSF.prefix('F')",,,,'maximized');
_win


\add_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Dodaje opis do e-Sprawozdania
::   WE: _a - kod opisu
::       _b - nazwa opisu
::       _c - typ
::----------------------------------------------------------------------------------------------------------------------
E_SPR_O.cntx_psh();
E_SPR_O.prefix();
E_SPR_O.blank(1);
E_SPR_O.E_SPR:=E_SPR.ref();
E_SPR_O.TYP:=_c;
E_SPR_O.RODZAJ:='M';
E_SPR_O.KOD:=_a;
E_SPR_O.NAZ:=_b;
E_SPR_O.add();
E_SPR_O.cntx_pop()


\bl_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość poczatkowa - zwraca nagłowek e-Sprawozdania
::----------------------------------------------------------------------------------------------------------------------
E_SPR.ref()


\blesprot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa pola E_SPR_O.TYP
::----------------------------------------------------------------------------------------------------------------------
{? var_press('JpkSF')>0 || JpkSF.typ || '' ?}


\blespror
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa pola E_SPR_O.RODZAJ
::----------------------------------------------------------------------------------------------------------------------
{? var_press('JpkSF')>0 || JpkSF.rodzaj || '' ?}


\blesprok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa pola E_SPR_O.KOD
::----------------------------------------------------------------------------------------------------------------------
{? var_press('JpkSF')>0
|| {? JpkSF.rodzaj='M'
   || _id:={? ( E_SPR.J_N().KOD='INNA' | E_SPR.J_N().KOD='ASI'  ) || 'U' || 'UM' ?};
      ISTDEFS.cntx_psh();
      ISTDEFS.index('TYPPOLA'); ISTDEFS.prefix(E_SPR.ISTDEF,_id,);
      _kod:={? ISTDEFS.first() || ISTDEFS.OPIS || 'Brak' ?};
      ISTDEFS.cntx_pop();
      {? (_p:=_kod*':') || _kod:=_p-_kod ?};
      _kod
   || ''
   ?}
|| ''
?}


\beesprok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redagowaniem pola E_SPR_O.KOD
::----------------------------------------------------------------------------------------------------------------------
{? var_press('JpkSF')>0
|| 0
|| 1
?}


\obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca tablicę JpkSF
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new('win','typ','rodzaj','prefix');
_obj.win:=exec('win_e_spr','jpk_sf');
_obj.typ:='U';
_obj.rodzaj:='M';
_obj.prefix:="
   {? var_press('_a')>0
   || .rodzaj:=_a;
      {? _a='M'
      || E_SPR_O.win_edit('RED_O')
      |? _a='B'
      || E_SPR_O.win_edit('RED_P')
      |? _a='F' & var_press('E_SPR_F')>0
      || E_SPR_F.win_edit('RED')
      ?}
   || {? -E_SPR.ISTDEF().VER*'asi'
      || tab_show(3,'bottom');tab_show(4,'bottom')
      || tab_hide(3,1,'bottom');tab_hide(4,1,'bottom')
      ?};
      {? E_SPR.ISTDEF().VER*'Skonsolid'
      || tab_show(5,'bottom')
      || tab_hide(5,1,'bottom')
      ?}
   ?};
   {? .rodzaj='F'
   || {? var_press('E_SPR_F')>0
      || _get:=E_SPR.get();
         E_SPR_F.index('SELECT');
         {? _get
         || E_SPR_F.prefix(E_SPR.ref(),'N',)
         || E_SPR_F.prefix(null,'N',)
         ?};
         _actions:='';
         {? ~_get | E_SPR.AKC='T' | E_SPR.JPK='T'
         || _actions:='DPu:D'
         ?};
         E_SPR_F.actions_grayed('WER',_actions);
         {? var_press('_a')<=0
         || grp_disp(E_SPR_F,'WER')
         ?}
      ?}
   || _win:='WER_'+{? .rodzaj='M' || 'O' || 'P' ?};
      {? var_press('FIRMA',E_SPR_O)>0
      || E_SPR_O.index('KOD2')
      || E_SPR_O.index('KOD')
      ?};
      _get:=E_SPR.get();
      {? _get
      || E_SPR_O.prefix(E_SPR.ref(),.rodzaj,)
      || E_SPR_O.prefix(null,.rodzaj,)
      ?};
      {? var_press('_a')<=0
      || grp_disp(E_SPR_O,_win)
      ?};
      _actions:='';
      {? ~_get | E_SPR.AKC='T' | E_SPR.JPK='T'
      || _actions:={? .rodzaj='M' || 'DPU:D' || 'DPUE:D' ?}
      ?};
      E_SPR_O.actions_grayed(_win,_actions)
   ?};
   1
";
_obj


\br_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Rekord przed tabeli E_SPR
::----------------------------------------------------------------------------------------------------------------------
_actions:='';
{? E_SPR.AKC='T'
|| _actions+='PUZ'
|| _actions+='W'
?};
{? E_SPR.JPK='T'
|| _actions+='U'
?};
E_SPR.actions_grayed('WER',_actions);
E_SPR.efld_opt(E_SPR.win_edit('?'),'enable='+$(E_SPR.J_N().KOD='INNA'| E_SPR.J_N().KOD='ASI'),E_SPR,'PKD');
~~


\ar_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Rekord po okna tabeli E_SPR
::----------------------------------------------------------------------------------------------------------------------
_ret:=__CHK.record2(E_SPR,
   'DATA','Data',
   'ISTDEF','Schemat',
   'NAZ','Nazwa'
);
{? _ret='' & ( E_SPR.J_N().KOD='INNA' | E_SPR.J_N().KOD='ASI' )
|| _ret:=__CHK.record2(E_SPR,'PKD','Kody PKD')
?};
{? _ret=''
|| _ret:=__CHK.record2(E_SPR,
      'J_N','Typ jednostki',
      'J_B','Typ jednostki - sprawozdania Bilans',
      'BILANS','Sprawozdanie Bilans',
      'J_R','Typ jednostki - sprawozdania Rachunek zysków i strat',
      'RZIS','Sprawozdanie Rachunek zysków i strat',
      'J_D','Typ jednostki - sprawozdania dotyczącego podatku'
   )
?};

{? _ret='' & (E_SPR.J_D().KOD='INNA' | E_SPR.J_D().KOD='ASI') & exec('el_req','jpk_sf',E_SPR.ISTDEF,'SP')
|| _ret:=__CHK.record2(E_SPR,'I_PODAT','Sprawozdanie dotyczące podatku')
?};
_ret


\ar_espro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Rekord po tabeli E_SPR_O
::----------------------------------------------------------------------------------------------------------------------
{? E_SPR_O.RODZAJ='M'
|| _ret:=__CHK.record(E_SPR_O,,'NAZ','MEMO')
|| _ret:='';
   {? exec('beesprof','jpk_sf')
   || _ret:=__CHK.record(E_SPR_O,,'FIRMA')
   ?};
   {? _ret=''
   || _ret:=__CHK.record(E_SPR_O,,'MEMO','BLOB')
   ?};
   {? _ret='' & (_dl:=+E_SPR_O.memo_txt(,0,'MEMO'))>3500
   || FUN.info(
         'Liczba znaków w opisie: %1.\n'
         'Maksymanlna liczba znaków to: 3500'@[$_dl]
      );
      _ret:='MEMO'
   ?};
   {? _ret=''
   || _name:=E_SPR_O.bl_file('BLOB');
      {? _name<>''
      || _name:=STR.gsub(_name,'/','\\');
         _tab:=spli_str(_name,'\\');
         _name:=_tab[obj_len(_tab)];
         _dl:=+_name;
         {? _dl<5 | _dl>55
         || FUN.info(
               'Liczba znaków w nazwie pliku: %1.\n'
               'Wymagana długość od 5 do 55 znaków.'@[$_dl]
            );
            _ret:='BLOB'
         ?};
         {? _ret=''
         || _baza:='abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789._-';
            _err:='';
            _ok:=1;
            {! _i:=1.._dl
            |! _zn:=_i+_name+1;
               {? _baza*_zn=0
               || {? _err*_zn=0
                  || _err:=_err+_zn
                  ?};
                  _ok:=0
               ?}
            !};
            {? _ok=0
            || FUN.info('Niedozwolone znaki w nazwie pliku: \'%1\'.'@[_err]);
               _ret:='BLOB'
            ?}
         ?}
      ?}
   ?}
?};
_ret


\opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: [_a] - pole e-Sprawozdania
::----------------------------------------------------------------------------------------------------------------------
_kod:={? var_press('_a')>0 || _a || ISTDEFS.OPIS ?};
{? (_p:=_kod*':') || _kod:=_p-_kod ?};
_txt:='';
E_SPR_O.cntx_psh();
E_SPR_O.index('KOD'); E_SPR_O.prefix(JPK.E_SPR,'M',_kod,);
{? E_SPR_O.first()
|| _txt:=E_SPR_O.memo_txt(,1,'MEMO')
?};
E_SPR_O.cntx_pop();
_txt


\big_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obsługa e-Sprawozdania - długie opisy
::   WE: _a - 1,10-start,stop pętli 2-następny element 3-wartość
::       _b - 1-start 0-stop
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? _b=1
   || ISTDEFS.cntx_psh();
      ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEFS.ISTDEF,ISTDEFS.ref());
      {? ISTDEFS.first()
      || _kod:=ISTDEFS.OPIS
      ?};
      ISTDEFS.cntx_pop();
      {? (_poz:=_kod*':') || _kod:=_poz-_kod ?};
      JpkTxt:='';
      E_SPR_O.cntx_psh();
      E_SPR_O.index('KOD'); E_SPR_O.prefix(JPK.E_SPR,'M',_kod,);
      {? E_SPR_O.first()
      || JpkTxt:=E_SPR_O.memo_txt(,1,'MEMO')
      ?};
      E_SPR_O.cntx_pop();
      JpkTxt<>''
   || VAR_DEL.delete('JpkTxt')
   ?}
|? _a=10
|| {? _b=1
   || JpkTxt:=E_SPR_O.memo_txt(,1,'MEMO');
      JpkTxt<>''
   || VAR_DEL.delete('JpkTxt')
   ?}
|? _a=2
|| JpkTxt<>''
|| _txt:=3500+JpkTxt;
   JpkTxt:=3500-JpkTxt;
   _txt
?}


\opisy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obsługa e-Sprawozdania - nazwy + opis
::   WE: _a - pole
::       _b - 1-start,stop pętli 2-następny element
::       _c - 1-start 0-stop
::----------------------------------------------------------------------------------------------------------------------
{? _b=1
|| {? _c=1
   || _kod:=_a;
      E_SPR_O.cntx_psh();
      E_SPR_O.index('KOD'); E_SPR_O.prefix(JPK.E_SPR,'M',_kod,);
      E_SPR_O.first()
   || E_SPR_O.cntx_pop()
   ?}
|? _b=2
|| E_SPR_O.next()
?}


\pliki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obsługa e-Sprawozdania - Dodatkowe informacje i objaśnienia (pliki)
::   WE:  _a  - 1-start,stop pętli 2-następny element
::       [_b] - 1-start 0-stop
::       [_c] - Wskazanie na firmę
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? _b=1
   || E_SPR_O.cntx_psh();
      {? var_press('_c')>0
      || E_SPR_O.index('FIRMA'); E_SPR_O.prefix(JPK.E_SPR,_c,'B')
      || E_SPR_O.index('KOD'); E_SPR_O.prefix(JPK.E_SPR,'B')
      ?};
      E_SPR_O.first()
   || E_SPR_O.cntx_pop()
   ?}
|? _a=2
|| E_SPR_O.next()
?}


\pliki_z
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Pawelm [21.37]
:: OPIS: Obsługa e-Sprawozdania - Dodatkowe informacje załącznik 2 (pliki)
::   WE:  _a  - 1-start,stop pętli 2-następny element
::       [_b] - 1-start 0-stop
::       [_c] - Wskazanie na firmę
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? _b=1
   || E_SPR_O.cntx_psh();
      {? var_press('_c')>0
      || E_SPR_O.index('FIRMA'); E_SPR_O.prefix(JPK.E_SPR,_c,'Z')
      || E_SPR_O.index('KOD'); E_SPR_O.prefix(JPK.E_SPR,'Z')
      ?};
      E_SPR_O.first()
   || E_SPR_O.cntx_pop()
   ?}
|? _a=2
|| E_SPR_O.next()
?}


\plik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obsługa e-Sprawozdania - zapis zawartości pliku do xml-a
::----------------------------------------------------------------------------------------------------------------------
{? var_press('XmlFile')<=0 || return(0) ?};
_tryb:='encode';
_in:=E_SPR_O.BLOB;
_out:='b64'+$SYSLOG.tm_stamp()+'.b64';
_max:=256;

_ret:=0;
_fin:=fopen(_in,'br',0,,1);
{? _fin.is_open()
|| _fout:=fopen(_out,'bw',1,,1);
   {? _fout.is_open()
   || _ret:=base64(_tryb,_fin,_fout);
      _fout.fclose()
   ?};
   &_fout;
   _fin.fclose()
?};
&_fin;
{? _ret
|| _fin:=fopen(_out,'r',1,,1);
   {? _fin.is_open()
   || fwrite(XmlFile,'<dtsf:Zawartosc>');
      {!
      |? {? (_line:=fread(_fin))<>'\n'
         || {!
            |? _tmp:=_max+_line;
               _line:=_max-_line;
               fwrite(XmlFile,_tmp);
               _line<>''
            !};
            1
         ?}
      !};
      fwrite(XmlFile,'</dtsf:Zawartosc>');
      _fin.fclose();
      ferase(_out,1)
   ?}
?}


\spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia sprawozdanie
::   WE: _a - typ sprawozdania
::----------------------------------------------------------------------------------------------------------------------
SIK.GR_SIK:=null;
{? _a='BILANS' || SIK.GR_SIK:=JPK.E_SPR().BILANS
|? _a='RZIS' || SIK.GR_SIK:=JPK.E_SPR().RZIS
|? _a='ZMWKAP' || SIK.GR_SIK:=JPK.E_SPR().ZMWKAP
|? _a='RACH' || SIK.GR_SIK:=JPK.E_SPR().RACH
|? _a='I_PODAT'
|| {? JPK.ISTDEF().VER*'Skonsolid'
   || SIK.GR_SIK:=E_SPR_F.I_PODAT
   || SIK.GR_SIK:=JPK.E_SPR().I_PODAT
   ?}
?};
{? SIK.GR_SIK
|| {? SIK.GR_SIK().AKC<>'T'
   || _path:=exec('getPath','xml');
      exec('addkom','jpk','W','Sprawozdanie: '+GR_SIK.SKROT+' nie jest zaakceptowane w firmie '+GR_SIK.FIRMA().SYMBOL+'.')
   ?};
   TysSpr:=SIK.GR_SIK().ZAOKR=1000 & SIK.GR_SIK().DOKL=0;
   GR_KOL.cntx_psh();
   GR_KOL.index('E_KOD'); GR_KOL.prefix(SIK.GR_SIK,'KwotaB',);
   KolB:=GR_KOL.first();
   GR_KOL.cntx_pop()
?};
SIK.GR_SIK<>null


\wiersz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ustawia wiersz sprawozdania
::----------------------------------------------------------------------------------------------------------------------
SIK.DEFW:=null;
{? SIK.GR_SIK
|| _kod:=ISTDEFS.OPIS;
   _p:=_kod*':';
   {? _p
   || _kod:=_p-_kod
   ?};
   DEFW.index('E_KOD'); DEFW.prefix(SIK.GR_SIK,_kod,);
   {? DEFW.first()
   || SIK.DEFW:=DEFW.ref()
   || _path:=exec('getPath','xml');
      exec('addkom','jpk','E','Sprawozdanie: '+SIK.GR_SIK().SKROT+'. Brak wiersza o e-kodzie: '+_kod+'.')
   ?};
   1
?}


\value
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca wartość sprawozdania
::   WE: [_a] - nazwa pola z kolumną
::       [_b] - okres: B-bieżacy P-poprzedni
::----------------------------------------------------------------------------------------------------------------------
_val:=0;
{? var_press('_a')>0 & _a='KwotaA' & var_press('_b')>0 & _b='P' & var_press('KolB')>0 & KolB
|| _a:=_b:=''
?};
{? var_press('_b')>0 & _b<>''
|| _okr:=OKRESY.OKRES1;
   WARTOSCI.cntx_psh();
   exec('okres','jpk_sf',_b)
?};
_tys:=var_press('TysZL')>0 & TysZL;
{? SIK.GR_SIK & SIK.DEFW
|| SIK.GR_KOL:=null;
   _kod:={? var_press('_a')>0 & _a<>'' || _a || ISTDEFS.OPIS ?};
   _p:=_kod*':';
   {? _p
   || _kod:=_p-_kod
   ?};
   GR_KOL.cntx_psh();
   GR_KOL.index('E_KOD'); GR_KOL.prefix(SIK.GR_SIK,_kod);
   {? GR_KOL.first()
   || SIK.GR_KOL:=GR_KOL.ref()
   ?};
   GR_KOL.cntx_pop();
   {? SIK.GR_KOL
   || WARTOSCI.index('OKRES');
      WARTOSCI.prefix(OKRESY.OKRES1,REF.WYKON,SIK.DEFW,SIK.GR_KOL,null,'N');
      {? WARTOSCI.first()
      || _val:={? _tys
               || {? var_press('TysSpr')>0 & TysSpr
                  || WARTOSCI.WZAOKR
                  || (WARTOSCI.WARTOSC/1000)$0
                  ?}
               || WARTOSCI.WARTOSC
               ?}
      ?}
   || exec('addkom','jpk',{? ISTDEFS.WYM='T' || 'E' || 'W' ?},'Sprawozdanie: '+SIK.GR_SIK().SKROT+'. Brak kolumny o e-kodzie: '+_kod+'.')
   ?}
?};
{? var_press('_b')>0 & _b<>''
|| OKRESY.OKRES1:=_okr;
   WARTOSCI.cntx_pop()
?};
{? _val
|| {? _tys
   || form(_val,,,'9.')
   || form(_val,,2,'9.')
   ?}
|? ISTDEFS.WYM='T'
|| {? _tys
   || '0'
   || '0.00'
   ?}
|| ''
?}


\okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia okres sprawozdawczy na potrzeby tworzenia e-Sprawozdań
::   WE: _a - typ okresu: B-bieżący P-poprzedni
::----------------------------------------------------------------------------------------------------------------------
{? var_press('eSprOkr')>0
|| {? _a='B'
   || OKRESY.OKRES1:=eSprOkr.OsprB;
      WARTOSCI.use('wsik_'+eSprOkr.MaskaB)
   || OKRESY.OKRES1:=eSprOkr.OsprP;
      WARTOSCI.use('wsik_'+eSprOkr.MaskaP)
   ?}
?};
1


\details
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obsługa pozycji uszczegóławiających
::   WE: _a - 0-czy są pozycje szczegółowe
::            1-start/stop
::            2-next
::            3-podpozycja
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| {? SIK.DEFW
   || DEFW.index('GR_NAD'); DEFW.prefix(SIK.GR_SIK,SIK.DEFW);
      {? DEFW.first()
      || SIK.DEFW:=DEFW.ref();
         1
      ?}
   ?}
|? _a=1
|| {? _b=1
   || 1
   ?}
|? _a=2
|| {? DEFW.next()
   || SIK.DEFW:=DEFW.ref();
      1
   ?}
|? _a=3 & var_press('XmlFile')>0 & XmlFile
|| DEFW.cntx_psh();
   DEFW.index('GR_NAD'); DEFW.prefix(SIK.GR_SIK,SIK.DEFW);
   {? DEFW.first()
   || {!
      |? _defw:=SIK.DEFW;
         SIK.DEFW:=DEFW.ref();
         fwrite(XmlFile,'<dtsf:Podpozycja>');
         fwrite(XmlFile,'<dtsf:NazwaPozycji>'+maz_utf8(DEFW.NAZWA)+'</dtsf:NazwaPozycji>');
         fwrite(XmlFile,'<dtsf:KwotyPozycji>');
         ISTDEFS.WYM:='T';
         fwrite(XmlFile,'<dtsf:KwotaA>'+exec('value','jpk_sf','KwotaA')+'</dtsf:KwotaA>');
         fwrite(XmlFile,'<dtsf:KwotaB>'+exec('value','jpk_sf','KwotaB')+'</dtsf:KwotaB>');
         ISTDEFS.WYM:='N';
         _val:=exec('value','jpk_sf','KwotaB1');
         {? _val<>''
         || fwrite(XmlFile,'<dtsf:KwotaB1>'+_val+'</dtsf:KwotaB1>')
         ?};
         fwrite(XmlFile,'</dtsf:KwotyPozycji>');
         exec('details','jpk_sf',3);
         fwrite(XmlFile,'</dtsf:Podpozycja>');
         SIK.DEFW:=_defw;
         DEFW.next()
      !}
   ?};
   DEFW.cntx_pop();
   0
?}


\parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS:  _a  - 1-tag poczatkowy 0-tag końcowy
::       [_b] - w tysiącach 1-tak [0]-nie
:: ~OST: INCLIEXEC,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| VAR_DEL.delete('eSpr');
   eSpr:=obj_new(
      'tab','tabI','tab2','tab2I','tabNag','tabNagI','tabOpis','tabPliki','tabF','spr','lp','lp2','lpNag','lpPliki','tys','firma',
      'get_nag','get_opis','get_naz','get_firma','plikOpis',
      'skonsolid','var',
      'setMF',
      'tabS'
   );
   XmlInfo.fml_def:=$"exec('parse_def','jpk_sf',_a,_b)";
   eSpr.tys:={? var_press('_b')>0 || _b ?};
   eSpr.skonsolid:=JPK.ISTDEF().VER*'Skonsolid';
   eSpr.lp:=0;
   eSpr.lp2:=0;
   eSpr.lpNag:=0;
   eSpr.lpPliki:=0;
   eSpr.var:=0;
   eSpr.tab:=tab_tmp(1,
      'LP','INTEGER','Lp.',
      'SPR','STRING[40]','Sprawozdanie',
      'WIERSZ','STRING[255]','Wiersz',
      'KA','REAL','Kwota A',
      'KB','REAL','Kwota B',
      'KB1','REAL','Kwota B1',
      'JA','INTEGER','Jest A',
      'JB','INTEGER','Jest B',
      'JB1','INTEGER','Jest B1'
   );
   eSpr.tabI:=obj_new('def','spr');
   eSpr.tabI.def:=eSpr.tab.index('?');
   eSpr.tabI.spr:=eSpr.tab.ndx_tmp('',1,'SPR',,0, 'LP',,0);
   eSpr.tab2:=tab_tmp(3,
      'LP','INTEGER','Lp.',
      'FIRMA','STRING[50]','Firma',
      'WIERSZ','STRING[200]','Wiersz',
      'PODSTAWA','STRING[40]','Podstawa prawna',
      'RBKA','REAL','Kwota A (rok bieżący)',
      'RBKB','REAL','Kwota B (rok bieżący)',
      'RBKC','REAL','Kwota C (rok bieżący)',
      'RPKA','REAL','Kwota A (rok poprzedni)',
      'RPKB','REAL','Kwota B (rok poprzedni)',
      'RPKC','REAL','Kwota C (rok poprzedni)',
      'RP','INTEGER','Rok poprzedni?',
      'JBKA','INTEGER','Jest Kwota A (rok bieżący)',
      'JBKB','INTEGER','Jest Kwota B (rok bieżący)',
      'JBKC','INTEGER','Jest Kwota C (rok bieżący)',
      'JPKA','INTEGER','Jest Kwota A (rok poprzedni)',
      'JPKB','INTEGER','Jest Kwota B (rok poprzedni)',
      'JPKC','INTEGER','Jest Kwota C (rok poprzedni)'
   );
   eSpr.tab2I:=obj_new('def','firma');
   eSpr.tab2I.def:=eSpr.tab2.index('?');
   eSpr.tab2I.firma:=eSpr.tab2.ndx_tmp('',1,'FIRMA',,0);

   eSpr.tabNag:=tab_tmp(1,
      'LP','INTEGER','LP',
      'K','STRING[50]','Klucz',
      'NAZ','STRING[255]','Nazwa',
      'V','STRING[255]','Wartość'
   );
   eSpr.tabNagI:=obj_new('def','K');
   eSpr.tabNagI.def:=eSpr.tabNag.index('?');
   eSpr.tabNagI.K:=eSpr.tabNag.ndx_tmp('',1,'K',,0);

   eSpr.tabOpis:=tab_tmp(1,
      'K','STRING[50]','Klucz',
      'NAZ','SYS_MEMO','Nazwa',
      'MEMO','SYS_MEMO','Opis'
   );
   eSpr.tabPliki:=tab_tmp(1,
      'LP','INTEGER','LP',
      'OPIS','SYS_MEMO','Opis',
      'NAZ','STRING[255]','Nazwa pliku',
      'FN','STRING[255]','Plik'
   );
   eSpr.plikOpis:="
      _tab:=.tabPliki;
      gsub(_tab.memo_txt(,1,'OPIS'),'\n','|')
   ";
   eSpr.tabF:=tab_tmp(1,
      'FIRMA','STRING[255]','Firma i siedziba',
      'FS','SYS_MEMO','Firma i siedziba - opis',
      'PRZED','SYS_MEMO','Przedmiot działalności',
      'UDZIAL','REAL','Udział',
      'GLOSY','REAL','Głosy',
      'POW','SYS_MEMO','Powiązanie',
      'TYP','STRING[1]','Typ',
      'TYP_STR','STRING[100]','Typ opis',
      'INNE','SYS_MEMO','Inne',
      'KAPITAL','SYS_MEMO','Kapitał',
      'PODSTAWA','SYS_MEMO','Podstawa wyłaczenia',
      'D_OD','DATE','Data od',
      'D_DO','DATE','Data do',
      'D_DO_O','SYS_MEMO','Data do opis',
      'S_OD','DATE','Okres spr. od',
      'S_DO','DATE','Okres spr. do',
      'L_TN','STRING[5]','Po połączeniu',
      'L_METODA','SYS_MEMO','Metoda połączenia'
   );
   eSpr.tabS:=tab_tmp(2,
      'DATE','DATE','Data',
      'TIME','TIME','Czas',
      'DT','STRING[255]','Data i czas',
      'S','STRING[255]','Sygnatariusz'
   );

   eSpr.setMF:="
      .tabF.memo_set(_b,_a);
      .tabF.memo_put(,_a);
      .tabF.put()
   ";
   eSpr.get_nag:="
      _kod:=_a;
      _first:={? var_press('_b')>0 || _b || 1 ?};
      _value:='';
      _tab:=.tabNag;
      _tab.cntx_psh();
      _tab.index(.tabNagI.K);
      _tab.prefix(_a,);
      {? _first>0 & _tab.first() | _first=0 & _tab.last()
      || {!
         |? _value+=_tab.V+{? _first=2 || ', ' || '' ?};
            _first=2 & _tab.next()
         !}
      ?};
      _tab.cntx_pop();
      {? _first=3
      || _value:={? _value='true' || 'TAK' || 'NIE' ?}
      ?};
      {? _first=2 || _value-2 || _value ?}
   ";
   eSpr.get_opis:="
      _kod:=_a;
      _fld:={? var_press('_b')>0 || _b || 'MEMO' ?};
      _value:='';
      _tab:=.tabOpis;
      _tab.cntx_psh();
      _tab.prefix(_a,);
      {? _tab.first()
      || _value:=_tab.memo_txt(,1,_fld)
      ?};
      _tab.cntx_pop();
      STR.gsub(_value,'\t',' ')
   ";
   eSpr.get_firma:="
      .tabF.memo_txt(,1,_a)
   ";
   eSpr.firma:=REF.FIRMA().OPIS;
   {? JPK.JPK_SIG
   || __tmpdir:=fmk_tmp_dir(0);
      {? type_of(__tmpdir) <> type_of(~~)
      || __fdir:=__tmpdir.get_path
      || return(0)
      ?};
      _fn:=__fdir+'/espr.tmp';
      {? JPK.bl_get('JPK_SIG',_fn,0)
      || exec('SignInfo','object');
         SignInfo.parse(_fn);
         {? SignInfo.first()
         || {!
            |? eSpr.tabS.blank(1);
               _sub:=SignInfo.get('subject');
               _sub1:=spli_str(_sub,',');
               {! _i:=1..obj_len(_sub1)
               |! {? (_p:=_sub1[_i]*'CN=')
                  || eSpr.tabS.S:=(_p+2)-_sub1[_i]
                  ?}
               !};
               &_sub1;
               {? eSpr.tabS.S='' || eSpr.tabS.S:=_sub ?};
::             2019-03-29T14:57:11Z
               _dt:=SignInfo.get('time');
               eSpr.tabS.DT:=_dt;
               eSpr.tabS.DATE:=date(#(4+_dt),#(2+(5-_dt)),#(2+(8-_dt)));
               _h:=#(2+(11-_dt));
               {? 1+(19-_dt)='Z' || _h+=2 ?};
               {? _h>23 || _h=_h-24; eSpr.tabS.DATE:=eSpr.tabS.DATE+1 ?};
               eSpr.tabS.TIME:=time(_h,#(2+(14-_dt)),#(2+(17-_dt)));
               eSpr.tabS.add();
               SignInfo.next()
            !}
         ?};
         ferase(_fn)
      ?}
   ?};
   0
|| _tab:=eSpr.tab;
   _aDruk:="exec('espr_prn','jpk_sf')";
   _win:=_tab.mk_sel(XmlInfo.title,'P',,'#esprdane',,,,,'U');
   _tab.win_fld(_win,,'SPR');
   _tab.win_fld(_win,,'WIERSZ',,,68);
   _tab.win_fld(_win,,'KA',,,20,{? eSpr.tys || 0 || 2 ?},,,,'Kwota na dzień kończący bieżący rok obrotowy');
   _tab.win_fld(_win,,'KB',,,20,{? eSpr.tys || 0 || 2 ?},,,,'Kwota na dzień kończący poprzedni rok obrotowy');
   _tab.win_fld(_win,,'KB1',,,20,{? eSpr.tys || 0 || 2 ?},,,,'Przekształcone dane porównawcze za poprzedni rok obrotowy');
   _tab.win_act(_win,,'Formuła','Drukuj',,,_aDruk);
   _tab.win_sel(_win);
   _fml:="
      _tab:=eSpr.tab;
      {? cur_afld()='KA' || {? _tab.JA || '' || 0 ?}
      |? cur_afld()='KB' || {? _tab.JB || '' || 0 ?}
      |? cur_afld()='KB1' || {? _tab.JB1 || '' || 0 ?}
      || ''
      ?}
   ";
   _tab.fld_fml('KA','BEFORE_DISPLAY',_fml);
   _tab.fld_fml('KB','BEFORE_DISPLAY',_fml);
   _tab.fld_fml('KB1','BEFORE_DISPLAY',_fml);

   _tab2:=eSpr.tab2;
   _win2:=_tab2.mk_sel(XmlInfo.title,'P',,'#esprdane2',,,,,'U');
   _tab2.win_fld(_win2,,'FIRMA',,,5);
   _tab2.win_fld(_win2,,'WIERSZ',,,12);
   _tab2.win_fld(_win2,,'PODSTAWA',,,10);
   _tab2.win_fld(_win2,,'RPKA',,,20,{? eSpr.tys || 0 || 2 ?},,,,'Wartość łączna (rok poprzedni)');
   _tab2.win_fld(_win2,,'RPKB',,,20,{? eSpr.tys || 0 || 2 ?},,,,'Z zysków kapitałowych (rok poprzedni)');
   _tab2.win_fld(_win2,,'RPKC',,,20,{? eSpr.tys || 0 || 2 ?},,,,'Z innych źródeł przychodów (rok poprzedni)');
   _tab2.win_fld(_win2,,'RBKA',,,20,{? eSpr.tys || 0 || 2 ?},,,,'Wartość łączna (rok bieżacy)');
   _tab2.win_fld(_win2,,'RBKB',,,20,{? eSpr.tys || 0 || 2 ?},,,,'Z zysków kapitałowych (rok bieżący)');
   _tab2.win_fld(_win2,,'RBKC',,,20,{? eSpr.tys || 0 || 2 ?},,,,'Wartość łączna (rok bieżący)');
   _tab2.win_act(_win2,,'Formuła','Drukuj',,,_aDruk);
   _fml:="
      _tab:=eSpr.tab2;
      {? cur_afld()='RPKA' || {? _tab.JPKA || '' || 0 ?}
      |? cur_afld()='RPKB' || {? _tab.JPKB || '' || 0 ?}
      |? cur_afld()='RPKC' || {? _tab.JPKC || '' || 0 ?}
      |? cur_afld()='RBKA' || {? _tab.JBKA || '' || 0 ?}
      |? cur_afld()='RBKB' || {? _tab.JBKB || '' || 0 ?}
      |? cur_afld()='RBKC' || {? _tab.JBKC || '' || 0 ?}
      || ''
      ?}
   ";
   _tab2.fld_fml('RPKA','BEFORE_DISPLAY',_fml);
   _tab2.fld_fml('RPKB','BEFORE_DISPLAY',_fml);
   _tab2.fld_fml('RPKC','BEFORE_DISPLAY',_fml);
   _tab2.fld_fml('RBKA','BEFORE_DISPLAY',_fml);
   _tab2.fld_fml('RBKB','BEFORE_DISPLAY',_fml);
   _tab2.fld_fml('RBKC','BEFORE_DISPLAY',_fml);

   _tabg:=_tab3:=eSpr.tabNag;
   _win3:=_tab3.mk_sel('Nagłowek','P',,'esprdane3',,,,,'U');
   _tab3.win_fld(_win3,,'NAZ',,,140);
   _tab3.win_fld(_win3,,'V',,,31);
   _tab3.win_act(_win3,,'Formuła','Drukuj'@@,,,_aDruk);

   _tab4:=eSpr.tabOpis;
   _win4:=_tab4.mk_sel('Wprowadzenie','P',,'esprdane4',,,,,'U');
   _tab4.win_fld(_win4,,'NAZ',,,71);
   _tab4.win_fld(_win4,,'MEMO',,,100);
   _tab4.win_act(_win4,,'Wyświetl',,,,"
      _tab:=cur_tab(1,1);
      _tab.memo_get(,'MEMO',0);
      _tab.memo_view(1,'MEMO')
   ");
   _tab4.win_act(_win4,,'Formuła','Drukuj'@@,,,_aDruk);

   {? eSpr.skonsolid
   || _tab6:=eSpr.tabF;
      _win6:=_tab6.mk_sel('Firmy','P',,'esprdane6',,,,,'U');
      _tab6.win_fld(_win6,,'FIRMA',,,130);
      _tab6.win_fld(_win6,,'TYP_STR',,,30);
      _tab6.win_fld(_win6,,'UDZIAL',,,10);
      _tab6.win_act(_win6,,'Formuła','Drukuj'@@,,,_aDruk);
      _tab6.win_act(_win6,,'Kolejność');
      _tab6.win_act(_win6,,'Wyświetl',,,,"
         _tab:=cur_tab(1,1);
         E_SPR_F.cntx_psh();
         E_SPR_F.blank(1);
         E_SPR_F.TYP:=_tab.TYP;
         E_SPR_F.UDZIAL:=_tab.UDZIAL;
         E_SPR_F.GLOSY:=_tab.GLOSY;
         E_SPR_F.memo_set(_tab.memo_txt(,1,'FS'),'SIEDZIBA');
         E_SPR_F.memo_set(_tab.memo_txt(,1,'PRZED'),'PRZED');
         E_SPR_F.memo_set(_tab.memo_txt(,1,'POW'),'POW');
         E_SPR_F.memo_set(_tab.memo_txt(,1,'INNE'),'INNE');
         E_SPR_F.memo_set(_tab.memo_txt(,1,'KAPITAL'),'KAPITAL');
         E_SPR_F.memo_set(_tab.memo_txt(,1,'PODSTAWA'),'PODSTAWA');
         _dz:=date(0,0,0);
         {? _tab.D_OD<>_dz
         || E_SPR_F.D_OD:=_tab.D_OD;
            E_SPR_F.D_DO:=_tab.D_DO;
            _txt:=_tab.memo_txt(,1,'D_DO_O');
            E_SPR_F.memo_set(_txt,'D_DO_O');
            {? _txt<>''
            || E_SPR_F.OGRANICZ:='TO'
            || E_SPR_F.OGRANICZ:='TD'
            ?}
         || E_SPR_F.OGRANICZ:='NN'
         ?};
         {? _tab.S_OD<>_dz
         || E_SPR_F.INNY_OKR:='T';
            E_SPR_F.S_OD:=_tab.S_OD;
            E_SPR_F.S_DO:=_tab.S_DO
         ?};
         E_SPR_F.L_TN:={? _tab.L_TN='true' || 'T' || 'N' ?};
         {? E_SPR_F.L_TN='T'
         || E_SPR_F.memo_set(_tab.memo_txt(,1,'L_METODA'),'L_METODA')
         ?};
         E_SPR_F.win_edit('DISP');
         E_SPR_F.edit();
         E_SPR_F.cntx_pop()
      ")
   ?};

   _tab5:=eSpr.tabPliki;
   _win5:=_tab5.mk_sel('Dodatkowe informacje','P',,'esprdane5',,,,,'U');
   _tab5.win_fld(_win5,,'OPIS',,,71);
   _tab5.win_fld(_win5,,'NAZ',,,100);
   _tab5.win_act(_win5,,'Formuła','Otwórz'@@,,,"
      _tab:=cur_tab(1,1);
      _fn:={? exec('interm','#system') || __fdir || '@'+tmp_dir() ?}+'/'+_tab.NAZ;
      {? fcopy(_tab.FN,_fn,1,0,1)
      || {? exec('interm','#system')
         || dlg_save(_fn)
::         exec('bl_save','#blob',_tab,'FN')
         || cli_exec(1-_fn)
         ?}
      || FUN.info('Nie udało się pobranie pliku z serwera.')
      ?}
   ",,1);
   _tab5.win_act(_win5,,'Formuła','Zapis'@@,,,"
      _tab:=cur_tab(1,1);
      _fn:=__fdir+'/'+_tab.NAZ;
      {? _fn<>''
      || fcopy(_tab.FN,_fn,1,0,1);
         {? dlg_save(_fn) =''
         || FUN.info('Nie udało się pobranie pliku z serwera.')
         ?}
      ?}
   ");
   _tab5.win_act(_win5,,'Wyświetl',,,,"
      _tab:=cur_tab(1,1);
      _tab.memo_get(,'OPIS',0);
      _tab.memo_view(1)
   ");
   _tab5.win_act(_win5,,'Formuła','Drukuj'@@,,,_aDruk);

   _tab7:=eSpr.tabS;
   _win7:=_tab7.mk_sel('Dodatkowe informacje','P',,'esprdane7',,,,,'U');
   _tab7.win_fld(_win7,,'DATE',,,10);
   _tab7.win_fld(_win7,,'TIME',,,8);
   _tab7.win_fld(_win7,,'S',,,152);
   _tab7.win_act(_win7,,'Formuła','Drukuj'@@,,,_aDruk);

   _grp:=_tabg.grp_make(XmlInfo.title,,'#esprdane3a');
   _tabg.grp_sel(_grp,_tab3,_win3,'Nagłówek',,,,,,,,,'maximized');
   _tabg.grp_sel(_grp,_tab4,_win4,'Wprowadzenie',,,,,,,,,'maximized');
   {? eSpr.skonsolid
   || _tabg.grp_sel(_grp,_tab6,_win6,'Firmy',,,,,,,,,'maximized')
   ?};
   _tabg.grp_sel(_grp,_tab,_win,'Sprawozdania',,,,,,,,,'maximized');
   _tabg.grp_sel(_grp,_tab5,_win5,'Informacje dodatkowe',,,,,,,,,'maximized');
   _tabg.grp_sel(_grp,_tab2,_win2,'Informacja dodatkowa dotycząca podatku dochodowego',,,,,,,,,'maximized');
   _tabg.grp_sel(_grp,_tab7,_win7,'Podpisy',,,,,,,,,'maximized');

   _tabg.win_sel(_grp);
   _tab.first();
   _tab2.first();
   _tab3.first();
   _tab4.first();
   _tab5.first();
   {? eSpr.skonsolid
   || _tab6.first()
   ?};
   _tab7.first();
   _tabg.select();
   {? _tab5.first()
   || {!
      |? ferase(_tab5.FN,1);
         _tab5.next()
      !}
   ?};
   VAR_DEL.delete('eSpr','__fdir','__tmpdir');
   0
?}


\p_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - sprawozdanie
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| eSpr.spr:=ISTDEFS.memo_txt(,1,'ANOTACJA')
?};
0


\p_wiersz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - wiersz
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| _kod:=ISTDEFS.OPIS;
   {? (_p:=_kod*':')
   || _kod:=_p-_kod
   ?};
   _tab:=eSpr.tab;
   _tab.blank(1);
   _tab.LP:=(eSpr.lp+=1);
   _tab.SPR:=eSpr.spr;
   _tab.WIERSZ:=ISTDEFS.memo_txt(,1,'ANOTACJA');
   _tab.add()
?};
0


\p_value
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - kolumna z wartością
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| _kod:=ISTDEFS.OPIS;
   {? (_p:=_kod*':')
   || _kod:=_p-_kod
   ?};
   _tab:=eSpr.tab;
   _val:=#_b;
   {? _kod='KwotaA' || _tab.KA:=_val; _tab.JA:=1; _tab.put()
   |? _kod='KwotaB' || _tab.KB:=_val; _tab.JB:=1; _tab.put()
   |? _kod='KwotaB1' || _tab.KB1:=_val; _tab.JB1:=1; _tab.put()
   ?}
?};
1


\p_w_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - wiersz informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| _kod:=ISTDEFS.OPIS;
   {? (_p:=_kod*':')
   || _kod:=_p-_kod
   ?};
   _tab:=eSpr.tab2;
   _tab.blank(1);
   _tab.FIRMA:=eSpr.firma;
   _tab.LP:=(eSpr.lp2+=1);
   _tab.WIERSZ:=ISTDEFS.memo_txt(,1,'ANOTACJA');
   _tab.add()
?};
0


\p_v_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - kolumna z wartością dla informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| _kod:=ISTDEFS.OPIS;
   {? (_p:=_kod*':')
   || _kod:=_p-_kod
   ?};
   _tab:=eSpr.tab2;
   _val:=#_b;
   _tab[_c+4]:=_val;
   {? _c>3
   || _tab.RP:=1
   ?};
   _tab[_c+11]:=1;
   _tab.put()
?};
1


\p_pu_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - Nazwa wiersza pozycji uszczegóławiającej
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| _tab:=eSpr.tab;
   _tab.blank(1);
   _tab.LP:=(eSpr.lp+=1);
   _tab.SPR:=eSpr.spr;
   _tab.WIERSZ:=_b;
   _tab.add()
?};
1


\akt_spr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Aktualizacja sprawozdania przed utworzeniem deklaracji CIT
::   WE:  _a  - wskazanie na sprawozdanie
::       [_b] - również poprzedni rok? 1-tak [0]-nie
::----------------------------------------------------------------------------------------------------------------------
_poprzedni:={? var_press('_b')>0 || _b || 0 ?};
GR_SIK.cntx_psh();
GR_SIK.index('SKROT');
GR_SIK.prefix();
{? GR_SIK.seek(_a)
|| HELPJPK.OKR_DO();
   SIK.GR_SIK:=GR_SIK.ref();
   OKRESY.ROK1:=SSTALE.AR;
   OKRESY.OKRES_OD:=HELPJPK.OKR_DO;
   OKRESY.OKRES_DO:=HELPJPK.OKR_DO;
   OKRESY.AKT_POW:='T';
   OKRESY.CZYMC:='T';
   AUTO:=0;
   __OneF:=0;
   {!
   |? progress(,
         'Aktualizacja sprawozdania: '+GR_SIK.SKROT+' za '+OKRESY.OKRES_OD().NAZ+' '+OKRESY.OKRES_OD().ROK().NAZ+' ...',,1,0
      );
      GR_SIK.prefix();
      exec('generuj_firma0','!fks_ksp_deat');
      _dalej:=0;
      {? _poprzedni
      || _poprzedni:=0;
         ROK_F.cntx_psh();
         ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA); SSTALE.AR();
         {? ROK_F.prev()
         || OKRESY.ROK1:=ROK_F.ref();
            OKRO_F.cntx_psh();
            OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref(),OKRESY.OKRES_OD().NR);
            {? OKRO_F.first()
            || OKRESY.OKRES_OD:=OKRESY.OKRES_DO:=OKRO_F.ref();
               _dalej:=1
            ?};
            OKRO_F.cntx_pop()
         ?};
         ROK_F.cntx_pop()
      ?};
      _dalej
   !};
   VAR_DEL.delete('AUTO','__OneF')
?};
GR_SIK.cntx_pop()


\espr_kopia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tworzy kopię definicji e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
_ref:=E_SPR.ref();
E_SPR.memo_get(,'PKD');
_edit:=E_SPR.win_edit('?');
E_SPR.win_edit(exec('espr_win_red','jpk_sf',0));
{? E_SPR.edit("exec('ar_espr','jpk_sf')")
|| E_SPR.AKC:='N';
   E_SPR.JPK:='N';
   {? E_SPR.add()
   || E_SPR.memo_put(,'PKD');
      E_SPR.put();
      E_SPR_O.cntx_psh();
      E_SPR_O.index('KOD'); E_SPR_O.prefix(_ref);
      {? E_SPR_O.first()
      || {!
         |? E_SPR_O.cntx_psh();
            E_SPR_O.prefix();
            E_SPR_O.memo_get(,'MEMO');
            E_SPR_O.E_SPR:=E_SPR.ref();
            {? E_SPR_O.add()
            || E_SPR_O.memo_put(,'MEMO');
               E_SPR_O.put()
            ?};
            E_SPR_O.cntx_pop();
            E_SPR_O.next()
         !}
      ?};
      E_SPR_O.cntx_pop();
      E_SPR_F.cntx_psh();
      E_SPR_F.index('E_SPR'); E_SPR_F.prefix(_ref);
      {? E_SPR_F.first()
      || {!
         |? E_SPR_F.cntx_psh();
            E_SPR_F.memo_get(,'SIEDZIBA');
            E_SPR_F.memo_get(,'PRZED');
            E_SPR_F.memo_get(,'POW');
            E_SPR_F.memo_get(,'INNE');
            E_SPR_F.memo_get(,'KAPITAL');
            E_SPR_F.memo_get(,'PODSTAWA');
            E_SPR_F.memo_get(,'D_DO_O');
            E_SPR_F.memo_get(,'L_METODA');
            E_SPR_F.prefix();
            E_SPR_F.E_SPR:=E_SPR.ref();
            E_SPR_F.add();
            E_SPR_F.memo_put(,'SIEDZIBA');
            E_SPR_F.memo_put(,'PRZED');
            E_SPR_F.memo_put(,'POW');
            E_SPR_F.memo_put(,'INNE');
            E_SPR_F.memo_put(,'KAPITAL');
            E_SPR_F.memo_put(,'PODSTAWA');
            E_SPR_F.memo_put(,'D_DO_O');
            E_SPR_F.memo_put(,'L_METODA');
            E_SPR_F.cntx_pop();
            E_SPR_F.next()
         !}
      ?};
      exec('esprf_sys','jpk_sf');
      E_SPR_F.cntx_pop()
   ?}
?};
E_SPR.win_edit(_edit)


\espro_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Edycja pliku z załacznikiem
:: ~OST: INBLEDIT
::----------------------------------------------------------------------------------------------------------------------
{? E_SPR_O.BLOB
|| {? exec('cli_functions','#system')=0
   || FUN.emsg(exec('indevice_nacc_msg','#system'));
      return(0)
   || E_SPR_O.bl_edit('BLOB')
   ?}
|| FUN.info('Nie znaleziono załącznika.')
?}


\espro_save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zapisanie pliku z załacznikiem
::----------------------------------------------------------------------------------------------------------------------
{? E_SPR_O.BLOB
|| _name:=E_SPR_O.bl_info('BLOB', 'NAME');
   _ext:=E_SPR_O.bl_info('BLOB','EXTENSION');
   __dir_controling:=fmk_tmp_dir(0);
   _path:= __dir_controling.get_path()+'/'+_name;
   {? _path<>''
   || E_SPR_O.bl_get('BLOB',_path)
   ?};
   dlg_save(_path,0,_name)
|| FUN.info('Nie znaleziono załącznika.'@)
?};
VAR_DEL.delete('__dir_controling')


\triger
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Triger dla tabeli JPK
::   WE: _a - 1-po add 0-przed del
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| E_SPR.cntx_psh();
   E_SPR.prefix();
   {? E_SPR.seek(JPK.E_SPR) & E_SPR.JPK<>'T'
   || E_SPR.JPK:='T';
      E_SPR.put()
   ?};
   E_SPR.cntx_pop();
   ~~
|? _a=0
|| E_SPR.cntx_psh();
   E_SPR.prefix();
   {? E_SPR.seek(JPK.E_SPR) & E_SPR.JPK='T'
   || JPK.cntx_psh();
      JPK.index('E_SPR'); JPK.prefix(E_SPR.ref());
      _jest:=JPK.first() & JPK.next();
      JPK.cntx_pop();
      {? _jest=0
      || E_SPR.JPK:='N';
         E_SPR.put()
      ?}
   ?};
   E_SPR.cntx_pop()
?}


\ekod_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zasila tabelę z e-Kodami
::   WE: [_a] - tabela z e-Kodami
::----------------------------------------------------------------------------------------------------------------------
_tab:={? var_press('_a')>0 || _a || cur_tab() ?};
_tablp:=tab_tmp(1,
   'LP','INTEGER','Lp',
   'SPR','STRING[50]','Sprawozdanie'
);
_jedn:='';
_i2:=_tab.ndx_tmp('',1,'TYP',,0, 'LP',,0);
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF');
{? ISTDEF.first()
|| ISTDEFS.index('TYPPOLA');
   {!
   |? _jedn:={? ISTDEF.VER*'Skonsolid' || 'Skonsolidowane' |? ISTDEF.VER*'Inna' || 'Inna' |? ISTDEF.VER*'Mala' || 'Mała' |? ISTDEF.VER*'Asi' || 'Asi' || '' ?};
      ISTDEFS.prefix(ISTDEF.ref(),'S');
      {? ISTDEFS.first()
      || {!
         |? _kod:=(ISTDEFS.OPIS*':')-ISTDEFS.OPIS;
            {? ~_tablp.find_key(ISTDEFS.LP)
            || _tablp.LP:=ISTDEFS.LP;
               _tablp.SPR:=_kod;
               _tablp.add()
            ?};
            {? ~_tab.find_key('S',_kod,)
            || _tab.TYP:='S';
               _tab.SPR:=_kod;
               _tab.KOD:=_kod;
               _tab.LP:=ISTDEFS.LP;
               _tab.MEMO:=ISTDEFS.memo_txt(,1,'ANOTACJA');
               _tab.J:=_jedn;
               _tab.add();
               _opis:=ISTDEFS.memo_txt(,1,'ANOTACJA');
               {? _opis<>'' & 0
               || _tab.memo_set(_opis,'MEMO');
                  _tab.memo_put();
                  _tab.put()
               ?}
            |? _tab.J*_jedn=0
            || _tab.J+=', '+_jedn;
               _tab.put()
            ?};
            ISTDEFS.next()
         !}
      ?};
      ISTDEFS.prefix(ISTDEF.ref(),'W',);
      {? ISTDEFS.first()
      || {!
         |? _spr:={? _tablp.find_le(ISTDEFS.LP) || _tablp.SPR || '' ?};
            _kod:=(ISTDEFS.OPIS*':')-ISTDEFS.OPIS;
            {? ~_tab.find_key('W',_spr,_kod,)
            || _tab.TYP:='W';
               _tab.SPR:=_spr;
               _tab.KOD:=_kod;
               _tab.LP:=ISTDEFS.LP;
               _tab.MEMO:=ISTDEFS.memo_txt(,1,'ANOTACJA');
               _tab.J:=_jedn;
               _tab.add();
               _opis:=ISTDEFS.memo_txt(,1,'ANOTACJA');
               {? _opis<>'' & 0
               || _tab.memo_set(_opis,'MEMO');
                  _tab.memo_put();
                  _tab.put()
               ?}
            |? _tab.J*_jedn=0
            || _tab.J+=', '+_jedn;
               _tab.put()
            ?};
            ISTDEFS.next()
         !}
      ?};
      ISTDEFS.prefix(ISTDEF.ref(),'K',);
      {? ISTDEFS.first()
      || {!
         |? _spr:={? _tablp.find_le(ISTDEFS.LP) || _tablp.SPR || '' ?};
            _kod:=(ISTDEFS.OPIS*':')-ISTDEFS.OPIS;
            {? ~_tab.find_key('K',_spr,_kod,)
            || _tab.TYP:='K';
               _tab.SPR:=_spr;
               _tab.KOD:=_kod;
               _tab.LP:=ISTDEFS.LP;
               _tab.MEMO:=ISTDEFS.memo_txt(,1,'ANOTACJA');
               _tab.J:=_jedn;
               _tab.add();
               _opis:=ISTDEFS.memo_txt(,1,'ANOTACJA');
               {? _opis<>'' & 0
               || _tab.memo_set(_opis,'MEMO');
                  _tab.memo_put();
                  _tab.put()
               ?}
            |? _tab.J*_jedn=0
            || _tab.J+=', '+_jedn;
               _tab.put()
            ?};
            ISTDEFS.next()
         !}
      ?};
      _tablp.erase();
      ISTDEF.next()
   !}
?};
_tab.ndx_drop(_i2)


\ekod_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca tabelę z e-Kodami używanymi w e-Sprawozdaniach
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(3,
   'TYP','STRING[1]','Typ',
   'SPR','STRING[50]','Sprawozdanie',
   'KOD','STRING[50]','e-Kod',
   'LP','INTEGER','Lp',
   'MEMO','STRING[255]','Opis',
   'J','STRING[30]','Jednostka'
);
exec('ekod_gen','jpk_sf',_tab);
_win:=_tab.mk_sel('e-Kody'@,'P',,'#ekodys',,,,,'U');
_tab.win_fld(_win,,'KOD');
_tab.win_fld(_win,,'MEMO',,,100);
_tab.win_fld(_win,,'J');
_tab.win_act(_win,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
_tab.win_act(_win,,'Kolejność');
_tab.win_sel(_win);
_tab


\beespr_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją pól sprawozdań w E_SPR
::----------------------------------------------------------------------------------------------------------------------
SIK.E_KOD:=exec('get_ekod','jpk_sf',cur_afld());
GR_SIK.f_clear();
GR_SIK.win_sel('SLO2');
GR_SIK.hdr_sel();
GR_SIK.hdr_sel(' - e-Kod: '+SIK.E_KOD);
exec('f_set_gr_sik2','sprfin');
E_SPR.JPK<>'T'


\get_ekod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca e-Kod sprwozdania
::   WE: _a - pole
::----------------------------------------------------------------------------------------------------------------------
_jedn:={? _a='BILANS'
       || E_SPR.J_B().KOD
       |? _a='RZIS'
       || E_SPR.J_R().KOD
       |? _a='ZMWKAP'
       || ''
       ?};
_kod:={? _a='BILANS'
      || 'S'+(1+_jedn)+'B'
      |? _a='RZIS'
      || 'S'+(1+_jedn)+'R'+E_SPR.RZIS_TYP
      |? _a='ZMWKAP'
      || 'SZ'
      |? _a='RACH'
      || 'SR'+E_SPR.RACH_TYP
      |? _a='I_PODAT'
      || 'SP'
      ?};
ISTDEFS.cntx_psh();
ISTDEFS.index('TYPPOLA'); ISTDEFS.prefix(E_SPR.ISTDEF,_kod);
_ekod:={? ISTDEFS.first() || ISTDEFS.OPIS || '' ?};
ISTDEFS.cntx_pop();
{? (_p:=_ekod*':') || _ekod:=_p-_ekod ?};
_ekod


\be_ekod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją DEFW.E_KOD oraz GR_KOL.E_KOD
::----------------------------------------------------------------------------------------------------------------------
GR_SIK.E_KOD<>''


\blekod_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa pola GR_SIK.E_KOD
::----------------------------------------------------------------------------------------------------------------------
SIK.E_KOD


\f3ekod_g
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla pola GR_SIK.E_KOD
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Ekod');
Ekod:=exec('ekod_tab','jpk_sf');
_sel:=~~;
Ekod.prefix('S');
{? fld<>''
|| Ekod.find_key(fld)
?};
{? Ekod.select(,1,2)
|| _sel:=fld(Ekod.KOD)
?};
VAR_DEL.delete('Ekod');
_sel


\f3ekod_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla pola DEFW.E_KOD
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Ekod');
Ekod:=exec('ekod_tab','jpk_sf');
_sel:=~~;
Ekod.prefix('W',GR_SIK.E_KOD,);
{? fld<>''
|| Ekod.find_key(fld)
?};
{? Ekod.select(,1,2)
|| _sel:=fld(Ekod.KOD)
?};
VAR_DEL.delete('Ekod');
_sel


\f3ekod_k
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla pola GR_KOL.E_KOD
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Ekod');
Ekod:=exec('ekod_tab','jpk_sf');
_sel:=~~;
Ekod.prefix('K',GR_SIK.E_KOD,);
{? fld<>''
|| Ekod.find_key(fld)
?};
{? Ekod.select(,1,2)
|| _sel:=fld(Ekod.KOD)
?};
VAR_DEL.delete('Ekod');
_sel


\beesprtyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redagowaniem typu sprawozdania
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='RZIS_TYP'
|| eSprT:=E_SPR.RZIS_TYP
|| eSprT:=E_SPR.RACH_TYP
?};
E_SPR.JPK<>'T'


\aeesprtyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redagowaniem typu sprawozdania
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='RZIS_TYP'
|| {? eSprT<>E_SPR.RZIS_TYP
   || E_SPR.RZIS:=null
   ?}
|| {? eSprT<>E_SPR.RACH_TYP
   || E_SPR.RACH:=null
   ?}
?};
1


\bl_espr_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa pola E_SPR.ISTDEF
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.ISTDEF


\be_espr_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją E_SPR.ISTDEF
::----------------------------------------------------------------------------------------------------------------------
SKID.ISTDEF:='J';
HELPJPK.RODZAJ:='SF';
-menu_txt()<>'popraw' & -menu_txt()<>'kopiuj'


\pu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tworzenie e-Sprawozdań - pozycje uszczegóławiające
::   WE:  _a  - typ formuły
::              0 - pocztąkowa
::              1 - start/stop pętli
::              2 - na następny element
::       [_b] - 1-start 0-stop
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| _defw:=null;
   _head:=0;
   ISTDEFS.cntx_psh();
   ISTDEFS.prefix();
   {? ISTDEFS.seek(ISTDEFS.TREE,)
   || {? ISTDEFS.TYPFLD='W'
      || _kod:=ISTDEFS.OPIS; {? (_p:=_kod*':') || _kod:=_p-_kod ?};
         DEFW.cntx_psh();
         DEFW.index('E_KOD'); DEFW.prefix(SIK.GR_SIK,_kod,);
         {? DEFW.first()
         || _defw:=DEFW.ref()
         ?};
         DEFW.cntx_pop()
      || _head:=1
      ?}
   ?};
   ISTDEFS.cntx_pop();
   _tab:="{? var_press('eSprDefw')<=0 || eSprDefw:=tab_tmp(1,'DEFW','INTEGER',) ?}";
   {? _head
   || _kod:=ISTDEFS.OPIS; {? (_p:=_kod*':') || _kod:=_p-_kod ?};
      DEFW.cntx_psh();
      DEFW.index('E_KOD'); DEFW.prefix(SIK.GR_SIK,_kod,);
      {? DEFW.first()
      || {!
         |? DEFA.cntx_psh();
            DEFA.index('SKLADNIK'); DEFA.prefix(DEFW.ref());
            {? ~DEFA.first() & DEFW.DEFW_NAD=0
            || _tab();
               {? ~eSprDefw.find_key(DEFW.ref())
               || eSprDefw.DEFW:=DEFW.ref();
                  eSprDefw.add()
               ?}
            ?};
            DEFA.cntx_pop();
            DEFW.next()
         !}
      ?};
      DEFW.cntx_pop();
      _jest:=var_press('eSprDefw')>0 & eSprDefw.first();
      {? ~_jest
      || VAR_DEL.delete('eSprDefw')
      ?};
      _jest
   |? _defw
   || _jest:=0;
      _kod:=ISTDEFS.OPIS; {? (_p:=_kod*':') || _kod:=_p-_kod ?};
      VAR_DEL.delete('eSprDefw');
      DEFA.cntx_psh();
      DEFA.index('WIERSZ'); DEFA.prefix(_defw);
      {? DEFA.first()
      || _tab();
         {!
         |? {? DEFA.ARGUMENT().E_KOD=_kod & DEFA.ARGUMENT().DEFW_NAD=0
            || {? ~eSprDefw.find_key(DEFA.ARGUMENT)
               || eSprDefw.DEFW:=DEFA.ARGUMENT;
                  eSprDefw.add()
               ?}
            ?};
            DEFA.next()
         !}
      ?};
      DEFA.cntx_pop();
      DEFW.cntx_psh();
      DEFW.index('GR_NAD'); DEFW.prefix(SIK.GR_SIK,_defw);
      {? DEFW.first()
      || _tab();
         {!
         |? {? DEFW.E_KOD=_kod
            || {? ~eSprDefw.find_key(DEFW.ref())
               || eSprDefw.DEFW:=DEFW.ref();
                  eSprDefw.add()
               ?}
            ?};
            DEFW.next()
         !}
      ?};
      DEFW.cntx_pop();
      _jest:=var_press('eSprDefw')>0 & eSprDefw.first();
      {? ~_jest
      || VAR_DEL.delete('eSprDefw')
      ?};
      _jest
   ?}
|? _a=1
|| {? _b=1
   || eSprD:=SIK.DEFW;
      eSprDefw.first();
      DEFW.prefix();
      {? DEFW.seek(eSprDefw.DEFW,)
      || SIK.DEFW:=DEFW.ref()
      ?};
      1
   || SIK.DEFW:=eSprD;
      VAR_DEL.delete('eSprDefw','eSprD')
   ?}
|? _a=2
|| {? eSprDefw.next()
   || DEFW.prefix();
      {? DEFW.seek(eSprDefw.DEFW,)
      || SIK.DEFW:=DEFW.ref()
      ?};
      1
   ?}
?}


\pu_pod
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Tworzenie e-Sprawozdań - podpozycja pozycji uszczegóławiającj
::----------------------------------------------------------------------------------------------------------------------
{? var_press('XmlFile')>0
|| _tab:=exec('pu_obj','jpk_sf');
   _defw:=DEFW.ref();
   _tab.clear(_defw);
   DEFA.cntx_psh();
   DEFA.index('LP');
   DEFA.prefix(DEFW.ref());
   {? DEFA.first()
   || {!
      |? {? ~_tab.exists(_defw,DEFA.ARGUMENT)
         || DEFW.cntx_psh();
            DEFA.ARGUMENT();
            exec('pu_pod2','jpk_sf');
            DEFW.cntx_pop()
         ?};
         DEFA.next()
      !}
   ?};
   DEFA.cntx_pop();
   DEFW.cntx_psh();
   DEFW.index('GR_NAD'); DEFW.prefix(DEFW.GRUPA,DEFW.ref());
   {? DEFW.first()
   || {!
      |? {? ~_tab.exists(_defw,DEFW.ref())
         || DEFW.cntx_psh();
            exec('pu_pod2','jpk_sf');
            DEFW.cntx_pop()
         ?};
         DEFW.next()
      !}
   ?};
   DEFW.cntx_pop();
   _tab.clear(_defw)
?};
0


\pu_pod2
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Tworzenie e-Sprawozdań - podpozycja pozycji uszczegóławiającj
::----------------------------------------------------------------------------------------------------------------------
_defw:=SIK.DEFW;
SIK.DEFW:=DEFW.ref();
fwrite(XmlFile,'<dtsf:Podpozycja>');
fwrite(XmlFile,'<dtsf:NazwaPozycji>'+maz_utf8(DEFW.NAZWA)+'</dtsf:NazwaPozycji>');
fwrite(XmlFile,'<dtsf:KwotyPozycji>');
ISTDEFS.cntx_psh();
ISTDEFS.WYM:='T';
ISTDEFS.OPIS:='KwotaA';
fwrite(XmlFile,'<dtsf:KwotaA>'+exec('value','jpk_sf','KwotaA')+'</dtsf:KwotaA>');
ISTDEFS.OPIS:='KwotaB';
fwrite(XmlFile,'<dtsf:KwotaB>'+exec('value','jpk_sf','KwotaA','P')+'</dtsf:KwotaB>');
ISTDEFS.WYM:='N';
_val:=exec('value','jpk_sf','KwotaB1');
ISTDEFS.cntx_pop();
{? _val<>''
|| fwrite(XmlFile,'<dtsf:KwotaB1>'+_val+'</dtsf:KwotaB1>')
?};
fwrite(XmlFile,'</dtsf:KwotyPozycji>');
exec('pu_pod','jpk_sf');
fwrite(XmlFile,'</dtsf:Podpozycja>');
SIK.DEFW:=_defw


\p_pu_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - podpozycje pozycji uszczegóławiającej
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| _b:='<Podpozycja>'+_b+'</Podpozycja>';
   VAR_DEL.delete('eSprRej');
   eSprRej:=obj_new('rej','value');
   eSprRej.rej:=0;
   x_parse(_b,,,"
      _kod:=_a;
      {? (_p:=_kod*':') || _kod:=_p-_kod ?};
      {? _kod='NazwaPozycji' | 5+_kod='Kwota'
      || eSprRej.rej:=1;
         eSprRej.value:=''
      ?};
      1
   ","
      _kod:=_a;
      {? (_p:=_kod*':') || _kod:=_p-_kod ?};
      {? _kod='NazwaPozycji'
      || _tab:=eSpr.tab;
         _tab.blank(1);
         _tab.LP:=(eSpr.lp+=1);
         _tab.SPR:=eSpr.spr;
         _tab.WIERSZ:=eSprRej.value;
         _tab.add()
      |? 5+_kod='Kwota'
      || _tab:=eSpr.tab;
         {? _kod='KwotaA' || _tab.KA:=#eSprRej.value; _tab.JA:=1; _tab.put()
         |? _kod='KwotaB' || _tab.KB:=#eSprRej.value; _tab.JB:=1; _tab.put()
         |? _kod='KwotaB1' || _tab.KB1:=#eSprRej.value; _tab.JB1:=1;_tab.put()
         ?}
      ?};
      eSprRej.rej:=0;
      eSprRej.value:=''
   ","
      {? eSprRej.rej
      || eSprRej.value+=_a
      ?};
      1
   ");
   VAR_DEL.delete('eSprRej')
?};
1


\can_modyfy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy można modyfikować schemat JPK
::   WE: _a - czy komunikować?
::----------------------------------------------------------------------------------------------------------------------
E_SPR.cntx_psh();
E_SPR.index('ISTDEF'); E_SPR.prefix(ISTDEF.ref());
_jest:=E_SPR.first();
E_SPR.cntx_pop();
{? _jest & var_press('_a')>0 & _a
|| FUN.info('Istnieje definicja e-Sprawozdań związana ze schematem.'@)
?};
~_jest


\espr_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przygotowanie okna e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('JpkSF');
JpkSF:=exec('obj','jpk_sf');
E_SPR.win_sel(JpkSF.win);
SLU.index('NAZ'); SLU.prefix('~TYPY JEDNOSTEK',); SLU.first()


\espr_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wyświetlenie definicji e-Sprawozdń z menu aplikacji
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('EsprStan');
exec('espr_win','jpk_sf');
FIRMA.cntx_psh();
FIRMA.win_sel('WER');
FIRMA.win_dict('WER');
ISTDEF.win_edit('R_JPK');
ISTDEF.win_sel('W_JPK');
ISTDEF.actions('W_JPK','DuPTHOEIA:DIA');
E_SPR.index('FIRMA'); E_SPR.prefix(REF.FIRMA);
exec('espr_red','jpk_sf');
E_SPR.select();
ISTDEF.actions('W_JPK');
FIRMA.cntx_pop();
VAR_DEL.delete('EsprStan')


\ae_espr_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji E_SPR.ISTDEF
::----------------------------------------------------------------------------------------------------------------------
_typ:=E_SPR.ISTDEF().VER;
_slo:=null;
_kod:={? _typ*'Inna'
      || 'INNA'
      |? _typ*'Mala'
      || 'MAŁA'
      |? _typ*'Mikro'
      || 'MIKRO'
      |? _typ*'Asi'
      || 'ASI'
      || ''
      ?};
_inna:=(_kod='INNA' | _kod='ASI');
{? _kod<>''
|| SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(SLU.ref(),_kod);
   {? SLO.first()
   || _slo:=SLO.ref()
   ?};
   SLO.cntx_pop()
?};
{? E_SPR.J_N=null | _inna & E_SPR.J_N<>_slo || E_SPR.J_N:=_slo ?};
{? E_SPR.J_B=null | _inna & E_SPR.J_B<>_slo || E_SPR.J_B:=_slo ?};
{? E_SPR.J_R=null | _inna & E_SPR.J_R<>_slo || E_SPR.J_R:=_slo ?};
{? E_SPR.J_D=null | _inna & E_SPR.J_D<>_slo || E_SPR.J_D:=_slo ?};
_red:=E_SPR.win_edit('?');
_enable:=(E_SPR.J_N().KOD='INNA' | E_SPR.J_N().KOD='ASI' );
E_SPR.efld_opt(_red,'enable='+$(_enable),E_SPR,'PKD');
E_SPR.efld_opt(_red,'mark='+$(_enable),E_SPR,'PKD');
_mark:=(E_SPR.J_D().KOD='INNA' | E_SPR.J_D().KOD='ASI' );
E_SPR.efld_opt(_red,'mark='+$_mark,E_SPR,'I_PODAT');
1


\be_espr_j
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją pól związanych z jednostką
::----------------------------------------------------------------------------------------------------------------------
_typ:=cur_afld()+1;
{? E_SPR.ISTDEF & E_SPR.JPK<>'T'
|| E_SPR.ISTDEF().VER*'Mala' & (_typ<>'N' | (-menu_txt()<>'popraw' & -menu_txt()<>'kopiuj'))
?}


\ae_espr_j
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji pól związanych z jednostką
::----------------------------------------------------------------------------------------------------------------------
_typ:=cur_afld()+1;
_kod:={? _typ='B' || 'BILANS'
      |? _typ='R' || 'RZIS'
      || ''
      ?};
{? _kod<>''
|| _ekod:=exec('get_ekod','jpk_sf',_kod);
   {? _typ='B' & E_SPR.BILANS().E_KOD<>_ekod || E_SPR.BILANS:=null
   |? _typ='R' & E_SPR.RZIS().E_KOD<>_ekod || E_SPR.RZIS:=null
   ?}
?};
_red:=E_SPR.win_edit('?');
{? _typ='D'
|| _mark:=(E_SPR.J_D().KOD='INNA' | E_SPR.J_D().KOD='ASI');
   E_SPR.efld_opt(_red,'mark='+$_mark,E_SPR,'I_PODAT')
|? _typ='N'
|| _mark:=(E_SPR.J_N().KOD='INNA' | E_SPR.J_N().KOD='ASI');
   E_SPR.efld_opt(_red,'mark='+$_mark,E_SPR,'PKD');
   E_SPR.efld_opt(_red,'enable='+$_mark,E_SPR,'PKD');
   {? ~_mark
   || E_SPR.memo_set('','PKD')
   ?}
?};
1


\be_espr_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją pól tabeli E_SPR
::----------------------------------------------------------------------------------------------------------------------
E_SPR.JPK<>'T'


\get_pem
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca nazwę certyfikatu z kluczem publicznym dla podpisu RSA
::----------------------------------------------------------------------------------------------------------------------
{? XINFO.SF_GATE*'tst'>0
|| 'jpk_sf_test.pem'
|| 'jpk_sf.pem'
?}


\f3_sf_gate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Klawisz F3 dla pola XINFO.SF_GATE
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(2,
   'ADR','STRING[255]','Adres',
   'TEST','INTEGER','Testowa'
);
_add:="
   _a.blank(1);
   _a.ADR:=_b;
   _a.TEST:=_c;
   _a.add()
";
_add(_tab,'e-sprawozdania-tst.mf.gov.pl',1);
_add(_tab,'e-sprawozdania.mf.gov.pl',0);
_o:=_tab.mk_sel('Bramki do wysyłania e-Sprawozdań'@);
_tab.win_fld(_o,,'ADR',,,100,,,'Adres bramki e-Sprawozdań'@);
_tab.win_fld(_o,,'TEST',,,,,,'Bramka testowa?'@,,,2,,"1","0");
_tab.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
_tab.win_sel(_o);
{? _tab.select()
|| _tab.ADR
|| ~~
?}


\jpk_sig_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca okna wertowania dla podpisów e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
_o:=JPK_SIG.mk_sel('Podpisy'@,'P',,'#jpk_sig_win',,,,,'U');
JPK_SIG.win_fld(_o,,'DATA');
JPK_SIG.win_fld(_o,,'TIME');
JPK_SIG.win_fld(_o,,'USER');
JPK_SIG.win_act(_o,,'Formuła','Dołącz'@@,,,"exec('jpk_sig_add','jpk_sf')",,1);
task_attach('FKS_JPK_SSPR');
JPK_SIG.win_act(_o,1,'Formuła','Dołącz'@@,,,"exec('jpk_sig_add','jpk_sf')",,1);
task_attach('FKS_JPK_SSPR');
JPK_SIG.win_act(_o,,'Formuła','Usuń wszystkie'@@,,,,"exec('jpk_sig_del','jpk_sf')");
task_attach('FKS_JPK_SSPR');
JPK_SIG.win_act(_o,,'Formuła','Zakończ'@@,,,,"
   exec('jpk_sig_end','jpk_sf',1);
   {? JPK.STAT='P' || sel_exit() ?}
");
task_attach('FKS_JPK_SSPR');
JPK_SIG.win_act(_o,,'Formuła','Wycofaj'@@,,,,"exec('jpk_sig_end','jpk_sf',0)");
task_attach('FKS_JPK_SSPR');
JPK_SIG.win_act(_o,,'Rekord',,,,"exec('br_jpk_sig','jpk_sf')");
JPK_SIG.win_act(_o,,'Kolejność');
_btn:=JPK_SIG.win_btn(_o,'text=%1,btn_label_align=center,panel=right,align=begin'['Dołącz'@],'menu:D');
JPK_SIG.btn_opt(_btn,'tooltip=%1'['Dołącz podpis'@]);
_btn:=JPK_SIG.win_btn(_o,'text=%1,btn_label_align=center,panel=bottom,align=end'['Zakończ'@],'menu:Z');
JPK_SIG.btn_opt(_btn,'tooltip=%1'[exec('help_red_zakoncz','#window','J_SIG')]);
_o


\espr_sig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Akcja podpisy dla e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
{? JPK.TYP<>'SF'
|| FUN.info('Akcja dostępna tylko dla e-Sprawozdań.'); return
|? JPK.AKC<>'T'
|| FUN.info('e-Sprawozdanie nie jest zaakceptowane.'); return
?};
JPK_SIG.index('DATE'); JPK_SIG.prefix(JPK.ref());
_wer:=exec('jpk_sig_win','jpk_sf');
JPK_SIG.win_sel(_wer);
JPK_SIG.actions(_wer);
JPK_SIG.select()


\br_jpk_sig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Rekord przed okna podpisów e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
JPK_SIG.cntx_psh();
JPK_SIG.index('USER'); JPK_SIG.prefix(JPK.ref(),OPERATOR.USER().KOD,);
_is:=JPK_SIG.first();
JPK_SIG.cntx_pop();
_actions:='';
{? _is
|| _actions+='D:D'
?};
JPK.cntx_psh();
{? JPK_SIG.JPK().STAT='P'
|| _actions:='ZU'+_actions
|| _actions:='W'+_actions
?};
JPK.cntx_pop();
JPK_SIG.actions_grayed(JPK_SIG.win_sel('?'),_actions);
~~


\jpk_sig_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Dodanie podpisu do e-Sprawozdań
:: ~OST: INBLGET,INBLPUT,INEXEDIR,INFCOPY,INFEXISTS,INFMKDIR,INFOPEN,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? exec('cli_functions','#system')=0
|| FUN.emsg(exec('indevice_nacc_msg','#system'));
   return(0)
?};
{? JPK.AKC<>'T'
|| FUN.info('Sprawozdanie nie jest zaakceptowane.'@)
|? (_pyt:=FUN.choice('Czy chcesz podpisać e-Sprawozdanie?'@,1,'Tak, podpis kwalifikowany','Tak, podpis zaufany'))
|| {? JPK.JPK_SIG=null
   || JPK.JPK_SIG:=JPK.PLIK;
      JPK.put()
   ?};
   _fn:=tmp_dir()+'/espr.xml';
   {? JPK.bl_get('JPK_SIG','@'+_fn,0)
   || _ok:=0;
      {? _pyt=1
      || fcopy('settings.xml','@'+exe_dir()+'/xades/settings_cryptoproviders.xml',1,0,1);
         _ok:=sign('EPO-XAdES-BES','@'+_fn)
      || _dir:=pth_dir('jpk.jar');
         _lib:=fmkdir('@!Tmp', 'jpk');
         _lib:=fmkdir(_lib, 'lib');
         _ok:=fcopy('jpk.jar','@!Tmp/jpk/jpk.jar',1,0,1);
         _ok*=fcopy('settings_v.xml','@!Tmp/jpk/settings.xml',1,0,1);
         _fc:="{? fexists('@'+_b+'/lib/'+_c,0) || 1 || fcopy(_a+'/lib/'+_c,_b+'/'+_c,0,0,1) ?}";
         _ok*=_fc(_dir,_lib,'axis.jar');
         _ok*=_fc(_dir,_lib,'commons-discovery-0.2.jar');
         _ok*=_fc(_dir,_lib,'commons-logging-1.0.4.jar');
         _ok*=_fc(_dir,_lib,'cryptodialogs.jar');
         _ok*=_fc(_dir,_lib,'cryptoengine.jar');
         _ok*=_fc(_dir,_lib,'cryptohelp.jar');
         _ok*=_fc(_dir,_lib,'cryptolibrary.jar');
         _ok*=_fc(_dir,_lib,'DJNativeSwing.jar');
         _ok*=_fc(_dir,_lib,'DJNativeSwing-SWT.jar');
         _ok*=_fc(_dir,_lib,'iaikPkcs11Wrapper.jar');
         _ok*=_fc(_dir,_lib,'iText-2.1.7.jar');
         _ok*=_fc(_dir,_lib,'jaxrpc.jar');
         _ok*=_fc(_dir,_lib,'jna_WindowUtils.jar');
         _ok*=_fc(_dir,_lib,'jna-4.0.0.jar');
         _ok*=_fc(_dir,_lib,'jpedal_lgpl.jar');
         _ok*=_fc(_dir,_lib,'kirutils.jar');
         _ok*=_fc(_dir,_lib,'MozillaInterfaces-1.8.1.3.jar');
         _ok*=_fc(_dir,_lib,'swing-layout-1.0_lightversion.jar');
         _ok*=_fc(_dir,_lib,'swt-4.5-win32-win32-x86.jar');
         _ok*=_fc(_dir,_lib,'swt-4.5-win32-win32-x86_64.jar');
         _ok*=_fc(_dir,_lib,'wsdl4j-1.5.1.jar');
         {? _ok
         || _f:=fopen('@!Tmp/jpk/todo.txt','uw',0);
            {? _f
            || fwrite(_f,'epuap='+exec('epuap','jpk'));
               fwrite(_f,'verify=1');
               fwrite(_f,'FILE@'+_fn+'@'+JPK.OPIS);
               fclose(_f);
               exec('MBJAR','#object');
               _ok:=~MBJAR.JavaExec(tmp_dir()+'/jpk',1,'-jar','jpk.jar','"'+tmp_dir()+'/jpk/todo.txt"')
            ?}
         || FUN.info('Błąd konfiguracji środowiska.'@)
         ?}
      ?};
      {? _ok & JPK.bl_put('JPK_SIG','@'+_fn,0)
      || JPK.put();
         _ref:=null;
         JPK_SIG.cntx_psh();
         JPK_SIG.prefix();
         JPK_SIG.blank(1);
         JPK_SIG.JPK:=JPK.ref();
         JPK_SIG.USER:=OPERATOR.USER().KOD;
         JPK_SIG.DATA:=date();
         JPK_SIG.TIME:=time();
         JPK_SIG.add();
         JPK_SIG.cntx_pop();
         {? _ref
         || JPK_SIG.seek(_ref)
         ?}
      ?}
   ?}
?}


\jpk_sig_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Usunięcie podpisów e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
_pyt:={? var_press('_a')>0 || _a || 1 ?};
{? ~_pyt | FUN.ask('Czy usunąć wszystkie podpisy?'@)
|| JPK_SIG.cntx_psh();
   JPK_SIG.index('USER');
   JPK_SIG.prefix(JPK.ref());
   {? JPK_SIG.first() || {! |? JPK_SIG.del !} ?};
   JPK_SIG.cntx_pop();
   JPK.JPK_SIG:=JPK.PLIK;
   JPK.put();
   {? cur_tab=JPK_SIG
   || _win:=JPK_SIG.win_sel('?');
      JPK_SIG.actions(_win,,,1);
      JPK_SIG.actions_grayed(_win)
   ?}
?}


\is_sig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy e-Sprawozdanie jest podpisane
::----------------------------------------------------------------------------------------------------------------------
JPK_SIG.cntx_psh();
JPK_SIG.index('USER'); JPK_SIG.prefix(JPK.ref());
_jest:=JPK_SIG.first();
JPK_SIG.cntx_pop();
_jest


\czy_krs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy e-Sprawozdanie jest kierowane do eKRS-u
::----------------------------------------------------------------------------------------------------------------------
PAR_SKID.get(62)='S'


\spr_in_e
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy sprawozdanie jest używane w e-Sprawozdaniach?
::----------------------------------------------------------------------------------------------------------------------
_tak:=0;
_spr:=GR_SIK.ref();
E_SPR.cntx_psh();
E_SPR.index('NAZ'); E_SPR.prefix(REF.FIRMA);
{? E_SPR.first()
|| {!
   |? _tak:=E_SPR.BILANS=_spr | E_SPR.RZIS=_spr | E_SPR.ZMWKAP=_spr | E_SPR.RACH=_spr | E_SPR.I_PODAT=_spr;
      _tak=0 & E_SPR.next()
   !}
?};
E_SPR.cntx_pop();
_tak


\ae_m_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji parametru 62 dotyczącego miejsca składania e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
PARAMETR.TRESC:=~-PARAMETR.TRESC;
{? 'SF'*PARAMETR.TRESC=0
|| FUN.info('Dopuszczalne wartości - \"S\" lub \"F\".'@); 0
|? PARAMETR.TRESC='F'
|| FUN.info('Wysyłanie e-Sprawozdań do KAS nie jest jeszcze dostępne.'@);
   PARAMETR.TRESC:='S';
   0
|| 1
?}


\be_espr_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją pola HELPJPK.E_SPR_A
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.RODZAJ='SF'


\ae_sch_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji HELPJPK.ISTDEF
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.RODZAJ='SF' & HELPJPK.ISTDEF & HELPJPK.E_SPR & HELPJPK.ISTDEF<>HELPJPK.E_SPR().ISTDEF
|| HELPJPK.E_SPR:=null
?};
1

\espr_win_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Przygotowuje okno redagowania dla e-Sprawozdań
::   WE: [_a] - wszystkie przyciski? [1]-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_all:={? var_pres('_a')=type_of(1) || _a || 1 ?};
_okn:=E_SPR.mk_edit('e-Sprawozdanie - definicja'@);
E_SPR.win_ewin(_okn,,'RED');
{? _all
|| E_SPR.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=begin'['&Pozycje'@],"
      _spr:=exec('ar_espr','jpk_sf');
      {? _spr<>''
      || 'edit:'+_spr
      || exec('espr_add_put','jpk_sf');
         _win:=exec('e_spr_o_win','jpk_sf');
         E_SPR_O.win_sel(_win);
         E_SPR_O.select();
         ''
      ?}
   ");
   _zakoncz:=E_SPR.win_ebtn(_okn,'text=%1,btn_label_align=center,panel=bottom,align=end'[exec('text_red_zakoncz','#window','S')],"
      _spr:=exec('ar_espr','jpk_sf');
      {? _spr<>''
      || 'edit:'+_spr
      || exec('espr_add_put','jpk_sf');
         exec('akc','jpk_sf',1);
         {? E_SPR.AKC='T'
         || 'key:F2'
         ||''
         ?}
      ?}
   ");
   E_SPR.btn_eopt(_okn,_zakoncz,'tooltip='+exec('help_red_zakoncz','#window','S'))
?};
_zapisz:=E_SPR.win_ebtn(_okn,'text=%1, btn_label_align=center, panel=bottom, align=end'['Zapisz'@],"'key:F2'");
_anuluj:=E_SPR.win_ebtn(_okn,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
E_SPR.btn_eopt(_okn,_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
E_SPR.btn_eopt(_okn,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
_okn


\espr_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Przygotowuje okno redagowania dla e-Sprawozdań
::----------------------------------------------------------------------------------------------------------------------
_okn:=exec('espr_win_red','jpk_sf');
E_SPR.win_edit(_okn)


\espr_add_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Dodanie poprawienie e-Sprawozdania
::  OLD: \espr_add_put/!fks_jpk_dspr.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;
{? var_press('EsprStan')<=0 || EsprStan:=1 ?};
{? EsprStan=0
|| E_SPR.prefix();
   {? E_SPR.add()
   || EsprStan:=1;
      E_SPR.memo_put(,'PKD');
      E_SPR.put();
      _ref:=E_SPR.ref();
      _kod:={? (E_SPR.J_N().KOD='INNA' | E_SPR.J_N().KOD='ASI') || 'OI' || 'OM' ?};
      ISTDEFS.index('TYPPOLA'); ISTDEFS.prefix(E_SPR.ISTDEF,_kod);
      {? ISTDEFS.first()
      || {!
         |? _kod:=ISTDEFS.OPIS;
            {? (_p:=_kod*':') || _kod:=_p-_kod ?};
            _typ:={? ISTDEFS.TYPFLD+1='T' || 'S' || 's' ?};
            exec('add_opis','jpk_sf',_kod,ISTDEFS.memo_txt(,1,'ANOTACJA'),_typ);
            ISTDEFS.next()
         !}
      ?};
      exec('esprf_sys','jpk_sf')
   ?}
|| E_SPR.memo_put(,'PKD');
   E_SPR.put();
   _ref:=E_SPR.ref()
?};
{? _ref
|| exec('desc_update','#b__box',E_SPR.uidref())
?};
_ref


\e_spr_o_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Zwraca okno tabeli E_SPR_O
::----------------------------------------------------------------------------------------------------------------------
{? var_press('JpkSF')<=0
|| JpkSF:=exec('obj','jpk_sf')
?};
_grp:=E_SPR_O.grp_make('Pozycje e-Sprawozdania',,'#espropoz');
E_SPR_O.grp_sel(_grp,E_SPR_O,'WER_O','Wprowadzenie'@,,,,,"JpkSF.prefix('M')",,,,'maximized');
E_SPR_O.grp_sel(_grp,E_SPR_O,'WER_P','Dodatkowe informacje'@,,,,,"JpkSF.prefix('B')",,,,'maximized');
{? -E_SPR.ISTDEF().VER*'asi'
|| E_SPR_O.grp_sel(_grp,E_SPR_O,'WER_P','Zestawienie lokat (Załącznik 1)'@,,,,,"JpkSF.prefix('L')",,,,'maximized');
   E_SPR_O.grp_sel(_grp,E_SPR_O,'WER_P','Dodatkowe informacje (Załącznik 2)'@,,,,,"JpkSF.prefix('Z')",,,,'maximized')
?};
{? E_SPR.ISTDEF().VER*'Skonsolid'
|| E_SPR_O.grp_sel(_grp,E_SPR_F,'WER','Firmy'@,,,,,"JpkSF.prefix('F')",,,,'maximized')
?};
_grp


\akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Akceptacja e-Sprawozdania
::   WE: _a - rodzaj operacji
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? E_SPR.AKC='T'
   || FUN.info('Definicja e-Sprawozdanie jest już zakończona.'@)
   |? exec('espr_ok','jpk_sf') & FUN.ask('Zakończyć definiowanie e-Sprawozdania?'@)
   || E_SPR.AKC:='T';
      E_SPR.put();
      _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='FKS_JPK_DSPR';
      _params.AKCJA:='Zakończ';
      _params.UIDREF:=E_SPR.uidref();
      _params.PROC_START:='N';
      exec('mp_run','#b__box',_params)
   ?}
|| {? E_SPR.AKC<>'T'
   || FUN.info('Definicja e-Sprawozdanie nie jest zakończona.'@)
   |? FUN.ask('Wycofać definicję e-Sprawozdania?'@)
   || E_SPR.AKC:='N';
      E_SPR.put()
   ?}
?}


\espr_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Czy definicja e-Sprawozdania jest kompletna
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_skonsolid:=E_SPR.ISTDEF().VER*'Skonsolid';
_komm:=exec('obj_kom','jpk_sf','Niewypełnione elementy w nagłówku e-Sprawozdania');
_komm.s_start('Wprowadzenie');
E_SPR_O.cntx_psh();
E_SPR_O.index('KOD');
E_SPR_O.prefix(E_SPR.ref(),'M',);
{? E_SPR_O.first()
|| {!
   |? {? E_SPR_O.TYP='S'
      || _txt:=E_SPR_O.memo_txt(,1,'MEMO');
         {? _txt=''
         || _komm.add(E_SPR_O.NAZ)
         |? _skonsolid & 'P_1B,P_12,P_13'*E_SPR_O.KOD & +_txt>3500
         || _komm.add('Opis powyżej 3500 znaków w: '+E_SPR_O.KOD+' - '+E_SPR_O.NAZ)
         ?}
      ?};
      E_SPR_O.next()
   !}
?};
_komm.s_end();

_komm.s_start('Dodatkowe informacje');
E_SPR_O.prefix(E_SPR.ref(),'B',);
{? ~E_SPR_O.first()
|| _komm.add('Dodatkowe informacje')
|? _skonsolid
|| E_SPR_O.cntx_psh();
   E_SPR_O.index('KOD2'); E_SPR_O.prefix(HELPJPK.E_SPR,'B',);
   E_SPR_F.cntx_psh();
   E_SPR_F.index('DODAT'); E_SPR_F.prefix(HELPJPK.E_SPR,'T');
   {? E_SPR_F.first()
   || {!
      |? {? ~E_SPR_O.find_key(E_SPR_F.FIRMA().SYMBOL,)
         || _komm.add('Brak dodatkowych informacji dla firmy: '+FIRMA.SYMBOL+' - '+FIRMA.OPIS)
         ?};
         E_SPR_F.next()
      !}
   ?};
   E_SPR_F.cntx_pop();
   E_SPR_O.cntx_pop()
?};
_komm.s_end();
{? -E_SPR.ISTDEF().VER * 'asi'
|| _komm.s_start('Zestawienie lokat');
    E_SPR_O.prefix(E_SPR.ref(),'L',);
    {? E_SPR_O.size()=0 || _komm.add('Brak załącznika 1') ?};
    {? E_SPR_O.size()>1 || _komm.add('Dozwolony tylko jeden załącznik') ?};
    _komm.s_end();
    _komm.s_start('Dodatkowe informacje (załącznik 2)');
    E_SPR_O.prefix(E_SPR.ref(),'Z',);
    {? E_SPR_O.size()=0 || _komm.add('Brak załącznika 2') ?};
    _komm.s_end()
?};
E_SPR_O.cntx_pop();
{? _skonsolid
|| _ok:=1;
   _komm.s_start('Firmy');
   E_SPR_F.cntx_psh();
   E_SPR_F.index('E_SPR'); E_SPR_F.prefix(E_SPR.ref(),'N',);
   {? ~E_SPR_F.first()
   || _ok:=0;
      _komm.add('Informacje o firmach')
   ?};
   {? _ok
   || {? ~E_SPR_F.find_key('Z')
      || _ok:=0;
         _komm.add('Informacji o firmie zależnej, współzależnej i stowarzyszonej')
      ?}
   ?};
   {? _ok
   || E_SPR_F.cntx_psh();
      E_SPR_F.index('FIRMA'); E_SPR_F.prefix(HELPJPK.E_SPR);
      E_SPR_O.cntx_psh();
      E_SPR_O.index('KOD'); E_SPR_O.prefix(HELPJPK.E_SPR,'B');
      {? E_SPR_O.first()
      || {!
         |? {? E_SPR_O.FIRMA & ~E_SPR_F.find_key(E_SPR_O.FIRMA)
            || _komm.add('Brak informacji o firmie: '+E_SPR_O.FIRMA().SYMBOL+' - '+FIRMA.OPIS)
            ?};
            E_SPR_O.next()
         !}
      ?};
      E_SPR_O.cntx_pop();
      E_SPR_F.cntx_pop()
   ?};
   E_SPR_F.cntx_pop();
   _komm.s_end()
?};
{? _komm.show()
|| _ok:=0
?};
_ok


\jpk_sig_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Akcja kończenie podpisywania
::   WE: _a - typ operacji: 1-kończenie 0-wycofanie
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| JPK_SIG.cntx_psh();
   JPK_SIG.index('DATE'); JPK_SIG.prefix(JPK.ref());
   _jest:=JPK_SIG.first();
   JPK_SIG.cntx_pop();
   {? JPK.STAT='P'
   || FUN.info('Składanie podpisów jest już zakończone.'@)
   |? ~_jest
   || FUN.info('Nie złożono żadnego podpisu.'@)
   |? FUN.ask('Czy zakończyć składanie podpisów?')
   || JPK.STAT:='P';
      JPK.put();
      _params:=exec('mp_run_a','#b__box');
      _params.ACT_UID:='FKS_JPK_SSPR';
      _params.AKCJA:='Zakończ';
      _params.UIDREF:=JPK.uidref();
      _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
      exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'JPK',JPK.ref());
      _params.PROC_START:='N';
      exec('mp_run','#b__box',_params)
   ?}
|| {? JPK.STAT<>'P'
   || FUN.info('Składanie podpisów nie jest jeszcze zakończone.'@)
   |? FUN.ask('Czy wycofać zakończenie składania podpisów?')
   || JPK.STAT:='N';
      JPK.put()
   ?}
?}


\espr_podat_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Uszczegółowienia informacji podatkowych e-Sprawozdania
::  OLD: \espr_podat_sel/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_nr:=#(5-DEFW.E_KOD);
{? _nr<2 | _nr>9
|| FUN.info('Funkcja nie jest dostępna dla bieżącego wiersza.'@);
   return()
?};
UD_POM.GLOWNY:='T';
SIK.FIRMA:=REF.S_FIRMA;
SIK.ROK1:=SSTALE.AR;
SIK.E_KOD:=DEFW.E_KOD;
SIK.DEFW_E:=DEFW.ref();
exec('esprp_prefix','jpk_sf');
E_SPR_P.select()


\esprp_prefix
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia dziedzinę dla informacji podatkowych
::  OLD: \esprp_prefix/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
E_SPR_P.index('FIRMA');
E_SPR_P.prefix(SIK.FIRMA,SIK.ROK1,SIK.DEFW_E,);
E_SPR_P.win_sel('WER');
E_SPR_P.hdr_sel();
E_SPR_P.hdr_sel(' - firma: '+SIK.FIRMA().SYMBOL+', rok: '+SIK.ROK1().NAZ+', '+DEFW.E_KOD+' - '+DEFW.NAZWA);
~~


\bl_esprp_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa pola E_SPR_P.FIRMA
::  OLD: \bl_esprp_firma/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
SIK.FIRMA


\bl_esprp_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa pola E_SPR_P.ROK_F
::  OLD: \bl_esprp_rok/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
SIK.ROK1


\bl_esprp_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość początkowa pola E_SPR_P.E_KOD
::  OLD: \bl_esprp_kod/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
DEFW.E_KOD


\bl_esprp_defw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartość poczatkowa pola E_SPR_P.DEFW
::  OLD: \bl_esprp_defw/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
DEFW.ref()


\ar_esprp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Rekord po okna WER tabeli E_SPR_OP
::  OLD: \ar_esprp/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_r:=__CHK.record2(E_SPR_P,
   'NAZ','Pozycja użytkownika - nazwa'
);
{? _r='' & E_SPR_P.B_KW_A=0 & E_SPR_P.P_KW_A=0
|| FUN.info('Nie wypełniono wartości łącznej w bieżącym ani poprzednim roku.'@);
   _r:='B_KW_A'
?};
{? _r='' & E_SPR_P.INNE<>'T'
|| _r:=__CHK.record(E_SPR_P,,'ART')
?};
{? _r='' & E_SPR_P.INNE='T' & -menu_txt()<>'popraw' & UD_POM.GLOWNY<>'T'
|| FUN.info('Pozycja \'Pozostała\' dostępna jest tylko na najwyższym poziomie struktury.'@);
   _r:='INNE'
?};
{? _r='' & E_SPR_P.INNE='T'
|| _ref:=E_SPR_P.ref();
   E_SPR_P.cntx_psh();
   E_SPR_P.index('INNA'); E_SPR_P.prefix(E_SPR_P.FIRMA,E_SPR_P.ROK_F,E_SPR_P.DEFW,'T',);
   {? E_SPR_P.first() & (-menu_txt()<>'popraw' | _ref<>E_SPR_P.ref())
   || FUN.info('Istnieje już pozycja \'Pozostała\'.'@);
      _r:='INNE'
   ?};
   E_SPR_P.cntx_pop()
?};
_r


\ba_esprp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed dołącz okna WER tabeli E_SPR_P
::  OLD: \ba_esprp/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_nad:={? E_SPR_P.get() || E_SPR_P.ref() ?};
E_SPR_P.memo_set(,'NAZ');
E_SPR_P.blank();
E_SPR_P.win_edit('RED');
{? E_SPR_P.edit("exec('ar_esprp','jpk_sf')")
|| E_SPR_P.TREE:={? UD_POM.GLOWNY<>'T' || _nad ?};
   {? E_SPR_P.add()
   || E_SPR_P.memo_put(,'NAZ');
      E_SPR_P.put()
   ?}
?}


\esprp_trig
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wyzwalacz po memo_put
::  OLD: \esprp_trig/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
E_SPR_P.NAZWA:=E_SPR_P.memo_txt(,1,'NAZ');
E_SPR_P.put();
~~


\bd_esprp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed akcją usuń okna WER tabeli E_SPR_P
::  OLD: \bd_esprp/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
E_SPR_P.cntx_psh();
E_SPR_P.index('FIRMA');
E_SPR_P.prefix(SIK.FIRMA,SIK.ROK1,SIK.DEFW_E,E_SPR_P.ref());
_pod:=E_SPR_P.first();
E_SPR_P.cntx_pop();
{? _pod
|| _pyt:=FUN.ask('Czy usunąć wiersz z podrzędnymi?'@)
|| _pyt:=FUN.ask('Czy usunąć wiersz?'@)
?};
{? _pyt
|| E_SPR_P.cntx_psh();
   E_SPR_P.index('FIRMA'); E_SPR_P.prefix(E_SPR_P.FIRMA,E_SPR_P.ROK_F,E_SPR_P.DEFW,E_SPR_P.TREE);
   _ref:={? E_SPR_P.next() | E_SPR_P.prev() | E_SPR_P.TREE<>0 & (E_SPR_P.prefix(); E_SPR_P.seek(E_SPR_P.TREE,))
         || E_SPR_P.ref()
         || null
         ?};
   E_SPR_P.cntx_pop();
   exec('esprp_del','jpk_sf');
   {? _ref
   || E_SPR_P.seek(_ref)
   ?}
?}


\esprp_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Usunięcie rekordu tabeli E_SPR_P
::  OLD: \esprp_del/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
E_SPR_P.cntx_psh();
E_SPR_P.index('FIRMA'); E_SPR_P.prefix(E_SPR_P.FIRMA,E_SPR_P.ROK_F,E_SPR_P.DEFW,E_SPR_P.ref());
{? E_SPR_P.first()
|| {!
   |? exec('esprp_del','jpk_sf')
   !}
?};
E_SPR_P.cntx_pop();
E_SPR_P.del()


\ae_esprp_inne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redakcji E_SPR_P.INNE
::  OLD: \ae_esprp_inne/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? E_SPR_P.INNE='T'
|| E_SPR_P.memo_set('Pozostała','NAZ')
|? E_SPR_P.memo_txt(,,'NAZ')='Pozostała'
|| E_SPR_P.memo_set('','NAZ')
?};
1


\be_esprp_naz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją E_SPR_P.NAZ
::  OLD: \be_esprp_naz/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
E_SPR_P.INNE<>'T'


\esprp_first
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Pozycje użytkownika w informacji podatkowej - początek/koniec pętli
::   WE: _a - 1-początek pętli 0-koniec pętli
::       _b - czy pozycja pozostała: T/N
::  OLD: \esprp_first/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? SIK.DEFW
|| {? _a=1
   || E_SPR_P.cntx_psh();
      E_SPR_P.index('INNA'); E_SPR_P.prefix(REF.FIRMA,JPK.OKRO_F().ROK,SIK.DEFW,_b,0);
      E_SPR_P.first()
   || E_SPR_P.cntx_pop()
   ?}
?}


\esprp_next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Pozycje użytkownika w informacji podatkowej - następny element
::  OLD: \esprp_next/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
E_SPR_P.next()


\esprp_nazwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Pozycje użytkownika w informacji podatkowej - nazwa pozycji
::  OLD: \esprp_nazwa/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
3500+E_SPR_P.memo_txt(,1,'NAZ')


\esprp_value
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Pozycje użytkownika w informacji podatkowej - wartość
::   WE: _a - 'P'-poprzedni rok
::  OLD: \esprp_value/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_val:=0;
_rok:={? var_press('_a')>0 & _a='P' || 'P' || 'B' ?};
_typ:=ISTDEFS.OPIS;
_val:=($('E_SPR_P.'+_rok+'_KW_'+(ISTDEFS.OPIS+1)))();
_tys:=var_press('TysZL')>0 & TysZL;
{? _tys
|| _val:=_val/1000$0
?};
{? _val
|| {? _tys
   || form(_val,,,'9.')
   || form(_val,,2,'9.')
   ?}
|? ISTDEFS.WYM='T'
|| {? _tys
   || '0'
   || '0.00'
   ?}
|| ''
?}


\esprp_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Pozycje użytkownika w informacji podatkowej - podpozycje
::  OLD: \esprp_pod/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('XmlFile')>0
|| ISTDEFS.cntx_psh();
   ISTDEFS.prefix();
   {? ISTDEFS.seek(ISTDEFS.TREE,)
   || ISTDEFS.OPIS:='dtsf:Podpozycja';
      ISTDEFS.FORMULA:=$"exec('esprp_pod_f','jpk_sf',_a)";
      exec('gen_one','xml',XmlFile,15)
   ?};
   ISTDEFS.cntx_pop()
?};
0


\esprp_pod_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Pozycje użytkownika w informacji podatkowej - początek/koniec pętli
::   WE: _a - 1-początek pętli 0-koniec pętli
::  OLD: \esprp_pod_f/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? SIK.DEFW
|| {? _a=1
   || E_SPR_P.cntx_psh();
      E_SPR_P.index('INNA'); E_SPR_P.prefix(REF.FIRMA,JPK.OKRO_F().ROK,SIK.DEFW,'N',E_SPR_P.ref());
      E_SPR_P.first()
   || E_SPR_P.cntx_pop()
   ?}
?}


\p_esprp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - wiersz informacji dodatkowych - pozycje użytkownika
::   WE:  _a  - 1-start 0-koniec taga
::       [_b] - nazwa
::  OLD: \p_esprp/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| _kod:=ISTDEFS.OPIS;
   {? (_p:=_kod*':')
   || _kod:=_p-_kod
   ?};
   _tab:=eSpr.tab2;
   _tab.blank(1);
   _tab.FIRMA:=eSpr.firma;
   _tab.LP:=(eSpr.lp2+=1);
   {? var_press('_b')>0 || _tab.WIERSZ:=' - '+_b ?};
   _tab.add()
?};
0


\p_esprp_naz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - wiersz informacji dodatkowych - nazwa pozycji użytkownika
::  OLD: \p_esprp_naz/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=eSpr.tab2;
_tab.WIERSZ:=' - '+STR.gsub(_b,'\n',' ');
_tab.put()


\p_esprp_pp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - wiersz informacji dodatkowych - podstawa prawna
::  OLD: \p_esprp_pp/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| _typ:=-(ISTDEFS.OPIS+3);
   _tab:=eSpr.tab2;
   _tab.PODSTAWA+={? _typ<>'lit' | _tab.PODSTAWA*'pkt'=0 || {? _tab.PODSTAWA<>'' || ' ' || '' ?}+_typ+{? _typ<>'pkt' || '. ' || ' ' ?} || '' ?}+_b;
   _tab.put()
?};
1


\p_esprp_pod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - wiersz informacji dodatkowych - podpozycja
::  OLD: \p_esprp_pod/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| _b:='<Podpozycja>'+_b+'</Podpozycja>';
   VAR_DEL.delete('eSprRej');
   eSprRej:=obj_new('rej','value','kw');
   eSprRej.rej:=0;
   x_parse(_b,,,"
      _kod:=_a;
      {? (_p:=_kod*':') || _kod:=_p-_kod ?};
      {? _kod='NazwaPozycji' | 5+_kod='Kwota' & +_kod=6 | _kod='Art' | _kod='Ust' | _kod='Pkt' | _kod='Lit'
      || eSprRej.rej:=1;
         eSprRej.value:=''
      ?};
      1
   ","
      _kod:=_a;
      {? (_p:=_kod*':') || _kod:=_p-_kod ?};
      {? _kod='NazwaPozycji'
      || eSprRej.kw:=-3;
         _tab:=eSpr.tab2;
         _tab.blank(1);
         _tab.FIRMA:=eSpr.firma;
         _tab.LP:=(eSpr.lp2+=1);
         _tab.WIERSZ:=' - '+STR.gsub(eSprRej.value,'\n',' ');
         _tab.add()
      |? 5+_kod='Kwota' & +_kod=6
      || _tab:=eSpr.tab2;
         _lit:=_kod+1;
         {? _lit='A' ||  eSprRej.kw+=3 ?};
         _tab[4+eSprRej.kw+(%_lit-64)]:=#eSprRej.value;
         _tab.put()
      |? _kod='Art' | _kod='Ust' | _kod='Pkt' | _kod='Lit'
      || _typ:=-_kod;
         _tab:=eSpr.tab2;
         _tab.PODSTAWA+={? _typ<>'lit' | _tab.PODSTAWA*'pkt'=0 || {? _tab.PODSTAWA<>'' || ' ' || '' ?}+_typ+{? _typ<>'pkt' || '. ' || ' ' ?} || '' ?}+eSprRej.value;
         _tab.put()
      ?};
      eSprRej.rej:=0;
      eSprRej.value:=''
   ","
      {? eSprRej.rej
      || eSprRej.value+=_a
      ?};
      1
   ");
   VAR_DEL.delete('eSprRej')
?};
1


\bv_esprp_inne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Przed wyświetleniem pola E_SPR_P.INNE
::----------------------------------------------------------------------------------------------------------------------
_red:=E_SPR_P.win_edit('?');
E_SPR_P.efld_opt(_red,'mark='+$(E_SPR_P.INNE='N'),E_SPR_P,'NAZ');
E_SPR_P.efld_opt(_red,'mark='+$(E_SPR_P.INNE='N'),E_SPR_P,'ART');
1


\podat_rp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Informacje podatkowe - rok poprzedni - czy przekazywać wartości zerowe?
::       Gdy brak zmiennej globalnej PodatRP lub jest równa 0 (wartość domyślna) -
::       nie są pokazywane wartości zerowe dla roku poprzedniego
::  OLD: \podat_rp/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
:: Aby wartości zerowe szły do roku poprzedniego należy odkomentować poniższy kod.
:: PodatRP:=1;
::
var_press('PodatRP')>0 & PodatRP | exec('grupa_if','xml')


\firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Nazwa (firmy) i siedziba jednostki
::  OLD: \firma/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
E_SPR_F.FIRMA().OPIS+' '+E_SPR_F.memo_txt(,1,'SIEDZIBA')


\firmy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Sprawozdanie skonsolidowane - firmy
::   WE: _a - 1 - start,stop
::            2 - next
::       _b - 1-start 2-stop
::       _c - typ zależności
::  OLD: \firmy/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? _b=1
   || E_SPR_F.cntx_psh();
      {? _c='T'
      || E_SPR_F.index('OGRANICZ'); E_SPR_F.prefix(JPK.E_SPR,'T')
      |? _c='O'
      || E_SPR_F.index('INNY_OKR'); E_SPR_F.prefix(JPK.E_SPR,'T',)
      |? _c='L'
      || E_SPR_F.index('L_TN'); E_SPR_F.prefix(JPK.E_SPR,'T',)
      |? _c='D'
      || E_SPR_F.index('DODAT'); E_SPR_F.prefix(JPK.E_SPR,'T',);
         _ver:=exec('ver','jpk_sf',JPK.ISTDEF);
         {? _ver>1
         || VAR_DEL.delete('EsprF');
            EsprF:=obj_new('tab','add','first','next','get');
            EsprF.tab:=tab_tmp(2,
               'FIRMA','INTEGER',,
               'REF','INTEGER',
            );
            EsprF.add:="
               _tab:=.tab;
               {? ~_tab.find_key(_a)
               || _tab.FIRMA:=_a;
                  _tab.REF:=_b;
                  _tab.add()
               ?}
            ";
            EsprF.get:="
               _tab:=.tab;
               {? _tab.REF
               || E_SPR_F.prefix();
                  E_SPR_F.seek(_tab.REF,)
               || E_SPR_F.blank(1);
                  FIRMA.cntx_psh();
                  FIRMA.prefix();
                  E_SPR_F.FIRMA:={? FIRMA.seek(_tab.FIRMA,) || FIRMA.ref() || null ?};
                  FIRMA.cntx_pop();
                  E_SPR_F.FIRMA
               ?};
               exec('set_okr_firmy','jpk_sf',E_SPR_F.FIRMA,HELPJPK.OKR_DO)
            ";
            EsprF.first:="
               _tab:=.tab;
               {? _tab.first()
               || .get(); 1
               ?}
            ";
            EsprF.next:="
               _tab:=.tab;
               {? _tab.next()
               || .get(); 1
               ?}
            ";
            {? E_SPR_F.first()
            || {!
               |? EsprF.add(E_SPR_F.FIRMA,E_SPR_F.ref());
                  E_SPR_F.next()
               !}
            ?};
            E_SPR_O.cntx_psh();
            E_SPR_O.index('KOD2'); E_SPR_O.prefix(JPK.E_SPR,'B');
            {? E_SPR_O.first()
            || {!
               |? EsprF.add(E_SPR_O.FIRMA,0);
                  E_SPR_O.prefix(JPK.E_SPR,'B',E_SPR_F.FIRMA().SYMBOL,);
                  E_SPR_F.last();
                  E_SPR_O.prefix(JPK.E_SPR,'B');
                  E_SPR_O.next()
               !}
            ?};
            E_SPR_O.cntx_pop()
         ?}
      || E_SPR_F.index('E_SPR'); E_SPR_F.prefix(JPK.E_SPR,'N',_c,)
      ?};
      var_press('EsprF')>0 & EsprF.first() | E_SPR_F.first()
   || E_SPR_F.cntx_pop();
      VAR_DEL.delete('EsprF')
   ?}
|| {? var_press('EsprF')>0
   || EsprF.next()
   || E_SPR_F.next()
   ?}
?}


\bm_esprf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed dołącz/popraw tabeli E_SPR_F
::   WE: _a - typ operacji 1-dołacz 2-popraw
::  OLD: \bm_esprf/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_add:=_a;
{? _add=1
|| E_SPR_F.blank();
   E_SPR_F.memo_set(,'SIEDZIBA');
   E_SPR_F.memo_set(,'PRZED');
   E_SPR_F.memo_set(,'POW');
   E_SPR_F.memo_set(,'INNE');
   E_SPR_F.memo_set(,'KAPITAL');
   E_SPR_F.memo_set(,'PODSTAWA');
   E_SPR_F.memo_set(,'D_DO_O');
   E_SPR_F.memo_set(,'L_METODA')
|| E_SPR_F.memo_get(,'SIEDZIBA');
   E_SPR_F.memo_get(,'PRZED');
   E_SPR_F.memo_get(,'POW');
   E_SPR_F.memo_get(,'INNE');
   E_SPR_F.memo_get(,'KAPITAL');
   E_SPR_F.memo_get(,'PODSTAWA');
   E_SPR_F.memo_get(,'D_DO_O');
   E_SPR_F.memo_get(,'L_METODA')
?};
FIRMA.win_sel('WER');
FIRMA.win_dict('WER');
E_SPR_F.win_edit('RED');
exec('esprf_fld_upd','jpk_sf');
{? E_SPR_F.edit("exec('ar_esprf','jpk_sf')")
|| {? _add=1
   || E_SPR_F.add()
   || E_SPR_F.put()
   ?};
   E_SPR_F.memo_put(,'SIEDZIBA');
   exec('esprf_put','jpk_sf','PRZED');
   exec('esprf_put','jpk_sf','POW');
   exec('esprf_put','jpk_sf','INNE');
   exec('esprf_put','jpk_sf','KAPITAL');
   exec('esprf_put','jpk_sf','PODSTAWA');
   exec('esprf_put','jpk_sf','D_DO_O');
   exec('esprf_put','jpk_sf','L_METODA')
?}


\ar_esprf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Rekord po okna WER tabeli E_SPR_F
::  OLD: \ar_esprf/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_edit:=__CHK.record(E_SPR_F,,'FIRMA');
{? _edit=''
|| {? E_SPR_F.FIRMA=REF.FIRMA
   || FUN.info('Firmy sprawozdania skonsolidowanego\nnie można dodać do listy firm.'@);
      _edit:='FIRMA'
   || _ref:=E_SPR_F.ref();
      E_SPR_F.cntx_psh();
      E_SPR_F.index('FIRMA'); E_SPR_F.prefix(E_SPR_F.E_SPR,E_SPR_F.FIRMA);
      {? E_SPR_F.first() & (-menu_txt()<>'popraw' | E_SPR_F.ref()<>_ref)
      || FUN.info('Istnieje informacja dla firmy: %1\n%2.'@[E_SPR_F.FIRMA().SYMBOL,FIRMA.OPIS]);
         _edit:='FIRMA'
      ?};
      E_SPR_F.cntx_pop()
   ?}
?};
E_SPR_F.UDZIAL:=E_SPR_F.UDZIAL$2;
{? _edit='' || _edit:=__CHK.record3(E_SPR_F,'UDZIAL','Udział procentowy w kapitale (funduszu) podstawowym') ?};
{? _edit='' & (E_SPR_F.UDZIAL<0 | E_SPR_F.UDZIAL>100)
|| FUN.info('Procent udziału powinien mieścić się w zakresie 0-100.'@);
   _edit:='UDZIAL'
?};
E_SPR_F.GLOSY:=E_SPR_F.GLOSY$2;
{? _edit='' & E_SPR_F.GLOSY
|| {? E_SPR_F.UDZIAL=E_SPR_F.GLOSY
   || FUN.info('Procent udziałów i głosów powinien być różny.'@);
      _edit:='GLOSY'
   |? E_SPR_F.GLOSY<0 | E_SPR_F.GLOSY>100
   || FUN.info('Procent głosów powinien mieścić się w zakresie 0-100.'@);
      _edit:='GLOSY'
   ?}
?};
{? _edit=''
|| _edit:=__CHK.record(E_SPR_F,,'SIEDZIBA');
   {? _edit='' & +E_SPR_F.memo_txt(,0,'SIEDZIBA')>3500
   || __CHK.err_fld(E_SPR_F,'SIEDZIBA',1,'Za długi opis. Przekracza 3500 znaków.');
      _edit:='SIEDZIBA'
   ?}
?};
{? _edit=''
|| _edit:=exec('esprf_chk','jpk_sf','PRZED','POW',{? E_SPR_F.TYP='I' || 'INNE' || '' ?},'PODSTAWA','D_OD','D_DO','D_DO_O','S_OD','S_DO','L_METODA')
?};
{? _edit='' & E_SPR_F.OGRANICZ='TD' & E_SPR_F.D_OD>E_SPR_F.D_DO
|| FUN.info('Data od ograniczeń czasowych jest późniejsza niż data do.'@);
   _edit:='D_OD'
?};
{? _edit='' & E_SPR_F.INNY_OKR='T' & E_SPR_F.S_OD>E_SPR_F.S_DO
|| FUN.info('Data od okresu sprawozdawczego jest późniejsza niż data do.'@);
   _edit:='S_OD'
?};
_edit


\esprf_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Sprawdzenie wypełnienia pola dla firm e-Sprawozdania skonsolidowanego
::   WE: _a - akronim pola
::  OLD: \esprf_chk/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_edit:='';
{! _ii:=1.._
|? _edit=''
|! {? exec('be_esprf','jpk_sf',_[_ii])
   || _edit:=__CHK.record(E_SPR_F,,_[_ii]);
      {? _edit='' & var_press(_[_ii],E_SPR_F)=36 & +E_SPR_F.memo_txt(,0,_[_ii])>3500
      || _edit:=_[_ii];
         __CHK.err_fld(E_SPR_F,_[_ii],1,'Za długi opis. Przekracza 3500 znaków.')
      ?}
   ?}
!};
_edit


\esprf_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zatwierdzenie pola memo dla firm
::   WE: _a - akronim pola
::  OLD: \esprf_put/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('be_esprf','jpk_sf',_a)
|| E_SPR_F.memo_set(,_a)
?};
E_SPR_F.memo_put(,_a)


\be_esprf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redagowaniem pól tabeli E_SPR_F
::   WE: [_a] - akronim pola
::  OLD: \be_esprf/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_fld:={? var_press('_a')>0 || _a || cur_afld() ?};
_edit:=0;
{? _fld='I_PODAT'
|| SIK.E_KOD:=exec('get_ekod','jpk_sf','I_PODAT');
   GR_SIK.f_clear();
   GR_SIK.win_sel('SLO2');
   GR_SIK.hdr_sel();
   GR_SIK.hdr_sel(' - e-Kod: '+SIK.E_KOD);
   {? PAR_SKID.get(95)<>'T'
   || __F_ref:='S';
      {? E_SPR_F.FIRMA & E_SPR_F.I_PODAT & E_SPR_F.I_PODAT().FIRMA<>E_SPR_F.FIRMA & E_SPR_F.I_PODAT().FIRMA<>REF.GRUPA
      || E_SPR_F.I_PODAT:=null
      ?}
   ?};
   _edit:=E_SPR_F.FIRMA<>null
|? _fld='PRZED'
|| _edit:=E_SPR_F.TYP='Z'
|? _fld='GLOSY'
|| _edit:='ZI'*E_SPR_F.TYP
|? _fld='POW'
|| _edit:=E_SPR_F.TYP='Z'
|? _fld='INNE'
|| _edit:='IW'*E_SPR_F.TYP
|? _fld='KAPITAL'
|| _edit:='I'*E_SPR_F.TYP
|? _fld='PODSTAWA'
|| _edit:=E_SPR_F.TYP='W'
|? _fld='D_OD'
|| _edit:=1+E_SPR_F.OGRANICZ='T'
|? _fld='D_DO'
|| _edit:=E_SPR_F.OGRANICZ='TD'
|? _fld='D_DO_O'
|| _edit:=E_SPR_F.OGRANICZ='TO'
|? _fld='S_OD' | _fld='S_DO'
|| _edit:=E_SPR_F.INNY_OKR='T'
|? _fld='L_METODA'
|| _edit:=E_SPR_F.L_TN='T'
?};
_edit


\bv_esprf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redagowaniem pól tabeli E_SPR_F
::  OLD: \bv_esprf/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
_edit:=exec('be_esprf','jpk_sf',_fld);
{? _edit
|| ''
|| exec('findfnrd','color')
?};
''


\ae_esprf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Po redagowaniu pól tabeli E_SPR_F
::  OLD: \ae_esprf/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_fld:=cur_afld();
_edit:=1;
{? _fld='TYP'
|| {? var_press('eSpr')>0
   || fld(eSpr.tabF.TYP)
   ?};
   exec('esprf_fld_upd','jpk_sf');
   win_disp()
|? _fld='OGRANICZ'
|| {? E_SPR_F.OGRANICZ='NN'
   || E_SPR_F.D_OD:=date(0,0,0);
      E_SPR_F.D_DO:=date(0,0,0)
   |? E_SPR_F.OGRANICZ='TO'
   || E_SPR_F.D_DO:=date(0,0,0)
   ?};
   exec('esprf_fld_upd','jpk_sf');
   win_disp()
|? _fld='INNY_OKR'
|| {? E_SPR_F.INNY_OKR='N'
   || E_SPR_F.S_OD:=date(0,0,0);
      E_SPR_F.S_DO:=date(0,0,0)
   ?};
   exec('esprf_fld_upd','jpk_sf');
   win_disp()
|? _fld='L_TN'
|| exec('esprf_fld_upd','jpk_sf');
   win_disp()
?};
_edit


\esprf_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Dodaje/modyfikuje zapis systemowy w tabeli E_SPR_F
::  OLD: \esprf_sys/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
{? E_SPR.ISTDEF().VER*'Skonsolid'
|| E_SPR_F.cntx_psh();
   E_SPR_F.index('E_SPR'); E_SPR_F.prefix(E_SPR.ref(),'T',);
   {? E_SPR_F.first()
   || E_SPR_F.I_PODAT:=E_SPR.I_PODAT;
      E_SPR_F.put()
   || E_SPR_F.blank(1);
      E_SPR_F.E_SPR:=E_SPR.ref();
      E_SPR_F.SYS:='T';
      E_SPR_F.FIRMA:=REF.S_FIRMA;
      E_SPR_F.OGRANICZ:='N';
      E_SPR_F.INNY_OKR:='N';
      E_SPR_F.L_TN:='N';
      E_SPR_F.I_PODAT:=E_SPR.I_PODAT;
      E_SPR_F.DODAT:='T';
      E_SPR_F.add()
   ?};
   E_SPR_F.cntx_pop()
?};
ISTDEF.cntx_pop()


\t_espr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wyzwalacz po put dla tabeli E_SPR
::  OLD: \t_espr/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? bfld('I_PODAT')<>E_SPR.I_PODAT
|| exec('esprf_sys','jpk_sf')
?};
~~


\t_esprf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wyzwalacz przed add i put tabeli E_SPR_F
::  OLD: \t_esprf/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
E_SPR_F.DODAT:={? E_SPR_F.I_PODAT || 'T' || 'N' ?};
1


\beesprof
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją pola E_SPR_O.FIRMA
::  OLD: \beesprof/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
_edit:=E_SPR.ISTDEF().VER*'Skonsolid';
ISTDEF.cntx_pop();
_edit


\p_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Odczyt e-Sprawozdania - wiersz informacji dodatkowych - pozycje użytkownika
::  OLD: \p_firma/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
|| eSpr.firma:=_b
?};
1


\obj_kom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tworzy tablice na komunikaty
::   WE: _a - tytuł
::  OLD: \obj_kom/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new('s_naz','is_s','s_start','is','s_end','add','show');
_tab.s_naz:='';
_tab.is_s:=0;
_tab.is:=0;
exec('KOMM','#object');
KOMM.init(,,_a);
_tab.s_start:="
   .s_naz:=_a
";
_tab.s_end:="
   {? .is_s
   || KOMM.sect_end();
      .is_s:=0;
      .s_naz:=''
   ?}
";
_tab.add:="
   {? .is_s=0 & .s_naz<>''
   || .is_s:=1;
      KOMM.sect_beg(.s_naz)
   ?};
   KOMM.add(_a);
   .is:=1
";
_tab.show:="
   {? .is
   || KOMM.select();
      1
   ?}
";
_tab


\parse_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Domyślna formuła odczytująca e-Sprawozdanie
::   WE: _a - typ
::       _b - wartość
::  OLD: \parse_def/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_rej:=0;
_tag:=ISTDEFS.OPIS;
{? (_p:=_tag*':') || _tag:=_p-_tag ?};
{? _a=1
|| {? ISTDEFS.OPIS+9='Zawartosc'
   || _rej:=1;
      XmlInfo.fname:='b64_'+($eSpr.tabPliki.tm_stamp())+'.b64'
   |? ISTDEFS.REGULY*'big_opis'
   || _rej:=1
   |? eSpr.skonsolid & 'P_2,P_4,P_5,P_6,P_7'*(3+_tag)
   || _rej:=1
   |? 'DataOd,DataDo,DataDoOpis'*(ISTDEFS.OPIS+6)
   || _rej:=eSpr.var
   || ISTDEFS.cntx_psh();
      ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEFS.ISTDEF,ISTDEFS.ref());
      _jest:=ISTDEFS.first();
      ISTDEFS.cntx_pop();
      _rej:=~_jest
   ?}
|? ISTDEFS.OPIS+12='NazwaPozycji'
|| _tab:=eSpr.tabOpis;
   _tab.blank(1);
   _tab.K:='P__';
   _tab.add();
   _tab.memo_set(_b,'NAZ');
   _tab.memo_put(,'NAZ');
   _tab.put()
|? ISTDEFS.REGULY*'big_opis' | ISTDEFS.REGULY*'opis' & 1+ISTDEFS.TYPFLD='O'
|| _key:=ISTDEFS.OPIS;
   {? (_p:=_key*':')>0 || _key:=_p-_key ?};
   _tab:=eSpr.tabOpis;
   {? _key<>'Opis'
   || {? ~_tab.find_key(_key,)
      || _tab.blank(1);
         _tab.K:=_key;
         _tab.add();
         _naz:=ISTDEFS.memo_txt(,1,'ANOTACJA');
         {? (_p:=_naz*';') || _naz:=(_p-1)+_naz ?};
         _tab.memo_set(_naz,'NAZ');
         _tab.memo_put(,'NAZ')
      ?}
   ?};
   _txt:=_tab.memo_txt(,1,'MEMO');
   _txt+=_b;
   _tab.memo_set(_txt,'MEMO');
   _tab.memo_put(,'MEMO');
   _tab.put()
|? ISTDEFS.OPIS+4='Opis' & eSpr.var=0
|| _tab:=eSpr.tabPliki;
   _tab.blank(1);
   _tab.LP:=(eSpr.lpPliki+=1);
   _tab.add();
   _tab.memo_set(_b,'OPIS');
   _tab.memo_put(,'OPIS')
|? ISTDEFS.OPIS+5='Nazwa'
|| _tab:=eSpr.tabPliki;
   _tab.NAZ:=_b;
   _tab.put()
|? ISTDEFS.OPIS+9='Zawartosc'
|| _f:=fopen(XmlInfo.fname,'br',1,,1);
   {? _f.is_open()
   || _tab:=eSpr.tabPliki;
      _naz:=$_tab.tm_stamp()+'.tmp';
      _f2:=fopen(_naz,'bw',1,,1);
      {? _f2.is_open()
      || {? base64('decode',_f,_f2)
         || _tab.FN:=_naz;
            _tab.put()
         ?};
         _f2.fclose()
      ?};
      _f.fclose();
      ferase(XmlInfo.fname,1)
   ?};
   XmlInfo.fname:=''
|? eSpr.skonsolid & 'P_2,P_4,P_5,P_6,P_7'*(3+_tag)
|| _tab:=eSpr.tabF;
   _typ:=#(1+(2-_tag));
   _lit:=_tag+1;
   {? _tag+1='A'
   || {? _typ<6
      || _tab.blank(1);
         _tab.FIRMA:=STR.gsub(_b,'\n',' ');
         _tab.TYP_STR:={? _typ=2
                       || 'zależna, współzależna i stowarzyszona'
                       |? _typ=4
                       || 'inna niż jednostka podporządkowana'
                       |? _typ=5
                       || 'podporządkowana wyłączona'
                       || 'nieznana'
                       ?};
         _tab.TYP:={? _typ=2 || 'Z' |? _typ=4 || 'I' |? _typ=5 || 'W' || 'N' ?};
         {? _tab.add()
         || eSpr.setMF('FS',_b)
         ?}
      || _firma:=STR.gsub(_b,'\n',' ');
         {? _tab.find_key(_firma)
         || eSpr.var:=_typ
         || eSpr.var:=0
         ?}
      ?}
   |? _typ=2
   || {? _lit='B'
      || eSpr.setMF('PRZED',_b)
      |? _lit='C'
      || _tab.UDZIAL:=#_b; _tab.put()
      |? _lit='D'
      || _tab.GLOSY:=#_b; _tab.put()
      |? _lit='E'
      || eSpr.setMF('POW',_b)
      ?}
   |? _typ=4
   || {? _lit='B'
      || eSpr.setMF('INNE',_b)
      |? _lit='C'
      || eSpr.setMF('KAPITAL',_b)
      |? _lit='D'
      || _tab.UDZIAL:=#_b; _tab.put()
      |? _lit='E'
      || _tab.GLOSY:=#_b; _tab.put()
      ?}
   |? _typ=5
   || {? _lit='B'
      || eSpr.setMF('PODSTAWA',_b)
      |? _lit='C'
      || _tab.UDZIAL:=#_b; _tab.put()
      |? _lit='D'
      || eSpr.setMF('INNE',_b)
      ?}
   ?}
|? 'DataOd,DataDo,DataDoOpis'*(ISTDEFS.OPIS+6)
|| _tab:=eSpr.tabF;
   {? eSpr.var=6
   || {? ISTDEFS.OPIS+2='Od'
      || _tab.D_OD:=date(#(4+_b),#(5-_b-3),#(_b+2)); _tab.put()
      |? ISTDEFS.OPIS+2='Do'
      || _tab.D_DO:=date(#(4+_b),#(5-_b-3),#(_b+2)); _tab.put(); eSpr.var:=0
      || eSpr.setMF('D_DO_O',_b); eSpr.var:=0
      ?}
   |? eSpr.var=7
   || _data:=date(#(4+_b),#(5-_b-3),#(_b+2));
      {? ISTDEFS.OPIS+2='Od'
      || _tab.S_OD:=_data; _tab.put()
      || _tab.S_DO:=_data; _tab.put();
         eSpr.var:=0
      ?}
   ?}
|| _tab:=eSpr.tabNag;
   _key:=ISTDEFS.OPIS;
   {? (_p:=_key*':')>0 || _key:=_p-_key ?};
   _tab.LP:=(eSpr.lpNag+=1);
   _tab.K:=_key;
   _tab.NAZ:=ISTDEFS.memo_txt(,1,'ANOTACJA');
   {? _tab.NAZ='' || _tab.NAZ:=_tab.K ?};
   _tab.NAZ:=STR.gsub(_tab.NAZ,'\t',' ');
   _tab.V:=_b;
   _tab.add();
   {? _key='KodSprawozdania'
   || ISTDEFI.cntx_psh();
      ISTDEFI.index('ISTDEFS'); ISTDEFI.prefix(ISTDEFS.ref(),'wersjaSchemy',);
      {? ISTDEFI.first()
      || _ws:=($ISTDEFI.REGULY)();
         _tab.LP:=(eSpr.lpNag+=1);
         _tab.K:='wersjaSchemy';
         _tab.NAZ:='Wersja';
         _tab.V:=_ws;
         _tab.add()
      ?};
      ISTDEFI.cntx_pop()
   ?}
?};
_rej


\espr_prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Akcja Drukuj podgladu e-Sprawozdania
::  OLD: \espr_prn/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy wydrukować e-Sprawozdanie?\n\nWydruk nie ma mocy prawnej.\nNie jest e-Sprawozdaniem.'@)
|| 0 & rep_tran('jpk_sf');
   exec('rep_exec','#b_report','','fks_jpk_sf',,,,,,,,'W')
?}


\si_decl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca obiekt do pobierania informacji o podpisach
::  OLD: \si_decl/jpk_sf.fml
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'TAB','LIB','FN',
   'parse','remove','first','next','get','getCN'
);
_obj.LIB:=0;
_obj.FN:=0;
_obj.TAB:=tab_tmp(1,
   'LP','INTEGER','Lp.',
   'SUBJECT','STRING[255]','Podmiot',
   'TIME','STRING[20]','Czas',
   'MEMO','SYS_MEMO','Public'
);
_obj.remove:="exec('remove_sign','jpk_sf',_a)";
_obj.parse:="
   _fn:=_a;
   _file:=fopen(_fn,'ur',0,,1);
   {? _file.is_open()
   || _hlp:=obj_new(
         'TXT','TAB','LP','EPUAP',
         'rej'
      );
      _hlp.TXT:='';
      _hlp.LP:=0;
      _hlp.EPUAP:=0;
      _tab:=.TAB;
      _tab.erase();
      _hlp.TAB:=_tab;
      params_set('hlp',_hlp,'obj',.);
      xml_sax_parse(_file,,,
         \"
            _hlp:=params_get().hlp;
            _fld:=_a;
            {? _fld='Signature'
            || _tab:=_hlp.TAB;
               _tab.blank(1);
               _tab.LP:=(_hlp.LP+=1);
               _tab.add()
            |? _fld='X509Certificate' | _fld='SigningTime' | _hlp.EPUAP & (_fld='Nazwisko' | _fld='Imie')
            || _hlp.rej:=1;
               _hlp.TXT:=''
            |? _fld='DaneZP'
            || _hlp.EPUAP:=1;
               _tab:=_hlp.TAB;
               _tab.SUBJECT:=''
            ?};
            1
         \",
         \"
            _fld:=_a;
            _hlp:=params_get().hlp;
            _obj:=params_get().obj;
            _tab:=_hlp.TAB;
            {? _fld='Signature'
            || _tab.put();
               _hlp.EPUAP:=0
            |? _fld='X509Certificate'
            || _tab.SUBJECT:=_obj.getCN(_hlp.TXT)
            |? _fld='SigningTime'
            || _tab.TIME:=_hlp.TXT
            |? _hlp.EPUAP
            || {? _fld='Nazwisko'
               || {? _tab.SUBJECT<>'' ||  _tab.SUBJECT+=' ' ?};
                  _tab.SUBJECT+=_hlp.TXT
               |? _fld='Imie'
               || {? _tab.SUBJECT<>''
                  || _tab.SUBJECT:=_hlp.TXT+' '+_tab.SUBJECT
                  || _tab.SUBJECT+=_hlp.TXT
                  ?}
               ?}
            ?};
            _hlp.rej:=0;
            1
         \",
         \"
            _hlp:=params_get().hlp;
            {? _hlp.rej=1
            || _hlp.TXT+=_a
            ?};
            1
         \"
      );
      fclose(_file)
   ?}
";
_obj.first:="
   .TAB.first()
";
_obj.next:="
   .TAB.next()
";
_obj.get:="
   _fun:=_a;
   {? _fun='subject'
   || .TAB.SUBJECT
   |? _fun='time'
   || .TAB.TIME
   || ''
   ?}
";
_obj.getCN:="
   _cer:=_a;
   _ret:='';
   {? exec('runtime_r10','#system')
   || _crt:=cert_info('-----BEGIN CERTIFICATE-----\n'+_a+'\n-----END CERTIFICATE-----');
      {? var_pres('_crt')>0 & var_pres('subject',_crt)>0 & var_pres('CN',_crt.subject)>0
      || _ret:=_crt.subject.CN
      ?};
      &_crt
   || _ret:=
      {? .LIB=0
      || .LIB:=lib_load('signinfo.dll',1)
      ?};
      {? .LIB
      || {? .FN=0
         || .FN:=lib_decl(.LIB,,'char *','getSubjectFromCer','char *')
         ?};
         {? .FN
         || _ret:=lib_call(.FN,_cer)
         ?}
      ?}
   ?};
   _ret
";
_obj


\remove_sign
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Usuwa podpisy z pliku xml
::   WE: _a - ścieżka do pliku xml
:: ~OST: INFCOPY,INFOPEN
::----------------------------------------------------------------------------------------------------------------------
_fn:=_a;
_fn2:=_a+'.s';
_fn3:=_a+'.b';
_res:=0;
{? fcopy('@'+_fn,'@'+_fn2,0,0,1)
|| _f3:=fopen('@'+_fn3,'Uw!',0);
   {? _f3
   || _f2:=fopen('@'+_fn2,'ur',0);
      {? _f2
      || _save:=1;
         _first:=1;
         {!
         |? {? (_str:=fread(_f2))<>'\n'
            || {? _first
               || _first:=0
               || fwrite(_f3,'\r\n')
               ?};
               {!
               |? _dalej:=0;
                  {? _save
                  || _pos:=_str*'<ds:Signature';
                     {? _pos
                     || {? _pos>1 || fwrite(_f3,(_pos-1)+_str) ?};
                        _str:=(_pos+1)-_str;
                        _save:=0;
                        _res:=1
                     ?}
                  ?};
                  {? ~_save
                  || _pos:=_str*'</ds:Signature>';
                     {? _pos
                     || _str:=(_pos+14)-_str;
                        _save:=1;
                        _dalej:=1
                     ?}
                  ?};
                  _dalej
               !};
               {? _save
               || fwrite(_f3,_str)
               ?};
               1
            ?}
         !};
         fclose(_f2)
      ?};
      fclose(_f3)
   ?}
?};
_res


\esprf_fld_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Aktualizacja pól tabeli E_SPR_F
::----------------------------------------------------------------------------------------------------------------------
_enable:=exec('be_esprf','jpk_sf','PRZED')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'PRZED');
E_SPR_F.efld_opt('RED','mark='+$_enable,E_SPR_F,'PRZED');
_enable:=exec('be_esprf','jpk_sf','GLOSY')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'GLOSY');
_enable:=exec('be_esprf','jpk_sf','POW')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'POW');
E_SPR_F.efld_opt('RED','mark='+$_enable,E_SPR_F,'POW');
_enable:=exec('be_esprf','jpk_sf','INNE')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'INNE');
E_SPR_F.efld_opt('RED','mark='+$(_enable & E_SPR_F.TYP='I' ),E_SPR_F,'INNE');
_enable:=exec('be_esprf','jpk_sf','KAPITAL')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'KAPITAL');
_enable:=exec('be_esprf','jpk_sf','PODSTAWA')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'PODSTAWA');
E_SPR_F.efld_opt('RED','mark='+$_enable,E_SPR_F,'PODSTAWA');
_enable:=exec('be_esprf','jpk_sf','D_OD')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'D_OD');
E_SPR_F.efld_opt('RED','mark='+$_enable,E_SPR_F,'D_OD');
_enable:=exec('be_esprf','jpk_sf','D_DO')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'D_DO');
E_SPR_F.efld_opt('RED','mark='+$_enable,E_SPR_F,'D_DO');
_enable:=exec('be_esprf','jpk_sf','D_DO_O')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'D_DO_O');
E_SPR_F.efld_opt('RED','mark='+$_enable,E_SPR_F,'D_DO_O');
_enable:=exec('be_esprf','jpk_sf','S_OD')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'S_OD');
E_SPR_F.efld_opt('RED','mark='+$_enable,E_SPR_F,'S_OD');
_enable:=exec('be_esprf','jpk_sf','S_DO')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'S_DO');
E_SPR_F.efld_opt('RED','mark='+$_enable,E_SPR_F,'S_DO');
_enable:=exec('be_esprf','jpk_sf','L_METODA')<>0;
E_SPR_F.efld_opt('RED','enable='+$_enable,E_SPR_F,'L_METODA');
E_SPR_F.efld_opt('RED','mark='+$_enable,E_SPR_F,'L_METODA');
~~


\pu_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Obiekt do obsługi powielonych elementów
::----------------------------------------------------------------------------------------------------------------------
{? var_press('TList')<0
|| TList:=obj_new('TAB','exists','clear');
   TList.TAB:=tab_tmp(2,
      'A','INTEGER',,
      'B','INTEGER',
   );
   TList.exists:="
      _tab:=.TAB;
      _tab.prefix();
      {? _tab.find_key(_a,_b)
      || 1
      || _tab.A:=_a;
         _tab.B:=_b;
         _tab.add();
         0
      ?}
   ";
   TList.clear:="
      _tab:=.TAB;
      _tab.prefix(_a);
      {? _tab.first() || {! |? _tab.del !} ?}
   "
?};
TList


\el_req
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy element schematu JPK jest wymagany
::   WE: _a - schemat (ISTDEF)
::       _b - typ pola
::----------------------------------------------------------------------------------------------------------------------
_wym:=1;
ISTDEFS.cntx_psh();
ISTDEFS.index('TYPPOLA'); ISTDEFS.prefix(_a,_b,);
{? ISTDEFS.first() || _wym:=ISTDEFS.WYM='T' ?};
ISTDEFS.cntx_pop();
_wym


\ver
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca wersję schematu e-Sprawozdania
::   WE: _a - schemat (ISTDEFS.ref)
::----------------------------------------------------------------------------------------------------------------------
_ver:='1-0E';
ISTDEFS.cntx_psh();
ISTDEFS.index('OPIS'); ISTDEFS.prefix(_a,'sin:KodSprawozdania',);
{? ISTDEFS.first()
|| ISTDEFI.cntx_psh();
   ISTDEFI.index('ISTDEFS'); ISTDEFI.prefix(ISTDEFS.ref(),'wersjaSchemy',);
   {? ISTDEFI.first()
   || _ver:=($ISTDEFI.REGULY)()
   ?};
   ISTDEFI.cntx_pop()
?};
ISTDEFS.cntx_pop();
_ver:=gsub(_ver,'-','.');
_ver:=gsub(_ver,'E','');
#_ver


\set_okr_firmy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Ustawia okresy dla firmy
::   WE: _a - wskazanie na firmę
::       _b - okres
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('eSprOkr')>0 & PAR_SKID.get(95)<>'T'
|| _firma:=_a;
   _okres:=_b;
   _bak:=OKRESY.OKRES;
   OKRO_F.cntx_psh();
   OKRESY.OKRES:=_okres;
   _pocz:=OKRESY.OKRES().POCZ;
   _kon:=OKRESY.OKRES().KON;
   OKRO_F.index('KON'); OKRO_F.prefix(_firma);
   OKRESY.OKRES:={? OKRO_F.find_key(_kon,_pocz) || OKRO_F.ref() || OKRESY.OKRES ?};
   OKRO_F.cntx_pop();
   eSprOkr.RokB:=OKRESY.OKRES().ROK;
   ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ'); ROK_F.prefix(_firma);
   eSprOkr.MaskaB:=OKRESY.OKRES().ROK().KOD;
   {? ROK_F.prev()
   || eSprOkr.RokP:=ROK_F.ref();
      eSprOkr.MaskaP:=ROK_F.KOD
   || eSprOkr.RokP:=null;
      eSprOkr.MaskaP:='__'
   ?};
   ROK_F.cntx_pop();
   OSPR.cntx_psh();
   OSPR.index('OKRES1'); OSPR.prefix(_firma,'M',eSprOkr.RokB,OKRESY.OKRES().NR,OKRESY.OKRES().NR);
   OKRESY.OKRES1:=eSprOkr.OsprB:={? OSPR.first() || OSPR.ref() || null ?};
   {? eSprOkr.RokP
   || OSPR.prefix(_firma,'M',eSprOkr.RokP,OKRESY.OKRES().NR,OKRESY.OKRES().NR);
      eSprOkr.OsprP:={? OSPR.first() || OSPR.ref() || null ?}
   || eSprOkr.OsprP:=null
   ?};
   OSPR.cntx_pop();
   OKRESY.OKRES:=_bak;
   REF.WYKON:=exec('wykon_firmy','konsolidacja',_firma)
?}


\ae_par_95
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Po redakcji parametru 95
::----------------------------------------------------------------------------------------------------------------------
PARAMETR.TRESC:=~-PARAMETR.TRESC;
{? ~(PARAMETR.TRESC='T' | PARAMETR.TRESC='N')
|| FUN.info('Dopuszczalne wartości - \"T\" lub \"N\".'); 0
|? wart_par<>PARAMETR.TRESC
|| _cur:=PARAMETR.TRESC='T';
   _tab:=tab_tmp(3,
      'D','DATE','Data',
      'S','STRING[50]','Schemat',
      'N','STRING[100]','Nazwa',
      'C','INTEGER','Zmiany',
      'R','INTEGER','ref'
   );
   _i1:=_tab.index('?');
   _i2:=_tab.ndx_tmp('',1,'C',,0, 'D',,0, 'S',,0, 'N',,0);
   _tpoz:=tab_tmp(1,
      'NR','INTEGER','Nagłowek',
      'R','INTEGER','Pozycje'
   );
   E_SPR.cntx_psh();
   E_SPR.index('FIRMA'); E_SPR.prefix(REF.FIRMA);
   {? E_SPR.first()
   || {!
      |? {? 14+E_SPR.ISTDEF().VER='Skonsolidowana'
         || _tab.blank();
            _tab.D:=E_SPR.DATA;
            _tab.S:=E_SPR.ISTDEF().VER;
            _tab.N:=E_SPR.NAZ;
            _tab.R:=#E_SPR.ref();
            _tab.add();
            _un_akc:=0;
            E_SPR_F.cntx_psh();
            E_SPR_F.index('FIRMA'); E_SPR_F.prefix(E_SPR.ref());
            {? E_SPR_F.first()
            || {!
               |? {? _cur
                  || _chg:=E_SPR_F.I_PODAT & E_SPR_F.I_PODAT().FIRMA<>REF.FIRMA
                  || _chg:=E_SPR_F.I_PODAT & E_SPR_F.I_PODAT().FIRMA<>E_SPR_F.FIRMA
                  ?};
                  {? _chg
                  || _tpoz.blank();
                     _tpoz.NR:=#_tab.ref();
                     _tpoz.R:=#E_SPR_F.ref();
                     _tpoz.add();
                     _un_akc:=1
                  ?};
                  E_SPR_F.next()
               !}
            ?};
            E_SPR_F.cntx_pop();
            {? _un_akc
            || _tab.C:=1;
               _tab.put()
            ?}
         ?};
         E_SPR.next()
      !}
   ?};
   E_SPR.cntx_pop();
   _tab.index(_i2);
   _tab.prefix(1);
   {? _tab.first()
   || {? FUN.ask(
            'Zmiana parametru wymusza ponowne wskazanie\n'+
            'sprawozdań podatkowych firm w definicjach\n'+
            'e-Sprawozdań skonsolidowanych oraz ich akceptację.\n\n'+
            'Kontynułować?'
         )
      || E_SPR.cntx_psh();
         E_SPR.prefix();
         E_SPR_F.cntx_psh();
         E_SPR_F.prefix();
         {!
         |? {? E_SPR.seek(_tab.R,)
            || _tpoz.prefix(#_tab.ref());
               {? _tpoz.first()
               || {!
                  |? {? E_SPR_F.seek(_tpoz.R)
                     || E_SPR_F.I_PODAT:=null;
                        E_SPR_F.put()
                     ?};
                     _tpoz.next()
                  !}
               ?};
               E_SPR.AKC:='N';
               E_SPR.put()
            ?};
            _tab.next()
         !};
         E_SPR_F.cntx_pop();
         E_SPR.cntx_pop();
         _wer:=_tab.mk_sel('e-Sprawozdania wymagające zmian','P',,'#esprchg',,,,,'U');
         _tab.win_fld(_wer,,'D');
         _tab.win_fld(_wer,,'S',,,50);
         _tab.win_fld(_wer,,'N',,,50);
         _tab.win_sel(_wer);
         _tab.select();
         1
      ?}
   || 1
   ?}
|| 1
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 30c141c0112cd97ed0e27561f23a74f3105c2ce208464adb1ab00aecb5d3545f8140d1aa01755b9b4cf8309413cae7a9a3dac2ce6469a7e4cabce94ae09014548ea2695493641a4d3661302eb2b7935be40eeae2ac329716e9e0ec538ca7b4cedb680c6c9b76510a1a38c56cdc79730a1553ef3506a10c08fbbdead198de92e7

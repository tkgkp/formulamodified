:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: edi_defxml.fml
:: Utworzony: 19.05.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Definiowanie komunikatów edi - xml
::======================================================================================================================


\cRIstdWe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: struktura po okna WER tabeli ISTDEF
::  OLD: \cRIstdWe/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~ISTDEF.r_lock(1,1,1)
||
   FUN.info('Zasób zablokowany przez innego użytkownika.'@)
||
   ISTDEFS.win_sel('WER');
   ISTDEFS.win_edit('RED');
   ISTDEFS.index('LP');
   ISTDEFS.prefix(ISTDEF.ref());
   ISTDEFS.select();
   ISTDEF.r_unlock()
?}


\istdefR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [8.40]
:: OPIS: formula na wartosc poczatkowa pola ISTDEFS.ISTDEF
::   WY: ISTDEF.ref
::  OLD: \istdefR/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.ref()


\cDItdsWe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: obsluga akcji dolacz (niepusta dziedzina) okienka WER tabeli ISTDEFS
::   WE: _a - brak argumentu dolacz; = 0 - pusta dziedzina; = 1 - dolacz
::  OLD: \cDItdsWe/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_nr:=
   {? _ = 0  || ISTDEFS.LP
   |? _a = 1 || ISTDEFS.LP + 1
   || 1
   ?};
_r:=1;
ISTDEFS.blank(1);
ISTDEFS.ISTDEF:=ISTDEF.ref();
ISTDEFS.LP:=_nr;
ISTDEFS.win_edit('RED');
{? ISTDEFS.edit("exec('chk_opis','edi_defxml','D')")
||
   {? _=0 || _r:=exec('reNrAdd','#table','ISTDEFS','LP') ?};
   {? _r || ISTDEFS.add() ?}
?}


\chk_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Formula sprawdza unikalnosc opisow struktury istrastatu
::       zastapienie unikalnego indeksu 'OPIS' z poprzednich wersji
::   WE: _a - znacznik czy sprawdzanie na dolacz czy na popraw
::            'D' - Dolacz
::            'P' - Popraw
::   WY: 'NAZFLD' - jezeli wystepuje juz akronim pola z redagowanego rekordu
::       '' - w p.p.
:: OLD: \chk_opis/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_ref:=ISTDEFS.ref();
ISTDEFS.cntx_psh();
ISTDEFS.index('OPIS');
ISTDEFS.prefix(ISTDEFS.ISTDEF,ISTDEFS.OPIS);
_opis:=ISTDEFS.OPIS;
{? _opis=''
||
   FUN.emsg('Niewypełnione pole: Nazwa elementu.'@);
   _wyn:='OPIS'
||

   {? ISTDEFS.first()
   || {!
      |? {? ISTDEFS.OPIS=_opis & (_a='D' | ISTDEFS.ref()<>_ref)
         || FUN.emsg('Występuje już pole w strukturze o nazwie: %1.'@[ISTDEFS.OPIS],ST.MSG);
            _wyn:='OPIS'
         ?};
         _wyn='' & ISTDEFS.next()
      !}
   ?}
?};
ISTDEFS.cntx_pop();
_wyn


\cPItdsWe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: obsluga akcji popraw (niepusta dziedzina) okienka WER tabeli ISTDEFS
::  OLD: \cPItdsWe/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFS.win_edit('RED');
{? ISTDEFS.edit("exec('chk_opis','edi_defxml','P')")
||
   ISTDEFS.put()
?}


\cUItdseWe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: usun po okna WER tabeli ISTDEFS
::  OLD: \cUItdseWe/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? ISTDEFS.count()>0
|| FUN.info('Do struktury przypisano atrybuty.\nUsunięcie niedozwolone.'@);
   0
|? FUN.ask('Czy usunąć bieżący wiersz?'@)
||
   _lp:=ISTDEFS.LP;
   {? ISTDEFS.del()
   ||
      {? _lp<ISTDEFS.LP || exec('ReNumAfterDel','#table','ISTDEFS','LP') ?}
   ?}
?}


\cXItdsWe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: dolacz po okna WER tabeli ISTDEFS
::  OLD: \cXItdsWe/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('cDItdsWe','edi_defxml',1)


\cDDItdsW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: (pusta dziedzina) dolacz po okna WER tabeli ISTDEFS
::  OLD: \cDDItdsW/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('cDItdsWe','edi_defxml',0)


\cAIdsWer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: atrybuty po okna WER tabeli ISTDEFS
::  OLD: \cAIdsWer/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFI.win_sel('WER');
ISTDEFI.win_edit('RED');
ISTDEFI.index('LP');
ISTDEFI.prefix(ISTDEFS.ref());
ISTDEFI.select()


\istdefsR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [8.40]
:: OPIS: formula na wartosc poczatkowa pola ISTDEFI.ISTDEFS
::   WY: ISTDEFS.ref
::  OLD: \istdefsR/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFS.ref()


\dItdiSlo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: klawisz F3 pola ISTDEFI.SLOWNIK
::   WE: wartosc wybranego elementu ze slownika, ~~ - wpp
::  OLD: \dItdiSlo/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_return:=~~;

istdsl_w:=ISTDEFSL.mk_sel('Słowniki'@,,0,'ditdisloist');
ISTDEFSL.win_sel(istdsl_w);
ISTDEFSL.win_fld(istdsl_w,,'SL_K',,,8,,,'Kod'@);
ISTDEFSL.win_fld(istdsl_w,,'O',,,67);
ISTDEFSL.win_act(istdsl_w,0,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');

ISTDEFSL.index('SL_K');
ISTDEFSL.prefix('N');
_slo:=ISTDEFI.SLOWNIK;
{? _slo<>'' || ISTDEFSL.find_key(_slo) ?};

{? ISTDEFSL.select(,1,2) || _return:=ISTDEFSL.SL_K ?};

&istdsl_w;

_return


\aItdiSlo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: po redakcji ISTDEFI.SLOWNIK
::   WY: 1
::  OLD: \aItdiSlo/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFSL.cntx_psh();
ISTDEFSL.index('SL');
_slo:=ISTDEFI.SLOWNIK;
{? _slo<>''
||
   ISTDEFSL.prefix(_slo,'N');
   {? ISTDEFSL.first() = 0 || FUN.info('Brak słownika %1.'@[_slo]) ?}
?};
ISTDEFSL.cntx_pop();
1


\cDItdiWe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: dolacz po okna WER tabeli ISTDEFI
::   WE: _a - brak argumentu - dolacz; = 0 - pusta dziedzina; = 1 - dolacz|
::  OLD: \cDItdiWe/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_nr:=
   {? _ = 0  || ISTDEFI.LP
   |? _a = 1 || ISTDEFI.LP+1
   || 1
   ?};
_r:=1;
ISTDEFI.blank(1);
ISTDEFI.ISTDEFS:=ISTDEFS.ref();
ISTDEFI.LP:=_nr;
ISTDEFI.win_edit('RED');
{? ISTDEFI.edit()
||
   {? _=0 || _r:=exec('reNrAdd','#table','ISTDEFI','LP') ?};
   {? _r || ISTDEFI.add() ?}
?}


\cUItdiWe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: usun po okna WER tabeli ISTDEFI
::  OLD: \cUItdiWe/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? ISTDEFI.count()>0
|| FUN.info('Atrybut wykorzystywany w systemie.\nUsunięcie niedozwolone.'@);
   0
|? FUN.ask('Czy usunąć bieżący wiersz?'@)
||
   _lp:=ISTDEFI.LP;
   {? ISTDEFI.del()
   ||
      {? _lp<ISTDEFI.LP || exec('ReNumAfterDel','#table','ISTDEFI','LP') ?}
   ?}
?}


\cXItdiWe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: dolacz| po okna WER tabeli ISTDEFI
::  OLD: \cXItdiWe/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('cDItdiWe','edi_defxml',1)


\cDDItdiW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: (pusta dziedzina) dolacz okna WER tabeli ISTDEFI
::  OLD: \cDDItdiW/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('cDItdiWe','edi_defxml',0)


\strukt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Definiowanie struktury komunikatu xml
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ISTDEFS.win_sel('W_EDI');
ISTDEFS.win_edit('R_EDI');
ISTDEFS.index('LP');
ISTDEFS.prefix(ISTDEF.ref());
ISTDEFS.hdr_sel();
ISTDEFS.hdr_sel(' '+ISTDEF.VER);
ISTDEFS.dnd_sel(ISTDEFS.win_sel('?'),,'records.ISTDEFS',"exec('renumistd','edi_defxml')");
ISTDEFS.select()


\edi_d1_struk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsługa akcji Dołącz| okienka W_EDI tabeli ISTDEFS
::   WY: 1 - dlaczono rekord
::       0 -w p.p.
::  OLD: \edi_d1_struk/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
exec('edi_dol_struk','edi_defxml',1)


\edi_dol_struk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsługa akcji Dołącz (niepusta dziedzina) okienka W_EDI tabeli ISTDEFS
::   WE: _a - brak argumentu Dołącz; = 0 - pusta dziedzina; = 1 - Dołącz|
::   WY: 1 - dlaczono rekord
::       0 -w p.p.
::  OLD: \edi_dol_struk/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
_nr:={? _=0  || ISTDEFS.LP
     |? _a=1 || ISTDEFS.LP+1
     || 1
     ?};
_r:=1;
ISTDEFS.blank(1);
ISTDEFS.ISTDEF:=ISTDEF.ref();
ISTDEFS.LP:=_nr;
ISTDEFS.win_edit('R_EDI'+{? 'MEI'*ISTDEF.IST_ISTK=0 || '_FK' || '' ?});
{? ISTDEFS.edit("__CHK.record(ISTDEFS,,'WYM')")
|| {? _=0 || _r:=exec('reNrAdd','#table','ISTDEFS','LP') ?};
   {? _r || ISTDEFS.add() ?}
?}


\edi_pop_struk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsługa akcji Popraw  okienka W_EDI tabeli ISTDEFS
::   WY: 1 - poprawiono rekord
::       0 -w p.p.
::  OLD: \edi_pop_struk/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFS.win_edit('R_EDI'+{? 'MEI'*ISTDEF.IST_ISTK=0 || '_FK' || '' ?});
{? ISTDEFS.edit("__CHK.record(ISTDEFS,,'WYM')")
|| ISTDEFS.put()
?}


\edi_usu_struk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsługa akcji Usuń okienka W_EDI tabeli ISTDEFS
::  OLD: \edi_usu_struk/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć bieżący wiersz?'@)
|| _lp:=ISTDEFS.LP;
   ISTDEFI.index('LP');
   ISTDEFI.prefix(ISTDEFS.ref());
   {? ISTDEFI.first() || {! |? ISTDEFI.del() !} ?};
   {? ISTDEFS.del()
   || {? _lp<ISTDEFS.LP || exec('reNrDel','#table','ISTDEFS','LP') ?}
   ?}
?}


\edi_atryb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Formula wyswietla atrybuty wybranej struktury specyfikacji EDI
::  OLD: \edi_atryb/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFI.win_sel('W_EDI'+{? 'MEI'*ISTDEF.IST_ISTK=0 || '_FK' || '' ?});
ISTDEFI.win_edit('R_EDI'+{? 'MEI'*ISTDEF.IST_ISTK=0 || '_FK' || '' ?});
ISTDEFI.index('LP');
ISTDEFI.prefix(ISTDEFS.ref());
ISTDEFI.hdr_sel();
ISTDEFI.hdr_sel(' '+ISTDEFS.OPIS);
ISTDEFI.select()


\edi_d2_struk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsługa akcji Dołącz (pusta dziedzina) okienka W_EDI tabeli ISTDEFS
::   WY: 1 - dlaczono rekord
::       0 -w p.p.
::  OLD: \edi_d2_struk/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
exec('edi_dol_struk','edi_defxml',0)


\edi_dol_atryb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsluga akcji Dolacz (niepusta dziedzina) okienka W_EDI tabeli ISTDEFI
::   WE: _a - brak argumentu Dolacz; = 0 - pusta dziedzina; = 1 - Dolacz|
::   WY: 1 - dlaczono rekord
::       0 -w p.p.
::  OLD: \edi_dol_atryb/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
_nr:={? _=0  || ISTDEFI.LP
     |? _a=1 || ISTDEFI.LP+1
     || 1
     ?};
_r:=1;
ISTDEFI.blank(1);
ISTDEFI.ISTDEFS:=ISTDEFS.ref();
ISTDEFI.LP:=_nr;
ISTDEFI.win_edit('R_EDI');
{? ISTDEFI.edit("exec('kontr_acr','edi_defxml','D')")
|| {? _=0 || _r:=exec('reNrAdd','#table','ISTDEFI','LP') ?};
   {? _r || ISTDEFI.add() ?}
?}


\kontr_acr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Formula sprawdza unikalnosc akronimow pol tabel tymczasowych wypelnianych
::       w definicji EDI
::   WE: _a - znacznik czy sprawdzanie na dolacz czy na popraw
::            'D' - Dolacz
::            'P' - Popraw
::   WY: 'NAZFLD' - jezeli wystepuje juz akronim pola z redagowanego rekordu
::       '' - w p.p.
::  OLD: \kontr_acr/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
_rr:=__CHK.record(ISTDEFI,,'WYM',{?'EOM'*ISTDEFI.WYM||'NAZFLD'||''?});
{? _rr=''
|| _i_ref:=ISTDEFI.ref();
   _i_defs:=ISTDEFI.ISTDEFS().OPIS;
   ISTDEFI.cntx_psh();
   ISTDEFI.index('AKRONIM');
   ISTDEFI.prefix(ISTDEFI.ISTDEFS().ISTDEF,ISTDEFS.NAZTAB,ISTDEFI.NAZFLD,ISTDEFI.NAZFLD,);
   {? _rr='' & (ISTDEFI.find_key('E') | ISTDEFI.find_key('O') | ISTDEFI.find_key('M'))
    & (_a='D' | _i_ref<>ISTDEFI.ref())
   || FUN.info('Występuje już pole o akronimie: %1'
         '\nw zdefiniowanej tabeli tymczasowej %2.'@[ISTDEFI.NAZFLD,ISTDEFS.NAZTAB]);
      _rr:='NAZFLD'
   ?};
   ISTDEFI.index('NAZ');
   ISTDEFI.prefix(ISTDEFI.ISTDEFS().ISTDEF,ISTDEFI.NAZ);
   {? _rr='' & ISTDEFI.first()
   || {!|?
         {? 'MOETA'*ISTDEFI.WYM & _i_defs=ISTDEFI.ISTDEFS().OPIS
             & (_a='D' | _i_ref<>ISTDEFI.ref())
         || FUN.info('Występuje już pole o tagu: %1.'@[ISTDEFI.NAZ]);
            _rr:='NAZ'
         ?};
         _rr='' & ISTDEFI.next()
      !}
   ?};
   ISTDEFI.cntx_pop();
   {? _rr='' & ISTDEFS.NAZTAB<>'' & ISTDEFI.NAZFLD<>''
   || {? ISTDEFI.TYP<>''
      || {? ~( ISTDEFI.TYP='DATE' | ISTDEFI.TYP='TIME' | ISTDEFI.TYP='INTEGER' | ISTDEFI.TYP='REAL'
               | ( (7+ISTDEFI.TYP)='STRING[' & (ISTDEFI.TYP+1)=']' )
             )
         || FUN.info('Nieprawidłowo wypełnione pole: Typ wartości atr. (TYP).'
              '\n\n Dostępne typy: DATE, TIME, INTEGER, REAL, STRING[..]'
              '\n gdzie ".." liczba znaków napisu.'@);
            _rr:='TYP'
         ?}
      || FUN.info('Niewypełnione pole: Typ wartości atr. (TYP).'@);
         _rr:='TYP'
      ?}
   ?}
?};
_rr


\edi_pop_atryb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsluga akcji Popraw (niepusta dziedzina) okienka W_EDI tabeli ISTDEFI
::   WY: 1 - poprawiono rekord
::       0 - w p.p.
::  OLD: \edi_pop_atryb/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
{? ISTDEFI.edit("exec('kontr_acr','edi_defxml','P')")
|| ISTDEFI.put()
?}


\edi_usu_atryb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsluga akcji Usun okienka W_EDI tabeli ISTDEFI
::  OLD: \edi_usu_atryb/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć bieżący wiersz?'@)
|| _lp:=ISTDEFI.LP;
   {? ISTDEFI.del()
   || {? _lp<ISTDEFI.LP || exec('reNrDel','#table','ISTDEFI','LP') ?}
   ?}
?}


\edi_d1_atryb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsluga akcji Dołącz| okienka WER tabeli ISTDEFI
::   WY: 1 - dlaczono rekord
::       0 -w p.p.
::  OLD: \edi_d1_atryb/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
exec('edi_dol_atryb','edi_defxml',1)


\edi_d2_atryb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2006]
:: OPIS: Obsluga akcji Dołącz (pusta dziedzina) okienka W_EDI tabeli ISTDEFI
::   WY: 1 - dlaczono rekord
::       0 -w p.p.
::  OLD: \edi_d2_atryb/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
exec('edi_dol_atryb','edi_defxml',0)


\edi_szuk_defin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: Przed Szukaj okna W_EDI tabeli ISTDEF
::  OLD: \edi_szuk_defin/skid_edi.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.win_edit('EDI_SZUK'); ~~


\edi_weryfikuj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Weryfikacja definicji komunikatu edi
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_file:='@!Tmp/pth_hash.log';
_out:=fopen(_file,'uw',0);
{? _out
||
   exec('xml_path','edi_wspolne',ISTDEF.ref(),_out);
   fclose(_out);
   txt_view(_file,0);
   ferase(_file,0)
?}


\usun_strukture
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Usuwa strukturę definicji
::   WE: _a - ISTDEF.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('def_del','edi_defxml',_a,null())


\def_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: usuwa definicję
::   WE: _a - ISTDEF.ref()
::       _b - ISTDEFS.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_istdef:=_a;
_istdefs:=_b;
ISTDEFS.cntx_psh();
ISTDEFS.index('DRZEWO');
ISTDEFS.prefix(_istdef,#_istdefs);
_loop:=ISTDEFS.first();
{!
|? _loop
|!
   exec('def_del','edi_defxml',_istdef,ISTDEFS.ref());
   ISTDEFI.cntx_psh();
   ISTDEFI.index('LP');
   ISTDEFI.prefix(ISTDEFS.ref());
   _loop:=ISTDEFI.first();
   {!
   |? _loop
   |!
      _loop:=ISTDEFI.del()
   !};
   ISTDEFI.cntx_pop();
   _loop:=ISTDEFS.del()
!};
ISTDEFS.cntx_pop();
~~


\renumistd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: renumeracja ISTDEFS
::   WE: [_a] - ''(domyślnie) - drag&drop, 'U','D','N'-akcja do przenumerowania
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (';UDN'*_a)>1 || _a || '' ?};

ISTDEFS.cntx_psh();
{? _tryb=''
|| _ref:=dnd_info('dest_record');
   {? ISTDEFS.seek(_ref) || exec('zmien_lp','#dragdrop','LP','LP') ?}
|| exec('zmien_lpa','#dragdrop','LP','LP',,,_tryb)
?};
ISTDEFS.cntx_pop()


\istdef_rekb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Rekord przed tabeli ISTDEF
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
||
   _wer:=ISTDEF.win_sel('?');
   _grayed_actions:='';
   {? ISTDEF.sel_size()=0 & ISTDEF.FORMAT<>'N' || _grayed_actions:='W' ?};
   ISTDEF.actions_grayed(_wer,_grayed_actions)
?};
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 71c586d85e9088962be288add751374d18e817170fa641bf1c4fce65b85d399f686bfc04d09aafd4210a32f608f7320ef3bdfc17aca174e0554546de96a45c8f342aae3ca6ccd0e0da4617b31baed894f0178f9b29e566012e3defc26c10752d08aab8257889f2b7424a1ca5c9bf92d9325d2b56109fb54674e58409af77fdfd

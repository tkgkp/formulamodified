:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: faktury_poz.fml
:: Utworzony: 27.03.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa dokumentów sprzedaży i zakupu - pozycje dokumentów
::======================================================================================================================


\buffer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.42]
:: OPIS: Zwraca obiekt nazwany - bufor tabeli FAP
::   WY: obj_new()
::----------------------------------------------------------------------------------------------------------------------
exec('FAP','buffer')


\wysw_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: ustawia zmienne FKOR dla biezacej pozycji korekty
::       !!! UWAGA - formula wywolywana w wielu miejscach systemu oraz w wydrukach !!!
::   WY: zwraca kod koloru dla rekordu
::  OLD: \wysw_kor/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
SLO.cntx_psh;
DISP.RABO:={? exec('czyrabp','ceny',TAZ.RAB_TYP) || '%' || FAKS.WAL().KOD ?};
DISP.RABZTYP:=exec('rabztyp','ceny',TAZ.RAB_TYP);
SLO.cntx_pop;
POMOC.RODZ:={? FAP.M<>null || FAP.M().RODZ || '' ?};
_old:="{? _a<>_b & _b<>''
        || FAKS.use(_b);
           FAP.use('fakpo'+(_b+3));
           FAKSPL.use('fakpl'+(_b+3))
        ?}";
_new:="{? _a<>_b
        || FAKS.use('faktu'+ST.ODDZ+(2-$ST.AR));
           FAP.use('fakpo'+ST.ODDZ+(2-$ST.AR));
           FAKSPL.use('fakpl'+ST.ODDZ+(2-$ST.AR))
        ?}";

FAP.cntx_psh();
FAKS.cntx_psh();
FAKSPL.cntx_psh();

FAP.FAKS();
DISP.RAB:=FAKS.RAB+FAP.RAB1;
exec('d_rab_bd','ceny','FAP',1);

_lksql:=FAKS.LKSQL;
_krs:=FAKS.KRS;
_poz:=FAP.POZ;

_ost_war:=obj_new(25); {! _ii:=1..25 |! _ost_war[_ii]:=0 !};
_przelic:=obj_new(4);  {! _ii:=1..4 |! _przelic[_ii]:=0 !};

_ost_war[1]:=FAP.IL;
_ost_war[2]:=FAP.CWAL;
_ost_war[3]:=FAP.CPR;
_ost_war[4]:=FAP.CN;
_ost_war[5]:=FAP.CB;
_ost_war[6]:=FAP.WN;
_ost_war[7]:=FAP.WV;
_ost_war[8]:=FAP.WB;

_ost_war[10]:=FAP.WWAL;
_ost_war[11]:=FAP.VWAL;
_ost_war[12]:=FAP.BWAL;

_ost_war[15]:=FAP.WWAL_P;
_ost_war[16]:=FAP.VWAL_P;
_ost_war[17]:=FAP.BWAL_P;

:: intrastat
_ost_war[18]:=FAP.MASAN;
_ost_war[19]:=FAP.ILUJM;
_ost_war[20]:=FAP.WF;
_ost_war[21]:=FAP.WS;
:: przeliczniki
_ost_war[22]:=FAP.IL2;
_ost_war[23]:=FAP.J2;
_ost_war[24]:=FAP.T2;
_ost_war[25]:=FAP.WS2;

:: wartości przed korektą
_il:=0;

_cwal:=0;
_cpr:=0;
_cn:=0;
_cb:=0;

_wn:=0;
_wv:=0;
_wb:=0;

_wwal:=0;
_vwal:=0;
_bwal:=0;

_wwal_p:=0;
_vwal_p:=0;
_bwal_p:=0;

:: intrastat
_masan:=0;
_ilujm:=0;
_kp:='';
_wf:=0;
_ws:=0;
:: przeliczniki
_przelic[1]:=0;
_przelic[4]:=0;

_fap_krs:=exec('fap_krs_obj','faktury_poz');

_czy_pozp:=1;
_poz_src:=tab_tmp(1
   ,'ONUM'     ,'INTEGER'     ,'ONUM'
   ,'REF'      ,'STRING[16]'  ,'REF'
   ,'POZ'      ,'INTEGER'     ,'POZ'
   ,'CZY_POZP' ,'INTEGER'     ,'CZY_POZP'
   ,'REF_NEXT' ,'STRING[16]'  ,'REF_NEXT');
_loop:=FAKS.ONUM & FAKS.LKSQL<>'';
{!
|? _loop
|!
   {? FAP.POZP<>0 || _czy_pozp:=0 ?};
   {? FAP.POZP<>0 || _poz:=FAP.POZP || _poz:=FAP.POZ ?};

   _poz_src.ONUM:=FAKS.ONUM;
   _poz_src.REF:=FAKS.LKSQL;
   _poz_src.POZ:=_poz;
   _poz_src.CZY_POZP:=_czy_pozp;
   _poz_src.REF_NEXT:=$FAKS.ref();
   _poz_src.add();

   _rk:=ref_num(FAKS.LKSQL);
   _old(FAKS.name(),ref_name(FAKS.LKSQL));
   FAP.index('FAP');
   FAP.prefix(_rk);
   _loop:={? FAP.find_key(_poz) || FAP.FAKS().ONUM>0 || 0 ?}
!};
_loop:=_poz_src.first();
{!
|? _loop
|!
   _rk:=ref_num(_poz_src.REF);
   _old(FAKS.name(),ref_name(_poz_src.REF));
   FAP.index('FAP');
   FAP.prefix(_rk);
   {? FAP.find_key(_poz_src.POZ)
   ||
      FAP.FAKS();

      exec('fap_krs','faktury_poz',_poz_src.REF_NEXT,FAP.POZ,_fap_krs);
      _fapkrs:={? _fap_krs.BKRS || _fap_krs.BKRS || 0 ?};
      _bkrs:=exec('FindAndGet','#table',FAKS,_poz_src.REF_NEXT,,"{? _b || _b || FAKS.BKRS ?}",0,_fapkrs);
      _bnkrs:=exec('FindAndGet','#table',FAKS,_poz_src.REF_NEXT,,"{? _b || _b || FAKS.BNKRS ?}",0,_fapkrs);
      _krs:={? _bkrs || _bkrs |? FAP.D<>date(0,0,0) || FAP.KRS || FAKS.KRS ?};
      _nkrs:={? _bnkrs || _bnkrs |? FAP.D<>date(0,0,0) || FAP.KRS || FAKS.NKRS ?};

      _cwal+=FAP.CWAL;
      _cpr+=FAP.CPR;
      _cn+=FAP.CN;
      _cb+=FAP.CB;

::    pozycja jest przeliczana jeśli jest kurs dla wartości przed korektą
      _liczfak:=_bkrs & _bnkrs;

      {? _poz_src.CZY_POZP=0
      ||
         {? _liczfak
         ||
::          przeliczenie wartości wg kursu
::          przepisanie pozycji przed korektą do FAP
            FAP.IL:=0;
            FAP.CWAL:=_cwal;
::          pozycje dodane przeliczane są po kursie korekty
            exec('liczfak','faktury_wspolne',,,
               ,exec('FindAndGet','#table',FAKS,_poz_src.REF_NEXT,,"FAKS.KRS",0)
               ,exec('FindAndGet','#table',FAKS,_poz_src.REF_NEXT,,"FAKS.NKRS",0)
               ,exec('FindAndGet','#table',FAKS,_poz_src.REF_NEXT,,"FAKS.T",null()));
            _cpr:=FAP.CPR;
            _cn:=FAP.CN;
            _cb:=FAP.CB
         ?}
      ||
         _il+=FAP.IL;
::       przeliczniki
         _przelic[1]+=FAP.IL2;
         _przelic[4]+=FAP.WS2;

::       co z wartościami do intrastat przy zmianie kursu?
::       intrastat
         _masan+=FAP.MASAN;
         _ilujm+=FAP.ILUJM;
         _wf+=FAP.WF;
         _ws+=FAP.WS;
         _kp:=FAP.KP;

         {? _liczfak
         ||
::          przeliczenie wartości wg kursu
::          przepisanie pozycji przed korektą do FAP
            FAP.IL:=_il;
            FAP.CWAL:=_cwal;
            exec('liczfak','faktury_wspolne',,,,_krs,_nkrs
               ,exec('FindAndGet','#table',FAKS,_poz_src.REF_NEXT,,"FAKS.T",null()));
::          przepisanie wartości z FAP jako przed korektą
            _cpr:=FAP.CPR;
            _cn:=FAP.CN;
            _cb:=FAP.CB;

            _wn:=FAP.WN;
            _wv:=FAP.WV;
            _wb:=FAP.WB;

            _wwal:=FAP.WWAL;
            _vwal:=FAP.VWAL;
            _bwal:=FAP.BWAL;

            _wwal_p:=FAP.WWAL_P;
            _vwal_p:=FAP.VWAL_P;
            _bwal_p:=FAP.BWAL_P
         ||
::          sumowanie wartości zapisanych w tabeli
            _wn+=FAP.WN;
            _wv+=FAP.WV;
            _wb+=FAP.WB;

            _wwal+=FAP.WWAL;
            _vwal+=FAP.VWAL;
            _bwal+=FAP.BWAL;

            _wwal_p+=FAP.WWAL_P;
            _vwal_p+=FAP.VWAL_P;
            _bwal_p+=FAP.BWAL_P
         ?}
      ?};
      _loop:=_poz_src.next()
   ||
      _loop:=0
   ?}
!};
_old(FAKS.name,form(8+_lksql));
FAP.index('FAP');
FAP.prefix(BB.sqlint(_lksql));
{? _lksql<>'' & FAP.find_key({? _poz_src.get() || _poz_src.POZ || _poz ?})
||
   _dokl_c:={? FAP.FAKS().SZ='Z' || FAP.M().DOKL_C || 2 ?};
   TAZ.RAB_TYP:=FAP.FAKS().RAB_TYP;
   FKOR.WAL:=FAP.WAL;
   FKOR.KRS:=_krs;
   FKOR.SV:=FAP.SV;
   FKOR.OO:=FAP.OO;
   {? exec('sp_active','faktury_wspolne',FAKS.DW) || FKOR.SP_WYM:=FAP.SP_WYM ?};
   {? FAKS.PROC=null() | FAKS.KRAJ_VAT().KODISO='' | FAKS.KRAJ_VAT().KODISO='PL'
   || FKOR.EF_PRVAT:=FAP.EF_PRVAT
   || FKOR.EF_PRVAT:=null()
   ?};
   {? FAKS.PROC=null()
   || FKOR.EF_PROC:=FAP.EF_PROC
   || FKOR.EF_PROC:=FAKS.PROC
   ?};
   FKOR.IL:=_il;

   FKOR.CWAL:=_cwal;
   FKOR.CPR:=_cpr;
   FKOR.CN:=_cn;
   FKOR.CB:=_cb;

   _rab:=FAP.FAKS().RAB+FAP.RAB1;
   FKOR.FRAB:={? exec('czyrabp','ceny',TAZ.RAB_TYP)
              || _rab
              || ({? FAKS.WAL<>FAKS.NWAL || FKOR.CWAL || FKOR.CPR ?}*_rab/100)$_dokl_c
              ?};
   FKOR.RAB:={? exec('czyrabp','ceny',TAZ.RAB_TYP) || FAP.RAB || FAP.RABK ?};

   FKOR.WN:=_wn;
   FKOR.WV:=_wv;
   FKOR.WB:=_wb;

   FKOR.WWAL:=_wwal;
   FKOR.VWAL:=_vwal;
   FKOR.BWAL:=_bwal;

   FKOR.WWAL_P:=_wwal_p;
   FKOR.VWAL_P:=_vwal_p;
   FKOR.BWAL_P:=_bwal_p;

:: intrastat
   FKOR.PMASAN:=_masan;
   FKOR.PILUJM:=_ilujm;
   FKOR.KP:=_kp;
   FKOR.PWF:=_wf;
   FKOR.PWS:=_ws;
:: przeliczniki
   FKOR.IL2:=_przelic[1];
   FKOR.WS2:=_przelic[4]
||
:: przypadek kiedy funkcja uruchamiana dla pozycji faktury a nie korekty
   FKOR.WAL:=null;
   FKOR.CWAL:=0;
   FKOR.KRS:=0;

   FKOR.IL:=0;
   FKOR.CPR:=0;
   FKOR.RAB:=0;
   FKOR.CN:=0;
   FKOR.CB:=0;
   FKOR.WN:=0;
   FKOR.SV:=0;
   FKOR.OO:='';
   {? exec('sp_active','faktury_wspolne',FAKS.DW) || FKOR.SP_WYM:='N' ?};
   FKOR.WV:=0;
   FKOR.WB:=0;

   FKOR.WWAL:=0;
   FKOR.VWAL:=0;
   FKOR.BWAL:=0;

   FKOR.WWAL_P:=0;
   FKOR.VWAL_P:=0;
   FKOR.BWAL_P:=0;

:: intrastat
   FKOR.PMASAN:=0;
   FKOR.PILUJM:=0;
   FKOR.PWF:=0;
   FKOR.PWS:=0;
:: przeliczniki
   FKOR.IL2:=0;
   FKOR.WS2:=0
?};

FKOR.P:='PO KOREKCIE';
FKOR.BIL:=FKOR.IL+_ost_war[1];
FKOR.BCWAL:=FKOR.CWAL+_ost_war[2];
FKOR.BCPR:=FKOR.CPR+_ost_war[3];
FKOR.BCN:=FKOR.CN+_ost_war[4];
FKOR.BCB:=FKOR.CB+_ost_war[5];

FKOR.BWN:=FKOR.WN+_ost_war[6];
FKOR.BWV:=FKOR.WV+_ost_war[7];
FKOR.BWB:=FKOR.WB+_ost_war[8];

FKOR.BWWAL:=FKOR.WWAL+_ost_war[10];
FKOR.BVWAL:=FKOR.VWAL+_ost_war[11];
FKOR.BBWAL:=FKOR.BWAL+_ost_war[12];

FKOR.BWWAL_P:=FKOR.WWAL_P+_ost_war[15];
FKOR.BVWAL_P:=FKOR.VWAL_P+_ost_war[16];
FKOR.BBWAL_P:=FKOR.BWAL_P+_ost_war[17];

:: intrastat
FKOR.BMASAN:=_masan+_ost_war[18];
FKOR.BILUJM:=_ilujm+_ost_war[19];
FKOR.BWF:=_wf+_ost_war[20];
FKOR.BWS:=_ws+_ost_war[21];
:: przeliczniki
FKOR.BIL2:=_przelic[1]+_ost_war[22];
FKOR.BWS2:=_przelic[4]+_ost_war[25];

obj_del(_ost_war); obj_del(_przelic);
_new(FAKS.name,form(8+_lksql));

FAKS.cntx_pop();
FAP.cntx_pop();
FAKSPL.cntx_pop();

''


\faks_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MAKO [2010]
:: OPIS: Wartość początkowa pola FAP.FAKS, FAKS_K.FAKS
::  OLD: \fak/umowy.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.ref()


\po_pozkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji pola FAP.POZ dla korekty historycznej
::   WY: 1-jest OK 0-pozycja powielona lub mniejsza od 1
::  OLD: \po_pozkh/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_poz:=FAP.POZ;
_ref:=FAP.ref;
_wyn:=1;
{? _poz>0
|| FAP.cntx_psh;
   FAP.index('FAP');
   FAP.prefix(FAKS.ref,_poz);
   {? FAP.first & _ref<>FAP.ref
   || FUN.info('Na fakturze istnieje już pozycja o numerze: %1.'@[form(_poz,,0,'99')]);
      _wyn:=0
   ?};
   FAP.cntx_pop
|| FUN.info('Nieprawidłowy numer pozycji.\nNależy podać numer większy lub równy 1.'@);
   _wyn:=0
?};
{? ~_wyn || FAP.POZ:=__hispoz ?};
_wyn


\pw_fap_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed wyświetleniem pól do redakcji na pozycji faktury
::   WY: kolor pola
::  OLD: \pw_fap_m/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
FAKS.cntx_psh();
{? FAP.M<>null
|| POMOC.RODZ:=FAP.M().RODZ;
   {? 1+(-menu_txt)<>'d'
   || POMOC.MGR:=POMOC.MGRF:=FAP.M().MGR
   ?}
?};
{? FAP.FAKS().AKC='N' & FAP.FAKS().T().KOR='N' & FAP.FAKS().WHERE='M' & POMOC.RODZ='T' & HELP.MOD_POZY=0
|| _wyn:=exec('findfnrd','color')
?};
FAKS.cntx_pop();
_wyn


\be_fap_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcją materiału na pozycji faktury
::   WY: 1-redakcja możliwa, 0-wpp
::  OLD: \be_fap_m/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAP.FAKS().WHERE='M' & FAP.M().RODZ='T' & HELP.MOD_POZY=0
|| 0
|| HELP.MOD_IND:=fld();
   ~HELP.MOD_POZY
?}


\ae_fap_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: po redakcji materiału na pozycjach faktury, FAP.M
::   WY: 1/0
::  OLD: \ae_fap_m/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
ATR.MJS:='FAP';
_wyn:=0;
{? fld()=0 & FAKS.WHERE<>'L'
|| FUN.info('Niewypełnione pole.'@)
|| _wyn:=1;
   {? FAP.SV=null | fld()<>HELP.MOD_IND
   || FAP.SV:=exec('m_vat','material',FAP.M,FAP.FAKS().KH,
         FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N',FAKS.AR,FAKS.AM,,,,FAKS.SZ);
      FAP.JM:=FAP.M().J
   ?};
   {? FAKS.SZ='S' & exec('get','#params',300200,2)='T' & FAP.M<>HELP.MOD_IND
   || FAP.KK:=exec('m_kk','material',FAP.M)
   ?};
   {? FAP.M<>HELP.MOD_IND
   || Plugin.run('POZLOG_M_001','FAP')
   ?};
   exec('fill_fld2prn','faktury_poz')
?};
_wyn


\be_fapil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: przed redakcją ilości w pozycji dokumentu sprzedaży/zakupu
::   WY: 1-redakcja możliwa, 0-wpp
::  OLD: \be_fapil/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
:: zapamiętanie wartości pola przed redakcją
{? FAP.FAKS().T().KOR='N' & FAP.FAKS().WHERE='M' & FAP.M().RODZ='T' & HELP.MOD_POZY=0
|| 0
|? FAP.FAKS().T().KOR='T' & FAP.FAKS().WGZWR
|| 0
|| {? ~HELP.MOD_POZY & FAKS.T().HIS='T' & FAKS.T().KOR='T' || exec('przepwar','faktury_poz') ?};
   DPOZ.WPR_R:=FAP.IL;
   ~HELP.MOD_POZY
?}


\przepwar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: podpowiada wartości winno być na podstawie było
::  OLD: \przepwar/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? FKOR.PODP
|| FAP.IL:=FKOR.IL;
   FAP.WAL:=FKOR.WAL;
   FAP.SV:=FKOR.SV;
   FAP.RAB:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || FKOR.RAB || 0 ?};
   FAP.RABK:={? ~exec('czyrabp','ceny',FAKS.RAB_TYP) || FKOR.RAB || 0 ?};
   FAP.CWAL:=FKOR.CWAL;
   FAP.CPR:=FKOR.CPR;
   exec('liczfak','faktury_wspolne');
   exec('liczfak_ist','faktury_wspolne',1,{? cur_afld()='CPR' || 0 || 1 ?});
   FKOR.PODP:=0;
   win_disp
?}


\pr_jmfap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: przed redakcją pola FAP.JM ustawia okienko słownika
::  OLD: \pr_jmfap/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_R:=fld;
{? (FAP.M=null & FAP.FAKS().WHERE<>'L') | FAP.T2='G'
|| 0
|? FAP.FAKS().WHERE='G' & FAP.M().J2<>null() & 'J2 JM'*cur_afld()
|| 0
|? FAP.FAKS().WHERE='G' & ((1+BEER.FMJM)<>'T' | FAP.IL<>0)
|| exec('po_jmfap','faktury_poz');
   0
|? FAP.FAKS().WHERE='M' & FAP.M().J2<>null() & cur_afld()='JM'
|| 0
|? FAP.FAKS().WHERE='P'
 | ((FAP.FAKS().WHERE='G' | (FAP.FAKS().WHERE='M' & FAP.T2<>'G' & FAP.M().RODZ='T')) & (1+BEER.FMJM)='T')
 | FAP.FAKS().SZ='Z' & BEER.JMZ
|| exec('ustafprz','faktury_wspolne',FAP.M,FAP.M().J);
   {? JM.f_active() || JM.f_first() & JM.f_next() || JM.first() & JM.next() ?}
:: Jednostka miary
|? BEER.SZ='Z' & FAP.M
|| 0
|? (';SZ'*BEER.SZ)>1 & FAKS.WHERE='L' & FAP.M=null()
|| exec('ustafprz','faktury_wspolne',FAP.M,FAP.M().J);
   1
|| JM.win_dict('WER');
   1
?}


\po_jmfap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: po redakcji pola FAP.JM
::  OLD: \po_jmfap/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAP.M().J2<>null() & FAP.JM=FAP.M().J2
|| FAP.J2:=FAP.M().J;
   {? FAP.FAKS().JMZ
   || _ws2:=exec('oblWSP','jm',FAP.M);
      FAP.WS2:={? _ws2>0 || 1/_ws2 || _ws2 ?}
   || FAP.WS2:={? FAP.WS2>0 || 1/FAP.WS2 || FAP.WS2 ?}
   ?}
|? FAP.JM=null()
 | FAP.J2=null()
 | FAP.M=null()
 | FAP.M & FAP.JM=FAP.M().J & FAP.J2=FAP.JM
 | FAP.T2='N'
|| {? FAP.FAKS().WHERE<>'P' & FAP.FAKS().WHERE<>'L' || FAP.J2:=FAP.JM:={? FAP.M || FAP.M().J || FAP.JM ?} ?};
   FAP.WS2:=1;
   {? FAP.FAKS().WHERE='M' || FAP.IL:=FAP.IL2 || FAP.IL2:=FAP.IL ?}
|| _jmz:=FAP.FAKS().JMZ;
   {? _jmz
   || {? FAP.JM<>FAP.M().J
      || FAP.J2:=FAP.M().J;
         FAP.WS2:=exec('prz','jm',FAP.M,FAP.J2,FAP.JM)
      |? FAP.M().J2<>null()
      || FAP.WS2:=exec('oblWSP','jm',FAP.M)
      |? FAP.J2=FAP.JM & FAP.T2<>'N'
      || FAP.J2:=null();
         exec('ustafprz','faktury_wspolne',FAP.M,FAP.JM)
      ?}
   ?};
   _jm2:=FAP.J2;
   _prz:=FAP.WS2;
   M.cntx_psh;
   JM.cntx_psh;
   _mat:=FAP.M;
   _jm:=FAP.JM;
   BEER.KJM:=FAP.JM().KOD;
   JM.fld_opt('WERF','col_name="= Ilość [%1]"'[BEER.KJM],BEER,'IL2');
   _ile:=FAP.IL2;

   BEER.PRZ:={? FAP.FAKS().WHERE='P' | (FAP.FAKS().SZ='Z' & _jmz)
             || exec('prz','jm',_mat,_jm2,_jm)
             || exec('prz','jm',_mat,_jm,_jm2)
             ?};
   BEER.IL2:={? FAP.FAKS().WHERE='M' || FAP.IL2 || _ile*BEER.PRZ $ exec('jaka_dok_m','jm',_mat) ?};
   JM.cntx_pop;
   M.cntx_pop;
   {? ~BEER.PRZ & FAP.T2='A' & FAP.FAKS().WHERE<>'M'
   || FAP.J2:=FAP.JM:={? FAP.M || FAP.M().J || FAP.JM ?};
      FAP.WS2:=1;
      FAP.IL2:=FAP.IL
   |? FAP.FAKS().WHERE='M' | (FAP.FAKS().WHERE='G' & FAP.IL<>0)
   || _dokl_s:=exec('jaka_dok_mjm','jm',FAP.M,FAP.J2,FAP.JM);
      {? FAP.T2<>'A' & _jm<>_jm2 & FAP.JM=_jm & FAP.WS2<>BEER.PRZ & FAP.WS2<>1
      || BEER.PRZ:=FAP.WS2;
         BEER.IL2:=_ile
      || FAP.WS2:={? ~BEER.PRZ || 1 || BEER.PRZ ?};
         FAP.IL:={? FAP.WS2<>1 || FAP.IL2/FAP.WS2 $ _dokl_s || FAP.IL2 ?}
      ?}
   |? ((FAP.FAKS().WHERE='G' & FAP.IL2=0)
      | ((FAP.FAKS().WHERE='P' | FAP.FAKS().WHERE='L' | (FAP.FAKS().SZ='Z' & _jmz)) & (FAP.T2='A' | (FAP.T2='R' & FAP.WS2=1))))
    & (FAP.T2='A'
       | (BEER.PRZ<>0 & FAP.WS2<>BEER.PRZ
        & FUN.ask('Obliczony współczynnik przeliczenia jest różny od wprowadzonego.\n'
                  'Czy zmienić współczynnik przeliczenia?'@)))
   || FAP.WS2:=BEER.PRZ
   ?}
?};
exec('aktfil2','faktury_wspolne');
{? DPOZ.WPR_R<>fld || exec('clr_promo_tap','ceny'); win_disp ?};
1


\be_fapcp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja FAP.CPR
::  OLD: \be_fapcp/faktury.fml
::   WE: [_a]  - 1-portal 0-nie (domyślnie)
::       [_b]  - 'FAKS'/'FAP' - z jakiego poziomu wywoływana jest formuła
::       [_c]  - 0/1/2 - wartośc parametru CZY_SEL
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? _>=2 || {? type_of(_b)=type_of('') & _b='FAKS' || _b:='A' ?} || _b:='F' ?};
{? _>=3 || {? type_of(_c)<>type_of(1) || _c:=0 ?} || _c:=0 ?};
_wyn:=
   {? exec('upr_cscz','ceny') & (FAP.M<>null | (FAKS.WHERE='L' & form(FAP.NX)<>''))
   || DPOZ.WPR_R:=FAP.CPR;
      exec('czy_rwal','eurusd',_b) & exec('fap_bcpr','faktury_poz')
   || 0
   ?};
{? _wyn & ((_b='F' & FAP.FAKS().T().KOR<>'T' | _b='F' & FAP.FAKS().KOR<>'' & FAP.POZP)
|(_b='A' & FAKS.T().KOR<>'T' | _b='A' & FAKS.KOR<>'' & FAP.POZP))
|| exec('taz_sd_set','ceny');
   {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?} & FAP.CPR=0
   || exec('be_fapcpr','faktury_poz', _a, _b, _c);
      {? FAP.CPR || {? _wyn:=exec('licz','ceny',FAP) || win_disp ?} ?}
   ?}
?};
_wyn


\fap_bcpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed redakcją ceny przed rabatem na pozycji faktury
::   WY: czy można redagować pole
::  OLD: \fap_bcpr/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
KSTE.first();
M.cntx_psh();
M.prefix();
{? M.seek(FAP.M)
|| {? FAP.CPR=0
   || {? M.VAT=null
      || FUN.info('Niewypełniona stawka VAT dla materiału: %1.'@[M.N]); _wyn:=0
      ?}
   ?};
   {? FAP.SV=null
   || FAP.SV:=exec('m_vat','material',M.ref(),FAKS.KH,
         FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N',FAKS.AR,FAKS.AM,,,,FAKS.SZ)
   ?}
|| {? FAKS.WHERE<>'L'
   || FUN.info({? POMOC.RODZ='U' || 'Nie znaleziono usługi.'@ || 'Nie znaleziono materiału.'@ ?}); _wyn:=0
   ?}
?};
M.cntx_pop();
_wyn


\be_fapcpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: przed redakcją FAP.CPR
::  OLD: \be_fapcpr/ceny.fml
::   WE: [_a]  - 1-portal 0-nie (domyślnie)
::       [_b]  - 'FAKS'/'FAP' - z jakiego poziomu wywoływana jest formuła
::       [_c]  - 0/1/2 - wartośc parametru CZY_SEL
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? _>=2 || {? type_of(_b)=type_of('') & _b<>'A' || FAP.FAKS() ?} || FAP.FAKS() ?};
{? _>=3 || {? type_of(_c)<>type_of(1) || _c:=0 ?} || _c:=0 ?};
exec('st_licz_wz','ceny','FAKS');
exec('stplicz','ceny',FAP.IL);
{? FAP.T2<>'G'
|| KALK.JM:=KALK.J2:=FAP.JM; KALK.WS2:=1
|| KALK.JM:=FAP.J2; KALK.J2:=FAP.JM; KALK.WS2:=FAP.WS2
?};
_tms:={? FAKS.D<>date(0,0,0) & exec('get','#params',300807)='N'
      || tm_stamp(FAKS.D~1,FAKS.D~2,FAKS.D~3)
      || FIRMA.tm_stamp()
      ?};

exec('ceny_mat','ceny_mat',FAP.M,FAKS.KH,'FAP.CPR',FAKS.KH_ODB,,'FAP.RAB','N',FAKS.NWAL,'',,
  {? FAKS.SZ='S' || FAKS.T().FIS='T' || FAKS.CB='T' ?},
  {? FAP.JM<>null || FAP.JM().KOD || '' ?},
  {? FAP.J2<>null & FAP.WS2<>0 || {? FAKS.WHERE='G' || 1/FAP.WS2 || FAP.WS2 ?} || 0 ?},_c,,BEER.SZ='S'
  ,,FAP.SV,,_a,FAKS.PL,_tms);
1


\f3_fapcpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: klawisz F3 dla FAP.CPR
::  OLD: \f3_fapcpr/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
exec('st_licz_wz','ceny','FAKS');
exec('stplicz','ceny',FAP.IL);
{? FAP.T2<>'G'
|| KALK.JM:=KALK.J2:=FAP.JM; KALK.WS2:=1
|| KALK.JM:=FAP.J2; KALK.J2:=FAP.JM; KALK.WS2:=FAP.WS2
?};
exec('ceny_mat','ceny_mat',FAP.M,FAP.FAKS().KH,'FAP.CPR',FAKS.KH_ODB,,'FAP.RAB','N',FAKS.NWAL,'',,FAKS.T().FIS='T',
  {? FAP.JM<>null || FAP.JM().KOD || '' ?},
  {? FAP.J2<>null & FAP.WS2<>0 || {? FAKS.WHERE='G' || 1/FAP.WS2 || FAP.WS2 ?} || 0 ?},1,,
  FAKS.KOR='' | FAP.POZP,,FAP.SV,,,FAKS.PL);
1


\fapf3cpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: f3 dla ceny przed rabatem na pozycji faktury
::  OLD: \fapf3cpr/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
KALK.WHERE:='F';
{? BEER.SZ='S'
|| exec('f3_fapcpr','faktury_poz')
|| exec('f3_zdcen','ceny_mat',FAP.M,FAKS.KH,FAP.CPR,,'FAP')
?};
KALK.WHERE:='';
''


\redwarvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: czy redagować wartości VAT
::  OLD: \redwarvat/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
|| {? cur_afld='WWAL' | cur_afld='VWAL' | cur_afld='BWAL'
      | cur_afld='WN' | cur_afld='WV' | cur_afld='WB'
   || exec('spr_npoz','eurusd')
   || 1
   ?}
?}


\be_fapsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcją FAP.SV
::  OLD: \be_fapsv/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
:: zapamiętanie wartosci pola przed redakcją
DPOZ.WPR_SLO:=FAP.SV;
exec('jakislsv','ustawienia')=''


\ae_fapsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: po redakcji stawki vat w pozycji faktury, FAP.SV
::  OLD: \ae_fapsv/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? fld()<>null
|| {? DPOZ.WPR_SLO<>FAP.SV
   || _wyn:=exec('liczfak','faktury_wspolne')
   || _wyn:=1
   ?};
   {? _wyn=1 & DPOZ.WPR_SLO<>FAP.SV
   ||
::    wariant jeśli zmiana wartości pola
      _wyn:=exec('liczfak_ist','faktury_wspolne')
   ?}
|? FAP.M=null
|| _wyn:=1
?};
exec('set_efld_opt','faktury_poz');
1


\be_fapko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed redakcją pola konto FAP.KK
::   WY: 1/0
::  OLD: \be_fapko/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('get','#params',{? FAP.FAKS().SZ='S' || 300200 || 200200 ?},2)='N'
|| _wyn:=0
|| _wyn:=1
?};
_wyn


\bl_walfp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: wartość początkowa dla pola waluta
::  OLD: \bl_walfp/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.WAL


\pw_fapw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: przed wyświetleniem pól walutowych tabeli FAP, zmiennej FKOR
::   WE: _a-akronim pola
::       _b-sprawdzać FKOR
::  OLD: \pw_fapw/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of(1) || _b:=0 ?};

{? (_a='CWAL' | _a='HAN') & exec('upr_cscz','ceny')=0
|| 0
|| {? exec('spr_ww','eurusd',BEER.WW<>2)
   || {? _b=1 & FAP.POZP
      || 0
      || ''
      ?}
   || ''
   ?}
?}


\befapwce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario & [rr] [2008]
:: OPIS: przed redakcją ceny walutowej przed rabatem na pozycji faktury
::  OLD: \befapwce/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny') & (FAP.M<>null | (FAKS.WHERE='L' & FAP.NX<>''))
|| _wyn:=exec('spr_npoz','eurusd');
:: zapamietanie wartosci pola przed redakcja
   DPOZ.WPR_R:=FAP.CWAL;
   {? _wyn
   || exec('taz_sd_set','ceny');
      {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?} & FAP.CWAL=0
      || exec('be_fapcwal','faktury_poz');
         {? FAP.CWAL || DPOZ.WPR_R:=-1; exec('po_cenaw','ceny',0); DPOZ.WPR_R:=fld ?}
      ?}
   ?}
|| _wyn:=0
?};
_wyn


\be_fapcwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: przed redakcją FAP.CWAL
::  OLD: \be_fapcwal/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
exec('st_licz_wz','ceny','FAKS');
exec('stplicz','ceny',FAP.IL);
{? FAP.T2<>'G'
|| KALK.JM:=KALK.J2:=FAP.JM; KALK.WS2:=1
|| KALK.JM:=FAP.J2; KALK.J2:=FAP.JM; KALK.WS2:=FAP.WS2
?};
_tms:={? FAP.FAKS().D<>date(0,0,0) & exec('get','#params',300807)='N'
      || tm_stamp(FAP.FAKS().D~1,FAP.FAKS().D~2,FAP.FAKS().D~3)
      || FIRMA.tm_stamp()
      ?};
exec('ceny_mat','ceny_mat',FAP.M,FAP.FAKS().KH,'FAP.CWAL',FAKS.KH_ODB,,'FAP.RAB','T',FAP.WAL,'FAP.CWAL',FAP.KRS
  ,{? FAKS.SZ='S' || FAKS.T().FIS='T' || FAKS.CB='T' ?},
  {? FAP.JM<>null || FAP.JM().KOD || '' ?},{? FAP.J2<>null || FAP.WS2 || 0 ?},0,,
  FAKS.KOR='' | FAP.POZP,,FAP.SV,,,FAKS.PL,_tms);
1


\f3fapwce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: F3 dla ceny walutowej przed rabatem na pozycji faktury
::  OLD: \f3fapwce/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
exec('f3_fapcwal','faktury_poz');
''


\f3_fapcwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: klawisz F3 dla FAP.CWAL
::  OLD: \f3_fapcwal/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
exec('st_licz_wz','ceny','FAKS');
exec('stplicz','ceny',FAP.IL);
{? TAZ.SD='S'
||
   {? FAP.T2<>'G'
      || KALK.JM:=KALK.J2:=FAP.JM; KALK.WS2:=1
      || KALK.JM:=FAP.J2; KALK.J2:=FAP.JM; KALK.WS2:=FAP.WS2
      ?};
   exec('ceny_mat','ceny_mat',FAP.M,FAP.FAKS().KH,'FAP.CPR',FAKS.KH_ODB,,'FAP.RAB','T',FAP.WAL,'FAP.CWAL',
      FAP.KRS,{? FAKS.SZ='S' || FAKS.T().FIS='T' || FAKS.CB='T' ?},
      {? FAP.JM<>null || FAP.JM().KOD || '' ?},{? FAP.J2<>null || FAP.WS2 || 0 ?},1,,
      FAKS.KOR='' | FAP.POZP,,FAP.SV,,,FAKS.PL);
      1
||
:: [rr] cena dla faktur zakupowych
   exec('f3_zdcen','ceny_mat',FAP.M,FAKS.KH,FAP.CWAL,,'FAP',FAP.WAL)
?}


\fap_podat_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: po redakcji wartości podatkowych
::  OLD: \fap_podat_po/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('spr_npoz','eurusd')
|| FAP.WWAL:=FAP.WN:=FAP.WWAL_P:=FAP.WWAL_P$2;
   FAP.VWAL:=FAP.WV:=FAP.VWAL_P:=FAP.VWAL_P$2;
   FAP.BWAL:=FAP.WB:=FAP.BWAL_P:=FAP.BWAL_P$2
?};
1


\ae_fpmag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: po edycji magazynu w pozycji dokumentu sprzedaży
::   WY: 1/0
::  OLD: \ae_fpmag/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_wyn:=exec('czy_z_ok','okresy',1,,,,fld);
{? _wyn=1 || exec('czy_z_ok','okresy',2,,,,fld) ?};
{? _wyn=1
|| _wyn:=exec('set_UserUpr','b_perm','MG','N')
?};
_wyn


\fap2del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: usuwa pozycje faktury wraz z tabelami powiazanymi
::       po wywolaniu formuly nalezy usunac VAR_DEL.delete('__msk')
::   WE: _a - 1 parametr dla del
::       [_b] - czy przywracac rezerwacje tymczasowe 1-tak 0-nie (domyslnie)
::       [_c] - KH.KOD
::       [_d] - TR_KOS.uidref()
::   WY: 1/0
::  OLD: \fap2del/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};
{? _>=3 || {? type_of(_c)<>2 || _c:=FAP.FAKS().KH().KOD ?} || _c:=FAP.FAKS().KH().KOD ?};
{? var_pres('_d')=type_of('')
|| _tr_kos:=_d
|| _tr_kos:=exec('pow_tr_kos','transport_koszt',FAP.uidref())
?};

exec('delprzyp','fakso','FAKSO','FAP',FAP.ref);
{? 'GM'*FAP.FAKS().WHERE || exec('nulfapdk','faktury_poz') ?};
{? FAP.count()>0 & (FAP.M().RODZ='T' | FAP.M=null())
|| exec('akt_frez','faktury_poz',0,0,FAP.ref());
   {? _b || exec('returnrt','rezerwacje',$FAP.ref(),form(8+$FAP.ref())+3,_b,FAP.FAKS().KH) ?}
?};
exec('dk_del','magdok_poz',$FAP.ref(),1);
:: usuniecie zwrotow utworzonych przez pozycje faktury za opakowania zwrotne po terminie
_mag_zwr:=exec('opak_zwr_del','opakow_stany',$FAP.ref(),'N');
:: usuniecie rozliczen projektow
exec('projdel','projekty',FAP.ref());
:: kontroling
:: DRO_TODO_AWI_OK______: exec('sch_fap','skid_prm',FAP.ref());
::exec('sch_fap','skid_prm',FAP.ref());
:: usuniecie tabelki wg wymiarow
DK_L.index('FAP');
DK_L.prefix(FAP.ref(),null);
{? DK_L.first() || {! |? DK_L.del() !} ?};
:: usuwanie powiazania do zlecenia fakturowania
exec('fakz2fap_del','faktury_wspolne',FAP.ref);
:: usuwanie powiązania do zgłoszeń fakturownia
exec('fap_us_z','faktury_poz');
exec('delFAP_K','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),null());
_wyn:=FAP.del(_a);
{? _tr_kos<>'' || exec('FindAndGet','#table',TR_KOS,_tr_kos,,"DOK_REF:='';POZ_REF:='';FAK:='T';put(1)",0) ?};
_wyn


\fap_kp_reg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: warunki na wyświetlenie lub redagowanie pola FAP.KP
::   WE: _a - 'PW' - dla funkcji przed wyświetleniem, 'PR' - dla funkcji przed redakcją
::   WY: _a = 'PW' - kolor lub ''
::          = 'PR' - 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fap_kp_reg/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_kolor:=exec('findfnv','#color');
{? exec('faks_kpw_reg','faktury_nag','PR',1) = 0
|| {? _a='PW' || return(_kolor) || return(0) ?}
|| {? exec('czy_kp','intrastat',FAKS.IST_TYP,FAKS.IST_OKR)
         &
      {? FAP.KCN().KOD='' || 0 || exec('rw3','intrastat',FAP.KCN().KOD)=1 ?}
   || {? _a='PW' || return('') || return(1) ?}
   || {? _a='PW' || return(_kolor) || return(0) ?}
   ?}
?}


\fap_kp_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyświetleniem FAP.KP
::   WY: kolor
::  OLD: \fap_kp_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fap_kp_reg','faktury_poz','PW')


\fap_kp_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcją FAP.KP
::   WY: 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fap_kp_pr/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fap_kp_reg','faktury_poz','PR')


\fap_kp_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: klawisz F3 FAP.F3
::  OLD: \fap_kp_f3/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('slownik','intrastat','007',FAP.KP)


\fap_kp_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: po redkacji FAP.KP
::   WY: 1 - wlaściwa wartość pola, 0 - wpp
::  OLD: \fap_kp_po/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld() <> ''
|| ISTDEFSL.index('SL');
   ISTDEFSL.prefix('007','P');
   {? ISTDEFSL.find_key(fld,)
   || 1
   || FUN.info('Brak wartości "%1" w słowniku kraje pochodzenia.'@[fld]);
      0
   ?}
|| 1
?}


\fap_masan_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyświetleniem FAP.MASAN
::   WY: kolor
::  OLD: \fap_masan_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.IST_TYP='' & FAP.MASAN<>0
|| exec('findfnrd','color')
|? exec('fap_masan_pr','faktury_poz')
|| return('')
|| return(exec('findfnv','#color'))
?}


\fap_masan_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcją FAP.MASAN
::   WY: 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fap_masan_pr/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('faks_kpw_reg','faktury_nag','PR',1)
||
   _kodt:=FAP.KCN().KOD;
   _kodt=''
      |
   _kodt<>'99500000'
      &
   _kodt<>'' & exec('find_sl','intrastat','188',_kodt)=0
?}


\fap_ilujm_reg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: warunki na wyświetlenie lub redagowanie pola FAP.ILUJM
::   WE: _a - 'PW' - dla funkcji przed wyświetleniem, 'PR' - dla funkcji przed redakcją
::   WY: _a = 'PW' - kolor lub ''
::          = 'PR' - 1 - redakcja dozwolona, 0 - wpp
::----------------------------------------------------------------------------------------------------------------------
_kolor:=exec('findfnv','#color');
{? exec('fap_masan_pr','faktury_poz')=0
|| {? _a = 'PW' || return(_kolor) || return(0) ?}
|| {? FAKS.IST_TYP<>'' & FAP.KCN().JU<>null
   || {? _a = 'PW' || return('') || return(1) ?}
   || {? _a = 'PW' || return(_kolor) || return(0) ?}
   ?}
?}


\fap_ilujm_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FAP.ILUJM
::   WY: kolor
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.IST_TYP='' & FAP.ILUJM<>0
|| exec('findfnrd','color')
|| exec('fap_ilujm_reg','faktury_poz','PW')
?}


\fap_ilujm_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcja FAP.ILUJM
::   WY: 1 - redakcja dozwolona, 0 - wpp
::----------------------------------------------------------------------------------------------------------------------
exec('fap_ilujm_reg','faktury_poz','PR')


\fap_wf_reg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.42]
:: OPIS: warunki na wyswietlenie lub redagowanie pola FAP.WF
::   WE: _a - 'PW' - dla funkcji przed wyswietleniem, 'PR' - dla funkcji przed redakcja
::   WY: _a = 'PW' - kolor lub ''
::          = 'PR' - 1 - redakcja dozwolona, 0 - wpp
::----------------------------------------------------------------------------------------------------------------------
_kolor:={? FAP.WF=0 || exec('findfnv','#color') || exec('findfnrd','color') ?};
_reg:=0;
{? exec('faks_kpw_reg','faktury_nag','PR',1)
||
   _reg:=exec('find_sl','intrastat','188',FAP.KCN().KOD)=0
?};
{? _reg=0
||
   {? _a='PW' || return(_kolor) || return(0) ?}
||
   {? _a='PW' || return('') || return(1) ?}
?}


\fap_wf_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyświetleniem FAP.WF
::   WY: kolor
::  OLD: \fap_wf_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
||
   {? FAKS.IST_TYP='' & FAP.WF<>0
   || exec('findfnrd','color')
   || exec('fap_wf_reg','faktury_poz','PW')
   ?}
?}


\fap_wf_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcją FAP.WF
::   WY: 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fap_wf_pr/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
||
   exec('fap_wf_reg','faktury_poz','PR')
?}


\fap_ws_reg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: warunki na wyświetlenie lub redagowanie pola FAP.WS
::   WE: _a - 'PW' - dla funkcji przed wyświetleniem, 'PR' - dla funkcji przed redakcją
::   WY: _a = 'PW' - kolor lub ''
::          = 'PR' - 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fap_ws_reg/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_kolor:={? FAP.WS=0 || exec('findfnv','#color') || exec('findfnrd','color') ?};
{? exec('faks_wd_reg','faktury_nag','PR',1) = 0
||
   {? FAKS.IST_TYP<>''
         &
      exec('find_sl','intrastat','189',FAP.KCN().KOD)=0
         &
      (
         exec('find_sl','intrastat','190',FAP.KCN().KOD)
            |
         exec('find_sl','intrastat','191',FAP.RTRANSAK)
      )
   ||
      {? _a='PW' || return('') || return(1) ?}
   ||
      {? _a='PW' || return(_kolor) || return(0) ?}
   ?}
||
   {? exec('find_sl','intrastat','189',FAP.KCN().KOD)
   ||
      {? _a='PW' || return(_kolor) || return(0) ?}
   ||
      {? _a='PW' || return('') || return(1) ?}
   ?}
?}


\fap_ws_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyświetleniem FAP.WS
::   WY: kolor
::  OLD: \fap_ws_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
||
   exec('fap_ws_reg','faktury_poz','PW')
?}


\fap_ws_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcją FAP.WS
::   WY: 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fap_ws_pr/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
||
   exec('fap_ws_reg','faktury_poz','PR')
?}


\fap_kcn_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: przed wyświetleniem FAP.KCN
::  OLD: \fap_kcn_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('fap_kcn_pr','faktury_poz')
|| ''
|? FAKS.T().KOR='T' & FAP.KCN<>null
|| exec('findfnrd','color')
|| exec('findfnv','#color')
?}


\fap_kcn_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: przed redakcją FAP.KCN
::  OLD: \fap_kcn_pr/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
FAP.FAKS().IST_TYP<>'' & FAP.M().RODZ='T'
   &
(
   FAKS.T().KOR='N' & (FAKS.STAT_REJ='N' | FAKS.INTRAKC='N') & FAKS.KORYG='N'
      |
   FAKS.T().KOR='T' & FAP.KCN=null | FAKS.T().HIS='T'
)


\fap_kcn_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2009]
:: OPIS: po redakcji FAP.KCN
::  OLD: \fap_kcn_po/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('uzup_cnm','sklad',FAP.M,FAP.KCN,FAP.FAKS().D);
_kodt:=FAP.KCN().KOD;
_his:=FAKS.T().HIS='T';
_null:=1;
{? FAKS.IST_TYP='P' | FAKS.IST_TYP='W'
||
   _null:=0;
   {? _kodt<>''
         &
      exec('rw3','intrastat',_kodt)=0
   ||
      _null:=1
   ?}
?};
FAP.WD:={? _null || '' || FAKS.WD ?};
FAP.RTRANSAK:={? _null || '' || FAKS.RTRANSAK ?};
FAP.RTRANSPO:={? _null || '' || FAKS.RTRANSPO ?};
{? exec('czy_kp','intrastat',FAKS.IST_TYP,FAKS.IST_OKR)
|| {? _null || FAP.KP:='' |? FAP.KP='' || exec('blank_kp','magdok_wspolne',FAP) ?}
|| {? FAKS.T().UE<>'T' || FAP.KP:='' ?}
?};
{? exec('fap_masan_pr','faktury_poz')=0 || FAP.MASAN:=0; {? _his || FKOR.PMASAN:=0 ?} ?};
{? exec('fap_ilujm_pr','faktury_poz')=0 || FAP.ILUJM:=0; {? _his || FKOR.PILUJM:=0 ?} ?};
{? exec('fap_wf_pr','faktury_poz')=0 || FAP.WF:=0; {? _his || FKOR.PWF:=0 ?} ?};
{? exec('fap_ws_pr','faktury_poz')=0 || FAP.WS:=0; {? _his || FKOR.PWS:=0 ?} ?};
{? _his
||
   exec('pops','intrastat')
?};
exec('set_efld_opt','faktury_poz');
win_disp();
1


\blft2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: blankuje typ przeliczenia jednostki dodatkowej (pozycje sprzedaży)
::  OLD: \blft2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
1+(1-BEER.FMJM)


\pwfj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed wyświetleniem pól dla jednostek dodatkowych (pozycje dokumentów sprzedaży)
::  OLD: \pwfj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAP.FAKS().T().KOR='T' & FAP.POZP
|| 0
|? FAP.M().J2<>null()
|| 1
|| {? {? FAP.FAKS().T().KOR='T' || FAP.T2='G' || (1+BEER.FMJM)='T' ?}
   || {? FAP.T2='G'
      || {? exec('get','#params',400000,2)*'MG'
         || exec('findfnrd','color')
         || exec('findfnv','#color')
         ?}
      || ''
      ?}
   || {? FAP.T2='G' & exec('get','#params',400000,2)*'MG'
      || exec('findfnrd','color')
      || exec('findfnv','#color')
      ?}
   ?}
?}


\prfj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcją pól dla jednostek dodatkowych (pozycje dokumentów sprzedaży)
::  OLD: \prfj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAP.M<>null
|| _flds:={? BEER.SZ='S' | BEER.JMZ || '' || 'J2' ?};
   _flds+={? FAP.T2='R' | {? BEER.SZ='S' | BEER.JMZ || FAP.JM<>FAP.J2 || 1 ?} & FAP.T2='A' || ' IL2' ?};
   _flds+={? FAP.T2='R' || ' WS2' || '' ?};
   {? _flds*cur_afld() & FAP.J2=null() || exec('ustafprz','faktury_wspolne',FAP.M,FAP.M().J) ?};
   {? FAP.FAKS().WHERE='G' & FAP.M().J2<>null() & 'J2 JM'*cur_afld()
   || 0
   |? 'GM'*FAP.FAKS().WHERE & cur_afld()='IL2' & (FAP.M=null | (FAP.M<>null & FAP.M().RODZ='T'))
   || 0
   |? FAP.FAKS().WHERE='G' & 'WS2 J2'*cur_afld() & (FAP.M=null | (FAP.M<>null & FAP.M().RODZ='T')) & FAP.IL<>0
   || 0
   |? FAP.T2='G' | FAP.T2='N' | FAP.T2=''
   || 0
   |? FAP.T2='A' & {? JM.f_active() || JM.f_size<=1 || 1 ?}
   || 0
   |? cur_afld()='WS2' & FAP.T2='A'
   || 0
   |? (BEER.FMJM+1)='0'
   || {? FAP.FAKS().SZ='Z' & ~BEER.JMZ || BEER.IL22:=FAP.IL2 ?};
      {? _flds*cur_afld() || {? JM.f_active() || JM.f_first() & JM.f_next() || JM.first() & JM.next() ?} ?}
   || (1+BEER.FMJM)='T'
   ?}
|| 0
?}


\pofj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji pól dla jednostek dodatkowych (pozycje dokumentów sprzedaży)
::  OLD: \pofj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='J2' & (FAP.T2='A' | FAP.T2='R')
||
   _ws2:={? FAP.FAKS().SZ='S' | BEER.JMZ
         || exec('prz','jm',FAP.M,FAP.J2,FAP.JM)
         || exec('prz','jm',FAP.M,FAP.M().J,FAP.J2)
         ?};
   {? FAP.T2='A'
    | (FAP.T2='R' & FAP.WS2=0)
    | (FAP.T2='R' & FAP.WS2<>_ws2 & _ws2<>0
     & FUN.ask('Współczynnik przeliczenia jest różny\nod przypisanego w przelicznikach jednostek.\nCzy zamienić?'@))
   || FAP.WS2:=_ws2
   ?}
?};

{? (FAP.FAKS().SZ<>'S' & ~BEER.JMZ) | FAP.FAKS().WHERE='M'
|| {? (FAP.FAKS().SZ='Z' | FAP.FAKS().WHERE='M') & FAP.JM=FAP.J2 || FAP.WS2:=1 ?};
   _dokl_s:=exec('jaka_dok_mjm','jm',FAP.M,FAP.J2,FAP.M().J);
   _dokl_m:=exec('jaka_dok_m','jm',FAP.M);
   {? _dokl_s<0 || _dokl_s:=_dokl_m ?};
   {? FAP.IL2>0 & FAP.IL2<>BEER.IL22 || FAP.IL:={? FAP.WS2 || FAP.IL2/FAP.WS2 || 0 ?} $ _dokl_m ?};
   {? FAP.IL>0 & cur_afld()<>'IL2' & FAP.FAKS().WHERE<>'M'
   || FAP.IL2:=FAP.IL*FAP.WS2 $ (_dokl_s+1);
      roundmet(BEER.MDOKL);
      FAP.IL2:=FAP.IL2 $ _dokl_s
   ?};
   {? DISP.ILF<>FAP.IL
   || _wyn:=exec('liczfak','faktury_wspolne');
      {? _wyn=1 || exec('liczfak_ist','faktury_wspolne',1,1) ?}
   ?};
   DISP.ILF:=FAP.IL;
   roundmet(5);
   _wyn:={? FAP.IL2>0 || exec('ae_il','faktury_poz') || 1 ?};
   _wyn

|| {? FAP.JM=FAP.J2 | (FAP.WS2=1 & FAP.T2='A') | FAP.WS2=0
   || FAP.WS2:=1;
      {? FAP.FAKS().WHERE<>'M' || {? BEER.SZ='S' & BEER.JMZ || FAP.JM:=FAP.J2 || FAP.J2:=FAP.JM ?} ?}
   ?};

   _dokl:=exec('jaka_dok_m','jm',FAP.M);
   _dokl_m:=exec('jaka_dok_mjm','jm',FAP.M,FAP.JM,FAP.J2);
   _dokl_s:=exec('jaka_dok_mjm','jm',FAP.M,FAP.J2,FAP.JM);
   {? _dokl_s<0 || _dokl_s:=_dokl ?};
   {? _dokl_m<0 || _dokl_m:=_dokl ?};

   _ilefap:={? FAP.JM=FAP.J2 || FAP.IL2 |? FAP.WS2 || FAP.IL2*FAP.WS2  $ _dokl_s || 0 ?};
   {? FAP.IL
   || _txt:={? FAP.FAKS().SZ='S'
            || 'Czy zmienić ilość "Sprzedaży" wg ilości "Przeliczona"?'@
            || 'Czy zmienić ilość "Zakupu" wg ilości "Przeliczona"?'@
            ?};
      {? _ilefap<>FAP.IL & FUN.ask(_txt)
      || FAP.IL:=_ilefap
      ?}
   || FAP.IL:={? FAP.WS2 || FAP.IL2*FAP.WS2 || 0 ?} $ _dokl_s
   ?};
   exec('aktfil2','faktury_wspolne');
   {? DISP.ILF<>FAP.IL
   || _wyn:=exec('liczfak','faktury_wspolne');
      {? _wyn || exec('liczfak_ist','faktury_wspolne',1,1) ?}
   ?};
   DISP.ILF:=FAP.IL
?};
win_disp;
1


\ae_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: akcja po edycji ilości (zmienna do redakcji ilości na pozycji dokumentu sprzedaży)
::   WE: [_a] - 1-odświeżenie okienka(domyslnie) 0-bez
::       [_b] - magazyn lub domyślnie null()
::       [_c] - 1 -nie wykonywać 'onemgrez' (wywołanie z poprawiania ilości w POS), 0 - można (domyślnie)
::  OLD: \ae_il/faktury1.fml
::       \ae_il/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};
_magfap:={? var_pres('_b')=type_of(null()) || _b || null() ?};
{? var_pres('_c')<>type_of(0)
|| _c:=0
?};
_wyn:=1;
_zmiana:=0;
_wgewi:=exec('czyJS','jm',FAP.M,FAP.JM);
{? _wyn
|| {? var_press('__FAKSG_F3')=type_of(obj_new(2))
   || DISP.ILF:=__FAKSG_F3[2];
      FAP.IL2:=__FAKSG_F3[1];
      VAR_DEL.delete('__FAKSG_F3')
   ?};
   _sprzsc:=exec('get','#params',300700,2)='T';
   _onemag:=FAKS.WHERE='G' & FAP.M().RODZ='T'
          & {? _magfap<>null()
            || exec('FindAndGet','#table',MG,_magfap,,"(1+TYP)<>'D'",0)
            |? FAKS.MAG<>null
            || (1+FAKS.MAG().TYP)<>'D'
            |? _sprzsc
            || {? (1+ST.MAG().TYP)<>'D' || 1 || 0 ?}
            || 0
            ?};
   {? _onemag & ~_c || exec('onemgrez','faktury_poz',_magfap) ?};

   {? FAP.FAKS().WHERE='G' & FAP.M<>null & FAP.M().RODZ='T'
   ||
      _ile_r:=exec('oblwgfap','faktury_poz',FAP.ref);
      {? _wgewi
      || _ile_r1:=_ile_r[1]; _ile_r2:=_ile_r[2]
      || _ile_r1:=_ile_r[2]; _ile_r2:=_ile_r[1]
      ?}; obj_del(_ile_r);&_ile_r;
      {? _ile_r1<>0 | _ile_r1<>FAP.IL
      ||
         FAP.IL:=_ile_r1;
         {? FAP.WS2<>0 || FAP.IL2:=_ile_r2 ?};
         _zmiana:=1
      ?}
   ||
      _zmiana:=FAP.IL<>DISP.ILF;
      FAP.IL:=DISP.ILF;
      {? _zmiana=1
      ||
         _wyn:=exec('liczfak','faktury_wspolne',,,0);
         {? _wyn=1 || _wyn:=exec('liczfak_ist','faktury_wspolne',1,1) ?}
      ?}
   ?}
?};

{? FAP.FAKS().WHERE='G' || {? FAP.M().RODZ='T' || FAP.IL2==FAP.IL || FAP.IL2:=FAP.IL ?} ?};
{? _wyn & _zmiana
|| _wyn:=exec('liczfak','faktury_wspolne',,,0);
   {? _wyn=1 || _wyn:=exec('liczfak_ist','faktury_wspolne',1,1) ?}
?};
{? _wyn || exec('aktfil2','faktury_wspolne') ?};
{? _wyn & _a || win_disp ?};
_wyn


\onemgrez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: rezerwacja dla pojedynczego magazynu
::   WE: [_a] - magazyn lub domyślnie null()
::  OLD: \onemgrez/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_magfap:={? var_pres('_a')=type_of(null()) || _a || null() ?};

{? exec('itsPositive','#field',,'T',DISP.ILF)
|| exec('utw_zk_tymc','zamsiw_wspolne'); __uzup_z(FAP.M);
   FAP.IL:=DISP.ILF;
   exec('aktfil2','faktury_wspolne');
   _tab:=exec('f3_il','faktury_poz',,FAP.IL2,-1,,,,_magfap);
   DISP.ILF:=_tab[2];
   FAP.IL2:=_tab[1];
   obj_del(_tab);
   win_disp()
?};
~~


\oblwgfap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: formula obliczająca ilość zarezerwowaną wg pozycji faktury
::   WE: _a - ref FAP-a
::       [_b] -ref magazynu
::       [_c] -ref SQL dostawy
::   WY: tabela wynikow: ilość zarezerwowana i ilość dodatkowa
::  OLD: \oblwgfap/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=obj_new(2); _wyn[1]:=0; _wyn[2]:=0;
REZ.cntx_psh;
REZ.index('FAPR');
{? _=1 || REZ.prefix($_a,)
|? _=2 || REZ.prefix($_a,$_a,_b)
|| REZ.prefix($_a,$_a,_b,_c,_c)
?};
{? REZ.first || {! |? _wyn[1]+=REZ.ILR; _wyn[2]+=REZ.IL2; REZ.next   !} ?};
REZ.cntx_pop;
_wyn


\korh_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: edycja pozycji korekty historycznej
::       old_ref; new_ref - refy faktury biezacej oraz historycznej
::   WE: [_a] - 1-(domyslnie) czy wyswietlac okienko 0-nie wyswietlac
::       [_b] - 1-reczna poprawa pozycji, 0-reczne dolaczanie pozycji
::       [_c] - 0/1 - okno dla danych do intrastat
::       [_d] - sposób redkacji danych do intrastat
::   WY: czy byla edycja
::  OLD: \korh_put/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_edit:={? var_pres('_a')=type_of(0) || _a || 1 ?};
_popraw:={? var_pres('_b')=type_of(0) || _b || 1 ?};
_intrastat:={? var_pres('_c')=type_of(0) || _c || 0 ?};

_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};

_poz_przed:=FAP.POZ;
__hispoz:=FAP.POZ;
_wyn:=
   {? _edit
   || {? _popraw=1 || FKOR.PODP:=0 ?};
      _wyn:=exec('kor_poz','faktury_poz',,_intrastat);
      {? _popraw=1 || FKOR.PODP:=1 ?};
      _wyn
   || 1
   ?};
{? _wyn
|| _poz_po:=FAP.POZ;
   BEER.MSK:='fakpo';
   exec('inf_dod','fakso',-1);
   {? var_pres('old_ref')<>7
   ||
      _ref:=FAP.FAKS().LKSQL;
      _name:=form(8+FAP.FAKS().LKSQL);
      FAKS.cntx_psh();
      FAKS.use(_name);
      FAKS.prefix();
      old_ref:={? FAKS.seek(_ref,) || FAKS.ref || null ?};
      FAKS.cntx_pop()
   ?};
   {? var_pres('f_rok')<>1 || f_rok:=form(8+FAP.FAKS().LKSQL)+2 ?};
   FAKS.cntx_psh();
   FAKSPL.cntx_psh();
   FAP.cntx_psh();
   exec('open','open_tab',ST.ODDZ,f_rok);
   FAKS.prefix();
   {? FAKS.seek(old_ref)
   ||
      FAP.index('FAP');
      FAP.prefix(FAKS.ref);
      {? _poz_po<>_poz_przed & FAP.find_key(_poz_przed)
      || FAP.POZ:=_poz_po;
         FAP.put(1)
      ?};
      {? FAP.find_key(_poz_po)
      ||
         FAP.FAKS:=FAKS.ref;
         FAP.IL:=FKOR.IL;
         FAP.CPR:=FKOR.CPR;
         FAP.CWAL:=FKOR.CWAL;
         FAP.RAB:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || FKOR.RAB || 0 ?};
         FAP.RABK:={? ~exec('czyrabp','ceny',FAKS.RAB_TYP) || FKOR.RAB || 0 ?};
         FAP.SV:=FKOR.SV;
         FAP.WAL:=FKOR.WAL;
         FAP.KRS:=FKOR.KRS;

         FAP.CN:=FKOR.CN;
         FAP.CB:=FKOR.CB;
         FAP.M:=FKOR.M;
         FAP.MX:=FAP.M().KTM;
         FAP.NX:=exec('fap_nx','faktury_poz');
         FAP.JM:=FAP.M().J;
::       intrastat
         FAP.MASAN:=FKOR.PMASAN;
         FAP.ILUJM:=FKOR.PILUJM;
         FAP.KP:=FKOR.KP;
         FAP.WF:=FKOR.PWF;
         FAP.WS:=FKOR.PWS;

         {? FAP.put() || FAP.memo_put(,'UWAGI') ?};
         exec('liczfak','faktury_wspolne');
         FAP.put()
      ?};
      exec('sumfak','faktury_wspolne')
   ?};
   exec('open','open_tab',ST.ODDZ,2-$ST.AR);
   FAP.cntx_pop();
   FAKSPL.cntx_pop();
   FAKS.cntx_pop();
   FAP.cntx_psh();
   exec('sumfak','faktury_wspolne');
   FAP.cntx_pop()
?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
_wyn


\fap_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dodawanie pozycji do wystawionych dokumentów (pozycje dokumentów sprzedaży)
::  OLD: \fap_add/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};

_env_fak_poz:=exec('env','faktury_poz');
params_set('env_fak_poz',_env_fak_poz);

{? FAKS.WHERE='G' & exec('get','#params',300700,2)='T'
||
:: sprzedaż według SM
   exec('fapsm_add','faktury_poz')
|| BEER.MW:={? FAKS.SZ='S' || 'F' || 'K' ?};
   exec('ustaw_ww','eurusd',BEER.MW); BEER.LW:={? BEER.WW || 'W' || '' ?};
   {? FAKS.ZAK='T'
   || FUN.info({? FAKS.SZ='S'
               || 'Dokument sprzedaży zaksięgowany.\n'@
               || 'Dokument zakupu zaksięgowany.\n'@
               ?}+'Modyfikacja niemożliwa.'@)
   |? FAKS.SZ='Z' & FAKS.AKC='T'
   || FUN.info('Dokument zakupu zaakceptowany.\n'@+
               'Modyfikacja niemożliwa.'@)
   |? (FAKS.WHERE='L' & ~exec('fzal_chk','faktury_zalicz'))
   || FUN.info('Wystawiono kolejne dokumenty.\n'@+
               'Dodanie pozycji niemożliwe.'@)
   ||
      exec('ustafprz','faktury_wspolne');
      BEER.TAB:='FAP';
      _mgrp:=Plugin.run('BLANK_GRPMAG_001','FAKS');
      POMOC.MGRF:={? exec('pr_mgrf','faktury_poz') & var_pres('_mgrp')=type_of(null()) || _mgrp || null() ?};

      {? FAKS.STS<>null & FAKS.STS().MGR<>'T' || POMOC.MGR:=POMOC.MGRF:=null ?};

      {? FAKS.WHERE='M' & (FAKS.CZYWZ<>'T' | FAKS.T().UE='T')
      || POMOC.RODZ:='U'
      || POMOC.RODZ:=FAKS.T().TOW_USL
::      || {? POMOC.RODZ='' | var_pres('__TYPYSP_POMOC_RODZ')=0 |
::            (var_pres('__TYPYSP_POMOC_RODZ')>0 & __TYPYSP_POMOC_RODZ<>FAKS.T)
::         || __TYPYSP_POMOC_RODZ:=FAKS.T;
::            POMOC.RODZ:=FAKS.T().TOW_USL;
::            VAR_DEL.delete('__WYBOR_POMOC_RODZ')
::         |? (var_pres('__TYPYSP_POMOC_RODZ')>0 & __TYPYSP_POMOC_RODZ=FAKS.T) &
::            var_pres('__WYBOR_POMOC_RODZ') > 0
::         || POMOC.RODZ:=__WYBOR_POMOC_RODZ
::         ?}
      ?};
      {? POMOC.MGRF().RODZ<>POMOC.RODZ || POMOC.MGRF:=null() ?};
      _da2isto:={? FAKS.IST_OKR='' || date(FAKS.DW~1,FAKS.DW~2,1) || date(#(4+FAKS.IST_OKR),#(FAKS.IST_OKR+2),1) ?};

      _refold:=FAP.ref();
      FAP.blank();
      FAP.memo_set();
      FAP.WAL:={? FAP.FAKS().WAL<>null || FAP.FAKS().WAL || INFO.NAROD ?};
      FAP.WAL_H:=FAP.WAL;
::    intrastat
      exec('istatr_faks2fap','faktury_nag');

      _edit:=exec('fap_win_edit','faktury_poz');
      FAP.win_edit(_edit);
      exec('set_efld_opt','faktury_poz');

      FAP.KH:=FAP.FAKS().KH;
      FAP.KH_ODB:=FAP.FAKS().KH_ODB;
      FAP.HAN:=FAP.FAKS().HAN;
      {? FAP.add() & FAP.r_lock(1,1,1)
      || FAP.WAL:=FAKS.WAL;
         {? FAKS.WHERE='L'
         ||
::          przeliczniki jednostek
            exec('ustafprz','faktury_wspolne',FAP.M,FAP.M().J)
         ?};
         {?
            {!
            |?
::             przeliczniki jednostek
               exec('ustafprz','faktury_wspolne',FAP.M,FAP.M().J);
               _ed:=FAP.edit("params_exec('chk_fakp','faktury_poz',0)");
               {? _ed
               ||
                  {? FAP.FAKS().WHERE='G' & FAP.M().RODZ='T'
                  || {? DISP.ILF<=0
                     || FUN.info('Ilość nie została rozpisana wg magazynów.\n'@+
                                 'Dodanie pozycji niemożliwe.\n\n'@+
                                 'Rozpisz ilość wg magazynów.'@);
                        _ed:=0
                     ?}
                  |? FAP.IL=0
                  || FUN.info('Niewypełnione pole: Ilość.'@);_ed:=0
                  || _ed:=1
                  ?}
               ||
                  _ed:=-1
               ?};
               JM.f_clear();
               JM.win_dict('WER');
               _ed=0
            !};
            _ed<>0
         ||
            {? _ed>0
            ||
               _refold:=null;
               {? ~FAP.put()
               || exec('fap2del','faktury_poz',1)
               || FAP.memo_put(,'UWAGI');
::    uzupełnienie FAP.U o wrtości z pola memo
                  FAP.U:=255+FAP.memo_txt(0,1,'UWAGI');
                  FAP.put()
::                  VAR_DEL.delete('__WYBOR_POMOC_RODZ');
::                  __WYBOR_POMOC_RODZ:=POMOC.RODZ
               ?};
               FAP.r_unlock();
::             naliczenie opłat dodatkowych
               exec('actTAXS','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),FAP.M,null());
               exec('inf_dod','fakso',0);
               exec('liczfak','faktury_wspolne',,,0);
::             rozpiska projektow wg szablonu
               {? FAP.PROJEKTY
               || exec('projdel','projekty',FAP.ref());
                  exec('projwgfap','projekty',FAP.ref())
               || _var:=obj_new('SZABLON','PROJREM');
                  _var.SZABLON:='N';
                  params_set('TAB',FAP,'var',_var);

                  _var.PROJREM:='P';
                  exec('proj2fap_rozlicz','projekty',FAP.ref(),1);

                  _var.PROJREM:='R';
                  exec('proj2fap_rozlicz','projekty',FAP.ref(),1)
               ?};
::  DRO_TODO: [rr]
::               exec('proj2fap_roz','skid_prm',0); exec('proj2fap_okn','skid_prm');
               exec('sumfak','faktury_wspolne');
               FAKS.get()
            ||
               FAP.r_unlock();
               exec('fap2del','faktury_poz',1);
               VAR_DEL.delete('__msk')
            ?}
         ?}
      ?};
      JM.f_clear;
      JM.win_dict('WER');
      {? _refold<>null || FAP.seek(_refold) ?};
      exec('fap_win_edel','faktury_poz',_edit)
   ?}
?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
''


\chk_fakp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: kontrola po edycji pozycji dokumentu sprzedaży/zakupu
::   WE: 0 - dokument manualny (domyślnie), 1 - import z komunikatu EDI
::  OLD: \chk_fakp/firma.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0)
|| _edi:=_a
|| _edi:=0
?};
_return:='';
:: pole indeks wymagane z pominieciem faktury zaliczkowej
{? FAP.FAKS().WHERE<>'L'
|| {? (_return='' | _edi) & {? _edi || FAP.M=null() || D_HELP.M_FAP='' ?}
   || _msg:='Należy podać indeks.'@;
      {? _edi
      || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
         exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
      ||FUN.info(_msg)
      ?};
      _return:='M_FAP'
   ?}
|| {? (_return='' | _edi) & {? _edi || FAP.M = null() || D_HELP.M_FAP='' ?} & FAP.NX=''
   || _msg:='Należy podać nazwę.'@;
      {? _edi
      || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
         exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
      || FUN.info(_msg)
      ?};
      _return:='NX'
   ?}
?};
:: pozostale wymagane pola
{? (_return='' | _edi) & FAP.SV=null
|| _msg:='Należy podać stawkę VAT.'@;
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:='SV'
?};
:: dla zaliczek i korekt jest do redakcji pole FAP.IL, dla reszty DISP.ILF
{? FAP.FAKS().WHERE='L'
|| {? (_return='' | _edi) & {? _edi || FAP.IL<=0 || DISP.ILF<=0 ?}
   || _msg:='Należy podać ilość większą od zera.'@;
      {? _edi
      || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
         exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
      || FUN.info(_msg)
      ?};
      _return:='ILF'
   ?}
|| {? (_return='' | _edi) & {? _edi || FAP.IL<=0 || DISP.ILF<=0 ?} & FAKS.WHERE<>'G'
   || _msg:='Należy podać ilość większą od zera.'@;
      {? _edi
      || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
         exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
      || FUN.info(_msg)
      ?};
      _return:={? FAKS.WHERE='M' || 'WS2' || 'ILF' ?}
   ?}
?};
:: cena
{? _return='' | _edi
||
:: czy walutowy
   {? exec('spr_npoz','eurusd') & FAKS.WAL<>exec('bl_nwal','ustawienia')
   ||
      {? {? FAP.FAKS().SZ='S' | _edi || FAP.CWAL<=0 || FAP.CWAL<0 ?}
      || {? FAP.FAKS().SZ='Z' & _edi & FAP.CWAL=0
         || _msg:='Cena zerowa.'@
         || _msg:='Należy podać cenę większą od zera.'@
         ?};
         {? _edi
         || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
            exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
         || FUN.info(_msg)
         ?};
         _return:='CWAL'
      |? _wyn_c:=exec('czy_cena_z_tap','ceny','FAP',~_edi);
         {? _edi & _wyn_c.MSG<>''
         || _msg:='Pozycja %1: '[$FAP.POZ]+_wyn_c.MSG;
            exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
         ?};
         _wyn_c.WYN
      || _return:='CWAL'
      ?};
      {? (_return='' | _edi) & params_exec('spr_wpoz','eurusd',0) & FAP.KRS=0
      || _msg:='Należy podać kurs.'@;
         {? _edi
         || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
            exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
         || FUN.info(_msg)
         ?};
         _return:='KRS'
      ?};
      {? _return='' | _edi
      || {? _edi
         || __msg:='';
            _wyn_r:=exec('spr_rab','ceny',DISP.RABP,0,!__msg,'fakpo');
            {? __msg<>''
            || _msg:='Pozycja %1: '[$FAP.POZ]+__msg;
               _msg:=gsub(_msg,'\n',' ');
               exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
            ?};
            &__msg
         || _wyn_r:=exec('spr_rab','ceny',DISP.RABP,0)
         ?};
         {? ~_wyn_r
         || {? ~_edi
            || win_disp
            ?};
            _return:=exec('rab_2130','ceny',FAP,'RABP')
         ?}
      ?}
  ||
::    czy mozna edytowac FAP.CPR
      {? exec('czy_rwal','eurusd','F')
      ||
         {? {? FAP.FAKS().SZ='S' | _edi || FAP.CPR<=0 || FAP.CPR<0 ?}
         || {? FAP.FAKS().SZ='Z' & _edi & FAP.CPR=0
            || _msg:='Cena zerowa.'@
            || _msg:='Należy podać cenę większą od zera.'@
            ?};
            {? _edi
            || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
               exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
            || FUN.info(_msg)
            ?};
            _return:={? exec('upr_cscz','ceny') || 'CPR' || 'SV' ?}
         |? _wyn_c:=exec('czy_cena_z_tap','ceny','FAP',~_edi);
            {? _edi & _wyn_c.MSG<>''
            || _msg:='Pozycja %1: '[$FAP.POZ]+_wyn_c.MSG;
               exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
            ?};
            _wyn_c.WYN
         || _return:='CPR'
         ?};
         {? _return='' | _edi
         || {? _edi
            || __msg:='';
               _wyn_r:=exec('spr_rab','ceny',DISP.RABP,0,!__msg,'fakpo');
               {? __msg<>''
               || _msg:='Pozycja %1: '[$FAP.POZ]+__msg;
                  _msg:=gsub(_msg,'\n',' ');
                  exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
               ?};
               &__msg
            || _wyn_r:=exec('spr_rab','ceny',DISP.RABP,0)
            ?};
            {? ~_wyn_r
            || {? ~_edi
               || win_disp
               ?};
               _return:=exec('rab_2130','ceny',FAP,'RABP')
            ?}
         ?}
      || _msg:='Nagłówek dokumentu jest walutowy.\n'
                  'Dla oddziału wyłączono wielowalutowość.\n'
                  'Edycja ceny niemożliwa.'@;
         {? _edi
         || _msg:='Pozycja %1: '[$FAP.POZ]+gsub(_msg,'\n',' ');
            exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
         || FUN.info(_msg)
         ?};
         _return:='SV'
      ?}
   ?}
?};
{? (_return='' | _edi) & ~HELP.MOD_POZY & FAP.FAKS().SZ='Z'
  & (FAP.IL-exec('ilrfapdk','faktury_poz',$FAP.ref,FAP.FAKS().WHERE))<0
|| _msg:='Pozycja jest powiązana z dokumentem magazynowym.\n'@+
         'Minimalna ilość na pozycji bez utraty powiązań to: '@+
         form(exec('ilrfapdk','faktury_poz',$FAP.ref,FAP.FAKS().WHERE))+'.';
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+gsub(_msg,'\n',' ');
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:='ILF'
?};
{? (_return='' | _edi) & FAP.JM=null
|| _msg:='Należy podać jednostkę miary.'@;
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:='JM'
?};
:: intrastat
{? (_return='' | _edi) || _return:=exec('chk_fakp_ist','faktury_poz',_edi) ?};
:: sprawdzenie wdrożeniaowe
{? (_return='' | _edi) & Plugin.runnable('VAL_POZLOG_001')
|| _return:=Plugin.run('VAL_POZLOG_001','FAP')
?};
:: należy wołać na końcu
{? (_return='' | _edi)
||
   _return:=Plugin.run('KSEF_001','LOG',_edi);
   {? _return='~~' || _return:=exec('ksef_001_default','edi_fo_ksef',_edi) ?}
?};

_return


\chk_fakp_ist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: sprawdzenie poprawności wypełnienia pól dotyczących INTRASTAT w tabeli FAP dla faktur
::   WE: _a=1 - wolane przy akceptacji dokumentu, =0 - inne przypadki
::   WY: akronim niepoprawnie wypełnionego pola, ''-wpp
::  OLD: \chk_fakp/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_disp:={? var_pres('_a')=type_of(0) || ~_a || 1 ?};

_atr:=exec('check_poz_a','intrastat');
_atr.DISP:=_disp;
_atr.TABNAG:=FAKS;
_atr.TABPOZ:=FAP;
_atr.KOR:='N';
_atr.HIS:='N';
_atr.POZ:=FAP.POZ;
_atr.MRODZ:=FAP.M().RODZ;

_atr.IST_TYP:=FAKS.IST_TYP;
_atr.DATE:={? FAKS.IST_OKR='' || date(0,0,0) || date(#(4+FAKS.IST_OKR),#(FAKS.IST_OKR+2),1) ?};

_atr.KCN:=FAP.KCN;
_atr.KODT:=FAP.KCN().KOD;
_atr.KPW:=FAP.KPW;
_atr.WD:=FAP.WD;
_atr.RTRANSAK:=FAP.RTRANSAK;
_atr.RTRANSPO:=FAP.RTRANSPO;
_atr.KP:=FAP.KP;
_atr.MASAN:=FAP.MASAN;
_atr.JU:=FAP.KCN().JU;
_atr.ILUJM:=FAP.ILUJM;
_atr.WF:=FAP.WF;
_atr.WS:=FAP.WS;
exec('check_poz','intrastat',_atr)


\ilrfapdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: ile zostało rozpisane względem faktury zakupu
::   WE: _a - ref SQL-owy FAP
::       _b - FAKS.WHERE
::   WY: ilosc rozpisana
::  OLD: \ilrfapdk/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
FAP2DK.index('FAP');
FAP2DK.prefix(_b,_a);
{? FAP2DK.first()
|| {!
   |? _wyn+=FAP2DK.IL;
      FAP2DK.next()
   !}
?};
_wyn


\pozycje_fak_var
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Zmienne używane w funkcji pozycje_fak
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
obj_new('DISPZM','HELP_WHERE','R_LOCK','EDIT','BEERMW','CZY_SEL','DICTM','DICTDKC')


\pozycje_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: okienko pozycji faktury dla bieżącego nagłówka menu
::   WE: [_a] - wyłączenie pozycji menu
::       [_b] - wyłączenie pozycji menu np. 'DPU:D' - parametr _a ma priorytet
::       [_c] - FAP.uidref(), na którym ma stanąć selektor
::       [_d] - 0/1 - zakładka w obszarze
::       [_e] - 0/1 - aktywacja okna
::       [_f] - zmienna do przekazywania zmiennych
::   WY: _wyn - wynik FAP.select()
::  OLD: \poz_fak/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_acts_off_all:={? var_pres('_a')<>type_of(1) || _a:=0 || _a ?};
_acts_off:={? var_pres('_b')=type_of('') || _b || '' ?};
_uidref:={? var_pres('_c')=type_of('') || _c || '' ?};
_tab_in_area:={? var_pres('_d')=type_of(0) || _d || 0 ?};
_grp_disp:={? _tab_in_area || _e=0 || 0 ?};
_var:={? _tab_in_area || _f || exec('pozycje_fak_var','faktury_poz') ?};

_r_lock:=_var.R_LOCK:=-1;

{? FAKS.WHERE='K' | FAKS.WHERE='N'
||
:: korekta zbiorcza, korekta nagłówkowa
   params_exec('faks_kzn_view','faktury_nag1',_uidref,_tab_in_area);
   return()
?};
::
:: dokumenty inne niż korekta zbiorcza, korekta nagłówkowa
::
{? ~_tab_in_area & FAKS.WHERE='L' & exec('czy_fapow','faktury_zalicz')=0 || return() ?};

_win_sel_prfx:={? _tab_in_area || 'T' || '' ?};

_dispzm:=DISP.ZMREZ;
_var.DISPZM:=_dispzm;
DISP.ZMREZ:=0;

BEER.KRAJ_VAT:=FAKS.KRAJ_VAT;
JPK_SLO.f_clear();

_formikon:=exec('ATR_INZATR','faktury_poz');
FAP.win_fml('WER_I',ATR,'INZATR',,'ICON_BEFORE',_formikon);
_formikon:="{? exec('FindInSet','#table','FAP_K','TAXS',FAP.uidref())<>null()
            || 'xwin16.png:10'
            || exec('pusta','#icon')
            ?}";
FAP.win_fml(_win_sel_prfx+'WER',FAP,'CN',,'ICON_BEFORE',_formikon);
FAP.win_fml(_win_sel_prfx+'WERK',FKOR,'CN',,'ICON_BEFORE',_formikon);
FAP.win_fml(_win_sel_prfx+'WERKH',FKOR,'CN',,'ICON_BEFORE',_formikon);
FAP.win_fml('WER_I',FAP,'CN',,'ICON_BEFORE',_formikon);
FAP.win_fml('WERKZ',FKOR,'CN',,'ICON_BEFORE',_formikon);

VAR_DEL.delete('__hispoz');
BEER.MENU_PTH+='Y';

{? KHVZ.WIDOKI || _acts_off_all:=1 ?};
_czy_sel:=0;
_var.CZY_SEL:=_czy_sel;

POMOC.MGR:=null;

FAKS.get();
FAKS.cntx_psh();

{? FAKS.AKC='N'
|| DISP.POP:='T'
|| DISP.POP:='N'
?};

_beermw:=BEER.MW;
_var.BEERMW:=_beermw;
BEER.MW:='F';
exec('ustaw_ww','eurusd',{? BEER.SZ='S' || 'F' || 'K' ?}); exec('ust_lw','eurusd',1);
exec('ustafprz','faktury_wspolne');
BEER.TAB:='FAP';
ATR.MJS:='FAP';

:: okno wertowania
_sel:=exec('fap_win_sel','faktury_poz',1);
:: okno redakcji
_edit:=exec('fap_win_edit','faktury_poz');
_var.EDIT:=_edit;
:: okno wzorca
{? FAKS.KOR<>''
|| FAP.win_patt('SZUKK')
|| FAP.win_patt({? exec('upr_cscz','ceny') || 'SZUK'|| 'SZUK1' ?})
?};
:: okna słowników
{? KHVZ.WIDOKI
|| _dictm:=M.win_dict('?');
   _var.DICTM:=_dictm;
   M.win_dict('SLO_M');
   _dictdkc:=DK_C.win_dict('?');
   _var.DICTDKC:=_dictdkc;
   DK_C.win_dict('WER')
?};
M.win_dict('SLO_M');
KK.win_dict('SLO');
:: akcje okna wertowania
BEER.FAP_ACTS:=
   {? _acts_off_all
   || 'DPUZAINT(SCYRXNPVW)J:D'
   || _acts_off
   ?};

BEER.LW:={? BEER.WW || 'W' || '' ?};

FAP.win_sel(_sel);
FAP.win_edit(_edit);
FAP.f_clear();
FAP.index('FAP');
FAP.prefix(FAKS.ref);
PROJZ.PROJEKTY:=FAKS.PROJEKTY;
TAZ.RAB_TYP:=FAKS.RAB_TYP;
DISP.RAB:=FAKS.RAB;
FKOR.FRAB:=FZ.RAB;
DISP.RABO:={? exec('czyrabp','ceny',TAZ.RAB_TYP) || '%' || FAKS.WAL().KOD ?};
DISP.RABZTYP:=exec('rabztyp','ceny',TAZ.RAB_TYP);
:: Dokument Businesslink - ustawienie się na załączniku w celu podglądu PDF w oknie pozycji dokumentu
exec('bl_dokum_seek','zbl',FAKS);

_po:=1;
:: jezeli jest zablokowany naglowek znaczy ze pozycje tez sa modyfikowane
{? ~_tab_in_area || _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref) ?};
_var.R_LOCK:=_r_lock;
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag')
||
   {? FAKS.T().HIS='T' & FAKS.KOR<>'' & ~_tab_in_area
   || exec('akt_kor','faktury_nag');
      exec('korh_rab','faktury_nag',FAKS.LKSQL,0)
   ?};
::   {? FAKS.T().KOR='T' & FAP.first() || exec('wysw_kor','faktury_poz') ?};
   VAR_DEL.delete('old_ref');

   _help_where:=HELP.WHERE;
   _var.HELP_WHERE:=_help_where;
   HELP.WHERE:=FAKS.WHERE;

   {? var_pres('hdr_dok')>0
   || FAP.hdr_sel(); FAP.hdr_sel(hdr_dok)
   ?};
::
:: select
::
   {? _tab_in_area
   || _czy_sel:=1
   || {? _uidref=''
      || _czy_sel:=FAP.select()
      || FAP.seek(_uidref);
         _czy_sel:=FAP.select(,1,5)
      ?}
   ?};
   _var.CZY_SEL:=_czy_sel;
::
:: po select
::
   {? _tab_in_area
   ||
      ~~
   ||
      FAKS.get();
      exec('pozycje_fak_po','faktury_poz',0,1,_var);
      _po:=0
   ?}
?};
{? _tab_in_area
||
   ~~

|? _po
||
   exec('pozycje_fak_po','faktury_poz',0,0,_var)
?}


\pozycje_fak_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: okienko pozycji faktury dla bieżącego nagłówka menu - po
::   WE: _a - 0/1 - zakładka w obszarze
::       _b - 0/1 - aktywacja okna
::       _c - zmienna do przekazywania zmiennych
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab_in_area:=_a;
_grp_disp:=_b=0;
_var:=_c;

{? ~_grp_disp & _var.R_LOCK>0
||
   {? DISP.ZMREZ & FAP.FAKS().STAT_REJ<>'A' & FAP.FAKS().SZ='Z'
    & exec('get','#params',6050,2,OPERATOR.USER)='T'
   ||
      _iledk:=0;
      FAP2DK.cntx_psh();
      FAP2DK.index('NFAKS');
      FAP2DK.prefix($FAKS.ref,);
      {? FAP2DK.first
      ||
         {!
         |?
            _iledk+=FAP2DK.DK<>'';
            _iledk=0 & FAP2DK.next()
         !}
      ?};
      FAP2DK.cntx_pop();
      {? _iledk || exec('FAPwycDK','faktury_poz') ?}
   ?};
   {? KHVZ.WIDOKI
   || M.win_dict(_var.DICTM);
      DK_C.win_dict(_var.DICTDKC)
   ?};

   {? FAKS.AKC<>'T'
   || exec('sumfak','faktury_wspolne');
      exec('plat_przel','faktury_plat',FAKS.ref())
   |? FAKS.ZAK='N'
   || FAP.index('FAP');
      FAP.prefix(FAKS.ref());
      {? FAP.first()
      ||
         _proj_w:=0;
         {!
         |?
            _proj_w+=FAP.PROJ_W;
            FAP.next()
         !};
         FAKS.PROJ_W:=_proj_w;
         FAKS.put()
      ?}
   ?};
   OPAK_N.r_unlock();

   HELP.WHERE:=_var.HELP_WHERE;
   exec('r_unlock_one','#table',FAKS,FAKS.ref(),_var.R_LOCK);
   JM.f_clear();
   JM.win_dict('WER')
?};
exec('fap_win_edel','faktury_poz',_var.EDIT);
FAP.win_patt('SZUK');
BEER.MW:=_var.BEERMW;
FAKS.cntx_pop();
FAKS.get();
PROJZ.PROJEKTY:=null();
BEER.FAP_ACTS:='';
BEER.MENU_PTH-=1;
DISP.ZMREZ:=_var.DISPZM;
VAR_DEL.delete('__hispoz');
BEER.KRAJ_VAT:=null();
{? _var.CZY_SEL || 'key:F2' || _var.CZY_SEL ?}


\fap_tm_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2009]
:: OPIS: ustala zmienna POMOC.RODZAJ przed funkcją popraw
::  OLD: \fap_tm_u/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAP.M<>null || POMOC.RODZ:=FAP.M().RODZ ?}


\fap_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: poprawianie pozycji wystawionych dokumentów
::  OLD: \fap_edit/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};
{? FAKS.AKC='T'
||
   FUN.info('Zakończono rejestrację dokumentu.\nPoprawa pozycji niedostępna.'@);
   exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
   return(0)
?};
_env_fak_poz:=exec('env','faktury_poz');
_env_fak_poz.POPRAW:='T';
params_set('env_fak_poz',_env_fak_poz);
_wal:=FAP.WAL;
_cn:=FAP.CN;
_fap_m:=FAP.M;
INTRAST.POPS:='N';
{? FAKS.WHERE<>'G' || DISP.ILF:=FAP.IL ?};
:: opakowania
{? FAKS.AKC='N' & FAP.OPAK_GEN='X'
||
   _ist:=FAKS.IST_TYP='W';
   _koszt:=exec('get','#params',300200,2)='T';
   {? _ist | _koszt
   ||
      {? FUN.ask('Pozycja wygenerowana automatycznie.\n'@+
            'Czy poprawić dane?'@)
      ||
         _red:=FAP.mk_edit('Zmiana konta kosztów'@,,'opakgenx_koszt',,,'html_maximized');
         FAP.win_esep(_red,'Konto kosztów'@);
         FAP.win_efld(_red,,'M','KTM',,50,,1,'Indeks'@);
         FAP.win_efld(_red,,'M','KTM',,50,,1,'Nazwa'@);
         FAP.win_efld(_red,,'KK','SYM','KONTASYS',50,,,'Konto'@);
         _win_red:=FAP.mk_edit('Pozycja dokumentu'@,,'opakgenx_ist',,,'html_maximized');
         {? _ist & _koszt
         ||
            FAP.win_etab(_win_red,'Dane do Intrastat'@);
            FAP.win_ewin(_win_red,,'INTRA');
            FAP.win_etab(_win_red,'Konto kosztów'@);
            FAP.win_ewin(_win_red,,_red);
            FAP.win_edit(_win_red)

         |? _ist
         ||
            FAP.win_edit('INTRA')

         |? _koszt
         ||
            FAP.win_edit(_red)
         ?};
         return(
            {? FAP.edit("params_exec('chk_fakp','faktury_poz',0)")
            ||
               FAP.put()
            ?};
            exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
         )
      ||
         exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
         return(0)
      ?}
   ||
      FUN.info('Pozycja wygenerowana automatycznie.\n'@+
               'Modyfikacja niemożliwa.'@);
      exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
      return(0)
   ?}
?};
_win_edit:=exec('fap_win_edit','faktury_poz',0);
FAP.win_edit(_win_edit);
exec('fap_tm_u','faktury_poz');
BEER.MW:={? FAKS.SZ='S' || 'F' || 'K' ?};
exec('ustaw_ww','eurusd',BEER.MW); BEER.LW:={? BEER.WW || 'W' || '' ?};
{? FAP.FAKS().ZAK='T' & INTRAST.POPS='N'
|| FUN.info({? FAP.FAKS().SZ='S'
            || 'Dokument sprzedaży zaksięgowany.\n'@
            || 'Dokument zakupu zaksięgowany.\n'@
            ?}+'Modyfikacja niemożliwa.'@)
|? FAP.FAKS().SZ='Z' & FAP.FAKS().AKC='T'
|| FUN.info('Dokument zakupu zaakceptowany.\n'@+
            'Modyfikacja niemożliwa.'@)
|? (FAKS.WHERE='L' & ~exec('fzal_chk','faktury_zalicz'))
|| FUN.info('Wystawiono kolejne dokumenty.\n'@+
            'Modyfikacja niemożliwa.'@)
||
   {? FAKS.SZ='Z' & FAP.M().RODZ='U' & exec('FindInSet','#table','FAKS_K','FAP',$FAP.ref(),'N',,,FAKS.ref())<>null
   || FUN.info('Pozycja uwzględniona w rozliczeniu kosztów.\n'@+
               'Po modyfikacji należy uwzględnić naniesione zmiany w rozliczeniu kosztów.'@)
   ?};
   _projekty:=FAP.PROJEKTY;
   _cena:=FAP.CN;
   _cwal:=FAP.CWAL;
   {?
      {!
      |?
         exec('ustafprz','faktury_wspolne',FAP.M,FAP.JM);
         _wed:=FAP.win_edit('?');
         {? (1+BEER.FMJM)='T' & FAP.FAKS().WHERE='M'
         || _edit:=exec('fap_win_edit','faktury_poz');
            FAP.win_edit(_edit);
            BEER.TAB:='FAP';
            _t2:=exec('blft2','faktury_poz');
            {? FAP.T2<>'G' || FAP.T2:=_t2 ?}
         ?};
         {? FAP.FAKS().WHERE='G' || exec('list_rez','faktury_poz',0) ?};

         {? exec('FindInSet','#table','FAKZ2FAP','FAP',FAP.ref()) || POMOC.MGRF:=null() ?};

         exec('set_efld_opt','faktury_poz');
         FAP.memo_get(,'UWAGI');
         _ed:=FAP.edit("params_exec('chk_fakp','faktury_poz',0)");
         {? _ed
         || {? FAKS.SZ='Z' & FAKS.T().KOR='N' & (FAP.CN<>_cena | FAP.CWAL<>_cwal) || DISP.ZMREZ:=1 ?};
            {? FAP.FAKS().WHERE='G' & FAP.M().RODZ='T'
            || {? DISP.ILF<=0
               || FUN.info('Ilość nie została rozpisana wg magazynów.\n'@+
                           'Poprawienie pozycji niemożliwe.'@);
                  _ed:=0
               ?}
            |? FAP.IL=0
            || FUN.info('Niewypełnione pole: Ilość.'@);_ed:=0
            || _ed:=1
            ?}
         ||
            _ed:=-1
         ?};
         JM.f_clear();
         JM.win_dict('WER');
         FAP.win_edit(_wed);
         {? var_pres('_edit')=type_of('') || exec('fap_win_edel','faktury_poz',_edit) ?};
         _ed=0
      !};
      {? FAP.FAKS().WHERE='G' & FAP.M().RODZ='T' || exec('list_rez','faktury_poz',{? _ed=-1 || 1 || 2 ?}) ?};
      _ed<>0
   || {? _ed>0
      || {? FAKS.WHERE='P' | (FAKS.WHERE='G' & (FAKS.MAG=null | FAKS.MAG().MG_OPAK<>''))
         || FAP.OPAK_GEN:='N'
         ?};
         {? ~HELP.MOD_POZY & FAP.FAKS().SZ='Z'
         || _rr:=FAP.IL-exec('ilrfapdk','faktury_poz',$FAP.ref,FAP.FAKS().WHERE)
         || _rr:=0
         ?};
         {? FAP.put() || FAP.memo_put(,'UWAGI') ?};
         {? ~HELP.MOD_POZY & FAP.FAKS().SZ='Z' & _rr<0
         || exec('aktfapdk','faktury_poz',$FAP.ref,_rr,FAP.FAKS().WHERE)
         ?};
         {? (_cn<>FAP.CN | _wal<>FAP.WAL) & FAP.FAKS().SZ='Z' || exec('wycenND','faktury_wspolne',FAP.ref()) ?};
::       naliczenie opłat dodatkowych
         exec('actTAXS','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),FAP.M,_fap_m);
         FAP.cntx_psh();
         {? FAKS.SZ='S' || exec('liczfak','faktury_wspolne',,,0) ?};
         {? FAP.PROJEKTY
         || exec('projdel','projekty',FAP.ref());
            exec('projwgfap','projekty',FAP.ref())
         || {? _projekty<>FAP.PROJEKTY || exec('projdel','projekty',FAP.ref()) ?};
            exec('proj2fap_akt','projekty')
         ?};
         {? FAP.put()
         || FAP.memo_put(,'UWAGI');
::    uzupełnienie FAP.U o wrtości z pola memo
            FAP.U:=255+FAP.memo_txt(0,1,'UWAGI');
            FAP.put()
         ?};
         exec('sumfak','faktury_wspolne');
         FAP.cntx_pop();
         FAP.get();
         FAKS.get()
      ?}
   ?}
?};
exec('fap_win_edel','faktury_poz',_win_edit);
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
''


\fap_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: usuwanie pozycji do wystawionych dokumentów
::   WE: [_a] - string - tekst wyświetlający się przy potwierdzeniu usunięcia pozycji
::       [_b] - 1 (domyślnie) - pokaż komunikat o potwierdzeniu usunięcia, 0 - nie wyświetla komunikatu
::  OLD: \fap_del/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};

_tr_kos:=exec('pow_tr_kos','transport_koszt',FAP.uidref());

_ask_del:={? _tr_kos<>''
          || 'Pozycja powstała na podstawie kosztu transportu.\n'
             'Usunąć bieżącą pozycję?'@
          || 'Usunąć bieżącą pozycję?'@
          ?};
{? _>=1 || {? type_of(_a)=type_of('') || _ask_del:=_a ?} ?};
_ask_del_show:=1;
{? _>=2 || {? type_of(_b)=type_of(0) & _b=0 || _ask_del_show:=0 ?} ?};

_del:=0;
FAP.cntx_psh();
{? FAP.prev()
|| _fap:=FAP.ref
|? FAP.next()
|| _fap:=FAP.ref
|| _fap:=null
?};
FAP.cntx_pop();
{? FAP.FAKS().ZAK='T'
|| FUN.info({? FAP.FAKS().SZ='S'
            || 'Dokument sprzedaży zaksięgowany.\n'@
            || 'Dokument zakupu zaksięgowany.\n'@
            ?}+'Nie można usunąć pozycji.'@)
|? FAP.FAKS().SZ='Z' & FAP.FAKS().AKC='T'
|| FUN.info('Dokument zakupu zaakceptowany.\n'@+
            'Modyfikacja niemożliwa.'@)
|? FAP.FAKS().WHERE='M' & FAKS.CZYWZ<>'T' & FAP.M().RODZ='T'
|| FUN.info('Dokument sprzedaży powiązany z dokumentem magazynowym.\n'@+
            'Nie można usunąć pozycji.'@)
|? (FAKS.WHERE='L' & ~exec('fzal_chk','faktury_zalicz'))
|| FUN.info('Wystawiono kolejne dokumenty.\n'@+
            'Nie można usunąć pozycji.'@)
|| _rr:=3;
   _up:={? FAP.FAKS().SZ='Z'
        || exec('czyfapdk','faktury_wspolne',$FAP.ref(),FAP.ref())
        |? FAP.FAKS().WHERE='G' & FAP.M().RODZ='T'
        || _rr:=exec('returnrt','rezerwacje',$FAP.ref(),form(8+$FAP.ref())+3,0,FAP.FAKS().KH);
           1
        || 1
        ?};
   {? FAKS.SZ='Z' & FAP.M().RODZ='U' & exec('FindInSet','#table','FAKS_K','FAP',$FAP.ref(),'N',,,FAKS.ref())<>null
   || FUN.info('Pozycja uwzględniona w rozliczeniu kosztów.\n'@+
               'Po usunięciu należy uwzględnić naniesione zmiany w rozliczeniu kosztów.'@)
   ?};
   {? _up & _rr & {? _up=2 | _rr<3 || 1 || {? _ask_del_show=1 || FUN.ask(_ask_del) || 1 ?} ?}
   || {? _up=2 || exec('fapdkdel','faktury_poz',$FAP.ref,FAP.ref,FAP.FAKS().WHERE) ?};
      exec('delFAP_K','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),null());
      _del:=exec('fap2del','faktury_poz',0,_rr,,_tr_kos);
      VAR_DEL.delete('__msk');
      FAP.cntx_psh();
      exec('ReNumAfterDel','#table','FAP','POZ');
      FAP.cntx_pop();
      exec('liczfak','faktury_wspolne');
      exec('sumfak','faktury_wspolne')
   ?}
?};
{? _del || FAP.seek(_fap) ?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
''


\be_prodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcją pola POMOC.RODZ na pozycji faktury
::   WY: 1-redakcja możliwa, 0-wpp
::  OLD: \be_prodz/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_da2isto:={? FAKS.IST_OKR='' || date(FAKS.DW~1,FAKS.DW~2,1) || date(#(4+FAKS.IST_OKR),#(FAKS.IST_OKR+2),1) ?};

{? FAKS.WHERE='G' & FAP.IL<>0
|| 0
|? (FAP.FAKS().WHERE='M' & POMOC.RODZ='T' & HELP.MOD_POZY=0) | (FAP.FAKS().WHERE='M' & cur_afld()='RODZ')
|| 0
|? ~HELP.MOD_POZY & FAKS.SZ='Z' & ~exec('czyfapdk','faktury_wspolne',$FAP.ref,,0)
|| 0
|| ~HELP.MOD_POZY
?}


\ae_prodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2003]
:: OPIS: po redakcji zmiennej POMOC.RODZ
::  OLD: \ae_pomro/magazyn2.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? fld()<>'T' & fld()<>'U'
|| FUN.info('Wpisz: T-Materiał lub U-Usługa.'@)
||
   _wyn:=1;
:: dodatkowo zerowanie pola FAP.M jesli rodzaj indeksu nie jest zgodny
   {? FAP.M<>null & FAP.M().RODZ<>fld() || FAP.M:=null; D_HELP.M_FAP:=''; POMOC.MGRF:=null ?};
:: ustawianie okienka selekcji dla materialow
   {? fld()='T'
   ||
::    dodatkowo dodano sprawdzanie czy wyswietlac stany
      {? FAKS.WHERE='P' & HELP.WMAG=0
      || M.win_dict('NL_WER')
      || M.win_dict('WER_S')
      ?}
   |? fld()='U'
   || M.win_dict('NL_WERU')
   ?}
?};
_wyn


\be_mfap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcją zminnej D_HELP.M_FAP
::   WY: czy można edytować pole
::  OLD: \be_mfap/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=FAKS.WHERE<>'M' & (FAKS.T().KOR='N' | FAKS.T().HIS='T') | (FAKS.WHERE='M' & POMOC.RODZ='U')
   | FAKS.T().KOR='T' & FAP.POZP<>0;
:: jeśli rozpisano rezerwacje to nie wolno poprawiać indeksu materiałowego - tymczasowa
{? _wyn & FAKS.WHERE='G' || _wyn:=exec('FindInSet','#table','ZK_P','FAP',FAP.ref)=null ?};
HELP.MOD_IND:=FAP.M;
:: sprawdzenie czy nie ma powiazań z PZ-tkami
{? _wyn & FAKS.SZ='Z' & FAP.M<>null() & ~exec('czyfapdk','faktury_wspolne',$FAP.ref,,0,,FAP.M().RODZ='U')
|| _wyn:=0
?};
_wyn


\pr_mgrf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: przed redakcją zmiennej POMOC.MGRF
::  OLD: \pr_mgrf/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? exec('be_mfap','faktury_poz')=0 | (FAKS.STS<>null & FAKS.STS().MGR<>'T')
|| _wyn:=0
?};
_wyn


\po_mgrf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: po redakcji zmiennej POMOC.MGRF
::  OLD: \po_mgrf/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
POMOC.MGR:=POMOC.MGRF;
{? D_HELP.M_FAP<>''
|| M.cntx_psh();
   M.index('MATKTM');
   M.prefix();
   {? M.find_key(D_HELP.M_FAP,D_HELP.M_FAP) || FAP.M:=M.ref ?};
   M.cntx_pop()
?};
1


\bd_mfap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: formula przed wyświetleniem pola D_HELP.M_FAP
::  OLD: \bd_mfap/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
D_HELP.M_FAP:={? FAP.M<>null || FAP.M().KTM || '' ?};
:: sprawdzenie czy pole jest do edycji
exec('pw_fap_m','faktury_poz')


\ae_mfap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: po edycji M na pozycji faktury
::  OLD: \ae_mfap/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
ATR.MJS:='FAP';
_wyn:=0;
_set_efld_opt:=0;

{? D_HELP.M_FAP='' & FAKS.WHERE<>'L'
||
   D_HELP.MR:=null;
   {? POMOC.RODZ='U'
   || exec('find_sql','#table','M','KTM;N;KODK'
       ,{? FAKS.T().KOR='T' || '' || 'M.A=\'\'T\'\' and ' ?}+'M.RODZ=\'\'U\'\''
       ,'D_HELP.MR','20@Indeks usługi;40@Nazwa usługi;20@Kod kreskowy;','REDU','','')
   ||
      {? FAKS.WHERE='G'
      || exec('find_sql','#table','M','KTM;N;KODK;*(R)IL','M.A=\'\'T\'\' and M.RODZ=\'\'T\'\'','D_HELP.MR'
          ,'20@Indeks materiału;40@Nazwa materiału;20@Kod kreskowy;12@Dostępne;','RED','obl_stf#faktury_poz','*IL')
      || exec('find_sql','#table','M','KTM;N;KODK','M.A=\'\'T\'\' and M.RODZ=\'\'T\'\'','D_HELP.MR'
          ,'20@Indeks materiału;40@Nazwa materiału;20@Kod kreskowy;','RED','','')
      ?}
   ?};
   {? D_HELP.MR<>null
   ||
      FAP.M:=D_HELP.MR; D_HELP.M_FAP:=FAP.M().KTM;
      POMOC.MGR:=POMOC.MGRF:=FAP.M().MGR
   ?}
?};

{? D_HELP.M_FAP='' & FAKS.WHERE<>'L'
|| FUN.info('Niewypełnione pole.'@)
|| _change:=FAP.M().KTM<>D_HELP.M_FAP;
   {? FAKS.T().HIS='T'  & exec('get','#params',300215,2)='T'
   || FKOR.fld_fml('IL','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $FAP.M().DOKL || $ST.DOKL ?}");
      FKOR.fld_fml('IL','DISPLAY_FORMAT',"'out_prec='+{? (2+cur_kwin())='e_' || $FAP.M().DOKL || $ST.DOKL ?}");
      FKOR.fld_fml('RAB','EDIT_FORMAT',"'in_prec=2'")
   ?};
   M.cntx_psh();
   {? FAKS.T().KOR='T' & POMOC.RODZ='U'
   || M.index('RODZ');
      M.prefix(POMOC.RODZ,D_HELP.M_FAP,D_HELP.M_FAP)
   || M.index('ARODZ');
      M.prefix('T',POMOC.RODZ,D_HELP.M_FAP,D_HELP.M_FAP)
   ?};
   {? M.first()
   || FAP.M:=M.ref();
      POMOC.MGR:=POMOC.MGRF:=FAP.M().MGR;
      _wyn:=1
   ||
::    dodatkowo poszukujemy pierwszego indeksu wg czesci KTM-u
      {? form(D_HELP.M_FAP)<>''
      || M.index('ARODZ');
         M.prefix('T',POMOC.RODZ);
         {? M.find_key(D_HELP.M_FAP)
         || FAP.M:=M.ref; D_HELP.M_FAP:=M.KTM;
            POMOC.MGR:=POMOC.MGRF:=FAP.M().MGR;
            _wyn:=1
         ?}
      ?};
      {? ~_wyn
      || {? FAKS.WHERE='L'
         || FAP.M:=null;
            _wyn:=1
         || {? POMOC.RODZ='U'
            || FUN.info('Brak usługi w słowniku.'@)
            || FUN.info('Brak materiału w słowniku.'@)
            ?}
         ?}
      ?}
   ?};
:: sprawdzenie czy dokument powstał z xml w formacie UFD
   _ufd:=0;
   {? FAP.M
   ||
      DOKUM.cntx_psh();
      DOKUM.index('BL_2');
      DOKUM.prefix(REF.FIRMA,'T',$FAKS.ref());
      _loop:=DOKUM.first();
      {!
      |? _loop
      |!
         _ufd:=
            {? HELP.MOD_IND=null()
            ||
               exec('czy_ufd_z_ocr','edi_fo_ufd',DOKUM.ref())
            ||
               exec('czy_ufd','edi_fo_ufd',DOKUM.ref())
            ?};
         _loop:=_ufd=0 & DOKUM.next()
      !};
      DOKUM.cntx_pop();
      {? HELP.MOD_IND & FAP.M<>HELP.MOD_IND & _ufd
      ||
         _ufd:=~FUN.ask('Dokument jest utworzony z dokumentu elektronicznego.\nCzy przeliczyć pozycję?'@)
      ?}
   ?};
:: wyznaczenie stawki VAT
   {? FAP.SV=null | FAP.M<>HELP.MOD_IND & _ufd=0
   ||
      {? KSTE.KRAJ=null
      ||
         FUN.info('Niewypełnione pole "Kraj" w parametrch oddziału.'@);
         {? FAP.FAKS().T().HIS='T'
         || FKOR.SV:=null
         ?};
         FAP.SV:=null
      ||
         _st_vat:=exec('m_vat','material',FAP.M,FAP.FAKS().KH,
            FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N',FAKS.AR,FAKS.AM,,,,FAKS.SZ);
         {? TYPYSP.HIS='T'
         || _st_vat:=exec('m_vat','material',FAP.M,FAP.FAKS().KH,
               FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N',FKOR.DF~1,FKOR.DF~2,,,,FAKS.SZ);
            FKOR.SV:=_st_vat
         ?};
         FAP.SV:=_st_vat
      ?};
      FAP.JM:=FAP.M().J
   ?};
   exec('fill_fld2prn','faktury_poz');
   {? FAP.M<>null & FAP.M<>HELP.MOD_IND & _ufd=0
   ||
::    zerowanie ilości na fakturze typu G
      {? FAP.FAKS().T().KOR='N' & FAP.FAKS().WHERE='G' || exec('zm_g_ind','faktury_poz') ?};
::    zerowanie wartosci sprzedazowych
      {? exec('FindInSet','#table','FAKZ2FAP','FAP',FAP.ref())=null() & FAP.FAKS().EDOKUM=''
      || FAP.CWAL:=FAP.CPR:=FAP.RAB:=FAP.RABK:=0
      ?};
      exec('clr_promo_tap','ceny');
      exec('liczfak','faktury_wspolne');
::    przeliczniki jednostek
      FAP.J2:=FAP.JM:=FAP.M().J;
      {? _change
      || {? FAP.M().J2<>null()
         || FAP.T2:='M';
            FAP.WS2:=exec('oblWSP','jm',ZD_POZ.M);
            FAP.IL2:=0;
            exec('oblJMG','jm',FAP,'IL')
         || FAP.T2:=1+(1-BEER.FMJM);
            FAP.WS2:=0;
::            FAP.J2:=null();
            FAP.IL2:=0
         ?}
      ?};
      exec('ustafprz','faktury_wspolne',FAP.M,FAP.M().J);
      {? FAP.WS2=0 & FAP.J2=null()
      || FAP.WS2:=1; FAP.JM:=FAP.J2:=FAP.M().J; FAP.IL2:=FAP.IL
      ?};
::    intrastat
      exec('istatr_faks2fap','faktury_nag',1);
      exec('liczfak_ist','faktury_wspolne',1,1);
::    odwrotne obciążenie
      {? exec('oo_zak','faktury_poz') || FAP.OO:=FAP.M().OO ?};
::    wymagany split payment
      FAP.SP_WYM:='N'; {? FAP.M || FAP.SP_WYM:=FAP.M().SP_WYM ?};
::    jpk
      {? var_pres('JPK_GTU',MGR)>0
      ||
         {? FAP.M().JPK_GTU=null & exec('get','#params', 300120, 2)='T'
         || FAP.JPK_GTU:=FAP.M().MGR().JPK_GTU
         || FAP.JPK_GTU:=FAP.M().JPK_GTU
         ?}
      ?}
   |? ~JM.f_active()
   || {? _change
      || {? FAP.M().J2<>null()
         || FAP.T2:='M';
            FAP.WS2:=exec('oblWSP','jm',ZD_POZ.M);
            FAP.IL2:=0;
            exec('oblJMG','jm',FAP,'IL')
         || FAP.T2:=1+(1-BEER.FMJM);
            FAP.WS2:=0;
            FAP.J2:=null();
            FAP.IL2:=0
         ?}
      ?};
      exec('ustafprz','faktury_wspolne',FAP.M,FAP.M().J);
      {? FAP.WS2=0 & FAP.J2=null()
      || FAP.WS2:=1; FAP.JM:=FAP.J2:=FAP.M().J; FAP.IL2:=FAP.IL
      ?}
   ?};
   M.cntx_pop();
   _set_efld_opt:=1
?};
{? _wyn
|| _prz:=0;
   {? FAP.M<>null() & FAKS.MAG<>null() & (1+FAKS.MAG().TYP)='E'
   || _time:=exec('spr_dttm','magazyn_inw',FAKS.MAG,FAKS.D,1);
      {? (*_time)>(*time(23,59,0)) || _time:=time(0,0,0) ?};
      _prz:=exec('sprprzec','magazyn_przec',FAKS.MAG,FAKS.D,FAP.M,_time);
      {? _prz=3
      || FUN.info('Podany indeks materiałowy znajduję się na otwartym arkuszu przecen\n'
          'lub dokumenty przeceny nie są zaakceptowane\n'
          '(data dokumentu jest mniejsza lub równa dacie przeceny).\n'
          'Dołączenie do dokumentu niemożliwe.'@);
         _wyn:=0
      |? _prz=4
      || FUN.info('Podany indeks materiałowy znajduję się na zamkniętym arkuszu przecen\n'
          '(data dokumentu jest mniejsza lub równa dacie przeceny).\n'
          'Dołączenie do dokumentu niemożliwe.'@);
         _wyn:=0
      ?}
   ?};
   {? _wyn
   || exec('juz_jest','material',{? FAP.FAKS().SZ='S' || 'F' || 'K' ?}
       ,FAP.FAKS,FAP.M,{? ~-(6+menu_txt())='POPRAW' | -(menu_txt())='korekta' | ~-(3+menu_txt())='WYBIERZ'
                       || FAP.ref()
                       || null
                       ?})
   ?}
?};
{? _wyn & FAKS.T().KOR='T'
|| exec('zmvat_chk','faktury_wspolne',FAP.M,FAKS.KH,FAKS.AR,FAKS.AM,FAP.SV,
      FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N',FAKS.SZ)
?};
{? _wyn & FAP.FAKS().T().KOR='N' & ~((';LG'*FAP.FAKS().WHERE)>1)
 & (exec('fap_win_btn','faktury_poz')*(FAP.win_edit('?')+','))
|| {? FAP.M<>null & FAP.M().M_ATR<>null
   || FAP.btn_eopt(FAP.win_edit('?'),'ATRYB','state=normal')
   || FAP.btn_eopt(FAP.win_edit('?'),'ATRYB','state=grayed')
   ?}
?};
exec('dk_atr_dict','mat_atr');
{? _wyn
|| {? FAKS.SZ='S' & exec('get','#params',300200,2)='T' & FAP.M<>HELP.MOD_IND
   || FAP.KK:=exec('m_kk','material',FAP.M)
   ?};
   {? FAP.M<>HELP.MOD_IND
   || Plugin.run('POZLOG_M_001','FAP')
   ?};
   _set_efld_opt:=1
?};
{? _set_efld_opt
||
   exec('set_efld_opt','faktury_poz');
   win_disp()
?};
_wyn


\fap_win_btn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MaX [12.30]
:: OPIS: Zwraca w jakich okienkach są przyciski z chechami
::  OLD: \fap_win_btn/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
'WRED,WRED1,WREDZ,WRED_SS,'


\zm_g_ind
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.41]
:: OPIS: po zmianie indeksu na pozycji faktury zeruje ilości i rezerwacje
::  OLD: \zm_g_ind/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_mat:=null();
_nrk:=0;
_ilr:=0;
REZ.index('FAPR');
REZ.prefix($FAP.ref(),);
{? REZ.first() || _mat:=REZ.M; {! |? _ilr+=REZ.ILR; _nrk:=REZ.NRK; REZ.del() !} || _mat:=null() ?};
{? _mat<>null || exec('aktu_rez','rezerwacje',_mat,_nrk,_ilr) ?};
FAP.IL:=0;
FAP.IL2:=0;
~~


\f3_mfap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: f3 na M w pozycjach dokumentów sprzedaży
::   WY: wybrany KTM lub string pusty
::  OLD: \f3_mfap/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_oldmfap:=D_HELP.M_FAP;
JM.f_clear;
JM.win_dict('WER');
{? (FAKS.STS<>null & FAKS.STS().MGR<>'T') | FAKS.T().KOR='T' || POMOC.MGR:=POMOC.MGRF:=null ?};

ZAKR.MATU:='A';
{? POMOC.RODZ='' || POMOC.RODZ:='T' ?};
{? POMOC.MGRF().RODZ<>POMOC.RODZ || POMOC.MGR:=POMOC.MGRF:=null ?};

BEER.KH:=FAKS.KH;
{? HELP.WHERE='G' & FAKS.MAG<>null & POMOC.RODZ='T'
|| {? POMOC.MGRF<>null
   || SM.index('ASAG');
      SM.prefix(FAKS.MAG,'N','T',POMOC.MGRF)
   || SM.index('ASA');
      SM.prefix(FAKS.MAG,'T')
   ?};
   SM.find_key(D_HELP.M_FAP)
|| {? POMOC.MGRF<>null
   || {? FAKS.T().KOR='T' & POMOC.RODZ='U'
      || M.index('MGM');
         M.prefix(POMOC.RODZ,POMOC.MGRF)
      || M.index('AMGM');
         M.prefix('T',POMOC.RODZ,POMOC.MGRF)
      ?}
   || {? FAKS.T().KOR='T' & POMOC.RODZ='U'
      || M.index('RODZ');
         M.prefix(POMOC.RODZ)
      || M.index('ARODZ');
         M.prefix('T',POMOC.RODZ)
      ?}
   ?};
   M.find_key(D_HELP.M_FAP)
?};
{? FAKS.WHERE='P' | FAKS.WHERE='L' | FAKS.SZ='Z'
|| _patt:='SZUK'+{? POMOC.RODZ='U' || 'U' || '_1' ?};
   _edit:='RED'+{? POMOC.RODZ='U' || 'U' || '' ?};
   _sele:='NL_WER'+{? POMOC.RODZ='U' || 'U' || '' ?};
   M.win_patt(_patt);
   M.win_edit(_edit);
   M.win_sel(_sele);
   M.actions(_sele,'')
|| {? POMOC.RODZ='U'
   || M.win_patt('SZUKU');
      M.win_edit('REDU');
      M.win_sel('NL_WERU')
   |? HELP.WHERE='G' & FAKS.MAG<>null
   || M.win_patt('SZUK'+{? exec('FindInSet','#table','M_ATR','TYP','B')<>null() || '_1' || '' ?});
      M.win_edit('RED');
      SM.win_sel('WER')
   || M.win_patt('SZUK'+{? exec('FindInSet','#table','M_ATR','TYP','B')<>null() || '_1' || '' ?});
      M.win_edit('RED');
::      M.win_sel({? HELP.WHERE<>'G' || 'NL_WERST' || exec('okngrp_m','faktury_poz') ?});
      M.win_sel('SEL');
      M.actions('SEL','')
   ?}
?};
{? HELP.WHERE='G' & FAKS.MAG<>null & POMOC.RODZ='T'
|| {? SM.select(,1,10)
   || D_HELP.M_FAP:=SM.M().KTM;_wyn:=SM.M().KTM; FAP.M:=SM.M;
      POMOC.MGR:=POMOC.MGRF:=FAP.M().MGR
   || {? _oldmfap<>'' & (_refm:=exec('FindInSet','#table','M','MATKTM',_oldmfap,_oldmfap))<>null
      || D_HELP.M_FAP:=_oldmfap;
         _wyn:=_oldmfap;
         FAP.M:=_refm
      ?}
   ?}
|| {? M.select(,1,10)
   || D_HELP.M_FAP:=M.KTM;_wyn:=M.KTM; FAP.M:=M.ref();
      POMOC.MGR:=POMOC.MGRF:=FAP.M().MGR
   || {? _oldmfap<>'' & (_refm:=exec('FindInSet','#table','M','MATKTM',_oldmfap,_oldmfap))<>null
      || D_HELP.M_FAP:=_oldmfap;
         _wyn:=_oldmfap;
         FAP.M:=_refm
      ?}
   ?}
?};
BEER.KH:=null;
_wyn


\bd_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed wyświetleniem ilosci (zmienna do redakcji ilości na pozycji dokumentu sprzedaży), DISP.ILF
::  OLD: \bd_il/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
DISP.ILF:=FAP.IL;
:: sprawdzenie czy pole jest do edycji
exec('pw_fap_m','faktury_poz')


\be_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed edycją ilości (zmienna do redakcji ilości na pozycji dokumentu sprzedaży),
::       pole DISP.ILF
::   WY: 1/0
::  OLD: \be_il/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;

_sprzsc:=exec('get','#params',300700,2)='T';
_onemag:=FAKS.WHERE='G' & FAP.M().RODZ='T'
       & {? FAKS.MAG<>null
         || (1+FAKS.MAG().TYP)<>'D'
         |? _sprzsc | var_pres('__winbad')=1
         || {? (1+ST.MAG().TYP)<>'D' || 1 || 0 ?}
         || 0
         ?};
{? exec('be_fapil','faktury_poz') || _wyn:=1 ?};
{? _wyn & FAKS.WHERE='G' & POMOC.RODZ='T' & FAP.M=null
|| _wyn:=0
|? _wyn & FAKS.WHERE='G' & POMOC.RODZ='T' & FAP.M<>null
|| _czydk_l:=exec('FindInSet','#table','DK_L','FAP',null,FAP.ref)<>null;
   {? _czydk_l & FUN.ask('Przypisano wymiary do pozycji faktury\n'
                         'Modyfikacja ilości możliwa wyłącznie po usunięciu wymiarów.\n\n'
                         'Czy usunąć wymiary?'@)
   || do();
      DK_L.index('FAP');
      DK_L.prefix(FAP.ref(),null);
      {? DK_L.first()
      || {!
         |? _usun:=DK_L.del(,1);
            {? ~_usun || undo(); 0
            |? _usun=1 || _czydk_l:=0; 0
            || 1
            ?}
         !}
      ?};
      end()
   ?};
   {? _onemag & ~_czydk_l
   || _wyn:=1
   |? ~_onemag & ((1+menu_txt())<>'P' | ~_czydk_l)
   || exec('utw_zk_tymc','zamsiw_wspolne'); __uzup_z(FAP.M);
      _tab:=exec('f3_il','faktury_poz');
      DISP.ILF:=_tab[2];
      FAP.IL2:=_tab[1];
      obj_del(_tab);
      exec('ae_il','faktury_poz');

      win_disp;
      _wyn:=0
   || _wyn:=0
   ?}
?};
_wyn


\f3_il_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: akcja f3 na ilosci (zmienna do redakcji ilosci na pozycji dokumentu sprzedazy), pole DISP.ILF
::   WE: [_a] - wymagana ilosc do rozpisania
::       [_b] - ilosc do rozpisania (bez okienka)
::   WY: zwraca ilosc jaka zostala rozpisana wg magazynow
::  OLD: \f3_il/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------

{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};

_wyn:=exec('f3_il','faktury_poz',_a,_b);
echo();
_sprzsc:=exec('get','#params',300700,2)='T';
_onemag:=FAKS.WHERE='G' & FAP.M().RODZ='T'
       & {? FAKS.MAG<>null
         || (1+FAKS.MAG().TYP)<>'D'
         |? _sprzsc | var_pres('__winbad')=1
         || {? (1+ST.MAG().TYP)<>'D' || 1 || 0 ?}
         || 0
         ?};
{? ~_b & _onemag
|| FAP.IL2:=_wyn[1];
   _dokl_s:=exec('jaka_dok_mjm','jm',FAP.M,FAP.J2,FAP.JM);
   DISP.ILF:=FAP.IL:={? FAP.WS2<>1 || FAP.IL2/FAP.WS2 $ _dokl_s || FAP.IL2 ?};
   exec('aktfil2','faktury_wspolne');
   win_disp()
?};
_wyn


\fap_us_z
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: usuwanie powiązań do ZLP podczas usuwania poz zlecenia dla bieżącej pozycji faktury
::  OLD: \fap_us_z/um.fml
::----------------------------------------------------------------------------------------------------------------------
_fap_ref:=FAP.ref();
_fap_z:=FAP.Z;

FPACZ.cntx_psh();
FAKS.cntx_psh();
FAP.cntx_psh();
ZLP.cntx_psh();

:: sprawdzanie wielu miesięcy jeżeli faktura została
:: wystawiona z paczki - mozliwość połączenia do ZLP z wielu okresów

{? FAP.FAKS().FPACZKA<>null
||
   _od:=FAP.FAKS().FPACZKA().OD~2;
   _do:=FAP.FAKS().FPACZKA().DO~2;

   {! _il:=_od.._do
   |!
      exec('zlm_open','open_tab',ST.ODDZ,2-$(FAP.FAKS().FPACZKA().OD~1),form(_il,-2));
      {? _fap_z='D'
      || _ndx:='FAPU'
      || _ndx:='FAP'
      ?};
      {!
      |?
         ZLP.index(_ndx);
         ZLP.prefix(_fap_ref);
         _first:=ZLP.first();
         {? _first
         ||
            ZLP.clear();
            {? _fap_z='D'
            || ZLP.FAPU:=null
            || ZLP.FAP:=null
            ?};
            ZLP.FAKS:='N';
            ZLP.put()
         ?};
         _first
      !};
      {? FAP.count>0
      ||
         {? _fap_z='D'
         || _ndx:='FAP'
         || _ndx:='FAPU'
         ?};
         {!
         |?
            ZLP.index(_ndx);
            ZLP.prefix(_fap_ref);
            _first:=ZLP.first();
            {? _first
            ||
               ZLP.clear();
               {? _fap_z='D'
               || ZLP.FAP:=null
               || ZLP.FAPU:=null
               ?};
               ZLP.FAKS:='N';
               ZLP.put()
            ?};
            _first
         !}
      ?}
   !};
   exec('zlm_open','open_tab',ST.ODDZ,2-$ST.AR,form(ST.AM,-2))
||
   exec('zlm_open','open_tab',ST.ODDZ,2-$(FAP.FAKS().AR),form(FAP.FAKS().AM,-2));
   {? _fap_z='D'
   || _ndx:='FAPU'
   || _ndx:='FAP'
   ?};
   {!
   |?
      ZLP.index(_ndx);
      ZLP.prefix(_fap_ref);
      _first:=ZLP.first();
      {? _first
      || ZLP.clear();
        {? _fap_z='D'
        || ZLP.FAPU:=null
        || ZLP.FAP:=null
        ?};
        ZLP.FAKS:='N';
        ZLP.put()
      ?};
      _first
   !};
   exec('zlm_open','open_tab',ST.ODDZ,2-$ST.AR,form(ST.AM,-2))
?};

ZLP.cntx_pop();
FAP.cntx_pop();
FAKS.cntx_pop();
FPACZ.cntx_pop();
''


\fapdkdel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: usunięcie powiazań pozycja faktury <-> pozycja dokumentu z poziomu faktury
::   WE: _a - ref SQL-owy FAP-a
::       _b - ref FAP-a domyślnie null
::       _c - rodzaj Z-zakup E-wewnętrzna
::  OLD: \fapdkdel/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
FAP.cntx_psh;
FAP2DK.index('FAP');
FAP2DK.prefix(_c,_a);
{? FAP2DK.first || {! |? exec('fapk_clear','magdok_wspolne'); FAP2DK.del !} ?};
_faks:=exec('FindAndGet','#table',FAP,FAP.ref(),,"$FAKS",'');
{? _faks<>'' || exec('FindAndGet','#table',FAKS,_faks,,"{? GEN=1 || GEN:=0; put(1) ?}") ?};
{? _b<>null
|| FAP_K.index('FAP_K');
   FAP_K.prefix(_b);
   {? FAP_K.first || {! |? FAP_K.del !} ?}
?};
FAP.cntx_pop


\bl_fkasv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: blankuje wartość FAKS dla FAKSV
::  OLD: \bl_fkasv/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
{? BEER.SZ='Z' || $FAKS.ref || '' ?}


\pwwalfsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed wyświetleniem pola waluta na FAKSV
::  OLD: \pwwalfsv/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.WAL


\pwkrsfsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed wyswietleniem pola kurs na FAKSV
::  OLD: \pwkrsfsv/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
{? BEER.SZ='Z' & FAKS.WAL<>INFO.NAROD || FAKS.KRS || 0 ?}


\edytuj_pozycje
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: edycja pozycji faktury z poziomu okienka redagowania FAKS-a
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
{? _res || exec('pozycje_fak','faktury_poz',1) ?};
''


\spr_makt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: sprawdza czy dla korekta ilosciowej wszystkie indeksy sa aktywne
::   WE:  _a  - FAKS.ref korekty
::       [_b] - czy uwzgledniac tylko pozycje materialowe (0-wszystkie, 1- tak)
::   WY: 1-tak jest OK  0-nie
::  OLD: \spr_makt/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};

_wyn:=1;
FAKS.cntx_psh();
FAP.cntx_psh();
FAP.index('FAP');
FAP.prefix(_a);
{? FAP.first()
|| {!
   |?
      {? exec('fap_war','faktury_poz',_b)='-I' & FAP.M().A='N'
      ||
         _wyn:=0;
         {? var_pres('__kom')>100
         || __kom.add('Materiał %1 na pozycji %2 jest nieaaktywny.'@[FAP.M().KTM,$FAP.POZ],7)
         || FUN.info('Materiał: %1 - %2\nna pozycji: %3 jest nieaaktywny.\n'
                     'Niemożliwa akceptacja korekty ze zwrotem na magazyn.'@[FAP.M().KTM,FAP.M().N,$FAP.POZ])
         ?}
      ?};
      _wyn & FAP.next()
   !}
?};
FAP.cntx_pop();
FAKS.cntx_pop();
_wyn


\fap_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: sprawdza jakie byly zmiany na pozycji korekty
::       - na razie tylko zmiejszenie ilosci
::   WE: [_a] - czy uwzgledniac tylko pozycje materialowe (0-wszystkie, 1- tak)
::       [_b] - czy uwzgledniac rozpisanie pozycji domyslnie 1-tak 0-nie
::   WY: '-I' - zmniejszenie ilosci
::       '+I' - zwiekszenie ilosci
::  OLD: \fap_war/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};

_wyn:='';
{? FAP.FAKS().KOR<>'' & {? _b || ~FAP.POZP || 1 ?}
|| _il:=0;
   _bil:=0;
   {? _b
   ||
      _mat:=FAP.M;
      FAKS.cntx_psh();
      FAP.cntx_psh();
      FAP.index('POZP');
      FAP.prefix(FAP.FAKS,FAP.POZ);
      {? FAP.first()
      || {!
         |? {? FAP.M=_mat
            ||
               exec('wysw_kor','faktury_poz');
               {? _a=0 | (_a=1 & FAP.M().RODZ='T')
               || _bil+=FKOR.BIL
               ?}
            ?};
            FAP.next()
         !}
      ?};
      FAP.cntx_pop();
      FAKS.cntx_pop()
   ?};
   exec('wysw_kor','faktury_poz');
   {? _a=0 | (_a=1 & FAP.M().RODZ='T')
   || _il+=FKOR.IL;
      _bil+=FKOR.BIL;
      {? _il>_bil || _wyn:='-I'
      |? _il<_bil || _wyn:='+I'
      ?}
   ?}
?};
_wyn


\spr_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: sprawdza czy dokument ma pozycje (FAKS w buforze)
::   WE: _a - FAKS.ref()
::       _b - wolane z akceptacji
::   WY: tekst komunikatu
::  OLD: \spr_poz/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_txt:='';
_Tab:=tab_tmp(1,
   'SV'     ,'STRING[8]'   ,'stawka vat',
   'BWAL'   ,'REAL'        ,'wartość brutto w walucie',
   'WWAL_P' ,'REAL'        ,'wartość netto',
   'VWAL_P' ,'REAL'        ,'kwota vat',
   'BWAL_P' ,'REAL'        ,'wartość brutto',
   'POZY'   ,'STRING[128]' ,'pozycje dokumentu',
   'V_BWAL'   ,'REAL'        ,'wartość brutto w walucie wg stawek VAT',
   'V_WWAL_P' ,'REAL'        ,'wartość netto wg stawek VAT',
   'V_VWAL_P' ,'REAL'        ,'kwota vat wg stawek VAT',
   'V_BWAL_P' ,'REAL'        ,'wartość brutto wg stawek VAT');
_ok:=1;
FAP.cntx_psh();
FAP.index('FAP');
FAP.prefix(_a);
{? FAKS.SZ='Z'
||
   _korekta:=FAKS.T().KOR='T';
:: dla faktur zakupowych
   {? ~FAP.first() & exec('FindInSet','#table','FAKSV','FAKS_SV',$_a)=null
   ||
      _txt+='Dokument nie posiada pozycji.\n'@+'Dokument nie ma pozycji VAT.\n'@;
      _ok:=0
   |? FAP.first()
   ||
::    kontrola cen zakupu
      {!
      |?
         {? _korekta
         || exec('wysw_kor','faktury_poz');
            {? _Tab.find_key(FAP.SV().KOD)
            || _Tab.BWAL+=FKOR.BBWAL;
               _Tab.WWAL_P+=FKOR.BWWAL_P;
               _Tab.VWAL_P+=FKOR.BVWAL_P;
               _Tab.BWAL_P+=FKOR.BBWAL_P;
               {? _Tab.POZY<>''
               || _Tab.POZY+=','
               ?};
               _Tab.POZY+=$FAP.POZ;
               _Tab.put
            || _Tab.SV:=FAP.SV().KOD;
               _Tab.BWAL:=FKOR.BBWAL;
               _Tab.WWAL_P:=FKOR.BWWAL_P;
               _Tab.VWAL_P:=FKOR.BVWAL_P;
               _Tab.BWAL_P:=FKOR.BBWAL_P;
               _Tab.POZY:=$FAP.POZ;
               _Tab.V_BWAL:=0;
               _Tab.V_WWAL_P:=0;
               _Tab.V_VWAL_P:=0;
               _Tab.V_BWAL_P:=0;
               _Tab.add
            ?};
            {? FAP.POZP=0
            || {? _Tab.find_key(FKOR.SV().KOD)
               || _Tab.BWAL-=FKOR.BWAL;
                  _Tab.WWAL_P-=FKOR.WWAL_P;
                  _Tab.VWAL_P-=FKOR.VWAL_P;
                  _Tab.BWAL_P-=FKOR.BWAL_P;
                  _Tab.put
               || _Tab.SV:=FKOR.SV().KOD;
                  _Tab.BWAL:=-FKOR.BWAL;
                  _Tab.WWAL_P:=-FKOR.WWAL_P;
                  _Tab.VWAL_P:=-FKOR.VWAL_P;
                  _Tab.BWAL_P:=-FKOR.BWAL_P;
                  _Tab.V_BWAL:=0;
                  _Tab.V_WWAL_P:=0;
                  _Tab.V_VWAL_P:=0;
                  _Tab.V_BWAL_P:=0;
                  _Tab.add
               ?}
            ?}
         || {? _Tab.find_key(FAP.SV().KOD)
            || _Tab.BWAL+=FAP.BWAL;
               _Tab.WWAL_P+=FAP.WWAL_P;
               _Tab.VWAL_P+=FAP.VWAL_P;
               _Tab.BWAL_P+=FAP.BWAL_P;
               {? _Tab.POZY<>''
               || _Tab.POZY+=','
               ?};
               _Tab.POZY+=$FAP.POZ;
               _Tab.put
            || _Tab.SV:=FAP.SV().KOD;
               _Tab.BWAL:=FAP.BWAL;
               _Tab.WWAL_P:=FAP.WWAL_P;
               _Tab.VWAL_P:=FAP.VWAL_P;
               _Tab.BWAL_P:=FAP.BWAL_P;
               _Tab.POZY:=$FAP.POZ;
               _Tab.V_BWAL:=0;
               _Tab.V_WWAL_P:=0;
               _Tab.V_VWAL_P:=0;
               _Tab.V_BWAL_P:=0;
               _Tab.add
            ?}
         ?};
         {? FAKS.T().KOR='N' & FAP.CPR=0
         || _txt+='Pozycja %1 faktury nie ma podanej ceny zakupu.\n'@[form(FAP.POZ,,0,'99')]
         ?};
         _txt='' & FAP.next()
      !}
   ?};
   {? _txt='' &  FAKS.WHERE<>'L'
   ||
      FAKSV.cntx_psh;
      FAKSV.index('FAKS_SV'); FAKSV.prefix($FAKS.ref);
      {? FAKSV.first
      || {!
         |?
            {? _Tab.find_key(FAKSV.SV().KOD)
            || _Tab.V_BWAL+=FAKSV.WW;
               _Tab.V_WWAL_P+=FAKSV.WN;
               _Tab.V_VWAL_P+=FAKSV.WV;
               _Tab.V_BWAL_P+=FAKSV.WB;
               _Tab.put
            || _Tab.SV:=FAKSV.SV().KOD;
               _Tab.BWAL:=0;
               _Tab.WWAL_P:=0;
               _Tab.VWAL_P:=0;
               _Tab.BWAL_P:=0;
               _Tab.POZY:='';
               _Tab.V_BWAL:=FAKSV.WW;
               _Tab.V_WWAL_P:=FAKSV.WN;
               _Tab.V_VWAL_P:=FAKSV.WV;
               _Tab.V_BWAL_P:=FAKSV.WB;
               _Tab.add
            ?};
            FAKSV.next
         !}
      ?};
      FAKSV.cntx_pop;
      {? _Tab.first
      || {!
         |?
            _txt1:='';
            {? _Tab.BWAL-_Tab.V_BWAL<>0
            || _txt1:='Niezgodność kwoty brutto w walucie wg pozycji (%1) i stawki VAT %2 (%3), różnica %4.'@
                      [form(_Tab.BWAL,,2),_Tab.SV,form(_Tab.V_BWAL,,2),form(_Tab.BWAL-_Tab.V_BWAL,,2)];
               {? _txt<>''
               || _txt+='\n'
               ?};
               _txt+=_txt1
            ?};
            {? _Tab.WWAL_P-_Tab.V_WWAL_P<>0
            || _txt1:='Niezgodność kwoty netto wg pozycji (%1) i stawki VAT %2 (%3), różnica %4.'@
                      [form(_Tab.WWAL_P,,2),_Tab.SV,form(_Tab.V_WWAL_P,,2),form(_Tab.WWAL_P-_Tab.V_WWAL_P,,2)];
               {? _txt<>''
               || _txt+='\n'
               ?};
               _txt+=_txt1
            ?};
            {? _Tab.VWAL_P-_Tab.V_VWAL_P<>0
            || _txt1:='Niezgodność kwoty VAT wg pozycji (%1) i stawki VAT %2 (%3), różnica %4.'@
                      [form(_Tab.VWAL_P,,2),_Tab.SV,form(_Tab.V_VWAL_P,,2),form(_Tab.VWAL_P-_Tab.V_VWAL_P,,2)];
               {? _txt<>''
               || _txt+='\n'
               ?};
               _txt+=_txt1
            ?};
            {? _Tab.BWAL_P-_Tab.V_BWAL_P<>0
            || _txt1:='Niezgodność kwoty brutto wg pozycji (%1) i stawki VAT %2 (%3), różnica %4.'@
                      [form(_Tab.BWAL_P,,2),_Tab.SV,form(_Tab.V_BWAL_P,,2),form(_Tab.BWAL_P-_Tab.V_BWAL_P,,2)];
               {? _txt<>''
               || _txt+='\n'
               ?};
               _txt+=_txt1
            ?};
            {? _txt1<>''
            || {? _Tab.POZY=''
               || _pozy:='brak'@
               |? _Tab.size()=1
               || _pozy:='wszystkie'@
               || _pozy:=_Tab.POZY
               ?};
               _txt1:='Pozycje ze stawką VAT %1: %2.'@[_Tab.SV,_pozy];
               {? _txt<>''
               || _txt+='\n'
               ?};
               _txt+=_txt1
            ?};
            _Tab.next
         !}
      ?}
   ?}
|| {? ~FAP.first()
   ||
      _txt+='Dokument nie posiada pozycji.\n'@;
      _ok:=0
   |? FAP.FAKS().T().KOR='T' & FAP.FAKS().DZ=date(0,0,0) & FAKS.WHERE<>'L'
   || {!
      |? {? FAP.M().RODZ='T' & FAP.IL<0
         || _txt+='Należy podać datę zwrotu.'@;
            _ok:=0;
            0
         || FAP.next()
         ?}
      !}
   ?}
?};
{? _ok & FAKS.T().KOR='T' & exec('get','#params',{? FAKS.SZ='S' || 300210 || 200210 ?},2)='T'
||
   _loop:=FAP.first();
   {!
   |? _loop
   |!
      exec('wysw_kor','faktury_poz');
      {? FAP.POWKOR=null() & exec('spr_kor','faktury_poz')=1
      ||
         _txt+='Pozycja nr %1: należy podać powód korekty.'@[$FAP.POZ];
         _ok:=0
      ?};
      _loop:=_ok & FAP.next()
   !}
?};
FAP.cntx_pop();
::{? _b || _txt:=STR.gsub(_txt,'\n',' ') ?};
_txt


\czy_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: czy korekta ilosciowa
::   WE:  _a  - FAKS.ref korekty
::       [_b] - czy uwzgledniac tylko pozycje materialowe (0-wszystkie, 1- tak)
::   WY: '-I' - zmniejszenie ilosci; '+I' - zwiekszenie ilosci
::  OLD: \czy_il/funkcje.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};

_wyn:='';
_act:=FAKS.name();
_new:=ref_name(_a);
FAKS.cntx_psh();
FAP.cntx_psh();
{? _new<>''
|| {? _act<>_new
   || FAKS.use(_new);
      FAP.use('fakpo'+(form(8+_new)+3))
   ?};
   FAP.index('FAP');
   FAP.prefix(_a);
   {? FAP.first || {! |? _wyn:=exec('fap_war','faktury_poz',_b);FAP.next & _wyn<>'-I' !} ?}
?};
FAP.cntx_pop();
FAKS.cntx_pop();
_wyn


\chk_fakp_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: sprawdzenie poprawnosci wypelnienia pol dotyczacych INTRASTAT w tabeli FAP dla faktur podczas akceptacji
::   WY: 0/1
::  OLD: \chk_fakp_akc/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_return:=1;

{? FAKS.T().UE='T'
||
   _kor:=TYPYSP.KOR='T';
   FAP.index('FAP');
   FAP.prefix(FAKS.ref());
   {? FAP.first()
   ||
      {!
      |? _return:=
            {? _kor=0
            || exec('chk_fakp_ist','faktury_poz',1)
            |? exec('spr_kor','faktury_poz')
            || exec('wysw_kor','faktury_poz');
               exec('spr_poz_kor_ist','faktury_poz',1)
            || ''
            ?}='';
         _return=1 & FAP.next()
      !}
   ?}
?};

_return


\spr_poz_kor_ist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: sprawdzenie poprawnosci wypelnienia pol dotyczacych INTRASTAT w tabeli FAP dla korekt
::   WE: _a=1 - wolane przy akceptacji dokumentu, =0 - inne przypadki
::   WY: akronim niepoprawnie wypelnionego pola, ''-wpp
::  OLD: \spr_poz_kor/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_disp:={? var_pres('_a')=type_of(0) || ~_a || 1 ?};

_atr:=exec('check_poz_a','intrastat');
_atr.DISP:=_disp;
_atr.TABNAG:=FAKS;
_atr.TABPOZ:=FAP;
_atr.KOR:={? FAKS.T().KOR='T' || 'T' || 'N' ?};
_atr.HIS:=FAKS.T().HIS;
_atr.POZ:=FAP.POZ;
_atr.MRODZ:=FAP.M().RODZ;
_atr.IL:={? _disp || FAP.IL || FKOR.BIL ?};

_atr.IST_TYP:=FAKS.IST_TYP;
_atr.DATE:={? FAKS.IST_OKR='' || date(0,0,0) || date(#(4+FAKS.IST_OKR),#(FAKS.IST_OKR+2),1) ?};
_atr.RTRANSAK:=FAKS.RTRANSAK;

_atr.KCN:=FAP.KCN;
_atr.KODT:=FAP.KCN().KOD;
_atr.KPW:=FAP.KPW;
_atr.WD:=FAP.WD;
_atr.RTRANSAK:=FAP.RTRANSAK;
_atr.RTRANSPO:=FAP.RTRANSPO;
_atr.KP:=FAP.KP;
_atr.MASAN:={? _disp || FAP.MASAN || FKOR.BMASAN ?};
_atr.JU:=FAP.KCN().JU;
_atr.ILUJM:={? _disp || FAP.ILUJM || FKOR.BILUJM ?};
_atr.WF:={? _disp || FAP.WF || FKOR.BWF ?};
_atr.WS:={? _disp || FAP.WS || FKOR.BWS ?};
_atr.FKOR_IL:=FKOR.IL;
_atr.FKOR_MASAN:=FKOR.PMASAN;
_atr.FKOR_ILUJM:=FKOR.PILUJM;
_atr.FKOR_WF:=FKOR.PWF;
_atr.FKOR_WS:=FKOR.PWS;
exec('check_poz','intrastat',_atr)


\del_zero
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2010]
:: OPIS: usuwa dolaczone zapisy do pozycji z zerowymi ilosciami
::   WE: _a - ref FAKS-a
::  OLD: \del_zero/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
FAP.cntx_psh();
FAP.index('FAP');
FAP.prefix(_a);
{? FAP.first()
|| {!
   |? {? FAP.POZP<>0 & ~FAP.IL
      || _next:=FAP.del();
         FAP.cntx_psh();
         exec('ReNumAfterDel','#table','FAP','POZ');
         FAP.cntx_pop();
         _next
      || FAP.next()
      ?}
   !}
?};
FAP.cntx_pop()


\disp_fap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2010]
:: OPIS: display dla pozycji faktury
::  OLD: \disp_fap/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.cntx_psh; TYPYSP.cntx_psh;
_pomoc_mgrf:=POMOC.MGRF;
_d_help_m_fap:=D_HELP.M_FAP;
POMOC.MGRF:=FAP.M().MGR;
D_HELP.M_FAP:=FAP.M().KTM;
BEER.DISP:=1;
_kor:=FAP.FAKS().T().KOR='T' & {? FAP.FAKS().T().HIS='T' & (ref_name(FAP.ref())+2)='hs' || 0 || 1 ?};
_oldedit:=FAP.win_edit('?');
_oldsel:=FAP.win_sel('?');
_atrmjs:=ATR.MJS;
ATR.MJS:='FAP';
{? _kor=0
|| _edit:=exec('fap_win_disp','faktury_poz');
   FAP.win_edit(_edit)
|? _kor
|| exec('wysw_kor','faktury_poz');
   _edit:=exec('fap_win_disp_kor','faktury_poz');
   FAP.win_edit(_edit)
?};
exec('czytadkc','mat_atr',FAP.DK_C,FAP.RDKC);
exec('set_efld_opt','faktury_poz');
FAP.memo_get(,'UWAGI');
DISP.ILF:=FAP.IL;
exec('dispWithFakso','fakso','FAP');

FAP.win_edit(_oldedit);
FAP.win_sel(_oldsel);
exec('fap_win_edel','faktury_poz',_edit);

BEER.DISP:=0;
D_HELP.M_FAP:=_d_help_m_fap;
POMOC.MGRF:=_pomoc_mgrf;
FAKS.cntx_pop; TYPYSP.cntx_pop;
ATR.MJS:=_atrmjs;
1


\set_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zaznacza wymagalne pola w pozycjach faktury oraz wyłacza pola nieobsługiwane
::   WE: [_a] - akronim okienka
::----------------------------------------------------------------------------------------------------------------------
_win_red:={? var_pres('_a')=type_of('') || _a || FAP.win_edit('?') ?};
{? _win_red='' || return() ?};

{? _win_red='WRED' | _win_red='DISP'
|| FAP.efld_opt(_win_red,'mark=1',DISP,'ILF')
?};

FAKS.cntx_psh();
:: Mtaeriał
{? FAP.FAKS().WHERE='L' & FAP.M=null() & FAP.NX<>''
|| FAP.efld_opt(_win_red,'mark=0',D_HELP,'M_FAP')
|| FAP.efld_opt(_win_red,'mark=1',D_HELP,'M_FAP')
?};
:: Nazwa materiału
FAP.efld_opt(_win_red,'mark=0',,'NX');
{? FAP.FAKS().WHERE='L' & FAP.M=null()
|| FAP.efld_opt(_win_red,'mark=1',,'NX')
?};
{? FAP.FAKS().WHERE='L' & FAP.M=null()
|| FAP.efld_opt(_win_red,'mark=1',,'NX')
?};
:: Przeliczniki
{? FAP.M().J2<>null()
|| FAP.efld_opt(_win_red,'enable=1',FKOR,'WS2');
   FAP.efld_opt(_win_red,'enable=1',,'T2');
   FAP.efld_opt(_win_red,'enable=1',FKOR,'IL2');
   FAP.efld_opt(_win_red,'enable=1',FKOR,'J2');
   FAP.efld_opt(_win_red,'enable=1',FKOR,'BWS2');
   FAP.efld_opt(_win_red,'enable=1',FKOR,'BIL2')
|? FAKS.T().KOR='T'
|| FAP.efld_opt(_win_red,'enable=0',FKOR,'WS2');
   FAP.efld_opt(_win_red,'enable=0',,'T2');
   FAP.efld_opt(_win_red,'enable=0',FKOR,'J2');
   FAP.efld_opt(_win_red,'enable=0',FKOR,'BWS2');
   FAP.efld_opt(_win_red,'enable=0',FKOR,'BIL2');
   FAP.efld_opt(_win_red,'enable=0',FKOR,'BCWAL')
?};
_kod:=exec('get','#params',400000,2);
BEER.JMZ:=FAKS.JMZ;
_ejm:='enable=0';
{? _kod*{? BEER.SZ='S' || 'FK' || 'FZ' ?} | FAP.M().J2<>null()
|| _ejm:='enable=1';
   {? 'RA'*(_rodz:=exec('get','#params',{? BEER.SZ='S' || 400101 || 400401 ?},2))
   || BEER.FMJM:='T'+_rodz+'0'
   ?}
?};
FAP.efld_opt(_win_red,_ejm,,'WS2');
FAP.efld_opt(_win_red,_ejm,,'T2');
FAP.efld_opt(_win_red,_ejm,,'J2');
FAP.efld_opt(_win_red,_ejm,,'IL2');
:: Cena przed rabatem
{? FAKS.T().KOR='T'
|| {? FKOR.WAL=FAKS.NWAL
   || FAP.efld_opt(_win_red,'enable=0',FKOR,'CWAL');
      FAP.efld_opt(_win_red,'enable=0',FKOR,'BCWAL');
      FAP.efld_opt(_win_red,'enable=0',FKOR,'KRS')
   || FAP.efld_opt(_win_red,'enable=1',FKOR,'CWAL');
      FAP.efld_opt(_win_red,'enable=1',FKOR,'BCWAL');
      FAP.efld_opt(_win_red,'enable=1',FKOR,'KRS')
   ?};
   {? FAP.WAL=FAKS.NWAL
   || FAP.efld_opt(_win_red,'enable=0',,'CWAL');
      FAP.efld_opt(_win_red,'enable=0',,'KRS');
      FAP.efld_opt(_win_red,'enable=0',,'DTK')
   || FAP.efld_opt(_win_red,'enable=1',,'CWAL');
      FAP.efld_opt(_win_red,'enable=1',,'KRS');
      FAP.efld_opt(_win_red,'enable=1',,'DTK')
   ?}
|| {? FAP.WAL=FAP.WAL_P
   || FAP.efld_opt(_win_red,'enable=0',,'CWAL');
      FAP.efld_opt(_win_red,'enable=0',,'KRS');
      FAP.efld_opt(_win_red,'enable=0',,'DTK')
   || FAP.efld_opt(_win_red,'enable=1',,'CWAL');
      FAP.efld_opt(_win_red,'enable=1',,'KRS');
      FAP.efld_opt(_win_red,'enable=1',,'DTK')
   ?}
?};
_atrmjs:=ATR.MJS;
ATR.MJS:='FAP';
:: Atrybuty dostaw
{? exec('samgdost','mat_atr')=0
|| FAP.efld_opt(_win_red,'enable=0',,'DK_C')
|| FAP.efld_opt(_win_red,'enable=1',,'DK_C')
?};
ATR.MJS:=_atrmjs;
:: Konto kosztowe
{? exec('be_fapko','faktury_poz')=0
|| FAP.efld_opt(_win_red,'enable=0',,'KK')
|| FAP.efld_opt(_win_red,'enable=1',,'KK')
?};
:: Intrastat
_kp:=
   exec('czy_kp','intrastat',FAKS.IST_TYP,FAKS.IST_OKR)
      &
   {? FAP.KCN().KOD='' || 0 || exec('rw3','intrastat',FAP.KCN().KOD)=1 ?};
FAP.efld_opt(_win_red,{? _kp || 'mark=1' || 'mark=0' ?},,'KP');
exec('pops','intrastat');
{? FAKS.IST_OKR<>''
|| FAP.efld_opt(_win_red,'mark=1',,'KCN')
|| FAP.efld_opt(_win_red,'mark=0',,'KCN')
   ?};
{? exec('fap_masan_pr','faktury_poz')
|| FAP.efld_opt(_win_red,'mark=1',,'MASAN')
|| FAP.efld_opt(_win_red,'mark=0',,'MASAN')
?};
{? __POPS.MASAN
|| FAP.efld_opt(_win_red,'mark=1',FKOR,'PMASAN')
|| FAP.efld_opt(_win_red,'mark=0',FKOR,'PMASAN')
?};
{? exec('fap_ilujm_pr','faktury_poz')
|| FAP.efld_opt(_win_red,'mark=1',,'ILUJM')
|| FAP.efld_opt(_win_red,'mark=0',,'ILUJM')
?};
{? __POPS.ILUJM
|| FAP.efld_opt(_win_red,'mark=1',FKOR,'PILUJM')
|| FAP.efld_opt(_win_red,'mark=0',FKOR,'PILUJM')
?};
{? exec('fap_wf_pr','faktury_poz','PR')
|| FAP.efld_opt(_win_red,'mark=1',,'WF')
|| FAP.efld_opt(_win_red,'mark=0',,'WF')
?};
{? __POPS.WF
|| FAP.efld_opt(_win_red,'mark=1',FKOR,'PWF')
|| FAP.efld_opt(_win_red,'mark=0',FKOR,'PWF')
?};
{? exec('fap_ws_reg','faktury_poz','PR')
|| FAP.efld_opt(_win_red,'mark=1',,'WS');
   {? exec('find_sl','intrastat','191',FAP.RTRANSAK)
   || FAP.efld_opt(_win_red,'mark=0',,'WF')
   ?}
|| FAP.efld_opt(_win_red,'mark=0',,'WS')
?};
{? __POPS.WS
|| FAP.efld_opt(_win_red,'mark=1',FKOR,'PWS');
   {? exec('find_sl','intrastat','191',FAP.RTRANSAK)
   || FAP.efld_opt(_win_red,'mark=0',FKOR,'PWF')
   ?}
|| FAP.efld_opt(_win_red,'mark=0',FKOR,'PWS')
?};
FAP.efld_opt(_win_red,'enable=0',,'IDKH');
:: Data zwrotu
{? FAP.FAKS().WHERE<>'L' & FAKS.SZ='S' & FAP.M().RODZ='T' & FKOR.IL>FAP.IL
|| FAP.efld_opt(_win_red,'mark=1',FKOR,'DZ')
|| FAP.efld_opt(_win_red,'mark=0',FKOR,'DZ')
?};
{? FAKS.SZ='S' & FAKS.WGZWR
|| FAP.efld_opt(_win_red,'editable=0',FKOR,'DZ')
|| FAP.efld_opt(_win_red,'editable=1',FKOR,'DZ')
?};

:: Parametr obowiązkowego powodu korekty
{? exec('get','#params',{? BEER.SZ='S' || 300210 || 200210 ?},2)='T' & exec('spredkor','faktury_poz')
|| FAP.efld_opt(_win_red,'mark=1',,'POWKOR')
|| FAP.efld_opt(_win_red,'mark=0',,'POWKOR')
?};
:: Projekt
{? FAP.PROJEKTY=null() & (FAKS.T().PROJZAKR='' | TYPYSP.PROJZAKR=exec('projzakr_nie_dotyczy','projekty'))
|| FAP.efld_opt(_win_red,'enable=0',,'PROJEKTY')
|| FAP.efld_opt(_win_red,'enable=1',,'PROJEKTY')
?};
:: Wymagany split payment
{? exec('sp_active','faktury_wspolne',FAKS.DW)
      |
   var_pres('SP_WYM',MPKW)=27
||
   {? exec('fap_spwym_be','faktury_poz')
   ||
      FAP.efld_opt(_win_red,'editable=1',,'SP_WYM')
   ||
      FAP.efld_opt(_win_red,'editable=0',,'SP_WYM')
   ?};
   FAP.efld_opt(_win_red,'editable=0',FKOR,'SP_WYM')
?};
:: Procedura
{? FAKS.PROC
|| FAP.efld_opt(_win_red,'editable=0',,'EF_PROC')
|| FAP.efld_opt(_win_red,'editable=1',,'EF_PROC')
?};
:: Data sprzedaży
{? exec('dsp_wpoz_enable','faktury_poz',FAKS.SZ)
|| FAP.efld_opt(_win_red,'enable=1',,'D')
|| FAP.efld_opt(_win_red,'enable=0',,'D')
?};

FAKS.cntx_pop()


\fap_szuk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: ustawia okienko do wyszukiwania dla cech
::  OLD: \fap_szuk/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
M.win_dict('SEL');
M.actions('SEL','W');
DK_C.win_dict('WER');
POMOC.MGR:=null();
1


\rodz_fap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: sprawdza czy na fakturze sa inne pozycje niz uslugowe
::   WE: 1-tylko uslugi 0-mieszane
::  OLD: \rodz_fap/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
FAP.cntx_psh();
FAP.index('FAP');
FAP.prefix(FAKS.ref());
{? FAP.first()
|| {!
   |? _wyn:=FAP.M().RODZ='U';
      _wyn & FAP.next()
   !}
?};
FAP.cntx_pop();
_wyn


\copypoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: kopiuje pozycje faktury korygowanej do korygujacej
::   WE: _a - FAKS.ref docelowy
::       _b - maska FAKS zrodlowa
::       _c - 0-korekta zbiorcza; 1-korekty pojedyncza
::       [_d] - 0(domyślnie)-nie 1-z naniesieniem zwrotu
::       [_e] - ref nagłówka dokumentu źródłowego do pobrania kursu
::  OLD: \copypoz/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:={? var_pres('_d')=type_of(0) & var_pres('__conv')>117 || _d || 0 ?};
_xx_krs_ref:={? var_pres('_e')=type_of('') || _e || '' ?};
_kz:=var_pres('_f')>100;
_kz_krs:={? _kz || _f.get('KRS') || 0 ?};

_old:="{? _a<>_b
        || FAKS.use(_b);
           FAP.use('fakpo'+(_b+3));
           FAKSPL.use('fakpl'+(_b+3))
        ?}";
_new:="{? _a<>_b
        || FAKS.use('faktu'+ST.ODDZ+(2-$ST.AR));
           FAP.use('fakpo'+ST.ODDZ+(2-$ST.AR));
           FAKSPL.use('fakpl'+ST.ODDZ+(2-$ST.AR))
        ?}";

_lksql:=FAKS.LKSQL;
FAKS.cntx_psh();
FAP.cntx_psh();
FAKSPL.cntx_psh();
_old(_b,form(8+_lksql));

_ile:=FAP.fld_num();
_pola:=obj_new(_ile);
_poz:=0;

_new_rab:=
   BEER.SZ='S'
   & exec('get','#params',800810,2)='C'

   & exec('get','#params',800813,2)*{? _c || 'KOR' || 'KZ' ?};

FAP.index('FAP');
FAP.prefix(BB.sqlint(_lksql));
{? FAP.first()
||
   _fap_krs:=exec('fap_krs_obj','faktury_poz');
   {? _new_rab
   || _rabn:=FAP.FAKS().RAB;
      _dwal:=FAKS.WAL<>FAKS.NWAL;
      FAKS.cntx_psh();
      FAKS.use(ref_name(_a));
      FAKS.prefix();
      {? FAKS.seek(_a)
      || FAKS.RAB:=DISP.RAB:=0;
         FAKS.put()
      ?};
      FAKS.cntx_pop()
   ?};
   {!
   |?
      _poz+=1;
      _poz:=FAP.POZ;
      _rfap:=$FAP.ref();
      FAP.memo_get(,'UWAGI');
      _uwagi:=FAP.memo_txt(0,1,'UWAGI');
      {! _licz:=1.._ile |! {? (';MEMO;SYSMEMO'*FAP.fld_join(_licz))>1 || '' || _pola[_licz]:=FAP[_licz] ?} !};
      FAP.cntx_psh();
      _new(_b,form(8+_lksql));
      FAP.clear();
      {! _licz:=1.._ile |! {? (';MEMO;SYSMEMO'*FAP.fld_join(_licz))>1 || '' || FAP[_licz]:=_pola[_licz] ?} !};
      FAP.memo_set(_uwagi,'UWAGI');
      FAP.FAKS:=_a;
      FAP.POZ:=_poz;
      FAP.CWAL:=0;
      FAP.CPR:=0;
      FAP.CN:=0;
      FAP.CB:=0;
      FAP.WN:=0;
      FAP.WV:=0;
      FAP.WB:=0;

      FAP.WWAL:=0;
      FAP.VWAL:=0;
      FAP.BWAL:=0;

      FAP.WWAL_P:=0;
      FAP.VWAL_P:=0;
      FAP.BWAL_P:=0;
      FAP.POZP:=0;

      FAP.TAR_H:=exec('tar_h_copy','ceny_dok',FAP.TAR_H,_b+3);

::    intrastat
      FAP.MASAN:=0;
      FAP.ILUJM:=0;
      FAP.WF:=0;
      FAP.WS:=0;
::    przeliczniki
      FAP.IL:=0;
      {? FAP.M().J2<>null() || FAP.T2='M' |? FAP.T2<>'N' || FAP.T2:='G' ?};
      FAP.IL2:=0;
      FAP.WS2:=0;

      FAP.POWKOR:=null();
      {? var_pres('JPK_GTU',MGR)>0
      ||
         {? FAP.JPK_GTU=null()
         ||
            {? FAP.M().JPK_GTU=null & exec('get','#params', 300120, 2)='T'
            || FAP.JPK_GTU:=FAP.M().MGR().JPK_GTU
            || FAP.JPK_GTU:=FAP.M().JPK_GTU
            ?}
         ?}
      ?};
:: kurs z korekty
      {? FAKS.KRS || FAP.KRS:=FAKS.KRS ?};
:: kurs z przed korekty zbiorczej
      FAP.BKRS:=0;
      exec('fap_krs','faktury_poz',_xx_krs_ref,FAP.POZ,_fap_krs);
      {? _fap_krs.KRS
      ||
         {? FAKS.BKRS & FAP.D<>date(0,0,0) || FAP.BKRS:=_fap_krs.KRS ?};
         FAP.KRS:=_fap_krs.KRS;
         FAP.SWAL:=_fap_krs.SWAL;
         FAP.RTK:=_fap_krs.RTK;
         FAP.NTK:=_fap_krs.NTK;
         FAP.DTK:=_fap_krs.DTK;
         FAP.BTK:=_fap_krs.BTK
      ?};
      {? _kz & _kz_krs
      ||
:: kurs z korekty zbiorczej jeśli podano
         FAP.KRS:=_kz_krs;
         FAP.SWAL:=2;
         FAP.RTK:=0;
         FAP.NTK:='';
         FAP.DTK:=date(0,0,0);
         FAP.BTK:=''
      ?};
      {? FAP.add() & _zwrot & (__conv.clear(); __conv.prefix(_rfap); __conv.first())
      || _ile:=0;
         _il2:=0;
         {!
         |? _ile+=__conv.ILE;
            _il2+=__conv.IL2;
            __conv.KOR:=$FAP.ref();
            __conv.put(1);
            __conv.next()
         !};
         FAP.POWKOR:=ZAKR.POWKOR;
         {? FAP.M().J2=null()
         ||  FAP.IL-=_ile;
             FAP.IL2-=_ile
         || {? exec('czyJS','jm',FAP.M)
            || FAP.IL-=_il2;
               FAP.IL2-=_ile
            || FAP.IL-=_ile;
               FAP.IL2-=_il2
            ?}
         ?};
         exec('liczfak','faktury_wspolne',0,,0);
         FAP.put(1)
      ?};
      FAP.memo_put(,'UWAGI');

      {? _new_rab || exec('kz_rab_od_cpor','faktury_poz',_rabn,_dwal,_c) ?};

      BEER.MSK:='fakpo';
      exec('inf_dod','fakso',-1);
      _old(_b,form(8+_lksql));
      FAP.cntx_pop();
      FAP.next()
   !}
?};
obj_del(_pola);
_new(_b,form(8+_lksql));
FAKSPL.cntx_pop();
FAP.cntx_pop();
FAKS.cntx_pop();
''


\kz_rab_od_cpor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Rabat od ceny po rabacie
::   WE: _a - rabat z naglowka przed korekta
::       _b - 0-niewalutowy, 1-walutowy
::       _c - 0-korekta zbiorcza; 1-korekty pojedyncza
::  OLD: \kz_rab_od_cpor/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_rabn:=_a;
_dwal:=_b;
{? BEER.SZ<>'S'
   | exec('get','#params',800810,2)<>'C'
   | exec('get','#params',800813,2)*{? _c || 'KOR' || 'KZ' ?}=0
|| return()
?};

exec('wysw_kor','faktury_poz');
_cen_rab:=exec('cen_rab','faktury_wspolne',_rabn,_dwal);
{? _dwal || FAP.CWAL:=_cen_rab-FKOR.CWAL || FAP.CPR:=_cen_rab-FKOR.CPR ?};
FAP.RAB:=FAP.RABK:=0;

exec('liczfak','faktury_wspolne',0);
FAP.put()


\fap_nx_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Przed redakcją FAP.NX
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
FAKS.cntx_psh();
{? FAP.FAKS
|| _wyn:=FAP.FAKS().WHERE='L' & FAP.M=null()
?};
FAKS.cntx_pop();
_wyn


\fap_nx_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Po redkacji FAP.NX
::----------------------------------------------------------------------------------------------------------------------
exec('set_efld_opt','faktury_poz');
1


\fap_wal_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Po redkacji FAP.WAL
::----------------------------------------------------------------------------------------------------------------------
{? exec('ae_poz_wal','ceny','F')
|| exec('set_efld_opt','faktury_poz');
   1
?}


\fap_win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zwraca okno redakcji tabeli FAP
::   WE: [_a] - 0/1 - okno dla Danych do intrastat
::   WY: akronim okna redakcji
::----------------------------------------------------------------------------------------------------------------------
_intrastat:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_win_akr:=
   {? _intrastat | FAKS.KZ='' & FAKS.AKC<>'N'
   ||
:: zakończona redakcja dokumentu
:: dokument
      {? FAKS.KOR='' | FAP.POZP>0   || 'INTRA'
:: korekta
                                    || 'INTRAK'
      ?}

   |? FAKS.KOR<>''
   ||
:: korekta zakupu
      {? FAKS.SZ='Z' & FAP.POZP=0   || 'WREDZK'
:: korekta zakupu - dołączona pozycja
      |? FAKS.SZ='Z' & FAP.POZP>0   || 'WREDZK1'
:: korekta historyczna
      |? FAKS.T().HIS='T'           || 'WREDKH'
:: korekta sprzedaży
      |? FAKS.SZ='S' & FAP.POZP=0   || 'WREDK'
:: korekta sprzedaży - dołączona pozycja
                                    || 'WREDK1'
      ?}
:: dokument zakupu
   |? FAKS.SZ='Z'                   || 'WREDZ'
:: dokument sprzedaży
                                    || 'WRED'
   ?};
{? _win_akr='INTRA' | _win_akr='INTRAK'
||
   _win_akr
||
   _win_akr:='Z'+_win_akr;
   _title:='Pozycja dokumentu'@;
   _win_red:=FAP.mk_edit(_title,,'fap%1'[-_win_akr],,,'html_maximized');
   FAP.win_etab(_win_red,'Dane podstawowe'@);
   FAP.win_ewin(_win_red,,_win_akr);
   {? FAKS.T().PAR='T' | FAKS.T().UE='N' | FAKS.T().ZAL='T'
   ||
      ~~
   ||
      FAP.win_etab(_win_red,'Dane do Intrastat'@);
      FAP.win_ewin(_win_red,,{? FAKS.T().KOR='N' || 'INTRA' || 'INTRAK' ?})
   ?};
   FAP.win_etab(_win_red,'Dane dodatkowe'@);
   FAP.win_ewin(_win_red,,'DANE_D');
   FAP.win_etab(_win_red,'Uwagi'@);
   FAP.win_ewin(_win_red,,'UWAGI');
:: Dokument Businesslink - ustawienie się na załączniku w celu podglądu PDF w oknie pozycji dokumentu
   {? exec('bl_dokum_seek','zbl',FAKS)
   ||
      FAP.win_etab(_win_red,'Podgląd'@);
      FAP.win_ewin(_win_red,DOKUM,'DOKUMI')
   ?};
:: Rezultat kontroli Businesscheck
   {? exec('upgrade2325_blbc1','zbl')
   || {? var_pres('__DOKUMZ_BC')<0 | __DOKUMZ_BC.REFSQL<>$FAKS.ref()
      || exec('faks_dokumzbc_seek','zbl')
      ?};
      {? var_pres('__DOKUMZ_BC')>0 & __DOKUMZ_BC.REFSQL=$FAKS.ref() & __DOKUMZ_BC.DOKUMZ<>null()
      || _ff:="DOKUMZ.cntx_psh();
            DOKUMZ.prefix();
            {? DOKUMZ.seek(__DOKUMZ_BC.DOKUMZ)
            || exec('blc_analyze','businesslink3',1)
            ?};
            DOKUMZ.cntx_pop();
            ''";
         FAP.win_ebtn(_win_red,'text='+'&Businesscheck'@+
                   ',btn_label_align=center,panel=bottom,align=begin,display=1',_ff)
      ?}
   ?};
   exec('ok_esc','#window',FAP,_win_red,1,,,,,exec('help_red_ok','#window','Z'),exec('text_red_ok','#window'),
exec('help_red_esc','#window','A'));
   {? FAKS.T().KOR='N'
   || {? ~exec('samgdost','mat_atr')
      || FAP.btn_eopt(FAP.win_edit(_win_akr),'ATRYB','state=grayed')
      || FAP.btn_eopt(FAP.win_edit(_win_akr),'ATRYB','state=normal')
      ?}
   ?};
   _win_red
?}


\fap_win_edel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Usuwa tymczasowe okno redakcji
::   WE: _a - akronim okna
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_akr:=_a;
{? _win_akr='INTRA' | _win_akr='INTRAK' || return() ?};
{? _win_akr=FAP.win_edit('?') || FAP.win_edit('') ?}; FAP.win_edel(_win_akr)


\fap_win_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zwraca okno podglądu pozycji tabeli FAP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_akr:={? FAKS.SZ='S' || 'DISP' || 'DISPZ' ?};
_win_akr:='Z'+_win_akr;
_title:='Pozycja dokumentu'@;
_win_red:=FAP.mk_edit(_title,,'fap%1'[-_win_akr],,,'html_maximized');
FAP.win_etab(_win_red,'Dane podstawowe'@);
FAP.win_ewin(_win_red,,_win_akr);
{? FAP.DK_C
||
   FAP.win_etab(_win_red,'Atrybuty'@);
   FAP.win_ewin(_win_red,,'ATR')
?};
{? FAKS.T().PAR='T' | FAKS.T().UE='N' | FAKS.T().ZAL='T'
||
   ~~
||
   FAP.win_etab(_win_red,'Dane do Intrastat'@);
   FAP.win_ewin(_win_red,,'INTRA')
?};
FAP.win_etab(_win_red,'Dane dodatkowe'@);
FAP.win_ewin(_win_red,,'DANE_D');
FAP.win_etab(_win_red,'Uwagi'@);
FAP.win_ewin(_win_red,,'UWAGI');
:: Dokument Businesslink - ustawienie się na załączniku w celu podglądu PDF w oknie pozycji dokumentu
{? exec('bl_dokum_seek','zbl',FAKS)
||
   FAP.win_etab(_win_red,'Podgląd'@);
   FAP.win_ewin(_win_red,DOKUM,'DOKUMI')
?};
:: Rezultat kontroli Businesscheck
{? exec('upgrade2325_blbc1','zbl')
|| {? var_pres('__DOKUMZ_BC')<0 | __DOKUMZ_BC.REFSQL<>$FAKS.ref()
   || exec('faks_dokumzbc_seek','zbl')
   ?};
   {? var_pres('__DOKUMZ_BC')>0 & __DOKUMZ_BC.REFSQL=$FAKS.ref() & __DOKUMZ_BC.DOKUMZ<>null()
   || _ff:="DOKUMZ.cntx_psh();
         DOKUMZ.prefix();
         {? DOKUMZ.seek(__DOKUMZ_BC.DOKUMZ)
         || exec('blc_analyze','businesslink3',1)
         ?};
         DOKUMZ.cntx_pop();
         ''";
      FAP.win_ebtn(_win_red,'text='+'&Businesscheck'@+
                ',btn_label_align=center,panel=bottom,align=begin,display=1',_ff)
   ?}
?};
_win_red


\fap_win_disp_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zwraca okno podglądu pozycji korekty tabeli FAP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_akr:=
   {? FAKS.SZ='Z' & FAP.POZP=0   || 'DISPZK'
   |? FAKS.SZ='Z' & FAP.POZP>0   || 'DISPZK1'
   |? FAKS.T().HIS='T'           || 'DISPKH'
   |? FAKS.SZ='S' & FAP.POZP=0   || 'DISPK'
                                 || 'DISPK1'
   ?};
_win_akr:='Z'+_win_akr;
_title:='Pozycja dokumentu'@;
_win_red:=FAP.mk_edit(_title,,'efap%1'[-_win_akr],,,'html_maximized');
FAP.win_etab(_win_red,'Dane podstawowe'@);
FAP.win_ewin(_win_red,,_win_akr);
{? FAP.DK_C
||
   FAP.win_etab(_win_red,'Atrybuty'@);
   FAP.win_ewin(_win_red,,'ATR')
?};
{? FAKS.T().PAR='T' | FAKS.T().UE='N' | FAKS.T().ZAL='T'
||
   ~~
||
   FAP.win_etab(_win_red,'Dane do Intrastat'@);
   FAP.win_ewin(_win_red,,'INTRAKD')
?};
FAP.win_etab(_win_red,'Dane dodatkowe'@);
FAP.win_ewin(_win_red,,'DANE_D');
FAP.win_etab(_win_red,'Uwagi'@);
FAP.win_ewin(_win_red,,'UWAGI');
:: Dokument Businesslink - ustawienie się na załączniku w celu podglądu PDF w oknie pozycji dokumentu
DOKUM.cntx_psh();
{? exec('bl_dokum_seek','zbl',FAKS)
||
   FAP.win_etab(_win_red,'Podgląd'@);
   FAP.win_ewin(_win_red,DOKUM,'DOKUMI')
?};
DOKUM.cntx_pop();
_win_red


\bd_zmkor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: formula przed wyswietleniem dla zmiennej korekty (FKOR)
::  OLD: \bd_zmkor/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAP.POZP<>0
   | cur_afld<>'IL' & cur_afld<>'SV' & exec('upr_cscz','ceny')=0
|| {? cur_afld<>'CN' || 0 || exec('FindInSet','#table','FAP_K','TAXS',FAP.uidref())<>null() ?}
|| ''
?}


\korh_li2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2006]
:: OPIS: obliczanie zmiennych dla korekty historycznej + sprawdzenie wartosci czy dodatnia
::   WY: 1-jest OK 0-pole ma wartosc ujemna
::  OLD: \korh_li2/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='SV' & FKOR.SV=null || FUN.info('Należy podać stawkę VAT.'@); 0
|? exec('itsPositive','#field')
|| exec('korh_lic','faktury_wspolne');
   exec('set_efld_opt','faktury_poz');

   win_disp
|| 0
?}


\po_walfk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: po redakcji FKOR.WAL
::  OLD: \po_walfk/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
exec('set_efld_opt','faktury_poz');
win_disp; 1


\pr_korcw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcja pol CWAL i KRS na korekcie historycznej
::   WY: 0-jesli waluta jest PLN, 1-jest rozna od PLN
::  OLD: \pr_korcw/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld='KRS'
|| FAP.FAKS().WAL=FAKS.NWAL & FAKS.T().HIS='T' & FKOR.WAL<>FAKS.NWAL
|| {? exec('upr_cscz','ceny') || FAKS.T().HIS='T' & FKOR.WAL<>FAKS.NWAL ?}
?}


\po_korcw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po polu CWAL i KRS na korekcie historycznej
::   WY: 0-wartosc pola ujemna 1-jest OK
::  OLD: \po_korcw/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('itsPositive','#field')
|| FKOR.CPR:=FKOR.CWAL*FKOR.KRS $2;
   exec('korh_lic','faktury_wspolne');
   exec('set_efld_opt','faktury_poz');

   win_disp;
   1
|| 0
?}


\fkor_rab_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed wyswietleniem FKOR.RAB
::  OLD: \fkor_rab_bd/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
exec('bd_zmkor','faktury_poz')


\fkor_rab_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed edycja FKOR.RAB
::  OLD: \fkor_rab_be/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
exec('bd_zmkor','faktury_poz')


\fkor_rab_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: po edycji FKOR.RAB
::  OLD: \fkor_rab_ae/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
_dwal:=FAKS.WAL<>FAKS.NWAL;
_doklFix:=FAKS.T().HIS='N' | exec('get','#params',300215,2)='T';
_dokl_c:=
   {? _doklFix
   || {? FAP.FAKS().SZ='Z' || FAP.M().DOKL_C || 2 ?}
   || _dokl_c:=+(2-$fabs(frac({? _dwal || FKOR.CWAL || FKOR.CPR ?})));
      {? _dokl_c<2 || 2 || _dokl_c ?}
   ?};
fld(fld()$_dokl_c);
{? exec('po_rabkh','faktury_poz')
|| exec('set_efld_opt','faktury_poz');
 1
|| 0
?}


\po_rabkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po polu RAB na korekcie historycznej
::   WY: 0-wartosc pola ujemna 1-jest OK
::  OLD: \po_rabkh/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? exec('itsPositive','#field')
||
   {? (';PS'*FAP.FAKS().RAB_TYP)>1 & FAP.FAKS().T().HIS='T' & FKOR.RAB+FKOR.FRAB>=100
   ||
      FUN.info('Rabat dla ceny jest sumą rabatu przed korektą z nagłówka i z pozycji faktury.'
               '\nMożliwe wprowadzenie maksymalnego rabatu przed korektą o wartości: %1.'@[form(99.99-FKOR.FRAB,,2)]);
      FKOR.RAB:=99.99-FKOR.FRAB $2;
      win_disp()
   ||
      _wyn:=1
   ?}
?};
{? _wyn=1
|| exec('korh_lic','faktury_wspolne')
?};
_wyn


\fkor_frab_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [$12.10]
:: OPIS: przed wyswietl FKOR.FRAB
::  OLD: \fkor_frab_bd/ceny.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('bd_zmkor','faktury_poz');
exec('korh_rab','faktury_nag',FAKS.LKSQL,0,FAP.POZ);
{? var_pres('_wyn')=type_of('stringi')
|| FKOR.FRAB:=
      {? exec('czyrabp','ceny',TAZ.RAB_TYP)
      || FZ.RAB
      || exec('kwota','ceny',FZ.RAB,exec('podstawa1','faktury_wspolne',cur_tab(1)))
      ?};
   {? {? FAKS.SZ='S' || exec('upr_cs','ceny') || exec('upr_cz','ceny') ?}
   || ''
   ?}
|| _wyn
?}


\fkor_kp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: warunki na wyswietlenie lub redagowanie pola FKOR.KP
::   WE: _a - 'PW' - dla funkcji przed wyswietleniem, 'PR' - dla funkcji przed redakcja
::   WY: _a = 'PW' - kolor lub ''
::  OLD: \fkpw_kp/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
_kolor:=exec('findfnv','#color');
_kolor1:=exec('findfnrd','color');
{? FAP.FAKS().T().HIS = 'T'
||
   return(_kolor)
||
   {? FAP.POZP = 0
   ||
      return(_kolor1)
   ||
      return(_kolor)
   ?}
?}


\fkor_kp_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: przed redakcją FKOR.KP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
0


\fkor_masan_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FKOR.PMASAN
::   WY: kolor
::  OLD: \fkor_masan_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? __POPS.MASAN
|| ''
|| exec('findfnrd','color')
?}


\fkor_masan_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcja FKOR.PMASAN
::   WY: 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fkor_masan_pr/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
__POPS.MASAN


\fkor_masan_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: po redakcja FAP.IST_USZL
::   WY: 1
::  OLD: \fkor_masan_po/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
fld(fld$0); 1


\fkor_ilujm_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FKOR.PILUJM
::   WY: kolor
::  OLD: \fkor_ilujm_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? __POPS.ILUJM
|| ''
|| exec('findfnrd','color')
?}


\fkor_ilujm_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcja FKOR.PILUJM
::   WY: 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fkor_ilujm_pr/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
__POPS.ILUJM


\fkor_ilujm_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: po redakcji FKOR.PILUJM
::   WY: 1
::  OLD: \fkor_ilujm_po/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
fld(fld$6); 1


\fkor_ju_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FKOR.BWS
::   WY: kolor
::  OLD: \fkor_ju_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
FKOR.PJU:=null();
{? exec('fap_ilujm_reg','faktury_poz','PR') = 0
||
   exec('findfnv','#color')
||
   {? FAP.POZP = 0
   ||
      FKOR.PJU:=FAP.KCN().JU;
      exec('findfnrd','color')
   ||
      exec('findfnv','#color')
   ?}
?}


\fkor_wf_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FKOR.PWF
::   WY: kolor
::  OLD: \fkor_wf_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
||
   {? __POPS.WF
   || ''
   || exec('findfnrd','color')
   ?}
?}


\fkor_wf_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcja FKOR.PWF
::   WY: 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fkor_wf_pr/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
||
   __POPS.WF
?}


\fkor_wf_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: po redakcji FKOR.PWF
::   WY: 1
::  OLD: \fkor_wf_po/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
fld(fld$0); 1


\fkor_ws_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FKOR.PWS
::   WY: kolor
::  OLD: \fkor_ws_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
||
   {? __POPS.WS
   || ''
   || exec('findfnrd','color')
   ?}
?}


\fkor_ws_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed redakcja FKOR.PR
::   WY: 1 - redakcja dozwolona, 0 - wpp
::  OLD: \fkor_ws_pr/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
||
   __POPS.WS
?}


\fkor_ws_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: po redakcji FKOR.PWS
::   WY: 1
::  OLD: \fkor_ws_po/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
fld(fld$0); 1


\fkor_bmasan_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FKOR.BMASAN
::   WY: kolor
::  OLD: \fkor_bmasan_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fap_masan_pw','faktury_poz')


\fkor_bilujm_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FKOR.BILUJM
::   WY: kolor
::  OLD: \fkor_bilujm_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fap_ilujm_reg','faktury_poz','PW',1)


\fkor_bwf_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FKOR.BWF
::   WY: kolor
::  OLD: \fkor_bwf_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('upr_cscz','ceny')
|| exec('faks_kpw_reg','faktury_nag','PW',1)
?}


\fkor_bws_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietleniem FKOR.BWS
::   WY: kolor
::  OLD: \fkor_bws_pw/ist01.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fap_ws_pw','faktury_poz')


\kor_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord dla tabeli korekt
::   WE: _a - 0/1 - ostatni wyświetlany rekord
::   WY: zwraca kod koloru
::  OLD: \kor_rek/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
params_exec('fap_rekb','faktury_poz',_a);
exec('wysw_kor','faktury_poz');
exec('rekprzed','color','FAKS#02')


\wysw_ko2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JF [--.--]
:: OPIS: Wyświetla pozycje f-ry korygowanej w rekord przed w RED (FAP) uwzgledniajac
::----------------------------------------------------------------------------------------------------------------------
exec('wysw_kor','faktury_poz');
FAP.cntx_psh();
DOKUM.cntx_psh();
_edit:=exec('fap_win_disp_kor','faktury_poz');
FAP.win_edit(_edit);
exec('set_efld_opt','faktury_poz');
exec('bl_dokum_seek','zbl',FAKS);
__FAPDISP:=1;
FAP.display();
VAR_DEL.delete('__FAPDISP');
exec('fap_win_edel','faktury_poz',_edit);
DOKUM.cntx_pop();
FAP.cntx_pop();
''


\kor_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: koryguje (edytuje) pozycje faktury
::   WE: [_a] - 1-(domyslnie) z okienkiem 0-bez okienka
::       [_b] - 0/1 - okno dla danych do intrastat
::   WY: czy byla edycja
::  OLD: \kor_poz/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_edit:={? var_pres('_a')=type_of(0) || _a || 1 ?};
_intrastat:={? var_pres('_b')=type_of(0) || _b || 0 ?};

_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};

_wyn:=0;

_env_fak_poz:=exec('env','faktury_poz');
_env_fak_poz.POPRAW:='T';
params_set('env_fak_poz',_env_fak_poz);

_win_edit:=exec('fap_win_edit','faktury_poz',_intrastat);
FAP.win_edit(_win_edit);
exec('set_efld_opt','faktury_poz');
_oldm:=FAP.M;

{? FAP.POZP=0 & FAP.M
|| exec('zmvat_chk','faktury_wspolne',FAP.M,FAKS.KH,FAKS.AR,FAKS.AM,
      FAP.SV,FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N',FAKS.SZ)
?};

{? FAKS.WAL=null || FAKS.WAL:=INFO.NAROD; FAKS.put(1) ?};

FAP.CWAL:=FAP.CWAL+FKOR.CWAL;
FAP.CPR:=FAP.CPR+FKOR.CPR;
FAP.CN:=FAP.CN+FKOR.CN;
FAP.CB:=FAP.CB+FKOR.CB;

{? FAP.POZP=0
||
   FAP.IL:=FAP.IL+FKOR.IL;
   FAP.IL2:=FAP.IL2+FKOR.IL2;
   FAP.WS2:=FAP.WS2+FKOR.WS2;

:: narodowa
   FAP.WN:=FAP.WN+FKOR.WN;
   FAP.WV:=FAP.WV+FKOR.WV;
   FAP.WB:=FAP.WB+FKOR.WB;

:: handlowa
   FAP.WWAL:=FAP.WWAL+FKOR.WWAL;
   FAP.VWAL:=FAP.VWAL+FKOR.VWAL;
   FAP.BWAL:=FAP.BWAL+FKOR.BWAL;

:: opodatkowania
   FAP.WWAL_P:=FAP.WWAL_P+FKOR.WWAL_P;
   FAP.VWAL_P:=FAP.VWAL_P+FKOR.VWAL_P;
   FAP.BWAL_P:=FAP.BWAL_P+FKOR.BWAL_P;

:: intrastat
   FAP.MASAN:=FAP.MASAN+FKOR.PMASAN;
   FAP.ILUJM:=FAP.ILUJM+FKOR.PILUJM;
   FAP.WF:=FAP.WF+FKOR.PWF;
   FAP.WS:=FAP.WS+FKOR.PWS
?};

:: data zwrotu
_dz:=FAKS.DZ;
FKOR.DZ:=FAKS.DZ;

exec('pops','intrastat');
FAP.memo_get(,'UWAGI');

{? ~_edit | FAP.edit("exec('spr_poz_kor','faktury_poz',0)")
||
   _wyn:=1;
   FKOR.M:=FAP.M;
   FAP.CWAL:=FAP.CWAL-FKOR.CWAL;
   FAP.CPR:=FAP.CPR-FKOR.CPR;
   FAP.CN:=FAP.CN-FKOR.CN;
   FAP.CB:=FAP.CB-FKOR.CB;

   {? FAP.POZP=0
   ||
      _vat:=#((FAP.SV().KOD*'%') + FAP.SV().KOD-1) / 100;

      FAP.IL:=FAP.IL-FKOR.IL;
      FAP.IL2:=FAP.IL2-FKOR.IL2;
      FAP.WS2:=FAP.WS2-FKOR.WS2;

      FAP.WN:=FAP.WN-FKOR.WN;
      FAP.WV:=FAP.WV-FKOR.WV;
      FAP.WB:=FAP.WB-FKOR.WB;

::    handlowa
      FAP.WWAL:=FAP.WWAL-FKOR.WWAL;
      FAP.VWAL:=FAP.VWAL-FKOR.VWAL;
      FAP.BWAL:=FAP.BWAL-FKOR.BWAL;

::    opodatkowania
      FAP.WWAL_P:=FAP.WWAL_P-FKOR.WWAL_P;
      FAP.VWAL_P:=FAP.VWAL_P-FKOR.VWAL_P;
      FAP.BWAL_P:=FAP.BWAL_P-FKOR.BWAL_P;

::    intrastat
      FAP.MASAN:=FAP.MASAN-FKOR.PMASAN;
      FAP.ILUJM:=FAP.ILUJM-FKOR.PILUJM;
      FAP.WF:=FAP.WF-FKOR.PWF;
      FAP.WS:=FAP.WS-FKOR.PWS
   ?};
:: rozliczenia projektów
   {? FAKS.PROJEKTY
   || FAP.PROJEKTY:=FAKS.PROJEKTY;
      exec('projdel','projekty',FAP.ref());
      exec('projwgfap','projekty',FAP.ref())
   |? FAP.PROJEKTY
   || exec('projdel','projekty',FAP.ref());
      exec('projwgfap','projekty',FAP.ref())
   ?};

   {? (_put:=FAP.put()) & _dz<>FKOR.DZ
   || FAKS.DZ:=FKOR.DZ;
      FAKS.put()
   ?};
   {? _put
   ||
      FAP.memo_put();
      exec('FindAndGet','#table',FAKS,FAKS.LKSQL,,"
         _name:=ref_name(FAKS.ref());
         FAP.cntx_psh();
         FAP.use((FAP.name()-3)+(_name+3));
         FAP.cntx_psh();
         FAP.index('FAP');
         FAP.prefix(FAKS.ref(),FAP.POZ);
         {? FAP.first()
         ||
            {? __POPS.MASAN || FAP.MASAN:=FKOR.PMASAN ?};
            {? __POPS.ILUJM || FAP.ILUJM:=FKOR.PILUJM ?};
            {? __POPS.WF || FAP.WF:=FKOR.PWF ?};
            {? __POPS.WS || FAP.WS:=FKOR.PWS ?};
            FAP.put()
         ?};
         FAP.cntx_pop();
         FAP.cntx_pop()
      ");
::    aktualizacja kwoty naliczonego rabatu
      {? FAKS.KZ<>'' || exec('faks_kzr_war_rabk_licz','faktury_nag1') ?}
   ?};
:: naliczenie opłat dodatkowych
   {? _put || exec('actTAXS','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),FAP.M,_oldm) ?};
   FAP.cntx_psh();
   {? FAKS.T().HIS<>'T' || exec('sumfak','faktury_wspolne') ?};
   FAP.cntx_pop()
?};
exec('fap_win_edel','faktury_poz',_win_edit);
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
_wyn


\spr_poz_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: sprawdzanie wypelnienia pol na pozycji korekty
::   WE: 0 - dokument manualny (domyślnie), 1 - import z komunikatu EDI
::   WY: zwraca akronim pola zle wypelnionego lub ''
::  OLD: \spr_poz_kor/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0)
|| _edi:=_a
|| _edi:=0
?};

_return:='';
_wal:=FAP.FAKS().WAL<>exec('bl_nwal','ustawienia');
_kor:=exec('spredkor','faktury_poz');
_korIl:=FKOR.IL>FAP.IL;

{? FAP.POWKOR<>null() & ~_kor || FAP.POWKOR:=null() ?};

{? FAP.FAKS().T().KOR='T' & FAP.FAKS().T().HIS='T' & FAP.M=null
|| _msg:='Należy podać indeks.'@;
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:='M_FAP'
?};
{? (_return='' | _edi) & FAP.IL<0
|| _msg:='Ilość nie może być ujemna.'@;
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:='IL'
?};
{? (_return='' | _edi) & FAP.CPR<0
|| _msg:='Cena nie może być ujemna.'@;
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:={?  exec('upr_cscz','ceny') || {? _wal || 'CWAL' || 'CPR' ?} || 'IL' ?}
?};
{? (_return='' | _edi) & FAP.CPR=0 & FAP.IL>0
|| _msg:='Jeżeli ilość jest większa od zera to cena nie może być równa zero.'@;
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:={?  exec('upr_cscz','ceny',) || {? _wal || 'CWAL' || 'CPR' ?} || 'IL' ?}
?};
{? (_return='' | _edi) & FAP.POZP & exec('czy_cena_z_tap','ceny','FAP',0).WYN
|| _return:={?  exec('upr_cscz','ceny') || {? _wal || 'CWAL' || 'CPR' ?} || 'IL' ?}
?};
{? _return='' | _edi
|| {? _edi
   || __msg:='';
      _wyn_r:=exec('spr_rab','ceny',DISP.RABP,0,!__msg,'fakpo');
      {? __msg<>''
      || _msg:='Pozycja %1: '[$FAP.POZ]+__msg;
         _msg:=gsub(_msg,'\n',' ');
         exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
      ?};
      &__msg
   || _wyn_r:=exec('spr_rab','ceny',DISP.RABP,0)
   ?};
   {? ~_wyn_r
   || {? ~_edi
      || win_disp
      ?};
      _return:=exec('rab_2130','ceny',FAP,'RABP')
   ?}
?};
{? (_return='' | _edi) & (FAP.SV=null | FKOR.SV=null)
|| _msg:='Należy podać stawkę VAT.'@;
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:='SV'
?};
{? (_return='' | _edi) & FAP.FAKS().WHERE<>'L' & FAP.FAKS().SZ='S' & FAP.M().RODZ='T' & _korIl & FKOR.DZ=date(0,0,0)
|| _msg:='Należy podać datę zwrotu.'@;
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:='DZ'
?};
{? (_return='' | _edi) & FAP.FAKS().WHERE<>'L' & FAP.FAKS().SZ='S' & FAP.M().RODZ='T' & _korIl & FKOR.DZ<FAKS.D
|| _msg:='Data zwrotu powinna być większa lub równa dacie sprzedaży %1.'@[$FAKS.D];
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:='DZ'
?};
{? (_return='' | _edi) & FAP.POWKOR=null() & exec('get','#params',{? BEER.SZ='S' || 300210 || 200210 ?},2)='T' & _kor
|| _msg:='Należy podać powód korekty.'@+
   {? INFO.POWKOR
   || ''
   || '\n\nPodanie powodu korekty niemożlwie ze względu na '
      'niewypełniony słownik powodów korekt w stałych systemu.'@
   ?};
   {? _edi
   || _msg:='Pozycja %1: '[$FAP.POZ]+_msg;
      exec('add_kom','#message',_msg,4,{? FAKS.ID='' || FAKS.SYM || FAKS.ID ?})
   || FUN.info(_msg)
   ?};
   _return:={? exec('FindInSet','#table','SLO','SL',INFO.POWKOR)<>null || 'POWKOR' || 'IL' ?}
?};
{? (_return='' | _edi) & (_ret:=exec('spr_poz_kor_ist','faktury_poz',0))<>''
:: intrastat
|| _return:=_ret
?};
:: sprawdzenie wdrożeniowe
{? (_return='' | _edi) & Plugin.runnable('VAL_POZLOG_001')
|| _return:=Plugin.run('VAL_POZLOG_001','FAP')
?};
_return


\spredkor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: sprawdza czy pozycja po korekcie rozni sie od faktury przed korekta dla biezacej pozycji korekty
::   WY: 0 - nie ma zmian, 1- zmieniano
::  OLD: \spredkor/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=FKOR.IL<>FAP.IL | FKOR.CPR<>FAP.CPR | FKOR.RAB<>FAP.RAB | FKOR.CN<>FAP.CN | FKOR.CB<>FAP.CB | FKOR.WN<>FAP.WN
    | FKOR.SV<>FAP.SV | FKOR.WV<>FAP.WV | FKOR.WB<>FAP.WB;
_wyn


\pkor_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: dodawanie pozycji na korekcie
::  OLD: \pkor_add/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag')

|? FAP.POZP
|| FUN.info('Pozycja powiązana z %1 pozycją na korekcie faktury.\n'
            'Operacja niemożliwa.'@[form(FAP.POZP,,0,'99')]);
   0
|? FUN.ask('Dodawana pozycja korekty będzie powiązana\nz bieżącą pozycją dokumentu korygowanego.'
           '\n\nDodać nową pozycję do dokumentu korygującego?'@)
||
   FAP.cntx_psh();
   _poz:=exec('NewOrder','#table','FAP','POZ',,1);
   _pozp:={? FAP.POZP<>0 || FAP.POZP || FAP.POZ ?};
   FAP.POZ:=_poz;
   FAP.memo_get(,'UWAGI');
   {? FAP.add(1)
   ||
      FAP.IL:=0;
      FAP.IL2:=0;
      FAP.POZP:=_pozp;
      FAP.MASAN:=0;
      FAP.WF:=0;
      FAP.WS:=0;
      exec('liczfak','faktury_wspolne');
      exec('fill_fld2prn','faktury_poz');
      {? FAP.put() || FAP.memo_put(,'UWAGI') ?}
   || FUN.info('Dołączenie pozycji nie powiodło się.'@)
   ?};
   FAP.cntx_pop()
?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
''


\pkor_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: usuwanie pozycji na korekcie
::  OLD: \pkor_del/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)

|? FAP.POZ<>FAP.POZP & FAP.POZP<>0
||
   {? FUN.ask('Usunąć bieżący wiersz?'@)
   || exec('delFAP_K','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),null());
      exec('fap2del','faktury_poz',0);
      VAR_DEL.delete('__msk');
      FAP.cntx_psh();
      exec('ReNumAfterDel','#table','FAP','POZ');
      FAP.cntx_pop();
      exec('liczfak','faktury_wspolne');
      exec('sumfak','faktury_wspolne')
   ?}
||
   FUN.info('Można usuwać tylko ręcznie dodawane pozycje.'@)
?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)


\kor_zwrb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: zwrot dla biezacej pozycji w buforze
::   WE: _a - 1-jedna pozycja, 0-wszystkie pozycje
::       _b - SLO.ref - powod korekty
::       _c - wartości pozycji do skorygowania
::  OLD: \kor_zwrb/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};
{? var_pres('_b')<>type_of(null()) || _b:=null() ?};
_warpoz2kor:={? var_pres('_c')=type_of(FIRMA) || _c || ~~ ?};

_powkor:=_b;
_is_warpoz2kor:=type_of(_warpoz2kor)<>type_of(~~) & _warpoz2kor.find_key(FAP.POZ);

_r_lock:={? _a=0 || 2 || exec('r_lock_one','#table',FAKS,FAKS.ref) ?};
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag')

|? FAP.POZP
|| FUN.info('Pozycja powiązana z %1 pozycją na korekcie faktury.\nOperacja niemożliwa.'@[form(FAP.POZP,,0,'99')]);
   0
||
   {? _a & INFO.POWKOR
   || {? ~exec('be_fap_powkor','faktury_nag') || return() ?};
      SLO.cntx_psh();
      SLO.index('SL');
      SLO.prefix(SLU.ref());
      SLO.win_sel('ONE_SEL');
      SLO.hdr_sel('Powody korekt'@);
      _powkor:={? SLO.select() || SLO.ref() || null() ?};
      SLO.cntx_pop();
      {? _powkor=null() & exec('get','#params',300210,2)='T'
      || FUN.info('Należy podać powód korekty.'@);
         return()
      ?};
      SLO.hdr_sel('');
      {? FAP.M().RODZ='T' & exec('dz','faktury_nag')=0 || return() ?}
   ?};

   {? _is_warpoz2kor
   ||
      FAP.IL:=_warpoz2kor.IL;
      FAP.WN:=_warpoz2kor.WN;
      FAP.WV:=_warpoz2kor.WV;
      FAP.WB:=_warpoz2kor.WB;
      FAP.WWAL_P:=_warpoz2kor.WWAL_P;
      FAP.VWAL_P:=_warpoz2kor.VWAL_P;
      FAP.BWAL_P:=_warpoz2kor.BWAL_P;
      FAP.WWAL:=_warpoz2kor.WWAL;
      FAP.VWAL:=_warpoz2kor.VWAL;
      FAP.BWAL:=_warpoz2kor.BWAL;
      FAP.IL2:=_warpoz2kor.IL2;
      FAP.MASAN:=_warpoz2kor.MASAN;
      FAP.ILUJM:=_warpoz2kor.ILUJM;
      FAP.WF:=_warpoz2kor.WF;
      FAP.WS:=_warpoz2kor.WS
   ||
      FAP.IL:=-FKOR.IL;
      FAP.WN:=-FKOR.WN;
      FAP.WV:=-FKOR.WV;
      FAP.WB:=-FKOR.WB;
      FAP.WWAL_P:=-FKOR.WWAL_P;
      FAP.VWAL_P:=-FKOR.VWAL_P;
      FAP.BWAL_P:=-FKOR.BWAL_P;
      FAP.WWAL:=-FKOR.WWAL;
      FAP.VWAL:=-FKOR.VWAL;
      FAP.BWAL:=-FKOR.BWAL;
      FAP.IL2:=-FKOR.IL2;
      FAP.MASAN:=-FKOR.PMASAN;
      FAP.ILUJM:=-FKOR.PILUJM;
      FAP.WF:=-FKOR.PWF;
      FAP.WS:=-FKOR.PWS;
      {? type_of(_warpoz2kor)<>type_of(~~)
      ||
         _warpoz2kor.POZ:=FAP.POZ;
         _warpoz2kor.IL:=FKOR.IL;
         _warpoz2kor.WN:=FKOR.WN;
         _warpoz2kor.WV:=FKOR.WV;
         _warpoz2kor.WB:=FKOR.WB;
         _warpoz2kor.WWAL_P:=FKOR.WWAL_P;
         _warpoz2kor.VWAL_P:=FKOR.VWAL_P;
         _warpoz2kor.BWAL_P:=FKOR.BWAL_P;
         _warpoz2kor.WWAL:=FKOR.WWAL;
         _warpoz2kor.VWAL:=FKOR.VWAL;
         _warpoz2kor.BWAL:=FKOR.BWAL;
         _warpoz2kor.IL2:=FKOR.IL2;
         _warpoz2kor.MASAN:=FKOR.PMASAN;
         _warpoz2kor.ILUJM:=FKOR.PILUJM;
         _warpoz2kor.WF:=FKOR.PWF;
         _warpoz2kor.WS:=FKOR.PWS;
         _warpoz2kor.add()
      ?}
   ?};
   FAP.POWKOR:=_powkor;
   {? FAP.put()
   ||
::    naliczenie opłat dodatkowych
      exec('actTAXS','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),FAP.M,FAP.M);
      {? _a
      ||
         exec('sumfak','faktury_wspolne');

         exec('plat_przel','faktury_plat',FAKS.ref())

      ?}
   ?}
?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)


\kor_zwra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: zwrot dla wszystkich pozycji w buforze
::   WE: _a - wartości pozycji do skorygowania
::  OLD: \kor_zwra/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_warpoz2kor:={? var_pres('_a')=type_of(FIRMA) || _a || ~~ ?};
_powkor:={? var_pres('_b')=type_of(null()) || _b || null() ?};

FAP.cntx_psh();
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag')

|? FAP.first()
||
   _continue:=1;
   {? _powkor=null() & INFO.POWKOR
   || {? ~exec('be_fap_powkor','faktury_nag') || _continue:=0 ?};
      {? _continue>0
      ||
         SLO.cntx_psh();
         SLO.index('SL');
         SLO.prefix(SLU.ref());
         SLO.win_sel('ONE_SEL');
         _powkor:={? SLO.select() || SLO.ref() || null() ?};
         SLO.cntx_pop();
         {? _powkor=null() & exec('get','#params',300210,2)='T'
         || FUN.info('Należy podać powód korekty.'@);
            _continue:=0
         ?};
         {? _continue>0
         ||
            FAP.cntx_psh();
            _mat:=0;
            _loop:=FAP.first();
            {!
            |? _loop
            |!
               _mat:=FAP.M().RODZ='T';
               _loop:=_mat=0 & FAP.next()
            !};
            FAP.cntx_pop();
            {? _mat & exec('dz','faktury_nag')=0 || _continue:=0 ?}
         ?}
      ?}
   ?};
   {? _continue>0
   ||
      {!
      |? exec('wysw_kor','faktury_poz');
         {? ~FAP.POZP || exec('kor_zwrb','faktury_poz',0,_powkor,_warpoz2kor) ?};
         FAP.next()
      !};
      exec('sumfak','faktury_wspolne');

      exec('plat_przel','faktury_plat',FAKS.ref())
   ?}
?};
FAP.cntx_pop();
FAP.get();
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)


\fak2_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.00]
:: OPIS: Formula Rekord przed dla okienek wertowania tabeli FAKS#02
::----------------------------------------------------------------------------------------------------------------------
{? FAP.POZP<>0
|| 'FAKS#02#01'
|? (_opc:=exec('spr_kor','faktury_poz'))
|| {? _opc=1 || 'FAKS#02#02' || 'FAKS#02#03' ?}
|| ''
?}


\spr_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: sprawdza czy pozycja po korekcie rozni sie od faktury przed korekta dla biezacej pozycji korekty
::   WY: 0 - nie ma zmian, 1- zmieniano, 2-zmieniono wartosci INTRASTAT
::  OLD: \spr_kor/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_old:="{? _a<>_b
        || FAKS.use(_b);
           FAP.use('fakpo'+(_b+3));
           FAKSPL.use('fakpl'+(_b+3))
        ?}";
_new:="{? _a<>_b
        || FAKS.use('faktu'+ST.ODDZ+(2-$ST.AR));
           FAP.use('fakpo'+ST.ODDZ+(2-$ST.AR));
           FAKSPL.use('fakpl'+ST.ODDZ+(2-$ST.AR))
        ?}";
_poz:=FAP.POZ;
_refkor:=BB.sqlint(FAP.FAKS().LKSQL);
_korm:=form(8+FAKS.LKSQL);

_frab:=FAP.FAKS().RAB;
_il:=FAP.IL;

FAP.cntx_psh();
_ff:="
   _wyn:=obj_new('rabn','dwal');
   _wyn.rabn:=FAKS.RAB;
   _wyn.dwal:=FAKS.WAL<>FAKS.NWAL;
   _wyn
";
_tabwar:=exec('FindAndGet','#table',FAKS,FAKS.LKSQL,,_ff,0);
_rabn:=_tabwar.rabn;
_dwal:=_tabwar.dwal;
exec('wysw_kor','faktury_poz');
{? BEER.SZ<>'S'
   | exec('get','#params',800810,2)<>'C'
   | exec('get','#params',800813,2)*{? FAKS.KZ_T=null() || 'KOR' || 'KZ' ?}=0
|| _cpr_p:={? _dwal || FKOR.CWAL || FKOR.CPR ?};
   _cpr_po:={? _dwal || FKOR.BCWAL || FKOR.BCPR ?};
   _rab_p:=FKOR.RAB
|| _cpr_p:=exec('cen_rab','faktury_wspolne',_rabn,_dwal);
   _cpr_po:={? _dwal || FKOR.BCWAL || FKOR.BCPR ?};
   _rab_p:=0
?};
FAP.cntx_pop();

_rab:=FAP.RAB;
_cn:=FAP.CN;
_cb:=FAP.CB;
_wn:=FAP.WN;
_sv:=FAP.SV;
_wv:=FAP.WV;
_wb:=FAP.WB;

:: intrastat
_kp:=FAP.KP;
_bwf:=FAP.WF;
_bws:=FAP.WS;
_bilujm:=FAP.ILUJM;

TYPYSP.cntx_psh();
_vatzpoz:=FAKS.T().VATZPOZ='T';
_wg_cb:=TYPYSP.FIS='T';
TYPYSP.cntx_pop();

FAP.cntx_psh();
FAKS.cntx_psh();
FAKSPL.cntx_psh();
_old(FAKS.name,_korm);
FAP.index('FAP');
FAP.prefix(_refkor);
{? FAP.find_key(_poz)
||
   {? _il<>0 || _wyn:=1 ?};
   {? _cpr_p<>_cpr_po || _wyn:=1 ?};
   {? _rab_p<>_rab  || _wyn:=1 ?};
   {? _cn<>0 || _wyn:=1 ?};
   {? _cb<>0 || _wyn:=1 ?};
   {? (_vatzpoz | _wg_cb=0) & _wn<>0 || _wyn:=1 ?};
   {? _sv<>FAP.SV || _wyn:=1 ?};
   {? _vatzpoz & _wv<>0 || _wyn:=1 ?};
   {? (_vatzpoz | _wg_cb) & _wb<>0 || _wyn:=1 ?};

   {? ~_wyn
   || {? _kp<>FAP.KP || _wyn:=2 ?};
      {? _bwf<>0 || _wyn:=2 ?};
      {? _bws<>0 || _wyn:=2 ?};
      {? _bilujm<>0 || _wyn:=2 ?}
   ?}
||
   _wyn:=1
?};
_new(FAKS.name(),_korm);
FAKSPL.cntx_pop();
FAKS.cntx_pop();
FAP.cntx_pop();
_wyn


\inf_kolf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: legenga na korekcie
::  OLD: \inf_kolf/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','FAKS#02')


\inf_korh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PS [17.00]
:: OPIS: Legenda na pozycjach korekty historycznej.
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','FAKS#02#02')


\korh_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dodawanie pozycji korekty historycznej,
::       old_ref; new_ref - refy faktury biezacej oraz historycznej
::  OLD: \korh_add/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};

_reffap:=FAP.ref;
{? var_pres('old_ref')<>7
||
   _ref:=FAKS.LKSQL;
   _name:=form(8+FAKS.LKSQL);
   FAKS.cntx_psh();
   FAKS.use(_name);
   {? FAKS.seek(_ref,)
   || old_ref:=FAKS.ref
   || old_ref:=null
   ?};
   FAKS.cntx_pop()
?};
{? var_pres('f_rok')<>1
||
   f_rok:=form(8+FAKS.LKSQL)+2
?};

:: [rr] czy przepisac dane z pozycji bylo do winno byc
FKOR.PODP:=1;

FKOR.IL:=0;
FKOR.CPR:=0;
FKOR.CN:=0;
FKOR.CB:=0;
FKOR.CWAL:=0;
FKOR.RAB:=0;
FKOR.SV:=null;
FKOR.WN:=FKOR.WWAL_P:=FKOR.WWAL:=FKOR.BWWAL_P:=0;
FKOR.WV:=FKOR.VWAL_P:=FKOR.VWAL:=FKOR.BVWAL_P:=0;
FKOR.WB:=FKOR.BWAL_P:=FKOR.BWAL:=FKOR.BBWAL_P:=0;
FKOR.WAL:=FAKS.WAL;
FKOR.KRS:=0;

:: intrastat
FKOR.PMASAN:=0;
FKOR.PILUJM:=0;
FKOR.PWF:=0;
FKOR.PWS:=0;

{? FAP.last()
|| _nr:=FAP.POZ+1
|| _nr:=1
?};

FAP.blank();
FAP.WAL:={? FAP.FAKS().WAL<>null || FAP.FAKS().WAL || INFO.NAROD ?};
FAP.WAL_H:=FAP.WAL;
FAP.POZ:=_nr;
__hispoz:=_nr;
:: intrastat
exec('istatr_faks2fap','faktury_nag');
FAP.add();
FAKS.cntx_psh();
FAP.cntx_psh();
FAKSPL.cntx_psh();
exec('open','open_tab',ST.ODDZ,f_rok);
{? FAKS.seek(old_ref)
||
   FAP.index('FAP');
   FAP.prefix(FAKS.ref);
   FAP.POZ:=_nr;
   FAP.FAKS:=FAKS.ref;
   FAP.IL:=FKOR.IL;
   FAP.CPR:=FKOR.CPR;
   FAP.CWAL:=FKOR.CWAL;
   FAP.RAB:={? exec('czyrabp','ceny',FAKS.RAB_TYP) || FKOR.RAB || 0 ?};
   FAP.RABK:={? ~exec('czyrabp','ceny',FAKS.RAB_TYP) || FKOR.RAB || 0 ?};
   FAP.SV:=FKOR.SV;
   FAP.WAL:=FKOR.WAL;
   FAP.KRS:=FKOR.KRS;

   FAP.CN:=FKOR.CN;
   FAP.CB:=FKOR.CB;
   FAP.JM:=FAP.M().J;

:: intrastat
   FAP.MASAN:=FKOR.PMASAN;
   FAP.ILUJM:=FKOR.PILUJM;
   FAP.KP:=FKOR.KP;
   FAP.WF:=FKOR.PWF;
   FAP.WS:=FKOR.PWS;
   FAP.add()
?};
FAP.cntx_pop();
FAKS.cntx_pop();
FAKSPL.cntx_pop();
exec('open','open_tab',ST.ODDZ,2-$ST.AR);
{? exec('korh_put','faktury_poz',1,0)=0
|| exec('korh_del','faktury_poz','N');
   FAP.seek(_reffap)
?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
''


\korh_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: usuwanie pozycji korekty historycznej
::       old_ref; new_ref - refy faktury biezacej oraz historycznej
::   WE: [_a] - czy pytac o usuniecie pozycji (T/N domyslnie tak)
::  OLD: \korh_del/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>2 || _a:='T' ?} || _a:='T' ?};

_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};

{? {? _a='T' || __hispoz:=FAP.POZ; FUN.ask('Usunąć bieżący wiersz?'@) || 1 ?}
||
   do();
   {? var_pres('old_ref')<>7
   ||
      _ref:=FAP.FAKS().LKSQL;
      _name:=form(8+FAP.FAKS().LKSQL);
      FAKS.cntx_psh();
      FAKS.use(_name);
      {? FAKS.seek(_ref,)
      || old_ref:=FAKS.ref
      || old_ref:=null
      ?};
      FAKS.cntx_pop()
   ?};
   _poz:={? FAP.POZ<>__hispoz || __hispoz || FAP.POZ ?};
   FAKS.cntx_psh();
   FAP.cntx_psh();
   FAKSPL.cntx_psh();
   exec('open','open_tab',ST.ODDZ,'hs');
   {? FAKS.seek(old_ref)
   ||
      FAP.index('FAP');
      FAP.prefix(FAKS.ref,_poz);
      {? FAP.first()
      || {!
         |?
            exec('opak_zwr_del','opakow_stany',$FAP.ref(),'');
            FAP.del()
         !}
      ?}
   ?};
   exec('open','open_tab',ST.ODDZ,2-$ST.AR);
   FAP.cntx_pop();
   FAKS.cntx_pop();
   FAKSPL.cntx_pop();
   exec('opak_zwr_del','opakow_stany',$FAP.ref(),'');
   exec('delprzyp','fakso','FAKSO','FAP',FAP.ref);
   {? FAP.del(1,1)
   ||
      FAP.cntx_psh();
      exec('sumfak','faktury_wspolne');
      FAP.cntx_pop()
   || undo()
   ?};
   end()
?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
''


\poz_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: okienko grupowe dla pozycji faktury zakupowej
::   WY: uchwyt okienka
::  OLD: \poz_grp/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__zakinf','__zakin2');

__zakinf:=tab_tmp(2,'DOK','STRING[20]',''
           ,'MAG','STRING[10]',''
           ,'DAT','DATE',''
           ,'ILE','REAL',''
           ,'CEN','REAL',''
           ,'WAR','REAL',''
           ,'DK_REF','STRING[16]',''
           ,'TYP','STRING[10]','');
exec('war_hid','ceny',__zakinf,'CEN','WAR');

_sel_zak:=__zakinf.mk_sel('Dokumenty przychodowe powiązane z pozycją faktury'@,'P',,'pow_zak3sel');

__zakinf.win_fld(_sel_zak,,'MAG',,,8,,1,'Magazyn'@);
__zakinf.win_fld(_sel_zak,,'TYP',,,10,,1,'Typ dokumentu'@);
__zakinf.win_fld(_sel_zak,,'DOK',,,20,,1,'Dokument'@);
__zakinf.win_fld(_sel_zak,,'DAT',,,10,,1,'Z dnia'@);
__zakinf.win_fld(_sel_zak,,'CEN',,,12,2,1,'Cena'@);
__zakinf.win_fld(_sel_zak,,'ILE',,,12,3,1,'Ilość'@);
__zakinf.win_fld(_sel_zak,,'WAR',,,12,2,1,'Wartość'@);
__zakinf.win_act(_sel_zak,0,'Szukaj');
{? (2+menu_pth())<>'MR' & (3+menu_pth())<>'ZWR' & KHVZ.WIDOKI=0 & FAKS.ZAK<>'T'
|| __zakinf.win_act(_sel_zak,0,'Formuła','&Usuń powiązanie'@@,,,,"exec('del_powf','faktury_poz')",,1
                    ,"exec('del_powf','faktury_poz')",,'U')
?};
_fb:="
   exec('FindAndGet','#table',DK,__zakinf.DK_REF,,\"exec('his_dost','magdok_wspolne')\")
";
__zakinf.win_act(_sel_zak,,'Formuła','&Historia dostaw'@@,,,,_fb);
__zakinf.win_sel(_sel_zak);

__zakinf.fld_fml('ILE','EDIT_FORMAT'
          ,"'in_prec='+{? (2+cur_kwin())='e_' || $FAP.M().DOKL || $ST.DOKL ?}");
__zakinf.fld_fml('ILE','DISPLAY_FORMAT'
          ,"'out_prec='+{? (2+cur_kwin())='e_' || $FAP.M().DOKL || $ST.DOKL ?}");
__zakinf.fld_fml('CEN','EDIT_FORMAT'
          ,"'in_prec='+{? (2+cur_kwin())='e_' || $FAP.M().DOKL_C || $ST.DOKL_C ?}");
__zakinf.fld_fml('CEN','DISPLAY_FORMAT'
          ,"'out_prec='+{? (2+cur_kwin())='e_' || $FAP.M().DOKL_C || $ST.DOKL_C ?}");

__zakin2:=tab_tmp(1,'OPI','STRING[20]',''
           ,'WAR','REAL','');
__zakin2.fld_fml('WAR','BEFORE_DISPLAY',"
   {? cur_tfld.OPI='Faktura' || exec('upr_cz','ceny')
   |? cur_tfld.OPI='Magazyn' || exec('upr_cm','ceny')
   |? cur_tfld.OPI='Wg VAT' || exec('upr_cz','ceny')
   ?}");

_sel_za2:=__zakin2.mk_sel('Kontrola rozliczenia'@,'P',,'pow_zak4sel');

__zakin2.win_fld(_sel_za2,,'OPI',,,9,,1,'');
__zakin2.win_fld(_sel_za2,,'WAR',,,10,2,1,'');
__zakin2.win_act(_sel_za2,0,'Szukaj');
__zakin2.win_sel(_sel_za2);

__zakin2.clear;
__zakin2.blank; __zakin2.OPI:='Faktura'; __zakin2.WAR:=0; __zakin2.add(1);
__zakin2.blank; __zakin2.OPI:='Magazyn'; __zakin2.WAR:=0; __zakin2.add(1);
__zakin2.blank; __zakin2.OPI:='Wg VAT'; __zakin2.WAR:=0; __zakin2.add(1);

_acr_grp:=FAP.grp_make('Dokument zakupu'@,,'poz_faks_zak');
FAP.grp_sel(_acr_grp,FAP,{? BPMN.DISPLAY || 'WER_IDOK' || 'WER_I' ?}
 ,,"exec('akt_pozy','faktury_poz');exec('opak_zak','opakow')",,,4,,,,,'maximized_with_title');
FAP.grp_splt(_acr_grp,,'horizontal','tab2');
FAP.grp_sel(_acr_grp,__zakin2,_sel_za2,,,,,3,,,,,'maximized_with_title');
FAP.grp_splt(_acr_grp,'tab2','vertical','tab3');
FAP.grp_sel(_acr_grp,__zakinf,_sel_zak,'Dokumenty przychodowe powiązane z pozycją faktury'@,,,,2,,,,,'maximized');
exec('opkz_init','opakow');
FAP.grp_sel(_acr_grp,__opkz_tab,__opkz_sel,'Opakowania powiązane z pozycją magazynową'@,,0,31,2,,,,,'maximized');

_acr_grp


\akt_pozy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: prefiksy dla FAKSV
::  OLD: \akt_pozy/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
FAKSV.index('FAKS_SV');
FAKSV.prefix($FAKS.ref);
exec('suma_fsv','faktury_vat');

__zakinf.clear;
__zakinf.erase;
{? exec('FindInSet','#table','FAP','FAP',FAKS.ref())<>null
|| FAP2DK.index('FAP');
   FAP2DK.prefix(FAKS.WHERE,$FAP.ref);
   {? FAP.size>0 & FAP2DK.first
   || {!
      |? __zakinf.blank;
         {? FAP2DK.ND<>''
         || ND.cntx_psh;
            DK.cntx_psh;
            ND.use(form(8+FAP2DK.ND));
            ND.clear;
            {? ND.seek(FAP2DK.ND,form(8+FAP2DK.ND))
            || __zakinf.MAG:=ND.MAG().SYM;
               __zakinf.DOK:=ND.SYM;
               __zakinf.DAT:=ND.D;
               __zakinf.TYP:=ND.TYP().T
            ?};
            DK.use(form(8+FAP2DK.DK));
            DK.clear;
            {? DK.seek(FAP2DK.DK,form(8+FAP2DK.DK))
            || __zakinf.CEN:=DK.C;
               __zakinf.WAR:=DK.WAR;
               __zakinf.DK_REF:=$DK.ref()
            ?};

            ND.cntx_pop;
            DK.cntx_pop;
            __zakinf.ILE:=FAP2DK.IL;
            __zakinf.WAR:=__zakinf.CEN*__zakinf.ILE $2;
            __zakinf.add(1)
         |? FAP2DK.ZD_RP<>''
         || exec('openzd_psh','open_tab');
            exec('openzd','open_tab',form(8+FAP2DK.ZD_RP)+3);
            ZD_RP.prefix();
            {? ZD_RP.seek(FAP2DK.ZD_RP)
            || __zakinf.MAG:='- USŁUGI -';
               __zakinf.DOK:=ZD_RP.ZD_POZ().ZD_NAG().SYM;
               __zakinf.DAT:=ZD_RP.ZD_RN().DR;
               __zakinf.TYP:=ZD_RP.ZD_POZ().ZD_NAG().T().T;
               __zakinf.CEN:=ZD_RP.ZD_POZ().CENA;
               __zakinf.DK_REF:=FAP2DK.ZD_RP
            ?};
            exec('openzd_pop','open_tab');
            __zakinf.ILE:=FAP2DK.IL;
            __zakinf.WAR:=__zakinf.CEN*__zakinf.ILE $2;
            __zakinf.add(1)
         ?};
         FAP2DK.next
      !}
   ?}
?};
exec('akt_poz2','faktury_poz');
1


\akt_poz2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: prefiksy dla pozostalych tabelek
::  OLD: \akt_poz2/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
__zakinf.clear;
__zakinf.first;
grp_disp(__zakinf,__zakinf.win_sel('?'));
__zakin2.clear;
__zakin2.first;
grp_disp(__zakin2,__zakin2.win_sel('?'));
1


\ADDPOZF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Dodaje pozycję fakutry
::  OLD: FD.ADDPOZF/funkcje.fml
::   WE: _a - M.ref()
::       _b - ilość
::       _c - cena przed rabatem
::       _d - rabat
::       _e - stawka vat
::       [_f] - Z-zlecenie podstawowe, D-usługa
::       [_g] - uwagi
::       [_h] - cena walutowa
::       [_i] - kurs
::       [_j] - waluta
::       [_k] - jednostka miary
::       [_l] - konto kosztowe
::       [_m] - rabat kwotowy
::       [_n] - TAR_H.ref
::       [_o] - promocja
::       [_p] - atrybuty
::       [_q] - znacznik czasowy cennika
::       [_r] - projekt
::       [_s] - 0/1 add
::       [_t] - data sprzedaży
::   WY: 0-nie dodano pozycji, 1-dodano pozycję
::----------------------------------------------------------------------------------------------------------------------
{? _<5
|| FUN.info('Zbyt mało danych do stworzenia pozycji faktury.'@);
   0
|| {? _>=6  || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};
   {? _>=7  || {? type_of(_g)<>2 || _g:='' ?} || _g:='' ?};
   {? _>=8  || {? type_of(_h)<>1 || _h:=0 ?}  || _h:=0  ?};
   {? _>=9  || {? type_of(_i)<>1 || _i:=0 ?}  || _i:=0  ?};
   {? _>=10 || {? type_of(_j)<>7 || _j:=exec('bl_wal','ustawienia') ?} || _j:=exec('bl_wal','ustawienia') ?};
   {? _>=11 || {? type_of(_k)<>7 || _k:=null ?} || _k:=null ?};
   {? _>=12 || {? type_of(_l)<>7 || _l:=null ?} || _l:=null ?};
   {? _>=13 || {? type_of(_m)<>1 || _m:=0 ?} || _m:=0 ?};
   {? _>=14 || {? type_of(_n)<>7 || _n:=null ?} || _n:=null ?};
   {? _>=15 || {? type_of(_o)<>7 || _o:=null ?} || _o:=null ?};
   {? _>=16 || {? type_of(_p)<>7 || _p:=null ?} || _p:=null ?};
   {? _>=17 || {? type_of(_q)<>1 || _q:=0 ?} || _q:=0 ?};
   {? _>=18 || {? type_of(_r)<>7 || _r:=null ?} || _r:=null ?};
   {? var_pres('_s')<>type_of(0) || _s:=1 ?};
   {? var_pres('_t')<>type_of(date(0,0,0)) || _t:=date(0,0,0) ?};
   FAP.index('FAP');
   FAP.prefix(FAKS.ref());
   FAP.blank();
   FAP.M:=_a;
   FAP.IL:=_b;
   FAP.CPR:=_c;
   FAP.RAB:=_d;
   FAP.RABK:=_m;
   FAP.SV:=exec('m_vat','material',FAP.M,FAP.FAKS().KH,exec('exp_ue','typysp'),,,_e,2,,,FAKS.KRAJ_VAT);
   FAP.OO:={? exec('oo_zak','faktury_poz') || FAP.M().OO || '' ?};
:: wymagany split payment
   FAP.SP_WYM:='N'; {? FAP.M || FAP.SP_WYM:=FAP.M().SP_WYM ?};
:: jpk
   {? var_pres('JPK_GTU',MGR)>0
   ||
      {? FAP.M().JPK_GTU=null & exec('get','#params', 300120, 2)='T'
      || FAP.JPK_GTU:=FAP.M().MGR().JPK_GTU
      || FAP.JPK_GTU:=FAP.M().JPK_GTU
      ?}
   ?};
   FAP.Z:=_f;
   FAP.U:=_g;
   FAP.memo_set(_g,'UWAGI');
   FAP.CWAL:=_h;
   FAP.KRS:=_i;
   FAP.WAL:=_j;
   FAP.WAL_H:=FAP.WAL;
   {? _k<>null || FAP.JM:=_k || FAP.JM:=FAP.M().J ?};
   exec('fill_fld2prn','faktury_poz');
   FAP.KK:=_l;
   FAP.TAR_H:=exec('tar_h_copy','ceny_dok',_n,FAP.name+3);
   FAP.TAR_TMS:=_q;
   FAP.PROMO:=_o;
   FAP.DK_C:=_p;
:projekty
   PROJEKTY.cntx_psh();TYPYSP.cntx_psh();PROJTYPY.cntx_psh();
   PROJEKTY.prefix();TYPYSP.prefix();PROJTYPY.prefix();
   FAP.PROJEKTY:=
   {? _r<>null & PROJEKTY.seek(_r) & (FAKS.T().PROJZAKR='Wszystkie' | (4+PROJEKTY.T().R)=(4+FAKS.T().PROJZAKR))
   || _r
   || null
   ?};
   PROJEKTY.cntx_pop();TYPYSP.cntx_pop();PROJTYPY.cntx_pop();
   exec('uzupjdod','magdok_poz','FAP');
   {? FAP.T2='M' & FAP.JM=FAP.J2 || FAP.WS2:=1; FAP.T2:='G' ?};
   FAP.D:=_t;

   exec('istatr_faks2fap','faktury_nag');
   exec('liczfak','faktury_wspolne');
   exec('liczfak_ist','faktury_wspolne',1,1);

   {? FAP.M=null() || FAP.WS2:=1; FAP.IL2:=FAP.IL ?};
   FAP.KH:=FAP.FAKS().KH;
   FAP.KH_ODB:=FAP.FAKS().KH_ODB;
   {? _s
   ||
      _res:=FAP.add();
      FAP.memo_put(,'UWAGI');
      _res
   ||
      1
   ?}
?}


\fap_gru2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: Grupuje pozycje na dokumencie o podanym ref dla biezacej maski.
::       Uwaga: na fakturze wychodza groszowe zaokraglenia w zwiazku z przeliczeniami.
::       Uwaga: funkcja dziala w transakcji.
::   WE:
::   WY: 1 - jezeli pozycje na dokumencie sprzedazy beda grupowane
::       0 - jezeli pozycje na dokumencie sprzedazy nie beda grupowane
::  OLD: \fap_gru2/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};

_wyn:=0;
_res:=FAP.size>0 & FAP.FAKS().SZ='S' & (_pa:=exec('get','#params',300103,2))<>'N';
{? _res
||
:: sprawdza czy jest w ogole cos do grupowania
   _res:=0;
   _faks:=FAP.FAKS;
   FAP.cntx_psh();
   FAP.index('FAP');
   FAP.prefix(_faks);
   {? FAP.first()
   || {!
      |? _mat:=FAP.M;
         FAP.cntx_psh();
         FAP.index('FAM');
         FAP.prefix(_faks,_mat);
         _res:=FAP.size()>1;
         FAP.cntx_pop();
         ~_res & FAP.next()
      !}
   ?};
   FAP.cntx_pop()
?};

{? {? _res
   || {? _pa='P' | _pa=''
      || FUN.ask('Grupować pozycje dokumentu sprzedaży?'@)
      |? _pa='T'
      || 1
      || 0
      ?}
   || 0
   ?}
|| _wyn:=exec('fap_gru3','faktury_poz',FAKS.ref());
   exec('sumfak','faktury_wspolne')
?};
_wyn


\fap_gru3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: grupuje pozycje na dokumencie o podanym ref dla biezacej maski
::       uwaga na fakturze moga wychodzic groszowe zaokraglenia w zwiazku z przeliczeniami
::   WE:  _a  - ref faktury FAKS.ref
::   WY:
::  OLD: \fap_gru3/faktury.fml
::----------------------------------------------------------------------------------------------------------------------
_czydkc:=exec('get','#params',300113,2)='T';
_wyn:=0;
_msk:=exec('tab_mask','faktury_wspolne',6,form(ST.AR-2000,-2,0,'99'));
FAKS.cntx_psh();
FAKS.prefix();
{? FAKS.seek(_a)  & (_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref()))
||
   FAP.index('FAP');
   FAP.prefix(FAKS.ref());
   _size:=FAP.size();
   {? _size>1
   || {? ~_czydkc & FAP.first() || {! |? FAP.DK_C:=null; FAP.put(1); FAP.next() !} ?};
      _nam:=BB.refsql(FAP.FAKS);
      _fap_tmp:=sql('
         select FAP.M || FAP.KP M, FAP.TAR_H TAP, FAP.PROMO PROMO, FAP.JM JM, FAP.SV SV, FAP.CPR CPR, FAP.RAB RAB
            , FAP.RABK RABK, FAP.CN CN, FAP.CB CB, FAP.CWAL CWAL, FAP.WAL WAL, sum(FAP.IL) IL, sum(FAP.IL2) IL2
            , FAP.J2 J2, FAP.WS2 WS2,
            FAP.DK_C DK_C, FAP.PROJEKTY PROJEKTY, FAP.D D
         from  FAP where FAP.FAKS= \':_a\'
         group by FAP.M, FAP.KP, FAP.TAR_H, FAP.PROMO, FAP.JM, FAP.SV, FAP.CPR, FAP.RAB, FAP.RABK, FAP.CN
            , FAP.CB, FAP.CWAL,
            FAP.WAL, FAP.J2, FAP.WS2, FAP.DK_C, FAP.PROJEKTY, FAP.D
         order by 1',_nam);
      _fap_tm2:=sql('
         select FAP.REFERENCE REF, 0 as ref_new, FAP.M || FAP.KP M, FAP.TAR_H TAP, FAP.PROMO PORMO, FAP.JM JM
            , FAP.SV SV, FAP.CPR CPR, FAP.RAB RAB, FAP.RABK RABK, FAP.CN CN, FAP.CB CB, FAP.CWAL CWAL, FAP.WAL WAL
            , FAP.J2 J2, FAP.WS2 WS2, FAP.DK_C DK_C, FAP.D D
         from  FAP where FAP.FAKS= \':_a\'
         order by 3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19',_nam);
      _fap_tm3:=sql('select \'                \' as FAP, 0 as FAP_NEW, 0 as DK, \'                \' as SQL
         from FIRMA where 0=1 order by 1');
      _fap_tm3.erase();

      _fps:=_fap_tmp.size();

      {? _fps<_size & _fps>0
      ||
         do();
         _wyn:=1;
         {!
         |? _refsql:=$FAP.ref();
            _akt:=FAP.name()+2;
            FAKS.cntx_psh();
            FAP.cntx_psh();
            ND.cntx_psh();
            DK.cntx_psh();
            _msk.clear();
            {? _msk.first()
            || {!
               |? exec('open','open_tab',ST.ODDZ,_msk.ROK);

                  DK.index('FAP');
                  DK.prefix(_refsql,_refsql);
                  {? DK.first()
                  || {!
                     |?
                        _fap_tm3.blank();
                        _fap_tm3.FAP:=_refsql;
                        _fap_tm3.DK:=#DK.ref();
                        _fap_tm3.SQL:=$DK.ref();
                        _fap_tm3.add();
                        _refik:=DK.ref; _ok:=DK.next();
                        DK.cntx_psh();
                        DK.clear;
                        {? DK.seek(_refik)
                        || DK.FAP:='';
                           DK.put();
                           exec('delfap2dk','magdok_wspolne',$DK.ref())
                           ?};
                        DK.cntx_pop();
                        _ok
                     !}
                  ?};
                  _msk.next()
               !}
            ?};
            exec('open','open_tab',ST.ODDZ,_akt);
            FAKS.cntx_pop();
            FAP.cntx_pop();
            ND.cntx_pop();
            DK.cntx_pop();
            exec('delprzyp','fakso','FAKSO','FAP',FAP.ref);
::          jesli powiazany dokument opakowan
            exec('dk_del','magdok_poz',$FAP.ref());
::          usuniecie zwrotow utworzonych przez pozycje faktury za opakowania zwrotne po terminie
            exec('opak_zwr_del','opakow_stany',$FAP.ref(),'');
            FAP.del()
         !};
         _fap_tmp.first();
         {!
         |?
            M.prefix(); JM.prefix(); SLO.prefix();
            _sv:=_wal:=_jm:=null;
            _sref:=16+_fap_tmp.M;{? ~M.seek(_sref,M.name()) || undo();_wyn:=0 ?};
            _sref:=BB.sqlint(_fap_tmp.JM);{? ~JM.seek(_sref,JM.name()) & _sref>0 || undo();_wyn:=0 || _jm:=JM.ref() ?};
            {? _fap_tmp.SV<>''
            || _sref:=_fap_tmp.SV;{? ~SLO.seek(_sref,SLO.name()) || undo();_wyn:=0 || _sv:=SLO.ref() ?}
            || _sv:=null
            ?};
            {? _fap_tmp.WAL<>''
            || _sref:=_fap_tmp.WAL;{? ~SLO.seek(_sref,SLO.name()) || undo();_wyn:=0 || _wal:=SLO.ref() ?}
            || _wal:=null
            ?};
            {? _fap_tmp.TAP<>''
            || _sref:=_fap_tmp.TAP;
               TAR_H.cntx_psh();
               TAR_H.use(8+_fap_tmp.TAP);
               TAR_H.prefix;
               {? TAR_H.seek(_sref,) || _tap:=TAR_H.ref() || _tap:=null ?};
               TAR_H.cntx_pop()
            || _tap:=null
            ?};
            {? _fap_tmp.PROMO<>''
            || _sref:=_fap_tmp.PROMO;PROMO.prefix();
               {? PROMO.seek(_sref,PROMO.name()) || _promo:=PROMO.ref() || _promo:=null ?}
            || _promo:=null
            ?};
            PROJEKTY.cntx_psh();PROJEKTY.prefix();
            _projekty:={? PROJEKTY.seek(_fap_tmp.PROJEKTY) || PROJEKTY.ref() || null() ?};
            PROJEKTY.cntx_pop();
            exec('przyjdod','jm',_fap_tmp.J2,_fap_tmp.WS2,_fap_tmp.IL2);
            exec('ADDPOZF','faktury_poz',M.ref,_fap_tmp.IL,_fap_tmp.CPR,_fap_tmp.RAB,_sv,
               ,,_fap_tmp.CWAL,,_wal,_jm,,_fap_tmp.RABK,_tap
               ,_promo,{? _czydkc || exec('FindAndGet','#table','DK_C',_fap_tmp.DK_C,,,null()) || null() ?},
               ,_projekty,,_fap_tmp.D);
            _fap_tm2.prefix(_fap_tmp.M, _fap_tmp.TAP, _fap_tmp.PROMO, _fap_tmp.JM, _fap_tmp.SV,
               _fap_tmp.CPR,_fap_tmp.RAB, _fap_tmp.RABK, _fap_tmp.CN, _fap_tmp.CB, _fap_tmp.CWAL, _fap_tmp.WAL,
               _fap_tmp.J2,_fap_tmp.WS2,{? _czydkc || _fap_tmp.DK_C || '' ?},_fap_tmp.D);
            {? _fap_tm2.first()
            ||
               {!
               |?
                  _fap_tm2.REF_NEW:=#FAP.ref();
                  _fap_tm2.put();
                  _fap_tm2.next()
               !}
            ?};
            FAP.TAR_H:=_tap;
            FAP.put();
::          naliczenie opłat dodatkowych
            exec('actTAXS','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),FAP.M,null());
            exec('inf_dod','fakso',0,'fakpo');
            _fap_tmp.next()
         !};
         exec('sumfak','faktury_wspolne');
         _ind:=_fap_tm2.ndx_tmp('',,'REF',,);
         _fap_tm2.index(_ind);
         _fap_tm3.prefix();
         {? _fap_tm3.first()
         || {!
            |?
               _fap_tm2.prefix(_fap_tm3.FAP);
               {? _fap_tm2.first()
               || _ref_new:=_fap_tm2.REF_NEW
               ?};
               DK.prefix(); FAP.prefix();
               {? FAP.seek(_ref_new,FAP.name())
               || _refik:=$FAP.ref; _kp:='__'; _ue:=FAP.FAKS().T().UE='T';
                  DK.cntx_psh();
                  DK.use(form(8+_fap_tm3.SQL));
                  DK.clear();
                  {? DK.seek(_fap_tm3.SQL,DK.name())
                  || _kp:={? _ue || DK.KP || '' ?}; DK.FAP:=_refik; DK.put();
                     FAKS.cntx_psh();
                     FAP2DK.cntx_psh();
                     FAP2DK.use('fapto'+{? FAKS.SZ='S' || 'sp' || 'dk' ?} + FAKS.ODDZ);
                     exec('adfap2dk','magdok_wspolne',$FAP.ref(),$DK.ref(),
                     $FAP.FAKS,$DK.N,DK.IL,FAP.FAKS().WHERE);
                     FAKS.cntx_pop();
                     FAP2DK.cntx_pop()
                  ?};
                  DK.cntx_pop();
                  {? _kp<>'__' || FAP.KP:=_kp; FAP.put(1) ?}
               ?};
               _fap_tm3.next()
            !}
         ?};
         end()
      ?}
   ?};
   exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)
?};
FAKS.cntx_pop();
_wyn


\spr_ceny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: sprawdza wypełnienie cen na pozycjach faktury
::   WY: 1 - są ceny
::       0 - brak choć na jednej pozycji
::  OLD: \spr_ceny/pommag.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
FAKS.cntx_psh();
FAP.cntx_psh();
FAP.index('FAP');
{? FAP.first
|| {!|?
      {? FAP.CN=0 || _wyn:=0 ?};
      FAP.next & _wyn=1
   !}
?};
FAP.cntx_pop();
FAKS.cntx_pop();
_wyn


\nulfapdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: usuwa polaczenie pozycji dokumentu sprzedazy w pozycji dokumentu magazynowego
::  OLD: \nulfapdk/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__msk')<100 || __msk:=exec('tab_mask','faktury_wspolne',3,FAP.name()+2) ?};

_refsql:=$FAP.ref();
_aktfap:=FAP.name(); _aktfak:=FAKS.name(); _aktnd:=ND.name(); _aktdk:=DK.name();
FAKS.cntx_psh();
FAP.cntx_psh();
ND.cntx_psh();
DK.cntx_psh();
__msk.clear();
{? __msk.first()
|| {!
   |?
      FAKS.use((_aktfak-2)+__msk.ROK); FAP.use((_aktfap-2)+__msk.ROK);
      ND.use((_aktnd-2)+__msk.ROK); DK.use((_aktdk-2)+__msk.ROK);
      DK.index('FAP');
      DK.prefix(_refsql,_refsql);
      {? DK.first()
      ||
         {!
         |?
            DK.cntx_psh();
            DK.clear();
            DK.FAP:='';
            DK.put();
            exec('delfap2dk','magdok_wspolne',$DK.ref());
            DK.cntx_pop();
            DK.first()
         !}
      ?};
      __msk.next()
   !}
?};
FAKS.use(_aktfak); FAP.use(_aktfap); ND.use(_aktnd); DK.use(_aktdk);

ND.cntx_pop();
DK.cntx_pop();
FAKS.cntx_pop();
FAP.cntx_pop();
''


\f3_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: akcja f3 na ilosci (zmienna do redakcji ilosci na pozycji dokumentu sprzedazy), pole DISP.ILF
::   WE: [_a] - wymagana ilosc do rozpisania
::       [_b] - ilosc do rozpisania (bez okienka)
::       [_c] - 0-wołane poza F3, 1-wołane na F3, -1-opakowania
::       [_d] - 1 - automatyczny wybór towaru, 0 (domyślne) - okienko z wyborem ilości towarów z dostaw i magazynów
::       [_e] - wymagana ilość: 1 (domyślnie) - włączone - 0 - wyłączone
::       [_f] - string - kod kreskowy dostawy - do ograniczenia stanów magazynu tylko do wybranych dostaw
::       [_g] - magazyn lub domyślnie null()
::   WY: zwraca ilosc jaka zostala rozpisana wg magazynow
::  OLD: \f3_il/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};
_c:={? var_press('_c')=type_of(0) || _c || 0 ?};
{? _>=4 || {? type_of(_d)=type_of(0) & _d=1 || _d:=1 || _d:=0 ?} || _d:=0 ?};
_e:={? _ >= 5 || {? type_of(_e)=type_of(0) & _e=0 || 0 || 1 ?} || 1 ?};
{? _>=6 || {? type_of(_f)<>type_of('') || _f:='' ?} || _f:='' ?};
_magfap:={? var_pres('_g')=type_of(null()) || _g || FAP.FAKS().MAG ?};
_ctrlil:={? _c=-2 || 1 || 0 ?};
_wgewi:=exec('czyJS','jm',FAP.M,FAP.JM);

_wyn:=obj_new(2);
{! _i..2 |! _wyn[_i]:=0 !};
_fld:={? _a=0 & _c<>-1 || fld() || 0 ?};
{? FAP.FAKS().WHERE='G' & FAP.M<>null & FAP.M().RODZ='T'
||
   BEER.ZK_N:=null;
   BEER.WG:='';
   BEER.TYP:='Z';
   DPOZ.DT:=date();

   {? BEER.DOL=1
   ||
      ZK_P.win_edit('REDZ');
      ZK_P.win_sel('ZAMZ')
   ?};

   VAR_DEL.delete('__wyb_mag','__il_wym','__wybbtn','__wyb_mag_wym_il');
   __il_wym:=_a;
   __wyb_mag_wym_il:=_e;
   __wyb_mag:=tab_tmp(5,'TREE','TREE_REF','galazka',
               'LAB','STRING[120]','Etykieta',
               'LP','INTEGER','Lp',
               'MAG','STRING[10]','Magazyn',
               'DOST','STRING[10]','Data dostawy',
               'TYP','STRING[20]','Typ magazynu',
               'STAN','REAL','Stan dostępny',
               'WYD','REAL','Do wydania',
               'REZ','INTEGER','Rez',
               'REF','STRING[16]','ref',
               'CENA','STRING[15]','Cena dostawy',
               'RSQL','STRING[20]','ref sql dostawy',
               'SUMA','REAL','Suma ilosci',
               'ILP','REAL','Ilosc zarezerwowana-na wejsciu',
               'ICON','INTEGER','Ikonka',
               'WY2','REAL','ilosc w jednostce dodatkowej',
               'IL2','REAL','ilosc dodatkowa na wejsciu');
   __wyb_mag.fld_fml('STAN','DISPLAY_FORMAT',"'out_prec='+$FAP.M().DOKL");
   exec('war_hid','ceny',__wyb_mag,'CENA');

   _win_sel:=__wyb_mag.mk_sel('Ilości do wydania wg dostępnych magazynów: '@+FAP.M().KTM+' - '+FAP.M().N,'P'
      ,,'jsysjdyeeaeokax',,,__wyb_mag.size(),1);

   __wyb_mag.win_fld(_win_sel,,'LAB',,,50,,1,'');
   __wyb_mag.win_fld(_win_sel,,'STAN',,,15,3,1,'Stan dostępny [%1]'@[FAP.M().J().KOD]);
   {? (1+BEER.FMJM)='T' & FAP.WS2>0 & FAP.J2<>null & FAP.J2<>FAP.JM
   || __wyb_mag.win_fld(_win_sel,FZ,'GIL2',,,15,3,,'Do wydania [%1]'@[FAP.JM().KOD])
   ?};
   __wyb_mag.win_fld(_win_sel,FZ,'GIL',,,15,3,,'Do wydania [%1]'@[FAP.J2().KOD]);
   __wyb_mag.win_act(_win_sel,0,'Popraw',,,,"exec('spr_edil','faktury_poz')",,1);
   {? FAP.M().M_ATR<>null
   || _formula:="exec('matr_gap','mat_atr')";
      __wyb_mag.win_act(_win_sel,0,'Formuła','Atr&ybuty'@@,,,_formula,,,,,,'Y');
      __wybbtn:=__wyb_mag.win_btn(_win_sel,'text=%1'['Atr&ybuty'@@],'menu:Y',,,,,,'noempty')
   || __wybbtn:=''
   ?};
   _formula:="exec('akc_il','faktury_poz',__il_wym, 0, __wyb_mag_wym_il);''";
   __wyb_mag.win_act(_win_sel,0,'Formuła','&Akceptuj'@@,,,_formula,,,,,,'A');
   _fb:="
      _grayed:={? exec('spr_edil','faktury_poz') || '' || 'P' ?};
      __wyb_mag.actions_grayed(cur_win(1,1),_grayed);
      exec('wyb_il','faktury_poz')";
   __wyb_mag.win_act(_win_sel,0,'Rekord',,,,_fb,"exec('spr_il','faktury_poz')",0);
   __wyb_mag.tr_fml(_win_sel,,"{? _a || {? __wyb_mag.TREE=0 | (1+__wyb_mag.TYP)='D' || 1 || _a ?} || _a ?}");
   __wyb_mag.win_fml(_win_sel,,'LAB',,'ICON_BEFORE',"
         {? __wyb_mag.TYP='RTYMC'
         || {? __wyb_mag.STAN=0 || 'xwin16.png:11' || 'xwin16.png:4' ?}
         |? __wyb_mag.ICON=37
         || {? __wyb_mag.WYD<>0 || 'xwin16.png:38' || 'xwin16.png:42' ?}
         |? __wyb_mag.ICON>0
         || 'xwin16.png:'+$__wyb_mag.ICON
         |? __wyb_mag.ICON=-1
         || 'xwin16.png:7'+{? __wyb_mag.tr_state()=1 || '5' || '4' ?}
         || ''
         ?}
      ");
   __wyb_mag.win_btn(_win_sel,'text='+'&Akceptuj'@+',btn_label_align=center,panel=bottom,align=end','menu:A');
   __wyb_mag.win_btn(_win_sel,'text='+'A&nuluj'@+',btn_label_align=center,panel=bottom,align=end','key:Esc');
   __wyb_mag.win_sel(_win_sel);

   __wyb_mag.TREE:=0;
   __wyb_mag.LAB:='Stany wg magazynów'
     +{? FAP.M().J2<>null() & FAP.WS2>0 & ~exec('czyJS','jm',FAP.M,FAP.JM)
      || '  (1 '+FAP.JM().KOD+' = '+form(1/FAP.WS2,,8)+' '+FAP.J2().KOD+')'
      |? FAP.WS2>0 & FAP.J2<>null & FAP.J2<>FAP.JM
      || '  (1 '+FAP.JM().KOD+' = '+form(FAP.WS2,,8)+' '+FAP.J2().KOD+')'
      || ''
      ?};
   __wyb_mag.ICON:=-1;
   _korzen:=__wyb_mag.add();
   _sprzsc:=exec('get','#params',300700,2)='T';
   {? FAP.FAKS().MAG<>null | _magfap | _sprzsc
   || {? _magfap<>null()
      || _magi:=_magfap;
         _mags:=exec('FindAndGet','#table',MG,_magfap,,"SYM",'');
         _magt:=exec('FindAndGet','#table',MG,_magfap,,"TYP",'')
      |? FAP.FAKS().MAG<>null()
      || _magi:=FAP.FAKS().MAG;
         _mags:=FAP.FAKS().MAG().SYM;
         _magt:=FAP.FAKS().MAG().TYP
      |? _sprzsc
      || _magi:=ST.MAG;
         _mags:=ST.MAG().SYM;
         _magt:=ST.MAG().TYP
      || _magi:=FAP.FAKS().MAG;
         _mags:=FAP.FAKS().MAG().SYM;
         _magt:=FAP.FAKS().MAG().TYP
      ?};
::    jezeli w naglowku wprowadzono magazyn
      _i:=0;
      _time:=exec('spr_dttm','magazyn_inw',FAKS.MAG,FAKS.D,1);
      {? (*_time)>(*time(23,59,0)) || _time:=time(0,0,0) ?};
        _prz:=exec('sprprzec','magazyn_przec',_magi,FAP.FAKS().D,FAP.M,_time);
        {? _prz<3
        || _ilw:=exec('oblwgfap','faktury_poz',FAP.ref,_magi);
           _ilw1:=_ilw[1]; _ilw2:=_ilw[2]; obj_del(_ilw);
         exec('obl_stan','magazyn_stan',FAP.M,1,_magi,,,FAP.FAKS().D);
         _stan:=BEER.SD;
         _oki:=(_stan+_ilw1)>0;
         {? ~_oki & _ctrlil || _ctrlil:=-1 ?};
         {? _oki
           & exec('czy_z_ok','okresy',1,0,FAP.FAKS().DW~1,FAP.FAKS().DW~2,_magi)
         || _i+=1;
            __wyb_mag.blank();
            __wyb_mag.TREE:=_korzen;
            __wyb_mag.ICON:=76;
            __wyb_mag.LP:=_i;
            __wyb_mag.MAG:=_mags;
            __wyb_mag.REF:=$_magi;
            __wyb_mag.TYP:=_magt;
            {? __wyb_mag.TYP*'DOST' & FAP.FAKS().TYPYDOK<>null & ('.LF'*FAP.FAKS().TYPYDOK().AFIFO)>1
            || __wyb_mag.TYP+=' auto'+(~FAP.FAKS().TYPYDOK().AFIFO)+'ifo'
            ?};
            __wyb_mag.LAB:=__wyb_mag.MAG+' ('+__wyb_mag.TYP+') ';
            __wyb_mag.WYD:=_ilw1;
            __wyb_mag.WY2:=_ilw2;
            __wyb_mag.ILP:=__wyb_mag.WYD;
            __wyb_mag.IL2:=__wyb_mag.WY2;
            __wyb_mag.add();
            _galaz:=__wyb_mag.ref;
            {? __wyb_mag.TYP*'DOST' & _c<>-2
            ||
               exec('sc_tymczas','magazyn_stan',_magi,FAP.M,'T',,,0,,,,_f);
               {? __sc.first
               || {!
                  |?
                     _stats:=1;
                     STATS.cntx_psh;
                     STATS.prefix;
                     {? __sc.STATS<>'' & STATS.seek(__sc.STATS)
                     ||
                        _stats:=STATS.KIND='Z'
                     ?};
                     STATS.cntx_pop;
                     {? __sc.D<=FAP.FAKS().D & _stats
                     || _i+=1;
                          _ilw:=exec('oblwgfap','faktury_poz',FAP.ref,_magi,__sc.SRDK);
                          _ilw1:=_ilw[1]; _ilw2:=_ilw[2]; obj_del(_ilw);
                          __wyb_mag.blank();
                           __wyb_mag.TREE:=_galaz;
                           __wyb_mag.ICON:=37;
                           {? __sc.DK_C<>''
                           || DK_C.cntx_psh();
                              DK_C.use(form(8+__sc.DK_C));
                              DK_C.clear();
                              _cecha:={? DK_C.seek(__sc.DK_C,form(8+__sc.DK_C)) || DK_C.SYM || '' ?};
                              DK_C.cntx_pop()
                           || _cecha:=''
                           ?};
                           __wyb_mag.LAB:=$__sc.D+{? exec('upr_cm','ceny')
                                                  || '   cena:   '+form(__sc.C,,FAP.M().DOKL_C)
                                                  || ''
                                                  ?}+
                            {? form(_cecha)<>'' || '   cecha:   '+form(_cecha) || '' ?};
                        __wyb_mag.LP:=_i;
                        __wyb_mag.REF:=$_magi;
                        __wyb_mag.TYP:=$__sc.D;
                        __wyb_mag.DOST:=$__sc.D;
                        __wyb_mag.RSQL:=__sc.REF;
                        __wyb_mag.WYD:=_ilw1;
                        __wyb_mag.WY2:=_ilw2;
                        __wyb_mag.ILP:=__wyb_mag.WYD;
                        __wyb_mag.IL2:=__wyb_mag.WY2;
                        __wyb_mag.CENA:=form(__sc.C,15,2);
                        __wyb_mag.add()
                     ?};
                        __sc.next
                    !}
               ?}
            ?}
         ?}
      ?}
   ||
::    magazyny wg uprawnien uzytkownika
      BUFMG.index('US');
      BUFMG.prefix(OPERATOR.USER,7);
      {? BUFMG.first()
      || _i:=0;
         {!
         |? _time:=exec('spr_dttm','magazyn_inw',BUFMG.MG,FAKS.D,1);
            {? (*_time)>(*time(23,59,0)) || _time:=time(0,0,0) ?};
            {? BUFMG.MG().ODDZ=ST.ODDZ_KOD & exec('sprprzec','magazyn_przec',BUFMG.MG,FAP.FAKS().D,FAP.M,_time)<3
             & {? BUFMG.MG().COS='T'
               || {? ~FAP.FAKS().KH().COS || 0
                  |? BUFMG.MG().DLAKH<>null() & BUFMG.MG().DLAKH<>FAP.FAKS().KH || 0
                  || 1
                  ?}
               || 1
               ?}
            || _rez_tym:=null;
               _ilw:=exec('oblwgfap','faktury_poz',FAP.ref,BUFMG.MG);
               _ilw1:=_ilw[1]; _ilw2:=_ilw[2]; obj_del(_ilw);
               exec('obl_stan','magazyn_stan',FAP.M,1,BUFMG.MG,,,FAP.FAKS().D);
               {? (BEER.SD+_ilw1)>0
                & exec('czy_z_ok','okresy',1,0,FAP.FAKS().DW~1,FAP.FAKS().DW~2,BUFMG.MG)
               || _i+=1;
                  __wyb_mag.blank();
                  __wyb_mag.TREE:=_korzen;
                  __wyb_mag.ICON:=76;
                  __wyb_mag.LP:=_i;
                  __wyb_mag.MAG:=BUFMG.MG().SYM;
                  __wyb_mag.REF:=$BUFMG.MG().ref;
                  __wyb_mag.TYP:=BUFMG.MG().TYP;
                  {? __wyb_mag.TYP*'DOST' & FAP.FAKS().TYPYDOK<>null & ('.FL'*FAP.FAKS().TYPYDOK().AFIFO)>1
                  || __wyb_mag.TYP+=' auto'+(~FAP.FAKS().TYPYDOK().AFIFO)+'ifo'
                  ?};
                  __wyb_mag.LAB:=BUFMG.MG().SYM+' ('+__wyb_mag.TYP+') ';
                  __wyb_mag.WYD:=_ilw1;
                  __wyb_mag.WY2:=_ilw2;
                  __wyb_mag.ILP:=__wyb_mag.WYD;
                  __wyb_mag.IL2:=__wyb_mag.WY2;
                  __wyb_mag.add();
                  _galaz:=__wyb_mag.ref;
                  {? __wyb_mag.TYP*'DOST'
                  ||
                     exec('sc_tymczas','magazyn_stan',BUFMG.MG,FAP.M,'T',,,0,,,,_f);
                     {? __sc.first
                     || {!
                          |?
                           _stats:=1;
                           STATS.cntx_psh;
                           STATS.prefix;
                           {? __sc.STATS<>'' & STATS.seek(__sc.STATS)
                           || _stats:=STATS.KIND='Z'
                           ?};
                           STATS.cntx_pop;
                             {? __sc.D<=FAP.FAKS().D & _stats
                             || _i+=1;
                                _ilw:=exec('oblwgfap','faktury_poz',FAP.ref,BUFMG.MG,__sc.SRDK);
                                _ilw1:=_ilw[1]; _ilw2:=_ilw[2]; obj_del(_ilw);
                                 __wyb_mag.blank();
                                 __wyb_mag.TREE:=_galaz;
                                 __wyb_mag.ICON:=37;
                                 {? __sc.DK_C<>''
                                 || DK_C.cntx_psh();
                                    DK_C.use(form(8+__sc.DK_C));
                                    DK_C.clear();
                                    _cecha:={? DK_C.seek(__sc.DK_C,form(8+__sc.DK_C)) || DK_C.SYM || '' ?};
                                    DK_C.cntx_pop()
                                 || _cecha:=''
                                 ?};
                                 __wyb_mag.LAB:=$__sc.D+
                                  {? exec('upr_cm','ceny') || '   cena:   '+form(__sc.C,,2) || '' ?}+
                                  {? form(_cecha)<>'' || '   cecha:   '+form(_cecha) || '' ?};
                              __wyb_mag.LP:=_i;
                              __wyb_mag.REF:=$BUFMG.MG().ref;
                              __wyb_mag.TYP:=$__sc.D;
                              __wyb_mag.DOST:=$__sc.D;
                              __wyb_mag.RSQL:=__sc.REF;
                              __wyb_mag.WYD:=_ilw1;
                              __wyb_mag.WY2:=_ilw2;
                              __wyb_mag.ILP:=__wyb_mag.WYD;
                              __wyb_mag.IL2:=__wyb_mag.WY2;
                              __wyb_mag.CENA:=form(__sc.C,15,2);
                              __wyb_mag.add()
                           ?};
                              __sc.next
                          !}
                     ?}
                  ?}
               ?}
            ?};
            BUFMG.next()
         !}
      ?}
   ?};
   __wyb_mag.clear();
   {? __wyb_mag.last() || {! |? exec('wyb_il','faktury_poz'); __wyb_mag.prev() !} ?};
   {? _b>0 & __wyb_mag.last()
   || FZ.GIL:=_b;
      exec('po_gil','faktury_poz');
      {? _ctrlil<>-1 & ~exec('spr_il','faktury_poz') || _b:=0 ?}
   ?};


::   ---------------- Automatyczny wybór towarów
{? _d=1
||
_wyb_mag_suma_stan:=0;
_wyb_mag_suma_stan_i_wyd:=0;
_wyb_mag_suma_wyd:=0;
_wyb_mag_suma_nowy:=_a;
_wyb_mag_suma_wyd_auto:=0;

::Sprawdza czy można edytować dany rekord:
::exec('spr_edil','faktury_poz')
::Po zmianie stanu:
::params_exec('po_gil','faktury_poz')

   {? __wyb_mag.first()
   ||
      {!
      |?
         {? exec('spr_edil','faktury_poz')
         ||
            _wyb_mag_suma_stan:=_wyb_mag_suma_stan + __wyb_mag.STAN;
            _wyb_mag_suma_wyd:=_wyb_mag_suma_wyd + __wyb_mag.WYD
         ?};
         __wyb_mag.next()
      !}
   ?};

   _wyb_mag_suma_stan_i_wyd:=_wyb_mag_suma_stan + _wyb_mag_suma_wyd;
   {? _wyb_mag_suma_stan_i_wyd < _wyb_mag_suma_nowy | _wyb_mag_suma_stan_i_wyd=0
   ||
      FUN.info('Brak materiału na stanie.'@);
::      FUN.info('Nie ma wystarczającej ilości na stanie, transakcja: %1, na stanie: %2'@
::      [$_wyb_mag_suma_nowy, $_wyb_mag_suma_stan_i_wyd]);
      {? _wyb_mag_suma_stan_i_wyd>0
      ||
         _d:=0;
         __il_wym:=_wyb_mag_suma_stan_i_wyd
      ?}
   ||
   {? _wyb_mag_suma_wyd<>_wyb_mag_suma_nowy | (_wyb_mag_suma_wyd=0 & _wyb_mag_suma_nowy>0)
   ||
      _dodaj_odejmij:=_wyb_mag_suma_nowy - _wyb_mag_suma_wyd;
      {? _dodaj_odejmij>0
      ||
         {? __wyb_mag.first()
         ||
            _wyb_mag_suma_wyd_auto:=_wyb_mag_suma_nowy;
            {!
            |?
               {? exec('spr_edil','faktury_poz')
               ||
                  {? _wyb_mag_suma_wyd_auto>0 & (__wyb_mag.STAN + __wyb_mag.WYD)>0
                  ||
                     {? (__wyb_mag.STAN + __wyb_mag.WYD)>_wyb_mag_suma_wyd_auto
                     ||
                        _dodaj_odejmij_stan:=_wyb_mag_suma_wyd_auto
                     ||
                        _dodaj_odejmij_stan:=__wyb_mag.STAN + __wyb_mag.WYD
                     ?};

                     _dokl_m:=exec('jaka_dok_m','jm',FAP.M);
                     _dokl_s:=exec('jaka_dok_mjm','jm',FAP.M,FAP.J2,FAP.JM);

                     _dodaj_odejmij_stan_2:={? FAP.WS2<>0 ||
                                            _dodaj_odejmij_stan/FAP.WS2 $ _dokl_s || _dodaj_odejmij_stan $ _dokl_m ?};

                     __wyb_mag.WY2:=_dodaj_odejmij_stan_2;
                     __wyb_mag.WYD:=_dodaj_odejmij_stan;

                     {? exec('spr_il','faktury_poz')
                     ||
                        __wyb_mag.put()
                     ?};

                     _wyb_mag_suma_wyd_auto:=_wyb_mag_suma_wyd_auto-_dodaj_odejmij_stan
                  ?}
               ?};
               __wyb_mag.next()
            !}
         ?}
      ||
         {? __wyb_mag.last()
         ||
            _wyb_mag_suma_wyd_auto:=_wyb_mag_suma_wyd - _wyb_mag_suma_nowy;
            {!
            |?
               {? exec('spr_edil','faktury_poz')
               ||
                  {? _wyb_mag_suma_wyd_auto>0 & __wyb_mag.WYD>0
                  ||
                     {? __wyb_mag.WYD>_wyb_mag_suma_wyd_auto
                     ||
                        _dodaj_odejmij_stan:=__wyb_mag.WYD-_wyb_mag_suma_wyd_auto;
                        _dodaj_odejmij_stan_ile:=0
                     ||
                        _dodaj_odejmij_stan:=0;
                        _dodaj_odejmij_stan_ile:=_wyb_mag_suma_wyd_auto - __wyb_mag.WYD
                     ?};
                     _dokl_m:=exec('jaka_dok_m','jm',FAP.M);
                     _dokl_s:=exec('jaka_dok_mjm','jm',FAP.M,FAP.J2,FAP.JM);

                     _dodaj_odejmij_stan_2:={? FAP.WS2<>0 ||
                                            _dodaj_odejmij_stan/FAP.WS2 $ _dokl_s || _dodaj_odejmij_stan $ _dokl_m ?};

                     __wyb_mag.WY2:=_dodaj_odejmij_stan_2;
                     __wyb_mag.WYD:=_dodaj_odejmij_stan;

                     {? exec('spr_il','faktury_poz')
                     ||
                        __wyb_mag.put()
                     ?};

                     _wyb_mag_suma_wyd_auto:=_dodaj_odejmij_stan_ile
                  ?}
               ?};
               __wyb_mag.prev()
            !}
         ?}

      ?}
   ?};
   {? __wyb_mag.first
   || {!
      |? _wyn[1]+={? exec('spr_edil','faktury_poz') || __wyb_mag.WYD || 0 ?};
         _wyn[2]+={? exec('spr_edil','faktury_poz') || {? _c || __wyb_mag.WY2 || __wyb_mag.IL2 ?} || 0 ?};
         __wyb_mag.next
      !}
   ?};
::   Podgląd automatycznego wyboru zamiast funkcji: exec('akc_il','faktury_poz',_wyb_mag_suma_nowy, 1)
::   __wyb_mag.select()
   exec('akc_il','faktury_poz',_wyb_mag_suma_nowy, 1, _e)
   ?}
::   ----------------
?};
{? _d=0
|| _wgewi:=exec('czyJS','jm',FAP.M,FAP.JM);
   __wyb_mag.hdr_sel(''@);
   {? _b>0 | (_c<>-2 & __wyb_mag.select())
   ||
::    aktualizujemy informacje o ilosci na pozycji dokumentu
      {? __wyb_mag.first
      || {!
         |? {? _wgewi
            || _wyn[1]+={? __wyb_mag.MAG<>'' || __wyb_mag.WYD || 0 ?};
               _wyn[2]+={? __wyb_mag.MAG<>'' || {? _c || __wyb_mag.WY2 || __wyb_mag.IL2 ?} || 0 ?}
            || _wyn[1]+={? __wyb_mag.MAG<>'' || __wyb_mag.WY2 || 0 ?};
               _wyn[2]+={? __wyb_mag.MAG<>'' || {? _c || __wyb_mag.WYD || __wyb_mag.ILP ?} || 0 ?}
            ?};
            __wyb_mag.next
         !}
      ?}
   ||
::    usuwamy rezerwacje lub aktualizujemy wg stanu wyjsciowego - nacisnieto klawisz ESC
      {? __wyb_mag.first
      || {!
         |? {? ~(__wyb_mag.TYP*'DOST') & __wyb_mag.REF<>''
            || _wyn[1]+=__wyb_mag.ILP;  _wyn[2]+=__wyb_mag.IL2;
               {? _wgewi
               || exec('akt_frez','faktury_poz',__wyb_mag.ILP,__wyb_mag.IL2)
               || exec('akt_frez','faktury_poz',__wyb_mag.IL2,__wyb_mag.ILP)
               ?}
            |? (__wyb_mag.TYP*'DOST') & (__wyb_mag.TYP*'auto') & __wyb_mag.REF<>''
            || _tr:=__wyb_mag.ref();
               _xilp:=0;
               _xil2:=0;
               __wyb_mag.cntx_psh();
               __wyb_mag.clear();
               __wyb_mag.prefix(_tr);
               {? __wyb_mag.first()
               || {!
                  |? _xilp+=__wyb_mag.ILP;
                     _xil2+=__wyb_mag.IL2;
                     __wyb_mag.next()
                  !}
               ?};
               __wyb_mag.cntx_pop();

               _wyn[1]+=(__wyb_mag.ILP-_xilp);  _wyn[2]+=(__wyb_mag.IL2-_xil2);

               {? _wgewi
               || exec('akt_frez','faktury_poz',(__wyb_mag.ILP-_xilp),(__wyb_mag.IL2-_xil2))
               || exec('akt_frez','faktury_poz',(__wyb_mag.IL2-_xil2),(__wyb_mag.ILP-_xilp))
               ?}
            ?};
            __wyb_mag.next
         !}
      ?}
   ?};

   {? _wyn[1]=0
   || _wyn[1]:=_fld
   ?}
?}
||
   _wyn[1]:=_fld
?};
echo();
{? _c>0
|| VAR_DEL.delete('__FAKSG_F3');
   __FAKSG_F3:=_wyn;
   _wyn[2]
|| _wyn
?}


\spr_edil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: formula sprawdzajaca ilosc przed edycja rekordu
::   WY: 1/0
::  OLD: \spr_edil/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
{? __wyb_mag.TYP*'DOST' & ~(__wyb_mag.TYP*'auto')
|| 0
|| _wgewi:=exec('czyJS','jm',FAP.M,FAP.JM);
   __wyb_mag.SUMA:={? _wgewi || __wyb_mag.WYD || __wyb_mag.WY2 ?}+__wyb_mag.STAN;
   __wyb_mag.put(1);
   __wyb_mag.SUMA>0 & __wyb_mag.REF<>''
?}


\wyb_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: akcja rekord przed na ilosci wg magazynow dla pozycji faktury
::  OLD: \wyb_il/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
{? __il_wym>0 || echo('Wymagana ilość do rozpisania: %1'@[form(__il_wym,,3)]) ?};
_wgewi:=exec('czyJS','jm',FAP.M,FAP.JM);
MG.cntx_psh();
MG.clear();
{? MG.seek(__wyb_mag.REF)
||
:: stan dostepny
   {? __wyb_mag.RSQL=''
   || _ilw:=exec('oblwgfap','faktury_poz',FAP.ref,MG.ref);
      _ilw1:=_ilw[1]; _ilw2:=_ilw[2]; obj_del(_ilw);
      {? -menu_txt()='akceptuj'
      || exec('obl_stan','magazyn_stan',FAP.M,1,MG.ref,1);
         {? __wyb_mag.MAG<>'' & __wyb_mag.WYD<>_ilw1
         || __wyb_mag.STAN:=BEER.SD;
            {? _wgewi
            || __wyb_mag.REZ:=__wyb_mag.WYD:=_ilw1;
               __wyb_mag.WY2:=_ilw2
            || __wyb_mag.REZ:=__wyb_mag.WY2:=_ilw1;
               __wyb_mag.WYD:=_ilw2
            ?};
            __wyb_mag.put()
         ?}
      ||
         exec('obl_stan','magazyn_stan',FAP.M,1,MG.ref,1,,FAP.FAKS().D);
         {? 1+MG.TYP='D' & ~(('.LF'*FAP.FAKS().TYPYDOK().AFIFO)>1)
         ||
            _beersd:=0;
            _tree_ref:=__wyb_mag.ref();
            __wyb_mag.cntx_psh();
            __wyb_mag.prefix(_tree_ref);
            {? __wyb_mag.first() || {! |? _beersd+=__wyb_mag.STAN; __wyb_mag.next() !} ?};
            __wyb_mag.cntx_pop();
            {? BEER.SD<_beersd || _beersd:={? BEER.SD>0 || BEER.SD || 0 ?} ?}
         ||
            _beersd:=BEER.SD
         ?};
         __wyb_mag.STAN:=_beersd;
         {? _wgewi
         || __wyb_mag.REZ:=__wyb_mag.WYD:=_ilw1;
            __wyb_mag.WY2:=_ilw2
         || __wyb_mag.REZ:=__wyb_mag.WY2:=_ilw1;
            __wyb_mag.WYD:=_ilw2
         ?};
         __wyb_mag.put()
      ?}
   ||
      {? (8+__wyb_mag.RSQL)<>'' & (2+__wyb_mag.RSQL)<>'zk'
      ||
         SC.cntx_psh();
         SC.use(8+__wyb_mag.RSQL);
         SC.clear;
         {? SC.seek(__wyb_mag.RSQL)
         || exec('sc_info','magazyn_stan');
            _ilw:=exec('oblwgfap','faktury_poz',FAP.ref,SC.MAG,SC.SRDK);
            _ilw1:=_ilw[1]; _ilw2:=_ilw[2]; obj_del(_ilw);
            __wyb_mag.CENA:={? exec('upr_cm','ceny') || form(BO.CEN,15,2) || '' ?};
            __wyb_mag.STAN:=
               {? exec('czyinwds','magdok_wspolne',MG.ref,FAP.FAKS().D,FAP.M,,1)
               || 0
               || DISP.SD
               ?};
            {? _wgewi
            || __wyb_mag.REZ:=__wyb_mag.WYD:=_ilw1;
               __wyb_mag.WY2:=_ilw2
            || __wyb_mag.REZ:=__wyb_mag.WY2:=_ilw1;
               __wyb_mag.WYD:=_ilw2
            ?};
            __wyb_mag.put()
         ?};
         SC.cntx_pop()
      ?}
   ?}
?};

MG.cntx_pop();
exec('aktstwyd','faktury_poz');
{? __wybbtn<>''
|| {? __wyb_mag.RSQL<>'' & exec('FindAndGet','#table',SC,__wyb_mag.RSQL,,"DK_C",null())<>null
   || __wyb_mag.btn_sopt(__wyb_mag.win_sel('?'),__wybbtn,'state=normal')
   || __wyb_mag.btn_sopt(__wyb_mag.win_sel('?'),__wybbtn,'state=grayed')
   ?}
?};
{? __wyb_mag.REF='' || 1 || '' ?}


\aktstwyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: informacja o ilosci dostepnej i wydanej
::  OLD: \aktstwyd/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
__wyb_mag.cntx_psh;
_stan:=0; _wydanie:=0; _wydani2:=0;
{? __wyb_mag.last
|| {!
   |? {? __wyb_mag.MAG<>'' | __wyb_mag.TYP='RTYMC'
      || _stan+=__wyb_mag.STAN;
         _wydanie+=__wyb_mag.WYD;
         _wydani2+=__wyb_mag.WY2
      ?};
      __wyb_mag.prev
   !};
   __wyb_mag.STAN:=_stan;
   __wyb_mag.WYD:=_wydanie;
   __wyb_mag.WY2:=_wydani2;
   __wyb_mag.put(1)
?};
__wyb_mag.cntx_pop;
1


\spr_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: formula sprawdzajaca ilosc po edycja rekordu
::  OLD: \spr_il/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_suma:=__wyb_mag.SUMA;
_wgewi:=exec('czyJS','jm',FAP.M,FAP.JM);
{? __wyb_mag.RSQL=''
|| _mag:=exec('FindAndGet','#table',MG,__wyb_mag.REF,,,null());
   exec('obl_stan','magazyn_stan',FAP.M,1,_mag,0,,FAP.FAKS().D);
:: dla magazynow dostaw oblicza niezaakceptowane ilosci w wydaniu
   {? var_pres('__wpd')>0 & __wpd[1]>0 || BEER.SD-=__wpd[1] ?};
   {? var_pres('__fpd')>0 & __fpd[1]>0 || BEER.SD-=__fpd[1] ?};
   {? BEER.SD<0 || BEER.SD:=0 ?};
   _ref:=__wyb_mag.ref;
   __wyb_mag.cntx_psh();
   __wyb_mag.clear();
   _wyd:={? __wyb_mag.seek(_ref) || {? _wgewi || __wyb_mag.WYD || __wyb_mag.WY2 ?} || 0 ?};
   __wyb_mag.cntx_pop();
   __wyb_mag.STAN:=BEER.SD;
   __wyb_mag.SUMA:=_wyd+__wyb_mag.STAN;
   __wyb_mag.put()
||
   {? (8+__wyb_mag.RSQL)<>'' & (2+__wyb_mag.RSQL)<>'zk'
   ||
      SC.cntx_psh();
      SC.use(8+__wyb_mag.RSQL);
      SC.clear;
      {? SC.seek(__wyb_mag.RSQL)
      || exec('obl_stan','magazyn_stan',SC.M,1,SC.MAG,0,,FAP.FAKS().D);
         _stan:=BEER.SD;
         exec('sc_info','magazyn_stan');
         _ref:=__wyb_mag.ref;
         __wyb_mag.cntx_psh();
         __wyb_mag.clear();
         _wyd:={? __wyb_mag.seek(_ref) || {? _wgewi || __wyb_mag.WYD || __wyb_mag.WY2 ?} || 0 ?};
         _sc_rez:={? SC.DK_C<>null() || exec('rezScWgAtr','magazyn_stan',SC.MAG,SC.M,SC.RDK,SC.NDK,SC.DK_C) || 0 ?};
         __wyb_mag.cntx_pop();
         __wyb_mag.STAN:={? DISP.SD>_stan || _stan || DISP.SD ?};
         __wyb_mag.STAN:={? _sc_rez>=__wyb_mag.STAN
                         || 0
                         || __wyb_mag.STAN-_sc_rez
                         ?};
         __wyb_mag.put()
      ?};
      SC.cntx_pop()
   || _wyd:=0
   ?}
?};
win_disp();

{? __wyb_mag.TYP*'auto'
|| _tr:=__wyb_mag.ref();
   _xwyd:=0;
   __wyb_mag.cntx_psh();
   __wyb_mag.clear();
   __wyb_mag.prefix(_tr);
   {? __wyb_mag.first()
   || {!
      |? _xwyd+={? _wgewi || __wyb_mag.WYD || __wyb_mag.WY2 ?};
         __wyb_mag.next()
      !}
   ?};
   __wyb_mag.cntx_pop()
|| _xwyd:=0
?};

{? __wyb_mag.WYD<0
|| FUN.info('Ilość w wydaniu nie może być ujemna.'@);
   __wyb_mag.WYD:=_wyd;
   __wyb_mag.SUMA:=_suma;
   __wyb_mag.put();
   0
|? {? _wgewi || (__wyb_mag.WYD+_xwyd)>__wyb_mag.SUMA || (__wyb_mag.WY2+_xwyd)>__wyb_mag.SUMA ?}
|| FUN.info('Zbyt duża ilość w wydaniu.'@);
   FZ.GIL:=__wyb_mag.WYD:=_wyd;
   __wyb_mag.SUMA:=_suma;
   __wyb_mag.put();
   0
||
:: Dokonuje nowej rezerwacji wg pozycji faktury
   exec('akt_frez','faktury_poz',__wyb_mag.WYD,__wyb_mag.WY2);
   1
?}


\akt_frez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: formula aktualizujaca rezerwacje na konkretnej pozycji __wyb_mag
::   WE: _a - ilosc do rozpisania
::       _b - ilosc w jednostce dodatkowej
::       [_c] - FAP.ref (wtedy usuniecie wszystkich pozycji rezerwacji), domyslnie null
::  OLD: \akt_frez/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>7 || _c:=null ?} || _c:=null ?};

_wgewi:=exec('czyJS','jm',FAP.M,FAP.JM);
_ws2:={? _wgewi | (~_wgewi & FAP.M().J2<>null())
      || {? FAP.WS2<>0 || 1/FAP.WS2 || 1 ?}
      || {? FAP.WS2<>0 || FAP.WS2 || 1 ?}
      ?};
_onemag:=FAKS.WHERE='G'
 & {? FAKS.MAG<>null
   || (1+FAKS.MAG().TYP)<>'D'
   |? var_pres('__winbad')=1
   || (1+ST.MAG().TYP)<>'D'
   || 0
   ?};

_mag:=null;
_sc:='';
{? _c=null
|| {? __wyb_mag.MAG='' & __wyb_mag.RSQL<>'' & (2+__wyb_mag.RSQL)<>'zk'
   || SC.cntx_psh();
      SC.use(8+__wyb_mag.RSQL);
      SC.clear();
      {? SC.seek(__wyb_mag.RSQL)
      || _mag:=SC.MAG;
         _sc:=SC.SRDK
      ?};
      SC.cntx_pop()
   || _mag:=exec('FindAndGet','#table',MG,__wyb_mag.REF,,,null())
   ?}
?};

{? _onemag & _sc='' & _a>0
||
:: usuniecie
   _nrk:=0;
   _ilr:=0;
   _mat:=null;

   _nrk:=0;
   REZ.cntx_psh();
   REZ.index('FAPR');
   {? _c<>null
   || REZ.prefix($FAP.ref(),)
   || REZ.prefix($FAP.ref(),$FAP.ref(),_mag)
   ?};
   {? REZ.first()
   || _mat:=REZ.M;
      {!
      |? _ilr+=REZ.ILR; _nrk:=REZ.NRK; _magik:=REZ.MG;
         _res:=REZ.del(); exec('obl_stan','magazyn_stan',_mat,1,_magik);
         _res
      !}
   || {? _c<>null || _mat:=FAP.M ?}
   ?};
   {? _mat<>null || exec('aktu_rez','rezerwacje',_mat,_nrk,_ilr,,,_mag) ?};
   REZ.cntx_pop();
:: dodanie

   {? _wgewi
   || _nrk:=exec('update','rezerwacje','FAP',FAP.ref(),_mag,FAP.M,_a,_sc,'B',,3,,,_b,FAP.JM,_ws2)
   || _nrk:=exec('update','rezerwacje','FAP',FAP.ref(),_mag,FAP.M,_b,_sc,'B',,3,,,_a,FAP.J2,_ws2)
   ?};
   exec('aktu_rez','rezerwacje',FAP.M,_nrk,{? _wgewi || _a || _b ?},,,_mag)
|? _a<=0
||
:: usuniecie
   _nrk:=0;
   _ilr:=0;
   _mat:=null;

   _nrk:=0;
   REZ.cntx_psh();
   REZ.index('FAPR');
   {? _c<>null || REZ.prefix($FAP.ref(),)
   |? _sc='' || REZ.prefix($FAP.ref(),$FAP.ref(),_mag)
   || REZ.prefix($FAP.ref(),$FAP.ref(),_mag,_sc,_sc)
   ?};
   {? REZ.first()
   || _mat:=REZ.M;
      {!
      |? _ilr+=REZ.ILR; _nrk:=REZ.NRK; _magik:=REZ.MG;
         _res:=REZ.del(); exec('obl_stan','magazyn_stan',_mat,1,_magik);
         _res
      !}
   || {? _c<>null || _mat:=FAP.M ?}
   ?};
   {? _mat<>null || exec('aktu_rez','rezerwacje',_mat,_nrk,_ilr,,,REZ.MG) ?};
   REZ.cntx_pop()
||
:: dodanie
   {? _wgewi
   || _nrk:=exec('update','rezerwacje','FAP',FAP.ref(),_mag,FAP.M,_a,_sc,'B',,3,,,_b,FAP.JM,_ws2)
   || _nrk:=exec('update','rezerwacje','FAP',FAP.ref(),_mag,FAP.M,_b,_sc,'B',,3,,,_a,FAP.J2,_ws2)
   ?};
   exec('aktu_rez','rezerwacje',FAP.M,_nrk,_a,,,_mag)
?}


\akc_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: akceptacja rozpisanej ilosci wg magazynow - pozycja dokumentu sprzedazy
::   WE: _a - wymagana ilosc (gdy _a>0)
::       _b - 1 - włączony automat, 0 (domyślnie) - wyłączony automat
::       _c - wymagana ilość: 1 (domyślnie) - włączone - 0 - wyłączone
::  OLD: \akc_il/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_il:=0;

_b:={? _ >= 2 || {? type_of(_b)=type_of(0) & _b=1 || 1 || 0 ?} || 0 ?};
_c:={? _ >= 3 || {? type_of(_c)=type_of(0) & _c=0 || 0 || 1 ?} || 1 ?};

__wyb_mag.cntx_psh();
__wyb_mag.clear();
{? __wyb_mag.first()
|| {!
   |? exec('wyb_il','faktury_poz');
      _il+={? __wyb_mag.MAG<>'' || __wyb_mag.WYD || 0 ?};
      __wyb_mag.next
   !}
?};
__wyb_mag.cntx_pop();
{? _il>0
|| MG.cntx_psh();
   _mags:=sql('select distinct :_a.REF REF from :_a where :_a.WYD>0 and :_a.TREE=1 ',__wyb_mag);
   {? _mags.size()
   || _tab:=obj_new(_mags.size); _i:=0;
      {? _mags.first()
      || {!
         |? {? (MG.clear(); MG.seek(_mags.REF)) || _i+=1; _tab[_i]:=MG.ref ?};
            _mags.next()
         !}
      ?}
   || _tab:=0
   ?};
   MG.cntx_pop();
   {? _a>0 & _a<>_il & _c
   ||
      FUN.info('Rozpisna ilość: %1 %2'
         '\njest różna od\nilości wymaganej: %3 %4'
         '\n\nAkceptacja wyboru niemożliwa.'@[form(_il,,3),FAP.JM().KOD,form(_a,,3),FAP.JM().KOD])
   ||
::    dodano sprawdzenie uprawnien do typow dokumentow realizujacych na magazynie sprzedaz
      _war:='"TYPYDOK".Z=\'T\' and "TYPYDOK".P=\'N\' and "TYPYDOK".DS=\'T\' and "TYPYDOK".DK=\'N\'';
      _typ:=exec('typ_dok','lmg',_war,,,1,_tab);
      {? form(_typ)=''
      || FUN.info('Brak uprawnionych typów dokumentów magazynowych realizujących sprzedaż wg magazynów.\n'
                  'Typ dokumentu magazynowego powinien być dostępny\n'
                  'dla każdego magazynu związanego z danym dokumentem sprzedaży.\n\n'
                  'Akceptacja dokumentu nie będzie możliwa.'@)
      ?};
      {? _b=0 || sel_exit() ?}
   ?}
||
   FUN.info('Nie podano ilości do wydania.'@)
?};
1


\po_gil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji ilosci dla pozycji faktur typu G
::  OLD: \po_gil/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_dokl_m:=exec('jaka_dok_m','jm',FAP.M);
_dokl_s:=exec('jaka_dok_mjm','jm',FAP.M,FAP.J2,FAP.JM);

_jmg:=FAP.M().J2<>null();
_wgewi:=exec('czyJS','jm',FAP.M,FAP.JM);
FZ.GIL2:={? ~_jmg
         || {? FAP.WS2<>0 || FZ.GIL/FAP.WS2 $ _dokl_s || FZ.GIL $ _dokl_m ?}
         || {? FAP.WS2<>0 || FZ.GIL*FAP.WS2 $ _dokl_s || FZ.GIL $ _dokl_m ?}
         ?};

__wyb_mag.WY2:=FZ.GIL2;
__wyb_mag.WYD:=FZ.GIL;
1


\pw_gil2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed wyswietleniem ilosci dodatkowej dla pozycji faktur typu G
::  OLD: \pw_gil2/faktury1fml
::----------------------------------------------------------------------------------------------------------------------
FZ.GIL2:=__wyb_mag.WY2;
1


\po_gil2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji ilosci dodatkowej dla pozycji faktur typu G
::  OLd: \po_gil2/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
_dokl_m:=exec('jaka_dok_m','jm',FAP.M);
_jmg:=FAP.M().J2<>null();
_wgewi:=exec('czyJS','jm',FAP.M,FAP.JM);
FZ.GIL:={? ~_jmg
        || FZ.GIL2*FAP.WS2 $ _dokl_m
        || FZ.GIL2/FAP.WS2 $ _dokl_m
        ?};
__wyb_mag.WY2:=FZ.GIL2;
__wyb_mag.WYD:=FZ.GIL;
1


\pw_gil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed wyswietleniem ilosci dla pozycji faktur typu G
::  OLD: \pw_gil/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
FZ.GIL:=__wyb_mag.WYD;
1


\pr_gil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcja ilosci dla pozycji faktur typu G
::  OLD: \pr_gil/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
{? __wyb_mag.TYP*'auto'
|| _tr:=__wyb_mag.ref();
   _xwyd:=0;
   _xwy2:=0;
   __wyb_mag.cntx_psh();
   __wyb_mag.clear();
   __wyb_mag.prefix(_tr);
   {? __wyb_mag.first()
   || {!
      |? _xwyd+=__wyb_mag.WYD;
         _xwy2+=__wyb_mag.WY2;
         __wyb_mag.next()
      !}
   ?};
   __wyb_mag.cntx_pop();
   FZ.GIL-=_xwyd;
   FZ.GIL2-=_xwy2
?};
1


\fapsm_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [12.30]
:: OPIS: dodawanie pozycji według SM do wystawionych dokumentow (pozycje dokumentow sprzedazy)
::  OLD: \fapsm_add/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
BEER.MW:={? FAKS.SZ='S' || 'F' || 'K' ?};
exec('ustaw_ww','eurusd',BEER.MW); BEER.LW:={? BEER.WW || 'W' || '' ?};
{? FAKS.ZAK='T'
|| FUN.info({? FAKS.SZ='S'
            || 'Dokument sprzedaży zaksięgowany.\n'@
            || 'Dokument zakupu zaksięgowany.\n'@
            ?}+'Modyfikacja niemożliwa.'@)
|? FAKS.SZ='Z' & FAKS.AKC='T'
|| FUN.info('Dokument zakupu zaakceptowany.\n'@+
            'Modyfikacja niemożliwa.'@)
|? (FAKS.WHERE='L' & ~exec('fzal_chk','faktury_zalicz'))
|| FUN.info('Wystawiono kolejne dokumenty.\n'@+
            'Dodanie pozycji niemożliwe.'@)
|| FAP.blank();
   FAP.memo_set();
   exec('f3_sm','faktury_poz')
?}


\f3_sm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [12.30]
:: OPIS: klawisz F3 dla pozycji dokumentow
::  OLD: \f3_sm/faktury_poz
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__win_sm','__win_sc','__winbad','__faks_r');

_stmag:=ST.MAG;

__faks_r:=FAKS.ref();
_wyn:='';
{? FAP.size() || _oldmfap:=D_HELP.M_FAP:=FAP.M().KTM || _oldmfap:=D_HELP.M_FAP:='' ?};

JM.f_clear;
JM.win_dict('WER');
{? FAKS.STS().MGR<>'T' || POMOC.MGR:=POMOC.MGRF:=null ?};

ZAKR.MATU:='A';
{? POMOC.RODZ='' || POMOC.RODZ:='T' ?};
{? POMOC.MGRF().RODZ<>POMOC.RODZ || POMOC.MGR:=POMOC.MGRF:=null ?};

BEER.KH:=FAKS.KH;

{? FAKS.MAG<>null()
|| _mag:=FAKS.MAG;
   _magsym:=FAKS.MAG().SYM+' - '+FAKS.MAG().NAZ;
   ST.MAG:=_mag
|| _mag:=ST.MAG;
   _magsym:=ST.MAG().SYM+' - '+ST.MAG().NAZ
?};

FAKS.cntx_psh();

_refmat:=null;
SM.index('ASA');
SM.prefix(_mag,'T');
{? D_HELP.M_FAP=''
|| {? SM.first()
   || _refmat:=SM.M;
      D_HELP.M_FAP:=SM.M().KTM
   ?}
|| {? ~SM.find_key(D_HELP.M_FAP,)
   || {? SM.first()
      || _refmat:=SM.M;
         D_HELP.M_FAP:=SM.M().KTM
      ?}
   || _refmat:=SM.M;
      D_HELP.M_FAP:=SM.M().KTM
   ?}
?};

{? D_HELP.M_FAP<>''
|| exec('sc_tymczas','magazyn_stan',_mag,_refmat,'T',,,2);
   __win_sc:=__sc_sel;
   __sc.actions(__sc_sel,'W');

   SM.f_clear();
   SM.win_sel('WER');
   SM.index('SA');
   SM.prefix();
   SM.f_set('M(N)','JOIN MG using (SM.MAG,MG.REFERENCE) JOIN M using (SM.M,M.REFERENCE)'
    ,'MG.REFERENCE=:_a and M.A=\'T\' and SM.SD>0',_mag);

   __winbad:=exec('get','#params',540101,2,null())='T';

   __win_sm:=SM.grp_make('Dostępne stany magazynowe w magazynie '@+_magsym,,
     {? __winbad || '#stan_fkmag' || '#stan0fkmag' ?});
   SM.grp_sel(__win_sm,SM,'WER_SS',,"exec('odsw_sm','faktury_poz')",0,,4,,,,,'maximized_with_title');
   SM.grp_splt(__win_sm,,'horizontal','tab1');
   SM.grp_sel(__win_sm,__sc,__win_sc,,"exec('odsw_sc','magazyn_stan',2)",0,,{? __winbad || 22 || 14 ?});
   SM.grp_splt(__win_sm,'tab1','horizontal','tab2');
   SM.grp_sel(__win_sm,FAP,'WER_MAG',,,0,,5);
:: DRO:[rr] ograniczenie do 3 paneli
::   SM.grp_splt(__win_sm,'tab1','vertical','tab3');
::   SM.grp_edit(__win_sm,DK_C,'DISPR',,"",,,,'maximized');
   __winbad:=0;
   {? __winbad
   || SM.grp_splt(__win_sm,'tab3','horizontal','tab4');
      SM.grp_sel(__win_sm,BADP,'WER_SS',,"",,,1,,,,,'maximized')
   ?};

   SM.win_sel(__win_sm);

   {? SM.select(,1,10)
   || D_HELP.M_FAP:=SM.M().KTM;
      _wyn:=SM.M().KTM;
      FAP.M:=SM.M;
      POMOC.MGR:=POMOC.MGRF:={? FAP.FAKS().STS=null | FAP.FAKS().STS().MGR<>'T' || null || FAP.M().MGR ?}
   || {? _oldmfap<>'' & (_refm:=exec('FindInSet','#table','M','MATKTM',_oldmfap,_oldmfap))<>null
      || D_HELP.M_FAP:=_oldmfap;
         _wyn:=_oldmfap;
         FAP.M:=_refm
      ?}
   ?};
   __sc.actions(__sc_sel,'');
   SM.win_sel();
   SM.win_del(__win_sm);
   SM.f_clear();
   BEER.KH:=null
|| FUN.info('W magazynie %1\nbrak dostępnych stanów materiałowych.'@[_magsym])
?};
FAKS.cntx_pop();
ST.MAG:=_stmag;
VAR_DEL.delete('__win_sm','__win_sc','__winbad','__faks_r');

_wyn


\odsw_sm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JSz [12.30]
:: OPIS: Odświeżaenie okien w oknie grupowym dla okna tabeli SM
::  OLD: \odsw_sm/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
exec('sc_tymczas','magazyn_stan',SM.MAG,SM.M,'T',,,0);
__sc.clear();
__sc.first();
grp_disp(__sc,__win_sc);

exec('odsw_sc','magazyn_stan',2);

FAP.index('FAP');
FAP.prefix(__faks_r);
{? FAP.last() || {! _i:=1..3 |! FAP.prev() !} ?};
grp_disp(FAP,'WER_MAG');
~~


\fap_win_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zwraca okno slekcji tabeli FAP
::   WE: _a - 0-akronim okna bazowego, 1-akronim okna docelowego
::   WY: akronim okna selekcji
::----------------------------------------------------------------------------------------------------------------------
_dst:=_a;
FAP.actions('WER','','D:D');
FAP.actions('WER_I','','D:D');
_win:={? FAKS.SZ='Z' & FAKS.KOR='' & FAKS.WHERE<>'L'
:: dokument zakupu
      ||
::         FAKSV.win_edit('RED');
         {? _dst
         || exec('poz_grp','faktury_poz')
         || {? BPMN.DISPLAY || 'WER_IDOK' || 'WER_I' ?}
         ?}
      ||
::   FAP.win_sel({? (1+_sel)<>'G' || BEER.LW || '' ?}+_sel);
:: [rr] jedno okienko do selekcji
         {? FAKS.KOR=''
:: dokument
         || {? FAKS.MAG & FAKS.MAG().MG_OPAK=''
               | (exec('wydaFaks','faktury_wspolne') & FAKS.WHERE<>'G')
:: dokument sprzedaży, zakupu zaliczkowy
            || {? FAKS.SZ='S'
               || {? BPMN.DISPLAY || 'WER_DOK' || 'WER' ?}
               || {? BPMN.DISPLAY || 'WER_IDOK' || 'WER_I' ?}
               ?}
:: dokument sprzedaży generujący bez powiązanych dokumentów magazynowych
            || _wer:={? FAKS.SZ='S'
                     || {? BPMN.DISPLAY || 'WER_DOK' || 'WER' ?}
                     || {? BPMN.DISPLAY || 'WER_IDOK' || 'WER_I' ?}
                     ?};
               {? _dst=0
               ||
                  _wer
               ||
                  _ident:={? FAKS.WHERE='G' || 'faktury_gene' || 'faktury' ?};
                  _win_grp:=FAP.grp_make('Pozycje dokumentu sprzedaży'@,,_ident);
                  {? FAKS.WHERE='G' & FAKS.STAT_REJ<>'T'
                  || _far:="exec('fap_rez','faktury_poz')";
                     FAP.grp_sel(_win_grp,FAP,_wer,'Pozycje faktury'@,_far,,,15,,,,,'maximized');
                     FAP.tab_splt(_win_grp,'tab0','horizontal','dostawy');
                     FAP.grp_sel(_win_grp,REZ,'FAP',,,,,5,,,,,'maximized_with_title')
                  || FAP.grp_sel(_win_grp,FAP,_wer,'Pozycje faktury'@,,,,15,,,,,'maximized')
                  ?};
                  {? FAKS.WHERE<>'L'
                  || FAP.grp_sel(_win_grp,OPAK_P,'WER','Opakowania'@,,,,30
                      ,"exec('opak_wer','opakow',FAKS.ref(),1)","exec('opak_out','opakow')",,,'maximized')
                  ?};
                  _win_grp
               ?}
            ?}
:: korekta
         || {? FAKS.T().HIS='T'
:: historyczna
            || 'WERKH'
:: zwykła
            || {? FAKS.SZ='S'
               || {? BPMN.DISPLAY || 'WERK_DOK' || 'WERK' ?}
               || {? BPMN.DISPLAY || 'WERKZDOK' || 'WERKZ' ?}
               ?}
            ?}
         ?}
      ?};
exec('enableFLD','faktury_poz',_win);
_win


\fap_actions
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Akcje okien wertowania
::   WE: [_a] - akronim okienka
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wer:={? var_press('_a')=type_of('') || _a || exec('fap_win_sel','faktury_poz',0) ?};

_wsk:=BEER.FAP_ACTS*':';
_act1:={? _wsk || _wsk-BEER.FAP_ACTS || '' ?};
_act0:={? _wsk || _wsk-1+BEER.FAP_ACTS || BEER.FAP_ACTS ?};

:: włączenie/wyłączenie akcji i ustawienie domyślnej
_actab0:='Z';
{? FAKS.STAT_REJ='N'
|| {? exec('upgrade2325_blbc1','zbl')
   || FAP.actions('TWER_I',_actab0+_act0+':'+_act1,'D:D',1)
   ?};
   FAP.actions('TWER',_actab0+_act0+':'+_act1,'D:D',1);
   FAP.actions('TWERKH',_actab0+_act0+':'+_act1,'D:D',1);
   FAP.actions('TWERK',_actab0+_act0+':'+_act1,'P',1);
   FAP.actions('WER_I',_act0+':'+_act1,'D:D',1);
   FAP.actions('WER',_act0+':'+_act1,'D:D',1);
   FAP.actions('WERKH',_act0+':'+_act1,'D:D',1);
   FAP.actions('WERK',_act0+':'+_act1,'P',1);
   FAP.actions('WERKZ',_act0+':'+_act1,'P',1)
|| {? exec('upgrade2325_blbc1','zbl')
   || FAP.actions('TWER_I',_actab0+_act0+':'+_act1,'s',1)
   ?};
   FAP.actions('TWER',_actab0+_act0+':'+_act1,'s',1);
   FAP.actions('TWERKH',_actab0+_act0+':'+_act1,'s',1);
   FAP.actions('TWERK',_actab0+_act0+'W:'+_act1,'s',1);
   FAP.actions('WER_I',_act0+':'+_act1,'s',1);
   FAP.actions('WER',_act0+':'+_act1,'s',1);
   FAP.actions('WERKH',_act0+':'+_act1,'s',1);
   FAP.actions('WERK',_act0+'W:'+_act1,'s',1);
   FAP.actions('WERKZ',_act0+':'+_act1,'s',1)
?};

:: wygaszenie akcji
_dopar:=FAKS.T().KOR='N' & FAKS.SYMF<>'' & exec('FindAndGet','#table',FAKS,FAKS.SYMF,,"T().PAR",'N')='T';
_nomod:={? FAKS.T().KOR='T' & FAKS.WGZWR || 'DUW(BW)' || '' ?};
_act:=
   {? DISP.DISP | FAKS.STAT_REJ='A'
   || exec('allActions','#string','wsSprL')
   |? FAKS.STAT_REJ='T'
   || {? (';MG'*FAKS.WHERE)>1 || '' || 'H' ?}
      +{? FAKS.T().PROJZAKR=exec('projzakr_nie_dotyczy','projekty')  || 'N(J)' || ''?}
      +{? FAKS.IST_TYP='' || 'N(E)' || '' ?}
      +{? FAKS.INTRA='T' || 'M' || '' ?}
      +'TPDUZGAN(CYRK)OW(BW):O'
   |? FAKS.STAT_REJ='Z' | _dopar
   || {? FAKS.WHERE='M' || '' || 'H' ?}
      +{? _dopar || '' || 'Z' ?}
      +{? FAKS.T().PROJZAKR=exec('projzakr_nie_dotyczy','projekty')   || 'N(J)' || ''?}
      +{? FAKS.IST_TYP='' || 'N(E)' || '' ?}
      +{? FAKS.INTRA='T' || 'M' || '' ?}
      +{? FAKS.KZ='' || 'P' || '' ?}
      +'DUGN(CYRK)OW(BW):O'
   || {? FAKS.WHERE='M' || '' || 'H' ?}
      +'AN(W)'
      +{? FAKS.T().KOR='T' & (FKOR.IL=0 | FAP.POZP<>0) || 'W(B)' || '' ?}
      +{? FAKS.T().PROJZAKR=exec('projzakr_nie_dotyczy','projekty')   || 'N(J)' || ''?}
      +{? FAKS.IST_TYP='' || 'N(E)' || '' ?}
      +{? FAKS.INTRA='T' || 'M' || '' ?}
      +_nomod
      +{? FAKS.WHERE<>'G' || 'GO:O' || '' ?}
   ?};
FAP.actions_grayed(_wer,_act);
{? BPMN.DISPLAY=0
|| {? FAKS.SZ='S' | (FAKS.SZ='Z' & exec('upgrade2325_blbc1','zbl'))
   || FAP.actions_grayed('T'+_wer,_act)
   ?}
?}


\fap_rez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: wyswietlenie informacji o rozpisaniu ilosci z faktury po magazynach
::  OLD: \fap_rez/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
REZ.clear;
REZ.index('FAPR');
{? FAP.size || REZ.prefix($FAP.ref(),) || REZ.prefix('xxx','xxx',null,'xxx') ?};
REZ.first;
REZ.cntx_psh();
REZ.win_sel('FAP');
REZ.hdr_sel();
REZ.hdr_sel(' nr %1'@[form(FAP.POZ)]);
REZ.cntx_pop();
grp_disp(REZ,'FAP')


\pwfaprez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: pobiera informacje o dostawie ZK_P dla FAP-ów
::  OLD: \pwfaprez/rezerw.fml
::----------------------------------------------------------------------------------------------------------------------
_opis:='<< automatyczne rozpisanie >>';
_data:=date(0,0,0);
_cena:=0;
_msk:=DK.name;

{? REZ.SC<>''
|| _jest:=0;
   ND.cntx_psh();
   DK.cntx_psh();

   ND.use({? REZ.SC<>'' || 'nagdo'+((8+REZ.SC)+3) || ND.name() ?});
   DK.use({? REZ.SC<>'' || (8+REZ.SC) || DK.name() ?});
   DK.index('DOST3');
   DK.prefix(REZ.MG,REZ.M,BB.sqlint(REZ.SC),(8+REZ.SC),'T');
   {? DK.first()
   ||
      _opis:='DOKUMENT: '+DK.N().TYP().T+' - '+$DK.N().NR+' poz.'+$DK.P+
       '     KONTRAHENT: '+DK.N().KH().KOD+'-'+DK.N().KH().NAZ;
      _cena:=DK.C;
      _data:=DK.N().D;
      _jest:=1
   ?};
   ND.use('nagdo'+(_msk+3));
   DK.use(_msk);
   DK.cntx_pop();
   ND.cntx_pop();
   _cena:=exec('cena_mag','ceny',REZ.M,BB.sqlint(REZ.SC),(8+REZ.SC),,_cena);
   {? _jest || _opis+=',  DATA: '+form(_data)+',  CENA: '+form(_cena,,2) ?}
?};
FZ.FAP_OZKP:=_opis;
FZ.FAP_DZKP:=_data;
FZ.FAP_CZKP:=_cena;
0


\list_rez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: zachowuje poprzednia rozpiske na pozycjach
::   WE: _a - 0-zachowanie 1-przywrocenie 2-usuniecie tabelki tymczasowej
::  OLD: \list_rez/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=0
||
:: zachowanie listy rezerwacji
   VAR_DEL.delete('__faprez');
   __faprez:=tab_tmp(,'MAG','STRING[16]',''
               ,'SC','STRING[16]',''
               ,'ILR','REAL',''
               ,'IL2','REAL',''
               ,'MAT','STRING[16]','');

   REZ.index('FAPR');
   REZ.prefix($FAP.ref(),);
   {? REZ.first()
   || {!
      |? __faprez.blank();
         __faprez.MAG:=$REZ.MG;
         __faprez.SC:=REZ.SC;
         __faprez.ILR:=REZ.ILR;
         __faprez.IL2:=REZ.IL2;
         __faprez.MAT:=$REZ.M;
         __faprez.add(1);
         REZ.next()
       !}
   ?}
|? _a=1
||
:: przywrocenie listy rezerwacji
   _nrk:=0;
   _ilr:=0;
   _mat:=null();
   __faprez.prefix();
   REZ.index('FAPR');
   REZ.prefix($FAP.ref(),);
   {? REZ.first() || _mat:=REZ.M; {! |? _ilr+=REZ.ILR; _nrk:=REZ.NRK; REZ.del() !} || _mat:=FAP.M ?};
   {? _mat<>null || exec('aktu_rez','rezerwacje',_mat,_nrk,_ilr) ?};

   {? __faprez.first()
   || _nrk:=0;
      _ilr:=0;
      {!
      |? _mag:=exec('FindAndGet','#table',MG,__faprez.MAG,,,null());
         _mat:=exec('FindAndGet','#table',M,__faprez.MAT,,,null());
         _new:=exec('update','rezerwacje','FAP',FAP.ref(),_mag,_mat,__faprez.ILR,__faprez.SC,'B',,3,,
                ,__faprez.IL2,FAP.J2,{? FAP.WS2<>0 || 1/FAP.WS2 || 1 ?});
         {? ~_nrk || _nrk:=_new ?};
         _ilr+=__faprez.ILR;
         __faprez.next()
      !};
      exec('aktu_rez','rezerwacje',_mat,_nrk,_ilr)

   ?}
|? _a=2
|| __faprez.prefix();
   {? __faprez.first()
   || {!
      |? _mat:=exec('FindAndGet','#table',M,__faprez.MAT,,,null());
         _mag:=exec('FindAndGet','#table',MG,__faprez.MAG,,,null());
         exec('obl_stan','magazyn_stan',_mat,1,_mag);
         __faprez.next()
      !}
   ?};
   VAR_DEL.delete('__faprez')
|| ''
?}


\okngrp_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: okienko wyboru materialow podczas wystawiania faktury z generacja dokumentow magazynowych
::  OLD: \okngrp_m/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_sel:=M.grp_make('Stany magazynowe'@,,'posramwyzcrakcm');

__okn:=exec('addsmmag','magazyn_stan');
__okt:=exec('addzk_pt','magazyn_stan',0);

_grp:="grp_disp(__smmag, __okn);
       exec('tymc_prf','magazyn_stan',M.ref,'MG',FAKS.KH)";

M.grp_sel(_sel, M, 'NL_WERST',,_grp,,,,,,,,'maximized_with_title');
M.grp_splt(_sel,,'vertical','szcz');
M.grp_sel(_sel,__smmag, __okn,,,,,16,,,,,'maximized_with_title');
M.grp_splt(_sel,'szcz','horizontal','szcz1');
M.grp_sel(_sel,__zk_pt,__okt,,,,18,,,,,,'maximized_with_title');
_sel


\acts_schem_faks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Schemat akcji do wyłączenia - Pozycje wyświetlane w kontekście faktury
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'AN(XNPVW)J'


\wgwymiaf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: akcja wprowadzenia do pozycji faktury rozpiski wg lokalizacji i terminu waznosci
::  OLd: \wgwymiaf/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};

VAR_DEL.delete('__fapdkl');

__fapdkl:=tab_tmp(2,'TREE','TREE_REF','galazka'
           ,'MAG','STRING[16]','Magazyn'
           ,'LAB','STRING[90]','Etykieta'
           ,'LOK','STRING[20]','Lokalizacja'
           ,'TW','DATE','Termin ważności'
           ,'IL','REAL','Ilość'
           ,'JM','STRING[10]','Jm.');

_win_sel:=__fapdkl.mk_sel('Ilość wg wymiarów'@,'P',,'komarekispolka',,,__fapdkl.size(),1);

__fapdkl.win_fld(_win_sel,,'LAB',,,40,,1,'');
__fapdkl.win_fld(_win_sel,,'LOK',,,20,,,'Lokalizacja'@);
__fapdkl.win_fld(_win_sel,,'TW',,,20,,,'Termin ważności'@);
__fapdkl.win_fld(_win_sel,,'IL',,,10,3,,'Ilość'@);
__fapdkl.win_fld(_win_sel,,'JM',,,5,,1,'jm'@);
__fapdkl.win_act(_win_sel,0,'Formuła','&Dołącz'@@,,,"exec('fap2dk_l','faktury_poz',0)",,1,,,,'D');
__fapdkl.win_act(_win_sel,0,'Formuła','&Popraw'@@,,,"exec('fap2dk_l','faktury_poz',1)",,,,,,'P');
__fapdkl.win_act(_win_sel,0,'Formuła','&Usuń'@@,,,"exec('ufap2dkl','faktury_poz')",,,,,,'U');
__fapdkl.win_act(_win_sel,0,'Rekord',,,,"exec('rfap2dkl','faktury_poz')");
__fapdkl.win_act(_win_sel,0,'Formuła','&Akceptuj'@@,,'Akceptacja ilości wg wymiarów'@,,"sel_exit()",,,,,'A');
__fapdkl.win_act(_win_sel,0,'Formuła','&Legenda'@@,,,"exec('legenda','color','DKLFAP#01')",,,,,,'L');

__fapdkl.tr_fml(_win_sel,,"{? _a || {? __fapdkl.TREE=0 || 1 || _a ?} || _a ?}");
__fapdkl.win_fml(_win_sel,,'LAB',,'ICON_BEFORE',"
         {? __fapdkl.TREE<>0
         || 'xwin16.png:33'
         || ''
         ?}
      ");
__fapdkl.win_sel(_win_sel);


_win_edit:=__fapdkl.mk_edit('Pozycja wg wymiarów'@,0);
__fapdkl.win_esep(_win_edit,'Wymiary'@);
__fapdkl.win_efld(_win_edit,EANX,'LOKF',,,20,,,'Lokalizacja'@);
__fapdkl.win_efld(_win_edit,,'TW',,,10,,,'Termin ważności'@);
__fapdkl.win_efld(_win_edit,,'IL',,,10,3,,'Ilość'@);
exec('ok_esc','#window',__fapdkl,_win_edit);
__fapdkl.win_edit(_win_edit);

__fapdkl.efld_opt(_win_edit,'mark=1',EANX,'LOKF');
__fapdkl.efld_opt(_win_edit,'mark=1',,'IL');

:: wypelnia info o ilosciach wg magazynow
{? FAP.FAKS().WHERE='G' & FAP.FAKS().T().KOR='N'
||
:: faktura typu G
:: dane na podstawie ilosci na pozycji w rozbiciu na magazyn
   REZ.index('FAPR');
   REZ.prefix($FAP.ref(),);
   {? REZ.first()
   || {!
      |? {? REZ.MG().SL='T'
         || __fapdkl.clear();
            {? __fapdkl.find_key(0,$REZ.MG)
            || __fapdkl.IL+=REZ.ILR;
               __fapdkl.put(1)
            || __fapdkl.blank();
               __fapdkl.MAG:=$REZ.MG;
               __fapdkl.LAB:=REZ.MG().SYM+' - '+REZ.MG().NAZ;
               __fapdkl.IL:=REZ.ILR;
               __fapdkl.JM:=FAP.M().J().KOD;
               __fapdkl.add(1)
            ?}
         ?};
         REZ.next()
      !}
   ?}
?};

:: dane na podstawie wprowadzonych DK_L-i
__fapdkl.clear();
{? __fapdkl.size()
|| DK_L.index('FAP');
   DK_L.prefix(FAP.ref(),null);
   {? DK_L.first()
   || {!
      |? __fapdkl.clear();
         {? __fapdkl.find_key(0,$DK_L.MG)
         || _korzen:=__fapdkl.ref();
            __fapdkl.blank();
            __fapdkl.TREE:=_korzen;
            __fapdkl.LAB:=DK_L.LOK().OP;
            __fapdkl.LOK:=DK_L.LOK().KOD;
            __fapdkl.TW:=DK_L.TW;
            __fapdkl.MAG:=$DK_L.MG;
            __fapdkl.IL:=DK_L.IL;
            __fapdkl.JM:=FAP.M().J().KOD;
            __fapdkl.add(1)
         ?};
         DK_L.next()
      !}
   ?};
   __fapdkl.clear();
   {? __fapdkl.select()
   ||
::    przepisanie aktualnej rozpiski
      do();
      DK_L.index('FAP');
      DK_L.prefix(FAP.ref(),null);
      {? DK_L.first() || {! |? DK_L.del() !} ?};
      __fapdkl.clear();
      {? __fapdkl.first()
      || {!
         |? {? __fapdkl.TREE<>0
            || exec('add2dk_l','magdok_wspolne',null,null
                ,exec('FindInSet','#table','EANL','KOD',__fapdkl.LOK,__fapdkl.LOK),null
                ,__fapdkl.TW,date(0,0,0),__fapdkl.IL
                ,exec('FindAndGet','#table',MG,__fapdkl.MAG,,,null()),FAP.M,0,FAP.ref())
            ?};
            __fapdkl.next()
         !}
      ?};
      end()
   ?}
|| FUN.info('Ilość została przypisana do magazynów bez zastosowania wymiarów\n(definicja magazynów).\n'@+
            'Funkcja niedostępna.'@)
?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock)


\fap2dk_l
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: edycja jednej pozycji wg wymiarow dla faktury
::   WE: _a - 0-dolaczenie 1-poprawa
::  OLD: \fap2dk_l/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | (_a & __fapdkl.TREE)
|| {? ~_a
   || __fapdkl.cntx_psh();
      {? __fapdkl.TREE
      || __fapdkl.clear;
         __fapdkl.seek(__fapdkl.TREE,)
      ?};
      _mag:=__fapdkl.MAG;
      _tree:=__fapdkl.ref();
      _ile:=__fapdkl.IL;
      __fapdkl.cntx_pop();
      __fapdkl.blank();
      __fapdkl.MAG:=_mag;
      __fapdkl.IL:=_ile-exec('ilfapdkl','faktury_poz',_tree);
      __fapdkl.TREE:=_tree;
      EANX.LOKF:=''
   || EANX.LOKF:=__fapdkl.LOK
   ?};
   {? __fapdkl.edit("exec('cfap2dkl','faktury_poz')")
   || __fapdkl.LOK:=EANX.LOKF;
      __fapdkl.JM:=FAP.M().J().KOD;
      __fapdkl.LAB:=exec('FindInSet','#table','EANL','KOD',EANX.LOKF,EANX.LOKF,"EANL.OP",,,'');
      {? _a || __fapdkl.put(1) || __fapdkl.add(1) ?}
   ?}
?}


\ilfapdkl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: oblicza ile juz rozpisano dla danej galezi
::   WE: _a - ref TREE
::   WY: sumaryczna ilosc rozpisana
::  OLD: \ilfapdkl/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
__fapdkl.cntx_psh();
__fapdkl.clear();
__fapdkl.prefix(_a);
{? __fapdkl.first()
|| {!
   |? _wyn+=__fapdkl.IL;
      __fapdkl.next()
   !}
?};
__fapdkl.cntx_pop();
_wyn


\cfap2dkl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: sprawdzenie poprawnosci wypelnienia pol
::   WY: ''-jest ok 'xxxx'-akronim pola
::  OLD: \cfap2dkl/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
{? EANX.LOKF=''
|| FUN.info('Należy podać lokalizację.'@);
   'LOKF'
|? __fapdkl.IL<=0
|| FUN.info('Należy podać ilość większą od zera.'@);
   'IL'
|| _mg:=exec('FindInSet','#table','EANL','KOD',EANX.LOKF,EANX.LOKF,"EANL.MG",,,null());
   {? _mg=null | $_mg<>__fapdkl.MAG
   || FUN.info('Brak lokalizacji na magazynie.'@);
      'LOKF'
   || _refk:=__fapdkl.TREE;
      _refa:={? (1+menu_txt())='P' || __fapdkl.ref() || null ?};
      __fapdkl.cntx_psh();
      __fapdkl.clear();
      _ile:={? __fapdkl.seek(_refk,) || __fapdkl.IL || 0 ?};
      {? _refa<>null
      || __fapdkl.clear();
         _ileb:={? __fapdkl.seek(_refa) || __fapdkl.IL || 0 ?}
      || _ileb:=0
      ?};
      __fapdkl.cntx_pop();
      {? _ile+_ileb-exec('ilfapdkl','faktury_poz',__fapdkl.TREE)-__fapdkl.IL<0
      || FUN.info('Rozpisano zbyt dużą ilość.'@);
         'IL'
      || ''
      ?}
   ?}
?}


\ufap2dkl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: usuniecie jednej pozycji wg wymiarow
::  OLD: \ufap2dkl/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
{? __fapdkl.TREE & FUN.ask('Czy usunąć pozycję wg wymiarów?'@)
|| __fapdkl.del()
?}


\rfap2dkl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: wlacza/wylacza akcje na rekordzie
::  OLD: \rfap2dkl/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~__fapdkl.TREE
|| __fapdkl.actions(__fapdkl.win_sel('?'),'PU',,1)
|| __fapdkl.actions(__fapdkl.win_sel('?'),'',,1)
?};
exec('rekprzed','color','DKLFAP#01')


\kol_dk_l
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: kolorowanie ilosci do rozpisania
::  OLD: \kol_dk_l/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
{? __fapdkl.TREE=0
|| _sum:=0;
   __fapdkl.cntx_psh();
   __fapdkl.clear();
   __fapdkl.prefix(__fapdkl.ref());
   {? __fapdkl.first() || {! |? _sum+=__fapdkl.IL; __fapdkl.next() !} ?};
   __fapdkl.cntx_pop();
   {? __fapdkl.IL>_sum || 'DKLFAP#01#01'
   |? __fapdkl.IL<_sum || 'DKLFAP#01#02'
   || 1
   ?}
|| 1
?}


\f3faplok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: klawisz F3 EANX.LOKF
::  OLD: \f3faplok/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__znwyb');
_mag:=EANX.MG:=exec('FindAndGet','#table','MG',__fapdkl.MAG,,,null());
EANX.WYLZAL:={? EANX.MG().MWS='T' || 'N' || 'T' ?};
__znwyb:='E';
{? EANX.MG().W_ALL
|| SL.index('MGBLOK');
   SL.prefix(_mag,FAP.M,'N')
|| SL.index('MGDOK');
   SL.prefix(_mag,FAP.M,'T','N')
?};
SL.win_sel('SLO');
SL.hdr_sel('');
SL.hdr_sel('(lokalizacja, termin ważności)'@);
_eanl:=exec('FindInSet','#table','EANL','KOD',EANX.LOKF,EANX.LOKF);
{? _eanl=null || _eanl:=exec('FindInSet','#table','EANL','KOD',EANX.LOKF) ?};
SL.find_key(_eanl);
{? SL.select(,1)
|| {? __znwyb='E'
   || EANX.LOKF:=SL.EANL().KOD;
      __fapdkl.LOK:=EANX.LOKF;
      __fapdkl.TW:=SL.TW
   ?}
?};
VAR_DEL.delete('__znwyb');
~~


\pofaplok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: po redakcji EANX.LOKF
::  OLD: \pofaplok/cechy.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? EANX.LOKF=''
|| _wyn
|| _mg:=exec('FindInSet','#table','EANL','KOD',EANX.LOKF,EANX.LOKF,"EANL.MG",,,null());
   _beermg:=BEER.MG;
   _czydok:={? _mg<>null || exec('FindInSet','#table','EANL','KOD',EANX.LOKF,EANX.LOKF,"EANL.DOK",,,'')='T' || 0 ?};
   BEER.MG:=exec('FindAndGet','#table','MG',__fapdkl.MAG,,,null());
   {? $_mg=__fapdkl.MAG & (_czydok | BEER.MG().W_ALL)
   || __fapdkl.LOK:=EANX.LOKF;
      _wyn:=1
   |? BEER.MG().MWS='T'
   || FUN.info('Dla magazynu typu MWS podana lokalizacja musi być typu Dok załadunkowy/wyładunkowy.'@);
      _wyn:=0
   |? _mg=null & FUN.ask('Brak podanej lokalizacji w magazynie.\nCzy dodać ją do słownika?'@)
   || EANL.clear();
      EANL.blank();
      EANL.MG:=exec('FindAndGet','#table','MG',__fapdkl.MAG,,,null());
      EANL.DOK:={? EANL.MG().MWS='T' || 'N' || 'T' ?};
      EANL.KOD:=EANX.LOKF;
      {? EANL.add()
      || __fapdkl.LOK:=EANX.LOKF;
         _wyn:=1
      || _wyn:=0
      ?}
   || _wyn:=0
   ?};
   BEER.MG:=_beermg
?};
1


\add_pfap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PeKa [12.30]
:: OPIS: dodanie pozycji do dokumentu
::  OLD: \add_pfap/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.clear();
{? FAKS.seek(__faks_r)
||
   _env_fak_poz:=exec('env','faktury_poz');
   params_set('env_fak_poz',_env_fak_poz);
   DPOZ.MJSR:='F';
   _sc_win:=__win_sc;

   exec('ustafprz','faktury_wspolne');
   BEER.TAB:='FAP';

   {? FAKS.STS<>null & FAKS.STS().MGR<>'T' || POMOC.MGR:=POMOC.MGRF:=null ?};

   {? FAKS.T().UE='T' || POMOC.RODZ:='T' ?};
   _da2isto:={? FAKS.IST_OKR='' || date(FAKS.DW~1,FAKS.DW~2,1) || date(#(4+FAKS.IST_OKR),#(FAKS.IST_OKR+2),1) ?};

   _refold:=FAP.ref();
   FAP.blank();
   FAP.WAL:={? FAP.FAKS().WAL<>null || FAP.FAKS().WAL || INFO.NAROD ?};
   FAP.WAL_H:=FAP.WAL;
:: intrastat
   exec('istatr_faks2fap','faktury_nag');

   FAP.M:=SM.M;
   D_HELP.M_FAP:=SM.M().KTM;
   FAP.JM:=FAP.M().J;
   FAP.WAL:=FAKS.WAL;
   FAP.SV:=exec('m_vat','material',FAP.M,FAP.FAKS().KH,FAP.FAKS().T().EXPORT='T' | FAP.FAKS().T().UE='T' & TYPYSP.ZAK='N'
            ,FAKS.DW~1,FAKS.DW~2,,,,FAKS.SZ);
:: JPK_GTU
   {? var_pres('JPK_GTU',MGR)>0
   ||
      {? FAP.JPK_GTU=null()
      || {? FAP.M().JPK_GTU=null & exec('param_get','abs_conf', 300120, 2)='T'
         || FAP.JPK_GTU:=FAP.M().MGR().JPK_GTU
         || FAP.JPK_GTU:=FAP.M().JPK_GTU
         ?}
      ?}
   ?};

   _edit:=exec('fap_win_edit','faktury_poz');
   FAP.win_edit(_edit);

   FAP.memo_set();
   exec('set_efld_opt','faktury_poz');
   FAP.KH:=FAP.FAKS().KH;
   FAP.KH_ODB:=FAP.FAKS().KH_ODB;
   {? FAP.add() & FAP.r_lock(1,1,1)
   ||
::    przeliczniki jednostek
      exec('ustafprz','faktury_wspolne',FAP.M,FAP.M().J);
      _ed:=FAP.edit("params_exec('chk_fakp','faktury_poz',0)");
      {? _ed
      || {? DISP.ILF<=0
         || FUN.info('Ilość nie została rozpisana wg magazynów.\nDodanie pozycji niemożliwe.\n\n'@
                    +'Rozpisz ilość wg magazynów.'@);
            _ed:=0
         ?}
      ?};
      JM.f_clear();
      JM.win_dict('WER');
      {? _ed>0
      || _refold:=null;
         {? ~FAP.put()
         || exec('fap2del','faktury_poz',1)
         || FAP.memo_put(,'UWAGI')
         ?};
         FAP.r_unlock();
         exec('inf_dod','fakso',0,5+FAP.name());
         exec('liczfak','faktury_wspolne');
         exec('sumfak','faktury_wspolne')
      || FAP.r_unlock();
         exec('fap2del','faktury_poz',1);
         VAR_DEL.delete('__msk')
      ?}
   ?};
   exec('fap_win_edel','faktury_poz',_edit);
   {? _refold<>null || FAP.seek(_refold) ?};
   __sc.win_sel(_sc_win)
?};
''


\pw_dkczz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed wyswietleniem cech dla zwrotu - korekta faktury
::  OLD: \pw_dkczz/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_beermg:=BEER.MG;
BEER.MG:=exec('FindInSet','#table','MG','MAGAZYNY',X_Xa.MG,X_Xa.MG);
{? (1+BEER.MG().TYP)='D'
|| {? X_Xa.DK_C<>''
   || FKOR.DK_CZZ:=exec('FindInSet','#table','DK_C','SYM',X_Xa.DK_C,X_Xa.DK_C,"DK_C.SYM",,,'');
      {? FKOR.DK_CZZ='' || FKOR.DK_CZZ:=exec('FindInSet','#table','DK_C','SYM',,X_Xa.DK_C,,"DK_C.SYM",,,'') ?};
      {? FKOR.DK_CZZ<>'' || X_Xa.DK_C:=FKOR.DK_CZZ; X_Xa.put(1) ?}
   || FKOR.DK_CZZ:=''
   ?}
|| _wyn:=''
?};
BEER.MG:=_beermg;
_wyn


\pr_dkczz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcja cech dla zwrotu - korekta faktury
::  OLD: \pr_dkczz/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? X_Xa.DK_C<>'' || FKOR.DK_CZZ:=X_Xa.DK_C ?};
_beermg:=BEER.MG;
BEER.MG:=exec('FindInSet','#table','MG','MAGAZYNY',X_Xa.MG,X_Xa.MG);
_wyn:=(1+BEER.MG().TYP)='D';
BEER.MG:=_beermg;
{? FAKS.T().HIS<>'T' & _wyn
|| _wyn:=~(FKOR.DK_CZZ<>'' & exec('FindInSet','#table','DK_C','SYM',FKOR.DK_CZZ,FKOR.DK_CZZ)<>null)
?};
_wyn


\f3_dkczz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: F3 dla cech dla zwrotu - korekta faktury
::  OLD: \f3_dkczz/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_beerm:=BEER.M;
_beermg:=BEER.MG;
_atrmjs:=ATR.MJS;
ATR.MJS:='BEER';
BEER.M:=exec('FindInSet','#table','M','MATKTM',X_Xa.KTM,X_Xa.KTM);
BEER.MG:=exec('FindInSet','#table','MG','MAGAZYNY',X_Xa.MG,X_Xa.MG);
{? (1+BEER.MG().TYP)='D'
|| {? BEER.M().M_ATR<>null
   || _m_atr:=BEER.M().M_ATR;
      _uzup:='';
      {! _i..10 |! _uzup+={? ($('BEER.M().M_ATR().SL_'+form(_i,-2,0,'99')))()=null || '-' ||  '1' ?} !};
      BEER.M().KTM;
      exec('dk_atr_dict','mat_atr',1);
      DK_C.index('UZUP');
      DK_C.prefix(_m_atr,_uzup);
      {? X_Xa.DK_C<>'' || DK_C.seek(exec('FindInSet','#table','DK_C','SYM',X_Xa.DK_C,X_Xa.DK_C)) ?};
      {? DK_C.select(,1)
      || FKOR.DK_CZZ:=DK_C.SYM;
         X_Xa.DK_C:=FKOR.DK_CZZ
      ?}
   || DK_C.index('SYM');
      DK_C.win_sel('SEL');
      {? ~DK_C.find_key(FKOR.DK_CZZ,FKOR.DK_CZZ) || DK_C.find_key(FKOR.DK_CZZ) ?};
      {? DK_C.select(,1)
      || FKOR.DK_CZZ:=DK_C.SYM;
         X_Xa.DK_C:=FKOR.DK_CZZ
      ?}
   ?}
|| FKOR.DK_CZZ:='';
   X_Xa.DK_C:=FKOR.DK_CZZ
?};
BEER.M:=_beerm;
BEER.MG:=BEER.MG;
ATR.MJS:=_atrmjs;
1


\po_dkczz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji cech dla zwrotu - korekta faktury
::  OLD: \po_dkczz/faktury0.fml
::----------------------------------------------------------------------------------------------------------------------
_beerm:=BEER.M;
BEER.M:=exec('FindInSet','#table','M','MATKTM',X_Xa.KTM,X_Xa.KTM);
{? FKOR.DK_CZZ<>''
|| _ref:=exec('FindInSet','#table','DK_C','SYM',FKOR.DK_CZZ,FKOR.DK_CZZ);
   {? _ref=null
   || {? BEER.M().M_ATR=null
      || _ref:=exec('FindInSet','#table','DK_C','SYM',FKOR.DK_CZZ);
         {? _ref<>null
         || DK_C.cntx_psh();
            DK_C.clear();
            {? DK_C.seek(_ref)
            || FKOR.DK_CZZ:=DK_C.SYM
            || FKOR.DK_CZZ:=''
            ?};
            DK_C.cntx_pop()
         ?}
      || _uzup:='';
         {! _i..10
         |! _uzup+={? ($('BEER.M().M_ATR().SL_'+form(_i,-2,0,'99')))()=null || '-' ||  '1' ?}
         !};
         DK_C.cntx_psh();
         DK_C.index('M_ATR');
         DK_C.prefix(BEER.M().M_ATR,_uzup);
         {? DK_C.find_key(FKOR.DK_CZZ)
         || FKOR.DK_CZZ:=DK_C.SYM
         || FKOR.DK_CZZ:=''
         ?};
         DK_C.cntx_pop()
      ?}
   || {? BEER.M().M_ATR<>null()
       & BEER.M().M_ATR<>exec('FindInSet','#table','DK_C','SYM',FKOR.DK_CZZ,FKOR.DK_CZZ,"DK_C.M_ATR",,,null())
      || FKOR.DK_CZZ:=''
      ?}
   ?}
?};
X_Xa.DK_C:=FKOR.DK_CZZ;
X_Xa.put(1);
BEER.M:=_beerm;
1


\del_powf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009+]
:: OPIS: usuwa powiazania dla danej pozycji z dokumentami magazynowymi
::  OLD: \del_powf/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=__zakinf.sel_aget(); __zakinf.sel_adel();

__zakinf.cntx_psh();
{? _tab.size() & FUN.ask('Czy usunąć powiązania z pozycją faktury zakupowej?'@)
||
:: sa jakies zaznaczone rekordy
   _tab.clear();
   {? _tab.first()
   || {!
      |? {? (__zakinf.clear(); __zakinf.seek(_tab.REF,__zakinf.name()))
         || {? (5+__zakinf.DK_REF)='dokma'
            || FAP2DK.index('FAPDK');
               FAP2DK.prefix($FAP.ref(),__zakinf.DK_REF);
               {? FAP2DK.first() || {! |? exec('fapk_clear','magdok_wspolne'); FAP2DK.del() !} ?};
               exec('FindAndGet','#table',FAKS,FAP.FAKS,,"{? GEN=1 || GEN:=0; put(1) ?}");
               __zakinf.del()
            |? (5+__zakinf.DK_REF)='zdhip'
            || FAP2DK.index('FAPZD_RP');
               FAP2DK.prefix($FAP.ref(),__zakinf.DK_REF);
               {? FAP2DK.first() || {! |? exec('fapk_clear','magdok_wspolne'); FAP2DK.del() !} ?};
               exec('FindAndGet','#table',FAKS,FAP.FAKS,,"{? GEN=1 || GEN:=0; put(1) ?}");
               __zakinf.del()
            ?}
         ?};
         _tab.next()
      !}
   ?};
   exec('suma_fsv','faktury_vat');
   __zakin2.clear;
   __zakin2.first;
   grp_disp(__zakin2,__zakin2.win_sel('?'))
|? ~_tab.size() & FUN.ask('Czy usunąć powiązanie z pozycją faktury zakupowej?'@)
|| {? (5+__zakinf.DK_REF)='dokma'
   || FAP2DK.index('FAPDK');
      FAP2DK.prefix($FAP.ref(),__zakinf.DK_REF);
      {? FAP2DK.first() || {! |? exec('fapk_clear','magdok_wspolne'); FAP2DK.del() !} ?};
      exec('FindAndGet','#table',FAKS,FAP.FAKS,,"{? GEN=1 || GEN:=0; put(1) ?}");
      __zakinf.del()
   |? (5+__zakinf.DK_REF)='zdhip'
   || FAP2DK.index('FAPZD_RP');
      FAP2DK.prefix($FAP.ref(),__zakinf.DK_REF);
      {? FAP2DK.first() || {! |? exec('fapk_clear','magdok_wspolne'); FAP2DK.del() !} ?};
      exec('FindAndGet','#table',FAKS,FAP.FAKS,,"{? GEN=1 || GEN:=0; put(1) ?}");
      __zakinf.del()
   ?};
   exec('suma_fsv','faktury_vat');
   __zakin2.clear;
   __zakin2.first;
   grp_disp(__zakin2,__zakin2.win_sel('?'))
?};
__zakinf.cntx_pop();
1


\delfaksd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: usuniecie powiazan z pozycje faktury <-> pozycje dokumentu z poziomu faktury
::   WE: _a - ref FAKS-a
::  OLD: \delfaksd/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.cntx_psh;
FAP.cntx_psh;
FAP.index('FAP');
FAP.prefix(_a);
{? FAP.first
|| {!
   |? exec('delFAP_K','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),null());
      exec('fapdkdel','faktury_poz',$FAP.ref,FAP.ref,FAP.FAKS().WHERE);
      FAP.next
   !}
?};
FAKS_K.index('FAKS');
FAKS_K.prefix('N',_a);
{? FAKS_K.first || {! |? FAKS_K.del !} ?};
FAKS.cntx_pop;
FAP.cntx_pop;
1


\pow_zdzk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: usuwa powiazania do realizacji zamowienia
::   WE: _a - ref SQL-owy FAKS
::  OLD: \pow_zdzk/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
ZD_RN.cntx_psh();
ZD_RN.index('FK');
ZD_RN.prefix(_a);
{? ZD_RN.first()
||
   {!
   |?
      _rn_faks:='';
      _rn_sfk:='';

      _ref:=ZD_RN.ref();
      _ok:=ZD_RN.next();

      ZD_RN.cntx_psh();

      ZD_RN.clear();
      {? ZD_RN.seek(_ref)
       ||
         _rn_faks:=ZD_RN.FAKS;
         _rn_sfk:=ZD_RN.SFK;

         {? ZD_RN.ND<>''
         ||
            ND.cntx_psh();
            ND.use(form(8+ZD_RN.ND));
            ND.clear();
            {? ND.seek(ZD_RN.ND,form(8+ZD_RN.ND))
            || ND.FZAK:='';
               ND.put(1)
            ?};
            ND.cntx_pop()
         ?}
       ?};

      {? _rn_faks<>'' & _rn_sfk<>''
      ||
         ZD_RN.index('FK');
         ZD_RN.prefix(_rn_faks,_rn_sfk);
         {? ZD_RN.first()
         ||
            {!
            |?
               ZD_RN.cntx_psh();
               ZD_RN.prefix();
               ZD_RN.FAKS:=''; ZD_RN.SFK:=''; ZD_RN.DFK:=date(0,0,0);
               ZD_RN.put();
               ZD_RN.cntx_pop();
               ZD_RN.first()
            !}
         ?}
      ?};

      ZD_RN.cntx_pop();
      _ok
   !}
?};
ZD_RN.cntx_pop();
1


\aktfapdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: ile zostalo rozpisane wzgledem faktury zakupu
::   WE: _a - ref SQL-owy FAP
::       _b - nowa ilosc powiazana
::       _c - FAKS.WHERE
::  OLD: \aktfapdk/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
{? _b<0
|| FAP2DK.index('FAP');
   FAP2DK.prefix(_c,_a);
   {? FAP2DK.first()
   || FAP2DK.IL+=_b;
      FAP2DK.put(1)
   ?}
?}


\form_ilf_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: format dla edycji DISP.ILF
::----------------------------------------------------------------------------------------------------------------------
'in_prec='+{? FAKS.cntx_psh; _war:=FAP.FAKS().SZ='Z'; FAKS.cntx_pop; _war
           || {? (2+cur_kwin())='e_' & FAP.M<>null()
              || $FAP.M().DOKL
              || $ST.DOKL
              ?}
           || {? (2+cur_kwin())='e_' & FAP.M<>null()
              || $exec('ustdokil','jm','S','IL')
              || $ST.DOKL
              ?}
           ?}


\form_ilf_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: format dla wyświetlenia DISP.ILF
::----------------------------------------------------------------------------------------------------------------------
'out_prec='+{? FAKS.cntx_psh; _war:=FAP.FAKS().SZ='Z'; FAKS.cntx_pop; _war
            || {? (2+cur_kwin())='e_' & FAP.M<>null()
               || $exec('ustdokil','jm','S','IL')
               || $ST.DOKL
               ?}
            || {? (2+cur_kwin())='e_' & FAP.M<>null()
               || $exec('ustdokil','jm','S','IL')
               || $ST.DOKL
               ?}
            ?}


\obl_stf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2009]
:: OPIS: oblicza stan w magazynach - faktury z generacja
::  OLD: \obl_stf/zk.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=exec('FindAndGet','#table',FUX.TFUX,FUX.FUX,,,null());
{? FAKS.WHERE='G' & FAP.FAKS().MAG=null
|| exec('obl_stan','magazyn_stan',_ref,7,,1,,FAP.FAKS().D,-2);
   BEER.SD
|? HELP.WHERE='G' & FAP.FAKS().MAG<>null
|| exec('obl_stan','magazyn_stan',_ref,1,FAP.FAKS().MAG,1,,FAP.FAKS().D,-2);
   BEER.SD
|| 0
?}


\zakpowkm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Powiąż z dokumentami (korektami magazynowymi)
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.INTRA='T'
||
   FUN.info('Dokument uwzględniony w deklaracji Intrastat.'
      '\nPowiązanie z dokumentami niemożliwe.'@);
   return()
?};

::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablice i komentować poszczególne jej elementy
:: powiedzmy, że to bedzie pole
_fld:="31+form(_a)";
:: powiedzmy, że to bedzie metoda
_mth:="31+form(_a)";
:: środowisko
_env:=obj_new(
:: pola
    _fld('FAKS',     'Korekta zakupu')
   ,_fld('WHERE',    'Skąd korekty zakupu')
   ,_fld('FAP',      'Korygowana pozycja zakupu')
   ,_fld('FAPPOZ',   'Nr korygowanej pozycji')
   ,_fld('NAG',      'Daty korekt magazynowych')
   ,_fld('POW',      'Powiązano')
   ,_fld('POZ',      'Dokumenty korekt magazynowych')
   ,_fld('WER_POZ',  'Dokumenty korekt magazynowych')
);
:: środowisko - init
_env.FAKS:=FAP.FAKS;
_env.WHERE:=FAKS.WHERE;
_env.FAP:=FAP.ref();
_env.FAPPOZ:=FAP.POZ;
_env.NAG:=_NAG:=tab_tmp(1
   ,'DATA','DATE','Data'
   ,'POW','STRING[1]','Powiązano'
);
_env.POZ:=_POZ:=tab_tmp(2
   ,'DATA','DATE','Data'
   ,'SYM','STRING[100]','Symbol'
   ,'P','INTEGER','Pozycja'
   ,'IL','REAL','Ilość'
   ,'C','REAL','Cena'
   ,'DK','STRING[16]','Wskazanie DK'
   ,'ND','STRING[16]','Wskazanie ND'
);
::
_dk_kor:=exec('FindAndGet','#table',FAKS,FAKS.FKSQL,,"
   _env:=_b;
   FAP.cntx_psh();
   FAP.use((FAP.name()-3)+(FAKS.name()+3));
   FAP.cntx_psh();
   FAP.index('FAP');
   FAP.prefix(FAKS.ref(),_env.FAPPOZ);
   {? FAP.first()
   ||
      FAP2DK.cntx_psh();
      FAP2DK.index('FAPDK');
      FAP2DK.prefix($FAP.ref());
      _loop:=FAP2DK.first();
      {!
      |? _loop
      |!
         DK.index('KOR');
         DK.prefix(FAP2DK.DK);
         _loop:=DK.first();
         {!
         |? _loop
         |!
            FAP2DK.cntx_psh();
            FAP2DK.index('NDK');
            FAP2DK.prefix($DK.ref());
            _env.POW:={? FAP2DK.first() || 'T' || 'N' ?};
            exec('FindAndGet','#table',ND,DK.N,,\"
               _env:=_b;
               _NAG:=_env.NAG;
               _POZ:=_env.POZ;
               {? ~_NAG.find_key(ND.D) || _NAG.DATA:=ND.D; _NAG.POW:=_env.POW; _NAG.add() ?};
               _POZ.DATA:=ND.D;
               _POZ.SYM:=ND.SYM;
               _POZ.P:=DK.P;
               _POZ.C:=DK.C;
               _POZ.IL:=DK.IL;
               _POZ.DK:=$DK.ref();
               _POZ.ND:=$ND.ref();
               _POZ.add()
            \",,_env);
            FAP2DK.cntx_pop();
            _loop:=DK.next()
         !};
         _loop:=FAP2DK.next()
      !};
      FAP2DK.cntx_pop()
   ?};
   FAP.cntx_pop();
   FAP.cntx_pop()
",,_env);
{? ~_NAG.first()
||
   FUN.info('Brak korekt magazynowych do powiązania.'@)
||
   _wer_nag:=_NAG.mk_sel('Korekty'@,,,'zakpowkmnag');
   _NAG.win_fld(_wer_nag,,'POW',,,10,,,'Powiązano'@,,,2,,"'T'","'N'");
   _NAG.win_fld(_wer_nag,,'DATA',,,20,,,'Data'@);
   _fb:="
      _ue:=FAKS.T().UE='T';
      _env:=params_get().env;
      _NAG:=_env.NAG;
      _POZ:=_env.POZ;
      {? _NAG.POW='T'
      ||
         {? _ue=0 | FUN.ask('Czy naliczyć wartości intrastat wg dokumentu zakupu?'@)
         ||
            FAP2DK.cntx_psh();
            FAP2DK.index('FAPDK');
            FAP2DK.prefix($_env.FAP);
            _loop:=FAP2DK.first();
            {!
            |? _loop
            |!
               _loop:=FAP2DK.del()
            !};
            FAP2DK.cntx_pop();
            _NAG.POW:='N';
            {? _NAG.put() & _ue
            ||
               exec('liczfak_ist_wg_dokmag','faktury_wspolne',0)
            ?}
         ?}
      ||
         {? _ue=0 | FUN.ask('Czy naliczyć wartości intrastat wg dokumentów magazynowych?'@)
         ||
            _POZ.cntx_psh();
            _POZ.prefix(_NAG.DATA);
            _loop:=_POZ.first();
            {!
            |? _loop
            |!
               exec('adfap2dk','magdok_wspolne',$_env.FAP,_POZ.DK,$_env.FAKS,_POZ.ND,_POZ.IL,_env.WHERE);
               _loop:=_POZ.next()
            !};
            _POZ.cntx_pop();
            _NAG.POW:='T';
            {? _NAG.put() & _ue
            ||
               exec('liczfak_ist_wg_dokmag','faktury_wspolne',1)
            ?}
         ?}
      ?}
   ";
   _NAG.win_act(_wer_nag,,'Formuła','Wybierz'@,,,_fb,,1);
   _env.WER_POZ:=_wer_poz:=_POZ.mk_sel('Pozycje'@,,,'zakpowkmpoz');
   _POZ.win_fld(_wer_poz,,'SYM',,,20,,,'Symbol'@);
   _POZ.win_fld(_wer_poz,,'P',,,10,,,'Pozycja'@);
   _POZ.win_fld(_wer_poz,,'IL',,,10,FAP.M().DOKL,,'Ilość'@);
   _POZ.win_fld(_wer_poz,,'C',,,10,FAP.M().DOKL_C,,'Cena'@);
   _wer_gr:=_NAG.grp_make('Korekty magazynowe'@,,'zakpowkm');
   _far:="_env:=params_get().env; _NAG:=_env.NAG; _POZ:=_env.POZ; _POZ.prefix(_NAG.DATA);grp_disp(_POZ,_env.WER_POZ)";
   _NAG.grp_sel(_wer_gr,,_wer_nag,,_far);
   _NAG.grp_splt(_wer_gr,,'vertical','kor',',50%');
   _NAG.grp_sel(_wer_gr,_POZ,_wer_poz);
   _NAG.win_sel(_wer_gr);
   params_set('env',_env);
   _NAG.select()
?}


\zakpowdm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: akcja powiazania pozycji PZ-tek, WNT z dokumentami zakupowymi
::  OLD: \zakpowdm/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.get();
{? FAKS.T().KOR='T' || exec('zakpowkm','faktury_poz'); return() ?};
FAP.cntx_psh();
_nopos:=~FAP.size();
_ile_rozl:={? ~_nopos || exec('ile_rozf','faktury_wspolne',$FAP.ref,FAKS.WHERE) || 0 ?};
_opc:=0;
_waluta:=FAKS.WAL;
BEER.JMZ:=FAKS.JMZ;
_powusl:=FAP.M().RODZ='U';
_jmz:=BEER.JMZ & FAP.J2<>FAP.JM & FAP.WS2<>1;
_fapil:={? _jmz || FAP.IL2 || FAP.IL ?};
_nousl:=exec('get','#params',200400,2)='N';
_czy2pow:=exec('get','#params',200310,2)='T';
_where:={? FAKS.WHERE='Z' || 'E' || 'Z' ?};
{? (_nousl | FAKS.WHERE='E') & FAP.M().RODZ='U'
|| FUN.info('Funkcja niedostępna dla pozycji usługowych.'@);
   ''
|? FAKS.STAT_REJ='A'
|| FUN.info('Dokument został anulowany.\nFunkcja niedostępna.'@);
   ''
|? {? FAKS.ZAK='T'
   || 0
   |? FAKS.STAT_REJ<>'N' | FAKS.AKC='T'
   || {? _nopos | _fapil-_ile_rozl>0 || 1 || FUN.info('Pozycja dokumentu jest w całości powiązana.\n'@); 0 ?}
   || 1
   ?}
 & {? FAKS.STAT_REJ<>'N' | FAKS.AKC='T' || BEER.SP_POW:=0; 1
   |? exec('FindInSet','#table','FAP','FAP',FAKS.ref)=null
   || _opc:=1;
      {? FAKS.WHERE='Z'
      || _opc:=FUN.choice('Wybierz jedną z opcji powiązania pozycji faktury\n '
                       'z pozycjami dokumentów magazynowych lub z pozycjami realizacji zamówień dostaw.'@,
                       ,'Dopisać pozycje Materiałowe'@
                       ,'Dopisać pozycje Usługowe'@);
         _powusl:={? _opc=2 || 1 || 0 ?}
      ?};
      BEER.SP_POW:=1; _opc
   |? _fapil-_ile_rozl>0
   || BEER.SP_POW:={? FAKS.WHERE='Z' & ~_nousl
                   || _opc:=FUN.choice('Wybierz jedną z opcji powiązania pozycji faktury\n '
                       'z pozycjami dokumentów magazynowych lub z pozycjami realizacji zamówień dostaw.'@,
                       ,'Powiązać z bieżącą pozycją'@
                       ,'Dopisać pozycje Materiałowe'@
                       ,'Dopisać pozycje Usługowe'@);
                       {? _opc=3 || _powusl:=1; _opc:=2 ?};
                      _opc-1
                   || _opc:=FUN.choice('Wybierz jedną z opcji powiązania pozycji faktury '
                       'z pozycjami dokumentów magazynowych.'@,,'Powiązać z bieżącą poz.faktury'@,
                       'Dopisać do pozycji faktury'@);
                      _opc-1
                   ?};
      _opc
   || {? FAKS.WHERE='Z'
      || _opc:=FUN.choice('Pozycja dokumentu jest w całości powiązana.\n'
                          'Dopisać pozycje?'@,,'Materiałowe'@,'Usługowe'@);
         _powusl:=_opc=2
      || _opc:=FUN.ask('Pozycja dokumentu jest w całości powiązana.\n'
                       'Dopisać pozycje materiałowe?'@);
         _powusl:=0
      ?};
      BEER.SP_POW:=1; _opc
   ?}
||
   _kh:=FAKS.KH;
   _ref_m:={? ~BEER.SP_POW || FAP.M || null ?};

   _mnd:=(ND.name-2); _mdk:=(DK.name-2); _akt:=FAKS.name+2;

   _msk:=exec('tab_mask','faktury_wspolne',2,_akt);

   VAR_DEL.delete('__powzak');
   __powzak:=tab_tmp(3,'TREE','TREE_REF','galazka'
              ,'LAB','STRING[100]','Etykieta'
              ,'LA2','STRING[100]','Etykieta2'
              ,'MAG','STRING[10]','Magazyn'
              ,'SYM','STRING[100]','Symbol-KTM'
              ,'TYP','STRING[10]','Typ dokumentu'
              ,'DAT','DATE','Data wystawienia'
              ,'ILE','REAL','Ilosc'
              ,'CEN','REAL','Cena'
              ,'RDK','STRING[20]','ref DK'
              ,'RND','STRING[20]','ref ND'
              ,'ILP','REAL','Ilosc rozpisana'
              ,'ILR','REAL','Ilosc do rozpisania'
              ,'ICON','INTEGER','Ikonka'
              ,'DOK','STRING[40]','Ref dokumentu'
              ,'KTM','STRING[50]','Indeks materialowy'
              ,'POZ','STRING[2]','typ pozycji'
              ,'CWL','REAL','cena w walucie'
              ,'KRS','REAL','kurs'
              ,'WAL','STRING[3]','waluta'
              ,'FAP','STRING[20]','poz.faktury'
              ,'COW','REAL','obliczona cena w walucie'
              ,'JM1','STRING[10]','jednostka miary'
              ,'DKC','STRING[20]','cecha'
              ,'DKL','INTEGER','dokladnosc'
              ,'WS2','REAL','wsp.przeliczenia'
              ,'JM2','STRING[10]','jm2'
              ,'TUE','STRING[1]','typ ue T/N'
              ,'PRJ','STRING[20]','ref PROJEKTY');
   exec('war_hid','ceny',__powzak,'CEN','CWL','COW');

   _win_sel:=__powzak.mk_sel({? ~_powusl
                             || {? BEER.SP_POW
                                || 'DOPISANIE POZYCJI: Dokumenty przychodowe dla kontrahenta %1'@[FAKS.KH().NAZ]
                                || 'POWIĄZANIE POZYCJI: Dokumenty przychodowe dla kontrahenta %1'@[FAKS.KH().NAZ]
                                ?}
                             || {? BEER.SP_POW
                                || 'DOPISANIE POZYCJI: Realizacje zamówień dla kontrahenta %1'@[FAKS.KH().NAZ]
                                || 'POWIĄZANIE POZYCJI: Realizacje zamówień dla kontrahenta %1'@[FAKS.KH().NAZ]
                                ?}
                             ?},'P',,'#pow_zak_sel',,,__powzak.size(),1);

   _czy_wal:=FAKS.WAL<>INFO.NAROD;

   __powzak.win_fld(_win_sel,,'LAB',,,{? _czy_wal || 8 || 27 ?},,1,'');
   __powzak.win_fld(_win_sel,,'SYM',,,15,,1,'Symbol dokumentu / materiał'@);
   __powzak.win_fld(_win_sel,,'JM1',,,4,,1,'jm'@);
   __powzak.win_fld(_win_sel,,'DKC',,,5,,1,'Cecha'@);
   __powzak.win_fld(_win_sel,,'ILE',,,12,3,1,'Ilość'@);
   __powzak.win_fld(_win_sel,,'ILP',,,12,3,1,'Ilość rozpisana'@);
   __powzak.win_fld(_win_sel,,'CEN',,,10,ST.DOKL_C,1,'Cena'@);
   {? _czy_wal
   || __powzak.win_fld(_win_sel,,'CWL',,,10,ST.DOKL_C,1,'w %1 Kurs: %2'@[FAKS.WAL().KOD,form(FAKS.KRS,,4,'99')])
   ?};
   __powzak.win_fld(_win_sel,,'ILR',,,12,3,,'Do rozliczenia'@);
   __powzak.win_act(_win_sel,0,'Formuła','&Zwiń/rozwiń całość'@@,,'Zwijanie/rozwijanie całości'@,
             "exec('zwrw_all','#tree','__powzak','TREE')",,1,,,,'Z');
   __powzak.win_act(_win_sel,0,'Popraw',,,,"exec('pop_pow','faktury_poz')",,0);
   __powzak.win_act(_win_sel,0,'Rekord',,,,"exec('rek_pow1','faktury_poz')","exec('rek_pow2','faktury_poz')",0);
   __powzak.win_act(_win_sel,0,'Formuła','&Akceptuj'@@,,,,"exec('akc_pow','faktury_poz')",,0,,,'A');
   __powzak.win_act(_win_sel,0,'Formuła','&Wybierz'@@,,,,"exec('zatw_wyb','faktury_poz')"
    ,,1,"exec('zatw_wyb','faktury_poz')",,'W');
   __powzak.win_act(_win_sel,0,'Formuła','Z&rezygnuj z wyboru'@@,,,,"exec('zatw_wyb','faktury_poz',0)"
    ,,1,"exec('zatw_wyb','faktury_poz',0)",,'R');
   __powzak.win_act(_win_sel,0,'Formuła','Dane wg &indeksu'@@,,,,"exec('prezdane','faktury_poz','I')",,0,,,'I');
   __powzak.win_act(_win_sel,0,'Formuła','Dane wg &dokumentu'@@,,,,"exec('prezdane','faktury_poz','D')",,0,,,'D');
   __powzak.win_act(_win_sel,0,'Szukaj');
   __powzak.win_act(_win_sel,0,'Kolejność');
   __powzak.tr_fml(_win_sel,,"{? _a || {? __powzak.TREE=0 || 1 || _a ?} || _a ?}");
   __powzak.win_fml(_win_sel,,'LAB',,'ICON_BEFORE',"
            {? __powzak.TREE=0
            || 'xwin16.png:32'
            |? __powzak.TREE<>0 & __powzak.ILR>0
            || 'xwin16.png:38'
            || ''
            ?}
        ");
   __powzak.win_sel(_win_sel);
   BEER.fld_fml('SW','EDIT_FORMAT',"'in_prec='+{? (2+cur_kwin())='e_' || $M.DOKL || $ST.DOKL ?}");

   __powzak.fld_fml('ILE','EDIT_FORMAT'
             ,"'in_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILE','DISPLAY_FORMAT'
             ,"'out_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILP','EDIT_FORMAT'
             ,"'in_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILP','DISPLAY_FORMAT'
             ,"'out_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILR','EDIT_FORMAT'
             ,"'in_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('ILR','DISPLAY_FORMAT'
             ,"'out_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL\")
                          || $ST.DOKL
                          ?}");
   __powzak.fld_fml('CEN','EDIT_FORMAT'
             ,"'in_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL_C\")
                          || $ST.DOKL_C
                          ?}");
   __powzak.fld_fml('CEN','DISPLAY_FORMAT'
             ,"'out_prec='+{? (2+cur_kwin())='e_'
                          || $exec(\'FindInSet\',\'#table\',\'M\',\'MATKTM\',__powzak.KTM,__powzak.KTM,\"M.DOKL_C\")
                          || $ST.DOKL_C
                          ?}");
   __powzak.fld_fml('CEN','DISPLAY_FORMAT',"{? ~__powzak.TREE || 'empty=1' || 'empty=0' ?}");
   __powzak.fld_fml('CWL','DISPLAY_FORMAT',"{? ~__powzak.TREE || 'empty=1' || 'empty=0' ?}");

   __powzak.clear();

   _tue:=exec('czytypue','zamsiw_rea');
   {? ~_powusl
   || ND.cntx_psh();
      DK.cntx_psh();
      _msk.clear();
      {? _msk.first()
      || {!
         |? ND.use(_mnd+_msk.ROK);
            DK.use(_mdk+_msk.ROK);
            USERS_UP.index('MG');
            USERS_UP.prefix(OPERATOR.USER,'MG');
            {? USERS_UP.first
            || {!
               |? ND.index('KH');
                  ND.prefix(_kh,'T','N',USERS_UP.MG);
                  {? ND.first()
                  || {!
                     |? {? ND.TYP().DN='N' &  (exec('get','#params',200300,2)='T' | ND.WAL=_waluta) &
                         {? FAKS.WHERE='E' || ND.TYP().UE='T' & FAKS.KPW=ND.KPW & FAKS.RTRANSAK=ND.RTRANSAK || 1 ?}
                         & {? _tue='' || 1 || ND.TYP().UE=_tue ?}
                         & (BEER.SP_POW | FAP.D=date(0,0,0) | FAP.D=ND.D)
                        || DK.index('DOKMAG');
                           DK.prefix(ND.ref());
                           {? ND.Z='T'
                            & (_ok:=0;
                             {? DK.first()
                             || {!
                                |? {? ~_czy2pow & exec('FindInSet','#table','FAP2DK','DK',$DK.ref(),_where)<>null()
                                   || DK.next()
                                   |? DK.IL>0 & (_ref_m=null | _ref_m=DK.M)
                                     & {? FAKS.AKC='T'
                                       || exec('FindInSet','#table','FAP','FAM',DK.M,FAKS.ref())<>null
                                       || 1
                                       ?}
                                   || _ok:=1; 0
                                   || DK.next()
                                   ?}
                                !}
                             ?};
                             _ok)
                           ||
                              __powzak.blank();
                              _etyk:=ND.MAG().SYM+' ... '+ND.TYP().T+' ... '+ND.SYM+' z dnia '+form(ND.D);
                              __powzak.MAG:=ND.MAG().SYM;
                              __powzak.TYP:=ND.TYP().T;
                              __powzak.LAB:=_etyk;
                              __powzak.LA2:='';
                              __powzak.DAT:=ND.D;
                              __powzak.SYM:=ND.SYM+{? ND.FZAK<>'' || ' , '+ND.FZAK || '' ?};
                              __powzak.DOK:=ND.SYM+{? ND.FZAK<>'' || ' , '+ND.FZAK || '' ?}+' '+$ND.ref();
                              __powzak.KTM:='';
                              __powzak.TREE:=0;
                              __powzak.POZ:='DN';
                              __powzak.JM1:='';
                              __powzak.TUE:=ND.TYP().UE;
                              _korzen:={? __powzak.add(1) || _czy_add:=0;#__powzak.ref() || _czy_add:=1;0 ?};
                              {!
                              |? _ilp:={? ~_czy2pow & exec('FindInSet','#table','FAP2DK','DK',$DK.ref(),_where)<>null()
                                       || DK.IL
                                       || exec('ile_rozp','magdok_wspolne',$DK.ref(),FAKS.WHERE)
                                       ?};
                                 {? (DK.IL-_ilp)>0 & (_ref_m=null | _ref_m=DK.M)
                                  & {? FAKS.AKC='T' || exec('FindInSet','#table','FAP','FAM',DK.M,FAKS.ref)<>null || 1 ?}
                                 ||
                                    __powzak.blank();
                                    __powzak.TREE:=_korzen;
                                    __powzak.LAB:=form(DK.P,4,,'99')+'. '+DK.M().KTM+' - '+DK.M().N;
                                    __powzak.LA2:=_etyk;
                                    __powzak.TYP:=DK.N().TYP().T;
                                    __powzak.TUE:=DK.N().TYP().UE;
                                    __powzak.MAG:=DK.N().MAG().SYM;
                                    __powzak.SYM:=DK.M().KTM;
                                    __powzak.JM1:=DK.M().J().KOD;
                                    __powzak.ILE:=DK.IL;
                                    __powzak.ILP:=_ilp;
                                    __powzak.ILR:=0;
                                    __powzak.DOK:=ND.SYM+{? ND.FZAK<>'' || ' , '+ND.FZAK || '' ?}+' '+$ND.ref();
                                    __powzak.KTM:=DK.M().KTM;
                                    __powzak.POZ:='DP';
                                    {? FAKS.KRS & DK.N().WAL=FAKS.WAL
                                    || __powzak.CWL:=DK.CWAL;
                                       __powzak.WAL:=DK.N().WAL().KOD;
                                       __powzak.KRS:=FAKS.KRS
                                    |? FAKS.KRS
                                    || __powzak.CWL:=(DK.C/FAKS.KRS) $2;
                                       __powzak.WAL:=FAKS.WAL().KOD;
                                       __powzak.KRS:=FAKS.KRS
                                    |? DK.KURS & DK.N().WAL=DK.WAL
                                    || __powzak.CWL:=DK.CWAL;
                                       __powzak.WAL:=DK.WAL().KOD;
                                       __powzak.KRS:=DK.KURS
                                    || __powzak.CWL:=0;
                                       __powzak.WAL:='PLN';
                                       __powzak.KRS:=0
                                    ?};
                                    __powzak.CEN:={? __powzak.CWL || (__powzak.CWL*__powzak.KRS)$DK.M().DOKL_C || DK.C ?};
                                    __powzak.RDK:=$DK.ref();
                                    __powzak.RND:=$ND.ref();
                                    __powzak.COW:={? _czy_wal & FAKS.KRS<>0 || __powzak.CEN/FAKS.KRS || 0 ?} $DK.M().DOKL_C;
                                    __powzak.DKC:=DK.DK_C().SYM;
                                    __powzak.DKL:=exec('jaka_dok_m','jm',DK.M);
                                    __powzak.JM2:=DK.J2().KOD;
                                    __powzak.WS2:=DK.WS2;
                                    __powzak.PRJ:=$DK.PROJEKTY;
                                    _czy_add:=__powzak.add(1)
                                 ?};
                                 DK.next()
                              !};
                              {? _czy_add=0
                              || __powzak.del
                              ?}
                           ?}
                        ?};
                        ND.next()
                     !}
                  ?};
                  USERS_UP.next
               !}
            ?};
            _msk.next()
         !}
      ?};
      ND.use(_mnd+_akt);
      DK.use(_mdk+_akt);
      ND.cntx_pop();
      DK.cntx_pop()
   || _mzd:=ZD_RN.names();
      exec('openzd_psh','open_tab');
      _mzd.clear();
      {? _mzd.first()
      || {!
         |? exec('openzd','open_tab',_mzd.NAME+3);
            ZD_RN.index('KH');
            ZD_RN.prefix(_kh,'- USŁUGI -',);
            {? ZD_RN.first()
            || {!
               |? _czy_add:=0;
                  ZD_RP.index('POZ');
                  ZD_RP.prefix(ZD_RN.ref());
                  {? ZD_RP.first() & (exec('get','#params',200300,2)='T' | ZD_RN.ZD_NAG().WAL=_waluta)
                   & (FAP.D=date(0,0,0) | FAP.D=ZD_RN.DR)
                  || {!
                     |? _ilf:=0;
                        FAP2DK.index('ZD_RP');
                        FAP2DK.prefix($ZD_RP.ref());
                        {? FAP2DK.first() || {! |? {? FAP2DK.FAP<>'' || _ilf+=FAP2DK.IL ?}; FAP2DK.next() !} ?};
                        {? (ZD_RP.IL_ZRE-_ilf)>0 & (_ref_m=null | _ref_m=ZD_RP.M)
                        || {? ~_czy_add
                           || __powzak.blank();
                              _etyk:=ZD_RN.ZD_NAG().T().T+' ... '+ZD_RN.ZD_NAG().SYM+' realizacja z dnia '+form(ZD_RN.DR);
                              __powzak.MAG:='- USŁUGI -';
                              __powzak.TYP:=ZD_RN.ZD_NAG().T().T;
                              __powzak.LAB:=_etyk;
                              __powzak.LA2:='';
                              __powzak.DAT:=ZD_RN.DR;
                              __powzak.SYM:=ZD_RN.ZD_NAG().SYM;
                              __powzak.DOK:=$ZD_RN.ref();
                              __powzak.KTM:='';
                              __powzak.TREE:=0;
                              __powzak.POZ:='DN';
                              __powzak.JM1:='';
                              __powzak.TUE:='N';
                              _korzen:={? __powzak.add(1) || _czy_add:=0;#__powzak.ref() || _czy_add:=1;0 ?}
                           ?};
                           __powzak.blank();
                           __powzak.TREE:=_korzen;
                           __powzak.LAB:=form(ZD_RP.ZD_POZ().POZ,4,,'99')+'. '+ZD_RP.M().KTM+' - '+ZD_RP.M().N;
                           __powzak.LA2:=_etyk;
                           __powzak.TYP:=ZD_RN.ZD_NAG().T().T;
                           __powzak.TUE:='N';
                           __powzak.MAG:='- USŁUGI -';
                           __powzak.SYM:=ZD_RP.M().KTM;
                           __powzak.JM1:=ZD_RP.M().J().KOD;
                           __powzak.ILE:=ZD_RP.IL_ZRE;
                           __powzak.ILP:=_ilf;
                           __powzak.ILR:=0;
                           __powzak.DOK:=$ZD_RN.ref();
                           __powzak.KTM:=ZD_RP.M().KTM;
                           __powzak.POZ:='DP';
                           {? ZD_RN.ZD_NAG().WAL<>null & ZD_RN.ZD_NAG().WAL<>INFO.NAROD
                           || __powzak.CWL:=ZD_RP.ZD_POZ().CWAL;
                              __powzak.WAL:=ZD_RN.ZD_NAG().WAL().KOD;
                              __powzak.KRS:=ZD_RN.ZD_NAG().KRS
                           |? ZD_RP.ZD_POZ().CWAL=0 & FAKS.KRS>0 & ZD_RN.ZD_NAG().WAL<>FAKS.WAL & FAKS.NWAL=INFO.NAROD
                           || __powzak.CWL:=(ZD_RP.ZD_POZ().CENA/FAKS.KRS) $2;
                              __powzak.WAL:=FAKS.WAL().KOD;
                              __powzak.KRS:=FAKS.KRS
                           || __powzak.CWL:=0;
                              __powzak.WAL:='PLN';
                              __powzak.KRS:=0
                           ?};
                           __powzak.CEN:={? _czy_wal & FAKS.KRS<>0
                                         || __powzak.CWL*FAKS.KRS
                                         || ZD_RP.ZD_POZ().CENA
                                         ?}$ZD_RP.M().DOKL_C;
                           __powzak.RDK:=$ZD_RP.ref();
                           __powzak.RND:=$ZD_RN.ref();
                           __powzak.COW:={? _czy_wal & FAKS.KRS<>0 || __powzak.CEN/FAKS.KRS || 0 ?} $ZD_RP.M().DOKL_C;
                           __powzak.DKC:=ZD_RP.ZD_POZ().DK_C().SYM;
                           __powzak.DKL:=exec('jaka_dok_m','jm',ZD_RP.M);
                           __powzak.JM2:=ZD_RP.ZD_POZ().J2().KOD;
                           __powzak.WS2:=ZD_RP.ZD_POZ().WS2;
                           _czy_add:=__powzak.add(1)
                        ?};
                        ZD_RP.next()
                     !}
                  ?};
                  ZD_RN.next()
               !}
            ?};
            _mzd.next()
         !}
      ?};
      obj_del(_mzd);
      exec('openzd_pop','open_tab')
   ?};

   obj_del(_msk);

   __powzak.clear();
   {? __powzak.first()
   || __powzak.hdr_sel(' (prezentacja wg Dokumentów).'@);
      __powzak.select();
      exec('odbl_all','faktury_poz')
   || {? _powusl
      || FUN.info({? exec('get','#params',200300,2)='N'
                  || 'Brak niepowiązanych realizacji zamówień dostaw '
                     'związanych z kontrahentem:\n%1 - %2.'
                     '\n\nWystawionych w walucie %3.'@[FAKS.KH().KOD,FAKS.KH().NAZ,FAKS.WAL().KOD]
                  || 'Brak niepowiązanych realizacji zamówień dostaw '
                     'związanych z kontrahentem:\n%1 - %2.'@[FAKS.KH().KOD,FAKS.KH().NAZ]
                  ?})
      || FUN.info({? exec('get','#params',200300,2)='N'
                  || 'Brak niepowiązanych pozycji dokumentów magazynowych '
                     'związanych z kontrahentem:\n%1 - %2.'
                     '\n\nWystawionych w walucie %3.'@[FAKS.KH().KOD,FAKS.KH().NAZ,FAKS.WAL().KOD]
                  || 'Brak niepowiązanych pozycji dokumentów magazynowych '
                     'związanych z kontrahentem:\n%1 - %2.'@[FAKS.KH().KOD,FAKS.KH().NAZ]
                  ?})
      ?}
   ?}
|? ~_opc
|| ''
||
   {? FAKS.ZAK='T'
   || FUN.info('Dokument jest zaksięgowany. Powiązanie pozycji nie jest możliwe.'@)
   || FUN.info('Pozycja dokumentu jest w całości powiązana.'@)
   ?}
?};
FAP.cntx_pop();
1


\pop_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed akcja popraw
::   WY: jesli __powzak.TREE>0 wowczas do edycji
::  OLD: \pop_pow/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
{? __powzak.TREE & exec('zabl_nag','faktury_poz',1,__powzak.RND)
|| {? ~BEER.SP_POW
   || {? __powzak.ILR=0
      || _ilr:=exec('ilakcpoz','faktury_poz',$FAP.ref,__powzak.ref);
         _ilp:=FAP.IL-_ilr;
         _ild:=__powzak.ILE-__powzak.ILP;
         __powzak.ILR:={? _ilp>_ild || _ild || _ilp ?} $ __powzak.DKL
      ?}
   || {? __powzak.ILR=0
      || __powzak.ILR:=__powzak.ILE-__powzak.ILP $ __powzak.DKL
      ?}
   ?};
   1
|| 0
?}


\zabl_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009+]
:: OPIS: zablokowanie lub odblokowanie naglowka ND
::   WE: _a - 1-zablokowanie 0-odblokowanie -1-odblokowanie na podstawie naglowka
::   WY: 1-OK 0-nie jest dobrze
::  OLD: \zabl_nag/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
__powzak.cntx_psh();
{? (5+__powzak.RND)='nagdo'
|| ND.cntx_psh();
   ND.use(form(8+__powzak.RND));
   ND.clear();
   {? ND.seek(__powzak.RND,ND.name())
   || {? _a<=0
      ||
::    odblokowywany jest naglowek wylacznie jezeli dla innych oprocz danego jest ILR=0
         _rnd:=__powzak.RND;
         _ref:=__powzak.ref();
         _unb:=1;
         __powzak.clear();
         {? ~_a & __powzak.first()
         || {!
            |?
               {? __powzak.TREE<>0 & __powzak.RND=_rnd
               || _unb:={? __powzak.ref()=_ref || 1 || __powzak.ILR=0 ?}
               ?};
               _unb & __powzak.next()
            !}
         ?};
         {? _unb || ND.r_unlock() ?};
         _wyn:=1
      || {? ND.r_lock(1,1,1)
         || _wyn:=1
         || {? FUN.ask('Dokument %1 obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@[ND.SYM])
              & ND.r_lock()
            || ND.r_unlock()
            ?}
         ?}
      ?}
   ?};
   ND.cntx_pop()
|? (5+__powzak.RND)='zdhin'
|| exec('openzd_psh','open_tab');
   exec('openzd','open_tab',form(8+__powzak.RND)+3);
   ZD_RN.prefix();
   {? ZD_RN.seek(__powzak.RND,ZD_RN.name())
   || {? _a<=0
      ||
::    odblokowywany jest naglowek wylacznie jezeli dla innych oprocz danego jest ILR=0
         _rnd:=__powzak.RND;
         _ref:=__powzak.ref();
         _unb:=1;
         __powzak.clear();
         {? ~_a & __powzak.first()
         || {!
            |?
               {? __powzak.TREE<>0 & __powzak.RND=_rnd
               || _unb:={? __powzak.ref()=_ref || 1 || __powzak.ILR=0 ?}
               ?};
               _unb & __powzak.next()
            !}
         ?};
         {? _unb || ZD_RN.r_unlock() ?};
         _wyn:=1
      || {? ZD_RN.r_lock(1,1,1)
         || _wyn:=1
         || {? FUN.ask('Realizację zamówienia %1 obsługuje inny operator.\nCzy chcesz zobaczyć kto?'@[ZD_RN.ZD_NAG().SYM])
              & ZD_RN.r_lock()
            || ZD_RN.r_unlock()
            ?}
         ?}
      ?}
   ?};
   exec('openzd_pop','open_tab')
?};
__powzak.cntx_pop();
_wyn


\ilakcpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: zwraca sumaryczna ilosc rozpisana do danej pozycji - uwzglednia pozycje aktualnie edytowane rowniez
::   WE: _a - $FAP.ref
::       _b - ref aktualnego __powzak
::   WY: ilosc rozpisana
::  OLD: \ilakcpoz/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('ilrfapdk','faktury_poz',$FAP.ref,FAP.FAKS().WHERE);
__powzak.cntx_psh();
__powzak.clear();
{? __powzak.first()
|| {!
   |? _wyn+={? __powzak.TREE>0 & __powzak.ref<>_b || __powzak.ILR ?};
      __powzak.next()
   !}
?};
__powzak.cntx_pop();
_wyn


\rek_pow1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: akcja podswietlenia rekordu
::   WY: jesli __powzak.TREE>0 i ilosc do realizacji jest wieksz od zera
::  OLD: \rek_pow1/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
__powzak.TREE<>0 & __powzak.ILR>0


\rek_pow2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po akcji popraw - dokonywane jest sprawdzenie czy nie przekraczamy ilosci dostepnej do przypisania
::   WY: 1-ok 0-gdy niepoprawna ilosc
::  OLD: \rek_pow2/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
__powzak.ILR:=__powzak.ILR $ __powzak.DKL;
{? __powzak.ILR<0
|| FUN.info('Ilość nie może być ujemna.'@);
   __powzak.ILR:=0;
   0
|? BEER.SP_POW & (_rozp:=__powzak.ILE-__powzak.ILP)<__powzak.ILR
|| FUN.info('Zbyt duża ilość. Możliwe do rozpisania: %1.'@[form(_rozp,12,3,'99')]);
   __powzak.ILR:=_rozp $ __powzak.DKL;
   0
|? ~BEER.SP_POW & (_ilp:=FAP.IL-exec('ilakcpoz','faktury_poz',$FAP.ref,__powzak.ref); _ild:=__powzak.ILE-__powzak.ILP;
   (_rozp:={? _ilp>_ild || _ild || _ilp ?})<__powzak.ILR)
|| FUN.info('Zbyt duża ilość. Możliwe do rozpisania: %1.'@[form(_rozp,12,3,'99')]);
   __powzak.ILR:=_rozp $ __powzak.DKL;
   0
|| {? __powzak.TREE & __powzak.ILR=0 || exec('zabl_nag','faktury_poz',0,__powzak.RND) ?};
   1
?}


\akc_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: akceptuje wybrane pozycje do faktury i przepisuje je na pozycje faktury
::  OLD: \akc_pow/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ini_kom','#message','Powiąż z dokumentami');
_czy_wal:=FAKS.WAL<>INFO.NAROD;

__powzak.cntx_psh();
__powzak.clear();
:: najpierw sprawdzenie czy cos jest do akceptacji
_sa_zazn:=0;
{? __powzak.first()
|| {!
   |? _sa_zazn:=__powzak.TREE<>0 & __powzak.ILR>0;
     ~_sa_zazn & __powzak.next()
   !}
?};
__dsp_w_poz:=exec('x00138','faktury_wspolne','Z')='T';
{? BEER.SP_POW & __dsp_w_poz=0 & exec('x00138','faktury_wspolne','Z')='P'
||
   {? FUN.ask('Czy umieścić datę sprzedaży w pozycji faktury?'@) || __dsp_w_poz:=1 ?}
?};
{? ~_sa_zazn
|| FUN.info('Żadna z pozycji nie jest zaznaczona.'@)
|? __powzak.first & FUN.ask('Czy zaakceptować wybór pozycji?'@)
|| _p100201:=exec('get','#params',100201,2)='T';
   {? FAKS.SZ='Z' & FAKS.T().KOR='N' & FAP.CWAL<>0 || DISP.ZMREZ:=1 ?};
   {!
   |? {? __powzak.TREE<>0 & __powzak.ILR>0
      || BEER.M:=exec('FindInSet','#table','M','MATKTM',__powzak.KTM,__powzak.KTM);
         _wal:=exec('FindInSet','#table','SLO','SL',__powzak.WAL,INFO.SLWAL().SLU);

         _ilr:=__powzak.ILR;
         _korzen:=#__powzak.ref;
::       najpierw robimy przypisania jezeli zostaly podane do pozycji
         __powzak.cntx_psh;
         __powzak.clear;
         __powzak.prefix(_korzen);
         {? __powzak.first
         || {!
            |? exec('aktpozdk','faktury_poz');
               {? __powzak.ILR>0
               || {? (5+__powzak.RDK)='dokma'
                  || exec('adfap2dk','magdok_wspolne',__powzak.FAP,__powzak.RDK,$FAKS.ref
                      ,__powzak.RND,__powzak.ILR,FAKS.WHERE,1)
                  |? (5+__powzak.RDK)='zdhip'
                  || exec('adfap2dk','magdok_wspolne',__powzak.FAP,'',$FAKS.ref
                      ,'',__powzak.ILR,FAKS.WHERE,1,__powzak.RDK)
                  ?};
                  _ilr-=__powzak.ILR
               ?};
               __powzak.next
            !}
         ?};
         __powzak.cntx_pop;

::       to co pozostalo _ilr dopisujemy do pozycji faktury
         {? _ilr>0 &
            {? BEER.SP_POW
            || __zmst:='';
               {? __powzak.WS2<>0
               || _j2:=exec('FindInSet','#table','JM','KOD',__powzak.JM2,__powzak.JM2);
                  _jm:=exec('FindInSet','#table','JM','KOD',__powzak.JM1,__powzak.JM1);

                  _dokl_s:=exec('jaka_dok_mjm','jm',BEER.M,_j2,_jm);
                  roundmet(BEER.MDOKL);
                  exec('przyjdod','jm',$_j2,1/__powzak.WS2,_ilr/__powzak.WS2 $ _dokl_s);
                  roundmet(5)
               ?};
               {? KSTE.KRAJ
               || do();
                  _st_vat:=exec('FindAndGet','#table',DK,__powzak.RDK,,"DK.SV",null());
                  __powzak.cntx_psh;
                  __powzak.prefix;
                  _st_vat:=
                     {? __powzak.TREE & __powzak.seek(__powzak.TREE,)
                     || exec('m_vat','material',BEER.M,FAKS.KH,,__powzak.DAT~1,__powzak.DAT~2,_st_vat,2,!__zmst,FAKS.SZ)
                     || exec('m_vat','material',BEER.M,FAKS.KH,,,,_st_vat,2,!__zmst,FAKS.SZ)
                     ?};
                  __powzak.cntx_pop;
                  _st_vat:=FAP.SV:=exec('m_vat','material',BEER.M,FAKS.KH,,FAKS.AR,FAKS.AM,_st_vat,2,!__zmst,FAKS.SZ);
                  _vat:=FAP.SV().KOD;
                  end()
               || _st_vat:=BEER.M().VAT;
                  _vat:=BEER.M().VAT().KOD
               ?};
               _vat:=#((_vat*'%')+_vat-1)/100;
               _cen:={? FAKS.CB='T' || (__powzak.CEN*(1+_vat))$BEER.M().DOKL_C || __powzak.CEN ?};
               _data:=__powzak.DAT;
               {? __powzak.TREE<>0 || _data:=exec('FindAndGet','#table',__powzak,__powzak.TREE,,"__powzak.DAT",date(0,0,0)) ?};
               _dsp:={? __dsp_w_poz || _data || date(0,0,0) ?};
               _ok:=
                  {? _czy_wal & __powzak.CWL=0
                  || exec('ADDPOZF','faktury_poz',BEER.M,_ilr,_cen,0,_st_vat,,
                        ,__powzak.COW,FAKS.KRS,FAKS.WAL,BEER.M().J,,,,,,,,,_dsp)
                  || exec('ADDPOZF','faktury_poz',BEER.M,_ilr,_cen,0,_st_vat,,
                        ,__powzak.CWL,__powzak.KRS,_wal,BEER.M().J,,,,,,,,,_dsp)
                  ?};
               {? __zmst<>'' || exec('add_kom','#message',__zmst,1,'Pozycja %1'[$FAP.POZ]) ?};

::             cecha
               {? (5+__powzak.RDK)='dokma'
               || DK.cntx_psh();
                  DK.use(form(8+__powzak.RDK));
                  DK.clear();
                  {? DK.seek(__powzak.RDK,form(8+__powzak.RDK))
                  || FAP.DK_C:=DK.DK_C;
                     FAP.put()
                  ?};
                  DK.cntx_pop()
               |? (5+__powzak.RDK)='zdhip'
               || exec('openzd_psh','open_tab');
                  exec('openzd','open_tab',form(8+__powzak.RDK)+3);
                  ZD_RP.use(form(8+__powzak.RDK));
                  ZD_RP.prefix();
                  {? ZD_RP.seek(__powzak.RDK,form(8+__powzak.RDK))
                  || FAP.DK_C:=ZD_RP.ZD_POZ().DK_C;
                     FAP.put()
                  ?};
                  exec('openzd_pop','open_tab')
               ?};
::projekty
               PROJEKTY.cntx_psh();TYPYSP.cntx_psh();PROJTYPY.cntx_psh();
               PROJEKTY.prefix();TYPYSP.prefix();PROJTYPY.prefix();
               {? __powzak.PRJ<>''
               ||
                  {? PROJEKTY.seek(__powzak.PRJ) & FAKS.PROJEKTY=null()
                  & (FAKS.T().PROJZAKR='Wszystkie' | (4+PROJEKTY.T().R)=(4+FAKS.T().PROJZAKR))
                  || FAP.PROJEKTY:=PROJEKTY.ref(); FAP.put()
                  ?}
               ?};
               {? FAKS.PROJEKTY<>null()
               || FAP.PROJEKTY:=FAKS.PROJEKTY; FAP.put()
               ?};
               PROJEKTY.cntx_pop();TYPYSP.cntx_pop();PROJTYPY.cntx_pop();

::             rozpiska projektow wg szablonu
:: DRO: [rr] bez projektów
::              exec('proj2fap_roz','skid_prm',0); exec('proj2fap_okn','skid_prm');

::             naliczenie opłat dodatkowych
               exec('actTAXS','oplaty_dod',FAP.FAKS().uidref(),FAP.uidref(),FAP.M,null());
::             tworzenie informacji dodatkowych
               exec('inf_dod','fakso',0,'fakpo');
               {? FAKS.SZ='Z' & FAKS.IST_TYP='P' & (5+__powzak.RDK)='dokma'
               || DK.cntx_psh();
                  DK.use(form(8+__powzak.RDK));
                  DK.clear();
                  {? DK.seek(__powzak.RDK,form(8+__powzak.RDK))
                  || FAP.KP:=DK.KP;
                     FAP.KCN:=DK.KCN;
                     {? FAP.IL<>DK.IL
                     ||
                        exec('liczfak','faktury_wspolne',1,1)
                     ||
                        FAP.MASAN:=DK.MASAN;
                        {? _p100201
                        || FAP.WF:=DK.WF;
                           FAP.WS:=DK.WS
                        ?};
                        FAP.ILUJM:=DK.ILUJM
                     ?};
                     FAP.put()
                  ?};
                  DK.cntx_pop()
               ?};
               _ok
            || 1
            ?}
         || {? (5+__powzak.RDK)='dokma'
            || exec('adfap2dk','magdok_wspolne',$FAP.ref(),__powzak.RDK,$FAKS.ref(),__powzak.RND,_ilr,FAKS.WHERE,1);
               exec('wycenND','faktury_wspolne',FAP.ref())
            |? (5+__powzak.RDK)='zdhip'
            || exec('adfap2dk','magdok_wspolne',$FAP.ref(),'',$FAKS.ref(),'',_ilr,FAKS.WHERE,1,__powzak.RDK)
            ?}
         ?}
      ?};
      __powzak.next
   !};
   sel_exit;
   exec('sumfak','faktury_wspolne')
?};
__powzak.cntx_pop();
VAR_DEL.delete('__zmst','__dsp_w_poz');
exec('end_kom','#message');
1


\aktpozdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009+]
:: OPIS: aktualizauje dane dla __powzak z DK lub z ZD_RP
::  OLD: \aktpozdk/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
{? (5+__powzak.RDK)='dokma'
|| ND.cntx_psh();
   DK.cntx_psh();
   _msk:=form(8+__powzak.RDK);
   ND.use('nagdo'+(_msk+3));
   DK.use(_msk);
   DK.clear();
   {? DK.seek(__powzak.RDK,DK.name())
   ||
      __powzak.CEN:=DK.C;
      __powzak.ILE:=DK.IL;
      {? FAKS.KRS & DK.N().WAL=FAKS.WAL
      || __powzak.CWL:=DK.CWAL;
         __powzak.WAL:=DK.N().WAL().KOD;
         __powzak.KRS:=FAKS.KRS
      |? FAKS.KRS
      || __powzak.CWL:=(DK.C/FAKS.KRS) $2;
         __powzak.WAL:=FAKS.WAL().KOD;
         __powzak.KRS:=FAKS.KRS
      |? DK.KURS & DK.N().WAL=DK.WAL
      || __powzak.CWL:=DK.CWAL;
         __powzak.WAL:=DK.WAL().KOD;
         __powzak.KRS:=DK.KURS
      || __powzak.CWL:=0;
         __powzak.WAL:='PLN';
         __powzak.KRS:=0
      ?};
      __powzak.CEN:={? __powzak.CWL || (__powzak.CWL*__powzak.KRS)$DK.M().DOKL_C || DK.C ?};
      __powzak.COW:={? FAKS.WAL<>INFO.NAROD & FAKS.KRS<>0 || __powzak.CEN/FAKS.KRS || 0 ?} $DK.M().DOKL_C;
      __powzak.put(1)
   ?};
   ND.cntx_pop();
   DK.cntx_pop()
|? (5+__powzak.RDK)='zdhip'
|| exec('openzd_psh','open_tab');
   exec('openzd','open_tab',form(8+__powzak.RDK)+3);
   ZD_RP.use(form(8+__powzak.RDK));
   ZD_RP.prefix();
   {? ZD_RP.seek(__powzak.RDK,ZD_RP.name())
   ||
      __powzak.CEN:=ZD_RP.ZD_POZ().CENA;
      __powzak.ILE:=ZD_RP.IL_ZRE;
      __powzak.CWL:=ZD_RP.ZD_POZ().CWAL;
      __powzak.CEN:={? FAKS.WAL<>INFO.NAROD & FAKS.KRS>0 || __powzak.CWL*FAKS.KRS || __powzak.CEN ?} $ZD_RP.M().DOKL_C;
      __powzak.put(1)
   ?};
   exec('openzd_pop','open_tab')
?};
1


\zatw_wyb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: zatwierdzenie wybrania rekordow na okienku
::   WE: _a - 1-zaznaczenie (domyslnie) 0-odznaczenie
::  OLD: \zatw_wyb/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=1 ?} || _a:=1 ?};

_czesc:=0;
_tab:=__powzak.sel_aget(); __powzak.sel_adel();
_nag:=sql('select distinct :_a.TREE TREE, 0 BYL from :_a order by 1',__powzak);
_ilezaz:=0;
_tue:=exec('czytypue','zamsiw_rea');
_ilenie:=0;
_dspnie:=0;
BEER.JMZ:=FAKS.JMZ;
_jmz:=BEER.JMZ & FAP.J2<>FAP.JM & FAP.WS2<>1;
_fapil:={? _jmz || FAP.IL2 || FAP.IL ?};
_dsp:={? ~BEER.SP_POW || FAP.D || date(0,0,0) ?};
_data:=__powzak.DAT;
{? __powzak.TREE<>0 || _data:=exec('FindAndGet','#table',__powzak,__powzak.TREE,,"__powzak.DAT",date(0,0,0)) ?};

__powzak.cntx_psh;
{? _tab.size
||
:: sa jakies zaznaczone rekordy
   _tab.clear;
   {? _tab.first
   || {!
      |? {? (__powzak.clear; __powzak.seek(_tab.REF,__powzak.name))
         || {? __powzak.TREE<>0 & (_nag.clear; (_nag.find_key(__powzak.TREE) & ~_nag.BYL))
             & {? _tue='' || 1 || {? __powzak.TUE=_tue || 1 || _ilenie+=1; 0 ?} ?}
             & {? _dsp=date(0,0,0) || 1 || {? _data=_dsp || 1 || _dspnie+=1; 0 ?} ?}
            || _ilezaz+=1;
               _tue:=__powzak.TUE;
               {? exec('zabl_nag','faktury_poz',_a,__powzak.RND)
               || exec('aktpozdk','faktury_poz');
                  {? ~BEER.SP_POW
                  || {? _a & __powzak.ILR>=0
                     || _ilr:=exec('ilakcpoz','faktury_poz',$FAP.ref,__powzak.ref);
                        _ilp:=_fapil-_ilr;
                        _ild:=__powzak.ILE-__powzak.ILP;
                        __powzak.ILR:={? _ilp>0 || {? _ilp>_ild || _ild || _ilp ?} || _czesc+=1; 0 ?} $ __powzak.DKL
                     |? ~_a & __powzak.ILR>0
                     || __powzak.ILR:=0
                     ?}
                  || {? _a & __powzak.ILR=0
                     || __powzak.ILR:=__powzak.ILE-__powzak.ILP $ __powzak.DKL
                     |? ~_a & __powzak.ILR>0
                     || __powzak.ILR:=0
                     ?}
                  ?};
                  __powzak.put(1)
               ?}
            |? __powzak.TREE=0 & {? _tue='' || 1 || {? __powzak.TUE=_tue || 1 || _ilenie+=1; 0 ?} ?}
             & {? _dsp=date(0,0,0) || 1 || {? _data=_dsp || 1 || _dspnie+=1; 0 ?} ?}
            || _tue:=__powzak.TUE;
               _nag.clear;
               _refzak:=#__powzak.ref;
               __powzak.tr_set(1,,,1);
               {? _nag.find_key(_refzak) || _nag.BYL:=1; _nag.put(1) ?};
               __powzak.clear;
               __powzak.prefix(_refzak);
               {? __powzak.first
               || {!
                  |? _ilezaz+=1;
                     {? exec('zabl_nag','faktury_poz',_a,__powzak.RND)
                     || exec('aktpozdk','faktury_poz');
                        {? ~BEER.SP_POW
                        || {? _a & __powzak.ILR>=0
                           || _ilr:=exec('ilakcpoz','faktury_poz',$FAP.ref,__powzak.ref);
                              _ilp:=_fapil-_ilr;
                              _ild:=__powzak.ILE-__powzak.ILP;
                              __powzak.ILR:={? _ilp>0 || {? _ilp>_ild || _ild || _ilp ?} || _czesc+=1; 0 ?} $ __powzak.DKL
                           |? ~_a & __powzak.ILR>0
                           || __powzak.ILR:=0
                           ?}
                        || {? _a & __powzak.ILR=0
                           || __powzak.ILR:=__powzak.ILE-__powzak.ILP $ __powzak.DKL
                           |? ~_a & __powzak.ILR>0
                           || __powzak.ILR:=0
                           ?}
                        ?};
                        __powzak.put(1)
                     ?};
                     __powzak.next
                  !}
               ?}
            || ''
            ?}
         ?};
         _tab.next
      !}
   ?};
   obj_del(_tab)
||
:: biezacy rekord
   {? __powzak.TREE<>0 & {? _tue='' || 1 || {? __powzak.TUE=_tue || 1 || _ilenie+=1; 0 ?} ?}
    & {? _dsp=date(0,0,0) || 1 || {? _data=_dsp || 1 || _dspnie+=1; 0 ?} ?}
   ||
      _ilezaz+=1;
      _tue:=__powzak.TUE;
      {? exec('zabl_nag','faktury_poz',_a,__powzak.RND)
      || exec('aktpozdk','faktury_poz');
         {? ~BEER.SP_POW
         || {? _a & __powzak.ILR>=0
            || _ilr:=exec('ilakcpoz','faktury_poz',$FAP.ref,__powzak.ref);
               _ilp:=_fapil-_ilr;
               _ild:=__powzak.ILE-__powzak.ILP;
               __powzak.ILR:={? _ilp>0 || {? _ilp>_ild || _ild || _ilp ?} || _czesc+=1; 0 ?} $ __powzak.DKL
            |? ~_a & __powzak.ILR>0
            || __powzak.ILR:=0
            ?}
         || {? _a & __powzak.ILR>=0
            || __powzak.ILR:=__powzak.ILE-__powzak.ILP $ __powzak.DKL
            |? ~_a & __powzak.ILR>0
            || __powzak.ILR:=0
            ?}
         ?};
         __powzak.put(1)
      ?}
   |? {? _tue='' || 1 || {? __powzak.TUE=_tue || 1 || _ilenie+=1; 0 ?} ?}
    & {? _dsp=date(0,0,0) || 1 || {? _data=_dsp || 1 || _dspnie+=1; 0 ?} ?}
   || _tue:=__powzak.TUE;
      _refzak:=#__powzak.ref;
      __powzak.tr_set(1,,,1);
      __powzak.clear;
      __powzak.prefix(_refzak);
      {? __powzak.first
      || {!
         |? _ilezaz+=1;
            {? exec('zabl_nag','faktury_poz',_a,__powzak.RND)
            || exec('aktpozdk','faktury_poz');
               {? ~BEER.SP_POW
               || {? _a & __powzak.ILR>=0
                  || _ilr:=exec('ilakcpoz','faktury_poz',$FAP.ref,__powzak.ref);
                     _ilp:=_fapil-_ilr;
                     _ild:=__powzak.ILE-__powzak.ILP;
                     __powzak.ILR:={? _ilp>0 || {? _ilp>_ild || _ild || _ilp ?} || _czesc:=1; 0 ?} $ __powzak.DKL
                  |? ~_a & __powzak.ILR>0
                  || __powzak.ILR:=0
                  ?}
               || {? _a & __powzak.ILR>=0
                  || __powzak.ILR:=__powzak.ILE-__powzak.ILP $ __powzak.DKL
                  |? ~_a & __powzak.ILR>0
                  || __powzak.ILR:=0
                  ?}
               ?};
               __powzak.put(1)
            ?};
            __powzak.next
         !}
      ?}
   ?}
?};
__powzak.cntx_pop;
{? _czesc
|| {? _czesc<>_ilezaz
   || FUN.info('Uwaga. Zostały wybrane jedynie pozycje\ndo ilości wynikającej z ilości na pozycji faktury.'@)
   || FUN.info('Ilość z pozycji została już rozpisana.'@)
   ?}
?};
{? _ilenie>0
|| FUN.info('Uwaga. Część pozycji nie została wybrana ze względu na niezgodny rodzaj dokumentu - UE.'@)
?};
{? _dspnie>0
|| FUN.info('Uwaga. Część pozycji nie została wybrana ze względu na niezgodą datę wystawienia.'@)
?};
1


\prezdane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: zmienia sposob prezenatacji danych
::   WE: _a - 'I' wg indeksu 'D' - wg dokumentu
::  OLD: \prezdane/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
_acq:=__powzak.ndx_tmp('',0,'TREE',,0,'LAB',,0);
{? _a='I'
||
:: prezentacja wg indeksu materialowego
   _oldref:={? __powzak.TREE>0 || #__powzak.ref || 0 ?};
   _newref:=null;
   _ktms:=sql('select distinct :_a.KTM KTM from :_a where :_a.KTM<>\'\' order by 1',__powzak);
   _acr:=__powzak.ndx_tmp('',0,'POZ',,0,'KTM',,0,'KTM',,0);
   {? exec('FindInSet','#table','__powzak',_acr,'DP')=null || _newref:=__powzak.ref ?};
   _ktms.clear;
   {? _ktms.first
   || {!
      |? __powzak.index(_acr);
         __powzak.prefix('DP',_ktms.KTM,_ktms.KTM);
         {? __powzak.first
         || __powzak.get;
            __powzak.cntx_psh;
            __powzak.clear;
            __powzak.TREE:=0;
            __powzak.POZ:='IN';
            __powzak.LAB:=6-__powzak.LAB;
            __powzak.CEN:=0;
            __powzak.CWL:=0;
            __powzak.ILP:=__powzak.ILR:=__powzak.ILE:=0;
            __powzak.DKC:='';
            _korzen:={? __powzak.add(1) || #__powzak.ref || 0 ?};
            __powzak.cntx_pop;
            _ile:=0; _ilr:=0; _ilp:=0;
            {!
            |?
               __powzak.get;
               _czyold:={? _oldref<>0 || _oldref=#__powzak.ref || 0 ?};
               _etyk:=__powzak.LAB;
               _krop:=_etyk*'.';
               _etyk2:=__powzak.LA2;
               _starkor:=#__powzak.ref;
               __powzak.cntx_psh;
               __powzak.clear;
               __powzak.TREE:=_korzen;
               __powzak.LAB:=(_krop+_etyk)+' '+_etyk2;
               __powzak.LA2:=_etyk;
               __powzak.POZ:='IP';
               __powzak.SYM:=form(__powzak.DOK-16);
               __powzak.add(1);
               _nowykor:=#__powzak.ref;
               {? _czyold || _newref:=__powzak.ref ?};
               _ilp+=__powzak.ILP;
               _ile+=__powzak.ILE;
               _ilr+=__powzak.ILR;
               __powzak.index(_acq);
               __powzak.prefix(_starkor);
               {? __powzak.first
               || {!
                  |? _ref:=__powzak.ref; _ok:=__powzak.next;
                     __powzak.cntx_psh;
                     __powzak.clear;
                     {? __powzak.seek(_ref)
                     || __powzak.TREE:=_nowykor; __powzak.POZ:='IF'; __powzak.put
                     ?};
                     __powzak.cntx_pop;
                     _ok
                  !}
               ?};
               __powzak.cntx_pop;
               __powzak.del
            !};
            __powzak.clear;
            {? __powzak.seek(_korzen,)
            || __powzak.ILP:=_ilp $ __powzak.DKL;
               __powzak.ILE:=_ile $ __powzak.DKL;
               __powzak.ILR:=_ilr $ __powzak.DKL;
               __powzak.put(1)
            ?}
         ?};
         _ktms.next
      !};
      __powzak.index(_acr);
      __powzak.prefix('D');
      {? __powzak.first || {! |? __powzak.del !} ?}
   ?};
   __powzak.ndx_drop(_acr);
   obj_del(_ktms)
||
:: prezentacja wg dokumentow
   _oldref:={? __powzak.TREE>0 || #__powzak.ref || 0 ?};
   _newref:=null;
   _doki:=sql('select distinct :_a.DOK DOK from :_a where :_a.TREE<>0 order by 1',__powzak);
   _acr:=__powzak.ndx_tmp('',0,'POZ',,0,'DOK',,0,'DOK',,0);
   {? exec('FindInSet','#table','__powzak',_acr,'IP')=null || _newref:=__powzak.ref ?};
   _doki.clear;
   {? _doki.first
   || {!
      |? __powzak.index(_acr);
         __powzak.prefix('IP',_doki.DOK,_doki.DOK);
         {? __powzak.first
         || __powzak.get;
            __powzak.cntx_psh;
            __powzak.clear;
            __powzak.TREE:=0;
            __powzak.POZ:='DN';
            __powzak.SYM:=form(__powzak.DOK-16);
            __powzak.CEN:=0;
            __powzak.CWL:=0;
            __powzak.ILP:=__powzak.ILR:=__powzak.ILE:=0;
            __powzak.DKC:='';
            _krop:=__powzak.LAB*'.';
            __powzak.LAB:={? _krop || (_krop+1)-__powzak.LAB || __powzak.LAB ?};
            _korzen:={? __powzak.add(1) || #__powzak.ref || 0 ?};
            __powzak.cntx_pop;
            {!
            |?
               __powzak.get;
               _czyold:={? _oldref<>0 || _oldref=#__powzak.ref || 0 ?};
               _etyk:=__powzak.LAB;
               _krop:=_etyk*'.';
               _etyk2:=__powzak.LA2;
               _starkor:=#__powzak.ref;
               __powzak.cntx_psh;
               __powzak.clear;
               __powzak.TREE:=_korzen;
               __powzak.LAB:=_etyk2;
               __powzak.LA2:={? _krop || (_krop+1)-_etyk || _etyk ?};
               __powzak.SYM:=__powzak.KTM;
               __powzak.POZ:='DP';
               __powzak.add(1);
               _nowykor:=#__powzak.ref;
               {? _czyold || _newref:=__powzak.ref ?};
               __powzak.index(_acq);
               __powzak.prefix(_starkor);
               {? __powzak.first
               || {!
                  |? _ref:=__powzak.ref; _ok:=__powzak.next;
                     __powzak.cntx_psh;
                     __powzak.clear;
                     {? __powzak.seek(_ref)
                     || __powzak.TREE:=_nowykor; __powzak.POZ:='DF'; __powzak.put
                     ?};
                     __powzak.cntx_pop;
                     _ok
                  !}
               ?};
               __powzak.cntx_pop;
               __powzak.del
            !}
         ?};
         _doki.next
      !};
      __powzak.index(_acr);
      __powzak.prefix('I');
      {? __powzak.first || {! |? __powzak.del !} ?}
   ?};
   __powzak.ndx_drop(_acr);
   obj_del(_doki)
?};
__powzak.ndx_drop(_acq);
__powzak.clear;
__powzak.first;
{? _newref<>null || __powzak.seek(_newref) ?};
__powzak.hdr_sel();
__powzak.hdr_sel({? _a='D' || ' (prezentacja wg Dokumentów).'@ || ' (prezentacja wg Indeksów).'@ ?});
win_disp;
1


\odbl_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009+]
:: OPIS: odblokowanie wszystkich zablokowanych naglowkow ND
::  OLD: \odbl_all/zakupy.fml
::----------------------------------------------------------------------------------------------------------------------
__powzak.clear();
{? __powzak.first()
|| {!
   |? {? __powzak.TREE<>0 || exec('zabl_nag','faktury_poz',-1,__powzak.RND) ?};
      __powzak.next()
   !}
?}


\ATR_INZATR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: ikony dla ATR.INZATR
::----------------------------------------------------------------------------------------------------------------------
"{? ATR.INZATR='X'
 || ATR.INZATR:=''; 'xwin16.png:15'
 |? ATR.INZATR='N'
 || ATR.INZATR:=''; 'xwin16.png:56'
 |? ATR.INZATR='Y'
 || ATR.INZATR:=''; 'xwin16.png:102'
 || ATR.INZATR:=''; ''
 ?}"


\fap_projekty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.14]
:: OPIS: Projekty tabela FAP
::       Kontekst wywołania - rekord tabeli FAP
::----------------------------------------------------------------------------------------------------------------------
_projzakr:={? +FAKS.KZ || exec('FindAndGet','#table',FAKS,FAKS.KZ,,"FAKS.T().PROJZAKR",'') || FAKS.T().PROJZKH ?};
{? _projzakr=exec('projzakr_nie_dotyczy','projekty')
|| FUN.info('Typ dokumentu niezwiązany z projektem.'@
      +'\n'+'Opcja niedostępna.'@);
   return()
?};

exec('fap_proj_or_rem','faktury_poz','P');
~~


\fap_rem_zgl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Zgłoszenia remontowe tabela FAP
::       Kontekst wywołania - rekord tabeli FAP
::----------------------------------------------------------------------------------------------------------------------
exec('fap_proj_or_rem','faktury_poz','R');
~~


\fap_proj_or_rem
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [19.22]
:: OPIS: Projekty (PROJEKTY) albo zgłoszenia remontowe (REM_ZGL) tabela FAP
::   WE: _a - 'P'rojekty, zgłoszenia 'R'emontowe
::----------------------------------------------------------------------------------------------------------------------
_proj_or_rem:=_a;

REM_ZGL.win_dict('WERP');

PROJ2FAP.index('FAP');
PROJ2FAP.prefix('N',_proj_or_rem,FAP.ref());
{? _proj_or_rem='P'
|| {? FAKS.SZ='S'
   || _wer:='WERSP'
   || _wer:='WERZP'
   ?}
|| _wer:='WRRZP'
?};
PROJ2FAP.win_sel(_wer);

PROJ2FAP.fld_fml('IL','EDIT_FORMAT',"'in_prec='+$FAP.M().DOKL");
PROJ2FAP.fld_fml('IL','DISPLAY_FORMAT',"'out_prec='+$FAP.M().DOKL");
PROJ2FAP.fld_fml('W','EDIT_FORMAT',"'in_prec=2'");
PROJ2FAP.fld_fml('W','DISPLAY_FORMAT',"'out_prec=2'");

PROJ2FAP.win_fml(_wer,,'IL',,'SUM_FORMAT',"'out_prec='+$ST.DOKL");
PROJ2FAP.win_fml(_wer,,'W',,'SUM_FORMAT',"'out_prec=2'");

_var:=obj_new('SZABLON','PROJREM');
_var.SZABLON:='N';
_var.PROJREM:=_proj_or_rem;
params_set('TAB',FAP,'var',_var);
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag');
   return(0)
?};
_hid_act:='';
{? FAKS.PROJEKTY | FAKS.ZAK='T' || _hid_act:='dpu:dR' ?};
PROJ2FAP.select(,,,_hid_act);
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
~~


\enableFLD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: wyłączenie/uzupełnienie pól związanych z obiegami
::----------------------------------------------------------------------------------------------------------------------
_brt:=FAKS.T().FIS='T';
_brk:=1;
_zak:=0;
_netto:=0;
_brutto:=0;
{? FAKS.EDOKUM<>''
|| _brk:=0;
   _zak:=FAKS.SZ='Z';
   _netto:=exec('FindAndGet','#table',EDOKUM,FAKS.EDOKUM,,"NETTO",0);
   _brutto:=exec('FindAndGet','#table',EDOKUM,FAKS.EDOKUM,,"WART",0)
|? FAKS.T().KOR='T' & (_fakz:=exec('FindInSet','#table','FAKZ2FAP','FAKS',FAKS.ref(),,"FAKZ2FAP.FAKZ+16",,,''))<>''
|| _brk:=0;
   _netto:=exec('FindAndGet','#table',FAKZ,_fakz,,"WN",0);
   _brutto:=exec('FindAndGet','#table',FAKZ,_fakz,,"WB",0)
?};
FAKSZ.fld_fml('OBINET','DISPLAY_FORMAT',{? _brk || "'empty=1'"
                                        |? _zak || "'empty=0'"
                                        |? _brt || "'empty=1'" || "'empty=0'" ?});
FAKSZ.fld_fml('OBIBRT','DISPLAY_FORMAT',{? _brk || "'empty=1'"
                                        |? _zak || "'empty=0'"
                                        |? _brt || "'empty=0'" || "'empty=1'" ?});
FAKSZ.OBINET:=_netto;
FAKSZ.OBIBRT:=_brutto;
~~


\fap_oo_pw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Przed wyświetleniem FAP.OO
::   WE:
::   WY:
::  OLD: \fap_oo_pw/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
exec('fap_oo_pr','faktury_poz')


\fap_oo_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Przed redakcją FAP.OO
::   WE:
::   WY:
::  OLD: \fap_oo_pr/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
exec('oo_zak','faktury_poz')


\oo_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Sprawdza czy aktywne odwrotne obciążenie dla zakupu
::   WE:
::   WY: 0-nie, 1-tak
::  OLD: \oo_zak/faktury1.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.SZ='Z' & FAKS.WHERE<>'E'


\bd_fakszsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: przed redakcją FAKSZ.SV
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
fld(exec('FindAndGet','#table',SLO,__przet.REFVAT,,,null()))


\be_fakszsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: przed redakcją FAKSZ.SV
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: zapamiętanie wartosci pola przed redakcją
DPOZ.WPR_SLO:=exec('FindAndGet','#table',SLO,__przet.REFVAT,,,null());
exec('jakislsv','ustawienia')=''


\ae_fakszsv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: po redakcji FAKSZ.SV
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? fld()<>null()
||
   _sv:=fld();
   __przet.REFVAT:=$_sv;
   __przet.VAT:=
      {? __przet.REFVAT<>''
      || exec('FindAndGet','#table',SLO,__przet.REFVAT,,"KOD",'')
      || ''
      ?};
   1
?}


\fap_fld_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Przed wyświetleniem pola tabeli FAP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_afld:=cur_afld();
{? 'KPW WD RTRANSAK RTRANSPO IDKH'*_afld
||
   exec('findfnrd','color')
||
   ''
?}


\fap_fld_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Przed redakcją pola tabeli FAP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
0


\fap_fld_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: F3 pola tabeli FAP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
0


\fap_fld_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.42]
:: OPIS: Po redakcji pola tabeli FAP
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
1


\FAPwycDK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Wycena dokumentu magazynowego na podstawie poprawy pozycji
::----------------------------------------------------------------------------------------------------------------------
_dk2fce:=FUN.choice('Wyceniać powiązane dokumenty magazynowe'@,,'Wyceń zawsze'@,'Tylko niewycenione'@);
{? _dk2fce
||
   exec('ini_kom','#message','Wycena dokumentów magazynowych:'@);
   _kgr:=FAKS.SYM;
   __kom.set_root(_kgr);
   FAP2DK.index('NFAKS');
   FAP2DK.prefix($FAKS.ref,);
   {? FAP2DK.first
   ||
      {!
      |?
         {? FAP2DK.DK<>''
         ||
            ND.cntx_psh();
            FAKS_K.cntx_psh();
            DK.cntx_psh();
            FAP_K.cntx_psh();
            DK_C.cntx_psh();
            DK_LN.cntx_psh();
            DK_LI.cntx_psh();
            DK_L.cntx_psh();
            DK_HS.cntx_psh();
            PAL_POZ.cntx_psh();
            FAP.cntx_psh();
            exec('mag_open','open_tab',(1+(5-FAP2DK.DK)),(2+(6-FAP2DK.DK)));
            DK.prefix(); ND.prefix(); FAP.prefix();
            {? DK.seek(FAP2DK.DK,8+FAP2DK.DK)
               & ND.seek(DK.N)
               & FAP.seek(FAP2DK.FAP,8+FAP2DK.FAP)
               & FAP.M=DK.M
               & DK.M().RODZ='T'
               & DK.N().ZAK='N'
               & FAP.WAL=DK.WAL
               & (FAP.CN>0 | FAP.CWAL>0)
               & (_dk2fce=1 | (_dk2fce=2 & DK.C=0 ))
            ||
               _cen:=DK.C; _cwal:=DK.CWAL;
               exec('dk_edit','magdok_poz','P',FAP2DK.FAP);
               {? DK.get & {? FAP.CWAL>0 || DK.CWAL<>FAP.CWAL || DK.C<>FAP.CN ?}
               ||
                  _txt:='Nie zmieniono wyceny dla %1 pozycja : %2'@[DK.N().SYM,$DK.P];
                  exec('add_kom','#message',_txt,7,_kgr)
               ?}
            ||
               {? DK.seek(FAP2DK.DK,8+FAP2DK.DK)
               ||
                  _txt:='Nie wyceniono %1 pozycja : %2'@[DK.N().SYM,$DK.P];
                  {? FAP.M<>DK.M || _txt+=' - Różny towar na pozycjach'@ ?};
                  {? DK.N().ZAK='T' || _txt+=' - Dokument magazynowy zadekretowany'@ ?};
                  {? FAP.WAL<>DK.WAL || _txt+=' - Różne waluty na pozycjach'@ ?};
                  {? (_dk2fce=2 & DK.C<>0 ) || _txt+=' - Pozycja już wyceniona'@ ?};
                  exec('add_kom','#message',_txt,7,_kgr)
               ||
                  _txt:='Nie odnalazłem pozycji dokumentu magazynowego'@;
                  exec('add_kom','#message',_txt,7,_kgr)
               ?}
            ?};

            DK.r_unlock();
            ND.r_unlock();
            ND.cntx_pop();
            FAKS_K.cntx_pop();
            DK.cntx_pop();
            FAP_K.cntx_pop();
            DK_C.cntx_pop();
            DK_LN.cntx_pop();
            DK_LI.cntx_pop();
            DK_L.cntx_pop();
            DK_HS.cntx_pop();
            PAL_POZ.cntx_pop();
            FAP.cntx_pop()
         ?};
         FAP2DK.next()
      !}
   ?};
   exec('end_kom','#message');
   VAR_DEL.delete('__kom')
?};
~~


\fap_rekb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: przed rekord tabeli FAP
::   WE: _a - 0/1 - ostatni wyświetlany rekord
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| params_exec('fap_actions','faktury_poz')
?};
~~


\fap_dane_do_intrastat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.22]
:: OPIS: Dane do intrastat - pozycja dokumentu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
{? ~_r_lock
||
   exec('who_rlock_faks','faktury_nag')

|? exec('nie_dotyczy','intrastat','FAKS')
||
   ~~

|? FAP.M().RODZ='U'
||
   FUN.info('Funkcja niedostępna dla pozycji usługowej.'@)
||
   _env_fak_poz:=exec('env','faktury_poz');
   _env_fak_poz.POPRAW:='T';
   params_set('env_fak_poz',_env_fak_poz);
   _win_edit:=exec('fap_win_edit','faktury_poz',1);
   FAP.win_edit(_win_edit);
   exec('set_efld_opt','faktury_poz');
   {? FAKS.INTRA='T'
   ||
      __FAPDISP:=1;
      FAP.display();
      VAR_DEL.delete('__FAPDISP')
   ||
:: można zmieniać dane do intrastat do czasu uwzględnienia dokumentu w deklaracji
      exec('bd_mfap','faktury_poz');
      exec('bd_il','faktury_poz');
      {? FAKS.T().KOR='N' | FAP.POZP>0
      ||
:: dokument
:: lub dodana pozycja w korekcie
         {? FAP.edit("params_exec('chk_fakp','faktury_poz',0)")
         ||
            _result:=FAP.put()
         ?}

      |? FAKS.T().HIS='T'
      ||
:: korekta historyczna
         exec('korh_put','faktury_poz',,,1)
      ||
:: korekta
         exec('kor_poz','faktury_poz',,1)
      ?}
   ?};
   exec('fap_win_edel','faktury_poz',_win_edit)
?};
exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock);
_result


\fap_spwym_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.51]
:: OPIS: Przed redakcją FAP.SP_WYM
::       Na wypadek jeśli będzie potrzeba redakcji tego pola
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
FAKS.KOR='' & FAKS.WHERE='L' & FAP.M=null()


\fkor_spwym_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.51]
:: OPIS: Przed redakcją FKOR.SP_WYM
::       Na wypadek jeśli będzie potrzeba redakcji tego pola
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
0


\fill_fld2prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.42]
:: OPIS: uzupełnia, wykorzystywane na wydruku, pola w pozycji dokumentu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FAP.M
||
   M.cntx_psh();
   FAP.M();
   FAP.MX:=M.KTM;
   FAP.NX:=exec('fap_nx','faktury_poz');
   FAP.PKWIU_CN:=exec('pkwiu_cn','material',FAKS.DW,FAP.M);
   M.cntx_pop()
||
   FAP.MX:='';
   FAKS.cntx_psh();
   _where:=FAP.FAKS().WHERE;
   {? _where<>'L'
   ||
      FAP.NX:=''
   ?};
   FAKS.cntx_pop();
   FAP.PKWIU_CN:=''
?}


\addpoz_mwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [20.14]
:: OPIS: Dodaje pozycje dokumentu sprzedaży - używana w webserwisie
::   WE: _a - pozycje - tablica "buforów" tabeli FAP
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_poz:=_a;
_wsenv:=exec('wsenv','#mwapi');
_wyn:=0;

_mag:=$FAKS.MAG;
{! _it:=1.. obj_len(_poz)
|!
   _wyn:=exec('ADDPOZF','faktury_poz',
      _poz[_it].M,
      _poz[_it].IL,
      _poz[_it].CPR,
      _poz[_it].RAB,
      _poz[_it].SV,
      ,
      _poz[_it].UWAGI
   );
   {? ~_wyn || return(0) ?};
   {? _wyn & FAP.FAKS().WHERE='G' & FAP.M<>null & FAP.M().RODZ='T'
   || _magfap:={? Plugin.exists('FAP_MG_001') || Plugin.run('FAP_MG_001') || null()?};
      {? _mag<>'xxx' & _magfap<>null() & ($_magfap)<>($_mag) || _mag:='xxx' ?};
      VAR_DEL.delete('__XLS_VALID');
      __XLS_VALID:=obj_new('MSG');
      __XLS_VALID.MSG:='';
      _ile:=FAP.IL;
      FAP.IL:=0;
      FAP.J2:=FAP.JM;
      exec('f3_il','faktury_poz',_ile,_ile,-2,1,,,_magfap);
      _buf:=exec('oblwgfap','faktury_poz',FAP.ref());
      _ok:=(_buf[1]=_ile);
      obj_del(_buf);
      &_buf;
      {? _ok
      ||
         FAP.IL2:=_ile;
         DISP.ILF:=_ile;
         exec('ae_il','faktury_poz',0,_magfap)
      ||
         _wsenv.add_info('Nie dokonano rezerwacji dla materiału: %1'@[FAP.M().KTM])
      ?};
      VAR_DEL.delete('__XLS_VALID')
   ?}
!};
{? _mag='xxx' || FAKS.MAG:=null(); FAKS.put(1) ?};
1


\fap_ef_prvat_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Przed redakcją FAP.EF_PRVAT
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.PROC=null() | FAKS.KRAJ_VAT().KODISO='' | FAKS.KRAJ_VAT().KODISO='PL'
|| exec('jpk_slo_proc_vat','edi_fo_ksef')
|| 0
?}


\fap_ef_proc_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Przed redakcją FAP.EF_PROC
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.PROC=null() || exec('jpk_slo_proc_fap','edi_fo_ksef') || 0 ?}


\fap_nx
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Wyliczenie wartości dla pola FAP.NX (nazwa w pozycji dokumentu)
::   WE:
::   WY: wartość dla FAP.NX
::----------------------------------------------------------------------------------------------------------------------
_nx:='';
{? FAP.M<>null()
|| {? FAKS.SZ='S' & +FAP.M().N_F
   || _nx:=FAP.M().N_F
   || _nx:=FAP.M().N
   ?}
?};
_nx


\dsp_wpoz_enable
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Data sprzedaży w pozycji włączona
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
(FAKS.WHERE='P' | FAKS.WHERE='M' | FAKS.WHERE='Z' | FAKS.WHERE='E') & exec('x00138','faktury_wspolne',_a)<>'N'


\fap_d_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Przed redakcji pola FAP.D
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_D:=FAP.D;
{? params_exec('dsp_wpoz_enable','faktury_poz',FAKS.SZ) || ~params_exec('fap_pow_z_dk','faktury_poz') ?}


\fap_d_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Po redakcji pola FAP.D
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? DPOZ.WPR_D<>fld() & FAKS.WAL<>FAKS.NWAL
||
   {? fld()=date(0,0,0)
   ||
      FAP.SWAL:=2;
      FAP.RTK:=0;
      FAP.NTK:='';
      FAP.DTK:=date(0,0,0);
      FAP.BTK:='';
      FAP.KRS:=FAKS.KRS

   |? FUN.ask('Czy wyznaczyć kurs wg daty %1?'@[form(FAP.D)])
   ||
      FAP.SWAL:=2;
      FAP.RTK:=0;
      FAP.NTK:='';
      FAP.DTK:=date(0,0,0);
      FAP.BTK:='';
      FAP.KRS:=0;
      exec('pr_krs','eurusd',FAP,'KRS')
   ?};
   params_exec('po_cenaw','ceny')
?};
1


\fap_krs_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Powołuje obiekt z informacjami o kursie w pozycji faktury
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
obj_new('FAKS','POZ','BKRS','KRS','SWAL','RTK','NTK','DTK','BTK','DSP_WPOZ')


\fap_krs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Zwraca obiekt z informacjami o kursie w pozycji _b faktury _a
::   WE: _a - FAKS.ref()
::       _b - FAP.POZ
::       _c - exec('fap_krs_obj','faktury_poz')
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:={? var_pres('_c')=type_of(obj_new('obj')) || _c || exec('fap_krs_obj','faktury_poz') ?};
_result.FAKS:=_a;
_result.POZ:=_b;
_result.BKRS:=_result.KRS:=_result.SWAL:=_result.RTK:=0;
_result.NTK:=_result.BTK:='';
_result.DTK:=date(0,0,0);

exec('FindAndGet','#table',FAKS,_a,,"
   _result:=_b;
   FAP.cntx_psh();
   FAP.use(gsub(FAP.name(1),'???',FAKS.name()+3));
   FAP.cntx_psh();
   FAP.index('FAP');
   FAP.prefix(FAKS.ref(),FAP.POZ);
   {? FAP.first()
   ||
      _result.BKRS:=FAP.BKRS;
      _result.KRS:=FAP.KRS;
      _result.SWAL:=FAP.SWAL;
      _result.RTK:=FAP.RTK;
      _result.NTK:=FAP.NTK;
      _result.DTK:=FAP.DTK;
      _result.BTK:=FAP.BTK
   ?};
   FAP.cntx_pop();
   FAP.cntx_pop()
",,_result);
_result


\env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Środowisko pozycji faktury
::   WE:
::   WY: objekt środowiska
::----------------------------------------------------------------------------------------------------------------------
_env:=obj_new('POPRAW');
_env.POPRAW:='N';
_env


\fap_pow_z_dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Czy FAP powiązany z DK?
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
{? params_get().env_fak_poz.POPRAW='T'
||
   FAP2DK.cntx_psh();
   FAP2DK.index('FAPDK');
   FAP2DK.prefix($FAP.ref());
   _result:=FAP2DK.first();
   FAP2DK.cntx_pop()
?};
_result

:Sign Version 2.0 jowisz:1045 2024/02/20 14:59:52 d3b9656c6e68a97ff4bf7acaa4c57043392cbc579791b2a11a383ec12278f7ea0cc190e53aac9f5410852025f7b0817bbff04b952dac5a5005c7fac667842d562e31d0fe416c1f39ed5aeb9462a6381f864790376f09e16a174d2b5c93691546b1c26df0985dfa1d729a94c18f533caeb6f1d51158cf903f634dab9978e4b70f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ppk_xra.fml [12.51]
:: Utworzony: 2019/05/08
:: Autor: jaws
:: Systemy: PPK
::======================================================================================================================
:: Zawartość: Formuły odpowiedzialne za obsługę listy raportów.
::======================================================================================================================


\_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Przed dołączeniem lub poprawieniem wiersza tabeli PPK_XRA.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
:: obsługa anomalii
PPK_XEL.cntx_psh();
PPK_XRA.OPIS:=PPK_XRA.PPK_XEL().OPIS;
PPK_XEL.cntx_pop();
1


\_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli PPK_XRA.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_xra')


\_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli PPK_XRA.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','ppk_xra')


\wnd_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: "Okienko przed" wszystkich okienek tabeli PPK_XRA.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
PPK_XEL.f_set('OPIS',,'REFERENCE in (select Q.PPK_XEL from PPK_XRA as Q where Q.PPK_XWE=:_a)',exec('ref','ppk_xwe'));
1


\wnd_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: "Okienko po" wszystkich okienek tabeli PPK_XRA.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
PPK_XEL.f_clear();
1


\szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Wyszukuje informacje o raporcie.
::   WE: _a _PPK_XWE - wersja
::       _b _PPK_XEL - element
::   WY: wskazanie istniejącego zapisu lub null
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_xwe:=_a;
_xel:=_b;

PPK_XRA.index('UNIQUE');
PPK_XRA.prefix(_xwe);
{? PPK_XRA.find_key(_xel)
|| PPK_XRA.ref()
|| null
?}


\dodaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [12.51]
:: OPIS: Dodaje informacje o raporcie.
::   WE: _a _PPK_XWE - wersja
::       _b _PPK_XEL - element
::   WY: wskazanie dodanego/istniejącego zapisu lub null
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_xwe:=_a;
_xel:=_b;

{? _xwe=null | _xel=null
:: zakończ działanie
|| return(null)
?};

:: sprawdź, czy element już istnieje
_ref:=exec('szukaj','ppk_xra',_xwe,_xel);
{? _ref<>null
:: znaleziono
|| return(_ref)
?};

PPK_XRA.blank();
PPK_XRA.PPK_XWE:=_xwe;
PPK_XRA.PPK_XEL:=_xel;
{? PPK_XRA.add()
|| _ref:=PPK_XRA.ref()
?};

_ref

:Sign Version 2.0 jowisz:1028 2019/10/14 09:21:32 0ea9ab67e4dba35ae1ea0852066b70c24d99ad00d17921679d24c517bd00b3c8974960c9cce118467dc66936f5f5b5a4061d5eca8f1a07de4d50409a28c851f46e99d93ea43ecc0a2208eeb09aceaed2c75c008e13f4b83b27152989164ce5de1396ba2150b65b371d525be2420661247a3fe864646c753eaf56847967628bbd

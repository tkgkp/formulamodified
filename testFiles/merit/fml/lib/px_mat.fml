:!UTF-8
::(c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: px_mat.fml [12.30]
:: Utworzony: 2012/11/06
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa surowców do przepisów planistycznych - plan strategiczny
::======================================================================================================================


\PX_MAT_edt_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Ustala i ustawia okienko edycyjne dla podanego PX_MAT
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
PX_MAT.win_edit('RED');
1


\PX_MAT_a_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: po rekord dla PX_MAT
::   WY: 1/0/fld
::----------------------------------------------------------------------------------------------------------------------
{? PX_MAT.M=null()
|| FUN.info('Indeks surowca nie został podany.'@);
   'M'
|? PX_MAT.IL=0
|| FUN.emsg('Należy wskazać ilość surowca.'@);
   'IL'
||
   PX_MAT.IL:=PX_MAT.IL$PX_MAT.M().DOKL;
   1
?}


\PX_MAT_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Dodanie PX_MAT z poziomu PxTex.MaterTab
::   WE: _a - PX_STAGE.ref()
::   WY: PX_MAT.add() / 0
::----------------------------------------------------------------------------------------------------------------------
_PX_STAGE:=_a;

{? _PX_STAGE=null()
||
   FUN.info('Nie można dołączyć surowca bez zdefiniowanego etapu.'@);
   0
||
   M.cntx_psh();
   {? exec('get','#params',500740,2)='N'
   || exec('filter_clear','material')
   || exec('filter','material','PS')
   ?};
   exec('slo_m_ok','material','T',1,,'W');
   exec('PX_MAT_edt_set','px_mat');
   M.f_clear();
   PX_MAT.blank();
   PX_MAT.PX_STAGE:=_PX_STAGE;

   {? PX_MAT.edit("exec('PX_MAT_a_rec','px_mat')")
   || PX_MAT.add()
   || 0
   ?};
   exec('filter_clear','material');
   M.cntx_pop()
?}


\PX_MAT_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Modyfikacja PX_MAT z poziomu PxTex.MaterTab
::   WY: PX_MAT.put() / 0
::----------------------------------------------------------------------------------------------------------------------
exec('PX_MAT_edt_set','px_mat');
M.f_clear();
{? PX_MAT.edit("exec('PX_MAT_a_rec','px_mat')")
|| PX_MAT.put()
|| 0
?}


\PX_MAT_delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Usuwanie PX_MAT z poziomu PxTex.MaterTab
::   WY: PX_MAT.del(,1) / 0
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć wskazany surowiec?'@)
|| exec('PX_MAT_del','px_mat',PX_MAT.ref())
|| 0
?}


\PX_MAT_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Podgląd PX_MAT z poziomu PxTex.MaterTab
::----------------------------------------------------------------------------------------------------------------------
exec('PX_MAT_edt_set','px_mat');

PX_MAT.display();
~~


\prefix_tmat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Prefiksuje tabelę PX_MAT (.MaterTab) danym PX_STAGE
::   WE: _a - PX_STAGE.ref()
::----------------------------------------------------------------------------------------------------------------------
_pxstage:=_a;

PX_MAT.index('UNIQALL');
PX_MAT.prefix(_pxstage);
~~


\import_tmat_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Tablica parametrów dla exec('import_tmat','px_mat')
::   WY: _args
::----------------------------------------------------------------------------------------------------------------------
_args:=obj_new('TKTL','TOPER','SurKO','PX_STAGE','Polfabr','Qcoef');
:: Źródłowa TKTL
_args.TKTL:=null();
:: Źródłowa TOPER
_args.TOPER:=null();
:: Surowce do karty (K), czy do operacji (O)
_args.SurKO:='K';
:: Docelowy etap planistyczny
_args.PX_STAGE:=null();
:: Importować półfabrykaty
_args.Polfabr:=1;
:: Współczynnik przeliczeniowy
_args.Qcoef:=1;
:: zwracamy tablicę
_args


\import_tmat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Import PX_MAT z TMAT
::   WE: _args - wynik formuły exec('import_tmat_a','px_mat')
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_args:={? _=1 || _a || exec('import_tmat_a','px_mat') ?};

TMAT.cntx_psh();
{? _args.SurKO='O'
||
   TMAT.index('ANNL');
   TMAT.prefix('T',_args.TKTL,_args.TOPER)
||
   TMAT.index('ANNL');
   TMAT.prefix('T',_args.TKTL,null())
?};

{? TMAT.first()
|| {!
   |?
      {? _args.Polfabr | (TMAT.KTL=null() & TMAT.DFLT_KTL<>'T') | TMAT.MAG='T'
      ||
         PX_MAT.index('UNIQALL');
         _material:={? TMAT.GRKTM='K' || TMAT.PT || TMAT.TGDFLT().PT ?};
         PX_MAT.prefix(_args.PX_STAGE,_material);
         {? PX_MAT.first()
         ||
            PX_MAT.IL+={? TMAT.SO='S' || TMAT.WARB || -TMAT.WARB ?}*_args.Qcoef;
            PX_MAT.put()
         ||
            PX_MAT.blank(1);
            PX_MAT.PX_STAGE:=_args.PX_STAGE;
            PX_MAT.M:=_material;
            PX_MAT.IL:={? TMAT.SO='S' || TMAT.WARB || -TMAT.WARB ?}*_args.Qcoef;
            PX_MAT.add()
         ?}
      ?};
      TMAT.next()
   !}
?};
TMAT.cntx_pop();
~~


\import_zlim_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Tablica parametrów dla exec('import_zlim','px_mat')
::   WY: _args
::----------------------------------------------------------------------------------------------------------------------
_args:=obj_new('ZGP','PX_STAGE','ZL');
:: pozycja przewodnika
_args.ZGP:=null();
:: etap planistyczny
_args.PX_STAGE:=null();
:: zlecenie
_args.ZL:=null();
:: zwracamy tablicę
_args


\import_zlim
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Import PX_MAT ze ZLIM
::   WE: _args - wynik formuły exec('import_zlim_a','px_mat')
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_args:={? _=1 || _a || exec('import_zlim_a','px_mat') ?};

ZLIM.cntx_psh();
exec('openmask','zl_common',_args.ZL);
ZLIM.index('ZGP_M');
ZLIM.prefix(_args.ZGP);

{? ZLIM.first()
|| {!
   |?
      PX_MAT.index('UNIQALL');
      PX_MAT.prefix(_args.PX_STAGE,ZLIM.KTM);
      {? PX_MAT.first()
      ||
         PX_MAT.IL+={? ZLIM.SO='S' || ZLIM.LIL || -ZLIM.LIL ?};
         PX_MAT.put()
      ||
         PX_MAT.blank(1);
         PX_MAT.PX_STAGE:=_args.PX_STAGE;
         PX_MAT.M:=ZLIM.KTM;
         PX_MAT.IL:={? ZLIM.SO='S' || ZLIM.LIL || -ZLIM.LIL ?};
         PX_MAT.ZL:=_args.ZL;
         PX_MAT.ZLIM:=ZLIM.ref();
         PX_MAT.add()
      ?};
      ZLIM.next()
   !}
?};
ZLIM.cntx_pop();
~~


\PX_MAT_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Kasuje wskazany PX_MAT
::   WE: _a - PX_MAT.ref()
::   WY: wynik PX_MAT.del(,1)
::----------------------------------------------------------------------------------------------------------------------
_PX_MAT:=_a;
exec('FindAndGet','#table',PX_MAT,#_PX_MAT,ref_name(_PX_MAT),"del(,1)",0,0)


\pxm_analiza_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Analiza dostępności materiałów w czasie - akcja 'przed' dla tabeli PX_MAT
::       Jeżeli przepis pochodzi ze zlecenia, to w analizie wyłączane są zapisy dotyczące limitów
::----------------------------------------------------------------------------------------------------------------------
{? exec('is_pdbuf','plan_dostaw')
||

   {? PX_MAT.sel_size()=0
   ||
      exec('env_create','plan_dostaw')
   ?};

   _xjm:={? PX_MAT.PX_STAGE().PX_TEX<>null()
         || PX_STAGE.PX_TEX().XJM
         || exec('FindAndGet','#table',TKTL,PX_STAGE.TKTL,,"XJM",1)
         ?};

   {? PX_MAT.sel_size()=0
   ||
      _dalej:=
         {? VAR.A_ZLEC=null()
         || {? exec('env_edit_il','plan_dostaw',_xjm)
            || 1
            || 0
            ?}
         || 1
         ?}
   ||
      _dalej:=1
   ?};

   {? _dalej
   ||
      {? VAR.A_ZLEC=null()
      || _coef:=exec('env_il','plan_dostaw')/_xjm
      || _coef:=VAR.A_ZLEC().IL/_xjm
      ?};
      _dokl:=PX_MAT.M().DOKL;

      _args:=exec('pda_a','plan_dostaw');
      _args.M:=PX_MAT.M;
      _args.TM_STAMP:=SYSLOG.tm_stamp();
::      _args.DK:=
::      _args.TK:=
      _args.IL:={? _dokl=0 || ceil(PX_MAT.IL*_coef) || (PX_MAT.IL*_coef)$_dokl ?};
      _args.DW:=exec('env_date','plan_dostaw');
      _args.TW:=exec('env_time','plan_dostaw');
      _args.DISP:=0;
      ZGP.index('PX_STAGE');
      ZGP.prefix(PX_MAT.PX_STAGE);
      {? ZGP.first()
      || exec('openmask','zl_common',ZGP.ZL);
         {!
         |? ZLIM.index('ZGP_M');
            ZLIM.prefix(ZGP.ref(),PX_MAT.M);
            {? ZLIM.first()
            || {!
               |? _args.skip_add($ZLIM.ref());
                  _zk_p:=exec('ZK_P_4_ZLIM','zamsiw_limit');
                  {? _zk_p<>null() || _args.skip_add($_zk_p) ?};
                  ZLIM.next()
               !}
            ?};
            ZGP.next()
         !}
      ?};
      _used:=exec('env_used_sum','plan_dostaw',_args.M);
      {? _used>0 || _args.insert_add($_args.M,_args.DW,_args.TW,_used) ?};
      _res:=exec('pda','plan_dostaw',_args);

      _argse:=exec('an_add_a','plan_dostaw');
      _argse.M:=_args.M;
      _argse.IL:=_args.IL;
      _argse.DW:=_args.DW;
      _argse.TW:=_args.TW;
      _argse.DOSTEPNY:=_res.DOSTEPNY;
      _argse.IL_DOST:=_res.IL_DOST;
      _argse.DM:=_res.NA_KIEDY;
      ZGP.index('PX_STAGE');
      ZGP.prefix(PX_MAT.PX_STAGE);
      {? ZGP.first()
      || {!
         |? ZLIM.index('ZGP_M');
            ZLIM.prefix(ZGP.ref(),PX_MAT.M);
            {? ZLIM.first()
            || {!
               |? _argse.skip_add($ZLIM.ref());
                  _zk_p:=exec('ZK_P_4_ZLIM','zamsiw_limit');
                  {? _zk_p<>null() || _argse.skip_add($_zk_p) ?};
                  ZLIM.next()
               !}
            ?};
            ZGP.next()
         !}
      ?};
      {? _used>0 || _argse.insert_add($_args.M,_args.DW,_args.TW,_used) ?};
      exec('an_add','plan_dostaw',_argse);
      exec('env_used_add','plan_dostaw',_args.M,_args.DW,_args.TW,_args.IL);

      {? PX_MAT.sel_size()=0
      ||
         exec('an_select','plan_dostaw')
      ?}
   ?};

   {? PX_MAT.sel_size()=0
   ||
      exec('env_delete','plan_dostaw')
   ?}
?};
~~


\pxm_analiza_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Analiza dostępności materiałów w czasie - akcja 'po' dla tabeli PX_MAT
::----------------------------------------------------------------------------------------------------------------------
~~


\pxm_analiza_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Analiza dostępności materiałów w czasie - akcja 'przed grupa' dla tabeli PX_MAT
::----------------------------------------------------------------------------------------------------------------------
{? exec('is_pdbuf','plan_dostaw')
||
   exec('env_create','plan_dostaw');
   {? VAR.A_ZLEC=null()
   ||
      _xjm:={? PX_MAT.PX_STAGE().PX_TEX<>null()
            || PX_STAGE.PX_TEX().XJM
            || exec('FindAndGet','#table',TKTL,PX_STAGE.TKTL,,"XJM",1)
            ?};
      {? exec('env_edit_il','plan_dostaw',_xjm)
      || 1
      || exec('env_delete','plan_dostaw');
         0
      ?}
   || 1
   ?}
|| 0
?}


\pxm_analiza_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Analiza dostępności materiałów w czasie - akcja 'po grupie' dla tabeli PX_MAT
::----------------------------------------------------------------------------------------------------------------------
exec('an_select','plan_dostaw');
exec('env_delete','plan_dostaw')


\win_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Tworzy okno selekcji tabeli PX_MAT
::   WE: _a - czy akcje redagowania (1/0)
::       _b - identyfikator okna
::       Nie jest sprawdzana obecność ani typ argumentów
::----------------------------------------------------------------------------------------------------------------------
_edit:=_a;
_win_id:=_b;
_tab:=PX_MAT;

_wer:=_tab.mk_sel('Surowce'@,'P',0,_win_id,1,1,,,'U');
_tab.win_fld(_wer,,'M','KTM',,20,,,'Indeks materiału'@,,'Indeks materiału'@);
_tab.win_fld(_wer,,'M','N',,50,,,'Nazwa materiału'@,,'Nazwa materiału'@);
_tab.win_fld(_wer,,'IL',,,15,ST.DOKL,,'Ilość'@,,'Ilość materiału'@);
_tab.win_fld(_wer,M,'J','KOD',,,,,'jm'@,,'Jednostka miary'@);

{? _edit
||
   _fb:="exec('PX_MAT_add','px_mat',PX_VAR.PX_STAGE)";
   _tab.win_act(_wer,0,'Formuła','Dołącz'@@,,,_fb,,,,,,'D');
   _tab.win_act(_wer,1,'Formuła','Dołącz'@@,,,_fb,,1,,,,'D');
   _tab.win_act(_wer,,'Formuła','Popraw'@@,,,"exec('PX_MAT_edit','px_mat')",,1,,,,'P');
   _tab.win_act(_wer,,'Formuła','Usuń'@@,,,"exec('PX_MAT_delete','px_mat')",,,,,,'U')
?};
_fb:="exec('info_zam','magazyn_stan',5,PX_MAT.M)";
_tab.win_act(_wer,0,'Formuła','&Szczegóły'@@,,,_fb,,1,,,,'S');
_btn:=_tab.win_btn(_wer,'text=%1'['&Szczegóły'@],'menu:S',,,,,,'noempty');
_tab.btn_sopt(_wer,_btn,'tooltip='+'Szczegóły indeksu'@);

_fb:="exec('pxm_analiza_b','px_mat')";
_fa:="exec('pxm_analiza_a','px_mat')";
_gr1:="exec('pxm_analiza_bg','px_mat')";
_gr2:="exec('pxm_analiza_ag','px_mat')";
_tab.win_act(_wer,0,'Formuła','&Analiza'@@,,,_fb,_fa,,1,_gr1,_gr2,'A');
_btn:=_tab.win_btn(_wer,'text=%1'['&Analiza'@],'menu:A',,,,,,'noempty');
_tab.btn_sopt(_wer,_btn,'tooltip='+'Analiza dostępności materiałów w czasie'@);

_tab.win_act(_wer,,'Wyświetl',,,,"exec('PX_MAT_view','px_mat')");

_tab.win_sel(_wer);
_wer


\is_mat_pxstag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Sprawdza, czy są zdefiniowane surowce do PX_STAGE
::   WE: _a - PX_STAGE.ref()
::       Nie jest spawdzana obecność ani typ argumentów
::   WY: 0 / 1
::----------------------------------------------------------------------------------------------------------------------
_px_stage:=_a;

PX_MAT.index('UNIQALL');
PX_MAT.prefix(_px_stage);
PX_MAT.first()


\ico_px_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Ikona symbolizująca materiał na pozycjach planu
::----------------------------------------------------------------------------------------------------------------------
{? exec('is_zlim_ploper','zl_limit',PX_POZ.PL_OPER)
|| 'xwin16.png:68'
|? exec('is_zlim_pxstag','zl_limit',PX_POZ.PX_STAGE)
|| 'xwin16.png:67'
|? _empty:=0;
   PX_POZ.cntx_psh();
   {? ~PX_POZ.first() || _empty:=1 ?};
   PX_POZ.cntx_pop();
   {? _empty || 0 || _res:=exec('is_sur_pxpoz','px_sur',PX_POZ.ref()) ?}
|| 'xwin16.png:71'
|| 'xwin16.png:110'
?}


\px_mat_fld_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Po redagowaniu pól w tabeli PX_MAT
::----------------------------------------------------------------------------------------------------------------------
1


\px_mat_fld_fd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Format wyświetlania pól w tabeli PX_MAT
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='IL'
|| 'out_prec='+
   {? (2+cur_kwin())='e_'
   || {? PX_MAT.M || $PX_MAT.M().DOKL || $4 ?}
   || $ST.DOKL
   ?}
|| ''
?}


\px_mat_fld_fe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [12.30]
:: OPIS: Format redagowania pól w tabeli PX_MAT
::----------------------------------------------------------------------------------------------------------------------
{? cur_afld()='IL'
|| 'in_prec='+{? PX_MAT.M || $PX_MAT.M().DOKL || $4 ?}
|| ''
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 3df49906e338ab1d971607cd89cee0de0b0b61bf415d3d2c5178d5266445001d163b4366fb1e16ca9a9464a26704283e73cacfa8926529790f666131ed1ad079caf0676084a0c6695466ecebcfa6edbb30e7cb04ecec5e9b49791735ae8c9c31d1e537371cbcd6424378b13f6c7b09a81d1b6086cbce76bdc0e66a3d5051852c

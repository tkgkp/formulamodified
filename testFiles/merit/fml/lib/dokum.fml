:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: dokum.fml
:: Utworzony: 20.03.2015
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa załączników
::======================================================================================================================


\deladok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: usuwa dokumenty powiazane z rekordem
::   WE:  _a  akronim tabeli
::   WY: czy udalo sie usunac dokumenty powiazane z rekordem
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
DOKUM.cntx_psh();
DOKUM.index('DOKUM');
DOKUM.win_sel('GR');
POLA.REFSQL:=$($(_a+'.ref()'))();
DOKUM.prefix(REF.FIRMA,POLA.REFSQL);
{? DOKUM.first()
||
   {!
   |?
      _wyn:=exec('del_dok','dokum',1);
      DOKUM.first() & _wyn=1
   !}
?};
_wyn:=~DOKUM.first();
DOKUM.cntx_pop();
_wyn


\del_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: usuniecie zapisu z bazy
::   WE: [_a] czy wyswietlac komunikaty 0 - tak / 1 - nie
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};

{? ~DOKUM.get() || return(0) ?};

{? DOKUM.FIRMA<>REF.FIRMA
|| {? _a=0 || FUN.info('Kontakt wprowadzony w innej firmie niż bieżąca. Akcja niemożliwa.'@) ?};
   return(0)
?};

{? ~DOKUM.r_lock(1,1,1)
|| {? _a=0 || FUN.info('Pozycja zablokowana przez innego użytkownika.\nUsunięcie pozycji niemożliwe.'@) ?};
   return(0)
?};

:: sprawdzanie czy dokument nie byl podpisany
_wyn:=0;
_cer:=1;

{? DOKUM.EDOKUMR<>''
|| FUN.info('Dla kontaktu został wcześniej uruchomiony obieg.'@);
   DOKUM.r_unlock();
   return(0)
?};

{? DOKUM.K_NAD='T'
|| FUN.info('Kontakt znajduje się w książce nadawczej.'@);
   DOKUM.r_unlock();
   return(0)
?};

::_refx:=ref_tab(DOKUM.REFSQL);
::{? type_of(_refx)<>type_of(~~) & _refx=FAKS
::|| _zak:=exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"ZAK",'N');
::   {? _zak='T'
::   || FUN.info('Dokument zaksięgowany.'@);
::      DOKUM.r_unlock();
::      return(0)
::   ?}
::?};

{? DOKUM.E_DOKUM<>null()
||
   {? _a=0
   || _cer:=FUN.ask('Dokument został podpisany.\nCzy na pewno usunąć dokument?'@)
   || _cer:=0
   ?}
?};

:: usuwanie powiązanych informacji dodatkowych
EDOK_ATR.cntx_psh();
EDOK_ATR.use('edokad__');
EDOK_ATR.index('REKKOLED'); EDOK_ATR.prefix(null(),DOKUM.ref(),DOKUM.ONEID,);
{? EDOK_ATR.first() || {! |? EDOK_ATR.del() !} ?};
EDOK_ATR.cntx_pop();

{? DOKUM.RODZAJ_K='W' & (DOKUM.ESEND='T' | DOKUM.WO='T')
|| _w_p:=exec('chk_sent','#mailbox',DOKUM.ref());
   {? _w_p<>''
   || {? _a=0
      || _upr:=exec('get','#params',7000,2,OPERATOR.USER)='T';
         FUN.info(
            'Załącznik tego dokumentu znajduje się w skrzynce poczty: \'%1\'.'@
            [exec('box_name','#mailbox',_w_p)]+'\n\n'+'Przed usunięciem dokumentu należy:'@+'\n'+
            '- usunąć załącznik ze wskazanej skrzynki pocztowej'+'\n'+
            {? _upr
            || 'albo:'+'\n'+
               '- odblokować znacznik wysłania (oznaczyć dokument jako niewysłany)'@
            || ''
            ?}
         )
      ?};
      DOKUM.r_unlock();
      return(0)
   ?}
|? DOKUM.RODZAJ_K='W' & DOKUM.RECEIVED='T' & DOKUM.BLC_KIND='' & DOKUM.KSEFSTAT<>'READY2GO'
|| {? _a=0
   || FUN.info('Załącznik tego dokumentu został odebrany w Businesslink.'@)
   ?};
   DOKUM.r_unlock();
   return(0)
|? DOKUM.RODZAJ_K='W' & DOKUM.BSEND<>'N' & (5+DOKUM.KSEFSTAT)<>'ERROR' & DOKUM.KSEFSTAT<>'NOTREADY'
   & DOKUM.KSEFSTAT<>'READY2GO'
|| {? _a=0
   || FUN.info('Załącznik tego dokumentu został wysłany do Businesslink.'@)
   ?};
   DOKUM.r_unlock();
   return(0)
?};

_w_p:='';
DOKUMP.cntx_psh();
DOKUMP.index('DISP_R');
DOKUMP.prefix('M',DOKUM.ref());
{? DOKUMP.first()
|| {!
   |? {? DOKUMP.ESEND='T' | DOKUMP.WO='T'
      || _w_p:=exec('chk_sent','#mailbox',DOKUMP.ref())
      ?};
      _w_p<>'S' & DOKUMP.next()
   !}
?};
DOKUMP.cntx_pop();
{? _w_p='S'
|| {? _a=0
   || FUN.info('Powiadomienie o tym kontakcie zostało wysłane mailem.'@)
   ?};
   DOKUM.r_unlock();
   return(0)
|? _w_p<>''
|| {? _a=0
   || FUN.info('Powiadomienie o tym kontakcie zostało przekazane do wysyłki mailem.\nUsuń mail wychodzący.'@)
   ?};
   DOKUM.r_unlock();
   return(0)
?};

_w_p:='';
DOKUMP.cntx_psh();
DOKUMP.index('DISP_R');
DOKUMP.prefix('K',DOKUM.ref());
{? DOKUMP.first()
|| _w_p:='K'
?};
DOKUMP.cntx_pop();
{? _w_p='K'
|| {? _a=0
   || FUN.info('Ten kontakt ma przypomnienie.'@)
   ?};
   DOKUM.r_unlock();
   return(0)
?};

::Usuwanie utworzonego automatycznie załącznika ksef
{? BPMN.SYM_DOM='FKS' & ref_tab(DOKUM.REFSQL)=FAKS & _a=1
|| DOKUM.r_unlock(); return(exec('del_dokumz_ksef','dokum'))
?};

{? _cer
|| _dokum:=DOKUM.DOKUM<>null();
   _kontr:=DOKUM.KH<>null();
   {? {? _a=0
      || {? _dokum & _kontr=0
         || FUN.ask('Istnieje plik zapisany na serwerze:\n%1\nUsunąć plik?'@[DOKUM.NAZWA])
         || {? _kontr & DOKUM.TYP='D'
            || {? DOKUM.BL='T' & DOKUM.RODZAJ_K='P' & DOKUM.REFSQL<>''
               || {? DOKUM.DG_UID<>''
                  || FUN.ask('Załącznik jest powiązanym dokumentem Businesslink.\n'+
                     'Powiązanie z nadrzędnym dokumentem Businesslink zostanie usunięte.\n'
                     'Czy usunąć załącznik?'@)
                  |? ref_tab(DOKUM.REFSQL)=ZK_N
                  || FUN.ask('Załącznik jest dokumentem Businesslink, na podstawie którego zarejestrowano zamówienie.\n'+
                     'Czy usunąć załącznik?'@)
                  || FUN.ask('Załącznik jest dokumentem Businesslink, na podstawie którego zarejestrowano dokument.\n'+
                     'Czy usunąć załącznik?'@)
                  ?}
               || FUN.ask('Usunąć kontakt / załącznik?'@)
               ?}
            |? _kontr
            || FUN.ask('Usunąć kontakt?'@)
            || 1
            ?}
         ?}
      || _a
      ?}
   ||
      {? DOKUM.BL='T' & DOKUM.RODZAJ_K='P'
            |
         DOKUM.RODZAJ_K='W' & DOKUM.BL_STAT=exec('cancelled_erp','zbl_stat')
      ||
         _wyn:=exec('bl_dokum_unlink','zbl_dok',DOKUM.ref(),DOKUM.RODZAJ_K='P')
      ||
         _wyn:=exec('del','dokum',,1,DOKUM.ref(),'oldnumer')>0
      ?}
   ?}
?};
DOKUM.r_unlock();
_wyn


\save_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: zapisuje dokument oraz usuwa pliki przetwarzane
::   WE:  _a  - akronim tabeli
::        _b  - nazwa wydruku wraz z rozszerzeniem
::       [_c] - nazwa pod ktora ma zostac zapisany dokument
::       [_d] - symbol dokumentu
::       [_e] - e-mail
::       [_f] - opis krotki, zostanie przekszalcony na Temat maila, dodano możliwość parametryzacji
::              jeżeli jest podana formuła na opis to zastąpi ona napis przesłany parametrem
::       [_g] - język, który został wybrany podczas tworzenia dokumentu (Utwórz PDF)
::       [_h] - status rejestracji drukowanego dokumentu (domyślnie 'N')
::       [_i] - 0/1 akcja E-dokument
::       [_j] - rodzaje e-dokumentu
::       [_k] - generuj e-dokument
::       [_l] - null()/DOKUM.ref()
::   WY: null lub ref zalozonego rekordu w tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
_without_file:={? var_pres('_b')=type_of(~~) || _b:=''; 1 || 0 ?};
{? _>=3 || {? type_of(_c)<>2 || _c:='' ?} || _c:='' ?};
{? _>=4 || {? type_of(_d)<>2 || _d:='' ?} || _d:='' ?};
{? _>=5 || {? type_of(_e)<>2 || _e:='' ?} || _e:='' ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};
{? _>=7 || {? type_of(_g)<>7 || _g:=null() ?} || _g:=null() ?};
{? _>=8 || {? type_of(_h)<>2 || _h:='N' ?} || _h:='N' ?};
_czy_edokument:={? var_pres('_i')=type_of(0) || _i || 0 ?};
_rodzaje_edokumentu:={? var_pres('_j')=type_of('') || _j || '' ?};
_generuj_edokument:={? var_pres('_k')=type_of(0) || _k || 0 ?};
_dokum_edokument:={? var_pres('_l')=type_of(null()) || _l || null() ?};

_wyn:=null();

:: grupowy wydruk
_grp_dr:=exec('czy_grp_dr','faktury_wydruk');

:: sprawdzanie i ew kopiowanie pliku
{? _c='' | _b=_c
||
   _file:=_b
||
   _file:=_c;
   {? fexists(_b,1)
   ||
      {? fcopy(_b,_c,1,1,1)
      ||
         ferase(_b,1)
      ?}
   ?}
?};

_zdarzt:=exec('FindInSet','#table','ZDARZT','ZDARZT',exec('get','#params',100800),'N',,1,,null());
{? _czy_edokument & ~_generuj_edokument & exec('kkh_lic','zws')='T' & _zdarzt=null()
||
   _msg:='Należy określić wartość parametru 100800.'@;
   {? var_pres('__kom')>100 || exec('add_kom','#message',_msg,7) || FUN.info(_msg) ?}

|? _without_file | fexists(_file,1)
||
   exec('sel_dok','dokum',_a,1);
   {? exec('wyb_dok','dokum',_file,_without_file=0,_dokum_edokument)
   ||
      _Tab:=ref_tab(POLA.REFSQL);
      _wyn:=DOKUM.ref();
      DOKUM.SYM_SYS:=_d;
      DOKUM.EM:=_e;
      DOKUM.TYP:='D';
      {? _Tab=FAKS | _Tab=ZK_N || DOKUM.CMS:='T' ?};
      DOKUM.AUTOADD:='T';
      DOKUM.KR_OP:=_f;
      DOKUM.JEZYK:=_g;
      DOKUM.STAT_REJ:=_h;
      {? _Tab=FAKS
      ||
         {? _zdarzt & FAKS.KH
         || DOKUM.DOT:=_zdarzt;
            DOKUM.KH:=exec('kh_dokum','zbl_dok',FAKS.ref());
            Plugin.run('KKH_VALID_001','ADD',1)
         ?}
      |? _Tab=ZD_NAG & ZD_NAG.KH &_czy_edokument
      ||
         {? _zdarzt
         || DOKUM.DOT:=_zdarzt;
            DOKUM.KH:=exec('kh_dokum_zam','zbl_dok',ZD_NAG.ref());
            Plugin.run('KKH_VALID_001','ADD',1)
         ?}
      ?};
      DOKUM.put();

::    przygotowanie nazwy i opisu
      _opis:=exec('get','#params',100250,2)();
      {? _opis<>~~
      || DOKUM.KR_OP:=_opis;
         DOKUM.put()
      ?};
      _opis_memo:=exec('get','#params',100251,2)();
      {? _opis_memo<>~~
      || DOKUM.memo_set(_opis_memo,'OPIS')
      || DOKUM.memo_set('Dokument '+_d+'. Wygenerowano automatycznie dnia '+$date+' o godz. '
                     +$time()+' przez '+exec('name','users')+'.','OPIS' )
      ?};
      DOKUM.memo_put(,'OPIS');

::    e-dokument wysyłany do Businesslink
      {? _czy_edokument & exec('czy_edokument_bl','zbl_dok',_rodzaje_edokumentu)
      ||
         DOKUM.cntx_psh();
         _bl_dokum_seek:=exec('bl_dokum_seek','zbl',{? _Tab=ZD_NAG || ZD_NAG || FAKS ?});
         DOKUM.cntx_pop();
         {? _generuj_edokument | ~_bl_dokum_seek || exec('bl_act','zbl_dok','PDW',DOKUM.ref()) ?}
      ?};

      {? _without_file
      || ~~
      |? _grp_dr
      || {? __GRP_DR.SHOW_PDF || prgs_clr(); exec('pok_dok','dokum') ?}
      |? FUN.ask({? _czy_edokument || 'Czy wyświetlić e-dokument?'@ || 'Czy wyświetlić wygenerowany dokument?'@ ?})
      || prgs_clr(); exec('pok_dok','dokum')
      ?}
   ?};

:: usuwanie dokumentu
   {? _without_file=0 & fexists(_file,1)
   || ferase(_file,1)
   ?}
|| FUN.info('Nie znaleziono pliku %1.'@[_file])
?};
_wyn


\sel_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wyswietla dokumenty powiazane z rekordem
::   WE:  _a  - akronim tabeli
::       [_b] - 0 - wyswietla select, 1 - nie wyswietla
::       [_c] - czy dostepne funkcje edycyjne, gdy wyswietlony select (0- tak, 1 -nie 2-nie, ale dostępne wysyłanie)
::       [_d] - refsql rekordu
::       [_e] - 0 - domyslnie, 1 - dodatkowy panel załączników z DOKUMZ
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0  ?};
{? _>=3 || {? type_of(_c)<>1 || _c:=0 ?} || _c:=0  ?};
{? _>=4 || {? type_of(_d)<>2 || _d:='' ?} || _d:='' ?};
{? _>=5 || {? type_of(_e)<>1 || _e:=0 ?} || _e:=0  ?};

{? ~_b & _e

|| exec('kkh_upraw','dokum_zal',1);

   VAR_DEL.delete('__CTR');
   __CTR:=obj_new('BTAB', 'BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','BLOKUJ','PODGLAD','WIN');
   __CTR.BTAB:='DOKUMZ';
   __CTR.TAB:='DOKUM';
   __CTR.BBLOB:='DOKUM';
   __CTR.WIN_ACR:=exec('mk_ctr','bloby','dokum_dokz',':');
   __CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
   __CTR.DIR_SEP:=exec('sep','#file',1);
   __CTR.DND:=1;
   __CTR.BLOKUJ:=0;
   {? _c
   || __CTR.PODGLAD:=1
   || __CTR.PODGLAD:=0
   ?};
   __CTR.WIN:='WER';

   _okno:=DOKUM.grp_make('Załączniki'@,,'dokum',,,,,'maximized');
   _fr:=$('exec(\'gr_dok\',\'dokum\');exec(\'dokum_rfr\',\'bloby\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\')');
   DOKUM.grp_sel(_okno,,'WER',,_fr);

   DOKUM.grp_splt(_okno,,'vertical','nlook1',',80%');
   DOKUM.grp_sel(_okno,DOKTYP,'CECH',,,,,,,,,,'maximized_with_title','doktyp');

   DOKUM.grp_splt(_okno,'panel0','horizontal','nlook2',16);
   _fb:=$("
      {? grp_empty(DOKUM,__CTR.WIN)
         | (DOKUM.TYP='D' & DOKUM.KH<>null() & KHVZ.UPRAW='B')
      || __CTR.BLOKUJ:=1
      || __CTR.BLOKUJ:=0
      ?};
   "+
   {? _c
   || ""
   || "{? (DOKUM.TYP='D' & DOKUM.KH<>null() & KHVZ.UPRAW<>'')
       || __CTR.PODGLAD:=1
       || {? DOKUM.BL_STAT='' | DOKUM.BL_STAT=exec('before_send','zbl_stat')
          || __CTR.PODGLAD:=0
          |? DOKUM.BL_STAT=exec('ready_to_send','zbl_stat') |
             DOKUM.BL_STAT=exec('send','zbl_stat') | DOKUM.BL_STAT=exec('validation_failed','zbl_stat') |
             DOKUM.BL_STAT=exec('waiting_auth','zbl_stat')
          || __CTR.PODGLAD:=-1
          || __CTR.PODGLAD:=1
          ?}
       ?};
      "
   ?}
   +
   "   exec('bobs_dokumz','bloby')");
   _fa:="";
   DOKUM.grp_ctr(_okno,DOKUMZ,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_fb,_fa,,'maximized')

|| _okno:='GR'

?};

($(_a+'.cntx_psh()'))();
KHVZ.EDIT:=_c;
DOKUM.index('DOKUM');
DOKUM.win_sel(_okno);
DOKUM.timer('sel',_okno,10,"win_disp()");
exec('icon','dokum');
POLA.REFSQL:={? _d<>'' || _d || $(($(_a+'.ref'))()) ?};
__REFSQL:=POLA.REFSQL;
_jest:=0;
{? ~_b
|| {? exec('dokum_refsql_kkh_moz','dokum_zal',__REFSQL) | exec('dokum_refsql_arch_moz','dokum_zal',__REFSQL)
   || _jest:=exec('zalacz_akt_arch','dokum',__REFSQL)
   ?}
?};
{? _a='PROJEKTY'
|| DOKUM.index('PRO_E');
   DOKUM.prefix(PROJEKTY.FIRMA,PROJEKTY.RODZIC,0,((PROJEKTY.POZ_PROJ*3)+PROJEKTY.KOL))
|? _a='DOK'
|| DOKUM.index('DOKR');
   DOKUM.prefix(REF.FIRMA,POLA.REFSQL)
|| DOKUM.index('DOKUM');
   DOKUM.prefix(REF.FIRMA,POLA.REFSQL)
?};
_par:='N';
{? _b=0
|| exec('dokum_act_hide','dokum_zal','D',_c,POLA.REFSQL);
   {? _jest=-1
   || DOKUM.select(,,,,,'R')
   || DOKUM.select()
   ?};
   DOKUM.actions('WER')
?};
KHVZ.EDIT:=0;
&__REFSQL;
($(_a+'.cntx_pop()'))();
''


\icon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: icony w okienkach tabeli kontaktow i zalacznikow
::----------------------------------------------------------------------------------------------------------------------
ZDARZT.win_fml('ZDARZT',,'ZDARZ',,'ICON_BEFORE',"ZDARZT.ICON");
DOKCECH.win_fml('WER',,'NAZWA',,'ICON_BEFORE',"DOKCECH.ICON");
DOKTYP.win_fml('CECH',,'DOKCECH','NAZWA','ICON_BEFORE',"DOKCECH.ICON");
DOKUM.win_fml('WERK',,'DOT','ZDARZ','ICON_BEFORE',"DOKUM.DOT().ICON");
ZDARZT.win_fml('ZDARZT1',,'ZDARZ',,'ICON_BEFORE',"ZDARZT.ICON");
ZDARZT.win_fml('ZDARZT2',,'ZDARZ',,'ICON_BEFORE',"ZDARZT.ICON");
DOKUM.win_efml('REDK',,'DOT','ZDARZ','ICON_BEFORE',"DOKUM.DOT().ICON");
DOKUM.win_efml('REDK_M',,'DOT','ZDARZ','ICON_BEFORE',"DOKUM.DOT().ICON");
DOKUM.win_efml('REDK_P',,'DOT','ZDARZ','ICON_BEFORE',"DOKUM.DOT().ICON");
DOKUM.win_efml('REDK_S',,'DOT','ZDARZ','ICON_BEFORE',"DOKUM.DOT().ICON");
DOKUM.win_efml('REDK1',,'DOT','ZDARZ','ICON_BEFORE',"DOKUM.DOT().ICON");

ZDARZT.fld_fml('ICON','AFTER_EDIT',"exec('chk_bmp','dokum')");
DOKCECH.fld_fml('ICON','AFTER_EDIT',"exec('chk_bmp','dokum')")


\wyb_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: dodawanie dokumentu
::   WE:  [_a] - gdy nie ma to wlacza sie okienko edycji / sciezka wraz z plikiem
::        [_b] - 1 - operacja na pliku / 0 - bez pliku
::        [_c] - DOKUM.ref()
::   WY:  dodano rekord 1 - tak 0 - nie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_typ:='D';
DOKUM.cntx_psh();
_ref_dok:=null();
::{? DOKUM.get() || _ref_dok:=DOKUM.ref() ?};
{? _>=1  || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};
{? _>=2  || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};
{? var_pres('_c')<>type_of(null()) || _c:=null() ?};
{? (POLA.REFSQL<>'' & ref_tab(POLA.REFSQL)=KH & KHVZ.KONTAKTY='T') | (var_pres('pulpit')=1 & pulpit=1)
|| _typ:='K';
   DOKUM.win_edit('REDK')
|? KHVZ.KONTAKTY='T' & (KHVZ.KHVP='kh_all' | KHVZ.KHVP='kh_osob' | KHVZ.KHVP='dokum'| KHVZ.KHVP='projekty')
|| _typ:='K'
|| DOKUM.win_edit('RED')
?};
{? REF.WFIRM & (DOKUM.index('?')='KH2F' | DOKUM.index('?')='KH3F')
|| _tmp:=DOKUM.cur_prfx();
   {? _tmp<>'' & _tmp*','>0
   || _tmp:=((_tmp*',')-1)+_tmp;
      {? #_tmp<>#REF.FIRMA
      || FUN.emsg('W obecnym widoku prezentowane są dane innej firmy.\n'
                 'Przed dodaniem kontaktu należy zmienić zakres danych.'@);
         DOKUM.cntx_pop();
         return(0)
      ?}
   ?}
?};
{? _c || DOKUM.seek(_c) || DOKUM.blank() ?};
DOKUM.bl_file('DOKUM',1);
DOKUM.REFSQL:=POLA.REFSQL;
DOKUM.TYP:=_typ;
{? POLA.REFSQL<>''
|| _refx:=ref_tab(POLA.REFSQL);
   {? type_of(_refx)<>type_of(~~) & _refx=PROJEKTY
   || DOKUM.PROJEKTY:=PROJEKTY.ref()
   |? type_of(_refx)<>type_of(~~) & _refx=DOK
   || DOKUM.DOKR:=POLA.REFSQL
   ?}
?};
{? POLA.REFSQL<>'' & ref_tab(POLA.REFSQL)=KH & KHVZ.KONTAKTY='T' | var_pres('pulpit')=1 & pulpit=1
|| DOKUM.KH:=KH.ref();
   DOKUM.KH_OSOB:=null()
|| {? KHVZ.KONTAKTY='T'
   || {? KHVZ.KHVP='kh_osob'
      || DOKUM.KH:=KH_OSOB.KH;
         DOKUM.KH_OSOB:=KH_OSOB.ref()
      |? KHVZ.KHVP='dokum'
      || DOKUM.KH:=null();
         DOKUM.KH_OSOB:=null()
      |? KHVZ.KHVP='projekty'
      || DOKUM.KH:=PROJEKTY.KH;
         DOKUM.KH_OSOB:=null();
         DOKUM.PROJEKTY:=PROJEKTY.ref()
      || DOKUM.KH:=KH.ref();
         DOKUM.KH_OSOB:=null();
:przepisanie kontrahenta z odbiorcy przy generowaniu pdfa faktury
         KH_ODB.cntx_psh();
         {? POLA.REFSQL<>''
            & ref_tab(POLA.REFSQL)=FAKS
         || DOKUM.KH:=exec('kh_dokum','zbl_dok',POLA.REFSQL)
         ?};
         KH_ODB.cntx_pop()
      ?}
   ||
:: Nie przypisujemy kontrahenta - na tym etapie to jeszcze nie jest 'Kontakt'
::      KH_ODB.cntx_psh();
::      {? POLA.REFSQL<>''
::         & ref_tab(POLA.REFSQL)=FAKS
::      || DOKUM.KH:=exec('kh_dokum','zbl_dok',POLA.REFSQL)
::      ?};
::      KH_ODB.cntx_pop();
      ~~
   ?}
?};
_pom_tab:=POM.TAB;
_pom_typdok:=POM.TYPDOK;
{? _c=null()
||
   POM.TAB:='DOKUM';
   POM.TYPDOK:='SYS';
   exec('add_grnr','numery','SYS');
   DOKUM.NR:=exec('numer_new','numery','PACZKA');
   DOKUM.prefix();
   DOKUM.memo_set(,'OPIS');
   DOKUM.memo_set(,'UPRAW')
?};
{? _a='' & _b
|| {? KHVZ.KONTAKTY='T'
   || {? params_exec('add_dok','dokum') || _ref_dok:=DOKUM.ref() ?}
   || {? DOKUM.add()
      || {? DOKUM.r_lock(1,1,1)

         || exec('znak','numery','DOKUM');
            _del_oldnumer:=1;
            _chk:={? ref_tab(POLA.REFSQL)=KH & KHVZ.KONTAKTY='T' | var_pres('pulpit')=1 & pulpit=1
                  || ""
                  || "exec('chk_doku','dokum')"
                  ?};
            {? DOKUM.edit(_chk)
            || _wyn:=DOKUM.put();
               {? _wyn || _del_oldnumer:=0 ?};
               DOKUM.memo_put(,'OPIS');
               DOKUM.memo_put(,'UPRAW');
               _file:=DOKUM.bl_file('DOKUM');
               {? _file<>''
               || DOKUM.bl_put('DOKUM',_file);
                  DOKUM.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
                  _extension:=DOKUM.bl_info('DOKUM','EXTENSION');
                  {? _extension<>'' & ';bmp;jpg;jpeg;png;tif;tiff;pdf;'*_extension
                  ||
                     DOKUM.DOKUMI:=DOKUM.DOKUM
                  ?}
               ?};
               {? DOKUM.put() || _ref_dok:=DOKUM.ref() ?}
            ?};
            {? _del_oldnumer
            || numer:=DOKUM.NR;
               oldnumer:=1;
               exec('nr_old','numery');
               _uid:=DOKUM.uidref(); _rr:=DOKUM.del(); exec('dokumlog_del','zbl_dok',_uid);
               _rr
            ?};
            DOKUM.r_unlock()
         ?}
      ?}
   ?}
|| DOKUM.memo_set('Dodano dnia '+$date+' o godz. '+$time()+' przez '+exec('name','users'),'OPIS');
   _wyn:={? _c || DOKUM.put() || DOKUM.add() ?};
   {? _c=null() || exec('znak','numery','DOKUM') ?};
   {? _b
   ||
      {? 1+_a='@' & exec('cli_functions','#system')=0
      || FUN.emsg(exec('indevice_nacc_msg','#system'))
      ||
         DOKUM.bl_put('DOKUM',_a,1);
         DOKUM.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
         _extension:=DOKUM.bl_info('DOKUM','EXTENSION');
         {? _extension<>'' & ';bmp;jpg;jpeg;png;tif;tiff;pdf;'*_extension
         ||
            DOKUM.DOKUMI:=DOKUM.DOKUM
         ?}
      ?}
   ?};
   DOKUM.memo_put(,'OPIS');
   DOKUM.memo_put(,'UPRAW');
   DOKUM.put();
   DOKUM.index('DOKUM');
   DOKUM.prefix(REF.FIRMA,DOKUM.REFSQL);
   {? _wyn || _ref_dok:=DOKUM.ref() ?}
?};
DOKUM.bl_file('DOKUM',1);
POM.TAB:=_pom_tab;
POM.TYPDOK:=_pom_typdok;
DOKUM.cntx_pop();
{? _ref_dok<>null() & KHVZ.KHVP<>'kn_nag'
||
   DOKUM.seek(_ref_dok);
   {? DOKUM.DOKUM<>null()
   ||
      DOKUMZ.cntx_psh();
      DOKUMZ.index('DOK');
      DOKUMZ.prefix();
      DOKUMZ.blank();
      DOKUMZ.DOK:=_ref_dok;
      {? exec('upgrade2325_blbc1','zbl')
      || DOKUMZ.REFSQL:=DOKUM.REFSQL
      ?};
      DOKUMZ.DOKUM:=DOKUM.DOKUM;
      DOKUMZ.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
      DOKUMZ.add();
      DOKUMZ.cntx_pop()
   ?}
?};
_wyn


\add_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Dolaczanie dokumentu
::   WY: 1/0 czy dolaczono dokument
::  OLD: \add_dok/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0; _add:=0;
ZDARZT.cntx_psh();
ZDARZT.index('OBJAKO'); ZDARZT.prefix('N',0);
ZDARZT.first();
{? DOKUM.DOT<>null()
|| ZDARZT.seek(DOKUM.DOT)
?};
ZDARZT.win_sel('ZDARZT2'); ZDARZT.win_patt('SZUK');
DOKUM.DOT:=null;
{? var_pres('dall_kh')>0 & ~dall_kh
|| DOKUM.DOT:=ref_zdt1
|? ZDARZT.select(,1)
|| DOKUM.DOT:=ZDARZT.ref();
   {? ZDARZT.RODZAJ_K<>'' || DOKUM.RODZAJ_K:=ZDARZT.RODZAJ_K ?}
?};
{? DOKUM.DOT
|| exec('br_dokum2','dokum_zal');
   {? DOKUM.DOT().RODZ='M'
   || {? DOKUM.KH_OSOB<>null()
      || DOKUM.EM:=DOKUM.KH_OSOB().EMAIL
      ?};
      {? DOKUM.EM='' & DOKUM.KH<>null()
      || DOKUM.EM:=DOKUM.KH().EM
      ?}
   |? DOKUM.DOT().RODZ='P'
   || _user:=exec('name','users');
      USERS.cntx_psh();
      {? exec('getUser','users',_user)
      || {? USERS.OSOBA<>null()
         || DOKUM.P_AUTOR:=|(USERS.OSOBA().PIERWSZE + ' ' + USERS.OSOBA().NAZWISKO)
         || DOKUM.P_AUTOR:=USERS.DANE
         ?}
      ?};
      USERS.cntx_pop()
   ?};
   DOKUM.DOT().RODZINF();
   DOKUM.KH();
   {? RODZINF.NAZ='Dokument prosty' | RODZINF.NAZ='Dokument definiowalny'
   || _add:=DOKUM.add();
      exec('znak','numery','DOKUM');
      DOKUM.put()
   ?};
:: osoba kontaktowa
   _params:=params_get();
   {? DOKUM.KH & DOKUM.KH_OSOB=null()
         &
      type_of(_params)<>type_of(~~) & var_pres('env',_params)=type_of(obj_new('obj'))
         &
      var_pres('KH_ALL_OK',_params.env)=type_of(null()) & _params.env.KH_ALL_OK & ref_tab(_params.env.KH_ALL_OK)=KH_OSOB
   ||
      DOKUM.KH_OSOB:=_params.env.KH_ALL_OK;
      KH_OSOB.cntx_psh();
      {? DOKUM.KH_OSOB().KH<>DOKUM.KH || DOKUM.KH_OSOB:=null() ?};
      KH_OSOB.cntx_pop()
   ?};
   {? RODZINF.NAZ='Dokument definiowalny' & ZDARZT.WYB_KW || DOKUM.WAL:=FINFO.NAROD ?};
   {? RODZINF.NAZ='Dokument definiowalny' & ZDARZT.WYB_DAT=2 || DOKUM.DATA_OD:=date() ?};
   exec('okna','dokum');
   KH_OSOB.f_clear(1);
   {? DOKUM.edit("exec('spr_dokum','dokum')")
   || {? REF.WFIRM & VAR.WYB_FIRM='Y' & VAR.FIRMA<>REF.FIRMA
      || FUN.info('Ze względu na ustawione warunki filtrowania danych\n'
                 'dodany kontakt będzie niewidoczny w okienku.'@);
         _hid:=1
      || _hid:=0
      ?};
      _newref:=null;
      DOKUM.cntx_psh();
      DOKUM.prefix();
      _wyn:=DOKUM.put();
      {? _wyn
      || _newref:=DOKUM.ref();
         DOKUM.memo_put(,'OPIS');
         DOKUM.memo_put(,'UPRAW');
         {? KHVZ.KONTAKTY='T' & (KHVZ.KHVP='kh_all' | KHVZ.KHVP='kh_osob' | KHVZ.KHVP='dokum'| KHVZ.KHVP='projekty')
         || exec('dokump_zapis','dokum'); exec('dokumpt_zapis','webhook')
         ?};
         {? (DOKUM.DOT().ATR_ONE>0) & (DOKUM.DOT().ATR_G1R>0)
         || 1
:: TODO
::            exec('edk_atr_dolacz','deleg_at',0)
         ?}
      ?};
      DOKUM.cntx_pop();
      {? _newref<>null & _hid=0
      || DOKUM.seek(_newref);
         {? DOKUM.f_active()
         || DOKUM.f_rfresh();
            DOKUM.f_seek(_newref)
         ?}
      ?}
   |? _add
   || numer:=DOKUM.NR;
      oldnumer:=1;
      exec('nr_old','numery');
      _uid:=DOKUM.uidref(); _rr:=DOKUM.del(); exec('dokumlog_del','zbl_dok',_uid);
      _rr
   ?};
   {? type_of(params_get())<>type_of(~~)
         &
      var_pres('env',params_get())>0
   ||
      params_exec('kh_osob_zakres_set','kontrahent_view',KH.ref())
   ?}
?};
ZDARZT.cntx_pop();
_wyn


\spr_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: DOKUM - sprawdzenie wprowadzonych parametrów
::   WE: _a - specyfikacja testu: 1-poprawianie, 0-dołączanie (domyślnie)
::  OLD: \spr_dokum/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_edit:=0;
{? var_pres('_a')=type_of(0)
|| _edit:=_a
?};
_zwrot:='';
DOKUM.DOT().RODZINF();
_okn:=DOKUM.win_edit('?');
{? RODZINF.NAZ='Dokument prosty' | DOKUM.DOT=null()
|| {? _okn='REDK_M' | _okn='REDK_P' | _okn='REDK_S'
   || _okn:='REDK'
   ?};
   {? DOKUM.DATAKONT=date(0,0,0)
   || FUN.info('Nie wprowadzono daty kontaktu.'@); _zwrot:='DATAKONT'
   ?};
   {? _zwrot='' & _okn<>'REDK' & DOKUM.ODD=null
   || FUN.info('Nie wybrano jednostki księgowej.'@); _zwrot:='ODD'
   ?};
   {? _zwrot='' & DOKUM.KH=null() & exec('dokum_kh_be','dokum')
   || FUN.info('Nie wybrano kontrahenta.'@); _zwrot:='KH'
   ?}
|? RODZINF.NAZ='Dokument definiowalny'
|| {? _okn='REDD_M' | _okn='REDD_P' | _okn='REDD_S'
   || _okn:='REDD'
   ?};
   {? DOKUM.DATAKONT=date(0,0,0)
   || FUN.info('Nie wprowadzono daty kontaktu.'@); _zwrot:='DATAKONT'
   ?};
   {? _zwrot='' & _okn<>'REDD' & DOKUM.ODD=null
   || FUN.info('Nie wybrano jednostki księgowej.'@); _zwrot:='ODD'
   ?};
   {? _zwrot='' & _okn='REDD' & DOKUM.KH=null() & exec('dokum_kh_be','dokum')
   || FUN.info('Nie wybrano kontrahenta.'@); _zwrot:='KH'
   ?};
   {? _zwrot='' & ZDARZT.WYB_SYM=2 & DOKUM.SYM=''
   || FUN.info('Nie wprowadzono symbolu.'@); _zwrot:='SYM'
   ?};
   {? _zwrot='' & ZDARZT.WYB_KW=2 & DOKUM.WAL=null()
   || FUN.info('Nie wybrano waluty.'@); _zwrot:='WAL'
   ?};
   {? _zwrot='' & ZDARZT.WYB_KW=2 & DOKUM.WART$2=0
   || FUN.info('Nie wprowadzono wartości.'@); _zwrot:='WART'
   ?};
   {? _zwrot='' & ZDARZT.WYB_DAT=2 & DOKUM.DATA_OD=date(0,0,0)
   || FUN.info('Nie wprowadzono daty początkowej.'@); _zwrot:='DATA_OD'
   ?};
   {? _zwrot='' & ZDARZT.WYB_DAT=2 & DOKUM.DATA_DO<>date(0,0,0) & DOKUM.DATA_DO<DOKUM.DATA_OD
   || FUN.info('Błędny zakres dat.'@); _zwrot:='DATA_OD'
   ?};
   {? _zwrot='' & ZDARZT.WYB_PRAC=2 & DOKUM.PRAC=null()
   || FUN.info('Nie wybrano pracownika.'@); _zwrot:='PRAC'
   ?};
   {? _zwrot='' & ZDARZT.WYB_OSK=2 & _okn='REDD' & DOKUM.KH_OSOB=null()
   || FUN.info('Nie wybrano osoby kontaktowej.'@); _zwrot:='KH_OSOB'
   ?};
   {? _zwrot='' & ZDARZT.WYB_OSK=2 & _okn<>'REDD' & DOKUM.OSOBA=''
   || FUN.info('Nie wybrano osoby kontaktowej.'@); _zwrot:='OSOBA'
   ?}
?};
{? _zwrot='' & _okn='REDD_' & DOKUM.DATA_OD<>date(0,0,0) & DOKUM.DATA_DO<>date(0,0,0) & DOKUM.DATA_OD>DOKUM.DATA_DO
|| FUN.info('Data OD nie może być późniejsza niż data DO.'@);
   _zwrot:='DATA_OD'
?};
{? _zwrot=''
|| {? _edit
   || _zwrot:=Plugin.run('KKH_VALID_001','MOD',0)
   || _zwrot:=Plugin.run('KKH_VALID_001','ADD',0)
   ?}
?};
_zwrot


\spr_obieg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Sprawdzanie po okienku - dodawanie kontaktu na liscie wszystkich kontaktow
::  OLD: \spr_obieg/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? OBIEGI.KH=null
|| FUN.info('Nie wybrano kontrahenta.'@); 'KH'
|? OBIEGI.RODZAJ_K=null
|| FUN.info('Nie wybrano rodzaju kontaktu.'@); 'RODZAJ_K'
|| 1
?}


\okna
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: DOKUM - ustawianie okienek slownikow
::  OLD: \okna/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
ODD.win_dict('SLO');
SLO.win_dict('ONE'); SLO.win_sel('ONE');
OSOBA.win_dict('SLO')


\chk_doku
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: akcja rekord po dla zalacznikow DOKUM
::  OLD: \chk_doku/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? ref_tab(POLA.REFSQL)=POS
|| __CHK.record(DOKUM,,'KR_OP')
|| __CHK.record(DOKUM,,'DOKUM')
?}


\pok_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: wyswietlenie dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? ~DOKUM.get() || return() ?};

{? #DOKUM.DOKUM<>0
||
   {? DOKUM.TYP='L'
   || _isrun:=exec('cli_functions','#system');
      {? ~_isrun || FUN.emsg(exec('indevice_nacc_msg','#system'));return() ?};
      exec('ed_dok','dokum')
   || exec('bl_view','#blob',DOKUM,'DOKUM')
   ?}
|? #DOKUM.DOKUMI<>0
|| exec('bl_view','#blob',DOKUM,'DOKUMI')
|| FUN.info('Brak pliku załącznika.'@)
?};
''


\gr_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: akcja odswiez dla grupy
::----------------------------------------------------------------------------------------------------------------------
_get:={? DOKUM.f_active()
      || DOKUM.sel_size()=0 & DOKUM.f_get()
      || DOKUM.get()
      ?};
DOKTYP.index('DOKDC');
DOKTYP.prefix({? _get || DOKUM.ref() || null() ?});
{? DOKUM.size()>0 & KHVZ.EDIT=0 || DOKTYP.actions('CECH',,,1) || DOKTYP.actions('CECH','DUPdup:Dd',,1) ?};
grp_disp(DOKTYP,'CECH');
''


\grmdok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: zalaczniki dla materialu
::----------------------------------------------------------------------------------------------------------------------
{? HELP.REFMAT<>null || M.seek(HELP.REFMAT) ?};
POLA.REFSQL:=$M.ref();
DOKUM.index('DOKUM');
DOKUM.prefix(REF.FIRMA,POLA.REFSQL);

exec('gr_dok','dokum')


\doku_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: popraw DOKUM
::----------------------------------------------------------------------------------------------------------------------
{? ~DOKUM.get() || return() ?};

{? DOKUM.FIRMA<>REF.FIRMA
|| FUN.info('Kontakt wprowadzony w innej firmie niż bieżąca. Akcja niemożliwa.'@);
   return(0)
?};

::{? DOKUM.WO='T' | DOKUM.ESEND='T' | DOKUM.BSEND<>'N' | DOKUM.RECEIVED='T' | DOKUM.K_NAD='T'
{? DOKUM.WO='T' | DOKUM.ESEND='T' | DOKUM.BSEND<>'N'
::| DOKUM.RECEIVED='T'
||
   {? DOKUM.BSEND<>'N' || _txt:='Załącznik do tego dokumentu został wysłany do Businesslink.'@
   |? DOKUM.WO='T' || _txt:='Załącznik do tego dokumentu został wysłany mailem.'@
   |? DOKUM.ESEND='T' || _txt:='Załącznik do tego dokumentu został przekazany do wysyłki mailem.'@
::   |? DOKUM.K_NAD='T' || _txt:='Dokument został przekazany do książki nadawczej.'@
   || _txt:=''
   ?};
   FUN.info(_txt+'\n\n'+'Akcja niemożliwa.'@);
   return(0)
?};

{? ~DOKUM.r_lock(1,1,1)
|| FUN.info('Pozycja zablokowana przez innego użytkownika.\nPoprawa pozycji niemożliwa.'@)
|| DOKUM.cntx_psh();
   DOKUM.memo_get(,'OPIS');
   DOKUM.memo_get(,'UPRAW');
   DOKUM.bl_file('DOKUM',1);
   {? KHVZ.KONTAKTY='T' | (DOKUM.TYP='D' & DOKUM.KH<>null())
   || exec('pop_dok','dokum')
   || exec('dokum_wine','dokum');
      _chk:={? ref_tab(POLA.REFSQL)=KH &  KHVZ.KONTAKTY='T' | var_pres('pulpit')=1 & pulpit=1
            || ""
            || "exec('chk_doku','dokum')"
            ?};
      {? DOKUM.edit(_chk) & DOKUM.put()
      || DOKUM.memo_put(,'OPIS');
         DOKUM.memo_put(,'UPRAW');
         {? (_file:=DOKUM.bl_file('DOKUM'))<>''
         || DOKUM.bl_put('DOKUM', _file);
            DOKUM.NAZWA:=DOKUM.bl_info('DOKUM', 'NAME')
         ?};
         DOKUM.put()
      ?}
   ?};
   DOKUM.bl_file('DOKUM',1);
   DOKUM.cntx_pop();
   DOKUM.r_unlock()
?};
''


\pop_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: Poprawianie dokumentu
::  OLD: \pop_dok/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.EDOKUMR<>''
|| _edok:=exec('FindAndGet','#table',EDOKUM,DOKUM.EDOKUMR,,,null());
   {? _edok<>null()
   || {? exec('edokos_oper','obiegi2',_edok)='W'
      || _typobieg:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"@.EDOKUM.TYP().TYPOBIEG().NAZWA",'');
         _typ_k:={? _typobieg='Obieg faktur' || 1 |? _typobieg='Obieg wniosków' || 2  || 0 ?};
         exec('ob_start','dokum',_typ_k,0)
      || FUN.info('Dla kontaktu został wcześniej uruchomiony obieg.'@)
      ?}
   || FUN.info('Nie znaleziono dokumentu w obiegu.'@)
   ?}
|| DOKUM.cntx_psh();
   DOKUM.DOT().RODZINF();
   exec('okna','dokum');
   exec('br_dokum2','dokum_zal');
   exec('tabkhini','kontrahent');
   _ref:=DOKUM.ref();
   _kh0:=DOKUM.KH;
   _dokumk0:=DOKUM.DOKUMK;
   _put:=0;
   DOKUM.prefix();
   {? DOKUM.edit("exec('spr_dokum','dokum',1)") & DOKUM.put()
   || DOKUM.memo_put(,'OPIS');
      DOKUM.memo_put(,'UPRAW');
      {? (KHVZ.KONTAKTY='T' & DOKUM.TYP='K') |(DOKUM.TYP='D' & DOKUM.KH<>null())
      || {? DOKUM.KH<>_kh0 | (DOKUM.DOKUMK<>null() & DOKUM.DOKUMK<>_dokumk0)
         || exec('dokump_zapis','dokum');
            exec('dokumpt_zapis','webhook')
         ?}
      ?};
::mod[DG]
      {? DOKUM.K_NAD='T'
      || KN_POZ.cntx_psh();
         _ind:=KN_POZ.ndx_tmp(,1,'DOKUM',,);
         KN_POZ.index(_ind);
         KN_POZ.prefix(DOKUM.ref);
         {? KN_POZ.first()
         || KN_POZ.KAT:=DOKUM.DOKUMK().NAZ;
            KN_POZ.KH:=DOKUM.KH;
            KN_POZ.put()
         ?};
         KN_POZ.cntx_pop()
      ?};
::<<
      _put:=1
   ?};
   DOKUM.cntx_pop();
   {? _put & DOKUM.f_active()
   || DOKUM.f_rfresh();
      DOKUM.f_seek(_ref)
   ?};
   exec('tabkhdel','kontrahent')
?}


\dokum_wine
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: ustawia okno redakcji dla DOKUM
::----------------------------------------------------------------------------------------------------------------------
_win:=
   {? DOKUM.TYP='D' & DOKUM.KH=null()
   || 'RED'
   |? DOKUM.TYP='D' | DOKUM.KH<>null()
   || exec('dokum_okno_red','dokum')
   |? DOKUM.TYP='L'
   || 'RED_L'
   |? DOKUM.TYP='K' | DOKUM.KH
   || exec('dokum_okno_red','dokum')
   |? DOKUM.TYP='I' & DOKUM.DOKUM=null()
   || DOKUM.efld_opt('REDK','mark=0',,'KH','KOD');
      DOKUM.efld_opt('REDK','mark=0',,'KH_OSOB','NAZWISKO');
      DOKUM.btn_eopt('REDK','UPRAW','state=grayed');
      'REDK'
   || 'RED_INNY'
   ?};
DOKUM.win_edit(_win)


\dokumceh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2011]
:: OPIS: akcja Cechy
::----------------------------------------------------------------------------------------------------------------------
{? ~DOKUM.get || return() ?};
{? DOKUM.FIRMA<>REF.FIRMA
|| FUN.info('Dla kontaktów dodanych w innej firmie (%1)\n'
            'niż bieżąca (%2), wykonanie akcji nie jest możliwe.'@[DOKUM.FIRMA().SYMBOL,REF.FIRMA().SYMBOL]);
   return()
?};

_edit:=
   {? DOKUM.r_lock(1,1,1)
   || 1
   || FUN.info('Pozycja zablokowana przez innego użytkownika.\nModyfikacja kategorii niemożliwa.'@);
      0
   ?};
DOKCECH.win_dict('WER');
DOKTYP.win_sel('CECH');
DOKTYP.index('DOKDC');
DOKTYP.prefix(DOKUM.ref());
DOKTYP.actions('CECH',{? _edit || '' || 'dpu:d' ?},,1);
DOKTYP.select();
{? _edit || DOKUM.r_unlock() ?}


\ed_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: edycja dokumentu
:: ~OST: INBLEDIT,INSYSEXEC
::----------------------------------------------------------------------------------------------------------------------
{? ~DOKUM.get() || return() ?};

{? DOKUM.WO='T' | DOKUM.ESEND='T' | DOKUM.BSEND<>'N' | DOKUM.RECEIVED='T'
||
   {? DOKUM.BSEND<>'N' || _txt:='Załącznik do tego dokumentu został wysłany do Businesslink.'@
   |? DOKUM.WO='T' || _txt:='Załącznik do tego dokumentu został wysłany mailem.'@
   |? DOKUM.ESEND='T' || _txt:='Załącznik do tego dokumentu został przekazany do wysyłki mailem.'@
   || _txt:=''
   ?};
   FUN.info(_txt+'\n\n'+'Akcja niemożliwa.'@);
   return(0)
?};

{? #DOKUM.DOKUM<>0
||
   {? ~DOKUM.r_lock(1,1,1)
   ||
      FUN.info('Pozycja zablokowana przez innego użytkownika.\nPoprawa pozycji niemożliwa.'@)
   ||
      {? DOKUM.TYP='L'
      || _plik:='lotus.ndl';
         {? DOKUM.bl_get('DOKUM',_plik,1)
         || {? exec('interm','#system')
            || dlg_save(_plik,1)
            || sys_exec(_plik)
            ?}
         || FUN.info('Nie istnieje połączenie z dokumentem Lotus Notes.'@)
         ?}
      || _ext:=DOKUM.bl_info('DOKUM','EXTENSION');
         {? _ext='html' | _ext='htm'
         || exec('edit_html','dokum')
         || {? exec('cli_functions','#system')>0
            || DOKUM.bl_edit('DOKUM')
            || exec('bl_save','#blob',DOKUM,'DOKUM')
            ?}
         ?}
      ?};
      DOKUM.r_unlock()
   ?}
?};
''


\lok_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: kopiowanie pliku na lokalny
::----------------------------------------------------------------------------------------------------------------------
{? ~DOKUM.get() || return() ?};

{? #DOKUM.DOKUM<>0
|| _name:=DOKUM.bl_info('DOKUM', 'NAME');
   _ext:=DOKUM.bl_info('DOKUM', 'EXTENSION');
   {? exec('blk_lock','#table','DOKUM',DOKUM.ref(),,1,'Załącznik: %1 jest redagowany.'@[_name])
   || exec('bl_save','#blob',DOKUM,'DOKUM');
      exec('blk_unlock','#table','DOKUM',DOKUM.ref())
   ?}
?};
''


\dokum_kont_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: dodaje zalacznik do kontaktow z kontrahentem
::----------------------------------------------------------------------------------------------------------------------
{? ~DOKUM.get() || return() ?};

_kh:=null();
{? ref_tab(DOKUM.REFSQL)=FAKS
|| _kh:=exec('FindAndGet','#table','FAKS',DOKUM.REFSQL,,"KH",null())

|? ref_tab(DOKUM.REFSQL)=ZD_NAG
|| _kh:=exec('FindAndGet','#table','ZD_NAG',DOKUM.REFSQL,,"KH",null())

|? ref_tab(DOKUM.REFSQL)=ZK_N
|| _kh:=exec('FindAndGet','#table','ZK_N',DOKUM.REFSQL,,"KH",null())

|? ref_tab(DOKUM.REFSQL)=ZDP_NAG
|| _kh:=exec('FindAndGet','#table','ZDP_NAG',DOKUM.REFSQL,,"KH",null())

|? ref_tab(DOKUM.REFSQL)=OFE
|| _kh:=exec('FindAndGet','#table','OFE',DOKUM.REFSQL,,"KH",null())

|? ref_tab(DOKUM.REFSQL)=UM
|| _kh:=exec('FindAndGet','#table','UM',DOKUM.REFSQL,,"KH",null())

|? ref_tab(DOKUM.REFSQL)=DOK
|| _kh:=exec('FindAndGet','#table','DOK',DOKUM.REFSQL,,"exec('kh_dokum_dks','zbl_dok',@.DOKUM.REFSQL)",null())
?};

{? _kh
||
   {? ~DOKUM.r_lock(1,1,1)
   ||
      FUN.info('Pozycja zablokowana przez innego użytkownika.\nWłączenie pozycji do kontatków niemożliwe.'@)
   ||
      _zapis:=0;
      ZDARZT.cntx_psh();
      ZDARZT.index('OBJAKO');
      ZDARZT.prefix('N',0);
      ZDARZT.first();
      ZDARZT.win_sel('ZDARZT2');
      ZDARZT.win_patt('SZUK');
      {? ZDARZT.select()
      || DOKUM.DOT:=ZDARZT.ref();
         DOKUM.KH:=_kh;
         DOKUM.OPER1:=exec('name','users');
         exec('br_dokum2','dokum_zal');
         DOKUM.memo_get(,'OPIS');
         {? DOKUM.edit("exec('spr_dokum','dokum')")
         ||
            DOKUM.memo_put(,'OPIS');
            DOKUM.memo_put(,'UPRAW');
            DOKUM.put();
            _zapis:=1
         ?}
      ?};
      ZDARZT.cntx_pop();
      DOKUM.r_unlock();
      {? _zapis
      || exec('dokump_zapis','dokum');
         exec('dokumpt_zapis','webhook')
      ?}
   ?}
||
   FUN.info('Nie powiodło się dodanie załącznika do kontaktów.\nNie znaleziono kontrahenta.'@)
?}


\dokum_kont_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: usuwa zalacznik do kontaktow z kontrahentem
::----------------------------------------------------------------------------------------------------------------------
{? ~DOKUM.get() || return() ?};

{? ~DOKUM.KH || FUN.info('Załącznik nie jest dodany do kontaktów kontrahenta.'@); return() ?};

{? ~DOKUM.r_lock(1,1,1)
||
   FUN.info('Pozycja zablokowana przez innego użytkownika.\nUsunięcie pozycji z kontatków niemożliwe.'@)
||
   _moz:=1;
   _kom:='';
   {? DOKUM.BL_STAT<>'' & DOKUM.RODZAJ_K='W'
   || {? DOKUM.RECEIVED='T'
      || _moz:=0;
         FUN.info('Załącznik został odebrany w Businesslink. Nie można usunąć z kontaktów.'@)
      |? DOKUM.BSEND='T'
      || _moz:=0;
         FUN.info('Załącznik został wysłany do Businesslink. Nie można usunąć z kontaktów.'@)
      || _kom:='Po usunięciu załacznika z kontaktów nie będzie możliwe wysłanie do Businesslink.\n'
      ?}
   ?};
   {? _moz
   ||
      _kom+='Usunąć załącznik z kontaktów z kontrahentem?'@;
      {? FUN.ask(_kom)
      ||
         DOKUM.KH:=null();
         DOKUM.DOT:=null();
         DOKUM.OPER1:='';
         DOKUM.memo_set(,'UPRAW');
         DOKUM.memo_put(,'UPRAW');
         DOKUM.put()
      ?}
   ?};
   DOKUM.r_unlock()
?}


\leg_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: legenda tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
exec('legenda','color','DOKUM#01')


\kol_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: kolory dla tabeli DOKUM (dodatkowo wyszarzanie akcji)
::----------------------------------------------------------------------------------------------------------------------
::{? DOKUM.KH
::|| _kh:=DOKUM.KH
::|? ref_tab(DOKUM.REFSQL)=FAKS
::|| _kh:=exec('FindAndGet','#table',FAKS,DOKUM.REFSQL,,"KH",null())
::|? ref_tab(DOKUM.REFSQL)=ZK_N
::|| _kh:=exec('FindAndGet','#table',ZK_N,DOKUM.REFSQL,,"KH",null())
::|? ref_tab(DOKUM.REFSQL)=EDI_I
::|| _kh:=exec('FindAndGet','#table',EDI_I,DOKUM.REFSQL,,"KH",null())
::|? ref_tab(DOKUM.REFSQL)=ZD_NAG
::|| _kh:=exec('FindAndGet','#table',ZD_NAG,DOKUM.REFSQL,,"KH",null())
::|? ref_tab(DOKUM.REFSQL)=OFE
::|| _kh:=exec('FindAndGet','#table',OFE,DOKUM.REFSQL,,"KH",null())
::|? ref_tab(DOKUM.REFSQL)=TR_NZL
::|| _kh:=exec('FindAndGet','#table',TR_NZL,DOKUM.REFSQL,,"KH",null())
::|| _kh:=null()
::?};

::{? _kh
::|| KH_DOD.cntx_psh();
::   KH_DOD.index('KH_DOD');
::   KH_DOD.prefix(REF.FIRMA,_kh);
::   {? KH_DOD.first()
::   || _hide:=
::         {? KH_DOD.EFAKTURA='B'
::         || {? DOKUM.ESEND='N' || 'W(R)' || '' ?}
::         |? KH_DOD.EFAKTURA='T'
::         || 'W(B)'+{? DOKUM.BSEND='N' || 'W(IU)' || '' ?}
::         || 'W(B)'+{? DOKUM.BSEND='N' || 'W(IU)' || '' ?}+{? DOKUM.ESEND='N' || 'W(R)' || '' ?}
::         ?}
::   || _hide:='W'
::   ?};
::   KH_DOD.cntx_pop()
::|| _hide:='W'
::?};
::DOKUM.actions_grayed('WER',_hide);

{? DOKUM.DLA_KH
|| 'DOKUM#01'
|| ''
?}


\wysw_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: wyswietl przed tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
exec('dokum_wine','dokum');
{? DOKUM.DLA_KH
|| exec('dokum_bd','dokum')
|| DOKUM.display()
?}


\dokum_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Arkadiusz Wielgus [2010]
:: OPIS: DOKUM.ref
::----------------------------------------------------------------------------------------------------------------------
DOKUM.ref()


\chk_bmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: formula po ZDARZT.ICON, DOKCECH.ICON
::   WY: 1-poprawna struktura; 0-bledna
::----------------------------------------------------------------------------------------------------------------------
_txt:=fld;

{? _txt='' || return(1) ?};

_wyn:=1;

_wsk:=_txt*':';
_src:={? _wsk>0 || (_wsk-1)+_txt || _txt ?};
_nr:={? _wsk>0 || _wsk-_txt || '' ?};

{? _src=''
||
   FUN.info('Nie podano nazwy pliku.'@);
   return(0)
?};

{? ~fexists(_src,1)
||
   FUN.info('Plik %1 nie istnieje.'@[_src]);
   fld('');
   _wyn:=0
?};

{? _wyn
||
   _fbmp:=fopen(_src,'br',1);
   {? _fbmp
   ||
      {? fseek(_fbmp,1) & fread(_fbmp)<>80 & fread(_fbmp)<>78
      ||
         FUN.info('Plik %1 nie jest bitmapą PNG.'@[_src]);
         _wyn:=0
      ?};
      fclose(_fbmp)
   ?}
?};

{? _wyn & _nr<>''
||
   _nr:=#_nr;
   _ileicon:=0;
   _fbmp:=fopen(_src,'br',1);
   {? _fbmp
   ||
      fread(_fbmp);
      fseek(_fbmp,20);
      _dword:=obj_new(4);
      {! _byte:=1..4 |! _dword[_byte]:=fread(_fbmp) !};
      {! _byte:=1..4 |! _ileicon:=_ileicon*256+_dword[_byte] !};
      fclose(_fbmp)
   ?};
   _ileicon:=_ileicon/16;
   {? _nr<1 | _nr>_ileicon
   ||
      FUN.info('Brak ikony w pliku %1'@[_src]);
      _wyn:=0
   ?}
?};

_wyn


\chk_dokcech
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: sprawdzanie wypelnienia pol po edycji DOKCECH
::----------------------------------------------------------------------------------------------------------------------
_wyn:=__CHK.record(DOKCECH,,'NAZWA');
{? _wyn=''
||
   _naz:=DOKCECH.NAZWA;
   _ref:=DOKCECH.ref();
   DOKCECH.cntx_psh();
   DOKCECH.index('DOKCECH');
   DOKCECH.prefix(_naz,_naz);
   {? DOKCECH.first()
   ||
      {!
      |?
         {? DOKCECH.ref()<>_ref | -(1+menu_txt)='d'
         || FUN.info('Istnieje już cecha %1.'@[_naz]); _wyn:='NAZWA'
         ?};
         _wyn='' & DOKCECH.next()
      !}
   ?};
   DOKCECH.cntx_pop()
?};
_wyn


\dokcech_fld_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: F3 - pola tabeli DOKCECH
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fld_akr:=cur_afld();

{? _fld_akr='ICON'
|| exec('ico_selall','#icon',,1,1,'.png')

?}


\chk_dokt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: sprawdzanie wypelnienia dla typow dokumentow DOKTYP
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? DOKTYP.DOKCECH=null()
||
   FUN.info('Wypełnij kategorię.'@);
   _wyn:='DOKCECH'
||
   _naz:=DOKTYP.DOKCECH().NAZWA;
   _cech:=DOKTYP.DOKCECH;
   _ref:=DOKTYP.ref();
   DOKTYP.cntx_psh();
   DOKTYP.index('DOKDC');
   DOKTYP.prefix(DOKUM.ref(),_cech);
   {? DOKTYP.first()
   ||
      {!
      |?
         {? DOKTYP.ref()<>_ref | -(1+menu_txt)='d'
         || FUN.info('Dla bieżącego dokumentu dodano już kategorię %1.'@[_naz]); _wyn:='DOKCECH'
         ?};
         _wyn='' & DOKTYP.next()
      !}
   ?};
   DOKTYP.cntx_pop()
?};
_wyn


\be_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.30]
:: OPIS: DOKUM - przed redakcja pol
::  OLD: \be_dokum/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
_pole:=cur_afld();
DOKUM.DOT().RODZINF();
{? _pole='ODD'
|| ODD.cntx_psh();
   ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   _zwrot:=(ODD.size()>1);
   ODD.cntx_pop()
|? RODZINF.NAZ='Dokument definiowalny'
|| {? (_pole='SYM' & ~ZDARZT.WYB_SYM) |
      ((_pole='DATA_OD' | _pole='DATA_DO') & ~ZDARZT.WYB_DAT) |
      ((_pole='WAL' | _pole='WART') & ~ZDARZT.WYB_KW) |
      (_pole='OSOBA' & ~ZDARZT.WYB_OSK) |
      (_pole='PRAC' & ~ZDARZT.WYB_PRAC)
   || _zwrot:=0
   ?}
?};
_zwrot


\ae_dokum_dot
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [12.30]
:: OPIS: Po redakcji pola DOKUM.DOT
::  OLD: \ae_dokum_dot/skid_obg.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.DOT().WYB_OSK=0 || DOKUM.OSOBA:='' ?};
1


\osoba_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: wola slownik osob kontaktowych kontrahenta
::  OLD: \osoba_f3/skid_kh.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=fld();
KH_OSOB.cntx_psh;
KH_OSOB.index('KH'); KH_OSOB.prefix(KH.ref);
KH_OSOB.win_sel('SLO');
{? DOKUM.KH_OSOB<>null()
|| KH_OSOB.seek(DOKUM.KH_OSOB)
?};
{? KH_OSOB.select(,1,5)
|| DOKUM.KH_OSOB:=KH_OSOB.ref();
   _wyn:=KH_OSOB.NAZWISKO+' '+KH_OSOB.IMIE
?};
KH_OSOB.cntx_pop;
_wyn


\osoba_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: po edycji pola OSOBA w tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.OSOBA=""
|| DOKUM.KH_OSOB:=null()
?};
~~


\f3_em_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MaX [12.30]
:: OPIS: Akcja f3 dla załączników z poziomu faktur
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_skad:=ref_tab(DOKUM.REFSQL);
_em:=DOKUM.EM;
_emtb:=spli_str(_em,';');
{? type_of(_skad)=type_of(~~)
|| {? DOKUM.TYP='K'
   || _tabela:=1;
      _kh:=DOKUM.KH
   || _tabela:=0;
      _kh:=null()
   ?}
|? _skad=FAKS
|| _tabela:=1;
   _kh:=FAKS.KH
|? _skad=ZK_N
|| _tabela:=1;
   _kh:=ZK_N.KH
|? _skad=ZD_NAG
|| _tabela:=1;
   _kh:=ZD_NAG.KH
|? _skad=ND
|| _tabela:=1;
   _kh:=ND.KH
|? _skad=OFE
|| _tabela:=1;
   _kh:=OFE.KH
|| _tabela:=0;
   _kh:=null()
?};
{? _tabela=1 & _kh<>null()
|| _tab:=tab_tmp(3,'LP','INTEGER','LP'@,
              'TYP','STRING[3]','Typ'@,
              'D','STRING[1]','Domyślny'@,
              'NAZ','STRING[91]','Nazwa'@,
              'EM','STRING[120]','e-mail'@
               );
   _o:=_tab.mk_sel('Adresy e-mail'@,'P',,,1,,,,'U');
   _tab.win_fld(_o,,'TYP',,,3);
   _tab.win_fld(_o,,'D',,,,,,,,,2,,"'T'","'N'");
   _tab.win_fld(_o,,'NAZ',,,60);
   _tab.win_fld(_o,,'EM',,,60);
   _tab.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
   _tab.win_act(_o,,'Kolejność');
   _tab.win_sel(_o);
   _lp:=0;
   _lp0:=0;
   KH.cntx_psh();
   KH.clear();
   {? KH.seek(_kh) & KH.EM<>''
   || _tab.blank();
      _tab.LP:=_lp+=1;
      _tab.TYP:='KH';
      _tab.NAZ:=KH.NAZ;
      _tab.EM:=KH.EM;
      _tab.add();
      {? _em<>'' & _em=_tab.EM
      || _lp0:=_lp
      ?}
   ?};
   KH.cntx_pop();
   KH_OSOB.cntx_psh();
   KH_OSOB.index('KH');
   KH_OSOB.prefix(_kh);
   {? KH_OSOB.first()
   || {!
      |? {? KH_OSOB.EMAIL<>''
         || _tab.blank();
            _tab.LP:=_lp+=1;
            _tab.TYP:='OS';
            _tab.NAZ:=KH_OSOB.IMIE+' '+KH_OSOB.NAZWISKO;
            _tab.EM:=KH_OSOB.EMAIL;
            _tab.D:=KH_OSOB.D;
            _tab.add();
            {? _em<>'' & _em=_tab.EM & _lp0=0
            || _lp0:=_lp
            ?}
         ?};
         KH_OSOB.next()
      !}
   ?};
   KH_OSOB.cntx_pop();
   {? _tab.size()
   || {? _lp0>0
      || _tab.find_key(_lp0)
      || _tab.first()
      ?};
      {? _tab.select(,1)
      || {? DOKUM.TYP='K'
         || _wyn:=_em;
            {? exec('array_index_of','#array', _emtb, _tab.EM)<=0
            || {? _wyn<>''
               || _wyn+=';'
               ?};
               _wyn+=_tab.EM
            ?}
         || _wyn:=_tab.EM
         ?}
      || _wyn:=0
      ?}
   || FUN.info('Brak wprowadzonych adresów e-mail dla danego kontrahenta.'@);
      _wyn:=0
   ?};
   _tab.win_sel('');
   _tab.win_del(_o);
   obj_del(_tab)
|| FUN.info('Brak adresów e-mail.'@);
   _wyn:=0
?};
_wyn


\del_autoadd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Usuwa automatycznie dodane załączniki
::   WE: _a - TAB.ref()
::       _b - 0-komunikaty na ekran, 1-komunikaty do __komm
::   WY: -1 - nie było nic do usunięcia
::        0 - usunięcie nie powiodło się
::        1 - usunięcię powiodło się - nie Edokument
::        2 - usunięcię powiodło się - Edokument
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<>type_of(0) || _b:=0 ?};

_is_autodok:=0;
_count:=0;
_bl:=0;
DOKUM.cntx_psh();
_Names:=DOKUM.names();
_loop:=_Names.last();
{!
|? _loop
|!
   DOKUM.use(_Names.NAME);
   DOKUM.cntx_psh();
   DOKUM.index('AUTOADD');
   DOKUM.prefix(REF.FIRMA,$_a,'T');
   _loop:=DOKUM.first();
   {!
   |? _loop
   |!
      _bl:=DOKUM.BL='T';
      _loop:=
         {? exec('del_dok','dokum',1)
         || _count+=1
         || DOKUM.next()
         ?}
   !};
   _is_autodok:=DOKUM.first();
   DOKUM.cntx_pop();
   _loop:=_is_autodok=0 & _Names.prev()
!};
DOKUM.cntx_pop();
{? _is_autodok
|| {? _b
   || {? var_pres('__kom')>100 || __kom.add('Dokument %1 ma załączniki.'@[FAKS.SYM],7) ?}
   || FUN.info('Dokument %1 ma załączniki.'@[FAKS.SYM])
   ?};
   0
|| {? _count || {? _bl || 2 || 1 ?} || -1 ?}
?}


\edit_html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [20.42]
:: OPIS: Uruchamia edytorek HTMLa który zawartość będzie trzymał w blobie w tabeli
::       DOKUM
::   WE: [_a] - DOKUM.ref lub bieżący rekord
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
{? var_pres('_a')=type_of(DOKUM.ref())
|| _ref:=_a
?};

_result:=0;
_can_continue:=1;

DOKUM.cntx_psh();
{? _ref<>null()
|| DOKUM.prefix();
   {? DOKUM.seek(_ref)
   || _can_continue:=1
   || _can_continue:=0
   ?}
?};

{? _can_continue>0
|| _can_continue:=exec('edit_html_blob','#edit',DOKUM,'DOKUM','dokum_edit')
?};
DOKUM.cntx_pop();
{? _can_continue>0
|| _result:=1
?};
_result


\dokum_kh_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: przed edycją pola KH w tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
:: edycja kontrahenta dopuszczalna tylko w Kontrahenci, zakładka "Kontakty"
:: i w "Projekty", dla projrktu bez kontrahenta
_wyn:=0;
{? (KHVZ.KONTAKTY='T' & KHVZ.KHVP='dokum') | (KHVZ.KHVP='projekty' & PROJEKTY.KH=null())
|| _wyn:=1
?};
_wyn


\dokum_kh_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: po edycji pola KH w tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.KH_OSOB<>null()
|| {? DOKUM.KH=null() | DOKUM.KH_OSOB().KH<>DOKUM.KH
   || DOKUM.KH_OSOB:=null()
   ?}
?};
{? DOKUM.DOT().RODZ='M'
|| {? DOKUM.KH=null()
   || DOKUM.EM:=''
   || {? DOKUM.KH().EM<>'' & DOKUM.KH_OSOB=null()
      || DOKUM.EM:=DOKUM.KH().EM
      ?}
   ?}
?};
~~


\dokum_khosob_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: przed edycją pola KH_OSOB w tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? DOKUM.KH<>null()
|| DOKUM.DOT().RODZINF();
   {? RODZINF.NAZ<>'Dokument definiowalny' | ZDARZT.WYB_OSK
   || KH_OSOB.index('KH');
      KH_OSOB.prefix(DOKUM.KH);
      {? KHVZ.KONTAKTY<>'T' | KHVZ.KHVP<>'kh_osob'
      || _wyn:=1
      ?}
   ?}
?};
_wyn


\dokum_khosob_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: po edycji pola KH_OSOB w tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.DOT().RODZ='M'
|| {? DOKUM.KH_OSOB<>null() & DOKUM.KH_OSOB().EMAIL<>''
   || DOKUM.EM:=DOKUM.KH_OSOB().EMAIL
   ?}
?};

~~


\dokum_upraw_pom_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: obiekt zawierający nazwy uprawnienia użytkowników i ról do kontaktu (czyli rekordu tabeli DOKUM)
::----------------------------------------------------------------------------------------------------------------------
_pom:=obj_new('UM','UP','RM','RP');
_pom.UM:='Użytkownicy (modyfikacja):';
_pom.UP:='Użytkownicy (podgląd):';
_pom.RM:='Role (modyfikacja):';
_pom.RP:='Role (podgląd):';
_pom


\dokum_upraw_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: tymczasowa tabela zawierająca uprawnienia użytkowników i ról do kontaktu (czyli rekordu tabeli DOKUM)
::   WE: [_a] - napis zawierający uprawnienia w ustalonym formacie
::----------------------------------------------------------------------------------------------------------------------
_napis:='';
{? var_pres('_a')=type_of('')
|| _napis:=_a
?};

_pom:=exec('dokum_upraw_pom_obj','dokum');

_tab:=tab_tmp(3,
             'RODZ','STRING[1]','Użytkownik/Rola',
             'NAZ','STRING[100]','Nazwa',
             'NAZ1','STRING[30]','Nazwa',
             'MODYF','STRING[1]','Uprawnienie do modyfikacji');

_tnap:=spli_str(_napis,'\n');
{! _ii:=1..obj_len(_tnap)
|! _nap:=_tnap[_ii];
   _rodz:='';
   _mod:='';
   _dl0:=0;
   {? _nap*_pom.UM=1
   || _rodz:='U';
      _mod:='T';
      _dl0:=+_pom.UM
   |? _nap*_pom.RM=1
   || _rodz:='R';
      _mod:='T';
      _dl0:=+_pom.RM
   |? _nap*_pom.UP=1
   || _rodz:='U';
      _mod:='N';
      _dl0:=+_pom.UP
   |? _nap*_pom.RP=1
   || _rodz:='R';
      _mod:='N';
      _dl0:=+_pom.RP
   ?};
   {? _rodz<>''
   || _nap:=_dl0-_nap;
      _tnap1:=spli_str(_nap,',');
      {! _jj:=1..obj_len(_tnap1)
      |! _nap1:=|_tnap1[_jj];
         {? _rodz='U'
         || _kk:=_nap1*' (';
            {? _kk>0
            || _kk-=1;
               _nap1:=_kk+_nap1
            ?}
         ?};
         {? _nap1<>''
         || _tab.RODZ:=_rodz;
            _tab.NAZ:=_nap1;
            _tab.MODYF:=_mod;
            _tab.add()
         ?}
      !};
      obj_del(_tnap1)
   ?}
!};
_tab


\dokum_upraw_okno
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: okno edycji uprawnień użytkowników i ról do kontaktu (czyli rekordu tabeli DOKUM)
::   WE: [_a] - napis zawierający uprawnienia w ustalonym formacie
::   WY: napis zawierający uprawnienia w ustalonym formacie
::----------------------------------------------------------------------------------------------------------------------
_upraw0:='';
{? var_pres('_a')=type_of('')
|| _upraw0:=_a
?};
_upraw:=_upraw0;

_t_upraw0:=exec('dokum_upraw_tab','dokum',_upraw0);

_tab1:=tab_tmp(1,
               'USER','STRING[10]','Użytkownik'@,
               'NAME','STRING[50]','Dane użytkownika'@,
               'ACCESS','STRING[1]','Podgląd'@,
               'MODIFY','STRING[1]','Modyfikacja'@);
_tab2:=tab_tmp(1,
               'ROLE','STRING[100]','Rola'@,
               'ACCESS','STRING[1]','Podgląd'@,
               'MODIFY','STRING[1]','Modyfikacja'@);

USERS.cntx_psh();
USERS.index('USR_AKOD');
USERS.prefix('T');
{? USERS.first()
|| {!
   |? _naz:=USERS.KOD;
      _pdg:='N';
      _mod:='N';
      {? _t_upraw0.find_key('U',_naz,)
      || _pdg:='T';
         {? _t_upraw0.MODYF='T'
         || _mod:='T'
         ?}
      ?};
      _tab1.USER:=_naz;
      _tab1.NAME:=USERS.DANE;
      _tab1.ACCESS:=_pdg;
      _tab1.MODIFY:=_mod;
      _tab1.add();
      USERS.next()
   !}
?};
USERS.cntx_pop();

B_ROLE.cntx_psh();
B_ROLE.index('UNIK');
B_ROLE.prefix(REF.FIRMA);
{? B_ROLE.first()
|| {!
   |? _naz:=B_ROLE.NAME;
      _pdg:='N';
      _mod:='N';
      {? _t_upraw0.find_key('R',_naz,)
      || _pdg:='T';
         {? _t_upraw0.MODYF='T'
         || _mod:='T'
         ?}
      ?};
      _tab2.ROLE:=_naz;
      _tab2.ACCESS:=_pdg;
      _tab2.MODIFY:=_mod;
      _tab2.add();
      B_ROLE.next()
   !}
?};
B_ROLE.cntx_pop();

_okn1:=_tab1.mk_sel('Użytkownicy'@,'P',,'user',,,,,'U');
_tab1.win_fld(_okn1,,'USER',,,,,1);
_tab1.win_fld(_okn1,,'NAME',,,20,,1);
_tab1.win_fld(_okn1,,'ACCESS',,,,,0,,,'Czy nadano uprawnienie do przeglądania [T/N]?'@,2,,"'T'","'N'");
_tab1.win_fld(_okn1,,'MODIFY',,,,,0,,,'Czy nadano uprawnienie do modyfikacji [T/N]?'@,2,,"'T'","'N'");
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.MODIFY:='T';_tab.put()";
_tab1.win_act(_okn1,,'Formuła','&Modyfikacja'@@,,,_fml,,,1,,,'M');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.MODIFY:='N';_tab.put()";
_tab1.win_act(_okn1,,'Formuła','&Podgląd'@@,,,_fml,,,1,,,'P');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='N';_tab.MODIFY:='N';_tab.put()";
_tab1.win_act(_okn1,,'Formuła','&Odznacz'@@,,,_fml,,,1,,,'O');
_tab1.win_act(_okn1,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');

_okn2:=_tab2.mk_sel('Role'@,'P',,'role',,,,,'U');
_tab2.win_fld(_okn2,,'ROLE',,,30,,1);
_tab2.win_fld(_okn2,,'ACCESS',,,,,0,,,'Czy nadano uprawnienie do przeglądania [T/N]?'@,2,,"'T'","'N'");
_tab2.win_fld(_okn2,,'MODIFY',,,,,0,,,'Czy nadano uprawnienie do modyfikacji [T/N]?'@,2,,"'T'","'N'");
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.MODIFY:='T';_tab.put()";
_tab2.win_act(_okn2,,'Formuła','&Modyfikacja'@@,,,_fml,,,1,,,'M');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.MODIFY:='N';_tab.put()";
_tab2.win_act(_okn2,,'Formuła','&Podgląd'@@,,,_fml,,,1,,,'P');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='N';_tab.MODIFY:='N';_tab.put()";
_tab2.win_act(_okn2,,'Formuła','&Odznacz'@@,,,_fml,,,1,,,'O');
_tab2.win_act(_okn2,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');

_okn:=_tab1.grp_make('Uprawnienia'@,,'upr');
_tab1.grp_sel(_okn,,_okn1);
_tab1.grp_splt(_okn,,'vertical','role_panel');
_tab1.grp_sel(_okn,_tab2,_okn2);

_tab1.win_sel(_okn);

_tab2.first();
_fml:="_par:=params_get();
      _tab2:=_par.tab2;
      _okn2:=_par.okn2;
      grp_disp(_tab2,_okn2,0)";

params_set('tab2',_tab2,'okn2',_okn2);

{? _tab1.select(,,,,,_fml)

|| _pom:=exec('dokum_upraw_pom_obj','dokum');

   _upr1_m:='';
   _upr1_p:='';
   {? _tab1.first()
   || {!
      |? _pdg:=_tab1.ACCESS;
         _mod:=_tab1.MODIFY;
         {? _mod='T' | _pdg='T'
         || _naz:=_tab1.USER+' ('+_tab1.NAME+')';
           {? _mod='T'
           || {? _upr1_m<>''
              || _upr1_m+=', '
              ?};
              _upr1_m+=_naz
           || {? _upr1_p<>''
              || _upr1_p+=', '
              ?};
              _upr1_p+=_naz
           ?}
         ?};
         _tab1.next()
      !}
   ?};

   _upr2_m:='';
   _upr2_p:='';
   {? _tab2.first()
   || {!
      |? _pdg:=_tab2.ACCESS;
         _mod:=_tab2.MODIFY;
         {? _pdg='T' | _mod='T'
         || _naz:=_tab2.ROLE;
           {? _mod='T'
           || {? _upr2_m<>''
              || _upr2_m+=', '
              ?};
              _upr2_m+=_naz
           || {? _upr2_p<>''
              || _upr2_p+=', '
              ?};
              _upr2_p+=_naz
           ?}
         ?};
         _tab2.next()
      !}
   ?};

   _upraw:='';
   {? _upr1_m<>''
   || _upraw:=_pom.UM+' '+_upr1_m
   ?};
   {? _upr1_p<>''
   || {? _upraw<>''
      || _upraw+='\n'
      ?};
      _upraw+=_pom.UP+' '+_upr1_p
   ?};
   {? _upr2_m<>''
   || {? _upraw<>''
      || _upraw+='\n'
      ?};
      _upraw+=_pom.RM+' '+_upr2_m
   ?};
   {? _upr2_p<>''
   || {? _upraw<>''
      || _upraw+='\n'
      ?};
      _upraw+=_pom.RP+' '+_upr2_p
   ?}

?};

_upraw


\dokum_upraw_ustaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: edycja uprawnień użytkowników i ról do kontaktu (czyli rekordu tabeli DOKUM)
::----------------------------------------------------------------------------------------------------------------------
_upr0:=DOKUM.memo_txt(,,'UPRAW');
_upr:=exec('dokum_upraw_okno','dokum',_upr0);
{? _upr<>_upr0
|| DOKUM.memo_set(_upr,'UPRAW')
?};
''


\dokum_upraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: uprawnienie użytkownika do kontaktu (czyli rekordu tabeli DOKUM)
::   WE: [_a] - login użytkownika, pusty = zalogowany użytkownik
::       [_b] - 0 - sprawdzenie tylko uprawnień dodatkowych (domyślnie), 1 - również do czynności
::   WY: '' - uprawnienie do modyfikacji, P - uprawnienie do przeglądania, B - brak uprawnień
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & _a<>''
|| _user:=_a;
   _sysobj:=exec(,'__sysusr');
   _sysusr:=_sysobj.getUser(_user);
   _super:=_sysusr.Superuser
|| _user:=exec('name','users');
   _super:=sec_superuser()
?};
_spr_czynn:=0;
{? var_pres('_b')=type_of(0)
|| _spr_czynn:=_b
?};

_upr:='';
{? ~_super & DOKUM.OPER<>_user & DOKUM.OPER1<>_user
|| _t_upraw:=exec('dokum_upraw_tab','dokum',DOKUM.memo_txt(,1,'UPRAW'));
   {? _t_upraw.size()>0
   || _upr:='B';
      {? _t_upraw.find_key('U',_user,)
      || {? _t_upraw.MODYF='T'
         || _upr:=''
         || _upr:='P'
         ?}
      || _t_upraw.prefix('R');
         {? _t_upraw.first()
         || {!
            |? {? exec('user_role_chk','dokum',_user,_t_upraw.NAZ)
               || {? _t_upraw.MODYF='T'
                  || _upr:=''
                  || _upr:='P'
                  ?}
               ?};
               _upr='B' & _t_upraw.next()
            !}
         ?}
      ?}
   ?}
?};

{? _spr_czynn
|| _user_ref:=null();
   USERS.cntx_psh();
   {? exec('getUser','users',_user)
   || _user_ref:=USERS.ref()
   ?};
   USERS.cntx_pop();
   {? _upr='' & (_user_ref=null() | ~exec('chk_role','#b__box',_user_ref,'KKH_KON_DRKO'))
   || _upr:='P'
   ?};
   {? _upr='P' & (_user_ref=null() | ~exec('chk_role','#b__box',_user_ref,'KKH_KON_PKON'))
   || _upr:='B'
   ?}
?};

_upr


\user_role_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: sprawdzenie, czy użytkownik ma przypisaną rolę
::   WE: [_a] - kod użytkownika
::       [_b] - nazwa roli
::   WY: 0 - nie, 1 -tak
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
B_USRROL.cntx_psh();
B_USRROL.index('WER');
B_USRROL.prefix(REF.FIRMA,_a,_b,);
{? B_USRROL.first()
|| _jest:=1
?};
B_USRROL.cntx_pop();
_jest


\dokum_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: przed "wyświetl" tabeli DOKUM - dopuszczenie tylko uprawnionych użytkowników
::   WY: 0 - nie, 1 -tak
::----------------------------------------------------------------------------------------------------------------------
{? KHVZ.UPRAW='' | KHVZ.UPRAW='P'
|| {? DOKUM.EDOKUMR<>''
   || _edok:=exec('FindAndGet','#table',EDOKUM,DOKUM.EDOKUMR,,,null());
      _typobieg:=exec('FindAndGet','#table',EDOKUM,DOKUM.EDOKUMR,,
         "{? @.EDOKUM.TYP().TYPOBIEG().NAZWA='Obieg faktur' || 1 || 2 ?}
         ",1);
      {? _edok<>null()
      || OBIEGI.EDOKOS:=null();
         {? _typobieg=1
         || exec('dok_spac','obg',_edok)
         || exec('dok_spac','obe',_edok)
         ?};
         exec('make_contact_view','kontrahent_view')
      || DOKUM.display()
      ?}
   || DOKUM.display()
   ?}
?};
0


\dokum_field_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: przed wyświetleniem pola tabeli DOKUM - widoczne tylko dla uprawnionych użytkowników
::   WY: 1 - widoczne, 0 - nie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? (KHVZ.KONTAKTY='T' | KHVZ.KHVP='kn_nag') & KHVZ.UPRAW<>'' & KHVZ.UPRAW<>'P'
|| _wyn:=0
|? DOKUM.TYP='D' & DOKUM.KH<>null() & KHVZ.UPRAW<>'' & KHVZ.UPRAW<>'P'
|| _wyn:=0
?};
_wyn


\dokum_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: przed "szukaj" tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
PROJEKTY.actions('SLO','L');
PROJEKTY.win_dict('SLO');
DOKUM.win_patt('REDK');
exec('projekty_f_set','projekty',DOKUM,0);
1


\dokump_zapis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: zapis do tabeli DOKUMP (powiadomienia o kontaktach) po dopisaniu do tabeli DOKUM (kontakty)
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.KH<>null()
|| _powiad:=DOKUM.KH().memo_txt(,1,'POWIAD');
   _t_powiad:=exec('kh_powiad_tab','kontrahent',_powiad);
   {? _t_powiad.first()
   || DOKUMP.cntx_psh();
      DOKUMP.index('DISP_R');
      DOKUMP.prefix();
      USERS.cntx_psh();
      USERS.index('USR_AKOD');
      {!
      |? _user:=_t_powiad.USER;
         {? _user<>''
         || USERS.prefix('T',_user,);
            {? USERS.first() & USERS.EMAIL<>''
            || _upr:=exec('dokum_upraw','dokum',_user);
               {? _upr='' | _upr='P'
               || DOKUMP.prefix('M',DOKUM.ref(),USERS.ref());
                  {? ~DOKUMP.first()
                  || DOKUMP.blank();
                     DOKUMP.RODZ:='M';
                     DOKUMP.DOKUM:=DOKUM.ref();
                     DOKUMP.USER:=USERS.ref();
                     DOKUMP.EMAIL:=USERS.EMAIL;
                     DOKUMP.add()
                  ?};
                  DOKUMP.prefix()
               ?}
            ?}
         ?};
         _t_powiad.next()
      !};
      USERS.cntx_pop();
      DOKUMP.cntx_pop()
   ?}
?};

{? DOKUM.DOKUMK<>null()
|| DOKUMKU.cntx_psh();
   DOKUMKU.index('DOKUMK');
   DOKUMKU.prefix(DOKUM.DOKUMK,'U',);
   {? DOKUMKU.first()
   || DOKUMP.cntx_psh();
      DOKUMP.index('DISP_R');
      DOKUMP.prefix();
      {!
      |? _user:=DOKUMKU.USER().KOD;
         _user_ref:=DOKUMKU.USER;
         _user_email:=DOKUMKU.USER().EMAIL;
         {? _user_email<>''
         || _upr:=exec('dokum_upraw','dokum',_user);
            {? _upr='' | _upr='P'
            || DOKUMP.prefix('M',DOKUM.ref(),_user_ref);
               {? ~DOKUMP.first()
               || DOKUMP.blank();
                  DOKUMP.RODZ:='M';
                  DOKUMP.DOKUM:=DOKUM.ref();
                  DOKUMP.USER:=_user_ref;
                  DOKUMP.EMAIL:=_user_email;
                  DOKUMP.add()
               ?};
               DOKUMP.prefix()
            ?}
         ?};
         DOKUMKU.next()
      !};
      DOKUMP.cntx_pop()
   ?};
   DOKUMKU.cntx_pop()
?};

{? DOKUM.KH<>null()
|| _powiad:=DOKUM.KH().memo_txt(,1,'POWNTC');
   _n_powiad:=exec('kh_powiad_tab','kontrahent',_powiad);
   {? _n_powiad.first()
   || DOKUMP.cntx_psh();
      DOKUMP.index('DISP_R');
      DOKUMP.prefix();
      USERS.cntx_psh();
      USERS.index('USR_AKOD');
      {!
      |? _user:=_n_powiad.USER;
         {? _user<>''
         || USERS.prefix('T',_user,);
            {? USERS.first()
            || _upr:=exec('dokum_upraw','dokum',_user);
               {? _upr='' | _upr='P'
               || DOKUMP.prefix('N',DOKUM.ref(),USERS.ref());
                  {? ~DOKUMP.first()
                  || DOKUMP.blank();
                     DOKUMP.RODZ:='N';
                     DOKUMP.DOKUM:=DOKUM.ref();
                     DOKUMP.USER:=USERS.ref();
                     DOKUMP.NTC_ID:=exec('ntc_send','dokum');
                     DOKUMP.add()
                  ?};
                  DOKUMP.prefix()
               ?}
            ?}
         ?};
         _n_powiad.next()
      !};
      USERS.cntx_pop();
      DOKUMP.cntx_pop()
   ?}
?};

{? DOKUM.DOKUMK<>null()
|| DOKUMKU.cntx_psh();
   DOKUMKU.index('DOKUMK');
   DOKUMKU.prefix(DOKUM.DOKUMK,'N',);
   {? DOKUMKU.first()
   || DOKUMP.cntx_psh();
      DOKUMP.index('DISP_R');
      DOKUMP.prefix();
      {!
      |? _user:=DOKUMKU.USER().KOD;
         _user_ref:=DOKUMKU.USER;
         _user_email:=DOKUMKU.USER().EMAIL;
         _upr:=exec('dokum_upraw','dokum',_user);
         {? _upr='' | _upr='P'
         || DOKUMP.prefix('N',DOKUM.ref(),_user_ref);
            {? ~DOKUMP.first()
            || DOKUMP.blank();
               DOKUMP.RODZ:='N';
               DOKUMP.DOKUM:=DOKUM.ref();
               DOKUMP.USER:=USERS.ref();
               DOKUMP.NTC_ID:=exec('ntc_send','dokum');
               DOKUMP.add()
            ?};
            DOKUMP.prefix()
         ?};
         DOKUMKU.next()
      !};
      DOKUMP.cntx_pop()
   ?};
   DOKUMKU.cntx_pop()
?};

~~


\dokump_mail
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: wysyłanie maili wg zapisów w tabeli DOKUMP (powiadomienia o kontaktach) - testy, do usunięcia
::----------------------------------------------------------------------------------------------------------------------
DOKUMP.cntx_psh();
DOKUMP.index('SEND_R');
DOKUMP.prefix('M','N');
{? DOKUMP.first()
|| {!
   |? _nast:=null();
      DOKUMP.cntx_psh();
      {? DOKUMP.next()
      || _nast:=DOKUMP.ref()
      ?};
      DOKUMP.cntx_pop();
      {? DOKUMP.DOKUM().TYP='K' | DOKUMP.DOKUM().TYP='D'
      || exec('dokump_mail_core','dokum')
      ?};
      {? _nast<> null()
      || DOKUMP.seek(_nast)
      || 0
      ?}
   !}
?};
DOKUMP.cntx_pop();
1


\dokump_mail_core
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: wysyłanie maili wg zapisów w tabeli DOKUMP (powiadomienia o kontaktach) - testy, do usunięcia
::----------------------------------------------------------------------------------------------------------------------
_args:=exec('mp_run_a','#b__box');
_args.ACT_UID:='ZPR_SND_DOKU';
_args.UIDREF:=DOKUMP.DOKUM().uidref();
_args.AKCJA:='WYŚLIJ';
_args.PROC_START:='N';
_args.PORTS_IN:=exec('portsIn','#b__box',_args.ACT_UID);

exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'DOKUM',DOKUM.ref());
exec('portsInSet','#b__box',_args.PORTS_IN,_args.ACT_UID,'DOKUMP',DOKUMP.ref());

exec('mp_run','#b__box',_args);
1


\dokump_pokaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: pokazanie listy zapisów w tabeli DOKUMP (powiadomienia o kontaktach)
::   WE: _a - rodzaj DOKUMP.RODZ M-mail, K-kalendarz
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='K' | _a='M')
|| _rodz:=_a
|| _rodz:='M'
?};
DOKUMP.cntx_psh();
DOKUMPT.cntx_psh();
DOKUMP.index('DISP_R');
DOKUMPT.index('DISP');
{? _rodz='K'
|| DOKUMP.prefix('K',DOKUM.ref());
   DOKUMP.win_sel('WER_K');
   DOKUMP.win_edit('RED_K');
   DOKUMP.win_patt('RED_K')
|| _win_grp:=DOKUMP.grp_make();
   _formula:="
   DOKUMP.prefix('M',DOKUM.ref());
   DOKUMP.win_edit('RED');
   DOKUMP.win_patt('RED')
   ";
   DOKUMP.grp_sel(_win_grp,DOKUMP,'WER','Powiadomienia email'@,,,,,_formula,,,,'maximized');
   _formula:="
   DOKUMP.prefix('M',DOKUM.ref());
   DOKUMP.win_edit('RED_N');
   DOKUMP.win_patt('RED_N')
   ";
   DOKUMP.grp_sel(_win_grp,DOKUMP,'WER_N','Powiadomienia w aplikacji'@,,,,,"DOKUMP.prefix('N',DOKUM.ref())",,,,'maximized');
   DOKUMPT.prefix(DOKUM.ref());
   DOKUMP.grp_sel(_win_grp,DOKUMPT,'WER','Powiadomienia MS Teams'@,,,,,,,,,'maximized');
   DOKUMP.win_sel(_win_grp)

?};
DOKUMP.select();
DOKUMP.cntx_pop();
DOKUMPT.cntx_pop();
~~


\kol_dokum2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: kolory dla tabeli kontaktów DOKUM
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? DOKUM.RODZAJ_K='P'
|| _wyn:='DOKUM#02#01'
|? DOKUM.RODZAJ_K='W'
|| _wyn:='DOKUM#02#02'
|? DOKUM.RODZAJ_K='N'
|| _wyn:='DOKUM#02#03'
|| ''
?};
_wyn


\bd_dokumk_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Przed usunieciem rekordu z tabeli DOKUMK - slownik kategorii kontaktów z kontrahentem
::----------------------------------------------------------------------------------------------------------------------
{? DOKUMK.count()>0
|| FUN.emsg('Kategoria jest używana w systemie i nie można jej usunąć.'@);
   0
|| 1
?}


\chk_dokumk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: akcja rekord po dla slownika kotegorii kontaktów z kontrahentem DOKUMK
::----------------------------------------------------------------------------------------------------------------------
exec('chk_dokumk_core','dokum',-menu_txt()='popraw')


\chk_dokumk_core
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Walidacja rekordu tabeli DOKUMK (Kategoria kontaktów z kontrahentem)
::       - wołana z poziomu okna oraz funkcji importujących
::   WE: _a - specyfikacja testu: 1-poprawianie, 0-dołączanie
::   WY: akronim pola / ''
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.record(DOKUMK,,'NAZ');
{? _chk=''
|| {? _a
   || _ref:=DOKUMK.ref()
   || _ref:=null()
   ?};
   _naz:=DOKUMK.NAZ;
   DOKUMK.cntx_psh();
   DOKUMK.index('DOKUMK');
   DOKUMK.prefix(_naz,);
   {? DOKUMK.first() & (~_a | DOKUMK.ref()<>_ref)
   || FUN.info('Istnieje kategoria o nazwie: %1.'@[_naz]);
      _chk:='NAZ'
   ?};
   DOKUMK.cntx_pop()
?};
_chk


\dokump_dolacz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: dopisywanie powiadomień o kontakcie z kontrahentem - wybór użytkowników i informacja
::   WE: _a - rodzaj DOKUMP.RODZ M-mail, K-kalendarz
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') & (_a='K' | _a='M' | _a='N')
|| _rodz:=_a
|| _rodz:='M'
?};
_user:=exec('name','users');

_tab:=tab_tmp(1,
              'USER','STRING[10]','Użytkownik'@,
              'DANE','STRING[50]','Dane użytkownika'@,
              'EMAIL','STRING[100]','E-mail'@,
              'ACCESS','STRING[1]','Powiadomienie'@,
              'USER_REF','STRING[16]','Użytkownik'@);

USERS.cntx_psh();
USERS.index('USR_AKOD');
USERS.prefix('T');
{? USERS.first()
|| {!
   |? {? USERS.EMAIL<>'' | _rodz='N'
      || _upr:=exec('dokum_upraw','dokum',USERS.KOD);
         {? _upr='' | _upr='P'
         || _tab.USER_REF:=$USERS.ref();
            _tab.USER:=USERS.KOD;
            _tab.DANE:=USERS.DANE;
            _tab.EMAIL:=USERS.EMAIL;
            _tab.ACCESS:='N';
            _tab.add()
         ?}
      ?};
      USERS.next()
   !}
?};
USERS.cntx_pop();

{? _tab.size()=0
|| FUN.info('Brak użytkowników z adresem e-mail, uprawnionych do kontaktu.'@);
   return(~~)
?};

_okn:=_tab.mk_sel('Użytkownicy'@,'P',,'user',,,,,'U');
{? _rodz='K'
|| _tab.win_fld(_okn,,'ACCESS',,,,,0,,,'Przypomnienie [T/N]?'@,2,,"'T'","'N'")
|| _tab.win_fld(_okn,,'ACCESS',,,,,0,,,'Powiadamienie [T/N]?'@,2,,"'T'","'N'")
?};
_tab.win_fld(_okn,,'USER',,,20,,1);
_tab.win_fld(_okn,,'DANE',,,30,,1);
{? _rodz<>'N' || _tab.win_fld(_okn,,'EMAIL',,,30,,1) ?};
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='T';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Zaznacz'@@,,,_fml,,1,1,,,'Z');
_fml:="_tab:=cur_tab(1);_tab.ACCESS:='N';_tab.put()";
_tab.win_act(_okn,,'Formuła','&Odznacz'@@,,,_fml,,,1,,,'O');
_tab.win_act(_okn,,'Formuła','&Akceptuj'@@,,,"sel_exit()",,,,,,'A');
_fb:="
   _lastdraw:=1;
   {? var_pres('_a')=type_of(0)
   || _lastdraw:=_a
   ?};
   _grayed:='';
   _tab:=cur_tab();
   _sel:=cur_win();
   {? _lastdraw>0
   || {? _tab.sel_size()=0
::       Pojedyncze zaznaczenie
      || {? _tab.ACCESS='T'
         || _tab.actions(_sel,,'O',1);
            _grayed+='Z'
         |? _tab.ACCESS='N' | _tab.ACCESS=''
         || _tab.actions(_sel,,'Z',1);
            _grayed+='O'
         ?}
::       Zaznaczenie grupowe
      || _tab.actions(_sel,,'Z',1)
      ?};
      _tab.actions_grayed(_sel,_grayed)
   ?}
";
_tab.win_act(_okn,,'Rekord',,,,_fb);

_tab.win_sel(_okn);

{? _rodz='K'
|| _tab0:=tab_tmp(,'INFO','STRING[255]','Informacja'@,
                   'DT','DATE','Data przypomnienia'@,
                   'GD1','TIME','Od godziny'@,
                   'GD2','TIME','Do godziny'@,
                   'CZP','INTEGER','Przypomnienie (w minutach)'@);
   _tab0.DT:=date()
|? _rodz='N'
|| _tab0:=tab_tmp(,'INFO','STRING[255]','Informacja'@,
                   'DT','DATE','Data powiadomienia'@);
   _tab0.DT:=date()
|| _tab0:=tab_tmp(,'INFO','STRING[255]','Informacja'@)
?};

_tab0.add();
{? _rodz='K'
|| _okn0:=_tab0.mk_edit('Przypomnienie'@,0);
   _tab0.win_esep(_okn0,'Przypomnienie'@);
   _tab0.win_efld(_okn0,,'DT');
   _tab0.win_efld(_okn0,,'GD1',,,13);
   _tab0.win_efld(_okn0,,'GD2',,,13);
   _tab0.win_efld(_okn0,,'CZP',,,13);
   _tab0.efld_opt(_okn0,'mark=1',_tab0,'DT')
|? _rodz='N'
|| _okn0:=_tab0.mk_edit('Powiadomienie'@,0);
   _tab0.win_esep(_okn0,'Powiadomienie'@);
   _tab0.win_efld(_okn0,,'DT');
   _tab0.efld_opt(_okn0,'mark=1',_tab0,'DT')
|| _okn0:=_tab0.mk_edit('Dodatkowa informacja'@,0);
   _tab0.win_esep(_okn0,'Dodatkowa informacja'@)
?};
_tab0.win_efld(_okn0,,'INFO',,,100);
_tab0.win_ebtn(_okn0,'text='+'&Zapisz'@+',align=end',"'key:F2'");
_tab0.win_ebtn(_okn0,'text='+'&Anuluj'@+',align=end',"'key:Esc'");
_tab0.win_edit(_okn0);

{? _rodz<>'K' | ~_tab.find_key(_user)
|| _tab.first()
?};

{? _rodz='K'
|| _f_chk:="_wyn:=__CHK.record(cur_tab(),,'DT');
            {? _wyn=''
            || {? cur_tab().CZP<0
               || FUN.info('Czas przypomnienia nie może być ujemny.'@);
                  _wyn:='CZP'
               |? cur_tab().GD1>cur_tab().GD2
               || FUN.info('Godzina zakończenia nie może być wcześniejsza niż godzina rozpoczęcia.'@);
                  _wyn:='GD2'
               ?}
            ?};
            _wyn"
|? _rodz='N'
|| _f_chk:="_wyn:=__CHK.record(cur_tab(),,'DT');_wyn"
|| _f_chk:="1"
?};

{? _tab.select(,1)
|| _jest:=0;
   {? _tab.first()
   || {!
      |? {? _tab.ACCESS='T'
         || _jest:=1
         ?};
         ~_jest & _tab.next()
      !}
   ?};
   {? _jest & _tab0.edit(_f_chk)
   || USERS.cntx_psh();
      USERS.clear();
      _tab.first();
      {!
      |? {? _tab.ACCESS='T' & USERS.seek(_tab.USER_REF)
         || DOKUMP.blank();
            DOKUMP.RODZ:=_rodz;
            DOKUMP.DOKUM:=DOKUM.ref();
            DOKUMP.USER:=USERS.ref();
            DOKUMP.EMAIL:=_tab.EMAIL;
            DOKUMP.INFO:=_tab0.INFO;
            {? _rodz='K'
            || DOKUMP.DT:=_tab0.DT;
               DOKUMP.GD1:=_tab0.GD1;
               DOKUMP.GD2:=_tab0.GD2;
               DOKUMP.CZP:=_tab0.CZP
            |? _rodz='N'
            || DOKUMP.DT:=_tab0.DT;
               DOKUMP.NTC_ID:=exec('ntc_send','dokum')
            ?};
            DOKUMP.add()
         ?};
         _tab.next()
      !};
      USERS.cntx_pop()
   ?}
?};

~~


\dokump_popraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: poprawienie powiadomienia o kontakcie z kontrahentem - wybór użytkownika i informacja
::----------------------------------------------------------------------------------------------------------------------
{? DOKUMP.WO='T' | DOKUMP.ESEND='T'
|| {? DOKUMP.WO='T'
   || _txt:='Powiadomienie zostało wysłane mailem.'@
   |? DOKUMP.ESEND='T'
   || _txt:='Powiadomienie zostało przekazane do wysyłki mailem.'@
   || _txt:=''
   ?};
   FUN.info(_txt+'\n\n'+'Akcja niemożliwa.'@);
   return(0)
?};
{? DOKUMP.r_lock(1,1,1)
|| {? DOKUMP.RODZ='K'
   || DOKUMP.win_edit('RED_K');
      _f_chk:="_wyn:=__CHK.record(DOKUMP,,'DT');
               {? _wyn=''
               || {? DOKUMP.CZP<0
                  || FUN.info('Czas przypomnienia nie może być ujemny.'@);
                     _wyn:='CZP'
                  |? DOKUMP.GD1>DOKUMP.GD2
                  || FUN.info('Godzina zakończenia nie może być wcześniejsza niż godzina rozpoczęcia.'@);
                    _wyn:='GD2'
                  ?}
               ?};
               _wyn"
   || DOKUMP.win_edit('RED');
      _f_chk:="1"
   ?};
   {? DOKUMP.edit(_f_chk)
   || DOKUMP.put()
   ?};
   DOKUMP.r_unlock()
?};
~~


\dokump_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: usunięcie powiadomienia o kontakcie z kontrahentem
::----------------------------------------------------------------------------------------------------------------------
{? (DOKUMP.WO='T' | DOKUMP.ESEND='T') & DOKUMP.RODZ<>'N'
|| {? DOKUMP.WO='T'
   || _txt:='Powiadomienie zostało wysłane mailem.'@
   |? DOKUMP.ESEND='T'
   || _txt:='Powiadomienie zostało przekazane do wysyłki mailem.'@
   || _txt:=''
   ?};
   FUN.info(_txt+'\n\n'+'Akcja niemożliwa.'@);
   return(0)
?};
{? DOKUMP.r_lock(1,1,1)
|| {? FUN.ask('Usunąć powiadomienie?'@)
   || {? DOKUMP.RODZ='N' || ntc_eat(DOKUMP.NTC_ID) ?};
      DOKUMP.del(1)
   ?};
   DOKUMP.r_unlock()
?};
~~


\f3_emdw_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Akcja f3 dla załączników z poziomu faktur
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_tabela:=0;
_kh:=null();
_em:='';
{? DOKUM.TYP='K'
|| _kh:=DOKUM.KH;
   {? cur_afld()='EM_UDW'
   || _em0:=DOKUM.EM_UDW
   || _em0:=DOKUM.EM_DW
   ?};
   _emtb:=spli_str(_em0,';');
   {? obj_len(_emtb)>0
   || _em:=_emtb[1]
   ?};
   _tab:=tab_tmp(3,'LP','INTEGER','LP'@,
              'TYP','STRING[3]','Typ'@,
              'D','STRING[1]','Domyślny'@,
              'NAZ','STRING[91]','Nazwa'@,
              'EM','STRING[120]','e-mail'@
               );
   _o:=_tab.mk_sel('Adresy e-mail'@,'P',,,1,,,,'U');
   _tab.win_fld(_o,,'TYP',,,3);
   _tab.win_fld(_o,,'D',,,,,,,,,2,,"'T'","'N'");
   _tab.win_fld(_o,,'NAZ',,,60);
   _tab.win_fld(_o,,'EM',,,60);
   _tab.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1,,,,'W');
   _tab.win_act(_o,,'Kolejność');
   _tab.win_sel(_o);
   _lp:=0;
   _lp0:=0;
   {? _kh<>null()
   || KH.cntx_psh();
      KH.clear();
      {? KH.seek(_kh) & KH.EM<>''
      || _tab.blank();
         _tab.LP:=_lp+=1;
         _tab.TYP:='KH';
         _tab.NAZ:=KH.NAZ;
         _tab.EM:=KH.EM;
         _tab.add();
         {? _em<>'' & _em=_tab.EM
         || _lp0:=_lp
         ?}
      ?};
      KH.cntx_pop();
      KH_OSOB.cntx_psh();
      KH_OSOB.index('KH');
      KH_OSOB.prefix(_kh);
      {? KH_OSOB.first()
      || {!
         |? {? KH_OSOB.EMAIL<>''
            || _tab.blank();
               _tab.LP:=_lp+=1;
               _tab.TYP:='OS';
               _tab.NAZ:=KH_OSOB.IMIE+' '+KH_OSOB.NAZWISKO;
               _tab.EM:=KH_OSOB.EMAIL;
               _tab.D:=KH_OSOB.D;
               _tab.add();
               {? _em<>'' & _em=_tab.EM & _lp0=0
               || _lp0:=_lp
               ?}
            ?};
            KH_OSOB.next()
         !}
      ?};
      KH_OSOB.cntx_pop()
   ?};
   USERS.cntx_psh();
   USERS.index('USR_AKOD');
   USERS.prefix('T');
   {? USERS.first()
   || {!
      |? {? USERS.EMAIL<>''
         || _tab.blank();
            _tab.LP:=_lp+=1;
            _tab.TYP:='US';
            _tab.NAZ:=USERS.KOD+' ('+USERS.DANE+')';
            _tab.EM:=USERS.EMAIL;
            _tab.add();
            {? _em<>'' & _em=_tab.EM
            || _lp0:=_lp
            ?}
         ?};
         USERS.next()
      !}
   ?};
   USERS.cntx_pop();
   {? _tab.size()
   || {? _lp0>0
      || _tab.find_key(_lp0)
      || _tab.first()
      ?};
      {? _tab.select(,1)
      || _wyn:=_em0;
         {? exec('array_index_of','#array', _emtb, _tab.EM)<=0
         || {? _wyn<>''
            || _wyn+=';'
            ?};
            _wyn+=_tab.EM
         ?}
      || _wyn:=0
      ?}
   || FUN.info('Brak wprowadzonych adresów e-mail dla danego kontrahenta.\nBrak wprowadzonych adresów e-mail dla użytkowników.'@);
      _wyn:=0
   ?};
   _tab.win_sel('');
   _tab.win_del(_o);
   obj_del(_tab)
|| _wyn:=0
?};
_wyn


\dokum_okno_red
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Okno redakcji kontektu dobrane wg typu kontaktu
::----------------------------------------------------------------------------------------------------------------------
_okn:='REDK';
{? DOKUM.DOT<>null() & (DOKUM.TYP='K' | (DOKUM.TYP='D' & DOKUM.KH<>null()))
|| {? DOKUM.DOT().RODZINF().NAZ='Dokument definiowalny'
   || _okn:='REDD'
   ?};
   _rd:=DOKUM.DOT().RODZ;
   {? _rd='T'
   || _rd:='S'
   ?};
   {? _rd<>''
   || _okn:=_okn+'_'+_rd
   ?}
?};
_okn


\copy_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Kopiowanie kontaktu
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? ~DOKUM.r_lock(1,1,1)
|| FUN.info('Pozycja zablokowana przez innego użytkownika.\nKopiowanie pozycji niemożliwe.'@)
|| DOKUM.cntx_psh();
   _typ:='K';
   _ref_dok0:=DOKUM.ref();
   _ref_dok:=null();
   DOKUM.blank();
   DOKUM.bl_file('DOKUM',1);
   DOKUM.get();
   DOKUM.DATAKONT:=date();
   DOKUM.NR:=0;
   DOKUM.OPER:=exec('name','users');
   DOKUM.DATA:=exec('data','#blank');
   DOKUM.CZAS:=exec('time','#blank');
   {? DOKUM.RODZAJ_K='P'
   || DOKUM.RODZAJ_K:='W'
   |? DOKUM.RODZAJ_K='W'
   || DOKUM.RODZAJ_K:='P'
   ?};
   DOKUM.SW:='';
   DOKUM.WO:='N';
   DOKUM.AUTOADD:='N';
   DOKUM.BL_ID:=0;
   DOKUM.BSEND:='N';
   DOKUM.RECEIVED:='N';
   DOKUM.K_NAD:='N';
   DOKUM.ZAL:='N';
   DOKUM.memo_get(,'OPIS');
   DOKUM.memo_get(,'UPRAW');
   _pom_tab:=POM.TAB;
   _pom_typdok:=POM.TYPDOK;
   POM.TAB:='DOKUM';
   POM.TYPDOK:='SYS';
   exec('add_grnr','numery','SYS');
   DOKUM.NR:=exec('numer_new','numery','PACZKA');
   DOKUM.prefix();
   {? params_exec('add_dok','dokum')
   || _ref_dok:=DOKUM.ref()
   ?};
   POM.TAB:=_pom_tab;
   POM.TYPDOK:=_pom_typdok;
   DOKUM.cntx_pop();
   {? _ref_dok=null() | ~DOKUM.seek(_ref_dok)
   || DOKUM.seek(_ref_dok0)
   ?};
   POM.TAB:=_pom_tab;
   POM.TYPDOK:=_pom_typdok
?};
_wyn


\dokum_zal_ustaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.14]
:: OPIS: Ustawienie pola DOKUM.ZAL - czy kontakt ma załączniki
::----------------------------------------------------------------------------------------------------------------------
DOKUMZ.cntx_psh();
DOKUMZ.index('DISP');
DOKUMZ.prefix(DOKUM.ref());
{? DOKUMZ.first()
|| _zal:='T'
|| _zal:='N'
?};
DOKUMZ.cntx_pop();
{? DOKUM.ZAL<>_zal
|| DOKUM.ZAL:=_zal;
   DOKUM.put(1)
?};
~~


\dokum_rodzaj_k_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Przed redagowaniem DOKUM.RODZAJ_K
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
DOKUM.DOT().RODZAJ_K=''


\kn_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Dodanie nowej pozycji do KN_NAG
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KN_NAG.cntx_psh();
KN_NAG.index('DUCU');
KN_NAG.prefix();
_ref:=null;
KN_NAG.cntx_pop();
USERS.cntx_psh();
USERS.seek(OPERATOR.USER);
_user:=USERS.DANE;
USERS.cntx_pop();

:KN_NAG.cntx_psh();
KN_NAG.win_edit('RED');
KN_NAG.prefix();
KN_NAG.blank();
KN_NAG.AUTOR:=_user;
KN_NAG.NR_UM:=exec('get','#params',8400,,OPERATOR.USER);
KN_NAG.NAD_W:=exec('get','#params',8401,,OPERATOR.USER);
KN_NAG.DATA:=date();
KN_NAG.efld_opt('RED','editable=0',,'AUTOR');
KN_NAG.efld_opt('RED','editable=1',,'DATA');
KN_NAG.efld_opt('RED','editable=0',,'STATUS');
KN_NAG.efld_opt('RED','editable=1',,'OPIS');

{? KN_NAG.edit() ||
   KN_NAG.STATUS:='Otwarty';
      KN_NAG.cntx_psh();
      KN_NAG.index('DUCU');
      KN_NAG.prefix();
      KN_NAG.cntx_pop();
   KN_NAG.DU:=date();
   KN_NAG.CU:=time();
   KN_NAG.add();
   _ref:=KN_NAG.ref()
?};
:KN_NAG.cntx_pop();
:KN_NAG.first();
KN_NAG.f_rfresh();
~~


\kn_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Modyfikowanie pozycji z tabeli KN_NAG
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',KN_NAG,KN_NAG.ref());
{? _r_lock
||
   KN_NAG.cntx_psh();
   KN_NAG.win_edit('RED');
   KN_NAG.prefix();
:KN_NAG.blank();
:KN_NAG.efld_opt('RED','editable=0',,'NR');
   KN_NAG.efld_opt('RED','editable=0',,'AUTOR');
   KN_NAG.efld_opt('RED','editable=0',,'DATA');
   {? KN_NAG.STATUS='Otwarty'
   || KN_NAG.efld_opt('RED','editable=1',,'STATUS');
      KN_NAG.efld_opt('RED','editable=1',,'OPIS');
      KN_NAG.efld_opt('RED','editable=1',,'DATA')
   || KN_NAG.efld_opt('RED','editable=0',,'STATUS');
      KN_NAG.efld_opt('RED','editable=0',,'OPIS');
      KN_NAG.efld_opt('RED','editable=0',,'DATA');
      KN_NAG.STATUS:='T'
   ?};

   {? KN_NAG.edit() ||
      KN_NAG.STATUS:={? KN_NAG.STATUS='T' || 'Wysłany' || 'Otwarty' ?};
      KN_NAG.put();
      {? KN_NAG.STATUS = 'Wysłany' ||
         KN_POZ.cntx_psh(); DOKUM.cntx_psh();
         KN_POZ.index('NAGLP');
         KN_POZ.prefix(KN_NAG.ref);
         {? KN_POZ.first() ||
            {! |?
               DOKUM.use(ref_name(KN_POZ.DOKUM));
               DOKUM.prefix();
               DOKUM.seek(KN_POZ.DOKUM);
               DOKUM.ESEND:='T';
               DOKUM.put();
               KN_POZ.next()
            !}
         ?};
         KN_POZ.cntx_pop(); DOKUM.cntx_pop()
      ?}
   ?};
   KN_NAG.cntx_pop();
   exec('r_unlock_one','#table',KN_NAG,KN_NAG.ref(),_r_lock)
|| FUN.info('Pozycja wykorzystywana przez innego użytkownika.'@)
?};
~~


\kn_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Usuwanie pozycji z tabeli KN_NAG
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',KN_NAG,KN_NAG.ref());
{? _r_lock
||
   {? KN_NAG.STATUS='Wysłany'
   || FUN.emsg('Książka nadawcza nie może zostać usunięta, ponieważ została już wysłana.'@)
   ||
      _odp:=FUN.ask('Czy usunąć książkę nadawczą?'@);
      {? _odp=1 ||
         KN_POZ.cntx_psh();DOKUM.cntx_psh();
         KN_POZ.index('NAGLP');DOKUM.index('DOKUM');
         KN_POZ.prefix(KN_NAG.ref);DOKUM.prefix(REF.FIRMA,$KN_NAG.ref());
         {? KN_POZ.first() | DOKUM.first()
         || FUN.emsg('Książka nadawcza nie może zostać usunięta, ponieważ posiada powiązane dokumenty.'@)
         || KN_NAG.del()
         ?};
         KN_POZ.cntx_pop(); DOKUM.cntx_pop()
      ?}
   ?};
   exec('r_unlock_one','#table',KN_NAG,KN_NAG.ref(),_r_lock)
|| FUN.info('Pozycja wykorzystywana przez innego użytkownika.'@)
?};
~~


\kn_show
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Okienko wertowania pozycji z tabeli KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref_nag:=KN_NAG.ref();
KN_POZ.cntx_psh();
KN_POZ.win_sel('WER');
KN_POZ.index('NAGLP');
KN_POZ.prefix(_ref_nag);
::KN_POZ.efld_opt('RED','editable=0',KN_NAG,'NR');
::KN_POZ.efld_opt('RED','editable=0',KN_NAG,'DATA');
::KN_POZ.efld_opt('RED','enable=1,editable=1',,'POLEC');
::KN_POZ.efld_opt('RED','enable=1',,'ZPO');
KN_POZ.select();
KN_POZ.cntx_pop();
~~


\kn_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Dodanie nowej pozycji do KN_NAG
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',KN_NAG,KN_NAG.ref());
{? _r_lock
||
   {? KN_NAG.STATUS='Otwarty'
   || _odp:=FUN.ask('Czy zmienić status na \'Wysłany\'?'@);
      {? _odp=1
      || KN_NAG.STATUS:='Wysłany'; KN_NAG.put();

         KN_POZ.cntx_psh(); DOKUM.cntx_psh();
         KN_POZ.index('NAGLP');
         KN_POZ.prefix(KN_NAG.ref);
         {? KN_POZ.first() ||
            {! |?
               DOKUM.use(ref_name(KN_POZ.DOKUM));
               DOKUM.prefix();
               DOKUM.seek(KN_POZ.DOKUM);
               DOKUM.ESEND:='T';
               DOKUM.put();
               KN_POZ.next()
            !}
         ?};
         KN_POZ.cntx_pop(); DOKUM.cntx_pop();

         FUN.emsg('Pomyślnie zmieniono status na \'Wysłany\'.'@)
      ?}
   || FUN.emsg('Wybrany dokument posiada już status \'Wysłany\'.'@)
   ?};
   exec('r_unlock_one','#table',KN_NAG,KN_NAG.ref(),_r_lock)
|| FUN.info('Pozycja wykorzystywana przez innego użytkownika.'@)
?};
~~


\knp_add2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Dodanie nowej pozycji do tabeli KN_POZ - okienko wertowania - OLD
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? KN_NAG.STATUS<>'Wysłany'
||
   DOKUM.cntx_psh();
   DOKUMLOG.cntx_psh();
   DOKUM.use('dokum');
   DOKUMLOG.use('dolog');
   _ind:=DOKUM.ndx_tmp(,1,'FIRMA',,,'RODZAJ_K',,,'ESEND',,,'DLA_KH',,);
   {? _ind <> ''
   ||
      DOKUM.index(_ind);
      DOKUM.prefix(REF.FIRMA,'W','N',1);
      _win_sel:=DOKUM.mk_sel('Wybierz dokument'@,,0,,,,,,'U');
      _win_acr:=DOKUM.win_sel(_win_sel);
      DOKUM.win_fld(_win_acr,,'DATAKONT',,,,,,);
      DOKUM.win_fld(_win_acr,,'DOT','ZDARZ',,20,,);
      DOKUM.win_fld(_win_acr,,'KH_OSOB','NAZWISKO',,20,,,);
      DOKUM.win_fld(_win_acr,,'KH_OSOB','IMIE',,20,,,);
      DOKUM.win_fld(_win_acr,,'OPER',,,,,,);
      DOKUM.win_fld(_win_acr,,'KR_OP',,,30,,,);
      DOKUM.win_fld(_win_acr,,'KH','NAZ_P',,30,,,);
      DOKUM.win_act(_win_acr,,'Formuła','Wybierz'@@,,,
         "exec('knp_sel','dokum')",,1,1,"exec('knp_sel_gr','dokum')",,'W',1);

      DOKUM.select()
   ?};

   DOKUM.cntx_pop();
   DOKUMLOG.cntx_pop()
|| FUN.emsg('Wybrany dokument został już wysłany. Nie można dodawać pozycji.'@)
?};
~~


\knp_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Dodanie nowej pozycji do tabeli KN_POZ - okienko wertowania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:DOKUM.cntx_psh();
{? DOKUM.name()<>'dokum'
|| DOKUM.use('dokum')
?};
{? BEER.KNP_DOD=date(0,0,0) || BEER.KNP_DOD:=date() ?};
{? BEER.KNP_DDO=date(0,0,0) || BEER.KNP_DDO:=date() ?};
{? KN_NAG.ref()<>null ||
   {? KN_NAG.STATUS<>'Wysłany' ||
      VAR_DEL.delete('__DDOKUM');
      __DDOKUM:=sql('SELECT DOKUM.FIRMA FIRMA, DOKUM.DATAKONT DATAKONT, DOKUM.DOT DOT, KH_OSOB.NAZWISKO NAZWISKO, KH_OSOB.IMIE IMIE'
                  ', DOKUM.OPER OPER, KH.NAZ_P NAZ_P, DOKUM.RODZAJ_K RODZAJ_K, DOKUM.ESEND ESEND, '
                  'DOKUM.DLA_KH DLA_KH, DOKUM.KR_OP KR_OP, ZDARZT.ZDARZ ZDARZ, DOKUM.REFERENCE DOKUM, DOKUM.NR DNUMER, DOKUM.K_NAD K_NAD '
                  'FROM DOKUM LEFT JOIN KH USING(DOKUM.KH,KH.REFERENCE) '
                  'LEFT JOIN KH_OSOB USING(DOKUM.KH_OSOB,KH_OSOB.REFERENCE) '
                  'LEFT JOIN ZDARZT USING(DOKUM.DOT,ZDARZT.REFERENCE)'
                  'WHERE DOKUM.DATAKONT :_b to_date(:_a) AND DOKUM.DATAKONT :_d to_date(:_c) AND DOKUM.ESEND = \'N\' AND DOKUM.K_NAD<>\'T\' '
                  ,BEER.KNP_DOD, '>=', BEER.KNP_DDO, '<=' );
:                 ,date()-BEER.KNP_ZAKR, '>=');
      _ind:=__DDOKUM.ndx_tmp(,1,'FIRMA',,,'RODZAJ_K',,,'ESEND',,,'DLA_KH',,,'DATAKONT',,1,'DNUMER',,1);
      {? _ind <> '' ||
         __DDOKUM.index(_ind);
         __DDOKUM.prefix($REF.FIRMA,'W','N',1,);
         DOKUM.cntx_psh();
         {? __DDOKUM.first() ||
            {! |?
               _dokum_ref:=exec('FindAndGet','#table',DOKUM,__DDOKUM.DOKUM,,,null());
               DOKUM.prefix();
               DOKUM.seek(_dokum_ref);
               _upr:=exec('dokum_upraw','dokum');
               {? _upr<>'' || __DDOKUM.del() || __DDOKUM.next() ?}
            !}
         ?};
         DOKUM.cntx_pop();

         _win_acr:=__DDOKUM.win_sel(__DDOKUM.mk_sel('Wybierz dokument'@,,0,,,,,,'U'));
         __DDOKUM.win_fld(_win_acr,,'DATAKONT',,,16,,,'Data kontaktu'@);
         __DDOKUM.win_fld(_win_acr,,'ZDARZ',,,15,,,'Typ'@);
         __DDOKUM.win_fld(_win_acr,,'NAZWISKO',,,20,,,'Nazwisko'@);
         __DDOKUM.win_fld(_win_acr,,'IMIE',,,20,,,'Imię'@);
         __DDOKUM.win_fld(_win_acr,,'OPER',,,15,,,'Operator'@);
         __DDOKUM.win_fld(_win_acr,,'KR_OP',,,30,,,'Temat'@);
         __DDOKUM.win_fld(_win_acr,,'NAZ_P',,,30,,,'Kontrahent'@);
         __DDOKUM.win_act(_win_acr,,'Formuła','Wybierz'@@,,,
         "exec('knp_sel','dokum')",,1,1,"exec('knp_sel_gr','dokum')",,'W',1);
         __DDOKUM.win_act(_win_acr,,'Formuła','Zmień zakres'@@,,,
         "exec('knp_chg_time','dokum')",,,,,,'Z',1);
         __DDOKUM.win_act(_win_acr,1,'Formuła','Zmień zakres'@@,,,
         "exec('knp_chg_time','dokum')",,,,,,'Z',1);
         __DDOKUM.win_act(_win_acr,,'Formuła','Szczegóły'@@,,,
         "exec('ddokum_dokum_bd','dokum')",,,,,,'S',1);
         __DDOKUM.select()

      ?};
      VAR_DEL.delete('__DDOKUM')
   ||
      FUN.emsg('Wybrany dokument został już wysłany. Nie można dodawać nowych pozycji.'@)
   ?}
||
   FUN.emsg('Brak książki nadawczej do której można dodać pozycję.'@)
?};
:DOKUM.cntx_pop();
~~


\knp_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Dodanie wybranej pozycji z tabeli DOKUM do tabeli KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? __DDOKUM.sel_size()=0 ||
   _nag_ref:=KN_NAG.ref();
:   _dokum_ref:=DOKUM.ref();
   _dokum_ref:=exec('FindAndGet','#table',DOKUM,__DDOKUM.DOKUM,,,null());
   _ddokum_ref:=__DDOKUM.ref();
   DOKUM.cntx_psh();
   DOKUM.prefix();
   DOKUM.seek(_dokum_ref);
   KN_POZ.cntx_psh();

   _r_lock:=exec('r_lock_one','#table',DOKUM,DOKUM.ref());
   {? _r_lock
   ||
      KN_POZ.KN_NAG:=_nag_ref;
      KN_POZ.KAT:=DOKUM.DOKUMK().NAZ;
      KN_POZ.KH:=DOKUM.KH;
      KN_POZ.DATA:=DOKUM.DATA;
:   KN_POZ.POLEC:={? 1 || 'T' || DOKUM.P_POL ?};
:   KN_POZ.POLEC:=exec('get','#params',700018,2);
:   KN_POZ.POLEC:=exec('get','#params',700900,2);
:   KN_POZ.NR_POLEC:=DOKUM.P_PNR;
      {? exec('get','#params',700900,2)='T'
      || KN_POZ.POLEC:='T'
      || KN_POZ.POLEC:=DOKUM.P_POL
      ?};
      KN_POZ.NR_POLEC:='';
      KN_POZ.KURIER:='N';
      KN_POZ.NR_PRZES:='';
      KN_POZ.PRIORYT:='N';
      KN_POZ.PACZKA:='N';
      KN_POZ.ZPO:='N';
      KN_POZ.DOKUM:=_dokum_ref;

      {? DOKUM.K_NAD='T'
      || FUN.info('Dokument znajduje się już w książce nadawczej.'@)
      ||
         KN_POZ.LP:=exec('knp_lp_gen','dokum');
         KN_POZ.add();

         DOKUM.cntx_psh();
         DOKUM.prefix();
         {? DOKUM.seek(_dokum_ref)
         || DOKUM.K_NAD:='T';
            DOKUM.put()
         ?};
         DOKUM.cntx_pop()
      ?};

      __DDOKUM.cntx_psh();
      __DDOKUM.prefix();
      {? __DDOKUM.seek(_ddokum_ref)
      ||
         __DDOKUM.del()
      ?};
      __DDOKUM.cntx_pop();

:   FUN.info('Wybrana pozycja została dodana do książki nadawczej.'@);
:      DOKUM.cntx_pop();
:      KN_POZ.cntx_pop();
      exec('r_unlock_one','#table',DOKUM,DOKUM.ref(),_r_lock)
   || FUN.info('Pozycja wykorzystywana przez innego użytkownika.'@)
   ?};
   DOKUM.prefix();
   DOKUM.cntx_pop();
   KN_POZ.cntx_pop()
?};
~~


\knp_sel_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Dodanie nowej pozycji do KN_NAG
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_tab:=__DDOKUM.sel_aget();
_nag_ref:=KN_NAG.ref();
DOKUM.cntx_psh();
KN_POZ.cntx_psh();
{? _tab.last ||
   {! |?
      __DDOKUM.seek(_tab.REF,);
:      _dokum_ref:=DOKUM.ref();
      _dokum_ref:=exec('FindAndGet','#table',DOKUM,__DDOKUM.DOKUM,,,null());
      _ddokum_ref:=__DDOKUM.ref();
      DOKUM.cntx_psh();
      DOKUM.prefix();
      DOKUM.seek(_dokum_ref);

      KN_POZ.KN_NAG:=_nag_ref;
      KN_POZ.KAT:=DOKUM.DOKUMK().NAZ;
      KN_POZ.KH:=DOKUM.KH;
      KN_POZ.DATA:=DOKUM.DATA;
:      KN_POZ.POLEC:={? 1 || 'T' || DOKUM.P_POL ?};
:      KN_POZ.POLEC:=exec('get','#params',700018,2);
:      KN_POZ.POLEC:=exec('get','#params',700900,2);
:      KN_POZ.NR_POLEC:=DOKUM.P_PNR;
      {? exec('get','#params',700900,2)='T'
      || KN_POZ.POLEC:='T'
      || KN_POZ.POLEC:=DOKUM.P_POL
      ?};
      KN_POZ.NR_POLEC:='';
      KN_POZ.KURIER:='N';
      KN_POZ.NR_PRZES:='';
      KN_POZ.PRIORYT:='N';
      KN_POZ.PACZKA:='N';
      KN_POZ.ZPO:='N';
      KN_POZ.DOKUM:=_dokum_ref;

      KN_POZ.LP:=exec('knp_lp_gen','dokum');
      KN_POZ.add();

      DOKUM.cntx_psh();
      DOKUM.prefix();
      {? DOKUM.seek(_dokum_ref)
      || DOKUM.K_NAD:='T';
         DOKUM.put()
      ?};
      DOKUM.cntx_pop();

      __DDOKUM.cntx_psh();
      __DDOKUM.prefix();
      {? __DDOKUM.seek(_ddokum_ref)
      ||
         __DDOKUM.del()
      ?};
      __DDOKUM.cntx_pop();

      DOKUM.cntx_pop();
      _tab.prev
   !}
?};

FUN.info('Wybrane pozycje zostały dodane do książki nadawczej.'@);

__DDOKUM.sel_adel;
DOKUM.cntx_pop();
KN_POZ.cntx_pop();
~~


\knp_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Usunięcie pozycji z tabeli KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? KN_POZ.sel_size()=0 ||
   _r_lock:=exec('r_lock_one','#table',KN_POZ,KN_POZ.ref());
   {? _r_lock
   ||
      _nag_ref:=KN_NAG.ref();
      _dokum_ref:=KN_POZ.DOKUM;

      _odp:=FUN.ask('Czy usunąć pozycję z książki nadawczej?'@);
      {? _odp=1 ||
         KN_NAG.cntx_psh();
         KN_NAG.prefix();
         {? KN_NAG.seek(_nag_ref) ||
            {? KN_NAG.STATUS='Otwarty'
            ||
               KN_POZ.del();
               DOKUM.cntx_psh();
:      {? DOKUM.seek(_dokum_ref) || DOKUM.ESEND:='N'; DOKUM.put() ?};
               DOKUM.use(ref_name(_dokum_ref));
               DOKUM.prefix();
               {? DOKUM.seek(_dokum_ref) || DOKUM.K_NAD:='N'; DOKUM.put() ?};
               DOKUM.cntx_pop();
               exec('nummggrp','dokum')
            || FUN.emsg('Nie można usunąć dokumentu, ponieważ został już wysłany.'@)
            ?}
         ?};
         KN_NAG.cntx_pop()
      ?};
      exec('r_unlock_one','#table',KN_POZ,KN_POZ.ref(),_r_lock)
   || FUN.info('Pozycja wykorzystywana przez innego użytkownika.'@)
   ?}
?};
~~


\knp_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Modyfikowanie wartości dla tabeli KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_r_lock:=exec('r_lock_one','#table',KN_POZ,KN_POZ.ref());
{? _r_lock
||
   {? KN_NAG.STATUS<>'Wysłany'
   ||
      KN_POZ.cntx_psh();
      KN_POZ.win_edit('RED');
      KN_POZ.prefix();
      {? KN_POZ.POLEC='T' || KN_POZ.efld_opt('RED','enable=1',,'NR_POLEC') || KN_POZ.efld_opt('RED','enable=0',,'NR_POLEC') ?};
      {? KN_POZ.KURIER='T' || KN_POZ.efld_opt('RED','enable=1',,'NR_PRZES') || KN_POZ.efld_opt('RED','enable=0',,'NR_PRZES') ?};

      {? KN_POZ.edit()
      ||
         KN_POZ.put()
      ?};

      KN_POZ.cntx_pop()
   ||
      KN_POZ.win_edit('RED');
      KN_POZ.display()
   ?};
   exec('r_unlock_one','#table',KN_POZ,KN_POZ.ref(),_r_lock)
|| FUN.info('Pozycja wykorzystywana przez innego użytkownika.'@)
?};
~~


\knp_pol_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Formuła przed redagowaniem, ustawiająca tryb modyfikacji dla pola nr_polec KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_red:=KN_POZ.win_edit('?');
{? _win_red='RED' ||
   _e1:='enable=1';
   _e0:='enable=0';
   {? KN_POZ.POLEC='T'
   || KN_POZ.efld_opt(_win_red,_e1,,'NR_POLEC')
   || KN_POZ.efld_opt(_win_red,_e0,,'NR_POLEC')
   ?}
?};
~~


\knp_kur_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Formuła przed redagowaniem, ustawiająca tryb modyfikacji dla pola nr_przes KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_red:=KN_POZ.win_edit('?');
{? _win_red='RED' ||
   _e1:='enable=1';
   _e0:='enable=0';
   {? KN_POZ.KURIER='T'
   || KN_POZ.efld_opt(_win_red,_e1,,'NR_PRZES')
   || KN_POZ.efld_opt(_win_red,_e0,,'NR_PRZES')
   ?}
?};
~~


\kn_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Formuła przed wyświetleniem, wyszarzająca akcje dla tabeli KN_NAG
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win_sel:=KN_NAG.win_sel('WER');
{? (KN_NAG.STATUS='Wysłany' & KN_NAG.sel_size()=0)
|| KN_NAG.actions_grayed(_win_sel, 'WPU')
|| KN_NAG.actions_grayed(_win_sel, '')
?}


\knnag_word
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Wydruk przesyłek krajowych do WORD
::----------------------------------------------------------------------------------------------------------------------
_env:=obj_new('DOKL'); _env.DOKL:=ST.DOKL;
params_set('env',_env);
POLA.REFSQL:=$KN_NAG.ref();

{? exec('get','#params',100233)='T' | FUN.ask('Dodać wygenerowany dokument do załączników książki nadawczej?'@)
||
   params_exec('generuj','szablon_zws','KKH_PRZESYLKI_KRAJ',$KN_NAG.ref(),1)
||
   params_exec('generuj','szablon_zws','KKH_PRZESYLKI_KRAJ',$KN_NAG.ref())
?}


\knp_dokum_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: przed "wyświetl" tabeli DOKUM - dopuszczenie tylko uprawnionych użytkowników
::   WY: 0 - nie, 1 -tak
::----------------------------------------------------------------------------------------------------------------------
_win:={? DOKUM.DOT().ZDARZ='EMAIL' ||'REDK_M'
      |? DOKUM.DOT().ZDARZ='PISMO' ||'REDK_P'
      |? DOKUM.DOT().ZDARZ='SPOTKANIE' | DOKUM.DOT().ZDARZ='TELEFON' ||'REDK_S'
      || 'REDK' ?};
DOKUM.win_edit(_win);
DOKUM.cntx_psh();
DOKUM.use(ref_name(KN_POZ.DOKUM));
DOKUM.prefix();
DOKUM.seek(KN_POZ.DOKUM);
{? KHVZ.UPRAW='' | KHVZ.UPRAW='P'
|| DOKUM.display()
?};
DOKUM.cntx_pop();
0


\knp_chg_time
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: okienko redagowania tabeli KN_POZ - określenie zakresu czasowego
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_okno:=__DDOKUM.mk_edit('Zakres dat'@,0);
:__DDOKUM.erase();
{? BEER.KNP_DOD=date(0,0,0) || BEER.KNP_DOD:=date() ?};
{? BEER.KNP_DDO=date(0,0,0) || BEER.KNP_DDO:=date() ?};
:__DDOKUM.win_efld(_okno, BEER, 'KNP_ZAKR',,,,,,'Dni wstecz');
__DDOKUM.win_efld(_okno, BEER, 'KNP_DOD',,,,,,'Data od'@);
__DDOKUM.win_efld(_okno, BEER, 'KNP_DDO',,,,,,'Data do'@);
__DDOKUM.win_ebtn(_okno,'text='+'&Zapisz'@+',align=end',"'key:F2'");
__DDOKUM.win_ebtn(_okno,'text='+'&Anuluj'@+',align=end',"'key:Esc'");
__DDOKUM.win_edit(_okno);
:{? __DDOKUM.edit("{? BEER.KNP_ZAKR<0 || FUN.info('Liczba dni nie może być ujemna.'@) || '' ?}")
{? __DDOKUM.edit("{? BEER.KNP_DDO<BEER.KNP_DOD || FUN.info('Data od nie może być późniejsza niż Data do.'@)
                  |? BEER.KNP_DDO=date(0,0,0)  || FUN.info('Data do musi być ustawiona.'@)
                  |? BEER.KNP_DOD=date(0,0,0)  || FUN.info('Data od musi być ustawiona.'@)
                  || '' ?}")
||
::   _DDOKUM:=sql('SELECT DOKUM.FIRMA FIRMA, DOKUM.DATAKONT DATAKONT, DOKUM.DOT DOT, KH_OSOB.NAZWISKO NAZWISKO, KH_OSOB.IMIE IMIE'
::            ', DOKUM.OPER OPER, KH.NAZ_P NAZ_P, DOKUM.RODZAJ_K RODZAJ_K, DOKUM.ESEND ESEND, '
::            'DOKUM.DLA_KH DLA_KH, DOKUM.KR_OP KR_OP, ZDARZT.ZDARZ ZDARZ, DOKUM.REFERENCE DOKUM, DOKUM.K_NAD K_NAD '
::            'FROM DOKUM LEFT JOIN KH LEFT JOIN KH_OSOB USING(DOKUM.KH_OSOB,KH_OSOB.REFERENCE) '
::            'LEFT JOIN ZDARZT USING(DOKUM.DOT,ZDARZT.REFERENCE)'
::            'WHERE DOKUM.DATAKONT :_b to_date(:_a) AND DOKUM.ESEND = \'N\' AND DOKUM.K_NAD<>\'T\''
::            ,date()-BEER.KNP_ZAKR, '>=');
   _num_prev:=__DDOKUM.DNUMER;
   _set:=0;
   __DDOKUM.erase();
   _DDOKUM:=sql('SELECT DOKUM.FIRMA FIRMA, DOKUM.DATAKONT DATAKONT, DOKUM.DOT DOT, KH_OSOB.NAZWISKO NAZWISKO, KH_OSOB.IMIE IMIE'
         ', DOKUM.OPER OPER, KH.NAZ_P NAZ_P, DOKUM.RODZAJ_K RODZAJ_K, DOKUM.ESEND ESEND, '
         'DOKUM.DLA_KH DLA_KH, DOKUM.KR_OP KR_OP, ZDARZT.ZDARZ ZDARZ, DOKUM.REFERENCE DOKUM, DOKUM.NR DNUMER, DOKUM.K_NAD K_NAD '
         'FROM DOKUM LEFT JOIN KH USING(DOKUM.KH,KH.REFERENCE) '
         'LEFT JOIN KH_OSOB USING(DOKUM.KH_OSOB,KH_OSOB.REFERENCE) '
         'LEFT JOIN ZDARZT USING(DOKUM.DOT,ZDARZT.REFERENCE)'
         'WHERE DOKUM.DATAKONT :_b to_date(:_a) AND DOKUM.DATAKONT :_d to_date(:_c) AND DOKUM.ESEND = \'N\' AND DOKUM.K_NAD<>\'T\''
         ,BEER.KNP_DOD, '>=', BEER.KNP_DDO, '<=' );

:   _ind:=_DDOKUM.ndx_tmp(,1,'FIRMA',,,'RODZAJ_K',,,'ESEND',,,'DLA_KH',,);
   _ind:=_DDOKUM.ndx_tmp(,1,'FIRMA',,,'RODZAJ_K',,,'ESEND',,,'DLA_KH',,,'DATAKONT',,1,'DNUMER',,1);
   _DDOKUM.index(_ind);
   _DDOKUM.prefix($REF.FIRMA,'W','N',1);

   {? _DDOKUM.first() ||
      {! |?
         DOKUM.cntx_psh();
         _dokum_ref:=exec('FindAndGet','#table',DOKUM,_DDOKUM.DOKUM,,,null());
         DOKUM.prefix();
         DOKUM.seek(_dokum_ref);
         _upr:=exec('dokum_upraw','dokum');
         DOKUM.cntx_pop();
         {? _upr='' ||

            __DDOKUM.FIRMA:=_DDOKUM.FIRMA;
            __DDOKUM.DATAKONT:=_DDOKUM.DATAKONT;
            __DDOKUM.DOT:=_DDOKUM.DOT;
            __DDOKUM.ZDARZ:=_DDOKUM.ZDARZ;
            __DDOKUM.NAZWISKO:=_DDOKUM.NAZWISKO;
            __DDOKUM.IMIE:=_DDOKUM.IMIE;
            __DDOKUM.OPER:=_DDOKUM.OPER;
            __DDOKUM.KR_OP:=_DDOKUM.KR_OP;
            __DDOKUM.NAZ_P:=_DDOKUM.NAZ_P;
            __DDOKUM.RODZAJ_K:=_DDOKUM.RODZAJ_K;
            __DDOKUM.DLA_KH:=_DDOKUM.DLA_KH;
            __DDOKUM.ESEND:=_DDOKUM.ESEND;
            __DDOKUM.DOKUM:=_DDOKUM.DOKUM;
            __DDOKUM.DNUMER:=_DDOKUM.DNUMER;
            __DDOKUM.add();
            {? __DDOKUM.DNUMER=_num_prev || _ref_dok:=__DDOKUM.ref; _set:=1 ?}
          ?};
         _DDOKUM.next()
      !};
      {? _set=0 || __DDOKUM.first || __DDOKUM.seek(_ref_dok) ?}
   ?}
?}


\knp_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Formuła przed wyświetleniem, wyszarzająca akcje dla tabeli KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:DOKUM.cntx_psh();
{? KN_POZ.DOKUM
||
   DOKUM.use(ref_name(KN_POZ.DOKUM));
   DOKUM.prefix();
   DOKUM.seek(KN_POZ.DOKUM)
?};
{? _a
|| _win_sel:=KN_POZ.win_sel('WER');
   KHVZ.UPRAW:=exec('dokum_upraw','dokum');
   {? (KN_POZ.KN_NAG().STATUS='Wysłany' & KN_POZ.sel_size()=0)
   || KN_POZ.actions_grayed(_win_sel, 'DPU')
   |? KHVZ.UPRAW<>''
   || KN_POZ.actions_grayed(_win_sel, 'PU')
   || KN_POZ.actions_grayed(_win_sel, '')
   ?}
?}
:DOKUM.cntx_pop()


\knp_kon_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Dodawanie pozycji z zakładki Kontakty do książki nadawczej
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? DOKUM.K_NAD='T'
|| exec('kn_kon_disp','dokum');
   return(~~)
?};

_r_lock:=exec('r_lock_one','#table',DOKUM,DOKUM.ref());
{? _r_lock
||
   KN_NAG.cntx_psh();
   KN_POZ.cntx_psh();
   KN_NAG.index('STATDT');
   KN_NAG.prefix(REF.FIRMA,'Otwarty',);
:KN_NAG.first();
   KN_NAG.win_sel('WER1');
   {? KN_NAG.select(,1)
   ||
      KN_POZ.prefix();
      KN_POZ.KN_NAG:=KN_NAG.ref;
      KN_POZ.KAT:=DOKUM.DOKUMK().NAZ;
      KN_POZ.KH:=DOKUM.KH;
      KN_POZ.DATA:=DOKUM.DATA;
:   KN_POZ.POLEC:={? 1 || 'T' || DOKUM.P_POL ?};
:   KN_POZ.POLEC:=exec('get','#params',700018,2);
:   KN_POZ.POLEC:=exec('get','#params',700900,2);
:   KN_POZ.NR_POLEC:=DOKUM.P_PNR;
      {? exec('get','#params',700900,2)='T'
      || KN_POZ.POLEC:='T'
      || KN_POZ.POLEC:=DOKUM.P_POL
      ?};
      KN_POZ.NR_POLEC:='';
      KN_POZ.KURIER:='N';
      KN_POZ.NR_PRZES:='';
      KN_POZ.PRIORYT:='N';
      KN_POZ.PACZKA:='N';
      KN_POZ.ZPO:='N';
      KN_POZ.DOKUM:=DOKUM.ref();
      KN_POZ.LP:=exec('knp_lp_gen','dokum');
      KN_POZ.add();

      DOKUM.cntx_psh();
      DOKUM.prefix();
      {? DOKUM.seek(DOKUM.ref)
      || DOKUM.K_NAD:='T';
         DOKUM.put()
      ?};
      DOKUM.cntx_pop();

      FUN.info('Wybrana pozycja została dodana do książki nadawczej.'@)
   ?};


   KN_POZ.cntx_pop();
   KN_NAG.cntx_pop();
   exec('r_unlock_one','#table',DOKUM,DOKUM.ref(),_r_lock)
|| FUN.info('Pozycja wykorzystywana przez innego użytkownika.'@)
?}


\kn_kon_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Pokazanie danych książki nadawczej, z którą jest zwiazany kontakt, z zakładki Kontakty
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
KN_NAG.cntx_psh();
KN_POZ.cntx_psh();
KN_POZ.index('DOKUM');
KN_POZ.prefix(DOKUM.ref());
{? KN_POZ.first()
|| KN_POZ.KN_NAG().ref();
   KN_NAG.get();
   KN_NAG.win_edit('RED');
   {? KN_NAG.STATUS='Otwarty'
   || KN_NAG.STATUS:='N'
   || KN_NAG.STATUS:='T'
   ?};
   KN_NAG.display()
?};
KN_NAG.cntx_pop();
KN_POZ.cntx_pop();
~~


\kn_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: przed "wyświetl" tabeli KN_NAG
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win:='RED';
KN_NAG.cntx_psh();
KN_NAG.win_edit(_win);
{? KN_NAG.STATUS='Otwarty' || KN_NAG.STATUS:='N' || KN_NAG.STATUS:='T' ?};
KN_NAG.display();
KN_NAG.cntx_pop()


\knp_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: przed "wyświetl" tabeli KN_POZ
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_win:='RED';
KN_POZ.win_edit(_win);
KN_POZ.display()


\save_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: zapisuje dokument docx oraz usuwa pliki przetwarzane
::   WE:  _a  - akronim tabeli
::        _b  - pełna ścieżka i nazwa wydruku wraz z rozszerzeniem
::       [_c] - nazwa pod ktora ma zostac zapisany dokument
::       [_d] - symbol dokumentu
::       [_e] - e-mail
::       [_f] - opis krotki, zostanie przekszalcony na Temat maila, dodano możliwość parametryzacji
::              jeżeli jest podana formuła na opis to zastąpi ona napis przesłany parametrem
::       [_g] - język, który został wybrany podczas tworzenia dokumentu (Utwórz PDF)
::       [_h] - status rejestracji drukowanego dokumentu (domyślnie 'N')
::   WY: null lub ref zalozonego rekordu w tabeli DOKUM
::----------------------------------------------------------------------------------------------------------------------
{? _>=3 || {? type_of(_c)<>2 || _c:='' ?} || _c:='' ?};
{? _>=4 || {? type_of(_d)<>2 || _d:='' ?} || _d:='' ?};
{? _>=5 || {? type_of(_e)<>2 || _e:='' ?} || _e:='' ?};
{? _>=6 || {? type_of(_f)<>2 || _f:='' ?} || _f:='' ?};
{? _>=7 || {? type_of(_g)<>7 || _g:=null() ?} || _g:=null() ?};
{? _>=8 || {? type_of(_h)<>2 || _h:='N' ?} || _h:='N' ?};

_wyn:=null();
_c:=(_b-22)+''+_c;
:: sprawdzanie i ew kopiowanie pliku
{? _c='' | _b=_c
||
   _file:=_b
||
   _file:=_c;
   {? fexists(_b,0)
   ||
      {? fcopy(_b,_c,0,0,1)
      ||
         ferase(_b,0)
      ?}
   ?}
?};

{? fexists(_file,0)
||
   {? exec('wyb_doc','dokum',_file)
   ||
      _wyn:=DOKUM.ref();
      DOKUM.SYM_SYS:=_d;
      DOKUM.EM:=_e;
      DOKUM.TYP:='D';
      {? ref_tab(POLA.REFSQL)=FAKS | ref_tab(POLA.REFSQL)=ZK_N || DOKUM.CMS:='T' ?};
      DOKUM.AUTOADD:='T';
      DOKUM.KR_OP:=_f;
      DOKUM.JEZYK:=_g;
      DOKUM.STAT_REJ:=_h;
      {? ref_tab(POLA.REFSQL)=FAKS
      ||
         _zdarzt:=exec('FindInSet','#table','ZDARZT','ZDARZT',exec('get','#params',100800),'N',,1,,null());
         {? _zdarzt & FAKS.KH
         ||
            DOKUM.DOT:=_zdarzt;
            DOKUM.KH:=FAKS.KH
         ?}
      ?};
      DOKUM.put();

::    przygotowanie nazwy i opisu
      _opis:=exec('get','#params',100250,2)();
      {? _opis<>~~
      || DOKUM.KR_OP:=_opis;
         DOKUM.put()
      || DOKUM.KR_OP:='Wydruk listy przesyłek';
         DOKUM.put()
      ?};
      _opis_memo:=exec('get','#params',100251,2)();
      {? _opis_memo<>~~
      || DOKUM.memo_set(_opis_memo,'OPIS')
      || DOKUM.memo_set('Wygenerowano automatycznie dnia '+$date+' o godz. '
                     +$time()+' przez '+exec('name','users')+'.','OPIS' )
      ?};
      DOKUM.memo_put(,'OPIS')
   ?};

:: usuwanie dokumentu
   {? fexists(_file,0)
   || ferase(_file,0)
   ?}
|| FUN.info('Nie znaleziono pliku %1.'@[_file])
?};
_wyn


\wyb_doc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: dodawanie dokumentu
::   WE:  [_a] sciezka wraz z plikiem
::   WY:  dodano rekord 1 - tak 0 - nie
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
_typ:='D';
DOKUM.cntx_psh();
_ref_dok:=null();
{? DOKUM.get() || _ref_dok:=DOKUM.ref() ?};
{? _>=1  || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};
{? (POLA.REFSQL<>'' & ref_tab(POLA.REFSQL)=KH & KHVZ.KONTAKTY='T') | (var_pres('pulpit')=1 & pulpit=1)
|| _typ:='K';
   DOKUM.win_edit('REDK')
|? KHVZ.KONTAKTY='T' & (KHVZ.KHVP='kh_all' | KHVZ.KHVP='kh_osob' | KHVZ.KHVP='dokum'| KHVZ.KHVP='projekty')
|| _typ:='K'
|| DOKUM.win_edit('RED')
?};
{? REF.WFIRM & (DOKUM.index('?')='KH2F' | DOKUM.index('?')='KH3F')
|| _tmp:=DOKUM.cur_prfx();
   {? _tmp<>'' & _tmp*','>0
   || _tmp:=((_tmp*',')-1)+_tmp;
      {? #_tmp<>#REF.FIRMA
      || FUN.emsg('W obecnym widoku prezentowane są dane innej firmy.\n'
                 'Przed dodaniem kontaktu należy zmienić zakres danych.'@);
         DOKUM.cntx_pop();
         return(0)
      ?}
   ?}
?};
DOKUM.blank();
DOKUM.bl_file('DOKUM',1);
DOKUM.REFSQL:=POLA.REFSQL;
DOKUM.TYP:=_typ;
{? POLA.REFSQL<>'' & ref_tab(POLA.REFSQL)=KH & KHVZ.KONTAKTY='T' | var_pres('pulpit')=1 & pulpit=1
|| DOKUM.KH:=KH.ref();
   DOKUM.KH_OSOB:=null()
|| {? KHVZ.KONTAKTY='T'
   || {? KHVZ.KHVP='kh_osob'
      || DOKUM.KH:=KH_OSOB.KH;
         DOKUM.KH_OSOB:=KH_OSOB.ref()
      |? KHVZ.KHVP='dokum'
      || DOKUM.KH:=null();
         DOKUM.KH_OSOB:=null()
      |? KHVZ.KHVP='projekty'
      || DOKUM.KH:=PROJEKTY.KH;
         DOKUM.KH_OSOB:=null();
         DOKUM.PROJEKTY:=PROJEKTY.ref()
      || DOKUM.KH:=KH.ref();
         DOKUM.KH_OSOB:=null()
      ?}
   ?}
?};
_pom_tab:=POM.TAB;
_pom_typdok:=POM.TYPDOK;
POM.TAB:='DOKUM';
POM.TYPDOK:='SYS';
exec('add_grnr','numery','SYS');
DOKUM.NR:=exec('numer_new','numery','PACZKA');
DOKUM.prefix();
DOKUM.memo_set(,'OPIS');
DOKUM.memo_set(,'UPRAW');

DOKUM.memo_set('Dodano dnia '+$date+' o godz. '+$time()+' przez '+exec('name','users'),'OPIS');
_wyn:=DOKUM.add();
exec('znak','numery','DOKUM');
DOKUM.bl_put('DOKUM',_a,0);
DOKUM.memo_put(,'OPIS');
DOKUM.memo_put(,'UPRAW');
DOKUM.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
DOKUM.put();
DOKUM.index('DOKUM');
DOKUM.prefix(REF.FIRMA,DOKUM.REFSQL);
{? _wyn || _ref_dok:=DOKUM.ref() ?};

DOKUM.bl_file('DOKUM',1);
POM.TAB:=_pom_tab;
POM.TYPDOK:=_pom_typdok;
DOKUM.cntx_pop();
{? _ref_dok<>null() || DOKUM.seek(_ref_dok) ?};
_wyn


\knp_del_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: Usunięcie pozycji z tabeli KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_nag_ref:=KN_NAG.ref();
_tab_poz:=KN_POZ.sel_aget();

KN_POZ.cntx_psh();
DOKUM.cntx_psh();
KN_NAG.cntx_psh();
KN_NAG.prefix();
{? KN_NAG.seek(_nag_ref) ||
   {? KN_NAG.STATUS='Otwarty' ||
      _odp:=FUN.ask('Czy usunąć wybrane pozycje z książki nadawczej?'@);
      {? _odp=1 ||
         {? _tab_poz.first ||
            {! |?
               KN_POZ.seek(_tab_poz.REF,);
               _dokum_ref:=KN_POZ.DOKUM;
               KN_POZ.del();
               DOKUM.cntx_psh();
               DOKUM.prefix();
:      {? DOKUM.seek(_dokum_ref) || DOKUM.ESEND:='N'; DOKUM.put() ?};
               {? DOKUM.seek(_dokum_ref) || DOKUM.K_NAD:='N'; DOKUM.put() ?};
               DOKUM.cntx_pop();
               _tab_poz.next()
            !}
         ?}
      ?}
   || FUN.emsg('Nie można usunąć dokumentów, ponieważ zostały już wysłane.'@)
   ?}
?};

KN_NAG.cntx_pop();
KN_POZ.cntx_pop();
DOKUM.cntx_pop();
KN_POZ.sel_adel();
~~


\knp_lp_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [DG] [20.42]
:: OPIS: wartosc poczatkowa pola LP
::  OLD: \bllp_grp/mws.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
KN_POZ.cntx_psh();
KN_POZ.index('NAGLP');
KN_POZ.prefix(KN_NAG.ref());
_wyn+={? KN_POZ.last() || KN_POZ.LP || 0 ?};
KN_POZ.cntx_pop();
_wyn


\knp_lp_renum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [DG] [20.42]
:: OPIS: renumeracja MG_GRP
::  OLD: \renumstr/mws.fml
::----------------------------------------------------------------------------------------------------------------------
_tryb:={? var_pres('_a')=type_of('') & (';UDN'*_a)>1 || _a || '' ?};

KN_POZ.cntx_psh();
{? _tryb=''
|| _ref:=dnd_info('dest_record');
   {? KN_POZ.seek(_ref) || exec('zmien_lp','#dragdrop','LP','NAGLP') ?}
|| exec('zmien_lpa','#dragdrop','LP','NAGLP',,,_tryb)
?};
KN_POZ.cntx_pop()


\nummggrp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [DG] [20.42]
:: OPIS: renumeracja KN_POZ
::----------------------------------------------------------------------------------------------------------------------
KN_POZ.cntx_psh();
{? KN_POZ.first()
|| _i:=0;
   {!
   |? _i+=1;
      KN_POZ.LP:=_i;
      KN_POZ.put(1);
      KN_POZ.next()
   !}
?};
KN_POZ.cntx_pop()


\dokum_opis_html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: edycja DOKUM.OPIS w edytorze HTML
::   WE:  [_a] 0 - edycja (domyślnie), 1 - display
::        [_b] 0 - odczyt z bazy danych (domyślnie), 1 - odczyt z bufora
::----------------------------------------------------------------------------------------------------------------------
_edit:=1;
{? var_pres('_a') = type_of(0) & _a
|| _edit:=0
?};
_read_buf:=0;
{? var_pres('_b') = type_of(0)
|| _read_buf:=_b
?};
{? _edit & DOKUM.TYP<>'K' & DOKUM.TYP<>'I' & DOKUM.TYP<>'D'
|| _edit:=0
?};
{? _edit & DOKUM.TYP='K' & KHVZ.UPRAW<>''
|| _edit:=0
?};
{? _edit & DOKUM.TYP='D' & KHVZ.KONTAKTY='T'
|| _edit:=0
?};
{? _edit & (DOKUM.WO='T' | DOKUM.ESEND='T' | DOKUM.BSEND<>'N' | DOKUM.RECEIVED='T')
|| _edit:=0
?};
{? _edit & DOKUM.FIRMA<>REF.FIRMA
|| _edit:=0
?};
exec('edit_html_sysmemo','#edit',DOKUM,'OPIS','d_opis_html',,'Treść'@,_edit,,_read_buf);
''


\ddokum_dokum_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [20.42]
:: OPIS: przed "wyświetl" tabeli DOKUM - dopuszczenie tylko uprawnionych użytkowników
::   WY: 0 - nie, 1 -tak
::----------------------------------------------------------------------------------------------------------------------
_win:={? __DDOKUM.ZDARZ='EMAIL' ||'REDK_M'
      |? __DDOKUM.ZDARZ='PISMO' ||'REDK_P'
      |? __DDOKUM.ZDARZ='SPOTKANIE' | __DDOKUM.ZDARZ='TELEFON' ||'REDK_S'
      ||'REDK' ?};
DOKUM.win_edit(_win);
DOKUM.cntx_psh();
DOKUM.prefix();
DOKUM.seek(__DDOKUM.DOKUM);
{? KHVZ.UPRAW='' | KHVZ.UPRAW='P'
|| DOKUM.display()
?};
DOKUM.cntx_pop();
0


\ob_start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Uruchamia obieg dokumentu z kontaktu - wywoływana z menu DOKUM
::   WE: _a - Faktura (1)
::            Wniosek (2)
::       _b - Dodaj DOKUM (0/1)
::       _c - Obszar Businesslink? (T/N)
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_addDOKUM:={? var_press('_b')>0 || _b || 0 ?};
_isBL:={? var_press('_c')>0 || _c || 'N' ?};
_dok_ref:=null();
DOKUM.cntx_psh();
{? _addDOKUM
|| _dok_ref:=exec('ob_add_dokum','dokum',_a)
?};
{? (_addDOKUM=0 | (_addDOKUM=1 & _dok_ref<>null())) & DOKUM.get()
|| {? DOKUM.r_lock(1,1)
   || {? DOKUM.BL='T'
      || {? DOKUM.get()
         || exec('bl_act','zbl_dok',{? _a=1 || 'DOB' || 'WOB' ?},DOKUM.ref())
         ?}
      || exec('ob_edit','dokum',_a,'N')
      ?};
      DOKUM.r_unlock()
   || FUN.info('Kontakt obsługuje inny operator'@)
   ?}
?};
DOKUM.cntx_pop();
{? _dok_ref<>null()
|| {? DOKUM.seek(_dok_ref)
   || {? _addDOKUM & DOKUM.REFSQL='' & DOKUM.EDOKUMR=''
      || _dok_ref:=null(); exec('del_dok','dokum',1)
      |? DOKUM.f_active()
      || DOKUM.f_rfresh();
         DOKUM.f_seek(_dok_ref)
      ?}
   ?}
?};
{? _isBL='N'
|| exec('make_contact_view','kontrahent_view')
?};
~~


\ob_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Obsługa faktur i wniosków w obieg z kontaktu
::   WE: _a - Faktura (1)
::            Wniosek (2)
::       _b - Automatyczna (T/N)
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_isAUTO:={? var_press('_b')>0 || _b || 'N' ?};
typobi:=_a;
utw_dob:=1;
DOKUM.cntx_psh(); EDOKUM.cntx_psh();
{? DOKUM.REFSQL<>''
|| _typobieg:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"@.EDOKUM.TYP().TYPOBIEG().NAZWA",'');
   _result:={? -(4+DOKUM.REFSQL)<>'skid' || 0 |? _a=1 || _typobieg='Obieg faktur' |? _a=2 || 1 ?};
   {? _result
   || _edok:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,,null());
      _edok_uidref:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"@.EDOKUM.uidref()",'');
      _edok_typ:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,
        "@.EDOKUM.TYP();
         _res:={? ETYPY.NAZWA<>''
               || ETYPY.NAZWA+{? ETYPY.W_PORTAL<>'N' || ';'+ETYPY.W_PORTAL || '' ?}
               || ''
               ?};
         _res",'');
      _ao:=__PARSES.getVal('OkresRok');
      _result:=exec('parses_ob','obiegi2',_edok);
      {? _result
      || _params:=exec('mp_run_a','#b__box');
         _params.ACT_UID:={? _a=1 || 'OBG_FZO_DARK' || 'OBE_FAW_DARP' ?};
         _params.AKCJA:='Pop_dokum';
         _procStatus:=exec('isActiveProcOrProblems','#b__box',_edok_uidref);
         {? ~_procStatus.isActive || _params.PROC_START:='T' ?};
         _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
         _params.UIDREF:=_edok_uidref;
         {? _a=2
         || exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'M_WYW','K');
            exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'R_WYW','JW')
         ?};
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',_edok);
         {? _edok_typ<>''
         || exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TYP_DOK',_edok_typ)
         ?};
         exec('mp_run','#b__box',_params)
      ?};
      __PARSES.setVal('OkresRok',_ao)
   || FUN.info('Kontakt jest już powiązany z innym dokumentem.'@)
   ?}
|| _isUFD:=exec('czy_ufd','edi_fo_ufd',DOKUM.ref());
   {? DOKUM.BL='T' & _isUFD & (DOKUM.BL_STAT=exec('ready_to_registration','zbl_stat') |
      DOKUM.BL_STAT=exec('registration_failed','zbl_stat'))
   || _result:=exec('ob_edi_o','dokum',_a,_isAUTO)
   ?};
   {? _result
   || {? DOKUM.BL='T' & _isUFD
      || _edok:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,,null());
         _edok_uidref:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,"@.EDOKUM.uidref()",'');
         _edok_typ:=exec('FindAndGet','#table',EDOKUM,DOKUM.REFSQL,,
            "@.EDOKUM.TYP();
             _res:={? ETYPY.NAZWA<>''
                   || ETYPY.NAZWA+{? ETYPY.W_PORTAL<>'N' || ';'+ETYPY.W_PORTAL || '' ?}
                   || ''
                   ?};
             _res",'');
         {? _isAUTO='T'
         || _obj:=obj_new(1); _obj[1]:=obj_new('PARAMETR','VALUE');
            _obj[1].PARAMETR:='EDOKUM'; _obj[1].VALUE:=_edok;
            exec('force_signal','#b__box',
                 {? _a=1 || exec('get','#params',100370) || exec('get','#params',100371) ?},,_obj);
            {? _a=1 || exec('ntc_ksef_auto','obiegi2',_edok) ?}
         || _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:={? _a=1 || 'OBG_FZO_DARK' || 'OBE_FAW_DARP' ?};
            _params.PROC_START:='T';
            _params.AKCJA:='Pop_dokum';
            _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
            _params.UIDREF:=_edok_uidref;
            {? _a=2
            || exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'M_WYW','K');
               exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'R_WYW','JW')
            ?};
            exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'EDOKUM',_edok);
            {? _edok_typ<>''
            || exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'TYP_DOK',_edok_typ)
            ?};
            exec('mp_run','#b__box',_params)
         ?};
         {? _a=1
         || EDOKPAR.cntx_psh(); EDOKPAR.use('skidh_'+(ref_name(_edok)+2)); EDOKPAR.index('UNIK');
            EDOKUM.cntx_psh();
            {? EDOKUM.seek(_edok,ref_name(_edok))
            || EDOKPAR.prefix(EDOKUM.ref());
               {? EDOKPAR.first() & EDOKPAR.ED_PODZ<>'P' || exec('tagger_tip_csh','tagger',EDOKUM.ref()) ?}
            ?};
            EDOKUM.cntx_pop();
            EDOKPAR.cntx_pop()
         ?}
      || exec('typobi','obiegi');
         _ar:=SSTALE.AR; _ao:=SSTALE.AO;
         _result:={? _a=1 || exec('sel_okres','okresy',DOKUM.DATA) || exec('sel_okres','okresy',,0) ?};
         {? _result
         || __PARSES.setVal('OkresRok',SSTALE.AO);
            exec('open_tabele','open_tab','F');
            _params:=exec('mp_run_a','#b__box');
            _params.ACT_UID:={? _a=1 || 'OBG_FZO_DARK' || 'OBE_FAW_DARP' ?};
            _params.AKCJA:='Dol_dokum';
            _params.PROC_START:='T';
            _params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
            {? _a=2
            || exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'M_WYW','K');
               exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'R_WYW','JW')
            ?};
            exec('mp_run','#b__box',_params);
            SSTALE.AR:=_ar; SSTALE.AO:=_ao;
            exec('open_tabele','open_tab');
            __PARSES.setVal('OkresRok',SSTALE.AO)
         ?}
      ?}
   ?}
?};
DOKUM.cntx_pop(); EDOKUM.cntx_pop();
VAR_DEL.delete('utw_dob','zak_rej_ed','__ProcParam','no_us');
{? _result || {? DOKUM.REFSQL='' || _result:=0 ?} ?};
_result


\ob_add_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Dodaje kontakt w obiegu
::   WE: _a - Faktura (1)
::            Wniosek (2)
::   WY: DOKUM.ref
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
ZDARZT.cntx_psh(); ZDARZT.index('OBJAKO'); ZDARZT.prefix('N',_a);
{? ZDARZT.first()
|| DOKUM.prefix();
   DOKUM.blank();
   DOKUM.DOT:=ZDARZT.ref();
   DOKUM.OPER:=exec('name','users');
   DOKUM.DATA:=exec('data','#blank');
   DOKUM.CZAS:=exec('time','#blank');
   DOKUM.FIRMA:=REF.FIRMA;
   DOKUM.RODZAJ_K:='P';
   DOKUM.TYP_K:={? _a=1 || 'F' |? _a=2 || 'W' ?};
   DOKUM.TYP:='K';
   {? POLA.REFSQL<>'' & ref_tab(POLA.REFSQL)=KH & KHVZ.KONTAKTY='T' | var_pres('pulpit')=1 & pulpit=1
   || DOKUM.KH:=KH.ref();
      DOKUM.KH_OSOB:=null()
   || {? KHVZ.KONTAKTY='T'
      || {? KHVZ.KHVP='kh_osob'
         || DOKUM.KH:=KH_OSOB.KH;
            DOKUM.KH_OSOB:=KH_OSOB.ref()
         |? KHVZ.KHVP='dokum'
         || DOKUM.KH:=null();
            DOKUM.KH_OSOB:=null()
         || DOKUM.KH:=KH.ref();
            DOKUM.KH_OSOB:=null()
         ?}
      ?}
   ?};
   _pom_tab:=POM.TAB;
   _pom_typdok:=POM.TYPDOK;
   POM.TAB:='DOKUM';
   POM.TYPDOK:='SYS';
   exec('add_grnr','numery','SYS');
   DOKUM.NR:=exec('numer_new','numery','PACZKA');
   RODZINF.NAZ:='Dokument prosty';
   {? DOKUM.add() || _result:=DOKUM.ref(); exec('znak','numery','DOKUM',1); DOKUM.put() ?};
   POM.TAB:=_pom_tab;
   POM.TYPDOK:=_pom_typdok
|| FUN.info('Brak zdefiniowanego typu kontaktu z obiegiem jako %1.'@[{? _a=1 || 'faktura' || 'wniosek' ?}])
?};
ZDARZT.cntx_pop();
_result


\edokumz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Dodawanie załączników do dokumentu w obiegu z kontaktu
::   WE: _a - EDOKUM.ref
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
EDOKUMZ.cntx_psh(); DOKUMZ.cntx_psh();
EDOKUMZ.use('skid_n'+(8+$_a+2));
EDOKUMZ.index('DISP'); EDOKUMZ.prefix();
DOKUMZ.index('DISP'); DOKUMZ.prefix(DOKUM.ref());
_loop:=DOKUMZ.first();
{!
|? _loop
|! EDOKUMZ.blank(1);
   EDOKUMZ.DOKUM:=_a;
   EDOKUMZ.DATE:=DOKUMZ.DATE;
   EDOKUMZ.TIME:=DOKUMZ.TIME;
   EDOKUMZ.USER:=exec('name','users');
   EDOKUMZ.NAZWA:=DOKUMZ.NAZWA;
   EDOKUMZ.KR_OP:=DOKUMZ.KR_OP;
   EDOKUMZ.EDOKUM:=DOKUMZ.DOKUM;
   _result:=EDOKUMZ.add();
   _loop:=_result & DOKUMZ.next()
!};
EDOKUMZ.cntx_pop(); DOKUMZ.cntx_pop();
_result


\dsp_cnt_as
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.14]
:: OPIS: Na wyswietl dla pola OBIEGI.CNT_AS
::----------------------------------------------------------------------------------------------------------------------
OBIEGI.CNT_AS:={? DOKUM.TYP_K='F' || 'Faktura' |? DOKUM.TYP_K='W' || 'Wniosek' || 'Zwykły' ?};
1


\trig_add_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Trigger add przed
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('dokum_tr_b','dokum_zal');
exec('dokum_tr_b','zbl');
1


\trig_add_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Trigger add po
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
DOKUM.ONEID:=exec('oneid','dokum');
DOKUM.put();
:: Wypełnienie informacji o załącznikach dla faktury
{? 5+DOKUM.REFSQL='faktu' || exec('faks_info_val','dokum',DOKUM.REFSQL) ?};
~~


\trig_put_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Trigger put przed
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('trig_add_b','dokum')


\trig_put_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Trigger put po
::   WE: _a - parametr systemowy
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
{? _a=0 || return() ?};

DOKTYP.cntx_psh();
_name:={? DOKUM.name()='dokum' || 'doktyp' || (DOKTYP.name()-2)+(DOKUM.name()+2) ?};
DOKTYP.use(_name);
DOKTYP.cntx_psh();
DOKTYP.index('DOKUM');
DOKTYP.prefix(DOKUM.ref());
_loop:=DOKTYP.first();
{!
|? _loop
|!
   _loop:=DOKTYP.put() & DOKTYP.next()
!};
DOKTYP.cntx_pop();
DOKTYP.cntx_pop();
:: DRO_TODO_AWI: (dokum_arch):   : aktualizacja pola EDOK_ATR.DOT ... co z wydajnością?
_Names:=EDOK_ATR.names();
EDOK_ATR.cntx_psh();
_loop:=_Names.first();
{!
|? _loop
|!
   EDOK_ATR.use(_Names.NAME);
   EDOK_ATR.cntx_psh();
   EDOK_ATR.index('DOKUM');
   EDOK_ATR.prefix(DOKUM.ref(),DOKUM.ONEID);
   _loop:=EDOK_ATR.first();
   {!
   |? _loop
   |!
      EDOK_ATR.DOT:=DOKUM.DOT;
      _loop:=EDOK_ATR.put() & EDOK_ATR.next()
   !};
   EDOK_ATR.cntx_pop();
   _loop:=_Names.next()
!};
EDOK_ATR.cntx_pop();
{? DOKUM.BL='T' || exec('dokumlog_add','zbl_dok',DOKUM.uidref(),1) ?};
VAR_DEL.delete('__bdokum');
:: Wypełnienie informacji o załącznikach dla faktury
{? 5+DOKUM.REFSQL='faktu' || exec('faks_info_val','dokum',DOKUM.REFSQL) ?};
:: Wypełnienie DOKUMZ.REFSQL
{? exec('upgrade2325_blbc1','zbl')
|| {? DOKUM.name()='dokum'
   || DOKUMZ.cntx_psh();
      DOKUMZ.index('DOK');
      DOKUMZ.prefix(DOKUM.ref());
      {? DOKUMZ.first()
      || {!
         |? DOKUMZ.REFSQL:=DOKUM.REFSQL;
            DOKUMZ.put();
            DOKUMZ.next()
         !}
      ?};
      DOKUMZ.cntx_pop()
   || DOKUMZA.cntx_psh();
      DOKUMZA.use((DOKUMZA.name()-2)+(DOKUM.name()+2));
      DOKUMZA.index('DOK');
      DOKUMZA.prefix(DOKUM.ref());
      {? DOKUMZA.first()
      || {!
         |? DOKUMZA.REFSQL:=DOKUM.REFSQL;
            DOKUMZA.put();
            DOKUMZA.next()
         !}
      ?};
      DOKUMZA.cntx_pop()
   ?}
?};
~~


\trig_del_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Trigger del przed
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('dokum_tr_del_b','zbl')


\trig_del_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Trigger del po
::----------------------------------------------------------------------------------------------------------------------
{? 5+bfld('REFSQL')='faktu' || exec('faks_info_val','dokum',bfld('REFSQL')) ?};
~~


\doktyp_trig_add_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Trigger add przed
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
DOKTYP.KH:=DOKTYP.DOKUM().KH;
1


\doktyp_trig_put_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Trigger put przed
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('doktyp_trig_add_b','dokum')


\oneid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: One ID rekordu tabeli DOKUM
::   WE:
::   WY: One ID
::----------------------------------------------------------------------------------------------------------------------
':'+DOKUM.IDADD+$DOKUM.ref()


\dokum_oneid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: DOKUM.ONEID
::   WE:
::   WY: DOKUM.ONEID
::----------------------------------------------------------------------------------------------------------------------
DOKUM.ONEID


\archive
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Archiwizacja dokumentu DOKUM
::   WE: _a - zakres danych BL/KKH/ZAL/ZWS
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_DOK_ARCH';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
{? _a<>'ZWS'
||
   _sel_size:=DOKUM.sel_size();
   _continue:=0;
   {? _sel_size>1
   ||
      {? _a='BL'  || _continue:=FUN.ask('Czy zarchiwizować dokumenty?'@)
      |? _a='KKH' || _continue:=FUN.ask('Czy zarchiwizować kontakty?'@)
      |? _a='ZAL' || _continue:=FUN.ask('Czy zarchiwizować załączniki?'@)
      ?}
   ||
      {? _a='BL'  || _continue:=FUN.ask('Czy zarchiwizować dokument?'@)
      |? _a='KKH' || _continue:=FUN.ask('Czy zarchiwizować kontakt?'@)
      |? _a='ZAL' || _continue:=FUN.ask('Czy zarchiwizować załącznik?'@)
      ?}
   ?};
   {? _continue=0 || DOKUM.sel_adel(); return() ?};
   {? _sel_size || _params.GRUPA:='T' ?};
   _params.UIDREF:=DOKUM.uidref();
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOKUM',DOKUM.ref());
   exec('ini_kom','#message','Archiwizacja dokumentów');
   exec('mp_run','#b__box',_params);
   exec('end_kom','#message');
   0
||
   DOKUM.cntx_psh();
   _red:=DOKUM.mk_edit('Parametry'@,,'dokum_arch_par');
   DOKUM.win_esep(_red,'Okres archiwizacji'@);
   DOKUM.win_efld(_red,,'DATAKONT',,,,,,'Do daty'@);
   DOKUM.win_esep(_red,'Ograniczenia'@);
   DOKUM.win_efld(_red,,'KH','KOD','KOD1',,,0,'Kontrahent'@);
   DOKUM.win_efld(_red,,'KH','NAZ',,,,0,,1);
   _ff:='key:F2';
   DOKUM.win_ebtn(_red,'text=%1,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
   _ff:='key:Esc';
   DOKUM.win_ebtn(_red,'text=%1,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
   DOKUM.win_edit(_red);
   DOKUM.blank(1);
   KHVZ.KONTAKTY:='T';
   KHVZ.KHVP:='dokum';
   _valid:="
      {? DOKUM.DATAKONT=date(0,0,0) & DOKUM.KH=null()
      ||
         FUN.info('Należy podać co najmniej jeden z parametrów.'@);
         'DATAKONT'
      ||
         ''
      ?}
   ";
   {? DOKUM.edit(_valid)
   ||
      {? DOKUM.DATAKONT<>date(0,0,0)
      ||
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DO',DOKUM.DATAKONT)
      ?};
      {? DOKUM.KH<>null()
      ||
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'KH',DOKUM.KH)
      ?};
      exec('mp_run','#b__box',_params)
   ?};
   DOKUM.cntx_pop();
   ''
?}


\archive_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Podgląd archiwum DOKUM
::   WE: _a - zakres danych BL/KKH/ZAL/BUID
::       _b - dokument dla _a=ZAL, KH.ref lub KH_OSOB.ref dla _a=KKH
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_zakr:=_a;
_continue:=0;

_area:=AreaTitle.area;

_kh_ref:=null();
_kh_osob_ref:=null();
_proj_ref:=null();
_proj_firma:=null();
_proj_rodzic:='';
_proj_kol:='';
_proj_poz:=0;

{? _zakr='KKH' & var_pres('_b')=type_of(null())
|| {? ref_name(_b)='kontr'
   || _kh_ref:=_b;
      {? KHVZ.KONTAKTY='T' & KHVZ.KHVP='projekty'
      || _proj_ref:=PROJEKTY.ref();
         _proj_firma:=PROJEKTY.FIRMA;
         _proj_rodzic:=PROJEKTY.RODZIC;
         _proj_kol:=PROJEKTY.KOL;
         _proj_poz:=PROJEKTY.POZ_PROJ
      ?}
   |? ref_name(_b)='kh_osob'
   || _kh_osob_ref:=_b;
      KH.cntx_psh();
      KH_OSOB.cntx_psh();
      {? KH_OSOB.seek(_kh_osob_ref) & KH_OSOB.KH<>null()
      || _kh_ref:=KH_OSOB.KH
      || _kh_osob_ref:=null()
      ?};
      KH.cntx_pop();
      KH_OSOB.cntx_pop()
   ?}
?};

_d0_ref:=null();
{? _zakr='ZAL'
|| {? var_pres('_b')=type_of(null()) & (5+ref_name(_b)='faktu' | 5+ref_name(_b)='nagdo' |
      5+ref_name(_b)='zknag' | 5+ref_name(_b)='zdnag' | ref_name(_b)='oferty')
   || _d0_ref:=_b
   |? _area='LSP_FAK' | _area='LSS_POS' | _area='LZK_ZAK'
   || _d0_ref:=FAKS.ref()
   |? _area='LMG_MAG'
   || _d0_ref:=ND.ref()
   |? _area='LSP_ZKN' | _area='LMG_ZAM'
   || _d0_ref:=ZK_N.ref()
   |? _area='LZK_ZDS'
   || _d0_ref:=ZD_NAG.ref()
   |? _area='LSP_OFE'
   || _d0_ref:=OFE.ref()
   |? _area='FKS_DKS'
   || _d0_ref:=DOK.ref()
   ?}
?};

_flt_jest:=DOKUM.f_active();
{? _flt_jest
|| _filtr:=exec('get','#filter',DOKUM)
?};
DOKUM.cntx_psh();
DOKUM.f_clear();
DOKTYP.cntx_psh();
DOKUMLOG.cntx_psh();

{? _zakr='KKH'
|| _wer:='WERKA';
   _tyt:='Archiwum kontaktów'@;
   exec('arch_kkh_init','dokum')
|? _zakr='BL'
|| _wer:=exec('wer_archive','zbl_dok');
   _tyt:='Archiwum dokumentów'@;
   exec('arch_bl_init','dokum')
|? _zakr='BUID'
|| _wer:=exec('wer_archive','zbl_dok',1);
   _tyt:='Archiwum dokumentów powiązanych'@;
   exec('arch_bl_init','dokum')
|? _zakr='ZAL'
|| _wer:='WERA';
   _tyt:='Archiwum załączników dokumentów'@;
   exec('arch_zal_init','dokum')
|| _wer:=exec('wer_archive','zbl_dok');
   _tyt:='Archiwum dokumentów'@
?};

_wer_gr:=DOKUM.grp_make(_tyt,,'dokum_arch'+(-_zakr));

_Names:=DOKUM.names();
_loop:=_Names.first();
{!
|? _loop
|!
   {? _Names.NAME<>'dokum' & _Names.NAME<>'dok__'
   ||
      _loop:=0;
      _name:=''''+_Names.NAME+'''';
      _name_t:='''dokt'+(_Names.NAME+2)+'''';
      _flt_s:='''''';
      _flt_w:='''''';
      {? _zakr='BL'
      || _loop:=1;
         _ind:='''BL''';
         _prfx:='''T'',REF.FIRMA'
      |? _zakr='BUID'
      || _loop:=1;
         _ind:='''BL''';
         _prfx:='''''';
         _flt_s:=''''+gsub(_filtr.sort,'\'','\'\'')+'''';
         _flt_w:=''''+gsub(_filtr.where,'\'','\'\'')+''''
      |? _zakr='KKH'
      || _loop:=1;
         {? _kh_ref<>null() & _kh_osob_ref<>null()
         || _ind:='''KHOK''';
            _prfx:='REF.FIRMA,'+$ref_num(_kh_ref)+','+$ref_num(_kh_osob_ref)
         |? _kh_ref<>null()
         || {? _proj_ref<>null()
            || _ind:='''PRO_E''';
               _prfx:=$ref_num(_proj_firma)+','''+_proj_rodzic+''',1,'''+((_proj_poz*3)+_proj_kol)+''''
            || _ind:='''KHF''';
               _prfx:='REF.FIRMA,'+$ref_num(_kh_ref)
            ?}
         || _ind:='''KH3F''';
            _prfx:='REF.FIRMA,1'
         ?}
      |? _zakr='ZAL' & _d0_ref<>null()
      || _loop:=1;
         _ind:='''DOKUM''';
         _prfx:='REF.FIRMA,'''+$_d0_ref+''''
      ?};
      {? _loop
      ||
         _tab:='20'+(_Names.NAME+2);
         _fb:=$('
               DOKUM.use(%1);
               _msk:=DOKUM.name()+2; {? _msk=\'um\' || _msk:=\'og\' ?};
               DOKUMLOG.use(\'dol\'+_msk);
               DOKUM.cntx_psh();
               DOKUMLOG.cntx_psh();
               DOKUM.index(%2);
               {? %5=\'\'
               || DOKUM.prefix(%3)
               || DOKUM.prefix();
                  DOKUM.f_set(%6,,%5)
               ?};
               DOKTYP.cntx_psh();
               DOKTYP.use(%4)
         '[_name,_ind,_prfx,_name_t,_flt_w,_flt_s]);
         _fa:="DOKUM.cntx_pop();DOKTYP.cntx_pop();DOKUMLOG.cntx_pop()";
         _fr:="";
         {? _zakr='KKH'
         || _fr:=$(
              'exec(\'dokum_rfr\',\'bloby\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\',\'DOKUMZA\');'+
              'exec(\'arch_opis_html\',\'dokum\');~~')
         |? _zakr='BL' | _zakr='BUID'
         || _fr:=$('
                {? DOKUM.sel_size()=0
                &
                {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
                || tab_show(0,''atta'');
                   tab_show(1,''view'');
                   grp_edisp(DOKUM,''%1'');
                   exec(''dokum_rfr'',''bloby'','':%2'',''%3'',''DOKUMZA'',0);
                   exec(''bl_log_html'',''zbl_dok'')
                || tab_hide(0,1,''atta'');
                   tab_hide(1,,''view'')
                ?};
                ~~'[__REDVIEW,__CTR.WIN_ID,__CTR.WIN_ACR]
                )
         |? _zakr='ZAL'
         || _fr:='exec(\'gr_dok\',\'dokum\');';
            {? var_pres('__CTR0')>0 & var_pres('__CTR')>0
            || _fr:=_fr+'exec(\'dokum_rfr\',\'bloby\',\':'+__CTR.WIN_ID+'\',\''+__CTR.WIN_ACR+'\',\'DOKUMZA\');'
            ?};
            _fr:=_fr+'~~';
            _fr:=$_fr
         ?};
         _fb();
         {? {? _flt_w='''''' || DOKUM.first() || DOKUM.f_first() ?}
         || _continue:=1;
            DOKUM.grp_sel(_wer_gr,,_wer,_tab,_fr,,,,_fb,_fa,,,'maximized')
         ?};
         _fa()
      ?}
   ?};
   _loop:=_Names.next()
!};

{? _continue
|| {? _zakr='KKH'
   || DOKUM.grp_splt(_wer_gr,'','vertical','dokumza');
      _fb:="__CTR.PODGLAD:=1;
           exec('bobs_dokumza','bloby')";
      _fa:="";
      DOKUM.grp_ctr(_wer_gr,DOKUMZA,__CTR.WIN_ACR,__CTR.WIN_ID,,,,,20,,_fb,_fa,,'maximized_with_title');

      DOKUM.grp_splt(_wer_gr,'','horizontal','dokum_html',16);
      _fb:="";
      _fa:="";
      DOKUM.grp_ctr(_wer_gr,,__CTH.WIN_ACR,,'Treść'@,,,,80,,_fb,_fa,,'maximized')

   |? _zakr='BL' | _zakr='BUID'
   || DOKUM.grp_splt(_wer_gr,'panel0','vertical','view');
      DOKUM.grp_edit(_wer_gr,,__REDVIEW,'Podgląd'@,,,,,'maximized');

      DOKUM.grp_splt(_wer_gr,'panel0','horizontal','atta',20);
      _fb:="__CTR.PODGLAD:=1;
            exec('bobs_dokumza','bloby')";
      _fa:="";
      DOKUM.grp_ctr(_wer_gr,DOKUMZA,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_fb,_fa,,'maximized');

      _fb:="";
      _fa:="";
      DOKUM.grp_ctr(_wer_gr,,__CTH.WIN_ACR,,'Log'@,,,,80,,_fb,_fa,,'maximized')

   |? _zakr='ZAL'
   || DOKUM.grp_splt(_wer_gr,,'vertical','nlook1');
      DOKUM.grp_sel(_wer_gr,DOKTYP,'CECH',,,,,,,,,,'maximized_with_title','doktyp');

      {? var_pres('__CTR0')>0 & var_pres('__CTR')>0
      || DOKUM.grp_splt(_wer_gr,'panel0','horizontal','nlook2',16);
         _fb:="__CTR.PODGLAD:=1;
               exec('bobs_dokumza','bloby')";
         _fa:="";
         DOKUM.grp_ctr(_wer_gr,DOKUMZA,__CTR.WIN_ACR,__CTR.WIN_ID,'Załączniki'@,,,,,,_fb,_fa,,'maximized')
      ?}

   ?}

?};

{? _continue>0
||
   DOKUM.win_sel(_wer_gr);
   DOKUM.select()
||
   FUN.info('Brak dokumentów w archiwum.'@)
?};

DOKUM.cntx_pop();
DOKTYP.cntx_pop();
DOKUMLOG.cntx_pop();
{? _flt_jest
|| DOKUM.cntx_psh();
   DOKUM.prefix();
   exec('set','#filter',DOKUM,_filtr);
   DOKUM.cntx_pop()
?};

{? _zakr='KKH'
|| exec('arch_kkh_end','dokum')
|? _zakr='BL' | _zakr='BUID'
|| exec('arch_bl_end','dokum')
|? _zakr='ZAL'
|| exec('arch_zal_end','dokum')
?};

~~


\kn_poz_tr_add_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Trigger add przed KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? KN_POZ.DOKUM
||
   DOKUM.cntx_psh();
   DOKUM.use(ref_name(KN_POZ.DOKUM));
   KN_POZ.DOKUMOID:=KN_POZ.DOKUM().ONEID;
   DOKUM.cntx_pop()
?};
1


\kn_poz_tr_put_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Trigger put przed KN_POZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('kn_poz_tr_add_b','dokum')


\refsql_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: Aktualizuje DOKUM.REFSQL
::   WE: _a - stary refsql
::       _b - nowy refsql
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref_old:=_a;
_ref_new:=_b;
{? do_state()<>2
|| _Names:=DOKUM.names();
   _loop:=_Names.first();
   {!
   |? _loop
   |!
      DOKUM.use(_Names.NAME);
      _msk:=DOKUM.name()+2; {? _msk='um' || _msk:='og' ?};
      DOKUMLOG.use('dol'+_msk);
      DOKUM.cntx_psh();
      DOKUMLOG.cntx_psh();
      DOKUM.index('DOKUM');
      DOKUM.prefix(REF.FIRMA,_ref_old);
      {? DOKUM.first
      ||
         {!
         |?
            DOKUM.cntx_psh();
            DOKUM.clear();
            DOKUM.REFSQL:=_ref_new;
            {? DOKUM.put()
            || DOKUMLOG.index('DOKUM');
               DOKUMLOG.prefix(DOKUM.uidref(),);
               {? DOKUMLOG.first()
               || {!
                  |? exec('dokumlog_update','zbl_dok',_ref_old,_ref_new);
                     DOKUMLOG.next()
                  !}
               ?}
            ?};
            DOKUM.cntx_pop();
            DOKUM.first()
         !}
      ?};
      DOKUM.cntx_pop();
      DOKUMLOG.cntx_pop();
      _loop:=_Names.next()
   !}
?}


\del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.14]
:: OPIS: DOKUM.del
::   WE: [_a] - next_lock
::       [_b] - mod_result
::       [_c] - (domyślna DOKUM.ref() z bufora DOKUM) DOKUM.ref()
::       [_d] - [''/'oldnumer] '' - (domyślna) bez aktualizacji numeracji, 'oldnumer' - aktualizacja numeracji
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_next_lock:={? var_pres('_a')=type_of(0) || _a || 0 ?};
_mod_result:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_dokum:={? var_pres('_c')=type_of(null()) || _c || DOKUM.ref() ?};
_oldnumer:={? var_pres('_d')=type_of('') & _d='oldnumer' || 1 || 0 ?};

_archiwum:=ref_name(_dokum)<>'dokum';
_maska:=ref_name(_dokum)+2;
:: DOKTYP
DOKTYP.cntx_psh();
{? _archiwum
|| DOKTYP.use((DOKTYP.name()-2)+_maska)
|| DOKTYP.use('doktyp')
?};
DOKTYP.cntx_psh();
DOKTYP.index('DOKDC');
DOKTYP.prefix(_dokum);
{? DOKTYP.first() || {! |? DOKTYP.del() !} ?};
DOKTYP.cntx_pop();
DOKTYP.cntx_pop();
:: DOKUMP, DOKUMPA
{? _archiwum
||
   DOKUMPA.cntx_psh();
   DOKUMPA.use((DOKUMPA.name()-2)+_maska);
   DOKUMPA.cntx_psh();
   DOKUMPA.index('DISP');
   DOKUMPA.prefix(_dokum);
   {? DOKUMPA.first() || {! |? DOKUMPA.del() !} ?};
   DOKUMPA.cntx_pop();
   DOKUMPA.cntx_pop()
||
   DOKUMP.cntx_psh();
   DOKUMP.index('DISP');
   DOKUMP.prefix(_dokum);
   {? DOKUMP.first() || {! |? DOKUMP.del() !} ?};
   DOKUMP.cntx_pop()
?};
:: DOKUMPT, DOKUMPTA
{? _archiwum
||
   DOKUMPTA.cntx_psh();
   DOKUMPTA.use((DOKUMPTA.name()-2)+_maska);
   DOKUMPTA.cntx_psh();
   DOKUMPTA.index('DISP');
   DOKUMPTA.prefix(_dokum);
   {? DOKUMPTA.first() || {! |? DOKUMPTA.del() !} ?};
   DOKUMPTA.cntx_pop();
   DOKUMPTA.cntx_pop()
||
   DOKUMPT.cntx_psh();
   DOKUMPT.index('DISP');
   DOKUMPT.prefix(_dokum);
   {? DOKUMPT.first() || {! |? DOKUMPT.del() !} ?};
   DOKUMPT.cntx_pop()
?};
:: DOKUMZ
exec('dokumz_del','dokum_zal',_dokum);
:: DOKUM
{? @.DOKUM.count()
||
   0
||
   {? _oldnumer
   ||
:: aktualizacja numeracji
      POM.TAB:='DOKUM';
      POM.TYPDOK:='SYS';
      numer:=@.DOKUM.NR;
      oldnumer:=1;
      exec('nr_old','numery')
   ?};
   _uid:=@.DOKUM.uidref(); _rr:=@.DOKUM.del(_next_lock,_mod_result); exec('dokumlog_del','zbl_dok',_uid);
   _rr
?}


\arch_kkh_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Archiwum kontaktów - deklaracje stałych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR0');
VAR_DEL.delete('__CTH0');

{? var_pres('__CTR') > 0
|| __CTR0:=obj_ntab_add(__CTR)
?};
VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB', 'BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','TAB_REF','BLOKUJ','PODGLAD');
__CTR.BTAB:='DOKUMZA';
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','bloby','dokza_ctr',':',75);
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('sep','#file',1);
__CTR.DND:=1;
__PAR188:=PAR_SKID.get(188)='T';
__CTR.TAB_REF:=null();
__CTR.BLOKUJ:=0;
__CTR.PODGLAD:=0;

{? var_pres('__CTH') > 0
|| __CTH0:=obj_ntab_add(__CTH)
?};
VAR_DEL.delete('__CTH');
__CTH:=obj_new('ctr_id','FILEPATH','FML_SAVE','FML_CONFIG','WIN_ACR','GRP','RESULT','CLOSE','ACTIVE');
_sep:=exec('sep','#file');
_filelocal:=exec('tmp_filepath','#file',$SYSLOG.tm_stamp()+'.html');
_env_html:=exec('html_core','#edit',DOKUM,_filelocal,'dk_ar_html','Treść',,0,0,0,0,0);
__CTH.ctr_id:=_env_html.ctr_id;
__CTH.FILEPATH:=_env_html.FILEPATH;
__CTH.FML_SAVE:=_env_html.FML_SAVE;
__CTH.FML_CONFIG:=_env_html.FML_CONFIG;
__CTH.WIN_ACR:=_env_html.WIN_ACR;
__CTH.GRP:=_env_html.GRP;
__CTH.RESULT:=_env_html.RESULT;
__CTH.CLOSE:=_env_html.CLOSE;
__CTH.ACTIVE:=0;

~~


\arch_kkh_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Archiwum kontaktów dokumentów - usunięcie stałych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR');
{? var_pres('__CTR0') > 0
|| __CTR:=obj_ntab_add(__CTR0);
   VAR_DEL.delete('__CTR0')
?};
VAR_DEL.delete('__CTH');
{? var_pres('__CTH0') > 0
|| __CTH:=obj_ntab_add(__CTH0);
   VAR_DEL.delete('__CTH0')
?};
~~


\arch_opis_html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: przygotowanie danych dla archiwalnego DOKUM.OPIS pokazywanym w edytorze HTML
::   WE: [_a] - wyczyszczenie okna z edytorem, domyślnie 0
::   WY:
:: ~OST: INFOPEN
::----------------------------------------------------------------------------------------------------------------------
_czysc:=0;
{? var_pres('_a')=type_of(0)
|| _czysc:=_a
?};

{? ~_czysc
|| _fget:=DOKUM.get();
   {? _fget & DOKUM.f_active()>=2
   || _fget:=DOKUM.f_test()
    ?};
   _czysc:=~_fget
?};

_filelocal:=__CTH.FILEPATH;

_can_continue:=2;
{? ~_czysc
||
   _memo:=DOKUM.memo_txt(,1,'OPIS');
   _tab:=tab_tmp(1,
         'ID','STRING[1]','aaa',
         'OPIS','SYS_MEMO','Nazwa pola 1',
         'BLOB','BLOBRAW','Blob');
   {? _memo*'<html>'=0 & _memo*'<!DOCTYPE html>'=0
   || _memo:=gsub(_memo,'\n','<br>')
   ?};
   _tab.blank();
   {? _tab.add()
   || _tab.memo_set(_memo);
      _tab.memo_put(,'OPIS');
      _file_memo:=_tab.memo_get('r','OPIS');
      {? var_pres('_file_memo')>100 & _file_memo.is_open()
      || _tab.bl_put('BLOB',_file_memo,0,,'info.txt');
         _can_continue:=_tab.bl_get('BLOB',_filelocal)
      || _can_continue:=0
      ?}
   ?};
   ~~
::   _file_memo:=DOKUM.memo_get('r','OPIS',0);
::   {? var_pres('_file_memo')>100 & _file_memo.is_open()
::   || _html:=0;
::      {!
::      |? _line:=_file_memo.fread();
::         _continue:=_line<>'\n';
::         {? _line<>'\n'
::         || {? _html=0 & _line*'<html>'>0
::            || _html:=1
::            ?};
::            {? _html=0 & _line*'<!DOCTYPE html>'>0
::            || _html:=1
::            ?};
::            {? _html=0
::            || _line+='<br>'
::            ?};
::            _line+='\n';
::            _file_copy.fwrite(_line)
::         ?};
::         _continue
::      !};
::      _file_memo.fclose()
::   || _can_continue:=1
::   ?}
?};

{? __CTH.ACTIVE
|| {? _can_continue & ~_czysc
   || ctr_call(,__CTH.ctr_id,'readFile',gsub(_filelocal,'\\','\\\\'))
   || ctr_call(,__CTH.ctr_id,'resetContent','')
   ?}
|| {? _can_continue
   || {? _czysc
      || fopen(_filelocal,'Uw',0,,1)
      ?};
      __CTH.FML_CONFIG();
      __CTH.ACTIVE:=1
   ?}
?};

~~


\arch_bl_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Archiwum Businesslink - deklaracje stałych
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__ARCH_BL')<0
|| __ARCH_BL:=0
?};
__ARCH_BL+=1;
_ctr_naz:='__CTR0'+$__ARCH_BL;
_cth_naz:='__CTH0'+$__ARCH_BL;

VAR_DEL.delete('__REDVIEW');
__REDVIEW:=exec('red_view','zbl_dok');
_fml:="
  VAR_DEL.delete('"+_ctr_naz+"');
  VAR_DEL.delete('"+_cth_naz+"');
  {? var_pres('__CTR') > 0
  || "+_ctr_naz+":=obj_ntab_add(__CTR)
  ?};
  {? var_pres('__CTH') > 0
  || "+_cth_naz+":=obj_ntab_add(__CTH)
  ?}";
_fml:=$_fml;
_fml();
exec('ctr_zal_init','zbl_dok','DOKUMZA');
exec('ctr_log_init','zbl_dok');
~~


\arch_bl_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Archiwum Businesslink - usunięcie stałych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__REDVIEW');
VAR_DEL.delete('__CTR');
VAR_DEL.delete('__CTH');

{? var_pres('__ARCH_BL')>0 & __ARCH_BL>0
|| _ctr_naz:='__CTR0'+$__ARCH_BL;
   _cth_naz:='__CTH0'+$__ARCH_BL;
   _fml:="
     {? var_pres('"+_ctr_naz+"') > 0
     || __CTR:=obj_ntab_add("+_ctr_naz+");
        VAR_DEL.delete('"+_ctr_naz+"')
     ?};
    {? var_pres('"+_cth_naz+"') > 0
    || __CTH:=obj_ntab_add("+_cth_naz+");
       VAR_DEL.delete('"+_cth_naz+"')
    ?}";
    _fml:=$_fml;
    _fml();
   __ARCH_BL-=1
?};
~~


\arch_zal_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Archiwum załączników dokumentów - deklaracje stałych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR0');
{? var_pres('__CTR') > 0
|| __CTR0:=obj_ntab_add(__CTR)
?};
VAR_DEL.delete('__CTR');
__CTR:=obj_new('BTAB', 'BBLOB','TAB','WIN_ID','WIN_ACR','DIR_SEP','DND','BLOKUJ','PODGLAD','WIN');
__CTR.BTAB:='DOKUMZA';
__CTR.TAB:='DOKUM';
__CTR.BBLOB:='DOKUM';
__CTR.WIN_ACR:=exec('mk_ctr','bloby','dokum_dokz',':');
__CTR.WIN_ID:=-(1-__CTR.WIN_ACR);
__CTR.DIR_SEP:=exec('sep','#file',1);
__CTR.DND:=1;
__CTR.BLOKUJ:=0;
__CTR.PODGLAD:=1;
 __CTR.WIN:='WERA';
~~


\arch_zal_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Archiwum załączników dokumentów - usunięcie stałych
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__CTR');
{? var_pres('__CTR0') > 0
|| __CTR:=obj_ntab_add(__CTR0);
   VAR_DEL.delete('__CTR0')
?};
~~


\czy_autoadd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Sprawdza czy dokument dodano automatycznie
::   WE: _a - ref
::       _b - 0/1 - sprawdzać czy dokument BL
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_b:={? var_pres('_b')=type_of(0) || _b || 0 ?};
_refsql:=$_a;
_result:=0;
DOKUM.cntx_psh();
_Names:=DOKUM.names();
_loop:=_Names.last();
{!
|? _loop
|!
   DOKUM.use(_Names.NAME);
   DOKUM.cntx_psh();
   DOKUM.index('AUTOADD');
   DOKUM.prefix(REF.FIRMA,_refsql,'T');
   _result:=DOKUM.first() & (_b=0 | DOKUM.BL='T');
   DOKUM.cntx_pop();
   _loop:=_result=0 & _Names.prev()
!};
DOKUM.cntx_pop();
_result


\zalacz_akt_arch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Sprawdzeinie, czy załączniki dokumentu są aktualne czy w archiwum
::   WE: refsql dokumentu
::   WY: 0 - brak załaczników, 1 - jest załącznik aktualny, -1 - jest załacznik archiwalny
::----------------------------------------------------------------------------------------------------------------------
_refsql:='';
{? var_pres('_a')>0
|| _refsql:=_a
?};
_jest:=0;

{? _refsql<>''
|| DOKUM.cntx_psh();
   DOKUM.index('DOKUM');
   DOKUM.prefix(REF.FIRMA,_refsql);
   {? DOKUM.first()
   || _jest:=1
   ?};
   {? ~_jest
   || _Names:=DOKUM.names();
      {? _Names.first()
      || {!
         |? {? _Names.NAME<>'dokum' & _Names.NAME<>'dok__'
            || DOKUM.use(_Names.NAME);
               DOKUM.index('DOKUM');
               DOKUM.prefix(REF.FIRMA,_refsql);
               {? DOKUM.first()
               || _jest:=-1
               ?}
            ?};
            ~_jest & _Names.next()
         !}
      ?}
   ?};
   DOKUM.cntx_pop()
?};

_jest


\archive_przywr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [21.37]
:: OPIS: Przywracanie z archiwum dokumentu DOKUM
::   WE: _a - zakres danych BL/KKH/ZAL/ZWS
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('mp_run_a','#b__box');
_params.ACT_UID:='ZWS_DOK_ARCH';
_params.PROC_START:='T';
_params.PORTS_IN:=exec('portsIn','#b__box',_params.ACT_UID);
_params.AKCJA:='Przywróć';
{? _a<>'ZWS'
||
   _sel_size:=DOKUM.sel_size();
   _continue:=0;
   {? _sel_size>1
   ||
      {? _a='BL'  || _continue:=FUN.ask('Czy przywrócić dokumenty?'@)
      |? _a='KKH' || _continue:=FUN.ask('Czy przywrócić kontakty?'@)
      |? _a='ZAL' || _continue:=FUN.ask('Czy przywrócić załączniki?'@)
      ?}
   ||
      {? _a='BL'  || _continue:=FUN.ask('Czy przywrócić dokument?'@)
      |? _a='KKH' || _continue:=FUN.ask('Czy przywrócić kontakt?'@)
      |? _a='ZAL' || _continue:=FUN.ask('Czy przywrócić załącznik?'@)
      ?}
   ?};
   {? _continue=0 || DOKUM.sel_adel(); return() ?};
   {? _sel_size || _params.GRUPA:='T' ?};
   DOKUM.cntx_psh();
   _params.UIDREF:=DOKUM.uidref();
   exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DOKUM',DOKUM.ref());
   exec('ini_kom','#message','Przywracanie dokumentów z archiwum');
   exec('mp_run','#b__box',_params);
   exec('end_kom','#message');
   DOKUM.cntx_pop();
   0
||
   DOKUM.cntx_psh();
   _red:=DOKUM.mk_edit('Parametry'@,,'dokum_arch_par');
   DOKUM.win_esep(_red,'Okres przywracania'@);
   DOKUM.win_efld(_red,,'DATAKONT',,,,,,'Od daty'@);
   DOKUM.win_esep(_red,'Ograniczenia'@);
   DOKUM.win_efld(_red,,'KH','KOD','KOD1',,,0,'Kontrahent'@);
   DOKUM.win_efld(_red,,'KH','NAZ',,,,0,,1);
   _ff:='key:F2';
   DOKUM.win_ebtn(_red,'text=%1,panel=bottom,align=end,display=1'['A&kceptuj'@],_ff);
   _ff:='key:Esc';
   DOKUM.win_ebtn(_red,'text=%1,panel=bottom,align=end,display=1'['&Anuluj'@],_ff);
   DOKUM.win_edit(_red);
   DOKUM.blank(1);
   KHVZ.KONTAKTY:='T';
   KHVZ.KHVP:='dokum';
   _valid:="
      {? DOKUM.DATAKONT=date(0,0,0) & DOKUM.KH=null()
      ||
         FUN.info('Należy podać co najmniej jeden z parametrów.'@);
         'DATAKONT'
      ||
         ''
      ?}
   ";
   {? DOKUM.edit(_valid)
   ||
      {? DOKUM.DATAKONT<>date(0,0,0)
      ||
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'DO',DOKUM.DATAKONT)
      ?};
      {? DOKUM.KH<>null()
      ||
         exec('portsInSet','#b__box',_params.PORTS_IN,_params.ACT_UID,'KH',DOKUM.KH)
      ?};
      exec('mp_run','#b__box',_params)
   ?};
   DOKUM.cntx_pop();
   ''
?}


\ob_edi_o
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Odczytuje komunikat EDI dla kontatków w obiegu
::   WE: _a - faktura (1), wniosek (2)
::       _b - Automatyczna? (T/N)
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_isAUTO:={? var_press('_a')>0 || _b || 'N' ?};
DOKUM.BL_LOG:=null();
{? DOKUM.BL_TYPE='' || DOKUM.BL_TYPE:='INV' ?};
DOKUM.put();
_edi:=exec('env','edi_wspolne');
{? ~_edi.init()
|| FUN.info('Nie powiodło się zainicjowanie środowiska.'@)
||
   _edi.context:=exec('context_obj_bl','edi_wspolne');
   _edi.context.DOKUM:=DOKUM.ref();
   _istdef:=exec('istdef','zbl_dok',_edi.context,{? _a=1 || 'OBG' || 'OBE' ?});
   {? _istdef=null()
   || _edi.context.TYPE:='';
      FUN.info('Nie ustalono definicji komunikatu.'@)
   ||
::          dodatkowy zapis w dzienniku inforumujący o starcie odczytu lub zapisu edi
      _dzien_a:=exec('dzien_add','edi_wspolne',ISTDEF.ref(),'W',date(),time(),OPERATOR.USER,'LOG',''
         ,null(),'Start','',,,'B');
      {? DOKUM.BL_STAT=exec('registration_failed','zbl_stat') |
         (_isAUTO='N' & {? _a=1 || ~Plugin.runnable('BL_OBG_001') || ~Plugin.runnable('BL_OBE_001') ?})
      || _edi.sza:=0
      || _edi.sza:=1
      ?};
      DOKUM.cntx_psh();
      exec('edi_read','edi_wspolne',_istdef);
      DOKUM.cntx_pop();
::          wtyczka wdożeniowa - pobranie danych dodatkowych z przychodzącego dokumentu Businesslink
      {? DOKUM.get() || Plugin.run('BL_DOKUMXML_GET_OBG',_edi) ?};
      {? DOKUM.get() || DOKUM.bl_put('BL_LOG',_edi.LOG,,,'log.txt') ?};
      _edi.logSave(_dzien_a);
      {? DOKUM.get() & DOKUM.BL_STAT=exec('during_registration','zbl_stat')
      || _result:=1
      ?}
   ?}
?};
_result


\del_dokumz_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Usuwa powiązany załącznik wysyłany do KSeF
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
DOKUMZ.cntx_psh(); DOKUMZ.index('KIND'); DOKUMZ.prefix(DOKUM.ref());
{? DOKUMZ.find_key(exec('bl_type_ksef_dks_inv','zbl_dok'),) |
   DOKUMZ.find_key(exec('bl_type_ksef_dks_inv_corr','zbl_dok'),)
|| {? DOKUMZ.EDI_I<>''
   ||
      exec('FindAndGet','#table',EDI_I,DOKUMZ.EDI_I,,"
         exec('dzien_anul','edi_wspolne',date(),time(),OPERATOR.USER)
      ")
   ?};
   {? exec('upgrade2325_blbc1','zbl') & DOKUMZ.BC='T'
   || DOKUMZ.prefix();
      DOKUMZ.DOK:=null();
      _res:=DOKUMZ.put(1);
      DOKUMZ.prefix(DOKUM.ref())
   || _res:=DOKUMZ.del()
   ?};
   {? _res
   || DOKUM.KSEFAUTH:=null();
      DOKUM.BL_STAT:=exec('before_send','zbl_stat');
      DOKUM.put();
      exec('dokum_tr_del_b','zbl')
   ?}
|? (DOKUMZ.find_key(exec('bl_type_ksef_inv','zbl_dok'),) |
   DOKUMZ.find_key(exec('bl_type_ksef_inv_corr','zbl_dok'),))
|| DOKUM.KSEFAUTH:=null();
   DOKUM.BL_STAT:=exec('before_send','zbl_stat');
   DOKUM.put();
   exec('dokum_tr_del_b','zbl')
?};
DOKUMZ.cntx_pop();
_res


\faks_info_val
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Informacja o załącznikach
::----------------------------------------------------------------------------------------------------------------------
FAKS.cntx_psh();
FAKS.prefix();
{? var_pres('_a')=type_of('') & +_a=16
|| {? ~FAKS.seek(_a,ref_name(_a)) || FAKS.cntx_pop(); return(0) ?}
|| FAKS.get()
?};

DOKUM.cntx_psh();
DOKUM.index('STAN');
:: dokumentów ogółem (żeby w ogóle wyświetlać progress)
DOKUM.prefix(REF.FIRMA,$FAKS.ref());
_ile:=DOKUM.size();
:: dokumentów wystawionych do wysłania e-mailem
DOKUM.prefix(REF.FIRMA,$FAKS.ref(),'T');
_ile_m:=DOKUM.size();
:: dokumentów wysłanych e-mailem
DOKUM.prefix(REF.FIRMA,$FAKS.ref(),'T','T');
_ile_w:=DOKUM.size();
DOKUM.index('STANB');
:: dokumentów wysłanych do Businesslinka
DOKUM.prefix(REF.FIRMA,$FAKS.ref(),'T');
_ile_b:=DOKUM.size();
:: dokumentów odebranych w Businesslinku
DOKUM.prefix(REF.FIRMA,$FAKS.ref(),'T','T');
_ile_o:=DOKUM.size();
DOKUM.cntx_pop();

{? _ile
|| {? _ile_m<>_ile_w || _info:='Nie wysłano załączników ( %1 / %2 )'@[$(_ile_m-_ile_w),$_ile_m]
   |? _ile_m<>0      || _info:='Wysłano załączniki ( %1 / %2 )'@[$_ile_w,$_ile]
   |? _ile_b<>0      || _info:='Wysłano załączniki ( %1 / %2 )'@[$_ile_b,$_ile]
                     || _info:='Załączniki ( %1 ) '@[$_ile]
   ?}
|| _info:='Brak załączników'@
?};
FAKS.INFO:=_info;
_wyn:=FAKS.put(1);
FAKS.cntx_pop();
FAKS.INFO:=_info;
_wyn


\find_keyref_biprel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Znajduje B_KEYREF.BI_PREL
::   WE: _a - identyfikator czynności
::       _b - poszukiwany uidref w B_KEYREF
::   WY: B_KEYREF.BI_PREL
::----------------------------------------------------------------------------------------------------------------------
B_KEYREF.cntx_psh();
B_KEYREF.index('UID_REF'); B_KEYREF.prefix(_a,_b);
{? B_KEYREF.first()
|| _bi_prel:=B_KEYREF.BI_PREL
|| _bi_prel:=null
?};
B_KEYREF.cntx_pop();
_bi_prel


\users_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła zwraca listę użytkowników, którzy są przypisani do danej roli
::   WE: _a - złączenie do B_PREL
::   WY: lista użytkowników z roli
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_b_prel:=_a;
B_ROLE.cntx_psh(); B_USRROL.cntx_psh(); USERS.cntx_psh();
B_ROLE.prefix();
{? B_ROLE.seek(exec('FindAndGet','#table',B_PREL,_b_prel,,"B_ROLE",null()))
|| B_USRROL.index('UNIK');
   B_USRROL.prefix(REF.FIRMA,B_ROLE.ref());
   {? B_USRROL.first()
   || {!
      |? _result+=B_USRROL.USERS().KOD+',';
         B_USRROL.next()
      !}
   ?}
?};
B_ROLE.cntx_pop(); B_USRROL.cntx_pop(); USERS.cntx_pop();
_result


\ntc_send
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Wysyłka notyfikacji o kontakcie
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_params:=exec('obj_ntab_set','#array',,'LINK',DOKUM.uidref());
_link_interm:=link_uri(_params);
_formula:={? exec('get','#params',8402,,OPERATOR.USER)='T'
          || '{"url": %1}'[json_val(_link_interm)]
          || '{"url": %1, "delete": %2}'[json_val(_link_interm), json_val(0)]
          ?};
ZDARZT.cntx_psh();
KH_OSOB.cntx_psh();
KH.cntx_psh();
_text:=
{? DOKUM.DATAKONT<> date(0,0,0) || 'Data kontaktu: %1,\n'[$DOKUM.DATAKONT] || '' ?}+
{? DOKUM.DOT<>null || 'Typ kontaktu: %1,\n'[DOKUM.DOT().ZDARZ] || '' ?}+
{? DOKUM.KH_OSOB<>null || 'Osoba kontaktowa: %1 %2,\n'[KH_OSOB.NAZWISKO,KH_OSOB.IMIE] || '' ?}+
{? DOKUM.KR_OP<>'' || 'Temat: %1,\n'[DOKUM.KR_OP] || '' ?}+
{? DOKUM.KH<>null || 'Kontrahent: %1,\n'[DOKUM.KH().NAZ_P] || '' ?}+
{? DOKUMP.INFO<>'' || 'Informacje dodatkowe: %1,\n'[DOKUMP.INFO] || '' ?};
_text:=_text-2;
ZDARZT.cntx_pop();
KH_OSOB.cntx_pop();
KH.cntx_pop();


ntc_send(USERS.KOD,,,'Powiadomienie o kontakcie'@,_text,,,DOKUMP.DT,,'Idź do kontaktu'@,_formula)


\bl_ufd_read
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Zwraca ISTDEF.ref definicji komunikatu dla odczytu - dokumenty księgowe i obiegi
::   WE: _a - DOKUM.ref() - dokumentu Businesslink
::       [_b] - DOKUMZ.ref() - załącznik Businesslink
::       [_c] - context
::       [_d] - FKS/OBE/OBG - obszar
::   WY: null/ISTDEF.ref
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_dokumz:={? var_pres('_b')=type_of(null()) || _b || null() ?};
_context:={? var_pres('_c')>0 || _c || ~~ ?};
_area:={? var_pres('_d')>0 || _d || 'FKS' ?};
_update:=exec('upgrade2325_ksef2','zbl');
{? _area='OBG' || _obieg:=1 || _obieg:=2 ?};


_result:=null();

_schemat:=params_exec('schemat','zbl_dok',_dokum,_dokumz,_context);

DOKUM.cntx_psh();
DOKUM.prefix();
{? DOKUM.seek(_dokum)
|| ISTDEF.cntx_psh();
   {? _update
   || {? _area='FKS'
      || ISTDEF.index('BL_DATA1');
         ISTDEF.prefix(__Firma,'ZWS','E',DOKUM.BL_TYPE+'_DKS',_schemat,'T','N',);
         _result:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?}
      || ISTDEF.index('BL_DATA1');
         ISTDEF.prefix(__Firma,'ZWS','E',DOKUM.BL_TYPE+{? _obieg=1 || '_OBG' || '_OBE' ?},_schemat,'T','N',);
         _result:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?}
      ?}
   || {? _area='FKS'
      || ISTDEF.index('BL_DATA');
         ISTDEF.prefix(__Firma,'ZWS','E',DOKUM.BL_TYPE+'_DKS','T','N',);
         _result:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?}
      || ISTDEF.index('BL_DATA');
         ISTDEF.prefix(__Firma,'ZWS','E',DOKUM.BL_TYPE+{? _obieg=1 || '_OBG' || '_OBE' ?},'T','N',);
         _result:={? ISTDEF.find_le(DOKUM.DATA) || ISTDEF.ref() || null() ?}
      ?}
   ?};
   ISTDEF.cntx_pop()
?};
DOKUM.cntx_pop();

_result


\ksef_to_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Generowanie PDF z KSeF
::   WE: _a - DOKUM
::       [_b] - 1 - przychodzące, 0 - wysyłane
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('upgrade2325_jst01','zbl') || return(0) ?};
_in:={? var_pres('_b')=type_of(1) || _b || 1 ?};
DOKUM.cntx_psh(); DOKUM.prefix();
DOKUMZ.cntx_psh(); DOKUMZ.index('KIND');
_dokum:=_a;
DOKUMZ.prefix(_dokum);
{? DOKUM.seek(_dokum) &
   ~DOKUMZ.find_key(exec('bl_type_ksef_inv','zbl_dok'),) &
   ~DOKUMZ.find_key(exec('bl_type_ksef_inv_corr','zbl_dok'),) &
   ~DOKUMZ.find_key(exec('bl_type_ksef_dks_inv','zbl_dok'),) &
   ~DOKUMZ.find_key(exec('bl_type_ksef_dks_inv_corr','zbl_dok'),)
|| 0
|| on_error(2);
   no_msg(1);
   _nrksef:=DOKUM.NRKSEF;
   _tmp_dir:=fmk_tmp_dir(0);
   _pth:=_tmp_dir.get_path();
   _sep:=exec('sep','#file',1);
   _file_html:=exec('ksef_to_html','dokum',_pth,_sep,_nrksef,_in);
   _pdf_name:='faktura_pdf_'+$SYSLOG.tm_stamp()+'.pdf';
   _file_pdf:=_pth+_sep+_pdf_name;
   _windows:=sys_name(1)='WINDOWS';
   _zip:={? _windows
         || 'win_htmltopdf.zip'
         || 'lin_htmltopdf.zip'
         ?};
   _zip_file:=pth_dir(_zip)+_sep+_zip;
   _hash:={? _windows
          || '67d5358ad791e262594e916749feb17d4aba2b75f046ae97281c70208c398296'
          || '01069caa06952b99702ed42c09fce055dd821c198841245427386b137e3d4c5e'
          ?};
   _testf:=fopen(_zip,'b',1,,1);
   _hashf:=hash(_testf,'sha256');
   fclose(_testf);
   &_testf;
   {? _hashf=_hash
   || funpack('zip',_zip_file,_pth);
      _create:=_pth+_sep+'bin'+_sep+'wkhtmltopdf';
      _create+=' -q -s A2 ';
      _create+=_file_html;
      _create+=' '+_file_pdf;
      {? ~_windows
      || system('chmod 755 %1'[_pth+_sep+'bin'+_sep+'wkhtmltopdf'],1)
      || _battxt:='@echo off\nstart /B /wait "" '+_create;
         _bat:=_pth+_sep+'htmltopdf.bat';
         _batfile:=fopen(_bat,'Uw',0);
         fwrite(_batfile,_battxt);
         fclose(_batfile);
         _vbstxt:='Set objShell = CreateObject("WScript.Shell")\n';
         _vbstxt+='Set objFSO = CreateObject("Scripting.FileSystemObject")\n';
         _vbstxt+='strBatPath = objFSO.GetAbsolutePathName("%1")\n'[_bat];
         _vbstxt+='objShell.Run strBatPath, 0, True\n';
         _vbstxt+='WScript.Quit';
         _vbs:=_pth+_sep+'htmltopdf.vbs';
         _vbsfile:=fopen(_vbs,'Uw',0);
         fwrite(_vbsfile,_vbstxt);
         fclose(_vbsfile);
         _cmd:=envget('COMSPEC');
         _cmd:=gsub(_cmd,'cmd.exe','wscript.exe');
         _cmd+=' /B ';
         _create:=_cmd+_vbs
      ?};
      system(_create,1);
      _nazwapdf:={? _nrksef<>'' || _nrksef || 'Faktura' ?}+'.pdf';
      {? fexists(_file_pdf)
      || DOKUMZ.blank();
         DOKUMZ.KR_OP:='Faktura - widok PDF';
         DOKUMZ.DATE:=date();
         DOKUMZ.TIME:=time();
         DOKUMZ.P_OCR:=DOKUMZ.W_OCR:='N';
         DOKUMZ.NAZWA:=_nazwapdf;
         DOKUMZ.DOK:=_dokum;
         {? exec('upgrade2325_blbc1','zbl')
         || DOKUMZ.REFSQL:=DOKUM.REFSQL
         ?};
         {? DOKUMZ.add()
         || DOKUMZ.bl_put('DOKUM',_file_pdf,0,,_nazwapdf)
         ?};
         DOKUM.prefix();
         {? DOKUM.seek(_dokum)
         || DOKUM.bl_put('DOKUMI',_file_pdf,0,,_nazwapdf)
         ?}
      ?}
   ?};
   on_error(0);
   no_msg(0)
?};
DOKUM.cntx_pop(); DOKUMZ.cntx_pop();
_file_pdf


\ksef_to_html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Tworzy html na podstawie xml z KSeF
::   WE: _a - ścieżka do folderu tymczsowego
::       _b - separator ścieżki
::       _c - nr KSeF
::       _d -  1 - przychodzące, 0 - wysyłane
::   WY: ścieżka do pliku html
::----------------------------------------------------------------------------------------------------------------------
_fdir:=_a;
_sep:=_b;
_nrksef:=_c;
_in:=_d;
_fin:=_fdir+_sep+'ksef.xml';
DOKUMZ.bl_get('DOKUM',_fin,0);
_fxsl:={? (var_pres('DOKUMZ.SCHEMAT')>0 & DOKUMZ.SCHEMAT<>'') || DOKUMZ.SCHEMAT-4+'.xsl' || '' ?};
{? _fxsl='' || _fxsl:=(exec('schemat','zbl_dok',DOKUMZ.DOK,DOKUMZ.ref()))-4+'.xsl' ?};
{? _fxsl='' || _fxsl:='Schemat_FA_VAT(1)_v9-0E.xsl' ?};
_fout:=_fdir+_sep+'ksef'+$SYSLOG.tm_stamp()+'.html';
{? fexists(_fxsl,1)
|| {? ~fexists(_fdir+_sep+_fxsl)
   || {? fcopy(_fxsl,_fdir+_sep+_fxsl,1,0,1)=0
      || return(~~)
      ?}
   || 1
   ?};
   exec('xml2html','xml',_fin,_fdir+_sep+_fxsl,_fout,_fdir);
   ferase(_fdir+_sep+_fxsl,0);
   {? fexists(_fout)=0
   || return(~~)
   ?}
?};
{? 0
|| _fout:=exec('ksef_qr','dokum',_in,_fin,_nrksef,_fdir,_sep,_fout)
?};
_fout


\ksef_link
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Zwraca odpowiedni początek linku dokumentu KSeF
::   WE: _a - 1 - przychodzące, 0 - wysyłane
::----------------------------------------------------------------------------------------------------------------------
_par:={? _a || 'BL.KSEF.DownloadEnvironment' || 'BL.KSEF.Environment' ?};
_env:=-(exec('bl_par','businesslink3',_par));
{? _env='test'
|| 'https://ksef-test.mf.gov.pl/web/verify/'
|? _env='demo'
|| 'https://ksef-demo.mf.gov.pl/web/verify/'
|| 'https://ksef.mf.gov.pl/web/verify/'
?}


\ksef_qr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Generowanie kodu QR dla tworzonego PDF-a
::   WE: _a - 1 - przychodzące, 0 - wysyłane
::       _b - ścieżka do XML z KSeF
::       _c - Nr KSeF
::       _d - ścieżka do folderu tymczasowego
::       _e - separator
::       _f - ścieżka pliku html do dodania obrazka
::----------------------------------------------------------------------------------------------------------------------
_in:=_a;
_xml:=_b;
_nrksef:=_c;
_pth:=_d;
_sep:=_e;
_html:=_f;

_link:=exec('ksef_link','dokum',_in)+_nrksef+'/';
_file:=fopen(_xml,'b',0,,1);
_hash:=_base:=hash(_file,'sha256',1);
fclose(_file);
_inet:=inet_get('http://');
_url:=_inet.url_encode(_base);
_link+=_url;
_codepth:=_pth+_sep+'qrcode.png';
_code:=fopen(_codepth,'b',0,,1);
barcode('QRCode',_link,_code,'H');
fclose(_code);
_new:=_pth+_sep+'ksefhtmlqr'+$SYSLOG.tm_stamp()+'.html';
_hfile:=fopen(_html,'Ur',0);
_newfile:=fopen(_new,'Uw',0);
_licznik:=0;
{! |? (_wiersz:=fread(_hfile))<>'\n' & (_licznik+=1; _licznik<10) |!
      _licznik:=0;
      {? form(_wiersz)<>'' & form(_wiersz)<>'</body>'
      || fwrite(_newfile,_wiersz)
      |? form(_wiersz)='</body>'
      || exec('whtml','dokum',_newfile,_codepth,_link,_nrksef);
         fwrite(_newfile,_wiersz)
      ?}
!};
fclose(_hfile);
fclose(_newfile);
_new


\whtml
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Zapisuje kod qr do html-a
::   WE: _a - uchwyt do pliku
::       _b - ścieżka do kodu QR
::       _c - link
::       _d - nr ksef
::----------------------------------------------------------------------------------------------------------------------
_newfile:=_a;
_qr:=_b;
_link:=_c;
_nrksef:=_d;

_file:=fopen(_qr,'b',0,,1);
_qrb:=base64('encode',_file);
fclose(_file);

_txt:='<div>';
_txt+='Numer KSeF: <b>%1</b>'[_nrksef];
_txt+='<br>';
_txt+='<b>Sprawdź, czy Twoja faktura znajduje się w KSeF</b>';
_txt+='<br><br>';
_txt+='<img height="207" alt="logo" src="data:image/png;base64,%1" />'[_qrb];
_txt+='<br><br>';
_txt+='Masz problem z otwarciem kodu QR? Skopiuj link i wklej do okna przeglądarki: <br> %1'[_link];
_txt+='<br><br>';
_txt+='</div>';

fwrite(_newfile,_txt);
1


\ksef_sended_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła sprawdzająca czy dogenerować podgląd KSeF dokumentu wysłanego z księgowości
::   WE: _a - DOKUM.ref()
::       _b - DOKUM.DOKR
::----------------------------------------------------------------------------------------------------------------------
_dokum:=_a;
_dokr:=_b;

{? (4+_dokr)='doku'
|| exec('ksef_to_pdf','dokum',_dokum,0)
?}


:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 1b259425441d1ecb62db19b463ef2275fd3719411ada4f21d42de3e446ee10b98311d5f1c932eb7f9e4219bee796c94b375d0104e44130aed776fa5b951267927533533891048655f30be4cba54f893995011d198d619f556960a2dfdf6aa3ac9a9a860515faac485b939a6509ae336b3e9f095a10309922f0a3bcf71c2e7ea2

:!UTF-8
:: (c) Macrologic SA odzial Wroclaw Wszelkie prawa zastrzeżone
::========================================================================================================================
:: Nazwa pliku: ml_przel.fml
:: Utworzony: 03.01.2008
:: Autor: AK
::========================================================================================================================
:: Zawartosc: Obsluga przelewow elektronicznych w Finanse i ksiegowosc wersja budzetowa
::========================================================================================================================


\prze3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [11.21]
:: OPIS: Formula wylicza tabele z numer rozdzialu, paragrafu i zadania na potrzeby przelewu dla pol RZ PG ZAD oraz TYT
::   WE: _a - ODD.ref()
::       _b - konto analityczne
::       _c - symbol rozrachunku
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__Optmpb');
__Optmp1:=tab_tmp(,'PROZ','STRING[35]','PROZ',
                   'PPAR','STRING[35]','PPAR',
                   'PZAD','STRING[35]','PZAD',
                   'TYT','STRING[100]','TYT',
                   'SAL','REAL','SAL');

__Proz:=__Ppar:=__Par:=__Pzad:='';
_broz:=_bppar:=_bpar:=_bzad:=0;
_slu_rz:=_slu_pg:=_slu_pp:=_slu_zad:=_pozref:=null();

_vks:=3+_b;
_vsynt:=+_vks;
ROZNE.ROKKON:=SSTALE.AR;
_vsep:=ROZNE.ROKKON().SEP;
{? var_pres('_kp')<0 || _kp:=obj_new(8) ?};
{! _i:=1..8
|!
   _kp[_i]:=''
!};
_kn:=_b;
_i:=1;
{!
|?
   _poz:=_kn*_vsep;
   {? _poz>0
   || _kp[_i]:=(_poz-1)+_kn;
      _kn:=_poz-_kn;
      _i+=1;
      1
   || _kp[_i]:=_kn;
      0
   ?}
!};
KS.cntx_psh();
KS.index('SYM'); KS.prefix(ROZNE.ROKKON,_vks);
{? KS.first() & _vks=KS.SYM
|| {? BUD.index('KS'); BUD.prefix(KS.ref); BUD.first()
   ||
      _i:=2;
      {!
      |?
         {? -BUD.SLU().SLU().WZ='pozycja' & __Ppar=''
         || __Ppar:=_kp[_i];
            _slu_pp:=BUD.SLU().SLU;
            _bppar:=1
         |? -BUD.SLU().SLU().WZ='paragraf' & __Par=''
         || __Par:=_kp[_i];
            _slu_pg:=BUD.SLU().SLU;
            _bpar:=1
         |? -BUD.SLU().SLU().WZ='rozdział' & __Proz=''
         || __Proz:=_kp[_i];
            _slu_rz:=BUD.SLU().SLU;
            _broz:=1
         |? -BUD.SLU().SLU().WZ='zadanie' & __Pzad=''
         || __Pzad:=_kp[_i];
            _slu_zad:=BUD.SLU().SLU;
            _bzad:=1
         ?};
         _i+=1;
         BUD.next()
      !}
   ?}
?};
KS.cntx_pop();
{? _broz=1 & (_bpar=1 | _bppar=1)
||
   {? _bpar=1
   ||
      __Optmp1.PPAR:=__Par;
      __Optmp1.SAL:=OPTMP.SAL
   ||
      _dl_pg:=4;
      ML_PG.prefix(OKRO_F.ROK);
      {? ML_PG.first || _dl_pg:=+ML_PG.SP ?};
      __Optmp1.PPAR:=_dl_pg+__Ppar;
      __Optmp1.SAL:=OPTMP.SAL
   ?};
   __Optmp1.PZAD:=__Pzad;
   __Optmp1.PROZ:=__Proz;
   __Optmp1.SAL:=OPTMP.SAL;

   {? _bzad=1
   ||
      __Optmp1.add()
   || ZAP_OP.index('OP');
      ZAP_OP.prefix(OP.ref());
      _sum:=0;
      {? ZAP_OP.first()
      ||
         {!
         |?
            _kwota:=ZAP_OP.MA-ZAP_OP.WN;
            {? _kwota<>0
            ||
               _pozref:=ZAP_OP.POZDOK;

               __Optmp1.PZAD:=exec('przezad_op','ml_przel',_pozref,ZAP_OP.OKRO,1);
               {? __Optmp1.PZAD<>''
               || __Optmp1.SAL:=_kwota;
                  _sum+=__Optmp1.SAL;
                  __Optmp1.add();
                  __Optmp1.PZAD:=''
               ?}
            ?};
            ZAP_OP.next()
         !}
      ?};
      {? _sum<OPTMP.SAL
      || __Optmp1.SAL:=OPTMP.SAL-_sum;
         __Optmp1.add()
      ?}
   ?}

||
   KS_W.cntx_psh();
   _kswind:=KS_W.ndx_tmp(,1,'S','ROK',0,'S','SYM',0,'LP',,0); KS_W.index(_kswind);
   KS_W.prefix(SSTALE.AR, SSTALE.AR().SYNT+_b);
   {? KS_W.first()
   || {!
      |?
         {? _slu_rz=null & KS_W.SLU().SLU().WZ='Rozdział' || _slu_rz:=KS_W.SLU().SLU
         |? _slu_pg=null & KS_W.SLU().SLU().WZ='Paragraf' || _slu_pg:=KS_W.SLU().SLU
         |? _slu_pp=null & KS_W.SLU().SLU().WZ='Pozycja'  || _slu_pp:=KS_W.SLU().SLU
         ?};
         KS_W.next()
      !}
   ?};
   KS_W.ndx_drop(_kswind);
   KS_W.cntx_pop();
   _dokname:=DOK.name(); _pozname:=POZ.name(); _powname:=POW.name();
   _dalej:=1;
   POW.cntx_psh(); POZ.cntx_psh(); DOK.cntx_psh();
   _powind:=POW.ndx_tmp(,1,'POZ','KON',0,'POZ','ID',0,'SLU','SLU',0,'POZ','STR',0);
   _powind2:=POW.ndx_tmp(,1,'POZ',,0,'POZ','KON',0,'POZ','ID',0,'SLU','SLU',0);
   POW.index(_powind);
   OKRO_F.cntx_psh(); OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
::Przeszukiwanie w petli od ostatniego okresu
   {? OKRO_F.last()
   ||
      {!
      |?
         POW.use('pow_'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
         POZ.use('pozy'+OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
         _dl_pg:=4;
         ML_PG.prefix(OKRO_F.ROK);
         {? ML_PG.first || _dl_pg:=+ML_PG.SP ?};
         _dl_pp:=6;
         ML_PP.prefix(OKRO_F.ROK);
         {? ML_PP.first || _dl_pp:=+ML_PP.KP ?};
         {? _broz=0
         || POW.index(_powind);
            POW.prefix(_b, _c, _slu_rz,'Ma'); _pozref:=null();
            _dal:=POW.first()
         || _dal:=_slu_rz<>null
         ?};
         {? _dal=1
         || {!
            |?
               __Optmp1.PZAD:='';
               {? _broz=1
               || __Optmp1.SAL:=OPTMP.SAL;
                  __Optmp1.PROZ:=__Proz
               || __Optmp1.SAL:=POW.KW;
                  __Optmp1.PROZ:=POW.SLO().KOD;
                  _pozref:=POW.POZ;
                  _dalej:=0;
                  __Optmp1.PZAD:=exec('przezad_op','ml_przel',_pozref,OKRO_F.ref(),1)
               ?};


               {? _bpar=1 | _bppar=1
               ||
                  {? _bpar=1
                  ||
                     __Optmp1.PPAR:=__Par;
                     __Optmp1.SAL:=OPTMP.SAL
                  ||
                     __Optmp1.PPAR:=_dl_pg+__Ppar;
                     __Optmp1.SAL:=OPTMP.SAL
                  ?};
                  __Optmp1.add()
               ||
                  POW.cntx_psh();
                  {? _broz=1
                  || POW.index(_powind);
                     POW.prefix(_b, _c, _slu_pg,'Ma'); _pozref:=null()
                  || POW.index(_powind2);
                     POW.prefix(_pozref, _b, _c, _slu_pg)
                  ?};
                  {? POW.first
                  || {!
                     |?
                        {? POW.SLU().SLU().WZ='Paragraf'
                        || {? __Optmp1.PZAD=''
                           || _pozref:=POW.POZ;
                              __Optmp1.PZAD:=exec('przezad_op','ml_przel',_pozref,OKRO_F.ref(),1)
                           ?};

                           __Optmp1.PPAR:=POW.SLO().KOD;
                           __Optmp1.SAL:=POW.KW;
                           __Optmp1.add()
                        ?};
                        POW.next
                     !}

                  ||
                     {? _broz=1
                     || POW.index(_powind);
                        POW.prefix(_b, _c, _slu_pp,'Ma'); _pozref:=null()
                     || POW.index(_powind2);
                        POW.prefix(_pozref, _b, _c, _slu_pp)
                     ?};
                     {? POW.first
                     || {!
                        |?
                           {? POW.SLU().SLU().WZ='Pozycja'
                           || {? __Optmp1.PZAD=''
                              || _pozref:=POW.POZ;
                                 __Optmp1.PZAD:=exec('przezad_op','ml_przel',_pozref,OKRO_F.ref(),1)
                              ?};
                              'ucina pozycje do dlugosci paragrafu';
                              __Optmp1.PPAR:=_dl_pg+POW.SLO().KOD;
                              __Optmp1.SAL:=POW.KW;
                              __Optmp1.add()
                           ?};
                           POW.next
                        !}
                     || {? ~_dalej || __Optmp1.add() ?}
                     ?}
                  ?};
                  POW.cntx_pop()
               ?};
               POW.next
            !}
         ?};
         _dalej & OKRO_F.prev()
      !}
   ?};
   POW.ndx_drop();
   POW.cntx_pop(); POZ.cntx_pop(); OKRO_F.cntx_pop(); DOK.cntx_pop();
   POW.use(_powname);POZ.use(_pozname); DOK.use(_dokname)
?};
{? __Optmp1.first
|| __Optmpb:=sql('select sum(SAL) SAL, PROZ, PPAR, PZAD from :_a group by PROZ, PPAR, PZAD order by 1 desc',__Optmp1)
?};
VAR_DEL.delete('__Optmp1');
1


\prze1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Sklada przekazane parametry w opis do przelewu
::   WE: _a - rozdzial
::       _b - paragraf
::       _c - zadanie
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? _a<>'' || _wyn+='r.:'+_a+'; ' ?};
{? _b<>'' || _wyn+='p.:'+_b+'; ' ?};
{? _c<>'' || _wyn+='z.:'+_c+'; ' ?};
_wyn


\przezad_op
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1040]
:: OPIS: Odnajduje zadanie do aktywnego OP o najwiekszej kwocie w ML_BZAD
::   WE: _a ref do POZ, _b - OKRO_F.ref, _c - 1 samo zadanie
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<>type_of(0) || _c:=0 ?};
_wyn:='';
_refb:=null;
_kw:=0;
ML_BZAD.index('MLBZOKRP');
ML_BZAD.prefix(_b,_a);
{? ML_BZAD.first
|| {!|?
      {? _kw<ML_BZAD.WN | _kw<ML_BZAD.MA
      || _refb:=ML_BZAD.ref; _kw:=ML_BZAD.MA+ML_BZAD.WN
      ?};
      ML_BZAD.next
   !};
   ML_BZAD.prefix;
   {? ML_BZAD.seek(_refb)
   || _wyn:={? _c=1
            || {? ML_BZAD.ZAD<>null || ML_BZAD.ZAD().KOD || '' ?}
            || ML_BZAD.FUN().KOD
                  +{? ML_BZAD.ZAD<>null || '-'+ML_BZAD.ZAD().KOD || '' ?}
                  +{? ML_BZAD.PODZAD<>null || '-'+ML_BZAD.PODZAD().KOD || '' ?}
                  +{? ML_BZAD.DZIALAN<>null || '-'+ML_BZAD.DZIALAN().KOD || '' ?}
            ?}
   ?}
?};
_wyn


\uzkk1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1020]
:: OPIS: Uzupelnia zmienna KONTO.K1 na podstawie tabeli MLCONNEC
::   WE:
::   WY:
:: HIST:
::----------------------------------------------------------------------------------------------------------------------
_k:='';
_x:=MLCONNEC.ndx_tmp(,,'AT',,0,'NP',,0,'NR',,0,'TP',,0,'ID',,0,'NR',,0);
MLCONNEC.index(_x);
MLCONNEC.clear;
MLCONNEC.prefix('ROK_F', ref_name(ROK_F.ref), #SSTALE.AR, 'BUD_KON','WYD');
{? MLCONNEC.first
|| _k:=exec('twbud','ml_przel',MLCONNEC.WR,1)
?};
MLCONNEC.ndx_drop(_x);
KONTO.K1:=_k;
1


\twbud
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1020]
:: OPIS: Tworzy zapis konta do uzupelnienia
::   WE: _a-konto (przynajmniej syntetyka); _b=0 wstawia '?' _b=1 wstawia pierwsze konto ze słownika
::   WY:
:: HIST:
::----------------------------------------------------------------------------------------------------------------------
{? _<2 || _b:=0 ?};
_vks:=3+_a;
_vsynt:=+_vks;
ROZNE.ROKKON:=SSTALE.AR;
_vsep:=ROZNE.ROKKON().SEP;
_kp:=obj_new(8);
{! _i:=1..8 |!
         _kp[_i]:=''
!};
_kn:=_a;
_i:=1;
{!|?
  _poz:=_kn*_vsep;
  {? _poz>0
  || _kp[_i]:=(_poz-1)+_kn;
     _kn:=_poz-_kn;
     _i+=1;
     1
  || _kp[_i]:=_kn;
     0
  ?}
!};
_v:='';
   {? KS.index('SYM'); KS.prefix(ROZNE.ROKKON,_vks); KS.first() & _vks=KS.SYM
   || {? BUD.index('KS'); BUD.prefix(KS.ref); BUD.first()
      ||
         _v:=_vks;
         _i:=2;
         {! |? _v+=_vsep+{? _kp[_i]<>'' & +_kp[_i]=BUD.SLU().SLU().DL
                         || _kp[_i]
                         |? _b
                         || BUD.SLU().SLU();
                            SLO.clear(); SLO.index('SL'); SLO.prefix(BUD.SLU().SLU);
                            {? SLO.first || SLO.KOD ?}
                         || BUD.SLU().SLU().DL*'?'
                         ?};
               _i+=1;
               BUD.next()
         !};
         _v+=''
      ?}
   ?};
_v


\ml_pwbl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1020]
:: OPIS: Tworzy tabele pomocnicze ML_PW i ML_PWOP
::   WE:
::   WY:
:: HIST:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('ML_PW','ML_PWOP','ML_PWBZ');
ML_PW:=tab_tmp(,'PW','INTEGER','PW ref',
                'ROZDZ','STRING[30]','Rodział',
                'PAR','STRING[30]','Paragraf',
                'PPAR','STRING[30]','Pozycja',
                'ZADC','STRING[35]','Zadanie kod cały',
                'FUN','STRING[2]','Funkcja',
                'ZAD','STRING[4]','Zadanie',
                'PODZ','STRING[4]','Podzadanie',
                'DZLAN','STRING[4]','Działanie',
                'KON','STRING[30]','Konto',
                'IDOP','STRING[50]','Identyfikator rozrachunku',
                'KWOTA','REAL','Kwota');
ML_PWOP:=tab_tmp(,'PW','INTEGER','PW ref','PW_OP','INTEGER','PW_OP ref',
                  'ROZDZ','STRING[30]','Rodział',
                  'PAR','STRING[30]','Paragraf',
                  'PPAR','STRING[30]','Pozycja',
                  'ZADC','STRING[35]','Zadanie kod cały',
                  'FUN','STRING[2]','Funkcja',
                  'ZAD','STRING[4]','Zadanie',
                  'PODZ','STRING[6]','Podzadanie',
                  'DZLAN','STRING[8]','Działanie',
                  'KON','STRING[30]','Konto',
                  'KONWYD','STRING[30]','Konto wyd.',
                  'IDOP','STRING[50]','Identyfikator rozrachunku',
                  'KWOTA','REAL','Kwota',
                  'S1','STRING[20]','S1',
                  'S2','STRING[20]','S2',
                  'S3','STRING[20]','S3',
                  'S4','STRING[20]','S4',
                  'S5','STRING[20]','S5',
                  'S6','STRING[20]','S6',
                  'E1','STRING[8]','E1',
                  'E2','STRING[8]','E2',
                  'E3','STRING[8]','E3',
                  'E4','STRING[8]','E4',
                  'E5','STRING[8]','E5',
                  'E6','STRING[8]','E6',
                  'KW1','INTEGER','KW1',
                  'KW2','INTEGER','KW2',
                  'KW3','INTEGER','KW3',
                  'KW4','INTEGER','KW4',
                  'KW5','INTEGER','KW5',
                  'KW6','INTEGER','KW6',
                  'ROZ','STRING[1]',);
ML_PWBZ:=tab_tmp(5,'PWOP','INTEGER','PWOP ref',
                  'FUN','STRING[2]','Funkcja',
                  'ZAD','STRING[4]','Zadanie',
                  'PODZ','STRING[6]','Podzadanie',
                  'DZLAN','STRING[8]','Działanie',
                  'SUM','REAL','Kwota',
                  'SUMZ','REAL','Kwota',
                  'WAL','STRING[16]','Waluta',
                  'TYP','STRING[3]','Typ'
                  );
1


\ml_pwdan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1020]
:: OPIS: Uzupelnia tabele pomocnicze ML_PW i ML_PWOP
::   WE:
::   WY:
:: HIST:
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
_tytop:=PW.TYTOP;
_dlpg:=exec('dl_sb','ml_fiks',3);
_dlpp:=exec('dl_sb','ml_fiks',4);

::ML_PW.ROZDZ:=exec('wyttyt','ml_przel',_tytop,'r.:',';');
::ML_PW.PAR  :=exec('wyttyt','ml_przel',_tytop,'p.:',';');
PW.cntx_psh();
PW_OP.index('PW');
PW_OP.prefix(PW.ref());
ML_PW.PW   :=PW.ref;
ML_PW.ROZDZ:={? PW.RZ<>'' || PW.RZ || exec('wyttyt','ml_przel',_tytop,'r.:',';') ?};
ML_PW.PAR  :={? PW.PG<>'' || PW.PG || exec('wyttyt','ml_przel',_tytop,'p.:',';') ?};
{? +ML_PW.PAR=_dlpg+_dlpp
|| ML_PW.PPAR:=ML_PW.PAR;
   ML_PW.PAR:=_dlpg+ML_PW.PAR
|| ML_PW.PPAR:=''
?};
ML_PW.IDOP:=PW.SYM;
ML_PW.KON:=PW.AN;
ML_PW.KWOTA:=PW.KW;
ML_PW.ZADC:={? PW.ZAD<>'' || PW.ZAD || exec('wyttyt','ml_przel',_tytop,'z.:',';') ?};
{? ML_PW.ROZDZ='' | ML_PW.PAR='' | ML_PW.ZADC=''
||
:: jesli nie w przelewie nie przekazano rozdzialu, paragrafu, zadania to sprawdza czy nie ma ich w koncie
   _vks:=3+ML_PW.KON;
   _vsynt:=+_vks;
   ROZNE.ROKKON:=SSTALE.AR;
   _vsep:=ROZNE.ROKKON().SEP;
   {? var_pres('_kp')<0 || _kp:=obj_new(8) ?};
   {! _i:=1..8
   |!
      _kp[_i]:=''
   !};
   _kn:=ML_PW.KON;
   _i:=1;
   {!
   |?
      _poz:=_kn*_vsep;
      {? _poz>0
      || _kp[_i]:=(_poz-1)+_kn;
         _kn:=_poz-_kn;
         _i+=1;
         1
      || _kp[_i]:=_kn;
         0
      ?}
   !};
::   msg(PW.AN);
   KS.index('SYM'); KS.prefix(ROZNE.ROKKON,_vks);
   {? KS.first() & _vks=KS.SYM
   || {? BUD.index('KS'); BUD.prefix(KS.ref); BUD.first()
      ||
         _i:=2;
         {!
         |?
            {? -BUD.SLU().SLU().WZ='pozycja' & ML_PW.PPAR=''
            || ML_PW.PPAR:=_kp[_i]
            |? -BUD.SLU().SLU().WZ='paragraf' & ML_PW.PAR=''
            || ML_PW.PAR:=_kp[_i]
            |? -BUD.SLU().SLU().WZ='rozdział' & ML_PW.ROZDZ=''
            || ML_PW.ROZDZ:=_kp[_i]
            |? -BUD.SLU().SLU().WZ='zadanie' & ML_PW.ZADC=''
            || ML_PW.ZADC:=_kp[_i]
            ?};
            _i+=1;
            BUD.next()
         !}
    ?}
   ?}
?};
ML_PW.FUN:=ML_PW.ZAD:=ML_PW.PODZ:=ML_PW.DZLAN:='';
{? ML_PW.ZADC<>''
|| _fun:=_zad:=_podz:=_dlan:='';
   _fun:=2+ML_PW.ZADC;
   {? 1+(2-ML_PW.ZADC)='-'
   || _zad:=4+(3-ML_PW.ZAD)
   ?};
   {? 1+(7-ML_PW.ZADC)='-'
   || _podz:=6+(8-ML_PW.ZAD)
   ?};
   {? 1+(14-ML_PW.ZADC)='-'
   || _dlan:=8+(15-ML_PW.ZAD)
   ?};
   {? exec('budcorct','ml_zad',_fun,_zad,_podz,_dlan,,ROK_F.ref)
   || ML_PW.FUN:=_fun;
      ML_PW.ZAD:=_zad;
      ML_PW.PODZ:=_podz;
      ML_PW.DZLAN:=_dlan
   ?}
?};

_wyn:=ML_PW.add(1);

{? _wyn
||
   _fnadd:="
      _ret:=_a;
      _tryb:=_b;
      {? _ret
      || {? ~_tryb
         ||
            {! _i:=1..6 |!
               {? ($('KA.KW'+$_i+'<>null'))() | ($('KA.S'+$_i+'<>'''''))()
               || ($('ML_PWOP.S'+$_i+':=KA.S'+$_i))();
                  ($('ML_PWOP.KW'+$_i+':=KA.KW'+$_i))();
                  ($('ML_PWOP.E'+$_i+':=KA.E'+$_i))()
               || ($('ML_PWOP.S'+$_i+':='''''))();
                  ($('ML_PWOP.KW'+$_i+':=null'))();
                  ($('ML_PWOP.E'+$_i+':='''''))()
               ?}
            !}
         || __ttmp:=_c;
            {! _i:=1..6 |!
               {? ($('__ttmp.KW'+$_i+'<>'''''))() | ($('__ttmp.S'+$_i+'<>'''''))()
               || SLO.clear();
                  ($('ML_PWOP.S'+$_i+':=__ttmp.S'+$_i))();
                  {? ($('SLO.seek(BB.sqlint(__ttmp.KW'+$_i+'),)'))()
                  || ($('ML_PWOP.KW'+$_i+':=SLO.ref()'))()
                  || ($('ML_PWOP.KW'+$_i+':=null'))()
                  ?};
                  ML_PWOP.KWOTA:=__ttmp.KW;
                  ($('ML_PWOP.E'+$_i+':=__ttmp.E'+$_i))()
               || ($('ML_PWOP.S'+$_i+':='''''))();
                  ($('ML_PWOP.KW'+$_i+':=null'))();
                  ($('ML_PWOP.E'+$_i+':='''''))()
               ?}
            !};
            VAR_DEL.delete('__ttmp')
         ?}
      || {! _i:=1..6 |!
            ($('ML_PWOP.S'+$_i+':='''''))();
            ($('ML_PWOP.KW'+$_i+':=null'))();
            ($('ML_PWOP.E'+$_i+':='''''))()
        !}
      ?};
      _wyn:=ML_PWOP.add(1);
      {? _ret & POZ.KON<>'' & _wyn & POZ.KOM().KS().BZ<>'N'
      ||
         ROK_F.cntx_psh(); OKRO_F.cntx_psh();
         OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
         _okro:=SSTALE.AO;
         {? OKRO_F.find_key(date(POZ.DOK().DTW~1,POZ.DOK().DTW~2,0), date(POZ.DOK().DTW~1,POZ.DOK().DTW~2,1))
         ||
            ML_BZAD.index('MLBZOKRP');
            ML_BZAD.prefix(OKRO_F.ref,POZ.ref());
            {? ML_BZAD.first
            || {!|?
                  ML_PWBZ.WAL:=$ML_BZAD.WAL;
                  ML_PWBZ.TYP:=ML_BZAD.TYP;
                  ML_PWBZ.FUN:=ML_BZAD.FUN().KOD;
                  ML_PWBZ.ZAD:=ML_BZAD.ZAD().KOD;
                  ML_PWBZ.PODZ:=ML_BZAD.PODZAD().KOD;
                  ML_PWBZ.DZLAN:=ML_BZAD.DZIALAN().KOD;
                  ML_PWBZ.SUMZ:=ML_BZAD.WN+ML_BZAD.MA;
                  ML_PWBZ.PWOP:=ML_PWOP.ref();
                  ML_PWBZ.add();
                  ML_BZAD.next
               !}
            ?};
            ML_PWBZ.cntx_psh();
            ML_PWBZ.prefix(ML_PWOP.ref());
            {? ML_PWBZ.first()
            ||
               _sum:=_sum2:=0;
               {!
               |?
                  _sum+=ML_PWBZ.SUMZ;
                  ML_PWBZ.next()
               !};
               ML_PWBZ.first();
               {!
               |?
                  ML_PWBZ.SUM:=(ML_PWOP.KWOTA*ML_PWBZ.SUMZ/_sum)$2;
                  _sum2+=ML_PWBZ.SUM;
                  ML_PWBZ.put;
                  ML_PWBZ.next()
               !};
               ML_PWBZ.first();
               {? _sum2<>ML_PWOP.KWOTA
               || ML_PWBZ.SUM-=(_sum2-ML_PWOP.KWOTA);
                  ML_PWBZ.put()
               ?}
            ?};
            ML_PWBZ.cntx_pop()
         ?};
         ROK_F.cntx_pop(); OKRO_F.cntx_pop();
         1
      ?};
      _wyn
   ";
   _pw_op:=PW_OP.first;

   {!
   |?
      ML_PWOP.PW:=PW.ref;
      ML_PWOP.PW_OP:={? _pw_op || PW_OP.ref || null ?};
::      ML_PWOP.ROZDZ:=exec('wyttyt','ml_przel',_tytop,'r.:',';');
::      ML_PWOP.PAR:=exec('wyttyt','ml_przel',_tytop,'p.:',';');
::      ML_PWOP.ROZDZ:=PW_OP.RZ;
::      ML_PWOP.PAR:=PW_OP.PG;
:: jesli w PW nie ma rozdzialu, paragrafu, zadania to pobiera z wczesniejszego szukania w koncie
      ML_PWOP.ROZDZ:={? _pw_op & PW_OP.RZ<>'' || PW_OP.RZ |? PW.RZ<>'' || PW.RZ || ML_PW.ROZDZ ?};
      ML_PWOP.PAR:={? _pw_op & PW_OP.PG<>'' || PW_OP.PG |? PW.PG<>'' || PW.PG || ML_PW.PAR ?};
      {? +ML_PWOP.PAR=_dlpg+_dlpp
      || ML_PWOP.PPAR:=ML_PWOP.PAR;
         ML_PWOP.PAR:=_dlpg+ML_PWOP.PAR
      |? ML_PW.PPAR<>''
      || ML_PWOP.PPAR:=ML_PW.PPAR
      || ML_PWOP.PPAR:=''
      ?};
      ML_PWOP.ZADC:={? _pw_op & PW_OP.ZAD<>'' || PW_OP.ZAD |? PW.ZAD<>'' || PW.ZAD || ML_PW.ZADC ?};
      ML_PWOP.IDOP:={? _pw_op || PW_OP.SYM || PW.SYM ?};
      ML_PWOP.KON:={? _pw_op || PW_OP.AN || PW.AN ?};
      ML_PWOP.KONWYD:=KONTO.K1;
      ML_PWOP.KWOTA:={? _pw_op || PW_OP.K || PW.KW ?};

      ML_PWOP.ROZ:=PW.WSK_R;
      ML_PWOP.FUN:=ML_PWOP.ZAD:=ML_PWOP.PODZ:=ML_PWOP.DZLAN:='';
      {? ML_PWOP.ZADC<>''
      || _fun:=_zad:=_podz:=_dlan:='';
         _fun:=2+ML_PWOP.ZADC;
         {? 1+(2-ML_PWOP.ZADC)='-'
         || _zad:=4+(3-ML_PWOP.ZAD)
         ?};
         {? 1+(7-ML_PWOP.ZADC)='-'
         || _podz:=4+(8-ML_PWOP.ZAD)
         ?};
         {? 1+(12-ML_PWOP.ZADC)='-'
         || _dlan:=4+(13-ML_PWOP.ZAD)
         ?};
         {? exec('budcorct','ml_zad',_fun,_zad,_podz,_dlan,,ROK_F.ref)
         || ML_PWOP.FUN:=_fun;
            ML_PWOP.ZAD:=_zad;
            ML_PWOP.PODZ:=_podz;
            ML_PWOP.DZLAN:=_dlan
         ?}
      ?};

      OP.index('STAN_O'); OP.prefix(FINFO.NAROD,{? _pw_op || PW_OP.ODD || PW.ODD ?},'N',{? _pw_op || PW_OP.AN || PW.AN ?});
      _ret:=0;
      _sym:={? _pw_op || PW_OP.SYM || PW.SYM ?};
      {? OP.first() || {! |? {? OP.SYM=_sym || _ret:=1 ?}; ~_ret & OP.next() !} ?};
      {? _ret
      || _ret:=0;
         ZAP_OP.cntx_psh;
         ZAP_OP.index('OP');
         ZAP_OP.prefix(OP.ref);
         {? ZAP_OP.first
         ||
            _kwota:=OP.MA-OP.WN;
            {? _kwota<0 || _kwota:=-_kwota ?};
            _podziel:=_kwota=ML_PWOP.KWOTA;
            _max:=_rozl:=_kwota:=0;
            _kwota_s:=ML_PWOP.KWOTA;
            _ref:=null;
:: petla szukajaca pozycji zgodnych co do kwoty
            {!|?
               DOK.cntx_psh(); POZ.cntx_psh(); POW.cntx_psh(); SKIDXD.cntx_psh(); VPOZ.cntx_psh();
               {? ZAP_OP.OKRO<>SSTALE.AO
               || _okres:=SSTALE.AO;
                  SSTALE.AO:=ZAP_OP.OKRO;
                  SSTALE.AO();
                  exec('open','rozrach',OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
                  OBR.use('obroty'+OKRO_F.ROK().KOD)
               || _okres:=ZAP_OP.OKRO
               ?};
               _kwota:=ZAP_OP.MA-ZAP_OP.WN;
               {? _kwota<0 || _kwota:=-_kwota ?};
               {? ZAP_OP.POZDOK().ID=_sym & _kwota<>0
               || {? ~_podziel
                  || {? _kwota>_max
                     || _max:=_kwota;
                        _ref:=ZAP_OP.ref
                     ?};
                     {? _kwota=ML_PWOP.KWOTA
                     ||
                        _rozl+=_kwota;
                        ZAP_OP.POZDOK();
                        __Tpodz:=exec('ustal_ka','ml_przel',_kwota);
                        _ret:=1;
                        {? __Tpodz.first()
                        || {!
                           |?
                              _wyn:=_fnadd(_ret,1,__Tpodz);
                              __Tpodz.next()
                           !}
                        ?};
                        VAR_DEL.delete('__Tpodz');
                        _ret:=_rozl>=_kwota_s
                     ?}
                  || _rozl+=_kwota;
                     ML_PWOP.KWOTA:=_kwota;
                     ZAP_OP.POZDOK();
                     __Tpodz:=exec('ustal_ka','ml_przel',_kwota);
                     _ret:=1;
                     {? __Tpodz.first()
                     || {!
                        |?
                           _wyn:=_fnadd(_ret,1,__Tpodz);
                           __Tpodz.next()
                        !}
                     ?};
                     VAR_DEL.delete('__Tpodz');
                     _ret:=_rozl>=_kwota_s
                  ?}
               ?};
               {? ZAP_OP.OKRO<>_okres
               || SSTALE.AO:=_okres;
                  SSTALE.AO();
                  exec('open','rozrach',OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
                  OBR.use('obroty'+OKRO_F.ROK().KOD)
               ?};
               DOK.cntx_pop(); POZ.cntx_pop(); POW.cntx_pop(); SKIDXD.cntx_pop(); VPOZ.cntx_pop();

               ~_ret & ZAP_OP.next()
            !}
         ?};
:: dla sytuacji co pozycje nie sa zgodne w kwocie - wtedy przechodzi to tej z najwieksza i z niego pobiera wartosci
         {? ~_podziel & ~_ret & _ref<>null
         ||
            DOK.cntx_psh(); POZ.cntx_psh(); POW.cntx_psh(); SKIDXD.cntx_psh(); VPOZ.cntx_psh();
            {? ZAP_OP.seek(_ref)
            ||
               {? ZAP_OP.OKRO<>SSTALE.AO
               || _okres:=SSTALE.AO;
                  SSTALE.AO:=ZAP_OP.OKRO;
                  SSTALE.AO();
                  exec('open','rozrach',OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
                  OBR.use('obroty'+OKRO_F.ROK().KOD)
               || _okres:=ZAP_OP.OKRO
               ?};

               ZAP_OP.POZDOK();

               _ret:=1;
               __Tpodz:=exec('ustal_ka','ml_przel',{? _pw_op || PW_OP.K || PW.KW ?});
               _ret:=1;
               {? __Tpodz.first()
               || {!
                  |?
                     _wyn:=_fnadd(_ret,1,__Tpodz);
                     __Tpodz.next()
                  !}
               ?};
               VAR_DEL.delete('__Tpodz');
               {? ZAP_OP.OKRO<>_okres
               || SSTALE.AO:=_okres;
                  SSTALE.AO();
                  exec('open','rozrach',OKRO_F.ROK().KOD+form(OKRO_F.NR,-2));
                  OBR.use('obroty'+OKRO_F.ROK().KOD)
               ?}
            ?};
            DOK.cntx_pop(); POZ.cntx_pop(); POW.cntx_pop(); SKIDXD.cntx_pop(); VPOZ.cntx_pop()
         |? ~_ret
         || _wyn:=_fnadd(_ret,0)
         ?};
         ZAP_OP.cntx_pop
      || _wyn:=_fnadd(_ret,0)
      ?};
      _wyn & {? _pw_op || PW_OP.next() || 0 ?}
   !}
?};
PW.cntx_pop();
_wyn


\wyttyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1020]
:: OPIS: Wycina fragment tekstu pomiedzy podanymi ogranicznikami
::   WE: _a tekst, _b od, _c do
::   WY:
:: HIST:
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? +_a>0 & +_b>0 & +_c>0
|| _poz:=_a*_b;
   {? _poz>0
   || _db:=+_b;
      _wyn:=(_poz+_db-1)-_a;
      _poz:=_wyn*_c;
      {? _poz>0
      || _wyn:=(_poz-1)+_wyn
      ?}
   ?}
?};
_wyn


\ustal_ka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [12.30]
:: OPIS: Formula tworzy tabele z rozbiciem jak w KA ale osobno kazdy wyronik wg rozdzielnika (KA.S1..KA.S6 KA.KW1..KA.KW6) ";
::       na podstwie istniejacych wyroznikow dla pozycji POZ lub definicji";
::       (z KS_W), o ile parametr nr 35 ='T'.                             ";
::    WE: _a - kwota do rozliczenia
::----------------------------------------------------------------------------------------------------------------------
__ttmp:=tab_tmp(1,
      'KWZ','REAL','Kwota',
      'KW','REAL','Kwota',
      'S1','STRING[20]','1.',
      'S2','STRING[20]','2.',
      'S3','STRING[20]','3.',
      'S4','STRING[20]','4.',
      'S5','STRING[20]','5.',
      'S6','STRING[20]','6.',
      'E1','STRING[20]','1.',
      'E2','STRING[20]','2.',
      'E3','STRING[20]','3.',
      'E4','STRING[20]','4.',
      'E5','STRING[20]','5.',
      'E6','STRING[20]','6.',
      'KW1','STRING[20]','Kod wyr. 1.',
      'KW2','STRING[20]','Kod wyr. 2.',
      'KW3','STRING[20]','Kod wyr. 3.',
      'KW4','STRING[20]','Kod wyr. 4.',
      'KW5','STRING[20]','Kod wyr. 5.',
      'KW6','STRING[20]','Kod wyr. 6.'
   );
   ROK_F.cntx_psh();
   ROK_F.index('KOD'); ROK_F.prefix(REF.FIRMA); ROK_F.find_key(4-POZ.name()-2);
_put:={? var_pres('PowPut')>0 || PowPut || 2 ?};
{? PAR_SKID.get(35)='T' & (_k:=exec('jak_kon','konto',ROK_F.SYNT+POZ.KON))<>null
|| KS_W.cntx_psh();
   SLU.cntx_psh();
   SLUAPPL.cntx_psh();
   POW.cntx_psh();
   POW.index('POZ');
   POW.prefix(POZ.ref());
   KS_W.index('LP');
   KS_W.prefix(_k);
   _bo:=exec('roz_bo','dok_fks');
   _add:=0;

   {! _i:=1..6 |!
      {? KS_W.find_key(_i)
      || ($('__ttmp.S'+$_i+':=KS_W.SLU().SLU().NAZ'))();
         {? POW.find_key(_i) & POW.SLU=KS_W.SLU
         || {? _add
            || __ttmp.cntx_psh();
               {? __ttmp.first()
               || {!
                  |?
                     ($('__ttmp.KW'+$_i+':=$POW.SLO'))();
                     ($('__ttmp.E'+$_i+':=POW.SLO().KOD'))();
                     ($('__ttmp.S'+$_i+':=KS_W.SLU().SLU().NAZ'))();
                     __ttmp.put();
                     __ttmp.next()
                  !}
               ?};
               __ttmp.cntx_pop()
            ||
               ($('__ttmp.KW'+$_i+':=$POW.SLO'))();
               ($('__ttmp.E'+$_i+':=POW.SLO().KOD'))();
               __ttmp.KWZ:=POW.KW
            ?};
            {? ~_add & KS_W.ROZDZ='T'
            || _pow:=POW.ref();
               POW.cntx_psh();
               POW.prefix(POZ.ref(),_i);
               {? POW.first() & POW.size()>1
               ||
                  {!
                  |?
                     __ttmp.KWZ:=POW.KW;
                     ($('__ttmp.KW'+$_i+':=$POW.SLO'))();
                     ($('__ttmp.E'+$_i+':=POW.SLO().KOD'))();
                     __ttmp.add();
                     _add:=1;
                     POW.next()
                  !}
               ?};
               POW.cntx_pop()
            ?}
         || ($('__ttmp.S'+$_i+':='''''))();
            ($('__ttmp.KW'+$_i+':='''''))();
            ($('__ttmp.E'+$_i+':='''''))()
         ?}
      || ($('__ttmp.S'+$_i+':='''''))();
         ($('__ttmp.KW'+$_i+':='''''))();
         ($('__ttmp.E'+$_i+':='''''))()
      ?}
  !};
  {? ~_add & __ttmp.KW<>0
  || __ttmp.add()
  ?};
  KS_W.cntx_pop();
  SLU.cntx_pop();
  SLUAPPL.cntx_pop();
  POW.cntx_pop()
|| {! _i:=1..6 |!
      ($('__ttmp.S'+$_i+':='''''))();
      ($('__ttmp.KW'+$_i+':='''''))();
      ($('__ttmp.E'+$_i+':='''''))()
   !}
?};
{? ~_add
|| __ttmp.add()
?};

ROK_F.cntx_pop();
{? __ttmp.first()
|| _sum:=_sum2:=0;
   {!
   |?
      _sum+=__ttmp.KWZ;
      __ttmp.next()
   !};
   __ttmp.first();
   {? _sum<>0
   ||
      {!
      |?
         __ttmp.KW:=(_a*__ttmp.KWZ/_sum)$2;
         _sum2+=__ttmp.KW;
         __ttmp.put;
         __ttmp.next()
      !};
      __ttmp.first();
      {? _sum2<>_a
      || __ttmp.KW-=(_sum2-_a);
         __ttmp.put()
      ?}
   ?}
?};
_wyn:=__ttmp;
VAR_DEL.delete('__ttmp');
_wyn


\tt2poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1020]
:: OPIS: Tworzenie zapisow na podstawie tabeli TT_WBPOZ   wersja z ML_PWOP
::   WE:
::   WY:
:: HIST:
::----------------------------------------------------------------------------------------------------------------------
::msg('tt2poz');
{? _<2 || _b:=0 ?};
_ml_inkon:=ML_PWOP.ndx_tmp(,1,'PW',,0,'KON',,0,'ROZ',,0,'IDOP',,0);
_ml_inid:=ML_PWOP.ndx_tmp(,1,'PW',,0,'KONWYD',,0,'ROZ',,0,'IDOP',,0);
{? TT_WBPOZ.first()
||
   __fn:="
      _wyn:='';
      SLO.cntx_psh();
      SLO.clear();
      {! _i:=1..6
      |!
         {? SLO.seek(($('ML_PWOP.KW'+$_i))(),SLO.name) &  {? _a<>'fiks' || SLO.SLU().WZ=_a || SLO.SLU().NAZ=_b ?}
         || _wyn:=($('ML_PWOP.E'+$_i))()
         ?}
      !};
      SLO.cntx_pop();
      _wyn
   ";
   _fnpoz:="
      _vks:=3+POZ.KON;
      _vsynt:=+_vks;
      ROZNE.ROKKON:=SSTALE.AR;
      _vsep:=ROZNE.ROKKON().SEP;
      {? var_pres('_kp')<0 || _kp:=obj_new(8) ?};
      {! _i:=1..8 |!
               _kp[_i]:=''
      !};
      _kn:=POZ.KON;
      _i:=1;
      {!|?
        _poz:=_kn*_vsep;
        {? _poz>0
        || _kp[_i]:=(_poz-1)+_kn;
           _kn:=_poz-_kn;
           _i+=1;
           1
        || _kp[_i]:=_kn;
           0
        ?}
      !};

      _v:='';
      KS.index('SYM'); KS.prefix(ROZNE.ROKKON,_vks);
      {? KS.first() & _vks=KS.SYM
      || {? BUD.index('KS'); BUD.prefix(KS.ref); BUD.first()
         ||
            _v:=_vks;
            _i:=2;
            {! |? _v+=_vsep+{? -BUD.SLU().SLU().WZ='paragraf'
                            || {? ML_PWOP.PAR<>''
                               || ML_PWOP.PAR
                               || _pz:=__fn(BUD.SLU().SLU().WZ,BUD.SLU().SLU().NAZ);
                                  {? _pz<>'' || _pz || _kp[_i] ?}
                               ?}
                            |? -BUD.SLU().SLU().WZ='pozycja'
                            || {? ML_PWOP.PPAR<>''
                               || ML_PWOP.PPAR
                               || _pz:=__fn(BUD.SLU().SLU().WZ,BUD.SLU().SLU().NAZ);
                                  {? _pz<>'' || _pz || _kp[_i] ?}
                               ?}
                            |? -BUD.SLU().SLU().WZ='rozdział'
                            || {? ML_PWOP.ROZDZ<>''
                               || ML_PWOP.ROZDZ
                               || _pz:=__fn(BUD.SLU().SLU().WZ,BUD.SLU().SLU().NAZ);
                                  {? _pz<>'' || _pz || _kp[_i] ?}
                               ?}
                            |? -BUD.SLU().SLU().WZ='funkcja'
                            || {? ML_PWOP.FUN<>''
                               || ML_PWOP.FUN
                               || _pz:=__fn(BUD.SLU().SLU().WZ,BUD.SLU().SLU().NAZ);
                                  {? _pz<>'' || _pz || _kp[_i] ?}
                               ?}
                            |? -BUD.SLU().SLU().WZ='zadanie'
                            || {? ML_PWOP.ZAD<>''
                               || ML_PWOP.ZAD
                               || _pz:=__fn(BUD.SLU().SLU().WZ,BUD.SLU().SLU().NAZ);
                                  {? _pz<>'' || _pz || _kp[_i] ?}
                               ?}
                            |? -BUD.SLU().SLU().WZ='podzadanie'
                            || {? ML_PWOP.PODZ<>''
                               || ML_PWOP.PODZ
                               || _pz:=__fn(BUD.SLU().SLU().WZ,BUD.SLU().SLU().NAZ);
                                  {? _pz<>'' || _pz || _kp[_i] ?}
                               ?}
                            |? -BUD.SLU().SLU().WZ='działanie'
                            || {? ML_PWOP.DZLAN<>''
                               || ML_PWOP.DZLAN
                               || _pz:=__fn(BUD.SLU().SLU().WZ,BUD.SLU().SLU().NAZ);
                                  {? _pz<>'' || _pz || _kp[_i] ?}
                               ?}
                            |? form(_kp[_i])<>'' & +_kp[_i]=BUD.SLU().SLU().DL
                            || _kp[_i]
                            |? _pz:=__fn(BUD.SLU().SLU().WZ,BUD.SLU().SLU().NAZ); _pz<>''
                            || _pz
                            |? _b
                            || BUD.SLU().SLU();
                               SLO.clear(); SLO.index('SL'); SLO.prefix(BUD.SLU().SLU);
                               {? SLO.first || SLO.KOD ?}
                            || BUD.SLU().SLU().DL*'?'
                            ?};
                  _i+=1;
                  BUD.next()
            !};
            _v+='';
            POZ.KON:=_v;
            {? _a=1
            || POZ.put
            || POZ.add
            ?};
            {? POZ.KOM().KS().BZ<>'N' & (ML_PWOP.FUN<>'' | ML_PWOP.ZAD<>'' | ML_PWOP.PODZ<>'' | ML_PWOP.DZLAN<>'')
               & exec('budcorct','ml_zad',ML_PWOP.FUN,ML_PWOP.ZAD,ML_PWOP.PODZ,ML_PWOP.DZLAN,,ROK_F.ref())
            || ML_BZAD.blank();
               ML_BZAD.OKR:=OKRO_F.ref();
               ML_BZAD.ODD:=POZ.DOK().ODD;
               ML_BZAD.WAL:=POZ.WAL;
               _typ:=exec('typ_kon','ml_conn',POZ.KON);
               ML_BZAD.TYP:=_typ;
               ML_BZAD.FUN:=ML_FUN.ref();
               {? ML_PWOP.ZAD <>'' || ML_BZAD.ZAD:=ML_ZAD.ref() ?};
               {? ML_PWOP.PODZ<>'' || ML_BZAD.ZAD:=ML_ZAD.ref(); ML_BZAD.PODZAD:=ML_PODZ.ref() ?};
               {? ML_PWOP.DZLAN<>'' || ML_BZAD.ZAD:=ML_ZAD.ref(); ML_BZAD.PODZAD:=ML_PODZ.ref(); ML_BZAD.DZIALAN:=ML_DZLAN.ref() ?};
               {? POZ.STR='Wn'
               || ML_BZAD.WN:=POZ.SUM
               || ML_BZAD.MA:=POZ.SUM
               ?};
               ML_BZAD.clear();
               ML_BZAD.add()
            |? POZ.KOM().KS().BZ<>'N'
            || ML_PWBZ.prefix(ML_PWOP.ref());
               {? ML_PWBZ.first()
               || {!
                  |?
                     {? exec('budcorct','ml_zad',ML_PWBZ.FUN,ML_PWBZ.ZAD,ML_PWBZ.PODZ,ML_PWBZ.DZLAN,,ROK_F.ref())
                     ||
                        ML_BZAD.blank();
                        ML_BZAD.OKR:=OKRO_F.ref();
                        ML_BZAD.ODD:=POZ.DOK().ODD;
                        ML_BZAD.WAL:=POZ.WAL;
                        _typ:=exec('typ_kon','ml_conn',POZ.KON);
                        ML_BZAD.TYP:=_typ;
                        ML_BZAD.FUN:=ML_FUN.ref();
                        {? ML_PWBZ.ZAD <>'' || ML_BZAD.ZAD:=ML_ZAD.ref() ?};
                        {? ML_PWBZ.PODZ<>'' || ML_BZAD.ZAD:=ML_ZAD.ref(); ML_BZAD.PODZAD:=ML_PODZ.ref() ?};
                        {? ML_PWBZ.DZLAN<>'' || ML_BZAD.ZAD:=ML_ZAD.ref(); ML_BZAD.PODZAD:=ML_PODZ.ref(); ML_BZAD.DZIALAN:=ML_DZLAN.ref() ?};
                        {? POZ.STR='Wn'
                        || ML_BZAD.WN:=ML_PWBZ.SUM
                        || ML_BZAD.MA:=ML_PWBZ.SUM
                        ?};
                        ML_BZAD.clear();
                        ML_BZAD.add()
                     ?};
                     ML_PWBZ.next()
                  !}
               ?}
            ?}
         ?}
      ?}
   ";
   {!
   |? {? var_pres('dodano')>0  || {? type_of(dodano)>100  || obj_del(dodano)  || &dodano  ?} ?};
      dodano:=null();
      ROZNE.ROKKON:=SSTALE.AR;
      _vks:=3+TT_WBPOZ.KONTO;
      KS.index('SYM'); KS.prefix(ROZNE.ROKKON,_vks);
      {? KS.first & KS.ROZR='Z'
      || TT_WBPOZ.R_SYM:=TT_WBPOZ.R_TYP:='';
         TT_WBPOZ.DO:=TT_WBPOZ.TP:=TT_WBPOZ.DR:=date(0,0,0)
      ?};
      dodano:=exec('poz_add','ml_auto',
         TT_WBPOZ.KW,
         TT_WBPOZ.STR,
         TT_WBPOZ.KONTO,
         TT_WBPOZ.R_SYM,
         TT_WBPOZ.R_TYP,
         TT_WBPOZ.DO,
         TT_WBPOZ.TP,
         TT_WBPOZ.OP,
         TT_WBPOZ.WAL,
         TT_WBPOZ.KW_WAL,
         TT_WBPOZ.KURS,'',
         TT_WBPOZ.DR,
         TT_WBPOZ.ODD
      );
      POZ.clear();
      {? dodano[1] & dodano[2]<>null & POZ.seek(dodano[2])
      ||
         ML_PWOP.index(_ml_inkon);
         ML_PWOP.prefix(TT_WBPOZ.PW,POZ.KON,TT_WBPOZ.ROZ,POZ.ID,);
         {? ML_PWOP.first & ML_PWOP.KON=POZ.KON
         ||
            _licz:=1;
            _kon:=POZ.KON;
            {!
            |?
               {? _licz>1
               ||
                  POZ.cntx_psh();
                  POZ.index('DOK');
                  POZ.prefix(POZ.DOK);
                  _poz:=POZ.size()+1;
                  POZ.cntx_pop();
                  POZ.POZ:=_poz
               ?};
               POZ.KON:=_kon;
               POZ.SUM:=ML_PWOP.KWOTA;
               _fnpoz(_licz=1,_b);
               exec('ustal_ka','dok_fks',,1);
               SLO.prefix;
               {! _i:=1..6 |!
                  {! _j:=1..6 |!
:                     {? SLO.seek(($('ML_PWOP.KW'+$_j))(),SLO.name) & ($('SLO.SLU().NAZ=KA.KW'+$_i+'().SLU().NAZ'))()
                     {? ($('ML_PWOP.S'+$_j+'=KA.S'+$_i))()
                     ||
                        {? SLO.seek(($('ML_PWOP.KW'+$_j))(),SLO.name)
                        || ($('KA.KW'+$_i+':=SLO.ref'))()
                        || ($('KA.KW'+$_i+':=null'))()
                        ?};
                        ($('KA.S'+$_i+':=ML_PWOP.S'+$_j))()
                     ?}
                  !}
               !};
               exec('dod_wyr','wyrozniki',1);
               _licz+=1;
               ML_PWOP.next()
            !}
         ||
            ML_PWOP.index(_ml_inid);
            ML_PWOP.prefix(TT_WBPOZ.PW,POZ.KON,TT_WBPOZ.ROZ,POZ.ID);
            {? ML_PWOP.first
            || _licz:=1;
               _kon:=POZ.KON;
               {!
               |?
                  {? _licz>1
                  ||
                     POZ.cntx_psh();
                     POZ.index('DOK');
                     POZ.prefix(POZ.DOK);
                     _poz:=POZ.size()+1;
                     POZ.cntx_pop();
                     POZ.POZ:=_poz
                  ?};
                  POZ.KON:=_kon;
                  POZ.SUM:=ML_PWOP.KWOTA;
                  _fnpoz(_licz=1,_b);
                  exec('ustal_ka','dok_fks',,1);
                  SLO.prefix;
                  {! _i:=1..6 |!
                     {! _j:=1..6 |!
                        {? ($('ML_PWOP.S'+$_j+'=KA.S'+$_i))()
                        ||
                           {? SLO.seek(($('ML_PWOP.KW'+$_j))(),SLO.name)
                           || ($('KA.KW'+$_i+':=SLO.ref'))()
                           || ($('KA.KW'+$_i+':=null'))()
                           ?};
                           ($('KA.S'+$_i+':=ML_PWOP.S'+$_j))()
                        ?}
                     !}
                  !};

                  exec('dod_wyr','wyrozniki',1);
                  _licz+=1;
                  ML_PWOP.next()
               !}
            ?}

         ?}
      ?};
      TT_WBPOZ.next()
   !}
?};
{? var_pres('dodano')>0  || {? type_of(dodano)>100  || obj_del(dodano)  || &dodano  ?} ?};
ML_PWOP.ndx_drop;
exec('ml_pwdel','ml_przel');
VAR_DEL.delete('TT_WBPOZ','__fn')


\ml_pwdel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [1020]
:: OPIS: Kasuje tabele pomocnicze ML_PW i ML_PWOP
::   WE:
::   WY:
:: HIST:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('ML_PW','ML_PWOP','ML_PWBZ');1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 2ca248b1d37bd7c94c9c2c82b73b0345065cb3b2ab66c2b4d4135189ffaa717cce84d2d6b12d336ba36777daa24ad4ed8b9ec9d21241cd5e69c57895c09fd55ec1e67113f209cbb2d198a875208ca144ae6c304dc3ba163450c40c26ea169644bc2821a4232516aedc4d60013122764c98d1099d443bcff7744e2e1ec2bfcfa4

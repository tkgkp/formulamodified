:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zl_uslugi.fml
:: Utworzony: 10.02.2021
:: Autor: WH
::======================================================================================================================
:: Zawartość: Obsługa usług w zleceniach produkcyjnych
::======================================================================================================================


\enabled
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Czy usługi w produkcji włączone?
::   WY: 0/1
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('get','#params',500742,type_of(''))='T';
_result


\mat_grp_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Tworzy okienko grupowe kartoteki materiałowej z zakładkami: materiały, usługi
::   WE: [_a] - czy ze stanami 0-tak 1-nie (tylko gdy _a=T)
::       [_b] - wyróżniki akcji do usunięcia (jak w metodzie actions)
::       [_c] - domyślne wyróżniki akcji (jak w metodzie actions)
::  OLD: \slo_m_ok/defin.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(0)  || _stany:=_a || _stany:=1 ?};
{? var_pres('_b')=type_of('') || _hidden:=_b || _hidden:='' ?};
{? var_pres('_c')=type_of('') || _default:=_c || _default:='' ?};

exec('filter','material','WP');

_grpberofe:="

";
_grp:=M.grp_make('Materiały'@,_grpberofe,'#zlmatusl',,,,,'normal');
_wer:='NL_WER'+{? _stany || '' || 'ST' ?};

_after_rfr:="";
_before:="
::   M.cntx_psh();
   exec('filter','material','WP');
   M.index('R_PROD');

   exec('zakres_set_towar','material');

::   _akt:={? ZAKR.MATU='N' || 'N' || 'T' ?};
::   M.prefix(_akt,POMOC.RODZ,ZAKR.R_PROD,);

   exec('m_win_edit','material');
   exec('m_win_patt','material');
   exec('wpm_opis','material',FILTER.T_WPM,' %1 — '['i usługi'@]+exec('zakr_naz','material',ZAKR.MATU));
   ~~
";
_after:="

::   M.cntx_pop();
   ~~
";
M.actions(_wer,_hidden,_default);
M.btn_sopt(_wer,'INDEKS_ADD','default=0');
M.grp_sel(_grp,,_wer,'Materiały'@,_after_rfr,,,,_before,_after,,,'maximized');

_wer:='NL_WERU';
_after_rfr:="";
_before:="
::   M.cntx_psh();
::   FILTER.T_WPM:=_a;
   POMOC.RODZ:='U';
   ZAKR.MATU:='A';
   ZAKR.R_PROD:='N';
   ZAKR.R_KOMP:='N';

   POMOC.RODZ:='U';
   TAZ.CEN_VIEW:='U';
   TAZ.RODZ:=POMOC.RODZ;
   {? TAZ.CEN_VIEW='' || TAZ.CEN_VIEW:=POMOC.RODZ ?};
   exec('zakres_set_usluga','material');
   exec('m_win_edit','material');
   exec('m_win_patt','material');
   exec('wpm_opis','material',FILTER.T_WPM,' %1 — '['i usługi'@]+exec('zakr_naz','material',ZAKR.MATU));
   ~~
";
_after:="

::   M.cntx_pop();
   ~~
";
_hidden:='';
M.actions(_wer,_hidden,_default);
M.grp_sel(_grp,,_wer,'Usługi'@,_after_rfr,,,,_before,_after,,,'maximized');

M.win_sel(_grp);
M.win_dict(_grp);
''


\valid_limit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Podczas walidacji limitu - sprawdza czy dla zlecenia na usługi można dodać/poprawić limit
::       Kontekst pracy - ZLIM
::   WY: STRING - nazwa pola do którego wrócić lub ''
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_result


\valid_nlim
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Podczas walidacji nielimitu - sprawdza czy dla zlecenia na usługi można dodać/poprawić nielimit
::       Kontekst pracy - ZLIM
::   WY: STRING - nazwa pola do którego wrócić lub ''
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_result


\valid_tktl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Podczas walidacji TKTL - sprawdza czy można dodać/poprawić kartę o takim indeksie
::       Kontekst pracy - TKTL
::   WY: STRING - nazwa pola do którego wrócić lub ''
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='';
{? _result=''
|| {? TKTL.KTM<>null() & TKTL.KTM().RODZ='U'
   || TOPER.cntx_psh();
      TOPER.index('ANNN');
      TOPER.prefix('T','N',TKTL.ref());
      {? TOPER.first()
      || {!
         |? {? TOPER.DOK<>null() & TOPER.DOK<>exec('get_typydok','zl_uslugi')
            || _msg:='Technologia zawiera operacje raportowane do magazynu dokumentem innym niż %1.\n'
                     'Taka konfiguracja operacji jest niedozwolona dla usług.'@[exec('get_typydok_sym','zl_uslugi')];
               FUN.emsg(_msg);
               _result:='KTM'
            ?};
            TOPER.next() & _result=''
         !}
      ?};
      TOPER.cntx_pop();
      ~~
   ?}
?};
_result


\valid_tmat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Podczas walidacji TMAT - sprawdza czy można dodać/poprawić taki surowiec do karty
::       Kontekst pracy - TMAT
::   WY: STRING - nazwa pola do którego wrócić lub ''
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_result


\valid_tktlw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Po redakcji pola TKTLW.KTM - sprawdza czy można dodać taki produkt do karty
::       Kontekst pracy - TKTLW
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? _result>0
|| {? TKTLW.KTM().RODZ='U'
   || TOPER.cntx_psh();
      TOPER.index('ANNN');
      TOPER.prefix('T','N',TKTLW.TKTL);
      {? TOPER.first()
      || {!
         |? {? TOPER.DOK<>null() & TOPER.DOK<>exec('get_typydok','zl_uslugi')
            ||  _msg:='Technologia zawiera operacje raportowane do magazynu dokumentem innym niż %1.\n'
                      'Taka konfiguracja operacji jest niedozwolona dla usług.'@[exec('get_typydok_sym','zl_uslugi')];
               FUN.emsg(_msg);
               _result:=0
            ?};
            TOPER.next() & _result=1
         !}
      ?};
      TOPER.cntx_pop()
   ?}
?};
_result


\valid_toper
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Podczas walidacji TOPER - sprawdza czy można dodać/poprawić taką operację do karty
::       Kontekst pracy - TMAT
::   WY: STRING - nazwa pola do którego wrócić lub ''
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='';
{? _result=''
|| {? VAR.A_KTL().KTM().RODZ='U'
   || {? TOPER.DOK<>null() & TOPER.DOK<>exec('get_typydok','zl_uslugi')
      || _msg:='Technologia na usługi może być raportowana do magazynu tylko dokumentem typu: %1.'@
               [exec('get_typydok_sym','zl_uslugi')];
         {? VAR.GRUPA='T'
         || KOMM.add(exec('grp_mod_msg','tech_oper')+_msg)
         || FUN.emsg(_msg)
         ?};
         _result:='DOK'
      ?}
   ?}
?};
_result


\zk_p_ilosc_zre
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Zwraca ilosc możliwą do realizacji na zamówieniu sprzedaży dotyczącym usługi
::   WE: [_a] - ZK_P.ref lub bieżący rekord
::   WY: REAL
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
{? var_pres('_a')=type_of(ZK_P.ref())
|| _ref:=_a
?};

_result:=0;
_can_continue:=1;

ZK_P.cntx_psh();
{? _ref<>null()
|| ZK_P.prefix();
   {? ZK_P.seek(_ref)
   || _can_continue:=1
   || _can_continue:=0
   ?}
?};

{? _can_continue>0
|| {? ZK_P.M().RODZ='U'
   || ZLZAM.cntx_psh();
      ZLZAM.index('ZMZL');
      ZLZAM.prefix($ZK_P.ref());
      {? ZLZAM.first()
      ||
::       Są zlecenia
         {!
         |? _result+=ZLZAM.ZL().ILDOK;
            ZLZAM.next()
         !}
      ||
::       Brak zleceń
         _result:=ZK_P.ILP
      ?};
      ZLZAM.cntx_pop()
   ?}
?};
ZK_P.cntx_pop();
_result


\get_typydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Zwraca typ dokumentu do raportowania zlecenia usługowego
::   WY: TYPYDOK.ref lub null
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
TYPYDOK.cntx_psh();
TYPYDOK.index('TYP');
TYPYDOK.prefix('RRU',);
{? TYPYDOK.first()
|| _result:=TYPYDOK.ref()
?};
TYPYDOK.cntx_pop();
_result


\get_typydok_sym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Zwraca typ dokumentu do raportowania zlecenia usługowego
::   WY: TYPYDOK.ref lub null
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_result:='';
_typ:=exec('get_typydok','zl_uslugi');
_result:=exec('FindAndGet','#table',TYPYDOK,_typ,,"T",'');
_result


\is_usluga_zl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Sprawdza czy zlecenie jest na usługę
::   WE: [_a] - ZL.ref lub bieżący rekord
::   WY: 0 - nie
::       1 - tak
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
{? var_pres('_a')=type_of(ZL.ref())
|| _ref:=_a
?};

_result:=0;
_can_continue:=1;

ZL.cntx_psh();
{? _ref<>null()
|| ZL.prefix();
   {? ZL.seek(_ref)
   || _can_continue:=1
   || _can_continue:=0
   ?}
?};

{? _can_continue>0
|| {? ZL.KTM().RODZ='U'
   || _result:=1
   ?}
?};
ZL.cntx_pop();
_result


\is_usluga_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Sprawdza czy podany typ dokumentu jest dokumentem do raportowania uslug
::   WE: _a - TYPYDOK.ref
::   WY: 0 - nie jest
::       1 - jest
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_typydok:=_a;
_result:=0;
{? _typydok<>null()
|| {? _typydok=exec('get_typydok','zl_uslugi')
   || _result:=1
   ?}
?};
_result


\zmcene
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Zmienia cenę na dokumencie RRU
::   WE: [_a] - DK.ref lub bieżący rekord
::       _b - REAL - nowa cena
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
{? var_pres('_a')=type_of(DK.ref())
|| _ref:=_a
?};
_cena:=_b;

_result:=0;
_can_continue:=1;

DK.cntx_psh();
{? _ref<>null()
|| DK.prefix();
   {? DK.seek(_ref)
   || _can_continue:=1
   || _can_continue:=0
   ?}
?};

{? _can_continue>0
|| DK.C:=_cena;
   DK.CN:=_cena;
   DK.WAR:=(DK.C*DK.IL)$2;
   _can_continue:=DK.put()
?};
DK.cntx_pop();
DK.get();
{? _can_continue>0
|| _result:=1
?};
_result


:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 9166ff3d415e734845c38c120034bacc41cb3ac35fe6c7b792d462c6b4f38456b5d8b55303fe47d936e2d84f119b4d5a337bf134f363438798ddcc6d3c3a4ec63bd25a2d99833814af4880c81877b971add4dae60cae40daf52e7664474aab97a230f59e0818f3da3f55b71874e30cc2618eb4a55d72154742ba23aa039e4a19

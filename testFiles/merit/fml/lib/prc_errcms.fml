:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku : prc_cmserr.fml [1996]
:: Utworzony   : 2017/02/28
:: Autor       : PM
::======================================================================================================================
:: Zawartosc: Udostępnienie wyników weryfikacji planów w portalu
::======================================================================================================================

\del_err
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [12.41]
:: OPIS: Usunięcie informacji o błędach
::   WE: _a - STRING[1] - Źródło [P|G]
::       _b - STRING[1] - Typ [O|M|D]
::       _c - STRING[16] - $P.ref
::       _d - STRING[16] - $A_OKR.ref
::   WY: [0|1]
::  OLD: \del_err/rcp_err.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || _a:='' ?};
{? var_pres('_b')<>type_of('') || _b:='' ?};
{? var_pres('_c')<>type_of('') || return(0) ?};
{? var_pres('_d')<>type_of('') || return(0) ?};
P.cntx_psh();
P.use('pracowni');
P.prefix();
_ret:=1;
{? P.seek(_c,,1)
|| A_OKRP.cntx_psh();
   A_OKRP.use('x_okrpi');
   A_OKR.cntx_psh();
   A_OKR.prefix();
   {? A_OKR.seek(_d,,1) || _ref_okr:=A_OKR.ref() || _ref_okr:=null() ?};
   A_OKR.cntx_pop();
   A_OKRP.index('A_OKRPR');
   A_OKRP.prefix(_ref_okr,P.ref());
   {? A_OKRP.first()
   || R_ERRCMS.cntx_psh();
      R_ERRCMS.index('R_ERRCMS');
      R_ERRCMS.prefix(P.ref(),A_OKRP.ref());
      {? R_ERRCMS.first()
      || {!
         |? {? (_b='' | R_ERRCMS.RODZAJ=_b) & (_a='' | R_ERRCMS.TYP=_a)
            || _ret:=R_ERRCMS.del(,1);
               _ret=2
            || R_ERRCMS.next()
            ?}
         !}
      ?};
      R_ERRCMS.cntx_pop()
   ?};
   A_OKRP.cntx_pop()
?};
P.cntx_pop();
_ret<>0


\add_err
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [12.41]
:: OPIS: Zapisanie błędów do systemu.
::   WE: _a - STRING[1] - Źródło [P|G]
::       _b - STRING[1] - Typ [O|M|D]
::       _c - STRING[16] - $P.ref
::       _d - DATE - dzień
::       _e - STRING[180] - Komunikat
::       _f - STRING[16] - $A_OKR.ref
::   WY: [0|1]
::  OLD: \add_err/rcp_err.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('') || return(0) ?};
{? var_pres('_b')<>type_of('') || return(0) ?};
{? var_pres('_c')<>type_of('') || return(0) ?};
{? var_pres('_d')<>type_of(date) || return(0) ?};
{? var_pres('_e')<>type_of('') || return(0) ?};
{? var_pres('_f')<>type_of('') || return(0) ?};
P.cntx_psh();
P.use('pracowni');
P.prefix();
_ret:=0;
{? P.seek(_c,,1)
|| A_OKRP.cntx_psh();
   A_OKRP.use('x_okrpi');
   A_OKR.cntx_psh();
   A_OKR.prefix();
   {? A_OKR.seek(_f,,1) || _ref_okr:=A_OKR.ref() || _ref_okr:=null() ?};
   A_OKR.cntx_pop();
   A_OKRP.index('A_OKRPR');
   A_OKRP.prefix(_ref_okr,P.ref());
   {? A_OKRP.first()
   || R_ERRCMS.cntx_psh();
      R_ERRCMS.index('R_ERRCMS');
      R_ERRCMS.prefix();
      R_ERRCMS.blank();
      R_ERRCMS.TYP:=_a;
      R_ERRCMS.RODZAJ:=_b;
      R_ERRCMS.P:=P.ref();
      R_ERRCMS.A_OKRP:=A_OKRP.ref();
      R_ERRCMS.DATA:=_d;
      R_ERRCMS.DESCR:=_e;
      _ret:=R_ERRCMS.add();
      R_ERRCMS.cntx_pop()
   || _ret:=0
   ?};
   A_OKRP.cntx_pop()
?};
P.cntx_pop();
_ret


\wer_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [12.41]
:: OPIS: Przeglądanie wyników weryfikacji planowania po stronie jTerm.
::       Wymagany ustawiony rekord A_OKRP.
::   WE: _a - P/G/'' - plan/grafik/wszystko
::   WY:
::  OLD: \wer_sel/rcp_err.fml
::----------------------------------------------------------------------------------------------------------------------
_fcond:=obj_new('ZAKRES','OD','DO','TYP');
_fcond.ZAKRES:='O';
_fcond.OD:=A_OKRP.OD;
_fcond.DO:=A_OKRP.DO;
_fcond.TYP:={? var_pres('_a')=type_of('') || _a || 'P' ?};
params_set('fcond',_fcond);
R_ERRCMS.cntx_psh();
exec('fmod_sel','prc_errcms',_fcond.TYP);
R_ERRCMS.actions_grayed('WER',_fcond.TYP);
R_ERRCMS.select();
{? R_ERRCMS.f_active() || R_ERRCMS.f_clear() ?};
R_ERRCMS.cntx_pop();
obj_del(_fcond)


\wer_selm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [12.41]
:: OPIS: Przeglądanie wyników weryfikacji planowania po stronie jTerm w ujęciu miesięcznym.
::       Musi być ustawiony A_OKRP.
::       Jeżeli brak _a i _b to musi być ustawiony rekord A_OKRP_M.
::   WE: _a - [INTEGER] - rok
::       _b - [INTEGER] - miesiąc
::       _c - [STRING] - P/G/'' - plan/grafik/wszystko
::  OLD: \wer_selm/rcp_err.fml
::----------------------------------------------------------------------------------------------------------------------
_fcond:=obj_new('ZAKRES','OD','DO','TYP');
_fcond.ZAKRES:='M';
_rok:={? var_pres('_a')=type_of(1) || _a || A_OKRP_M.ROK ?};
_msc:={? var_pres('_b')=type_of(1) || _b || A_OKRP_M.MSC ?};
_fcond.OD:=date(_rok,_msc,1);
_fcond.DO:=date(_rok,_msc,0);
_fcond.TYP:={? var_pres('_c')=type_of('') || _c || 'P' ?};
params_set('fcond',_fcond);
R_ERRCMS.cntx_psh();
R_ERRCMS.index('R_ERRCMS');
exec('fset_sel','prc_errcms');
R_ERRCMS.actions_grayed('WER',_fcond.TYP);
R_ERRCMS.select();
{? R_ERRCMS.f_active() || R_ERRCMS.f_clear() ?};
R_ERRCMS.cntx_pop();
obj_del(_fcond)


\fset_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [12.41]
:: OPIS: Ustawia filtr programowy w oknie weryfikacji planowania po stronie jTerm w ujęciu miesięcznym.
::   WE:
::   WY:
::  OLD: \fset_sel/rcp_err.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_fcond:=params_get().fcond;
R_ERRCMS.f_clear();
R_ERRCMS.prefix();
R_ERRCMS.f_set(,,
   'R_ERRCMS.DATA>=to_date(:_a) and R_ERRCMS.DATA<=to_date(:_b) and R_ERRCMS.A_OKRP=:_c'+
   {? _fcond.TYP<>'' || ' and R_ERRCMS.TYP=\':_d\'' || '' ?},
   _fcond.OD,_fcond.DO,A_OKRP.ref(),_fcond.TYP
);
1


\fmod_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [12.41]
:: OPIS: Zmiana ustawień filtra programowego w okienku weryfikacji planowania.
::   WE: _a - [STRING] - Typ wyświetlanych danych 'P'-plan, 'G'-grafik.
::   WY:
::  OLD: \fmod_sel/rcp_err.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_fcond:=params_get().fcond;
{? var_pres('_a')=type_of('') || _fcond.TYP:=_a || _fcond.TYP:={? _fcond.TYP<>'P' || 'P' || 'G' ?} ?};
{? _fcond.ZAKRES='M'
|| exec('fset_sel','prc_errcms')
|? _fcond.ZAKRES='O'
|| {? _fcond.TYP=''
   || R_ERRCMS.index('R_ERRCMS');
      R_ERRCMS.prefix(A_OKRP.P,A_OKRP.ref())
   || R_ERRCMS.index('R_ERRCMR');
      R_ERRCMS.prefix(A_OKRP.ref(),_fcond.TYP)
   ?}
?};
R_ERRCMS.actions_grayed('WER',_fcond.TYP);
1


\chk_err
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [12.41]
:: OPIS: Sprawdza warunek czy w miesiącu są błędy w planowaniu.
::       Jeżeli brak _a i _b to musi być ustawiony rekord A_OKRP_M.
::   WE: _a [INTEGER] - rok
::       _b [INTEGER] - miesiąc
::   WY: [0|1]
::  OLD: \chk_err/okres.fml
::----------------------------------------------------------------------------------------------------------------------
_rok:={? var_pres('_a')=type_of(1) || _a || A_OKRP_M.ROK ?};
_msc:={? var_pres('_b')=type_of(1) || _b || A_OKRP_M.MSC ?};
R_ERRCMS.cntx_psh();
_od:=date(_rok,_msc,1);
_do:=date(_rok,_msc,0);
R_ERRCMS.index('R_ERRCMT');
R_ERRCMS.prefix(A_OKRP.ref(),'P','D');
_ret:=R_ERRCMS.find_le(_do) & R_ERRCMS.DATA>=_od;
R_ERRCMS.prefix(A_OKRP.ref(),'P','M');
_ret+=R_ERRCMS.find_le(_do) & R_ERRCMS.DATA>=_od;
R_ERRCMS.cntx_pop();
_ret>0


\chk_errl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [12.41]
:: OPIS: Sprawdza warunek czy ustawiony miesiąc jest ostatnim miesiącem w okresie.
::       Jeżeli tak to sprawdza rownież błędy dla okresu.
::       Jeżeli brak _a i _b to musi być ustawiony rekord A_OKRP_M.
::   WE: _a [INTEGER] - rok
::       _b [INTEGER] - msc
::   WY: [0|1]
::  OLD: \chk_errl/okres.fml
::----------------------------------------------------------------------------------------------------------------------
_rok:={? var_pres('_a')=type_of(1) || _a || A_OKRP_M.ROK ?};
_msc:={? var_pres('_b')=type_of(1) || _b || A_OKRP_M.MSC ?};
A_OKRP_M.cntx_psh();
A_OKRP_M.index('A_OKRPM');
A_OKRP_M.prefix(A_OKRP.ref());
_last:={? A_OKRP_M.last() & A_OKRP_M.ROK=_rok & A_OKRP_M.MSC=_msc || 1 || 0 ?};
A_OKRP_M.cntx_pop();
_ret:=0;
{? _last || _ret+=exec('chk_erro','prc_errcms') ?};
_ret>0


\chk_erro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [12.41]
:: OPIS: Sprawdza warunek czy w okresie sa błędy w planowaniu.
::   WE:
::   WY: [0|1]
::  OLD: \chk_erro/okres.fml
::----------------------------------------------------------------------------------------------------------------------
R_ERRCMS.cntx_psh();
R_ERRCMS.index('R_ERRCMT');
R_ERRCMS.prefix();
_ret:=R_ERRCMS.find_key(A_OKRP.ref(),'P');
R_ERRCMS.cntx_pop();
_ret>0


\r_errcms_wer_br
::----------------------------------------------------------------------------------------------------------------------
::  UTW: areKc [18.02]
:: OPIS: Formuła "Rekord przed" dla okna WER tabeli R_ERRCMS.
::   WE: _a [NUMBER] - Rekord bieżący? [0 - nie / 1 - tak]
::   WY:
::----------------------------------------------------------------------------------------------------------------------
POLA_GRP.TXT_1:=
   {? R_ERRCMS.TYP='G'
   || 'Grafik'
   || 'Plan'
   ?};
POLA_GRP.TXT_2:=
   {? R_ERRCMS.RODZAJ='O'
   || 'Błędy planowania okresu'
   |? R_ERRCMS.RODZAJ='M'
   || 'Błędy planowania miesiąca'
   || 'Błędy planowania dnia'
   ?}

:Sign Version 2.0 jowisz:1028 2019/10/14 09:21:33 8a1b3d1f49c8af87dd323d19273fe7ba379a76760c8f8f357e4a29577aced377580058665c99d37ad85d9a1becb15a4b777a65909d7305f50dd3200f1101e8be58d9e162ffc4cc4830ed4fa2797a080411a8c62ad7a5b7313eed0ff2861c10f4cad0227a31e30cf454d7647a68b8b87f9d3a726e5b2124496ff25524623979e9

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: jksieg.fml
:: Utworzony: 30.01.2015
:: Autor: DRO
::======================================================================================================================
:: Zawartość:  Formuły do obsługi jednostek księgowych
::======================================================================================================================


\jksieg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.00]
:: OPIS: Formuła do definiowania jednostek księgowych
::----------------------------------------------------------------------------------------------------------------------
exec('load_par','#parametr');
_firma:=REF.FIRMA;
REF.FIRMA:=REF.S_FIRMA;
ODD.win_sel('WER');

:: usuwanie filtra programowego z okna j. księgowych
_filtr:=ODD.f_active();
{? _filtr=1 | _filtr=3 || ODD.f_clear(1) ?};

ODD.index('ODDZIALY'); ODD.prefix(REF.S_FIRMA);
{? REF.S_FIRMA().TYP='C' | FIRMA.TYP='W'
|| ODD.actions('WER','dUp')
?};
ODD.select();
ODD.actions('WER');
REF.FIRMA:=_firma


\aa_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [8.60]
:: OPIS: Po akcji dołącz, tabela ODD, okno WER
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.first() ||
   {! |?
      exec('add_bobz','rejestry',ROK_F.ref(),ODD.OD);
      exec('add_odd_view','rejestry');
      ROK_F.next()
   !}
?};
ROK_F.cntx_pop()


\bd_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [8.60]
:: OPIS: Przed akcją usuń, tabela ODD, okno WER
::  OLD: \bd_odd/skid_rej
::----------------------------------------------------------------------------------------------------------------------
{? _ok:=ODD.ref=OPERATOR.DEPT
|| FUN.emsg('Usunięcie aktywnej jednostki księgowej %1 jest niemożliwe.'@[ODD.OD])
?};
_ok:=0;
{? ~_ok ||
   REJ.cntx_psh();
   _i:=REJ.ndx_tmp(,1,'ODD',,);
   REJ.index(_i); REJ.prefix(ODD.ref());
   {? REJ.first() ||
      {! |?
         _ok:=(REJ.KOD<>'~BO' & REJ.KOD<>'~BZ');
         ~_ok & REJ.next()
      !}
   ?};
   _rok:=REJ.ROK().NAZ;
   REJ.cntx_pop();
   REJ.ndx_drop();
   {? _ok
   || FUN.emsg('Usunięcie jednostki księgowej %1'
       ' jest niemożliwe.\nW jednostce istnieją rejestry inne niż ~BO i ~BZ. Uwaga - rok %2'@[ODD.OD,_rok])
   ?}
?};
{? ~_ok
|| DOK.cntx_psh();
   OKRO_F.cntx_psh(); OKRO_F.index('ROK');
   ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   {? ROK_F.first()
   || {! |?
         OKRO_F.prefix(ROK_F.ref());
         {? OKRO_F.first() ||
            {! |?
               DOK.use('doku'+ROK_F.KOD+form(OKRO_F.NR,-2));
               DOK.index('O_REJ'); DOK.prefix(ODD.ref);
               _ok:=DOK.first();
               ~_ok & OKRO_F.next()
            !}
         ?};
         ~_ok & ROK_F.next()
      !}
   ?};
   OKRO_F.cntx_pop(); DOK.cntx_pop(); ROK_F.cntx_pop();
   {? _ok
   || FUN.emsg('Usunięcie jednostki księgowej %1'
      ' jest niemożliwe.\nIstnieją dokumenty w jednostce.'@[ODD.OD])
   ?}
?};
{? ~_ok & ODD.size=1
|| FUN.emsg('Usunięcie jednostki księgowej %1'
   ' jest niemożliwe.\nJedna jednostka księgowa musi istnieć.'@[ODD.OD]);
   _ok:=1
?};
~_ok


\ad_odd
::--------------------------------------------------------------------------------------------------
::  UTW: [MB] [8.60]
:: OPIS: Po akcji usuń, tabela ODD, okno WER
::  OLD: \ad_odd/skid_rej
::--------------------------------------------------------------------------------------------------
_odd:=ODD.OD;
{? FUN.ask('Usunąć jednostkę księgową %1?'@[_odd])
|| do();
   REJ.cntx_psh(); REJ.index('KOD');
   ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
   {? ROK_F.first() ||
      {! |?
         REJ.prefix(ROK_F.ref(),ODD.ref());
         {? REJ.first() ||
            {! |?
               exec('del_rej','rejestry')
            !}
         ?};
         exec('del_rok_view','rejestry',ROK_F.ref(),ODD.ref());
         ROK_F.next()
      !}
   ?};
   {? ODD.count()>0
   || undo()
   || ODD.del()
   ?};
   ROK_F.cntx_pop();
   REJ.cntx_pop();
   _ok:=end();
   {? ~_ok
   || FUN.emsg('Usuwanie jednostki księgowej %1 zakończyło się niepowodzeniem.'@[_odd])
   ?}
?}


\ar_odd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Rekord po okien tabeli ODD
::   WE: [_a] - typ operacji: 1-dołącz 0-popraw
::  OLD: \ar_odd/skid_rej
::----------------------------------------------------------------------------------------------------------------------
_a:={? var_press('_a')<=0 || -menu_txt()<>'popraw' || _a ?};
_r:='';
{? ODD.OD=''
|| FUN.info('Nie podano skróconej nazwy jednostki ksiegowej.'@); _r:='OD'
|? ODD.N=''
|| FUN.info('Nie podano pełnej nazwy jednostki ksiegowej.'@); _r:='N'
|| ODD.cntx_psh();
   _ref:=ODD.ref; ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.find_key(ODD.OD,ODD.OD) & (_a | _ref<>ODD.ref())
   || FUN.emsg('Istnieje już jednostka księgowa o skrócie: %1.'@[ODD.OD]);
      _r:='OD'
   ?};
   ODD.cntx_pop()
?}; _r

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:52 d34fc204ae98b9c16de95365d410a32b48e67e6c0c1e9a8fea7ce9975a0b8b507ffcf8d61175846924cd8c8b0fcc9309670c4bea8f2ff12be16f8bc73515acc89762cd21f5fbf462fa9794d43428d9a372cb7ed8ff98a62185ee584d510cdcc28f02f60fcfe0d2b48542fadb4f49d6ada9d9080aa74b611c76f4fea88d3de45c

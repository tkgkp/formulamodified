:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: fakso.fml [17.00]
:: Utworzony: 20.02.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: obsługa wspólnych operacji dla tabeli FAKSO - informacje dodatkowe
::======================================================================================================================


\delfakso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: sprawdza czy istnieją powiązane informacje dodatkowe FAKSO - i usuwa je
::   WE: _a maska FAKSO
::       _b wartość index-u
::       _c wartość prefix-u
::   WY: 0/1 czy usuwać
::  OLD: \delfakso/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
FAKSO.cntx_psh();
FAKSO.use(_a);
FAKSO.index(_b);
FAKSO.prefix(_c);
{? FAKSO.first()
|| {? FUN.ask('Istnieją powiązane informacje dodatkowe. Usunąć?'@)
   || {! |? FAKSO.del() !}
   || _wyn:=0
   ?}
?};
FAKSO.cntx_pop();
_wyn


\bl_typsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MAKO [2010]
:: OPIS: Wartość początkowa pola FAKSO.TYPSP
::  OLD: \bl_typsp/umowy.fml
::----------------------------------------------------------------------------------------------------------------------
POM.TYPYSP


\bl_typd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MAKO [2010]
:: OPIS: Wartość początkowa pola FAKSO.TYPYD
::  OLD: \bl_typd/umowy.fml
::----------------------------------------------------------------------------------------------------------------------
POM.TYPYDOK


\slo_chf3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: słowniki dla F3
::   WY: ~~
::  OLd: \slo_chf3/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
_pos:=get_cpos();
_war:=fld();

_lt:='<SFL3';
_sl:='<F3: <SL: <FL:';
_ch:=(_pos+_war)+1;
_nr:=_lt*_ch;
_poz:=
{? _nr=1         || _poz:=_pos+3
|? _nr=2 | _nr=3 || _poz:=_pos+2
|? _nr>3         || _poz:=_pos+1
|| _poz:=_pos
?};
_wsk:=(_poz+_war)+4;
{? _wsk<>'' & _sl*_wsk
|| _wyn:=exec('get_slo','fakso',_wsk);
   FAKSO.O:=(_poz+_war)+_wyn+'>'+(_poz-_war)
?};
~~


\get_slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: podpowiada tabele, słowniki pola
::   WY: nazwa słownika, ''-wpp
::  OLD: \get_slo/aufakso.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? _a='<SL:'
||
   SLU.cntx_psh();
   _acr_sel:=SLU.mk_sel('Słowniki systemu:'@,'T',0,'get_slo_acrsel');
   SLU.win_fld(_acr_sel,,'NAZ',,,20,,1,'Nazwa'@);
   SLU.win_fld(_acr_sel,,'OP',,,60,,1,'Opis'@);
   SLU.win_act(_acr_sel,0,'Formuła','&Wybierz'@@,,'Wybór bieżącego zapisu'@,"sel_exit()",,1,,,,'W');
   SLU.win_act(_acr_sel,0,'Szukaj',,,,,,0);
   SLU.index('NAZ');
   SLU.win_sel(_acr_sel);
   {? SLU.select() || _wyn:=SLU.NAZ ?};
   SLU.cntx_pop()
|? _a='<F3:'
|| ''
|? _a='<FL:'
|| ''
?};
_wyn


\fakso_mw_pr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: po redakcji FAKSO.MW
::   WY: 1
::  OLD: \fakso_mw_pr/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
{? 'TA'*FAKSO.PRN & FAKSO.MW='N'
|| {? FAKSO.GD='' || FAKSO.GD:=exec('bl_gd','fakso') ?}
|| FAKSO.GD:=''
?};
_enabl1:='enable='+{? FAKSO.MW='P' | FAKSO.PRN='N' || '0' || '1' ?};
FAKSO.efld_opt('RED',_enabl1,,'GD');
1


\bl_gd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: wartość początkowa FAKSO.GD
::   WY: 'D1' lub string ''
::  OLD: \bl_gd/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
{? 'TA'*FAKSO.PRN || 'D1' || '' ?}


\pr_gd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcją FAKSO.GD
::   WY: 1-redakcja dozwolona, 0-wpp
::  OLD: \pr_gd/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
{? 'TA'*FAKSO.PRN & FAKSO.MW='N'
|| VAR_DEL.delete('__faksogd');
   __faksogd:=tab_tmp(1,'KOD','STRING[2]' ,''
               ,'NAZ','STRING[40]','');
   __faksogd.KOD:='G1'; __faksogd.NAZ:='Góra - 1 kolumna'; __faksogd.add(1);
   __faksogd.KOD:='G2'; __faksogd.NAZ:='Góra - 2 kolumny'; __faksogd.add(1);
   __faksogd.KOD:='D1'; __faksogd.NAZ:='Dół  - 1 kolumna'; __faksogd.add(1);
   __faksogd.KOD:='D2'; __faksogd.NAZ:='Dół  - 2 kolumny'; __faksogd.add(1);
   __faksogd.KOD:='G3'; __faksogd.NAZ:='Góra - 3 kolumny'; __faksogd.add(1);
   __faksogd.KOD:='G4'; __faksogd.NAZ:='Góra - 4 kolumny'; __faksogd.add(1);
   __faksogd.KOD:='D3'; __faksogd.NAZ:='Dół  - 3 kolumny'; __faksogd.add(1);
   __faksogd.KOD:='D4'; __faksogd.NAZ:='Dół  - 4 kolumny'; __faksogd.add(1);
   _acr_sel:=__faksogd.mk_sel('Miejsca wydruku'@,'T',0,'autopopl_acrsel');
   __faksogd.win_fld(_acr_sel,,'KOD',,,2,,1,'Kod'@);
   __faksogd.win_fld(_acr_sel,,'NAZ',,,40,,1,'Opis'@);
   __faksogd.win_act(_acr_sel,0,'Formuła','Wybierz'@@,,'Wybór bieżącego zapisu'@,"sel_exit(); FAKSO.GD:=__faksogd.KOD",,1);
   __faksogd.win_sel(_acr_sel);
   1
|| 0
?}


\f3_gd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: klawisz F3 pola FAKSO.GD
::  OLD: \f3_gd/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
__faksogd.select()


\po_gd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: po redakcji FAKSO.GD
::   WY: 1 - warunki spełnione, 0 - wpp
::  OLD: \po_gd/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
__faksogd.prefix();
{? __faksogd.find_key(fld())
|| _wyn:=1
|| _wyn:=exec('f3_gd','fakso')
?};
VAR_DEL.delete('__faksogd');
_wyn


\infd_wert_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed dołącz okna WERT tabeli FAKSO
::   WE: _a - brak argumentu - dołącz; = 0 - pusta dziedzina; = 1 - dołącz|
::   WY: 1 - dodanie powiodło się, 0-wpp
::  OLD: \infd_wert_dol/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
_return:=1;

_nr:={? _=0  || FAKSO.LP
     |? _a=1 || FAKSO.LP+1
     || 1
     ?};

FAKSO.blank();
FAKSO.LP:=_nr;
FAKSO.win_edit('RED');
exec('fakso_mw_pr','fakso');
exec('after_or','fakso');

{? FAKSO.edit("exec('chkfakso','fakso')")
|| {? _=0 || _return:=exec('reNrAdd','#table','FAKSO','LP') ?};
   {? _return=1 || FAKSO.add() ?}
?};

_return


\infd_wert_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed usuń okna WERT tabeli FAKSO
::  OLD: \infd_wert_usu/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
_return:=0;

{? FUN.ask('Czy usunąć bieżący wiersz?'@)
||
   _lp:=FAKSO.LP;
   _oki:=exec('del_uid','tlumaczenia',FAKSO.uidref());
   {? _oki & FAKSO.del()
   ||
      {? _lp < FAKSO.LP || exec('ReNumAfterDel','#table','FAKSO','LP') ?}
   ?};
   {? ~_oki || FUN.info('Nie udało się usunąć tłumaczeń?\nRekord nie został usunięty.'@) || _return:=1 ?}
?};

_return


\infd_wert_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed popraw okna WERT tabeli FAKSO
::  OLD: \infd_wert_pop/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
_return:=0;

FAKSO.win_edit('RED');
exec('fakso_mw_pr','fakso');
exec('after_or','fakso');

{? FAKSO.edit("exec('chkfakso','fakso')")
||
   _return:=FAKSO.put()
?};

_return


\chkfakso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: kontrola wypełnienia pól
::  OLD: \chkfakso/defin.fml
::----------------------------------------------------------------------------------------------------------------------
_res:={? FAKSO.OR='W'
      || __CHK.record(FAKSO,,'LP','T','SLU')
      || __CHK.record(FAKSO,,'LP','T')
      ?};
{? _res='' & menu_pth()='Popraw' & FAKSO.OR<>'S' & exec('FindInSet','#table','TRANSLAT','UID',FAKSO.uidref())<>null()
|| FUN.info('Wprowadzono tłumaczenia.\nZmiana Sposobu generacji niemożliwa.'@);
   _res:='OR'
?};
_res


\infd_wert_szuk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed popraw okna WERT tabeli FAKSO
::  OLD: \infd_wert_szuk/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
FAKSO.win_patt('SZUD');
1


\infd_wert_wys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyświetl okna WERT tabeli FAKSO
::  OLd: \infd_wert_wys/fakso.fml
::----------------------------------------------------------------------------------------------------------------------
FAKSO.win_edit('RED');
FAKSO.display()


\ilefakso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: zwraca ilość rekordów
::   WE: _a maska FAKSO
::       _b wartość index-u
::       _c wartość prefix-u
::   WY: ilość rekordów
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
FAKSO.cntx_psh();
FAKSO.use(_a);
FAKSO.index(_b);
FAKSO.prefix(_c);
{? FAKSO.first() || _wyn:=FAKSO.size() ?};
FAKSO.cntx_pop();
_wyn


\inf_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: informacje dodatkowe dla wszystkiego
::       założenie że rekord dla którego są analizowane informacje dodatkowe ma otwartą prawidłową maskę
::   WE: [_a] - 0-uruchamiana dla funkcji aktualizacji; 1-wpp
::       [_b] - maska tabeli dla której wołana jest funkcja
::       [_c] - czy pytać o aktualizację (T/N/P - domyślnie N) P-dla funkcji popraw
::       [_d] - FAKS.LP pozycja którą chcesz  poprawić
::       [_e] - 1-wywołanie okienka jako akcji w okienku, 0-(domyślnie)-nie
::  OLD: \inf_dod/autofakso.fml
::----------------------------------------------------------------------------------------------------------------------
_blknag:={? _=0 || 0 || -1 ?};
{? _>=1 || {? type_of(_a)<>1 || _a:=1; BEER.MSK:='' ?} || _a:=1; BEER.MSK:='' ?};
{? _>=2 || {? type_of(_b)<>2 || _b:='' ?} || _b:='' ?};
{? _>=3 || {? type_of(_c)<>2 || _c:='N' ?} || _c:='N' ?};
{? _>=4 || {? type_of(_d)<>1 || _d:=0 ?} || _d:=0 ?};
_menuakc:={? var_pres('_e')=type_of(0) & _e=1 || 1 || 0 ?};

{? ~_a || BEER.MSK:='' ?};
_czy_sel:=_a>0;
BEER.MSK:={? _b='' || {? BEER.MSK='' || 5+cur_tab(1).name() || BEER.MSK ?} || _b ?};
_mask:=BEER.MSK;

_mrob:=1;
_czy_def:=0;
_anul:=0;
_nagl:='';

_ind:='';
_pol:='';
_ref:='';
_typ:='';
_maska:='';
_blk:='';
_unblk:='';

{? _mask='dokma'
|| _ind:={? ~DK.ZP || 'TYM' || '' ?};
   _pol:='DK.N().TYP';
   _ref:='DK';
   _typ:='P';
   _maska:='fakto'+(DK.name+3);
   _nagl:=': ('+DK.N().TYP().T+') '+DK.N().SYM;
   _blknag:=-2;
   BEER.UID_TAB:=DK.N().TYP().uidref()
|? _mask='nagdo'
|| _ind:='TYM';
   _pol:='ND.TYP';
   _ref:='ND';
   _typ:='N';
   _maska:='fakto'+(ND.name+3);
   _nagl:=': ('+ND.TYP().T+') '+ND.SYM;
   _blk:='ND.r_lock(1,1,1)';
   _unblk:='ND.r_unlock()';
   BEER.UID_TAB:=ND.TYP().uidref()
|? _mask='fakpo'
|| _ind:='TYP';
   _pol:='FAP.FAKS().T';
   _ref:='FAP';
   _typ:='P';
   _maska:='fakto'+(FAP.name+3);
   _nagl:=': ('+FAP.FAKS().T().T+') '+FAP.FAKS().SYM;
   _blknag:=-2;
   BEER.UID_TAB:=FAP.FAKS().T().uidref()
|? _mask='faktu'
|| _ind:='TYP';
   _pol:='FAKS.T';
   _ref:='FAKS';
   _typ:='N';
   _maska:='fakto'+(FAKS.name+3);
   _nagl:=': ('+FAKS.T().T+') '+FAKS.SYM;
   _blk:='FAKS.r_lock(1,1,1)';
   _unblk:='FAKS.r_unlock()';
   BEER.UID_TAB:=FAKS.T().uidref();
   {? FAKS.WHERE='K'
   || FUN.info('Funkcja niedostępna dla korekt zbiorczych.'@);
      return
   ?}
|? _mask='zkpoz'
|| _ind:='TYZ';
   _pol:='ZK_P.N().T';
   _ref:='ZK_P';
   _typ:='P';
   _mrob:=ZK_P.name()+2='__';
   _maska:='fakto'+(ZK_P.name+3);
   _nagl:=': ('+ZK_P.N().T().T+') '+ZK_P.N().SYM;
   _blknag:=-2;
   BEER.UID_TAB:=ZK_P.N().T().uidref()
|? _mask='zknag'
|| _ind:='TYZ';
   _pol:='ZK_N.T';
   _ref:='ZK_N';
   _typ:='N';
   _mrob:=ZK_N.name()+2='__';
   _maska:='fakto'+(ZK_N.name+3);
   _nagl:=': ('+ZK_N.T().T+') '+ZK_N.SYM;
   _blk:='ZK_N.r_lock(1,1,1)';
   _unblk:='ZK_N.r_unlock()';
   BEER.UID_TAB:=ZK_N.T().uidref()
|? _mask='zdpoz'
|| _ind:='TYZ';
   _pol:='ZD_POZ.ZD_NAG().T';
   _ref:='ZD_POZ';
   _typ:='P';
   _mrob:=ZD_POZ.name()+2='__';
   _maska:='fakto'+(ZD_POZ.name+3);
   _nagl:=': ('+ZD_POZ.ZD_NAG().T().T+') '+ZD_POZ.ZD_NAG().SYM;
   _blknag:=-2;
   BEER.UID_TAB:=ZD_POZ.ZD_NAG().T().uidref()
|? _mask='zdnag'
|| _ind:='TYZ';
   _pol:='ZD_NAG.T';
   _ref:='ZD_NAG';
   _typ:='N';
   _mrob:=ZD_NAG.name()+2='__';
   _maska:='fakto'+(ZD_NAG.name+3);
   _nagl:=': ('+ZD_NAG.T().T+') '+ZD_NAG.SYM;
   _blk:='ZD_NAG.r_lock(1,1,1)';
   _unblk:='ZD_NAG.r_unlock()';
   BEER.UID_TAB:=ZD_NAG.T().uidref()
?};

_fld:={? _ref<>'' || ($(_ref+'.ref'))() || null ?};

{? ~_blknag
|| _blknag:=($_blk)();
   {? ~_blknag
   || _txt:={? (1+_ref)='Z' || 'zamówienia'@ || 'dokumentu'@ ?};
      FUN.info('Nie udało się zablokować %1.\Informacje niedostępne.'@[_txt]);
      ($_blk)();
      return(0)
   ?}
?};

{? _c='P' & _d
||
   FAKSO.cntx_psh();
   FAKSO.use('faktodef');
   _ok:=0;
   FAKSO.index(_ind);
   FAKSO.prefix(($_pol)(),_d);
   {? FAKSO.first()
   ||
        _log:=FAKSO.LOG;
        _tytul:=FAKSO.T;
        _opis:=FAKSO.O;
        _or:=FAKSO.OR;
        _prn:=FAKSO.PRN;
        _gd:=FAKSO.GD;
        _ktr:=FAKSO.K_OR_TR;
        _mod:=FAKSO.MODIFY;
        FAKSO.cntx_pop();
        _war:=($_log)();
        {? type_of(_war)<>1 || _war:=0 ?};
        {? _log='' | _war
        ||
         FAKSO.T:=_tytul;
         FAKSO.O:={? _or='F' || ($_opis)() || _opis ?};
         FAKSO.PRN:=_prn;
         FAKSO.GD:=_gd;
         FAKSO.TYPYDOK:=FAKSO.TYPYSP:=FAKSO.TYPYZAM:=null;
         FAKSO.OR:=_or;
         FAKSO.K_OR_TR:=_ktr;
         FAKSO.MODIFY:=_mod;
          _ok+=FAKSO.put(1)
        ||
          _ok:=FAKSO.del()
        ?}
   ||
     FAKSO.cntx_pop();
     {? FUN.ask('Usunięto pozycję z definicji informacji dodatkowej \n Usunąć także tą pozycję w tym dokumecie ?'@)
     ||
       _ok:=FAKSO.del()
     ?}
   ?};
  return(_ok)
?};

FAKSO.cntx_psh();
FAKSO.use(_maska);
FAKSO.index(_ref);
FAKSO.prefix(_fld);

{? _mrob=1 & _ind<>'' & (exec('spr_defo','fakso',_ind,($_pol)())<>null | FAKSO.first)
||
:: tworzenie informacji dodatkowych w dokumencie jesli jest definicja informacji dodatkowych
   _choice:=0;
   _translat:=0;
   POM.TYPYDOK:=null;
   FAKSO.cntx_psh();
   FAKSO.use('faktodef');
   _ok:=0;
   _faksos:=';';
   FAKSO.index(_ind);
   FAKSO.prefix(($_pol)());
   {? FAKSO.first()
   ||
      _czy_def:=1;
      {!
      |? {? _typ=FAKSO.MW
         ||
            _translat+=exec('FindInSet','#table','TRANSLAT','UID',FAKSO.uidref(),,,,,null())<>null();
            _faksos+=$FAKSO.LP+';';
            _tytul:=FAKSO.T;
            _opis:=FAKSO.O;
            _or:=FAKSO.OR;
            _prn:=FAKSO.PRN;
            _gd:=FAKSO.GD;
            _log:=FAKSO.LOG;
            _slu:=FAKSO.SLU;
            _dom:={? ~FAKSO.K_OR_TR | _slu=null() | FAKSO.DOMSLO=''
                  || FAKSO.DOMSLO
                  || exec('FindInSet','#table','SLO','SL',FAKSO.DOMSLO,_slu,"@.SLO.TR",1,,FAKSO.DOMSLO)
                  ?};
            _ktr:=FAKSO.K_OR_TR;
            _mod:=FAKSO.MODIFY;
            FAKSO.cntx_psh();
            FAKSO.use(_maska);
            FAKSO.index(_ref);
            FAKSO.prefix(_fld);
            {? FAKSO.size()
            ||
               {? _c='T'
               || _choice:=FUN.ask('Czy aktualizować dane informacji dodatkowych wg wzorca?\n\n'
                                   'Uwaga. Dla informacji wg słowników zostaną przywrócone wartości domyślne.'@);
                  _anul:=_choice;
                  _c:='N'
               |? _c='P'
               || _choice:=1
               ?}
            ||
               _choice:={? _c='T'
                        || FUN.ask('Czy aktualizować dane informacji dodatkowych wg wzorca?\n\n'
                                   'Uwaga. Dla informacji wg słowników zostaną przywrócone wartości domyślne.'@)
                        || 1
                        ?};
               _anul:=_choice;
               _c:='N'
            ?};
            {? _choice=1
            ||
               {? FAKSO.find_key(FAKSO.LP)
               ||
::                aktualizacja informacji
                  _war:=($_log)();
                  {? type_of(_war)<>1 || _war:=0 ?};
                  {? _log='' | _war
                  ||
                     FAKSO.T:=_tytul;
                     FAKSO.O:={? _or='F' || ($_opis)() |? _or='S' || _opis || _dom ?};
                     FAKSO.PRN:=_prn;
                     FAKSO.GD:=_gd;
                     FAKSO.TYPYDOK:=FAKSO.TYPYSP:=FAKSO.TYPYZAM:=null;
                     FAKSO.OR:=_or;
                     FAKSO.SLU:={? _or='W' || _slu || null() ?};
                     FAKSO.K_OR_TR:=_ktr;
                     FAKSO.MODIFY:=_mod;
                     _ok+=FAKSO.put(1)
                  ||
                     FAKSO.del()
                  ?}
               ||
::                dopisanie informacji do dokumentu
                  _war:=($_log)();
                  {? type_of(_war)<>1 || _war:=0 ?};
                  {? _log='' | _war
                  ||
                     ($('FAKSO.'+_ref))():=_fld;
                     FAKSO.O:={? _or='F' || ($_opis)() |? _or='S' || _opis || _dom ?};
                     FAKSO.PRN:=_prn;
                     FAKSO.GD:=_gd;
                     FAKSO.OR:=_or;
                     FAKSO.TYPYDOK:=FAKSO.TYPYSP:=FAKSO.TYPYZAM:=null;
                     FAKSO.SLU:={? _or='W' || _slu || null() ?};
                     FAKSO.K_OR_TR:=_ktr;
                     FAKSO.MODIFY:=_mod;
                     _ok+=FAKSO.add(1)
                  ?}
               ?}
            ?};
            FAKSO.cntx_pop();
            _anul & FAKSO.next()
         || FAKSO.next
         ?}
      !}
   ?};
   FAKSO.use(_maska);
   FAKSO.index(_ref);
   FAKSO.prefix(_fld);
   {? FAKSO.first
   ||
      {!
      |?
         {? (_faksos*(';'+$FAKSO.LP+';'))=0
         ||
            {? _c='T'
            || _choice:=FUN.ask('Czy aktualizować dane informacji dodatkowych wg wzorca?'@);
               _anul:=_choice;
               _c:='N'
            |? _c='P'
            || _choice:=1
            ?};
            {? _choice & _c<>'P'
            ||
               FAKSO.del
            ?}
         || FAKSO.next
         ?}
      !}
   ?};

   FAKSO.use(_maska);

:: uzupelnienie danych
   {? ~_ok || _ok:=exec('FindInSet','#table','FAKSO',_ref,_fld)<>null ?};
   {? _czy_sel=1
   ||
::    ta czesc niedostepna dla funkcji aktualizuj dostepnej w oknie dane dodatkowe
      {? _ok=0
      ||
::       przypadek jesli brak informacji dodatkowych dla dokumentu
         FUN.info('Dla wskazanego typu dokumentu brak informacji dodatkowych.'@)
      ||
::       wyswietlanie informacji dodatkowych
         FAKSO.index(_ref);
         FAKSO.prefix(_fld);
         {? _translat || FAKSO.win_sel('GRP') || FAKSO.win_sel('WERF') ?};
         FAKSO.win_edit('');
::       analiza meta znakow
         {? FAKSO.first()
         ||
            {! |? {? FAKSO.O * '<FL:' || exec('slownik','fakso') ?}; FAKSO.next() !}
         ?};
         _modify:=exec('isModify','fakso',_mask);
         FAKSO.first();
         FAKSO.hdr_sel();
         FAKSO.hdr_sel(_nagl);
         {? ~_modify
         || FAKSO.actions('WERF','PpUuAa:Aa');
            FAKSO.select()
         || FAKSO.actions('WERF','');
            {? var_pres('__FaXo')>0 || &__FaXo ?};
            __FaXo:=FAKSO.index('?');
            _continue:=1;
            {? _ref='FAP'
            || _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
               {? ~_r_lock || exec('who_rlock_faks','faktury_nag'); _continue:=0 ?}
            ?};
            {? _continue>0 || FAKSO.select() ?};
            {? _ref='FAP' & _continue>0 || exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock) ?};
            &__FaXo
         ?}
      ?}
   ?};
   FAKSO.cntx_pop()
|? _ref<>'' & _fld<>null & exec('FindInSet','#table','FAKSO',_ref,_fld)<>null
||
:: przypadek jesli sa informacje dla dokumentu i brak definicji informacji dodatkowych
:: oraz dla zamowien archiwalnych
   FAKSO.index(_ref);
   FAKSO.prefix(_fld);
   FAKSO.win_sel('WERF');
   FAKSO.win_edit('');
   {? _mrob=0 || FAKSO.actions('WERF','puA:A') ?};
   FAKSO.hdr_sel();
   FAKSO.hdr_sel(_nagl);
   _continue:=1;
   {? _ref='FAP'
   || _r_lock:=exec('r_lock_one','#table',FAKS,FAKS.ref);
      {? ~_r_lock || exec('who_rlock_faks','faktury_nag'); _continue:=0 ?}
   ?};
   {? _continue>0 || FAKSO.select() ?};
   {? _ref='FAP' & _continue>0 || exec('r_unlock_one','#table',FAKS,FAKS.ref(),_r_lock) ?};
   {? _mrob=0 || FAKSO.actions('WERF') ?}
||
:: przypadek jesli brak definicji informacji dodatkowych i brak informacji dodatkowych dla dok.
:: wyswietlane tylko podczas wywolania funkcji z okienka

::   {? (~(';PDFIR;'*(1+menu_pth())>1) & (menu_pth()+1='T' | menu_pth()+1='I'))
::    | (menu_pth()='DOI' | menu_pth()='PT')
::    & var_pres('__winbad')<1
   {? _menuakc & var_pres('__winbad')<1
   || {? _mask='dokma' & _ind=''
      || DK.cntx_psh();
         _poz:={? DK.ZP & DK.seek(DK.ZP,) || DK.P || DK.P ?};
         DK.cntx_pop();
         FUN.info('Pozycja powstała na podstawie pozycji '@+$_poz+'.\nbrak informacji dodatkowych.'@)
      || FUN.info('Dla wskazanego typu dokumentu brak definicji\ndotyczących informacji dodatkowych.'@)
      ?}
   ?}
?};
FAKSO.cntx_pop();
{? _blknag>0 || ($_unblk)() ?};
''


\spr_defo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: sprawdza czy istnieje pozycja w definicji opisów
::   WE: _a - akronim indeksu
::       _b - ref typu
::   WY: _wyn - 1-jest 0-nie ma
::  OLD: \spr_defo/autofakso.fml
::----------------------------------------------------------------------------------------------------------------------
_mask:=FAKSO.name();
FAKSO.cntx_psh();
FAKSO.use('faktodef');
_wyn:=exec('FindInSet','#table','FAKSO',_a,_b)<>null;
FAKSO.use(_mask);
FAKSO.cntx_pop();
_wyn


\slownik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: obsługa słowników
::       <F3:_tab>
::       <SL:_nazwa SLU>
::       <FL:_tab.pole>
::   WY: wybrana wartość
::  OLD: \slownik/autofakso.fml
::----------------------------------------------------------------------------------------------------------------------
_ch1:='<F3:';  _ch2:='<SL:';  _ch4:='<FL:';  _ch3:='>';
{!
|? _opi:=FAKSO.O;
   _ws:=0; _ws1:=_opi*_ch1; _ws2:=_opi*_ch2; _ws3:=_opi*_ch4; _wyn:='';
   {? _ws1 | _ws2 | _ws3
   ||
::    zastosowano w opisie odwołanie do słowników
      _ws:={? ~_ws1 & ~_ws2 || _ws3
           |? ~_ws1 & ~_ws3 || _ws2
           |? ~_ws3 & ~_ws2 || _ws1
           |? ~_ws1 || {? _ws2<=_ws3 || _ws2 || _ws3 ?}
           |? ~_ws2 || {? _ws1<=_ws3 || _ws1 || _ws3 ?}
           |? ~_ws3 || {? _ws2<=_ws1 || _ws2 || _ws1 ?}
           |? _ws1<=_ws2 & _ws1<=_ws3 || _ws1
           |? _ws2<=_ws1 & _ws2<=_ws3 || _ws2
           |? _ws3<=_ws1 & _ws3<=_ws2 || _ws3
           ?};
      _wsk:=((_ws+3)-_opi)*_ch3;
      _tab:=(_wsk+((_ws+3)-_opi))-1;
      {? _ws=_ws2
      || SLO.index('SL');
         SLO.prefix(exec('FindInSet','#table','SLU','NAZ',_tab));
         {? SLO.first()
         || SLO.win_sel('ONE_SEL');
            {? SLO.select() || _wyn:=SLO.TR ?}
         ?}
      |? _ws=_ws1
      || _wyn:=exec('sel_tab','fakso',_tab)
      |? _ws=_ws3
      || _wyn:=form(($_tab)())
      ?};
      {? _wyn<>''
      || FAKSO.O:=((_ws-1)+FAKSO.O)+_wyn+((_ws+3+_wsk)-FAKSO.O); FAKSO.put(1)
      ?}
   ?};
   _ws & _wyn<>''
!};
_wyn


\sel_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: select() na podanej tabeli - 2 pierwsze pola stringowe
::   WE: _a - akronim tabeli
::   WY: wybrana wartosc
::  OLD: \sel_tab/autofakso.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{| ($_a)()
|! _fln:=fld_num();
   _acr_sel:=mk_sel('Wybierz pozycję'@,'T',0,'czy_jest_acrsel');
   _i:=0; _ile:=0; _fld:='';
   {!
   |? _i+=1;
      {? type_of(($(_a+'.'+fld_acr(_i)))())=2
      || _ile+=1;
         {? _fld='' || _fld:=_a+'.'+fld_acr(_i) ?};
         win_fld(_acr_sel,,fld_acr(_i),,,20,,1,fld_name(_i))
      ?};
      _ile<2 & _i<_fln
   !};
   win_act(_acr_sel,0,'Formuła','Wybierz'@@,,'Wybór bieżącego zapisu'@,"sel_exit()",,1);
   win_sel(_acr_sel);
   prefix();
   {? first() & select() || _wyn:=($_fld)() ?}
|};
_wyn


\pobgdnag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: pobiera i porzadkuje dane naglowkowe
::   WE: _a - typ naglowka F-faktura M-magazyn Z-zamowienie D-dostawa
::       _b - ref naglowka
::   WY: 1
::  OLD: \pobgdnag/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('M_GD','S_GD');
M_GD:=tab_tmp(3
   ,'GD','STRING[1]'  ,''
   ,'LK','INTEGER'  ,''
   ,'PZ','INTEGER'    ,''
   ,'OP','STRING[255]','');
S_GD:=tab_tmp(2
   ,'GD','STRING[1]',''
   ,'LK','INTEGER'  ,'');
_ind:={? _a='F' || 'FAKS' |? _a='M' || 'ND' |? _a='Z' || 'ZK_N' |? _a='D' || 'ZD_NAG' ?};
FAKSO.cntx_psh();
{? _a='F' || FAKSO.use('fakto' + (FAKS.name() + 3))
|? _a='M' || FAKSO.use('fakto' + (ND.name() + 3))
|? _a='Z' || FAKSO.use('fakto' + (ZK_N.name() + 3))
|? _a='D' || FAKSO.use('fakto' + (ZD_NAG.name() + 3))
?};
FAKSO.index(_ind);
FAKSO.prefix(_b);
{? FAKSO.first()
||
   _i:=0;
   {!
   |? {? 'ATA'*FAKSO.PRN
      ||
         _i+=1;
         M_GD.GD:=1 + FAKSO.GD; M_GD.PZ:=_i; M_GD.LK:=#(FAKSO.GD + 1);
         M_GD.OP:=exec('printFAKSO','fakso',PARAM_W.JEZYK);
         M_GD.add(1);
         {? ~S_GD.find_key(M_GD.GD,M_GD.LK)
         ||
            S_GD.GD:=M_GD.GD; S_GD.LK:=M_GD.LK; S_GD.add(1)
         ?}
      ?};
      FAKSO.next()
   !}
?};
FAKSO.cntx_pop();
1


\delprzyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: Usuwa tabelki wg przypisan
::   WE: _a - akronim bazy
::       _b - akronim pola do sprawdzenia
::       _c - ref do prefiksu
::  OLD: \delprzyp/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
{| ($_a)()
|! cntx_psh();
   index(_b);
   prefix(_c);
   {? first()
   || {!
      |? {? exec('sprprzyp','fakso',_a,ref)<2
         || del()
         || _ref:=ref(); _ok:=next();
            cntx_psh();
            prefix();
            {? seek(_ref) || ($(_a+'.'+_b))():=null; put(1) ?};
            cntx_pop();
            _ok
         ?}
      !}
   ?};
   cntx_pop()
|}


\sprprzyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: Sprawdza ilosc przypisan do roznych pol
::   WE: _a - akronim bazy
::       _b - ref rekordu
::   WY: ilosc przypisan
::  OLD: \sprprzyp/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{| ($_a)()
|! _n:=fld_num();
   {! _i.._n
   |! _acr:=fld_acr(_i);
      {? '*DK *ND *ZD_POZ *ZD_NAG *ZK_N *ZK_P *FAKS *FAP'*('*'+_acr) || _wyn+=($(_a+'.'+_acr))()<>null ?}
   !}
|};
_wyn


\dispWithFakso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: okienko do wyświetlania z tabelką FAKSO
::   WE: _a - akronim tabeli
::----------------------------------------------------------------------------------------------------------------------
_tab:=($_a);
_small:=-~_a;
_fakso_view:=0;
_dokum_view:=0;
{? _tab()=ZK_N | (_tab()=ZK_P & ZK_P.N)
|| _dokum:=exec('bl_dokum_seek','zbl',ZK_N) | exec('dokum_seek','zbl',ZK_N);
   _dokum_view:=_dokum & DOKUM.DOKUMI
|? _tab()=ZD_NAG | (_tab()=ZD_POZ & ZD_POZ.ZD_NAG)
|| _dokum:=exec('dokum_seek','zbl',ZD_NAG);
   _dokum_view:=_dokum & DOKUM.DOKUMI
?};

FAKSO.cntx_psh();
{| _tab()
|! DPOZ.DISPLAY:=1;
   _win_edit:=win_edit('?');
   @.FAKSO.cntx_psh();
   @.FAKSO.use('fakto'+(ref_name(ref())+3));
   @.FAKSO.prefix();
   _fakso_view:=(exec('FindInSet','#table','FAKSO',_a,ref())<>null());
   {? _fakso_view
   || _actions:=@.FAKSO.actions('WERF','puLA:A');
      _win_grp:=grp_make('',,'disp_grp_'+_small,,,,,'html_maximized');
      grp_edit(_win_grp,,_win_edit,'Pozycja'@,,,,,'maximized',,1);
      {? _a='DK' & 0
      || {? exec('FindInSet','#table','FAKSO',_a,ref())<>null()
         || grp_sel(_win_grp,FAKSO,'WERF','Informacje dodatkowe'@,,,,23,,,,,'maximized','infdoda_'+_small)
         ?};
         exec('his_dost','magdok_wspolne',,1);
         grp_sel(_win_grp,__hist,__hist.win_sel('?'),'Historia dostaw'@,,,,23,,,,,'maximized','histdost_'+_small)
      || grp_sel(_win_grp,FAKSO,'WERF','Informacje dodatkowe'@,,,,23,,,,,'maximized','infdoda_'+_small)
      ?};
     {? _dokum_view
     || grp_edit(_win_grp,DOKUM,'DOKUMI','Podgląd'@,,,,,'maximized')
     ?};
      win_sel(_win_grp);
      @.FAKSO.index(_a);
      @.FAKSO.prefix(ref());
      @.FAKSO.first();
      select();
      @.FAKSO.actions('WERF',_actions);
      win_sel(''); win_del(_win_grp);
      VAR_DEL.delete('__hist','__hsel','__katalog','__hprdk')
   || {? _dokum_view & _tab()=ZK_N
      || _win_red:=exec('zknag_win_edit','zamsiw_nag');
         win_edit(_win_red)
      |? _dokum_view & _tab()=ZK_P
      || _win_red:=exec('zkp_win_edit','zamsiw_poz',_win_edit);
         win_edit(_win_red)
      |? _dokum_view & _tab()=.ZD_NAG
      || _win_red:=exec('zdnag_win_edit','zamdst_nag','RED');
         win_edit(_win_red)
      |? _dokum_view & _tab()=ZD_POZ
      || _win_red:=exec('zdp_win_edit','zamdst_poz',_win_edit);
         win_edit(_win_red)
      || _win_red:=_win_edit
      ?};
      display();
      win_edit(_win_edit);
      {? _win_red<>_win_edit
      || win_edel(_win_red)
      ?}
   ?};
   @.FAKSO.cntx_pop();
   DPOZ.DISPLAY:=0
|};
FAKSO.cntx_pop();
~~


\infdodchk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [12.30]
:: OPIS: sprawdza istnienie definicji informacji dodatkowych dla kontekstu tabeli
::       zalozenie ze rekord dla ktorego sa analizowane informacje dodatkowe ma otwarta prawidlowa maske
::   WE: [_a] - maska tabeli dla ktorej wolana jest funkcja
::   WY: 0 - nie ma definicji; 1 - jest definicja
::  OLD: \infdodchk/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=2 || {? type_of(_a)<>2 || _a:='' ?} || _a:='' ?};

_ok:=0;

BEER.MSK:={? _a='' || {? BEER.MSK='' || 5+cur_tab(1).name() || BEER.MSK ?} || _a ?};
_mask:=BEER.MSK;

_ind:='';
_pol:='';
_ref:='';

{? _mask='dokma'
|| _ind:={? ~DK.ZP || 'TYM' || '' ?};
   _pol:='DK.N().TYP';
   _ref:='DK'
|? _mask='nagdo'
|| _ind:='TYM';
   _pol:='ND.TYP';
   _ref:='ND'
|? _mask='fakpo'
|| _ind:='TYP';
   _pol:='FAP.FAKS().T';
   _ref:='FAP'
|? _mask='faktu'
|| _ind:='TYP';
   _pol:='FAKS.T';
   _ref:='FAKS'
|? _mask='zkpoz'
|| _ind:='TYZ';
   _pol:='ZK_P.N().T';
   _ref:='ZK_P'
|? _mask='zknag'
|| _ind:='TYZ';
   _pol:='ZK_N.T';
   _ref:='ZK_N'
|? _mask='zdpoz'
|| _ind:='TYZ';
   _pol:='ZD_POZ.ZD_NAG().T';
   _ref:='ZD_POZ'
|? _mask='zdnag'
|| _ind:='TYZ';
   _pol:='ZD_NAG.T';
   _ref:='ZD_NAG'
?};

{? _ind<>'' & exec('spr_defo','fakso',_ind,($_pol)())<>null
||
  _ok:=1
?};
_ok


\aktfakso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed aktualizuj okna WERF tabeli FAKSO
::   WE: _a - czy akcja popraw dlaktualizacji = domyslnie 'N'
::  OLD: \aktfakso/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>2 ||_a:='N' ?} || _a:='N' ?};
_ndx:=FAKSO.index('?');
{? ~(_ndx='DK' | _ndx='ND' | _ndx='FAP' | _ndx='FAKS' | _ndx='ZK_P' |
    _ndx='ZK_N' | _ndx='ZD_POZ' | _ndx='ZD_NAG') & var_pres('__FaXo')>0
    & type_of(__FaXo)=2 & __FaXo<>''
|| _ndx:=__FaXo
?};
_mask:={? _ndx='DK'     || 'dokma'
       |? _ndx='ND'     || 'nagdo'
       |? _ndx='FAP'    || 'fakpo'
       |? _ndx='FAKS'   || 'faktu'
       |? _ndx='ZK_P'   || 'zkpoz'
       |? _ndx='ZK_N'   || 'zknag'
       |? _ndx='ZD_POZ' || 'zdpoz'
       |? _ndx='ZD_NAG' || 'zdnag'
       || ''
       ?};
{? _a='N'
|| exec('inf_dod','fakso',0,_mask,'T')
|| {? FUN.ask('Wybrana informacja dodatkowa została wygenerowana na podstawie zdefiniowanej formuły.\n'
       'Niemożliwe poprawienie informacji dodatkowej. Informacje można zaktualizować.\n\n'
       'Czy aktualizować dane informacji dodatkowych wg wzorca?'@)
   || exec('inf_dod','fakso',0,_mask,'P',FAKSO.LP)
   ?}
?}


\edifakso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed popraw okna WERF tabeli FAKSO
::   WY: 1-redagowanie dozwolone, 0-wpp
::  OLD: \edifakso/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAKSO.OR='W'
|| _kod:=FAKSO.K_OR_TR=0;
   SLU.cntx_psh();
   SLO.cntx_psh();
   SLO.index({? _kod || 'SL' || 'SL_TR' ?});
   SLO.prefix(FAKSO.SLU);
   {? FAKSO.O<>'' || {? ~SLO.find_key(FAKSO.O,) || SLO.find_key(FAKSO.O) ?} ?};
   FAKSO.SLU().NAZ;
   SLO.win_sel('SLO_FO');
   {? SLO.select(,1)
   || FAKSO.O:={? _kod || SLO.KOD || SLO.TR ?};
      FAKSO.put(1);
      FAKSO.get()
   ?};
   SLU.cntx_pop();
   SLO.cntx_pop();
   0
|? exec('slownik','fakso')<>''
|| 0
|? FAKSO.OR='S'
|| 1
|| exec('aktfakso','fakso','T');
   0
?}


\popfakso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: po popraw okna WERF tabeli FAKSO
::   WY: 1
::  OLD: \popfakso/autfakso.fml
::----------------------------------------------------------------------------------------------------------------------
1


\after_or
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: po zmianie typu wartości
::----------------------------------------------------------------------------------------------------------------------
{? FAKSO.OR='W'
|| _slu:='mark=1';
   _orw:='enable=1';
   _orn:='enable=0';
   FAKSO.O:='';
   POM.DOMSLO:=exec('FindInSet','#table','SLO','SL',FAKSO.DOMSLO,FAKSO.SLU,"SLO.TR",1,,'')
|| _slu:='mark=0';
   _orw:='enable=0';
   _orn:='enable=1';
   FAKSO.SLU:=null();
   FAKSO.DOMSLO:='';
   POM.DOMSLO:='';
   FAKSO.K_OR_TR:=0
?};
FAKSO.efld_opt('RED',_slu,,'SLU');
FAKSO.efld_opt('RED',_orw,,'SLU');
FAKSO.efld_opt('RED',_orw,,'DOMSLO');
FAKSO.efld_opt('RED',_orw,POM,'DOMSLO');
FAKSO.efld_opt('RED',_orw,,'K_OR_TR');
FAKSO.efld_opt('RED',_orn,,'O');
~~


\pr_domslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: przed ustawieniem domyślnej wartości ze słownika
::----------------------------------------------------------------------------------------------------------------------
FAKSO.SLU<>null()


\f3_domslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: klawisz F3 dla domyślnej wartości ze słownika
::----------------------------------------------------------------------------------------------------------------------
SLO.cntx_psh();
SLO.index('SL');
SLO.prefix(FAKSO.SLU);
{? FAKSO.DOMSLO<>'' || {? ~SLO.find_key(FAKSO.DOMSLO,) || SLO.find_key(FAKSO.DOMSLO) ?} ?};
SLO.win_sel('SLO_FO');
{? SLO.select(,1)
|| FAKSO.DOMSLO:=SLO.KOD;
   POM.DOMSLO:=SLO.TR
?};
SLO.cntx_pop();
~~


\po_domslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: po ustawieniu domyślnej wartości ze słownika
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
{? FAKSO.DOMSLO<>'' & FAKSO.SLU<>null()
|| SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(FAKSO.SLU,FAKSO.DOMSLO,);
   {? ~SLO.first()
   || SLO.prefix(FAKSO.SLU,FAKSO.DOMSLO);
      {? SLO.first()
      || FAKSO.DOMSLO:=SLO.KOD;
         POM.DOMSLO:=SLO.TR
      || FUN.info('Brak podanej wartości w słowniku %1'@[FAKSO.SLU().NAZ]);
         _res:=0
      ?}
   || POM.DOMSLO:=SLO.TR
   ?};
   SLO.cntx_pop()
?};
_res


\po_slufakso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: po ustawieniu/zmianie słownika dla FAKSO
::----------------------------------------------------------------------------------------------------------------------
{? FAKSO.SLU=null() | (FAKSO.DOMSLO<>'' & ~exec('FindInSet','#table','SLO','SL',FAKSO.DOMSLO,FAKSO.SLU,,1))
|| FAKSO.DOMSLO:='';
   POM.DOMSLO:='';
   win_disp()
|? FAKSO.DOMSLO<>''
|| POM.DOMSLO:=exec('FindInSet','#table','SLO','SL',FAKSO.DOMSLO,FAKSO.SLU,"SLO.TR",1,,'');
   win_disp()
?};
1


\isModify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: sprawdzenie czy choć jedna z informacji dodatkowych podlega edycji po akceptacji
::   WE: [_a] - maska tabeli dla której wołana jest funkcja

::   WY: 1-tak, 0-nie
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of('') || _a || _a:='' ?};
_mask:={? _a='' || {? BEER.MSK='' || 5+cur_tab(1).name() || BEER.MSK ?} || _a ?};

_res:=0;
{? _mask='dokma'
|| _ind:={? ~DK.ZP || 'TYM' || '' ?};
   _pol:='DK.N().TYP';
   _ref:='DK';
   _typ:='P';
   _maska:='fakto'+(DK.name+3);
   _stat:=DK.N().STAT_REJ
|? _mask='nagdo'
|| _ind:='TYM';
   _pol:='ND.TYP';
   _ref:='ND';
   _typ:='N';
   _maska:='fakto'+(ND.name+3);
   _stat:=ND.STAT_REJ
|? _mask='fakpo'
|| _ind:='TYP';
   _pol:='FAP.FAKS().T';
   _ref:='FAP';
   _typ:='P';
   _maska:='fakto'+(FAP.name+3);
   _stat:=FAP.FAKS().STAT_REJ
|? _mask='faktu'
|| _ind:='TYP';
   _pol:='FAKS.T';
   _ref:='FAKS';
   _typ:='N';
   _maska:='fakto'+(FAKS.name+3);
   _stat:=FAKS.STAT_REJ
|? _mask='zkpoz'
|| _ind:='TYZ';
   _pol:='ZK_P.N().T';
   _ref:='ZK_P';
   _typ:='P';
   _mrob:=ZK_P.name()+2='__';
   _maska:='fakto'+(ZK_P.name+3);
   _stat:=ZK_P.N().STAT_REJ
|? _mask='zknag'
|| _ind:='TYZ';
   _pol:='ZK_N.T';
   _ref:='ZK_N';
   _typ:='N';
   _mrob:=ZK_N.name()+2='__';
   _maska:='fakto'+(ZK_N.name+3);
   _stat:=ZK_N.STAT_REJ
|? _mask='zdpoz'
|| _ind:='TYZ';
   _pol:='ZD_POZ.ZD_NAG().T';
   _ref:='ZD_POZ';
   _typ:='P';
   _mrob:=ZD_POZ.name()+2='__';
   _maska:='fakto'+(ZD_POZ.name+3);
   _stat:=ZD_POZ.ZD_NAG().STAT_REJ
|? _mask='zdnag'
|| _ind:='TYZ';
   _pol:='ZD_NAG.T';
   _ref:='ZD_NAG';
   _typ:='N';
   _mrob:=ZD_NAG.name()+2='__';
   _maska:='fakto'+(ZD_NAG.name+3);
   _stat:=ZD_NAG.STAT_REJ
?};

_fld:={? _ref<>'' || ($(_ref+'.ref'))() || null ?};
{? _stat='N'
|| _res:=1
|? _stat='A'
|| _res:=0
|? _fld<>null()
|| FAKSO.cntx_psh();
   FAKSO.use(_maska);
   FAKSO.index(_ref);
   FAKSO.prefix(_fld);
   {? FAKSO.first()
   || {!
      |? _res:=FAKSO.MODIFY='T';
         ~_res & FAKSO.next()
      !}
   ?};
   FAKSO.cntx_pop()
?};
_res


\rek_infd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: akcja na rekord dla informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
_act:='';
{? ~exec('oneModify','fakso') || _act:='PUA' ?};
{? FAKSO.OR<>'S' || _act+='T' ?};
FAKSO.actions_grayed('WERF',_act);
FAKSO.actions_grayed('WERT',{? FAKSO.OR<>'S' || 'T' || '' ?});
{? FAKSO.MODIFY='T' || 'INFD#01#02'
|? FAKSO.MW='N' || 'INFD#01#01'
|| ''
?}


\oneModify
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: sprawdzenie czy dana pozycja informacji dodatkowych podlega edycji
::   WY: 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_res:=0;
{? FAKSO.MODIFY='N'
|| {? ref_name(FAKSO.FAKS)<>'' & FAKSO.FAKS().STAT_REJ='N' || _res:=1
   |? ref_name(FAKSO.FAP)<>'' & FAKSO.FAP().FAKS().STAT_REJ='N' || _res:=1
   |? ref_name(FAKSO.ND)<>'' & FAKSO.ND().STAT_REJ='N' || _res:=1
   |? ref_name(FAKSO.DK)<>'' & FAKSO.DK().N().STAT_REJ='N' || _res:=1
   |? ref_name(FAKSO.ZK_N)<>'' & FAKSO.ZK_N().STAT_REJ='N' || _res:=1
   |? ref_name(FAKSO.ZK_P)<>'' & FAKSO.ZK_P().N().STAT_REJ='N' || _res:=1
   |? ref_name(FAKSO.ZD_NAG)<>'' & FAKSO.ZD_NAG().STAT_REJ='N' || _res:=1
   |? ref_name(FAKSO.ZD_POZ)<>'' & FAKSO.ZD_POZ().ZD_NAG().STAT_REJ='N' || _res:=1
   ?}
|| _res:=1
?};
_res


\uid_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: zwraca uidrefa z definicji FAKSO
::   WE: _a - Kod informacji
::       _b - miejsce wystąpienia
::       _c - uidref tabeli
::   WY: uidref z definicji lub 'N'
::----------------------------------------------------------------------------------------------------------------------
_uidtab:={? var_pres('_c')=type_of('') & (+_c)=48 || _c || '' ?};

_res:='N';

{? _uidtab<>''
|| _sql:=_uidtab+16;
   _tab:=ref_tab(_sql);
   _fld:={? _tab=TYPYDOK || '$FAKSO.TYPYDOK'
         |? _tab=TYPYSP  || '$FAKSO.TYPYSP'
         |? _tab=TYPYZAM || '$FAKSO.TYPYZAM'
         || 'xxx'
         ?};
   FAKSO.cntx_psh();
   FAKSO.use('faktodef');
   FAKSO.index('TUID');
   FAKSO.prefix(_a,_b);
   {? FAKSO.first()
   || {!
      |? {? ($_fld)()=_sql || _res:=FAKSO.uidref() ?};
         _res='N' & FAKSO.next()
      !}
   ?};
   FAKSO.cntx_pop()
?};
_res


\po_fakso_sel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: wyswietlenie tłumaczeń dla Informacji dodatkowych
::----------------------------------------------------------------------------------------------------------------------
BEER.UID_TM:=FAKSO.uidref();
BEER.UID_TL:=exec('uid_def','fakso',FAKSO.T,FAKSO.MW,BEER.UID_TAB);

TRANSLAT.index('UID');
TRANSLAT.prefix(BEER.UID_TL);
grp_disp(TRANSLAT,'WERUIDM');
~~


\printFAKSO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: wydruk danej pozycji informacji dodatkowej
::   WE: [_a] - język, domyślnie brak - po polsku
::   WY: informacja dodatkowa
::----------------------------------------------------------------------------------------------------------------------
_lang:={? var_pres('_a')=type_of(null()) || _a || null() ?};
_res:='';
_res:={? FAKSO.PRN='T' || FAKSO.O |? FAKSO.PRN='A' || FAKSO.T+' '+FAKSO.O || '' ?};
FAKS.cntx_psh();FAP.cntx_psh();ND.cntx_psh();DK.cntx_psh();TYPYDOK.cntx_psh();TYPYSP.cntx_psh();ZK_N.cntx_psh();
TYPYZAM.cntx_psh();ZK_P.cntx_psh();ZD_NAG.cntx_psh();ZD_POZ.cntx_psh();

{? _lang<>null()
|| {? (';TA'*FAKSO.PRN)>1
   || BEER.UID_TAB:={? FAKSO.FAKS<>null()   || FAKSO.FAKS().T().uidref()
                    |? FAKSO.FAP<>null()    || FAKSO.FAP().FAKS().T().uidref()
                    |? FAKSO.ND<>null()     || FAKSO.ND().TYP().uidref()
                    |? FAKSO.DK<>null()     || FAKSO.DK().N().TYP().uidref()
                    |? FAKSO.ZK_N<>null()   || FAKSO.ZK_N().T().uidref()
                    |? FAKSO.ZK_P<>null()   || FAKSO.ZK_P().N().T().uidref()
                    |? FAKSO.ZD_NAG<>null() || FAKSO.ZD_NAG().T().uidref()
                    |? FAKSO.ZD_POZ<>null() || FAKSO.ZD_POZ().ZD_NAG().T().uidref()
                    || ''
                    ?};
      BEER.UID_TM:=FAKSO.uidref();
      BEER.UID_TL:=exec('uid_def','fakso',FAKSO.T,FAKSO.MW,BEER.UID_TAB);

      TRANSLAT.index('UID');
      TRANSLAT.prefix(BEER.UID_TL,_lang);
      {? TRANSLAT.first()
      || exec('pw_tresc','tlumaczenia');
         _res:={? FAKSO.PRN='T' || FAKSZ.TRESC |? FAKSO.PRN='A' || FAKSZ.TRESC2+' '+FAKSZ.TRESC || '' ?}
      ?}
   ?}
?};
FAKS.cntx_pop();FAP.cntx_pop();ND.cntx_pop();DK.cntx_pop();TYPYDOK.cntx_pop();TYPYSP.cntx_pop();ZK_N.cntx_pop();
TYPYZAM.cntx_pop();ZK_P.cntx_pop();ZD_NAG.cntx_pop();ZD_POZ.cntx_pop();
_res

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 0422f361ebb3df69f8d1d980dac614f63023bcab77e7a1ad2f79a275038365f355399d09d7e5ec5960f1877321357f824e6740e55df6a69030137a0a72dd59ebf171d28a204e5b63762cbaeb4be3a393002b5613c0708c8243e2a38fe425b479071d41ae15f772a6762c2eb5de0abab2aba634502914e31be208895e22ff273b

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: magdok_wydruk.fml
:: Utworzony: 16.11.2015
:: Autor: AK
::======================================================================================================================
:: Zawartość: Obsługa wydruku dokumentów magazynowych
::======================================================================================================================


\dokm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: wydruk dokumentu magazynowego
::  OLD: \dokm/wydruki.fml
::----------------------------------------------------------------------------------------------------------------------
ND.cntx_psh();
dokl_il:=dokl_ilk:=dokl_c:=0;
prod:=exec('tte_lic','tte');
VAR_DEL.delete('X_Xw1');
X_Xw1:=tab_tmp(1,
   'GR'     ,'STRING[250]'    ,'Klucz grupowania',
   'KO'     ,'STRING[250]'    ,'Klucz sortowania',
   'P'      ,'INTEGER'        ,'Pozycja',
   'M'      ,'STRING[160]'    ,'Materiał',
   'JM'     ,'STRING[10]'     ,'jm',
   'IL'     ,'REAL'           ,'Ilość',
   'ILK'    ,'REAL'           ,'Ilość',
   'C'      ,'REAL'           ,'Cena',
   'WAR'    ,'REAL'           ,'Wartość',
   'DK_C'   ,'STRING[255]'    ,'Cecha',
   'LOK'    ,'STRING[20]'     ,'Lokalizacja',
   'TW'     ,'DATE'           ,'Termin ważności',
   'SKL'    ,'STRING[1]'      ,'Składowa (T/N)',
   'ZP'     ,'REAL'           ,'Z pozycji',
   'ZL'     ,'STRING[20]'     ,'Zlecenie',
   'KK'     ,'STRING[35]'     ,'Konto Księgowe',
   'REF'    ,'STRING[16]'     ,'$DK.ref',
   'BAD'    ,'STRING[16]'     ,'$BADH.ref',
   'J2'     ,'STRING[10]'     ,'jm2',
   'IL2'     ,'REAL'           ,'Ilość 2');
X_Xw1.index(X_Xw1.ndx_tmp(,,'GR',,,'GR',,));
_zp:=0;

{? (czy_eanl=1 | czy_tw=1 | czy_war=3) & ND.TYP().P='N'
|| _zp:=1;
   DK.index('DOKZP');
   DK.prefix(ND.ref,0)
|| DK.index('DOKMAG');
   DK.prefix(ND.ref)
?};

:: teraz typy dokumentow z fo ::
VAR_DEL.delete('MrDOKS','dlkk');
MrDOKS:=_mrdoks:=exec('get','#params',500707,2,null());

"--zmienna wykorzystywana do szacowania szerokosci kolumny wartosc--";
_war:=0; dlkk:=0;
{? DK.first
|| DK_L.cntx_psh;
   X_Xw1.clear();
   {!
   |? _ewi:=DK.M().J2<>null();
      _js:={? _ewi || exec('czyJS','jm',DK.M) || 1 ?};
      _ileP:={? ~_zp | ~_ewi || DK.IL  || exec('obl_wgzp','magdok_wspolne',DK.N,#DK.ref(),DK.IL) ?};
      _il2P:={? ~_zp | ~_ewi || DK.IL2 || exec('obl_wgzp','magdok_wspolne',DK.N,#DK.ref(),DK.IL2,1) ?};
      _wnP:={? ~_zp | ~_ewi || DK.WN || exec('obl_wgzp','magdok_wspolne',DK.N,#DK.ref(),DK.WN,2) ?};
      _wbP:={? ~_zp | ~_ewi || DK.WB || exec('obl_wgzp','magdok_wspolne',DK.N,#DK.ref(),DK.WB,3) ?};

      _pop:=((+form((frac({? czy_war=3 & _js || _il2P || _ileP ?}))))-2);
      {? _pop>0 & _pop>dokl_il || dokl_il:=_pop ?};
      {? j2='T' & DK.M().J2<>null()
      || _pop:=((+form((frac(_il2P))))-2);
         {? _pop>0 & _pop>dokl_il || dokl_il:=_pop ?}
      ?};
      cechy+={? czy_cechy=1 || DK.DK_C<>null || 0 ?};
      zl+={? czy_zl=1 || DK.ZL<>null || 0 ?};

      _kkoszt:={? DK.KK<>null() || DK.KK().SYM |? DK.N().KK<>null() || DK.N().KK().SYM || '' ?};
      dlkk+={? __czykk=1 || {? +_kkoszt>dlkk || dlkk:=+_kkoszt ?} || 0 ?};

      _gro:={? czy_gr=1 || form(DK.M().KTM)+form({? czy_war=3 || DK.J2().KOD || DK.JM().KOD ?}) || '' ?};
      _il:={? czy_war=3 & _js || _il2P || _ileP ?};
      _gro+={? czy_gr=1 & czy_cechy=1 || form(exec('sym_dk_c','mat_atr',DK.DK_C)) || '' ?};
      _gro+={? czy_gr=1 & czy_zl=1 || form(DK.ZL().SYM) || '' ?};
      _gro+={? czy_gr=1 & czy_war>1
            || form({? czy_war=2 || DK.C || {? _il>0 || {? ND.CB='T' || _wbP/_il || _wnP/_il ?} ?} ?},,DK.M().DOKL_S)
            || ''
            ?};
      _gro+={?  czy_gr=1 & __czykk & _kkoszt<>'' || _kkoszt || '' ?};
      _gro+={? czy_gr=1 & czy_par=1 || exec('zwr_BADH','statexam') || '' ?};
      DK_L.index('DK'); DK_L.prefix(DK.ref); _is_dk_l:=DK_L.first;
      {!
      |? {? czy_eanl=1 & _is_dk_l=1 || eanl+=DK_L.LOK<>null ?};
         {? czy_tw=1 & _is_dk_l=1 || tw+=DK_L.TW<>date(0,0,0)
         |? czy_tw=1 || tw+=DK.TW<>date(0,0,0)
         ?};
         _gr:=_gro+{? czy_gr=1 & czy_eanl=1 & _is_dk_l=1 || DK_L.LOK().KOD || '' ?};
         _gr+={? czy_gr=1 & czy_tw=1 & _is_dk_l=1 || form(DK_L.TW) |? czy_gr=1 & czy_tw=1 || form(DK.TW) || '' ?};
         _gr+={? ~czy_gr & DK.PLUS='T'
              || DK.PRDK+{? czy_tw=1 & _is_dk_l=1 || form(DK_L.TW) |? czy_tw || form(DK.TW) || '' ?}
              || ''
              ?};
         _ko:=
            {? kolejno=1 || form(DK.P,-20)
            |? kolejno=2 || form(exec('sym_dk_c','mat_atr',DK.DK_C))
            |? kolejno=3 || {? czy_eanl=1 & _is_dk_l=1 || DK_L.LOK().KOD || '' ?}
            |? kolejno=4 || DK.ZL().SYM
            |? kolejno=5 || {? czy_tw=1 & _is_dk_l=1 || form(DK_L.TW) || form(date(0,0,0)) ?}
            || ''
            ?};
         _ko:={? czy_gr=1 || _ko+_gr || _ko ?};
         {? _is_dk_l
         || _pop:=((+form((frac(fabs(_ileP)))))-2);
            {? _pop>0 & _pop>dokl_il || dokl_il:=_pop ?};
            {? j2='T' & DK_L.M().J2<>null()
            || _pop:=((+form((frac(_il2P))))-2);
               {? _pop>0 & _pop>dokl_il || dokl_il:=_pop ?}
            ?}
         ?};
         {? _gr<>'' & X_Xw1.find_key(_gr,_gr)
         ||
            X_Xw1.IL+={? czy_war=3 & _js
                      || _il2P
                      || {? (czy_eanl=1 | czy_tw=1) & _is_dk_l=1
                         || {? DK.N().Z='T' & DK.N().TYP().P='N' || -DK_L.IL || DK_L.IL ?}
                         || _ileP
                         ?}
                      ?};
            X_Xw1.IL2+={? X_Xw1.J2<>''
                       || {? (czy_eanl=1 | czy_tw=1) & _is_dk_l=1
                          || {? DK.N().Z='T' & DK.N().TYP().P='N' || -DK_L.IL2 || DK_L.IL2 ?}
                          || {? czy_war=3 & _js || _ileP || _il2P ?}
                          ?}
                       || 0
                       ?};
            X_Xw1.ILK+=0;
            X_Xw1.WAR+={? czy_war=2 || DK.WAR || {? ND.CB='T' || _wbP || _wnP ?} ?};
            _war+=X_Xw1.WAR;
            X_Xw1.put(1)
         ||
            X_Xw1.blank();
            X_Xw1.GR:=_gr;
            X_Xw1.KO:=_ko;
            _tlumacz:='';
            {? PARAM_W.JEZYK<>null
            || _tlumacz:=exec('trans_m','tlumaczenia',DK.M,PARAM_W.JEZYK)
            ?};
            X_Xw1.M:=DK.M().KTM+' - '+{? _tlumacz=''||DK.M().N||_tlumacz?}+{? czy_gr=0 & czy_uwag & DK.UWAGI<>'' || ', '+DK.UWAGI || '' ?};
            _tlumacz:='';
            {? PARAM_W.JEZYK<>null
            || {? czy_war=3 & _js
               ||_tlumacz:=exec('trans_jm','tlumaczenia',DK.J2,PARAM_W.JEZYK)
               ||_tlumacz:=exec('trans_jm','tlumaczenia',DK.JM,PARAM_W.JEZYK)
               ?}
            ?};
            X_Xw1.JM:={? _tlumacz=''
                      || {? czy_war=3 & _js || DK.J2().KOD || DK.JM().KOD ?}
                      || _tlumacz
                      ?};
            X_Xw1.IL:={? czy_war=3 & _js
                      || _il2P
                      || {? (czy_eanl=1 | czy_tw=1) & _is_dk_l=1
                         || {? DK.N().Z='T' & DK.N().TYP().P='N' || -DK_L.IL || DK_L.IL ?}
                         || _ileP
                         ?}
                      ?};
            X_Xw1.J2:={? DK.M().J2<>null() || {? czy_war=3 & _js || DK.M().J().KOD || DK.M().J2().KOD ?} || '' ?};
            X_Xw1.IL2:={? X_Xw1.J2<>''
                       || {? (czy_eanl=1 | czy_tw=1) & _is_dk_l=1
                          || {? DK.N().Z='T' & DK.N().TYP().P='N' || -DK_L.IL2 || DK_L.IL2 ?}
                          || {? czy_war=3 & _js || _ileP || _il2P ?}
                          ?}
                       || 0
                       ?};
            X_Xw1.ILK:=0;
            _rabat:=1-exec('rab_proc','ceny',DK.RAB,ND.RAB,ND.RAB_TYP)/100;
            X_Xw1.C:={? czy_war=2 || DK.C || {? ND.CB='T' || DK.CB || DK.CN ?} ?};
            {? czy_war<>2 || X_Xw1.C:=X_Xw1.C*_rabat$DK.M().DOKL_S ?};
            _pop:=((+form((frac(X_Xw1.C))))-2);
            {? _pop>0 & _pop>dokl_c || dokl_c:=_pop ?};
            _pop:={? DK.M<>null() || DK.M().DOKL_C || ST.DOKL_C ?};
            {? exec('get','#params', 100235, 2)='T' & _pop>0 & _pop>dokl_c || dokl_c:=_pop ?};
            X_Xw1.WAR:={? czy_war=2 || DK.WAR || {? ND.CB='T' || _wbP || _wnP ?} ?};
            _war+=X_Xw1.WAR;
            X_Xw1.DK_C:=form(exec('sym_dk_c','mat_atr',DK.DK_C));
            X_Xw1.LOK:={? czy_eanl=1 & _is_dk_l=1 || DK_L.LOK().KOD || '' ?};
            X_Xw1.TW:={? czy_tw=1 & _is_dk_l=1 || DK_L.TW |? czy_tw=1 || DK.TW || date(0,0,0) ?};
            X_Xw1.SKL:='N';
            X_Xw1.REF:=$DK.ref();
            X_Xw1.ZP:=DK.ZP;
            X_Xw1.KK:={? DK.KK<>null() || DK.KK().SYM |? DK.N().KK<>null() || DK.N().KK().SYM || '' ?};
            X_Xw1.ZL:=DK.ZL().SYM;
            X_Xw1.BAD:=exec('zwr_BADH','statexam');
            {? X_Xw1.add(1) & czy_gr=0 & czy_eanl=0 & czy_tw=0 & DK.N().TYP().DN='K'
            || DK.cntx_psh();
               DK.index('SKLSQL'); DK.prefix($DK.ref());
               {? DK.first()
               ||
                  {!
                  |? X_Xw1.blank();
                     X_Xw1.GR:=_gr;
                     X_Xw1.KO:=_ko+'SKL';
                    _tlumacz:='';
                     {? PARAM_W.JEZYK<>null
                     || _tlumacz:=exec('trans_m','tlumaczenia',DK.M,PARAM_W.JEZYK)
                     ?};
                     X_Xw1.M:=DK.M().KTM+' - '+{? _tlumacz=''||DK.M().N||_tlumacz?}+', '+{? czy_uwag & DK.UWAGI<>'' || DK.UWAGI || '' ?};
                     _tlumacz:='';
                     {? PARAM_W.JEZYK<>null
                     || _tlumacz:=exec('trans_jm','tlumaczenia',DK.JM,PARAM_W.JEZYK)
                     ?};
                     X_Xw1.JM:={? _tlumacz=''||DK.JM().KOD||_tlumacz?};
                     X_Xw1.IL:=DK.IL;
                     _tlumacz:='';
                     {? PARAM_W.JEZYK<>null
                     || _tlumacz:=exec('trans_jm','tlumaczenia',DK.JM,PARAM_W.JEZYK)
                     ?};
                     X_Xw1.J2:={? _tlumacz=''||{? DK.M().J2<>null() || DK.J2().KOD || '' ?}||_tlumacz?};
                     X_Xw1.IL2:={? DK.M().J2<>null() || DK.IL2 || 0 ?};
                     _pop:=((+form((frac(DK.IL))))-2);
                     {? _pop>0 & _pop>dokl_il || dokl_il:=_pop ?};
                     {? j2='T' & DK.M().J2<>''
                     || _pop:=((+form((frac(DK.IL2))))-2);
                        {? _pop>0 & _pop>dokl_il || dokl_il:=_pop ?}
                     ?};
                     X_Xw1.ILK:=0;
                     X_Xw1.C:=DK.C;
                     _pop:=((+form((frac(X_Xw1.C))))-2);
                     {? _pop>0 & _pop>dokl_c || dokl_c:=_pop ?};
                     X_Xw1.WAR:=DK.WAR;
                     X_Xw1.DK_C:=form(exec('sym_dk_c','mat_atr',DK.DK_C));
                     X_Xw1.SKL:='T';
                     X_Xw1.KK:={? DK.KK<>null() || DK.KK().SYM |? DK.N().KK<>null() || DK.N().KK().SYM || '' ?};
                     X_Xw1.REF:=$DK.ref();
                     X_Xw1.BAD:=exec('zwr_BADH','statexam');
                     X_Xw1.add(1);
                     DK.next()
                  !}
               ?};
               DK.cntx_pop()
            ?}
         ?};
         (czy_eanl=1 | czy_tw=1) & DK_L.next()
      !};
      DK.next()
   !};
   DK_L.cntx_pop
?};
"--szerokosc kolumn--";
lmt:=charleft();
{? WYDRUKIL.JEZYK <> null || kk[1]:=8 || kk[1]:=5 ?};          pp[1]:='form(X_Xw1.IL,,dokl_il,\' ,\')';
pp[12]:='form(X_Xw1.IL2,,dokl_il,\' ,\')';
{? czy_war=2 | czy_war=3
|| kk[2]:=6;                              pp[2]:='form(X_Xw1.C,,dokl_c,\' ,\')';
   _szer:=+form(_war,,2,' ,');
   kk[3]:={? _szer>7 || _szer || 7 ?};    pp[3]:='form(X_Xw1.WAR,,2,\' ,\')'
?};
kk[4]:=-4;                                pp[4]:='X_Xw1.P';
kk[5]:=1;                                 pp[5]:='X_Xw1.M';
kk[6]:=4;                                 pp[6]:='X_Xw1.JM';
{? cechy
|| kk[7]:=10;                             pp[7]:='X_Xw1.DK_C'
?};
{? eanl
|| kk[8]:=11;                             pp[8]:='X_Xw1.LOK'
?};
{? kol_szer>0
|| kk[9]:=kol_szer;                       pp[9]:='kol_naz'
?};
{? zl
|| kk[10]:=10;                            pp[10]:='X_Xw1.ZL'
?};
{? tw
|| kk[11]:=10;                            pp[11]:='X_Xw1.TW'
?};

{? __czykk & dlkk
||
   {? dlkk<8 || dlkk:=8 ?};
   kk[13]:=dlkk;                            pp[13]:='X_Xw1.KK'
?};
VAR_DEL.delete('dlkk');
_len:=obj_len(kk);
_buf:=obj_new(_len); {! _i.._len |! _buf[_i]:=0 !};
_max:=obj_new(_len); {! _i.._len |! _max[_i]:=0 !};

X_Xw1.clear();
{? X_Xw1.first()
|| {!
   |? {? X_Xw1.IL<>0 | ND.TYP().DK='W'
      || {! _i.._len
         |! {? czy_war=1 & (_i=2 | _i=3) | cechy=0 & _i=7 | eanl=0 & _i=8 | kol_szer<=0 & _i=9 | zl=0 & _i=10
             | tw=0 & _i=11
            || _max[_i]:=_buf[_i]:=0
            ?};
            _war:=($pp[_i])();
            _j:={? _i=12 || 1 || _i ?};
            _typ:=type_of(_war);
            {? _typ=1 || _w:=(+form(_war));
                         {? _max[_j]<_w || _max[_j]:=_w; _buf[_j]:=_w ?}
            |? _typ=2 || _w:=(+_war);
                         {? _max[_j]<_w || _max[_j]:=_w; _buf[_j]:=_w ?}
            |? _typ=4 || _buf[_i]:=10
            |? _typ=5 || _buf[_i]:=5
            ?};
            {? _i=7 & _buf[_i]>20 || _max[_i]:=_buf[_i]:=20 ?}
         !};
         X_Xw1.next()
      || X_Xw1.del()
      ?}
   !}
?};

{! _i.._len
|! {? kk[_i]<0         || kk[_i]:=-kk[_i]
   |? kk[_i]<=_buf[_i] || kk[_i]:=_buf[_i]
   ?}
!};
obj_del(_buf); obj_del(_max);
"--definicja kolumn tabeli--";
{! _ii:=1..2
|!
   prnw:=lmt;
:: Lp
   PRN.add("ww[4]",kk[4],exec('tr','tlumaczenia','5000001'),1);
:: Lokalizacja
   {? kk[8]>0 || PRN.add("form(ww[8])",kk[8],exec('tr','tlumaczenia','5000002')) ?};
:: Termin ważności
   {? kk[11]>0 || PRN.add("form(ww[11])",kk[11],exec('tr','tlumaczenia','5000003')) ?};
:: Materiał lub usługa
   PRN.add("form(ww[5])",kk[5],exec('tr','tlumaczenia','5000004'));
:: Cecha
   {? kk[7]>0 || PRN.add("form(ww[7])",kk[7],exec('tr','tlumaczenia','5000005')) ?};
:: Zlecenie
   {? kk[10]>0 || PRN.add("form(ww[10])",kk[10],exec('tr','tlumaczenia','5000006')) ?};
:: Jednostka miary
   PRN.add("form(ww[6])",kk[6],exec('tr','tlumaczenia','5000007'));
:: Ilość
   PRN.add("form(ww[1])",kk[1],exec('tr','tlumaczenia','5000008'),1);
   {? kk[2]>0
   ||
:: Cena
      _cnetto:= exec('tr','tlumaczenia','5000029');
      _cbrutto:= exec('tr','tlumaczenia','5000030');
      PRN.add("ww[2]",kk[2],{? czy_war=2 || {? PARAM_W.JEZYK = null() || 'Cena' || _cnetto ?}  || {? ND.CB='T' || _cbrutto || _cnetto ?} ?},1);
:: Wartość
      _wnetto:= exec('tr','tlumaczenia','5000031');
      _wbrutto:= exec('tr','tlumaczenia','5000032');
      PRN.add("ww[3]",kk[3],{? czy_war=2 || {? PARAM_W.JEZYK = null() || 'Wartość' || _wnetto ?} || {? ND.CB='T' || _wbrutto || _wnetto ?} ?},1);
      prnw-=(__odsskr+__odsram+__odsskr+kk[3])
   ?};
:: Konto kosztowe
   {? kk[13]>0
   || PRN.add("form(ww[13])",kk[13],exec('tr','tlumaczenia','5000013'));
      prnw-=kk[13]+3
   ?};

   {? kk[9]>0 || PRN.add("form(ww[9])",kk[9],kol_naz); prnw-=(__odsram+kk[9]) ?};
   {? _ii=1
   ||
      kk[5]+=charleft-PRN.width();
      VAR_DEL.delete('PRN');
      exec('nd_begin','magdok_wydruk',1)
   ?}
!};

"--definicja kolumn podsumowania--";
{? kk[2]
|| _white:='\'%1\''[color_bw];
   PRNF.add("w1",prnw,,1,,$_white);
   PRNF.add("w2",kk[3],,1)
?};

"--definicja parametrów badania--";
PRNP.add("__param",PRN.width()-4);
ND.cntx_pop();


PAR_WYDR.SYM:=ND.SYM;
TYPYDOK.cntx_psh();
PAR_WYDR.TITLE:=ND.TYP().NAZ;
PAR_WYDR.TYPE:=ND.TYP().T;
TYPYDOK.cntx_pop();

podtytul:=exec('tr','tlumaczenia','5000009')+' '+$ND.NR+', '+exec('tr','tlumaczenia','5000011')+' '+ND.MAG().SYM;

prn1:='PRN'; prn2:='PRNF'


\druk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: akcja wydruk dokumentu magazynowego
::   WE: [_a] - czy zapisywać dokument 0 - nie 1 - tak
::       [_b] - maska wydruku
::  OLD: \druk_dok/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0  ?};
_b:={? var_pres('_b')=type_of('') || _b || '' ?};

__TOPDF:=_a;

_grp_dr:=var_pres('__GRP_DR')>100 & __GRP_DR.GR;
_wyn:={? _a || null || 0 ?};
_msk:=_b;
{? _msk<>''
|| _wydr:=_msk
||
   {? ND.TYP().WZ>''
      || _wydr:=ND.TYP().WZ
      || _wydr:='lmg_mag_*'
   ?}
?};

{? _a=1
||
   {? _grp_dr & __GRP_DR.FIRST=0 & __GRP_DR.WZ<>''
   ||
      _symb:=exec('str_to_pth','#string',ND.SYM);
      _file:='Dokument' +_symb+'.pdf';
      _wydr:=rep_exec(__GRP_DR.WZ,,,_file,1)
   ||
      _symb:=exec('str_to_pth','#string',ND.SYM);
      _file:='Dokument' +_symb+'.pdf';
      _wydr:=rep_exec(_wydr,,,_file)
   ?}
||
   {? _grp_dr & __GRP_DR.FIRST=0 & __GRP_DR.WZ<>''
   || _wydr:=exec('rep_exec','#b_report','LMG_MAG_WWDM',__GRP_DR.WZ,'Dokument magazynowy',2,,,,,,'W')
   || _wydr:=exec('rep_exec','#b_report','LMG_MAG_WWDM',_wydr,'Dokument magazynowy',2,,,,,,'W')
   ?}
?};
{? _grp_dr & _wydr=0 || __GRP_DR.ESC:=1 ?};
{? _a=1 & _wydr=1
|| _wyn:=exec('save_dok','dokum','ND',_file,,ND.SYM,ND.KH().EM
      ,'Dokument o symbolu: '+ND.SYM+' wystawca: '+XINFO.NAZ,,ND.STAT_REJ)
|? _a=0
|| _wyn:=_wydr
?};
VAR_DEL.delete('__TOPDF');
_wyn


\nd_begin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: przeniesienie BEGIN z lmg_mag_01.rpm
::   WE: [_a]=1-inicjuje tylko PRN, 0-wszystko
::  OLD: \nd_begin/wydruki.fml
::----------------------------------------------------------------------------------------------------------------------

{? var_pres('_a')<>type_of(1) || _a:=0 ?};

{? _a
||
   VAR_DEL.delete('PRN','PRNP')
||
   VAR_DEL.delete('PRN','PRNF','PRNP');
   VAR_DEL.delete('ww','kk','pp')
?};

_graf:=exec('get','#params',1121,2,OPERATOR.USER)<>'T';
_empty_char:={? exec('get','#params', 100236, 2)='N' || ^160 || ' ' ?};

_ilk:=13;

PRN:=obj_new(@.CLASS.PRINT,_ilk,_empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
PRN.framecol(color_frame_g);
PRNP:=obj_new(@.CLASS.PRINT,1,_empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
PRNP.framecol(color_frame_g);

{? _a=0
||
   PRNF:=obj_new(@.CLASS.PRINT,3,_empty_char*3,'  ' ,'  ', '───' ,'  ' ,'──','─',' ');
   PRNF.framecol(color_frame_g);

   ww:=obj_new(_ilk);
   kk:=obj_new(_ilk);  {! _i.._ilk |! kk[_i]:=0  !};
   pp:=obj_new(_ilk);  {! _i.._ilk |! pp[_i]:='' !};

   wartosc:=0
?};

dr_razem:=0;

''


\nd_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: przeniesienie END z lmg_mag_01.rpm
::  OLD: \nd_end/wydruki.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('PRN','PRNF');
VAR_DEL.delete('ww','kk','pp');
VAR_DEL.delete('M_GD','S_GD');
''


\nd_has_zl
::----------------------------------------------------------------------------------------------------------------------
:: DOST: PUBLIC
::  UTW: WH [12.30]
:: OPIS: Sprawdza czy w dokumencie jest pozycja powiazana ze zleceniem
::   WE: _a - ND.ref
::   WY: 0 - brak pozycji powiazanych ze zleceniem
::       1 - istnieje co najmniej jeden DK powiazany ze zleceniem
::  OLD: \nd_has_zl/wydruki.fml
::----------------------------------------------------------------------------------------------------------------------
_nd:=_a;

_result:=0;
_can_continue:=1;

DK.cntx_psh();
DK.index('DOKMAG');
DK.prefix(_nd);
{? DK.first()
|| {!
   |? {? DK.ZL<>null()
      || _result:=1;
         _can_continue:=0
      ?};
      DK.next() & _can_continue>0
   !}
?};
DK.cntx_pop();
_result


\druk_dsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: wydruk wyswietlanego dokumentu magazynowego
::  OLD: \druk_dsp/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
exec('beg_prnd','magdok_wydruk')


\definokr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RR [2008]
:: OPIS: definiuje okresy zalegania
::   WY: 0 - przygotowano okresy zalegania, 1 - zrezygnowano z przygotowania okresow
::  OLD: \definokr/help.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('XZAL');
XZAL:=tab_tmp(
   ,'OKR','INTEGER',''
   ,'NR' ,'INTEGER',''
   ,'OD' ,'INTEGER','');
_acr_sel:=XZAL.mk_sel('Okresy'@,'T',0,'definokracr_sel',,,5,,'U');
XZAL.win_fld(_acr_sel,,'OD',,,20,0,1,'Od ilości dni'@);
XZAL.win_fld(_acr_sel,,'OKR',,,20,0,0,'Do ilości dni'@);
XZAL.win_act(_acr_sel,1,'Dołącz',,,,,"exec('po_xzal','magdok_wydruk')",1);
XZAL.win_act(_acr_sel,0,'Dołącz',,,,,"exec('po_xzal','magdok_wydruk')",1);
XZAL.win_act(_acr_sel,0,'Usuń',,,,,,0);
XZAL.win_act(_acr_sel,0,'Popraw',,,,,"exec('po_xzal','magdok_wydruk')",0);
XZAL.win_act(_acr_sel,0,'Formuła','Wybierz'@@,,'Akceptacja zdefiniowanych okresów'@,"sel_exit()",,1);
XZAL.win_sel(_acr_sel);
XZAL.OD:=0; XZAL.OKR:= 30; XZAL.add(1);
XZAL.OD:=31; XZAL.OKR:= 60; XZAL.add(1);
XZAL.OD:=61; XZAL.OKR:=180; XZAL.add(1);
XZAL.OD:=181; XZAL.OKR:=360; XZAL.add(1);
XZAL.last();
_wyn:=XZAL.select(,1,4)=0;
{? XZAL.first() || _i:=0; {! |? _i+=1; XZAL.NR:=_i; XZAL.put(1); XZAL.next !} ?};
XZAL.index(XZAL.ndx_tmp('',,'OKR',,));
_wyn


\po_xzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2008]
:: OPIS: po dolaczeniu/poprawie
::   WE:
::  OLD: \po_xzal/help.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? XZAL.OKR>0
|| _okr:=XZAL.OKR;
   XZAL.cntx_psh;
   XZAL.clear;
   XZAL.prefix(_okr);
   _del:=XZAL.size>1;
   XZAL.cntx_pop;
   {? _del || XZAL.del() ?};
   XZAL.cntx_psh;
   {? XZAL.first
   ||
      _od:=XZAL.OKR+1;
      {? XZAL.next
      ||
         {!
         |?
            XZAL.OD:=_od;
            XZAL.put;
            _od:=XZAL.OKR+1;
            XZAL.next
         !}
      ?}
   ?};
   XZAL.cntx_pop;
   _wyn:=1
|| FUN.info('Ilość dni nie może być mniejsza lub równa zero.'@);
   XZAL.del()
?};
win_disp;
_wyn


\wylicz_il
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MLAK [2011]
:: OPIS: Odczytanie ilosci z dokumentu raportu z produkcji, do wydruku dokumentu rozliczającego
::   WE: _a - ND.NDSQL
::       _b - DK.ZL
::       _c - ZL.KTM
::  OLD: \wylicz_il/sur_nlim.fml
::----------------------------------------------------------------------------------------------------------------------
_il:=0;
ND.cntx_psh();
DK.cntx_psh();
ND.prefix();
{? ND.seek(_a,form(8+_a))
|| DK.index('MAM');
   DK.prefix(ND.ref(),_c);
   {? DK.first()
   || {!
      |? {? DK.ZL=_b
         || _il+=DK.IL
         ?};
         DK.next()
      !}
   ?}
?};
DK.cntx_pop();
ND.cntx_pop();
_il


\nd_analizy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Analizy dokumentów magazynowych
::----------------------------------------------------------------------------------------------------------------------
{? ND.sel_size()=0
||
   exec('nd_analizy_gr','magdok_wydruk')
?};
~~


\nd_analizy_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.42]
:: OPIS: Analizy dokumentów magazynowych - grupa
::----------------------------------------------------------------------------------------------------------------------
ND.cntx_psh();
exec('rep_exec','#b_report','LMG_MAG_XXXX','lmg_magb*','Analizy bieżących dokumentów magazynowych',1);
ND.cntx_pop();
~~


\beg_prnd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: przed akcja grupowa dla wydruku
::  OLD: \beg_prnd/magazyn.fml
::----------------------------------------------------------------------------------------------------------------------
{? ND.TYP().WZ>''
|| exec('rep_exec','#b_report','LMG_MAG_WWDM',ND.TYP().WZ,'Dokument magazynowy',2,,,,,,'W')
|| exec('rep_exec','#b_report','LMG_MAG_XXXX','lmg_mag_*','Wydruki dokumentów magazynowych',1)
?}


\nd_word
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Wydruk dokumentu magazynowego do WORD
::----------------------------------------------------------------------------------------------------------------------
_env:=obj_new('DOKL');
_env.DOKL:=ST.DOKL;
params_set('env',_env);

{? ND.KH<>null()
|| ND.KH()
|| KH.seek(0,,,1)
?};
params_exec('generuj','szablon_zws','LMG_DOKUMENT',$ND.ref())


\dk_nd_prn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Dokument magazynowy - Drukuj (przez tabelę DK)
::----------------------------------------------------------------------------------------------------------------------
exec('mag_druk_gr','!lmg_mag_wwdm',0,'LMG_MAG_WWDM','Area Drukuj',,1)


\dk_nd_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Dokument magazynowy - Utwórz PDF (przez tabelę DK)
::----------------------------------------------------------------------------------------------------------------------
exec('mag_druk_gr','!lmg_mag_wwdm',1,'LMG_MAG_WWDM','Area Utwórz PDF',,1)


\dk_nd_word
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [20.42]
:: OPIS: Wydruk dokumentu magazynowego do WORD (przez tabelę DK)
::----------------------------------------------------------------------------------------------------------------------
_env:=obj_new('DOKL');
_env.DOKL:=ST.DOKL;
params_set('env',_env);

DK.N();
{? ND.KH<>null()
|| ND.KH()
|| KH.seek(0,,,1)
?};
exec('wypelnij','szablon','LMG_DOKUMENT')

:Sign Version 2.0 jowisz:1045 2023/09/28 14:46:38 a6864327fbb4eddfa1a20e75ccc8ca454ae8ab5a9fa53ae11d00283fee565c9a2c29a9624441f9cc71a0c246e84af3e8316429b75b9364c62e046f31ca7ee950e4e302e4eb525af97eeef0469332431fb1236f81aca7e57245418e83e2bfb253ddfc40f27cc275348a7bbb0991079bb8327a20e820656abd76bfaeefc6038393

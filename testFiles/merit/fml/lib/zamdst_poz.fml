:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: zamdst_poz.fml
:: Utworzony: 12.05.2015
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa zamówień dostaw - pozycje zamówień
::======================================================================================================================


\pwdj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed wyświetleniem pól dla jednostek dodatkowych (pozycje zamówien dostaw)
::  OLD: \pwdj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
''


\prdj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed redakcją pól dla jednostek dodatkowych (pozycje zamówień dostaw)
::  OLD: \prdj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ATR.MJS='PD_K' & PD_K.TYPYZAM().R='D'
||
   {? 'WS2 J2 IL2'*cur_afld() || exec('ustapdkprz','jm',PD_K.M,PD_K.M().J) ?};
   {? PD_K.T2='A' & {? JM.f_active() || JM.f_size<=1 || 1 ?}
   || 0
   |? cur_afld()='WS2' & (PD_K.T2='A' | PD_K.J2=PD_K.M().J)
   || 0
   || (1+BEER.DMJM)='T'
   ?}
||
   {? 'WS2 J2 IL2'*cur_afld() || exec('ustadprz','zamdst_poz',ZD_POZ.M,ZD_POZ.M().J) ?};
:: DRO [rr] nie przenosiłem
::   {? ~exec('bezdilko','zd')
::   || 0
:: zamiast tego
::   {? 0=1
::   || 0
   {? 0=1
   || 0
   |? ZD_POZ.M().J2<>null()
   || {? cur_afld()='WS2' || BEER.ORD || 1 ?}
   |? ZD_POZ.T2='A' & {? JM.f_active() || JM.f_size<=1 || 1 ?}
   || 0
   |? cur_afld()='WS2' & (ZD_POZ.T2='A' | (ZD_POZ.J2=ZD_POZ.M().J & ~BEER.JMZ))
   || 0
   || (1+BEER.DMJM)='T'
   ?}
?}


\podj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po redakcji pól dla jednostek dodatkowych (pozycje magazynowe)
::  OLD: \podj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~BEER.ORD || BEER.ORD:=2 ?};
{? ATR.MJS='PD_K' & PD_K.TYPYZAM().R='D'
||
   {? cur_afld()='J2' & (PD_K.T2='A' | (PD_K.T2='R' & PD_K.WS2=0)) & (PD_K.J2<>PD_K.M().J | DPOZ.WPR_S<>$PD_K.J2)
   || PD_K.WS2:=exec('prz','jm',PD_K.M,PD_K.J2,PD_K.M().J)
   ?};

   _dokl_m:=exec('jaka_dok_m','jm',PD_K.M);
   _dokl_s:=exec('jaka_dok_mjm','jm',PD_K.M,PD_K.J2,PD_K.M().J);
   {? _dokl_s<0 || _dokl_s:=_dokl_m ?};

   {? PD_K.T2<>'N' & PD_K.WS2
   || _il2:=PD_K.IL/PD_K.WS2 $ (_dokl_s+1);
      roundmet(BEER.MDOKL);
      _il2:=_il2 $ _dokl_s;
      roundmet(5)
   || _il2:=0
   ?};

   {? PD_K.IL=0 | (_il2>0 & _il2<>PD_K.IL2
    & FUN.ask('Ilość wyliczona wg jednostki dodatkowej jest różna od ilości.\nCzy zmienić ilość?'@))
   || PD_K.IL:=PD_K.IL2*PD_K.WS2 $ _dokl_m
   || exec('aktdil2','zamdst_poz')
   ?}
|| _jmz:=ZD_POZ.ZD_NAG().JMZ;
   _jmg:=ZD_POZ.M().J2;
   {? cur_afld()='J2' & (ZD_POZ.T2='A' | ZD_POZ.T2='R') & ZD_POZ.J2<>ZD_POZ.M().J
   || _ws2:=exec('prz','jm',ZD_POZ.M,ZD_POZ.J2,ZD_POZ.M().J);
      {? _ws2<>ZD_POZ.WS2 || ZD_POZ.WS2:=_ws2 ?}
   ?};
   {? cur_afld()<>'IL2'
   || {? ~_jmz & ZD_POZ.M().J=ZD_POZ.J2
      || ZD_POZ.WS2:=1;
         ZD_POZ.IL2:=ZD_POZ.IL
      |? ZD_POZ.M().J<>ZD_POZ.J2 & ZD_POZ.J2<>null() & ZD_POZ.WS2=1
      || ZD_POZ.J2:=ZD_POZ.M().J;
         ZD_POZ.IL2:=ZD_POZ.IL
      ?}
   ?};
   _dokl_m:=exec('jaka_dok_m','jm',ZD_POZ.M);
   _dokl_s:=exec('jaka_dok_mjm','jm',ZD_POZ.M,ZD_POZ.J2,ZD_POZ.M().J);
   {? _dokl_s<0 || _dokl_s:=_dokl_m ?};

   {? ZD_POZ.T2<>'N' & ZD_POZ.WS2
   || _il2:=ZD_POZ.IL/ZD_POZ.WS2 $ (_dokl_s+1);
      roundmet(BEER.MDOKL);
::      _il2:=_il2 $ _dokl_s;
      roundmet(5)
   || _il2:=0
   ?};

   {? ZD_POZ.J2=null()
   || ZD_POZ.IL2:=0
   |? _jmz & ZD_POZ.WS2 & cur_afld()<>'IL2'
   || exec('aktdil2','zamdst_poz')
   |? (ZD_POZ.IL_KOR=0 & ZD_POZ.IL_ZAM=0) | (_il2>0 & _il2<>ZD_POZ.IL2
    & FUN.ask('Ilość wyliczona wg jednostki dodatkowej jest różna od ilości.\nCzy zmienić ilość?'@))
   || ZD_POZ.IL_KOR:=ZD_POZ.IL2*ZD_POZ.WS2 $ _dokl_m;
      ZD_POZ.IL:=ZD_POZ.IL_ZAM+ZD_POZ.IL_KOR $ _dokl_m
   || exec('aktdil2','zamdst_poz')
   ?}

?};
1


\bldj2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: blankuje jednostke dodatkową
::  OLD: \bldj2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
null


\bldt2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: blankuje typ przeliczenia jednostki dodatkowej (pozycje zamówien dostaw)
::  OLD: \bldt2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
1+(1-BEER.DMJM)


\ustadprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: ustawia zmienną BEER.DMJM
::   WE: [_a] - ref indeksu materiałowego
::       [_b] - ref jednostki miary
::  OLD: \ustadprz/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0
|| BEER.JMZ:=exec('get','#params',400999,2)='T';
   BEER.DMJM:='NN0';
   BEER.TAB:='ZD_POZ';
   _kod:=exec('get','#params',400000,2);
   {? _kod*'ZD'
   || {? 'RA'*(_rodz:=exec('get','#params',400301,2)) || BEER.DMJM:='T'+_rodz+'0' ?}
   ?}
|? _a<>null
|| BEER.JMZ:=ZD_POZ.ZD_NAG().JMZ;
   _jmg:=ZD_POZ.M().J2;
   _dom:=null();
   JM.win_dict('WERF');
   JM.f_clear();
   JM.index('KOD');
   JM.prefix();
   JM.f_set('KOD,NAZ',,,);
   {? _jmg<>null()
   || _dom:=_jmg
   |? (1+(1-BEER.DMJM))='A' & JM.f_first()
   || {!
      |? {? exec('prz','jm',_a,_b,JM.ref())<>0
         || {? exec('FindInSet','#table','MJM','JM',JM.ref(),_a,"MJM.DOM",,,0) || _dom:=JM.ref() ?};
            JM.f_next()
         || JM.f_del()
         ?}
      !}
   |? JM.f_first()
   || {!
      |? {? exec('FindInSet','#table','MJM','JM',JM.ref(),_a,"MJM.DOM",,,0) || _dom:=JM.ref() ?};
         _dom=null() & JM.f_next()
      !}
   ?};
   _ile:=JM.f_size();
   {? _jmg=null() & BEER.JMZ & ZD_POZ.J2=null()
   || {? _dom=null()
      || {? _ile=1
         || JM.f_first(); ZD_POZ.JM:=JM.ref(); ZD_POZ.WS2:=exec('prz','jm',_a,_b,JM.ref())
         |? {? JM.f_first()
            || _rr:=0;
               {!
               |? {? exec('FindInSet','#table','MJM','JM',JM.ref(),_a,,,,null())<>null()
                  || _rr:=1;0
                  || JM.f_next()
                  ?}
               !};
               _rr
            || 0
            ?}
         || ZD_POZ.JM:=JM.ref();
            ZD_POZ.WS2:=exec('prz','jm',ZD_POZ.M,ZD_POZ.JM,ZD_POZ.M().J)
         || ZD_POZ.JM:=ZD_POZ.J2:=ZD_POZ.M().J;
            ZD_POZ.WS2:=1
         ?};
         ZD_POZ.WS2:={? ZD_POZ.WS2>0 || 1/ZD_POZ.WS2 || 1 ?}
      || ZD_POZ.JM:=_dom
      ?}
   ?};
   {? _jmg<>null()
   || ZD_POZ.T2:='M';
      ZD_POZ.JM:=ZD_POZ.M().J;
      ZD_POZ.J2:=_jmg;
      {? ZD_POZ.WS2=0 || ZD_POZ.WS2:=exec('oblWSP','jm',ZD_POZ.M) ?}
   |? ~_ile
   || BEER.DMJM:='N'+(1-BEER.DMJM)
   |? BEER.JMZ
   || ZD_POZ.J2:=ZD_POZ.M().J;
      ZD_POZ.WS2:={? ZD_POZ.JM<>ZD_POZ.M().J || exec('prz','jm',ZD_POZ.M,ZD_POZ.JM,ZD_POZ.M().J) || 1 ?};
      {? BEER.JMZ || ZD_POZ.WS2:={? ZD_POZ.WS2>0 || 1/ZD_POZ.WS2 || 1 ?} ?}
   |? _ile=1
   || JM.f_first(); ZD_POZ.J2:=JM.ref(); ZD_POZ.WS2:=exec('prz','jm',_a,_b,JM.ref());
      {? BEER.JMZ || ZD_POZ.WS2:={? ZD_POZ.WS2>0 || 1/ZD_POZ.WS2 || 1 ?} ?}
   |? ZD_POZ.J2=null & exec('FindInSet','#table','MJM','JM',ZD_POZ.M().J,ZD_POZ.M)<>null
   || ZD_POZ.J2:={? ~BEER.JMZ & _dom<>null()
                 || _dom
                 || exec('FindInSet','#table','MJM','JM',ZD_POZ.M().J,ZD_POZ.M,"MJM.JP",,,null())
                 ?};
      ZD_POZ.WS2:=exec('prz','jm',ZD_POZ.M,ZD_POZ.J2,ZD_POZ.M().J);
      {? BEER.JMZ || ZD_POZ.WS2:={? ZD_POZ.WS2>0 || 1/ZD_POZ.WS2 || 1 ?} ?}
   |? ZD_POZ.J2=null & exec('FindInSet','#table','MJM','JP',ZD_POZ.M().J,ZD_POZ.M)<>null
   || ZD_POZ.J2:={? ~BEER.JMZ & _dom<>null()
                 || _dom
                 || exec('FindInSet','#table','MJM','JP',ZD_POZ.M().J,ZD_POZ.M,"MJM.JM",,,null())
                 ?};
      ZD_POZ.WS2:=exec('prz','jm',ZD_POZ.M,ZD_POZ.J2,ZD_POZ.M().J);
      {? BEER.JMZ || ZD_POZ.WS2:={? ZD_POZ.WS2>0 || 1/ZD_POZ.WS2 || 1 ?} ?}
   ?};
   {? ZD_POZ.J2=null()
   || ZD_POZ.J2:=ZD_POZ.JM;
      ZD_POZ.WS2:=1
   ?}
?}


\aktdil2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: aktualizuje pole ZD_POZ.IL2 po zmianie pola ZD_POZ.IL
::  OLD: \aktdil2/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
_dokl_m:=exec('jaka_dok_m','jm',ZD_POZ.M);
_dokl_s:=exec('jaka_dok_mjm','jm',ZD_POZ.M,ZD_POZ.J2,ZD_POZ.M().J);
{? _dokl_s<0 || _dokl_s:=_dokl_m ?};

_jmg:=ZD_POZ.M().J2;
_jmz:=ZD_POZ.ZD_NAG().JMZ;

{? ZD_POZ.T2<>'N' & ZD_POZ.WS2
|| ZD_POZ.IL2:=ZD_POZ.IL/ZD_POZ.WS2 $ (_dokl_s+1);
   roundmet(BEER.MDOKL);
   ZD_POZ.IL2:=ZD_POZ.IL2 $ _dokl_s;
   roundmet(5)
?}


\zdpo_wys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed wyswietl okna WER tabeli ZD
::   WE: [_a] - okienko do wyświetlenia - domyślnie 'DISP'
::  OLD: \zdpo_wys/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_ddit:={? var_pres('_a')=type_of('') || _a || '' ?};


{? _ddit='INFPOTW' & ZDP_POZ.ZD_POZ<>''
|| ZD_POZ.cntx_psh();
   ZD_NAG.cntx_psh();
   ZD_POZ.use(form(8+ZDP_POZ.ZD_POZ));
   ZD_NAG.use((ZD_NAG.name()-3)+(ZD_POZ.name()+3));
   ZD_POZ.prefix();
   ZD_POZ.seek(ZDP_POZ.ZD_POZ)
?};
BEER.JMZ:=ZD_POZ.ZD_NAG().JMZ;
{? ZD_POZ.M().RODZ='U'
|| ZD_POZ.win_edit('REDU')
|| ZD_POZ.win_edit('RED%1'[{? BEER.JMZ || '2' || '' ?}])
?};
_edit:=ZD_POZ.win_edit('?');
_sel:=ZD_POZ.win_sel('?');
_atrmjs:=ATR.MJS;
{? (1+ZD_POZ.MG().TYP)='D'
|| ATR.MJS:='ZD_POZ';
   ATR.M_ATR:=ZD_POZ.M().M_ATR
|| ATR.MJS:='M_ATR';
   M_ATR.blank(0);
   ATR.M_ATR:=null()
?};
{? _ddit<>''
|| ZD_POZ.win_edit(_ddit)
|? exec('czytadkc','mat_atr',ZD_POZ.DK_C,ZD_POZ.RDKC)
|| _ndit:='DISP';
   ZD_POZ.win_edit(_ndit)
?};
exec('set_efld_opt','zamdst_poz');
exec('set_efld_opt','mat_atr',ATR.MJS);

exec('dispWithFakso','fakso','ZD_POZ');

ZD_POZ.win_edit(_edit);
ZD_POZ.win_sel(_sel);
{? _ddit='INFPOTW' & ZDP_POZ.ZD_POZ<>''
|| ZD_NAG.cntx_pop();
   ZD_POZ.cntx_pop()
?};

ATR.MJS:=_atrmjs;
~~


\zdp_obl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: oblicza wartosci poyzji zamowienia dostaw
::  OLD: \zdp_obl/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZD_POZ.ZD_NAG().WAL<>INFO.NAROD & ZD_NAG.KRS
||
   _walj:=exec('FindInSet','#table','WAL','WAL_SYM',ZD_POZ.WAL().KOD,,"WAL.J",,,1);
   ZD_POZ.CENA:=(ZD_POZ.CWAL*ZD_POZ.KRS/_walj)$ZD_POZ.M().DOKL_Z;
   ZD_POZ.WAR:=(ZD_POZ.IL*ZD_POZ.CENA)$2;
   ZD_POZ.WARW:=(ZD_POZ.IL*ZD_POZ.CWAL)$2
||
   ZD_POZ.CENA:=ZD_POZ.CENA$ZD_POZ.M().DOKL_Z;
   ZD_POZ.WAR:=(ZD_POZ.IL*ZD_POZ.CENA)$2;
   ZD_POZ.WARW:={? ZD_POZ.CWAL || (ZD_POZ.IL*ZD_POZ.CWAL)$2 || ZD_POZ.WAR ?}
?}


\zd_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: pozycje zamowienia
::   WE: [_a] - =0-wolac ZD_NAG.r_lock i ZD_NAG.r_unlock , =1-wpp
::       [_b] - ZD_POZ.uidref(), na którym ma stanąć selektor
::  OLD: \zd_poz/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0  ?};
{? var_pres('_b')<>type_of('')
|| _b:=''
?};

ZD_NAG.cntx_psh();
{? {? ~_a || exec('zam_lock','zamdst_nag') & ZD_NAG.r_lock(1,1,1) || 1 ?}
||
:: Ustawia BEER.WW
   BEER.ORD:=1;
   BEER.MW:='W';
   exec('ustaw_ww','eurusd','W');
   exec('ust_lw','eurusd',0);
   {? exec('mw_bez_wal_opod','eurusd') || BEER.WW:=3 ?};
:: przelicznik jm
   exec('ustadprz','zamdst_poz');
:: ustawienie domyslnego okna dla pozycji zamowien
   {? POM.ZDPOZ_W='' || POM.ZDPOZ_W:='WER_IL' ?};
:: zmienne do liczenia wartości
   exec('st_licz_wz','ceny','ZD_NAG');

   ZD_POZ.index('POZ');
   ZD_POZ.prefix(ZD_NAG.ref);
   {? _b<>''
   || ZD_POZ.seek(_b)
   || ZD_POZ.first()
   ?};
   ATR.MJS:='ZD_POZ';
   {? _b<>''
   || exec('select_zd_poz','zamdst_poz',1)
   || exec('select_zd_poz','zamdst_poz')
   ?};

:: Ustawia BEER.WW
   exec('ustaw_ww','eurusd',BEER.MW);
:: usuniecie filtru programowego na tabeli JM
   JM.f_clear;
   JM.win_dict('WER');
:: ustawienie statusu zamowienia
   exec('aktu_zam','zamdst_wspolne',ZD_NAG.ref);

   {? ~_a || ZD_NAG.r_unlock() ?}
?};
ZD_NAG.cntx_pop();
''


\select_zd_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: select na tabele ZD_POZ
::   WE: [_a] - drugi parametr select(): 0 (domyślnie) - aktualny będzie pierwszy rekord, 1 - bieżący rekord
::  OLD: \select_zd_poz/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0)
|| _a:=0
?};

_menu_pth:=BEER.MENU_PTH;
BEER.MENU_PTH:='ZW';
:: zmienna dla stanow magazynowych
HELP.WMAG:=2;

:: ustawienie prefixu na materialy
POMOC.RODZ:='T';
POMOC.MGR:=null;

PROJZ.PROJEKTY:=ZD_NAG.PROJEKTY;
BEER.KH:=ZD_NAG.KH;
KH.cntx_psh();
ZD_NAG.cntx_psh();
ZD_POZ.cntx_psh();

ZD_POZ.hdr_sel(' nr: %1'@[ZD_NAG.SYM]);
ZD_POZ.win_sel('WER');
_act:=':';
{? ZD_NAG.STAT_REJ<>'N' | ZD_NAG.OBI_POW<>''
|| _act:={? (';ACN'*ZD_NAG.STAN)>1 & ZD_NAG.OBI_POW='' || '' || 'P' ?}+'ZDUGRC'+_act+'DRC'
?};
{? ZD_NAG.STAT_REJ='A' || _act:='W'+_act ?};
{? ZD_NAG.STAN='M' || _act:='W'+_act ?};
{? ZD_POZ.name()+2='__'
|| ZD_POZ.actions(ZD_POZ.win_sel('?'),_act)
|| ZD_POZ.actions(ZD_POZ.win_sel('?'),'ZWYDUPOGRC:DROGC')
?};
{? KHVZ.WIDOKI
|| _dictm:=M.win_dict('?');
   M.win_dict('SLO_M');
   _dictdkc:=DK_C.win_dict('?');
   DK_C.win_dict('WER')
?};
{? _a=0
|| ZD_POZ.select()
|| ZD_POZ.select(,1,5,,,,1)
?};
{? KHVZ.WIDOKI
|| M.win_dict(_dictm);
   DK_C.win_dict(_dictdkc)
?};

KH.cntx_pop();
ZD_NAG.cntx_pop();
ZD_POZ.cntx_pop();

PROJZ.PROJEKTY:=null();
BEER.KH:=null;
HELP.WMAG:=0;
BEER.MENU_PTH:=_menu_pth;
''


\zd_nag_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Wartosc poczatkowa ZD_POZ.ZD_NAG
::  OLD: \zd_nag_r/zd.fml
::----------------------------------------------------------------------------------------------------------------------
ZD_NAG.ref()


\bl_zd_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: blank dla ZD_POZ.KH
::  OLD: \bl_zd_kh/zd.fml
::----------------------------------------------------------------------------------------------------------------------
ZD_NAG.KH


\bezdilko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja pola ZD_POZ.IL
::  OLD: \bezdilko/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=(ZD_NAG.STAN='N' | ZD_POZ.ZRE<>'T')
     & {? -(1+menu_txt)='d' || 1 || exec('obl_ptw','zamdst_ptw',$ZD_POZ.ref()); __zdp_sum.IL_PTW=0 ?};
{? ~BEER.ORD || 0 || _res ?}


\aezdpoil
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: po redakcji pola ilosc zamawiana
::   WY: czy mozna opuscic redakcje pola
::  OLD: \aezdpoil/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
M.cntx_psh();

_dokl:=3;
{? ZD_POZ.M<>null
|| _dokl:=exec('jaka_dok_m','jm',ZD_POZ.M)
?};
ZD_POZ.IL:=ZD_POZ.IL$_dokl;

ZD_POZ.IL_KOR:=ZD_POZ.IL-ZD_POZ.IL_ZAM;
ZD_POZ.IL_POZ:=ZD_POZ.IL-ZD_POZ.IL_ZRE;
_il:=ZD_POZ.IL;
{? _il<ZD_POZ.IL_ZRE & _il>0 & ZD_POZ.M().RODZ='T'
|| FUN.info('Ilość materiału na zamówieniu nie może być mniejsza od ilości już zrealizowanej.'@)
|? _il<ZD_POZ.IL_ZRE & _il>0 & ZD_POZ.M().RODZ='U'
|| FUN.info('Ilość usługi na zamówieniu nie może być mniejsza od ilości już zrealizowanej.'@)
|? _il<ZD_POZ.IL_ZT & ZD_POZ.M().RODZ='T'
|| FUN.info('Ilość materiału na zamówieniu nie może być mniejsza od ilości\n'
            'aktualnie zadysponowanej do transportu: %1 %2.'@[$ZD_POZ.IL_ZT,ZD_POZ.JM().KOD]);
   ZD_POZ.IL:=ZD_POZ.IL_ZT
|? _il<ZD_POZ.IL_ZT & ZD_POZ.M().RODZ='U'
|| FUN.info('Ilość usługi na zamówieniu nie może być mniejsza od ilości\n'
            'aktualnie zadysponowanej do transportu: %1 %2.'@[$ZD_POZ.IL_ZT,ZD_POZ.JM().KOD]);
   ZD_POZ.IL:=ZD_POZ.IL_ZT
|| {? _il<0 & ZD_POZ.M().RODZ='T'
   || FUN.info('Ilość materiału na zamówieniu nie może być ujemna.'@)
   |? _il<0 & ZD_POZ.M().RODZ='U'
   || FUN.info('Ilość usługi na zamówieniu nie może być ujemna.'@)
   || exec('zezdp_ce','zamdst_poz');_wyn:=1
   ?}
?};

M.cntx_pop();
{? _wyn || exec('aktdil2','zamdst_poz') ?};
_wyn


\zezdp_ce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: po redakcji pola ZD_POZ.CENA
::   WY: czy mozna opuscic redakcje pola
::  OLD: \zezdp_ce/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? ZD_POZ.CENA<0
||
   _wyn:=0;
   FUN.info('Cena musi być dodatnia.'@)
||
   exec('zdp_obl','zamdst_poz')
?};
ZD_POZ.MG().SYM;
_wyn


\pr_cenzd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: sprawdzenie czy mozliwe do edycji pole ZD_POZ.CENA
::  OLD: \pr_cenzd/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('czy_rwal','eurusd','W') | ZD_NAG.KRS=0;
{? _wyn & ZD_POZ.sel_size()=0
||
   exec('taz_sd_set','ceny');
   {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?}
   || _jmz:=ZD_POZ.ZD_NAG().JMZ & ZD_POZ.JM<>ZD_POZ.J2;
      {? _jmz
      || KALK.JM:=KALK.J2:=ZD_POZ.JM; KALK.WS2:=1
      || KALK.JM:=KALK.J2:=ZD_POZ.M().J; KALK.WS2:=1
      ?};
      exec('stplicz','ceny',ZD_POZ.IL);
      exec('ceny_mat','ceny_mat',ZD_POZ.M,ZD_NAG.KH,'ZD_POZ.CENA',0,,'','N',INFO.NAROD,'',,0
       ,{? ZD_POZ.JM<>null || ZD_POZ.JM().KOD || '' ?}
       ,{? ZD_POZ.J2<>null & ZD_POZ.WS2<>0 || {? _jmz || ZD_POZ.WS2 || 1/ZD_POZ.WS2 ?} || 0 ?}
       ,0,0,0)
   ?}
?};
_wyn


\bl_zd_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: blank dla ZD_POZ.MG
::  OLD: \bl_zd_mg/zd.fml
::----------------------------------------------------------------------------------------------------------------------
ZD_NAG.MG


\bd_zd_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed wyswietl dla ZD_POZ.MG
::  OLD: \bd_zd_mg/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('be_zd_mg','zamdst_poz')
|| exec('findfnrd','color')
|| ''
?}


\be_zd_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: przed edycja dla ZD_POZ.MG
::   WY: 1/0
::  OLD: \be_zd_mg/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? ZD_NAG.MG<>null | ZD_POZ.IL_ZRE>0
|| _wyn:=0
|? var_press('__wyb_dm')>0
|| _wyn:=0
?};
_wyn


\ae_zd_mg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.41]
:: OPIS: po zmianie magazynu dla pozycji zamówienia
::  OLD: \ae_zd_mg/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
{? ZD_POZ.MG<>null() & ZD_POZ.MG().COS='T'
|| {? ZD_POZ.MG().DLAKH<>null() & ZD_POZ.ZD_NAG().KH<>ZD_POZ.MG().DLAKH
   || FUN.info('Wskazano magazyn konsygnacyjny dla %1.\n'
               'Wybór magazynu niemożliwy.'@[ZD_POZ.MG().DLAKH().SKR]);
      _res:=0
   |? ~ZD_POZ.ZD_NAG().KH().COS
   || FUN.info('Nie zdefiniowano obsługi magazynów konsygnacyjnych dla dostawcy: %1.\n'
               'Wybór magazynu niemożliwy.'@[ZD_POZ.ZD_NAG().KH().SKR]);
      _res:=0
   ?}
?};
{? _res
|| exec('set_efld_opt','mat_atr','ZD_POZ');
   {? ZD_POZ.MG<>null()
   || {? (1+ZD_POZ.MG().TYP)<>'D' || ZD_POZ.DK_C:=null(); ZD_POZ.DK_C().SYM; M_ATR.blank(0) ?};
      ATR.M_ATR:={? (1+ZD_POZ.MG().TYP)='D' || ZD_POZ.M().M_ATR || null() ?};
      ATR.FLAG_ED:=(1+ZD_POZ.MG().TYP)='D' & ATR.CZY_ATR & ATR.M_ATR().EDIT;
      ATR.MJS:={? (1+ZD_POZ.MG().TYP)='D' || 'ZD_POZ' || 'M_ATR' ?};
      ATR.FLAG:={? ATR.FLAG_ED & ZD_POZ.M().M_ATR<>null() || 2 || 0 ?};
      {? ATR.FLAG_ED || {? ZD_POZ.M().M_ATR<>null() || ATR.FLAG_ED:=2 ?} ?};
      {? ZD_POZ.DK_C<>null() & ZD_POZ.DK_C().M_ATR<>null()
      || {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():=($('ZD_POZ.DK_C().WAR'+form(_i,-2,,'99')))() !}
      |? ZD_POZ.M().M_ATR=null()
      || {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !}
      ?}
   || ATR.MJS:='M_ATR';
      ZD_POZ.DK_C:=null();
      M_ATR.blank(0);
      {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !};
      ATR.FLAG:=0;
      ATR_FLAG_ED:=0
   ?};
   exec('set_efld_opt','zamdst_poz');
   win_disp()
?};
_res


\zdpo_dol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2008]
:: OPIS: dolaczenie rekordu do tabeli ZD_POZ
::   WY: 1
::  OLD: \zdpo_dol/zd.fml
::----------------------------------------------------------------------------------------------------------------------
exec('magazyny','zamdst_wspolne');
{? ~('MQZ'*ZD_NAG.STAN)
||
   BEER.JMZ:=ZD_NAG.JMZ;
   M.win_dict('NL_WER');

   ZD_POZ.cntx_psh();
   ZD_POZ.index('POZ');
   ZD_POZ.prefix(ZD_NAG.ref);
   _ref:={? ZD_POZ.size() || ZD_POZ.ref || null() ?};
   _czy_ed:=ATR.CZY_ATR & ATR.EDIT;
   ZD_POZ.win_edit('RED%1'[{? BEER.JMZ || '2' || '' ?}]);
   {! |? exec('zdpo_don','zamdst_poz',_czy_ed,_ref) !};
   ZD_POZ.cntx_pop();
   ZD_POZ.win_edit('RED')
||
    {? ZD_NAG.STAN='M' || FUN.info('Zamówienie jest już zamknięte.\nDodawanie pozycji niemożliwe.'@)
    |? ZD_NAG.STAN='Q' || FUN.info('Dla zamówienia odrzucono zapotrzebowanie.\nDodawanie pozycji niemożliwe.'@)
    || FUN.info('Zamówienie jest już zrealizowane.\nDodawanie pozycji niemożliwe.'@)
    ?}
?};
MG.f_clear();
1


\zdpo_don
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.41]
:: OPIS: dolaczenie rekordu do tabeli ZD_POZ - wykorzystywane w zdpo_dol,zd
::   WE: [_a] - czy edycja atrybutow (domyslnie 0)
::       [_b] - ref do odszukania (domyslnie null)
::   WY: 1-dolaczono 0-rezygnacja z edycji
::  OLD: \zdpo_don/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1  || {? type_of(_a)<>1 || _a:=0 ?}  || _a:=0 ?};
{? _>=2  || {? type_of(_b)<>1 || _b:=null() ?}  || _b:=null() ?};

_czy_ed:=_a;
   _ref:=_b;

_wyn:=0;
ZD_POZ.blank();
ZD_POZ.D_POTW:=ZD_POZ.ZD_NAG().D_POTW;
ZD_POZ.TRN:=ZD_POZ.ZD_NAG().TRN;
ZD_POZ.POJAZD:=ZD_POZ.ZD_NAG().POJAZD;
{? D_HELP.M_ZDPOZ<>''
|| M.cntx_psh();
   M.index('ARODZ'); M.prefix('T','T',D_HELP.M_ZDPOZ,D_HELP.M_ZDPOZ);
   {? M.first() || ZD_POZ.M:=M.ref() || D_HELP.M_ZDPOZ:='' ?};
   M.cntx_pop()
?};
ATR.FLAG_ED:=(1+ZD_POZ.MG().TYP)='D' & ATR.CZY_ATR & ATR.EDIT;
{! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !};
ATR.FLAG:={? ATR.FLAG_ED & _czy_ed & ZD_POZ.M().M_ATR<>null() || 2 || 0 ?};
{? _czy_ed & ATR.FLAG_ED || {? ZD_POZ.M().M_ATR || ATR.FLAG_ED:=2 ?} ?};
exec('set_efld_opt','zamdst_poz','RED');
{? ZD_POZ.edit("exec('chk_zd_p','zamdst_poz')")
||
   ZD_POZ.IL_POZ:=ZD_POZ.IL_ZAM+ZD_POZ.IL_KOR;
   ZD_POZ.KH:=ZD_POZ.ZD_NAG().KH;
   ZD_POZ.KH_MSC:=ZD_POZ.ZD_NAG().KH_MSC;
   {? ZD_POZ.add()
   || _wyn:=1;
      exec('obl_stan','magazyn_stan',ZD_POZ.M,1,ZD_POZ.MG,,,,,,0);
      exec('inf_dod','fakso',0)
   ?};
   exec('war_zam','zamdst_wspolne',ZD_NAG.ref)
|| _wyn:=0;
   {? _ref<>null || ZD_POZ.seek(_ref) ?}
?};
_wyn


\zdpo_d_u
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [20.42]
:: OPIS: dolaczenie rekordu do tabeli ZD_POZ - usługi
::   WY: 1-dolaczono 0-rezygnacja z edycji
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
ZD_POZ.blank();
ZD_POZ.D_POTW:=ZD_POZ.ZD_NAG().D_POTW;

ZD_POZ.MG:=null();
M_ATR.blank();
POMOC.RODZ:='U';

{? D_HELP.M_ZDPOZ<>''
|| M.cntx_psh();
   M.index('ARODZ'); M.prefix('T','U',D_HELP.M_ZDPOZ,D_HELP.M_ZDPOZ);
   {? M.first() || ZD_POZ.M:=M.ref() || D_HELP.M_ZDPOZ:='' ?};
   M.cntx_pop()
?};

M.cntx_psh();
M.index('ARODZ');
M.prefix('T','U',D_HELP.M_ZDPOZ,D_HELP.M_ZDPOZ);
ZD_POZ.win_edit('REDU');
exec('set_efld_opt','zamdst_poz','REDU');

{? ZD_POZ.edit("exec('chk_zd_p','zamdst_poz')")
||
   ZD_POZ.IL_POZ:=ZD_POZ.IL_ZAM+ZD_POZ.IL_KOR;
   ZD_POZ.KH:=ZD_POZ.ZD_NAG().KH;
   ZD_POZ.KH_MSC:=ZD_POZ.ZD_NAG().KH_MSC;
   {? ZD_POZ.add()
   || _wyn:=1;
      exec('inf_dod','fakso',0)
   ?};
   exec('war_zam','zamdst_wspolne',ZD_NAG.ref)
?};
POMOC.RODZ:='T';
M.cntx_pop();

_wyn


\chk_zd_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2010]
:: OPIS: po redakcji pozycji zam. dostaw
::  OLD: \chk_zd_p/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_wyn:={? exec('ae_mzdp','zamdst_poz',0,0) & POMOC.RODZ<>'U'
      || __CHK.record(ZD_POZ,,'M_ZDPOZ','MG','IL','WAL')
      |? exec('ae_mzdp','zamdst_poz',0,0) & POMOC.RODZ='U'
      || __CHK.record(ZD_POZ,,'M_ZDPOZ','IL','WAL')
      || 'M_ZDPOZ'
      ?};
{? _wyn='' & POMOC.RODZ<>'U'
|| {? ATR.FLAG_ED & ATR.FLAG<>1
   || exec('akcepatr','mat_atr',0,1)
   ?}
?};
:: sprawdzenie wdrożeniowe
{? _wyn='' & Plugin.runnable('VAL_POZLOG_001')
|| _wyn:=Plugin.run('VAL_POZLOG_001','ZD_POZ')
?};
_wyn


\ae_mzdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW
:: OPIS: formula po redakcji pola D_HELP.M_ZDPOZ
::   WE: [_a] - 1-sprawdzac powtarzanie indeksu w pozycjach, 0-wpp
::       [_b] - 1-wolane z pola, 0-wpp
::   WY: 1/0
::  OLD: \ae_mzdp/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(1) || _a:=1 ?};
{? var_pres('_b')<>type_of(1) || _b:=1 ?};

ATR.MJS:='ZD_POZ';

_wyn:=0;
{? D_HELP.M_ZDPOZ=''
||
   D_HELP.MR:=null;
   {? POMOC.RODZ='U'
   ||
      exec('find_sql','#table','M','KTM;N;KODK','M.A=\'\'T\'\' and M.RODZ=\'\'U\'\'','D_HELP.MR'
       ,'20@Kod usługi;40@Nazwa usługi;20@Kod kreskowy;','REDU','','')
   ||
      exec('find_sql','#table','M','KTM;N;KODK;*(R)IL','M.A=\'\'T\'\' and M.RODZ=\'\'T\'\'','D_HELP.MR'
       ,'20@Kod materiału;40@Nazwa materiału;20@Kod kreskowy;12@Dostępne;','RED','obl_std#magazyn_stan','')
   ?};
   ZD_POZ.M:=D_HELP.MR; D_HELP.M_ZDPOZ:=ZD_POZ.M().KTM
?};
{? D_HELP.M_ZDPOZ=''
|| FUN.info('Niewypełnione pole.'@);
   _wyn:=0
|| BEER.JMZ:=ZD_POZ.ZD_NAG().JMZ;
   _zmiana:=ZD_POZ.M().KTM<>D_HELP.M_ZDPOZ;
   M.cntx_psh();
   M.index('ARODZ');
   M.prefix('T','T',D_HELP.M_ZDPOZ,D_HELP.M_ZDPOZ);
   {? M.first()
   || ZD_POZ.M:=M.ref();
      {? ~BEER.JMZ || ZD_POZ.JM:=ZD_POZ.M().J ?}; _wyn:=1
   ||
::    dodatkowo poszukujemy pierwszego indeksu wg czesci KTM-u
      {? form(D_HELP.M_ZDPOZ)<>''
      || M.index('ARODZ');
         M.prefix('T',POMOC.RODZ);
         {? M.find_key(D_HELP.M_ZDPOZ)
         || ZD_POZ.M:=M.ref; D_HELP.M_ZDPOZ:=M.KTM; ZD_POZ.JM:=ZD_POZ.M().J; _wyn:=1
         || FUN.info('Brak indeksu w słowniku.'@)
         ?}
      || FUN.info('Brak indeksu w słowniku.'@)
      ?}
   ?};
   M.cntx_pop();
   {? _zmiana
   || {? ZD_POZ.M().J2<>null()
      || ZD_POZ.T2:='M';
         ZD_POZ.WS2:=exec('oblWSP','jm',ZD_POZ.M);
         ZD_POZ.IL2:=0;
         exec('oblJMG','jm',ZD_POZ,'IL',1)
      || ZD_POZ.T2:=1+(1-BEER.DMJM);
         ZD_POZ.WS2:=0;
         ZD_POZ.J2:=null();
         ZD_POZ.IL2:=0
      ?}
   ?};
   exec('ustadprz','zamdst_poz',ZD_POZ.M,ZD_POZ.M().J)
?};
{? _wyn & _a
|| exec('juz_jest','material','D',ZD_POZ.ZD_NAG,ZD_POZ.M,{? ~-(6+menu_txt())='POPRAW' || ZD_POZ.ref() || null ?})
?};
{? _wyn
|| {? ZD_POZ.MG<>null & ZD_POZ.MG().TYP*'DOST'
   || ZD_POZ.btn_eopt('RED','ATRYB','state=normal');
      {? ZD_POZ.M().M_ATR<>null & ZD_POZ.M().M_ATR<>ZD_POZ.DK_C().M_ATR || ZD_POZ.DK_C:=null ?}
   || ZD_POZ.btn_eopt('RED','ATRYB','state=grayed')
   ?}
?};
{? _b & _wyn & DPOZ.WPR_S<>fld()
||
   ZD_POZ.CWAL:=ZD_POZ.CENA:=0;
   exec('zdp_obl','zamdst_poz')
?};
:: ustawienie slownika okienka cech
{? ZD_POZ.M().M_ATR<>null & #ZD_POZ.M().M_ATR<>#ATR.M_ATR
|| {! _i..10 |! ($('ATR.WAR'+form(_i,-2,0,'99')))():='' !};
   ZD_POZ.DK_C:=null
?};

ATR.M_ATR:=ZD_POZ.M().M_ATR;
{? ZD_POZ.M().M_ATR=null() || M_ATR.blank() || ZD_POZ.M().M_ATR().SYM ?};
ATR.FLAG_ED:=(1+ZD_POZ.MG().TYP)='D' & ATR.CZY_ATR & ATR.M_ATR().EDIT;
ATR.FLAG:={? ATR.FLAG_ED & ZD_POZ.M().M_ATR<>null() || 2 || 0 ?};
{? ATR.FLAG_ED || {? ZD_POZ.M().M_ATR<>null() || ATR.FLAG_ED:=2 ?} ?};
exec('set_efld_opt','mat_atr',ATR.MJS);
exec('set_efld_opt','zamdst_poz');
win_disp();
{? _wyn & ZD_POZ.IL=0 || BEER.ORD:=exec('ordIL','jm',ZD_POZ.M,1) ?};
_wyn


\zdpo_dol_gr_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: przed dolacz grupowo okna WER tabeli ZD_POZ, grupowe dodawanie pozycji
::  OLD: \zdpo_dol_gr_m/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? 'MQZ'*ZD_NAG.STAN
||
    {? ZD_NAG.STAN='M' || FUN.info('Zamówienie jest już zamknięte.\nDodawanie pozycji niemożliwe.'@)
    |? ZD_NAG.STAN='Q' || FUN.info('Dla zamówienia odrzucono zapotrzebowanie.\nDodawanie pozycji niemożliwe.'@)
    || FUN.info('Zamówienie jest już zrealizowane.\nDodawanie pozycji niemożliwe.'@)
    ?}
||
   __RODZ:='T'; __M_SEL:='WER_SEL'; __FADDP:='exec(''add_m_zdpo'',''zamdst_poz'')';

   exec('m_gr_poz','material');

   &__RODZ; &__M_SEL; &__FADDP
?}


\add_m_zdpo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: dodaje rekordy do tabeli ZD_POZ na podstawie zaznaczenia
::  OLD: \add_m_zdpo/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;

_mag:=ZD_NAG.MG;
_powielony:=0;
{? _mag=null
||
   {!
   |?
      {? exec('sel_upr','users','MG','SYM')
      || _mag:=USERS_UP.MG;
         {? USERS_UP.MG<>null() & USERS_UP.MG().COS='T'
         || {? USERS_UP.MG().DLAKH<>null() & ZD_NAG.KH<>USERS_UP.MG().DLAKH
            || FUN.info('Wskazano magazyn konsygnacyjny dla %1.\n'
                        'Wybór magazynu niemożliwy.'@[USERS_UP.MG().DLAKH().SKR]);
               _mag:=null()
            |? ~ZD_NAG.KH().COS
            || FUN.info('Nie zdefiniowano obsługi magazynów konsygnacyjnych dla dostawcy: %1.\n'
                        'Wybór magazynu niemożliwy.'@[ZD_NAG.KH().SKR]);
               _mag:=null()
            ?}
         ?}
      || _ok:=FUN.ask('Należy określić magazyn dla dostawy.\nKontynuować?'@)
      ?};
      _ok=1 & _mag=null
   !}
?};

{? _ok=1
|| exec('ini_kom','#message','Informacje o grupowym dołączeniu pozycji.'@);
   {? var_pres('__tab' )<100
   ||
      ZD_POZ.cntx_psh();
      ZD_POZ.index('POZ');
      ZD_POZ.prefix(ZD_NAG.ref);
      ZD_POZ.blank();
      ZD_POZ.M:=M.ref();
      ZD_POZ.MG:={? ZD_POZ.M().RODZ='T' || _mag || null() ?};
      ZD_POZ.JM:=ZD_POZ.M().J;
      ZD_POZ.IL_KOR:=ZD_POZ.IL:=ZD_POZ.IL_POZ:=1;
      exec('juz_jest','material','D',ZD_POZ.ZD_NAG,ZD_POZ.M,null,1);
      ZD_POZ.KH:=ZD_POZ.ZD_NAG().KH;
      ZD_POZ.KH_MSC:=ZD_POZ.ZD_NAG().KH_MSC;
      ZD_POZ.TRN:=ZD_POZ.ZD_NAG().TRN;
      ZD_POZ.POJAZD:=ZD_POZ.ZD_NAG().POJAZD;
      {? ZD_POZ.add()
      ||
         exec('obl_stan','magazyn_stan',ZD_POZ.M,1,ZD_POZ.MG,,,,,,0);
         exec('inf_dod','fakso',0)
      ?};
      ZD_POZ.cntx_pop()
   ||
      ZD_POZ.sel_adel();
      {? __tab.first()
      ||
         {!
         |? M.clear();
            {? M.seek(__tab.REF,)
            || ZD_POZ.cntx_psh();
               ZD_POZ.index('POZ');
               ZD_POZ.prefix(ZD_NAG.ref);
               ZD_POZ.blank();
               ZD_POZ.M:=M.ref();
               ZD_POZ.MG:={? ZD_POZ.M().RODZ='T' || _mag || null() ?};
               ZD_POZ.JM:=ZD_POZ.M().J;
               ZD_POZ.IL_KOR:=ZD_POZ.IL:=ZD_POZ.IL_POZ:=1;
               exec('ustadprz','zamdst_poz',ZD_POZ.M,ZD_POZ.M().J);
               exec('juz_jest','material','D',ZD_POZ.ZD_NAG,ZD_POZ.M,null,1);
               ZD_POZ.KH:=ZD_POZ.ZD_NAG().KH;
               ZD_POZ.KH_MSC:=ZD_POZ.ZD_NAG().KH_MSC;
               ZD_POZ.TRN:=ZD_POZ.ZD_NAG().TRN;
               ZD_POZ.POJAZD:=ZD_POZ.ZD_NAG().POJAZD;
               {? ZD_POZ.add()
               ||
                  exec('obl_stan','magazyn_stan',ZD_POZ.M,1,ZD_POZ.MG,,,,,,0);
                  exec('inf_dod','fakso',0);
                  sel_add()
               ?};
               ZD_POZ.cntx_pop()
            ?};
            __tab.next()
         !}
      ?};
      VAR_DEL.delete('__tab')
   ?};
   exec('war_zam','zamdst_wspolne',ZD_NAG.ref);
   exec('end_kom','#message')
|| VAR_DEL.delete('__tab')
?};
''


\zdpo_rec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: akcja rekord dla pozycji zamowien dostaw
::  OLD: \zdpo_rec/zd.fml
::----------------------------------------------------------------------------------------------------------------------
ZD_POZ.ZD_NAG().T();
{? ZD_POZ.KH=null() || ZD_POZ.KH:=ZD_POZ.ZD_NAG().KH; ZD_POZ.put(1) ?};
{? ZD_POZ.KH_MSC=null() || ZD_POZ.KH_MSC:=ZD_POZ.ZD_NAG().KH_MSC; ZD_POZ.put(1) ?};
ZD_POZ.KH().KOD;
ZD_POZ.MG().SYM;
D_HELP.M_ZDPOZ:=ZD_POZ.M().KTM;
exec('obl_ptw','zamdst_ptw',$ZD_POZ.ref(),1);
POMOC.IL_PTW:=__zdp_sum.IL_PTW;
POMOC.IL_REA:=__zdp_sum.IL_REA;
exec('zd_pst','zamdst_nag');
_usl:={? ZD_POZ.M().RODZ='U' || 'Y' || '' ?};
_grp:=ZD_POZ.sel_size();
_end:=ZD_POZ.END;
{? POMOC.IL_PTW>0 & ~ZD_POZ.sel_size()
|| ZD_POZ.actions_grayed(ZD_POZ.win_sel('?'),_usl+'W'+{? _grp || '' |? ~_end || 'Ź' || 'PUWŻ' ?})
|| ZD_POZ.actions_grayed(ZD_POZ.win_sel('?'),_usl+''+{? _grp || '' |? ~_end || 'Ź' || 'PUWŻ' ?})
?};
exec('rekprzed','color','ZDP#01')


\bl_zwal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS: Wartosc poczatkowa ZD_POZ.WAL
::  OLD: \bl_zwal/eurusd.fml
::----------------------------------------------------------------------------------------------------------------------
ZD_NAG.WAL


\bd_mzdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2009]
:: OPIS: przed wyswietl pola D_HELP.M_ZDPOZ
::  OLD: \bd_mzdp/zd.fml
::----------------------------------------------------------------------------------------------------------------------
''


\be_mzdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: przed redakcja pola D_HELP.M_ZDPOZ
::  OLD: \be_mzdp/zd.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_S:=fld();
ZD_NAG.STAN='N' & {? -(1+menu_txt)='d' || 1 || exec('obl_ptw','zamdst_ptw',$ZD_POZ.ref()); __zdp_sum.IL_PTW=0 ?}


\f3_mzdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW
:: OPIS: formula na f3 pola D_HELP.M_ZDPOZ
::   WY: M.KTM
::  OLD: \f3_mzdp/zd.fml
::----------------------------------------------------------------------------------------------------------------------
:: usuniecie filtru programowego na tabeli jednostek miar
JM.f_clear;
JM.win_dict('WER');

_beermenu:=BEER.MENU_PTH;
_stmag:=ST.MAG;
BEER.MENU_PTH:='ZW';
ZAKR.MATU:='A';
_rodz:={? POMOC.RODZ<>'' || POMOC.RODZ || 'T' ?};
M.index('ARODZ');
M.prefix('T',_rodz);
M.find_key(D_HELP.M_ZDPOZ);
_wer:=
   {? ZD_POZ.MG<>null() | ZD_POZ.ZD_NAG().MG<>null()
   || ST.MAG:={? ZD_POZ.MG<>null() || ZD_POZ.MG || ZD_POZ.ZD_NAG().MG ?};
      'NL_WERST'
   || 'NL_WER'+{? POMOC.RODZ='U' || 'U' || '' ?}
   ?};
M.win_sel(_wer);
M.actions(_wer,'RE:E');
M.actions_grayed(_wer,'N(KC)');
{? M.select(,1,10)
|| D_HELP.M_ZDPOZ:=M.KTM;_wyn:=M.KTM; ZD_POZ.M:=M.ref; ZD_POZ.JM:=ZD_POZ.M().J
|| _wyn:=D_HELP.M_ZDPOZ
?};
BEER.MENU_PTH:=_beermenu;
ST.MAG:=_stmag;
_wyn


\be_zdwce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: przed redakcja ceny walutowej na pozycji zamowienia dostaw
::   WY: 1/0
::  OLD: \be_zdwce/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=exec('spr_npoz','eurusd');
DPOZ.WPR_R:=fld;
{? _wyn=1 & ZD_POZ.WAL=null || _wyn:=0 ?};
{? _wyn & ZD_POZ.sel_size()=0
||
   exec('taz_sd_set','ceny');
   {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?} & ZD_POZ.CWAL=0
   ||
      KALK.JM:=KALK.J2:=ZD_POZ.M().J; KALK.WS2:=1;
      exec('stplicz','ceny',ZD_POZ.IL);
      exec('ceny_mat','ceny_mat',ZD_POZ.M,ZD_NAG.KH,'ZD_POZ.CWAL',0,,'','N',ZD_POZ.WAL,'',,0,,,0,0,0);
      {? ZD_POZ.CWAL || DPOZ.WPR_R:=-1; exec('po_cenaw','ceny',0); DPOZ.WPR_R:=fld ?}
   ?}
?};
_wyn


\f3_zdcen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: F3 pola ZD_POZ.CENA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
TAZ.RABOFF:=1;
exec('f3_zdcen','ceny_mat',ZD_POZ.M,ZD_POZ.ZD_NAG().KH,ZD_POZ.CENA,,'ZD_POZ');
TAZ.RABOFF:=0;
''


\f3_zdwce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: F3 pola ZD_POZ.CWAL
::  OLD: \f3_zdwce/zd.fml
::----------------------------------------------------------------------------------------------------------------------
:: cena walutowa magazynowa na dokumentach przychodowych
TAZ.RABOFF:=1;
exec('f3_zdcen','ceny_mat',ZD_POZ.M,ZD_POZ.ZD_NAG().KH,ZD_POZ.CWAL,,'ZD_POZ',ZD_POZ.WAL);
TAZ.RABOFF:=0;
''


\pwinfatr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: ustawia zmienna dla ZD_POZ
::  OLD: \pwinfatr/mat_atr.fml
::----------------------------------------------------------------------------------------------------------------------
_wg_atr:=ZD_POZ.MG<>null & (1+ZD_POZ.MG().TYP)='D' & (ZD_POZ.M().M_ATR<>null | ZD_POZ.M().IDMOB='A');
ATR.INFATR:={? _wg_atr & ZD_POZ.DK_C=null
            || 'X'
            |? _wg_atr & ZD_POZ.DK_C().UZUP*'0'
            || 'Y'
            |? ZD_POZ.MG<>null & ZD_POZ.IL_POZ>0
            || 'N'
            || ''
            ?}


\zdpo_po1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: popraw dla pozycji zamowien dostaw
::   WY: 1/0 czy poprawiono
::  OLD: \zdpo_po1/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
exec('magazyny','zamdst_wspolne');
{? ZD_POZ.sel_size()=0
||
   {? ZD_POZ.ZRE<>'T' & ZD_POZ.M().RODZ='T'
   ||
      {? (1+ZD_POZ.MG().TYP)='D'
      || ATR.MJS:='ZD_POZ';
         ATR.M_ATR:=ZD_POZ.M().M_ATR;
         ATR.M_ATR().SYM
      || ATR.MJS:='M_ATR';
         M_ATR.blank(0);
         ATR.M_ATR:=null()
      ?};
      ATR.UZUP:=exec('wz_uzup','mat_atr',ATR.M_ATR);
      ATR.FLAG_ED:=(1+ZD_POZ.MG().TYP)='D' & ATR.CZY_ATR & ATR.EDIT;
      {? ZD_POZ.DK_C<>null() & ZD_POZ.DK_C().M_ATR<>null()
      || {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():=($('ZD_POZ.DK_C().WAR'+form(_i,-2,,'99')))() !};
         exec('set_efld_opt','mat_atr',ATR.MJS)
      || {! _i:=1..10 |! ($('ATR.WAR'+form(_i,-2,,'99')))():='' !}
      ?};
      _old_mat:=ZD_POZ.M;
      _old_jm:=ZD_POZ.M().J;
      D_HELP.M_ZDPOZ:=ZD_POZ.M().KTM;
      {? ZD_POZ.MG<>null & ZD_POZ.MG().TYP*'DOST'
      || ZD_POZ.btn_eopt('RED','ATRYB','state=normal')
      || ZD_POZ.btn_eopt('RED','ATRYB','state=grayed')
      ?};
      BEER.JMZ:=ZD_POZ.ZD_NAG().JMZ;
      ZD_POZ.win_edit('RED%1'[{? BEER.JMZ || '2' || '' ?}]);
      exec('set_efld_opt','zamdst_poz','RED');
      {? ZD_POZ.edit("exec('chk_zd_p','zamdst_poz')")
      ||
         ZD_POZ.IL_POZ:=ZD_POZ.IL_KOR+ZD_POZ.IL_ZAM;
         {? _old_mat<>ZD_POZ.M || exec('zdaktprz','zamdst_poz',_old_mat,_old_jm) ?};
         ZD_POZ.US:=exec('operatorUser','#users');
         ZD_POZ.DK:=date();
         ZD_POZ.CK:=time();
         ZD_POZ.cntx_psh();
         ZD_POZ.clear();
         _wyn:=ZD_POZ.put();
         ZD_POZ.cntx_pop();
         {? _wyn || exec('war_zam','zamdst_wspolne',ZD_POZ.ZD_NAG) ?};
         exec('obl_stan','magazyn_stan',ZD_POZ.M,1,ZD_POZ.MG,,,,,,0)
      ?}
   |? ZD_POZ.ZRE<>'T' & ZD_POZ.M().RODZ='U'
   ||
      POMOC.RODZ:='U';
      _old_mat:=ZD_POZ.M;
      _old_jm:=ZD_POZ.M().J;
      D_HELP.M_ZDPOZ:=ZD_POZ.M().KTM;
      ZD_POZ.win_edit('REDU');
      exec('set_efld_opt','zamdst_poz','REDU');
      {? ZD_POZ.edit("exec('chk_zd_p','zamdst_poz')")
      ||
         ZD_POZ.IL_POZ:=ZD_POZ.IL_KOR+ZD_POZ.IL_ZAM;
         {? _old_mat<>ZD_POZ.M || exec('zdaktprz','zamdst_poz',_old_mat,_old_jm) ?};
         ZD_POZ.US:=exec('operatorUser','#users');
         ZD_POZ.DK:=date();
         ZD_POZ.CK:=time();
         ZD_POZ.cntx_psh();
         ZD_POZ.clear();
         _wyn:=ZD_POZ.put();
         ZD_POZ.cntx_pop();
         {? _wyn || exec('war_zam','zamdst_wspolne',ZD_POZ.ZD_NAG) ?};
         exec('obl_stan','magazyn_stan',ZD_POZ.M,1,ZD_POZ.MG,,,,,,0)
      ?};
      POMOC.RODZ:='T'
   ||
      FUN.info('Pozycja jest już realizowana lub zamówienie jest zamknięte.\nModyfikacja niemożliwa.'@)
   ?}
?};
MG.f_clear();
_wyn


\zdaktprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2009]
:: OPIS: po zmianie indeksu sprawdza dane
::   WE: _a - ref starego M
::       _b - ref starego JM
::  OLD: \zdaktprz/przelicz.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZD_POZ.T2='N' | ZD_POZ.T2=''
|| 1
|| {? 'AR'*ZD_POZ.T2 & exec('prz','jm',_a,ZD_POZ.J2,_b)=ZD_POZ.WS2
   || 1
   || ZD_POZ.J2:=ZD_POZ.M().J;
      ZD_POZ.WS2:=1;
      ZD_POZ.IL2:=ZD_POZ.IL_KOR
   ?}
?}


\zdpo_pog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: grupa przed popraw okna WER tabeli ZD_POZ
::   WY: 1 - poprawiac zaznaczone wiersze, 0 - wpp
::  OLD: \zdpo_pog/zd.fml
::----------------------------------------------------------------------------------------------------------------------
exec('magazyny','zamdst_wspolne');
{? var_pres('__tab')>100 || obj_del(__tab) ?};
__tab:=ZD_POZ.sel_aget();

ZD_POZ.blank(1);
ZD_POZ.ZD_NAG:=ZD_NAG.ref();
_win_red:='RED_GR';
:: Tylko ilość
{? ZD_NAG.MG<>null() & ZD_NAG.MG().IL='T'
|| ZD_POZ.efld_opt(_win_red,'enable=0',,'WAL');
   ZD_POZ.efld_opt(_win_red,'enable=0',,'CWAL');
   ZD_POZ.efld_opt(_win_red,'enable=0',,'KRS');
   ZD_POZ.efld_opt(_win_red,'enable=0',,'CENA');
   ZD_POZ.CENA:=ZD_POZ.WAR:=ZD_POZ.CWAL:=ZD_POZ.WARW:=0
|| ZD_POZ.efld_opt(_win_red,'enable=1',,'WAL');
   ZD_POZ.efld_opt(_win_red,'enable=1',,'CWAL');
   ZD_POZ.efld_opt(_win_red,'enable=1',,'KRS');
   ZD_POZ.efld_opt(_win_red,'enable=1',,'CENA')
?};

ZD_POZ.win_edit(_win_red);
{? ZD_NAG.WAL<>INFO.NAROD
|| ZD_POZ.WAL:=ZD_NAG.WAL;
   ZD_POZ.KRS:=ZD_NAG.KRS
|| ZD_POZ.WAL:=INFO.NAROD;
   ZD_POZ.KRS:=0
?};
_mat:=null;
_dokl_c:=0;
ZD_POZ.cntx_psh;
{? ZD_POZ.first
||
   {!
   |?
      {? ZD_POZ.sel_mark & (_dokl_c<ZD_POZ.M().DOKL_C) || _dokl_c:=ZD_POZ.M().DOKL_C; _mat:=ZD_POZ.M ?};
      ZD_POZ.next
   !}
?};
ZD_POZ.cntx_pop;
ZD_POZ.M:=_mat;
_czy_ed:=ZD_POZ.edit();
{? _czy_ed=1
||
   __mg:=ZD_POZ.MG;
   __kh:=ZD_POZ.KH;
   __il_zer:=ZD_POZ.IL_KOR=0 & FUN.ask('Zerować "Ilość użytkownika"?'@);
   __il_kor:=ZD_POZ.IL_KOR;
   __wal:=ZD_POZ.WAL;
   __cwal:=ZD_POZ.CWAL;
   __krs:=ZD_POZ.KRS;
   __cena:=ZD_POZ.CENA
?};
MG.f_clear();
_czy_ed=1


\zdpo_pgp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: grupa po popraw okna WER tabeli ZD_POZ
::  OLD: \zdpo_pgp/zd.fml
::----------------------------------------------------------------------------------------------------------------------
exec('zdpo_po2','zamdst_poz');
VAR_DEL.delete('__tab','__mg','__kh','__il_kor','__il_zer','__wal','__cwal','__krs','__cena')


\zdpo_po2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: popraw grupowe dla pozycji zamowien dostaw
::  OLD: \zdpo_po2/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? var_pres('__tab')>100
||
   ZD_POZ.sel_adel();
   {? __tab.first()
   ||
      {!
      |? {? ZD_POZ.seek(__tab.REF,)
         ||
            {? ZD_POZ.ZRE<>'T' & ZD_POZ.IL_ZRE=0 & ~ZD_POZ.END
            ||
               _dokl_c:=ZD_POZ.M().DOKL_Z;
               {? __mg<>null || ZD_POZ.MG:={? ZD_POZ.M().RODZ='T' || __mg || null() ?} ?};
               {? __kh<>null || ZD_POZ.KH:=__kh ?};
               {? __il_kor<>0 | __il_zer
               || ZD_POZ.IL_KOR:=__il_kor;
                  ZD_POZ.IL:=ZD_POZ.IL_ZAM+ZD_POZ.IL_KOR;
                  exec('aezdpoil','zamdst_poz');
                  exec('ustadprz','zamdst_poz',ZD_POZ.M,ZD_POZ.M().J)
               ?};
               {? __wal<>null & ZD_POZ.MG().IL<>'T' || ZD_POZ.WAL:=__wal ?};
               {? ZD_POZ.MG().IL='T' || ZD_POZ.CWAL:=0 |? __cwal<>0 || ZD_POZ.CWAL:=__cwal$_dokl_c ?};
               {? ZD_POZ.MG().IL='T' || ZD_POZ.KRS:=0 |? __krs<>0 || ZD_POZ.KRS:=__krs ?};
               {? ZD_POZ.MG().IL='T' || ZD_POZ.CENA:=0 |? __cena<>0 || ZD_POZ.CENA:=__cena$_dokl_c ?};
               {? ZD_POZ.WAL=exec('bl_nwal_mg','ustawienia')
               || ZD_POZ.CWAL:=0;
                  ZD_POZ.KRS:=0
               ?};
               {? __cwal<>0 | __krs<>0 | __cena<>0 | ZD_POZ.MG().IL='T' || exec('zdp_obl','zamdst_poz') ?};
               ZD_POZ.US:=exec('operatorUser','#users');
               ZD_POZ.DK:=date();
               ZD_POZ.CK:=time();

               ZD_POZ.cntx_psh();
               ZD_POZ.clear();
               {? ZD_POZ.put() || _wyn:=1; exec('war_zam','zamdst_wspolne',ZD_POZ.ZD_NAG) ?};
               ZD_POZ.cntx_pop()
            ?}
         ?};
         __tab.next()
      !}
   ?}
?};
_wyn


\zdpo_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: usuwa pozycje zamowienia dostaw
::   WY: 1
::  OLD: \zdpo_usu/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_Tab:=ZD_POZ.sel_aget;
{? ~_Tab.first || _Tab.REF:=#ZD_POZ.ref; _Tab.add ?};

_czydel:=1;
_gr:=0;
_txt:={? _Tab.first & _Tab.next || _gr:=1; 'Usunąć zaznaczone pozycje?'@ || 'Usunąć pozycję?'@ ?};
_czytra:=0;

{? _Tab.first
||
   {!
   |?
      {? ZD_POZ.seek(_Tab.REF,)
      ||
         {? ZD_POZ.ZRE<>'T'
         ||
            _czydel:=exec('czy_del_zd4zk','zamowienia',ZD_POZ.ref,_gr)
         ?};
         {? ZD_POZ.IL_ZT>0 || _czytra+=1 ?}
      ?};
      _czydel=1 & _Tab.next
   !}
?};
{? _czytra
|| {? ~_gr
   || _czydel:=0;
      FUN.info('Do pozycji są aktualnie wystawione dyspozycje transportu.\n\nUsunięcie pozycji zabronione.'@)

   || _txt+='\n\nUwaga. Pozycje, do których są aktualnie wystawione dyspozycje transportu zostaną pominięte.'@
   ?}
?};
{? _Tab.first & (_czydel=2 | _czydel=1 & FUN.ask(_txt))
||
   {!
   |?
      {? ZD_POZ.seek(_Tab.REF,)
      ||
         {? ZD_POZ.END | ZD_POZ.IL_ZT>0
         || 'pomijamy'
         |? ZD_POZ.ZRE<>'T'
         ||
            _msg:='';
            exec('obl_ptw','zamdst_ptw',$ZD_POZ.ref());
            {? __zdp_sum.IL_PTW>0
            || _msg+='Pozycja została potwierdzona.\n'@
            |? ZD_POZ.IL_ZRE>0
            || _msg+='Pozycja była już realizowana.\n'@
            ?};
            {? _msg=''
            ||
               _do_state:=do_state();
               {? _do_state=0 || do() ?};
               exec('zdpo2usu','zamdst_poz');
::             przenumeruj pozycje zamowienia
               exec('zdpo_num','zamdst_poz',ZD_NAG.ref);
               {? _do_state=0 || end() ?}
            ?}
         ||
            FUN.info('Pozycja jest już zrealizowana lub zamówienie jest zamknięte.\n'
                'Usuń realizację zamówienia przed usunięciem zamówienia.'@)
         ?}
      ?};
      VAR_DEL.delete('__Lock');
      _Tab.next
   !}
?};

ZD_POZ.sel_adel;
1


\zdpo2usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW:
:: OPIS: wlasciwe usuwanie pozycji zamowienia
::   WE:
::   WY:
::  OLD: \zdpo2usu/zd.fml
::----------------------------------------------------------------------------------------------------------------------
_zd_n:=ZD_POZ.ZD_NAG;
:: DRO_TODO_AWI_OK______: exec('proj2zdp_del','skid_prm',ZD_POZ.ref())
::exec('proj2zdp_del','skid_prm',ZD_POZ.ref());
FAKSO.cntx_psh();
FAKSO.use('fakto'+ST.ODDZ+'__');
exec('delprzyp','fakso','FAKSO','ZD_POZ',ZD_POZ.ref);
FAKSO.cntx_pop();
:: usuwanie polaczenia z koszykiem planu dostaw
exec('del_lnk','plan_dostaw',ZD_POZ.ZD_NAG,ZD_POZ.ref());
exec('obl_ptw','zamdst_ptw',$ZD_POZ.ref());
{? __zdp_sum.IL_PTW>0
|| undo;
   FUN.info('Pozycja %1 została potwierdzona.\nUsunięcie pozycji niemożliwe.'@[$ZD_POZ.POZ])
||
:: usuniecie powiazania z zamowieniami sprzedazy
   exec('del_zkbdelzd','zamowienia');
   _mat:=ZD_POZ.M; _mag:=ZD_POZ.MG;
   ZD_POZ.del;
   exec('obl_stan','magazyn_stan',_mat,1,_mag,,,,,,0);
   exec('war_zam','zamdst_wspolne',_zd_n)
?}


\zdpo_num
::----------------------------------------------------------------------------------------------------------------------
::  UTW:
:: OPIS: przenumerowanie pozycji zamowienia
:    WE: _a - ref zamowienia (ZD_NAG.ref)
::  OLD: \zdpo_num/zd.fml
::----------------------------------------------------------------------------------------------------------------------
ZD_POZ.cntx_psh();
ZD_POZ.index('POZ');
ZD_POZ.prefix(_a);
{? ZD_POZ.first()
|| _poz:=0;
   {!
   |? ZD_POZ.POZ:=(_poz+=1);
      ZD_POZ.put();
      ZD_POZ.next()
   !}
?};
ZD_POZ.cntx_pop()


\zdnagflt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed akcja szukaj - ustawia filtr na MG
::  OLD: \zdnagflt/zd.fml
::----------------------------------------------------------------------------------------------------------------------
exec('magazyny','zamdst_wspolne');
1


\pozdnagf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: po akcji szukaj - sciaga filtr na MG
::  OLD: \pozdnagf/zd.fml
::----------------------------------------------------------------------------------------------------------------------
MG.f_clear();
1


\zdp_kol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: awi [2008]
:: OPIS: kolorowanie rekordow pozycji zamowien dostaw
::   WY: kolor
::  OLD: \zdp_kol/zd.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZD_POZ.IL_POZ=0         || 'ZDP#01#01'
|? ZD_POZ.END              || 'ZDP#01#04'
|? ZD_POZ.IL_POZ=ZD_POZ.IL || 'ZDP#01#02'
                           || 'ZDP#01#03'
?}


\set_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zaznacza wymagalne pola w pozycji zamówienia dostaw
::   WE: _a - akronim okna redakcji
::----------------------------------------------------------------------------------------------------------------------
_win_red:={? var_pres('_a')=type_of('') || _a || ZD_POZ.win_edit('?') ?};

:: Cena
{? ZD_POZ.WAL=INFO.NAROD
|| ZD_POZ.efld_opt(_win_red,'enable=0',,'CWAL');
   ZD_POZ.efld_opt(_win_red,'enable=0',,'KRS')
|| ZD_POZ.efld_opt(_win_red,'enable=1',,'CWAL');
   ZD_POZ.efld_opt(_win_red,'enable=1',,'KRS')
?};

:: Projekt
{? ZD_POZ.PROJEKTY=null() & (ZD_NAG.T().PROJZAKR='' | TYPYZAM.PROJZAKR=exec('projzakr_nie_dotyczy','projekty'))
|| ZD_POZ.efld_opt(_win_red,'enable=0',,'PROJEKTY')
|| ZD_POZ.efld_opt(_win_red,'enable=1',,'PROJEKTY')
?};
_noed:=exec('get','#params',100702,2)='N';
:: Tylko ilość
{? ZD_POZ.MG<>null() & ZD_POZ.MG().IL='T' & _noed
|| ZD_POZ.efld_opt(_win_red,'enable=0',,'WAL');
   ZD_POZ.efld_opt(_win_red,'enable=0',,'CWAL');
   ZD_POZ.efld_opt(_win_red,'enable=0',,'KRS');
   ZD_POZ.efld_opt(_win_red,'enable=0',,'CENA');
   ZD_POZ.efld_opt(_win_red,'enable=0',,'WAR');
   ZD_POZ.efld_opt(_win_red,'enable=0',,'WARW');
   ZD_POZ.efld_opt(_win_red,'enable=0',INFO,'NAROD');
   ZD_POZ.efld_opt(_win_red,'enable=0',ZD_NAG,'WAL');
   ZD_POZ.CENA:=ZD_POZ.WAR:=ZD_POZ.CWAL:=ZD_POZ.WARW:=0
|| ZD_POZ.efld_opt(_win_red,'enable=1',,'WAL');
   ZD_POZ.efld_opt(_win_red,'enable=1',,'CWAL');
   ZD_POZ.efld_opt(_win_red,'enable=1',,'KRS');
   ZD_POZ.efld_opt(_win_red,'enable=1',,'CENA');
   ZD_POZ.efld_opt(_win_red,'enable=1',,'WAR');
   ZD_POZ.efld_opt(_win_red,'enable=1',,'WARW');
   ZD_POZ.efld_opt(_win_red,'enable=1',INFO,'NAROD');
   ZD_POZ.efld_opt(_win_red,'enable=1',ZD_NAG,'WAL')
?};
''


\zdpoz_wal_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Po redakcji ZD_POZ.WAL
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('ae_poz_wal','ceny','W')
|| exec('set_efld_opt','zamdst_poz');
   1
?}


\zd_poz_all_rek
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: akcja rekord dla pozycji zamowien dostaw w okienku ALL
::----------------------------------------------------------------------------------------------------------------------
ZD_POZ.ZD_NAG().SYM;
exec('zd_pst','zamdst_nag');
DPOZ.MG1:={? ZD_POZ.MG<>null() || ZD_POZ.MG().SYM || ZD_POZ.ZD_NAG().MG().SYM ?};
''


\find_zdpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.22]
:: OPIS: poszukuje pozycji ZD_POZ na zamowieniu
::   WE: _a - ref ZD_NAG
::       _b - indeks materialowy
::       _c - ilosc
::       _d - czy aktualizacja pozycji niezalezna od ilosci = szuka maksymalnej w stosunku do danej
::   WY: - ref ZD_POZ lub null
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
_max:=0;
ZD_POZ.cntx_psh();
ZD_POZ.index('DAM');
ZD_POZ.prefix(_a,_b);
{? ZD_POZ.first()
|| {!
   |? {? ZD_POZ.IL_POZ>=_c
      || _wyn:=ZD_POZ.ref();
         0
      |? _d & _max<ZD_POZ.IL_POZ
      || _wyn:=ZD_POZ.ref();
         _max:=ZD_POZ.IL_POZ;
         ZD_POZ.next()
      || ZD_POZ.next()
      ?}
   !}
?};
ZD_POZ.cntx_pop();
_wyn


\pr_d_potw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.02]
:: OPIS: przed redakzją pola ZD_POZ.D_POTW
::   WY: 1-można redagować, 0-nie
::----------------------------------------------------------------------------------------------------------------------
exec('obl_ptw','zamdst_ptw',$ZD_POZ.ref(),1);
__zdp_sum.IL_PTW<=0


\bezdptrn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2011]
:: OPIS: przed redakcja ZK_P.TRN
::  OLD: \bezkptrn/zk.fml
::----------------------------------------------------------------------------------------------------------------------
{? ZD_POZ.TRN='T'
|| ~exec('czyTRANSPORT','transport_wspolne',ZD_POZ.ZD_NAG().uidref(),ZD_POZ.uidref())
|| 1
?}


\pr_jmzdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: przed redakcją pola ZD_POZ.JM ustawia okienko słownika
::----------------------------------------------------------------------------------------------------------------------
{? ZD_POZ.M=null() | ZD_POZ.T2='G'
|| 0
|? (1+BEER.DMJM)<>'T'
|| exec('po_jmzdp','zamdst_poz');
   0
|| exec('ustadprz','zamdst_poz',ZD_POZ.M,ZD_POZ.M().J);
   {? JM.f_active() || JM.f_first() & JM.f_next() || JM.first() & JM.next() ?}
?}


\po_jmzdp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: po redakcji pola ZD_POZ.JM
::----------------------------------------------------------------------------------------------------------------------
_jmg:=ZD_POZ.M().J2;
{? _jmg<>null()
|| ZD_POZ.JM:=ZD_POZ.M().J;
   ZD_POZ.J2:=ZD_POZ.M().J2
|? ZD_POZ.JM=null()
 | ZD_POZ.J2=null()
 | ZD_POZ.M=null()
 | ZD_POZ.M & ZD_POZ.JM=ZD_POZ.M().J
 | ZD_POZ.T2='N'
|| ZD_POZ.J2:=ZD_POZ.JM:={? ZD_POZ.M || ZD_POZ.M().J || ZD_POZ.JM ?};
   ZD_POZ.WS2:=1;
   ZD_POZ.IL2:=ZD_POZ.IL
|| _jmz:=ZD_POZ.ZD_NAG().JMZ;
   _jm2:=ZD_POZ.J2;
   _prz:=ZD_POZ.WS2;
   M.cntx_psh;
   JM.cntx_psh;
   _mat:=ZD_POZ.M;
   _jm:=ZD_POZ.JM;
   BEER.KJM:=ZD_POZ.JM().KOD;
   JM.fld_opt('WERF','col_name="= Ilość [%1]"'[BEER.KJM],BEER,'IL2');
   _ile:=ZD_POZ.IL2;

   BEER.PRZ:={? _jmz || exec('prz','jm',_mat,_jm2,_jm) || exec('prz','jm',_mat,_jm2,_jm) ?};
   BEER.IL2:=_ile*BEER.PRZ $ exec('jaka_dok_m','jm',_mat);
   JM.cntx_pop;
   M.cntx_pop;
   {? ~BEER.PRZ & ZD_POZ.T2='A'
   || ZD_POZ.J2:=ZD_POZ.JM:={? ZD_POZ.M || ZD_POZ.M().J || ZD_POZ.JM ?};
      ZD_POZ.WS2:=1;
      ZD_POZ.IL2:=ZD_POZ.IL
   |? (ZD_POZ.T2='A' | (ZD_POZ.T2='R' & ZD_POZ.WS2=1))
    & (ZD_POZ.T2='A'
     | (BEER.PRZ<>0 & ZD_POZ.WS2<>BEER.PRZ
      & FUN.ask('Obliczony współczynnik przeliczenia jest różny od wprowadzonego.\n'
                'Czy zmienić współczynnik przeliczenia?'@)))
   || ZD_POZ.WS2:=BEER.PRZ
   ?}
?};
exec('aktdil2','zamdst_poz');
1


\end_zd_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: zamknięcie pozycji do realizacji dla zamówień
::   WE: [_a] - 0(domyślnie)-zamknięcie 1-przywrócenie do realizacji
::----------------------------------------------------------------------------------------------------------------------
_sel:=ZD_POZ.sel_aget();
_symz:=ZD_NAG.SYM;
_end:={? var_pres('_a')=type_of(0) || ~_a || 1 ?};

ZD_POZ.sel_adel();
ZD_POZ.cntx_psh();
{? _sel.first()
|| {? {? _end
      || FUN.ask('Czy zamknąć realizację wskazanych pozycji zamówienia %1?'@[_symz])
      || FUN.ask('Czy przywrócić do realizacji wskazane pozycje zamówienia %1?'@[_symz])
      ?}
   || {!
      |? {? (ZD_POZ.prefix(); ZD_POZ.seek(_sel.REF,))
         || {? _end & ~ZD_POZ.END
            || ZD_POZ.END:=1;
               ZD_POZ.put(1)
            |? ~_end & ZD_POZ.END
            || ZD_POZ.END:=0;
               ZD_POZ.put(1)
            ?}
         ?};
         _sel.next()
      !}
   ?}
|? {? ~ZD_POZ.END
   || FUN.ask('Czy zamknąć realizację danej pozycji zamówienia %1?'@[_symz])
   || FUN.ask('Czy przywrócić do realizacji daną pozycję zamówienia %1?'@[_symz])
   ?}
|| _end:=~ZD_POZ.END;
   {? _end & ~ZD_POZ.END
   || ZD_POZ.END:=1;
      ZD_POZ.put(1)
   |? ~_end & ZD_POZ.END
   || ZD_POZ.END:=0;
      ZD_POZ.put(1)
   ?}
?};
ZD_POZ.cntx_pop();
obj_del(_sel);
~~


\end_zdp_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: zamknięcie pozycji do realizacji dla potwiedzeń dostaw
::   WE: [_a] - 0(domyślnie)-zamknięcie 1-przywrócenie do realizacji
::----------------------------------------------------------------------------------------------------------------------
_sel:=ZDP_POZ.sel_aget();
_symz:=ZDP_NAG.SYM;
_end:={? var_pres('_a')=type_of(0) || ~_a || 1 ?};

ZDP_POZ.sel_adel();
ZDP_POZ.cntx_psh();
{? _sel.first()
|| {? {? _end
      || FUN.ask('Czy zamknąć realizację wskazanych pozycji potwierdzenia %1?'@[_symz])
      || FUN.ask('Czy przywrócić do realizacji wskazane pozycje potwierdzenia %1?'@[_symz])
      ?}
   || {!
      |? {? (ZDP_POZ.prefix(); ZDP_POZ.seek(_sel.REF,))
          & ZDP_POZ.ZD_POZ<>'' & ~exec('FindAndGet','#table',ZD_POZ,ZDP_POZ.ZD_POZ,,"END",0)
         || {? _end & ~ZDP_POZ.END
            || ZDP_POZ.END:=1;
               ZDP_POZ.put(1)
            |? ~_end & ZDP_POZ.END
            || ZDP_POZ.END:=0;
               ZDP_POZ.put(1)
            ?}
         ?};
         _sel.next()
      !}
   ?}
|? ZDP_POZ.ZD_POZ<>'' & exec('FindAndGet','#table',ZD_POZ,ZDP_POZ.ZD_POZ,,"END",0)
|| FUN.info('Pozycja zamknięta do realizacji na zamówieniu dostaw.\nZmiana zamknięcia niemożliwa.'@)
|? {? ~ZDP_POZ.END
   || FUN.ask('Czy zamknąć realizację danej pozycji potwierdzenia %1?'@[_symz])
   || FUN.ask('Czy przywrócić do realizacji daną pozycję potwierdzenia %1?'@[_symz])
   ?}
|| _end:=~ZDP_POZ.END;
   {? _end & ~ZDP_POZ.END
   || ZDP_POZ.END:=1;
      ZDP_POZ.put(1)
   |? ~_end & ZDP_POZ.END
   || ZDP_POZ.END:=0;
      ZDP_POZ.put(1)
   ?}
?};
ZDP_POZ.cntx_pop();
obj_del(_sel);
~~

\zd_poz_m_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Po redakcji ZD_POZ.M
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
D_HELP.M_ZDPOZ:={? ZD_POZ.M<>null || ZD_POZ.M().KTM || '' ?};
1


\zd_poz_cena2_bd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Przed wyświetleniem ZD_POZ.C2
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('pwdj2','zamdst_poz')


\zd_poz_cena2_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Przed redkacją ZD_POZ.C2
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ZD_POZ.T2='A' | ZD_POZ.T2='R' | ZD_POZ.T2='M'
||
   DPOZ.WPR_R:=fld();
   {? exec('spr_npoz','eurusd')
   ||
      _wyn:=1;
      {? _wyn=1 & ZD_POZ.WAL=null || _wyn:=0 ?};
      {? _wyn & ZD_POZ.sel_size()=0
      ||
         exec('taz_sd_set','ceny');
         {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?} & ZD_POZ.CENA2=0
         ||
            KALK.JM:=KALK.J2:=ZD_POZ.J2; KALK.WS2:=1;
            exec('stplicz','ceny',ZD_POZ.IL);
            exec('ceny_mat','ceny_mat',ZD_POZ.M,ZD_NAG.KH,'ZD_POZ.CENA2',0,,'','N',ZD_POZ.WAL,'',,0
             ,{? ZD_POZ.J2<>null || ZD_POZ.J2().KOD || '' ?}
             ,{? ZD_POZ.J2<>null & ZD_POZ.WS2<>0 || 1/ZD_POZ.WS2 || 0 ?}
             ,0,0,0);
            {? ZD_POZ.CENA2 || DPOZ.WPR_R:=-1; exec('zd_poz_cena2_ae','zamdst_poz',0); DPOZ.WPR_R:=fld ?}
         ?}
      ?};
      _wyn

   |? exec('czy_rwal','eurusd','W') | ZD_NAG.KRS=0
   ||
      _wyn:=1;
      {? _wyn & ZD_POZ.sel_size()=0
      ||
         exec('taz_sd_set','ceny');
         {? {? exec('ctdt','ceny')<>'T' || exec('fo8008x1','ceny')='T' || exec('fo8008x3','ceny')='T' ?} & ZD_POZ.CENA2=0
         ||
            KALK.JM:=KALK.J2:=ZD_POZ.J2; KALK.WS2:=1;
            exec('stplicz','ceny',ZD_POZ.IL);
            exec('ceny_mat','ceny_mat',ZD_POZ.M,ZD_NAG.KH,'ZD_POZ.CENA2',0,,'','N',INFO.NAROD,'',,0
             ,{? ZD_POZ.J2<>null || ZD_POZ.J2().KOD || '' ?}
             ,{? ZD_POZ.J2<>null & ZD_POZ.WS2<>0 || 1/ZD_POZ.WS2 || 0 ?}
             ,0,0,0);
            {? ZD_POZ.CENA2 || DPOZ.WPR_R:=-1; exec('zd_poz_cena2_ae','zamdst_poz',0); DPOZ.WPR_R:=fld; win_disp() ?}
         ?}
      ?};
      _wyn
   ?}
?}


\zd_poz_cena2_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Po redakcji ZD_POZ.CENA2
::   WE: [_a] - 0/1 przeliczaj
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0) || _a:=0 ?};
_wyn:=1;
{? (_a | DPOZ.WPR_R<>fld()) & ZD_POZ.WS2>0
||
   _dokl_c:=ZD_POZ.M().DOKL_C;
   {? ZD_POZ.CENA2<0
   ||
      FUN.info('Cena musi być dodatnia.'@);
      _wyn:=0

   |? ZD_POZ.WAL<>INFO.NAROD
   ||
      ZD_POZ.CWAL:=(ZD_POZ.CENA2/ZD_POZ.WS2)$_dokl_c;
      _wyn:=params_exec('po_cenaw','ceny')
   ||
      ZD_POZ.CENA:=(ZD_POZ.CENA2/ZD_POZ.WS2)$_dokl_c;
      _wyn:=params_exec('zezdp_ce','zamdst_poz')
   ?}
?};
_wyn


\zd_poz_cena2_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: F3 ZD_POZ.CENA2
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: cena walutowa magazynowa na dokumentach przychodowych
TAZ.RABOFF:=1;
exec('f3_zdcen','ceny_mat',ZD_POZ.M,ZD_POZ.ZD_NAG().KH,ZD_POZ.CENA2,,'ZD_POZ2',ZD_POZ.WAL);
TAZ.RABOFF:=0;
''


\zdpo_rea
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: akcja rekord dla pozycji zamowien dostaw od strony realizacji
::----------------------------------------------------------------------------------------------------------------------
exec('zdpo_rec','zamdst_poz');
exec('rekprzed','color','ZDP#02')


\zdp_kol_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: kolorowanie rekordow pozycji zamowien dostaw wybranych do realizacji
::   WY: kolor
::  OLD: \zdp_kol/zd.fml
::----------------------------------------------------------------------------------------------------------------------
__rea_tab.cntx_psh();
__rea_tab.prefix();
_is:=__rea_tab.first() & __rea_tab.find_tab('first','ZD_POZ',,'=',$ZD_POZ.ref());
__rea_tab.cntx_pop();

{? _is || 'ZDP#02#01' || '' ?}


\zdp_win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [23.25]
:: OPIS: Zwraca okno redakcji tabeli ZD_POZ
::   WE: [_a] - akronim okna
::   WY: akronim okna redakcji
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('')
|| {? ZK_P.M().RODZ='U'
   || _a:='REDU'
   || _a:='RED'
   ?}
?};

_win_akr:=_a;
_dokum_view:=0;
_dokum:=exec('dokum_seek','zbl',ZD_NAG);
_dokum_view:=_dokum & DOKUM.DOKUMI;
{? _dokum_view
:: Tymczasowe okno redakcji
|| _win_red:=ZD_POZ.mk_edit('Pozycja zamówienia'@,,,,,'html_maximized');
   ZD_POZ.win_etab(_win_red,'Dane podstawowe'@);
   ZD_POZ.win_ewin(_win_red,,_win_akr);
   ZD_POZ.win_etab(_win_red,'Podgląd'@);
   ZD_POZ.win_ewin(_win_red,DOKUM,'DOKUMI');
   exec('ok_esc','#window',ZD_POZ,_win_red,1);
   _win_red
|| _win_akr
?}


:Sign Version 2.0 jowisz:1045 2023/10/26 13:13:50 214bb6a12134840191c84c1036566d1d671bff92c92093e9db719ab33ebd6ee4685ed9b31814624c503fbe2f80967fd076f23850e2acd35e65ad70d51825347581d8f0c04acf1f70f8aded61161c76d46eb21bdb76d559138c6dec463fa66d151b0c5b05db4b16017cfc9ce26d17dd1cfa4c53fb01229a42996def0e2de63ccc

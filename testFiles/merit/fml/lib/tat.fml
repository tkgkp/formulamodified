:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: tat.fml
:: Utworzony: 29.01.2015
:: Autor: TMR
::======================================================================================================================
:: Zawartosc: Formuly do obslugi typow atrybutow
::======================================================================================================================

\exist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja sprawdza czy istnieje atrybut z podana nazwa
::   WE: _a - [STRING] - nazwa atrybutu
::   WY: null lub wskazanie na TAT
::----------------------------------------------------------------------------------------------------------------------
_nazwa:={? var_pres('_a')=type_of('') || _a || return(null) ?};
_res:=null();

TAT.cntx_psh();
TAT.index('NA');
TAT.prefix(_nazwa,);
{? TAT.first()
|| _res:=TAT.ref()
?};
TAT.cntx_pop();
_res


\last_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja zwraca ostatni numer atrybutu
::   WY: numer
::----------------------------------------------------------------------------------------------------------------------
0


\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja dodaje atrybut
::   WE: _a  - [OBJECT]  - obiekt bufora tabeli TAT
::      [_b] - [INTEGER] - wymuś dodanie/nadpisanie nowymi wartościami
::   WY: null lub wskazanie na TAT
::----------------------------------------------------------------------------------------------------------------------
_buffer:={? var_pres('_a')=117        || _a || return(null) ?};
 _force:={? var_pres('_b')=type_of(0) || _b || 0            ?};

_mydo:=do_state()=0;
_res:=null();

_res:=exec('exist','tat',_buffer.NA);
{? _res=null()
|| TAT.cntx_psh();
   TAT.blank(1);
   TAT.MIEJSCE:='W';
   _buffer.set();
   TAT.NR:=0;
   {? TAT.add(~_mydo)
   || _res:=TAT.ref()
   ?};
   TAT.cntx_pop()
|| {? _force
   || exec('put','tat',_buffer)
   ?}
?};
_res


\put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [12.50]
:: OPIS: Funkcja poprawia atrybut
::   WE: _a  - [OBJECT]  - obiekt bufora tabeli TAT
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_buffer:={? var_pres('_a')=117        || _a || return(null) ?};

_mydo:=do_state()=0;
_res:=0;

TAT.cntx_psh();
TAT.index('NA');
TAT.prefix(_buffer.NA,);
{? TAT.first()
|| _buffer.set();
   {? TAT.put(~_mydo)
   || _res:=1
   ?}
?};
TAT.cntx_pop();
_res


\bl_miejsce
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Wartość początkowa pola TAT.MIEJSCE
::----------------------------------------------------------------------------------------------------------------------
{? POMOC.TAT_M<>'' || POMOC.TAT_M || 'L' ?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 d6a6d32639890a12ff733c4baeec4c1f18d03a7c70c9d5554fa06d735baf713378839d37b7bb1270eae978d211851d681a3b85fb51b2ade0e8dd521d04ab6a1ce1030a02535bb9cbd195b435f52d0e4e8c6f4734aa178b53110c000db23f8c42a847a14c793d0569ecba22b8d6b0553a521cc96e0297c7d5a08581de7b2ae030

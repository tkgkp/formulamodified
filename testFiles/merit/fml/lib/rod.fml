:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: rod.fml
:: Utworzony: 16.05.2018
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Formuły oraz obszary robocze dziedziny ROD.
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Formuła inicjująca dla dziedziny ROD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('__KAL','object');
exec('__RUB','object');
exec('__HARM','object');
exec('__F_ZATR','object');
exec('MASK','object');
exec('DataMask','prc');
exec('rodo_decl','ro_class');
exec('lic','zam_okr_rok');
~~


\rod_req
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.22]
:: OPIS: Obsługa obszaru roboczego ROD_REQ - Obsługa żądań RODO.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init','rod');

AreaTitle.setTitle();

_RODO:=obj_new(@.CLASS.RODO);
params_set('RODO',_RODO);

:: Definicja zakładek, z których będzie zbudowane okno obszaru roboczego.
:: Każdy element tablicy _cfg opisuje jedną zakładkę. Znaczenie parametrów:
::    UID      - Identyfikator czynności.
::    RU       - Czynność wymaga współpracy RODO Utility.
::    TITLE    - Tytuł okna / zakładki
::    ENABLE   - Czy zakładka jest dostępna (parametr techniczny ).
_cfg:=exec('obj_ntab_set','#array',,
   'call',exec('obj_ntab_set','#array',,'UID','ROD_REQ_CALL','RU',0,'TITLE','Wywołanie żądania'@,'ENABLE',0),
   'rhis',exec('obj_ntab_set','#array',,'UID','ROD_REQ_RHIS','RU',1,'TITLE','Żądania do odpowiedzi'@,'ENABLE',0),
   'rret',exec('obj_ntab_set','#array',,'UID','ROD_REQ_RRET','RU',1,'TITLE','Żądania retencji'@,'ENABLE',0)
);

:: Czy współpraca z RU (na podstawie parametru)?
_parRU:=1;

_ile:=0;
{! _lp:=1 .. obj_len(_cfg)
|! _ile+=_cfg[_lp].ENABLE:=(~_cfg[_lp].RU | _parRU) & exec('chk_role','#b__box',OPERATOR.USER,_cfg[_lp].UID)
!};

RO_CFG.cntx_psh();
RO_PERM.cntx_psh();
RO_PERM.prefix();
RO_REQT.cntx_psh();
RO_REQT.prefix();
RO_REQN.cntx_psh();
RO_REQN.win_sel('WER');
RO_REQR.cntx_psh();
RO_REQR.win_edit('RED');

{? _ile>1 | ~_cfg.call.ENABLE
:: Okna grupowego nie będziemy tworzyli, jeżeli mamy pokazać tylko okno redagowania.
|| _grp:=RO_CFG.grp_make('Obsługa żądań RODO'@,,,,,"exec('exit','zws')",,'maximized')
?};

_mode:='maximized_with_title';
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
{? _cfg.call.ENABLE
|| {? _ile=1
   || {? (_rkey:=_RODO.edit())<>''
      || exec('call','rodo',_rkey,_RODO)
      ?}
   || _TBUF:=_RODO.TBUF;
      _fml:="exec('call','rodo',_a,params_get().RODO)";
      _we:=_RODO.get_we(_fml);
      RO_CFG.grp_edit(_grp,_TBUF,_we,{? _ile=1 || '' || _cfg.call.TITLE ?},,
         "  RO_REQN.cntx_psh();
            RO_REQN.prefix();
            RO_REQT.cntx_psh();
            RO_REQT.index('MNAME');
            RO_REQT.prefix('T');
            RO_REQT.fld_fml('MANUAL','BLANK',""'T'"");
            RO_REQN.fld_fml('RO_REQT','BEFORE_EDIT',$$(RO_REQN.RO_REQT=null()))
         ",
         "  RO_REQN.fld_fml('RO_REQT','BEFORE_EDIT',""*"");
            RO_REQT.cntx_pop();
            RO_REQT.fld_fml('MANUAL','BLANK',""*"");
            RO_REQN.cntx_pop()
         ",,_mode
      )
   ?}
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
{? _cfg.rhis.ENABLE
|| RO_CFG.grp_sel(_grp,RO_REQN,'WER',{? _ile=1 || '' || _cfg.rhis.TITLE?},
      "  _hide:='';
         RO_REQR.index('RO_REQN');
         {? RO_REQN.get()
         || RO_REQR.prefix(RO_REQN.ref());
            {? RO_REQN.STATUS<>'N'
            || _hide:='P'
            ?}
         || RO_REQR.prefix(null)
         ?};
         RO_REQR.actions('WER',_hide,,1);
         grp_disp(RO_REQR,'WER',,1)
      ",,,,"
         RO_REQN.hdr_sel();
         RO_REQN.hdr_sel('Żądania do wykonania'@);
         RO_REQN.actions('WER','',,1);
         exec('ro_reqn_prfx','rodo')
      ",,,,_mode
   );
   {? _ile=1
   || RO_CFG.grp_splt(_grp,,'horizontal','bottom',10)
   || RO_CFG.tab_splt(_grp,,'horizontal','bottom',10)
   ?};
   RO_CFG.grp_sel(_grp,RO_REQR,'WER',,,,,,
      "  {? grp_empty(RO_REQN,'WER',1)
         || '#disable'
         ?}
      ",,,,
      _mode
   )
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
{? _cfg.rret.ENABLE
|| RO_CFG.grp_sel(_grp,RO_REQN,'WER',{? _ile=1 || '' || _cfg.rret.TITLE?},
      "  _hide:='';
         RO_REQR.index('RO_REQN');
         {? RO_REQN.get()
         || RO_REQR.prefix(RO_REQN.ref());
            {? RO_REQN.STATUS<>'N'
            || _hide:='P'
            ?}
         || RO_REQR.prefix(null)
         ?};
         RO_REQR.actions('WER',_hide,,1);
         grp_disp(RO_REQR,'WER',,1)
      ",,,,"
         RO_REQN.hdr_sel();
         RO_REQN.hdr_sel('Żądania retencji'@);
         RO_REQN.actions('WER','ZAN:Z',,1);
         RO_REQN.index('RO_REQT');
         RO_REQN.prefix(exec('find_ro_reqt','ro_req','PERSONAL_DATA_REMOVE_RET'))
      ",,,,_mode
   );
   {? _ile=1
   || RO_CFG.grp_splt(_grp,,'horizontal','bottom',10)
   || RO_CFG.tab_splt(_grp,,'horizontal','bottom',10)
   ?};
   RO_CFG.grp_sel(_grp,RO_REQR,'WER',,,,,,
      "  {? grp_empty(RO_REQN,'WER',1)
         || '#disable'
         ?}
      ",,,,
      _mode
   )
?};
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
{? _ile>1 | ~_cfg.call.ENABLE
|| exec('ro_reqn_prfx','rodo');
   RO_CFG.win_sel(_grp);
   RO_CFG.select()
?};

RO_REQR.cntx_pop();
RO_REQN.cntx_pop();
RO_REQT.cntx_pop();
RO_PERM.cntx_pop();
RO_CFG.cntx_pop();

~~


\rod_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.02]
:: OPIS: Formuła sprawdzająca licencję dla obszaru FKS
::   WY: 'T' - jest licencja, 'N' - nie ma licencji
::----------------------------------------------------------------------------------------------------------------------
_result:='N';
_domain:=exec('domain_ref','#b_domain','ROD');
{? _domain<>null()
|| {? exec('lic','#b_domain',_domain)=1
   || _result:='T'
   ?}
?};
_result

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:23 c5eaeceaba4ce7f331903c21d63392e79cb72864e6f6bdf8a670bf6a3675ae025071adff36ff314a3f3a806571c89800f5db359884e80ddc119ba3a6caab8524462a96e160b9a37f7cb146f7576d36dfc462e205b84535a5a056b1e7931bd751788a5523298a2061b681a74f40829595313a270c088021e9a1a6e56a697fe6c9

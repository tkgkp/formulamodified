:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: plob_fo.fml
:: Utworzony: 17.04.2018
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Formuły do obsługi tabeli formuł wykorzystywanych do definiowania obiektu lokalizacji
::======================================================================================================================

\add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Dodaje rekord formuły do PLOB_FO
::   WE: _a [STRING] - nazwa formuły
::       _b [STRING] - treść formuły
::   WY: ref/null
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
_nazwa:={? var_pres('_a')=type_of('') & +_a<=60  || _a || return(_result) ?};
_tresc:={? var_pres('_b')=type_of('') & +_a<=255 || _b || return(_result) ?};

_firma:=exec('ref_firma','#firma');
PLOB_FO.cntx_psh();
PLOB_FO.index('UNIQUE');
PLOB_FO.prefix(_firma);
PLOB_FO.blank(1);
PLOB_FO.FIRMA:=_firma;
PLOB_FO.NAZWA:=_nazwa;
_result:={? PLOB_FO.find_rec()
         || PLOB_FO.ref()
         || PLOB_FO.TRESC:=_tresc;
            {? PLOB_FO.add() || PLOB_FO.ref() || null() ?}
         ?};
PLOB_FO.cntx_pop();
_result


\put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Poprawia rekord formuły do PLOB_FO.
::   WE: _a [STRING] - nazwa formuły
::       _b [STRING] - treść formuły
::   WY: ref/null
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
_nazwa:={? var_pres('_a')=type_of('') & +_a<=60  || _a || return(_result) ?};
_tresc:={? var_pres('_b')=type_of('') & +_a<=255 || _b || return(_result) ?};

_firma:=exec('ref_firma','#firma');
PLOB_FO.cntx_psh();
PLOB_FO.index('UNIQUE');
PLOB_FO.prefix(_firma,_nazwa);
_result:={? PLOB_FO.first()
         || PLOB_FO.TRESC:=_tresc;
            {? PLOB_FO.put() || PLOB_FO.ref() || null() ?}
         ?};
PLOB_FO.cntx_pop();
_result


\update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Aktualizuje rekord formuły do PLOB_FO.
::   WE: _a [STRING] - nazwa formuły
::       _b [STRING] - treść formuły
::   WY: ref/null
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
_nazwa:={? var_pres('_a')=type_of('') || _a || return(_result) ?};
_tresc:={? var_pres('_b')=type_of('') || _b || return(_result) ?};

{? exec('existUnique','plob_fo',_nazwa)
|| exec('put','plob_fo',_nazwa,_tresc)
|| exec('add','plob_fo',_nazwa,_tresc)
?}


\get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Wyszukuje rekord formuły w PLOB_FO
::   WE: _a [STRING] - nazwa formuły
::   WY: ref/null
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
_nazwa:={? var_pres('_a')=type_of('') & +_a<=60 || _a || return(_result) ?};

PLOB_FO.cntx_psh();
PLOB_FO.index('UNIQUE');
PLOB_FO.prefix(exec('ref_firma','#firma'),_nazwa);
{? PLOB_FO.first()
|| _result:=PLOB_FO.ref()
?};
PLOB_FO.cntx_pop();
_result


\plob_fo_dolacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Akcja "Dołącz" przed dla okienka wertowania WER
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PLOB_FO.blank();
PLOB_FO.win_edit('RED');
{? PLOB_FO.edit("exec('check_record','plob_fo','add')")
|| PLOB_FO.add()
?};
~~


\plob_fo_popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Akcja "Popraw" przed dla okienka wertowania WER
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
PLOB_FO.win_edit('RED');
{? PLOB_FO.edit("exec('check_record','plob_fo','put')")
|| PLOB_FO.put()
?};
~~


\check_record
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Formuła sprawdzająca rekord
::   WE:  _a  [STRING] - akcja po której następuje sprawdzenie
::       [_b] [NUMBER] - czy pomijać komunikaty?
::   WY: ''/akronim pola
::----------------------------------------------------------------------------------------------------------------------
_result:='';
 _akcja:={? var_pres('_a')=type_of('') || _a || return(_result) ?};
_no_msg:={? var_pres('_b')=type_of(0)  || _b || ~~              ?};

_result:=__CHK.record(PLOB_FO,_no_msg,'FIRMA','NAZWA','TRESC');

{? _result=''
|| _ref:=exec('existUnique','plob_fo',PLOB_FO.NAZWA);
   {? _akcja='add' & _ref
   || _result:='NAZWA'
   |? _akcja='put' & _ref & _ref<>PLOB_FO.ref()
   || _result:='NAZWA'
   ?};
   {? exec('valid_formula','#field',PLOB_FO.TRESC)=0
   || _result:='TRESC'
   ?};
   {? _result='NAZWA'
   || FUN.emsg(exec('existUniqueMsg','plob_fo'))
   ?}
?};
_result


\existUnique
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Funkcja sprawdza czy już istnieje definicja formuły dla obiektu do planowania na unikalnym indexie
::   WE: _a - [STRING] - wartość pola NAZWA
::   WY: ref/null
::----------------------------------------------------------------------------------------------------------------------
_result:=null();
_nazwa:={? var_pres('_a')=type_of('') || _a || return(_result) ?};

_firma:=exec('ref_firma','#firma');
PLOB_FO.cntx_psh();
PLOB_FO.index('UNIQUE');
PLOB_FO.prefix(_firma,_nazwa,);
{? PLOB_FO.first()
|| _result:=PLOB_FO.ref()
?};
PLOB_FO.cntx_pop();
_result


\existUniqueMsg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.22]
:: OPIS: Zwraca komunikat informujący o istnieniu innej definicji formuły
::   WE:
::   WY: komunikat
::----------------------------------------------------------------------------------------------------------------------
'Istnieje już inna definicja formuły o podanej nazwie.'@

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:57 8fc2ec763386e797f563023b9adc500ddcd5db7dcdbbe1f95db48cc472fd78664e9cc18bd5105926453ecb9aab83f60195f870a6c2aeed2976b34a4429078e58f584e26d4d1b2efe860fb5375c0cf6f7d09ca7ff378cea7d02760bd88a51ae3758d1e78152a270d8168f0c48ad2a6a71a4913cbffe63d81e7aaa6c3da9a8eef1

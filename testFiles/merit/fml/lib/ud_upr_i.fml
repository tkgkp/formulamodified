:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ud_upr_i.fml
:: Utworzony: 27.06.2018
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Plik zawiera formuły umożliwiające przenoszenie uprawnień.
::======================================================================================================================


\export
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy i wyświetla panel eksportu uprawnień do elementów schematów danych.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: utwórz elementy panelu (tabele, okna)
_GRP:=exec('xp_mk_grp','ud_upr_i');
_DOM:=exec('xp_mk_dom','ud_upr_i');
_SYS:=exec('xp_mk_sys','ud_upr_i');
_APP:=exec('xp_mk_app','ud_upr_i');
_SUB:=exec('xp_mk_sub','ud_upr_i');

: okienko główne konfiguracji przeniesienia uprawnień
_wnd:=_GRP.grp_make('Uprawnienia do schematów danych',,'#ud_upr_i');
: lista grup dziedzin produktowych
_GRP.grp_sel(_wnd,_GRP,_GRP.win_sel('?'),,
:  po odświeżeniu
   "  _DOM:=__EXP_UPR.UDB_DOM;
      _SYS:=__EXP_UPR.UDB_SYS;
      grp_disp(_DOM,_DOM.win_sel('?'),1);
      grp_disp(_SYS,_SYS.win_sel('?'),1);
      ~~
   ",
:  położenie, wysokość
   ,,,
:  przed obsługą
   "",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, tryb
   ,,'maximized_with_title'
);
: podziel okno w pionie
_GRP.grp_splt(_wnd,,'vertical','right');
: lista przypisanych dziedzin produktowych
_GRP.grp_sel(_wnd,_DOM,_DOM.win_sel('?'),'Dziedziny',
:  po odświeżeniu
   "  _DOM:=__EXP_UPR.UDB_DOM;
      _DOM.actions(_DOM.win_sel('?'),{? __EXP_UPR.UDB_GRP.size()=0 || 'd:d' || '' ?},,1)
   ",
:  położenie, wysokość
   ,,,
:  przed obsługą
   "cur_tab(1,1).prefix(__EXP_UPR.UDB_GRP.SYMBOL,)",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, tryb
   ,,'maximized'
);
: lista przypisanych typów danych
_GRP.grp_sel(_wnd,_SYS,_SYS.win_sel('?'),'Typy danych',
:  po odświeżeniu
   "  _SYS:=__EXP_UPR.UDB_SYS;
      _SYS.actions(_SYS.win_sel('?'),{? __EXP_UPR.UDB_GRP.size()=0 || 'd:d' || '' ?},,1)
   ",
:  położenie, wysokość
   ,,,
:  przed obsługą
   "cur_tab(1,1).prefix(__EXP_UPR.UDB_GRP.SYMBOL,)",
:  po obsłudze
   "",
:  utrwalenie, aktywacja, tryb
   ,,'maximized'
);
_GRP.win_sel(_wnd);

_buf:=obj_new('UDB_GRP','UDB_DOM','UDB_SYS','SYSTEM','B_DOMAIN');
_buf.UDB_GRP:=_GRP;
_buf.UDB_DOM:=_DOM;
_buf.UDB_SYS:=_SYS;
_buf.SYSTEM:=_APP;
_buf.B_DOMAIN:=_SUB;

UD_TYP.cntx_psh();
: okienko do wyboru typu danych dla emulacji słownika
_wnd:=UD_TYP.mk_sel('Typy','P',0,'#ud_upr_typ',,,,,'U');
UD_TYP.win_fld(_wnd,,'SYMBOL',,,8,,,'Symbol',,'Identyfikator');
UD_TYP.win_fld(_wnd,,'OPIS',,,30,,,'Opis',,'Krótki opis');
UD_TYP.win_act(_wnd,,'Formuła','Ten',,'Wybór bieżącego wiersza',,$'sel_exit()',1);
UD_TYP.win_act(_wnd,,'Szukaj');
UD_TYP.win_act(_wnd,,'Kolejność');
UD_TYP.win_sel(_wnd);

_file:='ud_upr_i.dfg';
: wczytaj konfigurację
{? ~exec('cfg_read','ud_upr_i',_buf,_file)
:  nie udało się wczytać, utwórz początkową
|| exec('cfg_create','ud_upr_i',_buf,_file)
?};

VAR_DEL.delete('__EXP_UPR');
__EXP_UPR:=_buf;
: wyświetl panel konfiguracji
__EXP_UPR.UDB_GRP.select();
VAR_DEL.delete('__EXP_UPR');

: zapisz konfigurację w pliku
exec('cfg_write','ud_upr_i',_buf,_file);

UD_TYP.cntx_pop();
UD_TYP.win_del(_wnd);
~~


\cfg_create
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy domyślną konfigurację eksportu uprawnień do elementów schematów danych.
::   WE: _a [OBJECT] - wskazanie na tablicę konfiguracji
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
: aliasy konfiguracji
_GRP:=_a.UDB_GRP;
_DOM:=_a.UDB_DOM;
_SYS:=_a.UDB_SYS;

: usuń dane
_GRP.erase();
_DOM.erase();
_SYS.erase();

_add:="
   _a.SYMBOL:=_b;
   _a.OPIS:=_c;
   _a.SYSTEM:=_d;
   _a.add()
";

: WYPOSAZ
: utwórz grupy dziedzin produktowych
_add(_GRP,'CTR','Controlling','CONTROL');
_add(_GRP,'FST','Środki trwałe','ESTRA');
_add(_GRP,'FINANSE','Finanse','FIKS');
_add(_GRP,'HBN','Płatności elektroniczne','HOMEBANK');
_add(_GRP,'KON','Konsolidacja sprawozdań','IF');
_add(_GRP,'KAS','Kasa','KASA');
_add(_GRP,'LOGISTYKA','Logistyka','EMAG');
_add(_GRP,'OBIEGI','Obieg wniosków i dokumentów','OBIEGI');
_add(_GRP,'PERSONEL','Personel','KALI');
_add(_GRP,'PRODUKCJA','Produkcja','PRODUKCJA');
_add(_GRP,'PRC','Czas pracy','RCP');
_add(_GRP,'WYP','Wyposażenie','WYPOSAZ');
_add(_GRP,'ZKL','Zarządzanie kapitałem ludzkim','ZKL');
_add(_GRP,'PPK','Pracownicze plany kapitałowe','PPK');
_add(_GRP,'ZDO','Zarządzanie danymi osobowymi','ZDO');
_add(_GRP,'EDP','Elektroniczna dokumentacja pracownicza','EDP');
_add(_GRP,'POR','HR Portal','POR');

_add:="
   _a.UDB_GRP:=_b;
   {! _ai:=3.._
   |! _a.B_DOMAIN:=_[_ai];
      _a.add()
   !}
";

: dodaj dziedziny do grup
_add(_DOM,'CTR','CTR');
_add(_DOM,'FST','FST');
_add(_DOM,'FINANSE','FKS');
_add(_DOM,'HBN','HBN');
_add(_DOM,'KAS','KAS');
_add(_DOM,'KON','KON');
_add(_DOM,'LOGISTYKA','LMG','LSP','LZK');
_add(_DOM,'OBIEGI','OBE','OBG');
_add(_DOM,'PERSONEL','PKD','PKW','PPL');
_add(_DOM,'PRODUKCJA','TPP','TTE');
_add(_DOM,'PRC','PRC');
_add(_DOM,'WYP','WYP');
_add(_DOM,'ZKL','PBA','POC','PSZ');
_add(_DOM,'ZDO','ROD');
_add(_DOM,'PPK','PPK');
_add(_DOM,'EDP','PED');
_add(_DOM,'POR','POR');

: dodaj typu danych do grup, informacje pobierz z istniejących przypisań w UD_SYS
_TMP:=sql('select distinct SYS as S, UD_TYP.SYMBOL as T from UD_SYS join UD_TYP');
_loop:=_TMP.first();
{!
|? _loop
|! {? _GRP.find_tab(,'SYSTEM',,'=',_TMP.S)
   || _SYS.UDB_GRP:=_GRP.SYMBOL;
      _SYS.UD_TYP:=_TMP.T;
      _SYS.add()
   ?};
   _loop:=_TMP.next()
!};
~~


\cfg_read
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Wczytuje konfigurację eksportu uprawnień do elementów schematów danych.
::   WE: _a [OBJECT] - wskazanie na tablicę konfiguracji
::       _b [STRING] - nazwa pliku do odczytu
::   WY: 0/1 - porażka/sukces
::----------------------------------------------------------------------------------------------------------------------
: otwórz plik z zapisem konfiguracji
{? (_file:=fopen(_b,'r',1))<=0
:  błąd otwarcia pliku
|| return(0)
?};

{!
|? (_line:=fread(_file))<>'\n'
:  utwórz wiersze w tabelach konfiguracji
|! exec('cfg_read_t','ud_upr_i',_a,spli_str(_line,%7))
!};

: zamknij plik
fclose(_file);

1


\cfg_read_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Dodaje do tabeli konfiguracji wiersz opisany ciągiem znaków.
::   WE: _a [OBJECT] - wskazanie na tablicę konfiguracji
::       _b [OBJECT] - tablica zawierająca dane wiersza
::   WY: ~l
::----------------------------------------------------------------------------------------------------------------------
_cfg:=_a;
_str:=_b;

: ustal tabelę konfiguracji
_TAB:=($('_a.'+_str[1]))(_cfg);
: przypisz wartości polom
{! _ni:=1.._TAB.fld_num()
|! _TAB[_ni]:=_str[_ni+1]
!};
: dołącz wiersz
_TAB.add()


\cfg_write
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Zapisuje konfigurację eksportu uprawnień do elementów schematów danych.
::   WE: _a [OBJECT] - wskazanie na tablicę konfiguracji
::       _b [STRING] - nazwa pliku do zapisu
::   WY: 0/1 - porażka/sukcml
::----------------------------------------------------------------------------------------------------------------------
: aliasy konfiguracji
_GRP:=_a.UDB_GRP;
_DOM:=_a.UDB_DOM;
_SYS:=_a.UDB_SYS;

_file:=fopen(_b,'w',1);

{? _file>0
:  plik otwarty, zapisz zawartość tabel
|| exec('cfg_write_t','ud_upr_i',_file,_a,'UDB_GRP');
   exec('cfg_write_t','ud_upr_i',_file,_a,'UDB_DOM');
   exec('cfg_write_t','ud_upr_i',_file,_a,'UDB_SYS');
   fclose(_file);
   1
?}


\cfg_write_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Zapisuje w pliku zawartość tabeli.
::   WE: _a [INTEGER] - uchwyt pliku
::       _b [OBJECT] - wskazanie na tablicę konfiguracji
::       _c [STRING] - nazwa tabeli konfiguracji
::   WY:fml
::----------------------------------------------------------------------------------------------------------------------
_fh:=_a;
_cfg:=_b;
_acr:=_c;
_TAB:=($('_a.'+_acr))(_cfg);

_TAB.cntx_psh();
_TAB.prefix();
_loop:=_TAB.first();
{!
|? _loop
|! _data:=_acr;
   {! _ni:=1.._TAB.fld_num()
   |! _data+=%7+_TAB[_ni]
   !};
   fwrite(_fh,_data);
   _loop:=_TAB.next()
!};
_TAB.cntx_pop();
~~


\xp_mk_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy listę grup dziedzin z określeniem aplikacji - substytut tabeli UDB_GRP.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_TAB:=exec('xp_mk_grp_t','ud_upr_i');
_TAB.trig_a('put',"exec('xp_dom_puta','ud_upr_i',_a,__EXP_UPR)");
_TAB.trig_a('del',"exec('xp_dom_dela','ud_upr_i',_a,__EXP_UPR)");

: po edycji pola SYSTEM
: emulacja pola słownikowego
_TAB.fld_fml('SYSTEM','AFTER_EDIT',"
   {? fld()<>''
   || {? ~__EXP_UPR.SYSTEM.find_tab(1,'SYMBOL',,':*',fld())
      || FUN.emsg('Błędna wartość pola.');
         return(0)
      || fld(__EXP_UPR.SYSTEM.SYMBOL)
      ?}
   ?};
   1
");
: obsługa F3 dla pola SYSTEM
: emulacja działania słownika
_TAB.fld_fml('SYSTEM','F3',"
   {? fld()<>''
   || __EXP_UPR.SYSTEM.find_tab(1,'SYMBOL',,':*',fld())
   || __EXP_UPR.SYSTEM.first()
   ?};
   {? __EXP_UPR.SYSTEM.select(,1)
   || fld(__EXP_UPR.SYSTEM.SYMBOL)
   ?}
");

: Podstawowe okienko wertowania substytutu tabeli UDB_GRP
_wnd:=_TAB.mk_sel('Grupy dziedzin','P',,'#udb_grp_upr',,,,,'U');
: ustal domyślne okienko
_TAB.win_sel(_wnd);

: dołącz kolumny
_TAB.win_fld(_wnd,,'SYMBOL',,,,,,'Symbol',,'Unikalny symbol');
_TAB.win_fld(_wnd,,'OPIS',,,,,,'Opis',,'Krótki opis');
_TAB.win_fld(_wnd,,'SYSTEM',,,,,,'Aplikacja',,'Symbol aplikacji');

: formuła sprawdzająca poprawność danych
_chk:="
   _TAB:=cur_tab(1,1);
:  test wypełnienia wymaganych pól
   {? (_chk:=exec('chk_rec','util',_TAB))<>''
   || return(_chk)
   ?};
:  test istnienia aplikacji o podanym symbolu
   {? ~__EXP_UPR.SYSTEM.find_tab(,'SYMBOL',,'=',_TAB.SYSTEM)
   || FUN.emsg('Błędna wartość pola.');
      return('SYSTEM')
   ?};
:  sprawdzenie powielenia symbolu lub opisu
   _TAB.cntx_psh();
   {? _TAB.find_tab(,'SYMBOL',,'=',_TAB.SYMBOL) |
      _TAB.find_tab(,'OPIS',,'=',_TAB.OPIS) |
      _TAB.find_tab(,'SYSTEM',,'=',_TAB.SYSTEM)
   || {? _TAB.ref()<>{? -menu_txt()='popraw' || _TAB.ref() || null ?}
      || FUN.emsg('Rekord o istniejącym kluczu');
         _TAB.cntx_pop();
         return(0)
      ?}
   ?};
   _TAB.cntx_pop();
:  sukces
   1
";

: dołącz standardowe akcja okienka
_TAB.win_act(_wnd,1,'Dołącz',,,,,,1);
_TAB.win_act(_wnd,1,'Rekord',,,,,_chk);
_TAB.win_act(_wnd,0,'Dołącz',,,,,,1);
_TAB.win_act(_wnd,0,'Popraw');
_TAB.win_act(_wnd,0,'Usuń');
_TAB.win_act(_wnd,0,'Formuła','Eksportuj uprawnienia',,'Zapisuje plik wymiany danych.',
   "exec('xp_upr_exp_b','ud_upr_i',__EXP_UPR,exec('xp_upr_file','ud_upr_i'))",
   "exec('xp_upr_exp_a','ud_upr_i',__EXP_UPR,exec('xp_upr_file','ud_upr_i'))"
);
_TAB.win_act(_wnd,0,'Szukaj');
_TAB.win_act(_wnd,0,'Kolejność');
_TAB.win_act(_wnd,0,'Rekord',,,,,_chk);

_TAB


\xp_mk_grp_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy tabelę grup dziedzin z określeniem aplikacji - substytut tabeli UDB_GRP.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
tab_tmp(1,
   'SYMBOL','STRING[8]','Symbol',
   'OPIS','STRING[60]','Opis',
   'SYSTEM','STRING[10]','Aplikacja'
)


\xp_dom_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Formuła wyzwalacza "po poprawieniu" dla substytutu tabeli UDB_DOM
::   WE: _a [INTEGER] - wynik operacji poprawiania, zgodna ze specyfikacją narzędzi
::       _b [OBJECT] - wskazanie na tablicę konfiguracji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a || return() ?};

_old:=bfld('SYMBOL');
_new:=_b.UDB_GRP.SYMBOL;
_DOM:=_b.UDB_DOM;
_SYS:=_b.UDB_SYS;

: aktualizuj przypisane dziedziny
_DOM.prefix();
_DOM.f_set(,,'UDB_GRP=\':_a\'',_old);
_loop:=_DOM.f_first();
{!
|? _loop
|! _DOM.UDB_GRP:=_new;
   _DOM.put();
   _loop:=_DOM.f_next()
!};

: aktualizuj przypisane typy
_SYS.prefix();
_SYS.f_set(,,'UDB_GRP=\':_a\'',_old);
_loop:=_SYS.f_first();
{!
|? _loop
|! _SYS.UDB_GRP:=_new;
   _SYS.put();
   _loop:=_SYS.f_next()
!};

~~


\xp_dom_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Formuła wyzwalacza "po usunięciu" dla substytutu tabeli UDB_DOM
::   WE: _a [INTEGER] - wynik operacji usuwania, zgodna ze specyfikacją narzędzi
::       _b [OBJECT] - wskazanie na tablicę konfiguracji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a || return() ?};

_sym:=bfld('SYMBOL');
_DOM:=_b.UDB_DOM;
_SYS:=_b.UDB_SYS;

: usuń dziedziny
_DOM.prefix(_sym);
{? _DOM.first()
|| {!
   |? _DOM.del()
   !}
?};

: usuń typy danych
_SYS.prefix(_sym);
{? _SYS.first()
|| {!
   |? _SYS.del()
   !}
?};

~~


\xp_mk_dom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy listę dziedzin grupy - substytut tabeli UDB_DOM.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_TAB:=exec('xp_mk_dom_t','ud_upr_i');

: wartość początkowa pola UDB_GRP
_TAB.fld_fml('UDB_GRP','BLANK',"__EXP_UPR.UDB_GRP.SYMBOL");

: po edycji pola B_DOMAIN
: emulacja pola słownikowego
_TAB.fld_fml('B_DOMAIN','AFTER_EDIT',"
   {? fld()<>''
   || {? ~__EXP_UPR.B_DOMAIN.find_tab(1,'SYMBOL',,':*',fld())
      || FUN.emsg('Błędna wartość pola.');
         return(0)
      || fld(__EXP_UPR.B_DOMAIN.SYMBOL);
         VAR.OPIS:=__EXP_UPR.B_DOMAIN.NAME
      ?}
   ?};
   1
");
: obsługa F3 dla pola B_DOMAIN
: emulacja działania słownika
_TAB.fld_fml('B_DOMAIN','F3',"
   {? fld()<>''
   || __EXP_UPR.B_DOMAIN.find_tab(1,'SYMBOL',,':*',fld())
   || __EXP_UPR.B_DOMAIN.first()
   ?};
   {? __EXP_UPR.B_DOMAIN.select(,1)
   || fld(__EXP_UPR.B_DOMAIN.SYMBOL)
   ?}
");

: Podstawowe okienko wertowania substytutu tabeli UDB_GRP
_wnd:=_TAB.mk_sel('Dziedziny','P',,'#udb_dom_upr',,,,,'U');
: ustal domyślne okienko
_TAB.win_sel(_wnd);

: dołącz kolumny
_TAB.win_fld(_wnd,,'B_DOMAIN',,,8,,0,'Symbol',,'Symbol dziedziny');
_TAB.win_fld(_wnd,VAR,'OPIS',,,30,,1,'Nazwa',,'Nazwa dziedziny');

: formuła sprawdzająca poprawność danych
_chk:="
   _TAB:=cur_tab(1,1);
:  test wypełnienia wymaganych pól
   {? (_chk:=exec('chk_rec','util',_TAB))<>''
   || return(_chk)
   ?};
:  test istnienia dziedziny o podanym symbolu
   {? ~__EXP_UPR.B_DOMAIN.find_tab(,'SYMBOL',,'=',_TAB.B_DOMAIN)
   || FUN.emsg('Błędna wartość pola.');
      return('SYSTEM')
   ?};
:  sprawdzenie powielenia dziedziny
   _TAB.cntx_psh();
   _TAB.prefix();
   {? _TAB.find_tab(,'B_DOMAIN',,'=',_TAB.B_DOMAIN)
   || {? _TAB.ref()<>{? -menu_txt()='popraw' || _TAB.ref() || null ?}
      || FUN.emsg('Rekord o istniejącym kluczu.');
         _TAB.cntx_pop();
         return(0)
      ?}
   ?};
   _TAB.cntx_pop();
:  sukces
   1
";

: dołącz standardowe akcja okienka
_TAB.win_act(_wnd,1,'Dołącz',,,,"VAR.OPIS:=''",,1);
_TAB.win_act(_wnd,1,'Rekord',,,,,_chk);
_TAB.win_act(_wnd,0,'Dołącz',,,,"VAR.OPIS:=''",,1);
_TAB.win_act(_wnd,0,'Popraw');
_TAB.win_act(_wnd,0,'Usuń');
_TAB.win_act(_wnd,0,'Szukaj');
_TAB.win_act(_wnd,0,'Kolejność');
_TAB.win_act(_wnd,0,'Rekord',,,,
   "  VAR.OPIS:='';
      _symbol:=cur_tab(1,1).B_DOMAIN;
      {? _symbol<>'' & __EXP_UPR.B_DOMAIN.find_tab(1,'SYMBOL',,'=',_symbol)
      || VAR.OPIS:=__EXP_UPR.B_DOMAIN.NAME
      ?};
      0
   ",
   _chk
);

_TAB


\xp_mk_dom_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy tabelę dziedzin grupy - substytut tabeli UDB_DOM.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
tab_tmp(1,
   'UDB_GRP','STRING[8]','Grupa',
   'B_DOMAIN','STRING[3]','Dziedzina'
)


\xp_mk_sys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy listę typów dla grupy dziedzin - substytut tabeli UDB_SYS.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_TAB:=exec('xp_mk_sys_t','ud_upr_i');

: wartość początkowa pola UDB_GRP
_TAB.fld_fml('UDB_GRP','BLANK',"__EXP_UPR.UDB_GRP.SYMBOL");

: po edycji pola UD_TYP
: emulacja pola słownikowego
_TAB.fld_fml('UD_TYP','AFTER_EDIT',"
   {? fld()<>''
   || {? ~UD_TYP.find_tab(1,'SYMBOL',,':*',fld())
      || FUN.emsg('Błędna wartość pola.');
         return(0)
      || fld(UD_TYP.SYMBOL);
         VAR.OPIS:=UD_TYP.OPIS
      ?}
   ?};
   1
");
: obsługa F3 dla pola UD_TYP
: emulacja działania słownika
_TAB.fld_fml('UD_TYP','F3',"
   {? fld()<>''
   || UD_TYP.find_tab(1,'SYMBOL',,':*',fld())
   || UD_TYP.first()
   ?};
   {? UD_TYP.select(,1)
   || fld(UD_TYP.SYMBOL)
   ?}
");

: Podstawowe okienko wertowania substytutu tabeli UDB_GRP
_wnd:=_TAB.mk_sel('Dziedziny','P',,'#udb_dom_upr',,,,,'U');
: ustal domyślne okienko
_TAB.win_sel(_wnd);

: dołącz kolumny
_TAB.win_fld(_wnd,,'UD_TYP',,,8,,0,'Symbol',,'Identyfikator');
_TAB.win_fld(_wnd,VAR,'OPIS',,,30,,1,'Opis',,'Krótki opis');

: formuła sprawdzająca poprawność danych
_chk:="
   _TAB:=cur_tab(1,1);
:  test wypełnienia wymaganych pól
   {? (_chk:=exec('chk_rec','util',_TAB))<>''
   || return(_chk)
   ?};
:  test istnienia dziedziny o podanym symbolu
   {? ~UD_TYP.find_tab(,'SYMBOL',,'=',_TAB.UD_TYP)
   || FUN.emsg('Błędna wartość pola.');
      return('SYSTEM')
   ?};
:  sprawdzenie powielenia dziedziny
   _TAB.cntx_psh();
   {? _TAB.find_tab(,'UD_TYP',,'=',_TAB.UD_TYP)
   || {? _TAB.ref()<>{? -menu_txt()='popraw' || _TAB.ref() || null ?}
      || FUN.emsg('Rekord o istniejącym kluczu');
         _TAB.cntx_pop();
         return(0)
      ?}
   ?};
   _TAB.cntx_pop();
:  sukces
   1
";

: dołącz standardowe akcja okienka
_TAB.win_act(_wnd,1,'Dołącz',,,,"VAR.OPIS:=''",,1);
_TAB.win_act(_wnd,1,'Rekord',,,,,_chk);
_TAB.win_act(_wnd,0,'Dołącz',,,,"VAR.OPIS:=''",,1);
_TAB.win_act(_wnd,0,'Popraw');
_TAB.win_act(_wnd,0,'Usuń');
_TAB.win_act(_wnd,0,'Szukaj');
_TAB.win_act(_wnd,0,'Kolejność');
_TAB.win_act(_wnd,0,'Rekord',,,,
   "  VAR.OPIS:='';
      _symbol:=cur_tab(1,1).UD_TYP;
      {? _symbol<>'' & UD_TYP.find_tab(1,'SYMBOL',,'=',_symbol)
      || VAR.OPIS:=UD_TYP.OPIS
      ?};
      0
   ",
   _chk
);

_TAB


\xp_mk_sys_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy tabelę typów dla grupy dziedzin - substytut tabeli UDB_SYS.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
tab_tmp(1,
   'UDB_GRP','STRING[8]','Grupa',
   'UD_TYP','STRING[8]','Typ'
)


\xp_mk_app
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy listę aplikacji na podstawie zawartości tabeli UD_SYS.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_TAB:=exec('xp_mk_app_t','ud_upr_i');

_wnd:=_TAB.mk_sel('Aplikacje','P',,'#ud_sys_upr',,,,,'U');
_TAB.win_fld(_wnd,,'SYMBOL',,,,,,'Symbol',,'Symbol aplikacji');
_TAB.win_act(_wnd,0,'Formuła','Ten',,'Wybór bieżącego zapisu',"sel_exit()",,1);
_TAB.win_act(_wnd,0,'Szukaj');
_TAB.win_act(_wnd,0,'Kolejność');
_TAB.win_sel(_wnd);

_TAB


\xp_mk_app_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy tabelę aplikacji na podstawie zawartości tabeli UD_SYS.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
sql('select distinct SYS as SYMBOL from UD_SYS order by SYMBOL')


\xp_mk_sub
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy listę dziedzin Merit - substytut tabeli B_DOMAIN.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_TAB:=exec('xp_mk_sub_t','ud_upr_i');

_wnd:=_TAB.mk_sel('Dziedziny produktowe','P',,'#b_domain_upr',,,,,'U');
_TAB.win_fld(_wnd,,'SYMBOL',,,,,1,'Symbol',,'Symbol dziedziny');
_TAB.win_fld(_wnd,,'NAME',,,,,1,'Nazwa',,'Nazwa dziedziny');
_TAB.win_act(_wnd,0,'Formuła','Ten',,'Wybór bieżącego zapisu',"sel_exit()",,1);
_TAB.win_act(_wnd,0,'Szukaj');
_TAB.win_act(_wnd,0,'Kolejność');
_TAB.win_sel(_wnd);

_TAB


\xp_mk_sub_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy tabelę dziedzin Merit - substytut tabeli B_DOMAIN.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
_TAB:=tab_tmp(1,
   'SYMBOL','STRING[3]','Symbol',
   'NAME','STRING[60]','Nazwa'
);

_add:="
   _a.SYMBOL:=_b;
   _a.NAME:=_c;
   _a.add()
";

_add(_TAB,'CTR','Controlling');
_add(_TAB,'FKS','Finanse');
_add(_TAB,'FST','Środki trwałe');
_add(_TAB,'HBN','Płatności elektroniczne');
_add(_TAB,'KAS','Kasa');
_add(_TAB,'KON','Konsolidacja sprawozdań');
_add(_TAB,'LMG','Magazyny');
_add(_TAB,'LSP','Sprzedaż');
_add(_TAB,'LZK','Zakupy');
_add(_TAB,'OBE','Obieg wniosków');
_add(_TAB,'OBG','Obieg dokumentów');
_add(_TAB,'PBA','Badania opinii');
_add(_TAB,'PKD','Kadry');
_add(_TAB,'PKW','Pracownik');
_add(_TAB,'POC','Oceny pracownicze');
_add(_TAB,'PPL','Płace');
_add(_TAB,'PRC','Czas pracy');
_add(_TAB,'PSZ','Obsługa szkoleń');
_add(_TAB,'ROD','Zarządzanie danymi osobowymi');
_add(_TAB,'TPP','Plan produkcji');
_add(_TAB,'TTE','Produkcja');
_add(_TAB,'WYP','Wyposażenie');
_add(_TAB,'PPK','Pracownicze plany kapitałowe');
_add(_TAB,'PED','Dokumentacja pracownicza w postaci elektronicznej');
_add(_TAB,'POR','HR Portal');

_TAB


\xp_mk_upr_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Tworzy tabelę uprawnień - substytut tabeli UDB_UPR.
::   WE:
::   WY: alias tabeli tymczasowej
::----------------------------------------------------------------------------------------------------------------------
tab_tmp(4,
   'UDB_GRP','STRING[8]','Grupa',
   'USERS','STRING[10]','Użytkownik',
   'UD_TYP','STRING[8]','Typ danych',
   'UD_SKL','STRING[16]','Element',
   'DOSTEP','STRING[1]','Dostęp'
)


\xp_upr_file
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Zwraca nazwę pliku zawierającego informacje potrzebne do odtworzenia uprawnień w Merit.
::   WE:
::   WY: nazwa pliku
::----------------------------------------------------------------------------------------------------------------------
'ud_upr_i.tra'


\xp_upr_exp_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Przed akcją "Eksportuj uprawnienia" okienka substytutu tabeli UDB_GRP.
::   WE: _a [OBJECT] - wskazanie na tablicę konfiguracji
::       _b [STRING] - nazwa pliku do zapisu
::   WY: 0/1 - nie wykonuj/wykonaj ekspoty
::----------------------------------------------------------------------------------------------------------------------
{? fexists(_b,1) & ~FUN.ask('Plik "'+_b+'" istnieje.\nCzy kontynuować działanie?')
|| return(0)
?};

1


\xp_upr_exp_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Akcja "Eksportuj uprawnienia" okienka substytutu tabeli UDB_GRP.
::   WE: _a [OBJECT] - wskazanie na tablicę konfiguracji
::       _b [STRING] - nazwa pliku do zapisu
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_name:=_b;

: zmodyfikowana konfiguracja (wykorzystanie wskazań)
_out:=obj_new('UDB_GRP','UDB_DOM','UDB_SYS','UDB_UPR');
: aliasy konfiguracji
_out.UDB_GRP:=_a.UDB_GRP;
_out.UDB_DOM:=_a.UDB_DOM;
_out.UDB_SYS:=_a.UDB_SYS;
_out.UDB_UPR:=exec('xp_mk_upr_t','ud_upr_i');
: obiekt z informacjami o aplikacjach
_app:=exec('_sys2dom','tran_exp');

: pobierz uprawnienia z UD_UPR
_UPR:=_out.UDB_UPR;
_out.UDB_GRP.cntx_psh();
UD_UPR.cntx_psh();
UD_UPR.index('TECSKL');
UD_UPR.prefix();
_loop:=UD_UPR.first();
{!
|? _loop
|! _sys:=UD_UPR.UD_SYS().SYS;
   {? _sys<>'' & _app.find_sys(_sys)
   || {? _out.UDB_GRP.find_tab(1,'SYSTEM',,'=',_sys)
      || _UPR.UDB_GRP:=_out.UDB_GRP.SYMBOL;
         _UPR.USERS:=UD_UPR.USERS().KOD;
         _UPR.UD_TYP:=UD_UPR.UD_SYS().UD_TYP().SYMBOL;
         _UPR.UD_SKL:=UD_UPR.UD_SKL().SYMBOL;
         _UPR.DOSTEP:=UD_UPR.DOSTEP;
         _UPR.add()
      || FUN.emsg(
            'W konfiguracji brak danych dla aplikacji "'+_sys+'".\n'+
            'Proces eksportu został przerwany.'
         );
         _out.UDB_GRP.cntx_pop();
         UD_UPR.cntx_pop();
         return(0)
      ?}
   ?};
   _loop:=UD_UPR.next()
!};
UD_UPR.cntx_pop();
_out.UDB_GRP.cntx_pop();

: otwórz plik wymiany danych do zapisu
{? (_file:=fopen(_name,'w',1,0))<=0
|| FUN.emsg('Wystąplił błąd podczas próby otwarcia pliku "'+_name+'".');
   return()
?};

: zapisz konfigurację i uprawnienia
exec('cfg_write_t','ud_upr_i',_file,_out,'UDB_GRP');
exec('cfg_write_t','ud_upr_i',_file,_out,'UDB_DOM');
exec('cfg_write_t','ud_upr_i',_file,_out,'UDB_SYS');
exec('cfg_write_t','ud_upr_i',_file,_out,'UDB_UPR');

FUN.info(
   'Zakończono zapis informacji potrzebnych do odtworzenia uprawnień do elementów schematów.\n'+
   'Dane zapisano w pliku "'+_name+'".'
);

fclose(_file);
~~


\import
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
::  MOD: ARTSLO [19.22]
:: OPIS: Wczytuje informacje o uprawnieniach do elementów schematów danych z pliku wymiany.
::   WE: _a [BRAK/INTEGER] - interakcja z użytkownikiem:
::                               - 0 lub pominięty - brak
::                               - 1 tylko paski postepu
::                               - inny nie zerowy komunikaty i paski postępu wyświetlane
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
_name:=exec('xp_upr_file','ud_upr_i');
_progress:="";
_msg:="";

{? var_pres('_a')<>type_of(0) | ~_a
|| _msg:="__UPG.msg(_a)"
|? _a=1
|| _progress:="progress(_a,_b)";
   _msg:="__UPG.msg(_a)"
|| _progress:="progress(_a,_b)";
   _msg:="FUN.emsg(_a)"
?};

{? ~fexists(_name,1)
|| _msg('Plik "%1" nie istnieje.\nDziałanie zostanie przerwane.'@[_name]);
   return(-1)
?};

: utwórz bufor do wczytania konfiguracji i uprawnień
_buf:=obj_new('UDB_GRP','UDB_DOM','UDB_SYS','UDB_UPR');
_buf.UDB_GRP:=exec('xp_mk_grp_t','ud_upr_i');
_buf.UDB_DOM:=exec('xp_mk_dom_t','ud_upr_i');
_buf.UDB_SYS:=exec('xp_mk_sys_t','ud_upr_i');
_buf.UDB_UPR:=exec('xp_mk_upr_t','ud_upr_i');

: odczytaj zawartość pliku wymiany danych
{? ~exec('cfg_read','ud_upr_i',_buf,_name)
|| _msg('Wystąpił błąd podczas dostępu do pliku "%1".\nDziałanie zostanie przerwane.'@[_name]);
   return(-1)
?};

_grp_ref:="
   _ref:=null;
   UDB_GRP.cntx_psh();
   UDB_GRP.index('SYMBOL');
   {? UDB_GRP.find_key(_a)
   || _ref:=UDB_GRP.ref()
   ?};
   UDB_GRP.cntx_pop();
   _ref
";

: uzupełenienie grup aplikacji
_GRP:=_buf.UDB_GRP;
UDB_GRP.cntx_psh();
UDB_GRP.prefix();
_GRP.cntx_psh();
_GRP.prefix();
_loop:=_GRP.first();
_size:=_GRP.size();
_cnt:=0;
{!
|? _loop
|! _progress(100*_cnt/_size,'Trwa aktualizacja grup dziedzin...'@);
   _ref_s:={? UDB_GRP.find_tab(1,'SYMBOL',,'=',_GRP.SYMBOL) || UDB_GRP.ref() || null ?};
   _ref_o:={? UDB_GRP.find_tab(1,'OPIS',,'=',_GRP.OPIS) || UDB_GRP.ref() || null ?};
:  aktualizuj zależnie od rezultatów wyszukiwania
   {? _ref_s<>null & _ref_o<>null
:     znaleziono wg symbolu i opisu
   || {? _ref_s<>_ref_o
:        katastrofa!
      || _msg(
            'Wykryto konflikt wartości unikalnych w definicji grupy dziedzin.\n'
            'Wartości SYMBOL: %1, OPIS: %2 nie da się włączyć do tabeli.'@
            [_GRP.SYMBOL,_GRP.OPIS]
         );
         UDB_GRP.cntx_pop();
         _GRP.cntx_pop();
         return(-1)
      ?}

   |? _ref_s<>null & UDB_GRP.seek(_ref_s)
:     znaleziono wg symbolu, aktualizuj opis
   || UDB_GRP.OPIS:=_GRP.OPIS;
      UDB_GRP.put()

   |? _ref_o<>null & UDB_GRP.seek(_ref_o)
:     znaleziono wg opisu, aktualizuj symbol
   || UDB_GRP.SYMBOL:=_GRP.SYMBOL;
      UDB_GRP.put()
   ||
:     nie znaleziono, utwórz grupę
      UDB_GRP.blank();
      UDB_GRP.SYMBOL:=_GRP.SYMBOL;
      UDB_GRP.OPIS:=_GRP.OPIS;
      UDB_GRP.add()
   ?};
   _cnt+=1;
   _loop:=_GRP.next()
!};
prgs_clr();
_GRP.cntx_pop();
UDB_GRP.cntx_pop();

: uzupełnienie składu grup
_DOM:=_buf.UDB_DOM;
UDB_DOM.cntx_psh();
UDB_DOM.prefix();
_DOM.cntx_psh();
_DOM.prefix();
_loop:=_DOM.first();
_size:=_GRP.size();
_cnt:=0;
{!
|? _loop
|! _progress(100*_cnt/_size,'Trwa aktualizacja składu grup dziedzin...'@);
   {? UDB_DOM.find_tab(1,'B_DOMAIN','SYMBOL','=',_DOM.B_DOMAIN)
   || {? UDB_DOM.UDB_GRP().SYMBOL<>_DOM.UDB_GRP
:        katastrofa!
      || _msg(
            'Wykryto konflikt wartości unikalnych w składzie grupy dziedzin.\n'
            'Wartości UDB_GRP: %1, B_DOMAIN: %2 nie da się włączyć do tabeli.'@
            [_DOM.UDB_GRP,_DOM.B_DOMAIN]
         );
         UDB_DOM.cntx_pop();
         _DOM.cntx_pop();
         return(-1)
      ?}

   || UDB_DOM.blank();
      UDB_DOM.UDB_GRP:=_grp_ref(_DOM.UDB_GRP);
      UDB_DOM.B_DOMAIN:=exec('domain_ref','#b_domain',_DOM.B_DOMAIN);
      {? UDB_DOM.UDB_GRP=null | UDB_DOM.B_DOMAIN=null
:        katastrofa!
      || _msg(
            'Brak wymaganych informacji o składzie grupy dziedzin.\n'
            'Wartości UDB_GRP: %1, B_DOMAIN: %2 nie da się włączyć do tabeli.'@
            [_DOM.UDB_GRP,_DOM.B_DOMAIN]
         );
         UDB_DOM.cntx_pop();
         _DOM.cntx_pop();
         return(-1)
      || UDB_DOM.add()
      ?}
   ?};
   _cnt+=1;
   _loop:=_DOM.next()
!};
prgs_clr();
_DOM.cntx_pop();
UDB_DOM.cntx_pop();

: przypisanie typów danych
_SYS:=_buf.UDB_SYS;
UDB_SYS.cntx_psh();
UDB_SYS.trig_off('add po','*');
UDB_SYS.index('TYP_SYM');
UDB_SYS.prefix();
_SYS.cntx_psh();
_SYS.prefix();
_loop:=_SYS.first();
_size:=_SYS.size();
_cnt:=0;
{!
|? _loop
|! _progress(100*_cnt/_size,'Trwa aktualizacja informacji o typach danych...'@);
   {? ~UDB_SYS.find_tab(1,'UDB_GRP','SYMBOL','=',_SYS.UDB_GRP,'UD_TYP','SYMBOL','=',_SYS.UD_TYP)
   || UDB_SYS.blank();
      UDB_SYS.UDB_GRP:=_grp_ref(_SYS.UDB_GRP);
      UDB_SYS.UD_TYP:=exec('szukaj_ud_typ','schemat',_SYS.UD_TYP);
      {? UDB_SYS.UDB_GRP=null | UDB_SYS.UD_TYP=null
:        katastrofa!
      || _msg(
            'Brak wymaganych informacji o typie przypisanym do grupy.\n'
            'Wartości UDB_GRP: %1, UD_TYP: %2 nie da się włączyć do tabeli.'@
            [_SYS.UDB_GRP,_SYS.UD_TYP]
         );
         UDB_SYS.cntx_pop();
         _SYS.cntx_pop();
         return(-1)
      || UDB_SYS.add()
      ?}
   ?};
   _cnt+=1;
   _loop:=_SYS.next()
!};
prgs_clr();
_SYS.cntx_pop();
UDB_SYS.trig_on('add po','*');
UDB_SYS.cntx_pop();

: aktualizacja uprawnień
: ograniczenie i przeglądanie informacji wg UDB_SYS aby uniknąć ustalania grupy i typu
: w "głównej" iteracji zamiast trzech wyszukiwań (grupy, typu, systemu) jest jedno
USERS.cntx_psh();
USERS.prefix();
: zablokuj aktualizacje wartości w kontroli uprawnień
USERS.for_each("exec('add','#bulk',USERS,'add',$USERS.ref())",0);

UD_SKL.cntx_psh();
_UPR:=_buf.UDB_UPR;
_UPR.index(_UPR.ndx_tmp(,1,'UDB_GRP',,0,'UD_TYP',,0));
UDB_UPR.cntx_psh();
_UPR.cntx_psh();
_SYS.cntx_psh();
_SYS.prefix();
_loop:=_SYS.first();
_size:=_SYS.size();
_cnt:=0;
{!
|? _loop
|! _progress(100*_cnt/_size,'Trwa aktualizacja uprawnień użytkowników...'@);
   {? UDB_SYS.find_tab(1,'UDB_GRP','SYMBOL','=',_SYS.UDB_GRP,'UD_TYP','SYMBOL','=',_SYS.UD_TYP)
:     przeglądaj uprawnienia i dane źródłowe wg UDB_SYS
   || UD_SKL.index('SYMBOL');
      UD_SKL.prefix(UDB_SYS.UD_TYP);
      _UPR.prefix(_SYS.UDB_GRP,_SYS.UD_TYP,);
      _loop:=_UPR.first();
      {!
      |? _loop
      |! USERS.index('USR_AKOD');
         USERS.prefix('T');
         {? UD_SKL.find_key(_UPR.UD_SKL,) & USERS.find_key(_UPR.USERS,)
         || UDB_UPR.index('UNIQUE');
            UDB_UPR.prefix(UDB_SYS.ref(),USERS.ref());
            {? UDB_UPR.find_key(UD_SKL.ref())
            || {? UDB_UPR.DOSTEP<>_UPR.DOSTEP
               || UDB_UPR.DOSTEP:=_UPR.DOSTEP;
                  UDB_UPR.put()
               ?}
            || UDB_UPR.blank();
               UDB_UPR.UDB_SYS:=UDB_SYS.ref();
               UDB_UPR.USERS:=USERS.ref();
               UDB_UPR.UD_SKL:=UD_SKL.ref();
               UDB_UPR.DOSTEP:=_UPR.DOSTEP;
               UDB_UPR.add()
            ?}
         ?};
         _loop:=_UPR.next()
      !}
   ?};
   _cnt+=1;
   _loop:=_SYS.next()
!};
prgs_clr();

USERS.prefix();
: odblokuj aktualizacje wartości w kontroli uprawnień
USERS.for_each("exec('del','#bulk',USERS,'add',$USERS.ref())",0);

_SYS.cntx_pop();
_UPR.cntx_pop();
USERS.cntx_pop();
UD_SKL.cntx_pop();

: aktualizacja kontroli uprawnień
_UST:=sql(
   'select max(UDB_UPR.REFERENCE) as REF '
   'from UDB_UPR join UDB_SYS using(UDB_UPR.UDB_SYS,UDB_SYS.REFERENCE) '
   'group by USERS, UD_TYP'
);
_loop:=_UST.first();
_size:=_UST.size();
_cnt:=0;
{!
|? _loop
|! _progress(100*_cnt/_size,'Trwa aktualizacja kontroli uprawnień...'@);
   UDB_UPR.prefix();
   {? UDB_UPR.seek(_UST.REF)
   || exec('udb_upr_zmien','schemat')
   ?};
   _cnt+=1;
   _loop:=_UST.next()
!};
prgs_clr();
UDB_UPR.cntx_pop();

_msg('Zaktualizowano uprawnienia do elementów struktur danych.'@);

1

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 41ae8db85f5b57c10c629eca73cfb3326e0ce5e8d6cc95aa0b45f46e888931152affc8b0144328afa091a20d7c67cded6ca755fde72b5a8455e5de8efaa6eb0c9c08a885c66ffd576f2cf063cd04991f293692161bc3c891ff3f91bfcae7bd6994a8f7e1030cb2073bf8cbd40bf570f119e5d31b9ccc611e187e1f97c0938b33

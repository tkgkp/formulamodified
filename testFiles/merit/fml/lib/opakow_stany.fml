:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: opakow_sm.fml
:: Utworzony: 05.01.2023
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Formuły do obsługi stanów opakowań zwrotnych
::======================================================================================================================
:: Baza wiedzy:
:: - Zapisy OPAK_ZWR są robione w masce dokumentu zwrotnego a nie naliczającego kaucję.

\env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Środowisko obszaru opakowań
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablice i komentować poszczególne jej elementy
::       powiedzmy, że to bedzie pole
         _fld:="31+form(_a)";
::       powiedzmy, że to bedzie metoda
         _mth:="31+form(_a)";

_env:=obj_new(
::             pola
                _fld('MAG',                  'Magazyny')
               ,_fld('CUR_MAG',              'Bieżący magazyn')
               ,_fld('CUR_TYP',              'Bieżący typ dokumentu')
               ,_fld('CUR_M',                'Bieżące opakowanie')
               ,_fld('CUR_KH',               'Bieżący kontrahent')
               ,_fld('CUR_KH_ODB',           'Bieżący odbiorca kontrahenta')
               ,_fld('DOK_OPAK_POZ',         'Bieżąca pozycja w oknie id=dok_opak')
               ,_fld('STANY_MAG_MAG_POZ',    'Bieżąca pozycja w oknie id=stany_mag_mag')

::             identyfikatory okienek

::             tytuly okienek

::             tabele tymczasowe

::             indeksy tymczasowych tabel

::             uchwyty do okien
               ,_fld('WIN_MAIN',             'Główne okno grupowe')
               ,_fld('stany_kh_wer_skh',     'Stany kontrahenta')
               ,_fld('stany_kh_wer_sk',      'Stany kontrahenta - kaucje')
               ,_fld('stany_kh_wer_skd',     'Stany kontrahenta - kaucje wg dokumentów')
               ,_fld('stany_mag_wer_mag',    'Stany magazynowe - magazyny')
               ,_fld('stany_mag_wer_sm',     'Stany magazynowe')
               ,_fld('stany_mag_wer_sd',     'Stany magazynowe - dostawy')

::             identyfikatory przycisków

::             METODY
               ,_mth('initObszar',           'Init obszaru')
               ,_mth('doneObszar',           'Done obszaru')
               ,_mth('winMain',              'Ustawienie głównego okna')
               );
_env.MAG:=sql($"select distinct MG.ODDZ ODDZ, MG.MG_OPAK MAG from MG where MG_OPAK<>'' order by 1,2");
_env.CUR_MAG:=exec('FindAndGet','#table',MG,ST.MAG,,"MG.MG_OPAK",'');
_env.CUR_TYP:='';
_env.CUR_M:=null();
_env.CUR_KH:=null();
_env.CUR_KH_ODB:=null();
_env.DOK_OPAK_POZ:=0;
_env.STANY_MAG_MAG_POZ:=0;
_env


\stany
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Stany opakowań
::   WE: _a - tabela
::       _b - uchwyt do okna grupowego
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;
_wer_gr:=_b;

_stn:=exec('chk_role','#b__box',OPERATOR.USER,'LMG_OPK_PSOP');
_add:=exec('chk_role','#b__box',OPERATOR.USER,'LMG_OPK_DPOP');

{? _stn | _add
||
   params_exec('stany_mag','opakow_stany',_Tab,_wer_gr);
   params_exec('stany_kh','opakow_stany',_Tab,_wer_gr);

   AreaTitle.setTabWin(OPAK_SM,~~);
   AreaTitle.setTitle();

   OPAK_SM.first();
   POMOC.OPAKOW:='N'
?}


\stany_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Stany magazynowe
::   WE: _a - tabela
::       _b - uchwyt do okna grupowego
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;
_wer_gr:=_b;

_env:=params_get().env;

_Mag:=_env.MAG;
_env.stany_mag_wer_mag:=_stany_mag_wer_mag:=exec('stany_mag_wer_mag','opakow_stany',_Mag);
_env.stany_mag_wer_sm:=_stany_mag_wer_sm:=exec('stany_mag_wer_sm','opakow_stany');
_env.stany_mag_wer_sd:=_stany_mag_wer_sd:=exec('stany_mag_wer_sd','opakow_stany');

:: zakładka - stany magazynowe
:: panel - magazyny
_far:="
   _env:=params_get().env; params_set(params_get());
   grp_disp(OPAK_SM,_env.stany_mag_wer_sm);
   grp_disp(OPAK_SD,_env.stany_mag_wer_sd)
";
_fb:="
   _env:=params_get().env;
   _Mag:=_env.MAG; _Mag.prefix(ST.ODDZ); _Mag.find_key(_env.CUR_MAG);
   win_set('cur_row_pos=%1'[$_env.STANY_MAG_MAG_POZ],'stany_mag_mag')
";
_fa:="
   _env:=params_get().env;
::   _env.STANY_MAG_MAG_POZ:=win_get('cur_row_pos','stany_mag_mag');
   _env.CUR_MAG:=_env.MAG.MAG;
   {? OPAK_SM.get() || _env.CUR_M:=OPAK_SM.M ?}
";
_Tab.grp_sel(_wer_gr,_Mag,_stany_mag_wer_mag,'Stany opakowań'@,_far,,,10,_fb,_fa,,,'maximized','stany_mag_mag');
_Mag.win_sopt(_stany_mag_wer_mag,'select_record_checkbox=0');
:: panel - stany magazynowe
_Tab.tab_splt(_wer_gr,,'vertical','opak_sm','0, 25%');
_far:="
   _env:=params_get().env;
   grp_disp(OPAK_SD,_env.stany_mag_wer_sd)
";
_fb:="
   _env:=params_get().env;
   _Mag:=_env.MAG;
   OPAK_SM.index('MAG'); OPAK_SM.prefix(_Mag.MAG,); OPAK_SM.find_key(_env.CUR_M)
";
_Tab.grp_sel(_wer_gr,OPAK_SM,_stany_mag_wer_sm,,_far,,,10,_fb,_fa,,,'maximized_with_title','stany_mag_sm');
:: panel - stany dostaw
_Tab.tab_splt(_wer_gr,'opak_sm','horizontal','opak_sd','0, 50%');
_fb:="
   OPAK_SD.index('MAG'); OPAK_SD.prefix(OPAK_SM.MAG,OPAK_SM.M)
";
_Tab.grp_sel(_wer_gr,OPAK_SD,_stany_mag_wer_sd,,,,,10,_fb,,,,'maximized_with_title','stany_mag_sd')


\stany_mag_wer_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Stany magazynowe - okno wertowania - Magazyny
::   WE: _a - tabela magazynów opakowań
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Mag:=_a;
_wer:=_Mag.mk_sel('Magazyny'@,,0,'stany_mag_mag',5,10,15,,'U');
_Mag.win_fld(_wer,,'MAG',,,10,,1,'Magazyn'@);
_wer


\stany_mag_wer_sm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Stany magazynowe - okno wertowania - Stany magazynowe
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wer:=OPAK_SM.mk_sel('Stany opakowań'@,,0,'stany_mag_sm',5,10,15,,'U');
OPAK_SM.win_fld(_wer,,'M','KTM',,52,,1,'Opakowanie'@);
OPAK_SM.win_fld(_wer,,'S',,,20,,1,'Stan w magazynie'@);
OPAK_SM.win_fld(_wer,,'ILW',,,20,,1,'Ilość w wydaniu'@);
OPAK_SM.win_fld(_wer,,'SD',,,20,,1,'Stan dostępny'@);
::_fb:="
::   _env:=params_get().env;
::
::   exec('rebuild','opakow_stany');
::
::   grp_disp(OPAK_SD,_env.stany_mag_wer_sd)
::";
::OPAK_SM.win_act(_wer,1,'Formuła','Odtwórz stany'@,,,_fb);
::OPAK_SM.win_act(_wer,,'Formuła','Odtwórz stany'@,,,_fb);
_fb:="exec('dokumenty','opakow_stany','SM')";
OPAK_SM.win_act(_wer,,'Formuła','Dokumenty'@,,,_fb,,1);
_wer


\stany_mag_wer_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: stany dostaw
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wer:=OPAK_SD.mk_sel('Stany dostaw'@,,0,'stany_dost',5,10,15,,'U');
OPAK_SD.win_fld(_wer,,'DATA',,,20,,1,'Data dokumentu'@);
OPAK_SD.win_fld(_wer,,'SYM',,,20,,1,'Symbol'@);
OPAK_SD.win_fld(_wer,,'POZ',,,10,,1,'Pozycja'@);
OPAK_SD.win_fld(_wer,,'S',,,20,,1,'Stan w magazynie'@);
OPAK_SD.win_fld(_wer,,'ILW',,,20,,1,'Ilość w wydaniu'@);
OPAK_SD.win_fld(_wer,,'SD',,,20,,1,'Stan dostępny'@);
_fb:="exec('dokumenty','opakow_stany','SD')";
OPAK_SD.win_act(_wer,,'Formuła','Dokumenty'@,,,_fb,,1);
OPAK_SD.win_sel(_wer);
_wer


\stany_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Stany kontrahenta
::   WE: _a - tabela
::       _b - uchwyt do okna grupowego
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;
_wer_gr:=_b;

_env:=params_get().env;

_env.stany_kh_wer_skh:=_stany_kh_wer_skh:=exec('stany_kh_wer_skh','opakow_stany');
_env.stany_kh_wer_sk:=_stany_kh_wer_sk:=exec('stany_kh_wer_sk','opakow_stany');
_env.stany_kh_wer_skd:=_stany_kh_wer_skd:=exec('stany_kh_wer_skd','opakow_stany');

:: zakładka - stany kontrahentów
:: panel - stany kontrahentów
_far:="
   _env:=params_get().env;
   _env.CUR_KH:=OPAK_SKH.KH;
   _env.CUR_KH_ODB:=OPAK_SKH.KH_ODB;
   grp_disp(OPAK_SK,_env.stany_kh_wer_sk);
   grp_disp(OPAK_SKD,_env.stany_kh_wer_skd)
";
_fb:="
   _env:=params_get().env;
   OPAK_SKH.index('KH'); OPAK_SKH.prefix(); OPAK_SKH.find_key(_env.CUR_KH,_env.CUR_KH_ODB,_env.CUR_M)
";
_fa:="
   _env:=params_get().env;
   _env.CUR_KH:=_env.CUR_KH_ODB:=null();
   {? OPAK_SKH.get() || _env.CUR_KH:=OPAK_SKH.KH; _env.CUR_KH_ODB:=OPAK_SKH.KH_ODB ?};
   _env.CUR_M:=null(); {? OPAK_P.get() || _env.CUR_M:=OPAK_P.M ?}
";
_Tab.grp_sel(_wer_gr,OPAK_SKH,_stany_kh_wer_skh,'Stany kaucji'@,_far,,,10,_fb,,,,'maximized','stany_kh_skh');
OPAK_SKH.win_sopt(_stany_kh_wer_skh,'select_record_checkbox=0');
:: kaucje
_Tab.tab_splt(_wer_gr,,'horizontal','opak_sk','0, 33%');
_far:="
   _env:=params_get().env;
   grp_disp(OPAK_SKD,_env.stany_kh_wer_skd)
";
_fb:="
   OPAK_SK.index('KH'); OPAK_SK.prefix(OPAK_SKH.KH,OPAK_SKH.M,)
";
_Tab.grp_sel(_wer_gr,OPAK_SK,_stany_kh_wer_sk,,_far,,,10,_fb,,,,'maximized_with_title','stany_kh_sk');
:: kaucje wg dokumentów
_Tab.tab_splt(_wer_gr,,'horizontal','opak_skd','0, 66%');
_fb:="
   OPAK_SKD.index('VIEW'); OPAK_SKD.prefix(OPAK_SK.MAG,OPAK_SK.KH,OPAK_SK.M,)
";
_Tab.grp_sel(_wer_gr,OPAK_SKD,_stany_kh_wer_skd,,,,,10,_fb,,,,'maximized_with_title','stany_kh_skd')


\stany_kh_wer_skh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Stany kontrahenta - okno wertowania - Stany kontrahenta
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wer:=OPAK_SKH.mk_sel('Stany kontrahentów'@,,0,'stany_kh_skh',5,10,15,,'U');
OPAK_SKH.win_fld(_wer,,'KH','SKR',,20,,1,'Kontrahent'@);
OPAK_SKH.win_fld(_wer,,'KH_ODB','SKR',,19,,1,'Odbiorca'@);
OPAK_SKH.win_fld(_wer,,'M','KTM',,20,,1,'Opakowanie'@);
OPAK_SKH.win_fld(_wer,,'N_SK',,,20,,1,'Stan kaucji naszych'@);
OPAK_SKH.win_fld(_wer,,'N_WK',,,20,,1,'Wartość'@);
OPAK_SKH.win_fld(_wer,,'KH_SK',,,20,,1,'Stan kaucji kontrahenta'@);
OPAK_SKH.win_fld(_wer,,'KH_WK',,,20,,1,'Wartość'@);
OPAK_SKH.win_fld(_wer,,'N_SBK',,,20,,1,'Stan bez kaucji u nas'@);
OPAK_SKH.win_fld(_wer,,'KH_SBK',,,20,,1,'Stan bez kaucji u kontrahenta'@);
::_fb:="
::   _env:=params_get().env;
::
::   exec('rebuild','opakow_stany');
::
::   grp_disp(OPAK_SK,_env.stany_kh_wer_sk);
::   grp_disp(OPAK_SKD,_env.stany_kh_wer_skd)
::";
::OPAK_SKH.win_act(_wer,1,'Formuła','Odtwórz stany'@,,,_fb);
::OPAK_SKH.win_act(_wer,,'Formuła','Odtwórz stany'@,,,_fb);
_fb:="exec('dokumenty','opakow_stany','SKH')";
OPAK_SKH.win_act(_wer,,'Formuła','Dokumenty'@,,,_fb,,1);
_wer


\stany_kh_wer_sk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Stany kontrahenta - okno wertowania - Stany kaucji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wer:=OPAK_SK.mk_sel('Stany kaucji'@,,0,'stany_kh_sk',5,10,15,,'U');
OPAK_SK.win_fld(_wer,,'MAG',,,40,,1,'Magazyn'@);
OPAK_SK.win_fld(_wer,,'M','KTM',,20,,1,'Opakowanie'@);
OPAK_SK.win_fld(_wer,,'N_SK',,,20,,1,'Stan kaucji naszych'@);
OPAK_SK.win_fld(_wer,,'N_WK',,,20,,1,'Wartość'@);
OPAK_SK.win_fld(_wer,,'KH_SK',,,20,,1,'Stan kaucji kontrahenta'@);
OPAK_SK.win_fld(_wer,,'KH_WK',,,20,,1,'Wartość'@);
_fb:="exec('dokumenty','opakow_stany','SK')";
OPAK_SK.win_act(_wer,,'Formuła','Dokumenty'@,,,_fb,,1);
_wer


\stany_kh_wer_skd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Stany kontrahenta - okno wertowania - Stany kaucji wg dokumentów
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wer:=OPAK_SKD.mk_sel('Stany kaucji wg dokumentów'@,,0,'stany_kh_skd',5,10,15,,'U');
OPAK_SKD.win_fld(_wer,,'DATA',,,10,,1,'Data'@);
OPAK_SKD.win_fld(_wer,,'SYM',,,20,,1,'Symbol dokumentu'@);
OPAK_SKD.win_fld(_wer,,'POZ',,,8,,1,'Pozycja'@);
OPAK_SKD.win_fld(_wer,,'M','KTM',,20,,1,'Opakowanie'@);
OPAK_SKD.win_fld(_wer,,'N_SK',,,20,,1,'Stan kaucji naszych'@);
OPAK_SKD.win_fld(_wer,,'N_WK',,,20,,1,'Wartość'@);
OPAK_SKD.win_fld(_wer,,'KH_SK',,,20,,1,'Stan kaucji kontrahenta'@);
OPAK_SKD.win_fld(_wer,,'KH_WK',,,20,,1,'Wartość kaucji'@);
_fb:="exec('dokumenty','opakow_stany','SKD')";
OPAK_SKD.win_act(_wer,,'Formuła','Dokumenty'@,,,_fb,,1);
_fb:="
   exec('FindAndGet','#table',OPAK_P,OPAK_SKD.POZ_DOK,,\"
      exec('FindAndGet','#table',@.OPAK_N,@.OPAK_P.OPAK_N,,\\\"
         exec('opow_mk','opakow',0,,1)
      \\\")
   \")
";
OPAK_SKD.win_act(_wer,,'Formuła','Ro&zlicznie kaucji'@@,,'Rozliczenie wydanych opakowań'@,_fb,,,,,,'Z');
_wer


\tmp_opak_sm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Tymczasowy odpowiednik tabeli OPAK_SM
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_OPAK_SM:=_env.TAB:=tab_tmp(1
   ,'MAG','STRING[8]','Magazyn'
   ,'M','STRING[16]','Materiał'
   ,'S','REAL','Stan'
   ,'SD','REAL','Stan dostępny'
   ,'ILW','REAL','Ilość w wydaniu'
);
_env.NDX_M:=_OPAK_SM.ndx_tmp(,,'M',,,'MAG',,);
_env.NDX_MAG:=_OPAK_SM.ndx_tmp(,,'MAG',,,'M',,)


\tmp_opak_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Tymczasowy odpowiednik tabeli OPAK_SD
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_OPAK_SD:=_env.TAB:=tab_tmp(1
   ,'MAG','STRING[8]','Magazyn'
   ,'M','STRING[16]','Opakowanie'
   ,'OPAK_P','STRING[48]','Dostawa'
   ,'DATA','DATE','Data'
   ,'SYM','STRING[20]','Symbol dokumentu'
   ,'POZ','INTEGER','Pozycja dokumentu'
   ,'S','REAL','Stan'
   ,'SD','REAL','Stan dostępny'
   ,'ILW','REAL','Ilość w wydaniu'
);
_env.NDX_DOST:=_OPAK_SD.ndx_tmp(,,'OPAK_P',,);
_env.NDX_MAG:=_OPAK_SD.ndx_tmp(,,'MAG',,,'M',,,'DATA',,,'SYM',,,'POZ',,)


\tmp_opak_skh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Tymczasowy odpowiednik tabeli OPAK_SKH
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_OPAK_SKH:=_env.TAB:=tab_tmp(1
   ,'KH','STRING[16]','Kontrahent'
   ,'KH_ODB','STRING[16]','Odbiorca kontrahenta'
   ,'M','STRING[16]','Opakowanie'
   ,'N_SK','REAL','Stan kaucji naszych'
   ,'N_WK','REAL','Wartość kaucji naszych'
   ,'KH_SK','REAL','Stan kaucji kontrahenta'
   ,'KH_WK','REAL','Wartość kaucji kontrahenta'
   ,'N_SBK','REAL','Stan bez kaucji u nas'
   ,'KH_SBK','REAL','Stan bez kaucji u kontrahenta'
);
_env.NDX_KH:=_OPAK_SKH.ndx_tmp(,,'KH',,,'KH_ODB',,,'M',,)


\tmp_opak_sk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Tymczasowy odpowiednik tabeli OPAK_SK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_OPAK_SK:=_env.TAB:=tab_tmp(1
   ,'MAG','STRING[8]','Magazyn'
   ,'KH','STRING[16]','Kontrahent'
   ,'M','STRING[16]','Opakowanie'
   ,'N_SK','REAL','Stan kaucji naszych'
   ,'N_WK','REAL','Wartość kaucji naszych'
   ,'KH_SK','REAL','Stan kaucji kontrahenta'
   ,'KH_WK','REAL','Wartość kaucji kontrahenta'
);
_env.NDX_KH:=_OPAK_SK.ndx_tmp(,,'KH',,,'MAG',,,'M',,);
_env.NDX_MAG:=_OPAK_SK.ndx_tmp(,,'MAG',,,'KH',,,'M',,)


\tmp_opak_skd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Tymczasowy odpowiednik tabeli OPAK_SKD
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_OPAK_SKD:=_env.TAB:=tab_tmp(1
   ,'MAG','STRING[8]','Magazyn'
   ,'KH','STRING[16]','Kontrahent'
   ,'M','STRING[16]','Materiał'
   ,'POZ_DOK','STRING[48]','Pozycja dokumentu'
   ,'SYM','STRING[20]','Symbol dokumentu'
   ,'DATA','DATE','Data'
   ,'POZ','INTEGER','Pozycja dokumentu'
   ,'N_SK','REAL','Stan kaucji naszych'
   ,'N_WK','REAL','Wartość kaucji naszych'
   ,'KH_SK','REAL','Stan kaucji kontrahenta'
   ,'KH_WK','REAL','Wartość kaucji kontrahenta'
);
_env.NDX_UNIK:=_OPAK_SKD.ndx_tmp(,,'MAG',,,'KH',,,'M',,,'POZ_DOK',,);
_env.NDX_VIEW:=_OPAK_SKD.ndx_tmp(,,'MAG',,,'KH',,,'M',,,'DATA',,,'SYM',,)


\tmp_s_src
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Dokumenty źródłowe uwzględnione przy obliczaniu stanu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_S_SRC:=_env.TAB:=tab_tmp(10
   ,'SM','STRING[1]','Stan magazynowy'
   ,'CZY_KAUC','STRING[1]','Czy kaucja'
   ,'KH','STRING[10]','Kontrahent'
   ,'KH_ODB','STRING[10]','Odbiorca kontrahenta'
   ,'M','STRING[50]','Materiał'
   ,'MAG','STRING[8]','Magazyn'
   ,'DATA','DATE','Data'
   ,'LP','INTEGER','LP'
   ,'SYM','STRING[20]','Symbol dokumentu'
   ,'POZ','INTEGER','Pozycja dokumentu'
   ,'POZ_DOK','STRING[48]','Pozycja dokumentu'
   ,'S','REAL','Stan'
   ,'N_SK','REAL','Stan kaucji naszych'
   ,'N_WK','REAL','Wartość kaucji naszych'
   ,'KH_SK','REAL','Stan kaucji kontrahenta'
   ,'KH_WK','REAL','Wartość kaucji kontrahenta'
   ,'N_SBK','REAL','Stan bez kaucji u nas'
   ,'KH_SBK','REAL','Stan bez kaucji u kontrahenta'
);
_env.NDX_SM:=_S_SRC.ndx_tmp(,,'SM',,,'MAG',,,'M',,,'DATA',,,'SYM',,,'POZ',,);
_env.NDX_SKH:=_S_SRC.ndx_tmp(,,'KH',,,'KH_ODB',,,'M',,,'MAG',,,'CZY_KAUC',,,'DATA',,,'LP',,,'SYM',,,'POZ',,);
_env.NDX_SK:=_S_SRC.ndx_tmp(,,'CZY_KAUC',,,'KH',,,'M',,,'MAG',,,'DATA',,,'LP',,,'SYM',,,'POZ',,)


\tmp_s_src_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Dokumenty źródłowe uwzględnione przy obliczaniu stanu - dodaje pozycję dokumentu do tabeli S_SRC
::   WE: _a - OPAK_P.ref() lub FAP.ref()
::       _b - zmiana stanu kaucji naszych
::       _c - zmiana wartości kaucji naszych
::       _d - zmiana stanu kaucji kontrahenta
::       _e - zmiana wartości kaucji kontrahenta
::       _f - zmiana stanu bez kaucji u nas
::       _g - zmiana stanu bez kaucji u kontrahenta
::       _h - magazyn
::       _i - T/N stan magazynowy
::       _j - zmiana stanu magazynowego
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_S_SRC:=_env.S_SRC.TAB;
_S_SRC.blank();
{? ref_tab(_a)=OPAK_P
||
   _S_SRC.CZY_KAUC:=OPAK_P.CZY_KAUC;
   _S_SRC.KH:=OPAK_N.KH().SKR;
   _S_SRC.KH_ODB:=OPAK_N.KH_ODB().SKR;
   _S_SRC.M:=OPAK_P.M().KTM;
   _S_SRC.MAG:=OPAK_N.MAG;
   _S_SRC.LP:={? OPAK_P.ZWROT='T'|| 3 |? OPAK_N.PLUS='T' || 1 || 2 ?};
   _S_SRC.DATA:=OPAK_N.DATA;
   _S_SRC.SYM:=OPAK_N.SYM;
   _S_SRC.POZ:=OPAK_P.POZ;
   _S_SRC.POZ_DOK:=$OPAK_P.ref()
||
   _params:=obj_new('S_SRC','MAG');
   _params.S_SRC:=_S_SRC;
   _params.MAG:=_h;
   exec('FindAndGet','#table',FAP,_a,,"
      _params:=_b;
      exec('FindAndGet','#table',@.FAKS,@.FAP.FAKS,,\"
         _params:=_b;
         _S_SRC:=_params.S_SRC;
         _S_SRC.CZY_KAUC:='T';
         _S_SRC.KH:=FAKS.KH().SKR;
         _S_SRC.KH_ODB:=FAKS.KH_ODB().SKR;
         _S_SRC.M:=FAP.M().KTM;
         _S_SRC.MAG:=_params.MAG;
         _S_SRC.LP:=3;
         _S_SRC.DATA:=FAKS.DW;
         _S_SRC.SYM:=FAKS.SYM;
         _S_SRC.POZ:=FAP.POZ;
         _S_SRC.POZ_DOK:=$FAP.ref()
      \",,_params)
   ",,_params)
?};
_S_SRC.N_SK:=_b;
_S_SRC.N_WK:=_c;
_S_SRC.KH_SK:=_d;
_S_SRC.KH_WK:=_e;
_S_SRC.N_SBK:=_f;
_S_SRC.KH_SBK:=_g;
_S_SRC.SM:=_i;
_S_SRC.S:=_j;
_S_SRC.add()


\tmp_rebuild
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Tymczasowe naliczenie stanów
::   WE: [_a] - magazyn
::       [_b] - materiał
::       [_c] - kontrahent
::       [_d] - odbiorca kontrahenta
::       [_e] - dzień
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mag:={? var_pres('_a')=type_of('') || _a || '' ?};
_mat:={? var_pres('_b')=type_of(null()) || _b || null() ?};
_kh:={? var_pres('_c')=type_of(null()) || _c || null() ?};
_kh_odb:={? var_pres('_d')=type_of(null()) || _d || ~~ ?};
_na_dzien:={? var_pres('_e')=type_of(date()) || _e || date(0,0,0) ?};
_rok:=_na_dzien~1;

_env:=obj_new('OPAK_SM','OPAK_SD','OPAK_SKH','OPAK_SK','OPAK_SKD','S_SRC');
_env.OPAK_SM:=obj_new('TAB','NDX_M','NDX_MAG');
exec('tmp_opak_sm','opakow_stany',_env.OPAK_SM);
_env.OPAK_SD:=obj_new('TAB','NDX_DOST','NDX_MAG');
exec('tmp_opak_sd','opakow_stany',_env.OPAK_SD);
_env.OPAK_SKH:=obj_new('TAB','NDX_KH');
exec('tmp_opak_skh','opakow_stany',_env.OPAK_SKH);
_env.OPAK_SK:=obj_new('TAB','NDX_KH','NDX_MAG');
exec('tmp_opak_sk','opakow_stany',_env.OPAK_SK);
_env.OPAK_SKD:=obj_new('TAB','NDX_UNIK','NDX_VIEW');
exec('tmp_opak_skd','opakow_stany',_env.OPAK_SKD);
params_set('env',_env);
_env.S_SRC:=obj_new('TAB','NDX_SM','NDX_SKH','NDX_SK');
exec('tmp_s_src','opakow_stany',_env.S_SRC);
params_set('env',_env);

_OPAK_SM:=_env.OPAK_SM.TAB;
_OPAK_SD:=_env.OPAK_SD.TAB;
_OPAK_SKH:=_env.OPAK_SKH.TAB;
_OPAK_SK:=_env.OPAK_SK.TAB;
_OPAK_SKD:=_env.OPAK_SKD.TAB;

_OPAK_SM.cntx_psh();
_OPAK_SD.cntx_psh();
_OPAK_SKH.cntx_psh();
_OPAK_SK.cntx_psh();
_OPAK_SKD.cntx_psh();
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
exec('cntx_psh','opakow_stany');
_loop:=OKR.first();
_licz:=0;
_size:=OKR.size();
_prgs:=100*_licz/_size;
{!
|? _loop
|!
   {? _na_dzien=date(0,0,0) | OKR.ROK<=_rok
   ||
      progress(_prgs,'Trwa przetwarzanie (%1%%)'@[$int(_prgs)]);
      exec('open_tab','opakow_stany',ST.ODDZ,$OKR.ROK+2);
      exec('cntx_psh','opakow_stany');
      exec('tmp_rebuild_rok','opakow_stany',_mag,_mat,_kh,_kh_odb,_na_dzien,OKR.ROK);
      exec('cntx_pop','opakow_stany')
   ?};
   _licz+=1;
   _prgs:=100*_licz/_size;
   _loop:=OKR.next()
!};
prgs_clr();
exec('cntx_pop','opakow_stany');
OKR.cntx_pop();
_OPAK_SKD.cntx_pop();
_OPAK_SK.cntx_pop();
_OPAK_SKH.cntx_pop();
_OPAK_SD.cntx_pop();
_OPAK_SM.cntx_pop();
_env


\tmp_rebuild_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Odbudowanie stanów opakowań w roku
::   WE: _a - magazyn
::       _b - materiał
::       _c - kontrahent
::       _d - odbiorca kontrahenta
::       _e - dzień
::       _f - rok
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mag:=_a;
_mat:=_b;
_kh:=_c;
_kh_odb:=_d;
_na_dzien:=_e;
_rok:=_f;

_tmp:=1;

OPAK_P.prefix();
OPAK_P.f_clear();
_sort:='OPAK_N(AR),OPAK_N(MAG),M,OPAK_N(AKC)^,OPAK_N(DATA),OPAK_N(PLUS)^';
_from:='join OPAK_N using(OPAK_P.OPAK_N,OPAK_N.reference)';
_where:=
   "OPAK_N.AR=:_a"
   +{? _mag='' || "" || " and OPAK_N.MAG=':_b'" ?}
   +{? _mat=null() || "" || " and OPAK_P.M=:_c" ?}
   +{? _kh=null() || "" || " and OPAK_N.KH=:_d" ?}
   +{? _kh_odb=~~ || "" |? _kh_odb=null() || "and OPAK_N.KH_ODB is null" || " and OPAK_N.KH_ODB=:_e" ?}
   +{? _na_dzien=date(0,0,0) || "" || " and OPAK_N.DATA<=to_date(:_f)" ?};
OPAK_P.f_set(_sort,_from,_where,_rok,_mag,_mat,_kh,_kh_odb,_na_dzien);
_loop:=OPAK_P.f_first();
{!
|? _loop
|!
   OPAK_P.OPAK_N();
   {? OPAK_N.AKC='T' | OPAK_N.PLUS='N' || params_exec('rebuild_poz','opakow_stany',1,,_tmp) ?};
   _loop:=OPAK_P.f_next()
!};
OPAK_P.f_clear();
:: kaucje nie dotyczą odbiorców kontrahentów
{? _kh_odb=~~ | _kh_odb=null() || params_exec('rebuild_fap','opakow_stany',1,,,_tmp,_mag,$_mat,$_kh,_na_dzien) ?};
~~


\rebuild
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Odbudowanie stanów opakowań
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
exec('cntx_psh','opakow_stany');
exec('stany_erase','opakow_stany');
_loop:=OKR.first();
_licz:=0;
_size:=OKR.size();
_prgs:=100*_licz/_size;
{!
|? _loop
|!
   progress(_prgs,'Trwa odtwarzanie stanów (%1%%)'@[$int(_prgs)]);
   exec('open_tab','opakow_stany',ST.ODDZ,$OKR.ROK+2);
   exec('cntx_psh','opakow_stany');
   exec('rebuild_rok','opakow_stany',OKR.ROK);
   exec('cntx_pop','opakow_stany');
   _licz+=1;
   _prgs:=100*_licz/_size;
   _loop:=OKR.next()
!};
prgs_clr();
OKR.cntx_pop();
exec('cntx_pop','opakow_stany')


\rebuild_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Odbudowanie stanów opakowań w roku
::   WE: _a - rok
::       _b - 0/1 - zapis do struktur tymczsowych
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_rok:=_a;
_tmp:={? var_pres('_b')=type_of(0) || _b || 0 ?};
OPAK_N.index('RMMP');
OPAK_N.prefix(_rok);
_loop:=OPAK_N.first();
{!
|? _loop
|!
   {? OPAK_N.AKC='T' | OPAK_N.PLUS='N'
   ||
      OPAK_P.index('OPAK_N');
      OPAK_P.prefix(OPAK_N.ref());
      _loop:=OPAK_P.first();
      {!
      |? _loop
      |!
         params_exec('rebuild_poz','opakow_stany',1,,_tmp);
         _loop:=OPAK_P.next()
      !}
   ?};
   _loop:=OPAK_N.next()
!};
params_exec('rebuild_fap','opakow_stany',1,,,_tmp);
~~


\rebuild_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Odbudowanie stanów opakowań wg pozycji dokumentu (wg bufora OPAK_N i OPAK_P)
::   WE: _a - 0/1 - odtwarzanie stanów
::       _b - 0/1 - wycofanie
::       _c - 0/1 - zapis do struktur tymczsowych
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_rebuild:=_a;
_wyc:={? var_pres('_b')=type_of(0) || _b || {? OPAK_N.AKC='N' || 1 || 0 ?} ?};
_tmp:={? var_pres('_c')=type_of(0) || _c || 0 ?};

_s_src:=0;
{? _tmp
||
   _env:=params_get().env;
   _OPAK_SD:=_env.OPAK_SD.TAB; _opak_sd_ndx_dost:=_env.OPAK_SD.NDX_DOST;
   _OPAK_SKH:=_env.OPAK_SKH.TAB; _opak_skh_ndx_kh:=_env.OPAK_SKH.NDX_KH;
   _OPAK_SK:=_env.OPAK_SK.TAB; _opak_sk_ndx_mag:=_env.OPAK_SK.NDX_MAG;
   _OPAK_SKD:=_env.OPAK_SKD.TAB; _opak_skd_ndx_unik:=_env.OPAK_SKD.NDX_UNIK;
   _S_SRC:=_env.S_SRC.TAB;
   _s_src:=1
||
   _OPAK_SD:=OPAK_SD; _opak_sd_ndx_dost:='DOST';
   _OPAK_SKH:=OPAK_SKH; _opak_skh_ndx_kh:='KH';
   _OPAK_SK:=OPAK_SK; _opak_sk_ndx_mag:='MAG';
   _OPAK_SKD:=OPAK_SKD; _opak_skd_ndx_unik:='UNIK'
?};

_data:=OPAK_N.DATA;
_sym:=OPAK_N.SYM;
_mag:=OPAK_N.MAG;
_plus:=OPAK_N.PLUS='T';
_wsk:={? _wyc || -1 || 1 ?};
_poz_dok:=$OPAK_P.ref();
_poz:=OPAK_P.POZ;
_il:=_wsk*OPAK_P.IL;
_war:=_wsk*OPAK_P.WAR;
{? _tmp
||
   _kh:=$OPAK_N.KH;
   _kh_odb:=$OPAK_N.KH_ODB;
   _mat:=$OPAK_P.M
||
   _kh:=OPAK_N.KH;
   _kh_odb:=OPAK_N.KH_ODB;
   _mat:=OPAK_P.M
?};

_n_sbk:=_kh_sbk:=0;
_n_sk:=_n_wk:=_kh_sk:=_kh_wk:=0;
{? OPAK_P.ZWROT='T'     || {? _plus || _n_sk-=_il; _n_wk-=_war || _kh_sk-=_il; _kh_wk-=_war ?}
|? OPAK_P.CZY_KAUC='T'  || {? _plus || _kh_sk:=_il; _kh_wk:=_war || _n_sk:=_il; _n_wk:=_war ?}
?};
_wsk:={? _plus || _wsk || -_wsk ?};
_il:=_wsk*OPAK_P.IL;
::
:: stany dostaw
::
_OPAK_SD.index(_opak_sd_ndx_dost);
{? _plus
||
   _OPAK_SD.prefix(_poz_dok);
   _put:={? _OPAK_SD.first() || 1 || _OPAK_SD.blank(); 0 ?};
   _OPAK_SD.MAG:=_mag;
   _OPAK_SD.M:=_mat;
   _OPAK_SD.DATA:=_data;
   _OPAK_SD.SYM:=_sym;
   _OPAK_SD.POZ:=_poz;
   _OPAK_SD.OPAK_P:=_poz_dok;
   _OPAK_SD.S+=_il;
   _OPAK_SD.SD+=_il;
   {? _OPAK_SD.S=0 || {? _put || _OPAK_SD.del() ?}
   |? _put || _OPAK_SD.put()
   || _OPAK_SD.add()
   ?}
||
   OPAK_ZWR.index('REFTROZL');
   OPAK_ZWR.prefix('N',_poz_dok);
   _loop:=OPAK_ZWR.first();
   {!
   |? _loop
   |!
      {? OPAK_ZWR.FAKT='N'
      ||
         _OPAK_SD.prefix(OPAK_ZWR.REF_POZ);
         _put:={? _OPAK_SD.first() || 1 || _OPAK_SD.blank(); 0 ?};
         {? _put=0
         ||
            _OPAK_SD.MAG:=_mag;
            _OPAK_SD.M:=_mat;
            exec('FindAndGet','#table',OPAK_P,OPAK_ZWR.REF_POZ,,"
               _OPAK_SD:=_b;
               exec('FindAndGet','#table',@.OPAK_N,@.OPAK_P.OPAK_N,,\"
                  _OPAK_SD:=_b;
                  _OPAK_SD.DATA:=@.OPAK_N.DATA;
                  _OPAK_SD.SYM:=@.OPAK_N.SYM;
                  _OPAK_SD.POZ:=@.OPAK_P.POZ
               \",,_OPAK_SD)
            ",,_OPAK_SD)
         ?};
         _OPAK_SD.OPAK_P:=OPAK_ZWR.REF_POZ;
         _OPAK_SD.S+={? _wyc & _rebuild || 0 || _wsk*OPAK_ZWR.IL ?};
         _OPAK_SD.ILW+={? _wyc || OPAK_ZWR.IL |? _rebuild || 0 || -OPAK_ZWR.IL ?};
         _OPAK_SD.SD:=_OPAK_SD.S-_OPAK_SD.ILW;
         {? _OPAK_SD.S=0 || {? _put || _OPAK_SD.del() ?}
         |? _put || _OPAK_SD.put()
         || _OPAK_SD.add()
         ?}
      ?};
      _loop:=OPAK_ZWR.next()
   !}
?};
::
:: stany magazynowe
::
params_exec('opak_sm_update','opakow_stany',_mag,_mat,_tmp);
::
:: stany kontrahenta
::
{? _rebuild=0 | _wyc=0
||
   _OPAK_SKH.index(_opak_skh_ndx_kh);
   _OPAK_SKH.prefix(_kh,_kh_odb,_mat);
   _put:={? _OPAK_SKH.first() || 1 || _OPAK_SKH.blank(); 0 ?};
   _OPAK_SKH.KH:=_kh;
   _OPAK_SKH.KH_ODB:=_kh_odb;
   _OPAK_SKH.M:=_mat;
   _OPAK_SKH.N_SK+=_n_sk;
   _OPAK_SKH.N_WK+=_n_wk;
   _OPAK_SKH.KH_SK+=_kh_sk;
   _OPAK_SKH.KH_WK+=_kh_wk;
   {? OPAK_P.CZY_KAUC='N'
   ||
      _sbk:=_OPAK_SKH.N_SBK-_OPAK_SKH.KH_SBK;
      {? _sbk=0
      || {? _sbk+_il>0 || _n_sbk:=_il || _kh_sbk:=-_il ?}
      |? _sbk>0
      || {? _sbk+_il>0 || _n_sbk:=_il || _n_sbk:=-_sbk; _kh_sbk:=-_il-_sbk ?}
      || {? _sbk+_il<0 || _kh_sbk:=-_il || _kh_sbk:=_sbk; _n_sbk:=_il+_sbk ?}
      ?};
      _sbk+=_il;
      {? _sbk>=0 || _OPAK_SKH.N_SBK:=_sbk; _OPAK_SKH.KH_SBK:=0 || _OPAK_SKH.N_SBK:=0; _OPAK_SKH.KH_SBK:=-_sbk ?}
   ?};
   {? _OPAK_SKH.N_SK=0 & _OPAK_SKH.N_WK=0 & _OPAK_SKH.KH_SK=0 & _OPAK_SKH.KH_WK=0 & _OPAK_SKH.N_SBK=0 & _OPAK_SKH.KH_SBK=0
   || {? _put || _OPAK_SKH.del() ?}
   |? _put || _OPAK_SKH.put()
   || _OPAK_SKH.add()
   ?}
?};
::
:: dokument źródłowy
::
{? _s_src || params_exec('tmp_s_src_add','opakow_stany',OPAK_P.ref(),_n_sk,_n_wk,_kh_sk,_kh_wk,_n_sbk,_kh_sbk,,'T',_il) ?};
::
:: kaucje wg dokumentów
::
{? OPAK_P.CZY_KAUC='T' & (_rebuild=0 | _wyc=0)
||
   {? OPAK_P.ZWROT='T'
   ||
      OPAK_ZWR.index('REFTROZL');
      OPAK_ZWR.prefix('N',$OPAK_P.ref());
      _loop:=OPAK_ZWR.first();
      {!
      |? _loop
      |!
         _ref_poz:={? OPAK_ZWR.REF_POZK='' || OPAK_ZWR.REF_POZ || OPAK_ZWR.REF_POZK ?};
         _OPAK_SKD.index(_opak_skd_ndx_unik);
         _OPAK_SKD.prefix(_mag,_kh,_mat,_ref_poz);
         _wsk:={? _wyc || -1 || 1 ?};
         _il:=OPAK_ZWR.IL;
         _kaucja:=exec('FindAndGet','#table',OPAK_P,_ref_poz,,"OPAK_P.KAUCJA",0);
         _il:=_wsk*_il;
         _war:=(_il*_kaucja)$2;
         _n_sk:=_n_wk:=_kh_sk:=_kh_wk:=0;
         {? OPAK_P.ZWROT='T'     || {? _plus || _n_sk-=_il; _n_wk-=_war || _kh_sk-=_il; _kh_wk-=_war ?}
         |? OPAK_P.CZY_KAUC='T'  || {? _plus || _kh_sk:=_il; _kh_wk:=_war || _n_sk:=_il; _n_wk:=_war ?}
         ?};
         _wsk:={? _plus || _wsk || -_wsk ?};
         _il:=_wsk*_il;
         _put:={? _OPAK_SKD.first() || 1 || _OPAK_SKD.blank(); 0 ?};
         _OPAK_SKD.MAG:=_mag;
         _OPAK_SKD.KH:=_kh;
         _OPAK_SKD.M:=_mat;
         _OPAK_SKD.POZ_DOK:=_ref_poz;
         exec('FindAndGet','#table',OPAK_P,_ref_poz,,"
            _OPAK_SKD:=_b;
            exec('FindAndGet','#table',@.OPAK_N,@.OPAK_P.OPAK_N,,\"
               _OPAK_SKD:=_b;
               _OPAK_SKD.DATA:=@.OPAK_N.DATA;
               _OPAK_SKD.SYM:=@.OPAK_N.SYM;
               _OPAK_SKD.POZ:=@.OPAK_P.POZ
            \",,_OPAK_SKD)
         ",,_OPAK_SKD);
         _OPAK_SKD.N_SK+=_n_sk;
         _OPAK_SKD.N_WK+=_n_wk;
         _OPAK_SKD.KH_SK+=_kh_sk;
         _OPAK_SKD.KH_WK+=_kh_wk;
         {? _OPAK_SKD.N_SK=0 & _OPAK_SKD.N_WK=0 & _OPAK_SKD.KH_SK=0 & _OPAK_SKD.KH_WK=0 || {? _put || _OPAK_SKD.del() ?}
         |? _put || _OPAK_SKD.put()
         || _OPAK_SKD.add()
         ?};
         _loop:=OPAK_ZWR.next()
      !}
   ||
      _OPAK_SKD.index(_opak_skd_ndx_unik);
      _OPAK_SKD.prefix(_mag,_kh,_mat,_poz_dok);
      _put:={? _OPAK_SKD.first() || 1 || _OPAK_SKD.blank(); 0 ?};
      _OPAK_SKD.MAG:=_mag;
      _OPAK_SKD.KH:=_kh;
      _OPAK_SKD.M:=_mat;
      _OPAK_SKD.POZ_DOK:=_poz_dok;
      _OPAK_SKD.SYM:=_sym;
      _OPAK_SKD.DATA:=_data;
      _OPAK_SKD.POZ:=_poz;
      _OPAK_SKD.N_SK+=_n_sk;
      _OPAK_SKD.N_WK+=_n_wk;
      _OPAK_SKD.KH_SK+=_kh_sk;
      _OPAK_SKD.KH_WK+=_kh_wk;
      {? _OPAK_SKD.N_SK=0 & _OPAK_SKD.N_WK=0 & _OPAK_SKD.KH_SK=0 & _OPAK_SKD.KH_WK=0 || {? _put || _OPAK_SKD.del() ?}
      |? _put || _OPAK_SKD.put()
      || _OPAK_SKD.add()
      ?}
   ?};
::
:: kaucje
::
   _OPAK_SK.index(_opak_sk_ndx_mag);
   _OPAK_SK.prefix(_mag,_kh,_mat);
   {? _OPAK_SK.first() || _OPAK_SK.del() ?};
   _OPAK_SK.blank();
   _OPAK_SK.MAG:=_mag;
   _OPAK_SK.KH:=_kh;
   _OPAK_SK.M:=_mat;
   _OPAK_SKD.index(_opak_skd_ndx_unik);
   _OPAK_SKD.prefix(_mag,_kh,_mat);
   _loop:=_OPAK_SKD.first();
   {!
   |? _loop
   |!
      _OPAK_SK.N_SK+=_OPAK_SKD.N_SK;
      _OPAK_SK.N_WK+=_OPAK_SKD.N_WK;
      _OPAK_SK.KH_SK+=_OPAK_SKD.KH_SK;
      _OPAK_SK.KH_WK+=_OPAK_SKD.KH_WK;
      _loop:=_OPAK_SKD.next()
   !};
   {? _OPAK_SK.N_SK | _OPAK_SK.N_WK | _OPAK_SK.KH_SK | _OPAK_SK.KH_WK || _OPAK_SK.add() ?}
?};
1


\opak_sm_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizuje OPAK_SM
::   WE: _a - magazyn
::       _b - materiał
::       [_c] - 0/1 - zapis do struktur tymczsowych
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mag:=_a;
_mat:=_b;
_tmp:={? var_pres('_c')=type_of(0) || _c || 0 ?};
{? _tmp
||
   _env:=params_get().env;
   _OPAK_SM:=_env.OPAK_SM.TAB;
   _opak_sm_ndx_mag:=_env.OPAK_SM.NDX_MAG;
   _OPAK_SD:=_env.OPAK_SD.TAB;
   _opak_sd_ndx_dost:=_env.OPAK_SD.NDX_MAG
||
   _OPAK_SM:=OPAK_SM;
   _opak_sm_ndx_mag:='MAG';
   _OPAK_SD:=OPAK_SD;
   _opak_sd_ndx_dost:='MAG'
?};
_OPAK_SM.cntx_psh();
_OPAK_SM.index(_opak_sm_ndx_mag);
_OPAK_SM.prefix(_mag,_mat);
_put:={? _OPAK_SM.first() || 1 || _OPAK_SM.blank(); 0 ?};
_OPAK_SM.MAG:=_mag;
_OPAK_SM.M:=_mat;
_OPAK_SM.S:=0;
_OPAK_SM.ILW:=0;
_OPAK_SM.SD:=0;
_OPAK_SD.cntx_psh();
_OPAK_SD.index(_opak_sd_ndx_dost);
_OPAK_SD.prefix(_mag,_mat);
_loop:=_OPAK_SD.first();
{!
|? _loop
|!
   _OPAK_SM.S+=_OPAK_SD.S;
   _OPAK_SM.ILW+=_OPAK_SD.ILW;
   _OPAK_SM.SD+=_OPAK_SD.SD;
   _loop:=_OPAK_SD.next()
!};
{? _OPAK_SM.S=0 || {? _put || _OPAK_SM.del() ?}
|? _put || _OPAK_SM.put()
|| _OPAK_SM.add()
?};
_OPAK_SD.cntx_pop();
_OPAK_SM.cntx_pop()


\rebuild_fap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Odbudowanie stanów opakowań dla FAP
::   WE: _a - 0/1 - odtwarzanie stanów
::       [_b] - pozycja rozliczająca tylko dla _a=0
::       [_c] - stan akceptacji pozycji rozliczającej tylko dla _a=0 i jeśli ref_tab(_b)=OPAK_P
::       [_d] - 0/1 - zapis do struktur tymczsowych
::       [_e] - magazyn
::       [_f] - materiał
::       [_g] - kontrahent
::       [_h] - dzień
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_rebuild:=_a;
_ref_rozl:={? _rebuild || '' || _b ?};
_akc_rozl:={? _rebuild  || '' |? var_pres('_c')=type_of('') || _c || '' ?};
_tmp:={? var_pres('_d')=type_of(0) || _d || 0 ?};
{? _tmp
||
   _tmp_mag:=_e;
   _tmp_mat:=$_f;
   _tmp_kh:=$_g;
   _tmp_na_dzien:=_h
?};
_s_src:=0;
{? _tmp
||
   _env:=params_get().env;
   _OPAK_SKH:=_env.OPAK_SKH.TAB; _opak_skh_ndx_kh:=_env.OPAK_SKH.NDX_KH;
   _OPAK_SK:=_env.OPAK_SK.TAB; _opak_sk_ndx_mag:=_env.OPAK_SK.NDX_MAG;
   _OPAK_SKD:=_env.OPAK_SKD.TAB; _opak_skd_ndx_unik:=_env.OPAK_SKD.NDX_UNIK;
   _S_SRC:=_env.S_SRC.TAB;
   _s_src:=1
||
   _OPAK_SKH:=OPAK_SKH; _opak_skh_ndx_kh:='KH';
   _OPAK_SK:=OPAK_SK; _opak_sk_ndx_mag:='MAG';
   _OPAK_SKD:=OPAK_SKD; _opak_skd_ndx_unik:='UNIK'
?};
OPAK_ZWR.index('REF_ROZF');
{? _rebuild
|| OPAK_ZWR.prefix('T')
|| OPAK_ZWR.prefix('T',_ref_rozl)
?};
_loop:=OPAK_ZWR.first();
{!
|? _loop
|!
   _akc:='';
   {? ref_tab(OPAK_ZWR.REF_ROZL)=FAP
   ||
:: faktura za niezwrócone opakowania
      _fap:=exec('FindAndGet','#table',FAP,OPAK_ZWR.REF_ROZL,,"FAP.FAKS",null());
      _akc:=exec('FindAndGet','#table',FAKS,_fap,,"FAKS.AKC",'')

   |? ref_tab(OPAK_ZWR.REF_ROZL)=OPAK_P
   ||
:: ręczne rozliczenie za niezwrócone opakowania
      {? _rebuild || _akc:='T' || _akc:=_akc_rozl ?}
   ?};
   {? _rebuild=0 & _akc<>'' | _akc='T'
   ||
      _wyc:=_akc='N';
      _poz_dok:=OPAK_ZWR.REF_POZ;
      _opak_p:=exec('FindAndGet','#table',OPAK_P,_poz_dok,,"
         _opak_p:=obj_new('OPAK_N','POZ','M','KAUCJA');
         _opak_p.OPAK_N:=OPAK_P.OPAK_N;
         _opak_p.POZ:=OPAK_P.POZ;
         _opak_p.M:=OPAK_P.M;
         _opak_p.KAUCJA:=OPAK_P.KAUCJA;
         _opak_p
      ");
      _opak_n:=exec('FindAndGet','#table',OPAK_N,_opak_p.OPAK_N,,"
         _opak_n:=obj_new('MAG','KH','SYM','DATA','PLUS');
         _opak_n.MAG:=OPAK_N.MAG;
         _opak_n.KH:=OPAK_N.KH;
         _opak_n.SYM:=OPAK_N.SYM;
         _opak_n.DATA:=OPAK_N.DATA;
         _opak_n.PLUS:=OPAK_N.PLUS;
         _opak_n
      ");
      _mag:=_opak_n.MAG;
      _sym:=_opak_n.SYM;
      _data:=_opak_n.DATA;
      _poz:=_opak_p.POZ;
      _wsk:={? _wyc || -1 || 1 ?};
      _il:=OPAK_ZWR.IL;
      _kaucja:=_opak_p.KAUCJA;
      _il:=_wsk*_il;
      _n_sk:=_n_wk:=_kh_sk:=_kh_wk:=0;
      {? _opak_n.PLUS='N'
      ||
         _n_sk:=-_il;
         _n_wk:=(_n_sk*_kaucja)$2
      ||
         _kh_sk:=-_il;
         _kh_wk:=(_kh_sk*_kaucja)$2
      ?};
      {? _tmp
      ||
         _kh:=$_opak_n.KH;
         _kh_odb:='';
         _mat:=$_opak_p.M
      ||
         _kh:=_opak_n.KH;
         _kh_odb:=null();
         _mat:=_opak_p.M
      ?};
::
:: naliczenie stanów
::
      {? _tmp=0
            |
         (_tmp_mag='' | _tmp_mag=_mag)
         & (_tmp_mat='' | _tmp_mat=_mat)
         & (_tmp_kh='' | _tmp_kh=_kh)
         & (_tmp_na_dzien=date(0,0,0) | _data<=_tmp_na_dzien)
      ||
::
:: dokument źródłowy
::
         {? _s_src
         ||
            params_exec('tmp_s_src_add','opakow_stany',OPAK_ZWR.REF_ROZL,_n_sk,_n_wk,_kh_sk,_kh_wk,0,0,_mag,'N',0)
         ?};
::
:: stany kontrahenta
::
         _OPAK_SKH.index(_opak_skh_ndx_kh);
         _OPAK_SKH.prefix(_kh,_kh_odb,_mat);
         _put:={? _OPAK_SKH.first() || 1 || _OPAK_SKH.blank(); 0 ?};
         _OPAK_SKH.KH:=_kh;
         _OPAK_SKH.KH_ODB:=_kh_odb;
         _OPAK_SKH.M:=_mat;
         {? _opak_n.PLUS='N'
         ||
            _OPAK_SKH.N_SK+=_n_sk;
            _OPAK_SKH.N_WK+=_n_wk
         ||
            _OPAK_SKH.KH_SK+=_kh_sk;
            _OPAK_SKH.KH_WK+=_kh_wk
         ?};
         {? _OPAK_SKH.N_SK=0 & _OPAK_SKH.N_WK=0 & _OPAK_SKH.KH_SK=0 & _OPAK_SKH.KH_WK=0 & _OPAK_SKH.N_SBK=0 & _OPAK_SKH.KH_SBK=0
         || {? _put || _OPAK_SKH.del() ?}
         |? _put || _OPAK_SKH.put()
         || _OPAK_SKH.add()
         ?};
::
:: kaucje wg dokumentów
::
         _OPAK_SKD.index(_opak_skd_ndx_unik);
         _OPAK_SKD.prefix(_mag,_kh,_mat,_poz_dok);
         _put:={? _OPAK_SKD.first() || 1 || _OPAK_SKD.blank(); 0 ?};
         _OPAK_SKD.MAG:=_mag;
         _OPAK_SKD.KH:=_kh;
         _OPAK_SKD.M:=_mat;
         _OPAK_SKD.POZ_DOK:=_poz_dok;
         _OPAK_SKD.SYM:=_sym;
         _OPAK_SKD.DATA:=_data;
         _OPAK_SKD.POZ:=_poz;
         _OPAK_SKD.N_SK+=_n_sk;
         _OPAK_SKD.N_WK+=_n_wk;
         _OPAK_SKD.KH_SK+=_kh_sk;
         _OPAK_SKD.KH_WK+=_kh_wk;
         {? _OPAK_SKD.N_SK=0 & _OPAK_SKD.N_WK=0 & _OPAK_SKD.KH_SK=0 & _OPAK_SKD.KH_WK=0 || {? _put || _OPAK_SKD.del() ?}
         |? _put || _OPAK_SKD.put()
         || _OPAK_SKD.add()
         ?};
::
:: kaucje
::
         _OPAK_SK.index(_opak_sk_ndx_mag);
         _OPAK_SK.prefix(_mag,_kh,_mat);
         {? _OPAK_SK.first() || _OPAK_SK.del() ?};
         _OPAK_SK.blank();
         _OPAK_SK.MAG:=_mag;
         _OPAK_SK.KH:=_kh;
         _OPAK_SK.M:=_mat;
         _OPAK_SKD.index(_opak_skd_ndx_unik);
         _OPAK_SKD.prefix(_mag,_kh,_mat);
         _loop:=_OPAK_SKD.first();
         {!
         |? _loop
         |!
            _OPAK_SK.N_SK+=_OPAK_SKD.N_SK;
            _OPAK_SK.N_WK+=_OPAK_SKD.N_WK;
            _OPAK_SK.KH_SK+=_OPAK_SKD.KH_SK;
            _OPAK_SK.KH_WK+=_OPAK_SKD.KH_WK;
            _loop:=_OPAK_SKD.next()
         !};
         {? _OPAK_SK.N_SK | _OPAK_SK.N_WK | _OPAK_SK.KH_SK | _OPAK_SK.KH_WK || _OPAK_SK.add() ?}
      ?};
      obj_del(_opak_p);
      obj_del(_opak_n)
   ?};
   _loop:=OPAK_ZWR.next()
!}


\stany_erase
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Usunięcie zapisów z tabel
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OPAK_SKD.erase();
OPAK_SK.erase();
OPAK_SKH.erase();
OPAK_SM.erase();
OPAK_SD.erase()


\cntx_psh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Odłożenie kontekstów tabel
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();
OPAK_SKH.cntx_psh();
OPAK_SK.cntx_psh();
OPAK_SKD.cntx_psh();
OPAK_SM.cntx_psh();
OPAK_SD.cntx_psh();
OPAK_ZWR.cntx_psh();
OPAK_OPT.cntx_psh()


\cntx_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Zdjęcie kontekstów tabel
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OPAK_N.cntx_pop();
OPAK_P.cntx_pop();
OPAK_SKH.cntx_pop();
OPAK_SK.cntx_pop();
OPAK_SKD.cntx_pop();
OPAK_SM.cntx_pop();
OPAK_SD.cntx_pop();
OPAK_ZWR.cntx_pop();
OPAK_OPT.cntx_pop()


\open_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Związanie tabel ze zbiorem fizycznym
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
  OPAK_N.use('opakn'+_a+_b);
  OPAK_P.use('opakp'+_a+_b);
OPAK_SKH.use('opakb'+_a+'__');
 OPAK_SK.use('opakk'+_a+'__');
OPAK_SKD.use('opakd'+_a+'__');
 OPAK_SM.use('opaks'+_a+'__');
 OPAK_SD.use('opaka'+_a+'__');
OPAK_ZWR.use('opakz'+_a+_b);
OPAK_OPT.use('opakopt'+_a)


\opks_mk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MZ [2008]
:: OPIS: utworzenie tabeli stanow opakowan __opks_tab, tabela __opks_tab (i jesli trzeba __sm_opak) sa zdefiniowane
::   WE: [_a] - 0/1stany zerowe
::       [_b] - Magazyn
::       [_c] - KH.ref() - kontrahent
::       [_d] - KH_ODB.ref() - odbiorca kontrahenta
::       [_e] - M.ref() - materiał
::       [_f] - T/N plus
::       [_g] - T/N zwrot
::
::       [_h] - na dzien (!! tylko dla _a=0 !!)
::       [_i] - 0(domyslnie)-nie 1-odtwarzanie stanow
::----------------------------------------------------------------------------------------------------------------------
_stz:={? var_pres('_a')=type_of(0) || _a || 1 ?};
_mag:={? var_pres('_b')=type_of('') || _b || null() ?};
_kh:={? var_pres('_c')=type_of(null()) || _c || null() ?};
_kh_odb:={? var_pres('_d')=type_of(null()) || _d || ~~ ?};
_mat:={? var_pres('_e')=type_of(null()) || _e || null() ?};
_plus:={? var_pres('_f')=type_of('') || _f || '' ?};
_zwrot:={? var_pres('_g')=type_of('') || _g || 'N' ?};

_ktm:=exec('FindAndGet','#table',M,_mat,,"M.KTM",'');

__opks_tab.erase();
_ndx:=__opks_tab.ndx_tmp(,,'INDEKS',,,'INDEKS',,);
__opks_tab.index(_ndx);

:: petla po M (OPAKOWANIE)
M.cntx_psh();
M.index('AROP');
{? _ktm<>''
|| M.prefix('T','T','T',_ktm,_ktm)
|| M.prefix('T','T','T')
?};
{? M.first()
||
   {!
   |?
      _mat:=M.ref();
      _stan:=_sk:=0;
::    obliczenie stanu na podstawie OPAK_SM
      {? _zwrot='T' & _kh
      ||
         OPAK_SK.index('MAG');
         {? _kh || OPAK_SK.prefix(_mag,_kh,_mat)
         || OPAK_SK.prefix(null(),null())
         ?};
         _loop:=OPAK_SK.first();
         {!
         |? _loop
         |!
            _sk+={? _plus='T' || OPAK_SK.N_SK || OPAK_SK.KH_SK ?};
            _loop:=OPAK_SK.next()
         !}
      ?};
      {? _mag<>''
      ||
         OPAK_SM.index('MAG');
         OPAK_SM.prefix(_mag,_mat);
         {? OPAK_SM.first()
         ||
            {!
            |?
               _stan+=OPAK_SM.S;
               OPAK_SM.next()
            !}
         ?}
      ?};
::    tworzenie zapisu w __opks_tab dla pojedynczego indeksu opakowan
      {? _stz | _stan>0
      ||
         __opks_tab.INDEKS:=M.KTM;
         __opks_tab.NAZWA:=M.N;
         __opks_tab.S:=_stan;
         __opks_tab.SK:=_sk;
         __opks_tab.JM:=M.J().KOD;
         __opks_tab.SFK:=0;
         __opks_tab.add()
      ?};
      M.next()
   !}
?};
M.cntx_pop();

__opks_tab.ndx_drop(_ndx);
__opks_tab.first();
''


\stanydost
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Stany dostaw na dzień
::   WE: _a - magazyn
::       _b - materiał
::       _c - data
::       _d - ref_rozl
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_mag:=_a;
_mat:=_b;
_data:=_c;
_ref_rozl:=_d;

_Tab:=tab_tmp(1
   ,'DATA','DATE','Data'
   ,'SYM','STRING[20]','Symbol'
   ,'POZ','INTEGER','Pozycja'
   ,'S','REAL','Stan'
   ,'SD','REAL','Stan dostępny'
   ,'IL','REAL','Do rozliczenia'
   ,'OPAK_P','STRING[16]','Dostawa'
);

OPAK_SD.cntx_psh();
OPAK_SD.index('MAG');
OPAK_SD.prefix(_mag,_mat);
_loop:=OPAK_SD.first();
{!
|? _loop
|!
   _Tab.DATA:=OPAK_SD.DATA;
   _Tab.SYM:=OPAK_SD.SYM;
   _Tab.POZ:=OPAK_SD.POZ;
   _Tab.S:=OPAK_SD.S;
::   _il:=exec('stany_do_rozl','opakow_stany',_ref_rozl);
::   _Tab.SD:=OPAK_SD.S-exec('stanydost_w_wydaniu','opakow_stany',OPAK_SD.OPAK_P)+_il;
::   _Tab.IL:=_il;
   _Tab.SD:=OPAK_SD.S-exec('stanydost_w_wydaniu','opakow_stany',OPAK_SD.OPAK_P);
   _Tab.OPAK_P:=OPAK_SD.OPAK_P;
   _Tab.add();
   _loop:=OPAK_SD.next()
!};
OPAK_SD.cntx_pop();

_wer:=_Tab.mk_sel('Stany dostaw'@,,0,'stanyd',5,10,15,,'U');
_Tab.win_fld(_wer,,'DATA',,,10,,1,'Data'@);
_Tab.win_fld(_wer,,'SYM',,,20,,1,'Symbol'@);
_Tab.win_fld(_wer,,'POZ',,,20,,1,'Pozycja'@);
_Tab.win_fld(_wer,,'S',,,20,,1,'Stan'@);
_Tab.win_fld(_wer,,'SD',,,20,,1,'Stan dostępny'@);
_Tab.win_fld(_wer,,'IL',,,20,,,'Do rozliczenia'@);
_Tab.win_sel(_wer);
_Tab.select();

VAR_DEL.delete('__il_poz');

_Tab


\stanydost_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizuje stan dostawy _a
::   WE: _a - dostawa
::   WY:
::----------------------------------------------------------------------------------------------------------------------
OPAK_SD.cntx_psh();
OPAK_SD.index('DOST');
OPAK_SD.prefix(_a,);
{? OPAK_SD.first()
||
   OPAK_SD.ILW:=exec('stanydost_w_wydaniu','opakow_stany',OPAK_SD.OPAK_P);
   OPAK_SD.SD:=OPAK_SD.S-OPAK_SD.ILW;
   OPAK_SD.put();
   exec('opak_sm_update','opakow_stany',OPAK_SD.MAG,OPAK_SD.M)
?};
OPAK_SD.cntx_pop()


\stanydost_w_wydaniu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Ilość w wydaniu dla dostawy _a
::   WE: _a - dostawa
::       _b - pozycja rozliczająca
::       [_c] - T/N - przyjęcie
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref_poz:=_a;
_ref_rozl:={? var_pres('_b')=type_of('') || _b || '' ?};
_plus:={? var_pres('_c')=type_of('') || _c || 'N' ?};
_il:=0;
_params:=obj_new('REF_POZ','REF_ROZL','PLUS');
_params.REF_POZ:=_ref_poz;
_params.REF_ROZL:=_ref_rozl;
_params.PLUS:=_plus;
OPAK_ZWR.cntx_psh();
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
||
   {!
   |?
      OPAK_ZWR.use('opakz'+ST.ODDZ+($OKR.ROK+2));
      OPAK_ZWR.index('REFTPOZ');
      OPAK_ZWR.prefix('N',_ref_poz);
      _loop:=OPAK_ZWR.first();
      {!
      |? _loop
      |!
         {? exec('FindAndGet','#table',OPAK_P,OPAK_ZWR.REF_ROZL,,"
               exec('FindAndGet','#table',@.OPAK_N,@.OPAK_P.OPAK_N,,\"
                  (_b.REF_ROZL='' | _b.REF_ROZL<>$@.OPAK_P.ref()) & @.OPAK_N.AKC='N' & @.OPAK_N.PLUS=_b.PLUS
               \",0,_b)
            ",0,_params)
         || _il+=OPAK_ZWR.IL
         ?};
         _loop:=OPAK_ZWR.next()
      !};
      OKR.next()
   !}
?};
OKR.cntx_pop();
OPAK_ZWR.cntx_pop();
_il


\stanydost_w_przyjeciu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Ilość w wydaniu dla dostawy _a
::   WE: _a - dostawa
::       _b - pozycja rozliczająca
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref_poz:=_a;
_ref_rozl:={? var_pres('_b')=type_of('') || _b || '' ?};
exec('stanydost_w_wydaniu','opakow_stany',_ref_poz,_ref_rozl,'T')


\stanydost_rozpisz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Rozpisuje ilość do rozliczenia na dostawy
::   WE: _a - magazyn
::       _b - materiał
::       _c - data
::       _d - ilosć do rozliczenia
::       _e - dostawa
::       _f - ref_rozl
::   WY: ilość rozliczona
::----------------------------------------------------------------------------------------------------------------------
_mag:=_a;
_mat:=_b;
_data:=_c;
_il:=_d;
_dost:=_e;
_ref_rozl:=_f;

_result:=0;

_il_poz_do_rozl:=_il;

OPAK_SD.cntx_psh();
OPAK_SD.index('DOST');
OPAK_SD.prefix(_dost);
{? OPAK_SD.first()
||
:: w pierszej kolejności rozliczenie dostawy tworzącej kaucję
   _il_do_rozl:={? OPAK_SD.S<_il_poz_do_rozl || OPAK_SD.S || _il_poz_do_rozl ?};
   _il_poz_do_rozl-=_il_do_rozl;
   exec('opak_zwr_update','opakow_stany',_ref_rozl,_dost,'',_il_do_rozl,'N','T')
?};
{? _il_poz_do_rozl
||
:: jeśli nie powiodło się na dostawie do rozliczenia rozliczamy dostawy tworzące stan
   OPAK_SD.index('MAG');
   OPAK_SD.prefix(_mag,_mat);
   _loop:=OPAK_SD.first();
   {!
   |? _loop
   |!
      {? OPAK_SD.OPAK_P<>_dost
      ||
         _il_do_rozl:={? OPAK_SD.S<_il_poz_do_rozl || OPAK_SD.S || _il_poz_do_rozl ?};
         _il_poz_do_rozl-=_il_do_rozl;
         exec('opak_zwr_update','opakow_stany',_ref_rozl,OPAK_SD.OPAK_P,_dost,_il_do_rozl,'N','T')
      ?};
      _loop:=OPAK_SD.next()
   !}
?};
OPAK_SD.cntx_pop();

_result:=_il-_il_poz_do_rozl;
{? _il_poz_do_rozl || FUN.info('Nie powiodło się rozliczenie zadanej ilośći.\nRozliczona ilość to %1.'@[$_result]) ?};
_result


\opak_zwr_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizuje zapis w OPAK_ZWR
::   WE: _a - ref pozycji rozliczającej
::       _b - ref pozycji rozliczanej
::       _c - ref pozycji rozliczanej - kaucja
::       _d - ilość
::       _e - sposób utworzenia
::       _f - T/N tymczasowa
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref_rozl:=_a;
_ref_poz:=_b;
_ref_pozk:=_c;
_il:=_d;
_fakt:=_e;
_tmp:=_f;
OPAK_ZWR.cntx_psh();
OPAK_ZWR.use(gsub(OPAK_ZWR.name(1),'???',ref_name(_ref_rozl)+3));
OPAK_ZWR.cntx_psh();
OPAK_ZWR.index('REFTROZL');
OPAK_ZWR.prefix(_tmp,_ref_rozl,_ref_poz,_ref_pozk,);
_put:={? OPAK_ZWR.first() || 1 || OPAK_ZWR.blank(); 0 ?};
{? _il=0
||
   {? _put || OPAK_ZWR.del() ?}
||
   OPAK_ZWR.REF_ROZL:=_ref_rozl;
   OPAK_ZWR.REF_POZ:=_ref_poz;
   OPAK_ZWR.REF_POZK:=_ref_pozk;
   OPAK_ZWR.IL:=_il;
   OPAK_ZWR.FAKT:=_fakt;
   OPAK_ZWR.TMP:=_tmp;
   {? _put || OPAK_ZWR.put() || OPAK_ZWR.add() ?}
?};
OPAK_ZWR.cntx_pop();
OPAK_ZWR.cntx_pop();
{? _tmp='N' & _fakt='N' || exec('stanydost_update','opakow_stany',_ref_poz) ?}


\opak_zwr_copy_to_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Tworzy tymczasowe rozliczenie dla pozycji rozliczającej _a
::   WE: _a - pozycja rozliczająca
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref_rozl:=_a;
exec('opak_zwr_del','opakow_stany',_ref_rozl,'T');
OPAK_ZWR.cntx_psh();
OPAK_ZWR.use(gsub(OPAK_ZWR.name(1),'???',ref_name(_ref_rozl)+3));
OPAK_ZWR.cntx_psh();
OPAK_ZWR.index('REFTROZL');
OPAK_ZWR.prefix('N',_ref_rozl);
_loop:=OPAK_ZWR.first();
{!
|? _loop
|!
   OPAK_ZWR.cntx_psh();
   OPAK_ZWR.prefix();
   OPAK_ZWR.TMP:='T';
   OPAK_ZWR.add();
   OPAK_ZWR.cntx_pop();
   _loop:=OPAK_ZWR.next()
!};
OPAK_ZWR.cntx_pop();
OPAK_ZWR.cntx_pop()


\opak_zwr_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Usuwa rozliczenie pozycji _a
::   WE: _a - pozycja rozliczająca
::       _b - T/N - tymczasowe
::       [_c] - pozycja rozliczana
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref_rozl:=_a;
_tmp:=_b;
_ref_poz:={? var_pres('_c')=type_of('') || _c || '' ?};
_do_state:=do_state();
{? _do_state=0 || do() ?};
OPAK_ZWR.cntx_psh();
OPAK_ZWR.use(gsub(OPAK_ZWR.name(1),'???',ref_name(_ref_rozl)+3));
OPAK_ZWR.cntx_psh();
{? _tmp=''
||
   OPAK_ZWR.index('REF_ROZL');
   OPAK_ZWR.prefix(_ref_rozl,)
||
   OPAK_ZWR.index('REFTROZL');
   OPAK_ZWR.prefix(_tmp,_ref_rozl,)
?};
_loop:=OPAK_ZWR.first();
{!
|? _loop
|!
   {? _ref_poz='' | OPAK_ZWR.REF_POZ=_ref_poz | OPAK_ZWR.REF_POZK=_ref_poz
   ||
      _opak_zwr_ref_poz:=OPAK_ZWR.REF_POZ;
      _opak_zwr_tmp:=OPAK_ZWR.TMP;
      _loop:=OPAK_ZWR.del(,1)=2;
      {? _opak_zwr_tmp='N' || exec('stanydost_update','opakow_stany',_opak_zwr_ref_poz) ?}
   ||
      _loop:=OPAK_ZWR.next()
   ?}
!};
OPAK_ZWR.cntx_pop();
OPAK_ZWR.cntx_pop();
_result:=do_state()=1;
{? _do_state=0 || end() ?};
_result


\opak_zwr_akc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Akceptacja tymczasowego rozliczenia
::   WE: _a - pozycja rozliczająca
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ref_rozl:=_a;
exec('opak_zwr_del','opakow_stany',_ref_rozl,'N');
OPAK_ZWR.cntx_psh();
OPAK_ZWR.use(gsub(OPAK_ZWR.name(1),'???',ref_name(_ref_rozl)+3));
OPAK_ZWR.cntx_psh();
OPAK_ZWR.index('REFTROZL');
OPAK_ZWR.prefix('T',_ref_rozl);
_loop:=OPAK_ZWR.first();
{!
|? _loop
|!
   OPAK_ZWR.cntx_psh();
   OPAK_ZWR.prefix();
   OPAK_ZWR.TMP:='N';
   OPAK_ZWR.put();
   OPAK_ZWR.cntx_pop();
   _loop:=OPAK_ZWR.first()
!};
OPAK_P.cntx_psh();
OPAK_N.cntx_psh();
OPAK_P.use(ref_name(_ref_rozl));
OPAK_P.cntx_psh();
OPAK_P.prefix();
{? OPAK_P.seek(_ref_rozl)
||
:: pozycja rozliczająca
   OPAK_N.use(ref_name(OPAK_P.OPAK_N));
:: nagłówek pozycji rozliczającej
   OPAK_P.OPAK_N();
   _mag:=OPAK_N.MAG;
   _mat:=OPAK_P.M;
   OPAK_ZWR.index('REFTROZL');
   OPAK_ZWR.prefix('N',_ref_rozl);
:: iteracja po rozliczeniu
   _loop:=OPAK_ZWR.first();
   {!
   |? _loop
   |!
::
:: rozliczenie kaucji powiązane z dostawą
::
      _ref_poz:=OPAK_ZWR.REF_POZ;
      _ref_pozk:={? OPAK_ZWR.REF_POZK='' || OPAK_ZWR.REF_POZ || OPAK_ZWR.REF_POZK ?};
      _opak_sm_update:=0;
      {? OPAK_N.PLUS='N'
      ||
         {? OPAK_P.ZWROT='T'
         ||
:: rozchód - zwrot kaucji kontrahenta
            OPAK_SKD.cntx_psh();
            OPAK_SKD.index('UNIK');
            OPAK_SKD.prefix(OPAK_N.MAG,OPAK_N.KH,OPAK_P.M,_ref_pozk);
            {? OPAK_SKD.first()
            || {? (OPAK_SKD.KH_SK- exec('stanydost_w_wydaniu','opakow_stany',_ref_pozk))<0
               ||
                  undo();
                  FUN.info('Za duża ilość do rozliczenia.'@)
               ?}
            ?};
            OPAK_SKD.cntx_pop()
         ?};
:: rozchód
         OPAK_SD.cntx_psh();
         OPAK_SD.index('DOST');
         OPAK_SD.prefix(_ref_poz);
         {? OPAK_SD.first()
         ||
            OPAK_SD.ILW:=exec('stanydost_w_wydaniu','opakow_stany',_ref_poz);
            OPAK_SD.SD:=OPAK_SD.S-OPAK_SD.ILW;
            {? OPAK_SD.SD<0
            ||
               undo();
               FUN.info('Za duża ilość do wydania.'@)
            ||
               {? OPAK_SD.put() || _opak_sm_update:=1 ?}
            ?}
         ?};
         OPAK_SD.cntx_pop();
         ~~
      ||
:: przychód - zwrot naszych kaucji
         {? OPAK_P.ZWROT='T'
         ||
            OPAK_SKD.cntx_psh();
            OPAK_SKD.index('UNIK');
            OPAK_SKD.prefix(OPAK_N.MAG,OPAK_N.KH,OPAK_P.M,_ref_poz);
            {? OPAK_SKD.first()
            ||
               {? (OPAK_SKD.N_SK- exec('stanydost_w_przyjeciu','opakow_stany',_ref_poz))<0
               ||
                  undo();
                  FUN.info('Za duża ilość do rozliczenia.'@)
               ?}
            ?};
            OPAK_SKD.cntx_pop()
         ||
:: przychód
            ~~
         ?}
      ?};
      {? _opak_sm_update || exec('opak_sm_update','opakow_stany',_mag,_mat) ?};
      _loop:=OPAK_ZWR.next()
   !}
?};
OPAK_P.cntx_pop();
OPAK_N.cntx_pop();
OPAK_P.cntx_pop();
OPAK_ZWR.cntx_pop();
OPAK_ZWR.cntx_pop()


\dok_bk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Dokumenty bez kaucji
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_czy_sel:=0;
_wer:='SEL';
_wer_gr:=OPAK_P.grp_make('Pozycje dokumentów'@,,'opak_dok_bk');
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
_loop:=OKR.last();
{!
|? _loop
|!
   _rok:=$OKR.ROK;
   {? exec('dok_bk_fb','opakow_stany',_rok)
   ||
      _fb:=($'exec(\'dok_bk_fb\',\'opakow_stany\',\'%1\')'[$OKR.ROK+2]);
      _fa:=($'exec(\'dok_bk_fa\',\'opakow_stany\')');
      OPAK_P.grp_sel(_wer_gr,,_wer,$OKR.ROK,,,,,_fb,_fa);
      _czy_sel:=1
   ?};
   exec('dok_bk_fa','opakow_stany');
   _loop:=OKR.prev()
!};
OKR.cntx_pop();

{? _czy_sel=0
||
   FUN.info('Brak dokumentów do wyświetlenia.'@)
||
   exec('cntx_psh','opakow_stany');
   OPAK_P.f_clear();
   OPAK_P.win_sel(_wer_gr);
   OPAK_P.select();
   exec('cntx_pop','opakow_stany')
?}


\dok_bk_fb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Dokumenty bez kaucji - przed wyświetleniem zakładki roku
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('open_tab','opakow_stany',ST.ODDZ,_a+2);
exec('cntx_psh','opakow_stany');
OPAK_P.index('ZWROTO');
OPAK_P.prefix(OPAK_SKH.KH,OPAK_SKH.KH_ODB,OPAK_SKH.M().KTM,'T','N','N');
OPAK_P.first()


\dok_bk_fa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Dokumenty bez kaucji - po wyświetleniu zakładki roku
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('cntx_pop','opakow_stany')


\dokumenty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Dokumenty
::   WE: _a - SM - stany magazynowe / SKH - stany kontrahenta / SK - stany kaucji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
{? _typ='SM' | _typ='SKH' | _typ='SK' || exec('dokumenty_stan','opakow_stany',_typ)
|? _typ='SD' | _typ='SKD' || exec('dokumenty_stand','opakow_stany',_typ)
|| FUN.info('Nieobsługiwany przypadek.'@); return()
?}


\dokumenty_stan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Dokumenty do wybranego stanu typu _a
::   WE: _a - SM - stany magazynowe / SKH - stany kontrahenta / SK - stany kaucji
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
_ident:='s_src';
{? _typ='SM' || _env:=exec('tmp_rebuild','opakow_stany',OPAK_SM.MAG,OPAK_SM.M); _ident+='_sm'
|? _typ='SKH' || _env:=exec('tmp_rebuild','opakow_stany',,OPAK_SKH.M,OPAK_SKH.KH,OPAK_SKH.KH_ODB); _ident+='_skh'
|? _typ='SK' || _env:=exec('tmp_rebuild','opakow_stany',OPAK_SK.MAG,OPAK_SK.M,OPAK_SK.KH,null()); _ident+='_sk'
|| FUN.info('Nieobsługiwany przypadek.'@); return()
?};
_S_SRC:=_env.S_SRC.TAB;
_wer:=_S_SRC.mk_sel('Dokumenty'@,,0,_ident,5,10,15,,'U');
{? _typ='SKH'
||
   _S_SRC.win_fld(_wer,,'MAG',,,10,,1,'Magazyn'@);
   _S_SRC.win_fld(_wer,,'CZY_KAUC',,,10,,1,'Czy kaucja'@)
?};
_S_SRC.win_fld(_wer,,'DATA',,,10,,1,'Data'@);
_S_SRC.win_fld(_wer,,'SYM',,,20,,1,'Symbol'@);
_S_SRC.win_fld(_wer,,'POZ',,,7,,1,'Pozycja'@);
{? _typ='SM'
||
   _S_SRC.win_fld(_wer,,'S',,,10,,1,'Stan w magazynie'@)
||
   _S_SRC.win_fld(_wer,,'N_SK',,,10,,1,'Stan kaucji naszych'@);
   _S_SRC.win_fld(_wer,,'N_WK',,,10,,1,'Wartość'@);
   _S_SRC.win_fld(_wer,,'KH_SK',,,10,,1,'Stan kaucji kontrahenta'@);
   _S_SRC.win_fld(_wer,,'KH_WK',,,10,,1,'Wartość'@);
   {? _typ='SKH'
   ||
      _S_SRC.win_fld(_wer,,'N_SBK',,,10,,1,'Stan bez kaucji u nas'@);
      _S_SRC.win_fld(_wer,,'KH_SBK',,,10,,1,'Stan bez kaucji u kontrahenta'@)
   ?}
?};
_fb:="
   _S_SRC:=cur_tab(1,1);
   _ref:=_S_SRC.POZ_DOK;
   {? ref_tab(_ref)=OPAK_P
   ||
      exec('cntx_psh','opakow_stany');
      exec('open_tab','opakow_stany',ST.ODDZ,(8+_ref)+2);
      exec('FindAndGet','#table',OPAK_P,_ref,,\"exec('init_var','opakow'); OPAK_P.display()\");
      exec('cntx_pop','opakow_stany')

   |? ref_tab(_ref)=FAP
   ||
      exec('fak_psh','open_tab');
      exec('fak_open','open_tab',ST.ODDZ,(8+_ref)+2);
      exec('FindAndGet','#table',FAP,_ref,,\"exec('disp_fap','faktury_poz')\");
      exec('fak_pop','open_tab')
   ?}
";
{? _typ='SM' || _S_SRC.index(_env.S_SRC.NDX_SM); _S_SRC.prefix('T',OPAK_SM.MAG,OPAK_SM.M().KTM,)
|? _typ='SKH' ||  _S_SRC.index(_env.S_SRC.NDX_SKH); _S_SRC.prefix(OPAK_SKH.KH().SKR,OPAK_SKH.KH_ODB().SKR,OPAK_SKH.M().KTM,)
|? _typ='SK' ||  _S_SRC.index(_env.S_SRC.NDX_SK); _S_SRC.prefix('T',OPAK_SK.KH().SKR,OPAK_SK.M().KTM,OPAK_SK.MAG,)
?};
_S_SRC.win_act(_wer,,'Wyświetl',,,,_fb);
_S_SRC.win_sel(_wer);
_S_SRC.select()


\dokumenty_stand
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Dokumenty do wybranego stanu typu _a
::   WE: _a - SD - stany dostaw / SKD - stany kaucji wg dokumentów
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;

_env:=obj_new('S_SRC');
_env.S_SRC:=obj_new('TAB','NDX_SM','NDX_SKH','NDX_SK');
exec('tmp_s_src','opakow_stany',_env.S_SRC);
_S_SRC:=_env.S_SRC.TAB;
params_set('env',_env);

_ident:='s_src';
{? _typ='SD'
||
   exec('cntx_psh','opakow_stany');
   exec('open_tab','opakow_stany',ST.ODDZ,(8+OPAK_SD.OPAK_P)+2);
   exec('FindAndGet','#table',OPAK_P,OPAK_SD.OPAK_P,,"
      OPAK_P.OPAK_N();
      exec('tmp_s_src_add','opakow_stany',OPAK_P.ref(),0,0,0,0,0,0,,'T',OPAK_P.IL)
   ");
   OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   _loop:=OKR.first();
   {!
   |? _loop
   |!
      exec('open_tab','opakow_stany',ST.ODDZ,$OKR.ROK+2);
      OPAK_ZWR.index('REFTPOZ');
      OPAK_ZWR.prefix('N',OPAK_SD.OPAK_P,);
      _loop:=OPAK_ZWR.first();
      {!
      |? _loop
      |!
         OPAK_P.prefix();
         {? OPAK_P.seek(OPAK_ZWR.REF_ROZL)
         ||
            OPAK_P.OPAK_N();
            exec('tmp_s_src_add','opakow_stany',OPAK_ZWR.REF_ROZL,0,0,0,0,0,0,,'T',-OPAK_ZWR.IL)
         ?};
         _loop:=OPAK_ZWR.next()
      !};
      _loop:=OKR.next()
   !};
   OKR.cntx_pop();
   exec('cntx_pop','opakow_stany');
   _ident+='_sd'

|? _typ='SKD'
||
   exec('fak_psh','open_tab');
   exec('cntx_psh','opakow_stany');
   exec('open_tab','opakow_stany',ST.ODDZ,(8+OPAK_SKD.POZ_DOK)+2);
   _params:=obj_new('MAG','KAUCJA');
   _params.MAG:='';
   _params.KAUCJA:=0;
   exec('FindAndGet','#table',OPAK_P,OPAK_SKD.POZ_DOK,,"
      _params:=_b;
      @.OPAK_P.OPAK_N();
      _n_sk:=_n_wk:=_kh_sk:=_kh_wk:=0;
      {? @.OPAK_N.PLUS='N' || _n_sk:=@.OPAK_P.IL; _n_wk:=@.OPAK_P.WAR || _kh_sk:=@.OPAK_P.IL; _kh_wk:=@.OPAK_P.WAR ?};
      exec('tmp_s_src_add','opakow_stany',@.OPAK_P.ref(),_n_sk,_n_wk,_kh_sk,_kh_wk,0,0,,'T',@.OPAK_P.IL);
      _params.MAG:=@.OPAK_N.MAG;
      _params.KAUCJA:=@.OPAK_P.KAUCJA
   ",,_params);
   _mag:=_params.MAG;
   _kaucja:=_params.KAUCJA;
   OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   _loop:=OKR.first();
   {!
   |? _loop
   |!
      exec('open_tab','opakow_stany',ST.ODDZ,$OKR.ROK+2);
      exec('fak_open','open_tab',ST.ODDZ,$OKR.ROK+2);
      OPAK_ZWR.index('REFTPOZ');
      OPAK_ZWR.prefix('N',OPAK_SKD.POZ_DOK,);
      _loop:=OPAK_ZWR.first();
      {!
      |? _loop
      |!
         {? ref_tab(OPAK_ZWR.REF_ROZL)=OPAK_P
         ||
            OPAK_P.prefix();
            {? OPAK_P.seek(OPAK_ZWR.REF_ROZL) & OPAK_P.ZWROT='T'
            ||
               {? OPAK_P.OPAK_N().AKC='T'
               ||
                  _n_sk:=_n_wk:=0;
                  _kh_sk:=_kh_wk:=0;
                  {? OPAK_N.PLUS='N' || _n_sk:=-OPAK_P.IL; _n_wk:=-OPAK_P.WAR || _kh_sk:=-OPAK_P.IL; _kh_wk:=-OPAK_P.WAR ?};
                  exec('tmp_s_src_add','opakow_stany',OPAK_P.ref(),_n_sk,_n_wk,_kh_sk,_kh_wk,0,0,,'N',0)
               ?}
            ?}

         |? ref_tab(OPAK_ZWR.REF_ROZL)=FAP
         ||
            FAP.prefix();
            {? FAP.seek(OPAK_ZWR.REF_ROZL)
            ||
               {? FAP.FAKS().AKC='T'
               ||
                  _n_sk:=-OPAK_ZWR.IL; _n_wk:=(_kaucja*_n_sk)$2;
                  _kh_sk:=_kh_wk:=0;
                  exec('tmp_s_src_add','opakow_stany',FAP.ref(),_n_sk,_n_wk,_kh_sk,_kh_wk,0,0,_mag,'N',0)
               ?}
            ?}
         ?};
         _loop:=OPAK_ZWR.next()
      !};
      _loop:=OKR.next()
   !};
   OKR.cntx_pop();
   exec('cntx_pop','opakow_stany');
   exec('fak_pop','open_tab');
   _ident+='_skd'
||
   FUN.info('Nieobsługiwany przypadek.'@); return()
?};

_wer:=_S_SRC.mk_sel('Dokumenty'@,,0,_ident,5,10,15,,'U');
{? _typ='SKH'
||
   _S_SRC.win_fld(_wer,,'MAG',,,10,,1,'Magazyn'@)
?};
_S_SRC.win_fld(_wer,,'DATA',,,10,,1,'Data'@);
_S_SRC.win_fld(_wer,,'SYM',,,20,,1,'Symbol'@);
_S_SRC.win_fld(_wer,,'POZ',,,7,,1,'Pozycja'@);
{? _typ='SD'
||
   _S_SRC.win_fld(_wer,,'S',,,10,,1,'Stan w magazynie'@)
||
   _S_SRC.win_fld(_wer,,'N_SK',,,10,,1,'Stan kaucji naszych'@);
   _S_SRC.win_fld(_wer,,'N_WK',,,10,,1,'Wartość'@);
   _S_SRC.win_fld(_wer,,'KH_SK',,,10,,1,'Stan kaucji kontrahenta'@);
   _S_SRC.win_fld(_wer,,'KH_WK',,,10,,1,'Wartość'@)
?};
_fb:="
   _S_SRC:=cur_tab(1,1);
   _ref:=_S_SRC.POZ_DOK;
   {? ref_tab(_ref)=OPAK_P
   ||
      exec('cntx_psh','opakow_stany');
      exec('open_tab','opakow_stany',ST.ODDZ,(8+_ref)+2);
      exec('FindAndGet','#table',OPAK_P,_ref,,\"exec('init_var','opakow'); OPAK_P.display()\");
      exec('cntx_pop','opakow_stany')

   |? ref_tab(_ref)=FAP
   ||
      exec('fak_psh','open_tab');
      exec('fak_open','open_tab',ST.ODDZ,(8+_ref)+2);
      exec('FindAndGet','#table',FAP,_ref,,\"exec('disp_fap','faktury_poz')\");
      exec('fak_pop','open_tab')
   ?}
";
{? _typ='SD' || _S_SRC.index(_env.S_SRC.NDX_SM); _S_SRC.prefix('T',OPAK_SD.MAG,OPAK_SD.M().KTM,)
|? _typ='SKD' ||  _S_SRC.index(_env.S_SRC.NDX_SK); _S_SRC.prefix('T',OPAK_SKD.KH().SKR,OPAK_SKD.M().KTM,OPAK_SKD.MAG,)
?};
_S_SRC.win_act(_wer,,'Wyświetl',,,,_fb);
_S_SRC.win_sel(_wer);
_S_SRC.select()

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 d72ffe2dade474638a2acb40998ee886d911f74eac66e7dbfc8f805e6157e86dd04150deaa72f73038b7d895d8cdf50606ce95084cd4e26e94e443df55736e49415af71090bb0699201b5449d396234cdefa85f4f3aef403b2b4d67d4a04a94159b253e9eadaf9e791426857a54b30a11ddf956ae45891fc344ec93a52cba47f

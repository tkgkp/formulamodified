:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ceny_uprawn.fml [2015]
:: Utworzony: 14.04.2015
:: Autor: Mario
::======================================================================================================================
:: Zawartość: Formuły do obsługi uprawnień cenników
::======================================================================================================================


\tar_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: nadawanie uprawnień do cenników
::   WE: _a - rodzaj cennika S - sprzedaż D - dostawców
::----------------------------------------------------------------------------------------------------------------------
__TarSD:=_a;
_TAB:=_b;
_wergr:=_c;

_cspr:=__TarSD<>'D';

_fa:="
   {? TARGP.KH_ODB
   || TARGP.KH:=TARGP.KH_ODB().KH
   ?};
   {? TARGP.KH
   || TARGP.GRKH:=TARGP.KH().GRKH
   ?};
   win_disp();
   1
";
TARGP.fld_fml('KH_ODB','AFTER_EDIT',_fa);

_fa:="
   {? TARGP.KH
   || TARGP.GRKH:=TARGP.KH().GRKH;
      {? TARGP.KH_ODB & TARGP.KH_ODB().KH<>TARGP.KH
      || TARGP.KH_ODB:=null()
      ?}
   ?};
   win_disp();
   1
";
TARGP.fld_fml('KH','AFTER_EDIT',_fa);

_fa:="
   {? TARGP.GRKH
   || {? TARGP.KH & TAR.KH().GRKH<>TARGP.GRKH
      || TARGP.KH:=null()
      ?};
      {? TARGP.KH_ODB & TARGP.KH_ODB().KH().GRKH<>TARGP.GRKH
      || TARGP.KH_ODB:=null()
      ?}
   ?};
   win_disp();
   1
";
TARGP.fld_fml('GRKH','AFTER_EDIT',_fa);

_fa:="
   {? TARGP.M
   || TARGP.MGRP:=TARGP.M().MGRP;
      TARGP.MGR:=TARGP.M().MGR
   ?};
   {? TARGP.MGRP
   || TARGP.MGR:=TARGP.MGRP().MGR
   ?};
   win_disp();
   1
";
TARGP.fld_fml('M','AFTER_EDIT',_fa);

_fa:="
   {? TARGP.MGRP
   || TARGP.MGR:=TARGP.MGRP().MGR;
      {? TARGP.M & TARGP.M().MGRP<>TARGP.MGRP
      || TARGP.M:=null()
      ?}
   ?};
   win_disp();
   1
";
TARGP.fld_fml('MGRP','AFTER_EDIT',_fa);

_fa:="
   {? TARGP.MGR
   || {? TARGP.MGRP & TARGP.MGRP().MGR<>TARGP.MGR
      || TARGP.MGRP:=null()
      ?};
      {? TARGP.M & TARGP.M().MGR<>TARGP.MGR
      || TARGP.M:=null()
      ?}
   ?};
   win_disp();
   1
";
TARGP.fld_fml('MGR','AFTER_EDIT',_fa);


_wer1:=TARGN.mk_sel({? _a='S' || 'Grupy cenników sprzedaży'@ || 'Grupy cenników dostawców'@ ?}
         ,,,'cckdwpqdlfzzawf',1,,,,'U');
TARGN.win_fld(_wer1,,'ODDZ','KOD',,2,,,'Oddział'@);
TARGN.win_fld(_wer1,,'KOD',,,20,,,'Kod'@);
TARGN.win_fld(_wer1,,'OP',,,67,,,'Opis'@);
{? _a='S'
|| _fa:="exec('usr_upr_uzupełnij_pelne','users_upraw','TARS')"
|| _fa:="exec('usr_upr_uzupełnij_pelne','users_upraw','TARD')"
?};
TARGN.win_act(_wer1,1,'Dołącz',,,,,_fa,1);
TARGN.win_act(_wer1,,'Dołącz',,,,,_fa,1);
_fb:="{? TARGN.r_lock(1,1,1) || 1 || FUN.info('Nagłówek obsługuje inny użytkownik.'@); 0 ?}";
TARGN.win_act(_wer1,,'Popraw',,,,_fb);
_fb:="exec('targn_usun','ceny_uprawn')";
TARGN.win_act(_wer1,,'Formuła','&Usuń'@@,,,_fb);
TARGN.win_act(_wer1,,'Szukaj');
TARGN.win_act(_wer1,,'Kolejność');
TARGN.win_act(_wer1,1,'Rekord',,,,,"__CHK.record(TARGN,,'KOD')");
TARGN.win_act(_wer1,,'Rekord',,,,"{? _a || TARGN.r_unlock() ?}","__CHK.record(TARGN,,'KOD')");

_wer2:=TARGP.mk_sel('Pozycje grupy'@,'P',,'lljkejcnndqpjec',1,,,,'U');
TARGP.win_sel(_wer2);
TARGP.win_fld(_wer2,,'GRKH','KOD','KOD',20,,,'Grupa kontrahenta'@);
TARGP.win_fld(_wer2,,'KH','SKR','SKR',12,,,'Kontrahent'@);
{? _cspr || TARGP.win_fld(_wer2,,'KH_ODB','NAZ','KOD',10,,,'Odbiorca'@) ?};
TARGP.win_fld(_wer2,,'MGR','KOD','MGR',15,,,'Grupa indeksu'@);
TARGP.win_fld(_wer2,,'MGRP','KOD','KOD',{? _cspr || 8 || 27 ?},,,'Podgrupa indeksu'@);
TARGP.win_fld(_wer2,,'M','KTM','MATKTM',50,,,'Indeks'@);
_fb:="exec('targp_edit','ceny_uprawn','d')";
TARGP.win_act(_wer2,1,'Formuła','&Dołącz'@@,,,_fb,,1,,,,'D');
TARGP.win_act(_wer2,,'Formuła','&Dołącz'@@,,,_fb,,1,,,,'D');
_fb:="{? TARGP.r_lock(1,1,1) || 1 || FUN.info('Pozycję obsługuje inny użytkownik.'@); 0 ?}";
TARGP.win_act(_wer2,,'Popraw',,,,_fb);
TARGP.win_act(_wer2,,'Usuń',,,,_fb,,,1);
TARGP.win_act(_wer2,,'Szukaj');
TARGP.win_act(_wer2,,'Kolejność');
_fa:="
   TARGP.TARGN:=TARGN.ref;
   {? TARGP.KH_ODB || TARGP.KH:=TARGP.KH_ODB().KH ?};
   {? TARGP.KH || TARGP.GRKH:=TARGP.KH().GRKH ?};
   {? TARGP.M || TARGP.MGRP:=TARGP.M().MGRP; TARGP.MGR:=TARGP.M().MGR ?};
   {? TARGP.MGRP || TARGP.MGR:=TARGP.MGRP().MGR ?};
   1
";
TARGP.win_act(_wer2,1,'Rekord',,,,,_fa);
TARGP.win_act(_wer2,,'Rekord',,,,"{? _a || TARGP.r_unlock() ?}",_fa);

_wer3:=TARUP.mk_sel('Uprawnieni użytkownicy'@,'P',,'hhggqdvbjlpedjs',1,,,,'U');
TARUP.win_sel(_wer3);
TARUP.win_fld(_wer3,,'USER','DANE','USR_KNAZ',25,,,'Nazwa'@);
TARUP.win_act(_wer3,,'Szukaj');
TARUP.win_act(_wer3,,'Kolejność');
_far:="
   {? TARGN.size()
   || TARGP.actions_grayed(TARGP.win_sel('?'),'');
      TARUP.actions_grayed(TARUP.win_sel('?'),'')
   || TARGP.actions_grayed(TARGP.win_sel('?'),'D:D');
      TARUP.actions_grayed(TARUP.win_sel('?'),'D:D')
   ?};
   grp_disp(TARGP,TARGP.win_sel('?'),,1);
   grp_disp(TARUP,TARUP.win_sel('?'),,1)
";
_TAB.grp_sel(_wergr,TARGN,_wer1,'Grupy uprawnień',_far,,,15,,,,,'maximized_with_title');
_TAB.tab_splt(_wergr,,'vertical','panel21');
_far:="";
_fb:="
   TARUP.index('TARGN'); TARUP.prefix({? TARGN.get || TARGN.ref || null ?})
";
_TAB.grp_sel(_wergr,TARUP,_wer3,,_far,,,15,_fb,,,,'maximized_with_title');
_TAB.tab_splt(_wergr,,'horizontal','panel22');
_far:="";
_fb:="
   TARGP.index('TARGN'); TARGP.prefix({? TARGN.get || TARGN.ref || null ?})
";
_TAB.grp_sel(_wergr,TARGP,_wer2,,_far,,,15,_fb,,,,'maximized_with_title');
{? TAZ.SD='S' || task_attach('LSP_CEN_LCSP') || task_attach('LZK_CEN_LCMG') ?};

TARGN.index('S');
{? _a='S'
|| TARGN.prefix('S')
|| TARGN.prefix('D')
?};
exec('slo_m_ok','material','T',,,'W','W');
exec('ustaw_okna','kontrahent')


\targn_oddz_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: przed redakcja pola TARGN.ODDZ
::----------------------------------------------------------------------------------------------------------------------

_wyn:=1;
{? 1+(-menu_txt())<>'d'
|| TARGN.cntx_psh();
   _wyn:=TARGN.count()=0;
   TARGN.cntx_pop()
?};
_wyn


\targn_oddz_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: F3 dla pola TARGN.ODDZ
::----------------------------------------------------------------------------------------------------------------------
{? exec('sel_upr','users','ODDZ','KOD')
|| TARGN.ODDZ:=ODDZ.KOD;
   {? exec('targn_oddz_ae','ceny_uprawn')
   || TARGN.ODDZ
   ?}
?}


\targn_oddz_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.30]
:: OPIS: po redakcji pola TARGN.ODDZ
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? TARGN.ODDZ<>''
||
   _oddz:=exec('FindInSet','#table','ODDZ','KOD',TARGN.ODDZ);
   {? _oddz
   || _wyn:=1
   || FUN.info('Niezdefiniowany oddział.'@)
   ?}
||
   FUN.info('Niewypełnione pole.'@)
?};
_wyn


\targp_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: edycja pozycji grupy uprawnień cenników
::   WE: _a-='d'-dolacz, ='p'-popraw, 'u'-usun
::----------------------------------------------------------------------------------------------------------------------

VAR_DEL.delete('__lpan','__rpan','__Ltab','__Rtab','__Upr','__uprndx','__Uprl','__uprill','__Uprr','__uprilr','__Lsel'
   ,'__Rsel');

TAZ.win_edit('RED_UPR'+{? __TarSD='D' || 'D' || '' ?});

TAZ.UPR_K:='B';
TAZ.UPR_M:='B';

_gwer:=_lwer:=_rwer:='';

{? TAZ.edit
||

   __lpan:=TAZ.UPR_K;
   __rpan:=TAZ.UPR_M;

   {? __lpan='' & __rpan=''
   ||
      FUN.info('Nie dodano uprawnień.\nW oknie parametrów należy zaznaczyć sposób dodawania.'@);
      return
   |? TARGN.SD='D' & (__lpan='O' | __rpan='O')
   ||
      FUN.info('Definiowanie uprawnień do cenników odbiorców jest nieostępne dla cenników dostaw.'@);
      return
   |? __lpan='B' & __rpan='B'
   ||
      TARGP.cntx_psh;
      TARGP.index('UPR');
      TARGP.prefix(TARGN.ODDZ,TARGN.SD,null,null,null,null,null,null,TARGN.ref);
      {? ~TARGP.first
      ||
         TARGP.blank;
         TARGP.TARGN:=TARGN.ref;
         TARGP.add
      ?};
      TARGP.cntx_pop;
      return
   ?};

   _sql:=
      "select "
      +
         {? __lpan='O' || "TARGP.KH_ODB LPAN"
         |? __lpan='K' || "TARGP.KH LPAN"
         |? __lpan='G' || "TARGP.GRKH LPAN"
         || ""
         ?}+
         {? __lpan='B' | __rpan='B' || "" || ", " ?}+
         {? __rpan='M' || "TARGP.M RPAN"
         |? __rpan='P' || "TARGP.MGRP RPAN"
         |? __rpan='G' || "TARGP.MGR RPAN"
         || ""
         ?}
      +
      " from TARGP "
      +
      " where "
      +
         "TARGP.TARGN = :_a and "+
         {? __lpan='O' || "TARGP.GRKH is not null and TARGP.KH is not null and TARGP.KH_ODB is not null"
         |? __lpan='K' || "TARGP.GRKH is not null and TARGP.KH is not null and TARGP.KH_ODB is null"
         |? __lpan='G' || "TARGP.GRKH is not null and TARGP.KH_ODB is null and TARGP.KH is null"
         || "TARGP.KH_ODB is null and TARGP.KH is null and TARGP.GRKH is null"
         ?}+
         " and "+
         {? __rpan='M' || "TARGP.MGR is not null and TARGP.M is not null"
         |? __rpan='P' || "TARGP.MGR is not null and TARGP.MGRP is not null and TARGP.M is null"
         |? __rpan='G' || "TARGP.MGR is not null and TARGP.MGRP is null and TARGP.M is null"
         || "TARGP.M is null and TARGP.MGRP is null and TARGP.MGR is null"
         ?}
      +
      " order by 1";
   __Upr:=sql(_sql,TARGN.ref());
   __uprill:=0;
   {? __lpan<>'B' & __rpan<>'B'
   ||
      __uprill:=sql($"select count(distinct :_a.LPAN) IL from :_a",__Upr).IL;
      __Uprr:=sql($"select :_a.RPAN, count(:_a.RPAN) ILR from :_a group by :_a.RPAN order by 1 ",__Upr);
      __uprilr:=sql($"select count(distinct :_a.RPAN) IL from :_a",__Upr).IL;
      __Uprl:=sql($"select :_a.LPAN, count(:_a.LPAN) ILL from :_a group by :_a.LPAN order by 1 ",__Upr)
   ?};

   _fuprl:=
      {? __lpan='B' | __rpan='B'
      || "{? __Upr.find_key($cur_tab(1,1).ref) || 'xwin16.png:179' || 'xwin16.png:180' ?}"
      || "
         _wyn:=
            {? __Uprl.find_key($cur_tab(1,1).ref)
            || {? __Uprl.ILL=__uprilr || 'xwin16.png:179' || 'xwin16.png:178' ?}
            || 'xwin16.png:180'
            ?};
         _wyn
         "
      ?};
   _fuprr:=
      {? __lpan='B' | __rpan='B'
      || "{? __Upr.find_key($cur_tab(1,1).ref) || 'xwin16.png:179' || 'xwin16.png:180' ?}"
      || "
         _wyn:=
            {? __Uprr.find_key($cur_tab(1,1).ref)
            || {? __Uprr.ILR=__uprill || 'xwin16.png:179' || 'xwin16.png:178' ?}
            || 'xwin16.png:180'
            ?};
         _wyn
         "
      ?};

   __Ltab:=__Rtab:=null;
   _lwer:=_rwer:='';

   {? __lpan='O'
   || __Ltab:=KH_ODB;
      _lwer:=KH_ODB.mk_sel('Odbiorcy'@,'P',,'zxkksllcvmmalen',,,,,'U');
      KH_ODB.win_fld(_lwer,,'KH','KOD',,8,,,'Kod kontrahenta'@);
      KH_ODB.win_fld(_lwer,,'KH','SKR',,10,,,'Skrót kontrahenta'@);
      KH_ODB.win_fld(_lwer,,'KOD',,,10,,,'Kod odbiorcy'@);
      KH_ODB.win_fld(_lwer,,'NAZ',,,30,,,'Nazwa odbiorcy'@);
      _fb:="exec('targp_add','ceny_uprawn')";
      KH_ODB.win_act(_lwer,,'Formuła','&Akceptuj'@@,,,_fb,,1,1,_fb,,'A');
      KH_ODB.win_act(_lwer,,'Kolejność');
      KH_ODB.win_act(_lwer,,'Szukaj');
      KH_ODB.win_fml(_lwer,,'KOD',,'ICON_BEFORE',_fuprl);
      KH_ODB.index('KH_ODB'); KH_ODB.prefix; KH_ODB.first
   |? __lpan='K'
   || __Ltab:=KH;
      _lwer:=KH.mk_sel('Kontrahenci'@,'P',,'zxkksllcvmmaleb',,,,,'U');
      KH.win_fld(_lwer,,'KOD',,,8,,,'Kod'@);
      KH.win_fld(_lwer,,'SKR',,,10,,,'Skrót'@);
      KH.win_fld(_lwer,,'NAZ',,,30,,,'Nazwa'@);
      _fb:="exec('targp_add','ceny_uprawn')";
      KH.win_act(_lwer,,'Formuła','&Akceptuj'@@,,,_fb,,1,1,_fb,,'A');
      KH.win_act(_lwer,,'Kolejność');
      KH.win_act(_lwer,,'Szukaj');
      KH.win_fml(_lwer,,'KOD',,'ICON_BEFORE',_fuprl);
      KH.index('KOD'); KH.prefix(2); KH.first
   |? __lpan='G'
   || __Ltab:=GRKH;
      _lwer:=GRKH.mk_sel('Grupy kontrahentów'@,'P',,'zxkksllcvmmalev',,,,,'U');
      GRKH.win_fld(_lwer,,'KOD',,,20,,,'Kod'@);
      GRKH.win_fld(_lwer,,'NAZ',,,30,,,'Nazwa'@);
      _fb:="exec('targp_add','ceny_uprawn')";
      GRKH.win_act(_lwer,,'Formuła','&Akceptuj'@@,,,_fb,,1,1,_fb,,'A');
      GRKH.win_act(_lwer,,'Kolejność');
      GRKH.win_act(_lwer,,'Szukaj');
      GRKH.win_fml(_lwer,,'KOD',,'ICON_BEFORE',_fuprl);
      GRKH.index('KOD'); GRKH.prefix(); GRKH.first
   ?};

   {? __rpan='M'
   || __Rtab:=M;
      _rwer:=M.mk_sel('Indeks'@,'P',,'zxkksllcvmmalec',,,,,'U');
      M.win_fld(_rwer,,'RODZ',,,5,,,'Rodzaj'@);
      M.win_fld(_rwer,,'KTM',,,30,,,'Kod'@);
      M.win_fld(_rwer,,'N',,,30,,,'Nazwa'@);
      _fb:="exec('targp_add','ceny_uprawn')";
      M.win_act(_rwer,,'Formuła','&Akceptuj'@@,,,_fb,,1,1,_fb,,'A');
      M.win_act(_rwer,,'Kolejność');
      M.win_act(_rwer,,'Szukaj');
      M.win_fml(_rwer,,'RODZ',,'ICON_BEFORE',{? __lpan='B' | __rpan='B' || _fuprl || _fuprr ?});
      M.index('MATKTM'); M.prefix; M.first
   |? __rpan='P'
   || __Rtab:=MGRP;
      _rwer:=MGRP.mk_sel('Podgrupy indeksów'@,'P',,'zxkksllcvmmalex',,,,,'U');
      MGRP.win_fld(_rwer,,'MGR','RODZ',,5,,,'Rodzaj'@);
      MGRP.win_fld(_rwer,,'MGR','KOD',,8,,,'Grupa kod'@);
      MGRP.win_fld(_rwer,,'MGR','NAZ',,20,,,'Grupa nazwa'@);
      MGRP.win_fld(_rwer,,'KOD',,,8,,,'Podgrupa kod'@);
      MGRP.win_fld(_rwer,,'NAZ',,,20,,,'Podgrupa kod'@);
      _fb:="exec('targp_add','ceny_uprawn')";
      MGRP.win_act(_rwer,,'Formuła','&Akceptuj'@@,,,_fb,,1,1,_fb,,'A');
      MGRP.win_act(_rwer,,'Kolejność');
      MGRP.win_act(_rwer,,'Szukaj');
      MGRP.win_fml(_rwer,,'KOD',,'ICON_BEFORE',{? __lpan='B' | __rpan='B' || _fuprl || _fuprr ?});
      MGRP.index('MGR'); MGRP.prefix; MGRP.first
   |? __rpan='G'
   || __Rtab:=MGR;
      _rwer:=MGR.mk_sel('Grupy indeksów'@,'P',,'zxkksllcvmmalez',,,,,'U');
      MGR.win_fld(_rwer,,'RODZ',,,5,,,'Rodzaj'@);
      MGR.win_fld(_rwer,,'KOD',,,8,,,'Kod'@);
      MGR.win_fld(_rwer,,'NAZ',,,20,,,'Nazwa'@);
      _fb:="exec('targp_add','ceny_uprawn')";
      MGR.win_act(_rwer,,'Formuła','&Akceptuj'@@,,,_fb,,1,1,_fb,,'A');
      MGR.win_act(_rwer,,'Kolejność');
      MGR.win_act(_rwer,,'Szukaj');
      MGR.win_fml(_rwer,,'RODZ',,'ICON_BEFORE',{? __lpan='B' | __rpan='B' || _fuprl || _fuprr ?});
      MGR.index('KOD'); MGR.prefix; MGR.first
   ?};

   {? __lpan='B'
   || _Tab:=__Rtab;
      _Tab.win_sel(_rwer)
   |? __rpan='B'
   || _Tab:=__Ltab;
      _Tab.win_sel(_lwer)
   || _Tab:=__Ltab;
      __Rtab.win_sel(_rwer);
      _gwer:=_Tab.grp_make('Dołącz'@,,'zxkksllcvmmalef');
      _fb:="grp_disp(__Rtab,__Rtab.win_sel('?'))";
      _fa:="
         VAR_DEL.delete('__Lsel');
         __Lsel:=__Ltab.sel_aget;
         {? ~__Lsel.first || __Lsel.REF:=#__Ltab.ref; __Lsel.add ?}
      ";
      _Tab.grp_sel(_gwer,,_lwer,,,,,,_fb,_fa,,,'maximized_with_title');
      _Tab.grp_splt(_gwer,,'vertical','panel1');
      _fa:="
         VAR_DEL.delete('__Rsel');
         __Rsel:=__Rtab.sel_aget;
         {? ~__Rsel.first || __Rsel.REF:=#__Rtab.ref; __Rsel.add ?}
      ";
      _Tab.grp_sel(_gwer,__Rtab,_rwer,,,,,,,_fa,,,'maximized_with_title');
      _Tab.win_sel(_gwer)
   ?};
   _Tab.select
?};

:: sprzatanie
{? _gwer<>'' || _Tab.win_sel; _Tab.win_del(_gwer) ?};
{? _lwer<>'' || __Ltab.win_sel; __Ltab.win_del(_lwer) ?};
{? _rwer<>'' || __Rtab.win_sel; __Rtab.win_del(_rwer) ?};
VAR_DEL.delete('__lpan','__rpan','__Ltab','__Rtab','__Upr','__uprndx','__Uprl','__uprill','__Uprr','__uprilr','__Lsel'
   ,'__Rsel')


\targp_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: dodawanie pozycji uprawnien
::----------------------------------------------------------------------------------------------------------------------

{? __lpan='B'
||
   _Rsel:=__Rtab.sel_aget;
   {? ~_Rsel.first || _Rsel.REF:=#__Rtab.ref; _Rsel.add ?};

   {? _Rsel.add
   ||
      __Rtab.cntx_psh;
      __Rtab.prefix;
      TARGP.cntx_psh;
      TARGP.index('UPR');
      {!
      |?
         {? __Rtab.seek(_Rsel.REF,)
         ||
            _mgr:=_mgrp:=_mat:=null;
            {? __rpan='M'
            || _mgr:=M.MGR; _mgrp:=M.MGRP; _mat:=M.ref
            |? __rpan='P'
            || _mgr:=MGRP.MGR; _mgrp:=MGRP.ref
            |? __rpan='G'
            || _mgr:=MGR.ref
            ?};
            TARGP.prefix(TARGN.ODDZ,TARGN.SD,null,null,null,_mgr,_mgrp,_mat,TARGN.ref);
            {? ~TARGP.first
            ||
               TARGP.blank;
               TARGP.TARGN:=TARGN.ref;
               TARGP.MGR:=_mgr;
               TARGP.MGRP:=_mgrp;
               TARGP.M:=_mat;
               TARGP.add
            ?}
         ?};
         _Rsel.next
      !};
      TARGP.cntx_pop;
      __Rtab.cntx_pop
   ?}
|? __rpan='B'
||
   _Lsel:=__Ltab.sel_aget;
   {? ~_Lsel.first || _Lsel.REF:=#__Ltab.ref; _Lsel.add ?};

   {? _Lsel.first
   ||
      __Ltab.cntx_psh;
      __Ltab.prefix;
      TARGP.cntx_psh;
      TARGP.index('UPR');
      {!
      |?
         {? __Ltab.seek(_Lsel.REF,)
         ||
            _grkh:=_odb:=_kh:=null;
            {? __lpan='O'
            || _grkh:=KH_ODB.KH().GRKH; _kh:=KH_ODB.KH; _odb:=KH_ODB.ref
            |? __lpan='K'
            || _grkh:=KH.GRKH; _kh:=KH.ref
            |? __lpan='G'
            || _grkh:=GRKH.ref
            ?};
            TARGP.prefix(TARGN.ODDZ,TARGN.SD,_grkh,_kh,_odb,null,null,null,TARGN.ref);
            {? ~TARGP.first
            ||
               TARGP.blank;
               TARGP.TARGN:=TARGN.ref;
               TARGP.GRKH:=_grkh;
               TARGP.KH:=_kh;
               TARGP.KH_ODB:=_odb;
               TARGP.add
            ?}
         ?};
         _Lsel.next
      !};
      TARGP.cntx_pop;
      __Ltab.cntx_pop
   ?}
||
   {? cur_tab(1,0)=__Ltab
   ||
      VAR_DEL.delete('__Lsel');
      __Lsel:=__Ltab.sel_aget;
      {? ~__Lsel.first || __Lsel.REF:=#__Ltab.ref; __Lsel.add ?}
   ||
      VAR_DEL.delete('__Rsel');
      __Rsel:=__Rtab.sel_aget;
      {? ~__Rsel.first || __Rsel.REF:=#__Rtab.ref; __Rsel.add ?}
   ?};

   {? __Lsel.first
   ||
      __Ltab.cntx_psh;
      __Ltab.prefix;
      TARGP.cntx_psh;
      TARGP.index('UPR');
      {!
      |?
         {? __Ltab.seek(__Lsel.REF,)
         ||
            _grkh:=_odb:=_kh:=null;
            {? __lpan='O'
            || _grkh:=KH_ODB.KH().GRKH; _kh:=KH_ODB.KH; _odb:=KH_ODB.ref
            |? __lpan='K'
            || _grkh:=KH.GRKH; _kh:=KH.ref
            |? __lpan='G'
            || _grkh:=GRKH.ref
            ?};
            {? __Rsel.first
            ||
               {!
               |?
                  {? __Rtab.seek(__Rsel.REF,)
                  ||
                     _mgr:=_mgrp:=_mat:=null;
                     {? __rpan='M'
                     || _mgr:=M.MGR; _mgrp:=M.MGRP; _mat:=M.ref
                     |? __rpan='P'
                     || _mgr:=MGRP.MGR; _mgrp:=MGRP.ref
                     |? __rpan='G'
                     || _mgr:=MGR.ref
                     ?};
                     TARGP.prefix(TARGN.ODDZ,TARGN.SD,_grkh,_kh,_odb,_mgr,_mgrp,_mat,TARGN.ref);
                     {? ~TARGP.first
                     ||
                        TARGP.blank;
                        TARGP.TARGN:=TARGN.ref;
                        TARGP.GRKH:=_grkh;
                        TARGP.KH:=_kh;
                        TARGP.KH_ODB:=_odb;
                        TARGP.MGR:=_mgr;
                        TARGP.MGRP:=_mgrp;
                        TARGP.M:=_mat;
                        TARGP.add
                     ?}
                  ?};
                  __Rsel.next
               !}
            ?}
         ?};
         __Lsel.next
      !};
      TARGP.cntx_pop;
      __Ltab.cntx_pop
   ?}
?};
sel_exit


\bl_targn_sd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [17.00]
:: OPIS: wartość domyślna cennika sprzedaży/dostaw
::----------------------------------------------------------------------------------------------------------------------

{? var_pres('__TarSD')=2
|| __TarSD
|| 'S'
?}


\ae_taz_parupr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.10]
:: OPIS: po redakcji pól parametrów funkcji dołącz, usuń, popraw uprawnień pozycji grupy cenników
::----------------------------------------------------------------------------------------------------------------------

_fld:=fld;

TAZ.BB:=TAZ.BM:=TAZ.BP:=TAZ.BG:='N';
TAZ.OB:=TAZ.OM:=TAZ.OP:=TAZ.OG:='N';
TAZ.KB:=TAZ.KM:=TAZ.KP:=TAZ.KG:='N';
TAZ.GB:=TAZ.GM:=TAZ.GP:=TAZ.GG:='N';

fld(_fld);
1


\targn_usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: usuwa grupę uprawnień do cenników
::----------------------------------------------------------------------------------------------------------------------
{? TARGN.count()-exec('targn_ile_upr','ceny_uprawn',TARGN.ref())>0
|| FUN.info('Grupa uprawnień wykorzystywana.\nUsunięcie niemożliwe.'@)
|? FUN.ask('Usunąć grupę uprawnień?'@)
|| TARUP.cntx_psh();
   TARUP.index('TARGN');
   TARUP.prefix(TARGN.ref());
   {? TARUP.first()
   || {!
      |? TARUP.del();
         TARUP.first()
      !}
   ?};
   TARUP.cntx_pop();
   TARGN.del()
?};
~~


\targn_ile_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [22.26]
:: OPIS: sprawdza liczbę użytkowników i grup uprawnionych do danej grupy uprawnień do cenników
::   WE: _a - TARGN.ref
::   WY: ilosc użytkowników
::----------------------------------------------------------------------------------------------------------------------
TARUP.cntx_psh();
TARUP.index('TARGN');
TARUP.prefix(_a);
_wyn:=TARUP.size();
TARUP.cntx_pop();
_wyn


:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:16 07c985c8ca2989005c76c6df18ddcf3381f8b09f7af2a902ed279ade2d15dd99af91c25afc66a1f9226eafd4226eae7a0df64f8326527f8ccc1907c16eba8a24bcf5f32ac1090046a94a5b4ad2e0c48d2600d5622ca237e7cfbc1b21fc8e3ce07edf6ac68608358d18349ca8ac85bd3e7f6e4ac22265151e43f3b92279d6789c

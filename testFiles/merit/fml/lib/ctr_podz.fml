:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: ctr_podz.fml
:: Utworzony: 27.11.2017
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi uprawnień do podziałów
::======================================================================================================================


\arf_k_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Po odswiezeniu okna grupowego tabeli K_ROLE
::  OLD: \arf_k_role/skid_con.fml
::  OLD: \arf_k_role/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
K_ROLE_U.index('K_ROLE');
{? K_ROLE.get()
|| K_ROLE_U.prefix(K_ROLE.ref())
|| K_ROLE_U.prefix(null)
?};
grp_disp(K_ROLE_U,'WER');
grp_disp(USERS,'SLO2_1')


\ae_k_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.30]
:: OPIS: Formula na wzorzec pola K_ROLE.NAZWA
::  OLD: \ae_k_role/skid_con.fml
::  OLD: \ae_k_role/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
_bad:='.,;\'`:/\\*|?"&%$!+=()[]{}<>';
_ok:=1;
{! _i:=1..+_bad
|? _ok
|! _ok:=K_ROLE.NAZWA*((_i+_bad)+1)=0
!};
{? _ok=0
|| FUN.info('Nazwa zawiera niedozwolone znaki: \'%1\'.'@[_bad])
?};
_ok


\bd_k_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Przed usunieciem roli
::  OLD: \bd_k_role/skid_con.fml
::  OLD: \bd_k_role/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
K_ROLE_U.cntx_psh(); _ok:=1;
K_ROLE_U.index('K_ROLE'); K_ROLE_U.prefix(K_ROLE.ref());
{? K_ROLE_U.first()
|| FUN.info('Istnieją użytkownicy przypisani do zakresu danych.'@);
   _ok:=0
?};
K_ROLE_U.cntx_pop();
{? _ok & FUN.ask('Usunąć zakres danych?'@)
|| do();
   K_ROLE_D.index('K_ROLE'); K_ROLE_D.prefix(K_ROLE.ref());
   {? K_ROLE_D.first()
   || {! |? K_ROLE_D.del() !}
   ?};
   {? K_ROLE.TYPE='O' & exec('run_con','control',6)<>0 || undo() ?};
   K_ROLE.del();
   end()
?}


\ar_k_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord po tabeli K_ROLE
::  OLD: \ar_k_role/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
_r:=__CHK.record(K_ROLE,,'NAZWA');
{? _r=''
|| _typ:={? K_ROLE.TYPE='O' || 'OLAP' || 'Qlik Sense' ?};
   _ref:=K_ROLE.ref();
   K_ROLE.cntx_psh();
   K_ROLE.index('TYPE'); K_ROLE.prefix(REF.FIRMA,K_ROLE.TYPE,K_ROLE.NAZWA,);
   {? K_ROLE.first() & (-menu_txt()<>'popraw' | K_ROLE.ref()<>_ref)
   || FUN.info('Istnieje zakres danych o nazwie: %1 dla %2.'@[K_ROLE.NAZWA,_typ]);
      _r:='NAZWA'
   ?};
   K_ROLE.cntx_pop()
?};
_r


\init_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodatkowe ustawienia okien
::  OLD: \init_win/!ctr_rap_zzad.fml
::  OLD: \init_win/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
K_ROLE.win_fml('WER',,'NAZWA',,'ICON_BEFORE',"
   {? K_ROLE.WYM_AKT='T' || 'xwin16.png:4' || '' ?}
");
K_ROLE_U.dnd_sel('WER',,'records.USERS',"
   {? ~K_ROLE.get()
   || ~~
   |? exec('k_info_first','control') & K_INFO.OLAP_USR
   || _tab:=dnd_info('dropped_records');
      USERS.cntx_psh();
      {? K_ROLE.TYPE<>'O' | exec('chk_licen','ctr_podz',_tab)
      || {? _tab.first()
         || {!
            |? {? USERS.seek(_tab.REF,)
               || exec('user_add','ctr_podz',0)
               ?};
               _tab.next()
            !};
            grp_disp(USERS,'SLO1')
         ?}
      ?};
      USERS.cntx_pop()
   || FUN.info('Nie zdefiniowano parametrów programu\n(liczba licencji na przeglądanie kostek OLAP).'@)
   ?}
");
K_ROLE.dnd_sel('WER',,'records.USERS',"
   {? exec('k_info_first','control') & K_INFO.OLAP_USR
   || _tab:=dnd_info('dropped_records');
      {? K_ROLE.TYPE<>'O' | exec('chk_licen','ctr_podz',_tab)
      || K_ROLE.cntx_psh();
         _ref:=dnd_info('dest_record');
         {? K_ROLE.seek(_ref)
         || {? _tab.first()
            || USERS.cntx_psh();
               {!
               |? {? USERS.seek(_tab.REF,)
                  || K_ROLE_U.K_ROLE:=K_ROLE.ref();
                     K_ROLE_U.USERS:=USERS.ref();
                     K_ROLE_U.cntx_psh(); K_ROLE_U.prefix();
                     {? ~K_ROLE_U.find_rec()
                     || K_ROLE_U.add();
                        {? K_ROLE.TYPE='O'
                        || K_ROLE.WYM_AKT:='T'; K_ROLE.put()
                        ?}
                     ?};
                     K_ROLE_U.cntx_pop()
                  ?};
                  _tab.next()
               !};
               USERS.cntx_pop()
            ?}
         ?};
         K_ROLE.cntx_pop()
      ?}
   || FUN.info('Nie zdefiniowano parametrów programu\n(liczba licencji na przeglądanie kostek OLAP).'@)
   ?}
");
USERS.dnd_sel('SLO1',,'records.K_ROLE_U',"
   _tab:=dnd_info('dropped_records');
   {? _tab.first()
   || {!
      |? {? K_ROLE_U.seek(_tab.REF,)
         || exec('user_role_del','ctr_podz')
         ?};
         _tab.next()
      !}
   ?};
   grp_disp(K_ROLE_U,'WER')
");

UNPAR.P10:=UNPAR.P11:='';
UNPAR.P10_BV:=UNPAR.P11_BV:='';
SKID_MBN.win_fml('K_ROLE',UNPAR,'P10',,'ICON_BEFORE',"
   _r:=exec('k_role_d_model','ctr_podz');
   'xwin16.png:'+
   {? _r=0
   || '180'
   |? _r=1
   || '181'
   || '179'
   ?}
");
SKID_MBP.win_fml('K_ROLE',UNPAR,'P11',,'ICON_BEFORE',"
   'xwin16.png:'+
   {? exec('k_role_d_dim','ctr_podz')
   || '179'
   || '180'
   ?}
");
UD_DEF.win_fml('K_ROLE',,'SYMBOL',,'ICON_BEFORE',"
   K_ROLE_D.index('K_ROLE'); K_ROLE_D.prefix(K_ROLE.ref(),SKID_MBP.ref(),UD_DEF.UD_SKL);
   'xwin16.png:'+
   {? K_ROLE_D.first()
   || '179'
   |? exec('k_role_d_up','ctr_podz')
   || '181'
   |? exec('k_role_d_down','ctr_podz')
   || '178'
   || '180'
   ?}
");
~~


\k_role_d_model
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Czy nadano uprawnienia do modelu
::   WY: 0-nie; 1-tak, czesci wymiarow; 2-tak, wszystkie wymiary
::  OLD: \k_role_d_model/skid_con.fml
::  OLD: \k_role_d_model/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
K_ROLE_D.index('K_ROLE'); K_ROLE_D.prefix(K_ROLE.ref());
SKID_MBP.cntx_psh();
SKID_MBP.index('LP'); SKID_MBP.prefix(SKID_MBN.ref());
_jest:=0; _size:=0;
{? SKID_MBP.first()
|| {!
   |? _size+=1;
      _jest+=K_ROLE_D.find_key(SKID_MBP.ref());
      SKID_MBP.next()
   !}
?};
SKID_MBP.cntx_pop();
{? _jest
|| {? _jest=_size
   || 2
   || 1
   ?}
?}


\k_role_d_dim
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Czy nadano uprawnienia do pozycji modelu (wymiaru)
::  OLD: \k_role_d_dim/skid_con.fml
::  OLD: \k_role_d_dim/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
K_ROLE_D.index('K_ROLE'); K_ROLE_D.prefix(K_ROLE.ref(),SKID_MBP.ref());
K_ROLE_D.first()


\k_role_d_up
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Czy rodzice elementu schematu danych sa zaznaczone
::  OLD: \k_role_d_up/skid_con.fml
::  OLD: \k_role_d_up/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.cntx_psh(); UD_DEF.index('SYMBOL'); UD_DEF.prefix(SKID_MBP.UD_SCH);
K_ROLE_D.index('K_ROLE'); K_ROLE_D.prefix(K_ROLE.ref(),SKID_MBP.ref());
_jest:=0;
{!
|? {? UD_DEF.UD_DEF<>0 & UD_DEF.seek(UD_DEF.UD_DEF,)
   || _jest:=K_ROLE_D.find_key(UD_DEF.UD_SKL);
      ~_jest
   ?}
!};
UD_DEF.cntx_pop();
_jest


\k_role_d_down
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Czy potomkowie elementu schematu danych sa zaznaczeni
::  OLD: \k_role_d_down/skid_con.fml
::  OLD: \k_role_d_down/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.cntx_psh(); UD_DEF.index('SYMBOL'); UD_DEF.prefix(SKID_MBP.UD_SCH,UD_DEF.ref());
K_ROLE_D.index('K_ROLE'); K_ROLE_D.prefix(K_ROLE.ref(),SKID_MBP.ref());
_jest:=0;
{? UD_DEF.first()
|| {!
   |? _jest:=K_ROLE_D.find_key(UD_DEF.UD_SKL) | exec('k_role_d_down','ctr_podz');
      _jest=0 & UD_DEF.next()
   !}
?};
UD_DEF.cntx_pop();
_jest


\arf_k_role_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Po odswiezeniu modelu w oknie grupowym tabeli SKID_MBN
::  OLD: \arf_k_role_m/skid_con.fml
::  OLD: \arf_k_role_m/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBN.cntx_psh();
_jest:=SKID_MBN.f_get() | SKID_MBN.f_first();
SKID_MBN.cntx_pop();
SKID_MBP.index('LP');
{? _jest
|| SKID_MBP.prefix(SKID_MBN.ref())
|| SKID_MBP.prefix(null)
?};
grp_disp(SKID_MBP,'K_ROLE',1)


\arf_k_role_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Po odswiezeniu okna grupowego tabeli K_ROLE
::  OLD: \arf_k_role_p/skid_con.fml
::  OLD: \arf_k_role_p/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.index('SYMBOL');
{? SKID_MBP.get()
|| UD_DEF.prefix(SKID_MBP.UD_SCH)
|| UD_DEF.prefix(null)
?};
grp_disp(UD_DEF,'K_ROLE')


\k_role_d_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Dodaje/usuwa uprawnienia do elementu danych dla roli
::   WE: _a - typ operacji: 1-dodaje 0-usuwa
::  OLD: \k_role_d_set/skid_con.fml
::  OLD: \k_role_d_set/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
K_ROLE_D.index('K_ROLE'); K_ROLE_D.prefix(K_ROLE.ref(),SKID_MBP.ref(),UD_DEF.UD_SKL);
{? _a=1
|| {? ~K_ROLE_D.first() & ~exec('k_role_d_up','ctr_podz')
   || K_ROLE_D.K_ROLE:=K_ROLE.ref();
      K_ROLE_D.SKID_MBP:=SKID_MBP.ref();
      K_ROLE_D.UD_SKL:=UD_DEF.UD_SKL;
      {? K_ROLE_D.add()
      || exec('k_role_d_del','ctr_podz')
      ?};
      {? K_ROLE.TYPE='O'
      || K_ROLE.WYM_AKT:='T'; K_ROLE.put()
      ?}
   ?}
|| {? K_ROLE_D.first()
   || {? K_ROLE.TYPE='O'
      || K_ROLE.WYM_AKT:='T'; K_ROLE.put()
      ?};
      K_ROLE_D.del()
   ?}
?};
grp_disp(SKID_MBN,'K_ROLE');
grp_disp(SKID_MBP,'K_ROLE')


\k_role_d_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2011]
:: OPIS: Usuwa uprawnienia do elementow podrzednych
::  OLD: \k_role_d_del/skid_con.fml
::  OLD: \k_role_d_del/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
UD_DEF.cntx_psh(); UD_DEF.index('SYMBOL'); UD_DEF.prefix(SKID_MBP.UD_SCH,UD_DEF.ref());
{? UD_DEF.first()
|| K_ROLE_D.index('K_ROLE'); K_ROLE_D.prefix(K_ROLE.ref(),SKID_MBP.ref());
   {!
   |? {? K_ROLE_D.find_key(UD_DEF.UD_SKL)
      || K_ROLE_D.del()
      || exec('k_role_d_del','ctr_podz')
      ?};
      UD_DEF.next()
   !}
?};
UD_DEF.cntx_pop()


\br_upr_ud_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed okna K_ROLE tabeli UD_DEF
::  OLD: \br_upr_ud_def/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
_mark:='';
K_ROLE_D.index('K_ROLE'); K_ROLE_D.prefix(K_ROLE.ref(),SKID_MBP.ref());
{? K_ROLE_D.find_key(UD_DEF.UD_SKL)
|| _gray+='N';
   _mark:='O'
|| _rjest:=0;
   UD_DEF.cntx_psh();
   UD_DEF.prefix();
   {!
   |? {? UD_DEF.UD_DEF & UD_DEF.seek(UD_DEF.UD_DEF)
      || _rjest:=K_ROLE_D.find_key(UD_DEF.UD_SKL);
         _rjest=0
      ?}
   !};
   UD_DEF.cntx_pop();
   {? _rjest
   || _gray+='ON'
   || _gray+='O';
      _mark:='N'
   ?}
?};
UD_DEF.actions_grayed('K_ROLE',_gray);
UD_DEF.actions('K_ROLE',,_mark,1);
~~


\chk_licen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Sprawdzenie licencji dla kostki OLAP
::   WE: [_a] - tabela z zaznaczonymi użytkownikami
::  OLD: \chk_licen/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('k_info_first','control') & K_INFO.OLAP_USR
|| _tab:={? var_press('_a')>0 || _a || ~~ ?};
   _lu:=exec('olap_usr','control');
   _nowe:=0;
   K_ROLE_U.cntx_psh();
   K_ROLE_U.index('USERS'); K_ROLE_U.prefix();
   {? var_press('_tab')<=0
   || {? ~K_ROLE_U.find_key(USERS.ref())
      || _nowe+=1
      ?}
   |? _tab.first()
   || {!
      |? {? USERS.seek(_tab.REF,) & ~K_ROLE_U.find_key(USERS.ref())
         || _nowe+=1
         ?};
         _tab.next()
      !}
   ?};
   K_ROLE_U.cntx_pop();
   {? K_INFO.OLAP_USR>=_lu+_nowe
   || 1
   || FUN.info(
         'Operacja spowoduje przekroczenie\nlicenicji na przeglądanie kostek OLAP.\n\n'
         'Licencje dostępne: %1\n'
         'Licencje zużyte: %2\n'
         'Nowe licencje: %3.'@[$K_INFO.OLAP_USR,$_lu,$_nowe]
      );
      0
   ?}
|| FUN.info('Nie zdefiniowano parametrów programu\n(liczba licencji na przeglądanie kostek OLAP).'@);
   0
?}


\user_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Dodanie użytkownika do roli
::   WE: [_a] - sprawdzać licencję
::  OLD: \user_add/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
{? _a=0 | exec('chk_licen','ctr_podz')
|| K_ROLE_U.blank(1);
   K_ROLE_U.K_ROLE:=K_ROLE.ref();
   K_ROLE_U.USERS:=USERS.ref();
   K_ROLE_U.cntx_psh(); K_ROLE_U.prefix();
   {? ~K_ROLE_U.find_rec()
   || K_ROLE_U.add();
      {? K_ROLE.TYPE='O'
      || K_ROLE.WYM_AKT:='T'; K_ROLE.put()
      ?}
   ?};
   K_ROLE_U.cntx_pop()
?}


\user_role_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Akcja dodanie użytkownika do roli
::  OLD: \user_role_add/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
{? USERS.sel_aget().size()>0 || _info:=0 || _info:=1 ?};
{? ~K_ROLE.get()
|| ~~
|? K_ROLE.TYPE<>'O'
|| exec('user_add','ctr_podz',0);
    grp_disp(K_ROLE_U,'WER')
|? exec('k_info_first','control') & K_INFO.OLAP_USR
|| {? exec('user_has_pass','control',_info)
   || exec('user_add','ctr_podz',0);
       grp_disp(K_ROLE_U,'WER')
   ?}
|| FUN.info('Nie zdefiniowano parametrów programu\n(liczba licencji na przeglądanie kostek OLAP).'@)
?}


\user_role_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Usunięcie użytkownika z roli
::  OLD: \user_role_del/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
K_ROLE_U.del();
{? K_ROLE.TYPE='O'
|| K_ROLE.WYM_AKT:='T'; K_ROLE.put()
?}


\k_role_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Definicje uprawnień
::  OLD: \k_role_def/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBN.index('KOD');
exec('set_skid_mbn_f','control',K_ROLE.TYPE);
{? REF.S_FIRMA=REF.GRUPA
|| _tab:=FIRMA;
   FIRMA.index('SYMBOL2'); FIRMA.prefix('S');
   _o:=FIRMA.mk_sel('Firmy'@);
   FIRMA.win_fld(_o,,'SYMBOL',,,3,,,'Symbol'@);
   FIRMA.win_fld(_o,,'OPIS',,,31,,,'Opis'@);
   FIRMA.win_sel(_o);
   _grp:=_tab.grp_make(,,'k_role_def1');
   _tab.grp_sel(_grp,FIRMA,_o,,"exec('arf_firma','ctr_podz')",,,,,,,,'maximized_with_title');
   _tab.grp_splt(_grp,,'horizontal','left0',10);
   _panel:='left0'
|| _tab:=SKID_MBN;
   _grp:=_tab.grp_make(,,'k_role_def2');
   _panel:='panel0'
?};
_tab.grp_sel(_grp,SKID_MBN,'K_ROLE',,"exec('arf_k_role_m','ctr_podz')",,,,,,,,'maximized_with_title');
_tab.grp_splt(_grp,_panel,'horizontal','left2',10);
_tab.grp_sel(_grp,SKID_MBP,'K_ROLE',,"exec('arf_k_role_p','ctr_podz')",,,,,,,,'maximized_with_title');
_tab.grp_splt(_grp,,'vertical','left3');
_tab.grp_sel(_grp,UD_DEF,'K_ROLE',,,,,,,,,,'maximized_with_title');
_tab.win_sel(_grp);
_tab.hdr_sel();
_tab.hdr_sel('Zakres danych - %1'@[K_ROLE.NAZWA]);
_tab.select()


\trig_k_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Triger przed put i add
::  OLD: \trig_k_role/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
K_ROLE.TYPEDESC:=
{? K_ROLE.TYPE='O' || 'OLAP'
|? K_ROLE.TYPE='Q' || 'Qlik Sense'
|? K_ROLE.TYPE='B' || 'Obiegi'
|| ''
?};
{? K_ROLE.TYPE<>'O'
|| K_ROLE.WYM_AKT:='N'
?};
1


\br_k_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Rekord przed tabeli K_ROLE
::  OLD: \br_k_role/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
{? K_ROLE.TYPE<>'O'
|| _gray+='A'
?};
K_ROLE.actions_grayed('WER',_gray);
~~


\arf_firma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS:
::   WE:
::   WY:
::  OLD: \arf_firma/!ctr_rap_zzup.fml
::----------------------------------------------------------------------------------------------------------------------
REF.FIRMA:=FIRMA.ref();
exec('set_skid_mbn_f','control');
exec('arf_k_role_m','ctr_podz');
grp_disp(SKID_MBN,'K_ROLE',1);
~~

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:16 a8e9fefd95eda9c22c998176a44909b951d3bf3030e7a23eac1383fe1dd4daa21a172782003730b5c66b90531a296fcba7548a0cbd3d287e6788597e61df948cef25084165d0c7837c691d7ed9d7843d8f27fa330a85c449b69023bc2854c8d3b2ef574c8091deab102ae0d59d8e9da4d4997888039852a67d67da34dd1e241e

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: lmo.fml
:: Utworzony: 05.09.2018
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły oraz wywoływacze obszaru LMO
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła inicjująca dla obszaru LMG
::----------------------------------------------------------------------------------------------------------------------
:: Podczytanie INFO.KRAJ - kraj waluty opodatkowania
exec('czytaj','#stalesys',,INFO);
exec('czytaj','#stalesys',,XINFO,'SLWYDZIA','SLJEZYK','SLWAL');
exec('czytaj','#stalesys',,KST,'BDO');

:: Podczytanie parametrów oddziału
KSTE.prefix; KSTE.first();

:: Ustawia zmienne informujące o dostępie do wartości sprzedaży, zakupu, magazynowych
exec('upr_set','ceny');

:: Formuła podczytująca dane specyficzne dla firmy
__War_f:="exec('FindInSet','#table',_a,_a,_c,REF.FIRMA,$(_a+'.'+_b))";

:: Walutowość dla sprzedaży
BEER.MW:='M';
HELP.WHERE:='M';
HELP.DKRED:='T';

:: Obiekt do obsługi parametrów
exec('Tpar_decl','tech_param');

:: RB - objekt obsługujący rachunki bankowe
exec('RB','object');

:: Ustawia BEER.WW, BEER.LW
exec('ustaw_ww','eurusd','M');
exec('ust_lw','eurusd',0);

:: Obiekt do obsługi dokumentacji elektronicznej + domyślna instancja
exec('declare','tech_doc');

:: Ustawienie prezycji pól w oknach
exec('fld_prec','win_ust');

:: ikony zwiazane ze statusem dostawy
exec('skid_kus','statexam');

:: Salda i obroty kont i rozrachunków
exec('F','object');

:: drukarki kodów kreskowych
exec('myDPRINT','object');
~~


\lmo_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.42]
:: OPIS: Formuła sprawdzająca licencję dla obszaru LMO - pośrednio wymaganarównież licencja do LMG
::   WY: 'T' - jest licencja, 'N' - nie ma licencji
::----------------------------------------------------------------------------------------------------------------------
_result:='N';
{? exec('lmg_lic','lmg')='T'
|| _domain:=exec('domain_ref','#b_domain','LMO');
   {? _domain<>null()
   || {? exec('lic','#b_domain',_domain)=1
      || _result:='T'
      ?}
   ?}
?};
_result


\lmo_mob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Główna formuła wywoływacza LMG_MOB - Mobilne magazyny
::----------------------------------------------------------------------------------------------------------------------
exec('init','lmo');
exec('operac_sel','magazyn_mob',exec('get','#params',600701,2),'M')


\lmo_pal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Główna formuła obszaru roboczego LMO_PAL - Palety
::   WE: [_a] 0(domyslnie)-palety aktywne 1-palety nieaktywne
::       [_b] 0(domyslnie)-menu Magazyn 1-menu definicje
::  OLD: \obsl_pal/mws.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=1 ?} || _b:=1 ?};
{? ~exec('pal_permissions','magdok_wspolne')
|| FUN.info('Brak uprawnień do magazynów z obsługą palet.'@);
   return(0)
|? ST.MAG().PAL='N'
|| FUN.info('Magazyn ustawiony w parametrach pracy jest bez obsługi palet.'
            '\nNależy zmienić parametry pracy.'@);
   return(0)
?};
exec('init','lmg');

:: ikonki dla pól
exec('eanx_wys','okienka');
POMOC.RODZ:='T';
ATR.MJS:='PAL_POZ';
ZAKR.AKT:='T';
PAL.f_clear();


_win_grp:=PAL.grp_make(,"",'#grpdompal',,,"exec('exit','zws',_a)");

:: Palety
PAL.grp_sel(_win_grp,PAL,'SEL','Palety'@
 ,"exec('pogrppal','magdok_palety')",,,10
 ,"ZAKR.ARCHPAL:='palety';exec('priszpal','magdok_palety')","exec('poiszpal','magdok_palety')",,,'maximized',,1);
PAL.tab_splt(_win_grp,'tab0','vertical','pozycje');
PAL.grp_sel(_win_grp,PAL_POZ,'WER',,"",,,10
 ,"exec('pwpalpoz','magdok_palety')","exec('popalpoz','magdok_palety')",,,'maximized_with_title');

PAL.grp_sel(_win_grp,PAL,'BEZ','Nieoznaczone'@,"",,,10
 ,"exec('prbezpal','magdok_palety')","exec('pobezpal','magdok_palety')",,,'maximized');
PAL.grp_sel(_win_grp,PAL,'NOPAL','Wydania bez rozpakowań'@,"exec('pogrnpal','magdok_palety')",,,10
 ,"exec('pr_nopal','magdok_palety')","exec('po_nopal','magdok_palety')",,,'maximized');
PAL.tab_splt(_win_grp,'tab0','vertical','pozycje');
PAL.grp_sel(_win_grp,PAL_POZ,'WERR',,"",,,10
 ,"exec('pwpalpoz','magdok_palety',1)","exec('popalpoz','magdok_palety')",,,'maximized_with_title');

:: Dodanie zakładek z archiwalnymi paletami
_msk:=PAL.names();
{? _msk.first()
|| {!
   |? {? _msk.NAME='palet_' | _msk.NAME='palety'
      || _msk.del()
      || _msk.next()
      ?}
   !}
?};

_msk.clear();
{? _msk.first()
|| {!
   |? _num:=exec('numArch','lmo',_msk.NAME);
      _form:='ZAKR.ARCHPAL:=\'%1\';exec(\'priszpal\',\'magdok_palety\')'[_msk.NAME];
      PAL.grp_sel(_win_grp,PAL,'SEL','Archiwum nr %1'@[_num],"exec('pogrppal','magdok_palety')",,,10
       ,$_form,"exec('poiszpal','magdok_palety')",,,'maximized');
      PAL.tab_splt(_win_grp,'tab0','vertical','pozycje');
      PAL.grp_sel(_win_grp,PAL_POZ,'WER',,"",,,10
       ,"exec('pwpalpoz','magdok_palety')","exec('popalpoz','magdok_palety')",,,'maximized_with_title');
      _msk.next()
   !}
?};
obj_del(_msk);


PAL.win_sel('GRP');
PAL.win_sel(_win_grp);

PAL.index('AKT');
PAL.prefix(ST.ODDZ,'N',ZAKR.AKT);
_act:=PAL.actions('SEL',{? ~_a || 'BRZWE(AMW):RE(MW)' || 'BRDUZApCWE(NWM):RDE(NWM)' ?});
EANX.WHERE:='O';
_m_sel:={? exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_ZPSM') || 'WER_S' || 'WER_B' ?};
M.win_dict(_m_sel);
M.win_sel(_m_sel);

AreaTitle.setTabWin(PAL,~~);
AreaTitle.setTitle();
{? ~PAL.size() || exec('reki_pal','magdok_palety') ?};

PAL.select();
PAL.actions('SEL',_act);
~~


\lmo_pal_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Zmiana parametrów pracy obszaru LMO_PAL
::----------------------------------------------------------------------------------------------------------------------
BPMN.TYP_MG:='P';
_oddz:=ST.ODDZ;
_okr_ref:=ST.OKR_REF;
{? __PARSES.editDom('LMG')
|| {? _okr_ref<>ST.OKR_REF
   || FILTER.TYPYDOK:=null();
      FILTER.ND:=null()
   ?};
   {? _oddz<>ST.ODDZ || exec('init','lmg') ?};
   {? AreaTitle.area='LMG_MAG'
   || exec('nd_actions','magdok_nag');
      grp_disp(ND,'WERP');
      exec('hideORshow','lmg')
   ?};
   AreaTitle.setTitle()
?}


\lmo_zakr_pal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: ustala zakres wyswietlanych palet
::   WE: [_a] 0(domyslnie)-palety aktywne 1-palety nieaktywne
::----------------------------------------------------------------------------------------------------------------------
POMOC.RODZ:='T';
ATR.MJS:='PAL_POZ';
ZAKR.AKT:={? ~_a || 'T' || 'N' ?};
PAL.f_clear();
PAL.index('AKT');
PAL.prefix(ST.ODDZ,'N',ZAKR.AKT);
M.win_dict('NL_WER');
M.win_sel('NL_WER');
PAL.actions('SEL',{? ZAKR.AKT='T' || 'ZWE(AMW):E(MW)' || 'DUZApCWE(NWM):DE(WM)' ?},,1);
{? ~PAL.size() || exec('reki_pal','magdok_palety') ?};
~~


\permissions_domain
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła na uprawnienia obszaru
::   WE: params_get().in - parametry wejściowe czynności
::       params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::       params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_wyn:=1;

_opis:='';

:: Sprawdzenie czy jest ustawiony rok
{? _wyn
|| _wyn:=ST.AR>0;
   {? _wyn=0 || _opis+='\n- okresu' ?}
?};

{? _wyn=0 & var_pres('opis',params_get())
|| params_get().opis.TXT:='Brak uprawnień do:'+_opis
?};

_wyn


\lmo_mob_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Zmiana parametrów pracy obszaru LMO_MOB
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
_okres:=$ST.AR+$ST.AM;

{? __PARSES.editPar('OkresRok','OddzialLogProd')
|| {? _oddz<>ST.ODDZ
   || exec('init','lmo')
   ?};
   AreaTitle.setTitle()
?}


\numArch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.22]
:: OPIS: Zwraca numer archiwum na podstawie maski
::   WE: _a - maska tabeli PAL
::   WY: numer archiwum
::----------------------------------------------------------------------------------------------------------------------
_res:='';
_ch:={? var_pres('_a')=type_of('') & _a<>'' || _a+1 || '' ?};
_ch:=gsub(_ch,'y',''); _ch:=gsub(_ch,'_','');
{? _ch<>''
|| _num:=%_ch;
   {? _num>=48 & _num<58
   || _res:=form(_num-47,2,0,'99')
   |? _num>=97 & _num<123
   || _res:=form(_num-86,2,0,'99')
   ?}
?};
_res

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 3b98c65bb5fc3f213943e447d7d551e0a2b9dd30c6e71378ccb5018761771d34db78df721e4aea8f692c1feb02ad850adb7a9b5137686c3dd42a668f5851125a96d39b801552d71fe24a8131b2cc0fe8b0cfe4735b735eb5db14636cee77c4105507e0dcc94b3af6db135226afa0589f2ad65c341c9d56b91861bd98491296e9

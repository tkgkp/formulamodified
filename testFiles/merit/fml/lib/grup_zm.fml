:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: grup_zm.fml
:: Utworzony:
:: Autor: KN
:: Systemy:
::======================================================================================================================
:: Zawartość: Formuły do obsłuki akcji grupowego wystawiania dokumentu zmiany miejsca.
::======================================================================================================================
\grup_zm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.42]
:: OPIS: Okno redagowania akcji grupowego wystawienia dokumentu zmiany miejsca
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__srdo_fld','refsrst');
rek:=SRDO.sel_size();
SRDT.cntx_psh(); SRDT.index('TYP');
{? FINFO.SRXI_MT<>null() & SRDT.seek(FINFO.SRXI_MT,,1)
|| __srdo_fld:=obj_new('JORG','POM','OSOBA','KONPOD','KONFIN','KONDOD');
   __srdo_fld.JORG:=null; __srdo_fld.POM:=null(); __srdo_fld.OSOBA:=null;
   __srdo_fld.KONPOD:=null(); __srdo_fld.KONFIN:=null(); __srdo_fld.KONDOD:=null();
   refsrst:=SRST.ref();
   SRDO.blank();
   SRDO_POM.blank();
   EDIT_ES.JORG:='';
   __DEPT:=0;
   SRDO.TYP:=ref:=SRDT.ref();
   SRDO.RODZ:='L';
   SRDO.win_edit('RED_GR2');
   {? FINFO.TOR_D='N'
   || _enable:=0
   || {? SRDO.TYP().KONDOD='N' || _enable:=0 || _enable:=1 ?}
   ?};
   SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'KONDOD');
   {? SRDO.TYP().JORG='N' || _enable:=0 || _enable:=1 ?};
   SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable, EDIT_ES, 'JORG');
::   {? SRDO.TYP().ODD='N' || _enable:=0 || _enable:=1 ?};
   SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'ODD');
   {? SRDO.TYP().POM='N' || _enable:=0 || _enable:=1 ?};
   SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'POM','S');
   SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'POM','O');
   _tab:=SRST.sel_aget; _SaBenefity:=0;
   {? _tab.first
   || {!|? SRST.seek(_tab.REF,);
           {? SRST.SRSR().BENEFIT='T' || _SaBenefity:=1 ?};
           ~_SaBenefity & _tab.next
      !}
   ?};
   {? SRDO.TYP().OSOBA='N'
   || _enable:=0;
      SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'OSOBA');
      SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,EDIT_ES, 'PRCNAZ');
      SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,EDIT_ES, 'STNAZ')
   || {? _SaBenefity || EDIT_ES.PRCNAZ:='';_enable:=1 || _enable:=0 ?};
      SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable, EDIT_ES, 'PRCNAZ');
      SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable, EDIT_ES, 'STNAZ');
      SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable, EDIT_ES, 'WYDZNAZ');
      SRDO.efld_opt(SRDO.win_edit('?'), 'enable=1,editable='+$~_enable,, 'OSOBA')
   ?};
   {? SRDO.TYP().KONPOD='N' || _enable:=0 || _enable:=1 ?};
   SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'KONPOD');
   {? SRDO.TYP().KONFIN='N' || _enable:=0 || _enable:=1 ?};
   SRDO.efld_opt(SRDO.win_edit('?'), 'enable='+$_enable,, 'KONFIN');
   SRDO.SYMBOL:=exec('symdok','fst');
   SRDO.NR:=exec('bl_srdo_nr','fst');
   {? SRDO.edit("exec('chk_dok','grup_zm')")
   || __srdo_fld.JORG:=SRDO.JORG; __srdo_fld.POM:=SRDO.POM; __srdo_fld.OSOBA:=SRDO.OSOBA;
      __srdo_fld.KONPOD:=SRDO.KONPOD; __srdo_fld.KONFIN:=SRDO.KONFIN; __srdo_fld.KONDOD:=SRDO.KONDOD;
      EDIT_ES.P:=SRDO.PRC
   || SRDT.cntx_pop();
      {? SRST.seek(refsrst) || 1 ?};
      exec('grup_po','grup_zm');
      return(0)
   ?}
|| FUN.emsg('W systemie nie zdefiniowano typu dokumentu zmiany właściwości.\n'
            'Należy uzupełnić brakujące dane w obszarze Ustawienia i parametryzacja.'@);
   SRDT.cntx_pop();
   return(0)
?};
SRDT.cntx_pop()


\grup_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.42]
:: OPIS: Dodanie dokumentu zmiany miejsca w akcji grupowej
::----------------------------------------------------------------------------------------------------------------------
{? var_press('rek')>0
|| SRDO.prefix();
   SRDO.TYP:=ref;
   SRDO.DOKKSIEG:='';
   SRDO.K:='N';
   SRDO.ODD_DOK:=SRST.ODD;
   SRDO.SRSR:=SRST.SRSR;
   SRDO.NR:=exec('bl_srdo_nr','fst');
   SRDO.SYMBOL:=exec('symdok','fst');
   _poj_ref:=exec('poj_srsr','samochody',SRST.SRSR);
   {? SRST.SRSR().POJAZD='T' & _poj_ref
   || _pojazd:=1
   || _pojazd:=0
   ?};
::    jeżeli dokument zmiany właściwości dofinansowania to wskazanie na dofinansowanie
   {? SRDO.RODZ='D' || SRDO.SRZF:=SRZF.ref() ?};
   {? SRDO.TYP().NRI='T' || SRDO_POM.NRI:=SRST.NRI ?};
   {? SRDO.TYP().NST='T' || SRDO_POM.NST:=SRST.NST ?};
   {? SRDO.TYP().JORG='T'
   || SRDO_POM.JORG:=SRST.JORG;
      {? __srdo_fld.JORG=null()
      || SRDO.JORG:=SRST.JORG

      ?};
      SRDO_POM.ODD:=SRST.ODD;
      SRDO.ODD:=SRST.ODD
   ?};


   {? SRDO.TYP().OSOBA='T'
   || SRDO_POM.OSOBA:=SRST.OSOBA;
      {? __srdo_fld.OSOBA=null()
      || SRDO.OSOBA:=SRST.OSOBA
      ?};
      {? SRST.SRSR().BENEFIT='T'
      || SRDO_POM.PRCD:=SRST.P().OSOBA().NAZWISKO;
         SRDO_POM.PRC:=SRST.P;
         SRDO.PRC:=EDIT_ES.P
      || SRDO.PRC:=null
      ?}
   ?};
   {? SRDO.TYP().POM='T'
   || SRDO_POM.POM:=SRST.POM;
      {? __srdo_fld.POM=null()
      || SRDO.POM:=SRST.POM
      ?}
   ?};
   {? SRDO.TYP().GR='T'
   || _klas:=SRST.GR().KST().KST;
      _obow:=exec('kst_obow','fst_kst',SSTALE.AO().RES);
      {? _klas=_obow || SRDO_POM.GR:=SRDO.GR:=SRST.GR || SRDO_POM.GR:=SRDO.GR:=null ?}
   ?};
   {? SRDO.TYP().R='T' || SRDO_POM.R:=SRDO.R:=SRST.R ?};
   {? SRDO.TYP().KONPOD='T'
   || SRDO_POM.KONPOD:=SRST.KONPOD;
      {? __srdo_fld.KONPOD=null()
      || SRDO.KONPOD:=SRST.KONPOD
      ?}
   ?};
   {? SRDO.TYP().KONFIN='T'
   || SRDO_POM.KONFIN:=SRST.KONFIN;
      {? __srdo_fld.KONFIN=null()
      || SRDO.KONFIN:=SRST.KONFIN
      ?}
   ?};
   {? FINFO.TOR_D='T'
   || {? SRDO.TYP().KONDOD='T'
      || SRDO_POM.KONDOD:=SRST.KONDOD;
         {? __srdo_fld.KONDOD=null()
         || SRDO.KONDOD:=SRST.KONDOD
         ?}
      ?}
   ?};
   _rodzaj:=SRST.R;
   _grupa:=SRST.GR;
   SRDO.R:=_rodzaj;
   exec('mod_rec','fst');
   {? SRDO.add()
   || _srdo_rf:=SRDO.ref();
      SRD.updateRecState(SRDO.OKRO_F,4);
      {? SRD.isSet() || SRD.updateElements(SRDO.OKRO_F,4) ?};
:: jeżeli pojazd to zapis zmian dla zasobu
      {? _pojazd
      || ZASOB_ZM.cntx_psh();
         ZASOB_ZM.index('SRDO_Z'); ZASOB_ZM.prefix(SRDO.ref());
         {? ZASOB_ZM.first()
         || ZASOB_ZM.DATAOD:=SRDO.DO;
            ZASOB_ZM.OSOBA:=SRDO.OSOBA;
            ZASOB_ZM.JORG:=SRDO.JORG;
            ZASOB_ZM.SRDO_Z:=SRDO.ref();
            ZM_ZASOB.ZASOBY:=ZASOB_ZM.ZASOBY;
            {? ZASOB_ZM.add()
            || exec('zam_poprz','zasoby_wspolne')
            ?}
         ?};
         ZASOB_ZM.cntx_pop()
      ?};

      {? SRDO.R<>_rodzaj
      || FUN.info('W przypadku zmiany rodzaju środka z trwałego na niskocenny (lub odwrotnie)\n'
                  'należy także wprowadzić dokument zmieniający metodę na odpowiednią.'@)
      ?};
      {? SRDO.GR<>null & SRDO.GR<>_grupa
      || FUN.info('W przypadku zmiany grupy środka należy także wprowadzić\n'
                  'dokument zmiany metody amortyzacji ze zmianą stawki na odpowiednią.'@)
      ?}
   ?}
|? ZM.GRUPOWE = 0
|| FUN.info('Funkcja grupowego wystawiania dokumentów zmiany właściwości.\nNie wybrano środków.'@)
?}


\grup_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.42]
:: OPIS: Akcja grupa po
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__srdo_fld','rek','tmpodd','refsrst')


\chk_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KN [20.42]
:: OPIS: Sprawdzenie poprawności danych w oknie redagowania
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
{? _wy=''
|| {? SRDO.DW=date(0,0,0)
   || FUN.emsg('Nieuzupełnione pole Data wystawienia.'@);
      _wy:='DW'
   ?}
?};
{? _wy=''
|| {? SRDO.DO=date(0,0,0)
   || FUN.emsg('Nieuzupełnione pole Data operacji.'@);
      _wy:='DO'
   ?}
?};
{? _wy=''
|| {? SRDO.OKRO_F=null
   || FUN.emsg('Nieuzupełnione pole Okres obowiązywania.'@);
      _wy:='OKRO_F'
   ?}
?};
{? _wy='' & SRDO.OKRO_F<>null
|| {? exec('test_new_year','fst',SRDO.OKRO_F().ROK)
   || FUN.emsg('Wskazana data operacji pochodzi z nowo utworzonego roku w obszarze Środków trwałych.\n'
               'Nowy rok nie ma utworzonych danych w rejestrze stanów środków. Przed dołączaniem zapisów\n'
               'w nowym roku należy uruchomić Kartotekę środków w nowym roku, system wówczas uruchmomi\n'
               'mechanizm aktualizacji danych rejestru stanów.'@);
      _wy:='DO'
   ?}
?};
:: kontrola miesiąca obowiązywania
{? _wy='' & ~exec('chk_dok_okres','fst') || _wy:='DO' ?};
:: kontrola daty wystawienia i daty eksploatacji środka
{? _wy='' & exec('ae_srdo_dw','fst')=0 || _wy:='DW' ?};
:: kontrola daty wystawienia i daty dofinansowania
{? _wy='' & SRDO.SRZF<>null & (SRDO.DW<SRDO.SRZF().DATA | SRDO.DO<SRDO.SRZF().DATA)
|| FUN.emsg('Data wystawienia dokumentu i data operacji nie mogą być wcześniejsze niż data dofinansowania.'@);
   {? SRDO.DW<SRDO.SRZF().DATA || _wy:='DW' || _wy:='DO' ?}
?};
:: pomieszczenie - weryfikacja czy pochodzi z jednostki zgodnej z dokumentem i/lub środkiem
{? _wy='' & SRDO.TYP().POM='T' & SRDO.POM<>null
   & ((SRDO.ODD<>null & SRDO.POM().ODD<>SRDO.ODD) | (SRDO.ODD=null & SRST.ODD<>SRDO.POM().ODD))
|| FUN.emsg('Wybrane pomieszczenie jest przypisane do innej jednostki księgowej niż\n'
            'wskazana w dokumencie lub aktualnie przypisana do środka.'@);
   _wy:='POM'
?};
:: kontrola pól z zestawami kont kosztowych
{? _wy='' & SRDO.TYP().KONPOD='T' & SRDO.KONPOD<>null
|| {? ~exec('zestaw_kont_ok','fst',SRDO.KONPOD)
   || FUN.emsg('Wybrany zestaw kont dla toru podatkowego jest niepoprawnie zdefiniowany.'@);
      _wy:='KONPOD'
   ?}
?};
{? _wy='' & SRDO.TYP().KONFIN='T' & SRDO.KONFIN<>null
|| {? ~exec('zestaw_kont_ok','fst',SRDO.KONFIN)
   || FUN.emsg('Wybrany zestaw kont dla toru bilansowego jest niepoprawnie zdefiniowany.'@);
      _wy:='KONFIN'
   ?}
?};
{? _wy='' & FINFO.TOR_D='T'
|| {? _wy='' & SRDO.TYP().KONDOD='T' & SRDO.KONDOD<>null
   || {? ~exec('zestaw_kont_ok','fst',SRDO.KONDOD)
      || FUN.emsg('Wybrany zestaw kont dla toru dodatkowego jest niepoprawnie zdefiniowany.'@);
         _wy:='KONDOD'
      ?}
   ?}
?};

{? _wy=''
|| _zsb_zm:=0;
   _dekret:=0;
   _konto:=0;
   SRST.cntx_psh();
   _srst_sel:=SRST.sel_aget();
   {? _srst_sel.first()
   || {!
      |? {? SRST.seek(_srst_sel.REF,SRST.name(),1)
         || {? SRST.SRSR().POJAZD='T'
:: kontrola czy wprowadzenie dokumentu nie spowoduje zdublowania dat zapisów zmian dla zasobu
:: co spowoduje błąd unikalności indeksu w systemie
            || _pojazd:=exec('poj_srsr','samochody',SRST.SRSR);
               {? _pojazd
               || POJAZDY.cntx_psh();
                  POJAZDY.prefix();
                  {? POJAZDY.seek(_pojazd)
                  || ZASOBY.cntx_psh();
                     ZASOBY.index('TYP');
                     ZASOBY.prefix('P',POJAZDY.ID);
                     {? ZASOBY.first()
                     || ZASOB_ZM.cntx_psh();
                        ZASOB_ZM.index('DISP');
                        ZASOB_ZM.prefix(ZASOBY.ref(),SRDO.DO);
                        {? ZASOB_ZM.first()
                        || _zsb_zm:=1
                        ?};
                        ZASOB_ZM.cntx_pop()
                     ?};
                     ZASOBY.cntx_pop()
                  ?};
                  POJAZDY.cntx_pop()
               ?}
            ?};
            {? _zsb_zm
            || FUN.emsg('Środek trwały jest pojazdem ewidencjonowanym w kartotece zasobów\n'
                        'a dla podanej daty operacji (%1) w systemie istnieje już zapis\n'
                        'zmiany dla zasobu. Należy wprowadzić inną datę.'@[$SRDO.DO]);
               _wy:='DO'
            ?};
            {? _wy='' & (SRDO.RODZ='W' | (SRDO.RODZ='L' & (SRDO.TYP().KONPOD='T' | SRDO.TYP().KONFIN='T'
               | (FINFO.TOR_D='T' & SRDO.TYP().KONDOD='T'))))
:: kontrola czy zmiana nie dotyczy zadekretowanej amortyzacji w przypadku dokumentów wartościowych
:: lub dokumentów zmiany właściwości dotyczących zestawów kont kosztowych
            ||
               SRST.cntx_psh(); SRST.index('SROD'); SRST.prefix(SRST.SRSR);
               {? SRST.find_key(SRDO.ROK_F,SRDO.OKRO_F)
               || {? SRDO.RODZ='L'
                  || {? SRST.KONPOD<>SRDO.KONPOD | SRST.KONFIN<>SRDO.KONFIN | (FINFO.TOR_D='T' & SRST.KONDOD<>SRDO.KONDOD)
                     || _konto:=1
                     ?}
                  ?};
                  {! |?
                     {? SRST.DEKRET='T' || _dekret:=1 ?};
                     _dekret=0 & SRST.next()
                  !}
               ?};
               SRST.cntx_pop();
               {? _dekret=1 & (SRDO.RODZ='W' | (SRDO.RODZ='L' & _konto=1))
               ||  FUN.emsg('Amortyzacja w okresie obowiązywania dokumentu lub w późniejszym została już\n'
                            'zadekretowana dla co najmniej jednego toru amortyzacji. Usuwanie amortyzacji\n'
                            'lub wprowadzanie zmian mogących wpłynąć na amortyzację nie jest możliwe.'@);
                  _wy:='DO'
               ?}
            ?};
            {? _wy='' & SRST.ODD<>SRDO.ODD
::kontrola czy istnieją inne dokumenty, wystawione później niż wpisany okres obowiązywania
            || _tmppocz:=SRDO.OKRO_F().POCZ;
               SRDO.cntx_psh();
               SRDO.index('AUTO');
               SRDO.prefix(SRST.SRSR);
               {? SRDO.first()
               || {!
                  |? {? SRDO.OKRO_F().POCZ>_tmppocz
                     || _wy:='OKRO_F';
                         FUN.emsg('Nie można zmienić jednostki księgowej, gdy istnieją dokumenty w poźniejszych okresach.'@)
                     ?};
                     _wy='' & SRDO.next()
                  !}
               ?};
               SRDO.cntx_pop()
            ?}
         ?};
         _wy='' & _srst_sel.next()
      !}
   ?};
   SRST.cntx_pop()
?};
_wy


\ot_grup_przed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Grupa przed akcją dodawania OT grupowo
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('SRSRREFS2','TAB_OT');
SRSRREFS2:=obj_new(SRST.sel_size());
_tab:=TAB_OT:=SRST.sel_aget();
_i:=1;
_chck:=0;
{? _tab.first()
|| SRST.cntx_psh(); SRST.prefix();
   SRSR.cntx_psh(); SRSR.prefix();
   {! |?
      {? SRST.seek(_tab.REF)
      || SRSRREFS2[_i]:=SRST.SRSR;
         SRST.SRSR();
         {? ~SRSR.r_lock(1,1,1) || _chck:=7 ?};
         {? SRST.SRSR().DOKPRZ<>null
         || _chck:=1
         |? SRST.SRSR().ROK<>SSTALE.AO().RES
         || _chck:=2
         |? SRST.SRSR().GRP='T'
         || _chck:=4
         |? ~SRD.isDataCompleted()
         || _chck:=5
         |? SRST.SRSR().WARF=0 | SRST.SRSR().WARP=0 | (FINFO.TOR_D='T' & SRST.SRSR().WARD=0)
         || _chck:=6
         ?};
         _i+=1
      ?};
      _tab.next() & ~_chck
   !};
   SRST.cntx_pop(); SRSR.cntx_pop()
?};
SRDT.cntx_psh();
SRDT.index('SRDT');
SRDT.prefix('P');
{? ~SRDT.first()
|| FUN.info('W systemie brak definicji dokumentu przyjęcia.'@);
   _chck:=3
?};
SRDT.cntx_pop();
{? _chck || exec('ot_grup_po','grup_zm') ?};
{? ~_chck
|| 1
|? _chck=1
|| FUN.info('Co najmniej jeden z zaznaczonych środków ma już powiązany dokument OT'@);
   0
|? _chck=2
|| FUN.info('Co najmniej jeden z zaznaczonych środków pochodzi z innego roku.\n'@+
            'Dokument przyjęcia można wystawić tylko w roku wprowadzenia środka do ewidencji.'@);
   0
|? _chck=3
|| 0
|? _chck=4
|| FUN.info('Zaznaczo zestaw środków.\nW przypadku zestawów dokumenty przyjęcia należy wystawić dla poszczególnych '@+
            'elemenów składowych.'@);
   0
|? _chck=5
|| FUN.info('Co najmniej jeden środek wymaga uzupełnienia danych '
            '(dotyczących metody amortyzacji, grupy środka, kont kosztowych).\n'
            'Dołączanie dokumentów nie jest możliwe.'@);
            0
|? _chck=6
|| FUN.info('Brak wartości dla co najmniej jednego środka.\n'@+
            'Przed grupowym dodaniem dokumentów OT należy je uzupełnić.'@);
   0
|? _chck=7
|| FUN.info('Co najmniej jeden środek jest zablokowany przez innego użytkownika'@);
   0
?}


\ot_grup_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Grupa po akcji dodawania OT grupowo
::----------------------------------------------------------------------------------------------------------------------
SRST.cntx_psh(); SRSR.cntx_psh();
{? var_pres('TAB_OT')>0 & TAB_OT.first()
|| {! |?
      {? SRST.seek(TAB_OT.REF)
      || SRST.SRSR();
         SRSR.r_unlock()
      ?};
      TAB_OT.next()
   !}
?};
SRST.cntx_pop(); SRSR.cntx_pop();
exec('srst_rfresh','!fst_ewi_drda');
VAR_DEL.delete('SRSRREFS2','TAB_OT'); 1


\ot_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła dodająca grupowo dokumenty OT
::----------------------------------------------------------------------------------------------------------------------
{? SRST.sel_size()<=0 || FUN.info('Brak zaznaczonych rekordów'@); return(0) ?};
{? var_pres('SRSRREFS2')>0
|| SRSR.cntx_psh();
   SRSR.index('NRI'); SRSR.prefix(); SRSR.blank();
   _k:=1;
   _tcounter:=0;
   {! |?
      {? var_press('['+$_k+']',SRSRREFS2)>0
      || {? SRSR.seek(SRSRREFS2[_k])
         || _tcounter:=_k; _tcounter+=1;
            0
         || _k+=1;
            _k<=obj_len(SRSRREFS2)
         ?}
      || _k+=1;
         _k<=obj_len(SRSRREFS2)
      ?}
   !};
   SRDO.cntx_psh();
   SRDO.index('AUTO'); SRDO.prefix();
   _addot:=1;
   _rsrdo:=exec('dok_add','!fst_ewi_drda',0);
   {? _rsrdo<>null
   || {! _j:=_tcounter..obj_len(SRSRREFS2)
      |! {? var_press('['+$_j+']',SRSRREFS2)>0
         || {? SRDO.seek(_rsrdo) & SRSR.seek(SRSRREFS2[_j])
            || exec('przed_red_ot','fst',1);
               SRDO.NR:=exec('bl_srdo_nr','fst');
               SRDO.SYMBOL:=exec('symdok','fst');
               {? SRSR.GRP='E'
               || SRDO.SRSR_E:=SRSR.ref();
                  SRDO.SRSR:=exec('get_srsr_root','fst',SRSR.ref())
               || SRDO.SRSR:=SRSR.ref()
               ?};
               {? SRDO.add()
               || exec('po_red_ot','fst');
                  SRSR.DOKPRZ:=SRDO.ref();
                  SRSR.put();
                  _addot+=1
               ?}
            ?}
         ?};
         echo('Dodawanie dokumentów OT: %1 z %2'@[$_addot,$obj_len(SRSRREFS2)])
      !}
   ?};
   SRDO.cntx_pop();
   SRSR.cntx_pop();
   {? _rsrdo<>null
   || FUN.info('Liczba dodanych dokumentów OT:  %1'@[$_addot])
   ?};
   VAR_DEL.delete('SRSRREFS2')
?}


\lt_grup_przed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Grupa przed dla dokumentów wartościowych
::----------------------------------------------------------------------------------------------------------------------
SRDT.cntx_psh();
SRST.cntx_psh();
SRDT.index('SRDTD');
SRDT.prefix('W','T');
_chck:=0;
_ref:=null;
{? ~SRDT.first()
|| FUN.info('W systemie brak definicji dokumentów likwidacyjnych.'@);
   _chck:=1
?};
{? ~_chck
|| SRDT.prefix('W','T','-','LT');
   {? SRDT.first()
   || SRDT.win_sel('SELECT')
   || _chck:=2
   ?};
   {? _chck=2
   || SRDT.prefix('W','T','-');
      {? SRDT.first() || _chck:=0 ?}
   ?};
   {? ~_chck & SRDT.size()>1
   || {? SRDT.select()
      || {? FINFO.TOR_D='T' & ~(SRDT.WARD='T' | SRDT.UMOD='T')
         || FUN.emsg('Nie można dodać dokumentu wybranego typu (%1 - %2).\n'
                     'Wskazany typ dokumentu nie uwzględnia pól wartościowych związanych\n'
                     'z torem dodatkowym, który jest obsługiwany w systemie.'@[SRDT.TYP,SRDT.NAZWA]);
            _chck:=3;
            _ref:=null
         || _ref:=SRDT.ref()
         ?}
      || _chck:=3
      ?}
   |? ~_chck & SRDT.size()=1
   || {? FINFO.TOR_D='T' & ~(SRDT.WARD='T' | SRDT.UMOD='T')
      || FUN.emsg('Nie można dodać dokumentu wybranego typu (%1 - %2).\n'
                  'Wskazany typ dokumentu nie uwzględnia pól wartościowych związanych\n'
                  'z torem dodatkowym, który jest obsługiwany w systemie.'@[SRDT.TYP,SRDT.NAZWA]);
         _chck:=3;
         _ref:=null
      || _ref:=SRDT.ref()
      ?}
   ?}
?};

SRDO.cntx_psh(); SRDO.prefix();
SRDO.blank();
SRDO.Z:='T';
SRDO.TYP:=_ref;
SRDO.win_edit('RED_LT_G');
SRDO.RODZ:=SRDO.TYP().RODZ;
exec('set_field','fst');

VAR_DEL.delete('TAB_LT');
_tab:=TAB_LT:=SRST.sel_aget();
{? ~_chck
|| {? _tab.first()
   || SRST.prefix(); SRSR.cntx_psh(); SRSR.prefix();
      {! |?
         {? SRST.seek(_tab.REF)
         || SRST.SRSR();
            {? ~SRSR.r_lock(1,1,1) || _chck:=9 ?};
            {? ~_chck & SRSR.Z='T'
            || FUN.info('Co najmniej jeden z wybranych środków jest zlikwidowany.\n'
                        'Wprowadzanie zmian nie jest możliwe.'@);
               _chck:=1
            ?}
         ?};
         _tab.next() & ~_chck
      !};
      SRSR.cntx_pop()
   ?}
?};
{? ~_chck & SRDO.edit("exec('chck_lt','grup_zm')")
|| 1
|| {? ~_chck || _chck:=1 ?}
?};

{? ~_chck
|| {? _tab.first()
   || SRST.prefix(); SRSR.cntx_psh(); SRSR.prefix();
      {! |?
         {? SRST.seek(_tab.REF)
         || SRST.SRSR();
            {? 1+SRST.MF().T<>'N' & exec('field_test_nat','fst')
            || _chck:=5
            |? SRST.SRSR().GRP='T'
            || SRST.cntx_psh();
               SRST.index('SROD');
               SRST.prefix();
               {? SRST.find_key(SRSR.ref,SRDO.ROK_F,SRDO.OKRO_F)
               || {? SRD.isSetElementsActive()<>0
                  || _chck:=4
                  ?}
               ?};
               SRST.cntx_pop()
            |? FINFO.SPR_GRP='T' & SRST.SRSR().ORG_REF<>''
            || _chck:=6
            |? ~exec('nal_amor','grup_zm',SRST.SRSR)
            || _chck:=7
            |? ~SRD.isDataCompleted()
            || _chck:=8
            |? exec('b_chk_lt','grup_zm')<>''
            || _chck:=1
            ?}
         || _chck:=1
         ?};
         _tab.next() & ~_chck
      !};
      SRSR.cntx_pop()
   ?}
?};

SRDT.cntx_pop();
SRST.cntx_pop();
{? _chck || exec('lt_grup_po','grup_zm') ?};
{? ~_chck
|| 1
|? _chck=1
|| 0
|? _chck=2
|| FUN.emsg('W systemie nie zdefiniowano żadnego typu dokumentu skreślającego.\n'
               'Należy uzupełnić brakujące dane w obszarze Ustawienia i parametryzacja.'@);
   0
|? _chck=3
|| 0
|? _chck=4
|| FUN.info('Zaznaczo zestaw środków.\nW przypadku zestawów dokumenty przyjęcia należy wystawić dla poszczególnych '@+
            'elemenów składowych.'@);
   0
|? _chck=5
|| FUN.emsg('Nie można dodać dokumentu wybranego typu (%1 - %2).\n'
                     'Wskazany typ dokumentu dotyczy tylko pól wartościowych związanych\n'
                     'z metodą naturalną bilansową. Środek nie jest amortyzowany tą metodą.'@[SRDT.TYP,SRDT.NAZWA]);
   0
|? _chck=6
|| FUN.emsg('Dla środków pochodzących z innej firmy w grupie kapitałowej wystawienie '@+
            'dokumentów grupowych jest niemożliwe'@);
   0
|? _chck=7
|| FUN.emsg('Nie naliczono amortyzacji w okresie poprzedzającym okres obowiązywania dokumentów dla '@+
            'wszystkich środków'@);
   0
|? _chck=8
|| FUN.info('Przynajmniej jeden środek wymaga uzupełnienia danych '
            '(dotyczących metody amortyzacji, grupy środka, kont kosztowych).\n'
            'Dołączanie dokumentów nie jest możliwe.'@);
            0
|? _chck=9
|| FUN.info('Co najmniej jeden środek jest zablokowany przez innego użytkownika'@);
   0
?}


\chck_lt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Weryfikacja poprawności dokumentu wartościowego
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRDO,,'DW','DO');
SRSR.cntx_psh();
{? _wy=''
|| {? SRDO.OKRO_F=null
   || FUN.emsg('Nie uzupełnione pole Okres obowiązywania\n'
               '(pole uzupełni się automatycznie po redakcji pola Data obowiązywania).'@);
      _wy:='DO'
   ?}
?};
:: kontrola daty operacji
{? _wy='' & SRDO.OKRO_F<>null
|| {? exec('test_new_year','fst',SRDO.OKRO_F().ROK)
   || FUN.emsg('Wskazana data operacji pochodzi z nowo utworzonego roku w obszarze Środków trwałych.\n'
               'Nowy rok nie ma utworzonych danych w rejestrze stanów środków. Przed dołączaniem zapisów\n'
               'w nowym roku należy uruchomić Kartotekę środków w nowym roku, system wówczas uruchmomi\n'
               'mechanizm aktualizacji danych rejestru stanów.'@);
      _wy:='DO'
   ?}
?};
:: kontrola czy okres jest zamknięty
{? _wy='' & SRDO.DW<>date(0,0,0)
|| _okres:=exec('find_okro_f','fst', SRDO.DW);
   {? ~exec('okr_mod','fst',_okres)
   || _wy:='DW'
   ?}
?};

SRSR.cntx_pop();
_wy


\b_chk_lt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Weryfikacja poprawności dokumentu LT - sprawdzenie dla środków
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
:: kontrola miesiąca obowiązywania
{? _wy='' & ~exec('chk_dok_okres','fst') || _wy:='DO' ?};
:: kontrola daty wystawienia
{? _wy='' & exec('ae_srdo_dw','fst')=0 || _wy:='DW' ?};
_wy


\lt_grup_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Grupa po dla dokumentów wartościowych
::----------------------------------------------------------------------------------------------------------------------
SRST.cntx_psh(); SRSR.cntx_psh();
{? var_pres('TAB_LT')>0 & TAB_LT.first()
|| {! |?
      {? SRST.seek(TAB_LT.REF)
      || SRST.SRSR();
         SRSR.r_unlock()
      ?};
      TAB_LT.next()
   !}
?};
SRST.cntx_pop(); SRSR.cntx_pop();
VAR_DEL.delete('TAB_LT');
SRDO.cntx_pop(); exec('srst_rfresh','!fst_ewi_drdf'); 1


\lt_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodaje dokument wartościowy - akcja grupowa
::----------------------------------------------------------------------------------------------------------------------
{? SRST.sel_size()<=0 || FUN.info('Brak zaznaczonych rekordów'@); return(0) ?};
SRSR.cntx_psh(); SRSR.prefix();
SRST.SRSR();
_el:=0;
{? SRSR.GRP='E'
|| SRDO.SRSR_E:=SRSR.ref();
   SRDO.SRSR:=exec('get_srsr_root','fst',SRSR.ref())
|| SRDO.SRSR:=SRSR.ref()
?};
exec('set_field_wart','fst');
SRDO.ODD_DOK:=SRST.ODD;
SRDO.NR:=exec('bl_srdo_nr','fst');
SRDO.SYMBOL:=exec('symdok','fst');
{? ~(SRDO.DW<SRDO.OKRO_F().ROK().POCZ_ROK)
|| SRDO.ROK:=SRDO.OKRO_F().RES;
   SRDO.OKRES:=SRDO.OKRO_F().OES
?};
exec('mod_rec','fst');
{? SRST.SRSR().GRP<>'T'
|| {? SRDO.add(1)
   || _ok:=1;
      _pdm_f:=null;
      {? SRDO.Z='T' & (SRSR.MP().PDM='T' | SRSR.MF().PDM='T' | (FINFO.TOR_D='T' & SRSR.MD().PDM='T'))
      || _pdm_f:=exec('find_prev_okro','fst',SRDO.OKRO_F().ROK,SRDO.OKRO_F().NR)
      ?};
      {? _pdm_f
      || SRD.updateRecState(_pdm_f,1)
      || SRD.updateRecState(SRDO.OKRO_F)
      ?};
      {? SRDO.TYP().RODZ='W' & (1+SRDO.TYP().TYP='K' | SRDO.TYP().TYP='LT') & SRDO.DW>=SRST.OKRO_F().POCZ & SRDO.DW<=SRST.OKRO_F().KON
         & SRDO.OKRO_F().POCZ~2<>SRDO.DW~2
      || {? SRDO.TYP().TYP='LT' | (SRDO.TYP().TYP='K-' & fabs(SRDO.WARF)>SRST.NETF)
         || SRST.NETFK+=SRDO.WARF-SRDO.UMOF;
            SRST.NETPK+=SRDO.WARP-SRDO.UMOP;
            {? FINFO.TOR_D='T' || SRST.NETDK+=SRDO.WARD-SRDO.UMOD ?}
         || SRST.NETFK+=SRDO.WARF;
            SRST.NETPK+=SRDO.WARP;
            {? FINFO.TOR_D='T' || SRST.NETDK+=SRDO.WARD ?}
         ?};
         SRST.put()
       |? SRDO.TYP().RODZ='W' & (1+SRDO.TYP().TYP='K' | SRDO.TYP().TYP='LT') & SRDO.DW>=SRST.OKRO_F().POCZ & SRDO.DW<=SRST.OKRO_F().KON
          & SRDO.OKRO_F().POCZ~2=SRDO.DW~2
       || SRST.cntx_psh(); OKRO_F.cntx_psh();
          _okro:=exec('find_prev_okro','fst', SRST.OKRO_F().ROK, SRST.OKRO_F().NR);
          _pocz:=exec('FindAndGet','#table',OKRO_F,_okro,,"@.OKRO_F.POCZ",date(0,0,0));
          _srsr:=SRST.SRSR;
          SRST.index('POCZ');
          SRST.prefix(_srsr, _pocz);
          {? SRST.first()
          || {? SRDO.TYP().TYP='LT' | (SRDO.TYP().TYP='K-' & fabs(SRDO.WARF)>SRST.NETF)
             || SRST.NETFK+=SRDO.WARF-SRDO.UMOF;
                SRST.NETPK+=SRDO.WARP-SRDO.UMOP;
                {? FINFO.TOR_D='T' || SRST.NETDK+=SRDO.WARD-SRDO.UMOD ?}
             || SRST.NETFK+=SRDO.WARF;
                SRST.NETPK+=SRDO.WARP;
                {? FINFO.TOR_D='T' || SRST.NETDK+=SRDO.WARD ?}
             ?};
                SRST.put()
          ?};
          SRST.cntx_pop(); OKRO_F.cntx_pop()
      ?}
   ?}
|| SRDO.cntx_psh();
   SRDO.WARF:=SRDO.UMOF:=SRDO.WARP:=SRDO.UMOP:=SRDO.WARD:=SRDO.UMOD:=0;
   exec('mod_rec','fst');
   {? SRDO.add(1) || SRD.updateRecState(SRDO.OKRO_F,1,1) ?};
   SRDO.cntx_pop()
?};
{? SRD.isElement() || SRD.rootUpdate(SRSR.TREE, SRDO.OKRO_F,1) ?};
:: jeżeli element ostatni jest likwidowany to wystawia automatycznie dokument likwidacji dla zestawu
{? SRD.isElement() & SRDO.TYP().TYP='LT'
|| _srsr:=SRSR.TREE;
   SRSR.cntx_psh();
   {? SRSR.seek(_srsr,)
   || SRST.cntx_psh();
      SRST.index('SROD');
      SRST.prefix();
      {? SRST.find_key(SRSR.ref,SRDO.ROK_F,SRDO.OKRO_F)
      || {? SRD.isSetElementsActive()=0
         || SRDO.ODD_DOK:=SRST.ODD;
            SRDO.NR:=exec('bl_srdo_nr','fst');
            SRDO.SYMBOL:=exec('symdok','fst');
            SRDO.SRSR:=SRST.SRSR; SRDO.SRSR_E:=null();
            SRDO.WARF:=SRDO.UMOF:=SRDO.WARP:=SRDO.UMOP:=SRDO.WARD:=SRDO.UMOD;
: jeżeli zbycie do kolejnej firmy w grupie kapitałowej (nie z pierwotnej!) to dokument zachowuje się jak likwidujący
            {? SRDO.TYP().P='S' & SRSR.ORG_REF<>'' || SRDO.Z:='T' ?};
               exec('mod_rec','fst');
               SRDO.cntx_psh();
               SRDO.prefix();
               {? SRDO.add() || SRD.updateRecState(SRDO.OKRO_F,1,1) ?};
               SRDO.cntx_pop()
         ?}
      ?};
      SRST.cntx_pop()
   ?};
   SRSR.cntx_pop();
   {? _=0 | _a=0 || exec('srst_rfresh','!fst_ewi_drdf') ?}
?};
SRSR.cntx_pop()


\nal_amor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Sprawdzenie czy w poprzednim okresie naliczono amortyzację
::   WE: _a - SRSR.ref()
::----------------------------------------------------------------------------------------------------------------------
_ref:=_a;
_rok:=SRDO.OKRO_F().RES;
_okr:=SRDO.OKRO_F().OES;
_wyn:=1;
SRST.cntx_psh();
SRST.index('PODAT');
SRST.prefix(_ref);
{? SRST.find_key(_rok,_okr)
|| {! |?
      {? SRST.prev()
      || {? SRST.OKRO_F().POCZ<>date(0,0,0) || _dalej:=0 || _dalej:=1 ?}
      || _dalej:=0
      ?};
      _dalej
   !};
   {? SRST.OKRO_F().POCZ<>date(0,0,0) & SRST.NAL<>'T'
   || _wyn:=0
   ?}
?};
SRST.cntx_pop();
_wyn


\drdm_grupa_przed
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Grupa przed akcją dodawania dokumentu zmiany metody
::----------------------------------------------------------------------------------------------------------------------
SRDT.cntx_psh();
SRST.cntx_psh();
SRSR.cntx_psh();
SRDT.index('SRDT');
SRDT.prefix('M');
_chck:=0;

VAR_DEL.delete('TAB_DRDM');
_tab:=TAB_DRDM:=SRST.sel_aget();
{? ~_chck
|| {? _tab.first()
   || SRST.prefix(); SRSR.cntx_psh(); SRSR.prefix();
      {! |?
         {? SRST.seek(_tab.REF)
         || SRST.SRSR();
            {? ~SRSR.r_lock(1,1,1) || _chck:=6 ?};
            {? ~SRD.isDataCompleted()
            || _chck:=3
            |? SRD.isElement()
            || _chck:=4
            ?};
            {? ~_chck & SRSR.Z='T'
            || FUN.info('Co najmniej jeden z wybranych środków jest zlikwidowany.\n'
                        'Wprowadzanie zmian nie jest możliwe.'@);
               _chck:=1
            ?}
         ?};
         _tab.next() & ~_chck
      !};
      SRSR.cntx_pop()
   ?}
?};

{? ~_chck & SRDT.first()
|| SRDT.win_sel('SELECT');
   {? SRDT.select()
   || _ref:=SRDT.ref()
   || _chck:=1
   ?}
|? ~SRDT.first()
|| _chck:=2
?};

SRDO.cntx_psh(); SRDO.prefix();
{? ~_chck & _ref<>null
|| SRDO.blank();
   SRDO.TYP:=_ref;
   SRDO.ODD_DOK:=SRST.ODD;
   SRDO.RODZ:=SRDO.TYP().RODZ;
   SRDO.WKP:=1;
   SRDO.WKF:=1;
   {? FINFO.TOR_D='T' || SRDO.WKD:=1 ?};
   {? EDIT_ES.NISKO='T'
   || SRDO.win_edit('RED_M_GR')
   || SRDO.win_edit('RED_M_G2')
   ?};

   exec('set_field_drdm','fst');
   {? SRDO.edit("exec('chk_drdm','grup_zm')")
   || exec('mod_rec','fst')
   || _chck:=1
   ?}
?};
VAR_DEL.delete('drdmwart');
drdmwart:=obj_new('MP','STAP','WKP','DPLP','MF','STAF','WKF','DPLF','MD','STAD','WKD','DPLD');
{? ~_chck
|| {? SRDO.TYP().MP='T' & SRDO.MP=null || drdmwart.MP:=1 || drdmwart.MP:=0 ?};
   {? SRDO.TYP().STAP='T' & SRDO.STAP=0 || drdmwart.STAP:=1 || drdmwart.STAP:=0 ?};
   {? SRDO.TYP().WKP='T' & SRDO.WKP=0 || drdmwart.WKP:=1 || drdmwart.WKP:=0 ?};
   {? SRDO.TYP().MP='T' & SRDO.DPLP=date(0,0,0) || drdmwart.DPLP:=1 || drdmwart.DPLP:=0 ?};

   {? SRDO.TYP().MF='T' & SRDO.MF=null || drdmwart.MF:=1 || drdmwart.MF:=0 ?};
   {? SRDO.TYP().STAF='T' & SRDO.STAF=0 || drdmwart.STAF:=1 || drdmwart.STAF:=0 ?};
   {? SRDO.TYP().WKF='T' & SRDO.WKF=0 || drdmwart.WKF:=1 || drdmwart.WKF:=0 ?};
   {? SRDO.TYP().MF='T' & SRDO.DPLF=date(0,0,0) || drdmwart.DPLF:=1 || drdmwart.DPLF:=0 ?};

   {? FINFO.TOR_D='T'
   || {? SRDO.TYP().MD='T' & SRDO.MD=null || drdmwart.MD:=1 || drdmwart.MD:=0 ?};
      {? SRDO.TYP().STAD='T' & SRDO.STAD=0 || drdmwart.STAD:=1 || drdmwart.STAD:=0 ?};
      {? SRDO.TYP().WKD='T' & SRDO.WKD=0 || drdmwart.WKD:=1 || drdmwart.WKD:=0 ?};
      {? SRDO.TYP().MD='T' & SRDO.DPLD=date(0,0,0) || drdmwart.DPLD:=1 || drdmwart.DPLD:=0 ?}
   ?}
?};

{? ~_chck
|| {? _tab.first()
   || SRSR.prefix();
      {! |?
         {? SRST.seek(_tab.REF)
         || SRST.SRSR();
            {? FINFO.SPR_GRP='T' & SRSR.ORG_REF<>''
            || _chck:=5
            |? exec('b_chk_drdm','grup_zm')<>''
            || _chck:=1
            ?}
         || _chck:=1
         ?};
         _tab.next() & ~_chck
      !}
   || _chck:=1
   ?}
?};

SRST.cntx_pop();
SRSR.cntx_pop();
SRDT.cntx_pop();

{? _chck || exec('drdm_grupa_po','grup_zm') ?};
{? ~_chck
|| 1
|? _chck=1
|| 0
|? _chck=2
|| FUN.emsg('W systemie nie zdefiniowano żadnego typu dokumentu zmiany metody amortyzacji.\n'
               'Należy uzupełnić brakujące dane w obszarze Ustawienia i parametryzacja.'@); 0
|? _chck=3
|| FUN.emsg('Co najmniej jeden środek wymaga uzupełnienia danych.\n'
            'Dołączanie i modyfikacja dokumentów nie jest możliwa.'@); 0
|? _chck=4
|| FUN.emsg('Zaznaczono element składowy zestawu.\n'
            'Nie można dodawać i modyfikować dokumentów zmiany metody dla elementów składowych zestawu.\n'
            'Dane tego typu są wspólne dla zestawu dlatego taki dokument należy dodawać na poziomie zestawu.'@); 0
|? _chck=5
|| FUN.emsg('Dla środków pochodzących z innej firmy w grupie kapitałowej wystawienie '@+
            'dokumentów grupowych jest niemożliwe'@);
   0
|? _chck=6
|| FUN.info('Co najmniej jeden środek jest zablokowany przez innego użytkownika'@);
   0
?}


\b_chk_drdm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła pomocnicza sprawdzająca poprawność dokumentu zmiany metody
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
:: kontrola miesiąca obowiązywania
{? _wy='' & ~exec('chk_dok_okres','fst') || _wy:='DO' ?};
:: kontrola daty wystawienia
{? _wy='' & exec('ae_srdo_dw','fst')=0 || _wy:='DW' ?};
:: kontrola zmiany metody
{? _wy='' & SRDO.TYP().MF='T' & 1+SRST.MF().T='N' & 1+SRDO.MF().T<>'N'
|| FUN.emsg('Nie można zmienić metody naturalnej na inną w trakcie amortyzowania środka.'@);
   _wy:='MF'
?};
{? _wy='' & SRDO.TYP().MP='T' & 1+SRST.MP().T='L' & 1+SRDO.MP().T='D'
|| FUN.emsg('Nie można zmienić metody liniowej na degresywną w trakcie amortyzowania środka.'@);
   _wy:='MP'
?};
{? _wy='' & SRDO.TYP().MF='T' & 1+SRST.MF().T='L' & 1+SRDO.MF().T='D'
|| FUN.emsg('Nie można zmienić metody liniowej na degresywną w trakcie amortyzowania środka.'@);
   _wy:='MF'
?};
{? FINFO.TOR_D='T'
|| {? _wy='' & SRDO.TYP().MD='T' & 1+SRST.MD().T='L' & 1+SRDO.MD().T='D'
   || FUN.emsg('Nie można zmienić metody liniowej na degresywną w trakcie amortyzowania środka.'@);
      _wy:='MD'
   ?}
?};
:: kontrola czy zmiana nie dotyczy zadekretowanej amortyzacji
{? _wy=''
|| _dekret:=0;
   SRST.cntx_psh();
   SRST.index('SROD');
   SRST.prefix(SRSR.ref());
   {? SRST.find_key(SRDO.ROK_F,SRDO.OKRO_F)
   || {! |?
         {? SRST.DEKRET='T' || _dekret:=1 ?};
         _dekret=0 & SRST.next()
      !}
   ?};
   SRST.cntx_pop();
   {? _dekret=1
   ||  FUN.emsg('Amortyzacja w okresie obowiązywania dokumentu lub w późniejszym została już\n'
                'zadekretowana dla co najmniej jednego toru amortyzacji. Usuwanie amortyzacji\n'
                'lub wprowadzanie zmian mogących wpłynąć na amortyzację nie jest możliwe.'@);
      _wy:='DO'
   ?}
?};
_wy


\chk_drdm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła sprawdzająca uzupełnienie pól
::----------------------------------------------------------------------------------------------------------------------
_wy:=__CHK.record(SRDO,,'DW','DO');
{? _wy=''
|| {? SRDO.OKRO_F=null
   || FUN.emsg('Nie uzupełnione pole Okres obowiązywania\n'
               '(pole uzupełni się automatycznie po redakcji pola Data obowiązywania).'@);
      _wy:='DO'
   ?}
?};
:: kontrola daty operacji
{? _wy='' & SRDO.OKRO_F<>null
|| {? exec('test_new_year','fst',SRDO.OKRO_F().ROK)
   || FUN.emsg('Wskazana data operacji pochodzi z nowo utworzonego roku w obszarze Środków trwałych.\n'
               'Nowy rok nie ma utworzonych danych w rejestrze stanów środków. Przed dołączaniem zapisów\n'
               'w nowym roku należy uruchomić Kartotekę środków w nowym roku, system wówczas uruchmomi\n'
               'mechanizm aktualizacji danych rejestru stanów.'@);
      _wy:='DO'
   ?}
?};
:: kontrola czy okres jest zamknięty
{? _wy='' & SRDO.DW<>date(0,0,0)
|| _okres:=exec('find_okro_f','fst', SRDO.DW);
   {? ~exec('okr_mod','fst',_okres)
   || _wy:='DW'
   ?}
?};

:: kontrola zmiany metody
{? _wy='' & SRDO.TYP().MF='T' & 1+SRDO.MF().T='N'
|| FUN.emsg('Nie można zmienić metody na naturalną w trakcie amortyzowania środka.\n'
            'Metodę naturalną bilansową można wybrać dla nowo wprowadzonego środka.'@);
   _wy:='MF'
?};
{? _wy='' & SRDO.TYP().STAP='T' & SRDO.STAP>100
|| FUN.emsg('Stawka podatkowa amortyzacji nie może być większa niż 100%%.'@);
   _wy:='STAP'
?};
{? _wy='' & SRDO.TYP().STAP='T' & SRDO.STAP<0
|| FUN.emsg('Stawka podatkowa amortyzacji nie może być mniejsza niż 0%%.'@);
   _wy:='STAP'
?};
{? _wy='' & SRDO.TYP().STAF='T' & SRDO.STAF>100
|| FUN.emsg('Stawka bilansowa amortyzacji nie może być większa niż 100%%.'@);
   _wy:='STAF'
?};
{? _wy='' & SRDO.TYP().STAF='T' & SRDO.STAF<0
|| FUN.emsg('Stawka bilansowa amortyzacji nie może być mniejsza niż 0%%.'@);
   _wy:='STAF'
?};
{? FINFO.TOR_D='T'
|| {? _wy='' & SRDO.TYP().STAD='T' & SRDO.STAD>100
   || FUN.emsg('Stawka dodatkowa amortyzacji nie może być większa niż 100%%.'@);
      _wy:='STAD'
   ?};
   {? _wy='' & SRDO.TYP().STAD='T' & SRDO.STAD<0
   || FUN.emsg('Stawka dodatkowa amortyzacji nie może być mniejsza niż 0%%.'@);
      _wy:='STAD'
   ?}
?};
_wy


\drdm_grupa_po
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Grupa po akcji dodawania dokumentu zmiany metody
::----------------------------------------------------------------------------------------------------------------------
SRST.cntx_psh(); SRSR.cntx_psh();
{? var_pres('TAB_DRDM')>0 & TAB_DRDM.first()
|| {! |?
      {? SRST.seek(TAB_DRDM.REF)
      || SRST.SRSR();
         SRSR.r_unlock()
      ?};
      TAB_DRDM.next()
   !}
?};
SRST.cntx_pop(); SRSR.cntx_pop();
SRDO.cntx_pop(); VAR_DEL.delete('drdmwart','TAB_DRDM'); 1


\drdm_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Akcja grupowego dodawania dokumentu zmiany metody
::----------------------------------------------------------------------------------------------------------------------
{? SRST.sel_size()<=0 || FUN.info('Brak zaznaczonych rekordów'@); return(0) ?};
SRSR.cntx_psh();
SRST.SRSR();
SRDO.SRSR:=SRSR.ref();
SRDO.NR:=exec('bl_srdo_nr','fst');
SRDO.SYMBOL:=exec('symdok','fst');
SRDO_POM.blank();
{? SRDO.TYP().MP='T' & drdmwart.MP || SRDO_POM.MP:=SRDO.MP:=SRST.MP ?};
{? SRDO.TYP().STAP='T' & drdmwart.STAP || SRDO_POM.STAP:=SRDO.STAP:=SRST.STAP ?};
{? SRDO.TYP().WKP='T' & drdmwart.WKP || SRDO_POM.WKP:=SRDO.WKP:=SRST.WKP ?};
{? SRDO.TYP().MP='T' & drdmwart.DPLP || SRDO_POM.DPLP:=SRDO.DPLP:=SRST.DPLP ?};

{? SRDO.TYP().MF='T' & drdmwart.MF || SRDO_POM.MB:=SRDO.MF:=SRST.MF ?};
{? SRDO.TYP().STAF='T' & drdmwart.STAF || SRDO_POM.STAB:=SRDO.STAF:=SRST.STAF ?};
{? SRDO.TYP().WKF='T' & drdmwart.WKF || SRDO_POM.WKF:=SRDO.WKF:=SRST.WKF ?};
{? SRDO.TYP().MF='T' & drdmwart.DPLF || SRDO_POM.DPLF:=SRDO.DPLF:=SRST.DPLF ?};

{? FINFO.TOR_D='T'
|| {? SRDO.TYP().MD='T' & drdmwart.MD || SRDO_POM.MD:=SRDO.MD:=SRST.MD ?};
   {? SRDO.TYP().STAD='T' & drdmwart.STAD || SRDO_POM.STAD:=SRDO.STAD:=SRST.STAD ?};
   {? SRDO.TYP().WKD='T' & drdmwart.WKD || SRDO_POM.WKD:=SRDO.WKD:=SRST.WKD ?};
   {? SRDO.TYP().MD='T' & drdmwart.DPLD || SRDO_POM.DPLD:=SRDO.DPLD:=SRST.DPLD ?}
?};
{? SRST.R='N' & 1+SRDO.MP().T='D'
|| SRDO.MP:=null
?};
{? SRST.R='N' & (1+SRDO.MF().T='D' | 1+SRDO.MF().T='N')
|| SRDO.MF:=null
?};
{? FINFO.TOR_D='T'
|| {? SRST.R='N' & 1+SRDO.MD().T='D'
   || SRDO.MD:=null
   ?}
?};
{? EDIT_ES.R='N' || EDIT_ES.NISKO:='T' || EDIT_ES.NISKO:='' ?};
{? SRDO.add()
|| SRD.updateRecState(SRDO.OKRO_F);
:: jeżeli zestaw to odświeżenie danych elementów
   {? SRD.isSet() || SRD.updateElements() ?}
?};
SRSR.cntx_pop()

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 7a8904bf221608044410e49938b42f288b11cd8f888fa397ecb366291e3756c726bfe41d201ad712219f277d3edd5ce3c8ff43c43ce548916b1a88e03316afebdbece01d16b259b33f561453a87658cecf719e6ef6858b8a1616a544ba9b5d09bec9ac7a93fa81e36ab49ae2a6ef0b10d1066cc30ba2b6fcb7a5bbd79fa9b5f2

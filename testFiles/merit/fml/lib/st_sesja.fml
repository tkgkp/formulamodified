:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_sesja.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa sesji dla systemu Statystyki
::======================================================================================================================


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Czyści powiązania do rekordu tabeli ST_SESJA
::   WE: _a - ST_SESJA.ref()
::   WY: >0  -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PRYWATNA><CLEAN>
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---
ST_SESJA.cntx_psh(); ST_SESJA.clear();
{? ST_SESJA.seek(_ref)
||
   {? _can_continue>0
   ||
::    1. Usuwam ST_SESTA które wskazują na ten element
      ST_SESTA.cntx_psh();
      ST_SESTA.index('ST_SESJA');
      ST_SESTA.prefix(ST_SESJA.ref());
      {? ST_SESTA.first()
      || {!
         |? _can_continue:=exec('delete','st_sesta',ST_SESTA.ref());
            ST_SESTA.first() & _can_continue>0
         !}
      ?};
      ST_SESTA.cntx_pop()
   ?};
   ~~
?};
ST_SESJA.cntx_pop();
:: --- wszystkie powiazania usuniete? ---

{? _can_continue>0
|| _result:=1
|| undo()
?};

{? _mydo || end() ?};

_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Kasuje podany rekord tabeli ST_SESJA (wykonywane w transakcji!!!)
::   WE: _a - ST_SESJA.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_SESJA.cntx_psh(); ST_SESJA.clear();
{? ST_SESJA.seek(_ref)
|| {? exec('clean','st_sesja',_ref)>0
   || {? ST_SESJA.del(,1)>0
      || _result:=1
      || undo();
         _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};

{? _result<0
|| undo()
?};

ST_SESJA.cntx_pop();
{? _mydo || end() ?};
_result


\erase
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Czyści wszystkie sesje
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

_can_continue:=1;
_result:=0;

_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_SESJA.cntx_psh();
ST_SESJA.index('PID');
ST_SESJA.prefix();
{? ST_SESJA.first()
|| {!
   |? _can_continue:=exec('delete','st_sesja',ST_SESJA.ref());
      ST_SESJA.first() & _can_continue>0
   !}
?};
ST_SESJA.cntx_pop();

{? _can_continue>0
|| _can_continue:=exec('erase','st_sesj2')
?};

{? _can_continue>0
|| _result:=1
|| undo()
?};
{? _mydo || end() ?};
_result

:Sign Version 2.0 jowisz:1048 2021/04/09 15:26:32 61b87545981605a39598622e252ed94e555002e22cd9e5564fa7c15bc3e0b59ca7cae332efdf6d9574076e778500ec07013434459993e6aa98ebac20e4198898da6cb308c5dd806562a951be276ce8cc3552196e01278de1d25d42ce0b9a372effb239f50701b8197617184914ce4a49019eec3c6bfbbfe8071c7c00e2dfc283

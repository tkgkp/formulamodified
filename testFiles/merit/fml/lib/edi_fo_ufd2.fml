:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: edi_fo_ufd2.fml
:: Utworzony: 25.07.2023
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość:  Formuły definicji komunikatów EDI dotyczących formatu UFD 2.0
::======================================================================================================================


\order_end
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Obsługa sekcji Order
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: połączenie pozycji
_gln:=0;
_ln:=0;
_loop:=__OL.first();
{!
|? _loop
|!
   {? _gln<>__OL.GLN
   ||
      _gln:=__OL.GLN;
      _pq:=__OL.Q;
      _punp:=__OL.UNP;
      _pna:=__OL.NA;
      _pta:=__OL.TA;
      _ptr:=__OL.TR
   ||
      _ln+=1;
      __OL.LN:=_ln;
      __OL.PQ:=-_pq;
      __OL.PUNP:=_punp;
      __OL.PNA:=-_pna;
      __OL.PTA:=-_pta;
      __OL.PTR:=_ptr;
      {? __OL.put() || __OL.prev(); __OL.del() ?}
   ?};
   _loop:=__OL.next()
!};
:: aktualizacja pozycji
_loop:=__OL.first();
{!
|? _loop
|!
   exec('fap_start','edi_fo_ufd');
   exec('kor_start','edi_fo_ufd',__OL.LN);
   FAP.POZ:=__OL.LN;
   __XML.VAL:=__OL.IC; exec('SupplierItemCode','edi_fo_ufd');
   __XML.VAL:=__OL.ID; exec('ItemDescription','edi_fo_ufd');
   __P.JM:=__OL.IU;
   FAP.IL:=__OL.Q;
   __P.SV_P:=__OL.TR;
   __P.PRICEA:=__OL.UNP;
   __P.WV:=__OL.TA;
   __P.WN:=__OL.NA;
   __P.PREV_WWAL:=__OL.PNA;
   __P.PREV_BWAL:=__OL.PNA+__OL.PTA;
   FAP.SP_WYM:={? __OL.SP='1' || 'T' || 'N' ?};
   exec('fap_end','edi_fo_ufd');
   _loop:=__OL.next()
!}


\OtherParties
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [RR.XX]
:: OPIS: Obsługa sekcji OtherParties
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

FAKSKH.cntx_psh();
{? exec('verify','edi_xml')
||
   _loop:=__OtherParties.first();
   {? _loop
   ||
      {!
      |? _loop
      |!
         _nip:=__OtherParties.TAXID;
         _idwew:=__OtherParties.INTERID;
         _rola:=ROLA:=exec('rola_podmiotu_ksef','edi_fo_ksef','',2,__OtherParties.ROLE).OPISROLI;
         _wyst_czy:=(_rola=exec('rola_wyst','edi_fo_ksef') | _rola=exec('rola_cz_grvat_wyst','edi_fo_ksef')
                    | _rola=exec('rola_jst_wyst','edi_fo_ksef'));
         {? _nip='' & ~_wyst_czy
         || exec('add_kom','#message','Nie znaleziono kontrahenta - nie podano NIP.'@,4,FAKS.ID)
         |? _nip='' & _idwew=''
         || exec('add_kom','#message','Nie znaleziono kontrahenta - nie podano NIP ani identyfikatora.'@,4,FAKS.ID)
         || _wyst_szuk:=exec('wystawca_szuk','edi_fo_ufd2',FAKS.KH,_nip,_idwew);
            {? (_wyst_czy & _wyst_szuk.KH_ODB=null() & _wyst_szuk.KH_LNK=null())
            | (~_wyst_czy & _wyst_szuk.KH_LNK=null())
            || _result:=0;
               _msg:='';
               {? _nip<>'' & _idwew<>''
               || _msg:='Nie znaleziono kontrahenta - NIP %1, identyfikator %2.'@[_nip,_idwew]
               |? _nip<>''
               || _msg:='Nie znaleziono kontrahenta - NIP %1.'@[_nip]
               |? _idwew<>''
               || _msg:='Nie znaleziono kontrahenta - identyfikator %1.'@[_idwew]
               ?};
               {? _msg<>''
               || exec('add_kom','#message',_msg,4,FAKS.ID)
               ?}
            ?};
            &_wyst_szuk
         ?};
         _loop:=__OtherParties.next()
      !}
   ?}
||
   FAKSKH.prefix();
   _loop:=__OtherParties.first();
   {!
   |? _loop
   |!
      _nip:=__OtherParties.TAXID;
      _idwew:=__OtherParties.INTERID;
      _rola:=ROLA:=exec('rola_podmiotu_ksef','edi_fo_ksef','',2,__OtherParties.ROLE).OPISROLI;
      _wyst_czy:=(_rola=exec('rola_wyst','edi_fo_ksef') | _rola=exec('rola_cz_grvat_wyst','edi_fo_ksef')
                 | _rola=exec('rola_jst_wyst','edi_fo_ksef'));
      _wyst_szuk:=exec('wystawca_szuk','edi_fo_ufd2',FAKS.KH,_nip,_idwew);
      {? _wyst_czy & _wyst_szuk.KH_ODB<>null()
      || FAKS.KH_ODB:=_wyst_szuk.KH_ODB;
         FAKS.put()
      ?};
      {? _wyst_szuk.KH_LNK<>null()
      || FAKSKH.blank();
         FAKSKH.FAKS:=FAKS.ref();
         FAKSKH.KH:=_wyst_szuk.KH_LNK;
         {? __OtherParties.OTR_ROLE='1'
         || FAKSKH.ROLA:=__OtherParties.ROLEDESC
         || FAKSKH.ROLA:=_rola
         ?};
         exec('duplikuj_dane_fakskh','faktury_wspolne');
         FAKSKH.UDZIAL:=__OtherParties.SHARE;
         FAKSKH.add()
      ?};
      &_wyst_szuk;
      _loop:=__OtherParties.next()
   !}
?};
FAKSKH.cntx_pop();

_result


\wystawca_szuk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [RR.XX]
:: OPIS: Wyszukanie wystawcy dokumentu wg NIP lub identyfikatora wewnętrznego
::   WE: _a - kontrahent dokumentu
::       _b - NIP
::       _c - identyfikator wewnętrzny
::   WY: obj_new(KH_ODB,KH_LNK)
::----------------------------------------------------------------------------------------------------------------------
_kh0:=_a;
_nip:=exec('niptostr','#string',_b);
_idwew:=_c;
_wyn:=obj_new('KH_ODB','KH_LNK');
_wyn.KH_ODB:=null();
_wyn.KH_LNK:=null();

KH_ODB.cntx_psh();
KH_ODB.index('KH_ODB');
KH_ODB.prefix(_kh0);
{? KH_ODB.first()
|| {!
   |? {? (exec('upgrade2325_jst01','zbl') & _nip<>'' & _idwew<>'' & KH_ODB.ID_WEW=_idwew & KH_ODB.SNIP=_nip)
         | (exec('upgrade2325_jst01','zbl') & _nip='' & _idwew<>'' & KH_ODB.ID_WEW=_idwew)
         | (_nip<>''& (~exec('upgrade2325_jst01','zbl') | _idwew='') & KH_ODB.SNIP=_nip)
      || {? _wyn.KH_ODB=null() | (_wyn.KH_ODB<>null() & _wyn.KH_LNK=null() & KH_ODB.KH_LNK()<>null())
         || _wyn.KH_ODB:=KH_ODB.ref();
            _wyn.KH_LNK:=KH_ODB.KH_LNK
         ?}
      ?};
      _wyn.KH_LNK=null() & KH_ODB.next()
   !}
?};
KH_ODB.cntx_pop();

{? _wyn.KH_ODB=null()
|| KH.cntx_psh();
   {? _nip<>''
   || KH.index('SNIP');
      KH.prefix(2,_nip);
      {? KH.first()
      || {!
         |? {? ~exec('upgrade2325_jst01','zbl') | _idwew='' | KH.ID_WEW=_idwew
            || _wyn.KH_LNK:=KH.ref()
            ?};
            _wyn.KH_LNK=null() & KH.next()
         !};
         {? _wyn.KH_LNK=null()
         || KH.first();
           _wyn.KH_LNK:=KH.ref()
         ?}
      ?}
   ?};
   {? exec('upgrade2325_jst01','zbl') & _wyn.KH_LNK=null() & _idwew<>''
   || KH.index('ID_WEW');
      KH.prefix(2,_idwew);
      {? KH.first()
      || {!
         |? {? _nip='' | KH.SNIP=_nip
            || _wyn.KH_LNK:=KH.ref()
            ?};
            _wyn.KH_LNK=null() & KH.next()
         !};
         {? _wyn.KH_LNK=null()
         || KH.first();
            _wyn.KH_LNK:=KH.ref()
         ?}
      ?}
   ?};
   KH.cntx_pop()
?};

_wyn

:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 c2b378ca33b4679b552b1e59cebc1289dda95ae210422ebb6f302a40eb56982fe6a358b6b3399b5f39221fcdc002490df5da315906069f9de32d12d693757219fb26dee52d62b71559e4b29945e7d0df8895076b1e7fa4b2f7402c17379e5bd166fa804487ad4c71f943647a4882fef376700904cb3f81dbb7d6995f13b6653f

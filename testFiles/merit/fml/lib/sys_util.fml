:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: sys_util.fml
:: Utworzony: 31.07.2002
:: Autor: AMK
:: Systemy: FIKS
::======================================================================================================================
:: Zawartosc:  Procedury do sprawdzania kartotek po awarii systemu
::======================================================================================================================


\sys_util
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Glowna formula do odtwarzania
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO);
exec('czytaj','#stalesys',,FINFO);
ROZNE.win_edit('SYS_UTIL');
ROZNE.UT_DOK:=ROZNE.UT_KOR:=ROZNE.UT_SP:=0;
ROZNE.UT_KS:=ROZNE.UT_OBR:=ROZNE.UT_ROZR:=ROZNE.UT_VAT:=ROZNE.UT_KOM:=ROZNE.UT_DOKTM:=1;
ROZNE.UT_ROZ1:=ROZNE.UT_ROZ2:=ROZNE.UT_BUDAN:=0;
exec('szuk_okr','okresy',date());
{? ROZNE.UT_OKROD
|| ROZNE.UT_ROKOD:=ROZNE.UT_ROKDO:=ROZNE.UT_OKROD().ROK;
   ROZNE.UT_OKRDO:=ROZNE.UT_OKROD
|| ROZNE.UT_ROKOD:=ROZNE.UT_ROKDO:=null;
   ROZNE.UT_OKROD:=ROZNE.UT_OKRDO:=null
?};
OKRO_F.win_dict('SLO');
{? ROZNE.edit("exec('spr_par','sys_util')")
|| numer_bl:=sys_util:=0; ind_tym:=ind_tym1:=ind_tym2:=''; bl_znacz:=0;
   exec('tab_tmp','sys_util');
   {? ROZNE.UT_ZN
   || exec('ut_dok','sys_util',ROZNE.UT_DOK,ROZNE.UT_KS)
   ?};
   {? ROZNE.UT_OBR || exec('ut_obr','sys_util'); exec('ut_obr1','sys_util') ?};
   {? ROZNE.UT_VAT || exec('ut_vat','sys_util') ?};
   {? ROZNE.UT_KOM || exec('ut_kom','sys_util') ?};
   {? ROZNE.UT_ROZR|| exec('ut_roz','sys_util') ?};
   {? ROZNE.UT_KOR || exec('ut_kor','sys_util') ?};
   {? ROZNE.UT_BUDAN || exec('ut_budan','sys_util') ?};
   {? ROZNE.UT_SP || exec('ut_sp','sys_util') ?};
   {? ROZNE.UT_DOKTM || exec('ut_tmp','sys_util') ?};
   echo();
   {? bl_znacz
   || FUN.info('Wykryto nieprawidłowe znaczniki'+
               '\n(akceptacji, księgowania próbnego)'+
               '\nw przynajmniej jednym sprawdzanym dokumencie'+
               '\nPo wykonaniu operacji odtwarzania usterek należy skontrolować'+
               '\ni w razie potrzeby odtworzyć obroty na kontach.'@)
   ?};
   {? SYS_UTIL.size()=0
   || FUN.info('Nie wykryto żadnych usterek.'@)
   |? SYS_UTIL.index(ind_tym3); SYS_UTIL.prefix('I');
      SYS_UTIL.hdr_sel('Lista wykrytych usterek - wszystkie istotne'@); SYS_UTIL.select()
   || exec('sys_odtw','sys_odtw')
   ?};
   VAR_DEL.delete('SYS_UTIL','numer_bl','ind_tym','ind_tym1','ind_tym2','ind_tym3','sys_util','bl_znacz')
?};
''


\spr_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie parametrow w okienku SYS_UTIL zmiennej ROZNE
::----------------------------------------------------------------------------------------------------------------------
{? ~ROZNE.UT_DOK & ~ROZNE.UT_KS & ~ROZNE.UT_OBR & ~ROZNE.UT_ROZR &
   ~ROZNE.UT_VAT & ~ROZNE.UT_KOM  & ~ROZNE.UT_KOR & ~ROZNE.UT_BUDAN & ~ROZNE.UT_DOKTM & ~ROZNE.UT_SP
|| FUN.info('Nie wybrano żadnego obszaru do odtwarzania.'@); 'UT_DOK'
|? ~ROZNE.UT_ROKOD | ~ROZNE.UT_ROKDO | ~ROZNE.UT_OKROD | ~ROZNE.UT_OKRDO |
   ~(OKRO_F.index('ROK'); OKRO_F.prefix(ROZNE.UT_ROKOD); OKRO_F.seek(ROZNE.UT_OKROD)) |
   ~(OKRO_F.prefix(ROZNE.UT_ROKDO); OKRO_F.seek(ROZNE.UT_OKRDO)) |
  ROZNE.UT_ROKOD().POCZ_ROK>ROZNE.UT_ROKDO().POCZ_ROK |
  (ROZNE.UT_ROKOD=ROZNE.UT_ROKDO & ROZNE.UT_OKROD().NR>ROZNE.UT_OKRDO().NR)
|| FUN.info('Nie wprowadzono poprawnie przedziału okresów.'@); 'UT_ROKOD'
|? ~exec('spr_par1','sys_util')
|| FUN.info('W podanym przedziale nie ma otwartych lat bilansowych.'@); 'UT_ROKOD'
|| 1
?}


\spr_par1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie parametrow w okienku SYS_UTIL zmiennej ROZNE
::   WY: 0 - nie, 1 - tak
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {! |?
      {? -ROK_F.ZAM='n' || _zwrot:=1 ?};
      ~_zwrot & ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next()
  !}
?}; _zwrot


\tab_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Stworzenie tabeli tymczasowej, okienka
::----------------------------------------------------------------------------------------------------------------------
SYS_UTIL:=tab_tmp(1,'LP','INTEGER','Lp.',
                    'WAGA','STRING[1]','Waga',
                    'AUT_NAP','STRING[1]','A',
                    'TP_USTER','INTEGER','Typ usterki',
                    'OPIS','STRING[255]','Opis',
                    'SZCZEG','STRING[255]','Szczegóły',
                    'L1','STRING[35]','Kod roku',
                    'L2','STRING[35]','L2',
                    'L3','STRING[50]','L3',
                    'L4','STRING[35]','L4',
                    'L5','STRING[50]','L5',
                    'L6','INTEGER','Numer okresu',
                    'L7','INTEGER','L7',
                    'L8','INTEGER','L8',
                    'L9','REAL','L9',
                    'L10','REAL','L10',
                    'L11','STRING[35]','L11',
                    'L12','STRING[35]','L12',
                    'L13','DATE','L13',
                    'L14','DATE','L14',
                    'L15','STRING[35]','L15',
                    'L16','STRING[55]','L16'
                 );
ind_tym:=SYS_UTIL.index('?');
ind_tym1:=SYS_UTIL.ndx_tmp('',1,'AUT_NAP',,0,'WAGA',,0,'LP',,0);
ind_tym2:=SYS_UTIL.ndx_tmp('',1,'AUT_NAP',,0,'L1',,0,'L6',,0,'LP',,0);
ind_tym3:=SYS_UTIL.ndx_tmp('',1,'WAGA',,0,'LP',,0);
_wer:=SYS_UTIL.mk_sel(,,,'sys_util_wer',,,,,'U');
SYS_UTIL.win_fld(_wer,,'LP',,,5);
SYS_UTIL.win_fld(_wer,,'OPIS',,,60);
SYS_UTIL.win_fld(_wer,,'SZCZEG',,,85);
SYS_UTIL.win_fld(_wer,,'AUT_NAP',,,,,,'Automatycznie naprawialny'@,,'Znacznik, czy błąd automatycznie naprawialny'@,2,,"'A'","'N'");
SYS_UTIL.index(ind_tym);
SYS_UTIL.win_sel(_wer);
:------------akcje - niepusta dziedzina
SYS_UTIL.win_act(_wer,,'Kolejność');
SYS_UTIL.win_act(_wer,,'Menu','Zmień zakres'@@);
SYS_UTIL.win_act(_wer,,'Formuła','Automatycznie naprawialne'@@,'Zmień zakres'@,,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - wszystkie automatycznie naprawialne'@);
                  SYS_UTIL.index(ind_tym1); SYS_UTIL.prefix('A')");
SYS_UTIL.win_act(_wer,,'Formuła','Istotne automatycznie naprawialne'@@,'Zmień zakres'@,,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - istotne automatycznie naprawialne'@);
                  SYS_UTIL.index(ind_tym1); SYS_UTIL.prefix('A','I')");
SYS_UTIL.win_act(_wer,,'Formuła','Nienaprawialne'@@,'Zmień zakres'@,,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - nienaprawialne'@);
                  SYS_UTIL.index(ind_tym1); SYS_UTIL.prefix('N')");
SYS_UTIL.win_act(_wer,,'Formuła','W&szystkie istotne'@@,'Zmień zakres'@,,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - wszystkie istotne'@);
                  SYS_UTIL.index(ind_tym3); SYS_UTIL.prefix('I')");
SYS_UTIL.win_act(_wer,,'Formuła','Wszystkie'@@,'Zmień zakres'@,,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - wszystkie'@);
                  SYS_UTIL.index(ind_tym); SYS_UTIL.prefix()");
SYS_UTIL.win_act(_wer,,'Formuła','Odtwarzaj'@@,,,"sel_exit()",,1);
SYS_UTIL.win_act(_wer,,'Formuła','Drukuj'@@,,,"exec('rep_exec','#b_report','','fks_sys_util',,,,,,,,'W')");
:------------akcje - pusta dziedzina
SYS_UTIL.win_act(_wer,1,'Menu','Zmień zakres'@@);
SYS_UTIL.win_act(_wer,1,'Formuła','Automatycznie naprawialne'@@,'Zmień zakres',,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - automatycznie naprawialne'@);
                  SYS_UTIL.index(ind_tym1); SYS_UTIL.prefix('A')");
SYS_UTIL.win_act(_wer,1,'Formuła','Istotne automatycznie naprawialne'@@,'Zmień zakres'@,,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - istotne automatycznie naprawialne'@);
                  SYS_UTIL.index(ind_tym1); SYS_UTIL.prefix('A','I')");
SYS_UTIL.win_act(_wer,1,'Formuła','Nienaprawialne'@@,'Zmień zakres'@,,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - nienaprawialne'@);
                  SYS_UTIL.index(ind_tym1); SYS_UTIL.prefix('N')");
SYS_UTIL.win_act(_wer,1,'Formuła','W&szystkie istotne'@@,'Zmień zakres'@,,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - wszystkie istotne'@);
                  SYS_UTIL.index(ind_tym3); SYS_UTIL.prefix('I')");
SYS_UTIL.win_act(_wer,1,'Formuła','Wszystkie'@@,'Zmień zakres'@,,
                 "SYS_UTIL.hdr_sel();
                  SYS_UTIL.hdr_sel('Lista wykrytych usterek - wszystkie'@);
                  SYS_UTIL.index(ind_tym); SYS_UTIL.prefix()");
SYS_UTIL.win_act(_wer,1,'Formuła','Odtwarzaj'@@,,,"sel_exit()",,1,,,,'O');
SYS_UTIL.win_btn(_wer,'text='+'Odtwarzaj'@+',btn_label_align=center,panel=bottom,align=end','menu:O')


\tab_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Dodawanie rekordow do tabeli tymczasowej
::   WE: _a - opis usterki
::       _b - lokalizacja usterki opisowa
::       _c - mozliwa naprawa automatyczna usterki (A - tak, N -nie)
::       _d - typ usterki
::       _e - _t  - dane lokalizacyjne do poprawienia usterki (opcja)
::       _e, _f, _g, _h, _i, _o, _p, _s, _t, _u - typu STRING
::       _j, _k, _l - typu INTEGER
::       _m, _n - typu REAL
::       _q, _r - typu DATE
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_e')<=0 || _e:='' ?};
{? var_pres('_f')<=0 || _f:='' ?};
{? var_pres('_g')<=0 || _g:='' ?};
{? var_pres('_h')<=0 || _h:='' ?};
{? var_pres('_i')<=0 || _i:='' ?};
{? var_pres('_j')<=0 || _j:=0 ?};
{? var_pres('_k')<=0 || _k:=0 ?};
{? var_pres('_l')<=0 || _l:=0 ?};
{? var_pres('_m')<=0 || _m:=0 ?};
{? var_pres('_n')<=0 || _n:=0 ?};
{? var_pres('_o')<=0 || _o:='' ?};
{? var_pres('_p')<=0 || _p:='' ?};
{? var_pres('_q')<=0 || _q:=date(0,0,0) ?};
{? var_pres('_r')<=0 || _r:=date(0,0,0) ?};
{? var_pres('_s')<=0 || _s:='I' ?};
{? var_pres('_t')<=0 || _t:='' ?};
{? var_pres('_u')<=0 || _u:='' ?};
SYS_UTIL.blank(1); numer_bl+=1;
SYS_UTIL.LP:=numer_bl;
SYS_UTIL.TP_USTER:=_d; SYS_UTIL.OPIS:=_a; SYS_UTIL.AUT_NAP:=_c; SYS_UTIL.WAGA:=_s;
SYS_UTIL.L1:=_e;  SYS_UTIL.L2:=_f;  SYS_UTIL.L3:=_g;  SYS_UTIL.L4:=_h; SYS_UTIL.L5:=_i;
SYS_UTIL.L6:=_j;  SYS_UTIL.L7:=_k;  SYS_UTIL.L8:=_l;  SYS_UTIL.L9:=_m; SYS_UTIL.L10:=_n;
SYS_UTIL.L11:=_o; SYS_UTIL.L12:=_p; SYS_UTIL.L13:=_q; SYS_UTIL.L14:=_r; SYS_UTIL.L15:=_t;
SYS_UTIL.L16:=_u; SYS_UTIL.SZCZEG:=_b;
SYS_UTIL.add()


\ut_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie dokumentow zrodlowych i ksiegowan
::   WE: _a=0/1 dokumenty
::       _b=0/1 - ksiegowania
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); OKRO_F.cntx_psh();
_ar:=SSTALE.AR; _ao:=SSTALE.AO;
_rok_od:=ROZNE.UT_ROKOD().KOD; _rok_do:=ROZNE.UT_ROKDO().KOD;
_okr_od:=ROZNE.UT_OKROD().NR; _okr_do:=ROZNE.UT_OKRDO().NR;
typ_dok:="{? -DOK.ZK='t'
          || 'zaksiegowanego końcowo'
          |? -DOK.ZP='t'
          || 'zaksięgowanego próbnie'
          |? -DOK.A='t'
          || 'zaakceptowanego'
          ?}";
lokal:="DOK.ODD().OD+'/'+$OKRO_F.NR+'/'+ROK_F.NAZ+' rejestr '+DOK.REJ().KOD+' numer dokumentu '+$DOK.NR";
lokal1:="DOK.ODD().OD+'/'+$OKRO_F.NR+'/'+ROK_F.NAZ+' rejestr '+DOK.REJ().KOD+' numer dokumentu '+$DOK.NR+' pozycja '+$POZ.POZ";
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA); OKRO_F.index('ROK');
ROZLVAT.index('DOK'); ROZLVAT.prefix();
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {! |?
      {? -ROK_F.ZAM='n'
      || OKRO_F.prefix(ROK_F.ref);
         {? OKRO_F.first
         || {! |?
               {? {? ROK_F.KOD=_rok_od || OKRO_F.NR>=_okr_od || 1 ?} &
                  {? ROK_F.KOD=_rok_do || OKRO_F.NR<=_okr_do || 1 ?}
               || SSTALE.AR:=ROK_F.ref; SSTALE.AO:=OKRO_F.ref;
                  exec('open_tabele','open_tab'); SLO.index('SL'); SLO.prefix();
                  DOK.index('PR1'); POZ.index('DOK'); VPOZ.index('VDOK');
                  RMK.index('NAZ'); RMK.prefix();
                  {? ROZNE.UT_ZN || DOK.prefix(); exec('ut_zn','sys_util') ?};
                  {? _a || DOK.prefix('T'); exec('ut_dok1','sys_util') ?};
                  {? _b
                  || DOK.prefix('T','T'); DVAT.index('DOK'); DVAT.prefix(); PVAT.index('NR'); PVAT.prefix();
                     AN.index('WALSYM'); AN.prefix(); OP.index('KON_O'); OP.prefix(); ZAP_OP.index('ZAP'); ZAP_OP.prefix();
                     exec('ut_ks','sys_util')
                  ?}
               ?};
               OKRO_F.next()
            !}
         ?}
      ?};
      ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next()
   !}
?};
{? SSTALE.AR<>_ar | SSTALE.AO<>_ao
|| SSTALE.AR:=_ar; SSTALE.AO:=_ao;
   exec('open_tabele','open_tab')
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop();
&typ_dok; &lokal; &lokal1;
1


\ut_zn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie znacznikow w dokumentach zrodlowych
::----------------------------------------------------------------------------------------------------------------------
_ile_dok:=DOK.size(); _nr_dok:=0;
{? DOK.first
|| {! |?
      _dok_a:=DOK.A; _dok_zk:=DOK.ZK; _dok_zp:=DOK.ZP; _bl_zn:=0; _nr_dok+=1;
      _proc:=$((_nr_dok/_ile_dok)$2*100)+'%';
      echo('Trwa sprawdzanie znaczników w dokumentach w okresie %1 %2 - %3'@[OKRO_F.NAZ,ROK_F.NAZ,_proc]);
      POZ.prefix(DOK.ref());
      {? POZ.first()
      || {! |?
            {? ~_bl_zn & (POZ.A<>_dok_a | POZ.ZP<>_dok_zp | POZ.ZK<>_dok_zk)
            || _bl_zn:=1; {? ~bl_znacz & (POZ.A<>_dok_a | POZ.ZP<>_dok_zp) || bl_znacz:=1 ?};
               _typ_d:={? -DOK.ZK='t' || 'zaksięgowanym końcowo'
                       |? -DOK.ZP='t' || 'zaksiegowanym próbnie'
                       |? -DOK.A='t' || 'zaakceptowanym'
                       || 'niezaakceptowanym'
                       ?};
               _opis:='Nieprawidłowe znaczniki (akceptacji, księgowania) w dokumencie '+_typ_d;
               exec('tab_add','sys_util',_opis,lokal(),'A',24,ROK_F.KOD,DOK.REJ().KOD,'','','',
                                         OKRO_F.NR,DOK.NR,,,,,,,,,DOK.REJ().ODD().OD)
            ?};
            ~_bl_zn & POZ.next()
         !}
      ?};
      DOK.next()
   !}
?};
echo()


\ut_dok1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie dokumentow i pozycji dokumentow zrodlowych
::----------------------------------------------------------------------------------------------------------------------
_ile_dok:=DOK.size(); _nr_dok:=0;
{? DOK.first
 || {! |?
     _nr_dok+=1; _proc:=$((_nr_dok/_ile_dok)$2*100)+'%';
     echo('Trwa sprawdzanie dokumentów w okresie %1 %2 - %3'@[OKRO_F.NAZ,ROK_F.NAZ,_proc]);
     POZ.prefix(DOK.ref);  _wn:=_ma:=0;
     _typ:=DOK.DOK_REJ().SLO().KOD;
     {? _typ='BO'
     || OKRO_F.cntx_psh(); ROK_F.cntx_psh();
        ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
        SSTALE.AO().ROK();
        {? ~ROK_F.prev() || _typ:='' ?};
        OKRO_F.cntx_pop(); ROK_F.cntx_pop()
     ?};
     _vat:=(_typ='VAT' | _typ='SAD');
     {? |DOK.NK=''
     || _opis:='Brak symbolu dokumentu '+typ_dok();
        exec('tab_add','sys_util',_opis,lokal(),'N',15)
     ?};
     {? DOK.ODD=null
     || _opis:='Dokument '+typ_dok()+' nie jest związany z żadną jednostką księgową';
        exec('tab_add','sys_util',_opis,lokal(),'N',5)
     |? DOK.ODD<>DOK.REJ().ODD
     || _opis:='Dokument '+typ_dok()+' jest zarejestrowany w innej jednostce księgowej niż jednostka rejestru księgowego';
        exec('tab_add','sys_util',_opis,lokal(),'N',25)
     ?};
     {? _vat
     || VPOZ.prefix(DOK.ref());
        {? VPOZ.first()
        ||  _zg:=exec('sprvpozp','dok_fks',DOK.ref());
            {? _zg & (DOK.OKRVAT<>VPOZ.OKRVAT | DOK.A_VAT<>VPOZ.A_VAT)
            || _opis:='Brak zgodności okresu lub trybu rozliczenia VAT w dokumencie '+typ_dok();
               exec('tab_add','sys_util',_opis,lokal(),'A',29,ROK_F.KOD,DOK.REJ().KOD,'','','',
                                         OKRO_F.NR,DOK.NR,,,,,,,,,DOK.REJ().ODD().OD)
            |? ~_zg & (DOK.OKRVAT<>null | DOK.A_VAT<>null)
            || _opis:='Błąd w wypełnieniu pola okres lub tryb rozliczenia w dokumencie '+typ_dok();
               exec('tab_add','sys_util',_opis,lokal(),'A',30,ROK_F.KOD,DOK.REJ().KOD,'','','',
                                         OKRO_F.NR,DOK.NR,,,,,,,,,DOK.REJ().ODD().OD)
            ?}
        || _opis:='Brak pozycji VAT w dokumencie '+typ_dok();
           exec('tab_add','sys_util',_opis,lokal(),'N',6)
        ?}
     ?};
     {? (DOK.DTW<OKRO_F.POCZ | DOK.DTW>OKRO_F.KON) & OKRO_F.KON<>date(0,0,0)
     || _opis:='Data zapisu księgowania spoza okresu rejestracji dokumentu '+typ_dok();
        exec('tab_add','sys_util',_opis,lokal(),'N',7)
     ?};
     {? DOK.DOP=date(0,0,0)
     || _opis:='Nie wypełniona data operacji (dokument '+typ_dok()+')';
        exec('tab_add','sys_util',_opis,lokal(),'N',8)
     ?};
     {? DOK.DTO=date(0,0,0)
     || _opis:='Nie wypełniona data dokumentu (dokument '+typ_dok()+')';
        exec('tab_add','sys_util',_opis,lokal(),'N',8)
     ?};
     _blad:=0; _blad1:=0; _poz:=0;
     {? POZ.first
     || {! |?
           _poz+=1;
           {? ~_blad & POZ.POZ<>_poz
           || _blad:=1; _opis:='Błędna numeracja pozycji w dokumencie '+typ_dok();
              exec('tab_add','sys_util',_opis,lokal(),'N',2)
           ?};
           {? POZ.KOM=null
           || _blad1:=1; _opis:='Nie wypełnione pole komentarz w pozycji dokumentu '+typ_dok();
              exec('tab_add','sys_util',_opis,lokal1(),'A',3,ROK_F.KOD,DOK.REJ().KOD,'','','',OKRO_F.NR,DOK.NR,
                                        POZ.POZ,,,,,,,,DOK.REJ().ODD().OD)
           |? POZ.KOM<>null & (|POZ.KOM().KS().SYM<>(ROK_F.SYNT+POZ.KON))
           || _blad1:=1;
              _opis:='Błędnie wypełnione pole komentarz w pozycji dokumentu '+typ_dok();
              exec('tab_add','sys_util',_opis,lokal1(),'N',26)
           |? ~_blad1 & -(1+POZ.KOM().KS().TYP)<>'p' & _typ<>'BO'
           || {? -(~POZ.STR)='wn' || _wn+=POZ.SUM$2 || _ma+=POZ.SUM$2 ?}
           ?};
           {? +|POZ.ID & _vat
           || {? POZ.DATA_R<>exec('dat_otw','dok_fks')
              || _opis:='Niezgodność daty rozliczenia w pozycji z dokumentem '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',10)
              ?}
           ?};
           {? POZ.SUM$2=0 & _typ<>'BO'
           || _opis:='Suma w pozycji dokumentu '+typ_dok()+' jest równa 0';
              exec('tab_add','sys_util',_opis,lokal1(),'N',23)
           ?};
           {? POZ.SUM<>POZ.SUM$2
           || _opis:='Suma w pozycji dokumentu '+typ_dok()+' nie jest zaokrąglona';
              exec('tab_add','sys_util',_opis,lokal1(),'A',12,ROK_F.KOD,DOK.REJ().KOD,'','','',OKRO_F.NR,DOK.NR,
                                        POZ.POZ,POZ.SUM$2,,,,,,,DOK.REJ().ODD().OD)
           ?};
           {? POZ.WAL<>null
           || {? POZ.SUMW<>POZ.SUMW$2
              || _opis:='Suma w walucie w pozycji dokumentu '+typ_dok()+' nie jest zaokrąglona';
                 exec('tab_add','sys_util',_opis,lokal1(),'A',13,ROK_F.KOD,DOK.REJ().KOD,'','','',OKRO_F.NR,DOK.NR,
                                           POZ.POZ,POZ.SUMW$2,,,,,,,DOK.REJ().ODD().OD)
              ?};
              {? _typ<>'BO' & (POZ.SUMW$2=0 | POZ.KURS=0)
              || _opis:='Brak informacji walutowych w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',14)
              ?}
           ?};
           {? POZ.KOM<>null & exec('akc_kont','edkonto',POZ.KON)
           || _opis:='Niepoprawne konto księgowe w pozycji dokumentu '+typ_dok();
              exec('tab_add','sys_util',_opis,lokal1(),'N',16)
           ?};
           {? -exec('akc_zabl','konto',POZ.KON)='b'
           || _opis:='Zablokowane konto księgowe w pozycji dokumentu '+typ_dok();
              exec('tab_add','sys_util',_opis,lokal1(),'N',17)
           ?};
           typ_roz:='';
           _p:=exec('spr_pop','dok_fks1',_typ,2);
           {? _p<>''
           || {? _p='r'
              || _opis:='Brak identyfikatora rozrachunku w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',18)
              |? _p='poz_odd'
              || _opis:='Niepotrzebna jednostka księgowa rozrachunku w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',18)
              |? _p='z'
              || _opis:='Niepotrzebny identyfikator rozrachunku w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',18)
              |? _p='do'
              || _opis:='Niepotrzebna data otwarcia rozrachunku w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',18)
              |? _p='tp'
              || _opis:='Niepotrzebny termin płatności rozrachunku w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',18)
              |? _p='data_r'
              || _opis:='Niepotrzebna data rozliczenia w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',18)
              |? (_p='b' | _p='c')
              || {? ROZNE.UT_ROZ1
                 || {? _p='b'
                    || _opis:='Typ identyfikatora rozrachunku niezgodny z definicją konta w pozycji dokumentu '+typ_dok();
                       exec('tab_add','sys_util',_opis,lokal1(),'N',18)
                    |? typ_roz<>''
                    || _opis:='Typ identyfikatora rozrachunku niezgodny z definicją konta w pozycji dokumentu '+typ_dok();
                       exec('tab_add','sys_util',_opis,lokal1(),'A',28,ROK_F.KOD,DOK.REJ().KOD,'','','',OKRO_F.NR,DOK.NR,
                                                                POZ.POZ,,,,typ_roz,,,,DOK.REJ().ODD().OD)
                    ?}
                 ?}
              |? ~(3+_p='Kom')
              || _opis:='Niewypełnione pole '+(-form(_p))+' w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',18)
              ?}
           ?};
           &typ_roz;
           {? +|POZ.ID &  PAR_SKID.get(38)='N' & POZ.DO<>date(0,0,0) & POZ.TP<>date(0,0,0) & POZ.DO>POZ.TP
           || _opis:='Data otwarcia rozrachunku jest większa od terminu płatności w pozycji dokumentu '+typ_dok();
              exec('tab_add','sys_util',_opis,lokal1(),'N',19)
           ?};
           {? -OKRO_F.NAZ<>'bilans otwarcia'
           || _zwrot:=exec('spr_wyr_w','dok_fks','',2);
              {? _zwrot<>''
              || _opis:='Niepoprawnie wypełnione wyróżniki w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',20)
              ?}
           ?};
           {? -POZ.TID='rmk'
           || {? ~RMK.find_key(POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK)
              || _opis:='Brak rekordu w tabeli RMK w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',21)
              |? RMK.KONZ=''|  ~(-RMK.STRZ='wn' | -RMK.STRZ='ma') |
                 RMK.ILEMIES<=0 | RMK.SUMZ$2<0 | RMK.ODD=null | RMK.ODD<>POZ.ODD |
                 ~(-RMK.BL='t' | -RMK.BL='n')  | {? -RMK.BL='t' || ~#RMK.BLOD |  ~#RMK.BLDO || 0 ?}
              || _opis:='Nieprawidłowo wypełnione pola RMK w pozycji dokumentu '+typ_dok();
                 exec('tab_add','sys_util',_opis,lokal1(),'N',22)
              ?}
           ?};
           POZ.next
         !}
     |? DOK.DOK_REJ().M_ODD<>'T'
     || {? DOK_REJ.PUDEK<>'T'
        || _opis:='Brak pozycji w dokumencie '+typ_dok();
           exec('tab_add','sys_util',_opis,lokal(),'N',1)
        ?}
     ?};
     {? ~_blad1 & ~exec('spr_mod','dok_fks') & DOK.DOK_REJ().M_ODD<>'T'
     || _p:=fabs((_wn-_ma));
        {? _p>0
        || _opis:='Niezbilansowany dokument '+typ_dok()+'( na kwotę '+form(_p,,2,' ,')+')';
           exec('tab_add','sys_util',_opis,lokal(),'N',4)
        ?}
     ?};
     {? (DOK.DOK_REJ().POW='T' | DOK.DOK_REJ().SLO().KOD='VAT')
     || _akcept:=1;
        VPOZ.cntx_psh();
        {? VPOZ.first()
        || {! |?
              _akcept:=~(VPOZ.GRVAT=null);
              {? _akcept
              || GR_VAT.cntx_psh();
                 GR_VAT.index('REJ_KOD'); GR_VAT.prefix(DOK.REJ);
                 {? ~GR_VAT.seek(VPOZ.GRVAT)
                 || _opis:='Grupa podatkowa w pozycji VAT niedozwolona dla wybranego rejestru źródłowego.';
                    _akcept:=0
                 ?};
                 GR_VAT.cntx_pop()
              || _opis:='Nie wypełniona grupa podatkowa w pozycji VAT.'
              ?};
              {? ~_akcept || exec('tab_add','sys_util',_opis,lokal(),'N',27) ?};
              _akcept & VPOZ.next()
           !}
        ?};
        VPOZ.cntx_pop()
     ?};
     DOK.next
   !}
?}


\ut_ks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie ksiegowan dokumentow
::----------------------------------------------------------------------------------------------------------------------
_ile_dok:=DOK.size(); _nr_dok:=0;
{? DOK.first
|| {! |?
    _nr_dok+=1; _proc:=$((_nr_dok/_ile_dok)$2*100)+'%';
    echo('Trwa sprawdzanie księgowań w okresie %1 %2 - %3'@[OKRO_F.NAZ,ROK_F.NAZ,_proc]);
    POZ.prefix(DOK.ref());
    DOK.cntx_psh();
    {? DOK.RVAT
    || VPOZ.prefix(DOK.ref());
       {? VPOZ.first()
       || {! |?
             _blad:=0;
             DVAT.cntx_psh(); PVAT.cntx_psh(); VPOZ.cntx_psh();
             {? ~VPOZ.OKRVAT & ~(VPOZ.OKR_Z | VPOZ.OKR_C) & ~exec('is_rozl_cz_full','fks_vat') &
               (~ROZLVAT.find_key(OKRO_F.ref(),DOK.ref(),VPOZ.RVAT().RVAT,VPOZ.D))
             || _opis:='Brak powiązanego rekordu w tabeli ROZLVAT w dokumencie '+typ_dok();
                exec('tab_add','sys_util',_opis,lokal(),'A',36,ROK_F.KOD,DOK.REJ().KOD,'','','',OKRO_F.NR,
                                          DOK.NR,,,,,,,,,DOK.REJ().ODD().OD)
             |? VPOZ.OKRVAT
             || ROK_F.cntx_psh(); OKRO_F.cntx_psh();
                VPOZ.OKRVAT().ROK();
                {? VPOZ.OKRVAT().ROK<>SSTALE.AR
                || DVAT.use('dvat__'+VPOZ.OKRVAT().ROK().KOD); DVAT.index('DOK'); DVAT.prefix();
                   PVAT.use('pvat__'+VPOZ.OKRVAT().ROK().KOD); PVAT.index('NR'); PVAT.prefix()
                ?};
                ROK_F.cntx_pop(); OKRO_F.cntx_pop();
                {? (~DVAT.find_key(DOK.ref(),SSTALE.AO,VPOZ.OKRVAT,VPOZ.RVAT().RVAT,VPOZ.D))
                || _opis:='Brak dokumentu VAT powiązanego z dokumentem '+typ_dok();
                   exec('tab_add','sys_util',_opis,lokal(),'A',37,ROK_F.KOD,DOK.REJ().KOD,'','','',OKRO_F.NR,
                                             DOK.NR,,,,,,,,,DOK.REJ().ODD().OD)
                || PVAT.prefix(DVAT.ref()); VPOZ.prefix(DOK.ref());
                   _kh:={? var_press('KH_FULL',DOK)>0 || DOK.KH_FULL || DOK.KH ?};
                   {? DVAT.OBR<>VPOZ.OKRVAT | DVAT.WYS<>DOK.WYS |
                      DVAT.NIP<>DOK.NIP | DVAT.KH<>_kh | DVAT.DAT1<>DOK.DTW | DVAT.DAT2<>DOK.DTO |
                      DVAT.DAT3<>DOK.D3 | DVAT.DAT4<>DOK.D4 |
                      (DVAT.DAT5<>DOK.DOP & DOK.DSP_WPOZ<>'T') | DVAT.SYM1<>DOK.NK |
                      DVAT.SYM2<>DOK.KOR | DVAT.ODD<>DOK.ODD | DVAT.WAL<>DOK.JORG | DVAT.KRAJ<>DOK.KRAJ
                   || _blad:=1
                   |? PVAT.first() & VPOZ.first()
                   ||{! |?
                        _wsp:={? PVAT.KNAG=2 || -1 || 1 ?};
                        {? PVAT.VPOZ_REF &
                           (
                            ~VPOZ.seek(PVAT.VPOZ_REF) |
                            PVAT.GRVAT<>VPOZ.GRVAT().GRVAT | PVAT.STVAT<>VPOZ.PR |
                            PVAT.SUM1*_wsp<>VPOZ.BRUTTO | PVAT.SUM2*_wsp<>VPOZ.NETTO |
                            PVAT.VAT*_wsp<>VPOZ.VAT | PVAT.CLO*_wsp<>VPOZ.CLO |
                            PVAT.VAT_ODL*_wsp<>VPOZ.VAT_ODL | PVAT.VATKOSZT*_wsp<>VPOZ.VATKOSZT |
                            PVAT.AKCYZA*_wsp<>VPOZ.AKCYZA | PVAT.KOS*_wsp<>VPOZ.KOS | PVAT.SUMC*_wsp<>VPOZ.WCLO
                            )
                         || _blad:=1
                         ?};
                         ~_blad & PVAT.next()
                      !}
                   || _blad:=1
                   ?};
                   {? _blad
                   || _opis:='Niezgodność zapisów w dokumencie VAT z dokumentem '+typ_dok();
                       exec('tab_add','sys_util',_opis,lokal(),'A',39,ROK_F.KOD,DOK.REJ().KOD,'','','',OKRO_F.NR,DOK.NR,
                                                       DVAT.NR,,,,,,,,DOK.REJ().ODD().OD)
                   ?}
                ?}
              ?};
              DVAT.cntx_pop(); PVAT.cntx_pop(); VPOZ.cntx_pop;
              _blad=0 & VPOZ.next()
          !}
        ?}
    ?};
    DOK.cntx_pop();
    {? POZ.first()
    || {! |?
         {? ~AN.find_key(FINFO.NAROD,POZ.KON)
         || {? -POZ.STR='wn' || _wn:=POZ.SUM; _ma:=0 || _wn:=0; _ma:=POZ.SUM ?};
            _opis:='Brak konta analitycznego ('+POZ.KON+' wal. '+FINFO.NAROD().KOD+')';
            exec('tab_add','sys_util',_opis,lokal1(),'A',50,ROK_F.KOD,FINFO.NAROD().KOD,FINFO.NAROD().SLU().NAZ,
            POZ.KON,POZ.KOM().KS().SYM,OKRO_F.NR,0,0,_wn,_ma,POZ.DOK().ODD().OD)
         ?};
         {? POZ.WAL<>null & ~AN.find_key(POZ.WAL,POZ.KON)
         || {? -POZ.STR='wn' || _wn_w:=POZ.SUMW; _ma_w:=0 || _wn_w:=0; _ma_w:=POZ.SUMW ?};
            _opis:='Brak konta analitycznego ('+POZ.KON+' wal. '+POZ.WAL().KOD+')';
            exec('tab_add','sys_util',_opis,lokal1(),'A',50,ROK_F.KOD,POZ.WAL().KOD,POZ.WAL().SLU().NAZ,
            POZ.KON,POZ.KOM().KS().SYM,OKRO_F.NR,0,0,_wn_w,_ma_w,POZ.DOK().ODD().OD)
         ?};
         {? |POZ.ID<>''
         || exec('sp_roz','sys_util',FINFO.NAROD,FINFO.NAROD().KOD);
            {? POZ.WAL<>null || exec('sp_roz','sys_util',POZ.WAL,POZ.WAL().KOD) ?}
         ?};
         POZ.next()
      !}
   ?};
   DOK.next()
  !}
?}


\sp_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie rozrachunkow z pozycji dokumentow ksiegowych
::   WE: _a - waluta (ref)
::       _b - waluta (kod ze SLO)
::----------------------------------------------------------------------------------------------------------------------
_czyroz:=OP.find_key(_a,POZ.ODD,POZ.KON,POZ.ID,POZ.ID,POZ.SYM_ROK); _spdo:=1;
{? ~_czyroz
|| _par42:=PAR_SKID.get(42);
   {? _par42='N' | (_par42='F' & DOK.DOK_REJ().ZAPL='T')
   || {? OP.find_key(_a,POZ.ODD,POZ.KON,POZ.ID,POZ.ID)
      || ZAP_OP.cntx_psh(); ZAP_OP.prefix(POZ.ref());
         {? ZAP_OP.first() || _czyroz:=1; _spdo:=0 ?};
         ZAP_OP.cntx_pop()
      ?}
   ?}
?};
{? ~_czyroz
|| _opis:='Brak rozrachunku ('+_b+') powiązanego z pozycją dokumentu '+typ_dok();
   exec('tab_add','sys_util',_opis,lokal1(),'A',40,ROK_F.KOD,DOK.REJ().KOD,
        FINFO.NAROD().SLU().NAZ,_b,'',OKRO_F.NR,DOK.NR,POZ.POZ,,,,,,,,DOK.REJ().ODD().OD)
|| ZAP_OP.cntx_psh();
   ZAP_OP.prefix(POZ.ref(),_a,SSTALE.AO,POZ.ODD); _zn_zap:=0; _refzap:=null;
   {? ZAP_OP.first()
   || {! |?
         {? ZAP_OP.PAR_POZ=null
         || {? _zn_zap=0 || _refzap:=ZAP_OP.ref() ?};
            _zn_zap+=1
         ?};
         ZAP_OP.next()
      !}
   ?};
   ZAP_OP.prefix();
   {? ~_zn_zap
   || _opis:='Brak ZAP_OP ('+_b+') powiązanego z pozycją dokumentu '+typ_dok();
      exec('tab_add','sys_util',_opis,lokal1(),'A',41,ROK_F.KOD,DOK.REJ().KOD,
           FINFO.NAROD().SLU().NAZ,_b,'',OKRO_F.NR,DOK.NR,POZ.POZ,,,,,,,,DOK.REJ().ODD().OD)
   |? _zn_zap>1
   || _opis:='Za dużo ('+$_zn_zap+') ZAP_OP ('+_b+') powiązanych z pozycją dokumentu '+typ_dok();
      exec('tab_add','sys_util',_opis,lokal1(),'A',42,ROK_F.KOD,DOK.REJ().KOD,
           FINFO.NAROD().SLU().NAZ,_b,'',OKRO_F.NR,DOK.NR,POZ.POZ,_zn_zap,,,,,,,DOK.REJ().ODD().OD)
   ?};
   {? POZ.DO<OP.DO & _spdo
   || _opis:='Błędne wypełnienie pola OP.DO w rozrachunku ('+_b+') powiązanym z pozycją dokumentu '+typ_dok();
      exec('tab_add','sys_util',_opis,lokal1(),'A',43,ROK_F.KOD,DOK.REJ().KOD,
           FINFO.NAROD().SLU().NAZ,_b,POZ.TID,OKRO_F.NR,DOK.NR,POZ.POZ,,,,,POZ.DO,POZ.TP,,DOK.REJ().ODD().OD)
   ?};
   {? _zn_zap=1 & _refzap<>null & ZAP_OP.seek(_refzap) &
      (DOK.REJ<>ZAP_OP.REJ | ZAP_OP.NR<>DOK.NR | ZAP_OP.DKS<>DOK.DKS |
       ZAP_OP.NK<>DOK.NK |  ZAP_OP.POZ<>POZ.POZ | ZAP_OP.KURS<>POZ.KURS |
       (ZAP_OP.TYP<>'Z' & DOK.DOK_REJ().ZAPL='T') |
       ZAP_OP.WAL2<>({? OP.WAL=FINFO.NAROD || POZ.WAL || FINFO.NAROD ?})
      )
   || _opis:='Błędne wypełnienie pól z dokumentu w zapisie na rozrachunku ('+_b+') powiązanym z pozycją dokumentu '+typ_dok();
      exec('tab_add','sys_util',_opis,lokal1(),'A',44,ROK_F.KOD,DOK.REJ().KOD,
           FINFO.NAROD().SLU().NAZ,_b,,OKRO_F.NR,DOK.NR,POZ.POZ,,,,,,,,DOK.REJ().ODD().OD)
   ?};
   ZAP_OP.cntx_pop()
?}


\ut_obr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie, czy konta analitycznego nie nalezy usunac
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.index('ROK');
AN.cntx_psh(); AN_SLU.cntx_psh(); POZ.cntx_psh();
_rok_od:=ROZNE.UT_ROKOD().KOD; _rok_do:=ROZNE.UT_ROKDO().KOD;
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {! |?
      {? -ROK_F.ZAM='n'
      || echo('Trwa sprawdzanie istnienia zapisów na kontach w roku %1'@[ROK_F.NAZ]);
         AN.use('koan__'+ROK_F.KOD); AN.index('WALSYM'); AN.prefix();
         AN_SLU.use('koansl'+ROK_F.KOD);
         OKRO_F.prefix(ROK_F.ref);
         {? AN.first()
         || {! |?
               _zn_poz:=0;
               {? OKRO_F.first
               || {! |?
                     POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2));  POZ.index('SYMKON');
                     {? AN.WAL<>FINFO.NAROD
                     || POZ.prefix('T','T',AN.SYM,AN.WAL)
                     || POZ.prefix('T','T',AN.SYM)
                     ?} ; _zn_poz:=POZ.first();
                     ~_zn_poz & OKRO_F.next()
                  !}
               ?};
               {? ~_zn_poz
               || _opis:='Brak zapisów na koncie';
                  _lokal:='Rok '+ROK_F.NAZ+' konto '+AN.SYM+' ('+AN.WAL().KOD+')';
                  exec('tab_add','sys_util',_opis,_lokal,'A',31,ROK_F.KOD,AN.WAL().SLU().NAZ,AN.WAL().KOD,
                                            AN.SYM,'',OKRO_F.NR,,,,,,,,,'N')
               ?};
               AN.next()
            !}
         ?}
      ?};
      ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next()
    !}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop(); AN.cntx_pop(); AN_SLU.cntx_pop();  POZ.cntx_pop()


\ut_obr1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie obrotow na kontach  analitycznych
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.index('ROK');
AN.cntx_psh(); AN_SLU.cntx_psh(); POZ.cntx_psh(); OBR.cntx_psh();
_rok_od:=ROZNE.UT_ROKOD().KOD; _rok_do:=ROZNE.UT_ROKDO().KOD;
_okr_od:=ROZNE.UT_OKROD().NR; _okr_do:=ROZNE.UT_OKRDO().NR;
ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {! |?
      {? -ROK_F.ZAM='n'
      || AN.use('koan__'+ROK_F.KOD); AN.index('WALSYM'); AN.prefix();
         AN_SLU.use('koansl'+ROK_F.KOD);
         OBR.use('obroty'+ROK_F.KOD); OBR.index('OKR'); OBR.prefix();
         OKRO_F.prefix(ROK_F.ref);
         {? OKRO_F.first
         || {! |?
               {? {? ROK_F.KOD=_rok_od || OKRO_F.NR>=_okr_od  || 1 ?} &
                  {? ROK_F.KOD=_rok_do || OKRO_F.NR<=_okr_do   || 1 ?}
               || echo('Trwa sprawdzanie obrotów na kontach w okresie %1 %2'@[OKRO_F.NAZ,ROK_F.NAZ]);
                  {? ODD.first()
                  || POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2));
                     {! |?
                        {? AN.first
                        || {! |?
                              _obwn:=_obma:=_pozwn:=_pozma:=0;
                              {? OBR.find_key(OKRO_F.NR,AN.ref,ODD.ref,OKRO_F.ref)
                              || _obwn:=OBR.WN; _obma:=OBR.MA
                              ?};
                              {? AN.WAL<>FINFO.NAROD
                              || POZ.index('SYMKON'); POZ.prefix('T','T',AN.SYM,AN.WAL,ODD.ref);  _wal:=1
                              || POZ.index('SYMKONBW'); POZ.prefix('T','T',AN.SYM,ODD.ref); _wal:=0
                              ?};
                              _zn_poz:=0;
                              {? POZ.first
                              || {! |?
                                    {? _wal
                                    || {? -POZ.STR='wn' || _pozwn+=POZ.SUMW || _pozma+=POZ.SUMW ?}
                                    || {? -POZ.STR='wn' || _pozwn+=POZ.SUM || _pozma+=POZ.SUM ?}
                                    ?};
                                    POZ.next()
                                 !};
                                 _zn_poz:=1
                              ?};
                              {? _obwn$2<>_pozwn$2 | _obma$2<>_pozma$2
                              || _opis:='Błędne obroty konta w jednostce księgowej '+ODD.OD+
                                        ' (wn '+form(fabs(_obwn$2-_pozwn$2),,2,' ,')+' ma '+form(fabs(_obma$2-_pozma$2),,2,' ,')+')';
                                 _lokal:=$OKRO_F.NR+'/'+ROK_F.NAZ+' konto '+AN.SYM+' ('+AN.WAL().KOD+')';
                                 exec('tab_add','sys_util',_opis,_lokal,'A',32,ROK_F.KOD,AN.WAL().SLU().NAZ,AN.WAL().KOD,
                                      AN.SYM,ODD.OD,OKRO_F.NR,0,0,_pozwn,_pozma)
                              |? _zn_poz & ~OBR.find_key(OKRO_F.NR,AN.ref,ODD.ref,OKRO_F.ref)
                              || _opis:='Brak rekordu w tabeli obrotów na koncie w jednostce księgowej '+ODD.OD;
                                 _lokal:=$OKRO_F.NR+'/'+ROK_F.NAZ+' konto '+AN.SYM+' ('+AN.WAL().KOD+')';
                                 exec('tab_add','sys_util',_opis,_lokal,'A',33,ROK_F.KOD,AN.WAL().SLU().NAZ,AN.WAL().KOD,
                                                           AN.SYM,ODD.OD,OKRO_F.NR,0,0,_pozwn,_pozma)
                              ?};
                              AN.next()
                           !}
                        ?};
                        ODD.next()
                     !}
                  ?}
               ?};
               OKRO_F.next()
            !}
         ?}
     ?};
     ROK_F.ref()<>ROZNE.UT_ROKOD & ROK_F.next()
  !}
?};
ROK_F.cntx_pop(); OKRO_F.cntx_pop(); AN.cntx_pop(); AN_SLU.cntx_pop(); POZ.cntx_pop(); OBR.cntx_pop();
1


\ut_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie kartotek VAT
::----------------------------------------------------------------------------------------------------------------------
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.index('ROK');
RVAT.cntx_psh();  RVAT.index('SYM');  RVAT.prefix(REF.FIRMA);
DVAT.cntx_psh(); PVAT.cntx_psh();
_rok_od:=ROZNE.UT_ROKOD().KOD; _rok_do:=ROZNE.UT_ROKDO().KOD;
_okr_od:=ROZNE.UT_OKROD().NR; _okr_do:=ROZNE.UT_OKRDO().NR;
SLO.cntx_psh(); SLU.cntx_psh(); SLUAPPL.cntx_psh();
SLU.index('NAZ'); SLU.prefix();
SLUAPPL.index('NAZ'); SLUAPPL.prefix('F','~KRAJE UE');
{? ROK_F.seek(ROZNE.UT_ROKOD) & SLUAPPL.first()
|| SLUAPPL.SLU();
   SLO.index('SL'); SLO.prefix(SLU.ref());
   {! |?
      {? -ROK_F.ZAM='n'
      || echo('Trwa sprawdzanie kartotek VAT w okresie %1 %2'@[OKRO_F.NAZ,ROK_F.NAZ]);
         OKRO_F.prefix(ROK_F.ref); _bl_dok:=0;
         DVAT.use('dvat__'+ROK_F.KOD);  DVAT.index('NR');
         PVAT.use('pvat__'+ROK_F.KOD); PVAT.index('NR');
         {? OKRO_F.first()
         || {! |?
               {? {? ROK_F.KOD=_rok_od || OKRO_F.NR>=_okr_od  || 1 ?} &
                  {? ROK_F.KOD=_rok_do || OKRO_F.NR<=_okr_do   || 1 ?}
               || {? RVAT.first()
                  || {! |? _blad:=0;
                        {? SLO.first()
                        || {! |?
                              DVAT.prefix(SLO.ref(),OKRO_F.ref(),RVAT.ref());
                              {? DVAT.first()
                              || _num:=0;
                                 {! |?
                                    _num+=1;
                                    DOK.cntx_psh(); ROK_F.cntx_psh(); OKRO_F.cntx_psh();
                                    _namedok:='doku'+DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
                                    {? DOK.name()<>_namedok || DOK.use(_namedok) ?};
                                    DOK.index('REJ'); DOK.prefix(); DVAT.DOK().REJ();
                                    {? ~_blad & ~(DVAT.NR=_num)
                                    || _blad:=1; _opis:='Błędna numeracja dokum. VAT';
                                       _lokal:=DOK.ODD().OD+'/'+$OKRO_F.NR+'/'+ROK_F.NAZ+' rejestr '+RVAT.SYM;
                                       exec('tab_add','sys_util',_opis,_lokal,'A',51,ROK_F.KOD,RVAT.SYM,'','','',OKRO_F.NR)
                                    ?};
                                    {? ~_bl_dok & DVAT.DOK=null
                                    || _bl_dok:=1; _opis:='Niewypełnione pole DVAT.DOK w jednym lub kilku dokumentach';
                                       _lokal:='Rok '+ROK_F.NAZ;
                                       exec('tab_add','sys_util',_opis,_lokal,'A',53,ROK_F.KOD,'','','','',OKRO_F.NR)
                                    ?};
                                    PVAT.prefix(DVAT.ref()); _blad1:=0;
                                   {? PVAT.first() & PVAT.DVAT<>null
                                   || _num1:=0;
                                      {! |? _num1+=1;
                                         {? ~_blad1 & ~(PVAT.NR=_num1)
                                         || _blad1:=1; _opis:='Błędna numeracja pozycji dokumentów VAT';
                                            _lokal:=DOK.ODD().OD+'/'+$OKRO_F.NR+'/'+ROK_F.NAZ+' rejestr '+RVAT.SYM+' numer '+$DVAT.NR;
                                            exec('tab_add','sys_util',_opis,_lokal,'A',52,DVAT.DOKOKRO().ROK().KOD,
                                                                      DOK.REJ().KOD,'','','',DVAT.DOKOKRO().NR,DOK.NR,
                                                                      DVAT.NR,,,,,,,DOK.REJ().ODD().OD)
                                         ?};
                                         ~_blad1 & PVAT.next()
                                      !}
                                   ?};
                                   DOK.cntx_pop(); ROK_F.cntx_pop(); OKRO_F.cntx_pop();
                                   DVAT.next()
                                 !}
                              ?};
                              SLO.next()
                           !}
                        ?};
                        RVAT.next()
                     !}
                  ?}
               ?};
               OKRO_F.next()
            !}
         ?}
      ?};
      ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next()
   !}
?};
SLO.cntx_pop(); SLU.cntx_pop(); SLUAPPL.cntx_pop();
ROK_F.cntx_pop(); OKRO_F.cntx_pop(); RVAT.cntx_pop(); DVAT.cntx_pop(); PVAT.cntx_pop()


\ut_kom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie komentarzy systemowych kont syntetycznych
::----------------------------------------------------------------------------------------------------------------------
KS.cntx_psh(); KS.index('SYM'); KOM.index('SYS');
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.index('ROK');
_rok_od:=ROZNE.UT_ROKOD().KOD; _rok_do:=ROZNE.UT_ROKDO().KOD;
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {! |?
      {? -ROK_F.ZAM='n'
      || echo('Trwa sprawdzanie komentarzy systemowych kont syntetycznych w roku %1'@[ROK_F.NAZ]);
         OKRO_F.prefix(ROK_F.ref()); KS.prefix(ROK_F.ref);
         {? OKRO_F.first() & KS.first()
         || {! |?
               KOM.prefix(KS.ref(),'T');
               {? ~KOM.first()
               || _opis:='Brak komentarza systemowego dla konta '+KS.SYM;
                  _lokal:='Rok '+ROK_F.NAZ;
                  exec('tab_add','sys_util',_opis,_lokal,'A',60,ROK_F.KOD,KS.SYM,'','','',OKRO_F.NR)
               ?};
               KS.next()
            !}
         ?}
      ?};
      ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next()
   !}
?};
KS.cntx_pop(); ROK_F.cntx_pop(); OKRO_F.cntx_pop()


\ut_roz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [7.60]
:: OPIS: Sprawdzanie kartotek rozrachunkow
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh(); ZAP_OP.cntx_psh(); STANY.cntx_psh(); RMK.cntx_psh();
ROK_F.cntx_psh(); ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix();
_rok_od:=ROZNE.UT_ROKOD().KOD; _rok_do:=ROZNE.UT_ROKDO().KOD;
_indtym:=ZAP_OP.ndx_tmp('',1,'OP',,0,'OKRO',,0);
{? ROK_F.seek(ROZNE.UT_ROKOD)
|| {! |?
      {? -ROK_F.ZAM='n'
      || echo('Trwa sprawdzanie kartotek rozrachunków w roku %1'@[ROK_F.NAZ]);
         OP.use('operac' + ROK_F.KOD); OP.index('KON_O'); OP.prefix();
         ZAP_OP.use('rozzap'+ ROK_F.KOD); ZAP_OP.index(_indtym); ZAP_OP.prefix();
         STANY.use('stany_'+ ROK_F.KOD); STANY.index('STAN'); STANY.prefix();
         RMK.use('rmkop_'+ROK_F.KOD); RMK.index('NAZ'); RMK.prefix();
         OKRO_F.prefix(ROK_F.ref());
         {? OP.first() & OKRO_F.first()
         || {! |?
               ZAP_OP.prefix(OP.ref); _wn:=_ma:=0;
               {? ZAP_OP.first
               || {! |?
                     _bl_zap:=0;
                     {? ZAP_OP.POZDOK<>null
                     || _wn+=ZAP_OP.WN; _ma+=ZAP_OP.MA
                     |? _bl_zap=0
                     || _bl_zap:=1;
                        _opis:='Nie związany z POZ zapis(y) rozrachunku '+OP.SYM+' ('+OP.WAL().KOD+')';
                        _lokal:='Rok '+ROK_F.NAZ+' konto '+OP.AN+' jednostka księgowa '+OP.ODD().OD;
                        exec('tab_add','sys_util',_opis,_lokal,'A',68,ROK_F.KOD,OP.WAL().SLU().NAZ,
                                                  OP.WAL().KOD,OP.AN,OP.SYM,OKRO_F.NR,,,,,OP.ODD().OD,,,,,,OP.SYM_ROK)
                     ?};
                     _wn2:=_ma2:=_war:=0;
                     {? ~_bl_zap &
                        {? ZAP_OP.PAR_POZ=null
                        || POZ.cntx_psh();
                           _maska:=ref_name(ZAP_OP.POZDOK); {? _maska<>POZ.name() || POZ.use(_maska) ?};
                           POZ.index('DOK'); POZ.prefix(); ZAP_OP.POZDOK();
                           _war:={? OP.WAL=FINFO.NAROD & POZ.WAL=null
                                 || (ZAP_OP.WN2<>0 | ZAP_OP.MA2<>0)
                                 |? OP.WAL=FINFO.NAROD & POZ.WAL<>null
                                 || {? -POZ.STR='wn'
                                    || _wn2:=POZ.SUMW; (ZAP_OP.MA2<>0 | ZAP_OP.WN2<>POZ.SUMW)
                                    || _ma2:=POZ.SUMW; (ZAP_OP.WN2<>0 | ZAP_OP.MA2<>POZ.SUMW)
                                    ?}
                                 |? OP.WAL<>FINFO.NAROD
                                 || {? -POZ.STR='wn'
                                    || _wn2:=POZ.SUM; (ZAP_OP.MA2<>0 | ZAP_OP.WN2<>POZ.SUM)
                                    || _ma2:=POZ.SUM; (ZAP_OP.WN2<>0 | ZAP_OP.MA2<>POZ.SUM)
                                    ?}
                                 || 0
                                 ?};
                           POZ.cntx_pop();
                           _war
                        |? ZAP_OP.WAL2=null
                        || _war:=(ZAP_OP.WN2<>0 | ZAP_OP.MA2<>0)
                        || PAR_NAG.cntx_psh(); PAR_POZ.cntx_psh();
                           _maska:=ref_name(ZAP_OP.PAR_POZ);
                           {? _maska<>PAR_POZ.name() || PAR_NAG.use('parnag'+(_maska+2)); PAR_POZ.use(_maska) ?};
                           PAR_NAG.index('UNIK'); PAR_NAG.prefix();
                           PAR_POZ.index('PAR_POZ'); PAR_POZ.prefix();
                           ZAP_OP.PAR_POZ().PAR_NAG();
                           _war:={? PAR_NAG.find_key(ZAP_OP.WAL2,PAR_NAG.TYP,PAR_NAG.UNIK) &
                                    PAR_POZ.find_key(PAR_NAG.ref(),PAR_POZ.TZ,PAR_POZ.OP,PAR_POZ.KONTO)
                                 || {? PAR_POZ.OP=OP.SYM
                                    || {? -PAR_NAG.STR='wn'
                                       || _wn2:=PAR_POZ.KWOTA; ZAP_OP.MA2<>0 | ZAP_OP.WN2<>PAR_POZ.KWOTA
                                       || _ma2:=PAR_POZ.KWOTA; ZAP_OP.WN2<>0 | ZAP_OP.MA2<>PAR_POZ.KWOTA
                                       ?}
                                    || {? -PAR_NAG.STR='wn'
                                       || _wn2:=(-PAR_POZ.KWOTA); ZAP_OP.MA2<>0 | ZAP_OP.WN2<>(-PAR_POZ.KWOTA)
                                       || _ma2:=(-PAR_POZ.KWOTA); ZAP_OP.WN2<>0 | ZAP_OP.MA2<>(-PAR_POZ.KWOTA)
                                       ?}
                                    ?}
                                 ?};
                           PAR_NAG.cntx_pop(); PAR_POZ.cntx_pop();
                           _war
                        ?}
                     || _opis:='Błędne wartości (II waluta) w pozycji rozrachunku '+OP.SYM+' ('+OP.WAL().KOD+')';
                        _lokal:='Rok '+ROK_F.NAZ+' konto '+OP.AN+' jednostka księgowa '+OP.ODD().OD;
                        exec('tab_add','sys_util',_opis,_lokal,'A',70,ROK_F.KOD,OP.WAL().SLU().NAZ,
                                                  OP.WAL().KOD,OP.AN,OP.SYM,OKRO_F.NR,,#ZAP_OP.ref(),_wn2,_ma2,OP.ODD().OD,,,,,,OP.SYM_ROK)
                     ?};
                     ZAP_OP.next()
                  !}
               ?};
               _ar:=SSTALE.AR; _ao:=SSTALE.AO; _asynt:=BILANSP.ASYNT; _asep:=BILANSP.ASEP;
               SSTALE.AR:=ROK_F.ref(); SSTALE.AO:=OKRO_F.ref(); BILANSP.ASYNT:=ROK_F.SYNT; BILANSP.ASEP:=ROK_F.SEP;
               _poch:=exec('wys_op','dok_fks_ks',OP.AN); _bl_poch:=0;
               {? OP.POCH<>_poch || OP.POCH:=_poch; _bl_poch:=1 ?};
               _khpoch:=exec('kh_op','dok_fks_ks');
               {? _poch=0 || _poch:=null ?}; {? _khpoch=0 || _khpoch:=null ?};
               {? _bl_poch | (_khpoch<>OP.KH)
               || _opis:='Błędne dane dotyczące źródła pochodzenia rozrachunku '+OP.SYM+' ('+OP.WAL().KOD+')';
                  _lokal:='Rok '+ROK_F.NAZ+' konto '+OP.AN+' jednostka księgowa '+OP.ODD().OD;
                  exec('tab_add','sys_util',_opis,_lokal,'A',72,ROK_F.KOD,OP.WAL().SLU().NAZ,OP.WAL().KOD,
                                            OP.AN,OP.SYM,OKRO_F.NR,#_poch,#_khpoch,0,0,OP.ODD().OD,,,,,,OP.SYM_ROK)
               ?};
               SSTALE.AR:=_ar; SSTALE.AO:=_ao; BILANSP.ASYNT:=_asynt; BILANSP.ASEP:=_asep;
               {? _wn$2<>OP.WN$2 | _ma$2<>OP.MA$2
               || _opis:='Błędne obroty w nagłówku rozrachunku '+OP.SYM+' ('+OP.WAL().KOD+')';
                  _lokal:='Rok '+ROK_F.NAZ+' konto '+OP.AN+' jednostka księgowa '+OP.ODD().OD;
                  exec('tab_add','sys_util',_opis,_lokal,'A',65,ROK_F.KOD,OP.WAL().SLU().NAZ,OP.WAL().KOD,
                                            OP.AN,OP.SYM,OKRO_F.NR,0,0,_wn,_ma,OP.ODD().OD,,,,,,OP.SYM_ROK)
               ?};
               {? _wn$2=_ma$2 & -OP.STAN<>'r' |  _wn$2<>_ma$2 & -OP.STAN<>'n'
               || _stan:={? _wn$2=_ma$2 || 'R' || 'N' ?};
                  _opis:='Błędny stan (Rozliczony/Nierozliczony) rozrachunku '+OP.SYM+' ('+OP.WAL().KOD+')';
                  _lokal:='Rok '+ROK_F.NAZ+' konto '+OP.AN+' jednostka księgowa '+OP.ODD().OD;
                  exec('tab_add','sys_util',_opis,_lokal,'A',66,ROK_F.KOD,OP.WAL().SLU().NAZ,OP.WAL().KOD,
                                            OP.AN,OP.SYM,OKRO_F.NR,,,,,OP.ODD().OD,_stan,,,,,OP.SYM_ROK)
               ?};
               _blad:=0;
               STANY.prefix(OP.ref());
               {? STANY.first()
               || {! |?
                     _zap_wn:=_zap_ma:=0;
                     ZAP_OP.prefix(OP.ref(),STANY.OKRO);
                     {? ZAP_OP.first()
                     || {! |?
                           {? ZAP_OP.POZDOK<>null || _zap_wn+=ZAP_OP.WN; _zap_ma+=ZAP_OP.MA ?};
                           ZAP_OP.next()
                        !};
                        {? STANY.WN<>_zap_wn | STANY.MA<>_zap_ma || _blad:=1 ?}
                     |? (STANY.WN<>0 | STANY.MA<>0)
                     || _blad:=1
                     ?};
                     ~_blad & STANY.next()
                  !}
              ?};
              ZAP_OP.prefix(OP.ref()); _wn:=_ma:=0; _okr_spr:=null;
              {? ~_blad & ZAP_OP.first()
              || {! |?
                    {? ZAP_OP.POZDOK<>null & ZAP_OP.OKRO<>_okr_spr
                    || _okr_spr:=ZAP_OP.OKRO;
                       STANY.prefix(OP.ref,_okr_spr); {? ~STANY.first() || _blad:=1 ?}
                    ?};
                    ~_blad & ZAP_OP.next()
                 !}
              ?};
              {? _blad
              || _opis:='Błędne stany rozrachunku '+OP.SYM+' ('+OP.WAL().KOD+')';
                 _lokal:='Rok '+ROK_F.NAZ+' konto '+OP.AN+' jednostka księgowa '+OP.ODD().OD;
                 exec('tab_add','sys_util',_opis,_lokal,'A',67,ROK_F.KOD,OP.WAL().SLU().NAZ,OP.WAL().KOD,
                                           OP.AN,OP.SYM,OKRO_F.NR,,,,,OP.ODD().OD,,,,,,OP.SYM_ROK)
              ?};
              {? ROZNE.UT_ROZ2
              || KS.cntx_psh();
                 KS.index('SYM'); KS.prefix(ROK_F.ref(),ROK_F.SYNT+OP.AN);
                 {? KS.first() & KS.ROZR<>'Z' & KS.OBTYPROZ & OP.TYP<>KS.T_ROZ
                 || {? OP.TYP<>'RMK' & KS.T_ROZ<>'RMK'
                    || _opis:='Typ identyfikatora niezgodny z definicją konta w rozrachunku '+OP.SYM+' ('+OP.WAL().KOD+')';
                       _lokal:='Rok '+ROK_F.NAZ+' konto '+OP.AN+' jednostka księgowa '+OP.ODD().OD;
                       exec('tab_add','sys_util',_opis,_lokal,'A',45,ROK_F.KOD,OP.WAL().SLU().NAZ,OP.WAL().KOD,
                                                 OP.AN,OP.SYM,OKRO_F.NR,0,0,,,OP.ODD().OD,KS.T_ROZ,,,,,OP.SYM_ROK)
                    || _opis:='Typ identyfikatora niezgodny z definicją konta w rozrachunku '+OP.SYM+' ('+OP.WAL().KOD+')';
                       _lokal:='Rok '+ROK_F.NAZ+' konto '+OP.AN+' jednostka księgowa '+OP.ODD().OD;
                       exec('tab_add','sys_util',_opis,_lokal,'N',46)
                    ?}
                 ?};
                 KS.cntx_pop()
              ?};
              OP.next()
           !}
         ?};
         POZ.cntx_psh(); OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref);
         OP.cntx_psh(); OP.index('KON_O');
         {? RMK.first()
         || {! |?
               {? RMK.ODD=null
               || _opis:='Nie związany z żadną pozycją zapis w tabeli RMK';
                  _lokal:='Rok '+ROK_F.NAZ+' konto '+RMK.KONP+' symbol '+RMK.SYM;
                  exec('tab_add','sys_util',_opis,_lokal,'A',71,ROK_F.KOD,RMK.KONP,RMK.SYM,'',,OKRO_F.NR,,,,,,,,,,,RMK.SYM_ROK)
               || OP.prefix(FINFO.NAROD,RMK.ODD,RMK.KONP,RMK.SYM,RMK.SYM,RMK.SYM_ROK);
                  _zn_poz:=OP.first();
                  {? ~_zn_poz & OKRO_F.first
                  || {! |?
                        POZ.use('pozy'+ROK_F.KOD+form(OKRO_F.NR,-2));
                        POZ.index('KON'); POZ.prefix(RMK.KONP);
                        {? POZ.first()
                        || {! |?
                              {? POZ.ID=RMK.SYM & -POZ.TID='rmk' & POZ.ODD=RMK.ODD || _zn_poz:=1 ?};
                              ~_zn_poz & POZ.next()
                           !}
                        ?};
                        ~_zn_poz & OKRO_F.next
                      !}
                  ?};
                  {? ~_zn_poz
                  || _opis:='Nie związany z żadną pozycją zapis w tabeli RMK';
                     _lokal:='Rok '+ROK_F.NAZ+' konto '+RMK.KONP+' symbol '+RMK.SYM+' jednostka księgowa '+RMK.ODD().OD;
                     exec('tab_add','sys_util',_opis,_lokal,'A',69,ROK_F.KOD,RMK.KONP,RMK.SYM,RMK.ODD().OD,,OKRO_F.NR,,,,,,,,,,,RMK.SYM_ROK)
                  ?}
               ?};
               RMK.next()
            !}
         ?};
         POZ.cntx_pop(); OP.cntx_pop()
      ?};
      ROK_F.ref()<>ROZNE.UT_ROKDO & ROK_F.next()
   !}
?};
OP.cntx_pop(); ZAP_OP.cntx_pop(); STANY.cntx_pop(); RMK.cntx_pop(); ROK_F.cntx_pop(); OKRO_F.cntx_pop();
ZAP_OP.ndx_drop()


\ut_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Dodawanie rekordu do tabeli tymczasowej - odtworzenie korespondencji seryjnej
::----------------------------------------------------------------------------------------------------------------------
_opis:='Odtwarzanie nagłówków korespondencji seryjnej'; _lokal:='';
exec('tab_add','sys_util',_opis,_lokal,'A',101)


\ut_budan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.70]
:: OPIS: Dodawanie rekordu do tabeli tymczasowej - odtworzenie budowy kont analitycznych
::----------------------------------------------------------------------------------------------------------------------
_opis:='Odtwarzanie budowy kont analitycznych'; _lokal:='';
exec('tab_add','sys_util',_opis,_lokal,'A',102)


\ut_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2011]
:: OPIS: Kasowanie tabel pomocniczych
::----------------------------------------------------------------------------------------------------------------------
_opis:='Kasowanie tabel pomocniczych'; _lokal:='';
exec('tab_add','sys_util',_opis,_lokal,'A',105)


\ut_dok_pole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS:
::----------------------------------------------------------------------------------------------------------------------
ROZNE.UT_DOK


\ut_roz_pole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS:
::----------------------------------------------------------------------------------------------------------------------
ROZNE.UT_ROZR


\ut_zn_pole
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS:
::----------------------------------------------------------------------------------------------------------------------
ROZNE.UT_DOK | ROZNE.UT_KS


\ut_sp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [18.42]
:: OPIS: Odtwarzanie znaczników SP
::----------------------------------------------------------------------------------------------------------------------
_opis:='Znaczniki dla SP'; _lokal:='';
exec('tab_add','sys_util',_opis,_lokal,'A',106)


:: typy_usterek_opis
:*****************************************************************************
:W nawiasie parametry przekazane dla znalezienia i ewentualnego pozniejszego poprawienia bledu
:
:************************sprawdzanie dokumentow************************
:1   - brak pozycji w dokumencie
:2   - nieprawidlowa numeracja pozycji w dokumencie
:3   - sprawdzenie wypelnienia pola komentarz w pozycji dokumentu (czy nie jest puste)
:4   - sprawdzenie bilansowania sie dokumentu
:5   - sprawdzenie wypelnienia pole jednostka ksiegowa w dokumencie - czy nie jest puste
:6   - brak pozycji VAT w dokumencie VAT lub SAD
:7   - sprawdzenie daty operacji w dokumencie
:8   - sprawdzenie daty dokumentu
:10 - sprawdzenie czy POZ.DATA_R=exec('dat_otw','automat') w dokumencie VAT lub SAD
:11 - sprawdzenie czy POZ.TP=DOK.D3 w dokumencie VAT lub SAD
:12 - sprawdzenie czy czy suma jest zaokraglona do 2 miejsc po przecinku
:13 - sprawdzenie czy czy suma w walucie jest zaokraglona do 2 miejsc po przecinku
:14 - sprawdzenie wypelnienia pol walutowych w pozycji dokumentu (suma w walucie i kurs)
:15 - sprawdzanie, czy dokument ma swoj symbol
:16 - sprawdzenie poprawnosci konta ksiegowego w pozycji dokumentu
:17 - sprawdzenie, czy konto ksiegowe w pozycji dokumentu nie jest zablokowane
:18 - sprawdzenie danych rozrachunkowych w pozycji dokumentu
:19 - sprawdzenie, czy termin platnosci jest rowny lub wiekszy dacie otwarcia w pozycji dokumentu
:20 - sprawdzenie poprawnosci wypelnienia kodow wyroznikow dla pozycji
:21 - brak rekordu w RMK dla pozycji dokumentu z rozrachunkiem typu R
:22 - sprawdzenie wypelnienia pol w tabeli RMK
:23 - sprawdzenie czy POZ.SUM$2<>0
:24 - sprawdzenie znacznikow (akceptacji, ksiegowania) w dokumencie
:25 - sprawdzenie wypelnienia pole jednostka ksiegowa w dokumencie - czy jest zgodna z jednostka ksiegowa rejestru ksiegowego
:26 - sprawdzenie wypelnienia pola komentarz w pozycji dokumentu (czy jest prawidlowo wypelnione wzgledem POZ.KON)
:27 - sprawdzenie czy pole VPOZ.GRVAT nie jest puste i jest prawidlowo wypelnione wzgledem grup podatkowych dla rejestru
:28 - sprawdzenie czy typ identyfikatora rozrachunku zgodny jest z definicja konta
:29 - sprawdzenie zgodnosci pol OKRVAT i A_VAT w tabelach DOK i VPOZ
:30 - sprawdzenie zgodnosci pol OKRVAT i A_VAT w tabelach DOK i VPOZ szczegolny przypadek
:
:************************sprawdzanie obrotow na kontach************************
:31 - sprawdzenie, czy konta nie mozna usunac (brak zapisow w POZ z roku tego konta)
:32 - sprawdzenie obrotow na kontach analitycznych
:33 - sprawdzanie, czy nie powinno byc rekordu w tabeli obrotow z zerowymi sumami
:
:************************sprawdzanie ksiegowan************************
:-----------------------------------vat---------------------------------------------
:36 - sprawdzenie dokumentow z VAT do rozliczenia
:37 - sprawdzenie dokumentow VAT - czy istnieje dokument VAT
:38 - spr. dok. VAT - czy dla dokument liczba VPOZ = liczbie PVAT
:39 - sprawdzenie DOK z DVAT i VPOZ z PVAT
:--------------------------------rozrachunki----------------------------------------
:40 - istnienie rozrachunku dla pozycji dokumentu
:41 - istnienie zapisu na rozrachunku dla pozycji dokumentu
:42 - istnienie kilku zapisow na rozrachunku dla jednej pozycji dokumentu
:43 - sprawdzenie wypelnienia pol OP.DO i OP.TZ w porownaniu do pozycji dokumentu ksiegowego
:44 - sprawdzenie wypelnienia pol tabeli ZAP_OP - REJ, NR, DKS, NK, POZ, KURS, TYP, WAL2
:45 - sprawdzenie czy typ rozrachunku zgodny jest z definicja konta (ale gdy nie jest ani biezacy ani wymagany RMK)
:46 - sprawdzenie czy typ rozrachunku zgodny jest z definicja konta (biezacy lub wymagany w koncie sytetycznym jest RMK)


:-----------------------------------istnienie kont---------------------------------------------
:50 - sprawdzenie istnienia konta analitycznego dla pozycji dokumentu (waluta narodowa)
:
:************************kartoteki VAT************************
:51 - ciaglosc dokumentow VAT (DVAT)
:52 - ciaglosc pozycji dokumentow VAT (PVAT)
:53 - sprawdzenie wypelnienia pola DVAT.DOK
:
:************************komentarze kont syntetycznych************************
:60 - brak komentarza systemowego konta syntetycznego
:
:************************kartoteki rozrachunkow************************
:65 - nieprawidlowe wartosci w OP.WN i OP.MA
:66 - nieprawidlowa wartosc w polu OP.STAN
:67 - nieprawidlowe STANY rozrachunku
:68 - sprawdzenie, czy nie ma ZAP_OP-ow nie powiazanych z POZ
:69 - sprawdzanie, czy nie ma wiszacych RMK (z wypelnionym polem RMK.ODD)
:70 - sprawdzanie pol ZAP_OP.WN,ZAP_O.MA
:71 - sprawdzanie, czy nie ma wiszacych RMK (bez wypelnionego pola RMK.ODD)
:
:101 - odtwarzanie korespondencji
:************************odtwarzanie budowy kont analitycznych************************
:102 - odtwarzanie budowy kont analitycznych
:105 - kasowanie tabel pomocniczych
:Zdarzenia od numeru 100 musza byc na koncu, w dowolnej kolejnosci

:Sign Version 2.0 jowisz:1045 2023/12/22 11:36:03 5f993043b30c78220a88d351cb4ab3faa7310e5cddc706ff3abe9f928d3a074e59103f752e76a9994e06a40c5f71c3cea86c706bca2fa7141a16c8b0fac81ca61292e89bc6781579c12990cde35e2eb2514be95e10323eb94a75a5e35c6235217ccc70e6bea77db7669eac98f84c114c01ff53142d70522a20f86042d327d641

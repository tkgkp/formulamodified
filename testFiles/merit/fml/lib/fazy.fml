:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: fazy.fml
:: Utworzony: 05.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły do obsługi faz produkcji (biblioteka wspólna dla TTE_TEC, TTE_WTE, TTE_PZL)
::======================================================================================================================


\a_pfaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Blankowanie pola PFAZ w różnych tabelach
::----------------------------------------------------------------------------------------------------------------------
VAR.A_PFAZ


\bl_pfaz_wyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2011]
:: OPIS: Wartość początkowa pola PFAZ.WYD
::  OLD: \bl_pfaz_wyd/produ1.fml
::----------------------------------------------------------------------------------------------------------------------
{? cli_ver()<>'cgi' & cur_tab(1,1)=TOPER
||
   TOPER.NRK().JORG
||
::   exec('st_a_wyd','tte')
   VAR.BL_WYD
?}


\poredwy
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Po redakcji pola PFAZ.WYD
::  OLD: \poredwy/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
{? ST.A_WYD=null() | ST.A_WYD=PFAZ.WYD
|| 1
|| {? PFAZ.WYD<>pfaz
   || FUN.ask('Faza produkcji dotyczy innego wydziału niż aktywny.\n\nCzy na pewno chcesz wpisać?'@)
   || 1
   ?}
?}


\pfazdol
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Przed dołączaniem w oknie selekcji faz produkcji
::  OLD: \pfazdol/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
pfaz:=null();
1


\pfaz_po
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Po popraw i dołącz w oknie selekcji faz produkcji
::  OLD: \pfaz_po/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
{? PFAZ.f_active()>0 || PFAZ.f_rfresh() ?};
&pfaz;
1


\pfazpop
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Przed popraw w oknie selekcji faz produkcji
::  OLD: \pfazpop/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
{? ST.A_WYD<>PFAZ.WYD & PFAZ.WYD<>null() & ST.A_WYD<>null()
|| FUN.emsg('Faza produkcji dotyczy innego wydziału niż aktywny.'@)
?};
pfaz:=PFAZ.WYD;
1


\usun_pfaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Usunięcie PFAZ
::  OLD: \usun_pfaz/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
_link:=PFAZ.testlink(2);
{? _link.first()
|| FUN.emsg('Usunięcie nie jest możliwe.\n\nWskazana faza produkcji jest już użyta w systemie.'@);
   0
|| {? FUN.ask('Czy usunąć wskazaną fazę?'@)
   || PFAZ.del()
   ?}
?};
~~


\prn_pfaz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [7.53] OWwPR013/5.1
:: OPIS: wydruk słownika faz produkcji
::  OLD: \prn_pfaz/drukujp.fml
::----------------------------------------------------------------------------------------------------------------------
rep_exec('tte_drukpfaz',,1);
1


\pfaz_filter
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Filtr programowy dla faz produkcji
::   WE: _a - 0 (wszystkie), 1 (z wydziału VAR.A_WYD i nieprzypisane)
::  OLD: \pfaz_filter/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a
|| {? VAR.A_WYD=null()
   || PFAZ.f_set('KOD');
      FILTER.PFAZ:=0
   || PFAZ.f_set('KOD',,'"PFAZ"."WYD"=:_a or "PFAZ"."WYD" is null',VAR.A_WYD);
      FILTER.PFAZ:=1
   ?}
|| PFAZ.f_set('KOD');
   FILTER.PFAZ:=0
?};
exec('pfaz_title','fazy');
~~


\pfaz_title
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Dokleja tytuł do okienka (zgodnie z ustawionym filtrem)
::----------------------------------------------------------------------------------------------------------------------
PFAZ.hdr_sel();
_tail:=
   {? FILTER.PFAZ=0
   || 'wszystkie'@
   |? FILTER.PFAZ=1
   || 'z wydziału %1 i nieprzypisane'@[VAR.A_WYD().KOD]
   || ''
   ?};
PFAZ.hdr_sel(' '+_tail);
''


\pfazarec
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [2010]
:: OPIS: Pekord-po dla faz produkcji
::   WE: _a - specyfikacja testu: 1-poprawianie, 0-dołączanie
::   WY: akronim pola / ''
::  OLD: \pfazarec/slownik1.fml
::----------------------------------------------------------------------------------------------------------------------
_chk:=__CHK.record(PFAZ,,'KOD','OPIS');
{? _chk=''
|| {? __CHK.index(PFAZ,_a)=''
   || ''
   || 'KOD'
   ?}
|| _chk
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 2fb0a1a8fc01c40e0ca9c98c993873990fd3bf9d53a8f7dd53e623b4556872611a6a0eb03db11f2a88b52b030eeef82b243d88c9603887facd8eaf13ee45c272473d3d8033ca442d703f685e661e66c3e3393c2d3aa328f2d21f689be9dcc4b2d52ca8858e6496a376b20141982fe3588b16a5dd4bcaa3502324e34d908bec43

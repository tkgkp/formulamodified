:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: staletrn.fml
:: Utworzony: 2011/05/10
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Funkcje odpowiedzialne za przeniesienie danych z archiwalnych wersji.
::======================================================================================================================


\exp_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws
:: OPIS: Zapisuje do pliku istniejącą historie stałych i dane z tabel ?INFO. Wywołanie maksymalnie dla wersji 11.22.
::       Dla wersji 12.10 i późniejszych działanie zostanie zakończone po zgłoszeniu bledu. Do poprawnego działania
::       wymagane sa pliki *.tra zawierające informacje o konfiguracji stałych systemu.
::   WY: wynik metody export zapisującej historie wartości stałych do pliku tekstowego
::----------------------------------------------------------------------------------------------------------------------
: utwórz tabele pomocnicze i ustal nazwy plików
_buf:=exec('imex_buf','#stalesys');
_naz:=exec('imex_naz','#stalesys');

: wczytaj konfiguracje stałych systemu
{? ~exec('rd_cfg','#stalesys',_buf,_naz)
|| return(0)
?};

_DEF:=_buf[1];
_MAP:=_buf[2];
_WAR:=_buf[5];

: uwzględnij historie stałych
: (przepisz jako wartości KST)
KST_WAR.clear;
_loop:=KST_WAR.first;
{!
|? _loop
|! {? _MAP.find_tab(0,'ZES',,'=','KST','MAP',,'=',KST_WAR.POLE)
   || _WAR.blank;
      _WAR.DEF:=_MAP.DEF;
      _WAR.DAT:=KST_WAR.DATA;
      _WAR.WAR:=KST_WAR.WARTOSC;
      _WAR.add
   ?};
   _loop:=KST_WAR.next
!};

: uzupełnij o wartości z tabel stałych
: przeglądaj definicje wszystkich stałych
_loop:=_DEF.first;
{!
|? _loop
|! _def:=_DEF.SYMBOL;
   _WAR.prefix(_def,_def);
:  uzupełniaj tylko wtedy, gdy dla stałej
:  nie zapisano jeszcze żadnej wartości
   {? ~_WAR.first
   || _MAP.prefix(_def,_def);
      _loop:=_MAP.first;
      _done:=0;
      {!
      |? _loop & ~_done
      |! _zes:=_MAP.ZES;
         {? var_pres(_zes)=type_of(SYSLOG)
         || _ZES:=($_zes)();
            _map:=_MAP.MAP;
            {? _ZES<>KST
            || ("_a.last")(_ZES)
            ?};
            {? var_pres(_map,_ZES)>0
            || _war:=($('_a.'+_map))(_ZES);
               _WAR.blank;
               _WAR.DEF:=_def;
               _WAR.DAT:=date(0,0,0);
:              zamień wartość na treść formuły
               _WAR.WAR:=exec('wartosc','#stalesys',_war,_zes,_map);
               _WAR.add;
:              wartość odpisana
:              pomiń pozostałe
               _done:=1
            ?};
            &_ZES
         ?};
:        źródło wartości zostało odrzucone
:        sprawdź kolejne, o ile istnieje
         _loop:=_MAP.next
      !}
   ?};
   _loop:=_DEF.next
!};

_WAR.clear;
: zapisz wartości stałych
_WAR.export(_naz[5],0,'^',,1024,
   'DEF',,1,,
   'DAT',,2,,
   'WAR',,3,
)

:Sign Version 2.0 jowisz:1028 2019/06/07 16:00:01 40cd05ca003bd826b66ebe147a1567ae9b83ad87830ae7b3d4cfd264fb29741bbbae560401ff46828d170bcb1ab80340d5b59a01b742d215d2ee0965e2d086f33fcd1f1a11e369c1b0492b57ce35735c4e98227172c44b9aebd5167d03b53eba6d8b414eaca5d088aad7878181f4baa1a195bdfef39389b4a587ab9f1576e82a

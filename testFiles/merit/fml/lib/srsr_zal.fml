:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: srsr_zal.fml [2015]
:: Utworzony: 02.12.2015
:: Autor: PJ
::======================================================================================================================
:: Zawartosc: Obsługa załączników w kartotece środków trwałych
::======================================================================================================================


\files_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Wypelnienie tabeli z multizalacznikami dla tabeli DOKUM
::   WE: _a - opcjonalnie, informacja że dziedzina tabeli TST jest pusta (_a=1) lub nie jest pusta (_a=0)
::   WY: _zal - tabela tymczasowa przekazywana do kontrolki
::----------------------------------------------------------------------------------------------------------------------
_zal:=exec('tab_tmp','srsr_zal');
_zal.blank();
_tab:=($(__CTR.TAB))();
_btab:=($(__CTR.BTAB))();
{? _=0 | _a=0
|| {? _tab.f_active() & _tab.sel_size()=0
   || _fget:=_tab.f_get()
   || _fget:=_tab.get()
   ?}
|| _fget:=0
?};
{? _fget
||
   ctr_set(__CTR.WIN_ID,'ctr_id','setEnabled',1);
   {? _tab=DOK
   || _btab.index('TYP');
      _btab.prefix($_tab.ref())
   |? _tab=PROJEKTY
   || _btab.index('PRO_E');
      _btab.prefix(_tab.FIRMA,_tab.RODZIC,0,((_tab.POZ_PROJ*3)+_tab.KOL))
   || _btab.prefix(REF.FIRMA,$_tab.ref())
   ?}
||
   ctr_set(__CTR.WIN_ID,'ctr_id','setEnabled',0);
   _btab.prefix(REF.FIRMA,'@!@!@!@!@!@!@!@!')
?};
{? _btab.first()
||
   {! |?
      {? _btab=DOKUM & DOKUM.BL='T'
      || _btab.next()
      || {? ($(__CTR.BTAB+'.'+__CTR.BBLOB))() || _size:=_btab.bl_info(__CTR.BBLOB,'SIZE') || _size:=0 ?};
         {? _btab.NAZWA='' || _btab.NAZWA:='nofile' ?};
         _zal.NAME:=_btab.NAZWA;
         _zal.SIZE:=_size;
         _zal.MOD_DATE:=((_btab.DATA) $ 1 )+' '+ (_btab.CZAS) $ 1;
         _zal.INT_DATA:=#_btab.ref();
         _zal.KR_OP:=_btab.KR_OP;
         _zal.add();
         _btab.next()
      ?}
   !}
?};
return(_zal)


\bobs_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG
:: OPIS: Przed obsluga DOKUM
::   WE: _a - czy tylko do odczytu (1) czy nie (0)
::----------------------------------------------------------------------------------------------------------------------
{? ~(($(__CTR.TAB+'.get'))())
|| __CTR.DND:=0;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar')
|? _a
|| __CTR.DND:=0;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   _btab:=($(__CTR.BTAB))();
   {? _btab.size()
   ||
      ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarAction','setview','','xwin16.png:47','Widok',0,0);
      ctr_set(__CTR.WIN_ID,'ctr_id','addToolbarSeparator');
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'view','P&OKAŻ','','Pokazanie dokumentu',1,1);
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'save','&Zapisz','','Kopiowanie dokumentu na wybraną lokalizację',2,0);
      ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'disp','&Właściwości','','Wyświetlenie większej ilości informacji',1,0);
      {? __CTR.TAB='DOK'
      || ctr_set(__CTR.WIN_ID,'ctr_id','addAction', 'arch','&Przeglądaj archiwum','','Przeglądanie archiwum',0,0)
      ?}
   ?};
   ctr_call(__CTR.WIN_ID,'ctr_id','initToolbar')
|| __CTR.DND:=1;
   ctr_set(__CTR.WIN_ID,'ctr_id','removeAllActions');
   exec('initf','srsr_zal',__CTR.WIN_ID)
?}


\mk_ctr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Utworzenie okienka z kontrolka
::   WE: _a - identyfikator dla okienka
::       _b - sposob wyszukiwania kontrolki
::   WY: _win - akronim utworzonego okienka z kontrolka
::----------------------------------------------------------------------------------------------------------------------
_w_ident:=_a;
_btab:=($(__CTR.BTAB))();
_win:=_btab.mk_ctr('Załączniki'@,_w_ident,1,15,125,10);
__win:=-(1-_win);
__CTR.WIN_ID:=_b+__win;
_btab.win_cctr(_win, 'ctr_id', '@filelistpanel', 'dnd');
_form:=$('exec(\'callb\',\'srsr_zal\',\''+__CTR.WIN_ID+'\')');
_btab.win_cfml(_win,'ctr_id','CONTROL_CALLBACK',_form);
_form:=$('exec(\'dsk_ini\',\'srsr_zal\',\''+__CTR.WIN_ID+'\')');
_btab.win_cfml(_win,'ctr_id','CONTROL_INIT',_form);
return(_win)


\callb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Obsluga akcji w kontrolce
::   WE: _a - identyfikator kontrolki
::----------------------------------------------------------------------------------------------------------------------
_win_id:=_a;
__CTR.WIN_ID:=_win_id;
__select:=0;
{? ctr_info('action_id') = 'dnd' & __CTR.DND
|| {? exec('blob_add','srsr_zal',2)
   || exec('getblobs','srsr_zal',_win_id)
   ?}
|? ctr_info('action_id') = 'add'
|| {? exec('blob_add','srsr_zal',1)
   || exec('getblobs','srsr_zal',_win_id)
   ?}
|? ctr_info('action_id') = 'del'
|| {? FUN.ask('Czy usunąć zaznaczone załączniki?'@)
   || _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
      exec('dellist','srsr_zal',_tab1);
      exec('getblobs','srsr_zal',_win_id)
   ?}
|? ctr_info('action_id') = 'save'
|| {? (_dir:=exec('get_dir','srsr_zal')) <> '' | exec('interm','#system')
   || _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
      exec('savelist','srsr_zal',_tab1, _dir);
      exec('getblobs','srsr_zal',_win_id)
   ?}
|? ctr_info('action_id') = 'arch'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('archive_view','dokum','ZAL');
   exec('getblobs','srsr_zal',_win_id)
|? ctr_info('action_id') = 'view'
|| _isrun:=exec('cli_functions','#system');
   {? ~_isrun || FUN.emsg(exec('indevice_nacc_msg','#system'));return() ?};
   _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('view','srsr_zal',_tab1)
|? ctr_info('action_id') = 'copy'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('copy','srsr_zal',_tab1)
|? ctr_info('action_id') = 'scan'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('scan','srsr_zal',_tab1);
   exec('getblobs','srsr_zal',_win_id)
|? ctr_info('action_id') = 'put'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('blob_put','srsr_zal',_tab1);
   exec('getblobs','srsr_zal',_win_id)
|? ctr_info('action_id') = 'edit'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('edit','srsr_zal',_tab1);
   exec('getblobs','srsr_zal',_win_id)
|? ctr_info('action_id') = 'disp'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('disp','srsr_zal',_tab1)
|? ctr_info('action_id') = 'print'
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   exec('print','bloby',_tab1)
|? ctr_info('action_id') = 'setview'
|| {? ctr_call(_win_id,'ctr_id','getViewName')='icons'
   || _view:='table'
   || _view:='icons'
   ?};
   ctr_call(_win_id,'ctr_id','setView',_view)
|? var_press('SELAKC',__CTR)>0
|| _tab1:=ctr_call(_win_id,'ctr_id','getCurrActionItems');
   _sel:=exec('find','srsr_zal',_tab1);
   {? __CTR.SELAKC(ctr_info('action_id'),_sel)
   || exec('getblobs','srsr_zal',_win_id)
   ?}
?};
{? __select
|| exec('selectitem','srsr_zal',_tab1)
?}


\initf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: utworzenie belki z akcjami dla kontrolki
::   WE: _a - identyfikator kontrolki
::----------------------------------------------------------------------------------------------------------------------
_Interm:=exec('interm','#system');
ctr_set(_a,'ctr_id','addToolbarAction','setview','','xwin16.png:47','Widok',0,0);
ctr_set(_a,'ctr_id','addToolbarSeparator');
{? _Interm
|| ctr_set(_a,'ctr_id','addAction', 'add','','@system|add-circle','Dołączanie załącznika',0,0);
   ctr_set(_a,'ctr_id','addMenuAction','add','&Dołącz',0,0);
   ctr_set(_a,'ctr_id','addAction', 'put','','@system|mode-edit','Poprawianie załącznika',1,0);
   ctr_set(_a,'ctr_id','addMenuAction','put','&Popraw',1,0);
   ctr_set(_a,'ctr_id','addAction', 'del','','@system|delete','Usuwanie załącznika',2,0);
   ctr_set(_a,'ctr_id','addMenuAction','del','&Usuń',2,0)
|| ctr_set(_a,'ctr_id','addAction','add','&Dołącz','','Dołączanie załącznika',0,0);
   ctr_set(_a,'ctr_id','addAction','put','&Popraw','','Poprawianie załącznika',1,0);
   ctr_set(_a,'ctr_id','addAction','del','&Usuń','','Usuwanie załącznika',2,0)
?};
::ctr_set(_a,'ctr_id','addAction', 'scan','&Skanuj','','Skanowanie dokumentu źrodłowego',0,0);
{? var_press('ADDAKC',__CTR)>0
|| __CTR.ADDAKC()
?};
ctr_set(_a,'ctr_id','addAction', 'view','P&OKAŻ','','Pokazanie dokumentu',1,1);
ctr_set(_a,'ctr_id','addAction', 'edit','&Redaguj','','Redagowanie dokumentu',1,0);
ctr_set(_a,'ctr_id','addAction', 'save','&Zapisz','','Kopiowanie dokumentu na wybraną lokalizację',2,0);
{? __CTR.TAB='DOK'
|| {? _Interm
   || ctr_set(_a,'ctr_id','addToolbarAction', 'print','','@system|print','Wydruk dokumentu',1,0)
   || ctr_set(_a,'ctr_id','addAction', 'print','&Drukuj','','Wydruk dokumentu',1,0)
   ?};
   ctr_set(_a,'ctr_id','addAction', 'arch','&Przeglądaj archiwum','','Archiwizacja dokumentu',0,0)
?};
ctr_set(_a,'ctr_id','addAction', 'copy','Kop&ia','','Wykonanie kopii dokumentu',1,0);
ctr_set(_a,'ctr_id','addAction', 'disp','&Właściwości','','Wyświetlenie większej ilości informacji',1,0);
ctr_call(_a,'ctr_id','initToolbar')


\copy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Tworzenie kopii zalacznika
::   WE: _a - tabela z rekordem do obslugi
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   ||
      {? _btab.r_lock(1)
      ||
         _btab.cntx_psh();
         _ref:=#_btab.ref();
         _file:=fopen(($(__CTR.BTAB+'.'+__CTR.BBLOB))(), 'b', 0, 1, 1, 0);
         {? ~_file.is_open
         || FUN.emsg('Błąd otwarcia pliku'@)
         ||
            _edit:=_btab.mk_edit('Załącznik'@,,'btab',1,1);
            _btab.win_efld(_edit,,'KR_OP',,,80,,,'Krótki opis'@);
            _btab.win_efld(_edit,,'NAZWA',,,80,,,'Nowa nazwa załącznika'@);
            _btab.win_edit(_edit);
            {? _btab.edit("{? exec('chk_blob','srsr_zal',cur_tab(1,1).NAZWA)=0 || 'NAZWA' || 1 ?}")
            ||
               do();
               _btab.DATA:=date();
               _btab.CZAS:=time();
               {? _btab.add()
               || _btab.bl_put(__CTR.BBLOB,_file,,,_btab.NAZWA);
                  _ref:=#_btab.ref();
                  __select:=1
               ?};
               end()
            ?}
         ?};
         &_file;
         _btab.cntx_pop();
         _btab.r_unlock();
         {? __select
         ||
            exec('getblobs','srsr_zal',__CTR.WIN_ID);
            ctr_set(__CTR.WIN_ID,'ctr_id','selectItem',_ref, '', 1)
         ?};
         __select:=0
      ?}
   || FUN.emsg('Załącznik został usunięty.'@);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\addblobs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Dodanie nowych zalacznikow
::   WE: _a - tabela z wybranymi plikami
::       _b - opis dla pola OPIS tabeli z zalacznikami
::----------------------------------------------------------------------------------------------------------------------
_blobs:=_a;
_opis:=_b;
{? ~_blobs.first() || return() ?};
{! |?
   {? __CTR.BTAB='DOKUM'
   ||
      POM.TAB:='DOKUM';
      POM.TYPDOK:='SYS';
      exec('add_grnr','numery','SYS');
      DOKUM.blank(1);
      DOKUM.TYP:='I';
      DOKUM.BSEND:='N';
      DOKUM.KR_OP:=_b;
      {? __CTR.TAB='SRSR'
      || {? SRSR.GRP='E'
         || _ref:=$(exec('get_srsr_root','fst',SRSR.ref()))
         || _ref:=$SRSR.ref()
         ?}
      |? __CTR.TAB='DOK'
      || _ref:=$DOK.ref()
      |? __CTR.TAB='PROJEKTY'
      || DOKUM.PROJEKTY:=PROJEKTY.ref();
         DOKUM.TYP:='D';
         _ref:=($('$'+__CTR.TAB+'.ref()'))()
      || _ref:=($('$'+__CTR.TAB+'.ref()'))()
      ?};
      DOKUM.REFSQL:=_ref;
      DOKUM.FIRMA:=REF.FIRMA;
      {? DOKUM.add()
      || DOKUM.NR:=exec('numer_new','numery','PACZKA');
         DOKUM.memo_set(,'OPIS');
         exec('znak','numery','DOKUM');
         DOKUM.CZAS:=time();
         DOKUM.DATA:=date();
         DOKUM.DATAKONT:=date();
         DOKUM.bl_file('DOKUM',1);
         _file:=_blobs.FILENAME;
         {? _file<>''
         ||
            DOKUM.bl_put('DOKUM',_blobs.DIR+'/'+_blobs.FILENAME,,,,);
            DOKUM.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
            DOKUM.put();
            {? __CTR.TAB='PROJEKTY'
            || DOKUMZ.cntx_psh();
               DOKUMZ.index('DOK');
               DOKUMZ.prefix();
               DOKUMZ.blank();
               DOKUMZ.DOK:=DOKUM.ref();
               {? exec('upgrade2325_blbc1','zbl')
               || DOKUMZ.REFSQL:=DOKUM.REFSQL
               ?};
               DOKUMZ.DOKUM:=DOKUM.DOKUM;
               DOKUMZ.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
               DOKUMZ.add();
               DOKUMZ.cntx_pop()
            ?}
         ?}
      ?}
   ?};
   _blobs.next()
!}


\getblobs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Pobranie zalacznikow
::   WE: _a - identyfikator kontrolki, _b - opcjonalnie informacja że tabela TST jest pusta
::----------------------------------------------------------------------------------------------------------------------
_tab:=exec('files_data','srsr_zal',{? _>1 || _b || 0 ?});
ctr_tab('files_data', _a, 'ctr_id', '', _tab);
ctr_call(_a,'ctr_id','updateItems', 'files_data')


\dsk_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Wstepne ustawienia dla kontrolki
::   WE: _a - identyfikator kontrolki
::----------------------------------------------------------------------------------------------------------------------
__CTR.WIN_ID:=_a;
_tab:=exec('tab_tmp','srsr_zal');
ctr_tab('files_data', _a, 'ctr_id', '', _tab);
ctr_call(_a,'ctr_id','setSortColumn','MOD_DATE',1);
ctr_call(_a,'ctr_id','setColumnWidth','KR_OP',50);
ctr_call(_a,'ctr_id','updateItems', 'files_data')


\dellist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja usuniecia zalacznikow
::   WE: _a - tabela z elementami do usuniecia
::----------------------------------------------------------------------------------------------------------------------
_dtab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _dtab.first()
|| {!
   |? {? _btab.seek(_dtab.INT_DATA,)
      || {? __CTR.BTAB='DOKUM'
         || exec('dokumz_del','dokum_zal')
         ?};
         _btab.del()
      ?};
      _dtab.next()
   !}
?};
_btab.cntx_pop()


\get_dir
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Wybor katalogu
::   WY: sciezka z wybranym katalogiem lub ciag pusty w przypadku rezygnacji
:: ~OST: INFILECHOOSER
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')
|| return('')
|| ctr_set('!application', 'filechooser','reset');
   ctr_set('!application', 'filechooser','setFileSelectionMode','DIRECTORIES_ONLY');
   ctr_set('!application', 'filechooser','setDialogTitle', 'Wybór katalogu');
   _ret:=ctr_call('!application', 'filechooser','showSaveDialog');
   {? _ret
   || return(ctr_call('!application', 'filechooser','getSelectedFile'))
   || return('')
   ?}
?}


\savelist
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Zapisywanie plikow do katalogu
::   WE: _a - tabela z lista plikow
::       _b - katalog do zapisu
:: ~OST: INFEXISTS
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_dir:=_b;
_group:=0;
{? _tab.size()>1
|| _group:=1
?};
{? _group>0
|| KOMM.init(250,,'Zapisywanie załączników'@)
?};
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? exec('interm','#system')
   || {? _group
      || _tmp_dir:=fmk_tmp_dir(0);
         {? type_of(_tmp_dir)<>type_of(~~)
         || _pth:=_tmp_dir.get_path();
            {!
            |? {? _btab.seek(_tab.INT_DATA,,1)
               || _what:='';
                  _blob_ref:=($(__CTR.BTAB+'.'+__CTR.BBLOB))();
                  {? _blob_ref<>null()
                  || _what:=_btab.bl_info(__CTR.BBLOB,'NAME')
                  ?};
                  {? exec('blk_lock','#table',__CTR.BTAB,_btab.ref(),,1,'Załącznik: %1 jest redagowany.'@[_what],,_group)
                  || {? _blob_ref<>null()
                     || _name:=_btab.bl_info(__CTR.BBLOB,'NAME');
                        _file:=_pth+exec('sep','#file',2)+_name;
                        _can_continue:=_btab.bl_get(__CTR.BBLOB,_file,0)
                     || KOMM.add('Brak pliku w bazie danych. Załącznik jest pusty.'@)
                     ?};
                     exec('blk_unlock','#table',__CTR.BTAB,_btab.ref())
                  ?}
               ?};
               _tab.next() & _can_continue>0
            !}
         || _can_continue:=0;
            KOMM.add('Nie udało się utworzenie katalogu tymczasowego po stronie serwera.'@,2,,1)
         ?};
         {? _can_continue>0
         || _tmp_dir2:=fmk_tmp_dir(0);
            {? type_of(_tmp_dir2)<>type_of(~~)
            || _pth2:=_tmp_dir2.get_path();
               _name:='Załączniki'@+'_'+$SYSLOG.tm_stamp()+'.zip';
               _arch:=_pth2+exec('sep','#file',2)+_name;
               _can_continue:=fpack_add(_arch,_pth);
               {? _can_continue>0
               || dlg_save(_arch,0,_name)
               ?}
            || _can_continue:=0;
               KOMM.add('Nie udało się utworzenie katalogu tymczasowego po stronie serwera.'@,2,,1)
            ?}
         ?}
      || {? _btab.seek(_tab.INT_DATA,,1)
         || _what:='';
            _blob_ref:=($(__CTR.BTAB+'.'+__CTR.BBLOB))();
            {? _blob_ref<>null()
            || _what:=_btab.bl_info(__CTR.BBLOB,'NAME')
            ?};
            {? exec('blk_lock','#table',__CTR.BTAB,_btab.ref(),,1,'Załącznik: %1 jest redagowany.'@[_what],,_group)
            || {? _blob_ref<>null()
               || exec('bl_save','#blob',_btab,__CTR.BBLOB)
               || FUN.emsg('Brak pliku w bazie danych. Załącznik jest pusty.'@)
               ?};
               exec('blk_unlock','#table',__CTR.BTAB,_btab.ref())
            ?}
         ?}
      ?}
   || {!
      |? {? _btab.seek(_tab.INT_DATA,)
         || {? _btab.r_lock(1)
            || _file:=_btab.bl_info(__CTR.BBLOB, 'NAME');
               {? _dir + 1 <> __CTR.DIR_SEP
               || _dir_sep:=__CTR.DIR_SEP
               || _dir_sep:=''
               ?};
               {? ~fexists('@'+_dir+_dir_sep+_file,) |
                  FUN.ask('Plik %1 już istnieje. Czy zapisać?'@[_file])
               || _btab.bl_get(__CTR.BBLOB, '@'+ _dir+_dir_sep+_file)
               ?};
               _btab.r_unlock()
            ?}
         || FUN.emsg('Załącznik został usunięty.'@)
         ?};
         _tab.next()
      !}
   ?}
?};
_btab.cntx_pop()


\dir_sep
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Separator katalogu
::----------------------------------------------------------------------------------------------------------------------
{? sys_name = 'U_LINUX'
|| return('/')
|| return('\\\\')
?}


\view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Pokaz dla bloba
::   WE: _a - tabela z rekordem do obejrzenia
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   || exec('bl_view','#blob',_btab,__CTR.BBLOB);
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      exec('getblobs','srsr_zal',__CTR.WIN_ID);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\scan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja skanowania dokumentu
::   WE: _a - tabela z rekordem do obslugi
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
||
   {! |?
      {? _btab.seek(_tab.INT_DATA,)
      ||
         {? _btab.r_lock(1)
         ||
            {? exec('b_skanuj','dok_fks')
            || exec('a_skanuj','dok_fks',_btab,__CTR.BBLOB)
            ?};
            _btab.r_unlock()
         ?};
         __select:=1
      || FUN.emsg('Załącznik został usunięty.'@);
         __select:=0
      ?};
      _tab.next()
   !}
||
   exec('blob_add','srsr_zal',0);
   __select:=1
?};
_btab.cntx_pop()


\blob_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja Dodaj dla Zalacznika
::   WE: _a - flaga wyboru plikow
::       _a=0 - akcja Skanuj
::       _a=1 - akcja Dodaj
::       _a=2 - akcja DnD
::----------------------------------------------------------------------------------------------------------------------
_btab:=($(__CTR.BTAB))();
_btab.KR_OP:='';
_btab.cntx_psh();
_edit:=_btab.mk_edit('Załączniki'@,,'btab',1,1);
_btab.win_efld(_edit,,'KR_OP',,,80,,,'Krótki opis'@);
_ok:=_btab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['Zapisz'@],"'key:F2'");
_btab.btn_eopt(_edit,_ok,'tooltip='+exec('help_red_ok','#window','Z'));
_anuluj:=_btab.win_ebtn(_edit,'text=%1, btn_label_align=center, panel=bottom, align=end'['&Anuluj'@],'key:Esc');
_btab.btn_eopt(_edit,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
_btab.win_edit(_edit);
_add:=0;
{? _btab.edit()
||
   {? _a=0
   ||
      _tab0:=ctr_call(__CTR.WIN_ID,'ctr_id','getDroppedFiles');
      _tab0.add();
      do();
      exec('addblobs','srsr_zal',_tab0,_btab.KR_OP);
      {? exec('a_skanuj','dok_fks',_btab,__CTR.BBLOB)=0
      || undo()
      ?};
      _add:=1;
      end()
   |? _a=1
   ||
      _tmp_dir:=fmk_tmp_dir(0);
      {? type_of(_tmp_dir)<>type_of(~~)
      || _pth:=_tmp_dir.get_path();
         {? _pth<>''
         || _tab0:=exec('files_upload','bloby',_pth);
            {? _tab0.size()>0
            || do();
               exec('addblobs','srsr_zal',_tab0,_btab.KR_OP);
               _add:=1;
               end()
            ?}
         ?}
      ?}
   |? _a=2
   ||
      _tab0:=ctr_call(__CTR.WIN_ID,'ctr_id','getDroppedFiles');
      do();
      exec('addblobs','srsr_zal',_tab0,_btab.KR_OP);
      _add:=1;
      end()
   ?}
?};
_btab.cntx_pop();
return(_add)


\blob_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja Popraw dla Zalacznika
::   WE: _a - tabela z rekordem do poprawienia
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   ||
      {? _btab.r_lock(1)
      ||
         exec('ded_pop','srsr_zal');
         _btab.r_unlock()
      ?};
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja Redaguj dla Zalacznika
::   WE: _a - tabela z rekordem do poprawienia
:: ~OST: INBLEDIT
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   ||
      {? _btab.r_lock(1)
      ||
         {? exec('cli_functions','#system')=0
         || FUN.emsg(exec('indevice_nacc_msg','#system'))
         || _btab.bl_edit(__CTR.BBLOB)
         ?};
         _btab.r_unlock()
      ?};
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Akcja Wlasciwosci dla Zalacznika
::   WE: _a - tabela z rekordem do poprawienia
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.cntx_psh();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   ||
      {? __CTR.BTAB='DOKUM'
      ||
         _disp:=_btab.mk_edit('Załącznik'@,,'dzbtab',1,1);
         _btab.win_efld(_disp,,'DATA');
         _btab.win_efld(_disp,,'CZAS');
         _btab.win_efld(_disp,,'KR_OP',,,80);
         _btab.win_efld(_disp,,'DOKUM',,,80);
         _btab.win_edit(_disp);
         _btab.display()
      ?};
      __select:=1
   || FUN.emsg('Załącznik został usunięty.'@);
      exec('getblobs','srsr_zal',__CTR.WIN_ID);
      __select:=0
   ?}
?};
_btab.cntx_pop()


\dokum_rfr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Po odswiezeniu SRSR
::   WE: _a - identyfikator kontrolki
::       _b - akronim okienka kontrolki
::----------------------------------------------------------------------------------------------------------------------
DOKUM.index('DOKUM');
{? __CTR.TAB='DOK'
|| {? DOK.size>0 & DOK.get()
   || DOKUM.prefix(REF.FIRMA,$DOK.ref())
   || DOKUM.prefix(REF.FIRMA,'@!@!@!@!@!@!@!@!')
   ?}
|? __CTR.TAB='SRSR'
|| {? EDIT_ES.TSK<>'T'
   || {? SRSR.get()
      || {? SRSR.GRP='E'
         || _ref:=$(exec('get_srsr_root','fst',SRSR.ref()))
         || _ref:=$SRSR.ref()
         ?};
         DOKUM.prefix(REF.FIRMA,_ref)
      || DOKUM.prefix(REF.FIRMA,'@!@!@!@!@!@!@!@!')
      ?}
   || {? TST.size()>0 & TST.get()
      || DOKUM.prefix(REF.FIRMA,$TST.SRST().SRSR)
      || DOKUM.prefix(REF.FIRMA,'@!@!@!@!@!@!@!@!')
      ?}
   ?}
?};
{? ~DOKUM.get() || DOKUM.first() ?};
__CTR.WIN_ID:=_a;
__CTR.WIN_ACR:=_b;
{? __CTR.TAB='SRSR'
|| {? SSTALE.AO().POCZ=date(0,0,0) & SSTALE.AO().KON=date(0,0,0)
   || _first:=exec('first_okr','fst',SSTALE.AO);
      {? ~_first
      || _empty:=1
      || _empty:={? (EDIT_ES.TSK='T' & TST.size()>0) | (EDIT_ES.TSK<>'T' & SRSR.size()>0)
                 || 0
                 || 1
                 ?}
      ?}
   || _empty:={? (EDIT_ES.TSK='T' & TST.size()>0) | (EDIT_ES.TSK<>'T' & SRSR.size()>0)
                 || 0
                 || 1
                 ?}
   ?};
   exec('getblobs','srsr_zal',__CTR.WIN_ID,_empty)
|| exec('getblobs','srsr_zal',__CTR.WIN_ID)
?};
grp_disp(($(__CTR.BTAB))(),__CTR.WIN_ACR,,1);
1


\ded_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Zalaczniki dla EDOKUM - EDOKUMZ - popraw
::----------------------------------------------------------------------------------------------------------------------
_btab:=($(__CTR.BTAB))();
_btab.bl_file(__CTR.BBLOB,1);
_btab.win_edit('RED');
{? _btab.edit("__CHK.record(($(__CTR.BTAB))(),,'DOKUM')")
|| _file:=_btab.bl_file(__CTR.BBLOB);
   {? _btab.put()
   ||
      {? _file<>''
      || _btab.bl_put(__CTR.BBLOB,_file);
         _btab.NAZWA:=_btab.bl_info(__CTR.BBLOB,'NAME')
      ?};
      _btab.put()
   ?}
?};
_btab.bl_file(__CTR.BBLOB,1)


\chk_blob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: Sprawdzenie poprawnosci nazwy zalacznika
::   WE: _a - nazwa zalacznika
::   WY: _err - znacznik poprawnosci nazwy
::----------------------------------------------------------------------------------------------------------------------
_name:=_a;
_ax:=0;
_zx:=0;
_err:=0;
_l:=+_name;
{? _name=''
|| _ax:=1
|? _l>64
|| _ax:=3
|| _cx:=_name+1;
   {? _cx=' '
   || _ax:=2
   ||
      {! _i:=1.._l |? _ax=0 |!
        _cx:=(_i+_name)+1;
        {? ('><\\/*"?:|\t'*_cx>0)
        || _ax:=2
        ?}
      !}
   ?}
?};
{? _ax=1
|| undo();
   FUN.info('Niedozwolona pusta nazwa pliku.\n\n*** Niepoprawna wartość. ***'@)
|? _ax=2
|| undo();
   FUN.info('Niedozwolone znaki w nazwie pliku: %1\n\n*** Niepoprawna wartość. ***'@[_name])
|? _ax=3
|| undo();
   FUN.info('Nazwa pliku za długa.\n\n*** Niepoprawna wartość. ***'@)
|| _err:=1
?};
return(_err)


\selectitem
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: zaznaczenie elementu po akcji w kontrolce
::   WE: _a - tabela z rekordem do poprawienia
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
{? _tab.first()
||
   ctr_set(__CTR.WIN_ID,'ctr_id','selectItem',_tab.INT_DATA, '', 1)
?}


\tab_tmp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PG [12.30]
:: OPIS: struktura tabeli tymczasowej dla zalacznikow
::   WY: _zal - tabela tymczasowa przekazywana do kontrolki
::----------------------------------------------------------------------------------------------------------------------
{? __CTR.BTAB='DOKUM'
||
   _zal:=tab_tmp(1, 'NAME', 'STRING[255]', 'Nazwa'@ ,
                      'SIZE', 'INTEGER', 'Rozmiar'@,
                      'MOD_DATE', 'STRING[30]','Data modyfikacji'@,
                      'INT_DATA', 'INTEGER',,
                      'STR_DATA','STRING[10]',,
                      'KR_OP','STRING[200]','Opis'@)
?};
_zal


\find
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Ustawia rekord tabeli
::   WE: _a - tabela z lista plikow
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
_btab:=($(__CTR.BTAB))();
_btab.clear();
{? _tab.first()
|| {? _btab.seek(_tab.INT_DATA,)
   || 1
   ?}
?}

:Sign Version 2.0 jowisz:1045 2023/09/27 10:38:08 25443315cb6f5beffa90e1a90e710ab2969ed29111c5de4fef3ed0092576f082202cc045848f878de752bc809307521bf58dfeb74bc36ebadb1541ed7a7888bd58c815c44b81332354c87d810a19841202423fbdeab0bf00f1722f17ed14e7fd98a4388f305fc4a34a929ccf731d142621b0251d9c1b7d67bd5b3859145130c9

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: lista_hist.fml
:: Utworzony: 2016/01/13
:: Autor: DRO
::======================================================================================================================
:: Zawartość: Historia skladnikow listy plac.
::======================================================================================================================


\hist_grp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GL [8.60]
:: OPIS: Przygotowanie historii składnika listy płac
::   WE: _a - 1 - rubryki brane z tabeli tymczasowej
::            brak - historia bezpośrednio z listy płac
::  OLD: \historia/histls.fml
::----------------------------------------------------------------------------------------------------------------------
R.cntx_psh();
_end:="R.cntx_pop";
{? var_pres('_a')=type_of(0)
|| R.prefix();
   R.seek(wszskl.REF,);
   {? wszskl.sel_size()=0
   || {? ~exec('hist_grp_b','lista_hist')
      || _end();
         return(0)
      ?}
   ?};
   tabtmp2:=sql(zapytanie,R.ref, P.ref);
   {? prezent=1
   || _miesiac:=O.M;
      _rok:=O.R
   || _miesiac:=O.MP;
      _rok:=O.RP
   ?};
   {! _i:=1..ile_mies
   |! _m:=_miesiac-_i;
      _r:=_rok;
      {!
      |? _m<=0
      |! _r-=1;
         _m+=12
      !};
      tabtmp2.KW:=0;
      tabtmp2.R:=_r;
      tabtmp2.M:=_m;
      tabtmp2.add()
   !};
   tabtmp:=sql('select sum(KW) KW, R, M from :_a group by R, M order by 2 desc, 3 desc',tabtmp2);
   _for:="
      sklhist.KOD:=R.RN;
      sklhist.NAZWA:=R.RT;
   ";
   tabtmp.first();
   {! _i:=1..sklhist.fld_num()-3
   |! _for+="sklhist.KOL"+$_i+":=tabtmp.KW; tabtmp.next();"
   !};
   _for+="
      sklhist.SUMA:=sql('select sum(KW) KW from :_a',tabtmp).KW;
      sklhist.add()
   ";
   ($_for)();
   VAR_DEL.delete('tabtmp2','tabtmp');
   {? wszskl.sel_size()=0
   || exec('hist_grp_a','lista_hist')
   ?}

|| {? LS.sel_size()=0
   || {? ~exec('hist_grp_b','lista_hist')
      || _end();
         return(0)
      ?}
   ?};
   tabtmp2:=sql(zapytanie,LS.RB,LS.P);
   {? prezent=1
   || _miesiac:=O.M;
      _rok:=O.R
   || _miesiac:=O.MP;
      _rok:=O.RP
   ?};
   {! _i:=1..ile_mies
   |! _m:=_miesiac-_i;
      _r:=_rok;
      {!
      |? _m<=0
      |! _r-=1;
         _m+=12
      !};
      tabtmp2.KW:=0;
      tabtmp2.R:=_r;
      tabtmp2.M:=_m;
      tabtmp2.add()
   !};
   tabtmp:=sql('select sum(KW) KW, R, M from :_a group by R, M order by 2 desc, 3 desc',tabtmp2);
   _for:="
      sklhist.NAZWA:=LS.RB().RT;
      sklhist.KOD:=R.RN;
   ";
   tabtmp.first();
   {! _i:=1..sklhist.fld_num()-3
   |! _for+="sklhist.KOL"+$_i+":=tabtmp.KW; tabtmp.next();"
   !};
   _for+="
      sklhist.SUMA:=sql('select sum(KW) KW from :_a',tabtmp).KW;
      sklhist.add()
   ";
   ($_for)();
   VAR_DEL.delete('tabtmp2','tabtmp');
   {? LS.sel_size()=0
   || exec('hist_grp_a','lista_hist')
   ?}
?};
_end();
1


\hist_grp_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GL [8.60]
:: OPIS: przygotowanie tabeli tymczasowej dla celów wyświetlenia historii składnika
::  OLD: \przygotuj/histls.fml
::----------------------------------------------------------------------------------------------------------------------
miesiac:="exec('num_to_roman','#convert',_a)";

prezent:=FUN.choice('Sposób pobierania danych:'@,1,'Kosztowo'@,'Podatkowo'@);
{? ~prezent
|| &prezent;
   return(0)
?};
_pyt:=FUN.choice('Przygotowanie danych z ostatnich:'@,,'3 miesięcy'@,'12 miesięcy'@,'Dowolna liczba'@);
{? _pyt=0
|| &prezent;
   return(0)
|? _pyt=1
|| ile_mies:=3
|? _pyt=2
|| ile_mies:=12
|| undefine();
   ile_mies:=6;
   define('ILE',ile_mies,'Liczba miesięcy'@,,2,2);
   def_btn('text=%1,icon=xwin16.png:13'['OK'@],'key:F2');
   def_btn('text=%1,icon=xwin16.png:14'['Anuluj'@],'key:Esc');
   _exec:=def_edit(
            "  {? DEFINE.ILE<=0 | DEFINE.ILE>24
               || FUN.info('Liczba miesięcy musi być większa od %1 i nie większa niż %2.'@ ['0','24']);
                  0
               || ile_mies:=DEFINE.ILE
               ?}
            ",'Zakres analizy'@
          );
   undefine();
   {? ~_exec || return(0) ?}
?};
O.cntx_psh();
{? prezent=2
|| O.index('LISTYPLP')
?};
R.clear();
{? var_pres('sklhist')>100
|| obj_del(sklhist)
?};
{? var_pres('tabtmp')>100
|| obj_del(tabtmp)
?};
{? var_pres('tabtmp2')>100
|| obj_del(tabtmp2)
?};
_miesiac:={? prezent=1 || O.M || O.MP ?};
_rok:={? prezent=1 || O.R || O.RP ?};
_tmp:=(_rok-P.DZA~1)*12+(_miesiac-P.DZA~2);
{? _tmp <= 0
|| FUN.emsg('Pracownik zatrudniony od dnia %1.'@ [$P.DZA]+'\n'+'Operacja została przerwana.'@);
   {? var_pres('ile_mies')>0
   || &ile_mies
   ?};
   {? var_pres('miesiac')>0
   || &miesiac
   ?};
   O.cntx_pop();
   return(0)
|? ile_mies>_tmp
|| ile_mies:=_tmp;
   FUN.info(
      'Pracownik zatrudniony od dnia %1.'@ [$P.DZA]+'\n'+
      {? _tmp=1
      || 'Operacja zostanie wykonana dla jednego miesiąca.'@
      || 'Operacja zostanie wykonana dla %1 miesięcy.'@ [$_tmp]
      ?}
   )
?};
_for:="sklhist:=tab_tmp(1,'KOD','INTEGER','Kod','NAZWA','STRING[20]','Nazwa rubryki'@";
{? prezent=1
|| zapytanie:='select LS.KW KW, O.R R, O.M M from @LS join O where LS.RB=:_a and LS.P=:_b and ('
|| zapytanie:='select LS.KW KW, O.RP R, O.MP M from @LS join O where LS.RB=:_a and LS.P=:_b and ('
?};
_next:=0;
{! _i:=1..ile_mies
|! _m:=_miesiac-_i;
   _r:=_rok;
   {!
   |? _m<=0
   |! _r-=1;
      _m+=12
   !};
   _m+=100;
   {? prezent=1
   || _next:=1;
      {? _i<>ile_mies
      || zapytanie+=' LS.REFERENCE like \'l'+(($_r)+2)+(($_m)+2)+'%\' or'
      || zapytanie+=' LS.REFERENCE like \'l'+(($_r)+2)+(($_m)+2)+'%\''
      ?}
   || O.prefix(exec('ref_firma','ustawienia'),__F_ZATR.O,_r,_m-100);
      {? O.first()
      || {? _next
         || zapytanie+=' or '
         ?};
         _next:=1;
         zapytanie+=' LS.REFERENCE like \''+O.LT+'%\'';
         {!
         |? O.next()
         |! zapytanie+=' or LS.REFERENCE like \''+O.LT+'%\''
         !}
      || zapytanie+=''
      ?}
   ?};
   _for+=",'KOL"+$_i+"','REAL','"+miesiac(_m-100)+" "+$_r+"'"
!};
_for+=",'SUMA','REAL','Suma')";
zapytanie+={? _next || ')' || '1=2)' ?};
($_for)();
O.cntx_pop;
1


\hist_grp_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GL [8.60]
:: OPIS: wyświetlenie odpowiedniego okienka z historią składników
::  OLD: \wyswietl/histls.fml
::----------------------------------------------------------------------------------------------------------------------
_for:="select";
{! _i:=1..sklhist.fld_num-3
|! _for+=" sum(KOL"+$_i+") KOL"+$_i+","
!};
_for+='sum(SUMA) SUMA from :_a';
tabtmp:=sql(_for,sklhist);
tabtmp.first();
_for:="
   sklhist.KOD:=10000;
   sklhist.NAZWA:='Razem'@;
";
{! _i:=1..sklhist.fld_num()-3
|! _for+="sklhist.KOL"+$_i+":=tabtmp.KOL"+$_i+"; tabtmp.next();"
!};
_for+='sklhist.SUMA:=tabtmp.SUMA; sklhist.add()';
($_for)();

{? sklhist.fld_num<=9
|| _wer:=sklhist.mk_sel('Historia składników'@,,,'lshist',,,,,'U',,,,,'html_maximized');
   sklhist.win_fld(_wer,sklhist,'KOD',,,5,,,MS.name(R,'RN'),,MS.comment(R,'RN'));
   sklhist.win_fld(_wer,sklhist,'NAZWA',,,,,,MS.name(R,'RT'),,MS.comment(R,'RT'));
   {! _i:=1..sklhist.fld_num-3
   |! sklhist.win_fld(_wer,sklhist,'KOL'+$_i,,,,,,,,MS.name(sklhist,'KOL'+$_i))
   !};
   sklhist.win_fld(_wer,sklhist,'SUMA');
   sklhist.win_sel(_wer);
   sklhist.select()

|| {? var_pres('okno')>100
   || obj_del(okno)
   ?};
   il_okien:=(sklhist.fld_num()-3)%6;
   {? (sklhist.fld_num-3)%*6>0
   || il_okien+=1
   ?};
   okno:=obj_new(il_okien+3);
   {! _i:=4..il_okien+3
   |! okno[_i]:=sklhist.mk_sel('Historia składników'@,,,'lshist'+$_i,,,,,'U',,,,,'html_maximized');
      sklhist.win_fld(okno[_i],sklhist,'KOD',,,5,,,MS.name(R,'RN'),,MS.comment(R,'RN'));
      sklhist.win_fld(okno[_i],sklhist,'NAZWA',,,,,,MS.name(R,'RT'),,MS.comment(R,'RT'));
      {! _nr:=1..{? (_i-4)*6+6>(sklhist.fld_num()-3) || (sklhist.fld_num()-3)-((_i-4)*6) || 6 ?}
      |! sklhist.win_fld(okno[_i],sklhist,'KOL'+$(((_i-4)*6)+_nr),,,,,,,,MS.name(sklhist,'KOL'+$(((_i-4)*6)+_nr)))
      !};
      sklhist.win_fld(okno[_i],sklhist,'SUMA',,,,,,'Suma'@,,'Suma z wszystkich miesięcy'@);
      {? _i<>4
      || sklhist.win_act(okno[_i],0,'Formuła','Poprzednie'@@,,,"sel_exit; okno[2]:=1; okno[3]:=0",,,,,,'P')
      ?};
      {? _i<>il_okien+3
      || sklhist.win_act(okno[_i],0,'Formuła','Następne'@@,,,"sel_exit; okno[2]:=1; okno[3]:=1",,,,,,'N')
      ?}
   !};
   sklhist.win_sel(okno[4]);
   okno[1]:=4;
   okno[2]:=0;
   sklhist.select();
   {!
   |? okno[2]
   |! okno[2]:=0;
      {? okno[3]
      || exec('str_nastepna','lista_hist')
      || exec('str_poprzednia','lista_hist')
      ?}
   !}
?};

VAR_DEL.delete('sklhist','tabtmp','prezent');
{? var_pres('okno')>100
|| obj_del(okno);
   &il_okien;
   &okno
?};
{? var_pres('zapytanie')>0
|| &zapytanie
?};
{? var_pres('ile_mies')>0
|| &ile_mies
?};
{? var_pres('miesiac')>0
|| &miesiac
?};

1


\hist_wsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GL [8.60]
:: OPIS: wyświetlenie okienka wszystkich rubryk
::  OLD: \histwsz/histls.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('wszskl')>100
|| obj_del(wszskl)
?};

wszskl:=tab_tmp(1,
   'KOD','INTEGER','Kod'@,
   'NAZWA','STRING[20]','Nazwa'@,
   'REF','INTEGER','Wskazanie'@
);

R.clear();
R.for_each("
   wszskl.KOD:=R.RN;
   wszskl.NAZWA:=R.RT;
   wszskl.REF:=#R.ref();
   wszskl.add()
");

_wer:=wszskl.mk_sel('Rubryki'@,,,'rubhist',,,,,'U');
wszskl.win_fld(_wer,,'KOD',,,5,,,MS.name(R,'RN'),,MS.comment(R,'RN'));
wszskl.win_fld(_wer,,'NAZWA',,,,,,MS.name(R,'RT'),,MS.comment(R,'RT'));

wszskl.win_act(_wer,0,'Formuła','Historia'@@,,,
   "exec('hist_grp','lista_hist',1)",,1,1,
   "exec('hist_grp_b','lista_hist')",
   "exec('hist_grp_a','lista_hist')",
   'H'
);

wszskl.win_sel(_wer);
wszskl.select();

VAR_DEL.delete('wszskl','prezent')


\str_nastepna
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GL [8.60]
:: OPIS: nastepne kolumny historii składników listy płac
::  OLD: \next/histls.fml
::----------------------------------------------------------------------------------------------------------------------
{? okno[1]<>il_okien+3
|| okno[1]+=1;
   sklhist.win_sel(okno[okno[1]])
?};
sklhist.select()


\str_poprzednia
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GL [8.60]
:: OPIS: poprzednie kolumny historii składników listy płac
::  OLD: \prev/histls.fml
::----------------------------------------------------------------------------------------------------------------------
{? okno[1]<>4
|| okno[1]-=1;
   sklhist.win_sel(okno[okno[1]])
?};
sklhist.select()


::======================================================================================================================
:: Historia składników wg list płac.
::======================================================================================================================

\hswglp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Historia składników wg list płac (dla bieżącego pracownika).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_pref:=P.ref();

_zakres:=exec('hswglp_zakres','lista_hist',_pref);
{? ~_zakres.OK
|| return()
?};

progress(,'Przygotowanie danych. Proszę czekać ...'@,FUN.TYT,1);

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: Tabela - nawigator w pierwszej zakładce: wszystkie listy rozliczające pracownika (w szczególności puste).
_O:=obj_new('TAB','NDX','WS');

_O.TAB:=sql(
   'select '
      'upper(O.LT) as LT, O.Z, O.R, O.M, O.RP, O.MP, O.RU, O.MU, O_P.KT, O_P.LS, O.REFERENCE as OREF '
   'from '
      'O_P join O using(O_P.O,O.REFERENCE) '
   'where '
      'O_P.P=:_a and (O.R*12+O.M>=:_b) and (O.R*12+O.M<=:_c) '
   'order by '
      'O.R, O.M, LT',
   _pref,_zakres.rod*12+_zakres.mod,_zakres.rdo*12+_zakres.mdo
);

{? ~_O.TAB.first()
|| prgs_clr();
   FUN.info('Brak danych.'@);
   return()
?};

_O.NDX:=obj_new('RMLT');

_O.NDX.RMLT:=_O.TAB.index('?');

_O.WS:=obj_new('NAV');
:: Okno nawigatora w pierwszej zakładce.
_O.WS.NAV:=_O.TAB.mk_sel('Listy płac'@,,,,,,,,'U');
_O.TAB.win_fld(_O.WS.NAV,,'LT',,,-8,,,MS.name(O,'LT'),,MS.comment(O,'LT'));
_O.TAB.win_fld(_O.WS.NAV,,'Z',,,-5,,,MS.name(O,'Z'),,MS.comment(O,'Z'),2,,"'T'","'N'");
_O.TAB.win_fld(_O.WS.NAV,,'R',,,-6,,,MS.name(O,'R'),,MS.comment(O,'R'));
_O.TAB.win_fld(_O.WS.NAV,,'M',,,-4,,,MS.name(O,'M'),,MS.comment(O,'M'));
_O.TAB.win_fld(_O.WS.NAV,,'RP',,,-6,,,MS.name(O,'RP'),,MS.comment(O,'RP'));
_O.TAB.win_fld(_O.WS.NAV,,'MP',,,-4,,,MS.name(O,'MP'),,MS.comment(O,'MP'));
_O.TAB.win_fld(_O.WS.NAV,,'RU',,,-6,,,MS.name(O,'RU'),,MS.comment(O,'RU'));
_O.TAB.win_fld(_O.WS.NAV,,'MU',,,-4,,,MS.name(O,'MU'),,MS.comment(O,'MU'));
_O.TAB.win_fld(_O.WS.NAV,,'KT',,,-5,,,MS.name(O_P,'KT'),,MS.comment(O_P,'KT'),2,,"'T'","'N'");
_O.TAB.win_fld(_O.WS.NAV,,'LS',,,-5,,,MS.name(O_P,'LS'),,MS.comment(O_P,'LS'),2,,"'T'","'N'");
_O.TAB.win_act(_O.WS.NAV,,'Wyświetl',,,,
   "  _O:=params_get().O;
      O.cntx_psh();
      O.prefix();
      {? O.seek(_O.TAB.OREF)
      || UD_SKL.cntx_psh();
         UD_SKL.prefix();
         ODD.cntx_psh();
         ODD.prefix();
         O.win_edit('RED');
         O.display();
         ODD.cntx_pop();
         UD_SKL.cntx_pop()
      ?};
      O.cntx_pop()
   "
);
_O.TAB.fld_attr(,2);

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: Tabela z właściwymi danymi do prezentacji.
_SRC:=obj_new('TAB','NDX','WS','SUM');

_mf:='';
{? _O.TAB.first()
|| {!
   |? _mf+=',\'%1*\'' [-_O.TAB.LT];
      _O.TAB.next()
   !}
?};

_SRC.TAB:=sql(
   'select /* + MASK_FILTER(LS'+_mf+') */ '
      'upper(O.LT) as LT, O.Z, O.R, O.M, O.RP, O.MP, O.RU, O.MU, \'X\' as KT, \'X\' as LS, O.REFERENCE as OREF, '
      'R.LP, R.RN, R.RT, R.REFERENCE as RREF, '
      'LS.KW, KK.SYM as KK '
   'from '
      '@LS join R using(LS.RB,R.REFERENCE) '
          'left join KK using(LS.KK,KK.REFERENCE) '
          'join O using(LS.O,O.REFERENCE) '
   'where '
      'LS.P=:_a '
   'order by '
      'OREF, LP',
   _pref,_O.TAB
);
: Uzupełnienie znaczników KT i LS.
{? _O.TAB.first()
|| {!
   |? _SRC.TAB.prefix(_O.TAB.OREF,);
      {? _SRC.TAB.first()
      || {!
         |? _SRC.TAB.KT:=_O.TAB.KT;
            _SRC.TAB.LS:=_O.TAB.LS;
            _SRC.TAB.put();
            _SRC.TAB.next()
         !}
      ?};
      _SRC.TAB.prefix();
      _O.TAB.next()
   !}
?};

_SRC.NDX:=obj_new('O','R');

_SRC.NDX.O:=_SRC.TAB.index('?');
_SRC.NDX.R:=_SRC.TAB.ndx_tmp(,,'RREF',,,'R',,,'M',,,'LT',,);

:: Tabela wraz obsługą podsumowań w oknie danych w drugiej zakładce.
_SRC.SUM:=tab_tmp(1,
   'SUW','REAL','Suma wszystkich'@,
   'ILW','INTEGER','Ile wszystkich'@,
   'SRW','REAL','Średnia wszystkich'@,
   'SUZ','REAL','Suma zaznaczonych'@,
   'ILZ','INTEGER','Ile zaznaczonych'@,
   'SRZ','REAL','Średnia zaznaczonych'@
);
_SRC.SUM.fld_fml('SUW','BEFORE_DISPLAY',
   "  _par:=params_get();
      _TAB:=_par.SRC.TAB;
      _SUM:=_par.SRC.SUM;

      _TAB.cntx_psh();
      _sel_size:=_TAB.sel_size();
      _fml:='\\'empty='+$(_sel_size=0)+'\\'';
      _SUM.fld_fml('SUZ','DISPLAY_FORMAT',$_fml);
      _SUM.fld_fml('SRZ','DISPLAY_FORMAT',$_fml);
      _w_on:=~~;
      {? _sel_size
      || _TMP:=sql(
            'select sum(TAB.KW) SUMA, count(*) ILE '
            'from :_a TAB '
            'where reference_num(TAB.REFERENCE) in (select SEL.REF from :_b SEL)',
            _TAB,_TAB.sel_aget()
         );
         {? _TMP.first() & _TMP.ILE
         || _SUM.SUZ:=_TMP.SUMA;
            _SUM.SRZ:=_TMP.SUMA/_TMP.ILE$2
         ?}
      |? _TAB.f_active()
      || _SUM.SUW:=_SUM.ILW:=0;
         params_set('TAB',_TAB,'SUM',_SUM);
         _TAB.f_each(""_par:=params_get(); _TAB:=_par.TAB; _SUM:=_par.SUM; _SUM.SUW+=_TAB.KW; _SUM.ILW+=1"");
         {? _w_on:=_SUM.ILW
         || _SUM.SRW:=_SUM.SUW/_SUM.ILW$2
         ?}
      || _TMP:=sql('select sum(TAB.KW) SUMA, count(*) ILE from PREFIXED_TABLE(:_a) TAB',_TAB);
         {? _w_on:=_TMP.first() & _TMP.ILE
         || _SUM.SUW:=_TMP.SUMA;
            _SUM.SRW:=_TMP.SUMA/_TMP.ILE$2
         ?}
      ?};
      {? _w_on<>~~
      || _fml:='\\'empty='+$~_w_on+'\\'';
         _SUM.fld_fml('SUW','DISPLAY_FORMAT',$_fml);
         _SUM.fld_fml('SRW','DISPLAY_FORMAT',$_fml)
      ?};
      _TAB.cntx_pop();
      ~~
   "
);

_SRC.WS:=obj_new('O','R');
:: Okno danych w pierwszej zakładce.
_SRC.WS.O:=_SRC.TAB.mk_sel('Składniki na liście płac'@,,,,,,,,'U');
_SRC.TAB.win_fld(_SRC.WS.O,,'RN',,,-5,,,MS.name(R,'RN'),,MS.comment(R,'RN'));
_SRC.TAB.win_fld(_SRC.WS.O,,'RT',,,-20,,,MS.name(R,'RT'),,MS.comment(R,'RT'));
_SRC.TAB.win_fld(_SRC.WS.O,,'KW',,,12,2,,MS.name(LS,'KW'),,MS.comment(LS,'KW'));
_SRC.TAB.win_fld(_SRC.WS.O,,'KK',,,-20,,,MS.name(LS,'KK'),,MS.comment(LS,'KK'));
:: Okno danych w drugiej zakładce.
_SRC.WS.R:=_SRC.TAB.mk_sel('Listy płac ze składnikiem'@,,,,,,,,'U');
_SRC.TAB.win_fld(_SRC.WS.R,,'LT',,,'-8,10',,,MS.name(O,'LT'),,MS.comment(O,'LT'));
_SRC.TAB.win_fld(_SRC.WS.R,,'Z',,,-5,,,MS.name(O,'Z'),,MS.comment(O,'Z'),2,,"'T'","'N'");
_SRC.TAB.win_fld(_SRC.WS.R,,'R',,,-6,,,MS.name(O,'R'),,MS.comment(O,'R'));
_SRC.TAB.win_fld(_SRC.WS.R,,'M',,,-4,,,MS.name(O,'M'),,MS.comment(O,'M'));
_SRC.TAB.win_fld(_SRC.WS.R,,'RP',,,-6,,,MS.name(O,'RP'),,MS.comment(O,'RP'));
_SRC.TAB.win_fld(_SRC.WS.R,,'MP',,,-4,,,MS.name(O,'MP'),,MS.comment(O,'MP'));
_SRC.TAB.win_fld(_SRC.WS.R,,'RU',,,-6,,,MS.name(O,'RU'),,MS.comment(O,'RU'));
_SRC.TAB.win_fld(_SRC.WS.R,,'MU',,,-4,,,MS.name(O,'MU'),,MS.comment(O,'MU'));
_SRC.TAB.win_fld(_SRC.WS.R,,'KT',,,-5,,,MS.name(O_P,'KT'),,MS.comment(O_P,'KT'),2,,"'T'","'N'");
_SRC.TAB.win_fld(_SRC.WS.R,,'LS',,,-5,,,MS.name(O_P,'LS'),,MS.comment(O_P,'LS'),2,,"'T'","'N'");
_SRC.TAB.win_fld(_SRC.WS.R,,'KW',,,12,2,,MS.name(LS,'KW'),,MS.comment(LS,'KW'));
_SRC.TAB.win_fld(_SRC.WS.R,,'KK',,,-20,,,MS.name(LS,'KK'),,MS.comment(LS,'KK'));
_SRC.TAB.win_sfld(_SRC.WS.R,,'KW',,_SRC.SUM,'SUW',,,'Suma wszystkich'@,,'Suma wszystkich'@,0);
_SRC.TAB.win_sfld(_SRC.WS.R,,'KW',,_SRC.SUM,'SRW',,,'Średnia wszystkich'@,,'Średnia wszystkich'@,1);
_SRC.TAB.win_sfld(_SRC.WS.R,,'KW',,_SRC.SUM,'SUZ',,,'Suma zaznaczonych'@,,'Suma zaznaczonych'@,2);
_SRC.TAB.win_sfld(_SRC.WS.R,,'KW',,_SRC.SUM,'SRZ',,,'Średnia zaznaczonych'@,,'Średnia zaznaczonych'@,3);

_SRC.TAB.fld_attr(,2);

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: Tabela - nawigator w drugiej zakładce: wszystkie rubryki związane z pracownikiem.
_R:=obj_new('TAB','NDX','WS');

_R.TAB:=sql('select distinct LP, RN, RT, RREF from :_a order by LP',_SRC.TAB);

_R.NDX:=obj_new('LP');

_R.NDX.LP:=_R.TAB.index('?');

_R.WS:=obj_new('NAV');
:: Okno nawigatora w drugiej zakładce.
_R.WS.NAV:=_R.TAB.mk_sel('Składniki'@,,,,,,,,'U');
_R.TAB.win_fld(_R.WS.NAV,,'RN',,,-5,,,MS.name(R,'RN'),,MS.comment(R,'RN'));
_R.TAB.win_fld(_R.WS.NAV,,'RT',,,-20,,,MS.name(R,'RT'),,MS.comment(R,'RT'));
_R.TAB.win_act(_R.WS.NAV,,'Wyświetl',,,,
   "  _R:=params_get().R;
      R.cntx_psh();
      R.prefix();
      {? R.seek(_R.TAB.RREF)
      || R.win_edit('RED');
         R.display()
      ?};
      R.cntx_pop()
   "
);
_R.TAB.fld_attr(,2);
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
:: Budowa okna grupowego.
_mode:='maximized_with_title';

_tytul:=exec('record','#to_string',_pref)+' (%1)' [_zakres.opis];

_grp:=_SRC.TAB.grp_make(_tytul,,,,,,,'html_maximized');

_SRC.TAB.grp_sel(_grp,_O.TAB,_O.WS.NAV,'Składniki wg list'@,
   "  params_set(_par:=params_get());
      _SRC:=_par.SRC;
      grp_disp(_SRC.TAB,_SRC.WS.O,,1)
   ",,,,,,,,
   _mode,,
   1
);
_SRC.TAB.tab_splt(_grp,,'vertical','ls1',',60%');
_SRC.TAB.grp_sel(_grp,_SRC.TAB,_SRC.WS.O,,,,,,
   "  _par:=params_get();
      _O:=_par.O;
      _SRC:=_par.SRC;
      {? grp_empty(_O.TAB,_O.WS.NAV,1)
      || '#disable'
      || _SRC.TAB.index(_SRC.NDX.O);
         _SRC.TAB.prefix(_O.TAB.OREF,)
      ?}
   ",,,,
   _mode
);

_SRC.TAB.grp_sel(_grp,_R.TAB,_R.WS.NAV,'Listy wg składników'@,
   "  params_set(_par:=params_get());
      _SRC:=_par.SRC;
      grp_disp(_SRC.TAB,_SRC.WS.R,,1)
   ",,,,,,,,
   _mode
);
_SRC.TAB.tab_splt(_grp,,'vertical','ls2',',25%');
_SRC.TAB.grp_sel(_grp,_SRC.TAB,_SRC.WS.R,,,,,,
   "  _par:=params_get();
      _R:=_par.R;
      _SRC:=_par.SRC;
      {? grp_empty(_R.TAB,_R.WS.NAV,1)
      || '#disable'
      || _SRC.TAB.index(_SRC.NDX.R);
         _SRC.TAB.prefix(_R.TAB.RREF,)
      ?}
   ",,,,
   _mode
);

_SRC.TAB.win_sel(_grp);
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

prgs_clr();

_O.TAB.first();
_R.TAB.first();

params_set(
   'O',_O,
   'SRC',_SRC,
   'R',_R
);
_SRC.TAB.select();

~~


\hswglp_zakres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [18.42]
:: OPIS: Zakres przeglądania historii składników wg list płac.
::   WE: _a [REFERENCE] - Wskazanie pracownika.
::   WY: Tablica elementów nazwanych - parametry.
::----------------------------------------------------------------------------------------------------------------------
_pref:=_a;

_ret:=obj_new('OK','opis','rod','mod','rdo','mdo');
_ret.OK:=0;

_rok:=obj_new('min','max','cur');
_mc:=obj_new('min','max','cur');

_lista:=__PARSES.getVal('ListaPłac');
_rok.cur:=_lista.R;
_mc.cur:=_lista.M;

_PAR:=tab_tmp(1,
   'TYP','STRING[3]','Zakres'@,
   'ROD','INTEGER','Rok - od'@,
   'MOD','INTEGER','Miesiąc - od'@,
   'RDO','INTEGER','Rok - do'@,
   'MDO','INTEGER','Miesiąc - do'@
);
_ae:="
   _PAR:={? var_pres('_a')=type_of(SYSLOG) || _a || cur_tab(1,1) ?};
   _we:={? var_pres('_b')=type_of('') || _b || cur_win(1,1) ?};

   _par:=params_get();
   _rok:=_par.rok;
   _mc:=_par.mc;
   _okres:=_par.okres;
   {? _PAR.TYP='M3'
   || _dp:=date(_rok.cur,_mc.cur-3,1);
      _dk:=date(_rok.cur,_mc.cur-1,1)
   |? _PAR.TYP='R1'
   || _dp:=date(_rok.cur,_mc.cur-12,1);
      _dk:=date(_rok.cur,_mc.cur-1,1)
   |? _PAR.TYP='ALL'
   || _dp:=date(_rok.min,_mc.min,1);
      _dk:=date(_rok.max,_mc.max,1)
   |? _PAR.TYP='O'
   || _dmin:=date(_rok.min,_mc.min,1);
      _dmax:=date(_rok.max,_mc.max,1);
      _dp:=date(_okres[1],_okres[2],1);
      _dk:=date(_okres[3],_okres[4],1);
      {? _dmax<_dp | _dk<_dmin
::       Przedziały nie mają części wspólnej
      || _dp:=_dmin;
         _dk:=_dmax
      || {? _dp<_dmin
         || _dp:=_dmin
         ?};
         {? _dmax>_dk
         || _dk:=_dmax
         ?}
      ?}
   ?};
   _PAR.ROD:=_dp~1;
   _PAR.MOD:=_dp~2;
   _PAR.RDO:=_dk~1;
   _PAR.MDO:=_dk~2;

   _sval:=$(_PAR.TYP='O');
   _PAR.efld_opt(_we,'editable=%1,mark=%1' [_sval],,'ROD');
   _PAR.efld_opt(_we,'editable=%1,mark=%1' [_sval],,'MOD');
   _PAR.efld_opt(_we,'editable=%1,mark=%1' [_sval],,'RDO');
   _PAR.efld_opt(_we,'editable=%1,mark=%1' [_sval],,'MDO');
   ~~
";
_PAR.fld_fml('TYP','BLANK',"'M3'");
_PAR.fld_fml('TYP','AFTER_EDIT',_ae);
_PAR.blank();

O.cntx_psh();
O.prefix();
O_P.cntx_psh();
O_P.index('PRMK');
O_P.prefix(_pref);
{? O_P.first()
|| _rok.min:=_PAR.ROD:=O_P.O().R;
   _mc.min:=_PAR.MOD:=O_P.O().M
?};
{? O_P.last()
|| _rok.max:=_PAR.RDO:=O_P.O().R;
   _mc.max:=_PAR.MDO:=O_P.O().M
?};
O_P.cntx_pop();
O.cntx_pop();

:: Kolejność elementów - istotna.
_okres:=obj_new('rod','mod','rdo','mdo');
_upf:=exec('obj_ntab_set','#array',,'s','HistoriaSkladnikowWgList','p','OKRES');
_os:=exec('get','#profile',,_upf.s,_upf.p);
{? _os=''
|| _os:='%1,%2,%3,%4' [$_rok.min,$_mc.min,$_rok.max,$_mc.max]
?};
_oa:=spli_str(_os,',');
{! _lp:=1 .. 4
|! _okres[_lp]:=#_oa[_lp]
!};

_we:=_PAR.mk_edit('Historia składników według list [%1]'@ [~_lista.LT],,'#hswglp_par');
_PAR.win_esep(_we,'Dane podstawowe'@);
_PAR.win_efld(_we,,'TYP',,,,,,'Zakres'@,,'Zakres prezentacji danych'@,'radio-buttons',,
   'Listy z ostatnich 3 miesięcy'@,"'M3'",
   'Listy z ostatnich 12 miesięcy'@,"'R1'",
   'Listy z wybranego okresu'@,"'O'",
   'Wszystkie listy'@,"'ALL'"
);
_PAR.win_ecol(_we);
_PAR.win_esep(_we,'Okres'@);
_PAR.win_efld(_we,,'ROD',,,5,,,,,'Początek okresu - rok'@);
_PAR.win_efld(_we,,'MOD',,,5,,,,,'Początek okresu - miesiąc'@);
_PAR.win_efld(_we,,'RDO',,,5,,,,,'Koniec okresu - rok'@);
_PAR.win_efld(_we,,'MDO',,,5,,,,,'Koniec okresu - miesiąc'@);
exec('ok_esc','#window',_PAR,_we);
_PAR.win_edit(_we);

params_set(
   'rok',_rok,
   'mc',_mc,
   'okres',_okres
);
_ae(_PAR,_we);
{? _PAR.edit("
      _PAR:=cur_tab(1,1);
      {? _PAR.TYP<>'O'
      || return(1)
      ?};
      {? (_ret:=__CHK.record(_PAR,,'ROD','MOD','RDO','MDO'))<>''
      || return(_ret)
      ?};
      _par:=params_get();
      _rok:=_par.rok;
      _mc:=_par.mc;
      {? _PAR.ROD<_rok.min
      || __CHK.err_fld(_PAR,'ROD',1,'Wartość nie może być mniejsza niż %1.'@ [$_rok.min])
      |? _PAR.MOD<1 | _PAR.MOD>12
      || __CHK.err_fld(_PAR,'MOD',1,'Dozwolone wartości z przedziału %1-%2.'@ ['1','12'])
      |? _PAR.RDO<_PAR.ROD | _PAR.RDO>_rok.max
      || __CHK.err_fld(_PAR,'RDO',1,'Dozwolone wartości z przedziału %1-%2.'@ [$_PAR.ROD,$_rok.max])
      |? _PAR.MDO<1 | _PAR.MDO>12
      || __CHK.err_fld(_PAR,'MDO',1,'Dozwolone wartości z przedziału %1-%2.'@ ['1','12'])
      |? _PAR.ROD*12+_PAR.MOD>_PAR.RDO*12+_PAR.MDO
      || __CHK.err_msg(
            '""Okres do"" (%1/%2) nie może być wcześniejszy niż ""Okres od"" (%3/%4).'@
            [$_PAR.RDO,$_PAR.MDO,$_PAR.ROD,$_PAR.MOD]
         )
      || 1
      ?}
   ")
|| _ret.OK:=1;
   _ret.rod:=_PAR.ROD;
   _ret.mod:=_PAR.MOD;
   _ret.rdo:=_PAR.RDO;
   _ret.mdo:=_PAR.MDO;
   _ret.opis:=
      '%1 %2 - %3 %4'
      [exec('num_to_roman','#convert',_ret.mod),$_ret.rod,exec('num_to_roman','#convert',_ret.mdo),$_ret.rdo];
   {? _PAR.TYP='O'
   || exec('set','#profile',,_upf.s,_upf.p,'%1,%2,%3,%4' [$_PAR.ROD,$_PAR.MOD,$_PAR.RDO,$_PAR.MDO])
   ?}
?};

_ret

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 98decdec9a197edd6823c09666a53d30e9a66c4f97e611a19152d0444c2d816e070f6e5ecba570666fca2dfe892c450c1b485892ac5043c494f2d538ca36beccedd1410b4588d487bfa67f62b1a1f69f232e63f8b48d77eea291884145f777b3263192eba4784e04a5f265abcea3229f7dfe98df13b2c1aae8388471ba655b18

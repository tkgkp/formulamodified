:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: img_blob.fml
:: Utworzony: 05.08.2019
:: Autor: IS
::======================================================================================================================
:: Zawartość: Formuły do obsługi tabeli IMG_BLOB
::======================================================================================================================


\get_podpis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Szuka w tabeli IMG_BLOB podpisu i zwraca go
::   WE: _a [STRING] - kod podpisu
::   WY: _blob [BLOBIMAGE] - znaleziony obraz lub null() gdy nie znaleziono
::----------------------------------------------------------------------------------------------------------------------
_user:=USERS.ref();
exec('get_blob','img_blob','U',_user,exec('ref_firma','#firma'),_a)


\get_logo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Szuka w tabeli IMG_BLOB loga firmy i zwraca znalezione logo
::   WY: _blob [BLOBIMAGE] - znaleziony obraz lub null() gdy nie znaleziono
::----------------------------------------------------------------------------------------------------------------------
exec('get_blob','img_blob','F',null(),exec('ref_firma','#firma'),'logo')


\get_blob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Szuka w tabeli IMG_BLOB bloba i zwraca go
::   WE: _a [STRING] - Rodzaj obrazu [F/U - Firma/User]
::       _b [REF] - Użytkownik systemu (null dla firmy)
::       _c [REF] - Firma
::       _d [STRING] - Szukany typ
::   WY: _blob [BLOBIMAGE] - znaleziony obraz lub null() gdy nie znaleziono
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('')
   | var_pres('_b')<>type_of(USERS.ref())
   | var_pres('_c')<>type_of(USERS.ref())
   | var_pres('_d')<>type_of('')
   || return(null())
?};
exec('get_blob','#blob',_a,_b,_c,_d)


\tmp_cp_logo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Szuka logotypu firmy w tabeli IMG_BLOB. Znalezione logo lub plik logo.bmp z serwera (jeśli nie znajdzie
::       logotypu w tabeli IMG_BLOB) kopiuje do katalogu tymczasowego końcówki.
::       Zwraca ścieżkę do pliku z logotypem firmy zapisanego w katalogu tymczasowym końcówki, lub pusty napis
::       jeśli nie uda się nic skopiować.
::   WY: [STRING] - ścieżka do logotypu firmy na końcówce
:: ~OST: INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')=1
|| return('')
?};
_dir:='@'+tmp_dir();
_name:='logo_'+REF.FIRMA().SYMBOL;
exec('cp_logo','img_blob',_dir,_name)


\cp_logo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Szuka logotypu firmy w tabeli IMG_BLOB. Znalezione logo lub plik logo.bmp z serwera (jeśli nie znajdzie
::       logotypu w tabeli IMG_BLOB) kopiuje do podanego katalogu.
::       Zwraca ścieżkę do pliku z logotypem firmy zapisanego w podanym katalogu, lub pusty napis
::       jeśli nie uda się nic skopiować.
::   WE: _a [STRING] - ścieżka przed nazwą pliku (np. wynik funkcji tmp_dir poprzedzony @)
::       _b [STRING] - nazwa pliku bez rozszerzenia (np. "logo")
::   WY: _dir [STRING] - ścieżka do logotypu firmy na końcówce
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of('')
   | var_pres('_b')<>type_of('')
   || return('')
?};
_file:=_a+'/'+_b+'.';
_dir:='';
_ext:='';
::próba znalezienia odpowiedniego bloba w tabeli IMG_BLOB:
_blob:=exec('get_logo','img_blob');
{? _blob
::znaleziono logo firmy:
|| _tab:=tab_tmp(,'OBRAZ','BLOBRAW','');
   _tab.OBRAZ:=_blob;
   _tab.add();
   _ext:=_tab.bl_info('OBRAZ','EXTENSION');
   _tab.bl_get('OBRAZ',_file+_ext);
   _dir:=1-(_file+_ext)
::jeśli nie znaleziono odpowiedniego bloba w tabeli IMG_BLOB
|| {? fexists(pth_dir('logo.bmp')+'/logo.bmp')=1
   || _ext:='bmp';
      fcopy(pth_dir('logo.bmp')+'/logo.bmp',_file+_ext);
      _dir:=1-(_file+_ext)
   ?}
?};
_dir


\_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli IMG_BLOB
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_chk','img_blob',0)


\_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli IMG_BLOB
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_chk','img_blob',1)


\_puta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wyzwalacz "Popraw - po" dla tabeli IMG_BLOB.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};
{? IMG_BLOB.OBRAZ<>bfld('OBRAZ')
|| exec('firma_put','img_blob',IMG_BLOB.SLO_KOD,IMG_BLOB.FIRMA)
?};
~~


\_dela
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wyzwalacz "Usuń - po" dla tabeli IMG_BLOB.
::   WE: _a [INTEGER] - Wynik właściwej operacji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~_a | do_state()<>1
|| return()
?};
exec('firma_put','img_blob',bfld('SLO_KOD'),bfld('FIRMA'));
~~


\firma_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Po modyfikacji i usunięciu rekordu.
::       Formuła wywoływana z wyzwalaczy "po": "Dołącz", "Popraw" i "Usuń" dla tabeli IMG_BLOB.
::   WE: _a [REFERENCE] - Typ zapisu [SLO_KOD].
::       _b [REFERENCE] - Firma [FIRMA].
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_slo_kod:=_a;
_firma:=_b;

{? exec('lic','#b_domain','POR')
|| SLO_KOD.cntx_psh();
   SLO_KOD.prefix();
   {? SLO_KOD.seek(_slo_kod) & SLO_KOD.KOD='logo'
   || FIRMA.cntx_psh();
      FIRMA.prefix();
      {? FIRMA.seek(_firma)
      || FIRMA.put(,1)
      ?};
      FIRMA.cntx_pop()
   ?};
   SLO_KOD.cntx_pop()
?};

~~


\rek_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS
:: OPIS: Po edycji wiersza tabeli IMG_BLOB.
::   WY: 1 w przypadku gdy rekord jest poprawny, akronim wymaganego pola
::----------------------------------------------------------------------------------------------------------------------
exec('_chk','img_blob',-menu_txt()='popraw')


\_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS
:: OPIS: Sprawdza wypełnienie wymaganych pól w tabeli IMG_BLOB.
::       Wykorzystywana w wyzwalaczach "Dołącz - przed" i "Popraw - przed" oraz akcji "Rekord - po".
::   WE: [_a] [NUMBER] - Specyfikacja testu:
::             0 - Dołącz [domyślnie];
::             1 - Popraw
::----------------------------------------------------------------------------------------------------------------------
_put:=var_pres('_a')=type_of(0) & _a;
__CHK.validate(IMG_BLOB,
   $("_a.table(_b,"+$_put+",,'RODZ','SLO_KOD','OBRAZ')"),
   "1"
)


\obraz_pa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Formuła na wzorzec pola OBRAZ tabeli IMG_BLOB
::----------------------------------------------------------------------------------------------------------------------
'Pliki graficzne;*.bmp;*.jpg;*.jpeg;*.png'


\grp_f_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Przy wypełnianiu okienka grupowego GRP_F tabeli IMG_BLOB
::----------------------------------------------------------------------------------------------------------------------
IMG_BLOB.fld_fml('RODZ','BLANK',"'F'");
IMG_BLOB.fld_fml('USER','BLANK',"null()");
IMG_BLOB.cntx_psh();
FIRMA.cntx_psh();
SLO_KOD.cntx_psh();
IMG_BLOB.index('KOD');
IMG_BLOB.win_edit('RED');
FIRMA.index('SYMBOL2');
FIRMA.prefix('S',);
SLO_KOD.win_dict('SLO2');
~~


\grp_f2_bf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Przy wypełnianiu okienka grupowego GRP_F2 tabeli IMG_BLOB
::----------------------------------------------------------------------------------------------------------------------
IMG_BLOB.fld_fml('RODZ','BLANK',"'F'");
IMG_BLOB.fld_fml('USER','BLANK',"null()");
IMG_BLOB.cntx_psh();
FIRMA.cntx_psh();
SLO_KOD.cntx_psh();
IMG_BLOB.index('KOD');
IMG_BLOB.win_edit('RED');
SLO_KOD.win_dict('SLO2');
~~


\grp_f_wer_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Przed obsługą okienka WER w okienku grupowym GRP_F tabeli IMG_BLOB
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(FIRMA,'SLO',1)
|| return('#disable')
?};
IMG_BLOB.fld_fml('FIRMA','BLANK',"FIRMA.ref()");
IMG_BLOB.prefix('F',null(),FIRMA.ref(),);
~~


\grp_f2_wer_bs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Przed obsługą okienka WER w okienku grupowym GRP_F2 tabeli IMG_BLOB
::----------------------------------------------------------------------------------------------------------------------
IMG_BLOB.fld_fml('FIRMA','BLANK',"FIRMA.ref()");
IMG_BLOB.prefix('F',null(),FIRMA.ref(),);
~~


\grp_f_wer_bs_dom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Przed obsługą okienka WER w okienku grupowym GRP_F tabeli IMG_BLOB w zakładce Domyślne
::----------------------------------------------------------------------------------------------------------------------
IMG_BLOB.fld_fml('FIRMA','BLANK',"null()");
IMG_BLOB.prefix('F',null(),null(),);
~~


\grp_f_oc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Przy zamykaniu okienka grupowego GRP_F tabeli IMG_BLOB
::----------------------------------------------------------------------------------------------------------------------
SLO_KOD.cntx_pop();
FIRMA.cntx_pop();
IMG_BLOB.cntx_pop();
IMG_BLOB.fld_fml('USER','BLANK',"*");
IMG_BLOB.fld_fml('FIRMA','BLANK',"*");
IMG_BLOB.fld_fml('RODZ','BLANK',"*");
1


\grp_f_slo_ar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [19.22]
:: OPIS: Po odświeżeniu okienka SLO w oknie grupowym GRP_F tabeli IMG_BLOB
::----------------------------------------------------------------------------------------------------------------------
grp_disp(IMG_BLOB,'WER',0);
~~


\get_footer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [21.14]
:: OPIS: Szuka w tabeli IMG_BLOB footera firmy i zwraca znalezione logo
::   WY: _blob [BLOBIMAGE] - znaleziony obraz lub 0 gdy nie znaleziono
::----------------------------------------------------------------------------------------------------------------------
exec('get_blob','img_blob','F',null(),exec('ref_firma','#firma'),'footer')

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:18 8fe3e6219feafac3700dbe371828bda364def5e4143f0bf23d6e3f29be954e7a83b35d7a74c303b82cf91ecaf7c4ab01b712d3514ff258b2a631e9bffc82d92705e98c28d702c2a6e981e0be888a8cf4812d6468cd6672aebc46aadf785fdf7cbbcb65a872a7ddc850298d7c46ae597388b2e74a655ac126bd82946fea20f24f

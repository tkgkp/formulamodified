:!UTF-8
::(c) Macrologic S.A. Wszelkie prawa zastrzezone
::========================================================================================================================
::Nazwa pliku: historia.fml
::Utworzony: 2019
::Autor: [PJ] Piotr Jaskulski
::Systemy: ESTRA
::========================================================================================================================
::Zawartosc: Historia srodka trwalego
::========================================================================================================================

\historia
::---------------------------------------------------------------------------
:: UTW  : PJ [8.70]
:: OPIS : Formula do wyswietlania historii srodka
::---------------------------------------------------------------------------
VAR_DEL.delete('H_SROD','SZ');
echo('Trwa przetwarzanie danych środka...'@);

h_licznik:=1;
H_SROD:=tab_tmp(5, 'WID','TREE_REF','Widoczny',
                'ROK','INTEGER','Rok',
                'POZIOM','INTEGER','Poziom',
                'DATA','STRING[10]','Data',
                'Z','STRING[1]','Likwidacja',
                'RODZAJ','STRING[20]','Rodzaj',
                'DOK','STRING[20]','Symbol dokumentu',
                'OPIS', 'STRING[60]', 'Opis',
                'NR', 'INTEGER', 'Numer zmiany',
                'WIDOK','STRING[24]','Widok',
                'ZWIROZ','INTEGER','Zwinięty/Rozwinięty'@);
_winh:=H_SROD.mk_sel('Lista zmian'@,'P',, '_h_wer',,,10,1);
hndx:=H_SROD.ndx_tmp(,1,'ROK',,0,'POZIOM',,0);
H_SROD.win_fld(_winh,H_SROD,'WIDOK'  ,,,,,,'Rok/Symbol'@);
H_SROD.win_fld(_winh,H_SROD,'DATA'  ,,,,,,'Data'@);
H_SROD.win_fld(_winh,H_SROD,'RODZAJ',,,,,,'Rodzaj zmiany'@);
H_SROD.win_fld(_winh,H_SROD,'OPIS'  ,,,,,,'Opis'@);
H_SROD.win_act(_winh,0,'Formuła','&Historia zestawów i elementów',,,"exec('memo_grp_win','srodobj')");
H_SROD.win_act(_winh,1,'Formuła','&Historia zestawów i elementów',,,"exec('memo_grp_win','srodobj')");
H_SROD.win_sel(_winh);

_edit:=H_SROD.mk_edit(,,'_h_edit');
H_SROD.win_efld(_edit,H_SROD,'DATA');
H_SROD.win_efld(_edit,H_SROD,'RODZAJ');
H_SROD.win_efld(_edit,H_SROD,'DOK');
H_SROD.win_efld(_edit,H_SROD,'OPIS');
H_SROD.win_edit(_edit);

H_SROD.win_act(_winh,,'Formuła','Drukuj'     ,,'Wydruk historii środka'@            ,"exec('rep_exec','#b_report','','fst_historia',,,,,,,,'W')",,,,,,,,'icon=print');
H_SROD.win_act(_winh,,'Rekord' , ''          ,,''                                  ,"exec('czy_new_rok','historia')");

SZ:=tab_tmp(1,'ST','INTEGER','Numer',
                'A','STRING[35]','',
                'B','STRING[78]','');

sz_okno:=SZ.mk_sel('Informacje szczegółowe'@,,,'_sz_wer',,,10);
SZ.win_fld(sz_okno, SZ, 'A',,,,,,'');
SZ.win_fld(sz_okno, SZ, 'B',,,,,,'');
SZ.win_sel(sz_okno);
_mode:='maximized_with_title';
_group:=H_SROD.grp_make('Historia środka: ' + SRST.NRI,,'_h_grp');
H_SROD.grp_sel(_group, H_SROD, _winh,,"exec('szczegol','historia')",0,0,11,,,,,_mode);
H_SROD.grp_splt(_group,,'horizontal','panel1',16);
H_SROD.grp_sel(_group, SZ, sz_okno,,"",0,16,4,,,,,_mode);

_nri:=SRST.NRI; _srod_id:=SRST.IDADD; _odd:=SRST.ODD().OD; _de:=SRST.DE;
fr:=exec('first_rok_pocz','fst')~1; _ar:=SSTALE.AR().NAZ; "_ao:=SSTALE.AO().KON; date(SRDO.DW~1,SRDO.OKRO_F().NR,0)<=_ao";

SRST.cntx_psh();
SRDO.cntx_psh();
_ref:=(exec('dod_rok','historia',SRST.DE~1));
:: petla zbierajaca dane z dokumentow wg lat
SRDO.index('AUTO');
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.first()
|| {! |? _first:=1;
         SRDO.prefix(SRSR.ref(),ROK_F.ref());
         {? SRDO.first()
            || {! |?
                     _rok_m:=#ROK_F.NAZ;
                     ROK_F.cntx_psh();
                     _ref:=exec('travel','historia', _rok_m, _ref, ROK_F.ref());
                     ROK_F.cntx_pop();
                     SRDO.next()
                !}
            ?};
         ROK_F.next()
   !}
?};
SRST.cntx_pop();
SRDO.cntx_pop();

echo();

:: wyswietlenie zmian
H_SROD.win_sel(_group);
H_SROD.prefix();
{? H_SROD.first()
|| H_SROD.select()
?};
VAR_DEL.delete('H_SROD','SZ','sz_okno','fr','hndx')


\travel
::---------------------------------------------------------------------------
:: UTW  : PJ [19.42]
:: OPIS : Pobiera dane z dokumentow dla biezacego srodka
:: WE   : _a= nazwa roku, _b=ref dodanego H_SROD, _c =ref roku
::---------------------------------------------------------------------------
_dodano:=0;
_ref:=_b;
 {! |?
        {? ~(SRST.DE~1=_a | fr=_a) || _ref:=exec('dod_rok','historia', _a) ?};
        H_SROD.blank();
        H_SROD.WID:=_ref;
        H_SROD.POZIOM:=2;
        h_licznik+=1;
        H_SROD.NR:=h_licznik;
        H_SROD.DATA:=form(SRDO.DW,,1);
        SZ.ST:=H_SROD.NR;
        _rodz:=SRDO.TYP().RODZ;
        {? _rodz='P'
        ||  SZ.A:='Wartość początkowa podatkowa'; SZ.B:=form(SRST.WARP,,2); SZ.add(1);
            SZ.A:='Wartość początkowa finansowa'; SZ.B:=form(SRST.WARF,,2); SZ.add(1);
            {? FINFO.TOR_D='T'
            || SZ.A:='Wartość początkowa dodatkowa'; SZ.B:=form(SRST.WARD,,2); SZ.add(1)
            ?}
        |? _rodz='W'
        || SRST.cntx_psh();
           _m:=exec('find_okro_f','fst',SRDO.DW);
           SRST.index('SROD');
           SRST.prefix(SRSR.ref(),_c,_m);
           {? SRST.first
           || SZ.A:='Wartość podatkowa'; SZ.B:=form(SRDO.WARP,,2);  SZ.add(1);
              SZ.A:='Wartość bilansowa'; SZ.B:=form(SRDO.WARF,,2);  SZ.add(1);
              {? FINFO.TOR_D='T'
              || SZ.A:='Wartość dodatkowa'; SZ.B:=form(SRDO.WARD,,2);  SZ.add(1)
              ?}
           ?};
           SRST.cntx_pop()

        |? _rodz='L'
        || SRST.cntx_psh();
           _mp:=exec('find_prev_okro','fst',_c,SRDO.DW~2);
           _m:=exec('find_okro_f','fst',SRDO.DW);
           SRST.index('SROD');
           SRST.prefix(SRSR.ref(),_c,_m);
           {? SRST.first
           || _pomi:=SRST.POM().S; _osob:=SRST.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE;
              _jorg:=SRST.JORG().SYMBOL;
              _konf:=SRST.KONFIN().KOD;  _konp:=SRST.KONPOD().KOD;
              _gr:=SRST.GR().GR; _nri:=SRST.NRI; _nst:=SRST.NST
           || _pomi:=_osob:=_jorg:=_konf:=_konp:= _gr:=_nri:=_nst:=''
           ?};
           SRST.prefix(SRSR.ref(),_c,_mp);
           {? SRST.first
           || SZ.A:='Dokument ewidencyjny'; SZ.B:=SRDO.SYMBOL; SZ.add(1);
              SZ.A:='Miesiąc obowiązywania'; SZ.B:=$SRDO.OKRO_F().NR; SZ.add(1);
              {? SRDT.JORG='T' & _jorg<>SRST.JORG().SYMBOL
              || SZ.A:='Stara jedn. org.'; SZ.B:=SRST.JORG().SYMBOL; SZ.add(1);
                 SZ.A:='Nowa jedn. org.'; SZ.B:=_jorg; SZ.add(1)
              ?};
              {? SRDT.POM='T' & _pomi<>SRST.POM().S
              || SZ.A:='Stare pomieszczenie'; SZ.B:=SRST.POM().S; SZ.add(1);
                 SZ.A:='Nowe pomieszczenie'; SZ.B:=_pomi; SZ.add(1)
              ?};
              {? SRDT.OSOBA='T' & _osob<>SRST.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE
              || SZ.A:='Stary użytkownik'; SZ.B:=SRST.OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE; SZ.add(1);
                 SZ.A:='Nowy użytkownik'; SZ.B:=_osob; SZ.add(1)
              ?};
              {? SRDT.KONFIN='T' & _konf<>SRST.KONFIN().KOD
              || SZ.A:='BYŁO'; SZ.B:=SRST.KONFIN().KOD; SZ.add(1);
                 SZ.A:='JEST'; SZ.B:=_konf; SZ.add(1)
              ?};
              {? SRDT.KONPOD='T' & _konp<>SRST.KONPOD().KOD
              || SZ.A:='BYŁO'; SZ.B:=SRST.KONPOD().KOD; SZ.add(1);
                 SZ.A:='JEST'; SZ.B:=_konp; SZ.add(1)
              ?};
              {? SRDT.GR='T' & _gr<>SRST.GR().GR
              || SZ.A:='BYŁO'; SZ.B:=SRST.GR().GR; SZ.add(1);
                 SZ.A:='JEST'; SZ.B:=_gr; SZ.add(1)
              ?};
              {? SRDT.NRI='T' &  _nri<>SRST.NRI
              || SZ.A:='BYŁO'; SZ.B:=SRST.NRI; SZ.add(1);
                 SZ.A:='JEST'; SZ.B:=_nri; SZ.add(1)
              ?};
              {? SRDT.NST='T' & _nst<>SRST.NST
              || SZ.A:='BYŁO'; SZ.B:=SRST.NST; SZ.add(1);
                 SZ.A:='JEST'; SZ.B:=_nst; SZ.add(1)
              ?}
           ?};
           SRST.cntx_pop()
        ?};
        H_SROD.Z:='N'; H_SROD.ROK:=SRDO.DW~1; H_SROD.RODZAJ:=SRDO.TYP().O_RODZ;
        H_SROD.DOK:=SRDO.SYMBOL; H_SROD.OPIS:=SRDO.TYP().NAZWA; H_SROD.WIDOK:=H_SROD.DOK;
        H_SROD.add(1); _dodano+=1;
        SRDO.next()
   !};
_ref


\szczegol
::---------------------------------------------------------------------------
:: UTW  : PJ [8.70]
:: OPIS : Formula do wyswietlania szczegolow zmiany dot. srodka
::---------------------------------------------------------------------------
SZ.prefix(H_SROD.NR);
SZ.first();
grp_disp(SZ, sz_okno)


\czy_new_rok
::---------------------------------------------------------------------------
:: UTW  : PJ [8.70]
:: OPIS : Formula do podswietlania wierszy zaczynajacych nowy rok w tabeli H_SROD
::---------------------------------------------------------------------------
{? H_SROD.DATA='' & H_SROD.RODZAJ=''
|| 1
|? H_SROD.Z='T'
|| '0:0:0,255:255:0'
|| 0
?}


\dod_rok
::---------------------------------------------------------------------------
:: UTW  : PJ [8.70]
:: OPIS : Formula dodaje wiersz zaczynajacy nowy rok w tabeli H_SROD
:: WE   : _a=rok
::---------------------------------------------------------------------------
H_SROD.cntx_psh();
H_SROD.index(hndx);
{? H_SROD.find_key(_a,1)
|| _ref:=H_SROD.ref()
|| H_SROD.blank();
   H_SROD.WID:=null;
   H_SROD.POZIOM:=1;
   H_SROD.NR:=100000 + _a;
   H_SROD.DATA:='';
   H_SROD.ROK:=_a;
   H_SROD.Z:='N';
   H_SROD.RODZAJ:='';
   H_SROD.DOK:='';
   H_SROD.OPIS:='';
   H_SROD.WIDOK:=$_a;
   H_SROD.ZWIROZ:=0;
   H_SROD.add(1);
   _ref:=H_SROD.ref();
   {? SRST.DE~1<>_a
   || SZ.ST:=H_SROD.NR;
      SZ.A:='Netto pod. na BO';
      SZ.B:=form(SRST.NETP,,2);
      SZ.add(1);
      SZ.A:='Netto fin. na BO';
      SZ.B:=form(SRST.NETF,,2);
      SZ.add(1)
   ?}
?};
H_SROD.cntx_pop();
_ref


\zwinroz
::---------------------------------------------------------------------------
:: UTW  : PJ [8.70]
:: OPIS : Formula zwija rozwija poziom informacji
::---------------------------------------------------------------------------
{? H_SROD.POZIOM=1
|| {? H_SROD.ZWIROZ=0
   || H_SROD.cntx_psh();
      H_SROD.prefix(0,H_SROD.ROK,2);
      {? H_SROD.first()
      || {! |?
            H_SROD.cntx_psh();
            H_SROD.prefix();
            H_SROD.WID:=1;
            H_SROD.put();
            H_SROD.cntx_pop();
            H_SROD.first()
         !}
      || FUN.emsg('We wskazanym roku brak zapisów zmieniających stan środka.'@)
      ?};
      H_SROD.cntx_pop();
      H_SROD.ZWIROZ:=1;
      H_SROD.put()
   || H_SROD.cntx_psh();
      H_SROD.prefix(1,H_SROD.ROK,2);
      {? H_SROD.first()
      || {! |?
            H_SROD.cntx_psh();
            H_SROD.prefix();
            H_SROD.WID:=0;
            H_SROD.put();
            H_SROD.cntx_pop();
            H_SROD.first()
         !}
      || FUN.emsg('We wskazanym roku brak zapisów zmieniających stan środka.'@)
      ?};
      H_SROD.cntx_pop();
      H_SROD.ZWIROZ:=0;
      H_SROD.put()
   ?}
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 1d06a52ba747722fdfab80abf4444bb8769089b5f76633183576e4b1e43b156520bb892c962d2d278d93078277e3412e660cb2e8b932559b0c18bb25cc46fc7e5d9d44cb8a01ab62e7801917b1c8582b4fd905cdd123ec396c5abc8b239367c036a35269998f83483b99614764532c2ae31e983c8c4eabceda34e34305e6877e

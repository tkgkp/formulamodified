:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: oplaty_zest.fml
:: Utworzony: 20.01.2022
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Obsługa opłat dodatkowych - dane do zestawień
::======================================================================================================================

\pob_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Tworzy tabelę z danymi
::   WE: _a - oddział
::       [_a] - zakres 'S'-sprzedaż, 'Z'-zakupy, 'M'-magazyn
::       [_b] - od daty
::       [_c] - do daty
::   WY: zwraca tabelę z danymi
::----------------------------------------------------------------------------------------------------------------------
_oddz:={? var_pres('_a')=type_of('') || _a || ST.ODDZ ?};
_zakr:={? var_pres('_b')=type_of('') & (';SZM'*_b)>1 || _b || '' ?};
_oddt:={? var_pres('_c')=type_of(date()) || _c || date(0,0,0) ?};
_dodt:={? var_pres('_d')=type_of(date()) || _d || date(0,0,0) ?};

_res:=tab_tmp(1,'TAXS','STRING[10]','Kod opłaty'
       ,'DATA','DATE','Data dokumentu'
       ,'MKTM','STRING[50]','Indeks materiałowy'
       ,'NKTM','STRING[150]','Nazwa indeksu'
       ,'MGSP','STRING[10]','Miejsce rejestracji opłaty'
       ,'DESC','STRING[100]','Opis opłaty'
       ,'NOPL','REAL','Wartość w walucie narodowej'
       ,'WOPL','REAL','Wartość w walucie'
       ,'WAL','STRING[3]','Waluta'
       ,'W01','STRING[150]','Wyróżnik 1'
       ,'W02','STRING[150]','Wyróżnik 2'
       ,'W03','STRING[150]','Wyróżnik 3'
       ,'W04','STRING[150]','Wyróżnik 4'
       ,'W05','STRING[150]','Wyróżnik 5'
       ,'W06','STRING[150]','Wyróżnik 6');

_okrB:={? _oddt=date(0,0,0) || 0 || _oddt~1 ?};
_okrE:={? _dodt=date(0,0,0) || 0 || _dodt~1 ?};
 _all:=_oddt=date(0,0,0) & _dodt=date(0,0,0);

exec('openod_psh','open_tab');
OKR.cntx_psh();
OKR.index('MC');
OKR.prefix(REF.FIRMA,1);
{? OKR.first()
|| {!
   |?  {? _okrB=0 | (OKR.ROK>=_okrB & (_okrE=0 | OKR.ROK<=_okrE))
       || exec('openod','open_tab',_oddz,form(OKR.ROK-2000,-2,0,'99'));
          FAP_K.index('OPLDOD');
          FAP_K.prefix('T');
          {? FAP_K.first()
          || {!
             |? _Tab:=ref_tab(FAP_K.UIN);
                _dmag:=_Tab=ND;
                _dzak:=0;
                _dspr:=0;
                {? ~_dmag & _Tab=FAKS
                || {? exec('FindAndGet','#table',FAKS,FAP_K.UIN,,"SZ='S'",0) || _dspr:=1 || _dzak:=1 ?}
                ?};
                _data:={? _Tab=ND | _Tab=FAKS
                       || exec('FindAndGet','#table',_Tab,FAP_K.UIN,,"D",date(0,0,0))
                       || date(0,0,0)
                       ?};
                _mgsp:={? _Tab=ND
                       || exec('FindAndGet','#table',_Tab,FAP_K.UIN,,"{? MAG=null() || '--' || MAG().SYM ?}",'--')
                       |? _Tab=FAKS
                       || exec('FindAndGet','#table',_Tab,FAP_K.UIN,,"{? STS=null() || '--' || STS().KOD ?}",'--')
                       || '--'
                       ?};
                {? (_zakr='' | ((_zakr*'M') & _dmag) | ((_zakr*'S') & _dspr) | ((_zakr*'Z') & _dzak))
                 & (_all | ((_oddt=date(0,0,0) | _data>=_oddt) & (_dodt=date(0,0,0) | _data<=_dodt)))
                || _res.blank();
                   _res.TAXS:=FAP_K.TAXS().TAX;
                   _res.DATA:=_data;
                   _res.MGSP:=_mgsp;
                   _res.MKTM:=FAP_K.M().KTM;
                   _res.NKTM:=FAP_K.M().N;
                   _res.DESC:=FAP_K.TAXS().DESC;
                   _res.NOPL:=FAP_K.NOPL;
                   _res.WOPL:=FAP_K.WOPL;
                   _res.WAL:=exec('FindAndGet','#table',_Tab,FAP_K.UIN,,"WAL().KOD",'PLN');
                   _lp:=9;
                   TAXV.index('TAXS');
                   TAXV.prefix(FAP_K.TAXS);
                   {? TAXV.first()
                   || {!
                      |?
                         TAXV.cntx_psh();
                         {? TAXV.TAXT().R='M'
                         || _lp+=1;
                            TAXI.cntx_psh();
                            TAXI.index('M');
                            TAXI.prefix(FAP_K.M,TAXV.TAXS,TAXV.TAXT().KOD,);
                            {? TAXI.first()
                            || _res[_lp]:='%1: %2'[TAXI.TAXT,TAXI.VALUE]
                            || _res[_lp]:='...'
                            ?};
                            TAXI.cntx_pop()
                         |? TAXV.TAXT().R='N'
                         || _lp+=1;
                            TAXJ.cntx_psh();
                            TAXJ.index('UIP');
                            TAXJ.prefix(FAP_K.UID,TAXV.TAXS,TAXV.TAXT().KOD,);
                            {? TAXJ.first()
                            || _res[_lp]:='%1: %2'[TAXJ.TAXT,TAXJ.VALUE]
                            || _res[_lp]:='...'
                            ?};
                            TAXJ.cntx_pop()
                         ?};
                         TAXV.cntx_pop();
                         _lp<14 & TAXV.next()
                      !}
                   ?};
                   _res.add(1)
                ?};
                obj_del(_Tab);
                FAP_K.next()
             !}
          ?}
       ?};
       OKR.next()
   !}
?};
OKR.cntx_pop();
exec('openod_pop','open_tab');
_res

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:20 da18c074bdd2b52cbb72d00de15536e54890159af2ec9356e4bc94c8b4c080d51a14ed3389d6a62ba7f1f14dbacbc5c41959ee23302f1f13d3dd380fe25554f86d1dd2bd8b485bfb70762a7ea3680ebe3747f36ddcf240bc8abb86e3442b3707d1d155def98f37590d37ec6b4dc65d83aae9e00ea3f3433cb98891f69912ffe3

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tpp.fml
:: Utworzony: 06.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Formuły oraz obszary robocze dziedziny TPP
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła inicjująca dla dziedziny TPP
::----------------------------------------------------------------------------------------------------------------------
:: Czytanie stałych z zestawu XINFO
exec('czytaj','#stalesys',,XINFO,'SLWYDZIA','SLWAL','NAZ');

:: Formuła podczytująca dane specyficzne dla firmy
__War_f:="exec('FindInSet','#table',_a,_a,_c,REF.FIRMA,$(_a+'.'+_b))";

:: inicjalizacja wszystkich zmiennych itp. rzeczy dla PXow
exec('init_all','px_init');

:: Obiekty
:: - obiekt do obsługi parametrów
exec('Tpar_decl','tech_param');
:: - obiekt do obsługi drzewa operacji
exec('declare','tech_tree');
:: - obiekt do obsługi dokumentacji elektronicznej + domyślna instancja
exec('declare','tech_doc');

:: Dogenerowywanie zapisów w tabeli DATY
exec('nowedane_DATY','daty');

:: Ustawienie prezycji pól w oknach
exec('fld_prec','win_ust');

:: Ustawia zmienne informujące o dostępie do wartości sprzedaży, zakupu, magazynowych
exec('upr_set','ceny');

:: Formy zatrudnienia
exec('__F_ZATR','object');

PX_VAR.PL_GROP:=exec('get','#params',500390,type_of(''))='T';
~~


\tpp_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Formuła sprawdzająca licencję dla obszaru TPP
::   WY: 'T' - jest licencja, 'N' - nie ma licencji
::  OLD: \Ardea_Purpurea/px_plan.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('lic','#b_domain','TPP')
|| 'T'
|| 'N'
?}


\tpp_pps
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Obszar roboczy - plan strategiczny
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
exec('init','tpp');

:: czyszcze wersje 'smieci'
exec('garbage','px_ver');

:: sprawdzam czy jest założona główna wersja planu, jeśli nie to ją zakładam
exec('mainversion_chk','px_ver');

exec('select_action','px_plan');
~~


\tpp_ppo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [17.00]
:: OPIS: Obszar roboczy - plan operacyjny
::   WE: [_a] - INTEGER - 1 - wersja dystrybucyjna jara, 2 - wersja testowa jara
::  OLD: \pl_res_java/pl_okr3.fml
:: ~OST: INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
AreaTitle.setTabWin(~~);
AreaTitle.setTitle();
{? exec('interm','#system')
|| FUN.emsg(exec('interm_nacc_msg','#system'));
   return()
?};

exec('init','tpp');
_wyglad:='system';

_pth:=tmp_dir();
{? sys_name(0)='WINDOWS'
|| ~~
|? sys_name(0)='U_LINUX'
|| ~~
|| FUN.emsg('Nieobsługiwany system operacyjny.'@);
   return()
?};
_debug:=1;
{? var_pres('_a')=type_of(0)
|| _debug:=_a
?};

_edit:=exec('chk_role','#b__box',exec('operatorUser','#users'),'TPP_PPO_DPPL');

{? _edit>0
||
   {? _debug=2
   || params_exec('orderplan_debug','tpp',ST.WYD().SYMBOL,4,_pth,1,_wyglad)
   || params_exec('orderplan','tpp',ST.WYD().SYMBOL,4,_pth,1,_wyglad)
   ?}
||
   {? _debug=2
   || exec('orderplan_debug','tpp',ST.WYD().SYMBOL,4,_pth,0,_wyglad)
   || exec('orderplan','tpp',ST.WYD().SYMBOL,4,_pth,0,_wyglad)
   ?}
?};
~~


\orderplan
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: Uruchamia panel zarzadzania planem produkcji
::   WE: _a - wymagany typu STRING - symbol wydzialu
::       _b - numer zakladki aktywnej
::       _c - sciezka do folderu tymczasowego z obrazkami
::       _d - redakcja (1), podglad (0)
::       _e - string - symbol schematu kolorów: 'system'/'ocean'
::  OLD: \orderplan/skid_jar.fml
:: ~OST: INEXEDIR,INFCOPY,INFMKDIR,INODBCDSN,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')
|| FUN.emsg(exec('interm_nacc_msg','#system'));
   return()
?};

_sep:=exec('sep','#file');
_dsn:=jdbc_dsn();
{? _dsn='' || return() ?};
_cfg:=exec('get_cfg','#xml','orderplan','org.mcl.czest.orderplan.core.app.Config',0,,'org.mcl.czest.orderplan.core.app.MainClass');
_cfg.UI_SCALE:=0;
_cfg.add_param('URL','string',_dsn);
_cfg.add_param('mjdbcPath','string',exe_dir(0)+_sep+'mjdbc.jar');
_cfg.add_param('planDept','string',_a);
_cfg.add_param('startTab','int',$_b);
_cfg.add_param('tempImage','string',_c);
_cfg.add_param('mode','int',$_d);
_cfg.add_param('is4k','int','0');
_cfg.add_param('moveWyk','string',exec('get','#params',8080,type_of(''),OPERATOR.USER));

{? exec('get','#params',500621,2)='P' &
   exec('get','#params',500612,2)='T' &
   exec('get','#params',500632,2)='T'
|| _cfg.add_param('operTab','int','1')
|| _cfg.add_param('operTab','int','0')
?};
_help:=exec('set_help','#help','TPP_PPO_DPPL');
_help:=gsub(_help,'&','&amp;');
_cfg.add_param('helpAddress','string',_help);

_cfg.add_param('lookAndFeel','string',_e);
_cfg.add_param('sessionId','string',exec('string_to_ascii','#string',KAL_UD.ses_id()));
_cfg.add_param('systemName','string',sys_name(0));

_cfg.add_blob('orderplan.jar');
_can_continue:=1;
{? _can_continue>0 & fmkdir('@'+tmp_dir(),'lib')=''
|| _can_continue:=0
?};
{? _can_continue>0 & fmkdir('@'+tmp_dir(),'fonts')=''
|| _can_continue:=0
?};
{? _can_continue>0
|| _can_continue:=fcopy('@'+exe_dir(0)+_sep+'lib'+_sep+'MacroLAF.jar','@'+tmp_dir()+_sep+'lib'+_sep+'MacroLAF.jar',0,0,1)
?};
{? _can_continue>0
|| _can_continue:=fcopy('Font-Macrologic.iconmap','@'+tmp_dir()+_sep+'fonts'+_sep+'Font-Macrologic.iconmap',1,0,1)
?};
{? _can_continue>0
|| _can_continue:=fcopy('Font-Macrologic.ttf','@'+tmp_dir()+_sep+'fonts'+_sep+'Font-Macrologic.ttf',1,0,1)
?};
{? _can_continue>0
|| _can_continue:=fcopy('Font-Macrologic.variables','@'+tmp_dir()+_sep+'fonts'+_sep+'Font-Macrologic.variables',1,0,1)
?};
{? _can_continue>0
|| _can_continue:=fcopy('@'+exe_dir(0)+_sep+'fonts'+_sep+'Lumen-Icon-Font.ttf','@'+tmp_dir()+_sep+'fonts'+_sep+'Lumen-Icon-Font.ttf',0,0,1)
?};
{? _can_continue>0
|| _can_continue:=fcopy('@'+exe_dir(0)+_sep+'fonts'+_sep+'Lumen-Icon-Font.variables','@'+tmp_dir()+_sep+'fonts'+_sep+'Lumen-Icon-Font.variables',0,0,1)
?};
{? _can_continue>0
|| {? _cfg.getXML('orderplan.xml')>0
   || _cfg.execute()
   ?}
|| FUN.emsg('Nie powiodło się kopiowanie plików pomocniczych do katalogu: %1'@[tmp_dir()])
?};
obj_del(_cfg);
~~


\orderplan_debug
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2009]
:: OPIS: Uruchamia panel zarzadzania planem produkcji
::   WE: _a - wymagany typu STRING - symbol wydzialu
::       _b - numer zakladki aktywnej
::       _c - sciezka do folderu tymczasowego z obrazkami
::       _d - redakcja (1), podglad (0)
::       _e - string - symbol schematu kolorów: 'system'/'ocean'
::  OLD: \orderplan_debug/skid_jar.fml
:: ~OST: INEXEDIR,INODBCDSN
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')
|| FUN.emsg(exec('interm_nacc_msg','#system'));
   return()
?};
_sep:=exec('sep','#file');
_dsn:=jdbc_dsn();
{? _dsn='' || return() ?};
_cfg:=exec('get_cfg','#xml','orderplan_debug','org.mcl.czest.orderplan.core.app.Config',0,,'org.mcl.czest.orderplan.core.app.MainClass');
_cfg.UI_SCALE:=0;
_cfg.add_param('URL','string',_dsn);
_cfg.add_param('mjdbcPath','string',exe_dir(0)+_sep+'mjdbc.jar');
_cfg.add_param('planDept','string',_a);
_cfg.add_param('startTab','int',$_b);
_cfg.add_param('tempImage','string',_c);
_cfg.add_param('mode','int',$_d);
_cfg.add_param('is4k','int','0');

{? exec('get','#params',500621,2)='P' &
   exec('get','#params',500612,2)='T' &
   exec('get','#params',500632,2)='T'
|| _cfg.add_param('operTab','int','1')
|| _cfg.add_param('operTab','int','0')
?};
_cfg.add_param('helpAddress','string',exec('set_help','#help','TPP_PPO_DPPL'));
_cfg.add_param('lookAndFeel','string',_e);
_cfg.add_param('sessionId','string',exec('string_to_ascii','#string',KAL_UD.ses_id()));
_cfg.add_param('systemName','string',sys_name(0));

_cfg.add_blob('orderplan_debug.jar');
{? _cfg.getXML('orderplan.xml')>0
|| _cfg.execute()
?};
obj_del(_cfg);
~~


\tpp_prz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Obszar roboczy - przepisy planistyczne
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
{? exec('chk_role','#b__box',OPERATOR.USER,'TPP_PPS_PPPL')=0 &
   exec('chk_role','#b__box',OPERATOR.USER,'TPP_PPS_DPPL')=0
|| FUN.emsg('Brak uprawnień do obszaru roboczego.'@);
   return()
?};

exec('init','tpp');
exec('icon_before','px_plan');

::plan strategiczny (wielowymiarowy plan pojemnosciowy) - inicjalizacja przepisow systemowych
exec('px_tex_init','px_tex');

:: środowisko
_env:=obj_new(
:: tabela nawigatora
   'TAB',
:: okno nawigatora
   'WER',
:: okno grupowe
   'GRP',
:: okna wertowania przepisów
   'WIN',
:: identyfikatory okien w grupie
   'ID',
:: ukryte akcje
   'HIDDEN',
:: okna redagowania przepisów
   'RED',
:: tytuły zakładek
   'TITLE',
:: prefiksy dla poszczególnych zakładek
   'PREFIX',
:: numer aktywnej zakładki
   'LP',
:: czynności podglądu
   'ACT_VIEW',
:: czynności redagowania
   'ACT_RED'
);
params_set('env',_env);

_max:=4;
_win:=obj_new(_max);
   _win[1]:='WER_M';
   _win[2]:='WER_SYS';
   _win[3]:='WER_MGR';
   _win[4]:='WER_SYS';
   _env.WIN:=_win;
_id:=obj_new(_max);
   _id[1]:='material';
   _id[2]:='technolo';
   _id[3]:='grupy';
   _id[4]:='systemowe';
   _env.ID:=_id;
_hidden:=obj_new(_max);
   _hidden[1]:=':';
   _hidden[2]:='DUPATE:D';
   _hidden[3]:=':';
   _hidden[4]:='DU:D';
   _env.HIDDEN:=_hidden;
_red:=obj_new(_max);
   _red[1]:='RED_M';
   _red[2]:='RED';
   _red[3]:='RED_MGR';
   _red[4]:='RED';
   _env.RED:=_red;
_title:=obj_new(_max);
   _title[1]:='Materiały'@;
   _title[2]:='Karty technologiczne'@;
   _title[3]:='Grupy materiałowe'@;
   _title[4]:='Systemowe'@;
   _env.TITLE:=_title;
_prefix:=obj_new(_max);
   _prefix[1]:=PXoTEX.kind.table('M');
   _prefix[2]:=PXoTEX.kind.table('TKTL');
   _prefix[3]:=PXoTEX.kind.table('MGR');
   _prefix[4]:=PXoTEX.kind.system();
   _env.PREFIX:=_prefix;
_act_view:=obj_new(_max);
   _act_view[1]:='TPP_PPS_PPPL';
   _act_view[2]:='TPP_PPS_PPPL';
   _act_view[3]:='TPP_PPS_PPPL';
   _act_view[4]:='TPP_PPS_PPPL';
   _env.ACT_VIEW:=_act_view;
_act_red:=obj_new(_max);
   _act_red[1]:='TPP_PPS_DPPL';
   _act_red[2]:='TPP_PPS_DPPL';
   _act_red[3]:='TPP_PPS_DPPL';
   _act_red[4]:='TPP_PPS_DPPL';
   _env.ACT_RED:=_act_red;

_tab:=tab_tmp(1,'LP','INTEGER','Lp','NAZWA','STRING[20]','');
{! _it:=1.._max
|! _tab.LP:=_it;
   {? exec('chk_role','#b__box',OPERATOR.USER,_act_view[_it])>0 |
      exec('chk_role','#b__box',OPERATOR.USER,_act_red[_it])>0
   || _tab.NAZWA:=_title[_it]; _tab.add()
   ?}
!};

_wer:=_tab.mk_sel(,,,'nawigatortpp');
_tab.win_fld(_wer,,'NAZWA');
_tab.win_sel(_wer);

_before:="
   tab_hide(,0,'przepis');
   grp_disp(params_get().env.TAB,params_get().env.WER,0);
   ~~
";
_grp:=_tab.grp_make(,_before,'nawigatorgrp',,,"exec('exit','zws',_a)",,'maximized');
_after_refresh:="
   params_set(params_get());
   _env:=params_get().env;
   _tab:=_env.TAB;
   _prefix:=_env.PREFIX;
   _win:=_env.WIN;
   _red:=_env.RED;
   _hidden:=_env.HIDDEN;
   PX_TEX.index('KSYM');
   PX_TEX.prefix(_prefix[_tab.LP],); PXoTEX.KIND:=_prefix[_tab.LP];
   tab_hide(,1,'przepis'); tab_show(_tab.LP,'przepis');
   PX_TEX.win_edit(_red[_tab.LP]);
   PX_TEX.actions(_win[_tab.LP],_hidden[_tab.LP],,1);
   grp_disp(PX_TEX,_win[_tab.LP],0)
";
_tab.grp_sel(_grp,,_wer,,_after_refresh,,,,,,,,'maximized');

_tab.grp_splt(_grp,,'vertical','przepis',',20%');
{! _it:=1.._max
|!
   _before:="
      _env:=params_get().env;
      _tab:=_env.TAB;
      _red:=_env.RED;
      PX_TEX.win_edit(_red[_tab.LP]);
      ~~
   ";
   _tab.grp_sel(_grp,PX_TEX,_env.WIN[_it],_env.TITLE[_it],,,,20,_before,,,,'maximized',_env.ID[_it])
!};

:: przygotujemy zmienna do prefiksów i blankowania
{? var_pres('PXoTEX')<100 || exec('PXoTEX','px_tex') ?};
PXoTEX.KIND:='';
PXoTEX.PrfxVal:=null();
PX_TEX.cntx_psh();
M.cntx_psh();
PX_TEX.clear();

_env.TAB:=_tab;
_env.WER:=_wer;
_env.GRP:=_grp;

:: Ustawienie okien dla M
exec('slo_m_ok','material','T',1,,'W');

:: Filtrowanie kartoteki M
exec('filter','material','WP');

:: Header w oknie wertowania M
exec('wpm_opis','material','WP',' — '+exec('zakr_naz','material',ZAKR.MATU));

AreaTitle.setTabWin(_tab,_grp);
AreaTitle.setTitle();

_tab.win_sel(_grp);
_tab.first();
PX_TEX.index('KSYM');
PX_TEX.prefix(_prefix[1],); PXoTEX.KIND:=_prefix[1];
_tab.select(,,,,_env.ID[1]);

:: Przywracam tytuł i akcje w słowniku materiałów
exec('filter_clear','material');
M.cntx_pop();
PX_TEX.cntx_pop();
~~


\tpp_gop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SZP [12.30]
:: OPIS: Obszar roboczy - grupy operacji
::  OLD: \shGrOp/gr_oper.fml
::----------------------------------------------------------------------------------------------------------------------
exec('init','tpp');

exec('__KAL','object');

_env_grop:=exec('env','zl_grop');
params_set('env_grop',_env_grop);

ZGP.cntx_psh();
ZGH.cntx_psh();
TOPER.cntx_psh();
exec('filtrPlResGr','zl_grop');

GROP.win_edit('RED');
GROP.win_sel('GRP');
GROPP.win_edit('RED');

AreaTitle.setTabWin(GROP,'GRP');
AreaTitle.setTitle();

_formikon:="
   {? GROP.PROBLEM='T' & GROP.PROBKLAS=exec('prob_class_blocking','zl_wkj')
   || exec('problem_blocking','icon')
   |? GROP.PROBLEM='T' & GROP.PROBKLAS=exec('prob_class_suspensing','zl_wkj')
   || exec('problem_suspensing','icon')
   |? GROP.PROBLEM='T' & GROP.PROBKLAS=exec('prob_class_information','zl_wkj')
   || exec('problem_information','icon')
   || exec('pusta','#icon')
   ?}
";
GROP.win_fml('WER',,'OPIS',,'ICON_BEFORE',_formikon);

_formikon:="exec('grop_icon','po_ogr')";
GROP.win_fml('WER',,'KOD',,'ICON_BEFORE',_formikon);

_formikon:="exec('icon_stan','zl_head')";
GROPP.win_fml('WER',,'ZL','SYM','ICON_BEFORE',_formikon);

exec('icon_before','po_param');

GROP.index('OKOD');
GROP.clear();
exec('clFiltrGrOp','zl_grop');
exec('grop_f_set','zl_grop');
GROP.select();
GROP.f_clear();

PL_RES.f_clear();
TOPER.cntx_pop();
ZGH.cntx_pop();
ZGP.cntx_pop();
~~

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 735f66609a100f42fd827e3a0baf9e209bd2f13606a73362fdc4d3216bb1ac6547ce203bdeea5f3f114ae346826d47bf148f86f9e135685cd165a282387f4bc8b19f74799e34d04870793c9e64a5667e1b8a7bc3e261936d438bc7f0108f507d9de802d6f8fa52c8f6f88086e04bd29a8b038ee29fe89a78aaf0a21706721fe4

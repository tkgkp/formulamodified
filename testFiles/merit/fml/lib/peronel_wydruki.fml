:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: personel_wydruki.fml
:: Utworzony: 06.09.2022
:: Autor: MicKoc
:: Systemy: MERIT
::======================================================================================================================
:: Zawartość: Biblioteka funkcji zwracającej teksty stałe na potrzebę sprawozdań
::======================================================================================================================

\info_wojsko
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [21.37]
:: OPIS: Informacja o obowiązku
::   WE: [_a] [DATE] - data obowiązywania
::   WY: tekst
::----------------------------------------------------------------------------------------------------------------------
_dt:={? var_pres('_a')=type_of(date()) || _a || date() ?};
{? _dt<date(2022,4,23)
|| 'na podstawie art. 124 ustawy o powszechnym obowiązku obrony Rzeczypospolitej Polskiej'@
|| 'na podstawie ustawy z dnia 11 marca 2022 r. o obronie Ojczyzny'@
?}


\zaswsw_dane
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [23.25]
:: OPIS: Pobieranie danych do Świadectwa pracy
::----------------------------------------------------------------------------------------------------------------------
_kom_naz:=_kom_syg:=_szcz_war:='';
_kom_kw:=0;
P_INFO.cntx_psh();
P_INFO.index('OD');
P_INFO.prefix(exec('ref_firma','ustawienia'),P.OSOBA);
{? P_INFO.first()
|| {!
   |? {? (P_INFO.SZCZOD<>#0 & P_INFO.SZCZDO<>#0) & ((sw.UOD=#0 & P_INFO.SZCZOD<=sw.UDO)
         |
         (sw.UOD<>#0 & P_INFO.SZCZOD<=sw.UDO & P_INFO.SZCZDO>=sw.UOD ))
      || __TAB.OD:={? sw.UOD<>#0 & P_INFO.SZCZOD<sw.UOD || sw.UOD || P_INFO.SZCZOD ?};
         __TAB.DO:={? sw.UDO<>#0 & P_INFO.SZCZDO>sw.UDO || sw.UDO || P_INFO.SZCZDO ?};
         __TAB.add(1)
      ?};
      P_INFO.next()
   !}
?};
P_INFO.cntx_pop();
H.cntx_psh();
H.index('_HISTKOD');
H.prefix(P.ref(),'Z');
{? H.first()
|| {!
   |? {? H.SP='T'
      || {? (sw.UOD=#0 & H.OD<=sw.UDO) | (sw.UOD<>#0 & H.OD<=sw.UDO & H.DO>=sw.UOD)
         || __TAB.OD:=H.OD;
            __TAB.DO:=H.DO;
            __TAB.add(1)
         ?}
      ?};
      H.next()
   !}
?};
H.cntx_pop();
{? __TAB.first()
|| {!
   |? _do:=__TAB.DO;
       _ref:=__TAB.ref();
      {? __TAB.next()
      || {? __TAB.OD<=(_do+1)
         || _do:=__TAB.DO;
            __TAB.del();
            __TAB.seek(_ref);
            {? __TAB.DO<_do || __TAB.DO:=_do ?};
            __TAB.put();
            1
         || 1
         ?}
      ?}
   !};

   {? __TAB.first()
   || {!
      |? {? __TAB.OD<>#0 | __TAB.DO<>#0
         || _szcz_war+='od %1 do %2,' [__TAB.OD$4,{? __TAB.DO<>#0 || __TAB.DO$4 || 'nie podano'  ?}]
         ?};
         __TAB.next()
      !}
   ?};
   __TAB.erase()
?};
{? _szcz_war<>'' || _szcz_war-=1  ?};
sw.OK:=0;
undefine();
define('DTW',date(),'Data wystawienia'@);
_stale:=exec('zasw_pob','profile');
define('NIRE',{? _stale.nipregon<>'' || _stale.nipregon || 'R' ?},'Identyfikator pracodawcy [N/R]'@,
       'N - NIP / R - REGON-PKD'@,1,2);
define('HUSP',~~,'Ustanie stosunku pracy / stosunku służbowego'@);
define('RUM','R','Rodzaj [R/W]'@,'R - rozwiązanie / W - Wygaśnięcie'@,1,2);
{? sw.USPKODK+1='W'
|| DEFINE.RUM:='W'
?};
:: ER/WRT/XP/23.25/2401/0032 Punkt czwarty świadectwa pracy nie mieści się na wydruku, decyzja ATA, 08.01.2024r.
define('ORO',sw.ROU,'Przyczyna'@,'Przyczyna ustania stosunku pracy'@,150,90);
{? sw.USPKODO<>''
|| DEFINE.ORO:=sw.USPKODO
?};
:: ER/WRT/XP/23.25/2401/0032 Punkt czwarty świadectwa pracy nie mieści się na wydruku, decyzja ATA, 08.01.2024r.
define('PRO',sw.USPPPRAK,'Podstawa'@,'Podstawa prawna ustania stosunku pracy'@,200,90);
define('HD1',~~,'Skrócony okres wypowiedzenia'@);
define('OD1',#0,'od dnia'@);
define('DO1',#0,'do dnia'@);
{? ~sw.ALL_UM
|| define('HD7',~~,'Urlop ojcowski'@);
   define('OJCIEC_W',{? nieo[15] || (nieo[15]/7)$0 ?},'   w wymiarze tygodni'@,'',,3,-1);
   define('OJCIEC_C',{? nieo[19] || nieo[19] ?},'   ilość części'@,'',,3,-1);
   _rodzic:=
      {? proc_exe('url_rodz@zasw',$P.ref(),$(sw.UDO~1)+'-'+$(sw.UDO~2)+'-'+$(sw.UDO~3),1154,,$sw.UOD).first()
      || 100
      || 0
      ?};
   {? ~_rodzic
   || define('HD8',~~,'Urlop rodzicielski'@);
      define('RODZIC_P','','  podstawa prawna udzielenia urlopu'@,'',150,90);
      define('RODZIC_W',0,'   w wymiarze tygodni'@,'',,3,-1);
      define('RODZIC_C',0,'   ilość części'@,'',,3,-1)
   ?};
   {? nieo[6]
   || define('HD10',~~,'Dane do urlopu wychowawczego'@);
      define('WYCH_P','','  podstawa prawna udzielenia urlopu'@,'',150,90)
   ?}
?};
define('HD9',~~,'korzystał z ochrony stosunku pracy, art. 186(8) § 1 pkt 2 Kp'@);
define('OCHRONA','','   w okresie (okresach)'@,'',150,90);
define('HD4',~~,'Praca w szczególnych'@);
define('SCW',_szcz_war,'warunkach/charakterze'@,'',250,90);
define('DUR','','Inne uprawnienia lub świadczenia'@,'',70,90);
{? PAR_SKID.get(230)<>'T'
|| define('HD2',~~,'Zajęcia wynagrodzenia'@);
   define('OKR',_kom_naz,'oznaczenia komornika'@,'',120,90);
   define('NRE',_kom_syg,'nr sprawy egzekucyjnej'@,'',120,90);
   define('WKP',_kom_kw,'potrącona kwota'@,'',,17,2)
?};
define('IUH',~~,'Informacje uzupełniające'@);
define('IU1','','','',70,90);
define('IU2','','','',70,90);
define('IU3','','','',70,90);
define('IU4','','','',70,90);
define('HD5',~~,'Czas na wystąpienie o sprostowanie'@);
define('ODW',14,'do pracodawcy / sądu pracy w dniach'@,
       'obowiązuje 14 dni od 7 września'@,2,3,0);
define('SAD',_stale.sad,'Odwołanie do Sądu Rejonowego - Sądu Pracy... (dotyczy 7 dni)'@,'Np. "we Wrocławiu"'@,
   150,90);
{? par.P
|| def_btn('text=%1'['Zakończ'@],"status:=1;'key:F2'");
   def_btn('text=%1'['Drukuj'@],"status:=0;'key:F2'");
   def_btn('text=%1'['Anuluj'@],"status:=0; 'key:Esc'");
   par.P:=''
|| def_btn('text=%1'['OK'@],"'key:F2'");
   def_btn('text=%1'['Anuluj'@],"'key:Esc'")
?};
{? czy_szablon | def_edit(
   "  {? DEFINE.DTW<sw.UDO
      || FUN.emsg('Data wystawienia świadectwa pracy nie może być wcześniejsza niż data zakończenia umowy.'@);
         DEFINE.DTW:=date();
         'DTW'
      || 1
      ?}
   ",'Dane do świadectwa pracy dla %1 %2 nr teczki %3'@[P.OSOBA().NAZWISKO,P.OSOBA().PIERWSZE,|P.T]
   )
|| _stale.sad:=DEFINE.SAD;
   _stale.nipregon:=DEFINE.NIRE;
   sw.OK:=1;
   exec('zasw_zap','profile',_stale)
?};
{? sw.OK
|| define('UDO',sw.UDO);
   define('UOD',sw.UOD);
   {? PAR_SKID.get(230)='T'
   || define('OKR','TAB','oznaczenia komornika'@,,120,90);
      define('NRE',_kom_syg,'nr sprawy egzekucyjnej'@,'',120,90);
      define('WKP',_kom_kw,'potrącona kwota'@,'',,17,2)
   ?};
   cnt+=1;
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'F_NAZWA',KST.NAZWA,
      'MIEJSCE',PAR_WYDR.MIEJSC,
      'DATA',DEFINE.DTW$4,
      'F_ULICA',exec('ulica','stalesys'),
      'F_POCZTA',exec('poczta','stalesys'),
      'F_REGON',KST.REG,
      'F_NIP',{? DEFINE.NIRE='N' || KST.NIP || '' ?},
      'F_PKD',KST.PKD,
      'F_LOGO',{? PAR_WYDR.LOGO || exec('tmp_cp_logo','img_blob') || '' ?}
   );
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'DO_DATY',$(DEFINE.UDO~1)+'-'+$(DEFINE.UDO~2)+'-'+$(DEFINE.UDO~3),
      'PRACOWN',$P.ref(),
      'OSOBA',$P.OSOBA,
      'NAZWISKO',P.OSOBA().NAZWISKO,
      'IMIE',OSOBA.PIERWSZE,
      'DRUGIEIM',OSOBA.DRUGIE,
      'OJCIEC',OSOBA.OJCIEC,
      'MATKA',OSOBA.MATKA,
      'UR_DATA',OSOBA.UR_DATA$4,
      'PLEC',OSOBA.PLEC
   );
:: ER/WRT/XP/23.25/2401/0032 Punkt czwarty świadectwa pracy nie mieści się na wydruku, decyzja ATA, 08.01.2024r.
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'RUM',DEFINE.RUM,
      'OPIS_ROZ',100+DEFINE.ORO,
      'OPIS_RO2',100-DEFINE.ORO,
      'PP_ROZ',100+DEFINE.PRO,
      'PP_RO2',100-DEFINE.PRO,
      'SKRWYPOD',{? DEFINE.OD1<>#0 || DEFINE.OD1$4 || '' ?},
      'SKRWYPDO',{? DEFINE.DO1<>#0 || DEFINE.DO1$4 || '' ?}
   );
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'SZCZ_WAR',DEFINE.SCW,
      'URLOPDOD',DEFINE.DUR,
      'KOMORNIK',DEFINE.OKR,
      'NRSPRAWY',DEFINE.NRE,
      'POTRKW',DEFINE.WKP,
      'SADPRACY',{? DEFINE.ODW=14 || '14' || DEFINE.SAD ?}
   );
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'INFUZUP1',DEFINE.IU1,
      'INFUZUP2',DEFINE.IU2,
      'INFUZUP3',DEFINE.IU3,
      'INFUZUP4',DEFINE.IU4,
      'D_ZATR',{? DEFINE.UOD=#0
               || $(P.DZA~1)+'-'+$(P.DZA~2)+'-'+$(P.DZA~3)
               || $(DEFINE.UOD~1)+'-'+$(DEFINE.UOD~2)+'-'+$(DEFINE.UOD~3)
               ?},
      'OD_DATY',{? DEFINE.UOD=#0 || '0' || $(DEFINE.UOD~1)+'-'+$(DEFINE.UOD~2)+'-'+$(DEFINE.UOD~3) ?},
      'OCHRONA',DEFINE.OCHRONA,
      'ODWOL', $DEFINE.ODW
   );
   {? sw.ALL_UM
   || _ojciec:=
         {? proc_exe('url_rodz@zasw',$P.ref(),$(sw.UDO~1)+'-'+$(sw.UDO~2)+'-'+$(sw.UDO~3),1153).first()
         || 100
         ?};
      _rodzic:=
         {? proc_exe('url_rodz@zasw',$P.ref(),$(sw.UDO~1)+'-'+$(sw.UDO~2)+'-'+$(sw.UDO~3),1154).first()
         || 100
         ?};
      _wych:=
         {? proc_exe('url_rodz@zasw',$P.ref(),$(sw.UDO~1)+'-'+$(sw.UDO~2)+'-'+$(sw.UDO~3),114).first()
         || 'DATABASE'
         || ''
         ?};
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'OJCIEC_W',sw.OJCIEC_W:=_ojciec,
         'RODZIC_W',sw.RODZIC_W:=_rodzic,
         'URLOPW',_wych
      )
   || {? {? nieo[15] || (nieo[15]/7)$0 ?}=DEFINE.OJCIEC_W & DEFINE.OJCIEC_W
         & proc_exe('url_rodz@zasw',$P.ref(),$(sw.UDO~1)+'-'+$(sw.UDO~2)+'-'+$(sw.UDO~3),1153,,$sw.UOD).first()
      || _ojciec:=100
      || _ojciec:=0
      ?};
      {? proc_exe('url_rodz@zasw',$P.ref(),$(sw.UDO~1)+'-'+$(sw.UDO~2)+'-'+$(sw.UDO~3),114,,$sw.UOD).first()
      || _wych:='DATABASE'
      || _wych:=''
      ?};
      exec('ins_par','rap_zew',__xr_ins,cnt,
         'OJCIEC_W',{? _ojciec || sw.OJCIEC_W:=_ojciec || DEFINE.OJCIEC_W ?},
         'OJCIEC_C',DEFINE.OJCIEC_C,
         'RODZIC_P',{? _rodzic || '' || DEFINE.RODZIC_P ?},
         'RODZIC_W',{? _rodzic || sw.RODZIC_W:=_rodzic || DEFINE.RODZIC_W ?},
         'RODZIC_C',{? _rodzic || 0 || DEFINE.RODZIC_C ?},
         'URLOPW',{? _wych='' & nieo[6] || $nieo[6] || _wych ?},
         'WYCH_P',{? _wych='' & nieo[6] || DEFINE.WYCH_P || '' ?}
      )
   ?};
   exec('urlop2swiadectwa','!pkd_zes_orza');
   dn:="{? _a=0 || '' || $_a+' '+{? _a<>int(_a) || 'dnia' |? _a=1 || 'dzień' || 'dni' ?} ?}";
   gd:="{? _a=0 || '' || $_a+' '+{? _a<>int(_a) || 'godziny' |? _a=1 || 'godzina' || 'godzin' ?} ?}";
   {? nieo[1]$2<>nieo[2]/fnorma(sw.UDO)$2 || nieo[1]:=nieo[2]/fnorma(sw.UDO)$2 ?};
   nieo[22]:=nieo[22]$2;
   {? nieo[22]<>nieo[23]/fnorma(sw.UDO)$2 || nieo[22]:=nieo[23]/fnorma(sw.UDO)$2 ?};
   nieo[27]:=nieo[27]$2;
   {? nieo[27]<>nieo[28]/fnorma(sw.UDO)$2 || nieo[27]:=nieo[28]/fnorma(sw.UDO)$2 ?};

   exec('ins_par','rap_zew',__xr_ins,cnt,
      'URLOP',{? nieo[1] || dn(nieo[1]) || '0 dni' ?},
      'URLOPG',{? nieo[2] | 2004<=sw.UDO~1 || $nieo[2] || '' ?},
      'URLOPNZ',{? nieo[18] || dn(nieo[18]) || '0 dni' ?},
      'URLOPNZG',{? nieo[4] | 2004<=sw.UDO~1 || $nieo[4] || '' ?},
      'URLNSP',{? nieo[22] || dn(nieo[22]) || '0 dni' ?},
      'URLNSPG',{? nieo[23] | 2004<=sw.UDO~1 || $nieo[23] || '' ?},
      'URLREH',{? nieo[27] || dn(nieo[27]) || '0 dni' ?},
      'URLREHG',{? nieo[28] | 2004<=sw.UDO~1 || $nieo[28] || '' ?},
      'URLOPB',{? nieo[5] || $nieo[5] || '' ?},
      'OPIEKA',{? sw.DS_LIM='R' || dn(nieo[7]) || gd(nieo[7]) ?}
   );
   par.ZAL:={? sw.IL>1 || '0' || par.ZAL ?};
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'CHOROBA',dn(nieo[8]),
      'WOJSKO', {? nieo[9] | nieo[21] || 'WOJSKO' || '' ?},
      'INFO_WO1',{? 1+nieo[26]='1' || exec('info_wojsko','peronel_wydruki',date(2022,4,22)) || '' ?},
      'INFO_WO2',{? nieo[26]+1='1' || exec('info_wojsko','peronel_wydruki',date(2022,4,23)) || '' ?},
      'OKR_NSKL',dn(nieo[10]),
      'ART92P1',{? nieo[11] || $nieo[11] || '' ?},
      'URLOPBW',{? nieo[5] | nieo[9] | nieo[21] || 'URLOPBW' || '' ?},
      'CZY_ZAP',par.ZAL,
      'ALL_UM',sw.ALL_UM
   );
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'UOP', dn(nieo[29]),
      'USW', {? sw.DS_LIM_SW='R' || dn(nieo[30]) || gd(nieo[30]) ?}
   );
   {? __ppsf
   || _ppsfr:=exec('FindInSet','#table','PPSFR','KOD','ZDALN_OK',exec('ref_firma','#firma'));
      _startPz:=date(DEFINE.UDO~1,1,1);
      {? _startPz<DEFINE.UOD
      || _startPz:=DEFINE.UOD
      ?}
   ?};
   exec('ins_par','rap_zew',__xr_ins,cnt,
      'ZDALNA',dn({? __ppsf || exec('zlicz_ppsf','ppsf',P.ref(),_ppsfr,_startPz,DEFINE.UDO).DR || 0 ?})
   )
?}


\zaswsw_nieobecnosci
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [23.25]
:: OPIS: Nieobecności dla Świadectwa pracy
::----------------------------------------------------------------------------------------------------------------------
:: Jezeli swiadectwo drukowane jest dla wybranej umowy, i wskazana zostala kolejna umowa, nie
:: rozpoczynajaca sie 1 stycznia, to w celu prawidlowego rozliczenia urlopu, potrzebujemy informacji o
:: wykorzystanym w biezacym roku
:: urlopie, ale sprzed drukowanej umowy, czyli od 1 stycznia do sw.UOD.
:: Wyniki zapisujemny w kolejnych elementach tablicy nieo:
:: 16 - dni urlopu wypoczynkowego i na zadanie
:: 17 - godziny urlopu wypoczynkowego i na zadanie
:: 25 - godziny urlopu dodatkowego
:: 27 - dni urlopu rehabilitacyjnego
:: 28 - godziny urlopu rehabilitacyjnego
:: Różnice Xpertis<>Merit
::       Akronim Xpertis   Akronim  Merit Xpertis  Merit 22.26
::       URLOP             URLOP          1        1
::       URLOPG            URLOPG         2        2
::       URLOPNZ           URLOPNZ        18       18
::       URLOPNZG          URLOPNZG       4        4
::       URLNSP            URLNSP         27       22
::       URLNSPG           URLNSPG        28       23
::       URLREH            URLREH         25       27
::       URLREHG           URLREHG        26       28
::       URLOPB            URLOPB         5        5
::       OPIEKA            OPIEKA         7        7
::                         ?              23       24
::                         ?              24       25

{? sw.UOD<>#0 & sw.UOD<>P.DZA & sw.UOD~1=sw.UDO~1 & sw.UOD<>date(sw.UOD~1,1,1) & N.OD<sw.UOD
   &
   N.DO>=date(sw.UOD~1,1,1) & (N.NB=KST_PAR.URLOP | N.NB=KST_PAR.URL_CHOR | __RUB.sys_attr(N.NB,1113))
|| _pocz:=date(sw.UOD~1,1,1);
   _kon:=sw.UOD-1;
   _czy_zaw:=N.DO<=_kon & N.OD>=_pocz;
   _new_od:=_new_do:=#0;
   {? ~_czy_zaw
   || _new_od:={? N.OD<_pocz || _pocz || N.OD ?};
      _new_do:={? N.DO>_kon || _kon || N.DO ?}
   ?};
   {? __RUB.sys_attr(N.NB,1113)
   || _nr_nieo1:=25;
      _nr_nieo2:=24
   |? __RUB.sys_attr(N.NB,1171)
   || _nr_nieo1:=28;
      _nr_nieo2:=27
   || _nr_nieo1:=17;
      _nr_nieo2:=16
   ?};
   nieo[_nr_nieo1]+={? _czy_zaw || N.NG || exec('nominal','godziny',_new_od,_new_do) ?};
   {? 2004<=sw.UOD~1
   || nieo[_nr_nieo2]+={? _czy_zaw || N.NG || exec('nominal','godziny',_new_od,_new_do) ?}/fnorma(N.OD)
   || nieo[_nr_nieo2]+={? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?}
   ?}
|? (sw.UOD=#0 & N.OD<=sw.UDO) | (sw.UOD<>#0 & N.OD<=sw.UDO & N.DO>=sw.UOD)
|| _nb:=N.NB().RN;
   _nod:=N.OD;
::    zmienna _czy_nrok okresla czy wybrana nieobecnosc jest z ostatniego roku dla ktorego jest
::    robiony wydruk, jest ona okreslana na podstawie daty wydruku lub zmiennej sw.UDO (jesli
::    nie zerowa) ktora jest okreslana na podstawie daty do umowy (ktora jesli jest ostatnia, jest
::    rownoznaczna z P.DZ)
::    zmienna _czy_zaw okresla czy cala nieobecnosc zawiera sie w okresie dla ktorego jest tworzone zasw
   _czy_nrok:=sw.UDO~1=(N.OD~1);
   _czy_zaw:=N.DO<=sw.UDO & (sw.UOD=#0 | N.OD>=sw.UOD);
   _new_od:=_new_do:=#0;
   {? ~_czy_zaw
   || _new_od:={? N.OD<sw.UOD || sw.UOD || N.OD ?};
      _new_do:={? N.DO>sw.UDO || sw.UDO || N.DO ?}
   ?};
   {? (N.NB=KST_PAR.URLOP | N.NB=KST_PAR.URL_CHOR | __RUB.sys_attr(N.NB,1114)) & _czy_nrok
   || nieo[{? N.NB<>KST_PAR.URL_CHOR || 2 || 4 ?}]+=
         {? _czy_zaw || N.NG || exec('nominal','godziny',_new_od,_new_do) ?};
      {? 2004<=sw.UDO~1
      || nieo[{? N.NB<>KST_PAR.URL_CHOR || 1 || 3 ?}]+=({? _czy_zaw
                                                        || N.NG
                                                        || exec('nominal','godziny',_new_od,_new_do)
                                                        ?}/fnorma(N.OD))
      || nieo[{? N.NB<>KST_PAR.URL_CHOR || 1 || 3 ?}]+={? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?}
      ?};
      {? N.NB=KST_PAR.URL_CHOR
      || nieo[18]+={? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?}
      ?}
   |? __RUB.sys_attr(N.NB,1113) & _czy_nrok
   || nieo[23]+={? _czy_zaw || N.NG || exec('nominal','godziny',_new_od,_new_do) ?};
      {? 2004<=sw.UDO~1
      || nieo[22]+=({? _czy_zaw || N.NG || exec('nominal','godziny',_new_od,_new_do) ?}/fnorma(N.OD))
      || nieo[22]+={? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?}
      ?}
   |? __RUB.sys_attr(N.NB,1171) & _czy_nrok
   || nieo[28]+={? _czy_zaw || N.NG || exec('nominal','godziny',_new_od,_new_do) ?};
      nieo[27]+=({? _czy_zaw || N.NG || exec('nominal','godziny',_new_od,_new_do) ?}/fnorma(N.OD))
   |? __RUB.sys_attr(_nb,112,_nod)
   || nieo[5]+={? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?}
   |? __RUB.sys_attr(_nb,114,_nod)
   || nieo[6]+={? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?}
   |? _czy_nrok & __RUB.sys_attr(_nb,121,_nod)
   || nieo[8]+={? _czy_zaw || N.NK || _new_do-_new_od+1 ?}
   |? __RUB.sys_attr(_nb,1323,_nod)
   || {? N.OD<date(2022,4,23)
      || nieo[26]:='1'+(nieo[26]+1)
      || N.cntx_psh();
         _wojOd:=N.OD;
         {! |?
            _prev:=N.prev();
            {? _prev & N.DO+1=_wojOd & __RUB.sys_attr(N.NB,1323,_nod)
            || {? N.OD<date(2022,4,23)
               || nieo[26]:='1'+(nieo[26]+1); 0
               || _wojOd:=N.OD;
                  _prev
               ?}
            || nieo[26]:=(1+nieo[26])+'1';
               0
            ?}
         !};
         N.cntx_pop()
      ?};
      nieo[9]+={? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?}
   |? _czy_nrok & __RUB.sys_attr(_nb,1162,_nod)
   || {? sw.UDO~1>=2016
      || nieo[7]+=
            {? _czy_zaw
            || {? sw.DS_LIM='R' || N.NR || ceil(N.NG) ?}
            || {? sw.DS_LIM='R' || __KAL.w_days(_new_od,_new_do) || ceil(__KAL.w_hours(_new_od,_new_do)) ?}
            ?}
      || nieo[7]+={? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?}
      ?}
   |? _czy_nrok & __RUB.sys_attr(_nb,1181,_nod)
   || nieo[30]+=
         {? __nru01
         || {? _czy_zaw
            || {? sw.DS_LIM_SW='R' || N.NR || ceil(N.NG) ?}
            || {? sw.DS_LIM_SW='R' || __KAL.w_days(_new_od,_new_do) || ceil(__KAL.w_hours(_new_od,_new_do)) ?}
            ?}
         || 0
         ?}
   |? _czy_nrok & __RUB.sys_attr(_nb,1191,_nod)
   || nieo[29]+={? __nru01 || {? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?} || 0 ?}
   |? N.OD>=date(sw.UDO~1-2,sw.UDO~2,{? sw.UDO=date(sw.UDO~1,sw.UDO~2,0) || 0 || sw.UDO~3 ?})
      & __RUB.sys_attr(_nb,1153,_nod)
   || nieo[15]+={? _czy_zaw || N.NK || _new_do-_new_od+1 ?};
      {? nieo[20]<>N.OD || nieo[19]+=1 ?}; nieo[20]:=N.DO+1
   |? __RUB.sys_attr(_nb,13232,_nod)
   || nieo[21]+={? _czy_zaw || N.NR || __KAL.w_days(_new_od,_new_do) ?}
   ?};
   _lim_data:=date(1991,11,14);
   {? __RUB.sys_attr(_nb,196,_nod) & _lim_data<N.DO
   || nieo[10]+=
         {? _czy_zaw
         || {? N.OD<=_lim_data
            || N.DO-_lim_data
            || N.NK
            ?}
         || {? N.OD<=_lim_data
            || N.DO-_lim_data
            || _new_do-_new_od+1
            ?}
         ?}
   ?};
   {? date(2003,1,1)<=N.OD & N.OD<=date(2003,12,0)
   || {? N.DK
      || nieo[11]+=N.DK
      |? N.DN>=1 & __RUB.sys_attr(N.NB().RN,12111,N.OD) & N.DO-N.OD+1<=6
         & N.KDCH().KOD<>'C' & N.OD<=sw.UDO
      || nieo[11]+=1
      ?}
   ?}
?}


\zaswsw_przebieg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [23.25]
:: OPIS: Przebiegi zatrudnienia dla Świadectwa pracy
::----------------------------------------------------------------------------------------------------------------------
{? {? par.H_UM=null()
   || FUN.ask('%1Czy świadectwo ma być drukowane tylko dla wybranych umów pracownika?'@
         ['Pracownik %1 %2 nr teczki %3\n'@[P.OSOBA().NAZWISKO,P.OSOBA().PIERWSZE,|P.T]])
   || 0
   ?}
|| H_UM.prefix();
   H_UM.f_set('OD,DO','left join RU','"H_UM".DO is not null and "H_UM".P=:_a',P.ref());
   {? H_UM.f_first()
   || {? H_UM.select()
      || H_UM.prefix(P.ref());
         {? H_UM.find_key(sw.UOD)
         || {!
            |? {? H_UM.DO~1=sw.UDO~1 || ekw+=H_UM.EKW; ekw_nsp+=H_UM.EKW_NSP ?};
               {? H_UM.DO=sw.UDO
               || S_ZUS.cntx_psh();
                  {? H_UM.USPKOD
                  || S_ZUS.prefix('USPKOD',H_UM.USPKOD().KOD);
                     {? S_ZUS.first()
                     || sw.USPKODO:=S_ZUS.memo_txt(0,1,'OPIS');
                        sw.USPKODK:=S_ZUS.KOD
                     ?}
                  ?};
                  {? H_UM.USPPPRAK
                  || S_ZUS.prefix('USPPPRAK',H_UM.USPPPRAK().KOD);
                     {? S_ZUS.first()
                     || sw.USPPPRAK:={? S_ZUS.KOD='550' || H_UM.USPPPRAT || S_ZUS.memo_txt(0,1,'OPIS') ?}
                     ?}
                  ?};
                  S_ZUS.cntx_pop();
                  sw.ROU:=H_UM.ROU().NAZWA
               ?};
               {? H_UM.SW<>'T' || H_UM.SW:='T'; H_UM.put() ?};
               H_UM.next() & H_UM.DO<=sw.UDO
            !}
         ?}
      ?}
   || Text:='Nie można drukować świadectwa dla niezakończonej umowy.';
      FUN.emsg(Text)
   ?};
   H_UM.f_clear()
|| H_UM.prefix(P.ref());
   {? H_UM.last() & H_UM.DO<>#0
   || sw.UDO:=H_UM.DO;
      S_ZUS.cntx_psh();
      {? H_UM.USPKOD
      || S_ZUS.prefix('USPKOD',H_UM.USPKOD().KOD);
         {? S_ZUS.first()
         || sw.USPKODO:=S_ZUS.memo_txt(0,1,'OPIS');
            sw.USPKODK:=S_ZUS.KOD
         ?}
      ?};
      {? H_UM.USPPPRAK
      || S_ZUS.prefix('USPPPRAK',H_UM.USPPPRAK().KOD);
         {? S_ZUS.first()
         || sw.USPPPRAK:={? S_ZUS.KOD='550' || H_UM.USPPPRAT || S_ZUS.memo_txt(0,1,'OPIS') ?}
         ?}
      ?};
      S_ZUS.cntx_pop();
      sw.ROU:=H_UM.ROU().NAZWA;
      ekw:=0;
      ekw_nsp:=0;
      sw.ALL_UM:=1;
      'Suma wyplaconych ekwiwalentow (ale w ramach b.r.).';
      H_UM.cntx_psh();
      {!
      |? {? H_UM.DO~1=sw.UDO~1 || ekw+=H_UM.EKW; ekw_nsp+=H_UM.EKW_NSP ?};
         {? H_UM.SW<>'T' || H_UM.SW:='T'; H_UM.put() ?};
         H_UM.prev()
      !};
      H_UM.cntx_pop()
   || Text:='Nie można drukować świadectwa dla niezakończonej umowy.';
      FUN.emsg(Text)
   ?}
?}


\gender
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Sterowanie formą męską/żeńską w wydrukach
::       .setGender('K/M') - ustawia pole .sex zgodnie z płcią, =K kobieta lub <>K nie kobieta
::       .g('tekst dla kobiet','tekst dla mężczyzn') - zwraca tekst (wartość dowolnego argumentu) w zależności od .sex
::       .gender('K/M','ona','on') - ustawia pole .sex i zwraca odpowiedni tekst
::   WE:
::   WY: obiekt
::----------------------------------------------------------------------------------------------------------------------
_ret:=obj_new('setGender','gender','sex','text');
_ret.sex:='';
_ret.setGender:=".sex:={? var_pres('_a')=type_of('') || _a || '' ?}";
_ret.text:="{? .sex='K' || _a || _b ?}";
_ret.gender:=".setGender(_a);.text(_b,_c)";
_ret


:Sign Version 2.0 jowisz:1045 2024/01/30 10:33:26 4b904d34df324c7baf2aae808a911ceb2e505fedc63aa57bb23b9bda3524ff33dc10839085d4afb087f43d6edf1145f886dcec7335271bb2a7bf1940baddeb7efc341872f1332d1ed26610a858ff65f808e8f99113a56efa39f167f4f31ff6cc349582452a519e8e645920637f165681297c7391d57f5662383820cbcbff3695

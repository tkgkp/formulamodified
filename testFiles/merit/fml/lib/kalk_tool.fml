:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: kalk_tool.fml
:: Utworzony: 12.05.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa N-P-U (narzędzia, przyrządy, urządzenia) w kalkulacjach kart technologicznych
::            Plik biblioteczny - wspólna obsługa dla TTE_TEC, TTE_PZL
::======================================================================================================================


\get_utils_price
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: funkcja n() dla kalkulacji
::   WE: _a=wskazanie na obiekt kalkulacyjny
::       _b=TKTL.ref() albo $TKTL.ref()
::       _c=1 - zuzycie na XJM
::       _c=2 - zuzycie na godzine
::       [_d] - REAL - opcjonalny współczynik który skaluje wynik
::   WY:
::       _wynik[i][j] - tablica zawierajaca
::       i - rozbicie na narzedzia, przyrzady i urzadzenia
::       j - rozbicie na poszczegolne fazy produkcyjne
::  OLD: \get_utils_price/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
_mode:=1;
{? var_pres('_c')=type_of(0)
|| _mode:=c
?};
_wynik:=obj_new(3);
{! _i:=1..3
|! _wynik[_i]:=obj_new(obj_len(_a.FAZY));
   {! _j:=1..obj_len(_a.FAZY)
   |! _wynik[_i][_j]:=0
   !}
!};

{? var_pres('_b')=type_of('')
|| _tktl_ref:=BIT.sqlint(_b);
   _msk:=(8+_b)+3
|? var_pres('_b')=type_of(null())
|| _tktl_ref:=#_b;
   _msk:=ref_name(_b)+3
|| return(_wynik)
?};
_coefinal:=1;
{? var_pres('_d')=type_of(0)
|| _coefinal:=_d
?};

exec('tktl_cntx_psh','tech_common');
exec('tktl_use','tech_common',_msk);

TACTTLS.index('KNROP');
TACTTLS.prefix(_tktl_ref);
{? TACTTLS.first()
|| {!
   |? _w:=0;
      {? TACTTLS.ACT='T' & _a.tpar.calc(TACTTLS.EXIST)
      || _coef:={? _mode=1  || {? TACTTLS.FZXJM<>'' || _a.tpar.calc(TACTTLS.FZXJM) || TACTTLS.ZXJM ?}
                |? _mode=2  || {? TACTTLS.FZH<>'' || _a.tpar.calc(TACTTLS.FZH) || TACTTLS.ZH  ?}
                         || 0
                ?};
         _ile:={? TACTTLS.FILE<>'' || _a.tpar.calc(TACTTLS.FILE) || TACTTLS.ILE ?};
         _ile:=ceil(_ile*_coefinal);
         _koszt:=0;
         PTC.clear();
         PTC.index('TO');
         PTC.prefix(TACTTLS.M);
         {? PTC.last()
         || {!
            |? _prev:=0;
               {? KKTL.DT_D=date(0,0,0)
               || _koszt:=PTC.PRICE
               || {? KKTL.DT_D>=PTC.DOD
                  || {? ~((KKTL.DT_D>PTC.DDO)&(PTC.DDO<>date(0,0,0))) || _koszt:=PTC.PRICE ?}
                  || _prev:=1
                  ?}
               ?};
               {? _prev || PTC.prev() ?}
            !}
         ?};
         _w:=_koszt*_coef*_ile;
         _a.anu.add(TACTTLS.M,_koszt*_coef,,,_ile)
      ?};
      {? TACTTLS.M().MGR().KOD=exec('get','#params',500750,2,null()) || _i:=1
      |? TACTTLS.M().MGR().KOD=exec('get','#params',500751,2,null()) || _i:=2
      |? TACTTLS.M().MGR().KOD=exec('get','#params',500752,2,null()) || _i:=3
      ?};
      {! _k:=1..(obj_len(_a.FAZY))
      |! {? _a.FAZY[_k]=TACTTLS.PFAZ || _j:=_k;_k:=(obj_len(_a.FAZY))+2 ?}
      !};
      _wynik[_i][_j]+=(_w);
      TACTTLS.next()
   !}
?};

exec('tktl_cntx_pop','tech_common');
_wynik

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 600c0418b332f144a00b29052f7a150b5cf6e0c9c7d54148e14d8acd827ab9f3b4b59d44d8c10f91e852eca066f17070bf605dea9c5bc27b90426aafb99b97764197d6e018e01a8b9e76ec4497a767df2fcc50d681093a3644577e120dac4e0fbb6dfef7a17e61234ae584ce30c58eabd61e8ba05310bc5d724a2b396e0b81b7

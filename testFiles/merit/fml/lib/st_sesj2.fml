:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_sesja.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa sesji dla systemu Statystyki
::======================================================================================================================


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Czyści powiązania do rekordu tabeli ST_SESJ2
::   WE: _a - ST_SESJ2.ref()
::   WY: >0  -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PRYWATNA><CLEAN>
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---
ST_SESJ2.cntx_psh(); ST_SESJ2.clear();
{? ST_SESJ2.seek(_ref)
||
   {? _can_continue>0 & ST_SESJ2.count()>0
   ||
::    1. Usuwam ST_SEST2 które wskazują na ten element
      ST_SEST2.cntx_psh();
      ST_SEST2.use(4+ST_SEST2.name()+(ST_SESJ2.name+4));
      ST_SEST2.index('ST_SESJ2');
      ST_SEST2.prefix(ST_SESJ2.ref());
      {? ST_SEST2.first()
      || {!
         |? _can_continue:=exec('delete','st_sest2',ST_SEST2.ref());
            ST_SEST2.first() & _can_continue>0
         !}
      ?};
      ST_SEST2.cntx_pop()
   ?};
   ~~
?};
ST_SESJ2.cntx_pop();
:: --- wszystkie powiazania usuniete? ---

{? _can_continue>0
|| _result:=1
|| undo()
?};

{? _mydo || end() ?};

_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Kasuje podany rekord tabeli ST_SESJ2 (wykonywane w transakcji!!!)
::   WE: _a - ST_SESJ2.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_SESJ2.cntx_psh(); ST_SESJ2.clear();
{? ST_SESJ2.seek(_ref)
|| {? exec('clean','st_sesj2',_ref)>0
   || {? ST_SESJ2.del(,1)>0
      || _result:=1
      || undo();
         _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};

{? _result<0
|| undo()
?};

ST_SESJ2.cntx_pop();
{? _mydo || end() ?};
_result


\st_sesja_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Dołącz dla ST_SESJ2
::----------------------------------------------------------------------------------------------------------------------
ST_SESJ2.win_edit('RED');
ST_SESJ2.blank();
{? ST_SESJ2.edit()
|| ST_SESJ2.add()
?};
~~


\st_sesja_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń dla ST_SESJ2
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRUPA='T' | FUN.ask('Czy usunąć bieżący wiersz?'@)
|| exec('delete','st_sesj2',ST_SESJ2.ref())
?};
~~


\st_sesja_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Popraw dla ST_SESJ2
::----------------------------------------------------------------------------------------------------------------------
ST_SESJ2.win_edit('RED');
{? ST_SESJ2.edit()
|| ST_SESJ2.put()
?};
~~


\st_sesja_del_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń przed dla grupy rekordów (tabela ST_SESJ2)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?'@)
|| VAR.GRUPA:='T';
   1
|| 0
?}


\st_sesja_del_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń po dla grupy rekordów (tabela ST_SESJ2)
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~


\sap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.42]
:: OPIS: Wartość parametru app_par1 z sekcji aplikacji w pliku cluster.cfg.
::   WE: _a - obj_new() - środowisko parsera, wynik działania exec('env_parse','st_common')
::       _b - obj_new() - obiekt zawierający treść wiersza, wynik działania exec('SAP1','st_parse_obj')
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_obj:=_b;

_result:=0;
_can_continue:=1;

_kind:=_obj.KIND;
_st_rodz:=exec('FindInSet','#table','ST_RODZ','SYMBOL',_kind,,,1,,null());
_error:=_env.TXT_ERROR;
_warning:=_env.TXT_WARNING;
_st_sesja:=exec('FindAndGet','#table',ST_SEST2,_env.REG.ST_SEST2,,"ST_SEST2.ST_SESJ2",null());

:: sesja
{? _can_continue>0
||
   ST_SESJ2.cntx_psh();
   ST_SESJ2.prefix();
   {? ST_SESJ2.seek(_st_sesja)
   || _val:=gsub(_obj.Value,'\'','\'\'');
      ($('ST_SESJ2.%1:=\'%2\''[ _kind,_val]))();
      {? ~ST_SESJ2.put()
      ||
         _env.message('%1: %2 - ST_SESJ2.put()'@[ _error, _kind]);
         _can_continue:=0
      ?}
   ?};
   ST_SESJ2.cntx_pop()
?};

{? _can_continue>0
|| _result:=1
?};
_result


\erase
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Czyści wszystkie sesje
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

_can_continue:=1;
_result:=0;

_mydo:=do_state()=0;
{? _mydo || do() ?};

_rule:="
   _args:=_b;
   _result:=1;
   _can_continue:=1;
   ST_SESJ2.cntx_psh();
   ST_SESJ2.index('PID');
   ST_SESJ2.prefix();

   {? ST_SESJ2.first()
   || {!
      |? _can_continue:=exec('delete','st_sesj2',ST_SESJ2.ref());
         ST_SESJ2.first() & _can_continue>0
      !}
   ?};

   {? _can_continue<=0
   || _result:=0
   ?};
   ST_SESJ2.cntx_pop();
   _result
";
_can_continue:=exec('for_each_mask','#table',ST_SESJ2,_rule,,,,1);

{? _can_continue>0
|| _result:=1
|| undo()
?};
{? _mydo || end() ?};
_result


\delete_not_used
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Usuwa wszystkie nieużywane rekordy
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};

ST_SESJ2.cntx_psh();
ST_SESJ2.index('ST_APL');
ST_SESJ2.prefix();
{? ST_SESJ2.first()
|| {!
   |? _next:=0;
      _ref_nxt:=null();
      ST_SESJ2.cntx_psh();
      {? ST_SESJ2.next()
      || _ref_nxt:=ST_SESJ2.ref()
      ?};
      ST_SESJ2.cntx_pop();

      {? ST_SESJ2.count()=0
      || _can_continue:=exec('delete','st_sesj2',ST_SESJ2.ref())
      ?};

      {? _ref_nxt<>null()
      || _next:=ST_SESJ2.seek(_ref_nxt)
      ?};
      _next>0 & _can_continue>0
   !}
?};
ST_SESJ2.cntx_pop();
{? _can_continue>0
|| _result:=1
|| undo()
?};
{? _mydo || end() ?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 55f5b3e2c4822bc43489d07e2f5b9f85ad6f7c456675e1debe8c6e43f9e9a65266628e9f95d56371b256db8995fab04df62bfd9bc13a3c575f974f654a97b4e1cc35945c4ab9d40e2bfd4a24b03133c27cc6094b800e48e0f329b8a18f2a85c1f1c31b7683199fd018078a3585f7a5b341be975e27124d86c61c063ba87fafb3

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: genius_fks.fml
:: Utworzony: 30.03.2022
:: Autor: KS, AMK
:: Systemy:
::======================================================================================================================
:: Zawartość: Obsługa intencji ChatBot dla finansów i obiegów
::======================================================================================================================


\check_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KS, AMK [22.26]
:: OPIS: Obsługa intencji Genius - sprawdzenie możliwośći wygenerowania JPK_V7
::----------------------------------------------------------------------------------------------------------------------
REF.FIRMA:=REF.S_FIRMA:=exec('firma_ref','#firma',app_info('app_ident'));
VAR_DEL.delete('ERP_fks');
ERP_fks:=exec('erp_obj','genius',1,150);
ERP_fks.data[1].type:='choice'; _i:=1;
exec('okres_pop','genius_fks');
{? ~ROZNE.UT_OKROD
|| _sentencja:='Nie znaleziono w systemie okresu dla poprzedniego miesiąca kalendarzowego';
   ERP_fks.data[1].values[_i].display_name:=_sentencja;
   ERP_fks.data[1].values[_i].icon:='bi bi-dash-circle-fill icon-size-lg'
|| ROZNE.UT_OKROD().ROK();
   _okres:=OKRO_F.NAZ+' '+ROK_F.NAZ;
   exec('inicjalizacja','genius_fks');
   VAR_DEL.delete('TYM_ROZ','TYM_SMS','ind_sms1','ind_sms2');
:: DOKUMENTY KSIĘGOWE
   ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || {! |?
         {? exec('usr_fjks','b_perm',ODD.ref())
         || _ile:=exec('check_dok','genius_fks',1,'',1);
            {? _ile
            || _sentencja:='Liczba niezaksięgowanych dokumentów VAT w okresie '+_okres;
               _sentencja+=' z jednostki księgowej '+ODD.OD+': '+$_ile;
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_dok\',\'genius_fks\',\''+$ODD.ref()+'\',1,'+'\'\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-card-list icon-size-lg';
               _i+=1
            ?}
         ?};
         ODD.next()
      !}
   ?};
   ODD.cntx_pop();
:: NIEZADEKRETOWANE DOKUMENTY SPRZEDAŻY Z LOGISTYKI
   ODDZ.cntx_psh(); ODDZ.index('ODD'); ODDZ.prefix();
   {? ODDZ.first()
   || {! |?
         {? exec('usr_upr','b_perm','ODDZ',ODDZ.ref())
         || _ile:=exec('check_faks','genius_fks','S',1,null,1);
            {? _ile
            || _sentencja:='Liczba niezadekretowanych dokumentów sprzedaży z logistyki w okresie '+_okres;
               _sentencja+=' z oddziału '+ODDZ.KOD+': '+$_ile;
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_faks\',\'genius_fks\',\''+$ODDZ.ref()+'\',\'S\''+',1,\'\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-basket-fill icon-size-lg';
               _i+=1
            ?}
         ?};
         ODDZ.next()
      !}
   ?};
:: NIEZADEKRETOWANE DOKUMENTY ZAKUPU Z LOGISTYKI
   ODDZ.index('ODD'); ODDZ.prefix();
   {? ODDZ.first()
   || {! |?
         {? exec('usr_upr','b_perm','ODDZ',ODDZ.ref())
         || _ile:=exec('check_faks','genius_fks','Z',1,null,1);
            {? _ile
            || _sentencja:='Liczba niezadekretowanych dokumentów zakupu z logistyki w okresie '+_okres;
               _sentencja+=' z oddziału '+ODDZ.KOD+': '+$_ile;
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_faks\',\'genius_fks\',\''+$ODDZ.ref()+'\',\'Z\''+',1,\'\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-basket2-fill icon-size-lg';
               _i+=1
            ?}
         ?};
         ODDZ.next()
      !}
   ?};
   ODDZ.cntx_pop();
:: FAKTURY W OBIEGU
   ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || {! |?
         {? exec('usr_fjks','b_perm',ODD.ref())
         || _ile:=exec('check_edokum','genius_fks');
            {? _ile
            || _sentencja:='Liczba niezadekretowanych faktur w obiegu dla dat otrzymania z okresu '+_okres;
               _sentencja+=' z jednostki księgowej '+ODD.OD+': '+$_ile;
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_edokum\',\'genius_fks\',\''+$ODD.ref()+'\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-disc icon-size-lg';
               _i+=1
            ?}
         ?};
         ODD.next()
      !}
   ?};
   ODD.cntx_pop();
:: ZAWIESZONE ROZLICZENIA
   exec('check_rozlvat','genius_fks');
   TYM_ROZ.cntx_psh(); TYM_ROZ.prefix();
   {? TYM_ROZ.first()
   || ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
      {? ODD.first()
      || {! |?
            {? exec('usr_fjks','b_perm',ODD.ref())
            || TYM_ROZ.prefix(ODD.OD);
               _ile:=TYM_ROZ.size();
               {? _ile
               || _sentencja:='Liczba zawieszonych rozliczeń VAT (do rozliczenia w okresie '+_okres;
                  _sentencja+=') z jednostki księgowej '+ODD.OD+': '+$_ile;
                  ERP_fks.data[1].values[_i].display_name:=_sentencja;
                  ERP_fks.data[1].values[_i].link:='exec(\'show_rozlvat\',\'genius_fks\',\''+ODD.OD+'\')';
                  ERP_fks.data[1].values[_i].icon:='bi bi-door-open icon-size-lg';
                  _i+=1
               ?}
            ?};
            ODD.next()
         !}
      ?}
   ?};
   TYM_ROZ.cntx_pop();
:: WIADOMOŚCI SMS DO UŻYTKOWNIKÓW FAKTUR W OBIEGU
   _error:=_i-1;
   {? var_pres('TYM_SMS')>0
   || TYM_SMS.index(ind_sms1); TYM_SMS.prefix();
      {? TYM_SMS.first()
      || ERP_fks.data[1].values[_i].display_name:='Wysłać ponaglenia SMS do użytkowników zalegających z akceptacją faktur'+
                                                      ' w obiegu? (Liczba użytkowników: '+$TYM_SMS.size()+')? ';
         ERP_fks.data[1].values[_i].link:='exec(\'sendSMS\',\'genius_fks\')';
         ERP_fks.data[1].values[_i].icon:='bi bi-envelope icon-size-lg';
         _i+=1
      ?}
   ?};
:: PROCES JPK_V7
   {? exec('hasAction','users','FKS_VED_DVJD')
   || _tekst:={? ~_error
              || 'Nie znaleziono dokumentów nieobsłużonych z okresu '+_okres+', które powinny wejść do deklaracji JPK_V7.'+
                 ' Uruchomić proces dla deklaracji i ewidencji JPK_V7?'
              || 'Liczba dokumentów z okresu '+_okres+', którymi trzeba się zająć i do których masz uprawnienia: '+$_error+
                 '. Proces dla deklaracji i ewidencji JPK_V7 powinien być uruchomiony dopiero po usunięciu powyższych usterek'+
                 '. Możesz jednak go uruchomić.'
              ?};
      ERP_fks.data[1].values[_i].display_name:=_tekst;
      ERP_fks.data[1].values[_i].link:='exec(\'proces_jpkv7\',\'genius_fks\')';
      ERP_fks.data[1].values[_i].icon:='bi bi-exclude icon-size-lg'
   ?}
?};
ERP_fks


\okres_pop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Szukanie poprzedniego miesiąca kalendarzowego
::----------------------------------------------------------------------------------------------------------------------
exec('szuk_okr','okresy',date(,(date()~2)-1,1))


\okres_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Szukanie aktualnego miesiąca kalendarzowego
::----------------------------------------------------------------------------------------------------------------------
exec('szuk_okr','okresy',date())


\inicjalizacja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Inicjalizuje obiekty potrzebne do obslugi okienek wertowania
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO);
exec('czytaj','#stalesys',,FINFO);
exec('Color','#object');
exec('Legend','#object');
exec('init_kol_start','color');
exec('AreaTitle','#object');
exec('Icon','#object');
exec('decl_color','#color');
exec('PAR_SKID','object');
exec('load_par','#parametr');
exec('F','object');
{? var_press('WinRej')<=0
|| WinRej:=obj_new('DOK','AKC','WINREJ','REJ')
?};
WinRej.DOK:='C_REN_DZ';
exec('decl_perm','#b_perm');
{? var_press('Perm')<>type_of(@.CLASS.PERM)
|| Perm:=obj_new(@.CLASS.PERM)
?};
_web_sesid:=app_info('web_sesid');
OPERATOR.USER:={? _web_sesid=''
               || exec('users','#users',exec('operatorKod','#users'))
               || {? exec('getUser','users',app_info('web_user')) || USERS.ref() || null ?}
               ?};
exec('open_all_tabele','open_tab')


\check_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Dokumenty księgowe
::       _a - 1 - okres poprzedni
::            2 - okres bieżący
::       _b - skrót kontrahenta (opcja)
::       _c - 1 - ilość dokumentów
::            2 - wartość (w zmiennych dok_wn, dok_ma)
::   WY: Liczba niezaksięgowanych dokumentów
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? _a=1
|| exec('okres_pop','genius_fks')
|| exec('okres_akt','genius_fks')
?};
{? ROZNE.UT_OKROD
|| ROZNE.UT_OKROD().ROK();
   ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   _slo_vat:=exec('slo_vat','genius_fks');
   {? _slo_vat
   || _mas1:=ROK_F.KOD;
      _mas2:=_mas1+form(OKRO_F.NR,-2);
      DOK.use('doku'+_mas2);
      POZ.use('pozy'+_mas2);
      {? _b<>''
      || DOK.index('GENIUSKH'); DOK.prefix(_b,ODD.ref(),_slo_vat,'N')
      || DOK.index('GENIUS');  DOK.prefix(ODD.ref(),_slo_vat,'N')
      ?};
      _zwrot:=DOK.size();
      {? _c=2 & DOK.first()
      || POZ.index('DOK');
         {! |?
            POZ.prefix(DOK.ref());
            {? POZ.first()
            || {! |?
                  {? POZ.TID='NAL'
                  || {? -POZ.STR='wn' || ndok_wn+=POZ.SUM || ndok_ma+=POZ.SUM ?}
                  |? POZ.TID='ZOB'
                  || {? -POZ.STR='wn' || zdok_wn+=POZ.SUM || zdok_ma+=POZ.SUM ?}
                  ?};
                  POZ.next()
               !}
            ?};
            DOK.next()
         !}
      ?}
   ?}
?};
_zwrot


\slo_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Zwraca pozycję rodzaju dokumentu VAT (SLO)
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=null;
SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix('~RODZAJE DOKUMENTÓW');
{? SLU.first()
|| SLO.cntx_psh(); SLO.index('SL'); SLO.prefix(SLU.ref(),'VAT');
   {? SLO.first() || _zwrot:=SLO.ref() ?};
   SLO.cntx_pop()
?};
SLU.cntx_pop();
_zwrot


\show_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wyświetla niezaksięgowane dokumenty
::   WE: _a - ref ODD
::       _b - 1 - okres poprzedni
::            2 - okres bieżący
::       _c - skrót kontrahenta (opcja)
::----------------------------------------------------------------------------------------------------------------------
{? exec('hasAction','users','FKS_DKS_DARK') | exec('hasAction','users','FKS_DKS_EKSP') |
   exec('hasAction','users','FKS_DKS_EKSZ')
|| {? _b=1
   || exec('okres_pop','genius_fks')
   || exec('okres_akt','genius_fks')
   ?};
   {? ROZNE.UT_OKROD
   || ROZNE.UT_OKROD().ROK();
      SSTALE.AR:=ROK_F.ref(); SSTALE.AO:=OKRO_F.ref();
      exec('open_tabele','open_tab');
      ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix();
      {? ODD.seek(_a)
      || _slo_vat:=exec('slo_vat','genius_fks');
         {? _slo_vat
         || DOK.win_sel('GENIUS');
            DOK.hdr_sel();
            {? _c<>''
            || DOK.index('GENIUSKH'); DOK.prefix(_c,ODD.ref(),_slo_vat,'N');
               DOK.hdr_sel(' kontrahenta '+_c)
            || DOK.index('GENIUS'); DOK.prefix(ODD.ref(),_slo_vat,'N')
            ?};
            PozView:=1;
            DOK.select()
         ?}
      ?};
      ODD.cntx_pop()
   ?}
|| FUN.info('Brak uprawnień do rejestracji lub księgowania dokumentów.'@)
?};
ERP_fks


\check_faks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Liczba dokumentow z logistyki
::   WE: _a - 'S'przedaż lub 'Z'akup
::       _b - 1 - okres poprzedni
::            2 - okres bieżący
::       _c - ref kontrahenta (lub null)
::       _d - 1 - zwraca ilość
::            2 - zwraca wartość
::   WY: liczba dokumentów z sprzedaży z logistyki dla bieżącego miesiąca
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? _b=1
|| exec('okres_pop','genius_fks')
|| exec('okres_akt','genius_fks')
?};
{? ROZNE.UT_OKROD || ROZNE.UT_OKROD().ROK() ?};
_maska:=ODDZ.KOD+(($(ROK_F.POCZ_ROK~1))+2);
FAKS.cntx_psh(); FAKS.use('faktu'+_maska);
FAKS.index('GENIUS');
{? _c
|| FAKS.prefix(ODDZ.KOD,ROK_F.POCZ_ROK~1,OKRO_F.NR,'N',_a,_c)
|| FAKS.prefix(ODDZ.KOD,ROK_F.POCZ_ROK~1,OKRO_F.NR,'N',_a)
?};
{? _d=1
|| _zwrot:=FAKS.size()
|| {? FAKS.first()
   || {! |?
         _zwrot+=FAKS.BRUTTO;
         FAKS.next()
      !}
   ?}
?};
FAKS.cntx_pop();
_zwrot


\show_faks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wyswietlanie dokumentów z logistyki
::   WE: _a - ref ODD
::       _b - 'S'przedaż lub 'Z'akup
::       _c - 1 - okres poprzedni
::            2 - okres bieżący
::       _d - ref kontrahenta (lub '')
::----------------------------------------------------------------------------------------------------------------------
{? (_b='S' &
    (exec('hasAction','users','LSP_FAK_DKHI') | exec('hasAction','users','LSP_FAK_DKOR') |
     exec('hasAction','users','LSP_FAK_DKZB') | exec('hasAction','users','LSP_FAK_DRFP') |
     exec('hasAction','users','LSP_FAK_DRZA') | exec('hasAction','users','LSP_FAK_EASP')))
   |
   (_b='Z' &
    (exec('hasAction','users','LZK_ZAK_DKOR') | exec('hasAction','users','LZK_ZAK_DKZB') |
     exec('hasAction','users','LZK_ZAK_DWEW') | exec('hasAction','users','LZK_ZAK_DZAK') |
     exec('hasAction','users','LZK_ZAK_EZAK')))
|| {? _c=1
   || exec('okres_pop','genius_fks')
   || exec('okres_akt','genius_fks')
   ?};
   {? ROZNE.UT_OKROD || ROZNE.UT_OKROD().ROK() ?};
   ODDZ.cntx_psh(); ODDZ.index('ODD'); ODDZ.prefix();
   {? ODDZ.seek(_a)
   || FAKS.cntx_psh();
      ST.ODDZ:=ODDZ.KOD;
      ST.AR:=date()~1;
      ST.AM:=date()~2;
      exec('open_tabele','open_tab','L',ODDZ.KOD,form((date()~1)-2000,,0,'99'));
      FAKS.index('GENIUS');
      {? _b='S'
      || FAKS.win_sel('GENIUS_S')
      || FAKS.win_sel('GENIUS_Z')
      ?};
      {? _d<>''
      || KH.cntx_psh(); KH.index('SKR'); KH.prefix();
         {? KH.seek(_d)
         || FAKS.prefix(ODDZ.KOD,ROK_F.POCZ_ROK~1,OKRO_F.NR,'N',_b,KH.ref());
            {? _b='S'
            || FAKS.hdr_sel(' sprzedaży kontrahenta '+KH.SKR)
            || FAKS.hdr_sel(' zakupu kontrahenta '+KH.SKR)
            ?}
         ?};
         KH.cntx_pop()
      || FAKS.prefix(ODDZ.KOD,ROK_F.POCZ_ROK~1,OKRO_F.NR,'N',_b);
         {? _b='S'
         || FAKS.hdr_sel(' sprzedaży')
         || FAKS.hdr_sel(' zakupu')
         ?}
      ?};
      FAKS.select();
      FAKS.cntx_pop()
   ?};
   ODDZ.cntx_pop()
|| {? _b='S'
   || FUN.info('Brak uprawnień do rejestracji lub akceptacji dokumentów sprzedaży.'@)
   || FUN.info('Brak uprawnień do rejestracji lub akceptacji dokumentów zakupu.'@)
   ?}
?};
ERP_fks


\check_edokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Liczba faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
exec('okres_pop','genius_fks');
{? ROZNE.UT_OKROD
|| zakres:=2;
   typobi:=1;
   ROZNE.UT_OKROD().ROK();
   EDOKUM.cntx_psh();
   exec('filtr_edokum','genius_fks',1);
   _zwrot:=EDOKUM.f_size();
   EDOKUM.cntx_pop()
?};
_zwrot


\show_edokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wyświetlanie faktur w obiegu
::   WE: _a - ref jednostki księgowej
::----------------------------------------------------------------------------------------------------------------------
{? exec('hasAction','users','OBG_FZO_ZBRK') & exec('hasAction','users','OBG_FZO_EAKC')
|| exec('okres_pop','genius_fks');
   {? ROZNE.UT_OKROD
   || ROZNE.UT_OKROD().ROK();
      SSTALE.AR:=ROK_F.ref(); SSTALE.AO:=OKRO_F.ref();
      exec('open_tabele','open_tab','F');
      ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix();
      {? ODD.seek(_a)
      || EDOKUM.cntx_psh();
         exec('filtr_edokum','genius_fks',2);
         EDOKUM.win_sel('GENIUS'); EDOKUM.hdr_sel(); EDOKUM.hdr_sel('Faktury w obiegu');
         EDOKUM.select();
         EDOKUM.cntx_pop()
      ?};
      ODD.cntx_pop()
   ?}
|| FUN.info('Brak uprawnień do akceptacji faktur w obiegu i do przeglądania wszystkich faktur.'@)
?};
ERP_fks


\filtr_edokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Zakładanie filtra na fakturach w obiegu dla dat otrzymania z okresu
::   WE: _a - 1 check
::            2 show
::----------------------------------------------------------------------------------------------------------------------
_typobi:=exec('bl_typ','obiegi',1);
_maska:=($(ROK_F.POCZ_ROK~1))+2;
EDOKUM.use('skid_v'+_maska); EDOKUM.index('TYPOBI2'); EDOKUM.prefix();
EDOKOS.use('skid_y'+_maska); EDOKOS.index('EDOKUM'); EDOKOS.prefix();
_join:='join EDOKOS using(EDOKOS.EDOKUM,EDOKUM.REFERENCE) '+
       'join ETYPY using(EDOKUM.TYP,ETYPY.REFERENCE) '+
       'join TYPOBIEG using(EDOKUM.TYPOBIEG,TYPOBIEG.REFERENCE)';
_odd:=' and EDOKUM.ODD=:_b';
_daty1:=' and EDOKUM.DO>=to_date(:_c)';
_daty2:=' and EDOKUM.DO<=to_date(:_d)';
_where:='EDOKUM.ZAM=''N'' and EDOKOS.WID=''T'' and ETYPY.DEKR=''T'' and EDOKUM.REJ is null and EDOKUM.TYPOBIEG=:_a'+_odd+_daty1+_daty2;
EDOKUM.f_set('DATAW,USERS(DANE),TP',_join,_where,_typobi,ODD.ref(),OKRO_F.POCZ,OKRO_F.KON);
{? _a=1 & EDOKUM.f_first()
|| exec('tab_sms','genius_fks');
   exec('tab_sms_add','genius_fks')
?}


\tab_sms
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Tabela z SMS do wysyłki
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('TYM_SMS')<=0
|| TYM_SMS:=tab_tmp(1,'USERS','STRING[20]','Użytkownik',
                      'ILE','INTEGER','Liczba faktur',
                      'WYSLANO','INTEGER','Znacznik czy wysłano wiadomość SMS',
                      'DANE','STRING[50]','Dane użytkownika'
                   );
   ind_sms1:=TYM_SMS.index('?');
   ind_sms2:=TYM_SMS.ndx_tmp('',1,'WYSLANO',,0,'DANE',,0);
   _wer:=TYM_SMS.mk_sel('Wysłane wiadomości SMS'@,'P',,'tym_sms_wer1',,,20,,'U');
   TYM_SMS.win_fld(_wer,,'DANE',,,,,,,,'Dane użytkownika'@);
   TYM_SMS.win_sel(_wer)
?}


\tab_sms_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Tabela z SMS do wysyłki - dodawanie rekordów
::----------------------------------------------------------------------------------------------------------------------
EDOKOS.cntx_psh(); EDOKOS.index('DISP1');
USERS.cntx_psh(); USERS.prefix();
{! |?
   EDOKOS.prefix('T',EDOKUM.ref(),EDOKUM.B_PREL);
   {? EDOKOS.first()
   || {! |?
         {? EDOKOS.USERS & EDOKOS.USERS().TEL<>''
         || TYM_SMS.index(ind_sms1); TYM_SMS.prefix($USERS.ref());
            {? TYM_SMS.first()
            || TYM_SMS.ILE+=1; TYM_SMS.put()
            || TYM_SMS.ILE:=1;
               TYM_SMS.USERS:=$USERS.ref();
               TYM_SMS.DANE:=USERS.DANE;
               TYM_SMS.add()
            ?}
         ?};
         EDOKOS.next()
      !}
   ?};
   EDOKUM.f_next()
!};
EDOKOS.cntx_pop(); USERS.cntx_pop()


\check_rozlvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Liczba zawieszonych rozliczeń
::----------------------------------------------------------------------------------------------------------------------
exec('okres_pop','genius_fks');
{? ROZNE.UT_OKROD
|| ROZNE.UT_OKROD().ROK();
   SSTALE.AR:=ROK_F.ref(); SSTALE.AO:=OKRO_F.ref();
   VAR_DEL.delete('TYM_ROZ');
   VPOZ.cntx_psh();
   TYM_ROZ:=tab_tmp(3,'ODD','STRING[8]','Jednostka księgowa',
                      'OK','STRING[1]','Zaznaczone',
                      'DATA','DATE','Data operacji'@,
                      'ROZLVAT','INTEGER','Ref ROZLVAT',
                      'RVAT_SYM','STRING[8]','Rejestr VAT'@,
                      'SYM','STRING[20]','Symbol dokumentu'@,
                      'CO','STRING[60]','',
                      'DOK','INTEGER','Ref DOK',
                      'VPOZ','INTEGER','Ref VPOZ',
                      'NETTO','REAL','Netto'@,
                      'VAT','REAL','VAT'@,
                      'REJ','STRING[8]','Rejestr'@,
                      'TRYB','STRING[45]','Tryb'@,
                      'DOK_POZ','STRING[1]','Dokument/Pozycja',
                      'DOK_POZ1','STRING[1]','Dokument/Pozycja',
                      'BRUTTO','REAL','Brutto'@,
                      'CZ','INTEGER','Częściowe'@
                   );
   _wer:=TYM_ROZ.mk_sel('Dokumenty (pozycje) do automatycznego rozliczenia'@,'P',,'tym_roz_wer1',,,,,'U');
   TYM_ROZ.win_fld(_wer,,'DATA',,,,,,,,'Data operacji'@);
   TYM_ROZ.win_fld(_wer,,'RVAT_SYM',,,,,,,,'Symbol rejestru VAT'@);
   TYM_ROZ.win_fld(_wer,,'SYM',,,,,,,,'Symbol dokumentu'@);
   TYM_ROZ.win_fld(_wer,,'CO',,,,,,'Sposób kwalifikacji'@,,'Opis sposobu kwalifikacji rozliczenia'@);
   TYM_ROZ.win_fld(_wer,,'NETTO',,,18,2,,,,'Kwota netto do rozliczenia'@);
   TYM_ROZ.win_fld(_wer,,'VAT',,,18,2,,,,'Kwota VAT do rozliczenia'@);
   TYM_ROZ.win_fld(_wer,,'OK',,,3,,,,,'Znacznik kwalifikacji'@,2,,"'T'","'N'");
   TYM_ROZ.win_fld(_wer,,'DOK_POZ1',,,3,,,,,'Dokument / Pozycja dokumentu'@);
   TYM_ROZ.win_fml(_wer,,'DOK_POZ1',,'ICON_BEFORE',"{? TYM_ROZ.DOK_POZ='D' || 'xwin16.png:74' || 'xwin16.png:76' ?}");
   TYM_ROZ.win_sel(_wer);
   TYM_ROZ.win_act(_wer,,'Wyświetl',,,,,
                   "{? ROZLVAT.seek(TYM_ROZ.ROZLVAT,ROZLVAT.name())
                    || exec('rozldok','!fks_vat_zroz')
                    || FUN.info('Brak rozliczenia VAT.'@)
                    ?}");
   TYM_ROZ.win_act(_wer,,'Formuła','Pozycje &VAT'@@,,'Pozycje VAT dokumentu'@,,
                   "{? ROZLVAT.seek(TYM_ROZ.ROZLVAT,ROZLVAT.name())
                    || exec('rozlvpoz','!fks_vat_droz')
                    || FUN.info('Brak rozliczenia VAT.'@)
                    ?}",,,,,'V');
   TYM_ROZ.win_act(_wer,,'Formuła','Podsumownie'@@,,'Podsumowanie pozycji'@,,"exec('s_rozl','!fks_vat_droz')",,,,,'P');
   TYM_ROZ.win_act(_wer,,'Szukaj');
   TYM_ROZ.win_act(_wer,,'Kolejność');
   TYM_ROZ.win_act(_wer,,'Formuła','Legenda'@@,,'Opis znaczenia kolorów wierszy i ikon'@,,
                  "exec('legenda','color','$Zaznaczone dokumenty (pozycje)'@,'#TYM_ROZ')",,,,,'L');
   TYM_ROZ.win_act(_wer,,'Rekord',,,,"
      _win:=TYM_ROZ.win_sel('?');
      {? TYM_ROZ.CZ
      || TYM_ROZ.actions_grayed(_win,);
         TYM_ROZ.actions(_win,,'Z')
      |? TYM_ROZ.OK='T'
      || TYM_ROZ.actions_grayed(_win,'Z');
         TYM_ROZ.actions(_win,,'O',1)
      || TYM_ROZ.actions_grayed(TYM_ROZ.win_sel('?'),'O');
         TYM_ROZ.actions(_win,,'Z',1)
      ?};
      {? TYM_ROZ.OK='T' || exec('findtmp','#color') ?}
   ");
   TYM_ROZ.win_sel(_wer);
   ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || {! |?
         {? exec('usr_fjks','b_perm',ODD.ref())
         || ROZLVAT.cntx_psh(); ROZLVAT.index('REJ_O'); ROZLVAT.prefix(ODD.ref());
            {? ROZLVAT.first()
            || {!
               |? exec('mask','dok_fks_ks',ROZLVAT.OKRO().ROK().KOD,ROZLVAT.OKRO().NR);
                  ROZLVAT.DOK();
                  {? DOK.A_VAT<>null
                  || {? (_co:=exec('czy_roz','!fks_vat_droz',DOK.A_VAT,DOK.ref()))<>''
                     || TYM_ROZ.ODD:=ODD.OD;
                        TYM_ROZ.ROZLVAT:=#ROZLVAT.ref();
                        TYM_ROZ.RVAT_SYM:=ROZLVAT.RVAT().SYM;
                        TYM_ROZ.DATA:=ROZLVAT.DATA;
                        TYM_ROZ.SYM:=ROZLVAT.SYM;
                        TYM_ROZ.CO:=_co;
                        TYM_ROZ.DOK:=DOK.ref;
                        TYM_ROZ.VPOZ:=null;
                        TYM_ROZ.NETTO:=0;
                        TYM_ROZ.VAT:=0;
                        TYM_ROZ.TRYB:=DOK.A_VAT().KOD+' - '+DOK.A_VAT().TR;
                        TYM_ROZ.CZ:=0;
                        VPOZ.index('VDRV'); VPOZ.prefix(DOK.ref,ROZLVAT.RVAT);
                        {? VPOZ.first()
                        || VPOZ_ROZ.index('VPOZ_ROZ');
                           {!
                           |? TYM_ROZ.NETTO+=VPOZ.NETTO;
                              TYM_ROZ.VAT+=VPOZ.VAT;
                              VPOZ_ROZ.prefix(VPOZ.ref());
                              {? VPOZ_ROZ.first()
                              || {? TYM_ROZ.CZ=0
                                 || TYM_ROZ.CZ:=exec('is_rozl_cz','fks_vat',SSTALE.AO)
                                 ?};
                                 {!
                                 |? TYM_ROZ.NETTO-=VPOZ_ROZ.NETTO;
                                    TYM_ROZ.VAT-=VPOZ_ROZ.VAT;
                                    VPOZ_ROZ.next()
                                 !}
                              ?};
                              VPOZ.next()
                           !}
                        ?};
                        {? TYM_ROZ.CZ
                        || TYM_ROZ.OK:={? 1+TYM_ROZ.CO='!' || '!' || 'N' ?}
                        ?};
                        TYM_ROZ.REJ:=ROZLVAT.RVAT().SYM;
                        TYM_ROZ.DOK_POZ:='D';
                        TYM_ROZ.add()
                     ?}
                  || VPOZ.index('VDRV'); VPOZ.prefix(DOK.ref(),ROZLVAT.RVAT);
                     {? VPOZ.first()
                     || VPOZ_ROZ.index('VPOZ_ROZ');
                        {!
                        |? {? VPOZ.A_VAT & ~VPOZ.OKRVAT & ~exec('is_rozl_cz_full','fks_vat')
                           || {? (_co:=exec('czy_roz','!fks_vat_droz',VPOZ.A_VAT,VPOZ.ref()))<>''
                              || TYM_ROZ.ROZLVAT:=#ROZLVAT.ref();
                                 TYM_ROZ.RVAT_SYM:=ROZLVAT.RVAT().SYM;
                                 TYM_ROZ.DATA:=ROZLVAT.DATA;
                                 TYM_ROZ.SYM:=ROZLVAT.SYM;
                                 TYM_ROZ.CO:=_co;
                                 TYM_ROZ.DOK:=DOK.ref;
                                 TYM_ROZ.VPOZ:=VPOZ.ref;
                                 TYM_ROZ.NETTO:=VPOZ.NETTO;
                                 TYM_ROZ.VAT:=VPOZ.VAT;
                                 VPOZ_ROZ.prefix(VPOZ.ref());
                                 {? VPOZ_ROZ.first()
                                 || TYM_ROZ.CZ:=exec('is_rozl_cz','fks_vat',SSTALE.AO);
                                    {? TYM_ROZ.CZ
                                    || TYM_ROZ.OK:={? 1+TYM_ROZ.CO='!' || '!' || 'N' ?}
                                    ?};
                                    {!
                                    |? TYM_ROZ.NETTO-=VPOZ_ROZ.NETTO;
                                       TYM_ROZ.VAT-=VPOZ_ROZ.VAT;
                                       VPOZ_ROZ.next()
                                    !}
                                 ?};
                                 TYM_ROZ.REJ:=ROZLVAT.RVAT().SYM;
                                 TYM_ROZ.TRYB:=VPOZ.A_VAT().KOD+' - '+VPOZ.A_VAT().TR;
                                 TYM_ROZ.DOK_POZ:='P';
                                 TYM_ROZ.add()
                              ?}
                           ?};
                           VPOZ.next()
                        !}
                     ?}
                  ?};
                  ROZLVAT.next()
               !}
            ?};
            ROZLVAT.cntx_pop()
         ?};
         ODD.next()
      !}
   ?};
   ODD.cntx_pop();
   VPOZ.cntx_pop()
?}


\show_rozlvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wyswietlanie zawieszonych rozliczeń VAT
::   WE: _a - ref ODD
::----------------------------------------------------------------------------------------------------------------------
{? exec('hasAction','users','FKS_VAT_DROZ') | exec('hasAction','users','FKS_VAT_ZROZ')
|| TYM_ROZ.prefix(_a); TYM_ROZ.select()
|| FUN.info('Brak uprawnień do obsługi zawieszonych rozliczeń VAT.'@)
?};
ERP_fks


\sendSMS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Obsługa SMS Genius
::----------------------------------------------------------------------------------------------------------------------
{? exec('get','#params',700021)=''
|| ERP_fks.display_message:='Brak wypełnionego parametru 700021.'@;ERP_fks.interact.status:=0;
   return(ERP_fks)
?};
{? exec('get','#params',700020)=''
|| ERP_fks.display_message:='Brak wypełnionego parametru 700020.'@;ERP_fks.interact.status:=0;
   return(ERP_fks)
?};
{? FUN.ask('Wysłać ponaglenia SMS do użytkowników zalegających z akceptacją faktur?'@)
|| USERS.cntx_psh(); USERS.prefix();
   TYM_SMS.index(ind_sms1); TYM_SMS.prefix();
   {? TYM_SMS.first()
   || {! |?
         {? USERS.seek(TYM_SMS.USERS)
         || _to:=gsub(USERS.TEL,'-','');  _to:=gsub(_to,'+','');
            {? +_to=9 || _to:='48'+_to ?};
            _message:='Posiadasz niezaakceptowane faktury w obiegu w liczbie: '+$TYM_SMS.ILE;
            _from:=exec('get','#params',700021);
            _flash:='0'; _fast:='0';
            _date:=date();
:: zmienić na (21,,) na czas testów - pozwala cofnąc wysyłkę
::          _time:=time(21,,);
            _time:=time();
            exec('send_sms','#sms_send',_to,_message,_from,_flash,_fast,_date,_time);
            TYM_SMS.WYSLANO:=1; TYM_SMS.put()
         ?};
         TYM_SMS.next()
      !}
   ?};
   USERS.cntx_pop();
   TYM_SMS.index(ind_sms2); TYM_SMS.prefix(1);
   {? TYM_SMS.first()
   || TYM_SMS.select()
   || FUN.info('Nie wysłano żadnej wiadomości SMS.'@)
   ?}
?};
ERP_fks


\proces_jpkv7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Uruchamianie procesu dla deklaracji i ewidencji JPK_V7
::----------------------------------------------------------------------------------------------------------------------
exec('init','jpk_v');
exec('start_process','genius_zws',,'FIN_V7');
ERP_fks


\check_kh_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Uruchamianie procesu dla niezapłaconych faktur kontrahenta
::----------------------------------------------------------------------------------------------------------------------
REF.FIRMA:=REF.S_FIRMA:=exec('firma_ref','#firma',app_info('app_ident'));
VAR_DEL.delete('ERP_fks');
ERP_fks:=exec('erp_obj','genius',2,150);
exec('inicjalizacja','genius_fks');
VAR_DEL.delete('TYM_SMS');
ERP_fks.data[1].type:='choice';
ERP_fks.data[2].type:='label';
_find:=0;
_kh_kod:=AI.getPar('kodKh');
_kh_naz:=AI.getPar('nazwaKh');
_kh_sql:=exec('kh_sql','genius_lsp',_kh_kod,_kh_naz);
{? _kh_sql=''
|| ERP_fks.display_message:='Trzeba podać kontrahenta.'@
|| _kh_tab:=sql(_kh_sql);
   _kh_il:=_kh_tab.size();
   {? _kh_il=0
   || ERP_fks.display_message:='Nie ma takiego aktywnego kontrahenta.'@
   |? _kh_il=1 & _kh_tab.first()
   || KH.cntx_psh(); KH.index('SKR'); KH.prefix();
      {? KH.seek(_kh_tab.REFERENCE)
      || _ref_kh:=KH.ref(); _skr_kh:=KH.SKR; _find:=1
      ?};
      KH.cntx_pop()
   || ERP_fks.display_message:='Liczba znalezionych aktywnych kontrahentów: %1.'@[$_kh_il]+
                                   ' Trzeba podać dokładniejsze dane kontrahenta.'@
   ?}
?};
{? _find
|| exec('okres_akt','genius_fks');
   {? ~ROZNE.UT_OKROD
   || _opis:='Nie znaleziono w systemie okresu dla bieżącego miesiąca kalendarzowego';
      ERP_fks.data[2].values[1].display_name:='<B>Podsumowanie sprawdzenia faktur w obiegu</B>';
      ERP_fks.data[2].values[1].icon:='bi bi-dash-circle-fill icon-size-lg';
      ERP_fks.data[2].values[1].desc:=_opis;
      _find:=0
   ?}
?};
{? _find
|| ROZNE.UT_OKROD().ROK();
   _okres:=OKRO_F.NAZ+' '+ROK_F.NAZ; _rok:=ROK_F.NAZ;
   _i:=1; _nal:=_zob:=0;
:: DOKUMENTY KSIĘGOWE
   ODD.cntx_psh();
   ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || {! |?
         {? exec('usr_fjks','b_perm',ODD.ref())
         || ndok_wn:=ndok_ma:=zdok_wn:=zdok_ma:=0;
            _ile:=exec('check_dok','genius_fks',2,_skr_kh,2);
            {? _ile
            || _nal+=F.SWn(ndok_wn,ndok_ma);
               _zob+=F.SMa(zdok_wn,zdok_ma);
               _sentencja:='Wartość niezaksięgowanych dokumentów VAT kontrahenta '+_skr_kh;
               _sentencja+=' w okresie '+_okres;
               _sentencja+=' z jednostki księgowej '+ODD.OD;
               _sentencja+='<ol><li><B>Należności: '+form(F.SWn(ndok_wn,ndok_ma),,2,' ,')+' '+FINFO.NAROD().KOD+'</B></li>';
               _sentencja+='<li><B>Zobowiązania: '+form(F.SMa(zdok_wn,zdok_ma),,2,' ,')+' '+FINFO.NAROD().KOD+'</B></li></ol>';
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_dok\',\'genius_fks\',\''+$ODD.ref()+'\',2,\''+_skr_kh+'\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-card-list icon-size-lg';
               _i+=1
            ?}
         ?};
         ODD.next()
      !};
      VAR_DEL.delete('dok_wn','dok_na')
   ?};
:: NALEŻNOŚCI
   ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || {! |?
         {? exec('usr_fjks','b_perm',ODD.ref())
         || _ile:=exec('check_op_kh','genius_fks',_ref_kh,ODD.ref(),'NAL');
            {? _ile
            || _nal+=_ile;
               _sentencja:='Wartość nierozliczonych rozrachunków (<B>należności</B>) kontrahenta '+_skr_kh;
               _sentencja+=' roku '+_rok;
               _sentencja+=' i jednostce księgowej '+ODD.OD+': <B>'+form(_ile,,2,' ,')+' '+FINFO.NAROD().KOD+'</B>';
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_op_kh\',\'genius_fks\',\''+$_ref_kh+'\',\''+$ODD.ref()+'\',\'NAL\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-card-checklist icon-size-lg';
               _i+=1
            ?}
         ?};
         ODD.next()
      !}
   ?};
:: ZOBOWIĄZANIA
   ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || {! |?
         {? exec('usr_fjks','b_perm',ODD.ref())
         || _ile:=exec('check_op_kh','genius_fks',_ref_kh,ODD.ref(),'ZOB');
            {? _ile
            || _zob+=_ile;
               _sentencja:='Wartość nierozliczonych rozrachunków (<B>zobowiązania</B>) kontrahenta '+_skr_kh;
               _sentencja+=' roku '+_rok;
               _sentencja+=' i jednostce księgowej '+ODD.OD+': <B>'+form(_ile,,2,' ,')+' '+FINFO.NAROD().KOD+'</B>';
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_op_kh\',\'genius_fks\',\''+$_ref_kh+'\',\''+$ODD.ref()+'\',\'ZOB\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-card-heading icon-size-lg';
               _i+=1
            ?}
         ?};
         ODD.next()
      !}
   ?};
   ODD.cntx_pop();
:: NIEZADEKRETOWANE DOKUMENTY ZAKUPU Z LOGISTYKI
   ODDZ.cntx_psh(); ODDZ.index('ODD'); ODDZ.prefix();
   {? ODDZ.first()
   || {! |?
         {? exec('usr_upr','b_perm','ODDZ',ODDZ.ref())
         || _ile:=exec('check_faks','genius_fks','Z',2,_ref_kh,2);
            _zob+=_ile;
            {? _ile
            || _sentencja:='Wartość <B>zobowiązań</B> z niezadekretowanych dokumentów zakupu z logistyki kontrahenta '+_skr_kh;
               _sentencja+=' w okresie '+_okres;
               _sentencja+=' z oddziału '+ODDZ.KOD+': <B>'+form(_ile,,2,' ,')+' '+FINFO.NAROD().KOD+'</B>';
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_faks\',\'genius_fks\',\''+$ODDZ.ref()+'\',\'Z\''+',2,\''+$_ref_kh+'\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-basket2-fill icon-size-lg';
               _i+=1
            ?}
         ?};
         ODDZ.next()
      !}
   ?};
:: NIEZADEKRETOWANE DOKUMENTY SPRZEDAŻY Z LOGISTYKI
   ODDZ.index('ODD'); ODDZ.prefix();
   {? ODDZ.first()
   || {! |?
         {? exec('usr_upr','b_perm','ODDZ',ODDZ.ref())
         || _ile:=exec('check_faks','genius_fks','S',2,_ref_kh,2);
            _nal+=_ile;
            {? _ile
            || _sentencja:='Wartość <B>należności</B> z niezadekretowanych dokumentów sprzedaży z logistyki kontrahenta '+_skr_kh;
               _sentencja+=' w okresie '+_okres;
               _sentencja+=' z oddziału '+ODDZ.KOD+': <B>'+form(_ile,,2,' ,')+' '+FINFO.NAROD().KOD+'</B>';
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_faks\',\'genius_fks\',\''+$ODDZ.ref()+'\',\'S\''+',2,\''+$_ref_kh+'\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-basket-fill icon-size-lg';
               _i+=1
            ?}
         ?};
         ODDZ.next()
      !}
   ?};
   ODDZ.cntx_pop();
:: FAKTURY W OBIEGU
   ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || {! |?
         {? exec('usr_fjks','b_perm',ODD.ref())
         || _ile:=exec('check_edokum_kh','genius_fks',_ref_kh);
            {? _ile$2<>0
            || _zob+=_ile;
               _sentencja:='Wartość <B>zobowiązań</B> z niezadekretowanych faktur w obiegu kontrahenta '+_skr_kh;
               _sentencja+=' w roku '+_rok;
               _sentencja+=' z jednostki księgowej '+ODD.OD+': <B>'+form(_ile,,2,' ,')+' '+FINFO.NAROD().KOD+'</B>';
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_edokum_kh\',\'genius_fks\',\''+$ODD.ref()+'\',\''+$_ref_kh+'\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-disc icon-size-lg';
               _i+=1
            ?}
         ?};
         ODD.next()
      !}
   ?};
   ODD.cntx_pop();
:: WIADOMOŚCI SMS DO UŻYTKOWNIKÓW FAKTUR W OBIEGU
   _error:=_i-1;
   {? var_pres('TYM_SMS')>0
   || TYM_SMS.index(ind_sms1); TYM_SMS.prefix();
      {? TYM_SMS.first()
      || ERP_fks.data[1].values[_i].display_name:='Wysłać ponaglenia SMS do użytkowników zalegających z akceptacją faktur'+
                                                      ' w obiegu? (Liczba użytkowników: '+$TYM_SMS.size()+')? ';
         ERP_fks.data[1].values[_i].link:='exec(\'sendSMS\',\'genius_fks\')';
         ERP_fks.data[1].values[_i].icon:='bi bi-envelope icon-size-lg';
         _i+=1
      ?}
   |? _error=0
   || _opis:='Nie znaleziono w systemie danych dla zadanego kontrahenta';
      ERP_fks.data[2].values[1].display_name:='<B>Podsumowanie kontrahenta '+_skr_kh+'</B>';
      ERP_fks.data[2].values[1].icon:='bi bi-dash-circle-fill icon-size-lg';
      ERP_fks.data[2].values[1].desc:=_opis
   ?};
   {? _nal$2<>0 | _zob$2<>0
   || _opis:='<ol><li>Sumaryczna wartość <B>należności: '+form(_nal,,2,' ,')+' '+FINFO.NAROD().KOD+'</B></li>';
      _opis+='<li>Sumaryczna wartość <B>zobowiązań: '+form(_zob,,2,' ,')+' '+FINFO.NAROD().KOD+'</B></li></ol>';
      ERP_fks.data[2].values[1].display_name:='<B>Podsumowanie kontrahenta '+_skr_kh+'</B>';
      ERP_fks.data[2].values[1].icon:='bi bi-plus-slash-minus';
      ERP_fks.data[2].values[1].desc:=_opis
   ?}
?};
ERP_fks


\check_edokum_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Liczba faktur w obiegu dla kontrahenta
::   WE: _a - ref kontrahenta
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
exec('okres_akt','genius_fks');
{? ROZNE.UT_OKROD
|| zakres:=2;
   typobi:=1;
   ROZNE.UT_OKROD().ROK();
   EDOKUM.cntx_psh();
   exec('filtr_edokum_kh','genius_fks',1,_a);
   {? EDOKUM.f_first()
   || {! |?
         {? EDOKUM.WAL=FINFO.NAROD
         || _zwrot+=EDOKUM.WART
         ?};
         EDOKUM.f_next()
      !}
   ?};
   EDOKUM.cntx_pop()
?};
_zwrot


\filtr_edokum_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Zakładanie filtra na fakturach w obiegu z niedokończonym obiegiem dla kontrahenta
::   WE: _a - 1 check
::            2 show
::       _b - ref kontrahenta
::----------------------------------------------------------------------------------------------------------------------
_typobi:=exec('bl_typ','obiegi',1);
_maska:=(($(ROK_F.POCZ_ROK~1))+2);
EDOKUM.use('skid_v'+_maska); EDOKUM.index('TYPOBI2'); EDOKUM.prefix();
EDOKOS.use('skid_y'+_maska); EDOKOS.index('EDOKUM'); EDOKOS.prefix();
_join:='join EDOKOS using(EDOKOS.EDOKUM,EDOKUM.REFERENCE) '+
       'join ETYPY using(EDOKUM.TYP,ETYPY.REFERENCE) '+
       'join TYPOBIEG using(EDOKUM.TYPOBIEG,TYPOBIEG.REFERENCE)';
_odd:=' and EDOKUM.ODD=:_b';
_kh:=' and EDOKUM.KHKH=:_c';
_where:='EDOKUM.ZAM=''N'' and EDOKOS.WID=''T'' and ETYPY.DEKR=''T'' and EDOKUM.REJ is null and EDOKUM.TYPOBIEG=:_a';
_where+=_odd+_kh;
EDOKUM.f_set('DATAW,USERS(DANE),TP',_join,_where,_typobi,ODD.ref(),_b);
{? _a=1 & EDOKUM.f_first()
|| exec('tab_sms','genius_fks');
   exec('tab_sms_add','genius_fks')
?}


\show_edokum_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wyświetlanie faktur w obiegu dla kontrahenta
::   WE: _a - ref jednostki księgowej
::       _b - ref kontrahenta
::----------------------------------------------------------------------------------------------------------------------
{? exec('hasAction','users','OBG_FZO_ZBRK') & exec('hasAction','users','OBG_FZO_EAKC')
|| KH.cntx_psh(); KH.index('SKR'); KH.prefix();
   {? KH.seek(_b)
   || exec('okres_akt','genius_fks');
      {? ROZNE.UT_OKROD
      || ROZNE.UT_OKROD().ROK();
         SSTALE.AR:=ROK_F.ref(); SSTALE.AO:=OKRO_F.ref();
         exec('open_tabele','open_tab','F');
         ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix();
         {? ODD.seek(_a)
         || EDOKUM.cntx_psh();
            exec('filtr_edokum_kh','genius_fks',2,KH.ref());
            EDOKUM.win_sel('GENIUS');
            EDOKUM.hdr_sel(); EDOKUM.hdr_sel('Faktury w obiegu kontrahenta '+KH.SKR);
            genius_fin:=1;
            EDOKUM.select();
            VAR_DEL.delete('genius_fin');
            EDOKUM.cntx_pop()
         ?};
         ODD.cntx_pop()
      ?}
   ?};
   KH.cntx_pop()
|| FUN.info('Brak uprawnień do akceptacji faktur w obiegu i do przeglądania wszystkich faktur.'@)
?};
ERP_fks


\check_op_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wartość rozrachunków dla kontrahenta
::   WE: _a - ref kontrahenta
::       _b - ref jednostki księgowej
::       _c - 'NAL' lub 'ZOB'
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
exec('okres_akt','genius_fks');
{? ROZNE.UT_OKROD
|| ROZNE.UT_OKROD().ROK();
   SSTALE.AR:=ROK_F.ref(); SSTALE.AO:=OKRO_F.ref();
   exec('open_tabele','open_tab','F');
   OP.cntx_psh();
   OP.index('GENIUSKH'); OP.prefix(_a,FINFO.NAROD,_b,_c,'N');
   {? OP.first()
   || _wn:=_ma:=0;
      {! |?
         _wn+=OP.WN; _ma+=OP.MA;
         OP.next()
      !};
      _zwrot:={? _c='NAL' || F.SWn(_wn,_ma) || F.SMa(_wn,_ma) ?}
   ?};
   OP.cntx_pop()
?};
_zwrot


\show_op_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wyświetla rozrachunki kontrahenta
::   WE: _a - ref kontrahenta
::       _b - ref jednostki księgowej
::       _c - 'NAL' lub 'ZOB'
::----------------------------------------------------------------------------------------------------------------------
{? exec('hasAction','users','FKS_ROZ_DARK') | exec('hasAction','users','FKS_ROZ_DCLI') |
   exec('hasAction','users','FKS_ROZ_ZARK') | exec('hasAction','users','FKS_ROZ_ZAZR') |
   exec('hasAction','users','FKS_ROZ_ZSQL')
|| exec('okres_akt','genius_fks');
   {? ROZNE.UT_OKROD
   || ROZNE.UT_OKROD().ROK();
      SSTALE.AR:=ROK_F.ref(); SSTALE.AO:=OKRO_F.ref();
      exec('open_tabele','open_tab');
      exec('set_rbop','rozrach_przel');
      exec('RB','object');
      ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix();
      {? ODD.seek(_b)
      || KH.cntx_psh(); KH.index('SKR'); KH.prefix();
         {? KH.seek(_a)
         || OP.index('GENIUSKH'); OP.prefix(KH.ref(),FINFO.NAROD,ODD.ref(),_c,'N');
            OP.win_sel('GENIUS');
            OP.hdr_sel();
            OP.hdr_sel(' kontrahenta '+KH.SKR+{? _c='NAL' || ' (należności)' || ' (zobowiązania)' ?});
            OP.select()
         ?};
         KH.cntx_pop()
      ?};
      ODD.cntx_pop()
   ?}
|| FUN.info('Brak uprawnień do rejestracji lub księgowania dokumentów.'@)
?};
ERP_fks


\check_faktury_obieg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Obsługa intencji Genius - sprawdzenie faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
REF.FIRMA:=REF.S_FIRMA:=exec('firma_ref','#firma',app_info('app_ident'));
VAR_DEL.delete('ERP_fks');
ERP_fks:=exec('erp_obj','genius',2,150);
ERP_fks.data[1].type:='choice';
ERP_fks.data[2].type:='label';
VAR_DEL.delete('TYM_SMS');
exec('inicjalizacja','genius_fks');
_find:=1;
exec('okres_akt','genius_fks');
{? ~ROZNE.UT_OKROD
|| _opis:='Nie znaleziono w systemie okresu dla bieżącego miesiąca kalendarzowego';
   ERP_fks.data[2].values[1].display_name:='<B>Podsumowanie sprawdzenia faktur w obiegu</B>';
   ERP_fks.data[2].values[1].icon:='bi bi-dash-circle-fill icon-size-lg';
   ERP_fks.data[2].values[1].desc:=_opis;
   _find:=0
?};
{? _find
|| ROZNE.UT_OKROD().ROK();
   _okres:=OKRO_F.NAZ+' '+ROK_F.NAZ; _rok:=ROK_F.NAZ; _i:=1;
   ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix(REF.FIRMA);
   {? ODD.first()
   || {! |?
         {? exec('usr_fjks','b_perm',ODD.ref())
         || _ile:=exec('check_edokum_all','genius_fks');
            {? _ile
            || _sentencja:='Liczba niezadekretowanych faktur w obiegu w roku '+_rok;
               _sentencja+=' z jednostki księgowej '+ODD.OD+': '+$_ile;
               ERP_fks.data[1].values[_i].display_name:=_sentencja;
               ERP_fks.data[1].values[_i].link:='exec(\'show_edokum_all\',\'genius_fks\',\''+$ODD.ref()+'\')';
               ERP_fks.data[1].values[_i].icon:='bi bi-disc icon-size-lg';
               _i+=1
            ?}
         ?};
         ODD.next()
      !}
   ?};
   ODD.cntx_pop();
   {? var_pres('TYM_SMS')>0 & TYM_SMS.size()
   || TYM_SMS.index(ind_sms1); TYM_SMS.prefix();
      {? TYM_SMS.first()
      || ERP_fks.data[1].values[_i].display_name:='Wysłać ponaglenia SMS do użytkowników zalegających z akceptacją faktur'+
                                                      ' w obiegu? (Liczba użytkowników: '+$TYM_SMS.size()+')? ';
         ERP_fks.data[1].values[_i].link:='exec(\'sendSMS\',\'genius_fks\')';
         ERP_fks.data[1].values[_i].icon:='bi bi-envelope icon-size-lg';
         _i+=1
      ?}
   || _opis:='Nie znaleziono w systemie danych faktur z niedokończonym obiegiem';
      ERP_fks.data[2].values[1].display_name:='<B>Podsumowanie sprawdzenia faktur w obiegu</B>';
      ERP_fks.data[2].values[1].icon:='bi bi-dash-circle-fill icon-size-lg';
      ERP_fks.data[2].values[1].desc:=_opis
   ?}
?};
ERP_fks


\check_edokum_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Liczba wszystkich faktur w obiegu
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
exec('okres_akt','genius_fks');
{? ROZNE.UT_OKROD
|| zakres:=2;
   typobi:=1;
   ROZNE.UT_OKROD().ROK();
   EDOKUM.cntx_psh();
   exec('filtr_edokum_all','genius_fks',1);
   _zwrot:=EDOKUM.f_size();
   EDOKUM.cntx_pop()
?};
_zwrot


\filtr_edokum_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Zakładanie filtra na wszystkich fakturach w obiegu z niedokończonym obiegiem
::   WE: _a - 1 check
::            2 show
::----------------------------------------------------------------------------------------------------------------------
_typobi:=exec('bl_typ','obiegi',1);
_maska:=(($(ROK_F.POCZ_ROK~1))+2);
EDOKUM.use('skid_v'+_maska); EDOKUM.index('TYPOBI2'); EDOKUM.prefix();
EDOKOS.use('skid_y'+_maska); EDOKOS.index('EDOKUM'); EDOKOS.prefix();
_join:='join EDOKOS using(EDOKOS.EDOKUM,EDOKUM.REFERENCE) '+
       'join ETYPY using(EDOKUM.TYP,ETYPY.REFERENCE) '+
       'join TYPOBIEG using(EDOKUM.TYPOBIEG,TYPOBIEG.REFERENCE)';
_odd:=' and EDOKUM.ODD=:_b';
_where:='EDOKUM.ZAM=''N'' and EDOKOS.WID=''T'' and ETYPY.DEKR=''T'' and EDOKUM.REJ is null and EDOKUM.TYPOBIEG=:_a';
_where+=_odd;
EDOKUM.f_set('DATAW,USERS(DANE),TP',_join,_where,_typobi,ODD.ref());
{? _a=1 & EDOKUM.f_first()
|| exec('tab_sms','genius_fks');
   exec('tab_sms_add','genius_fks')
?}


\show_edokum_all
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wyświetlanie faktur w obiegu dla kontrahenta
::   WE: _a - ref jednostki księgowej
::----------------------------------------------------------------------------------------------------------------------
{? exec('hasAction','users','OBG_FZO_ZBRK') & exec('hasAction','users','OBG_FZO_EAKC')
|| exec('okres_akt','genius_fks');
   {? ROZNE.UT_OKROD
   || ROZNE.UT_OKROD().ROK();
      SSTALE.AR:=ROK_F.ref(); SSTALE.AO:=OKRO_F.ref();
      exec('open_tabele','open_tab','F');
      ODD.cntx_psh(); ODD.index('ODDZIALY'); ODD.prefix();
      {? ODD.seek(_a)
      || EDOKUM.cntx_psh();
         exec('filtr_edokum_all','genius_fks',2);
         EDOKUM.win_sel('GENIUS');
         EDOKUM.hdr_sel(); EDOKUM.hdr_sel('Faktury w obiegu');
         genius_fin:=1;
         EDOKUM.select();
         VAR_DEL.delete('genius_fin');
         EDOKUM.cntx_pop()
      ?};
      ODD.cntx_pop()
   ?}
|| FUN.info('Brak uprawnień do akceptacji faktur w obiegu i do przeglądania wszystkich faktur.'@)
?};
ERP_fks

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 450ee7db8f031f39a199fbee24fcc0fdf285c7ee7ee9c82b79000d50d609e0949bd09d924a7f43a7bc32e3d0f2752f31e597fdcd97cc33de28f1841cae22c62dc1a7552272caf0fa5afcf5d412b3cecc8ae52ec8e17f251818d306e23e30bd4e448e64b715fe0d4ebfec69fff51577f4f08e592f85be4afb3b4e6df9005f9d0b

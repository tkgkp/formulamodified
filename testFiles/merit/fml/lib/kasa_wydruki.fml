:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: kasa_wydruki.fml
:: Utworzony: 21.03.2016 [17.00]
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły do obsługi zestawień dokumentów kasowych
::======================================================================================================================

\an_ptyp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Parametry analizy typy
::  OLD: \an_ptyp/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
exec('an_par','kasa_wydruki',0,1,0)


\an_par
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Parametry analizy rodzaje
::   WE: [_a] - 1 tylko stanowiska ze znakami
::       [_b] - 1 z wyborem typu
::       [_c] - 1 z dodatkowym parametrem
::  OLD: \an_par/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
{? _>=2 || {? type_of(_b)<>1 || _b:=0 ?} || _b:=0 ?};
{? _>=3 || {? type_of(_c)<>1 || _c:=0 ?} || _c:=0 ?};
__Zn:=_a;
__Par:=_c;

VAR_DEL.delete('__Ttyp');

{? _c
|| {? -WYDRUKI.PAR1<>'t' & -WYDRUKI.PAR1<>'n' || WYDRUKI.PAR1:='N' ?};
   {? -WYDRUKI.PAR2<>'t' & -WYDRUKI.PAR2<>'n' || WYDRUKI.PAR2:='N' ?}
?};
WYDRUKI.MKS:='';
rodz_mks:='';
ref_mks:=0;

:: wygaszanie/zapalanie pól
_fldb:={? _b || 'enable=1' || 'enable=0' ?};
WYDRUKI.efld_opt('KAS_WYDR',_fldb,,'TYP_DOK');
_fldc:={? _c || 'enable=1' || 'enable=0' ?};
WYDRUKI.efld_opt('KAS_WYDR',_fldc,,'PAR1');
WYDRUKI.efld_opt('KAS_WYDR',_fldc,,'PAR2');

WYDRUKI.win_edit('KAS_WYDR');
{? WYDRUKI.DAT_OD=date(0,0,0)
|| WYDRUKI.DAT_OD:=ROK_F.POCZ_ROK
?};
{? WYDRUKI.DAT_DO=date(0,0,0)
|| WYDRUKI.DAT_DO:=ROK_F.KON_ROK
?};
_wyn:={? WYDRUKI.edit("{? WYDRUKI.DAT_OD=date(0,0,0) | WYDRUKI.DAT_OD=date(0,0,0) | WYDRUKI.DAT_OD>WYDRUKI.DAT_DO
                       || FUN.info('Podaj poprawne daty'@);0
                       || {? __Par & ((-WYDRUKI.PAR1<>'t' & -WYDRUKI.PAR1<>'n') | (-WYDRUKI.PAR2<>'t' & -WYDRUKI.PAR2<>'n'))
                          || FUN.info('Parametry mogą przyjmować tylko wartości \nT lub N'@);0
                          || 1
                          ?}
                       ?}")
      || 1
      || 0
      ?};
&__Zn; &__Par;
_wyn


\f3typd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: F3 na typ dokumentu w zmiennej WYDRUKI.TYPD
::  OLD: \f3typd/ml_kasa.fml
:: ~OST: INWINBTN
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
VAR_DEL.delete('__zatw');
__zatw:=0;
{? var_pres('__typ_wyb')>0 & __typ_wyb=2
|| {? var_pres('__Ttyp')<0
   || __Ttyp:=sql('select distinct TYP_DOK, 0    as jest, \' \' zn from @DOKUMENT where '+
               ' DATA between to_date(:_a) and to_date(:_b) '
               ,WYDRUKI.DAT_OD, WYDRUKI.DAT_DO);

      _w_acr:=__Ttyp.mk_sel('Wybór typów dokumentów'@,'P',,'#ttypdok',,,,,'U');

      __Ttyp.win_fld(_w_acr,,'TYP_DOK',,,10,,,'Typ dokumentu'@);
      __Ttyp.win_fld(_w_acr,,'ZN',,,5,,1,'Wybrano'@,,'Czy wybrano [T/N]?'@,2,,"'T'","'N'");
      _formula:="__Ttyp.ZN:='T'; __Ttyp.JEST:=1; __Ttyp.put()";
      __Ttyp.win_act(_w_acr,,'Formuła','&Wybierz'@@,,,_formula,,1,1,,,'W');
      __Ttyp.win_btn(_w_acr,'text=%1,panel=right,align=begin'['&Wybierz'@],'menu:W',,,,,,'noempty');
      _formula:="__Ttyp.ZN:='N'; __Ttyp.JEST:=0; __Ttyp.put()";
      __Ttyp.win_act(_w_acr,,'Formuła','&Odbierz'@@,,,_formula,,,1,,,'O');
      __Ttyp.win_btn(_w_acr,'text=%1,panel=right,align=begin'['&Odbierz'@],'menu:O',,,,,,'noempty');
      _formula:="__zatw:=1; sel_exit()";
      __Ttyp.win_act(_w_acr,,'Formuła','&Akceptuj'@@,,,_formula,,,,,,'A');
      __Ttyp.win_btn(_w_acr,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Akceptuj'@],
        'menu:A',,,,,,'noempty');
      __Ttyp.win_btn(_w_acr,'text=%1,btn_label_align=center,panel=bottom,align=end'['A&nuluj'@],'key:Esc');
      __Ttyp.win_act(_w_acr,,'Kolejność',);
      __Ttyp.win_act(_w_acr,,'Okienko',,,,,"
      {? ~__zatw
      || FUN.ask('Czy zakończyć wybór typu dokumentu?\nUwaga. Wprowadzone zmiany nie zostaną uwzględnione.'@)
      || 1
      ?}");
      __Ttyp.win_act(_w_acr,,'Rekord',,,,
        "_size:=__Ttyp.sel_size();
         {? __Ttyp.ZN='N' | _size
         || __Ttyp.actions(__Ttyp.win_sel('?'),,'W',1)
         || __Ttyp.actions(__Ttyp.win_sel('?'),,'O',1)
         ?}");
      __Ttyp.win_sel(_w_acr)
   ?};

   __Ttyp.select;
   _il:=0; _typ:='';
   {? __Ttyp.first
   || {!|? {? __Ttyp.JEST || _typ:=__Ttyp.TYP_DOK; _il+=1 ?};__Ttyp.next !}
   ?};
   {? _il=1
   || _wyn:=_typ
   |? _il>1
   || _wyn:='*'
   ?}
||
   _ttmp:=sql('select distinct TYP_DOK from @DOKUMENT order by 1');
   _w_acr:=_ttmp.mk_sel('Typy dokumentów'@,'P',,'#typyw01',,,,);
   _ttmp.win_fld(_w_acr,,'TYP_DOK',,,15,,,'Typ'@);
   _ttmp.win_act(_w_acr,,'Formuła','&Wybierz'@@,,,"sel_exit",,1,,,,'W');
   _ttmp.win_sel(_w_acr);
   {? _ttmp.select
   || _wyn:=_ttmp.TYP_DOK
   ?}
?};
VAR_DEL.delete('__zatw');
_wyn


\f3_mks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Wybór multikasy lub stanowiska
::  OLD: \f3_mks/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__Zn')<0
|| __Zns:=0
|| __Zns:=__Zn
?};
MKASAUPR.cntx_psh();
KUSERUPR.cntx_psh();
STANKAS.cntx_psh();
VAR_DEL.delete('__Zn_stan','__jstan','__w_acr');
__Zn_stan:=tab_tmp(,'TREE','TREE_REF',,
   'KOD','STRING[40]','Stanowisko',
   'KODS','INTEGER','Kod Stanowiska',
   'RODZ','STRING[1]','M/S',
   'ZN','STRING[1]','Znacznik',
   'REF','STRING[16]','ref');
MKASAUPR.index('MKASANAZ'); MKASAUPR.prefix(USERS.ref);
__jstan:=0;
_fo:="{? __Zns=0 | (__Zns & KUSERUPR.STANKAS().ZNAKI='T')
      || __jstan+=1;
         __Zn_stan.TREE:=__tref;
         __Zn_stan.KOD:=(('   '+$KUSERUPR.STANKAS().KOD)+3)+' '+KUSERUPR.STANKAS().NAZWA;
         __Zn_stan.KODS:=KUSERUPR.STANKAS().KOD;
         __Zn_stan.RODZ:='S';
         __Zn_stan.ZN:=KUSERUPR.STANKAS().ZNAKI;
         __Zn_stan.REF:=$KUSERUPR.STANKAS;
         __Zn_stan.add
      ?};
      KUSERUPR.next";
{? MKASAUPR.first
|| KUSERUPR.index('MKASA');
   {!
   |?
      __Zn_stan.TREE:=0;
      __Zn_stan.KOD:=MKASAUPR.MKASA().NAZ;
      __Zn_stan.KODS:=0;
      __Zn_stan.RODZ:='M';
      __Zn_stan.ZN:='N';
      __Zn_stan.REF:=$MKASAUPR.MKASA;
      __Zn_stan.add;
      __tref:=__Zn_stan.ref;
      __jstan:=0;
      KUSERUPR.prefix(MKASAUPR.MKASA);
      {? KUSERUPR.first
      || {!|? _fo() !}
      ?};
      {? __jstan=0 || __Zn_stan.del ?};
      &__tref;
      MKASAUPR.next
   !}
?};
KUSERUPR.index('KU'); KUSERUPR.prefix(USERS.ref());
__tref:=0;
{? KUSERUPR.first
|| {!|? _fo() !}
?};
&__tref;

{? __Zn_stan.first
||
   __w_acr:=__Zn_stan.mk_sel(,'P',,'tree_kzp',,,,1);
   __Zn_stan.win_fld(__w_acr,,'KOD',,,34,,,'Stanowisko'@);
   __Zn_stan.win_act(__w_acr,,'Formuła','&Wybierz'@@,,,"WYDRUKI.MKS:=__Zn_stan.KOD;
      rodz_mks:=__Zn_stan.RODZ; ref_mks:=__Zn_stan.REF; sel_exit",,1,,,,'W');
   __Zn_stan.win_act(__w_acr,,'Formuła','&Zwiń/rozwiń'@@,,'Zwija/rozwija wszystkie gałęzie'@,"",
           "__Zn_stan.prefix();
            {! |? {? __Zn_stan.TREE || __Zn_stan.seek(__Zn_stan.TREE,) || 0 ?} !};
            {? __Zn_stan.tr_state || __Zn_stan.tr_set(0,__w_acr) || __Zn_stan.tr_set(1,__w_acr) ?}
           ",,,,,'Z');
   __Zn_stan.win_act(__w_acr,,'Szukaj');
   _wfr:="__Zn_stan.cntx_psh(); __Zn_stan.prefix(__Zn_stan.ref());
          _zwrot:={? __Zn_stan.first()
                  || __Zn_stan.cntx_pop();
                     {? __Zn_stan.tr_state()=1
                     || 'xwin16.png:75'
                     || 'xwin16.png:74'
                     ?}
                  || __Zn_stan.cntx_pop(); 'xwin16.png:76'
                  ?}; _zwrot";
   __Zn_stan.win_fml(__w_acr,,'KOD',,'ICON_BEFORE',_wfr);
   __Zn_stan.tr_fml(__w_acr,,"{? _a=-1 || 1 || _a ?}");
   __Zn_stan.win_sel(__w_acr);
   {? __Zn_stan.select(,1)
   || 1
   ?}
?};

VAR_DEL.delete('__Zn_stan','__jstan','__w_acr');
&__Zns;
MKASAUPR.cntx_pop();
KUSERUPR.cntx_pop();
STANKAS.cntx_pop();
1


\an_prodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Parametry analizy rodzaje
::   WE: [_a] - 1 tylko stanowiska ze znakami
::  OLD: \an_prodz/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
exec('an_par','kasa_wydruki',_a)


\spr_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formuła po okienku dal wydruku kas_vat.rpm
::  OLD: \spr_vat/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? WYDRUKI.ZAKRES=0 & WYDRUKI.RAP=null
|| FUN.info('Proszę wybrać raport.'@); 'RAP'
|? WYDRUKI.ZAKRES=0 & WYDRUKI.DOK_OD<>null & WYDRUKI.DOK_DO<>null &
   WYDRUKI.DOK_OD().LP>WYDRUKI.DOK_DO().LP
|| FUN.info('Proszę podać poprawny zakres dokumentów.'@); 'DOK_OD'
|? WYDRUKI.ZAKRES=1 & WYDRUKI.RAP_OD<>null & WYDRUKI.RAP_DO<>null &
   WYDRUKI.RAP_OD().NUM_RAP>WYDRUKI.RAP_DO().NUM_RAP
|| FUN.info('Proszę podać poprawny zakres raportów.'@); 'RAP_OD'
|| 1
?}


\vwyd1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pol WYDRUKI.RAP_OD i WYDRUKI.RAP_DO
::  OLD: \vwyd1/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? WYDRUKI.ZAKRES=1 || 1 || 0 ?}


\vwyd2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pol WYDRUKI.DOK_OD i WYDRUKI.DOK_DO
::  OLD: \vwyd2/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? WYDRUKI.ZAKRES=0 & WYDRUKI.RAP<>null
|| WYDRUKI.RAP(); 1
|| 0
?}


\vwyd3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula przed redakcja pola WYDRUKI.RAP
::  OLD: \vwyd3/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? WYDRUKI.ZAKRES=0 || 1 || 0 ?}


\vwyd4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula po redakcji pola WYDRUKI.RAP
::  OLD: \vwyd4/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? WYDRUKI.RAP=null
|| WYDRUKI.DOK_OD:=WYDRUKI.DOK_DO:=null
|| DOKUMENT.cntx_psh();
   DOKUMENT.index('RAPORT');
   DOKUMENT.prefix();
   {? ~DOKUMENT.find_key(WYDRUKI.RAP,WYDRUKI.DOK_OD().LP) || WYDRUKI.DOK_OD:=null ?};
   {? ~DOKUMENT.find_key(WYDRUKI.RAP,WYDRUKI.DOK_DO().LP) || WYDRUKI.DOK_DO:=null ?};
   DOKUMENT.cntx_pop(); 1
?}


\vwyd5
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [8.10]
:: OPIS: Formula po redakcji pola WYDRUKI.ZAKRES
::  OLD: \vwyd5/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? WYDRUKI.ZAKRES=0
|| WYDRUKI.RAP_OD:=WYDRUKI.RAP_DO:=null
|| WYDRUKI.DOK_OD:=WYDRUKI.DOK_DO:=WYDRUKI.RAP:=null
?};
1


\ae_mks
::---------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS:
::  OLD: \ae_mks/ml_kasa.fml
::---------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? fld()=''
|| rodz_mks:='';
   ref_mks:=null
|? rodz_mks='S'
|| STANKAS.cntx_psh();
   {? STANKAS.seek(ref_mks,STANKAS.name)
   || fld((('   '+$STANKAS.KOD)+3)+' '+STANKAS.NAZWA)
   || _wyn:=0;
      rodz_mks:='';
      ref_mks:=null;
      fld('')
   ?};
   STANKAS.cntx_pop()
|? rodz_mks='M'
|| MKASA.cntx_psh();
   {? MKASA.seek(ref_mks,MKASA.name)
   || fld(MKASA.NAZ)
   || _wyn:=0;
      rodz_mks:='';
      ref_mks:=null;
      fld('')
   ?};
   MKASA.cntx_pop()

|? (rodz_mks='' & fld()<>'')
|| STANKAS.cntx_psh();
   STANKAS.index('NAZWA');
   STANKAS.clear();
   {? STANKAS.find_key(fld())
   || rodz_mks:='S';
      ref_mks:=BB.refsql(STANKAS.ref);
      fld((('   '+$STANKAS.KOD)+3)+' '+STANKAS.NAZWA)
   || _wyn:=0
   ?};
   STANKAS.cntx_pop()
|| _wyn:=0
?};
{? ~_wyn || FUN.info('Niewłaściwe stanowisko'@) ?};
_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:38 2c201291ea421ec330df8705542571f1aa01d6275dde64ef454adde488838e475b1739ed43979c14f8f9b83ae96a4f530516db188e001a94fd7059f051413a32d7b5c1480759be23757eff754a6ffdd7391ce1a88430867ab7b72e9267d91f176fff62b8ae3b645393ab181cd997f2778c990dba636866348090791d9820f967

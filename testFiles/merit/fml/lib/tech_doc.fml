:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: tech_doc.fml
:: Utworzony: 30.03.2015
:: Autor: TS
::======================================================================================================================
:: Zawartość: Obsługa dokumentacji elektronicznej w kartach technologicznych, wzorcach, zleceniach
::            Plik biblioteczny - wspólna obsługa dla TTE_TEC, TTE_WTE, TTE_PZL
::======================================================================================================================


\declare
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Deklaruje obiekt do obsługi bibliotek dokumentów
::   WY: ~~
::  OLD: \plib/plib.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('oPLIB',@.CLASS)<=0
|| obj_decl('oPLIB',
      obj_fld('EDIT',0),
      obj_fld('VIEW',0),
      obj_fld('TITLE',''),
      obj_fld('TAB',''),
      obj_fld('MSK',''),
      obj_fld('REF',0),
      obj_fld('TAB2',''),
      obj_fld('MSK2',''),
      obj_fld('REF2',0),

      obj_meth('select',"
         .TAB:=_a+'*';
         .REF:=#_b;
         .MSK:=ref_name(_b);
         .EDIT:=_c;
         .VIEW:=_e;
         .TITLE:=_d+{? .VIEW || ' — '+'PODGLĄD'@ || '' ?};
         PLIKIDOC.index('TRS');
         PLIKIDOC.prefix(.TAB,.MSK,.REF);
         _args:=exec('fml_args','tech_doc'); _args.TAB:=.TAB; _args.MSK:=.MSK; _args.REF:=.REF;
         exec('edit_blob','#edit',PLIKIDOC
                                 ,'DOC'
                                 ,exec('PLIKIDOC','buffer')
                                 ,\"params_exec('fml_edit','tech_doc',_a,_b,_c,_d)\"
                                 ,\"params_exec('fml_file','tech_doc',_a,_b,_c,_d)\"
                                 ,_args
                                 ,.EDIT
                                 ,.TITLE
                                 ,
                                 ,
                                 ,
                                 ,\"params_exec('fml_valid','tech_doc',_a,_b,_c,_d,_e)\");
         ~~
      ",type_of(''),type_of(null()),type_of(0),type_of(''),type_of(0)),

      obj_meth('del',"
         .TAB:=_a+'*';
         .MSK:=ref_name(_b);
         .REF:=#_b;
         PLIKIIMG.index('TRS');
         PLIKIIMG.prefix(.TAB,.MSK,.REF);
         {? PLIKIIMG.first() || {! |? PLIKIIMG.del() !} ?};
         PLIKIDOC.index('TRS');
         PLIKIDOC.prefix(.TAB,.MSK,.REF);
         {? PLIKIDOC.first() || {! |? PLIKIDOC.del() !} ?};
         1
      ",type_of(''),type_of(null())),

      obj_meth('exist',"
         .TAB:=_a+'*';
         .MSK:=ref_name(_b);
         .REF:=#_b;
         PLIKIIMG.index('TRS');
         PLIKIIMG.prefix(.TAB,.MSK,.REF);
         _img:=PLIKIIMG.first();
         PLIKIDOC.index('TRS');
         PLIKIDOC.prefix(.TAB,.MSK,.REF);
         _doc:=PLIKIDOC.first();
         _img | _doc
      ",type_of(''),type_of(null())),

      obj_meth('copy',"
         .TAB:=_a+'*';
         .MSK:=ref_name(_b);
         .REF:=#_b;
         .TAB2:=_c+'*';
         .REF2:=#_d;
         .MSK2:=ref_name(_d);
         _ok:=1;
         PLIKIIMG.index('TRS');
         PLIKIIMG.prefix(.TAB,.MSK,.REF);
         {? PLIKIIMG.first()
         || {!
            |? PLIKIIMG.cntx_psh();
               PLIKIIMG.prefix(.TAB2,.MSK2,.REF2,PLIKIIMG.FILE().SYM);
               {? PLIKIIMG.first()
               || KOMM.add(
                     'W elemencie docelowym (%1) występuje już dokument (grafika) %2'
                     '. Poprzednia wartość zostaje zachowana.'@
                     [exec('record','#to_string',_d),PLIKIIMG.FILE().SYM],7,,1
                  )
               || PLIKIIMG.TAB:=.TAB2;
                  PLIKIIMG.REF:=.REF2;
                  PLIKIIMG.MSK:=.MSK2;
                  _ok:=PLIKIIMG.add()
               ?};
               PLIKIIMG.cntx_pop();
               _ok & PLIKIIMG.next()
            !}
         ?};
         PLIKIDOC.index('TRS');
         PLIKIDOC.prefix(.TAB,.MSK,.REF);
         {? PLIKIDOC.first()
         || {!
            |? PLIKIDOC.cntx_psh();
               PLIKIDOC.prefix(.TAB2,.MSK2,.REF2,PLIKIDOC.FILE().SYM,PLIKIDOC.FILE,PLIKIDOC.DOC);
               {? PLIKIDOC.first()
               || KOMM.add(
                     'W elemencie docelowym (%1) występuje już dokument %2'
                     '. Poprzednia wartość zostaje zachowana.'@
                     [exec('record','#to_string',_d),PLIKIDOC.FILE().SYM],7,,1
                  )
               || PLIKIDOC.TAB:=.TAB2;
                  PLIKIDOC.REF:=.REF2;
                  PLIKIDOC.MSK:=.MSK2;
                  _ok:=PLIKIDOC.add()
               ?};
               PLIKIDOC.cntx_pop();
               _ok & PLIKIDOC.next()
            !}
         ?};
         _ok
      ",type_of(''),type_of(null()))
   )
?};
{? var_pres('DocLib')<=0 || DocLib:=obj_new(@.CLASS.oPLIB) ?};
~~


\bl_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: blank pól PLIKI*.TAB
::   WY: PLIB.TAB
::  OLD: \bl_tab/plib.fml
::----------------------------------------------------------------------------------------------------------------------
DocLib.TAB


\bl_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: blank pól PLIKI*.REF
::   WY: PLIB.REF
::  OLD: \bl_ref/plib.fml
::----------------------------------------------------------------------------------------------------------------------
DocLib.REF


\bl_msk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [12.41]
:: OPIS: blank pól PLIKI*.MSK
::   WY: PLIB.MSK
::  OLD: \bl_msk/plib.fml
::----------------------------------------------------------------------------------------------------------------------
DocLib.MSK


\dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Selekcja zbioru dokumentacji do podanego rekordu
::   WE: _a - akronim tabeli (STRING)
::       _b - TAB.ref() rekordu
::       [_c] - możliwość redagowania 1/0 - domyślnie 1
::       [_d] - dodatkowy opis w nagłówku okna - domyślnie ''
::       [_e] - wyświetlanie napisu 'PODGLĄD' 1/0 - domyślnie 0
::   WY: ~~
::  OLD: \dokum/plib.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<>type_of(0) || _c:=1 ?};
{? var_pres('_d')<>type_of('') || _d:='' ?};
{? var_pres('_e')<>type_of(0) || _e:=0 ?};
DocLib.select(_a,_b,_c,_d,_e);
~~


\win_t_doc_disp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Wyświetlenie okna z dokumentami przy wejściu do okna z obrazkami
::   WY: ~~
::  OLD: \win_doc_disp/plib.fml
::----------------------------------------------------------------------------------------------------------------------
grp_disp(PLIKIDOC,'WER');
~~


\TKTL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Wyświetlanie/redagowanie dokumentacji elektronicznej dla karty/wzorca
::   WE: _a - TKTL.ref()
::       [_b] - karta/wzorzec używana na wyższym poziomie wywołania
::  OLD: \dok_tktl/tech1.fml
::----------------------------------------------------------------------------------------------------------------------
_tktl:=_a;
_used:={? var_pres('_b')=type_of(0) || _b || 0 ?};

_locked:=exec('tktl_lock','tech_common',_tktl,'P');
_used:=_used | ~_locked;
params_set('used',_used);
exec('FindAndGet','#table',TKTL,_tktl,,
   "_noedit:=_c.used | STAT_N='N' & TORW<>'Z' | STAT_P='T' & STAN<>'T' |
               STAN='T' & exec('can_modify','tech_common')=0 | ARCH='T' |
             ~(exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRDP') & TORW='T' |
               exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRDP') & TORW='W' |
               exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC') & TORW='Z');
    _view:=_c.used | STAT_N='N' & TORW<>'Z' | STAT_P='T' & STAN<>'T' |
             STAN='T' & exec('can_modify','tech_common')=0 | ARCH='T';
    exec('dokum','tech_doc','TKTL',ref(),~_noedit,
       {? TORW='W' || 'Załączniki do wzorca: %1'@[NRK] || 'Załączniki do karty: %1'@[NRK] ?}+' '+'wersja: %1'@[WER],
       _view
    )
   "
);
{? _locked || exec('tktl_unlock','tech_common',_tktl,'P') ?};
''


\TOPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Wyświetlanie / redagowanie dokumentacji elektronicznej
::       Wołane z menu operacji technologii
::  OLD: \dok_toper/tex_ope1.fml
::----------------------------------------------------------------------------------------------------------------------
_noedit:=params_get().env_oper.used | TKTL.STAT_N='N' & TKTL.TORW<>'Z' | TKTL.STAT_O='T' & TKTL.STAN<>'T' |
           TKTL.STAN='T' & exec('can_modify','tech_common')=0 | TKTL.ARCH='T' | TOPER.ACT<>'T' |
         ~(exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTO') & TKTL.TORW='T' |
           exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTO') & TKTL.TORW='W' |
           exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC') & TKTL.TORW='Z');
_view:=params_get().env_oper.used | TKTL.STAT_N='N' & TKTL.TORW<>'Z' | TKTL.STAT_O='T' & TKTL.STAN<>'T' |
        TKTL.STAN='T' & exec('can_modify','tech_common')=0 | TKTL.ARCH='T' | TOPER.ACT<>'T';
exec('dokum','tech_doc','TOPER',TOPER.ref(),~_noedit,'Załączniki'@,_view);
~~


\TMAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Wyświetlanie / redagowanie dokumentacji elektronicznej
::       Wołane z okna wertowania TMAT
::  OLD: \dok_tmat/tex_tma1.fml
::----------------------------------------------------------------------------------------------------------------------
_noedit:=params_get().env_mater.used | TKTL.STAT_N='N' & TKTL.TORW<>'Z' | TKTL.STAT_S='T' & TKTL.STAN<>'T' |
           TKTL.STAN='T' & exec('can_modify','tech_common')=0 | TKTL.ARCH='T' | TMAT.ACT<>'T' |
         ~(exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTS') & TKTL.TORW='T' |
           exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTS') & TKTL.TORW='W' |
           exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC') & TKTL.TORW='Z');
_view:=params_get().env_mater.used | TKTL.STAT_N='N' & TKTL.TORW<>'Z' | TKTL.STAT_S='T' & TKTL.STAN<>'T' |
        TKTL.STAN='T' & exec('can_modify','tech_common')=0 | TKTL.ARCH='T' | TMAT.ACT<>'T';
exec('dokum','tech_doc','TMAT',TMAT.ref(),~_noedit,'Załączniki'@,_view);
~~


\TCHMAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Wyświetlanie / redagowanie dokumentacji elektronicznej
::       Wołane z okna wertowania TCHMAT
::  OLD: \dok_tchmat/tex_tma1.fml
::----------------------------------------------------------------------------------------------------------------------
_noedit:=params_get().env_mater.used | TKTL.STAT_N='N' & TKTL.TORW<>'Z' | TKTL.STAT_S='T' & TKTL.STAN<>'T' |
          TKTL.STAN='T' & exec('can_modify','tech_common')=0 | TKTL.ARCH='T' | TCHMAT.ACT<>'T' |
         ~(exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRTS') & TKTL.TORW='T' |
           exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRTS') & TKTL.TORW='W' |
           exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC') & TKTL.TORW='Z');
_view:=params_get().env_mater.used | TKTL.STAT_N='N' & TKTL.TORW<>'Z' | TKTL.STAT_S='T' & TKTL.STAN<>'T' |
        TKTL.STAN='T' & exec('can_modify','tech_common')=0 | TKTL.ARCH='T' | TCHMAT.ACT<>'T';
exec('dokum','tech_doc','TCHMAT',TCHMAT.ref(),~_noedit,'Załączniki'@,_view);
~~


\TACTTLS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Wyświetlanie / redagowanie dokumentacji elektronicznej
::       Wołane z okna wertowania TACTTLS
::  OLD: \dok_tacttls/tex_util.fml
::----------------------------------------------------------------------------------------------------------------------
_noedit:=params_get().env_tool.used | TKTL.STAT_N='N' & TKTL.TORW<>'Z' | TKTL.STAT_P='T' & TKTL.STAN<>'T' |
           TKTL.STAN='T' & exec('can_modify','tech_common')=0 | TKTL.ARCH='T' | TACTTLS.ACT<>'T' |
         ~(exec('chk_role','#b__box',OPERATOR.USER,'TTE_TEC_DRDP') & TKTL.TORW='T' |
           exec('chk_role','#b__box',OPERATOR.USER,'TTE_WTE_DRDP') & TKTL.TORW='W' |
           exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DTEC') & TKTL.TORW='Z');
_view:=params_get().env_tool.used | TKTL.STAT_N='N' & TKTL.TORW<>'Z' | TKTL.STAT_P='T' & TKTL.STAN<>'T' |
        TKTL.STAN='T' & exec('can_modify','tech_common')=0 | TKTL.ARCH='T' | TACTTLS.ACT<>'T';
exec('dokum','tech_doc','TACTTLS',TACTTLS.ref(),~_noedit,'Załączniki'@,_view);
~~


\ZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [2006]
:: OPIS: Wyświetlanie / redagowanie dokumentacji elektronicznej
::   WE: _a - ZL.ref()
::       [_b] - zlecenie używane na wyższym poziomie wywołania
::  OLD: \dok_zl/zlec5.fml
::----------------------------------------------------------------------------------------------------------------------
_zl:=_a;
_used:={? var_pres('_b')=type_of(0) || _b || 0 ?};

_locked:=exec('zl_lock','zl_common',_zl,'I',1);
_used:=_used | ~_locked;
params_set('used',_used);
exec('FindAndGet','#table',ZL,_zl,,
   "_noedit:=_c.used | ZL.STAT_N='N' | ZL.STAN='Z';
    _view:=_c.used | ZL.STAT_N='N' | ZL.STAN='Z';
    exec('dokum','tech_doc','ZL',ZL.ref(),~_noedit,'Załączniki do zlecenia: %1'@[ZL.SYM],_view)
   "
);
{? _locked || exec('zl_unlock','zl_common',_zl,'I') ?};
~~


\action_docum_button
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Podgląd dokumentacji karty technologicznej - obsługa przycisku w oknie redagowania TKTL w czynnościach
::----------------------------------------------------------------------------------------------------------------------
exec('TKTL','tech_doc',TKTL.ref(),1);
''


\chk_rec_plib_img
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rekord po dla PLIB_IMG
::----------------------------------------------------------------------------------------------------------------------
__CHK.record(PLIB_IMG,,'SYM','IMAGE')


\chk_rec_plikiimg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rekord po dla PLIKIIMG
::----------------------------------------------------------------------------------------------------------------------
__CHK.record2(PLIKIIMG,'FILE','Symbol'@)


\chk_rec_plikidoc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Rekord po dla PLIKIDOC
::----------------------------------------------------------------------------------------------------------------------
__CHK.record2(PLIKIDOC,'FILE','Symbol'@)


\after_refresh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [17.00]
:: OPIS: Po odświeżeniu w oknie PLIKIIMG.GRP
::----------------------------------------------------------------------------------------------------------------------
{? grp_empty(PLIKIIMG,'WER')
|| PLIB_IMG.blank()
?};
~~


\GROP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Wyświetlanie / redagowanie dokumentacji elektronicznej
::       Wołane z menu grup operacji
::----------------------------------------------------------------------------------------------------------------------
_locked:=exec('blk_lock','#table','GROP',GROP.ref());
_used:=~_locked;
_noedit:=_used | ~exec('chk_role','#b__box',OPERATOR.USER,'TPP_GOP_DRED') | GROP.STATUS='Z';
_view:=_used | GROP.STATUS='Z';
exec('dokum','tech_doc','GROP',
   GROP.ref(),
   ~_noedit,
   'Załączniki do grupy operacji: %1'@[GROP.KOD],
   _view
);
exec('blk_unlock','#table','GROP',GROP.ref());
~~


\ZGP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Wyświetlanie / redagowanie dokumentacji elektronicznej
::       Wołane z menu pozycji przewodnika
::----------------------------------------------------------------------------------------------------------------------
ZL.cntx_psh(); ZGH.cntx_psh();
_zgh_stan:=ZGP.NRZLP().STAN;
_zl_stan:=ZGP.ZL().STAN;
ZL.cntx_pop(); ZGH.cntx_pop();
_locked:=exec('blk_lock','#table','ZGP',ZGP.ref());
_used:=~_locked;
_noedit:=_used | ~exec('chk_role','#b__box',OPERATOR.USER,'TTE_PZL_DPRZ') | _zgh_stan='T' | _zl_stan='Z';
_view:=_used | _zgh_stan='T' | _zl_stan='Z';
exec('dokum','tech_doc','ZGP',
   ZGP.ref(),
   ~_noedit,
   'Załączniki do pozycji przewodnika: %1'@[exec('record','#to_string',ZGP.ref())],
   _view
);
exec('blk_unlock','#table','ZGP',ZGP.ref());
~~


\fml_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: formuła na edycję, musi zwrócić 0/1
::   WE: _a - ANY - argumenty przekazane jako _f
::       _b - obj_new - bufor tabeli przekazany jako _c
::       _c - STRING - 'Dołącz' - dołączanie
::                     'Popraw' - poprawianie
::      [_d] - dla poprawiania, uidref() poprawianego rekordu
::
::      wewnątrz formuły można za pomocą params_get().env_blob pobrać obiekt środowiska oraz jest dostępny KOMM
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env_blob;
_args:=_a;
_buffer:=_b;
_action:=_c;
_uid:=_d;
{? _action='Popraw'
|| _env.act_popraw(_uid)
|| _buffer.TAB:=_args.TAB;
   _buffer.MSK:=_args.MSK;
   _buffer.REF:=_args.REF;
   1
?}


\fml_file
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: formuła techniczna, wykonywana dla każdego pliku w transakcji, musi zwrócić 0/1
::   WE: _a - ANY - argumenty przekazane jako _f
::       _b - obj_new - bufor tabeli przekazany jako _c
::       _c - STRING - nazwa pliku
::       _d - STRING - ścieżka do pliku (bez @)
::
::       wewnątrz formuły można za pomocą params_get().env_blob pobrać obiekt środowiska oraz jest dostępny KOMM
::----------------------------------------------------------------------------------------------------------------------
1


\fml_valid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: formuła walidująca czy można dołączyć, poprawić załącznik
::   WE: _a - ANY - argumenty przekazane jako _f
::       _b - obj_new - bufor tabeli przekazany jako _c
::       _c - STRING - nazwa pliku
::       _d - STRING - ścieżka do pliku (bez @)
::       _e - INTEGER - 0 - komunikaty na ekran, 1 - do KOMMa
::----------------------------------------------------------------------------------------------------------------------
_args:=_a;
_buffer:=_b;
_filename:=_c;
_filepath:=_d;
_dialog:=_e;

_result:=1;
_env:=params_get().env_blob;
{? _env.has_file(_filename)>0
|| _result:=0;
   _msg:='Plik o nazwie: %1 jest już dodany jako załącznik i nie może zostać dodany ponownie.'@[_filename];
   {? _dialog=0
   || FUN.emsg(_msg)
   || KOMM.add(_msg,2,,1)
   ?}
?};
_result


\fml_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [18.02]
:: OPIS: Agrumenty formuł - do użycia z kontrolką filelistpanel
::----------------------------------------------------------------------------------------------------------------------
_args:=obj_new('TAB','MSK','REF');
_args.TAB:='*';
_args.MSK:='';
_args.REF:=0;
_args

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:36 76654d44b26982b1d526069a7fd5a2b4c650f7acd4269b46b5989bc176993e13836e5ed0c0f2edc4bf978298026cd4cd063af76169059f538d2f2ca47a9dc06aefbcd82cac421e303b4de8804e5c00547d6348f398b1366b29e2018c3c7cf9148f40ae1fc907a66319c583a187f2d77c0d29ab2b43db5bcad7f8cf4c512e133d

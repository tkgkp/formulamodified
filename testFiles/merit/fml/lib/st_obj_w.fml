:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_obj_w.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa wersji obiektów dla systemu Statystyki
::======================================================================================================================


\clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Czyści powiązania do rekordu tabeli ST_OBJ_W
::   WE: _a - ST_OBJ_W.ref()
::   WY: >0  -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PRYWATNA><CLEAN>
::UWAGA: Parametry bez [] są wymagane, formuła może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};
:: --- powiązania do ---
ST_OBJ_W.cntx_psh(); ST_OBJ_W.clear();
{? ST_OBJ_W.seek(_ref)
||
   {? _can_continue>0 & ST_OBJ_W.count()>0
   ||
::    1. Usuwam ST_LOG które wskazują na ten element
      _rule:="
         _ref:=_b;
         _result:=1;
         _can_continue:=1;
         ST_LOG.cntx_psh();
         ST_LOG.index('ST_OBJ_W');
         ST_LOG.prefix(_ref);
         {? ST_LOG.first()
         || {!
            |? _can_continue:=exec('delete','st_log',ST_LOG.ref());
               ST_LOG.first() & _can_continue>0
            !}
         ?};
         ST_LOG.cntx_pop();

         {? _can_continue<=0
         || _result:=0
         ?};
         _result
      ";
      _can_continue:=exec('for_each_mask','#table',ST_LOG,_rule,,,_ref,1)

   ?};
   ~~
?};
ST_OBJ_W.cntx_pop();
:: --- wszystkie powiazania usuniete? ---

{? _can_continue>0
|| _result:=1
|| undo()
?};

{? _mydo || end() ?};

_result


\delete
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Kasuje podany rekord tabeli ST_OBJ_W (wykonywane w transakcji!!!)
::   WE: _a - ST_OBJ_W.ref()
::   WY: >0 -wyczyszczone,
::       <=0 -niewyczyszczone
::  TAG: <PUBLICZNA><DEL>
::UWAGA: Parametry bez [] są wymagane, formula może nie sprawdzać czy zostały podane i może wystąpić błąd.
::----------------------------------------------------------------------------------------------------------------------
:: jeżeli transakcja została zerwana, to nie ma sensu przetwarzać formuły
{? do_state()=2 || return(-100) ?};

_ref:=_a;

_result:=0;
_can_continue:=1;

:: sprawdzam, czy to w tej formule będę zakładał transakcję, czy już jest założona
_mydo:=do_state()=0;
{? _mydo || do() ?};
ST_OBJ_W.cntx_psh(); ST_OBJ_W.clear();
{? ST_OBJ_W.seek(_ref)
|| {? exec('clean','st_obj_w',_ref)>0
   || {? ST_OBJ_W.del(,1)>0
      || _result:=1
      || undo();
         _result:=-3
      ?}
   || _result:=-2
   ?}
|| _result:=0
?};

{? _result<0
|| undo()
?};

ST_OBJ_W.cntx_pop();
{? _mydo || end() ?};
_result


\st_obj_w_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Dołącz dla ST_OBJ_W
::----------------------------------------------------------------------------------------------------------------------
ST_OBJ_W.win_edit('RED');
ST_OBJ_W.blank();
{? VAR.A_ST_OBJ<>null()
|| ST_OBJ_W.ST_OBJ:=VAR.A_ST_OBJ
?};
{? ST_OBJ_W.edit()
|| ST_OBJ_W.add()
?};
~~


\st_obj_w_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń dla ST_OBJ_W
::----------------------------------------------------------------------------------------------------------------------
{? VAR.GRUPA='T' | FUN.ask('Czy usunąć bieżący wiersz?'@)
|| exec('delete','st_obj_w',ST_OBJ_W.ref())
?};
~~


\st_obj_w_mod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Popraw dla ST_OBJ
::----------------------------------------------------------------------------------------------------------------------
ST_OBJ_W.win_edit('RED');
{? ST_OBJ_W.edit()
|| ST_OBJ_W.put()
?};
~~


\st_obj_w_del_bg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń przed dla grupy rekordów (tabela ST_OBJ_W)
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy usunąć zaznaczone wiersze?'@)
|| VAR.GRUPA:='T';
   1
|| 0
?}


\st_obj_w_del_ag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [20.42]
:: OPIS: Akcja Usuń po dla grupy rekordów (tabela ST_OBJ_W)
::----------------------------------------------------------------------------------------------------------------------
VAR.GRUPA:='N';
~~


\delete_not_used
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Usuwa wszystkie nieużywane rekordy
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------

_result:=0;
_can_continue:=1;

_mydo:=do_state()=0;
{? _mydo || do() ?};

ST_OBJ_W.cntx_psh();
ST_OBJ_W.index('ST_WER');
ST_OBJ_W.prefix();
{? ST_OBJ_W.first()
|| {!
   |? _next:=0;
      _ref_nxt:=null();
      ST_OBJ_W.cntx_psh();
      {? ST_OBJ_W.next()
      || _ref_nxt:=ST_OBJ_W.ref()
      ?};
      ST_OBJ_W.cntx_pop();

      {? ST_OBJ_W.count()=0
      || _can_continue:=exec('delete','st_obj_w',ST_OBJ_W.ref())
      ?};

      {? _ref_nxt<>null()
      || _next:=ST_OBJ_W.seek(_ref_nxt)
      ?};
      _next>0 & _can_continue>0
   !}
?};
ST_OBJ_W.cntx_pop();
{? _can_continue>0
|| _result:=1
|| undo()
?};
{? _mydo || end() ?};
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:37 217af6e0385c3294f74e1a194ce9aa8510ed3df308be46334c130e61acda1939ffd2eb86a6ccfff7390e9b2aa28734b272dbd0f23fb112b480c28800dbc89416897c9b147386b8741df606e49dea13c8f59d75f83a1cda3f3c078085ccb9f81ad31ebc339085bbad05a00de143a28667909af0f454f14184d4af4a0f477ea3b6

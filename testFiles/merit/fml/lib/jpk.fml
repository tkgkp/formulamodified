:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: jpk.fml [17.00]
:: Utworzony: 29.03.2016
:: Autor: MB
::======================================================================================================================
:: Zawartosc: Formuly odpowiedzialne za obsługę Jednolitego Pliku Kontrolnego (JPK)
::======================================================================================================================


\def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Definicje struktur JPK
::----------------------------------------------------------------------------------------------------------------------
SKID.ISTDEF:='J';
BPMN.SYM_DOM:='FKS';
ISTDEF.index('VER'); ISTDEF.prefix(BPMN.SYM_DOM,SKID.ISTDEF);
ISTDEF.win_sel('W_JPK'); ISTDEF.win_edit('R_JPK2');
ISTDEFS.win_edit('R_JPK_P');
ISTDEF.actions('W_JPK');
ISTDEF.select()


\bm_jpk_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2008]
:: OPIS: Czy mozna modyfikowac specyfikacje e-Deklaracji
::   WE: _a - czy komunikowac? 1-tak 0-nie
::   WY: 1-tak 0-nie
::  OLD: \bm_jpk_def/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
_ok:=1;
JPK.cntx_psh();
JPK.index('ISTDEF'); JPK.prefix(ISTDEF.ref());
{? JPK.first()
|| _ok:=0;
   {? _a
   || FUN.info('Istnieją pliki JPK dla schematu.\nModyfikacje nie są możliwe.'@)
   ?}
?};
JPK.cntx_pop();
_ok


\bd_jpk_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed usun okna W_JPK tabeli ISTDEF
::   WE: _a - opcjonalny-integer 0-nie wyswietla komunikatow lub pytan, brak _a:=1-komunikaty
::  OLD: \bd_jpk_def/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')=type_of(1) || _a || _a:=1 ?};
{? _a || {? ~exec('bm_jpk_def','jpk',1) || return(0) ?} ?};
{? _a || {? ~exec('can_modyfy','jpk_sf',1) || return(0) ?} ?};
{? {? _a || FUN.ask('Czy usunąć bieżący wiersz?'@) || 1 ?}
|| {? ISTDEF.r_lock(1,1)
   || ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),0);
      {? ISTDEFS.first()
      || {!
         |? exec('del_el','jpk');
            ISTDEFI.index('LP'); ISTDEFI.prefix(ISTDEFS.ref());
            {? ISTDEFI.first() || {! |? ISTDEFI.del() !} ?};
            ISTDEFS.del()
         !}
      ?};
      {? ~ISTDEF.del(,1) || ISTDEF.r_unlock() ?}
   || FUN.info('Struktura jest definiowana przez innego użytkownika.'@)
   ?}
?}


\del_el
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [12.41]
:: OPIS: Usuwa elementy drzewa struktury e-Deklaracji
::  OLD: \del_el/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEFS.cntx_psh(); ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(ISTDEF.ref(),ISTDEFS.ref());
{? ISTDEFS.first()
|| {!
   |? exec('del_el','jpk');
      ISTDEFI.index('LP'); ISTDEFI.prefix(ISTDEFS.ref());
      {? ISTDEFI.first() || {! |? ISTDEFI.del() !} ?};
      ISTDEFS.del()
   !}
?};
ISTDEFS.cntx_pop()


\ar_jpk_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Rekord po okna W_JPK tabeli ISTDEF
::   WE: [_a] - tryb: 1-dołacz 0-popraw
::  OLD: \ar_jpk_def/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=-menu_txt='dołącz' | -menu_txt='kopia' ?};
_r:=__CHK.record2(ISTDEF,
   'RODZAJ','Rodzaj'@,
   'DATA','Data'@,
   'VER','Nazwa'@,
   'XMLNS','Schemat'@
);
{? _r=''
|| {? ISTDEF.RODZAJ='SF'
|| ISTDEF.cntx_psh();
   _ref:=ISTDEF.ref();
   ISTDEF.index('DATA'); ISTDEF.prefix(BPMN.SYM_DOM,SKID.ISTDEF);
      {? ISTDEF.find_key(ISTDEF.RODZAJ,ISTDEF.NR,ISTDEF.DATA,ISTDEF.VER,) & (_a | _ref<>ISTDEF.ref())
      || FUN.info('Istnieje już specyfikacja JPK dla rodzaju: %1 ważna od dnia: %2 o nazwie: %3.'@[ISTDEF.RODZAJ,$ISTDEF.DATA,ISTDEF.VER]);
      _r:='DATA'
   ?};
   ISTDEF.cntx_pop()
   || ISTDEF.cntx_psh();
      _ref:=ISTDEF.ref();
      ISTDEF.index('DATA'); ISTDEF.prefix(BPMN.SYM_DOM,SKID.ISTDEF);
      {? ISTDEF.find_key(ISTDEF.RODZAJ,ISTDEF.NR,ISTDEF.DATA) & (_a | _ref<>ISTDEF.ref())
      || FUN.info('Istnieje już specyfikacja JPK dla rodzaju %1 ważna od dnia %2.'@[ISTDEF.RODZAJ,$ISTDEF.DATA]);
         _r:='DATA'
      ?};
      ISTDEF.cntx_pop()
   ?}
?};
_r


\shaAdd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Dodanie napisu do funkcji liczacej skrot dokumentu
::   WE: _a - napis
::  OLD: \shaAdd/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? shaFile.is_open
|| fwrite(shaFile,_a)
?}


\shaReset
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Poczatek funkcji liczacej skrot dokumentu
::  OLD: \shaReset/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('shaFile','shaFN');
shaFN:='sha'+$SYSLOG.tm_stamp()+'.txt';
shaFile:=fopen(shaFN,'w',1,,1)


\shaDigest
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Funkcja zwraca skrot dokumentu, usuwa wykorzystywane zmienne
::  OLD: \shaDigest/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('shaFile')>0 & shaFile.is_open
|| _sha:=hash(shaFile);
   fclose(shaFile);
   ferase(shaFN,1);
   VAR_DEL.delete('shaFile');
   _sha
|| ''
?}


\f3_rodzaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Rodzaj schematu
::  OLD: \f3_rodzaj/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_r:='';
VAR_DEL.delete('P_RODZ');
P_RODZ:=obj_new(@.CLASS.POPUP);
_r:={? var_pres('J_GV_POZ')>0
    || 3+P_RODZ.select(
       'FA   - Faktury VAT',
       'GV   - Ewidencja członka grupy VAT',
       'KR   - Księgi rachunkowe',
       'MAG  - Magazyn',
       'SF   - e-Sprawozdania',
       'VAT  - Ewidencja zakupów i sprzedaży',
       'V7M  - Ewidencja zakupów i sprzedaży oraz deklaracja VAT-7',
       'V7K  - Ewidencja zakupów i sprzedaży oraz deklaracja VAT-7',
       'WB   - Wyciągi bankowe'
       )
    || 3+P_RODZ.select(
       'FA   - Faktury VAT',
       'KR   - Księgi rachunkowe',
       'MAG  - Magazyn',
       'SF   - e-Sprawozdania',
       'VAT  - Ewidencja zakupów i sprzedaży',
       'V7M  - Ewidencja zakupów i sprzedaży oraz deklaracja VAT-7',
       'V7K  - Ewidencja zakupów i sprzedaży oraz deklaracja VAT-7',
       'WB   - Wyciągi bankowe'
       )
    ?};
_r:=form(_r);
VAR_DEL.delete('P_RODZ');
{? _r<>'' || _r || 0 ?}


\ae_rodzaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji pola rodzaj
::  OLD: \ae_rodzaj/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld=''
|| FUN.info('Niewypełnione pole rodzaj.'@); 0
|? exec('prfx_rodzaj','jpk')*(fld+'|')=0
|| FUN.info('Niewłaściwa wartość pola rodzaj.'@); 0
|| SKID.DEKL_NAZ:=fld();
   1
?}


\prfx_rodzaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Rodzaje plików JPK
::  OLD: \prfx_rodzaj/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
'FA|KR|MAG|VAT|WB|GV|SF|V7M|V7K|'


\cel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca cel zlozenia pliku JPK
::  OLD: \cel/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JPK.KOR || '2' || '1' ?}


\dateTime
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca date i czas w formacie XML
::   WE: _a - data
::       _b - czas
::  OLD: \dateTime/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
STR.gsub($_a,'/','-')+'T'+_b$3


\f3_jpkgate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Kalwisz F3 dla pola XINFO.JPKGATE
::  OLD: \f3_jpkgate/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(2,
   'ADR','STRING[255]','Adres',
   'TEST','INTEGER','Testowa'
);
_add:="
   _a.blank(1);
   _a.ADR:=_b;
   _a.TEST:=_c;
   _a.add()
";
_add(_tab,'test-e-dokumenty.mf.gov.pl',1);
_add(_tab,'e-dokumenty.mf.gov.pl',0);
_o:=_tab.mk_sel('Bramki do wysyłania JPK'@);
_tab.win_fld(_o,,'ADR',,,100,,,'Adres bramki JPK'@);
_tab.win_fld(_o,,'TEST',,,,,,'Bramka testowa?'@,,,2,,"1","0");
_tab.win_act(_o,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
_tab.win_sel(_o);
{? _tab.select()
|| _tab.ADR
|| ~~
?}


\chk_dok_rej
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Sprawdzenie wypelnienia pol w rodzaju dokumentu zwiazanych z JPK
::  OLD: \chk_dok_rej/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK_REJ.JPK_TYP='W'
|| __CHK.record2(DOK_REJ,'JPK_RB','Rachunek','JPK_AN','Konto')
|| ''
?}


\bv_jpk_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed wyswietleniem DOK_REJ.TYP
::----------------------------------------------------------------------------------------------------------------------
DOK_REJ.efld_opt('RED','enable='+$(DOK_REJ.JPK_TYP='W'),DOK_REJ,'JPK_AN');
DOK_REJ.efld_opt('RED','mark='+$(DOK_REJ.JPK_TYP='W'),DOK_REJ,'JPK_AN');
DOK_REJ.efld_opt('RED','enable='+$(DOK_REJ.JPK_TYP='W'),DOK_REJ,'JPK_RB');
DOK_REJ.efld_opt('RED','mark='+$(DOK_REJ.JPK_TYP='W'),DOK_REJ,'JPK_RB');
~~


\be_jpk_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja DOK_REJ.JPK_TYP
::  OLD: \be_jpk_typ/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
JpkTyp:=DOK_REJ.JPK_TYP;
1


\ae_jpk_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji DOK_REJ.JPK_TYP
::  OLD: \ae_jpk_typ/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK_REJ.JPK_TYP='F'
|| _kod:=DOK_REJ.SLO().KOD;
   {? _kod<>'SAD' & _kod<>'VAT'
   || DOK_REJ.JPK_TYP:=JpkTyp
   ?}
?};
{? DOK_REJ.JPK_TYP<>'W'
|| DOK_REJ.JPK_AN:='';
   DOK_REJ.JPK_RB:=''
?};
{? DOK_REJ.JPK_TYP<>'F' & DOK_REJ.JPK_TYP<>''
|| DOK_REJ.JPK_FA_P:='N'
?};
exec('bv_jpk_typ','jpk');
1


\be_jpk_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redagowaniem DOK_REJ.JPK_AN
::  OLD: \be_jpk_kon/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK_REJ.JPK_TYP='W'
|| exec('be_konto','edkonto','DOK_REJ','JPK_AN')
?}


\ae_jpk_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji DOK_REJ.JPK_AN
::  OLD: \ae_jpk_kon/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ae_konto','edkonto',_a,'DOK_REJ','JPK_AN',0,1)


\bd_jpk_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed wyswietl pola DOK_REJ.JPK_RB
::  OLD: \bd_jpk_rb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK_REJ.JPK_RB<>''
|| exec('bd_varrb','rachunki','DOK_REJ','JPK_RB',REF.INFO,0)
|| ''
?}


\be_jpk_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redagowaniem pola DOK_REJ.JPK_RB
::  OLD: \be_jpk_rb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
DOK_REJ.JPK_TYP='W'


\f3_jpk_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Klawisz F3 pola DOK_REJ.JPK_RB
::  OLD: \f3_jpk_rb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('AllRB');
AllRB:=1;
OPERATOR.USER();
exec('f3_vrbup','rachunki','DOK_REJ','JPK_RB',REF.INFO,0)


\ae_jpk_rb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji pola DOK_REJ.JPK_RB
::  OLD: \ae_jpk_rb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
exec('ae_kb','rachunki')


\be_fatypc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja DOK_REJ.TYP_CENY
::  OLD: \be_fatypc/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
DOK_REJ.JPK_FA_P='T'


\be_jpk_typ_mag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redakcja pola DOK_REJ.TYP_MAG
::  OLD: \be_jpk_typ_mag/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
DOK_REJ.JPK_TYP='M'


\be_jpk_fp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed redagowanie pol rodzaju dokumentu związanego z JPK_FA
::  OLD: \be_jpk_fp/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
DOK_REJ.JPK_TYP='F'


\okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca wartosc pola JPK.OKRES na podstawie OKRO_F
::   WE: _a - okres finansowy (OKRO_F)
::  OLD: \okres/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_okres:='';
OKRO_F.cntx_psh();
OKRO_F.prefix();
{? OKRO_F.seek(_a)
|| _okres:=exec('okres2','jpk',OKRO_F.POCZ)
?};
OKRO_F.cntx_pop();
_okres


\okres2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca wartosc pola JPK.OKRES na podstawie daty
::   WE: _a - data
::  OLD: \okres2/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=date(0,0,0)
|| OKRO_F.cntx_psh();
   OKRO_F.index('ROK'); OKRO_F.prefix(OKRO_F.ROK);
   {? OKRO_F.NR=0
   || _typ:='BO';
      OKRO_F.next()
   || _typ:='BZ';
      OKRO_F.prev()
   ?};
   _okr:=$(OKRO_F.POCZ~1)+'/'+_typ;
   OKRO_F.cntx_pop();
   _okr
|| $(_a~1)+'/'+(('0'+$(_a~2))+2)
?}


\can_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy użytkownik ma uprawnienia do podglądu piku JPK
::   WE: [_a] - pokazywać komunikat? [1]-tak 0-nie
::  OLD: \can_view/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
{? JPK.TYP='WB'
|| _jest:=0;
   {? Perm.hasFull('HRB')
   || _jest:=1
   || UPR_RB.index('RB'); UPR_RB.prefix(OPERATOR.USER);
      {? UPR_RB.first()
      || {!
         |? _jest:=UPR_RB.RB().KRAJ().KODISO+SKID_RBK.N=JPK.DODAT;
            _jest=0 & UPR_RB.next()
         !}
      ?}
   ?};
   {? _jest=0 & _a
   || FUN.info('Brak uprawnień do przeglądania danych\nzwiązanych z rachunkiem bankowym pliku wyciągu.'@)
   ?};
   _jest
|| 1
?}


\ae_r_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji pola rodzaj
::  OLD: \ae_r_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld<>HELPJPK.ISTDEF().RODZAJ
|| HELPJPK.ISTDEF:=null
?};
{? fld<>''
|| {? exec('prfx_rodzaj','jpk')*(fld+'|')=0
   || FUN.info('Niewłaściwa wartość pola rodzaj.'@); 0
   || {? HELPJPK.ISTDEF=null
      || exec('find_sch','jpk',SSTALE.AO().POCZ)
      ?};
      {? var_press('JpkXml')<=0
      || {? fld='KR' | fld='VAT' | fld='SF' | 2+fld='V7' | 2+fld='GV'
         || HELPJPK.OKR_TYP:='O'
         |? fld='WB' | fld='MAG'
         || HELPJPK.OKR_TYP:='D'
         ?};
         {? HELPJPK.OKR_TYP='O'
         || {? ~HELPJPK.OKR_OD & ~HELPJPK.OKR_DO
            || HELPJPK.ROK_OD:=HELPJPK.ROK_DO:=SSTALE.AR;
               {? HELPJPK.RODZAJ='SF'
               || OKRO_F.cntx_psh();
                  OKRO_F.index('ROK'); OKRO_F.prefix(HELPJPK.ROK_OD);
                  {? OKRO_F.first() & OKRO_F.POCZ=date(0,0,0) & OKRO_F.next()
                  || HELPJPK.OKR_OD:=OKRO_F.ref()
                  ?};
                  HELPJPK.OKR_DO:=SSTALE.AO;
                  OKRO_F.cntx_pop()
               || HELPJPK.OKR_OD:=HELPJPK.OKR_DO:=SSTALE.AO
               ?}
            ?};
            HELPJPK.DATA_OD:=HELPJPK.DATA_DO:=date(0,0,0)
         || {? HELPJPK.DATA_OD=date(0,0,0) & HELPJPK.DATA_OD=HELPJPK.DATA_DO
            || HELPJPK.DATA_OD:=SSTALE.AO().POCZ;
               HELPJPK.DATA_DO:=OKRO_F.KON
            ?};
            HELPJPK.ROK_OD:=HELPJPK.ROK_DO:=HELPJPK.OKR_OD:=HELPJPK.OKR_DO:=null
         ?};
         {? fld='FA'
         || {? ~HELPJPK.WAL & XINFO.SLWAL
            || HELPJPK.WAL:=FINFO.NAROD
            ?}
         || HELPJPK.WAL:=null
         ?};
         {? fld()<>'WB'
         || HELPJPK.RB:=''
         ?};
         {? fld()<>'SF'
         || HELPJPK.E_SPR:=null;
            HELPJPK.E_SPR_A:='N'
         ?}
      ?};
      HELPJPK.CYKL:={? 'VAT|SF|V7M|V7K|GV|'*HELPJPK.RODZAJ || 'T' || 'N' ?};
      win_disp();
      1
   ?}
|| win_disp();
   1
?}


\ae_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji pola HELPJPK.OKRES
::  OLD: \ae_okres/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()<>'' & ~JpkF3o.find_key(fld(),)
|| FUN.info('Nie znaleziono plików JPK dla okresu %1.'@[fld()]);
   0
|| 1
?}


\f3_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Klawisz F3 dla pola HELPJPK.OKRES
::  OLD: \f3_okres/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? fld()<>''
|| JpkF3o.find_key(fld(),) | JpkF3o.first()
|| JpkF3o.first()
?};
{? JpkF3o.select(,1,5)
|| JpkF3o.OKRES
|| ~~
?}


\uzu_daty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Uzupelnia daty zmiennej HELPJPK
::  OLD: \uzu_daty/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.OKR_TYP='O'
|| OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
   {? HELPJPK.OKR_OD().POCZ=date(0,0,0)
   || {? OKRO_F.NR=0
      || OKRO_F.next()
      || OKRO_F.prev()
      ?}
   ?};
   HELPJPK.DATA_OD:=OKRO_F.POCZ;
   {? HELPJPK.OKR_DO().KON=date(0,0,0)
   || {? OKRO_F.NR=0
      || OKRO_F.next()
      || OKRO_F.prev()
      ?}
   ?};
   HELPJPK.DATA_DO:=OKRO_F.KON
?}


\tabKom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca tabele na tymczasowe komunikaty
::  OLD: \tabKom/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(1,
   'O'  ,'STRING[255]','Opis',
   'REF','STRING[16]' ,'FAKS.ref()'
);
_o:=_tab.mk_sel('Uwagi','P',,'tabkomjpk',,,,,'U');
_tab.win_fld(_o,,'O',,,100);
::_tab.win_act(_o,,'Formuła','Nazwa_akcji',,,"przed","po",1);
_tab.win_sel(_o);
_tab


\addkom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Dodaje komunikat
::   WE: _a - typ
::       _b - treść
::  OLD: \addkom/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('NoKom')>0 || return() ?};
_opis:=STR.gsub(_b,'\n',' ');
JPK_KOM.index('JPK'); JPK_KOM.prefix(JPK.ref(),_opis,);
{? ~JPK_KOM.first()
|| JPK_KOM.blank();
   JPK_KOM.JPK:=JPK.ref();
   JPK_KOM.TYP:=_a;
   JPK_KOM.OPIS:=_opis;
   {? JPK_KOM.add() & +_b>255
   || JPK_KOM.memo_set(_b,'MEMO');
      JPK_KOM.memo_put()
   ?}
?}


\upd_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Aktualizacja informacji o komunikatach
::  OLD: \upd_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
JPK_KOM.index('TYP');
JPK_KOM.prefix(JPK.ref());
JPK.KOM:={? JPK_KOM.find_key('E')
         || {? var_press('jpkErr')>0 || jpkErr+=1 ?};
            'E'
         |? JPK_KOM.find_key('W')
         || {? var_press('jpkWrn')>0 || jpkWrn+=1 ?};
            'W'
         || 'N'
         ?};
JPK.SHA:=exec('shaDigest','jpk');
JPK.LINII:={? var_press('LiczbaL')>0 || LiczbaL || 0 ?}


\jpk_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzenie JPK dla okresu
::   WE:  _a  - okres
::        _b  - data od
::        _c  - data do
::        _d  - opis pliku
::        _e  - dodatkowe informacje (np. nr rachunku dla wyciagu)
::       [_f] - czy plik generowany z systemu? ['T'] - tak
::       [_g] - numer kolejny?]
::       [_h] - początek okresu
::       [_i] - część pliku JPK
::  OLD: \jpk_nag/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
JPK.blank();
JPK.FIRMA:=REF.FIRMA;
JPK.D_GEN:={? var_press('DATE',HELPJPK)>0  & HELPJPK.DATE<>date(0,0,0) || HELPJPK.DATE || date() ?};
JPK.T_GEN:={? var_press('TIME',HELPJPK)>0  & HELPJPK.TIME<>time(0,0,0) || HELPJPK.TIME || time() ?};
JPK.U_GEN:=OPERATOR.USER().KOD;
JPK.TYP:=HELPJPK.RODZAJ;
JPK.ISTDEF:=HELPJPK.ISTDEF;
JPK.OKRO_F:=_a;
JPK.OD:=_b;
JPK.DO:=_c;
JPK.OPIS:=_d;
JPK.DODAT:=_e;
JPK.GEN:={? var_press('_f')>0 || _f || 'T' ?};
JPK.KOR:=HELPJPK.KOR;
JPK.OKRES:=exec('okres2','jpk',{? var_press('_h')>0 || _h |? JPK.TYP='SF' || JPK.DO || JPK.OD ?});
JPK.CYKL:=HELPJPK.CYKL;
{? var_press('JpkStamp')>0
|| JPK.TM_STAMP:=JpkStamp
?};
{? 'VAT,V7M,V7K,GV'*JPK.TYP & (JPK.OKRES>='2018/01' | JPK.KOR & JPK.ISTDEF().DATA>=date(2018,1,1))
|| {? var_press('_g')<=0 | _g=-1
   || JPK.cntx_psh();
      JPK.index('LP'); JPK.prefix(REF.FIRMA,JPK.OKRES,JPK.TYP,);
      _lp:={? JPK.last() || JPK.LP+1 || 0 ?};
      JPK.cntx_pop()
   || _lp:=_g
   ?};
   JPK.LP:=_lp;
   JPK.EMAIL:=HELPJPK.EMAIL
?};
{? JPK.TYP='SF'
|| JPK.E_SPR:=HELPJPK.E_SPR
?};
{? var_pres('_i')>0 & var_pres('PART',JPK)>0
|| JPK.PART:=_i
?};
JPK.add()


\gen_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Tworzy plik JPK
::   WE: [_a] - czy tworzyc plik? [1]-tak 0-nie
::   WY: Nazwa utworzonego pliku
::  OLD: \gen_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')<=0 || _a:=1 ?};
_naz:='';
ISTDEFS.index('DRZEWO'); ISTDEFS.prefix(JPK.ISTDEF,0);
{? ISTDEFS.first()
|| _naz:=(44+JPK.ISTDEF().VER)+$JPK.tm_stamp()+'.xml';
   _f:={? _a || XmlFile:=fopen(_naz,'w',1) || 1 ?};
   {? _f
   || exec('shaReset','jpk');
      NoSHA:='DataWytworzeniaJPK,dtsf:DataSporzadzenia';
      LiczbaL:=1;
      ErrFun:="exec('errfun','jpk',_a,_b)";
      {? _a || fwrite(_f,'<?xml version="1.0" encoding="UTF-8"?>') ?};
      {!
      |? exec('gen_one','xml',{? _a || _f ?},0,1);
         ISTDEFS.next()
      !};
      {? _a
      || fclose(_f);
         _tmpdir:= fmk_tmp_dir();
         {? type_of(_tmpdir) <> type_of(~~)
         || _fdir:=_tmpdir.get_path;
            {? fcopy(_naz,_fdir+'/'+_naz,1,0,1)
            || exec('validate','xml',_fdir+'/'+_naz);
               ferase(_fdir+'/'+_naz,0)
            ?}
         ?}
      ?}
   || _naz:=''
   ?};
   VAR_DEL.delete('TAB_ERR','ErrFun','XmlFile')
?};
_naz


\kr_zois
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na start/stop petli dla elementu ZOIS JPK_KR
::   WE: _a - 1-start 0-stop
::  OLD: \kr_zois/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('Xml')>0
|| XmlProg:=1;
   {? Xml[1].size()>0 || progress(100*XmlProg/Xml[1].size(),'ZOiS','Generowanie pliku JPK') ?};
   Xml[1].first()
|| {? _a=1
   || AN.index('WALSYM'); AN.prefix(FINFO.NAROD);
   {? AN.first()
   || {? exec('kr_an_ok','jpk')
      || 1
      || exec('kr_zois_next','jpk')
      ?}
   ?}
   ?}
?}


\kr_zois_next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formuła na next petli dla elementu ZOIS JPK_KR
::  OLD: \kr_zois_next/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
{? var_press('Xml')>0
|| XmlProg+=1;
   {? Xml[1].size()>0 || progress(100*XmlProg/Xml[1].size(),'ZOiS','Generowanie pliku JPK') ?};
   _ok:=Xml[1].next()
|| {!
   |? {? AN.next()
   || {? exec('kr_an_ok','jpk')
      || _ok:=1;
         echo('JPK_KR '+OKRO_F.NAZ+' '+ROK_F.NAZ+': '+AN.SYM);
         0
      || 1
      ?}
   ?}
   !}
?};
_ok

\kr_an_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy konto powinno trafic do JPK
::  OLD: \kr_an_ok/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
1


\kr_zois_el
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wartosc elementu sekcji ZOIS JPK_KR
::  OLD: \kr_zois_el/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? ISTDEFS.OPIS='OpisKonta'
|| exec('op_konta','konto',AN.SYM,null,2);
   {? ~KS.KS_ZEST | ~KS.KS_KAT
   || exec('addkom','jpk','E','Nie wskazano '+{? ~KS.KS_ZEST || 'zestawu kont' || 'kategorii zestawu' ?}+' dla konta '+KS.SYM)
   ?};
   256+exec('kr_ka_opis','jpk')
|? ISTDEFS.OPIS='TypKonta'
|| {? AN.KS().TYP='BL' || 'Bilansowe'
   |? KS.TYP='BW' || 'Wynikowe'
   || 'Pozabilansowe'
   ?}
|? ISTDEFS.OPIS='BilansOtwarciaWinien'
|| F.Saldo(0);
   form(F.wn,,2,'9.')
|? ISTDEFS.OPIS='BilansOtwarciaMa'
|| form(F.ma,,2,'9.')
|? ISTDEFS.OPIS='ObrotyWinien'
|| F.Obr1(OKRO_F.NR,OKRO_F.NR);
   form(F.wn,,2,'9.')
|? ISTDEFS.OPIS='ObrotyMa'
|| form(F.ma,,2,'9.')
|? ISTDEFS.OPIS='ObrotyWinienNarast'
|| F.Obr1(0,OKRO_F.NR);
   form(F.wn,,2,'9.')
|? ISTDEFS.OPIS='ObrotyMaNarast'
|| form(F.ma,,2,'9.')
|? ISTDEFS.OPIS='SaldoWinien'
|| F.Saldo(OKRO_F.NR);
   form(F.SWn(F.wn,F.ma),,2,'9.')
|? ISTDEFS.OPIS='SaldoMa'
|| form(F.SMa(F.wn,F.ma),,2,'9.')
|| ''
?}


\kr_ka_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca opis konta na podstawie zmiennej KA
::  OLD: \kr_ka_opis/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_sep:='/';
_t:=KS.NAZ+_sep+
    {? KA.O2<>'' || KA.O2+_sep || '' ?}+
    {? KA.O3<>'' || KA.O3+_sep || '' ?}+
    {? KA.O4<>'' || KA.O4+_sep || '' ?}+
    {? KA.O5<>'' || KA.O5+_sep || '' ?}+
    {? KA.O6<>'' || KA.O6+_sep || '' ?}+
    {? KA.O7<>'' || KA.O7+_sep || '' ?}+
    {? KA.O8<>'' || KA.O8+_sep || '' ?}+
    {? KA.O9<>'' || KA.O9+_sep || '' ?}+
    {? KA.O10<>'' || KA.O10+_sep || '' ?}+
    {? KA.O11<>'' || KA.O11+_sep || '' ?};
_t:=_t-(+_sep);
_t


\kr_dz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na start/stop petli dla elementu Dziennik JPK_KR
::   WE: _a - 1-start 0-stop
::  OLD: \kr_dz/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('Xml')>0
|| XmlProg:=1;
   {? Xml[2].size()>0 || progress(100*XmlProg/Xml[2].size(),'Dziennik','Generowanie pliku JPK') ?};
   Xml[2].first()
|| {? _a=1
   || NrJPK:=0;
   SumaJPK:=0;
   POZ.index('DOK');
   DOK.index('PR1');
   DOK.prefix('T','T');
   DOK.first()
   ?}
?}


\kr_dz_next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formuła na next petli dla elementu Dziennik JPK_KR
::  OLD: \kr_dz_next/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('Xml')>0
|| XmlProg+=1;
   {? Xml[2].size()>0 || progress(100*XmlProg/Xml[2].size(),'Dziennik','Generowanie pliku JPK') ?};
   Xml[2].next()
|| {? DOK.next()
   || echo('JPK_KR '+OKRO_F.NAZ+' '+ROK_F.NAZ+': Dokument '+$NrJPK);
   1
   ?}
?}


\kr_dz_el
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wartosc elementu sekcji Dziennik JPK_KR
::  OLD: \kr_dz_el/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_suma:=0;
POZ.prefix(DOK.ref());
{? POZ.first()
|| {!
   |? {? -POZ.STR='wn' || _suma+=POZ.SUM ?}; POZ.next()
   !}
?};
SumaJPK+=_suma;
form(_suma,,2,'9.')


\kr_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na start/stop petli dla elementu KontoZapis JPK_KR
::   WE: _a - 1-start 0-stop
::  OLD: \kr_kon/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('Xml')>0
|| XmlProg:=1;
   {? Xml[4].size()>0 || progress(100*XmlProg/Xml[4].size(),'KontoZapis','Generowanie pliku JPK') ?};
   Xml[4].first()
|| {? _a=1
   || NrJPK:=0;
   SumaWn:=SumaMa:=0;
   POZ.index('DOK');
   DOK.index('PR1');
   DOK.prefix('T','T');
   {? DOK.first()
   || POZ.prefix(DOK.ref());
      POZ.first()
   ?}
   ?}
?}


\kr_kon_next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formuła na next petli dla elementu KontoZapis JPK_KR
::  OLD: \kr_kon_next/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('Xml')>0
|| XmlProg+=1;
   {? Xml[4].size()>0 || progress(100*XmlProg/Xml[4].size(),'KontoZapis','Generowanie pliku JPK') ?};
   Xml[4].next()
|| {? POZ.next()
   || echo('JPK_KR '+OKRO_F.NAZ+' '+ROK_F.NAZ+': Pozycja '+$NrJPK);
   1
   || {? DOK.next()
   || POZ.prefix(DOK.ref());
      {? POZ.first()
      || 1
      ?}
   ?}
   ?}
?}


\triger
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Triger przed add i put tabeli JPK
::  OLD: \triger/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
JPK.STATOPIS:={? JPK.STAT='B'
              || 'Błąd'
              |? JPK.STAT='T'
              || 'Pobrano UPO'
              |? JPK.STAT='W'
              || 'Wysłano'
              |? JPK.STAT='P'
              || 'Podpisano'
              || 'Nie wysłano'
              ?};
1


\bd_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed usun okna WER tabeli JPK
::  OLD: \bd_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_gr:=JPK.sel_size();
{? JPK.AKC='T'
|| {? ~_gr
   || FUN.info('Plik JPK jest zaakceptowany.'@)
   ?}
|? _gr | FUN.ask('Czy usunąć plik JPK?'@)
|| do();
   JPK_KOM.index('JPK');
   JPK_KOM.prefix(JPK.ref());
   {? JPK_KOM.first() || {! |? JPK_KOM.del !} ?};
   {? JPK.del(,1) & _gr
   || JpkOk+=1
   ?};
   end()
?}


\bgd_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Przed grupowa akcja usun okna WER tabeli JPK
::  OLD: \bgd_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Usunąć zaznaczone pliki JPK?'@)
|| JpkOk:=0;
   JpkSel:=JPK.sel_size();
   1
?}


\agd_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po grupowej akcja usun okna WER tabeli JPK
::  OLD: \agd_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info(
   'Liczba zaznaczonych plików JPK: %1\n'
   'Liczba usuniętych plików JPK: %2.'@[$JpkSel,$JpkOk]
);
VAR_DEL.delete('JpkOk','JpkSel')


\vat_nip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca NIP ze szeczegolow deklaracji VAT dla JPK_VAT
::   WE: [_a] - brak parametru lub typ operacji: 'Z'akup, 'S'przedaz
::  OLD: \vat_nip/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
VAT_PS.NIP:=STR.gsub(VAT_PS.NIP,' ','');
{? 'V7M,V7K'*HELPJPK.RODZAJ
|| _nip:=gsub(VAT_PS.NIP,'-','');
   KRAJE.index('KRAJE');
   KRAJE.prefix(2+_nip,);
   {? KRAJE.first()
   || VAT_PS.NIP:=2-_nip
   ?}
?};
_nk:=VAT_PS.SYM_ZEW;
_kom:='';
{? _nk<>''
|| _rej:=VAT_PS.REJ_VAT;
   _kom:=': symbol dok. '+_nk+', rejestr VAT: '+_rej
?};
{? ~_
|| {? VAT_PS.KH=''
   || exec('addkom','jpk','W','Brak wypełnionej nazwy kontrahenta JPK/GrupaZakup/ZakupWiersz/NazwaWystawcy uzupełniono braki wyrażeniem \'brak nazwy wystawcy\''+_kom)
   ?};
   {? 3+VAT_DEK.TYP<>'V7M' & 3+VAT_DEK.TYP<>'V7K' & VAT_PS.ADRES=''
   || exec('addkom','jpk','W','Brak wypełnionego adresu kontrahenta JPK/GrupaZakup/ZakupWiersz/AdresWystawcy uzupełniono braki wyrażeniem \'brak adresu wystawcy\''+_kom)
   ?};
   {? VAT_PS.NIP='' | VAT_PS.NIP ='braknip'
   || exec('addkom','jpk','W','Brak wypełnionego NIP-u kontrahenta JPK/GrupaZakup/ZakupWiersz/NrIdWystawcy uzupełniono braki wyrażeniem \'brak\''+_kom);
      'BRAK'
   |? +VAT_PS.NIP<3
   || VAT_PS.NIP
   |? (15*'-')*VAT_PS.NIP
   || exec('addkom','jpk','W','Brak wypełnionego NIP-u kontrahenta JPK/GrupaZakup/ZakupWiersz/NrIdWystawcy uzupełniono braki wyrażeniem \'brak\''+_kom);
      'BRAK'
   || _nip:=STR.gsub(VAT_PS.NIP,'-','');
      STR.gsub(_nip,' ','')
   ?}
|? _a='Z'
|| {? VAT_PS.KH=''
   || exec('addkom','jpk','W','Brak wypełnionej nazwy kontrahenta JPK/GrupaZakup/NazwaDostawcy uzupełniono braki wyrażeniem \'brak nazwy dostawcy\''+_kom)
   ?};
   {? 3+VAT_DEK.TYP<>'V7M' & 3+VAT_DEK.TYP<>'V7K' & VAT_PS.ADRES=''
   || exec('addkom','jpk','W','Brak wypełnionego adresu kontrahenta JPK/GrupaZakup/AdresDostawcy uzupełniono braki wyrażeniem \'brak adresu dostawcy\''+_kom)
   ?};
   {? VAT_PS.NIP='' | VAT_PS.NIP ='braknip'
   || exec('addkom','jpk','W','Brak wypełnionego NIP-u kontrahenta JPK/GrupaZakup/NrDostawcy uzupełniono braki wyrażeniem \'brak\''+_kom);
      'BRAK'
   |? +VAT_PS.NIP<3
   || VAT_PS.NIP
   |? (15*'-')*VAT_PS.NIP
   || exec('addkom','jpk','W','Brak wypełnionego NIP-u kontrahenta JPK/GrupaZakup/NrDostawcy uzupełniono braki wyrażeniem \'brak\''+_kom);
      'BRAK'
   || _nip:=STR.gsub(VAT_PS.NIP,'-','');
      STR.gsub(_nip,' ','')
   ?}
|? _a='S'
|| {? VAT_PS.KH=''
   || exec('addkom','jpk','W','Brak wypełnionej nazwy kontrahenta JPK/GrupaSprzedaż/NazwaKontrahenta uzupełniono braki wyrażeniem \'brak nazwy kontrahenta\''+_kom)
   ?};
   {? 3+VAT_DEK.TYP<>'V7M' & 3+VAT_DEK.TYP<>'V7K' & VAT_PS.ADRES=''
   || exec('addkom','jpk','W','Brak wypełnionego adresu kontrahenta JPK/GrupaSprzedaż/AdresKontrahenta uzupełniono braki wyrażeniem \'brak adresu kontrahenta\''+_kom)
   ?};
   {? VAT_PS.NIP='' | VAT_PS.NIP ='braknip'
   || exec('addkom','jpk','W','Brak wypełnionego NIP-u kontrahenta JPK/GrupaSprzedaż/NrKontrahenta uzupełniono braki wyrażeniem \'brak\''+_kom);
      'BRAK'
   |? +VAT_PS.NIP<3
   || VAT_PS.NIP
   |? (15*'-')*VAT_PS.NIP
   || exec('addkom','jpk','W','Brak wypełnionego NIP-u kontrahenta JPK/GrupaSprzedaż/NrKontrahenta uzupełniono braki wyrażeniem \'brak\''+_kom);
      'BRAK'
   || _nip:=STR.gsub(VAT_PS.NIP,'-','');
      STR.gsub(_nip,' ','')
   ?}
?}


\ar_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Po redakcji okna RED zmiennej HELPJPK
::  OLD: \ar_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_r:=__CHK.record2(HELPJPK,'RODZAJ','Rodzaj');
{? _r=''
|| {? 'VAT,V7M,V7K'*HELPJPK.RODZAJ
   || {? HELPJPK.ISTDEF=null
      || FUN.info('Brak schematu dla pliku %1.'@['JPK_'+HELPJPK.RODZAJ]);
         _r:='RODZAJ'
      ?}
   || _r:=__CHK.record2(HELPJPK,'ISTDEF','Schemat')
   ?}
?};
{? _r=''
|| {? HELPJPK.OKR_TYP='O'
   || {? HELPJPK.RODZAJ='SF'
      || _r:=__CHK.record(HELPJPK,,'ROK_DO','OKR_DO')
      || _r:=__CHK.record(HELPJPK,,'ROK_OD','OKR_OD','ROK_DO','OKR_DO')
      ?}
   |? HELPJPK.OKR_TYP='D'
   || _r:=__CHK.record(HELPJPK,,'DATA_OD','DATA_DO')
   ?}
?};
{? _r=''
|| {? HELPJPK.OKR_TYP='O'
   || {? 'VAT,V7M,V7K'*HELPJPK.RODZAJ
      || {? HELPJPK.OKR_OD().POCZ=date(0,0,0)
         || FUN.info('Wybierz inny okres niż Bilans otwarcia i Bilans zamknięcia.'@);
            _r:='OKR_OD'
         |? 'V7M,V7K'*HELPJPK.RODZAJ & HELPJPK.OKR_OD().POCZ<date(2020,7,1)
         || FUN.info('Plik JPK_'+HELPJPK.RODZAJ+' obowiązuje od lipca 2020 roku.');
            _r:='OKR_OD'
         |? ~(_nr:=exec('vat7_nr','jpk'); exec('is_vat7','jpk',_nr,{? HELPJPK.RODZAJ='VAT' || 'VAT7' || HELPJPK.RODZAJ ?}))
         || _typ:={? HELPJPK.KOR || 'korekty' || '' ?};
            _rodz:={? HELPJPK.RODZAJ='VAT' || 'VAT-7' || HELPJPK.RODZAJ ?};
            FUN.info('Brak %1 deklaracji %2 (%3) dla okresu %4.'@[_typ,_rodz,$_nr,OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
            _r:='ROK_OD'
         ?}
      |? HELPJPK.ROK_OD=HELPJPK.ROK_DO & HELPJPK.OKR_OD().NR>HELPJPK.OKR_DO().NR |
         HELPJPK.ROK_OD().POCZ_ROK>HELPJPK.ROK_DO().POCZ_ROK
      || FUN.info('Okres od późniejszy niż okres do.'@);
         _r:={? HELPJPK.RODZAJ='SF' || 'ROK_DO' || 'ROK_OD' ?}
      ?}
   |? HELPJPK.OKR_TYP='D'
   || {? HELPJPK.DATA_OD>HELPJPK.DATA_DO
      || FUN.info('Data od późniejsza niż data do.'@);
         _r:='DATA_DO'
      ?}
   ?}
?};
{? _r='' & HELPJPK.OKR_TYP='D'
|| OKRO_F.index('FIRMA_NR');
   OKRO_F.prefix(REF.FIRMA);
   {? OKRO_F.first() & (OKRO_F.POCZ<>date(0,0,0) | OKRO_F.next())
   || {? HELPJPK.DATA_OD<OKRO_F.POCZ
      || FUN.info('Data od mniejsza od daty początkowej\npierwszego okresu w systemie (%1).'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
         HELPJPK.DATA_OD:=OKRO_F.POCZ;
         _r:='DATA_OD'
      ?}
   ?};
   {? _r='' & OKRO_F.last() & (OKRO_F.KON<>date(0,0,0) | OKRO_F.prev())
   || {? HELPJPK.DATA_DO>OKRO_F.KON
      || FUN.info('Data do późniejsza od daty końcowej\nostatniego okresu w systemie (%1).'@[OKRO_F.NAZ+' '+OKRO_F.ROK().NAZ]);
         HELPJPK.DATA_DO:=OKRO_F.KON;
         _r:='DATA_DO'
      ?}
   ?}
?};
{? _r='' & HELPJPK.RODZAJ='SF' & var_press('JpkXml')<=0
|| _r:=exec('chk','jpk_sf')
?};
{? _r='' & HELPJPK.RODZAJ='FA' & JPK.GEN='T'
|| _r:=__CHK.record(HELPJPK,,'WAL')
?};
{? _r='' & 'VAT,V7M,V7K,GV'*HELPJPK.RODZAJ
||  _dat:={? HELPJPK.OKR_OD || HELPJPK.OKR_OD().POCZ || HELPJPK.DATA_OD ?};
    {? _dat>=date(2018,1,1) | HELPJPK.KOR & HELPJPK.ISTDEF().DATA>=date(2018,1,1)
    || {? HELPJPK.EMAIL<>''
       || _dl:=+HELPJPK.EMAIL;
          {? _dl<5 | _dl>50
          || FUN.info('Adres e-mail powinien zawierać od 5 do 50 znaków\nlub pozostać niewypełniony.'@);
             _r:='EMAIL'
          |? ~exec('mail_ok','#email',HELPJPK.EMAIL)
          || FUN.info('Nieprawidłowy adres e-mail.'@);
             _r:='EMAIL'
          ?}
       |? 2+HELPJPK.RODZAJ='V7' | 2+HELPJPK.RODZAJ='GV'
       || FUN.info('Nie podano adresu e-mail.'@);
          _r:='EMAIL'
       || _r:=exec('chk_jpk_lp','jpk',REF.FIRMA,_dat,HELPJPK.RODZAJ,HELPJPK.LP)
       ?}
    ?}
?};
{? _r='' & HELPJPK.KOR & 2+HELPJPK.RODZAJ='V7' & var_pres('KOR_D',HELPJPK)>0
|| {? HELPJPK.KOR_D=0 & HELPJPK.KOR_E=0
   || FUN.info('Należy wskazać, która część JPK_V7 ma być korygowana.'@);
      _r:='KOR'
   ?}
?};
{? _r='' & var_press('DATE',HELPJPK)>0
|| _r:=__CHK.record2(HELPJPK,'DATE','Data sporządzenia')
?};
_r


\wb_wiersz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na start/stop petli dla elementu WyciagWiersz JPK_WB
::   WE: _a - 1-start 0-stop
::  OLD: \wb_wiersz/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('Xml')>0
|| XmlProg:=1;
   {? Xml[2].size()>0 || progress(100*XmlProg/Xml[2].size(),'WyciagWiersz','Generowanie pliku JPK') ?};
   Xml[2].first()
|| {? _a=1
   || NrJPK:=0;
   WbPw:=0;
   TabDok:=sql(
      'select DOP, REFERENCE as REF from DOK '+
      'where DOK_REJ in (select REF from :_a ) '+
      'and ZP=\'T\' and DTW between to_date(:_b) and to_date(:_c) order by 1',TabRej2,DataOd,DataDo
   );
   {? var_press('PwInd')<=0
   || PwInd:=PW.ndx_tmp(,1,'REJ',,,'NRD',,,'NRD',,)
   ?};
   JpkDane:=1;
   {? TabDok.first()
   || DOK.prefix();
      {? DOK.seek(BIT.sqlint(TabDok.REF),)
      || WbPw:=2+DOK.DOKZRODL='pw';
         {? WbPw
         || PW.use(6+DOK.DOKZRODL);
            PW.index(PwInd);
            _nrd:=$DOK.NR+'/'+$(DOK.DTW~2);
            PW.prefix(DOK.REJ,_nrd,_nrd);

            {? PW.first()
            || 1
            || exec('wb_next','jpk')
            ?}
         || POZ.index('DOK');
            POZ.prefix(DOK.ref());
            {? POZ.first()
            || {? POZ.KON<>DOK_REJ.JPK_AN
               || 1
               || exec('wb_next','jpk')
               ?}
            ?}
         ?}
      ?}
   || JpkDane:=0; 1
   ?}
   || VAR_DEL.delete('TabDok');
   VAR_DEL.delete('JpkDane');
   1
   ?}
?}


\wb_next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formuła na next petli dla elementu WyciagWiersz JPK_WB
::  OLD: \wb_next/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('Xml')>0
|| XmlProg+=1;
   {? Xml[2].size()>0 || progress(100*XmlProg/Xml[2].size(),'WyciagWiersz','Generowanie pliku JPK') ?};
   Xml[2].next()
|| _ok:=JpkDane;
   {? _ok
   || {!
   |? {? WbPw=0 & POZ.next()
      || {? POZ.KON<>DOK_REJ.JPK_AN
         || echo('JPK_WB '+$DataOd+' '+$DataDo+': pozycja wyciągu '+$NrJPK);
            0
         || 1
         ?}
      |? WbPw=1 & PW.next()
      || 0
      || {? TabDok.next()
         || {? DOK.seek(BIT.sqlint(TabDok.REF),)
            || WbPw:=2+DOK.DOKZRODL='pw';
               {? WbPw
               || PW.use(6+DOK.DOKZRODL);
                  PW.index(PwInd);
                  _nrd:=$DOK.NR+'/'+$(DOK.DTW~2);
                  PW.prefix(DOK.REJ,_nrd,_nrd);

                  {? PW.first()
                  || 0
                  || 1
                  ?}
               || POZ.index('DOK');
                  POZ.prefix(DOK.ref());
                  {? POZ.first()
                  || {? POZ.KON<>DOK_REJ.JPK_AN
                     || 0
                     || 1
                     ?}
                  ?}
               ?}
            || 1
            ?}
         || _ok:=0
         ?}
      ?}
   !}
   ?};
   _ok
?}


\wb_salda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Liczy saldo poczatkowe i koncowe wyciagu
::  OLD: \wb_salda/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
Saldo:=SumaP:=SumaK:=SumaWn:=SumaMa:=0;
AN.index('WALSYM');
AN.prefix(HELPJPK.WAL,DOK_REJ.JPK_AN,);
{? AN.first()
|| _wal:=SSTALE.WAL;
   SSTALE.WAL:=HELPJPK.WAL;
   OKRO_F.index('KON'); OKRO_F.prefix(REF.FIRMA);
   _o1:=_o2:=-1;
   _o1:=0;
   {? OKRO_F.find_ge(DataOd) & OKRO_F.KOD='111111111111111111111110'
   || _o2:=0;
      OKRO_F.KON:=date(DataOd~1,(DataOd~2)-1,0)
   |? OKRO_F.find_ge(date(DataOd~1,(DataOd~2)-1,0))
   || _o2:=OKRO_F.NR
   ?};
   {? _o1<>-1 & _o2<>-1
   || KS.index('SYM'); KS.prefix(SSTALE.AR,SSTALE.AR().SYNT+DOK_REJ.JPK_AN,);
      {? KS.first()
      || AN.index('WALSYM'); AN.prefix(SSTALE.WAL,DOK_REJ.JPK_AN,);
         {? AN.first()
         || F.Obr1(_o1,_o2);
            Saldo:=SumaP:=F.wn-F.ma
         ?}
      ?};
      {? DataOd-1>=OKRO_F.KON+1
      || F.DObr2(DOK_REJ.JPK_AN,OKRO_F.KON+1,DataOd-1,1);
         _r:=F.dwn-F.dma;
         Saldo+=_r;
         SumaP+=_r
      ?}
   ?};
   F.DObr2(DOK_REJ.JPK_AN,DataOd,DataDo,1);
   SSTALE.WAL:=_wal;
   SumaK:=SumaP+F.dwn-F.dma
|| exec('addkom','jpk','W','Brak zapisów. Konto '+DOK_REJ.JPK_AN+' nie zostało otwarte.')
?};
~~


\wb_nazwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca nazwe podmiotu wyciagu bankowego na podstawie konta analitycznego pozycji dokumentu
::  OLD: \wb_nazwa/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JpkDane
|| {? WbPw
   || {? form(PW.KONTR)<>'' || form(PW.KONTR) || PW.BL().NB ?}
   || _wy:='';
      _ks:=SSTALE.AR().SYNT+POZ.KON;
      _konto:=ROK_F.SYNT-POZ.KON;
      KS.index('SYM'); KS.prefix(ROK_F.ref(),_ks,);
      {? KS.first()
      || BUD.index('KS'); BUD.prefix(KS.ref());
         {? BUD.first()
         || {!
            |? _konto:=1-_konto;
               {? BUD.SLU().SLU().WZ<>'prosty'
               || SLO.index('SL');
                  SLO.prefix(SLU.ref(),SLU.DL+_konto,);
                  {? SLO.first()
                  || _wy:=SLO.TR
                  ?}
               ?};
               _konto:=SLU.DL-_konto;
               _wy='' & BUD.next()
            !}
         ?}
      ?};
      {? var_press('NoKom')<=0 & BrakOpisu=0 & POZ.OP=''
      || BrakOpisu:=1;
         exec('addkom','jpk','W','Użyto \'brak\' w opisie operacji')
      ?};
      {? _wy=''
      || _rb:=exec('rb_nosp','rachunki',POZ.DOK().DOK_REJ().JPK_RB);
         SKID_RBK.cntx_psh();
         SKID_RBK.index('NUMER');
         SKID_RBK.prefix(_rb,_rb);
         {? SKID_RBK.first
         || _wy:=SKID_RBK.BANK().NB
         ?};
         SKID_RBK.cntx_pop()
      ?};
      {? _wy=''
      || exec('addkom','jpk','W','Użyto nazwy podmiotu \'brak\' dla zapisu konta '+POZ.KON);
         'Brak'
      || _wy
      ?}
   ?}
|| 'Brak'
?}


\wb_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca opis operacji wyciagu bankowego
::  OLD: \wb_opis/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JpkDane
|| {? WbPw
   || {? PW.TYTOP<>'' || PW.TYTOP |? PW.TYPTRAN<>'' || PW.TYPTRAN || 'brak opisu' ?}
   || {? POZ.OP<>'' || POZ.OP || 'brak' ?}
   ?}
|| 'brak'
?}


\wb_kwota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca kwote operacji bankowej, uaktualnia Saldo wyciagu oraz sume obciazen i uznan
::  OLD: \wb_kwota/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JpkDane
|| _wal:=HELPJPK.WAL<>FINFO.NAROD;
   {? WbPw
   || _kwota:=PW.KW;
      _kw:={? -PW.STR='wn' || -_kwota || _kwota ?};
      {? -PW.STR='wn'
      || SumaWn+=_kwota
      || SumaMa+=_kwota
      ?}
   || _kwota:={? _wal || POZ.SUMW || POZ.SUM ?};
      _kw:={? -POZ.STR='wn' || -_kwota || _kwota ?};
      {? -POZ.STR='wn'
      || SumaWn+=_kwota
      || SumaMa+=_kwota
      ?}
   ?}
|| _kw:=Saldo:=0
?};
Saldo+=_kw;
form(_kw,,2,'9.')


\wb_ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca ref i crc wyciagu bankowego
::  OLD: \wb_ref/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JpkDane
|| {? WbPw
   || exec('ref','jpk','PW')
   || exec('ref','jpk','POZ')
   ?}
|| ''
?}


\vat_gr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy istnieja wiersze pliku JPK danego typu
::   WE: _a - typ operacji: 'Z'akup, 'S'przedaz
::  OLD: \vat_gr/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('VatHT')<=0
|| VatHT:=exec('create_set','blp','STRING[50]')
?};
{? PAR_SKID.get(86)='T'
|| {? var_press('VatTab')<=0
   || VatTab:=exec('vat_tab','jpk')
   ?};
   VAT_PS.prefix();
   {? JPK.TYP='V7K' & 1+(VAT_DEK.OKRES+2)='/'
   || _kw:=#(VAT_DEK.OKRES+1);
      _first:=VatTab.first(_a,_kw*3)
   || _first:=VatTab.first(_a)
   ?};
   {? _first
   || 1
   |? _a='Z'
   || exec('vat_clean','jpk');
      0
   ?}
|| VAT_PS.index('TYP_OPER');
   {? JPK.TYP='V7K' & 1+(VAT_DEK.OKRES+2)='/'
   || _kw:=#(VAT_DEK.OKRES+1);
      VAT_PS.prefix(VAT_DEK.ref(),_a,_kw*3)
   || VAT_PS.prefix(VAT_DEK.ref(),_a)
   ?};
   VAT_PS.first()
?}


\vat_wiersz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na start/stop petli dla elementu WyciagWiersz JPK_VAT
::   WE: _a - 1-start 0-stop
::       _b - typ operacji
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| NrJPK:=0;
   SumaVat:=0;
   VatTyp:=_b;
   {? exec('vat_gr','jpk',_b)
   || {? _b='Z' || exec('blp','jpk') ?};
      1
   ?}
|? _b='Z'
|| exec('vat_clean','jpk')
?}


\vat_clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42]
:: OPIS: Usuwa zmienne po utworzeniu pliku JPK_VAT
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('VatTab','VatHT','VatTyp')


\vat_next
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formuła na next petli dla elementu WyciagWiersz JPK_WB
::   WE:
::   WY:
::  OLD: \vat_next/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
echo('JPK_VAT '+OKRO_F.NAZ+' '+ROK_F.NAZ+' - Dokument: '+$NrJPK);
{? var_press('VatTab')>0
|| _ok:=VatTab.next()
|| _ok:=VAT_PS.next()
?};
{? _ok & (var_pres('VatTyp')<=0 | VatTyp='Z')
|| exec('blp','jpk')
?};
_ok


\vat_kw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wartosc kwoty dla JPK_VAT
::   WE: _a - mnożnik do sumy VAT
::  OLD: \vat_kw/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_nr:=#(2-ISTDEFS.OPIS);
{? var_press('VatTab')>0
|| _kw:=VatTab.get(_nr);
   {? _a & VatTab.VAT
   || SumaVat+=VatTab.VAT*_a
   ?};
   {? _kw | ISTDEFS.WYM='T'
   || STR.gsub($_kw,',','.')
   || ''
   ?}
|? VAT_PS.VAT_POZ().POZ1=_nr
|| _kw:={? exec('vat_pomin','jpk_v',1)=0 || VAT_PS.NETTO ?};
   {? _kw | ISTDEFS.WYM='T'
   || STR.gsub($_kw,',','.')
   || ''
   ?}
|? VAT_POZ.POZ2=_nr
|| {? _a & exec('vat_pomin','jpk_v',2)=0
   || SumaVat+=VAT_PS.VAT*_a
   ?};
   _kw:={? exec('vat_pomin','jpk_v',1)=0 || VAT_PS.VAT ?};
   {? _kw | ISTDEFS.WYM='T'
   || STR.gsub($_kw,',','.')
   || ''
   ?}
|| ''
?}


\valid_nip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Walidacja pola NIP
::   WE: _a - NIP do walidacji
::  OLD: \valid_nip/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('JPK_GTU',MGR)>0 & 'V7M,V7K'*HELPJPK.RODZAJ
|| VAT_PS.cntx_psh();
   VAT_PS.get();
   _nip:=gsub(gsub(VAT_PS.NIP,' ',''),'-','');
   {? #(2+_nip)=0 & 2-_nip=_a
   || _a:=VAT_PS.NIP
   ?};
   VAT_PS.cntx_pop()
?};
{? +_a=10 & #(2+_a)<>0
|| exec('nip_ok','#id',_a,1)
|| 1
?}


\fa_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na start/stop petli dla elementu FakturaWiersz JPK_FA
::   WE: _a - 1-start 0-stop
::  OLD: \fa_poz/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| exec('fa_poz_i','jpk');
   _ok:=1; JpkDane:=1;
   {? var_pres('FaVer')>0 & FaVer>2
   || TabDok.index(TabDok2);
      TabDok.prefix(0)
   || TabDok.index(TabDok1);
      TabDok.prefix()
   ?};
   {!
   |? {? TabDok.first()
      || {? DOK.name()<>8+TabDok.REF || DOK.use(8+TabDok.REF); VPOZ.use('pozv'+((8+TabDok.REF)+4)); POZF.use('pozf'+((8+TabDok.REF)+4)) ?};
         DOK.prefix();
         {? DOK.seek(BIT.sqlint(TabDok.REF),)
         || {? (_jest:=exec('fa_poz1','jpk'))>0
            || exec('fa_p_ele','jpk');
               0
            || {? _jest=0
               || exec('addkom','jpk','W','Brak pozycji faktury: '+DOK.ODD().OD+' '+DOK.REJ().KOD+' '+$DOK.NR+': '+DOK.NK)
               ?};
               FaPozT:='';
               JpkDane:=exec('fa_poz_n','jpk');  _ok:=1;
               {? ~JpkDane || NrJPK+=1 ?};
               0
             ?}
         || exec('fa_poz_n','jpk')
         ?}
      ||  _ok:=1; JpkDane:=0; {? ~JpkDane || NrJPK+=1 ?}; 0
      ?}
   !};
   _ok
|| {? var_pres('FaVer')<=0 | FaVer<3
   || VAR_DEL.delete('JpkDane','TabDok','TabDok1','TabDok2')
   ?};
   1
?}


\fa_poz1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Ustawia pierwsza pozycje faktury
::  OLD: \fa_poz1/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
FaZal:=DOK.DOK_REJ().JPK_FA_R='Z';
{? DOK.DOK_REJ().JPK_FA_P='T'
|| FaPozT:='F';
   CzyFaKor:=0;
   CenaB:=DOK_REJ.TYP_CENY='B';
   POZF.index('DOK'); POZF.prefix(DOK.ref());
   POZF.first()
|| FaPozT:='L';
   CzyFaKor:=0;
   _sys:={? +DOK.ZAR & DOK.ZAR*':' || (DOK.ZAR*':'-1)+DOK.ZAR || '' ?};
   {? _sys='LSP' | _sys='LZK'
   || {? 1+DOK.DOKZRODL='D'
         | 1+DOK.DOKZRODL='K'
      || _maska:=3+(1-DOK.DOKZRODL);
         FAKS.use('faktu'+_maska);
         FAP.use('fakpo'+_maska);
         FAKS.prefix();
         _reffak:=#(4-DOK.DOKZRODL);
         {? FAKS.seek(_reffak,)
         || CzyFaKor:=FAKS.T().KOR<>'N';
            CenaB:=FAKS.T().FIS='T';
            ST.ODDZ:='_'; ST.AR:=date()~1; ST.AM:=date()~2;
            exec('upr_set','ceny');
            {? FAKS.T().KOR='Z'
            || FAP.index('KZ'); FAP.prefix($FAKS.ref())
            || FAP.index('FAP'); FAP.prefix(FAKS.ref())
            ?};
            FAP.first()
         ?}
      || -1
      ?}
   || -1
   ?}
?}


\fa_poz_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formuła na next petli dla elementu FakturaWiersz JPK_FA
::  OLD: \fa_poz_n/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=JpkDane;
{? _ok
|| {!
   |? {? FaPozT='F' & POZF.next() | FaPozT='L' & FAP.next()
      || exec('fa_p_ele','jpk');
         0
      || {!
         |? {? TabDok.next()
            || {? DOK.name()<>8+TabDok.REF || DOK.use(8+TabDok.REF); VPOZ.use('pozv'+((8+TabDok.REF)+4)); POZF.use('pozf'+((8+TabDok.REF)+4)) ?};
               DOK.prefix();
               {? DOK.seek(BIT.sqlint(TabDok.REF),)
               || {? (_jest:=exec('fa_poz1','jpk'))>0
                  || exec('fa_p_ele','jpk');
                     0
                  || {? _jest=0
                     || exec('addkom','jpk','W','Brak pozycji faktury: '+DOK.ODD().OD+' '+DOK.REJ().KOD+' '+$DOK.NR+': '+DOK.NK)
                     ?};
                     1
                  ?}
               || 1
               ?}
            || _ok:=0
            ?}
         !};
         0
      ?}
   !}
?};
_ok


\fa_p_ele
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Pozycja faktury
::   WE: [_a] - zwiększać licznik? [1]-tak 0-nie
::  OLD: \fa_p_ele/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<=0 | _a
|| NrJPK+=1
?};
echo('JPK_FA '+OKRO_F.NAZ+' '+ROK_F.NAZ+' - Pozycja: '+$NrJPK);
{? FaPozT='F'
|| {? TabDok.KN='2' || POZF.WN:=-POZF.WN; POZF.IL:=-POZF.IL; POZF.C:=-POZF.C ?};
   FaPoz[1]:=POZF.NAZ;
   FaPoz[2]:=POZF.JM().KOD;
   FaPoz[3]:=form(POZF.IL,,0,'9.');
   FaPoz[4]:={? CenaB || '' || form(POZF.C,,2,'9.') ?};
   FaPoz[5]:={? CenaB || form(POZF.C,,2,'9.') || '' ?};
   FaPoz[6]:={? POZF.RABAT || form(POZF.RABAT,,2,'9.') || '' ?};
   FaPoz[7]:={? CenaB  & FaZal=0 || '' || SumaP+=POZF.WN; form(POZF.WN,,2,'9.') ?};
   FaPoz[8]:={? CenaB || form(POZF.WB,,2,'9.') || '' ?};
   FaPoz[9]:=exec('fa_vat','jpk',POZF.SV().KOD);
   FaPoz[10]:=exec('ref','jpk','POZF');
   FaPoz[11]:=form(POZF.WV,,2,'9.')
|| _rab_w:=FAKS.RAB_TYP='W';
   _rabk:=_rabkp:=0;
   _cena:=_cenap:=0;
   _waluto:=DOK.WAL<>null & DOK.WAL<>FINFO.NAROD;
   {? CzyFaKor
:: Korekta
   || {? FAP.POZP
      || _rabkp:=_cenap:=0
      || FAKS.cntx_psh();
         FAKS.use(ref_name(FAP.FAKS));
         _lksql:=FAP.FAKS().LKSQL;
         FAKS.cntx_pop();
         FAP.cntx_psh();
         FAP.use('fakpo'+(5-form(8+_lksql)));
         FAKS.cntx_psh();
         FAKS.use(form(8+_lksql));
         FAP.index('FAP');
         FAP.prefix(BB.sqlint(_lksql),{? FAP.POZP || FAP.POZP || FAP.POZ ?});
         {? FAP.first()
         || FAP.FAKS();
            exec('wysw_kor','faktury_poz');
            {? _rab_w
            || _rabkp:=FAP.RABK
            || _cenap:=
                  {? _waluto
                  || {? exec('czyrabp','ceny',FAKS.RAB_TYP)
                     || _w7:=
                           {? exec('czyrabp','ceny',FAKS.RAB_TYP)
                           || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)
                           || FAP.RABK+(FAKS.RAB*FKOR.BCWAL/100)$2
                           ?};
                        _rabat:=1-_w7/100;
                        FKOR.BCWAL*_rabat $2
                     || FKOR.BCWAL-FAP.RABK-(FKOR.BCWAL*FAKS.RAB/100)$2
                     ?}
                  || 0
                  ?}
            ?}
         ?};
         FAKS.cntx_pop();
         FAP.cntx_pop()
      ?};
      exec('wysw_kor','faktury_poz');
      {? FAP.POZP
      || {? _rab_w
         || _cena:={? _waluto || FKOR.BCWAL || FKOR.BCPR ?};
            _rabk:=FAP.RABK
         || _cena:=
               {? _waluto
               || {? exec('czyrabp','ceny',FAKS.RAB_TYP)
                  || _w7:=
                        {? exec('czyrabp','ceny',FAKS.RAB_TYP)
                        || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)
                        || FAP.RABK+(FAKS.RAB*FKOR.BCWAL/100)$2
                        ?};
                     _rabat:=1-_w7/100;
                     FKOR.BCWAL*_rabat $2
                  || FKOR.BCWAL-FAP.RABK-(FKOR.BCWAL*FAKS.RAB/100)$2
                  ?}
               || {? CenaB || FKOR.BCB || FKOR.BCN ?}
               ?}
         ?}
      || {? _rab_w
         || _cena:={? _waluto || FAP.CWAL || FAP.CPR ?};
            _rabk:=FAP.RABK
         || _cena:=
               {? _waluto
               || {? exec('czyrabp','ceny',FAKS.RAB_TYP)
                  || _w7:=
                        {? exec('czyrabp','ceny',FAKS.RAB_TYP)
                        || exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)
                        || FAP.RABK+(FAKS.RAB*FKOR.BCWAL/100)$2
                        ?};
                     _rabat:=1-_w7/100;
                     FKOR.BCWAL*_rabat $2
                  || FKOR.BCWAL-FAP.RABK-(FKOR.BCWAL*FAKS.RAB/100)$2
                  ?}
               || {? CenaB || FAP.CB || FAP.CN ?}
               ?}
         ?}
      ?}
:: Faktura
   || {? _rab_w
      || _cena:={? _waluto || FAP.CWAL || FAP.CPR ?};
         _rabk:=FAP.RABK
      || _cena:=
            {? _waluto
            || {? exec('czyrabp','ceny',FAKS.RAB_TYP)
               || _rabat:=1-exec('rab_proc','ceny',FAP.RAB,FAKS.RAB,FAKS.RAB_TYP)/100;
                  (FAP.CWAL*_rabat)$2
               || FAP.CWAL-FAP.RABK-(FAKS.RAB*FAP.CWAL/100)$2
               ?}
            || {? CenaB || FAP.CB || FAP.CN ?}
            ?}
      ?}
   ?};
   {? FAP.NX='' || FAP.NX:=FAP.M().N; FAP.put() ?};
   FaPoz[1]:=FAP.NX;
   FaPoz[2]:=FAP.JM().KOD;
   FaPoz[3]:=form(FAP.IL,,0,'9.');
   FaPoz[4]:={? CenaB || '' || form(_cena-_cenap,,2,'9.') ?};
   FaPoz[5]:={? CenaB || form(_cena-_cenap,,2,'9.') || '' ?};
   FaPoz[6]:={? _rab_w || form(_rabk-_rabkp,,2,'9.') || '' ?};
   FaPoz[7]:={? FaZal || form(FAP.WWAL_P,,2,'9.') |? CenaB || '' || SumaP+=FAP.WWAL; form(FAP.WWAL,,2,'9.') ?};
   FaPoz[8]:={? CenaB || form(FAP.BWAL,,2,'9.') || '' ?};
   FaPoz[9]:=
      {? FAKS.NDVAT='N'
      || {? FAP.SV().KOD=' -zw0' || ''
         |? -SLO.KOD*'zw' || 'zw'
         |? -SLO.KOD*'-o' & var_pres('FaVer')>0 & FaVer>1 || 'oo'
         |? -SLO.KOD*' -' & var_pres('FaVer')>0 & FaVer>1 || 'np'
         || |(SLO.KOD-2)
         ?}
      |? FAKS.NDVAT='T' & var_pres('FaVer')>0 & FaVer>1
      || 'np'
      |? FAKS.NDVAT='O' & var_pres('FaVer')>0 & FaVer>1
      || 'oo'
      || |(FAP.SV().KOD-2)
      ?};
   FaPoz[10]:=exec('ref','jpk','FAP');
   FaPoz[11]:=form(FAP.VWAL_P,,2,'9.')
?}


\fa_kw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wartosc kwoty dla JPK_FA
::   WE: _a - kwota
::  OLD: \fa_kw/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JpkDane & (_a | ISTDEFS.WYM='T')
|| form(_a,,2,'9.')
|| ''
?}


\jpk_mag_t
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy dostepne dokumety magazynowe danego typu
::   WE: _a - typ dokumentu: PZ,WZ,RW,MM
::  OLD: \jpk_mag_t/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
DokTyp:=_a;
NrJPK:=NrJPKp:=0;
Suma:=0;
VAR_DEL.delete('DokMag');
DokMag:=sql(
   'select DOK.DTW, DOK.REFERENCE as REF from DOK join DOK_REJ where '+
   'DOK.ZP=\'T\' and DTW between to_date(:_a) and to_date(:_b) and DOK.MG=\':_c\' and '+
   'DOK_REJ.JPK_TYP=\'M\' and DOK_REJ.TYP_MAG=\':_d\' order by 1',
   DataOd,DataDo,JPK_MAG,_a
);
DokMag.first()


\jpk_mn_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na start/stop petli dla elementu Wartosc JPK_MAG
::   WE: _a - 1-start 0-stop
::  OLD: \jpk_mn_f/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| exec('set_dok','jpk',1)
?}


\jpk_mn_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na next petli dla elementu Wartosc JPK_MAG
::  OLD: \jpk_mn_n/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{!
|? {? DokMag.next()
   || ~exec('set_dok','jpk',1)
   || _ok:=0
   ?}
!};
_ok


\set_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Ustawia dokument ksiegowy
::   WE: _a - ustawiac tablice naglowka
::   WY: Czy udalo sie ustawic: 1-tak 0-nie
::  OLD: \set_dok/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
DOK.prefix();
{? DOK.seek(BIT.sqlint(DokMag.REF),)
|| exec('set_nag','jpk_log',_a)
?}


\jpk_mp_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na start/stop petli dla elementu Wiersz JPK_MAG
::   WE: _a - 1-start 0-stop
::       _b - Czy ustawiac pierwszy element tabeli dokumentow
::  OLD: \jpk_mp_f/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? _b || DokMag.first() ?};
   {? exec('set_dok','jpk',0)
   || DK.use('dokma'+(3+(1-DOK.DOKZRODL)));
      DK.index('DOKMAG'); DK.prefix(ND.ref());
      {? DK.first()
      || 1
      || exec('jpk_mp_n','jpk')
      ?}
   ?}
?}


\jpk_mp_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na next petli dla elementu Wiersz JPK_MAG
::  OLD: \jpk_mp_n/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{!
|? {? DK.next()
   || NrJPKp+=1;
      echo('JPK_MAG - '+JPK_MAG+' - '+OKRO_F.NAZ+' '+ROK_F.NAZ+' - Pozycja: '+$NrJPKp);
      0
   || {? DokMag.next()
      || {? exec('jpk_mp_f','jpk',1,0)
         || 0
         ?}
      || _ok:=0
      ?}
   ?}
!};
_ok


\jpk_tryb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zmienia tryb przesyłania pliku
::  OLD: \jpk_tryb/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JPK.AKC='T'
|| FUN.info('Plik JPK jest zaakceptowany.'@)
|? {? JPK.CYKL='T'
   || FUN.ask('Czy zmienić tryb na przesyłanie doraźnie w ramach kontroli?'@)
   || FUN.ask('Czy zmienić tryb na przesyłanie cykliczne?'@)
   ?}
|| {? JPK.CYKL='T'
   || JPK.CYKL:='N'
   || JPK.CYKL:='T'
   ?};
   JPK.put()
?}


\loop_vat1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Poszukuje deklaracji VAT dla pliku JPK dla okresu
::  OLD: \loop_vat1/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
_nr:=exec('vat7_nr','jpk');
_okres:=$(OKRO_F.POCZ~1)+'/'+form(OKRO_F.POCZ~2,-2);
VAT_DEK.index('VAT_DEKR');
VAT_DEK.prefix('V',OKRO_F.ROK().POCZ_ROK,_okres,'VAT7');
{? VAT_DEK.last() & VAT_DEK.NR().NR=_nr
|| DataOd:=OKRO_F.POCZ;
   DataDo:=OKRO_F.KON;
   _ok:=1
?};
_ok


\fa_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula na start/stop petli dla elementu Faktura JPK_FA
::   WE: _a - 1-start 0-stop
::       _b  - wersja JPK
::  OLD: \fa_nag/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<0 || _arg:=0 || _arg:=_b ?};
{? _a=1
|| {? $FINFO.NAROD=2-JPK.DODAT
   || _wal:=' and (DOK.WAL is null or DOK.WAL=\''+(2-JPK.DODAT)+'\') '
   || _wal:=' and DOK.WAL=\''+(2-JPK.DODAT)+'\' '
   ?};
   VAR_DEL.delete('TabDok');
   {? HELPJPK.OKR_TYP='D'
   || TabDok:=sql(
         'select distinct DOK.DTW, DOK.REFERENCE as REF, DOK.KN as KN, DOK.DSP_WPOZ as DSP, to_date(null) as DOP, '+
         'case when DOK_REJ.JPK_FA_R=\'Z\' then 1 else 0 end as ZAL '+
         'from DOK join DOK_REJ '+
         'where DOK_REJ in (select REF from :_a ) '+
         'and ZP=\'T\' '+_wal+' and DTW between to_date(:_b) and to_date(:_c) order by DTW',
         TabRej2,DataOd,DataDo
      )
   || TabDok:=sql(
         'select distinct DAT1 as DTW, DVAT.DOK as REF, DOK.KN as KN, DOK.DSP_WPOZ as DSP, to_date(null) as DOP, '+
         'case when DOK_REJ.JPK_FA_R=\'Z\' then 1 else 0 end as ZAL '+
         'from DVAT join @DOK join DOK_REJ '+
         'where DOK.DOK_REJ in (select REF from :_a ) '+
         'and DOK.ZP=\'T\' '+_wal+' and DVAT.OBR=\''+$JPK.OKRO_F+'\'  order by DTW',
         TabRej2
         )
   ?};
:: dodanie pozycji faktury korygowanej korektą nagłówkową na minus
   {? TabDok.first()
   || {! |?
         {? 4+TabDok.KN='doku'
         || _maska:=(8+TabDok.KN)+4;
            DOK.use('doku'+_maska);
            DOK.prefix();
            TabDok.cntx_psh();
            {? DOK.seek(BB.sqlint(TabDok.KN),) & DOK.DOK_REJ().KOR_NAG='T' & DOK.ZP='T'
            || TabDok.KN:='2'; TabDok.add()
            ?};
            TabDok.cntx_pop()
         ?};
         {? TabDok.DSP='T' & TabDok.DOP=date(0,0,0)
         || VPOZ.cntx_psh(); DOK.cntx_psh(); _mdok:=(8+TabDok.REF)+4;
            VPOZ.use('pozv'+_mdok); VPOZ.prefix();
            DOK.use('doku'+_mdok); DOK.prefix();
            {? HELPJPK.OKR_TYP='D'
            || _sqldsp:='select distinct VPOZ.D as D from VPOZ where VPOZ.DOK=\':_a\'';
               _dsptab:=sql(_sqldsp,TabDok.REF)
            || _sqldsp:='select distinct VPOZ.D as D from VPOZ '+
                        'where VPOZ.DOK=\':_a\' '+
                        'and (VPOZ.OKRVAT=\':_b\' or VPOZ.OKR_C=\':_b\' or VPOZ.OKR_Z=\':_b\')';
               _dsptab:=sql(_sqldsp,TabDok.REF,$JPK.OKRO_F)
            ?};
            VPOZ.cntx_pop(); DOK.cntx_pop();
            {? _dsptab.first()
            || TabDok.DOP:=_dsptab.D; TabDok.put();
               {? _dsptab.next()
               || {! |?
                     TabDok.cntx_psh();
                     TabDok.DOP:=_dsptab.D; TabDok.add();
                     TabDok.cntx_pop();
                     _dsptab.next()
                  !}
               ?}
            ?}
         ?};
         {? var_pres('_dsptab')>0 || &_dsptab ?};
         TabDok.next()
      !}
   ?};
::   TabDok.win_sel(TabDok.mk_sel(,,1)); TabDok.select();
   TabDok1:=TabDok.index('?');
   TabDok2:=TabDok.ndx_tmp('',1,'ZAL',,0, 'DTW',,0);
   exec('fa_nag_f','jpk',1,0,_arg)
|| exec('fa_nag_f','jpk',0,0,_arg)
?}


\fa_nag_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formuła na next petli dla elementu Faktura JPK_FA
::   WE: _a - wersja JPK FA
::  OLD: \fa_nag_n/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<0 || _arg:=0 || _arg:=_a ?};
_ok:=JpkDane;
{? _ok
|| {!
   |? {? TabDok.next()
      || {? DOK.name()<>8+TabDok.REF || DOK.use(8+TabDok.REF); DOK.prefix(); VPOZ.use('pozv'+((8+TabDok.REF)+4)); POZF.use('pozf'+((8+TabDok.REF)+4)) ?};
         {? DOK.seek(BIT.sqlint(TabDok.REF),8+TabDok.REF)
         || exec('fa_suma','jpk',_arg);
            echo('JPK_FA '+OKRO_F.NAZ+' '+ROK_F.NAZ+' - Faktura: '+$NrJPK);
            0
         || 1
         ?}
      || _ok:=0
      ?}
   !}
?};
_ok


\fa_naz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Nr faktury
::  OLD: \fa_naz/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JpkDane
|| {? DOK.DOK_REJ().JPK_FA_T='S' || DOK.NK || DOK.SYM_ZEW+','+DOK.REJ().KOD+'('+$DOK.NR+')' ?}
|| 'brak'
?}


\fa_ele
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wartosc dla pola Faktury JPK_FA
::  OLD: \fa_ele/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? ~JpkDane & 'P_3A P_3B P_3C P_3D'*ISTDEFS.OPIS
|| return('brak')
?};
{? ~JpkDane & 'P_4B'*ISTDEFS.OPIS
|| return('')
?};
{? ISTDEFS.OPIS='P_3A'
|| {? DOK_REJ.JPK_FA_T='S' || DOK.KH_FULL || XINFO.NAZ ?}
|? ISTDEFS.OPIS='P_3B'
|| {? DOK_REJ.JPK_FA_T='S'
   || exec('adres','jpk',1)
   || exec('adres','jpk',0)
   ?}
|? ISTDEFS.OPIS='P_3C'
|| {? DOK_REJ.JPK_FA_T='S' || XINFO.NAZ || DOK.KH_FULL ?}

|? ISTDEFS.OPIS='P_3D'
|| {? DOK_REJ.JPK_FA_T='S'
   || exec('adres','jpk',0)
   || exec('adres','jpk',1)
   ?}
|? ISTDEFS.OPIS='P_4A'
|| {? ~JpkDane
   || ''
   || {? 1+DOK.RVAT().RVAT().KVAT().SYM='_'
      || {? DOK_REJ.JPK_FA_T='S'
         || 'PL'
         || 2+DOK.NIP
         ?}
      || ''
      ?}
   ?}
|? ISTDEFS.OPIS='P_4B'
|| {? DOK_REJ.JPK_FA_T='S'
   || STR.gsub(XINFO.NIP,'-','')
   || _nip:=STR.gsub(DOK.NIP,'-','');
      _nip:=STR.gsub(_nip,' ','');
      {? JPK.ISTDEF().VER='JPK_FA(1)_v1-0'
      || {? 1+KVAT.SYM='_'
         || _nip:=2-_nip
         ?};
         {? +_nip<>10 || _nip:='' ?}
      || {? 1+KVAT.SYM='_'
         || _nip:=2-_nip;
            {? +_nip<8 & +_nip>12 || _nip:='' ?}
         || {? 2+_nip='PL' & +_nip=12 || _nip:=2-_nip |? +_nip<>10 || _nip:='' ?}
         ?}
      ?};
      _nip
   ?}
|? ISTDEFS.OPIS='P_5A'
|| {? ~JpkDane
   || ''
   || {? 1+KVAT.SYM='_'
      || {? DOK_REJ.JPK_FA_T='Z'
         || 'PL'
         || 2+DOK.NIP
         ?}
      || ''
      ?}
   ?}
|? ISTDEFS.OPIS='P_5B'
|| {? ~JpkDane
   || ''
   || {? DOK_REJ.JPK_FA_T='Z'
      || STR.gsub(XINFO.NIP,'-','')
      || _nip:=STR.gsub(DOK.NIP,'-','');
         _nip:=STR.gsub(_nip,' ','');
         {? JPK.ISTDEF().VER='JPK_FA(1)_v1-0'
         || {? 1+KVAT.SYM='_'
            || _nip:=2-_nip
            ?};
            {? +_nip<>10 || _nip:='' ?}
         || {? 1+KVAT.SYM='_'
            || _nip:=2-_nip;
               {? +_nip<8 & +_nip>12 || _nip:='' ?}
            || {? 2+_nip='PL' & +_nip=12 || _nip:=2-_nip |? +_nip<>10 || _nip:='' ?}
            ?}
         ?};
         _nip
      ?}
   ?}
|? ISTDEFS.OPIS='RodzajFaktury'
|| {? ~JpkDane
   || {? FaVer>2
      || 'VAT'
      || 'POZ'
      ?}
   |? DOK_REJ.JPK_FA_R='K' || 'KOREKTA'
   |? DOK_REJ.JPK_FA_R='Z' || 'ZAL'
   |? DOK_REJ.JPK_FA_R='W' || 'VAT'
   |? DOK_REJ.JPK_FA_R='P' & var_press('FaVer')>0 & FaVer>1
   || exec('addkom','jpk','W','Nieprawidłowy rodzaj faktury: Pozostała. Zmieniono na fakturę VAT.');
      'VAT'
   || 'POZ'
   ?}
|? ISTDEFS.OPIS='PrzyczynaKorekty'
|| VAR_DEL.delete('FaKor');
   FaKor:=obj_new(3);
   FaKor[1]:=FaKor[2]:=FaKor[3]:='';
   _sys:={? +DOK.ZAR & DOK.ZAR*':' || (DOK.ZAR*':'-1)+DOK.ZAR || '' ?};
   {? _sys='LSP' | _sys='LZK'
   || {? 1+DOK.DOKZRODL='D'
         | 1+DOK.DOKZRODL='K'
      || FAKS.cntx_psh();
         FAKS.use('faktu'+(3+(1-DOK.DOKZRODL)));
         FAKS.prefix();
         _reffak:=#(4-DOK.DOKZRODL);
         {? FAKS.seek(_reffak,FAKS.name)
         || FaKor[1]:=FAKS.TZP;
            FaKor[2]:=
               {? FAKS.T().KOR='Z' || 256+exec('kor_dok','jpk_log',FAKS.ref())
               |? FAKS.SZ='Z' || FAKS.KOR_ID
               || FAKS.KOR
               ?};
            FaKor[3]:=
               {? FAKS.KZ_OD<>date(0,0,0)
               || 'Od '+FAKS.KZ_OD$1+' do '+FAKS.KZ_DO$1
               || exec('FindAndGet','#table',FAKS,FAKS.FKSQL,,$"
                     _am:=FAKS.AM; _ar:=FAKS.AR;
                     TYPYSP.cntx_psh();
                     {? FAKS.T().HIS='T' || _am:=FAKS.DW~2; _ar:=FAKS.DW~1 ?};
                     TYPYSP.cntx_pop();
                     (('0'+$_am)+2)+'.'+$_ar
                     ",'')
               ?}
         ?};
         FAKS.cntx_pop()
      ?}
   |? DOK.KOR<>''
   || FaKor[1]:=DOK.KOR_PRZY;
      FaKor[2]:=DOK.KOR_ZEW;
      FaKor[3]:=DOK.KOR_OKR
   ?}
?}


\adres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca adres
::   WE: _a - 1-kontrahenta 0-licencjobiorcy
::  OLD: \adres/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? DOK.UL<>'' || {? 3+(-DOK.UL)<>'ul.' || 'ul. ' || '' ?}+DOK.UL || '' ?}+
   {? DOK.MIASTO<>'' || ', '+DOK.MIASTO || '' ?}+
   {? DOK.POCZ<>'' || ', '+DOK.POCZ || '' ?}
|| {? XINFO.UL<>''
   || 'ul. '+XINFO.UL+' '+
      {? XINFO.NR_L<>''
      || XINFO.NR_D+'/'+XINFO.NR_L
      || XINFO.NR_D
      ?}
   || ''
   ?}+
   {? XINFO.MIA<>''
   || {? XINFO.UL<>'' || ', ' || '' ?}+XINFO.MIA+{? XINFO.UL='' || XINFO.NR_D || '' ?}
   || ''
   ?}+
   {? XINFO.KOD_P<>''
   || ', '+XINFO.KOD_P+' '+{? XINFO.POCZ<>'' || XINFO.POCZ || '' ?}
   |? XINFO.POCZ<>''
   || ', '+XINFO.POCZ
   ?}
?}


\rodzaj_nipu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KS [21.37]
:: OPIS: jaki rodzaj numery identyfikacyjnego użyć
::----------------------------------------------------------------------------------------------------------------------
exec('czytaj','#stalesys',,XINFO);
exec('czytaj','#stalesys',,KST);
_nip:=STR.gsub(XINFO.NIP,'-','');
_nip:=STR.gsub(_nip,' ','');
_ret:='NIP';
{? _nip<>''
|| _ret:='NIP'
|| {? var_press('OSS',KST)>0 & KST.OSS<>''
   || _ret:='OSS'
   || {? var_press('IOSS',KST)>0 & KST.IOSS<>''
      || _ret:='IOSS'
      || exec('addkom','jpk','E','Brak wypełnionego numeru NIP lub OSS lub IOSS w Danych Licencjonobiorcy')
      ?}
   ?}
?};
_ret


\czy_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KS [21.37]
:: OPIS: Czy dokument jest zaliczką
::----------------------------------------------------------------------------------------------------------------------
_ret:='N';
{? var_press('DOK_REJ',DOK)>0 & var_press('ZAL',DOK_REJ)>0
|| _ret:=DOK.DOK_REJ().ZAL
?};
_ret


\kod_kraj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KS [21.37]
:: OPIS: Kraj podatku WAT
::----------------------------------------------------------------------------------------------------------------------
_ret:='PL';
{? var_press('KRAJ',DOK)>0 & DOK.KRAJ().KOD<>''
|| _ret:=DOK.KRAJ().KOD
?};
_ret


\czy_proc_wsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KS [12.51]
:: OPIS: Czy procedura wspólna jest ecomemersowa i jej kod
::----------------------------------------------------------------------------------------------------------------------
_ret:='NO';
VAR_DEL.delete('_dokkod');
{? var_press('PROC',DOK)>0
|| _dokkod:={? type_of(DOK.PROC)=type_of('Test')
            || spli_str(DOK.PROC,',')
            || spli_str(DOK.PROC().KOD,',')
            ?};
   _len:=obj_len(_dokkod);
   _kod:=_dokkod[_len];
   VAR_DEL.delete('_dokkod');
   {? _kod='EE'
   || _ret:='OSS'
   |? _kod='IED'
   || _ret:='OSS'
   |? _kod='WSTO_EE'
   || _ret:='OSS'
   |? _kod='SOTI'
   || _ret:='IOSS'
   || {? var_press('JPK_PROC',DOK)>0
      || _dokjpkkod:={? type_of(DOK.JPK_PROC)=type_of('Test')
                     || spli_str(DOK.JPK_PROC,',')
                     || spli_str(DOK.JPK_PROC().KOD,',')
                     ?};
         _len:=obj_len(_dokjpkkod);
         _kod:=_dokjpkkod[_len];
          VAR_DEL.delete('_dokjpkkod');
         {? _kod='EE'
         || _ret:='OSS'
         |? _kod='IED'
         || _ret:='OSS'
         |? _kod='WSTO_EE'
         || _ret:='OSS'
         |? _kod='SOTI'
         || _ret:='IOSS'
         |? var_press('PROCED',DOK)>0 & DOK.PROCED>0
         || _ret:='OSS'
         ?}
      |? var_press('PROCED',DOK)>0 & DOK.PROCED >0
      || _ret:='OSS'
      ?}
   ?}
|? var_press('PROCED',DOK)>0 & DOK.PROCED>0
|| _ret:='OSS'
?};
_ret


\fa_suma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Suma pozycji VAT dokumentu
::   WE: _a - wersja JPK
::  OLD: \fa_suma/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
NrJPK+=1;
sFN23:=sFV23:=sFVW23:=0;
sFN8:=sFV8:=sFVW8:=0;
sFN5:=sFV5:=sFVW5:=0;
sFN0:=sFV0:=0;
sFNZ:=0;
sFB:=0;
sFV:=0;
sOO:=0;
sNO:=0;
Mpp:=DOK.SP_WYM='T';
NrFaZal:=exec('fa_sym_zal','jpk');
{? TabDok.DSP<>'T'
|| VPOZ.index('VDPR');
   VPOZ.prefix(DOK.ref())
|| VPOZ.index('VDSP');
   VPOZ.prefix(DOK.ref(),TabDok.DOP)
?};
{? JpkDane & VPOZ.first()
|| _wal:=DOK.WAL<>null & DOK.WAL<>FINFO.NAROD & var_pres('NETTO_W',VPOZ)>0 & var_pres('FaVer')>0 & FaVer>2;
   {!
   |? {? HELPJPK.OKR_TYP='O' & (VPOZ.OKRVAT=JPK.OKRO_F | VPOZ.OKR_C=JPK.OKRO_F | VPOZ.OKR_Z=JPK.OKRO_F)
         | HELPJPK.OKR_TYP<>'O' & DOK.DTW>=JPK.OD & DOK.DTW<=JPK.DO
      || _pr:=3+|VPOZ.PR().KOD;
         _netto:={? _wal || VPOZ.NETTO_W || VPOZ.NETTO ?};
         _vat:={? _wal || VPOZ.VAT_W || VPOZ.VAT ?};
         _brutto:={? _wal || VPOZ.BRUTTO_W || VPOZ.BRUTTO ?};
         {? _pr='22 ' | _pr='23 '
         || sFN23+=_netto;
            sFV23+=_vat;
            {? _wal || sFVW23+=VPOZ.VAT ?}
         |? _pr='8 %' | _pr='7 %'
         || sFN8+=_netto;
            sFV8+=_vat;
            {? _wal || sFVW8+=VPOZ.VAT ?}
         |? _pr='5 %'
         || sFN5+=_netto;
            sFV5+=_vat;
            {? _wal || sFVW5+=VPOZ.VAT ?}
         |? _pr='0 %'
         || sFN0+=_netto
         |? _pr='Zwo'
         || sFNZ+=_netto
         ?};
         {? VPOZ.RVAT().RVAT().KVAT().SYM='SprzNOp' & 7+VPOZ.GRVAT().KOD='SprPDos'
            & (VPOZ.PR().KOD=' -' | VPOZ.PR().KOD=' -o')
            | KVAT.SYM='ZakOpod' & VPOZ.GRVAT().KOD='SprPDost' & (VPOZ.PR().KOD=' -' | VPOZ.PR().KOD=' -o')
         || sOO+=_netto
         |? (KVAT.SYM*'SprzNOp' & (VPOZ.PR().KOD=' -' | VPOZ.PR().KOD=' -o')) | ((var_pres('_a') & _a>3) & (exec('kod_kraj','jpk')<> 'PL' &  exec('czy_proc_wsp','jpk') <> 'NO'))
         || sNO+=_netto
         ?};
         sFV+=_vat;
         sFB+=_brutto
      ?};
      VPOZ.next()
   !};
   {? TabDok.KN='2'
   || sFN23:=-sFN23; sFV23:=-sFV23; sFVW23:=-sFVW23;
      sFN8:=-sFN8; sFV8:=-sFV8; sFVW8:=-sFVW8;
      sFN5:=-sFN5; sFV5:=-sFV5; sFVW5:=-sFVW5;
      sFN0:=-sFN0; sFV0:=-sFV0;
      sFNZ:=-sFNZ;
      sFB:=-sFB;
      sFV:=-sFV;
      sOO:=-sOO
   ?};
   SumaB+=sFB
?};
exec('buf_fill','pola',DOK,DOK.ref(),POLAFAKS,DOKPOLA)


\jpk4vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Sprawdza czy istnieje plik JPK dla deklaracji VAT
::  OLD: \jpk4vat/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
JPK.cntx_psh();
JPK.index('DODAT');
JPK.prefix({? 2+VAT_DEK.TYP='V7' || 3+VAT_DEK.TYP |? 2+VAT_DEK.TYP='GV' || 'GV' || 'VAT' ?},$VAT_DEK.ref());
_jest:=JPK.first();
JPK.cntx_pop();
_jest


\wb_dataop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: Zwraca date operacji
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh;
_data:={? JpkDane || STR.gsub($DOK.DOP,'/','-') || STR.gsub($SSTALE.AO().KON,'/','-') ?};
OKRO_F.cntx_pop;
_data


\fa_datad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.41]
:: OPIS: Zwraca date dokumentu
::----------------------------------------------------------------------------------------------------------------------
OKRO_F.cntx_psh;
_data:={? JpkDane || STR.gsub($DOK.DTO,'/','-') || STR.gsub($SSTALE.AO().KON,'/','-') ?};
OKRO_F.cntx_pop;
_data


\prn_upo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Wydruk UPO dla JPK
::  OLD: \prn_upo/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? JPK.UPO
|| exec('upo_prn','xml',JPK,'UPO',JPK.OD)
|| FUN.info('Brak UPO dla pliku JPK.'@)
?}


\jpk_dw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca znacznik czasu wpłyniecia pliku JPK na potrzeby wydruku UPO
::   WE: _a - znacznik czasowy
::  OLD: \jpk_dw/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? +_a>28
|| _dt:='';
   _p:=_a*'.';
   _dt:=_p+_a;
   _p2:=_a*'+';
   _a:=_p-_a;
   _dt+=3+_a;
   _dt+(_a+6)
|| _a
?}


\zadania
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Zadania na ToDO dla JPK
::----------------------------------------------------------------------------------------------------------------------
exec('todo_select','#b__box',JPK.uidref())


\vat7_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Nr deklaracji VAT-7 uzywany w JPK_VAT
::  OLD: \vat7_nr/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? HELPJPK.RODZAJ='VAT'
|| {? OKRO_F.POCZ<date(2016,7,1)
   || 16
   |? OKRO_F.POCZ<date(2018,7,1)
   || 17
   |? OKRO_F.POCZ<date(2019,1,1)
   || 18
   |? OKRO_F.POCZ<date(2019,11,1)
   || 19
   || 20
   ?}
|? 2+HELPJPK.RODZAJ='V7'
|| {? OKRO_F.POCZ<date(2022,1,1)
   || 1
   || 2
   ?}
|| 1
?}


\is_vat7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy istnieje VAT-7 za okres
::   WE: _a - nr e-deklaracji
::       _b - typ: VAT7 lub VAT7_KOR
::  OLD: \is_vat7/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
HELPJPK.OKR_OD();
_okres:={? 3+_b='V7K' & OKRO_F.POCZ~2%*3=0
        || _nr:=OKRO_F.POCZ~2;
           _kw:={? _nr<4 || 1 |? _nr<7 || 2 |? _nr<10 || 3 || 4 ?};
           $(OKRO_F.POCZ~1)+'/'+$_kw
        || $(OKRO_F.POCZ~1)+'/'+form(OKRO_F.POCZ~2,-2)
        ?};
VAT_DEK.cntx_psh();
VAT_DEK.index('VAT_DEKR');
VAT_DEK.prefix({? 4+_b='VAT7' || 'V' |? 1+(2-_b)='M' || 'j' || 'J' ?},OKRO_F.ROK().POCZ_ROK,_okres,_b);
{? VAT_DEK.first()
|| {!
   |? _jest:=VAT_DEK.NR().NR=_a;
      _jest=0 & VAT_DEK.next()
   !}
?};
VAT_DEK.cntx_pop();
_jest


\is_edek_vat7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Czy istnieje zaakceptowana e-deklaracja
::   WE: _a - wynik rowniez na zmienna? 1-tak 0-nie
::  OLD: \is_edek_vat7/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
E_DEK.index('ZRODLO');
E_DEK.prefix(VAT_DEK.ref());
{? ~E_DEK.first() | E_DEK.AKC<>'T'
|| {? _a || NoEdek:=1 ?};
   0
|| 1
?}


\find_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Ustawia schemat JPK
:    WE: _a - data
::  OLD: \find_sch/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
ISTDEF.cntx_psh();
ISTDEF.index('DATA'); ISTDEF.prefix('FKS','J',HELPJPK.RODZAJ,null,);
{? ISTDEF.find_le(_a) | ISTDEF.first()
|| HELPJPK.ISTDEF:=ISTDEF.ref()
?};
ISTDEF.cntx_pop()


\vat_agr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Agreguje pozycje VAT (VAT_PS)
::  OLD: \vat_agr/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(86)='T' & var_press('VatPsI')>0
|| VAT_PS.index(VatPsI);
   VAT_PS.prefix(
      VAT_PS.VAT_DEK,VAT_PS.TYP_OPER,VAT_PS.VAT_POZ,
      VAT_PS.REJ_VAT, VAT_PS.NR_VAT, VAT_PS.SYM, VAT_PS.NIP, VAT_PS.SVAT
   );
   _vn:=_vv:=_vb:=0;
   {? VAT_PS.first()
   || {!
      |? _vn+=VAT_PS.NETTO;
         _vv+=VAT_PS.VAT;
         _vb+=VAT_PS.BRUTTO;
         VAT_PS.next()
      !}
   ?};
   VAT_PS.prefix(VAT_PS.VAT_DEK,VAT_PS.TYP_OPER);
   VAT_PS.NETTO:=_vn;
   VAT_PS.VAT:=_vv;
   VAT_PS.BRUTTO:=_vb
?}


\is_jpk_lp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Sprawdza czy istnieje plik JPK o większym LP
::   WE: _a - firma
::       _b - okres (OKRO_F) lub data
::       _c - typ
::       _d - numer
::       _e - pominąć bieżący plik JPK
::----------------------------------------------------------------------------------------------------------------------
_okres:={? var_press('_b')=type_of('') || _b |? var_press('_b')=type_of(null) || exec('okres','jpk',_b) || exec('okres2','jpk',_b) ?};
{? _c='VAT' & _okres>='2018/01'
|| _cur:=JPK.ref();
   _lp:=JPK.LP;
   JPK.cntx_psh();
   JPK.index('LP'); JPK.prefix(_a,_okres,_c,);
   _jest:=JPK.find_ge(_d);
   {? _jest & _e
   || JPK.prefix(_a,_okres,_c,JPK.LP);
      {? JPK.first()
      || {? ~JPK.next() & JPK.ref()=_cur
         || JPK.prefix(_a,_okres,_c);
            _jest:=JPK.find_ge(JPK.LP+1)
         ?}
      ?}
   ?};
   JPK.cntx_pop();
   _jest
?}


\chk_jpk_lp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Sprawdza istnienie JPK o podanym numerze
::   WE:  _a  - firma
::        _b  - okres OKRO_F lub data
::        _c  - typ
::        _d  - numer
::       [_e] - pomin bieżący
::   WY: Pole do redakcji
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_e')<=0 || _e:=0 ?};
_okres:={? var_press('_b')=type_of(null) || exec('okres','jpk',_b) || exec('okres2','jpk',_b) ?};
_r:='';
{? HELPJPK.KOR & HELPJPK.LP<1 | HELPJPK.LP>999
|| FUN.info('Numer powinien pochodzić z zakresu 1-999.'@);
   _r:='LP'
|? exec('is_jpk_lp','jpk',_a,_okres,_c,_d,_e)
|| {? HELPJPK.LP=0
   || {? ~FUN.ask('Istnieje już plik JPK_%1 za okres %2?\nKontynuować?'@[_c,_okres])
      || _r:='KOR'
      ?}
   || {? ~FUN.ask('Istnieje już korekta pliku JPK_%1 o numerze większym lub równym %2 za okres %3?\nKontynuować?'@[_c,$_d,_okres])
      || _r:='LP'
      ?}
   ?}
|| ''
?};
{? _r<>'' & HELPJPK.win_edit('?')<>'RED'
|| _r:=0
?};
_r


\system_name
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [18.02]
:: OPIS: Nazwa systemu
::----------------------------------------------------------------------------------------------------------------------
exec('nazwa','#system')


\pyt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Pytanie o wysyłanie lub pobieranie UPO
::   WE: _a - 1-pojedyncze 2-grupowe
::       _b - 1-wysyłanie 2-pobieranie UPO
::----------------------------------------------------------------------------------------------------------------------
_test:=XINFO.JPKGATE*'test'>0;
_bramka:={? _test || 'Bramka testowa'@ || 'Bramka'@ ?}+': '+XINFO.JPKGATE+'\n\n';
_opt1:='Tak, podpis kwalifikowany';
_opt2:='Tak, podpis zaufany';
_opt3:='Tak, podpis testowy';
{? _a=1
|| _sf:=JPK.TYP='SF';
   _test:=_sf & XINFO.SF_GATE*'tst'>0 | ~_sf & XINFO.JPKGATE*'test'>0;
   _bramka:={? _test || 'Bramka testowa'@ || 'Bramka'@ ?}+': '+{? JPK.TYP='SF' || XINFO.SF_GATE || XINFO.JPKGATE ?}+'\n\n';
   _typ:={? _sf || 'e-Sprawozdanie'@ || 'plik JPK'@ ?};
   {? _test
   || {? _b=1
      || FUN.choice(_bramka+'Czy wysłać %1 na bramkę testową?'@[_typ],'EXCLAM',_opt1,_opt2,_opt3)
      || FUN.ask(_bramka+'Czy pobrać UPO z bramki testowej?'@,'EXCLAM')
      ?}
   || {? _b=1
      || FUN.choice(_bramka+'Czy wysłać %1?'@[_typ],'ASK',_opt1,_opt2)
      || FUN.ask(_bramka+'Czy pobrać UPO?'@)
      ?}
   ?}
|| _typ:=exec('jpk_typ2','jpk');
   _bramka:='';
   _tg:=0;
   {? _typ.jpk
   || _tg+=_test;
      _bramka+={? _test || 'Bramka testowa JPK'@ || 'Bramka JPK'@ ?}+': '+XINFO.JPKGATE+'\n'
   ?};
   {? _typ.sf
   || _test:=XINFO.SF_GATE*'tst'>0;
      _tg+=_test;
      _bramka+={? _test || 'Bramka testowa e-Sprawozdań'@ || 'Bramka e-Sprawozdań'@ ?}+': '+XINFO.SF_GATE+'\n'
   ?};
   _bramka+='\n';
   {? _test
   || {? _b=1
      || FUN.choice(_bramka+'Czy wysłać zaznaczone pliki JPK?'@,'EXCLAM',_opt1,_opt2,_opt3)
      || FUN.ask(_bramka+'Czy pobrać UPO dla zaznaczonych plików JPK?'@,'EXCLAM')
      ?}
   || {? _b=1
      || FUN.choice(_bramka+'Czy wysłać zaznaczone pliki JPK?'@,'ASK',_opt1,_opt2)
      || FUN.ask(_bramka+'Czy pobrać UPO dla zaznaczonych plików JPK?'@)
      ?}
   ?}
?}


\loop_lata
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Petla po latach
::   WE: _a - formula do wykonania
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
ROK_F.index('ROKPOCZ'); ROK_F.prefix(REF.FIRMA);
{? ROK_F.first()
|| HELPJPK.ROK_OD();
   VAR_DEL.delete('eSprOkr');
   eSprOkr:=obj_new('RokB','RokP','OsprB','OsprP','MaskaB','MaskaP');
   {!
   |? eSprOkr.RokB:=ROK_F.ref();
      eSprOkr.MaskaB:=ROK_F.KOD;
      ROK_F.cntx_psh();
      {? ROK_F.prev()
      || eSprOkr.RokP:=ROK_F.ref();
         eSprOkr.MaskaP:=ROK_F.KOD
      || eSprOkr.RokP:=null;
         eSprOkr.MaskaP:='__'
      ?};
      ROK_F.cntx_pop();
      SSTALE.AR:=ROK_F.ref();
      OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref());
      OKRO_F.last();
      {? OKRO_F.KON=date(0,0,0) || OKRO_F.prev() ?};
      SSTALE.AO:=OKRO_F.ref();
      exec('open_tabele','open_tab');
      OSPR.cntx_psh();
      OSPR.index('OKRES1'); OSPR.prefix(REF.FIRMA,'M',ROK_F.ref(),HELPJPK.OKR_DO().NR,HELPJPK.OKR_DO().NR);
      OKRESY.OKRES1:=eSprOkr.OsprB:={? OSPR.first() || OSPR.ref() || null ?};
      {? eSprOkr.RokP
      || OSPR.prefix(REF.FIRMA,'M',eSprOkr.RokP,HELPJPK.OKR_DO().NR,HELPJPK.OKR_DO().NR);
         eSprOkr.OsprP:={? OSPR.first() || OSPR.ref() || null ?}
      || eSprOkr.OsprP:=null
      ?};
      ROK_F.cntx_psh();
      _dalej:=_a();
      ROK_F.cntx_pop();
      OSPR.cntx_pop();
      _dalej & HELPJPK.ROK_DO<>ROK_F.ref() & ROK_F.next()
   !};
   VAR_DEL.delete('eSprOkr')
?}


\is_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Czy dany tag paskuje do specyfikacji JPK
::   WE: _a - tag
::----------------------------------------------------------------------------------------------------------------------
_jest:=0;
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J');
{? ISTDEF.first()
|| ISTDEFS.cntx_psh();
   ISTDEFS.index('LP'); ISTDEFS.prefix();
   {!
   |? ISTDEFS.prefix(ISTDEF.ref());
      {? ISTDEFS.first()
      || _tag:=ISTDEFS.OPIS;
         {? (_p:=_tag*':') || _tag:=_p-_tag ?};
         _jest:=_tag=_a;
         {? _jest
         || HELPJPK.RODZAJ:=ISTDEF.RODZAJ
         ?}
      ?};
      _jest=0 & ISTDEF.next()
   !};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
_jest


\jpk_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca typy plików JPK do wysłania
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new('jpk','sf');
_tab.jpk:=_tab.sf:=0;
Jpk.cntx_psh();
{? Jpk.first()
|| {!
   |? {? Jpk.TYP='JPKSF' || _tab.sf+=1 || _tab.jpk+=1 ?};
      Jpk.next()
   !}
?};
Jpk.cntx_pop();
_tab


\jpk_typ2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca typy plików JPK do wysłania - akcja grupowa
::----------------------------------------------------------------------------------------------------------------------
_var:=obj_new('jpk','sf');
_var.jpk:=_var.sf:=0;
_tab:=JPK.sel_aget();
_jpk:=_sf:=0;
JPK.cntx_psh();
{? _tab.first()
|| {!
   |? {? JPK.seek(_tab.REF,)
      || {? JPK.TYP='SF'
         || _var.sf+=1
         || _var.jpk+=1
         ?}
      ?};
      _tab.next()
   !}
?};
JPK.cntx_pop();
_var


\errfun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Formuła wykonywana w przypadku znalezienia błędu przy walidacji pliku JPK
::   WE: _a - typ komunikatu
::       _b - treść komunikatu
::----------------------------------------------------------------------------------------------------------------------
{? ISTDEF.RODZAJ='KR'
|| {? ISTDEFS.OPIS='KodKontaWinien' | ISTDEFS.OPIS='KodKontaMa'
   || return()
   ?}
?};
_path:=exec('getPath','xml');
{? _a=-1
|| exec('addkom','jpk','E','Niezgodności ze schematem xsd:\n\n'+_b)
|? _a=0 & _b<>''
||  exec('addkom','jpk','E','Brak wartości wymaganego pola '+_path+': '+_b)
|? _a=0
|| exec('addkom','jpk','E','Brak wartości wymaganego pola: '+_path)
|| exec('addkom','jpk','E','Nieprawidłowa wartość pola '+_path+': '+_b)
?}


\vat_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Tabela na potrzeby JPK_VAT
::----------------------------------------------------------------------------------------------------------------------
_tab:=obj_new('tab','poz','add','first','next','get','VAT');
_tab.tab:=tab_tmp(13,
   'TYP','STRING[1]','Typ',
   'OKRO_F','INTEGER','Okres',
   'NIP','STRING[20]','NIP',
   'KH','STRING[150]','Kontrahent',
   'ADRES','STRING[255]','Adres',
   'DATA','DATE','Data',
   'DATA2','DATE','Data 2',
   'SYM','STRING[35]','Symbol',
   'KOR','INTEGER','Korekta',
   'REJ_KS','STRING[16]','Rejestr księgowy',
   'NR_KS','INTEGER','Numer księgowy',
   'KNAG','INTEGER','Znacznik korekt nagłówkowych',
   'DATA_ZAP','DATE','Data zapisu',
   'VAT_PS','INTEGER','ref',
   'MARZA','REAL','Marża'
);
_tab.poz:=tab_tmp(2,
   'REF','INTEGER','Nagłówek',
   'POZ','INTEGER','Pozycja',
   'KW','REAL','Kwota',
   'VAT','REAL','Kwota VAT'
);
_tab.VAT:=0;
_tab.add:="
   _tab:=.tab;
   _poz:=.poz;
   _tab.blank(1);
   _tab.TYP:=VAT_PS.TYP_OPER;
   _tab.NIP:=VAT_PS.NIP;
   _tab.KH:=VAT_PS.KH;
   _tab.ADRES:=VAT_PS.ADRES;
   _tab.DATA:=VAT_PS.DATA;
   _tab.DATA2:=VAT_PS.DATA2;
   _tab.SYM:=VAT_PS.SYM_ZEW;
   _tab.KOR:={? VAT_PS.JPK_GTU+1=';' || 2 |? VAT_PS.JPK_GTU+1=':' || 3 || 1 ?};
   _tab.DATA_ZAP:={? -VAT_PS.TYP_OPER<>'z' || VAT_PS.DATA_ZAP || date(0,0,0) ?};
   _tab.VAT_PS:=#VAT_PS.ref();
   _tab.REJ_KS:=$VAT_PS.REJ_KS;
   _tab.NR_KS:=VAT_PS.NR_KS;
   _tab.KNAG:=_a;
   _tab.OKRO_F:={? var_pres('OKRO_F',VAT_PS)>0 || VAT_PS.OKRO_F().POCZ~2 ?};
   _tab.prefix(_tab.TYP,_tab.OKRO_F,_tab.NIP,_tab.KH,_tab.ADRES,_tab.DATA,_tab.DATA2,_tab.SYM,_tab.KOR,_tab.REJ_KS,_tab.NR_KS,_tab.KNAG,_tab.DATA_ZAP);
   {? ~_tab.first() | _a=2
   || {? VAT_PS.JPK_PROC*'MR_T' | VAT_PS.JPK_PROC*'MR_UZ'
      || _tab.MARZA:=VAT_PS.BRUTTO
      ?};
      _tab.add()
   |? VAT_PS.JPK_PROC*'MR_T' | VAT_PS.JPK_PROC*'MR_UZ'
   || _tab.MARZA+=VAT_PS.BRUTTO;
      _tab.put()
   ?};
   {? VAT_PS.VAT_POZ().POZ1
   || {? _poz.find_key(_tab.ref(),VAT_POZ.POZ1)
      || {? exec('vat_pomin','jpk_v',1)=0
         || _poz.KW+=VAT_PS.NETTO;
            _poz.put()
         ?}
      || _poz.REF:=_tab.ref();
         _poz.POZ:=VAT_POZ.POZ1;
         _poz.KW:={? exec('vat_pomin','jpk_v',1)=0 || VAT_PS.NETTO ?};
         _poz.VAT:=0;
         _poz.add()
      ?}
   ?};
   {? VAT_PS.VAT_POZ().POZ2
   || {? _poz.find_key(_tab.ref(),VAT_POZ.POZ2)
      || {? exec('vat_pomin','jpk_v',1)=0
         || _poz.KW+=VAT_PS.VAT
         ?};
         {? exec('vat_pomin','jpk_v',2)=0
         || _poz.VAT+=VAT_PS.VAT
         ?};
         _poz.put()
      || _poz.REF:=_tab.ref();
         _poz.POZ:=VAT_POZ.POZ2;
         _poz.KW:={? exec('vat_pomin','jpk_v',1)=0 || VAT_PS.VAT ?};
         {? exec('vat_pomin','jpk_v',2)=0
         || _poz.VAT:=VAT_PS.VAT
         || _poz.VAT:=0
         ?};
         _poz.add()
      ?}
   ?}
";
_tab.first:="
   _tab:=.tab;
   _poz:=.poz;
   {? var_pres('_b')>0
   || _tab.prefix(_a,_b)
   || _tab.prefix(_a)
   ?};
   {? _tab.first()
   || VAT_PS.seek(_tab.VAT_PS,);
      _poz.prefix(_tab.ref());
      _poz.first()
   ?}
";
_tab.next:="
   _tab:=.tab;
   _poz:=.poz;
   {? _tab.next()
   || _poz.prefix(_tab.ref());
      VAT_PS.seek(_tab.VAT_PS,);
      1
   ?}
";
_tab.get:="
   _poz:=.poz;
   .VAT:=0;
   {? _poz.find_key(_a)
   || .VAT:=_poz.VAT;
      _poz.KW
   ?}
";
VAT_PS.cntx_psh();
VAT_PS.index('VAT_PS'); VAT_PS.prefix(VAT_DEK.ref());
{? VAT_PS.first()
|| _istotne:=exec('istotne','jpk');
   _par:=PAR_SKID.get(108);
   {!
   |? _knag:=exec('knag','jpk');
      {? (VAT_PS.VAT_POZ().POZ1 & _istotne*(';'+$VAT_PS.VAT_POZ().POZ1+';'))
      |  (VAT_PS.VAT_POZ().POZ2 & _istotne*(';'+$VAT_PS.VAT_POZ().POZ2+';'))
      || _tab.add(_knag)
      ?};
      VAT_PS.next()
   !}
?};
VAT_PS.cntx_pop();
_tab


\knag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [20.xx]
:: OPIS: Sprawdzenie czy i w jaki sposób pozycja ma związek z korektą nagłówkową
::   WY: 2 - zapis na minus w dokumencie pierwotnym powstały wskutek wystawienia do niego korekty nagłówkowej
::       1 - zapis związany z dokumentem korekty nagłówkowej
::       0 - zwykła faktura
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
{? 4+VAT_PS.PVAT_REF<>'pvat' || return(0) ?};
PVAT.cntx_psh(); DVAT.cntx_psh();
PVAT.use(VAT_PS.PVAT_REF-8); PVAT.prefix();
DVAT.use('dvat__'+(PVAT.name()+2));
_ok:=PVAT.seek(BB.sqlint(VAT_PS.PVAT_REF),PVAT.name());
{? _ok
|| {? PVAT.KNAG=2
   || _ret:=2
   || PVAT.DVAT();
      _maska:=DVAT.DOKOKRO().ROK().KOD+form(DVAT.DOKOKRO().NR,-2);
      DOK.cntx_psh();
      DOK.use('doku'+_maska);
      DVAT.DOK();
      {? DOK.DOK_REJ().KOR_NAG='T' || _ret:=1 ?};
      DOK.cntx_pop()
   ?}
?};
PVAT.cntx_pop(); DVAT.cntx_pop();
_ret


\istotne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.51]
:: OPIS: Zwraca string złożony z kodów pozycji K_?? istotnych dla danego wzorca e-deklaracji
::----------------------------------------------------------------------------------------------------------------------
_ret:=';';
JPK.ISTDEF(); HELPJPK.ISTDEF();
ISTDEFS.cntx_psh();
ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref(),'K_');
{? ISTDEFS.first()
|| {!
   |? {? #(ISTDEFS.OPIS+2)>0  || _ret+=2-ISTDEFS.OPIS+';' ?};
      ISTDEFS.next()
   !}
?};
ISTDEFS.cntx_pop();
_ret


\fa_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.22]
:: OPIS: Zwraca stawkę VAT dla pozycji faktury
::   WE: _a - kod stawki VAT
::----------------------------------------------------------------------------------------------------------------------
{? _a=' -zw0'
|| ''
|? -_a*'zw'
|| 'zw'
|? _a=' -o' & var_press('FaVer')>0 & FaVer>1
|| 'oo'
|? _a=' -' & var_press('FaVer')>0 & FaVer>1
|| {? PAR_SKID.get(490)='T'
   || {? exec('fa_oo','jpk')
      || 'oo'
      || 'np'
      ?}
   || 'np'
   ?}
|| |(SLO.KOD-2)
?}


\fa_oo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Sprawdzenie, czy dokument związany z odwrotnym obciążeniem
::  OLD: \fa_oo/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_oo:=0;
VPOZ.cntx_psh();
VPOZ.index('VDOK'); VPOZ.prefix(POZF.DOK);
{? VPOZ.first()
|| {!
   |? _klasa:=VPOZ.RVAT().RVAT().KVAT().SYM;
      {? _klasa='SprzNOp'
      || _oo:=7+VPOZ.GRVAT().KOD='SprPDos'
      |? _klasa='ZakOpod'
      || _oo:=1+VPOZ.GRVAT().KOD='Z'
      ?};
      _oo=0 & VPOZ.next()
   !}
?};
VPOZ.cntx_pop();
_oo


\ten_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [19.42]
:: OPIS: Formuła na Wybierz w okienku wertowania 'WER2' tabeli JPK

::----------------------------------------------------------------------------------------------------------------------
sel_exit()


\epuap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [19.42]
:: OPIS: Adres do usługi podpisywania profilem zaufanym
::----------------------------------------------------------------------------------------------------------------------
'https://obywatel.gov.pl/praca-i-biznes/podpisz-dokument-elektronicznie-wykorzystaj-podpis-zaufany/usluga-online/signer/upload?xFormsAppName=SIGNER&xFormsOrigin=EXTERNAL'


\blp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Sprawdzenie aktywności podatników VAT w fakturach JPK_VAT
::----------------------------------------------------------------------------------------------------------------------
{? (var_pres('BLP_JPK',XINFO)<=0 | XINFO.BLP_JPK=0)
|| _snip:=exec('niptostr','#string',VAT_PS.NIP);
   _data:={? VAT_PS.DATA2<>date(0,0,0) || VAT_PS.DATA2 || VAT_PS.DATA ?};
   {? _snip<>'' & (var_pres('VatHT')<=0 | VatHT.addIfNotExists(_snip+$_data)) &
      exec('spr_typ','blp',_snip)
   || _obj:=exec('check','blp','K',_snip,_data);
      {? _obj.ACTIVE='N'
      || exec('addkom','jpk','W','Nieaktywny podatnik VAT ('+$_data+') '+VAT_PS.NIP+' - '+VAT_PS.KH)
      |? _obj.ACTIVE='?'
      || _err:='Nie udało się sprawdzenie podatnika VAT ('+$_data+') '+VAT_PS.NIP+' - '+VAT_PS.KH+'.';
         {? _obj.ERR<>''
         || _err+=' Bramka zwraca następujące informacje: '+_obj.ERR
         ?};
         exec('addkom','jpk','W',_err)
      ?};
      {? _obj.NAZ<>'' & VAT_PS.KH<>_obj.NAZ
      || exec('addkom','jpk','W','Nazwa kontrahenta: "'+VAT_PS.KH+'" niezgodna z wykazem podatników VAT: "'+_obj.NAZ+'"')
      ?}
   ?}
?}


\fa_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca walutę dokumentu
::----------------------------------------------------------------------------------------------------------------------
{? ~JpkDane | DOK.WAL=null
|| 'PLN'
|| DOK.WAL().KOD
?}


\vat_nip_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Kod kraju z NIP
::  OLD: \vat_nip_kod/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_nip:=VAT_PS.NIP;
{? _nip='' || return('') ?};
_nip:=gsub(_nip,'-','');
_nip:=gsub(_nip,' ','');
KRAJE.index('KRAJE');
KRAJE.prefix(2+_nip,);
{? KRAJE.first()
|| 2+_nip
|| ''
?}


\be_jpk_kor2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Przed redakcją HELPJPK.KOR_D i HELPJPK.KOR_E
::  OLD: \be_jpk_kor2/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
HELPJPK.KOR & HELPJPK.RODZAJ<>'GV'


\vat_marza
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Wartosc kwoty w JPK_V7 dla pól Sprzedaż/zakup VAT Marża
::  OLD: \vat_marza/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_press('VatTab')>0
|| _kw:=VatTab.tab.MARZA;
   {? _kw & VatTab.VAT
   || SumaVat+=VatTab.VAT
   ?};
   {? _kw | ISTDEFS.WYM='T'
   || STR.gsub($_kw,',','.')
   || ''
   ?}
|? VAT_PS.JPK_PROC*'MR_T' & (JpkV7.MrT=1 | VAT_PS.JPK_TD<>'WEW') |
   VAT_PS.JPK_PROC*'MR_UZ' & (JpkV7.MrUz=1 | VAT_PS.JPK_TD<>'WEW')
|| {? VAT_PS.BRUTTO | ISTDEFS.WYM='T'
   || STR.gsub($VAT_PS.BRUTTO,',','.')
   || ''
   ?}
|| ''
?}


\v7k_mc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca miesiąc dla JPK_V7K
::  OLD: \v7k_mc/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? 1+(VAT_DEK.OKRES+3)='/'
|| $#(VAT_DEK.OKRES+2)
|| _kw:=#(VAT_DEK.OKRES+1);
   $(_kw*3)
?}


\br_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Rekord przed okna WER tabeli JPK
::   WE: _a - akronim okna wertowania
::  OLD: \br_jpk/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_gray:='';
_win:={? var_pres('_a')>0 & type_of(_a)=type_of('') || _a |? var_pres('_b')>0 & type_of(_b)=type_of('') || _b || '' ?};
{? _win='' || _win:='WER' ?};
_v7def:=var_pres('JPK_GTU',DOK);
_gv:=2+JPK.TYP='GV';
_w_upo:={? _v7def | _gv || 'J(U)' || 'J' ?};
_w_jpk:={? _v7def || 'J(J)' || '' ?};
{? JPK.sel_size()=0
|| {? JPK.AKC='T'
   || _gray:='EUAF(C)';
      {? JPK.STAT='W'
      || _gray+='WY'+_w_upo
      |? JPK.STAT='T'
      || _gray+='WYB'
      |? JPK.STAT='B'
      || _gray+='WYB'+_w_upo
      || _gray+='BP'+_w_upo
      ?}
   || _gray:='WYBP'+_w_upo;
      {? JPK.OKRES<'2017/01' || _gray+='F(C)' ?}
   ?};
   {? _v7def & (2+JPK.TYP<>'V7' & 2+JPK.TYP<>'GV')
   || _gray+=_w_jpk
   ?}
?};
{? JPK.TYP<>'SF' | JPK.AKC<>'T'
|| _gray+='I'
?};
JPK.actions_grayed(_win,_gray);
{? JPK.STAT='W'
|| Color.fnd_kol('JPK#01#01')
|? JPK.STAT='T'
|| Color.fnd_kol('JPK#01#03')
|? JPK.STAT='B'
|| Color.fnd_kol('JPK#01#02')
|| ''
?}


\fa_sym_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca symbole zaliczek dla dokumentu pochodzącego z logistyki
::  OLD: \fa_sym_zal/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_sym:='';
_sys:={? +DOK.ZAR & DOK.ZAR*':' || (DOK.ZAR*':'-1)+DOK.ZAR || '' ?};
{? _sys='LSP' | _sys='LZK'
|| {? 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'
   || _maska:=3+(1-DOK.DOKZRODL);
      FAKS.cntx_psh();
      FAKS.use('faktu'+_maska);
      FAKS.prefix();
      _reffak:=#(4-DOK.DOKZRODL);
      {? FAKS.seek(_reffak,)
      || _tab:=exec('zal_lista','faktury_nag',FAKS.ref());
         {? _tab.first()
         || {!
            |? _sym+=_tab.FAKS_SYM+',';
               _tab.next()
            !};
            _sym:=_sym-1
         ?}
      ?};
      FAKS.cntx_pop()
   ?}
?};
_sym


\settings_p12
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia scieżkę do pliku *.p12 w settings.xml
::   WE: _a - scieżka do pliku settings.xml
::       _b - scieżka do pliku *.p12
::  OLD: \settings_p12/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
_f1:=fopen('settings_test.xml','ur',1);
{? _f1
|| _f2:=fopen(_a,'Uw',0);
   {? _f2
   || {!
      |? {? (_line:=fread(_f1))<>'\n'
         || {? _line*'[PATH]'
            || _b:=gsub(_b,' ','%20');
               _line:=gsub(_line,'[PATH]',_b)
            ?};
            fwrite(_f2,_line);
            1
         ?}
      !};
      _ok:=1;
      fclose(_f2)
   ?};
   fclose(_f1)
?};
_ok


\set_jpk_nip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Ustawia w pliku JPK ostatnią cyfrę NIP-u na 1 - wymaganie bramki testowej
::       https://www.podatki.gov.pl/komunikaty-techniczne/aktualizacja-srodowiska-testowego-dla-wysylki-plikow-jpk-11-03/
::   WE: _a - scieżka do pliku JPK
::  OLD: \set_jpk_nip/jpk.fml
:: ~OST: INFOPEN
::----------------------------------------------------------------------------------------------------------------------
_fp:=400;
_tryb:=1;
_poz:=1;
_baza:='NIP>';
_f:=fopen('@'+_a,'b',0);
{? _f
|| _znak:=(_poz+_baza)+1;
   fseek(_f,_fp);
   {!
   |? _byte:=fread(_f);
      _fp+=1;
      {? _tryb=1
      || {? _byte=%_znak
         || _poz+=1;
            {? _poz=5
            || _tryb:=0
            || _znak:=(_poz+_baza)+1
            ?}
         |? _poz<>1
         || _poz:=1;
            _znak:=(_poz+_baza)+1
         ?}
      || {? _byte=%'<'
         || _fp-=2;
            fseek(_f,_fp);
            fwrite(_f,%'1');
            _byte:=-1
         ?}
      ?};
      _byte<>-1
   !};
   fclose(_f)
?};
_fp


\fa_nag_f
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Formula na start petli dla elementu Faktura JPK_FA
::   WE: _a - 1-start 0-stop
::       _b - czy zaliczka? 1-tak 0-wszystkie
::       _c - numer wersji JPK
::  OLD: \fa_nag_f/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_c')<0 || _arg:=0 || _arg :=_c ?};
{? _a=1
|| NrJPK:=0;
   SumaB:=0;
   JpkDane:=1;
   {? _b=0
   || TabDok.index(TabDok1);
      TabDok.prefix()
   || TabDok.index(TabDok2);
      TabDok.prefix(1)
   ?};
   {? TabDok.first()
   || {? _b || exec('fa_poz_i','jpk') ?};
      {? DOK.name()<>8+TabDok.REF || DOK.use(8+TabDok.REF); VPOZ.use('pozv'+((8+TabDok.REF)+4)); POZF.use('pozf'+((8+TabDok.REF)+4)) ?};
      DOK.prefix();
      {? DOK.seek(BIT.sqlint(TabDok.REF),8+TabDok.REF)
      || exec('fa_suma','jpk',_arg);
         1
      || exec('fa_nag_n','jpk',_arg)
      ?}
   |? _b=0
   || JpkDane:=0; exec('fa_suma','jpk',_arg); 1
   || NrJPK:=0
   ?}
|| VAR_DEL.delete('FodzFak','FaKor');
   VAR_DEL.delete('JpkDane','FaZal','FaPoz','FaPozT');
   {? _b=1
   || VAR_DEL.delete('TabDok','TabDok1','TabDok2')
   ?};
   1
?}


\fa_zal_p
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Formuła na start/stop pętli po pozycjach faktury zaliczkowej
::   WE: _a - 1-star 0-stop 2-next
::  OLD: \fa_zal_p/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a=1
|| {? (_jest:=exec('fa_poz1','jpk'))>0
   || exec('fa_p_ele','jpk',0);
      1
   || {? _jest=0
      || exec('addkom','jpk','W','Brak pozycji faktury: '+DOK.ODD().OD+' '+DOK.REJ().KOD+' '+$DOK.NR+': '+DOK.NK)
      ?};
      FaPozT:='';
      0
    ?}
|? _a=2
|| {? FaPozT='F' & POZF.next() | FaPozT='L' & FAP.next()
   || exec('fa_p_ele','jpk',0);
      1
   ?}
|| 1
?}


\fa_poz_i
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Inicjuje tablicę na wartości z pozycji faktury
::  OLD: \fa_poz_i/jpk.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('FaPoz');
FaPozT:='';
FaPoz:=obj_new(11);
FaPoz[1]:=FaPoz[2]:='brak';
{! _i:=3..8 |!  FaPoz[_i]:=form(0,,2,'9.') !};
FaPoz[9]:='zw';
FaPoz[10]:=form(1,,2,'9.');
FaPoz[11]:=form(0,,2,'9.');
NrJPK:=0;
SumaP:=0;
SumaB:=0;
~~


\dek_jpkupo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [20.42]
:: OPIS: Sprawdza względem parametru 108 czy brać pod uwagę deklarację podczas tworzenia korekt JPK_V7
::   WY: 0 - Należy pominąć deklarację JPK_V
::----------------------------------------------------------------------------------------------------------------------
_par:=PAR_SKID.get(108);
_ret:=0;
{? _par='W'
|| JPK.cntx_psh();
   JPK.index('DODAT');
   JPK.prefix({? 2+VAT_DEK.TYP='V7' || 3+VAT_DEK.TYP || 'VAT' ?},$VAT_DEK.ref());
   {? JPK.first()
   || {!
      |? {? JPK.STAT='T' || _ret:=1 ?};
         ~_ret & JPK.next()
      !}
   ?};
   JPK.cntx_pop()
|? _par='D'
|| _ret:=1
|? _par='J'
|| JPK.cntx_psh();
   JPK.index('DODAT');
   JPK.prefix({? 2+VAT_DEK.TYP='V7' || 3+VAT_DEK.TYP || 'VAT' ?},$VAT_DEK.ref());
   {? JPK.first()
   || _ret:=1
   ?};
   JPK.cntx_pop()
?};
_ret


\proc_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.14]
:: OPIS: Dodatkowe komunikaty dla plików JPK (uproszczenia 01/07/2021) oraz nowa wersja schematu JPK_V7 (od stycznia 2022)
::----------------------------------------------------------------------------------------------------------------------
VAT_DEK.cntx_psh();
_okres:=VAT_PS.VAT_DEK().OKRES;
{? 1+(_okres+2)='/'
|| _mc:=(#(_okres+1)-1)*3+1
|| _mc:=#(_okres+2)
?};
_data:=date(#(4+_okres),_mc,1);
{? _data>=exec('data_upr','jpk_v')
|| {? VAT_PS.JPK_PROC<>''
   || {? VAT_PS.JPK_TD='RO'
      || _txt:='Rejestr VAT: '+VAT_PS.REJ_VAT+' | nr: '+$VAT_PS.NR_VAT+' | symbol: '+
           {? var_press('SYM_ZEW',VAT_PS)>0 || VAT_PS.SYM_ZEW || VAT_PS.SYM ?}+
           ' - typ dokumentu RO (sprzedaż z kas fiskalnych) - usunięto kody procedur szczególnych.';
         exec('addkom','jpk','W',_txt)
      || _proc:=','+VAT_PS.JPK_PROC+',';
         {? (_proc*',WSTO_EE,' | _proc*',SW,') & exec('d_upr_chk','jpk_v')
         || _code:={? _proc*',WSTO_EE,' || 'WSTO_EE, ' || '' ?}+{? _proc*',SW,' || 'SW, ' || '' ?};
            _txt:='Rejestr VAT: '+VAT_PS.REJ_VAT+' | nr: '+$VAT_PS.NR_VAT+' | symbol: '+
              {? var_press('SYM_ZEW',VAT_PS)>0 || VAT_PS.SYM_ZEW || VAT_PS.SYM ?}+
              ' - zgodnie z uproszczeniami JPK_V7 zmieniono '+
              {? +_code<10 || 'procedurę ' || 'procedury ' ?}+(_code-2)+' na EE.';
            exec('addkom','jpk','W',_txt)
         ?};
         {? (_proc*',IED,' | _proc*',SOTI,') & exec('d_upr_chk','jpk_v')
         || _code:={? _proc*',IED,' || 'IED, ' || '' ?}+{? _proc*',SOTI,' || 'SOTI, ' || '' ?};
            _txt:='Rejestr VAT: '+VAT_PS.REJ_VAT+' | nr: '+$VAT_PS.NR_VAT+' | symbol: '+
              {? var_press('SYM_ZEW',VAT_PS)>0 || VAT_PS.SYM_ZEW || VAT_PS.SYM ?}+
              {? +_code<7
              || ' - procedura '+(_code-2)+' nie została zawarta '
              || ' - procedury '+(_code-2)+' nie zostały zawarte '
              ?}+'w pliku JPK zgodnie z aktualnym schematem pliku.';
            exec('addkom','jpk','W',_txt)
         ?};
         {? (_proc*',MPP,') & exec('d_upr_chk','jpk_v')
         || _txt:='Rejestr VAT: '+VAT_PS.REJ_VAT+' | nr: '+$VAT_PS.NR_VAT+' | symbol: '+
              {? var_press('SYM_ZEW',VAT_PS)>0 || VAT_PS.SYM_ZEW || VAT_PS.SYM ?}+
              ' - zgodnie z uproszczeniami JPK_V7 procedura MPP nie została zawarta w pliku JPK.';
            exec('addkom','jpk','W',_txt)
         ?}
      ?}
   ?};
   {? (VAT_PS.JPK_TD='RO' | VAT_PS.JPK_TD='WEW') & VAT_PS.JPK_GTU<>''
   || _txt:='Rejestr VAT: '+VAT_PS.REJ_VAT+' | nr: '+$VAT_PS.NR_VAT+' | symbol: '+
        {? var_press('SYM_ZEW',VAT_PS)>0 || VAT_PS.SYM_ZEW || VAT_PS.SYM ?}+
        ' - typ dokumentu '+{? VAT_PS.JPK_TD='RO' || 'RO (sprzedaż z kas fiskalnych)' || 'WEW (wewnętrzny)' ?}+
        ' - usunięto oznaczenia grup towarów i usług.';
      exec('addkom','jpk','W',_txt)
   ?}
?};
:: Komunikaty dla nowej wersji jpk_v7(2) od stycznia 2022
{? _data>=date(2022,1,1) & 2+VAT_DEK.TYP='V7' & (','+VAT_PS.JPK_PROC+',')*',EE'
|| {? PAR_SKID.get(489)='T'
   || exec('addkom','jpk','W','Rejestr VAT: '+VAT_PS.REJ_VAT+' | nr: '+$VAT_PS.NR_VAT+' | symbol: '+
                             {? var_press('SYM_ZEW',VAT_PS)>0 || VAT_PS.SYM_ZEW || VAT_PS.SYM ?}+
                             ' - procedura EE została zamieniona na procedurę WSTO_EE.')
   || exec('addkom','jpk','W','Rejestr VAT: '+VAT_PS.REJ_VAT+' | nr: '+$VAT_PS.NR_VAT+' | symbol: '+
                             {? var_press('SYM_ZEW',VAT_PS)>0 || VAT_PS.SYM_ZEW || VAT_PS.SYM ?}+
                             ' - procedura EE nie została zawarta w pliku JPK zgodnie z aktualnym schematem pliku.')
   ?}
?};
VAT_DEK.cntx_pop();
1


\ref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.37]
:: OPIS: Zwraca wskazanie do rekordu wykorzystywane w schemacje JPK dla zadanej tabeli
::   WE: _a - akronim tabeli
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
($('$'+_tab+'.ref()'))()+{? PAR_SKID.get(212)<>'N' || exec('sha1','#to_sha1',_tab) || '' ?}


\get_lp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Zwraca LP dla JPK_V7
::   WE: [_a] - data
::----------------------------------------------------------------------------------------------------------------------
_lp:=0;
{? HELPJPK.KOR
|| _data:={? var_pres('_a')>0 || _a || HELPJPK.DATA_OD ?};
   JPK.cntx_psh();
   _okr:=exec('okres2','jpk',_data);
   JPK.index('LP'); JPK.prefix(REF.FIRMA,_okr,HELPJPK.RODZAJ);
   _lp:={? JPK.last() || JPK.LP+1 || 1 ?};
   JPK.cntx_pop()
?};
_lp


\jpk_fa_tt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca wartość dla pola JPK/Faktura/P_23 schematu JPK_FA
::----------------------------------------------------------------------------------------------------------------------
{? (','+DOK.JPK_PROC+',')*',TT_WNT,' | (','+DOK.JPK_PROC+',')*',TT_D,' |
   POLAFAKS.TT='T' |
   DOK.RVAT().RVAT().KVAT().SYM='_WWspNat'
|| 'true'
|| 'false'
?}


\jpk_fa_dsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Zwraca wartość dla pola JPK/Faktura/P_6 schematu JPK_FA
::----------------------------------------------------------------------------------------------------------------------
{? TabDok.DSP<>'T'
|| {? DOK.DOP<>DOK.DTO || STR.gsub($DOK.DOP,'/','-') || '' ?}
|| {? TabDok.DOP<>DOK.DTO || STR.gsub($TabDok.DOP,'/','-') || '' ?}
?}


:Sign Version 2.0 jowisz:1045 2023/09/07 12:13:13 deaa12798c3b99d66de9739a27f872f1c5f05f0b7a8cecb169ae7a74fbe2c7f113baf824d741fbf1d1d87c8e825a3ad85e6ee45bca7f45cc0999491e19de0fbcf6b12e51db835eb7dcb07598df1acb19c369c615083c0d5afb26b2b00fe5e3a5e9ab0bde7ab079d5864e3bfc0e9dcc61353738782e731f4264f0c15c1e6f80f0

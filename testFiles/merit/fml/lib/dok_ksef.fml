:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: dok_ksef.fml
:: Utworzony: 03.11.2021 [22.26]
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły do obsługi dokumentów księgowych w KSEF
::======================================================================================================================


\fakspola2dokpola
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Kopiowanie dodatkowych informacji o fakturze między fakturą a dokumentem księgowym
::   WE: _a - faktura
::       _b - dokument księgowy
::       _c - procedury szczególne z formułu
::----------------------------------------------------------------------------------------------------------------------
_faks:=_a;
_dok:=_b;
_proc:=_c;

FAKSPOLA.cntx_psh();
FAKSPOLA.blank();
FAKSPOLA.REK:=_faks;
_crc:=FAKSPOLA.crc();
FAKSPOLA.cntx_pop();
FAKSPOLA.cntx_psh();
FAKSPOLA.use('faktp'+((ref_name(_faks))+3));
FAKSPOLA.index('UNIK');
FAKSPOLA.prefix(_faks);
{? FAKSPOLA.first()
|| {? FAKSPOLA.UDS='T' || _proc:=exec('add_str','dok_ksef',_proc,'MR_UZ') ?};
   {? FAKSPOLA.USL_TUR='T' || _proc:=exec('add_str','dok_ksef',_proc,'MR_T') ?};
   FAKSPOLA.IDADD:='';
   {? _crc<>FAKSPOLA.crc()
   || DOKPOLA.cntx_psh();
      DOKPOLA.use('dokp'+((ref_name(_dok))+4));
      DOKPOLA.prefix();
      exec('buf_save','pola',_dok,FAKSPOLA,DOKPOLA);
      DOKPOLA.add();
      DOKPOLA.cntx_pop()
   ?}
?};
FAKSPOLA.cntx_pop();
FAP.cntx_psh();
FAP.use('fakpo'+((ref_name(_faks))+3));
FAP.index('FAP'); FAP.prefix(_faks);
{? FAP.first()
|| {!
   |? {? FAP.EF_PROC
      || _proc:=exec('add_str','dok_ksef',_proc,FAP.EF_PROC().KOD)
      ?};
      FAP.next()
   !}
?};
FAP.cntx_pop();
FAKS.cntx_psh();
FAKS.use(ref_name(_faks));
FAKS.prefix();
{? FAKS.seek(_faks) & FAKS.PROC
|| _proc:=exec('add_str','dok_ksef',_proc,FAKS.PROC().KOD)
?};
FAKS.cntx_pop();
{? _proc<>''
|| DOK.cntx_psh();
   DOK.use(ref_name(_dok));
   DOK.prefix();
   {? DOK.seek(_dok)
   || DOK.memo_set(_proc,'PROC_LOG');
      DOK.memo_put(,'PROC_LOG');
      DOK.JPK_PROC:=exec('get_dok_jpk_proc','dok_ksef');
      DOK.put()
   ?};
   DOK.cntx_pop()
?}


\edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Edycja dodatkowych danych do faktury
::   WE: _a - 0-przed dołaczeniem, 1-przed edycją (podczytania), 2-po edycji (zapis)
::----------------------------------------------------------------------------------------------------------------------
_tryb:=_a;
{? _tryb=0
|| DOKPOLA.index('UNIK');
   DOKPOLA.prefix();
   DOKPOLA.blank();
   DOKPOLA.memo_set(,'WZ');
   DOKPOLA.memo_set(,'NRFAZAL');
   DOKPOLA.memo_set(,'ZALPOW');
   VAR_DEL.delete('DokPolaCRC');
   DokPolaCRC:=DOKPOLA.crc()
|? _tryb=1
|| DOKPOLA.index('UNIK');
   DOKPOLA.prefix({? var_pres('dok')>0 || dok || DOK.ref() ?});
   {? DOKPOLA.first()
   || DOKPOLA.memo_get(,'WZ',0);
      DOKPOLA.memo_get(,'NRFAZAL',0);
      DOKPOLA.memo_get(,'ZALPOW',0)
   || DOKPOLA.blank();
      DOKPOLA.memo_set(,'WZ');
      DOKPOLA.memo_set(,'NRFAZAL');
      DOKPOLA.memo_set(,'ZALPOW')
   ?};
   {? var_pres('__STORNO')<=0 || DOK.memo_get(,'NRKSEFDK',0) ?};
   VAR_DEL.delete('DokPolaCRC');
   DokPolaCRC:=DOKPOLA.crc()
|? _tryb=2
|| DOKPOLA.cntx_psh();
   DOKPOLA.index('UNIK');
   DOKPOLA.prefix(DOK.ref());
   _ref:={? DOKPOLA.first() || DOKPOLA.ref() || null ?};
   DOKPOLA.cntx_pop();
   {? _ref
   || {? _ref=DOKPOLA.ref()
      || DOKPOLA.memo_put(,'WZ');
         DOKPOLA.memo_put(,'NRFAZAL');
         DOKPOLA.memo_put(,'ZALPOW');
         DOKPOLA.put()
      ?}
   |? var_pres('DokPolaCRC')<=0 | DokPolaCRC<>DOKPOLA.crc() |
      (var_pres('dolndok')>0 & dolndok=1) |
      DOKPOLA.memo_txt(,0,'WZ')<>'' | DOKPOLA.memo_txt(,0,'NRFAZAL')<>'' | DOKPOLA.memo_txt(,0,'ZALPOW')<>''
   || DOKPOLA.cntx_psh();
      DOKPOLA.prefix();
      DOKPOLA.REK:=DOK.ref();
      DOKPOLA.add();
      DOKPOLA.memo_put(,'WZ');
      DOKPOLA.memo_put(,'NRFAZAL');
      DOKPOLA.memo_put(,'ZALPOW');
      DOKPOLA.put();
      DOKPOLA.cntx_pop()
   ?}
?};
1


\get_dok_jpk_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca procedury ustawione w dokumencie
::   WE: [_a] - stan dokumentu: 0-nie istnieje (dane tylko w buforze), [1]-istnieje
::----------------------------------------------------------------------------------------------------------------------
_stan:={? var_pres('_a')=type_of(0) || _a || 1 ?};
{? ~DOK.RVAT || return('') ?};
_str:='';
{? _stan
|| _str:=DOK.memo_txt(,1,'PROC_LOG');
   POZF.cntx_psh();
   POZF.index('DOK');
   POZF.prefix(DOK.ref());
   {? POZF.first()
   || {!
      |? {? POZF.EF_PROC
         || _str:=exec('add_str','dok_ksef',_str,POZF.EF_PROC().KOD)
         ?};
         POZF.next()
      !}
   ?};
   POZF.cntx_pop();
   {? DOK.PROC
   || _str:=exec('add_str','dok_ksef',_str,DOK.PROC().KOD)
   ?};
   DOKPOLA.cntx_psh();
   DOKPOLA.index('UNIK');
   DOKPOLA.prefix(DOK.ref());
   {? DOKPOLA.first()
   || {? DOKPOLA.UDS='T'
      || _str:=exec('add_str','dok_ksef',_str,'MR_UZ')
      ?};
      {? DOKPOLA.USL_TUR='T'
      || _str:=exec('add_str','dok_ksef',_str,'MR_T')
      ?}
   ?};
   DOKPOLA.cntx_pop();
   VPOZ.cntx_psh();
   VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
   {? VPOZ.first()
   || GR_VAT.cntx_psh();
      VAT_REJ.cntx_psh();
      RVAT.cntx_psh();
      KVAT.cntx_psh();
      {!
      |? {? VPOZ.GRVAT().KOD='SprzWys'
         || _str:=exec('add_str','dok_ksef',_str,'SW')
         ?};
         {? VPOZ.RVAT().RVAT().KVAT().SYM='_WWspNat'
         || _str:=exec('add_str','dok_ksef',_str,'TT_WNT')
         |? KVAT.SYM='_WWspDot'
         || _str:=exec('add_str','dok_ksef',_str,'TT_D')
         ?};
         VPOZ.next()
      !};
      KVAT.cntx_pop();
      RVAT.cntx_pop();
      VAT_REJ.cntx_pop();
      GR_VAT.cntx_pop()
   ?};
   VPOZ.cntx_pop()
?};
_czy_spr:=DOK.RVAT().RVAT().RT='S';
_czy_wdt:=6+RVAT.KVAT().SYM='_WWspD' | KVAT.SYM='_SprzNOp' | 6+KVAT.SYM='_WWspN';
{? (_czy_spr | _czy_wdt) & +DOK.KH<=10
|| _kh:=exec('FindInSet','#table','KH','SKR',DOK.KH,2,,1);
   {? _kh & exec('FindInSet','#table','KH_DOD','KH_DOD',_kh,REF.FIRMA,"KH_DOD.TP")='P'
   || _str:=exec('add_str','dok_ksef',_str,'TP')
   ?}
?};
_str


\upd_dok_jpk_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Aktualizacja pola DOK.JPK_PROC
::   WE: [_a] - wskazanie na dokument księgowy (DOK.ref())
::       [_b] - kod usuwanej procedury
::----------------------------------------------------------------------------------------------------------------------
_czy_spr:=DOK.RVAT().RVAT().RT='S';
_czy_wdt:=6+RVAT.KVAT().SYM='_WWspD' | KVAT.SYM='_SprzNOp';
_dok:={? var_pres('_a')>0 & type_of(_a)=type_of(null) || _a || DOK.ref() ?};
_del:={? var_pres('_b')>0 & type_of(_b)=type_of('') || _b || '' ?};
{? (_czy_spr | _czy_wdt) & _dok
|| DOK.cntx_psh();
   {? DOK.name()<>ref_name(_dok)
   || DOK.use(ref_name(_dok))
   ?};
   DOK.prefix();
   {? DOK.seek(_dok) & DOK.RVAT
   || _save_str:='B_MPV_PROWIZJA,B_SPV,B_SPV_DOSTAWA,IMP,I_42,I_63';
      _save:=spli_str(_save_str,',');
      _len:=obj_len(_save);
      _save_on:=obj_new(_len);
      {! _ii:=1.._len
      |! _save_on[_ii]:={? (DOK.JPK_PROC+',')*(_save[_ii]+',') || 1 ?}
      !};
      _str:=exec('get_dok_jpk_proc','dok_ksef');
      {? _del<>'' & _str*_del=0
      || {! _ii:=1.._len
         |! {? _del*_save[_ii]
            || _save_on[_ii]:=0
            ?}
         !}
      ?};
      {! _ii:=1.._len
      |! {? _save_on[_ii]
         || _str:=exec('add_str','dok_ksef',_str,_save[_ii])
         ?}
      !};
      _arr:=spli_str(_str,',');
      exec('array_sort','dok_ksef',_arr);
      _str:=exec('array2str','dok_ksef',_arr);
      {? DOK.JPK_PROC<>_str
      || DOK.JPK_PROC:=_str;
         DOK.trig_off('*','*');
         DOK.put();
         DOK.trig_on('*','*')
      ?}
   ?};
   DOK.cntx_pop()
?};
1


\upd_dok_jpk_tp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: Aktualizacja znacznik TP w DOK.JPK_PROC
::----------------------------------------------------------------------------------------------------------------------
_str:=DOK.JPK_PROC;
_str:=exec('del_str','dok_ksef',_str,'TP');
_tp:=exec('get_dok_jpk_proc','dok_ksef',0);
{? _tp<>''
|| _str:=exec('add_str','dok_ksef',_str,_tp)
?};
{? DOK.JPK_PROC<>_str
|| DOK.JPK_PROC:=_str
?}


\add_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Dodaje napis po przecinku o ile już nie istnieje
::   WE: _a - napis
::       _b - dodawany napis
::----------------------------------------------------------------------------------------------------------------------
_str:=_a;
_add:=_b;
{? (','+_str+',')*(','+_add+',')=0
|| _str:=_str+{? _str<>'' || ',' || '' ?}+_add
?};
_str


\del_str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Usuwa napis z elementami oddzielonymi przecinkiem
::   WE: _a - napis z przecinkami
::       _b - usuwany napis
::   WY: Napis po usunięciu danego napisu
::----------------------------------------------------------------------------------------------------------------------
_str:=','+_a+',';
_del:=','+_b+',';
_str:=gsub(_str,_del,',');
_str:=gsub(_str,',,','');
{? _str+1=','
|| _str:=_str-1
?};
{? 1+_str=','
|| _str:=1-_str
?};
_str


\a_trig_dokpola
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Triger po put, add i del tabeli DOKPOLA
::   WE: _a - typ operacji: 1-add, 2-put 3-del
::       _b - czy operacj się powiodła? 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_typ:=_a;
_ok:=_b;
{? _ok
|| _dok:={? _typ=3 || bfld('REK') || DOKPOLA.REK ?};
   {? _dok
   || exec('upd_dok_jpk_proc','dok_ksef',_dok)
   ?}
?};
~~


\array_sort
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Sortuje tablicę
::   WE: _a - tablica
::----------------------------------------------------------------------------------------------------------------------
_array:={? var_pres('_a')>0 & type_of(_a)=type_of(obj_new(1)) || _a || ~~ ?};
{? var_pres('_array')>0
|| _length:=obj_len(_array);
   {? _length>1
   || {! _ii:=_length-1//-1..1
      |! {! _jj:=1.._ii
         |! {? _array[_jj]>_array[_jj+1]
            || _hlp:=_array[_jj];
               _array[_jj]:=_array[_jj+1];
               _array[_jj+1]:=_hlp
            ?}
         !}
      !}
   ?}
?}


\array2str
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Tworzy napis oddzielony separatorem przecina na podstawie tablicy napisów
::   WE: _a - tablica napisów
::----------------------------------------------------------------------------------------------------------------------
_array:=_a;
_str:='';
{! _ii:=1..obj_len(_array)
|! _str+=_array[_ii]+','
!};
_str-1


\dol_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: dołącza nagłówek załącznika dla dokumentu księgowego
::----------------------------------------------------------------------------------------------------------------------
exec('sel_dok','dokum','DOK',1);
exec('wyb_dok','dokum',,0);
DOKUM.AUTOADD:='T';
DOKUM.put()


\find_dokum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Szuka powiązanego powiązanego rekordu dokumentu dla edokumentu
::   _WE: [_a] - Tabela (domyślnie DOK)
::   WY: DOKUM.ref
::----------------------------------------------------------------------------------------------------------------------
_ref:=null();
_record:='';
_tab:={? var_pres('_a')>0 || _a || DOK ?};
{? _tab=DOK
|| _record:=$DOK.ref();
   {? 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'
   || FAKS.cntx_psh();
      FAKS.use('faktu'+(3+(1-DOK.DOKZRODL)));
      {? FAKS.seek(#(4-DOK.DOKZRODL),FAKS.name)
      || _record:=$FAKS.ref()
      ?};
      FAKS.cntx_pop()
   |? DOK.EDOKUM<>''
   || EDOKUM.cntx_psh();
      {? EDOKUM.seek(DOK.EDOKUM,ref_name(DOK.EDOKUM),1)
      || _record:=$EDOKUM.ref()
      ?};
      EDOKUM.cntx_pop()
   ?}
|? _tab=EDOKUM
|| _record:=$EDOKUM.ref()
|? _tab=FAKS
|| _record:=$FAKS.ref()
?};
{? _record<>''
|| DOKUM.index('DOKUM');
   DOKUM.prefix(REF.FIRMA,_record);
   _loop:=DOKUM.first();
   {!
   |? _loop
   |! _seek:={? exec('upgrade2325_blbc1','zbl')
             || {? DOKUM.BL='T' & DOKUM.BLC_KIND='' || _ref:=DOKUM.ref; 1 || 0 ?}
             || {? DOKUM.BL='T' || _ref:=DOKUM.ref; 1 || 0 ?}
             ?};
      _loop:=~_seek & DOKUM.next()
   !}
?};
_ref


\print
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [12.51_148]
:: OPIS: Generuje wydruk html z pliku DOKUMZ.DOKUM
::   WE: _a - Wskazanie na akronim tabel z załącznikami DOKUMZ lub EDOKUMZ
::   WY:
:: ~OST: INBLGET,INCLIEXEC,INFCOPY,INFERASE,INFEXISTS,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? _a='DOKUMZ' || _btab:=.DOKUMZ |? _a='EDOKUMZ' || _btab:=.EDOKUMZ || _btab:=.DOKUM ?};
VAR_DEL.delete('__keepfile');
__keepfile:=_tmpdir:=fmk_tmp_dir(0);
_interm:=exec('interm','#system');
{? _interm
|| {? type_of(_tmpdir) <> type_of(~~)
   || _fdir:=_tmpdir.get_path
   || _fdir:=''
   ?}
|| _fdir:=tmp_dir()
?};
_fin:=_fdir+'/ksef.xml';
_btab.bl_get({? _a='DOKUMZ' || 'DOKUM' |? _a='EDOKUMZ' || 'EDOKUM' || 'DOKUM' ?},{? _interm || '' || '@' ?}+_fin,0);
{? {? _a='DOKUMZ'
   || _btab.BL_TYPE=exec('bl_type_ksef_inv','zbl_dok') |
      _btab.BL_TYPE=exec('bl_type_ksef_inv_corr','zbl_dok') |
      _btab.BL_TYPE=exec('bl_type_ksef_dks_inv','zbl_dok') |
      _btab.BL_TYPE=exec('bl_type_ksef_dks_inv_corr','zbl_dok')
   |? _a='EDOKUMZ' | _a='DOKUM'
   || exec('get_typ','dok_ksef',_fin)='FA'
   ?}
|| _fxsl:={? (var_pres('_btab.SCHEMAT')>0 & _btab.SCHEMAT<>'') || _btab.SCHEMAT-4+'.xsl' || '' ?};
   {? _fxsl='' & _a='DOKUMZ' || _fxsl:=(exec('schemat','zbl_dok',_btab.DOK,_btab.ref()))-4+'.xsl' ?};
   {? _fxsl='' || _fxsl:='Schemat_FA_VAT(1)_v9-0E.xsl' ?};
   _fout:=_fdir+'//ksef'+$SYSLOG.tm_stamp()+'.html';
   {? fexists(_fxsl,1)
   || {? ~fexists({? _interm || '' || '@' ?}+_fdir+'/'+_fxsl)
      || {? fcopy(_fxsl,{? _interm || '' || '@' ?}+_fdir+'/'+_fxsl,1,0,1)=0
         || FUN.emsg('Błąd podczas kopiowania pliku '@+_fxsl+' z serwera.');
            return(~~)
         ?}
      || 1
      ?};
      {? _interm
      || exec('xml2html','xml',_fin,_fdir+'/'+_fxsl,_fout,_fdir)
      || exec('xml2html','xml',_fin,_fdir+'/'+_fxsl,_fout)
      ?};
      ferase({? _interm || '' || '@' ?}+_fdir+'/'+_fxsl,0);
      ferase({? _interm || '' || '@' ?}+_fin,0);
      {? fexists({? _interm || '' || '@' ?}+_fout)=0
      || FUN.emsg('Błąd podczas tworzenia wyduku. Brak pliku wynikowego.'@)
      || {? _interm || dlg_save(_fout) || cli_exec(_fout) ?}
      ?}
   || echo('Brak na serwerze pliku '@+_fxsl+'.');
      FUN.info('Wydruk niedostępny.'@);
      echo()
   ?}
|| FUN.info('Wydruk dostępny tylko dla plików KSeF.'@)
?}


\set_dok_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Ustawia flagę czy dokument jest wysyłany do KSeF
::   WE: 0/1 - ustal tylko statusy
::  OLD: \is_dok_ksef/dok_fks1.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=0 ?};
_set_ksef:=0;
_statksef:='';
{? 1+DOK.DOKZRODL='D' | 1+DOK.DOKZRODL='K'
|| FAKS.cntx_psh();
   {? FAKS.seek(#(4-DOK.DOKZRODL),'faktu'+(3+(1-DOK.DOKZRODL)),1)
   || _set_ksef:=FAKS.STATKSEF<>'' & FAKS.STATKSEF<>exec('ksef_donot_applay','zbl_stat');
      {? _set_ksef
      || _statksef:=FAKS.STATKSEF;
         {? ~_a || DOK.KSEFAUTH:=FAKS.KSEFAUTH ?}
      ?}
   ?};
   FAKS.cntx_pop()
|| _kh:={? DOK.KH_ODB().ROZ_NAL='T' || DOK.KH_ODB().KH_LNK || exec('FindInSet','#table','KH','KOD1',DOK.WYS().KOD) ?};
   KH_DOD.cntx_psh();
   {? exec('kh_dod_ini','kontrahent',_kh,0)
   || _set_ksef:=KH_DOD.KSEFKH='T' & KH_DOD.KSEFKHOD<=DOK.DTO
   ?};
   KH_DOD.cntx_pop();
   {? _set_ksef || _set_ksef:=exec('FindAndGet','#table',DOK_REJ,DOK.DOK_REJ,,"(DOK_REJ.KSEF='T')",0) ?}
?};
{? _statksef=''
|| {? exec('typ','jpk_v',DOK.RVAT().RVAT)='S'
   ||  _statksef:=exec('ksef_donot_applay','zbl_stat');
      {? _set_ksef & DOK.MANSKSEF<>'T'
      || _statksef:=exec('before_send','zbl_stat')
      ?}
   ?}
?};
{? ~_a || DOK.KSEF:={? _set_ksef || 'T' || 'N' ?} ?};
DOK.STATKSEF:=_statksef


\znacznik_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [RR.xx]
:: OPIS: Znacznik faktury zaliczkowej z KSeF
::----------------------------------------------------------------------------------------------------------------------
'[KSeF]'


\dokpola_memo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51_148]
:: OPIS: Formuła na przycisk w oknie definicji danych dodatkowych faktury
::   WE: _a - tabela
::       _b - akronim pola typu SYS_MEMO tabeli
::----------------------------------------------------------------------------------------------------------------------
_tabela:=_a;
_fld:=_b;
_znksef:=exec('znacznik_ksef','dok_ksef');
_znlen:=+_znksef;
_tit_sel:='Brak';
_tit_edit:='Brak';
{? _fld='WZ'
|| _tit_sel:='Numery dokumentów magazynowych';
   _tit_edit:='Numer dokumentu magazynowego'
|? _fld='NRFAZAL'
|| _tit_sel:='Numery faktur zaliczkowych';
   _tit_edit:='Numer faktury zaliczkowej'
|| _tit_sel:='Numery KSeF faktur korygujących';
   _tit_edit:='Numer KSeF faktury korygującej'
?};
_tit_fld:='Numer';

_tab:=tab_tmp(1,
   'TXT','STRING[255]','Opis',
   'KSEF','STRING[1]','Dokument KSeF'
);
_wer:=_tab.mk_sel(_tit_sel,'P',,'#dokpolamemos',,,,,'U');
_tab.win_fld(_wer,,'TXT',,,30,,,_tit_fld);
{? _fld='NRFAZAL'
|| _tab.win_fld(_wer,,'KSEF',,,10,,,'Numer KSeF'@,,,2,,"'T'","'N'")
?};
_tab.win_act(_wer,,'Dołącz',,,,"","",1);
_tab.win_act(_wer,1,'Dołącz',,,,"","",1);
_tab.win_act(_wer,,'Popraw',,,,"","");
_tab.win_act(_wer,,'Usuń',,,,"","");
_tab.win_act(_wer,,'Formuła','Zapisz',,,"sel_exit()","");
_tab.win_act(_wer,1,'Formuła','Zapisz',,,"sel_exit()","");
_tab.win_act(_wer,,'Rekord',,,,"","
   _ret:=chk_rec();
   {? _ret=''
   || _tab:=cur_tab();
      _ref:=_tab.ref();
      _tab.cntx_psh();
      _tab.prefix(_tab.TXT,);
      {? _tab.first() & (-menu_txt()<>'popraw' | _tab.ref()<>_ref)
      || FUN.info('Istnieje już numer o symbolu: '+_tab.TXT);
         _ret:='TXT'
      ?};
      _tab.cntx_pop()
   ?};
   _ret
");
_tab.win_btn(_wer,'text=Dołącz','menu:D');
_tab.win_btn(_wer,'text=Popraw','menu:P');
_tab.win_btn(_wer,'text=Usuń','menu:U');
_zapisz:=_tab.win_btn(_wer,'text=Zapisz,panel=bottom','menu:Z');
_tab.btn_opt(_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
_tab.win_sel(_wer);

_red:=_tab.mk_edit(_tit_edit,,'#dokpolamemoe');
_tab.win_efld(_red,,'TXT',,,30,,,_tit_fld);
{? _fld='NRFAZAL'
|| _tab.win_efld(_red,,'KSEF',,,30,,,'Numer KSEF',,,'check-box',,"'T'","'N'")
?};
_tab.win_edit(_red);

_zapisz:=_tab.win_ebtn(_red,'text=%1'['Zapi&sz'@],'key:F2');
_tab.btn_eopt(_red,_zapisz,'tooltip='+exec('help_red_ok','#window','Z'));
_anuluj:=_tab.win_ebtn(_red,'text=%1,btn_label_align=center,panel=bottom,align=end,display=1'['&Anuluj'@],'key:Esc');
_tab.btn_eopt(_red,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));

_txt:=_tabela.memo_txt(,0,_fld);
_arr:=spli_str(_txt,'\n');
{! _ii:=1..obj_len(_arr)
|! {? _arr[_ii]<>''
   || _tab.blank(1);
      _tab.KSEF:={? _znlen+_arr[_ii]=_znksef || 'T' || 'N' ?};
      {? _tab.KSEF='T'
      || _tab.TXT:=_znlen-_arr[_ii]
      || _tab.TXT:=_arr[_ii]
      ?};
      _tab.add()
   ?}
!};
_gray:={? DOK.A='T' || 'DPUZ:DZ' || '' ?};
{? _gray=''
|| _gray:={? PAR_SKID.get(441)='T' & exec('status_bl','dok_fks1')
          || 'DUZ:DZ'
          || ''
          ?}
?};
{? var_pres('editdpolazn')>0 & editdpolazn=1 || _gray:='DPUZ:DZ' ?};
{? _a<>WEB_DPLA
|| _tab.actions_grayed(_tab.win_sel('?'),_gray)
?};
_bsize:=_tab.size();
_btxt:='';
{? _tab.first()
|| {!
   |? _btxt+={? _tab.KSEF='T' || _znksef || '' ?}+_tab.TXT+'\n';
      _tab.next()
   !};
   _btxt:=_btxt-1;
   _tab.first()
?};
{! |?
   {? _tab.select()
   || _txt:='';
      {? _tab.first()
      || {!
         |? _txt+={? _tab.KSEF='T' || _znksef || '' ?}+_tab.TXT+'\n';
            _tab.next()
         !};
         _txt:=_txt-1
      ?};
      {? _tabela=DOKPOLA & menu_txt()<>'' & 1+(-menu_pth())<>'d' & var_pres('__STORNO')<=0
      || exec('edit','dok_ksef',1)
      ?};
      _tabela.memo_set(_txt,_fld);
      {? menu_txt()<>'' & 1+(-menu_pth())<>'d' & var_pres('__STORNO')<=0
      || {? _tabela=DOKPOLA
         || DOKPOLA.cntx_psh();
            exec('edit','dok_ksef',2);
            DOKPOLA.cntx_pop()
         |? ~(var_pres('dob_dek')>0 & _tabela=DOK)
         || _tabela.memo_put(,_fld)
         ?}
      ?};
      0
   || _pytaj:=0;
      _pytaj:=_tab.size<>_bsize;
      {? ~_pytaj
      || _txt:='';
         {? _tab.first()
         || {!
            |? _txt+=_tab.TXT+'\n';
               _tab.next()
            !};
            _txt:=_txt-1
         ?};
         _pytaj:=_txt<>_btxt
      ?};
      _wynik:=0;
      {? _pytaj || _wynik:=~FUN.ask('Niezapisane zmiany zostaną utracone. Czy na pewno wyjść z okna?'@) ?};
      _wynik
   ?}
!};
''


\be_dokpola_memo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51_148]
:: OPIS: Przed redakcją DOKPOLA.WZ oraz DOKPOLA.NRFAZAL
::----------------------------------------------------------------------------------------------------------------------
0


\dok_pref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [12.51]
:: OPIS: Ustawia dziedzinę dla widoku dokumentów KSeF
::   WE: _a - 1 - domyślnie, dokumenty KSeF
::            2 - dokumenty nie KSeF
::            3 - wszystkie
::----------------------------------------------------------------------------------------------------------------------
{? var_press('_a')=type_of(1) & _a<>1
|| {? _a=2
   || ROZNE.ZAKRES:=2;
      {? OPERATOR.DEPT
      || DOK.index('O_KF'); DOK.prefix(OPERATOR.DEPT,'N',)
      || DOK.index('KF_REJ'); DOK.prefix('N',)
      ?}
   |? _a=3
   || ROZNE.ZAKRES:=3;
      {? OPERATOR.DEPT
      || DOK.index('O_REJ'); DOK.prefix(OPERATOR.DEPT,)
      || DOK.index('REJ'); DOK.prefix()
      ?}
   ?}
|| ROZNE.ZAKRES:=1;
   {? OPERATOR.DEPT
   || DOK.index('O_KF'); DOK.prefix(OPERATOR.DEPT,'T',)
   || DOK.index('KF_REJ'); DOK.prefix('T',)
   ?}
?};
AreaTitle.setTitle(exec('ksf_scope','dok_ksef'))


\ksf_scope
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.37_14]
:: OPIS: Zwraca zakres widoku Dokumenty KSeF
::----------------------------------------------------------------------------------------------------------------------
_title:='';
{? ROZNE.ZAKRES=1
|| _title:='wysyłane do KSeF'@
|? ROZNE.ZAKRES=2
|| _title:='niewysyłane do KSeF'@
|? ROZNE.ZAKRES=3
|| _title:='wszystkie'@
|| _title:='wysyłane do KSeF'@
?};
_title


\is_ksef_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła sprawdzająca, czy dokument ksef został wygenerowany w obszarze dokumentów sprzedaży (logistyka)
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_faks:=exec('find_faks','dok_fks1');
_ref:={? _faks<>null() || $_faks || $DOK.ref() ?};
DOKUM.cntx_psh(); DOKUMZ.cntx_psh();
DOKUM.index('BL_2'); DOKUMZ.index('KIND');
DOKUM.prefix(REF.FIRMA,'T',_ref);
{? DOKUM.first()
|| {!
   |? {? ~exec('upgrade2325_blbc1','zbl') | DOKUM.BLC_KIND=''
      || DOKUMZ.prefix(DOKUM.ref());
         _result:=DOKUMZ.find_key(exec('bl_type_ksef_inv','zbl_dok'),) |
                  DOKUMZ.find_key(exec('bl_type_ksef_inv_corr','zbl_dok'),);
         0
      || DOKUM.next()
      ?}
   !}
?};
DOKUM.cntx_pop(); DOKUMZ.cntx_pop();
_result


\is_ksef
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [21.37]
:: OPIS: Formuła sprawdzająca, czy dokument ksef został wygenerowany
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_faks:=exec('find_faks','dok_fks1');
_ref:={? _faks<>null() || $_faks || $DOK.ref() ?};
DOKUM.cntx_psh(); DOKUMZ.cntx_psh();
DOKUM.index('BL_2'); DOKUMZ.index('KIND');
DOKUM.prefix(REF.FIRMA,'T',_ref);
{? DOKUM.first()
|| {!
   |? {? ~exec('upgrade2325_blbc1','zbl') | DOKUM.BLC_KIND=''
      || DOKUMZ.prefix(DOKUM.ref());
         _result:=DOKUMZ.find_key(exec('bl_type_ksef_inv','zbl_dok'),) |
                  DOKUMZ.find_key(exec('bl_type_ksef_inv_corr','zbl_dok'),) |
                  DOKUMZ.find_key(exec('bl_type_ksef_dks_inv_corr','zbl_dok'),) |
                  DOKUMZ.find_key(exec('bl_type_ksef_dks_inv','zbl_dok'),);
         0
      || DOKUM.next()
      ?}
   !}
?};
DOKUM.cntx_pop(); DOKUMZ.cntx_pop();
_result


\ef_proc_default
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [12.51]
:: OPIS: Domyślne wartości dla pola POZF.EF_PROC
::----------------------------------------------------------------------------------------------------------------------
{? DOK.DOK_REJ().JPK_FA_T='S' & PAR_SKID.get(449)='T'
||
   DOK.RVAT().RVAT().KVAT();
   {? POZF.EF_PRVAT=null() & DOK.PROC=null()
   || _rt:=RVAT.RT;
      _sv:=POZF.SV().KOD;
      _fa_prvat:=
:: 1 - Stawka 0% stosowana w ramach sprzedaży krajowej
         {? _rt='S' & _sv=' 0%'                        || '1'
:: 2 - Stawka 0% - wewnątrzwspólnotowa dostawa towarów
         |? 6+KVAT.SYM='_WWspD' & _sv=' 0%'            || '2'
:: 3 - Stawka 0% - eksport towarów
         |? _rt='E' & _sv=' 0%'                        || '3'
:: 4 - Dostawa, świadczenie poza terytorium kraju
         |? 6+KVAT.SYM='_WWspD'                        || '4'
:: 5 - Świadczenie usług z art. 100 ust. 1 pkt 4 ustawy
         |? RVAT.KVAT().SYM='_WWspNus'                   || '5'
:: 6 - Towar/usługa wymienione w załączniku 15
         |? KVAT.SYM='ZakOpod' | _sv=' -o'             || '6'
:: 7 - Pozostała sprzedaż krajowa
                                                       || '7'
         ?};
      exec('FindInSet','#table','JPK_SLO','KOD',_fa_prvat,1,"POZF.EF_PRVAT:=JPK_SLO.ref()",1,'PROC_VAT')
   ?};
   {? POZF.EF_PROC=null() & DOK.PROC=null()
   ||
      _fa_proc:=
:: SW - Dostawa w ramach sprzedaży wysyłkowej z terytorium kraju, o której mowa w art. 23 ustawy
:: EE - Świadczenie usług telekomunikacyjnych, nadawczych i elektronicznych, o których mowa w art. 28k ustawy
:: TT_D - W przypadku faktur wystawianych w procedurze uproszczonej przez drugiego w kolejności podatnika, o którym mowa w art. 135 ust. 1 pkt 4 lit. b i c oraz ust. 2, zawierającej adnotację, o której mowa w art. 136 ust. 1 pkt 1 i stwierdzenie, o którym mowa w art. 136 ust. 1 pkt 2 ustawy
         {? KVAT.SYM='_WWspDot' || 'TT_D'
:: B_SPV - Transfer bonu jednego przeznaczenia dokonany przez podatnika działającego we własnym imieniu, opodatkowany zgodnie z art. 8a ust. 1 ustawy
:: B_SPV_DOSTAWA - Dostawa towarów oraz świadczenie usług, których dotyczy bon jednego przeznaczenia na rzecz podatnika, który wyemitował bon zgodnie z art. 8a ust. 4 ustawy
:: B_MPV_PROWIZJA - Świadczenie usług pośrednictwa oraz innych usług dotyczących transferu bonu różnego przeznaczenia, opodatkowane zgodnie z art. 8b ust. 2 ustawy
:: w pozostałych przypadkach
         || ''
         ?};
      {? _fa_proc<>'' || exec('FindInSet','#table','JPK_SLO','KOD',_fa_proc,1,"POZF.EF_PROC:=JPK_SLO.ref()",1,'PROC') ?}
   ?}
||
:: I_42 - Wewnątrzwspólnotowa dostawa towarów następująca po imporcie tych towarów w ramach procedury celnej 42 (import)
:: I_63 - Wewnątrzwspólnotowa dostawa towarów następująca po imporcie tych towarów w ramach procedury celnej 63 (import)
   ~~
?};
~~


\get_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2009+]
:: OPIS: Zwraca typ formularza e-Deklaracji
::  OLD: \get_typ/skid_xml.fml
::----------------------------------------------------------------------------------------------------------------------
_f:=fopen(_a,'r',0); _kod:='';
{? _f
|| {!
   |? _r:=fread(_f);
      {? _r<>'\n'
      || {? _r*'<KodFormularza'
         || _p:=_r*'>';
            _r:=_p-_r;
            _p:=_r*'<';
            _kod:=_p+_r-1
         ?};
         _kod=''
      ?}
   !};
   fclose(_f)
?};
_kod


\pdf_test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Test PDF
::----------------------------------------------------------------------------------------------------------------------
{? _a='DOKUMZ' || _btab:=.DOKUMZ |? _a='EDOKUMZ' || _btab:=.EDOKUMZ || _btab:=.DOKUM ?};
VAR_DEL.delete('__keepfile');
__keepfile:=_tmpdir:=fmk_tmp_dir(0);
_interm:=exec('interm','#system');

_fdir:=_tmpdir.get_path;

_fin:=_fdir+'/ksef.xml';
_btab.bl_get({? _a='DOKUMZ' || 'DOKUM' |? _a='EDOKUMZ' || 'EDOKUM' || 'DOKUM' ?},_fin,0);
_fxsl:={? (var_pres('_btab.SCHEMAT')>0 & _btab.SCHEMAT<>'') || _btab.SCHEMAT-4+'.xsl' || 'Schemat_FA_VAT(1)_v9-0E.xsl' ?};
   _fout:=_fdir+'//ksef'+$SYSLOG.tm_stamp()+'.html';
   {? fexists(_fxsl,1)
   || {? ~fexists(_fdir+'/'+_fxsl)
      || {? fcopy(_fxsl,_fdir+'/'+_fxsl,1,0,1)=0
         || FUN.emsg('Błąd podczas kopiowania pliku '@+_fxsl+' z serwera.');
            return(~~)
         ?}
      || 1
      ?};
      _fpdf:=_fdir+'/'+'ksef_pdf'+$SYSLOG.tm_stamp()+'.pdf';
      exec('xml2html','xml',_fin,_fdir+'/'+_fxsl,_fout,_fdir);
      exec('pdf2','dok_ksef',1,_fout,_fpdf,_fdir);

      ferase(_fdir+'/'+_fxsl,0);
      ferase(_fin,0);
      {? fexists(_fout)=0
      || FUN.emsg('Błąd podczas tworzenia wyduku. Brak pliku wynikowego.'@)
      ?}
   || echo('Brak na serwerze pliku '@+_fxsl+'.');
      FUN.info('Wydruk niedostępny.'@);
      echo()
   ?};

HTML_PDF.ID:=_b;
::HTML_PDF.DOKUM:=DOKUMZ.uidref();
HTML_PDF.add();
HTML_PDF.bl_put('HTML',_fout);
HTML_PDF.bl_put('PDF',_fpdf)


\pdf2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST Obsługa zmiany HTML na PDF
::----------------------------------------------------------------------------------------------------------------------
_fdir:=_d;
exec('MBJAR','#object');
fcopy('htmltopdf.jar',{? var_press('_d')<=0 || '@' || '' ?}+_fdir+'/'+'htmltopdf.jar',1,0,1);
_options:='runOnAppServer='+'1'+',wait=1';
jexec(_fdir,_options,'-Dfile.encoding=utf-8','-jar',_fdir+'/'+'htmltopdf.jar',_b,_c,'1000','1500');
_fdir


\t_pdf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
DOKUMZ.cntx_psh(); DOKUMZ.seek('dokumz  00000197');
{! _i:=1..101 |!
exec('pdf_test','dok_ksef','DOKUMZ',1+_i)
!};
DOKUMZ.cntx_pop()


\html_b_gen
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
_xml:=fopen(_a,'b',0,,1);
_xml_b:=base64('encode',_xml);
fclose(_xml);
_html:=exec('html_template2','dok_ksef',_xml_b);
_wyn:=fopen(_b,'Uw',0);
fwrite(_wyn,_html);
fclose(_wyn);
_b


\html_template
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
'<!DOCTYPE html>'
'<html>'
'<head>'
    '<title>Businesscheck | Wyniki kontroli</title>'
    '<link rel="icon" type="image/x-icon" href="https://test.businesscheck.cloud/favicon.ico">'
'</head>'
'<body>'
    '<div class="content-body">'
        '<iframe id="CONTENT-FRAME" src="https://test.businesscheck.cloud/result" frameborder="0" class="content-frame"></iframe>'
    '</div>'
    '<style>'
        'body,'
        'html {'
            'width: 100%%;'
            'height: 100%%;'
            'margin: 0;'
            'padding: 0'
        '}'

        '.content-body {'
            'background-color: #fafafa;'
            'display: flex;'
            'width: 100%%;'
           'height: 100%%;'
            'flex-direction: column;'
            'overflow: hidden;'
        '}'

        '.content-frame {'
            'flex-grow: 1;'
            'border: none;'
            'margin: 0;'
            'padding: 0;'
        '}'
   '</style>'
    '<script type="text/javascript">'
        'window.addEventListener("message", function (e) {'
            'const data = e.data;'
            'if (data.iframeheight) {'
                'document.getElementById("CONTENT-FRAME").style.height = (parseInt(data.iframeheight) + 120) + "px";'
            '} else if (data.frame_loaded) {'
                'const content = {"results": %1 } \n'
                'document.getElementById("CONTENT-FRAME").contentWindow.postMessage(content, "*");'
            '}'
        '});'
    '</script>'
'</body>'

'</html>'[exec('json_template','dok_ksef',_a)]


\json_template
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
'{'
   '"transaction_id": "9edf0662-94f7-5341-80c9-5adea4afb5cc",'
   '"date": "2023-04-21T06:33:00.412Z",'
   '"status": "DONE",'
   '"result": "STOP",'
   '"document_id": "9edf0662-94f7-5341-80c9-5adea4afb5cc",'
   '"checklist": ['
       '{'
           '"code": "BC-FRM-DSP",'
           '"rule_version_date": "2023-02-05",'
           '"result": "OK-1",'
           '"paths": ['
               '"Faktura/Fa/P_6",'
               '"Faktura/Fa/FaWiersze/FaWiersz[0]"'
           '],'
           '"message": "Kontrola poprawna: Wszystko w porządku"'
       '}'
   '],'
   '"doc_types": ['
       '"FPLN"'
   '],'
   '"xsl": "https://businesscheck-test-s3-bucket-xsl.s3.eu-central-1.amazonaws.com/KSEF-2023-01-01-stylall.xsl",'
   '"schema": "KSEF",'
   '"tenant": "795d07e38f7ec341b9d2cf49b5ad0cf2135b3a0ed5773a6ee86b63bfd5f28852",'
   '"doc_uri": "https://businesscheck-test-s3-bucket-test-upload-validation-document.s3.amazonaws.com/KSeF_INV_00024_FVS202300016E.xml-1682058774956?X-Amz-Algorithm=AWS4-HMAC-SHA256&X-Amz-Credential=ASIAUYEPU3RO46WXRIIF%2F20230421%2Feu-central-1%2Fs3%2Faws4_request&X-Amz-Date=20230421T063256Z&X-Amz-Expires=3600&X-Amz-SignedHeaders=host&X-Amz-Security-Token=IQoJb3JpZ2luX2VjEKf%2F%2F%2F%2F%2F%2F%2F%2F%2F%2FwEaDGV1LWNlbnRyYWwtMSJHMEUCIC9eu92Ktp%2BQGB0mE1tAZ9Y%2BW6xOcUv2qUm2lzbmfAhrAiEAi%2BWmH4HCs3EW%2FWxx4kUKMypo6qGQ3mm%2BI56OOI5qchoqswMIoP%2F%2F%2F%2F%2F%2F%2F%2F%2F%2FARAAGgwzMjY3MTg3NzQzNjUiDDjape58y07uuPEhVyqHAyJ5d5xgDWWvezBGbUDFcH%2F4nRe7g2l98ZUoo4gY%2B93l4Gakna%2Fh%2BNawxurCabHccHwgc%2FE%2BhpbznKuonlFjnoYjQ4kzRfEM0bO3CU4o5baTzXWwRu02bmB06nXyPqgUSRN7jIDZvzRYSB7JLBQXp1ZyGDnHa0H5Irel%2F7m%2FHwC6BQV8lFdNXxa9Edc%2BW6tos5k4IhFhuWm1qsO%2FoikprwaP6KDvBsiZq1GGoyJkdzGcS92ZVTybCAkH639kxecv5ZMgkJUzatFExnq3J2J2vQAwykmxm%2BdTnwrJ5uv3Y843yIMdIMUM%2FGmhzSR7DXqh3g8KMUBW6oFVtRc%2BW0lCUUTNlsD%2BWPqq0uZB%2FZqZainj9UQd%2BKPkW5%2F%2BXTJLSUsM%2FNE3pQH8Xyq57PBGknNtVhz5wx8GGb5JhnpGU7fN1lCe1gxzgsNGSQsvOTVJFSA%2FNc%2F%2BcU%2BkpF8TrvIgb7PJ7ms%2FaAkNhy%2BrSe9KlKiBOwUnQCN%2FbViHrqEjAtRp%2BQcnKpEiEbvEsikwl9yIogY6nQHFA%2Bi0Ye10Vbj%2BK3TFdLRWA7VQLuXeD1mm85y47p2nImxMU6tz%2BuDUJ7fnK%2B%2FWLG9oFuVjs%2FSAwqqCLJJ4HbbT4M6Gcx6i4v0nBaDaXjbhiy8dzkI1s%2FYuinxNM%2FbTAWp%2FWrhGUIX6gLGrOnL79JDI0PwkLMtrePcqpipYSqyrNbk%2FbIqOpORjbfuUk8ZrPtC2rP4qRUNKUCgtYWH9&X-Amz-Signature=4d3de2239679f589b974f1855a276d51ca29c5ce3ee3181d5a8cb5f898b534a6",'
   '"duration": 3401,'
   '"url_expression": "https://test.businesscheck.cloud/test-KSEF/9edf0662-94f7-5341-80c9-5adea4afb5cc?",'
   '"document": "%1",'
   '"template": "https://businesscheck-dev-s3-bucket-xsl.s3.eu-central-1.amazonaws.com/KSEF-TEST-res-template.html"'
  '}'[_a,'%2']


\html_template2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
_html:=fopen('KSEF-TEST-res-template.html','Ur',1);
_wiersz:=fread(_html);
_wyn:='';
{! |?
   _wyn+=_wiersz;
   _wiersz:=fread(_html);
   {? 7+_wiersz='</html>'
   || _wyn+=_wiersz; 0
   |? _wiersz+15='(content, "*");'
   || _wyn+='\n'; 1
   || 1
   ?}
!};
_wyn;
_json:=exec('json_template2','dok_ksef',_a);
_wyn:=gsub(_wyn,'$content',_json);
fclose(_html);
_wyn


\json_template2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
_json:=fopen('json_template.json','Ur',1);
_wiersz:=fread(_json);
_wyn:='';
{! |?
   _wyn+=_wiersz;
   _wiersz:=fread(_json);
   {? 1+_wiersz='}'
   || _wyn+=_wiersz; 0
   || 1
   ?}
!};
_wyn;
_wyn:=gsub(_wyn,'$content',_a);
fclose(_json);
_wyn


\test_html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
{? _a='DOKUMZ' || _btab:=.DOKUMZ |? _a='EDOKUMZ' || _btab:=.EDOKUMZ || _btab:=.DOKUM ?};
VAR_DEL.delete('__keepfile');
__keepfile:=_tmpdir:=fmk_tmp_dir(0);

_fdir:=_tmpdir.get_path;

_fin:=_fdir+'/ksef.xml';
_btab.bl_get({? _a='DOKUMZ' || 'DOKUM' |? _a='EDOKUMZ' || 'EDOKUM' || 'DOKUM' ?},_fin,0);
_fout:=_fdir+'//ksef'+$SYSLOG.tm_stamp()+'.html';
exec('html_b_gen','dok_ksef',_fin,_fout);

ferase(_fin,0);



HTML_PDF.ID:=_b;
::HTML_PDF.DOKUM:=DOKUMZ.uidref();
HTML_PDF.add();
HTML_PDF.bl_put('HTML',_fout);
ferase(_fout,0)


\t_html
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
DOKUMZ.cntx_psh(); DOKUMZ.seek('dokumz  00000197');
{! _i:=1..101 |!
exec('test_html','dok_ksef','DOKUMZ',1500+_i)
!};
DOKUMZ.cntx_pop()


\test_view
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST Generuje okienko z podglądem dokumentu
::----------------------------------------------------------------------------------------------------------------------
DOKUM.cntx_psh(); DOKUM.use('dokum'); DOKUM.prefix();
exec('web_view','zbl_dok');
_wer_in_all:=exec('wer_in','zbl_dok','ZBL_DOK_ALL');
VAR_DEL.delete('__choosetype','__filepathoverride');
__choosetype:={? var_pres('_a')>0 || _a || 0 ?};
__filepathoverride:={? var_pres('_b')>0 || _b || '' ?};
::
:: Dokumenty Businesslink - okno grupowe
::
_win_grp:=DOKUM.grp_make('Dokumenty'@,,'identideid',,,"exec('exit','zws',_a)");
DOKUM.win_sel(_win_grp);
::
:: przychodzące
::
_far_in:=$('
   {? DOKUM.sel_size()=0
         &
      {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
   ||
      exec(''web_view_load'',''dok_ksef'',__choosetype,__filepathoverride)
   ||
      tab_hide(0,1,''atta'');
      exec(''web_view_load'',''dok_ksef'',1,__filepathoverride);
      tab_hide(1,,''view'')
   ?}
   '
);
_fb:=$('
   DOKUM.index(''BL'');
   DOKUM.prefix(''T'',REF.FIRMA,''P'')
   '
);

DOKUM.grp_sel(_win_grp,,_wer_in_all,'Przychodzące'@,_far_in,,,,_fb,,,,'maximized','bl_wer_in');
:: podgląd załącznika
::
DOKUM.grp_splt(_win_grp,'panel0','vertical','view',',60%');
DOKUM.grp_ctr(_win_grp,,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,'Podgląd'@,,,,100,50,"","",,'maximized');
::


DOKUM.win_sel(_win_grp);
DOKUM.select();
DOKUM.cntx_pop()


\web_view_load
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
_ret:=~~;
_loaded:=0;
_interm:=exec('interm','#system');
_a:={? var_pres('_a')>0 || _a || 0 ?};
{? 1
|| _put:=0;
   {? DOKUM.name()='dokum'
   || DOKUMZ.index('DISP');
      DOKUMZ.prefix(DOKUM.ref());
      {? DOKUMZ.first()
      || {!
         |? {? DOKUMZ.BL_TYPE=exec('bl_type_ksef_inv','zbl_dok') |
               DOKUMZ.BL_TYPE=exec('bl_type_ksef_inv_corr','zbl_dok') |
               DOKUMZ.BL_TYPE=exec('bl_type_ksef_dks_inv','zbl_dok') |
               DOKUMZ.BL_TYPE=exec('bl_type_ksef_dks_inv_corr','zbl_dok')
            || {? 1
               || _filename:=exec('get_unique_file_name','#file',DOKUMZ.bl_info('DOKUM','NAME'),'#!Tmp/');
                  _filepath:=exec('tmp_filepath','#file',_filename);
                  _loaded:=exec('bl_get','#blob',DOKUMZ.DOKUM,,_filename);
                  0
               || _tmpdir:=fmk_tmp_dir(0);
                  {? type_of(_tmpdir) <> type_of(~~)
                  || _fdir:=_tmpdir.get_path
                  || _fdir:=''
                  ?};
                  _filepath:=_fdir+'/ksef'+$SYSLOG.tm_stamp()+'.xml';
                  _filepath2:=_fdir+'/ksef'+$SYSLOG.tm_stamp()+'.html';
                  _loaded:=exec('bl_get','#blob',DOKUMZ.DOKUM,_fdir,'ksef'+$SYSLOG.tm_stamp()+'.xml');
                  0
               ?}
            || DOKUMZ.next()
            ?}
         !}
      ?}
   ?}
?};
{? 1
|| {? _loaded>0
   || __CTR_WB.DOKUMI:=1;
      {? 1
      || _filename2:=exec('get_unique_file_name','#file','ksef_view.html','#!Tmp/');
         _filepath2:=exec('tmp_filepath','#file',_filename2)
      ?};
      {? _a=1
      || _filepath:=exec('html_b_gen','dok_ksef',_filepath,_filepath2)
      |? _a=2
      || _filepath:=exec('gen_html_prev','dok_ksef',_filepath,_filepath2,1)
      |? _a=10
      || _filename:=exec('get_unique_file_name','#file','sample.html','#!Tmp/');
                  _filepath:=exec('tmp_filepath','#file',_filename);
         fcopy(_b,_filepath,,,1)
      || _filepath:=exec('gen_html_prev','dok_ksef',_filepath,_filepath2,0)
      ?};
      _filepath:=gsub(_filepath,'\\','\\\\');
      ctr_call(':'+__CTR_WB.WIN_ID,'ctr_id','navigate',_filepath)
   || __CTR_WB.DOKUMI:=0;
      ctr_call(':'+__CTR_WB.WIN_ID,'ctr_id','setHTMLContent', '<html></html>');
      _ret:='#disable'
   ?}
?};
_ret


\prev_template2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
'<!DOCTYPE html>'
'<html>'
'<head>'
    '<title>Businesscheck | Podgląd dokumentu</title>'
    '<link rel="icon" type="image/x-icon" href="https://test.businesscheck.cloud/favicon.ico">'
    '<script type="text/javascript"'
        'src="https://businesscheck-test-s3-bucket-xsl.s3.eu-central-1.amazonaws.com/preview-lib.js"></script>'
'</head>'
'<body>'
    '<span id="doc-container"> </span>'
    '<script type="text/javascript"> '
        'const encodedDocument = "%1" \n'
        'generatePreview(encodedDocument)'
    ' </script>'
'</body>'
'</html>'[_a]


\gen_html_prev
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
_xml:=fopen(_a,'b',0,,1);
_xml_b:=base64('encode',_xml);
fclose(_xml);
{? _c=1
|| _html:=exec('prev_template','dok_ksef',_xml_b)
|| _html:=exec('prev_template2','dok_ksef',_xml_b)
?};
_wyn:=fopen(_b,'Uw',0);
fwrite(_wyn,_html);
fclose(_wyn);
_b


\prev_template
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
_html:=fopen('prev-template.html','Ur',1);
_wiersz:=fread(_html);
_wyn:='';
{! |?
   _wyn+=_wiersz;
   _wiersz:=fread(_html);
   {? 7+_wiersz='</html>'
   || _wyn+=_wiersz; 0
   |? _wiersz+16='encodedDocument)'
   || _wyn+='\n'; 1
   || 1
   ?}
!};
_wyn;
_wyn:=gsub(_wyn,'$content',_a);
fclose(_html);
_wyn


\test_interm_js
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: TEST
::----------------------------------------------------------------------------------------------------------------------
exec('web_view','zbl_dok');
_win_grp:=DOKUM.grp_make('Dokumenty'@,,'identideid',,,"exec('exit','zws',_a)");
DOKUM.win_sel(_win_grp);
DOKUM.grp_ctr(_win_grp,,__CTR_WB.WIN_ACR,__CTR_WB.WIN_ID,'Podgląd'@,,,,100,50,"","",,'maximized');
DOKUM.select()

:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 94a2894bab7519663bc0a97196b22ba3a9e659784475b6a96755e5041bb0d353adee6887cdd602fe6dd3e080c2bbba8d188e1a8e5dc8e831614e025292675f02afb798ad2da548d4e4390d23ef506023eccb7331641aec134895be162f88c13e2437138f2f83e7199d1997b8cc5543606c035116529a9fb949a587a24528e250

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: lzk.fml
:: Utworzony: 27.01.2015
:: Autor: mario
::======================================================================================================================
:: Zawartość: Formuły oraz wywoływacze obszaru LZK
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: mario [17.00]
:: OPIS: Formuła inicjująca dla obszaru LZK
::----------------------------------------------------------------------------------------------------------------------
:: Podczytanie INFO.KRAJ - kraj waluty opodatkowania
exec('czytaj','#stalesys',,INFO,FINFO);
exec('czytaj','#stalesys',,XINFO,'SLWYDZIA','SLP','SLJEZYK','SLWAL');

:: Podczytanie parametrów oddziału
KSTE.prefix; KSTE.first();

:: Ustawia zmienne informujące o dostępie do wartości sprzedaży, zakupu, magazynowych
exec('upr_set','ceny');

:: Ustawienie dostępności sumowania kolumn wg dostępu do wartości zakupu
exec('sum_kolumn','lsp','Z');

:: Formuła podczytująca dane specyficzne dla firmy
__War_f:="exec('FindInSet','#table',_a,_a,_c,REF.FIRMA,$(_a+'.'+_b))";

:: blank FAKS.SZ - zakup
BEER.SZ:='Z';

:: Obiekt do obsługi parametrów
exec('Tpar_decl','tech_param');

:: RB - objekt obsługujący rachunki bankowe
exec('RB','object');

:: Ustawienie prezycji pól w oknach
exec('fld_prec','win_ust');

:: Salda i obroty kont i rozrachunków
exec('F','object');

:: kody kreskowe
exec('myDPRINT','object');

:: uprawnienia do cen
{? exec('upr_cm','ceny')=0 | exec('upr_cz','ceny')=0
||
   _ff:="0";
   ZD_POZ.fld_fml('CENA','BEFORE_DISPLAY',_ff);
   ZD_POZ.fld_fml('CENA','BEFORE_EDIT',_ff)
?};

:: format dla reklamacji
exec('f_DISP_EDIT','reklamacje');

ZD_NAG.fld_fml('PR','DISPLAY_FORMAT',"'out_prec=2'");
~~


\init_zds
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła inicjująca dla obszaru LZK_ZDS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Walutowość dla zamówień
{? INFO.WW='' || exec('czytaj','#stalesys',,INFO) ?};
BEER.MW:='W';
:: Ustawia BEER.WW
exec('ustaw_ww','eurusd',BEER.MW);
:: Ustawia uprawnienia do magazynów
exec('usuprmag','parses')


\init_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła inicjująca dla obszaru LZK_ZAK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Walutowość dla dokumentów zakupu
{? INFO.WW='' || exec('czytaj','#stalesys',,INFO) ?};
BEER.MW:='K';
BEER.SZ:='Z';
:: Ustawia BEER.WW
exec('ustaw_ww','eurusd',BEER.MW);
:: Ustawia uprawnienia do magazynów
exec('usuprmag','parses')


\lzk_dst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Główna formuła obszaru LZK_CEN - Cenniki dostaw
::   WE: [_a] -   jakie cenniki wyswietlać
::            0  - wszystkie
::            1  - kontrahenta
::            2  - grupy kontrahentów
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_a')<>type_of(0) || _a:=0 ?};

_params:=params_get();
_link:={? type_of(_params)>0 & var_pres('LINK',_params)>0 & _params.LINK<>'' || _params.LINK || '' ?};

{? _link<>''
||
   {? ref_tab(_link)=TAR
   ||
::    Ustalenie parametrów na podstawie przekazanego rekordu
      _dobrze:=1;
      _oddz:=exec('FindAndGet','#table',TAR,_link,,"TAR.ODDZ",'');

      {? _oddz=''
      || _dobrze:=0;
         FUN.info('Nie odnaleziono nagłówka cennika: %1'@[exec('record','#to_string',_link)])
      ?};

      {? _dobrze
      || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
         {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
         || _dobrze:=0
         ?}
      ?};

      {? _dobrze
::       Ustalenie parametrów sesji
      || __PARSES.setVal('OddzialLogProd',_oddz)
      || return(0)
      ?}
::======================================================================================================================
   |? ref_tab(_link)=TAP
   ||
::    Ustalenie parametrów na podstawie przekazanego rekordu
      _dobrze:=1;
      _tar:=exec('FindAndGet','#table',TAP,_link,,"TAP.TAR",'');
      _oddz:=exec('FindAndGet','#table',TAR,_tar,,"TAR.ODDZ",'');

      {? _oddz=''
      || _dobrze:=0;
         FUN.info('Nie odnaleziono nagłówka zamówienia: %1'@[exec('record','#to_string',_link)])
      ?};

      {? _dobrze
      || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
         {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
         || _dobrze:=0
         ?}
      ?};

      {? _dobrze
::       Ustalenie parametrów sesji
      || __PARSES.setVal('OddzialLogProd',_oddz)
      || return(0)
      ?}
   ?}
?};

exec('init','lzk');
params_exec('sel_tary','ceny','D',_a);
''


\par_plan_dst
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: parametry formuł dla planu dostaw
::----------------------------------------------------------------------------------------------------------------------
ZMIENNE.RODZAJ:='3';
PD_Z.TYP:='S';
FORMULA.win_dict('FORMULY');
FORMULA.win_edit('FORMULY');

PD_D.win_edit('RED');
PD_D.win_patt('SZUK');
PD_D.index('TYP1');
PD_D.prefix(PD_Z.TYP);
PD_D.first();
PD_D.win_sel('GRP');
PD_D.select()


\pd_d_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: przełączenie pomiędzy zakładkami
::   WE: _a - typ formuł
::----------------------------------------------------------------------------------------------------------------------
{? _a='B'
|| ZMIENNE.RODZAJ:='2';
   PD_Z.TYP:='B';
   PD_BUF_D.index('A');
   PD_BUF_D.prefix()
|| ZMIENNE.RODZAJ:='3';
   PD_Z.TYP:=_a;
   PD_D.index('TYP1');
   PD_D.prefix(PD_Z.TYP)
?}


\lzk_pld
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: obszar roboczy dla planów dostaw
::----------------------------------------------------------------------------------------------------------------------
exec('init','lzk');
exec('select','!lzk_pld_ppla')


\lzk_zak_env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Środowisko obszaru LZK_ZAK - Dokumenty zakupu
::----------------------------------------------------------------------------------------------------------------------
::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablice i komentować poszczególne jej elementy
::       powiedzmy, że to bedzie pole
         _fld:="31+form(_a)";
::       powiedzmy, że to bedzie metoda
         _mth:="31+form(_a)";

::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablice i komentować poszczególne jej elementy
::       powiedzmy, że to bedzie pole
         _fld:="31+form(_a)";
::       powiedzmy, że to bedzie metoda
         _mth:="31+form(_a)";

_env:=obj_new(
::             pola
                _fld('DATE',                 'przy kopiowaniu - data wystawienia nowych dokumentów')
               ,_fld('DATE_S',               'przy kopiowaniu - data sprzedaży nowych dokumentów')
               ,_fld('OUT_FAKS',             'przy kopiowaniu - ostatnia dołączona faktura')
               ,_fld('B_PREL',               'przy kopiowaniu - zapamiętuje wybrany proces w pętli')
               ,_fld('LINK',                 'obiekt do obsługi linków')
               ,_fld('TTABS_CUR_TAB',        'górny panel - id bieżacej zakładki')
               ,_fld('BTABS',                'dolny panel - tabela zakładek')
               ,_fld('BTABS_NDX_ID',         'dolny panel - tabela zakładek - index NDX_ID')
               ,_fld('BTABS_NDX_NR',         'dolny panel - tabela zakładek - index NDX_NR')
               ,_fld('BTABS_ON',             'dolny panel - 0/1 - panel włączony')
               ,_fld('BTABS_NR',             'dolny panel - nr ostatnio dodanej zakładki')
               ,_fld('BTABS_CUR_TAB',        'dolny panel - id bieżacej zakładki')
               ,_fld('BTABS_REF_REFRESH',    'dolny panel - ref odświeżonego rekordu')
               ,_fld('BTABS_CRC_REFRESH',    'dolny panel - crc odświeżonego rekordu')
               ,_fld('POZYCJE_FAK_VAR',      'dolny panel - zmienna używana w zakładkach pozycji')

::             identyfikatory okienek
               ,_fld('wid_win_main',         'ID okna glownego')

::             tytuly okienek
               ,_fld('tit_main',             'Tytul okienka glownego')

::             tabele tymczasowe
               ,_fld('TAB_DOKUM',            'Robocza tabela $DOKUM.ref(), np. do akcji grupowych')

::             indeksy tymczasowych tabel

::             uchwyty do okien
               ,_fld('WIN_MAIN',             'Główne okno grupowe')

::             kontrolki - załączniki kontaktów
               ,_fld('CTR1',                 'dolny panel - dane kontrolki załączników')

::             identyfikatory przycisków

::             METODY
               ,_mth('initObszar',           'Init obszaru')
               ,_mth('doneObszar',           'Done obszaru')
               ,_mth('winMain',              'Ustawienie głównego okna')
               ,_mth('btabs_add',            'Panel dolny - Dodanie zakładki')
               ,_mth('btabs_show',           'Panel dolny - Wyświetlenie zakładek')
               ,_mth('btabs_pozycje',        'Panel dolny - Nazwa zakładki pozycji dokumentu')
               );

_env.wid_win_main:='lzkdok_winmain';

:: Tytuł okna ustawiany w obiekcie AreaTitle
_env.tit_main:='';

_env.TAB_DOKUM:=tab_tmp(1,'REF','STRING[16]','$DOKUM.ref()');

_env.WIN_MAIN:='';
_env.LINK:=exec('Link','#object');
_env.LINK.FORMULA:="";
_env.CTR1:=obj_new('WIN_ID','WIN_ACR');
_env.BTABS:=tab_tmp(1
   ,'ID','STRING[50]','Id'
   ,'NR','INTEGER','Nr'
   ,'STAN','INTEGER','Stan'
   ,'ZSTAN','INTEGER','Zadany stan'
   ,'FAR','STRING[150]','Formuła na odśwież');
_env.BTABS_NDX_ID:=_env.BTABS.index('?');
_env.BTABS_NDX_NR:=_env.BTABS.ndx_tmp(,,'NR',,);
_env.BTABS_ON:=0;
_env.BTABS_NR:=0;
_env.BTABS_CUR_TAB:='';
_env.BTABS_REF_REFRESH:='';
_env.BTABS_CRC_REFRESH:=0;
_env.POZYCJE_FAK_VAR:=exec('pozycje_fak_var','faktury_poz');
_env.btabs_add:="
   {? ~.BTABS.find_key(_a)
   ||
      .BTABS.blank();
      .BTABS.ID:=_a;
      .BTABS.NR:=.BTABS_NR+=1;
      .BTABS.STAN:=1;
      .BTABS.FAR:=_b;
      .BTABS.add()
   ?}
";
_env.btabs_show:="
   {? ~.BTABS_ON || return() ?};
   _Tab:=.BTABS;
   _Cur_tab:=cur_tab(1,1);
   {? {? _Cur_tab.f_active() || {? _Cur_tab.sel_size() || 1 || ~_Cur_tab.f_get() ?} || ~_Cur_tab.get() ?}
   ||
      tab_hide(,1,'bottom');
      .BTABS_REF_REFRESH:='';
      .BTABS_CRC_REFRESH:=0;
      _loop:=_Tab.first(); {! |? _loop |! _Tab.STAN:=0; _loop:=_Tab.put() & _Tab.next() !};
      return()
   ?};
   _loop:=_Tab.first(); {! |? _loop |! _Tab.ZSTAN:=0; _loop:=_Tab.put() & _Tab.next() !};
   {! _ii:=1.._ |! {? _Tab.find_key(_[_ii],) || _Tab.ZSTAN:=1; _Tab.put() ?} !};
   {? ~_Tab.find_key(.BTABS_CUR_TAB,) | _Tab.ZSTAN=0 || .BTABS_CUR_TAB:='' ?};
   _Tab.cntx_psh();
   _Tab.index(.BTABS_NDX_NR);
   _first_tab:='';
   _loop:=_Tab.first();
   {!
   |? _loop
   |!
      {? _Tab.STAN<>_Tab.ZSTAN
      ||
         {? _Tab.STAN
         || tab_hide(_Tab.NR,,'bottom')
         || tab_show(_Tab.NR,'bottom')
         ?};
         _Tab.STAN:=_Tab.ZSTAN;
         _Tab.put()
      ?};
      {? _first_tab='' & _Tab.STAN || _first_tab:=_Tab.ID ?};
      _loop:=_Tab.next()
   !};
   _Tab.cntx_pop();
   {? .BTABS_CUR_TAB='' || .BTABS_CUR_TAB:=_first_tab ?};
   {? (.BTABS_REF_REFRESH<>$_Cur_tab.ref() | .BTABS_CRC_REFRESH<>$_Cur_tab.crc())
         &
      .BTABS_CUR_TAB<>'' & _Tab.find_key(.BTABS_CUR_TAB,)
   ||
      tab_sel(_Tab.NR,'bottom');
      params_set(params_get());
      ($_Tab.FAR)()
   ?};
   .BTABS_REF_REFRESH:=$_Cur_tab.ref();
   .BTABS_CRC_REFRESH:=$_Cur_tab.crc()
";
_env.btabs_pozycje:="
   {? FAKS.WHERE='K'
   || 'Pozycje korekty zbiorczej'
   |? FAKS.KOR=''
   || 'Pozycje dokumentu'
   || 'Pozycje korekty'
   ?}
";

_env.initObszar:="
   _wyn:=1;

   exec('init','lzk');
   exec('lzk_zak_var','lzk');

   BEER.NAWIG:='Z';

   .BTABS_ON:=exec('upgrade2325_blbc1','zbl');

   _wyn
";

_env.doneObszar:="
   BEER.NAWIG:=''
";

:: Takie czary, żeby zadziałały tłumaczenia poniżej (teksty MUSZĄ być identyczne)
'Zakres dokumentów'@; 'Dokumenty'@; 'Stany magazynowe'@; 'Pozycje dokumentów'@;
_env.winMain:="
   _formula:=\"
      _env:=params_get().env;
      exec('lzk_zak_filtr','lzk');
::      grp_disp(FAKS,'WERZ',1,1);
      _env.LINK.FORMULA();
      1
   \";
   FILTER.STATUS:='M';
   FILTER.HIDDEN:='N';

   _upr:=exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_ZPTM');

::
:: okno grupowe
::
   _grp_w:=.WIN_MAIN:=FAKS.grp_make(.tit_main,_formula,.wid_win_main,,,\"exec('exit','zws',_a)\");
:: zakładka - Dokumenty
   _far:=\"
      params_set(params_get());
      _env:=params_get().env;
      exec('lzk_zak_var','lzk');
      params_exec('faks_far_werz','lzk');
      {? _env.BTABS_ON
      || {? FAKS.WHERE='K'
         || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Dane dokumentu','Stawki VAT',
               'Rezultat kontroli Businesscheck')
         || _env.btabs_show(_env.btabs_pozycje(),'Dane dokumentu','Stawki VAT','Rezultat kontroli Businesscheck')
         ?}
      || grp_edisp(FAKS,exec('faks_win_info','faktury_nag'))
      ?}
      \";
   _fb:=\"
      {? _a
      ||
:: aktywowanie okna
         params_set(params_get());
         _env:=params_get().env;
         _env.TTABS_CUR_TAB:='Dokumenty';
         exec('lzk_zak_var','lzk');
         {? FILTER.FAKS<>null() || {? FAKS.f_active() || FAKS.f_seek(FILTER.FAKS) || FAKS.seek(FILTER.FAKS) ?} ?};
         FILTER.FAKS:=null();
         {? _env.BTABS_ON
         || {? FAKS.WHERE='K'
            || _env.btabs_show('Korygowane dokumenty',_env.btabs_pozycje(),'Dane dokumentu','Stawki VAT',
                  'Rezultat kontroli Businesscheck')
            || _env.btabs_show(_env.btabs_pozycje(),'Dane dokumentu','Stawki VAT','Rezultat kontroli Businesscheck')
            ?}
         ?}
      ?}
      \";
   _fa:=\"
      ~~
      \";
   _wer:='WERZ';
   FAKS.win_fml(_wer,,'ID',,'ICON_BEFORE',exec('faks_sym_ib','faktury_nag'));
   FAKS.win_fml(_wer,,'NR',,'ICON_BEFORE',exec('faks_sym_it','faktury_nag'));
   FAKS.win_fml(_wer,,'KH','NAZ_P','ICON_BEFORE',exec('faks_sym_ob','faktury_nag'));
   FAKS.win_fml(_wer,,'NETTO',,'ICON_BEFORE',exec('faks_war_ib','faktury_nag'));
   FAKS.win_sel(_wer);
   FAKS.grp_sel(_grp_w,,_wer,'Dokumenty'@,_far,,,23,_fb,_fa,,,'maximized','WER');

   {? ~.BTABS_ON
   ||
      FAKS.tab_splt(_grp_w,,'horizontal','info','0, 60%');
      FAKS.grp_edit(_grp_w,,exec('faks_win_info','faktury_nag'),,,,,,'maximized')
   ?};

   {? _upr
   ||
:: zakładka - Stany magazynowe
      _far:=\"
         params_set(params_get());
         _env:=params_get().env;
         _env.btabs_show('Stany magazynowe')
      \";
      _fb:=\"
         params_set(params_get());
         _env:=params_get().env;
         _env.TTABS_CUR_TAB:='Stany magazynowe';
         exec('stan_mg_up','magazyn_stan','K');
         _env.btabs_show('Stany magazynowe')
      \";
      FAKS.grp_sel(_grp_w,M,'STANY_UP','Stany magazynowe'@,_far,,,23,_fb,,,,'maximized');
      exec('addsmmag','magazyn_stan');
      __smmag.win_sopt(__smmag.win_sel('?'),'select_record_checkbox=0');
      {? ~.BTABS_ON
      ||
         _far:=\"\";
         _fb:=\"\";
         FAKS.tab_splt(_grp_w,,'vertical','stany','0, 80%');
         FAKS.grp_sel(_grp_w,__smmag,__smmag.win_sel('?'),,_far,,,23,_fb,,,,'maximized')
      ?}
   ?};

:: zakładka - Pozycje dokumentów
   _far:=\"
      {? (FAP.sel_size() | {? FAP.f_active() || FAP.f_size() || FAP.size() ?})
          & (FILTER.STATUS='R' | FAKS.AM=ST.AM)
      || FILTER.FAKS:=FAKS.ref()
      || FILTER.FAKS:=null()
      ?};
      params_set(params_get());
      _env:=params_get().env;
      _env.btabs_show('Dane dokumentu','Dokumenty magazynowe')
   \";
   _fb:=\"
      params_set(params_get());
      _env:=params_get().env;
      _env.TTABS_CUR_TAB:='Pozycje dokumentów';
      exec('all_poz','lzk');
      _env.btabs_show('Dane dokumentu','Dokumenty magazynowe')
   \";
   {? .BTABS_ON
   || FAKS.grp_sel(_grp_w,FAP,'TALLZ','Pozycje dokumentów'@,_far,,,23,_fb,,,,'maximized_with_title','POZ')
   || FAP.win_fml('ALLZ',,'FAKS','ID','ICON_BEFORE',exec('faks_sym_ib','faktury_nag'));
      FAKS.grp_sel(_grp_w,FAP,'ALLZ','Pozycje dokumentów'@,_far,,,23,_fb,,,,'maximized_with_title','POZ')
   ?};

:: Dokumenty Businesslink
   _far:=\"
      params_set(params_get());
      _env:=params_get().env;
      {? DOKUM.sel_size()=0 & {? DOKUM.f_active() || DOKUM.f_get() || DOKUM.get() ?}
      || exec('bl_log_html','zbl_dok');
         exec('web_view_load','zbl_dok');
         exec('dokumzbc_load','zbl_dok');
         params_exec('dokum_rfr','bloby',':'+_env.CTR1.WIN_ID,_env.CTR1.WIN_ACR)
      || exec('web_view_load','zbl_dok',1)
      ?};
      _env.btabs_show('Log','Załączniki','Rezultat kontroli Businesscheck')
   \";
   _fb:=\"
      params_set(params_get());
      _env:=params_get().env;
      _env.TTABS_CUR_TAB:='Przychodzące Businesslink';
      DOKUM.index('BL_DZK');
      DOKUM.prefix('T',REF.FIRMA,'P','T');
      _env.btabs_show('Log','Załączniki','Rezultat kontroli Businesscheck')
   \";
   {? .BTABS_ON
   || exec('winLzk','zbl_dok',_grp_w,1,_far,_fb);
      .CTR1.WIN_ACR:=__CTR.WIN_ACR;
      .CTR1.WIN_ID:=__CTR.WIN_ID
   || exec('winLzk','zbl_dok',_grp_w)
   ?};

::
:: --- panel - bottom
::
   {? .BTABS_ON
   ||
::
:: Dokumenty
::
      params_set('env',.);
      FAKS.grp_splt(_grp_w,'panel0','horizontal','bottom','0,67%');
:: zakładka - Pozycje dokumentu
      .btabs_add('Pozycje dokumentu',$\"params_set(params_get()); grp_disp(FAP,'TWER_I',,,'bottom')\");
      _far:=\"
         params_set(params_get());
         {? var_pres('__FAKS_GRP_DISP')>0 & __FAKS_GRP_DISP || grp_disp(FAKS,'WERZ',,,'panel0'); __FAKS_GRP_DISP:=0 ?}
      \";
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Pozycje dokumentu';
         params_exec('pozycje_fak','faktury_poz',,exec('acts_schem_faks','faktury_poz'),,1,_a,_env.POZYCJE_FAK_VAR);
         1
      \";
      _fa:=\"
         _env:=params_get().env;
         params_exec('pozycje_fak_po','faktury_poz',1,_a,_env.POZYCJE_FAK_VAR);
         FAKS.trig_a('put',,'faks_grp_disp')
      \";
      FAKS.grp_sel(_grp_w,FAP,'TWER_I','Pozycje dokumentu'@,_far,,,12,_fb,_fa,,,'maximized','FAP_TWER_I');
:: zakładka - Pozycje korekty
      .btabs_add('Pozycje korekty',$\"params_set(params_get()); grp_disp(FAP,'TWERKZ',,,'bottom')\");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Pozycje korekty';
         params_exec('pozycje_fak','faktury_poz',,exec('acts_schem_faks','faktury_poz'),,1,_a,_env.POZYCJE_FAK_VAR)
      \";
      FAKS.grp_sel(_grp_w,FAP,'TWERKZ','Pozycje korekty'@,_far,,,12,_fb,_fa,,,'maximized','FAP_TWERKZ');
:: zakładka - Korygowane dokumenty
      __faks_kzf_wer:=exec('faks_kzf','faktury_nag',2);
      .btabs_add('Korygowane dokumenty',$\"
         FAKS.cntx_psh();
         params_set(params_get()); grp_disp(FAKS_KZF,__faks_kzf_wer,,,'bottom');
         FAKS.cntx_pop()
      \");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         FAKS.cntx_psh();
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Korygowane dokumenty';
         FAKS_KZF.win_sel(__faks_kzf_wer);
         params_exec('faks_kzf','faktury_nag',1,_a,_env.POZYCJE_FAK_VAR)
      \";
      _fa:=\"
         _env:=params_get().env;
         params_exec('faks_kzf_po','faktury_nag',_env.POZYCJE_FAK_VAR);
         FAKS.cntx_pop();
         FAKS.trig_a('put',,'faks_grp_disp')
      \";
      FAKS.grp_sel(_grp_w,FAKS_KZF,__faks_kzf_wer,'Korygowane dokumenty'@,_far,,,12,_fb,_fa,,,'maximized','FAKS_KZF_WER1');
:: zakładka - Pozycje korekty zbiorczej
      .btabs_add('Pozycje korekty zbiorczej',$\"
         FAKS.cntx_psh();
         params_set(params_get()); grp_disp(FAP,'TWERKZB',,,'bottom');
         FAKS.cntx_pop()
      \");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         FAKS.cntx_psh();
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Pozycje korekty zbiorczej';
         params_exec('pozycje_fak','faktury_poz',,exec('acts_schem_faks','faktury_poz'),,1,_a,_env.POZYCJE_FAK_VAR)
      \";
      _fa:=\"
         FAKS.cntx_pop();
         FAKS.trig_a('put',,'faks_grp_disp')
      \";
      FAKS.grp_sel(_grp_w,FAP,'TWERKZB','Pozycje korekty zbiorczej'@,,,,12,_fb,_fa,,,'maximized','FAP_TWERKZB');
:: zakładka - Stawki VAT
      .btabs_add('Stawki VAT',$\"params_set(params_get()); grp_disp(FAKSV,'WER',,,'bottom')\");
      _far:=\"
      \";
      _fb:=\"
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Stawki VAT';
         FAKSV.cntx_psh();
         FAKSV.use('faksv'+(ref_name(FAKS.ref())+3));
         FAKSV.index('FF_SV');
         FAKSV.prefix('',$FAKS.ref());
         1
      \";
      _fa:=\"
         FAKSV.cntx_pop()
      \";
      FAKS.grp_sel(_grp_w,FAKSV,'WER','Stawki VAT'@,_far,,,12,_fb,_fa,,,'maximized','FAKSV_WER');
::
:: Dokumenty, Pozycje dokumentów
::
:: zakładka - Dane dokumentu
      .btabs_add('Dane dokumentu',$\"params_set(params_get()); grp_edisp(FAKS,'TINFO')\");
      _fb:=\"
         FAKS.cntx_psh();
         FAKSZ.STATOREJ:=exec('statorej','faktury_nag1');
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Dane dokumentu';
         {? _env.TTABS_CUR_TAB='Pozycje dokumentów' || FAP.FAKS() ?}
      \";
      _fa:=\"
         FAKS.cntx_pop()
      \";
      FAKS.grp_edit(_grp_w,FAKS,'TINFO','Dane dokumentu'@,,_fb,_fa,, 'maximized');
::
:: Pozycje dokumentów
::
:: zakładka - Dokumenty magazynowe
      .btabs_add('Dokumenty magazynowe',$\"params_set(params_get()); grp_disp(__nd_tab,__nd_sel,,,'bottom')\");
      _far:=\"
      \";
      _fb:=\"
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Dokumenty magazynowe';
         exec('nd_tab_fap','powdok');
         1
      \";
      _fa:=\"
      \";
      FAKS.grp_sel(_grp_w,__nd_tab,__nd_sel,'Dokumenty magazynowe'@,_far,,,12,_fb,_fa,,,'maximized','NDTAB_NDSEL');
::
:: Przychodzące Businesslink
::
:: zakładka - Log
::
      .btabs_add('Log',$\"\");
      _fb:=\"
            {? _a
            || exec('bl_log_html','zbl_dok');
               _env:=params_get().env;
               _env.BTABS_CUR_TAB:='Log'
            ?}
       \";
      _fa:=\"\";
      FAKS.grp_ctr(_grp_w,DOKUM,__CTH.WIN_ACR,,'Log'@,,,,,12,_fb,_fa,,'maximized');
::
:: zakładka - Załączniki
::
      .btabs_add('Załączniki',$\"\");
      _fb:=\"
            _env:=params_get().env;
            __CTR.WIN_ID:=':'+_env.CTR1.WIN_ID;
            __CTR.WIN_ACR:=_env.CTR1.WIN_ACR;
            __CTR.PODGLAD:=-1;
            exec('bobs_dokumz','bloby');
            _env.BTABS_CUR_TAB:='Załączniki'
       \";
      _fa:=\"\";
      FAKS.grp_ctr(_grp_w,DOKUMZ,.CTR1.WIN_ACR,.CTR1.WIN_ID,'Załączniki'@,,,,,12,_fb,_fa,,'maximized');
::
:: Dokumenty, Przychodzące Businesslink
::
:: zakładka - Rezultat kontroli Businesscheck
      .btabs_add('Rezultat kontroli Businesscheck',$\"
         FAKS.cntx_psh();
         params_set(params_get()); grp_disp(DOKUMZBC,'WER1',,,'bottom');
         FAKS.cntx_pop()
      \");
      _fb:=\"
         __FAKS_GRP_DISP:=0;
         FAKS.trig_a('put',$'__FAKS_GRP_DISP:=1; ~~','faks_grp_disp');
         FAKS.cntx_psh();
         _env:=params_get().env;
         _env.BTABS_CUR_TAB:='Rezultat kontroli Businesscheck';
         {? _env.TTABS_CUR_TAB='Przychodzące Businesslink'
         || params_exec('dokumzbc_load','zbl_dok')
         || params_exec('faks_dokumzbc_seek','zbl')
         ?};
         {? __DOKUMZ_BC.DOKUMZ=null()
         || DOKUMZBC.actions_grayed('WER','WH:WH')
         || DOKUMZBC.actions_grayed('WER')
         ?}
      \";
      _fa:=\"
         FAKS.cntx_pop();
         FAKS.trig_a('put',,'faks_grp_disp')
      \";
      FAKS.grp_sel(_grp_w,DOKUMZBC,'WER1','Rezultat kontroli Businesscheck'@,,,,12,_fb,_fa,,,'maximized','DOKUMZBC_WER1');
::
:: Stany magazynowe
::
:: zakładka - Stany magazynowe
      {? _upr
      ||
         .btabs_add('Stany magazynowe',$\"\");
         FAKS.grp_sel(_grp_w,__smmag,__smmag.win_sel('?'),'Stany magazynowe'@,,,,12,,,,,'maximized','SMMAG')
      ?}
   ?};
::
   FAKS.win_sel(.WIN_MAIN);
   FAKS.timer('sel',.WIN_MAIN,2,\"{? FAKS.sel_size()=0 || FAKS.f_rfresh() ?}\")
";

_env


\lzk_zak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Główna formuła obszaru LZK_ZAK - Dokumenty zakupu
::----------------------------------------------------------------------------------------------------------------------
exec('eanx_wys','okienka');
exec('nd_tab','powdok');
_env:=exec('lzk_zak_env','lzk');

{? _env.initObszar()
|| _env.winMain();
   params_set('env',_env);

::======================================================================================================================
:: Ustalenie treści linku
   _params:=params_get();
   {? type_of(_params)>0 & var_pres('LINK',_params)
   || _link:=_params.LINK
   || _link:=''
   ?};
:: Obsługa linku
   {? _link<>''
   ||
::    Ustalenie parametrów na podstawie przekazanego rekordu
      {? ref_tab(_link)=FAKS
      || _oddz:=exec('FindAndGet','#table',FAKS,_link,,"ODDZ",'');
         _rok:=exec('FindAndGet','#table',FAKS,_link,,"AR",0);
         _mc:=exec('FindAndGet','#table',FAKS,_link,,"AM",0);
         _sts:=exec('FindAndGet','#table',FAKS,_link,,"STS",null())
      |? ref_tab(_link)=FAP
      || _oddz:=exec('FindAndGet','#table',FAP,_link,,"exec('FindAndGet','#table',@.FAKS,FAKS,,\"ODDZ\",'')",'');
         _rok:=exec('FindAndGet','#table',FAP,_link,,"exec('FindAndGet','#table',@.FAKS,FAKS,,\"AR\",0)",0);
         _mc:=exec('FindAndGet','#table',FAP,_link,,"exec('FindAndGet','#table',@.FAKS,FAKS,,\"AM\",0)",0);
         _sts:=exec('FindAndGet','#table',FAP,_link,,"exec('FindAndGet','#table',@.FAKS,FAKS,,\"STS\",null())",null())
      ?};
      _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);

::    Wykluczenie dostępu na podstawie uprawnień do danych - oddziały i stanowiska zakupu
      {? exec('spr_upr','users','ODDZ',_oddz_ref) & {? _sts=null() || 1 || exec('spr_upr','users','STZ',_sts) ?}
      ||
::       Ustalenie parametrów sesji
         __PARSES.setVal('OddzialLogProd',_oddz);
         _args:=__PARSES.args('OkresRok');
         _args.OBSZAR:='LZK';
         _args.AR:=_rok;
         _args.AM:=_mc;
         __PARSES.setVal('OkresRok',_args);
         __PARSES.setVal('StZakupu',_sts);

::       Znalezienie i wyświetlenie obszaru roboczego w kontekście przekazanego rekordu
         {? ref_tab(_link)=FAKS & FAKS.seek(_link)
         || AreaTitle.setTabWin(FAKS,~~);
            AreaTitle.setTitle();
            FILTER.FAKS:=FAKS.ref();
            FAKS.select(,1,5,,'WER',,1)
         |? ref_tab(_link)=FAP & FAP.seek(_link)
         || AreaTitle.setTabWin(FAKS,~~);
            AreaTitle.setTitle();
            FILTER.FAKS:=FAP.FAKS;
            {? 0 & FUN.ask('Pozycje w zakładce?'@)
            || FAKS.select(,1,5,,'POZ')
            || {? FAKS.seek(FAP.FAKS)
               ||
                  _env.LINK.FORMULA:=
                     $('{? FAKS.ref() || params_exec(\'faks_pozycje_wer\',\'faktury_nag\',,\'%1\') ?}'[_link]);
                  FAKS.select(,1,5,,'WER',,1)
               ?}
            ?}
         || FUN.info('Nie odnaleziono dokumentu %1.'@[exec('record','#to_string',_link)])
         ?}
      ?}
::======================================================================================================================

:: Standardowe działanie
   || AreaTitle.setTabWin(FAKS,~~);
      AreaTitle.setTitle();
      FILTER.FAKS:=FAKS.ref();
      FAKS.select(,,,,'WER')
   ?};

   _env.doneObszar()
?}


\lzk_zak_var
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła inicjująca zmienne obszaru LZK_ZAK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('init_zak','lzk')


\lzk_zak_filtr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Filtr dokumentów zakupu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_stz:={? ST.STZ || '(\''+$ST.STZ+'\')' || '' ?};
_sort:='NR';
_from:='';
_where:="
   FAKS.HIDDEN='N'
   and FAKS.SZ=':_a'
   and FAKS.AR=:_b "
   +{? FILTER.STATUS='M' || "and FAKS.AM=:_c " || "" ?}
   +{? _stz='' || "" || "and FAKS.STS in :_d" ?};
{? FAKS.sel_size() || FAKS.sel_adel() ?};
FAKS.prefix();
FAKS.f_clear();
FAKS.f_set(_sort,_from,_where,BEER.SZ,ST.AR,ST.AM,_stz)


\pd_d_use_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: przełączenie pomiędzy zakładkami
::   WE: _a - typ formuł
::----------------------------------------------------------------------------------------------------------------------
PD_Z.TYP:=_a;
PD_D_USE.index('TYP');
PD_D_USE.prefix(PD_Z.TYP,PD_N.ref(),{? PD_Z.NAGPOZ='N' || null() || PD_P.ref() ?})


\permissions_domain
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Formuła na uprawnienia obszaru
::   WE: params_get().in - parametry wejściowe czynności
::       params_get().user - użytkownik dla którego sprawdzane są uprawnienia
::       params_get().mp - Menedżer Procesów
::   WY: 0 - użytkownik nie ma uprawnień do czynności
::       1 - użytkownik ma uprawnienia do czynności
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_user:=params_get().user;
_mp:=params_get().mp;

_wyn:=1;

_opis:='';

:: Sprawdzenie czy jest ustawiony rok
{? _wyn
|| _wyn:=ST.AR>0;
   {? _wyn=0 || _opis+='\n- okresu' ?}
?};

{? _wyn=0 & var_pres('opis',params_get())
|| params_get().opis.TXT:='Brak uprawnień do:'+_opis
?};

_wyn


\permissions_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Sprawdzenie uprawnień
::   WE: _a - nazwa pliku czynności
::       UWAGA. Do pobrania parametrów stosować params_get() = tablica nazwana:
::       in  - [obj_new] - parametry wejściowe czynności
::       int - [obj_new] - parametry wewnętrzne czynności
::       out - [obj_new] - parametry wyjściowe czynności
::       mp  - obiekt odpowiedzialny za obsługę procesu
::   WY: 0-brak uprawnień, 1-są uprawnienia
::  OLD: \permissions_chk/!lsp_fak_drfp.fml
::  OLD: \permissions_chk/lsp.fml
::----------------------------------------------------------------------------------------------------------------------
_fml:=_a;

_in:=params_get().in;
_out:=params_get().out;
_mp:=params_get().mp;

:: Sprawdzenie uprawnień obszaru
_opis:=obj_new('TXT'); _opis.TXT:='';
params_set('in',_in,'user',OPERATOR.USER,'mp',_mp,'opis',_opis);
{? exec('permissions_domain','lzk')=0
|| _txt:='Brak uprawnień do uruchomienia czynności.';
   {? _opis.TXT<>'' || FUN.info('%1\n\n%2'@[_txt,_opis.TXT]) ?};
   _mp.error(_txt);
   return(0)
?};
:: Sprawdzenie uprawnień czynności
{? _fml<>''
|| params_set('in',_in,'user',OPERATOR.USER,'mp',_mp);
   {? exec('permissions',_fml)=0
   || _txt:='Brak uprawnień do uruchomienia czynności.'@;
      FUN.info(_txt);
      _mp.error(_txt);
      return(0)
   ?}
?};
1


\lzk_zak_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zmiana parametrów pracy obszaru LZK_ZAK
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
_stz:=ST.STZ;
_ar:=ST.AR;
{? __PARSES.editDom('LZK')
|| {? _oddz<>ST.ODDZ || exec('init','lzk') ?};
   exec('lzk_zak_filtr','lzk');
   {? _stz<>ST.STZ | _ar<>ST.AR || exec('all_poz','lzk') ?};
   AreaTitle.setTitle()
?}


\lzk_zds_typy_tab
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Typy zamówień dostaw - tabela
::   WE:
::   WY: Uchwyt do tabeli
::----------------------------------------------------------------------------------------------------------------------
_p3024:=exec('get','#params',3024,2,OPERATOR.USER);
_p3024='';

_sql:="
   select T, NAZ, REFERENCE REF, ' ' INF, MOB, ZAP
   from TYPYZAM
   where TYPYZAM.R='D'
   ";
_sql+=
   {? _p3024=''
   || ""
   || "
      and
      ':_a' like '% '||TYPYZAM.T||' %'
      "
   ?};
_sql+="
   order by T, T
   ";

{? _p3024<>'' || _p3024:=' '+_p3024+' ' ?};

sql(_sql,_p3024)


\lzk_zds_typy_win
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Typy zamówień dostaw - okno wertowania
::   WE: _a - środowisko obszaru LZK_ZDS
::   WY: Akronim okna
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a.TAB_TYPY;

_win:=_Tab.mk_sel(_a.tit_typy,'P',0,_a.wid_win_typy,,,,,'U','T');
_Tab.win_fld(_win,,'T',,,8,,0,'Typ'@,,'Symbol typu zamówienia'@);
_Tab.win_fld(_win,,'NAZ',,,27,,0,'Nazwa'@,,'Nazwa zamówienia'@);
_Tab.win_act(_win,0,'Szukaj');
_Tab.win_act(_win,0,'Kolejność');

_win


\lzk_zds_env
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Środowisko obszaru LZK_ZDS - Dokumenty zakupu
::----------------------------------------------------------------------------------------------------------------------

::UWAGA: _fld, i _mth to formułki pomocnicze, żeby wygodniej tworzyć tablice i komentować poszczególne jej elementy
::       powiedzmy, że to bedzie pole
         _fld:="31+form(_a)";
::       powiedzmy, że to bedzie metoda
         _mth:="31+form(_a)";

_env:=obj_new(
::             pola

::             identyfikatory okienek
                _fld('wid_win_main',         'ID okna głownego')
               ,_fld('wid_win_typy',         'ID okna typów')
               ,_fld('wid_win_zdn_',         'ID okna zamówień dostaw')
               ,_fld('wid_win_ptw_',         'ID okna potwierdzeń zamówień dostaw')

::             tytuly okienek
               ,_fld('tit_main',             'Tytuł okna głównego')
               ,_fld('tit_typy',             'Tytuł okna typów')

::             tabele tymczasowe
               ,_fld('TAB_TYPY',             'Tabela typów')

::             indeksy tymczasowych tabel
               ,_fld('IND_TYPY1',            'Indeks tabeli typów: KOD')

::             uchwyty do okien
               ,_fld('WIN_MAIN',             'Główne okno grupowe')
               ,_fld('WIN_TYPY',             'Okno typów')
               ,_fld('WIN_ZAKR',             'Okno zakresu')
               ,_fld('WIN_ZDN_',             'Okno zamówień dostaw')
               ,_fld('WIN_INFO',             'Okno szczegółów zamówienia dostaw')
               ,_fld('WIN_ZPTW',             'Okno zakresu potwierdzeń')
               ,_fld('WIN_PTW_',             'Okno potwierdzeń zamówień dostaw')
               ,_fld('WIN_INFP',             'Okno szczegółów potwierdzeń zamówień dostaw')

::             identyfikatory przycisków

::             METODY
               ,_mth('initObszar',           'Init obszaru')
               ,_mth('doneObszar',           'Done obszaru')
               ,_mth('winMain',              'Ustawienie głównego okna')
               );

_env.wid_win_main:='lzkzds_winmain';
_env.wid_win_typy:='lzkzds_typy';
_env.wid_win_zdn_:='lzkzds_zdnag';
_env.wid_win_ptw_:='lzkzds_zdptw';

:: Tytuł okna ustawiany w obiekcie AreaTitle
_env.tit_main:='';
_env.tit_typy:='Typy zamówień';

_env.WIN_MAIN:='';
_env.WIN_TYPY:='';
_env.WIN_ZAKR:='';
_env.WIN_ZDN_:='';
_env.WIN_INFO:='';
_env.WIN_PTW_:='';
_env.WIN_ZPTW:='';
_env.WIN_INFP:='';

_env.initObszar:="
   _wyn:=1;

   exec('init','lzk');
   exec('init_zds','lzk');

   .TAB_TYPY:=exec('lzk_zds_typy_tab','lzk');
   .IND_TYPY1:=.TAB_TYPY.index('?');
   .WIN_TYPY:=exec('lzk_zds_typy_win','lzk',.);

   .WIN_ZAKR:=exec('zakr_win_zamd','zamdst_nag');
   _ff:=\"edit_start(); ''\";
   _btn:=ZAKR.win_ebtn(.WIN_ZAKR,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1,edit=0'['&Zmień'@],_ff);
   ZAKR.btn_opt(_btn,'tooltip=%1'['Zmień parametry zakresu filtrowania zamówień'@]);
   exec('ok_esc','#window',ZAKR,.WIN_ZAKR);

   .WIN_ZDN_:='WER';
   .WIN_PTW_:='WER';

   .WIN_INFO:=exec('zdnag_win_info','zamdst_nag');

   .WIN_ZPTW:=exec('zptw_win_zamd','zamdst_nag');
   _ff:=\"edit_start(); ''\";
   _btn:=ZAKR.win_ebtn(.WIN_ZPTW,'text=%1,btn_label_align=center,panel=bottom,align=begin,display=1,edit=0'['&Zmień'@],_ff);
   ZAKR.btn_opt(_btn,'tooltip=%1'['Zmień parametry zakresu filtrowania zamówień'@]);
   exec('ok_esc','#window',ZAKR,.WIN_ZPTW);

   .WIN_INFP:=exec('zdptw_win_info','zamdst_ptw');

   BEER.NAWIG:='ZD';
   BEER.MENU_PTH:='ZW';

   {? ~.TAB_TYPY.first()
   || FUN.info('Brak uprawnionych typów zamówień dostaw.\nObszar niedostępny.'@);
      _wyn:=0
   ?};

   _wyn
";

_env.doneObszar:="
   BEER.NAWIG:='';
   BEER.MENU_PTH:=''
";

_env.winMain:="
:: Okno grupowe
   _potw:=exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZDS_DPTW')
        | exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZDS_APTW')
        | exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZDS_PARC')
        | exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZDS_PPTW');
   _fb:=\"
      _params:=params_get();
      _env:=_params.lzkzds_env;
      {? type_of(_params)>0 & var_pres('LINK',_params)=type_of('')
      || _link:=_params.LINK
      || _link:=''
      ?};
      _Tab:=_env.TAB_TYPY;
      {? _link=''
::    Ustawienie się na domyślnym typie zamówienia
      || _Tab.first()
      ?};
      _tr:=exec('get','#params',3006,2,OPERATOR.USER);
      _Tab.find_key(_tr,_tr);
      params_set(params_get());
      grp_disp(_Tab,_env.WIN_TYPY);
      {? _link<>''
      || ZAKR.T:='W';
         ZAKR.WG:='W'
      || exec('zakr_get','zamdst_nag')
      ?};
::      grp_edisp(ZAKR,_env.WIN_ZAKR);
      BEER.TYPYZAM:=exec('FindAndGet','#table',TYPYZAM,_Tab.REF,,,null());
      exec('zakr_set','zamdst_nag',0)
      \";
   _grp_w:=.WIN_MAIN:=ZD_NAG.grp_make(.tit_main,_fb,.wid_win_main,,,\"exec('exit','zws',_a)\");
   _far:=\"params_exec('zd_nag_far_typyzam','lzk')\";
   _fb:=\"params_exec('zd_nag_fb_typyzam','lzk')\";
   {? exec('chk_role','#b__box',OPERATOR.USER,'LMG_MAG_ZPTM')
   ||
:: Panel typów zamówień dostaw
      ZD_NAG.grp_sel(_grp_w,.TAB_TYPY,.WIN_TYPY,'Zamówienia do dostawców'@,_far,,,8,_fb,,,,'maximized');

:: Panel zakresu
::      ZD_NAG.tab_splt(_grp_w,'tab0','horizontal','zakres');
::      _far:=\"params_exec('zd_nag_far_zakr','lzk')\";
::      ZD_NAG.grp_edit(_grp_w,ZAKR,.WIN_ZAKR,,_far,,,, 'maximized');

:: Panel zamówień dostaw
      ZD_NAG.tab_splt(_grp_w,,'vertical','zam',',33%');
      _far:=\"params_exec('zd_nag_far_wer','lzk')\";
      ZD_NAG.grp_sel(_grp_w,,.WIN_ZDN_,,_far,,,23,,,,1,'maximized',.wid_win_zdn_,1);

::    Panel szczegółów zamówienia dostaw
      ZD_NAG.tab_splt(_grp_w,'zam','horizontal','info',',66%');
      _fb:=\"params_exec('zd_nag_fb_info','lzk')\";
      ZD_NAG.grp_edit(_grp_w,,.WIN_INFO,,,_fb,,, 'maximized');

      {? _potw
      ||
::       Zakładka potwierdzeń zamówień dostaw
::       Panel zakresu
::         _far:=\"params_exec('zd_ptw_far_zakr','lzk')\";
::         ZD_NAG.grp_edit(_grp_w,ZAKR,.WIN_ZPTW,'Potwierdzenia zamówień'@,_far,,,,'maximized');

::         ZD_NAG.tab_splt(_grp_w,,'vertical','ptw');
         _far:=\"params_exec('zd_ptw_far_wer','lzk')\";
         _fb:=\"params_exec('zdp_nag','zamdst_ptw')\";
         _fa:=\"params_exec('zakr_ind','zamdst_nag')\";
         ZD_NAG.grp_sel(_grp_w,ZDP_NAG,.WIN_PTW_,'Potwierdzenia zamówień'@,_far,,,23,_fb,_fa,,1,'maximized');
         ZD_NAG.tab_splt(_grp_w,'tab0','horizontal','infp',',66%');
         _fb:=\"params_exec('zd_ptw_fb_info','lzk')\";
         ZD_NAG.grp_edit(_grp_w,ZDP_NAG,.WIN_INFP,,,_fb,,, 'maximized')
      ?};

::    Zakładka stanów magazynowych
      _far:=\"\";
      _fb:=\"exec('stan_mg_up','magazyn_stan','D')\";
      ZD_NAG.grp_sel(_grp_w,M,'STANY_UP','Stany magazynowe'@,_far,,,23,_fb,,,,'maximized');
      ZD_NAG.tab_splt(_grp_w,,'vertical','stany',',66%');
      _far:=\"\";
      _fb:=\"\";
      exec('addsmmag','magazyn_stan');
      ZD_NAG.grp_sel(_grp_w,__smmag,__smmag.win_sel('?'),,_far,,,23,_fb,,,,'maximized');

::    Zakładka wszystkich pozycji
      _far:=\"\";
      _fb:=\"_env:=params_get().lzkzds_env;
             exec('pr_allZPoz','lzk',_env.TAB_TYPY)\";
      _fa:=\"{? ~ZD_POZ.sel_size() || ZD_POZ.f_clear() ?}\";
      ZD_NAG.grp_sel(_grp_w,ZD_POZ,'ALL','Pozycje dokumentów'@,_far,,,23,_fb,_fa,,,'maximized');

      {? _potw
      ||
::       Zakładka wszystkich pozycji potwierdzeń
         _far:=\"\";
         _fb:=\"_env:=params_get().lzkzds_env;
                exec('pr_allZPtw','lzk',_env.TAB_TYPY)\";
         _fa:=\"{? ~ZDP_POZ.sel_size() || ZDP_POZ.f_clear() ?}\";
         ZD_NAG.grp_sel(_grp_w,ZDP_POZ,'ALL','Pozycje potwierdzeń'@,_far,,,23,_fb,_fa,,,'maximized')
      ?};
::    Zakładka wszystkich realizacji
      _far:=\"\";
      _fb:=\"_env:=params_get().lzkzds_env;
             exec('pr_allZReal','lzk',_env.TAB_TYPY)\";
      _fa:=\"{? ~ZD_RN.sel_size() || ZD_RN.f_clear() ?}\";
      ZD_NAG.grp_sel(_grp_w,ZD_RN,'ALLWER','Realizacje zamówień'@,_far,,,23,_fb,_fa,,,'maximized')

   ||
:: Panel typów zamówień dostaw
      ZD_NAG.grp_sel(_grp_w,.TAB_TYPY,.WIN_TYPY,'Zamówienia do dostawców'@,_far,,,10,_fb,,,,'maximized');

:: Panel zakresu
::      ZD_NAG.tab_splt(_grp_w,'tab0','horizontal','zakres');
::      _far:=\"params_exec('zd_nag_far_zakr','lzk')\";
::      ZD_NAG.grp_edit(_grp_w,ZAKR,.WIN_ZAKR,,_far,,,, 'maximized');

:: Panel zamówień dostaw
      ZD_NAG.tab_splt(_grp_w,,'vertical','zam',',33%');
      _far:=\"params_exec('zd_nag_far_wer','lzk')\";
      ZD_NAG.grp_sel(_grp_w,,.WIN_ZDN_,,_far,,,23,,,,1,'maximized',.wid_win_zdn_,1);

::    Panel szczegółów zamówienia dostaw
      ZD_NAG.tab_splt(_grp_w,'zam','horizontal','info',',66%');
      _fb:=\"params_exec('zd_nag_fb_info','lzk')\";
      ZD_NAG.grp_edit(_grp_w,,.WIN_INFO,,,_fb,,, 'maximized');

      {? _potw
      ||
::       Zakładka potwierdzeń zamówień dostaw
::       Panel zakresu
::         _far:=\"params_exec('zd_ptw_far_zakr','lzk')\";
::         ZD_NAG.grp_edit(_grp_w,ZAKR,.WIN_ZPTW,'Potwierdzenia zamówień'@,_far,,,,'maximized');

::         ZD_NAG.tab_splt(_grp_w,,'vertical','ptw');
         _far:=\"params_exec('zd_ptw_far_wer','lzk')\";
         _fb:=\"params_exec('zdp_nag','zamdst_ptw')\";
         _fa:=\"params_exec('zakr_ind','zamdst_nag')\";
         ZD_NAG.grp_sel(_grp_w,ZDP_NAG,.WIN_PTW_,'Potwierdzenia zamówień'@,_far,,,23,_fb,_fa,,1,'maximized');
         ZD_NAG.tab_splt(_grp_w,'tab0','horizontal','infp',',66%');
         _fb:=\"params_exec('zd_ptw_fb_info','lzk')\";
         ZD_NAG.grp_edit(_grp_w,ZDP_NAG,.WIN_INFP,,,_fb,,, 'maximized')
      ?};

::    Zakładka wszystkich pozycji
      _far:=\"\";
      _fb:=\"_env:=params_get().lzkzds_env;
             exec('pr_allZPoz','lzk',_env.TAB_TYPY)\";
      _fa:=\"{? ~ZD_POZ.sel_size() || ZD_POZ.f_clear() ?}\";
      ZD_NAG.grp_sel(_grp_w,ZD_POZ,'ALL','Pozycje dokumentów'@,_far,,,23,_fb,_fa,,,'maximized');

      {? _potw
      ||
::       Zakładka wszystkich pozycji potwierdzeń
         _far:=\"\";
         _fb:=\"_env:=params_get().lzkzds_env;
                exec('pr_allZPtw','lzk',_env.TAB_TYPY)\";
         _fa:=\"{? ~ZDP_POZ.sel_size() || ZDP_POZ.f_clear() ?}\";
         ZD_NAG.grp_sel(_grp_w,ZDP_POZ,'ALL','Pozycje potwierdzeń'@,_far,,,23,_fb,_fa,,,'maximized')
      ?};
::    Zakładka wszystkich realizacji
      _far:=\"\";
      _fb:=\"_env:=params_get().lzkzds_env;
             exec('pr_allZReal','lzk',_env.TAB_TYPY)\";
      _fa:=\"{? ~ZD_RN.sel_size() || ZD_RN.f_clear() ?}\";
      ZD_NAG.grp_sel(_grp_w,ZD_RN,'ALLWER','Realizacje zamówień'@,_far,,,23,_fb,_fa,,,'maximized')

   ?};

:: Ustawienie okna wertowania
   ZD_NAG.win_sel(.WIN_MAIN);
   ZD_NAG.win_fml(.WIN_ZDN_,,'SYM',,'ICON_BEFORE',exec('zdnag_sym_ib','zamdst_nag'));
   ZD_NAG.win_fml(.WIN_ZDN_,,'DATA',,'ICON_BEFORE',exec('zdnag_tra_ib','zamdst_nag'));
   ZD_NAG.win_fml(.WIN_ZDN_,,'STATUS',,'ICON_BEFORE',exec('zdnag_sym_ob','zamdst_nag'));
   ZD_POZ.win_fml('ALL',,'ZD_NAG','SYM','ICON_BEFORE',exec('zdnag_sym_ib','zamdst_nag'));
   ZD_POZ.win_fml('ALL',,'ZD_NAG','DATA','ICON_BEFORE',exec('zdnag_tra_ib','zamdst_nag'));
   ZD_POZ.win_fml('ALL',POM,'STAN',,'ICON_BEFORE',exec('zknag_sym_ob','zamsiw_nag'));
   ZD_NAG.win_fml(.WIN_ZDN_,EANX,'WYS',,'ICON_BEFORE',exec('zdnag_wys_ib','zamdst_nag'));
   ZDP_NAG.win_fml(.WIN_PTW_,,'SYM',,'ICON_BEFORE',exec('zdptw_sym_ib','zamdst_ptw'));
   ZDP_NAG.win_fml(.WIN_PTW_,,'D_WYS',,'ICON_BEFORE',exec('zdptw_tra_ib','zamdst_ptw'))
";

_env


\lzk_zds
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Główna formuła obszaru LZK_ZDS - zamówienia dostaw
::----------------------------------------------------------------------------------------------------------------------

:: Ustalenie treści linku
_params:=params_get();
{? type_of(_params)>0 & var_pres('LINK',_params)=type_of('')
|| _link:=_params.LINK
|| _link:=''
?};
{? type_of(_params)>0 & var_pres('PROJ_REF',_params)
|| VAR_DEL.delete('__PROJ_REF');__PROJ_REF:=_params.PROJ_REF
?};

exec('eanx_wys','okienka');
_env:=exec('lzk_zds_env','lzk');

{? _env.initObszar()
|| _env.winMain();

:: Obsługa linku
   {? _link<>''
::    Ustalenie parametrów na podstawie przekazanego rekordu
   || _maska:=ref_name(_link)+3;
      {? _maska+2<>'__'
      || exec('openzd','open_tab',_maska);
         ZAKR.ARCH_ROK:='20'+(_maska+2)
      ?};
      _oddz:='';
      _typ:=_oddz_ref:=null();
      _dobrze:=1;
      {? ref_tab(_link)=ZD_NAG
      || _oddz:=exec('FindAndGet','#table',ZD_NAG,_link,,"ODDZ",'');
         _typ:=exec('FindAndGet','#table',ZD_NAG,_link,,"T",null())
      |? ref_tab(_link)=ZD_POZ
      || _oddz:=exec('FindAndGet','#table',ZD_POZ,_link,,"exec('FindAndGet','#table',@.ZD_NAG,ZD_NAG,,\"ODDZ\",'')",'');
         _typ:=exec('FindAndGet','#table',ZD_POZ,_link,,"exec('FindAndGet','#table',@.ZD_NAG,ZD_NAG,,\"T\",null())",null())
      ?};
      {? _oddz='' | _typ = null()
      || _dobrze:=0;
         {? ref_tab(_link)=ZD_POZ
         || FUN.info('Nie odnaleziono pozycji zamówienia dostawy: %1'@[exec('record','#to_string',_link)])
         || FUN.info('Nie odnaleziono zamówienia dostawy: %1'@[exec('record','#to_string',_link)])
         ?}
      ?};

      {? _dobrze
      || _oddz_ref:=exec('FindInSet','#table','ODDZ','KOD2',_oddz,,,1);
::   Wykluczenie dostępu na podstawie uprawnień do danych - oddziały
         {? ~exec('spr_upr','users','ODDZ',_oddz_ref)
         || _dobrze:=0
         ?}
      ?};

      {? _dobrze
::   Brak dostępu do typu zamówienia
      || {? ~_env.TAB_TYPY.find_tab(,'REF',,'=',$_typ)
         || _dobrze:=0;
            FUN.info('Brak uprawnień do typu zamówienia %1.'@[exec('record','#to_string',_typ)])
         ?}
      ?};

      {? _dobrze
::       Ustalenie parametrów sesji
      || __PARSES.setVal('OddzialLogProd',_oddz)
      || return(0)
      ?}

   ?};

   params_set('lzkzds_env',_env,'LINK',_link);

   AreaTitle.setTabWin(ZD_NAG,~~);
   AreaTitle.setTitle();

   {? _link <> ''
   || {? ref_tab(_link)=ZD_NAG & ZD_NAG.seek(_link)
      || ZD_NAG.select(,1,5,,'lzkzds_zdnag',,1)
      |? ref_tab(_link)=ZD_POZ & ZD_POZ.seek(_link,,1) & ZD_NAG.seek(ZD_POZ.ZD_NAG)
      || _f:=$('{? ZD_NAG.ref() || exec(\'zdnag_pozycje_wer\',\'!lzk_zds_dzad\',\'%1\')?}'[_link]);
         ZD_NAG.select(,1,5,,'lzkzds_zdnag',_f,1)
      || return(0)
      ?}
   || ZD_NAG.select()
   ?};

   _env.doneObszar()
?}


\lzk_zds_param_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zmiana parametrów pracy obszaru LZK_ZDS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_oddz:=ST.ODDZ;
{? __PARSES.editDom('LZK')
|| {? _oddz<>ST.ODDZ || exec('init','lzk') ?};
   exec('zakr_set','zamdst_nag');
   AreaTitle.setTitle()
?}


\faks_far_werz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: po odświeżeniu dla okienka WERZ
::----------------------------------------------------------------------------------------------------------------------
_sel_size:=FAKS.sel_size();

{? _sel_size | {? FAKS.f_active() || FAKS.f_get() || FAKS.get() ?}
||
   FAKS.actions_grayed(cur_win(1,1),'');
   _id_act:=exec('faks_dolacz_act_uid','faktury_nag',FAKS.WHERE,FAKS.T().KOR,FAKS.T().HIS,FAKS.OPAK,FAKS.SYMF);
   _czy_z_ok:=exec('czy_z_ok','okresy',-3,0,FAKS.AR,FAKS.AM);
   _czy_zokr:=exec('czy_z_ok','okresy',-3,0,ST.AR,ST.AM);
   _czyanu:={? ~exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZAK_EANU') || 'N(A)' || '' ?};
   _czywyc:=
      {? ~_sel_size
            &
         (FAKS.ZAK='T'
          | FAKS.STAT_REJ='T' & ~exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZAK_EZAK')
          | FAKS.STAT_REJ='Z' & ~exec('chk_role','#b__box',OPERATOR.USER,_id_act)
          | FAKS.STAT_REJ='N')
      || 'N(W)'
      || ''
      ?};
   _czydel:={? _sel_size || '' || 'U' ?};
   _czyakc:={? _sel_size || '' || 'A' ?};
   _czyzak:={? _sel_size || '' || 'Z' ?};
   _przymg:={? FAKS.WHERE='L'
             | FAKS.T().KOR='T'
             | exec('FindInSet','#table','FAP','FAP',FAKS.ref)=null
             | exec('sprfp2dk','faktury_wspolne',FAKS.ref)<>1
            || 'G'
            || ''
            ?};
   _actions:=
      {? ~_czy_z_ok        || {? ~_czy_zokr || 'D' || '' ?}+'ZAN(W)'
                           || ''
      ?}+
      {? FAKS.STAT_REJ='A' || 'D(R)PMGN(IJCAKL(R))'+_czyanu+_czydel+_czyakc+_czyzak+_czywyc
      |? FAKS.STAT_REJ='N' || 'I(DW)D(R)N(L(RH))'+_czywyc+_przymg+_czyanu+_czyakc
      |? FAKS.STAT_REJ='Z' || 'PWD(R)N(L(RH))'+_czywyc+_przymg+_czyanu+_czydel+_czyzak
      |? FAKS.STAT_REJ='T'
         & FAKS.ZAK='N'
      || {? FAKS.WHERE='L' || 'PAZI(D)N(L(RH))'+_czywyc+_przymg+_czyanu+_czydel
                           || 'P'+_czywyc+_przymg+_czyanu+_czydel+_czyakc+_czyzak
         ?}
      |? FAKS.STAT_REJ='T'
         & FAKS.ZAK='T'    || 'PAZI(DW)'+_czywyc+_przymg+_czyanu+_czydel
                           || ''+_czyanu
      ?}+
      {? FAKS.OBI_POW<>'' | FAKS.STAT_REJ='A' | FAKS.STAT_REJ='N'
      || 'N(G)'
      || ''
      ?}+
      {? FAKS.WHERE='K'    || ''
                           || 'M'
      ?}+
:: Dane do intrastat, akceptacja/wycofanie akceptacji danych do intrastat
      {? FAKS.STAT_REJ='A' | FAKS.T & (TYPYSP.UE<>'T' | TYPYSP.KOR='Z')
      || 'N(E)'
      |? FAKS.IST_TYP='' | FAKS.INTRA='T' | FAKS.AKC<>'T'
      || 'N(E(AW))'
      |? FAKS.INTRAKC='T'
      || 'N(E(A))'
      || 'N(E(W))'
      ?}+
:: Rezultat kontroli Businesscheck
      {? ~exec('upgrade2325_blbc1','zbl') | FAKS.T().BL='T'
      || ''
      || 'N(B)'
      ?}+
:: Nalicz wg dokumentów magazynowych
      {? FAKS.INTRA='T'
      || 'N(E(N))'
      || ''
      ?};
   _actions0:=
      {? ~_czy_zokr  || 'D'
                     || ''
      ?};
   FAKS.actions_grayed(cur_win(1,1),_actions+':'+_actions0)
|| FAKS.actions_grayed(cur_win(1,1),'');
   _czy_z_ok:=exec('czy_z_ok','okresy',-3,0);
   _actions0:=
      {? ~_czy_z_ok  || 'D'
                     || ''
      ?};
   FAKS.actions_grayed(cur_win(1,1),':'+_actions0)
?}


\zd_nag_far_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: po odświeżeniu głównego okienka dla ZD_NAG
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().lzkzds_env;
{? ZD_NAG.sel_size() | {? ZD_NAG.f_active() || ZD_NAG.f_get() || ZD_NAG.get() ?}
|| ZD_NAG.actions_grayed(cur_win(1,1),'');
   _msk:=ZD_NAG.name()+2;
   _inftra:={? ~exec('czyTRANSPORT','transport_wspolne',ZD_NAG.uidref()) || 'N(B(I))' || '' ?};
   _czydtr:={? ZD_NAG.STAT_REJ='T' & (';AC'*ZD_NAG.STAN)>1
             & exec('czyTR_NZL','transport_wspolne',ZD_NAG.uidref())
            || ''
            || 'N(B(T))'
            ?}+
            {? exec('czyTR_NZL','transport_wspolne',ZD_NAG.uidref(),1) || 'U' || '' ?};
   _czywyc:={? _msk<>'__'
          | ZD_NAG.STAT_REJ='T' & ~exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZDS_AZAD')
          | ZD_NAG.STAT_REJ='Z' & ~exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZDS_DZAD')
          | ZD_NAG.STAT_REJ='N'
         || 'N(W)'
         || ''
         ?};
   _czyrea:={? exec('FindInSet','#table','ZD_RN','ZAM',ZD_NAG.ref())=null() || 'I' || '' ?};
   _actions:=_czyrea+
      {? _msk='__' || 'N(H)' || '' ?}
      +
      {? ZD_NAG.STAN='N' | ZD_NAG.STAN='A' | ZD_NAG.STAN='C' || '' || 'P' ?}
      +
      {? ZD_NAG.STAN='M' || 'ZN(J)' || '' ?}
      +
      {? ZD_NAG.STAN<>'A' & ZD_NAG.STAN<>'C' || 'R' || '' ?}
      +
      {? _msk<>'__'          || 'ŻWFRDPUZAEN(WJNICOPEB(TIR)S(W)):DO'
      |? ZD_NAG.STAT_REJ='N' || 'AW(DZ)RN(WJUB(RH))'+_czydtr+_inftra
      |? ZD_NAG.STAT_REJ='Z' || {? ZD_NAG.sel_size()=0 || 'U' || '' ?}+'ZRN(JB(RH))'+_czywyc+_czydtr+_inftra
      |? ZD_NAG.STAT_REJ='T' || {? ZD_NAG.sel_size()=0 || 'U' || '' ?}+'AZ'+_czywyc+_czydtr+_inftra
      |? ZD_NAG.STAT_REJ='A' || {? ZD_NAG.sel_size()=0 || 'U' || '' ?}+'PZARN(WJNOPB(R))'+_czydtr+_inftra
      ?};

   {? ~exec('list_nd_without_faks','magdok_wspolne','Zamówienie',1).first() & ~exec('uslNOfakt','magdok_wspolne')
   || _actions:='F'+_actions
   ?};

   {? ZD_NAG.sel_size()=0
   || {? exec('rodzaje_zam_edokumentu','zbl_dok',ZD_NAG.KH,ZD_NAG.T,ZD_NAG.DATA)=''
      || _actions:='C(S)'+_actions
      || {? ZD_NAG.STAT_REJ<>'T' | ZD_NAG.STAN='M' | ZD_NAG.STAN='U' | _msk<>'__'
         || _actions:='N(S(W))'+_actions
         ?}
      ?}
   ?};
   ZD_NAG.actions_grayed(cur_win(1,1),_actions)
|? ~ZD_NAG.size()
|| ZD_NAG.actions_grayed(cur_win(1,1),'');
   _msk:=ZD_NAG.name()+2;
   _actions:=
      {? _msk<>'__'
      || 'ŻWFRDPUZAEN(WJNRHICOPEB(TIRH)S(W)):DO'
      || ''
      ?};
   ZD_NAG.actions_grayed(cur_win(1,1),_actions)
?};
params_set(params_get());
grp_edisp(ZD_NAG,_env.WIN_INFO)


\zd_nag_fb_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: przed obsługą dla okienka INFO
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().lzkzds_env;
_win_red:=_env.WIN_INFO;
_enable:={? {? ZD_NAG.f_active() || ZD_NAG.f_get() || ZD_NAG.get() ?} || 'enable=1' || 'enable=0' ?};
ZD_NAG.efld_opt(_win_red,_enable,,'WAR');
ZD_NAG.efld_opt(_win_red,_enable,INFO,'NAROD');
ZD_NAG.efld_opt(_win_red,_enable,,'WARW');
ZD_NAG.efld_opt(_win_red,_enable,,'WAL');
ZD_NAG.efld_opt(_win_red,_enable,,'KH');
ZD_NAG.efld_opt(_win_red,_enable,,'DATA');
ZD_NAG.efld_opt(_win_red,_enable,,'DTPREAL')


\zd_nag_far_typyzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: po odświeżeniu dla okienka TYPYZAM
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().lzkzds_env;
_Tab:=_env.TAB_TYPY;
_old:=BEER.TYPYZAM;
BEER.TYPYZAM:=exec('FindAndGet','#table',TYPYZAM,_Tab.REF,,,null());
{? BEER.TYPYZAM=null()
|| ZD_NAG.index('GRP_KEY'); ZD_NAG.prefix('xxx')
|| exec('zakr_set','zamdst_nag',_old=BEER.TYPYZAM)
?};
params_set(params_get());
grp_disp(ZD_NAG,_env.WIN_ZDN_);
grp_edisp(ZD_NAG,_env.WIN_INFO)


\zd_nag_fb_typyzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: przed obsługą dla okienka TYPYZAM
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().lzkzds_env;
_Tab:=_env.TAB_TYPY;
BEER.TYPYZAM:=exec('FindAndGet','#table',TYPYZAM,_Tab.REF,,,null())


\zd_nag_far_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: po odświeżeniu dla okienka ZAKR
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().lzkzds_env;
exec('zakr_set','zamdst_nag',0);
params_set(params_get());
::grp_edisp(ZAKR,_env.WIN_ZPTW);
grp_disp(ZD_NAG,_env.WIN_ZDN_);
grp_edisp(ZD_NAG,_env.WIN_INFO)


\lzk_lic
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: Formuła sprawdzająca licencję dla obszaru LZK
::   WY: 'T' - jest licencja, 'N' - nie ma licencji
::----------------------------------------------------------------------------------------------------------------------
_result:='N';
_domain:=exec('domain_ref','#b_domain','LZK');
{? _domain<>null()
|| {? exec('lic','#b_domain',_domain)=1
   || _result:='T'
   ?}
?};
_result


\disp_all_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: wyświetlenie całego dokumentu
::----------------------------------------------------------------------------------------------------------------------
FAKS.cntx_psh();
FAP.cntx_psh();
_refn:=FAP.FAKS;
_refp:=FAP.ref();
FAP.FAKS().SYM;
_red:=exec('faks_win_edit_set','faktury_nag',1);

FAP.index('FAP');
FAP.prefix(_refn);
FAP.seek(_refp);
exec('dselWithNag','okienka','FAP','WER_DOK','FAKS',_red,,'Dokument %1'@[FAP.FAKS().SYM]);
FAKS.cntx_pop();
FAP.cntx_pop();
~~


\disp_all_zad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: wyświetlenie całego zamówienia
::   WE: [_a] - uidref ZD_NAG
::----------------------------------------------------------------------------------------------------------------------
_uid:={? var_pres('_a')=type_of('') & (+_a)=48 || _a || '' ?};
_refn:=null();
_refp:=null();

ZD_NAG.cntx_psh();
ZD_POZ.cntx_psh();
{? _uid<>''
|| _msk:=form(8+(_uid+16));
   ZD_NAG.use(_msk);
   ZD_POZ.use('zdpoz'+(_msk+3));
   ZD_NAG.prefix();
   {? ZD_NAG.seek(_uid)
   || _refn:=ZD_NAG.ref();
      _refp:=exec('FindInSet','#table','ZD_POZ','POZ',ZD_NAG.ref(),,,,,null());
      ZD_NAG.SYM
   ?}
|| _refn:=ZD_POZ.ZD_NAG;
   _refp:=ZD_POZ.ref();
   ZD_POZ.ZD_NAG().SYM
?};
_edit:=ZD_NAG.win_edit('?');
_sele:=ZD_POZ.win_sel('?');
_red:=ZD_NAG.mk_edit('',,'zamdst_xxx');
ZD_NAG.win_ewin(_red,,'RED');
ZD_NAG.win_edit(_red);
ZD_POZ.win_sel('WER_DOK');
ZD_POZ.index('POZ');
ZD_POZ.prefix(_refn);
ZD_POZ.seek(_refp);
exec('dselWithNag','okienka','ZD_POZ','WER_DOK','ZD_NAG',_red,,'Zamówienie dostaw %1'@[ZD_POZ.ZD_NAG().SYM]);
ZD_NAG.win_edit(_edit);
ZD_POZ.win_sel(_sele);
ZD_NAG.cntx_pop();
ZD_POZ.cntx_pop();
~~


\all_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: wyświetlenie wszystkich pozycji zakupów
::----------------------------------------------------------------------------------------------------------------------
_fap:=exec('FindInSet','#table','FAP','FAP',1,FAKS.ref());
FAP.index('ALL');
{? FAP.sel_size() || FAP.sel_adel() ?};
FAP.f_clear();
FAP.f_set('FAKS(DW),FAKS(SYM),POZ','join FAKS'
 ,'FAKS.SZ=\'Z\''+{? ST.STZ<>null() || ' and FAKS.STS=:_a' || '' ?},ST.STZ);
{? FAP.f_active() || FAP.f_seek(_fap) || FAP.seek(_fap) ?};
~~


\pr_allZPoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.02]
:: OPIS: przed dla wszystkich pozycji zamówień
::   WE: _a - tabela typów zamówień
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;
_zdp:=exec('FindInSet','#table','ZD_POZ','POZ',1,ZD_NAG.ref());
_tref:=_Tab.ref();
_in:='(';
{? _Tab.f_active()
|| {? _Tab.f_first() || {! |? _in+='\''+_Tab.REF+'\','; _Tab.f_next() !}; _Tab.f_seek(_tref) ?}
|| {? _Tab.first() || {! |? _in+='\''+_Tab.REF+'\','; _Tab.next() !}; _Tab.seek(_tref) ?}
?};
_in:=(_in-1)+')';
{? (+_in)=1 || _in:='(\'\')' ?};
ZD_POZ.prefix();
{? ZD_POZ.sel_size() || ZD_POZ.sel_adel() ?};
ZD_POZ.f_clear();
ZD_POZ.f_set('ZD_NAG(DATA),ZD_NAG(SYM),POZ'
   ,'join ZD_NAG using(ZD_NAG.REFERENCE,ZD_POZ.ZD_NAG)'
   ,'\"ZD_NAG\".T in '+_in);
ZD_POZ.f_seek(_zdp);
~~


\zd_ptw_far_zakr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.00]
:: OPIS: po odświeżeniu dla okienka ZAKR
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().lzkzds_env;
exec('zakr_set','zamdst_ptw',0);
params_set(params_get());
grp_disp(ZDP_NAG,_env.WIN_PTW_);
grp_edisp(ZD_NAG,_env.WIN_INFP);
:: grp_edisp(ZAKR,_env.WIN_ZAKR);
~~


\zd_ptw_far_wer
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.02]
:: OPIS: po odświeżeniu głównego okienka dla ZD_NAG
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().lzkzds_env;
{? ZDP_NAG.sel_size() | {? ZDP_NAG.f_active() || ZDP_NAG.f_get() || ZDP_NAG.get() ?}
|| ZDP_NAG.actions_grayed(cur_win(1,1),'');
   _msk:=ZDP_NAG.name()+2;
   _inftra:={? ~exec('czyTRANSPORT','transport_wspolne',ZDP_NAG.uidref()) || 'N(B(I))' || '' ?};
   _dorea:={? ~exec('ctrlREA','zamdst_ptw') || 'R' || '' ?};
   _czydtr:={? ZDP_NAG.STAT_REJ='T'
             & exec('czyTR_NZL','transport_wspolne',ZDP_NAG.uidref())
            || ''
            || 'N(B(T))'
            ?}+
            {? exec('czyTR_NZL','transport_wspolne',ZDP_NAG.uidref(),1) || 'U' || '' ?};
   _czywyc:={? _msk<>'__'
          | ZDP_NAG.STAT_REJ='T' & ~exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZDS_APTW')
          | ZDP_NAG.STAT_REJ='Z' & ~exec('chk_role','#b__box',OPERATOR.USER,'LZK_ZDS_DPTW')
          | ZDP_NAG.STAT_REJ='N'
         || 'N(W)'
         || ''
         ?};
   _czyrea:={? exec('FindInSet','#table','ZD_RN','ZDP_NAG',$ZDP_NAG.ref(),$ZDP_NAG.ref())=null() || 'E' || '' ?};

   _actions:=
      {? _msk<>'__'           || 'DpURZAN(WCB(TI)):D'
      |? ZDP_NAG.STAT_REJ='N' || 'REAN(H)'+_czywyc
      |? ZDP_NAG.STAT_REJ='Z' || {? ZDP_NAG.sel_size()=0 || 'U' || '' ?}+'N(H)REZUP'+_czywyc+_czydtr
      |? ZDP_NAG.STAT_REJ='T' || {? ZDP_NAG.sel_size()=0 || 'U' || '' ?}+'N(H)UPAZ'+_dorea+_czywyc+_czyrea+_czydtr
      ?};

   ZDP_NAG.actions_grayed(cur_win(1,1),_actions)
|? ~ZDP_NAG.size()
|| ZDP_NAG.actions_grayed(cur_win(1,1),'');
   _msk:=ZDP_NAG.name()+2;
   _actions:=
      {? _msk<>'__'           || 'DPUYIZARNLT:D'
      || ''
      ?};
   ZDP_NAG.actions_grayed(cur_win(1,1),_actions)
?};
grp_edisp(ZDP_NAG,_env.WIN_INFP)


\zd_ptw_fb_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [19.02]
:: OPIS: przed obsługą dla okienka INFO
::----------------------------------------------------------------------------------------------------------------------
_win_red:='INFO';
_enable:={? {? ZDP_NAG.f_active() || ZDP_NAG.f_get() || ZDP_NAG.get() ?} || 'enable=1' || 'enable=0' ?};
ZDP_NAG.efld_opt(_win_red,_enable,,'KH');
ZDP_NAG.efld_opt(_win_red,_enable,,'D_WYS');
ZDP_NAG.efld_opt(_win_red,_enable,,'D_REA');
ZDP_NAG.efld_opt(_win_red,_enable,,'FAKTURA')


\pr_allZPtw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.14]
:: OPIS: przed dla wszystkich pozycji potwiedzeń zamówień
::   WE: _a - tabela typów zamówień
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;
_zdp:=exec('FindInSet','#table','ZDP_POZ','ZDP_POZ',ZDP_NAG.ref());
_tref:=_Tab.ref();
_in:='(';
{? _Tab.f_active()
|| {? _Tab.f_first() || {! |? _in+='\''+_Tab.REF+'\','; _Tab.f_next() !}; _Tab.f_seek(_tref) ?}
|| {? _Tab.first() || {! |? _in+='\''+_Tab.REF+'\','; _Tab.next() !}; _Tab.seek(_tref) ?}
?};
_in:=(_in-1)+')';
{? (+_in)=1 || _in:='(\'\')' ?};
ZDP_POZ.prefix();
{? ZDP_POZ.sel_size() || ZDP_POZ.sel_adel() ?};
ZDP_POZ.f_clear();
ZDP_POZ.f_set('ZDP_NAG(D_WYS),ZDP_NAG(SYM),ZD_P_POZ'
   ,'join @ZD_POZ using(ZD_POZ.REFERENCE,ZDP_POZ.ZD_POZ) join @ZD_NAG using(ZD_NAG.REFERENCE,ZD_POZ.ZD_NAG)'
   ,'\"ZD_NAG\".T in '+_in);
ZDP_POZ.f_seek(_zdp);
~~


\disp_all_ztw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: wyświetlenie całego zamówienia
::----------------------------------------------------------------------------------------------------------------------
ZD_NAG.cntx_psh();
ZD_POZ.cntx_psh();
ZDP_NAG.cntx_psh();
ZDP_POZ.cntx_psh();
_msk:=form(8+ZDP_POZ.ZD_POZ)+3;
{? _msk<>(ZD_POZ.name()+3)
|| ZD_NAG.use('zdnag'+_msk);
   ZD_POZ.use('zdpoz'+_msk)
?};
_refp:=exec('FindAndGet','#table',ZD_POZ,ZDP_POZ.ZD_POZ,,,null());
ZD_POZ.prefix();
_refn:={? ZD_POZ.seek(_refp) || ZD_POZ.ZD_NAG || null() ?};
ZD_POZ.ZD_NAG().SYM;
_edit:=ZD_NAG.win_edit('?');
_sele:=ZD_POZ.win_sel('?');
_red:=ZD_NAG.mk_edit('',,'zamdst_xxx');
ZD_NAG.win_ewin(_red,,'RED');
ZD_NAG.win_edit(_red);
ZD_POZ.win_sel('WER_DOK');
ZD_POZ.index('POZ');
ZD_POZ.prefix(_refn);
ZD_POZ.seek(_refp);
exec('dselWithNag','okienka','ZD_POZ','WER_DOK','ZD_NAG',_red,,'Zamówienie dostaw %1'@[ZD_POZ.ZD_NAG().SYM]);
ZD_NAG.win_edit(_edit);
ZD_POZ.win_sel(_sele);
ZD_NAG.cntx_pop();
ZD_POZ.cntx_pop();
ZDP_NAG.cntx_pop();
ZDP_POZ.cntx_pop();
~~


\disp_all_ptw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [18.02]
:: OPIS: wyświetlenie całego zamówienia
::----------------------------------------------------------------------------------------------------------------------
ZD_NAG.cntx_psh();
ZD_POZ.cntx_psh();
ZDP_NAG.cntx_psh();
ZDP_POZ.cntx_psh();

_refp:=ZDP_POZ.ref();
_refn:=ZDP_POZ.ZDP_NAG;
ZDP_POZ.ZDP_NAG().SYM;
_edit:=ZDP_NAG.win_edit('?');
_sele:=ZDP_POZ.win_sel('?');
_red:=ZDP_NAG.mk_edit('',,'zamptw_xxx');
ZDP_NAG.win_ewin(_red,,'RED');
ZDP_NAG.win_edit(_red);
ZDP_POZ.win_sel('WER_DOK');
ZDP_POZ.index('ZDP_NAG');
ZDP_POZ.prefix(_refn);
ZDP_POZ.seek(_refp);
_act:=ZDP_POZ.actions('WER_DOK','DpUZ:D');
exec('dselWithNag','okienka','ZDP_POZ','WER_DOK','ZDP_NAG',_red
 ,,'Potwierdzenie zamówienia dostaw %1'@[ZDP_POZ.ZDP_NAG().SYM]);
ZDP_NAG.win_edit(_edit);
ZDP_POZ.win_sel(_sele);
ZDP_POZ.actions('WER_DOK',_act);
ZD_NAG.cntx_pop();
ZD_POZ.cntx_pop();
ZDP_NAG.cntx_pop();
ZDP_POZ.cntx_pop();
~~


\bl_jmz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [20.42]
:: OPIS: wartość początkowa pola JMZ
::----------------------------------------------------------------------------------------------------------------------
exec('get','#params',400999,2)='T'


\pr_allZReal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.37]
:: OPIS: przed dla wszystkich realizacji zamówień
::----------------------------------------------------------------------------------------------------------------------
_zdrn:=exec('FindInSet','#table','ZD_RN','ZAM',ZD_NAG.ref());
_Tab:=_a;
_tref:=_Tab.ref();
_in:='(';
{? _Tab.f_active()
|| {? _Tab.f_first() || {! |? _in+='\''+_Tab.REF+'\','; _Tab.f_next() !}; _Tab.f_seek(_tref) ?}
|| {? _Tab.first() || {! |? _in+='\''+_Tab.REF+'\','; _Tab.next() !}; _Tab.seek(_tref) ?}
?};
_in:=(_in-1)+')';
{? (+_in)=1 || _in:='(\'\')' ?};
ZD_RN.prefix();
{? ZD_RN.sel_size() || ZD_RN.sel_adel() ?};
ZD_RN.f_clear();
ZD_RN.f_set('ZD_NAG(NR),DR,CR'
   ,'join ZD_NAG using(ZD_NAG.REFERENCE,ZD_RN.ZD_NAG)'
   ,'\"ZD_NAG\".T in '+_in);
ZD_RN.f_seek(_zdrn);
~~


\zakr_lzk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: zakres dokumentów zakupu
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
FILTER.win_edit('STATUS');
{? FILTER.edit()
|| exec('lzk_zak_filtr','lzk');
   params_set('env',_env);
   grp_disp(FAKS,'WERZ',1,1);
   AreaTitle.setTitle()
?};
~~

:Sign Version 2.0 jowisz:1045 2024/01/29 14:27:18 4c81f80ec9989d56e3115a79809bfad69dcda4b7ecd2ad0d3be38b16eab697fbc1ba9f46d0bc33e62a18391f404918f9de7da9c65b742bf650854d79a5ea39c76c86aaccf85fa0601a0a0e5a2f00a1ca518afe1c43c2c7d9b600160e92731c22d86f6e230e7282aae47200ea59f7e47e4b4c090482494b4d4385e706d21ff15d

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: st_wer.fml
:: Utworzony: 03.06.2020
:: Autor: AKUL
::======================================================================================================================
:: Zawartość: Obsługa cenzurowania plików źródłowych dla systemu Statystyki
::======================================================================================================================

\start
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzuruje przekazany plik
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - ścieżka do pliku
::       _c - STRING - nazwa pliku
::   WY: 0 - porażka (błąd)
::       1 - sukces (ale nic nie ocenzurowano)
::       2 - sukces (i plik został ponownie zapisany na dysku)
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_filepath:=_b;
_filename:=_c;

_can_continue:=1;
_result:=0;
_censure_count:=0;

_mydo:=do_state()=0;
{? _mydo || do() ?};

_tab_src:=_env.TAB_SRC;
_tab_src.cntx_psh();
_tab_src.index(_env.NDX_SRC2);

_banned_titles:=tab_tmp(1,'TITLE','STRING[255]','Nazwa pola 1');

_rodzaje:='SW,SWE,EW,EWE,CW,CWE,GW,GWE,DW,BA,FBA,POPUP,POPUPITEM';

_split:=spli_str(_rodzaje,',');
{! _it:=1..obj_len(_split)
|? _can_continue>0
|! _kind:=_split[_it];
   {? _kind<>''
   || _tab_src.prefix(_filepath,_kind,);
      {? _tab_src.first()
      || {!
         |? _line:=_tab_src.memo_txt(,1,'LINE');
            {? _line<>''
            || _censured_txt:=exec('censure_line','st_censure',_env,_line,_tab_src.KIND,_filename,_banned_titles);
               {? _censured_txt<>'' & _censured_txt<>_line
               || _censure_count+=1;
                  _tab_src.memo_set(_censured_txt,'LINE');
                  _can_continue:=_tab_src.memo_put(,'LINE')
               ?}
            ?};
            _tab_src.next() & _can_continue>0
         !}
      ?}
   ?}
!};
{? _can_continue>0
|| {? _censure_count>0
   || _tab_src.index(_env.NDX_SRC1);
      _tab_src.prefix(_filepath,);
      {? _tab_src.first()
      || _file:=fopen(_filepath,'Uw',,,1);
         {? _file.is_open()>0
         ||
            {!
            |? _line:=_tab_src.memo_txt(,1,'LINE');
               fwrite(_file,_line);
               _tab_src.next()
            !};
            fclose(_file)
         || _can_continue:=0
         ?}
      ?}
   ?}
?};
_tab_src.cntx_pop();

{? _can_continue>0
|| {? _censure_count>0
   || _result:=2
   || _result:=1
   ?}
|| undo()
?};

{? _mydo || end() ?};
_result


\censure_line
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzuruje przekazaną linię
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - rodzaj zapisu
::       _d - STRING - nazwa pliku
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: STRING - ocenzurowana treść
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_line:=_b;
_kind:=_c;
_filename:=_d;
_banned_titles:=_e;

{? _line<>''
||
:: WYciągam czas UTC
   _split:=spli_str(_line,_env.SEPARATOR);
   _utc:='';
   {? obj_len(_split)>1
   || _utc:=_split[2]
   ?};

   _res_obj:=exec('create_parse_obj','st_parse_core',_env,_line,_filename);

   {? _res_obj.RESULT>0
   || _obj:=_res_obj.OBJ;
      _rule:='exec(\'censure_%1\',\'st_censure\',_a,_b,_c,_d,_e)'[_kind];
      _obj_res:=($_rule)(_env,_line,_filename,_obj,_banned_titles);
      {? var_pres('_obj_res')>100
      || _line:=exec('parse_obj_to_string','st_parse_core',_env,_obj_res,_utc)
      ?}
   ?}
?};
_line


\censure_text
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Zwraca tekst który świadczy o tym że została zrobiona cenzura
::   WY: STRING
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
''


\censure_SW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów SW
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_line:=_b;
_filename:=_c;
_obj:=_d;
_banned_titles:=_e;

{? var_pres('_obj')>100
|| {? _obj.Tmp='1' & _obj.Title<>''
   || _banned_titles.prefix(_obj.Title,);
      {? _banned_titles.first()=0
      || _banned_titles.TITLE:=_obj.Title;
         _banned_titles.add(1)
      ?};
      _banned_titles.prefix();
      _obj.Title:=exec('censure_text','st_censure')
   ?}
?};
_obj


\censure_SWE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów SWE
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_line:=_b;
_filename:=_c;
_obj:=_d;
_banned_titles:=_e;

{? var_pres('_obj')>100
||
   {? _obj.Title<>''
   || _banned_titles.prefix(_obj.Title,);
      {? _banned_titles.first()
      || _obj.Title:=exec('censure_text','st_censure')
      ?};
      _banned_titles.prefix()
   ?}
?};
_obj


\censure_EW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów EW
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('censure_SW','st_censure',_a,_b,_c,_d,_e)


\censure_EWE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów EWE
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('censure_SWE','st_censure',_a,_b,_c,_d,_e)


\censure_CW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów CW
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('censure_SW','st_censure',_a,_b,_c,_d,_e)


\censure_CWE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów CWE
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('censure_SWE','st_censure',_a,_b,_c,_d,_e)


\censure_GW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów GW
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('censure_SW','st_censure',_a,_b,_c,_d,_e)


\censure_GWE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów GWE
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('censure_SWE','st_censure',_a,_b,_c,_d,_e)


\censure_DW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów DW
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_line:=_b;
_filename:=_c;
_obj:=_d;
_banned_titles:=_e;

{? var_pres('_obj')>100
|| {? _obj.Title<>''
   || _obj.Title:=exec('censure_text','st_censure')
   ?}
?};
_obj


\censure_BA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów BA
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_line:=_b;
_filename:=_c;
_obj:=_d;
_banned_titles:=_e;

{? var_pres('_obj')>100
||
   {? _obj.Ident*':'>0
   || _obj.Ident:=exec('censure_text','st_censure')
   ?}
?};
_obj


\censure_FBA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów FBA
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('censure_BA','st_censure',_a,_b,_c,_d,_e)


\censure_POPUP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów POPUP
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
_env:=_a;
_line:=_b;
_filename:=_c;
_obj:=_d;
_banned_titles:=_e;

{? var_pres('_obj')>100
|| {? _obj.Title*'Koszty uzyskania:'>0
   || _obj.Title:='Koszty uzyskania:'
   ?}
?};
_obj


\censure_POPUPITEM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [21.14]
:: OPIS: Cenzura zapisów POPUPITEM
::   WE: _a - obj_new - obiekt środowiska, wynik działania exec('env_parse','st_common')
::       _b - STRING - linia
::       _c - STRING - nazwa pliku
::       _d - obj_new - obiekt z wartościami
::       _e - tab_tmp - tablica tymczasowa z tytułami niedozwolonymi
::   WY: obj_new - ocenzurowany obiekt
::  TAG: <PRYWATNA>
::----------------------------------------------------------------------------------------------------------------------
exec('censure_POPUP','st_censure',_a,_b,_c,_d,_e)

:Sign Version 2.0 jowisz:1048 2021/04/09 15:26:32 07fc726959226ac69c7d7c5149c081bab4801bd46ffb585868fe6013946390004d724f0f860b5194c58ec857d4046596d7451d2a87f1a4228392e794abc5be3ed8d3f57b9c188ed4345ab63209912234bff6c2fb34ac1d1bcc879c536b2b7a71224e9e262dc8b8b3b250fadf5eede43723c2489b8150fe70f67c3fc3cbb6942f

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: waluty.fml
:: Utworzony: 06.03.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa walut
::======================================================================================================================


\czy_euro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.51]
:: OPIS: Sprawdza, czy dopuszczalne jest uzywanie podanego symbolu waluty (ze wzgledu na wykonanie przeliczenia na EURO)
::   WE: _a - symbol waluty
::       _b (0/1) - czy wyswietlac komunikat w przypadku wyniku 0
::  OLD: \czy_euro/skid_wal.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(9)='N' & exec('czy_nar','waluty',_a,0)
|| {? _b
   || FUN.info('Waluty państw unii walutowej nie są dopuszczalne.\n(należy użyć waluty "EUR").'@)
   ?}; 0
|| 1
?}


\czy_nar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.51]
:: OPIS: Sprawdza, czy symbol waluty nalezy do walut panstw unii walutowej
::   WE: _a - symbol waluty
::       _b - czy wyswietlac komunikat w przypadku wyniku 0
::  OLD: \czy_nar/skid_wal.fml
::----------------------------------------------------------------------------------------------------------------------
{? +_a=3
|| {? 'DEM,FRF,ATS,ITL,ESP,NLG,BEF,FIM,PTE,LUF,IEP,GRD,SIT,MTL,CYP,SKK,EEK'*_a>0
   || 1
   || {? _b
      || FUN.info('Waluta o symbolu: %1 nie należy do walut państw '
                  'unii walutowej.\nNie będzie przeliczana na EURO.'@[_a]);
         0
      ?}
   ?}
?}


\euro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.51]
:: OPIS: Funkcja sprawdza czy jedna z podanych walut jest EURO, a druga z unii walutowej
::   WE: _a - waluta pierwsza - ref SLO
::       _b - waluta druga - ref SLO
::   WY: 0 - istnieje, 1 - nie istnieje
::  OLD: \euro/dok_zrd2.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(9)='T'
|| _ok:=1;
   SLO.cntx_psh(); SLO.prefix();
   {? SLO.seek(_a)
   || _w:=SLO.KOD;
      {? SLO.seek(_b)
      || {? (_w='EUR' & exec('czy_nar','waluty',SLO.KOD,0)) |
            (SLO.KOD='EUR' & exec('czy_nar','waluty',_w,0))
         || _ok:=0
         ?}
      ?}
   ?};
   SLO.cntx_pop(); _ok
|| 0
?}


\jed_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Formuła zwraca jednostkę dla symbolu waluty
::  OLD: \jed_wal/roz_krs.fml
::----------------------------------------------------------------------------------------------------------------------
WAL.index('WAL_SYM'); WAL.prefix();
_j:={? WAL.find_key(_a) || WAL.J ?};
{? _j=0 || _j:=1 ?};
_j


\ten_walu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO
:: OPIS: Wybór waluty
::  OLD: \ten_walu/param.fml
::----------------------------------------------------------------------------------------------------------------------
wyb:=1;
sel_exit


\okn_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.40]
:: OPIS: Stworzenie okienek tabeli SLO do wyboru waluty
::  OLD: \okn_wal/skid_wal.fml
::----------------------------------------------------------------------------------------------------------------------
_wer:=SLO.mk_sel('Waluty'@,'P',,'slo_wer4',,,,,'U');
SLO.win_fld(_wer,,'KOD',,,8,,1,'Kod'@,,'Kod pozycji słownika walut'@);
SLO.win_fld(_wer,,'TR',,,40,,1,,,'Treść pozycji słownika walut'@);
SLO.win_act(_wer,0,'Formuła','Wybierz'@@,,'Wybór bieżącego zapisu'@,,"POMOC.WAL_TKRS:=SLO.ref(); sel_exit()",1,,,,'W');
SLO.win_act(_wer,0,'Wyświetl',,,,"exec('pw_kodsl','slo_slu')","SLO.display()");
SLO.win_sel(_wer)


\eur_tkrs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [21.14_09]
:: OPIS: Formuła do przeglądanie tabel kursów EUR
::   WE: _a - ref waluty (SLO)
::----------------------------------------------------------------------------------------------------------------------
exec('tkrs_eb','fks_viudo',1);
TKRS.prefix(); KRS.prefix();
TKRS.win_sel('WERGEB');
SLO.cntx_psh(); SLU.cntx_psh();
exec('okn_wal','waluty');
SLO.win_dict('ONE');
SLO.win_sel('ONE');
POMOC.WAL_TKRS:=_a;
POMOC.BANKEB:=_b;
{? TKRS.get() | TKRS.last()
|| KRS.index('KRS_KRAJ');
   KRS.prefix(TKRS.ref());
   KRS.first()
|| TKRS.BANK:=SLO.ref()
?};
TKRS.actions('WEREB','R');
TKRS.select(,1,5);
TKRS.actions('WEREB');
POMOC.WAL_TKRS:=null();
SLO.cntx_pop(); SLU.cntx_pop();
TKRS.actions('WEREB','');
KRS.actions('WEREB','');
exec('tkrs_eb','fks_viudo',2);
1


\ban_tkrs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DRO
:: OPIS: Formuła do przeglądanie tabel kursów
::   WE: _a - ref waluty (SLO)
::       _b - ref banku (SLO)
::  OLD: \ban_tkrs/skid_wal.fml
::----------------------------------------------------------------------------------------------------------------------
TKRS.cntx_psh();
KRS.cntx_psh();
TKRS.win_sel('WERG');
SLO.cntx_psh();
exec('okn_wal','waluty');
SLO.win_dict('ONE');
SLO.win_sel('ONE');
POMOC.WAL_TKRS:=_a;
POMOC.BANK:=SLO.ref();
TKRS.index('TKRS_SM');
TKRS.prefix(POMOC.BANK,_a);
TKRS.index('TKRS_DT');
TKRS.prefix(POMOC.BANK,_a);
{? TKRS.get() | TKRS.last()
|| KRS.index('KRS_KRAJ');
   KRS.prefix(TKRS.ref());
   KRS.first()
|| TKRS.BANK:=SLO.ref()
?};
SLU.cntx_psh();
TKRS.actions('WER','R');
TKRS.select(,1,5);
TKRS.actions('WER');
SLU.cntx_pop();
POMOC.WAL_TKRS:=null();
SLO.cntx_pop();
TKRS.actions('WER','');
KRS.actions('WER','');
TKRS.cntx_pop();
KRS.cntx_pop();
1

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:25 27f91faad97c33661034d6bf61a15283787e6165e86bc539f5a5064e95475a089a27c58762875fcf533314d3dc73bf6411ffac0a6039e8c6dd41c10ac572af753170bea4db09c4db1bb522d4f81fdae92fdb08e9b733d5a287bffa6c7e182b9604bdf9b4087e73f4c9ecd1823a96bdd1f366751aff6741a734e34b8bb6e4ea19

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: plob_pb.fml
:: Utworzony: 04.07.2018
:: Autor: TMR
::======================================================================================================================
:: Zawartość: Formuły do obsługi tabeli formuł wykorzystywanych do definiowania obiektu lokalizacji
::======================================================================================================================

\trig_add_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Trigger przed dodaniem PLOB_PB
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('trig_mod_b','plob_pb')


\trig_put_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Trigger przed poprawieniem PLOB_PB
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('trig_mod_b','plob_pb')


\trig_mod_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Trigger przed modyfikacją PLOB_PB
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? PLOB_PB.PLOB_H
|| PLOB_PB.PLOB_L:=PLOB_PB.PLOB_H().PLOB_L
?};
1


\mod_bpodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Modyfikuje dzien w brudnopisie pracownika, gdzie dzielony jest czas pracy na obiekty (np. jedn. org.)
::   WE:  _a [REFERENCE] - wskazanie na pracownika
::        _b [STRING]    - warstwa 'p'(Plan)/'g'(Grafik)/'w'(Wykonanie)
::        _c [DATE]      - DATA_DP      data doby pracowniczej
::        _d [DATE]      - DATA_OD      data początku podziału
::        _e [DATE]      - DATA_DO      data końca podziału
::        _f [TIME]      - POCZATEK     początek podziału
::        _g [TIME]      - KONIEC       koniecz podziału
::        _h [TIME]      - CZAS         czas podziału
::        _i [STRING]    - PLOB_H.ref   wskazanie na obiekt kosztowy
::   WY: -1/0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
  _p_ref:={? var_pres('_a')=type_of(null) || _a || return(_result) ?};
_warstwa:={? var_pres('_b')=type_of('')   || _b || return(_result) ?};
_data_dp:={? var_pres('_c')=type_of(date) || _c || return(_result) ?};
_data_od:={? var_pres('_d')=type_of(date) || _d || return(_result) ?};
_data_do:={? var_pres('_e')=type_of(date) || _e || return(_result) ?};
_godz_od:={? var_pres('_f')=type_of(time) || _f || return(_result) ?};
_godz_do:={? var_pres('_g')=type_of(time) || _g || return(_result) ?};
   _czas:={? var_pres('_h')=type_of(time) || _h || return(_result) ?};
_sPlob_h:={? var_pres('_i')=type_of('')   || _i || return(_result) ?};
_result:=0;

_warstwa:=~-_warstwa;
{? _warstwa='P' | _warstwa='G' | _warstwa='W' || 1 || return(_result) ?};
_plob_h:=exec('FindAndGet','#table',PLOB_H,_sPlob_h,,,null());

PLOB_PB.cntx_psh();
PLOB_PB.index('ID_P');
PLOB_PB.prefix();
P.cntx_psh();
P.index('OSOBA');
P.prefix();
{? _plob_h & P.seek(_p_ref)
|| PLOB_PB.prefix(_p_ref,_warstwa,_data_dp,_data_od,_godz_od);
   {? ~PLOB_PB.first()
   || PLOB_PB.blank(1);
      PLOB_PB.P:=_p_ref;
      PLOB_PB.GPW:=_warstwa;
      PLOB_PB.PLOB_H:=_plob_h;
      PLOB_PB.DATA_DP:=_data_dp;
      PLOB_PB.DATA_OD:=_data_od;
      PLOB_PB.DATA_DO:=_data_do;
      PLOB_PB.GODZ_OD:=_godz_od;
      PLOB_PB.GODZ_DO:=_godz_do;
      PLOB_PB.GR:=_czas;
      PLOB_PB.STATUS:='P';
      _result:=PLOB_PB.add()
   || PLOB_PB.PLOB_H:=_plob_h;
      PLOB_PB.DATA_DO:=_data_do;
      PLOB_PB.GODZ_DO:=_godz_do;
      PLOB_PB.GR:=_czas;
      PLOB_PB.STATUS:='M';
      _result:=PLOB_PB.put()
   ?}
?};
PLOB_PB.cntx_pop();
P.cntx_pop();
_result


\rewr_draft
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Funkcja przepisuje dane z brudnopisu do właściwego podziału czasu pracy na obiekty
::   WE:  _a [REFERENCE] - wskazanie na pracownika
::        _b [STRING]    - warstwa 'p'(Plan)/'g'(Grafik)/'w'(Wykonanie)
::        _c [DATE]      - data podziału
::   WY: -1/0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
  _p_ref:={? var_pres('_a')=7 || _a   || return(_result) ?};
_warstwa:={? var_pres('_b')=2 || ~-_b || return(_result) ?};
_data_dp:={? var_pres('_c')=4 || _c   || return(_result) ?};

:: powrot w przypadku zerwanej transakcji
{? do_state()=2 || return(_result) ?};

:: zalozenie transakcji
_mydo:=do_state()=0;
{? _mydo || do() ?};

_result:=exec('del_podz','plob_p',_p_ref,_warstwa,_data_dp);
{? _result>0
|| PLOB_PB.cntx_psh();
   PLOB_PB.index('ID_P');
   PLOB_PB.prefix(_p_ref,_warstwa,_data_dp);
   {? PLOB_PB.first()
   || PLOB_P.cntx_psh();
      PLOB_P.prefix();
      {!
      |? PLOB_P.blank(1);
         PLOB_P.P:=_p_ref;
         PLOB_P.GPW:=_warstwa;
         PLOB_P.PLOB_H:=PLOB_PB.PLOB_H;
         PLOB_P.DATA_DP:=_data_dp;
         PLOB_P.DATA_OD:=PLOB_PB.DATA_OD;
         PLOB_P.DATA_DO:=PLOB_PB.DATA_DO;
         PLOB_P.GODZ_OD:=PLOB_PB.GODZ_OD;
         PLOB_P.GODZ_DO:=PLOB_PB.GODZ_DO;
         PLOB_P.GR:=PLOB_PB.GR;
         PLOB_P.STATUS:='P';
         _result+=PLOB_P.add(1);
         PLOB_PB.next()
      !};
      PLOB_P.cntx_pop()
   ?};
   PLOB_PB.cntx_pop()
?};
{? _result>0
|| _result:=exec('del_draft','plob_pb',_p_ref,_warstwa,_data_dp)
?};
{? _result=0 || undo() ?};
{? _mydo || end() ?};
_result


\del_draft
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [18.42]
:: OPIS: Funkcja sprawdza czy są, a jeżeli tak to usuwa podziały z brudnopisu na wskazany dzień
::   WE:  _a [REFERENCE] - wskazanie na pracownika
::        _b [STRING]    - warstwa 'p'(Plan)/'g'(Grafik)/'w'(Wykonanie)
::        _c [DATE]      - data podziału
::   WY: -1/0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
  _p_ref:={? var_pres('_a')=type_of(null) || _a   || return(_result) ?};
_warstwa:={? var_pres('_b')=type_of('')   || ~-_b || return(_result) ?};
_data_dp:={? var_pres('_c')=type_of(date) || _c   || return(_result) ?};

PLOB_PB.cntx_psh();
PLOB_PB.index('PRAC');
PLOB_PB.prefix(_p_ref,_warstwa,_data_dp);
{? PLOB_PB.first()
|| {! |? PLOB_PB.del() !}
?};
_result:=(PLOB_PB.size()=0);
PLOB_PB.cntx_pop();
_result

:Sign Version 2.0 jowisz:1028 2019/06/07 15:59:57 e380bcbab57ddfd1eb8840cccd62c9e04102c7fb27948ff7bfda09185e35054ce69e4968302d9dcb2c84230d6f1322bb381b30f262c1ad2b3958261954bba1cd6f4dd7bfae7866ebabaac98bf1b5e115a44c97994585982a5e3aca4c7ef06376c7ae408eeb373fcee8979c9cd3ec86d21bb4fe7d4302111cce8cc829d604461e

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: kasa_pozdok.fml
:: Utworzony: 01.03.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Kasa - pozycje dokumentu
::======================================================================================================================


\dokument
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: DOKUMENT.ref
::  OLD: \dokument/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMENT.ref


\betrpd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula przed redakcja pola POZDOK.POZOPER
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1; trans_pd:=POZDOK.POZOPER;
{? -menu_txt='popraw' & DOKUMENT.OPER().CZY1_POZ='T'
|| POZDOK.cntx_psh(); POZDOK.index('DOKUMENT'); POZDOK.prefix(DOKUMENT.ref);
   _ok:=POZDOK.first();
   POZDOK.cntx_pop()
|| _ok:=1
?};
{? -menu_txt='popraw' & _ok
|| VPD.cntx_psh(); VPD.index('VPD'); VPD.prefix(POZDOK.ref());
   {? ~VPD.first() || _zwrot:=1 || _zwrot:=0  ?};
   VPD.cntx_pop()
?};
{? POZDOK.POZOPER=null || POZDOK.POZOPER:=exec('dom_typ','kasa_wspolne',OPER.ref(),1) ?};
exec('set_efld_opt','kasa_pozdok');
_zwrot


\poz_opea
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: po edycji pola POZOPER - wypełnienie odpowiednich parametrów pozycji dokumentu
::   WE:
::   WY:
::  OLD: \poz_opea/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? POZDOK.POZOPER=null || POZDOK.POZOPER:=exec('dom_typ','kasa_wspolne',OPER.ref(),1) ?};
POZDOK.POZOPER();
SLOSLU.SLSLU:=POZOPER.DOTYCZY().NAZ;
{? POZOPER.DOTYCZY & POZOPER.DOTYCZY=POZOPER.OPER().DOTYCZY &
   (POZDOK.SLO=null | POZDOK.SLO().SLU<>POZOPER.DOTYCZY)
|| POZDOK.SLO:=DOKUMENT.DLA
|? POZOPER.DOTYCZY=null | POZDOK.SLO().SLU<>POZOPER.DOTYCZY
|| POZDOK.SLO:=null
?};
POZDOK.PLUS_MIN:=POZOPER.PLUS_MIN;
{? -POZDOK.POZOPER().VAT<>'t'
|| POZDOK.KH:=POZDOK.NIP:=APARAM.OKROVAT1:='';  POZDOK.TP:=POZDOK.DATA1:=date(0,0,0);
   POZDOK.OKROVAT:=POZDOK.REJ:=POZDOK.RVAT:=POZDOK.DOK_REJ:=null
|? -POZDOK.POZOPER().VAT='t' & PAR_SKID.get(90)='T' & (trans_pd<>POZDOK.POZOPER)
|| OKRO_F.cntx_psh();
   ROK_F.cntx_psh(); ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA,4+DOKUMENT.DATA$1);
   {? ROK_F.first()
   || OKRO_F.index('ROK'); OKRO_F.prefix(ROK_F.ref);
      {? OKRO_F.first()
      || {!|? {? DOKUMENT.DATA>=OKRO_F.POCZ & DOKUMENT.DATA<=OKRO_F.KON
              || POZDOK.OKROVAT:=OKRO_F.ref; APARAM.OKROVAT1:=OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ;
                 _dalej:=0
              || _dalej:=OKRO_F.next()
              ?};
              _dalej
         !}
      ?}
   ?};
   ROK_F.cntx_pop();
   OKRO_F.cntx_pop();
   POZDOK.TP:=POZDOK.DATA1:=DOKUMENT.DATA;
   RS.index('RS'); RS.prefix(); SLO.clear(); DOKUMENT.DLA();
   {? POZDOK.POZOPER().REJDOM<>''
   || {? (_r:=exec('get_rej','rejestry',POZDOK.POZOPER().REJDOM,SSTALE.AR,POZDOK.ODD))<>null
      || POZDOK.REJ:=_r
      || _ok:=0; FUN.info('Brak rejestru księgowego %1 w aktualnym roku.'@[POZOPER.REJDOM])
      ?}
   ?};
   {? _ok
   || {? POZDOK.POZOPER().DMDOKREJ<>null
      || {? POZDOK.POZOPER().DMDOKREJ().REJ().ODD=POZDOK.ODD
         || POZDOK.DOK_REJ:=POZDOK.POZOPER().DMDOKREJ
         || FUN.info('Oddział dla rodzaju dokumentu w definicji typu transakcji niezgodny z jednostką księgową.'@);
            _ok:=0
         ?}
      ?};
      {? POZDOK.POZOPER().RVATDOM<>null
      || {? POZDOK.POZOPER().RVATDOM().REJ().ODD=POZDOK.ODD
         || POZDOK.RVAT:=POZDOK.POZOPER().RVATDOM
         || FUN.info('Oddział dla rejestru VAT w definicji typu transakcji niezgodny z jednostką księgową.'@); _ok:=0
         ?}
      ?};
      {? POZDOK.KH='' & RS.find_key(DOKUMENT.DOTYCZY().WZ) & RS.TAB='KH'
      || KH.index('KOD');KH.prefix(2);
         {? KH.find_key(SLO.KOD) || POZDOK.KH:=KH.SKR; exec('be_vat','kasa_pozdok') ?}
      ?}
   ?}
?};
{? _ok & POZDOK.POZOPER().MF_TIP='N'
|| POZDOK.LOG_OKR:=null;
   ROZNE.LOG_OKR:=''
?};
{? _ok & POZDOK.LOG_OKR=null & POZDOK.POZOPER().MF_TIP='T'
|| POZDOK.LOG_OKR:=exec('blokrlog','kasa_pozdok')
?};
{? __CHK.record(POZDOK,,'POZOPER')='' & _ok
|| exec('set_efld_opt','kasa_pozdok');
   1
?}


\be_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula przed redakcja pol VAT w tabeli POZDOK
::   WE:
::   WY:
::  OLD: \be_vat/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.POZOPER().VAT='T' & exec('get_par','#parametr',90)='T'
|| {? form(POZDOK.NIP)=''
   || {? POZDOK.KH<>''
      || {? KH.index('SKR'); KH.prefix(2,POZDOK.KH); KH.first()
         || POZDOK.NIP:={? KH.TYP='R'
                        || {? KH.NIP<>'' || KH.NIP || KH.PESEL ?}
                        || {? 1+POZDOK.RVAT().RVAT().KVAT().SYM<>'_'
                           || KH.NIP
                           || exec('jak_nue','kontrahent')
                           ?}
                        ?}
         ?}
      ?}
   ?};
   1
|| 0
?}


\blokrlog
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2008]
:: OPIS: Ustawia wartosc poczatkowa pola POZDOK.LOG_OKR i ROZNE.LOG_OKR
::  OLD: \blokrlog/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=null;
POZDOK.LOG_OKR:=null; ROZNE.LOG_OKR:='';
{? USRCONST.ROK<>null
|| ROK_F.cntx_psh();
   _rok:=#USRCONST.ROK().NAZ;
   {? _rok<>0
   || OKR.cntx_psh();
      OKR.index('OKR'); OKR.prefix(_rok);
      {? OKR.first() || _zwrot:=OKR.ref() ?};
      OKR.cntx_pop()
   ?};
   ROK_F.cntx_pop()
?};
_zwrot


\poz_mfb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przed edycją w pola faktura rozrachunek
::   WE:
::   WY:
::  OLD: \poz_mfb/ope_pola.fml
::----------------------------------------------------------------------------------------------------------------------
1


\poz_mff3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: Na klawisz f3 w polu faktura rozrachunek
::  OLD: \poz_mff3/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.POZOPER();
{? POZOPER.MF_TIP='T'
|| exec('emag','kasa_pozdok')
|? POZOPER.FIKS_TIP='T'
|| ROZRACH.TABELA:='DOK';
   exec('fiks','kasa_pozdok')
|| FUN.info('Transakcja %1 nie ma ustawionych podpowiedzi\ndla pola Faktura/rozrachunek.'@[POZOPER.KOD])
?}


\emag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Wybor dokumentu sprzedazy z Logistyka
::  OLD: \emag/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? exec('tab_magf','kasa_pozdok',1)
|| _warunek:={? POZDOK.MF_FIKS<>''
             || {? POZDOK.FAKS=null
                || TMPDPOM.blank(1); TMPDPOM.SYM:=POZDOK.MF_FIKS; TMPDPOM.find_rec()
                || TMPDPOM.index(ROZNE.INDEX2);
                   TMPDPOM.prefix(POZDOK.MF_FIKS,POZDOK.MF_FIKS,ref_name(POZDOK.FAKS),#POZDOK.FAKS);
                   _zwrot:=TMPDPOM.first(); {? ~_zwrot || POZDOK.FAKS:=null ?}; _zwrot
                ?}
             || POZDOK.FAKS:=null; 0
             ?};
   {? _warunek=0 || _warunek:=TMPDPOM.first() ?};
   {? _warunek
   || TMPDPOM.index(ROZNE.INDEX1);
      {? TMPDPOM.select(,1)
      || FAKS.cntx_psh(); FAKS.index('FAK_SYM'); FAKS.prefix(); _zwrot:='';
         {? FAKS.seek(TMPDPOM.FAKS_REF,TMPDPOM.FAKS_MAS)
         || POZDOK.MF_FIKS:=TMPDPOM.SYM;
            POZDOK.KW_PLN:=fabs(TMPDPOM.WAR);
            POZDOK.KW_WAL:=fabs(TMPDPOM.WARW);
            POZDOK.FAKS:=FAKS.ref();
            _zwrot:=TMPDPOM.SYM
         ?};
         FAKS.cntx_pop(); _zwrot
      || POZDOK.MF_FIKS
      ?}
   ?}
?}


\tab_magf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Napelnianie tabelki tymczasowej dla dokumentow sprzedazy z Emaga
::   WE: _a - 1 (na klawisz F3), 2 - po okienku
::   WY: Czy sa rekordy w tabelce tymczasowej
::       (0 jesli nie ma lub byl blad)
::  OLD: \tab_magf/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
{? POZDOK.LOG_OKR=null
|| {? _a=1 || FUN.info('Najpierw należy wskazać okres faktury.'@) ?}; _ok:=0
?};
{? _ok & POZDOK.POZOPER().ODDZ=null
|| {? _a=1
   || FUN.info('W parametrach transakcji:\n'
         '%1\nnie ma wybranego oddziału dla systemu Logistyka.'@[POZOPER.NAZWA])
   ?}; _ok:=0
?};
{? _ok
|| OKR.cntx_psh(); OKR.index('OKR'); OKR.prefix();
   TMPDPOM.erase(); POZDOK.LOG_OKR();
   FAKS.cntx_psh();
   FAKS.use('faktu'+POZDOK.POZOPER().ODDZ().KOD+(2-$OKR.ROK));
   FAKSPL.use('fakpl'+POZDOK.POZOPER().ODDZ().KOD+(2-$OKR.ROK));
   FAKS.index('FAK_KAS');
   {? POZOPER.STS<>null & POZOPER.TYPYSP=null
   || FAKS.prefix(OKR.ROK,OKR.MC,'T',POZOPER.STS)
   |? POZOPER.STS<>null & POZOPER.TYPYSP<>null
   || FAKS.prefix(OKR.ROK,OKR.MC,'T',POZOPER.STS,POZOPER.TYPYSP)
   || FAKS.prefix(OKR.ROK,OKR.MC,'T')
   ?};
   {? FAKS.first()
   || _kh:=null;
      {? DOKUMENT.DLA<>null
      || KH.cntx_psh();
         KH.index('KOD'); KH.prefix(2);
         {? KH.find_key(DOKUMENT.DLA().KOD) & KH.KOD=SLO.KOD || _kh:=KH.ref() ?};
         KH.cntx_pop()
      ?};
      {! |?
         {? (_kh=FAKS.KH | _kh=null) &
            (DOKUMENT.WALUTA=exec('wal_nar','kasa_wspolne') | DOKUMENT.WALUTA().SYM=FAKS.WAL().KOD)
         || exec('dpom','kasa_pozdok')
         ?};
         FAKS.next()
      !}
   ?};
   FAKS.cntx_pop(); OKR.cntx_pop();
   _ok:=TMPDPOM.first();
   {? ~_ok & _a=1 || FUN.info('Brak dokumentów spełniających zadane warunki.'@) ?}
?};
_ok


\dpom
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Dodanie rekordu do tabelki tymczasowej dla dokumentow sprzedazy z Logistyki
::  OLD: \dpom/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_lsp_wsz:=0;
{? KPARAM.LSP_WSZ='T'
|| _lsp_wsz:=1
?};
TMPDPOM.index(ROZNE.INDEX1);
TMPDPOM.prefix();
_gotowka:=0;
_got_pl_jest:='N';
FAKSPL.cntx_psh();
FAKSPL.index('FAKS'); FAKSPL.prefix(FAKS.ref());
{? FAKSPL.first()
|| {! |?
      {? FAKSPL.PL<>null
      || ZR_SLO.index('SLO_NR');
         ZR_SLO.prefix(FAKSPL.PL);
         {? ZR_SLO.first()
         || _got_pl:=0;
            {? ZR_SLO.WAR='T'
            || _got_pl:=1;
               _got_pl_jest:='T'
            ?};
            {? _got_pl | _lsp_wsz
            || _gotowka+=FAKSPL.WAR
            ?}
         ?}
      ?};
      FAKSPL.next()
   !}
?};
FAKSPL.cntx_pop();
_wartosc:=_gotowka-{? _gotowka<0 || -1 || 1 ?}*FAKS.PLATKAS+
          {? (-(6+menu_txt())='popraw' | -(7+menu_txt())='pozycje') &
             ROZNE.FAKS=FAKS.ref()
          || {? _gotowka<0 || -1 || 1 ?}*ROZNE.FAKSWART
          || 0
          ?};
{? {? POZDOK.PLUS_MIN='+' & FAKS.SZ='S' | POZDOK.PLUS_MIN='-' & FAKS.SZ='Z' || _wartosc$2>0
   |? POZDOK.PLUS_MIN='-' & FAKS.SZ='S' | POZDOK.PLUS_MIN='+' & FAKS.SZ='Z' || _wartosc$2<0
   ?}
|| _wal_nar:=(DOKUMENT.WALUTA=exec('wal_nar','kasa_wspolne'));
   TMPDPOM.WARW:={? ~_wal_nar || _wartosc || 0 ?};
   _jedn:=DOKUMENT.WALUTA().J;
   TMPDPOM.WAR:={? _wal_nar
                || _wartosc
                || {? _jedn<>0
                   || (_wartosc*DOKUMENT.KURS/_jedn)$2
                   || 0
                   ?}
                ?};
   TMPDPOM.STS:=FAKS.STS().KOD;
   TMPDPOM.KH:=FAKS.KH().SKR;
   TMPDPOM.KH_NAZ:=KH.NAZ;
   TMPDPOM.TYPYSP:=FAKS.T().T;
   TMPDPOM.SYM:=FAKS.SYM;
   TMPDPOM.NR:=FAKS.NR;
   TMPDPOM.DW:=FAKS.DW;
   TMPDPOM.D:=FAKS.D;
   TMPDPOM.G:=_got_pl_jest;
   TMPDPOM.FAKS_MAS:=FAKS.name();
   TMPDPOM.FAKS_REF:=#FAKS.ref();
   TMPDPOM.add()
?}


\fiks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: wybór rozrachunku z FIKSa
::   WE: [_a] - 0 - wybór (domyślnie), 1 - tylko wyszukanie wg symbolu
::   WY:
::  OLD: \fiks/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_bez_wyb:=0;
{? var_pres('_a')=type_of(0)
|| _bez_wyb:=_a
?};
exec('null_uns','kasa_pozdok');
_w:=null; zm_zakr:=0;
SLUAPPL.index('WZ');
{? SLUAPPL.find_key('F','Waluta')
|| SLO.index('SL'); SLO.prefix();
   {? SLO.find_key(SLUAPPL.SLU,DOKUMENT.WALUTA().SYM) || _w:=SLO.ref() ?}
?};
{? _w<>null
|| _cobylo:=POZDOK.MF_FIKS; _cobylou:=POZDOK.SYM_ROK;
   {!
   |?
      zm_zakr:=1;

      _jest:=0;
      OP.hdr_sel();
      SLO.cntx_psh(); SLO.prefix(); SLU.cntx_psh(); SLU.prefix();
      {? RS.find_key(DOKUMENT.DLA().SLU().WZ) & RS.TAB='KH'
      || KH.index('KOD'); KH.prefix(2);
         {? KH.find_key(SLO.KOD)
         || OP.index('KH'); OP.prefix(_w,KH.ref());
            {? OP.first()
            || {? POZDOK.MF_FIKS<>''
               || OP.blank(1); OP.WAL:=_w; OP.KH:=KH.ref(); OP.STAN:='N';
                  {? POZDOK.ODD<>null || OP.ODD:=POZDOK.ODD ?};
                  OP.SYM:=OP.SYM_ZEW:=POZDOK.MF_FIKS;
                  {? POZDOK.SYM_ROK<>'' || OP.SYM_ROK:=POZDOK.SYM_ROK ?};
                  _jest:=OP.find_rec();
                  {? ~_jest || OP.STAN:='R'; _jest:=OP.find_rec() ?}
               ?}
            ?};
            OP.hdr_sel(': '+(DOKUMENT.WALUTA().SYM)+' '+SLO.KOD+' '+SLO.TR)
         ?}
      || exec('initWARL','dok_fks_aut_dok',USRCONST.ROK,'KAS','S');
         exec('init','dok_fks_aut_dok','Informacje o błędach dekretacji raportów kasowych');
         exec('set_fld','dok_fks_aut_dok','O','S'); exec('set_fld','dok_fks_aut_dok','N','S');
         pierwszy:=1;
         _konto:=exec('konto_z_dek','dok_fks_aut_dok');
         exec('done','dok_fks_aut_dok'); VAR_DEL.delete('pierwszy');
         _znal:=0;
         {? POZDOK.MF_FIKS<>''
         || {? POZDOK.ODD=null
            || OP.index('KON'); OP.prefix(_w,_konto,POZDOK.MF_FIKS)
            || OP.index('KON_O'); OP.prefix(_w,POZDOK.ODD,_konto,POZDOK.MF_FIKS)
            ?};
            {? OP.first()
            || _znal:=1;
               {? OP.SYM=POZDOK.MF_FIKS & POZDOK.SYM_ROK<>'' || OP.find_key(POZDOK.SYM_ROK) ?}
            ?}
         || {? POZDOK.ODD=null
            || OP.index('KON'); OP.prefix(_w,_konto)
            || OP.index('KON_O'); OP.prefix(_w,POZDOK.ODD,_konto)
            ?};
            OP.first()
         ?};
         {? POZDOK.MF_FIKS<>'' & _znal
         || _jest:=1; OP.hdr_sel(' konto: '+(DOKUMENT.WALUTA().SYM)+' '+_konto)
         || OP.hdr_sel(' '+(DOKUMENT.WALUTA().SYM))
         ?}
      ?};
      SLU.cntx_pop(); SLO.cntx_pop();
      {? ~_jest || OP.first() ?};
      {? (_bez_wyb & _jest) | (~_bez_wyb & OP.select(,1))
      || {? zm_zakr<>-1
         || POZDOK.MF_FIKS:=OP.SYM; POZDOK.SYM_ROK:=OP.SYM_ROK; POZDOK.ODD:=OP.ODD;
            {? OP.WAL=KINFO.NAROD
            || {? POZDOK.KW_PLN$2=0
               || POZDOK.KW_PLN:={? POZDOK.PLUS_MIN='+'
                                 || (OP.WN-OP.MA)$2
                                 || (OP.MA-OP.WN)$2
                                 ?}
               ?}
            || {? POZDOK.KW_WAL$2=0
               || POZDOK.KW_WAL:={? POZDOK.PLUS_MIN='+'
                                 || (OP.WN-OP.MA)$2
                                 || (OP.MA-OP.WN)$2
                                 ?};
                  _jedn:=DOKUMENT.WALUTA().J;
                  POZDOK.KW_PLN:={? _jedn<>0
                                 || (POZDOK.KW_WAL*DOKUMENT.KURS/_jedn)$2
                                 || 0
                                 ?}
               ?}
            ?}
         ?}
      || zm_zakr:=0
      ?};
      zm_zakr=-1
   !};
   {? zm_zakr=0 || POZDOK.MF_FIKS:=_cobylo; POZDOK.SYM_ROK:=_cobylou ?}
?};
&zm_zakr; POZDOK.MF_FIKS


\null_uns
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009+]
:: OPIS: Sprawdzanie, czy nie wyzerowac pola POZDOK.SYM_ROK
::  OLD: \null_uns/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.MF_FIKS='' | (POZDOK.SYM_ROK<>'' & (POZDOK.SYM_ROK*POZDOK.MF_FIKS<>1))
|| POZDOK.SYM_ROK:=''
?}


\ae_mftid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Po redakcji pola POZDOK.KH i po okienku
::   WE: _a zadeklarowane - po okienku
::  OLD: \ae_mftid/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
exec('null_uns','kasa_pozdok');
{? POZOPER.MF_TIP='T' & POZDOK.MF_FIKS<>'' & exec('tab_magf','kasa_pozdok',2)
|| TMPDPOM.cntx_psh();
   TMPDPOM.index(ROZNE.INDEX2);
   {? POZDOK.FAKS<>null
   || TMPDPOM.prefix(POZDOK.MF_FIKS,POZDOK.MF_FIKS,ref_name(POZDOK.FAKS),#POZDOK.FAKS);
      {? TMPDPOM.first()
      || {? _=0 & POZDOK.KW_PLN$2=0 & POZDOK.KW_WAL$2=0
         || POZDOK.KW_PLN:=fabs(TMPDPOM.WAR);
            POZDOK.KW_WAL:=fabs(TMPDPOM.WARW)
         ?};
         FAKS.cntx_psh();
         {? FAKS.seek(TMPDPOM.FAKS_REF,TMPDPOM.FAKS_MAS) || POZDOK.FAKS:=FAKS.ref() ?};
         FAKS.cntx_pop()
      || POZDOK.FAKS:=null
      ?}
   || TMPDPOM.prefix(POZDOK.MF_FIKS,POZDOK.MF_FIKS);
      {? TMPDPOM.first()
      || FAKS.cntx_psh(); FAKS.index('FAK_SYM'); FAKS.prefix();
         {? FAKS.seek(TMPDPOM.FAKS_REF,TMPDPOM.FAKS_MAS)
         || POZDOK.MF_FIKS:=TMPDPOM.SYM;
            {? POZDOK.KW_PLN$2=0 & POZDOK.KW_WAL$2=0
            || POZDOK.KW_PLN:=fabs(TMPDPOM.WAR);
               POZDOK.KW_WAL:=fabs(TMPDPOM.WARW)
            ?};
            POZDOK.FAKS:=FAKS.ref()
         ?};
         FAKS.cntx_pop()
      ?}
   ?};
   TMPDPOM.cntx_pop()
|? POZOPER.FIKS_TIP='T' & POZDOK.MF_FIKS<>''
|| POZDOK.FAKS:=null();
   exec('fiks','kasa_pozdok',1)
|| POZDOK.FAKS:=null()
?};
1


\bv_pozkw1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przed wyswietl dla POZDOK.KW_???
::  OLD: \bv_pozkw1/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.POZOPER().ZM_OBR='N' || Color.fnd_kol('POZDOK#01#02') || '' ?}


\poz_kwwb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przed redakcja pola kwota w walucie
::   WE:
::   WY:
::  OLD: \poz_kwwb/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
DPOZ.WPR_R:=fld();
USRCONST.STANKAS();
KUSERUPR.STANKAS();
STANKAS.WALUTA<>exec('wal_nar','kasa_wspolne')


\poz_kwwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: po redakcji pola kwota w walucie                  ';
::   WE:
::   WY:
::  OLD: \poz_kwwa/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? DPOZ.WPR_R=fld() || return(1) ?};
POZDOK.KW_WAL:=POZDOK.KW_WAL$2;
{? POZDOK.KW_WAL<0
|| FUN.info('Niedozwolona kwota ujemna.'@); 0
|| {? exec('chksaldo','kasa_pozdok',POZDOK.KW_WAL)
   || exec('licz_kwpln','kasa_pozdok');
      exec('set_efld_opt','kasa_pozdok');
      1
   ?}
?}


\chksaldo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: kontrola czy nie schodzimy z saldem ponizej zera
::       ale tylko jesli pozycja jest rozchodowa i kasowa
::   WE:
::   WY: \chksaldo/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('_b')<=0 || _b:='Kwota' ?};
USRCONST.STANKAS();
KUSERUPR.STANKAS();
{? POZDOK.POZOPER().ZM_OBR='T' & POZDOK.PLUS_MIN='-'
|| _srodki:=exec('srodki','kasa_wspolne',DOKUMENT.WALUTA(); WAL.SYM);
   {? -_b<>'kwota' || _srodki+=POZDOK.KW_WAL ?};
   {? STANKAS.CZY_SMIN='N' & (_srodki-_a)<0
   || {? -_b<>'suma brutto'
      || FUN.info('Niedozwolona wartość pola >>%1<<\n'
            'Saldo kasy ujemne.\nDostępne środki: %2 %3'@[_b,WAL.SYM,form(_srodki,,2)]);
         0
      || FUN.info('Niedozwolona wartość pola >>%1<<\n'
            'Saldo kasy ujemne.\nDostępne środki: %2 %3\n'
            'Kwota przekroczenia: %4 %5.'@[_b,WAL.SYM,form(_srodki,,2),WAL.SYM,form(_a-_srodki,,2)]);
         0
      ?}
   || 1
   ?}
|| 1
?}


\spsmin
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: sprawdza czy przekroczymy stan minimalny
::   WE: _a - stan minimalny, _b - akutalne saldo PLN, _c - kwota zmniejszenia salda
::  OLD: \spsmin/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a>0 & _b-_c<_a
|| {? KPARAM.CZY_B='T'
   || FUN.info('Rozchód na kwotę: \n%1PLN \nspowoduje przekroczenie pogotowia kasowego.'@[form(_c,,2)])
   || FUN.info('Rozchód na kwotę: \n%1PLN \nspowoduje przekroczenie stanu minimalnego.'@[form(_c,,2)])
   ?}
?}


\bv_pozkw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2006]
:: OPIS: Przed wyswietl dla POZDOK.KW_???
::  OLD: \bv_pozkw/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.POZOPER().ZM_OBR='N' || Color.fnd_kol('POZDOK#01#02') || '' ?}


\poz_kwpb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przed redakcja pola kwota w PLN
::   WE:
::   WY: \poz_kwpb/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
USRCONST.STANKAS();
KUSERUPR.STANKAS();
STANKAS.WALUTA=exec('wal_nar','kasa_wspolne') | KPARAM.CZY_ZMKU='T' & POZDOK.KW_WAL=0


\poz_kwpa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: po redakcji pola kwota w PLN
::   WE:
::   WY:
::  OLD: \poz_kwpa/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.KW_PLN:=POZDOK.KW_PLN$2;
{? POZDOK.KW_PLN<0
|| FUN.info('Niedozwolona kwota ujemna.'@); 0
|| {? SLOSLU.WAL_OP<>SLOSLU.WAL_N
   || exec('set_efld_opt','kasa_pozdok');
      1
   ?};
   {? exec('chksaldo','kasa_pozdok',POZDOK.KW_PLN)
   || {? DOKUMENT.KURS
      ||
         {? DOKUMENT.WALUTA
         || POZDOK.KW_WAL:=(POZDOK.KW_PLN*DOKUMENT.WALUTA().J/DOKUMENT.KURS)$2
         || POZDOK.KW_WAL:=(POZDOK.KW_PLN/DOKUMENT.KURS)$2
         ?}
      ||
         POZDOK.KW_WAL:=POZDOK.KW_PLN
      ?};
:: jesli jest okreslony stan minimalny to sprawdza czy wpisana kwota spowoduje przekroczenie limitu
      {? MLKAS.MIN>0 & MLKAS.MIN<=SALDO.PLN
      || exec('spsmin','kasa_pozdok', MLKAS.MIN, SALDO.PLN, POZDOK.KW_PLN)
      ?};
      1
   ?}
?}


\ae_pozkh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Po redakcja pola POZDOK.KH
::  OLD: \ae_pozkh/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.POZOPER().DOTYCZY & POZDOK.KH<>'' & POZDOK.SLO=null ||
   RS.index('RS'); RS.prefix();
   {? RS.find_key(POZOPER.DOTYCZY().WZ) & RS.WZ=SLU.WZ & RS.TAB='KH' ||
      KH.index('SKR'); KH.prefix(2,POZDOK.KH,POZDOK.KH);
      {? KH.first() ||
         SLO.index('SL'); SLO.prefix(POZOPER.DOTYCZY,KH.KOD);
         {? SLO.first() & SLO.KOD=KH.KOD ||
            POZDOK.SLO:=SLO.ref()
         ?}
      ?}
   ?}
?};
POZDOK.NIP:=KH.NIP;
1


\ae_nip
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: Po redagowaniu POZDOK.NIP
::  OLD: \ae_nip/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
{? 1+POZDOK.RVAT().RVAT().KVAT().SYM<>'_'
|| exec('nip_ok','#id',fld)
|| 1
?}


\aedatpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula po redakcji pola POZDOK.DATA1                        ";
::   WE:
::   WY:
::  OLD: \aedatpoz/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.DATA1=date(0,0,0)
|| FUN.info('Nie podano daty wystawienia.'@); 0
|| {? POZDOK.DATA1>DOKUMENT.DATA
   || FUN.info('Data wystawienia nie może być większa'
         '\nod daty dokumentu kasowego (%1).'@[DOKUMENT.DATA$1]); 0
   || 1
   ?}
?}


\bervatpd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula przed redakcja pola POZDOK.RVAT
::   WE:
::   WY:
::  OLD: \bervatpd/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? -POZDOK.POZOPER().ZMDOMRVT='t' & POZDOK.REJ<>null
|| POZDOK.REJ(); 1
|| 0
?}


\berejpd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula przed redakcja pola POZDOK.REJ
::   WE:
::   WY:
::  OLD: \berejpd/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=0;
{? exec('be_vat','kasa_pozdok') & -POZDOK.POZOPER().ZMDOMREJ='t'
|| {? -menu_txt<>'dołącz'
   || VPD.cntx_psh();
      VPD.index('VPD'); VPD.prefix(POZDOK.ref());
      {? ~VPD.first() || _zwrot:=1 ?};
      VPD.cntx_pop()
   || _zwrot:=1
   ?}
?};
_zwrot


\aerejpd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula po redakcji pola POZDOK.REJ
::   WE:
::   WY:
::  OLD: \aerejpd/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
DOK_REJ.cntx_psh(); _a:=POZDOK.DOK_REJ().NAZ;
DOK_REJ.index('NAZ'); DOK_REJ.prefix(POZDOK.REJ,_a);
{? DOK_REJ.first() & DOK_REJ.NAZ=POZDOK.DOK_REJ().NAZ
|| POZDOK.DOK_REJ:=DOK_REJ.ref()
|| POZDOK.DOK_REJ:=null
?};
DOK_REJ.cntx_pop();
VAT_REJ.cntx_psh(); _a:=POZDOK.RVAT().RVAT; _b:=POZDOK.REJ().KOD;
VAT_REJ.index('VAT_REJ'); VAT_REJ.prefix(_a,_b);
{? ~VAT_REJ.first() | POZDOK.REJ=null || POZDOK.RVAT:=null ?};
VAT_REJ.cntx_pop();
1


\be_rd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula przed redakcja pola POZDOK.DOK_REJ
::   WE:
::   WY:
::  OLD: \be_rd/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.POZOPER().VAT='N'
|| 0
|? POZDOK.POZOPER().ZMDDKREJ='T' & POZDOK.REJ<>null
|| POZDOK.REJ(); 1
|| 1
?}


\aetppoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [8.50]
:: OPIS: Formula po redakcji pola POZDOK.TP
::   WE:
::   WY:
::  OLD: \aetppoz/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? PAR_SKID.get(38)='N'
|| {? POZDOK.DATA1>POZDOK.TP
   || FUN.info('Termin płatności nie może być wcześniejszy \nod daty wystawienia.'@); 0
   || 1
   ?}
|| {? POZDOK.TP=date(0,0,0)
   || FUN.info('Nie podano terminu płatności.'@); 0
   || 1
   ?}
?}


\bepozslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Przed redakcja pola POZDOK.SLO
::  OLD: \bepozslo/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.POZOPER().DOTYCZY<>null


\aepozslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [2006]
:: OPIS: Po redakcja pola POZDOK.SLO
::  OLD: \aepozslo/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.POZOPER().VAT='T' & PAR_SKID.get(90)='T' & POZDOK.KH='' & POZDOK.SLO ||
   RS.index('RS'); RS.prefix();
   {? RS.find_key(POZDOK.SLO().SLU().WZ) & RS.WZ=SLU.WZ & RS.TAB='KH' ||
      KH.index('KOD'); KH.prefix(2,SLO.KOD);
      {? KH.first() & KH.KOD=SLO.KOD ||
         POZDOK.KH:=KH.SKR
      ?}
   ?}
?}; 1


\odd_pozd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS:
::  OLD: \odd_pozd/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
USRCONST.STANKAS().STANKAS().ODD


\beodpozd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: Przed redagowaniem POZDOK.ODD
::  OLD: \beodpozd/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.POZOPER<>null & POZDOK.POZOPER().ZMDOMODD='T'


\bud_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: EP [2011]
:: OPIS: Formula przed redakcja pola POZDOK.BUD
::   WY: 1 - wybrana operacja zwiazana z budzetem; 0 - pole nieredagowalne
::  OLD: \bud_be/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMENT.cntx_psh;
ML_KSBUD.index('STKAS');
ML_KSBUD.prefix(KUSERUPR.STANKAS().KOD);
DOKUMENT.cntx_pop;
{? POZDOK.POZOPER().ZW_BUD='T' & STANKAS.PGPP<>'N'
|| 1
|| fld('');0
?}


\bud_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: EP [2011]
:: OPIS: Formula na 'f3' podczas redakcji pola paragrafu lub pozycji w oknie redakcji transakcji
::       tabela POZDOK, pole BUD, klawisz F3
::  OLD: \bud_f3/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------

{? USRCONST.STANKAS().STANKAS().PGPP='P'
|| ML_KSBUD.hdr_sel('Wybierz paragraf'@)
|| ML_KSBUD.hdr_sel('Wybierz pozycję'@)
?};
{? ML_KSBUD.select
|| POZDOK.RZ:=ML_KSBUD.RZ;
   POZDOK.BUD:=ML_KSBUD.BUD
?}


\bud_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: EP [2011]
:: OPIS: Formula po redakcji pola POZDOK.BUD
::  OLD: \bud_ae/ml_kasa.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=0;
{? POZDOK.BUD<>''
|| ML_KSBUD.cntx_psh();
   _stk:=ML_KSBUD.STANKAS;
   ML_KSBUD.clear;
   ML_KSBUD.blank;
   ML_KSBUD.STANKAS:=_stk;
   ML_KSBUD.BUD:=POZDOK.BUD;
   {? POZDOK.RZ<>null
   || ML_KSBUD.RZ:=POZDOK.RZ;
      _wyn:=ML_KSBUD.find_rec;
      {? ~_wyn
      || ML_KSBUD.blank;
         ML_KSBUD.STANKAS:=_stk;
         ML_KSBUD.BUD:=POZDOK.BUD;
         _wyn:=ML_KSBUD.find_rec
      ?}
   || _wyn:=ML_KSBUD.find_rec
   ?};
   {? _wyn
   || POZDOK.RZ:=ML_KSBUD.RZ
   ?};
   ML_KSBUD.cntx_pop()
|| FUN.emsg({? USRCONST.STANKAS().STANKAS().PGPP='P'
            || 'Podaj paragraf'
            || 'Podaj pozycję'
            ?})
?};
_wyn


\be_pozdn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Przed redakcja znakow na pozycji dokumentu - ustawia dostepne znaki
::  OLD: \be_pozdn/ml_znaki.fml
::----------------------------------------------------------------------------------------------------------------------
ML_ZN.index('ZN');
ML_ZN.prefix(STANKAS.ref)


\pozdokkw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BogNal [2011]
:: OPIS: Wyliczenie kwoty na pozycji dok. kasowego
::  OLD: \pozdokkw/ml_znaki.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.KW_PLN:=POZDOK.IL*POZDOK.N().NO;
1


\o_dokpoz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: wywoływana na akcję pozycje w okienku dokumentów
::   WE: _a - czy poprawic dokument jednopozycyjny
::   WY:
::  OLD: \o_dokpoz/okna.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0 || _a:=0 ?};
exec('dost_poz','kasa_pozdok');
OP.win_sel('KASA');
OP.win_dict('KASA');
DOKUMENT.OPER();
POZDOK.index('DOKUMENT'); POZDOK.prefix(DOKUMENT.ref()); _first:=POZDOK.first();

exec('pozdok_win_edit_set','kasa_pozdok');

_rokuser:=USRCONST.ROK;
trans_pd:=null;
_koniec:=0;
{? OPER.CZY1_POZ='N'
|| POZDOK.select(,,,{? PAR_SKID.get(80)='N' || 'Z' || '' ?})
|| exec('tmpdpom','kasa_wspolne',2);
   _is_1:=POZDOK.first(); Popraw:=1;
   {? ~_is_1 || POZDOK.blank(); POZDOK.DOKUMENT:=DOKUMENT.ref() ?};
   {? POZDOK.POZOPER().VAT='T' &
      (_choice:=FUN.choice('Proszę wybrać wariant:'@,1,'Poz&ycja dokumentu'@,'Pozycje &vat'@);
      {? _choice=0 || return() ?};
      _choice=2)
   || exec('poz_vat','kasa_pozdok')
   || exec('pozdok_win_edit_set','kasa_pozdok');
      {? OPER.CZY_ZAL='T' || ROZNE.SYM_ZAL:='' ?};
      _upr:=USRCONST.STANKAS().UPR;
      KUSERUPR.cntx_psh;
      KUSERUPR.index('KU_ST'); KUSERUPR.prefix(OPERATOR.USER,KUSERUPR.STANKAS);
      {? KUSERUPR.first || _upr:=KUSERUPR.UPR ?};
      KUSERUPR.cntx_pop;
      {? (DOKUMENT.DOK_NUM<>0 & OPER.CZY_BLOK='T')
         | (DOKUMENT.RAPORT().STATUS<>exec('rap_stat','kasa_raport',0)
            & DOKUMENT.RAPORT().STATUS<>exec('rap_stat','kasa_raport',1))
         | _upr='N'
         | exec('chk_role','#b__box',OPERATOR.USER,'KAS_DOK_DDOK')=0
      || POZDOK.display()
      || _koniec:=0;
         {? ~DOKUMENT.r_lock(1,1,1)
         || FUN.info('Pozycja obsługiwana przez innego operatora.'@)
         || {? ~_a || dok1poz:=1 || {? KPARAM.CZY_B='T'|| dok1poz:=0 ?} ?};
            BO.PR_WAL:={? OPER.PLUS_MIN='+' || -POZDOK.KW_WAL || POZDOK.KW_WAL ?};
            {? {? _a
               || POZDOK.edit("_w:=exec('dok_chk','kasa_dokument'); {? _w='' || _w:=exec('poz_wal','kasa_pozdok') ?}; _w")
               || POZDOK.edit("exec('poz_wal','kasa_pozdok')")
               ?}
            || {? -menu_txt='popraw' || do() ?};
               {? _a || DOKUMENT.put() ?};
               {? _is_1 || POZDOK.put() || POZDOK.add() ?};
               exec('poz_calc','kasa_pozdok');
               {? -menu_txt='popraw' || end() ?}
            || _koniec:=1
            ?};
            {? ~_a || &dok1poz ?};
            DOKUMENT.r_unlock()
         ?}
      ?}
   ?}
?};
VAR_DEL.delete('trans_pd','__BE_SYM_ZAL');
USRCONST.ROK:=_rokuser;
{? _koniec | RAPORT.STATUS=exec('rap_stat','kasa_raport',2) || return(0) ?};
{? DOKUMENT.DOK_NUM=0 & OPER.CZY_DOK='T'
|| exec('kp_kw','kasa_dokument')
?};
DOKUMENT.cntx_psh();
exec('dok_suma','kasa_wspolne');
OBROTY.index('RAPORTR'); OBROTY.prefix();
{? SUMA.WAL_PLUS$2<>0 |SUMA.WAL_MIN$2<>0
|| {?~OBROTY.find_key(RAPORT.ref(),DOKUMENT.WALUTA().SYM,'W')
   || OBROTY.blank();
      OBROTY.RAPORT:=RAPORT.ref();
      OBROTY.RODZAJ:='W';
      OBROTY.POZOPER:=POZOPER.ref();
      OBROTY.WALUTA:=WAL.ref();
      OBROTY.add()
   ?};
   OBROTY.PLUS_WAL:=SUMA.WAL_PLUS;
   OBROTY.PLUS_PLN:=SUMA.PLN_PLUS;
   OBROTY.MIN_PLN:=SUMA.PLN_MIN;
   OBROTY.MIN_WAL:=SUMA.WAL_MIN;
   OBROTY.put()
|| {? OBROTY.find_key(RAPORT.ref(),DOKUMENT.WALUTA().SYM,'W')
   || OBROTY.PLUS_WAL:=SUMA.WAL_PLUS;
      OBROTY.PLUS_PLN:=SUMA.PLN_PLUS;
      OBROTY.MIN_PLN:=SUMA.PLN_MIN;
      OBROTY.MIN_WAL:=SUMA.WAL_MIN;
      OBROTY.put()
   ?}
?};
DOKUMENT.cntx_pop();

0


\dost_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: Wybor odpowiedniego okienka dla pozycji dokumenty - w zaleznosci
::       od uprawnien, statusu raportu, statusu dokumentu
::  OLD: \dost_poz/okna.fml
::----------------------------------------------------------------------------------------------------------------------
USRCONST.STANKAS().STANKAS().WALUTA();
WAL.cntx_psh();
_wal:=STANKAS.WALUTA=null | STANKAS.WALUTA().SYM<>KINFO.NAROD().KOD;
WAL.cntx_pop();
exec('set_poz','kasa_pozdok',_wal,
   USRCONST.STANKAS().UPR='T' & ROZNE.BLOK_RAP &
   ((DOKUMENT.DOK_NUM = 0 & DOKUMENT.OPER().CZY_BLOK='T') | DOKUMENT.OPER().CZY_BLOK='N') &
   (exec('rap_code','kasa_raport',RAPORT.STATUS)=0 | exec('rap_code','kasa_raport',RAPORT.STATUS)=1)
)


\set_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Ustawia okna i akcje tabeli POZDOK
::   WE: _a - wielowalutowe? 1-tak, 0-nie
::       _b - modyfikowalne? 1-tak, 0-nie
::  OLD: \set_poz/okna.fml
::----------------------------------------------------------------------------------------------------------------------
_okno:='WER';
POZDOK.win_sel(_okno);
{? _b || POZDOK.actions(_okno,'','D:D') || POZDOK.actions(_okno,'DPU:D','V') ?}


\poz_wal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: Walidacja pol w tabeli POZDOK
::  OLD: \poz_wal/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:='';
USRCONST.STANKAS();
KUSERUPR.STANKAS();
_sym_zal:=DOKUMENT.OPER().CZY_ZAL='T';
_be_vat:=exec('be_vat','kasa_pozdok');
{? _zwrot=''
||
   _war1:=POZDOK.POZOPER().VAT='T';
   _war2:=PAR_SKID.get(90)='T';
   {? _war1<>0 & _war2=0
   ||
      FUN.info('Dokumenty VAT można wprowadzać po ustawieniu parametru 90 na T.'@);
      _zwrot:='POZOPER'
   ?}
?};
{? _zwrot=''
||
   {? STANKAS.WALUTA=exec('wal_nar','kasa_wspolne')
   ||
      _zwrot:=
         {? -POZDOK.POZOPER().MF_TIP='t' | -POZDOK.POZOPER().FIKS_TIP='t' | _be_vat
         ||
            {? _sym_zal
            || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','MF_FIKS','SYM_ZAL','KW_PLN')
            || {? -POZDOK.POZOPER().MF_TIP='t'
               || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','LOG_OKR','MF_FIKS','KW_PLN')
               || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','MF_FIKS','KW_PLN')
               ?}
            ?}
         ||
            {? _sym_zal
            || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','SYM_ZAL','KW_PLN')
            || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','KW_PLN')
            ?}
         ?};
      {? _zwrot='' & exec('poz_kwpa','kasa_pozdok')=0 || _zwrot:='KW_PLN' ?};
      {? _zwrot='' & exec('spr_zal','kasa_pozdok',1)=0 || _zwrot:='KW_PLN' ?}
   ||
      _zwrot:=
         {? -POZDOK.POZOPER().MF_TIP='t' | -POZDOK.POZOPER().FIKS_TIP='t' | _be_vat
         ||
            {? _sym_zal
            || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','MF_FIKS','SYM_ZAL','KW_WAL')
            || {? -POZDOK.POZOPER().MF_TIP='t'
               || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','LOG_OKR','MF_FIKS','KW_WAL')
               || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','MF_FIKS','KW_WAL')
               ?}
            ?}
         ||
            {? POZDOK.KW_PLN
            ||
               {? _sym_zal
               || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','SYM_ZAL')
               || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD')
               ?}
            ||
               {? _sym_zal
               || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','SYM_ZAL','KW_WAL')
               || __CHK.record(POZDOK,,'POZOPER','PLUS_MIN','ODD','KW_WAL')
               ?}
            ?}
         ?};
      {? _zwrot='' & exec('spr_zal','kasa_pozdok',2)=0 || _zwrot:='KW_WAL' ?}
   ?}
?};
{? _zwrot='' & USRCONST.STANKAS().STANKAS().CZY_SMIN='N'
||
   _srodki:=exec('srodki','kasa_wspolne',DOKUMENT.WALUTA().SYM);
   _razem:=
      {? POZDOK.PLUS_MIN='+'
      || (_srodki+POZDOK.KW_WAL)$2
      || (_srodki-POZDOK.KW_WAL)$2
      ?};
   {? _razem<0
   ||
      FUN.info('Wprowadzenie wartości spowodowałoby powstanie niedozwolonego\n'
          'na danym stanowisku kasowym ujemnego salda dla waluty '@+
          DOKUMENT.WALUTA().SYM+'.');
      _zwrot:={? STANKAS.WALUTA=exec('wal_nar','kasa_wspolne') || 'KW_PLN' || 'KW_WAL' ?}
   ?}
?};
{? _zwrot='' & -POZDOK.POZOPER().MF_TIP='t'
||
   _zwrot:=exec('pozdok_d','kasa_pozdok')
?};
{? _zwrot='' & exec('be_vat','kasa_pozdok')
||
   _zwrot:=__CHK.record(POZDOK,,'KH','NIP','REJ','DOK_REJ','RVAT')
?};
_zwrot


\spr_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Sprawdzenie, czy wprowadzona kwota nie przekracza kwoty zaliczki
::       pozostalej do wyplaty
::   WE: _a - 1 - waluta narodowa
::            2 - waluta obca
::  OLD: \spr_zal/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? DOKUMENT.OPER().CZY_ZAL='T' & DOKUMENT.DOTYCZY<>null & DOKUMENT.DLA<>null &
   ROZNE.SYM_ZAL<>'' & POZDOK.PLUS_MIN='-' & DOKUMENT.WALUTA<>null & KINFO.SLWAL<>null
|| SKID_WAL_DOK:=null;
   SLO.cntx_psh();
   SLO.index('SL'); SLO.prefix(KINFO.SLWAL().SLU);
   {? SLO.first() & SLO.find_key(DOKUMENT.WALUTA().SYM) & WAL.SYM=SLO.KOD || SKID.WAL_DOK:=SLO.ref() ?};
   SLO.cntx_pop();
   {? SKID.WAL_DOK<>null
   || EZAL.cntx_psh();
      EZAL.index('INDEX4OZ'); EZAL.prefix(REF.FIRMA,SKID.OSOBA,SKID.WAL_DOK,ROZNE.SYM_ZAL);
      {? EZAL.first()
      || _kw:=EZAL.KW_PROP$2-EZAL.ROZCHOD;
         {? (-menu_txt()='pozycje' & -(menu_pth()+1)<>'z') | -menu_txt()='popraw'
         || EZALPOZ.cntx_psh();
            EZALPOZ.index('ZRODLO'); EZALPOZ.prefix(REF.FIRMA,$POZDOK.ref(),$POZDOK.ref());
            {? EZALPOZ.first() & EZALPOZ.EZAL().SYM=ROZNE.SYM_ZAL || _kw+=EZALPOZ.ROZCHOD ?};
            EZALPOZ.cntx_pop()
         ?};
         {? (_a=1 & POZDOK.KW_PLN$2>_kw$2) | (_a=2 & POZDOK.KW_WAL$2>_kw$2)
         || _dalej:=FUN.choice('Wprowadzona w pozycji dokumentu kasowego kwota jest większa od\n'
               'pozostałej do wypłaty kwoty zaliczki\n(%1 %2)'@[form(_kw,,2,' ,'),EZAL.WAL().KOD]
               ,,'&Popraw kwotę'@,'&Kontynuuj'@)
         ?}
      ?};
      EZAL.cntx_pop()
   ?}
?};
_dalej

\not_close
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Czy zaliczka nie jest zamknieta - mozna modyfikowac
::  OLD: \not_close/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
EZAL.ZAM='N' | exec('kwota','kasa_pozdok')=EZAL.PRZYCHOD & EZAL.PRZYCHOD=EZAL.ROZCHOD


\kwota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [2009]
:: OPIS: Kwota do wyplaty zaliczki
::  OLD: \kwota/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
EZAL.KW_PROP


\poz_calc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: przeliczenie sumy dla pozycji dokumentu - wolane z okna pozycji dok. po
::       dolacz, popraw, usun
::   WE: _a - jesli jest - nie sa wyswietlane cechy dodatkowe pozycji
::       _b - jesli jest - zapis w bazie informacji o platnosciach
::   WY:
::  OLD: \poz_calc/okna.fml
::----------------------------------------------------------------------------------------------------------------------
STANKAS.cntx_psh();
{? -menu_txt<>'usuń' & DOKUMENT.OPER().CZY_ZAL='T' || exec('pop_zalk','kasa_pozdok') ?};
:: wyliczenie sumy z pozycji dokumentu
exec('poz_suma','kasa_pozdok');
DOKUMENT.KW_WAL:=SUMA.WAL_PLUS;
DOKUMENT.KW_PLN:=SUMA.PLN_PLUS;
DOKUMENT.put();
STANKAS.cntx_pop();
1


\pop_zalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Poprawianie zaliczek - kasa
::  OLD: \pop_zalk/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? -menu_txt='pozycje vat'
|| EZALPOZ.index('ZRODLO'); EZALPOZ.prefix(REF.FIRMA,$POZDOK.ref(),$POZDOK.ref());
   ROZNE.SYM_ZAL:={? EZALPOZ.first() || EZALPOZ.EZAL().SYM || '' ?}
|| DOKUMENT.get()
?};
EZAL.cntx_psh(); EZAL.index('UNIK'); EZAL.prefix(REF.FIRMA);
EZALPOZ.cntx_psh();
EZALPOZ.index('ZRODLO'); EZALPOZ.prefix(REF.FIRMA,$POZDOK.ref(),$POZDOK.ref());
{? EZALPOZ.first()
|| EZALPOZ.EZAL();
   _dalej:=exec('upd_ezal','kasa_pozdok',1)
?};
{? ROZNE.SYM_ZAL<>'' & exec('szuk_oso','kasa_pozdok',DOKUMENT.DLA)
|| EZAL.index('UNIK'); EZAL.prefix(REF.FIRMA,SKID.OSOBA,ROZNE.SYM_ZAL,ROZNE.SYM_ZAL);
   {? EZAL.first()
   || {? POZDOK.PLUS_MIN='+'
      || _przych:=POZDOK.KW_WAL; _rozch:=0
      || _przych:=0; _rozch:=POZDOK.KW_WAL
      ?};
      exec('upd_ezal','kasa_pozdok',2,$POZDOK.ref(),_przych,_rozch)
   ?}
?};
EZALPOZ.cntx_pop(); EZAL.cntx_pop()


\upd_ezal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Poprawianie naglowka zaliczki
::   WE: _a - 1 - usuwanie pozycji zaliczki
::            2 - dodawanie pozycji zaliczki
::            3 - poprawianie sumy na pozycji zaliczki
::       _b - wskazanie na zrodlo pochodzenia
::       _c - przychod
::       _d - rozchod
::   WY: 1/0 - udala/nie udala sie aktualizacja zaliczki
::  OLD: \upd_ezal/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:=0;
{? var_pres('refpoz_z')>0 | EZAL.r_lock(1,1,1)
|| {? _a=1
   || _dalej:=EZALPOZ.del(,1)
   |? _a=2
   || EZALPOZ.EZAL:=EZAL.ref();
      EZALPOZ.POCH:=_b;
      EZALPOZ.PRZYCHOD:=_c;
      EZALPOZ.ROZCHOD:=_d;
      EZALPOZ.DATE_ZAP:=date();
      EZALPOZ.TIME_ZAP:=time();
      EZALPOZ.cntx_psh(); EZALPOZ.prefix();
      _dalej:=EZALPOZ.add();
      {? var_pres('refpoz_z')>0 || refpoz_z:=$EZALPOZ.ref() ?};
      EZALPOZ.cntx_pop()
   |? _a=3
   || EZALPOZ.PRZYCHOD:=_c;
      EZALPOZ.ROZCHOD:=_d;
      EZALPOZ.DATE_ZAP:=date();
      EZALPOZ.TIME_ZAP:=time();
      EZALPOZ.cntx_psh(); EZALPOZ.prefix();
      _dalej:=EZALPOZ.put();
      {? var_pres('refpoz_z')>0 || refpoz_z:=$EZALPOZ.ref() ?};
      EZALPOZ.cntx_pop()
   ?};
   {? _dalej
   || EZALPOZ.cntx_psh();
      EZALPOZ.index('ZALICZKA'); EZALPOZ.prefix(EZAL.ref());
      _przych:=_rozch:=0;
      {? EZALPOZ.first()
      || {! |?
            _przych+=EZALPOZ.PRZYCHOD;
            _rozch+=EZALPOZ.ROZCHOD;
            EZALPOZ.next()
         !}
      ?};
      EZAL.PRZYCHOD:=_przych; EZAL.ROZCHOD:=_rozch;
      _dalej:=EZAL.put();
      EZALPOZ.cntx_pop()
   ?};
   {? var_pres('refpoz_z')<=0 || EZAL.r_unlock() ?}
?};
_dalej


\szuk_oso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Szukanie osoby dla rekordu ze SLO
::   WE: _a - ref rekordu w tabeli SLO
::   WY: 1/0 - znaleziono/nie znaleziono osoby
::  OLD: \szuk_oso/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
_edit:=0;
RS.cntx_psh(); RS.index('RS'); RS.prefix();
SLO.cntx_psh(); SLO.prefix();
{? _a<>null & SLO.seek(_a) & RS.find_key(SLO.SLU().WZ) &
   (RS.TAB='SLO_OSOB' | RS.TAB='OS' | RS.TAB='OSOBA')
|| _pre:=REF.WFIRM & exec('czy_tab_glob','#table',RS.TAB)=0;
   OSOBA.cntx_psh();
   _kod:={? type_of(($(RS.TAB+'.'+RS.KOD))())=1
         || {? ~_pre || #SLO.KOD || #(3-SLO.KOD) ?}
         || {? ~_pre || '\''+SLO.KOD+'\'' || '\''+(3-SLO.KOD)+'\'' ?}
         ?};
   {? RS.TAB='SLO_OSOB'
   || _find:=($(RS.TAB+'.find_key(RS.F_ZATR,'+$_kod+')'))
   || _find:=($(RS.TAB+'.find_key('+$_kod+')'))
   ?};
   ($(RS.TAB+'.cntx_psh()'))();
   ($(RS.TAB+".index('"+RS.KOD+"')"))();
   ($(RS.TAB+'.prefix()'))();
   {? _find() & (RS.TAB='OSOBA' | OSOBA.seek(($(RS.TAB+'.OSOBA'))()))
   || _edit:=1;
      SKID.OSOBA:=OSOBA.ref()
   ?};
   ($(RS.TAB+'.cntx_pop()'))();
   OSOBA.cntx_pop()
?};
SLO.cntx_pop(); RS.cntx_pop();
_edit


\poz_suma
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: Wypełnia pola zmiennej SUMA
::   WE:
::   WY: \poz_suma/sumy.fml
::----------------------------------------------------------------------------------------------------------------------
SUMA.WAL_PLUS:=SUMA.PLN_PLUS:=0;
POZDOK.cntx_psh;
{? POZDOK.first()
|| {! |?
::Poczatek modyfikacji dla Maclex 1.12.2008 EP[1040]
      {? POZDOK.POZOPER().ZM_OBR='T' & POZDOK.ZAKS<>'A'
::Koniec modyfikacji dla Maclex
      || {? (DOKUMENT.PLUS_MIN='+' & POZDOK.PLUS_MIN='-') |
            (DOKUMENT.PLUS_MIN='-' & POZDOK.PLUS_MIN='+')
         || SUMA.WAL_PLUS-=POZDOK.KW_WAL$2;
            SUMA.PLN_PLUS-=POZDOK.KW_PLN$2
         || SUMA.WAL_PLUS+=POZDOK.KW_WAL$2;
            SUMA.PLN_PLUS+=POZDOK.KW_PLN$2
         ?}
      ?};
      POZDOK.next()
   !}
?};
POZDOK.cntx_pop


\dspokrl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Na wyswietl dla pola ROZNE.LOG_OKR
::  OLD: \dspokrl/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.LOG_OKR:=POZDOK.LOG_OKR().NAZ;
1


\poz_okrl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Przed redakcja pola ROZNE.OKR_LOG
::  OLD: \poz_okrl/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.POZOPER().MF_TIP='T'


\aeokrl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Po redakcji pola ROZNE.LOG_OKR
::  OLD: \aeokrl/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? ROZNE.LOG_OKR<>''
|| OKR.cntx_psh();
   OKR.index('NAZ'); OKR.prefix();
   {? OKR.find_key(REF.FIRMA,ROZNE.LOG_OKR)
   || POZDOK.LOG_OKR:=OKR.ref()
   || FUN.info('Proszę wprowadzić poprawnie nazwę okresu\n'
         'z systemu Logistyka.'@);
      POZDOK.LOG_OKR:=null; _zwrot:=0
   ?};
   OKR.cntx_pop()
|| POZDOK.LOG_OKR:=null
?};
_zwrot


\set_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zaznacza wymagalne pola w pozycji dokumentu kasowego
::   WE: _a - akronim okna redakcji
::       _b - tabela
::----------------------------------------------------------------------------------------------------------------------
_win_red:={? var_pres('_a')=type_of('') || _a || POZDOK.win_edit('?') ?};
_Tab:={? var_pres('_b')=type_of(POZDOK) || _b || POZDOK ?};

_enable0:='enable=0';
_enable1:='enable=1';

_mark0:='mark=0';
_mark1:='mark=1';

:: dotyczy
_enable:={? POZDOK.POZOPER().DOTYCZY<>0 || _enable1 || _enable0 ?};
_Tab.efld_opt(_win_red,_enable,SLOSLU,'SLSLU');
_Tab.efld_opt(_win_red,_enable,POZDOK,'SLO','KOD');
_Tab.efld_opt(_win_red,_enable,POZDOK,'SLO','TR');
:: kwota, waluta
_enable:={? STANKAS.WALUTA=exec('wal_nar','kasa_wspolne') || _enable0 || _enable1 ?};
_Tab.efld_opt(_win_red,_enable,POZDOK,'KW_WAL');
_Tab.efld_opt(_win_red,_enable,SLOSLU,'WAL_OP');
_Tab.efld_opt(_win_red,_enable,DOKUMENT,'KURS');
{? _enable=_enable1 & (POZDOK.KW_WAL | POZDOK.KW_PLN=0)
|| _Tab.efld_opt(_win_red,_mark1,POZDOK,'KW_WAL')
|| _Tab.efld_opt(_win_red,_mark0,POZDOK,'KW_WAL')
?};
{? KPARAM.CZY_ZMKU='T' & POZDOK.KW_WAL=0
   | KPARAM.CZY_ZMKU='N' & _enable<>_enable1
|| _Tab.efld_opt(_win_red,_mark1,POZDOK,'KW_PLN')
|| _Tab.efld_opt(_win_red,_mark0,POZDOK,'KW_PLN')
?};
:: okres logistyka
_enable:={? -POZDOK.POZOPER().MF_TIP='t' || _enable1 || _enable0 ?};
_Tab.efld_opt(_win_red,_enable,ROZNE,'LOG_OKR');
_mark:={? -POZDOK.POZOPER().MF_TIP='t' || _mark1 || _mark0 ?};
_Tab.efld_opt(_win_red,_mark,ROZNE,'LOG_OKR');
:: faktura/rozrachunek
_vat:=exec('be_vat','kasa_pozdok');
_mark:={? -POZDOK.POZOPER().MF_TIP='t' | -POZDOK.POZOPER().FIKS_TIP='t' | _vat || _mark1 || _mark0 ?};
_Tab.efld_opt(_win_red,_mark,POZDOK,'MF_FIKS');
:: zaliczka
_enable:={? DOKUMENT.OPER().CZY_ZAL='T' || 'enable=1,mark=1' || 'enable=0,mark=0' ?};
_Tab.efld_opt(_win_red,_enable,ROZNE,'SYM_ZAL');
:: dane vat
_enable:={? _vat || 'enable=1' || 'enable=0' ?};
_Tab.efld_opt(_win_red,_enable,POZDOK,'KH');
_Tab.efld_opt(_win_red,_enable,POZDOK,'NIP');
_Tab.efld_opt(_win_red,_enable,POZDOK,'DATA1');
_Tab.efld_opt(_win_red,_enable,POZDOK,'TP');
_Tab.efld_opt(_win_red,_enable,POZDOK,'REJ');
_Tab.efld_opt(_win_red,_enable,POZDOK,'DOK_REJ');
_Tab.efld_opt(_win_red,_enable,POZDOK,'RVAT');
_Tab.efld_opt(_win_red,_enable,APARAM,'OKROVAT1');
_mark:={? _enable=_enable1 || _mark1 || _mark0 ?};
_Tab.efld_opt(_win_red,_mark,POZDOK,'KH');
_Tab.efld_opt(_win_red,_mark,POZDOK,'NIP');
_Tab.efld_opt(_win_red,_mark,POZDOK,'DATA1');
_Tab.efld_opt(_win_red,_mark,POZDOK,'TP');
_Tab.efld_opt(_win_red,_mark,POZDOK,'REJ');
_Tab.efld_opt(_win_red,_mark,POZDOK,'DOK_REJ');
_Tab.efld_opt(_win_red,_mark,POZDOK,'RVAT')


\tyt_srod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [8.70]
:: OPIS: Wartosc poczatkowa dla pola SLOSLU.SRODKI
::  OLD: \tyt_srod/wzorce.fml
::----------------------------------------------------------------------------------------------------------------------
WAL.cntx_psh();
_set:=menu_pth='DD' | menu_pth='DP' | menu_pth='Z';
{? _set
||
   RAPORT.cntx_psh(); DOKUMENT.cntx_psh();
   POZDOK.DOKUMENT().RAPORT();
   DOKUMENT.index('RAPORT'); DOKUMENT.prefix(RAPORT.ref())
?};
_pr:=BO.PR_WAL;
BO.PR_WAL:={? POZDOK.POZOPER().ZM_OBR='N' || 0 |? POZDOK.PLUS_MIN='+' || -1 || 1 ?}*POZDOK.KW_WAL;
SLOSLU.SRODKI:=exec('srodki','kasa_wspolne',DOKUMENT.WALUTA().SYM);
BO.PR_WAL:=_pr;
{? _set || DOKUMENT.cntx_pop(); RAPORT.cntx_pop() ?};
WAL.cntx_pop();
1


\bdsymza2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Przed wyswietl dla pola ROZNE.SYM_ZAL2 (kasa)
::  OLD: \bdsymza2/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
EZALPOZ.index('ZRODLO'); EZALPOZ.prefix(REF.FIRMA,$POZDOK.ref(),$POZDOK.ref());
ROZNE.SYM_ZAL2:={? EZALPOZ.first() || EZALPOZ.EZAL().SYM || '' ?};
1


\tyt_obp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2010]
:: OPIS: tytul dla waluty obcej
::  OLD: \tyt_obp/wzorce.fml
::----------------------------------------------------------------------------------------------------------------------
SLOSLU.TYT_OBP:='';
{? DOKUMENT.WALUTA
|| SLOSLU.TYT_OBP:='Kwota w '+DOKUMENT.WALUTA().SYM
|| SLOSLU.TYT_OBP:='Kwota w walucie'
?}


\bo_prclr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2010]
:: OPIS: Przed dolacz POZDOK
::  OLD: \bo_prclr/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
BO.PR_WAL:=0; ROZNE.SYM_ZAL:='';
exec('tmpdpom','kasa_wspolne',1);
_wyn:=1;
_wyn:=exec('czy_pzn','kasa_pozdok');
_wyn


\czy_pzn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WS [2011]
:: OPIS: Sprawdzenie czy mozna dodac pozycje na znaki - musimy miec wybrana multikase i aktualne stanowisko musi
::       w tej kasie byc zdefiniowane
::   WY: 0/1
::  OLD: \czy_pzn/ml_znaki.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=1;
{? DOKUMENT.OPER().ZNAKI<>'T' | (var_pres('__multik')>0 & __multik=1)
|| 1
|? KPARAM.TZN='T' & USRCONST.STANKAS=null
|| FUN.info('W parametrach pracy należy ustawić stanowisko kasowe'@);
   _wyn:=0
?};
_wyn


\bo_prset
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2009]
:: OPIS: Przed poprawianiem rekordu w tabeli POZDOK
::  OLD: \bo_prset/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
BO.PR_WAL:=POZDOK.KW_WAL; exec('tmpdpom','kasa_wspolne',1);
1


\pop_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL  [8.50]
:: OPIS: Formula Popraw po dla okienek wertowania tabeli POZDOK
::   WE:
::   WY:
::  OLD: \pop_poz/okna.fml
::----------------------------------------------------------------------------------------------------------------------
ROZNE.SYM_ZAL:='';
{? ~POZDOK.r_lock(1,1,1)
||
   FUN.info('Pozycja obsługiwana przez innego operatora.'@)
||
   {? POZDOK.edit("exec('poz_wal','kasa_pozdok')")
   || do();
      POZDOK.put();
      exec('poz_calc','kasa_pozdok');
      end()
   ?};
   POZDOK.r_unlock()
?};
1


\poz_usu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Usuwanie pozycji dokumentow
::  OLD: \poz_usu/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_sel_size:=POZDOK.sel_size(); _komun:={? _sel_size=0 || 1 || 0 ?};
_ok:=1;
{? POZDOK.PLUS_MIN='+' & USRCONST.STANKAS().STANKAS().CZY_SMIN='N'
|| _srodki:=exec('srodki','kasa_wspolne',DOKUMENT.WALUTA().SYM);
   {? (_srodki-POZDOK.KW_WAL)$2<0
   || {? ~_sel_size
      || FUN.info('Usunięcie pozycji spowodowałoby powstanie niedozwolonego\n'
            'na danym stanowisku kasowym ujemnego salda dla waluty %1.'@[DOKUMENT.WALUTA().SYM])
      ?};
      _ok:=0
   ?}
?};
{? _ok & ((_sel_size=0 & FUN.ask('Usunąć pozycję dokumentu kasowego?'@)) | _sel_size=1)
||
   do();
   _komun:=0;
   VPD.index('VPD'); VPD.prefix(POZDOK.ref());
   {? VPD.first ||  {! |? VPD.del !} ?};
   EZALPOZ.index('ZRODLO'); EZALPOZ.prefix(REF.FIRMA,$POZDOK.ref(),$POZDOK.ref());
   {? EZALPOZ.first()
   || {? ~exec('upd_ezal','kasa_pozdok',1)
      || {? ~_sel_size || _komun:=1 ?}; undo()
      ?}
   ?};
   exec('del_d','kasa_pozdok');
   POZDOK.del();
   {? var_pres('licz_gr')>0 || licz_gr+=1 ?};
   exec('poz_calc','kasa_pozdok');
   end();

   {? _komun
   || FUN.info('Zaliczka powiązana z pozycją dokumentu kasowego jest zamknięta lub\n'
         'edytowana przez innego operatora.'@)
   ?}
?}


\del_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Usuwa wartosc zaplaty kasowej przy usuwaniu rekordu w tabeli POZOPER
::  OLD: \del_d/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.FAKS<>null
|| FAKS.cntx_psh();
   FAKS.use(ref_name(POZDOK.FAKS)); FAKS.prefix();
   {? FAKS.seek(POZDOK.FAKS)
   || {? DOKUMENT.WALUTA=exec('wal_nar','kasa_wspolne')
      || FAKS.PLATKAS-=POZDOK.KW_PLN
      || FAKS.PLATKAS-=POZDOK.KW_WAL
      ?};
      FAKS.put()
   ?};
   FAKS.cntx_pop()
?}


\pozusgbe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula poczatkowa dla grupowego usuwania pozycji dokumentow
::  OLD: \pozusgbe/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
licz_gr:=0; ile_zazn:=POZDOK.sel_size();
_pyt:=FUN.ask('Czy usunąć zaznaczone pozycje dokumentów?'@);
{? ~_pyt || &licz_gr; &ile_zazn ?};
_pyt


\pozusgae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.60]
:: OPIS: Formula koncowa dla grupowego usuwania pozycji dokumentow
::  OLD: \pozusgae/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
FUN.info('Liczba zaznaczonych pozycji dokumentów: %1\n'
   'Liczba usuniętych pozycji dokumentów: %2'@[$ile_zazn,$licz_gr]);
&licz_gr; &ile_zazn; 1


\pd_buf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: ustawienie buforów tabel których pola są pokazywane w okienku
::       wyciemnienie rekordów które pokazują transakcje związane z VAT
::   WE:
::   WY:
::  OLD: \pd_buf/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.FAKS || FAKS.use(ref_name(POZDOK.FAKS)) ?};
POZDOK.DOKUMENT();
POZDOK.POZOPER();
DOKUMENT.OPER();
DOKUMENT.RAPORT();
exec('pozdok_win_edit_set','kasa_pozdok');
_act:={? POZDOK.POZOPER().VAT='T' || '' || 'V' ?};
{? POZDOK.DOKUMENT().STAT_REJ='Z' || _act:=_act+'dUP' ?};
POZDOK.actions_grayed(POZDOK.win_sel('WER'),_act);
{? -POZDOK.ZAKS='t'
|| 'POZDOK#01#01'
|| ''
?}


\pozdkref
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RA [2010]
:: OPIS:
::  OLD: \pozdkref/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.ref()


\bl_stvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula na wartosc poczatkowa dla pola VPD.PR
::   WE:
::   WY: \bl_stvat/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.POZOPER().STVATDOM


\vatredpd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula przed redakcja pola VPD.VAT
::   WE:
::   WY:
::  OLD: \vatredpd/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? VPD.VAT=0    || VPD.VAT:=(0.01*VPD.NETTO*exec('vat','dok_fks',VPD.PR().KOD))$2 ?};
{? VPD.BRUTTO=0 || VPD.BRUTTO:=(VPD.NETTO+VPD.VAT)$2 ?};
1


\vat_redb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula przed redakcja pola VPD.BRUTTO
::   WE:
::   WY:
::  OLD: \vat_redb/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? VPD.BRUTTO=0 || VPD.BRUTTO:=(VPD.NETTO+VPD.VAT)$2 ?}; 1


\bl_grvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula na wartosc poczatkowa dla pola VPD.GRVAT
::   WE:
::   WY:
::  OLD: \bl_grvat/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.REJ();
{? POZDOK.REJ=exec('get_rej','rejestry',POZDOK.POZOPER().REJDOM)
|| POZDOK.POZOPER().GRPODDOM
|| null
?}


\ae_brut
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Po redakcji pola VPD.BRUTTO
::   WE:
::   WY:
::  OLD: \ae_brut/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_wart:=fld(fld()$2);
{? VPD.NETTO$2=0 & VPD.VAT$2=0
|| VPD.NETTO:=(_wart/(1+0.01*exec('vat','dok_fks',VPD.PR().KOD)))$2;
   VPD.VAT:=(VPD.BRUTTO-VPD.NETTO)$2
?}; 1


\begrvvpd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula przed redakcja pola VPD.GRVAT
::   WE:
::   WY:
::  OLD: \begrvvpd/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? POZDOK.POZOPER().ZMDOMGRU='T'
|| POZDOK.REJ(); 1
|| 0
?}


\f3_r_vpd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula na klawisz F3 w polu VPD.R i VPD.KK
::   WE:
::   WY:
::  OLD: \f3_r_vpd/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
KK.index('KONTASYS'); KK.prefix(exec('ref_firma','ustawienia'),exec('bl_kk_system','konto')); KK.first();
{? fld<>'' || KK.find_key(fld()) ?};
{? KK.select(,1) || KK.SYM ?}


\ae_kkvpd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula po redakcji pol VPD.R i VPD.KK
::   WE:
::   WY:
::  OLD: \ae_kkvpd/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
{? |fld()<>''
|| KK.index('KONTASYS'); KK.prefix(exec('ref_firma','ustawienia'),exec('bl_kk_system','konto'),);
   {? KK.find_key(fld())
   || fld(KK.SYM)
   || _zwrot:=FUN.ask('W tabeli kont kosztowych nie znaleziono'
         '\nkonta o symbolu %1'
         '\nZaakceptować?'@[fld()])
   ?}
?};
_zwrot


\poz_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Akcja pozycje Vat w okienkach wertowania tabeli POZDOK oraz ZALPOZ
::   WE: _a - zadeklarowane - domyslnie przy dolaczaniu pozycji
::  OLD: \poz_vat/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? _ | (~_ & exec('be_vat','kasa_pozdok'))
||
   _upr:=USRCONST.STANKAS().UPR;
   KUSERUPR.cntx_psh;
   KUSERUPR.index('KU_ST'); KUSERUPR.prefix(OPERATOR.USER,KUSERUPR.STANKAS);
   {? KUSERUPR.first || _upr:=KUSERUPR.UPR ?};
   KUSERUPR.cntx_pop;

   {? (POZDOK.DOKUMENT().RAPORT().STATUS=exec('rap_stat','kasa_raport',0)
      | POZDOK.DOKUMENT().RAPORT().STATUS=exec('rap_stat','kasa_raport',1)) & ROZNE.BLOK_RAP
      & _upr='T'
   || VPD.win_sel('WER_1'); _chk:=1
   || VPD.win_sel('WER_2'); _chk:=0
   ?};
   exec('sum_vpd','kasa_pozdok');
   _chk:=_chk & 1+menu_pth()<>'Z';
   VPD.index('VPD'); VPD.prefix(POZDOK.ref()); VPD.first();
   {! |?
       VPD.select();
       {? _chk
       || DOKUMENT.cntx_psh();
          DOKUMENT.index('RAPORT'); DOKUMENT.prefix(POZDOK.DOKUMENT().RAPORT);
          _ok:=~exec('chksaldo','kasa_pozdok',SKID.SUMWN,'Suma brutto');
          DOKUMENT.cntx_pop(); _ok
       ?}
   !};
   {? _chk ||
      POZDOK.DOKUMENT();
      {? POZDOK.KW_PLN$2<>SKID.SUMWN$2
      || {? VPD.first() & SKID.SUMWN$2>0 &
            {? DOKUMENT.DOK_NUM=0 | (DOKUMENT.DOK_NUM<>0 & -DOKUMENT.OPER().CZY_BLOK='n')
            || 1
            || 0
            ?} &
            {? USRCONST.STANKAS().STANKAS().CZY_SMIN='N'
            || BO.PR_WAL:=0; _srodki:=exec('srodki','kasa_wspolne',DOKUMENT.WALUTA().SYM);
               _ile_jest:={? DOKUMENT.WALUTA=exec('wal_nar','kasa_wspolne')
                          || SKID.SUMWN$2
                          || (SKID.SUMWN$2/DOKUMENT.KURS*DOKUMENT.WALUTA().J)$2
                          ?};
               _ile_bylo:=POZDOK.KW_WAL;
               (_srodki-_ile_bylo+_ile_jest)$2>=0
            ?}
         || _zwrot:=FUN.ask('Kwota z pozycji VAT nie zgadza się'
               '\nz kwotą w pozycji dokumentu.'
               '\nPoprawić kwotę w pozycji dokumentu?'@);
            {? _zwrot
            || {? DOKUMENT.WALUTA=exec('wal_nar','kasa_wspolne')
               || POZDOK.KW_PLN:=POZDOK.KW_WAL:=SKID.SUMWN$2
               || POZDOK.KW_PLN:=SKID.SUMWN$2;
                  POZDOK.KW_WAL:=(POZDOK.KW_PLN/DOKUMENT.KURS*DOKUMENT.WALUTA().J)$2
               ?};
               POZDOK.put();
               exec('poz_calc','kasa_pozdok',0)
            ?}
         |? ~VPD.first()
         || FUN.info('Przed zamknięciem raportu należy wprowadzić pozycje VAT.'@)
         || FUN.info('Kwota z pozycji VAT nie zgadza się'
               '\nz kwotą w pozycji dokumentu.'
               '\nPrzed zamknięciem raportu należy skorygować pozycje VAT.'@)
         ?}
      ?}
   ?}
|| _war1:=POZDOK.POZOPER().VAT='T';
   _war2:=PAR_SKID.get(90)='T';
   {? _war1=0 & _war2=0
   || FUN.info('Funkcja niedostępna.'
         '\nWybrana transakcja musi umożliwiać wprowadzanie dokumentów VAT'
         '\na także parametr 90 systemu musi mieć ustawioną wartość na T.'@)
   |? _war1=0 &_war2<>0
   || FUN.info('Akcja dostępna tylko dla dokumentów VAT.'@)
   |? _war1<>0 & _war2=0
   || FUN.info('Funkcja niedostępna.'
         '\nParametr 90 systemu musi mieć ustawioną wartość na T.'@)
   ?}
?}


\sum_vpd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Uzupelnianie pol SKID.SUMWN i SKID.SUMMA w okienku wertowania tabeli VPD
::   WE:
::   WY:
::  OLD: \sum_vpd/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_sum:=0;
VPD.cntx_psh();
VPD.index('VPD'); VPD.prefix(POZDOK.ref());
{? VPD.first() || {! |? _sum+=VPD.BRUTTO; VPD.next() !} ?};
SKID.SUMWN:=_sum; SKID.SUMMA:=POZDOK.KW_PLN-_sum;
VPD.cntx_pop();
1


\vat_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Rekord po dla tabeli VPD
::   WE:
::   WY:
::  OLD: \vat_chk/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? VPD.NETTO=0 & VPD.VAT=0 & VPD.BRUTTO=0
|| FUN.info('Przynajmniej jedna z pozycji: Netto, VAT lub Brutto\n'
      'musi być różna od zera.'@); 'NETTO'
|? POZDOK.cntx_psh(); DOKUMENT.cntx_psh();
   DOKUMENT.index('RAPORT'); DOKUMENT.prefix(VPD.POZDOK().DOKUMENT().RAPORT);
   _wy:={? DOKUMENT.first() || ~exec('chksaldo','kasa_pozdok',VPD.BRUTTO,'Brutto') || 0 ?};
   POZDOK.cntx_pop(); DOKUMENT.cntx_pop(); _wy
|| 0
|| _v:='';
   {? VPD.PR=null
   || FUN.info('Proszę wybrać stawkę VAT.'@); _v:='PR'
   ?};
   {? _v='' & VPD.GRVAT=null
   || FUN.info('Proszę wybrać grupę podatkową'
         '\nlub ewentualnie skorygować parametry transakcji.'@); _v:='GRVAT'
   ?};
   {? _v=''
   || VPOZ.NETTO:=VPD.NETTO;
      VPOZ.VAT:=VPD.VAT;
      VPOZ.BRUTTO:=VPD.BRUTTO;
      VPOZ.PR:=VPD.PR;
      _w:=exec('brut_net','dok_fks');
      {? (type_of(_w)=2 & _w<>'') |  (type_of(_w)=1 & _w=0) ||  _v:='NETTO' ?}
   ?};
   _v
?}


\okvatwys
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula na 'Wyswietl' dla pola APARAM.OKROVAT1
::   WE:
::   WY:
::  OLD: \okvatwys/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK.DOK_REJ<>null & (DOK.DOK_REJ().SLO().KOD='VAT' | SLO.KOD='SAD')
|| ROK_F.cntx_psh();
   APARAM.OKROVAT1:={? POZDOK.OKROVAT<>null
                    || POZDOK.OKROVAT(); OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ
                    || ''
                    ?};
   ROK_F.cntx_pop()
|| ''
?}


\f3_okrv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [8.10]
:: OPIS: Formula na F3 dla pola APARAM.OKROVAT1
::   WE:
::   WY:
::  OLD: \f3_okrv/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=POZDOK.OKROVAT;
POZDOK.OKROVAT();
_naz:=OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ;
ROK_F.cntx_psh(); ROK_F.index('NAZWA'); ROK_F.prefix(REF.FIRMA);
OKRO_F.cntx_psh(); OKRO_F.win_sel('WER');
OKRO_F.index('FIRMA_NR'); OKRO_F.prefix(REF.FIRMA);
{? APARAM.OKROVAT1<>''
|| {? OKRO_F.first()
   || {! |? {? OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ=APARAM.OKROVAT1 || 0 || OKRO_F.next() ?} !}
   ?}
?};
{? OKRO_F.select(,1)
|| APARAM.OKROVAT1:=OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ;
   POZDOK.OKROVAT:=OKRO_F.ref();
   _zwrot:=1
|| APARAM.OKROVAT1:=_naz;
   POZDOK.OKROVAT:=_ref;
   _zwrot:=0
?};
OKRO_F.cntx_pop();
ROK_F.cntx_pop();
{? _zwrot || APARAM.OKROVAT1 || 0 ?}


\por_okr2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Po redagowaniu APARAM.OKROVAT1
::   WE:
::   WY:
::  OLD: \por_okr2/slo_slu.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=1 || APARAM.OKROVAT:=APARAM.OKROVAT1 ?};
OKRO_F.cntx_psh();
ROK_F.cntx_psh();
OKRO_F.index('NAZ'); OKRO_F.prefix(REF.FIRMA);
_zwrot:=0;
{? |APARAM.OKROVAT<>''
|| _i:=APARAM.OKROVAT*'/';
   {? _i>0
   || _r:=_i-APARAM.OKROVAT; _m:=(_i-1)+APARAM.OKROVAT;
      {? OKRO_F.find_key(_r,_m)
      || APARAM.OKROVAT:=OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ;
         DOK.OKRVAT:=POZDOK.OKROVAT:=OKRO_F.ref();
         _zwrot:=1
      ?}
   || {? OKRO_F.find_key(ROK_F.NAZ,APARAM.OKROVAT)
      || APARAM.OKROVAT:=OKRO_F.NAZ+'/'+OKRO_F.ROK().NAZ;
         DOK.OKRVAT:=POZDOK.OKROVAT:=OKRO_F.ref();
         _zwrot:=1
      ?}
   ?}
|| DOK.OKRVAT:=POZDOK.OKROVAT:=null; _zwrot:=1
?};
{? _zwrot=0
|| FUN.info('Błędna nazwa okresu'@);
   DOK.OKRVAT:=POZDOK.OKROVAT:=null; _zwrot:=0
?};
OKRO_F.cntx_pop();
ROK_F.cntx_pop();
_zwrot


\pozdok_win_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zwraca okno redakcji tabeli DOKUMENT
::   WE: _a - 0-nie tworzyć okna, 1-tworzyć okno
::   WY: akronim okna redakcji
::  OLD: \dok_poz/vat_ks.fml
::----------------------------------------------------------------------------------------------------------------------
_mk_edit:={? var_press('_a')=type_of(0) || _a || 1 ?};

WAL.cntx_psh();
_st_wal:=STANKAS.WALUTA; _st_waln:=STANKAS.WALUTA().SYM;
WAL.cntx_pop();
_okn_vat:=0;
{? exec('get_par','#parametr',90)='T'
|| POZOPER.cntx_psh();
   POZOPER.index('OPER'); POZOPER.prefix(OPER.ref());
   {? POZOPER.first()
   || {!
      |?
         {? -POZOPER.VAT='t' || _okn_vat:=1 ?};
         ~_okn_vat & POZOPER.next()
      !}
   ?};
   POZOPER.cntx_pop()
?};

_wal:=_st_wal=null | (_st_wal<>null & _st_waln<>KINFO.NAROD().KOD);

_title:='Pozycja'@;

_win_akr:=
   {? _okn_vat || 'REDVAT'
               || 'RED'
   ?};
_interm:=exec('interm','#system');
_mode:='';
{? _interm || _mode:='maximized'?};
_win_red:=
   {? _mk_edit
   || _win_red:=POZDOK.mk_edit(_title,,'pozdok',,,_mode);
      POZDOK.win_ewin(_win_red,,_win_akr);
      _win_red
   || _win_akr
   ?};

_win_red


\pozdok_win_edit_set
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Ustawia okno redakcji tabeli POZDOK, wymagane pola, okna słowników
::   WE: [_a] - 1-tryb Display 0-(domyślnie) nie
::       [_b] - typ
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_disp:={? var_pres('_a')=type_of(0) || _a || 0 ?};
 _typ:={? var_pres('_b')=type_of('') || _b || '' ?};

_win_red:=exec('pozdok_win_edit','kasa_pozdok',_typ);

{? ~_disp
|| exec('ok_esc','#window',POZDOK,_win_red,1,,,,,exec('help_red_ok','#window','Z'),exec('text_red_ok','#window'),
exec('help_red_esc','#window','A'))
?};
POZDOK.win_edit(_win_red);

exec('set_efld_opt','kasa_pozdok')


\f3okrl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2006]
:: OPIS: Na f3 dla pola ROZNE.LOG_OKR
::  OLD: \f3okrl/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=ROZNE.LOG_OKR;

OKR.cntx_psh();
OKR.index('OKR'); OKR.prefix(FIRMA.ref,ST.AR,ST.AM);
_ref:={? OKR.first() || OKR.ref || null ?};
OKR.cntx_pop();

OKR.cntx_psh();
OKR.index('OKRN'); OKR.prefix();
{? OKR.first()
|| {? POZDOK.LOG_OKR<>null || OKR.seek(POZDOK.LOG_OKR) || OKR.seek(_ref) ?};
   OKR.win_sel('WER_1');
   {? OKR.select(,1)
   || POZDOK.LOG_OKR:=OKR.ref();
      _zwrot:=POZDOK.LOG_OKR().NAZ
   ?}
|| _zwrot:=''
?};
OKR.cntx_pop();
_zwrot


\pozdok_d
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AMK] [2006]
:: OPIS: Sprawdzanie i ustawianie danych dotyczacych dokumentow sprzedazy z Logistyki
::   WY: Pusty string (ok) akronim pola (blad)
::  OLD: \pozdok_d/poz_pola.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=''; _wal_nar:=(DOKUMENT.WALUTA=exec('wal_nar','kasa_wspolne'));
{? -menu_txt='popraw' || exec('ae_mftid','kasa_pozdok',1) ?};
{? POZDOK.FAKS<>null
|| TMPDPOM.cntx_psh();
   TMPDPOM.index(ROZNE.INDEX2);
   TMPDPOM.prefix(POZDOK.MF_FIKS,POZDOK.MF_FIKS,ref_name(POZDOK.FAKS),#POZDOK.FAKS);
   {? TMPDPOM.first() &
      (_wal_nar & POZDOK.KW_PLN>fabs(TMPDPOM.WAR)) | (~_wal_nar & POZDOK.KW_WAL>fabs(TMPDPOM.WARW))
   || {? FUN.ask('Kwota w pozycji dokumentu jest większa od'
            '\nod bieżącego salda wybranego dokumentu sprzedaży.\nKontynuować?'@)
      || _zwrot:={? _wal_nar || 'KW_PLN' || 'KW_WAL' ?}
      ?}
   ?};
   TMPDPOM.cntx_pop()
?};
{? _zwrot=''
|| {? ROZNE.FAKS<>null
   || FAKS.cntx_psh();
      FAKS.use(ref_name(ROZNE.FAKS)); FAKS.prefix();
      {? FAKS.seek(ROZNE.FAKS) || FAKS.PLATKAS-=ROZNE.FAKSWART; FAKS.put() ?};
      FAKS.cntx_pop()
   ?};
   {? POZDOK.FAKS<>null
   || FAKS.cntx_psh();
      FAKS.use(ref_name(POZDOK.FAKS)); FAKS.prefix();
      {? FAKS.seek(POZDOK.FAKS)
      || {? FAKS.r_lock(1,1,1)
         || {? _wal_nar
            || FAKS.PLATKAS+=POZDOK.KW_PLN
            || FAKS.PLATKAS+=POZDOK.KW_WAL
            ?};
            FAKS.put();
            FAKS.r_unlock()
         || _zwrot:={? _wal_nar || 'KW_PLN' || 'KW_WAL' ?};
            exec('who_rlock_faks','faktury_nag')
         ?}
      ?};
      FAKS.cntx_pop()
   ?}
?};
_zwrot


\o_dokupo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [--] [--.--]
:: OPIS:  wywoływana na akcję pozycje w okienku przegladania dokumentów
::        bez możliwosci poprawiania
::  OLD: \o_dokupo/kasa.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMENT.OPER();
POZDOK.index('DOKUMENT');
POZDOK.prefix(DOKUMENT.ref);
exec('set_poz','kasa_pozdok',0,0);
POZDOK.hdr_sel(': '+OPER.NAZWA);
POZDOK.select(,,,{? PAR_SKID.get(80)='N' || 'Z' || '' ?});
0


\bdsymzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Przed wyswietl dla pola ROZNE.SYM_ZAL (kasa)
::  OLD: \bdsymzal/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
_form:="_first:=0;
        POZDOK.cntx_psh(); POZDOK.index('DOKUMENT'); POZDOK.prefix(DOKUMENT.ref());
        {? POZDOK.first()
        || EZALPOZ.cntx_psh();
           EZALPOZ.index('ZRODLO'); EZALPOZ.prefix(REF.FIRMA,$POZDOK.ref(),$POZDOK.ref()); _first:=EZALPOZ.first();
           EZALPOZ.cntx_pop()
        ?}; POZDOK.cntx_pop(); _first";
{? DOKUMENT.OPER=null | DOKUMENT.OPER().CZY_ZAL<>'T'
|| ROZNE.SYM_ZAL:=''
|? -menu_txt<>'dołącz' &
   ((-menu_txt<>'popraw' & -menu_txt<>'pozycje') |
    ((-menu_txt='popraw' | -menu_txt='pozycje') & ROZNE.SYM_ZAL='') |
    (-menu_txt='pozycje' & DOKUMENT.OPER().CZY1_POZ='T' & var_pres('dsp_dok')>0)
   ) &
   (DOKUMENT.OPER().CZY1_POZ='N' | (OPER.CZY1_POZ='T' & _form()))
|| EZALPOZ.cntx_psh();
   EZALPOZ.index('ZRODLO'); EZALPOZ.prefix(REF.FIRMA,$POZDOK.ref(),$POZDOK.ref());
   ROZNE.SYM_ZAL:={? EZALPOZ.first() || EZALPOZ.EZAL().SYM || '' ?};
   EZALPOZ.cntx_pop()
?};
1


\f3symzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Akcja na klawisz f3 pola ROZNE.SYM_ZAL
::  OLD: \f3symzal/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
POZDOK.DOKUMENT();
exec('RB','object');
zakr_zal:=3; zal_kasa:=1;
EZAL.cntx_psh();
EZAL.win_sel('WER_KASA'); EZAL.win_edit('WPR_KASA');
_zakr:='K'; _find:=0;
{? ROZNE.SYM_ZAL<>''
|| EZAL.index('INDEX4OZ');
   EZAL.prefix(REF.FIRMA,SKID.OSOBA,SKID.WAL_DOK,ROZNE.SYM_ZAL,ROZNE.SYM_ZAL);
   _find:=EZAL.first()
?};
exec('zakr_zal','zaliczki',3);
{? ~_find || EZAL.first() ?};
_sym:={? EZAL.select(,1) || EZAL.SYM || 0 ?};
EZAL.cntx_pop();
VAR_DEL.delete('zakr_zal','zal_kasa');
_sym


\besymzalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Przed redakcja symbolu zaliczki w kasie
::  OLD: \besymzalk/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
DOKUMENT.cntx_psh();
_edit:=(DOKUMENT.OPER().CZY_ZAL='T' & DOKUMENT.DOTYCZY<>null & DOKUMENT.DLA<>null);
{? _edit
|| _edit:=0;
   {? DOKUMENT.WALUTA<>null & KINFO.SLWAL<>null
   || SLO.cntx_psh();
      SLO.index('SL'); SLO.prefix(KINFO.SLWAL().SLU);
      {? SLO.first() & SLO.find_key(DOKUMENT.WALUTA().SYM) & WAL.SYM=SLO.KOD
      || SKID.WAL_DOK:=SLO.ref(); _edit:=1
      ?};
      SLO.cntx_pop()
   ?}
?};
{? _edit || _edit:=exec('szuk_oso','kasa_pozdok',DOKUMENT.DLA) ?};
DOKUMENT.cntx_pop();
{? _edit || __BE_SYM_ZAL:=ROZNE.SYM_ZAL ?};
_edit


\aesymzalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2009]
:: OPIS: Po redakcji symbolu zaliczki w kasie
::  OLD: \aesymzalk/skid_zal.fml
::----------------------------------------------------------------------------------------------------------------------
_dalej:=1;
{? ROZNE.SYM_ZAL=''
|| FUN.info('Nie wprowadzono symbolu zaliczki.'@); _dalej:=0
?};
{? _dalej
|| _dalej:=0;
   EZAL.cntx_psh();
   EZAL.index('INDEX4OZ'); EZAL.prefix(REF.FIRMA,SKID.OSOBA,SKID.WAL_DOK,ROZNE.SYM_ZAL);
   {? ~EZAL.first()
   || FUN.info('Nie znaleziono zaliczki o symbolu %1.'@[ROZNE.SYM_ZAL])
   || _dalej:=1
   ?};
   {? _dalej & EZAL.ZAKCREJ<>'T'
   || FUN.info('Wybrano zaliczkę z niezakończoną rejestracją.'@); _dalej:=0
   ?};
   {? _dalej & EZAL.ETYPZAL().KASPRZEL<>'K'
   || _dalej:=FUN.ask('Wybrano zaliczkę, która nie jest realizowana w kasie. Kontynuować?'@)
   ?};
   {? _dalej
   || fld(EZAL.SYM);
      _dalej:=1;
      _pln:=USRCONST.STANKAS().STANKAS().WALUTA<>null & STANKAS.WALUTA().SYM=KINFO.NAROD().KOD;
      {? __BE_SYM_ZAL<>ROZNE.SYM_ZAL | (_pln & POZDOK.KW_PLN$2=0) | (~_pln & POZDOK.KW_WAL$2=0)
      || _kw:=EZAL.KW_PROP$2-EZAL.ROZCHOD;
         {? -menu_txt<>'dołącz'
         || _rpd:=null;
            EZALPOZ.cntx_psh();
            {? DOKUMENT.OPER().CZY1_POZ='T'
            || POZDOK.cntx_psh();
               POZDOK.index('DOKUMENT'); POZDOK.prefix(DOKUMENT.ref());
               {? POZDOK.first() || _rpd:=POZDOK.ref() ?};
               POZDOK.cntx_pop()
            || _rpd:=POZDOK.ref()
            ?};
            {? _rpd<>null
            || EZALPOZ.index('ZRODLO'); EZALPOZ.prefix(REF.FIRMA,$_rpd,$_rpd);
               {? EZALPOZ.first()
               || {? POZDOK.PLUS_MIN='+' || EZALPOZ.PRZYCHOD || EZALPOZ.ROZCHOD ?}
               || 0
               ?}
            ?};
            EZALPOZ.cntx_pop()
         ?};
         _kw:=_kw$2;
         {? _kw>0
         || {? _pln
            || POZDOK.KW_PLN:=_kw
            || POZDOK.KW_WAL:=_kw;
               _jedn:=DOKUMENT.WALUTA().J;
               POZDOK.KW_PLN:={? _jedn<>0
                              || (_kw*DOKUMENT.KURS/_jedn)$2
                              || 0
                              ?}
            ?}
         ?}
      ?}
   ?};
   EZAL.cntx_pop()
?};
_dalej


\licz_kwpln
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [19.02]
:: OPIS: wylicza wartość POZDOK.KW_PLN
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? DOKUMENT.WALUTA
|| POZDOK.KW_PLN:=(POZDOK.KW_WAL*DOKUMENT.KURS/DOKUMENT.WALUTA().J)$2
|| POZDOK.KW_PLN:=(POZDOK.KW_WAL*DOKUMENT.KURS)$2
?};
:: jesli jest okreslony stan minimalny to sprawdza czy wpisana kwota spowoduje przekroczenie limitu
{? MLKAS.MIN>0 & MLKAS.MIN<=SALDO.PLN
|| exec('spsmin','kasa_pozdok', MLKAS.MIN, SALDO.PLN, POZDOK.KW_PLN)
?}

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 ccce0261bb5f5d430b77c1f524f0e413d8ec49e26df73f9f62c9c4a571e88e4c26eebd9261a82c6df9fec875cdb9d4176ce6878ff3154f51b7359a88c50e2b744b7651b1fdcd67706b5ec2b6c3dcf5ec3bcd0eadc0c30d0d289b32dd26f9f51383fdf8c428d1f01502d9378b250c4aa93b0087226753c7c101de5632248cbd99

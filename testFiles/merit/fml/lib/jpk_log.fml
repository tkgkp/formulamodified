:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: jpk_log.fml
:: Utworzony: 04.04.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Obsługa JPK - logistyka
::======================================================================================================================


\polafaks_get
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Wypełnienie bufora FAKSPOLA dla _a
::   WE: _a - FAKS.ref()
::       _b - tabela w której osadzone jest okno redakcji
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_faks:={? var_pres('_a')=type_of(null()) || _a || FAKS.ref() ?};
{? var_pres('_b')=type_of(FAKS)
||
   VAR_DEL.delete('TAB2FAKSPOLA'); TAB2FAKSPOLA:=_b
?};
_result:=1;
FAKSPOLA.index('UNIK');
FAKSPOLA.prefix(_faks);
{? ~FAKSPOLA.first()
||
   _copy:=0;
   FAKS.cntx_psh();
   {? FAKS.seek(_faks) & FAKS.KOR <> ''
   ||
      _kor:= FAKS.KOR;
      FAKS.index('FAK_SYM1');
      {? FAKS.find_key(_kor,) || FAKSPOLA.prefix(); _copy:= FAKSPOLA.find_key(FAKS.ref) ?}
   ?};
   FAKS.cntx_pop();
   {? ~_copy || FAKSPOLA.blank() ?};
   FAKSPOLA.REK:= _faks;
   _result:=FAKSPOLA.add()
?};
{? FAKS.SZ='S'
||
   exec('polafaks_update','jpk_log')
?};
_result


\polafaks_edit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Edycja pól zdefiniowanych w FAKS.POLA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? exec('polafaks_get','jpk_log',FAKS.ref(),FAKSPOLA)
||
   FAKSPOLA.win_edit('RED');
   {? FAKS.STAT_REJ<>'N'
   || FAKSPOLA.display()
   || {? FAKSPOLA.edit("exec('polafaks_chk','jpk_log',FAKSPOLA)")
      || FAKSPOLA.put()
      ?}
   ?}
?}


\polafaks_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Sprawdza poprawność wypełnienie pól zmiennej POLAFAKS
::   WE: _a - uchwyt do tabeli
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a;
{? _Tab.MK<>'' & 'TN'*_Tab.MK=0 || _Tab.MK:=''
|? _Tab.SAMFAK<>'' & 'TN'*_Tab.SAMFAK=0 || _Tab.SAMFAK:=''
|? _Tab.OO<>'' & 'TN'*_Tab.OO=0 || _Tab.OO:=''
|? _Tab.ZW<>'' & 'TN'*_Tab.ZW=0 || _Tab.ZW:=''
|? _Tab.EGZ<>'' & 'TN'*_Tab.EGZ=0 || _Tab.EGZ:=''
|? _Tab.PP<>'' & 'TN'*_Tab.PP=0 || _Tab.PP:=''
|? _Tab.TT<>'' & 'TN'*_Tab.TT=0 || _Tab.TT:=''
|? _Tab.USL_TUR<>'' & 'TN'*_Tab.USL_TUR=0 || _Tab.USL_TUR:=''
|? _Tab.UDS<>'' & 'TN'*_Tab.UDS=0 || _Tab.UDS:=''
|| ''
?};
{? _Tab.ZW='T' & _Tab.PZW_PRZE='' & _Tab.PZW_DUE=''  & _Tab.PZW_INNA=''
|| FUN.info('Jedno z pól Podstawy zwolnienia:\nPrzepis, Dyrektywa, Inna musi być wypełnione.'@);
   'PZW_PRZE'
|? _Tab.EGZ='T' & (_r:=__CHK.record(_Tab,,'EGZ_NAZ','EGZ_ADR'))<>''
|| _r
|? _Tab.PP='T' & (_r:=__CHK.record(_Tab,,'PP_NAZ','PP_ADR','PP_ID'))<>''
|| _r
|? _Tab.NST_DATA<>date(0,0,0) & (_Tab.NST_KM='' & _Tab.NST_GODZ='' | _Tab.NST_KM<>'' & _Tab.NST_GODZ<>'')
|| FUN.info('Jedno z pól: Przebieg albo Liczba godzin użytkowania\npowinno być wypełnione.'@);
   'NST_KM'
|? _Tab.UDS='T' & (_r:=__CHK.record(_Tab,,'UDS_OPIS'))<>''
|| _r
|? _Tab.UDS='T' & _Tab.UDS_OPIS<>exec('uds_opis','jpk_log',1) &
   _Tab.UDS_OPIS<>exec('uds_opis','jpk_log',2) &
   _Tab.UDS_OPIS<>exec('uds_opis','jpk_log',3)
|| FUN.info('Niepoprawny opis użycia dzieła sztuki.\nWybierz wartość ze słownika.'@);
   'UDS_OPIS'
|| _r:={? _Tab=DOKPOLA
       || __CHK.record(_Tab,,'MK','SAMFAK','OO','EGZ','ZW','PP','USL_TUR','UDS')
       || __CHK.record(_Tab,,'MK','SAMFAK','OO','EGZ','ZW','PP','TT','USL_TUR','UDS')
       ?};
   {? _r='' & var_pres('OKRES_DO',_Tab)>0 & _Tab.OKRES_DO<_Tab.OKRES_OD
   ||
      FUN.info('Data początku okresu powinna być wcześniejsza od daty jego końca.'@);
      _r:='OKRES_OD'
   ?};
   _r
?}


\polafaks_fld_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Przed redakcja pól zmiennej POLAFAKS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_afld:=cur_afld();
_Tab:=cur_tfld();
_result:={? _afld='OO'        || _Tab=DOKPOLA | FAKS.SZ='Z'
         |? _afld='PZW_PRZE'  || _Tab.ZW='T'
         |? _afld='PZW_DUE'   || _Tab.ZW='T'
         |? _afld='PZW_INNA'  || _Tab.ZW='T'
         |? _afld='EGZ_KH'    || _Tab.EGZ='T'
         |? _afld='EGZ_NAZ'   || _Tab.EGZ='T'
         |? _afld='EGZ_ADR'   || _Tab.EGZ='T'
         |? _afld='PP_KH'     || _Tab.PP='T'
         |? _afld='PP_NAZ'    || _Tab.PP='T'
         |? _afld='PP_ADR'    || _Tab.PP='T'
         |? _afld='PP_ID'     || _Tab.PP='T'
         |? _afld='NST_DATA'  || _Tab.NST_RODZ<>''
         |? _afld='NST_POJ'   || _Tab.NST_RODZ<>''
         |? _afld='NST_KM'    || _Tab.NST_DATA<>date(0,0,0) & _Tab.NST_RODZ<>''
         |? _afld='NST_GODZ'  || _Tab.NST_DATA<>date(0,0,0) & _Tab.NST_RODZ<>''
         |? _afld='NST_WBST'  || _Tab.NST_RODZ<>''
         |? _afld='TT'        || _Tab=DOKPOLA | FAKS.SZ='Z'
         |? _afld='UDS_OPIS'  || _Tab.UDS='T'
         |? _afld='OKRES_OD'  || _Tab=DOKPOLA | FAKS.WHERE<>'K' & FAKS.FPACZKA=null()
         |? _afld='OKRES_DO'  || _Tab=DOKPOLA | FAKS.WHERE<>'K' & FAKS.FPACZKA=null()
         |? _afld='UDS'       || _Tab<>DOKPOLA | exec('edit_proc','dok_fks1')
         |? _afld='USL_TUR'   || _Tab<>DOKPOLA | exec('edit_proc','dok_fks1')
         || 1
         ?};
{? _result || _result:=exec('be_edok_fld','dok_fks1') ?};
_result


\uds_opis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Zwraca opis uzycia dziela sztuki
::   WE: _a - nr 1..3
::----------------------------------------------------------------------------------------------------------------------
{? _a=1 || 'procedura marży - towary używane'
|? _a=2 || 'procedura marży - dzieła sztuki'
|? _a=3 || 'procedura marży - przedmioty kolekcjonerskie i antyki'
|| ''
?}


\polafaks_fld_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: F3 pól zmiennej POLAFAKS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_afld:=cur_afld();
VAR_DEL.delete('__Slo');
__Slo:=tab_tmp(1
   ,'O'  ,'STRING[120]' ,'Opis');
_add:="
   __Slo.O:=_a;
   __Slo.add()";
{? _afld='UDS_OPIS'
|| _add(exec('uds_opis','jpk_log',1));
   _add(exec('uds_opis','jpk_log',2));
   _add(exec('uds_opis','jpk_log',3))
?};
__Slo.win_sel(__Slo.mk_sel('Słownik'@,,1));
__Slo.win_act(__Slo.win_sel('?'),,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
_wyn:=
   {? __Slo.first() & __Slo.select()
   || __Slo.O
   || ~~
   ?};
VAR_DEL.delete('__Slo');
_wyn


\polafaks_fld_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Po redakcji pól zmiennej POLAFAKS
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_afld:=cur_afld();
_Tab:=cur_tfld();
_val:=fld();
{? _afld='ZW' & _Tab.ZW='N'  || _Tab.PZW_PRZE:=_Tab.PZW_DUE:=_Tab.PZW_INNA:=''
|? _afld='EGZ' & _Tab.EGZ='N' || _Tab.EGZ_NAZ:=_Tab.EGZ_ADR:=''; _Tab.EGZ_KH:=null()
|? _afld='EGZ_KH'
|| {?_Tab.EGZ_KH=null()
   || _Tab.EGZ_NAZ:=_Tab.EGZ_ADR:=''
   || _Tab.EGZ_NAZ:=_Tab.EGZ_KH().NAZ_P;
      _Tab.EGZ_ADR:=exec('kpocz','faktury_wydruk',_Tab.EGZ_KH().KPOCZ,_Tab.EGZ_KH().POCZ,
                    _Tab.EGZ_KH().MIASTO,_Tab.EGZ_KH().UL) + ' ' +
                    exec('adr','faktury_wydruk',_Tab.EGZ_KH().UL,,,_Tab.EGZ_KH().MIASTO,_Tab.EGZ_KH().POCZ)
   ?}
|? _afld='PP' & _Tab.PP='N' || _Tab.PP_NAZ:=_Tab.PP_ADR:=_Tab.PP_ID:=''; _Tab.PP_KH:=null()
|? _afld='PP_KH'
|| {? _Tab.PP_KH=null()
   || _Tab.PP_NAZ:=_Tab.PP_ADR:=''
   || _Tab.PP_NAZ:=_Tab.PP_KH().NAZ_P;
      _Tab.PP_ADR:=exec('kpocz','faktury_wydruk',_Tab.PP_KH().KPOCZ,_Tab.PP_KH().POCZ,
                   _Tab.PP_KH().MIASTO,_Tab.PP_KH().UL) + ' ' +
                   exec('adr','faktury_wydruk',_Tab.PP_KH().UL,,,_Tab.PP_KH().MIASTO,_Tab.PP_KH().POCZ);
     _Tab.PP_ID:=KH.NIP
   ?}
|? _afld='NST_DATA' & _Tab.NST_DATA=date(0,0,0) || _Tab.NST_KM:=_Tab.NST_GODZ:=''
|? _afld='UDS'
||
   {? _Tab.UDS='N' || _Tab.UDS_OPIS:='' |? _Tab.UDS='T' || _Tab.USL_TUR:='N' ?};
   exec('polafaks_set_efld_opt','jpk_log')


|? _afld='USL_TUR' || {? _Tab.USL_TUR='T' || _Tab.UDS:='N'; _Tab.UDS_OPIS:='' ?}
|? _afld='NST_RODZ'
||
   {? _val='' || _Tab.NST_POJ:=null(); _Tab.NST_DATA:=date(0,0,0); _Tab.NST_KM:=_Tab.NST_GODZ:='' ?};
   exec('polafaks_set_efld_opt','jpk_log')
?};
1


\polafaks_fld_bv
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Przed wyświetleniem pól zmiennej POLAFAKS
::----------------------------------------------------------------------------------------------------------------------
exec('polafaks_set_efld_opt','jpk_log');
~~


\polafaks_set_efld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Ustawia opcje wyświetlania pól
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_afld:=cur_afld();
:: DRO_TODO_AWI: (KSeF):   : redakcja dodatkowych pól w oknie redakcji dokumentu sprzedaży
_Tab:={? var_pres('TAB2POLA')>0 || TAB2POLA || cur_tfld() ?};
VAR_DEL.delete('TAB2FAKSPOLA');
TAB2FAKSPOLA:=cur_tab();
_Tab1:=TAB2FAKSPOLA;
_win_red:=_Tab1.win_edit('?');
{? _afld='ZW' | _afld='PZW_PRZE' | _afld='PZW_DUE' | _afld='PZW_INNA'
|| _Tab1.efld_opt(_win_red,'enable='+$(_Tab.ZW='T'),_Tab,'PZW_PRZE');
   _Tab1.efld_opt(_win_red,'mark='+$(+_Tab.PZW_PRZE | _Tab.PZW_PRZE+_Tab.PZW_DUE+_Tab.PZW_INNA=''),_Tab,'PZW_PRZE');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.ZW='T'),_Tab,'PZW_DUE');
   _Tab1.efld_opt(_win_red,'mark='+$(+_Tab.PZW_DUE | _Tab.PZW_PRZE+_Tab.PZW_DUE+_Tab.PZW_INNA=''),_Tab,'PZW_DUE');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.ZW='T'),_Tab,'PZW_INNA');
   _Tab1.efld_opt(_win_red,'mark='+$(+_Tab.PZW_INNA | _Tab.PZW_PRZE+_Tab.PZW_DUE+_Tab.PZW_INNA=''),_Tab,'PZW_INNA')
|? _afld='EGZ'
|| _Tab1.efld_opt(_win_red,'enable='+$(_Tab.EGZ='T'),_Tab,'EGZ_KH');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.EGZ='T'),_Tab,'EGZ_NAZ');
   _Tab1.efld_opt(_win_red,'mark='+$(_Tab.EGZ='T'),_Tab,'EGZ_NAZ');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.EGZ='T'),_Tab,'EGZ_ADR');
   _Tab1.efld_opt(_win_red,'mark='+$(_Tab.EGZ='T'),_Tab,'EGZ_ADR')
|? _afld='PP'
|| _Tab1.efld_opt(_win_red,'enable='+$(_Tab.PP='T'),_Tab,'PP_KH');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.PP='T'),_Tab,'PP_NAZ');
   _Tab1.efld_opt(_win_red,'mark='+$(_Tab.PP='T'),_Tab,'PP_NAZ');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.PP='T'),_Tab,'PP_ADR');
   _Tab1.efld_opt(_win_red,'mark='+$(_Tab.PP='T'),_Tab,'PP_ADR');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.PP='T'),_Tab,'PP_ID');
   _Tab1.efld_opt(_win_red,'mark='+$(_Tab.PP='T'),_Tab,'PP_ID')
|? _afld='NST_RODZ' | _afld='NST_DATA' | _afld='NST_KM' | _afld='NST_GODZ'
|| _Tab1.efld_opt(_win_red,'enable='+$(_Tab.NST_RODZ<>''),_Tab,'NST_POJ');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.NST_RODZ<>''),_Tab,'NST_DATA');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.NST_DATA<>date(0,0,0) &
                 _Tab.NST_RODZ=exec('nst_ladowy','jpk_log')),_Tab,'NST_KM');
   _Tab1.efld_opt(_win_red,'mark='+$(_Tab.NST_DATA<>date(0,0,0) &
                 _Tab.NST_RODZ=exec('nst_ladowy','jpk_log')),_Tab,'NST_KM');
   _Tab1.efld_opt(_win_red,'enable='+$(_Tab.NST_DATA<>date(0,0,0) &
                 (_Tab.NST_RODZ=exec('nst_plywajacy','jpk_log') |
                 _Tab.NST_RODZ=exec('nst_powietrzny','jpk_log'))),_Tab,'NST_GODZ');
   _Tab1.efld_opt(_win_red,'mark='+$(_Tab.NST_DATA<>date(0,0,0) &
                 (_Tab.NST_RODZ=exec('nst_plywajacy','jpk_log') |
                 _Tab.NST_RODZ=exec('nst_powietrzny','jpk_log'))),_Tab,'NST_GODZ');
   {? var_pres('NST_WBST',_Tab)
   || _Tab1.efld_opt(_win_red,'enable='+$(_Tab.NST_RODZ<>''),_Tab,'NST_WBST')
   ?}
|? _afld='UDS'
|| _Tab1.efld_opt(_win_red,'enable='+$(_Tab.UDS='T'),_Tab,'UDS_OPIS');
   _Tab1.efld_opt(_win_red,'mark='+$(_Tab.UDS='T'),_Tab,'UDS_OPIS')
?}


\jpk_fa_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: JPK_FA - Kontrola zaakceptowania i zaksięgowania dokumentów
::   WE:  _a  - Data od
::        _b  - Data do
::        _c  - 'S'-Dokumenty sprzedaży, 'Z'-Dokumenty zakupu, ''-wszystkie
::        _d  - 0-zakończyć sprawdzanie po napotkaniu pierwszego przypadku, 1-wszystkie przypadki
::       [_e] - tabela na komunikaty
::   WY: uchwyt do tabeli komunikatów
::----------------------------------------------------------------------------------------------------------------------
_od:={? var_press('_a')=type_of(date()) || _a || date(0,0,0) ?};
_do:={? var_press('_b')=type_of(date()) || _b || date(0,0,0) ?};
_sz:={? var_press('_c')=type_of('') || _c || '' ?};
_all:={? var_press('_d')=type_of(0) || _d || 0 ?};
_Kom:={? var_press('_e')=type_of(SYSLOG) || _e || exec('tabKom','jpk') ?};
_ndx:=FAKS.ndx_tmp(,1
   ,'AKC',,
   ,'ZAK',,
   ,'SZ',,);
_cont:=1;
ODDZ.cntx_psh();
ODDZ.index('KOD2');
ODDZ.prefix();
_loop:=ODDZ.first();
{!
|? _loop
|!
   _oddz:=ODDZ.KOD;
   OKR.cntx_psh();
   OKR.index('MC');
   OKR.prefix(REF.FIRMA,1);
   _loop:=OKR.first();
   {!
   |? _loop
   |!
      {? _od~1<=OKR.ROK & OKR.ROK<=_do~1
      || FAKS.cntx_psh();
         FAKS.use((FAKS.name()-3)+_oddz+($OKR.ROK+2));
         FAKS.index(_ndx);
         {! _ii:=1..2
         |!
            FAKS.prefix({? _ii=1 || 'N' || 'T' ?},'N',_sz);
            _loop:=FAKS.first();
            {!
            |? _loop
            |!
               {? FAKS.KZ='' & _od<=FAKS.DW & FAKS.DW<=_do & FAKS.T().JPK='T'
               || _Kom.REF:=$FAKS.ref();
                  {? _ii=1
                  || _Kom.O:='Niezaakceptowany dokument: '@+FAKS.SYM
                  || _Kom.O:='Niezadekretowany dokument: '@+FAKS.SYM
                  ?};
                  {? _Kom.add() || _cont:=_all ?}
               ?};
               _loop:=_cont & FAKS.next()
            !}
         !};
         FAKS.cntx_pop()
      ?};
      _loop:=_cont & OKR.next()
   !};
   OKR.cntx_pop();
   _loop:=ODDZ.next()
!};
ODDZ.cntx_pop();
FAKS.ndx_drop(_ndx);

_Kom


\jpk_mag_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: JPK_MAG - Kontrola zaksięgowania dokumentów
::   WE:  _a  - MG.SYM
::        _b  - Data od
::        _c  - Data do
::        _d  - 0-zakończyć sprawdzanie po napotkaniu pierwszego przypadku, 1-wszystkie przypadki
::       [_e] - tabela komunikatow
::   WY: uchwyt do tabeli komunikatów
::----------------------------------------------------------------------------------------------------------------------
_mgsym:={? var_press('_a')=type_of('') || _a || '' ?};
_od:={? var_press('_b')=type_of(date()) || _b || date(0,0,0) ?};
_do:={? var_press('_c')=type_of(date()) || _c || date(0,0,0) ?};
_all:={? var_press('_d')=type_of(0) || _d || 0 ?};
_Kom:={? var_press('_e')=type_of(SYSLOG) || _e || exec('tabKom','jpk') ?};

MG.cntx_psh();
MG.index('MAGAZYNY');
MG.prefix(_mgsym,_mgsym);
_mgref:={? MG.first() || MG.ref() || null() ?};
MG.cntx_pop();
_cont:=1;

MG.cntx_psh();
MG.prefix();
{? _mgsym='' & MG.first() | _mgsym<>'' & MG.seek(_mgref)
|| _ndx:=ND.ndx_tmp(,1
      ,'MAG',,
      ,'Z',,
      ,'ZAK',,);
   {!
   |? _oddz:=MG.ODDZ;
      OKR.cntx_psh();
      OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      _loop:=OKR.first();
      {!
      |? _loop
      |!
         {? _od~1<=OKR.ROK & OKR.ROK<=_do~1
         || ND.cntx_psh();
            ND.use((ND.name()-3)+_oddz+($OKR.ROK+2));
            ND.index(_ndx);
            {! _ii:=1..2
            |!
               ND.prefix(MG.ref(),{? _ii || 'N' || 'T' ?},'N');
               _loop:=ND.first();
               {!
               |? _loop
               |!
                  {? _od<=ND.D & ND.D<=_do & ND.TYP().JPK<>''
                  || _Kom.REF:=$ND.ref();
                     {? _ii
                     || _Kom.O:='Niezaakceptowany dokument: '@+ND.SYM
                     || _Kom.O:='Niezadekretowany dokument: '@+ND.SYM
                     ?};
                     {? _Kom.add() || _cont:=_all ?}
                  ?};
                  _loop:=_cont & ND.next()
               !}
            !};
            ND.cntx_pop()
         ?};
         _loop:=_cont & OKR.next()
      !};
      OKR.cntx_pop();
      _mgsym='' & MG.next()
   !};
   ND.ndx_drop(_ndx)
?};
MG.cntx_pop();

_Kom


\set_nag
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Ustawia bufor dla naglowka dokumentu magazynowego
::   WE: _a - czy ustawiac tablice danych? 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
_sys:={? +DOK.ZAR & DOK.ZAR*':' || (DOK.ZAR*':'-1)+DOK.ZAR || '' ?};
{? _sys='LMG'
|| {? 1+DOK.DOKZRODL='M'
   || _reffak:=#(4-DOK.DOKZRODL);
      ND.use('nagdo'+(3+(1-DOK.DOKZRODL)));
      ND.prefix();
      {? ND.seek(_reffak,ND.name)
      || {? _a
         || NrJPK+=1;
            echo('JPK_MAG - '+JPK_MAG+' - '+OKRO_F.NAZ+' '+ROK_F.NAZ+' - Dokument: '+$NrJPK);
            _znak:=
               {? DokTyp='PZ'
               || {? ND.TYP().P='N' || -1 || 1 ?}
               || {? ND.TYP().P='T' || -1 || 1 ?}
               ?};
            Suma+=_znak*ND.WAR;
            MagNag[1]:=ND.SYM;
            MagNag[2]:=STR.gsub($ND.D,'/','-');
            MagNag[3]:=form(_znak*ND.WAR,,2,'9.');
            MagNag[4]:=STR.gsub($ND.D,'/','-');
            _continue:=1;
            ND.cntx_psh();
            _nd:=ND.ref();
            _dkor:=ND.TYP().DK='T';
            {? _dkor
::          Dokument korygujący - dostawcę pobieramy z dokumentu korygowanego
            || DK.cntx_psh();
               DK.use((DK.name()-3)+(ND.name()+3));
               DK.index('DOKMAG');
               DK.prefix(ND.ref());
               {? DK.first()
               || _continue:=0;
                  _ref:=DK.REF_KOR;
                  DK.cntx_psh();
                  DK.use(8+_ref);
                  DK.prefix();
                  {? DK.seek(BB.sqlint(_ref),)
                  || ND.use(ref_name(DK.N));
                     ND.prefix();
                     _continue:=ND.seek(DK.N);
                     {? _continue || _nd:=ND.ref() ?}
                  ?};
                  DK.cntx_pop()
               ?};
               DK.cntx_pop()
            ?};
            MagNag[5]:=
               {? _continue
               || {? ND.DOST_ODB='' || exec('poland_save','jpk_log',1) ?};
                  ND.DOST_ODB
               || ''
               ?};
            ND.cntx_pop();
::          Tylko dla PZ, WZ
            MagNag[6]:='';
            MagNag[7]:='';
            {? DokTyp='PZ'
            || DK.cntx_psh();
               DK.use((DK.name()-3)+(ND.name()+3));
               FAP2DK.cntx_psh();
               FAP2DK.use((FAP2DK.name()-1)+ND.ODDZ);
               _tab_pow:=exec('faks_pow','faktury_wspolne',1,_nd,'');
               exec('faks_pow','faktury_wspolne',2,_nd,'',_tab_pow);
               FAP2DK.cntx_pop();
               DK.cntx_pop();
               _tab_pow.prefix();
               {? _tab_pow.first() & _tab_pow.next()=0
               || BEER.SZ:='Z';
                  FAKS.cntx_psh();
                  _ref:=_tab_pow.FAKS_REF;
                  FAKS.use(8+_ref);
                  FAKS.prefix();
                  {? FAKS.seek(BB.sqlint(_ref),) & FAKS.AKC='T'
                  || {? _dkor & FAKS.KORYG='T'
                     || exec('szuk_kor','faktury_nag',$FAKS.ref(),FAKS.D);
                        {? X_Xa.first() & X_Xa.next()=0
                        || _ref:=X_Xa.REF;
                           FAKS.use(8+_ref);
                           {? FAKS.seek(BB.sqlint(_ref),) & FAKS.AKC='T'
                           || MagNag[6]:=FAKS.ID;
                              MagNag[7]:=STR.gsub($FAKS.DW,'/','-')
                           ?}
                        ?}
                     || MagNag[6]:=FAKS.ID;
                        MagNag[7]:=STR.gsub($FAKS.DW,'/','-')
                     ?}
                  ?};
                  FAKS.cntx_pop()
               ?}
            |? DokTyp='WZ' & ND.FAKS
            || FAKS.use(ref_name(ND.FAKS));
               FAKS.prefix();
               MagNag[6]:=ND.FAKS().SYM;
               MagNag[7]:={? ND.FAKS().SYM<>'' || STR.gsub($FAKS.DW,'/','-') || '' ?}
            ?}
         ?};
         1
      ?}
   ?}
?}


\dk_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.00]
:: OPIS: Zwraca wartość pola dla pozycji dokumentu magazynowego
::   WE: _a - pole
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _a='MX'
|| {? DK.MX='' || DK.MX:=DK.M().KTM; DK.put() ?};
   DK.MX
|? _a='NX'
|| {? DK.NX='' || DK.NX:=DK.M().N; DK.put() ?};
   DK.NX
?}


\td_typjpk_f3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: F3 TYPYDOK.TYP_JPK
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_Tab:=tab_tmp(1
   ,'TYP'   ,'STRING[8]'   ,'Typ JPK'
   ,'O'     ,'STRING[50]'  ,'Opis');
_Tab.TYP:=''; _Tab.O:='Nie dotyczy'; _Tab.add();
_Tab.TYP:='PZ'; _Tab.O:='Przyjęcie z zewnątrz'; _Tab.add();
_Tab.TYP:='WZ'; _Tab.O:='Wydanie na zewnątrz'; _Tab.add();
_Tab.TYP:='RW'; _Tab.O:='Rozchód wewnętrzny'; _Tab.add();
_Tab.TYP:='MM'; _Tab.O:='Przesunięcie międzymagazynowe'; _Tab.add();

_win_wer:=_Tab.mk_sel('Typy JPK'@,'P',0,'typ_jpk',,,,,'U');
_Tab.win_fld(_win_wer,,'TYP',,,10,,,'Typ');
_Tab.win_fld(_win_wer,,'O',,,30,,,'Opis');
_Tab.win_act(_win_wer,,'Formuła','Wybierz'@@,,,"sel_exit()",,1);
_Tab.win_sel(_win_wer);

_Tab.find_key(fld());

{? _Tab.select(,1,5)
|| TYPYDOK.JPK:=_Tab.TYP
?};

~~


\td_typjpk_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Po redakcji TYPYDOK.TYP_JPK
::   WE:
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_fld:=fld();

{? _fld<>'' & _fld<>'PZ' & _fld<>'WZ' & _fld<>'RW' & _fld<>'MM'
|| FUN.info('Niedozwolona wartość.'@);
   0
|| 1
?}


\kor_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Lista korygowanych dokumentów w ramach korekty zbiorczej _a
::   WE: _a - FAKS.ref() korekty zbiorczej
::   WY: lista symboli korygowanych dokumentów
::----------------------------------------------------------------------------------------------------------------------
_wyn:=_sep:='';
_kz_uid:=exec('FindAndGet','#table',FAKS,_a,,"FAKS.uidref()",'');
FAKS_KZF.cntx_psh();
FAKS_KZF.index('KZ_UID');
FAKS_KZF.prefix(_kz_uid);

_loop:=FAKS_KZF.first();
{!
|? _loop
|!
   _wyn+=_sep+FAKS_KZF.FAK_SYM;
   _sep:=', ';
   _loop:=FAKS_KZF.next()
!};
FAKS_KZF.cntx_pop();
_wyn


\poland_save
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Zapis dodatkowych pól tabeli ND
::   WE: _a - 0 - bez put, 1 - put
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ND.KH
|| ND.DOST_ODB:=ND.KH().NAZ_P;
   {? _a || ND.put() ?}
?}


\oss_default_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Domyślna procedura
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('FindInSet','#table','JPK_SLO','KOD',exec('get','#params',300603,2),1,,1,'PROC',null())


\nst_ladowy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Nowy środek transportu - Lądowy
::----------------------------------------------------------------------------------------------------------------------
'Lądowy'


\nst_plywajacy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Nowy środek transportu - Pływający
::----------------------------------------------------------------------------------------------------------------------
'Pływający'


\nst_powietrzny
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [21.37]
:: OPIS: Nowy środek transportu - Powietrzny
::----------------------------------------------------------------------------------------------------------------------
'Powietrzny'


\polafaks_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizuje pola tabeli FAKSPOLA automatycznie uzupełniane
::   WE: _a - 0/1 - FAKSPOLA.put()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_put:={? var_pres('_a')=type_of(0) || _a || 0 ?};
{? FAKS.SZ='S' & (FAKSPOLA.OO<>FAKS.NDVAT | FAKSPOLA.TT<>FAKS.DO_UE)
||
   FAKSPOLA.OO:=exec('oo','pola');
   FAKSPOLA.TT:=FAKS.DO_UE;
   _put:=1
?};
{? FAKS.WHERE='K'
||
   FAKSPOLA.OKRES_OD:=FAKS.KZ_OD;
   FAKSPOLA.OKRES_DO:=FAKS.KZ_DO;
   _put:=1

|? FAKS.FPACZKA
||
   FAKSPOLA.OKRES_OD:=FAKS.FPACZKA().OD;
   FAKSPOLA.OKRES_DO:=FAKS.FPACZKA().DO;
   _put:=1
?};
{? _put || FAKSPOLA.put() ?}

:Sign Version 2.0 jowisz:1045 2023/10/06 11:13:45 5af5361f1962b6450f09678150cc40ede8a8b6c7c28875b7588911ca30b725c68d5a1682863387bd276b71a3bcdc44512d2e86afa199b3085289ff768434831170b4718b8c1e954647e8f85840dae74cb0bef7426d024e89ac76443b45772fc8c0b4bab0a5ada56c809164a81145e5a194b6acdd2ed187fa47986545ca92f47c

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzezone
::======================================================================================================================
:: Nazwa pliku: buffermk.fml
:: Utworzony: 11.07.2019
:: Autor: WH
::======================================================================================================================
:: Zawartość: Formuły obsługujące mechanizm zamiany elementów na reprezentację tekstową.
:: UWAGA: To jest narzedzie, do wywolania recznie przez programiste/wdrozeniowca po zmianie struktury tabel
::        lub do zwiekszenia funkcjonalnosci.
::        Wywolanie formul z tego pliku powinno odbywac sie _recznie_ z Administracja -> Formula
::        np.: exec('make_strings','to_string_mk','#to_string_auto.fml')
::======================================================================================================================


\make_strings
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Generator formuł typu to_string
::   WE: [_a] - STRING - nazwa pliku .fml do którego generować
::   WY: 0 - porażka
::       1 - sukces
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_fml_file:='#to_string_auto.fml';
{? var_pres('_a')=type_of('')
|| _fml_file:=_a
?};

:: Parametryzacja szukajki pól
_levels:=obj_new(3);
_levels[1]:='SYMBOL,SYM,KOD,NR,K,S,TYP,CODE';
_levels[2]:='NAZ,NAZWA,NAME,NAM,NA,OPIS,OP,DESC,N,O';
_levels[3]:='VER,WER';

:: Wzorce formuł
_patterns:=obj_new(3);
_patterns[1]:='%1';
_patterns[2]:='%1 - %2';
_patterns[3]:='%1 - %2 (%3)';

:: Jakie typy umożliwiamy - (konwersja na STRING będzie robiona za pomocą $)
_type_allow:='STRING,INTEGER,REAL';

_tables:=tab_tmp(1
                ,'ACRONYM','STRING[8]','Akronim'
                ,'NAME'   ,'STRING[8]','Maska'
                ,'COMMENT','STRING[60]','Komentarz'
                ,'KIND'   ,'STRING[1]','Tabela czy zmienna'
                ,'NR'     ,'INTEGER','Nr'
                );
_fields:=tab_tmp(3
                ,'TABLE','STRING[8]','Akronim tabeli'
                ,'ACRONYM','STRING[8]','Akronim'
                ,'TYPE'   ,'STRING[20]','Typ pola'
                ,'NAME'   ,'STRING[8]','Nazwa'
                ,'COMMENT','STRING[60]','Komentarz'

                ,'NR'     ,'INTEGER','Nr'
                );

_type:=exec('mbFieldsType','#table');

{! _tt:=1..tab_num()
|! _tables.blank();
   _acr:=tab_acr(_tt);
   _tables.NR:=_tt;
   _tables.ACRONYM:=_acr;
   _tables.NAME:=($_acr)().name(1);
   _tables.COMMENT:=($_acr)().comment();
   _tables.KIND:={? tab_real(_tt) || 'T' || 'Z' ?};
   {? _tables.add()
   ||
      {? var_pres('_table')>100
      || obj_del(_table)
      ?};
      _table:=($_acr)();
      _fld_num:=_table.fld_num();

      {! _ff:=1.._fld_num
      |!
         _fields.blank();
         _fields.TABLE:=_acr;
         _fields.NR:=_ff;
         _fields.ACRONYM:=_table.fld_acr(_ff);
         _fields.NAME:=_table.fld_name(_ff);
         _fields.COMMENT:=_table.fld_comm(_ff);
         _fields.TYPE:=_table.fld_join(_ff);

         {? _fields.TYPE<>''
         ||
            {? _fields.TYPE='MEMO' | _fields.TYPE='SYSMEMO'
            || ~~
            || _fields.TYPE:='_'+_fields.TYPE
            ?}
         |? _fields.TYPE=''
         || _type_of:=var_pres(_fields.ACRONYM,_table,'diff_blob');
            _fields.TYPE:=_type.name[_type_of]
         ?};
         _fields.add()
      !}
   ?}
!};

::exec('select','#table',_tables);
::exec('select','#table',_fields);

_ndx1:=_tables.ndx_tmp('',1,'KIND',,0, 'ACRONYM',,0);
_tables.index(_ndx1);
_tables.prefix('T',);

_can_continue:=1;

_maxstrB:=524172;
_rules:='';

_header:=
':!UTF-8\r\n'+
':: (c) Macrologic S.A. Wszelkie prawa zastrzeżone\r\n'+
'::======================================================================================================================\r\n'+
':: Nazwa pliku: '+_fml_file+'\r\n'+
':: Autor: <wygenerowany automatycznie>\r\n'+
'::======================================================================================================================\r\n'+
':: Zawartość: Obiekty buforowe tabel - PLIK GENEROWANY AUTOMATYCZNIE - przy ponownej generacji zmiany zostaną nadpisane!\r\n'+
'::            W celu odświeżenia formuł, należy wykonać exec(\'make_strings\',\'to_string_mk\',\'#to_string_auto.fml\')\r\n'+
'::======================================================================================================================\r\n'+
'\r\n';

_rules:=_header;

{? _tables.first()
|| {!
   |?
      {? 3+_tables.COMMENT<>'---'
      ||
::       Sprawdzam czy tabela jest już w zwykłym #to_string
         {? exec('is_fun','#file','#to_string',_tables.ACRONYM)=0
         || _fml:=exec('formula4table','to_string_mk',_tables,_fields,_levels,_patterns,_type_allow);
::         exec('edit_memo','#edit',_fml,'Formuła');
            {? _fml<>''
            ||
               _single:='\\'+_tables.ACRONYM+'\r\n';
               _single+=exec('comment','to_string_mk',_tables);
               _single+=_fml;
               _fml_size:=exec('get_str_size','#string',_rules+_single);
               {? _fml_size>_maxstrB
               || _can_continue:=0;
                  FUN.emsg('Przekroczono maksymalny rozmiar pliku.'@)
               || _rules+=_single;
                  _rules+='\r\n\r\n'
               ?}
            ?};
            ~~
         ?}
      ?};
      _tables.next() & _can_continue>0
   !}
?};

{? _rules<>''
|| _file:=fopen(_fml_file,'uw',1);
   {? _file>0
   || fwrite(_file,_rules);
      _can_continue:=1;
      fclose(_file)
   || _can_continue:=0;
      FUN.emsg('Nie udało się utworzyć pliku: %1'@[_fml_file])
   ?}
?};
_tables.ndx_drop(_ndx1);

{? _can_continue>0
|| _result:=1
?};
_result


\formula4table
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Generuje formułę to_string dla tabeli
::   WE: _a - tab_tmp - tabela tymczasowa zawierająca tabele
::       _b - tab_tmp - tabela tymczasowa zawierająca pola
::       _c - obj_new - poziomy szukania pól
::       _d - obj_new - wzorce formuł
::       _e - STRING - jakie typy pozwalamy
::   WY: STRING - wygenerowana formuła
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_tables:=_a;
_fields:=_b;
_levels:=_c;
_patterns:=_d;
_type_allow:=_e;

_result:='';
_fields.cntx_psh();
_fields.prefix(_tables.ACRONYM,);

_can_continue:=1;

:: Szukam ile poziomów będzie
_lev_num:=0;
{? _fields.first()
|| {!
   |?
      {! _it:=1..obj_len(_levels)
      |!
         _can_continue:=1;
         {? var_pres('_split')>100
         || obj_del(_split)
         ?};
         _split:=spli_str(_levels[_it],',');
         {! _it2:=1..obj_len(_split)
         |? _can_continue>0
         |! _fld:=_split[_it2];

            _fields.prefix(_tables.ACRONYM,_fld,);
            {? _fields.first()
            ||
               {? _type_allow*_fields.TYPE>0
               || _lev_num+=1;
                  _can_continue:=0
               ?}
            ?}
         !}
      !};
      _fields.next()
   !}
?};

{? _lev_num>0
||
   _patt:=_patterns[_lev_num];
   _fields.prefix(_tables.ACRONYM,);
   {? _fields.first()
   || {!
      |?
         {! _it:=1..obj_len(_levels)
         |!
            _can_continue:=1;
            {? var_pres('_split')>100
            || obj_del(_split)
            ?};
            _split:=spli_str(_levels[_it],',');
            {! _it2:=1..obj_len(_split)
            |? _can_continue>0
            |! _fld:=_split[_it2];

               _fields.prefix(_tables.ACRONYM,_fld,);
               {? _fields.first()
               ||
                  {? _type_allow*_fields.TYPE>0
                  ||
                     _rule:=_tables.ACRONYM+'.'+_fields.ACRONYM;

                     {? _fields.TYPE<>'STRING'
                     || _rule:='$'+_rule
                     ?};

                     {? _result=''
                     || _result:='\''+_patt+'\'['+_rule
                     || _result+=','+_rule
                     ?};
                     _can_continue:=0
                  ?}
               ?}
            !}
         !};
         _fields.next()
      !};
      {? _result<>''
      || _result+=']'
      ?}
   ?}
?};
_fields.cntx_pop();
_result


\comment
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [19.42]
:: OPIS: Zwraca komentarz
::   WE: _a - tab_tmp - tabela tymczasowa zawierająca tabele
::   WY:
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
_tables:=_a;

_result:='::'+('-'*118)+'\r\n';
_result+='::  UTW: [AUTO]\r\n';
_result+=':: OPIS: Opis tabeli %1.\r\n'[_tables.ACRONYM];
_result+='::   WY: STRING\r\n';
_result+='::'+('-'*118)+'\r\n';
_result

:Sign Version 2.0 jowisz:1048 2023/06/23 14:14:35 2f9bce6e9b8a09c8af08331f5118c074f3c69aad852f408f52c08ba3b98cffc25b70d260ee881826345f91458b0092f2b58ea474790552aacfb6499b307b32d41aa837d43823d4520d84408b2a8cbc8b15a4351c98817441abceb0e60c7cb806bb73187639db28bc9daf5708a01ef9ab62c2c4f5b534f6a7b1185bac52fbb9db

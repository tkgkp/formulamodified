:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: portal_method_seod.fml
:: Utworzony: 02.02.2022
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły obsługujące metody dostępu do HR Portal (patrz opis w portal_method.fml).
::======================================================================================================================


\cseod_AcceptHeaderModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Parametry zapytania dla cseod_AcceptOperatorModify
::  OLD: \cseod_AcceptHeaderModify_args/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
_formula_json:="params_exec('cseod_AcceptHeaderModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core','PORTAL_EDOKUM_PTH_ID',_formula_json)


\cseod_AcceptHeaderModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Parametry zapytania dla cseod_AcceptOperatorModify
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::  OLD: \cseod_AcceptHeaderModify_json/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;
_ext:=_b;

_rows:=2;
_cols:=4;
_caption:=obj_new(_rows);
_value:=obj_new(_rows);
{! _row:=1 .. _rows
|! _caption[_row]:=obj_new(_cols);
   _value[_row]:=obj_new(_cols);
   {! _col:=1 .. _cols
   |! _caption[_row][_col]:=~~;
      _value[_row][_col]:=~~
   !}
!};

_name:=EDOKUM.name();
{? _name=exec('nwu_edokum_mask','portal_method_chr')
:: Wnioski urlopowe
|| _sync_id:='PORTAL_EDOKUM_NWU_ID';
   _type:='Wniosek urlopowy';
   _src_nr:='Wniosek %1' [EDOKUM.ID];
   _src_desc:='Wniosek urlopowy';

   P.cntx_psh();
   P.prefix();
   {? +EDOKUM.OSOBAWWW=16 & type_of(ref_tab(EDOKUM.OSOBAWWW))>0 & ref_tab(EDOKUM.OSOBAWWW)=P & P.seek(EDOKUM.OSOBAWWW)
   || OSOBA.cntx_psh();
      OSOBA.prefix();
      P.OSOBA();
      STN.cntx_psh();
      STN.prefix();
      P.ST();
      _caption[1][1]:='Wnioskodawca';
      _value[1][1]:='%1 %2' [OSOBA.PIERWSZE,OSOBA.NAZWISKO];
      _caption[1][2]:='Stanowisko';
      _value[1][2]:=STN.ST;
      STN.cntx_pop();
      OSOBA.cntx_pop()
   ?};
   P.cntx_pop();
   NWU.cntx_psh();
   NWU.prefix();
   _nwu:=exec('edokum2nwu','portal_nieobecnosci',EDOKUM.ref());
   {? _nwu<>null() & NWU.seek(_nwu)
   || R.cntx_psh();
      _src_nr:=NWU.R().RT;
      R.cntx_pop();
      {? NWU.OD=NWU.DO
      || _caption[2][1]:='Wnioskowany dzień';
         _value[2][1]:=$NWU.OD
      || _caption[2][1]:='Wnioskowany okres';
         _value[2][1]:='%1 - %2' [$NWU.OD,$NWU.DO]
      ?};
      _caption[2][2]:='Czas trwania';
      _value[2][2]:=
         {? NWU.PARTDAY='T'
         || _jm:=
               {? NWU.G=1
               || 'godzina'
               |? '234'*(($NWU.G)+1)
               || 'godziny'
               || 'godzin'
               ?};
            '%1 %2' [$NWU.G,_jm]
         || '%1 %2' [$NWU.NR,{? NWU.NR=1 || 'dzień' || 'dni' ?}]
         ?}
   ?};
   NWU.cntx_pop()
|? 6+_name='skid_v'
:: Wnioski personelowe, faktury w obiegu lub delegacje
|| _wniosek:=exec('EDOKUM_typ','portal_seod','W');
   _faktura:=exec('EDOKUM_typ','portal_seod','F');
   ETYPY.cntx_psh();
   ETYPY.prefix();
   {? _wniosek
   || _sync_id:='PORTAL_EDOKUM_ID';
      _tabRodz:=exec('etypy_rodz_wp_tab','obiegi2');
      _type:={? _tabRodz.find_tab(,'KOD',,'=',EDOKUM.TYP().RODZ_WP) || _tabRodz.AKC_TYPE || 'Wniosek pracownika' ?};
      _src_nr:=EDOKUM.TYP().NAZWA;
      _src_desc:=EDOKUM.TYP().NAZWA;
      exec('EDOKUM_details','portal_wnioski',_caption,_value)
   |? _faktura
   || _sync_id:='PORTAL_SEOD_EDOK_ID';
      _type:={? EDOKUM.TYP().TYPVATPR=0 || 'Faktura' || 'Rachunek' ?};
      _src_nr:=EDOKUM.SYM;
      _src_desc:=_sub_type:=EDOKUM.TYP().NAZWA;
      exec('EDOKUM_details','portal_seod',_caption,_value,1)
   || _sync_id:='PORTAL_SEOD_EDOK_DEL_ID';
      _type:=EDOKUM.TYP().ID_WP+4;
      _src_nr:=EDOKUM.ID;
      _src_desc:=_sub_type:=EDOKUM.TYP().NAZWA;
      exec('EDOKUM_details','portal_seod',_caption,_value,3)
   ?};
   ETYPY.cntx_pop()
|| _sync_id:=''
?};

{? _sync_id=''
|| return(0)
?};

_tab_id:=exec('get_id','portal_core',_sync_id,EDOKUM.uidref());
{? var_pres('_tab_id')<=0 | ~_tab_id.first()
|| return(0)
?};

params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;

OSOBA.cntx_psh();
OSOBA.prefix();
EDOKUM.DOSTAWCA();

_obj.create();

_obj.set('__id_cloud',{? _id || _id || ~~ ?});
_obj.set('AcceptanceType',_type);
{? var_pres('_sub_type')=type_of('') || _obj.set('AcceptanceSubType',_sub_type) ?};
_obj.set('SourceNumber',_src_nr);
_obj.set('SourceId',#_tab_id.ID);
{? EDOKUM.TYP().IN_POR
|| _obj.set('SourceLink',exec('EDOKUM_link','portal_interm'))
|? -ETYPY.W_PORTAL='s'
|| _tw:=PAR_SKID.get(466);
   {? +_tw<>2 || _tw:='BN' ?};
   {? _faktura
   || _obj.set('SourceValue',{? 1+_tw='B' || EDOKUM.WART || EDOKUM.NETTO ?})
   || _obj.set('SourceValue',{? _tw+1='B' || EDOKUM.WART || EDOKUM.NETTO ?})
   ?};
   _obj.set('SourceValueQuantity',{? EDOKUM.WAL || EDOKUM.WAL().KOD || 'PLN' ?})
?};
_obj.set('SourceDescription',_src_desc);
_obj.set('SourceRequestPerson','%1 %2' [OSOBA.PIERWSZE,OSOBA.NAZWISKO]);
{! _row:=1 .. _rows
|! {! _col:=1 .. _cols
   |! {? _value[_row][_col]<>~~
      || {? _caption[_row][_col]<>~~
         || _obj.set('Caption_%1_%2' [$_row,$_col],_caption[_row][_col]);
            _obj.set('GridCaption_%1_%2' [$_row,$_col],_caption[_row][_col])
         ?};
         _obj.set('Value_%1_%2' [$_row,$_col],_value[_row][_col])
      ?}
   !}
!};
_obj.set('__ModifiedDate_erp',EDOKUM.idput_value());
_obj.set('__id_erp',EDOKUM.uidref());
_obj.set('__JSON',exec('EDOKUM_akc_json','portal_wnioski'));
_obj.set('SourceId_id_erp',EDOKUM.uidref());

_ret:=_obj.save();

OSOBA.cntx_pop();

_ret


\cseod_AcceptHeaderModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Parametry zapytania dla cseod_AcceptHeaderModify
::  OLD: \cseod_AcceptHeaderModify_parse/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
params_exec('Modify_parse','portal_core','PORTAL_EDOKUM_PTH_ID')


\cseod_AcceptHeaderDelete_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Parametr zapytania cseod_AcceptanceHeaderDelete.
::   WY: JSON
::  OLD: \cseod_AcceptHeaderDelete_args/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_uidref:=_env.Param;
_id:='PORTAL_EDOKUM_AKC_ID';
_tab:=exec('get_id','#sync_id',_id,_uidref);
params_exec('Delete_args','portal_core',_id,_tab)


\cseod_AcceptHeaderDelete_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [21.14]
:: OPIS: Parsowanie odpowiedzi dla cseod_AcceptanceHeaderDelete
::  OLD: \cseod_AcceptHeaderDelete_parse/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
params_exec('Delete_parse','portal_core','PORTAL_EDOKUM_AKC_ID')


\cseod_AcceptOperatorGet_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Parametry zapytania dla cseod_AcceptOperatorGet
::  OLD: \cseod_AcceptOperatorGet_args/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
params_exec('Get_args','portal_core')


\cseod_AcceptOperatorGet_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Przetwarzanie wyniku zapytania cseod_AcceptOperatorGet
::  OLD: \cseod_AcceptOperatorGet_parse/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
params_exec('Get_parse','portal_core','PORTAL_EDOKUM_AKC_ID',
   "exec('cseod_AcceptOperatorGet_update','portal_method_seod',_a,_b)"
)


\cseod_AcceptOperatorGet_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.42]
:: OPIS: Przetwarzanie wyniku zapytania cseod_AcceptOperatorGet
::   WE: _a [OBJECT] - Obiekt klasy JSON_MAN.
::       _b [STRING] - Akcja do wykonania: add/put/del.
::       _c [STRING] - Dodatkowy opis (wyróżnik) rekordu. Dla akcji 'add' - zawsze ''.
::   WY: Identyfikator rekordu lub 0 - dla akcji 'add', '' - dla akcji 'put' i 'del' lub ~~ w przypadku błędu.
::  OLD: \cseod_AcceptOperatorGet_update/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_act:=_b;

{? _act='del' || return('') ?};

_b_prel:=_obj.getValue('AcceptGroup');
_b_prels:=_obj.getValue('AcceptRole');
_action:=_obj.getValue('OrderedAction');

_mod:=_obj.getValue('__ModifiedDate_cloud');
_md:=exec('str2date','#convert',10+_mod);
_mt:=exec('str2time','#convert',8+(11-_mod));

_json_str:=_obj.getValue('__JSON');
_ref:=~~;
_email:=~~;
_usr:=~~;
_reason:=~~;
{? var_pres('_json_str')>0
|| _json:=json_parse(_json_str);
   {? var_pres('AcceptanceHeader',_json)>0
   || {? obj_len(_json.AcceptanceHeader)>0
      || _el:=_json.AcceptanceHeader[1];
         {? var_pres('__id_erp',_el)>0
         || _ref:=_el.__id_erp
         ?};
         &_el
      ?}
   ?};
   {? var_pres('TenantUser',_json)>0
   || {? obj_len(_json.TenantUser)>0
      || _el:=_json.TenantUser[1];
         {? var_pres('LoginName',_el)>0
         || _email:=_el.LoginName
         ?};
         &_el
      ?}
   ?};
   {? var_pres('AcceptActnParam',_json)>0
   || {? obj_len(_json.AcceptActnParam)>0
      || _el:=_json.AcceptActnParam[1];
         {? var_pres('ParamValue',_el)>0
         || _reason:=_el.ParamValue
         ?};
         &_el
      ?}
   ?}
?};
{? var_pres('_email')>0
|| _usr:=exec('get_users','portal_wnioski',_email);
   {? _usr=null
   || _usr:=exec('FindInSet','#table','USERS','USR_MAIL',_email,,,1,,null)
   ?}
?};
{? var_pres('_ref')>0 & var_pres('_email')>0 & var_pres('_usr')
|| EDOKUM.cntx_psh();
   EDOKUM.use(8+(_ref+16));
   EDOKUM.prefix();
   {? EDOKUM.seek(_ref) & exec('EDOKOS_time_ok','portal_wnioski',_md,_mt,_b_prel)
   || _status:={? _action='ACCEPT' || 'T'
               |? _action='REJECT' || 'O'
               |? _action='REVOKE' || 'W'
               |? _action='CLOSE' || 'E'
               || 'T'
               ?};
      {? _status='T' & var_pres('_json')>0
      || exec('EDOKUSER_parse','portal_seod',_b_prel,_b_prels,_json)
      ?};
      {? _action='REVOKE'
      || _edokos_ref:=exec('search_last_act','obiegi2','',_usr);
         EDOKUM.WYCOF_DO:=exec('edokos_action','obiegi2',_edokos_ref);
         EDOKUM.WYCOFAJ:=1+EDOKUM.WYCOF_DO
      ?};
      EDOKOS.cntx_psh();
      EDOKOS.use('skid_y'+(EDOKUM.name()+2));
      EDOKOS.index('PORTAL');
      EDOKOS.prefix(EDOKUM.ref(),'T',{? _status='W' || EDOKUM.B_PREL || _b_prel ?},_usr);
      {? EDOKOS.first()
      || EDOKOS.cntx_psh();
         EDOKOS.prefix();
         {? 'TE'*_status=1
         || EDOKOS.STATUS:='T'
         |? _status='W'
         || EDOKOS.STATUS:='N'
         || EDOKOS.WID:='N'
         ?};
         VAR_DEL.delete('ObeNoT');
         ObeNoT:=1;
         EDOKOS.put();
         VAR_DEL.delete('ObeNoT');
         EDOKOS.cntx_pop()
      ?};
      EDOKOS.prefix(EDOKUM.ref(),'S',_b_prel,_usr);
      {? EDOKOS.first()
      || {? _status<>'W'
         || EDOKOS.STATUS:=_status;
            {? var_pres('_reason')>0
            || EDOKOS.memo_set(_reason,'UW_DL');
               EDOKOS.memo_put(,'UW_DL')
            ?};
            EDOKOS.DATA:=_md;
            EDOKOS.TIME:=_mt;
            EDOKOS.put()
         ?};

         {? 1+_b_prel<>'A' | _status='W'
         || EDOKOS.cntx_psh();
            EDOKOS.index('PORTAL');
            EDOKOS.prefix(EDOKUM.ref(),'S',{? _status='W' || EDOKUM.B_PREL || _b_prel ?},);
            {? EDOKOS.first()
            || {!
               |? EDOKOS.cntx_psh();
                  EDOKOS.prefix();
                  {? EDOKOS.STATUS='N'
                  || EDOKOS.STATUS:={? _status='W' || 'W' || 'x' ?}
                  ?};
                  EDOKOS.B_PREL:='A'+$EDOKOS.OPER_NUM;
                  EDOKOS.DATA:=_md;
                  EDOKOS.TIME:=_mt;
                  EDOKOS.put();
                  EDOKOS.cntx_pop();
                  EDOKOS.first()
               !}
            ?};
            EDOKOS.cntx_pop()
         ?};

         _stat:={? _action='ACCEPT' || 'Y' |? _action='REVOKE' || 'P' |? _action='CLOSE' || 'E' || 'D' ?};
         _akc:=0;
         {? EDOKOS.OPERACJA='Y' & EDOKUM.STATUS<>_stat & (EDOKUM.B_PREL=_b_prel | EDOKUM.WYCOFAJ<>'')
         || exec('add_elog','obiegi_akc',
               {? _action='ACCEPT'
               || 'Akceptacja wniosku'
               |? _action='REVOKE'
               || 'Wycofanie wniosku'
               |? _action='REJECT'
               || 'Odrzucenie wniosku'
               || 'Zamknięcie wniosku'
               ?}+' (HR Portal)',1,_usr,_reason
            );
            exec('EDOKOS_add','portal_wnioski',EDOKUM.ref(),_usr,_stat,_reason);
            EDOKUM.STATUS:=_stat;
            {? EDOKUM.WYCOFAJ='' || _akc:=1 ?};
            {? _action='CLOSE'
            || EDOKUM.ZAM:='T';
               _akc:=0
            ?}
         ?};
         EDOKUM.put(,1);
         {? _akc
         || exec('EDOKUM_akc','portal_wnioski',_usr,EDOKUM.STATUS,_b_prel)
         ?}
      ?};
      EDOKOS.cntx_pop()
   ?};
   EDOKUM.cntx_pop()
?};
_ref


\cseod_CostCenter_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca identyfikator przeznaczenia danych
::   WE: _a - akronim tabeli
::----------------------------------------------------------------------------------------------------------------------
_tab:=_a;
{? _tab='UD_DEF'
|| 'PORTAL_SEOD_UD_DEF_ID'
|| 'PORTAL_KK_ID'
?}


\cseod_CostCenterModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: JSON dla zapytania cseod_CostCenterModify.
::       Kontekst wywołania - rekord tabeli KK lub UD_SKL.
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::  OLD: \cseod_CostCenterModify_json/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;
_ext:=_b;

params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;
_tab:=_env.TabAcr;
_ret:=0;

{? _tab='KK'
|| _firma:=exec('ref_firma','ustawienia');

:: W tym zastosowaniu wystawianie są tylko konta kosztów z systemu PPL dla danej firmy
   {? KK.SYSTEM<>'PPL' | KK.FIRMA<>_firma
   || return(0)
   ?};

   ROK_F.cntx_psh();

   _obj.create();

   _obj.set('__id_cloud',{? _id || _id || ~~ ?});
:: CostCenterType zgodnie z założeniami, MCL - dedykowany typ
   _obj.set('CostCenterType','MCL');
   _obj.set('CostCenter',KK.SYM);
   _obj.set('Description',KK.NAZWA);
   _obj.set('ActiveFrom',KK.ROK_F().POCZ_ROK);
   _obj.set('ActiveTo',KK.ROK_F().KON_ROK);
   _obj.set('__ModifiedDate_erp',KK.idput_value());
   _obj.set('__id_erp',KK.uidref());

   _ret:=_obj.save();

   ROK_F.cntx_pop()

|? _tab='UD_DEF'
|| _obj.create();
   _obj.set('__id_cloud',{? _id || _id || ~~ ?});
:: CostCenterType zgodnie z założeniami, MCL - dedykowany typ
   _obj.set('CostCenterType',exec('SKID_MBP_type','portal_seod',UD_DEF.UD_SCH));
   _obj.set('CostCenter',UD_DEF.SYMBOL);
   _obj.set('Description',UD_DEF.OPIS);
::   _obj.set('ActiveFrom',KK.ROK_F().POCZ_ROK);
::   _obj.set('ActiveTo',KK.ROK_F().KON_ROK);
   _obj.set('__ModifiedDate_erp',UD_DEF.idput_value());
   _obj.set('__id_erp',UD_DEF.uidref());
   _ret:=_obj.save()
?};

_ret


\cseod_CostCenterModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Dodanie/modyfikacja danych konta kosztowego - parametr zapytania cseod_CostCenterModify.
::   WE:
::   WY: JSON
::  OLD: \cseod_CostCenterModify_args/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_id:=exec('cseod_CostCenter_id','portal_method_seod',_env.TabAcr);
_formula_json:="params_exec('cseod_CostCenterModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core',_id,_formula_json)


\cseod_CostCenterModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Parsowanie odpowiedzi dla cseod_CostCenterModify.
::   WE:
::  OLD: \cseod_CostCenterModify_parse/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_id:=exec('cseod_CostCenter_id','portal_method_seod',_env.TabAcr);
params_exec('Modify_parse','portal_core',_id)


\cseod_CostCenterDelete_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Parametr zapytania cseod_CostCenterDelete
::   WE:
::   WY: JSON
::  OLD: \cseod_CostCenterDelete_args/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_id:=exec('cseod_CostCenter_id','portal_method_seod',_env.TabAcr);
params_exec('Delete_args','portal_core',_id)


\cseod_CostCenterDelete_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Parsowanie odpowiedzi dla cseod_CostCenterDelete
::   WE:
::  OLD: \cseod_CostCenterDelete_parse/portal_method.fml
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_id:=exec('cseod_CostCenter_id','portal_method_seod',_env.TabAcr);
params_exec('Delete_parse','portal_core',_id)


\cseod_BCAddresHModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parametr komunikatu cseod_BCAddresHModify.
::----------------------------------------------------------------------------------------------------------------------
_formula_json:="params_exec('cseod_BCAddresHModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core','PORTAL_KH_ID',_formula_json)


\cseod_BCAddresHModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: JSON dla zapytania cseod_BCAddresHModify.
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;

params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;
_obj.create();

_obj.set('__id_cloud',{? _id || _id || ~~ ?});

_obj.set('__ModifiedDate_erp',KH.idput_value());
_obj.set('__id_erp',KH.uidref());


:: "BusinessContactId": <long | null>,
_obj.set('Name',{? KH.NAZ<>'' || KH.NAZ || KH.SKR ?});
_obj.set('FullName',{? KH.NAZ_P<>'' || KH.NAZ_P || KH.SKR ?});
_obj.set('NIP',KH.SNIP);
_obj.set('PESEL',KH.PESEL);
_obj.set('Regon',KH.REG);
_obj.set('KRS',KH.KRS);
_obj.set('Address',KH.UL+' '+{? KH.DOM<>'' || KH.DOM+{? KH.LOKAL<>'' || ' '+KH.LOKAL || '' ?} || '' ?});
_obj.set('ZipCode',KH.KPOCZ);
:: "District": <string | null>,
_obj.set('City',KH.MIASTO);
_obj.set('Country',{? KH.KRAJISO || KH.KRAJISO().SYM || 'PL' ?});
:: "State": <string | null>,
_obj.set('PostOffice',KH.POCZ);
:: "AddNote": <string | null>,
:: "__ModifiedDate_cloud": <dateTime | null>,
:: "TenantCompanyId": <long | null>,
:: "BusinessContactId__id_erp": <string | null>,
_obj.set('TenantCompanyId__id_erp',REF.FIRMA().uidref());

_ret:=_obj.save();
_ret


\cseod_BCAddresHModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parsowanie odpowiedzi dla cseod_BCAddresHModify.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Modify_parse','portal_core','PORTAL_KH_ID')


\cseod_CostTypeTModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parametr komunikatu cseod_CostTypeTModify.
::----------------------------------------------------------------------------------------------------------------------
_formula_json:="params_exec('cseod_CostTypeTModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core','PORTAL_UD_SEOD_ID',_formula_json)


\cseod_CostTypeTModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: JSON dla zapytania cseod_CostTypeTModify.
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;

params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;

_ret:=1;
{? UD_SEOD.ETYPY & UD_SEOD.UD_SCH
|| _obj.create();

   _obj.set('__id_cloud',{? _id || _id || ~~ ?});

   _obj.set('CostCenterType',exec('SKID_MBP_type','portal_seod',UD_SEOD.UD_SCH));
   _obj.set('Description',UD_SEOD.NAZ);
::"CostCenterRequired": <unsignedByte | null>,
   _wp:=UD_SEOD.ETYPY().W_PORTAL;
   _obj.set('SubType',{? _wp='D' || ETYPY.ID_WP+4 |? _wp='S' || $UD_SEOD.ETYPY || ETYPY.ID_WP ?});

   _obj.set('__ModifiedDate_erp',UD_SEOD.idput_value());
   _obj.set('__id_erp',UD_SEOD.uidref());

   _ret:=_obj.save()
|? UD_SEOD.ETYPY & UD_SEOD.LP=0
|| exec('PORTALK_upd','portal_seod',UD_SEOD.ETYPY)
?};
_ret


\cseod_CostTypeTModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parsowanie odpowiedzi dla cseod_CostTypeTModify.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Modify_parse','portal_core','PORTAL_UD_SEOD_ID')


\cseod_CostTypeTDelete_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parametr zapytania cseod_CostTypeTDelete.
::   WY: JSON
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_uidref:=_env.Param;
_id:='PORTAL_UD_SEOD_ID';
_tab:=exec('get_id','#sync_id',_id,_uidref);
params_exec('Delete_args','portal_core',_id,_tab)


\cseod_CostTypeTDelete_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parsowanie odpowiedzi dla cseod_CostTypeTDelete
::----------------------------------------------------------------------------------------------------------------------
params_exec('Delete_parse','portal_core','PORTAL_UD_SEOD_ID')


\cseod_CostCategoryModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parametr komunikatu cseod_CostCategoryModify.
::----------------------------------------------------------------------------------------------------------------------

_formula_json:="params_exec('cseod_CostCategoryModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core','PORTAL_SEOD_ETYPWYD_ID',_formula_json)


\cseod_CostCategoryModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: JSON dla zapytania cseod_CostCategoryModify.
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;

params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;
_obj.create();

_obj.set('__id_cloud',{? _id || _id || ~~ ?});
_obj.set('CostCategory',ETYPWYD.NAZWA);
_obj.set('Active',1);
::"TenantCompanyId": <long | null>,
_obj.set('TenantCompanyId__id_erp',REF.FIRMA().uidref());
_obj.set('__ModifiedDate_erp',ETYPWYD.idput_value());
_obj.set('__id_erp',ETYPWYD.uidref());

_ret:=_obj.save();
_ret



\cseod_CostCategoryModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parsowanie odpowiedzi dla cseod_CostCategoryModify.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Modify_parse','portal_core','PORTAL_SEOD_ETYPWYD_ID')


\cseod_PurchaseDocModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parametr komunikatu cseod_PurchaseDocModify.
::----------------------------------------------------------------------------------------------------------------------
_formula_json:="params_exec('cseod_PurchaseDocModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core','PORTAL_SEOD_EDOK_ID',_formula_json)


\cseod_PurchaseDocModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: JSON dla zapytania cseod_PurchaseDocModify.
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;

:: walidacja
::{? 'T'*EDOKUM.TYP().W_PORTAL=0 | EDOKUM.STATUS=''
::|| return(0)
::?};
{? ~exec('EDOKUM_typ','portal_seod','F','S') | EDOKUM.STATUS=''
|| return(0)
?};

_id_firma:=exec('get_id_cloud','portal_core','PORTAL_FIRMA_ID',EDOKUM.FIRMA().uidref());
{? _id_firma=0
|| KOMM.add('Nie znaleziono identyfikatora firmy: %1 na portalu.'@[EDOKUM.FIRMA().SYMBOL]);
   return(0)
?};

_id_kh:=exec('get_id_cloud','portal_core','PORTAL_KH_ID',EDOKUM.KHKH().uidref());
{? _id_kh=0
|| KOMM.add('Nie znaleziono identyfikatora kontrahenta: %1 na portalu.'@[EDOKUM.KHKH().SKR]);
   return(0)
?};

params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;
_obj.create();

_obj.set('__id_cloud',{? _id || _id || ~~ ?});
_obj.set('__ModifiedDate_erp',EDOKUM.idput_value());
::_obj.set('__ModifiedDate_cloud',EDOKUM.idput_value());
_obj.set('__id_erp',EDOKUM.uidref());

_obj.set('DokType',{? EDOKUM.TYP().TYPVATPR=0 || 'Faktura' || 'Rachunek' ?});
_obj.set('DokNd',EDOKUM.NR);
_obj.set('DokNumber',EDOKUM.ID);
_obj.set('InvoiceNumber',EDOKUM.SYM);
_obj.set('CostCategory',EDOKUM.TYP().NAZWA);
::"BusinessContactId": <long | null>,

_obj.set('BCAddresHistoryId',_id_kh);
_obj.set('CurrencySymbol',EDOKUM.WAL().KOD);
_obj.set('RegistryDate',EDOKUM.DATAW);
_obj.set('ReceivedDate',EDOKUM.DO);
_obj.set('InvoiceDate',EDOKUM.DW);
_obj.set('SaleDate',EDOKUM.DOP);
_obj.set('GrossValue',EDOKUM.WART);
_obj.set('NetValue',EDOKUM.NETTO);
:: 'VATValue": <decimal | null>,
_obj.set('PaymentType',exec('sposob_pl','%seod',2,EDOKUM.PLATNOSC));
_obj.set('DueDate',EDOKUM.TP);
::"payedFromAdvance": <unsignedByte | null>,
_obj.set('SplitPayment',EDOKUM.SP_WYM='T');
_obj.set('Status',1);
_obj.set('Description',{? EDOKUM.TR<>'' || EDOKUM.TR || 'brak' ?});
::TenantUserId
_obj.set('__JSON',exec('EDOKUM_json','portal_seod'));

:: Wartości pól poniższego typu są przygotowywane przez mechanizm kastomizacji
::"__exf__cseod_PurchaseDocument_d_01": <dateTime | null>,
::"__exf__cseod_PurchaseDocument_n_01": <decimal | null>,
::"__exf__cseod_PurchaseDocument_s_01": <string | null>,

_obj.set('TenantCompanyId',_id_firma);
_obj.set('SubType',$EDOKUM.TYP);
_obj.set('__BinAttachment',exec('EDOKUM_att_json','portal_wnioski',2));
::"__Notes": <string | null>,
::"__SessionId": <int | null>,
_obj.set('BCAddresHistoryId__id_erp',EDOKUM.KHKH().uidref());
::"NumberPath": <string | null>,
_obj.set('TenantCompanyId__id_erp',EDOKUM.FIRMA().uidref());

{? EDOKUM.DOSTAWCA
|| USERS.cntx_psh();
   USERS.index('OSOBA');
   USERS.prefix();
   {? USERS.find_key(EDOKUM.DOSTAWCA) & exec('FindInSet','#table','USERSF','UNIK',USERS.ref(),REF.S_FIRMA,"USERSF.PORTAL2")='T'
   || _obj.set('TenantUserId__id_erp',USERS.WEBLOGIN)
   ?};
   USERS.cntx_pop()
?};

_ret:=_obj.save();
_ret



\cseod_PurchaseDocModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parsowanie odpowiedzi dla cseod_PurchaseDocModify.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Modify_parse','portal_core','PORTAL_SEOD_EDOK_ID')



\cseod_PurchaseDocGet_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parametr zapytania cseod_PurchaseDocGet.
::   WY: argumenty w postaci tablicy nazwanej.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Get_args','portal_core')


\cseod_PurchaseDocGet_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parsowanie odpowiedzi dla cseod_PurchaseDocGet.
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_tab:=_env.TabAcr;
{? _tab='EDOKUM'
|| params_exec('Get_parse','portal_core','PORTAL_SEOD_EDOK_ID',
      "exec('cseod_PurchaseDocGet_update','portal_method_seod',_a,_b)"
   )
|? _tab='EDOK_ZAL'
|| params_exec('Get_parse','portal_core','PORTAL_SEOD_EDOK_ZAL_ID',
      "exec('cseod_PurchaseDocGet2_update','portal_method_seod',_a,_b)"
   )
?}


\cseod_PurchaseDocGet_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Formuła aktualizująca dane w tabeli SLO_NAZ na podstawie wyniku zapytania cseod_PurchaseDocGet.
::   WE: _a [OBJECT] - Obiekt klasy JSON_MAN.
::       _b [STRING] - Akcja do wykonania: add/put/del.
::       _c [STRING] - Dodatkowy opis (wyróżnik) rekordu. Dla akcji 'add' - zawsze ''.
::   WY: Identyfikator rekordu lub 0 - dla akcji 'add', '' - dla akcji 'put' i 'del' lub ~~ w przypadku błędu.
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_act:=_b;

_typ:=_obj.getValue('SubType');
{? var_pres('_typ')<>type_of('')
|| KOMM.add('Brak typu faktury. Pominięto przetwarzania.');
   return(0)
?};

{? _act='del'
|| {? exec('EDOKUM_del','portal_wnioski')
   || return('')
   || return(~~)
   ?}
?};

_result:=~~;
EDOKUM.cntx_psh();
{? _act='add'
|| _ok:=1;
   EDOKUM.blank(1);
   _dtc:=_obj.getValue('RegistryDate');
   {? var_pres('_dtc')=type_of('')
   || EDOKUM.DATAW:=exec('str2date','#convert',10+_dtc);
      {? var_pres('CZASW',EDOKUM)>0
      || EDOKUM.CZASW:=exec('str2time','#convert',8+(11-_dtc))
      ?}
   ?};
   {? EDOKUM.DATAW=date(0,0,0)
   || EDOKUM.DATAW:=date()
   ?};
   _firma:=_obj.getValue('TenantCompanyId');
   {? var_pres('_firma')=type_of(1)
   || EDOKUM.FIRMA:=exec('get_id_erp','portal_core','PORTAL_FIRMA_ID',_firma);
      {? ~EDOKUM.FIRMA
      || KOMM.error('Nie znaleziono firmy o ID: %1'@[_firma],0);
         _ok:=0
      ?}
   || KOMM.error('Brak identyfikatora firmy'@,0);
      _ok:=0
   ?};
   {? _ok
   || ROK_F.cntx_psh();
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(EDOKUM.FIRMA);
      {? ROK_F.find_le(date(EDOKUM.DATAW~1,1,1)) | ROK_F.last()
      || EDOKUM.ROK_F:=ROK_F.ref()
      ?};
      ROK_F.cntx_pop();
      _maska:=$(EDOKUM.ROK_F().POCZ_ROK~1)+2;
      EDOKUM.use('skid_v'+_maska);
      EDOKUM.prefix();
      EDOKUM.NR:=_obj.getValue('DokNd');
      EDOKUM.ID:=_obj.getValue('DokNumber');
      EDOKUM.SYM:=_obj.getValue('InvoiceNumber');
      _typobi:=exec('bl_typ','obiegi',1);
      ETYPY.cntx_psh();
      ETYPY.index('UNIK_WP'); ETYPY.prefix(_typobi,'N',);
      {? var_pres('_typ')=type_of('') & ETYPY.seek(_typ) | ETYPY.first()
      || EDOKUM.TYP:=ETYPY.ref()
      ?};
      ETYPY.cntx_pop();
      EDOKUM.KHKH:=exec('get_id_erp','portal_core','PORTAL_KH_ID',_obj.getValue('BCAddresHistoryId'));
      _str:=_obj.getValue('CurrencySymbol');
      {? var_pres('_str')=type_of('')
      || {? ~XINFO.SLWAL
         || exec('czytaj','#stalesys',,XINFO,'SLWAL')
         ?};
         EDOKUM.WAL:=exec('FindInSet','#table','SLO','SL',_str,XINFO.SLWAL,,1,,null)
      ?};
      _data:=_obj.getValue('ReceivedDate');
      {? var_pres('_data')=type_of('')
      || EDOKUM.DO:=exec('str2date','#convert',10+_data)
      ?};
      _data:=_obj.getValue('InvoiceDate');
      {? var_pres('_data')=type_of('')
      || EDOKUM.DW:=exec('str2date','#convert',10+_data)
      ?};
      _data:=_obj.getValue('SaleDate');
      {? var_pres('_data')=type_of('')
      || EDOKUM.DOP:=exec('str2date','#convert',10+_data)
      ?};
      _data:=_obj.getValue('DueDate');
      {? var_pres('_data')=type_of('')
      || EDOKUM.TP:=exec('str2date','#convert',10+_data)
      ?};
      _num:=_obj.getValue('GrossValue');
      {? var_pres('_num')=type_of(1)
      || EDOKUM.WART:=_num
      ?};
      _num:=_obj.getValue('NetValue');
      {? var_pres('_num')=type_of(1)
      || EDOKUM.NETTO:=_num
      ?};
      _num:=_obj.getValue('SplitPayment');
      {? var_pres('_num')=type_of(1)
      || EDOKUM.SP_WYM:={? _num || 'T' || 'N' ?}
      ?};
      _str:=_obj.getValue('Description');
      {? var_pres('_str')=type_of('')
      || EDOKUM.TR:=_str
      ?};
      _str:=_obj.getValue('PaymentType');
      {? var_pres('_str')=type_of('')
      || EDOKUM.PLATNOSC:=exec('sposob_pl','%seod',1,_str)
      ?};
      EDOKUM.USERS:=exec('get_id_erp','portal_core','PORTAL_USERS_ID',_obj.getValue('TenantUserId'));
      _result:=exec('EDOKUM_add','portal_seod')
   ?}
|? _act='put'
|| _str:=_obj.getValue('Description');
   {? var_pres('_str')=type_of('')
   || EDOKUM.TR:=_str
   ?};
   _data:=_obj.getValue('DueDate');
   {? var_pres('_data')=type_of('')
   || EDOKUM.TP:=exec('str2date','#convert',10+_data)
   ?};
   _str:=_obj.getValue('PaymentType');
   {? var_pres('_str')=type_of('')
   || EDOKUM.PLATNOSC:=exec('sposob_pl','%seod',1,_str)
   ?};
   EDOKUM.KHKH:=exec('get_id_erp','portal_core','PORTAL_KH_ID',_obj.getValue('BCAddresHistoryId'));
   _str:=_obj.getValue('CurrencySymbol');
   {? var_pres('_str')=type_of('')
   || {? ~XINFO.SLWAL
      || exec('czytaj','#stalesys',,XINFO,'SLWAL')
      ?};
      EDOKUM.WAL:=exec('FindInSet','#table','SLO','SL',_str,XINFO.SLWAL,,1,,null)
   ?};
   _str:=_obj.getValue('InvoiceNumber');
   {? var_pres('_str')=type_of('')
   || EDOKUM.SYM:=_str
   ?};
   _data:=_obj.getValue('ReceivedDate');
   {? var_pres('_data')=type_of('')
   || EDOKUM.DO:=exec('str2date','#convert',10+_data)
   ?};
   _data:=_obj.getValue('InvoiceDate');
   {? var_pres('_data')=type_of('')
   || EDOKUM.DW:=exec('str2date','#convert',10+_data)
   ?};
   _data:=_obj.getValue('SaleDate');
   {? var_pres('_data')=type_of('')
   || EDOKUM.DOP:=exec('str2date','#convert',10+_data)
   ?};
   _num:=_obj.getValue('GrossValue');
   {? var_pres('_num')=type_of(1)
   || EDOKUM.WART:=_num
   ?};
   _num:=_obj.getValue('NetValue');
   {? var_pres('_num')=type_of(1)
   || EDOKUM.NETTO:=_num
   ?};
   EDOKUM.put();
   _result:=''
?};
{? _result<>~~
|| exec('EDOK_ATR_parse','portal_seod',_obj);
   exec('EDOKUM_att','portal_wnioski',_obj);
   _json_str:=_obj.getValue('__JSON');
   {? var_pres('_json_str')=type_of('')
   || _json:=json_parse(_json_str);
      {? var_pres('_json')>0
      || exec('EDOKUM_parse','portal_seod',_json)
      ?}
   ?}
?};
EDOKUM.cntx_pop();

_result


\cseod_PurchaseDocDel_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parametr zapytania cseod_PurchaseDocDel.
::   WY: JSON
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_acr:=_env.TabAcr;
_uidref:=_env.Param;
_id:={? _acr='EDOKUM' || 'PORTAL_SEOD_EDOK_ID' || 'PORTAL_SEOD_EDOK_ZAL_ID' ?};
_tab:=exec('get_id','#sync_id',_id,_uidref);
params_exec('Delete_args','portal_core',_id,_tab)


\cseod_PurchaseDocDel_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Parsowanie odpowiedzi dla cseod_PurchaseDocDel.
::----------------------------------------------------------------------------------------------------------------------
_env:=params_get().env;
_id:={? _env.TabAcr='EDOKUM' || 'PORTAL_SEOD_EDOK_ID' || 'PORTAL_SEOD_EDOK_ZAL_ID' ?};
params_exec('Delete_parse','portal_core',_id)


\cseod_BTReasonModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parametr komunikatu cseod_BusinessTripReasonModify.
::----------------------------------------------------------------------------------------------------------------------
_formula_json:="params_exec('cseod_BTReasonModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core','PORTAL_SLO_BTR_ID',_formula_json)


\cseod_BTReasonModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: JSON dla zapytania cseod_BusinessTripReasonModify.
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;

{? SLO.SLU<>XINFO.SLU_CEL
|| KOMM.add('Pozycja z innego słownik niż cel delegacji');
   return(0)
?};

params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;
_obj.create();

_obj.set('__id_cloud',{? _id || _id || ~~ ?});
_obj.set('Reason',SLO.TR);
_obj.set('__ModifiedDate_erp',SLO.idput_value());
_obj.set('__id_erp',SLO.uidref());

_ret:=_obj.save();
_ret


\cseod_BTReasonModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parsowanie odpowiedzi dla cseod_BusinessTripReasonModify.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Modify_parse','portal_core','PORTAL_SLO_BTR_ID')


\cseod_BTReasonDelete_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parametr zapytania cseod_BusinessTripReasonDelete.
::   WY: JSON
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_uidref:=_env.Param;
_id:='PORTAL_SLO_BTR_ID';
_tab:=exec('get_id','#sync_id',_id,_uidref);
params_exec('Delete_args','portal_core',_id,_tab)


\cseod_BTReasonDelete_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parsowanie odpowiedzi dla cseod_BusinessTripReasonDelete
::----------------------------------------------------------------------------------------------------------------------
params_exec('Delete_parse','portal_core','PORTAL_SLO_BTR_ID')


\cseod_BTVehicleModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parametr komunikatu cseod_BusinessTripVehicleModify.
::----------------------------------------------------------------------------------------------------------------------
_formula_json:="params_exec('cseod_BTVehicleModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core','PORTAL_INNESRTR_ID',_formula_json)


\cseod_BTVehicleModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: JSON dla zapytania cseod_BusinessTripVehicleModify.
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;

params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;
_obj.create();

_obj.set('__id_cloud',{? _id || _id || ~~ ?});
_obj.set('VehicleKind',INNESRTR.NAZWA);
_obj.set('ShowCarInfo',INNESRTR.DODAT='T');
_obj.set('__ModifiedDate_erp',INNESRTR.idput_value());
_obj.set('__id_erp',INNESRTR.uidref());

_ret:=_obj.save();
_ret



\cseod_BTVehicleModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parsowanie odpowiedzi dla cseod_BusinessTripVehicleModify.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Modify_parse','portal_core','PORTAL_INNESRTR_ID')


\cseod_BTVehicleDelete_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parametr zapytania cseod_BTVehicleDelete.
::   WY: JSON
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_uidref:=_env.Param;
_id:='PORTAL_INNESRTR_ID';
_tab:=exec('get_id','#sync_id',_id,_uidref);
params_exec('Delete_args','portal_core',_id,_tab)


\cseod_BTVehicleDelete_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parsowanie odpowiedzi dla cseod_BTVehicleDelete
::----------------------------------------------------------------------------------------------------------------------
params_exec('Delete_parse','portal_core','PORTAL_INNESRTR_ID')


\cseod_BTGet_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parametr zapytania cseod_BusinessTripGet.
::   WY: argumenty w postaci tablicy nazwanej.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Get_args','portal_core')


\cseod_BTGet_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parsowanie odpowiedzi dla cseod_BusinessTripGet.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Get_parse','portal_core','PORTAL_SEOD_EDOK_DEL_ID',
   "exec('cseod_BTGet_update','portal_method_seod',_a,_b)"
)


\cseod_BTGet_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Formuła aktualizująca dane w tabeli SLO_NAZ na podstawie wyniku zapytania cseod_BusinessTripGet.
::   WE: _a [OBJECT] - Obiekt klasy JSON_MAN.
::       _b [STRING] - Akcja do wykonania: add/put/del.
::       _c [STRING] - Dodatkowy opis (wyróżnik) rekordu. Dla akcji 'add' - zawsze ''.
::   WY: Identyfikator rekordu lub 0 - dla akcji 'add', '' - dla akcji 'put' i 'del' lub ~~ w przypadku błędu.
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_act:=_b;

{? _act='del'
|| {? exec('EDOKUM_del','portal_wnioski')
   || return('')
   || return(~~)
   ?}
?};

_result:=~~;
EDOKUM.cntx_psh();
{? _act='add'
|| _ok:=1;
   EDOKUM.blank(1);
   _dtc:=_obj.getValue('RegisterDate');
   {? var_pres('_dtc')=type_of('')
   || EDOKUM.DATAW:=exec('str2date','#convert',10+_dtc);
      {? var_pres('CZASW',EDOKUM)>0
      || EDOKUM.CZASW:=exec('str2time','#convert',8+(11-_dtc))
      ?}
   ?};
   {? EDOKUM.DATAW=date(0,0,0)
   || EDOKUM.DATAW:=date()
   ?};
   _firma:=_obj.getValue('TenantCompanyId');
   {? var_pres('_firma')=type_of(1)
   || EDOKUM.FIRMA:=exec('get_id_erp','portal_core','PORTAL_FIRMA_ID',_firma);
      {? ~EDOKUM.FIRMA
      || KOMM.error('Nie znaleziono firmy o ID: %1'@[_firma],0);
         _ok:=0
      ?}
   || KOMM.error('Brak identyfikatora firmy'@,0);
      _ok:=0
   ?};
   {? _ok
   || ROK_F.cntx_psh();
      ROK_F.index('ROKPOCZ'); ROK_F.prefix(EDOKUM.FIRMA);
      {? ROK_F.find_le(date(EDOKUM.DATAW~1,1,1)) | ROK_F.last()
      || EDOKUM.ROK_F:=ROK_F.ref()
      ?};
      ROK_F.cntx_pop();
      _maska:=$(EDOKUM.ROK_F().POCZ_ROK~1)+2;
      EDOKUM.use('skid_v'+_maska);
      EDOKUM.prefix();
::    EDOKUM.NR:=_obj.getValue('btNo');
::    EDOKUM.ID:=_obj.getValue('Number');
::    EDOKUM.SYM:=_obj.getValue('InvoiceNumber');
      _typ:=_obj.getValue('Kind');
      {? var_pres('_typ')=type_of('') & _typ<>'' || _typ:='SEODREQUEST#'+_typ ?};
      _typobi:=exec('bl_typ','obiegi',3);
      ETYPY.cntx_psh();
      ETYPY.index('ID_WP'); ETYPY.prefix();
      {? var_pres('_typ')=type_of('') & ETYPY.find_key(_typ,)
      || EDOKUM.TYP:=ETYPY.ref()
      ?};
      {? ~EDOKUM.TYP
      || ETYPY.index('UNIK_WP'); ETYPY.prefix(_typobi,'D',);
         {? ETYPY.first()
         || EDOKUM.TYP:=ETYPY.ref()
         ?}
      ?};
      ETYPY.cntx_pop();
      _str:=_obj.getValue('CountryCode');
      {? var_pres('_str')=type_of('')
      || EDOKUM.KRAJ:=exec('FindInSet','#table','KRAJE','KRAJE',_str,,,1)
      ?};
      _str:=_obj.getValue('CurrencySymbol');
      {? var_pres('_str')=type_of('')
      || {? ~XINFO.SLWAL
         || exec('czytaj','#stalesys',,XINFO,'SLWAL')
         ?};
         EDOKUM.WAL:=exec('FindInSet','#table','SLO','SL',_str,XINFO.SLWAL,,1,,null)
      ?};
      _data:=_obj.getValue('Departure');
      {? var_pres('_data')=type_of('')
      || EDOKUM.DATA_OD:=exec('str2date','#convert',10+_data);
         EDOKUM.GODZ_OD:=exec('str2time','#convert',8+(11-_data))
      ?};
      _data:=_obj.getValue('Arrival');
      {? var_pres('_data')=type_of('')
      || EDOKUM.DATA_DO:=exec('str2date','#convert',10+_data);
         EDOKUM.GODZ_DO:=exec('str2time','#convert',8+(11-_data))
      ?};
      _str:=_obj.getValue('Description');
      {? var_pres('_str')=type_of('')
      || EDOKUM.TR:=_str
      ?};
      _str:=_obj.getValue('Destination');
      {? var_pres('_str')=type_of('')
      || EDOKUM.DEL_MIE:=_str
      ?};
      _str:=_obj.getValue('Reason');
      {? var_pres('_str')=type_of('')
      || {? ~XINFO.SLU_CEL
         || exec('czytaj','#stalesys',,XINFO,'SLU_CEL')
         ?};
         EDOKUM.DEL_CEL:=exec('FindInSet','#table','SLO','SL_TR',_str,XINFO.SLU_CEL,,1,,null)
      ?};
      EDOKUM.USERS:=exec('get_id_erp','portal_core','PORTAL_USERS_ID',_obj.getValue('DelegatedUser'));
      _result:=exec('EDOKUM_add','portal_seod',3)
   ?}
|? _act='put'
|| _result:=''
?};
{? _result<>~~
|| exec('EDOK_ATR_parse','portal_seod',_obj);
   exec('EDOKUM_att','portal_wnioski',_obj);
   _json_str:=_obj.getValue('__JSON');
   {? var_pres('_json_str')=type_of('')
   || _json:=json_parse(_json_str);
      {? var_pres('_json')>0
      || exec('EDOKUM_parse','portal_seod',_json,3)
      ?}
   ?};
   EDOKUM.STATUS:='V';
   EDOKUM.put()
?};
EDOKUM.cntx_pop();
_result


\cseod_BTModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parametr komunikatu cseod_BTModify.
::----------------------------------------------------------------------------------------------------------------------
_formula_json:="params_exec('cseod_BTModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core','PORTAL_SEOD_EDOK_DEL_ID',_formula_json)


\cseod_BTModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: JSON dla zapytania cseod_BTModify.
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;

{? ~exec('EDOKUM_typ','portal_seod','D','D') | EDOKUM.STATUS=''
|| return(0)
?};

_id_firma:=exec('get_id_cloud','portal_core','PORTAL_FIRMA_ID',EDOKUM.FIRMA().uidref());
{? _id_firma=0
|| KOMM.add('Nie znaleziono identyfikatora firmy: %1 na portalu.'@[EDOKUM.FIRMA().SYMBOL]);
   return(0)
?};
_id_user:=exec('get_id_cloud','portal_core','PORTAL_USERS_ID',EDOKUM.USERS().uidref());
{? _id_user=0
|| KOMM.add('Nie znaleziono identyfikatora użytkownika: %1 na portalu.'@[EDOKUM.USERS().KOD]);
   return(0)
?};


params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;
_obj.create();

_obj.set('__id_cloud',{? _id || _id || ~~ ?});
_obj.set('__ModifiedDate_erp',EDOKUM.idput_value());
::_obj.set('__ModifiedDate_cloud',EDOKUM.idput_value());
_obj.set('__id_erp',EDOKUM.uidref());

_obj.set('Kind',EDOKUM.TYP().ID_WP+4);
_obj.set('Reason',EDOKUM.DEL_CEL().TR);
:"btNo": <int | null>,
_obj.set('Number',EDOKUM.ID);
_obj.set('Status',1);
_obj.set('Destination',EDOKUM.DEL_MIE);
_obj.set('DelegatedUser',_id_user);
:"BusinessContactId": <long | null>,
_obj.set('RegisterDate',EDOKUM.DATAW,EDOKUM.CZASW);
_obj.set('Description',EDOKUM.TR);
_obj.set('Departure',EDOKUM.DATA_OD,EDOKUM.GODZ_OD);
_obj.set('Arrival',EDOKUM.DATA_DO,EDOKUM.GODZ_DO);
:"VehicleKind": <string | null>,
:"AssistanceNeeded": <string | null>,
_obj.set('PlannedExpenses',0);
_obj.set('CurrencySymbol',EDOKUM.WAL().KOD);
:"__ModifiedDate_cloud": <dateTime | null>,
_obj.set('__JSON',exec('EDOKUM_json','portal_seod'));
::"__exf__cseod_BusinessTrip_d_01": <dateTime | null>,
::"__exf__cseod_BusinessTrip_n_01": <decimal | null>,
::"__exf__cseod_BusinessTrip_s_01": <string | null>,
_obj.set('CountryCode',EDOKUM.KRAJ().KODISO);
_obj.set('TenantCompanyId',_id_firma);
::"__BinAttachment": <string | null>,
::"__Notes": <string | null>,
::"BCAddresHistoryId__id_erp": <string | null>,
::"btNo__id_erp": <string | null>,
::"DelegatedUser__id_erp": <string | null>,
::"NUmber__id_erp": <string | null>,
::"TenantCompanyId__id_erp": <string | null>

_ret:=_obj.save();
_ret



\cseod_BTModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parsowanie odpowiedzi dla cseod_BTModify.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Modify_parse','portal_core','PORTAL_SEOD_EDOK_DEL_ID')


\cseod_BTDelete_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parametr zapytania cseod_BTDelete.
::   WY: JSON
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_uidref:=_env.Param;
_id:='PORTAL_SEOD_EDOK_DEL_ID';
_tab:=exec('get_id','#sync_id',_id,_uidref);
params_exec('Delete_args','portal_core',_id,_tab)


\cseod_BTDelete_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parsowanie odpowiedzi dla cseod_BTDelete
::----------------------------------------------------------------------------------------------------------------------
params_exec('Delete_parse','portal_core','PORTAL_SEOD_EDOK_DEL_ID')


\cseod_PurchaseDocGet2_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Formuła aktualizująca dane w tabeli EDOK_ZAL na podstawie wyniku zapytania cseod_PurchaseDocGet.
::   WE: _a [OBJECT] - Obiekt klasy JSON_MAN.
::       _b [STRING] - Akcja do wykonania: add/put/del.
::       _c [STRING] - Dodatkowy opis (wyróżnik) rekordu. Dla akcji 'add' - zawsze ''.
::   WY: Identyfikator rekordu lub 0 - dla akcji 'add', '' - dla akcji 'put' i 'del' lub ~~ w przypadku błędu.
::----------------------------------------------------------------------------------------------------------------------
_obj:=_a;
_act:=_b;

_typ:=_obj.getValue('SubType');
{? var_pres('_typ')=type_of('')
|| KOMM.add('Wskazany typu faktury. Pominięto przetwarzania.');
   return(0)
?};

{? _act='del'
|| {? EDOK_ZAL.del(,1)
   || return('')
   || return(~~)
   ?}
?};

_result:=~~;
EDOK_ZAL.cntx_psh();
{? _act='add'
|| _ok:=1;
   EDOK_ZAL.blank(1);
   EDOK_ZAL.ZW:='W';
   _num:=_obj.getValue('__id_cloud');
   {? var_pres('_num')=type_of(1)
   || EDOK_ZAL.ID_CLOUD:=$_num
   ?};
   _data:=_obj.getValue('SaleDate');
   {? var_pres('_data')=type_of('')
   || EDOK_ZAL.DATDOK:=exec('str2date','#convert',10+_data)
   ?};
   _str:=_obj.getValue('CurrencySymbol');
   {? var_pres('_str')=type_of('')
   || {? ~XINFO.SLWAL
      || exec('czytaj','#stalesys',,XINFO,'SLWAL')
      ?};
      EDOK_ZAL.WAL:=exec('FindInSet','#table','SLO','SL',_str,XINFO.SLWAL,,1,,null)
   ?};
   _firma_id:=_obj.getValue('TenantCompanyId');
   {? var_pres('_firma_id')=type_of(1)
   || _firma:=exec('get_id_erp','portal_core','PORTAL_FIRMA_ID',_firma_id);
      {? ~_firma
      || KOMM.error('Nie znaleziono firmy o ID: %1'@[_firma_id],0);
         _ok:=0
      ?}
   || KOMM.error('Brak identyfikatora firmy'@,0);
      _ok:=0
   ?};
   {? _ok
   || _ok:=0;
      _json_str:=_obj.getValue('__JSON');
      {? var_pres('_json_str')=type_of('')
      || _json:=json_parse(_json_str);
         {? var_pres('_json')>0
         || {? var_pres('LinkDocuments',_json)>0
            || _linki:=_json.LinkDocuments;
               {! _ii:=1..obj_len(_linki)
               |! _link:=_linki[_ii];
                  {? var_pres('LinkType',_link)>0 & _link.LinkType='PurchaseDocument-BusinessTrip'
                  || _ok:=1
                  ?};
                  &_link
               !}
            ?}
         ?}
      ?}
   ?};
   {? _ok
   || _maska:='_1';
      EDOK_ZAL.use('edokzl'+_maska);
      EDOK_ZAL.prefix();
      _str:=_obj.getValue('CostCategory');
      {? var_pres('_str')=type_of('')
      || EDOK_ZAL.ETYPWYD:=exec('FindInSet','#table','ETYPWYD','UNIK',_str,,,1,,null)
      ?};
      _num:=_obj.getValue('GrossValue');
      {? var_pres('_num')=type_of(1)
      || EDOK_ZAL.BRUTTO:=_num
      ?};
      _num:=_obj.getValue('NetValue');
      {? var_pres('_num')=type_of(1)
      || EDOK_ZAL.NETTO:=_num
      ?};
      _str:=_obj.getValue('Description');
      {? var_pres('_str')=type_of('')
      || EDOK_ZAL.OPIS:=_str
      ?};
      {? EDOK_ZAL.add()
      || _result:=EDOK_ZAL.uidref()
      ?}
   ?}
|? _act='put'
|| _str:=_obj.getValue('CostCategory');
   {? var_pres('_str')=type_of('')
   || EDOK_ZAL.ETYPWYD:=exec('FindInSet','#table','ETYPWYD','UNIK',_str,,,1,,null)
   ?};
   _str:=_obj.getValue('Description');
   {? var_pres('_str')=type_of('')
   || EDOK_ZAL.OPIS:=_str
   ?};
   EDOK_ZAL.put();
   _result:=''
?};
EDOK_ZAL.cntx_pop();

_result


\cseod_PaymentTTDelete_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parametr zapytania cseod_PaymentTypeTenantDelete.
::   WY: JSON
::----------------------------------------------------------------------------------------------------------------------
params_set(params_get());
_env:=params_get().env;
_uidref:=_env.Param;
_id:='PORTAL_SLO_SP_ID';
_tab:=exec('get_id','#sync_id',_id,_uidref);
params_exec('Delete_args','portal_core',_id,_tab)


\cseod_PaymentTTDelete_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parsowanie odpowiedzi dla cseod_PaymentTypeTenantDelete
::----------------------------------------------------------------------------------------------------------------------
params_exec('Delete_parse','portal_core','PORTAL_SLO_SP_ID')


\cseod_PaymentTTModify_args
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parametr komunikatu cseod_PaymentTypeTenantModify.
::----------------------------------------------------------------------------------------------------------------------
_formula_json:="params_exec('cseod_PaymentTTModify_json','portal_method_seod',_a,_b)";
params_exec('Modify_args','portal_core','PORTAL_SLO_SP_ID',_formula_json)


\cseod_PaymentTTModify_json
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: JSON dla zapytania cseod_PaymentTypeTenantModify.
::   WE: _a [NUMBER] - id_cloud / 0
::       _b [STRING] - opis dodatkowy / ''
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_id:=_a;
_dod:={? var_pres('_b')=type_of('') || _b || '' ?};
{? _dod<>''
|| return(0)
?};

{? SLO.SLU<>XINFO.SLP
|| KOMM.add('Pozycja z innego słownik niż sposób płatności');
   return(0)
?};

params_set(_par:=params_get());
_env:=_par.env;
_obj:=_env.Adds.obj;

:: Przy pierwszej wysyłce danych usuwamy domyślne sposoby płatności
_send:={? var_pres('PorSloSP')=type_of(1)
       || PorSloSP
       || VAR_DEL.delete('PorSloSP');
          PorSloSP:=exec('isAny','#sync_id','PORTAL_SLO_SP_ID')
       ?};
{? ~_send
|| PorSloSP:=1;
   _lista:=obj_new(4);
   _lista[1]:='Zapłacono gotówką z zaliczki';
   _lista[2]:='Zapłacono kartą służbową';
   _lista[3]:='Zapłacono ze środków własnych';
   _lista[4]:='Płatność przelewem';
   SLO.cntx_psh();
   SLO.index('SL_TR');
   SLO.prefix(XINFO.SLP);
   {! _ii:=1..obj_len(_lista)
   |! {? ~SLO.find_key(_lista[_ii],)
      || _hash:=hash(_lista[_ii]);
         _obj.create();
::       _obj.set('__id_cloud',{? _id || _id || ~~ ?});
         _obj.set('PaymentType',_hash);
         _obj.set('PaymentDescription',_lista[_ii]);
         _obj.set('AdvUsage',0);
         _obj.set('PurchaseUage',-1);
         _obj.set('__ModifiedDate_erp',date,time());
         _obj.set('__id_erp',SLO.uidref()+_hash);
         _obj.save()
      ?}
   !};
   SLO.cntx_pop()
?};


_obj.create();

_obj.set('__id_cloud',{? _id || _id || ~~ ?});

_obj.set('PaymentType',SLO.KOD);
_obj.set('PaymentDescription',SLO.TR);
_obj.set('AdvUsage',0);
_obj.set('PurchaseUage',1);
_tp:=exec('plat_dni','slo_slu',SLO.ref());
_obj.set('DueDateOffset',_tp);
_obj.set('HideDueDate',_tp=0);
_obj.set('__ModifiedDate_erp',SLO.idput_value());
_obj.set('__id_erp',SLO.uidref());

_ret:=_obj.save();
_ret



\cseod_PaymentTTModify_parse
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Parsowanie odpowiedzi dla cseod_PaymentTypeTenantModify.
::----------------------------------------------------------------------------------------------------------------------
params_exec('Modify_parse','portal_core','PORTAL_SLO_SP_ID')

:Sign Version 2.0 jowisz:1045 2023/12/08 07:41:16 452e0f66ff592387bc675e10831af4639376186ade0a2af688a7bb2eeb6bfd3db9edd3073fc9c5e77e8dbb1b89d8d8c78199e847053640078583488382848938bc4b5e9a4f9867d730e46af38810e75f72f39ed07f04911422713aba910d736bd408739a83af405c70ab75f53abddf26ee39f0517129d00658f7347a22b9e959

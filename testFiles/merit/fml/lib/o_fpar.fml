:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: o_fpar.fml
:: Utworzony: 28.10.2020
:: Autor: jaws
::======================================================================================================================
:: Zawartość: Formuły biblioteczne dla obsługi tabeli O_FPAR.
::======================================================================================================================


\_modb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Przed dołączeniem lub poprawieniem wiersza tabeli O_FPAR.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
{? O_FPAR.WLACZONY='N' || O_FPAR.WYM_JEST:='N' ?};
1


\_chk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Sprawdza wypełnienie wymaganych pól. Wykorzystywana w wyzwalaczach przed dołącz i popraw.
::   WE: _a [INTEGER] - Tryb modyfikacji danych
::          0 - dołączanie (domyślnie)
::          1 - poprawianie
::   WY: wynik testu poprawności danych zależny od kontekstu wywołania:
::       - jako formuła "rekord po"
::          - akronim niewypełnionego pola
::          - akronim pola o błędnej wartości
::          - 0 w przypadku powielenia unikalnego klucza
::       - jako formuła sprawdzająca dla wyzwalaczy "dołącz przed" i "popraw przed"
::          - 1 wiersz poprawny
::          - 0 niewypełnione wymagane pole, błędna wartość pola, powielony unikalny klucz
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_mode:={? var_pres('_a')=type_of(0) || _a<>0 ?};

__CHK.validate(O_FPAR,
:: sprawdź wymagane dane i unikalność indeksów
   $("_a.table(_b,"+$_mode+",'FIRMA','O_DPAR','WLACZONY','WYM_JEST','MOD_BL','MOD_AE')"),
   "{? O_FPAR.MOD_BL='T' || _a.record(_b,,'FML_BL') || 1 ?}",
   "{? O_FPAR.MOD_AE='T' || _a.record(_b,,'FML_AE') || 1 ?}",
:: zweryfikuj dozwolone wartości
   "_a.in_set(_b,'WLACZONY',,'T','N')",
   "_a.in_set(_b,'WYM_JEST',,'T','N')",
   "_a.in_set(_b,'MOD_BL',,'T','N')",
   "_a.in_set(_b,'MOD_AE',,'T','N')",
:: kontrola składni formuł
   "exec('_x_form_chk','o_dpar',_a,_b,'FML_BL','FML_AE')"
)


\_addb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Wyzwalacz przed dołączeniem wiersza tabeli O_FPAR.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','o_fpar') & exec('_chk','o_fpar',0)


\_putb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Wyzwalacz przed poprawieniem wiersza tabeli O_FPAR.
::   WE:
::   WY: 0/1 - wiersz błędny/poprawny
::----------------------------------------------------------------------------------------------------------------------
exec('_modb','o_fpar') & exec('_chk','o_fpar',1)


\_delb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Wyzwalacz przed usunięciem wiersza tabeli O_FPAR.
::   WE:
::   WY: 0/1 - wiersz może/nie może być usunięty
::----------------------------------------------------------------------------------------------------------------------
exec('del_ndx','o_xpar',O_FPAR.ref())


\buf_utwórz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Tworzy i ustawia wartościami domyślnymi bufor danych dla wiersza tabeli O_FPAR.
::   WE:
::   WY: wskazanie tablicy nazwanej o strukturze:
::          REF - wskazanie wiersza w tabeli O_DPAR
::          FIRMA - wskazanie wiersza w tabeli FIRMA
::          O_DPAR - wskazanie wiersza w tabeli O_DPAR
::          WLACZONY - parametr uwzględniany w firmie
::          WYM_JEST - parametr wymagany dla listy płac
::          MOD_BL - dedykowana formuła na wartość początkową
::          FML_BL - treść formuły na wartość początkową
::          MOD_AE - dedykowana formuła weryfikująca wartość
::          FML_AE - treść formuły weryfikującej wartość
::----------------------------------------------------------------------------------------------------------------------
_ret:=obj_new(
   'REF','FIRMA','O_DPAR','WLACZONY','WYM_JEST',
   'MOD_BL','FML_BL','MOD_AE','FML_AE'
);

_ret.REF:=null;
_ret.FIRMA:=null;
_ret.O_DPAR:=exec('buf_utwórz','o_dpar');
_ret.WLACZONY:='';
_ret.WYM_JEST:='';
_ret.MOD_BL:='';
_ret.FML_BL:='';
_ret.MOD_AE:='';
_ret.FML_AE:='';

_ret


\buf_ustaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Wypełnia bufor danych wiersza tabeli O_FPAR bieżącymi wartościami.
::   WE: _a OBJECT - wskazanie tablicy nazwanej bufora (patrz buf_utwórz)
::   WY: _a
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_ret:=_a;

_ret.REF:=O_FPAR.ref();
_ret.FIRMA:=O_FPAR.FIRMA;
{? O_FPAR.O_DPAR<>null
|| O_DPAR.cntx_psh();
   O_DPAR.clear();
   {? O_DPAR.seek(O_FPAR.O_DPAR)
   || exec('buf_ustaw','o_dpar',_ret.O_DPAR)
   ?};
   O_DPAR.cntx_pop()
?};
_ret.WLACZONY:=O_FPAR.WLACZONY;
_ret.WYM_JEST:=O_FPAR.WYM_JEST;
_ret.MOD_BL:=O_FPAR.MOD_BL;
_ret.FML_BL:={? O_FPAR.MOD_BL='T' || O_FPAR.memo_txt(,1,'FML_BL') || '' ?};
_ret.MOD_AE:=O_FPAR.MOD_AE;
_ret.FML_AE:={? O_FPAR.MOD_AE='T' || O_FPAR.memo_txt(,1,'FML_AE') || '' ?};

_ret


\szukaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Ustala definicję parametru listy.
::   WE: _a [_FIRMA] - wskazanie wiersza w tabeli FIRMA, jeśli pominięty lub null, to przyjęta będzie bieżąca firma
::       _b STRING/_O_DPAR - symbol/wskazanie parametru
::       _c [INTEGER] - typ zwracanego wyniku: różny od zera - wskazanie tablicy nazwanej, wpp referencja
::       _d [INTEGER] - zachowanie kontekstu: różny od zera - pomiń zachowanie i odtworzenie konteksu, wpp zachowuj
::   WY: dla _a:
::       - różnego od zera wskazanie tablicy nazwanej (patrz buf_utworz)
::       - wpp wskazanie wiersza w tabeli O_DPAR lub null
::       - ~~ w przypadku błędnego wywołania
::----------------------------------------------------------------------------------------------------------------------
:: kontrola wywołania
{? (var_pres('_b')<>type_of('') & var_pres('_b')<>type_of(null)) |
   (var_pres('_b')=type_of(null) & (_b=null | ref_tab(_b)<>O_DPAR))
:: _b typu innego niż napis lub wskazanie wiersza w tabeli O_DPAR
|| return()
?};
:: mapa argumentów
_fir:={? var_pres('_a')<>type_of(null) | _a=null || exec('ref_firma','ustawienia') || _a ?};
_def:=_b;
_ext:=(var_pres('_c')=type_of(0) & _c<>0);
_ctx:=(var_pres('_d')<>type_of(0) | _d=0);

:: wartość zwracana
_ret:=null;
{? _ext<>0
:: rozszerzona
|| _ret:=exec('buf_utwórz','o_fpar')
?};

{? _ctx<>0
|| O_FPAR.cntx_psh()
?};
:: szukaj zależnie od typu _def
{? {? type_of(_def)=type_of(null)
   || O_FPAR.index('UNIQUE');
      O_FPAR.prefix(_fir);
      O_DPAR.find_key(_def)
   || O_FPAR.index('SYMBOL');
      O_FPAR.prefix(_fir);
      O_FPAR.find_key(_def,)
   ?}
:: znaleziono
|| {? _ext=0
   || _ret:=O_FPAR.ref()
   || exec('buf_ustaw','o_fpar',_ret)
   ?}
?};
{? _ctx<>0
|| O_FPAR.cntx_pop()
?};

_ret


\dodaj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Tworzy wiersz na podstawie danych zawartych w tablicy nazwanej.
::   WE: _a OBJECT - wskazanie tablicy nazwanej bufora (patrz buf_utwórz)
::       _b [INTEGER] - zachowanie kontekstu: różny od zera - pomiń zachowanie i odtworzenie konteksu, wpp zachowuj
::   WY: ref/~~ - wskazanie dodanego/istniejącego wiersza lub błąd
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_buf:=_a;
_ctx:=(var_pres('_b')<>type_of(0) | _b=0);

{? _ctx<>0
|| O_FPAR.cntx_psh()
?};
{? (_ret:=exec('szukaj','o_fpar',_buf.FIRMA,_buf.SYMBOL,,1))=null
|| O_FPAR.blank(1);
   O_FPAR.FIRMA:=_buf.FIRMA;
   O_FPAR.O_DPAR:=_buf.O_DPAR.REF;
   O_FPAR.WLACZONY:=_buf.WLACZONY;
   O_FPAR.WYM_JEST:=_buf.WYM_JEST;
   O_FPAR.MOD_BL:=_buf.MOD_BL;
   O_FPAR.memo_set(_buf.FML_BL,'FML_BL');
   O_FPAR.MOD_AE:=_buf.MOD_AE;
   O_FPAR.memo_set(_buf.FML_AE,'FML_AE');
   {? O_FPAR.add() &
      O_FPAR.memo_put(,'FML_BL') & O_FPAR.memo_put(,'FML_AE')
   || _ret:=O_FPAR.ref()
   || _ret:=~~
   ?}
?};
{? _ctx<>0
|| O_FPAR.cntx_pop()
?};

_ret


\usun
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Usuwa właściwy dla firmy wiersz identyfikowany symbolem lub wskazaniem.
::   WE: _a [_FIRMA] - wskazanie wiersza w tabeli FIRMA, jeśli pominięty lub null, to przyjęta będzie bieżąca firma
::       _b STRING/_O_DPAR - symbol/wskazanie parametru
::   WY: 0/1/~~ - wiersz nie istnieje/usunięto/błąd
::----------------------------------------------------------------------------------------------------------------------
:: kontrola wywołania
{? (var_pres('_b')<>type_of('') & var_pres('_b')<>type_of(null)) |
   (var_pres('_b')=type_of(null) & (_b=null | ref_tab(_b)<>O_DPAR))
:: _b typu innego niż napis lub wskazanie wiersza w tabeli O_DPAR
|| return()
?};
:: mapa argumentów
_fir:={? var_pres('_a')<>type_of(null) | _a=null || exec('ref_firma','ustawienia') || _a ?};
_def:=_b;

_ret:=0;
O_FPAR.cntx_psh();
{? exec('szukaj','o_fpar',_fir,_def,,1)<>null
|| _ret:={? O_FPAR.del(,1) || 1 || ~~ ?}
?};
O_FPAR.cntx_pop();
_ret


\system_n
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Sprawdza, czy parametr w firmie jest zapisem klienta.
::   WE:
::   WY: 0/1 - zapis nie może być modyfikowany/można zmieniać dane
::----------------------------------------------------------------------------------------------------------------------
O_FPAR.O_DPAR().SYSTEM<>'T'


\fld_opt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Ustawie opcje pól aktualnego okienka wertowania tabeli O_FPAR.
::   WE: _a [INTEGER] - <>0/0* - ustaw/wycofaj opcej
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
_set:=(var_pres('_a')=type_of(0) & _a<>0);
_wnd:=O_FPAR.win_edit('?');
O_FPAR.efld_opt(_wnd,'enable=%1'[${? _set || O_FPAR.WLACZONY='T' ?}],,'WYM_JEST');
O_FPAR.efld_opt(_wnd,'enable=%1'[${? _set || O_FPAR.MOD_BL='T' ?}],,'FML_BL');
O_FPAR.efld_opt(_wnd,'enable=%1'[${? _set || exec('mod_ae_be','o_fpar') ?}],,'MOD_AE');
O_FPAR.efld_opt(_wnd,'enable=%1'[${? _set || O_FPAR.MOD_AE='T' ?}],,'FML_AE');
~~


\wlaczony_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Przed edycją pola WLACZONY tabeli O_FPAR.
::   WE:
::   WY: 0/1 - edycja zablokowana/możliwa
::----------------------------------------------------------------------------------------------------------------------
exec('system_n','o_fpar')


\wlaczony_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Po edycji pola WLACZONY tabeli O_FPAR.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('fld_opt','o_fpar',1);
1


\wym_jest_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Przed edycją pola WYM_JEST tabeli O_FPAR.
::   WE:
::   WY: 0/1 - edycja zablokowana/możliwa
::----------------------------------------------------------------------------------------------------------------------
O_FPAR.WLACZONY='T' & O_FPAR.O_DPAR().WYM_DOST='T'


\wym_jest_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Po edycji pola WYM_JEST tabeli O_FPAR.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
1


\mod_bl_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Przed edycją pola MOD_BL tabeli O_FPAR.
::   WE:
::   WY: 0/1 - edycja zablokowana/możliwa
::----------------------------------------------------------------------------------------------------------------------
1


\mod_bl_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Po edycji pola MOD_BL tabeli O_FPAR.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('fld_opt','o_fpar',1);
{? O_FPAR.MOD_BL='T'
|| _fml:=O_FPAR.memo_txt(,0,'FML_BL')
?};
1


\mod_ae_be
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Przed edycją pola MOD_AE tabeli O_FPAR.
::   WE:
::   WY: 0/1 - edycja zablokowana/możliwa
::----------------------------------------------------------------------------------------------------------------------
O_FPAR.O_DPAR().TYP<>'Z'


\mod_ae_ae
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Po edycji pola MOD_AE tabeli O_FPAR.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('fld_opt','o_fpar',1);
1


\zmien
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Włącza/wyłącza wykorzystanie parametru.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
O_FPAR.WLACZONY:={? O_FPAR.WLACZONY='T' || 'N' || 'T' ?};
O_FPAR.put();
~~


\wlacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Przed włączeniem zapisu w okienkach tabeli O_FPAR.
::   WE:
::   WY: wynik sprawdzenia warunku wykonania zasadniczej akcji
::----------------------------------------------------------------------------------------------------------------------
{? O_FPAR.WLACZONY<>'T'
|| exec('system_n','o_fpar')
?}


\wlacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Włączenie zapisu w okienkach tabeli O_FPAR.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('zmien','o_fpar')


\wylacz_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Przed wyłączeniem zapisu w okienkach tabeli O_FPAR.
::   WE:
::   WY: wynik sprawdzenia warunku wykonania zasadniczej akcji
::----------------------------------------------------------------------------------------------------------------------
{? O_FPAR.WLACZONY<>'N'
|| exec('system_n','o_fpar')
?}


\wylacz_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Włączenie zapisu w okienkach tabeli O_FPAR.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
exec('zmien','o_fpar')


\popraw_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Przed poprawieniem zapisu w okienkach tabeli O_FPAR.
::   WE:
::   WY: wynik sprawdzenia warunku wykonania zasadniczej akcji
::----------------------------------------------------------------------------------------------------------------------
1


\popraw_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Poprawienie zapisu w okienkach tabeli O_FPAR.
::   WE:
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
O_FPAR.get();
O_FPAR.memo_get(,'FML_BL');
O_FPAR.memo_get(,'FML_AE');
exec('fld_opt','o_fpar',1);
{? O_FPAR.edit("exec('_chk','o_fpar',1)")
|| O_FPAR.put() & O_FPAR.memo_put(,'FML_BL') & O_FPAR.memo_put(,'FML_AE')
?};
~~


\rekord_b
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: "Rekord przed" w okienkach tabeli O_FPAR.
::   WE: _a INTEGER - zgodny ze specyfikacją narzędzi
::   WY: ~~
::----------------------------------------------------------------------------------------------------------------------
O_FPAR.O_DPAR();
POLA_GRP.TXT_1:=exec('typ_opis','o_dpar',O_DPAR.TYP);
{? _a=0 || return() ?};

_aid:='';
{? ~exec('wlacz_b','o_fpar')
:: zablokuj "włącz"
|| _aid+='W'
?};
{? ~exec('wylacz_b','o_fpar')
:: zablokuj "wyłącz"
|| _aid+='Y'
?};
{? ~exec('popraw_b','o_fpar')
:: zablokuj "popraw"
|| _aid+='P'
?};

O_FPAR.actions_grayed(cur_win(1,1),_aid);
~~


\wskazowki_a
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Wyświetla wskazówki programistyczne.
::   WE:
::   WY: ''
::----------------------------------------------------------------------------------------------------------------------
exec('show_tips','#edit',
   'Formuła "Wartość początkowa" wykonywana jest w momencie przypisywania parametru do nagłówka lub typu listy płac.\n'
   'Do formuły nie jest przekazywany żaden argument.\n'
   'Wartość zwrócona przez formułę powinna być napisem "T" lub "N".\n\n'
   'Formuła "Weryfikacja wartości" wykonywana jest po edycji parametru przypisanego do nagłówka lub typu listy płac.\n'
   'Do formuły jest przekazywany jeden argument (_a) równy podanej podczas edycji wartości parametru.\n'
   'Wartością zwróconą może być:\n'
   '\tliczba różna od 0 - co oznacza, że podana wartość została uznana za poprawną;\n'
   '\t0 - w przypadku, gdy podana wartość jest błędna (wyświetlony zostanie uproszczony komunikat);\n'
   '\tnapis - również dla błędnej wartości parametru (w treści komunikatu uwzględniony zostanie zwrócony tekst).\n\n'
   'Przykład:\n'
   '\t_a<1 | _a>12\n'
   'Jeśli podano wartość -1, to nie będzie ona zapisana a wyświetlony komunikat będzie miał treść:\n'
   'Podano nieprawidłową wartość -1.\n\n'
   'Przykład:\n'
   '\t{? _a<1 | _a>12\n'
   '\t|| \'Należy podać liczbę z zakresu od 1 do 12.\'\n'
   '\t|| 1\n'
   '\t?}\n'
   'Jeśli podano wartość 22, to nie będzie ona zapisana a wyświetlony komunikat będzie miał treść:\n'
   '22 nie jest prawidłową wartością.\n'
   'Należy podać liczbę z zakresu od 1 do 12.\n\n'
   'Treści formuł mogą być zapisane w bibliotece FML.',
   O_FPAR.comment()
);
''


\wybierz_buf
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Tworzy tablicę wyniku wyboru parametrów w firmie.
::   WE:
::   WY: wskazanie tablicy o strukturze:
::       OK INTEGER - 0*/1 - wynik wyboru
::       O_FPAR - alias tabeli z wybranymi wierszami
::----------------------------------------------------------------------------------------------------------------------
_buf:=obj_new('OK','O_FPAR');
_buf.OK:=0;
_buf.O_FPAR:=tab_tmp(1,
   'NUM','INTEGER','Lp.'@,
   'REF','INTEGER','Numer wiersza'@,
   'SQL','STRING[16]','Wskazanie wiersza'@
);
_buf


\wybierz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [21.14]
:: OPIS: Wybór opisu parametru w okienku innym niż słownikowe.
::   WE: _a STRING - typ parametru [Z/R]
::       _a STRING - identyfikator rekordu, z którym powiązane są parametry
::       _b [INTEGER] - możliwość wyboru wielu paramatrów <>0, wpp pojedynczy wiersz
::   WY: alias tabeli z wybranymi wierszami
::----------------------------------------------------------------------------------------------------------------------
:: mapa argumentów
_typ:=_a;
_rid:=_b;
_one:=(var_pres('_c')<>type_of(0) | _c=0);

_ret:=exec('wybierz_buf','o_fpar');
params_set('SEL',_ret.O_FPAR);

:: utwórz okienko wyboru jednego/wielu parametrów w firmie
_wnd:=O_FPAR.mk_sel('Parametry'@,'P',,'#o_dpar_wyb',10,,,,'U',,,,,'normal');

:: dodaj kolumny widoku
O_FPAR.win_fld(_wnd,O_DPAR,'SYSTEM',,,-3,,,MS.name(O_DPAR,'SYSTEM'),,MS.comment(O_DPAR,'SYSTEM'),2,,"'T'","'N'");
O_FPAR.win_fld(_wnd,O_FPAR,'WLACZONY',,,-3,,,MS.name(O_FPAR,'WLACZONY'),,MS.comment(O_FPAR,'WLACZONY'),2,,"'T'","'N'");
O_FPAR.win_fld(_wnd,O_FPAR,'O_DPAR','NAZWA',,,,,MS.name(O_DPAR,'NAZWA'));
O_FPAR.win_fld(_wnd,O_FPAR,'O_DPAR','SYMBOL',,,,,MS.name(O_DPAR,'SYMBOL'));

:: dodaj obsługę
_act:='Wybierz'@@;
_nfo:='Wybór bieżącego wiersza'@;
_sel:="
   _SEL:=params_get().SEL;
   _SEL.NUM:=_SEL.size()+1;
   _SEL.REF:=#O_FPAR.ref();
   _SEL.SQL:=$O_FPAR.ref();
   _SEL.add();
   1
";
_grp:="
   {? O_FPAR.sel_size()=0
   || sel_exit()
   ?}
";
_cls:="
   sel_exit()
";
{? _one
|| O_FPAR.win_act(_wnd,,'Formuła',_act,,_nfo,_sel,_cls,1)
|| O_FPAR.win_act(_wnd,,'Formuła',_act,,_nfo,_sel,_grp,1,1,_cls)
?};
O_FPAR.win_act(_wnd,,'Szukaj');
O_FPAR.win_act(_wnd,,'Kolejność');
O_FPAR.win_act(_wnd,,'Rekord',,,,"O_FPAR.O_DPAR()");

:: ogranicz zakres zgodnie z parametrami i kontekstem:
:: - bieżąca firma
:: - już użyte
O_FPAR.cntx_psh();
O_FPAR.prefix();
O_FPAR.f_set(
   'O_DPAR(NAZWA)',
   'join O_DPAR using(O_FPAR.O_DPAR,O_DPAR.REFERENCE)',
   'O_FPAR.FIRMA=:_a and O_FPAR.WLACZONY=\'T\' and '
   'O_DPAR.TYP=\':_b\' and O_FPAR.REFERENCE not in ('
   '  select :_c.O_FPAR from :_c where :_c.IDREK=\':_d\''
   ')',
   exec('ref_firma','ustawienia'),_typ,{? _typ='Z' || O_ZPAR || O_RPAR ?},_rid
);

:: wybierz parametry
O_FPAR.win_sel(_wnd);
_ret.OK:=O_FPAR.select();

:: porządki
O_FPAR.cntx_pop();
O_FPAR.f_clear(1);
O_FPAR.win_del(_wnd);

_ret

:Sign Version 2.0 jowisz:1048 2021/04/09 15:26:29 22dac2e3bbe53494e655ca635ea16447e3abc6837176115902c4df47df912781f340bff4dfa0886382300686ad4cd5d82816ab1dcd9addb692e6c6fc9fa602d25da5bdd7e4fe41c440539c0f0c8a8fc5d3e3eccf3801ea8af68dffeb2c76f0559b8b43139175fb978fc6ff086c224de94efc237fd7687d79601e6b1a3c6a3122

:!UTF-8
(c) Macrologic S.A. Wszelkie prawa zastrzeżone
========================================================================================================================
Nazwa pliku: %mws_form.fml [2011]
Utworzony: 2015/09/16
Autor: [rr]
========================================================================================================================
Zawartość: Formuły do nadawania kodow kreskowych i obsługi magazynu z obsługą palet
========================================================================================================================

\dokpal_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: kod kreskowy wg palet
::  OLD: \dokpal_m/mws_form.fml
::----------------------------------------------------------------------------------------------------------------------
exec('dok_m','%mws_form')+(($DK_L.PAL)+4)


\dok_m
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: kod kreskowy wg dokumentu
::  OLD: \dok_m/mws_form.fml
::----------------------------------------------------------------------------------------------------------------------
_nag:=(($DK_L.DK().N)+11);
(3+_nag)+(_nag+4)+(($DK_L.DK().M)+4)


\auto_pal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: automatyczny wybor palet
::  OLD: \auto_pal/mws_form.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(2,'REF','INTEGER',''
       ,'PAL','STRING[16]',''
       ,'LOK','STRING[20]',''
       ,'TW','DATE',''
       ,'ILE','REAL',''
       ,'ILZ','REAL',''
       ,'LP','INTEGER','');

_typ:=ZK_N.MG().SP_REANL;

:: zbieramy dane

__palkpl.cntx_psh();
__palkpl.clear();
{? __palkpl.first()
|| {!
   |? __palkpl.cntx_psh();
      PAL.cntx_psh();
      PAL_POZ.cntx_psh();
      PAL.clear();
      {? (5+__palkpl.SQL)='palet' & __palkpl.KPL<>'T' & __palkpl.WYD<>'W' & PAL.seek(__palkpl.SQL)
      || {? PAL.AKT='N'
         || PAL_POZ.use('palet'+form(PAL.AR-2000,-2,0,'99'))
         || PAL_POZ.use('paletyp')
         ?};
         PAL_POZ.index('PAL');
         PAL_POZ.prefix(PAL.ref());
         {? PAL_POZ.first()
         || _tab.clear();
            _tab.blank();
            _tab.REF:=#__palkpl.ref();
            _tab.PAL:=$PAL.ref();
            _ilz:=0;
            _ile:=0;
            _tw:=date(0,0,0);
            {!
            |? {? PAL_POZ.ILP>0
               || {? _typ='A' | _typ='N' || {? PAL_POZ.ILP>_ile || _ile:=PAL_POZ.ILP ?} ?};
                  {? _typ='D' || {? PAL_POZ.ILP<_ile || _ile:=PAL_POZ.ILP ?} ?};
                  _ilz+=PAL_POZ.ILP;
                  {? PAL_POZ.TW<>date(0,0,0)
                  || {? _typ='T' | _typ='N' || {? PAL_POZ.TW>_tw || _tw:=PAL_POZ.TW ?} ?};
                     {? _typ='W' || {? PAL_POZ.TW<_tw || _tw:=PAL_POZ.TW ?} ?}
                  ?}
               ?};
               PAL_POZ.next()
            !};
            _tab.LOK:=__palkpl.LOK;
            _tab.ILE:=_ile;
            _tab.ILZ:=_ilz;
            _tab.TW:={? _tw=date(0,0,0) || {? _typ='T' | _typ='N' || date(3000,1,1) || date(1900,1,1) ?} || _tw ?};
            _tab.LP:=0;
            _tab.add()
         ?}
      ?};
      PAL.cntx_pop();
      PAL_POZ.cntx_pop();
      __palkpl.cntx_pop();

      __palkpl.next()
   !}
?};
__palkpl.cntx_pop();

:: porzadkujemy dane
{? _typ='S' || _ndx:=_tab.ndx_tmp('',0,'LOK',,,'TW',,,'ILZ',,,'ILE',,,'PAL',,)
|? _typ='A' || _ndx:=_tab.ndx_tmp('',0,'ILE',,,'ILZ',,,'TW',,,'LOK',,,'PAL',,)
|? _typ='D' || _ndx:=_tab.ndx_tmp('',0,'ILE',,1,'ILZ',,1,'TW',,,'LOK',,,'PAL',,)
|? _typ='T' || _ndx:=_tab.ndx_tmp('',0,'TW',,,'ILE',,1,'ILZ',,1,'LOK',,,'PAL',,)
|? _typ='W' || _ndx:=_tab.ndx_tmp('',0,'TW',,1,'ILE',,,'ILZ',,,'LOK',,,'PAL',,)
|? _typ='N' || _ndx:=_tab.ndx_tmp('',0,'TW',,,'ILE',,1,'ILZ',,1,'LOK',,,'PAL',,)
?};

:: zaznaczenie kolejnych __palkpl
__palkpl.cntx_psh();
_tab.index(_ndx);
_tab.prefix();
{? _tab.first()
|| {!
   |? __palkpl.clear();
      {? __palkpl.seek(_tab.REF,) || exec('wybzkkpl','zamsiw_palety',1,1) ?};
      _tab.next()
   !}
?};
exec('blkzkkpl','zamsiw_palety');
__palkpl.cntx_pop();
obj_del(_tab);
~~


\autorpal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.30]
:: OPIS: automatyczny wybor palet do rozpakowania
::  OLD: \autorpal/mws_form.fml
::----------------------------------------------------------------------------------------------------------------------
_tab:=tab_tmp(2,'REF','INTEGER',''
       ,'PAL','STRING[16]',''
       ,'ROZ','STRING[1]',''
       ,'LOK','STRING[20]',''
       ,'TW','DATE',''
       ,'ILE','REAL',''
       ,'ILZ','REAL',''
       ,'DKC','STRING[1]',''
       ,'LP','INTEGER',''
       ,'WYB','INTEGER','');

_typ:=ZK_N.MG().SP_REANL;

:: zbieramy dane
__palkpl.cntx_psh();
__pozkpl.cntx_psh();
__pozkpl.clear();
__pozkpl.prefix($__palkpl.ref());
{? __pozkpl.first()
|| {!
   |? _tab.clear();
      _tab.blank();
      _tab.REF:=#__pozkpl.ref();
      _tab.PAL:=__pozkpl.PQL;
      _tab.ROZ:={? __pozkpl.ILE<__pozkpl.ILW || 'T' || 'N' ?};
      _tab.LOK:=__pozkpl.NAZ;
      _tab.ILE:=__pozkpl.ILE;
      _tab.ILZ:=__pozkpl.ILE;
      _tab.TW:={? __pozkpl.TW=date(0,0,0) || date(3000,1,1) || __pozkpl.TW ?};
      _tab.DKC:=__pozkpl.DKC;
      _tab.LP:=0;
      __palkpl.prefix();
      _tab.WYB:=__palkpl.first() & __palkpl.find_tab('first','SQL',,'=',__pozkpl.PQL,'WYB',,'=',1);
      _tab.add();
      __pozkpl.next()
   !}
?};

:: porzadkujemy dane
{? _typ='S' || _ndx:=_tab.ndx_tmp('',0,'WYB',,,'DKC',,,'ROZ',,1,'LOK',,,'TW',,,'ILZ',,,'ILE',,,'PAL',,)
|? _typ='A' || _ndx:=_tab.ndx_tmp('',0,'WYB',,,'DKC',,,'ROZ',,1,'ILE',,,'ILZ',,,'TW',,,'LOK',,,'PAL',,)
|? _typ='D' || _ndx:=_tab.ndx_tmp('',0,'WYB',,,'DKC',,,'ROZ',,1,'ILE',,1,'ILZ',,1,'TW',,,'LOK',,,'PAL',,)
|? _typ='T' || _ndx:=_tab.ndx_tmp('',0,'WYB',,,'DKC',,,'ROZ',,1,'TW',,,'ILE',,1,'ILZ',,1,'LOK',,,'PAL',,)
|? _typ='W' || _ndx:=_tab.ndx_tmp('',0,'WYB',,,'DKC',,,'ROZ',,1,'TW',,1,'ILE',,,'ILZ',,,'LOK',,,'PAL',,)
|? _typ='N' || _ndx:=_tab.ndx_tmp('',0,'WYB',,,'DKC',,,'ROZ',,1,'TW',,,'ILE',,1,'ILZ',,1,'LOK',,,'PAL',,)
?};

:: zaznaczenie kolejnych __pozkpl
_nrk:=0;
_tab.index(_ndx);
_tab.prefix();
{? _tab.first()
|| {!
   |? __pozkpl.clear();
      {? __pozkpl.seek(_tab.REF,)
      || _nrk+=1;
         __pozkpl.NRK:=_nrk;
         __pozkpl.put(1)
      ?};
      _tab.next()
   !}
?};
__palkpl.cntx_pop();
__pozkpl.cntx_pop();
obj_del(_tab);
~~


\pal_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: kod kreskowy dla palety
::  OLD: \pal_kod/mws_form.fml
::----------------------------------------------------------------------------------------------------------------------
_ref:=#PAL.ref();
_buf:='.'+form(_ref,-8,0,'99');
PAL.cntx_psh();
PAL.index('PAL');
{? PAL.find_key(_buf,_buf) | exec('kodkINarch','magdok_palety',_buf)
|| _i:=0;
   {!
   |? _i+=1;
      _wyn:=_buf+form(_i,,0,'99');
      PAL.index('PAL');
      PAL.find_key(_wyn,_wyn) | exec('kodkINarch','magdok_palety',_wyn)
   !}
|| _wyn:=_buf
?};
PAL.cntx_pop();
_wyn


\pal_bez
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [12.10]
:: OPIS: kod kreskowy dla palety nieoznaczonej (kod tymczasowy)
::  OLD: \pal_bez/mws_form.fml
::----------------------------------------------------------------------------------------------------------------------
_dlg:=4;
_wyn:='';
PAL.cntx_psh();
PAL.index('BEZOZN');
PAL.prefix('T'+EANX.BEZOZN);
_nr:={? PAL.last() || #(PAL.KODK+_dlg) || 0 ?}+1;
PAL.cntx_pop();
_wyn:=EANX.BEZOZN+form(_nr,-_dlg,0,'99');
_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:15:44 4e83334d3678e63f30f52c0cc12e075e321f37a3911ef01acea7652885546288e7ade6d2ca5741eca568c565d7f546bf8602c20a91dd11bc54566a082cb4f8b8517a587cee1c60647af14bbb0fc692b45a2bee90f6c14c6f37d4f9c79d5773078652c819629e46a60479cf854b9ecb8a30a78810bbf678c7d46bd04998da2d6e

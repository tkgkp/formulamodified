:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: Nowy1.fml
:: Utworzony: 24.03.2016
:: Autor: AWI
::======================================================================================================================
:: Zawartość: Formuły pomocniczne dla kasy
::======================================================================================================================


\nr_dok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: nadanie numeru dokumentu wedlug formuly w parametrach stanowiska
::       2 pierwsze argunenty sa podnoszone do duzych liter
::       Wazne sa 3 pierwsze znaki multikasy
::   WE: _a - typ dokumentu - string 3
::       _b - typ numeracji   - CONT - ciagla w roku
::                            - NONCONT  - nieciagla (od jeden w kazdym miesiacu)
::       [_c] -   1 - numeracja wspolna dla wszystkich kas
::                2 - numeracja wspolna dla wszystkich kas w ramach multikasy
::                   (UWAGA na uzycie przy kasach w wielu multikasach)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? _<2
|| FUN.info('Nieprawidłowa ilość parametrów!\n-----Formuła >>nr_dok<<-----');
   0
||
   {? _<3 || _c:=0 ?};
   _mkasa:='';
   {? _c=2 & var_pres('__multik')<0
   || KUSERUPR.cntx_psh;
      KUSERUPR.index('KU');
      KUSERUPR.prefix(null,STANKAS.KOD);
      {? KUSERUPR.first
      || {!
         |?
            {? KUSERUPR.MKASA<>null
            || _mkasa:=3+KUSERUPR.MKASA().NAZ; 0
            || KUSERUPR.next
            ?}
         !}
      ?};
      KUSERUPR.cntx_pop
   |? _c=2
   || _mkasa:=3+MKASA.NAZ
   ?};
   {? _c=2 & _mkasa=''
   || FUN.emsg('Nieprawidłowy  trzeci parametr (2). /n Dla stanowiska bez przypisania do multikasy nie można go używać');
      return(0)
   ?};
   {? ~-_b='CONT' | ~-_b='NONCONT'
   || _a:=~-_a;
      {? _c
      || _numnaz:=NUM_DOK.name;
         NUM_DOK.use('knd'+{? _c=2 || ('___'+_mkasa)+3 || '___' ?}+(_numnaz+2))
      ?};
      NUM_DOK.index('TYP_OKR');
      _x:={? _b='CONT'
          || form(DOKUMENT.DATA~1,-4,0,'99')+'RR'
          || form(DOKUMENT.DATA~1,-4,0,'99')+form(DOKUMENT.DATA~2,-2)
          ?};
      _sym:=_a;
      NUM_DOK.prefix(_sym,_x);
      {? ~NUM_DOK.first()
      || NUM_DOK.blank();
         NUM_DOK.TYP_DOK:=_sym;
         NUM_DOK.OKRES:=_x;
         NUM_DOK.add()
      ?};
      NUM_DOK.NUMER+=1;
      NUM_DOK.put();
      _wyn:=NUM_DOK.NUMER;
      {? _c
      || NUM_DOK.use(_numnaz)
      ?};
      _wyn
   || FUN.info('Nieprawidłowy drugi parametr!\nDozwolone parametry >>CONT<< i >>NONCONT<<\n-----Formuła >>nr_dok<<-----');
      0
   ?}
?}


\nr_okres
::----------------------------------------------------------------------------------------------------------------------
::  UTW: - [--.--]
:: OPIS: nadanie numeru okresu dla dokumentu
::       używane do wyznazcenia odpowiddniego prefiksu dla numeru
::       w przypadku numeracji nieciagłej w roku
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
#(form(DOKUMENT.DATA~2)+(form(DOKUMENT.DATA~1)+2))

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:02 55a7291266195a2a5ad05567ad58edf58f0f9ac3dfa6d4c33f100d8f94c96278ee1e78c9cdad93b61662a2ff3a76d9ce4d0ba0cf770a667db2750089c1e56b4ee86bfd5397e1747d67c86822ee74760f47c07162bc75e16f399920e0fe07b7c1456311618cb092488565a5123784fbba07442d616730ccad5a6c4847f4f5fe13

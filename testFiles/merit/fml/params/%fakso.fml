:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %fakso.fml
:: Utworzony: 27.06.2017
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły do obsługi informacji dodatkowych
::======================================================================================================================


\fakz2fakso
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.28]
:: OPIS: Przepisuje z pola wartość do rozpisania do wartości do informacji dodatkowych
::       wg schematu <TYTUŁ:WARTOŚĆ>
::       Jeżeli dana wartość nie zostanie odnaleziona to też i nie zostanie uzupełniona
::   WY: 0-zawsze
:: UWAGA Formułę należy uruchomić w schemacie danych dodatkowych jako formułę warunku o ostatniej liczbie porządkowej.
::       Formuła zawsze zwraca 0 czyli nie
::       Formuła dotyczy danej pozycji faktury - FAP
::----------------------------------------------------------------------------------------------------------------------
_opis:='';
_fap:=FAP.ref();
:: pobranie wartości
FAKZ2FAP.cntx_psh();
FAKZ.cntx_psh();
FAKZ2FAP.index('FAP');
FAKZ2FAP.prefix(_fap);
{? FAKZ2FAP.first() & ((FAKZ.prefix(); FAKZ.seek(FAKZ2FAP.FAKZ)) & FAKZ.memo_lin('FAKSO')<>'\n')
|| FAKZ.memo_get(,'FAKSO');
   _opis:=FAKZ.memo_txt(0,1,'FAKSO')
?};
FAKZ2FAP.cntx_pop();
FAKZ.cntx_pop();
:: przepisanie wartości
{? _opis<>''
|| FAP.cntx_psh();
   FAKSO.cntx_psh();
   _be:='<';
   _ae:='>';
   _sp:=':';
   _ok:=1;
   {!
   |? _tyt:='';
      _war:='';
      _wsk:=_opis*_be;
      {? _wsk
      || _opis:=_wsk-_opis;
         _wsk:=_opis*_sp;
         {? _wsk
         || _tyt:=form((_wsk-1)+_opis);
            _opis:=_wsk-_opis;
            _wsk:=_opis*_ae;
            {? _wsk
            || _war:=form((_wsk-1)+_opis);
               _opis:=_wsk-_opis
            ?}
         ?}
      || _ok:=0
      ?};
      {? _tyt<>'' & _war<>''
      || FAKSO.index('TFAP');
         FAKSO.prefix(FAP.ref(),_tyt,);
         {? FAKSO.first()
         || FAKSO.O:=_war;
            FAKSO.put(1)
         ?}
      ?};
      _ok & +_opis
   !};
   FAP.cntx_pop();
   FAKSO.cntx_pop()
?};
0

:Sign Version 2.0 jowisz:1028 2019/06/07 16:01:02 75a1da5bfe6c0275400c721ea169c9cd749b29538eb55752fddb9e18d9e25870b8cb28e95eb97ee5045c9b97c0b118def4aa05dd7e14a397088d1c9a7c7c6e36cce410ba1ac23c6798a9b37f168d36cfca92087dd153685d53e72edfffdb579c189aae2013a279360aca1e0042faa68ed6f776e969a900ab2e15816d7cc903b1

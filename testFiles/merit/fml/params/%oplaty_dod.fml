:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %oplaty_dod.fml
:: Utworzony: 21.12.2021
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Formuły dla obliczeń opłat dodatkowych
::======================================================================================================================


\calculat_SUGAR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Obliczenie opłaty cukrowej
::   WE: [_a] - wskazanie na tabelę
::----------------------------------------------------------------------------------------------------------------------
_Tab:={? var_pres('_a')=type_of(FIRMA) || _a || cur_tab() ?};
_tax:=0;

_ile:=_Tab.IL;

_taxLitr:=0;
_taxPlus:=0;

:: ilosć w litrach
_ilosc:=_ile*__aod.PJM $3;  _intIL:=int(_ilosc);    _fracIL:=frac(_ilosc);
_plusIL:=ceil(_ile);

:: zawartość cukru
{? __aod.PJUI>=__aod.PROC_JUI || _wsugA:=0;           _wsugB:=__aod.WSUG
|? __aod.WSUG<=5              || _wsugA:=__aod.WSUG;  _wsugB:=0
                              || _wsugA:=5;           _wsugB:=__aod.WSUG-5
?};
roundmet(2);
_taxLitr:=(ceil(_wsugA/5)+(_wsugB*0.1))*__aod.TAX_SUG;

:: zawartość kofeiny lub tauryny
{? __aod.TAURYNA='T' | __aod.KOFEINA='T' || _taxPlus+=__aod.TAX_COF ?};

:: obliczenie podatku dla ilości
{? (_taxLitr+_taxPlus)>=__aod.TAX_MAX
|| _tax:=__aod.TAX_MAX*_ilosc $2
|| _tax+=_taxLitr*_intIL $2;
   _tax+=_taxLitr*_fracIL $2;
   _tax+=_taxPlus*_plusIL $2
?};
roundmet(5);
_tax


\valid_SUGAR_M
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Formuła walidacji dla danych indeksu - opłata cukrowa
::----------------------------------------------------------------------------------------------------------------------
_res:='';

_rodz:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.RV",'L');
_prec:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.RP",0);
_ctrl:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.CTRL",'');

{? _rodz='R' & _ctrl='A' & TAXI.VALR<=0
|| FUN.info('Należy podać wartość większą od zera.'@);
   _res:='VALR'
|? _rodz='R' & _ctrl='B' & TAXI.VALR<0
|| FUN.info('Należy podać wartość większą lub równą zero.'@);
   _res:='VALR'
|? _rodz='S' & _ctrl='T' & TAXI.VALS=''
|| FUN.info('Należy podać wartość atrybutu.'@);
   _res:='VALS'
|? _rodz='D' & _ctrl='T' & TAXI.VALD=date(0,0,0)
|| FUN.info('Należy podać niezerową datę.'@);
   _res:='VALD'
|? _rodz='T' & _ctrl='T' & TAXI.VALT=''
|| FUN.info('Należy podać niezerowy czas.'@);
   _res:='VALT'
|? _rodz='T' & ~((';TN'*TAXI.VALL)>1)
|| FUN.info('Należy wybrać wartość logiczną.'@);
   _res:='VALL'
?};
_res


\calculat_MONKEY
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Obliczenie opłaty cukrowej
::   WE: [_a] - wskazanie na tabelę
::----------------------------------------------------------------------------------------------------------------------
_Tab:={? var_pres('_a')=type_of(FIRMA) || _a || cur_tab() ?};
_tax:=0;

_ile:=_Tab.IL;

_taxLitr:=0;

{? __aod.PJM<=__aod.MAX_POJ & __aod.PALK>0 & __aod.PALK<=100
||
:: ilosć w litrach
   _ilosc:=_ile;  _intIL:=int(_ilosc);    _fracIL:=frac(_ilosc);

:: zawartość alkoholu
   _procent:=__aod.PALK*0.01;
   roundmet(2);
   _taxLitr:=__aod.PJM*_procent*__aod.TAX_MON;

:: obliczenie podatku dla ilości
   _tax+=_taxLitr*_intIL $2;
   _tax+={? _fracIL>0 || _taxLitr || 0 ?}$2
?};
roundmet(5);
_tax


\valid_MONKEY_M
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Formuła walidacji dla danych indeksu - opłata za małpki
::----------------------------------------------------------------------------------------------------------------------
_res:='';

_rodz:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.RV",'L');
_prec:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.RP",0);
_ctrl:=exec('FindInSet','#table','TAXT','TAXS',TAXI.TAXT,TAXI.TAXS,"@.TAXT.CTRL",'');

{? _rodz='R' & _ctrl='A' & TAXI.VALR<=0
|| FUN.info('Należy podać wartość większą od zera.'@);
   _res:='VALR'
|? _rodz='R' & _ctrl='B' & TAXI.VALR<0
|| FUN.info('Należy podać wartość większą lub równą zero.'@);
   _res:='VALR'
|? _rodz='R' & TAXI.TAXT='PALK' & TAXI.VALR>=100
|| FUN.info('Należy podać wartość mniejszą od 100\%.'@);
   _res:='VALR'
|? _rodz='S' & _ctrl='T' & TAXI.VALS=''
|| FUN.info('Należy podać wartość atrybutu.'@);
   _res:='VALS'
|? _rodz='D' & _ctrl='T' & TAXI.VALD=date(0,0,0)
|| FUN.info('Należy podać niezerową datę.'@);
   _res:='VALD'
|? _rodz='T' & _ctrl='T' & TAXI.VALT=''
|| FUN.info('Należy podać niezerowy czas.'@);
   _res:='VALT'
|? _rodz='T' & ~((';TN'*TAXI.VALL)>1)
|| FUN.info('Należy wybrać wartość logiczną.'@);
   _res:='VALL'
?};
_res



\calculat_liquid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Obliczenie akcyzy na liquidy
::   WE: [_a] - wskazanie na tabelę
::----------------------------------------------------------------------------------------------------------------------
_Tab:={? var_pres('_a')=type_of(FIRMA) || _a || cur_tab() ?};
_tax:=0;

_ile:=_Tab.IL;

_taxLitr:=0;

{? __aod.PJM>0
||
:: ilosć w mililitrach
   _ilosc:=_ile*__aod.PJM $3;

   _taxLitr:=_ilosc*__aod.TAX_LIQ;

:: obliczenie podatku dla ilości
   _tax+=_taxLitr $2
?};
_tax

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:39 345c24f156800e00df8d09ecd0a33e5bd574415cb8401d791d6221f140fd7e02acf30204a9e377414203a8f1b04363eaa864b83be42102e7efe02da9310228e4c2b3eb16d24f1788b331a6660d1a7652ece16c66e4478e879bc200d44dd3f7739dc7962a7cf146120d697b2f4d6e656451f05d0fc8c00e4604f38d2a8599fab4

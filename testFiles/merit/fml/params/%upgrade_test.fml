:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %upgrade_test.fml
:: Utworzony: 12.10.2017
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Środowisko testowe zadań potransferowych.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Główna formuła środowiska testowego zadań potransferowych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
on_error(0);

_ret:=exec('_get_file','%upgrade_test');
{? _ret.MSG<>''
|| return(_ret.MSG)
?};

_vp:=var_pres('__UPG');
{? _vp>100
|| _kopiaUPG:=__UPG;
   obj_del(__UPG)
|? _vp>=0
|| _kopiaUPG:=__UPG
?};

exec('_run','%upgrade_test',_ret.VER);

obj_del(__UPG);
{? _vp>=0
|| __UPG:=_kopiaUPG
|| &__UPG
?};
''


\_get_file
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Formuła pozwala wybrać plik z zadaniami potransferowymi.
::   WE:
::   WY: Tablica elementów nazwanych (wypełnianie jest jedno z pól):
::          VER - Nazwa pliku (bez rozszerzenia) wybranej wersji.
::          MSG - Przyczyna braku wyboru.
::----------------------------------------------------------------------------------------------------------------------
_ret:=obj_new('MSG','VER');
_ret.MSG:=_ret.VER:='';

_FILE:=sql(
   'select substr(FILENAME,9,char_length(FILENAME)-12) as WERSJA '
   'from :_a '
   'order by 1 desc',files('upgrade_*.fml',1)
);
{? ~_FILE.first()
|| _ret.MSG:='Brak plików z zadaniami potransferowymi.'@;
   return(_ret)
?};

{!
|? {? exec('is_fun','#file','upgrade_%1'[_FILE.WERSJA],'init')
   || _FILE.next()
   || _FILE.del(,1)=2
   ?}
!};
_ws:=_FILE.size()+3;
{? _ws<5
|| _ws:=5
|? _ws>20
|| _ws:=20
?};
_ws:=_FILE.mk_sel('Pliki z zadaniami potransferowymi'@,,,'#upgver',,,_ws,,'U');
_FILE.win_fld(_ws,,'WERSJA',,,12,,,'Wersja'@,,'Obsługiwana wersja'@);
_FILE.win_act(_ws,,'Formuła','Wybierz'@,,,"sel_exit()",,1,,,,'W');
_FILE.win_sel(_ws);
_FILE.win_edit(_FILE.mk_edit(,1));
{? _FILE.select()
|| _ret.VER:='upgrade_%1'[_FILE.WERSJA]
|| _ret.MSG:='Zrezygnowano z wyboru wersji.'@
?};
_ret


\_run
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [21.37]
:: OPIS: Formuła tworząca i uruchamiająca środowiskowe testowe zadań potransferowych.
::   WE: _a [STRING] - Nazwa pliku (bez rozszerzenia) wybranej wersji.
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_fn:=_a;

__UPG:=obj_new('fn','MSG','TASK','add_task','msg','ctrl_run','off_task','id_ver');

__UPG.fn:=_fn;
:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
__UPG.MSG:=obj_new('TAB','ws');

__UPG.MSG.TAB:=tab_tmp(2,
   'TASK','STRING[16]','SQL-ref procedury'@,
   'LP','INTEGER','Lp.'@,
   'MSG','STRING[255]','Treść'@,
   'OK','STRING[1]','OK?'@
);

__UPG.MSG.ws:=__UPG.MSG.TAB.mk_sel('Komunikaty'@,'P',,'#upgmsg',,,,,'U');
:: 82 - z MacroBUILDER'a.
__UPG.MSG.TAB.win_fld(__UPG.MSG.ws,,'MSG',,,82,,,,);
__UPG.MSG.TAB.win_fld(__UPG.MSG.ws,,'OK',,,-3,,,,,'Długość komunikatu nie przekracza 255 znaków [T/N]'@,2,,"'T'","'N'");

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
__UPG.TASK:=obj_new('TAB','ws','we','info');

__UPG.TASK.TAB:=tab_tmp(1,
   'LP','INTEGER','Lp.'@,
   'NAME','STRING[%1]'[$MS.fld_len(UPG_TASK,'SYMBOL')],'Symbol zadania'@,
   'DESCR','SYS_MEMO','Opis zadania'@,
   'AUTO','STRING[1]','Automatyczne'@,
   'MF','STRING[1]','Wielofirmowe'@,
   'DOMAIN','STRING[3]','Dziedzina'@,
   'VER','INTEGER','Wersja'@,
   'REQUIRED','STRING[1]','Wymagane'@,
   'MANUAL','STRING[31]','Zadanie manualne'@,
   'NOFIRM','INTEGER','Bez 000'@,
   'ONLYACT','STRING[1]','Zadanie aktualizacyjne'@,
   'COMMENT','SYS_MEMO','Stan definicji'@,
   'OK','STRING[1]','Zapis poprawny'@,
   'STAN','STRING[1]','Wykonane'@,
   'RESULT','INTEGER','Wynik przetwarzania'@
);

:: Okno wertowania.  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
__UPG.TASK.ws:=__UPG.TASK.TAB.mk_sel('Zadania potransferowe'@,,,'#upgprocwer',,,,,'U');
__UPG.TASK.TAB.win_fld(__UPG.TASK.ws,,'STAN',,,,,,,,'Zadanie wykonane [T/N]'@,2,,"'T'","'N'");
__UPG.TASK.TAB.win_fld(__UPG.TASK.ws,,'NAME',,,,,,,,'Symbol zadania potransferowego'@);
__UPG.TASK.TAB.win_fld(__UPG.TASK.ws,,'DOMAIN',,,,,,,,'Dziedzina'@);
__UPG.TASK.TAB.win_fld(__UPG.TASK.ws,,'MF',,,,,,,,'Zadnie wielofirmowe'@,2,,"'T'","'N'");
__UPG.TASK.TAB.win_fld(__UPG.TASK.ws,,'VER',,,,,,,,'Wersja zadnie'@);
__UPG.TASK.TAB.win_fld(__UPG.TASK.ws,,'OK',,,-4,,,'OK'@,,'Zapis poprawny [T/N]'@,2,,"'T'","'N'");
__UPG.TASK.TAB.win_act(__UPG.TASK.ws,,'Formuła','Uruchom'@@,,'Uruchomienie zadania potransferowego'@,"
   {? __UPG.TASK.TAB.OK='T' &
      (__UPG.TASK.TAB.sel_size() | FUN.ask('Czy na pewno chcesz uruchomić zadanie \"%1\"?'@[__UPG.TASK.TAB.NAME]))
   || __UPG.MSG.TAB.prefix($__UPG.TASK.TAB.ref(),);
      {? __UPG.MSG.TAB.first()
      || {!
         |? __UPG.MSG.TAB.del()
         !}
      ?};
      __UPG.MSG.TAB.prefix();
      _ret:=exec(__UPG.TASK.TAB.NAME,__UPG.fn);
      __UPG.TASK.TAB.STAN:='T';
      __UPG.TASK.TAB.RESULT:={? type_of(_ret)=type_of(0) || _ret || -99 ?};
      __UPG.TASK.TAB.put()
   ?}",,
   1,
   1,
   "FUN.ask('Czy na pewno chcesz uruchomić WSZYSTKIE zaznaczone zadania?'@)",,
   'U'
);
__UPG.TASK.TAB.win_act(__UPG.TASK.ws,,'Rekord',,,,"
   {? _a
   || _ga:={? __UPG.TASK.TAB.sel_size() | __UPG.TASK.TAB.OK='T' || '' || 'U' ?};
      __UPG.TASK.TAB.actions_grayed(__UPG.TASK.ws,_ga)
   ?}
");
:: Podstawowe okno redagowania.  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
__UPG.TASK.we:=__UPG.TASK.TAB.mk_edit('Formuła potransferowa [%1.fml]'@[__UPG.fn],,'#upgprocred');
__UPG.TASK.TAB.win_esep(__UPG.TASK.we,'Dane podstawowe'@);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'LP');
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'NAME',,,33);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'DESCR',,,64,-5);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'AUTO',,,,,,,,,
   'check-box','check_label="%1"'['Tak, formuła automatyczna'@],"'T'","'N'"
);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'MF',,,,,,,,,
   'check-box','check_label="%1"'['Tak, formuła wielofirmowa'@],"'T'","'N'"
);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'DOMAIN',,,33);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'VER');
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'REQUIRED',,,,,,,,,
   'check-box','check_label="%1"'['Tak, zadanie jest wymagalne'@],"'T'","'N'"
);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'MANUAL',,,33);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'NOFIRM');
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'ONLYACT',,,,,,,,,
   'check-box','check_label="%1"'['Tak, zadanie aktualizacyjne'@],"'T'","'N'"
);
__UPG.TASK.TAB.win_esep(__UPG.TASK.we,'Status'@);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'COMMENT',,,64,-5);
__UPG.TASK.TAB.win_efld(__UPG.TASK.we,,'OK',,,,,,,,,
   'check-box','check_label="%1"'['Tak, zapis jest poprawny'@],"'T'","'N'"
);
__UPG.TASK.TAB.win_edit(__UPG.TASK.we);
:: Dodatkowe okno redagowania (element okna grupowego).  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
__UPG.TASK.info:=__UPG.TASK.TAB.mk_edit(,,'#upgprocinfo');
__UPG.TASK.TAB.win_esep(__UPG.TASK.info,'Informacje dodatkowe'@);
__UPG.TASK.TAB.win_efld(__UPG.TASK.info,,'DESCR',,,64,-4);
__UPG.TASK.TAB.win_efld(__UPG.TASK.info,,'COMMENT',,,64,-4);
__UPG.TASK.TAB.win_efld(__UPG.TASK.info,,'RESULT',,,,,,,,,'radio-buttons',,
   'Nieprawidłowy typ wyniku'@,"-99",
   'Całość przeszła bez błędów'@,"1",
   'Całość przeszła z uwagami'@,"-1",
   'Został stwierdzony błąd'@,"0"
);

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

_grp:=__UPG.TASK.TAB.grp_make('Środowisko testowe zadań potransferowych [%1.fml]'@[__UPG.fn],,'#upggrp',,,,,'normal');
__UPG.TASK.TAB.grp_sel(_grp,,__UPG.TASK.ws,,"
   grp_edisp(__UPG.TASK.TAB,__UPG.TASK.info);
   grp_disp(__UPG.MSG.TAB,__UPG.MSG.ws)
");

__UPG.TASK.TAB.grp_splt(_grp,,'vertical','info');
__UPG.TASK.TAB.grp_edit(_grp,,__UPG.TASK.info,,,"
   __UPG.TASK.TAB.memo_get(,'DESCR');
   __UPG.TASK.TAB.memo_get(,'COMMENT');
   __UPG.TASK.TAB.efld_opt(__UPG.TASK.info,'enable=%1'[$(__UPG.TASK.TAB.STAN='T')],,'RESULT')
   ",,,
   'maximized'
);
__UPG.TASK.TAB.grp_splt(_grp,'info','horizontal','msg');
__UPG.TASK.TAB.grp_sel(_grp,__UPG.MSG.TAB,__UPG.MSG.ws,,,,,,"
   __UPG.MSG.TAB.prefix($__UPG.TASK.TAB.ref(),);
   __UPG.MSG.TAB.first()
");

__UPG.TASK.TAB.win_sel(_grp);

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
__UPG.add_task:="
:: _a name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
:: _b auto     [STRING] - formuła automatyczna: T [domyślnie] / N
:: _c mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
:: _d domain   [STRING] - kod dziedziny produktowej (informacyjnie)
:: _e ver      [NUMBER] - wersja formuły
:: _f required [STRING] - czy zadanie musi być wykonane
:: _g manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
:: _h nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
:: _i onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
   _lp:=.TASK.TAB.size()+1;
   .TASK.TAB.memo_set(,'DESCR');
   _comment:='';
   .TASK.TAB.blank();
   .TASK.TAB.LP:=_lp;
   .TASK.TAB.NAME:=_a;
   .TASK.TAB.OK:='N';
   {? ~exec('is_fun','#file',.fn,.TASK.TAB.NAME)
   || _comment+='W pliku %1.fml brak formuły %2.'@[.fn,.TASK.TAB.NAME]+'\n'
   ?};
   {? ~exec('is_fun','#file',.fn,'%1_desc'[.TASK.TAB.NAME])
   || _comment+='W pliku %1.fml brak formuły %2.'@[.fn,'%1_desc'[.TASK.TAB.NAME]]+'\n'
   |? _desc:=exec('%1_desc'[.TASK.TAB.NAME],.fn);
      type_of(_desc)<>type_of('')
   || _comment+='Nieprawidłowy rezultat wykonania formuły %1_desc z pliku %2.fml.'@[.TASK.TAB.NAME,.fn]+'\n'
   || .TASK.TAB.memo_set(_desc,'DESCR')
   ?};
   .TASK.TAB.AUTO:={? var_pres('_b')=type_of('') & (_b='T' | _b='N') || _b || 'T' ?};
   .TASK.TAB.MF:={? var_pres('_c')=type_of('') & (_c='T' | _c='N') || _c || 'T' ?};
   .TASK.TAB.DOMAIN:={? var_pres('_d')=type_of('') || _d || '???' ?};
   .TASK.TAB.VER:={? var_pres('_e')=type_of(0) || _e || 0 ?};
   .TASK.TAB.REQUIRED:={? var_pres('_f')=type_of('') & (_f='T' | _f='N') || _f || 'N' ?};
   .TASK.TAB.MANUAL:={? var_pres('_g')=type_of('') || _g || '' ?};
   .TASK.TAB.NOFIRM:={? var_pres('_h')=type_of(0) || _h || 0 ?};
   .TASK.TAB.ONLYACT:={? var_pres('_i')=type_of('') & (_i='T' | _i='N') || _i || 'N' ?};
   {? .TASK.TAB.MANUAL<>'' & ~exec('is_fun','#file',.fn,.TASK.TAB.MANUAL)
   || _comment+='Brak zadania manualnego %1.'@[.TASK.TAB.MANUAL]+'\n'
   ?};
   {? _comment=''
   || .TASK.TAB.OK:='T'
   ?};
   .TASK.TAB.STAN:='N';
   .TASK.TAB.memo_set(_comment,'COMMENT');
   .TASK.TAB.add() & .TASK.TAB.memo_put(,'DESCR') & .TASK.TAB.memo_put(,'COMMENT')
";

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
__UPG.msg:="
:: _a [STRING] - Treść komunikatu.
   _proc:=$.TASK.TAB.ref();
   .MSG.TAB.prefix(_proc,);
   .MSG.TAB.blank();
   .MSG.TAB.TASK:=_proc;
   .MSG.TAB.LP:=.MSG.TAB.size()+1;
   .MSG.TAB.MSG:=_a;
   .MSG.TAB.OK:={? +_a<=255 || 'T' || 'N' ?};
   _ret:=.MSG.TAB.add();
   .MSG.TAB.prefix();
   grp_disp(.MSG.TAB,.MSG.ws);
   _ret
";

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

__UPG.ctrl_run:="1";
__UPG.off_task:="";
__UPG.id_ver:="";

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

exec('init',__UPG.fn);
__UPG.TASK.TAB.select()

:Sign Version 2.0 jowisz:1045 2023/06/02 09:38:10 b3c0146aa210c65e277817366b86844f6d894e6c5d6a6a88a90369be64f6600eb974bbb0b7b2e0b79231537dbeaaab2d48c2021f69df3041dcd30cd4442f7ce490214972174a27446a45df27368c938d7cbab672cc856b5d73a6b72f1a8ac4a773477dd78c447acb38574908cec2cc2e812e2e668aecc00c489e2ce307329bee

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %magdok.fml
:: Utworzony: 08.06.2018
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Magazynowe formuły parametryzacyjne
::======================================================================================================================


\dost2kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: zestaw dostaw do korekty
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_in:=params_get().in;
_mp:=params_get().mp;

_faks:=_in.p01;
_mp.grpkey();

params_exec('FindAndGet','#table',FAKS,_faks,,"exec('korLubKz','%magdok')");

_mp.done()


\korLubKz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: korekta lub korekta zbiorcza
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_param:=obj_new('TAB');
_param.TAB:=tab_tmp(1
   ,'PRDK'     ,'STRING[16]',
   ,'FAP2DKIL' ,'REAL',
   ,'DKIL'     ,'REAL',
   ,'FAPIL'    ,'REAL',
   ,'KORIL'    ,'REAL',
   ,'CN'       ,'REAL',);
{? FAKS.WHERE='K'
:: korekta zbiorcza
||
   _kz_uid:=exec('FindAndGet','#table',FAKS,FAKS.ref(),,"FAKS.uidref()",'');
   FAKS_KZF.cntx_psh();
   FAKS_KZF.index('KZ_UID');
   FAKS_KZF.prefix(_kz_uid);
   _loop:=FAKS_KZF.first();
   {!
   |? _loop
   |!
      {? FAKS_KZF.KOR<>''
      ||
         params_exec('FindAndGet','#table',FAKS,FAKS_KZF.KOR,,"exec('korekta','%magdok',_param)",,_param)
      ?};
      _loop:=FAKS_KZF.next()
   !};
   FAKS_KZF.cntx_pop()

|? FAKS.T().KOR='T'
:: zwykła korekta
||
   params_exec('korekta','%magdok',_param)
?}


\korekta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: korekta lub korekta zbiorcza - dodanie pozycji kulcza grupującego
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_a.TAB;

_mp:=params_get().mp;

FAP.cntx_psh();
FAP.use((FAP.name()-3)+(ref_name(FAKS.ref())+3));
FAP.index('FAP');
FAP.prefix(FAKS.ref());
_loop:=FAP.first();
{!
|? _loop
|!
   {? FAP.CN
   ||
      exec('wysw_kor','faktury_poz');
::    wyznaczyć dostawy z korygowanej pozycji faktury
      {? var_press('_Tab')=type_of(~~)
      || _Tab:=exec('fap2dk','%magdok',FAP.ref(),_Tab,FKOR.BCN)
      || exec('fap2dk','%magdok',FAP.ref(),_Tab,FKOR.BCN)
      ?};
      _loop:=_Tab.first();
      {!
      |? _loop
      |!
         _Tab.KORIL+=FKOR.BIL;
         _Tab.put();
         {? _Tab.FAP2DKIL=_Tab.DKIL & _Tab.FAPIL=_Tab.KORIL & _Tab.CN
         ||
::          ilość z dostawy musi być równa ilości z powiązanych faktur
::          ilość z faktur powiązanych z dostawą musi być równa ilości z korekt do nich wystawionych
::          cena z korekt powiązanych z dostawą musi być taka sama
            _mp.grpkeyAdd(FAP.uidref()+_Tab.PRDK)
         ?};
         _loop:=_Tab.next()
      !}
   ?};
   _loop:=FAP.next()
!};
FAP.cntx_pop()


\fap2dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: Zwraca tabelę z listą dostaw powiązanych z pozycją faktury zakupowej
::   WE: _a - FAP.ref()
::       _b - tabela z dostawami pierwotnymi
::       _c - cena netto
::   WY: _Tab
::----------------------------------------------------------------------------------------------------------------------
_Tab:=_b;
_cn:=_c;
_lksql:=exec('FindAndGet','#table',FAKS,FAP.FAKS,,"LKSQL");
{? _lksql=''
||
   FAP2DK.cntx_psh();
   FAP2DK.use((FAP2DK.name()-1)+(6+FAP.name()+1));
   FAP2DK.index('FAPDK');
   FAP2DK.prefix($FAP.ref());
   _loop:=FAP2DK.first();
   {!
   |? _loop
   |!
      {? _Tab.find_key(FAP2DK.DK)
      ||
         _Tab.FAP2DKIL+=FAP2DK.IL;
         _Tab.FAPIL+=FAP.IL;
         {? _Tab.CN & _Tab.CN<>_cn || _Tab.CN:=0 ?};
         _Tab.put()
      ||
         _Tab.PRDK:=FAP2DK.DK;
         _Tab.FAP2DKIL:=FAP2DK.IL;
         _Tab.DKIL:=exec('FindAndGet','#table',DK,FAP2DK.DK,,"DK.IL",0);
         _Tab.FAPIL:=FAP.IL;
         _Tab.KORIL:=0;
         _Tab.CN:=_cn;
         _Tab.add()
      ?};
      _loop:=FAP2DK.next()
   !};
   FAP2DK.cntx_pop()
||
:: przejść do pozycji przed korektą
   _lksql:=exec('FindAndGet','#table',FAKS,_lksql,,,null());
   {? _lksql
   ||
      FAP.cntx_psh();
      FAP.use((FAP.name()-3)+(ref_name(_lksql)+3));
      FAP.index('FAP');
      FAP.prefix(_lksql,FAP.POZ);
      {? FAP.first() & FAP.POZP
      ||
         FAP.prefix(_lksql,FAP.POZP)
      ?};
      {? FAP.first()
      ||
         exec('fap2dk','%magdok',FAP.ref(),_Tab,_cn)
      ?};
      FAP.cntx_pop()
   ?}
?};
_Tab


\prdk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: dostawa pierwotna
::   WE: params_get().in.p01 - FAP.uidref() - pozycji korekty + $DK.ref() dostawy pierwotnej
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_get().in.p01+16


\fap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: pozycja korekty zakupu
::   WE: params_get().in.p01 - FAP.uidref() - pozycji korekty + $DK.ref() dostawy pierwotnej
::   WY:
::----------------------------------------------------------------------------------------------------------------------
params_get().in.p01-16


\cena
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: cena po korekcie z korekty zakupowej
::   WE: params_get().in.p01 - FAP.uidref() - pozycji korekty + $DK.ref() dostawy pierwotnej
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('upr_set','ceny');
__Logupr.CM:=__Logupr.CZ:=0;
_cena:=exec('FindAndGet','#table',FAP,params_get().in.p01-16,,"
   exec('FindAndGet','#table',@.FAKS,@.FAP.FAKS,,\"
      @.TAZ.RAB_TYP:=@.FAKS.RAB_TYP;
      exec('wysw_kor','faktury_poz');
      @.FKOR.BCN
      \",0)
   ",0)


\date
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: data korekty magazynowej = FAKS.DW korekty zakupowej
::   WE: params_get().in.p01 - FAP.uidref() - pozycji korekty + $DK.ref() dostawy pierwotnej
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('FindAndGet','#table',FAP,params_get().in.p01-16,,"
   exec('FindAndGet','#table',@.FAKS,@.FAP.FAKS,,\"DW\",date(0,0,0))",date(0,0,0))


\dok_przy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [18.42]
:: OPIS: dokument przychodowy
::   WE: _a - ND.ref()
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_a<>~~ & exec('FindAndGet','#table',ND,_a,,"ND.TYP().P='T'",0)

:Sign Version 2.0 jowisz:1048 2020/10/16 15:25:17 d13b39e6c59f2bb5da1f5b1affe5040530aee03e87f490104bef579af6968f99f32c02a50ea410dee9e5168bf0813c8bc019724def6966b135f1c86b6e9e9ca2da12222f3eb19d7643aa773dbe12079d936a8cb8ce32536559525ac5e11130b13cfb13e5f54b2071ff05826a21bff13261736f51fc2c9512d8372aea136e48b0

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %cfg.fml
:: Utworzony: 19.06.2021
:: Autor: RWR
::======================================================================================================================
:: Zawartość: Prezentacja parametrów konfiguracyjnych systemu.
::======================================================================================================================


\main
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Prezentacja parametrów konfiguracyjnych systemu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_FN:=tab_tmp(1
   ,'LP','INTEGER','Lp.'
   ,'FILE','STRING[64]','Plik'@
   ,'DIR','STRING[255]','Katalog'@
);

_CFG:=tab_tmp(1
   ,'NAME','STRING[40]','Parametr'@
   ,'VALUE','STRING[32]','Wartość'@
   ,'SRC','STRING[12]','Plik'@
);

:: Kolejność analizowanych plików jest istotna - zgodna z merytoryką.
_acfg:=spli_str('macro.cfg|%1.cfg' [app_info('pth_name')],'|');
_cfglen:=obj_len(_acfg);
{! _lp:=1 .. _cfglen
|! _cfg:=_acfg[_lp];

   _FN.blank();
   _FN.LP:=_cfglen-_lp+1;
   _FN.FILE:=_cfg;
   _FN.DIR:=pth_dir(_cfg);
   _FN.add();

   _fh:=fopen(_cfg,'ur',1,0,1,1);
   {? _fh.is_open()
   || {!
      |? (_line:=_fh.fread())<>'\n'
      |! _pos:=_line*'=';
::       Linia zawiera znak równości - być może to co przed znakiem równości jest nazwą parametru.
         {? _pos
         || _name:=form((_pos-1)+_line);
            _v1:=form(_pos-_line);
            _comm:=1+_name=';';
            _name:=form(gsub(_name,';',''));
            _v2:=cfg_info(_name);
            {? type_of(_v2)=2
::             Jeżeli funkcja cfg_info() zwróciła napis, to w _v2 jest jednak nazwa parametru.
            || {? _CFG.find_key(_name,)
               || {? ~_comm
                  || _CFG.VALUE:=_v2;
                     _CFG.SRC:=_cfg;
                     _CFG.put()
                  ?}
               || _CFG.blank();
                  _CFG.NAME:=_name;
                  _CFG.VALUE:=_v2;
                  _CFG.SRC:={? _comm || '<domyślnie>' || _cfg ?};
                  _CFG.add()
               ?}
            ?}
         ?}
      !};
      _fh.fclose()
   ?};
   obj_del(_fh)
!};

_dh:=_FN.size();
{? _dh<5
|| _dh:=5
|? _dh>30
|| _dh:=30
?};
_ws:=_FN.mk_sel('Pliki konfiguracyjne'@,,,'#cfgpth',,,_dh,,'U');
_FN.win_fld(_ws,,'LP',,,4,,,,,'Liczba porządkowa'@);
_FN.win_fld(_ws,,'FILE',,,16,,,,,'Nazwa pliku konfiguracyjnego'@);
_FN.win_fld(_ws,,'DIR',,,64,,,,,'Ścieżka dostępu do pliku konfiguracyjnego'@);
_FN.win_act(_ws,,'Szukaj');
_FN.win_act(_ws,,'Kolejność');
_FN.win_sel(_ws);

_ws:=_CFG.mk_sel('Parametry konfiguracyjne'@,,1,,,,,,'U');
_CFG.win_act(_ws,,'Formuła','Pokaż ścieżki'@,,'Prezentacja ścieżżek dostępu do plików konfiguracyjnych'@
   ,"params_get().FN.select()",,,,,
   ,'P'
);
_CFG.win_act(_ws,,'Szukaj');
_CFG.win_act(_ws,,'Kolejność');
_CFG.win_sel(_ws);

params_set('FN',_FN);
_CFG.select()

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:39 e30999ec43935953c69b0d30a16f07c53cbc34dd3ed685dc6555f483af6aa155f54b0d96a08872f78433c3706116c17320c853f03121259bdbd6a3818eca022d9446fe301568016fd124c4196d0daf9ec63432dc2b07acbed6cf023fdc212f60bb7651dcddfe599ca14fb9c1cdff4c331d7ba884bf87d4706d535d1710265647

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %biling_tmobile.fml
:: Utworzony: 14.06.2017
:: Autor: MB
::======================================================================================================================
:: Zawartość: Formuły importu bilingu z T-Mobile
::======================================================================================================================


\read
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Importuje bilingi T-Mobile
:: ~OST: INBLPUT,INFCOPY,INFERASE,INFEXISTS,INFMKDIR,INFUNPACK,INTMPDIR
::----------------------------------------------------------------------------------------------------------------------
{? exec('interm','#system')
|| FUN.emsg(exec('interm_nacc_msg','#system'));
   return(0)
?};
_cFaktura:=exec('FindInSet','#table','EDI_D','K','_FAKTURA',EDI_Z.C,"EDI_D.FLF",,,'');
_cTel:=exec('FindInSet','#table','EDI_D','K','_TEL',EDI_Z.C,"EDI_D.FLF",,,'');
_brak:='';
{? _cFaktura='' || _brak:='komórka z fakturą, ' ?};
{? _cTel='' || _brak+='komórka z telefonem, ' ?};
{? _brak<>''
|| _brak:=_brak-2;
   exec('add_kom','#message','Niepoprawna konfiguracja importu (%1)'@[_brak],38,'UWAGA'@);
   return(0)
?};
SSTALE.AO:=__PARSES.getVal('OkresRok').OKRO_F;
SSTALE.AR:=SSTALE.AO().ROK;
{? SSTALE.AO=null
|| exec('add_kom','#message','Nie ustawiono okresu w parametrach pracy'@,38,'UWAGA'@);
   return(0)
?};
{? ~FUN.ask('Czy rozpocząć import bilingów T-Mobile dla okresu: %1.'@[SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ])
|| return(0)
?};
file_csv:=exec('plik','%biling_tmobile');
{? file_csv='' || return(0) ?};
_tmp:=fmkdir('@'+tmp_dir(),'bilingi');
{? _tmp=''
|| exec('add_kom','#message','Nie udało się utworzenie katalogu (%1)'@[tmp_dir()+'/bilingi/'],38,'UWAGA'@);
   return(0)
?};
_java_tmp:=1-_tmp;
{? funpack('zip','@'+file_csv,_tmp)=0
|| exec('add_kom','#message','Wystąpił błąd podczas rozpakowywania archiwum ZIP'@,38,'UWAGA'@);
   return(0)
?};
_fn:=exec('filename','#string',file_csv);
fcopy('@'+file_csv,_fn,0,1,1);
file_csv:=_fn;
exec('XlsSplit','object');
_sTel:='tel';
_sFaktura:='faktura';
XlsSplit.addCell(_sFaktura,_cFaktura);
XlsSplit.addCell(_sTel,_cTel);
XlsSplit.addDir(_java_tmp);
_lp:=0;
{? XlsSplit.first()
|| BILINGI.index('OKRO_F'); BILINGI.prefix(SSTALE.AO);
   {!
   |? _tel:=XlsSplit.getCell(_sTel);
      _faktura:=XlsSplit.getCell(_sFaktura);
      {? (_file:=XlsSplit.save(_java_tmp+'/plik'+$_lp,_tel+' '+_faktura))<>''
      || BILINGI.blank(1);
         BILINGI.NR_SIM:=_tel;
         BILINGI.OKRO_F:=SSTALE.AO;
         BILINGI.ROK_F:=SSTALE.AR;
         BILINGI.DATA_DO:=SSTALE.AO().KON;
         exec('set_biling','zasoby_biling',_tel);
         _err:='';
         {? BILINGI.NRKRTSIM=null || _err:='numeru karty, '@ ?};
         {? BILINGI.OSOBA=null || _err+='osoby użytkującej, '@ ?};
         {? _err<>''
         || exec('add_kom','#message','%1 - nie znaleziono: %2'@[_tel,_err-2],38,'UWAGA'@)
         || {? ~BILINGI.find_key(BILINGI.NRKRTSIM)
            || _ok:=BILINGI.add()
            || _ok:=1
            ?};
            {? _ok
            || BILINGI.DATA:=date();
               BILINGI.bl_put('PLIK','@'+_file);
               BILINGI.put()
            ?}
         ?};
         {? fexists('@'+_file,0) || ferase('@'+_file,0) ?}
      || exec('add_kom','#message','Nie udało się zapisać bilingu dla %1'@[_tel],38,'UWAGA'@)
      ?};
      _lp+=1;
      XlsSplit.next()
   !}
?};
VAR_DEL.delete('XlsSplit');
1


\plik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.28]
:: OPIS: Zwraca scieżkę do importowanego pliku *.zip z fakturami
::----------------------------------------------------------------------------------------------------------------------
_file:='';
exec('myDIALOG','object');
_file:=myDIALOG.OpenFile('Wybór pliku'@);
{? _file<>''
|| {? _file+4<>'.zip'
   || exec('add_kom','#message','Użytkownik wskazał inny plik niż archiwum ZIP (*.zip).'@,38,'UWAGA'@);
      _file:=''
   ?}
?};
_file

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:39 4649596ce2fb93ecd1b3f8707627617d9389197412199bb1508bcf5ffaae79417e304816361eb1a72a497e571c8a18ea1c46b2dee9f96e3c1dfe12396a72378ba14881d16da67a62e947a00ed0b191abe3ab5ee6229a1657cb3503a805237b0e83f5f79e5df40425970cdb8b19d8720a90711aff2b19094182bfd74693a8613e

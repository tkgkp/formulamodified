:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %dokuml.fml
:: Utworzony: 03.07.2017
:: Autor: AWI
:: Systemy:
::======================================================================================================================
:: Zawartość: Załącznik logistyczne
::======================================================================================================================


\zalaczniki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [17.28]
:: OPIS: Wybór załączników
::   WE: _a - ND.ref(), FAKS.ref()
::       _b - 0-dostawy, 1-dokumentu, 2-dostawy i dokumentu
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_return:=0;
{? type_of(params_get())=type_of(~~) || return(_return) ?};

{? var_pres('in',params_get())=type_of(~~) || return(_return) ?};
_in:=params_get().in;
_ref:=null();
{? var_pres('p01',_in)=type_of(null()) || _ref:=_in.p01 ?};
{? _ref=null() || return(_return) ?};

{? var_pres('mp',params_get())=type_of(~~) || return(_return) ?};
_mp:=params_get().mp;

{? var_pres('out',params_get())=type_of(~~) || return(_return) ?};
_out:=params_get().out;

_uidrefs:='';

_Dok:=tab_tmp(1
   ,'REF'   ,'STRING[16]'  ,'ND.ref()');

_ref_tab:=ref_tab(_ref);
{? _ref_tab=ND
|| _Dok.REF:=$_ref;
   _Dok.add()
|? _ref_tab=FAKS
|| FAKS.cntx_psh();
   FAKS.use(ref_name(_ref));
   FAKS.prefix();
   {? FAKS.seek(_ref)
   ||
      __PARSES.setVal('OddzialLogProd',FAKS.ODDZ);
      _args:=__PARSES.args('OkresRok');
      _args.OBSZAR:='LSP';
      _args.AR:=FAKS.AR;
      _args.AM:=FAKS.AM;
      __PARSES.setVal('OkresRok',_args);
      __PARSES.setEnv('LSP_FAK_EASP');
      exec('dok_maga','powdok',0);
      _loop:=__tt.first();
      {!
      |? _loop
      |!
         _Dok.REF:=__tt.REF;
         _Dok.add();
         _loop:=__tt.next()
      !};
      VAR_DEL.delete('__tt')
   ?};
   FAKS.cntx_pop()
?};
ND.cntx_psh();
_loop:=_Dok.first();
{!
|? _loop
|!
   ND.use(ref_name(_Dok.REF));
   ND.prefix();
   {? ND.seek(_Dok.REF)
   ||
      {? ND.TYP().P='T'
      ||
         _uidrefs:='(\''+ND.uidref()+'\')'
      ||
         DK.cntx_psh();
         DK.use((DK.name()-3)+(ND.name()+3));
         DK.index('DOKMAG');
         DK.prefix(ND.ref());
         _loop:=DK.first();
         {!
         |? _loop
         |!
            _uidrefs+=
               {? _uidrefs=''
               || '(\''+DK.uidref()+'\''
               || ',\''+DK.uidref()+'\''
               ?};
            _srdk_uidref:=exec('FindAndGet','#table',DK,DK.PRDK,,"DK.uidref()",'');
            {? _srdk_uidref<>'' || _uidrefs+=',\''+_srdk_uidref+'\'' ?};
            _loop:=DK.next()
         !};
         DK.cntx_pop();
         {? _uidrefs<>'' || _uidrefs+=')' ?}
      ?}
   ?};
   _loop:=_Dok.next()
!};
ND.cntx_pop();

{? _uidrefs<>''
||
:: Lista plików do wyboru
   _Tab:=tab_tmp(2
      ,'ESEND' ,'STRING[1]'   ,'Wysłano'
      ,'NAME'  ,'STRING[255]' ,'Nazwa pliku'
      ,'SYM'   ,'STRING[20]'  ,'Symbol dokumentu'
      ,'P'     ,'INTEGER'     ,'Pozycja'
      ,'SEL'   ,'STRING[1]'   ,'Wybrano'
      ,'DOKUML','STRING[16]'  ,'Załącznik');
   DOKUML.cntx_psh();
   _Dokuml:=sql($"
      select DOKUML.REFERENCE REF
      from @DOKUML
      where DOKUML.GRP1 in :_a or DOKUML.GRP2 in :_a
      order by 1
   ",_uidrefs);
   _loop:=_Dokuml.first();
   {!
   |? _loop
   |!
      DOKUML.use(ref_name(_Dokuml.REF));
      DOKUML.prefix();
      {? DOKUML.seek(_Dokuml.REF)
      ||
         _Tab.ESEND:=DOKUML.ESEND;
         _Tab.NAME:=DOKUML.bl_info('BLOB','NAME');
         _Tab.SYM:=exec('FindAndGet','#table',ND,DOKUML.GRP1,,"ND.SYM",'');
         _Tab.P:=exec('FindAndGet','#table',DK,DOKUML.GRP2,,"DK.P",'');
         _Tab.DOKUML:=$DOKUML.ref();
         _Tab.add()
      ?};
      _loop:=_Dokuml.next()
   !};
   DOKUML.cntx_pop();

   {? ~_Tab.first()
   ||
      FUN.info('Dokument %1 nie ma załączników pozycji.'@[ exec('record','#to_string',_ref)])
   ||
::    Wybór pliku
      _wer:=_Tab.mk_sel('Załączniki'@,,,'zalacznikipoz',,,,,'U');
      _Tab.win_fld(_wer,,'SEL',,,3,,,'Wybrany'@,,,2,,"'T'","'N'");
      _Tab.win_fld(_wer,,'ESEND',,,3,,,'Wysłano'@,,,2,,"'T'","'N'");
      _Tab.win_fld(_wer,,'NAME',,,50,,,'Nazwa pliku'@);
      _Tab.win_fld(_wer,,'SYM',,,20,,,'Symbol dokumentu'@);
      _Tab.win_fld(_wer,,'P',,,5,,,'Pozycja dokumentu'@);
      _fb:="
         _Tab:=cur_tab(1,1);
         _Tab.SEL:={? _Tab.SEL='T' || 'N' || 'T' ?};
         _Tab.put()
      ";
      _fbg:="sel_nchk()";
      _Tab.win_act(_wer,,'Formuła','Wybierz'@@,,,_fb,,1,1,_fbg);
      _fb:="sel_exit()";
      _Tab.win_act(_wer,,'Formuła','Zakończ'@@,,,_fb);
      _Tab.win_btn(_wer,'text=%1,panel=right,align=begin'['&Wybierz'@],'menu:W');
      _Tab.win_btn(_wer,'text=%1,panel=bottom,align=end'['&Zakończ'@],'menu:Z');
      _Tab.win_btn(_wer,'text=%1,panel=bottom,align=end'['&Anuluj'@],'key:Esc');
      _Tab.win_sel(_wer);
      {? _Tab.select()
      ||
::    Utworzenie blobów procesowych na podstawie wybranych plików
         _save:=0;
         DOKUML.cntx_psh();
         _loop:=_Tab.first();
         {!
         |? _loop
         |!
            {? _Tab.SEL='T'
            || _save:=1;
               DOKUML.use(ref_name(_Tab.DOKUML));
               DOKUML.prefix();
               {? DOKUML.seek(_Tab.DOKUML)
               ||
                  _mp.bl_add(DOKUML.BLOB);
                  DOKUML.ESEND:='T';
                  DOKUML.put()
               ?}
            ?};
            _loop:=_Tab.next()
         !};
         {? _save || _mp.save(,_out) ?};
         DOKUML.cntx_pop();
         _return:=1
      ?}
   ?}
?};
_return

:Sign Version 2.0 jowisz:1048 2023/06/23 14:15:44 cf43365a64f40bc3eb56c136c24163746d25d4ae102d08b815fa510c1cdabe5c44e7de803687e46b33f4ae97a6e8196e4126d31ff6af9d5864ef01e9f5666d4f23d9afff9d945a12d3ee7ab5f29a4f53c58cabaf0ca657fca74fab55cbd78ecc8c1289e52926a7cbbbf2094e96f14a7e2cb7e825d363499a4c297739050b3f8e

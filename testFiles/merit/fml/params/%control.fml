:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %control.fml
:: Utworzony: 01.06.2016
:: Autor: [MB]
::======================================================================================================================
:: Zawartość: Formuły dla controllingu
::======================================================================================================================


\podzialy
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Formula do tworzenia pozycji budzetowych w wartosciach domyslnych planow amortyzacji
::  OLD: \podzialy/kontrol.fml
::----------------------------------------------------------------------------------------------------------------------
_sym:=SRST.JORG().SYMBOL;
_kod:={? 1+_sym='A' | 1+_sym='Z'
      || 'KZT'
      |? 1+_sym='H'
      || 'KST'
      || 'KWT'
      ?};
_typ:='POZ_BUD';
UD_TYP.index('SYMBOL');
UD_TYP.prefix(_typ,_typ);
{? UD_TYP.first()
|| UD_SKL.index('SYMBOL');
   UD_SKL.prefix(UD_TYP.ref(),_kod,_kod);
   {? UD_SKL.first() & UD_SKL.AKTYWNY='T'
   || UD_SKL.ref()
   || null
   ?}
|| null
?}


\dk_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: Zwraca ref do obiektu kosztow dla dokumentow RW
::   WE: _a-nr wymiaru
::   WY: UD_SKL.REFERENCE - jesli przypisany obiekt kosztow
::       null - wpp
::  OLD: \dk_ok/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
_ud_typ:=exec('ud_typ','%control',_a);
{? _ud_typ<>null()
|| _ktm:={? DK.ZL().KTM<>null() || ZL.KTM().KTM || DK.M().KTM ?};
   _sym:={? 1+_ktm='E'|| 3+_ktm |? 1+_ktm='G'|| 4+_ktm |? 3+_ktm='PRZ'|| 5+_ktm || 'ZZ' ?};
   _wyn:=exec('find_skl','%control',_sym,_ud_typ)
?};
_wyn


\dk_jo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: Zwraca ref do jednostki organizacyjnej dla dokumentow WZ
::   WE: _a-nr wymiaru
::   WY: UD_SKL.REFERENCE - jesli przypisana jednostka organizacyjna
::       null - wpp
::  OLD: \dk_jo/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
_ud_typ:=exec('ud_typ','%control',_a);
{? _ud_typ<>null()
||
   {? DK.ZL().JORG<>null()
   || _sym:=ZL.JORG().KOD; exec('find_skl','%control',_sym,_ud_typ)
   ?}
?}


\fap_ok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: Zwraca ref do obiektu kosztow dla dokumentow sprzedazy
::   WE: _a-nr wymiaru
::   WY: UD_SKL.REFERENCE - jesli przypisany obiekt kosztow
::       null - wpp
::  OLD: \fap_ok/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
_ud_typ:=exec('ud_typ','%control',_a);
{? _ud_typ<>null()
||
   _rodz:=FAP.M().RODZ;
   _ktm:=FAP.M().KTM;
   _sym:={? 1+_ktm='E'|| 3+_ktm |? 1+_ktm='G'|| 4+_ktm |? 3+_ktm='PRZ'|| 5+_ktm || 'ZZ' ?};
   _wyn:=exec('find_skl','%control',_sym,_ud_typ)
?};
_wyn


\fap_jo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: Zwraca ref do jednostki organizacyjnej dla dokumentow WZ
::   WE: _a-nr wymiaru
::   WY: UD_SKL.REFERENCE - jesli przypisana jednostka organizacyjna
::       null - wpp
::  OLD: \fap_jo/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
_ud_typ:=exec('ud_typ','%control',_a);
{? _ud_typ<>null()
|| {? FAP.FAKS().STS<>null
   || _sym:={? FAKS.STS().KOD='SP-01' || 'DSW' || 'BOK' ?};
      exec('find_skl','%control',_sym,_ud_typ)
   ?}
?}


\fap_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: Zwraca ref do pozycji budzetowej
::   WE: _a-nr wymiaru
::   WY: UD_SKL.REFERENCE - jesli przypisana pozycja budzetowa
::       null - wpp
::  OLD: \fap_pb/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
_rr:=null();
_ud_typ:=exec('ud_typ','%control',_a);
{? _ud_typ<>null
||
   {? FAKS.KH().GRKH<>null()
   || KH.GRKH();
      _pb:={? GRKH.KOD='KLUCZOWI' || 'SPK' || 'SPP' ?}
   ||
      _pb:='SPP'
   ?};
   _rr:=exec('find_skl','%control',_pb,_ud_typ)
?};
_rr


\nd_pb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: Zwraca ref do pozycji budzetowej
::   WY: UD_SKL.REFERENCE - jesli przypisana pozycja budzetowa
::       null - wpp
::  OLD: \nd_pb/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
MG.cntx_psh();
_pb:={? ND.MAG().SYM='WYROBY' || 'ZWSP' || 'KT' ?};
MG.cntx_pop();
UD_OBJ.skl(__POZ_BUD,_pb)


\ok_zlec
::------------------------------------------------------------------------------
::  UTW: TS [2008]
:: OPIS: Zwraca ref do obiektu kosztów dla zlecenia, jak nie ma - dla produktu zlecenia, jak nie - grupy mat.
::   WY: UD_SKL.REFERENCE - jeśli przypisany obiekt kosztów
::       null - wpp
::  OLD: \ok_zlec/sch_par.fml
::------------------------------------------------------------------------------
_ud_typ:=UD_OBJ.typ(__OB_KOSZ);
_t_tow:=ZL.KTM;
_t_mgr:=ZL.KTM().MGR;
_rr:=null();
{? _ud_typ<>null()
|| _rr:=exec('find_skl','%control',ZL.ref(),_ud_typ);
   {? _rr=null()
   || {? _t_tow<>null()
      || _rr:=exec('find_skl','%control',_t_tow,_ud_typ);
         {? _rr=null()
         || _rr:=exec('find_skl','%control',_t_mgr,_ud_typ)
         ?}
      ?}
   ?}
?};
_rr
\find_skl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2008]
:: OPIS: Wyznacza element schematu, z ktorym powiazany jest obiekt.
::   WE: _a - symbol
::       _b - UD_TYP.REFERENCE
::   WY: UD_SKL.REFERENCE - ; null - wpp
::  OLD: \find_skl/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
UD_SKL.index('SYMBOL');
UD_SKL.prefix(_b,_a,_a);
{? UD_SKL.first || UD_SKL.ref || null ?}


\ud_typ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [2011]
:: OPIS: UD_TYP.ref dla numeru wymiaru
::   WE: _a-nr wymiaru
:  OLD: \ud_typ/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
SKID_MBP.index('LP');
SKID_MBP.prefix(CON_DEF.SKID_MB);
{? _a & SKID_MBP.find_key(_a)
|| SKID_MBP.UD_SCH().UD_TYP
|| null
?}


\fap_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI[2008] BZ[2012]
:: OPIS: Wyznacza wartosc _a pozycji dokumentu.
::   WE: _a = 'il'; ='war'
::   WY: okreslona wartosc z pozycji dokumentu
:  OLD: \fap_war/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='il'     || {? FAP.IL2>0 || FAP.IL2 || FAP.IL ?}
|? _a='wn'     || FAP.WN
|? _a='wwal'   || FAP.WWAL
|? _a='war_rb' || ((FAP.IL*FAP.CPR)$2)-{? FAP.FAKS().T().FIS='T' || FAP.BWAL_P || FAP.WWAL_P ?}
|| 0
?}


\dk_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI[2008] BZ[2012]
:: OPIS: Wyznacza wartosc _a pozycji dokumentu.
::   WE: _a = 'il' - PDM.IL; ='war' - PDM.WAR
::   WY: okreslona wartosc z pozycji dokumentu
::  OLD: \dk_war/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='il'     || DK.IL
|? _a='war'    || DK.WAR
|| 0
?}


\kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2012]
:: OPIS: Zwraca ref do pozycji budzetowej KH dla dokumentow sprzedazy
::   WE: _a - nr wymiaru
::       _b - domyslna pozycja
::   WY: UD_SKL.REFERENCE - jesli przypisany obiekt kosztow
::       null - wpp
::  OLD: \kh/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
_ud_typ:=exec('ud_typ','%control',_a);
{? _ud_typ<>null()
|| _sym:=FAP.FAKS().KH().KOD;
   _wyn:=exec('find_skl','%control',_sym,_ud_typ);
   {? _wyn=null || _wyn:=exec('find_skl','%control',_b,_ud_typ) ?}
?};
_wyn


\han
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2012]
:: OPIS: Zwraca ref do pozycji budzetowej HAN dla dokumentow sprzedazy
::   WE: _a - nr wymiaru
::       _b - domyslna pozycja
::   WY: UD_SKL.REFERENCE - jesli przypisany obiekt kosztow
::       null - wpp
::  OLD: \han/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
_ud_typ:=exec('ud_typ','%control',_a);
{? _ud_typ<>null()
|| _sym:=FAP.FAKS().HAN().KOD;
   _wyn:=exec('find_skl','%control',_sym,_ud_typ);
   {? _wyn=null || _wyn:=exec('find_skl','%control',_b,_ud_typ) ?}
?};
_wyn


\obsz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [2012]
:: OPIS: Zwraca ref do pozycji budzetowej obszar dla dokumentow sprzedazy
::   WE: _a - nr wymiaru
::       _b - domyslna pozycja
::   WY: UD_SKL.REFERENCE - jesli przypisany obiekt kosztow
::       null - wpp
::  OLD: \obsz/sch_par.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:=null;
_ud_typ:=exec('ud_typ','%control',_a);
{? _ud_typ<>null()
|| _sym:=FAP.FAKS().KH().OBS().KOD;
   _wyn:=exec('find_skl','%control',_sym,_ud_typ);
   {? _wyn=null || _wyn:=exec('find_skl','%control',_b,_ud_typ) ?}
?};
_wyn

:Sign Version 2.0 jowisz:1048 2023/06/23 14:15:44 34540ece0a24980294f2753998c433c591e29edc5ee3bbba24256e1cd93a1ccb261a5947d5804f3d126258c52b5f80c9d07fa589128c99782bf32465be9780ae887b8a277f7ddd61fc420dedec6a26190058adefb657706ff26fd4f64d249433c35776e5f5bcfdbd7bf043800cd87e1f73ac8e358cb3d1e641b9e37f14f62806

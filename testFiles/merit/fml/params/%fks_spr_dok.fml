:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %fks_spr_kont.fml
:: Utworzony: 07.07.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa formuł kontrolnych
::======================================================================================================================


\kon_pocz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RL] [8.60]
:: OPIS: Wywolanie formuly poczatkowej dla formuly uzupelnijacej konto
::       przeciwstawne w dokumencie
::  OLD: \kon_pocz/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
KON_WN:=KON_MA:=0;
1


\kon_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RL] [8.60]
:: OPIS: Wywolanie formuly dla pozycji dla formuly uzupelnijacej konto
::       przeciwstawne w dokumencie
::  OLD: \kon_poz/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('KON_WN')>0 & var_pres('KON_MA')>0
|| {? 1+POZ.STR='W' || KON_WN+=POZ.SUM || KON_MA+=POZ.SUM ?}
?};
1


\brt_pocz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PB [8.50]
:: OPIS: Wywolanie formuły początkowej dla formuły kontrolującej zgodność kwoty Razem brutto w nagłówku dokumentu
::       z kwotą brutto wynikającą z pozycji VAT
::   WE: [_a - 1 - ask, w p.w. - msg z komunikatem]
::   WY: 0/1
::  OLD: \brt_pocz/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK.SUMWAL$2<>0
||  _ile_wal:=0;
  {? DOK.DOK_REJ().SLO().KOD='SAD' | (_ile_wal:=exec('ile_wal','dok_fks'))<2
  || VPOZ.index('VDOK');
      VPOZ.prefix(DOK.ref);
      _w:=0;
      {? VPOZ.first() || {! |? _w+={? _ile_wal || VPOZ.WARW || VPOZ.BRUTTO ?}; VPOZ.next() !} ?};
      {? _w$2<>DOK.SUMWAL$2
      || {? var_pres('KOMUNIKAT')>0 & KOMUNIKAT
         || _t:='Suma '@ + {? _ile_wal || 'wartości w walucie '@+ DOK.WAL().KOD || 'brutto'@ ?}+
                ' pozycji VAT różna od Razem brutto z nagłówka dokumentu.\n'@+
                '\nKwota z nagłówka dokumentu: '@+form(DOK.SUMWAL,16,2,' ,')+
                '\nKwota z pozycji dokumentu:  '@+form(_w,16,2,' ,')+
                '\n\nKontynuować?'@;
             {? _=0 | _a=1
             || FUN.ask(_t,0)
             || FUN.info(_t);0
             ?}
         || 0
         ?}
      || 1
      ?}
   || 1
   ?}
|| 1
?}


\kon_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RL] [8.60]
:: OPIS: Wywolanie formuly koncowej dla formuly uzupelnijacej konto
::       przeciwstawne w dokumencie
::  OLD: \kon_kon/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('KON_WN')>0 & var_pres('KON_MA')>0
|| _konto:=DOK.DOK_REJ().AN;
   {? _konto<>'' & KON_WN$2<>KON_MA$2
   || {? KON_WN$2<KON_MA$2
      || _str:='Wn'; _kw:=F.SMa(KON_WN,KON_MA)
      || _str:='Ma'; _kw:=F.SWn(KON_WN,KON_MA)
      ?};
      exec('poz_add','dekret_aut',_kw,_str,_konto,'','',date(0,0,0),date(0,0,0),DOK.TR,'')
   ?};
   &KON_WN; &KON_MA
?};
1


\spr_stvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [1210]
:: OPIS: Sprawdzenie stawek VAT wprowadzonych w pozycjach
::  OLD: \spr_stvat/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
_zwrot:=1;
SLO.cntx_psh();
{? DOK.KRAJ=null | DOK.KRAJ().KOD='PL'
|| _czy7:=_czy22:=_czy8:=_czy23:=0;
   VPOZ.cntx_psh(); VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
   {? VPOZ.first()
   || {! |?
         _proc:=(#(2+VPOZ.PR().KOD));
         {? _proc=7 || _czy7:=1 ?};
         {? _proc=22 || _czy22:=1 ?};
         {? _proc=8 || _czy8:=1 ?};
         {? _proc=23 || _czy23:=1 ?};
         VPOZ.next()
      !}
   ?};
   VPOZ.cntx_pop();
   _rok:=exec('rok_stale','dok_fks');
   {? _rok
   || {? _rok>2010 & (_czy7 | _czy22)
      || _zwrot:=0;
         {? var_pres('KOMUNIKAT')>0 & KOMUNIKAT
         || _t:='Dokument zawiera w pozycjach stawki VAT 7 i/lub 22 procent wykorzystywane do roku 2010.'@;
            _zwrot:=FUN.ask(_t,0,'Przerwij'@,'Kontynuuj'@)
         ?}
      |? _rok<2011 & (_czy8 | _czy23)
      || _zwrot:=0;
         {? var_pres('KOMUNIKAT')>0 & KOMUNIKAT
         || _t:='Dokument zawiera w pozycjach stawki VAT 8 i/lub 23 procent wykorzystywane od roku 2011.'@;
            _zwrot:=FUN.ask(_t,0,'Przerwij'@,'Kontynuuj'@)
         ?}
      ?}
   ?}
?};
SLO.cntx_pop();
_zwrot


\kr_pocz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.10]
:: OPIS: Wywolanie formuly poczatkowej dla formuly kontrolujacej krag kosztowy
::       zmienna KOMUNIKAT (jesli jest) decyduje czy wyswietlac komunikat
::   WY: 0/1
::  OLD: \kr_pocz/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
exec('kr_pocz','dok_fks')


\kr_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.10]
:: OPIS: Wywolanie formuly dla pozycji dla formuly kontrolujacej krag kosztowy
::   WY: 0/1
::  OLD: \kr_poz/spr_dok.fml
:-----------------------------------------------------------------------------------------------------------------------
exec('kr_poz','dok_fks')


\kr_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.10]
:: OPIS: Wywolanie formuly koncowej dla formuly kontrolujacej krag kosztowy
::       zmienna KOMUNIKAT (jesli jest) decyduje czy wyswietlac komunikaty, pytania
::  WE: [_a:  1 - ask, w p.w. - msg z komunikatem]
::      [_b: - kasowac zmienne]
::  WY: 0/1
::  OLD: \kr_kon/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=1
|| exec('kr_kon','dok_fks',_a)
|? _=2
|| exec('kr_kon','dok_fks',_a,_b)
?}


\vat_pocz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Wywolanie formuly poczatkowej dla formuly kontrolujacej zgodnosc
::       pozycji VAT z dekretacja na kontach
::       zmienna KOMUNIKAT (jesli jest) decyduje czy wyswietlac komunikat
::   WE: formula - wymagane 2 parametry (3 opcjonalny)
::       _a  - prefix konta do dekretacji naleznego VAT LUB naliczonego VAT
::       _b  - prefix konta do dekretacji naleznego zawieszonego VAT LUB naliczonego zawieszonego VAT
::      [_c] - prefiks konta do dekretacji naliczonego VAT idacego w koszty
::             wartosci _a i _b oznaczaja podatek VAT do odliczenia
::   WE: wydruk - wymagane 2 lub 4 parametry
::       gdy 2 parametry - opis jak dla formuli
::       gdy 4 parametry - opis, kolejnosc taka:
::         _a - prefix konta do dekretacji naleznego VAT                KONTO.K1
::         _b - prefix konta do dekretacji naleznego zawieszonego VAT   KONTO.K3
::         _c - prefix konta do dekretacji naliczonego VAT              KONTO.K2
::         _d - prefix konta do dekretacji naliczonego zawieszonego VAT KONTO.K4
::         _e - prefix konta do dekretacji naliczonego VATu w koszty    KONTO.K5
::  OLD: \vat_pocz/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
exec('vat_pocz','dok_fks',_a,_b,{? var_pres('_c')>0 || _c || '' ?})


\vat_poz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Wywolanie formuly dla pozycji dla formuly kontrolujacej zgodnosc  pozycji VAT z dekretacja na kontach
::   WY: 0/1
::  OLD: \vat_poz/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
exec('vat_poz','dok_fks')


\vat_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.40]
:: OPIS: Wywolanie formuly koncowej dla formuly kontrolujacej zgodnosc
::       pozycji VAT z dekretacja na kontach
::       zmienna KOMUNIKAT (jesli jest) decyduje czy wyswietlac komunikat
::   WE: [_a - 1 - pytanie, w p.w. - komunikat]
::       [_b - jesli jest nie kasowac zmienne]
::   WY: 0/1
::  OLD: \vat_kon/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
{? _=0
|| exec('vat_kon','dok_fks')
|? _=1
|| exec('vat_kon','dok_fks',_a)
|? _=2
|| exec('vat_kon','dok_fks',_a,_b)
?}


\pozf_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.41]
:: OPIS: Formula kontrolna - poczatkowa - sprawdza czy pozycje faktury sa zgodne z pozycjami VAT
::   WE: [_a] - w przypadku niezgodnosci: 1-pytanie,  [0]-informacja
::  OLD: \pozf_vat/spr_dok.fml
::----------------------------------------------------------------------------------------------------------------------
{? DOK.DOK_REJ().JPK_FA_P='T' & ~DOK.WAL
|| SLU.index('NAZ'); SLU.prefix();
   {? SLU.find_key('~STAWKI VAT PL')
   || SLO.index('SL'); SLO.prefix(SLU.ref());
      {? SLO.first()
      || POZF.index('SV');
         VPOZ.index('VDPR');
         {!
         |? _netto:=_brutto:=_vat:=0;
            POZF.prefix(DOK.ref(),SLO.ref());
            {? POZF.first()
            || {!
               |? _netto+=POZF.WN;
                  _brutto+=POZF.WB;
                  _vat+=POZF.WV;
                  POZF.next()
               !}
            ?};
            VPOZ.prefix(DOK.ref(),SLO.ref());
            {? VPOZ.first()
            || {!
               |? _netto-=VPOZ.NETTO;
                  _brutto-=VPOZ.BRUTTO;
                  _vat-=VPOZ.VAT;
                  VPOZ.next()
               !}
            ?};
            _ok:=_netto=0 & _brutto=0 & _vat=0;
            _ok & SLO.next()
         !};
         {? _ok=0
         || {? var_press('KOMUNIKAT')>0 & KOMUNIKAT
            || _t:='Niezgodna suma pozycji '+
               {? _netto || 'Netto' || '' ?}+
               {? _vat || {? _netto || ', ' || '' ?}+'VAT' || '' ?}+
               {? _brutto || {? _netto | _vat || ', ' || '' ?}+'Brutto' || '' ?}+
               '\nfaktury z pozycjami VAT.';
               {? _=0 | _a=0
               || FUN.info(_t)
               || _ok:=FUN.choice(_t,,'Kontynuuj'@)
               ?}
            ?}
         ?};
         _ok
      || 1
      ?}
   || 1
   ?}
|| 1
?}


\st_zwol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Wywolanie formuly poczatkowej dla formuly kontrolujacej uzupełnienie powodu użycia stawki zwolnionej
::----------------------------------------------------------------------------------------------------------------------
_st_zwol:=0;
_ask:=0;
_ok:=1;
VPOZ.cntx_psh();
VPOZ.index('VDOK'); VPOZ.prefix(DOK.ref());
{? VPOZ.first()
|| {!
   |? _st_zwol:=-VPOZ.PR().KOD='zwol.';
      _st_zwol=0 & VPOZ.next()
   !}
?};
VPOZ.cntx_pop();
{? _st_zwol
|| DOKPOLA.cntx_psh();
   DOKPOLA.index('UNIK'); DOKPOLA.prefix(DOK.ref());
   _ask:={? DOKPOLA.first()
         || DOKPOLA.ZW<>'T'
         || 1
         ?};
   DOKPOLA.cntx_pop()
?};
{? _ask
|| _ok:=0;
   {? var_press('KOMUNIKAT')>0 & KOMUNIKAT
   || _ok:=FUN.ask(
         'Użyto zwolnionej stawki VAT i nie wskazano podstawy zwolnienia.\n'
         '(Sekcja \'Podstawa zwolnienia\' w zakładce \'Dane dodatkowe\' dokumentu)\n\n'
         'Czy kontynuować?'
      )
   ?}
?};
_ok

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:39 12183bcd03f3a4617b6e03a9672c493c00449be593e71ecc3c6d6ec494e3c9ce5ee805ba77f2959aa972946945a2addbb4894274267af1220f362806576b6522559f9aca96826288c3faffcee5ee19591680ec11b037a45638720d522ffb72f2e68fc5c838fb2fec22e228307e9b142f244d5be1c940f7b8034e98cf1b2fb970

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %fks_vat.fml
:: Utworzony: 06.05.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa automatycznej dekretacji
::======================================================================================================================


\p_221
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Analityka konta 221
::  OLD: \p_223/vat_auto.fml
::----------------------------------------------------------------------------------------------------------------------
{? _ || exec('p_2231','%fks_vat',_a) || exec('p_2231','%fks_vat') ?}


\p_2231
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [2010]
:: OPIS: zwraca kod grupy podatkowej
::   WY: kod grupy
::  OLD: \p_223/skid.fml
::----------------------------------------------------------------------------------------------------------------------
_g:={? _ || _a || VPOZ.GRVAT().KOD ?};
_a:=(5+_g)+1;
{? _g+1='N' || _a:='N' ?};
_b:=(2+_g)+1;
{? _b='I'
|| {? _a='P'
   || 'I21'
   |? _a='N'
   || 'I01'
   || 'I11'
   ?}
|| {? _a='P'
   || 'P21'
   |? _a='N'
   || 'P01'
   || 'P11'
   ?}
?}


\p_701
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: Konto 701
::  OLD: \p_701/vat_auto.fml
::----------------------------------------------------------------------------------------------------------------------
_i:={? _ || 2+_a || 2+VPOZ.PR().KOD?};
{? (1+_i<>' ') & _i<>'Zw' || _i
|? _i=' -'                || 'ND'
|? 1+_i=' '               || {? exec('vat','dok_fks',{? _ || _a || VPOZ.PR().KOD ?})=6.5 || '0R' || '0'+(1-_i) ?}
|? _i='Zw'                || 'ZW'
?}


\wdt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: WDT
::  OLD: \wdt/vat_auto.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='PL'|| _v:=exec('p_701','%fks_vat');'222-'+_v || '223-02-'+DOK.KRAJ().KOD ?}


\wnt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: WNT
::  OLD: \wnt/vat_auto.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='PL'|| _v:=exec('p_2231','%fks_vat');'221-'+_v || '223-01-'+DOK.KRAJ().KOD ?}


\wdt2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: WDT2
::  OLD: \wdt2/vat_auto.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='PL'|| _v:=exec('p_701','%fks_vat');'927-'+_v || '' ?}


\wnt2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ??? [?????]
:: OPIS: WNT2
::  OLD: \wnt2/vat_auto.fml
::----------------------------------------------------------------------------------------------------------------------
{? _a='PL'|| _v:=exec('p_2231','%fks_vat');'926-'+_v || '' ?}


\kwotapl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [12.30]
:: OPIS: Funkcja zwraca wartosc zawsze w PLN dla formuly automatycznej dekretacji >D_4_5<
::       wykorzystujacych wartosci pozycji aktualnego dokumentu
::  OLD: \kwotapl/auto_poz.fml

::----------------------------------------------------------------------------------------------------------------------
k:='40'+(1+(POZ.KON+3))+'-'+(POZ.KON+2);
exec('plobr','fks_auto',POZ.KON,POZ.WAL);
_w:=wn-ma;
{? 2+POZ.KON<>'64'
|| POZ.cntx_psh();
   F.PObr('64');
   POZ.cntx_pop();
   _w-=F.ma-F.wn
?};
_w


\kwota
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [8.10]
:: OPIS: Funkcja zwraca wartosc dla formul automatycznej dekretacji
::       wykorzystujacych wartosci pozycji aktualnego dokumentu
::  OLD: \kwota/auto_poz.fml
::----------------------------------------------------------------------------------------------------------------------
k:='40'+(1+(POZ.KON+3))+'-'+(POZ.KON+2);
F.PObr(POZ.KON);
_w:=F.wn-F.ma;
{? 2+POZ.KON<>'64'
|| POZ.cntx_psh();
   F.PObr('64');
   POZ.cntx_pop();
   _w-=F.ma-F.wn
?};
_w


\pre_euro
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.51]
:: OPIS: Funkcja sterująca do utworzenia automatycznych dekretów do przeksiegowania obrotów kont w walutach z unii
::       walutowej na walutę EURO - formuła początkowa
::  OLD: \pre_euro/euro.fml
::----------------------------------------------------------------------------------------------------------------------
SLO.cntx_psh();
SLO.index('SL');
SLO.prefix(FINFO.SLWAL().SLU,'EUR');
wyb:={? SLO.first()
     || 1
     || FUN.info('Brak waluty o symbolu "EUR" w słowniku walut.'@); 0
     ?};
SLO.cntx_pop();
{? wyb & PAR_SKID.get(9)='N' || wyb:=0 ?};
{? wyb
|| ROZNE.WAL:=null; ROZNE.PREFIX:=''; ROZNE.KONTO:='260-04-PLN';
   ROZNE.PAR4:=0;
   ROZNE.win_edit('EURO');
   {? ~ROZNE.edit("exec('spr_pare','fks_auto')")
   || wyb:=0
   ?}
?}


\eur_kont
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RL [7.51]
:: OPIS: Zamiana symbolu konta AN.SYM, jeśli w analityce zawierał symbol waluty
::  OLD: \eur_kont/euro.fml
::----------------------------------------------------------------------------------------------------------------------
_k:=_p:=AN.SYM;
BUD.cntx_psh(); BUD.index('KS'); BUD.prefix(AN.KS);
{? BUD.first()
|| {! |?
      {? BUD.SLU=FINFO.SLWAL
      || _p:=(AN.SYM*_a);
         _k:=((_p-1)+_k)+'EUR'+((_p+2)-_k); 0
      || BUD.next()
      ?}
   !}
?};
BUD.cntx_pop();
_k


\pre_akrs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS:  Formuła liczy różnice kursowe dla rozliczanego rozrachunku na podstawie wprowadzanej pozycji dokumentu.
::        Formuła szuka (na podstawie pozycji dokumentu na którym znajdował się kursor w momencie wywołania automatu)
::        rozrachunków w walucie takiej jak w pozycji dokumentu, które nie są rozliczone, a strona salda jest
::        przeciwna jak pozycji dokumentu. - Formula poczatkowa
::  OLD: \pre_akrs/auto_krs.fml
::----------------------------------------------------------------------------------------------------------------------
undefine();
define('k_zw','754-02-01','Konto dodatnich różnic kursowych:'@,,35);
define('k_zm','759-02-01','Konto ujemnych różnic kursowych:'@,,35);
_ok:=def_btn('text=%1'['OK'@],'key:F2');
_anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
def_bopt(_ok,'tooltip='+exec('help_red_ok','#window','P'));
def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
{? ~def_edit(,'Definicja kont różnic kursowych'@) || wyb:=0 ?}


\pre_rkrs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: ?? [?????]
:: OPIS: Formuła liczy różnice dodatnich i ujemnych różnic kursowych dla rozliczonych rozrachunków w walucie obcej.
::       Formuła początkowa.
::  OLD: \pre_rkrs/automat.fml
::----------------------------------------------------------------------------------------------------------------------
undefine();
define('k_zw','754-02-01','Konto dodatnich różnic kursowych:'@,,35);
define('k_zm','759-02-01','Konto ujemnych różnic kursowych:'@,,35);
_ok:=def_btn('text=%1'['OK'@],'key:F2');
_anuluj:=def_btn('text=%1'['Anuluj'@],'key:Esc');
def_bopt(_ok,'tooltip='+exec('help_red_ok','#window','P'));
def_bopt(_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
{? ~def_edit(,'Definicja kont różnic kursowych'@) || wyb:=0 ?}




\kh_pow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BW
:: OPIS: Ustalenie pierwszego symbolu analityki kont rozrachunkowych/sprzedaży
:: _a: skrot kontrahenta
::----------------------------------------------------------------------------------------------------------------------

_wyn:='';
KH.cntx_psh;
KH.index('SKR');
KH.prefix(2,_a,);
{? KH.first
|| _khref:=KH.ref
|| _khref:=null
?};
KH.cntx_pop;
{? _khref<>null
||
KH_DOD.index('KH_DOD');
KH_DOD.prefix(REF.FIRMA,_khref);
{? KH_DOD.first
||
   _kh:=KH_DOD.STATUS
||
   _kh:='N'
?};
{? _kh='P'
||
   _wyn:='01-'
||
   _wyn:='02-'
?}
?};
_wyn


\nod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [17.00]
:: OPIS: Konto przeciwstawne do dekretacji not odsetkowych
::----------------------------------------------------------------------------------------------------------------------
'751-02-02'


\kh_pow_rk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BW
:: OPIS: Ustalenie pierwszego symbolu analityki kont rozrachunkowych na POZDOK
:: _a: kod kontrahenta
::----------------------------------------------------------------------------------------------------------------------

_wyn:='';
KH.cntx_psh;
KH.index('KOD');
KH.prefix(2,_a,);
{? KH.first
|| _khref:=KH.ref
|| _khref:=null
?};
KH.cntx_pop;
{? _khref<>null
||
   KH_DOD.index('KH_DOD');
   KH_DOD.prefix(REF.FIRMA,_khref);
   {? KH_DOD.first
   ||
      _kh:=KH_DOD.STATUS
   ||
      _kh:='N'
   ?};
   {? _kh='P'
   ||
      _wyn:='01-'
   ||
      _wyn:='02-'
   ?}
?};
_wyn

\chk_pow_kh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: funkcja pobiera rodzaj powiązania na daną datę dla danego kontrahenta
::   WE: _a - KH.ref()
::       _b - data
::   WY: kod rodzaju powiązania lub informacja o braku powiązań
::----------------------------------------------------------------------------------------------------------------------
_a:=KH.ref();
_naz:=KH.NAZ+' '+$_a;
_b:=date;
msg(_naz);
{? var_press('_a')<>7
|| FUN.error('Błędny typ pierwszego parametru')
?};
{? var_press('_b')<>type_of(date)
|| FUN.error('Błędny typ drugiego parametru')
?};
KH_POWD.cntx_psh();
KH_POWD.index('KH');
KH_POWD.win_sel('WER');
::KH_POWD.select();
KH_POWD.prefix(_a);
{? KH_POWD.first()
||
::   msg($_a);
::   msg($KH_POWD.KH);
   {!
   |? {? KH_POWD.DATAOD<_b & (KH_POWD.DATADO>_b | KH_POWD.DATADO=date(0,0,0))
      || msg(KH_POWD.RODZAJ().KOD);0
      || {? KH_POWD.next()=0 || FUN.info('Brak powiązań') || 1 ?}
      ?}
   !}
|| FUN.info('Brak powiązań')
?};
KH_POWD.cntx_pop()

\chk_pow_kh2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: funkcja sprawdza czy kontrahent jest powiązany z firmą za pomocą danego rodzaju powiązania
::   WE: _a - KH_POWR.KOD
::       _b - FIRMA
::       _c - KH.ref()
::       _d - KH_POW.DATAOD
::       _e - KH_POW.DATADO
::
::   WY: 1 - tak
::       0 - nie
::----------------------------------------------------------------------------------------------------------------------
_rodzaj:=_a;
_firma:=_b;
_kh:=_c;
_dataod:=_d;
_datado:=_e;
_zwrot:=0;
KH_POW.cntx_psh();
KH_POW.index('RODZAJ');
KH_POW.prefix(_rodzaj,_firma,_kh);
{? KH_POW.first()
|| {? (_dataod<=KH_POW.DATAOD & (_datado>=KH_POW.DATAOD | _datado = date(0,0,0)))
   | (KH_POW.DATAOD<=_dataod & (KH_POW.DATADO>=_dataod | KH_POW.DATADO = date(0,0,0)))
   || _zwrot:=1
   ?}
?};
KH_POW.cntx_pop();
_zwrot

:Sign Version 2.0 jowisz:1048 2023/06/23 14:15:44 ce3a79a048a3e7789be3f8d99a068e95109371dae4b66f050e38f3a411ae93bb34d29a59df517d5cb699083facd21472c4d1ad0c1eb9cfd184c592a89671c4a84aca9e5126080d5fd3380d0d5e6de0e726d1d86585a44090f994eb45dd43fdf5f9dbadf5c1eb9446d72e805e0d3d8c31414dcb8eaf59e9418a3b69d97a3d219a

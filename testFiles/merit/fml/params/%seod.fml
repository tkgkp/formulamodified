:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %seod.fml
:: Utworzony: 10.02.2022
:: Autor: [MB]
::======================================================================================================================
:: Zawartość: Formuły do obsługi SEOD
::======================================================================================================================


\sposob_pl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Formula zwraca sposób płatności dla faktury w obiegu
::   WE: _a - tryb: 1-SEOD>ERP, 2-ERP>Seod
::       _b - nazwa płatności w SEOD lub _SLO
::            np: Przelew na rachunek bankowy, Gotówka, Karta, Za pobraniem, Przedpłata, Barter, Kompensata, Inny
::   WY: gdy tryb=1 zwraca _SLO, gdy tryb=2 zwraca napis ze sposobem płatności w SEOD
::----------------------------------------------------------------------------------------------------------------------
_tryb:=_a;
{? _tryb=1
|| _seod:=_b;
   _slo:=null;
   {? XINFO.SLP=null
   || exec('czytaj','#stalesys',,XINFO,'SLP')
   ?};
   {? XINFO.SLP
   || _slo:=exec('FindInSet','#table','SLO','SL_TR',_seod,XINFO.SLP,,1,,null)
   ?};
   _slo
|? _tryb=2
|| _slo:=_b;
   _ret:='';
   SLO.cntx_psh();
   SLO.prefix();
   {? SLO.seek(_slo)
   || _ret:=SLO.TR
   ?};
   SLO.cntx_pop();
   _ret
?}


\st_vat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwrca stawkę VAT na podstawie napisu
::   WE:  _a  - napis ze stawką VAT
::       [_b] - kod kraju stawki VAT
::----------------------------------------------------------------------------------------------------------------------
_st:={? var_pres('_a')=type_of('') || _a || '' ?};
_kraj:={? var_pres('_b')=type_of('') & +_b=2 || _b || 'PL' ?};
_ret:=null;
{? _st<>''
|| _slu:=exec('FindInSet','#table','SLU','NAZ','~STAWKI VAT '+_kraj,,,1,,null);
   {? _slu
   || {? _st+1='%'
      || _st:=_st-1;
         {! |? {? _st+1=' ' || _st:=_st-1; 1 ?} !};
         _st:=('  '+_st)+2
      |? _st='ZW'
      || _st:='Zwol.'
      |? _st='-zw0'
      || _st:=' -zw0'
      ?};
      _ret:=exec('FindInSet','#table','SLO','SL',_st,_slu,,,,null)
   ?}
?};
_ret


\pojazd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Zwraca pojazd na podstawie numeru rejestracyjnego
::   WE: _a - numer rejestracyjny
::----------------------------------------------------------------------------------------------------------------------
_nr:=_a;
_ref:=null;
POJAZDY.cntx_psh();
POJAZDY.index('NRREJ'); POJAZDY.prefix();
{? POJAZDY.find_key(EDOKUM.FIRMA,_nr,)
|| _ref:=POJAZDY.ref()
|| POJAZDY.blank();
   POJAZDY.FIRMA:=EDOKUM.FIRMA;
   POJAZDY.NRREJ:=_nr;
   POJAZDY.NAZ:='Samochód '+_nr;
   {? POJAZDY.add()
   || _ref:=POJAZDY.ref()
   ?}
?};
POJAZDY.cntx_pop();
_ref


\EDOKUM_kh2slo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodaje kontrahenta z dokumentu w obiegu do słownika
::   WY: Zwraca wskazanie na pozycję słownika z kontrahentami
::----------------------------------------------------------------------------------------------------------------------
_slo:=null;
ETYPY.cntx_psh();
{? EDOKUM.KHKH & EDOKUM.TYP().DOM_SL
|| _wz:=ETYPY.DOM_SL().SLU().WZ;
   RS.cntx_psh();
   RS.index('RS'); RS.prefix(_wz,);
   {? RS.first()
   || _kod:=($(RS.TAB+'.'+RS.KOD))();
      SLO.cntx_psh();
      SLO.index('SL');
      SLO.prefix(SLU.ref());
      {? SLO.find_key(_kod,)
      || _slo:=SLO.ref()
      || SLO.blank();
         SLO.SLU:=SLU.ref();
         SLO.KOD:=_kod;
         SLO.TR:=($(RS.TAB+'.'+RS.TR))();
         {? SLO.add()
         || _slo:=SLO.ref()
         ?}
      ?};
      SLO.cntx_pop()
   ?};
   RS.cntx_pop()
?};
ETYPY.cntx_pop();
_slo

:Sign Version 2.0 jowisz:1048 2023/06/23 14:15:44 014ebfb33724d430c8107e0b496a4a06528b2020f3c016557869f058345087a178cbfe6e2ad6077cd29c3f622de41ade03bff3a8606003cba2261f87ed350579677ccc73b06763ff9ee3783f67ff38739d441cf5dd673fe6225c7d0980d584d5c24333e6d0fe57217f28bc0e1e0396118324f4ebe43ceebb63006563e502c789

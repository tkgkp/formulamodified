:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %tel_tmobile.fml
:: Utworzony: 02.06.2017
:: Autor: PJ
::======================================================================================================================
:: Zawartość: Formuły importu faktur telekomunikacyjnych z T-Mobile
::======================================================================================================================


\dok_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Import faktury telekomunikacyjnej - Formuła generuje nagłówek dokumentu księgowego
::   WE: _a - jeśli 0 to dodawanie DOK, jeżeli 1 to dodawanie EDOKUM
::----------------------------------------------------------------------------------------------------------------------
_ref:=null;

{? _a=0
|| _odd:=_b; _rej:=_c; _rvt:=_d; _rdo:=_e; _kh:=_f; _tkh:=_g;
   _slo:=exec('find_slokh','edi_wspolne',_rdo,_tkh)
|| _odd:=_b; _typ:=_c; _kh:=_d
?};

:: dołączanie dokumentu księgowego
{? _a=0
|| {? _odd & _rej & _rvt & _rdo & _kh
   || DOK.blank(1);
      DOK.ODD:=_odd;
      DOK.REJ:=_rej;
      DOK.RVAT:=_rvt;
      DOK.DOK_REJ:=_rdo;
      DOK.WYS:=_slo;
::    Typ importu poprzed EDI, N - nie EDI, S - pojazdy, P - paliwo
      DOK.EDI:='S';
      DOK.DR:=exec('data','#blank');
      DOK.JPK_V_T:=exec('typ_dok','obiegi2');
      KH.cntx_psh();
      KH.prefix();
      _t_pla:=0;
      {? KH.seek(_kh)
      || DOK.KH:=KH.SKR;
         DOK.KH_FULL:=KH.NAZ_P;
         DOK.KH_KRAJ:=KH.KRAJ; DOK.KH_KRISO:=KH.KRAJISO;
         DOK.MIASTO:=KH.MIASTO;
         DOK.UL:=KH.UL;
         DOK.DOM:=KH.DOM;
         DOK.LOKAL:=KH.LOKAL;
         DOK.POCZ:=KH.POCZ;
         DOK.KPOCZ:=KH.KPOCZ;
         DOK.NIP:=KH.NIP;
         DOK.SP_PL:=KH.PLATNOSC;
         {? KH.PLATNOSC<>null
         || _t_pla:=#exec('get_par','slo_slu',KH.PLATNOSC,2)
         ?}

      ?};
      KH.cntx_pop();

      DOK.KRAJ:=exec('kod_pl','slo_slu');
      DOK.JORG:=FINFO.NAROD;
      DOK.TR:='';
      DOK.cntx_psh();
      DOK.index('REJ');
      DOK.prefix(DOK.REJ);
      {? DOK.last() || _vnr:=DOK.NR+1 || _vnr:=1 ?};
      DOK.cntx_pop();
      DOK.DR:=date();
      DOK.NR:=_vnr;
      DOK.ZAR:=exec('usr_zar','dok_fks');
      DOK.NK:=__TABFNR.NR;
      DOK.SYM_ZEW:=__TABFNR.NR;
      DOK.A:=DOK.ZK:=DOK.ZP:=DOK.WSK_XD:='N';
      DOK.DTW:=__TABFNR.DTW;
      DOK.DOP:=__TABFNR.DOP;
      DOK.DTO:=__TABFNR.DTO;
      DOK.D4:=__TABFNR.D4;
      {? DOK.DTO<>date(0,0,0)
      || {? DOK.SP_PL || DOK.D3:=DOK.DTO+_t_pla || DOK.D3:=DOK.DTO+FINFO.OKRROZ ?}
      ?};
      DOK.DOKZRODL:='';
      {? DOK.add()
      || _ref:=DOK.ref();
         __TABFNR.IDADD:=DOK.uidref();
         __TABFNR.put();
         DOKUM.blank();
         DOKUM.bl_file('DOKUM',0);
         DOKUM.NAZWA:=file_csv;
         DOKUM.KR_OP:='Faktura za usługi telekomunikacyjne z TMOBILE';
         DOKUM.REFSQL:=$DOK.ref();
         DOKUM.FIRMA:=REF.FIRMA;
         DOKUM.DATA:=date();
         DOKUM.CZAS:=time();
         {? DOKUM.add() || DOKUM.bl_put('DOKUM',file_csv,1,,file_csv) ?};
         DOKUM.bl_file('DOKUM',1)
      || exec('add_kom','#message','Błąd podczas dodawania dokumentu księgowego'@,38,'UWAGA'@)
      ?}
   ?}
:: dołączanie faktury w obiegu
|? _a=1
|| {? _odd & _typ & _kh
   || EDOKUM.blank();
      EDOKUM.FIRMA:=REF.FIRMA;
      EDOKUM.ROK_F:=SSTALE.AR;
      EDOKUM.ODD:=_odd;
      EDOKUM.TYP:=_typ;
      EDOKUM.KHKH:=_kh;
      EDOKUM.DO:=__TABFNR.D4;
      EDOKUM.DOP:=__TABFNR.DS;
      EDOKUM.DW:=__TABFNR.DTO;
      EDOKUM.DATAW:=date();
      EDOKUM.WAL:=FINFO.NAROD;
      EDOKUM.ALERTY:='T';
      EDOKUM.AUTOMAT:=2;
      SKID.SL_KH:='';
      OBIEGI.DEL_ETAT:='';
      EDOKUM.TYP();
      EDOKUM.TYPOBIEG:=EDOKUM.TYP().TYPOBIEG;
      EDOKUM.DOSTAWCA:=OPERATOR.USER().OSOBA;
      EDOKUM.USERS:=OPERATOR.USER;
      EDOKUM.UNIK_ID:=tm_stamp();
      EDOKUM.TP:=__TABFNR.TP;
      EDOKUM.SYM:=__TABFNR.NR;
      EDOKUM.EDI:='S';
      EDOKUM.KOREKTA:='N';
      {? EDOKUM.add()
      || {? ETYPY.AUT_ID='T' & EDOKUM.ID=''
         || VAR_DEL.delete('id_edok');
            exec('ustal_numer','obiegi',1,SSTALE.AR,1,0);
            EDOKUM.ID:={? var_pres('id_edok')>0 || id_edok || '' ?};
            EDOKUM.put()
         ?};
         EDOKUM.memo_put(,'UW_OPDL');
         zakres:=1;
         exec('dol_edokos','obiegi');
         {? EDOKUM.TYP().ATR_ONE>0
         || exec('edk_atr_dolacz','obiegi',0)
         ?};
         _ref:=EDOKUM.ref();
         __TABFNR.IDADD:=EDOKUM.uidref();
         __TABFNR.put();
         EDOKUMZ.blank();
         EDOKUMZ.bl_file('EDOKUM',0);
         EDOKUMZ.NAZWA:=file_csv;
         EDOKUMZ.KR_OP:='Faktura telekominukacyjna z T-Mobile';
         EDOKUMZ.DOKUM:=EDOKUM.ref();
         EDOKUMZ.DATE:=date();
         EDOKUMZ.TIME:=time();
         {? EDOKUMZ.add() || EDOKUMZ.bl_put('EDOKUM',file_csv,1,,file_csv) ?};
         EDOKUMZ.bl_file('EDOKUM',1);
:: ustalenie maski w POZF
         POZF.use('pozf__'+(EDOKUM.name()+2));
         POZF.prefix()
      || exec('add_kom','#message','Błąd podczas dodawania faktury w obiegu'@,38,'UWAGA'@)
      ?}
   ?}
?};
_ref


\plik
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Import faktury telekomunikacyjnej - formuła na ścieżkę do pliku
:: ~OST: INFCOPY
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__TABFNR');
_file:='';
exec('myDIALOG','object');
_file:=myDIALOG.OpenFile('Wybór pliku'@);
{? _file<>''
|| {? exec('czy_fak_bp','%tel_tmobile',_file)
   || _ffile:=exec('filename','#string',_file);
      fcopy('@'+_file,_ffile,0,1,1);
      _file:=_ffile;
      exec('bp_find_fak','%tel_tmobile',_file)
   || _file:='@!'
   ?}
?};
_file


\bl_dok_data
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Zwraca datę do ustawienia na blank dla pól tabeli __TABFNR
::   WY: data
::----------------------------------------------------------------------------------------------------------------------
{? date()>=SSTALE.AO().POCZ & date()<=SSTALE.AO().KON
|| date()
|? date()>SSTALE.AO().KON
|| SSTALE.AO().KON
|? date()<SSTALE.AO().POCZ
|| SSTALE.AO().POCZ
?}


\bp_find_fak
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Import faktury telekomunikacyjnej - formuła wyszukuje unikalne nr fakur w pliku transakcji
::       orz wczytuje zawartość pliku do tabeli tymczasowej
::   WE: _a - nazwa pliku
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__TABFNR','__FNRLIN');
__TABFNR:=tab_tmp(1,'NR','STRING[50]','Nr faktury',
                    'DOP','DATE','Data operacji',
                    'DTW','DATE','Data zapisu',
                    'DTO','DATE','Data wystawienia',
                    'D4','DATE','Data otrzymania',
                    'DS','DATE','Data sprzedaży',
                    'TP','DATE','Termin płatności',
                    'IMPORT','STRING[1]','Czy importować?',
                    'IDADD','STRING[48]','IDADD faktury');

:: Nr faktury;Konto klienta;Nr telefonu;Taryfa;Us│uga;Us│uga szczegˇ│owo;Kwota netto;Stawka VAT
__FNRLIN:=tab_tmp(1,'NR','STRING[35]','Nr faktury',
                    'ZSB_TXT','STRING[100]','Nr telefonu',
                    'KOD_PRD','STRING[100]','Taryfa',
                    'NAZWA','STRING[100]','Usługa',
                    'NETTO','REAL','Netto',
                    'VAT','REAL','Vat',
                    'BRUTTO','REAL','Brutto',
                    'PVAT','STRING[100]','Pvat');

_f:=fopen(_a,'r',1);
_licznik:=0;
{? _f
|| progress(,'Trwa analiza pliku...'@,'Faktura telekomunikacyjna'@,1);
   {! |? (_wiersz:=fread(_f))<>'\n' |!
      _licznik+=1;
      {? _wiersz<>''
      || _wiersz:=STR.w952maz(_wiersz);
         _tab:=spli_str(_wiersz,';');
         {? obj_len(_tab)>0 & _licznik>4
         || _nr:=form(_tab[1]);
            {? ~__TABFNR.find_key(_nr)
            || __TABFNR.blank();
               __TABFNR.IMPORT:='T';
               __TABFNR.NR:=_nr;
               __TABFNR.DOP:=__TABFNR.DTW:=__TABFNR.DTO:=__TABFNR.D4:=__TABFNR.DS:=__TABFNR.TP:=exec('bl_dok_data','%tel_tmobile');
               __TABFNR.add()
            ?};
:: import pozycji do bufora
            __FNRLIN.blank();
            __FNRLIN.NR:=_nr;
            __FNRLIN.ZSB_TXT:=form(_tab[3]);
            __FNRLIN.KOD_PRD:=form(_tab[4]+' - '+_tab[5]+' - '+_tab[6]);
            __FNRLIN.NAZWA:=form(_tab[4]+' - '+_tab[5]+' - '+_tab[6]);
            {? _tab[7]*'zł' || _tab[7]:=gsub(_tab[7],'zł','') ?};
            __FNRLIN.NETTO:=(exec('str2real','#convert',_tab[7]))$2;
            __FNRLIN.VAT:=0;
            __FNRLIN.BRUTTO:=0;
            __FNRLIN.PVAT:=form(_tab[8]);
            __FNRLIN.add()
         ?};
         &_tab
      ?}
   !};
   prgs_clr();
   file_ln:=_licznik;
   fclose(_f)
?}


\sel_exit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ []
:: OPIS: Obsługa akcji/przycisku Import w oknie z listą faktur
::   WY: 1/0
::----------------------------------------------------------------------------------------------------------------------
__TABFNR.cntx_psh();
__TABFNR.prefix();
_exit:=1; _nr:=null;
{? __TABFNR.first()
|| {! |?
      {? __KINDIMP=0 & __TABFNR.IMPORT='T'
      || {? __TABFNR.DOP=date(0,0,0) | __TABFNR.DTW=date(0,0,0) | __TABFNR.DTO=date(0,0,0) | __TABFNR.D4=date(0,0,0)
         || _exit:=0; _nr:=__TABFNR.NR
         ?}
      |? __KINDIMP=1 & __TABFNR.IMPORT='T'
      || {? __TABFNR.DS=date(0,0,0) | __TABFNR.TP=date(0,0,0) | __TABFNR.DTO=date(0,0,0) | __TABFNR.D4=date(0,0,0)
         || _exit:=0; _nr:=__TABFNR.NR
         ?}
      ?};
      _exit & __TABFNR.next()
   !}
?};
__TABFNR.cntx_pop();
{? _exit
|| _check:=0;
   __TABFNR.first();
   {! |?
      {? __TABFNR.IMPORT='T' || _check:=1 ?};
      _check=0 & __TABFNR.next()
   !};
   {? _check>0
   || __PALIMP:=1; sel_exit()
   || FUN.emsg('Nie wybrano żadnej z faktur do importu.'@)
   ?}
|| FUN.emsg('Brak uzupełnionych danych (faktura: %1).'@[_nr]);
   __TABFNR.find_key(_nr)
?}


\read_tmobile
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Import faktury z T-Mobile
::   WE: _a - cel importu: 0 lub brak = dokument księgowy, 1 - faktura w obiegu
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('__wybopc')>0 & __wybopc=2 || _r_only:=1 || _r_only:=0 ?};
{? _=0 || _a:=0 ?};
__KINDIMP:=_a;

_todd:=exec('FindInSet','#table','EDI_D','K','_ODD',EDI_Z.C,"EDI_D.FLF",,,'');
_odd:=exec('find_odd','fst',_todd);
{? _a=0
|| _trej:=exec('FindInSet','#table','EDI_D','K','_REJ',EDI_Z.C,"EDI_D.FLF",,,'');
   _rej:=exec('rejestr','edi_wspolne',__PARSES.getVal('OkresRok').OKRO_F,_odd,_trej);
   _trvt:=exec('FindInSet','#table','EDI_D','K','_RVT',EDI_Z.C,"EDI_D.FLF",,,'');
   _rvt:=exec('rejvat','edi_wspolne',_rej,_trvt);
   _trdo:=exec('FindInSet','#table','EDI_D','K','_RDO',EDI_Z.C,"EDI_D.FLF",,,'');
   _rdo:=exec('dokrej','edi_wspolne',_rej,_trdo)
|? _a=1
|| _ttyp:=exec('FindInSet','#table','EDI_D','K','_TYP',EDI_Z.C,"EDI_D.FLF",,,'');
   _typ:=exec('typfak','edi_wspolne',_ttyp)
?};
_tkh:=exec('FindInSet','#table','EDI_D','K','_KH',EDI_Z.C,"EDI_D.FLF",,,'');
_kh:=exec('find_kh','edi_wspolne',_tkh);

 _txt_err:='';
{? _a=0 & ~(_odd & _rej & _rvt & _rdo & _kh)
|| {? _odd=null || _txt_err+='j. księgowa, ' ?};
   {? _rej=null || _txt_err+='rejestr, ' ?};
   {? _rvt=null || _txt_err+='rejestr VAT, ' ?};
   {? _rdo=null || _txt_err+='typ dokumentu, ' ?};
   {? _kh=null || _txt_err+='kontrahent, ' ?};
   {? _txt_err<>'' || _txt_err:=_txt_err-2 ?};
   exec('add_kom','#message','Niepoprawna konfiguracja importu (%1)'@[_txt_err],38,'UWAGA'@)
|? _a=1 & ~(_odd & _typ & _kh)
|| _txt:='';
   {? _odd=null || _txt_err+='j. księgowa, ' ?};
   {? _typ=null || _txt_err+='typ faktury, ' ?};
   {? _kh=null || _txt_err+='kontrahent, ' ?};
   {? _txt_err<>'' || _txt_err:=_txt_err-2 ?};
   exec('add_kom','#message','Niepoprawna konfiguracja importu (%1)'@[_txt_err],38,'UWAGA'@)
?};

{? _txt_err<>'' || return(0) ?};

exec('RB','object');

exec('czytaj','#stalesys',,FINFO);
exec('czytaj','#stalesys',,XINFO);
SSTALE.AO:=__PARSES.getVal('OkresRok').OKRO_F;
SSTALE.AR:=SSTALE.AO().ROK;
exec('open_tabele','open_tab','F');
DOK.prefix();

file_ln:=0;
file_csv:=exec('plik','%tel_tmobile');
{? file_csv='@!'
|| exec('add_kom','#message','Użytkownik wskazał plik o nieprawidłowej strukturze.'@,38,'UWAGA'@);
   VAR_DEL.delete('file_ln');
   return(0)
|? file_csv=''
|| VAR_DEL.delete('file_ln');
   return(0)
?};
__PALIMP:=0;

{? file_csv<>''
|| {? __TABFNR.first()
   ||  {? _r_only=1
      || VAR_DEL.delete('file_ln');
         return(1)
      ?};
      _win:=__TABFNR.mk_sel('Lista faktur w pliku: %1'@[file_csv],,0,'_tabfnr',10,5,12);
      __TABFNR.win_fld(_win,,'NR',,,30,,1,'Nr faktury'@,,'Numer faktury w pliku'@);
      __TABFNR.win_fld(_win,,'IMPORT',,,10,,1,'Czy importować?'@,,'Czy importować fakturę?'@,2,,"'T'","'N'");
      {? _a=0
      || __TABFNR.win_fld(_win,,'DOP',,,10,,,'Data operacji'@,,'Data operacji'@);
         __TABFNR.win_fld(_win,,'DTW',,,10,,,'Data zapisu'@,,'Data zapisu'@)
      |? _a=1
      || __TABFNR.win_fld(_win,,'DS',,,10,,,'Data sprzedaży'@,,'Data sprzedaży'@);
         __TABFNR.win_fld(_win,,'TP',,,10,,,'Termin płatności'@,,'Termin płatności'@)
      ?};
      __TABFNR.win_fld(_win,,'DTO',,,10,,,'Data wystawienia'@,,'Data wystawienia'@);
      __TABFNR.win_fld(_win,,'D4',,,10,,,'Data otrzymania'@,,'Data otrzymania'@);

      __TABFNR.win_act(_win,,'Formuła'@,'Zaznacz'@,,'Wybierz do importu'@,,"__TABFNR.IMPORT:='T';__TABFNR.put()",1,1,,,'Z');
      __TABFNR.win_act(_win,,'Formuła'@,'Odznacz'@,,'Zrezygnuj z importu'@,,"__TABFNR.IMPORT:='N';__TABFNR.put()",0,1,,,'O');
      __TABFNR.win_act(_win,,'Popraw');
      __TABFNR.win_act(_win,,'Formuła'@,'Importuj faktury'@,,'Importuj wszystkie faktury'@,,"exec('sel_exit','%tel_tmobile')",,,,,'I');
      __TABFNR.win_act(_win,,'Rekord',,,,"exec('record_before','%tel_tmobile')","exec('record_after','%tel_tmobile')");

      __TABFNR.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Popraw'@],'menu:P');
      __TABFNR.win_btn(_win,'text=%1,btn_label_align=center,panel=right,align=begin'['Importuj faktury'@],'menu:I');
      __TABFNR.win_sel(_win);
      __TABFNR.select()
   || exec('add_kom','#message','Brak faktur we wskazanym pliku.'@,38,'UWAGA'@);
      VAR_DEL.delete('file_ln');
      return(-1)
   ?}
?};

{? __PALIMP=0
|| exec('add_kom','#message','Użytkownik zrezygnował z importu faktury telekomunikacyjnej.'@,38,'UWAGA'@);
   VAR_DEL.delete('file_ln');
   return(-1)
?};

_licznik:=0;
{? __FNRLIN.first()>0 & __TABFNR.first()
|| {! |?
      {? __TABFNR.IMPORT='T'
      || {? _a=0
         || _ref:=exec('dok_add','%tel_tmobile',_a,_odd,_rej,_rvt,_rdo,_kh,_tkh)
         |? _a=1
         || _ref:=exec('dok_add','%tel_tmobile',_a,_odd,_typ,_kh)
         ?};

         {? _ref
         || __FNRLIN.prefix(__TABFNR.NR,);
            {? __FNRLIN.first()
            || _licznik:=0;
               _size:=__FNRLIN.size();
               {! |?
                  _licznik+=1;
                  progress((_licznik/_size)*100,'Trwa odczyt danych faktury: %1, pozycja nr: %2.'@[__TABFNR.NR,form(_licznik,9,0,'99')],'Pozycje faktury telekomunikacyjnej'@,0);
                  POZF.blank(1);
                  {? _a=0 || POZF.DOK:=_ref
                  |? _a=1 || POZF.EDOKUM:=_ref
                  ?};
                  {? __FNRLIN.ZSB_TXT<>'' || POZF.ZASOB:=exec('find_zasob','edi_wspolne',__FNRLIN.ZSB_TXT,'S') ?};
                  POZF.LP:=exec('bl_pf_lp','dok_fks');
                  POZF.JM:=exec('jm_szt','edi_wspolne');
                  POZF.NAZ:=__FNRLIN.NAZWA;
                  POZF.IL:=1;
                  POZF.WN:=__FNRLIN.NETTO;
                  POZF.DZ:=date();
                  POZF.C:=__FNRLIN.NETTO;
                  POZF.KODP:=__FNRLIN.KOD_PRD;
::                grupa podatkowa
                  {? _a=0
                  || POZF.GRVAT:=exec('find_grvat','dok_fks',_kh,POZF.KODP,POZF.DOK().REJ,POZF.DOK().EDI,0)
                  ?};
::                stawka VAT
                  {? __FNRLIN.PVAT<>''
                  || POZF.SV:=exec('procvat4xml','obiegi2',__FNRLIN.PVAT)
                  || POZF.SV:=null
                  ?};
                  {? POZF.SV<>null
                  || _tmp:=#(form(gsub(__FNRLIN.PVAT,'%','')));
                     POZF.WV:=(POZF.WN*(_tmp/100))$2;
                     POZF.WB:=POZF.WN+POZF.WV
                  ?};
                  POZF.add();
                  __FNRLIN.next()
               !}
            ?}
         ?}
      ?};
      __TABFNR.next()
   !};
   prgs_clr()
?};

_wyn:=1;
__TABFNR.prefix();
{? __TABFNR.first()
|| {! |?
      {? __TABFNR.IDADD='' & __TABFNR.IMPORT='T'
      || exec('add_kom','#message','Import niepełny, nie utworzono wszystkich wybranych faktur ze wskazanego pliku.'@,38,'UWAGA'@);
         _wyn:=0
      ?};
      _wyn & __TABFNR.next()
   !}
?};
VAR_DEL.delete('file_ln');
EDI_Z.SYM:=file_csv;
_wyn


\record_after
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Formuła na rekord po w tabeli tymczasowej __TABFNR
::----------------------------------------------------------------------------------------------------------------------
_return:='';
{? __KINDIMP=0 & __TABFNR.DOP=date(0,0,0) & __TABFNR.IMPORT='T'
|| FUN.info('Nie podano daty operacji.'@);
   _return:='DOP'
|? __KINDIMP=0 & __TABFNR.DTW=date(0,0,0) & __TABFNR.IMPORT='T'
|| FUN.info('Nie podano daty zapisu.'@);
   _return:='DTW'
|? __TABFNR.DTO=date(0,0,0) & __TABFNR.IMPORT='T'
|| FUN.info('Nie podano daty wystawienia.'@);
   _return:='DTO'
|? __TABFNR.D4=date(0,0,0) & __TABFNR.IMPORT='T'
|| FUN.info('Nie podano daty otrzymania.'@);
   _return:='D4'
|? __KINDIMP=1 & __TABFNR.DS=date(0,0,0) & __TABFNR.IMPORT='T'
|| FUN.info('Nie podano daty sprzedaży.'@);
   _return:='DS'
|? __KINDIMP=1 & __TABFNR.TP=date(0,0,0) & __TABFNR.IMPORT='T'
|| FUN.info('Nie podano terminu płatności.'@);
   _return:='TP'
|? __KINDIMP=0 & __TABFNR.D4<__TABFNR.DTO & __TABFNR.IMPORT='T'
|| FUN.emsg('Data otrzymania nie może być wcześniejsza od daty wystawienia.'@);
   _return:='D4'
|? __KINDIMP=0 & (__TABFNR.DTW<SSTALE.AO().POCZ | __TABFNR.DTW>SSTALE.AO().KON) & SSTALE.AO().KON<>date(0,0,0) & __TABFNR.IMPORT='T'
|| FUN.emsg('Data zapisu pochodzi spoza aktywnego okresu (%1).'@[SSTALE.AO().NAZ+' '+SSTALE.AR().NAZ]);
   _return:='DTW'
|? __KINDIMP=1 & (__TABFNR.DS<SSTALE.AR().POCZ_ROK | __TABFNR.DS>SSTALE.AR().KON_ROK) & __TABFNR.IMPORT='T'
|| FUN.emsg('Data sprzedaży pochodzi spoza aktywnego roku (%1).'@[SSTALE.AR().NAZ]);
   _return:='DS'
?};
_return


\record_before
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [1700]
:: OPIS: Formuła na rekord przed tabeli __TABFNR
::----------------------------------------------------------------------------------------------------------------------
_win:=__TABFNR.win_sel('?');
{? __TABFNR.IMPORT='T'
|| __TABFNR.actions_grayed(_win,'Z');
   __TABFNR.actions(_win,,'O',1)
|? __TABFNR.IMPORT='N'
|| __TABFNR.actions_grayed(_win,'O');
   __TABFNR.actions(_win,,'Z',1)
?};
''


\czy_fak_bp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PJ [17.28]
:: OPIS: Import faktury telekomunikacyjnej - formuła sprawdza czy podany plik jest fakturą z T-Mobile
::   WE: _a - nazwa pliku
::   WY: 1 - tak, faktura z T-Mobile, 0 - struktura pliku niezgodna z oczekiwaną
:: ~OST: INFOPEN
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_f:=fopen('@'+_a,'r',0);
{? _f
|| _wiersz:=fread(_f);
   _wiersz:=fread(_f);
   _wiersz:=fread(_f);
:: kontrola 4 wiersza
   _wiersz:=fread(_f);
   {? form(_wiersz)<>''
   || {? _wiersz*'Nr faktury;Konto klienta;Nr telefonu;Taryfa;'
      || _result:=1
      ?}
   ?};
   fclose(_f)
?};
_result

:Sign Version 2.0 jowisz:1045 2023/11/17 11:03:18 3f3e6a8a34c4ea8fcc7ab430d414034284157d485eedd5b0dae417d142612d9f9a944b4e95059024e792cf2efb4f1ba158d90c997af4bffefc05296b7bf3bf0911a6e2cd9ecf9eeefb0e7b8614da03f16e16d0021427282100cd5694a800477ee1b6b4903a37f13054208c966d60092c7b88c80f7bcf1a3719825871e5f3b165

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %spr_role.fml
:: Utworzony: 2022.09.28
:: Autor: AMK
::======================================================================================================================
:: Zawartość:  Formuła listuje czynności nieprzypięte do roli z toru na którym są umieszczone w procesie.
::======================================================================================================================


\analiza_role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Formuła listuje czynności nieprzypięte do roli z toru na którym są umieszczone w procesie.
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('ANALIZA');
ANALIZA:=tab_tmp(3,'PROC','STRING[100]','Symbol procesu',
                   'ROLA','STRING[100]','Rola',
                   'ACT','STRING[12]','Symbol czynności',
                   'PROC_ACT','STRING[1]','Proces wymagany',
                   'PROC_NAM','STRING[45]','Nazwa procesu',
                   'ACT_NAZ','STRING[30]','Nazwa czynności'
                );
_wer:=ANALIZA.mk_sel('Czynności nieprzypięte do ról z toru','P',,'tmpanal_wer',,,,,'U','U');
ANALIZA.win_fld(_wer,,'PROC',,,20);
ANALIZA.win_fld(_wer,,'PROC_ACT',,,1);
ANALIZA.win_fld(_wer,,'PROC_NAM',,,30);
ANALIZA.win_fld(_wer,,'ROLA',,,30);
ANALIZA.win_fld(_wer,,'ACT',,,12);
ANALIZA.win_fld(_wer,,'ACT_NAZ',,,30);
ANALIZA.win_act(_wer,0,'Kolejność');
B_ROLE.cntx_psh(); B_ROLE.index('UNIK'); B_ROLE.prefix();
B_ACTION.cntx_psh(); B_ACTION.index('UNIK'); B_ACTION.prefix();
B_ELE.cntx_psh(); B_ELE.index('SYMBOL'); B_ELE.prefix();
B_ACTROL.cntx_psh(); B_ACTROL.index('UNIK');
B_PROC.cntx_psh(); B_PROC.index('FIRMA'); B_PROC.prefix(REF.FIRMA);
B_PREL.cntx_psh();
{? B_PROC.first()
|| {! |?
      B_PREL.index('PROC'); B_PREL.prefix(B_PROC.ref(),'B_LANE');
      {? B_PREL.first()
      || {! |?
            _lane:=B_PREL.B_ROLE; _lanen:=B_PREL.B_ROLE().NAME;
            B_PREL.cntx_psh(); B_PREL.index('TREE'); B_PREL.prefix(B_PROC.ref(),B_PREL.UID);
            {? B_PREL.first()
            || {! |?
                  {? B_PREL.CLASS='B_ACTION' & B_PREL.TYPE<>'S'
                  || B_ACTION.prefix(B_PREL.B_ELE().SYMBOL,);
                     {? B_ACTION.first()
                     || B_ACTROL.prefix(REF.FIRMA,_lane,B_ACTION.ref());
                        {? ~B_ACTROL.first()
                        || ANALIZA.prefix(B_PROC.SYMBOL,_lanen,B_ACTION.UID);
                           {? ~ANALIZA.first()
                           || ANALIZA.blank();
                              ANALIZA.PROC:=B_PROC.SYMBOL;
                              ANALIZA.PROC_NAM:=B_PROC.NAME;
                              ANALIZA.ROLA:=_lanen;
                              ANALIZA.ACT:=B_ACTION.UID;
                              ANALIZA.ACT_NAZ:=B_ACTION.NAME;
                              ANALIZA.PROC_ACT:=B_PROC.CRUCIAL;
                              ANALIZA.add()
                           ?}
                        ?}
                     ?}
                  ?};
                  B_PREL.next()
               !}
            ?};
            B_PREL.cntx_pop();
            B_PREL.next()
         !}
      ?};
      B_PROC.next()
   !}
?};
B_PROC.cntx_pop(); B_PREL.cntx_pop(); B_ACTROL.cntx_pop(); B_ELE.cntx_pop(); B_ACTION.cntx_pop(); B_ROLE.cntx_pop();
ANALIZA.win_sel(_wer);
ANALIZA.prefix(); ANALIZA.select();
VAR_DEL.delete('ANALIZA')


:Sign Version 2.0 jowisz:1048 2023/06/23 14:15:44 930889462c8fa923aed2fc580579d971579849c6a87fe97071b996a20aca6ec207e710e22d52303f16f3b34d9fa459281422991c01347084efdda6a7119b49c4d4d5f49d3d9dc17f52af7221aa31ab6d494686a8ff77e82bce62444dcebeb7191f6d7fdc131110def0b693730ce4d58c2ef2fda227dbdc7cadb88fcacf5ccee7

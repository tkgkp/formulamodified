:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %skanuj.fml
:: Utworzony: 24.03.2020
:: Autor: [MB]
::======================================================================================================================
:: Zawartość: Formuły do obsługi skanowania i OCR dokumentów
::======================================================================================================================


\sp_pl
::----------------------------------------------------------------------------------------------------------------------
:: OPIS: Formula zwraca sposób płatności dla dokumentu
::   WE _a - nazwa płatności z OCR
::      _b  - Wywołanie badania dla 'DOK' lub 'EDOKUM'
::      np: Przelew na rachunek bankowy, Gotówka, Karta, Za pobraniem, Przedpłata, Barter, Kompensata, Inny
::
::----------------------------------------------------------------------------------------------------------------------
{? _=2 || _typ:=_b || _typ:='DOK' ?};
_kod:='';
{? 7+_a='Przelew'
|| _kod:='PRZEL'
|? _a='Gotówka'
|| _kod:='GOTOW'
?};
_slo:=null;
exec('czytaj','#stalesys',,XINFO,'SLP');
{? XINFO.SLP & 7+_a='Przelew'
|| SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(XINFO.SLP);
   {? SLO.first()
   || {!
      |? _rd:=exec('get_par','slo_slu',SLO.ref(),6);
         {? _typ='DOK'
         || {? DOK.D3={? _rd='S' || DOK.DOP |? _rd='O' || DOK.D4 || DOK.DTO ?}+#exec('get_par','slo_slu',SLO.ref(),2)
            || _slo:=SLO.ref()
            ?}
         || {? EDOKUM.TP={? _rd='S' || EDOKUM.DOP |? _rd='O' || EDOKUM.DO || EDOKUM.DW ?}+#exec('get_par','slo_slu',SLO.ref(),2)
            || _slo:=SLO.ref()
            ?}
         ?};
         _slo=null & SLO.next()
      !}
   ?};
   SLO.cntx_pop()
?};
{? _slo=null & XINFO.SLP & _kod<>''
|| SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(XINFO.SLP,_kod,);
   {? SLO.first()
   || _slo:=SLO.ref()
   ?};
   SLO.cntx_pop()
?};
_slo



\kh_skr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Formuła zwraca skrót kontrahenta dodawanego z danych OCR
::   WE: _a - obiekt z danymi z OCR
::----------------------------------------------------------------------------------------------------------------------
_prefix:='OCR';
_max:=10;
_nr:=1;

KH.cntx_psh();
KH.index('SKR'); KH.prefix(2);
{!
|? _skr:=_prefix+(((_max*'0')+$_nr)+(_max-+_prefix));
   {? KH.find_key(_skr,)
   || _nr+=1;
      1
   ?}
!};
KH.cntx_pop();
_skr


\kh_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Zwraca kod na kontrahenta dodawanego z danych OCR.
::----------------------------------------------------------------------------------------------------------------------
_kod:=Plugin.run('KONTRAH_T_KH_001');
{? _kod=''
|| _number:=0; _dl:=5;
   KH.cntx_psh(); KH.index('KOD1'); KH.prefix();
   _kod:={? KH.last()
         || {!
            |? {? #KH.KOD=0 | +KH.KOD<>5
               || _step:=KH.prev()
               || _dl:=+KH.KOD;
                  _number:=#KH.KOD;
                  _step:=0
               ?};
               _step
            !};
            {? +$(_number+1)>_dl
            || ''
            || form(((_number)+1),-_dl,0,'9.')
            ?}
         || '00001'
         ?};
   KH.cntx_pop()
?};
_kod


\kh_skr_web
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [20.14]
:: OPIS: Formuła zwraca skrót kontrahenta dodawanego z danych OCR
::   WE: _a - obiekt z danymi z OCR
::----------------------------------------------------------------------------------------------------------------------
_prefix:='ZEW';
_max:=10;
_nr:=1;

KH.cntx_psh();
KH.index('SKR'); KH.prefix(2);
{!
|? _skr:=_prefix+(((_max*'0')+$_nr)+(_max-+_prefix));
   {? KH.find_key(_skr,)
   || _nr+=1;
      1
   ?}
!};
KH.cntx_pop();
_skr


\kh_odb_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [20.42]
:: OPIS: Zwraca kod na kontrahenta dodawanego z danych OCR.
::----------------------------------------------------------------------------------------------------------------------
_number:=0; _dl:=5;
KH_ODB.cntx_psh(); KH_ODB.index('KOD'); KH_ODB.prefix();
_kod:={? KH_ODB.last()
      || {!
         |? {? #KH_ODB.KOD=0 | +KH_ODB.KOD<>5
            || _step:=KH_ODB.prev()
            || _dl:=+KH_ODB.KOD;
               _number:=#KH_ODB.KOD;
               _step:=0
            ?};
            _step
         !};
         {? +$(_number+1)>_dl
         || ''
         || form(((_number)+1),-_dl,0,'9.')
         ?}
      || '00001'
      ?};
KH.cntx_pop();
_kod

:Sign Version 2.0 jowisz:1048 2023/06/23 14:15:44 5539d2da35d66de643bfdefd30b5a811542443d6c6da958a781c2b8c13a0e2c890263f59bd8feae35aa56a875c9274bdf211c8a3f112628ffe4f0eacb08ccc04ec382528061455b38cc1b584e671f85189b5ce97c515f90afb25e7584d979e6c949d310656c77ed30c427afcdc20c30a2cb7ed182644937e4ae2faab9b92f966

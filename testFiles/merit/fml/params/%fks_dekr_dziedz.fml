:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: %fks_dekr_dziedz.fml
:: Utworzony: 16.06.2015
:: Autor: AMK
::======================================================================================================================
:: Zawartość: Obsługa dekretacji dziedzinowej
::======================================================================================================================



\fak_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2006]
:: OPIS: zwraca kod waluty z naglowka dokumentu sprzedazy
::   WE:  _a  - 'WN' lub 'WB' lub 'WV' lub 'IL' lub 'WW' lub 'WPB' lub 'WPV'
::   WY: zwracana wartosc wg parametru _a (wartosc : netto, brutto, vat, ilosc, walut, brutto_waluta, vat_waluta)
::  OLD: \fak_war/ksg_form.fml
::----------------------------------------------------------------------------------------------------------------------
_knetto:=_kvat:=_kbrutto:=0; _prosty:=1;
{? EKSG.TSP='F' & (FAKS.SZ='S' | (FAKS.SZ='Z' & DEK_NAG.VPOZ_STD='R'))
|| _prosty:=0;
:: dekretacja wg FAP (__Fap_z)
   _wg_td:=var_press('__Fap_z')>0;
   _rozzal:=exec('sp_rozlicz_zal','%fks_dekr_dziedz');
:: wartości w walucie opodatkowania
   _netto   :={? _wg_td & var_press('WWAL_P',__Fap_z)>0 || __Fap_z.WWAL_P$2 || FAP.WWAL_P$2 ?};
   _brutto  :={? _wg_td & var_press('BWAL_P',__Fap_z)>0 || __Fap_z.BWAL_P$2 || FAP.BWAL_P$2 ?};
   _vat     :={? _wg_td & var_press('VWAL_P',__Fap_z)>0 || __Fap_z.VWAL_P$2 || FAP.VWAL_P$2 ?};
:: ilość
   _il      :={? _wg_td & var_press('IL',__Fap_z)>0 || __Fap_z.IL || FAP.IL ?};
:: wartości w walucie handlowej
   _walut   :={? FAKS.WAL<>INFO.NAROD || {? _wg_td & var_press('WWAL',__Fap_z)>0 || __Fap_z.WWAL || FAP.WWAL ?} || 0 ?}$2;
   _wpb     :={? FAKS.WAL<>INFO.NAROD || {? _wg_td & var_press('BWAL',__Fap_z)>0 || __Fap_z.BWAL || FAP.BWAL ?} || 0 ?}$2;
   _wpv     :={? FAKS.WAL<>INFO.NAROD || {? _wg_td & var_press('VWAL',__Fap_z)>0 || __Fap_z.VWAL || FAP.VWAL ?} || 0 ?}$2;
   _wpn     :={? FAKS.WAL<>INFO.NAROD || {? _wg_td || _wpb-_wpv || FAP.BWAL-FAP.VWAL ?} || 0 ?}$2;
:: wartości w walucie narodowej
   _knetto  :={? _wg_td & var_press('WN',__Fap_z)>0 || __Fap_z.WN || {? _rozzal || FAKSV.WN_Z + FAKSV.WN_R || FAP.WN ?} ?};
   _kvat    :={? _wg_td & var_press('WV',__Fap_z)>0 || __Fap_z.WV || {? _rozzal || FAKSV.WV_Z + FAKSV.WV_R || FAP.WV ?} ?};
   _kbrutto :={? _wg_td & var_press('WB',__Fap_z)>0 || __Fap_z.WB || {? _rozzal || FAKSV.WB_Z + FAKSV.WB_R || FAP.WB ?} ?}

|? (EKSG.TSP='F' | ((EKSG.TSP='B' | EKSG.TSP='A') & DEK_NAG.VPOZ_STD<>'R')) & FAKS.SZ='Z'
||
:: wartości w walucie opodatkowania
   _netto   :=FAKSV.WN$2;
   _brutto  :=FAKSV.WB$2;
   _vat     :=FAKSV.WV$2;
:: ilość
   _il      :=0;
:: wartości w walucie handlowej
   _walut   :={? FAKS.WAL<>INFO.NAROD || FAKSV.WWN || 0 ?}$2;
   _wpb     :={? FAKS.WAL<>INFO.NAROD || FAKSV.WW || 0 ?}$2;
   _wpv     :={? FAKS.WAL<>INFO.NAROD || FAKSV.WWV || 0 ?}$2;
   _wpn     :={? FAKS.WAL<>INFO.NAROD || FAKSV.WWN || 0 ?}$2;
:: wartości w walucie narodowej
   {? FAKS.NWAL<>FINFO.NAROD
   || _sv      :=#((FAKSV.SV().KOD*'%')+FAKSV.SV().KOD-1)/100;
      _knetto  :=_walut;
      EKSG.NKRS:={? Qwksg.FKNKRS=0 || 1 || Qwksg.FKNKRS ?};
      _jedn:=exec('jed_wal','waluty',FAKS.WAL().KOD);
      _knetto  :=(_knetto*EKSG.NKRS/_jedn)$2;
      _kvat    :=(_knetto*_sv)$2;
      _kbrutto :=_knetto+_kvat
   || _knetto  :=_netto;
      _kvat    :=_vat;
      _kbrutto :=_brutto
   ?}
|? EKSG.TSP='B'
|| _prosty:=0;
   FAP.FAKS().SYM; exec('wysw_kor','faktury_poz');
:: wartości w walucie opodatkowania
   _netto   :=FKOR.BWWAL_P$2;
   _brutto  :=FKOR.BBWAL_P$2;
   _vat     :=FKOR.BVWAL_P$2;
:: ilość
   _il      :=FKOR.BIL;
:: wartości w walucie handlowej
   _walut   :={? FAKS.WAL<>INFO.NAROD || FKOR.BWWAL || 0 ?}$2;
   _wpb     :={? FAKS.WAL<>INFO.NAROD || FKOR.BBWAL || 0 ?}$2;
   _wpv     :={? FAKS.WAL<>INFO.NAROD || FKOR.BVWAL || 0 ?}$2;
   _wpn     :={? FAKS.WAL<>INFO.NAROD || FKOR.BBWAL-FKOR.BVWAL || 0 ?}$2;
:: wartości w walucie narodowej
   _knetto  :=FKOR.BWN;
   _kvat    :=FKOR.BWV;
   _kbrutto :=FKOR.BWB

|? EKSG.TSP='A' & (FAKS.SZ='S' | (FAKS.SZ='Z' & DEK_NAG.VPOZ_STD='R'))
|| _prosty:=0;
   exec('wysw_kor','faktury_poz');
:: wartości w walucie opodatkowania
   _netto   :=FKOR.BWWAL_P$2;
   _brutto  :=FKOR.BBWAL_P$2;
   _vat     :=FKOR.BVWAL_P$2;
:: ilość
   _il      :=FKOR.BIL;
:: wartości w walucie handlowej
   _walut   :={? FAKS.WAL<>INFO.NAROD || FKOR.BWWAL || 0 ?}$2;
   _wpb     :={? FAKS.WAL<>INFO.NAROD || FKOR.BBWAL || 0 ?}$2;
   _wpv     :={? FAKS.WAL<>INFO.NAROD || FKOR.BVWAL || 0 ?}$2;
   _wpn     :={? FAKS.WAL<>INFO.NAROD || FKOR.BBWAL-FKOR.BVWAL || 0 ?}$2;
:: wartości w walucie narodowej
   _knetto  :=FKOR.BWN;
   _kvat    :=FKOR.BWV;
   _kbrutto :=FKOR.BWB
|? EKSG.TSP='Z'
||
:: wartości w walucie opodatkowania
   _netto   :=FAKSV.WN_Z$2;
   _brutto  :=FAKSV.WB_Z$2;
   _vat     :=FAKSV.WV_Z$2;
:: ilość
   _il      :=0;
:: wartości w walucie handlowej
   _walut   :={? FAKS.WAL<>INFO.NAROD || FAKSV.WWN_Z || 0 ?}$2;
   _wpb     :={? FAKS.WAL<>INFO.NAROD || FAKSV.WW_Z || 0 ?}$2;
   _wpv     :={? FAKS.WAL<>INFO.NAROD || FAKSV.WWV_Z || 0 ?}$2;
   _wpn     :={? FAKS.WAL<>INFO.NAROD || FAKSV.WWN_Z || 0 ?}$2;
:: wartości w walucie narodowej
   {? FAKS.NWAL<>FINFO.NAROD
   || _sv      :=#((FAKSV.SV().KOD*'%')+FAKSV.SV().KOD-1)/100;
      _knetto  :=_walut;
      EKSG.NKRS:={? Qwksg.FKNKRS=0 || 1 || Qwksg.FKNKRS ?};
      _jedn:=exec('jed_wal','waluty',FAKS.WAL().KOD);
      _knetto  :=(_knetto*EKSG.NKRS/_jedn)$2;
      _kvat    :=(_knetto*_sv)$2;
      _kbrutto :=_knetto+_kvat
   || _knetto  :=_netto;
      _kvat    :=_vat;
      _kbrutto :=_brutto
   ?}
|? EKSG.TSP='R'
||
:: rozliczenie zaliczki
:: wartości w walucie opodatkowania
   _netto   :=FAKSV.WN_R$2;
   _brutto  :=FAKSV.WB_R$2;
   _vat     :=FAKSV.WV_R$2;
:: ilość
   _il      :=0;
:: wartości w walucie handlowej
   _walut   :={? FAKS.WAL<>INFO.NAROD || FAKSV.WW_R || 0 ?}$2;
   _wpb     :={? FAKS.WAL<>INFO.NAROD || FAKSV.WW_R || 0 ?}$2;
   _wpv     :=0;
   _wpn     :={? FAKS.WAL<>INFO.NAROD || FAKSV.WW_R || 0 ?}$2;
:: wartości w walucie narodowej
   _sv      :=#((FAKSV.SV().KOD*'%')+FAKSV.SV().KOD-1)/100;
   {? FAKS.NWAL<>FINFO.NAROD
   || _knetto  :=_walut;
      EKSG.NKRS:={? Qwksg.FKNKRS=0 || 1 || Qwksg.FKNKRS ?};
      _jedn:=exec('jed_wal','waluty',FAKS.WAL().KOD);
      _knetto  :=(_knetto*EKSG.NKRS/_jedn)$2;
      _kvat    :=(_knetto*_sv)$2;
      _kbrutto :=_knetto+_kvat
   || _knetto  :=_netto;
      _kvat    :=_vat;
      _kbrutto :=_brutto
   ?}
||
:: wartości w walucie opodatkowania
   _netto   :=0;
   _brutto  :=0;
   _vat     :=0;
:: ilość
   _il      :=0;
:: wartości w walucie handlowej
   _walut   :=0;
   _wpb     :=0;
   _wpv     :=0;
   _wpn     :=0;
:: wartości w walucie narodowej
   _knetto  :=0;
   _kvat    :=0;
   _kbrutto :=0
?};
{? FAP.WAL = INFO.NAROD || _walut:=0 ?};
{? _a ='WN'  || {? FAKS.PROC().PROCED<>'' & FAP.WAL_H = INFO.NAROD
                || _netto
                |? FAKS.PROC().PROCED='' & FAP.WAL_P = INFO.NAROD
                || _netto
                || _wpn
                ?}
|? _a ='WB'  || {?  FAKS.PROC().PROCED<>'' & FAP.WAL_H = INFO.NAROD
                || {? FAKS.SP_WYM='T' & FAKS.WAL<>FAKS.NWAL &
                      {? _prosty || FAKSV.SP_WYM='T' || FAP.SP_WYM='T' ?}
                   || _netto
                   || _brutto
                   ?}
                |? FAKS.PROC().PROCED='' & FAP.WAL_P = INFO.NAROD
                || {? FAKS.SP_WYM='T' & FAKS.WAL<>FAKS.NWAL &
                      {? _prosty || FAKSV.SP_WYM='T' || FAP.SP_WYM='T' ?}
                   || _netto
                   || _brutto
                   ?}
                || {? FAKS.SP_WYM='T' & FAKS.WAL<>FAKS.NWAL &
                      {? _prosty || FAKSV.SP_WYM='T' || FAP.SP_WYM='T' ?}
                   || _walut
                   || _wpb
                   ?}
                ?}
|? _a ='WV'  || {? FAKS.PROC().PROCED<>'' & FAP.WAL_H = INFO.NAROD
                || _vat
                |? FAKS.PROC().PROCED='' & FAP.WAL_P = INFO.NAROD
                || _vat
                || _wpv
                ?}
|? _a ='IL'  || _il
|? _a ='WW'  || _walut
|? _a ='WPB' || {? FAKS.SP_WYM='T' & FAKS.WAL<>FAKS.NWAL &
                   {? _prosty || FAKSV.SP_WYM='T' || FAP.SP_WYM='T' ?}
                || _walut
                || _wpb
                ?}
|? _a ='WPV' || _wpv
|? _a='WPN'  || _wpn
|? _a ='KWN' || _knetto
|? _a ='KWB' || {? FAKS.SP_WYM='T' & FAKS.WAL<>FAKS.NWAL &
                   {? _prosty || FAKSV.SP_WYM='T' || FAP.SP_WYM='T' ?}
                || _knetto
                || _kbrutto
                ?}
|? _a ='KWV' || _kvat
?}


\kurs
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: zwraca kurs z naglowka dokumentu sprzedazy
::  OLD: \kurs/ksg_form.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.KRS


\kurs_dekret
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [$12.10]
:: OPIS: zwraca kurs dla dekretu z naglowka dokumentu sprzedazy
::  OLD: \kurs_dekret/ksg_form.fml
::----------------------------------------------------------------------------------------------------------------------
FAKS.NKRS


\fap_dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: zwraca roznice pomiedzy powiazaniami FAP i DK
::   WE: _a - ref SQL-owy FAP-a
::   WY: roznica wartosci netto FAP i DK
::  OLD: \fap_dk/ksg_form.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1
|| {? type_of(_a)<>2 || _a:={? (5+cur_tab.name())<>'fakpo' || '' || $FAP.ref() ?} ?}
|| _a:={? (5+cur_tab.name())<>'fakpo' || '' || $FAP.ref() ?}
?};
{? (+_a)=16
|| _wyn:=FAP.WN;
   _rodz:=FAP.FAKS().WHERE;
   FAP2DK.index('FAP');
   FAP2DK.prefix(_rodz,_a);
   {? FAP2DK.first()
   || {!
      |? _cena:=0;
         DK.cntx_psh();
         DK.use(form(8+FAP2DK.DK));
         DK.clear; _cena:={? DK.seek(BB.sqlint(FAP2DK.DK),form(8+FAP2DK.DK)) || DK.C || 0 ?};
         DK.cntx_pop();
         _wyn-=(FAP2DK.IL*_cena)$2;
         FAP2DK.next()
      !}
   ?}
|| _wyn:=0
?};
_wyn


\stvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2006]
:: OPIS: zwraca kod stawki VAT z pozycji dokumentu lub z tabelki VAT dla dokumentu
::   WY: kod stawki VAT
::  OLD: \stvat/ksg_form.fml
::----------------------------------------------------------------------------------------------------------------------

_kod:=2+{? EKSG.TSP<>'Z'
        || {? FAKS.SZ='S' | (FAKS.SZ='Z' & DEK_NAG.VPOZ_STD='R')
           || FAP.SV().KOD
           || FAKSV.SV().KOD
           ?}
        || FAKSV.SV().KOD
        ?};

{? _kod=' -'
|| _kod:='ND'
|? _kod='Zw'
|| _kod:='ZW'
|| {? exec('vat','dok_fks',_kod)=6.5 || '0R' || _kod:=form((#_kod),-2) ?}
?};
_kod


\fak_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [2006]
:: OPIS: wartosci tylko dla zaliczek
::   WE: _a - zgodnie z parametrami \fak_war
::  OLD: \fak_zal/ksg_form.fml
::----------------------------------------------------------------------------------------------------------------------
_buf:=EKSG.TSP;
EKSG.TSP:='Z';
_wyn:=exec('fak_war','%fks_dekr_dziedz',_a);
EKSG.TSP:=_buf;
_wyn


\fak_rzal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [12.51]
:: OPIS: wartosci tylko dla rozliczenia zaliczek
::   WE: _a - zgodnie z parametrami \fak_war
::----------------------------------------------------------------------------------------------------------------------
_buf:=EKSG.TSP;
EKSG.TSP:='R';
_wyn:=exec('fak_war','%fks_dekr_dziedz',_a);
EKSG.TSP:=_buf;
_wyn


\waluta
::----------------------------------------------------------------------------------------------------------------------
::  UTW: Mario [2006]
:: OPIS: zwraca kod waluty z naglowka dokumentu sprzedazy
::   WE:  _a  waluta handlowa (WH) lub waluta podatkowa (WP)
::  OLD: \waluta/ksg_form.fml
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? _a='WH' & FAKS.WAL<>INFO.NAROD || _wyn:=FAP.WAL().KOD
|? _a='WP' & FAKS.WAL<>INFO.NAROD || _wyn:=FAP.WAL_P().KOD
|| _wyn:=FAKS.WAL().KOD
?};
_wyn


\kontotxt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [2008]
:: OPIS: przed dolacz dla pozycji dekretu
::   WE: [_a] - liczba znakow do obciecia konta = domyslnie 0, wartosc ujemna obcina "z tylu"
::  OLD: \kontotxt/ksg.fml
::----------------------------------------------------------------------------------------------------------------------
{? _>=1 || {? type_of(_a)<>1 || _a:=0 ?} || _a:=0 ?};
_wyn:={? EKSG.STR='W' || DEK_POZ.DEKRET_W().KONTOTXT || DEK_POZ.DEKRET_M().KONTOTXT ?};
{? _a>0 || _a+_wyn
|? _a<0 || _wyn+_a
|? _a=0 || _wyn
?}


\spr_przycz_kor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: BZ [12.41]
:: OPIS: Sprawdzenie parmetru w przyczynie korekty.
::   WY: wartosc 1 parametru ustawiona w wypelnionej przyczynie korekty
::  OLD: \spr_przycz_kor/ksg.fml
::----------------------------------------------------------------------------------------------------------------------
_wy:='';
{? EKSG.TSP='A'
|| _wy:=exec('get_par','slo_slu',FAP.POWKOR,1);
   fap_faks:=FAP.FAKS
|? EKSG.TSP='B'
|| FAP.cntx_psh();
   _msk:=ref_name(fap_faks)+3;
   FAP.use((FAP.name()-3)+_msk);
   FAP.index('FAP'); FAP.prefix(fap_faks,FAP.POZ);
   {? FAP.first() || _wy:=exec('get_par','slo_slu',FAP.POWKOR,1) ?};
   FAP.cntx_pop()
?};
_wy


\kh_kod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [12.41]
:: OPIS: Kod kontrahenta lub kontrahenta z odbiorcy
::  OLD: \kh_kod/ksg.fml
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.KH_ODB().ROZ_NAL='T'
|| FAKS.KH_ODB().KH_LNK().KOD
|| FAKS.KH().KOD
?}


\sympz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GC [17.14]
:: OPIS: Symbol Pz na fakturze zakupu
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
_skad:={? FAP.FAKS().WHERE<>'' || FAP.FAKS().WHERE || 'Z' ?};
FAP.cntx_psh(); FAP2DK.cntx_psh(); ND.cntx_psh(); ND.prefix();
FAP2DK.index('FAP'); FAP2DK.prefix(_skad,$FAP.ref()) ;
{? FAP2DK.first()
|| {! |?
      {? ND.seek(BB.sqlint(FAP2DK.ND),(8+FAP2DK.ND)) || _wyn:=ND.SYM ?};
       FAP2DK.next()
   !}
?};
FAP.cntx_pop(); FAP2DK.cntx_pop(); ND.cntx_pop();
_wyn


\zldk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GC [17.14]
:: OPIS: Symbol zlecenia na dokumencie magazynowym
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
DK.cntx_psh(); DK.prefix();
{? DK.seek(_a)
|| {? DK.PRDK<>''
   || _prdk:=DK.PRDK;
      DK.cntx_psh(); DK.use(8+_prdk); DK.prefix();
      {? DK.seek(BB.sqlint(_prdk),) || _wyn:=DK.ZL().SYM ?};
      DK.cntx_pop()
   ?}
?};
DK.cntx_pop();
{? _wyn<>''
|| _symzl:=FIRMA.SYMBOL+(_wyn+5);
   _wyn:=_symzl;
   SLO.cntx_psh(); SLO.index('SL');
   SLU.cntx_psh(); SLU.index('WZORZEC'); SLU.prefix('Zlecenia');
   {? SLU.first()
   || {! |?
         SLO.prefix(SLU.ref(),_symzl);
         {? ~SLO.first()
         || SLO.blank();
            SLO.SLU:=SLU.ref();
            SLO.KOD:=_symzl;
            SLO.TR:=ZL.SYM+''+ZL.OPIS;
            SLO.add()
         ?};
         SLU.next()
      !}
   ?};
   SLO.cntx_pop(); SLU.cntx_pop()
?};
_wyn


\odch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AK [17.14]
:: OPIS: Różnica między wartością Pz a netto faktury zakupu
::----------------------------------------------------------------------------------------------------------------------
DK.cntx_psh(); DK.prefix();
FAP.index('FAP'); FAP.prefix(FAKS.ref());
FAP2DK.index('FAPDK');
_war:=0;_fak:=0;
{? FAP.first()
|| {! |?
      FAP2DK.prefix($FAP.ref());
      _fak+=FAP.WN;
      {? FAP2DK.first()
      || {! |?
            {? DK.seek(BB.sqlint(FAP2DK.DK),form(8+FAP2DK.DK)) || _war+=DK.WAR ?};
            FAP2DK.next()
         !}
      ?};
      FAP.next()
   !}
 ?};
DK.cntx_pop();
_fak-_war


\zlfap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GC [17.14]
:: OPIS: Pobiera zlecenie na podstawie pozycji faktury sprzedaży
::   WE: _a - ref FAP
::----------------------------------------------------------------------------------------------------------------------
_wyn:='';
{? FAKS.KOR=''
|| DK.cntx_psh(); DK.index('FAP'); DK.prefix($_a);
   {? DK.first() || _wyn:=exec('zldk','%fks_dekr_dziedz',DK.ref()) ?};
   DK.cntx_pop()
|? FAP.FAKS().KZ<>''
|| _ktm:=FAP.M().KTM; _poz:=FAP.POZ; _ref:=$FAP.FAKS;
   FAKS_KZF.cntx_psh(); FAKS_KZF.index('KOR'); FAKS_KZF.prefix(_ref);
   {? FAKS_KZF.size()=1 & FAKS_KZF.first()
   || _wy:=FAKS_KZF.ref();
      _fak:=FAKS_KZF.FAK;
      _maska:=8+form(_fak);
      {? _maska<>FAKS.name() & _maska<>''
      || FAKS.use(_maska);
         FAP.use('fakpo'+(_maska+3));
         ND.use('nagdo'+(_maska+3))
      ?};
      FAKS.prefix();
      {? FAKS.seek(BB.sqlint(_fak),)
      || FAP.index('FAP'); FAP.prefix(FAKS.ref(),_poz);
         {? FAP.size()=1 & FAP.first() & _ktm=FAP.M().KTM
         || DK.index('FAP'); DK.prefix($FAP.ref());
            {? DK.first() || _wyn:=exec('zldk','%fks_dekr_dziedz',DK.ref()) ?}
         ?}
      ?}
  ?};
  FAKS_KZF.cntx_pop()
|? FAKS.KOR<>''
|| FAKS.cntx_psh(); FAP.cntx_psh(); DK.cntx_psh();
   _pozp:=FAP.POZP;
   {? FAP.POZP<>0  || _poz:=FAP.POZP || _poz:=FAP.POZ ?};
   {? FAP.FAKS().LKSQL<>''
   || _rk:=BB.sqlint(FAP.FAKS().LKSQL);
      _maska:=8+form(FAP.FAKS().LKSQL);
      {? _maska<>FAKS.name() & _maska<>''
      || FAKS.use(_maska);
         FAP.use('fakpo'+(_maska+3));
         ND.use('nagdo'+(_maska+3))
      ?};
      FAP.index('FAP'); FAP.prefix(_rk);
      {? FAP.find_key(_poz)
      || DK.index('FAP'); DK.prefix($FAP.ref());
         {? DK.first() || _wyn:=exec('zldk','%fks_dekr_dziedz',DK.ref()) ?}
      ?}
   ?};
   DK.cntx_pop(); FAP.cntx_pop(); FAKS.cntx_pop()
?};
_wyn


\sp_krajw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [12.51]
:: OPIS: Sprawdzenie, czy faktura walutowa w obrocie krajowym
::   WE: _a - 'S' - sprzedaż
::            'Z' - zakup
::            'L' - faktura zaliczkowa
::----------------------------------------------------------------------------------------------------------------------
{? _a='Z'
|| FAKS.WAL<>FAKS.NWAL &
   {? DEK_NAG.VPOZ_STD='R'
   || FAP.SP_WYM='T'
   || FAKSV.SP_WYM='T'
   ?}
|? _a='S'
|| FAP.SP_WYM='T' & FAKS.WAL<>FAKS.NWAL
|? _a='L'
|| FAKS.WAL<>FAKS.NWAL & FAKSV.SP_WYM='T'
|| 0
?}


\mag_COS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [21.14]
:: OPIS: Sprawdzenie, czy dany dokument magzynowy zotsał wystawiony w magazynie z obsługa call-off stock
::   WY: 1-tak 0-nie
::----------------------------------------------------------------------------------------------------------------------
ND.MAG().COS='T'


\nalezn_kn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Dekretacja korekt nagłówkowych z logistyki - zwraca kontrahenta z dokumentu korygowanego
::----------------------------------------------------------------------------------------------------------------------
_kh:=FAKS.KH().KOD;
{? FAKS.LKSQL<>''
|| _kn:=FAKS.LKSQL;
   _m1:=FAKS.name(); _m2:=form(8+FAKS.LKSQL);
    FAKS.cntx_psh();
   {? _m1<>_m2 || FAKS.use(_m2) ?};
   FAKS.prefix();
   {? FAKS.seek(_kn)
   || _kh:=FAKS.KH().KOD
   ?};
   FAKS.cntx_pop()
?};
_kh


\faktura_kn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Dekretacja korekt nagłówkowych z logistyki - zwraca symbol dokumentu korygowanego
::----------------------------------------------------------------------------------------------------------------------
{? FAKS.WHERE='N' || FAKS.KOR || FAKS.SYM ?}


\sp_rozlicz_zal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Sprawdzenie, czy faktura jest rozliczeniem zaliczki
::----------------------------------------------------------------------------------------------------------------------
{? FAKSV.first() || FAKS.WHERE<>'L' & FAKSV.WB_Z<>0 || 0 ?}


:Sign Version 2.0 jowisz:1045 2023/08/02 14:57:02 9428f3e416b44c32449f14f4fadad54e5d502826b959d2b2dea2855746bbd71f11fbb1d50316899f18033d267af295a3afddcf5a28fa37e7dbf919e6357621a93375c89b7eedf832bb1b1ae2f2833a7e053cfd21e082d2e515bb8299a3d300743029804e712c177a8287caf67ac5fe388cdc01f412aae67bcc2da6d5d0fde32e

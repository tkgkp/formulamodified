:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2325.fml
:: Utworzony: 01.07.2022
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 22.26 na 23.25
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::
::            Programowe wyłączenie zadania transferowego.
::             W plikach upgrade_rrxx.fml korzystamy z metody
::               __UPG.off_task(<task>,[<operator>],[<value>])
::                 - dodaje do listy zadanie,
::                   które ma zostać wyłączone wg warunku porównania wg operatora porównania i wartości, gdzie:
::                <task> - symbol zadania do wyłączenia;
::                [<operator>] - operator porównania - domyślnie operator '<'; dostępne opcje:
::                                   '<' - wartość mniejsza;
::                                   '<=' - wartość mniejsza lub równa;
::                                   '=' - wartość równa;
::                [<value>] - wersja systemu lub aktualizacji do porównania z wersją zadania
::                            - domyślnie aktualna wersja aktualizacji (wg pliku).
::            W pliku upgrade.fml NALEŻY umieścić wywołanie poniższej metody na końcu formuły \init
::               (po wywołaniach __UPG.add_head()):
::              __UPG.act_off() - wyłączenie wszystkich zadań wskazanych do wyłączenia.
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Główna formuła upgrade-ów RR.xx
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
__UPG.id_ver(date(2023,6,23),time(0,0,0));

:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('init_b_perm',,,'ZWS');
__UPG.add_task('upgrade_act',,,'ZPR');
__UPG.add_task('SD_XSLO',,,'ZWS');
__UPG.add_task('funpar',,,'ZWS',2);
__UPG.add_task('color',,,'ZWS',2,,,,'T');
__UPG.add_task('TAT',,,'ZWS');
__UPG.add_task('KST_upd',,,'PPL');
__UPG.add_task('STAZ_praca_r',,,'PKD');
__UPG.add_task('stalesys_ini',,,'ZWS');
__UPG.add_task('stalesys_wid',,,'ZWS');
__UPG.add_task('areatitle',,,'ZWS');
__UPG.add_task('URZ_TAG',,,'ZWS');
__UPG.add_task('report',,,'ZWS',35,,,,'T');
__UPG.add_task('rozrach_waluta_obca',,,'FKS');
__UPG.add_task('sch_imp',,,'OBE');
__UPG.add_task('etypy_upd',,,'ZWS');
__UPG.add_task('edokum_upd',,,'ZWS');
__UPG.add_task('B_ACTION',,,'ZPR');
__UPG.add_task('BI_PORT',,,'ZPR');
__UPG.add_task('nosezon',,,'FST');
__UPG.add_task('f_kontrolna_poz_pln',,,'FKS');
__UPG.add_task('AN_OPIS',,,'FKS',2);
__UPG.add_task('rubryki_atrybuty',,,'PPL');
__UPG.add_task('portal_bnftt_nag_id',,,'POR');
__UPG.add_task('DOKUM',,,'ZWS');
__UPG.add_task('deleg_seod',,,'SEO');
__UPG.add_task('spr_amor',,,'FST');
__UPG.add_task('USERSF',,,'ZWS');
__UPG.add_task('id_sync',,,'ZWS');
__UPG.add_task('OP_AN_OPIS',,,'FKS');
__UPG.add_task('f_kontrolna_roz_nad',,,'FKS');
__UPG.add_task('rest_acc',,,'HBN');
__UPG.add_task('POCZTA',,,'ZWS');
__UPG.add_task('DOK_upd',,,'FKS');
__UPG.add_task('porsloit_clear',,,'POR');
__UPG.add_task('porslo_global',,,'POR');
__UPG.add_task('B_PREL',,,'ZPR');
__UPG.add_task('B_TODO_U',,,'ZPR');
__UPG.add_task('BI_TODO',,,'ZPR');
__UPG.add_task('jpk_dl_sym',,,'FKS');
__UPG.add_task('jpk_fa_dsp',,,'FKS');
__UPG.add_task('kh_tagger',,,'ZWS');
__UPG.add_task('B_WORKER',,,'ZPR');
__UPG.add_task('long_sym_rok',,,'FKS');
__UPG.add_task('KH',,,'ZWS');
__UPG.add_task('KH_ODB',,,'ZWS');
__UPG.add_task('MJM',,,'ZWS');
__UPG.add_task('mwa_wnioski',,,'POR');
__UPG.add_task('grvat',,,'FKS');
__UPG.add_task('par_skid',,,'ZWS');
__UPG.add_task('mwa_faktury',,,'SEO');
__UPG.add_task('xinfo_base_id_repair',,,'ZWS');
__UPG.add_task('sync_mwa_blmt',,,'ZBL');
__UPG.add_task('akt_proc',,,'LSP',,,,,'T');
__UPG.add_task('rub_atr_ER23080037',,,'PPL',,,,,'T');
__UPG.add_task('EDOK_ATR_b_war',,,'OBE',2,,,,'T');
__UPG.add_task('atrybut_zusz3',,,'PPL',,,,1,'T');
__UPG.add_task('upgrade_act_obe',,,'ZPR',,,,,'T');
__UPG.add_task('rubryki_ER23100009',,,'PPL',,,,,'T');
__UPG.add_task('ATR_DG1',,,'PKD',1,,,,'T');
__UPG.add_task('por_work_place',,,'POR',1,,,,'T');
__UPG.add_task('p_kal_nazw',,,'PKD',,,,,'T');
__UPG.add_task('kal_buff_w',,,'PRC',,,,,'T');
__UPG.add_task('krs_napraw_jedn',,,'ZWS');
__UPG.add_task('upgrade_act_obg',,,'ZPR',,,,,'T');
__UPG.add_task('edokum_resend',,,'ZWS',,,,,'T');
__UPG.add_task('upgrade_act_per',,,'ZPR',,,,,'T');
__UPG.add_task('init_sys_log',,,'ZWS',,,,,'T');
__UPG.add_task('projekty_clean_tree',,,'ZWS',,,,,'T');
__UPG.add_task('zbl_rpt_del',,,'ZBL',,,,,'T');
__UPG.add_task('stawka_7',,,'FKS',,,,,'T');
__UPG.add_task('ob_czy_dekr',,,'ZWS',,,,,'T');
__UPG.add_task('rubryki_ER24020016',,,'PPL',,,,,'T');
__UPG.add_task('zalacz_www',,,'POR',,,,,'T');

:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('FO_DEF',,'N','ZWS');
__UPG.add_task('FO_USR',,'N','ZWS',1);
__UPG.add_task('EAND',,'N','ZWS',,,,1);
__UPG.add_task('EK_SLOP',,'N','TTE',,,,1);
__UPG.add_task('EK_PROB',,'N','TTE',,,,1);
__UPG.add_task('BADH',,'N','TTE',,,,1);
__UPG.add_task('BADP',,'N','TTE',,,,1);
__UPG.add_task('BADSEP',,'N','TTE',,,,1);
__UPG.add_task('ISTDEF',,'N','ZWS',,,,1);
__UPG.add_task('PX_VER',,'N','TPP',,,,1);
__UPG.add_task('PX_GRP',,'N','TPP',,,,1);
__UPG.add_task('PX_OBJ',,'N','TPP',,,,1);
__UPG.add_task('PX_TEX4TKTL',,'N','TPP',,,,1);
__UPG.add_task('GROP',,'N','TPP',,,,1);
__UPG.add_task('GROPS',,'N','TPP',,,,1);
__UPG.add_task('EDI',,'N','ZWS',4,,,1);
__UPG.add_task('ZGP',,'N','TTE',,,,1);
__UPG.add_task('TP2TP_KOR_ZWR',,'N','ZWS',,,,1);
__UPG.add_task('PX_POZ',,'N','TPP',,,,1);
__UPG.add_task('PL_OPER',,'N','TPP',,,,1);
__UPG.add_task('zws_par_atra',,'N','ZPR',,,,0);
__UPG.add_task('tpp_gop_eakc',,'N','ZPR',,,,0);
__UPG.add_task('ZGH',,'N','TTE',,,,1);
__UPG.add_task('stalesys_PPK_VI',,'N','PPK');
__UPG.add_task('uzupIDpal',,'N','LMG',,,,1);
__UPG.add_task('BADMSEP',,'N','TTE',,,,1);
__UPG.add_task('PORTALU',,'N','ZWS',,,,1);
__UPG.add_task('zws_par_blzd',,'N','ZPR',,,,0);
__UPG.add_task('zws_par_blzk',,'N','ZPR',,,,0);
__UPG.add_task('FPACZ',,'N','LUM',,,,1);
__UPG.add_task('ilmg2ZAM',,'N','LZK',,,,1);
__UPG.add_task('update_ZLIM',,'N','TTE',,,,1);
__UPG.add_task('porslo_reinit',,'N','POR',,,,1);
__UPG.add_task('OPAK_N',,'N','LMG',,,,1);
__UPG.add_task('rozlvat_dop',,'N','FKS',,,,1);
__UPG.add_task('REZ_update',,'N','TTE',,,,1);
__UPG.add_task('REJ_MAT_update',,'N','TTE',,,,1);
__UPG.add_task('PROD_REJ',,'N','TTE',,,,1);
__UPG.add_task('TOPER',,'N','TTE',,,,1);
__UPG.add_task('OPAK_ZWR',,'N','LMG',,,,1);
__UPG.add_task('TYPYSP',,'N','ZWS',,,,1);
__UPG.add_task('actOperMob',,'N','LMO',,,,1);
__UPG.add_task('ZL',,'N','TTE',,,,1);
__UPG.add_task('FAKS',,'N','LSP',,,,1);
__UPG.add_task('users_upr_dane_napraw',,'N','ZWS',,,,1);
__UPG.add_task('fap2dk',,'N','LSP',5,,,1);
__UPG.add_task('inw_typydok',,'N','LMG',,,,1);
__UPG.add_task('inn_typydok',,'N','LMG',,,,1);
__UPG.add_task('nd_inn',,'N','LMG',,,,1);
__UPG.add_task('PAL',,'N','LMG',,,,1);
__UPG.add_task('dokum_to_dokumz',,'N','FKS',2,,,1,'T');
__UPG.add_task('ofe_to_ofp',,'N','LSP',,,,1);
__UPG.add_task('potwZAM',,'N','LZK',,,,1);
__UPG.add_task('TTOPER',,'N','TTE',,,,1);
__UPG.add_task('noty_ods',,'N','FKS',,,,1);
__UPG.add_task('tpp_gop_ezam',,'N','ZPR',,,,0);
__UPG.add_task('tte_pzl_pprz',,'N','ZPR',,,,0);
__UPG.add_task('usersrej_bperm_up',,'N','ZWS',,,,1);
__UPG.add_task('TRANS_PL',,'N','LSP',,,,0,'T');
__UPG.add_task('fap2dk_clear',,'N','LSP',2,,,1,'T');
__UPG.add_task('webdok_dokzrodl',,'N','FKS',,,,1,'T');
__UPG.add_task('etypy_portalk',,'N','ZWS',,,,1,'T');
__UPG.add_task('par_wyd',,'N','FKS',,,,,'T');

:: Zadania manualne wykonywane raz dla całego systemu
__UPG.add_task('szablony','N',,'ZWS',4,,,1);
__UPG.add_task('tat','N',,'ZWS',,,,1);
__UPG.add_task('mechanizm_importow','N',,'ZWS',13,,,1);
__UPG.add_task('base_id','N',,'ZWS',,,,1);
__UPG.add_task('porslo_global_info','N','T','POR',,,,1);
__UPG.add_task('sync_mwa_portal','N','T','POR',1,,,1,'T');
__UPG.add_task('sync_def_portal','N','T','POR',,,,1);
__UPG.add_task('cluster_cfg','N','T','ZWS',,,,,'T');
__UPG.add_task('genius_updt','N','T','ZWS',,,,,'T');
__UPG.add_task('DOK_sum','N','T','FKS',,,,,'T');

:: Zadania manualne wykonywane dla każdej firmy osobno
__UPG.add_task('folp_std','N','N','PPL',,,,1);
__UPG.add_task('dekstal_rejodd','N','N','FKS',2,,,1,'T');
__UPG.add_task('procesy_ER_2310_0028','N','N','PKD',,,,1,'T');
__UPG.add_task('procesy_personelowe','N','N','PKD',2,,,1);
__UPG.add_task('procesy_ER_2401_0062','N','N','POR',,,,1,'T');

:: Wyłączenie zadań z poprzednich wersji
__UPG.off_task('funpar','<','23.25');
__UPG.off_task('color','<','23.25');
__UPG.off_task('atr_pak_mob','<','23.25');
__UPG.off_task('report','<','23.25');
__UPG.off_task('mechanizm_importow_PKD','<','23.25');
__UPG.off_task('mechanizm_importow','<','23.25');
__UPG.off_task('areatitle','<','23.25');
__UPG.off_task('atrybut_zusz3','<','23.25');
__UPG.off_task('por_work_place','<','23.25');
__UPG.off_task('formuly_placowe_ER23080022','=','22.26');
__UPG.off_task('formuly_placowe_ER23080032','=','22.26');
__UPG.off_task('formuly_placowe_ER23080027','=','22.26');
__UPG.off_task('rubryki_ER23080027','=','22.26');
__UPG.off_task('formuly_placowe_kor_rh','=','22.26');
__UPG.off_task('del_funpar','<=','21.14');
__UPG.off_task('rubryki_ER23100002','=','22.26');
__UPG.off_task('procesy_ER_2310_0014','=','22.26');
__UPG.off_task('sync_mwa_portal','=','22.26');
__UPG.off_task('kal_buff_w','<','23.25');
__UPG.off_task('p_kal_nazw','<','23.25');
~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::   WE: [_a] - dziedziny
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności (ciekawe, czy uda się alfabetycznie ...)
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.BIQ','.');
_dom_str:={? var_pres('_a')=type_of('')
          || _a
          || 'CTR.FKS.FST.HBN.LMG.LMO.LSP.LTR.LUO.LZK.PKD.PPK.PRC.SEO.TPP.TRE.TTE.ZPR.ZUI.ZWS'
          ?};
_dom:=spli_str(_dom_str,'.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\FO_USR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Aktualizacja parametrów FO użytkowników
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_res:=exec('upgrade_usr','#params');

__UPG.msg('Zaktualizowano parametry użytkowników');
VAR_DEL.delete('__lrec','__lmod');
_res


\FO_USR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Aktualizacja parametrów FO użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametów użytkowników'


\FO_DEF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Aktualizacja parametrów FO aplikacyjnych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

exec('init','params');

__UPG.msg('Zaktualizowano parametry aplikacyjne');
VAR_DEL.delete('__lrec','__lmod');
_res


\FO_DEF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Aktualizacja parametrów FO aplikacyjnych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametów aplikacyjnych'


\SD_XSLO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Aktualizacja elementów zestawów danych
::----------------------------------------------------------------------------------------------------------------------
_insert:="
   _res:=exec('xslo_insert','szablon_std',_a,exec('_xslo_kol','szablon_std'),_b,_c,_d);
   __UPG.msg(_res.INFO);
   _res.STATUS<>~~
";

{? ~_insert('RODZAJ_KOL_1','WARTOSC','Wartość wyliczana typu prostego lub obiektowego',1) |
   ~_insert('RODZAJ_KOL_2','OBRAZ','Obraz pobrany z wiersza tabeli bazy danych',2)
|| return(-1)
?};

_kod:='RODZAJ_KOL_1';
{? (_slo:=exec('xslo_search','szablon_std',_kod))=null
|| __UPG.msg('Brak elementu "%1" w słowniku.'[_kod]);
   return(-1)
?};

_cnt:=0;
_err:=0;

SD_WKOL.trig_off('*','*');
SD_WKOL.cntx_psh();
SD_WKOL.clear();
SD_WKOL.f_set(,,'SD_XSLO is null');
_loop:=SD_WKOL.f_first();
{!
|? _loop
|! SD_WKOL.SD_XSLO:=_slo;
   {? SD_WKOL.put()
   || _cnt+=1
   || _err+=1
   ?};
   _loop:=SD_WKOL.f_next()
!};
SD_WKOL.f_clear();
SD_WKOL.cntx_pop();
SD_WKOL.trig_on('*','*');

{? _cnt=0
|| __UPG.msg('Zawartość tabeli SD_WKOL nie wymagała aktualizacji.')
|| __UPG.msg('Zaktualizowano zawartość tabeli SD_WKOL:');
   __UPG.msg('przetworzono %1 zapisów,'[$_cnt]);
   __UPG.msg('wystąpiło %1 błędów.'[$_err])
?};

{? _err<>0 || -1 || 1 ?}


\SD_XSLO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Aktualizacja elementów zestawów danych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja elementów zestawów danych'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
{? exec('aktualizuj','funpar',1)
|| _usun:=obj_new(21);
   _usun[1]:='ZWS_PAR_WSAM';
   _usun[2]:='ZWS_PAR_WNOR';
   _usun[3]:='ZWS_PAR_WWYM';
   _usun[4]:='ZWS_PAR_LCMG';
   _usun[5]:='ZWS_PAR_LCSP';
   _usun[6]:='ZWS_PAR_DELEG';
   _usun[7]:='ZWS_PAR_STZAGR';
   _usun[8]:='ZWS_PAR_ETYPWYD';
   _usun[9]:='ZWS_PAR_SKLPLC';
   _usun[10]:='ZWS_PAR_ISTR';
   _usun[11]:='ZWS_PAR_RPRZ';
   _usun[12]:='ZWS_PAR_SAMSTA';
   _usun[13]:='LSP_PAR_LRAK';
   _usun[14]:='LZK_PAR_LRAD';
   _usun[15]:='TRE_PAR_LRAK';
   _usun[16]:='TRE_PAR_LRAD';
   _usun[17]:='ZWS_PAR_PPNW';
   _usun[18]:='ZWS_PAR_PPZT';
   _usun[19]:='ZWS_PAR_PPZR';
   _usun[20]:='ZWS_PAR_PPZS';
   _usun[21]:='ZWS_PAR_PORCAS';
   FP_DEF.cntx_psh();
   FP_DEF.index('ID');
   _del:=1;
   {! _licz:=1 .. obj_len(_usun) |!
      FP_DEF.prefix('T',_usun[_licz],);
      _del*={? FP_DEF.first() || FP_DEF.del(,1) || 1 ?}
   !};
   FP_DEF.cntx_pop();
   {? _del>0
   || __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
   || __UPG.msg('Usunięcie czynności parametryzacyjnych nie powiodło się.');
      _ret:=-1
   ?}
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.');
   _ret:=-1
?};
_ret


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\EAND
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pola dla definicji kodów GS1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

:: uzupełnienie tabeli formuł przetwarzania danych
exec('initeanf','magazyn_mobi');
:: uzupełnienie dodatkowych pól na tabeli EAND
_digit:=',00,01,02,253,255,402,410,411,412,413,414,415,8003,8017,8018,';
_wsp:=tab_tmp(1,'JMO','STRING[10]','','WSP','REAL','','JM','STRING[10]','');
_wsp.blank(); _wsp.JMO:='lb'; _wsp.WSP:=2.20462262; _wsp.JM:='kg'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='in'; _wsp.WSP:=39.37007874; _wsp.JM:='mb'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='ft'; _wsp.WSP:=3.2808399; _wsp.JM:='mb'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='yd'; _wsp.WSP:=1.0936133; _wsp.JM:='mb'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='in2'; _wsp.WSP:=1550.00310001; _wsp.JM:='m2'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='ft2'; _wsp.WSP:=10.76391042; _wsp.JM:='m2'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='yd2'; _wsp.WSP:=1.19599005; _wsp.JM:='m2'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='in3'; _wsp.WSP:=61023.74409473; _wsp.JM:='m3'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='ft3'; _wsp.WSP:=35.31466672; _wsp.JM:='m3'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='yd3'; _wsp.WSP:=1.30795062; _wsp.JM:='m3'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='ozt'; _wsp.WSP:=32.15074657; _wsp.JM:='kg'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='oz'; _wsp.WSP:=35.27396195; _wsp.JM:='kg'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='gal'; _wsp.WSP:=0.26417205; _wsp.JM:='l'; _wsp.add(1);
_wsp.blank(); _wsp.JMO:='q'; _wsp.WSP:=1; _wsp.JM:='szt'; _wsp.add(1);

EAND.cntx_psh();
EAND.index('KOD');
EAND.prefix();
{? EAND.first()
|| {!
   |? __lrec+=1;
      _fdigit:={? _digit*(',%1,'[EAND.KOD]) || 'T' || 'N' ?};
      _feanf:={? EAND.FORMAT='N4+N6'                || 'CNNR'
              |? EAND.FORMAT='N2+N6'                || 'CNND'
              |? EAND.FORMAT*'X' & EAND.FORMAT*'..' || {? _fdigit='T' || 'VATS' || 'VANS' ?}
              |? EAND.FORMAT*'X'                    || {? _fdigit='T' || 'CATS' || 'CANS' ?}
              |? EAND.FORMAT*'..'                   || {? _fdigit='T' || 'VNTS' || 'VNNS' ?}
              |? EAND.FORMAT*'N'                    || {? _fdigit='T' || 'CNTS' || 'CNNS' ?}
              || ''
              ?};
      _flen:={? #(EAND.FORMAT+2)>=1 || #(EAND.FORMAT+2)
             |? #(EAND.FORMAT+1)>0 || #(EAND.FORMAT+1)
             || 0
             ?};
      _fonly:={? EAND.FORMAT*'X' || 'N' || 'T' ?};
      _fjmo:='';
      _fwsp:=0;
      _fjm:={? (3+EAND.KOD)>='310' & (3+EAND.KOD)<='369'
            || _beg:=EAND.RODZ*'('; _end:=EAND.RODZ*')';
               _kod:={? _beg>0 & _end>_beg
                     || (_end-_beg-1)+(_beg-EAND.RODZ)
                     || ''
                     ?};
               {? _kod=''
               || null()
               || {? _kod='m'  || _kod:='mb'
                  |? _kod='t'  || _kod:='ozt'
                  |? _kod='i'  || _kod:='in'
                  |? _kod='f'  || _kod:='ft'
                  |? _kod='y'  || _kod:='yd'
                  |? _kod='i2' || _kod:='in2'
                  |? _kod='f2' || _kod:='ft2'
                  |? _kod='y2' || _kod:='yd2'
                  |? _kod='i3' || _kod:='in3'
                  |? _kod='f3' || _kod:='ft3'
                  |? _kod='y3' || _kod:='yd3'
                  |? _kod='g'  || _kod:='gal'
                  ?};
                  _fjmo:=_kod;
                  _ref:=exec('FindInSet','#table','JM','KOD',_kod,,,1,,null());
                  {? _ref=null()
                  || _wsp.clear();
                     _wsp.prefix(_kod,);
                     {? _wsp.first()
                     || _ref:=exec('FindInSet','#table','JM','KOD',_wsp.JM,,,1,,null());
                        {? _ref<>null() || _fwsp:=_wsp.WSP ?}
                     ?};
                     _ref
                  || _fwsp:=1;
                     _ref
                  ?}
               ?}
            || null()
            ?};
      {? (_fdigit<>EAND.DIGIT | _feanf<>EAND.EANF | _flen<>EAND.LEN | _fonly<>EAND.ONLY
        | _fjm<>EAND.JM | _fjmo<>EAND.JMO | _fwsp<>EAND.WSP)
      || EAND.DIGIT:=_fdigit;
         EAND.EANF:=_feanf;
         EAND.LEN:=_flen;
         EAND.ONLY:=_fonly;
         EAND.JM:=_fjm;
         EAND.JMO:=_fjmo;
         EAND.WSP:=_fwsp;
         {? EAND.put(1) || __lmod+=1 ?}
      ?};
      EAND.next()
   !}
?};
EAND.cntx_pop();
obj_del(_wsp);
__UPG.msg('Zaktualizowano definicję kodów GS1 o dane potrzebne do generacji kodów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\EAND_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pola dla definicji kodów GS1 - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie obsługi tworzenia kodów GS1'


\EK_SLOP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnienie słownika problemów
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_slu_ref:=null();
_slu_naz:='PROBLEMY';

SLU.cntx_psh();
SLU.index('NAZ');
SLU.prefix(_slu_naz,);
{? SLU.first()
|| _slu_ref:=SLU.ref()
?};
SLU.cntx_pop();

{? _slu_ref=null()
||
   __UPG.msg('W systemie brakuje słownika o nazwie %1.\nZawartość słownika nie została przeniesiona.'[_slu_naz])
||
   SLU.cntx_psh(); SLO.cntx_psh(); EK_SLOP.cntx_psh();

   SLO.index('SL');
   SLO.prefix(_slu_ref);
   {? SLO.first()
   || {!
      |? __lrec+=1;
         EK_SLOP.index('KOD');
         EK_SLOP.prefix(SLO.KOD,);
         {? ~EK_SLOP.first()
         || EK_SLOP.KOD:=SLO.KOD;
            EK_SLOP.NAZ:=SLO.TR;
            EK_SLOP.KLASA:=exec('prob_class_suspensing','zl_wkj');
            {? EK_SLOP.add() || __lmod+=1 ?}
         ?};
         SLO.next()
      !}
   ?};

   __UPG.msg('Przetworzono: %1 zapisów słownika SLO, dodano: %2 zapisów słownika EK_SLOP.'[$__lrec,$__lmod]);
   VAR_DEL.delete('__lrec','__lmod');

   SLU.cntx_pop(); SLO.cntx_pop(); EK_SLOP.cntx_pop()
?};

_result


\EK_SLOP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnienie słownika problemów - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie słownika problemów.'


\EK_PROB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli EK_PROB - Rejestracja wykonań - zgłoszone problemy
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
EK_PROB.cntx_psh();
ZGP.cntx_psh();
EK_PROB.trig_off('*','*');
EK_PROB.prefix();
ZGP.prefix();
_result:=EK_PROB.for_each("
   _put:=0;
   __lrec+=1;
   {? EK_PROB.PLACE=null() & EK_PROB.ZGP<>null()
   || _place:=EK_PROB.ZGP().PLACE;
      {? _place<>null()
      || EK_PROB.PLACE:=_place;
         _put:=1
      ?}
   ?};
   {? EK_PROB.EK_SLOP=null()
   || EK_SLOP.index('KOD');
      EK_SLOP.prefix(EK_PROB.SLO().KOD,);
      {? EK_SLOP.first()
      || EK_PROB.EK_SLOP:=EK_SLOP.ref();
         _put:=1
      ?}
   ?};
   {? EK_PROB.SQLSRC=''
   || {? EK_PROB.ZGP<>null()
      || EK_PROB.SQLSRC:=$EK_PROB.ZGP;
         _put:=1
      |? EK_PROB.GROP<>null()
      || EK_PROB.SQLSRC:=$EK_PROB.GROP;
         _put:=1
      |? EK_PROB.PL_OPER<>null()
      || EK_PROB.SQLSRC:=$EK_PROB.PL_OPER;
         _put:=1
      |? EK_PROB.PX_POZ<>null()
      || EK_PROB.SQLSRC:=$EK_PROB.PX_POZ;
         _put:=1
      ?}
   ?};
   {? _put || {? EK_PROB.put(1) || __lmod+=1 ?} ?}
",0);
EK_PROB.trig_on('*','*');
EK_PROB.cntx_pop();
ZGP.cntx_pop();
__UPG.msg('Zaktualizowano zgłoszone problemy do rejestracji wykonań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\EK_PROB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli EK_PROB - Rejestracja wykonań - zgłoszone problemy - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla zgłoszonych problemów do rejestracji wykonań'


\TAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Uzupełnia pole TAT.MIEJSCE
::----------------------------------------------------------------------------------------------------------------------
_sql:=sql(
:: Wnioski
   'select TAT.REFERENCE as REF, \'W\' as MIEJSCE from ETYP_ATR join TAT order by 1 '
   'union all '

:: Definicje atrybutów dla tabel - KH, SRSR
   'select TAT.REFERENCE as REF, \'T\' as MIEJSCE from CDDEF join TAT order by 1 '
   'union all '

:: Używane atrybuty
   'select TAT.REFERENCE as REF, \'U\' as MIEJSCE from UAT join TAT order by 1 '
);
_res:=obj_new('TO_SET','SET');
_res.TO_SET:=_res.SET:=0;
params_set('tab',_sql,'res',_res);
TAT.trig_off('*','*');
TAT.prefix();
TAT.for_each("
   {? TAT.MIEJSCE=''
   || _res:=params_get().res;
      _res.TO_SET+=1;
      {? TAT.W_PORTAL='N'
      || _tab:=params_get().tab;
         TAT.MIEJSCE:={? _tab.find_key($TAT.ref()) || _tab.MIEJSCE |? TAT.count()>0 || 'L' || '' ?}
      || TAT.MIEJSCE:='W'
      ?};
      {? TAT.MIEJSCE<>''
      || TAT.put();
         _res.SET+=1
      ?}
   ?}
",1);
TAT.trig_on('*','*');
__UPG.msg('Liczba rekordów do uzupełnienia: %1'[$_res.TO_SET]);
__UPG.msg('Liczba rekordów uzupełnionych: %1'[$_res.SET]);
__UPG.msg('Liczba rekordów nieuzupełnionych: %1'[$(_res.TO_SET-_res.SET)]);
1


\TAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Uzupełnia pole TAT.MIEJSCE - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola z miejscem użycia typu atrybutu: TAT.MIEJSCE'


\BADH
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADH - zestaw wykonanych badań
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=BADH.names();
_msk.clear();
BADH.trig_off('*','*');
BADH.cntx_psh();
ZLGD.cntx_psh();
ZLGD.prefix();
{? _msk.first()
|| {!
   |? BADH.use(_msk.NAME);
      BADH.prefix();
      BADH.for_each("
         _put:=0;
         __lrec+=1;
::       Uzupełnienie pól MAD_DT/MAD_USER dla badań wykonywanych w trakcie wykonywania operacji
         {? BADH.ZLGD<>null() & BADH.TYP='C'
         || _mask:=ref_name(BADH.ZLGD);
            {? ZLGD.name()<>_mask || ZLGD.use(_mask); ZLGD.prefix() ?};
            {? ZLGD.seek(BADH.ZLGD)
            || BADH.MAD_DT:=ZLGD.DT;
               BADH.MAD_USER:=ZLGD.USER;
               _put:=1
            ?}
         ?};
         {? _put || {? BADH.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
BADH.cntx_pop();
ZLGD.cntx_pop();
BADH.trig_on('*','*');
__UPG.msg('Zaktualizowano zestawy wykonanych badań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BADH_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADH - zestaw wykonanych badań - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla zestawów wykonanych badań'


\BADP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADP - Badania - zestaw wykonanych badań - pozycje
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=BADP.names();
_msk.clear();
BADP.trig_off('*','*');
BADP.cntx_psh();
{? _msk.first()
|| {!
   |? BADP.use(_msk.NAME);
      BADP.prefix();
      BADP.for_each("
         _put:=0;
         __lrec+=1;
         {? BADP.CZY_NAG=''
         || BADP.CZY_NAG:='N';
            _put:=1
         ?};
         {? _put || {? BADP.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
BADP.cntx_pop();
BADP.trig_on('*','*');
__UPG.msg('Zaktualizowano pozycje zestawów wykonanych badań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BADP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADP - Badania - zestaw wykonanych badań - pozycje - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla pozycji zestawów wykonanych badań'


\BADSEP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADSEP - Badania - zestawy badań - pozycje zestawu
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
BADSEP.trig_off('*','*');
BADSEP.cntx_psh();
BADSEP.prefix();
BADSEP.for_each("
   _put:=0;
   __lrec+=1;
   {? BADSEP.CZY_DEF=''
   || BADSEP.CZY_DEF:='N';
      _put:=1
   ?};
   {? BADSEP.CYKL_CO=''
   || BADSEP.CYKL_CO:='N';
      _put:=1
   ?};
   {? _put || {? BADSEP.put(1) || __lmod+=1 ?} ?}
",0);
BADSEP.cntx_pop();
BADSEP.trig_on('*','*');
__UPG.msg('Zaktualizowano pozycje zestawów badań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BADSEP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADP - Badania - zestawy badań - pozycje zestawu - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla pozycji zestawów badań'


\BADMSEP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADMSEP - Badania - zestawy badań grupy/materiału - pozycje
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
BADMSEP.trig_off('*','*');
BADMSEP.cntx_psh();
BADMSEP.prefix();
BADMSEP.for_each("
   _put:=0;
   __lrec+=1;
   {? BADMSEP.CZY_DEF=''
   || BADMSEP.CZY_DEF:='N';
      _put:=1
   ?};
   {? _put || {? BADMSEP.put(1) || __lmod+=1 ?} ?}
",0);
BADMSEP.cntx_pop();
BADMSEP.trig_on('*','*');
__UPG.msg('Zaktualizowano pozycje badań materiałów i grup materiałowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BADMSEP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli BADMSEP - Badania - zestawy badań grupy/materiału - pozycje - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla pozycji badań materiałów i grup materiałowych'


\KST_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [23.25]
:: OPIS: Dodanie nowych i aktualizacja istniejących pól dla stałej KST
::----------------------------------------------------------------------------------------------------------------------
:: Aktualizacja pola związanego z procentowym wynagrodzeniem za terminową wpłatę podatków.
{? exec('kst_war_add','#stalesys','KST.PW_TWP','0.60',date(2022,7,1))
|| __UPG.msg('Zaktualizowano informacje o procencie wynagrodzenia za terminową wpłatę podatków.')
|| __UPG.msg('Informacje o procencie wynagrodzenia za terminową wpłatę podatków są już aktualne.')
?};
1


\KST_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [23.25]
:: OPIS: Opis funkcji aktualizacji stałych systemu
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu.'


\STAZ_praca_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Uzupełnienie wartości w nowych polach tabel STAZ i STAZ_H
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_poprawione:=_dobre:=_bledy:=0;
_ret:=1;
STAZ.trig_off('*','*');
STAZ.cntx_psh();
STAZ.prefix();
{? STAZ.first()
|| {!
   |? {? STAZ.PRACA_R=''
      || STAZ.WU_R:=STAZ.WU;
         STAZ.WUG_R:=STAZ.WUG;
         STAZ.WUZ_R:=STAZ.WUZ;
         STAZ.WUZG_R:=STAZ.WUZG;
         STAZ.WC_R:=STAZ.WC;
         STAZ.WMD_R:=STAZ.WMD;
         STAZ.WMDG_R:=STAZ.WMDG;
         STAZ.WUNSP_R:=STAZ.WUNSP;
         STAZ.WUNSPG_R:=STAZ.WUNSPG;
         STAZ.PRACA_R:='N';
         {? STAZ.put()
         || _poprawione+=1
         || _bledy+=1
         ?}
      || _dobre+=1
      ?};

      STAZ.next()
   !}
?};
STAZ.cntx_pop();
STAZ.trig_on('*','*');
__UPG.msg('Poprawionych rekordów STAZ: %1'[$_poprawione]);
__UPG.msg('Nie wymagających poprawy: %1'[$_dobre]);
_poprawione:=_dobre:=0;
STAZ_H.trig_off('*','*');
STAZ_H.cntx_psh();
STAZ_H.prefix();
{? STAZ_H.first()
|| {!
   |? {? ~(STAZ_H.RWY*STAZ_H.RWYL*STAZ_H.RWYM)
      || {? STAZ_H.RWY | STAZ_H.RWYL | STAZ_H.RWYM
         || _bledy+=1
         || STAZ_H.RWY:=STAZ_H.RWYL:=STAZ_H.RWYM:=1;
            {? STAZ_H.put()
            || _poprawione+=1
            || _bledy+=1
            ?}
         ?}
      || _dobre+=1
      ?};

      STAZ_H.next()
   !}
?};
STAZ_H.cntx_pop();
STAZ_H.trig_on('*','*');
__UPG.msg('Poprawionych rekordów STAZ_H: %1'[$_poprawione]);
__UPG.msg('Nie wymagających poprawy: %1'[$_dobre]);

__UPG.msg('Nie udało się poprawić (STAZ i STAZ_H): %1'[$_bledy]);
{? _bledy
|| _ret:=-1
?};

_ret


\STAZ_praca_r_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Uzupełnienie wartości w nowych polach tabel STAZ i STAZ_H - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie wartości w nowych polach tabel STAZ i STAZ_H'


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');

_result:=0;
_can_continue:=1;

_can_continue:=exec('stalesys_move','#stalesys','XINFO','BASE_ID',0);

{? _can_continue>0
|| _can_continue:=exec('stalesys_move','#stalesys','XINFO','LINK_INT',1)
?};

{? _can_continue>0
|| _can_continue:=exec('stalesys_move','#stalesys','XINFO','LINK_SRV',1)
?};

{? _can_continue>0
|| _can_continue:=exec('stalesys_move','#stalesys','XINFO','LINK_PRT',1)
?};
{? _can_continue>0
|| __UPG.msg('Zaktualizowano stałe systemu.')
|| __UPG.msg('Nie udała się aktualizacja stałych systemu.')
?};
{? _can_continue>0
|| _result:=1
|| _result:=-1
?};
_result


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'




\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\ISTDEF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli ISTDEF - Definicje komunikatów EDI
::----------------------------------------------------------------------------------------------------------------------
::_log_name:='istdef.log';
::_log:=fopen(_log_name,'Ua',1,,1);
::{? _log.is_open()
::||
::   _txt:='Firma';
::   fwrite(_log,'%1\t%2'[_txt,__Firma])
::?};
::obj_del(_log);
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ISTDEF.cntx_psh();
ISTDEF.prefix();
_result:=ISTDEF.for_each("
   _put:=0;
   {? ISTDEF.FORMAT='N'
   ||
      __lrec+=1;
      ISTDEF.cntx_psh();
      ISTDEF.index('LP');
      ISTDEF.prefix(__Firma,ISTDEF.SYSTEM,ISTDEF.IST_ISTK,ISTDEF.TM_STAM);
      _continue:=~ISTDEF.first();
      ISTDEF.cntx_pop();
      {? _continue=0
      ||
         {? ISTDEF.FIRMA<>__Firma
         ||
            exec('def_del','edi_xml',ISTDEF.ref(),null())
         ?}
      ||
         ISTXML.cntx_psh();
         ISTXML.index('TREE');
         ISTXML.prefix(ISTDEF.ref());
         {? ISTXML.first()
         ||
            {? ISTDEF.FIRMA=''
            ||
               ISTDEF.FIRMA:=__Firma;
               ISTDEF.VER:=ISTDEF.K+ISTDEF.FIRMA;
               {? ISTDEF.put() || __lmod+=1 ?}

            |? ISTDEF.FIRMA<>__Firma
            ||
               {? exec('istxml_copy','edi_xml',ISTDEF.ref(),0,1,,,1) || __lmod+=1 ?}
            ?}
         ?};
         ISTXML.cntx_pop()
      ?}
   ?}
",0);
ISTDEF.cntx_pop();
__UPG.msg('Zaktualizowano definicje komunikatów EDI.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ISTDEF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli ISTDEF - Definicje komunikatów EDI - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola definicji komunikatów EDI'


\URZ_TAG
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizuję pole URZ_TAG.RODZAJ
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod','__typwtag','__typftag');
__lrec:=__lmod:=0;
__typftag:=';<NRI>;<NST>;<GRUPA>;<ODD>;<JORG>;';
__typwtag:=';<FIRMA>;';
URZ_TAG.cntx_psh();
URZ_TAG.prefix();
URZ_TAG.for_each("_put:=0;
                 __lrec+=1;
                 {? URZ_TAG.RODZAJ='D'
                 || {? __typwtag*(';%1;'[URZ_TAG.KOD])
                    || URZ_TAG.RODZAJ:='DW';
                       _put:=1
                    |? __typftag*(';%1;'[URZ_TAG.KOD])
                    || URZ_TAG.RODZAJ:='DF';
                       _put:=1
                    || URZ_TAG.RODZAJ:='DN';
                       _put:=1
                    ?}
                 ?};
                 {? _put & URZ_TAG.put(1) || __lmod+=1 ?}
             ",0);
URZ_TAG.cntx_pop();
__UPG.msg('Zaktualizowano definicje tagów dla urządzeń.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod','__typwtag','__typftag');
_result


\URZ_TAG_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizuję pole URZ_TAG.RODZAJ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola rodzaju tagu dla urządzeń'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [23.25]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
_result:=rep_tran('*');
__UPG.msg('Zaktualizowano wydruki i zestawienia.');
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [23.25]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\rozrach_waluta_obca
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formuła przechodzi po wszystich rozrachunkach i dla każdego szuka jego odpowiednika w obcej walucie
::       W przypadku znalezieniu bliźniaczego rozrachunku oba rozrachunki są aktualizowane o obroty,salda i walutę
::       drugiego rozrachunku.
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh();
_licznik:=0;
_result:=1;
_msk:=OP.names();
_msk.clear();
{? _msk.first()
||
:: Pętla dla wszystkih masek
   {!
   |? OP.use(_msk.NAME);
      OP.index('KON_O');
      OP.prefix();
      {? OP.first()
      ||
:: Pętla dla wszystkich rozrachunków w obcej walucie
          OP.trig_off('*','*');
         {!
         |?
:: Wyszukanie bliźniaczego rozrachunku
            {? OP.WAL<>FINFO.NAROD
            || _wal:=OP.WAL;
               _symbol:=OP.SYM;
               _konto:=OP.AN;
               _sal_ma:=OP.SMA;
               _sal_wn:=OP.SWN;
               _ob_ma:=OP.MA;
               _ob_wn:=OP.WN;
               OP.cntx_psh();
               OP.index('KON_O');
               OP.prefix(FINFO.NAROD,OP.ODD,_konto,_symbol,_symbol,OP.SYM_ROK);
               {? OP.first()
               ||
:: Aktualizacja znalezionego rozrachunku w obcej walucie
                  OP.WAL_OB:=_wal;
                  OP.SMA_WAL:=_sal_ma;
                  OP.SWN_WAL:=_sal_wn;
                  OP.MA_WAL:=_ob_ma;
                  OP.WN_WAL:=_ob_wn;
                  _wal:=OP.WAL;
                  _sal_ma:=OP.SMA;
                  _sal_wn:=OP.SWN;
                  _ob_ma:=OP.MA;
                  _ob_wn:=OP.WN;
                  OP.put();
:: Aktualizacja aktualnego rozrachunku
                  OP.cntx_pop();
                  OP.WAL_OB:=_wal;
                  OP.SMA_WAL:=_sal_ma;
                  OP.SWN_WAL:=_sal_wn;
                  OP.MA_WAL:=_ob_ma;
                  OP.WN_WAL:=_ob_wn;
                  OP.put();
                  _licznik+=1
               || OP.cntx_pop()
               ?}
            ?};
            OP.next()
         !};
         OP.trig_on('*','*')
      ?};
      _msk.next()
   !};
   __UPG.msg('Znaleziono i zaktualizowano '+$_licznik +' par bliźniaczych rozrachunków.')
?};
OP.cntx_pop();
_result


\rozrach_waluta_obca_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formuła przechodzi po wszystich rozrachunkach i dla każdego szuka jego odpowiednika w obcej walucie
::       W przypadku znalezieniu bliźniaczego rozrachunku oba rozrachunki są aktualizowane o obroty,salda i walutę
::       drugiego rozrachunku.
::----------------------------------------------------------------------------------------------------------------------
'Formuła aktualizuje rozrachunki o dane z bliźniaczego rozrachunku w obcej walucie'


\GROP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli GROP
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
GROP.trig_off('*','*');
GROP.cntx_psh();
GROP.prefix();
GROP.index('KOD');
_result:=GROP.for_each("
   _put:=0;
   __lrec+=1;
   {? GROP.DIR=0
   || exec('dir_update','zl_grop');
      _put:=1
   ?};
   {? GROP.PLAN_PX=''
   || GROP.PLAN_PX:='N';
      _put:=1
   ?};
   {? GROP.PLAN_PO=''
   || {? exec('get_min_start','po_ogr',GROP.ref())>0
      || GROP.PLAN_PO:='T'
      || GROP.PLAN_PO:='N'
      ?};
      _put:=1
   ?};
   {? GROP.TM_REA=0
   || exec('termin_update','zl_grop');
      _put:=1
   ?};
   {? GROP.PROBLEM='T' & GROP.PROBKLAS=''
   || GROP.PROBKLAS:=exec('prob_class_suspensing','zl_wkj');
      _put:=1
   ?};
   {? GROP.NO_START=''
   || GROP.NO_START:='N';
      _put:=1
   ?};
   {? GROP.ODDZ=''
   || GROP.ODDZ:='c';
      _put:=1
   ?};
   {? GROP.ROK_MAX=0
   || GROP.ROK_MAX:=GROP.DATA~1;
      _put:=1
   ?};
   {? GROP.IDEAN=''
   || GROP.IDEAN:=exec('bl_idean','kody_kresk',GROP);
      _put:=1
   ?};
   {? GROP.LIM=''
   || GROP.LIM:='N';
      _put:=1
   ?};
   {? _put || {? GROP.put() || __lmod+=1 ?} ?}
",0);
GROP.cntx_pop();
GROP.trig_on('*','*');
__UPG.msg('Zaktualizowano grupy operacji');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\GROP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_GRP - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla grup operacji'


\PX_GRP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_GRP
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PX_GRP.trig_off('*','*');
PX_GRP.cntx_psh();
PX_GRP.index('UID');
PX_GRP.prefix();
_result:=PX_GRP.for_each("
   _put:=0;
   __lrec+=1;
   {? PX_GRP.SIM_MOD=''
   || PX_GRP.SIM_MOD:='N';
      _put:=1
   ?};
   {? PX_GRP.SIM_DEL=''
   || PX_GRP.SIM_DEL:='N';
      _put:=1
   ?};
   {? PX_GRP.KIND=''
   || PX_GRP.KIND:='Z';
      _put:=1
   ?};
   {? PX_GRP.HAS_ZL=''
   || PX_GRP.HAS_ZL:='N';
      _put:=1
   ?};
   {? _put || {? PX_GRP.put(1) || __lmod+=1 ?} ?}
",0);
PX_GRP.cntx_pop();
PX_GRP.trig_on('*','*');
__UPG.msg('Zaktualizowano elementy kolejki planu strategicznego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_GRP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_GRP - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla elementów kolejki planu strategicznego'


\PX_OBJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_OBJ
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PX_OBJ.trig_off('*','*');
PX_OBJ.cntx_psh();
PX_OBJ.index('SYMBOL');
PX_OBJ.prefix();
_result:=PX_OBJ.for_each("
   _put:=0;
   __lrec+=1;
   {? PX_OBJ.HAS_TAG=''
   || PX_OBJ.HAS_TAG:='N';
      _put:=1
   ?};
   {? PX_OBJ.HAS_ZL=''
   || PX_OBJ.HAS_ZL:='N';
      _put:=1
   ?};
   {? _put || {? PX_OBJ.put(1) || __lmod+=1 ?} ?}
",0);
PX_OBJ.cntx_pop();
PX_OBJ.trig_on('*','*');
__UPG.msg('Zaktualizowano obiekty do kolejki planu strategicznego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_OBJ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_OBJ - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla obiektów do kolejki planu strategicznego'


\PX_VER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_VER
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PX_VER.trig_off('*','*');
PX_VER.cntx_psh();
PX_VER.index('TIME');
PX_VER.prefix();
_result:=PX_VER.for_each("
   _put:=0;
   __lrec+=1;
   {? PX_VER.WHATIF=''
   || PX_VER.WHATIF:='N';
      _put:=1
   ?};
   {? _put || {? PX_VER.put(1) || __lmod+=1 ?} ?}
",0);
PX_VER.cntx_pop();
PX_VER.trig_on('*','*');
__UPG.msg('Zaktualizowano wersje planu strategicznego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_VER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli PX_VER - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla wersji planu strategicznego'


\PX_TEX4TKTL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Generuje przepisy dla nagłówków kart
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

{? var_pres('PXoTEX')<100 || exec('PXoTEX','px_tex') ?};

PX_TEX.trig_off('*','*');
PX_TEX.cntx_psh();
PX_TEX.index('TKTL');
PX_TEX.prefix('T',PXoTEX.kind.table('M'),);
{? PX_TEX.first()
|| {!
   |? {? PX_TEX.TKTL<>null()
      ||
         PX_TEX.cntx_psh();
         PX_TEX.index('TKTL');
         _rtktl:=PX_TEX.RTKTL;
         PX_TEX.prefix('T',PXoTEX.kind.table('TKTL'),PX_TEX.TKTL);
         {? PX_TEX.first()=0
         || {? exec('create4tktl','px_tex',PX_TEX.TKTL)<>null()
            || __lrec+=1
            || _result:=0
            ?}
         || PX_TEX.RTKTL:=_rtktl;
            {? PX_TEX.put()
            || __lmod+=1
            ?}
         ?};
         PX_TEX.cntx_pop()
      ?};
      PX_TEX.next()
   !}
?};
PX_TEX.cntx_pop();
PX_TEX.trig_on('*','*');
__UPG.msg('Wygenerowano nagłówki przepisów na podstawie nagłówków kart tech.');
__UPG.msg('Utworzono: %1 przepisów. Zmodyfikowano: %2 przepisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_TEX4TKTL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Generuje przepisy dla nagłówków kart - opis
::----------------------------------------------------------------------------------------------------------------------
'Generowanie nagłówków przepisów planistycznych dla nagłówków kart technologicznych'


\GROPS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli GROPS
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod','__lrec1','__lmod1');
__lrec:=__lmod:=__lrec1:=__lmod1:=0;
GROPS.trig_off('*','*');
GROPS.cntx_psh();
GROPS.index('DEFAULT');
GROP.cntx_psh();
GROP.index('KOD');
GROP.prefix();
_result:=GROP.for_each("
   _put:=0;
   _can_continue:=1;
   GROPS.prefix(GROP.ref(),'',);
   _set_first:=0;
   _size:=GROPS.size();
   {? GROPS.first()
   || _set_first:=1;
      {!
      |? __lrec+=1;
         GROPS.cntx_psh();
         GROPS.prefix();
         GROPS.DEFAULT:='N';
         _can_continue:=GROPS.put();
         GROPS.cntx_pop();
         __lmod+=1;
         GROPS.first() & _can_continue>0
      !}
   ?};
   GROPS.prefix(GROP.ref(),'N',);
   {? _can_continue>0 & _set_first>0 & GROPS.first()
   || GROPS.cntx_psh();
      GROPS.prefix();
      GROPS.DEFAULT:='T';
      _can_continue:=GROPS.put();
      GROPS.cntx_pop()
   ?};
   _can_continue
",0);
GROPS.prefix();
_result:=GROPS.for_each("
   _put:=0;
   __lrec1+=1;
   {? GROPS.IDEAN=''
   || GROPS.IDEAN:=exec('bl_idean','kody_kresk',GROPS);
      _put:=1
   ?};
   {? _put || {? GROPS.put() || __lmod1+=1 ?} ?}
",0);

GROPS.cntx_pop();
GROP.cntx_pop();
GROPS.trig_on('*','*');
__UPG.msg('Zaktualizowano zasoby grup operacji.');
__UPG.msg('Domyślny zasób - przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
__UPG.msg('Kod kreskowy - przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec1,$__lmod1]);
VAR_DEL.delete('__lrec','__lmod');
_result


\GROPS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli GROPS - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla zasobów grup operacji'


\tpp_gop_eakc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Formuła dodaje czynność TPP_GOP_EAKC do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:='\'TPP_GOP_DRED\'';
_uid:='TPP_GOP_EAKC';
exec('actionRoleAction','upgrade',_uid,_cr)


\tpp_gop_eakc_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Formuła dodaje czynność TPP_GOP_EAKC do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "TPP_GOP_EAKC - Akceptacja grupy operacji" do odpowiednich ról'


\EDI
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizuje definicje komunikatów EDI
::----------------------------------------------------------------------------------------------------------------------
:: transfer naprawczy ISTDEFV w wersji RR.xx, w emisyjnej do usunięcia
ISTDEFV.cntx_psh();
ISTDEFV.prefix();
ISTDEFV.for_each("{? ISTDEFV.ARCHIWUM='' || ISTDEFV.ARCHIWUM:='N'; ISTDEFV.put() ?}");
ISTDEFV.cntx_pop();
::
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
:: upgrade_edi_er_wrt_xp_2226_2207_0022.zip
:: upgrade_edi_23xx.zip
_fname:='upgrade_edi_%1.zip'['2325'];
_tmp_dir:=fmk_tmp_dir(0);
_tmp_pth:=_tmp_dir.get_path();
_sep:=exec('sep','#file',2);
_zip_name:=_tmp_pth+_sep+'zipfile.zip';
{? ~fcopy(_fname,_zip_name,1,0)
|| __UPG.msg('Nie powiodło się skopiowanie pliku %1.'[_fname]);
   return(0)
?};
{? ~funpack('zip',_zip_name,_tmp_pth)
|| __UPG.msg('Nie powiodło się rozpakowanie archiwum %1.'[_zipname]);
   return(0)
?};
_Files:=fdir(_tmp_pth,1,0);
_loop:=_Files.first();
{!
|? _loop
|!
   {? _Files.TYPE='f' & (_Files.NAME+4)='.dem' & (_Files.NAME+9)<>'_form.dem' & (_Files.NAME+8)<>'_xsd.dem'
   ||
      __lrec+=1;
      _msg:=exec('edi_imp_core','edi_def',_tmp_pth,_Files.NAME,0);
      {? _msg='' | _msg*'Nadpisano strukturę komunikatu:' || __lmod+=1 ?}
   ?};
   _loop:=_Files.next()
!};
__UPG.msg('Zaktualizowano definicje komunikatów EDI.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\EDI_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizuje definicje komunikatów EDI - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizuje definicje komunikatów EDI'


\ZGP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizacja pól tabeli ZGP
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ZGP.trig_off('*','*');
ZGP.cntx_psh();
TOPER.cntx_psh(); TTOPER.cntx_psh(); TTOUT.cntx_psh();
TOPER.prefix(); TTOPER.prefix(); TTOUT.prefix();
ZGP.prefix();
ZGP.for_each("
   __lrec+=1;
   _put:=0;
   {? ZGP.OPER_NAZ='' & ZGP.TOPER<>null()
   || {? TOPER.seek(ZGP.TOPER,ref_name(ZGP.TOPER),1)
      || ZGP.OPER_NAZ:={? TOPER.WEW='T' || TOPER.OPER().NA || TOPER.TTOUT().NA ?};
         _put:=1
      ?}
   ?};
   {? ZGP.PROBLEM='T' & ZGP.PROBKLAS=''
   || ZGP.PROBKLAS:=exec('prob_class_suspensing','zl_wkj');
      _put:=1
   ?};
   {? ZGP.WYK_FAST=''
   || ZGP.WYK_FAST:='N';
      _put:=1
   ?};
   {? ZGP.NO_START=''
   || ZGP.NO_START:='N';
      _put:=1
   ?};
   {? ZGP.IDEAN=''
   || ZGP.IDEAN:=exec('bl_idean','kody_kresk',ZGP);
      _put:=1
   ?};
   {? _put || {? ZGP.put(1) || __lmod+=1 ?} ?}
",1);
ZGP.cntx_pop();
TOPER.cntx_pop(); TTOPER.cntx_pop(); TTOUT.cntx_pop();
ZGP.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola pozycji przewodników produkcyjnych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZGP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizacja pól tabeli ZGP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli pozycji przewodników produkcyjnych.'


\TP2TP_KOR_ZWR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Aktualizacja tabeli TP2TP - dopisanie powiązań typów dokumentów korekty i zwrotu
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

TP2TP.cntx_psh();
TP2TP.index('T2');
TP2TP.prefix(2);
_jest:=TP2TP.first();
TP2TP.cntx_pop();

{? ~_jest

|| _typy_zwr:=exec('get','#params',300501,2);

   {? _typy_zwr<>''

   || _typy_zwr:=exec('typ_dok','lmg',_typy_zwr,,,1);

      TYPYSP.cntx_psh();
      TYPYDOK.cntx_psh();
      TP2TP.cntx_psh();
      TYPYSP.index('HIS');
      TYPYSP.prefix('N','T');
      TYPYDOK.index('KRW');
      TYPYDOK.prefix('T');
      TP2TP.index('T2');

      {? TYPYSP.first()
      || {!
         |? __lrec+=1;
            TP2TP.prefix(2,TYPYSP.ref());
            {? TYPYDOK.first()
            || {!
               |? {? (' '+_typy_zwr+' ')*(' '+TYPYDOK.T+' ')>0 & TYPYDOK.Z='T' & TYPYDOK.DN='T'
                      & TYPYDOK.TD='' & TYPYDOK.DS='N' & ~TP2TP.find_key(TYPYDOK.ref())
                  || TP2TP.blank();
                     TP2TP.RODZ:=2;
                     TP2TP.TPS1:=TYPYSP.ref();
                     TP2TP.TPM2:=TYPYDOK.ref();
                     TP2TP.add();
                     __lmod+=1
                  ?};
                  TYPYDOK.next()
               !}
            ?};
            TYPYSP.next()
         !}
      ?};

      TYPYSP.cntx_pop();
      TYPYDOK.cntx_pop();
      TP2TP.cntx_pop()

   ?}

?};

__UPG.msg('Przetworzono: %1 typów dokumentów korygujacych, dodano: %2 powiązań z typami dokumentów zwrotu .'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');

_result


\TP2TP_KOR_ZWR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Aktualizacja tabeli TP2TP - dopisanie powiązań typów dokumentów korekty i zwrotu - opis
::----------------------------------------------------------------------------------------------------------------------
'Dopisanie powiązań typów dokumentów korekty i zwrotu.'


\noty_ods
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uzupełnienie w nagłówku noty sumy kwot odsetek z pozycji
::----------------------------------------------------------------------------------------------------------------------
_przed:=_po:=0;
SER_POZ.cntx_psh(); SER_POZ.use('ser_pxx'); SER_NAG.cntx_psh();
SER_NAG.index('SERNAG1'); SER_NAG.prefix(REF.FIRMA,'N');
SER_POZ.index('SER_POZO');
{? SER_NAG.first()
|| {! |?
      {? SER_NAG.WAL<>null
      || SER_POZ.prefix(REF.FIRMA,SER_NAG.ref());
         {? SER_POZ.first()
         || _przed:=_po:=0;
            {! |?
               {? SER_POZ.UMORZNOT=0 || {? SER_POZ.PRZEDPO ||  _przed+=SER_POZ.ODS ||  _po+=SER_POZ.ODS ?} ?};
               SER_POZ.next()
            !};
            SER_NAG.SUM_ODSA:=_po; SER_NAG.SUM_ODSB:=_przed;  SER_NAG.put()
         ?}
      ?};
      SER_NAG.next()
   !}
?};
SER_NAG.cntx_pop(); SER_POZ.cntx_pop();
{? _przed | _po
|| __UPG.msg('Uzupełnienie w nagłówku noty sumy kwot odsetek z pozycji.')
|| __UPG.msg('Brak korespondencji seryjnej typu: Noty odsetkowe.')
?};
1


\noty_ods_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uzupełnienie w nagłówku noty sumy kwot odsetek z pozycji - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie w nagłówku noty sumy kwot odsetek z pozycji.'


\PX_POZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnia pola tabeli PX_POZ
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PX_POZ.trig_off('*','*');
PX_POZ.cntx_psh();
PX_POZ.index('LP');
PX_POZ.prefix();
_result:=PX_POZ.for_each("
   _put:=0;
   __lrec+=1;
   {? PX_POZ.PROBLEM='T' & PX_POZ.PROBKLAS=''
   || PX_POZ.PROBKLAS:=exec('prob_class_suspensing','zl_wkj');
      _put:=1
   ?};
   {? _put || {? PX_POZ.put(1) || __lmod+=1 ?} ?}
",0);
PX_POZ.cntx_pop();
PX_POZ.trig_on('*','*');
__UPG.msg('Zaktualizowano pozycje planu strategicznego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PX_POZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnia pola tabeli PX_POZ - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla pozycji planu strategicznego.'


\PL_OPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnia pola tabeli PL_OPER
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PL_OPER.trig_off('*','*');
PL_OPER.cntx_psh();
PL_OPER.index('UID');
PL_OPER.prefix();
_result:=PL_OPER.for_each("
   _put:=0;
   __lrec+=1;
   {? PL_OPER.PROBLEM='T' & PL_OPER.PROBKLAS=''
   || PL_OPER.PROBKLAS:=exec('prob_class_suspensing','zl_wkj');
      _put:=1
   ?};
   {? _put || {? PL_OPER.put(1) || __lmod+=1 ?} ?}
",0);
PL_OPER.cntx_pop();
PL_OPER.trig_on('*','*');
__UPG.msg('Zaktualizowano pozycje planu operacyjnego.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\PL_OPER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnia pola tabeli PL_OPER - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla pozycji planu operacyjnego.'


\tat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja nieuzupełnionych pól TAT.MIEJSCE
::----------------------------------------------------------------------------------------------------------------------
_ret:=-1;
_tab:=sql(
   'select REFERENCE as REF, NA, OPIS, \'B\' as M, \'           \' as S, \'                   \' as O, 0 as Z '
   'from TAT '
   'where TAT.MIEJSCE=\'\' '
   'order by NA,OPIS'
);
{? _tab.first()
|| TAT.cntx_psh();
   TAT.prefix();
   {!
   |? {? TAT.seek(_tab.REF)
      || {? TAT.count()=0 & TAT.W_PORTAL='N'
         || _tab.next()
         || _tab.del()
         ?}
      || _tab.del()
      ?}
   !};
   TAT.cntx_pop()
?};
{? _tab.first()
|| VAR_DEL.delete('TatM');
   TatM:=obj_new(
      'FLD','SEL_EXIT','D0','D1','P0','P1','N0','N1',
      'I0','I1'
   );
   TatM.SEL_EXIT:=0;
   TatM.I0:=_tab.index('?');
   TatM.I1:=_tab.ndx_tmp(,,'Z',,);
   _tab.fld_fml('M','AFTER_EDIT',"TatM.FLD:=fld(); 1");
   _red:=_tab.mk_edit('Miejsce',,'#tatupdmiejsce');
   _tab.win_efld(_red,,'M',,,,,,'Miejsce',,,'radio-buttons',,
      'Brak',"'B'",
      'Logistyka',"'L'",
      'Obieg dokumentów',"'W'",
      'Produkcja',"'U'",
      'Środki trwałe',"'T'"
   );
   _ok:=_tab.win_ebtn(_red,'text=%1'['OK'@],'key:F2');
   _anuluj:=_tab.win_ebtn(_red,'text=%1'['Anuluj'@],'key:Esc');
   _tab.btn_eopt(_red,_ok,'tooltip='+exec('help_red_ok','#window','P'));
   _tab.btn_eopt(_red,_anuluj,'tooltip='+exec('help_red_esc','#window','A'));
   _tab.win_edit(_red);
   _wer:=_tab.mk_sel('Atrybuty','P',,'#tatupdmiejsce',,,,,'U');
   _tab.win_fld(_wer,,'NA',,,,,,'Nazwa');
   _tab.win_fld(_wer,,'OPIS',,,100,,,'Opis');
   _tab.win_fld(_wer,,'S',,,11,,,'Operacja');
   _tab.win_fld(_wer,,'O',,,20,,,'Miejsce');
   _tab.win_act(_wer,,'Formuła','Popraw',,,"
      _tab:=cur_tab();
      {? _tab.sel_size() | _tab.edit()
      || _tab.M:=TatM.FLD;
         _tab.S:={? _tab.M<>'B' || 'Poprawienie' || '' ?};
         _tab.Z:=_tab.M<>'B';
         _tab.O:={? _tab.M='L' || 'Logistyka'
                 |? _tab.M='W' || 'Obieg dokumentów'
                 |? _tab.M='U' || 'Produkcja'
                 |? _tab.M='T' || 'Środki trwałe'
                 || ''
                 ?};
         _tab.put()
      ?}
   ",,1,1,"
      _tab:=cur_tab();
      {? _tab.edit()
      || 1
      ?}
   ");
   _tab.win_act(_wer,,'Formuła','Usuń',,,"
      _tab:=cur_tab();
      {? _tab.sel_size() | FUN.ask('Czy usunąć atrybut?'@)
      || _tab.S:='Usunięcie';
         _tab.M:='D';
         _tab.O:='';
         _tab.Z:=1;
         _tab.put()
      ?}
   ",,,1,"
      {? FUN.ask('Czy usunąć zaznaczone atrybuty?'@)
      || 1
      ?}
   ");
   _tab.win_act(_wer,,'Formuła','Zakończ',,,"TatM.SEL_EXIT:=1; sel_exit()");
   _tab.win_act(_wer,,'Okienko',,,,,"
      _tab:=cur_tab();
      {? TatM.SEL_EXIT
      || _tab.cntx_psh();
         _tab.index(TatM.I1);
         _tab.prefix(0);
         _not:=_tab.first();
         _tab.cntx_pop();
         {? _not
         || FUN.ask('Istnieją atrybuty bez uzupełnionej operacji.\nCzy kontynuować?'@)
         || 1
         ?}
      || _tab.cntx_psh();
         _tab.index(TatM.I1);
         _tab.prefix(1);
         _set:=_tab.first();
         _tab.cntx_pop();
         {? _set
         || FUN.ask('Istnieją atrybuty z uzupełnioną operacją.\nAnulować zmiany?'@)
         || 1
         ?}
      ?}
   ");
   _tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['&Popraw'@],'menu:P');
   _tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=right,align=begin'['&Usuń'@],'menu:U');
   _tab.win_btn(_wer,'text=%1,btn_label_align=center,panel=bottom,align=end'['&Zakończ'@],'menu:Z');
   _tab.win_sel(_wer);
   {? _tab.select()
   || TatM.D0:=TatM.D1:=TatM.P0:=TatM.P1:=0;
      TatM.N0:=TatM.N1:=0;
      _tab.cntx_psh();
      _tab.index(TatM.I1);
      _tab.prefix(1);
      {? _tab.first()
      || TAT.cntx_psh();
         TAT.prefix();
         {!
         |? {? TAT.seek(_tab.REF)
            || {? _tab.M='D'
               || TatM.D0+=1;
                  {? TAT.count()=0
                  || {? TAT.del(,1)
                     || TatM.D1+=1
                     ?}
                  ?}
               |? TAT.MIEJSCE=''
               || TatM.P0+=1;
                  TAT.MIEJSCE:=_tab.M;
                  {? TAT.put()
                  || TatM.P1+=1
                  ?}
               || TatM.N1+=1
               ?}
            || TatM.N0+=1
            ?};
            _tab.next()
         !};
         TAT.cntx_pop()
      ?};
      _tab.prefix(0);
      _bez:=_tab.size();
      {? _bez=0 || _ret:=1 ?};
      _tab.cntx_pop();
      {? TatM.D0
      || __UPG.msg('Usunięto: %1 z %2'[$TatM.D1,$TatM.D0])
      ?};
      {? TatM.P0
      || __UPG.msg('Poprawiono: %1 z %2'[$TatM.P1,$TatM.P0])
      ?};
      {? TatM.N0
      || __UPG.msg('Nie znaleziono: %1'[$TatM.N0])
      ?};
      {? TatM.N1
      || __UPG.msg('Miejsce już uzupełnione: %1'[$TatM.N1])
      ?};
      {? _bez
      || __UPG.msg('Do uzupełnienia: %1'[$_bez])
      ?}
   ?}
|| _info:='Brak atrybutów do uzupełnienia.'@;
   __UPG.msg(_info);
   FUN.info(_info);
   _ret:=1
?};
VAR_DEL.delete('TatM');
_ret


\tat_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja nieuzupełnionych pól TAT.MIEJSCE - opis
::----------------------------------------------------------------------------------------------------------------------
'Umożliwia uzupełnienie znaczników miejsca użycia typów atrybutów (TAT.MIEJSCE), które nie zostały wypełnione formułą'
' automatyczną lub usunięcie tych rekordów.'


\zws_par_atra
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Formuła dodaje czynność ZWS_PAR_ATRA do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'ZWS_PAR_ALIC'";
_uid:='ZWS_PAR_ATRA';
_res:=1;
_res:=exec('actionRoleAction','upgrade',_uid,_cr);
{? _res<>0
|| _rola:='Administrator';
   _ref:=exec('ref','#b_role',_rola);
   {? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
   || __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_uid]);
      _res:=-1
   || {? exec('add_actrol_one','#b_role',_rola,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_rola])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_rola]);
         _res:=0
      ?}
   ?}
?};
_res


\zws_par_atra_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Formuła dodaje czynność ZWS_PAR_ATRA do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZWS_PAR_ATRA - Definicje tłumaczeń" do odpowiednich ról'


\ZGH
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja pól tabeli ZGH
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ZL.cntx_psh();
ZL.prefix();
ZGH.trig_off('*','*');
ZGH.cntx_psh();
ZGH.prefix();
ZGH.for_each("
   __lrec+=1;
   _put:=0;
:: Ustawienie znacznika BRAKI (robiony put wewnątrz formuły)
   {? ZGH.BRAKI=''
   || exec('update_braki','zl_common',ZGH.ref(),,,ZGH)
   ?};
   {? ZGH.PROBLEM=''
   || ZGH.PROBLEM:='N';
      _put:=1
   ?};
   {? ZGH.KTM=null()
   || ZGH.KTM:=ZGH.ZLEC().KTM;
      _put:=1
   ?};
   {? ZGH.KH=null()
   || ZGH.KH:=ZGH.ZLEC().KH;
      {? ZGH.KH<>null()
      || _put:=1
      ?}
   ?};
   {? ZGH.DT=date(0,0,0)
   || _date:=10+ZGH.IDADD;
      ZGH.DT:=date(#(4+_date),#(2+(5-_date)),#(_date+2));
      _put:=1
   ?};
   {? _put || {? ZGH.put(1) || __lmod+=1 ?} ?}
",1);
ZGH.cntx_pop();
ZL.cntx_pop();
ZGH.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola nagłówków przewodników produkcyjnych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZGH_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja pól tabeli ZGH - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli nagłówków przewodników produkcyjnych.'


\ZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizacja pól tabeli ZL
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod','__zltab');
__lrec:=__lmod:=0;
__zltab:=~~;
ZL.cntx_psh();
ZL.trig_off('*','*');
ZL.prefix();
{? ZL.find_tab('first','ROK_MAX',,'=',0)
||
:: Jeżeli jest chociaż jeden rekord z nieuzupełnionym polem ROK_MAX to tworzona tabelka
   _sql:='select DK.ZL as REF, MAX(DK.AR) as DATA from @DK join ZL using (DK.ZL, ZL.REFERENCE) group by DK.ZL';
   __zltab:=sql(_sql)
?};
ZL.for_each("
   __lrec+=1;
   _put:=0;
   {? ZL.ROK_MAX=0
   || {? var_pres('__zltab')>100 & __zltab.find_tab(,'REF',,'=',$ZL.ref()) & __zltab.DATA<>0
      || ZL.ROK_MAX:=__zltab.DATA
      || ZL.ROK_MAX:=ZL.OD~1
      ?};
      _put:=1
   ?};
   {? _put || {? ZL.put(1) || __lmod+=1 ?} ?}
",1);
ZL.cntx_pop();
ZL.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola zleceń produkcyjnych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod','__zltab');
_result


\ZL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja pól tabeli ZL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli zleceń produkcyjnych.'


\stalesys_wid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Aktualizacja widoków stałych systemu
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

:: Uzupełnienie widoków stałych systemu:
KST_LST.cntx_psh();
KST_LST.prefix();
KST_LST.f_set(
   'NUMER',
   'join KST_DOM using(KST_LST.KST_DOM,KST_DOM.REFERENCE) '
   'join KST_MAP using(KST_LST.KST_MAP,KST_MAP.REFERENCE) '
   'join KST_ZES using(KST_MAP.KST_ZES,KST_ZES.REFERENCE) '
   'join B_DOMAIN using(KST_DOM.B_DOMAIN,B_DOMAIN.REFERENCE) ',
   'B_DOMAIN.SYMBOL=\'PPK\' and KST_ZES.SYMBOL=\'KST_PPK\''
);
{? KST_LST.f_last()
|| _num:=KST_LST.NUMER;
   _dom:=KST_LST.KST_DOM;
   KST_MAP.cntx_psh();
   KST_MAP.index('KST_MAP');
   KST_MAP.prefix('KST_PPK','KST_PPK');
   {? KST_MAP.find_key('POP_ZAW','POP_ZAW')
::    Znajdź pole w zmiennej reprezentujące stałą
   || {? ~KST_LST.find_tab(,'KST_DOM',,'=',_dom,'KST_MAP',,'=',KST_MAP.ref())
::       Uzupełnij widok o znalezioną stałą
      || KST_LST.KST_MAP:=KST_MAP.ref();
         KST_LST.NUMER:=_num+1;
         KST_LST.add()
      ?}
   ?};
   KST_MAP.cntx_pop()
?};
KST_LST.f_clear();
KST_LST.cntx_pop();

__UPG.msg('Zaktualizowano widoki stałych systemu.');

_ret


\stalesys_wid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Aktualizacja widoków stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja widoków stałych systemu'


\sch_imp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Importuje "personelowe" wnioski w obiegu
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_msg:="
{? _a
|| __UPG.msg('Zakończono import wniosku: \"%1\".'@[_b])
|| __UPG.msg('Nie udało się zaimportować wniosku: \"%1\".'@[_b])
?}
";
:: Nazwy formuł w obiegi.fml zwracających nazwę konkretnego wniosku (odzdzielone przecinkami):
_nazwy:=spli_str('nazwa_dwwd',',');

{! _ii:=1..obj_len(_nazwy)
|! _nazwa:=exec(_nazwy[_ii],'obiegi');
   _msg(exec('imp_wn','sch_imp',_nazwa,1,0),_nazwa)
!};

obj_del(_nazwy);
1


\sch_imp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Importuje "personelowe" wnioski w obiegu - opis
::----------------------------------------------------------------------------------------------------------------------
'Import typów wniosków w obiegu'


\stalesys_PPK_VI
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Aktualizacja stałych dziedziny PPK
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
{? var_pres('POP_ZAW',KST_PPK)>0
|| _zap:=obj_new('TAB','add');
   _zap.TAB:=tab_tmp(1,'DATE','DATE','Data'@,'WAR','INTEGER','Wartość'@);
   _zap.add:="
      .TAB.DATE:=_a;
      .TAB.WAR:=_b;
      .TAB.add()";
:: Zapisy do dodania (konieczne zachowanie kolejności)
   _zap.add(date(0,0,0),0);
   _zap.add(date(2022,6,4),14);

   {? _zap.TAB.first()
   || {!
      |? exec('czytaj','#stalesys',_zap.TAB.DATE,KST_PPK,'POP_ZAW');
         {? KST_PPK.POP_ZAW=0
         || KST_PPK.POP_ZAW:=_zap.TAB.WAR;
            exec('zapisz_zes','#stalesys',_zap.TAB.DATE,KST_PPK,'POP_ZAW');
            _info:='Ustawiono wartość %1 dla daty %2.'@[$_zap.TAB.WAR,_zap.TAB.DATE$1]
         || _info:='Ustawienie wartości dla daty %1 nie było konieczne.'@[_zap.TAB.DATE$1]
         ?};
         __UPG.msg(_info);

         _zap.TAB.next()
      !}
   ?};
   obj_del(_zap)
|| __UPG.msg('Brak pola POP_ZAW w stałych systemu (KST_PPK).');
   _ret:=-1
?};

_ret


\stalesys_PPK_VI_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Aktualizacja stałych dziedziny PPK - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych dziedziny PPK'


\B_ACTION
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja czynności
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
B_ACTION.cntx_psh();
B_ACTION.prefix();
B_ACTION.for_each("
   __lrec+=1;
   {? B_ACTION.RUNNPROC=''
   || B_ACTION.RUNNPROC:='N';
      {? B_ACTION.put(1) || __lmod+=1 ?}
   ?}",
   1
);
B_ACTION.cntx_pop();
__UPG.msg('Zaktualizowano czynności.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\B_ACTION_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja czynności - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktulizacja czynności'


\BI_PORT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli BI_PORT
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

BI_PORT.cntx_psh();
BI_PORT.use(gsub(BI_PORT.name(1),'?','')+'____');
BI_PORT.prefix();
BI_PORT.for_each("
   _put:=0;
   __lrec+=1;
   {? BI_PORT.VAL_VOID=''
   ||
      BI_PORT.VAL_VOID:='N';
      _put:=1
   ?};
   {? _put & BI_PORT.put(1) || __lmod+=1 ?}
",1);
BI_PORT.cntx_pop();
__UPG.msg('Zaktualizowano porty uruchomionych procesów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BI_PORT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli BI_PORT - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja portów uruchomionych procesów.'


\nosezon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uzupełnia pole NOSEZON tabeli MA
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
MA.cntx_psh();
MA.prefix();
MA.for_each(" _put:=0;  __lrec+=1;
             {? MA.NOSEZON='' || MA.NOSEZON:='N'; _put:=1 ?};
             {? _put & MA.put(1) || __lmod+=1 ?}
            ",0);
MA.cntx_pop();
__UPG.msg('Zaktualizowano znacznik ignorowania sezonowości pracy środka dla metod amortyzacji.');
__UPG.msg('Przetworzono: %1 pozycji, z czego zmodyfikowano: %2 pozycji.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\nosezon_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uzupełnia pole NOSEZON tabeli MA - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika ignorowania sezonowości pracy środka dla metod amortyzacji.'


\f_kontrolna_poz_pln
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Dodaje formułę kontrolną która sprawdza złotówkowe pozycje 2??, podczas akceptacji dokumentu
:: w wyciągach bankowych, czy istnieje analogiczny rozrachunek w walucie obcej.
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G','POZ_PLN',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:='POZ_PLN';
   FORMULA.NAZWA:='Formuła dla pozycji sprawdzająca pozycje złotówkowe';
   FORMULA.FORMULA:='exec(\'spr_poz_pln\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=2
?};
FORMULA.cntx_pop();
{? _ret=2
|| __UPG.msg('Formuła kontrolna POZ_PLN jest już dodana w systemie.')
|? _ret=1
|| __UPG.msg('Dodano formułę kontrolną POZ_PLN.')
|| __UPG.msg('Dodanie formuły kontrolnej POZ_PLN nie powiodło się.')
?};
_ret


\f_kontrolna_poz_pln_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Dodaje formułę kontrolną która sprawdza złotówkowe pozycje 2??, podczas akceptacji dokumentu
:: w wyciągach bankowych, czy istnieje analogiczny rozrachunek w walucie obcej. - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły kontrolnej, która sprawdza złotówkowe pozycje 2??, podczas akceptacji dokumentu w '
'wyciągach bankowych. W celu sprawdzenia czy istnieje analogiczny rozrachunek w walucie obcej.'


\AN_OPIS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KW [23.25]
:: OPIS: Uzupełnia pole OPIS tabeli AN - Opis ostatniego poziomu konta
::----------------------------------------------------------------------------------------------------------------------
_licznik:=0;
_result:=1;
ROK_F.cntx_psh(); ROK_F.index('KODG'); ROK_F.prefix();
KS.cntx_psh(); KS.index('SYM'); KS.prefix();
BUD.cntx_psh(); BUD.index('KS');
SLO.cntx_psh(); SLO.index('SL'); SLO.prefix();
SLU.cntx_psh(); SLU.index('NAZ'); SLU.prefix();
{? ROK_F.first()
|| {! |?
      KS.prefix(ROK_F.ref());
      AN.cntx_psh(); AN.use('koan__'+ROK_F.KOD); AN.index('WALSYM'); AN.prefix();
      {? AN.first()
      || {! |?
            BUD.prefix(AN.KS);
            _opis:={? ~BUD.last()
                   || {? AN.KS || AN.KS().NAZ || '' ?}
                   || {? SLU.seek(BUD.SLU().SLU)
                      || {? SLO.find_key(BUD.SLU().SLU,|(AN.SYM+SLU.DL))
                         || SLO.TR
                         || ''
                         ?}
                      || ''
                      ?}
                   ?};
            {? AN.OPIS<>_opis
            || AN.OPIS:=_opis;
              {? AN.put() || _licznik+=1 ?}
            ?};
            AN.next()
         !}
      ?};
      AN.cntx_pop();
      ROK_F.next()
   !}
?};
ROK_F.cntx_pop();
KS.cntx_pop();
BUD.cntx_pop();
SLO.cntx_pop();
SLU.cntx_pop();
{? _licznik<>0
|| __UPG.msg('Przetworzono %1 zapisów.'[$_licznik])
|| __UPG.msg('Nie wprowadzono zmian, dane aktualne.')
?};
_result


\AN_OPIS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: KW [23.25]
:: OPIS: Uzupełnia pole OPIS tabeli AN - Opis ostatniego poziomu konta
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych tabeli AN'


\rubryki_atrybuty
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [23.25]
:: OPIS: Dodanie rubryk i atrybutów płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

:: Aktualizacja i dodanie nowych składników płacowych. ----------------------------------------------------------------

:: ER/WRT/XP/12.51/2205/0004 - Błąd po poprawce do ER/WRT/XP/12.51/1811/0014 Nieprawidłowa wartość
::                             970 Podst. skł. zas.mac.  w miesiącu, w którym następuje przekroczenie maksymalnego
::                             wymiaru składek emer-rent.
:: 7217  Podst. Z.M. - kor.

{? +exec('import','rubryki','g',
      '7211,7212,7213,7214,7217,7218,7219,7220,7221,7222,7223',
      1,0
   )
|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
   _result:=-1
|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
?};
__UPG.msg(_txt);
{? _result=-1 || return(_result) ?};

:: Aktualizacja atrybutów rubryk. -------------------------------------------------------------------------------------

:: ER/WRT/XP/12.51/2205/0004 - Błąd po poprawce do ER/WRT/XP/12.51/1811/0014 Nieprawidłowa wartość
::                             970 Podst. skł. zas.mac.  w miesiącu, w którym następuje przekroczenie maksymalnego
::                             wymiaru składek emer-rent.
exec('add_attr','rubatr',5612,56122,'Podstawa Emerytalno-Rentowa (korekta)',1,
   'Podstawa Emerytalno-Rentowa (korekta)',,7217);

_tip_1:="
   'Składniki z atrybutem '+form(_a,,,'99')+', które powinny być uwzględnione w podstawie, '+
   'w kwocie pomniejszonej proporcjonalnie o liczbę dni nieobecności w stosunku do liczby '+
   'dni kalendarzowych miesiąca listy płac.\nNieobecności powodujące pomniejszenie są '+
   'określone w atrybucie '+form(_b,,,'99')+'.'
";
_tip_2:="
   'Nieobecności powodujące pomniejszenie podstawy o część kwoty składników z atrybutem '+
   form(_a,,,'99')+'. Kwota pomniejszenia jest proporcjonalna do łącznej liczby dni nieobecności '+
   ' w stosunku do liczby dni kalendarzowych listy płac.'
";
_tip_3:="
   'Podstawa zostanie zmieniona o sumę składników z przypisanym atrybutem.'
";
_txt_1:='Składnik dla bieżącej listy płac';
_txt_2:='Składnik dla poprzednich list płac';
exec('add_attr','rubatr',100,1007,'Składniki pomniejszane w podstawach ubezp.',,
   'Powiązane atrybuty umożliwiają zmianę podstaw składek/wpłat. Pewne składniki wynagrodzenia wypłacane '+
   'w stałej kwocie nie powinny być wliczane w pełnej wysokości w podstawach w przypadku wystąpienia '+
   'określonych nieobecności. W podstawach powinny być uwzględnione tylko do wysokości proporcjonalnej '+
   'do czasu, w którym pracownik nie był nieobecny z określonych powodów.'
);
   exec('add_attr','rubatr',1007,10071,'Ubezpieczenie emerytalno-rentowe',,
      'Powiązane atrybuty umożliwiają określenie, które składniki mają być uwzględniane proporcjonalnie '+
      'do czasu nieobecności w podstawie składek na ubezpieczenie emerytalno-rentowe.'
   );
      exec('add_attr','rubatr',10071,100711,'Pomniejszane składniki płacowe',,_tip_1(100711,100712));
      exec('add_attr','rubatr',10071,100712,'Nieobecności powodujące pomniejszenie',,_tip_2(100711),,
         3,5,28,32
      );
      exec('add_attr','rubatr',10071,100713,'Składniki zmieniające podstawę ubezpieczenia',,_tip_3());
         exec('add_attr','rubatr',100713,1007131,_txt_1,,_tip_3(),,7211);
         exec('add_attr','rubatr',100713,1007132,_txt_2,,_tip_3(),,7219);
   exec('add_attr','rubatr',1007,10072,'Ubezpieczenie chorobowe i wypadkowe',,
      'Powiązane atrybuty umożliwiają określenie, które składniki mają być uwzględniane proporcjonalnie '+
      'do czasu nieobecności w podstawach składek na ubezpieczenie chorobowe i wypadkowe.'
   );
      exec('add_attr','rubatr',10072,100721,'Pomniejszane składniki płacowe',,_tip_1(100721,100722));
      exec('add_attr','rubatr',10072,100722,'Nieobecności powodujące pomniejszenie',,_tip_2(100721),,
         3,5,28,32
      );
      exec('add_attr','rubatr',10072,100723,'Składniki zmieniające podstawę ubezpieczenia',,_tip_3());
         exec('add_attr','rubatr',100723,1007231,_txt_1,,_tip_3(),,7212);
         exec('add_attr','rubatr',100723,1007232,_txt_2,,_tip_3(),,7220);
   exec('add_attr','rubatr',1007,10073,'Składki na FP i FGŚP',,
      'Powiązane atrybuty umożliwiają określenie, które składniki mają być uwzględniane proporcjonalnie '+
      'do czasu nieobecności w podstawach składek na Fundusz Pracy i Fundusz Gwarantowanych Świadczeń Pracowniczych.'
   );
      exec('add_attr','rubatr',10073,100731,'Pomniejszane składniki płacowe',,_tip_1(100731,100732));
      exec('add_attr','rubatr',10073,100732,'Nieobecności powodujące pomniejszenie',,_tip_2(100731),,
         3,5,28,32
      );
      exec('add_attr','rubatr',10073,100733,'Składniki zmieniające podstawę ubezpieczenia',,_tip_3());
         exec('add_attr','rubatr',100733,1007331,_txt_1,,_tip_3(),,7213);
         exec('add_attr','rubatr',100733,1007332,_txt_2,,_tip_3(),,7223);
   exec('add_attr','rubatr',1007,10074,'Wpłaty na PPK',,
      'Powiązane atrybuty umożliwiają określenie, które składniki mają być uwzględniane proporcjonalnie '+
      'do czasu nieobecności w podstawie wpłat na Pracowniczy Plan Kapitałowy.'
   );
      exec('add_attr','rubatr',10074,100741,'Pomniejszane składniki płacowe',,_tip_1(100741,100742));
      exec('add_attr','rubatr',10074,100742,'Nieobecności powodujące pomniejszenie',,_tip_2(100741),,
         3,5,28,32
      );
      exec('add_attr','rubatr',10074,100743,'Składniki zmieniające podstawę wpłat',,_tip_3());
         exec('add_attr','rubatr',100743,1007431,_txt_1,,_tip_3(),,7214);
         exec('add_attr','rubatr',100743,1007432,_txt_2,,_tip_3(),,7221);
   exec('add_attr','rubatr',1007,10075,'Ubezpieczenie zdrowotne',,
      'Powiązane atrybuty umożliwiają określenie, które składniki mają być uwzględniane proporcjonalnie '+
      'do czasu nieobecności w podstawie składek na ubezpieczenie zdrowotne.'
   );
      exec('add_attr','rubatr',10075,100751,'Pomniejszane składniki płacowe',,_tip_1(100751,100752));
      exec('add_attr','rubatr',10075,100752,'Nieobecności powodujące pomniejszenie',,_tip_2(100751),,
         3,5,28,32
      );
      exec('add_attr','rubatr',10075,100753,'Składniki zmieniające podstawę ubezpieczenia',,_tip_3());
         exec('add_attr','rubatr',100753,1007531,_txt_1,,_tip_3(),,7218);
         exec('add_attr','rubatr',100753,1007532,_txt_2,,_tip_3(),,7222);

:: ER/WRT/XP/22.26/2207/0046 : ER/WRT/XP/12.51/2207/0009 : Wynagrodzenie chorobowe (Pakiet mobilności)
{? __RUB.ref(7074) & __RUB.ref(7204)
|| exec('add_attr','rubatr',901,90107,'Kwota diet za czas oddelegowania do pracy za gran.',1,
        'Kwota diet za czas oddelegowania do pracy za granicą.',,7074);
   exec('add_attr','rubatr',901,90108,'Kwota diet kierowcy (pakiet mobilności)',1,
        'Kwota diet kierowcy (pakiet mobilności).',,7204)
|| __UPG.msg('W systemie brak rubryk płacowych: 7074, 7204.');
   _result:=-1
?};

{? _result=1
|| __UPG.msg('Zakończono aktualizację atrybutów składników płacowych.')
?};

_result


\rubryki_atrybuty_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [23.25]
:: OPIS: Dodanie rubryk i atrybutów płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk i atrybutów płacowych.'


\szablony
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import zestawów danych i definicji szablonów.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_ASDZ,ZWS_PAR_ASDD',','));
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['szablony'])
|| __UPG.msg('Zaktualizowano zestawy danych i definicje szablonów.');
   _result:=1
|| _result:=-1
?};
_result


\szablony_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import zestawów danych i definicji szablonów - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import zestawów danych i definicji szablonów.\n'
'Zostaną zaimportowane aktualne definicje szblonów wydruków WORD.\n'
'Dodatkowo, uzupełnione zostaną predefiniowane elementy danych do wykorzystania podczas tworzenia wzorców wydruków.\n'


\uzupIDpal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pola IDPAL, KODPAL dla powiązanych tabel z tabelą PAL
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');


_form:="__lrec:=__lmod:=0;
        _tab:=_a;
        _msk:=_tab.names();
        _tab.cntx_psh();
        _msk.clear();
        {? _msk.first()
        || {!
           |? _tab.use(_msk.NAME);
              _tab.prefix();
              {? _tab.first()
              || {!
                 |? __lrec+=1;
                    {? ref_name(_tab.PAL)='palety' & exec('uzupIDkod','magdok_palety',_tab)
                    || {? _tab.put(1) || __lmod+=1 ?}
                    ?};
                    _tab.next()
                 !}
              ?};
              _msk.next()
           !}
        ?};
        _tab.cntx_pop();
        obj_del(_msk);
        __UPG.msg('Zaktualizowano pola w tabeli %1.'[_b]);
        __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])";

PAL.cntx_psh();
PAL.use('palety');
_form(DK_L,'DK_L');
_form(DK_LI,'DK_LI');
_form(EANP,'EANP');
_form(INY,'INY');
_form(PAL_POZ,'PAL_POZ');
_form(PAL_ZAM,'PAL_ZAM');
_form(SL,'SL');
_form(TR_ZLM,'TR_ZLM');
PAL.cntx_pop();

VAR_DEL.delete('__lrec','__lmod');
_result


\uzupIDpal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pola IDPAL, KODPAL dla powiązanych tabel z tabelą PAL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól w tabelach powiązanych z paletami'


\portal_bnftt_nag_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: Poprawka błędu ER/WRT/XP/22.26/2208/0073 - Benefity - synchronizacja załączników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
SYNC_IDD.cntx_psh();
SYNC_IDD.index('SYSTEM');
SYNC_IDD.prefix('PORTAL','PORTAL_BNFTT_NAG_ID',);
{? SYNC_IDD.first()
|| SYNC_IDM.cntx_psh();
   SYNC_IDM.index('KOD');
   SYNC_IDM.prefix(SYNC_IDD.KOD,);
   {? SYNC_IDM.first()
   || _stat:=obj_new('poprawnych','niepoprawnych','nieznalezionych');
      {! _lp:=1 .. obj_len(_stat)
      |! _stat[_lp]:=0
      !};

      BNFTT.cntx_psh();
      SYNC_ID.cntx_psh();
      {!
      |? SYNC_ID.use(SYNC_IDM.MASKA_ID);
         SYNC_ID.index('REK');
         SYNC_ID.prefix();
         {? SYNC_ID.first()
         || {!
            |? {? BNFTT.seek(SYNC_ID.REK,)
               || {? SYNC_ID.OPIS=BNFTT.HASH
                  || _stat.poprawnych+=1;
                     SYNC_ID.next()
                  || _stat.niepoprawnych+=1;
                     SYNC_ID.del(,1)=2
                  ?}
               || _stat.nieznalezionych+=1;
                  SYNC_ID.del(,1)=2
               ?}
            !}
         ?};
         SYNC_IDM.next()
      !};
      SYNC_ID.cntx_pop();
      BNFTT.cntx_pop();

      __UPG.msg('Zapisów poprawnych: %1'[$_stat.poprawnych]);
      __UPG.msg('Zapisów niepoprawnych: %1'[$_stat.niepoprawnych]);
      __UPG.msg('Nieznalezionych benefitów (usuniętych identyfikatorów): %1'[$_stat.nieznalezionych])

   || __UPG.msg('Brak danych do weryfikacji [SYNC_IDM].')
   ?};
   SYNC_IDM.cntx_pop()
|| __UPG.msg('Brak danych do weryfikacji [SYNC_IDD].')
?};
SYNC_IDD.cntx_pop();
_ret


\portal_bnftt_nag_id_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie zbędnych identyfikatorów rekordów dla kodu PORTAL_BNFTT_NAG_ID'


\DOKUM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnienie nowych pól w tabeli DOKUM oraz brakujących wartości DOKUM.DOKUMI
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=DOKUM.names();
_msk.clear();
DOKUM.cntx_psh(); DOKUMZ.cntx_psh(); DOKUMZA.cntx_psh();
DOKUM.trig_off('*','*');
{? _msk.first()
|| {!
   |? DOKUM.use(_msk.NAME);
      DOKUM.index('DOKUM');
      DOKUM.prefix();
      {? DOKUM.first()
      || {!
         |? __lrec+=1;
            _put:=0;
            {? DOKUM.KSEFUPOG=''
            || {? DOKUM.BL_GUID<>''
               || DOKUMZ.index('GUID');
                  DOKUMZ.prefix(DOKUM.ref(),DOKUM.BL_GUID);
                  {? DOKUMZ.first()
                  || DOKUM.KSEFUPOG:='T'
                  || {? DOKUM.KSEFUPO=''
                     || DOKUM.KSEFUPOG:='N'
                     || DOKUM.KSEFUPOG:='P'
                     ?}
                  ?}
               || DOKUM.KSEFUPOG:='N'
               ?};
               _put:=1
            ?};
            {? DOKUM.name()='dokum' & DOKUM.DOKUMI=null()
            || DOKUMZ.index('DISP');
               DOKUMZ.prefix(DOKUM.ref());
               _ok:=0;
               {? DOKUMZ.first()
               || {!
                  |? {? DOKUMZ.DOKUM<>null() & DOKUMZ.bl_info('DOKUM','EXTENSION')='pdf'
                     || DOKUM.DOKUMI:=DOKUMZ.DOKUM;
                        _put:=1; _ok:=1
                     ?};
                     _ok=0 & DOKUMZ.next()
                  !}
               ?}
            ?};
            {? DOKUM.BL_VDZK=''
            || {? DOKUM.RODZAJ_K='P' & DOKUM.BL_VISIB='T' & (3+DOKUM.BL_TYPE='INV' | DOKUM.BL_TYPE='DOC')
               || DOKUM.BL_VDZK:='T'
               || DOKUM.BL_VDZK:='N'
               ?};
               _put:=1
            ?};
            {? DOKUM.BL_VORD=''
            || {? DOKUM.RODZAJ_K='P' & DOKUM.BL_VISIB='T' & (DOKUM.BL_TYPE='ORD' | DOKUM.BL_TYPE='DOC')
               || DOKUM.BL_VORD:='T'
               || DOKUM.BL_VORD:='N'
               ?};
               _put:=1
            ?};
            {? DOKUM.INFO_END=''
            || DOKUM.INFO_END:='N';
               _put:=1
            ?};
            {? DOKUM.BATCH=''
            || DOKUM.BATCH:='N';
               _put:=1
            ?};
            {? DOKUM.KSEF2SGN=''
            || DOKUM.KSEF2SGN:='N'
            ?};
            {? _put
            || __lmod+=1;
               DOKUM.put()
            ?};
            DOKUM.next()
         !}
      ?};
      _msk.next()
   !}
?};
DOKUM.trig_on('*','*');
DOKUM.cntx_pop(); DOKUMZ.cntx_pop(); DOKUMZA.cntx_pop();
__UPG.msg('Zaktualizowano załączniki.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\DOKUM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnienie nowych pól w tabeli DOKUM oraz brakujących wartości DOKUM.DOKUMI - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja załączników, danych dokumentów Businesslink.'


\spr_amor
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodanie stałej systemu do FINFO
::----------------------------------------------------------------------------------------------------------------------
exec('add_acr','#stalesys','SPR_AMOR','Wyłączenie kontroli dekretacji amor. w poprzednim okresie',
     'T','T','T','FINFO','FST');
_wyn:=exec('kst_war_add','#stalesys','FINFO.SPR_AMOR','\'N\'',date(0,0,0));
{? _wyn
|| __UPG.msg('Zaktualizowano stałe systemu.')
|| __UPG.msg('Stałe systemu są już aktualne.')
?};
1


\spr_amor_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodanie stałej systemu do FINFO - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie SPR_AMOR do stałych systemu.'


\deleg_seod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Formuły uzupełniające dane na potrzeby delegacji SEOD
::----------------------------------------------------------------------------------------------------------------------
:: Typy delegacji
_dk:=exec('ETYPY_deleg_add','portal_seod','K');
_dz:=exec('ETYPY_deleg_add','portal_seod','Z');
__UPG.msg('Utworzenie typu delegacji krajowej: %1'[{? _dk || 'Tak' || 'Nie' ?}]);
__UPG.msg('Utworzenie typu delegacji zagranicznej: %1'[{? _dz || 'Tak' || 'Nie' ?}]);

:: Środki transportu
INNESRTR.cntx_psh();
INNESRTR.index('NAZWA');
INNESRTR.prefix();
INNESRTR.for_each("{? INNESRTR.DODAT='' || INNESRTR.DODAT:='N'; INNESRTR.put() ?}");
INNESRTR.cntx_pop();
__UPG.msg('Uzupełniono pole INNESRTR.DODAT');
_add_st:="
   _naz:=_a;
   INNESRTR.cntx_psh();
   INNESRTR.index('NAZWA'); INNESRTR.prefix();
   {? INNESRTR.find_key(_naz,)
   || INNESRTR.DODAT:='T';
      INNESRTR.put();
      __UPG.msg('Uaktualniono środek transportu: %1'[_naz])
   || INNESRTR.blank(1);
      INNESRTR.NAZWA:=_naz;
      INNESRTR.DODAT:='T';
      INNESRTR.add();
      __UPG.msg('Dodano środek transportu: %1'[_naz])
   ?};
   INNESRTR.cntx_pop()
";
_add_st('Samochód służbowy');
_add_st('Samochód prywatny');

:: Nowe komunikaty
_obj:=exec('portal_hr_obj','upgrade_2226');

:: PORTAL_SEOD_CFG
_obj.add('PORTAL_SEOD_CFG','Usuwanie','SLO','cseod_BusinessTripReasonDelete',
   'Cel delegacji',$"exec('SLO_ok','portal_seod')",,'F'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','SLO','cseod_BusinessTripReasonModify',
   'Cel delegacji',$"exec('SLO_ok','portal_seod')",,'F'
);
_obj.add('PORTAL_SEOD_CFG','Usuwanie','INNESRTR','cseod_BusinessTripVehicleDelete',
   'Środki transportu',,,'F'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','INNESRTR','cseod_BusinessTripVehicleModify',
   'Środki transportu',,,'F'
);
_obj.add('PORTAL_SEOD_CFG','Usuwanie','ETYPWYD','cseod_CostCenterDelete',
   'Typy wydatków',,,'F'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','ETYPWYD','cseod_CostCategoryModify',
   'Typy wydatków',,,'F'
);
:: PORTAL_SEOD
_obj.add('PORTAL_SEOD','Odbieranie','EDOKUM','cseod_BusinessTripGet',
   'Delegacja',$"exec('EDOKUM_seod','portal_seod')",'cseod_AcceptanceHeaderModify','T'
);
_obj.add('PORTAL_SEOD','Wysyłanie','EDOKUM','cseod_BusinessTripModify',
   'Delegacja',$"exec('EDOKUM_seod','portal_seod')",'cseod_BusinessTripGet','T'
);
_obj.add('PORTAL_SEOD','Usuwanie','EDOKUM','cseod_BusinessTripDelete',
   'Delegacja',$"exec('EDOKUM_seod','portal_seod')",'cseod_BusinessTripModify','T'
);

_obj.add('PORTAL_SEOD','Odbieranie','EDOK_ZAL','cseod_PurchaseDocumentGet',
   'Wydatki delegacji',$"exec('EDOK_ZAL_ok','portal_seod')",'cseod_BusinessTripGet','T'
);
_obj.add('PORTAL_SEOD','Usuwanie','EDOK_ZAL','cseod_PurchaseDocumentDelete',
   'Wydatki delegacji',$"exec('EDOK_ZAL_ok','portal_seod')",'cseod_PurchaseDocumentDelete','T'
);
1


\deleg_seod_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Formuły uzupełniające dane na potrzeby delegacji SEOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie danych na potrzeby delegacji SEOD:\n'
'- dodanie typów delegacji'
'- uzupełnienie pola INNESRTR.DODAT\n'
'- dodanie innych środków transportu: samochodu służbowego i prywatnego\n'
'- dodanie obsługi nowych komunikatów API\n'


\USERSF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja użytkowników w firmach
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
USERSF.trig_off('*','*');
USERSF.cntx_psh();
USERSF.prefix();
USERSF.for_each("
   __lrec+=1;
   _put:=0;
   {? USERSF.BLK_SYNC='' || USERSF.BLK_SYNC:='N'; _put:=1 ?};
   {? _put
   || {? USERSF.put(1) || __lmod+=1 ?}
   ?}",
   1
);
USERSF.cntx_pop();
USERSF.trig_on('*','*');
__UPG.msg('Zaktualizowano użytkowników w firmach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\USERSF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja użytkowników w firmach - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja użytkowników w firmach'


\PORTALU
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja uprawnień do funkcjonalności
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PORTALU.trig_off('*','*');
PORTALU.cntx_psh();
PORTALU.prefix();
PORTALU.for_each("
   __lrec+=1;
   _put:=0;
   {? PORTALU.TYPE='' || PORTALU.TYPE:='U'; _put:=1 ?};
   {? _put
   || {? PORTALU.put(1) || __lmod+=1 ?}
   ?}",
   1
);
PORTALU.cntx_pop();
PORTALU.trig_on('*','*');
__UPG.msg('Zaktualizowano uprawnienia do funkcjonalności.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\PORTALU_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja uprawnień do funkcjonalności - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień do funkcjonalności'


\TOPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli TOPER - operacje procesów technologicznych
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=TOPER.names();
_msk.clear();
TOPER.trig_off('*','*');
TOPER.cntx_psh();
{? _msk.first()
|| {!
   |? TOPER.use(_msk.NAME);
      TOPER.prefix();
      TOPER.for_each("
         _put:=0;
         __lrec+=1;
         {? TOPER.WYK_FAST=''
         || TOPER.WYK_FAST:='N';
            _put:=1
         ?};
         {? TOPER.NO_START=''
         || TOPER.NO_START:='N';
            _put:=1
         ?};
         {? _put || {? TOPER.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
TOPER.cntx_pop();
TOPER.trig_on('*','*');
__UPG.msg('Zaktualizowano operacje procesów technologicznych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\TOPER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [23.25]
:: OPIS: Uzupełnia pola tabeli TOPER - operacje procesów technologicznych - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla operacji procesów technologicznych'


\TTOPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli TTOPER - taryfikator operacji
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
TTOPER.trig_off('*','*');
TTOPER.cntx_psh();
TTOPER.prefix();
{? TTOPER.first()
|| {!
   |?
      _put:=0;
      __lrec+=1;
      {? TTOPER.BRYG=''
      || TTOPER.BRYG:='N';
         _put:=1
      ?};
      {? TTOPER.PL_GRP=''
      || TTOPER.PL_GRP:='N';
         _put:=1
      ?};
      {? TTOPER.WYK_FAST=''
      || TTOPER.WYK_FAST:='N';
         _put:=1
      ?};
      {? TTOPER.NO_START=''
      || TTOPER.NO_START:='N';
         _put:=1
      ?};
      {? _put || {? TTOPER.put(1) || __lmod+=1 ?} ?};
      TTOPER.next()
   !}
?};
TTOPER.cntx_pop();
TTOPER.trig_on('*','*');
__UPG.msg('Zaktualizowano taryfikatory operacji.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\TTOPER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Uzupełnia pola tabeli TOPER - taryfikator operacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla taryfikatorów operacji'


\id_sync
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów
::----------------------------------------------------------------------------------------------------------------------
_init:=exec('init','sync_id');
_result:=_init.RESULT;
_args:=_init.ARGS;
{? _result
|| __UPG.msg('Zaktualizowano definicje identyfikatorów rekordów.')
|| __UPG.msg('Wystąpił problem podczas aktualizacji definicji identyfikorów rekordów %1 - %2 do systemu %3.'
      [_args.KOD,_args.OPIS,_args.SYSTEM])
?};
_result


\id_sync_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie definicji identyfikatorów rekordów'


\mechanizm_importow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Import zmian w definicjach mechanizmu importów.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
:: Zastaw oddzielonych przecinkami identyfikatorów mechanizmów importu, np. 'ZWS_IMPORTY_PKD,ZWS_IMPORTY_PPL':
_domains:='ZWS_IMPORTY_PKD,ZWS_IMPORTY_PPL,ZWS_IMPORTY_PPK';
_set:=spli_str(_domains,',');
_names:='';
_len:=obj_len(_set);
{! _ii:=1.._len
|! {? _set[_ii]*'ZWS_IMPORTY_'
   || _names+=exec('name','#b_domain',gsub(_set[_ii],'ZWS_IMPORTY_',''));
      {? _ii<_len
      || _names+=', '
      ?}
   ?}
!};
{? FUN.ask('Czy uruchomić import z nadpisywaniem mechanizmów importu %1 %2?'@
           [{? _len>1 || 'dziedzin'@ || 'dziedziny'@ ?},_names])
|| exec('import_init','#excel_imex',_set,,1);
   {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['mechanizm_importow'])
   || __UPG.msg('Zaktualizowano definicje mechanizmu importów.');
      _result:=1
   || _result:=-1
   ?}
|| _result:=-1
?};
_result


\mechanizm_importow_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Import zmian w definicjach mechanizmu importów - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import zmian w definicjach mechanizmu importów związany z emisją poprawek:\n'
' * ER/WRT/XP/23.25/2306/0002: kopia błędu ER/WRT/XP/22.26/2305/0007 : Błąd importu Nieobecności i dokumentacji'
' - zawsze wymagane pole KDSW nawet dla niewymaganych 350\n'
' * ER/WRT/XP/23.25/2307/0002: kopia błędu ER/WRT/XP/22.26/2303/0026 - Błąd unikalności importu '
'Uczestników PPK ppk_ucz.csv\n'
' * ER/WRT/XP/23.25/2308/0041: kopia błędu ER/WRT/XP/22.26/2304/0005 - Import początkowy osób\n'
' * ER/WRT/XP/23.25/2308/0042: kopia błędu ER/WRT/XP/22.26/2308/0021 - Brak możliwości zaimportowania dzieci\n'
' * ER/WRT/XP/23.25/2308/0028: kopia błędu ER/WRT/XP/22.26/2303/0020 - Nieścisłość w dokumentacji Importu'
' Nagłówki list płac - nag_lis.csv Typ listy\n'
' * ER/WRT/XP/23.25/2308/0066: kopia błędu ER/WRT/XP/22.26/2308/0009 - Badania lekarskie importują się bez wyniku'
' orzeczenia i daty następnego badania dla podanego pełnego wyniku badania z opisem\n'
' * PR/WRT/XP/23.25/2307/0008 - Modyfikacja importu danych przez mechanizmy EDI\n'
' * ER/WRT/XP/23.25/2311/0055 - Błąd importu PPK_POD dla poprawnie wypełnionego pliku ppk_pod.csv '
'to samo co 22.26 ER/WRT/XP/22.26/2303/0052\n'
' * ER/WRT/XP/23.25/2310/0132 - Brak możliwości importu składek PPK przy imporcie rachunków umów zleceń RH\n'
' * ER/WRT/XP/23.25/2311/0065 - EDI - import staz.csv niezgodny z dokumentacją\n'


\OP_AN_OPIS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Aktualizacja pola AN_OPIS tabeli OP.
::----------------------------------------------------------------------------------------------------------------------
OP.cntx_psh();
ROK_F.cntx_psh();ROK_F.index('KODG');ROK_F.prefix();
_result:=1;
_licznik:=0;
OP.trig_off('*','*');
{? ROK_F.first()
|| {!
   |? rokpoz:=ROK_F.ref();
      SSTALE.AR:=ROK_F.ref();
      exec('set_var','!fks_ksr_zbob');
      OP.use('operac'+ROK_F.KOD);
      OP.index('KON_O');
      OP.prefix();
      {? OP.first()
      ||
         {!
         |? opis_poz:='';odd_ksn:=0;
            ROK_F.cntx_psh(); OKRO_F.cntx_psh();
            exec('op_konta','konto',OP.AN,null,2);
            exec('ka_opis','konto');
            OP.AN_OPIS:=opis_poz;
            {? OP.put() || _licznik+=1 ?};
            OKRO_F.cntx_pop(); ROK_F.cntx_pop();
            &opis_poz; &odd_ksn;
            OP.next()
         !}
      ?};
      ROK_F.next()
   !}
?};
OP.trig_on('*','*');
__UPG.msg('Zaktualizowano opis konta dla ' + $_licznik + ' rozrachunków.');
ROK_F.cntx_pop();
OP.cntx_pop();
_result


\OP_AN_OPIS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Aktualizacja danych w polu AN_OPIS tabeli OP.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych w polu AN_OPIS tabeli OP.'


\zws_par_blzd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Formuła dodaje czynność ZWS_PAR_BLZD do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'ZWS_PAR_LTZD'";
_uid:='ZWS_PAR_BLZD';
_res:=1;
_res:=exec('actionRoleAction','upgrade',_uid,_cr);
{? _res<>0
|| _rola:='Administrator';
   _ref:=exec('ref','#b_role',_rola);
   {? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
   || __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_uid]);
      _res:=-1
   || {? exec('add_actrol_one','#b_role',_rola,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_rola])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_rola]);
         _res:=0
      ?}
   ?}
?};
_res


\zws_par_blzd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Formuła dodaje czynność ZWS_PAR_BLZD do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZWS_PAR_BLZD - Typy zamówień dostaw dla Businesslink"'


\f_kontrolna_roz_nad
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodaje formułę kontrolną, która przy księgowaniu sprawdzi czy nie jest płacona wyższa kwota niż istniejące
::       zobowiązanie/należność
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G','ROZ_NAD',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:='ROZ_NAD';
   FORMULA.NAZWA:='Formuła dla pozycji sprawdzająca czy nie jest płacona wyższa kwota niż istniejący rozrachunek';
   FORMULA.FORMULA:='exec(\'spr_roz_nad\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=2
?};
FORMULA.cntx_pop();
{? _ret=2
|| __UPG.msg('Formuła kontrolna ROZ_NAD jest już dodana w systemie.')
|? _ret=1
|| __UPG.msg('Dodano formułę kontrolną ROZ_NAD.')
|| __UPG.msg('Dodanie formuły kontrolnej ROZ_NAD nie powiodło się.')
?};
_ret


\f_kontrolna_roz_nad_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodaje formułę kontrolną, która przy księgowaniu sprawdzi czy nie jest płacona wyższa kwota niż istniejące
::       zobowiązanie/należność - opis formuły
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły kontrolnej dla pozycji, która przy księgowaniu sprawdzi czy nie jest płacona wyższa kwota niż '
'istniejące zobowiązanie/należność'


\FPACZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Wypełnia pola tabeli FPACZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
FPACZ.cntx_psh();
FPACZ.index('ROK');
FPACZ.prefix();
_res:=FPACZ.for_each("
   __lrec+=1;
   _put:=0;
   {? FPACZ.STAT_GEN=''
   ||
      FPACZ.STAT_GEN:=exec('stat_gen','umowy_faktury',0);
      _put:=1
   ?};
   {? _put
   ||
      {? FPACZ.put() || __lmod+=1 ?}
   ?}
");
FPACZ.cntx_pop();
__UPG.msg('Zaktualizowano paczki faktur.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\FPACZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Wypełnia pola tabeli FPACZ - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja paczek faktur'


\ilmg2ZAM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Wypełnia pola ILMG w pozycjach zamówień
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
_zam:=tab_tmp(1,'SQL','STRING[16]','','ILMG','REAL','','RODZ','STRING[1]','');

__lrec:=__lmod:=0;
OKR.cntx_psh();
ND.cntx_psh();
DK.cntx_psh();
ODDZ.cntx_psh();
ODDZ.index('KOD');
ODDZ.prefix();
{? ODDZ.first()
|| {!
   |? OKR.index('MC');
      OKR.prefix(REF.FIRMA,1);
      {? OKR.first()
      || {!
         |? exec('mag_open','open_tab',ODDZ.KOD,form(OKR.ROK-2000,-2,0,'99'));
            ND.index('NAGDT');
            ND.prefix();
            {? ND.first() & ND.find_tab('first','Z',,'=','T')
            || {!
               |? DK.index('DOKMAG');
                  DK.prefix(ND.ref());
                  {? DK.first()
                  || {!
                     |? {? DK.ZAM_RP<>''
                        || _poz:=_ptw:='';
                           _rodz:='';
                           _zre:=0;
                           {? (5+DK.ZAM_RP)='zdhip'
                           || _rodz:='D';
                              _poz:=exec('FindAndGet','#table',ZD_RP,DK.ZAM_RP,,"$ZD_POZ",'');
                              _ptw:=exec('FindAndGet','#table',ZD_RP,DK.ZAM_RP,,"form(ZDP_POZ)",'');
                              _zre:=exec('FindAndGet','#table',ZD_RP,DK.ZAM_RP,,"IL_ZRE",0)
                           |? (5+DK.ZAM_RP)='zkhip'
                           || _rodz:='Z';
                              _poz:=exec('FindAndGet','#table',ZK_RP,DK.ZAM_RP,,"$P",'');
                              _zre:=exec('FindAndGet','#table',ZK_RP,DK.ZAM_RP,,"ILR",0)
                           ?};
                           {? _poz<>''
                           || _zam.clear();
                              _zam.prefix(_poz);
                              {? _zam.first()
                              || _zam.ILMG+=_zre;
                                 _zam.put(1)
                              || _zam.blank();
                                 _zam.SQL:=_poz;
                                 _zam.ILMG:=_zre;
                                 _zam.RODZ:=_rodz;
                                 _zam.add(1)
                              ?}
                           ?};
                           {? _ptw<>''
                           || _zam.clear();
                              _zam.prefix(_ptw);
                              {? _zam.first()
                              || _zam.ILMG+=_zre;
                                 _zam.put(1)
                              || _zam.blank();
                                 _zam.SQL:=_ptw;
                                 _zam.ILMG:=_zre;
                                 _zam.RODZ:='P';
                                 _zam.add(1)
                              ?}
                           ?}
                        ?};
                        DK.next()
                     !}
                  ?};
                  ND.find_tab('next','Z',,'=','T')
               !}
            ?};
            OKR.next()
         !}
      ?};
      ODDZ.next()
   !}
?};
_bufil:=EANX.BUFIL;
ZD_POZ.trig_off('*','*');
ZDP_POZ.trig_off('*','*');
ZK_P.trig_off('*','*');
do();
_zam.clear();
{? _zam.first()
|| {!
   |? __lrec+=1;
      EANX.BUFIL:=_zam.ILMG;
      {? _zam.RODZ='Z'
      || exec('FindAndGet','#table',ZK_P,_zam.SQL,,
          "{? ILMG<>EANX.BUFIL || ILMG:=EANX.BUFIL; {? put(1) || __lmod+=1 ?} ?}",0)
      |? _zam.RODZ='D'
      || exec('FindAndGet','#table',ZD_POZ,_zam.SQL,,
          "{? ILMG<>EANX.BUFIL || ILMG:=EANX.BUFIL; {? put(1) || __lmod+=1 ?} ?}",0)
      |? _zam.RODZ='P'
      || exec('FindAndGet','#table',ZDP_POZ,_zam.SQL,,
          "{? ILMG<>EANX.BUFIL || ILMG:=EANX.BUFIL; {? put(1) || __lmod+=1 ?} ?}",0)
      ?};
      _zam.next()
   !}
?};
end();
ZD_POZ.trig_on('*','*');
ZDP_POZ.trig_on('*','*');
ZK_P.trig_on('*','*');

EANX.BUFIL:=_bufil;
ODDZ.cntx_pop();
OKR.cntx_pop();
ND.cntx_pop();
DK.cntx_pop();
__UPG.msg('Zaktualizowano informacje o ilościach zrealizowanych z zamówień.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
obj_del(_zam);
_res


\ilmg2ZAM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Wypełnia pola ILMG w pozycjach zamówień - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja informacji o ilościach zrealizowanych z zamówień'


\update_ZLIM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizacja pól tabeli ZLIM na podstawie pozycji dokumentów magazynowych DK
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('update_ildok4ZLIM','zl_nlimit',0);
__UPG.msg('Zaktualizowano nowe pola limitów surowców zleceń.');
_result


\update_ZLIM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizacja pól tabeli ZLIM na podstawie pozycji dokumentów magazynowych DK - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli limitów surowców zleceń.'


\zws_par_blzk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Formuła dodaje czynność ZWS_PAR_BLZK do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'ZWS_PAR_LTZK'";
_uid:='ZWS_PAR_BLZK';
_res:=1;
_res:=exec('actionRoleAction','upgrade',_uid,_cr);
{? _res<>0
|| _rola:='Administrator';
   _ref:=exec('ref','#b_role',_rola);
   {? _ref=null()
:: To nie jest nowa rola. Jeżeli została usunięta, to uszanujmy to i nie aktualizujmy jej.
   || __UPG.msg('Nie znaleziono roli ''%1'' - czynność ''%2'' nie zostanie z nią powiązana.' [_rola,_uid]);
      _res:=-1
   || {? exec('add_actrol_one','#b_role',_rola,_uid)
      || __UPG.msg('Czynność %1 przypisano do roli "%2".'[_uid,_rola])
      || __UPG.msg('Przypisanie czynności %1 do roli "%2" nie powiodło się.'[_uid,_rola]);
         _res:=0
      ?}
   ?}
?};
_res


\zws_par_blzk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Formuła dodaje czynność ZWS_PAR_BLZK do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZWS_PAR_BLZK - Typy zamówień sprzedaży dla Businesslink"'


\rest_acc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [23.25]
:: OPIS: Dodanie stałej systemu REST_ACC do XINFO
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('add_acr','#stalesys','REST_ACC','Przelew zaakceptowany po przywróceniu z archiwum',
     'T','N','T','XINFO','HBN');
_wyn:=exec('kst_war_add','#stalesys','XINFO.REST_ACC','\'T\'',date(0,0,0));
{? _wyn
|| __UPG.msg('Dodano informacje o statusie przelewu po przywróceniu z archiwum.')
?};
1


\rest_acc_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WHAN [23.25]
:: OPIS: Dodanie stałej systemu REST_ACC do XINFO - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu - czy przelew zaakceptowany po przywróceniu z archiwum'


\base_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Działania związane z obsługą identyfikatora bazy danych
::----------------------------------------------------------------------------------------------------------------------
_res:=exec('base_id_manage','#system',1);
_res


\base_id_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Działania związane z obsługą identyfikatora bazy danych - opis
::----------------------------------------------------------------------------------------------------------------------
exec('base_id_info_transfer','#system')


\porsloit_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [23.25]
:: OPIS: Czyści błędne powiązania systemowych słowników portalowych
::----------------------------------------------------------------------------------------------------------------------
_TAB:=tab_tmp(,'UIDREF','STRING[48]','Uidref rekordu');

:: zalozenie transakcji
_mydo:=do_state()=0;
{? _mydo || do() ?};

:: dezaktywujemy wszystkie metody API dla wskazanego przeznaczenia danych i tabeli
_ok:=exec('toggle_activ_4tab','sync_mwa','PORTAL_CFG','PORSLO','N',_TAB,'add')>0 &
     exec('toggle_activ_4tab','sync_mwa','PORTAL_CFG','PORSLOIT','N',_TAB,'add')>0 &
     exec('toggle_activ_4tab','sync_mwa','PORTAL_HR_CFG','PORSLO','N',_TAB,'add')>0 &
     exec('toggle_activ_4tab','sync_mwa','PORTAL_HR_CFG','PORSLOIT','N',_TAB,'add')>0;

:: zapamiętujemy do pliku tylko te rekordy SYNC_MWA, które dezaktywowaliśmy
{? _ok
|| _file:=fopen('~upgrade_porslo.dfg','uw',1,0,1);
   {? _file.is_open()
   || _sep:=';';
      _TAB.cntx_psh();
      _TAB.prefix();
      {? _TAB.first()
      || {!
         |? _line:=_TAB.UIDREF+_sep;
            _file.fwrite(_line);
            _TAB.next()
         !}
      ?};
      _TAB.cntx_pop();
      _file.fclose();
      __UPG.msg('Zapamiętano listę dezaktywowanych rekordów tabeli SYNC_MWA.')
   || _ok:=0
   ?}
?};

:: modyfikujemy definicję wymiany danych dla tabel PORSLO i PORSLOIT
_size:=0;
_l_mod:=0;
{? _ok
|| SYNC_DEF.cntx_psh();
   SYNC_DEF.index('PDTAB');
   SYNC_DEF.prefix('PORSLO');
   {? SYNC_DEF.first()
   || _size+=SYNC_DEF.size();
      {!
      |? SYNC_DEF.WAR_FORM:='';
         SYNC_DEF.FIR:='F';
         _l_mod+=SYNC_DEF.put(1);
         SYNC_DEF.next()
      !}
   ?};
   SYNC_DEF.prefix('PORSLOIT');
   {? SYNC_DEF.first()
   || _size+=SYNC_DEF.size();
      {!
      |? SYNC_DEF.WAR_FORM:='';
         SYNC_DEF.FIR:='F';
         _l_mod+=SYNC_DEF.put(1);
         SYNC_DEF.next()
      !}
   ?};
   SYNC_DEF.cntx_pop()
?};

_result:=_ok & (_l_mod=_size);

:: jeżeli coś poszło nie tak, to wycofujemy wszystkie zmiany
{? ~_result  || undo() ?};
:: kończymy transakcję
{? _mydo || end() ?};

{? _result>0
||
:: przepisanie pozostawionych rekordów PORSLOIT z PORSLO.FIRMA<>null do PORSLO.FIRMA=null
:: wraz z wypełnieniem pola PORSLOIT.FIRMA
   _getPorslo:="
      PORSLO.prefix(null,_a,_b,_c);
      {? PORSLO.first()
      || _result:=PORSLO.ref()
      || null()
      ?}
   ";

   PORSLO.cntx_psh();
   PORSLO.index('FIELD');
   PORSLO.prefix();
   PORSLOIT.cntx_psh();
   PORSLOIT.index('SLOVAL');
   PORSLOIT.prefix();
   {? PORSLOIT.f_active() || PORSLOIT.f_clear() ?};
   PORSLOIT.f_set(,'join PORSLO','PORSLO.FIRMA is not null');
   _f_size:=PORSLOIT.f_size();
   _l_mod:=0;
   {? PORSLOIT.f_first()
   || FUN.prg_start(_f_size,'Przeniesienie powiązania z firmą z PORSLO.FIRMA do PORSLOIT.FIRMA.');
      {!
      |? _firma:=PORSLOIT.PORSLO().FIRMA;
         _porslo:=_getPorslo(PORSLO.FIELD,PORSLO.TYPE,PORSLO.CODE);
         {? _porslo<>null()
         || PORSLOIT.PORSLO:=_porslo;
            PORSLOIT.FIRMA:=_firma;
            _l_mod+=PORSLOIT.put()
         ?};
         FUN.prg_next();
         PORSLOIT.f_next()
      !};
      FUN.prg_stop()
   ?};
   PORSLOIT.cntx_pop();
   PORSLO.cntx_pop();
   {? _l_mod>0
   || _opis:="$_a+{? _a=1 || ' rekordu' || ' rekordów' ?}";
      __UPG.msg('Przeniesiono powiązania z firmą z PORSLO.FIRMA do PORSLOIT.FIRMA dla %1.'[_opis(_l_mod)])
   || __UPG.msg('Nie znaleziono rekordów tabeli PORSLOIT, które wymagałyby przeniesienia powiązania z firmą.')
   ?};

:: czyszczenie błędnych powiązań
   _PORSLO:=tab_tmp(1,
      'FIELD','STRING[255]','Lista wartości - nazwa pola',
      'CODE','STRING[100]','Kod dynamicznej listy wartości',
      'TYPE','STRING[100]','Typ listy wartości',
      'OPIS','STRING[255]','Opis słownika',
      'SYSTEM','STRING[1]','Element systemowy [T/N]'
   );
   KOMM.init();
   FIRMA.cntx_psh();
   PORSLO.cntx_psh();
   PORSLO.index('FIELD');
   PORSLO.prefix();
   {? PORSLO.first()
   || FUN.prg_start(PORSLO.size(),'Usuwanie błędnych powiązań dla systemowych słowników portalowych.');
      {!
      |? _del:=0;
         {? PORSLO.SYSTEM='T'
         || _del:={? PORSLO.FIELD=exec('customField','portal_slowniki')
                  || exec('porsloit_delete4','portal_slowniki',PORSLO.ref()) & PORSLO.count()=0
                  || PORSLO.count()=0
                  ?}
         || _del:=PORSLO.count()=0 & PORSLO.FIRMA<>null();
            _PORSLO.FIELD:=PORSLO.FIELD;
            _PORSLO.TYPE:=PORSLO.TYPE;
            _PORSLO.CODE:=PORSLO.CODE;
            _PORSLO.OPIS:=PORSLO.OPIS;
            _PORSLO.SYSTEM:=PORSLO.SYSTEM;
            _PORSLO.add(1)
         ?};
         FUN.prg_next();
         {? _del
         || PORSLO.del()
         || PORSLO.GLOBAL:='N';
            PORSLO.put();
            PORSLO.next()
         ?}
      !};
      FUN.prg_stop();
      _PORSLO.prefix();
::    dla pewności zakładamy (jeżeli nie było) wpisy nullowe dla słowników nie systemowych (wstępnie jako lokalne)
      {? _PORSLO.first()
      || {!
         |? exec('porslo_add','portal_slowniki',
                  _PORSLO.FIELD,_PORSLO.TYPE,_PORSLO.CODE,_PORSLO.OPIS,null(),_PORSLO.SYSTEM,'N','N');
            _PORSLO.next()
         !}
      ?}
   ?};
   PORSLO.cntx_pop();
   FIRMA.cntx_pop();
   __UPG.msg('Usunięto błędne powiązania dla systemowych słowników portalowych.')
|| _result:=-1;
   __UPG.msg('Nie powiodła się modyfikacja definicji wymiany danych dla tabel PORSLO oraz PORSLOIT.')
?};
_result


\porsloit_clear_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [23.25]
:: OPIS: Czyści błędne powiązania systemowych słowników portalowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie błędnych powiązań dla systemowych słowników portalowych.'


\porslo_global
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [23.25]
:: OPIS: Zaznaczenie wybranych słowników jako globalnych
::----------------------------------------------------------------------------------------------------------------------
_conf:=XINFO.POR_CONF;
exec('czytaj','#stalesys',,XINFO,'POR_CONF');
_conf==XINFO.POR_CONF;

_l_mod:=0;
PORSLO.cntx_psh();
PORSLO.index('SYSTEM');
PORSLO.prefix();
{? PORSLO.first()
|| _customField:=exec('customField','portal_slowniki');
   _sql:="select * from PORSLOIT join PORSLO using(PORSLO.REFERENCE, PORSLOIT.PORSLO)
          where PORSLO.FIRMA is not null and PORSLO.FIELD=':_a' and PORSLO.CODE=':_b'"+'';
   FUN.prg_start(PORSLO.size(),'Analiza słowników użytkownika');
   {!
   |? {? (PORSLO.FIELD<>_customField | exec('oCustomGlobal','portal_slowniki').exists(PORSLO.CODE))
      || PORSLO.GLOBAL:='T'
      ?};
      _l_mod+=PORSLO.put(1);
      FUN.prg_next();
      PORSLO.next()
   !};
   FUN.prg_stop()
?};
PORSLO.cntx_pop();
_opis:=$_l_mod+{? _l_mod=1 || ' rekordu' || ' rekordów' ?};
__UPG.msg('Ustawiono znacznik "GLOBAL" dla %1 tabeli PORSLO.'[_opis]);

:: modyfikujemy definicję wymiany danych dla tabel PORSLO i PORSLOIT
_mydo:=do_state()=0;
{? _mydo || do() ?};
_size:=0;
_l_mod:=0;

SYNC_DEF.cntx_psh();
SYNC_DEF.index('PDTAB');
SYNC_DEF.prefix('PORSLO');
{? SYNC_DEF.first()
|| _size+=SYNC_DEF.size();
   {!
   |? SYNC_DEF.WAR_FORM:=''+"exec('PORSLO_ok','portal_slowniki',_a,_b,_c)";
      SYNC_DEF.FIR:='F';
      _l_mod+=SYNC_DEF.put(1);
      SYNC_DEF.next()
   !}
?};
SYNC_DEF.prefix('PORSLOIT');
{? SYNC_DEF.first()
|| _size+=SYNC_DEF.size();
   {!
   |? SYNC_DEF.WAR_FORM:=''+"exec('PORSLOIT_ok','portal_slowniki',_a,_b,_c)";
      SYNC_DEF.FIR:='F';
      _l_mod+=SYNC_DEF.put(1);
      SYNC_DEF.next()
   !}
?};
SYNC_DEF.cntx_pop();

_result:=(_l_mod=_size);

:: jeżeli coś poszło nie tak, to wycofujemy wszystkie zmiany
{? ~_result  || undo() ?};
:: kończymy transakcję
{? _mydo || end() ?};

{? _result
||
:: putujemy wszystkie PORSLO i PORSLOIT w celu ponownego wysłania ich na portal
   PORSLO.trig_off('*','*');
   PORSLOIT.trig_off('*','*');

   PORSLO.cntx_psh();
   PORSLO.prefix();
   PORSLO.for_each("PORSLO.put(,1)",1);
   PORSLO.cntx_pop();
   PORSLOIT.cntx_psh();
   PORSLOIT.prefix();
   PORSLOIT.for_each("PORSLOIT.put(,1)",1);
   PORSLOIT.cntx_pop();

   PORSLOIT.trig_on('*','*');
   PORSLO.trig_on('*','*');

:: aktywujemy wyłączone wcześniej w zadaniu 'porsloit_clear' metody API
   _TAB:=tab_tmp(,'UIDREF','STRING[48]','Uidref rekordu');
   _fileName:='~upgrade_porslo.dfg';
   {? fexists(_fileName,1)=1
   || _TAB.import(_fileName,0,0,,'UTF-8,noheader',,'UIDREF',,1,);
      ferase(_fileName,1)
   ?};

   _ok:=exec('toggle_activ_4tab','sync_mwa','PORTAL_HR_CFG','PORSLO','T',_TAB,'check')>0 &
        exec('toggle_activ_4tab','sync_mwa','PORTAL_HR_CFG','PORSLOIT','T',_TAB,'check')>0 &
        exec('toggle_activ_4tab','sync_mwa','PORTAL_CFG','PORSLO','T',_TAB,'check')>0 &
        exec('toggle_activ_4tab','sync_mwa','PORTAL_CFG','PORSLOIT','T',_TAB,'check')>0;
   {? _ok
   || __UPG.msg('Przywrócono status aktywności dla listy zapamiętanych rekordów tabeli SYNC_MWA.')
   || __UPG.msg('Nie udało się przywrócić aktywacji dla listy zapamiętanych rekordów tabeli SYNC_MWA.')
   ?}

|| _result:=-1;
   __UPG.msg('Nie powiodła się modyfikacja definicji wymiany danych dla tabel PORSLO oraz PORSLOIT.')
?};

_result


\porslo_global_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [23.25]
:: OPIS: Zaznaczenie wybranych słowników jako globalnych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Zaznaczenie wybranych słowników jako globalnych.'


\porslo_reinit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [23.25]
:: OPIS: Ponowna aktualizacja elementów słowników wykorzystywanych na portalu.
::----------------------------------------------------------------------------------------------------------------------
exec('init','portal_slowniki');
__UPG.msg('Zaktualizowano zawartość słowników portalowych.');
1


\porslo_reinit_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [23.25]
:: OPIS: Ponowna aktualizacja elementów słowników wykorzystywanych na portalu - opis.
::----------------------------------------------------------------------------------------------------------------------
'Ponowna aktualizacja elementów słowników wykorzystywanych na portalu.'


\porslo_global_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Zaznaczenie wybranych słowników jako globalnych - informacja o konieczności weryfikacji listy
::----------------------------------------------------------------------------------------------------------------------
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['porslo_global_info'])
|| _result:=1
|| _result:=-1
?};
_result


\porslo_global_info_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [23.25]
:: OPIS: Zaznaczenie wybranych słowników jako globalnych - informacja o konieczności weryfikacji listy - opis.
::----------------------------------------------------------------------------------------------------------------------
'Wprowadzono nowy sposób obsługi słowników portalowych.\n'
'Do tabeli PORSLO dodano pole GLOBAL określające, czy słownik obsługujący uniwersalną listę wartości\n'
'jest słownikiem globalnym (wspólnym dla wszystkich firm).\n'
'Mechanizm dostępny jest w obszarze "Ustawienia i parametryzacja" w sekcji:\n'
' ● HR Portal->Słowniki\n\n'
'Po aktualizacji wersji i uruchomieniu formuł potransterowych automatycznych, należy zweryfikować\n'
'poprawność wpisów dla słowników użytkownika (nie systemowych).'


\POCZTA
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnia pola w tabeli POCZTA
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');

__lrec:=__lmod:=0;
_formula:="
   __lrec+=1;
   _put:=0;
   {? POCZTA.SENDINFO=''
   || POCZTA.SENDINFO:='N';
      _put:=1
   ?};
   {? _put || {? POCZTA.put(1) || __lmod+=1 ?} ?}
";

POCZTA.cntx_psh();
POCZTA.prefix();
_result:=POCZTA.for_each(_formula,1);
POCZTA.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano pola w kartotece wiadomości e-mail.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji kartoteki wiadomości e-mail.')
?};

VAR_DEL.delete('__lrec','__lmod');
_result


\POCZTA_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Uzupełnia pola w tabeli POCZTA - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki wiadomości e-mail.'


\OPAK_N
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizuje OPAK_N, OPAK_P
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=OPAK_N.names();
OPAK_N.cntx_psh();
OPAK_P.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |?
      OPAK_N.use(_msk.NAME);
      OPAK_N.prefix();
      OPAK_N.for_each("
         __lrec+=1;
         _put:=0;
         OPAK_P.use('opakp'+(OPAK_N.name()+3));
         OPAK_P.index('OPAK_N');
         OPAK_P.prefix(OPAK_N.ref());
         _loop:=OPAK_P.first();
         {!
         |? _loop
         |!
            {? OPAK_P.KH=null() | OPAK_P.KH_ODB=null()
            ||
               OPAK_P.KH:=OPAK_N.KH;
               OPAK_P.KH_ODB:=OPAK_N.KH_ODB;
               OPAK_P.put();
               _put:=1
            ?};
            _loop:=OPAK_P.next()
         !};
         {? _put || __lmod+=1 ?}
      ");
      _msk.next()
   !}
?};
OPAK_P.cntx_pop();
OPAK_N.cntx_pop();

__UPG.msg('Zaktualizowano pola w dokumentach opakowań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\OPAK_N_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Aktualizuje OPAK_N, OPAK_P - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól dokumentów opakowań.'


\DOK_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ, JK, PBS [23.25]
:: OPIS: Uzupełnienie w nagłówku dokumentu sum kwot netto, vat, brutto i obrotów WN/MA z pozycji VAT dokumentu.
::       Przepisanie wartości wybranych pól z tabeli DOK do tabel VPOZ i POZ
::       Do tabeli POZ:  DOK().DOK_REJ, DOK().HAN, DOK().REJ, DOK().KH_KRISO, DOK().O_KRISO, DOK().RB, DOK().RB_V,
::                       DOK().WYS, DOK().BANK, DOK().JORG, DOK().SP_PL, DOK().KRAJ, DOK().RVAT
::       Do tabeli VPOZ: DOK().DOK_REJ, DOK().HAN, DOK().ODD, DOK().REJ, DOK().KH_KRISO, DOK().O_KRISO, DOK().RB,
::                       DOK().RB_V, DOK().WYS, DOK().WAL, DOK().BANK, DOK().JORG, DOK().SP_PL, DOK().KRAJ
::       Dodatkowo dla tabeli POZ uzupełnienie pola POZ.KON_OPIS przechowującego opis analityczny konta.
::       Uzupełnienie pól: DOK.DSP_WPOZ, VPOZ.D, POZF.DT, DOK.GVKHSTAT, DOK.GVLISTAT
::----------------------------------------------------------------------------------------------------------------------
_lpoz:=_lvpoz:=_ldok:=_lpozf:=0;
_dok_put:=_sum_vpoz:=_sum_poz:=0;
DOK.cntx_psh(); DOK.trig_off('*','*');
VPOZ.cntx_psh(); VPOZ.trig_off('*','*');
POZF.cntx_psh(); POZF.trig_off('*','*');
POZ.cntx_psh(); POZ.trig_off('*','*');
ROK_F.index('KOD'); ROK_F.prefix();
OKRO_F.index('ROK');
{? ROK_F.first()
|| _kon:=exec('obj_kon','upgrade_2325');
   {!
   |? SSTALE.AR:=ROK_F.ref();
      OKRO_F.prefix(ROK_F.ref());
      {? OKRO_F.first()
      || {!
         |? SSTALE.AO:=OKRO_F.ref();
            exec('set_var','!fks_ksr_zbob');
            _maska:=ROK_F.KOD+form(OKRO_F.NR,-2);
            VPOZ.use('pozv'+_maska); VPOZ.index('VDOK');
            POZF.use('pozf'+_maska); POZF.index('DOK');
            POZ.use('pozy'+_maska); POZ.index('DOK');
            DOK.use('doku'+_maska); DOK.index('O_REJ'); DOK.prefix();
            {? DOK.first()
            || {!
               |? _sum_poz:=_sum_vpoz:=_dok_put:=_pozf_put:=0;
                  {? DOK.KSEF_ERR=''
                  || DOK.KSEF_ERR:='N';
                     _dok_put:=1
                  ?};
                  {? DOK.DSP_WPOZ=''
                  || DOK.DSP_WPOZ:='N';
                     _dok_put:=1
                  ?};
                  {? DOK.DSP_WPZF=''
                  || DOK.DSP_WPZF:='N';
                     _dok_put:=1
                  ?};
                  {? DOK.NETTO=0 & DOK.VAT=0 & DOK.BRUTTO=0 & (DOK.DOK_REJ().SLO().KOD='VAT' | SLO.KOD='SAD')
                  || _sum_vpoz:=1
                  ?};
                  {? DOK.GVKHSTAT=''
                  || DOK.GVKHSTAT:=DOK.GVLISTAT:='N';
                     _dok_put:=1
                  ?};
                  POZF.prefix(DOK.ref);
                  {? POZF.first()
                  || {!
                     |? {? POZF.SP_WYM='' || POZF.SP_WYM:='N' ?};
                        {? POZF.DT=date(0,0,0) || POZF.DT:=DOK.DOP ?};
                        {? POZF.put(1) || _lpozf+=1 ?};
                        POZF.next()
                     !}
                  ?};
                  VPOZ.prefix(DOK.ref());
                  {? VPOZ.first()
                  || {!
                     |? VPOZ.DOK_REJ:=DOK.DOK_REJ;
                        VPOZ.HAN:=DOK.HAN;
                        VPOZ.ODD:=DOK.ODD;
                        VPOZ.REJ:=DOK.REJ;
                        VPOZ.KH_KRISO:=DOK.KH_KRISO;
                        VPOZ.O_KRISO:=DOK.O_KRISO;
                        VPOZ.RB:=DOK.RB;
                        VPOZ.RB_V:=DOK.RB_V;
                        VPOZ.WYS:=DOK.WYS;
                        VPOZ.WAL:=DOK.WAL;
                        VPOZ.BANK:=DOK.BANK;
                        VPOZ.JORG:=DOK.JORG;
                        VPOZ.SP_PL:=DOK.SP_PL;
                        {? VPOZ.D=date(0,0,0)
                        || VPOZ.D:=DOK.DOP
                        ?};
                        VPOZ.KRAJ:=DOK.KRAJ;
                        {? VPOZ.put() || _lvpoz+=1 ?};
:: Uzupełnienie w nagłówku dokumentu sum kwot netto, vat, brutto
                        {? _sum_vpoz=1
                        || _dok_put:=1;
                           DOK.NETTO+=VPOZ.NETTO;
                           DOK.VAT+=VPOZ.VAT;
                           DOK.BRUTTO+=VPOZ.BRUTTO
                        ?};
                        VPOZ.next()
                     !}
                  ?};
                  {? DOK.SUMWN=0 & DOK.SUMMA=0 || _sum_poz:=1 ?};
                  no_msg(1); lock_time(0);
                  POZ.prefix(DOK.ref());
                  {? POZ.first()
                  || {!
:: Przypisywanie wybranych wartości z DOK do POZ
                     |? POZ.DOKREJ:=DOK.DOK_REJ;
                        POZ.HAN:=DOK.HAN;
                        POZ.REJ:=DOK.REJ;
                        POZ.KH_KRISO:=DOK.KH_KRISO;
                        POZ.O_KRISO:=DOK.O_KRISO;
                        POZ.RB:=DOK.RB;
                        POZ.RB_V:=DOK.RB_V;
                        POZ.WYS:=DOK.WYS;
                        POZ.BANK:=DOK.BANK;
                        POZ.JORG:=DOK.JORG;
                        POZ.SP_PL:=DOK.SP_PL;
                        POZ.KRAJ:=DOK.KRAJ;
                        POZ.RVAT:=DOK.RVAT;
                        {? POZ.KON_OPIS=''
                        || opis_poz:=_kon.get(POZ.KON); odd_ksn:=0;
                           {? opis_poz=''
                           || ROK_F.cntx_psh(); OKRO_F.cntx_psh();
                              POZ.KOM().KS();
                              exec('opis','konto',POZ.KON);
                              exec('ka_opis','konto',0);
                              OKRO_F.cntx_pop(); ROK_F.cntx_pop();
                              _kon.set(POZ.KON,opis_poz)
                           ?};
                           POZ.KON_OPIS:=opis_poz;
                           &opis_poz; &odd_ksn
                        ?};
                        {? POZ.put() || _lpoz+=1 ?};
:: Uzupełnianie sum obrotów WN/MA
                        {? _sum_poz=1
                        || _ok_poz:=1;
                           KS.cntx_psh();
                           {? POZ.KOM=null
                           || {? POZ.KON=''
                              || _ok_poz:=0
                              || KS.index('SYM'); KS.prefix(ROK_F.ref(),ROK_F.SYNT+POZ.KON);
                                 {? ~KS.first() || _ok_poz:=0 ?}
                              ?}
                           || POZ.KOM().KS()
                           ?};
                           {? _ok_poz & -(1+KS.TYP)<>'p'
                           || {? POZ.STR='Wn' || DOK.SUMWN+=POZ.SUM || DOK.SUMMA+=POZ.SUM ?};
                              _dok_put:=1
                           ?};
                           KS.cntx_pop()
                        ?};
                        POZ.next()
                     !}
                  ?};
                  no_msg(0); lock_time(-1);
                  {? _dok_put & DOK.put() || _ldok+=1 ?};
                  DOK.next()
               !}
            ?};
            OKRO_F.next()
         !}
      ?};
      _kon.clear();
      ROK_F.next()
   !}
?};
DOK.cntx_pop(); DOK.trig_on('*','*');
VPOZ.cntx_pop(); VPOZ.trig_on('*','*');
POZF.cntx_pop(); POZF.trig_on('*','*');
POZ.cntx_pop(); POZ.trig_on('*','*');
{? _ldok | _lpoz | _lvpoz | _lpozf
|| {? _ldok || __UPG.msg('Zaktualizowano nagłówki dokumentów źródłowych. %1 zmodyfikowany(ch) rekord(ów).'[$_ldok]) ?};
   {? _lpoz || __UPG.msg('Zaktualizowano pozycje dokumentów źródłowych. %1 zmodyfikowany(ch) rekord(ów).'[$_lpoz]) ?};
   {? _lvpoz || __UPG.msg('Zaktualizowano pozycje dokumentów VAT. %1 zmodyfikowany(ch) rekord(ów).'[$_lvpoz]) ?};
   {? _lpozf || __UPG.msg('Zaktualizowano pozycje faktur. %1 zmodyfikowany(ch) rekord(ów).'[$_lpozf]) ?}
|| __UPG.msg('Nie stwierdzono potrzeby aktualizacji danych dla dokumentów księgowych.')
?};
1


\DOK_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ, JK, PBS [23.25]
:: OPIS: Uzupełnienie w nagłówku dokumentu sum kwot netto, vat i brutto z pozycji VAT dokumentu - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie w nagłówku dokumentu sum kwot netto, vat, brutto oraz obrotów WN/MA z pozycji VAT dokumentu. '
'Aktualizacja pozycji dokumentów źródłowych oraz pozycji dokumentów VAT na podstawie tabeli DOK. '


\obj_kon
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Obiekt pomocniczy na potrzeby opisu konta
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'TAB',
   'clear',
   'get','set'
);
_obj.TAB:=tab_tmp(1,
   'K','STRING[35]','Symbol konta',
   'O','STRING[255]','Opis'
);
_obj.clear:="
   .TAB.erase()
";
_obj.get:="
   _kon:=_a;
   _tab:=.TAB;
   {? _tab.find_key(_kon,)
   || _tab.O
   || ''
   ?}
";
_obj.set:="
   _kon:=_a;
   _desc:=_b;
   _tab:=.TAB;
   _tab.blank(0);
   _tab.K:=_kon;
   _tab.O:=_desc;
   _tab.add()
";
_obj


\REZ_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizuje (przepina) dedykowane rezerwacje pod zlecenia na rezerwacje pod surowce
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');

__lrec:=__lmod:=0;
_result:=1;

REZ.cntx_psh();
ZLIM.cntx_psh();
_rez_mask:=(REZ.name()-2)+'__';
{? REZ.name()<>_rez_mask || REZ.use(_rez_mask) ?};
_ndx1:=REZ.ndx_tmp(,,'TYP',,,'ZL_REZ',,);
REZ.index(_ndx1);
:: Rezerwacje do zleceń, do których nie wygenerowano jeszcze limitów - zmiana typu
REZ.prefix('T','T');
{? REZ.first()
||
   {!
   |?
      __lrec+=1;
      _next:=0;
      _ref_nxt:=null();
      REZ.cntx_psh();
      {? REZ.next() || _ref_nxt:=REZ.ref() ?};
      REZ.cntx_pop();
      REZ.TYP:='S';
      REZ.cntx_psh();
      REZ.prefix();
      {? REZ.put(1)
      || __lmod+=1
      ?};
      REZ.cntx_pop();
      {? _ref_nxt<>null()
      || _next:=REZ.seek(_ref_nxt)
      ?};
      _next>0
   !}
?};

_zlim_rez:=tab_tmp(,
   'ZLIM','STRING[16]','$ZLIM.ref()',
   'ILR','REAL','Ilość zarezerwowana',
   'TMAT_SRC','STRING[16]','Tmat źródłowy'
);
:: Rezerwacje do zleceń, do których wygenerowano limity - zmiana typu i podpięcie pod konkretny surowiec
REZ.prefix('T','P');
{? REZ.first()
||
   {!
   |?
      __lrec+=1;
      _refrea:=REZ.REFREA;
      _dorozp:=REZ.ILR;
      ZLIM.cntx_psh();
      exec('openmask','zl_common',REZ.ZL);
      ZLIM.index('ZKN');
      ZLIM.prefix(REZ.ZL,'N',REZ.M);
      {? ZLIM.first()
      || {!
         |?
            {? _zlim_rez.find_key($ZLIM.ref())
            || _tmat_src:=_zlim_rez.TMAT_SRC;
               _ilr:=ZLIM.LIL-_zlim_rez.ILR
            || _tmat_src:=exec('FindAndGet','#table',TMAT,ZLIM.TMAT,,"{? SRC<>'' || SRC || $ref() ?}",'');
               _ilr:=ZLIM.LIL
            ?};
            {? _tmat_src=_refrea & _ilr>0
            || _ilosc:={? _ilr>_dorozp || _dorozp || _ilr ?};
               REZ.ILR-=_ilosc;
               {? REZ.put()
               ||
::                Dodanie nowej rezerwacji dedykowanej na nielimity
                  REZ.TYP:='S';
                  REZ.ZL_REZ:='P';
                  REZ.REFREA:='';
                  REZ.REFSQL:=REZ.ZLIM:=$ZLIM.ref();
                  REZ.ZGP:=$ZLIM.ZGP;
                  REZ.DODT:=date(0,0,0);
                  REZ.BTERM:='T';
                  REZ.ILR:=_ilosc;
                  REZ.cntx_psh();
                  REZ.prefix();
                  {? REZ.add(1)
                  || __lmod+=1;
                     _dorozp-=_ilosc;
                     {? _zlim_rez.find_key(REZ.ZLIM)
                     || _zlim_rez.ILR+=REZ.ILR;
                        _zlim_rez.put()
                     || _zlim_rez.ZLIM:=REZ.ZLIM;
                        _zlim_rez.ILR:=REZ.ILR;
                        _zlim_rez.TMAT_SRC:=_tmat_src;
                        _zlim_rez.add()
                     ?}
                  ?};
                  REZ.cntx_pop()
               ?}
            ?};
            ZLIM.next() & _dorozp>0
         !}
      ?};
      ZLIM.cntx_pop();
      REZ.del()
   !}
?};
REZ.ndx_drop(_ndx1);
ZLIM.cntx_pop();
REZ.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano rezerwacje do surowców zleceń.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji rezerwacji do surowców zleceń.')
?};

VAR_DEL.delete('__lrec','__lmod');
_result


\REZ_update_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizuje (przepina) dedykowane rezerwacje pod zlecenia na rezerwacje pod surowce - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rezerwacji do surowców zleceń.'


\REJ_MAT_update
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Na podstawie rozpisek rozliczeń surowców (REJ_MAT) aktualizuje rezerwacje pod surowce zlecenia
::   WE:
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');

__lrec:=__lmod:=0;
_result:=1;
REZ.cntx_psh();
_rez_mask:=(REZ.name()-2)+'__';
{? REZ.name()<>_rez_mask || REZ.use(_rez_mask) ?};
REZ.prefix();
REJ_MAT.cntx_psh();
REJ_MAT.prefix();
{? REJ_MAT.first()
|| {!
   |?
      __lrec+=1;
      {? REJ_MAT.JM*'OK'=0
      || {? REJ_MAT.RREZ<>''
         || {? ref_name(REJ_MAT.RREZ)<>REZ.name()
            || REZ.use(ref_name(REJ_MAT.RREZ));
               REZ.prefix()
            ?};
            {? REZ.seek(REJ_MAT.RREZ) & REZ.TYP='T' & $REZ.ZL=REJ_MAT.RZL & REZ.MG=REJ_MAT.MG & REZ.M=REJ_MAT.KTM
            || REZ.TYP:='S';
               REZ.BTERM:={? REJ_MAT.TMP='T' || 'N' || 'T' ?};
               {? REZ.BTERM='T'
               || REZ.DODT:=date(0,0,0)
               ?};
               REZ.ZLIM:=REZ.REFSQL:=REJ_MAT.RZLIM;
               REZ.ZGP:=$REJ_MAT.ZGP;
               REZ.REFREA:=$REJ_MAT.ZLGD;
               REZ.ZL_REZ:='R';
               REZ.ZD_POZ:=REJ_MAT.REANL;
               {? REZ.put(1)
               || REJ_MAT.JM:='OK'
               || REJ_MAT.JM:='ERR_REZ_PT'
               ?}
            || REJ_MAT.JM:='ERR_REZ_SK'
            ?};
            {? REJ_MAT.put(1)
            || __lmod+=1
            ?}
         || REJ_MAT.JM:='ERR_REZ_NF';
            {? REJ_MAT.put(1)
            || __lmod+=1
            ?}
         ?}
      ?};
      REJ_MAT.next()
   !}
?};
REJ_MAT.cntx_pop();
REZ.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano rezerwacje do surowców zleceń na podstawie zarejestrowanych pobrań surowców.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji rezerwacji do surowców zleceń.')
?};

VAR_DEL.delete('__lrec','__lmod');
_result


\REJ_MAT_update_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Na podstawie rozpisek rozliczeń surowców (REJ_MAT) aktualizuje rezerwacje pod surowce zlecenia - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja rezerwacji do surowców zleceń na podstawie zarejestrowanych pobrań surowców.'


\B_PREL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja elementów procesów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
B_PREL.cntx_psh();
B_PREL.prefix();
B_PREL.for_each("
   __lrec+=1;
   {? B_PREL.NOTIFY_3=''
   || B_PREL.NOTIFY_3:='N';
      {? B_PREL.put(1) || __lmod+=1 ?}
   ?}",
   1
);
B_PREL.cntx_pop();
__UPG.msg('Zaktualizowano elementy procesów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\B_PREL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja elementów procesów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktulizacja elementów procesów'


\B_TODO_U
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja elementów personalizacji zadań
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
B_TODO_U.cntx_psh();
B_TODO_U.prefix();
B_TODO_U.for_each("
   __lrec+=1;
   {? B_TODO_U.NOTIFY_3=''
   || B_TODO_U.NOTIFY_3:='N';
      {? B_TODO_U.put(1) || __lmod+=1 ?}
   ?}",
   1
);
B_TODO_U.cntx_pop();
__UPG.msg('Zaktualizowano elementy personalizacji zadań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\B_TODO_U_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja elementów pesronalizacji zadań - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktulizacja elementów personalizacji zadań'


\BI_TODO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wypełnia pola w tabeli BI_TODO
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
BI_TODO.cntx_psh();
BI_TODO.index('BI_PROC');
BI_TODO.prefix();
_res:=BI_TODO.for_each("
   __lrec+=1;
   _put:=0;
   {? BI_TODO.NOTIFY_3=''
   || BI_TODO.NOTIFY_3:='N';
      _put:=1
   ?};
   {? _put>0
   || {? BI_TODO.put()
      || __lmod+=1
      ?}
   ?}
");
BI_TODO.cntx_pop();
__UPG.msg('Zaktualizowano zadania na listach TODO.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
_res


\BI_TODO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Wypełnia pola w tabeli BI_TODO - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zadań na listach TODO'


\PROD_REJ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizacja rejestracji wykonań
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PROD_REJ.trig_off('*','*');
PROD_REJ.cntx_psh();
PROD_REJ.prefix();
PROD_REJ.for_each("
   __lrec+=1;
   _put:=0;
:: Ustawienie pola GKTL (grupa kart technologicznych)
   {? PROD_REJ.GKTL=null()
   || {? PROD_REJ.ZGP<>null() & PROD_REJ.ZGP().NRPRZ().GKTL<>null()
      || PROD_REJ.GKTL:=PROD_REJ.ZGP().NRPRZ().GKTL;
         _put:=1
      |? PROD_REJ.PL_OPER<>null() & PROD_REJ.PL_OPER().ZGP<>null() & PROD_REJ.PL_OPER().ZGP().NRPRZ().GKTL<>null()
      || PROD_REJ.GKTL:=PROD_REJ.PL_OPER().ZGP().NRPRZ().GKTL;
         _put:=1
      ?}
   ?};
   {? _put>0
   || PROD_REJ.put();
      __lmod+=1
   ?};
   ~~
   ",
   1
);
PROD_REJ.cntx_pop();
PROD_REJ.trig_on('*','*');
__UPG.msg('Zaktualizowano zapisy do rejestracji wykonań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\PROD_REJ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Aktualizacja rejestracji wykonań - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów do rejestracji wykonań'


\init_b_perm
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Odświeża definicję uprawnień do danych
::----------------------------------------------------------------------------------------------------------------------
exec('init','b_perm');
__UPG.msg('Zaktualizowano definicje uprawnień do danych.');
1


\init_b_perm_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Odświeża definicję uprawnień do danych - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji uprawnień do danych.'


\OPAK_ZWR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli OPAK_ZWR - tabela zwrotów opakowań
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=OPAK_ZWR.names();
_msk.clear();
OPAK_ZWR.cntx_psh();
{? _msk.first()
|| {!
   |? OPAK_ZWR.use(_msk.NAME);
      OPAK_ZWR.prefix();
      OPAK_ZWR.for_each("
         _put:=0;
         __lrec+=1;
         {? OPAK_ZWR.TMP=''
         ||
            OPAK_ZWR.TMP:='N';
            _put:=1
         ?};
         {? _put || {? OPAK_ZWR.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
OPAK_ZWR.cntx_pop();
__UPG.msg('Zaktualizowano tabelę zwrotów opakowań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\OPAK_ZWR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [23.25]
:: OPIS: Uzupełnia pola tabeli OPAK_ZWR - tabela zwrotów opakowań - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola w tabeli zwrotów opakowań'


\TYPYSP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Aktualizacja tabeli TYPYSP - typy dokumentów
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;
TYPYSP.prefix();
TYPYSP.for_each("
   __lrec+=1;
   _put:=0;
   {? TYPYSP.UE_WSU=''
   || TYPYSP.UE_WSU:='N';
      _put:=1
   ?};
   {? _put & TYPYSP.put(1)
   || __lmod+=1
   ?}
");
__UPG.msg('Zaktualizowano dane typów dokumentów sprzedaży i zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\TYPYSP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Aktualizacja tabeli TYPYSP - typy dokumentów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych typów dokumentów sprzedaży i zakupu'


\sync_mwa_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: Import definicji synchronizacji dla przeznaczeń danych PORTAL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',,,1,
   exec('sync_mwa_portal_desc','upgrade_2325'),0,,
   'SYNC_MWA_PORTAL,SYNC_MWA_PORTAL_CFG,SYNC_MWA_PORTAL_WNIOSKI,SYNC_MWA_PORTAL_SEOD'
);

_choice:=FUN.choice(
   'Czy dane zostały zaimportowane?\n\n'
   ' * Tak = zmiana statusu zadania na "Wykonane"\n'
   ' * Nie = pozostawienie zadanie do ponownego uruchomienia\n'
   ' * Anuluj = rezygnacja z wykonywania, zmiana statusu zadania na "Wykonane"\n'@
   ,2,'Tak'@,'Nie'@
);

USERS.cntx_psh();
__UPG.msg('Użytkownik: %1 | Data: %2 | Godzina: %3'[OPERATOR.USER().KOD,date()$1,time()$3]);
USERS.cntx_pop();

{? _choice=1
|| __UPG.msg('Zadanie zostało wykonane.')
|? _choice=2
|| __UPG.msg('Zadanie zostało odłożone do ponownego uruchomienia.')
|? _choice=0
|| __UPG.msg('Zrezygnowano z wykonania zadania.')
|| __UPG.msg('Nierozpoznana odpowiedź.')
?};

{? _choice<=1
|| 1
|| -1
?}


\sync_mwa_portal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [23.25]
:: OPIS: Import definicji synchronizacji dla przeznaczeń danych PORTAL - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Import definicji wymiany danych przez API dla przeznaczeń danych: PORTAL, PORTAL_CFG, PORTAL_WNIOSKI, PORTAL_SEOD.'


\jpk_dl_sym
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Wydłużenie pola symbol dokumentu do 50 znaków
::----------------------------------------------------------------------------------------------------------------------
_r:=_p:=0;
ISTDEF.cntx_psh();
ISTDEFS.cntx_psh();
ISTDEF.index('DATA');
ISTDEF.prefix('FKS','J','V7K',null,date(2022,1,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'DowodSprzedazy');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'DowodZakupu');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?}
|| _r:=-1
?};
{? _r<0
|| __UPG.msg('Nie znaleziono deklaracji JPK_V7K(2).')
|? _r>0
|| __UPG.msg('Zaktualizowano pozycje deklaracji JPK_V7K(2).');
   __UPG.msg('Przetworzono: %1 pozycji deklaracji JPK_V7K(2), z czego zmodyfikowano: %2 pozycji.'[$_r,$_p])
?};
_r:=_p:=0;
ISTDEF.prefix('FKS','J','V7M',null,date(2022,1,1));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'DowodSprzedazy');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?};
   ISTDEFS.prefix(ISTDEF.ref,'DowodZakupu');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?}
|| _r:=-1
?};
{? _r<0
|| __UPG.msg('Nie znaleziono deklaracji JPK_V7M(2).')
|? _r>0
|| __UPG.msg('Zaktualizowano pozycje deklaracji JPK_V7M(2).');
   __UPG.msg('Przetworzono: %1 pozycji deklaracji JPK_V7M(2), z czego zmodyfikowano: %2 pozycji.'[$_r,$_p])
?};

_r:=_p:=0;
ISTDEF.prefix('FKS','J','KR',null,date(2016,3,11));
{? ISTDEF.first()
|| ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref,'NrDowoduKsiegowego');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?}
|| _r:=-1
?};
{? _r<0
|| __UPG.msg('Nie znaleziono deklaracji JPK_KR(1).')
|? _r>0
|| __UPG.msg('Zaktualizowano pozycje deklaracji JPK_KR(1).');
   __UPG.msg('Przetworzono: %1 pozycji deklaracji JPK_KR(1), z czego zmodyfikowano: %2 pozycji.'[$_r,$_p])
?};
_r:=_p:=0;
ISTDEF.prefix('FKS','J','MAG',null,date(2016,3,11));
{? ISTDEF.first()
|| ISTDEFS.prefix(ISTDEF.ref,'NumerFaPZ');
   {? ISTDEFS.first()
   || {? ISTDEFS.TYPFLD='STRING[50]'
      || _r+=1
      || _r+=1;
         ISTDEFS.TYPFLD:='STRING[50]';
         _p+=ISTDEFS.put()
      ?}
   ?}
|| _r:=-1
?};
{? _r<0
|| __UPG.msg('Nie znaleziono deklaracji JPK_MAG(1).')
|? _r>0
|| __UPG.msg('Zaktualizowano pozycje deklaracji JPK_MAG(1).');
   __UPG.msg('Przetworzono: %1 pozycji deklaracji JPK_MAG(1), z czego zmodyfikowano: %2 pozycji.'[$_r,$_p])
?};
ISTDEFS.cntx_pop();
ISTDEF.cntx_pop();
1


\jpk_dl_sym_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS:  Wydłużenie pola symbol dokumentu do 50 znaków - opis
::----------------------------------------------------------------------------------------------------------------------
' Wydłużenie pola symbol dokumentu do 50 znaków'


\kh_tagger
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodanie pola Tagger do tabeli KH
::----------------------------------------------------------------------------------------------------------------------
KH.cntx_psh();
KH.index('KOD'); KH.prefix();
_all:=KH.size(); _zmod:=0;
{? KH.first()
|| {! |?
      {? KH.NAZ<>'Kontrahent jednorazowy' & PAR_SKID.get(120)<>KH.KOD
      || KH.TAGGER:='T';
         _zmod+=1;
         KH.put(1)
      || KH.TAGGER:='N';
         _zmod+=1;
         KH.put(1)
      ?};
      KH.next()
   !}
?};
KH.cntx_pop();
__UPG.msg('Przetworzono: %1 kontrahentów, z czego zmodyfikowano %2 pozycji.'[$_all,$_zmod]);
1


\kh_tagger_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Dodanie pola Tagger do tabeli KH - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie obsługi inteligentnych podpowiedzi dla kontrahentów'


\B_WORKER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja kolejek
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
B_WORKER.cntx_psh();
B_WORKER.prefix();
B_WORKER.for_each("
   __lrec+=1;
   {? B_WORKER.TECH='' | B_WORKER.TECH='0'
   || B_WORKER.TECH:='N';
      {? B_WORKER.put(1) || __lmod+=1 ?}
   |? B_WORKER.TECH='1'
   || B_WORKER.TECH:='T';
      {? B_WORKER.put(1) || __lmod+=1 ?}
   ?}",
   1
);
B_WORKER.cntx_pop();
__UPG.msg('Zaktualizowano kolejki.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\B_WORKER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizacja kolejek - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktulizacja elementów procesów'


\actOperMob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pola EANN.IS_R, EANN.IS_M i EANP.IS_RM
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_msk:=EANN.names();
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
EANN.cntx_psh();
EANP.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? EANN.use(_msk.NAME);
      EANP.use('eanp'+(_msk.NAME+3));
      EANN.prefix();
      EANN.for_each("
         _put:=0;
         __lrec+=1;
         {? EANN.TYP='R' & EANN.IS_R='' & EANN.IS_M=''
         || _put:=1; EANN.IS_R:='T'; EANN.IS_M:='N'
         || {? EANN.IS_R='' || _put:=1; EANN.IS_R:='N' ?};
            {? EANN.IS_M='' || _put:=1; EANN.IS_M:='N' ?}
         ?};
         {? EANN.ST_R='' || _put:=1; EANN.ST_R:='N' ?};
         {? EANN.ST_M='' || _put:=1; EANN.ST_M:='N' ?};
         {? _put
         || {? EANN.put()
            || __lmod+=1;
               EANP.index('EANN');
               EANP.prefix(EANN.ref());
               {? EANP.first()
               || {!
                  |? {? EANP.IS_RM=''
                     || EANP.IS_RM:={? EANP.EANN().TYP='R' || 'R' || 'N' ?};
                        EANP.put(1)
                     ?};
                     EANP.next()
                  !}
               ?}
            ?}
         ?}
      ");
      _msk.next()
   !}
?};
EANN.cntx_pop();
EANP.cntx_pop();
obj_del(_msk);
__UPG.msg('Zaktualizowano zapisy dla operacji mobilnych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'@[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\actOperMob_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.42]
:: OPIS: Uzupełnia pole EANN.ZLEC - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie znaczników operacji na urządzeniach mobilnych'


\long_sym_rok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uaktualnienie pola SYM_ROK
::----------------------------------------------------------------------------------------------------------------------
__lrec:=__lmod:=0; _NAMES:=POZ.names; POZ.trig_off('*','*');
{? _NAMES.first()
|| POZ.cntx_psh();
   {! |?
      POZ.use(_NAMES.NAME); POZ.prefix();
      POZ.for_each("__lrec+=1;
                   {? POZ.SYM_ROK<>'' & +POZ.SYM_ROK<55 || POZ.SYM_ROK:=exec('op_unik_sym','rozrach',POZ.ID,,POZ.SYM_ROK+4);
                      {? POZ.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   POZ.cntx_pop()
?};
POZ.trig_on('*','*');
__UPG.msg('Zaktualizowano SYM_ROK w pozycjach dokumentów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=OP.names; OP.trig_off('*','*');
{? _NAMES.first()
|| OP.cntx_psh();
   {! |?
      OP.use(_NAMES.NAME); OP.prefix();
      OP.for_each("__lrec+=1;
                      {? OP.SYM_ROK<>'' & +OP.SYM_ROK<55 || OP.SYM_ROK:=exec('op_unik_sym','rozrach',OP.SYM,,OP.SYM_ROK+4);
                      {? OP.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   OP.cntx_pop()
?};
OP.trig_on('*','*');
__UPG.msg('Zaktualizowano SYM_ROK w rozrachunkach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=RMK.names;
{? _NAMES.first()
|| RMK.cntx_psh();
   {! |?
      RMK.use(_NAMES.NAME); RMK.prefix();
      RMK.for_each("__lrec+=1;
                      {? RMK.SYM_ROK<>'' & +RMK.SYM_ROK<55 || RMK.SYM_ROK:=exec('op_unik_sym','rozrach',RMK.SYM,,RMK.SYM_ROK+4);
                      {? RMK.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   RMK.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli RMK.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=VAT_PS.names;
{? _NAMES.first()
|| VAT_PS.cntx_psh();
   {! |?
      VAT_PS.use(_NAMES.NAME); VAT_PS.prefix();
      VAT_PS.for_each("__lrec+=1;
                      {? VAT_PS.SYM_ROK<>'' & +VAT_PS.SYM_ROK<55 || VAT_PS.SYM_ROK:=exec('op_unik_sym','rozrach',VAT_PS.SYM,,VAT_PS.SYM_ROK+4);
                      {? VAT_PS.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   VAT_PS.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli VAT_PS.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=OPTMP.names;
{? _NAMES.first()
|| OPTMP.cntx_psh();
   {! |?
      OPTMP.use(_NAMES.NAME); OPTMP.prefix();
      OPTMP.for_each("__lrec+=1;
                      {? OPTMP.SYM_ROK<>'' & +OPTMP.SYM_ROK<55 || OPTMP.SYM_ROK:=exec('op_unik_sym','rozrach',OPTMP.SYM,,OPTMP.SYM_ROK+4);
                      {? OPTMP.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   OPTMP.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli OPTMP.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=PAR_NAG.names;
{? _NAMES.first()
|| PAR_NAG.cntx_psh();
   {! |?
      PAR_NAG.use(_NAMES.NAME); PAR_NAG.prefix();
      PAR_NAG.for_each("__lrec+=1;
                      {? PAR_NAG.SYM_ROK<>'' & +PAR_NAG.SYM_ROK<55 || PAR_NAG.SYM_ROK:=exec('op_unik_sym','rozrach',PAR_NAG.SYMDOK,,PAR_NAG.SYM_ROK+4);
                      {? PAR_NAG.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   PAR_NAG.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli PAR_NAG.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=PAR_POZ.names;
{? _NAMES.first()
|| PAR_POZ.cntx_psh();
   {! |?
      PAR_POZ.use(_NAMES.NAME); PAR_POZ.prefix();
      PAR_POZ.for_each("__lrec+=1;
                      {? PAR_POZ.SYM_ROK<>'' & +PAR_POZ.SYM_ROK<55 || PAR_POZ.SYM_ROK:=exec('op_unik_sym','rozrach',PAR_POZ.OP,,PAR_POZ.SYM_ROK+4);
                      {? PAR_POZ.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   PAR_POZ.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli PAR_POZ.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=PB.names;
{? _NAMES.first()
|| PB.cntx_psh();
   {! |?
      PB.use(_NAMES.NAME); PB.prefix();
      PB.for_each("__lrec+=1;
                      {? PB.SYM_ROK<>'' & +PB.SYM_ROK<55 || PB.SYM_ROK:=exec('op_unik_sym','rozrach',PB.SYM,,PB.SYM_ROK+4);
                      {? PB.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   PB.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli PB.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=PB_OP.names;
{? _NAMES.first()
|| PB_OP.cntx_psh();
   {! |?
      PB_OP.use(_NAMES.NAME); PB_OP.prefix();
      PB_OP.for_each("__lrec+=1;
                      {? PB_OP.SYM_ROK<>'' & +PB_OP.SYM_ROK<55 || PB_OP.SYM_ROK:=exec('op_unik_sym','rozrach',PB_OP.SYM,,PB_OP.SYM_ROK+4);
                      {? PB_OP.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   PB_OP.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli PB_OP.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=SER_POZ.names;
{? _NAMES.first()
|| SER_POZ.cntx_psh();
   {! |?
      SER_POZ.use(_NAMES.NAME); SER_POZ.prefix();
      SER_POZ.for_each("__lrec+=1;
                      {? SER_POZ.SYM_ROK<>'' & +SER_POZ.SYM_ROK<55 || SER_POZ.SYM_ROK:=exec('op_unik_sym','rozrach',SER_POZ.DOK,,SER_POZ.SYM_ROK+4);
                      {? SER_POZ.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   SER_POZ.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli SER_POZ.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=WIND_OP.names;
{? _NAMES.first()
|| WIND_OP.cntx_psh();
   {! |?
      WIND_OP.use(_NAMES.NAME); WIND_OP.prefix();
      WIND_OP.for_each("__lrec+=1;
                      {? WIND_OP.SYM_ROK<>'' & +WIND_OP.SYM_ROK<55 || WIND_OP.SYM_ROK:=exec('op_unik_sym','rozrach',WIND_OP.SYM,,WIND_OP.SYM_ROK+4);
                      {? WIND_OP.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   WIND_OP.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli WIND_OP.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;

_NAMES:=POZDOK.names;
{? _NAMES.first()
|| POZDOK.cntx_psh();
   {! |?
      POZDOK.use(_NAMES.NAME); POZDOK.prefix();
      POZDOK.for_each("__lrec+=1;
                      {? POZDOK.SYM_ROK<>'' & +POZDOK.SYM_ROK<55 || POZDOK.SYM_ROK:=exec('op_unik_sym','rozrach',POZDOK.MF_FIKS-5,,POZDOK.SYM_ROK+4);
                      {? POZDOK.put() || __lmod+=1 ?}
                   ?}
                  ");
      _NAMES.next()
   !};
   POZDOK.cntx_pop()
?};
__UPG.msg('Zaktualizowano SYM_ROK w tabeli POZDOK.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod'); __lrec:=__lmod:=0; &_NAMES;
1


\long_sym_rok_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Uaktualnienie pola SYM_ROK - opis
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie pola SYM_ROK'


\KH
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Uzupełnia pola dla tabeli kontrahentów
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_fml:="
   __lrec+=1;
   _put:=0;
   {? KH.GRVAT=''
   || KH.GRVAT:='N';
      _put:=1
   ?};
   {? _put
   || {? KH.put(1)
      || __lmod+=1
      ?}
   ?}
";
KH.trig_off('*','*');
KH.cntx_psh();
KH.prefix();
_result:=KH.for_each(_fml,1);
KH.cntx_pop();
KH.trig_on('*','*');
{? _result
|| __UPG.msg('Zaktualizowano dane kontrahentów.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji danych kontrahentów.')
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\KH_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Uzupełnia pola dla tabeli kontrahentów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych kontrahentów'


\KH_ODB
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Wypełnia pola w tabeli KH_ODB
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_fml:="
   __lrec+=1;
   _put:=0;
   {? KH_ODB.CGVAT=''
   || KH_ODB.CGVAT:='N';
      _put:=1
   ?};
   {? _put
   || {? KH_ODB.put(1)
      || __lmod+=1
      ?}
   ?}
";
KH_ODB.cntx_psh();
KH_ODB.prefix();
_result:=KH_ODB.for_each(_fml,1);
KH_ODB.cntx_pop();
{? _result
|| __UPG.msg('Zaktualizowano dane odbiorców kontrahentów.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
|| __UPG.msg('Wystąpił błąd podczas aktualizacji danych odbiorców kontrahentów.')
?};
VAR_DEL.delete('__lrec','__lmod');
_res


\KH_ODB_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Wypełnia pola w tabeli KH_ODB - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych odbiorców kontrahentów'


\FAKS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizuje pola FAKS.KSEF_ERR, FAKS.GV_WEW
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=FAKS.names();
FAKS.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? FAKS.use(_msk.NAME);
      FAKS.prefix();
      FAKS.for_each("
         __lrec+=1;
         _put:=0;
         {? FAKS.KSEF_ERR=''
         || FAKS.KSEF_ERR:='N';
            _put:=1
         ?};
         {? FAKS.GV_WEW=''
         || FAKS.GV_WEW:='N';
            _put:=1
         ?};
         {? _put & FAKS.put() || __lmod+=1 ?}
      ");
      _msk.next()
   !}
?};
FAKS.cntx_pop();

__UPG.msg('Zaktualizowano pola w nagłówku dokumentów sprzedaży i zakupu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\FAKS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Aktualizuje pola FAKS.KSEF_ERR, FAKS.GV_WEW
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól w nagłówku dokumentów sprzedaży i zakupu.'


\users_upr_dane_napraw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Usuwa błędne zapisy w USERS_UP i TARUP
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('user_upraw_usun_blad','users_upraw');
__UPG.msg('Zaktualizowano uprawnienia użytkowników do danych.');
_res


\users_upr_dane_napraw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [23.25]
:: OPIS: Usuwa błędne zapisy w USERS_UP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do danych'


\fap2dk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przeniesienie złączenia pozycji dokumentów sprzedaży z pozycjami magazynowymi do tabeli FAP2DK
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=DK.names();
exec('fap2dk_psh','open_tab');
_msk.clear();
{? _msk.first()
||
   {!
   |?
      _kod:=1+(_msk.NAME+3);
      _rok:=_msk.NAME+2;
      exec('fap2dk_open','open_tab',_kod,_rok,'sp');
      DK.prefix();
      FAP.prefix();
      FAKS.prefix();
      FAP2DK.index('NDK');
      DK.for_each("
         __lrec+=1;
         {? DK.FAP<>'' & ~(FAP2DK.prefix($DK.ref());FAP2DK.first()) & FAP.seek(DK.FAP,ref_name(DK.FAP))
         & FAP.FAKS<>null() & FAKS.seek(FAP.FAKS, ref_name(FAP.FAKS))
         || {? exec('adfap2dk','magdok_wspolne',$FAP.ref(),$DK.ref(),$FAP.FAKS,$DK.N,DK.IL,FAP.FAKS().WHERE)
            || __lmod+=1
            ?}
         |? DK.FAP<>'' & (FAP2DK.prefix($DK.ref());FAP2DK.first()) & FAP.seek(DK.FAP,ref_name(DK.FAP)) & (FAP2DK.FAKS='' | FAP2DK.FAP='')
         || FAP2DK.FAKS:=$FAP.FAKS;
            FAP2DK.FAP:=$FAP.ref();
            FAP2DK.prefix();
            {? FAP2DK.put(1) || __lmod+=1 ?}
         ?}
      ");
      _msk.next()
   !}
?};
exec('fap2dk_pop','open_tab');

__UPG.msg('Przeniesienie złączenia pozycji magazynowych z pozycjami magazynowymi do tabeli FAP2DK.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\fap2dk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przeniesienie złączenia pozycji dokumentów sprzedaży z pozycjami magazynowymi do tabeli FAP2DK
::----------------------------------------------------------------------------------------------------------------------
'Przeniesienie złączenia pozycji dokumentów sprzedaży z pozycjami magazynowymi do tabeli FAP2DK.'


\MJM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pole MJM.MOB, MJM.MOD
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

MJM.cntx_psh();
MJM.prefix();
MJM.for_each("__lrec+=1;
             _put:=0;
             {? ~((';TN'*MJM.MOB)>1) || _put:=1 ?};
             {? ~((';TN'*MJM.MOD)>1) || _put:=1 ?};
             {? _put & MJM.put(1) || __lmod+=1 ?}",1);
MJM.cntx_pop();

__UPG.msg('Zaktualizowano przeliczniki jednostek miary.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\MJM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia pole MJM.MOB, MJM.MOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja tabeli przeliczników jednostek miary dla materiałów.'


\inw_typydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przypisanie typom dokumentów magazynowych znacznika inwentaryzacji/przeceny.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_inw_p:=exec('FindInSet','#table','TYPYDOK','TYP','INW+','INW+');
_inw_m:=exec('FindInSet','#table','TYPYDOK','TYP','INW-','INW-');
_prc_p:=exec('FindInSet','#table','TYPYDOK','TYP','PRC+','PRC+');
_prc_m:=exec('FindInSet','#table','TYPYDOK','TYP','PRC-','PRC-');
TYPYDOK.cntx_psh();
TYPYDOK.prefix();
{? TYPYDOK.first()
|| {!|?
      _put:=0;
      __lrec+=1;
      {? TYPYDOK.ref()=_inw_p | TYPYDOK.ref()=_inw_m  || TYPYDOK.INW:='I'; _put:=1
      |? TYPYDOK.ref()=_prc_p | TYPYDOK.ref()=_prc_m  || TYPYDOK.INW:='E'; _put:=1
      |? TYPYDOK.INW=''                               || TYPYDOK.INW:='N'; _put:=1
      ?};
      {? _put & TYPYDOK.put(1) || __lmod+=1 ?};
      TYPYDOK.next()
   !}
?};
TYPYDOK.cntx_pop();

__UPG.msg('Przypisano typom dokumentów magazynowych znacznik inwentaryzacji/przeceny.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

VAR_DEL.delete('__lrec','__lmod');
_res


\inw_typydok_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przypisanie typom dokumentów magazynowych znacznika inwentaryzacji/przeceny.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie typom dokumentów magazynowych znacznika inwentaryzacji/przeceny.'


\inn_typydok
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przypisanie do nagłówka inwentaryzacji typów dokumentów magazynowych.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod','__inw_p','__inw_m','__prc_p','__prc_m');
__lrec:=__lmod:=0;
__inw_p:=exec('FindInSet','#table','TYPYDOK','TYP','INW+','INW+');
__inw_m:=exec('FindInSet','#table','TYPYDOK','TYP','INW-','INW-');
__prc_p:=exec('FindInSet','#table','TYPYDOK','TYP','PRC+','PRC+');
__prc_m:=exec('FindInSet','#table','TYPYDOK','TYP','PRC-','PRC-');
_msk:=INN.names();
INN.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |?
      INN.use(_msk.NAME);
      INN.prefix();
      {? _msk.NAME<>'inw_nwyc'
      ||
         INN.for_each("
            __lrec+=1;
            {? INN.TYP_P=null() |  INN.TYP_M=null()
            || INN.TYP_P:=__inw_p;
               INN.TYP_M:=__inw_m;
               {? INN.put(1) || __lmod+=1 ?}
            ?}")
      ||
         INN.for_each("
            __lrec+=1;
            {? INN.TYP_P=null() |  INN.TYP_M=null()
            || INN.TYP_P:=__prc_p;
               INN.TYP_M:=__prc_m;
               {? INN.put(1) || __lmod+=1 ?}
            ?}")
      ?};
      _msk.next()
   !}
?};
INN.cntx_pop();

__UPG.msg('Przypisano do nagłówków inwentaryzacji typów dokumentów magazynowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod','__inw_p','__inw_m','__prc_p','__prc_m');
_res


\inn_typydok_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przypisanie do nagłówka inwentaryzacji typów dokumentów magazynowych.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie do nagłówka inwentaryzacji typów dokumentów magazynowych.'


\nd_inn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przypisanie do nagłówka dokumentu magazynowego czy pochodzi z arkusza inwentaryzacji/przeceny.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=ND.names();
ND.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |?
      ND.use(_msk.NAME);
      ND.prefix();
      ND.for_each("
         __lrec+=1;
         {? ND.TYP().INW='N' & ND.INN=''
         || ND.INN:='N';
            {? ND.put(1) || __lmod+=1 ?}
         |? ND.INN=''
         || _typ:={? ND.TYP().P='T' || 'P' || 'M' ?};
            _mag:=ND.MAG;
            _dt:=ND.D;
            _nr:=ND.NR;
            INN.cntx_psh();
            {? ND.TYP().INW='E'
            || INN.use('inw_nwyc')
            || INN.use('inw_n'+(ND.name()+3))
            ?};
            {? _typ<>''
            ||
               INN.index('IND'+_typ);
               INN.prefix();
               {? INN.find_key(_mag,_dt,_nr)
               || ND.INN:='T';
                  {? ND.put(1) || __lmod+=1 ?}
               ?}
            ?};
            INN.cntx_pop()
         ?}");
      _msk.next()
   !}
?};
ND.cntx_pop();

__UPG.msg('Przypisanie do nagłówka dokumentu magazynowego czy pochodzi z arkusza inwentaryzacji/przeceny.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\nd_inn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przypisanie do nagłówka dokumentu magazynowego czy pochodzi z arkusza inwentaryzacji/przeceny.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie do nagłówka dokumentu magazynowego czy pochodzi z arkusza inwentaryzacji/przeceny.'


\jpk_fa_dsp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizacja struktury pliku JPK_FA
::----------------------------------------------------------------------------------------------------------------------
_fml:="exec('jpk_fa_dsp','jpk')";
_zmiana:=-1;
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J','FA','JPK_FA(4)_v1-0',);
{? ISTDEF.first()
|| _zmiana:=0;
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref(),'P_6');
   {? ISTDEFS.first() & ISTDEFS.REGULY<>$_fml
   || ISTDEFS.REGULY:=$_fml;
      _zmiana:=ISTDEFS.put()
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
{? _zmiana=1
|| __UPG.msg('Zaktualizowano schemat JPK_FA (4).')
|? _zmiana=-1
|| __UPG.msg('Nie znaleziono schematu JPK_FA (4).')
|| __UPG.msg('Aktualizacja schematu JPK_FA (4) nie była wymagana.')
?};
1


\jpk_fa_dsp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizacja struktury pliku JPK_FA - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu JPK_FA (4)\n'
'Zmiana formuły dla pola JPK\\Faktura\\P_6'


\rozlvat_dop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła uzupełniająca nowe pole ROZLVAT.DOP
::----------------------------------------------------------------------------------------------------------------------
_liczba:=0;
ROZLVAT.cntx_psh(); DOK.cntx_psh();
ROZLVAT.index('S1'); ROZLVAT.prefix();
{? ROZLVAT.first()
|| {! |?
      {? ROZLVAT.DOP=date(0,0,0)
      || DOK.use(ref_name($ROZLVAT.DOK));
         DOK.prefix();
         ROZLVAT.DOK();
         ROZLVAT.DOP:=DOK.DOP;
         {? ROZLVAT.put(1) || _liczba+=1 ?}
      ?};
      ROZLVAT.next()
   !}
?};
ROZLVAT.cntx_pop(); DOK.cntx_pop();
{? _liczba
|| __UPG.msg('Zaktualizowano zawieszone dokumenty VAT. Ilość zmodyfikowanych rekordów: %1.'[$_liczba])
|| __UPG.msg('Nie stwierdzono potrzeby aktualizacji zawieszonych dokumentów VAT do rozliczenia')
?};
1


\rozlvat_dop_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Formuła uzupełniająca nowe pole ROZLVAT.DOP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja daty operacji w dokumentach VAT zawieszonych do rozliczenia'


\PAL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizuje pola domyśłny status dostawy dla palety
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec');
__lrec:=0;
_msk:=PAL.names();
PAL.cntx_psh();
_msk.clear();
{? _msk.first()
||
   {!
   |? PAL.use(_msk.NAME);
      PAL.prefix();
      PAL.for_each("
         __lrec+=1;
         exec('statsPAL','magdok_palety',PAL.ref())
      ");
      _msk.next()
   !}
?};
PAL.cntx_pop();

__UPG.msg('Zaktualizowano status dostawy dla palet.');
__UPG.msg('Przetworzono: %1 zapisów.'[$__lrec]);

obj_del(_msk);
VAR_DEL.delete('__lrec');
_res


\PAL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Aktualizuje pola domyśłny status dostawy dla palety - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja statusu dostawy dla palety'


\dokum_to_dokumz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [23.25]
:: OPIS: Ujednolicenie obsługi załączników w dokumentach księgowych z obsługą w dokumentach sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=DOKUM.names();
_msk.clear();
DOKUM.cntx_psh();
{? _msk.first()
|| {!
   |? DOKUM.use(_msk.NAME);
      DOKUM.index('DOKUM');
      DOKUM.prefix(REF.FIRMA,'doku');
      {? DOKUM.first()
      || {!
         |? __lrec+=1;
            _put:=0;
            _ref_dok:=DOKUM.ref();
            {? DOKUM.DOKUM<>null()
            || DOKUMZ.cntx_psh();
               DOKUMZ.index('DOK');
               DOKUMZ.prefix();
               DOKUMZ.blank();
               DOKUMZ.DOK:=_ref_dok;
               DOKUMZ.DOKUM:=DOKUM.DOKUM;
               DOKUMZ.NAZWA:=DOKUM.bl_info('DOKUM','NAME');
               DOKUMZ.add();
               DOKUMZ.cntx_pop()
            ?};
            {? DOKUM.DOKR=''
            || DOKUM.DOKR:=DOKUM.REFSQL;
               _put:=1
            ?};
            {? DOKUM.TYP='I'
            || DOKUM.TYP:='D';
               _put:=1
            ?};
            {? _put
            || __lmod+=1;
               DOKUM.put()
            ?};
            DOKUM.next()
         !}
      ?};
      _msk.next()
   !}
?};
DOKUM.cntx_pop();
__UPG.msg('Utworzono załącznik główny (DOKUMZ) do dokumentu (DOK).');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\dokum_to_dokumz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Ujednolicenie obsługi załączników w dokumentach księgowych z obsługą w dokumentach sprzedaży - opis
::----------------------------------------------------------------------------------------------------------------------
'Utworzonie załącznika głównego (DOKUMZ) do dokumentu (DOK)'


\mwa_wnioski
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodanie metod API dla PORTAL_WNIOSKI
::----------------------------------------------------------------------------------------------------------------------
exec('init','sync_id');
__UPG.msg('Zaktualizowano identyfikatory rekordów dla portalu.');


_obj:=exec('portal_hr_obj','upgrade_2226');

:: PORTAL_HR_CFG
_obj.add('PORTAL_HR_CFG','Wysyłanie','ETYPY','chr_RequestTypeParDefParamModify',
   'Wysłanie pól wyświetlanych w szczegółach wniosku inTerm.',,'chr_RequestTypeParDefParamModify',,3
);

:: PORTAL_WNIOSKI
_obj.add('PORTAL_WNIOSKI','Uruchomienie','EDOKUM','chr_PersonRequestModify',
   'Wysyłanie wniosków.',,'chr_PersonRequestModify'
);
_obj.add('PORTAL_WNIOSKI','Uruchomienie','EDOKUM','cseod_AcceptanceHeaderModify',
   'Wysyłanie ścieżki akceptacji wniosków.',,'cseod_AcceptanceHeaderModify'
);
1


\mwa_wnioski_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodanie metod API dla PORTAL_WNIOSKI - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja identyfikatorów rekordów na portalu.
Dołączenie metod API do:
- PORTAL_HR_CFG
- PORTAL_WNIOSKI'


\grvat
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Aktualizacja znacznika dokument wewnetrzny na DOK_REJ
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
DOK_REJ.cntx_psh();
DOK_REJ.prefix();
DOK_REJ.for_each("{? DOK_REJ.WEW='' || DOK_REJ.WEW:='N'; __lmod+=DOK_REJ.put(); __lrec+=1 || __lrec+=1 ?} ");
DOK_REJ.cntx_pop();
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\grvat_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [23.25]
:: OPIS: Aktualizacja znacznika dokument wewnetrzny na DOK_REJ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika dokument wewnetrzny na DOK_REJ'


\ofe_to_ofp
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przeniesienie wartości do pozycji oferty w celu podglądu danych.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
OFE.cntx_psh(); OFE.prefix();
OFP.cntx_psh(); OFP.prefix();
M.cntx_psh();M.prefix();

OFP.for_each("
   __lrec+=1;
   _put:=0;
   {? OFP.KH_N=null()   || OFP.KH_N:=OFP.OFE().KH;  _put:=1 ?};
   {? OFP.KH_ODB=null() || OFP.KH_ODB:=OFP.OFE().ODB; _put:=1 ?};
   {? OFP.JM=null()     || OFP.JM:=OFP.M().J;   _put:=1 ?};
   {? _put              || {? OFP.put(1) || __lmod+=1 ?} ?}
");

OFE.cntx_pop();
OFP.cntx_pop();
M.cntx_pop();

__UPG.msg('Przeniesienie wartości do pozycji oferty w celu podglądu danych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

VAR_DEL.delete('__lrec','__lmod');
_res


\ofe_to_ofp_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Przeniesienie wartości do pozycji oferty w celu podglądu danych.
::----------------------------------------------------------------------------------------------------------------------
'Przeniesienie wartości do pozycji oferty w celu podglądu danych.'


\etypy_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizacja typów wniosków/dokumentów w obiegu
::----------------------------------------------------------------------------------------------------------------------
ETYPY.cntx_psh();
ETYPY.trig_off('*','*');
ETYPY.index('UNIK_WP'); ETYPY.prefix();
_licznik:=0;
_all:=0;
{? ETYPY.first()
|| {! |?
      _all+=1;
      {? ETYPY.ZAPOT
      || ETYPY.CZ_ZAPOT:=1
      || ETYPY.CZ_ZAPOT:=0
      ?};
      {? ETYPY.put(1) || _licznik+=1 ?};
      ETYPY.next()
   !}
?};
ETYPY.trig_on('*','*');
ETYPY.cntx_pop();
__UPG.msg('Przetworzono: %1 typów, z czego zmodyfikowano: %2 zapisów.'[$_all,$_licznik]);
1


\etypy_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizacja typów wniosków/dokumentów w obiegu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów dokumentów i wniosków w obiegu'


\edokum_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizacja dokumentów i wniosków w obiegu
::----------------------------------------------------------------------------------------------------------------------
EDOKUM.cntx_psh();
EDOKUM.trig_off('*','*');
_licznik:=0;
_all:=0;
_tab:=EDOKUM.names();
{? _tab.first()
|| {! |?
      EDOKUM.use(_tab.NAME); EDOKUM.prefix();
      {? EDOKUM.first()
      || {! |?
            {? EDOKUM.ZAM=''
            || EDOKUM.ZAM:='N';
               _licznik+=1;
               EDOKUM.put(1)
            ?};
            _all+=1;
            EDOKUM.next()
         !}
      ?};
      _tab.next()
   !}
?};
EDOKUM.trig_on('*','*');
EDOKUM.cntx_pop();
__UPG.msg('Przetworzono: %1 rekordów, z czego zmodyfikowano: %2 zapisów.'[$_all,$_licznik]);
1


\edokum_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizacja dokumentów i wniosków w obiegu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja dokumentów i wniosków w obiegu'


\folp_std
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Standaryzacja formuł obliczeniowych list płac.
::----------------------------------------------------------------------------------------------------------------------
exec('folp_std_upd','lista_plac');
1


\folp_std_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Standaryzacja formuł obliczeniowych list płac - opis.
::----------------------------------------------------------------------------------------------------------------------
'Standaryzacja formuł obliczeniowych list płac.\n\n'
'Zadanie przeprowadza analizę treści formuł obliczeniowych list płac pod kątem\n'
'zgodności ich treści z formułami zapisanymi w odpowiednich plikach ppl_formuly?.fml.\n\n'
'Treść formuły klienta porównywana jest z formułami zarówno w pliku właściwym\n'
'dla typu formuły jak i plikiem formuł wspólnych. Przykładowo standaryzacja formuł typu U\n'
'będzie polegała na porównaniu treści formuł zapisanych w tabeli z formułami w plikach\n'
'ppl_formulyu.fml i ppl_formuly.fml.\n\n'
'Wynik analizy jest udostępniany w oknie wertowania. Na listach prezentowane są tylko te formuły,\n'
'których treść udało się dopasować do treści formuł standardowych.'


\par_skid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja listy parametrów
::----------------------------------------------------------------------------------------------------------------------
exec('PAR_SKID','object');

PAR_SKID.set(463,2,0,'','365',
             'Liczba dni wstecz dla sprawdzania wniosków o zapotrzebowanie (względem daty rejestracji wniosku)');
PAR_SKID.ae_set(463,"
   {? #PARAMETR.TRESC<0
   || FUN.emsg('Wartość parametru nie może być mniejsza od 0.')
   |? #PARAMETR.TRESC>36500
   || FUN.emsg('Wartość parametru nie może przekraczać 36500')
   || 1
   ?}
");
PAR_SKID.set(464,5,1,'','T',
             'Zakładka Zapotrzebowania (bez akc.) widoczna w obszarze Faktury w obiegu','''T - Tak'',''N - Nie''');

PAR_SKID.set(466,8,0,'','BN','SEOD: Typ wartości w Centrum akceptacji');
PAR_SKID.f3_set(466,"exec('par_wart','portal_seod','F3')");
PAR_SKID.ae_set(466,"exec('par_wart','portal_seod','AE')");

__UPG.msg('Zaktualizowano listę parametrów');
1


\par_skid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Aktualizacja listy parametrów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja listy parametrów.'


\mwa_faktury
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodanie metod API dla PORTAL_SEOD_CFG
::----------------------------------------------------------------------------------------------------------------------
exec('init','sync_id');
__UPG.msg('Zaktualizowano identyfikatory rekordów dla portalu.');


_obj:=exec('portal_hr_obj','upgrade_2226');

:: PORTAL_SEOD_CFG
_obj.add('PORTAL_SEOD_CFG','Usuwanie','SLO','cseod_PaymentTypeTenantDelete',
   'Sposób płatności'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','SLO','cseod_PaymentTypeTenantModify',
   'Sposób płatności'
);

{? XINFO.SLP=null
|| exec('czytaj','#stalesys',,XINFO,'SLP')
?};
{? XINFO.SLP
|| SLO.cntx_psh();
   SLO.index('SL');
   SLO.prefix(XINFO.SLP);
   {? SLO.first()
   || {!
      |? SLO.put(,1);
         SLO.next()
      !}
   ?};
   SLO.cntx_pop();
   __UPG.msg('Oznaczono sposoby płatności do ponownej wysyłki na portal.')
?};

1


\mwa_faktury_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Dodanie metod API dla PORTAL_SEOD_CFG - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja identyfikatorów rekordów na portalu.\n'
'Dołączenie metod API do:\n'
'- PORTAL_SEOD_CFG\n'
'Oznaczenie sposobów płatności do ponownej wysyłki.'


\potwZAM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia zapisy dla ZDP_POZ
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');

__lrec:=__lmod:=0;
_oddz:=ST.ODDZ;
_msk:=ZDP_POZ.names();
exec('openzd_psh','open_tab');
_msk.clear();
{? _msk.first()
|| {!
   |? exec('openzd','open_tab',_msk.NAME+3);
      ST.ODDZ:=1+(_msk.NAME+3);
      ZDP_POZ.prefix();
      ZDP_POZ.for_each("
         _put:=0;
         __lrec+=1;
         {? ZDP_POZ.D_WYS=date(0,0,0)
         || _put:=1;
            ZDP_POZ.D_WYS:=ZDP_POZ.ZDP_NAG().D_WYS;
            ZDP_POZ.RR:=ZDP_POZ.D_WYS~1;
            ZDP_POZ.MM:=ZDP_POZ.D_WYS~2
         ?};
         {? _put || {? ZDP_POZ.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
exec('openzd_pop','open_tab');
__UPG.msg('Zaktualizowano zapisy dla pozycji potwierdzeń zamówień dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);
ST.ODDZ:=_oddz;

VAR_DEL.delete('__lrec','__lmod');
_res


\potwZAM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [23.25]
:: OPIS: Uzupełnia zapisy dla ZDP_POZ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów dla pozycji potwierdzeń zamówień dostaw.'


\xinfo_base_id_repair
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Naprawa zmiennej XINFO.BASE_ID
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_msg:=exec('xinfo_base_id_repair','sl_api');
__UPG.msg('Zaktualizowano wspisy dla zmiennej XINFO.BASE_ID');
__UPG.msg(_msg);
_res


\xinfo_base_id_repair_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Naprawa zmiennej XINFO.BASE_ID - opis
::----------------------------------------------------------------------------------------------------------------------
'Naprawienie zmiennej XINFO.BASE_ID'


\tpp_gop_ezam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Formuła dodaje czynność TPP_GOP_EZAM do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:='\'TPP_GOP_DRED\'';
_uid:='TPP_GOP_EZAM';
exec('actionRoleAction','upgrade',_uid,_cr)


\tpp_gop_ezam_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Formuła dodaje czynność TPP_GOP_EZAM do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "TPP_GOP_EZAM - Zamknięcie grupy operacji" do odpowiednich ról'


\tte_pzl_pprz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Formuła dodaje czynność TTE_PZL_PPRZ do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'TTE_PZL_DPRZ','TTE_PZL_DPZG'";
_uid:='TTE_PZL_PPRZ';
exec('actionRoleAction','upgrade',_uid,_cr)


\tte_pzl_pprz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [23.25]
:: OPIS: Formuła dodaje czynność TTE_PZL_PPRZ do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "TTE_PZL_PPRZ - Przegląd przewodników prod." do odpowiednich ról'


\sync_mwa_blmt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Dopisuje definicje nowych metod dla Businesslink
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_res*=exec('sync_mwa_blmt_core','upgrade_2325','BLMT_DOCUMENT_SEND','BLMT_DOCUMENT_GETINFO');
_res*=exec('sync_mwa_blmt_core','upgrade_2325','BLMT_DOCUMENT_SEND','BLMT_DOCUMENT_SEND:batch');
_res*=exec('sync_mwa_blmt_core','upgrade_2325','BLMT_DOCUMENT_SEND:batch','BLMT_DOCUMENT_SEND_GO');
_res*=exec('sync_mwa_blmt_core','upgrade_2325','BLMT_DOCUMENT_GETINFO','BLMT_DOCUMENT_GETINFO:batch');
_res*=exec('sync_mwa_blmt_core','upgrade_2325','BLMT_DOCUMENT_GETINFO:batch','BLMT_SIGNATURE_SEND:batch');
_res


\sync_mwa_blmt_core
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Dopisuje definicje nowych metod dla Businesslink - część wspólna
::   WE: _a - metoda, za którą umieścić nową metodę
::       _b - nowa metoda
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_pd_sym:='BLMT';
_method1:=_a;
_method2:=_b;

SYNC_MWA.cntx_psh();
SYNC_MWA.index('PD_METH');
SYNC_MWA.prefix(_pd_sym,_method2,);
{? ~SYNC_MWA.first()
|| SYNC_MWA.prefix(_pd_sym,_method1,);
   {? SYNC_MWA.first()
   || _lp:=SYNC_MWA.LP;
::    Przenumerowanie z pozostawieniem dziury na nową metodę
      SYNC_MWA.cntx_psh();
      SYNC_MWA.index('TREE1');
      SYNC_MWA.prefix(SYNC_MWA.PARENT);
      {? SYNC_MWA.last()
      || {!
         |? {? SYNC_MWA.LP>_lp
            || SYNC_MWA.LP+=1;
               SYNC_MWA.put()
            ?};
            SYNC_MWA.prev()
         !}
      ?};
      SYNC_MWA.cntx_pop();
::    Dodanie nowej metody
      SYNC_MWA.LP+=1;
      SYNC_MWA.METHOD:=_method2;
      SYNC_MWA.prefix();
      {? SYNC_MWA.add()
      || __UPG.msg('Dopisano metodę %1.'[_method2])
      || __UPG.msg('Nie dopisano metody %1.'[_method2]);
         _res:=0
      ?}
   || __UPG.msg('Brak metody %1, nie można dopisać metody %2.'[_method1,_method2]);
      _res:=-1
   ?}
|| __UPG.msg('Metoda %1 była już dopisana.'[_method2])
?};
SYNC_MWA.cntx_pop();

_res


\sync_mwa_blmt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Dopisuje definicje nowych metod - opis
::----------------------------------------------------------------------------------------------------------------------
'Dopisanie definicji metod:\n'
'BLMT_DOCUMENT_GETINFO\n'
'BLMT_DOCUMENT_SEND:batch\n'
'BLMT_DOCUMENT_GETINFO:batch\n'
'BLMT_DOCUMENT_SEND_GO\n'
'BLMT_SIGNATURE_SEND:batch'


\usersrej_bperm_up
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RO] [23.25]
:: OPIS: Uprawnienia do rejestrów. Aktualizuje zapisy dla B_PERM_U - Uprawnienia użytkownika do danych.
::       Weryfikacja dla każdego użytkownika.
::----------------------------------------------------------------------------------------------------------------------
_perm:=null();
B_PERM.cntx_psh();
B_PERM.index('NAME'); B_PERM.prefix();
{? B_PERM.find_key('FREJ')
|| _perm:=B_PERM.ref()
?};
B_PERM.cntx_pop();

_info:=0;
USERS.cntx_psh();
USERS.prefix();
B_PERM_U.cntx_psh();
B_PERM_U.index('USER');
_loop:=USERS.first();
{!
|? _loop
|! B_PERM_U.prefix(REF.FIRMA,USERS.ref(),_perm);
   {? B_PERM_U.first()
   || B_PERM_U.ACCESS:='T';
      B_PERM_U.FULL:={? exec('usersrej_check','fks',$USERS.ref()) || 'T' || 'N' ?};
      _info+=B_PERM_U.put()
   ?};
   _loop:=USERS.next()
!};
B_PERM_U.cntx_pop();
USERS.cntx_pop();
{? _info
|| __UPG.msg('Zaktualizowano zapisy uprawnień do rejestrów.')
|| __UPG.msg('Aktualizacja nie była wymagana.')
?};
1


\usersrej_bperm_up_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [RO] [23.25]
:: OPIS: Uprawnienia do rejestrów. Aktualizuje zapisy dla B_PERM_U - Uprawnienia użytkownika do danych. Opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów dla uprawnień użytkownika do danych - uprawnienia do rejestrów.'


\dekstal_rejodd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AFI] [23.25]
:: OPIS: Dla schematu dziedzinowego przypisuje rejestr ze wskazaniem na poprawną jednostkę księgową
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');

__lrec:=__lmod:=0;
REJ.cntx_psh(); DEK_STAL.cntx_psh();
DEK_STAL.prefix(); REJ.index('KOD'); REJ.prefix();
DEK_NAG.cntx_psh(); DEK_NAG.prefix();
DOK_REJ.cntx_psh(); DOK_REJ.prefix();
DEK_STAL.for_each("
   _put:=0;
   __lrec+=1;
   {? DEK_STAL.REJ & REJ.find_key(DEK_STAL.DEK_NAG().ROK,DEK_STAL.ODD,DEK_STAL.REJ().KOD)
   || DEK_STAL.REJ:=REJ.ref();
      {? DEK_STAL.put() || __lmod+=1 ?}
   |? ~DEK_STAL.REJ & REJ.find_key(DEK_STAL.DEK_NAG().ROK,DEK_STAL.ODD,DEK_STAL.DOK_REJ().REJ().KOD)
   || DEK_STAL.REJ:=REJ.ref();
      {? DEK_STAL.put() || __lmod+=1 ?}
   ?}
");
REJ.cntx_pop(); DEK_STAL.cntx_pop(); DEK_NAG.cntx_pop(); DOK_REJ.cntx_pop();
__UPG.msg('Zaktualizowano zapisy dla schematów dziedzinowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);

VAR_DEL.delete('__lrec','__lmod');
_res


\dekstal_rejodd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AFI] [23.25]
:: OPIS: Dla schematu dziedzinowego przypisuje rejestr ze wskazaniem na poprawną jednostkę księgową - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja jednostek księgowych dla schematów dziedzinowych.'


\sync_def_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Import definicji synchronizacji dla przeznaczeń danych PORTAL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',,,1,
   exec('sync_def_portal_desc','upgrade_2325'),0,,
   'SYNC_DEF_PORTAL,SYNC_DEF_PORTAL_CFG,SYNC_DEF_PORTAL_WNIOSKI,SYNC_DEF_PORTAL_SEOD'
);

_choice:=FUN.choice(
   'Czy dane zostały zaimportowane?\n\n'
   ' * Tak = zmiana statusu zadania na "Wykonane"\n'
   ' * Nie = pozostawienie zadanie do ponownego uruchomienia\n'
   ' * Anuluj = rezygnacja z wykonywania, zmiana statusu zadania na "Wykonane"\n'@
   ,2,'Tak'@,'Nie'@
);

USERS.cntx_psh();
__UPG.msg('Użytkownik: %1 | Data: %2 | Godzina: %3'[OPERATOR.USER().KOD,date()$1,time()$3]);
USERS.cntx_pop();

{? _choice=1
|| __UPG.msg('Zadanie zostało wykonane.')
|? _choice=2
|| __UPG.msg('Zadanie zostało odłożone do ponownego uruchomienia.')
|? _choice=0
|| __UPG.msg('Zrezygnowano z wykonania zadania.')
|| __UPG.msg('Nierozpoznana odpowiedź.')
?};

{? _choice<=1
|| 1
|| -1
?}


\sync_def_portal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Import definicji synchronizacji dla przeznaczeń danych PORTAL - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Import definicji wymiany danych dla przeznaczeń danych: PORTAL, PORTAL_CFG, PORTAL_WNIOSKI, PORTAL_SEOD.'


\cluster_cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Modyfikacja pliku cluster.cfg
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice(
   exec('cluster_cfg_desc','upgrade_2325')+'\n\n'+
   'Czy plik cluster.cfg został zmodyfikowany?\n'
   ' * Tak = zmiana statusu zadania na "Wykonane"\n'
   ' * Nie = pozostawienie zadanie do ponownego uruchomienia\n'
   ' * Anuluj = rezygnacja z wykonywania, zmiana statusu zadania na "Wykonane"\n'@
   ,2,'Tak'@,'Nie'@
);

USERS.cntx_psh();
__UPG.msg('Użytkownik: %1 | Data: %2 | Godzina: %3'[OPERATOR.USER().KOD,date()$1,time()$3]);
USERS.cntx_pop();

{? _choice=1
|| __UPG.msg('Zadanie zostało wykonane.')
|? _choice=2
|| __UPG.msg('Zadanie zostało odłożone do ponownego uruchomienia.')
|? _choice=0
|| __UPG.msg('Zrezygnowano z wykonania zadania.')
|| __UPG.msg('Nierozpoznana odpowiedź.')
?};

{? _choice<=1
|| 1
|| -1
?}


\cluster_cfg_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Modyfikacja pliku cluster.cfg - opis
::----------------------------------------------------------------------------------------------------------------------
'W wersji 23.25 został wprowadzony mechanizm kolejek technicznych.\n'
'Jeżeli w pliku cluster.cfg nie ma wpisów sterujących uruchamianiem kolejek technicznych, to nie będą one działały.\n'
'Należy sprawdzić oraz w miarę potrzeby zmodyfikować plik cluster.cfg, a następnie zrestartować serwer MacroBASE.\n'
'Nowe zapisy w cluster.cfg powinny uruchamiać kolejki o nazwie \'blink\' w każdej firmie.\n'
'Przykładowy zapis, który tworzy kolejkę \'blink\' w firmie \'001\':\n\n'
'[blink001]\n'
'name=merit\n'
'path=[KATALOG_BAZOWY]\\pth\\f001.pth\n'
'hidden=1\n'
'cgi_html=cgi_blink.htm\n'
'cgi_output=merit_blink.log\n'
'app_par1=001\n'
'cgi_group=f001\n\n'
'Analogicznie należy zmodyfikować definicję grupy, dopisując do niej nową aplikację:\n\n'
'f001=merit001;event001;mail001;task001;blink001'


\akt_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizacja słownika procedur szczególnych.
::----------------------------------------------------------------------------------------------------------------------
exec('init_slo','jpk_v');
__UPG.msg('Zaktualizowano słownik procedur szczególnych')


\akt_proc_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizacja słownika procedur szczególnych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słownika procedur szczególnych'


\rub_atr_ER23080037
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [23.25]
:: OPIS: Poprawka ER/WRT/XP/23.25/2308/0037: Wycofanie urlopu rehabilitacyjnego
::       dla wypłaconej kwoty 7134 Wynagr. rehabil. brak rubryki z korektą tego składnik co powoduje nadpłatę.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();

{? ~(+exec('import','rubryki','g','7237',0,0))
|| _txt:='Dodanie rubryki "7237 Kor. wyn. rehabil." zakończone powodzeniem. ';
   exec('add_attr','rubatr',423,4231,'Korekta wynagrodzenia urlopowego',1,
         'Korekta wynagrodzenia dla urlopów.',,439);
   exec('add_attr','rubatr',423,4232,'Korekta wynagrodzenia za urlop rehabilitacyjny',1,
         'Korekta wynagrodzenia za urlop rehabilitacyjny.',,7237)
|| _txt:='Dodanie rubryki "7237 Kor. wyn. rehabil." zakończyło się niepowodzeniem.';
   _result:=-1
?};
__UPG.msg(_txt);
R.trig_on('*','*');
_result


\rub_atr_ER23080037_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [23.25]
:: OPIS: Poprawka ER/WRT/XP/23.25/2308/0037: Wycofanie urlopu rehabilitacyjnego
::       dla wypłaconej kwoty 7134 Wynagr. rehabil. brak rubryki z korektą tego składnik co powoduje nadpłatę.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/23.25/2308/0037\n'
'Dodanie rubryki: "7237 Kor. wyn. rehabil."\n'
'Dodanie atrybutów: "4231 Korekta wynagrodzenia urlopowego" oraz "4232 Korekta wynagrodzenia za urlop rehabilitacyjny"'


\EDOK_ATR_b_war
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: ER/WRT/XP/23.25/2308/0063 - oświadczenie pracy zdalnej - brak akceptacji w zdarzeniach kadrowych
::       Formuła naprawiająca błędne rekordy EDOK_ATR o typie TAK/NIE
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
_tab:=sql('select EDOK_ATR.REFERENCE as REF, EDOK_ATR.WAR, TAT.TYP from @EDOK_ATR '
          'join TAT using (EDOK_ATR.TAT, TAT.REFERENCE) '
          'where TAT.TYP=\'B\' and EDOK_ATR.WAR like \'_\'');
{? _tab.first()
|| {!
   |? EDOK_ATR.cntx_psh();
      EDOK_ATR.prefix();
      {? EDOK_ATR.seek(_tab.REF,ref_name(_tab.REF),1)
      || _put:=0;
         {? EDOK_ATR.WAR='T'
         || EDOK_ATR.WAR:='Tak';
            _put:=1
         |? EDOK_ATR.WAR='N'
         || EDOK_ATR.WAR:='Nie';
            _put:=1
         ?};
         {? _put || {? EDOK_ATR.put() || _result+=1 ?} ?}
      ?};
      EDOK_ATR.cntx_pop();
      _tab.next()
   !}
?};
{? _result>0
|| __UPG.msg('Naprawiono %1 z %2 błędnych zapisów.'[$_result,$_tab.size()])
|| {? _tab.size>0
   || __UPG.msg('Operacja zakończyła się niepowodzeniem.')
   || __UPG.msg('Nie znaleziono nieprawidłowych rekordów do modyfikacji.');
      _result:=1
   ?}
?};
VAR_DEL.delete('_tab');
_result


\EDOK_ATR_b_war_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: ER/WRT/XP/23.25/2308/0063 - oświadczenie pracy zdalnej - brak akceptacji w zdarzeniach kadrowych
::       Formuła naprawiająca błędne rekordy EDOK_ATR o typie TAK/NIE - opis
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/23.25/2308/0063:  Formuła naprawiająca błędne rekordy informacji dodatkowych dokumentu w obiegu \n'
'(EDOK_ATR) - o typie TAK/NIE'


\atrybut_zusz3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Dodanie atrybutów płacowych (ER/WRT/XP/23.25/2309/0010).
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

{? __RUB.ref(436) & __RUB.ref(438) & __RUB.ref(442) & __RUB.ref(443) & __RUB.ref(7132) & __RUB.ref(7157) &
   __RUB.ref(7133) & __RUB.ref(7134)
|| exec('add_attr','rubatr',652,6525,'Uwzględniane w miesiącu wystąpienia',1,
        'Nieobecności wykazywane na raporcie w miesiącu ich wystąpienia.');
        exec('add_attr','rubatr',6525,65251,'Niebecności uwzględniane w miesiącu wystąpienia',1,
        'Nieobecności, za które wynagrodzenie jest wykazywane na raporcie w miesiącu ich wystąpienia.',,
        11,12,13,22,7132,7157,7133);
        exec('add_attr','rubatr',6525,65252,'Wynagr. za nieob. uwzględniane w miesiącu wystąp.',1,
        'Wynagrodzenia za nieobecności wykazywane na raporcie w miesiącu ich wystąpienia.',,436,438,442,443,7134)
|| _result:=-1
?};

{? _result=1
|| __UPG.msg('Zakończono aktualizację atrybutów składników płacowych.')
|| __UPG.msg('Brak rubryk płacowych.')
?};

_result


\atrybut_zusz3_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Dodanie atrybutów płacowych ER/WRT/XP/23.25/2309/0010 - opis.
::----------------------------------------------------------------------------------------------------------------------
'Zadanie do poprawki ER/WRT/XP/23.25/2309/0010 - ER/WRT/XP/22.26/2302/0082 : Zaświadczenie płatnika składek ZUS '
'Z-3-rozliczona nieobecność choroba wynagrodzenie 29/12/2022-31/12/2022 ze średnią urlopową z wypłata inny mc '
'niż urlop\n'
'uzupełniające listę atrybutów o atrybuty:\n'
'* 6525 Uwzględniane w miesiącu wystąpienia\n'
'** 65251 Niebecności uwzględniane w miesiącu wystąpienia\n'
'** 65252 Wynagr. za nieob. uwzględniane w miesiącu wystąp.'


\upgrade_act_obe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja czynności OBE
::----------------------------------------------------------------------------------------------------------------------
exec('upgrade_act','upgrade_2325','OBE')


\upgrade_act_obe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja czynności OBE - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja czynności dziedziny OBE'


\rubryki_ER23100009
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Poprawka ER/WRT/XP/23.25/2310/0009 - ER/WRT/XP/22.26/2310/0002 : ER/WRT/XP/12.51/2309/0031 : Plik rubryki.dfg
::       błędy językowe w komentarzu do składnika 7015 i 7016
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();

{? ~(+exec('import','rubryki','g','7015,7016',1,0))
|| _txt:='Aktualizacja rubryk "7015 L.g. dod. odb. 50%" oraz "7016 L.g. dod. odb. 100%" zakończona powodzeniem.'
|| _txt:='Aktualizacja rubryk "7015 L.g. dod. odb. 50%" oraz "7016 L.g. dod. odb. 100%" zakończona niepowodzeniem.';
   _result:=-1
?};
R.trig_on('*','*');
_result


\rubryki_ER23100009_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Poprawka ER/WRT/XP/23.25/2310/0009 - ER/WRT/XP/22.26/2310/0002 : ER/WRT/XP/12.51/2309/0031 : Plik rubryki.dfg
::       błędy językowe w komentarzu do składnika 7015 i 7016
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/23.25/2310/0009\n'
'Aktualizacja rubryk "7015 L.g. dod. odb. 50%" oraz "7016 L.g. dod. odb. 100%".'


\TRANS_PL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [RR.xx]
:: OPIS: Aktualizacja kodów tłumaczeń
::----------------------------------------------------------------------------------------------------------------------
exec('init','tlumaczenia');
__UPG.msg('Zaktualizowano kody tłumaczeń.');
1


\TRANS_PL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [RR.xx]
:: OPIS: Aktualizacja kodów tłumaczeń
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kodów tłumaczeń.'


\ATR_DG1
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [23.25]
:: OPIS: Uzupełnia atrybuty systemowe na potrzeby sprawozdania GUS DG1
::----------------------------------------------------------------------------------------------------------------------
exec('atrplaimex_decl','rubatr');
_atr:=obj_new(@.CLASS.AtrPlaImEx);
_result:=_atr.add_attr('g','634',1);
{? _result<>''
|| __UPG.msg('Dodawanie atrybutów nie powiodło się.\n'+_result);
   return(-1)
|| __UPG.msg('Dodawanie atrybutów zakończone.');
   return(1)
?}


\ATR_DG1_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PM [23.25]
:: OPIS: Uzupełnia atrybuty systemowe na potrzeby sprawozdania GUS DG1 - opis
::----------------------------------------------------------------------------------------------------------------------
'Zadanie dodające atrybuty systemowe z których korzystają formuły sprawozdania GUS DG-1.'


\procesy_ER_2310_0028
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Import procesów poprawka
::       ER/WRT/XP/23.25/2310/0028 - ER/WRT/XP/22.26/2310/0014 : Czynność PKD_EZK_AOON w procesie PER_OON_AUTO
::       uruchomiona serwisowo zgłasza błędy do log
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('procesy','upgrade','22.26','PER_OON_AUTO','PER_');
{? _result>0
|| _result:=0;
   {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack(0).name])
   || __UPG.msg('Zakończono import procesów z dziedziny PER');
      _result:=1
   || _result:=-1
   ?}
?};
_result


\procesy_ER_2310_0028_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [23.25]
:: OPIS: Import procesów poprawka
::       ER/WRT/XP/23.25/2310/0028 - ER/WRT/XP/22.26/2310/0014 : Czynność PKD_EZK_AOON w procesie PER_OON_AUTO
::       uruchomiona serwisowo zgłasza błędy do log - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/23.25/2310/0028 - ER/WRT/XP/22.26/2310/0014 : Czynność PKD_EZK_AOON w procesie PER_OON_AUTO '
'uruchomiona serwisowo zgłasza błędy do log.\n'
'Pozwala zaimportować zmienione procesy dziedziny PKD:\n'
' ● PER_OON_AUTO - Pow. pracowników o końcu orzecz. o niepełn.\n'


\por_work_place
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: ER/WRT/XP/23.25/2309/0037: ER/WRT/XP/12.51/2307/0024 : Miejsca pracy - komunikat chr_WorkPlaceModify
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_txt:='';
{? ~exec('lic','#b_domain','POR')
|| _ret:=-1;
   _txt:='Brak licencji na Portal. Aktualizacja miejsc pracy nie jest wymagana.'
|| SLO_TYP.cntx_psh();
   SLO_TYP.index('SYMBOL');
   {? SLO_TYP.find_key('MIEJSCPR')
   || _slo_typ:=SLO_TYP.ref();
      SLO_NAZ.cntx_psh();
      SLO_NAZ.index('NAZWA');
      SLO_NAZ.prefix(_slo_typ,);
      {? SLO_NAZ.first()
      || _result:=exec('data_delete_cntx','portal_engine','PORTAL','portal.mwac','chr_WorkPlaceDelete','SLO_NAZ');
         {? _result.STATUS='OK'
         || {! |? _ret*=SLO_NAZ.put(,1);
                  SLO_NAZ.next()
            !};
            {? _ret
            || _txt:='Aktualizacja słownika miejsc pracy zakończona powodzeniem.'
            || _txt:='Aktualizacja słownika miejsc pracy zakończona niepowodzeniem.'
            ?}
         |? _result.STATUS='ErrPD' | _result.STATUS='ErrMWA'
         || _ret:=-1;
            _txt:=_result.INFO
         |? _result.STATUS='ErrARG'
         || _ret:=0;
            _txt:=_result.INFO
         ?}
      || _ret:=-1;
         _txt:='Brak rekordów w słowniku miejsc pracy.'
      ?};
      SLO_NAZ.cntx_pop()
   || _ret:=-1;
     _txt:='Nie odnaleziono słownika miejsc pracy.'
   ?};
   SLO_TYP.cntx_pop()
?};
__UPG.msg(_txt);
_ret


\por_work_place_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [23.25]
:: OPIS: ER/WRT/XP/23.25/2309/0037: ER/WRT/XP/12.51/2307/0024 : Miejsca pracy - komunikat chr_WorkPlaceModify - opis
::----------------------------------------------------------------------------------------------------------------------
'Zadanie do poprawki ER/WRT/XP/23.25/2309/0037'
' - ER/WRT/XP/12.51/2307/0024 : Miejsca pracy - komunikat chr_WorkPlaceModify.\n'
'Formuła usuwa z chmury dane dotyczące miejsc pracy (chr_WorkPlaceDelete).\n'
'Następnie putuje wszystkie rekordy słownika MIEJSCPR w celu ponownego wysłania na portal.'


\kal_buff_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/12.51/2304/0005 : Wykonanie czasu pracy (poza okresem)
::----------------------------------------------------------------------------------------------------------------------
_wynik:=0;

_query:=''+"select REFERENCE as REF from KAL_BUFF where KAL_BUFF.GPW='W' and KAL_BUFF.CZY_OKR='N'";
_TAB:=sql(_query);
{? ~_TAB.first()
|| __UPG.msg('Brak danych do naprawy - uruchomienie zadania nie jest wymagane.');
   return(1)
||
   KAL_BUFF.cntx_psh();
   KAL_BUFF.index('PRACDATA');
   KAL_BUFF.prefix();
   {!
   |? {? KAL_BUFF.seek(_TAB.REF,)
      || _okr:=exec('find_okr','grafik',KAL_BUFF.P,KAL_BUFF.DATA,'okr');
         KAL_BUFF.A_OKR:=_okr;
         KAL_BUFF.CZY_OKR:='T';
         _wynik+=KAL_BUFF.put(1)
      ?};
      _TAB.next()
   !};
   KAL_BUFF.cntx_pop()
?};

{? _wynik>0
|| __UPG.msg('Naprawiono %1 z %2 błędnych zapisów.'[$_wynik,$_TAB.size()])
|| __UPG.msg('Nie udało się naprawić danych.')
?};

_wynik


\kal_buff_w_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/12.51/2304/0005 : Wykonanie czasu pracy (poza okresem) - opis
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/12.51/2304/0005 : Wykonanie czasu pracy (poza okresem)'


\krs_napraw_jedn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formuła naprawcza korygująca kursy dla walut mających ustawioną jednostkę 100.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
WAL.cntx_psh(); SLU.cntx_psh(); SLO.cntx_psh(); KRS.cntx_psh();
WAL.index('WAL_KOD'); WAL.prefix();
KRS.use('kursy___');
_licz:=0;
SLU.index('NAZ'); SLU.prefix('WALUTY');
{? WAL.first() & SLU.first()
|| {!
   |? {? WAL.J<>1
      || SLO.index('SL'); SLO.prefix(SLU.ref(),WAL.SYM);
         {? SLO.first()
         || KRS.index('S1'); KRS.prefix(SLO.ref());
            {? KRS.first()
            || {!
               |? _change:=0;
                  {? KRS.KP<>0 & KRS.KP<1
                  || KRS.KP:=KRS.KP*WAL.J;
                     _change:=1
                  ?};
                  {? KRS.SP<>0 & KRS.SP<1
                  || KRS.SP:=KRS.SP*WAL.J;
                     _change:=1
                  ?};
                  {? KRS.SR<>0 & KRS.SR<1
                  || KRS.SR:=KRS.SR*WAL.J;
                     _change:=1
                  ?};
                  {? _change
                  || {? KRS.put() || _licz+=1 || _result:=0 ?}
                  ?};
                  _result & KRS.next()
               !}
            ?}
         ?}
      ?};
      WAL.next()
   !}
?};
WAL.cntx_pop(); SLU.cntx_pop(); SLO.cntx_pop(); KRS.cntx_pop();
{? _result<>0
|| {? _licz>0
   || __UPG.msg('Zaktualizowane rekordy: %1'[$_licz])
   || __UPG.msg('Nie znaleziono rekordów wymagająch aktualizacji')
   ?}
|| __UPG.msg('Błąd podczas wykonywania zadania naprawczego.')
?};
_result


\krs_napraw_jedn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [23.25]
:: OPIS: Formuła naprawcza korygująca kursy dla walut mających ustawioną jednostkę 100. - opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Formuła naprawcza korygująca kursy dla walut mających ustawioną jednostkę 100.'


\fap2dk_clear
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Usuwa nieprawidłowe powiązania pozycji dokumentów sprzedaży z pozycjami dokumentów magazynowych
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;
FAP2DK.cntx_psh();
FAP.cntx_psh();
DK.cntx_psh();
_NAMES:= FAP2DK.names;
{? _NAMES.first
|| {!
   |?
      {? (2+(5-_NAMES.NAME))='sp' & FAP2DK.use(_NAMES.NAME)
      ||
         FAP2DK.prefix();
         FAP2DK.for_each("
            _del:=0;
            {? FAP2DK.FAP<>'' &  FAP.use(ref_name(FAP2DK.FAP)) & (FAP.prefix();FAP.seek(FAP2DK.FAP))
            || {? FAP2DK.IDADD<FAP.IDADD
               || _del:=1;__lmod:=FAP2DK.del(,0)
               ?}
            |? FAP2DK.FAP<>'' &  FAP.use(ref_name(FAP2DK.FAP)) & ~(FAP.prefix();FAP.seek(FAP2DK.FAP))
            || _del:=1;__lmod:=FAP2DK.del(,0)
            ?};
            {? ~_del & FAP2DK.DK<>'' &  DK.use(ref_name(FAP2DK.DK)) & (DK.prefix();DK.seek(FAP2DK.DK))
            || {? FAP2DK.IDADD<DK.IDADD
               || __lmod:=FAP2DK.del(,0)
               ?}
            |? ~_del & FAP2DK.DK<>'' &  DK.use(ref_name(FAP2DK.DK)) & ~(DK.prefix();DK.seek(FAP2DK.DK))
            || __lmod:=FAP2DK.del(,0)
            ?};
            __lrec+=1
            ")
      ?};
      _NAMES.next
   !}
?};
FAP2DK.cntx_pop();
FAP.cntx_pop();
DK.cntx_pop();

__UPG.msg('Usunięto nieprawidłowe powiązania pozycji dokumentów sprzedaży z pozycjami dokumentów magazynowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
_res


\fap2dk_clear_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [23.25]
:: OPIS: Usuwa nieprawidłowe powiązania pozycji dokumentów sprzedaży z pozycjami dokumentów magazynowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Czyszczenie tabeli FAP2DK z nieprawidłowych zapisów'@


\webdok_dokzrodl
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [23.25]
:: OPIS: Uzupełnienie dokumentu utworzonego na tabeli WEB_DOK na podstawie DOKZRODL tabeli DOK
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec');
__lrec:=0;
DOK.cntx_psh(); WEB_DOK.cntx_psh();

_NAMES:=DOK.names;
{? _NAMES.first
|| {!
   |? DOK.use(_NAMES.NAME);
      DOK.index('DOKZRODL');
      DOK.prefix('webdok');
      {? DOK.first()
      || {!
         |? WEB_DOK.use(ref_name(DOK.DOKZRODL));
            WEB_DOK.prefix();
            {? WEB_DOK.seek(DOK.DOKZRODL) & (WEB_DOK.UTW_DOK='' | {? DOK.ZK='T' | DOK.ZP='T'
                                                                  || WEB_DOK.STATUS<>'zaksięgowany'
                                                                  || WEB_DOK.STATUS<>'zadekretowany'
                                                                  ?})
            || WEB_DOK.UTW_DOK:=DOK.uidref();
               {? DOK.ZK='T' | DOK.ZP='T' || WEB_DOK.STATUS:='zaksięgowany' || WEB_DOK.STATUS:='zadekretowany' ?};
               __lrec+=WEB_DOK.put()
            ?};
            DOK.next()
         !}
      ?};
      _NAMES.next()
   !}
?};
WEB_DOK.cntx_pop(); DOK.cntx_pop();
DOK.cntx_psh(); WEB_DOK.cntx_psh();
_wnames:=WEB_DOK.names();
{? _wnames.first()
|| {!
   |? WEB_DOK.use(_wnames.NAME);
      WEB_DOK.index('ARH');
      WEB_DOK.prefix('T');
      {? WEB_DOK.first()
      || {!
         |? {? 4+ref_name(WEB_DOK.UTW_DOK)='doku'
            || DOK.use(ref_name(WEB_DOK.UTW_DOK));
               DOK.prefix();
               {? ~DOK.seek(WEB_DOK.UTW_DOK)
               || WEB_DOK.cntx_psh();
                  WEB_DOK.prefix();
                  WEB_DOK.UTW_DOK:='';
                  WEB_DOK.ARH:='N';
                  WEB_DOK.STATUS:='dodany';
                  __lrec+WEB_DOK.put();
                  WEB_DOK.cntx_pop()
               ?}
            ?};
            WEB_DOK.next()
         !}
      ?};
      _wnames.next()
   !}
?};
WEB_DOK.cntx_pop();
DOK.cntx_pop();

__UPG.msg('Naprawiono %1 zapisów zewnętrznych dokumentów.'[$__lrec]);
VAR_DEL.delete('__lrec');
_res


\webdok_dokzrodl_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [23.25]
:: OPIS: Uzupełnienie dokumentu utworzonego na tabeli WEB_DOK na podstawie DOKZRODL tabeli DOK - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie dokumentu utworzonego dla dokumentów zewnętrznych'


\upgrade_act_obg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja czynności OBG
::----------------------------------------------------------------------------------------------------------------------
exec('upgrade_act','upgrade_2325','OBG')


\upgrade_act_obg_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Aktualizacja czynności OBG - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja czynności dziedziny OBG'


\procesy_personelowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Import procesów personelowych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','23.25','PER_IMF,PER_IMT','PER_') &
exec('procesy','upgrade','2226a','PER_KUM_AUTO','PER_')


\procesy_personelowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Import procesów personelowych - opis
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować nowe procesy dziedziny PER:\n'
' ● PER_IMF - Import danych pracy poza siedzibą firmy\n'
' ● PER_IMT - Import danych kontrola trzeźwości\n'
' ● PER_KUM_AUTO - Cykliczna kontrola kończących się umów prac. (wersja 2226a)\n'


\p_kal_nazw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: ER/WRT/XP/23.25/2310/0081 - Błąd w kalendarzach pracowników po transferze wersji wielofirmowej do Merit ERP.
::----------------------------------------------------------------------------------------------------------------------
_upd:=0;
_err:=0;
_fch:=%255;

KAL_NAZW.trig_off('*','*');
KAL_NAZW.cntx_psh();
KAL_NAZW.clear();
P.cntx_psh();
P.use('pracowni');
P.index('IDADD');
P.prefix();

_loop:=P.first();
{!
|? _loop
|! {? 1+P.KAL().NAZWA=_fch
   || _str:=_fch+$P.ref();
      {? _str<>KAL_NAZW.NAZWA
      || KAL_NAZW.NAZWA:=_str;
         {? KAL_NAZW.put()
         || _upd+=1
         || _err+=1
         ?}
      ?}
   ?};
   _loop:=P.next()
!};

P.cntx_pop();
KAL_NAZW.cntx_pop();
KAL_NAZW.trig_on('*','*');

__UPG.msg('Aktualizacja nazw kalendarzy (zmiany: %1, błędy: %2).'[$_upd,$_err]);

{? _err>0 || -1 || 1 ?}


\p_kal_nazw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: ER/WRT/XP/23.25/2310/0081 - Błąd w kalendarzach pracowników po transferze wersji wielofirmowej do Merit ERP.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/23.25/2310/0081\n'
'Błąd w kalendarzach pracowników po transferze wersji wielofirmowej do Merit ERP.'


\edokum_resend
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Oznaczenie dokumentów do ponownej wysyłki na portal w związku z błędem ER/WRT/XP/23.25/2311/0020
::----------------------------------------------------------------------------------------------------------------------
_ile:=obj_new(3);
{! _ii:=1..obj_len(_ile) |! _ile[_ii]:=0 !};
EDOKUM.trig_off('*','*');
FIRMA.cntx_psh();
FIRMA.index('EWID'); FIRMA.prefix('T');
{? FIRMA.first()
|| ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ');
   PORTALSU.cntx_psh();
   PORTALSU.index('TYP');
   {!
   |? PORTALSU.prefix(FIRMA.ref());
      {? PORTALSU.first()
      || ROK_F.prefix(FIRMA.ref());
         {? ROK_F.find_le(date())
         || _maska:=$(ROK_F.POCZ_ROK~1)+2;
            {!
            |? echo('Analizowanie dokumentów z firmy %1 z roku %2'[FIRMA.SYMBOL,ROK_F.NAZ]);
               EDOKUM.cntx_psh();
               EDOKUM.use('skid_v'+_maska);
               EDOKUM.index('W_PORTAL');
               EDOKUM.prefix(FIRMA.ref());
               {? EDOKUM.first()
               || EDOKOS.cntx_psh();
                  EDOKOS.use('skid_y'+_maska);
                  EDOKOS.index('DISP');
                  {!
                  |? {? EDOKUM.TYP().W_PORTAL<>'' & ETYPY.W_PORTAL<>'N' & -EDOKUM.STATUS='s'
                     || _typ:=EDOKUM.TYPOBIEG().NAZWA;
                        _nr:={? _typ*'faktur' || 1 |? _typ*'wniosków' || 2 || 3 ?};
                        EDOKOS.prefix('S',EDOKUM.ref());
                        _last:={? EDOKOS.last() || EDOKOS.OPER_NUM ?};
                        EDOKOS.prefix('S',EDOKUM.ref(),_last);
                        {? EDOKOS.first()
                        || {!
                           |? _put:=PORTALSU.find_key(_nr,EDOKOS.USERS);
                              _put=0 & EDOKOS.next()
                           !};
                           {? _put & EDOKUM.put(,1)
                           || _ile[_nr]+=1
                           ?}
                        ?}
                     ?};
                     EDOKUM.next()
                  !};
                  EDOKOS.cntx_pop()
               ?};
               EDOKUM.cntx_pop();
               ROK_F.next()
            !}
         ?}
      ?};
      FIRMA.next()
   !};
   PORTALSU.cntx_pop();
   ROK_F.cntx_pop()
?};
FIRMA.cntx_pop();
EDOKUM.trig_on('*','*');
echo();
__UPG.msg('Liczba wniosków do ponownej wysyłki: %1.'[$_ile[2]]);
__UPG.msg('Liczba faktur do ponownej wysyłki: %1.'[$_ile[1]]);
__UPG.msg('Liczba delegacji do ponownej wysyłki: %1.'[$_ile[3]]);
1


\edokum_resend_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Oznaczenie dokumentów do ponownej wysyłki na portal w związku z błędem ER/WRT/XP/23.25/2311/0020
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/23.25/2311/0020 - Na portalu nie pojawia się dokumenty do akceptacji dla użytkowników z uprawniwniami do czynności przeglądania wniosków, delegacji i faktur\n'
'Oznaczenie dokumentów (wniosków/faktur/delegacji) do ponownej wysyłki na portal HR/SEOD'


\upgrade_act_per
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Aktualizacja czynności obszaru Personel
::----------------------------------------------------------------------------------------------------------------------
exec('upgrade_act','upgrade_2325','PKD.POC.PPK.PPL.PSZ')


\upgrade_act_per_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Aktualizacja czynności obszaru Personel - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja czynności obszaru Personel'


\init_sys_log
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Aktywacja rejestru zmian
::----------------------------------------------------------------------------------------------------------------------
SYSSLGMN.cntx_psh();
SYSSLGMN.clear();
SKID_LOG.cntx_psh();
SKID_LOG.index('STATUS');
SKID_LOG.prefix('F');
_loop:=SKID_LOG.first();
{!
|? _loop
|! {? SYSSLGMN.find_key(SKID_LOG.MASKA,SKID_LOG.SYMBOL,)
   || {? SKID_LOG.STATUS<>'T'
      || SYSSLGMN.del()
      ?}
   |? SKID_LOG.STATUS='T'
   || SYSSLGMN.blank(1);
      SYSSLGMN.MASK:=SKID_LOG.MASKA;
      SYSSLGMN.ACR:=SKID_LOG.SYMBOL;
      SYSSLGMN.add()
   ?};
   _loop:=SKID_LOG.next()
!};
SKID_LOG.cntx_pop();
SYSSLGMN.cntx_pop();
1


\init_sys_log_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [23.25]
:: OPIS: Aktywacja rejestru zmian - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktywacja rejestru zmian'


\genius_updt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Aktualizacja definicji asystenta Genius [ER/WRT/XP/23.25/2310/0017]
::----------------------------------------------------------------------------------------------------------------------
_result:=-1;
_info:=exec('genius_updt_desc','upgrade_2325');
_file:='genius_def.json';
{? FUN.ask(_info+'\nCzy chcesz kontynuować?')
|| {? exec('import_def','genius','',_file)
   || __UPG.msg('Aktualizacja definicji zakończona sukcesem.'@);
      _result:=1
   || __UPG.msg('Aktualizacja definicji nie powiodła się. Błąd otwarcia pliku.'@)
   ?}
|| __UPG.msg('Anulowano wykonanie zadania.'@)
?};
_result


\genius_updt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: Aktualizacja definicji asystenta Genius - opis [ER/WRT/XP/23.25/2310/0017]
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji asystenta Genius (genius_def.json) w związku z realizacją poprawki: ER/WRT/XP/23.25/2310/0017. '
'Uruchomienie zadania spowoduje wczytanie nowej (zaktualizowanej) definicji. '
'Wprowadzone wcześniej własne modyfikacje standardowych formuł asystenta Genius '
'(Administracja -> Narzędzia -> Definicja asystenta Genius) zostaną utracone.'


\etypy_portalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Uaktualnienie danych w tabeli PORTALK na podstawie typów dokumentów SEOD
::----------------------------------------------------------------------------------------------------------------------
_typy_wp:='S,s';
_wp:=spli_str(_typy_wp,',');
ETYPY.cntx_psh();
ETYPY.index('GRUPA_WP');
{! _ii:=1..obj_len(_wp)
|! ETYPY.prefix(_wp[_ii]);
   {? ETYPY.first()
   || {!
      |? exec('PORTALK_upd','portal_seod',ETYPY.ref());
         ETYPY.next()
      !}
   ?}
!};
ETYPY.cntx_pop();
__UPG.msg('Zaktualizowano dane w tabeli PORTALK.');
1


\etypy_portalk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [23.25]
:: OPIS: Uaktualnienie danych w tabeli PORTALK na podstawie typów dokumentów SEOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/23.25/2311/0063 - Nie są wysyłane Inne dane dla faktury do SEOD\n'
'Uaktualnienia tabeli PORTALK na podstawie innych danych w typach dokumentów SEOD'


\par_wyd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: Aktualizacja parametrów wydruku - [ER/WRT/XP/23.25/2312/0001]
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec');
__lrec:=0;
_ok:=0;
PARWYD.cntx_psh();
PARWYD.prefix();
PARWYD.for_each("{? PARWYD.DOKUMLT=1
                 || PARWYD.DOKUMLT:=2;
                    {? PARWYD.put() || __lrec+=1 ?}
                 ?}
");
PARWYD.cntx_pop();
{? __lrec
|| __UPG.msg('Naprawiono układ wyświetlania dokumentów księgowych. Zmodyfikofano %1 rekordów tabeli PARWYD'[$__lrec])
|| __UPG.msg('Naprawa układu wyświetlania dokumentów księgowych nie jest wymagana.')
?};
1


\par_wyd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [RR.xx]
:: OPIS: Aktualizacja parametrów wydruku - [ER/WRT/XP/23.25/2312/0001] - opis
::----------------------------------------------------------------------------------------------------------------------
'Formuła naprawcza układu wyświetlania dokumentów księgowych - realizacja poprawki ER/WRT/XP/23.25/2312/0001.'


\projekty_clean_tree
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [RR.xx]
:: OPIS: Porządkowanie kolejności projektów
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec');
__lrec:=0;
PROJEKTY.for_each("
__lrec+=1;
{? PROJEKTY.POZ_PROJ=1
|| _rodzic:=PROJEKTY.RODZIC;
   _firma:=PROJEKTY.FIRMA;
   exec('clean_tree','projekty_view',_firma,_rodzic);
   exec('clean_tree','projekty_view',_firma,_rodzic)
?}
");
__UPG.msg('Uporządkowano kolejności projektów.');
__UPG.msg('Przetworzono: %1 zapisów.'[$__lrec]);
VAR_DEL.delete('__lrec');
_result


\projekty_clean_tree_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [RR.xx]
:: OPIS: Porządkowanie kolejności projektów
::----------------------------------------------------------------------------------------------------------------------
'Porządkowanie kolejności projektów.'


\zbl_rpt_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Usunięcie wydruku zbl_dok_001
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
_name:='zbl_dok_001';
{? fexists('%1.rpt'[_name],1)
|| _res:=ferase('%1.rpt'[_name],1);
   {? _res
   || __UPG.msg('Usunięto wydruk %1.'[_name])
   || __UPG.msg('Nie usunięto wydruku %1.'[_name])
   ?}
|| __UPG.msg('Wydruk %1 został już wcześniej usunięty.'[_name])
?};
_res


\zbl_rpt_del_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Usunięcie wydruku zbl_dok_001 - opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie wydruku zbl_dok_001.'


\stawka_7
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: ER/WRT/XP/23.25/2401/0027 - dodanie stawki VAT 7 % z opisem "zryczałtowany zwrot podatku (ZZP)"
::----------------------------------------------------------------------------------------------------------------------
exec('dane1','dane_startowe');
__UPG.msg('Zaktualizowano słownik ~STAWKI VAT PL');
1


\stawka_7_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PBS [23.25]
:: OPIS: ER/WRT/XP/23.25/2401/0027 - dodanie stawki VAT 7 % z opisem "zryczałtowany zwrot podatku (ZZP)" - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słownika ~STAWKI VAT PL'


\procesy_ER_2401_0062
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Import procesów poprawka
::       ER/WRT/XP/23.25/2401/0062 - Zadania potransferowe gdy klient posiada uruchomiony Portal HR
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','23.25',
   'ZUI_POR_IN_ALL,ZUI_POR_UPR,ZUI_POR_WN,ZUI_POR_WN_IN,ZUI_POR_WN_OUT,ZUI_POR_WN_PROC','ZUI_'
)


\procesy_ER_2401_0062_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [23.25]
:: OPIS: Import procesów poprawka - opis
::       ER/WRT/XP/23.25/2401/0062 - Zadania potransferowe gdy klient posiada uruchomiony Portal HR
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/23.25/2401/0062 - Zadania potransferowe gdy klient posiada uruchomiony Portal HR.\n'
'Pozwala zaimportować zmienione procesy dziedziny ZUI:\n'
' ● ZUI_POR_IN_ALL - Komunikacja z portalem - odbieranie danych\n'
' ● ZUI_POR_UPR - Konfiguracja portalu - uprawnienia\n'
' ● ZUI_POR_WN - Komunikacja z portalem (wnioski) - cały cykl\n'


\ob_czy_dekr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: ER/WRT/XP/23.25/2401/0047 - Poprawa w procesach warunku na bramie sprawdzającej, czy dokument
::       w obiegu będzie dekretowany
::----------------------------------------------------------------------------------------------------------------------
B_CHOICE.cntx_psh();
B_CHOICE.prefix();
{? B_CHOICE.first()
|| {!
   |? {? B_CHOICE.FORMULA='exec(\'FindAndGet\',\'#table\',EDOKUM,_a.EDOKUM,,"LOG_PRZ<>\'T\'",0)'
      || B_CHOICE.FORMULA:='exec(\'FindAndGet\',\'#table\',EDOKUM,_a.EDOKUM,,"TYP().DEKR=\'T\'",0)';
         B_CHOICE.put()
      ?};
      B_CHOICE.next()
   !}
?};
B_CHOICE.cntx_pop();
__UPG.msg('Poprawiono warunek sprawdzający, czy dokument w obiegu jest dekretowany.');
1


\ob_czy_dekr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: ER/WRT/XP/23.25/2401/0047 - Poprawa w procesach warunku na bramie sprawdzającej, czy dokument
::       w obiegu będzie dekretowany - opis
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/23.25/2401/0047 - Poprawa w procesach warunku na bramie sprawdzającej, czy dokument w obiegu'
' będzie dekretowany.'


\rubryki_ER24020016
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RN [23.25]
:: OPIS: ER/WRT/XP/23.25/2402/0016 - W poprawce SPIS ER/WRT/XP/23.25/2308/0037: ER/WRT/XP/12.51/2306/0033
::       nowy składnik 7237 Kor. wyn. rehabil. nie dodany do atrybut 48 Składniki do brutto o kodzie większym niż 500.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

{? __RUB.ref(7237)
|| exec('add_use','rubatr',48,7237)
|| _result:=-1
?};

{? _result=1
|| __UPG.msg('Zakończono aktualizację atrybutu 48 - Składniki do brutto o kodzie większym niż 500.')
|| __UPG.msg('Brak rubryki o kodzie 7237.')
?};

_result


\DOK_sum
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: ER/WRT/XP/23.25/2402/0030 - Formuła naprawcza dla pól suma obrotów WN/MA dla nagłówka dokumentu księgowego.
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice(
   'ER/WRT/XP/23.25/2402/0030 - Formuła naprawcza dla pól sumy obrotów WN/MA w nagłówku dokumentu księgowego.\n'
   'Błąd mógł spowodować, że w niektórych przypadkach suma obrotów WN/MA na nagłówku dokumentu księgowego nie przedstawia stanu faktycznego '
   'występującego na pozycjach dokumentu źródłowego. W zależności od ilości danych formuła naprawcza może być czasochłonna.\n\n'
   'Czy na pewno chcesz uruchomić formułę? \n'
   ' * Tak - wykonaj formułę\n'
   ' * Oznacz jako wykonane - rezygnacja z wykonywania, zmiana statusu zadania na "Wykonane"\n'
   ' * Anuluj\n'@
   ,0,'Tak'@,'Oznacz jako wykonane'@
);

_result:=0;
{? _choice=1
|| _ldok_n:=_ldok:=_dok_put:=0;
   DOK.cntx_psh(); DOK.trig_off('*','*');
   POZ.cntx_psh(); KS.cntx_psh();
   ROK_F.index('KOD'); ROK_F.prefix();
   OKRO_F.index('ROK');
   {? ROK_F.first()
   || {!
      |? OKRO_F.prefix(ROK_F.ref());
         {? OKRO_F.first()
         || {!
            |? _maska:=ROK_F.KOD+form(OKRO_F.NR,-2);
               POZ.use('pozy'+_maska); POZ.index('DOK');
               DOK.use('doku'+_maska); DOK.index('O_REJ'); DOK.prefix();
               {? DOK.first()
               || progress(,'Przetwarzanie danych tabeli DOK - bieżąca maska: %1'@['doku'+_maska],FUN.TYT,1);
                  {!
                  |? _dok_put:=_poz_wn:=_poz_ma:=0;
                     no_msg(1); lock_time(0);
                     POZ.prefix(DOK.ref());
                     {? POZ.first()
                     || {!
                        |? _ok_poz:=1;
                           {? POZ.KOM=null
                           || {? POZ.KON=''
                              || _ok_poz:=0
                              || KS.index('SYM'); KS.prefix(ROK_F.ref(),ROK_F.SYNT+POZ.KON);
                                 {? ~KS.first() || _ok_poz:=0 ?}
                              ?}
                           || POZ.KOM().KS()
                           ?};
                           {? _ok_poz & -(1+KS.TYP)<>'p'
                           || {? POZ.STR='Wn' || _poz_wn+=POZ.SUM || _poz_ma+=POZ.SUM ?}
                           ?};
                           POZ.next()
                        !}
                     ?};
                     no_msg(0); lock_time(-1);
                     {? DOK.SUMWN<>_poz_wn || _dok_put:=1; DOK.SUMWN:=_poz_wn ?};
                     {? DOK.SUMMA<>_poz_ma || _dok_put:=1; DOK.SUMMA:=_poz_ma ?};
                     {? _dok_put
                     || {? DOK.put()
                        || _ldok+=1
                        || _ldok_n+=1
                        ?}
                     ?};
                     DOK.next()
                  !}
               ?};
               OKRO_F.next()
            !}
         ?};
         ROK_F.next()
      !}
   ?};
   POZ.cntx_pop(); KS.cntx_pop();
   DOK.cntx_pop(); DOK.trig_on('*','*');
   {? _ldok | _ldok_n
   || {? _ldok || _result:=1; __UPG.msg('Zaktualizowano nagłówki dokumentów źródłowych. %1 zmodyfikowany(ch) rekord(ów).'[$_ldok]) ?};
      {? _ldok_n || _result:=0; __UPG.msg('Nie powiodło się zmodyfikować %1 rekord(ów).'[$_ldok_n]) ?}
   || _result:=1; __UPG.msg('Nie stwierdzono potrzeby aktualizacji danych dla dokumentów księgowych.')
   ?}
|? _choice=2
|| __UPG.msg('Zrezygnowano z wykonania zadania. Zadanie zostało oznaczone jako wykonane.');
   _result:=1
|? _choice=0
|| __UPG.msg('Zadanie zostało odłożone do ponownego uruchomienia.');
   _result:=-1
|| __UPG.msg('Nierozpoznana odpowiedź.');
   _result:=0
?};
prgs_clr();
_result


\DOK_sum_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [23.25]
:: OPIS: ER/WRT/XP/23.25/2402/0030 - Formuła naprawcza dla pól suma obrotów WN/MA dla nagłówka dokumentu księgowego - opis
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/23.25/2402/0030 - Formuła naprawcza dla pól sumy obrotów WN/MA w nagłówku dokumentu księgowego.'


\zalacz_www
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [RR.xx]
:: OPIS: ER/WRT/XP/23.25/2402/0067 -
::       Baza wiedzy-Moje dokumenty: nadmiarowa prezentacja dokumentów z wersji roboczej kartotek.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
ZALACZ.cntx_psh();
ZALACZ.prefix();
{? ZALACZ.first()
|| ZALACZ.trig_off('*','*');

   _stat:=obj_new('all','poprawnych','do_zmiany','put1','put0');
   _stop:=obj_len(_stat);
   {! _lp:=1 .. _stop
   |! _stat[_lp]:=0
   !};

   _rodz:=spli_str('H_UM,h_u_|H,_his_','|');
   _stop:=obj_len(_rodz);
   {! _lp:=1 .. _stop
   |! _tab:=spli_str(_rodz[_lp],',');
      ZALACZ.f_set('RODZAJ,WWWE',,'"ZALACZ".RODZAJ=\':_a\' and "ZALACZ".WWWE=\'T\'',_tab[1]);
      _stat.all+=ZALACZ.f_size();
      {? ZALACZ.f_first()
      || {!
         |? _maska:=form(8+(ZALACZ.NAG+16));
            _www:=
               {? (ZALACZ.RODZAJ='H_UM' & _maska='h_u_') | (ZALACZ.RODZAJ='H' & _maska='_his_')
               || 'N'
               |? ZALACZ.WWWE='T' | ZALACZ.WWWS='T'
               || 'T'
               || 'N'
               ?};
            {? ZALACZ.WWW<>_www | _www='N'
            || _stat.do_zmiany+=1;
               ZALACZ.WWW:=_www;
               {? ZALACZ.put(,1)
               || _stat.put1+=1
               || _stat.put0+=1
               ?}
            || _stat.poprawnych+=1
            ?};
            ZALACZ.f_next()
         !}
      ?};
      ZALACZ.f_clear();
      obj_del(_tab)
   !};

   {? _stat.put0
   || _ret:=-1
   ?};

   __UPG.msg('Liczba rekordów do sprawdzenia: %1'[$_stat.all]);
   __UPG.msg('   w tym:');
   __UPG.msg('   * niewymagających zmiany: %1'[$_stat.poprawnych]);
   __UPG.msg('   * wymagających zmiany: %1'[$_stat.do_zmiany]);
   __UPG.msg('      w tym:');
   __UPG.msg('      - poprawionych: %1'[$_stat.put1]);
   __UPG.msg('      - niepoprawionych: %1'[$_stat.put0]);

   ZALACZ.trig_on('*','*')
|| __UPG.msg('Brak załączników - przetwarzanie nie jest wymagane.')
?};
ZALACZ.cntx_pop();

_ret


\zalacz_www_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [RR.xx]
:: OPIS: ER/WRT/XP/23.25/2402/0067 -
::       Baza wiedzy-Moje dokumenty: nadmiarowa prezentacja dokumentów z wersji roboczej kartotek - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/23.25/2402/0067 - Baza wiedzy-Moje dokumenty: nadmiarowa prezentacja dokumentów z wersji roboczej kartotek'


:Sign Version 2.0 jowisz:1045 2024/02/26 10:22:30 c1f97ac04d0ca40685854af3974f9f6d418d15ae682b350fafb08d734df06fadcc6d99fbf8de468af5750ce295e691d15e77cfcbda49e2f3a4836c64745f0ccc76044f5f23f337a5fa82bc4c8123da6d1cb9d281d543d9f47701f54f951fcb450f5cb2505862cec5ef77b46b1a06781f8911ca1924177cb027c8902b367b103d

:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2226_pl001.fml
:: Utworzony: 07.02.2023
:: Autor: IS
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.37 na 22.26
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================

\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Główna formuła aktualizacji 22.26_PL001
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('mod_dane_PL001',,'N','PPL',,,,1,'T');
:: Zadania manualne, wykonywane dla każdej firmy
__UPG.add_task('wn_rok_PL001','N','N','PPL',,,,1,'T');
~~


\mod_dane_PL001
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie wartości nowego pola, modyfikacja słownika wniosków.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_firma:=exec('ref_firma','ustawienia');

OS_ZWSLO.trig_off('*','*');
OS_ZWSLO.cntx_psh();
OS_ZWSLO.index('KOD');
OS_ZWSLO.prefix(_firma,'UZ_POD',);
{? OS_ZWSLO.first()
|| OS_ZWSLO.ROCZNE:='N';
   OS_ZWSLO.put()
?};
OS_ZWSLO.cntx_pop();
OS_ZWSLO.trig_on('*','*');

ZC.trig_off('*','*');
ZC.index('ZLECDAT');
ZC.prefix(_firma);
RH.index('RACHPDWY');
{? ZC.first()
|| exec('PROGRESS','#object');
   _size:=ZC.size();
   PROGRESS.set(_size,'\nTrwa weryfikacja umów zleceń.');
   {!
   |? {? ZC.DZB=#0
      || ZC.DZB:=ZC.DW;
         RH.prefix(ZC.ref());
         {? RH.last() & RH.DWY>ZC.DW
         || ZC.DZB:=RH.DWY
         ?};
         ZC.put()
      ?};
      PROGRESS.next();
      ZC.next()
   !};
   PROGRESS.close()
?};
ZC.trig_on('*','*');
__UPG.msg('Zakończono uzupełnianie i modyfikację danych w kartotece umów zleceń i słowniku typów wniosków.');

_result


\mod_dane_PL001_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie wartości nowego pola, modyfikacja słownika wniosków - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie i modyfikacja danych związanych z Polskim Ładem (styczeń 2023) w kartotece umów zleceń'
' i słowniku typów wniosków.'


\wn_rok_PL001
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Lista wniosków, dla których wniosków wyzerowano informację w polu "ROK"
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_firma:=exec('ref_firma','ustawienia');

OS_ZWSLO.cntx_psh();
OS_ZWSLO.index('KOD');
OS_ZWSLO.prefix(_firma,'UZ_POD',);
{? OS_ZWSLO.first()
||
:: wyzerowanie pola ROK dla 'UZ_POD' lat od 2022 począwszy
   OS_ZWZAL.trig_off('*','*');
   OS_ZWZAL.cntx_psh();
   _ndx:=OS_ZWZAL.ndx_tmp(,1,'OS_ZWSLO',,);
   OS_ZWZAL.index(_ndx);
   OS_ZWZAL.prefix(OS_ZWSLO.ref());
   {? OS_ZWZAL.first()
   || _TAB:=sql('
         select OSOBA.NAZWISKO, OSOBA.PIERWSZE, OSOBA.PESEL,
            OS_ZWZAL.D_OB, OS_ZWZAL.D_ZO, OS_ZWZAL.ROK, OS_ZWSLO.RODZAJ, OS_ZWSLO.KOD
         from OS_ZWZAL
            join OS_ZWSLO using(OS_ZWZAL.OS_ZWSLO, OS_ZWSLO.REFERENCE)
            join OSOBA using(OS_ZWZAL.OSOBA, OSOBA.REFERENCE)
         where 1=2
         order by 1,2
      ');
      {!
      |? {? OS_ZWZAL.ROK>=2022
         || _TAB.ROK:=OS_ZWZAL.ROK;
            _TAB.NAZWISKO:=OS_ZWZAL.OSOBA().NAZWISKO;
            _TAB.PIERWSZE:=OS_ZWZAL.OSOBA().PIERWSZE;
            _TAB.PESEL:=OS_ZWZAL.OSOBA().PESEL;
            _TAB.D_OB:=OS_ZWZAL.D_OB;
            _TAB.D_ZO:=OS_ZWZAL.D_ZO;
            _TAB.RODZAJ:=OS_ZWZAL.OS_ZWSLO().RODZAJ;
            _TAB.KOD:=OS_ZWZAL.OS_ZWSLO().KOD;
            OS_ZWZAL.ROK:=0;
            {? OS_ZWZAL.put()
            || _TAB.add(1)
            ?}
         ?};
         OS_ZWZAL.next()
      !};
      {? _TAB.first()
      || FUN.info('Zostaną pokazane wnioski za lata 2022 i 2023, które wcześniej były typu rocznego.\n'
                  'Dla tych wniosków wyzerowano informację w polu "ROK".\n'
                  'W związku z tym wnioski te stają się dla systemu bezterminowymi.\n'
                  'Należy zweryfikować wyświetlone wnioski i w razie konieczności dokonać dezaktywacji wniosku.'@);
         _ws:=_TAB.mk_sel('Zweryfikuj wnioski'@,,0,'#kalipl001');
         _TAB.win_fld(_ws,,'NAZWISKO',,,30,,,'Nazwisko'@,,'Nazwisko'@);
         _TAB.win_fld(_ws,,'PIERWSZE',,,20,,,'Imię'@,,'Pierwsze imię'@);
         _TAB.win_fld(_ws,,'PESEL',,,10,,,'PESEL'@,,'Numer PESEL'@);
         _TAB.win_fld(_ws,,'KOD',,,10,,,'Kod'@,,'Kod wniosku'@);
         _TAB.win_fld(_ws,,'RODZAJ',,,40,,,'Rodzaj'@,,'Rodzaj wniosku'@);
         _TAB.win_fld(_ws,,'ROK',,,10,,,'Rok'@,,'Rok wniosku'@);
         _TAB.win_fld(_ws,,'D_ZO',,,10,,,'Data złożenia'@,,'Data złożenia wniosku'@);
         _TAB.win_fld(_ws,,'D_OB',,,10,,,'Data stosowania'@,,'Data stosowania wniosku'@);
         _TAB.win_sel(_ws);
         _TAB.select()
      ?};
      obj_del(_TAB)
   ?};
   OS_ZWZAL.ndx_drop(_ndx);
   OS_ZWZAL.cntx_pop();
   OS_ZWZAL.trig_on('*','*')
?};
OS_ZWSLO.cntx_pop();
__UPG.msg('Wyświetlono listę wniosków, dla których wniosków wyzerowano informację w polu "ROK".');

_ret


\wn_rok_PL001_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Lista wniosków, dla których wniosków wyzerowano informację w polu "ROK" - opis
::----------------------------------------------------------------------------------------------------------------------
'Lista wniosków, dla których wyzerowano informację w polu "ROK".\n'
'Zostaną pokazane wnioski za lata 2022 i 2023, które wcześniej były typu rocznego.\n'
'Dla tych wniosków wyzerowano informację w polu "ROK".\n'
'W związku z tym wnioski te stają się dla systemu bezterminowymi.\n'
'Należy zweryfikować wyświetlone wnioski i w razie konieczności dokonać dezaktywacji wniosku.\n'



:Sign Version 2.0 jowisz:1048 2023/06/23 14:16:36 7db7ba1e22eaaebfe128450e63ffe4c5ba2ac7ef8d12a24fa819f8bb71fab90676dc96381c10a84ba24044c26778438115d81933aa997560e59a93b6551b91fa157f15817813cc4117cd2a39a8fbc4aeb6879f26f9d3bf7ba481f8979aba8612180679f8cc63a71927d4408f60e5745f6c4933b094a1aff07090e7c53bfd383e

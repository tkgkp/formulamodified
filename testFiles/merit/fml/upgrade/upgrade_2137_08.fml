:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2137_08.fml
:: Utworzony: 20.01.2022
:: Autor: MP
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.14 na 21.37
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Główna formuła aktualizacji 21.37
::----------------------------------------------------------------------------------------------------------------------
:: Zadania automatyczne, wykonywane raz dla całego systemu
__UPG.add_task('jpk2_upr',,,'FKS',,,,1,'T');
__UPG.add_task('proc_add',,,'FKS',,,,1,'T');
__UPG.add_task('jpkv72_sch',,,'FKS',2,,,1,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
:: Zadania manualne, wykonywane raz dla całego systemu
:: Zadania manualne, wykonywane dla każdej firmy
~~



\jpk2_upr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodaje formułę kontrolną dla kontroli procedury EE
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G','PROC_V72',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:='PROC_V72';
   FORMULA.NAZWA:='Formuła kon. sprawdzająca wymagania procedury EE';
   FORMULA.FORMULA:='exec(\'jpk_proc_ee\',\'spr_dok\')';
   {? _ret:=FORMULA.add()
   || __UPG.msg('Dodano formułę PROC_V72.')
   || __UPG.msg('Nie udało się dodać formuły PROC_V72.')
   ?}
|| __UPG.msg('Formuła kontrolna PROC_V72 jest już dodana.')
?};
FORMULA.cntx_pop();
_ret


\jpk2_upr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodaje formułę kontrolną dla kontroli procedury EE - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodaje formułę kontrolną dla kontroli procedury EE.'


\proc_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Formuła dodaje procedury dla nowej wersji JPK_V7 od 2022.01.01 oraz dodatkowo procedura SOTI.
::----------------------------------------------------------------------------------------------------------------------
_typ:='PROC';
_wer:=1;
_ret:=1;
JPK_SLO.cntx_psh();
JPK_SLO.index('KOD');
JPK_SLO.prefix();
_ok:=1;
{? ~JPK_SLO.find_key(_wer,_typ,'WSTO_EE',) & ~JPK_SLO.find_key(_wer,_typ,'IED',) & ~JPK_SLO.find_key(_wer,_typ,'SOTI',)
|| exec('add_slo','jpk_v',_wer,_typ,'WSTO_EE',
      'Wewnątrzwspólnotowa sprzedaż towarów na odległość',,'OSS'
   );
   exec('add_slo','jpk_v',_wer,_typ,'IED',
      'Dostawy towarów, o których mowa w art. 7a ust. 1  i 2 Ustawy, na terytorium kraju',,'OSS'
   );
   exec('add_slo','jpk_v',_wer,_typ,'SOTI',
      'Sprzedaż na odległość towarów importowanych',,'IOSS'
   );
   __UPG.msg('Dodano nowe procedury szczególne.')
|| __UPG.msg('Nowe procedury szczególne były już dodane')
?};
JPK_SLO.cntx_pop();
_ret


\proc_add_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodaje procedury dla nowej wersji JPK_V7 od 2022.01.01 oraz dodatkowo procedura SOTI - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodaje procedury dla nowej wersji JPK_V7 od 2022.01.01 oraz dodatkowo procedura SOTI.'


\jpkv72_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodanie nowych schematów JPK_V7M (2) oraz JPK_V7K (2)
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_txt:='Schemat JPK_V7M (2)';
_imp:=exec('upg_imp','xml','FKS','J','V7M','JPK_V7M(2)_v1-0',date(2022,1,1),'jpk_v7m_2_1.dfg');
{? _imp=1
|| __UPG.msg(_txt+' został dodany.')
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.')
|| __UPG.msg(_txt+' nie został dodany.');
   _ok:=0
?};
_txt:='Schemat JPK_V7K (2)';
_imp:=exec('upg_imp','xml','FKS','J','V7K','JPK_V7K(2)_v1-0',date(2022,1,1),'jpk_v7k_2_1.dfg');
{? _imp=1
|| __UPG.msg(_txt+' został dodany.')
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.')
|| __UPG.msg(_txt+' nie został dodany.');
   _ok:=0
?};
_ok


\jpkv72_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [21.37]
:: OPIS: Dodanie nowych schematów JPK_V7M (2) oraz JPK_V7K (2) - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych schematów: JPK_V7M (2) oraz JPK_V7K (2)\n'
'Schematy obowiązują od 1 stycznia 2022 r.'

:Sign Version 2.0 jowisz:1045 2022/06/30 14:23:25 150d7d1f32ca8994a593d8332a72221d0290bda92e9e391aea9f66e1d28a84b8f7d7a87b6e905ac744889a86bb2b1f7a80b8483da9138ab26198cf5dad209dbfcc1ce2a4a29645a01db4d80f189930d0f959f29399d8a5672ba4225826c9049dcbb5d3ba9227302dd5e13dc3b047a6e3b4efae2001eae22bce0b630cd7184d99

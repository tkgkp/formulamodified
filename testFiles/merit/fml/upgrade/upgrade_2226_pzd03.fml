:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2226_pzd03.fml
:: Utworzony: 29.06.2023
:: Autor: RWR [22.26]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.37 na 22.26
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Główna formuła aktualizacji 22.26_pzd02
::----------------------------------------------------------------------------------------------------------------------
:: Informacja o dacie i czasie aktualizacji
__UPG.id_ver(date(2023,8,25),time(0,0,0));
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('upgrade_act',,,'ZPR',3,,,,'T');
__UPG.add_task('eatr_upd_memo',,,'POR',,,,,'T');
__UPG.add_task('funpar',,,'ZWS',,,,,'T');
__UPG.add_task('id_sync',,,'ZWS',,,,1,'T');
:: Zadania automatyczne, wykonywane dla każdej firmy
__UPG.add_task('role',,'N','ZWS',2,,,1,'T');
__UPG.add_task('r_por_ww_por_nr',,'N','POR',,,,1,'T');
:: Zadania manualne
__UPG.add_task('procesy_pzd03','N','N','POR',,,,1,'T');
__UPG.add_task('excel_wnioski_portalowe','N',,'ZWS',,,,1,'T');
__UPG.add_task('excel_wymiana_danych','N',,'ZWS',,,,1,'T');
~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::   WE: [_a] - dziedziny
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności (ciekawe, czy uda się alfabetycznie ...)
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.BIQ','.');
_dom_str:={? var_pres('_a')=type_of('')
          || _a
          || 'POR.ZWS'
          ?};
_dom:=spli_str(_dom_str,'.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\role
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami.
::----------------------------------------------------------------------------------------------------------------------
{? exec('roleActions','upgrade'
      ,'Portal HR - Pracownik'
      ,'POR_PRA_PZSP,POR_PRA_PZWN,POR_PRA_PKNZ'
   )
   &
   exec('roleActions','upgrade'
      ,'Portal HR - Przełożony akceptujący (wnioski bez wynagrodzeń)'
      ,'POR_KIE_PZKP,POR_KIE_PZSP'
   )
   &
   exec('roleActions','upgrade'
      ,'Administrator'
      ,'ZWS_PAR_PPHD'
   )
|| 1
|| -1
?}


\role_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie uprawnień dla roli "Portal HR - Pracownik" do czynności:\n'
' ● POR_PRA_PZSP - Moje dane / Świadczenie pracy\n'
' ● POR_PRA_PZWN - Wnioski / Świadczenie pracy\n'
' ● POR_PRA_PKNZ - Pulpit / Kalendarz / Nieobecności zespołu\n'
'Dodanie uprawnień dla roli "Portal HR - Przełożony akceptujący (wnioski bez wynagrodzeń)" do czynności:\n'
' ● POR_KIE_PZKP - Karta pracownika\n'
' ● POR_KIE_PZSP - Mój zespół / Świadczenie pracy\n'
'Dodanie uprawnień dla roli "Administrator" do czynności:\n'
' ● ZWS_PAR_PPHD - Wart. dom. oświadczeń (ppsf)\n'


\procesy_pzd03
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import procesów zmienionych w aktualizacji 22.26_PZD03.
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade_2226','POR_OOPZ_WWW,POR_PZDO_WWW','POR_')


\procesy_pzd03_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import procesów zmienionych w aktualizacji 22.26_PZD02 - opis.
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować nowy proces dziedziny POR:\n'
' ● POR_OOPZ_WWW - Oświadczenie o pracy zdalnej (wersja 22.26)\n'
' ● POR_PZDO_WWW - Wniosek o świadczenie pracy zdalnej (wersja 22.26).\n'


\excel_wnioski_portalowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import wniosków portalowych.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',,,0,,,1,'TAT_WP,ETYPPROC,ETYP_ATR_WP,ETYPY_WP,PORSLO,PORSLOIT');

_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack().name])
|| __UPG.msg('Zaktualizowano definicje wniosków portalowych.');
   _result:=1
|| _result:=-1
?};
_result


\excel_wnioski_portalowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import wniosków portalowych - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import wniosków portalowych:\n'
' ● OswiadczeniePracaZdalna - Oświadczenie o pracy zdalnej\n'
' ● ZdarzeniaDotPracy - Wniosek o świadczenie pracy zdalnej\n'


\excel_wymiana_danych
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import definicji wymiany danych.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_DOK_SYNCHRO',','),,1);

_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack().name])
|| __UPG.msg('Zaktualizowano definicje wymiany danych.');
   _result:=1
|| _result:=-1
?};
_result


\excel_wymiana_danych_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Import definicji wymiany danych - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import definicji wymiany dancyh:\n'
' ● Definicja PORTAL\n'
' ● Definicje synchronizacji z zewnętrznymi API\n'


\eatr_upd_memo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Uaktualnia pole domyślne na nowe dla atrybutów wielolinijkowych
::----------------------------------------------------------------------------------------------------------------------
_cnt:=0;

_sql:=sql('
       select
         ETYP_ATR.REFERENCE,
         ETYP_ATR.DEF_MEMO,
         TAT.TYP
       from ETYP_ATR join TAT
       where TAT.TYP LIKE \'W\' and ETYP_ATR.DEFAULT<>\'\'
        ');

{? _sql.first()
|| ETYP_ATR.cntx_psh();
   ETYP_ATR.prefix();
   {!
   |? {? ETYP_ATR.seek(_sql.REFERENCE)
      || ETYP_ATR.memo_set(ETYP_ATR.DEFAULT,'DEF_MEMO');
         ETYP_ATR.memo_put(,'DEF_MEMO');
         ETYP_ATR.DEFAULT:='';
         _cnt+=1;
         ETYP_ATR.put()
      ?};
      _sql.next()
   !};
   __UPG.msg('Zaktualizowano %1 atrybutów.'[$_cnt]);
   ETYP_ATR.cntx_pop()
|| __UPG.msg('Dane nie wymagały aktualizacji atrybutów.')
?};
1


\eatr_upd_memo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Uaktualnia pole domyślne na nowe dla atrybutów wielolinijkowych - opis
::   WE: _a [NUMBER] - czy test? (0/1)
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola domyślnego na nowe dla atrybutów wielolinijkowych'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Aktualizacja obszaru "Ustawienia i parametryzacja".
::----------------------------------------------------------------------------------------------------------------------
{? exec('aktualizuj','funpar',1)
|| __UPG.msg('Zaktualizowano zawartość obszaru "Ustawienia i parametryzacja".')
|| __UPG.msg('Nie zaktualizowano zawartości obszaru "Ustawienia i parametryzacja".')
?}


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja obszaru "Ustawienia i parametryzacja"'


\id_sync
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów
::----------------------------------------------------------------------------------------------------------------------
_init:=exec('init','sync_id');
_result:=_init.RESULT;
_args:=_init.ARGS;
{? _result
|| __UPG.msg('Zaktualizowano definicje identyfikatorów rekordów.')
|| __UPG.msg('Wystąpił problem podczas aktualizacji definicji identyfikorów rekordów %1 - %2 do systemu %3.'
      [_args.KOD,_args.OPIS,_args.SYSTEM])
?};
_result


\id_sync_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [20.14]
:: OPIS: Dodaje definicje identyfikatorów rekordów - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie definicji identyfikatorów rekordów:\n'
'PORTAL_PPSFT_ID\n'
'PORTAL_PPSFROZD_ID\n'
'PORTAL_PPSFL_ID\n'
'PORTAL_PPSF_NO_ID\n'
'PORTAL_PPSF_PO_ID\n'
'PORTAL_PPSF_H_ID\n'


\r_por_ww_por_nr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [22.26]
:: OPIS: Uzupełnia nowe pole POR_NR w tabeli R_POR_WW
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_TAB:=sql('select R_POR_WW.REFERENCE from @R_POR_WW where R_POR_WW.POR_NR=0');
{? type_of(_TAB)=type_of(~~)
|| _ret*=0;
   __UPG.msg('Wystąpił błąd. Wartości pola POR_NR nie zosały uzupełnione.')
|? ~_TAB.first()
|| __UPG.msg('Brak rekodrów do uzupełnienia.')
|| _mydo:=do_state()=0;
   {? _mydo || do() ?};
   _NAMES:=R_POR_WW.names;
   {? _NAMES.first()
   || R_POR_WW.trig_off('*','*');
      {! |? R_POR_WW.cntx_psh();
            R_POR_WW.use(_NAMES.NAME);
            R_POR_WW.index('DZK');
            R_POR_WW.prefix();
            {? R_POR_WW.first() & R_POR_WW.lock(1,1,1)
            || {! |? R_POR_WW.POR_NR:=exec('por_numer','ppsf',R_POR_WW.ref());
                     _ret*=R_POR_WW.put();
                     R_POR_WW.next()
               !};
               R_POR_WW.unlock()
            ?};
            R_POR_WW.cntx_pop();
            _NAMES.next()
      !};
      R_POR_WW.trig_on('*','*')
   ?};
   {? ~_ret
   || undo();
      __UPG.msg('Wystąpiły błędy przy uzupełnianiu wartości pola POR_NR. Wszystkie zmiany zostały wycofane.')
   || __UPG.msg('Uzupełniono wartości pola POR_NR we wszystkich wymagających tego rekordach.')
   ?};
   {? _mydo || end() ?}
?};
_ret


\r_por_ww_por_nr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [22.26]
:: OPIS: Uzupełnia nowe pole POR_NR w tabeli R_POR_WW - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie nowego pola POR_NR w tabeli R_POR_WW.'

:Sign Version 2.0 jowisz:1045 2023/08/25 11:56:17 d898fdca20184e4ae983ad3d53fa733cdb9e6bd1fd4e667d59bea431f622de7a3ab53c013737811062ca85ab6b6fdef396fd6624b04cbd8a365191e372588a36c74ea48a8d42319121840450fe6ef453ca87eee6a9debe1eb11430362000251d571f827b61fbc7775d43d6ea2fd6e25c49a1f3f13ec850f61c4af86859ba0e43

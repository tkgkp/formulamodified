:!UTF-8
:: (c) Macrologic S.A. Wszelkie prawa zastrzeżone
::======================================================================================================================
:: Nazwa pliku: upgrade_2226.fml
:: Utworzony: 21.09.2021
:: Autor: [rr]
::======================================================================================================================
:: Zawartość: Funkcje do obsługi transferu danych z wersji 21.37 na 22.26
::
::            Każda formuła powinna zwracać wartość liczbową, interpretacja:
::                1 - całość przeszła bez błędów - realizacja zakwalifikowana jako wykonana
::               -1 - całość przeszła z uwagami  - dla zadania automatycznego:
::                                                   * realizacja zakwalifikowana jako wykonana
::                                                 dla zadania manualnego:
::                                                   * zrezygnowano z wykonania zadania w danej chwili - zadanie nadal
::                                                     pozostaje do wykonania
::                0 - został stwierdzony błąd    - realizacja zakwalifikowana jako przerwana
::
::            Definicja zadania transferowego:
::               __UPG.add_task(name,[auto],[mf],domain,[ver],[required],[manual],[nofirm],[onlyact])
::                gdzie:
::                   name     [STRING] - nazwa formuły potransferowej (z bieżącego pliku).
::                   auto     [STRING] - formuła automatyczna: T [domyślnie] / N
::                   mf       [STRING] - formuła wielofirmowa: T [domyślnie] / N
::                                        T - wykonywana tylko raz
::                                        N - wykonywana w każdej firmie
::                   domain   [STRING] - kod dziedziny produktowej (informacyjnie)
::                   ver      [NUMBER] - wersja formuły
::                   required [STRING] - czy zadanie musi być wykonane
::                                       Dla zadań automatycznych parametr zawsze przyjmuje wartość T.
::                                       Dla zadań manualnych - domyślnie: N.
::                   manual   [STRING] - kod powiązanego zadania manualne w ramach wersji
::                                        domyślnie brak
::                                        jeśli podano i istnieje oraz zostaną dokonane zapisy w buforze to zadanie
::                                        zmieni status na wymagalne
::                   nofirm   [NUMBER] - dla zadań jednofirmowych pomija firmę '000' (wartość 1), 0-domyślna wartość
::                   onlyact  [STRING] - zadanie aktualizacyjne dla danej wersji: T-tak, N-nie (domyślnie)
::                                        (Czy zadanie weszło po emisji danej wersji?)
::
::            W formułach można stosować zapis do przesłania komunikatów dla realizacji, np.
::               __UPG.msg('Brak zapisów dla roku 2016 w tabeli FAKS.')
::            Uwaga: ponieważ wynik jest zapisywany do bazy danych, to NIE tłumaczymy go - nie kończymy znakiem @.
::
::            W formułach automatycznych, które zostały powiązane z formułą manualną można stosować zapis o danych
::            wymagalnych do zweryfikowania, np.
::               __UPG.buf(USERS.uidref(),'Zweryfikuj parametry użytkownika w kontekście anulowania dokumentów')
::            zapis jest dołączony bezpośrednio pod wskazaną czynością manualną
::            Do uzyskania zapisów dla realizacji zadania manualnego służy metoda:
::               __UPG.get_buf()
::            wynik zwracany jest w formie tabeli o strukturze:
::               CODE  - STRING[48]
::               DESC  - STRING[255]
::               FIRMA - STRING[16] - ref SQL
::
::            \formuła      - formuła potransferowa
::            \formuła_desc - opis dla formuły potransferowej
::======================================================================================================================
:: Sposób tworzenia i opisu formuł transferowych:
::           - każda formuła musi zawierać opis (\formuła_desc) która w pierwszych zdaniach opisuje co robi, w kolejnych
::             zdaniach zawiera krótki opis techniczny np. uzupełniono pole XX w tabeli AA wartością 'T'
::           - każda formuła musi badać wynik swojego działania, zwracać 1, 0, -1 jeżeli od jej wykonania jest zależne
::             działanie systemu
::           - każda formuła musi tworzyć opis tego, jak przebiegło jej wykonanie wykorzystując __UPG.msg
::             tak aby po opisie można było wywnioskować co formuła konkretnie zrobiła
::             np. w tabeli AA Poprawiono X rekordów z N przeanalizowanych
::           - każda funkcja powinna posiadać progress, pokazujący postęp wykonania (jeżeli funkcja jest przewidziana
::             do wykonania na wielu rekordach)
::======================================================================================================================


\init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Główna formuła upgrade-ów 22.26
::----------------------------------------------------------------------------------------------------------------------
:: Zadania wykonywane raz dla całego systemu
__UPG.add_task('B_DOMAIN',,,'ZPR');
__UPG.add_task('B_ACTION',,,'ZPR');
__UPG.add_task('BI_ARCH',,,'ZPR');
__UPG.add_task('upgrade_act',,,'ZPR',3,,,,'T');
__UPG.add_task('funpar',,,'ZWS');
__UPG.add_task('color',,,'ZWS',3,,,,'T');
__UPG.add_task('stalesys_ini',,,'ZWS');
__UPG.add_task('par_skid',,,'ZWS');
__UPG.add_task('areatitle',,,'ZWS');
__UPG.add_task('report',,,'ZWS',40,,,,'T');
__UPG.add_task('projekty_id',,,'ZWS');
__UPG.add_task('akt_proc',,,'LSP');
__UPG.add_task('rubryki_placowe',,,'PPL');
__UPG.add_task('jpk_fa_upd',,,'FKS');
__UPG.add_task('odd_pola',,,'PKD');
__UPG.add_task('dok_proc_upd',,,'FKS');
__UPG.add_task('kontr_st_zwol',,,'FKS');
__UPG.add_task('USERSF',,,'ZWS');
__UPG.add_task('h_um_pola',,,'PKD');
__UPG.add_task('POCZTAM',,,'ZWS');
__UPG.add_task('PORTALO',,,'ZPR');
__UPG.add_task('B_PREL',,,'ZPR');
__UPG.add_task('BI_TODO',,,'ZWS');
__UPG.add_task('KH_POWR',,,'FKS');
__UPG.add_task('KH_POW',,,'FKS');
__UPG.add_task('portal_hr_cfg',,,'ZWS');
__UPG.add_task('portal_seod',,,'SEO');
__UPG.add_task('portal_seod2',,,'SEO',,'T');
__UPG.add_task('portal_cfg',,,'ZWS');
__UPG.add_task('B_PORT',,,'ZPR');
__UPG.add_task('abs_upd',,,'ZWS');
__UPG.add_task('B_PROC',,,'ZPR');
__UPG.add_task('USERS',,,'ZWS');
__UPG.add_task('zle_dlugi',,,'FKS');
__UPG.add_task('fiks_81',,,'FKS');
__UPG.add_task('TIMER_2_PREL',,,'ZWS');
__UPG.add_task('ser_sch_ods',,,'FKS');
__UPG.add_task('projekty_etapy',,,'ZWS');
__UPG.add_task('rubryki_placowe_ppk',,,'PPL');
__UPG.add_task('jpk_Asi_sch',,,'FKS');
__UPG.add_task('jpk_d1_upd',,,'FKS');
__UPG.add_task('proc_ksef_emag2fiks',,,'ZWS');
__UPG.add_task('sync_mwa',,,'POR',3,,,,'T');
__UPG.add_task('p_zdjecie',,,'POR');
__UPG.add_task('DOKUMZ',,,'ZWS');
__UPG.add_task('portalwu_init',,,'POR');
__UPG.add_task('cit8_31_akt',,,'FKS');
__UPG.add_task('KST_upd',,,'PPL',1,,,,'T');
__UPG.add_task('op_drb',,,'FKS');
__UPG.add_task('ud_def_id',,,'FKS',,'T');
__UPG.add_task('atrybut_pakiet_mob',,,'PPL',,,,,'T');
__UPG.add_task('pkalsync_resend',,,'POR',,,,,'T');
__UPG.add_task('slo_naz_miejscpr',,,'POR',,,,,'T');
__UPG.add_task('portalk',,,'SEO',,,,,'T');
__UPG.add_task('portal_bnftt_nag_id',,,'POR',,,,,'T');
__UPG.add_task('sync_def_ud_def',,,'POR',,,,,'T');
__UPG.add_task('atr_plac_cw_woj',,,'PPL',,,,1,'T');
__UPG.add_task('portal_url_dod',,,'POR',,,,1,'T');
__UPG.add_task('fix_wn_seod',,,'SEO',,,,,'T');
__UPG.add_task('fix_wn_zaliczka',,,'POR',,,,,'T');
__UPG.add_task('wn_praktyka',,,'POR',,,,,'T');
__UPG.add_task('map_r',,,'POR',,,,,'T');
__UPG.add_task('chr_workinghours',,,'POR',,,,,'T');
__UPG.add_task('zs_def_put',,,'POR',,,,,'T');
__UPG.add_task('jpk_fa_4',,,'FKS',,,,,'T');
__UPG.add_task('PIT_2022_akt_2226_11',,,'PPL',,,,,'T');
__UPG.add_task('PIT_4R_akt_2226_19',,,'PPL',,,,,'T');
__UPG.add_task('sync_def_wv2',,,'POR',,,,1,'T');
__UPG.add_task('rubryki_PR22120041',,,'PPL',,,,,'T');
__UPG.add_task('atr_pak_mob',,,'PPL',,,,1,'T');
__UPG.add_task('H_pat_upd',,,'PKD',,,,,'T');
__UPG.add_task('PIT01_2226',,,'PPL',,,,,'T');
__UPG.add_task('ETYPY_P_pack',,,'POR',,,,,'T');
__UPG.add_task('s_zus',,,'PKD',,,,,'T');
__UPG.add_task('fix_defw_uid',,,'FKS',,,,,'T');
__UPG.add_task('fix_HTTPBANK',,,'HBN',,,,,'T');
__UPG.add_task('fix_edek_us',,,'ZWS',,,,,'T');
__UPG.add_task('rubryki_ER23020080',,,'PPL',,,,,'T');
__UPG.add_task('pkalsync_resend2',,,'POR',2,,,,'T');
__UPG.add_task('nbp_kursy_fsp2',,,'HBN',,,,,'T');
__UPG.add_task('fiks_87',,,'FKS',,,,,'T');
__UPG.add_task('cit8_32',,,'FKS',2,,,,'T');
__UPG.add_task('rubryki_ER23080027',,,'PPL',,,,,'T');
__UPG.add_task('portal_wn_urlop',,,'POR',,,,,'T');
__UPG.add_task('atrybut_zusz3',,,'PPL',,,,1,'T');
__UPG.add_task('faktury_seod_resend',,,'OBG',,,,,'T');
__UPG.add_task('fix_wn_no_seo',,,'SEO',,,,,'T');
__UPG.add_task('rubryki_ER23100002',,,'PPL',,,,,'T');
__UPG.add_task('por_work_place',,,'POR',,,,,'T');
__UPG.add_task('p_kal_nazw',,,'PKD',,,,,'T');
__UPG.add_task('kal_buff_w',,,'PRC',,,,,'T');

:: Zadania wykonywane dla każdej firmy osobno
__UPG.add_task('FO_USR',,'N','ZWS');
__UPG.add_task('FO_USR_1209',,'N','ZWS');
__UPG.add_task('BRAKI_K',,'N','TTE',,,,1);
__UPG.add_task('TOPER',,'N','TTE',,,,1);
__UPG.add_task('ZL',,'N','TTE',,,,1);
__UPG.add_task('ZLGD',,'N','TTE',,,,1);
__UPG.add_task('ZGH',,'N','TTE',,,,1);
__UPG.add_task('OFE',,'N','LSP',,,,1);
__UPG.add_task('FPACZ',,'N','LUM',,,,1);
__UPG.add_task('zaomeda',,'N','PED',,,,1);
__UPG.add_task('proj_typyzam',,'N','ZWS',,,,1);
__UPG.add_task('porslo',,'N','POR',1,,,1);
__UPG.add_task('porsloit',,'N','POR',,,,1);
__UPG.add_task('usr_upr_sts',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_stz',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_zam',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_zaw',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_trrodz',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_tars',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_tard',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_mkas',,'N','ZWS',,,,1);
__UPG.add_task('usr_upr_pelne',,'N','ZWS',,,,1);
__UPG.add_task('BIPKAT',,'N','POR',,,,1);
__UPG.add_task('REM_ZGL',,'N','TRE',,,,1);
__UPG.add_task('role_portal',,'N','ZWS',,,,1);
__UPG.add_task('ZPARN',,'N','TTE',,,,1);
__UPG.add_task('STATS',,'N','LMG',,,,1);
__UPG.add_task('role_personel',,'N','ZWS',,,,1);
__UPG.add_task('f_zatra_add',,'N','ZWS',,,,0);
__UPG.add_task('actKoszt',,'N','LZK',,,,1);
__UPG.add_task('cenniki_akt',,'N','LSP',,,,1);
__UPG.add_task('potwZAM',,'N','LZK',,,,1);
__UPG.add_task('zpr_ins_zsql',,'N','ZPR');
__UPG.add_task('TR_NZL',,'N','LTR',,,,1);
__UPG.add_task('TMAT',,'N','TTE',,,,1);
__UPG.add_task('GRUPPRAC',,'N','ZWS',,,,1);
__UPG.add_task('OZ',,'N','LZK',,,,1);
__UPG.add_task('GROP',,'N','TTE',,,,1);
__UPG.add_task('BADH',,'N','TTE',,,,1);
__UPG.add_task('zaomedadtcreate',,'N','PED',,,,1);
__UPG.add_task('PORTALB2',,'N','POR',,,,1);
__UPG.add_task('KEOP',,'N','LUO',,,,1);
__UPG.add_task('faks_info',,'N','LSP',,,,1);
__UPG.add_task('BRAKI_GD',,'N','TTE',,,,1);
__UPG.add_task('SYNC_IDP',,'N','ZWS',,,,1);
__UPG.add_task('REZ',,'N','LMG',,,,1);
__UPG.add_task('REJ_MAT',,'N','TTE',,,,1);
__UPG.add_task('sch_form_portal_nwu_access',,'N','POR',,,,1,'T');
__UPG.add_task('archiwum_del_test',,'N','ZWS',,,,1,'T');
__UPG.add_task('fiks_86',,,'FKS',,,,,'T');
__UPG.add_task('UPG_REAL_clean',,'N','ZWS',,,,1,'T');
__UPG.add_task('warlog_lopis',,'N','FKS',,,,2);
__UPG.add_task('PPK01_2226',,'N','PPK',,,,1,'T');
__UPG.add_task('cit8_31v1_upd',,'N','FKS',,,,1,'T');
__UPG.add_task('espr_jpk',,'N','FKS',,,,,'T');
__UPG.add_task('zam_R_M_K',,'N','LTR',,,,1,'T');

:: Zadania globalne, które muszą być wykonane po zadaniach dla firm
__UPG.add_task('cit8_31v1_del',,,'FKS',,,,,'T');

:: Zadania manualne
__UPG.add_task('szablony','N',,'ZWS',2,,,1,'T');
__UPG.add_task('cit8_31','N',,'FKS',,,,1);
__UPG.add_task('formuly_placowe','N','N','PPL',,,,1);
__UPG.add_task('procesy_produkcyjne','N','N','TTE',,,,1);
__UPG.add_task('procesy_portal_HR','N','N','POR',,,,1);
__UPG.add_task('pakiet_mobilnosci','N','N','PPL',,,,2);
__UPG.add_task('formuly_placowe_ppk','N','N','PPL',,,,1);
__UPG.add_task('procesy_sprzedazy','N','N','LSP',,,,1);
__UPG.add_task('procesy_zakupu','N','N','LZK',,,,1);
__UPG.add_task('procesy_remontowe','N','N','TRE',,,,1);
__UPG.add_task('procesy_businesslink','N','N','ZBL',,,,1);
__UPG.add_task('procesy_integracja','N','N','ZUI',,,,1);
__UPG.add_task('procesy_wspolne','N','N','ZWS',,,,1);
__UPG.add_task('procesy_faktur_obiegu','N','N','OBG',,,,1);
__UPG.add_task('procesy_paperlesshr','N','N','PEP',,,,1);
__UPG.add_task('uprawnienia_akcep_wn','N','N','POR',,,,1);
__UPG.add_task('procesy_personelowe','N','N','PKD',,,,1);
__UPG.add_task('podzial_kosztow','N','N','PPL',,,,1);
__UPG.add_task('uprawnienia_szablony','N','N','ZWS',,,,1);
__UPG.add_task('nieplatna_przerwa','N','N','PKD',,,,1);
__UPG.add_task('h_overlap','N','T','PKD',,,,1,'T');
__UPG.add_task('por_slowniki','N','N','POR',,,,1,'T');
__UPG.add_task('placa_oddelegowanie','N','N','PPL',,,,1,'T');
__UPG.add_task('platnosci_US','N','T','ZWS',,,,1,'T');
__UPG.add_task('portal_kre_his','N','N','POR',,,,1,'T');
__UPG.add_task('mechanizm_importow_PKD','N',,'ZWS',2,,,1,'T');
__UPG.add_task('procesy_pep_2301_0034','N','N','PEP',,,,1,'T');
__UPG.add_task('procesy_por_2301_0034','N','N','POR',,,,1,'T');
__UPG.add_task('PPK01_2226_man','N','N','PPK',,,,1,'T');
__UPG.add_task('formuly_placowe_kor_rh','N','N','PPL',,,,1,'T');
__UPG.add_task('mechanizm_importow','N',,'ZWS',3,,,1,'T');
__UPG.add_task('formuly_placowe_ER23080022','N','N','PPL',,,,1,'T');
__UPG.add_task('formuly_placowe_ER23080032','N','N','PPL',,,,1,'T');
__UPG.add_task('formuly_placowe_ER23080027','N','N','PPL',,,,1,'T');
__UPG.add_task('procesy_ER_2310_0014','N','N','PKD',,,,1,'T');

~~


\upgrade_act
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja typów danych oraz czynności dla wskazanych obszarów
::   WE: [_a] - dziedziny
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
:: Poniżej wpisać domeny, których dotyczą nowe lub aktualizowane czynności (ciekawe, czy uda się alfabetycznie ...)
::_dom:=spli_str('LMG.LSP.LZK.LUM.LOD.PKD.POR.PPK.PKW.PPL.TPP.TTE.TRE.WYP.ZWS.FST.FKS.KON.OBE.OBG.PBA.BIQ','.');
_dom_str:={? var_pres('_a')=type_of('')
          || _a
          || 'LSP.PED.ZWS.PKD.TTE.TRE.POR.OBE.ZPR.ZBL.OBG.SEO.BIQ.LUM.PRC.PPK'
          ?};
_dom:=spli_str(_dom_str,'.');
_len:=obj_len(_dom);

{? _len & _dom[1]<>''
|| FUN.prg_start(_len+2,'Aktualizacja czynności.',,,1);

:: Uzupełnienie domen (zawsze)
   FUN.prg_next();
   exec('add_domain','#b_action');
   __UPG.msg('Zaktualizowano dziedziny.');

:: Uzupełnienie typów (zawsze)
   FUN.prg_next();
   exec('fill_tab','#b_type',0);
   __UPG.msg('Zaktualizowano typy danych.');

   {! _i.._len
   |! FUN.prg_next();
      _domain:=exec('FindInSet','#table','B_DOMAIN','SYMBOL',_dom[_i]);
      {? _domain<>null()
      || exec('import_needed_dom','#b_design',_domain,0);
         __UPG.msg('Zaktualizowano czynności dziedziny: %1.'[_dom[_i]])
      ?}
   !};
   FUN.prg_stop()
?};
obj_del(_dom);
_res


\upgrade_act_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja typy danych oraz czynności dla wskazanych obszarów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja typów danych oraz czynności dla modelera'


\B_ACTION
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Aktualizacja czynności
::   WY: 0/1
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_result:=1;

_task:=app_info('task');
B_ACTION.cntx_psh();
B_ACTION.index('UNIK');
{? _task.first()
|| {!
   |? _put:=0;
      B_ACTION.prefix(_task.SYMBOL,);
      {? B_ACTION.first()
      || __lrec+=1;
         {? B_ACTION.INTERM<>_task.INTERM
         || B_ACTION.INTERM:=_task.INTERM;
            _put:=1
         ?};
         {? B_ACTION.MOBILE<>_task.MOBILE
         || B_ACTION.MOBILE:=_task.MOBILE;
            _put:=1
         ?};
         {? _put>0
         || __lmod+=1;
            {? B_ACTION.put()=0
            || _result:=0
            ?}
         ?}
      ?};
      _task.next()
   !}
?};
B_ACTION.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano czynności z definicji systemu.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\B_ACTION_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Aktualizacja czynności - opis
::   WY: STRING
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja czynności'


\B_DOMAIN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Aktualizacja dziedzin
::   WY: 0/1
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_result:=1;

_domain:=app_info('domain');
B_DOMAIN.cntx_psh();
B_DOMAIN.index('SYMBOL');
{? _domain.first()
|| {!
   |? _put:=0;
      B_DOMAIN.prefix(_domain.SYMBOL,);
      {? B_DOMAIN.first()
      || __lrec+=1;
         {? B_DOMAIN.INTERM<>_domain.INTERM
         || B_DOMAIN.INTERM:=_domain.INTERM;
            _put:=1
         ?};
         {? B_DOMAIN.MOBILE<>_domain.MOBILE
         || B_DOMAIN.MOBILE:=_domain.MOBILE;
            _put:=1
         ?};
         {? _put>0
         || __lmod+=1;
            {? B_DOMAIN.put()=0
            || _result:=0
            ?}
         ?}
      ?};
      _domain.next()
   !}
?};
B_DOMAIN.cntx_pop();
{? _result
|| __UPG.msg('Zaktualizowano czynności z definicji systemu.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\B_DOMAIN_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Aktualizacja dziedzin - opis
::   WY: STRING
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja dziedzin'


\BI_ARCH
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Aktualizacja archiwów procesów
::   WY: 0/1
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_result:=1;

BI_ARCH.cntx_psh();
BI_ARCH.index('MASKFIRM');
BI_ARCH.prefix();
{? BI_ARCH.first()
|| {!
   |? __lrec+=1;
      {? exec('update_counters','#bi_arch')>0
      || __lmod+=1
      || _result:=0
      ?};
      BI_ARCH.next()
   !}
?};
BI_ARCH.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano archiwa procesów.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\BI_ARCH_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Aktualizacja archiwów procesów - opis
::   WY: STRING
::  TAG: <PUBLICZNA>
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja archiwów procesów'


\funpar
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [20.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja.
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
{? exec('aktualizuj','funpar',1)
|| FP_DEF.cntx_psh();
   FP_DEF.index('ID');
   FP_DEF.prefix('T','ZWS_PAR_PPNW',);
   _del:={? FP_DEF.first() || FP_DEF.del(,1) || 1 ?};
   FP_DEF.cntx_pop();
   {? _del>0
   || __UPG.msg('Zaktualizowano zawartość obszaru: Ustawienia i parametryzacja.')
   || __UPG.msg('Usunięcie czynności parametryzacyjnej ZWS_PAR_PPNW nie powiodło się.');
      _ret:=-1
   ?}
|| __UPG.msg('Nie zaktualizowano zawartości obszaru: Ustawienia i parametryzacja.');
   _ret:=-1
?};
_ret


\funpar_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [17.14]
:: OPIS: Aktualizacja obszaru Ustawienia i parametryzacja - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ustawień i parametryzacji'


\color
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
exec('Color','#object');
exec('Legend','#object');
exec('odtw_kolor_iko','color',2);
__UPG.msg('Zostały zaaktualizowane schematy kolorów i ikon dla akcji Legenda.');
_res


\color_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MB] [21.37]
:: OPIS: Aktualizacja schematów kolorów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu kolorów.'


\stalesys_ini
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu
::----------------------------------------------------------------------------------------------------------------------
exec('init_wer','#stalesys');
__UPG.msg('Zaktualizowano stałe systemu.');
1


\stalesys_ini_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [18.42]
:: OPIS: Aktualizacja stałych systemu - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu'


\areatitle
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych
::----------------------------------------------------------------------------------------------------------------------
exec('init','areatitle');
__UPG.msg('Zainicjowano tytuły obszarów roboczych.');
1


\areatitle_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: GZ [18.42]
:: OPIS: Inicjuje AreaTitle - obiekt do wyświetlania tytułów w obszarach roboczych - opis
::----------------------------------------------------------------------------------------------------------------------
'Inicjuje AreaTitle - obiekt do wyświetlania tytułów obszarów roboczych'


\report
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Kompilacja wydruków
::----------------------------------------------------------------------------------------------------------------------
{? rep_tran('*')
|| _result:=1;
   __UPG.msg('Zaktualizowano wydruki i zestawienia.')
|| _result:=-1;
   __UPG.msg('Nie zaktualizowano wydruków i zestawień (wystąpił błąd kompilacji).')
?};
_result


\report_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Kompilacja wydruków - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wydruków i zestawień.'


\FO_USR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Aktualizacja parametrów FO użytkowników
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

_res:=exec('upgrade_usr','#params');

__UPG.msg('Zaktualizowano parametry użytkowników');
VAR_DEL.delete('__lrec','__lmod');
_res


\FO_USR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Aktualizacja parametrów FO użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja parametów użytkowników'


\FO_USR_1209
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Aktualizacja parametru FO: 1209
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

USERS.cntx_psh();
USERS.index('USR_AKOD');
USERS.prefix('T');
{? USERS.first()
|| {!
   |? __lrec+=1;
      _val:=exec('get','#params',1209,type_of(''),USERS.ref());
      {? _val='X'
      || exec('set','#params',1209,'T',USERS.ref());
         __lmod+=1
      ?};
      USERS.next()
   !}
?};
USERS.cntx_pop();

__UPG.msg('Zaktualizowano uniwersalny parametr (użytkownika) nr 1209 dla aktywnych użytkowników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\FO_USR_1209_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Aktualizacja parametrów FO: 1209 - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uniwersalnego parametru (użytkownika) nr 1209'


\TOPER
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli TOPER - operacje procesów technologicznych
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=TOPER.names();
_msk.clear();
TOPER.trig_off('*','*');
TOPER.cntx_psh();
{? _msk.first()
|| {!
   |? TOPER.use(_msk.NAME);
      TOPER.prefix();
      TOPER.for_each("
         _put:=0;
         __lrec+=1;
         {? TOPER.WHERE=''
         || TOPER.WHERE:='W';
            _put:=1
         ?};
         {? TOPER.NAZWA=''
         || TOPER.NAZWA:=exec('toper_nazwa','tech_oper');
            _put:=1
         ?};
         {? TOPER.TKTL_KTM=null()
         || TOPER.TKTL_KTM:=exec('FindAndGet','#table',TKTL,TOPER.NRK,,\"KTM\",null());
            _put:=1
         ?};
         {? _put || {? TOPER.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
TOPER.cntx_pop();
TOPER.trig_on('*','*');
__UPG.msg('Zaktualizowano operacje procesów technologicznych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\TOPER_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli TOPER - operacje procesów technologicznych - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla operacji procesów technologicznych'


\TMAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli TMAT - Surowce potrzebne w procesie technologicznym
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=TMAT.names();
_msk.clear();
TMAT.trig_off('*','*');
TMAT.cntx_psh();
{? _msk.first()
|| {!
   |? TMAT.use(_msk.NAME);
      TMAT.prefix();
      TMAT.for_each("
         _put:=0;
         __lrec+=1;
         {? TMAT.KTM=null()
         || {? TMAT.GRKTM='G'
            || TMAT.KTM:=exec('FindAndGet','#table',TTGP,TMAT.TGDFLT,,\"PT\",null())
            || TMAT.KTM:=TMAT.PT
            ?};
            _put:=1
         ?};
         {? TMAT.TKTL_KTM=null()
         || TMAT.TKTL_KTM:=exec('FindAndGet','#table',TKTL,TMAT.NRK,,\"KTM\",null());
            _put:=1
         ?};
         {? _put || {? TMAT.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
TMAT.cntx_pop();
TMAT.trig_on('*','*');
__UPG.msg('Zaktualizowano surowce technologiczne.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\TMAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli TOPER - Surowce potrzebne w procesie technologicznym - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla surowców technologicznych'


\ZGH
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli ZGH - nagłówki przewodników
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ZGH.cntx_psh();
ZGH.trig_off('*','*');
ZGH.prefix();
ZGH.for_each("
   _put:=0;
   __lrec+=1;
   {? ZGH.W_FIRST=''
   || ZGH.W_FIRST:='N';
      _put:=1
   ?};
   {? ZGH.W_LAST=''
   || ZGH.W_LAST:='N';
      _put:=1
   ?};
   {? _put || {? ZGH.put(1) || __lmod+=1 ?} ?}
",0);
ZGH.trig_on('*','*');
ZGH.cntx_pop();
__UPG.msg('Zaktualizowano nagłówki przewodników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZGH_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli ZGH - nagłówki przewodników - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla nagłówków przewodników'


\BRAKI_K
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Uzupełnia pola tabeli BRAKI_K - kwalifikacje braków
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
BRAKI_K.cntx_psh();
BRAKI_K.trig_off('*','*');
BRAKI_K.prefix();
_result:=BRAKI_K.for_each("
   _put:=0;
   __lrec+=1;
   {? BRAKI_K.STAT_B=''
   || BRAKI_K.STAT_B:='N';
      _put:=1
   ?};
   {? _put || {? BRAKI_K.put(1) || __lmod+=1 ?} ?}
",0);
BRAKI_K.trig_on('*','*');
BRAKI_K.cntx_pop();
__UPG.msg('Zaktualizowano kwalifikacje braków.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BRAKI_K_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Uzupełnia pola tabeli BRAKI_K - kwalifikacje braków - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla kwalifikacji braków'


\ZLGD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Uzupełnia pola tabeli ZLGD - robocizna
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=ZLGD.names();
_msk.clear();
ZLGD.trig_off('*','*');
ZLGD.cntx_psh();
{? _msk.first()
|| {!
   |? ZLGD.use(_msk.NAME);
      ZLGD.prefix();
      _result:=ZLGD.for_each("
         _put:=0;
         __lrec+=1;
         {? ZLGD.STAT_B=''
         || ZLGD.STAT_B:='N';
            _put:=1
         ?};
         {? ZLGD.ZGP().DOK<>null() & ZLGD.DK=''
         ||
            _dk:=exec('transfer_zlnd4zlgd','magdok_prod');
            {? _dk<>''
            || ZLGD.DK:=_dk;
               _put:=1
            ?}
         ?};
         {? _put || {? ZLGD.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
ZLGD.cntx_pop();
ZLGD.trig_on('*','*');
__UPG.msg('Zaktualizowano robociznę na zleceniach produkcyjnych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZLGD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Uzupełnia pola tabeli ZLGD - robocizna
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla robocizny na zleceniach produkcyjnych'


\ZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Uzupełnia pola tabeli ZL
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ZL.cntx_psh();
ZL.trig_off('*','*');
ZL.prefix();
ZL.for_each("
   _put:=0;
   __lrec+=1;
   {? ZL.STAT_B=''
   || {? ZL.BRAKI='T'
      || ZL.STAT_B:='N'
      || ZL.STAT_B:='T'
      ?};
      _put:=1
   ?};
   {? _put || {? ZL.put(1) || __lmod+=1 ?} ?}
",0);
ZL.trig_on('*','*');
ZL.cntx_pop();
__UPG.msg('Zaktualizowano zlecenia produkcyjne.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Uzupełnia pola tabeli ZGH - nagłówki przewodników - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla zleceń produkcyjnych'


\STATS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Naprawia ikonki w tabeli STATS - słownik statusów dostaw
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
STATS.cntx_psh();
STATS.prefix();
STATS.for_each("
   _put:=0;
   __lrec+=1;
   {? STATS.ICON*'status'>0
   || {? STATS.KIND='O'
      || STATS.ICON:=exec('get','#params',540011,type_of(''));
         _put:=1
      ?};
      {? STATS.KIND='Z'
      || STATS.ICON:=exec('get','#params',540012,type_of(''));
         _put:=1
      ?};
      {? STATS.KIND='N'
      || STATS.ICON:=exec('get','#params',540013,type_of(''));
         _put:=1
      ?}
   ?};
   {? _put || {? STATS.put(1) || __lmod+=1 ?} ?}
",0);
STATS.cntx_pop();
__UPG.msg('Zaktualizowano ikonki w słowniku statusów dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\STATS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Naprawia ikonki w tabeli STATS - słownik statusów dostaw - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizuje ikonki w słowniku statusów dostaw.'


\projekty_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Aktualizacja identyfikatorów wewnętrznych projektów i znaczników uprawnień do projektów.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

PROJEKTY.cntx_psh();
PROJEKTY.index('FIRMA');
PROJEKTY.prefix();
PROJEKTY.for_each("
__lrec+=1;
_put:=0;
{? PROJEKTY.ID_WEW=''
||
   PROJEKTY.ID_WEW:=(((7*'0')+$PROJEKTY.ID_KSG)+8);
   _put:=1
?};
{? PROJEKTY.UPR=''
||
   PROJEKTY.UPR:='N';
   _put:=1
?};
{? _put & PROJEKTY.put(1) || __lmod+=1 ?}
",1);
PROJEKTY.cntx_pop();

__UPG.msg('Zaktualizowano identyfikatory wewnętrzne projektów i znaczniki uprawnień do projektów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\projekty_id_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Aktualizacja identyfikatorów wewnętrznych projektów i znaczników uprawnień do projektów.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja identyfikatorów wewnętrznych projektów i znaczniki uprawnień do projektów.'


\proj_typyzam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Aktualizacja wartości typów ofert.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

TYPYZAM.cntx_psh();
TYPYZAM.prefix();
TYPYZAM.for_each("
__lrec+=1;
_put:=0;
{? TYPYZAM.PROJ=''
||
   TYPYZAM.PROJ:='N';
   _put:=1
?};
{? _put & TYPYZAM.put(1) || __lmod+=1 ?}
",1);
TYPYZAM.cntx_pop();

__UPG.msg('Zaktualizowano wartości typów ofert.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\proj_typyzam_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Aktualizacja wartości typów ofert.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wartości typów ofert.'


\OFE
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Uzupełnia pola typ oferty
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod','__typ_o');
__lrec:=__lmod:=0; __typ_o:=null();
TYPYZAM.cntx_psh();
OFE.cntx_psh();
TYPYZAM.index('TYP');
TYPYZAM.prefix('O','OFE',);
{? TYPYZAM.first()
|| __typ_o:=TYPYZAM.ref()
|| TYPYZAM.prefix();
   TYPYZAM.blank();
   TYPYZAM.R:='O';
   TYPYZAM.T:='OFE';
   TYPYZAM.NAZ:='Oferta';
   TYPYZAM.KOD:='OFE';
   {? TYPYZAM.add(1) || __typ_o:=TYPYZAM.ref() ?}
?};

OFE.prefix();
OFE.for_each("_put:=0;
              __lrec+=1;
              {? OFE.T=null()
              || OFE.T:=__typ_o;
                 _put:=1
              ?};
              {? _put || {? OFE.put(1) || __lmod+=1 ?} ?}
             ",0);
TYPYZAM.cntx_pop();
OFE.cntx_pop();
__UPG.msg('Zaktualizowano oferty o typ oferty.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod','__typ_o');
_result


\OFE_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Uzupełnia pola typ oferty - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie obsługi typów ofert'


\zaomeda
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Zmiana budowy identyfikatora dokumentacji i identyfikatora dokumentu.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_bledow:=0;
_poprawnych:=_do_poprawy:=_poprawionych:=_niepoprawionych:=0;
ZAOMEDA.cntx_psh();
ZAOMEDA.prefix();
ZAOMEDA.f_set('NAG');
_wszystkich:=ZAOMEDA.size();
{? ZAOMEDA.f_first()
|| OSOBA.cntx_psh();
   OSOBA.prefix();
   ZALACZ.cntx_psh();
   ZALACZ.prefix();
   ZAOMEDA.trig_off('*','*');
   {!
   |? {? ref_tab(ZAOMEDA.NAG)=ZALACZ
      || {? ZALACZ.seek(ZAOMEDA.NAG)
         || {? ZAOMEDA.NAG=ZALACZ.uidref() & ZAOMEDA.OSOBA=ZALACZ.OSOBA & ZAOMEDA.ID=ZALACZ.OSOBA().uidref()
            || _poprawnych+=1
            || _do_poprawy+=1;
               ZAOMEDA.NAG:=ZALACZ.uidref();
               ZAOMEDA.ID:=ZALACZ.OSOBA().uidref() ;
               ZAOMEDA.OSOBA:=ZALACZ.OSOBA;
               {? ZAOMEDA.put()
               || _poprawionych+=1
               || _niepoprawionych+=1
               ?}
            ?}
         || _bledow+=1
         ?}
       ?};
      ZAOMEDA.f_next()
   !};
   ZAOMEDA.trig_on('*','*');
   ZALACZ.cntx_pop();
   OSOBA.cntx_pop()
?};
ZAOMEDA.f_clear();
ZAOMEDA.cntx_pop();

__UPG.msg('Rekordów w dziedzinie: %1' [$_wszystkich]);
__UPG.msg(' * Rekordów poprawnych (niewymagających zmian): %1' [$_poprawnych]);
__UPG.msg(' * Rekordów do zmiany (wymagających zmian): %1' [$_do_poprawy]);
__UPG.msg('   - Rekordów poprawionych: %1' [$_poprawionych]);
__UPG.msg('   - Rekordów, których nie udało się poprawić: %1' [$_niepoprawionych]);
{? _bledow>0
|| __UPG.msg('');
   __UPG.msg('W trakcie przetwarzania nie udało się znaleźć wszystkich rekordów źródłowych.');
   __UPG.msg('Operacja musi być powtórzona.')
?};

{? _bledow=0 || 1 || -1 ?}


\zaomeda_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Zmiana budowy identyfikatora dokumentacji i identyfikatora dokumentu - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dokumentacja pracownicza:\n'
' - zmiana budowy identyfikatora dokumentacji;\n'
' - zmiana budowy identyfikatora dokumentu;\n'
' - uzupełnienie nowego pola (OSOBA).'


\rubryki_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie rubryk i atrybutów płacowych.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

:: Aktualizacja i dodanie nowych składników płacowych. ----------------------------------------------------------------
::7158     OZ: płaca nominalna
::7159     OZ: płaca ZUS i pod.
::7160     OZ: płaca ZUS
::7161     OZ: płaca podatek.
::7162     OZ: pł. b.ZUS b.pod.
::7081     OZ: przych. op. zag.
::7150     OZ: podst. ubezp.
::7151     OZ: Zaliczka podatek
::7152     OZ: Ubz. społ. prac.
::7153     OZ: Ubz. społ. firma
::7154     OZ: Ubz. zdro. prac.
::7155     OZ: Ubz. zdro. firma
::7124     OZ: ZUS od prz. zag.
::7156     OZ: Choroba
::7157     OZ: Urlop
::7163     OZ: pods. zdr. zag.
::7164     OZ: suma wyn. zagr.
::7165     OZ: suma bez pod.
::7166     OZ: Diety pom. zag.
::7167     OZ: suma bez ZUS
::7076     OZ: Noclegi bez pod.
::7078     OZ: P.służb.bez pod.
::7073     OZ: Kwota za noclegi
::7074     OZ: Kwota diet
::7075     OZ: Noclegi-przychód
:: Pakiet mobilności->oddelegowania:
::7191     OZ: Pom. p. em.i re.
::7192     OZ: Pom. p. ch.i wy.
::7193     OZ: Pom. p. ub. zdr.
::7194     OZ: Pom. p. FP i FG.
::7195     OZ: Sk. nst. ob. p.
::7196     OZ: Pom. podst. PPK
::7197     OZ: Diety pomn. prz.
::7198     ZDR: Diety pom. prz.
::7199     ZDR: Sk. nst. ob. p.
::7200     Przychód do wyliczeń

::7204     OZ: Diety mobilne

:: ER/WRT/XP/12.51/2205/0040 : Brak rozliczenia zerowych premii okresowych na liście dodatkowej
::                             - problem po aktualizacji Polski Ład AK/WRT/XP/12.51/2112/12.51_139
::7210     Rozl. prem. okresowe

{? +exec('import','rubryki','g',
      '7158,7159,7160,7161,7162,7081,7150,7151,7152,7153,7154,7155,7124,7156,7157,7163,7164,7165,7077,7166,7167,7168,'
      '7076,7078,7073,7074,7075,7191,7192,7193,7194,7195,7196,7197,7198,7199,7200,7204,7210',
      1,0)
|| _txt:='Dodanie nowych rubryk zakończyło się niepowodzeniem';
   _result:=-1
|| _txt:='Dodanie nowych rubryk zakończone powodzeniem'
?};
__UPG.msg(_txt);
{? _result=-1 || return(_result) ?};

:: Aktualizacja atrybutów rubryk. -------------------------------------------------------------------------------------

:: Rozbudowa oddelegowań:
exec('add_use','rubatr',48,7158);
exec('add_use','rubatr',1011223,7158);
exec('add_attr','rubatr',111,1114,'Urlop oddelegowanie',1,,,7157);
exec('add_use','rubatr',193,7157);
exec('add_attr','rubatr',,102,'Oddelegowanie zagraniczne',1,'');
exec('add_attr','rubatr',102,1021,'Przychód opodatkowany zagraniczny',1);
   exec('add_attr','rubatr',1021,10211,'Wynagrodzenia opodatkowane zagraniczne',1,,,7160,7162);
   exec('add_attr','rubatr',1021,10212,'Dodatki opodatkowane zagraniczne',1,'Dodatki opodatkowane zagraniczne');
exec('add_attr','rubatr',102,1022,'Podstawa ubezpieczeń zagranicznych',1);
   exec('add_attr','rubatr',1022,10221,'Wynagrodzenia - podstawa ubez. zagranicznych',1,
      'Wynagrodzenia - podstawa ubezpieczeń zagranicznych.',,7161,7162);
   exec('add_attr','rubatr',1022,10222,'Dodatki - podstawa ubez. zagranicznych',1,
      'Dodatki - podstawa ubezpieczeń zagranicznych.');
exec('add_use','rubatr',333,7151,7152,7154);
exec('add_attr','rubatr',102,1023,'Podstawa ubezpieczeń zdrowotnych zagranicznych',1);
   exec('add_attr','rubatr',1023,10231,'Wynagrodzenia - podstawa ubez. zdr. zagranicznych',1,
      'Wynagrodzenia - podstawa ubezpieczeń zdrowotnych zagranicznych.',,7161,7162);
   exec('add_attr','rubatr',1023,10232,'Dodatki - podstawa ubez. zdr. zagranicznych',1,
      'Dodatki - podstawa ubezpieczeń zdrowotnych zagranicznych.');
exec('add_attr','rubatr',102,1024,'Wynagrodzenie oddelegowań zagranicznych',1,,,7158);
exec('add_attr','rubatr',102,1025,'Wynagrodzenia jednocześnie krajowe i zagraniczne',1,
   'Wynagrodzenia jednocześnie krajowe i zagraniczne wyliczane wg proporcji ilości dni pracy w oddelegowaniu.');
exec('add_attr','rubatr',102,1026,'Przychód opodatkowany w Polsce',1,'Przychód opodatkowany w Polsce.');
   exec('add_attr','rubatr',1026,10261,'Wynagrodzenia opodatkowane w Polsce',1,
      'Wynagrodzenia z oddelegowań opodatkowane w Polsce.',,7159,7161);
   exec('add_attr','rubatr',1026,10262,'Dodatki opodatkowane w Polsce',1,
      'Dodatki z oddelegowań opodatkowane w Polsce.');

:: Rozbudowa oddelegowań - pakiet mobilności:
exec('add_attr','rubatr',901,9016,'Kwota diet pomniejszająca podstawę emeryt. i rent.',1,
      'Kwota diet pomniejszająca podstawę emerytalną i rentową.',,7191);
exec('add_attr','rubatr',901,9017,'Kwota diet pomniejszająca podstawę chor. i wyp.',1,
      'Kwota diet pomniejszająca podstawę chorobową i wypadkową.',,7192);
exec('add_attr','rubatr',901,9018,'Kwota diet pomniejszająca podstawę ubezp. zdrowot.',1,
      'Kwota diet pomniejszająca podstawę ubezpieczenia zdrowotnego.',,7193);
exec('add_attr','rubatr',901,9019,'Kwota diet pomniejszająca podstawę FP i FGŚP',1,
      'Kwota diet pomniejszająca podstawę FP i FGŚP.',,7194);
exec('add_attr','rubatr',901,90101,'Kwota diet pomniejszająca podstawę PPK',1,
      'Kwota diet pomniejszająca podstawę PPK.',,7196);
exec('add_attr','rubatr',901,90102,'Rubryki nie stanowiące przychodu do odliczeń',1,
      'Rubryki nie stanowiące przychodu do odliczeń.',,
      510,511,512,513,514,515,516,517,523,524,529,530,531,532,537,7017);
exec('add_attr','rubatr',901,90103,'Kwota diet obniżająca przychód',1,
      'Kwota diet obniżająca przychód.',,7197);
exec('add_attr','rubatr',901,90104,'Składki niestanowiące obniżenia podstawy opodat.',1,
      'Składki niestanowiące obniżenia podstawy opodatkowania.',,7195);
exec('add_attr','rubatr',901,90105,'Kwota diet obniżająca przychód zagraniczny',1,
      'Kwota diet obniżająca przychód zagraniczny.',,7166);
exec('add_use','rubatr',9053,7197);
exec('add_attr','rubatr',906,9068,'Kwota diet pomniejszająca przychód',1,
     'Kwota diet pomniejszająca przychód.',,7198);
exec('add_attr','rubatr',906,9069,'Składki niestanowiące obniżenia podstawy opodat.',1,
     'Składki niestanowiące obniżenia podstawy opodatkowania.',,7199);

:: ER/WRT/XP/12.51/2107/0002 : Przeliczanie godzin do urlopu-brak przeliczenia godzin przy zmianie zaszeregowania ...
exec('add_attr','rubatr',1011214,101121406,'L.g. dod. odb. 50%',1,'',17,7015);
exec('add_attr','rubatr',1011214,101121407,'L.g. dod. odb. 100%',1,'',18,7016);

__UPG.msg('Zakończono aktualizację atrybutów składników płacowych.');

_result


\rubryki_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie rubryk i atrybutów płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk i atrybutów płacowych.'


\formuly_placowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie formuł płacowych.
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

::Przesunięcie 7124 po 769:
R.cntx_psh();
R.index('RUBLP');
R.clear();
{? R.find_tab(,'RN',,'=',769)
|| _lp:=R.LP+1
|| _lp:=0
?};
{? _lp & R.find_tab(,'RN',,'=',7124) & R.get() & R.lock(1,,1)
|| R.LP:=_lp;
   R.put();
   exec('lp_put_a','!zws_par_pskl')
?};
R.unlock();
R.cntx_pop();
__RUB.fill();

_rs:='7157,780,7081,7150,7124,7156,7163,791,7159,7160,7161,7162,7165,7166,7200,7197,7198,7195,7199,754,790,981,100';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('D,F,R,U,W,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie formuł płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuł płacowych'


\formuly_txt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły. Jeśli zamiast treści zwrócona będzie liczba 0 (zwrócona wartość typu liczbowego) - oznacza to
::       chęć usunięcia danej formuły płacowej.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=7157 & ',U,F,%1,'[%255]*',%1,'[_tp]
:: =================
:: OZ: Urlop.
|| "exec('oz_urlop','oddelegowanie')"

|? _rn=780 & _tp<>'R'
:: =================
:: Przychód
|| "
   exec('przychod','lista_licz')"

|? _rn=7081 & ',U,F,D,%1,'[%255]*',%1,'[_tp]
:: =================
:: OZ: przych. op. zag.
|| "exec('przych_op_zag','oddelegowanie')"

|? _rn=7150 & ',U,F,D,%1,'[%255]*',%1,'[_tp]
:: =================
:: OZ: podst. ubezp.
|| "FUNKCJE.L_SYS(1022)"

|? _rn=7124 & ',U,F,D,%1,'[%255]*',%1,'[_tp]
:: =================
:: OZ: ZUS od prz. zag.
|| "
   exec('zus_prz_zag','oddelegowanie')"

|? _rn=7156 & ',U,F,%1,'[%255]*',%1,'[_tp]
:: =================
:: OZ: Choroba.
|| "exec('oz_choroba','oddelegowanie')"

|? _rn=7163 & ',U,F,D,%1,'[%255]*',%1,'[_tp]
:: =================
:: OZ: pods. zdr. zag.
|| "FUNKCJE.L_SYS(1023)"

|? _rn=7162 & ',U,F,D,%1,'[%255]*',%1,'[_tp]
:: =================
:: OZ:
|| "exec('zlicz_wynagrodzenie','oddelegowanie')"

|? _rn=791 & _tp<>'R'
:: =================
:: Podst. ub. zdrow.
|| "
   exec('podst_ub_zdrow','lista_licz')"

|? _rn=7166 & ',U,F,%1,'[%255]*',%1,'[_tp]
:: =================
:: OZ: Diety pom. zag.
|| "exec('diety_pom_zag','oddelegowanie')"

|? _rn=7159 | _rn=7160 | _rn=7161 | _rn=7165
:: =================
:: OZ: xx. - usunięcie nieaktualnych formuł płacowych
|| 0

::7200     Przychód do wyliczeń
::7197     OZ: Diety pomn. prz.
::7198     ZDR: Diety pom. prz.
::7195     OZ: Sk. nst. ob. p.
::7199     ZDR: Sk. nst. ob. p.
|? _rn=7200 & ',D,F,U,W,%1,'[%255]*',%1,'[_tp]
|| "exec('przych_do_wyl','lista_licz')"
|? _rn=7197 & ',D,F,U,W,%1,'[%255]*',%1,'[_tp]
|| "exec('dieta2podatek','oddelegowanie',90103,1)"
|? _rn=7198 & ',D,F,U,W,%1,'[%255]*',%1,'[_tp]
|| "exec('dieta2podatek','oddelegowanie',9068,0)"
|? _rn=7195 & ',D,F,U,W,%1,'[%255]*',%1,'[_tp]
|| "exec('skl_n_p_op','lista_licz',90103,90104,1)"
|? _rn=7199 & ',D,F,U,W,%1,'[%255]*',%1,'[_tp]
|| "exec('skl_n_p_op','lista_licz',9068,9069,0)"
::
::754    Podst. chor. i wyp.
::780    Przychód
::790    Podatek nominalny
::981    Podst. FP i FGŚP
|? _rn=754 & ',R,'*',%1,'[_tp]
|| "
{? RH.ZC_INFO().FW='T'
|| exec('podstawa_sk','oddelegowanie','.ZUS',754,1)+FUNKCJE.Z(752)
?}"
|? _rn=780 & ',R,'*',%1,'[_tp]
|| "exec('przychod','zlec_rh')"
|? _rn=790 & ',R,'*',%1,'[_tp]
|| "
DoList.z[12]:={? RH.ZC_INFO().KC='T'
              || exec('podstawa_sk','oddelegowanie','.UBZ',791,1)-FUNKCJE.Z(765,767)
              ?};
exec('podatek','zlec_rh')
"
|? _rn=981 & ',R,'*',%1,'[_tp]
|| "{? RH.ZC_INFO().FP='T' | RH.ZC_INFO().FG='T' || exec('podstawa_sk','oddelegowanie','.ZUS',981,1) ?}"
|? _rn=100 & ',U,'*',%1,'[_tp]
|| "exec('placa_zasadnicza','!ppl_pll_nals','U')"
|? _rn=100 & ',F,'*',%1,'[_tp]
|| "exec('placa_zasadnicza','!ppl_pll_nals','F')"

|| ""
?}


\akt_proc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizacja słownika procedur szczególnych.
::----------------------------------------------------------------------------------------------------------------------
exec('init_slo','jpk_v');
__UPG.msg('Zaktualizowano słownik procedur szczególnych')


\akt_proc_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Aktualizacja słownika procedur szczególnych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słownika procedur szczególnych'


\porslo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Aktualizacja słowników wykorzystywanych na portalu.
::----------------------------------------------------------------------------------------------------------------------
exec('init','portal_slowniki','J');
exec('init','portal_slowniki','W');
__UPG.msg('Zaktualizowano słowniki portalowe.');
1


\porslo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Aktualizacja słowników wykorzystywanych na portalu - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słowników wykorzystywanych na portalu.'


\jpk_fa_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Aktualizacja struktury pliku JPK_FA
::----------------------------------------------------------------------------------------------------------------------
_fml:="exec('jpk_fa_tt','jpk')";
_zmiana:=-1;
ISTDEF.cntx_psh();
ISTDEF.index('JPK');
ISTDEF.prefix('J','FA','JPK_FA(3)_v1-0',);
{? ISTDEF.first()
|| _zmiana:=0;
   ISTDEFS.cntx_psh();
   ISTDEFS.index('OPIS');
   ISTDEFS.prefix(ISTDEF.ref(),'P_23');
   {? ISTDEFS.first() & ISTDEFS.REGULY<>$_fml
   || ISTDEFS.REGULY:=$_fml;
      _zmiana:=ISTDEFS.put()
   ?};
   ISTDEFS.cntx_pop()
?};
ISTDEF.cntx_pop();
{? _zmiana=1
|| __UPG.msg('Zaktualizowano schemat JPK_FA (3).')
|? _zmiana=-1
|| __UPG.msg('Nie znaleziono schematu JPK_FA (3).')
|| __UPG.msg('Aktualizacja schematu JPK_FA (3) nie była wymagana.')
?};
1


\jpk_fa_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Aktualizacja struktury pliku JPK_FA - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja schematu JPK_FA (3)\n'
'Zmiana formuły dla pola JPK\\Faktura\\P_23'


\odd_pola
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie nowych pól O.B_ODD, H_ODDE.POD_M, H.CZYWAL4, H_ODDP.KW_PLN.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err:=_suc:=_goo:=0;
_ret:=1;
{? var_pres('POD_M',H_ODDE)>0
|| _MSK:=H_ODDE.names();
   _MSK.clear();
   {? _MSK.first()
   || H_ODDE.cntx_psh();
      {!
      |? H_ODDE.use(_MSK.NAME);
         H_ODDE.prefix();
         {? H_ODDE.first()
         || H_ODDE.trig_off('*','*');
            {!
            |? {? ',W,O,B,'*',%1,'[H_ODDE.POD_M]
               || _goo+=1
               || H_ODDE.POD_M:='B';
                  {? H_ODDE.put()
                  || _suc+=1
                  || _err+=1
                  ?}
               ?};

               H_ODDE.next()
            !};
            H_ODDE.trig_on('*','*')
         ?};

         _MSK.next()
      !};
      H_ODDE.cntx_pop()
   ?};
   obj_del(_MSK)
|| __UPG.msg('Brak pola H_ODDE.POD_M w definicji.');
   _ret:=-1
?};

{? var_pres('CZYWAL4',H)>0
|| _MSK:=H.names();
   _MSK.clear();
   {? _MSK.first()
   || H.cntx_psh();
      {!
      |? H.use(_MSK.NAME);
         H.prefix();
         {? H.first()
         || H.trig_off('*','*');
            {!
            |? {? ',T,N,'*',%1,'[H.CZYWAL4]
               || _goo+=1
               || H.CZYWAL4:='N';
                  {? H.put()
                  || _suc+=1
                  || _err+=1
                  ?}
               ?};

               H.next()
            !};
            H.trig_on('*','*')
         ?};

         _MSK.next()
      !};
      H.cntx_pop()
   ?};
   obj_del(_MSK)
|| __UPG.msg('Brak pola H.CZYWAL4 w definicji.');
   _ret:=-1
?};

{? var_pres('B_ODD',O)>0
|| O.cntx_psh();
   O.prefix();
   {? O.first()
   || O.trig_off('*','*');
      {!
      |? {? ',T,N,'*',%1,'[O.B_ODD]
         || _goo+=1
         || O.B_ODD:='N';
            {? O.put()
            || _suc+=1
            || _err+=1
            ?}
         ?};

         O.next()
      !};
      O.trig_on('*','*')
   ?};
   O.cntx_pop()
|| __UPG.msg('Brak pola O.B_ODD w definicji.');
   _ret:=-1
?};

{? var_pres('KW_PLN',H_ODDP)>0
|| WAL.cntx_psh();
   O.cntx_psh();
   O.index('LISTYPZN');
   H.cntx_psh();
   P.cntx_psh();
   H_ODDE.cntx_psh();
   H_ODDP.cntx_psh();
   H_ODDP.prefix();
   {? H_ODDP.first()
   || H_ODDP.trig_off('*','*');
      exec('RAPLS','object');
      {!
      |? {? +H_ODDP.LT
         || O.prefix(H_ODDP.H_ODDE().H().P().FIRMA);
            {? O.find_key(-H_ODDP.LT)
            || {? H_ODDP.KW_PLN<>0
               || _goo+=1
               || _kurs:=
                     {? H_ODDP.WAL().SYM<>'PLN'
                     || exec('kurs_waluty','oddelegowanie',WAL.SYM,H_ODDP.H_ODDE().TKRS)
                     || 1
                     ?};
                  H_ODDP.KW_PLN:=H_ODDP.KW*_kurs$2;
                  {? H_ODDP.put()
                  || _suc+=1
                  || _err+=1
                  ?}
               ?}
            || _err+=1
            ?}
         ?};

         H_ODDP.next()
      !};
      H_ODDP.trig_on('*','*')
   ?};
   H_ODDP.cntx_pop();
   H_ODDE.cntx_pop();
   P.cntx_pop();
   H.cntx_pop();
   O.cntx_pop();
   WAL.cntx_pop()
|| __UPG.msg('Brak pola O.B_ODD w definicji.');
   _ret:=-1
?};

__UPG.msg('Liczba poprawionych rekordów: %1' [$_suc]);
{? _goo
|| __UPG.msg('Liczba rekordów niewymagających poprawienia: %1' [$_goo])
?};
{? _err
|| __UPG.msg('Liczba niepoprawionych rekordów: %1' [$_err]);
   _ret:=-1
?};

_ret


\odd_pola_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie nowych pól H_ODDE.POD_M, H.CZYWAL4, O.B_ODD.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełniono pola O.B_ODD, H_ODDE.POD_M i H.CZYWAL4 wartością początkową.'


\porsloit
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Aktualizacja elementów słowników wykorzystywanych na portalu.
::----------------------------------------------------------------------------------------------------------------------
_porslo:=obj_ntab_names(obj_new('TypOdbioru','OdbiorNadgodzin','OkresyRozliczeniowe'));
_firma:=exec('por_conf_firma','portal_lib',exec('ref_firma','ustawienia')).firma;

_delAllFrom:=
   "_firma:={? var_pres('_a')=type_of(null) || _a || return(_result) ?};
    _code:={? var_pres('_b')=type_of('') & _b<>'' || _b || return(_result) ?};
    PORSLO.cntx_psh();
    PORSLO.index('FIELD');
    PORSLO.prefix(_firma,exec('customField','portal_slowniki'),'',_code);
    {? PORSLO.first()
    || PORSLOIT.cntx_psh();
       PORSLOIT.index('SLOVAL');
       PORSLOIT.prefix(PORSLO.ref,);
       {? PORSLOIT.first() || {! |? PORSLOIT.del !} ?};
       PORSLOIT.cntx_pop()
    ?};
    PORSLO.cntx_pop()
   ";

{! _index:=1..obj_len(_porslo) |! _delAllFrom(_firma,_porslo[_index]) !};

exec('init','portal_slowniki','J');
exec('init','portal_slowniki','W');

__UPG.msg('Zaktualizowano zawartość słowników portalowych.'@);

1


\porsloit_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Aktualizacja elementów słowników wykorzystywanych na portalu - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zawartości słowników wykorzystywanych na portalu.'


\dok_proc_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Aktualizacja pola dla procedury szczególnej DOK.PROC
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=0;
__lmod:=0;

DOK.cntx_psh();
_maskAr:=DOK.names();
{? _maskAr.first()
|| {!
   |? DOK.use(_maskAr.NAME);
      DOK.prefix();
      DOK.for_each("
         __lrec+=1;
         {? DOK.JPK_PROC<>''
         || _valsAr:=spli_str(DOK.JPK_PROC,',');
            {! _i:=1..obj_len(_valsAr)
            |! {? DOK.PROC=null()
               || DOK.PROC:=exec('FindInSet','#table','JPK_SLO','KOD',_valsAr[_i],1,,1,'PROC_FAKS',null())
               ?}
            !};
            {? DOK.put(1)
            || __lmod+=1
            ?}
         ?}
      ");
      _maskAr.next()
   !}
?};
DOK.cntx_pop();

__UPG.msg('Zaktualizowano pole dla procedury szczególnej.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\dok_proc_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Aktualizacja pola dla procedury szczególnej DOK.PROC
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola dla procedury szczególnej'


\usr_upr_sts
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do stanowisk sprzedaży
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_0','users_upraw','STS');
1


\usr_upr_sts_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do stanowisk sprzedaży
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do stanowisk sprzedaży'


\usr_upr_stz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do stanowisk zakupu
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_0','users_upraw','STZ');
1


\usr_upr_stz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do stanowisk zakupu
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do stanowisk zakupu'


\usr_upr_zam
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do magazynów realizujących zamówienia sprzedaży
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_0','users_upraw','ZAM');
1


\usr_upr_zam_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do magazynów realizujących zamówienia sprzedaży
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do magazynów realizujących zamówienia sprzedaży'


\usr_upr_zaw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do magazynów realizujących zamówienia wewnętrzne
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_0','users_upraw','ZAW');
1


\usr_upr_zaw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do magazynów realizujących zamówienia wewnętrzne
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do magazynów realizujących zamówienia wewnętrzne'


\usr_upr_trrodz
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do kategorii transportu
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_0','users_upraw','TR_RODZ');
1


\usr_upr_trrodz_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do kategorii transportu
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do kategorii transportu'


\usr_upr_tars
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do cenników sprzedaży
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_0','users_upraw','TARS');
1


\usr_upr_tars_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do cenników sprzedaży
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do cenników sprzedaży'


\usr_upr_tard
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do cenników dostawców
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_0','users_upraw','TARD');
1


\usr_upr_tard_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do cenników dostawców
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do cenników dostawców'


\usr_upr_mkas
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do multikas
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_0','users_upraw','MKAS');
1


\usr_upr_mkas_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja uprawnień użytkowników do multikas
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja uprawnień użytkowników do multikas'


\kontr_st_zwol
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Dodaje formułę początkową formuły kontrolnej sprawdzającej podanie powodu użycia stawki zwolnionej VAT
::----------------------------------------------------------------------------------------------------------------------
_name:='ST_ZWOL';
_ret:=1;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G',_name,);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:=_name;
   FORMULA.NAZWA:='Formuła początkowa dla kontroli uzupełnienia powodu użycia zwolnionej stawki VAT';
   FORMULA.FORMULA:=$"exec('st_zwol','%fks_spr_dok')";
   _ret:=FORMULA.add();
   {? _ret
   || __UPG.msg('Dodano formułę %1.'[_name])
   || __UPG.msg('Nie udało się dodać formuły %1.'[_name])
   ?}
|| __UPG.msg('Istnieje już formuła %1.'[_name])
?};
FORMULA.cntx_pop();
_ret


\kontr_st_zwol_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Dodaje formułę początkową formuły kontrolnej sprawdzającej podanie powodu użycia stawki zwolnionej VAT - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły ST_ZWOL.\n'
'Jest to początkowa formuła formuły kontrolnej dla sprawdzenia uzupełnienia powodu użycia zwolnionej stawki VAT.'


\BIPKAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Uzupełnienie wartości w nowym polu BIPKAT.SYSTEM
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

BIPKAT.cntx_psh();
BIPKAT.prefix();
{? BIPKAT.first()
|| BIPKAT.trig_off('*','*');
   {!
   |? {? BIPKAT.SYSTEM=''
      || BIPKAT.SYSTEM:='N';
         BIPKAT.put(1)
      ?};

      BIPKAT.next()
   !};
   BIPKAT.trig_on('*','*')
?};

:: Uzupełnienie wartości systemowej
::_firma:=exec('ref_firma','ustawienia');
::_funkc:='WIEDZA';
::_kod:='OPPSF';
::BIPKAT.index('KOD');
::BIPKAT.prefix(_firma,_funkc,);
::BIPKAT.trig_off('*','*');
::{? BIPKAT.find_key(_kod)
::|| {? BIPKAT.SYSTEM<>'T'
::   || BIPKAT.SYSTEM:='T';
::      BIPKAT.put(1)
::   ?}
::|| _nazwa:='Oświadczenia odnośnie wykonywania pracy poza siedzibą firmy';
::   _obieg:='Obieg wniosków';
::   _typObieg:=exec('FindInSet','#table','TYPOBIEG','UNIK',_obieg,_obieg,,1,,null);
::   _typ:='Polecenie pracy poza siedzibą firmy';
::   _portal:='T';
::   _etypy:=exec('FindInSet','#table','ETYPY','UNIK_WP',_typ,_typObieg,,1,_portal,null);
::   {? _etypy<>null
::   || BIPKAT.blank(1);
::      BIPKAT.FIRMA:=_firma;
::      BIPKAT.FUNKC:=_funkc;
::      BIPKAT.KOD:=_kod;
::      BIPKAT.NAZWA:=_nazwa;
::      BIPKAT.IKONA:='absui-icon--doc-file';
::      BIPKAT.OPIS:=_nazwa;
::      BIPKAT.WNIOSEK:=_etypy;
::      BIPKAT.POW_LIST:='N';
::      BIPKAT.POW_PUSH:='N';
::      BIPKAT.SAMOUCZ:='N';
::      BIPKAT.SYSTEM:='T';
::      BIPKAT.add(1);
::      __UPG.msg('Dodano kategorię elementów bazy wiedzy "%1 - %2" do kartoteki BIPKAT.'[_kod,_nazwa])
::   || __UPG.msg('Nie znaleziono typu wniosku "%1" dla kategorii systemowej "%2 - %3".'[_typ,_kod,_nazwa]);
::      _result:=-1
::   ?}
::?};
::BIPKAT.trig_on('*','*');
BIPKAT.cntx_pop();

__UPG.msg('Uzupełniono wartości w nowym polu SYSTEM kartoteki BIPKAT.');

_result


\BIPKAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Uzupełnienie wartości w nowym polu BIPKAT.SYSTEM - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie wartości w nowym polu SYSTEM kartoteki BIPKAT.'


\REM_ZGL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli REM_ZGL - zgłoszenia remontowe
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
REM_ZGL.trig_off('*','*');
REM_ZGL.cntx_psh();
REM_ZGL.prefix();
REM_ZGL.for_each("
   _put:=0;
   __lrec+=1;
   {? REM_ZGL.GEN_POW=''
   || REM_ZGL.GEN_POW:='N';
      _put:=1
   ?};
   {? REM_ZGL.SYM_TOP=''
   || REM_ZGL.SYM_TOP:=REM_ZGL.SYM;
      _put:=1
   ?};
   {? _put || {? REM_ZGL.put(1) || __lmod+=1 ?} ?}
",0);
REM_ZGL.cntx_pop();
REM_ZGL.trig_on('*','*');
__UPG.msg('Zaktualizowano zgłoszenia remontowe.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\REM_ZGL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli REM_ZGL - zgłoszenia remontowe - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla zgłoszeń remontowych'


\USERSF
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja użytkowników w firmach
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
USERSF.trig_off('*','*');
USERSF.cntx_psh();
USERSF.prefix();
USERSF.for_each("
   __lrec+=1;
   _put:=0;
   {? USERSF.PORTAL2E='' || USERSF.PORTAL2E:='N'; _put:=1 ?};
   {? USERSF.KSEF_DL='' || USERSF.KSEF_DL:='N'; _put:=1 ?};
   {? USERSF.KSEF_UL='' || USERSF.KSEF_UL:='N'; _put:=1 ?};
   {? _put
   || {? USERSF.put(1) || __lmod+=1 ?}
   ?}",
   1
);
USERSF.cntx_pop();
USERSF.trig_on('*','*');
__UPG.msg('Zaktualizowano użytkowników w firmach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\USERSF_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja użytkowników w firmach - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja użytkowników w firmach'


\par_skid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Aktualizacja listy parametrów
::----------------------------------------------------------------------------------------------------------------------
exec('PAR_SKID','object');
PAR_SKID.set(365,1,1,'','N','Personel: ograniczenie listy współpracowników z uwzględnieniem grup uprawnień','''T'',''N''');
__UPG.msg('Zaktualizowano listę parametrów');
1


\par_skid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Aktualizacja listy parametrów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja listy parametrów.'


\role_portal
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Dodanie/aktualizacja ról/czynności związanych z POR
::----------------------------------------------------------------------------------------------------------------------
{? exec('roleActions','upgrade',
      'Portal HR - Akceptujący wnioski o dofinansowanie',
      'POR_KIE_CAKC,POR_KIE_EAKP,POR_KIE_PSWN,POR_KIE_WYCO'
   ) &
   exec('roleActions','upgrade',
      'Portal HR - Pracownik',
      'POR_PRA_POSW'
   ) &
   exec('roleActions','upgrade',
      'Specjalista HR,Specjalista ds. kadr,Administrator',
      'ZWS_PAR_PPWU'
   )
|| 1
|| -1
?}


\role_portal_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [22.26]
:: OPIS: Dodanie/aktualizacja ról/czynności związanych z POR - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie/aktualizacja ról/czynności związanych z POR'


\szablony
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import zestawów danych i definicji szablonów.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_ASDZ,ZWS_PAR_ASDD',','));
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['szablony'])
|| __UPG.msg('Zaktualizowano zestawy danych i definicje szablonów.');
   _result:=1
|| _result:=-1
?};
_result


\szablony_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: achol [20.42]
:: OPIS: Import zestawów danych i definicji szablonów - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import zestawów danych i definicji szablonów.'


\ZPARN
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli ZPARN - partie produktów
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
ZPARN.cntx_psh();
ZPARN.prefix();
ZPARN.for_each("
   _put:=0;
   __lrec+=1;
   {? ZPARN.KTM=null()
   || _ktm:=exec('FindAndGet','#table',ZL,ZPARN.ZL,,\"ZL.KTM\",null());
      {? _ktm<>null()
      || ZPARN.KTM:=_ktm;
         _put:=1
      ?}
   ?};
   {? _put>0 || {? ZPARN.put(1) || __lmod+=1 ?} ?}
",0);
ZPARN.cntx_pop();
__UPG.msg('Zaktualizowano partie produktów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\ZPARN_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli ZGH - partie produktów - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla partii produktów'


\role_personel
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi.
::----------------------------------------------------------------------------------------------------------------------
{? exec('roleActions','upgrade',
      'Administrator',
      'ZWS_PAR_PZPT,ZWS_PAR_AGPD,ZWS_PAR_AGPU'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. kadr,'
      'Specjallista ds. zleceniobiorców',
      'PKD_EZK_OPHG,PKD_EZK_ORHG'
   ) &
   exec('roleActions','upgrade',
      'Dyrektor HR,'
      'Przeglądający Kadry,'
      'Specjalista ds. kadr',
      'PKD_EZK_OPOS,PKD_EZK_OPWP'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. kadr,'
      'Specjallista ds. zleceniobiorców',
      'PKD_EZK_OROS,PKD_EZK_ORWP'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. kadr',
      'PKD_EZK_AWDA,PKD_EZK_AODD,PKD_EZK_ATRZ,PKD_EZK_AZKN,PKD_EZK_EZLA'
   ) &
   exec('roleActions','upgrade',
      'Specjalista ds. płac',
      'PKD_EZK_APRP'
   ) &
   exec('roleActions','upgrade',
      'Administrator',
      'ZWS_KAL_AKTD'
   )
|| 1
|| -1
?}


\role_personel_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Aktualizacja ról/uprawnień związana z czynnościami personelowymi
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja ról/uprawnień związana z czynnościami personelowymi.'


\actKoszt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Aktualizacja kosztów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_msk:=FAKS_K.names();
_msk.clear();
{? _msk.first()
|| exec('fak_psh','open_tab');
   {!
   |? _oddz:=1+(_msk.NAME+3);
      _rok:=(_msk.NAME+3)+2;
      exec('fak_open','open_tab',_oddz,_rok);
      FAKS_K.prefix();
      FAKS_K.for_each("_put:=0;
                       __lrec+=1;
                       {? FAKS_K.OPLD=''
                       || FAKS_K.OPLD:='N';
                          _put:=1
                       ?};
                       {? _put || {? FAKS_K.put(1) || __lmod+=1 ?} ?}",1);
      _msk.next()
   !};
   exec('fak_pop','open_tab')
?};
obj_del(_msk);
__UPG.msg('Zaktualizowano koszty dla zakupów i magazynów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\actKoszt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: rr [20.42]
:: OPIS: Aktualizacja kosztów - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktulizacja kosztów dla zakupów i magazynu'


\cenniki_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Aktualizacja aktywności nagłówków cenników.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

TAR.cntx_psh();
TAR.prefix();
TAR.for_each("
__lrec+=1;
_put:=0;
{? TAR.AKT=''
||
   TAR.AKT:='T';
   _put:=1
?};
{? _put & TAR.put(1) || __lmod+=1 ?}
",1);
TAR.cntx_pop();

__UPG.msg('Zaktualizowano aktywności nagłówków cenników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\cenniki_akt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Aktualizacja aktywności nagłówków cenników.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja aktywności nagłówków cenników.'


\potwZAM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Uzupełnia zapisy dla ZDP_POZ
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod','__lstn');

__lrec:=__lmod:=__lrecZ:=__lmodZ:=__lstn:=0;
_oddz:=ST.ODDZ;
_msk:=ZDP_POZ.names();
exec('openzd_psh','open_tab');
_msk.clear();
{? _msk.first()
|| {!
   |? exec('openzd','open_tab',_msk.NAME+3);
      ST.ODDZ:=1+(_msk.NAME+3);
      ZDP_NAG.prefix();
      ZDP_NAG.for_each("
         _stan:='';
         _stan:={? ZDP_NAG.STAT_REJ='N' || 'N'
                |? ZDP_NAG.STAT_REJ='Z' || 'A'
                || _ctrl:=exec('ctrlREA','zamdst_ptw',1);
                   {? _ctrl=0           || 'Z'
                   |? _ctrl=1           || 'R'
                   |? _ctrl=2           || 'C'
                   || 'T'
                   ?}
                ?};
         {? _stan<>'' & ZDP_NAG.STAN<>_stan
         || ZDP_NAG.STAN:=_stan;
            {? ZDP_NAG.put(1) || __lstn+=1 ?}
         ?}
      ");
      ZDP_POZ.prefix();
      ZDP_POZ.for_each("
         _put:=0;
         __lrec+=1;
         _nag:=exec('FindAndGet','#table',ZD_POZ,ZDP_POZ.ZD_POZ,,\"$ZD_NAG\",'');
         _wal:={? _nag<>'' || exec('FindAndGet','#table',ZD_NAG,_nag,,\"WAL\",null()) || INFO.NAROD ?};
         _krs:={? _nag<>'' || exec('FindAndGet','#table',ZD_NAG,_nag,,\"KRS\",null()) || INFO.NAROD ?};
         {? ZDP_POZ.WAL=null() || _put:=1; ZDP_POZ.WAL:=_wal ?};
         {? ZDP_POZ.WAL<>INFO.NAROD & ZDP_POZ.KRS=0 || _put:=1; ZDP_POZ.KRS:=_krs ?};
         {? _put || {? ZDP_POZ.put() || __lmod+=1 ?} ?}
      ");
      _msk.next()
   !}
?};
exec('openzd_pop','open_tab');
{? __lstn>0 || __UPG.msg('Zaktualizowano stan potwierdzeń zamówień dostaw (liczba modyfikacji: %1).'[$__lstn]) ?};
__UPG.msg('Zaktualizowano zapisy dla pozycji potwierdzeń zamówień dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);
ST.ODDZ:=_oddz;

VAR_DEL.delete('__lrec','__lmod','__lstn');
_res


\potwZAM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Uzupełnia zapisy dla ZDP_POZ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zapisów dla pozycji potwierdzeń zamówień dostaw.'


\h_um_pola
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie nowych pól H_UM.P_OD, H_UM.P_DO
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err:=_suc:=_goo:=0;
_ret:=1;
_d0:=date(0,0,0);
{? var_pres('P_OD',H_UM)>0
|| _MSK:=H_UM.names();
   _MSK.clear();
   {? _MSK.first()
   || H_UM.cntx_psh();
      H_UM.trig_off('*','*');
      {!
      |? H_UM.use(_MSK.NAME);
         H_UM.prefix();
         {? H_UM.first()
         || {!
            |? {? H_UM.P_OD<>_d0
               || _goo+=1
               || H_UM.P_OD:=H_UM.OD;
                  H_UM.P_DO:=H_UM.DO;
                  {? H_UM.put()
                  || _suc+=1
                  || _err+=1
                  ?}
               ?};

               H_UM.next()
            !}
         ?};

         _MSK.next()
      !};
      H_UM.trig_on('*','*');
      H_UM.cntx_pop()
   ?};
   obj_del(_MSK)
|| __UPG.msg('Brak pola H_UM.P_OD w definicji.');
   _ret:=-1
?};

__UPG.msg('Liczba poprawionych rekordów: %1' [$_suc]);
{? _goo
|| __UPG.msg('Liczba rekordów niewymagających poprawienia: %1' [$_goo])
?};
{? _err
|| __UPG.msg('Liczba niepoprawionych rekordów: %1' [$_err]);
   _ret:=-1
?};

_ret


\h_um_pola_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie nowych pól H_UM.P_OD, H_UM.P_DO - opis zadania
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pola H_UM.P_OD, H_UM.P_DO wartościami początkowymi pochodzącymi z H_UM.OD i H_UM.DO.'


\zpr_ins_zsql
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Formuła dodaje czynność ZPR_INS_ZSQL do odpowiednich ról
::----------------------------------------------------------------------------------------------------------------------
_cr:=''+"'ZPR_INS_ARCH','ZPR_INS_DELE','ZPR_INS_PRCH','ZPR_INS_STAT','ZPR_PRO_MODE','ZPR_PRO_MODW'";
_uid:='ZPR_INS_ZSQL';
exec('actionRoleAction','upgrade',_uid,_cr)


\zpr_ins_zsql_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Formuła dodaje czynność ZPR_INS_ZSQL do odpowiednich ról - opis.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie czynności "ZPR_INS_ZSQL - Zapytania SQL procesów, czynn." do odpowiednich ról'


\POCZTAM
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja definicji serwerów SMTP
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
POCZTAM.trig_off('*','*');
POCZTAM.cntx_psh();
POCZTAM.prefix();
POCZTAM.for_each("
   __lrec+=1;
   _put:=0;
   {? POCZTAM.AUTH_KND='' || POCZTAM.AUTH_KND:=exec('auth_basic','#mailbox'); _put:=1 ?};
   {? _put
   || {? POCZTAM.put(1) || __lmod+=1 ?}
   ?}",
   1
);
POCZTAM.cntx_pop();
POCZTAM.trig_on('*','*');
__UPG.msg('Zaktualizowano definicje serwerów SMTP.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\POCZTAM_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja definicji serwerów SMTP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji serwerów SMTP'


\TR_NZL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [22.26]
:: OPIS: Wypełnia pola w tabeli TR_NZL
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=TR_NZL.names();
TR_NZL.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? TR_NZL.use(_msk.NAME);
      TR_NZL.prefix();
      TR_NZL.for_each("
        __lrec+=1;
        _put:=0;
        {? TR_NZL.STATUS='Anulowane'
        || TR_NZL.STATUS:='anulowane';
           _put:=1
        |? TR_NZL.STATUS='brak1'
        || TR_NZL.STATUS:='nieokreślone';
           _put:=1
        |? TR_NZL.STATUS='brak2'
        || {? TR_NZL.STAT_REJ='A'
           || TR_NZL.STATUS:='anulowane'
           || TR_NZL.STATUS:='nieokreślone'
           ?};
           _put:=1
        ?};
        {? _put & TR_NZL.put(1)
        || __lmod+=1
        ?}
        ");
      _msk.next()
   !}
?};
TR_NZL.cntx_pop();
__UPG.msg('Zaktualizowano dyspozycje transportu.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\TR_NZL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [MW] [22.26]
:: OPIS: Wypełnia pola w tabeli TR_NZL - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja dyspozycji transportu'


\PORTALO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja definicji komunikatów dla licencji Portal HR
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
PORTALO.trig_off('*','*');
PORTALO.cntx_psh();
PORTALO.prefix();
PORTALO.for_each("
   __lrec+=1;
   _put:=0;
   {? PORTALO.COMM<>~-PORTALO.COMM || PORTALO.COMM:=~-PORTALO.COMM; _put:=1 ?};
   {? _put
   || {? PORTALO.put(1) || __lmod+=1 ?}
   ?}",
   1
);
PORTALO.cntx_pop();
PORTALO.trig_on('*','*');
__UPG.msg('Zaktualizowano definicje komunikatów dla licencji Portal HR.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\PORTALO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja definicji komunikatów dla licencji Portal HR
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja definicji komunikatów dla licencji Portal HR'


\B_PREL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Nawija nowe pola w tabeli B_PREL
::   WY: -1/0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_can_continue:=1;
B_PREL.cntx_psh();
B_PREL.index('CODE');
B_PREL.prefix();
{? B_PREL.first()
|| {!
   |? _put:=0;
      {? B_PREL.COND_CHK=''
      || B_PREL.COND_CHK:='T';
         _put:=1
      ?};
      {? B_PREL.VISIBLE=''
      || B_PREL.VISIBLE:={? B_PREL.CLASS='B_ACTION'  || 'T' || 'N' ?};
         B_PREL.NOTIFY_1:='N';
         B_PREL.NOTIFY_2:='N';
         B_PREL.PERSONAL:={? B_PREL.CLASS='B_ACTION' || 'T' || 'N' ?};
         _put:=1
      ?};
      {? B_PREL.RELEASE=''
      ||
         B_PREL.RELEASE:='T';
         _put:=1
      ?};
      {? _put>0
      || _can_continue:=B_PREL.put();
         {? _can_continue=0
         || _result:=0;
            __UPG.msg('Nie udany zapis elementu procesu: %1. Proces: %2'@[exec('B_PREL','#to_string'),exec('record','#to_string',B_PREL.B_PROC)])
         ?}
      ?};
      B_PREL.next()
   !}
?};
B_PREL.cntx_pop();
{? _result>0
|| __UPG.msg('Zaktualizowano zapisy tabeli B_PREL.')
|| __UPG.msg('Nie udało się zaktualizować wszystkich zapisów tabeli B_PREL.')
?};
_result


\B_PREL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Nawija nowe pola w tabeli B_PREL - opis
::   WY: -1/0/1
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie pól w tabeli B_PREL (elementy procesów)'


\BI_TODO
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wypełnia pola w tabeli BI_TODO
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
BI_TODO.cntx_psh();
BI_TODO.index('BI_PROC');
BI_TODO.prefix();
_res:=BI_TODO.for_each("
   __lrec+=1;
   _put:=0;
   {? BI_TODO.VISIBLE=''
   || BI_TODO.VISIBLE:='T';
      _put:=1
   ?};
   {? _put>0
   || {? BI_TODO.put()
      || __lmod+=1
      ?}
   ?}
");
BI_TODO.cntx_pop();
__UPG.msg('Zaktualizowano zadania na listach TODO.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
_res


\BI_TODO_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wypełnia pola w tabeli BI_TODO - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zadań na listach TODO'


\KH_POW
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Aktualizacja danych dotyczących powiązań kontrahentów
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
__lrec:=0;
__lmod:=0;
KH_POW.trig_off('*','*');
KH_POW.cntx_psh();
KH_POW.prefix();
KH_POWR.cntx_psh();
KH_POWR.prefix();
KH_POWR.find_key('NOK');
KH_POW.for_each("
   __lrec+=1;
   _put:=0;
   {? KH_POW.RODZAJ=null || KH_POW.RODZAJ:=KH_POWR.ref(); _put:=1 ?};
   {? _put
   || {? KH_POW.put() || __lmod+=1 ?}
   ?}",
   1
);
KH_POW.cntx_pop();
KH_POWR.cntx_pop();
KH_POW.trig_on('*','*');
__UPG.msg('Zaktualizowano rekordy powiązania kontrahentów bez określonego rodzaju powiązania');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\KH_POW_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Aktualizacja danych dotyczących powiązań kontrahentów
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja danych dotyczących powiązań kontrahentów'


\KH_POWR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Formuła dodaje systemowy rekord do tabeli KH_POWR
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
KH_POWR.trig_off('*','*');
KH_POWR.cntx_psh();
KH_POWR.index('KOD');
KH_POWR.prefix('NOK');
{? ~KH_POWR.first()
|| KH_POWR.KOD:='NOK';
   KH_POWR.OPIS:='Nieokreślone powiązanie';
   KH_POWR.SYSTEM:='T';
   KH_POWR.add()
?};
KH_POWR.cntx_pop();
KH_POWR.trig_on('*','*');
__UPG.msg('Zaktualizowano typy powiązania kontrahentów');
_res


\KH_POWR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Formuła dodaje systemowy rekord do tabeli KH_POWR
::----------------------------------------------------------------------------------------------------------------------
'Formuła dodaje systemowy rekord do tabeli KH_POWR'


\f_zatra_add
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Lista czynności, dla których przypisane zostaną formy współpracy. Założenia:
:: 1. Czynności są oddzielone przecinkami.
:: 2. W ramach czynności przekazywana jest również informacja o kodach form współpracy (oddzielonych znakiem dwukropka).
::    Zakładamy, że duże litery dotyczą form docelowych.
_lista:=spli_str(
   'PKD_EZK_OPHG:P:Z:K:R:T:p:z:k:r:t,'
   'PKD_EZK_ORHG:P:Z:K:R:T:p:z:k:r:t,'
   'PKD_EZK_OPOS:P:Z:K:T,'
   'PKD_EZK_OPWP:P:Z:K:T,'
   'PKD_EZK_OROS:P:Z:K:T,'
   'PKD_EZK_ORWP:P:Z:K:T,'
   'BIQ_EZK_DISP:P',
   ','
);

_ret:=1;
B_ACTION.cntx_psh();
B_ACTION.f_clear();
B_ACTION.index('UNIK');
B_ACTION.prefix();
_lenl:=obj_len(_lista);
{! _lp:=1 .. _lenl
|! _acz:=spli_str(_lista[_lp],':');
   _uid:=_acz[1];
   {? B_ACTION.find_key(_uid,)
   || _lena:=obj_len(_acz);
      {! _lf:=2 .. _lena
      |! _fz:=_acz[_lf];
         __UPG.msg('Forma współpracy: %1 | Czynność: %2 - %3' [_fz,_uid,B_ACTION.NAME]);
         _add:=exec('f_zatra_add','f_zatr',{? -_fz=_fz || 'N' || 'T' ?},_fz,_uid);
         {? _add=''
         || __UPG.msg(' - uprawnienia nadane')
         || __UPG.msg(_add);
            _ret:=-1
         ?}
      !}
   || __UPG.msg('"%1" - nieprawidłowy kod czynności.' [_uid]);
      _ret:=-1
   ?};
   obj_del(_acz)
!};
B_ACTION.cntx_pop();
_ret


\f_zatra_add_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: Nadanie czynnościom uprawnień do form współpracy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Nadanie czynnościom uprawnień do form współpracy'


\FPACZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wypełnia pola tabeli FPACZ
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
FPACZ.cntx_psh();
FPACZ.index('ROK');
FPACZ.prefix();
_res:=FPACZ.for_each("
   __lrec+=1;
   _put:=0;
   {? FPACZ.RAB_NAL=''
   ||
      FPACZ.RAB_NAL:='P';
      _put:=1
   ?};
   {? _put
   ||
      {? FPACZ.put() || __lmod+=1 ?}
   ?}
");
FPACZ.cntx_pop();
__UPG.msg('Zaktualizowano paczki faktur.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\FPACZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Wypełnia pola tabeli FPACZ - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja paczek faktur'


\portal_seod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie danych na potrzeby portalu SEOD
::----------------------------------------------------------------------------------------------------------------------
exec('PAR_SKID','object');
PAR_SKID.set(465,8,0,'','TWN','SEOD: Komentarz w akceptacji, odrzuceniu i zamknięciu');
__UPG.msg('Dodano parametr 465 - SEOD: Komentarz w akceptacji, odrzuceniu i zamknięciu');

exec('init','sync_id');
__UPG.msg('Zaktualizowano identyfikatory rekordów dla portalu.');

exec('portalu_update','#portal',0);
__UPG.msg('Zaktualizowano uprawnienia użytkowników na portalu.');

exec('UD_SEOD_init','portal_seod',2);
exec('UD_SEOD_init','portal_seod',1);
__UPG.msg('Uzupełniono tabelę UD_SEOD.');

_obj:=exec('portal_hr_obj','upgrade_2226');
_obj.add('PORTAL_SEOD','Usuwanie','EDOKUM','cseod_AcceptanceHeaderDelete','Scieżka akceptacji faktur w obiegu',
   $"exec('EDOKUM_typ','portal_seod','F','S')"
);
_obj.add('PORTAL_SEOD','Usuwanie','EDOKUM','cseod_PurchaseDocumentDelete','Faktury w obiegu');
_obj.add('PORTAL_SEOD','Wysyłanie','EDOKUM','cseod_PurchaseDocumentModify','Faktury w obiegu');
_obj.add('PORTAL_SEOD','Odbieranie','EDOKUM','cseod_PurchaseDocumentGet','Faktury w obiegu');
_obj.add('PORTAL_SEOD','Wysyłanie','EDOKUM','cseod_AcceptanceHeaderModify','Ścieżka akceptacji faktury w obiegu');
_obj.add('PORTAL_SEOD','Odbieranie','EDOKUM','cseod_AcceptOperatorGet','Odbieranie informacji o akceptacji faktury');

_obj.add('PORTAL_SEOD_CFG','Usuwanie','UD_SEOD','cseod_CostCenterTypeTenantDelete',
   'Wymiary controllingowe typów faktur i wniosków',
   $"exec('UD_SEOD_ok','portal_seod')",'cseod_CostCenterTypeTenantModify'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','UD_SEOD','cseod_CostCenterTypeTenantModify',
   'Wymiary controllingowe typów faktur i wniosków',
   $"exec('UD_SEOD_ok','portal_seod')"
);
_obj.add('PORTAL_SEOD_CFG','Usuwanie','UD_DEF','cseod_CostCenterDelete',
   'Elementy wymiaru controllingowego',
   $"exec('UD_DEF_ok','portal_seod')",'cseod_CostCenterModify'
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','UD_DEF','cseod_CostCenterModify',
   'Elementy wymiaru controllingowego',
   $"exec('UD_DEF_ok','portal_seod')"
);
_obj.add('PORTAL_SEOD_CFG','Wysyłanie','KH','cseod_BCAddresHistoryModify','Kontrahenci');
_obj.clear();
1


\portal_seod_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie danych na potrzeby portalu SEOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie danych na potrzeby portalu SEOD:\n'
'- dodanie parametru 465 - SEOD: Komentarz w akceptacji, odrzuceniu i zamknięciu\n'
'- aktualizacja identyfikatorów rekordów dla portalu\n'
'- aktualizacja uprawnień użytkowników na portalu\n'
'- uzupełnienie tabeli UD_SEOD\n'
'- dodanie przestrzenie wymiany danych PORTAL_SEOD_CFG i PORTAL_SEOD'


\portal_seod2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie danych na potrzeby portalu SEOD - cz. 2
::----------------------------------------------------------------------------------------------------------------------
exec('TAT_upd','portal_seod');
__UPG.msg('Dodano predefiniowane atrybuty dla schematów informacji dodatkowych dla faktur SEOD.');
1


\portal_seod2_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie danych na potrzeby portalu SEOD - cz. 2 - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie predefiniowanych atrybutów dla schematów informacji dodatkowych dla faktur SEOD.'


\portal_hr_obj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zwraca obiekt do obsługi definicji wymiany danych
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'STR_FROM','STR_TO',
   'FROM','TO',
   'TREE_FROM','TREE_TO',
   'TREE_FROM_M','TREE_TO_M',
   'NDX',
   'move','sort','add',
   'clear'
);
_obj.move:="
   _pd_from:=.FROM;
   _pd_to:=.TO;
   _tab:={? var_pres('_a')=type_of('') || _a || '' ?};
   _method:={? var_pres('_b')=type_of('') || _b || '' ?};
   _copy:={? var_pres('_c')=type_of(1) || _c ?};
   {? _tab<>''
   || SYNC_DEF.cntx_psh();
      SYNC_DEF.index('TREE');
      SYNC_DEF.prefix(.TREE_FROM,.STR_FROM,_tab,);
      {? SYNC_DEF.first()
      || SYNC_DEF.cntx_psh();
         SYNC_DEF.prefix();
         SYNC_DEF.REKORD:=.TREE_TO;
         SYNC_DEF.SYNC_PD:=.TO;
         {? _copy
         || SYNC_DEF.add();
            __UPG.msg('Dodano obsługę tabeli %1 dla %2.'[_tab,.STR_TO])
         || SYNC_DEF.put();
            __UPG.msg('Przeniesiono obsługę tabeli %1 z %2 do %3.'[_tab,.STR_FROM,.STR_TO])
         ?};
         SYNC_DEF.cntx_pop()
      ?};
      SYNC_DEF.cntx_pop()
   ?};
   SYNC_MWA.cntx_psh();
   {? _tab<>''
   || SYNC_MWA.index(.NDX);
      SYNC_MWA.prefix(.STR_FROM,_tab,_method)
   || SYNC_MWA.index('PD_METH');
      SYNC_MWA.prefix(.STR_FROM,_method,)
   ?};
   {? SYNC_MWA.first()
   || {!
      |? _rep:=null;
         {? SYNC_MWA.SYNC_REP
         || SYNC_REP.cntx_psh();
            SYNC_REP.index('PDNR');
            SYNC_REP.prefix();
            {? ~SYNC_REP.find_key(.TO,SYNC_MWA.SYNC_REP().NR)
            || {? SYNC_REP.seek(SYNC_MWA.SYNC_REP)
               || SYNC_REP.SYNC_PD:=.TO;
                  SYNC_REP.add()
               ?}
            ?};
            _rep:=SYNC_REP.ref();
            SYNC_REP.cntx_pop()
         ?};
         SYNC_MWA.cntx_psh();
         SYNC_MWA.prefix();
         SYNC_MWA.PARENT:=.TREE_TO_M;
         SYNC_MWA.SYNC_PD:=.TO;
         SYNC_MWA.SYNC_REP:=_rep;
         SYNC_MWA.put();
         __UPG.msg('Przeniesiono obsługę komunikatu %1 z %2 do %3.'[_method,.STR_FROM,.STR_TO]);
         SYNC_MWA.cntx_pop();
         SYNC_MWA.first()
      !}
   ?};
   SYNC_MWA.cntx_pop()
";
_obj.sort:="
   _pd:=_a;
   SYNC_MWA.cntx_psh();
   SYNC_MWA.index('PD_LP');
   SYNC_MWA.prefix(_pd);
   {? SYNC_MWA.first()
   || _lp:=-1;
      {!
      |? SYNC_MWA.LP:=(_lp+=1);
         SYNC_MWA.put();
         SYNC_MWA.next()
      !}
   ?};
   SYNC_MWA.cntx_pop();
   __UPG.msg('Przenumerowano komunikaty dla %1.'[_pd])
";
_obj.add:="
   _pd_str:=_a;
   _typ:=_b;
   _tab:=_c;
   _api:=_d;
   _desc:=_e;
   _fml:={? var_pres('_f')=type_of('') || _f || ~~ ?};
   _before:={? var_pres('_g')=type_of('') || _g || '' ?};
   _firma:={? var_pres('_h')=type_of('') || _h || ~~ ?};
   _replika:={? var_pres('_i')=type_of(1) || _i || ~~ ?};
   SYNC_PD.cntx_psh();
   SYNC_PD.index('RSYM');
   SYNC_PD.prefix();
   {? ~SYNC_PD.find_key('I',_pd_str,)
   || SYNC_PD.blank(1);
      SYNC_PD.RODZAJ:='I';
      SYNC_PD.SYM:=_pd_str;
      SYNC_PD.add();
      __UPG.msg('Dodano przeznaczenie danych %1.'[_pd_str])
   ?};
   _pd:=SYNC_PD.ref();
   SYNC_PD.cntx_pop();
   SYNC_DEF.cntx_psh();
   SYNC_DEF.index('TREE');
   SYNC_DEF.prefix();
   {? ~SYNC_DEF.find_key(0,_pd_str,)
   || SYNC_DEF.blank();
      SYNC_DEF.SYNC_PD:=_pd;
      SYNC_DEF.add()
   ?};
   _sync_def:=SYNC_DEF.ref();
   {? ~SYNC_DEF.find_key(#_sync_def,_pd_str,_tab,)
   || SYNC_DEF.blank();
      SYNC_DEF.REKORD:=_sync_def;
      SYNC_DEF.SYNC_PD:=_pd;
      SYNC_DEF.ACR_TAB:=_tab;
      SYNC_DEF.ACR_FLD:='uidref';
      SYNC_DEF.LP:=1;
      SYNC_DEF.FORM:=_tab+'.uidref()';
      SYNC_DEF.WGIDPUT:='T';
      SYNC_DEF.TYP:='S';
      SYNC_DEF.FIR:={? var_pres('_firma')>0 || _firma || 'N' ?};
      SYNC_DEF.AKT:='T';
      {? var_pres('_fml')>0
      || SYNC_DEF.WAR_FORM:=_fml
      ?};
      SYNC_DEF.add();
      __UPG.msg('Dodano obsługę tabeli %1 dla %2.'[_tab,_pd_str])
   || _put:=0;
      {? var_pres('_firma')>0
      || SYNC_DEF.FIR:=_firma;
         _put:=1
      ?};
      {? var_pres('_fml')>0
      || SYNC_DEF.WAR_FORM:=_fml;
         _put:=1
      ?};
      {? _put || SYNC_DEF.put() ?}
   ?};
   SYNC_DEF.cntx_pop();
   SYNC_MWA.cntx_psh();
   SYNC_MWA.index('TREE');
   SYNC_MWA.prefix();
   {? ~SYNC_MWA.find_key(0,_pd_str,)
   || {? SYNC_MWA.find_key(0,'PORTAL_CFG',)
      || SYNC_MWA.SYNC_PD:=_pd;
         SYNC_MWA.add()
      ?}
   ?};
   _sync_mwa:=SYNC_MWA.ref();
   _lp:=-1;
   {? _before<>''
   || SYNC_MWA.index('PD_METH'); SYNC_MWA.prefix(_pd_str,_before,);
      _lp:={? SYNC_MWA.first() || SYNC_MWA.LP || -1 ?};
      {? _lp<>-1
      || SYNC_MWA.index('TREE1'); SYNC_MWA.prefix(_sync_mwa);
         {? SYNC_MWA.last()
         || {!
            |? {? SYNC_MWA.LP>=_lp
               || SYNC_MWA.LP+=1;
                  SYNC_MWA.put();
                  SYNC_MWA.prev()
               ?}
            !}
         ?}
      ?}
   ?};
   {? _lp=-1
   || SYNC_MWA.index('TREE1');
      SYNC_MWA.prefix(_sync_mwa);
      _lp:={? SYNC_MWA.last() || SYNC_MWA.LP+1 || 1 ?}
   ?};
   SYNC_MWA.index('PD_API');
   SYNC_MWA.prefix();
   {? ~SYNC_MWA.find_key(_pd,'portal.mwac',_api,_tab,_typ,)
   || SYNC_MWA.blank();
      SYNC_MWA.PARENT:=_sync_mwa;
      SYNC_MWA.SYNC_PD:=_pd;
      SYNC_MWA.AKT:='T';
      SYNC_MWA.TAB_ACR:=_tab;
      SYNC_MWA.METHOD:=_api;
      SYNC_MWA.TYPE:=_typ;
      SYNC_MWA.LP:=_lp;
      SYNC_MWA.MWAC:='portal.mwac';
      {? var_pres('_replika')>0
      || SYNC_MWA.SYNC_REP:=exec('FindInSet','#table','SYNC_REP','PDNR',_replika,_pd,,,,null)
      ?};
      {? SYNC_MWA.add()
      || SYNC_MWA.memo_set(_desc,'DESC');
         SYNC_MWA.memo_put(,'DESC');
         __UPG.msg('Dodano obsługę komunikatu %1 dla tabeli %2 w %3.'[_api,_tab,_pd_str])
      ?}
   || __UPG.msg('Istnieje już komunikatu %1 dla tabeli %2 w %3.'[_api,_tab,_pd_str])
   ?};
   SYNC_MWA.cntx_pop()
";
_obj.clear:="
   SYNC_MWA.ndx_drop(.NDX)
";
_obj.NDX:=SYNC_MWA.ndx_tmp('',1,
   'SYNC_PD','SYM',0,
   'TAB_ACR',,0,
   'METHOD',,0
);
_obj


\portal_hr_cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zmiana przeznaczenia danych PORTAL_CFG na PORTAL_HR_CFG
::----------------------------------------------------------------------------------------------------------------------
_obj:=exec('portal_hr_obj','upgrade_2226');
_obj.STR_FROM:='PORTAL_CFG';
_obj.STR_TO:='PORTAL_HR_CFG';

SYNC_PD.cntx_psh();
SYNC_PD.index('RSYM');
SYNC_PD.prefix();
{? ~SYNC_PD.find_key('I',_obj.STR_TO,)
|| _obj.FROM:=null;
   {? SYNC_PD.find_key('I',_obj.STR_FROM)
   || SYNC_PD.SYM:=_obj.STR_TO;
      SYNC_PD.put();
      _obj.FROM:=SYNC_PD.ref();
      __UPG.msg('Zmieniono definicję wymiany danych z %1 na %2.'[_obj.STR_FROM,_obj.STR_TO])
   ?};
   {? ~SYNC_PD.find_key('I',_obj.STR_FROM)
   || SYNC_PD.blank(1);
      SYNC_PD.SYM:=_obj.STR_FROM;
      SYNC_PD.RODZAJ:='I';
      SYNC_PD.add();
      __UPG.msg('Dodano definicję wymiany danych z %1.'[_obj.STR_FROM])
   ?};
   _obj.TO:=SYNC_PD.ref();
   _hlp:=_obj.STR_FROM;
   _obj.STR_FROM:=_obj.STR_TO;
   _obj.STR_TO:=_hlp;
   {? _obj.FROM
   || SYNC_DEF.cntx_psh();
      SYNC_DEF.index('TREE');
      SYNC_DEF.prefix();
      {? ~SYNC_DEF.find_key(0,_obj.STR_TO,)
      || {? SYNC_DEF.find_key(0,_obj.STR_FROM,)
         || _obj.TREE_FROM:=SYNC_DEF.ref();
            SYNC_DEF.SYNC_PD:=_obj.TO;
            SYNC_DEF.add()
         ?}
      ?};
      _obj.TREE_TO:=SYNC_DEF.ref();
      SYNC_DEF.cntx_pop();
      SYNC_MWA.cntx_psh();
      SYNC_MWA.index('TREE');
      SYNC_MWA.prefix();
      {? ~SYNC_MWA.find_key(0,_obj.STR_TO,)
      || {? SYNC_MWA.find_key(0,_obj.STR_FROM,)
         || _obj.TREE_FROM_M:=SYNC_MWA.ref();
            SYNC_MWA.SYNC_PD:=_obj.TO;
            SYNC_MWA.add()
         ?}
      ?};
      _obj.TREE_TO_M:=SYNC_MWA.ref();
      SYNC_MWA.cntx_pop();

      _obj.move(,'SL_GETBYPOSTDATAABOUTENTITIESTOSYNC');
      _obj.move(,'sl_TenantGenericDashItemsModify');
      _obj.move('PORSLO','sl_TenantLookupCodes',1);
      _obj.move('PORSLOIT','sl_TenantLookupCodeItems',1);
      _obj.move('PORTALK');
      _obj.move('PORTALL');
      _obj.move('PORTALO');
      _obj.move('PORTALU');
      _obj.move('USERS');
      _obj.move('USR_SIGN');

      _obj.sort(_obj.STR_FROM);
      _obj.sort(_obj.STR_TO);

      _obj.add('PORTAL_CFG','Uruchomienie','FIRMA','csl_TenantCompanyModify','Firma',$"exec('FIRMA_ok','portal_seod')");
      _obj.add('PORTAL_CFG','Wysyłanie','FIRMA','csl_TenantCompanyModify','Firma');
      _obj.add('PORTAL_CFG','Uruchomienie','USERSF','csl_TenantUserCompanyModify','Użytkownicy firmy',
         $"exec('USERSF_ok','portal_seod')"
      );
      _obj.add('PORTAL_CFG','Uruchomienie','USERSF','csl_TenantUserCompanyDelete','Użytkownicy firmy');
      _obj.add('PORTAL_CFG','Wysyłanie','USERSF','csl_TenantUserCompanyModify','Użytkownicy firmy');
      ~~
   ?}
|| __UPG.msg('Istnieje już definicja wymiany danych %1.'[_obj.STR_TO])
?};
SYNC_PD.cntx_pop();
1


\portal_hr_cfg_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Zmiana przeznaczenia danych PORTAL_CFG na PORTAL_HR_CFG - opis
::----------------------------------------------------------------------------------------------------------------------
'- Utworzenie przestrzeni wymiany danych PORTAL_HR_CFG\n'
'- Uzupełnienie komunikatów PORTAL_CFG o obsługę tabeli FIRMA i USERSF'


\procesy_portal_HR
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Import procesów portal HR
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','POR_')


\procesy_portal_HR_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Import procesów portal HR - opis
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować zmienione procesy dziedziny POR:\n'
' ● POR_NADP_WWW - Polecenie pracy w godzinach nadliczbowych\n'
' ● POR_PRAK_WWW - Praktyka\n'
' ● POR_SZK_WWW - Wniosek o szkolenie\n'
' ● POR_WNI_FL - Obsługa wniosku admin. - Flota samochodowa\n'
' ● POR_WNI_IT - Obsługa wniosku admin. - Infrastruktura IT\n'
' ● POR_WNI_PB - Obsługa wniosku admin. - Powierzchnie biurowe\n'
' ● POR_WNI_TEL - Obsługa wniosku admin. - Telefonia komórkowa\n'
' ● POR_WNI_WB - Obsługa wniosku admin. - Wyposażenia biur\n'
'\n'
'Pozwala zaimportować nowe procesy dziedziny POR:\n'
' ● POR_CZW_WWW - Wniosek o czas wolny (wyjście prywatne)\n'
' ● POR_DOFS_WWW - Wniosek o dofinansowanie studiów\n'
' ● POR_NJO_WWW - Podjęcie lub kontynuacja nauki języka obcego\n'
' ● POR_ODBP_WWW - Polecenie odbioru nadgodzin\n'
' ● POR_ODBW_WWW - Wniosek o odbiór nadgodzin\n'
' ● POR_OOP_WWW - Oświadcz. dot. opieki nad dzieckiem do lat 14\n'
' ● POR_OZO_WWW - Oświadczenie opiekuna dziecka do lat 4\n'
' ● POR_WPU_OJC - Wniosek o urlop ojcowski\n'
' ● POR_ZOC_WWW - Wniosek o zasiłek opiekuńczy (Z-15B)\n'
' ● POR_ZOD_WWW - Wniosek o zasiłek opiekuńczy (Z-15A)\n'


\procesy_produkcyjne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Import procesów produkcyjnych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','RPO_BRK','RPO_')


\procesy_produkcyjne_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Import procesów produkcyjnych - opis
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować zmienione procesy dziedziny TTE:\n'
'Uporządkowano sygnały sterujące wyzwalaniem kolejnych procesów produkcyjnych.\n'
'Zoptymalizowano działanie zdarzeń warunkowych pośrednich.\n'
'Zrezygnowano z sygnału Zlecenie do realizacji. Wprowadzono sygnały:\n\n'
' ● Zlecenie do rozliczenia limitów\n'
' ● Zlecenie do rejestracji wykonań\n'
' ● Zlecenie do raportowania wyrobu\n'
' ● Zlecenie do kooperacji\n\n'
'Sygnały są emitowane pod warunkiem, że zlecenie spełnia dane warunki np. posiada kooperację, limity itp.\n\n'
'Zrezygnowano z procesu RPO_BRK - Obsługa braków. Jest on teraz częścią procesu RPO_WYK - Wykonanie produkcji.\n\n'
'Zoptymalizowano działanie zdarzeń warunkowych pośrednich tam, gdzie był badany tylko stan przetwarzanego rekordu (dotyczy zleceń i dokumentów magazynowych).\n'
'Lista zmodyfikowanych procesów:\n\n'
' ● RPO_KOO Obsługa kooperacji\n'
' ● RPO_OTW Awaryjne otwarcie zlecenia\n'
' ● RPO_RLI Pobranie surowców limitowanych\n'
' ● RPO_WYK Wykonanie produkcji\n'
' ● RPO_WYR Przyjęcie wyrobu gotowego\n'
' ● RPO_ZAM Zamknięcie zlecenia\n'
' ● RPO_ZLE_AUTO Zlecenie produkcyjne proste generowane autom.\n'
' ● RPO_ZLN Zlecenie produkcyjne niezależne\n'
' ● RPO_ZLW Zlecenie warsztatowe\n'
' ● RPO_ZLZ Zlecenie produkcyjne złożone\n'
' ● RPO_ZME Modyfikacja zlecenia produkcyjnego prostego\n'
' ● RPO_ZMZ Modyfikacja zlecenia produkcyjnego złożonego\n'


\B_PORT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja definicji portów w procesach
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_result:=1;

B_PORT.cntx_psh();
B_PORT.index('UNIK');
{? B_PORT.first()
|| {!
   |? _put:=0;
      __lrec+=1;
      {? B_PORT.C_EVENT=''
      || B_PORT.C_EVENT:='N';
         _put:=1
      ?};
      {? _put>0
      || __lmod+=1;
         {? B_PORT.put()=0
         || _result:=0
         ?}
      ?};
      B_PORT.next()
   !}
?};
B_PORT.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano porty z definicjach procesów.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\B_PORT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja definicji portów w procesach - opis
::   WY: STRING
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja portów w definicjach procesów'


\abs_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Aktualizacja znaczników AbStore
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
:: Słownik materiałów
M.cntx_psh();
M.for_each("
__lrec+=1;
_put:=0;
{? M.ABSTORE<>'' | M.ABSTOREC<>'' || M.ABSTORE:=''; M.ABSTOREC:=''; _put:=1 ?};
{? M.MGAT<>null() & M.GATKOD<>M.MGAT().KOD || M.GATKOD:=M.MGAT().KOD; _put:=1 ?};
{? M.ATEST='' || M.ATEST:='N'; _put:=1 ?};
{? _put || M.prefix(); {? M.put(1) || __lmod+=1 ?} ?}
",1);
M.cntx_pop();
:: Grupy materiałów
MGR.cntx_psh();
MGR.for_each("
__lrec+=1;
{? MGR.ABSTORE<>'' | MGR.ABSTOREC<>''
||
   MGR.ABSTORE:='';
   MGR.ABSTOREC:='';
   MGR.prefix();
   {? MGR.put() || __lmod+=1 ?}
?}
",1);
MGR.cntx_pop();
:: Kontrahenci
KH.cntx_psh();
KH.for_each("
__lrec+=1;
{? KH.ABSTORE<>''
||
   KH.ABSTORE:='';
   KH.prefix();
   {? KH.put() || __lmod+=1 ?}
?}
",1);
KH.cntx_pop();
:: Grupy kontrahentów
GRKH.cntx_psh();
GRKH.for_each("
__lrec+=1;
{? GRKH.ABSTORE<>''
||
   GRKH.ABSTORE:='';
   GRKH.prefix();
   {? GRKH.put() || __lmod+=1 ?}
?}
",1);
GRKH.cntx_pop();
:: Odbiorcy kontrahenta
KH_ODB.cntx_psh();
KH_ODB.for_each("
__lrec+=1;
{? KH_ODB.ABSTORE<>''
||
   KH_ODB.ABSTORE:='';
   KH_ODB.prefix();
   {? KH_ODB.put() || __lmod+=1 ?}
?}
",1);
KH_ODB.cntx_pop();
:: Osoby kontaktowe
KH_OSOB.cntx_psh();
KH_OSOB.for_each("
__lrec+=1;
{? KH_OSOB.ABSTORE<>''
||
   KH_OSOB.ABSTORE:='';
   KH_OSOB.prefix();
   {? KH_OSOB.put() || __lmod+=1 ?}
?}
",1);
KH_OSOB.cntx_pop();

__UPG.msg('Zaktualizowano kartotekę materiałową oraz znaczniki AbStore.');
__UPG.msg('Przetworzono: %1 rekordów, z czego zmodyfikowano: %2.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
1


\abs_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Aktualizacja znaczników AbStore
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja kartoteki materiałowej. Zadanie dla klientów nie korzystających z AbStore,
które wyłącza znaczniki integracji dla tabel:
Słownik materiałów, Grupy materiałów, Kontrahenci, Grupy kontrahentów, Odbiorcy kontrahenta, Osoby kontaktowe.'


\B_PROC
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja definicji procesów
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_result:=1;

B_PROC.cntx_psh();
B_PROC.index('ACCEPTED');
B_PROC.prefix('N');
{? B_PROC.first()
|| {!
   |? _put:=0;
      {? B_PROC.ACCEPTED='T'
      || __lrec+=1;
         {? B_PROC.A_IDTIME=''
         || B_PROC.A_IDTIME:=time_ident(date(),time());
            _put:=1
         ?}
      ?};
      {? _put>0
      || __lmod+=1;
         {? B_PROC.put()=0
         || _result:=0
         ?}
      ?};
      B_PROC.next()
   !}
?};
B_PROC.cntx_pop();

{? _result
|| __UPG.msg('Zaktualizowano definicje procesów.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\B_PROC_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Aktualizacja definicji procesów - opis
::   WY: STRING
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja procesów'


\USERS
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja użytkowników
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
USERS.cntx_psh();
USERS.prefix();
USERS.for_each("
   __lrec+=1;
   _put:=0;
   {? USERS.OF365=''
   || USERS.OF365:='N';
      _put:=1
   ?};
   {? USERS.LOG2=''
   || USERS.LOG2:='N';
      _put:=1
   ?};
   {? _put & USERS.put(1)
   || __lmod+=1
   ?}",
   1
);
USERS.cntx_pop();
__UPG.msg('Zaktualizowano użytkowników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\USERS_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja użytkowników'


\GRUPPRAC
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Aktualizacja danych GRUPPRAC.OSOBA
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
{? var_pres('OSOBA',@.GRUPPRAC)>0
|| GRUPPRAC.clear();
   GRUPPRAC.for_each("GRUPPRAC.put()");
   __UPG.msg('Zaktualizowano grupy pracowników.');
   __UPG.msg('Przetworzono: %1 zapisów.'[$GRUPPRAC.size()])
|| __UPG.msg('Nie zaktualizowano grup pracowników.');
   _res:=0
?};
_res


\GRUPPRAC_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Aktualizacja danych GRUPPRAC.OSOBA - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja grup pracowników'


\fiks_81
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/2203/0051 - e-sprawozdanie jednostkowe inne - łączne ale nie dla spółek - pole P_4
::  OLD: \fiks_81/napraw_f.fml
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('Typ');
_typ:=Typ:=obj_new('test','res');
_typ.test:={? var_pres('_a')>0 || _a=1 ?};
_typ.res:=1;
ISTDEF.cntx_psh();
ISTDEF.index('JPK'); ISTDEF.prefix('J','SF','JednostkaInnaW');
{? ISTDEF.first()
|| _fun:="
      {? ISTDEFS.find_key(_a,)
      || {? Typ.test
         || {? ISTDEFS.REGULY<>_c || Typ.res:=0 ?}
         || {? ISTDEFS.REGULY=_b
            || ISTDEFS.REGULY:=_c;
               ISTDEFS.put();
               __UPG.msg('Zmieniono element %1 schematu %2.'[_a,ISTDEF.VER])
            || __UPG.msg('Zmiana elementu %1 schematu %2 nie była wymagana.'[_a,ISTDEF.VER])
            ?}
         ?}
      || Typ.res:=-1;
         __UPG.msg('Nie znaleziono elementu %1 schematu %2.'[_a,ISTDEF.VER])
      ?}
   ";
   {!
   |? {? ISTDEF.VER+1='2'
      || ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS');
         ISTDEFS.prefix(ISTDEF.ref());
         _fun(
            'tns:P_6',
            $"JPK.E_SPR().L_TN='T'",
            $"JPK.E_SPR().L_PRZED<>'N'"
         );
         _fun(
            'tns:P_6A',
            $"{? JPK.E_SPR().L_PRZED='T' || 'false' || 'true' ?}",
            $"{? JPK.E_SPR().L_PRZED='B' || 'false' || 'true' ?}"
         );
         ISTDEFS.cntx_pop()
      ?};
      ISTDEF.next()
   !}
?};
ISTDEF.cntx_pop();
VAR_DEL.delete('Typ');
_typ.res


\fiks_81_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/2203/0051 - e-sprawozdanie jednostkowe inne - łączne ale nie dla spółek - pole P_4
::----------------------------------------------------------------------------------------------------------------------
'Poprawka błędu w schemacie e-Sprawozdań.\n'
'ER/WRT/XP/12.51/2203/0051'


\usr_upr_pelne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja pełnych uprawnień użytkowników do danych
::----------------------------------------------------------------------------------------------------------------------
exec('usr_upr_uzupełnij_pelne','users_upraw','ZAM');
exec('usr_upr_uzupełnij_pelne','users_upraw','ZAW');
exec('usr_upr_uzupełnij_pelne','users_upraw','TR_RODZ');
_msg:='Zaktualizowano pełne uprawnienia użytkowników do magazynów realizujących zamówienia sprzedaży, '+
      'magazynów realizujących zamówienia wewnętrzne, kategorii transportu.';
__UPG.msg(_msg);
1


\usr_upr_pelne_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Aktualizacja pełnych uprawnień użytkowników do danych
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pełnych uprawnień użytkowników do danych'


\TIMER_2_PREL
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Poprawienie powiązania B_PREL będących zdarzeniami czasowymi startowymi z kolejkami
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_b_ele:=null();
B_EVENT.cntx_psh();
B_EVENT.index('TYP');
B_EVENT.prefix(exec('type_start','#b_event'),exec('kind_timer','#b_event'),);
{? B_EVENT.first() || _b_ele:=B_EVENT.B_ELE ?};
B_EVENT.cntx_pop();

{? _b_ele=null() || return(0) ?};

B_TIMER.cntx_psh();
B_PREL.cntx_psh();
B_PREL.index('ELSTART');
B_PREL.prefix(_b_ele);
{? B_PREL.first()
|| {!
   |? __lrec+=1;
      {? B_PREL.B_WORKER=null()
      || B_TIMER.index('B_PREL');
         B_TIMER.prefix(B_PREL.ref());
         {? B_TIMER.first()
         || B_PREL.B_WORKER:=B_TIMER.B_WORKER;
            {? B_PREL.put() || __lmod+=1 ?}
         ?}
      ?};
      B_PREL.next()
   !}
?};
B_PREL.cntx_pop();
B_TIMER.cntx_pop();

__UPG.msg('Zaktualizowano elementy procesów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\TIMER_2_PREL_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Poprawienie powiązania B_PREL będących zdarzeniami czasowymi startowymi z kolejkami
::----------------------------------------------------------------------------------------------------------------------
'Poprawiono powiązanie elementów procesów z kolejkami.'


\zle_dlugi
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: Realizacja propozycji: Ulga na złe długi VAT tryb 05 FV sprzedaży
::----------------------------------------------------------------------------------------------------------------------
_ret:=-1;
{? RS.find_key('~Rozliczenie VAT')
|| _ret:=exec('new_par','slo_slu',RS.ref(),6,'Uwzględniaj w "złych długach"','TAK_NIE')
?};
{? _ret<0
|| __UPG.msg('Nie powiodło się dodanie nowego parametru (6) do słownika opartego o wzorzec ~Rozliczenie VAT.')
|? _ret>0
|| __UPG.msg('Dodano nowy parametr (6) do słownika opartego o wzorzec ~Rozliczenie VAT.')
|| __UPG.msg('Istnieje parametr (6) dot. złych długów w słowniku opartym o wzorzec ~Rozliczenie VAT.')
?};
_ret:=-1;
{? RS.find_key('~Rozliczenie VAT')
|| _ret:=exec('new_par','slo_slu',RS.ref(),7,'Rozlicz max. w dacie sprzedaży, jeśli nie wystąpiła zapłata.','TAK_NIE')
?};
{? _ret<0
|| __UPG.msg('Nie powiodło się dodanie nowego parametru (7) do słownika opartego o wzorzec ~Rozliczenie VAT.')
|? _ret>0
|| __UPG.msg('Dodano nowy parametr (7) do słownika opartego o wzorzec ~Rozliczenie VAT.')
|| __UPG.msg('Istnieje parametr (7) dot. złych długów w słowniku opartym o wzorzec ~Rozliczenie VAT.')
?};
1


\zle_dlugi_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: Realizacja propozycji: Ulga na złe długi VAT tryb 05 FV sprzedaży
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych parametrów do obsługi słownika opartego o wzorzec ~Rozliczenie VAT.'


\ser_sch_ods
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/21.37/2204/0063
::----------------------------------------------------------------------------------------------------------------------
SER_SCH.cntx_psh();
SER_SCH.index('KOD'); SER_SCH.prefix();
_size:=SER_SCH.size(); ile:=0;
{? _size || SER_SCH.for_each("{? SER_SCH.CZY_ODS<>'T' || SER_SCH.CZY_ODS:='T'; ile+=SER_SCH.put() ?}") ?};
SER_SCH.cntx_pop();
__UPG.msg('Wynik dotyczy parametru, czy naliczać odsetki przy notach odsetkowych dla schematów windykacyjnych.');
{? _size=0
|| __UPG.msg('Brak schematów windykacyjnych.')
|? ile=0
|| __UPG.msg('Nie stwierdzono potrzeby zmiany wartości parametru.')
|| __UPG.msg('Zmieniono wartość parametru dla %1 schematów windykacyjnych'[$ile])
?};
1


\ser_sch_ods_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/21.37/2204/0063
::----------------------------------------------------------------------------------------------------------------------
'Brak możliwości zdefiniowania schematu windykacyjnego dla wezwań bez generowania odsetek'


\OZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Uzupełnia pola tabeli OZ - okresy i parametry dla użytkowników
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
OZ.cntx_psh();
OZ.prefix();
OZ.for_each("
   _put:=0;
   __lrec+=1;
   {? OZ.PD_WIDOK=''
   ||
      OZ.PD_WIDOK:='G';
      _put:=1
   ?};
   {? _put || {? OZ.put(1) || __lmod+=1 ?} ?}
",0);
OZ.cntx_pop();
__UPG.msg('Zaktualizowano okresy i parametry dla użytkowników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\OZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AWI [22.26]
:: OPIS: Uzupełnia pola tabeli OZ - okresy i parametry dla użytkowników - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola w tabeli okresy i parametry dla użytkowników'


\portal_cfg
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie komunikatów dla PORTAL_CFG
::----------------------------------------------------------------------------------------------------------------------
exec('init','sync_id');
__UPG.msg('Zaktualizowano identyfikatory rekordów dla portalu.');

_obj:=exec('portal_hr_obj','upgrade_2226');
_obj.add('PORTAL_CFG','Wysyłanie','PORTALAH','sl_TenantExfAvailCtrlValSetModify','Dostępność pól',,'sl_TenantDataDictionaryDelete');
_obj.add('PORTAL_CFG','Usuwanie','PORTALAH','sl_TenantExfAvailCtrlValSetDelete','Dostępność pól',,'sl_TenantExfAvailCtrlValSetModify');

exec('PORTALAH_init','portal_seod');
1


\portal_cfg_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uzupełnienie komunikatów dla PORTAL_CFG - opis
::----------------------------------------------------------------------------------------------------------------------
'- aktualizacja identyfikatorów rekordów\n'
'- dodanie obsługi kounikatu sl_TenantExfAvailCtrlValSetModify\n'
'- dodanie obsługi kounikatu sl_TenantExfAvailCtrlValSetDelete'


\projekty_etapy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Przypisanie wartości w tabeli PROJEKTY potrzebnych do prawidłowej obsługi etapów projektów.
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

PROJEKTY.cntx_psh();
PROJEKTY.index('FIRMA');
PROJEKTY.prefix();
PROJEKTY.for_each("
__lrec+=1;
_put:=0;
{? PROJEKTY.POZ_PROJ=0
||
   do();
   PROJEKTY.cntx_psh();
   PROJEKTY.index('KOL');
   PROJEKTY.prefix(PROJEKTY.FIRMA, PROJEKTY.IDADD);
   {? PROJEKTY.first()
   ||
      {!|?
         PROJEKTY.POZ_PROJ+=1;
         {? PROJEKTY.put(1) || __lmod+=1 ?};
         PROJEKTY.next()
      !}
   ?};
   PROJEKTY.cntx_pop();
   exec('clean_tree','projekty_view',PROJEKTY.FIRMA, PROJEKTY.IDADD);
   exec('clean_tree','projekty_view',PROJEKTY.FIRMA, PROJEKTY.IDADD);
   end()
?}
",1);
PROJEKTY.cntx_pop();

__UPG.msg('Zaktualizowano projekty dla prawidłowej obsługi etapów.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\projekty_etapy_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PD [22.26]
:: OPIS: Przypisanie wartości w tabeli PROJEKTY potrzebnych do prawidłowej obsługi etapów projektów.
::----------------------------------------------------------------------------------------------------------------------
'Przypisanie wartości w tabeli PROJEKTY potrzebnych do prawidłowej obsługi etapów projektów.'


\GROP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli GROP - grupy operacji
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
::VAR_DEL.delete('__lrec','__lmod');
::__lrec:=__lmod:=0;
GROP.cntx_psh();
GROP.trig_off('*','*');
GROP.prefix();
_result:=GROP.for_each("
::   __lrec+=1;
::   __lmod+=1;
:: Put robiony dla każdego rekordu
:: Aktualizuje pola ILPS oraz CZY_PLAN (put robiony wewnątrz formuły)
   exec('actualize_grop','!tpp_gop_dred',GROP.ref())
",0);
GROP.trig_on('*','*');
GROP.cntx_pop();

:: Aktualizuje pola ILW, ILPOTW, CZY_WYK, CZY_POTW (put robiony wewnątrz formuły)
{? _result
|| _result:=exec('odtworz_ilw','zl_grop',0)
?};
{? _result
|| __UPG.msg('Zaktualizowano grupy operacji.')
?};
::__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
::VAR_DEL.delete('__lrec','__lmod');
_result


\GROP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: WH [22.26]
:: OPIS: Uzupełnia pola tabeli GROP - grupy operacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla grup operacji'


\BADH
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli BADH - zestaw wykonanych badań
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=BADH.names();
_msk.clear();
BADH.trig_off('*','*');
BADH.cntx_psh();
{? _msk.first()
|| {!
   |? BADH.use(_msk.NAME);
      BADH.prefix();
      BADH.for_each("
         _put:=0;
         __lrec+=1;
         {? BADH.ZLGD<>null() & BADH.MZLGD=''
         || BADH.MZLGD:=ref_name(BADH.ZLGD);
            _put:=1
         ?};
         {? _put || {? BADH.put(1) || __lmod+=1 ?} ?}
      ",0);
      _msk.next()
   !}
?};
BADH.cntx_pop();
BADH.trig_on('*','*');
__UPG.msg('Zaktualizowano zestawy wykonanych badań.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BADH_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli BADH - zestaw wykonanych badań - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla zestawów wykonanych badań'


\pakiet_mobilnosci
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie parametrów oddelegowań (pakiet mobilności).
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? PAR_SKID.get(261)='N'
|| __UPG.msg('Brak włączonych oddelegowań w systemie.'@);
   return(1)
?};
_parNum:=397;
_dataParam:=exec('get_par_date','parametr',_parNum);
{? _dataParam=date(0,0,0)
|| {? FUN.ask('%1 %2: "%3".\n(%4 -> %5 -> %6 -> %7 -> %8)\n%9\n%10\n\n%11'
         ['Należy uzupełnić wartość parametru'@,$_parNum,PAR_SKID.opis(_parNum),'Ustawienia i parametryzacja'@,
         'Parametryzacja'@,'Wspólne'@,'Ustawienia systemu'@,'Lista parametrów'@,
         'Parametr powoduje, że od wprowadzonej daty diety pomniejszające przychód wykorzystywane przy rozliczaniu '
         'oddelegowań oraz kierowców (pakiet mobilności) będą liczone na nowym składniku 7197. '
         'Składnik 780 - Podst. opodatkowania zostanie pomniejszony o wartość 30\% diet (7197 - OZ: Diety pomn. prz.).'@,
         'Zmianę daty parametru należy wykonać po zamknięciu wszystkich list z wybranego miesiąca.'@,
         'Czy ustawiono wartość parametru?'@])
   || __UPG.msg('%1 %2.'['Użytkownik potwierdził wypełnienie parametru'@,$_parNum]);
      _result:=1
   || _result:=-1
   ?}
|| __UPG.msg('%1 %2 %3.'['Parametr'@,$_parNum,'posiada już ustawioną datę'@]);
   _result:=1
?};
{? _result=1
|| _parNum:=398;
   {? ~exec('czy_par_t','parametr',_parNum)
   || {? FUN.ask('%1 %2: "%3".\n(%4 -> %5 -> %6 -> %7 -> %8)\n%9\n%10\n\n%11'
            ['Należy uzupełnić wartość parametru'@,$_parNum,PAR_SKID.opis(_parNum),'Ustawienia i parametryzacja'@,
            'Parametryzacja'@,'Wspólne'@,'Ustawienia systemu'@,'Lista parametrów'@,
            'Parametr pozwala na naliczanie skladki zdrowotnej dla pracownika oddelegowaanego płacącego składki ZUS '
            'w Polsce a podadki w kraju oddelegowania.'@,
            'Jeśli naliczać składkę zdrowotną tak jak w wersji 21.37 należy ustawić w parametrze wartość "T".'@,
            'Czy ustawiono wartość parametru?'@])
      || __UPG.msg('%1 %2.'['Użytkownik potwierdził wypełnienie parametru'@,$_parNum]);
         _result:=1
      || _result:=-1
      ?}
   || __UPG.msg('%1 %2 %3.'['Parametr'@,$_parNum,'posiada już ustawioną wartość "T".'@]);
      _result:=1
   ?}
?};

_result


\pakiet_mobilnosci_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Uzupełnienie parametrów oddelegowań - opis.
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie parametrów oddelegowań (pakiet mobilności)'


\rubryki_placowe_ppk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Aktualizacja rubryk i atrybutów płacowych.
::       PR/WRT/XP/21.37/2204/0074 - PR/WRT/XP/12.51/2203/0026 : Wyksięgowanie wpłaty PPK firma w przypadku,
::       gdy przychód nie jest rozliczany na liście ze zwrotami
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';

R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();

{? +exec('import','rubryki','g','7190,7201,7202',1,0)
|| _txt:='Dodanie rubryk 7190, 7201, 7202 oraz atrybutów 771, 772 zakończyło się niepowodzeniem.';
   _result:=-1
|| _txt:='Dodanie rubryk 7190, 7201, 7202 oraz atrybutów 771, 772 zakończone powodzeniem.'
?};
R.trig_on('*','*');

{? _result=1
|| exec('add_attr','rubatr',7,77,'Zwroty wpłat',1,'Zwroty wpłat PPK.');
      exec('add_attr','rubatr',77,771,'Zwrot składek PPK firma - wyksięgowanie kosztów',1,
           'Zwrot składek PPK firma - wyksięgowanie kosztów.',,7190);
      exec('add_attr','rubatr',77,772,'Kwoty zwrotów składek PPK firma - wyksięg. kosztów',1,
        'Kwoty zwrotów składek PPK firma - wyksięgowanie kosztów.');
         exec('add_attr','rubatr',772,7721,'Kwota zwrotu składek podstawowych PPK firma',1,
              'Kwota zwrotu składek podstawowych PPK firma - wyksięgowanie kosztów.',,7201);
         exec('add_attr','rubatr',772,7722,'Kwota zwrotu składek dodatkowych PPK firma',1,
              'Kwota zwrotu składek dodatkowych PPK firma - wyksięgowanie kosztów.',,7202)
?};
__UPG.msg(_txt);

_result


\rubryki_placowe_ppk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Aktualizacja rubryk i atrybutów płacowych - opis.
::       PR/WRT/XP/21.37/2204/0074 - PR/WRT/XP/12.51/2203/0026 : Wyksięgowanie wpłaty PPK firma w przypadku,
::       gdy przychód nie jest rozliczany na liście ze zwrotami
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryki 7190, 7201, 7202 oraz atrybuty 771, 772.\n'
'Poprawka PR/WRT/XP/21.37/2204/0074'


\formuly_placowe_ppk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie formuł płacowych.
::       PR/WRT/XP/21.37/2204/0074 - PR/WRT/XP/12.51/2203/0026 : Wyksięgowanie wpłaty PPK firma w przypadku,
::       gdy przychód nie jest rozliczany na liście ze zwrotami
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='7190';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,R,D,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt_ppk',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_ppk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Dodanie formuł płacowych - opis.
::       PR/WRT/XP/21.37/2204/0074 - PR/WRT/XP/12.51/2203/0026 : Wyksięgowanie wpłaty PPK firma w przypadku,
::       gdy przychód nie jest rozliczany na liście ze zwrotami
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuł płacowych.\n'
'Poprawka PR/WRT/XP/21.37/2204/0074'


\formuly_txt_ppk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [21.37]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=7190
:: ==================
:: Zwrot skł. PPK firma
|| "
{? var_pres('KST_PPK')<>type_of(SYSLOG) || return(0) ?};
exec('zlicz_zwrot','ppk','Z',772)"

|| ""
?}


\zaomedadtcreate
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Poprawka PR/WRT/XP/21.37/2204/0071 - data utworzenia dokumentu w metadanych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','PED')
|| __UPG.msg('Brak licencji na dziedzinę "Dokumentacja pracownicza" - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

_ret:=1;
ZAOMEDA.cntx_psh();
ZAOMEDA.index('NAG');
ZAOMEDA.prefix();
{? ZAOMEDA.first()
|| ZAOMEDA.trig_off('*','*');

   _stat:=obj_new('size','zalacz','seek0','seek1','zm0','zm1','put0','put1','del0','del1');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};
   _stat.size:=ZAOMEDA.size();

   ZALACZ.cntx_psh();
   ZALACZ.prefix();
   {!
   |? _del:=0;
      {? ref_tab(ZAOMEDA.NAG)=ZALACZ
      || _stat.zalacz+=1;
         {? ZALACZ.seek(ZAOMEDA.NAG,)
         || _stat.seek1+=1;
            {? ZAOMEDA.DTCREATE<>ZALACZ.DATA
            || _stat.zm1+=1;
               ZAOMEDA.DTCREATE:=ZALACZ.DATA;
               {? ZAOMEDA.put()
               || _stat.put1+=1
               || _stat.put0+=1
               ?}
            || _stat.zm0+=1
            ?}
         || _stat.seek0+=1;
            _del:=ZAOMEDA.del(,1);
            {? _del>0
            || _stat.del1+=1
            || _stat.del0+=1
            ?}
         ?}
      ?};
      _del=2 | ZAOMEDA.next()
   !};
   ZALACZ.cntx_pop();

   ZAOMEDA.trig_on('*','*');

   __UPG.msg('Liczba rekordów metadanych: %1'[$_stat.size]);
   __UPG.msg('Liczba rekordów metadanych do weryfikacji: %1'[$_stat.zalacz]);
   __UPG.msg('Liczba rekordów źródłowych [znalezionych / nieznalezionych]: %1 / %2'[$_stat.seek1,$_stat.seek0]);
   __UPG.msg('Liczba znalezionych rekordów źródłowych [wymagających / niewymagających] zmiany: %1 / %2'
      [$_stat.zm1,$_stat.zm0]
   );
   __UPG.msg('Liczba rekordów źródłowych wymagających zmiany i [zmienionych / niezmienionych]: %1 / %2'
      [$_stat.put1,$_stat.put0]
   );
   __UPG.msg('Liczba rekordów metadanych bez rekordów źródłowych [usuniętych / nieusuniętych]: %1 / %2'
      [$_stat.del1,$_stat.del0]
   );

   {? _stat.seek0 | _stat.put0 | _stat.del0
   || _ret:=-1
   ?}

|| __UPG.msg('Tabela metadanych nie zawiera rekordów.')
?};
ZAOMEDA.cntx_pop();

_ret


\zaomedadtcreate_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Poprawka PR/WRT/XP/21.37/2204/0071 - data utworzenia dokumentu w metadanych - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'PR/WRT/XP/21.37/2204/0071 - weryfikacja daty utworzenia dokumentów w metadanych'


\jpk_Asi_sch
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Dodanie nowych schematów JPK_ASI
::----------------------------------------------------------------------------------------------------------------------
_ok:=1;
_txt:='AsiWZlotych(1)_v1-2';
_imp:=exec('upg_imp','xml','FKS','J','SF','AsiWZlotych(1)_v1-2',date(2019,9,1),'jpk_asi_v1-2.dfg');
{? _imp=1
|| __UPG.msg(_txt+' został dodany.')
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.')
|| __UPG.msg(_txt+' nie został dodany.');
   _ok:=0
?};
_ok


\jpk_Asi_sch_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: SG [22.26]
:: OPIS: Dodanie nowych schematów JPK_ASI - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych schematów: JPK_ASI\n'
'Schematy obowiązują od 1 września 2019 r.'


\PORTALB2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Przepisanie definicji tabel dodatkowych do kastomizacji portalu do nowej struktury
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

PORTALB.cntx_psh(); PORTALB2.cntx_psh();
PORTALB.index('ENTITY'); PORTALB2.index('ENTITY');
PORTALB.prefix();
{? PORTALB.first()
|| {!
   |? __lrec+=1;
      PORTALB2.prefix(PORTALB.ENTITY,);
      {? ~PORTALB2.first()
      || PORTALB2.blank();
         PORTALB2.FIRMA:=null();
         PORTALB2.ENTITY:=PORTALB.ENTITY;
         PORTALB2.FORM_RRN:=PORTALB.FORM_RRN;
         PORTALB2.FORM_UPD:=PORTALB.FORM_UPD;
         PORTALB2.TAB_ACR:=PORTALB.TAB_ACR;
         {? PORTALB2.add() || __lmod+=1 ?}
      ?};
      PORTALB.next()
   !}
?};

PORTALB.cntx_pop(); PORTALB2.cntx_pop();

__UPG.msg('Zaktualizowano definicje tabel dodatkowych do kastomizacji portalu.');
__UPG.msg('Przetworzono: %1 zapisów źródłówych, z czego dołączono: %2 nowych zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');

_res


\PORTALB2_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Przepisanie definicji tabel dodatkowych do kastomizacji portalu do nowej struktury - opis
::----------------------------------------------------------------------------------------------------------------------
'Przepisanie definicji tabel dodatkowych do kastomizacji portalu do nowej struktury'


\KEOP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Aktualizacja pól tabeli KEOP
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_msk:=KEOP.names();
_msk.clear();
KEOP.trig_off('*','*');
KEOP.cntx_psh();
{? _msk.first()
|| {!
   |? KEOP.use(_msk.NAME);
      KEOP.prefix();
      KEOP.for_each("
         _put:=0;
         {? KEOP.GEN=''
         || KEOP.GEN:='N';
            _put:=1
         ?};
         {? _put || KEOP.put() ?}
      ",1);
      _msk.next()
   !}
?};
KEOP.cntx_pop();
KEOP.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola pozycji kart ewidencji odpadów.');
_result


\KEOP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Aktualizacja pól tabeli KEOP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli pozycji kart ewidencji odpadów.'


\jpk_d1_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Naprawa pozycji D.2.1 deklaracji JPK_V7
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_lrec:=0;
VAT_DEK.cntx_psh(); VAT_POZ.cntx_psh();
VAT_DEK.index('VAT_DEK');
VAT_DEK.prefix(date(2022,01,01),'V7');
{? VAT_DEK.first()
|| {!
   |? VAT_POZ.index('POZ1');
      VAT_POZ.prefix(VAT_DEK.ref(),40);
      {? VAT_POZ.first()
      || {? VAT_POZ.TYP='R'
         || VAT_POZ.TYP:='A';
            _tmp:=VAT_POZ.put();
            {? _tmp || _lrec+=1 ?};
            {? _ret || _ret:=_tmp ?}
         ?}
      ?};
      VAT_DEK.next()
   !}
?};
VAT_DEK.cntx_pop(); VAT_POZ.cntx_pop();
{? _ret
|| __UPG.msg('Zaktualizowano pole nr 40 w %1 deklaracjach JPK_V7'[$_lrec])
|| __UPG.msg('Zaktualizowano pole nr 40 w %1 deklaracjach JPK_V7'[$_lrec]);
   __UPG.msg('Aktualizacja pola nr 40 w deklaracjach JPK_V7 nie powiodła się.')
?};
_ret


\jpk_d1_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [12.51]
:: OPIS: Naprawa pozycji D.2.1 deklaracji JPK_V7 - opis
::----------------------------------------------------------------------------------------------------------------------
'Naprawa pozycji D.2.1 deklaracji JPK_V7'


\proc_ksef_emag2fiks
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Dodaje formułę kontrolną do sprawdzenia zgodności procedur szczególnych KSeF między dokumentami księgowymi
::       w rejestrach, a dokumentami oznaczonymi tymi procedurami w logistyce.
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
FORMULA.cntx_psh();
FORMULA.index('FORMULA4');
FORMULA.prefix('G','PROC_KSF',);
{? ~FORMULA.first()
|| FORMULA.blank();
   FORMULA.RODZAJ:='G';
   FORMULA.SKROT:='PROC_KSF';
   FORMULA.NAZWA:='Formuła kon. sprawdzająca zgodność procedur szczególnych';
   FORMULA.FORMULA:='exec(\'proc_ksef\',\'dok_fks1\')';
   _ret:=FORMULA.add()
|| _ret:=1
?};
FORMULA.cntx_pop();
{? _ret
|| __UPG.msg('Dodano formułę kontrolną PROC_KSF.')
|| __UPG.msg('Dodanie formuły kontrolnej PROC_KSF nie powiodło się.')
?};
_ret


\proc_ksef_emag2fiks_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Dodaje formułę kontrolną do sprawdzenia zgodności procedur szczególnych KSeF między dokumentami księgowymi
::       w rejestrach, a dokumentami oznaczonymi tymi procedurami w logistyce - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły kontrolnej PROC_KSEF do sprawdzenia zgodności procedur szczególnych KSeF między dokumentami księgowym'
' w rejestrach, a dokumentami oznaczonymi tymi procedurami w logistyce.'


\faks_info
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Aktualizacja informacji o załącznikach
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;

_msk:=FAKS.names();
_msk.clear();
{? _msk.first()
|| FAKS.cntx_psh();
   {!
   |? FAKS.use(_msk.NAME);
      FAKS.prefix();
      FAKS.for_each("__lrec+=1;__lmod+=exec('faks_info_val','dokum')",1);
      _msk.next()
   !};
   FAKS.cntx_pop()
?};
obj_del(_msk);
__UPG.msg('Zaktualizowano informacje o załącznikach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\faks_info_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [PD] [22.26]
:: OPIS: Aktualizacja informacji o załącznikach
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja informacji o załącznikach'


\sync_mwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Dodanie komunikatów API.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na Portal HR.');
   return(1)
?};

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

_mtype:=obj_new('DELETE','SEND');
_mtype.DELETE:=exec('mwac_type_delete','synchro');
_mtype.SEND:=exec('mwac_type_send','synchro');

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

_data:=obj_new(1);
{! _lpdd:=1 .. obj_len(_data)
|! _data[_lpdd]:=obj_new('PD','el')
!};

_data[1].PD:='PORTAL';
_data[1].el:=obj_new(6);
_el:=0;

_el+=1;
_data[1].el[_el]:=obj_new('BEFORE','NEW');
_data[1].el[_el].BEFORE:=obj_new('TYPE','METHOD','TAB_ACR');
_data[1].el[_el].BEFORE.TYPE:=_mtype.DELETE;
_data[1].el[_el].BEFORE.METHOD:='chr_PersonDelete';
_data[1].el[_el].BEFORE.TAB_ACR:='P';
_data[1].el[_el].NEW:=obj_new('TYPE','METHOD','TAB_ACR','PACK','SYNC_REP','DESC');
_data[1].el[_el].NEW.TYPE:=_mtype.DELETE;
_data[1].el[_el].NEW.METHOD:='sl_SloImageDelete';
_data[1].el[_el].NEW.TAB_ACR:='P';
_data[1].el[_el].NEW.PACK:=0;
_data[1].el[_el].NEW.SYNC_REP:=1;
_data[1].el[_el].NEW.DESC:='Zdjęcie współpracownika.';

_el+=1;
_data[1].el[_el]:=obj_new('BEFORE','NEW');
_data[1].el[_el].BEFORE:=obj_new('TYPE','METHOD','TAB_ACR');
_data[1].el[_el].BEFORE.TYPE:=_mtype.SEND;
_data[1].el[_el].BEFORE.METHOD:='chr_PersonWorkScheduleDelete';
_data[1].el[_el].BEFORE.TAB_ACR:='PKALSYNC';
_data[1].el[_el].NEW:=obj_new('TYPE','METHOD','TAB_ACR','PACK','SYNC_REP','DESC');
_data[1].el[_el].NEW.TYPE:=_mtype.SEND;
_data[1].el[_el].NEW.METHOD:='sl_SloImageDelete';
_data[1].el[_el].NEW.TAB_ACR:='P';
_data[1].el[_el].NEW.PACK:=0;
_data[1].el[_el].NEW.SYNC_REP:=2;
_data[1].el[_el].NEW.DESC:='Zdjęcie współpracownika.';

_el+=1;
_data[1].el[_el]:=obj_new('BEFORE','NEW');
_data[1].el[_el].BEFORE:=obj_new('TYPE','METHOD','TAB_ACR');
_data[1].el[_el].BEFORE.TYPE:=_mtype.SEND;
_data[1].el[_el].BEFORE.METHOD:='chr_PersonWorkScheduleDelete';
_data[1].el[_el].BEFORE.TAB_ACR:='PKALSYNC';
_data[1].el[_el].NEW:=obj_new('TYPE','METHOD','TAB_ACR','PACK','SYNC_REP','DESC');
_data[1].el[_el].NEW.TYPE:=_mtype.SEND;
_data[1].el[_el].NEW.METHOD:='sl_SloImageModify';
_data[1].el[_el].NEW.TAB_ACR:='P';
_data[1].el[_el].NEW.PACK:=0;
_data[1].el[_el].NEW.SYNC_REP:=2;
_data[1].el[_el].NEW.DESC:='Zdjęcie współpracownika.';

_el+=1;
_data[1].el[_el]:=obj_new('BEFORE','NEW');
_data[1].el[_el].BEFORE:=obj_new('TYPE','METHOD','TAB_ACR');
_data[1].el[_el].BEFORE.TYPE:=_mtype.SEND;
_data[1].el[_el].BEFORE.METHOD:='chr_KnowledgeBaseItemModify';
_data[1].el[_el].BEFORE.TAB_ACR:='BNFTT';
_data[1].el[_el].NEW:=obj_new('TYPE','METHOD','TAB_ACR','PACK','SYNC_REP','DESC');
_data[1].el[_el].NEW.TYPE:=_mtype.SEND;
_data[1].el[_el].NEW.METHOD:='chr_KnowledgeBaseItemDelete';
_data[1].el[_el].NEW.TAB_ACR:='BNFTT';
_data[1].el[_el].NEW.PACK:=0;
_data[1].el[_el].NEW.SYNC_REP:=-1;
_data[1].el[_el].NEW.DESC:='Nagłówek elementu benefitu.';

_el+=1;
_data[1].el[_el]:=obj_new('BEFORE','NEW');
_data[1].el[_el].BEFORE:=obj_new('TYPE','METHOD','TAB_ACR');
_data[1].el[_el].BEFORE.TYPE:=_mtype.DELETE;
_data[1].el[_el].BEFORE.METHOD:='sl_TenantParamsDelete';
_data[1].el[_el].BEFORE.TAB_ACR:='PORPAR';
_data[1].el[_el].NEW:=obj_new('TYPE','METHOD','TAB_ACR','PACK','SYNC_REP','DESC');
_data[1].el[_el].NEW.TYPE:=_mtype.DELETE;
_data[1].el[_el].NEW.METHOD:='chr_WorkingHoursDelete';
_data[1].el[_el].NEW.TAB_ACR:='PORSLOIT';
_data[1].el[_el].NEW.PACK:=0;
_data[1].el[_el].NEW.SYNC_REP:=1;
_data[1].el[_el].NEW.DESC:='Wymiary etatów.';

_el+=1;
_data[1].el[_el]:=obj_new('BEFORE','NEW');
_data[1].el[_el].BEFORE:=obj_new('TYPE','METHOD','TAB_ACR');
_data[1].el[_el].BEFORE.TYPE:=_mtype.SEND;
_data[1].el[_el].BEFORE.METHOD:='chr_PersonModify';
_data[1].el[_el].BEFORE.TAB_ACR:='P';
_data[1].el[_el].NEW:=obj_new('TYPE','METHOD','TAB_ACR','PACK','SYNC_REP','DESC');
_data[1].el[_el].NEW.TYPE:=_mtype.SEND;
_data[1].el[_el].NEW.METHOD:='chr_WorkingHoursModify';
_data[1].el[_el].NEW.TAB_ACR:='PORSLOIT';
_data[1].el[_el].NEW.PACK:=1;
_data[1].el[_el].NEW.SYNC_REP:=2;
_data[1].el[_el].NEW.DESC:='Wymiary etatów.';

:: - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
_ret:=1;
SYNC_REP.cntx_psh();
SYNC_REP.index('PDNR1');
_ndx:=SYNC_MWA.ndx_tmp(,
   ,'SYNC_PD','SYM',
   ,'TYPE',,
   ,'METHOD',,
   ,'TAB_ACR',,
);
SYNC_MWA.cntx_psh();
{! _lppd:=1 .. obj_len(_data)
|! SYNC_REP.prefix(_data[_lppd].PD,);
   SYNC_MWA.index('TREE');
   SYNC_MWA.prefix(0,_data[_lppd].PD,0);
   {? SYNC_MWA.first()
   || _root:=#SYNC_MWA.ref();
      _pd:=SYNC_MWA.SYNC_PD;
      _mwac:=SYNC_MWA.MWAC;
      SYNC_MWA.index(_ndx);
      SYNC_MWA.prefix(_data[_lppd].PD,);
      {! _lpm:=1 .. obj_len(_data[_lppd].el)
      |! {? SYNC_MWA.find_key(_data[_lppd].el[_lpm].NEW.TYPE
               ,_data[_lppd].el[_lpm].NEW.METHOD
               ,_data[_lppd].el[_lpm].NEW.TAB_ACR
               ,
            )
         || __UPG.msg(
               'Komunikat %1 typu "%2" dla przeznaczenia danych %3 już istniał.'
               [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
            )
         |? SYNC_MWA.find_key(_data[_lppd].el[_lpm].BEFORE.TYPE
               ,_data[_lppd].el[_lpm].BEFORE.METHOD
               ,_data[_lppd].el[_lpm].BEFORE.TAB_ACR
               ,
            )
         || do();
            _lp:=SYNC_MWA.LP;
            _sync_rep:=
               {? _data[_lppd].el[_lpm].NEW.SYNC_REP=-1
               || SYNC_MWA.SYNC_REP
               |? _data[_lppd].el[_lpm].NEW.SYNC_REP=0
               || null()
               |? SYNC_REP.find_key(_data[_lppd].el[_lpm].NEW.SYNC_REP)
               || SYNC_REP.ref()
               || __UPG.msg(
                     'Komunikat %1 typu "%2" dla przeznaczenia danych %3  - zweryfikuj informacje o replice.'
                     [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
                  );
                  null()
               ?};
            SYNC_MWA.cntx_psh();
            SYNC_MWA.index('TREE1');
            SYNC_MWA.prefix(_root);
            {? SYNC_MWA.last()
            || {!
               |? SYNC_MWA.LP+=1;
                  SYNC_MWA.put();
                  SYNC_MWA.LP>_lp+1 & SYNC_MWA.prev()
               !};
               SYNC_MWA.blank();
               SYNC_MWA.SYNC_PD:=_pd;
               SYNC_MWA.MWAC:=_mwac;
               SYNC_MWA.PARENT:=_root;
               SYNC_MWA.LP:=_lp;
               SYNC_MWA.TYPE:=_data[_lppd].el[_lpm].NEW.TYPE;
               SYNC_MWA.METHOD:=_data[_lppd].el[_lpm].NEW.METHOD;
               SYNC_MWA.TAB_ACR:=_data[_lppd].el[_lpm].NEW.TAB_ACR;
               SYNC_MWA.AKT:='T';
               SYNC_MWA.PACK:=_data[_lppd].el[_lpm].NEW.PACK;
               SYNC_MWA.SYNC_REP:=_sync_rep;
               {? SYNC_MWA.add()
               || SYNC_MWA.memo_set(_data[_lppd].el[_lpm].NEW.DESC,'DESC');
                  {? SYNC_MWA.memo_put(,'DESC')
                  || __UPG.msg(
                        'Komunikat %1 typu "%2" dla przeznaczenia danych %3 został dodany.'
                        [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
                     )
                  || __UPG.msg(
                        'Opis komunikatu %1 typu "%2" dla przeznaczenia danych %3 nie został dodany.'
                        [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
                     );
                     _ret:=-1
                  ?}
               || __UPG.msg(
                     'Komunikat %1 typu "%2" dla przeznaczenia danych %3 nie został dodany (1).'
                     [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
                  );
                  _ret:=-1
               ?}
            ?};
            SYNC_MWA.cntx_pop();
            {? ~end()
            || __UPG.msg(
                  'Komunikat %1 typu "%2" dla przeznaczenia danych %3 został dodany (2).'
                  [_data[_lppd].el[_lpm].NEW.METHOD,_data[_lppd].el[_lpm].NEW.TYPE,_data[_lppd].PD]
               );
               _ret:=-1
            ?}
         || __UPG.msg(
               'Odnalezienie komunikatu %1 typu "%2" nie powiodło się.'
               [_data[_lppd].el[_lpm].BEFORE.METHOD,_data[_lppd].el[_lpm].BEFORE.TYPE]
            );
            _ret:=-1
         ?}
      !}

   || __UPG.msg('Brak przeznaczenia danych %1.\nKomunikaty nie zostaną dodane.'[_data[_lppd].PD])
   ?}
!};
SYNC_MWA.cntx_pop();
SYNC_MWA.ndx_drop(_ndx);
SYNC_REP.cntx_pop();
_ret


\sync_mwa_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Dodanie komunikatów API.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Dodanie komunikatów API'


\p_zdjecie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wymuszenie wysyłki na Portal HR zdjęć współpracowników.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na Portal HR.');
   return(1)
?};

_ret:=1;
P.cntx_psh();
P.index('FIRMA_ZA');
P.prefix();
{? P.first()
|| _ile:=0;
   P.trig_off('*','*');
   OSOBA.cntx_psh();
   OSOBA.prefix();
   {!
   |? {? P.PORTAL='T' & P.OSOBA().ZDJECIE<>null()
      || _ile+=P.put(,1)
      ?};
      P.next()
   !};
   OSOBA.cntx_pop();
   P.trig_on('*','*');
   __UPG.msg('Liczba współpracowników ze zdjęciami: %1'[$_ile])
?};
P.cntx_pop();
_ret


\p_zdjecie_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Wymuszenie wysyłki na Portal HR zdjęć współpracowników.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wymuszenie wysyłki na Portal HR zdjęć współpracowników.'


\BRAKI_GD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli BRAKI_GD - braki zakwalifikowane do zapisów akordowych
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
BRAKI_K.cntx_psh();
BRAKI_GD.cntx_psh();
BRAKI_GD.trig_off('*','*');
BRAKI_K.prefix();
BRAKI_GD.index('BRAKI');
BRAKI_GD.prefix();
{? BRAKI_K.first()
||
   ZLGD.cntx_psh();
   ZLGD.clear();
   {!
   |?
      _il_napr:=BRAKI_K.IL_NAPR;
      _il_nnapr:=BRAKI_K.IL_NNAPR;
      BRAKI_GD.prefix(BRAKI_K.ref());
      {? BRAKI_GD.first()
      ||
         {!
         |?
            _put:=0;
            __lrec+=1;
::          Uzupełnienie nowych, dodanych pól
            {? BRAKI_GD.ZL=null() | BRAKI_GD.KTM=null() | BRAKI_GD.WYKON=' '
            || _put:=1;
               BRAKI_GD.BRAKI_RK:=BRAKI_K.BRAKI_R;
               _date:=exec('str2date','#convert',10+BRAKI_GD.idadd_value());
               BRAKI_GD.DTK:=exec('get','daty',_date);
               {? ZLGD.seek(BRAKI_GD.ZLGD,ref_name(BRAKI_GD.ZLGD),1)
               || BRAKI_GD.ZL:=ZLGD.ZL;
                  BRAKI_GD.ZGH:=ZLGD.ZGH;
                  BRAKI_GD.ZGP:=ZLGD.ZGP;
                  {? ZLGD.KOOP='T'
                  || BRAKI_GD.WYKON:=ZLGD.ZGP().KONTRAH().NAZ
                  || {? ZLGD.BP='P'
                     || BRAKI_GD.WYKON:=ZLGD.P().OSOBA().NAZWISKO+' '+OSOBA.PIERWSZE
                     || BRAKI_GD.WYKON:=ZLGD.B().KOD+' '+ZLBR.NAZ
                     ?}
                  ?};
                  BRAKI_GD.KTM:=ZLGD.ZL().KTM;
                  BRAKI_GD.MGR:=ZLGD.ZL().KTM().MGR;
                  BRAKI_GD.JM:=ZLGD.ZL().KTM().J;
                  BRAKI_GD.PLACE:=ZLGD.PLACE;
                  BRAKI_GD.BRAKI_RR:=ZLGD.BRAKI_R;
                  BRAKI_GD.DTR:=exec('get','daty',ZLGD.DT)
               ?}
            ?};
::          Rozpisanie na ilości naprawialne/nienaprawialne
            {? BRAKI_GD.IL_NAPR+BRAKI_GD.IL_NNAPR<>BRAKI_GD.IL
            || _put:=1;
               {? BRAKI_GD.size()=1
               ||
::                Jak tylko 1 rekord powiązany to przepisanie z BRAKI_K
                  BRAKI_GD.IL_NAPR:=BRAKI_K.IL_NAPR;
                  BRAKI_GD.IL_NNAPR:=BRAKI_K.IL_NNAPR
               ||
                  _rm:=roundmet();
                  roundmet(5);
                  _dokl:=exec('jaka_dok_m','jm',BRAKI_GD.KTM);
                  _wsp:=BRAKI_GD.IL/BRAKI_K.IL;
                  _napr:=(BRAKI_K.IL_NAPR*_wsp)$_dokl;
                  _nnapr:=(BRAKI_K.IL_NNAPR*_wsp)$_dokl;
                  {? _napr>_il_napr
                  || BRAKI_GD.IL_NAPR:=_il_napr
                  || BRAKI_GD.IL_NAPR:=_napr
                  ?};
                  {? _nnapr>_il_nnapr
                  || BRAKI_GD.IL_NNAPR:=_il_nnapr
                  || BRAKI_GD.IL_NNAPR:=_nnapr
                  ?};
::                Ewentualne korekty zaokrągleń
                  _diff:=BRAKI_GD.IL_NAPR+BRAKI_GD.IL_NNAPR-BRAKI_GD.IL;
                  {? _diff<>0
                  || _left:=_diff;
                     {? _diff>0
                     ||
::                      Rozpisano za dużo
                        {? BRAKI_GD.IL_NAPR>0
                        || {? BRAKI_GD.IL_NAPR>=_left
                           || BRAKI_GD.IL_NAPR-=_left;
                              _left:=0
                           || _left-=BRAKI_GD.IL_NAPR;
                              BRAKI_GD.IL_NAPR:=0
                           ?}
                        ?};
                        {? _left>0 & BRAKI_GD.IL_NNAPR>0
                        || {? BRAKI_GD.IL_NNAPR>=_left
                           || BRAKI_GD.IL_NNAPR-=_left;
                              _left:=0
                           || _left-=BRAKI_GD.IL_NNAPR;
                              BRAKI_GD.IL_NNAPR:=0
                           ?}
                        ?}
                     ||
::                      Rozpisano za mało
                        {? _il_napr>0
                        || {? BRAKI_GD.IL_NAPR-_left<=_il_napr
                           || BRAKI_GD.IL_NAPR-=_left;
                              _left:=0
                           || _left+=(_il_napr-BRAKI_GD.IL_NAPR);
                              BRAKI_GD.IL_NAPR:=_il_napr
                           ?}
                        ?};
                        {? _left<0 & _il_nnapr>0
                        || {? BRAKI_GD.IL_NNAPR-_left<=_il_nnapr
                           || BRAKI_GD.IL_NNAPR-=_left;
                              _left:=0
                           || _left+=(_il_nnapr-BRAKI_GD.IL_NNAPR);
                              BRAKI_GD.IL_NNAPR:=_il_nnapr
                           ?}
                        ?}
                     ?}
                  ?};
                  roundmet(_rm)
               ?}
            ?};
            {? _put || {? BRAKI_GD.put(1) || __lmod+=1 ?} ?};
            BRAKI_GD.next()
         !}
      ?};
      BRAKI_K.next()
   !};
   ZLGD.cntx_pop()
?};
BRAKI_GD.trig_on('*','*');
BRAKI_GD.cntx_pop();
BRAKI_K.cntx_pop();
__UPG.msg('Zaktualizowano braki zakwalifikowane do zapisów akordowych.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\BRAKI_GD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli BRAKI_GD - braki zakwalifikowane do zapisów akordowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola dla braków zakwalifikowanych do zapisów akordowych'


\SYNC_IDP
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AWI] [22.26]
:: OPIS: Aktualizacja znaczników synchronizacji
::----------------------------------------------------------------------------------------------------------------------
_res:=1;

SYNC_IDP.prefix();
SYNC_IDP.for_each("
   SYNCNIDP.FIRMA:=REF.FIRMA().SYMBOL;
   SYNCNIDP.SYNC_PD:=SYNC_IDP.SYNC_PD;
   SYNCNIDP.SYNC_REP:=SYNC_IDP.SYNC_REP;
   SYNCNIDP.ACR:=SYNC_IDP.ACR;
   SYNCNIDP.IDPUT:=SYNC_IDP.IDPUT;
   SYNCNIDP.IDPUTMP:=SYNC_IDP.IDPUTMP;
   SYNCNIDP.IDSYNC:=SYNC_IDP.IDSYNC;
   SYNCNIDP.IDADD:=SYNC_IDP.IDADD;
   SYNCNIDP.IDADDTMP:=SYNC_IDP.IDADDTMP;
   SYNCNIDP.IDADDOD:=SYNC_IDP.IDADDOD;
   do();
   SYNCNIDP.add();
   SYNC_IDP.del();
   end()
");
__UPG.msg('Zaktualizowano znaczniki synchronizacji.');
_res


\SYNC_IDP_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [AWI] [22.26]
:: OPIS: Aktualizacja znaczników synchronizacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znaczników synchronizacji'


\DOKUMZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Uzupełnienie dat załaczników
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
DOKUMZ.trig_off('*','*');
DOKUMZ.cntx_psh();
DOKUMZ.prefix();
DOKUMZ.for_each("
   __lrec+=1;
   _put:=0;
   {? DOKUMZ.DOKUM<>null() & DOKUMZ.DATE=date(0,0,0)
   || DOKUMZ.DATE:=DOKUMZ.bl_info('DOKUM','MODIFY_DATE');
      DOKUMZ.TIME:=DOKUMZ.bl_info('DOKUM','MODIFY_TIME');
      _put:=1
   ?};
   {? _put
   || {? DOKUMZ.put(1) || __lmod+=1 ?}
   ?}",
   1
);
DOKUMZ.cntx_pop();
DOKUMZ.trig_on('*','*');
__UPG.msg('Uzupełniono daty załączników.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_res


\DOKUMZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TS [22.26]
:: OPIS: Uzupełnienie dat załaczników - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnienie dat załączników'


\portalwu_init
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Formuła inicjująco-sprzątająca w PORTALWU - odpowiedzialna za synchronizację zawartości tabeli na podstawie
::       atrybutu 193.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na Portal HR.');
   return(1)
?};
_ret:=exec('portalwu_init','portal_nieobecnosci',1);
{? _ret=''
|| __UPG.msg('Aktualizacja parametrów wniosków o urlop nie była wymagana.')
|| __UPG.msg('Lista kodów nieobecności zaktualizowanych pozycji: %1.'[_ret])
?};
1


\portalwu_init_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Formuła inicjująco-sprzątająca w PORTALWU - odpowiedzialna za synchronizację zawartości tabeli na podstawie
::       atrybutu 193 - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Formuła inicjująco parametry wniosków o urlop'


\cit8_31
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37_19]
:: OPIS:  OPIS: Import sprawozdań na potrzeby deklaracji CIT-8 (31)
::----------------------------------------------------------------------------------------------------------------------
_spr:='cit-8_31_spr.dfg';
_edek:='cit-8_31.dfg';
_ok:=1;
{? fexists(_spr,1)
|| _ok:=exec('imp_spr','sprfin',_spr,2,0);
   {? _ok
   || __UPG.msg('Zaimportowano sprawozdania z pliku: %1'[_spr])
   || __UPG.msg('Nie udało się zaimportować sprawozdania z pliku: %1'[_spr])
   ?}
|| __UPG.msg('Nie znaleziono na serwerze pliku: %1'[_spr]);
   _ok:=0
?};
{? fexists(_edek,1)
|| _imp:=-1;
   {? exec('add_ver','xml','FKS',date(2021,1,1),'CIT8',31)
   || _imp:=exec('upg_imp','xml','FKS','D','CIT8',31,date(2021,1,1),'cit-8_31.dfg')
   ?};
   _txt:='e-Deklaracja CIT-8 (31) obowiązująca za 2021 rok';
   {? _imp=1
   || __UPG.msg(_txt+' została dodana.')
   |? _imp=0
   || __UPG.msg(_txt+' już istnieje.')
   || __UPG.msg(_txt+' nie została dodana.');
      _ok:=0
   ?}
|| __UPG.msg('Nie znaleziono na serwerze pliku: %1'[_edek]);
   _ok:=0
?};
_ok


\cit8_31_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Import sprawozdań na potrzeby deklaracji CIT-8 (31) - opis
::----------------------------------------------------------------------------------------------------------------------
'Import sprawozdań CIT-8 (31) i CIT-8O (17) za 2021 rok\n'
'Import schematów e-Deklaracji CIT-8 (31) za 2021 rok'


\cit8_31_akt
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Poprawka ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji
::----------------------------------------------------------------------------------------------------------------------
res:=0;
VAT_VER.cntx_psh();
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS','CIT8','CIT8',31);
_ver:={? VAT_VER.first() || VAT_VER.ref() || null ?};
VAT_VER.cntx_pop();
{? _ver
|| ISTDEF.cntx_psh();
   ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','CIT8',_ver);
   {? ISTDEF.first()
   || ISTDEFS.cntx_psh();
      ISTDEFS.index('LP');
      exec('pomoc_85','upgrade_2226',205,185); exec('pomoc_85','upgrade_2226',307,264); exec('pomoc_85','upgrade_2226',309,259);
      exec('pomoc_85','upgrade_2226',310,265); exec('pomoc_85','upgrade_2226',312,260); exec('pomoc_85','upgrade_2226',313,266);
      exec('pomoc_85','upgrade_2226',315,261); exec('pomoc_85','upgrade_2226',316,267); exec('pomoc_85','upgrade_2226',318,262);
      exec('pomoc_85','upgrade_2226',319,268); exec('pomoc_85','upgrade_2226',321,263); exec('pomoc_85','upgrade_2226',322,269);
      exec('pomoc_85','upgrade_2226',324,270); exec('pomoc_85','upgrade_2226',325,276); exec('pomoc_85','upgrade_2226',327,271);
      exec('pomoc_85','upgrade_2226',328,277); exec('pomoc_85','upgrade_2226',330,272); exec('pomoc_85','upgrade_2226',331,278);
      exec('pomoc_85','upgrade_2226',333,273); exec('pomoc_85','upgrade_2226',334,279); exec('pomoc_85','upgrade_2226',336,274);
      exec('pomoc_85','upgrade_2226',337,280); exec('pomoc_85','upgrade_2226',339,275); exec('pomoc_85','upgrade_2226',340,281);
      exec('pomoc_85','upgrade_2226',342,282); exec('pomoc_85','upgrade_2226',343,288); exec('pomoc_85','upgrade_2226',345,283);
      exec('pomoc_85','upgrade_2226',346,289); exec('pomoc_85','upgrade_2226',348,284); exec('pomoc_85','upgrade_2226',349,290);
      exec('pomoc_85','upgrade_2226',351,285); exec('pomoc_85','upgrade_2226',352,291); exec('pomoc_85','upgrade_2226',354,286);
      exec('pomoc_85','upgrade_2226',355,292); exec('pomoc_85','upgrade_2226',357,287); exec('pomoc_85','upgrade_2226',358,293);
      exec('pomoc_85','upgrade_2226',360,294); exec('pomoc_85','upgrade_2226',361,299); exec('pomoc_85','upgrade_2226',363,295);
      exec('pomoc_85','upgrade_2226',364,300); exec('pomoc_85','upgrade_2226',366,296); exec('pomoc_85','upgrade_2226',367,301);
      exec('pomoc_85','upgrade_2226',369,297); exec('pomoc_85','upgrade_2226',370,302); exec('pomoc_85','upgrade_2226',372,298);
      ISTDEFS.cntx_pop()
   ?};
   ISTDEF.cntx_pop()
?};

_txt:='e-Deklaracja CIT-8 (31) obowiązująca za 2021 rok';
{? res=1
|| __UPG.msg(_txt+' została naprawiona.');  _ok:=1
|? res=0
|| __UPG.msg(_txt+' nie istnieje w systemie.');  _ok:=1
|| __UPG.msg(_txt+' nie wymagała naprawy.');
   _ok:=1
?};
&res; _ok


\pomoc_85
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: Pomocnicza do ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji
::   WE: _a - LP , _b - na co zmienić
::----------------------------------------------------------------------------------------------------------------------
_b:=$_b;
ISTDEFS.prefix(ISTDEF.ref(),_a);
{? ISTDEFS.first()
|| _fml:=$"exec('poz_edek_dod','xml','CIT8 ',1,"+_b+')'; _opis:='P_'+_b;
   res:={? ISTDEFS.REGULY=_fml || 2 || ISTDEFS.REGULY:=_fml; ISTDEFS.OPIS:=_opis; res:=ISTDEFS.put() ?}
?};
res


\cit8_31_akt_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [21.37]
:: OPIS: ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji - opis.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/21.37/2206/0022 - CIT- błędy w walidacji'


\op_drb
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wypełnia pole OP.RATY
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_NAMES:=OP.names;
{? _NAMES.first()
|| OP.cntx_psh(); D_RB.cntx_psh();
   {! |?
      OP.use(_NAMES.NAME); OP.index('HAN'); OP.prefix();
      D_RB.use('dr__fk'+(OP.name()+2)); D_RB.index('OP');
      OP.for_each("__lrec+=1; _raty:=OP.RATY;
                   D_RB.prefix(OP.ref());
                   OP.RATY:={? D_RB.first() || 'T' || 'N' ?};
                   {? _raty<>OP.RATY & OP.put() || __lmod+=1 ?}
                  ");
      _NAMES.next()
   !};
   OP.cntx_pop(); D_RB.cntx_pop()
?};
__UPG.msg('Zaktualizowano znacznik rat w rozrachunkach.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
1


\op_drb_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AMK [22.26]
:: OPIS: Wypełnia pole OP.RATY - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja znacznika rat w rozrachunkach'


\REZ
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Aktualizacja pól tabeli REZ
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=REZ.names();
_msk.clear();
REZ.trig_off('*','*');
REZ.cntx_psh();
{? _msk.first()
|| {!
   |? REZ.use(_msk.NAME);
      REZ.prefix();
      REZ.for_each("
         _put:=0;
         __lrec+=1;
         {? REZ.ZL_REZ=''
         || REZ.ZL_REZ:='N';
            _put:=1
         ?};
         {? _put || {? REZ.put() || __lmod+=1 ?} ?}
      ",1);
      _msk.next()
   !}
?};
REZ.cntx_pop();
REZ.trig_on('*','*');
__UPG.msg('Zaktualizowano nowe pola dla rezerwacji.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
VAR_DEL.delete('__lrec','__lmod');
_result


\REZ_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Aktualizacja pól tabeli REZ - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pól tabeli rezerwacji.'


\procesy_sprzedazy
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów sprzedaży
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','SPR_')


\procesy_sprzedazy_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów sprzedaży - opis
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować zmienione procesy dziedziny LSP:\n'+
'"SPR_DTR - Dyspozycja transportu" - poprawiono błąd (przy tworzeniu dyspozycji magazynowej)\n'+
'"SPR_OFE - Oferta sprzedaży" - uproszczenie, usunięto akceptację oferty i tworzenie zamówienia sprzedaży \n'+
'"SPR_ZAM - Zamówienie sprzedaży z realizacją" - dodano zamówienie sprzedaży na podstawie reklamacji, optymalizacja działania zdarzeń warunkowych\n'+
'"SPR_ZAM_OBG - Zamówienie sprzedaży z obiegiem i realizacją" - dodano zamówienie sprzedaży na podstawie reklamacji, optymalizacja działania zdarzeń warunkowych'@


\procesy_zakupu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów zakupu i magazynowych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','ZKM_')


\procesy_zakupu_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów zakupu i magazynowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować zmienione procesy dziedziny LZK i LMG:\n'+
'"ZKM_DOS_OBG - Zamówienie dostaw z obiegiem i realizacją" - optymalizacja działania zdarzeń warunkowych\n'+
'"ZKM_MOB_ZAM - Mobilna realizacja zamówienia" - optymalizacja działania zdarzeń warunkowych\n'+
'"ZKM_ZAKO - Faktura zakupowa z obiegiem" - optymalizacja działania zdarzeń warunkowych\n'+
'"ZKM_ZAM - Zamówienie wewnętrzne z realizacją" - poprawiono błąd (proces się nie kończył), optymalizacja działania zdarzeń warunkowych\n'+
'"ZKM_ZAM_OBG - Zamówienie wewnętrzne z obiegiem i realizacją" - poprawiono błąd (proces się nie kończył), optymalizacja działania zdarzeń warunkowych'@


\procesy_remontowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów remontowych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','RZC_')


\procesy_remontowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów remontowych - opis
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować zmienione procesy dziedziny TRE:\n'+
'"RZC_ZCR - Obsługa zgłoszeń remontowych cyklicznych" - umożliwiono automatyczne generowanie powiązanych zgłoszeń remontowych (kopii) podczas zamykania zgłoszenia\n'+
'"RZC_ZGR - Obsługa zgłoszenia remontowego" - umożliwiono automatyczne generowanie powiązanych zgłoszeń remontowych (kopii) podczas zamykania zgłoszenia'@


\procesy_businesslink
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów Businesslink
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','ZBL_')


\procesy_businesslink_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów Businesslink - opis
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować zmienione procesy dziedziny ZBL:\n'+
'"ZBL_BLK - Aktual. kontrahentów z systemu Businesslink" - zmieniono domyślny harmonogram zdarzenia czasowego\n'+
'"ZBL_BLR - Odczyt potw. i dok. z systemu Businesslink" - zmieniono zdarzenie czasowe na warunkowe, uruchamiane po odebraniu notyfikacji z Businesslink'@


\procesy_integracja
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów usług integracyjnych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','ZUI_')


\procesy_integracja_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów usług integracyjnych - opis
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować zmienione procesy dziedziny ZUI:\n'+
'"ZUI_ABSTORE_B2B - Integracja AbStore B2B" - wyodrębnieno wysyłkę rozrachunków, optymalizacja działania\n'+
'"ZUI_POR_IN_ALL - Komunikacja z portalem - odbieranie danych" - pobieranie wszystkich danych z portalu HR i SEOD (nowy proces)\n'+
'"ZUI_POR_OUT_CFG - Komunikacja z portalem - wysyłanie danych" - dodano dane konfiguracyjne nowego typu wniosków pozostałych (SEOD)\n'+
'"ZUI_SEO_FA_IN - Komunikacja z portalem (faktury) - odbieranie" - pobieranie faktur z portalu SEOD (nowy proces)\n'+
'"ZUI_SEO_FA_OUT - Komunikacja z portalem (faktury) - wysyłanie" - wysyłanie faktur na portal SEOD (nowy proces)'@


\procesy_wspolne
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów wspólnych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','ZWS_')


\procesy_wspolne_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MW [22.26]
:: OPIS: Import procesów wspólnych - opis
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować nowe i zmienione procesy dziedziny ZWS:\n'+
'"ZWS_BAK - Backup bazy danych" - dodano automatyczne ustalenie administratora systemu jako adresata ewentualnych e-maili z powiadomieniami\n'+
'"ZWS_UPG - Aktualizacja systemu (zadania potransferowe)" - dodano możliwość wysyłania maili o nieprzetworzonych zadaniach potransferowych\n'+
'"ZWS_UPG_MAIL - Mail o nieprzetworzonych zadaniach" - wysłanie maila o nieprzetworzonych zadaniach potransferowych automatycznych (nowy proces)\n'+
'"ZWS_UPG_M_MAIL - Mail o nieprzetworzonych zadaniach manualnych" - wysłanie maila o nieprzetworzonych zadaniach potransferowych manualnych (nowy proces)'@


\procesy_faktur_obiegu
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Import procesów faktur i wniosków w obiegu SEOD
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','FIN_')


\procesy_faktur_obiegu_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Import procesów faktur w obiegu SEOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować nowe procesy dziedziny OBG:\n'
'- FIN_FWO_WWPS - Faktura w obiegu - akceptacja w SEOD\n\n'

'Pozwala zaimportować nowe procesy dziedziny OBE:\n'
'- FIN_WOZ_SEOD - Wniosek o akceptację zakupu'


\procesy_paperlesshr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Import procesów faktur w obiegu SEOD
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','PEP_')


\procesy_paperlesshr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: Import procesów faktur w obiegu SEOD - opis
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować zmienione procesy dziedziny PEP:\n'+
'"PEP_ZWP_WWW - Paperless HR - Rez. ze zwol. z opod. do 26 l." - '@


\REJ_MAT
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli REJ_MAT - Rozpiska rozliczeń surowców do operacji
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
REJ_MAT.trig_off('*','*');
REJ_MAT.cntx_psh();
REJ_MAT.prefix();
REJ_MAT.for_each("
   _put:=0;
   __lrec+=1;
   {? REJ_MAT.ZLGD<>null() & REJ_MAT.MZLGD=''
   || REJ_MAT.MZLGD:=ref_name(REJ_MAT.ZLGD);
      _put:=1
   ?};
   {? _put || {? REJ_MAT.put(1) || __lmod+=1 ?} ?}
",0);
REJ_MAT.cntx_pop();
REJ_MAT.trig_on('*','*');
{? _result
|| __UPG.msg('Zaktualizowano pola rozliczeń surowców do operacji.');
   __UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod])
?};
VAR_DEL.delete('__lrec','__lmod');
_result


\REJ_MAT_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AKUL [22.26]
:: OPIS: Uzupełnia pola tabeli REJ_MAT - Rozpiska rozliczeń surowców do operacji - opis
::----------------------------------------------------------------------------------------------------------------------
'Uzupełnia pola rozliczeń surowców do operacji.'


\KST_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Dodanie nowych pól do KST
::----------------------------------------------------------------------------------------------------------------------
:: Dodanie pola związanego z procentowym wynagrodzeniem za terminową wpłatę podatków
exec('add_acr','#stalesys','PW_TWP','Procent wynagrodzenia za terminową wpłatę podatków','T','T','T','KST','PPL');
_dodano:=exec('kst_war_add','#stalesys','KST.PW_TWP','0.00',date(0,0,0));
_dodano+=exec('kst_war_add','#stalesys','KST.PW_TWP','0.30',date(2013,1,1));
_dodano+=exec('kst_war_add','#stalesys','KST.PW_TWP','0.60',date(2022,7,1));
{? _dodano
|| __UPG.msg('Dodano informacje o procencie wynagrodzenia za terminową wpłatę podatków.')
?};

1


\KST_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Opis funkcji aktualizacji stałych systemu
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja stałych systemu.'


\uprawnienia_akcep_wn
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Informacja o zmienionej funkcjonalności związanej z uprawnieniami do akceptacji wniosków
::----------------------------------------------------------------------------------------------------------------------
_info:=exec('uprawnienia_akcep_wn_desc','upgrade_2226');
FUN.info(_info);
_result:=-1;
_can_continue:=FUN.ask('Czy wszystkie kroki zostały wykonane prawidłowo i zakończyć zadanie?');
{? _can_continue>0
|| _result:=1
?};
_result


\uprawnienia_akcep_wn_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Opis do zadania informującego o zmienionej funkcjonalności związanej z uprawnieniami do akceptacji wniosków
::----------------------------------------------------------------------------------------------------------------------
'Informacja o zmianie sposóbu wyznaczania użytkowników uprawnionych do akceptacji wniosków w obiegu.\n'
'W wersji 22.26 zmieniono sposób wyznaczania użytkowników uprawnionych do akceptacji wniosków w obiegu.\n'
'Obecnie użytkownicy akceptujący wnioski dotyczące pracownika muszą mieć uprawnienia '
'do jednostki organizacyjnej wnioskującego.'
'\n\n'
'Szczegóły w nocie N/22.26/0337.'


\procesy_personelowe
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Import procesów personelowych
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
exec('procesy','upgrade','22.26','','PER_')


\procesy_personelowe_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Import procesów personelowych - opis
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
'Pozwala zaimportować nowe procesy dziedziny PER:\n'
' ● PER_EZLA - Import danych e-ZLA z PUE ZUS\n'
' ● PER_WDA - Pow. o niezaakceptowanych wnioskach o urlop\n'


\podzial_kosztow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Rozszerzono zakres funkcjonalny mechanizmu podziału kosztów
::----------------------------------------------------------------------------------------------------------------------
_info:=exec('podzial_kosztow_desc','upgrade_2226');
FUN.info(_info);
_result:=-1;
_can_continue:=FUN.ask('Czy wszystkie kroki zostały wykonane prawidłowo i zakończyć zadanie?'@);
{? _can_continue>0
|| _result:=1
?};
_result


\podzial_kosztow_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Opis Rozszerzono zakres funkcjonalny mechanizmu podziału kosztów
::----------------------------------------------------------------------------------------------------------------------
'Rozszerzono zakres funkcjonalny mechanizmu podziału kosztów:\n'
' ● Nowy parametr 395 - Personel: stały podział kosztów (S-domyślnie), podział wg typu listy płac (L),\n'
' ● Nowy parametr 396 - Personel: miesięczny podział kosztów (M-domyślnie), podział wg listy płac (L),\n'
' ● Nowa wtyczka wdrożeniowa PPL_PLL_PODZ_KOSZT_M - Miesięczny podział kosztów,\n'
' ● Definicja wzoru stałego podziału kosztów w ramach typu listy płac,\n'
' ● Definicja miesięcznego podziału kosztów w ramach listy płac (nagłówka listy płac).\n'
'\n\n'
'Szczegóły w nocie N/22.26/0071.'


\uprawnienia_szablony
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Uprawnienia dla zestawów szablonów dokumentów
::----------------------------------------------------------------------------------------------------------------------
_info:=exec('uprawnienia_szablony_desc','upgrade_2226');
FUN.info(_info);
_result:=-1;
_can_continue:=FUN.ask('Czy wszystkie kroki zostały wykonane prawidłowo i zakończyć zadanie?'@);
{? _can_continue>0
|| _result:=1
?};
_result


\uprawnienia_szablony_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Opis Uprawnienia dla zestawów szablonów dokumentów
::----------------------------------------------------------------------------------------------------------------------
'Wprowadzono możliwość blokady prezentacji danych podlegających ochronie podczas tworzenia \n'
'wydruków z szablonów dokumentów.\n'
'\n\n'
'Szczegóły w nocie N/22.26/0097.'


\nieplatna_przerwa
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Niepłatna przerwa
::----------------------------------------------------------------------------------------------------------------------
_info:=exec('nieplatna_przerwa_desc','upgrade_2226');
FUN.info(_info);
_result:=-1;
_can_continue:=FUN.ask('Czy wszystkie kroki zostały wykonane prawidłowo i zakończyć zadanie?'@);
{? _can_continue>0
|| _result:=1
?};
_result


\nieplatna_przerwa_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Opis Niepłatna przerwa
::----------------------------------------------------------------------------------------------------------------------
'Podstawa prawna: Ustawa z dnia z dnia 26 czerwca 1974 r. Kodeks pracy\n\n'
'Rozwiązanie, które skierowane jest do klientów stosujących ustawową niepłatną przerwę w pracy, ma na celu\n'
'umożliwienie zaplanowania i rozliczenia tych przerw w pracy.\n'
'W systemie dodano dwa parametry:\n'
' ● 321 - Personel: Uwzględniać w Harmonogramach niepłatną przerwę w pracy? - decyduje on o włączeniu obsługi\n'
'niepłatnych przerw w systemie.\n'
' ● 322 - Personel: Sposób ustalania niepłatnej przerwy w pracy - decyduje o tym, na jakiej podstawie ustalana\n'
'będzie informacja o przerwie.\n'
'Dopuszczalne wartości tego parametru to:\n'
' P - przerwa na podstawie planu lub wykonania z grafików,\n'
' C - przerwa zgodna z odpowiednim zapisem z czytników.\n'
'\n\n'
'Szczegóły w nocie N/22.26/0133.'


\ud_def_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Przepina identyfikatory UD_DEF z PORTAL_KK_ID do PORTAL_SEOD_UD_DEF_ID
::----------------------------------------------------------------------------------------------------------------------
_from_str:='PORTAL_KK_ID';
_from:=null;
_from_m:='';
_to_str:='PORTAL_SEOD_UD_DEF_ID';
_to:=null;
_to_m:='';

SYNC_IDM.cntx_psh();
SYNC_IDM.index('KOD');
SYNC_IDM.prefix();
{? SYNC_IDM.find_key(_to_str,'','UD_DEF','ud_def',)
|| _to:=SYNC_IDM.ref();
   _to_m:=SYNC_IDM.MASKA_ID
?};
{? SYNC_IDM.find_key(_from_str,'','KK','ud_def',)
|| _from:=SYNC_IDM.ref();
   _from_m:=SYNC_IDM.MASKA_ID
?};
{? _from & ~_to
|| {!
   |? _maskaid:=exec('maska_id','#sync_id');
      SYNC_IDM.cntx_psh();
      SYNC_IDM.index('MASKA_ID');
      SYNC_IDM.prefix(_maskaid,);
      _jest:=SYNC_IDM.first();
      SYNC_IDM.cntx_pop();
      _jest
   !};
   SYNC_IDM.MASKA_ID:=_maskaid;
   SYNC_IDM.KOD:=_to_str;
   SYNC_IDM.TABELA:='UD_DEF';
   {? SYNC_IDM.add()
   || _to:=SYNC_IDM.ref()
   ?}
?};
{? _from & +_from_m=8 & _to & +_to_m=8
|| SYNC_ID.cntx_psh();
   SYNC_ID.use(_from_m);
   SYNC_ID.prefix();
   params_set('ref',_to,'maska',_to_m,'kod',_to_str);
   SYNC_ID.for_each("
      {? +SYNC_ID.REK=48 & ref_tab(SYNC_ID.REK)=UD_DEF
      || _to:=params_get().ref;
         _to_m:=params_get().maska;
         _to_str:=params_get().kod;
         SYNC_ID.cntx_psh();
         SYNC_ID.use(_to_m);
         SYNC_ID.index('UNIK');
         SYNC_ID.prefix();
         {? ~SYNC_ID.find_key(SYNC_ID.REK,SYNC_ID.OPIS,SYNC_ID.ID,)
         || SYNC_ID.KOD:=_to_str;
            _add:=SYNC_ID.add()
         || _add:=1
         ?};
         SYNC_ID.cntx_pop();
         {? _add
         || SYNC_ID.del()
         ?}
      ?}
   ");
   SYNC_ID.cntx_pop()
?};
SYNC_IDM.cntx_pop();
{? _from
|| __UPG.msg('Przeniesiono identyfikatory')
|| __UPG.msg('Brak identyfikatorów do przeniesienia')
?};
1


\ud_def_id_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Przepina identyfikatory UD_DEF do PORTAL_SEOD_UD_DEF_ID - opis
::----------------------------------------------------------------------------------------------------------------------
'Przeniesienie identyfikatorów rekordów portalu z PORTAL_KK_ID do PORTAL_SEOD_UD_DEF_ID '
'dla tabeli UD_DEF'


\h_overlap
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: kod - ER/WRT/XP/21.37/2208/0006 - Miesce pracy - zmiana stanowiska -
::             zmian daty miejsca pracy daje datę 0001/01/01
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? FUN.ask(
      'W zadaniu przeanalizowane zostaną zapisy tabeli H\n'
      'na pod kątem błędnej daty OD oraz nakładających się okresów.\n\n'
      'Czy chcesz kontynuować?'@
   )
||
   exec('KOMM','#object');
   KOMM.init();

   FIRMA.cntx_psh();
   F_ZATR.cntx_psh();
   OSOBA.cntx_psh();
   P.cntx_psh();
   P.index('FOP_ZATR');
   P.prefix();
   {? P.first()
   || H.cntx_psh();
      H.use('_hist');
      H.index('_HISTDAT');
      FUN.prg_start(P.size(),'Analiza błędnych zapisów...',5,,1);
      {!
      |? H.prefix(P.ref());
         {? H.first()
         || {!
            |?  _res:=exec('check','overlap',H.ref(),H,'OD','DO',2,,,'HISTUM',H.UMOWA);
               {? type_of('_res')=type_of('') & _res<>''
               || _txt:='Błędne zapisy w tabeli "Miejsca pracy" dla współpracownika';
                  {? ~KOMM.find_msg(_txt) || KOMM.sect_beg(_txt) ?};
                  _txt:='%3 [Firma: %1, Forma zatr.: %2]'
                        [P.FIRMA().SYMBOL,P.F_ZATR().KOD,exec('P','#to_string')];
                  {? ~KOMM.find_msg('%1 - nakładające się okresy'[_txt]) &
                     ~KOMM.find_msg('%1 - błędna data OD: %2'[_txt,$H.OD])
                  || KOMM.add('%1 - nakładające się okresy'[_txt])
                  ?}
               ?};
               {? H.OD<date(1900,1,1)
               || _txt:='%3 [Firma: %1, Forma zatr.: %2]'
                        [P.FIRMA().SYMBOL,P.F_ZATR().KOD,exec('P','#to_string')];
                  {? ~KOMM.find_msg('%1 - nakładające się okresy'[_txt]) &
                     ~KOMM.find_msg('%1 - błędna data OD: %2'[_txt,$H.OD])
                  || KOMM.add('%1 - błędna data OD: %2'[_txt,$H.OD])
                  ?}
               ?};
               H.next()
            !}
         ?};
         FUN.prg_next();
         P.next()
      !};
      FUN.prg_stop();
      H.cntx_pop()
   ?};
   P.cntx_pop();
   OSOBA.cntx_pop();
   F_ZATR.cntx_pop();
   FIRMA.cntx_pop();

   {? ~KOMM.empty() || KOMM.select('Błędne zapisy',,,1) ?};

   _funInfo:={? KOMM.empty() || 'Brak miejsc pracy spełniających warunki.\n' || '' ?};
   {? FUN.ask('%1Czy oznaczyć zadanie: \'%2\' jako wykonane?'@[_funInfo,'h_overlap'])
   || __UPG.msg('Poprawiono błędne daty oraz nakładające się okresy.');
      _result:=1
   || _result:=-1
   ?}
|| _result:=-1
?};

_result


\h_overlap_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [21.37]
:: OPIS: kod - ER/WRT/XP/21.37/2208/0006 - Miesce pracy - zmiana stanowiska -
::             zmian daty miejsca pracy daje datę 0001/01/01
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/21.37/2208/0006 - Miejsca pracy - błędne daty oraz nakładające się okresy'


\por_slowniki
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Aktualizacja słowników portalowych.
::       ER/WRT/XP/22.26/2204/0001 - ER/WRT/XP/21.37/2202/0060 : ER/WRT/XP/12.51/2202/0026 : Wniosek o zatrudnienie -
::       stanowisko nie jest przypisane do wybranej jednostki organizacyjnej
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
{? FUN.ask(
      'Przed naniesieniem poprawki należy zakończyć obieg wszystkich aktywnie przetwarzanych w ramach Portalu HR '
      'wniosków związanych z zatrudnieniem ze względu na zmianę pola na wniosku oraz sposobu weryfikacji '
      'poprawności wprowadzonych danych:\n'
      '- Wnioski o zatrudnienie\n'
      '- Wnioski o zmianę zatrudnienia\n'
      '- Wnioski o przedłużenie lub rozwiązanie umowy\n'
      '- Wnioski o rekrutację\n\n'
      'Czy chcesz kontynuować?'@
   )
|| PORSLO.trig_off('*','*');
   PORSLO.cntx_psh();
   PORSLO.f_set(,,'PORSLO.SYSTEM=\'T\' and PORSLO.FIELD=\'DzialNazwa\' and PORSLO.TYPE=\'Request\'');
   {? PORSLO.f_first()
   || {!
      |? PORSLO.TYPE:='RequestPersonDep_Symb';
         {? ~PORSLO.put()
         || _result:=-1
         ?};

         _result<>-1 & PORSLO.f_next()
      !}
   ?};
   PORSLO.f_clear();
   {? _result<>-1
   || PORSLO.f_set(,,'PORSLO.SYSTEM=\'N\' and PORSLO.FIELD=\'DzialNazwa\' and PORSLO.TYPE=\'Request\'');
      {? PORSLO.f_first()
      || {? FUN.ask('Znaleziono nieprawidłowe niesystemowe słowniki portalowe o nazwie "DzialNazwa".\n'
                 'Czy zmienić widok na "RequestPersonDep_Symb" w znalezionych słownikach?'@)
         || {!
            |? PORSLO.TYPE:='RequestPersonDep_Symb';
               {? ~PORSLO.put()
               || _result:=-1
               ?};

               _result<>-1 & PORSLO.f_next()
            !}
         || {? FUN.ask('Czy przerwać zadanie?'@)
            || _result:=-1
            ?};
            __UPG.msg('Znaleziono nieprawidłowe niesystemowe słowniki portalowe o nazwie "DzialNazwa".')
         ?}
      ?};
      PORSLO.f_clear()
   ?};
   PORSLO.cntx_pop();
   PORSLO.trig_on('*','*');
   {? _result<>-1
   || exec('import_init','#excel_imex',spli_str('ZWS_PAR_PTYP,ZWS_PAR_PSLO',','));
      {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['por_slownik'])
      || __UPG.msg('Zaktualizowano słowniki portalowe.');
         _result:=1
      || _result:=-1
      ?}
   ?}
|| _result:=-1
?};

_result


\por_slowniki_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Aktualizacja słowników portalowych - opis.
::       ER/WRT/XP/22.26/2204/0001 - ER/WRT/XP/21.37/2202/0060 : ER/WRT/XP/12.51/2202/0026 : Wniosek o zatrudnienie -
::       stanowisko nie jest przypisane do wybranej jednostki organizacyjnej
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słowników portalowych.\n'
'ER/WRT/XP/22.26/2204/0001'


\sch_form_portal_nwu_access
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Utworzenie formuły portal_nwu_access w tabeli SCH_FORM.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
:: Ewentualne zmiany nanieść również w \def_wu_ogolny/etypy.fml.
::
:: Formuła walidacyjna użytkownika akceptującego (przełożonego).
_typ:='E';
_system:='T';
_skrot:='portal_nwu_access';
_nazwa:='HR Portal: Wyszukanie przełożonego (akceptanta)';
_tresc:=''+"exec('przelozony','form_ob',,,,'AKCWNIO')";

_ret:=1;
{? exec('sch_form_exists','sch_imp',_typ,_skrot)=null()
|| {? exec('sch_form_add','sch_imp',_typ,_system,_skrot,_nazwa,_tresc)
   || __UPG.msg('Utworzenie formuły "%1" powiodło się.'[_skrot])
   || __UPG.msg('Utworzenie formuły "%1" NIE powiodło się.'[_skrot]);
      _ret:=-1
   ?}
|| __UPG.msg('Utworzzenie formuły "%1" nie było wymagane - formuła już istniała.'[_skrot])
?};
_ret


\sch_form_portal_nwu_access_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Utworzenie formuły portal_nwu_access w tabeli SCH_FORM - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Utworzenie formuły portal_nwu_access w tabeli SCH_FORM.'


\atrybut_pakiet_mob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: ER/WRT/XP/12.51/2207/0014 - Brak naliczania kosztów po aktualizacji Pakiet mobilności przy duzym udziale 50%KU,
::       naliczanie składników 7199 oraz 7195 (Pakiet mobilności)
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('__RUB','object');
__RUB.fill();

:: Warunek na istnienie atrybutu
{? __RUB.find_def('S',901)
|| exec('add_attr','rubatr',901,90106,'Składniki niestanowiące przychodu do proporcji',1,
        'Składniki, które wchodzą do postawy opodatkowania ale nie wchodzą do podstawy ubezpieczeń i nie są zasiłkami. '
        'Składniki te służą do pomniejszenia przychodu używanego do proporcjonalnego wyliczenia '
        'składek niestanowiących obniżenia podstawy opodatkowania.',,655,771,779,7093,7109);
   __UPG.msg('Zakończono aktualizację atrybutów składników płacowych.')
|| __UPG.msg('Aktualizacja atrybutów składników płacowych zakończyła się niepowodzeniem - brak atrybutu 901.');
   _result:=-1
?};

_result


\atrybut_pakiet_mob_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: ER/WRT/XP/12.51/2207/0014 - Brak naliczania kosztów po aktualizacji Pakiet mobilności przy duzym udziale 50%KU,
::       naliczanie składników 7199 oraz 7195 (Pakiet mobilności) - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie atrybutu płacowego (Pakiet mobilności).'


\pkalsync_resend
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2208/0044 - Nie aktualizują się wpisy w Kalendarzu/Szablonie/Harmonogramie
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_sql:=''+"select distinct PKALSYNC from @PKALSYND where PKALSYND.SEND=1";
_TAB:=sql(_sql);
{? _TAB.first()
|| PKALSYNC.cntx_psh();
   PKALSYNC.index('PRAC');
   PKALSYNC.prefix();
   {!
   |? {? PKALSYNC.seek(_TAB.PKALSYNC)
      || PKALSYNC.put(,1)
      ?};
      _TAB.next()
   !};
   PKALSYNC.cntx_pop()
?};
__UPG.msg('Zakończono aktualizację wybranych kalndarzy w celu ponownego wysłania ich na HR Portal.');

_result


\pkalsync_resend_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2208/0044 - Nie aktualizują się wpisy w Kalendarzu/Szablonie/Harmonogramie - opis.
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja wybranych kalndarzy w celu ponownego wysłania ich na HR Portal.'


\placa_oddelegowanie
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: ER/WRT/XP/22.26/2209/0008 - Brak informacji o przepracowanych godzinach na liście płac
::       Lista pracowników, którym należy przenieść wynagrodzenie do sekcji "Płaca oddelegowanie".
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_zad:='placa_oddelegowanie';
{? PAR_SKID.get(261)='N'
|| __UPG.msg('Brak włączonych oddelegowań w systemie.'@);
   return(1)
?};
H.cntx_psh();
H.use('_hist');
H_ODDE.cntx_psh();
H_ODDE.use('h_oddet');
_query:='
   select OSOBA.NAZWISKO as "Nazwisko", OSOBA.PIERWSZE as "Imię",
          P.T as "Numer teczki", P.ZA as "Zatrudniony?", UD_SKL.SYMBOL as "Jednostka", CP.CP as "Charakter pracy",
          H_UM.OD as "Początek obowiązywania (umowa)", H_UM.DO as "Koniec obowiązywania (umowa)",
          H_UM.NUMER as "Numer umowy", case
                                          when H_UM.DO is null or H_UM.DO>=to_date(:_a)
                                             then \'T\'
                                             else \'N\'
                                       end as "Aktywna umowa?",
          H.ODDEL as "Oddelegowanie", H.OD as "Data rozpoczęcia (przebieg)", H.DO as "Data zakończenia (przebieg)",
          H.S1 as "Kwota (płaca zasadnicza/stawka godzinowa)", H.S4 as "Kwota (płaca oddelegowanie)",
          H.REFERENCE as REFERENCE,
          case
             when (select count(*) from H_ODDE where H_ODDE.H=H.REFERENCE and (H_ODDE.POD=\'N\' or H_ODDE.ZUS=\'N\'))>0
                then \'N\'
                else \'T\'
          end as "Podatek i ZUS w Polsce?"
   from H
   join H_UM using(H.UMOWA, H_UM.REFERENCE)
   join P using(H.P, P.REFERENCE)
   join OSOBA using(P.OSOBA, OSOBA.REFERENCE)
   join CP using(H.CP, CP.REFERENCE)
   join UD_SKL using(H.WYDZIAL, UD_SKL.REFERENCE)
   where H.ODDEL=\'T\' and H.S4=0 and H.S1>0 and (H.DO is null or H.DO>=to_date(:_b))
   order by 1
   ';
_LIST:=sql(_query,date(),date(,1,1));
H_ODDE.cntx_pop();
H.cntx_pop();
{? _LIST.first()
|| _info:=
      'Zostanie wyświetlona lista pracowników oddelegowanych oraz ich przebiegów, w których należy przenieść '
      'wynagrodzenie za okres oddelegowania z pól w sekcji "Płaca zasadnicza" do pól w sekcji "Płaca oddelegowanie".\n'
      'Operacja musi być wykonana dla oddelegowań z podatkiem lub ubezpieczeniem opłacanym za granicą (rekordy '
      'wyróżnione kolorem czerwonym).\nNiewykonanie zadania spowoduje zablokowanie obliczenia/przeliczenia list '
      'płac dla wyróżnionych pracowników.\n\n'
      'Czy kontynuować?'@;
   {? ~FUN.ask(_info)
   || return(-1)
   ?}
|| __UPG.msg('Brak pracowników spełniających warunki.'@);
   {? FUN.ask('Brak pracowników spełniających warunki.\nCzy oznaczyć zadanie: \'%1\' jako wykonane?'@[_zad])
   || _result:=1
   || _result:=-1
   ?};
   return(_result)
?};

:: Wyświetlanie tabeli:
_tytul:='Lista pracowników, którym należy przenieść wynagrodzenie do sekcji "Płaca oddelegowanie"'@;
_ws:=_LIST.mk_sel(_tytul,,,'#placa_oddeleg',,,,,'U');
_LIST.win_fld(_ws,,'NAZWISKO',,,25,,,'Nazwisko'@);
_LIST.win_fld(_ws,,'IMIĘ',,,20,,,'Imię'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(3),,,MS.fld_len(P,'T'),,,'Numer teczki'@,,'Numer teczki'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(5),,,MS.fld_len(UD_SKL,'SYMBOL'),,,'Jednostka'@,,'Jednostka organizacyjna'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(6),,,MS.fld_len(CP,'CP'),,,'Charakter pracy'@,,'Charakter pracy'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(12),,,-10,,,'Data rozpoczęcia (przebieg)'@,,'Data rozpoczęcia (przebieg)'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(13),,,-10,,,'Data zakończenia (przebieg)'@,,'Data zakończenia (przebieg)'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(17),,,-3,,,'Podatek i ZUS w Polsce?'@,,'Podatek i ZUS tylko w Polsce?'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(4),,,-3,,,'Zatrudniony?'@,,'Pracownik zatrudniony?'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(10),,,-3,,,'Aktywna umowa?'@,,'Aktywna umowa (dotyczy niezakończonej umowy)?'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(9),,,MS.fld_len(H_UM,'NUMER'),,,'Numer umowy'@,,'Numer umowy'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(7),,,-10,,,'Początek obowiązywania (umowa)'@,,'Początek obowiązywania (umowa)'@);
_LIST.win_fld(_ws,,_LIST.fld_acr(8),,,-10,,,'Koniec obowiązywania (umowa)'@,,'Koniec obowiązywania (umowa)'@);
_LIST.win_act(_ws,0,'Kolejność');
_LIST.win_act(_ws,0,'Rekord',,,,"{? cur_tab().PODATEK='N' || '255:0:0' ?}");
_LIST.win_sel(_ws);
_LIST.select();

:: Eksport do pliku:
{? FUN.ask('Czy wyeksportować dane do pliku?'@)
|| _folder:=fmk_tmp_dir(0).get_path();
   _plik:=_folder+'\\placa_oddelegowanie.csv';
   _exp:=_LIST.export(
            _plik,,,'UTF-8,header,nopth',,
            'NAZWISKO',,1,,
            'IMIĘ',,2,,
            _LIST.fld_acr(3),,3,,
            _LIST.fld_acr(5),,4,,
            _LIST.fld_acr(6),,5,,
            _LIST.fld_acr(12),,6,1,
            _LIST.fld_acr(13),,7,1,
            _LIST.fld_acr(17),,8,,
            _LIST.fld_acr(4),,9,,
            _LIST.fld_acr(10),,10,,
            _LIST.fld_acr(9),,11,,
            _LIST.fld_acr(7),,12,1,
            _LIST.fld_acr(8),,13,1
            );
   {? _exp
   || _zapis:=dlg_save(_plik);
      {? type_of(_zapis)=type_of('')
      || {? +_zapis
         || _eksp:='Wyeksportowano do: %1'@[_zapis]
         || _eksp:='Użytkownik zrezygnował z zapisania pliku'@
         ?}
      || _eksp:='Błąd podczas zapisywania pliku'@
      ?}
   || _eksp:='Eksport do pliku zakończył się niepowodzeniem.'@
   ?};
   __UPG.msg(_eksp);
   FUN.info(_eksp)
?};
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[_zad])
|| __UPG.msg('Wyświetlono: %1'@[_tytul]);
   _result:=1
|| _result:=-1
?};

_result


\placa_oddelegowanie_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: ER/WRT/XP/22.26/2209/0008 - Brak informacji o przepracowanych godzinach na liście płac - opis.
::----------------------------------------------------------------------------------------------------------------------
'Lista pracowników, którym należy przenieść wynagrodzenie do sekcji "Płaca oddelegowanie"'


\slo_naz_miejscpr
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2208/0069 - Problem z encją: chr_WorkPlaceModify_json - brak możliwości
::       modyfikowania słownika z miejscami pracy
::----------------------------------------------------------------------------------------------------------------------
exec('KOMM','#object');
KOMM.init();

:: Środowisko wywołania
_env:=exec('env','sync_mwa');
_env.LogLevel:=-1;

::wyszukujemy rekord SYNC_MWA
_sql:=''+"select REFERENCE as REF from SYNC_MWA where SYNC_MWA.METHOD='chr_WorkPlaceDelete'";
_TAB:=sql(_sql);

:: tabela do zapamiętania usuniętych uidref-ów
_UIDS:=tab_tmp(1, 'UIDREF','STRING[48]','');


SYNC_MWA.cntx_psh();
SYNC_PD.cntx_psh();
{? _TAB.first() & SYNC_MWA.seek(_TAB.REF,,1)
|| _env.MethodName:=SYNC_MWA.METHOD;
   _env.TabAcr:=SYNC_MWA.TAB_ACR;
   _sync_pd:=SYNC_MWA.SYNC_PD().SYM;
   _tab:=($(SYNC_MWA.TAB_ACR))();

   params_set('env',_env,'tab',_tab,'sync_pd',_sync_pd,'UIDS',_UIDS);
   _formula:="
      params_set(params_get());
      _UIDS:=params_get().UIDS;
      _env:=params_get().env;
      _tab:=params_get().tab;
      _sync_pd:=params_get().sync_pd;
      _uidref:=_tab.uidref();
      _env.Param:=_uidref;
      _res:=exec('run','sync_mwa');
      {? var_pres('_res')=type_of(0) & _res>0
      || _UIDS.UIDREF:=_uidref;
         _UIDS.add(1)
      ?};
      ~~
   ";
   params_exec('for_each','#table',_tab,_formula);

:: a teraz raz jeszcze put-ujemy wcześniej usunięte rekordy w celu ponownej wysyłki
   _formula:="
      _UIDS:=params_get().UIDS;
      SLO_NAZ.cntx_psh();
      {? SLO_NAZ.seek(_UIDS.UIDREF,,1)
      || SLO_NAZ.put(,1)
      ?};
      SLO_NAZ.cntx_pop()
   ";
   params_exec('for_each','#table',_UIDS,_formula)
?};
SYNC_MWA.cntx_pop();
SYNC_PD.cntx_pop();

_size:=_UIDS.size();
{? _size>0
|| __UPG.msg('Usunięto %1 %2 tabeli SLO_NAZ i oznaczono je do ponownego wysłania na Portal HR (encja chr_WorkPlace).'
             [$_size,{? _size=1 || 'rekord' |? _size<5 || 'rekordy' || 'rekordów' ?}])
|| __UPG.msg('Brak rekordów do ponownego wysłania na HR Portal.')
?};
1


\slo_naz_miejscpr_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2208/0069 - Problem z encją: chr_WorkPlaceModify_json - brak możliwości
::       modyfikowania słownika z miejscami pracy - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2208/0069 - Problem z encją: chr_WorkPlaceModify_json - brak możliwości '
'modyfikowania słownika z miejscami pracy'


\archiwum_del_test
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS: Usunięcie zbędnej formuły
::----------------------------------------------------------------------------------------------------------------------
ARCH_UST.cntx_psh();
ARCH_UST.index('UNIQ');
ARCH_UST.prefix(exec('ref_firma','#firma'),'STN','ZAL',);
_zmian:=0;
_test:='exec(\'test\',\'test\')';
{? ARCH_UST.first()
|| {!
   |? {? |ARCH_UST.FORM=_test
      || ARCH_UST.FORM:='';
         _zmian+=ARCH_UST.put(1)
      ?};
      ARCH_UST.next()
   !}
?};
ARCH_UST.cntx_pop();
{? _zmian
|| __UPG.msg('Usunięto formułę "%1"'[_test])
|| __UPG.msg('Brak rekordów do zmiany.')
?};
1


\archiwum_del_test_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MicKoc [22.26]
:: OPIS:  Usunięcie zbędnej formuły - Opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie zbędnej formuły w warunku ustawień archiwizacji załączników.'


\portalk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Aktualizacja znakcznika PORTALK.ENABLED
::----------------------------------------------------------------------------------------------------------------------
_wym:=_akt:=0;
ETYP_ATR.cntx_psh();
ETYP_ATR.index('ID_WP');
PORTALK.cntx_psh();
PORTALK.index('FLD');
ETYPY.cntx_psh();
ETYPY.index('GRUPA_WP');
ETYPY.prefix('S');
{? ETYPY.first()
|| {!
   |? ETYP_ATR.prefix(ETYPY.ref());
      {? ETYP_ATR.first()
      || {!
         |? PORTALK.prefix(ETYP_ATR.TAT().ID_WP,);
            {? PORTALK.first() & PORTALK.ENABLED<>'T'
            || _wym+=1;
               PORTALK.ENABLED:='T';
               _akt+=PORTALK.put()
            ?};
            ETYP_ATR.next()
         !}
      ?};
      ETYPY.next()
   !}
?};
ETYPY.cntx_pop();
PORTALK.cntx_pop();
ETYP_ATR.cntx_pop();
__UPG.msg('Liczna rekordów do aktualizacji: %1'[$_wym]);
__UPG.msg('Liczna rekordów zaktualizowanych: %1'[$_akt]);
1


\portalk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Aktualizacja znakcznika PORTALK.ENABLED - opis
::----------------------------------------------------------------------------------------------------------------------
'Włączenie dodatkowych pól na fakturze SEOD.\n'
'Aktualizacja pola PORTALK.ENABLED.'


\portal_bnftt_nag_id
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Poprawka błędu ER/WRT/XP/22.26/2208/0073 - Benefity - synchronizacja załączników
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
SYNC_IDD.cntx_psh();
SYNC_IDD.index('SYSTEM');
SYNC_IDD.prefix('PORTAL','PORTAL_BNFTT_NAG_ID',);
{? SYNC_IDD.first()
|| SYNC_IDM.cntx_psh();
   SYNC_IDM.index('KOD');
   SYNC_IDM.prefix(SYNC_IDD.KOD,);
   {? SYNC_IDM.first()
   || _stat:=obj_new('poprawnych','niepoprawnych','nieznalezionych');
      {! _lp:=1 .. obj_len(_stat)
      |! _stat[_lp]:=0
      !};

      BNFTT.cntx_psh();
      SYNC_ID.cntx_psh();
      {!
      |? SYNC_ID.use(SYNC_IDM.MASKA_ID);
         SYNC_ID.index('REK');
         SYNC_ID.prefix();
         {? SYNC_ID.first()
         || {!
            |? {? BNFTT.seek(SYNC_ID.REK,)
               || {? SYNC_ID.OPIS=BNFTT.HASH
                  || _stat.poprawnych+=1;
                     SYNC_ID.next()
                  || _stat.niepoprawnych+=1;
                     SYNC_ID.del(,1)=2
                  ?}
               || _stat.nieznalezionych+=1;
                  SYNC_ID.del(,1)=2
               ?}
            !}
         ?};
         SYNC_IDM.next()
      !};
      SYNC_ID.cntx_pop();
      BNFTT.cntx_pop();

      __UPG.msg('Zapisów poprawnych: %1'[$_stat.poprawnych]);
      __UPG.msg('Zapisów niepoprawnych: %1'[$_stat.niepoprawnych]);
      __UPG.msg('Nieznalezionych benefitów (usuniętych identyfikatorów): %1'[$_stat.nieznalezionych])

   || __UPG.msg('Brak danych do weryfikacji [SYNC_IDM].')
   ?};
   SYNC_IDM.cntx_pop()
|| __UPG.msg('Brak danych do weryfikacji [SYNC_IDD].')
?};
SYNC_IDD.cntx_pop();
_ret


\portal_bnftt_nag_id_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS:
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie zbędnych identyfikatorów rekordów dla kodu PORTAL_BNFTT_NAG_ID'


\platnosci_US
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [22.26]
:: OPIS: Import słowników użytkownika. Poprawka SPIS ER/WRT/XP/22.26/2209/0052.
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',spli_str('ZWS_PAR_KSLO,',','));
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['platnosci_US'])
|| __UPG.msg('Zaktualizowano zawartość słownika Typy płatności do US .');
   _result:=1
|| _result:=-1
?};
_result


\platnosci_US_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [22.26]
:: OPIS: Import słowników użytkownika - opis. Poprawka SPIS ER/WRT/XP/22.26/2209/0052.
::   WE:
::   WY: Opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import zawartości słownika Typy płatności do US w poprawka SPIS ER/WRT/XP/22.26/2209/0052.'


\sync_def_ud_def
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Poprawka definicji wymiany danych tabeli UD_DEF dla przeznaczenia PORTAL.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
::exec('zmiany','#syslog',SYNC_DEF)
_ret:=1;
SYNC_DEF.cntx_psh();
SYNC_DEF.index('PDNR1');
SYNC_DEF.prefix('PORTAL','UD_DEF',1);
{? SYNC_DEF.first()
|| _old:=''+"UD_DEF.UD_SCH=exec('domyslny','schemat','PODZORG',0)";
   _new:=''+"UD_DEF.UD_SCH=exec('domyslny','schemat','PODZORG',0,1)";
   {? SYNC_DEF.WAR_FORM=_new
   || __UPG.msg('Poprawka była już zastosowana.')
   |? SYNC_DEF.WAR_FORM=_old
   || SYNC_DEF.WAR_FORM:=_new;
      {? SYNC_DEF.put()
      || __UPG.msg('Zastosowanie poprawki zakończyło się sukcesem.')
      || __UPG.msg('Zastosowanie poprawki nie powiodło się.');
         _ret:=-1
      ?}
   ?}
|| __UPG.msg('Brak odpowiedniej definicji = brak danych do poprawiania.')
?};
SYNC_DEF.cntx_pop();
_ret


\sync_def_ud_def_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: Poprawka definicji wymiany danych tabeli UD_DEF dla przeznaczenia PORTAL - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka definicji wymiany danych tabeli UD_DEF dla przeznaczenia PORTAL'


\fiks_86
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/2210/0023 - schemat e-deklaracji CIT8wer.31
::----------------------------------------------------------------------------------------------------------------------
_res:=-1;
VAT_VER.cntx_psh();
VAT_VER.index('VER_NR'); VAT_VER.prefix('FKS','CIT8','CIT8',31);
_ver:={? VAT_VER.first() || VAT_VER.ref() || null ?};
VAT_VER.cntx_pop();
{? _ver
|| ISTDEF.cntx_psh();
   ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','CIT8',_ver);
   {? ISTDEF.first()
   || _res:=0;
      _fml:="
         _tag:=_a;
         _reg:=_b;
         _wym:=_c;
         _res:=0;
         ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
         {? ISTDEFS.find_key(_tag,)
         || {? ISTDEFS.REGULY<>_reg | ISTDEFS.WYM<>_wym
            || ISTDEFS.REGULY:=_reg;
               ISTDEFS.WYM:=_wym;
               _res:=ISTDEFS.put()
            ?}
         ?};
         ISTDEFS.cntx_pop();
         _res
      ";
      _res+=_fml('P_315',$"{? exec('poz_edek_dod','xml','CIT8 ',1,316)<>'' || '1' || '2' ?}",'N');
      _res+=_fml('P_316',$"exec('poz_edek_dod','xml','CIT8 ',1,316)",'N')
   ?};
   ISTDEF.cntx_pop()
?};
{? _res=-1
|| __UPG.msg('Nie znaleziono schematu e-deklaracji CIT-8 (31).')
|? _res=0
|| __UPG.msg('Zmiany w schemacie e-deklaracji CIT-8 (31) nie były wymagane.')
|| __UPG.msg('Poprawiono schemat e-deklaracji CIT-8 (31).')
?};
1


\fiks_86_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [12.51]
:: OPIS: ER/WRT/XP/12.51/2210/0023 - schemat e-deklaracji CIT8wer.31 - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/12.51/2210/0023\n'
'Naprawa obsługi pola P_316 w schemacie e-deklatacji CIT-8 (31)'


\portal_url_dod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0055 - Brak możliwości złożenia wniosku o Urlop dodatkowy.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na Portal HR. Zmiany nie są wymagane.');
   return(1)
?};


KART_URL.cntx_psh();
KART_URL.index('ROK_PRAC');
KART_URL.prefix(2022);
{? KART_URL.first()
|| KART_URL.trig_off('*','*');
   P.cntx_psh();
   P.prefix();
   _d0:=date(0,0,0);
   _stat:=obj_new('przeanalizowanych','do_zmiany','zmienionych','niezmienionych');
   {! _lp:=1 .. obj_len(_stat)
   |! _stat[_lp]:=0
   !};
   {!
   |? _stat.przeanalizowanych+=1;
      {? KART_URL.NSP_ZAL=0
         & (KART_URL.DATA_NSP<>_d0 & KART_URL.URL_NSP<>0 & KART_URL.URL_NSPG<>0)
         & KART_URL.P().PORTAL='T'
::       Warunek typuje rekordy, które wceśniej nie zostały wysłane na portal, a być może powinny.
      || _stat.do_zmiany+=1;
         {? KART_URL.put(,1)
         || _stat.zmienionych+=1
         || _stat.niezmienionych+=1
         ?}
      ?};
      KART_URL.next()
   !};
   __UPG.msg('Liczba rekordów:');
   __UPG.msg(' - przeanalizowanych: %1'[$_stat.przeanalizowanych]);
   __UPG.msg(' - wymagających zmiany: %1'[$_stat.do_zmiany]);
   __UPG.msg(' - zmienionych: %1'[$_stat.zmienionych]);
   __UPG.msg(' - niezmienionych: %1'[$_stat.niezmienionych]);
   {? _stat.niezmienionych>0
   || _ret:=-1
   ?};
   P.cntx_pop();
   KART_URL.trig_on('*','*')
|| __UPG.msg('Brak kart urlopowych na rok 2022. Zmiany nie są wymagane.')
?};
KART_URL.cntx_pop();
_ret


\portal_url_dod_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0055 - Brak możliwości złożenia wniosku o Urlop dodatkowy - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2210/0055\n'
'Brak możliwości złożenia wniosku o Urlop dodatkowy.'


\fix_wn_seod
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0005 - Brak możliwości poprawy wniosku SEOD w PORTALU HR po odrzuceniu do rejestracji
::----------------------------------------------------------------------------------------------------------------------
_sql:=sql(
   'select EDOKUM.REFERENCE as REF '
   'from @EDOKUM join ETYPY '
   'where ETYPY.W_PORTAL=\'s\' and EDOKUM.STATUS=\'P\''
);
_put:=0;
{? _sql.first()
|| EDOKUM.cntx_psh();
   {!
   |? EDOKUM.use(8+_sql.REF);
      EDOKUM.prefix();
      {? EDOKUM.seek(_sql.REF)
      || EDOKUM.put(,1);
         _put+=1
      ?};
      _sql.next()
   !};
   EDOKUM.cntx_pop()
?};
__UPG.msg('Liczba naprawionych wniosków SEOD: %1.'[$_put]);
1


\fix_wn_seod_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0005 - Brak możliwości poprawy wniosku SEOD w PORTALU HR po odrzuceniu do rejestracji
::       opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2211/0005\n'
'Brak możliwości poprawy wniosku SEOD po odrzuceniu do rejestracji'


\UPG_REAL_clean
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Oznaczenie nadmiarowych zadań manualnych jako wykonanych.
::       Poprawka do błędu: ER/WRT/XP/22.26/2211/0003 - Zdublowane zadania manualne potransferowe
::       Lista nadmiarowych zadań: paperless_dane_wzorcowe,nieobecnosci_2022,ppk_zc_wn_zus,pomoc_AK_10,kody_zus_import,
::                                 kal_def_dataw,stalesys_PPK_1942_08
::----------------------------------------------------------------------------------------------------------------------
_tasksList:='%1'[
   'paperless_dane_wzorcowe||21.37,'
   'nieobecnosci_2022||21.14,'
   'ppk_zc_wn_zus||21.14,'
   'pomoc_AK_10||21.14,'
   'kody_zus_import||21.37,'
   'kal_def_dataw||20.14,'
   'stalesys_PPK_1942_08||19.42'
];
_tasksTab:=spli_str(_tasksList,',');
_tasksSize:=obj_len(_tasksTab);
{? _tasksSize
|| {! _ind:= 1 .. _tasksSize
   |! _taskData:=spli_str(_tasksTab[_ind],'||');
      _taskSize:=obj_len(_taskData);
      {? _taskSize=2
      || {? _taskData[1]<>'' & _taskData[2]<>''
         || _sql:=sql(
               'select UPG_REAL.REFERENCE as REF, UPG_TASK.SYMBOL as SYMBOL, '
               '  UPG_TASK.SYSTEM as SYSTEM '
               'from UPG_REAL '
               '  join UPG_TASK using (UPG_REAL.UPG_TASK,UPG_TASK.REFERENCE) '
               'where UPG_REAL.FIRMA=:_a and UPG_TASK.SYMBOL=\':_b\' and UPG_TASK.AUTO=\'N\' and '
               '  UPG_REAL.SYSTEM<\':_c\' and UPG_REAL.STAN=\'N\' '
               'order by SYSTEM desc',
               exec('ref_firma','ustawienia'),_taskData[1],_taskData[2]
            );
            {? _sql.size()
            || {? _sql.first()
               || UPG_TASK.cntx_psh();
                  UPG_REAL.cntx_psh();
                  UPG_TASK.prefix();
                  UPG_REAL.prefix();
                  {!
                  |? {? UPG_REAL.seek(_sql.REF) & UPG_REAL.STAN<>'T'
                     || UPG_REAL.DATA:=date();
                        UPG_REAL.CZAS:=time();
                        UPG_REAL.RUNT:={? UPG_REAL.BEGD=date(0,0,0)
                                       || time(0,0,0)
                                       || exec('timeDiff','#datetime',UPG_REAL.BEGD,UPG_REAL.BEGT
                                           ,UPG_REAL.DATA,UPG_REAL.CZAS)
                                       ?};
                        UPG_REAL.USERS:=OPERATOR.USER;
                        UPG_REAL.STAN:='T';
                        UPG_REAL.ERROR:=0;
                        UPG_REAL.RUN:='N';

                        {? UPG_REAL.put(1) & UPG_TASK.seek(UPG_REAL.UPG_TASK)
                        || UPG_TASK.STAN:='T';
                           UPG_TASK.put(1)
                        ?}
                     ?};

                     _sql.next()
                  !};
                  UPG_REAL.cntx_pop();
                  UPG_TASK.cntx_pop()
               ?}
            ?};
            &_sql
         ?}
      ?};
      &_taskData;
      &_taskSize
   !}
?};

__UPG.msg('Oznaczono nadmiarowe zadania manualne jako wykonane.');

1


\UPG_REAL_clean_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Oznaczenie nadmiarowych zadań manualnych jako wykonanych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Oznaczenie nadmiarowych zadań manualnych jako wykonanych.'


\fix_wn_zaliczka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0077 - Wniosek o zaliczkę (POR_WOL_WWW) wprowadzony przez portal HR
::       nie przenosi rachunku bankowego pracownika do Merita do pola EDOKUM.N
::----------------------------------------------------------------------------------------------------------------------
_sql:=''+"
   select
      EZAL.REFERENCE as REF, EZAL.N from @EZAL
   join
      @EDOKUM using(EDOKUM.REFERENCE, EZAL.EDOKUM)
   join
      @ETYPY using(EDOKUM.TYP, ETYPY.REFERENCE)
   where
      EZAL.N='' and ETYPY.ID_WP='Zaliczka'
";

_TAB:=sql(_sql);

_result:=1;

{? _TAB.first()
|| _logFile:='upgrade_fix_wn_zaliczka.log';
   _l_rek:=0;
   _size:=_TAB.size();

   FUN.prg_start(_size,'Przetwarzanie błędnych zapisów ...');
   EZAL.cntx_psh();
   {!
   |? {? EZAL.seek(_TAB.REF)
      || EDOKUM.cntx_psh();
         _name:=ref_name(EZAL.EDOKUM);
         {? +_name & EDOKUM.name()<>_name
         || EDOKUM.use(_name)
         ?};
         EZAL.EDOKUM();

         {? var_pres('_res')>0 || obj_del(_res) ?};
         _res:=exec('zaliczka_walid','portal_walidacja');
         {? ~_res.ok
         || exec('write','%logger',_logFile,'zaliczka_walid/portal_walidacja.fml',
                                   'Błąd podczas walidacji EDOKUM.ref() - %1'[$EDOKUM.ref()],TKOM13.OPIS)
         ?};

         _mydo:=do_state()=0;
         {? _mydo || do() ?};
         {? _res.ok
         || {? exec('zaliczka_akcept','portal_walidacja').ok
            || EZAL.N:=EDOKUM.N;
               {? EZAL.put()
               || _l_rek+=1;
                  exec('write','%logger',_logFile,'zaliczka_akcept/portal_walidacja.fml',
                                         'Przepisano numer rachunku z wniosku: %1 do nagłówka zaliczki: %2'
                                         [$EDOKUM.ref(),$EZAL.ref()])
               ?}
         || undo()
            ?}
         ?};
         {? _mydo || end() ?};
         EDOKUM.cntx_pop()
      ?};
      FUN.prg_next();
      _TAB.next()
   !};
   EZAL.cntx_pop();
   FUN.prg_stop();
   __UPG.msg('Naprawiono %1 z %2 zapisów'@[$_l_rek,$_size]);
   _path:=pth_dir(_logFile)+exec('sep','#file',1)+_logFile;
   __UPG.msg('Informacje o przetworzonych rekordach odpisano do pliku: %1'@[_path]);
   _result:={? _l_rek=_size || 1 || -1 ?}
|| __UPG.msg('Nie znaleziono błędnych zapisów.'@)
?};
_result


\fix_wn_zaliczka_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0077 - Wniosek o zaliczkę (POR_WOL_WWW) wprowadzony przez portal HR
::       nie przenosi rachunku bankowego pracownika do Merita do pola EDOKUM.N
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2210/0077\n'
'Wniosek o zaliczkę (POR_WOL_WWW) wprowadzony przez portal HR\n'
'nie przenosi rachunku bankowego pracownika do Merita do pola EDOKUM.N'


\warlog_lopis
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0050 - zmienna dla formuł LOPIS
::       ER/WRT/XP/22.26/2303/0053 - LKONTO1
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
WARLOG.cntx_psh();
WARLOG.index('KKOD');
WARLOG.prefix();
{? WARLOG.find_key('K','LOPIS',) & WARLOG.FO*'PAR_SKID.get(300)'
|| WARLOG.FO:=STR.gsub(WARLOG.FO,' | PAR_SKID.get(300)=\'N\' ',' ');
   {? WARLOG.put()
   || __UPG.msg('Zmodyfikowano treść formuły stałej LOPIS.'@); _result:=1
   || __UPG.msg('Nie powiodła się modyfikacja treść formuły stałej LOPIS.'@)
   ?}
||  __UPG.msg('Nie stwierdzono potrzeby modyfikacji treści formuły stałej LOPIS.'@); _result:=-1
?};
{? WARLOG.find_key('K','LKONTO1',) & WARLOG.FO*'kontohis'
|| WARLOG.FO:='_ref:=exec(\'kk_hist\',\'pracownik\',LS.P,date(O.R,O.M,0)); KK.prefix(); {? _ref<>null & KK.seek(_ref)|| KK.SYM || \'\' ?}';
   {? WARLOG.put()
   || __UPG.msg('Zmodyfikowano treść formuły stałej LKONTO1.'@); _result:=1
   || __UPG.msg('Nie powiodła się modyfikacja treść formuły stałej LKONTO1.'@)
   ?}
||  __UPG.msg('Nie stwierdzono potrzeby modyfikacji treści formuły stałej LKONTO1.'@); _result:=-1
?};
WARLOG.cntx_pop();
_result


\warlog_lopis_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0050 - zmienna dla formuł LOPIS
::       opis
::----------------------------------------------------------------------------------------------------------------------
'Z treści formuły na zmienną usunięto sekwencję opartą o nieaktywny parametr 300'


\wn_praktyka
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0053 - Wniosek o praktykę - jest odrzucany z powodu nieodnalezienia opiekuna.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

_firma:=null();
_field:='OpiekunImieNazwisko';
_type:='Request';
_system:='T';

PORSLO.cntx_psh();
PORSLO.index('SYSTEM');
PORSLO.prefix(_system,_firma,_field,_type,'',);
{? PORSLO.first()
|| __UPG.msg('Słownik %1 już istniał.'[_field]);
   _porslo:=PORSLO.ref()
|? PORSLO.blank();
   PORSLO.FIRMA:=_firma;
   PORSLO.FIELD:=_field;
   PORSLO.TYPE:=_type;
   PORSLO.SYSTEM:=_system;
   PORSLO.add()
|| __UPG.msg('Utworzonie słownika %1 zakończyło się sukcesem.'[_field]);
   _porslo:=PORSLO.ref()
|| __UPG.msg('Utworzenie słownika %1 nie powiodło się.'[_field]);
   _porslo:=null()
?};
PORSLO.cntx_pop();

{? _porslo=null()
|| return(-1)
?};

ETYPY.cntx_psh();
ETYPY.index('UNIK_WP');
ETYPY.prefix(2,'T','Praktyka',);
ETYP_ATR.cntx_psh();
ETYP_ATR.index('ID_WP');
TAT.cntx_psh();
TAT.prefix();
{? ~ETYPY.first()
|| __UPG.msg('W systemie niezdefiniowano wniosku "Praktyka". Zmiany nie są wymagane.')
|? ETYP_ATR.prefix(ETYPY.ref(),'OpiekunImieNazwisko',);
   ~ETYP_ATR.first()
|| __UPG.msg(
      'Niestandardowa budowa wniosku "Praktyka" - brak pola "OpiekunImieNazwisko". '
      'Zmiany nie mogą zostać wykonane.');
   _ret:=-1
|? ETYP_ATR.TAT().PORSLO=_porslo & TAT.PORTYPE=_type
|| __UPG.msg('Atrybt nie wymagał zmiany.')
|? TAT.PORSLO:=_porslo;
   TAT.PORTYPE:=_type;

   ~TAT.put()
|| __UPG.msg('Aktualizacja atrybutu nie powiodła się.');
   _ret:=-1
|| __UPG.msg('Aktualizacja atrybutu zakończyła się sukcesem.')
?};
TAT.cntx_pop();
ETYP_ATR.cntx_pop();
ETYPY.cntx_pop();

_ret


\wn_praktyka_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0053 - Wniosek o praktykę - jest odrzucany z powodu nieodnalezienia opiekuna - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2210/0053\n'
'Wniosek o praktykę - jest odrzucany z powodu nieodnalezienia opiekuna.'


\map_r
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0028 - Kłopoty z typami / podtypami nieobecności
::      (wysyłanie nieobecności i limitów / odbieranie wniosków urlopowych).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

_tabs:=spli_str('R,N,KART_URL,DS',',');
_dl:=obj_len(_tabs);
{! _lt:=1 .. _dl
|! _TAB:=($_tabs[_lt])();
   _TAB.cntx_psh();
   _TAB.prefix();
   {? _TAB.first()
   || _TAB.trig_off('*','*');
      {? _TAB.for_each(($(_tabs[_lt]+'.put(,1)')),1)
      || __UPG.msg('Znaczniki modyfikacji rekordów tabeli %1 zostały zaktualizowane.'[_tabs[_lt]])
      ?};
      _TAB.trig_on('*','*')
   || __UPG.msg('Brak rekordów w tabeli %1. Zmiany nie są wymagane.'[_tabs[_lt]])
   ?};
   _TAB.cntx_pop();
   obj_del(_TAB)
!};

_ret


\map_r_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0028 - Kłopoty z typami / podtypami nieobecności
::      (wysyłanie nieobecności i limitów / odbieranie wniosków urlopowych) - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2211/0028\n'
'Kłopoty z typami / podtypami nieobecności\n'
'(wysyłanie nieobecności i limitów / odbieranie wniosków urlopowych)'


\chr_workinghours
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0056 - Pusta lista nieobecności pracowników podwładnych.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na Portal HR.');
   return(1)
?};

_ret:=1;

exec('init','sync_id');
__UPG.msg('Zaktualizowano definicje identyfikatorów rekordów.');

_pd:='PORTAL';
_tab:='PORSLOIT';
_fld:='uidref';

SYNC_PD.cntx_psh();
SYNC_PD.index('SYM');
SYNC_PD.prefix(_pd,);
{? ~SYNC_PD.first()
|| __UPG.msg('Brak definicji przeznaczenia danych %1 - aktualizacja nie może być wykonana.'[_pd])

|| SYNC_DEF.cntx_psh();
   SYNC_DEF.index('PD');
   SYNC_DEF.prefix(SYNC_PD.ref(),'','',);
   {? ~SYNC_DEF.first()
   || __UPG.msg('Brak definicji wymiany danych dla przeznaczenia %1 - aktualizacja nie może być wykonana.'[_pd])

   |? _nadrz:=SYNC_DEF.ref();
      SYNC_DEF.prefix(SYNC_PD.ref(),_tab,_fld,);
      SYNC_DEF.first()
   || __UPG.msg('Definicja wymiany danych dla przeznaczenia / tabeli %1/%2 już istniała.'[_pd,_tab])

   |? SYNC_DEF.blank();
      SYNC_DEF.SYNC_PD:=SYNC_PD.ref();
      SYNC_DEF.ACR_TAB:=_tab;
      SYNC_DEF.ACR_FLD:=_fld;
      SYNC_DEF.FORM:='PORSLOIT.uidref()';
      SYNC_DEF.AKT:='T';
      SYNC_DEF.REKORD:=_nadrz;
      SYNC_DEF.LP:=1;
      SYNC_DEF.TYP:='S';
      SYNC_DEF.FIR:='F';
      SYNC_DEF.WAR_FORM:=$"exec('PORSLOIT_ok','portal_slowniki',_a,_b,_c)";
      SYNC_DEF.TR_ADD:=SYNC_DEF.TR_PUT:=SYNC_DEF.TR_DEL:='T';
      SYNC_DEF.WGIDPUT:='T';
      SYNC_DEF.add()
   || __UPG.msg(
         'Utworzenie definicj wymiany danych dla przeznaczenia / tabeli %1/%2 zakończyło się sukcesem.'[_pd,_tab])
   || _ret:=-1;
      __UPG.msg('Utworzenie definicj wymiany danych dla przeznaczenia / tabeli %1/%2 nie powiodło się.'[_pd,_tab])
   ?};
   SYNC_DEF.cntx_pop()
?};
SYNC_PD.cntx_pop();

_ret


\chr_workinghours_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0056 - Pusta lista nieobecności pracowników podwładnych - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2211/0056\n'
'Pusta lista nieobecności pracowników podwładnych.'


\zs_def_put
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0019 - Synchronizacja zależności służbowych (zmiana typu domyślnego).
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
ZS_DEF.cntx_psh();
ZS_DEF.index('IDADD');
ZS_DEF.prefix();
{? ZS_DEF.first()
|| ZS_DEF.trig_off('*','*');
   ZS_DEF.for_each("ZS_DEF.put(,1)",1);
   ZS_DEF.trig_on('*','*');
   __UPG.msg('Zmieniono znaczniki czasowe modyfikacji.')
|| __UPG.msg('Brak zależności służbowych. Zmiany nie są wymagane.')
?};
ZS_DEF.cntx_pop();
1


\zs_def_put_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2211/0019 - Synchronizacja zależności służbowych (zmiana typu domyślnego) - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2211/0019\n'
'Z portalu NIE są usuwane informacje związane z wcześniej domyślnym typem zależności służbowych'


\PIT_2022_akt_2226_11
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [22.26]
:: OPIS: Dodanie nowych e-deklaracji obowiązujących w 2022 dotyczące przychodów od 01.01.2022 zmiany po 1 lipca 2022
::----------------------------------------------------------------------------------------------------------------------
_dod_ver:="
   VAT_VER.index('VER_OD'); VAT_VER.prefix(_a,_c,_c,_b,_d);
   _jest:=VAT_VER.first();
   {? ~_jest
   || _old:={? _a='FKS' || 'FIKS' |? _a='PPL' || 'KALI' || '' ?};
      VAT_VER.prefix(_old,_c,_c,_b,_d);
      _jest:=VAT_VER.first()
   ?};
   {? _jest
   || _add:=0
   || _add:=1;
      VAT_VER.blank();
      VAT_VER.SYSTEM:=_a;
      VAT_VER.OD:=_b;
      VAT_VER.NAZWA:=_c;
      VAT_VER.NR:=_d
   ?};
   {? _<6 || _f:='' ?};
   {? _<5 || _e:=0 ?};
   _put:=0;
   {? VAT_VER.NRF<>_e || VAT_VER.NRF:=_e; _put+=1 ?};
   {? VAT_VER.WIERSZ<>_f || VAT_VER.WIERSZ:=_f; _put+=1 ?};
   VAT_VER.prefix();
   {? _add || VAT_VER.add(1) |? _put || VAT_VER.put(1) ?}
";
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
{? _dod_ver('PPL',date(2022,11,9),'PIT11',14,29,',1,4,5,6,9,10,15,')
|| __UPG.msg('Dodano 14 wersja e-deklaracji PIT-11 obowiązująca od dnia 2022.11.09 dla przychodów od 2022.01.01')
|| __UPG.msg('Brak aktualizacji, 14 wersja e-deklaracji PIT-11 już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-11 wersja 14
{? exec('edek_stru','edeklar','PIT11',14,'pit-11_29_v1-1.dfg')
|| __UPG.msg('Dodano schemat 14 wersji e-deklaracji PIT-11.')
|| __UPG.msg('Brak aktualizacji, schemat 14 wersji e-deklaracji PIT-11 już istnieje.')
?};
{? _dod_ver('PPL',date(2022,8,10),'PIT8C',11,11)
|| __UPG.msg('Dodano 11 wersja e-deklaracji PIT-8C obowiązująca od dnia 2022.8.10 dla przychodów od 2022.01.01')
|| __UPG.msg('Brak aktualizacji, 11 wersja e-deklaracji PIT-8C już istnieje.')
?};
::import poprawnych struktur e-deklaracji PIT-8C wersja 11
{? exec('edek_stru','edeklar','PIT8C',11,'pit-8c-11v1-0.dfg')
|| __UPG.msg('Dodano schemat 11 wersji e-deklaracji PIT-8C.')
|| __UPG.msg('Brak aktualizacji, schemat 11 wersji e-deklaracji PIT-8C już istnieje.')
?};
BPMN.SYM_DOM:=_dom;
:: Dodanie wartości pola określającego limit odliczenia od dochodu składek członkowskich od lipca 2022 kwota 500zł.
{? var_pres('LIM_SKZW',KST)>0
|| _dodano:=exec('kst_war_add','#stalesys','KST.LIM_SKZW','500.00',date(2022,7,1));
   {? _dodano
   || __UPG.msg('Dodano informacje o limicie 500 zł odliczenia od dochodu składek członkowskich od lipca 2022.')
   ?}
?};
1


\PIT_2022_akt_2226_11_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [22.26]
:: OPIS: Dodanie nowych e-deklaracji obowiązujących w 2022 dotyczące przychodów od 01.01.2022 zmiany po 1 lipca 2022
::----------------------------------------------------------------------------------------------------------------------
'Dodanie nowych e-deklaracji PIT-11(29), PIT-8C(11) obowiązujących w 2022 dotyczące przychodów od 01.01.2022'


\jpk_fa_4
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2212/0010 - brak JPK_FA w wersji 4
::----------------------------------------------------------------------------------------------------------------------
_ret:=0;
_imp:=exec('upg_imp','xml','FKS','J','FA','JPK_FA(4)_v1-0',date(2022,4,1),'jpk_fa_4.dfg');
_txt:='Schemat plik JPK_FA (4) obowiązujący od 01.04.2022';
{? _imp=1
|| __UPG.msg(_txt+' został dodany.');
   _ret:=1
|? _imp=0
|| __UPG.msg(_txt+' już istnieje.');
   _ret:=1
|| __UPG.msg(_txt+' nie został dodany.')
?};
_ret


\jpk_fa_4_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2212/0010 - brak JPK_FA w wersji 4 - opis
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/22.26/2212/0010 - brak JPK_FA w wersji 4\n'
'Import schematu JPK_FA (4)'


\PIT_4R_akt_2226_19
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [22.26]
:: OPIS: PIT-4R(12) za rok 2022 - 3 wersja schemy xsd z 12.12.2022
::----------------------------------------------------------------------------------------------------------------------
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
::import poprawnych struktur e-deklaracji PIT-4R(12) 3 numer schemy 12
{? exec('edek_stru','edeklar','PIT4R',12,'pit-4r12_v1-3.dfg',date(2022,12,12))
|| __UPG.msg('Dodano 3 numer schematu 12 wersji e-deklaracji PIT-4R(12).')
|| __UPG.msg('Brak aktualizacji, 3 numer schematu 12 wersji e-deklaracji PIT-4R(12) już istnieje.')
?};
BPMN.SYM_DOM:=_dom;
1


\PIT_4R_akt_2226_19_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [22.26]
:: OPIS: PIT-4R(12) za rok 2022 - 3 wersja schemy xsd z 12.12.2022
::----------------------------------------------------------------------------------------------------------------------
'Dodano 3 numer schematu 12 wersji e-deklaracji PIT-4R(12) dotyczącej przychodów od 01.01.2022'


\sync_def_wv2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0072 - Możliwe pozostawienie na portalu rekordu usuniętego w ERP-ie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;

SYNC_PD.cntx_psh();
SYNC_PD.index('SYM');
SYNC_PD.prefix('PORTAL');
{? SYNC_PD.first()
|| SYNC_DEF.cntx_psh();
   SYNC_DEF.index('PDACR');
:: Statystyka:
   _stat:=obj_new('wszystkich','poprawnych','zmienionych','niezmienionych','bz');
:: Katalog przechowywania tabel lokalnych (firmowych).
   _ld:=pth_dir(':%1.mdb'['ą'*8]);
   {!
   |? SYNC_DEF.prefix(SYNC_PD.ref());
      {? SYNC_DEF.first()
      || {! _lp:=1 .. obj_len(_stat)
         |! _stat[_lp]:=0
         !};
         {!
         |? {? SYNC_DEF.ACR_TAB<>''
            || _stat.wszystkich+=1;
               _loc:={? pth_dir(':%1.mdb'[gsub(($SYNC_DEF.ACR_TAB)().name(1),'?','ą')])=_ld || 'T' || 'N' ?};
               _wf:=SYNC_DEF.WAR_FORM;
               {? _loc='N' & SYNC_DEF.FIR='F'
               || _fir:='F'
               |? _loc='N' & SYNC_DEF.WAR_FORM*'=_c'
               || _fir:='F'
               |? _loc='T'
               || _fir:='T'
               || _fir:='?'
               ?};


               {? SYNC_DEF.ACR_TAB='B_USRROL' || _fir:='F'; _wf:=$"_a='del' | B_USRROL.FIRMA=_c"
               |? SYNC_DEF.ACR_TAB='B_ROLE' || _fir:='F'; _wf:=$"exec('B_ROLE_ok','zastepstwa',_a,_b,_c)"
               |? SYNC_DEF.ACR_TAB='ZAST_NAG' || _fir:='F'; _wf:=$"exec('ZAST_NAG_ok','zastepstwa',_a,_b,_c)"
               |? SYNC_DEF.ACR_TAB='EDOKUM'
               || {? SYNC_PD.SYM='PORTAL_SEOD'
                  || _fir:='F'; _wf:=$"_a='del' | EDOKUM.FIRMA=_c & exec('EDOKUM_typ','portal_seod','F','S')"
                  || _fir:='F'; _wf:=$"exec('EDOKUM_ok','portal_wnioski',_a,_b,_c)"
                  ?}
               |? SYNC_DEF.ACR_TAB='FIRMA' || _fir:='T'; _wf:=$"exec('FIRMA_ok','portal_seod',_a,_b,_c)"
               |? SYNC_DEF.ACR_TAB='H'
               || _fir:='F';
                  _wf:=$"H.name()='_hist' & (_a='del' | (P.cntx_psh(); _ret:=H.P().FIRMA=_c; P.cntx_pop(); _ret))"
               |? SYNC_DEF.ACR_TAB='H_UM'
               || _fir:='F';
                  _wf:=$"H_UM.name()='h_um' & (_a='del' | (P.cntx_psh(); _ret:=H_UM.P().FIRMA=_c; P.cntx_pop(); _ret))"
               |? SYNC_DEF.ACR_TAB='PORTALL' || _fir:='F'; _wf:=$"PORTALL.MAIN=0 & (_a='del' | PORTALL.FIRMA=_c)"
               |? SYNC_DEF.ACR_TAB='USERSF' || _fir:='F'; _wf:=$"exec('USERSF_ok','portal_seod',_a,_b,_c)"
               |? SYNC_DEF.ACR_TAB='USR_SIGN' || _fir:='F'
               |? SYNC_DEF.ACR_TAB='ZALACZ'
               || _wf:=$"_a='del' | ZALACZ.P=null() | (P.cntx_psh(); _ret:=ZALACZ.P().FIRMA=_c; P.cntx_pop(); _ret)"
               |? SYNC_DEF.WAR_FORM*$"_a='del'"<>1 & SYNC_DEF.WAR_FORM*'=_c'
               || {? SYNC_DEF.WAR_FORM*'%1'[%59]
::                   %59=';'
                  || _wf:='_a=\'del\' | (%1)'[SYNC_DEF.WAR_FORM]
                  || _wf:='_a=\'del\' | %1'[SYNC_DEF.WAR_FORM]
                  ?}
               ?};

               {? SYNC_DEF.FIR=_fir & SYNC_DEF.WAR_FORM=_wf
               || _stat.poprawnych+=1
               |? _fir='?'
               || _stat.bz+=1
               || SYNC_DEF.FIR:=_fir;
                  SYNC_DEF.WAR_FORM:=_wf;
                  {? SYNC_DEF.put()
                  || _stat.zmienionych+=1
                  || _stat.niezmienionych+=1
                  ?}
               ?}
            ?};
            SYNC_DEF.next()
         !};
         {? _stat.wszystkich>0
         || __UPG.msg('%1 - Liczba wszystkich definicji: %2, w tym:'[SYNC_PD.SYM,$_stat.wszystkich]);
            _dozmiany:=_stat.zmienionych+_stat.niezmienionych;
            __UPG.msg(
               ' * niemodyfikowanych: %1 | niewymagających zmiany: %2 | do zmiany: %3'
               [$_stat.bz,$_stat.poprawnych,$_dozmiany]
            );
            {? _dozmiany>0
            || __UPG.msg(' * zmienionych: %1 | niezmienionych: %2'[$_stat.zmienionych,$_stat.niezmienionych])
            ?}
         ?}
      ?};
      SYNC_PD.next()
   !};
   SYNC_DEF.cntx_pop()
|| __UPG.msg('Brak definicji wymiany danych z innymi systemami. Naprawa danych jest zbędna.')
?};
SYNC_PD.cntx_pop();

_ret


\sync_def_wv2_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0072 - Możliwe pozostawienie na portalu rekordu usuniętego w ERP-ie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zmiana miejsca przechowywania informacji o rekordach usuniętych dla wybranych tabel'


\portal_kre_his
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0072 - Możliwe pozostawienie na portalu rekordu usuniętego w ERP-ie.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_choice:=FUN.choice(
   'Czy wykonane zostały wszystkie kroki z opisu zadania?\n\n'
   ' * Tak = zmiana statusu zadania na "Wykonane"\n'
   ' * Nie = pozostawienie zadanie do ponownego uruchomienia\n'
   ' * Anuluj = rezygnacja z wykonywania, zmiana statusu zadania na "Wykonane"\n'
   ,2,'Tak','Nie'
);

USERS.cntx_psh();
__UPG.msg('Użytkownik: %1 | Data: %2 | Godzina: %3'[OPERATOR.USER().KOD,date()$1,time()$3]);
USERS.cntx_pop();

{? _choice=1
|| __UPG.msg('Zadanie zostało wykonane.')
|? _choice=2
|| __UPG.msg('Zadanie zostało odłozone do ponownego uruchomienia.')
|? _choice=0
|| __UPG.msg('Zrezygnowano z wykonania zadania.')
|| __UPG.msg('Nierozpoznana odpowiedź.')
?};

{? _choice<=1
|| 1
|| -1
?}


\portal_kre_his_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2210/0072 - Możliwe pozostawienie na portalu rekordu usuniętego w ERP-ie - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka wprowadza zmiany w synchronizacji tabeli KRE_HIS (historia spłaty / rozliczenia pożyczek / wkładów).
Dokładniej: wymaga ponownego wysłania tych danych na portal. Ponieważ jest to operacja czasochłonna i nie ma
możliwości sprawdzenia czy była juz wykonana, to nie przygotowano formuły transferowej - wszystkie kroki należy
wykonać ręcznie. Scenariusz jest następujący:
1. WYŁĄCZYĆ kolejki.
2. W management-cie usunąć dane związane z encjami:
   a) chr_PersonContribution
   b) chr_PersonContributionPay
   c) chr_PersonLoan
   d) chr_PersonLoanRepayment
3. W Mericie usunąć identyfikatory rekordów związane z (obszar "Ustawienia i parametryzacja",
   zakładka "Parametryzacja", Wspólne -> Wymiana danych -> Identyfikatory rekordów):
   a) Kod definicji: PORTAL_K_W_ID, Tabela: K, Maska: kre_his
   b) Kod definicji: PORTAL_K_P_ID, Tabela: K, Maska: kre_his
   UWAGA:
   Dane tabeli K zapisane są w masce "kredyty". NIE usuwamy definicji związanych z maską "kredyty"
   - usuwamy zapisy związane wyłącznie z maską "kre_his".
4. W obszarze "Usługi integracyjne - klient", stojąc na przeznaczeniu danych "PORTAL", należy:
   a) Wybrać akcję "Inicjuj".
   b) W okienku z pytaniem wybrać opcję "Wybrane tabele".
   c) W oknie wertowania odnaleźć i wybrać tabelę KRE_HIS.
5. W zależności od potrzeb wybrać JEDNĄ z poniższych dróg:
   a) Wciąż stojąc na przeznaczeniu danych "PORTAL" wybrać akcję "Zasilanie". Jest to najszybszy sposób na przesłanie
      danych. Jednak ewentualna analiza rejestru zdarzeń w zakresie tych wysyłanych rekordów będzie utrudniona.
   b) Wciąż stojąc na przeznaczeniu danych "PORTAL" wybrać akcję "Testuj wysyłanie". Akcja będzie trwała długo -
      decydująca jest liczba wysyłanych rekordów. W "Rejestrze zdarzeń" odłoży się komunikat związany z każdym
      wysyłanym rekordem, a dodatkowo po zakończeniu wysyłki wyświetlony zostanie pełen raport.
   c) Uruchomić kolejki - dane zostaną wysłane w ramach standardowej obsługi zmian. Pełna synchronizacja będzie
      trwała długo - decydująca jest liczba wysyłanych rekordów, ale wysyłka nie blokuje nam komputera.
6. Jeżeli w poprzednim kroku nie wybrano rozwiązania c), to należy uruchomić kolejki.'


\rubryki_PR22120041
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: PR/WRT/XP/22.26/2212/0041 - PŁ: Po aktualizacji 12.51_167 PL styczeń. Naliczanie listy płac dla wniosku
::       o niepobieranie zaliczek zgodnie z Objaśnieniami podatkowymi z 8 grudnia 2022 r
::       Dodanie rubryk i atrybutów płacowych propozcyji
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();
{? ~(',%1,'[__RUB.sys_sql(908)]*',%1,'[$7230])
|| __UPG.msg('Brak wykonanych zadań aktualizacji 22.26_06.');
   {? fexists('upgrade_2226_06.fml',1) & exec('rubryki_placowe_PL2023','upgrade_2226_06')=1
   || __UPG.msg('Wykonano zadanie "rubryki_placowe_PL2023" z aktualizacji 22.26_06.')
   || __UPG.msg('Nie udało się wykonać zadania "rubryki_placowe_PL2023" z aktualizacji 22.26_06.');
      _result:=-1
   ?}
?};

{? _result=1 & ~(+exec('import','rubryki','g','7232',1,0))
|| _txt:='Dodanie rubryki 7232 oraz atrybutu 90813 zakończone powodzeniem. '
         'Należy sprawdzić poprawność atrybutów w gałęzi 908: Ulgi podatkowe - pozostałe.'
|| _txt:='Dodanie rubryki 7232 oraz atrybutu 90813 zakończyło się niepowodzeniem.';
   _result:=-1
?};
R.trig_on('*','*');
{? _result=1
|| exec('add_attr','rubatr',9081,90813,'Składki ZUS',1,
            'Kwota składek przypadająca na zwolnienie z podatku art.31c',,7232);
   exec('add_use','rubatr',90812,7232)
?};
__UPG.msg(_txt);

_result


\rubryki_PR22120041_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: PR/WRT/XP/22.26/2212/0041 - PŁ: Po aktualizacji 12.51_167 PL styczeń. Naliczanie listy płac dla wniosku
::       o niepobieranie zaliczek zgodnie z Objaśnieniami podatkowymi z 8 grudnia 2022 r
::       Dodanie rubryk i atrybutów płacowych propozcyji - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk płacowych:\n'
' ● 7232: Zw. art.31c skł. ZUS\n'
'Dodanie atrybutów płacowych :\n'
' ● 90813 Składki ZUS - Kwota składek przypadająca na zwolnienie z podatku art.31c'


\atr_pak_mob
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie atrybutów płacowych związanych z oddelegowaniami i pakietem mobilności (ER/WRT/XP/22.26/2207/0046).
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

{? __RUB.ref(7074) & __RUB.ref(7204)
|| exec('add_attr','rubatr',901,90107,'Kwota diet za czas oddelegowania do pracy za gran.',1,
        'Kwota diet za czas oddelegowania do pracy za granicą.',,7074);
   exec('add_attr','rubatr',901,90108,'Kwota diet kierowcy (pakiet mobilności)',1,
        'Kwota diet kierowcy (pakiet mobilności).',,7204)
|| _result:=-1
?};

{? _result=1
|| __UPG.msg('Zakończono aktualizację atrybutów składników płacowych.')
|| __UPG.msg('Brak rubryk płacowych.')
?};

_result


\atr_pak_mob_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie atrybutów płacowych związanych z oddelegowaniami i pakietem mobilności - opis.
::----------------------------------------------------------------------------------------------------------------------
'Zadanie do poprawki ER/WRT/XP/22.26/2207/0046 uzupełniające listę atrybutów o atrybuty '
'90107 Kwota diet za czas oddelegowania do pracy za gran. i 90108 Kwota diet kierowcy (pakiet mobilności).'


\H_pat_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: kod - ER/WRT/XP/22.26/2301/0036 - EDI - błąd importu Przebieg pracy zawodowej, gdy nie jest podane pole
::             H.PAT nie można wydrukować zaimportowanej umowy o pracę tylko zawsze umowę z prawami autorskimi.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_err:=_suc:=_goo:=0;
_ret:=1;

_MSK:=H.names();
_MSK.clear();
{? _MSK.first()
|| H.trig_off('*','*');
   H.cntx_psh();
   {!
   |? H.use(_MSK.NAME);
      H.prefix();
      {? H.first()
      || {!
         |? {? H.PAT<>''
            || _goo+=1
            || H.PAT:='N';
               {? H.put()
               || _suc+=1
               || _err+=1
               ?}
            ?};
            H.next()
         !}
      ?};
      _MSK.next()
   !};
   H.cntx_pop();
   H.trig_on('*','*')
?};
obj_del(_MSK);

__UPG.msg('Liczba poprawionych rekordów: %1' [$_suc]);
{? _goo
|| __UPG.msg('Liczba rekordów niewymagających poprawienia: %1' [$_goo])
?};
{? _err
|| __UPG.msg('Liczba niepoprawionych rekordów: %1' [$_err]);
   _ret:=-1
?};

_ret


\H_pat_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: kod - ER/WRT/XP/22.26/2301/0036 - EDI - błąd importu Przebieg pracy zawodowej, gdy nie jest podane pole
::             H.PAT nie można wydrukować zaimportowanej umowy o pracę tylko zawsze umowę z prawami autorskimi.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2301/0036 - EDI - błąd importu Przebieg pracy zawodowej, gdy nie jest podane pole H.PAT nie '
'można wydrukować zaimportowanej umowy o pracę tylko zawsze umowę z prawami autorskimi.'


\mechanizm_importow_PKD
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Aktualizacja mechanizmu importu danych do kadr.
::       ER/WRT/XP/22.26/2301/0036 - EDI - błąd importu Przebieg pracy zawodowej, gdy nie jest podane pole
::             H.PAT nie można wydrukować zaimportowanej umowy o pracę tylko zawsze umowę z prawami autorskimi
::       ER/WRT/XP/22.26/2302/0022 - Błąd unikalności importu Badań lekarskich - nie uwzględnia TYPu badania
::   WE:
::   WY: 1
::----------------------------------------------------------------------------------------------------------------------
exec('import_init','#excel_imex',obj_ntab_names(obj_new('ZWS_IMPORTY_PKD')));
_result:=0;
{? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack(0).name])
|| __UPG.msg('Zaktualizowano mechanizm importu danych do kadr.');
   _result:=1
|| _result:=-1
?};
_result


\mechanizm_importow_PKD_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Aktualizacja mechanizmu importu danych do kadr - opis.
::       ER/WRT/XP/22.26/2301/0036 - EDI - błąd importu Przebieg pracy zawodowej, gdy nie jest podane pole
::             H.PAT nie można wydrukować zaimportowanej umowy o pracę tylko zawsze umowę z prawami autorskimi
::       ER/WRT/XP/22.26/2302/0022 - Błąd unikalności importu Badań lekarskich - nie uwzględnia TYPu badania
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja mechanizmu importu danych do kadr.\n'
'Poprawki SPIS:'
'\n* ER/WRT/XP/22.26/2301/0036 - EDI - błąd importu Przebieg pracy zawodowej, gdy nie jest podane pole '
'H.PAT nie można wydrukować zaimportowanej umowy o pracę tylko zawsze umowę z prawami autorskimi,'
'\n* ER/WRT/XP/22.26/2302/0022 - Błąd unikalności importu Badań lekarskich - nie uwzględnia TYPu badania.'


\PIT01_2226
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [22.26]
:: OPIS: 22.26_PIT01 - PIT-8AR(12) - aktualizacja xsd od 18.01.2023 2 wersja schemy
::----------------------------------------------------------------------------------------------------------------------
_dom:=BPMN.SYM_DOM;
BPMN.SYM_DOM:='PPL';
SKID.ISTDEF:='D';
::import poprawnych struktur e-deklaracji PIT-4R(12) 3 numer schemy 12
{? exec('edek_stru','edeklar','PIT8AR',13,'pit-8ar13_v1-2.dfg',date(2022,12,12))
|| __UPG.msg('Dodano 2 numer schematu 13 wersji e-deklaracji PIT-8AR(12).')
|| __UPG.msg('Brak aktualizacji, 2 numer schematu 13 wersji e-deklaracji PIT-8AR(12) już istnieje.')
?};
BPMN.SYM_DOM:=_dom;
1


\PIT01_2226_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DAROKR [22.26]
:: OPIS: 22.26_PIT01 - PIT-8AR(12) - aktualizacja xsd od 18.01.2023 2 wersja schemy - opis
::----------------------------------------------------------------------------------------------------------------------
'Dodano 2 numer schematu 13 wersji e-deklaracji PIT-8AR(12) dotyczącej przychodów od 01.01.2022'


\procesy_pep_2301_0034
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Import procesów portal HR (dziedzina PEP)
::       ER/WRT/XP/22.26/2301/0034 - Wnioski Papperles i HR Portal dostępne w webterm
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('procesy','upgrade','22.26',,'PEP_',0,0,0);
{? _result>0
|| _result:=0;
   {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack(0).name])
   || __UPG.msg('Zakończono import procesów z dzieniny PEP');
      FUN.info('Należy zweryfikować powiązania zaimportowanych procesów z typami wniosków.\n'
               '(Ustawienia i parametryzacja->Paperless HR->Typy wniosków w obiegu->'
               '(wybrany wniosek)->Procesy powiązane)'@);
      _result:=1
   || _result:=-1
   ?}
?};
_result


\procesy_pep_2301_0034_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Import procesów portal HR (dziedzina PEP) - opis
::       ER/WRT/XP/22.26/2301/0034 - Wnioski Papperles i HR Portal dostępne w webterm
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
'Poprawka - ER/WRT/XP/22.26/2301/0034 - Wnioski Papperles i HR Portal dostępne w webterm.\n'
'Pozwala zaimportować zmienione procesy dziedziny PEP:\n'
' ● PEP_ZASW_WWW - Paperless HR - Wydanie zaświadczenia\n'
' ● PEP_ZDOK_WWW - Paperless HR - Złożenie dokumentu\n'
' ● PEP_ZWP_WWW - Paperless HR - Rez. ze zwol. z opod. do 26 l.\n'


\procesy_por_2301_0034
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Import procesów portal HR (dziedzina POR)
::       ER/WRT/XP/22.26/2301/0034 - Wnioski Papperles i HR Portal dostępne w webterm
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('procesy','upgrade','22.26',,'POR_',0,0,0);
{? _result>0
|| _result:=0;
   {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack(0).name])
   || __UPG.msg('Zakończono import procesów z dzieniny PEP');
      FUN.info('Należy zweryfikować powiązania zaimportowanych procesów z typami wniosków.\n'
               '(Ustawienia i parametryzacja->HR Portal->Typy wniosków w obiegu->'
               '(wybrany wniosek)->Procesy powiązane)'@);
      _result:=1
   || _result:=-1
   ?}
?};
_result


\procesy_por_2301_0034_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: Import procesów portal HR (dziedzina POR) - opis
::       ER/WRT/XP/22.26/2301/0034 - Wnioski Papperles i HR Portal dostępne w webterm
::   WY: 0/1
::----------------------------------------------------------------------------------------------------------------------
'Poprawka - ER/WRT/XP/22.26/2301/0034 - Wnioski Papperles i HR Portal dostępne w webterm.\n'
'Pozwala zaimportować nowe procesy dziedziny POR:\n'
' ● POR_DOWU_WWW - Deklaracja opłacania wpłat uczestnika art. 25\n'
' ● POR_DRWD_WWW - Wniosek o rezygnację z % wpłaty dod. PPK\n'
' ● POR_DWWD_WWW - Wniosek o zmianę % wpłaty dodatkowej PPK\n'
' ● POR_DWWP_WWW - Wniosek o zmianę % wpłaty podstawowej PPK\n'
' ● POR_ERP7_WWW - Wniosek o zaświadczenie ERP-7\n'
' ● POR_NADW_WWW - Wniosek o pracę w godzinach nadliczbowych\n'
' ● POR_NIP_WWW - Zmiana numeru NIP\n'
' ● POR_OPIF_WWW - Oświadczenie o podpisanych umowach w IF\n'
' ● POR_PREM_WWW - Premia\n'
' ● POR_PRUO_WWW - Przedł.rozw. umowy z oceną - akc. Dyr. Hr\n'
' ● POR_RDCR_WWW - Rejestracja danych członka rodziny\n'
' ● POR_REK_WWW - Zgłoszenie rekrutacji - akceptacja Dyr. Hr\n'
' ● POR_WAD_WWW - Zmiana danych adresowych\n'
' ● POR_WDSP_WWW - Wniosek o duplikat świadectwa pracy\n'
' ● POR_WKD_WWW - Wniosek o udostępnienie kopii dokumentacji\n'
' ● POR_WNI_FL - Obsługa wniosku admin. - Flota samochodowa\n'
' ● POR_WNI_IT - Obsługa wniosku admin. - Infrastruktura IT\n'
' ● POR_WNI_PB - Obsługa wniosku admin. - Powierzchnie biurowe\n'
' ● POR_WNI_TEL - Obsługa wniosku admin. - Telefonia komórkowa\n'
' ● POR_WNI_WB - Obsługa wniosku admin. - Wyposażenia biur\n'
' ● POR_WODW_WWW - Wniosek o dokonywanie wpłat PPK\n'
' ● POR_WOL_WWW - Wniosek o zaliczkę pracowniczą\n'
' ● POR_WOU_WWW - Wniosek o uczestnictwo w PPK\n'
' ● POR_WPAP_WWW - Wniosek z załącznikiem\n'
' ● POR_WPU_OJC - Wniosek o urlop ojcowski\n'
' ● POR_WPU_W1P - Wniosek urlopowy (1 poziom akceptacji)\n'
' ● POR_WPU_W2P - Wniosek urlopowy (2 poziomy akceptacji)\n'
' ● POR_WRH_WWW - Zmiana numeru rachunku ROR\n'
' ● POR_WZ1_WWW - Wniosek o zatrudnienie - akceptacja Dyr. HR\n'
' ● POR_ZMDO_WWW - Zmiana wybranych danych osobowych\n'
' ● POR_ZMND_WWW - Zmiana numeru dowodu osobistego\n'
' ● POR_ZMOB_WWW - Zmiana obywatelstwa\n'
' ● POR_ZMPA_WWW - Zmiana numeru paszportu\n'
' ● POR_ZMPE_WWW - Zmiana numeru PESEL\n'
' ● POR_ZMUS_WWW - Zmiana urzędu skarbowego\n'
' ● POR_ZZ1_WWW - Zmiana zatrudnienia - akceptacja Dyr. Hr\n'


\ETYPY_P_pack
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Ustawienie grupowego zasilania uprawnień do wniosków w portalu HR
::----------------------------------------------------------------------------------------------------------------------
{? var_pres('PACK',SYNC_MWA)>0
|| SYNC_MWA.cntx_psh();
   SYNC_MWA.index('PD_METH');
   SYNC_MWA.prefix();
   {? SYNC_MWA.find_key('PORTAL','sl_TenantUserRowRightsModify',)
   || SYNC_MWA.PACK:=1;
      SYNC_MWA.put();
      __UPG.msg('Ustawiono grupowe zasilanie uprawnień do wniosków w portalu HR')
   || __UPG.msg('Nie znaleziono metody %1 w %2.'['sl_TenantUserRowRightsModify','PORTAL'])
   ?};
   SYNC_MWA.cntx_pop()
|| __UPG.msg('Brak pola %1 w definicji systemu.'['SYNC_MWA.PACK'])
?};
1


\ETYPY_P_pack_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Ustawienie grupowego zasilania uprawnień do wniosków w portalu HR - opis
::----------------------------------------------------------------------------------------------------------------------
'Ustawienie grupowego zasilania uprawnień do wniosków w portalu HR'


\s_zus
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Aktualizacja słownika kodów ZUS.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_wynik:=1;
S_ZUS.cntx_psh();
S_ZUS.prefix();
{? exec('s_zus_import','personel',0)
|| __UPG.msg('Aktualizacja słownika kodów ZUS zakończyła się sukcesem.')
|| __UPG.msg('Aktualizacja słownika kodów ZUS nie powiodła się.');
   _wynik:=-1
?};
S_ZUS.cntx_pop();
_wynik


\s_zus_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Aktualizacja słownika kodów ZUS - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja słownika kodów ZUS'


\fix_defw_uid
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Aktualizacja pola UID dla tabeli związanej z definicjami wierszy sprawozdań.
::----------------------------------------------------------------------------------------------------------------------
VAR_DEL.delete('__defsize','__defmod');
__defsize:=0;
__defmod:=0;
DEFW.index('UID');
DEFW.prefix();
DEFW.for_each("
   __defsize+=1;
   _prev_uid:=DEFW.UID;
   _new_uid:='';
   {? _prev_uid=''
   || _new_uid:=exec('set_defw_uid','sprfin')
   || DEFW.cntx_psh();
      DEFW.prefix(_prev_uid,);
      {? DEFW.size>1
      || _new_uid:=exec('set_defw_uid','sprfin')
      ?};
      DEFW.cntx_pop()
   ?};
   {? _new_uid<>''
   || DEFW.UID:=_new_uid;
      {? DEFW.put() || __defmod+=1 ?}
   ?}
");
{? __defmod>0
|| __UPG.msg('Zmodyfikowano pola UID definicji wierszy sprawozdań.'
             ' Zmodyfikowano %1 z %2 rekordów.'[$__defmod,$__defsize])
|| __UPG.msg('Modyfikacja pola UID definicji wierszy sprawozdań nie jest wymagana.'
             ' Pole UID dla tabeli DEFW jest unikalne.')
?};
VAR_DEL.delete('__defsize','__defmod');
1


\fix_defw_uid_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: JK [22.26]
:: OPIS: Aktualizacja pola UID dla tabeli związanej z definicjami wierszy sprawozdań. - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja pola UID dla tabeli związanej z definicjami wierszy sprawozdań.'


\atr_plac_cw_woj
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Aktualizacja atrybutów płacowych.
::       Poprawka błędu ER/WRT/XP/22.26/2210/0049 - Kod świadczenia przerwy ZUS dla składnika 7068 - ćwiczenia wojskowe
::----------------------------------------------------------------------------------------------------------------------
_result:=1;

R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();

RA_DEF.cntx_psh();
RA_USE.cntx_psh();
RA_DEF.index('RODZAJ');
RA_DEF.prefix('S');
RA_USE.index('RA_USE');

{? RA_DEF.find_key(14151)
|| RA_USE.prefix(RA_DEF.ref(),__RUB.ref(7068));
   {? RA_USE.first()
   || RA_USE.del()
   ?}
?};
{? __RUB.find_def('S',14111)
|| exec('add_uses','rubatr',7068,14,14111);
   __UPG.msg('Zakończono aktualizację atrybutów składników płacowych.')
|| __UPG.msg('Aktualizacja atrybutów składników płacowych zakończyła się niepowodzeniem - brak atrybutu 14111.');
   _result:=-1
?};

RA_USE.cntx_pop();
RA_DEF.cntx_pop();

R.trig_on('*','*');

_result


\atr_plac_cw_woj_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: J9SZAFRA [21.37]
:: OPIS: Aktualizacja atrybutów płacowych - opis.
::       Poprawka błędu ER/WRT/XP/22.26/2210/0049 - Kod świadczenia przerwy ZUS dla składnika 7068 - ćwiczenia wojskowe
::----------------------------------------------------------------------------------------------------------------------
'Funkcja naprawcza przenosząca rubrykę 7068 - Ćwiczenia wojskowe z atrybutu 14151 do 14111.'
'Poprawka błędu ER/WRT/XP/22.26/2210/0049'


\formuly_placowe_kor_rh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Dodanie formuł płacowych.
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='951';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('R',',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt_kor_rh',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_kor_rh_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Dodanie formuł płacowych - opis.
::----------------------------------------------------------------------------------------------------------------------
'Dodanie formuły płacowej typu R dla składnika płacowego 951 - Nadp. skł.E/R. b.rok'


\formuly_txt_kor_rh
::----------------------------------------------------------------------------------------------------------------------
::  UTW: DG [22.26]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=951
:: =================
:: Nadp. skł.E/R. b.rok
|| "exec('kor_zc_zwrot','korekty_zus')"

|| ""
?}


\PPK01_2226
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Aktualizacja PPK 22.26_PPK01
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
exec('__RUB','object');
__RUB.fill();
:: Rodzaje deklaracji/wniosków
{? exec('dane_sys','ppk_wrd')
|| __UPG.msg('Wykonano aktualizację definicji wniosków i deklaracji.')
|| __UPG.msg('Nie wykonano aktualizacji definicji wniosków i deklaracji.');
   _result:=-1
?};
:: Rodzaje komunikatów
{? exec('dane_sys','ppk_krd')
|| __UPG.msg('Wykonano aktualizację definicji komunikatów.')
|| __UPG.msg('Nie wykonano aktualizacji definicji komunikatów.');
   _result:=-1
?};
:: Kompilacja wydruków
{? rep_tran('ppk*')<>0
|| __UPG.msg('Zaktualizowano wydruki dziedziny PPK.')
|| __UPG.msg('Aktualizacja wydruków dziedziny PPK nie powiodła się.');
   _result:=-1
?};
:: Przypięcie czynności do roli
{? exec('roleActions','upgrade','Specjalista ds. PPK','PPK_EWI_AKRW,PPK_EWI_ARNW')
|| __UPG.msg('Dodano czynności do roli.')
|| __UPG.msg('Dodanie czynności do roli nie powiodło się.');
   _result:=-1
?};

_result


\PPK01_2226_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Aktualizacja PPK 22.26_PPK01 - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja 22.26_PPK01:\n'
'* Wczytanie definicji wniosków, deklaracji i komunikatów,\n'
'* Kompilacja wydruków dziedziny PPK,\n'
'* Dodanie czynności "Info.o wznowieniu wpłat do PPK - PPK_EWI_AKRW" oraz "Wznowienie wpłat do PPK - PPK_EWI_ARNW" '
'do roli "Specjalista ds. PPK".'


\PPK01_2226_man
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Aktualizacja PPK 22.26_PPK01 - zadanie manualne
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
:: Import procesów
{? exec('procesy','upgrade','22.26','PER_PPK_ARNW,PER_PPK_AWWO,PER_PPK_AWWU','PER_PPK')
|| __UPG.msg('Zaimportowano procesy.')
|| __UPG.msg('Import procesów nie powiódł się.');
   _result:=-1
?};
{? _result & ~FUN.ask('Czy oznaczyć zadanie "%1" jako wykonane?'@['PPK01_2226_man'])
|| _result:=-1
?};
_result


\PPK01_2226_man_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Aktualizacja PPK 22.26_PPK01 - zadanie manualne - opis
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja 22.26_PPK01:\n'
'* Import zmienionych procesów dziedziny PPK:\n'
'** PER_PPK_ARNW - Wznowienie wpłat do PPK,\n'
'** PER_PPK_AWWO - Wznowienie wpłat osób do PPK,\n'
'** PER_PPK_AWWU - Wznowienie wpłat uczestników do PPK.'


\fix_HTTPBANK
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: PR/WRT/XP/22.26/2302/0007 - Ustawienie adresu NBP do pobierania listy banków.
::----------------------------------------------------------------------------------------------------------------------
HINFO.HTTPBANK:='https://ewib.nbp.pl/plewibnra?dokNazwa=';
exec('zapisz','#stalesys',1,HINFO,'HTTPBANK');
__UPG.msg('Zaktualizowano adres serwera NBP z danymi banków.');
1


\fix_HTTPBANK_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: PR/WRT/XP/22.26/2302/0007 - Ustawienie adresu NBP do pobierania listy banków. - opis
::----------------------------------------------------------------------------------------------------------------------
'Ustawienie adresu serwera NBP z danymi banków.\n'
'PR/WRT/XP/22.26/2302/0007'


\fix_edek_us
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Usunięcie błędnych spacji w kodach do e-deklaracji urzędów skarbowych
::       Poprawka do ER/WRT/XP/22.26/2301/0084
::----------------------------------------------------------------------------------------------------------------------
_obj:=obj_new(
   'FIRST'
);
_obj.FIRST:=0;
params_set('obj',_obj);
US.cntx_psh();
US.index('URZSKNAZ');
US.prefix();
US.for_each("
   {? US.EDEK_SYM<>form(US.EDEK_SYM)
   || US.EDEK_SYM:=form(US.EDEK_SYM);
      {? US.put()
      || _obj:=params_get().obj;
         {? _obj.FIRST=0
         || __UPG.msg('Usunięto błędne spacje w kodach do e-deklaracji następujących urzędów skarbowych:');
            _obj.FIRST:=1
         ?};
         __UPG.msg('- '+US.EDEK_SYM+' - '+US.NU)
      ?}
   ?}
");
US.cntx_pop();
{? _obj.FIRST=0
|| __UPG.msg('Nie znaleziono błędnych spacji w kodach do e-deklaracji urzędów skarbowych.')
?};
1


\fix_edek_us_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Usunięcie błędnych spacji w kodach do e-deklaracji urzędów skarbowych - opis
::       Poprawka do ER/WRT/XP/22.26/2301/0084
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie błędnych spacji w kodach do e-deklaracji urzędów skarbowych.\n'
'Poprawka do ER/WRT/XP/22.26/2301/0084'


\cit8_31v1_upd
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/22.26/2302/0060 - Nie można utworzyć i wysłać e-deklarację CIT-8
::----------------------------------------------------------------------------------------------------------------------
_ok:=_licz:=_srec:=0;
VAT_VER.cntx_psh(); VAT_DEK.cntx_psh();
VAT_VER.index('VER_OD');
VAT_VER.prefix('FKS','CIT8','CIT8');
{? VAT_VER.find_key(date(2021,1,1),31)
|| _ref21:=VAT_VER.ref();
   {? VAT_VER.find_key(date(2022,1,1),31)
   || _ref22:=VAT_VER.ref();
      VAT_DEK.index('VAT_PITG');
      VAT_DEK.prefix();
      {? VAT_DEK.first()
      || {! |? {? VAT_DEK.NR=_ref22
               || _srec+=1;
                  VAT_DEK.NR:=_ref21;
                  {? VAT_DEK.put() || _licz+=1; _ok:=1 ?}
                ?};
                VAT_DEK.next()
          !}
      ?}
   || _ok:=2
   ?}
|| _ok:=3
?};
VAT_DEK.cntx_pop(); VAT_VER.cntx_pop();

_txt:='Wersja deklaracji CIT-8 (31) błędnie wskazana jako obowiązująca za 2022 rok';
_txt2:='Wersja deklaracji CIT-8 (31) obowiązująca za 2021 rok';
{? _ok=3
|| __UPG.msg(_txt2+' nie została wykryta.')
|? _ok=2
|| __UPG.msg(_txt+' nie została wykryta.')
|? _ok=1
|| __UPG.msg('Zmodyfikowano powiązanie deklaracji zbłędną wersją CIT-8 (31).'
             ' Zmodyfikowano %1 z %2 rekordów.'[$_licz,$_srec])
|| __UPG.msg(_txt+' - nie wykryto sytuacji błędnych.')
?};
1


\cit8_31v1_upd_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/22.26/2302/0060 - Nie można utworzyć i wysłać e-deklarację CIT-8 - opis
::----------------------------------------------------------------------------------------------------------------------
'Przepięcie powiązań do błędnej wersji deklaracji CIT-8 (31) za 2022 rok.'


\cit8_31v1_del
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/22.26/2302/0060 - Nie można utworzyć i wysłać e-deklarację CIT-8
::----------------------------------------------------------------------------------------------------------------------
_ok:=0;
VAT_VER.cntx_psh(); VAT_DEK.cntx_psh();
VAT_VER.index('VER_OD');
VAT_VER.prefix('FKS','CIT8','CIT8');
{? VAT_VER.find_key(date(2022,1,1),31)
|| {? VAT_VER.count()=0
   || {? VAT_VER.del() || _ok:=1 ?}
   ?}
|| _ok:=2
?};
VAT_DEK.cntx_pop(); VAT_VER.cntx_pop();

_txt:='Wersja deklaracji CIT-8 (31) błędnie wskazana jako obowiązująca za 2022 rok';
{? _ok=2
|| __UPG.msg(_txt+' nie została wykryta.')
|? _ok=1
|| __UPG.msg(_txt+' została usunięta.')
|| __UPG.msg(_txt+' nie mogła zostać usunięta.')
?};
1


\cit8_31v1_del_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IFZ [22.26]
:: OPIS: ER/WRT/XP/22.26/2302/0060 - Nie można utworzyć i wysłać e-deklarację CIT-8 - opis
::----------------------------------------------------------------------------------------------------------------------
'Usunięcie błędnej wersji deklaracji CIT-8 (31) za 2022 rok.'


\rubryki_ER23020080
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: ER/WRT/XP/22.26/2302/0080: Brak składnika 7157 OZ: Urlop w atrybucie 19421
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
:: ER/WRT/XP/22.26/2302/0080: Brak składnika 7157 OZ: Urlop w atrybucie 19421
exec('add_use','rubatr',19421,7156,7157);
__UPG.msg('Dodano rubryki "7156 OZ: Choroba" oraz "7157 OZ: Urlop" do atrybutu płacowego '
          '"19421 Warunkowo pomijane w zapytaniach sql"');
_result


\rubryki_ER23020080_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: ER/WRT/XP/22.26/2302/0080: Brak składnika 7157 OZ: Urlop w atrybucie 19421
::----------------------------------------------------------------------------------------------------------------------
'Dodanie rubryk "7156 OZ: Choroba" oraz "7157 OZ: Urlop" do atrybutu płacowego:\n'
' ● 19421 Warunkowo pomijane w zapytaniach sql'


\pkalsync_resend2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2303/0006 - Nieprawidłowo ustalana wartość pola __ModifiedDate_cloud
::       dla komunikatów chr_PersonWorkScheduleModify.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic','#b_domain','POR')
|| __UPG.msg('Brak licencji na dziedzinę "HR Portal" - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

_TAB:=sql('select distinct PKALSYNC.ROK from PKALSYNC where PKALSYNC.ROK>=:_a',date()~1);
{? ~_TAB.first()
|| __UPG.msg('Brak danych - uruchomienie zadania nie jest wymagane.');
   return(1)
?};

_ig:=0;
_err:=0;
PKALSYNC.trig_off('*','*');
PKALSYND.trig_off('*','*');
PKALSYNC.cntx_psh();
PKALSYNC.index('ROK');
{!
|? PKALSYNC.prefix(_TAB.ROK);
   {? PKALSYNC.first()
   || PKALSYND.cntx_psh();
      {? exec('PKALSYND_mask','pkalsync',PKALSYNC.ROK)
      || PKALSYND.index('SYNC');
         {!
         |? PKALSYND.prefix(PKALSYNC.ref());
            _il:=0;
            do();
            {? PKALSYND.first()
            || {!
               |? PKALSYND.SEND:=1;
                  _il+=PKALSYND.put();
                  PKALSYND.next()
               !}
            ?};
            exec('del_rek','#sync_id','PORTAL_PKALSYNC_ID',PKALSYNC.uidref());
            PKALSYNC.put(,1);
            {? end()
            || _ig+=_il
            || __UPG.msg('Zmiana wartości flagi nie powiodła się [%1,%2].'[$PKALSYNC.ROK,$PKALSYNC.ref()]);
               _err+=1
            ?};
            PKALSYNC.next()
         !}
      || __UPG.msg('Błąd otwarcia maski %1.'[$PKALSYNC.ROK]);
         _err+1
      ?};
      PKALSYND.cntx_pop()
   ?};
   _TAB.next()
!};
PKALSYNC.cntx_pop();
PKALSYND.trig_on('*','*');
PKALSYNC.trig_on('*','*');

{? _err
|| -1
|| __UPG.msg('Rekordów do (ponownego) wysłania: %1.'[$_ig]);
   1
?}


\pkalsync_resend2_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: RWR [22.26]
:: OPIS: ER/WRT/XP/22.26/2303/0006 - Nieprawidłowo ustalana wartość pola __ModifiedDate_cloud
::       dla komunikatów typu 1 do wielu (np. chr_PersonWorkScheduleModify) - opis.
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Wymuszenie wysyłki kalendarzy.\n'
'ER/WRT/XP/22.26/2303/0006\n'
'Nieprawidłowo ustalana wartość pola __ModifiedDate_cloud dla komunikatów chr_PersonWorkScheduleModify.'


\nbp_kursy_fsp2
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Aktualizacja specyfikacji importu kursów walut z NBP
::----------------------------------------------------------------------------------------------------------------------
exec('spc_imp','homebank','10100000_NBP kursy walut (Internet).fsp',1,1,,1);
exec('spc_imp','homebank','10100000_NBP kursy walut (Proces).fsp',1,1,,1);
__UPG.msg('Zaktualizowano specyfikacje importu kursów walut z NBP.');
1


\nbp_kursy_fsp2_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MP [22.26]
:: OPIS: Aktualizacja specyfikacji importu kursów walut z NBP - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja specyfikacji importu kursów walut z NBP'


\fiks_87
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2303/0079 - schemat e-deklaracji CIT 8 wersja 32
::----------------------------------------------------------------------------------------------------------------------
_res:=-1;
VAT_VER.cntx_psh();
VAT_VER.index('VER_OD'); VAT_VER.prefix('FKS','CIT8','CIT8',date(2022,01,01),32);
_ver:={? VAT_VER.first() || VAT_VER.ref() || null ?};
VAT_VER.cntx_pop();
{? _ver
|| ISTDEF.cntx_psh();
   ISTDEF.index('DATA'); ISTDEF.prefix('FKS','D','CIT8',_ver);
   {? ISTDEF.first()
   || _res:=0;
      _fml:="
         _tag:=_a;
         _reg:=_b;
         _wym:=_c;
         _res:=0;
         ISTDEFS.cntx_psh();
         ISTDEFS.index('OPIS'); ISTDEFS.prefix(ISTDEF.ref());
         {? ISTDEFS.find_key(_tag,)
         || {? ISTDEFS.REGULY<>_reg | ISTDEFS.WYM<>_wym
            || ISTDEFS.REGULY:=_reg;
               ISTDEFS.WYM:=_wym;
               _res:=ISTDEFS.put()
            ?}
         ?};
         ISTDEFS.cntx_pop();
         _res
      ";
      _res+=_fml('P_35',$"P_36:=exec('poz_edek_dod','xml','CIT8 ',1,36); {? P_36='0' | P_36='' || '' || '1'  ?}",'N');
      _res+=_fml('P_36',$"_r:=P_36; &P_36; _r",'N')
   ?};
   ISTDEF.cntx_pop()
?};
{? _res=-1
|| __UPG.msg('Nie znaleziono schematu e-deklaracji CIT-8 (32).')
|? _res=0
|| __UPG.msg('Zmiany w schemacie e-deklaracji CIT-8 (32) nie były wymagane.')
|| __UPG.msg('Poprawiono schemat e-deklaracji CIT-8 (32).')
?};
1


\fiks_87_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: ER/WRT/XP/22.26/2303/0079 - schemat e-deklaracji CIT 8 wersja 32 - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2303/0079\n'
'Naprawa obsługi pola P_35 w schemacie e-deklatacji CIT-8 (32)'


\cit8_32
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS:  OPIS: PR/WRT/XP/22.26/2304/0060 - Import aktualnej specyfikacji e-deklaracji CIT-8 (32)
::----------------------------------------------------------------------------------------------------------------------
_edek:='cit-8_32.dfg';
_err:=0;
_txt:='e-Deklaracja CIT-8 (32) obowiązująca za 2022 rok';

{? exec('edek_stru_del','edeklar','CIT8',32,date(2022,01,01),date(2022,01,01),'FKS')
|| {? fexists(_edek,1)
   || _imp:=-1;
      {? exec('add_ver','xml','FKS',date(2022,1,1),'CIT8',32)
      || _imp:=exec('upg_imp','xml','FKS','D','CIT8',32,date(2022,1,1),'cit-8_32.dfg')
      ?};
      {? _imp=1
      || __UPG.msg(_txt+' została dodana.')
      |? _imp=0
      || __UPG.msg(_txt+' już istnieje.');
         _err+=1
      || __UPG.msg(_txt+' nie została dodana.');
         _err+=1
      ?}
   || __UPG.msg('Nie znaleziono na serwerze pliku: %1'[_edek]);
      _err+1
   ?}
|| __UPG.msg(_txt+' nie mogła zostać usunięta.');
   _err+=1
?};
{? _err
|| -1
|| 1
?}


\cit8_32_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: AFI [22.26]
:: OPIS: PR/WRT/XP/22.26/2304/0060 - Import aktualnej specyfikacji e-deklaracji CIT-8 (32) - opis
::----------------------------------------------------------------------------------------------------------------------
'Import schematów e-Deklaracji CIT-8 (32) za 2022 rok'


\espr_jpk
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uaktualnia pole E_SPR.JPK
::----------------------------------------------------------------------------------------------------------------------
_ile:=0;
_wym:=0;
E_SPR.cntx_psh();
E_SPR.index('FIRMA');
E_SPR.prefix(REF.FIRMA);
{? E_SPR.first()
|| JPK.cntx_psh();
   JPK.index('E_SPR');
   {!
   |? JPK.prefix(E_SPR.ref());
      _jest:={? JPK.first() || 'T' || 'N' ?};
      {? E_SPR.JPK<>_jest
      || _wym+=1;
         E_SPR.JPK:=_jest;
         _ile+=E_SPR.put()
      ?};
      E_SPR.next()
   !};
   JPK.cntx_pop()
?};
E_SPR.cntx_pop();
__UPG.msg('Liczba definicji e-Sprawozdań wymagających zmiany: %1.'[$_wym]);
__UPG.msg('Liczba zmienionych definicji e-Sprawozdań: %1.'[$_ile]);
_ile=_wym


\espr_jpk_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Uaktualnia pole E_SPR.JPK
::----------------------------------------------------------------------------------------------------------------------
'Uaktualnienie znacznika wygenerowanych plików JPK_SF na definicji e-Sprawozdania.\n'
'(Pole E_SPR.JPK)'


\formuly_placowe_ER23080022
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2308/0022 - Wyrównanie do minim.wynagr.
::       w przypadku wycofania urlopu z poprzedniego miesiąca dla pracownika o stawce godzinowej
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='500';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt_ER23080022',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_ER23080022_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2308/0022 - Wyrównanie do minim.wynagr.
::       w przypadku wycofania urlopu z poprzedniego miesiąca dla pracownika o stawce godzinowej - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2308/0022'
'Wyrównanie do minim.wynagr. w przypadku wycofania urlopu z poprzedniego miesiąca dla pracownika o stawce godzinowej.'


\formuly_txt_ER23080022
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=500
:: ==================
:: Brutto
|| "
_BRUTTO:=FUNKCJE.L(100,498)+FUNKCJE.L_SYS(48);
{? PAR_SKID.get(195)='T'
|| _wyr:=exec('wyr_minkraj','!ppl_pll_nals',_BRUTTO);
   FUNKCJE.LK(499,,_wyr);
   _BRUTTO+=_wyr
?};
_BRUTTO"

|| ""
?}


\formuly_placowe_ER23080032
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2308/0032 dotyczący wyliczania kwoty rubryka 500 Brutto,
::       gdy parametr 195 jest wyłączony brak sumowania rubryki 499 do kwoty 500
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='500';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt_ER23080032',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_ER23080032_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2308/0032 dotyczący wyliczania kwoty rubryka 500 Brutto,
::       gdy parametr 195 jest wyłączony brak sumowania rubryki 499 do kwoty 500 - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2308/0032 dotyczący wyliczania kwoty rubryka 500 Brutto,/n'
'gdy parametr 195 jest wyłączony brak sumowania rubryki 499 do kwoty 500.'


\formuly_txt_ER23080032
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=500
:: ==================
:: Brutto
|| "
{? PAR_SKID.get(195)='T'
|| _BRUTTO:=FUNKCJE.L(100,498)+FUNKCJE.L_SYS(48);
   _wyr:=exec('wyr_minkraj','!ppl_pll_nals',_BRUTTO);
   FUNKCJE.LK(499,,_wyr);
   _BRUTTO+=_wyr
|| _BRUTTO:=FUNKCJE.L(100,499)+FUNKCJE.L_SYS(48)
?};
_BRUTTO"

|| ""
?}


\formuly_placowe_ER23080027
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2308/0027 : Wycofanie urlopu rehabilitacyjnego dla wypłaconej kwoty
::       7134 Wynagr. rehabil. brak rubryki z korektą tego składnik co powoduje nadpłatę
::----------------------------------------------------------------------------------------------------------------------
exec('__RUB','object');

_rs:='7237';
_rn:=spli_str(_rs,',');
{! _ii:=1..obj_len(_rn)
|! {? __RUB.ref(#_rn[_ii])=null()
   || __UPG.msg('Aktualizacja została wstrzymana. Wymagane istnienie rubryk: %1'[_rs]);
      return(-1)
   ?}
!};
_tp:=spli_str('U,F,%1'[%255],',');
_err:=exec('formuly_import','rubryki',_tp,_rn,'formuly_txt_ER23080027',form_stack(0).file);

{? _err.TAB.first()
|| {!
   |? __UPG.msg(_err.TAB.MSG);
      _err.TAB.next()
   !}
?};

{? _err.counter>0
|| __UPG.msg('Podczas aktualizacji formuł wystąpiło %1 błędów.'[$_err.counter]);
   -1
|| 1
?}


\formuly_placowe_ER23080027_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2308/0027: Wycofanie urlopu rehabilitacyjnego
::       dla wypłaconej kwoty 7134 Wynagr. rehabil. brak rubryki z korektą tego składnik co powoduje nadpłatę.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2308/0027: Wycofanie urlopu rehabilitacyjnego '
'dla wypłaconej kwoty 7134 Wynagr. rehabil. brak rubryki z korektą tego składnik co powoduje nadpłatę.'


\formuly_txt_ER23080027
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Formuła wewnętrzna zwracająca treść formuły płacowej.
::       Formuła wykorzystywana przez \formuly_placowe (patrz wyżej).
::   WE: _a [STRING]  - Typ formuły.
::       _b [INTEGER] - Numer rubryki.
::   WY: Treść formuły.
::----------------------------------------------------------------------------------------------------------------------
_tp:=_a;
_rn:=_b;

{? _rn=7237
:: ==================
|| "
DoList.c[14]$2"

|| ""
?}


\rubryki_ER23080027
::----------------------------------------------------------------------------------------------------------------------
::  UTW: PK [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2308/0027: Wycofanie urlopu rehabilitacyjnego
::       dla wypłaconej kwoty 7134 Wynagr. rehabil. brak rubryki z korektą tego składnik co powoduje nadpłatę.
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();

{? ~(+exec('import','rubryki','g','7237',1,0))
|| _txt:='Dodanie rubryki "7237 Kor. wyn. rehabil." zakończone powodzeniem. '
|| _txt:='Dodanie rubryki "7237 Kor. wyn. rehabil." zakończyło się niepowodzeniem.';
   _result:=-1
?};
R.trig_on('*','*');
_result


\rubryki_ER23080027_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2308/0027: Wycofanie urlopu rehabilitacyjnego
::       dla wypłaconej kwoty 7134 Wynagr. rehabil. brak rubryki z korektą tego składnik co powoduje nadpłatę.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2308/0027\n'
'Dodanie rubryki "7237 Kor. wyn. rehabil."'


\portal_wn_urlop
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [22.26]
:: OPIS: Zmiana kolejności przetwarzania metod API dla przeznaczenia danych PORTAL_WNIOSKI
::       ER/WRT/XP/22.26/2301/0060 - Problem z wnioskami po aktualizacji 22.26_04_Portal_HR_wielofirmowosc
::   WE:
::   WY: -1/1
::----------------------------------------------------------------------------------------------------------------------
_ret:=-1;
_pd:='PORTAL_WNIOSKI';
_tp:='Wysyłanie';
SYNC_MWA.cntx_psh();
SYNC_MWA.index('PD_METH1');
SYNC_MWA.prefix();
:: szukanie lp metod cseod_AcceptanceHeaderModify i chr_PersonAbsenceRequestModify
{? ~SYNC_MWA.find_key(_pd,_tp,'cseod_AcceptanceHeaderModify',)
|| __UPG.msg('Nie odnaleziono metody cseod_AcceptanceHeaderModify.')
|? _header:=SYNC_MWA.LP;
   __UPG.msg('Metoda: cseod_AcceptanceHeaderModify Lp: %1.'[$_header]);
   ~SYNC_MWA.find_key(_pd,_tp,'chr_PersonAbsenceRequestModify',)
|| __UPG.msg('Nie odnaleziono metody chr_PersonAbsenceRequestModify.')
|? _request:=SYNC_MWA.LP;
   __UPG.msg('Metoda: chr_PersonAbsenceRequestModify Lp: %1.'[$_request]);
:: sprawdzenie i w razie potrzeby zmiana kolejności metod
   _request<_header
|| __UPG.msg('Zmiana kolejności przetwarzania metod nie była wymagana.');
   _ret:=1
|? __UPG.msg('Wymagana jest zmiana kolejności przetwarzania metod API.');
   _ds:=do_state();
   _ds=2
|| __UPG.msg('Zmiany nie przeprowadzone z powodu istnienia zerwanej transakcji.')
|| {? _ds=0 || do() ?};
   SYNC_MWA.LP:=-1;
   {? SYNC_MWA.put()
   || SYNC_MWA.cntx_psh();
      SYNC_MWA.index('PD_LP');
      {? SYNC_MWA.find_key(_pd,_request-1)
      || {!
         |? {? SYNC_MWA.LP>=_header
            || SYNC_MWA.LP+=1;
               SYNC_MWA.put() & SYNC_MWA.prev()
            ?}
         !}
      ?};
      SYNC_MWA.cntx_pop();
      SYNC_MWA.LP:=_header;
      SYNC_MWA.put()
   ?};
   {? {? _ds=0 || end() || do_state()=1 ?}
   || __UPG.msg('Zmiana kolejności przetwarzania metod API zakończona powodzeniem.');
      _ret:=1
   || __UPG.msg('Zmiana kolejności przetwarzania metod API niepowiodła się. Zmiany zostały wycofane.')
   ?}
?};
SYNC_MWA.cntx_pop();
_ret


\portal_wn_urlop_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [22.26]
:: OPIS: Zmiana kolejności przetwarzania metod API dla przeznaczenia danych PORTAL_WNIOSKI- opis
::       ER/WRT/XP/22.26/2301/0060 - Problem z wnioskami po aktualizacji 22.26_04_Portal_HR_wielofirmowosc
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
'Zmiana kolejności przetwarzania metod API chr_PersonAbsenceRequestModify i chr_PersonAbsenceRequestModify\n'
'dla przeznaczenia danych PORTAL_WNIOSKI.'


\mechanizm_importow
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Import zmian w definicjach mechanizmu importów.
::       ER/WRT/XP/22.26/2303/0026 - Błąd unikalności importu Uczestników PPK ppk_ucz.csv
::       ER/WRT/XP/22.26/2303/0052 - Błąd importu PPK_POD dla poprawnie wypełnionego pliku ppk_pod.csv
::       ER/WRT/XP/22.26/2305/0007 - Błąd importu Nieobecności i dokumentacji - zawsze wymagane pole KDSW nawet
::                                   dla niewymaganych 350
::   WE:
::   WY:
::----------------------------------------------------------------------------------------------------------------------
_result:=0;
:: Zastaw oddzielonych przecinkami identyfikatorów mechanizmów importu, np. 'ZWS_IMPORTY_PKD,ZWS_IMPORTY_PPL':
_domains:='ZWS_IMPORTY_PPK,ZWS_IMPORTY_PPL';
_set:=spli_str(_domains,',');
_names:='';
_len:=obj_len(_set);
{! _ii:=1.._len
|! {? _set[_ii]*'ZWS_IMPORTY_'
   || _names+=exec('name','#b_domain',gsub(_set[_ii],'ZWS_IMPORTY_',''));
      {? _ii<_len
      || _names+=', '
      ?}
   ?}
!};
{? FUN.ask('Czy uruchomić import z nadpisywaniem mechanizmów importu %1 %2?'@
           [{? _len>1 || 'dziedzin'@ || 'dziedziny'@ ?},_names])
|| exec('import_init','#excel_imex',_set,,1);
   {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@['mechanizm_importow'])
   || __UPG.msg('Zaktualizowano definicje mechanizmu importów.');
      _result:=1
   || _result:=-1
   ?}
|| _result:=-1
?};
_result


\mechanizm_importow_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Import zmian w definicjach mechanizmu importów - opis.
::   WE:
::   WY: opis zadania potransferowego
::----------------------------------------------------------------------------------------------------------------------
'Import zmian w definicjach mechanizmu importów związany z emisją poprawek:\n'
' * ER/WRT/XP/22.26/2303/0026 - Błąd unikalności importu Uczestników PPK ppk_ucz.csv\n'
' * ER/WRT/XP/22.26/2303/0052 - Błąd importu PPK_POD dla poprawnie wypełnionego pliku ppk_pod.csv\n'
' * ER/WRT/XP/22.26/2305/0007 - Błąd importu Nieobecności i dokumentacji - zawsze wymagane pole KDSW nawet '
'dla niewymaganych 350\n'


\atrybut_zusz3
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie atrybutów płacowych (ER/WRT/XP/22.26/2302/0082).
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
_txt:='';
exec('__RUB','object');
__RUB.fill();

{? __RUB.ref(436) & __RUB.ref(438) & __RUB.ref(442) & __RUB.ref(443) & __RUB.ref(7132) & __RUB.ref(7157)
|| exec('add_attr','rubatr',652,6525,'Uwzględniane w miesiącu wystąpienia',1,
        'Nieobecności wykazywane na raporcie w miesiącu ich wystąpienia.');
        exec('add_attr','rubatr',6525,65251,'Niebecności uwzględniane w miesiącu wystąpienia',1,
        'Nieobecności, za które wynagrodzenie jest wykazywane na raporcie w miesiącu ich wystąpienia.',,
        11,12,13,22,7132,7157);
        exec('add_attr','rubatr',6525,65252,'Wynagr. za nieob. uwzględniane w miesiącu wystąp.',1,
        'Wynagrodzenia za nieobecności wykazywane na raporcie w miesiącu ich wystąpienia.',,436,438,442,443)
|| _result:=-1
?};

{? _result=1
|| __UPG.msg('Zakończono aktualizację atrybutów składników płacowych.')
|| __UPG.msg('Brak rubryk płacowych.')
?};

_result


\atrybut_zusz3_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Dodanie atrybutów płacowych ER/WRT/XP/22.26/2302/0082 - opis.
::----------------------------------------------------------------------------------------------------------------------
'Zadanie do poprawki ER/WRT/XP/22.26/2302/0082 - Zaświadczenie płatnika składek ZUS Z-3-rozliczona nieobecność '
'choroba wynagrodzenie 29/12/2022-31/12/2022 ze średnią urlopową z wypłata inny mc niż urlop\n'
'uzupełniające listę atrybutów o atrybuty:\n'
'* 6525 Uwzględniane w miesiącu wystąpienia\n'
'** 65251 Niebecności uwzględniane w miesiącu wystąpienia\n'
'** 65252 Wynagr. za nieob. uwzględniane w miesiącu wystąp.'


\faktury_seod_resend
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Wymuszenie ponownej wysyłki faktur SEOD po naprawie ER/WRT/XP/22.26/2309/0020
::----------------------------------------------------------------------------------------------------------------------
_ile:=0;
FIRMA.cntx_psh();
FIRMA.index('EWID'); FIRMA.prefix('T');
{? FIRMA.first()
|| _typobieg:=exec('bl_typ','obiegi',1);
   ROK_F.cntx_psh();
   ROK_F.index('ROKPOCZ');
   POZF.cntx_psh();
   {!
   |? ROK_F.prefix(FIRMA.ref());
      {? ROK_F.find_le(date())
      || {!
         |? echo('Analizowanie faktur SEOD z firmy %1 z roku %2'[FIRMA.SYMBOL,ROK_F.NAZ]);
            EDOKUM.cntx_psh();
            EDOKUM.use('skid_v'+($(ROK_F.POCZ_ROK~1)+2));
            EDOKUM.index('W_PORTAL');
            EDOKUM.prefix(FIRMA.ref(),_typobieg,'S');
            POZF.use('pozf__'+(EDOKUM.name()+2));
            POZF.index('EDOKUM');
            POZF.prefix();
            {? EDOKUM.first()
            || {!
               |? {? POZF.find_key(EDOKUM.ref())
                  || _ile+=EDOKUM.put(,1)
                  ?};
                  EDOKUM.next()
               !}
            ?};
            EDOKUM.cntx_pop();
            ROK_F.next()
         !}
      ?};
      FIRMA.next()
   !};
   POZF.cntx_pop();
   ROK_F.cntx_pop()
?};
FIRMA.cntx_pop();
echo();
__UPG.msg('Liczba faktur SEOD do ponownej wysyłki: %1.'[$_ile]);
1


\faktury_seod_resend_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Wymuszenie ponownej wysyłki faktur SEOD po naprawie ER/WRT/XP/22.26/2309/0020 - oppis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2309/0020\n'
'Brak nazwy towaru na fakturze w SEOD.\n'
'Oznaczenie faktur SEOD do ponownej wysyłki.'


\fix_wn_no_seo
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2307/0021 - anulowanie akywynych typów niosków SEOD gdy brak licencji na SEO
::----------------------------------------------------------------------------------------------------------------------
{? ~exec('lic_or','#b_domain','SEO')
|| _ile:=0;
   ETYPY.cntx_psh();
   ETYPY.index('GRUPA_WP'); ETYPY.prefix('s');
   {? ETYPY.first()
   || {!
      |? {? ETYPY.AKTYWNY='T'
         || ETYPY.AKTYWNY:='N';
            _ile+=ETYPY.put()
         ?};
         ETYPY.next()
      !}
   ?};
   ETYPY.cntx_pop();
   __UPG.msg('Liczba anulowanych aktywnych wniosków SEOD: %1.'[$_ile])
|| __UPG.msg('Licencja na dziedzinę SEO jest dostępna.')
?};
1


\fix_wn_no_seo_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: MB [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2307/0021 - anulowanie akywynych typów niosków SEOD gdy brak licencji na SEO - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2307/0021\n'
'Anulowanie aktywnych wniosków SEOD gdy brak licencji do dziedziny SEO'


\rubryki_ER23100002
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2310/0002 - ER/WRT/XP/12.51/2309/0031 : Plik rubryki.dfg błędy językowe w komentarzu
::       do składnika 7015 i 7016
::----------------------------------------------------------------------------------------------------------------------
_result:=1;
R.trig_off('*','*');
exec('__RUB','object');
__RUB.fill();

{? ~(+exec('import','rubryki','g','7015,7016',1,0))
|| _txt:='Aktualizacja rubryk "7015 L.g. dod. odb. 50%" oraz "7016 L.g. dod. odb. 100%" zakończona powodzeniem.'
|| _txt:='Aktualizacja rubryk "7015 L.g. dod. odb. 50%" oraz "7016 L.g. dod. odb. 100%" zakończona niepowodzeniem.';
   _result:=-1
?};
R.trig_on('*','*');
_result


\rubryki_ER23100002_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Poprawka ER/WRT/XP/22.26/2310/0002 - ER/WRT/XP/12.51/2309/0031 : Plik rubryki.dfg błędy językowe w komentarzu
::       do składnika 7015 i 7016 - opis zadania
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2310/0002\n'
'Aktualizacja rubryk "7015 L.g. dod. odb. 50%" oraz "7016 L.g. dod. odb. 100%".'


\procesy_ER_2310_0014
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Import procesów poprawka
::       ER/WRT/XP/22.26/2310/0014 - Czynność PKD_EZK_AOON w procesie PER_OON_AUTO uruchomiona serwisowo zgłasza
::       błędy do log
::----------------------------------------------------------------------------------------------------------------------
_result:=exec('procesy','upgrade','22.26','PER_OON_AUTO','PER_');
{? _result>0
|| _result:=0;
   {? FUN.ask('Czy oznaczyć zadanie: \'%1\' jako wykonane?'@[form_stack(0).name])
   || __UPG.msg('Zakończono import procesów z dziedziny PER');
      _result:=1
   || _result:=-1
   ?}
?};
_result


\procesy_ER_2310_0014_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IS [22.26]
:: OPIS: Import procesów poprawka
::       ER/WRT/XP/22.26/2310/0014 - Czynność PKD_EZK_AOON w procesie PER_OON_AUTO uruchomiona serwisowo zgłasza
::       błędy do log - opis
::----------------------------------------------------------------------------------------------------------------------
'Poprawka - ER/WRT/XP/22.26/2310/0014 - Czynność PKD_EZK_AOON w procesie PER_OON_AUTO uruchomiona serwisowo zgłasza '
'błędy do log.\n'
'Pozwala zaimportować zmienione procesy dziedziny PKD:\n'
' ● PER_OON_AUTO - Pow. pracowników o końcu orzecz. o niepełn.\n'


\por_work_place
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [22.26]
:: OPIS: ER/WRT/XP/22.26/2309/0023 - ER/WRT/XP/12.51/2307/0024 : Miejsca pracy - komunikat chr_WorkPlaceModify
::----------------------------------------------------------------------------------------------------------------------
_ret:=1;
_txt:='';
{? ~exec('lic','#b_domain','POR')
|| _ret:=-1;
   _txt:='Brak licencji na Portal. Aktualizacja miejsc pracy nie jest wymagana.'
|| SLO_TYP.cntx_psh();
   SLO_TYP.index('SYMBOL');
   {? SLO_TYP.find_key('MIEJSCPR')
   || _slo_typ:=SLO_TYP.ref();
      SLO_NAZ.cntx_psh();
      SLO_NAZ.index('NAZWA');
      SLO_NAZ.prefix(_slo_typ,);
      {? SLO_NAZ.first()
      || {? exec('data_delete_cntx','portal_engine','PORTAL','portal.mwac','chr_WorkPlaceDelete','SLO_NAZ')
         || {! |? _ret*=SLO_NAZ.put(,1);
                  SLO_NAZ.next()
            !};
            {? _ret
            || _txt:='Aktualizacja słownika miejsc pracy zakończona powodzeniem.'
            || _txt:='Aktualizacja słownika miejsc pracy zakończona niepowodzeniem.'
            ?}
         || _ret:=0;
            _txt:='Ustawienie kontekstu tabeli SYNC_MWA nie powiodło się.'
         ?}
      || _ret:=-1;
         _txt:='Brak rekordów w słowniku miejsc pracy.'
      ?};
      SLO_NAZ.cntx_pop()
   ?};
   SLO_TYP.cntx_pop()
?};
__UPG.msg(_txt);
_ret


\por_work_place_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: IgPtasze [22.26]
:: OPIS: ER/WRT/XP/22.26/2309/0023 - ER/WRT/XP/12.51/2307/0024 : Miejsca pracy - komunikat chr_WorkPlaceModify - opis
::----------------------------------------------------------------------------------------------------------------------
'Zadanie do poprawki ER/WRT/XP/22.26/2309/0023'
' - ER/WRT/XP/12.51/2307/0024 : Miejsca pracy - komunikat chr_WorkPlaceModify.\n'
'Formuła usuwa z chmury dane dotyczące miejsc pracy (chr_WorkPlaceDelete).\n'
'Następnie putuje wszystkie rekordy słownika MIEJSCPR w celu ponownego wysłania na portal.'


\kal_buff_w
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/12.51/2304/0005 : Wykonanie czasu pracy (poza okresem)
::----------------------------------------------------------------------------------------------------------------------
_wynik:=0;

_query:=''+"select REFERENCE as REF from KAL_BUFF where KAL_BUFF.GPW='W' and KAL_BUFF.CZY_OKR='N'";
_TAB:=sql(_query);
{? ~_TAB.first()
|| __UPG.msg('Brak danych do naprawy - uruchomienie zadania nie jest wymagane.');
   return(1)
||
   KAL_BUFF.cntx_psh();
   KAL_BUFF.index('PRACDATA');
   KAL_BUFF.prefix();
   {!
   |? {? KAL_BUFF.seek(_TAB.REF,)
      || _okr:=exec('find_okr','grafik',KAL_BUFF.P,KAL_BUFF.DATA,'okr');
         KAL_BUFF.A_OKR:=_okr;
         KAL_BUFF.CZY_OKR:='T';
         _wynik+=KAL_BUFF.put(1)
      ?};
      _TAB.next()
   !};
   KAL_BUFF.cntx_pop()
?};

{? _wynik>0
|| __UPG.msg('Naprawiono %1 z %2 błędnych zapisów.'[$_wynik,$_TAB.size()])
|| __UPG.msg('Nie udało się naprawić danych.')
?};

_wynik


\kal_buff_w_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: TMR [22.26]
:: OPIS: ER/WRT/XP/12.51/2304/0005 : Wykonanie czasu pracy (poza okresem) - opis
::----------------------------------------------------------------------------------------------------------------------
'ER/WRT/XP/12.51/2304/0005 : Wykonanie czasu pracy (poza okresem)'


\zam_R_M_K
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Koryguje pola rok,miesiąc,kwartał dla zamówień
::----------------------------------------------------------------------------------------------------------------------
_res:=1;
VAR_DEL.delete('__lrec','__lmod');
__lrec:=__lmod:=0;
_msk:=ZD_NAG.names();
ZD_NAG.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? ZD_NAG.use(_msk.NAME);
      ZD_NAG.prefix();
      ZD_NAG.for_each("
        __lrec+=1;
        _put:=0;
        {? ZD_NAG.R<>(ZD_NAG.DATA~1) | ZD_NAG.M<>(ZD_NAG.DATA~2)
        || ZD_NAG.R:=ZD_NAG.DATA~1;
           ZD_NAG.M:=ZD_NAG.DATA~2;
           ZD_NAG.K:=exec('zwr_okr','#datetime',date(ZD_NAG.R,ZD_NAG.M,1),5);
           _put:=1
        ?};
        {? _put & ZD_NAG.put(1)
        || __lmod+=1
        ?}
        ");
      _msk.next()
   !}
?};
ZD_NAG.cntx_pop();
__UPG.msg('Zaktualizowano zamówienia dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

__lrec:=__lmod:=0;
_msk:=ZDP_NAG.names();
ZDP_NAG.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? ZDP_NAG.use(_msk.NAME);
      ZDP_NAG.prefix();
      ZDP_NAG.for_each("
        __lrec+=1;
        _put:=0;
        {? ZDP_NAG.R<>(ZDP_NAG.D_WYS~1) | ZDP_NAG.M<>(ZDP_NAG.D_WYS~2)
        || ZDP_NAG.R:=ZDP_NAG.D_WYS~1;
           ZDP_NAG.M:=ZDP_NAG.D_WYS~2;
           _put:=1
        ?};
        {? _put & ZDP_NAG.put(1)
        || __lmod+=1
        ?}
        ");
      _msk.next()
   !}
?};
ZDP_NAG.cntx_pop();
__UPG.msg('Zaktualizowano potwierdzenia zamówień dostaw.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);

__lrec:=__lmod:=0;
_msk:=ZK_N.names();
ZK_N.cntx_psh();
ZK_P.cntx_psh();
_msk.clear();
{? _msk.first()
|| {!
   |? ZK_N.use(_msk.NAME);
      ZK_P.use('zkpoz'+(_msk.NAME+3));
      ZK_N.prefix();
      ZK_N.for_each("
        __lrec+=1;
        _put:=0;
        {? ZK_N.R<>(ZK_N.DP~1) | ZK_N.M<>(ZK_N.DP~2)
        || ZK_N.R:=ZK_N.DP~1;
           ZK_N.M:=ZK_N.DP~2;
           ZK_N.RR:=ZK_N.DT~1;
           ZK_N.RM:=ZK_N.DT~2;
           ZK_N.RT:=exec('NumberOfWeek','#datetime',ZK_N.DT);
           _put:=1
        ?};
        {? _put & ZK_N.put(1)
        || __lmod+=1
        ?}
        ");
      ZK_P.prefix();
      ZK_P.for_each("{? ZK_P.RR<>ZK_P.N().RR | ZK_P.RM<>ZK_P.N().RM | ZK_P.RT<>ZK_P.N().RT
                     || ZK_P.RR:=ZK_P.N().RR;
                        ZK_P.RM:=ZK_P.N().RM;
                        ZK_P.RT:=ZK_P.N().RT;
                        ZK_P.put(1)
                     ?}");
      _msk.next()
   !}
?};
ZK_N.cntx_pop();
ZK_P.cntx_pop();
__UPG.msg('Zaktualizowano zamówienia sprzedaży i wewnętrzne.');
__UPG.msg('Przetworzono: %1 zapisów, z czego zmodyfikowano: %2 zapisów.'[$__lrec,$__lmod]);
obj_del(_msk);
VAR_DEL.delete('__lrec','__lmod');
_res


\zam_R_M_K_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: [rr] [22.26]
:: OPIS: Koryguje pola rok,miesiąc,kwartał dla zamówień - opis
::----------------------------------------------------------------------------------------------------------------------
'Aktualizacja zamówień'


\p_kal_nazw
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: ER/WRT/XP/22.26/2310/0030 - Błąd w kalendarzach pracowników po transferze wersji wielofirmowej do Merit ERP.
::----------------------------------------------------------------------------------------------------------------------
_upd:=0;
_err:=0;
_fch:=%255;

KAL_NAZW.trig_off('*','*');
KAL_NAZW.cntx_psh();
KAL_NAZW.clear();
P.cntx_psh();
P.use('pracowni');
P.index('IDADD');
P.prefix();

_loop:=P.first();
{!
|? _loop
|! {? 1+P.KAL().NAZWA=_fch
   || _str:=_fch+$P.ref();
      {? _str<>KAL_NAZW.NAZWA
      || KAL_NAZW.NAZWA:=_str;
         {? KAL_NAZW.put()
         || _upd+=1
         || _err+=1
         ?}
      ?}
   ?};
   _loop:=P.next()
!};

P.cntx_pop();
KAL_NAZW.cntx_pop();
KAL_NAZW.trig_on('*','*');

__UPG.msg('Aktualizacja nazw kalendarzy (zmiany: %1, błędy: %2).'[$_upd,$_err]);

{? _err>0 || -1 || 1 ?}


\p_kal_nazw_desc
::----------------------------------------------------------------------------------------------------------------------
::  UTW: jaws [22.26]
:: OPIS: ER/WRT/XP/22.26/2310/0030 - Błąd w kalendarzach pracowników po transferze wersji wielofirmowej do Merit ERP.
::----------------------------------------------------------------------------------------------------------------------
'Poprawka ER/WRT/XP/22.26/2310/0030\n'
'Błąd w kalendarzach pracowników po transferze wersji wielofirmowej do Merit ERP.'

:Sign Version 2.0 jowisz:1045 2023/11/13 13:22:03 d0076a543a7ce37b898819aa727b8c8f64abdd9016fa0dbb7c519d912a028a10c77c7eaf280c157e458cd139aab39349bfd2f2e53f737ee85077b217755222a8733c3d9e31f8e5122f1ae57ea0727336e446df69156d3aa1735a62e5dd4141cb581db49ac758d7fc32c2620c8e03da4cbf8fee7708d415176821e52e484762b4
